|FICHEROS;
     rem00100 #100;
     rem00101 #101;
     rem00099 #998;
     retabla1 #401;
     rew99999 #999,MANTE;

     rem99999  #666;
     progacti@ #220;
|FIN;

|VARIABLES;
     aAlfa      = "";
     aFichero   = "";
     aDef       = "";
     aPathDef   = "";
     aPathRenta = "";
     aLongi     = "";
     aAlfan     = "";
     aVersion   = "";
     aCaract    = "";
     aTecla     = "";

     nConta     = 0;
     nContador  = 0;
     nTipo      = 0;
     nAnyoDec   = 0;
     nAnyoUsu   = 0;
     nPorce     = 0;
     nLinea     = 0;
     nLongi     = 0;
     nPara      = 0;
     nVersion   = 0;
     nPosic     = 0;
     nNume      = 0;

     &enEmpresa    = 0;
     &enComunes    = 0;
     &eaDeclarante = "";
     &enMinusvalia = 0;
     &eaMinuAyuda  = "";
     &eaFallece    = "";
     &enAutonomia  = 0;
     &eaSwVarios   = "";
     &enSwPresenta = 0;
     &eaPrograma   = "";
     &eaPunto      = "";
     &eaDni        = "";
     &eaEdadDec    = "";

     &eaLetra      = "";
     &eaDeDonde    = "";
     &eaLinDonde   = "";
     &eaAnyoUsu    = "";
     &eaPatr06     = "";
     &eaPatr07     = "";
     &eaPatr08     = "";
     &eaPatr09     = "";
     &eaPatr10     = "";
     &eaPatr11     = "";
     &eaPatr12     = "";
     &eaPatr13     = "";
     &eaPatr14     = "";
     &eaPatr15     = "";
     &eaPatr16     = "";
     &eaPatr16_1   = "";
     &eaPatr17     = "";
     &eaPatr18     = "";
     &eaPatr19     = "";
     &eaPatr20     = "";
     &eaPatr21     = "";
     &eaPatr22     = "";
     &eaPatr23     = "";
     &enNomeDat    = 0;
     &enNomeAgr    = 0;
     &eaMuerto     = "";
|FIN;

|PROCESO QueVersion;
     |VERSIONRTME aVersion;
     |SI aVersion ] "9.09T";  |ACABA;  |FINSI;

     aLongi = aVersion % 0;
     nLongi = aLongi;
     aAlfan = "";
     |PARA nPara = 1;  |SI nPara [ nLongi;  |HACIENDO nPara = nPara + 1;
           nPosic  = (100 * nPara) + 1;
           aCaract = aAlfan % nPosic;
           |SI aCaract = ".";  |SAL;  |FINSI;

           aVersion = aVersion + aCaract;
     |SIGUE;

     nVersion = aVersion;

     |SI nVersion ] 10;  |ACABA; |FINSI;

     |MENAV "        No se esta ejecutando en DIAGRAM9 o Version incorrecta.";
     |PTEC 807;
|FPRC;

|PROCESO Familiar;
     nConta    = nConta + 1;
     |SI nConta > 4;  |ERROR10;  |ACABA;  |FINSI;

     nContador  = nContador + 1;
     #999#0     = nContador;
     #999#1     = nConta + " FAMILIAR";
     #999#2     = #101#3;
     #999#3     = #101#6;
     #999#4     = #101#14;
     #999#5     = "N";
     #999#6     = #101#5;
     #999#7     = #100#11;
     #999#8     = #101#2;
     #999#9     = #101#20;
     |GRABA 020#999n;
     |LIBERA #999;
     eaSwVarios = eaSwVarios + nConta;
|FPRC;

|DEFBUCLE rem00101;
     #101#1;
     13;
     #100#0,  1, "S";
     #100#0, 99, "S";
     ;
     NULL, Familiar;
|FIN;

|RUTINA ClaveBaja999;
     |SI FSalida = "POSICION";
         #999#0 = nTipo;
         |ACABA;
     |FINSI;

     #999#0 = 1;
|FRUT;

|RUTINA ClaveAlta999;
     #999#0 = nContador;
|FRUT;

|PROCESO Tipo0w9999;  |TIPO 0;
     eaDeclarante = #999#1;
     enMinusvalia = #999#3;
     eaMinuAyuda  = #999#4;
     eaFallece    = #999#5;
     eaEdadDec    = #999#6;
     enAutonomia  = #999#7;
     eaDni        = #999#8;
     |PTEC 807;
|FPRC;

|PROCESO SacaDeclarantes;
     |ABRE #100;
     #100#0 = enEmpresa;
     |LEE 040#100=;
     |SI FSalida ! 0;  |DDEFECTO #100;  |FINSI;
     |CIERRA #100;

     aFichero = (Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #999 aFichero;
     |ABRE #999;  |CIERRA #999;  |DELINDEX #999;

     |ABRE #999;

     nContador = 1;
     #999#0     = nContador;
     #999#1     = "TITULAR";
     #999#2     = #100#5;
     #999#3     = #100#8;
     #999#4     = #100#9;
     #999#5     = "N";
     #999#6     = #100#7;
     #999#7     = #100#11;
     #999#8     = #100#1;
     #999#9     = #100#105;
     eaSwVarios = "T";
     |SI #100#91 = "T";  #999#5 = "S";  |FINSI;
     |GRABA 020#999n;
     |LIBERA #999;

     aAlfa = #100#39;  |QBT aAlfa;
     |SI aAlfa ! "";
         nContador = nContador + 1;
         #999#0    = nContador;
         #999#1    = "CONYUGE";
         #999#2    = #100#39;
         #999#3    = #100#41;
         #999#4    = #100#42;
         #999#5    = "N";
         #999#6    = #100#40;
         #999#7    = #100#43;
         #999#8    = #100#35;
         #999#9    = #100#106;
         |SI #100#91 = "C";  #999#5 = "S";  |FINSI;
         |GRABA 020#999n;
         |LIBERA #999;
         eaSwVarios = eaSwVarios + "C";
     |FINSI;

     |SI nContador = 2;
         |SI enComunes = 1;  |O enComunes = 2;
             nContador = nContador + 1;
             #999#0    = nContador;
             #999#1    = "GANANCIAL";
             #999#2    = "PORCENTAJE 50% ENTRE EL TITULAR Y LA CONYUGE";
             #999#3    = 0;
             #999#4    = "N";
             #999#5    = "N";
             #999#6    = #100#7;
             #999#7    = #100#11;
             #999#8    = #100#1;
             #999#9    = #100#105;
             |SI #100#90 = "S";  #999#5 = "S";  |FINSI;
             |GRABA 020#999n;
             |LIBERA #999;
         |FINSI;
     |FINSI;

     |SI enComunes = 0;  |O enComunes = 1;
         nConta = 0;
         |BUCLE rem00101;
     |FINSI;

     |SI enComunes = 1;
         nContador = nContador + 1;
         #999#0    = nContador;
         #999#1    = "VARIOS";
         #999#2    = "ENTRADA POR PORCENTAJE DE LOS DECLARANTES";
         #999#3    = 0;
         #999#4    = "N";
         #999#5    = "N";
         #999#6    = 0;
         #999#7    = #100#11;
         #999#8    = #100#1;
         #999#9    = #100#105;
         |SI #100#90 = "S";  #999#5 = "S";  |FINSI;
         |GRABA 020#999n;
         |LIBERA #999;
     |FINSI;

     |SELECT #2#999;
     #999#1 = eaDeclarante;
     |LEE 040#999=;
     |SI FSalida ! 0;  |DDEFECTO #999;  |FINSI;
     nTipo = #999#0;

     |CIERRA #999;

     |SI enSwPresenta = 0;
         |PUSHV 0501 2380;
         |PARA;  |SI;  |HACIENDO;
                 |ENTLINEAL #1#999, -1, 4, 18, 1, ClaveBaja999, ClaveAlta999;
                 aAlfa = eaDeclarante;  |QBT aAlfa;
                 |SI aAlfa ! "";  |SAL;  |FINSI;
         |SIGUE;
         |POPV;
     |SINO;
         eaDeclarante = #999#1;
         enMinusvalia = #999#3;
         eaMinuAyuda  = #999#4;
         eaFallece    = #999#5;
         eaEdadDec    = #999#6;
         enAutonomia  = #999#7;
         eaDni        = #999#8;
     |FINSI;
|FPRC;

|PROCESO ProgramaActivo;
     |DBASE aPathRenta;  |QBT aPathRenta;
     aPathDef = aPathRenta + "def/";
     aDef     = aPathDef + eaPrograma + ".mas";
     |CARGA_DEF aDef, progacti@;
     |SI FSalida ! 0;
         aAlfa = "     Error en cargar el Def " + aFichero + ".mas";
         |MENAV aAlfa;
         |ACABA;
     |FINSI;

     |ABRE #666;
     #666#0 = enEmpresa;
     #666#1 = eaPrograma;
     #666#2 = eaPunto;
     |LEE 101#666=;
     |SI FSalida ! 0;
         #666#0 = enEmpresa;
         #666#1 = eaPrograma;
         #666#2 = eaPunto;
         |GRABA 020#666n;
     |FINSI;

     |ABRE #220;
     |SI eaPrograma = "rem00099";
         #220#0 = enEmpresa;
         #220#1 = eaLetra;
         #220#2 = 1;
     |SINO;
         |SI eaPrograma ! "rem01001";
             #220#0 = enEmpresa;
             #220#1 = 1;
         |SINO;
             #220#0 = enEmpresa;
             #220#1 = 0;
             #220#2 = "";
         |FINSI;
     |FINSI;
     |LEE 040#220];
     |SI eaPrograma = "rem00099";
         |SI FSalida ! 0;  |O #220#0 ! enEmpresa;  |O #220#1 ! eaLetra;
             |BORRA 101#666c;
         |FINSI;
     |SINO;
         |SI FSalida ! 0;  |O #220#0 ! enEmpresa;
             |BORRA 101#666c;
         |FINSI;
     |FINSI;
     |CIERRA #220;

     |LIBERA #666;
     |CIERRA #666;

     |DESCARGA_DEF #progacti@;
|FPRC;

|PROCESO LimpiaPatrim;
     eaPatr06   = "";
     eaPatr07   = "";
     eaPatr08   = "";
     eaPatr09   = "";
     eaPatr10   = "";
     eaPatr11   = "";
     eaPatr12   = "";
     eaPatr13   = "";
     eaPatr14   = "";
     eaPatr15   = "";
     eaPatr16   = "";
     eaPatr16_1 = "";
     eaPatr17   = "";
     eaPatr18   = "";
     eaPatr19   = "";
     eaPatr20   = "";
     eaPatr21   = "";
     eaPatr22   = "";
     eaPatr23   = "";
|FPRC;

|PROCESO CalcuUsufruc;
     |SI eaPatr16 = "U";
         nAnyoDec = eaEdadDec;
         |SI nAnyoDec = 0;  nAnyoDec = #100#7;  |FINSI;
         nAnyoDec = 2009 - nAnyoDec;
     |SINO;
         nAnyoDec = nAnyoUsu;
     |FINSI;

     |SI nAnyoDec < 20; nPorce = 70; |FINSI;

     |SI nAnyoDec ] 20;
         nPorce = 70 - (nAnyoDec - 19);
         |SI nPorce < 10; nPorce = 10; |FINSI;
     |FINSI;

     |SI eaPatr16 = "N"; nPorce = 100 - nPorce; |FINSI;
|FPRC;

|PROCESO GrabaPatrimonio;
     nPorce = 100;
     |SI eaPatr16 ! "P"; |Y eaPatr16 ! "";  |Y eaPatr16 ! "M";
         nAnyoUsu = eaAnyoUsu;
         |HAZ CalcuUsufruc;
     |FINSI;

     |ABRE #998;
     |SELECT #1#998;
     #998#0 = enEmpresa;
     #998#1 = eaLetra;
     #998#2 = 999;
     |LEE 040#998];
     |SI FSalida = 0;
         |LEE 040#998a;
     |SINO;
         |LEE 040#998u;
     |FINSI;
     nLinea = 1;
     |SI FSalida = 0;  |Y  #998#0 = enEmpresa;  |Y #998#1 = eaLetra;
         nLinea = #998#2 + 1;
     |FINSI;

     |DDEFECTO #998;
     #998#0  = enEmpresa;
     #998#1  = eaLetra;
     #998#2  = nLinea;
     #998#3  = eaDeclarante;
     #998#4  = eaDeDonde;
     #998#5  = eaLinDonde;
     #998#6  = eaPatr06;
     #998#7  = eaPatr07;
     #998#8  = eaPatr08;
     #998#9  = eaPatr09;
     #998#10 = eaPatr10;
     #998#10 = #998#10 % nPorce;
     #998#23 = eaPatr23;
     #998#23 = #998#23 % nPorce;
     #998#11 = eaPatr11;
     #998#12 = eaPatr12;
     #998#13 = eaPatr13;
     #998#14 = eaPatr14;
     #998#15 = eaPatr15;
     #998#16 = eaPatr16;
     |SI eaPatr16_1 ! "";
         #998#16 = eaPatr16_1;
     |FINSI;
     #998#17 = eaPatr17;
     #998#18 = eaPatr18;
     #998#19 = eaPatr19;
     #998#20 = eaPatr20;
     #998#21 = eaPatr21;
     #998#22 = eaPatr22;
     |SI #998#22 = "S";
         |SI #998#16 ! "P"; |Y #998#16 ! "U";
             #998#16 = "";
         |FINSI;
     |FINSI;

     |SI #998#22 = "S";
         |ABRE #401;
         |LEE 040#401p;
         |SI FSalida ! 0;  |DDEFECTO #401;  |FINSI;
         |CIERRA #401;

         nNume    = #401#90;
         |SI #998#3 = "GANANCIAL ";
             nNume    = #401#91;
         |FINSI;

         |SI #998#23 < nNume;
             #998#23 = #998#10;
         |FINSI;
     |FINSI;

     |GRABA 020#998n;
     |LIBERA #998;
     |CIERRA #998;

     eaPrograma   = "rem00099";
     eaPunto      = eaLetra;
     |HAZ ProgramaActivo;
|FPRC;

|PROCESO BorraPat;
     |BORRA 020#998a;
     |LIBERA #998;
|FPRC;

|DEFBUCLE rem00099P;
     #998#2;
     ;
     eaDeDonde, nLinea, enEmpresa;
     eaDeDonde, nLinea, enEmpresa;
     be;
     NULL, BorraPat;
|FIN;

|PROCESO BorraPatrimonio;
     nLinea = eaLinDonde;
     |BUCLE rem00099P;

     eaPrograma   = "rem00099";
     eaPunto      = eaLetra;
     |HAZ ProgramaActivo;
|FPRC;

|PROCESO CuadroPatri;  |TIPO 0;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     |PUSHV 0501 2380;
     |CUADRO 0819 2275;
     |BLANCO 0920 2174;

     |ATRI R;
     |PINTA 0920, "             APARTADOS PATRIMONIO                 ";
     |ATRI r;

     |ATRI S;
     |PINTA 1120, "E1 - Deposito Cuentas Corrientes                  ";
     |PINTA 1220, "F1 - Cesion Terceros, Capitales Propios (en Bolsa)";
     |PINTA 1320, "F2 - Cesion Terceros, Capitales Propios (no Bolsa)";
     |PINTA 1420, "G1 - Fondos Propios Int.Inv.Colectiva   (en Bolsa)";
     |PINTA 1520, "G2 - Fondos Propios otras entidades     (en Bolsa)";
     |PINTA 1620, "G3 - Fondos Propios Int.Inv.Colectiva   (no Bolsa)";
     |PINTA 1720, "G4 - Fondos Propios otras entidades     (no Bolsa)";
     |PINTA 1820, "H1 - Valores Exentos                    (en Bolsa)";
     |PINTA 1920, "H2 - Valores Exentos                    (no Bolsa)";
     |ATRI s;

     |LEETECLA aTecla;
     |POPV;

     |ERROR;
|FPRC;
