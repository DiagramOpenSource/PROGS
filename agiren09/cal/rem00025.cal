|FICHEROS;
     rem00025 #20;
     rem00058 #58;
     rem99999 #666;
     rew99998 #900,MANTE;
     rew00605 #1003,MANTE;
|FIN;

|VARIABLES;
     aPathRenta    = "";
     aDef          = "";
     aAlfa         = "";
     aAlfa1        = "";
     aEscape       = "";
     aAvance       = "";
     aRetroceso    = "";
     aQueQuiero    = "";
     aNumCampos    = "";
     aNumPanta     = "";
     aLong         = "";
     aTecla        = "";

     nPanta        = 0;
     nNumCampos    = 0;
     nNumPanta     = 0;
     nCampos       = 0;
     nModo         = 0;
     nEntrada      = 0;
     nFactor       = 0;
     nLong         = 0;
     nCaracter     = 0;
     nPosi         = 0;
     nCampo        = 0;
     nDesde        = 0;
     nValor        = 0;
     nImporte1     = 0;
     nImporte2     = 0;
     nLinea        = 0;

     &enEmpresa    = 0;
     &enModo       = 0;
     &eaDeclarante = "";
     &enComunes    = 0;
     &enMinusvalia = 0;
     &eaMinuAyuda  = "";
     &eaFallece    = "";
     &eaSwVarios   = "";
     &enSwLiquida  = 0;
     &enSwRetorno  = 0;
     &eaPrograma   = "";
     &eaPunto      = "";

     &eaLetra      = "";
     &eaDeDonde    = "";
     &eaLinDonde   = "";
     &eaAnyoUsu    = "";
     &eaEdadDec    = "";
     &eaPatr06     = "";
     &eaPatr07     = "";
     &eaPatr08     = "";
     &eaPatr09     = "";
     &eaPatr10     = "";
     &eaPatr11     = "";
     &eaPatr12     = "";
     &eaPatr13     = "";
     &eaPatr14     = "";
     &eaPatr15     = "";
     &eaPatr16     = "";
     &eaPatr17     = "";
     &eaPatr18     = "";
     &eaPatr19     = "";
     &eaPatr20     = "";
     &eaPatr21     = "";

     aNifQbf1   = "";
     aNifQbf2   = "";

     &eaVarDni  = "";
     &enCalcCif = 0;
|FIN;

|| ************************************************************************
|| PROCESOS DE CONSULTA DE PANTALLAS NORMALES
|| ************************************************************************

|PROCESO PintaPantalla;
     |SI nPanta < 0;   nPanta = 0;                         |FINSI;
     |SI nPanta = 0;   |PINPA #00#1003;  |PINDA #00#1003;  |FINSI;
     |SI nPanta = 1;   |PINPA #01#1003;  |PINDA #01#1003;  |FINSI;
|FPRC;

|PROCESO Varios;  |TIPO 0;
     |SI #20#3 = "X";  |ACABA;  |FINSI;

     |SI #20#2 ! "VARIOS    ";
         #20#5  = 0;
         #20#6  = 0;
         #20#7  = 0;
         #20#8  = 0;
         #20#9  = 0;
         #20#10 = 0;
         |ACABA;
     |FINSI;

     |C_M #900#0,  1, "N";
     |C_M #900#1,  1, "N";
     |C_M #900#2,  1, "N";
     |C_M #900#3,  1, "N";
     |C_M #900#4,  1, "N";
     |C_M #900#5,  1, "N";

     aAlfa = eaSwVarios;
     aLong = aAlfa % 0;
     nLong = aLong;
     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPosi = (100 * nCaracter) + 1;
           aAlfa1 = aAlfa % nPosi;
           |SI aAlfa1 = "T";  |C_M #900#0,  1, "S";  |FINSI;
           |SI aAlfa1 = "C";  |C_M #900#1,  1, "S";  |FINSI;
           |SI aAlfa1 = "1";  |C_M #900#2,  1, "S";  |FINSI;
           |SI aAlfa1 = "2";  |C_M #900#3,  1, "S";  |FINSI;
           |SI aAlfa1 = "3";  |C_M #900#4,  1, "S";  |FINSI;
           |SI aAlfa1 = "4";  |C_M #900#5,  1, "S";  |FINSI;
     |SIGUE;

     #900#0 = #20#5;
     #900#1 = #20#6;
     #900#2 = #20#7;
     #900#3 = #20#8;
     #900#4 = #20#9;
     #900#5 = #20#10;

     |PUSHV 0501 2380;

     |PINPA #0#900;
     |PINDA #0#900;
     |ENDATOS #1#900;
     |SI FSalida = 0;
         #20#5  = #900#0;
         #20#6  = #900#1;
         #20#7  = #900#2;
         #20#8  = #900#3;
         #20#9  = #900#4;
         #20#10 = #900#5;
     |FINSI;

     |POPV;
|FPRC;

|PROCESO MensaPorce;  |TIPO 7;
     |SI #1003#14 = "N";
         #1003#15 = 100;   |PINTA #1003#15;
         #1003#21 = "";    |PINTA #1003#21;
         |C_M #1003#15, 1, "N";
         |C_M #1003#21, 1, "N";
         |ACABA;
     |FINSI;

     |C_M #1003#15, 1, "S";
     |C_M #1003#21, 1, "S";

     |PUSHV 0501 2380;
     |CUADRO 0937 1480;
     |BLANCO 1038 1379;
     |ATRI R;
     |PINTA 1038, "Las cantidades se introduciran al 100 %,  ";
     |PINTA 1138, "luego el calculo las computara al porcen- ";
     |PINTA 1238, "taje de comunero que le hayamos informado.";
     |ATRI r;
     |LEETECLA aAlfa;
     |POPV;
|FPRC;

|PROCESO LetraNif;  |TIPO 6;
     |SI FSalida = 10;
         eaVarDni   = #1003Campo;
         enCalcCif  = 1;
         |HAZ CalculoDNI;
         #1003Campo = eaVarDni;
         |PINTA #1003Campo;
         |ERROR;
         |ACABA;
     |FINSI;

     eaVarDni  = #1003Campo;
     enCalcCif = 0;
     |HAZ CalculoDNI;

     aNifQbf1 = #1003Campo;   |QBF aNifQbf1;
     aNifQbf2 = eaVarDni;  |QBF aNifQbf2;

     |SI aNifQbf1 ! aNifQbf2; |Y aNifQbf1 ! "";
         |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
         |SI FSalida = 0;
             #1003Campo = eaVarDni;  |PINTA #1003Campo;
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO CampoNif;  |TIPO 0;
     |SI #1003#14 = "N";  |ACABA;  |FINSI;

     |SI #1003#21 = "            ";
         |MENAV "     Introduzca un Nif.";
         |ERROR;
         |ACABA;
     |FINSI;

     aLong = #1003#21;  |QBF aLong;
     aLong = aLong % 0;
     nLong = aLong;
     |SI nLong ! 9;
         |MENAV "     Longitud Erronea.";
         |ERROR;
         |ACABA;
     |FINSI;

     aAlfa = #1003#21 % 101;
     |SI aAlfa ! "E";  |Y aAlfa ! "H";  |Y aAlfa ! "J";
         |MENAV "     El Nif  debe empezar por E, H o J";
         |ERROR;
         |ACABA;
     |FINSI;
|FPRC;

|PROCESO Totalw1003;
     #1003#29 = #1003#26 + #1003#27 + #1003#28;
     #1003#44 = 0;
     |PARA nCampo = 30;  |SI nCampo [ 43;  |HACIENDO nCampo = nCampo + 1;
           #1003#44 = #1003#44 + #1003nCampo;
     |SIGUE;
     #1003#44 = #1003#44 + #1003#49;
     #1003#44 = #1003#44 + #1003#50;

     #1003#45 = #1003#29 - #1003#44;

     #1003#47 = 0;
     |SI #1003#46 = "S";
          #1003#47 = #1003#45 % 40;
     |FINSI;

     #1003#51 = #1003#45 - #1003#47;

     #1003#53 = 0;
     |SI #1003#52 = "S";
         #1003#53 = #1003#51 % 65;
     |FINSI;

     #1003#48 = #1003#51 - #1003#53;

     |C_M #1003#56, 1, "N";

     |SI #1003#55 = "N";
         #1003#56 = 0;
     |FINSI;

     |SI #1003#55 = "F";
         |C_M #1003#56, 1, "S";
         |SI #1003#56 = 0;
             |SI #1003#51 > 0;
                 #1003#56 = #1003#51 % 20;
             |FINSI;
         |FINSI;
     |FINSI;

     |SI #1003#55 = "S";
         nImporte1 = 0;
         |SI #1003#51 > 0;
             nImporte1 = (#1003#51 % 20) ! 2;
         |FINSI;
         nImporte2 = (#1003#31 / 2) ! 2;
         |SI nImporte1 > nImporte2;
             nImporte1 = nImporte2;
         |FINSI;

         #1003#56 = nImporte1;
     |FINSI;

     |PINTA #1003#29;
     |PINTA #1003#44;
     |PINTA #1003#45;
     |PINTA #1003#47;
     |PINTA #1003#48;
     |PINTA #1003#51;
     |PINTA #1003#53;
     |PINTA #1003#56;
|FPRC;

|PROCESO Fantasma;
     |SI #20#3 = "X";  |Y nModo = 1;
         |ACABA;
     |FINSI;

     |PARA nCampos = nDesde;  |SI nCampos [ nNumCampos;  |HACIENDO nCampos = nCampos + 1;
           #1003nCampos = #20nCampos;
     |SIGUE;

     aEscape    = &255 + "806";
     aAvance    = &255 + "815";
     aRetroceso = &255 + "814";

     |SI nNumPanta ] 1;
         |MENSA "      Av.Pag Pantalla Siguiente. Re.Pag Pantalla Anterior. ESC Salir.";
     |SINO;
         |MENSA "      ESC Salir.";
     |FINSI;

     |PUSHV 0401 2380;
     |SI nModo ! 4;
         |PINPA #0#1003;
         |PINDA #0#1003;
         |ENDATOS #1#1003;
         |SI FSalida = 0;
             |PARA nCampos = nDesde;  |SI nCampos [ nNumCampos;  |HACIENDO nCampos = nCampos + 1;
                   |SI enSwLiquida = 1;
                       |SI #20nCampos ! #1003nCampos;
                           enSwRetorno = 1;
                       |FINSI;
                   |FINSI;
                   #20nCampos = #1003nCampos;
             |SIGUE;
         |FINSI;
     |SINO;
         |PINPA #0#1003;
         |PINDA #0#1003;
         nPanta = 0;
         |PARA;  |SI;  |HACIENDO;
              |LEETECLA aAlfa;
              |SI aAlfa = aEscape;  |SAL;  |FINSI;
              |SI aAlfa = aAvance;
                  |SI nPanta < nNumPanta;
                      nPanta = nPanta + 1;
                      |HAZ PintaPantalla;
                  |FINSI;
              |FINSI;

              |SI aAlfa = aRetroceso;
                  |SI nPanta [ nNumPanta;
                      nPanta = nPanta - 1;
                      |HAZ PintaPantalla;
                  |FINSI;
              |FINSI;
         |SIGUE;
     |FINSI;
     |POPV;
|FPRC;

|RUTINA ClaveBaja20;
     #20#0 = enEmpresa;
     #20#1 = 1;
|FRUT;

|RUTINA ClaveAlta20;
     #20#0 = enEmpresa;
     #20#1 = 99;
|FRUT;

|PROCESO PonDeclarante;  |TIPO 7;
     eaDeclarante = #20#2;
     enComunes    = 1;
     |HAZ SacaDeclarantes;
     #20#2 = eaDeclarante;
     |PINTA #20#2;
|FPRC;

|PROCESO Fallece;  |TIPO 7;
     |SI #20#3 = "X";
         |C_M #20#4, 1, "N";
         |ACABA;
     |FINSI;

     |SI eaFallece = "S";
         |C_M #20#4, 1, "S";
     |SINO;
         |C_M #20#4, 1, "N";  #20#4 = 0;  |PINTA #20#4;
     |FINSI;
|FPRC;

|PROCESO Patrimonio;
     |SI #20#3  = "R";           |ACABA;  |FINSI;
     |SI #20#3  = "X";           |ACABA;  |FINSI;
     |SI #20#2  = "1 FAMILIAR";  |ACABA;  |FINSI;
     |SI #20#2  = "2 FAMILIAR";  |ACABA;  |FINSI;
     |SI #20#2  = "3 FAMILIAR";  |ACABA;  |FINSI;
     |SI #20#2  = "4 FAMILIAR";  |ACABA;  |FINSI;
     |SI #20#11 = 0;             |ACABA;  |FINSI;

     |HAZ LimpiaPatrim;

     eaLetra    = "C1";
     eaDeDonde  = "D0";
     eaLinDonde = #20#1;
     eaAnyoUsu  = 0;
     eaPatr06   = #20#25;
     eaPatr08   = #20#23;
     eaPatr09   = #20#24;
     eaPatr15   = #20#22;
     eaPatr18   = #20#19;
     nValor     = #20#11 % #20#15;
     eaPatr10   = nValor;
     eaPatr20   = #20#5;
     eaPatr21   = #20#6;

     |HAZ GrabaPatrimonio;
|FPRC;

|PROCESO Graba58;
     |ABRE #58;
     |SELECT #1#58;
     #58#0 = #20#0;
     #58#1 = 99;
     |LEE 040#58];
     |SI FSalida = 0;
         |LEE 040#58a;
     |SINO;
         |LEE 040#58u;
     |FINSI;
     nLinea = 1;

     |SI FSalida = 0;  |Y #58#0 = #20#0;
         nLinea = #58#1 + 1;
     |FINSI;

     |DDEFECTO #58;
     #58#0  = #20#0;
     #58#1  = nLinea;
     #58#2  = #20#2;
     #58#3  = "R";
     |SI #20#3 = "X";  #58#3 = "X";  |FINSI;
     #58#4  = #20#4;
     #58#5  = #20#5;
     #58#6  = #20#6;
     #58#7  = #20#7;
     #58#8  = #20#8;
     #58#9  = #20#9;
     #58#10 = #20#10;
     #58#11 = #20#15;
     #58#12 = #20#21;
     #58#13 = 3;
     #58#14 = "E1";
     #58#15 = #20#1;
     #58#22 = #20#45 % #58#11;
     #58#23 = #20#47 % #58#11;
     #58#24 = #58#22 - #58#23;
     |GRABA 020#58n;
     |LIBERA #58;

     |CIERRA #58;

     eaPrograma   = "rem00058";
     eaPunto      = "  ";
     |HAZ ProgramaActivo;
|FPRC;

|PROCESO Tipo2m20;  |TIPO 2;
     nModo = (FEntrada / 100) ! 0;

     nEntrada = 0 +. 1;
     |SI nEntrada ! 1;
         eaDeDonde  = "D0";
         eaLinDonde = #20#1;
         eaLetra    = "C1";
         |HAZ BorraPatrimonio;

         |ABRE #58;
         |SELECT #2#58;
         #58#0  = #20#0;
         #58#14 = "E1";
         #58#15 = #20#1;
         |LEE 101#58=;
         |SI FSalida = 0;
             |BORRA 020#58a;
             |LIBERA #58;

             eaPrograma   = "rem00058";
             eaPunto      = "  ";
             |HAZ ProgramaActivo;
         |FINSI;
         |CIERRA #58;
     |FINSI;

     |SI nModo = 3;  enSwRetorno = 1;  |ACABA;  |FINSI;
     |SI nModo = 1;  enSwRetorno = 1;           |FINSI;

     |SI nEntrada = 1;
         nDesde = 5;
         |SI nModo = 1;  nDesde = 0;  |FINSI;
         nModo     = 1;

         |HAZ Fantasma;

         |SI enSwLiquida = 1;
             |PARA nCampos = 0;  |SI nCampos [ 4;  |HACIENDO nCampos = nCampos + 1;
                   |SI #20nCampos ! #1003nCampos;
                       enSwRetorno = 1;
                   |FINSI;
             |SIGUE;
         |FINSI;
         |HAZ Patrimonio;

         |SI #20#14 = "S";  |HAZ Graba58;  |FINSI;
     |SINO;
         |PARA nCampos = 0;  |SI nCampos [ 4;  |HACIENDO nCampos = nCampos + 1;
               #1003nCampos = #20nCampos;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO Tipo11m20;  |TIPO 11;
     |SI FSalida = 10;
         nDesde = 0;   nModo = 4;   |HAZ Fantasma;
         |ERROR;
     |FINSI;

     |SI FSalida = 11;
         |SI #20#2 ! "VARIOS    ";  |ACABA;  |FINSI;

         #900#0 = #20#5;
         #900#1 = #20#6;
         #900#2 = #20#7;
         #900#3 = #20#8;
         #900#4 = #20#9;
         #900#5 = #20#10;

         |PUSHV 0501 2380;
         |PINPA #0#900;
         |PINDA #0#900;
         |LEETECLA aTecla;
         |POPV;
         |ERROR;
     |FINSI;
|FPRC;

|PROGRAMA;
     aQueQuiero = "|NP";
     aNumPanta  = #1003#aQueQuiero#;
     nNumPanta  = aNumPanta;
     nNumPanta  = nNumPanta - 1;

     aQueQuiero = "|NC";
     aNumCampos = #1003#aQueQuiero#;
     nNumCampos = aNumCampos;
     nNumCampos = nNumCampos - 1;

     |ENTLINEAL #1#20, -2, enModo, 22, 1, ClaveBaja20, ClaveAlta20;

     eaPrograma   = "rem00025";
     eaPunto      = "  ";
     |HAZ ProgramaActivo;
|FPRO;
