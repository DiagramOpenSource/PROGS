|FICHEROS;
     rem00054 #20;
     rew99998 #900,MANTE;
     espejo@ #99;
|FIN;

|VARIABLES;
     aAlfa         = "";
     aAlfa1        = "";
     aLong         = "";
     aQueQuiero    = "";
     aNumCampos    = "";
     aNumPanta     = "";
     aPathRenta    = "";
     aDef          = "";
     aTecla        = "";

     nLong         = 0;
     nCaracter     = 0;
     nPosi         = 0;
     nPanta        = 0;
     nNumCampos    = 0;
     nNumPanta     = 0;
     nCampo        = 0;
     nModo         = 0;
     nEntrada      = 0;

     &enEmpresa    = 0;
     &enModo       = 0;
     &eaDeclarante = "";
     &enComunes    = 0;
     &enMinusvalia = 0;
     &eaMinuAyuda  = "";
     &eaFallece    = "";
     &eaSwVarios   = "";
     &eaVarDni     = "";
     &enCalcCif    = 0;
     &enSwLiquida  = 0;
     &enSwRetorno  = 0;
     &eaPrograma   = "";
     &eaPunto      = "";
|FIN;

|| ************************************************************************
|| PROCESOS DE CONSULTA DE PANTALLAS NORMALES
|| ************************************************************************

|PROCESO Modificable;
     |C_M #20#11, 1, "S";
     |C_M #20#12, 1, "S";
     |C_M #20#15, 1, "S";
     |SI #20#14 = "S";
         |C_M #20#11, 1, "N";  #20#11 = "";  |PINTA #20#11;
         |C_M #20#12, 1, "N";  #20#12 = "";  |PINTA #20#12;
     |SINO;
         |C_M #20#15, 1, "N";  #20#15 = 0;   |PINTA #20#15;
     |FINSI;
|FPRC;

|PROCESO LetraNif;  |TIPO 6;
     |SI FSalida = 10;
         eaVarDni  = #20Campo;
         enCalcCif = 1;
         |HAZ CalculoDNI;
         #20Campo = eaVarDni;
         |PINTA #20Campo;
         |ERROR;
         |ACABA;
     |FINSI;

     eaVarDni  = #20Campo;
     enCalcCif = 0;
     |HAZ CalculoDNI;
     eaVarDni = (eaVarDni + "            ") % 112;
     |SI #20Campo ! eaVarDni;
         |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
         |SI FSalida = 0;
             #20Campo = eaVarDni;  |PINTA #20Campo;
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo11m20;  |TIPO 11;
     |SI FSalida = 11;
         |SI #20#2 ! "VARIOS    ";  |ACABA;  |FINSI;

         #900#0 = #20#5;
         #900#1 = #20#6;
         #900#2 = #20#7;
         #900#3 = #20#8;
         #900#4 = #20#9;
         #900#5 = #20#10;

         |PUSHV 0501 2380;
         |PINPA #0#900;
         |PINDA #0#900;
         |LEETECLA aTecla;
         |POPV;
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Varios;  |TIPO 0;
     |SI #20#3 = "X";  |ACABA;  |FINSI;

     |SI #20#2 ! "VARIOS    ";
         #20#5  = 0;
         #20#6  = 0;
         #20#7  = 0;
         #20#8  = 0;
         #20#9  = 0;
         #20#10 = 0;
         |ACABA;
     |FINSI;

     |C_M #900#0,  1, "N";
     |C_M #900#1,  1, "N";
     |C_M #900#2,  1, "N";
     |C_M #900#3,  1, "N";
     |C_M #900#4,  1, "N";
     |C_M #900#5,  1, "N";

     aAlfa = eaSwVarios;
     aLong = aAlfa % 0;
     nLong = aLong;
     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPosi = (100 * nCaracter) + 1;
           aAlfa1 = aAlfa % nPosi;
           |SI aAlfa1 = "T";  |C_M #900#0,  1, "S";  |FINSI;
           |SI aAlfa1 = "C";  |C_M #900#1,  1, "S";  |FINSI;
           |SI aAlfa1 = "1";  |C_M #900#2,  1, "S";  |FINSI;
           |SI aAlfa1 = "2";  |C_M #900#3,  1, "S";  |FINSI;
           |SI aAlfa1 = "3";  |C_M #900#4,  1, "S";  |FINSI;
           |SI aAlfa1 = "4";  |C_M #900#5,  1, "S";  |FINSI;
     |SIGUE;

     #900#0 = #20#5;
     #900#1 = #20#6;
     #900#2 = #20#7;
     #900#3 = #20#8;
     #900#4 = #20#9;
     #900#5 = #20#10;

     |PUSHV 0501 2380;

     |PINPA #0#900;
     |PINDA #0#900;
     |ENDATOS #1#900;
     |SI FSalida = 0;
         #20#5  = #900#0;
         #20#6  = #900#1;
         #20#7  = #900#2;
         #20#8  = #900#3;
         #20#9  = #900#4;
         #20#10 = #900#5;
     |FINSI;

     |POPV;
|FPRC;

|RUTINA ClaveBaja20;
     #20#0 = enEmpresa;
     #20#1 = 1;
|FRUT;

|RUTINA ClaveAlta20;
     #20#0 = enEmpresa;
     #20#1 = 99;
|FRUT;

|PROCESO PonDeclarante;  |TIPO 7;
     eaDeclarante = #20#2;
     enComunes    = 0;
     |HAZ SacaDeclarantes;
     #20#2 = eaDeclarante;
     |PINTA #20#2;
|FPRC;

|PROCESO Fallece;  |TIPO 7;
     |SI eaFallece = "S";
         |C_M #20#4, 1, "S";
     |SINO;
         |C_M #20#4, 1, "N";  #20#4 = 0;  |PINTA #20#4;
     |FINSI;
|FPRC;

|PROCESO TipoX;
     |SI #20Campo = "X";
         |PTEC 806;
     |FINSI;
|FPRC;

|PROCESO Tipo2m20;  |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;  |O nModo = 3;
         enSwRetorno  = 1;
         |ACABA;
     |FINSI;

     nEntrada = 0 +. 1;
     |SI nEntrada ! 1;
         |PARA nCampo = 0;  |SI nCampo [ nNumCampos;  |HACIENDO nCampo = nCampo + 1;
               #99nCampo = #20nCampo;
         |SIGUE;
     |SINO;
         |PARA nCampo = 0;  |SI nCampo [ nNumCampos;  |HACIENDO nCampo = nCampo + 1;
               |SI #99nCampo ! #20nCampo;
                   enSwRetorno  = 1;
               |FINSI;
         |SIGUE;
     |FINSI;
|FPRC;

|PROGRAMA;
     aQueQuiero = "|NP";
     aNumPanta  = #20#aQueQuiero#;
     nNumPanta  = aNumPanta;
     nNumPanta  = nNumPanta - 1;

     aQueQuiero = "|NC";
     aNumCampos = #20#aQueQuiero#;
     nNumCampos = aNumCampos;
     nNumCampos = nNumCampos - 1;

     |DBASE aPathRenta;  |QBT aPathRenta;
     aDef = aPathRenta + "def/rem00054.mas";
     |CARGA_DEF aDef, espejo@;
     |SI FSalida ! 0;
         aAlfa = "     Error en cargar el Def rem00054.mas";
         |MENAV aAlfa;
         |ERROR;
     |FINSI;

     |ENTLINEAL #1#20, -2, enModo, 22, 1, ClaveBaja20, ClaveAlta20;

     |DESCARGA_DEF #espejo@;

     eaPrograma   = "rem00054";
     eaPunto      = "  ";
     |HAZ ProgramaActivo;
|FPRO;
