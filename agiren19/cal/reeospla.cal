|FICHEROS;
     reeospla;
     reeoscfg;

     eosconpl@;
     agicontr;
|FIN;

|VARIABLES;
     alfa1 = "";
     alfa2 = "";
     alfa3 = "";
     alfa4 = "";
     nume1 = 0;
     nume2 = 0;
     nume3 = 0;
     nume4 = 0;
     para1 = 0;
     para2 = 0;
     para3 = 0;
     para4 = 0;
     fech1 = @;
     fech2 = @;
     fech3 = @;
     fech4 = @;
     FEstado = 0;
     seleccio = 0;
     agieos = "";
     xxx    = 0;
     yyy    = 0;

     Comodin = "";
     Caracter = "";
     Calc     = 0;
     LongAlfa = "";
     Long     = 0;
     Primero  = 0;
     aTecla   = "";
|FIN;

||INCLUYE i_movimi;
____________________________________/

|CALCULO lee_concep;
     |DBASS alfa1;  |QBF alfa1;
     |PATH_DAT #agicontr#alfa1#;

     |ABRE #agicontr;
     |LEE 040#agicontr.p;
     |SI FSalida ! 0; |DDEFECTO #agicontr; |FINSI;
     |CIERRA #agicontr;

     agieos = #agicontr#3; |QBF agieos;
     Comodin = agieos;
     FSalida = 0;
     |PARA; |SI FSalida = 0; |HACIENDO;
        Caracter = Comodin % -101;
        |SI Caracter = "/";
           |SI Primero = 0;
              Primero = 1;
           |SINO;
              |SAL;
           |FINSI;
        |FINSI;
        LongAlfa = Comodin % 0;
        Long = LongAlfa;
        Long = Long - 1;
        Calc = 100 + Long;
        Comodin = Comodin % Calc;
     |SIGUE;


     Comodin = Comodin - "fich/";
     Comodin = Comodin + "def/eosconpl.mas";
     |CARGA_DEF Comodin, eosconpl@;
     |SI FSalida < 0;
        |MENAV "    Problemas al abrir la referencia ";
        |ERROR;
        |ACABA;
     |FINSI;

     |PATH_DAT #eosconpl@#agieos#;

     |ABRE #eosconpl@;
     #eosconpl@#0 = 19;                   || 2019;
     |LEE 001#eosconpl@.=;
     |SI FSalida ! 0;
         |MENAV "    Conceptos de Gastos no definidos en R.E.O. ...";
         |PTEC 807;
     |FINSI;
|FCAL;

|CALCULO defecto;
     |PARA xxx = 1; |SI xxx [ 20; |HACIENDO xxx = xxx + 1;
           #reeospla#xxx# = #eosconpl@#xxx#;
     |SIGUE;

     |PARA xxx = 61; |SI xxx [ 80; |HACIENDO xxx = xxx + 1;
           yyy = xxx - 40;
           #reeospla#xxx# = #eosconpl@#yyy#;
     |SIGUE;
|FCAL;

|PROCESO T7; |TIPO 7;
     |MENSA "0000 Pulse F11 para consultar";
|FPRC;

|PROCESO Baja;
     #reeoscfg#0 = "B";
     #reeoscfg#1 = 1;
|FPRC;

|PROCESO Alta;
     #reeoscfg#0 = "B";
     #reeoscfg#1 = 99;
|FPRC;

|PROCESO T66;  |TIPO 66;
     |ABRE #reeoscfg;
     |CONSULTA_F_CLAVE #1#reeoscfg, Baja, Alta;
     |SI FSalida = 0;
         #reeospla#Campo# = #reeoscfg#1;
         |PINTA #reeospla#Campo#;
     |FINSI;
     |CIERRA #reeoscfg;
|FPRC;

|PROCESO T0;  |TIPO 0;
     |SI #reeospla#Campo# = 0;
         |ACABA;
     |FINSI;

     |ABRE #reeoscfg;
     #reeoscfg#0 = "B";
     #reeoscfg#1 = #reeospla#Campo#;
     |LEE 000#reeoscfg.=;
     |SI FSalida ! 0;
         |MENAV "0000Codigo inexistente.";
         |ERROR;
     |FINSI;
     |CIERRA #reeoscfg;
|FPRC;

|PROGRAMA;
     |HAZ lee_concep;

     |CLS;
     |CABEZA "Control Conceptos";
     |DDEFECTO #reeospla;
     |ABRE #reeospla;
     |LEE 101#reeospla.p;
     FEstado = FSalida;
     |HAZ defecto;
     |PINPA #0#reeospla;
     |PINDA #0#reeospla;
     |ENDATOS #1#reeospla;
     |SI FSalida = 0;
         |SI FEstado = 0;  |GRABA 020#reeospla.a;  |FINSI;
         |SI FEstado ! 0;  |GRABA 020#reeospla.n;  |FINSI;
     |FINSI;
     |LIBERA #reeospla;
     |CIERRA #reeospla;
     |CIERRA #eosconpl@;
     |DESCARGA_DEF #eosconpl@;
|FPRO;

|PROCESO T80;  |TIPO 80;
     |ABRE #reeoscfg;
|FPRC;

|PROCESO T90;  |TIPO 90;
     |CIERRA #reeoscfg;
|FPRC;
