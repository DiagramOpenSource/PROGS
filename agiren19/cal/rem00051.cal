|FICHEROS;
     rem00051;

     rew00651,MANTE;

     rew00003;
     rew00005;
|FIN;

|VARIABLES;
     aAlfa         = "";
     aAlfa1        = "";
     aAlfa2        = "";
     aAlfa3        = "";
     aLong         = "";
     aQueQuiero    = "";
     aNumCampos    = "";
     aNumPanta     = "";
     aPathRenta    = "";
     aDef          = "";
     aDescri       = "";

     aEsc1         = "";
     aEsc2         = "";
     aRetu         = "";
     aTecla        = "";
     aID           = "";

     nVentana       = -1;
     nCampo         = 0;
     nRango         = 0;
     nGrid          = -1;
     nBotonAlta     = 0;
     nBotonModi     = 0;
     nBotonBaja     = 0;
     nBotonCntr     = 0;
     nBotonRdto     = 0;
     nBotonCalc1    = 0;
     nPanta         = 0;
     nID            = 0;
     nEntrar        = 0;

     nLong         = 0;
     nCaracter     = 0;
     nPosi         = 0;
     nNumCampos    = 0;
     nNumPanta     = 0;
     nModo         = 0;
     nEntrada      = 0;
     nPtas         = 0;
     nRetencion    = 0;
     nCampo15      = 0;
     nCampo16      = 0;
     nCampo17      = 0;
     nCampo20      = 0;
     nCampo27      = 0;
     nValor        = 0;
     nValor1       = 0;
     nValorPat     = 0;
     nDias         = 0;
     nAnyoVenta    = 0;
     nAnyoCompra   = 0;
     nDiferencia   = 0;
     nCoefi        = 0;
     nLinea        = 0;
     nContenido    = 0;
     nSwHay        = 0;
     nCampo1       = 0;
     nCampo2       = 0;
     nCampo3       = 0;
     nCampo4       = 0;
     nCampo5       = 0;
     nFallecido    = 0;

     nTodosDias    = 0;
     nParciDias    = 0;

     fFechaLim     = @;

     fFechaAdq     = @;
     fFechaTra     = @;
     fFecha1       = @;
     fFecha2       = @;

     &enEmpresa    = 0;
     &enModo       = 0;
     &enModoReg    = 0;
     &eaDeclarante = "";
     &enComunes    = 0;
     &enChequeo    = 0;
     &enMinusvalia = 0;
     &eaMinuAyuda  = "";
     &eaFallece    = "";
     &eaSwVarios   = "";
     &eaVarDni     = "";
     &enSwLiquida  = 0;
     &enSwRetorno  = 0;
     &eaPrograma   = "";
     &eaPunto      = "";

     &eaLetra      = "";
     &eaDeDonde    = "";
     &eaLinDonde   = "";
     &eaAnyoUsu    = "";
     &eaEdadDec    = "";
     &eaPatr06     = "";
     &eaPatr07     = "";
     &eaPatr08     = "";
     &eaPatr09     = "";
     &eaPatr10     = "";
     &eaPatr11     = "";
     &eaPatr12     = "";
     &eaPatr13     = "";
     &eaPatr14     = "";
     &eaPatr15     = "";
     &eaPatr16     = "";
     &eaPatr16_1   = "";
     &eaPatr17     = "";
     &eaPatr18     = "";
     &eaPatr19     = "";
     &eaPatr20     = "";
     &eaPatr21     = "";

     &eaAzul       = "";
     &eaVerde      = "";
     &eaRojo       = "";
     &eaRosa       = "";
     &enBtnSalir   = 0;
|FIN;

|| ---- PROCESOS GRABACIONES EXTERIORES

|PROCESO Patrimonio;
     |SI #rem00051#3  = "R";           |ACABA;  |FINSI;
     |SI #rem00051#3  = "X";           |ACABA;  |FINSI;
     |SI #rem00051#2  = "1 FAMILIAR";  |ACABA;  |FINSI;
     |SI #rem00051#2  = "2 FAMILIAR";  |ACABA;  |FINSI;
     |SI #rem00051#2  = "3 FAMILIAR";  |ACABA;  |FINSI;
     |SI #rem00051#2  = "4 FAMILIAR";  |ACABA;  |FINSI;
     |SI #rem00051#15 = 0;             |ACABA;  |FINSI;

     |HAZ LimpiaPatrim;

     eaDeclarante = #rem00051#2;

     eaLetra    = #rem00051#11;
     |SI #rem00051#13 = "U";
         |SI #rem00051#11 ! "H1"; |Y #rem00051#11 ! "H2";
             eaPatr14  =  #rem00051#15;
             eaLetra   = "M1";
         |FINSI;
     |FINSI;

     eaDeDonde  = "C3";
     eaLinDonde = #rem00051#1;
     eaAnyoUsu  = #rem00051#14;
     eaPatr06   = #rem00051#17;
     eaPatr16   = #rem00051#13;
     eaPatr16_1 = #rem00051#19;
     eaPatr10   = #rem00051#15;
     eaPatr14   = #rem00051#24;

     eaPatr20 = "0";
     eaPatr21 = "0";

     |SI #rem00051#2 = "TITULAR   ";
         eaPatr20 = "100";
     |FINSI;

     |SI #rem00051#2 = "CONYUGE   ";
         eaPatr21 = "100";
     |FINSI;

     |SI #rem00051#2 = "GANANCIAL ";
         eaPatr20 = "50";
         eaPatr21 = "50";
     |FINSI;

     |SI #rem00051#2 = "VARIOS    ";
         eaPatr20 = #rem00051#5;
         eaPatr21 = #rem00051#6;
     |FINSI;

     |HAZ GrabaPatrimonio;
|FPRC;

|| ---- PROCESOS DEL REW00651

|PROCESO Tipo12Fw651;  |TIPO 12;
     |SI FSalida = 7;  |ACABA;  |FINSI;

     aAlfa = #rew00651#2;  |QBF aAlfa;
     |SI aAlfa = "";
         |MENAV "0000No se puede validar la ficha, no hay contribuyente introducido";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo0C1Fw651;  |TIPO 0;
     |C_M #rew00651#1, 0, aAlfa;
     |SI aAlfa = "N";  |ACABA;  |FINSI;

     #rem00051#0 = #rew00651#0;
     #rem00051#1 = #rew00651#1;
     |LEE 000#rem00051.=;
     |SI FSalida = 0;
         |MENAV "0000El registro ya existe.";
         |ERROR;
         |ACABA;
     |FINSI;
|FPRC;

|PROCESO PrimeraVez1;  |TIPO 7;
     |SI #rew00651#Campo# = 0;
         #rew00651#Campo# = #rew00651#15;
         |PINTA #rew00651#Campo#;
     |FINSI;
|FPRC;

|PROCESO PrimeraVez2;  |TIPO 7;
     |SI #rew00651#Campo# = 0;
         #rew00651#Campo# = #rew00651#21;
         |PINTA #rew00651#Campo#;
     |FINSI;
|FPRC;

|PROCESO Totalm020;
     #rew00651#23 = #rew00651#21 - #rew00651#22;

     |SI #rew00651#25 ! -1;
         |C_M #rew00651#26, 1, "N";

         #rew00651#25 = 0;
         |SI #rew00651#19 = "V";
             |SI #rew00651#20 [ 39;                         #rew00651#25 = 40;  |FINSI;
             |SI #rew00651#20 ] 40;  |Y #rew00651#20 [ 49;  #rew00651#25 = 35;  |FINSI;
             |SI #rew00651#20 ] 50;  |Y #rew00651#20 [ 59;  #rew00651#25 = 28;  |FINSI;
             |SI #rew00651#20 ] 60;  |Y #rew00651#20 [ 65;  #rew00651#25 = 24;  |FINSI;
             |SI #rew00651#20 ] 66;  |Y #rew00651#20 [ 69;  #rew00651#25 = 20;  |FINSI;
             |SI #rew00651#20 ] 70;                    #rew00651#25 = 8;   |FINSI;
         |SINO;
             |SI #rew00651#20 [ 5;                     #rew00651#25 = 12;  |FINSI;
             |SI #rew00651#20 ] 6;   |Y #rew00651#20 [ 10;  #rew00651#25 = 16;  |FINSI;
             |SI #rew00651#20 ] 11;  |Y #rew00651#20 [ 15;  #rew00651#25 = 20;  |FINSI;
             |SI #rew00651#20 ] 16;                    #rew00651#25 = 25;  |FINSI;
         |FINSI;

         #rew00651#26 = #rew00651#24 % #rew00651#25;
     |SINO;
         |C_M #rew00651#26, 1, "S";
     |FINSI;

     |PINTA #rew00651#23;
     |PINTA #rew00651#25;
     |PINTA #rew00651#26;
|FPRC;

|PROCESO Usufructuario;
     |C_M #rew00651#14, 1, "S";
     |SI #rew00651#13 ! "N";
         |C_M #rew00651#14, 1, "N";  #rew00651#14 = 0;  |PINTA #rew00651#14;
     |FINSI;
|FPRC;

|PROCESO CargaFantasma;
     |C_M #rew00651#13, 1, "S";
     |C_M #rew00651#14, 1, "S";
     |C_M #rew00651#15, 1, "S";
     |C_M #rew00651#16, 1, "S";
     |C_M #rew00651#17, 1, "S";
     |C_M #rew00651#18, 1, "S";
     |C_M #rew00651#19, 1, "S";
     |C_M #rew00651#20, 1, "S";
     |C_M #rew00651#21, 1, "S";
     |C_M #rew00651#22, 1, "S";
     |C_M #rew00651#24, 1, "S";
     |C_M #rew00651#25, 1, "S";
     |C_M #rew00651#29, 1, "S";

     |PARA nCampo = 0;  |SI nCampo [ nNumCampos;  |HACIENDO nCampo = nCampo + 1;
           #rew00651#nCampo# = #rem00051#nCampo#;

           |C_V #rew00651#nCampo#, 0, aAlfa;
           |SI aAlfa = "S";  |PINTA #rew00651#nCampo#;  |FINSI;
     |SIGUE;

     eaDeDonde  = "C3";
     eaLinDonde = #rem00051#1;
     eaLetra    = #rem00051#11;
     |HAZ BorraPatrimonio;

     |SI #rem00051#13 = "U";
         eaLetra   = "M1";
         |HAZ BorraPatrimonio;
     |FINSI;
|FPRC;

|PROCESO DescargaFantasma;
     |PARA nCampo = 0;  |SI nCampo [ nNumCampos;  |HACIENDO nCampo = nCampo + 1;
           |SI enSwLiquida = 1;
               |SI #rem00051#nCampo# = #rew00651#nCampo#;
                   enSwRetorno = 1;
               |FINSI;
           |FINSI;

           #rem00051#nCampo# = #rew00651#nCampo#;
     |SIGUE;

                        #rem00051#30 = "";
     |SI #rem00051#3  = "X";  #rem00051#30 = eaRojo;  |FINSI;

     |HAZ PintaDatosPantalla;
|FPRC;

|PROCESO Alta;
     |ESTADO_CONTROL nBotonAlta,   "DISABLE";
     |ESTADO_CONTROL nBotonModi,   "DISABLE";
     |ESTADO_CONTROL nBotonBaja,   "DISABLE";
     |ESTADO_CONTROL nBotonRdto,   "DISABLE";
     |ESTADO_CONTROL nBotonCalc1,  "ENABLE";

     |HAZ QuitaBtnSalir;

     |ESTADO_CONTROL nGrid, "DISABLE";

     |PULSATECLA;

     #rem00051#0 = enEmpresa;
     #rem00051#1 = 99;
     |LEE 000#rem00051.];
     |SI FSalida = 0;
         |LEE 000#rem00051.a;
     |SINO;
         |LEE 000#rem00051.u;
     |FINSI;

     |DDEFECTO #rew00651;
     #rew00651#0 = enEmpresa;
     #rew00651#1 = 1;
     |SI FSalida = 0;  |Y  #rem00051#0 = enEmpresa;
         #rew00651#1 = #rem00051#1 + 1;
     |FINSI;

     |DDEFECTO #rem00051;

     #rem00051#0 = #rew00651#0;
     #rem00051#1 = #rew00651#1;

     enModoReg = 1;
     |HAZ CargaFantasma;

     |C_M #rew00651#1, 1, "S";
     |ENDATOS #1#rew00651;
     |SI FSalida = 0;
         |HAZ DescargaFantasma;

         |GRABA 020#rem00051.n;
         |LIBERA #rem00051;

         |HAZ Patrimonio;
     |FINSI;

     |SI enModo ! 4;
         |ESTADO_CONTROL nBotonAlta, "ENABLE";
     |FINSI;

     |ESTADO_CONTROL nBotonRdto,  "ENABLE";
     |ESTADO_CONTROL nBotonCalc1, "DISABLE";

     |HAZ PonBtnSalir;

     enModoReg = 4;
     |ESTADO_CONTROL nGrid, "ENABLE";
     |REFRESCACONTROL nGrid;

     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO Modificar;
     |SI nEntrar = 0;  |ACABA;  |FINSI;

     |LEE 101#rem00051.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |ESTADO_CONTROL nBotonAlta,  "DISABLE";
     |ESTADO_CONTROL nBotonModi,  "DISABLE";
     |ESTADO_CONTROL nBotonBaja,  "DISABLE";
     |ESTADO_CONTROL nBotonRdto,  "DISABLE";
     |ESTADO_CONTROL nBotonCalc1, "ENABLE";

     |HAZ QuitaBtnSalir;

     |ESTADO_CONTROL nGrid, "DISABLE";

     enModoReg = 2;
     |C_M #rew00651#1, 1, "N";

     |HAZ CargaFantasma;
     |ENDATOS #2#rew00651;
     |SI FSalida = 0;
         |HAZ DescargaFantasma;

         |GRABA 020#rem00051.a;
     |FINSI;
     |LIBERA #rem00051;

     |HAZ Patrimonio;

     |ESTADO_CONTROL nBotonAlta, "ENABLE";
     |ESTADO_CONTROL nBotonModi, "ENABLE";
     |ESTADO_CONTROL nBotonBaja, "ENABLE";
     |ESTADO_CONTROL nBotonRdto, "ENABLE";

     |ESTADO_CONTROL nBotonCalc1, "DISABLE";

     |HAZ PonBtnSalir;

     enModoReg = 4;
     |ESTADO_CONTROL nGrid, "ENABLE";
     |REFRESCACONTROL nGrid;
     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO Baja;
     |SI nEntrar = 0;  |ACABA;  |FINSI;

     |CONFI "0000 Confirma la baja del registro seleccionado";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |LEE 101#rem00051.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |BORRA 020#rem00051.a;
     |LIBERA #rem00051;

     eaDeDonde  = "C3";
     eaLinDonde = #rem00051#1;
     eaLetra    = #rem00051#11;
     |HAZ BorraPatrimonio;

     |SI #rem00051#13 = "U";
         eaLetra   = "M1";
         |HAZ BorraPatrimonio;
     |FINSI;

     enSwRetorno = 1;
     nEntrar     = 0;

     |ESTADO_CONTROL nBotonModi,  "DISABLE";
     |ESTADO_CONTROL nBotonBaja,  "DISABLE";

     |REFRESCACONTROL nGrid;
     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO BotonCntr;
     enComunes   = 1;
     enChequeo   = 0;

     #rew00003#0 = #rew00651#2;
     #rew00003#1 = #rew00651#3;
     #rew00003#2 = #rew00651#4;
     #rew00003#3 = #rew00651#5;
     #rew00003#4 = #rew00651#6;
     #rew00003#5 = #rew00651#7;
     #rew00003#6 = #rew00651#8;
     #rew00003#7 = #rew00651#9;
     #rew00003#8 = #rew00651#10;

     |HAZ PantaREW00003;

     #rew00651#2  = #rew00003#0;    |PINTA #rew00651#2;
     #rew00651#3  = #rew00003#1;
     #rew00651#4  = #rew00003#2;
     #rew00651#5  = #rew00003#3;
     #rew00651#6  = #rew00003#4;
     #rew00651#7  = #rew00003#5;
     #rew00651#8  = #rew00003#6;
     #rew00651#9  = #rew00003#7;
     #rew00651#10 = #rew00003#8;

     aAlfa = "|C013WID";
     aID   = #rew00651#aAlfa#;
     nID   = aID;
     |FOCOTECLADO nID;

     |SI #rew00651#3 = "X";  |Y enModoReg ! 4;
         |PTEC 806;
     |FINSI;
|FPRC;

|PROCESO Tipo7C2Fw651;  |TIPO 7;
     aAlfa = #rew00651#2;  |QBF aAlfa;
     |SI aAlfa ! "";  |Y #rew00651#3 ! "X";
         |ACABA;
     |FINSI;

     |HAZ BotonCntr;
|FPRC;

|PROCESO BotonCalc;
     |SI enModoReg = 4;  |ACABA;  |FINSI;

     |DDEFECTO #rew00005;

     |SI FSalida = nBotonCalc1; nCampo = 15;  |FINSI;

     #rew00005#1  = #rew00651#nCampo#;
     #rew00005#20 = #rew00651#nCampo#;

     |HAZ PantaREW00005;

     #rew00651#nCampo# = #rew00005#20;  |PINTA #rew00651#nCampo#;

     aAlfa = "|C" + (("000" + nCampo) % -103) + "WID";
     aID   = #rew00651#aAlfa#;
     nID   = aID;
     |FOCOTECLADO nID;
|FPRC;

|PROCESO Rendimientos;
     |SI enModoReg ! 4;  |ACABA;  |FINSI;

     |HAZ QuitaBtnSalir;

     |SUB_EJECUTA_NP "rew00551";

     |HAZ PonBtnSalir;

     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO PintaDatosPantalla;
     |DDEFECTO #rew00651;
     |PARA nCampo = 0;  |SI nCampo [ nNumCampos;  |HACIENDO nCampo = nCampo + 1;
           #rew00651#nCampo# = #rem00051#nCampo#;

           |C_V #rew00651#nCampo#, 0, aAlfa;
           |SI aAlfa = "S";  |PINTA #rew00651#nCampo#;  |FINSI;

           |C_M #rew00651#nCampo#, 1, "N";
     |SIGUE;
|FPRC;

|PROCESO Evento20;
     |SI enModoReg ! 4;  |ACABA;  |FINSI;

     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#rem00051.=;

         |SI enModo = 4;
             |ESTADO_CONTROL nBotonAlta, "DISABLE";
             |ESTADO_CONTROL nBotonModi, "DISABLE";
             |ESTADO_CONTROL nBotonBaja, "DISABLE";
         |SINO;
             |ESTADO_CONTROL nBotonModi, "ENABLE";
             |ESTADO_CONTROL nBotonBaja, "ENABLE";
         |FINSI;

         |PULSATECLA;

         nEntrar   = 1;
         enModoReg = 4;
         |HAZ PintaDatosPantalla;
     |FINSI;

     |SI FSalida = 4;
         |SI enModo = 4;  |ACABA;  |FINSI;

         |HAZ Modificar;
     |FINSI;
|FPRC;

|PROCESO BotonesPanta;
     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}&Alta ", 2264, 2274, Alta;
     nBotonAlta = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}&Modificar ", 2276, 2286, Modificar;
     nBotonModi = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{199}}&Borrar", 2288, 2298, Baja;
     nBotonBaja = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}&Rendimientos", 0580, 0598, Rendimientos;
     nBotonRdto = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}} &Y", 2440, 2444, BotonCntr;
     nBotonCntr = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,&1", 2750, 2751, BotonCalc;
     nBotonCalc1 = FSalida;

     nEntrar = 0;

     |ESTADO_CONTROL nBotonModi,   "DISABLE";
     |ESTADO_CONTROL nBotonBaja,   "DISABLE";
     |ESTADO_CONTROL nBotonCalc1,  "DISABLE";

     |SI enModo = 4;
         |ESTADO_CONTROL nBotonAlta, "DISABLE";
     |FINSI;
|FPRC;

|PROCESO Baja20;
     #rem00051#0 = enEmpresa;
     #rem00051#1 = 1;
|FPRC;

|PROCESO Alta20;
     #rem00051#0 = enEmpresa;
     #rem00051#1 = 99;
|FPRC;

|PROGRAMA;
     |HAZ CargaColores;

     enSwRetorno = 0;
     aQueQuiero  = "|NC";
     aNumCampos  = #rem00051#aQueQuiero#;
     nNumCampos  = aNumCampos;
     nNumCampos  = nNumCampos - 1;
     nRango      = 4 + 8 + 16 + 32 + 128;
     enModoReg   = 4;

     |PINPA #0#rew00651;  nPanta = FSalida;

     |HAZ BotonesPanta;

     |DDEFECTO #rem00051;
     |HAZ PintaDatosPantalla;

     |ABRET #rem00051;

     |LINEAL_SIMPLE #1#rem00051, nRango, 0602, 2198, Baja20, Alta20, Evento20;
     nGrid = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{001}}sa&Lir", 3488, 3498, Ptec806;
     enBtnSalir = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::," + nGrid;
          |LEETECLA aTecla;
          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;

          |SI aTecla = aRetu;
              |HAZ Modificar;
          |FINSI;
     |SIGUE;

     |CIERRAT #rem00051;

     eaPrograma   = "rem00051";
     eaPunto      = "  ";
     |HAZ ProgramaActivo;
|FPRO;
