|FICHEROS;
     rez00301 #0;
     reacceso #1;

     origen@  #100;
|FIN;

|VARIABLES;
     aRuta        = "";
     aAlfa        = "";
     aAlfa1       = "";
     aAlfa2       = "";
     aLong        = "";
     aExtension   = "";
     aPunto       = "";
     aDirDestino  = "";
     aCaracter    = "";
     aFichero     = "";
     aNombreFic   = "";
     aNombreRea   = "";
     aCR          = "";
     aLF          = "";
     aTrabajo     = "";
     aCadena      = "";
     aMas         = "";
     aOrigen      = "";
     aDestino     = "";
     aIPRemoto    = "";
     aBase        = "";

     nLong        = 0;
     nCarac       = 0;
     nPos         = 0;
     nPrincipio   = 0;
     nError       = 0;
     nErrorDef    = 0;
     nHandle      = 0;
     nCampo       = 0;
     nVentana     = 0;

 {-1}aVent        = "";
     aVent1       = 0;
     aVent2       = 0;
     aVent3       = 0;
     aVent4       = 0;
     aVent5       = "";

     &enEmpresa   = 0;
|FIN;

|PROCESO Tipo0C2;
     |ABRE #1;
     #1#0 = #0#2;
     |LEE 000#1=;
     |SI FSalida = 0;
         |CONFI "0000NEl codigo destino YA EXISTE, desea reemplazarlo";
         |SI FSalida ! 0;
             |ERROR;
         |FINSI;
     |FINSI;
     |CIERRA #1;
|FPRC;

|PROCESO PideDatos;
     |VENTANA 0501, 3499, -1, 16, "";
     nVentana = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     aTrabajo   = aCadena - "[RENTA]";
     |SI FSalida = 0;
         |MENAV "0000 Formato del fichero no valido.";
         nError  = 1;
         |ACABA;
     |FINSI;

     aAlfa1  = aTrabajo % 105;
     aAlfa2  = aTrabajo % 640;

     #0#0    = aAlfa1;
     #0#1    = aAlfa2;
     #0#2    = aAlfa1;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida ! 0;
         |MENAV "0000 Proceso interrunpido.";
         nError  = 1;
     |SINO;
         #0#3 = "Borrando renta destino ... ";  |PINTA #0#3;

         |ABRE #1;
         #1#0 = #0#2;
         |LEE 101#1=;
         |SI FSalida = 0;
             enEmpresa = #1#0;
             |HAZ BorraRenta;

             |BORRA 020#1a;
             |LIBERA #1;
         |SINO;
             enEmpresa = #0#2;
             |HAZ BorraRenta;
         |FINSI;
         |CIERRA #1;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVentana;
|FPRC;

|PROCESO TrataCadena;
     nError = 0;

     |SI nPrincipio = 0;
         |HAZ PideDatos;
         nPrincipio = 1;
         |ACABA;
     |FINSI;

     aTrabajo   = aCadena - "[FICHERO]";
     |SI FSalida ! 0;
         |SI aMas ! "";
             |CIERRA #100;
             |DESCARGA_DEF #origen@;
         |FINSI;

         nErrorDef = 0;
         aMas      = (aTrabajo + ".mas") < "";
         |CARGA_DEF aMas, origen@;
         |SI FSalida < 0;
             nErrorDef = 1;
             aMas      = "";
             |ACABA;
         |FINSI;

         |ABRE #100;

         #0#3 = "Recogiendo datos del fichero " + aMas;  |PINTA #0#3;

         |ACABA;
     |FINSI;

     |SI nErrorDef = 1;  |ACABA;  |FINSI;

     aTrabajo   = aCadena - "[INICIOREGISTRO]";
     |SI FSalida ! 0;
         nCampo = 0;
         |ACABA;
     |FINSI;

     aTrabajo   = aCadena - "[FINREGISTRO]";
     |SI FSalida ! 0;
         |GRABA 020#100n;
         |LIBERA #100;
         |ACABA;
     |FINSI;

     |SI nCampo = 0;
         #100nCampo = #0#2;
     |SINO;
         #100nCampo = aCadena;
     |FINSI;

     nCampo = nCampo + 1;
|FPRC;

|PROCESO TrataFichero;
     aFichero = aDirDestino + aNombreFic;  |QBF aFichero;
     |XABRE "E", aFichero, nHandle;
     |SI FSalida < 0;
         |MENAV "0000Fichero TXT no encontrado.";
         |ACABA;
     |FINSI;

     |XABRE "AB", aFichero, nHandle;

     aCR        = &10;
     aLF        = &13;
     nPrincipio = 0;
     aMas       = "";
     nCampo     = 0;

     |XLEE nHandle, 1, aAlfa;
     |PARA; |SI FSalida > 0; |HACIENDO;
            |SI aAlfa = aCR;
                |HAZ TrataCadena;
                aCadena = "";
                |SI nError = 1;  |SAL;  |FINSI;
            |SINO;
                |SI aAlfa ! aLF;
                    aCadena = aCadena + aAlfa;
                |FINSI;
            |FINSI;
            |XLEE nHandle, 1, aAlfa;
     |SIGUE;

     |SI aMas ! "";
         |CIERRA #100;
         |DESCARGA_DEF #origen@;
     |FINSI;

     |XCIERRA nHandle;

     |FBORRA aFichero;
|FPRC;

|PROGRAMA;
     |MENAV "0000A continuacion abra el fichero a importa. Debe estar situado en su PC.";

     aRuta = "F*.TGZ";
     |BROWSE aRuta;

     aAlfa = aRuta;
     aRuta = "";
     aLong = aAlfa % 0;
     nLong = aLong;
     |PARA nCarac = 2;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos       = (nCarac * 100) + 1;
           aCaracter  = aAlfa % nPos;
           aRuta      = aRuta      + aCaracter;
           aNombreFic = aNombreFic + aCaracter;

           |SI aCaracter = "/";  |O aCaracter = "\";
               aNombreFic = "";
           |FINSI;

     |SIGUE;

     aNombreRea = aNombreFic < "";
     aNombreFic = aNombreFic > "";
     aNombreFic = aNombreFic - ".TGZ" + ".TXT";
     aExtension = (aRuta % -104) > "";
     aPunto     = aRuta % -401;
     |SI aPunto ! ".";
         |MENAV "0000Fichero sin Extension. Formato exigido extension de 3 caracteres.";
         |ACABA;
     |FINSI;

     |SI aExtension ! ".TGZ";
         |MENAV "0000Fichero no valido. La  Extension debe ser TGZ.";
         |ACABA;
     |FINSI;

     |DBASE aBase;  |QBF aBase;
     aDirDestino = aBase + "importacion";  |MKDIR aDirDestino;
     aDirDestino = aBase + "importacion/";

     aOrigen  = aRuta;
     aDestino = (aDirDestino + aNombreRea) < "";

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |SI aIPRemoto ! "";
         |RECIBE_FICHERO aOrigen, aDestino;
     |SINO;
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;

     |XABRE "E", aDestino, nHandle;
     |SI FSalida < 0;
         |MENAV "0000Hubo un error a copiar el fichero al servidor.";
         |ACABA;
     |FINSI;

     aAlfa = aRuta < "";
     Pos   = -1;
     |DESCOMPRIME aDestino;
     |DETAR aDestino, aDirDestino;

     aAlfa = aDestino;                    |FBORRA aAlfa;
     aAlfa = aDestino - ".tgz" + ".tar";  |FBORRA aAlfa;

     |HAZ TrataFichero;
|FPRO;
