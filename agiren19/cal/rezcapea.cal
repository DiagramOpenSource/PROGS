|FICHEROS;
     rezcapea;

     reacceso;
     rem00042;
     rem00048;
     rem00056;
     retabla1;

     fictmp@;

     &reacceso@;
|FIN;

|INCLUYE capeac99;


|| -------------------------------------------------------------------------

|PROCESO VarRezcapea;
     eaCampo37  = #rezcapea#37;
     eaCampo0   = #rezcapea#0;

     eaCampo24  = #rezcapea#24;
     eaCampo25  = #rezcapea#25;
     eaCampo26  = #rezcapea#26;
     eaCampo27  = #rezcapea#27;
     eaCampo28  = #rezcapea#28;
     eaCampo29  = #rezcapea#29;
     eaCampo30  = #rezcapea#30;
     eaCampo31  = #rezcapea#31;
     eaCampo32  = #rezcapea#32;
     eaCampo33  = #rezcapea#33;
     eaCampo34  = #rezcapea#34;
     eaCampo35  = #rezcapea#35;
     eaCampo36  = #rezcapea#36;
     eaCampo37  = #rezcapea#37;
     eaCampo38  = #rezcapea#38;
     eaCampo39  = #rezcapea#39;
     eaCampo40  = #rezcapea#40;
     eaCampo41  = #rezcapea#41;
     eaCampo42  = #rezcapea#42;
     eaCampo43  = #rezcapea#43;
     eaCampo44  = #rezcapea#44;
|FPRC;

|PROCESO rem00042B;
     |BORRA 020#rem00042.a;
     |LIBERA #rem00042;
|FPRC;

|DEFBUCLE rem00042B;
     #rem00042#1;
     21;
     enEmpresa, 01, 01;
     enEmpresa, 99, 99;
     be;
     NULL, rem00042B;
|FIN;

|PROCESO rem00056B;
     |BORRA 020#rem00056.a;
     |LIBERA #rem00056;
|FPRC;

|DEFBUCLE rem00056B;
     #rem00056#1;
     ;
     enEmpresa, 01;
     enEmpresa, 99;
     be;
     NULL, rem00056B;
|FIN;

|PROCESO rem00048B;
     |BORRA 020#rem00048.a;
     |LIBERA #rem00048;
|FPRC;

|DEFBUCLE rem00048B;
     #rem00048#1;
     ;
     enEmpresa, 01;
     enEmpresa, 99;
     be;
     NULL, rem00048B;
|FIN;

|PROCESO Pasa2019;
     |SI #rezcapea#39 = "S";
         |BUCLE rem00056B;
         |BUCLE rem00042B;
         |BUCLE rem00048B;
     |FINSI;

     |SI #rezcapea#38 = "S";  |O #rezcapea#24 = "S";
      |O #rezcapea#25 = "S";
         |SUB_EJECUTA "rezcape1";
     |FINSI;

     |SI #rezcapea#26 = "S";  |O #rezcapea#43 = "S";
         |SUB_EJECUTA "rezcape2";
     |FINSI;

     |SI #rezcapea#35 = "S";
         |SUB_EJECUTA "rezcape3";
     |FINSI;

     |SI #rezcapea#36 = "S";  |O #rezcapea#27 = "S";
      |O #rezcapea#28 = "S";
         |SUB_EJECUTA "rezcape4";
     |FINSI;

     |SI #rezcapea#39 = "S";
         |SUB_EJECUTA "rezcape5";
     |FINSI;

     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
|FPRC;

|PROCESO Acceso2018;
     |SI #reacceso@#19 = "S";   |ACABA;  |FINSI;

     |SI #rezcapea#42 = "C";
         |SI #reacceso@#12 ! "*";  |ACABA;  |FINSI;
     |FINSI;

     enEmpresa = #reacceso@#0;

     aAlfa1 = #reacceso@#1;  |QBF aAlfa1;
     aAlfa  = "     Recogiendo Contribuyente [" + (("00000" + enEmpresa) % -105) + " " + aAlfa1 + "]";

     |ABRE #reacceso;
     #reacceso#0 = enEmpresa;
     |LEE 101#reacceso.=;
     |SI FSalida = 0;
         |SI enCodCli = 0;
             |SI #rezcapea#41 = "S";
                 aAlfa  = "0000NYa existe Contribuyente [" + (("00000" + enEmpresa) % -105) + " " + aAlfa1 + "] Desea pasar los datos de nuevo : ";
                 |CONFI aAlfa;
             |SINO;
                 |SI #reacceso#12 = "*";
                     FSalida = 1;
                 |FINSI;
             |FINSI;

             |SI FSalida ! 0;
                 |LIBERA #reacceso;
                 |CIERRA #reacceso;
                 |ACABA;
             |FINSI;
         |FINSI;

         |SI #reacceso#2 ! #reacceso@#2;  |Y #reacceso#2 ! "            ";
             |SI enCodCli ! 0;
                 |MENAV "0000El NIF del contribuyente del ejercicio anterior no coincide con el actual. Revise el dato";
             |SINO;
                 nMensaDupli = 1;
             |FINSI;

             |LIBERA #reacceso;
             |CIERRA #reacceso;
             |ACABA;
         |FINSI;

         #reacceso#0  = enEmpresa;
         #reacceso#1  = #reacceso@#1;
         #reacceso#2  = #reacceso@#2;
         #reacceso#20 = "";
         #reacceso#21 = #reacceso@#21;
         #reacceso#22 = #reacceso@#22;
         |GRABA 020#reacceso.a;
     |SINO;
         |DDEFECTO #reacceso;
         #reacceso#0  = enEmpresa;
         #reacceso#1  = #reacceso@#1;
         #reacceso#2  = #reacceso@#2;
         #reacceso#20 = "";
         #reacceso#21 = #reacceso@#21;
         #reacceso#22 = #reacceso@#22;
         |GRABA 020#reacceso.n;
     |FINSI;
     |LIBERA #reacceso;
     |CIERRA #reacceso;

     aAlfa1 = #reacceso@#1;  |QBF aAlfa1;
     aAlfa  = "     Recogiendo Contribuyente [" + (("00000" + enEmpresa) % -105) + " " + aAlfa1 + "]";
     |MENSA aAlfa;

     |HAZ Pasa2019;
|FPRC;

|DEFBUCLE reacceso;
     #reacceso@#1001;
     ;
     #rezcapea#2;
     #rezcapea#3;
     ;
     NULL, Acceso2018;
|FIN;

|PROCESO Lee2018;
     |HAZ VarRezcapea;

     aDef = #rezcapea#0 + "/def/reacceso.mas";  |QBT aDef;
     |CARGA_DEF aDef, reacceso@;
     |SI FSalida ! 0;
         aAlfa = "     Error en cargar el Def reacceso A¤o 2018 ";
         |MENAV aAlfa;
         |ACABA;
     |FINSI;

     eaPathRen2018 = #rezcapea#0 + "/" + #rezcapea#1 + "/";  |QBT eaPathRen2018;
     |PATH_DAT #reacceso@#eaPathRen2018#;

     |SI #rezcapea#2 ! 0;
         |BUCLE reacceso;
     |SINO;
         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |SI #rezcapea#nCampo# ! 0;
                   #rezcapea#2 = #rezcapea#nCampo#;
                   #rezcapea#3 = #rezcapea#nCampo#;
                   |BUCLE reacceso;
               |FINSI;
         |SIGUE;
     |FINSI;

     |DESCARGA_DEF #reacceso@;
|FPRC;

|PROCESO CopiaFichero;
     aAlfa1 = #rezcapea#0 + "/" + #rezcapea#1 + "/"  + aFichero + ".dat";  |QBT aAlfa1;
     aAlfa2 = eaPathRen2019 + "fich/" + aFichero + ".dat";
     |COPIA_FICHERO aAlfa1, aAlfa2;

     aAlfa1 = #rezcapea#0 + "/" + #rezcapea#1 + "/"  + aFichero + ".ddx";  |QBT aAlfa1;
     aAlfa2 = eaPathRen2019 + "fich/" + aFichero + ".ddx";
     |COPIA_FICHERO aAlfa1, aAlfa2;

     aAlfa1 = #rezcapea#0 + "/" + #rezcapea#1 + "/"  + aFichero + ".ixx";  |QBT aAlfa1;
     aAlfa2 = eaPathRen2019 + "fich/" + aFichero + ".ixx";
     |COPIA_FICHERO aAlfa1, aAlfa2;
|FPRC;

|PROCESO Auxiliares;
     aDef = eaPathRen2019 + "def/" + aFichero + ".mas";
     |CARGA_DEF aDef, fictmp@;
     |SI FSalida ! 0;
         aAlfa = "     Error en cargar el Def " + aFichero + " A¤o 2019 ";
         |MENAV aAlfa;
         |ACABA;
     |FINSI;

     |ABRE #fictmp@;
     |LEE 040#fictmp@.p;
     |SI FSalida ! 0;
         |CIERRA #fictmp@;
         |HAZ CopiaFichero;
     |FINSI;
     |CIERRA #fictmp@;

     |DESCARGA_DEF #fictmp@;
|FPRC;

|PROCESO ListadosDefinibles;
     aAlfa1 = eaPathRen2019 + "fich/lis*.*";
     |_PDIR aAlfa1;
     |PARA;  |SI FSalida = 0;  |HACIENDO;
             |FBORRA aAlfa1;

             |_SDIR aAlfa1;
     |SIGUE;

     aRaiz  =  #rezcapea#0 + "/" + #rezcapea#1 + "/";  |QBT aRaiz;
     aAlfa1 =  aRaiz + "lis*.*";
     |_PDIR aAlfa1;
     |PARA;  |SI FSalida = 0;  |HACIENDO;
             aAlfa = aAlfa1 - aRaiz;
             aAlfa2 = eaPathRen2019 + "fich/" + aAlfa;
             |COPIA_FICHERO aAlfa1, aAlfa2;

             |_SDIR aAlfa1;
     |SIGUE;

     |DFICE aAlfa;  |QBF aAlfa;
     aAlfa = aAlfa + "listados.dir";
     |XABRE "E", aAlfa, nHandle;
     |SI FSalida ] 0;  |ACABA;  |FINSI;

     |XABRE "WB", aAlfa, nHandle;

     aAlfa = "LOCK00" + &13 + &10;  |XGRABA nHandle, aAlfa;

     |XCIERRA nHandle;
|FPRC;

|PROCESO Maestros;
     |SI #rezcapea#44 = "S";
         |HAZ ListadosDefinibles;
     |FINSI;

     aFichero = "reparame";  |HAZ Auxiliares;
     aFichero = "repoblac";  |HAZ Auxiliares;
     aFichero = "reauxpai";  |HAZ Auxiliares;
     aFichero = "rerenpre";  |HAZ Auxiliares;
     aFichero = "reauxdiv";  |HAZ Auxiliares;
     aFichero = "reauxval";  |HAZ Auxiliares;
     aFichero = "rebancos";  |HAZ Auxiliares;
|FPRC;

|PROCESO Tipo12;  |TIPO 12;
     |SI FSalida = 7;  |ACABA;  |FINSI;

     |CONFI "0000SDesea hacer la importaci¢n en estos momentos.";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     #rezcapea#41 = #rezcapea#41 > "";

     |DBASE eaPathRen2019;  |QBT eaPathRen2019;

     |HAZ Maestros;
     |HAZ Lee2018;
|FPRC;

|| -------------------------------------------------------------------------

|PROCESO Ptec807;
     |PTEC 807;
|FPRC;

|PROCESO Directorios; |TIPO 7;
     |SI nBtnCancelar = 1;  |ACABA;  |FINSI;

     nBtnCancelar = 1;

     aQueBoton = "|X000";
     aID       = #rezcapea#aQueBoton#;
     nBoton    = aID;
     |ESTADO_CONTROL nBoton, "HIDE";

     aQueBoton = "|X001";
     aID       = #rezcapea#aQueBoton#;
     nBoton    = aID;
     |ESTADO_CONTROL nBoton, "HIDE";

     |SI enCodCli ! 0;
         |CONTROL_SIMPLE 0, "BOTON,{{197}}Importar", 3488, 3498, Ptec806;
         |CONTROL_SIMPLE 0, "BOTON,{{001}}Salir", 3475, 3485, Ptec807;
     |SINO;
         |CONTROL_SIMPLE 0, "BOTON,{{197}}Importar", 3688, 3698, Ptec806;
         |CONTROL_SIMPLE 0, "BOTON,{{001}}Salir", 3675, 3685, Ptec807;
     |FINSI;

     |DBASS aAlfa;
     #rezcapea#0  = aAlfa + "agiren18";
     #rezcapea#1  = "fich";
     #rezcapea#44 = aListados;

     |PINTA #rezcapea#0;
     |PINTA #rezcapea#1;
     |PINTA #rezcapea#44;
|FPRC;

|PROCESO Parrilla;
     |SI enCodCli ! 0;  |ACABA;  |FINSI;

     |SI #rezcapea#2 = 0;
         #rezcapea#2 = 0;  |PINTA #rezcapea#2;
         #rezcapea#3 = 0;  |PINTA #rezcapea#3;  |C_M #rezcapea#3, 1, "N";
         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #rezcapea#nCampo#, 1, "S";
         |SIGUE;
     |SINO;
         |C_M #rezcapea#2, 1, "S";
         |C_M #rezcapea#3, 1, "S";
         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #rezcapea#nCampo#, 1, "N";
               #rezcapea#nCampo# = 0;  |PINTA #rezcapea#nCampo#;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasContri;
     |SI enCodCli ! 0;  |ACABA;  |FINSI;

     |SI #rezcapea#Campo# = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               #rezcapea#Campo# = 0;  |C_M #rezcapea#nCampo#, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #rezcapea#nCampo#, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO ModInmuebles;
     |C_M #rezcapea#29, 1, "S";
     |C_M #rezcapea#30, 1, "S";
     |C_M #rezcapea#32, 1, "S";

     |SI #rezcapea#25 = "N";
         |C_M #rezcapea#29, 1, "N";  #rezcapea#29 = "N";  |PINTA #rezcapea#29;
         |C_M #rezcapea#30, 1, "N";  #rezcapea#30 = "N";  |PINTA #rezcapea#30;
         |C_M #rezcapea#32, 1, "N";  #rezcapea#32 = "N";  |PINTA #rezcapea#32;
     |FINSI;

     |C_M #rezcapea#31, 1, "S";
     |SI #rezcapea#30 = "N";
         |C_M #rezcapea#31, 1, "N";  #rezcapea#31 = 0;    |PINTA #rezcapea#31;
     |FINSI;
|FPRC;

|PROCESO ModMobiliario;
     |C_M #rezcapea#33, 1, "S";
     |SI #rezcapea#26 = "N";
         |C_M #rezcapea#33, 1, "N";  #rezcapea#33 = "N";  |PINTA #rezcapea#33;
     |FINSI;
|FPRC;

|PROCESO ModPatrimonio;
     |C_M #rezcapea#34, 1, "S";
     |SI #rezcapea#28 = "N";
         |C_M #rezcapea#34, 1, "N";  #rezcapea#34 = "N";  |PINTA #rezcapea#34;
     |FINSI;
|FPRC;

|PROCESO CompruebaListados;
     aListados = "N";

     |DFICE aAlfa;  |QBF aAlfa;
     aAlfa = aAlfa + "listados.dir";
     |XABRE "E", aAlfa, nHandle;
     |SI FSalida < 0;
         aListados = "S";
     |FINSI;
|FPRC;

|PROGRAMA;
     |ABRE #retabla1;
     |LEE 040#retabla1.p;
     nPatrones = FSalida;
     |CIERRA #retabla1;

     |SI nPatrones ! 0;
        |DELINDEX #retabla1;
         aAlfa = "*:agiren19/zacceso9;" + "00000PATRONES               ";
         |SUB_EJECUTA aAlfa;
     |FINSI;

     |HAZ CargaColores;

     nMensaDupli = 0;

     |HAZ CompruebaListados;

     |SI enCodCli ! 0;
         |PARA nCampo = 2;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #rezcapea#nCampo#, 1, "N";
         |SIGUE;
         |C_M #rezcapea#41, 1, "N";
         |C_M #rezcapea#42, 1, "N";

         #rezcapea#2  = enCodCli;  |PINTA #rezcapea#2;
         #rezcapea#3  = enCodCli;  |PINTA #rezcapea#3;
         #rezcapea#41 = "n";
         #rezcapea#42 = "T";

         |PINPA #0#rezcapea;
         |PINDA #0#rezcapea;
         |ENDATOS #1#rezcapea;
     |SINO;
         |DDEFECTO #rezcapea;
         |PINPA #0#rezcapea;
         |PINDA #0#rezcapea;
         |ENDATOS #1#rezcapea;
     |FINSI;

     |SI nMensaDupli = 1;
         |MENAV "0000Han habido renta que no se han pasado.";
         |MENAV "0000Porque los contribuyente ya existian y los NIF eran diferentes";
     |FINSI;
|FPRO;

|PROCESO Tipo80;  |TIPO 80;
     FSalida = 3699;

     |SI enCodCli ! 0;
         FSalida = 3499;
     |FINSI;
|FPRC;
