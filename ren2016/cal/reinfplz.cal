|FICHEROS;
     reinfplz #0;

     reacceso #1;
     rem00100 #2;
     rem00101 #3;
     rem01001 #4;

     :/integral/def/dsam0054 #54;

     &poblaci@ #703;
|FIN;

|VARIABLES;
     nParrilla = 0;
     nLinea    = 0;

     aFichero     = "";

     aAlfa      = "";
     aAlfa1     = "";
     aAlfa2     = "";
     aOrigen    = "";
     aDestino   = "";
     aWord      = "";
     aAbre      = "";
     aAsigna    = "";
     aIdenti    = "";
     aPath01    = "";
     aIPRemoto  = "";
     aDef       = "";
     aDModelo   = "";
     aHModelo   = "";
     aCaracter  = "";
     aCadena    = "";
     aLen       = "";
     aTipo      = "";
     aDescri    = "";
     aPathDoc   = "";

     nSwDialogo = 0;
     nWord      = 0;
     nContador  = 0;
     nCampo     = 0;
     nCampoDes  = 0;
     nCampoDes2 = 0;
     nHandle    = 0;
     nNume      = 0;
     nLen       = 0;
     nPos       = 0;
     nImporte   = 0;

     &enSwDeDonde     = 0;
     &enCodProv       = 0;
     &enCodPobl       = 0;
     &eaRutaDocumento = "";
     &enFSalida       = 0;
     &enCodUbica      = 0;
     &eaNomUbicacion  = "";
     &eaImporte       = "";
     &eaUnidadBasico  = "";
|FIN;

|PROCESO Seleccion;
   |SI #0#1 = "S";
      |C_M #0#2,1,"S";
      |C_M #0#3,1,"S";
      |PARA nParrilla = 4; |SI nParrilla [ 24; |HACIENDO nParrilla = nParrilla + 1;
         |C_M #0nParrilla, 1, "N";
      |SIGUE;
   |SINO;
      |C_M #0#2,1,"N";
      |C_M #0#3,1,"N";
      |PARA nParrilla = 4; |SI nParrilla [ 24; |HACIENDO nParrilla = nParrilla + 1;
         |C_M #0nParrilla, 1, "S";
      |SIGUE;
   |FINSI;
|FPRC;

|PROCESO Documental;  |TIPO 7;
     |C_M #0#30, 1, "S";
     |SI enSwDeDonde = 0;
         |C_M #0#30, 1, "N";  #0#30 = "N";  |PINTA #0#30;
     |FINSI;
|FPRC;

|PROCESO Ubicacion;  |TIPO 0;
     |C_M #0#31, 1, "S";
     |SI #0#30 = "N";
         |C_M #0#31, 1, "N";  #0#31 = 0;    |PINTA #0#31;
                              #0#32 = "";   |PINTA #0#32;
     |FINSI;
|FPRC;

|PROCESO BuscaUbicacion;
     |SI #0#30 = "N";  |ACABA;  |FINSI;

     enCodUbica = #0#31;
     |HAZ LeeUbicaciones;
     |SI enFSalida ! 0;  |ERROR;  |ACABA;  |FINSI;

     #0#31 = enCodUbica;      |PINTA #0#31;
     #0#32 = eaNomUbicacion;  |PINTA #0#32;
|FPRC;

|PROCESO Asigna;
     aCadena = "";
     aLen    = aAsigna % 0;
     nLen    = aLen;
     |PARA nCampo = 1;  |SI nCampo [ nLen;  |HACIENDO nCampo = nCampo + 1;
           nPos = (100 * nCampo) + 1;
           aCaracter = aAsigna % nPos;
           |SI aCaracter = "¥";  aCaracter = &209; |FINSI;
           |SI aCaracter = "¤";  aCaracter = &241; |FINSI;
           |SI aCaracter = " ";  aCaracter = &225; |FINSI;
           |SI aCaracter = "‚";  aCaracter = &233; |FINSI;
           |SI aCaracter = "¡";  aCaracter = &237; |FINSI;
           |SI aCaracter = "¢";  aCaracter = &243; |FINSI;
           |SI aCaracter = "£";  aCaracter = &250; |FINSI;
           |SI aCaracter = "§";  aCaracter = &176; |FINSI;
           |SI aCaracter = "¦";  aCaracter = &170; |FINSI;
           aCadena = aCadena + aCaracter;
     |SIGUE;
     aAsigna = aCadena;

     |PARA;  |SI;  |HACIENDO;
          |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;
          |SI FSalida ! 0;  |SAL;  |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Mes;
   |SI aAlfa1 = "01";  aAlfa = "Enero";      |FINSI;
   |SI aAlfa1 = "02";  aAlfa = "Febrero";    |FINSI;
   |SI aAlfa1 = "03";  aAlfa = "Marzo";      |FINSI;
   |SI aAlfa1 = "04";  aAlfa = "Abril";      |FINSI;
   |SI aAlfa1 = "05";  aAlfa = "Mayo";       |FINSI;
   |SI aAlfa1 = "06";  aAlfa = "Junio";      |FINSI;
   |SI aAlfa1 = "07";  aAlfa = "Julio";      |FINSI;
   |SI aAlfa1 = "08";  aAlfa = "Agosto";     |FINSI;
   |SI aAlfa1 = "09";  aAlfa = "Septiembre"; |FINSI;
   |SI aAlfa1 = "10"; aAlfa = "Octubre";     |FINSI;
   |SI aAlfa1 = "11"; aAlfa = "Noviembre";   |FINSI;
   |SI aAlfa1 = "12"; aAlfa = "Diciembre";   |FINSI;
|FPRC;

|PROCESO AbreWord;
     aAlfa = eaUnidadBasico + "/MODELOS";                   |RMKDIR aAlfa;
     aAlfa = eaUnidadBasico + "/MODELOS/DOCUMENTOS";        |RMKDIR aAlfa;

     |SI #0#26 = "D";
         aOrigen   = aPathDoc + "word0149.doc";
         aDestino  = eaUnidadBasico + "/MODELOS/DOCUMENTOS/word0149.doc";
         aDescri   = "CARTA 2§ PAGO DOMICILIADOS RENTA 2016";
     |SINO;
         aOrigen   = aPathDoc + "word0150.doc";
         aDestino  = eaUnidadBasico + "/MODELOS/DOCUMENTOS/word0150.doc";
         aDescri   = "CARTA 2§ PAGO NO DOMICILIADOS RENTA 2016";
     |FINSI;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |SI aIPRemoto ! "";
         |ENVIA_FICHERO aOrigen, aDestino;
     |SINO;
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;

     nWord  = 1;
     aAbre  = aDestino;

     |MENSA "      Abriendo el Documento, espere un Momento.";

     |WORD_ABRE nWord, aAbre;
     |WORD_PROPIEDADES nWord, "Visible", "NO";
|FPRC;

|PROCESO GuardaDocumento;
     |ABRE #54;
     #54#0  = #1#0;
     #54#1  = 9999;
     |LEE 040#54];
     |SI FSalida = 0;
         |LEE 040#54a;
     |SINO;
         |LEE 040#54u;
     |FINSI;
     |SI FSalida = 0;  |Y #54#0 = #1#0;
         nLinea = #54#1 + 1;
     |SINO;
         nLinea = 1;
     |FINSI;

     |DDEFECTO #54;
     |HORA aAlfa;
     aAlfa1 = aAlfa % 102;
     aAlfa2 = aAlfa % 402;

     #54#0  = #1#0;
     #54#1  = nLinea;
     #54#2  = aDescri;
     #54#3  = "DOC" + (("00000" + #54#0) % -105) + (("0000" + #54#1) % -104) + ".DOC";
     #54#4  = ~;
     #54#5  = aAlfa1;
     #54#6  = aAlfa2;
     #54#7  = ~;
     #54#8  = aAlfa1;
     #54#9  = aAlfa2;
     #54#10 = #0#31;
     #54#11 = "";
     #54#12 = "";
     |GRABA 020#54n;
     |LIBERA #54;
     |CIERRA #54;

     aOrigen  = aAbre;
     aDestino = eaRutaDocumento + "/" + "DOC" + (("00000" + #1#0) % -105) + (("0000" + nLinea) % -104) + ".DOC";
     |QBT aOrigen;
     |QBT aDestino;

     |RCOPIA_FICHERO aOrigen, aDestino;
     |SI FSalida ! 0;
         |MENAV "     No se ha podido copia el Documento al Cliente.";
     |FINSI;
|FPRC;

|PROCESO CargaFamiliar;
     aAlfa     = #4#2 % 101;
     nNume     = aAlfa;
     nContador = nContador + 1;
     |SI nContador = nNume;
         aAsigna = #3#3;  |QBF aAsigna;
         aIdenti = "!#1#";
         |HAZ Asigna;
     |FINSI;
|FPRC;

|DEFBUCLE rem00101;
     #3#1;
     13;
     #1#0, 01, "S";
     #1#0, 99, "S";
     ;
     NULL, CargaFamiliar;
|FIN;

|PROCESO CargaPersonal;
     |SI #4#2 = "CONJUNTA  ";  |O #4#2 = "CONJUNTAE ";  |O #4#2 = "TITULAR   ";
         aAsigna = #2#5;
         aIdenti = "!#1#";
         |HAZ Asigna;
     |FINSI;

     |SI #4#2 = "CONYUGE   ";
         aAsigna = #2#39;
         aIdenti = "!#1#";
         |HAZ Asigna;
     |FINSI;

     nContador = 0;
     |SI #4#2 = "1 FAMILIAR";  |BUCLE rem00101; |FINSI;
     |SI #4#2 = "2 FAMILIAR";  |BUCLE rem00101; |FINSI;
     |SI #4#2 = "3 FAMILIAR";  |BUCLE rem00101; |FINSI;
     |SI #4#2 = "4 FAMILIAR";  |BUCLE rem00101; |FINSI;

     aAsigna = #2#24;  |QBF aAsigna;
     aAsigna = aAsigna + " " + #2#25;   |QBF aAsigna;
     |SI #2#26 ! "    ";
         aAsigna = aAsigna + ", " + #2#26;   |QBF aAsigna;

         |SI #2#28 ! "  ";
             aAsigna = aAsigna + " " + #2#28;   |QBF aAsigna;

             |SI #2#29 ! "  ";
                 aAsigna = aAsigna + " " + #2#29;   |QBF aAsigna;
             |FINSI;
         |FINSI;
     |FINSI;
     aIdenti = "!#2#";
     |HAZ Asigna;

     aAsigna = (("00" + #2#30) % -102) + (("000" + #2#31) % -103);
     aAsigna = aAsigna + " " + #2#32;
     aIdenti = "!#3#";
     |HAZ Asigna;

     enCodProv = #2#30;
     enCodPobl = #2#31;
     |HAZ LeePoblacion;

     aAsigna = "(" + #703#2;    |QBF aAsigna;
     aAsigna = aAsigna + ")";
     aIdenti = "!#4#";
     |HAZ Asigna;

|FPRC;

|PROCESO CargaCampos;
     |HAZ AbreWord;

     |MENSA "      Asignando Campos, espere un Momento.";

     #2#0 = #1#0;
     |LEE 040#2=;
     |SI FSalida ! 0;  |DDEFECTO #2;  |FINSI;

     |HAZ CargaPersonal;

     aAsigna = #0#29;  |QBF aAsigna;
     aAsigna = aAsigna + ",";
     aAlfa   = #0#25 % 0102;   aAsigna = aAsigna + " " + aAlfa + " de ";
     aAlfa1  = #0#25 % 0402;   |HAZ Mes;
                               aAsigna = aAsigna + aAlfa + " de ";
     aAlfa   = #0#25 % 0704;   aAsigna = aAsigna + aAlfa;

     aIdenti = "!#5#";
     |HAZ Asigna;

     aAsigna = #0#27;  |QBF aAsigna;
     aAsigna = aAsigna;
     aIdenti = "!#ATENCION#";
     |HAZ Asigna;

     aAsigna = #0#28;  |QBF aAsigna;
     aAsigna = aAsigna + ",";
     aIdenti = "!#NOMASESORIA#";
     |HAZ Asigna;

     nImporte  = #4#16 % 40;
     eaImporte = nImporte;  |HAZ Importe;
     aAsigna   = eaImporte + " Euros.";
     aIdenti   = "!#IMPORTE#";
     |HAZ Asigna;

     |MENSA "      Imprimiendo, espere un Momento.";

     |WORD_IMPRIME nWord, "";
     |WORD_PROPIEDADES nWord, "Visible", "NO";

     |SLEEP 10;

     |MENSA "      Cerrando Documento";

     |WORD_SALVA nWord, "";

     |SI #0#31 = "S";  |HAZ GuardaDocumento;  |FINSI;

     |RFBORRA aAbre;
|FPRC;

|DEFBUCLE rem01001;
     #4#1;
     5, 14, 8, 10;
     #1#0, 00, "          ", "100", "N", "S", aTipo;
     #1#0, 99, "zzzzzzzzzz", "101", "N", "S", aTipo;
     ;
     NULL, CargaCampos;
|FIN;

|PROCESO Emision;
     aDModelo = "100";
     aHModelo = "101";
     |SI #1#14 ! "IMPRESO   ";  |ACABA;  |FINSI;

     |BUCLE rem01001;
|FPRC

|DEFBUCLE reacceso;
     #1#1;
     ;
     #0#2;
     #0#3;
     ;
     NULL, Emision;
|FIN;

|PROCESO Tipo3;  |TIPO 3;
     aTipo = "N";
     |SI #0#26 = "D";  aTipo = "S";  |FINSI;

     |ABRE #2;
     |SI #0#1 = "S";
         |BUCLE reacceso;
     |SINO;
         |PARA nParrilla = 4; |SI nParrilla [ 24; |HACIENDO nParrilla = nParrilla + 1;
               |SI #0nParrilla ! 0;
                   #0#2 = #0nParrilla;
                   #0#3 = #0nParrilla;
                   |BUCLE reacceso;
               |FINSI;
         |SIGUE;
     |FINSI;
     |CIERRA #2;
|FPRC;

|PROGRAMA;
     |HAZ PreparaInformes;
     |HAZ LeeUnidadBasico;

     |CLS;
     |CABEZA "Emision Carta Segundo Plazo";

     |DBASS aPathDoc; |QBF aPathDoc;
     aPathDoc = aPathDoc + "basica/word/";

     aAbre = aPathDoc + "word0149.doc";
     |XABRE "E", aAbre, nHandle;
     |SI FSalida < 0;
         |SUB_EJECUTA_NP ":basica/wordm000;CREALOS";
     |FINSI;

     aAbre = aPathDoc + "word0149.doc";
     |XABRE "E", aAbre, nHandle;
     |SI FSalida < 0;
         |MENAV "     Instale Ficheros patrones (Documentos en Word)";
         |ACABA;
     |FINSI;

     |ABRET #0;
     |LEE 101#0p;
     |SI FSalida ! 0;
         |DDEFECTO #0;
         |GRABA 020#0b;
     |FINSI;

     |PDEFECTO #0, 1, 25;

     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |GRABA 020#0a;
     |LIBERA #0;
|FPRO;

|PROCESO Tipo80;  |TIPO 80;
     FSalida = 2499;
|FPRC;
