|FICHEROS;
     reinfcmp #0;
     :/ren1999/def/rem00100 #1;   || Datos contribuyentes

     referen@ #2;                  || Referencia para lectura de datos de la renta del a¤o 99

     &poblaci@ #703;
|FIN;

|VARIABLES;
     nParrilla = 0;

     aFichero     = "";
     nCabecera    = 0;
     nSubCabecera = 0;

     aAlfa      = "";
     aAlfa1     = "";
     aOrigen    = "";
     aDestino   = "";
     aWord      = "";
     aAbre1     = "";
     aAbre2     = "";
     aAsigna    = "";
     aIdenti    = "";
     aPath99    = "";
     aPath00    = "";
     aPathDef99 = "";
     aPathFic99 = "";
     aPathDoc00 = "";
     aIPRemoto  = "";
     aDef       = "";

     nSwDialogo = 0;
     nWord      = 0;
     nWordAnt   = 0;
     nContador  = 0;
     nCampo     = 0;
     nCampoDes  = 0;
     nCampoDes2 = 0;

     &enCodProv = 0;
     &enCodPobl = 0;
|FIN;

|PROCESO Seleccion;
   |SI #0#0 = "S";
      |C_M #0#1,1,"S";
      |C_M #0#2,1,"S";
      |PARA nParrilla = 3; |SI nParrilla [ 23; |HACIENDO nParrilla = nParrilla + 1;
         |C_M #0nParrilla, 1, "N";
      |SIGUE;
   |SINO;
      |C_M #0#1,1,"N";
      |C_M #0#2,1,"N";
      |PARA nParrilla = 3; |SI nParrilla [ 23; |HACIENDO nParrilla = nParrilla + 1;
         |C_M #0nParrilla, 1, "S";
      |SIGUE;
   |FINSI;
|FPRC;

|PROCESO AbreWord2;
     aAlfa = "C:/MODELOS";                   |RMKDIR aAlfa;
     aAlfa = "C:/MODELOS/DOCUMENTOS";        |RMKDIR aAlfa;

     aOrigen   = aPathDoc00 + "word0008.doc";
     aDestino  = "C:/MODELOS/DOCUMENTOS/word0008.doc";

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |SI aIPRemoto ! "";
         |ENVIA_FICHERO aOrigen, aDestino;
     |SINO;
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;

     nWord  = 2;
     aAbre2 = aDestino;

     |WORD_ABRE nWord, aAbre2;
     |WORD_PROPIEDADES nWord, "Visible", "NO";
|FPRC;

|PROCESO AbreOtro;
     |WORD_SALVA nWord, "";

     nWord = nWordAnt;

     |WORD_CARGA_TEXTO nWord, "!<<PEGAR>>", aDestino;

     |RFBORRA aAbre2;

     |HAZ AbreWord2;
|FPRC;

|PROCESO Asigna;
     nContador = nContador + 1;
     aIdenti   = "!#" + nContador + "#";
     |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;
     |SI FSalida ! 0;
         aAlfa1 = aAsigna;
         |HAZ AbreOtro;

         aAsigna   = aAlfa1;
         nContador = 1;
         aIdenti   = "!#" + nContador + "#";
         |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;
     |FINSI;
|FPRC;

|PROCESO MiraSiDatos;
     |SI aFichero = "rem00020.mas";  nCampoDes = 5;                     |FINSI;
     |SI aFichero = "rem00021.mas";  nCampoDes = 13;                    |FINSI;
     |SI aFichero = "rem00022.mas";  nCampoDes = 17;  nCampoDes2 = 18;  |FINSI;
     |SI aFichero = "rem00023.mas";  nCampoDes = 17;  nCampoDes2 = 18;  |FINSI;
     |SI aFichero = "rem00024.mas";  nCampoDes = 15;                    |FINSI;
     |SI aFichero = "rem00051.mas";  nCampoDes = 17;                    |FINSI;
     |SI aFichero = "rem00052.mas";  nCampoDes = 16;                    |FINSI;
     |SI aFichero = "rem00055.mas";  nCampoDes = 16;                    |FINSI;
     |SI aFichero = "rem00025.mas";  nCampoDes = 22;                    |FINSI;
     |SI aFichero = "rem00026.mas";  nCampoDes = 22;                    |FINSI;
     |SI aFichero = "rem00027.mas";  nCampoDes = 20;                    |FINSI;
     |SI aFichero = "rem00028.mas";  nCampoDes = 21;                    |FINSI;

     |SI nCabecera = 0;
         |SI aFichero = "rem00020.mas";
             aAsigna   = " ";
             |HAZ Asigna;

             aAsigna   = "RENDIMIENTOS DEL TRABAJO";
             |HAZ Asigna;

             aAsigna   = " ";
             |HAZ Asigna;

             aAsigna   = "- Certificado de rendimientos y retenciones";
             |HAZ Asigna;

             aAsigna   = " ";
             |HAZ Asigna;

             aAsigna   = "En el ejercicio 1999 se declararon los siguientes rendimientos ";
             |HAZ Asigna;

             aAsigna   = " ";
             |HAZ Asigna;
         |FINSI;

         |SI aFichero = "rem00021.mas";
             aAsigna   = " ";
             |HAZ Asigna;

             aAsigna   = "RENDIMIENTOS DEL CAPITAL INMOBILIARIO";
             |HAZ Asigna;

             aAsigna   = " ";
             |HAZ Asigna;

             aAsigna   = "- Recibo del Impuesto sobre Bienes Inmuebles (Ant.Contrib.):";
             |HAZ Asigna;

             aAsigna   = " ";
             |HAZ Asigna;

             aAsigna   = "En el ejercicio 1999 se declararon los siguientes rendimientos ";
             |HAZ Asigna;

             aAsigna   = " ";
             |HAZ Asigna;
         |FINSI;

         |SI aFichero = "rem00022.mas"; |O aFichero = "rem00023.mas";
          |O aFichero = "rem00024.mas"; |O aFichero = "rem00051.mas";
          |O aFichero = "rem00052.mas"; |O aFichero = "rem00055.mas";
             aAsigna   = " ";
             |HAZ Asigna;

             aAsigna   = "RENDIMIENTOS DEL CAPITAL MOBILIARIO";
             |HAZ Asigna;

             aAsigna   = " ";
             |HAZ Asigna;

             aAsigna   = "En el ejercicio 1999 se declararon los siguientes rendimientos ";
             |HAZ Asigna;

             aAsigna   = " ";
             |HAZ Asigna;
         |FINSI;

         |SI aFichero = "rem00025.mas"; |O aFichero = "rem00026.mas";
          |O aFichero = "rem00027.mas"; |O aFichero = "rem00028.mas";
             aAsigna   = " ";
             |HAZ Asigna;

             aAsigna   = "RENDIMIENTOS POR ACTIVIDADES EMPRESARIALES";
             |HAZ Asigna;

             aAsigna   = " ";
             |HAZ Asigna;

             aAsigna   = "En el ejercicio 1999 se declararon los siguientes rendimientos ";
             |HAZ Asigna;

             aAsigna   = " ";
             |HAZ Asigna;
         |FINSI;
     |FINSI;

     |SI nSubCabecera = 0;
         aAsigna = "";
         |SI aFichero = "rem00022.mas"; aAsigna = "- Participaciones en Fondos Propios";      |FINSI;
         |SI aFichero = "rem00023.mas"; aAsigna = "- Cesion a terceros y otros rendimientos"; |FINSI;
         |SI aFichero = "rem00024.mas"; aAsigna = "- Contrato de Seguros, Capital diferido";  |FINSI;
         |SI aFichero = "rem00051.mas"; aAsigna = "- Rentas vitalicias y temporales";         |FINSI;
         |SI aFichero = "rem00052.mas"; aAsigna = "- Deuda publica";                          |FINSI;
         |SI aFichero = "rem00055.mas"; aAsigna = "- Fondos de inversion colectiva";          |FINSI;
         |SI aFichero = "rem00025.mas"; aAsigna = "- Estimacion Directa (Normal)";            |FINSI;
         |SI aFichero = "rem00026.mas"; aAsigna = "- Estimacion Directa (Simplificada)";      |FINSI;
         |SI aFichero = "rem00027.mas"; aAsigna = "- Estimacion Objetiva (Modulos)";          |FINSI;
         |SI aFichero = "rem00028.mas"; aAsigna = "- Estimacion Objetiva (Agricolas)";        |FINSI;
         |SI aAsigna ! "";
             |SI nCabecera ! 0;
                 aAlfa     = aAsigna;
                 aAsigna   = "";
                 |HAZ Asigna;
                 aAsigna   = aAlfa;
             |FINSI;

             |HAZ Asigna;

             aAsigna   = " ";
             |HAZ Asigna;
         |FINSI;

         nSubCabecera = 1;
     |FINSI;

     nCabecera = 1;

     |SI nCampoDes2 ! 0;
         aAsigna   = #2nCampoDes + " " + #2nCampoDes2;
     |SINO;
         aAsigna   = #2nCampoDes;
     |FINSI;

     |HAZ Asigna;
|FPRC;

|DEFBUCLE MiraSiDatos;
     #2#1002;
     ;
     #rem00100#0,01;
     #rem00100#0,99;
     ;
     NULL, MiraSiDatos;
|FIN;

|PROCESO BuscaDatos;
     aDef = aPathDef99 + aFichero;
     |CARGA_DEF aDef, referen@;
     |SI FSalida ! 0;
         aAlfa = "     Error en cargar el def " + aFichero;
         |MENAV aAlfa;
         |ACABA;
     |FINSI;

     |PATH_DAT #2 aPathFic99;

     nCampoDes2   = 0;
     nSubCabecera = 0;
     |ABRE #2;
     |BUCLE MiraSiDatos;
     |CIERRA #2;

     |DESCARGA_DEF #referen@;
|FPRC;

|PROCESO Mes;
   |SI aAlfa1 = 1;  aAlfa = "Enero";      |FINSI;
   |SI aAlfa1 = 2;  aAlfa = "Febrero";    |FINSI;
   |SI aAlfa1 = 3;  aAlfa = "Marzo";      |FINSI;
   |SI aAlfa1 = 4;  aAlfa = "Abril";      |FINSI;
   |SI aAlfa1 = 5;  aAlfa = "Mayo";       |FINSI;
   |SI aAlfa1 = 6;  aAlfa = "Junio";      |FINSI;
   |SI aAlfa1 = 7;  aAlfa = "Julio";      |FINSI;
   |SI aAlfa1 = 8;  aAlfa = "Agosto";     |FINSI;
   |SI aAlfa1 = 9;  aAlfa = "Septiembre"; |FINSI;
   |SI aAlfa1 = 10; aAlfa = "Octubre";    |FINSI;
   |SI aAlfa1 = 11; aAlfa = "Noviembre";  |FINSI;
   |SI aAlfa1 = 12; aAlfa = "Diciembre";  |FINSI;
|FPRC;

|PROCESO Anexo;
     nContador = 0;
     |HAZ AbreWord2;

     aAsigna = "IDENTIFICACION";
     aIdenti = "!#1#";
     |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;

     aAsigna = " ";
     aIdenti = "!#2#";
     |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;

     aAsigna = "- Etiquetas de Hacienda o Fotocopia del D.N.I.";
     aIdenti = "!#3#";
     |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;

     aAsigna = " ";
     aIdenti = "!#4#";
     |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;
     nContador = 4;

     aFichero = "rem00020.mas";  nCabecera = 0;  |HAZ BuscaDatos;
     aFichero = "rem00021.mas";  nCabecera = 0;  |HAZ BuscaDatos;
     aFichero = "rem00022.mas";  nCabecera = 0;  |HAZ BuscaDatos;
     aFichero = "rem00023.mas";                  |HAZ BuscaDatos;
     aFichero = "rem00051.mas";                  |HAZ BuscaDatos;
     aFichero = "rem00052.mas";                  |HAZ BuscaDatos;
     aFichero = "rem00055.mas";                  |HAZ BuscaDatos;
     aFichero = "rem00025.mas";  nCabecera = 0;  |HAZ BuscaDatos;
     aFichero = "rem00026.mas";                  |HAZ BuscaDatos;
     aFichero = "rem00027.mas";                  |HAZ BuscaDatos;
     aFichero = "rem00028.mas";                  |HAZ BuscaDatos;

     |PARA nCampo = nContador + 1;  |SI nCampo [ 50;  |HACIENDO nCampo = nCampo + 1;
           aAsigna = " ";
           aIdenti = "!#" + nCampo + "#";
           |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;
     |SIGUE;

     aAsigna = " ";
     aIdenti = "!<<PEGAR>>";
     |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;

     |WORD_SALVA nWord, "";

     nWord = nWordAnt;
     |WORD_CARGA_TEXTO nWord, "!<<PEGAR>>", aDestino;

     |RFBORRA aAbre2;
|FPRC;

|PROCESO AbreWord1;
     aAlfa = "C:/MODELOS";                   |RMKDIR aAlfa;
     aAlfa = "C:/MODELOS/DOCUMENTOS";        |RMKDIR aAlfa;

     aOrigen   = aPathDoc00 + "word0007.doc";
     aDestino  = "C:/MODELOS/DOCUMENTOS/word0007.doc";

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |SI aIPRemoto ! "";
         |ENVIA_FICHERO aOrigen, aDestino;
     |SINO;
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;

     nWord  = 1;
     aAbre1 = aDestino;

     |MENSA "      Abriendo el Documento, espere un Momento.";

     |WORD_ABRE nWord, aAbre1;
     |WORD_PROPIEDADES nWord, "Visible", "NO";

     nWordAnt = nWord;
|FPRC;

|PROCESO Emision;
     |HAZ AbreWord1;

     |MENSA "      Asignando Campos, espere un Momento.";

     aAsigna = #1#5;
     aIdenti = "!#1#";
     |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;

     aAsigna = #1#24;  |QBF aAsigna;
     aAsigna = aAsigna + " " + #1#25;   |QBF aAsigna;
     |SI #1#26 ! "    ";
         aAsigna = aAsigna + ", " + #1#26;   |QBF aAsigna;

         |SI #1#28 ! "  ";
             aAsigna = aAsigna + " " + #1#28;   |QBF aAsigna;

             |SI #1#29 ! "  ";
                 aAsigna = aAsigna + " " + #1#29;   |QBF aAsigna;
             |FINSI;
         |FINSI;
     |FINSI;
     aIdenti = "!#2#";
     |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;

     aAsigna = (("00" + #1#30) % -102) + (("000" + #1#31) % -103);
     aAsigna = aAsigna + " " + #1#32;
     aIdenti = "!#3#";
     |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;


     enCodProv = #1#30;
     enCodPobl = #1#31;
     |HAZ LeePoblacion;

     aAsigna = "(" + #703#2;    |QBF aAsigna;
     aAsigna = aAsigna + ")";
     aIdenti = "!#4#";
     |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;

     aAlfa   = #0#24 % 0102;   aAsigna = aAlfa + " de ";
     aAlfa1  = #0#24 % 0402;   |HAZ Mes;
                               aAsigna = aAsigna + aAlfa + " de ";
     aAlfa   = #0#24 % 0704;   aAsigna = aAsigna + aAlfa;

     aIdenti = "!#5#";
     |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;

     |HAZ Anexo;

     |MENSA "      Imprimiendo, espere un Momento.";

     nWord = nWordAnt;

     |WORD_PROPIEDADES nWord, "limpiafinal", "";
     |WORD_PROPIEDADES nWord, "Paginacion", "1";

     nWord = nWordAnt;
     |SI nSwDialogo = 0;  |WORD_IMPRIME nWord, "Dialogo";  |FINSI;
     |SI nSwDialogo = 1;  |WORD_IMPRIME nWord, "";         |FINSI;
     |WORD_PROPIEDADES nWord, "Visible", "NO";

     nSwDialogo = 1;

     |SLEEP 10;

     |MENSA "      Cerrando Documento";

     |WORD_SALVA nWord, "";

     |RFBORRA aAbre1;
|FPRC

|DEFBUCLE Emision;
     #rem00100#1;
     ;
     #0#1;
     #0#2;
     ;
     NULL, Emision;
|FIN;

|PROGRAMA;
     |SUB_EJECUTA_NP ":basica/wordm000;CREALOS";

     |DBASS aPath99; |QBF aPath99;
     aPathDef99 = aPath99 + "ren1999/def/";
     aPathFic99 = aPath99 + "ren1999/fich/";

     |DBASS aPath00; |QBF aPath00;
     aPathDoc00 = aPath00 + "basica/word/";

     |PATH_DAT #1 aPathFic99;

     |CLS;
     |CABEZA "Emision informe comprobacion";
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI #0#0 = "S";
         |BUCLE Emision;
     |SINO;
         |PARA nParrilla = 3; |SI nParrilla [ 23; |HACIENDO nParrilla = nParrilla + 1;
               #0#1 = #0nParrilla;
               #0#2 = #0nParrilla;
               |BUCLE Emision;
         |SIGUE;
     |FINSI;
|FPRO;
