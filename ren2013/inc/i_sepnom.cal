|VARIABLES;
    &Viene   = 0;
    &Cadena  = "";
    &Cadena1 = "";
    &Cadena2 = "";
    &Cadena3 = "";

    Blanco1  = 0;
    Blanco2  = 0;
    Coma     = 0;
    Xxx      = 0;
    Posi     = 0;
    Caracter = "";
    Empresa  = 0;
    Contador = 0;
    LongAlfa = "";
    LongNume = 0;
    ParaQuitar = "";
|FIN;

|PROCESO SeparaNombre;
|| Busca una coma en la cadena

|QBF Cadena;

Cadena1 = "";
Cadena2 = "";
Cadena3 = "";

Blanco1 = 0;
Blanco2 = 0;
Coma    = 0;

|PARA Xxx = 1; |SI Xxx [ 40; |HACIENDO Xxx = Xxx + 1;
      Posi = Xxx * 100 + 1;
      Caracter = Cadena % Posi;

      |SI Caracter = ","; Coma = Xxx; |SAL; |FINSI;
|SIGUE;

|SI Coma = 0;
    || Si no hay coma busca el primer blanco empezando por detras

    LongAlfa = Cadena % 0;
    LongNume = LongAlfa;

    |PARA Xxx = LongNume; |SI Xxx ] 1; |HACIENDO Xxx = Xxx - 1;
          Posi = Xxx * 100 + 1;
          Caracter = Cadena % Posi;
          |SI Caracter = " "; Coma = Xxx; |SAL; |FINSI;
    |SIGUE;
|FINSI;

|| Coge los dos primeros caracteres del segundo apellido

Blanco1  = Coma - 1;
Posi     = Blanco1 + 100;
Cadena2  = Cadena % Posi;
LongAlfa = Cadena2 % 0;
LongNume = LongAlfa;

|PARA Xxx = LongNume; |SI Xxx ] 1; |HACIENDO Xxx = Xxx - 1;
      Posi = Xxx * 100 + 1;
      Caracter = Cadena2 % Posi;
      |SI Caracter = " "; Blanco1 = Xxx; |SAL; |FINSI;
|SIGUE;

Blanco1 = Blanco1 + 1;
Posi    = Blanco1 * 100 + LongNume;
Cadena2 = Cadena2 % Posi;

|| Coge las dos primeros caracteres del primer apellido

Blanco1 = Blanco1 - 1;
Posi    = 100 + Blanco1;
Cadena1 = Cadena % Posi;

|| Recoge los dos primero caracteres de Nombre

Posi    = ((Coma + 1) * 100) + 40;
Cadena3 = Cadena % Posi;

ParaQuitar = Cadena1; |HAZ QuitaBlanco; Cadena1 = ParaQuitar;
ParaQuitar = Cadena2; |HAZ QuitaBlanco; Cadena2 = ParaQuitar;
ParaQuitar = Cadena3; |HAZ QuitaBlanco; Cadena3 = ParaQuitar;

|QBF Cadena1;
|QBF Cadena2;
|QBF Cadena3;
|FPRC;

|PROCESO QuitaBlanco;
|ET Cadena;
    |QBF ParaQuitar;
    |SI ParaQuitar ! "";
        Caracter = ParaQuitar % 101;
        |SI Caracter = " ";
            ParaQuitar = ParaQuitar % 240;
            |VETE Cadena;
        |FINSI;
    |FINSI;
|FPRC;
