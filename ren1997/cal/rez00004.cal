|FICHEROS;
   rez00004 #0;
   reacceso #1;
   reauxval #4;
   redatcon #6;
   reempres #7;
   repartid #8;
   rerenpre #9;
   retransp #10;
   revalono #11;
   revalore #12;
|FIN;

|VARIABLES;
 swnif = 0;
 nume1 = 0;
 nume2 = 0;
 lugar = 0;
 nifnum = 0;
 resto = 0;
 xxx   = 0;
    x = "";
    letrasnif = "TRWAGMYFPDXBNJZSQVHLCKE";
    letra = "";
    alfa1 = "";
    alfa2 = "";
    final = "";
    trabajo = "";
    pos1 = "";
    pos2 = "";
    pos3 = "";
    pos4 = "";
    conta = 0;
    anyo = "";
    c    = 0;
    Cadena   = "";
    Caracter = "";
    Posi     = 0;
    XPara    = 0;
    Nume     = 0;
|FIN;

|PROCESO letranif;
  alfa1 = trabajo;  |QBF alfa1;
  x     = alfa1 % -101;
  Nume  = &x;
  |SI Nume < 65;  |O Nume > 90;  |ACABA;  |FINSI;

  x     = trabajo % 101;  alfa1 = trabajo;  |QBF alfa1;

  alfa1 = alfa1 % 0;
  nume1 = alfa1;
  |SI nume1 < 6;  |ACABA;  |FINSI;

  |PARA XPara = 1; |SI XPara [ 12; |HACIENDO XPara = XPara + 1;
        Posi  = (100 * XPara) + 1;
        x     = trabajo % Posi;
        |SI x ! " ";  |SAL;  |FINSI;
  |SIGUE;

  |SI x ! "A";|Y x ! "B"; |Y x ! "C"; |Y x ! "D"; |Y x ! "E";|Y x ! "F";
   |Y x ! "G";|Y x ! "P"; |Y x ! "Q"; |Y x ! "S"; |Y x ! "X";
      swnif = 0;
      alfa1 = trabajo;   |QBF alfa1;
      final = "";
      alfa2 = alfa1 % 0;
      nume1 = alfa2;
      conta = 0;
      |PARA nume2 = 1;  |SI nume2 [ nume1;  |HACIENDO nume2 = nume2 + 1;
            lugar = (nume2 * 100) + 1;
            alfa2 = alfa1 % lugar;
            |SI alfa2 = "0"; |O alfa2 = "1"; |O alfa2 = "2"; |O alfa2 = "3";
             |O alfa2 = "4"; |O alfa2 = "5"; |O alfa2 = "6"; |O alfa2 = "7";
             |O alfa2 = "8"; |O alfa2 = "9";
                final = final + alfa2;
            |SINO;
                |SI nume2 ! nume1; |Y alfa2 ! "."; |Y alfa2 ! "-"; |Y alfa2 ! " ";
                    conta = conta + 1;
                |FINSI;
            |FINSI;
      |SIGUE;
      |SI conta ! 0; swnif = 1; |ACABA; |FINSI;

      nifnum = final;
      alfa2  = final % 0;
      Nume   = alfa2;
      |SI Nume > 8;
          trabajo = final;
          |ACABA;
      |FINSI;

      |SI nifnum ! 0; |Y final ! "";
          resto   = nifnum $ 23;
          resto   = resto + 1;
          lugar   = (resto * 100) + 1;
          letra   = letrasnif % lugar;
          trabajo = ("000000000" + final + letra) % -109;
      |SINO;
          swnif = 1;                 || no es bueno y no graba
      |FINSI;
  |SINO;
      Cadena = "";
      |PARA XPara = 1; |SI XPara [ 12; |HACIENDO XPara = XPara + 1;
            Posi = (100 * XPara) + 1;
            Caracter = trabajo % Posi;
            |SI Caracter ! "."; |Y Caracter ! "-"; |Y Caracter ! " ";
                Cadena = Cadena + Caracter;
            |FINSI;
      |SIGUE;
      trabajo = Cadena;
      swnif = 0;
  |FINSI;
  |QBF trabajo;
|FPRC;

|PROCESO el01;
trabajo = #1#2; |HAZ letranif; |SI swnif ! 0; |ACABA; |FINSI;
#1#2 = trabajo; |GRABA 140#1a; |LIBERA #1;
|FPRC;

|PROCESO el04;
trabajo = #4#2; |HAZ letranif; |SI swnif ! 0; |ACABA; |FINSI;
#4#2 = trabajo; |GRABA 140#4a; |LIBERA #4;
|FPRC;

|PROCESO el06;
trabajo = #6#2;  |HAZ letranif; |SI swnif = 0; #6#2  = trabajo; |FINSI;
trabajo = #6#29; |HAZ letranif; |SI swnif = 0; #6#29 = trabajo; |FINSI;
trabajo = #6#46; |HAZ letranif; |SI swnif = 0; #6#46 = trabajo; |FINSI;
trabajo = #6#48; |HAZ letranif; |SI swnif = 0; #6#48 = trabajo; |FINSI;
trabajo = #6#49; |HAZ letranif; |SI swnif = 0; #6#49 = trabajo; |FINSI;
trabajo = #6#50; |HAZ letranif; |SI swnif = 0; #6#50 = trabajo; |FINSI;
trabajo = #6#51; |HAZ letranif; |SI swnif = 0; #6#51 = trabajo; |FINSI;
trabajo = #6#52; |HAZ letranif; |SI swnif = 0; #6#52 = trabajo; |FINSI;
trabajo = #6#53; |HAZ letranif; |SI swnif = 0; #6#53 = trabajo; |FINSI;
trabajo = #6#54; |HAZ letranif; |SI swnif = 0; #6#54 = trabajo; |FINSI;
trabajo = #6#55; |HAZ letranif; |SI swnif = 0; #6#55 = trabajo; |FINSI;
trabajo = #6#56; |HAZ letranif; |SI swnif = 0; #6#56 = trabajo; |FINSI;
trabajo = #6#57; |HAZ letranif; |SI swnif = 0; #6#57 = trabajo; |FINSI;

|GRABA 140#6a; |LIBERA #6;
|FPRC;

|PROCESO el07;
trabajo = #7#7; |HAZ letranif; |SI swnif ! 0; |ACABA; |FINSI;
#7#7 = trabajo; |GRABA 140#7a; |LIBERA #7;
|FPRC;

|PROCESO el08;
trabajo = #8#2; |HAZ letranif; |SI swnif ! 0; |ACABA; |FINSI;
#8#2 = trabajo; |GRABA 140#8a; |LIBERA #8;
|FPRC;

|PROCESO el09;
trabajo = #9#2; |HAZ letranif; |SI swnif ! 0; |ACABA; |FINSI;
#9#2 = trabajo; |GRABA 140#9a; |LIBERA #9;
|FPRC;

|PROCESO el10;
trabajo = #10#14; |HAZ letranif; |SI swnif ! 0; |ACABA; |FINSI;
#10#14 = trabajo; |GRABA 140#10a; |LIBERA #10;
|FPRC;

|PROCESO el11;
trabajo = #11#15; |HAZ letranif; |SI swnif ! 0; |ACABA; |FINSI;
#11#15 = trabajo; |GRABA 140#11a; |LIBERA #11;
|FPRC;

|PROCESO el12;
trabajo = #12#15; |HAZ letranif; |SI swnif ! 0; |ACABA; |FINSI;
#12#15 = trabajo; |GRABA 140#12a; |LIBERA #12;
|FPRC;

|DEFBUCLE reacceso;
#1#1;
;
00000;
99999;
be;
NULL, el01;
|FIN;

|DEFBUCLE reauxval;
#4#1;
;
"    ";
"zzzz";
be;
NULL, el04;
|FIN;

|DEFBUCLE redatcon;
#6#1;
;
00000;
99999;
be;
NULL, el06;
|FIN;

|DEFBUCLE reempres;
#7#1;
;
00;
99;
be;
NULL, el07;
|FIN;

|DEFBUCLE repartid;
#8#1;
;
00000;
99999;
be;
NULL, el08;
|FIN;

|DEFBUCLE rerenpre;
#9#1;
;
00000;
99999;
be;
NULL, el09;
|FIN;

|DEFBUCLE retransp;
#10#1;
;
00000, 00;
99999, 99;
be;
NULL, el10;
|FIN;

|DEFBUCLE revalono;
#11#1;
;
00000, 00;
99999, 99;
be;
NULL, el11;
|FIN;

|DEFBUCLE revalore;
#12#1;
;
00000, 00;
99999, 99;
be;
NULL, el12;
|FIN;

|PROCESO convi; |TIPO 3;
|SI #0#0 ! "SI"; |ACABA; |FINSI;

|BUCLE reacceso;
|BUCLE reauxval;
|BUCLE redatcon;
|BUCLE reempres;
|BUCLE repartid;
|BUCLE rerenpre;
|BUCLE retransp;
|BUCLE revalono;
|BUCLE revalore;
|FPRC;
