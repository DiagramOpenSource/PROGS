|FICHEROS;
    reimpor1 #00;

    reprogra #01;

    reacceso #02;
    zeacceso #03;

    redatcon #04;
    zedatcon #05;

    rerendtr #06;
    zerendtr #07;

    recapinm #08;
    zecapinm #09;

    revalore #10;
    zevalore #11;

    revalono #12;
    zevalono #13;

    rectacor #14;
    zectacor #15;
|FIN;

|VARIABLES;
    campos_1 = 29;  || nro. de campos de cada uno
    campos_2 = 74;
    campos_3 = 45;
    campos_4 = 48;
    campos_5 = 31;
    puntero = 0;    || puntero para 'reprogra'
|FIN;

|INCLUYE i_impor1;
|INCLUYE i_impor2;

|CALCULO borrar_si; |TIPO 0;
|SI #0#2 = "S";
    |CAMPO_MODIFICA #0#5, 1, "N";
|SINO;
    |CAMPO_MODIFICA #0#5, 1, "S";
|FINSI;
|FCAL;

|PROGRAMA;
|SI devuelta = 1;
        codi = codi + 1;
    |HAZ importa;
|SINO;
        devuelta = 1;
    |CLS;
    |CABEZA "Importacion";
    |PINPA #0#0;
    |PINDA #0#0;
    |ENDATOS #1#0;
    |SI FSalida = 0;
        |HAZ path;
        |SI #0#1 = "SI";|Y aqui ! path;
                has_cod = #0#3;
                borra = #0#2;
                codi = #0#0;
                marcar = #0#5;
            |HAZ importa;
        |FINSI;
        |SI path = aqui;
            |MENAV "    ATENCION!!! Esta situado en el directorio principal ...";
        |FINSI;
    |FINSI;
|FINSI;
|FPRO;

|PROCESO path;
|DBASS alfa1;
    path = alfa1 + "ren1997/fich/";
|DFICE aqui;
|QBF aqui;
|FPRC;

|PROCESO importa;
|SI codi [ has_cod;
        acceso = 0;
    |HAZ imp_a;     || acceso introduccion
    |SI acceso = 1;                || si existe en el acceso
            alfa1 = codi;  alfa1 = "00000" + alfa1;  alfa1 = alfa1 % -105;
        |ATRI P;  |PINTA 1646, alfa1;  |ATRI 0;
        |HAZ imp_b;     || datos contribuyentes
        |HAZ imp_c;     || programas en activo
        |HAZ lee_repro;
        |HAZ imp_1;     || 'rerendtr'
        |HAZ imp_2;     || 'recapinm'
        |HAZ imp_3;     || 'revalore'
        |HAZ imp_4;     || 'revalono'
        |HAZ imp_5;     || 'rectacor'
        |CORRE "reimpor2.run";
    |SINO;
        |CORRE "reimpor1.run";
    |FINSI;
|FINSI;
|FPRC;

|PROCESO imp_a;          || acceso
    |PATH_DAT #2 path;
        error = -1;
    |PARA; |SI error ! 0; |HACIENDO;
        |ABRE #2;
            error = FSalida;
    |SIGUE;
        #2#0 = codi;
    |LEE 001#2];
    |SI FSalida = 0;
            codi = #2#0;
        |SI codi [ has_cod;
            |HAZ comprueba;
            |SI acceso = 1;
                |SI borra = "S";  |BORRA 020#2a; |FINSI;
                |ABRE #3;
                |PARA nume1 = 0; |SI nume1 [ 2; |HACIENDO nume1 = nume1 + 1;
                    #3nume1 = #2nume1;
                |SIGUE;
                |GRABA 020#3n;
                |CIERRA #3;
            |FINSI;
        |FINSI;
    |SINO;
            codi = has_cod;
    |FINSI;
    |CIERRA #2;
|FPRC;

|PROCESO imp_b;          || datos contribuyente
    |PATH_DAT #4 path;
        error = -1;
    |PARA; |SI error ! 0; |HACIENDO;
        |ABRE #4;
            error = FSalida;
    |SIGUE;
        #4#0 = codi;
    |LEE 101#4=;
    |SI FSalida = 0;
        |SI borra = "S";  |BORRA 020#4a; |FINSI;
        |ABRE #5;
        |PARA nume1 = 0; |SI nume1 [ 208; |HACIENDO nume1 = nume1 + 1;
            #5nume1 = #4nume1;
        |SIGUE;
        |GRABA 020#5n;
        |CIERRA #5;
    |FINSI;
    |HAZ impresas;
    |LIBERA #4;
    |CIERRA #4;
|FPRC;

|PROCESO imp_c;               || programas en activo
    |PATH_DAT #1 path;
        error = -1;
    |PARA; |SI error ! 0; |HACIENDO;
        |ABRE #1;
            error = FSalida;
    |SIGUE;
        #1#0 = codi;
    |LEE 001#1=;
        nume1 = FSalida;
    |SI borra = "S";  |BORRA 020#1a; |FINSI;
    |CIERRA #1;               || este solo traga con esta estructura ¨...?
    |SI nume1 = 0;
        |PATH_DAT #20 aqui;
        |ABRE #20;
        |PARA nume1 = 0; |SI nume1 [ 50; |HACIENDO nume1 = nume1 + 1;
            #20nume1 = #1nume1;
        |SIGUE;
        |GRABA 020#20n;
        |CIERRA #20;
    |FINSI;
|FPRC;

|PROCESO comprueba;
    |PATH_DAT #4 path;
        error = -1;
    |PARA; |SI error ! 0; |HACIENDO;
        |ABRE #4;
            error = FSalida;
    |SIGUE;
        #4#0 = codi;
    |LEE 001#4=;
    |SI FSalida = 0;
        |SI #0#4 = "S";
            |SI #4#142 = "S";    acceso = 1;     |FINSI;    || preparada S
            |PARA nume1 = 124; |SI nume1 [ 139; |HACIENDO nume1 = nume1 + 1;
                |SI #4nume1 = "S";   acceso = 0;  |FINSI;   || si algo esta
            |SIGUE;                                         || /impreso
        |SINO;
                acceso = 1;
        |FINSI;
    |FINSI;
    |CIERRA #4;
|FPRC;

|CALCULO impresas;
|SI marcar = "S";
                              || renta impresa
    |SI #4#103 ! " ";  #4#124 = "S";    |FINSI;   || conjunta
    |SI #4#104 ! " ";  #4#125 = "S";    |FINSI;   || titular
    |SI #4#105 ! " ";  #4#126 = "S";    |FINSI;   || conyuge
    |SI #4#106 ! " ";  #4#127 = "S";    |FINSI;   || 1er. familiar
    |SI #4#107 ! " ";  #4#128 = "S";    |FINSI;   || 2do. familiar
    |SI #4#108 ! " ";  #4#129 = "S";    |FINSI;   || 3er. familiar
    |SI #4#109 ! " ";  #4#130 = "S";    |FINSI;   || 4to. familiar
                              || 2do. plazo impreso
    |SI #4#110 ! "N";  #4#131 = "S";    |FINSI;   || conjunta
    |SI #4#111 ! "N";  #4#132 = "S";    |FINSI;   || titular
    |SI #4#112 ! "N";  #4#133 = "S";    |FINSI;   || conyuge
    |SI #4#113 ! "N";  #4#134 = "S";    |FINSI;   || 1er. familiar
    |SI #4#114 ! "N";  #4#135 = "S";    |FINSI;   || 2do. familiar
    |SI #4#115 ! "N";  #4#136 = "S";    |FINSI;   || 3er. familiar
    |SI #4#116 ! "N";  #4#137 = "S";    |FINSI;   || 4to. familiar
                              || patrimonio impreso
    |SI #4#140 ! "N";  #4#138 = "S";    |FINSI;   || titular
    |SI #4#141 ! "N";  #4#139 = "S";    |FINSI;   || conyuge
    |SI #4#194 ! "N";  #4#198 = "S";    |FINSI;   || 1er. familiar
    |SI #4#195 ! "N";  #4#199 = "S";    |FINSI;   || 2do. familiar
    |SI #4#196 ! "N";  #4#200 = "S";    |FINSI;   || 3er. familiar
    |SI #4#197 ! "N";  #4#201 = "S";    |FINSI;   || 4to. familiar

    |GRABA 020#4a;
|FINSI;
|FCAL;
