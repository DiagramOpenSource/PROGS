|FICHEROS;
    reincire #0;
    reincpre #1;
    reincpen #2;
    rejerant #3;
    rededu01 #4;
    reincide #5;
    retotren #6;
    retotrea #9;
    retotreb #10;
|FIN;

|INCLUYE i_varcal;

|VARIABLES;
  diferencia = 0;
|FIN;
|| **************************************************************************
||                   PROCESO DE LECTURA DE LOS DISTINTOS FICHEROS
|| **************************************************************************

|PROCESO fich_04_0;   || Incrementos/Diminuciones Patrimoniales Irregulares
alfa1     = "INCREMENTOS/DISMINUCIONES PATRIMONIALES IRREGULARES -------------";
descri    = #0#13;
|QBF descri;
|SI #0#24 ! 0; descri = descri + " I: "; rendi = #0#24; |FINSI;
|SI #0#25 ! 0; descri = descri + " D: "; rendi = #0#25; |FINSI;
tipod     = #0#2;
tipore    = #0#4;
linea     = #0#1;
perceptor = #0#3;
numero1   = #0#7;
numero2   = #0#8;
numero3   = #0#9;
numero4   = #0#10;
numero5   = #0#11;
numero6   = #0#12;
sw_fich   = 0;
empreac   = #0#0;
periodo   = #0#27;

|SI #0#4 = "X"; |O #0#4 = "P"; |O #0#4 = "R";
    |HAZ incide_04;
|FINSI;

|SI #0#4 = "A"; |O #0#4 = "R"; |HAZ tipode_04; |FINSI;
|FPRC;

|PROCESO fich_04_1;            || Trata Premios, Sorteos,Juegos, Rifas
alfa1 = "PREMIOS, SORTEOS, JUEGOS, RIFAS -------------------------------------";
tipod     = #1#2;
tipore    = #1#9;
descri    = #1#5;
linea     = #1#1;
rendi     = #1#8;
perceptor = #1#3;
numero1   = #1#10;
numero2   = #1#11;
numero3   = #1#12;
numero4   = #1#13;
numero5   = #1#14;
numero6   = #1#15;
sw_fich   = 1;
empreac   = #1#0;
periodo   = #1#16;
|SI tippar = 2;
    |SI muerto = "C"; |Y #3#2 = "C";
        periodo = 1;
    |FINSI;
    |SI muerto = "T"; |Y #3#2 = "T";
        periodo = 1;
    |FINSI;
|FINSI;

|SI #1#9 = "X"; |O #1#9 = "P"; |O #1#9 = "R";
    |HAZ incide_04;
|FINSI;
|SI #1#9 = "A"; |O #1#9 = "R"; |HAZ tipode_04; |FINSI;
|FPRC;

|PROCESO fich_04_2;       || Trata  Pendientes por Operaciones a Plazos
alfa1 = "INCREMENTOS PENDIENTES POR OPERACIONES A PLAZOS ---------------------";
descri    = #2#13;
|QBF descri;
|SI #2#25 ! 0; descri = descri + " R: "; rendi = #2#25; |FINSI;
|SI #2#26 ! 0; descri = descri + " I: "; rendi = #2#26; |FINSI;
tipod     = #2#2;
tipore    = #2#4;
linea     = #2#1;
perceptor = #2#3;
numero1   = #2#7;
numero2   = #2#8;
numero3   = #2#9;
numero4   = #2#10;
numero5   = #2#11;
numero6   = #2#12;
sw_fich   = 2;
empreac   = #2#0;
periodo   = #2#29;

|SI #2#4 = "X"; |O #2#4 = "P"; |O #2#4 = "R";
    |HAZ incide_04;
|FINSI;
|SI #2#4 = "A"; |O #2#4 = "R"; |HAZ tipode_04; |FINSI;
|FPRC;

|PROCESO fich_04_3;            || Trata Rendimiento Ejercicio A¤os anteriores
alfa1     = "RENDIMIENTOS NETOS EJERCICIOS ANTERIORES ------------------------";
tipod     = #3#2;
perceptor = #3#3;
sw_fich   = 3;
empreac   = #3#0;
periodo   = #3#8;
|SI tippar = 2;
    |SI muerto = "C"; |Y #3#2 = "C";
        periodo = 1;
    |FINSI;
    |SI muerto = "T"; |Y #3#2 = "T";
        periodo = 1;
    |FINSI;
|FINSI;

|HAZ tipode_04;
|FPRC;

|PROCESO fich_04_4;       || Trata Deducciones Pendientes Ejercicios anteriores
alfa1 = "DEDUCCIONES PENDIENTES DE EJERCICIOS ANTERIORES ---------------------";
tipod     = #4#2;
tipore    = #4#11;
descri    = #4#6;
rendi     = #4#9;
linea     = #4#1;
perceptor = #4#3;
numero1   = #4#12;
numero2   = #4#13;
numero3   = #4#14;
numero4   = #4#15;
numero5   = #4#16;
numero6   = #4#17;
sw_fich   = 4;
empreac   = #4#0;
periodo   = #4#18;
|SI tippar = 2;
    |SI muerto = "C"; |Y #4#2 = "C";
        periodo = 1;
    |FINSI;
    |SI muerto = "T"; |Y #4#2 = "T";
        periodo = 1;
    |FINSI;
|FINSI;

|SI #4#11 = "X"; |O #4#11 = "P"; |O #4#11 = "R";
    |HAZ incide_04;
|FINSI;
  |SI #4#11 = "A"; |O #4#11 = "R";
      |HAZ tipode_04;
      |SI #4#19 = "S";
          #4#9 = #4#9 + #4#10;
          #4#10 = 0;
          |GRABA 021#4a;
      |FINSI;
  |FINSI;
|FPRC;

|| **************************************************************************
                        TRATAMIENTO DE LA LIQUIDACION
|| **************************************************************************

|PROCESO tipode_04;
  coefi11 = 1;
  funeral = 0;
  |SI tipod = "G";  |Y tippar = 1;   || Si es Ganancial  Y MATRIMONIO;
      |SI periodo = 0;
          coefi1= 50*parte1; coefi11 = .5; zz = 25; tipod="T"; |HAZ leeliq_04;
          coefi1= 50*parte1; coefi11 = .5; zz = 25; tipod="C"; |HAZ leeliq_04;
          coefi1=100*parte2; coefi11 =  1; zz=  50;  tipod="A"; |HAZ leeliq_04;
      |SINO;
          |SI periodo = 1;
              coefi1 = 50; coefi11 = .5; zz = 50; tipod  = "T"; |HAZ leeliq_04;
              coefi1 = 50; coefi11 = .5; zz = 50; tipod  = "C"; |HAZ leeliq_04;
          |SINO;
              coefi1 = 100; coefi11 = 1; zz =100; tipod  = "A"; |HAZ leeliq_04;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;

  |SI tipod = "G";  |Y tippar = 3;   || Si es Ganancial  Y SEPARACION;
      |SI periodo = 0;
          coefi1= 50*parte2; coefi11 = .5; zz = 25; tipod="T"; |HAZ leeliq_04;
          coefi1= 50*parte2; coefi11 = .5; zz = 25; tipod="C"; |HAZ leeliq_04;
          coefi1=100*parte1; coefi11 =  1; zz = 50; tipod="A"; |HAZ leeliq_04;
      |SINO;
          |SI periodo = 1;
              zz = 100;
              coefi1 = 100; coefi11 = 1; tipod  = "A"; |HAZ leeliq_04;
          |SINO;
              zz = 50;
              coefi1 =  50; coefi11 = .5;  tipod  = "T"; |HAZ leeliq_04;
              coefi1 =  50; coefi11 = .5;  tipod  = "C"; |HAZ leeliq_04;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;


  || Si es Ganancial, conjunta y SE MUERE EL;

  |SI tipod = "G";  |Y tippar = 2; |Y muerto = "T";  |Y obitus = 1;
      |SI periodo = 0;
          coefi1= 100*parte2; coefi11 = 1; zz = 50; tipod="C"; |HAZ leeliq_04;
          coefi1= 100*parte1; coefi11 = 1; zz = 50; tipod="A"; |HAZ leeliq_04;
      |SINO;
          |SI periodo = 1;
              zz = 100;
              coefi1 = 100; coefi11 = 1; tipod  = "A"; |HAZ leeliq_04;
          |SINO;
              zz = 100;
              coefi1 = 100; coefi11 = 1; tipod  = "C"; |HAZ leeliq_04;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;


  || Si es Ganancial, conjunta y SE MUERE ELLA;

  |SI tipod = "G";  |Y tippar = 2; |Y muerto = "C";  |Y obitus = 1;
      |SI periodo = 0;
          coefi1= 100*parte2; coefi11 = 1; zz = 50; tipod="T"; |HAZ leeliq_04;
          coefi1= 100*parte1; coefi11 = 1; zz = 50; tipod="A"; |HAZ leeliq_04;
      |SINO;
          |SI periodo = 1;
              zz = 100;
              coefi1 = 100; coefi11 = 1; tipod  = "A"; |HAZ leeliq_04;
          |SINO;
              zz = 100;
              coefi1 = 100; coefi11 = 1; tipod  = "T"; |HAZ leeliq_04;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;


  ||Si es Ganancial, individual Y SE MUERE EL;

  |SI tipod = "G";  |Y tippar = 2; |Y muerto = "T"; |Y obitus = 2;
      |SI periodo = 0;
          funeral = 1;
          coefi1=  50 * parte1; coefi11=.5; zz=  50; tipod = "T"; |HAZ leeliq_04;
          coefi1= 100 - coefi1; coefi11= 1; zz=  50; tipod = "C"; |HAZ leeliq_04;
      |SINO;
          |SI periodo = 1;
              coefi1  = 50;
              coefi11 = .5;
              zz = 50;
              tipod  = "T"; |HAZ leeliq_04;
              tipod  = "C"; |HAZ leeliq_04;
          |SINO;
              zz = 100; coefi11 = 1; coefi1 = 100; tipod  = "C"; |HAZ leeliq_04;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;

  ||Si es Ganancial, individual Y SE MUERE ELLA;

 |SI tipod = "G"; |Y tippar = 2; |Y muerto = "C"; |Y obitus = 2;
      |SI periodo = 0;
          funeral = 1;
          coefi1=  50 * parte1;  coefi11=.5; zz= 50; tipod = "C"; |HAZ leeliq_04;
          coefi1 = 100 - coefi1; coefi11= 1; zz= 50; tipod = "T"; |HAZ leeliq_04;
      |SINO;
          |SI periodo = 1;
              coefi11 = .5;
              coefi1 = 50;
              zz = 50;
              tipod  = "T"; |HAZ leeliq_04;
              tipod  = "C"; |HAZ leeliq_04;
          |SINO;
              zz = 100; coefi11 = 1; coefi1 = 100;  tipod  = "T"; |HAZ leeliq_04;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;

  |SI tipod ! "V";  |Y tippar = 1;      ||SI ES TITULAR O CONYUGE Y SE CASAN;
      |SI periodo = 0;
          coefi11 = 1;
          zz = 100;
          coefi1 =  100 * parte1;                |HAZ leeliq_04;
          coefi1 =  100 * parte2;  tipod = "A";  |HAZ leeliq_04;
      |SINO;
          |SI periodo = 1;
              coefi11 = 1;
              zz = 100;
              coefi1 = 100; |HAZ leeliq_04;
          |SINO;
              coefi11 = 1;
              zz = 100;
              coefi1 = 100;  tipod = "A";  |HAZ leeliq_04;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;

  |SI tipod ! "V";  |Y tippar = 3;      ||SI TITULAR O CONYUGE Y SE SEPARAN;
      |SI periodo = 0;
          coefi11 = 1;
          zz = 100;
          coefi1 =  100 * parte2;                |HAZ leeliq_04;
          coefi1 =  100 * parte1;  tipod = "A";  |HAZ leeliq_04;
      |SINO;
          |SI periodo = 1;
              coefi11 = 1;
              zz = 100;
              coefi1 = 100;  tipod = "A";  |HAZ leeliq_04;
          |SINO;
              coefi11 = 1;
              zz = 100;
              coefi1 = 100;                |HAZ leeliq_04;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;


  ||SI TITULAR O CONYUGE, conjunta Y SE MUERE UNO;
  |SI tipod ! "V";  |Y tippar = 2; |Y obitus = 1;
      |SI periodo = 0;
          coefi11 = 1;
          zz = 100;
          |SI muerto = "T";  |Y tipod = "T";
              coefi1 =  100;  tipod = "A";  |HAZ leeliq_04;
          |SINO;
           |SI muerto = "C";  |Y tipod = "C";
                  coefi1 =  100;  tipod = "A";  |HAZ leeliq_04;
              |SINO;
                  coefi1 =  100 * parte2;                |HAZ leeliq_04;
                  coefi1 =  100 * parte1;  tipod = "A";  |HAZ leeliq_04;
              |FINSI;
          |FINSI;
      |SINO;
          |SI periodo = 1;
              coefi11 = 1;
              zz = 100;
              coefi1 = 100;  tipod = "A";  |HAZ leeliq_04;
          |SINO;
              coefi11 = 1;
              zz = 100;
              coefi1 = 100;                |HAZ leeliq_04;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;

||SI TITULAR O CONYUGE, individual  Y SE MUERE UNO;

  |SI tipod ! "V"; |Y tippar = 2; |Y obitus = 2;
       coefi11 = 1;
       zz = 100;
       coefi1 = 100; |HAZ leeliq_04;
      |ACABA;
  |FINSI;

  |SI tippar = 1;          ||VARIOS Y SE CASAN;
      |SI periodo = 0;
          |SI numero1 ! 0;
              zz = numero1;
              coefi11 = numero1 / 100;
              coefi1 =  numero1 * parte1;
              tipod = "T";
              |HAZ leeliq_04;
          |FINSI;
          |SI numero2 ! 0;
              zz = numero2;
              coefi11 = numero2 / 100;
              coefi1 =  numero2 * parte1;
              tipod = "C";
              |HAZ leeliq_04;
          |FINSI;
          zz = numero1 + numero2;
          coefi1 = ( numero1 + numero2 ) * parte2;
          coefi11 = ( numero1 + numero2 ) / 100;
          tipod = "A"; |HAZ leeliq_04;
      |SINO;
          |SI periodo = 1;
             |SI numero1 ! 0;
                 zz = numero1;
                 coefi11 = numero1 / 100;
                 coefi1 = numero1;
                 tipod = "T";
                 |HAZ leeliq_04;
             |FINSI;
             |SI numero2 ! 0;
                 zz = numero2;
                 coefi11 = numero2 / 100;
                 coefi1 = numero2;
                 tipod = "C";
                 |HAZ leeliq_04;
             |FINSI;
          |SINO;
             zz = numero1 + numero2;
             coefi1 = numero1 + numero2;
             coefi11 = (numero1 + numero2) / 100;
             tipod = "A"; |HAZ leeliq_04;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;

  |SI tippar = 3;          ||VARIOS Y SE SEPARAN;
      |SI periodo = 0;
          |SI numero1 ! 0;
              zz = numero1;
              coefi11 = numero1 / 100;
              coefi1 =  numero1 * parte2;
              tipod = "T";
              |HAZ leeliq_04;
          |FINSI;
          |SI numero2 ! 0;
              zz = numero2;
              coefi11 = numero2 / 100;
              coefi1 =  numero2 * parte2;
              tipod = "C";
              |HAZ leeliq_04;
          |FINSI;
          zz = numero1 + numero2;
          coefi1 = ( numero1 + numero2 ) * parte1;
          coefi11 = ( numero1 + numero2 ) / 100;
          tipod = "A"; |HAZ leeliq_04;
      |SINO;
          |SI periodo = 1;
              zz = numero1 + numero2;
              coefi1 = numero1 + numero2;
              coefi11 = (numero1 + numero2) / 100;
              tipod = "A"; |HAZ leeliq_04;
          |SINO;
              |SI numero1 ! 0;
                  zz = numero1;
                  coefi1 = numero1;
                  coefi11 = numero1 / 100;
                  tipod = "T";
                  |HAZ leeliq_04;
              |FINSI;
              |SI numero2 ! 0;
                  zz = numero2;
                  coefi1 = numero2;
                  coefi11 = numero2 / 100;
                  tipod = "C";
                  |HAZ leeliq_04;
              |FINSI;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;

  |SI tippar = 2;  |Y obitus = 1;     ||VARIOS, conjunta Y SE MUERE;
      |SI periodo = 0;
          |SI numero1 ! 0;
              zz = numero1;
              coefi11 = numero1 / 100;
              coefi1 =  numero1 * parte2;
              tipod = "T";
              |HAZ leeliq_04;
          |FINSI;
          |SI numero2 ! 0;
              zz = numero2;
              coefi11 = numero2 / 100;
              coefi1 =  numero2 * parte2;
              tipod = "C";
              |HAZ leeliq_04;
          |FINSI;
          zz = numero1 + numero2;
          coefi1 = ( numero1 + numero2 ) * parte1;
          coefi11 = ( numero1 + numero2 ) / 100;
          tipod = "A"; |HAZ leeliq_04;
      |SINO;
          |SI periodo = 1;
              zz = numero1 + numero2;
              coefi1 = numero1 + numero2;
              coefi11 = (numero1 + numero2) / 100;
              tipod = "A"; |HAZ leeliq_04;
          |SINO;
              |SI numero1 ! 0;
                  zz = numero1;
                  coefi1 = numero1;
                  coefi11 = numero1 / 100;
                  tipod = "T";
                  |HAZ leeliq_04;
              |FINSI;
              |SI numero2 ! 0;
                  zz = numero2;
                  coefi1 = numero2;
                  coefi11 = numero2 / 100;
                  tipod = "C";
                  |HAZ leeliq_04;
              |FINSI;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;

  |SI tippar = 2;  |Y obitus = 2;     ||VARIOS, individual Y SE MUERE ALGUIEN;
      |SI numero1 ! 0;
          zz = numero1;
          coefi1 =  numero1;
          coefi11 =  numero1 / 100;
          tipod = "T";
          |HAZ leeliq_04;
      |FINSI;
      |SI numero2 ! 0;
          zz = numero2;
          coefi1 =  numero2;
          coefi11 =  numero2 / 100;
          tipod = "C";
          |HAZ leeliq_04;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO leeliq_04;                 || Lee la Liquidacion
  #6#0 = empreac;
  #6#141 = tipod;
  |LIBERA #6; |LEE 110#6=;
  FEstado = FSalida;
  |SI FSalida ! 0;
      |DDEFECTO #6;
      #6#000 = empreac;
      #6#141 = tipod;
  |FINSI;

  #9#0 = empreac;
  #9#1 = tipod;
  |LIBERA #9; |LEE 110#9=;
  FEstad1 = FSalida;
  |SI FSalida ! 0;
      |DDEFECTO #9;
      #9#0 = empreac;
      #9#1 = tipod;
  |FINSI;

  #10#0 = empreac;
  #10#1 = tipod;
  |LIBERA #10; |LEE 110#10=;
  FEstad2 = FSalida;
  |SI FSalida ! 0;
      |DDEFECTO #10;
      #10#0 = empreac;
      #10#1 = tipod;
  |FINSI;

  |SI sw_fich = 0; |HAZ grabafi_04_0; |FINSI;
  |SI sw_fich = 1; |HAZ grabafi_04_1; |FINSI;
  |SI sw_fich = 2; |HAZ grabafi_04_2; |FINSI;
  |SI sw_fich = 3; |HAZ grabafi_04_3; |FINSI;
  |SI sw_fich = 4; |HAZ grabafi_04_4; |FINSI;

  #6#213 = obitus;
  |SI FEstado = 0; |GRABA 021#6a;  |SINO; |GRABA 021#6n;  |FINSI;
  |SI FEstad1 = 0; |GRABA 021#9a;  |SINO; |GRABA 021#9n;  |FINSI;
  |SI FEstad2 = 0; |GRABA 021#10a; |SINO; |GRABA 021#10n; |FINSI;
|FPRC;

|PROCESO grabafi_04_0;
  diferencia = #0#29 - #0#28;
  |SI diferencia [ 731;
      #6#218 = #6#218 + (#0#24 % zz);
      #6#219 = #6#219 + (#0#25 % zz);
  |SINO;
      |SI #0#26 = "A";  |O #0#26 = "I";  |O #0#26 = "R";
          #9#41 = #9#41 + (#0#24 % zz);
          #9#42 = #9#42 + (#0#25 % zz);
      |SINO;
          #9#26 = #9#26 + (#0#24 % zz);
          #9#27 = #9#27 + (#0#25 % zz);
      |FINSI;
  |FINSI;
  si_arte = #0#13 % 102;
  |SI si_arte = "**";
      #9#182 = #9#182 + 500001;
  |SINO;
      #9#182 = #9#182 + (#0#14 % zz);
  |FINSI;
|FPRC;

|PROCESO grabafi_04_1;
  |SI #1#4 = "M";
      #6#73  = #6#73  + (#1#8 % zz);
  |SINO;
      #6#120 = #6#120 + (#1#8 % zz);
  |FINSI;
  #6#136 = #6#136 + (#1#7 % zz);
|FPRC;

|PROCESO grabafi_04_2;
  |SI #2#30 = "R";  |Y #2#18 = "I";
      #6#221 = #6#221 + (#2#16 % zz);
  |FINSI;

  |SI #2#30 = "R";  |Y #2#18 = "D";
      #6#222 = #6#222 + (#2#16 % zz);
  |FINSI;

  |SI #2#30 = "I";  |Y #2#18 = "I";  |Y #2#31 = 1;
      #6#223 = #6#223 + (#2#16 % zz);
  |FINSI;

  |SI #2#30 = "I";  |Y #2#18 = "D";  |Y #2#31 = 1;
      #6#224 = #6#224 + (#2#16 % zz);
  |FINSI;

  |SI #2#30 = "I";  |Y #2#18 = "I";  |Y #2#31 = 2;
      #6#225 = #6#225 + (#2#16 % zz);
  |FINSI;

  |SI #2#30 = "I";  |Y #2#18 = "D";  |Y #2#31 = 2;
      #6#226 = #6#226 + (#2#16 % zz);
  |FINSI;

  |SI #2#30 = "I";  |Y #2#18 = "I";  |Y #2#31 = 3;
      #6#227 = #6#227 + (#2#16 % zz);
  |FINSI;

  |SI #2#30 = "I";  |Y #2#18 = "D";  |Y #2#31 = 3;
      #6#228 = #6#228 + (#2#16 % zz);
  |FINSI;
|FPRC;

|PROCESO grabafi_04_3;
  |SI #3#4 = "DPN";
      #6#51  = #6#51  + (#3#7 % zz);
  |FINSI;
  |SI #3#4 = "DPI";
      #9#249 = #9#249 + (#3#7 % zz);
  |FINSI;
  |SI #3#4 = "RNI";
      |SI #3#6 < 92; #6#53  = #6#53   + (#3#7 % zz);  |FINSI;
      |SI #3#6 ] 92; #9#250 = #9#250 + (#3#7 % zz);   |FINSI;
  |FINSI;
  |SI #3#4 = "EXC"; #6#80 = #6#80 + (#3#7 % zz); |FINSI;
  |SI #3#4 = "CNC"; |Y #3#6 < 92; #6#84 = #6#84 + (#3#7 % zz); |FINSI;
  |SI #3#4 = "RNR";
      #6#31 = #6#31 + (#3#7 % zz);
      |SI #3#6 < 92; #9#202 = #9#202 + (#3#7 % zz); |FINSI;
      |SI #3#6 ] 92; #9#203 = #9#203 + (#3#7 % zz); |FINSI;
  |FINSI;
|FPRC;

|PROCESO grabafi_04_4;
  suma  = #4#9;          ||  Aplicado en declaracion.
  suma1 = #4#9;
  suma2 = #4#9 + #4#10; ||  Aplicado en decl. + Pendiente.

  |SI #4#19 = "S";
      suma  = suma + #4#10; suma1 = 0;
  |SINO;
      suma  = 0;
  |FINSI;

  #10#72 = #10#72 + (suma1 % coefi1);

  |SI #4#4   = 92;
      #10#2  = #10#2  + (suma2 % coefi1);
      #10#3  = #10#3  + (suma1 % coefi1);
  |FINSI;

  |SI #4#4 = 93;  |O #4#4 = 94;  |O #4#4 = 95;
      #10#4  = #10#4  + (suma2 % coefi1);
      #10#5  = #10#5  + (suma1 % coefi1);
  |FINSI;

  |SI #4#4   = 96;
      Valfa = #4#6 % 101;
      |SI Valfa = "*";
         #10#6  = #10#6  + (suma2 % coefi1);
         #10#7  = #10#7  + (suma1 % coefi1);
      |SINO;
         #10#8  = #10#8  + (suma2 % coefi1);
         #10#9  = #10#9  + (suma1 % coefi1);
      |FINSI;
  |FINSI;

  #6#99 = #6#99 + (#4#9 % coefi1);
|FPRC;

|| **************************************************************************
||                            DILIGENCIAS
|| **************************************************************************

|PROCESO incide_04;
|SI incisino = "S";
   |SI sw = 0; |HAZ grabaci_04; sw = 1; |FINSI;
   linea = "00" + linea;
   linea = linea % -102;

   rendi = "          " + rendi;
   rendi = rendi % -110;

   |QBF descri;
   descri = descri + blanco;
   descri = descri % 130;

   alfa1 = "NO CALCULO ->  " + linea + " " + descri + "    " + rendi;
   |SI tipore = "R";
       alfa1 = "RENTA ------>  " + linea + " " + descri + "    " + rendi;
   |FINSI;
   |SI tipore = "P";
       alfa1 = "PATRIMONIO ->  " + linea + " " + descri + "    " + rendi;
   |FINSI;
   |HAZ grabaci_04;
|FINSI;
|FPRC;

|PROCESO grabaci_04;
swcalcu = 1;
lininci = lininci + 5;
#5#0 = empreac;
#5#1 = lininci;
#5#2 = alfa1;
#5#3 = "S";
|GRABA 020#5n;
|LIBERA #5;
|FPRC;

|| **************************************************************************
||                             PROCESO BUCLES
|| **************************************************************************

|DEFBUCLE leefic0;   || Bucle Transparencias Fiscal
#0#1;
;
empresa, 01;
emprefi, 99;
e;
NULL, fich_04_0;
|FIN;

|DEFBUCLE leefic1;   || Bucle Profesional Directa
#1#1;
;
empresa, 01;
emprefi, 99;
e;
NULL, fich_04_1;
|FIN;

|DEFBUCLE leefic2;    || Bucle Profesional Coeficiente
#2#1;
;
empresa, 01;
emprefi, 99;
e;
NULL, fich_04_2;
|FIN;

|DEFBUCLE leefic3;    || Bucle Empresarial Directa
#3#1;
;
empresa, 01;
emprefi, 99;
e;
NULL, fich_04_3;
|FIN;

|DEFBUCLE leefic4;    || Bucle Empresarial Coeficiente
#4#1;
;
empresa, 01;
emprefi, 99;
be;
NULL, fich_04_4;
|FIN;


|| **************************************************************************
                              INICIO
|| **************************************************************************

|PROCESO recalpa4;
|ABRE #5;
|ABRE #6;
|ABRE #9;
|ABRE #10;

|SI progra_16 = 1; sw = 0; lininci = 4500; |BUCLE leefic0; |FINSI;
|SI progra_17 = 1; sw = 0; lininci = 4800; |BUCLE leefic1; |FINSI;
|SI progra_18 = 1; sw = 0; lininci = 5100; |BUCLE leefic2; |FINSI;
|SI progra_19 = 1; sw = 0; lininci = 5400; |BUCLE leefic3; |FINSI;
|SI progra_20 = 1; sw = 0; lininci = 5700; |BUCLE leefic4; |FINSI;

|CIERRA #5;
|CIERRA #6;
|CIERRA #9;
|CIERRA #10;
|FPRC;
