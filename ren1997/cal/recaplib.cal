|FICHEROS;
    recaplib #00;   || captura
    reproedi #01;   || profesionales estimacion directa(PC)
    reprofco #02;   || profesionales por coeficientes  (PB)
    reempedi #03;   || empresarios estimacion directa  (EC)
    reemprco #04;   || empresarios por coeficientes    (EB)
    redatcon #11;   || datos contribuyentes
    recontli #13;   || control planilla 1 hoja desde renta
    repagret #25;   || Retenciones y Pagos a Cuenta
    reempagr #40;   || Agricolas Cabeceras
    reempag1 #41;   || Agricolas Lineas
    recapinm #42;   || Capital Inmobiliario
    recontdt #43;   || control Arrendamientos

    eosclien #06;   || clientes REO
    eosactiv #07;   || actividades REO
    eoslinve #12;   || ventas lins.
    eoslinco #14;   || compras lins.
    eoscomul #16;   || Comuneros Lineas
    eosexist #17;   || Existencias
    eosm3130 #24;   || 130 REO
    eosma131 #26;   || 131 REO
    eosconcd #27;   || Conceptos Agricolas
    eosconce #28;   || Conceptos
    eosm0011 #29;   || Maestros Referencias
    eosm0012 #30;   || Registros Arrendamientos

    agicontr #20;
|FIN;

|VARIABLES;
    linea_p = 0;
    FEstado = 0;

    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;

    coef_gas = 0;
    agieos = "";
    retenci = 0;
    ultima = 0;
    tipo = "";
    perc = "";
    seleccio = 0;
    grabalo = 0;
    buscanif = "";
    control = 0;
    n_cont  = 0;
    n_cant  = 0;
    n_tipo  = "";
    n_esta  = 0;
    n_busc  = 0;
    campo_e = 0;
    campo_a = 0;


    empresa = 0;
    campo16 = 0;
    campo17 = 0;
    campo18 = 0;
    campo19 = 0;
    campo20 = 0;
    campo21 = 0;
    campo22 = 0;
    campo23 = 0;
    campo24 = 0;
    campo25 = 0;
    campo26 = 0;
    campo27 = 0;
    campo28 = 0;
    campo29 = 0;
    campo30 = 0;
    campo31 = 0;
    campo32 = 0;
    campo33 = 0;
    campo34 = 0;
    campo35 = 0;
    campo36 = 0;
    campo37 = 0;
    campo38 = 0;
    campo39 = 0;
    campo40 = 0;
    campo41 = 0;

    fichero = "";
    anyo = "";
    porce   = 0;
    nombre  = "";
    sw_estar = 0;
    LoHago   = 0;
    Nombre   = "";

    Total    = 0;
    Producto = 0;
    _Agri    = "";
    TipoT    = "";
    Porce    = 0;
    Bas      = 0;
    _Iv      = 0;
    Rec      = 0;
    Ret      = 0;
    Retencion= 0;
    Con      = 0;
    Dto1     = 0;
    Dto2     = 0;
    Dto3     = 0;
    _Ingresos = 0;
    Campo01   = 0;
    Campo02   = 0;
    Campo03   = 0;
    Campo04   = 0;
    Campo05   = 0;
    RCatastral = "";
    LineaB     = 0;

    &dni = "";
    &codigo = 0;
    &error = "";
    &empre_e = 0;
    &empre_r = 0;
    &activ_e = 0;
    &tipo_e  = "";
    &EsArrend = 0;
    &LineaArre = 0;
|FIN;

|INCLUYE i_husuar;
|INCLUYE i_activo;

     *********************************************************************
     LLAMADAS DESDE CAMPOS
     *********************************************************************

|PROCESO path;
|DBASS alfa1;  |QBF alfa1;
|PATH_DAT #20 alfa1;
|ABRE #20;
|LEE 040#20p;
|SI FSalida ! 0; |DDEFECTO #20; |FINSI;
|CIERRA #20;

agieos = #20#3; |QBF agieos;

|PATH_DAT #06 agieos;  || empresas
|PATH_DAT #07 agieos;  || empresas/actividades
|PATH_DAT #12 agieos;  || ventas lins.
|PATH_DAT #14 agieos;  || compras lins.
|PATH_DAT #16 agieos;  || Comuneros lineas
|PATH_DAT #17 agieos;  || Existencias
|PATH_DAT #24 agieos;  || 130
|PATH_DAT #26 agieos;  || 131
|PATH_DAT #27 agieos;  || Conceptos Agricolas
|PATH_DAT #28 agieos;  || Conceptos
|PATH_DAT #29 agieos;  || arrendamientos
|PATH_DAT #30 agieos;  || arrendamientos
|FPRC;

|PROCESO lee_eos1; |TIPO 0;        || lee empresa en R.E.O.
|ABRE #6;
    #6#0 = #0#1;
|LEE 001#6=;
|SI FSalida ! 0;
    |DDEFECTO #6;
    |MENAV "    ATENCION!!! Empresa inexistente en R.E.O. ...";
    |ERROR;
|FINSI;
|ATRI I;
|PINTA 1419, #6#2;
|ATRI 0;
|CIERRA #6;
|FPRC;

|PROCESO lee_eos2; |TIPO 0;   || lee actividad en R.E.O.
|ABRE #7;
    #7#0 = #0#1;
    #7#1 = #0#2;
    #7#2 = 97;
|LEE 001#7=;
|SI FSalida ! 0;
    |DDEFECTO #7;
    |MENAV "    ATENCION!!! Actividad inexistente en R.E.O. ...";
    |ERROR;
    |ACABA;
|FINSI;

|SI #7#14 = "S";
    |MENAV "    ATENCION!!! Actividad Comunero, introduzca la Comunidad.";
    |ERROR;
    |ACABA;
|FINSI;

|SI #7#46 = "S";
    |CAMPO_MODIFICA #0#5, 1, "S";
    |CAMPO_MODIFICA #0#6, 1, "S";
|SINO;
    |CAMPO_MODIFICA #0#5, 1, "N";
    |CAMPO_MODIFICA #0#6, 1, "N";
    #0#5 = 0;  |PINTA #0#5;
    #0#6 = 0;  |PINTA #0#6;
    |PINTA 1719, "                                       ";
|FINSI;

|ATRI I;
|PINTA 1519, #7#6;
|ATRI 0;
|CIERRA #7;
|FPRC;

|PROCESO es_comuner;
|SI #16#0 = #0#1; |Y #16#1 = #0#2;
    sw_estar = 1;
    porce    = #16#6;
    nombre   = #16#4;
|FINSI;
|FPRC;

|DEFBUCLE eoscomul;
#16#2;
;
#0#5, #0#6, 97;
#0#5, #0#6, 97;
e;
NULL, es_comuner;
|FIN;

|PROCESO mira_comu; |TIPO 0;
|SI #7#46 = "N"; |ACABA; |FINSI;
sw_estar = 0;
|BUCLE eoscomul;
|SI sw_estar = 0;
    |MENAV "    La empresa comunero no pertenece a la Comunidad de Bienes";
    |ERROR;
    |ACABA;
|SINO;
    |PINTA 1719, nombre;
|FINSI;
|FPRC;

|PROCESO lee_renta1; |TIPO 0;
|ABRE #11;
    #11#0 = #0#3;
|LEE 001#11=;
|SI FSalida ! 0;
    |DDEFECTO #11;
    |MENAV "    ATENCION!!! Contribuyente inexistente en RENTA ...";
    |ERROR;
|FINSI;
|CIERRA #11;
Nombre = #11#1;
|FPRC;

|PROCESO lee_renta2; |TIPO 0;
|ATRI I;
|SI #0#4 = "T";  |PINTA 1919, #11#01;  |FINSI;
|SI #0#4 = "C";  |PINTA 1919, #11#28;  |FINSI;
|ATRI 0;
|FPRC;

|PROCESO pide_otro;
|PUSHV 10141464;
|AVISO;
|ATRI P;
|CUADRO 10141464;
|ATRI 0;
|ATRI I;
|PINTA 1115," Actividad con regimen 1997 diferente a 1997 !!! ";
|PINTA 1315," Informe del regimen al que pertenecia en 1997 ..";
|ATRI 0;
|ATRI R;
|PINTA 1215,"   (E)mpr./(P)rof.:    *  (B)coef./(C)direc.:    ";
|ATRI 0;
    E_sup = "1";
    E_inf = "EPX";
|MENSA "0400Entre: (E) empresarial; (P) profesional; (X) fuera";
    alfa2 = 1235 ? 0;
|BLIN 4;
|SI alfa2 ! "X";
        E_sup = "1";
        E_inf = "BCX";
    |MENSA "0400Entre: (B) coeficientes; (C) directa; (X) fuera";
        alfa3 = 1261 ? 0;
    |BLIN 4;
|FINSI;
|POPV;
    alfa1 = alfa2 + alfa3;
    #7#23 = alfa2;
    #7#22 = alfa3;
|FPRC;

     *********************************************************************
     LECTURA ACTIVIDADES
     *********************************************************************

|PROCESO captura1;
    tipo = #0#4;
|SI tipo = "T";  perc = "TITULAR";  |FINSI;
|SI tipo = "C";  perc = "CONYUGE";  |FINSI;

coef_gas = #7#25;
|SI #7#46 = "S";
    |ABRE #7;
    #7#0 = #0#5;
    #7#1 = #0#6;
    #7#2 = 97;
    |LEE 040#7=;
    |SI FSalida ! 0; |DDEFECTO #7; |FINSI;
    |CIERRA #7;
|SINO;
    porce = 100;
|FINSI;

|SI EsArrend = 1;
    |HAZ Arrendamientos;
    |ACABA;
|FINSI;

|SI #7#41 = "S";
    |HAZ pagos;
    |HAZ Agricolas;
|SINO;
    alfa1 = #7#23 + #7#22;
    n_tipo = alfa1;                               || variable conserva
    |SI alfa1 = "EA";   |HAZ pide_otro;  |FINSI;
    |SI alfa1 = "PC";   |HAZ profdire1;  |FINSI;
    |SI alfa1 = "PB";   |HAZ profcoef1;  |FINSI;
    |SI alfa1 = "EC";   |HAZ emprdire1;  |FINSI;
    |SI alfa1 = "EB";   |HAZ emprcoef1;  |FINSI;
    |HAZ pagos;
|FINSI;
|FPRC;

*********************************************************************
LECTURA DEL 131 Y MODULOS
*********************************************************************

|PROCESO lee_131b;
  |SI #26#3 = "          "; |ACABA; |FINSI;

  #25#0 = #0#3;
  #25#1 = linea_p;
  |LIBERA #25;
  |LEE 140#25=;
  FEstado = FSalida;
  |SI FEstado ! 0; |DDEFECTO #25; |FINSI;

  #25#0 = #0#3;
  #25#1 = linea_p;
  #25#2 = #0#4;
  #25#3 = perc;
  #25#4 = 0;

  |SI #26#24 > 0;
      |SI #26#1 = 1;  #25#5 = #26#24;  |FINSI;
      |SI #26#1 = 2;  #25#6 = #26#24;  |FINSI;
      |SI #26#1 = 3;  #25#7 = #26#24;  |FINSI;
      |SI #26#1 = 4;  #25#8 = #26#24;  |FINSI;
  |FINSI;
  |SI #7#41 ! "S";  #25#9 = #25#9 + #26#40;  |FINSI;

  |SI #25#5 ! 0; |O #25#6 ! 0; |O #25#7 ! 0; |O #25#8 ! 0; |O #25#9 ! 0;
      |SI FEstado = 0; |GRABA 140#25a; |SINO; |GRABA 140#25n; |FINSI;
      npro = 51;  empresa = #0#3;  |HAZ sigrabo;
      HContribu  = #25#0;
      HNombre    = Nombre;
      HPrograma  = "(repagret) RETENCIONES Y PAGOS A CUENTA";
      HLinea     = #25#1;
      HPerceptor = #25#3;
      HRdto      = #25#5 + #25#6 + #25#7 + #25#8;
      HTipo      = "ALTA FICHA DESDE PASE REO";
      |HAZ AltaHistorico;
  |FINSI;
|FPRC;

|DEFBUCLE lee_131;
  #26#1;
  ;
  #7#0, 1, 97;
  #7#0, 4, 97;
  e;
  NULL, lee_131b;
|FIN;

|PROCESO lee_m3130b;
  |SI #24#4 = "          "; |ACABA; |FINSI;

  #25#0 = #0#3;
  #25#1 = linea_p;
  |LIBERA #25;
  |LEE 140#25=;
  FEstado = FSalida;
  |SI FEstado ! 0; |DDEFECTO #25; |FINSI;

  #25#0 = #0#3;
  #25#1 = linea_p;
  #25#2 = #0#4;
  #25#3 = perc;
  #25#4 = 0;

  |SI #24#29 > 0;
      |SI #24#2 = 1;  #25#5 = #24#29;  |FINSI;
      |SI #24#2 = 2;  #25#6 = #24#29;  |FINSI;
      |SI #24#2 = 3;  #25#7 = #24#29;  |FINSI;
      |SI #24#2 = 4;  #25#8 = #24#29;  |FINSI;
  |FINSI;

  |SI #7#41 ! "S";  #25#9 = #25#9 + #24#23 + #24#36;  |FINSI;

  |SI #25#5 ! 0; |O #25#6 ! 0; |O #25#7 ! 0; |O #25#8 ! 0; |O #25#9 ! 0;
      |SI FEstado = 0; |GRABA 140#25a; |SINO; |GRABA 140#25n; |FINSI;
      npro = 51;  empresa = #0#3;  |HAZ sigrabo;
      HContribu  = #25#0;
      HNombre    = Nombre;
      HPrograma  = "(repagret) RETENCIONES Y PAGOS A CUENTA";
      HLinea     = #25#1;
      HPerceptor = #25#3;
      HRdto      = #25#5 + #25#6 + #25#7 + #25#8;
      HTipo      = "ALTA FICHA DESDE PASE REO";
      |HAZ AltaHistorico;
  |FINSI;
|FPRC;

|DEFBUCLE lee_m3130;
#24#1;
;
#7#0, 0, 1, 97;
#7#0, 9, 4, 97;
e;
NULL, lee_m3130b;
|FIN;

|PROCESO ChequeaPagos;
|SI #25#2 = #0#4; linea_p = #25#1; LoHago = 1; |ERROR10; |ACABA; |FINSI;
linea_p = #25#1;
|FPRC;

|DEFBUCLE repagret;
#25#1;
;
#0#3,  1;
#0#3, 99;
;
NULL, ChequeaPagos;
|FIN;

|PROCESO pagos;
  linea_p = 0;
  LoHago  = 0; |BUCLE repagret;
  |SI LoHago = 1; |ACABA; |FINSI;

  |ABRE #25;
  linea_p = linea_p + 1; |BUCLE lee_131;
  linea_p = linea_p + 1; |BUCLE lee_m3130;
  |CIERRA #25;
|FPRC;

|PROCESO tipo_3; |TIPO 3;
|SI #0#0 = "SI";
    |HAZ path;      || (por si acaso se le olvidan)
    |HAZ captura1;
    |SI grabalo = 0;
        |MENAV "    ATENCION!!! Empresa/Actividad sin Libros ni 130 ...";
    |FINSI;
|FINSI;
|FPRC;

     *********************************************************************
     LECTURA DE LIBROS Y CARGA DE CAMPOS
     *********************************************************************

|PROCESO cto_ce;
|SI n_cont ! 0;|Y n_cant ! 0;
    |SI n_cont =  1;  campo24 = campo24 + n_cant;  |FINSI;  || tributos
    |SI n_cont =  2;  campo25 = campo25 + n_cant;  |FINSI;  || mater.primas
    |SI n_cont =  3;  campo26 = campo26 + n_cant;  |FINSI;  || coste person.
    |SI n_cont =  4;  campo27 = campo27 + n_cant;  |FINSI;  || seg.social
    |SI n_cont =  5;  campo28 = campo28 + n_cant;  |FINSI;  || planes pens.
    |SI n_cont =  6;  campo29 = campo29 + n_cant;  |FINSI;  || honor.profes.
    |SI n_cont =  7;  campo30 = campo30 + n_cant;  |FINSI;  || alquileres
    |SI n_cont =  8;  campo31 = campo31 + n_cant;  |FINSI;  || int.prestamos
    |SI n_cont =  9;  campo32 = campo32 + n_cant;  |FINSI;  || amortizac.
    |SI n_cont = 10;  campo33 = campo33 + n_cant;  |FINSI;  || cons.y rep.
    |SI n_cont = 11;  campo34 = campo34 + n_cant;  |FINSI;  || agua,tfno.
    |SI n_cont = 12;  campo35 = campo35 + n_cant;  |FINSI;  || seguros
    |SI n_cont = 13;  campo36 = campo36 + n_cant;  |FINSI;  || dudoso cobro
    |SI n_cont = 14;  campo37 = campo37 + n_cant;  |FINSI;  || reduc.fond.edit.
    |SI n_cont = 15;  campo38 = campo38 + n_cant;  |FINSI;  || fondos rever.
    |SI n_cont = 16;  campo39 = campo39 + n_cant;  |FINSI;  || gtos.admon.
    |SI n_cont = 17;  campo40 = campo40 + n_cant;  |FINSI;  || cuotas corp.
    |SI n_cont = 18;  campo41 = campo41 + n_cant;  |FINSI;  || otros gastos
|FINSI;
|FPRC;

|PROCESO cto_be;
|SI n_cont ! 0;|Y n_cant ! 0;
    |SI n_cont = 1;  campo40 = campo40 + n_cant;  |FINSI;   || compras
    |SI n_cont = 2;  campo18 = campo18 + n_cant;  |FINSI;   || coste pers.
    |SI n_cont = 3;  campo19 = campo19 + n_cant;  |FINSI;   || agua,tfno.
    |SI n_cont = 4;  campo20 = campo20 + n_cant;  |FINSI;   || alquileres
    |SI n_cont = 5;  campo21 = campo21 + n_cant;  |FINSI;   || gtos.finan.
    |SI n_cont = 6;  campo22 = campo22 + n_cant;  |FINSI;   || tributos
    |SI n_cont = 7;  campo23 = campo23 + n_cant;  |FINSI;   || rep.y cons.
    |SI n_cont = 8;  campo24 = campo24 + n_cant;  |FINSI;   || trab.otras emp.
|FINSI;
|FPRC;

|PROCESO cto_cp;
|SI n_cont ! 0;|Y n_cant ! 0;
    |SI n_cont = 1;  campo18 = campo18 + n_cant;  |FINSI;   || mat.consum.
    |SI n_cont = 2;  campo19 = campo19 + n_cant;  |FINSI;   || cost.personal
    |SI n_cont = 3;  campo20 = campo20 + n_cant;  |FINSI;   || int.prestamo
    |SI n_cont = 4;  campo21 = campo21 + n_cant;  |FINSI;   || alqui.locales
    |SI n_cont = 5;  campo22 = campo22 + n_cant;  |FINSI;   || amortiz.
    |SI n_cont = 6;  campo23 = campo23 + n_cant;  |FINSI;   || agua, tfno.
    |SI n_cont = 7;  campo24 = campo24 + n_cant;  |FINSI;   || otros gtos.
|FINSI;
|FPRC;

|PROCESO cto_bp;
|SI n_cont ! 0;|Y n_cant ! 0;
    |SI n_cont = 1;  campo39 = campo39 + n_cant;  |FINSI;   || compras
    |SI n_cont = 2;  campo18 = campo18 + n_cant;  |FINSI;   || coste pers.
    |SI n_cont = 3;  campo19 = campo19 + n_cant;  |FINSI;   || agua, tfno.
    |SI n_cont = 4;  campo20 = campo20 + n_cant;  |FINSI;   || alqui.locales
    |SI n_cont = 5;  campo21 = campo21 + n_cant;  |FINSI;   || gtos.finan.
    |SI n_cont = 6;  campo22 = campo22 + n_cant;  |FINSI;   || tributos
    |SI n_cont = 7;  campo23 = campo23 + n_cant;  |FINSI;   || rep. y cons.
    |SI n_cont = 8;  campo24 = campo24 + n_cant;  |FINSI;   || trab.otras emp.
|FINSI;
|FPRC;

|PROCESO lee_con;
  |DDEFECTO #13;
  |ABRE #13;
  #13#0 = n_busc;
  |LEE 001#13=;
  |SI FSalida = 0;   n_esta = 1;  |FINSI;
  |CIERRA #13;
|FPRC;

|PROCESO busca_cto;
    n_esta = 0;
|SI n_busc ! 0; |Y n_cant ! 0;
    |HAZ lee_con;
    |SI n_esta ! 0;
        |SI n_tipo = "PB";  n_cont = #13#3;  |FINSI;
        |SI n_tipo = "PC";  n_cont = #13#5;  |FINSI;
        |SI n_tipo = "EB";  n_cont = #13#4;  |FINSI;
        |SI n_tipo = "EC";  n_cont = #13#6;  |FINSI;
        |SI n_cont ! 0;
            |SI #13#2 = "G";                      || compras
                |SI n_tipo = "PB";  |HAZ cto_bp;  |FINSI;
                |SI n_tipo = "PC";  |HAZ cto_cp;  |FINSI;
                |SI n_tipo = "EB";  |HAZ cto_be;  |FINSI;
                |SI n_tipo = "EC";  |HAZ cto_ce;  |FINSI;
            |FINSI;
            |SI #13#2 = "I";                      || ingresos
                |SI n_tipo = "PB";  campo17 = campo17 + n_cant;  |FINSI;
                |SI n_tipo = "PC";  campo16 = campo16 + n_cant;  |FINSI;
                |SI n_tipo = "EB";  campo17 = campo17 + n_cant;  |FINSI;
                |SI n_tipo = "EC";
                    |SI n_cont = 1; campo17 = campo17 + n_cant;  |FINSI;
                    |SI n_cont = 2; campo18 = campo18 + n_cant;  |FINSI;
                    |SI n_cont = 3; campo19 = campo19 + n_cant;  |FINSI;
                    |SI n_cont = 4; campo20 = campo20 + n_cant;  |FINSI;
                    |SI n_cont = 5; campo21 = campo21 + n_cant;  |FINSI;
                    |SI n_cont = 6; campo22 = campo22 + n_cant;  |FINSI;
                |FINSI;
            |FINSI;
        |FINSI;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO lee_librocb;
    grabalo = 1;

    n_busc = #14#5;                       || concepto 1
    n_cant = #14#7 % porce;               || importe 1
|HAZ busca_cto;

    n_busc = #14#33;                      || concepto 2
    n_cant = #14#23 % porce;              || importe 2
|HAZ busca_cto;

    n_busc = #14#34;                      || concepto 3
    n_cant = #14#24 % porce;              || importe 3
|HAZ busca_cto;
|FPRC;

|PROCESO lee_librovb;
    grabalo = 1;

    n_busc = #12#5;                       || concepto 1
    n_cant = #12#7 % porce;               || importe 1
|HAZ busca_cto;

    n_busc = #12#33;                      || concepto 2
    n_cant = #12#23 % porce;              || importe 2
|HAZ busca_cto;

    n_busc = #12#34;                      || concepto 3
    n_cant = #12#24 % porce;              || importe 3
|HAZ busca_cto;

    n_busc = #12#60;                      || concepto 4
    n_cant = #12#45 % porce;              || importe 4
|HAZ busca_cto;

    n_busc = #12#61;                      || concepto 5
    n_cant = #12#46 % porce;              || importe 5
|HAZ busca_cto;

    n_busc = #12#62;                      || concepto 6
    n_cant = #12#47 % porce;              || importe 6
|HAZ busca_cto;
|FPRC;

|PROCESO lee_libroa;
    retenci = 0;
    campo16 = 0;
    campo17 = 0;
    campo18 = 0;
    campo19 = 0;
    campo20 = 0;
    campo21 = 0;
    campo22 = 0;
    campo23 = 0;
    campo24 = 0;
    campo25 = 0;
    campo26 = 0;
    campo27 = 0;
    campo28 = 0;
    campo29 = 0;
    campo30 = 0;
    campo31 = 0;
    campo32 = 0;
    campo33 = 0;
    campo34 = 0;
    campo35 = 0;
    campo36 = 0;
    campo37 = 0;
    campo38 = 0;
    campo39 = 0;
    campo40 = 0;
    campo41 = 0;
|FPRC;

|DEFBUCLE lee_libroc;
 #14#1;
 19;
 #0#1, 97,    0, #0#2;
 #0#1, 97, 9999, #0#2;
 e;
 NULL, lee_librocb;
|FIN;

|DEFBUCLE lee_librov;
 #12#1;
 19;
 #0#1, 97,    0, #0#2;
 #0#1, 97, 9999, #0#2;
 e;
 NULL, lee_librovb;
|FIN;

|PROCESO LeeExist;
  |ABRE #17;
  #17#0 = #0#1;
  #17#1 = #0#2;
  #17#2 = 97;
  |LEE 040#17=;
  |SI FSalida ! 0;  |DDEFECTO #17;  |FINSI;

  |CIERRA #17;
|FPRC;

|PROCESO comun;
    grabalo = 0;
|HAZ LeeExist;
|HAZ lee_libroa;
|HAZ re_compras;  |BUCLE lee_libroc;
|HAZ re_ventas;   |BUCLE lee_librov;
|FPRC;

|PROCESO re_compras;
    fichero = #0#1;
    fichero = "00000" + fichero;
    fichero = fichero % -105;
    anyo    = "97";
    fichero = "c" + fichero + anyo;
|NOME_DAT #14 fichero;
|FPRC;

|PROCESO re_ventas;
    fichero = #0#1;
    fichero = "00000" + fichero;
    fichero = fichero % -105;
    anyo    = "97";
    fichero = "v" + fichero + anyo;
|NOME_DAT #12 fichero;
|FPRC;

     *********************************************************************
     GRABACION DE PROFESIONALES EN ESTIMACION DIRECTA
     *********************************************************************

|PROCESO profdire3;
    #1#27 = #1#33 - #1#34;
    #1#25 = #1#16 % 1;   || 1% ingresos integros
    #1#26 = #1#18 + #1#19 + #1#20 + #1#21 + #1#22 + #1#23 + #1#24 + #1#25;
    #1#28 = #1#16 - #1#26 + #1#27;      || rendimiento neto
|FPRC;

|PROCESO profdire2;
|ABRE #1;
|HAZ ultimo1;
|DDEFECTO #1;
    #1#00 = #0#3;        || codigo
    #1#01 = ultima;      || linea
    #1#02 = tipo;        || tipo
    #1#03 = perc;        || perceptor
    #1#13 = #7#4;        || epigrafe -1-
    #1#14 = #7#5;        || epigrafe -2-
    #1#15 = #7#6;        || nombre actividad
    #1#16 = campo16;     || ingresos integros
    #1#18 = campo18;     || material consumido
    #1#19 = campo19;     || coste personal
    #1#20 = campo20;     || inter.prestamo
    #1#21 = campo21;     || alquiler locales
    #1#22 = campo22;     || amortizaciones
    #1#23 = campo23;     || energia, agua, etc.
    #1#24 = campo24;     || otros gastos
    #1#37 = #7#11;       || Codigo Provincia
    #1#38 = #7#12;       || Codigo Poblacion
    #1#39 = #7#9;        || Domicilio

|HAZ profdire3;          || CALCULA ...
|GRABA 020#1n;
|SI FSalida=0; npro=7; empresa=#0#3; |HAZ sigrabo; |FINSI;      || marca (*)
|CIERRA #1;

HContribu  = #1#0;
HNombre    = Nombre;
HPrograma  = "(reproedi) PROFESIONALES DIRECTA";
HLinea     = #1#1;
HPerceptor = #1#3;
HRdto      = #1#28;
HTipo      = "ALTA FICHA DESDE PASE REO";
|HAZ AltaHistorico;
|FPRC;

|PROCESO ultimo1;
    #1#0 = #0#3;
    #1#1 = 99;
|LEE 001#1];
|SI FSalida = 0;
    |LEE 001#1a;
|SINO;
    |LEE 001#1u;
|FINSI;
|SI FSalida = 0; |Y #1#0 = #0#3;
        ultima = #1#1 + 1;
|SINO;
        ultima = 1;
|FINSI;
|FPRC;

|PROCESO profdire1;
|HAZ comun;
|SI grabalo ! 0;
    |HAZ profdire2;
|FINSI;
|FPRC;

     *********************************************************************
     GRABACION DE PROFESIONALES EN COEFICIENTES
     *********************************************************************

|PROCESO profcoef3;
    #2#25 = #2#17 + #2#37;
    #2#25 = (#2#25-(#2#18+#2#19+#2#20+#2#21+#2#22+#2#23+#2#24+#2#38+#2#39))%10;
|SI #2#25 < 0;  #2#25 = 0;  |FINSI;
    #2#30 = 0;
|PARA nume1 = 18; |SI nume1 [ 25; |HACIENDO nume1 = nume1 + 1;
        #2#30 = #2#30 + #2nume1;
|SIGUE;
    #2#30 = #2#30 + #2#38 + #2#39 - #2#37;
    #2#40 = #2#17;
    #2#41 = #2#40 - #2#30;
    #2#31 = #2#26 - #2#27 - #2#28 - #2#29;
    #2#32 = #2#41 + #2#31;
|FPRC;

|PROCESO profcoef2;
|ABRE #2;
|HAZ ultimo2;
|DDEFECTO #2;
    #2#00 = #0#3;        || codigo
    #2#01 = ultima;      || linea
    #2#02 = tipo;        || tipo
    #2#03 = perc;        || perceptor
    #2#13 = #7#4;        || epigrafe -1-
    #2#14 = #7#5;        || epigrafe -2-
    #2#15 = #7#6;        || nombre actividad
    #2#17 = campo17;     || ingresos integros
    #2#18 = campo18;     || coste personal
    #2#19 = campo19;     || energia, agua, etc.
    #2#20 = campo20;     || alquileres locales
    #2#21 = campo21;     || gastos financieros
    #2#22 = campo22;     || tributos
    #2#23 = campo23;     || rep. y conservacion
    #2#24 = campo24;     || trabajos otras empresas
    #2#37 = #17#10;      || existencia final
    #2#38 = #17#3;       || existencia inicio
    #2#39 = campo39;     || compras
    #2#44 = #7#11;       || Codigo Provincia
    #2#45 = #7#12;       || Codigo Poblacion
    #2#46 = #7#9;        || Domicilio

|HAZ profcoef3;          || CALCULA ...
|GRABA 020#2n;
|SI FSalida=0; npro=8; empresa=#0#3; |HAZ sigrabo; |FINSI;      || marca (*)
|CIERRA #2;

HContribu  = #2#0;
HNombre    = Nombre;
HPrograma  = "(reprofco) PROFESIONALES COEFICIENTE";
HLinea     = #2#1;
HPerceptor = #2#3;
HRdto      = #2#32;
HTipo      = "ALTA FICHA DESDE PASE REO";
|HAZ AltaHistorico;
|FPRC;

|PROCESO ultimo2;
    #2#0 = #0#3;
    #2#1 = 99;
|LEE 001#2];
|SI FSalida = 0;
    |LEE 001#2a;
|SINO;
    |LEE 001#2u;
|FINSI;
|SI FSalida = 0; |Y #2#0 = #0#3;
        ultima = #2#1 + 1;
|SINO;
        ultima = 1;
|FINSI;
|FPRC;

|PROCESO profcoef1;
|HAZ comun;
|SI grabalo ! 0;
    |HAZ profcoef2;
|FINSI;
|FPRC;

     *********************************************************************
     GRABACION DE EMPRESARIALES EN ESTIMACION DIRECTA
     *********************************************************************

|PROCESO emprdire3;
    #3#23 = #3#17 + #3#18 + #3#19 + #3#20 + #3#21 + #3#22;
    #3#42 = 0;
|PARA nume1 = 24; |SI nume1 [ 41; |HACIENDO nume1 = nume1 + 1;
        #3#42 = #3#42 + #3nume1;
|SIGUE;
    #3#43 = #3#23;
    #3#44 = #3#42;

    #3#45 = #3#43 - #3#44;
    #3#48 = #3#46 - #3#47;
    #3#49 = #3#45 + #3#48;
|FPRC;

|PROCESO emprdire2;
|ABRE #3;
|HAZ ultimo3;
|DDEFECTO #3;
    #3#00 = #0#3;        || codigo
    #3#01 = ultima;      || linea
    #3#02 = tipo;        || tipo
    #3#03 = perc;        || perceptor
    #3#13 = #7#4;        || epigrafe -1-
    #3#14 = #7#5;        || epigrafe -2-
    #3#15 = #7#6;        || nombre actividad
    #3#17 = campo17;     || ingresos ordinarios por ventas y servicios
    #3#18 = campo18;     || autoconsumo
    #3#19 = campo19;     || subvenciones
    #3#20 = campo20;     || otras transferencias
    #3#21 = campo21;     || ingresos financieros
    #3#22 = campo22;     || otros ingresos
    #3#23 = #3#17 + #3#18 + #3#19 + #3#20 + #3#21 + #3#22;  || total ingresos
    #3#24 = campo24;     || tributos
    #3#25 = campo25;     || materias primas
    #3#26 = campo26;     || coste personal
    #3#27 = campo27;     || seguridad social
    #3#28 = campo28;     || planes pensiones
    #3#29 = campo29;     || honorarios profesionales
    #3#30 = campo30;     || alquileres
    #3#31 = campo31;     || intereses prestamos
    #3#32 = campo32;     || amortizaciones
    #3#33 = campo33;     || conservacion y reparaciones
    #3#34 = campo34;     || energia, agua, etc.
    #3#35 = campo35;     || seguros
    #3#36 = campo36;     || dudoso cobro
    #3#37 = campo37;     || reduc.fond.edit.
    #3#38 = campo38;     || fondos rever.
    #3#39 = campo39;     || gastos admon.
    #3#40 = campo40;     || cuotas corpor.
    #3#41 = campo41;     || otros gastos
    #3#43 = #3#23;       || total ingresos computados
    #3#45 = #3#23;       || rendimiento neto previo
    #3#49 = #3#23;       || rendimiento neto
    #3#55 = #7#11;       || Codigo Provincia
    #3#56 = #7#12;       || Codigo Poblacion
    #3#57 = #7#9;        || Domicilio

|HAZ emprdire3;
|GRABA 020#3n;
|SI FSalida=0; npro=9; empresa=#0#3; |HAZ sigrabo; |FINSI;      || marca (*)
|CIERRA #3;

HContribu  = #3#0;
HNombre    = Nombre;
HPrograma  = "(reempedi) EMPRESARIALES DIRECTA";
HLinea     = #3#1;
HPerceptor = #3#3;
HRdto      = #3#49;
HTipo      = "ALTA FICHA DESDE PASE REO";
|HAZ AltaHistorico;
|FPRC;

|PROCESO ultimo3;
    #3#0 = #0#3;
    #3#1 = 99;
|LEE 001#3];
|SI FSalida = 0;
    |LEE 001#3a;
|SINO;
    |LEE 001#3u;
|FINSI;
|SI FSalida = 0; |Y #3#0 = #0#3;
        ultima = #3#1 + 1;
|SINO;
        ultima = 1;
|FINSI;
|FPRC;

|PROCESO emprdire1;
|HAZ comun;
|SI grabalo ! 0;
    |HAZ emprdire2;
|FINSI;
|FPRC;

     *********************************************************************
     GRABACION DE EMPRESARIALES EN COEFICIENTES
     *********************************************************************

|PROCESO emprcoef3;
    #4#25 = #4#17 + #4#38;
    #4#25 = #4#25-(#4#18+#4#19+#4#20+#4#21+#4#22+#4#23+#4#24+#4#39+#4#40);
|SI #4#25 < 0;  #4#33 = 0;  |FINSI;
    #4#25 = #4#25 % #4#33;
    #4#30 = 0;
|PARA nume1 = 18; |SI nume1 [ 25; |HACIENDO nume1 = nume1 + 1;
        #4#30 = #4#30 + #4nume1;
|SIGUE;
    #4#30 = #4#30 + #4#39 + #4#40 - #4#38;
    #4#41 = #4#17;
    #4#42 = #4#41 - #4#30;
    #4#31 = #4#26 - #4#27 - #4#28 - #4#29;
    #4#32 = #4#42 + #4#31;
|FPRC;

|PROCESO emprcoef2;
|ABRE #4;
|HAZ ultimo4;
|DDEFECTO #4;
    #4#00 = #0#3;       || codigo
    #4#01 = ultima;      || linea
    #4#02 = tipo;        || tipo
    #4#03 = perc;        || perceptor
    #4#13 = #7#4;        || epigrafe -1-
    #4#14 = #7#5;        || epigrafe -2-
    #4#15 = #7#6;        || nombre actividad
    #4#17 = campo17;     || ingresos integros
    #4#18 = campo18;     || coste personal
    #4#19 = campo19;     || energia, agua, etc.
    #4#20 = campo20;     || alquileres locales
    #4#21 = campo21;     || gastos financieros
    #4#22 = campo22;     || tributos
    #4#23 = campo23;     || rep. y conservacion
    #4#24 = campo24;     || trabajos otras empresas
    #4#33 = coef_gas;    || (%) gastos
    #4#38 = #17#10;      || existencia final
    #4#39 = #17#3;       || existencia inicio
    #4#40 = campo40;     || compras
    #4#47 = #7#41;       || agraria S/N
    #4#44 = #7#11;       || Codigo Provincia
    #4#45 = #7#12;       || Codigo Poblacion
    #4#46 = #7#9;        || Domicilio

|HAZ emprcoef3;          || CALCULA ...
|GRABA 020#4n;
|SI FSalida=0; npro=10; empresa=#0#3; |HAZ sigrabo; |FINSI;      || marca (*)
|CIERRA #4;

HContribu  = #4#0;
HNombre    = Nombre;
HPrograma  = "(reemprco) EMPRESARIALES COEFICIENTE";
HLinea     = #4#1;
HPerceptor = #4#3;
HRdto      = #4#32;
HTipo      = "ALTA FICHA DESDE PASE REO";
|HAZ AltaHistorico;
|FPRC;

|PROCESO ultimo4;
    #4#0 = #0#3;
    #4#1 = 99;
|LEE 001#4];
|SI FSalida = 0;
    |LEE 001#4a;
|SINO;
    |LEE 001#4u;
|FINSI;
|SI FSalida = 0; |Y #4#0 = #0#3;
        ultima = #4#1 + 1;
|SINO;
        ultima = 1;
|FINSI;
|FPRC;

|PROCESO emprcoef1;
|HAZ comun;
|SI grabalo ! 0;
    |HAZ emprcoef2;
|FINSI;
|FPRC;

     *********************************************************************
     GRABACION DE LOS AGRICOLAS
     *********************************************************************

|PROCESO GrabaLinea;
  grabalo = 1;

  |LIBERA #41;
  |LEE 140#41=;
  |SI FSalida = 0;
     #40#37 = #40#37 - #41#7;
     #40#19 = #40#19 - #41#8;

     #41#7 = #41#7 + Total;
     #41#8 = #41#7 * #41#9;
     |GRABA 140#41a;
  |SINO;
     #41#3 = Producto;
     #41#4 = _Agri;
     #41#5 = TipoT;
     #41#6 = "S";
     #41#7 = Total;
     #41#9 = Porce;
     #41#8 = #41#7 * #41#9;
     |GRABA 140#41n;
  |FINSI;

  #40#37 = #40#37 + #41#7;
  #40#19 = #40#19 + #41#8;
|FPRC;

|PROCESO LeeConce;
  |SI Bas = 0; |Y _Iv = 0; |Y Rec = 0; |Y Ret = 0; |ACABA; |FINSI;

  #28#0 = Con;
  |LEE 040#28=;
  |SI FSalida ! 0;    |ACABA;  |FINSI;

  RCatastral = "";
  |SI EsArrend = 1;
      |SI #28#9 = 1; _Ingresos = _Ingresos + Bas;               |FINSI;
      |SI #28#9 = 2; _Ingresos = _Ingresos + (Bas + _Iv + Rec); |FINSI;

      |ABRE #30;
      #30#0 = #12#0;
      #30#1 = #12#14;
      #30#2 = LineaB;
      #30#3 = #12#1;
      |LEE 040#30=;
      |SI FSalida = 0;
          |ABRE #29;
          #29#0 = #30#0;
          #29#1 = #30#4;
          |LEE 040#29=;
          |SI FSalida ! 0;  |DDEFECTO #29;  |FINSI;
          |CIERRA #29;
          RCatastral = #29#5;
      |FINSI;
      |CIERRA #30;
      |ACABA;
  |FINSI;

  |SI #28#3   ! "I";  |ACABA;  |FINSI;

  #27#0 = Con;
  |LEE 040#27=;
  |SI FSalida ! 0; |ACABA; |FINSI;

  |SI #28#9 = 1; Total  = Bas;                  |FINSI;
  |SI #28#9 = 2; Total  = Bas + _Iv + Rec; |FINSI;
  Retencion = Retencion + Ret;

  |DDEFECTO #41;
  |SI #27#7 = .12;
      #41#0    = #40#0;
      #41#1    = #40#1;
      #41#2    = 1;
      Producto = 1;
      _Agri    = "";
      TipoT    = "";
      Porce    = .12;
      |HAZ GrabaLinea;
  |FINSI;

  |SI #27#7 = .24;
      #41#0    = #40#0;
      #41#1    = #40#1;
      #41#2    = 2;
      Producto = 2;
      _Agri    = "A";
      TipoT    = "P";
      Porce    = .24;
      |HAZ GrabaLinea;
  |FINSI;

  |SI #27#7 = .216;
      #41#0    = #40#0;
      #41#1    = #40#1;
      #41#2    = 3;
      Producto = 2;
      _Agri    = "A";
      TipoT    = "A";
      Porce    = .216;
      |HAZ GrabaLinea;
  |FINSI;

  |SI #27#7 = .30;
      #41#0    = #40#0;
      #41#1    = #40#1;
      #41#2    = 4;
      Producto = 3;
      _Agri    = "A";
      TipoT    = "P";
      Porce    = .30;
      |HAZ GrabaLinea;
  |FINSI;

  |SI #27#7 = .27;
      #41#0    = #40#0;
      #41#1    = #40#1;
      #41#2    = 5;
      Producto = 3;
      _Agri    = "A";
      TipoT    = "A";
      Porce    = .27;
      |HAZ GrabaLinea;
  |FINSI;

  |SI #27#7 = .35;
      #41#0    = #40#0;
      #41#1    = #40#1;
      #41#2    = 6;
      Producto = 4;
      _Agri    = "A";
      TipoT    = "P";
      Porce    = .35;
      |HAZ GrabaLinea;
  |FINSI;

  |SI #27#7 = .315;
      #41#0    = #40#0;
      #41#1    = #40#1;
      #41#2    = 7;
      Producto = 4;
      _Agri    = "A";
      TipoT    = "A";
      Porce    = .315;
      |HAZ GrabaLinea;
  |FINSI;

  |SI #27#7 = .40;
      #41#0    = #40#0;
      #41#1    = #40#1;
      #41#2    = 8;
      Producto = 5;
      _Agri    = "A";
      TipoT    = "P";
      Porce    = .40;
      |HAZ GrabaLinea;
  |FINSI;

  |SI #27#7 = .36;
      #41#0    = #40#0;
      #41#1    = #40#1;
      #41#2    = 9;
      Producto = 5;
      _Agri    = "A";
      TipoT    = "A";
      Porce    = .36;
      |HAZ GrabaLinea;
  |FINSI;

  |SI #27#7 = .55;
      #41#0    = #40#0;
      #41#1    = #40#1;
      #41#2    = 10;
      Producto = 6;
      _Agri    = "";
      TipoT    = "";
      Porce    = .55;
      |HAZ GrabaLinea;
  |FINSI;

|FPRC;

|PROCESO PonCoef;
  Con = #12#5;  Bas = #12#07; _Iv = #12#09; Rec = #12#11; Ret = #12#42; LineaB = 1; |HAZ LeeConce;
  Con = #12#33; Bas = #12#23; _Iv = #12#27; Rec = #12#31; Ret = #12#43; LineaB = 2; |HAZ LeeConce;
  Con = #12#34; Bas = #12#24; _Iv = #12#28; Rec = #12#32; Ret = #12#44; LineaB = 3; |HAZ LeeConce;
  Con = #12#60; Bas = #12#45; _Iv = #12#51; Rec = #12#57; Ret = #12#69; LineaB = 4; |HAZ LeeConce;
  Con = #12#61; Bas = #12#46; _Iv = #12#52; Rec = #12#58; Ret = #12#70; LineaB = 5; |HAZ LeeConce;
  Con = #12#62; Bas = #12#47; _Iv = #12#53; Rec = #12#59; Ret = #12#71; LineaB = 6; |HAZ LeeConce;
|FPRC;

|DEFBUCLE eoslinve;
  #12#1;
  19;
  #0#1, 97,    0, #0#2;
  #0#1, 97, 9999, #0#2;
  e;
  NULL, PonCoef;
|FIN;

|PROCESO LeeConceC;
  |SI Bas = 0; |Y _Iv = 0; |Y Rec = 0; |ACABA; |FINSI;

  #43#0 = Con;
  |LEE 040#43=;
  |SI FSalida ! 0; |ACABA; |FINSI;

  #28#0 = Con;
  |LEE 040#28=;
  |SI FSalida ! 0;    |ACABA;  |FINSI;

  Total = 0;
  |SI #28#9 = 1; Total = Bas;                  |FINSI;
  |SI #28#9 = 2; Total = Bas + _Iv + Rec; |FINSI;

  |SI #43#3 = 1;  Campo01 = Campo01 + Total;    |FINSI;
  |SI #43#3 = 2;  Campo02 = Campo02 + Total;    |FINSI;
  |SI #43#3 = 3;  Campo03 = Campo03 + Total;    |FINSI;
  |SI #43#3 = 4;  Campo04 = Campo04 + Total;    |FINSI;
  |SI #43#3 = 5;  Campo05 = Campo05 + Total;    |FINSI;
|FPRC;

|PROCESO PonArrend;
  Con = #14#5;  Bas = #14#07; _Iv = #14#09; Rec = #14#11; |HAZ LeeConceC;
  Con = #14#33; Bas = #14#23; _Iv = #14#27; Rec = #14#31; |HAZ LeeConceC;
  Con = #14#34; Bas = #14#24; _Iv = #14#28; Rec = #14#32; |HAZ LeeConceC;
|FPRC;

|DEFBUCLE eoslinco;
  #14#1;
  19;
  #0#1, 97,    0, #0#2;
  #0#1, 97, 9999, #0#2;
  e;
  NULL, PonArrend;
|FIN;

|PROCESO PonLineas;
  |ABRE #27;
  |ABRE #28;
  |ABRE #41;
  |HAZ re_ventas;
  |BUCLE eoslinve;
  |CIERRA #27;
  |CIERRA #28;
  |CIERRA #41;
|FPRC;

|PROCESO CalcuAgri;
  #40#24 = #40#19;

  |SI #40#21 = "S";
      #40#24 = #40#24 * 0.75;
  |SINO;
      #40#22 = (#40#20 / #40#37) * 100;
      |SI #40#22 ! 0;
          Porce = 1;
          |SI #40#22 ] 10; Porce = 0.90; |FINSI;
          |SI #40#22 ] 20; Porce = 0.85; |FINSI;
          |SI #40#22 ] 30; Porce = 0.80; |FINSI;
          |SI #40#22 ] 40; Porce = 0.75; |FINSI;

          #40#24 = #40#24 * Porce;
      |FINSI;
  |FINSI;

  |SI #40#23 = "S"; #40#24 = #40#24 * 0.8; |FINSI;

  #40#27 = #40#24 + #40#33 - #40#25 - #40#26;
  #40#34 = #40#27;
                               Dto1 = (#40#34 % #40#28) ! 0;
  Dto2  = 0;  |SI #40#29 = "S"; Dto2 = (#40#34 % 20)    ! 0;  |FINSI;
  Dto3  = 0;  |SI #40#30 = "S"; Dto3 = (#40#34 % 25)    ! 0;  |FINSI;
  #40#34 = #40#34 - Dto1 - Dto2 - Dto3;
  #40#34 = #40#34 + #40#31 - #40#32;
|FPRC;

|PROCESO GrabaRetencion;
  #25#0 = #0#3;
  #25#1 = linea_p;
  |LIBERA #25;
  |LEE 140#25=;
  FEstado = FSalida;
  |SI FEstado ! 0; |DDEFECTO #25; |FINSI;

  #25#0  = #0#3;
  #25#1  = linea_p;
  #25#2  = #0#4;
  #25#3  = perc;
  #25#4  = 0;
  #25#10 = Retencion;

  |SI FEstado = 0; |GRABA 140#25a; |SINO; |GRABA 140#25n; |FINSI;

  npro = 51;  empresa = #0#3;  |HAZ sigrabo;
  HContribu  = #25#0;
  HNombre    = Nombre;
  HPrograma  = "(repagret) RETENCIONES Y PAGOS A CUENTA";
  HLinea     = #25#1;
  HPerceptor = #25#3;
  HRdto      = #25#5 + #25#6 + #25#7 + #25#8;
  HTipo      = "ALTA FICHA DESDE PASE REO";
  |HAZ AltaHistorico;
|FPRC;

|PROCESO Agricolas;
  |ABRE #40;
  |HAZ ultimo40;
  |DDEFECTO #40;
  #40#00 = #0#3;        || codigo
  #40#01 = ultima;      || linea
  #40#02 = tipo;        || tipo
  #40#03 = perc;        || perceptor
  #40#13 = #7#4;        || epigrafe -1-
  #40#14 = #7#5;        || epigrafe -2-
  #40#15 = #7#6;        || nombre actividad
  #40#16 = #7#9;        || domicilio

  |HAZ PonLineas;

  |SI grabalo = 0;  |CIERRA #40;  |ACABA;  |FINSI;

  |HAZ CalcuAgri;
  |GRABA 020#40n;
  |SI FSalida=0; npro=52; empresa=#0#3; |HAZ sigrabo; |FINSI;      || marca (*)
  |CIERRA #40;

  HContribu  = #40#0;
  HNombre    = Nombre;
  HPrograma  = "(reempagr) AGRICOLAS Y GANADERAS";
  HLinea     = #40#1;
  HPerceptor = #40#3;
  HRdto      = #40#34;
  HTipo      = "ALTA FICHA DESDE PASE REO";
  |HAZ AltaHistorico;

  |SI Retencion = 0; |ACABA; |FINSI;

  linea_p = 0;
  LoHago  = 0; |BUCLE repagret;
  |SI LoHago = 0;  linea_p = linea_p + 1;  |FINSI;

  |ABRE #25;
  |HAZ GrabaRetencion;
  |CIERRA #25;
|FPRC;

|PROCESO ultimo40;
  #40#0 = #0#3;
  #40#1 = 99;
  |LEE 001#40];
  |SI FSalida = 0;
      |LEE 001#40a;
  |SINO;
      |LEE 001#40u;
  |FINSI;
  |SI FSalida = 0; |Y #40#0 = #0#3;
      ultima = #40#1 + 1;
  |SINO;
      ultima = 1;
  |FINSI;
|FPRC;

|PROCESO Arrendamientos;
   |ABRE #27;
   |ABRE #28;
   |ABRE #43;
   |ABRE #42;
   #42#00 = #0#3;        || codigo
   #42#01 = LineaArre;   || linea
   |BORRA 140#42c;
   |LIBERA #42;

   |HAZ re_ventas;    |BUCLE eoslinve;
   |HAZ re_compras;   |BUCLE eoslinco;

   |DDEFECTO #42;
   grabalo = 1;
    #42#00 = #0#3;        || codigo
    #42#01 = LineaArre;   || linea
    #42#02 = tipo;        || tipo
    #42#03 = perc;        || perceptor
    #42#4  = "U";         || Rustico o Urbano
    #42#5  = "ARRENDAMIENTOS";
    #42#13 = "O";
    #42#21 = "S";
    #42#22 = "N";
    #42#23 = _Ingresos;
    #42#26 = Campo02;
    #42#27 = Campo05;
    #42#28 = Campo01;
    #42#29 = Campo03;
    #42#30 = Campo04;
    #42#47 = #42#23 - (#42#26 + #42#27 + #42#28 + #42#29 + #42#30);
    #42#72 = RCatastral;

    |GRABA 020#42n;
    |SI FSalida=0; npro=2; empresa=#0#3; |HAZ sigrabo; |FINSI;      || marca (*)
    |CIERRA #42;
    |CIERRA #43;
    |CIERRA #27;
    |CIERRA #28;

    HContribu  = #42#0;
    HNombre    = Nombre;
    HPrograma  = "(recapinm) CAPITAL INMOBILIARIO";
    HLinea     = #42#1;
    HPerceptor = #42#3;
    HRdto      = #42#47;
    HTipo      = "ALTA FICHA DESDE PASE REO";
    |HAZ AltaHistorico;
|FPRC;

     *********************************************************************
     LLAMADAS DESDE CAMPOS
     *********************************************************************

|PROCESO que_matriz;
campo_e  = #16#0;
campo_a  = #16#1;
|FPRC;

|DEFBUCLE eoscomul_2;
#16#2;
;
#0#5, #0#6, 97;
#0#5, #0#6, 97;
e;
NULL, que_matriz;
|FIN;

|PROGRAMA;
|HAZ path;

|SI empre_r = 0;
    |CLS;
    |CABEZA "Captura R.E.O.";
    |DDEFECTO #0;
    |PINPA #0#0;
    |PINDA #0#0;
    |ENDATOS #1#0;
    |SI FSalida = 0; |HAZ tipo_3; |FINSI;
|SINO;
    |ABRE #7;
    #7#0 = empre_e;
    #7#1 = activ_e;
    #7#2 = 97;
    |LEE 001#7=;
    |SI FSalida ! 0; |CIERRA #7; |ACABA; |FINSI;
    |CIERRA #7;

    #0#0 = "SI";
    #0#1 = empre_e;
    #0#2 = activ_e;
    #0#3 = empre_r;
    #0#4 = tipo_e;
    #0#5 = 0;
    #0#6 = 0;
    |PINPA #0#0;
    |PINDA #0#0;

    |ABRE #11;
    #11#0 = #0#3;
    |LEE 040#11=;
    |SI FSalida ! 0; |DDEFECTO #11; |FINSI;
    |CIERRA #11;
    Nombre = #11#1;

    |SI #7#46 = "S";
        |PUSHV 2017  2366;
        |CUADRO 2017 2366;
        |ATRI R;
        |PINTA 2118, "  La Actividad es una Comunidad de Bienes intro-";
        |PINTA 2218, "duzca en la ficha general el codigo del comunero";
        |ATRI 0;
        |PAUSA;
        |POPV;
        |ACABA;
    |FINSI;

    |SI #7#14 = "S";
        #0#5    = #7#0;
        #0#6    = #7#1;
        campo_e = 0;
        campo_a = 0;
        |BUCLE eoscomul_2;
        #0#1    = campo_e;
        #0#2    = campo_a;
    |FINSI;

    |HAZ lee_eos1;
    |HAZ lee_eos2;
    |HAZ lee_renta1;
    |HAZ lee_renta2;
    |HAZ mira_comu;
    |PINDA #0#0;
    |HAZ tipo_3;
|FINSI;
|FPRO;
