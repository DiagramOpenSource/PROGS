|FICHEROS;
     zaccesox #100;
     zw000001 #101,MANTE;
     reacces1 #0;
     reparame #1;
     retabla1 #401;
     retabla2 #402;
|FIN;

|VARIABLES;
     nRango          = 0;
     nGrid           = 0;
     nCampo          = 0;
     nPanta100       = 0;
     nVent           = 0;
     nHandle         = 0;
     nError          = 0;

     nBotonAlta      = 0;
     nBotonModi      = 0;
     nBotonBaja      = 0;
     nBotonCons      = 0;
     nBotonReco      = 0;
     nBotonAnot      = 0;
     nBotonExpo      = 0;
     nBotonImpo      = 0;
     nBotonEmis      = 0;
     nBotonGad       = 0;
     nBotonMCli      = 0;
     nBotonDFam      = 0;
     nBotonDUsu      = 0;
     nBotonActi      = 0;
     nBotonDesa      = 0;
     nBotonCont      = 0;

     aAlfa           = "";
     aEsc1           = "";
     aEsc2           = "";
     aRetu           = "";
     aTecla          = "";
     aBoton          = "";
     aLanza          = "";
     aUsuario1       = "";
     aUsuario2       = "";
     aBase           = "";
     aParametro      = "";

     {-1}aVent       = "";
         aVent1      = 0;
         aVent2      = 0;
         aVent3      = 0;
         aVent4      = 0;
         aVent5      = "";

     &enError        = 0;
     &enCodCli       = 0;
|FIN;

|PROCESO Lanzador;
     aLanza = "*:ren2010/zacceso9;" + (("00000" + #0#0) % -105) + aBoton;
     |SUB_EJECUTA aLanza;

     |REFRESCACONTROL nGrid;
     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO BotonAlta;
     |VENTANA 0501, 1162, -1, 16, "Nuevo c¢digo de renta";
     nVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |DDEFECTO #101;
     |PINPA #0#101;
     |PINDA #0#101;
     |ENDATOS #1#101;
     |SI FSalida ! 0;
         aVent1 = -1;
         aVent2 = -1;
         aVent3 = 0;
         aVent4 = nVent;
         aVent5 = "MODELESS";
         |ESPECIFICA "ESTADO_VENTANA", aVent;

         |FINVENTANA nVent;

         |ACABA;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVent;

     |DDEFECTO #0;
     #0#0   = #101#0;
     #0#1   = #101#1;
     #0#2   = #101#2;
     |GRABA 020#0n;
     |LIBERA #0;

     aBoton = "ALTA";
     |HAZ Lanzador;

     enCodCli = #101#0;
     |HAZ LeePersona;
     |SI enError = 1;
         #0#0   = #101#0;
         |LEE 101#0=;
         |SI FSalida = 0;
             |BORRA 020#0a;
             |LIBERA #0;
         |FINSI;
     |FINSI;

     |SELECT #1#0;
     #0#0   = #101#0;
     |LEE 000#0=;

     |REFRESCACONTROL nGrid;
     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO BotonModifica;
     aBoton = "MODIFICAR";
     |HAZ Lanzador;
|FPRC;

|PROCESO BotonBaja;
     aBoton = "BAJA";
     |HAZ Lanzador;
|FPRC;

|PROCESO BotonConsulta;
     aBoton = "CONSULTAR";
     |HAZ Lanzador;
|FPRC;

|PROCESO BotonRecoger;
     aBoton = "RECOGER";
     |HAZ Lanzador;
|FPRC;

|PROCESO BotonAnotacion;
     aAlfa    = "rem01005;" + (("00000" + #0#0) % -105);
     |SUB_EJECUTA_NP aAlfa;
|FPRC;

|PROCESO BotonExportacion;
     aBoton = "EXPORTAR";
     |HAZ Lanzador;
|FPRC;

|PROCESO BotonImportacion;
     aBoton = "IMPORTAR";
     |HAZ Lanzador;
|FPRC;

|PROCESO BotonEmision;
     aBoton = "EMISION";
     |HAZ Lanzador;
|FPRC;

|PROCESO BotonGAD;
     aAlfa = (("00000" + #0#0) % -105) + "2010" + "99" + "100" + "00";
     aAlfa = aAlfa + #0#1;
     aAlfa = ":dsarchi/dsarz017;" + aAlfa;
     |SUB_EJECUTA aAlfa;

     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO BotonModCliente;
     aBoton = "MODCLIENTE";
     |HAZ Lanzador;
|FPRC;

|PROCESO BotonDFamiliares;
     aBoton = "DFAMILIAR";
     |HAZ Lanzador;
|FPRC;

|PROCESO BotonDesacUsuario;
     aUsuario1 = #0#16;  |QBF aUsuario1;
     aUsuario2 = Usuario % 810;  |QBF aUsuario2;

     |SI aUsuario1 = "";
         |MENAV "0000 Renta sin Usuario.";
         |ACABA;
     |FINSI;

     |SI aUsuario1 ! aUsuario2;
         |MENAV "0000 Usted no tiene permiso para Desactivar el Usuario de esta Renta.";
         |ACABA;
     |FINSI;

     |CONFI "0000NSeguro que quiere desactivar el usuario de esta Renta ";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SELECT #1#0;
     |LEE 101#0=;
     |SI FSalida = 0;
         #0#16 = "";  |PINTA #0#16;
         |GRABA 020#0a;
         |LIBERA #0;
     |FINSI;

     |REFRESCACONTROL nGrid;
     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO BotonContador;
     aBoton = "CONTADOR";
     |HAZ Lanzador;
|FPRC;

|PROCESO BotonDesac2011;
     |SI #0#19 = "N";
         |CONFI "0000NSeguro que quiere DESACTIVAR la renta para el pase del 2010.";
     |SINO;
         |CONFI "0000NSeguro que quiere ACTIVAR la renta para el pase del 2010.";
     |FINSI;

     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SELECT #1#0;
     |LEE 101#0=;
     |SI FSalida = 0;
         |SI #0#19 = "N";
             #0#20 = "215,120,210;255,255,255";
             #0#19 = "S";
         |SINO;
             #0#20 = "";
             #0#19 = "N";
         |FINSI;
         |GRABA 020#0a;
         |LIBERA #0;
     |FINSI;

     |SI #0#19 = "N";
         |ESTADO_CONTROL nBotonDesa, "SHOW";
         |ESTADO_CONTROL nBotonActi, "HIDE";
     |SINO;
         |ESTADO_CONTROL nBotonDesa, "HIDE";
         |ESTADO_CONTROL nBotonActi, "SHOW";
     |FINSI;

     |REFRESCACONTROL nGrid;
     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO ActivaDesactiva;
     |SI #0#12 = "*";
         |ESTADO_CONTROL nBotonReco, "DISABLE";

         |SI #0#14 = "INACTIVO  ";
             |ESTADO_CONTROL nBotonEmis, "DISABLE";
         |SINO;
             |ESTADO_CONTROL nBotonEmis, "ENABLE";
         |FINSI;
     |SINO;
         |ESTADO_CONTROL nBotonReco, "ENABLE";
         |ESTADO_CONTROL nBotonEmis, "DISABLE";
     |FINSI;

     aUsuario1 = #0#16;  |QBF aUsuario1;
     aUsuario2 = Usuario % 810;  |QBF aUsuario2;

     |SI aUsuario1 = aUsuario2;
         |ESTADO_CONTROL nBotonDUsu, "ENABLE";
     |SINO;
         |ESTADO_CONTROL nBotonDUsu, "DISABLE";
     |FINSI;

     |DBASS aBase;  |QBF aBase;
     aAlfa = aBase + "dsarchi/def/dsarz017.mas";
     |XABRE "E", aAlfa, nHandle;
     |SI FSalida ] 0;
         |ESTADO_CONTROL nBotonGad, "ENABLE";
     |SINO;
         |ESTADO_CONTROL nBotonGad, "DISABLE";
     |FINSI;

     |SI #0#19 = "N";
         |ESTADO_CONTROL nBotonDesa, "SHOW";
         |ESTADO_CONTROL nBotonActi, "HIDE";
     |SINO;
         |ESTADO_CONTROL nBotonDesa, "HIDE";
         |ESTADO_CONTROL nBotonActi, "SHOW";
     |FINSI;
|FPRC;

|PROCESO EventoGrid;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#0=;
         |SI FSalida ! 0;
             |ACABA;
         |FINSI;

         |PARA nCampo = 0;  |SI nCampo [ 19;  |HACIENDO nCampo = nCampo + 1;
               #100nCampo = #0nCampo;
               |SI #0nCampo = "*";  #100nCampo = "S";  |FINSI;
               |SI #0nCampo = " ";  #100nCampo = "N";  |FINSI;

               |C_V #100nCampo, 0, aAlfa;
               |SI aAlfa = "S";
                   |PINTA #100nCampo;
               |FINSI;
         |SIGUE;

         |HAZ ActivaDesactiva;
     |FINSI;

     |SI FSalida = 4;
         |HAZ BotonModifica;
     |FINSI;
|FPRC;

|PROCESO Botones;
     |CONTROL_SIMPLE nPanta100, "BOTON,{{193}}Alta ", 2739, 2749, BotonAlta;
     nBotonAlta = FSalida;

     |CONTROL_SIMPLE nPanta100, "BOTON,{{197}}Modificar ", 2751, 2761, BotonModifica;
     nBotonModi = FSalida;

     |CONTROL_SIMPLE nPanta100, "BOTON,{{199}}Baja ", 2763, 2773, BotonBaja;
     nBotonBaja = FSalida;

     |CONTROL_SIMPLE nPanta100, "BOTON,{{199}}Consulta", 2727, 2737, BotonConsulta;
     nBotonCons = FSalida;

     |CONTROL_SIMPLE nPanta100, "BOTON,Recoger 2009", 2702, 2712, BotonRecoger;
     nBotonReco = FSalida;

     |CONTROL_SIMPLE nPanta100, "BOTON,Anotaciones", 2714, 2725, BotonAnotacion;
     nBotonAnot = FSalida;

     |CONTROL_SIMPLE nPanta100, "BOTON,Mod.Cliente", 2276, 2286, BotonModCliente;
     nBotonMCli = FSalida;

     |CONTROL_SIMPLE nPanta100, "BOTON,D.Familiares", 2288, 2298, BotonDFamiliares;
     nBotonDFam = FSalida;

     |CONTROL_SIMPLE nPanta100, "BOTON,Emisi¢n", 2376, 2386, BotonEmision;
     nBotonEmis = FSalida;

     |CONTROL_SIMPLE nPanta100, "BOTON,Consulta GAD", 2388, 2398, BotonGAD;
     nBotonGad  = FSalida;

     |CONTROL_SIMPLE nPanta100, "BOTON,Des.Usuario", 2476, 2486, BotonDesacUsuario;
     nBotonDUsu = FSalida;

     |CONTROL_SIMPLE nPanta100, "BOTON,Exportaci¢n", 2576, 2586, BotonExportacion;
     nBotonExpo = FSalida;

     |CONTROL_SIMPLE nPanta100, "BOTON,Importaci¢n", 2588, 2598, BotonImportacion;
     nBotonImpo = FSalida;

     |CONTROL_SIMPLE nPanta100, "BOTON,Contador", 2676, 2686, BotonContador;
     nBotonCont = FSalida;

     |CONTROL_SIMPLE nPanta100, "BOTON,Act.para 2011", 2488, 2498, BotonDesac2011;
     nBotonActi = FSalida;

     |ESTADO_CONTROL nBotonActi, "HIDE";

     |CONTROL_SIMPLE nPanta100, "BOTON,Des.para 2011", 2488, 2498, BotonDesac2011;
     nBotonDesa = FSalida;
|FPRC;

|PROGRAMA;
     |CLS;
     |CABEZA "Renta 2010";

     |SUB_EJECUTA_NP "rezcalve;AUTOMATICO";
     |SI enError = 1;   |ACABA;  |FINSI;

     |ABRE #1;
     |LEE 000#1p;
     |SI FSalida ! 0;  |DDEFECTO #1;  |FINSI;
     |CIERRA #1;

     |PINPA #0#100;
     nPanta100 = FSalida;

     aAlfa = #1#20;
     |C_V #100#16, 1, aAlfa;

     |SI aAlfa = "N";
         |CONTROL_SIMPLE nPanta100, "LABEL,{{12}}Desactivado", 0588, 0598, NULL;
     |FINSI;

     |HAZ Botones;

     nRango = 2 + 4 + 8 + 16 + 32 + 128;
     |LINEAL_SIMPLE #1#0, nRango, 0702, 2673, NULL, NULL, EventoGrid;
     nGrid = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
           FSalida = "::{{001}}Salir," + nGrid;
           |LEETECLA aTecla;
           |SI aTecla = aEsc1; |SAL;  |FINSI;
           |SI aTecla = aEsc2; |SAL;  |FINSI;
           |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;
|FPRO;

|PROCESO Tipo80;  |TIPO 80;
     aParametro = PARAMETRO;

     |ABRET #0;

     FSalida = 2699;
|FPRC;

|PROCESO Tipo90;  |TIPO 90;
     |CIERRAT #0;

     |SI aParametro ! "PORMENURENTA";
         |DBASS aBase;
         aBase = aBase + "integral/def/dsaz9999.mas";
         |XABRE "E", aBase, nHandle;
         |SI FSalida ] 0;
             |SUB_EJECUTA_NP "|:integral/dsaz9999";
         |FINSI;
     |FINSI;
|FPRC;
