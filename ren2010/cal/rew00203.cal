|FICHEROS;
     rew00203 #203;
     recofisc #2;
|FIN;

|VARIABLES;
     nCampo      = 0;
     nLCod       = 0;
     nLDes       = 0;
     nCalc       = 0;

     aAlfa       = "";
     aAlfa1      = "";
     aAlfa2      = "";

     &eaTabla    = "";
     &eaDestino  = "";
     &eaAlfa     = "";
     &enPosicion = 0;
     &enComp     = 0;
     &enError    = 0;
|FIN;

|PROCESO Tipo12Fw203;  |TIPO 12;
|FPRC;

|PROCESO CampoObligatorio; |TIPO 6;
     |C_M #203Campo, 0, aAlfa;
     |SI aAlfa = "N";  |ACABA;  |FINSI;

     aAlfa = #203Campo;  |QBF aAlfa;

     |SI aAlfa = "";
         |MENAV "0000Contenido obligatorio. ";
         |ERROR;
         |ACABA;
     |FINSI;
|FPRC;

|PROCESO Tipo0C3Fw203; |TIPO 0;
     |C_M #203#3, 0, aAlfa;
     |SI aAlfa = "N";  |ACABA;  |FINSI;

     |SI #203#3 ! "NUM";  |Y #203#3 ! "KM.";  |Y #203#3 ! "S/N";
      |Y #203#3 ! "OTR";  |Y #203#3 ! "   ";
         |MENAV "0000Contenido incorrecto.";
         |ERROR;
         |ACABA;
     |FINSI;

     |C_M #203#4, 1, "S";
     |SI #203#3 = "S/N";
         |C_M #203#4, 1, "N";   #203#4 = "0000";  |PINTA #203#4;
     |FINSI;
|FPRC;

|PROCESO Tipo7C1Fw203; |TIPO 7;
     |C_M #203#1, 0, aAlfa;
     |SI aAlfa = "N";  |ACABA;  |FINSI;

     eaTabla = "zsiglas.txt";
     nLCod   = 5;
     nLDes   = 15;
     |HAZ CargaTabla;
|FPRC;

|PROCESO Tipo0C1Fw203;
     |C_M #203#1, 0, aAlfa;
     |SI aAlfa = "N";  |ACABA;  |FINSI;

     nCampo   = Campo + 1;

     |SI FSalida = 10;
         enPosicion = 20;
         |HAZ SacaTabla;
         nCalc = 100 + nLCod;
         aAlfa1   = eaDestino % nCalc;
         #203Campo  = aAlfa1; |PINTA #203Campo;
     |FINSI;

     |SI #203Campo = "     ";
         |MENAV "0000 Contenido Incorrecto. Consulta la Tabla con F2";
         |ERROR; |ACABA;
     |FINSI;

     enComp = nLCod;
     aAlfa  = "|C" + (("000" + Campo) % -103) + "TIPO";
     aAlfa1 = #203#aAlfa#;
     aAlfa  = #203Campo;   |QBF aAlfa;

     |SI aAlfa1 = "N";
         nCalc = - (100 + nLCod);
         aAlfa = (("0" * enComp) + aAlfa) % nCalc;
     |FINSI;

     |SI aAlfa1 ! "N";
         nCalc = 100 + nLCod;
         aAlfa = (aAlfa + (" " * enComp)) % nCalc;
     |FINSI;
     eaAlfa = aAlfa;
     |HAZ CompruebaTabla;

     |SI enError = 1;
         |MENAV "0000 Contenido Incorrecto. Consulta la Tabla con F2";
         |ERROR; |ACABA;
     |FINSI;

     nCalc      = 100 + nLCod;
     aAlfa1     = eaDestino % nCalc;
     nCalc      = ((nLCod + 2) * 100) + nLDes;
     aAlfa2     = eaDestino % nCalc;
     #203Campo  = aAlfa1;

     |PINTA #203Campo;

     |HAZ FinTabla;
|FPRC;

|PROCESO Tipo0C0Fw203;  |TIPO 0;
     |PARA nCampo = 1;  |SI nCampo [ 27;  |HACIENDO nCampo = nCampo + 1;
           |C_M #203nCampo, 1, "S";
     |SIGUE;
     |C_M #203#16, 1, "N";
     |C_M #203#26, 1, "N";

     |SI #203#0 = "N";
         |PARA nCampo = 17;  |SI nCampo [ 24;  |HACIENDO nCampo = nCampo + 1;
               |C_M #203nCampo, 1, "N";  #203nCampo = "";  |PINTA #203nCampo;
         |SIGUE;
     |SINO;
         aAlfa1 = #203#2;   |QBF aAlfa1;
         aAlfa2 = #203#17;  |QBF aAlfa2;

         |SI aAlfa2 = "";
             |SI aAlfa1 ! "";
                 aAlfa = #203#1;  |QBF aAlfa;
                 |SI aAlfa ! "";  aAlfa = aAlfa + " ";  |FINSI;
                 aAlfa = aAlfa + #203#2;   |QBF aAlfa;
                 |SI #203#4  ! "    ";  aAlfa = aAlfa + " " + #203#4;   |FINSI;
                 |SI #203#8  ! "   ";   aAlfa = aAlfa + " " + #203#8;   |FINSI;
                 |SI #203#9  ! "  ";    aAlfa = aAlfa + " " + #203#9;   |FINSI;
                 |SI #203#10 ! "  ";    aAlfa = aAlfa + " " + #203#10;  |FINSI;

                 #203#17 = aAlfa;    |PINTA #203#17;
                 #203#18 = #203#11;  |PINTA #203#18;
                 #203#19 = #203#15;  |PINTA #203#19;
                 #203#21 = (("00" + #203#13) % -102) + (("000" + #203#14) % -103);  |PINTA #203#21;
                 #203#22 = #203#16;  |PINTA #203#22;
             |FINSI;
         |FINSI;

         |PARA nCampo = 1;  |SI nCampo [ 16;  |HACIENDO nCampo = nCampo + 1;
               |C_M #203nCampo, 1, "N";  #203nCampo = "";  |PINTA #203nCampo;
         |SIGUE;

         |C_M #203#27, 1, "N";  #203#27 = "";  |PINTA #203#27;
     |FINSI;
|FPRC;

|PROCESO Tipo0C13Fw203;
||                        |C_M #203#25, 1, "N";
||     |SI #203#0 = "S";  |C_M #203#25, 1, "S";  |ACABA;  |FINSI;
     |SI #203#0 = "S";  |ACABA;  |FINSI;

     |ABRE #2;
     #2#0 = #203#13;
     |LEE 040#2=;
     |SI FSalida ! 0;  |DDEFECTO #2;  |FINSI;
     |CIERRA #2;

     #203#25 = #2#3;
     #203#26 = #2#4;

     |PINTA #203#25;
     |PINTA #203#26;
|FPRC;
