|FICHEROS;
     rem00019 #19;
     rem00020 #20;
     rem00053 #53;
     retabla1 #401;
     rem99999 #666;
     rew00600 #1003,MANTE;
|FIN;

|VARIABLES;
     aPathRenta    = "";
     aDef          = "";
     aAlfa         = "";
     aEscape       = "";
     aAvance       = "";
     aRetroceso    = "";
     aQueQuiero    = "";
     aNumCampos    = "";
     aNumPanta     = "";

     aCampoCambia1 = "";
     nCampoCambia1 = 0;

     nPanta        = 0;
     nNumCampos    = 0;
     nNumPanta     = 0;
     nCampos       = 0;
     nModo         = 0;
     nEntrada      = 0;
     nFactor       = 0;
     nDesde        = 0;
     nLimite       = 0;
     nSw           = 0;
     nCampo1       = 0;
     nCampo2       = 0;
     nCampo3       = 0;
     nCampo4       = 0;
     nCampo5       = 0;

     &enEmpresa    = 0;
     &enModo       = 0;
     &eaDeclarante = "";
     &enComunes    = 0;
     &enMinusvalia = 0;
     &eaMinuAyuda  = "";
     &eaFallece    = "";
     &enSwLiquida  = 0;
     &enSwRetorno  = 0;
     &eaPrograma   = "";
     &eaPunto      = "";
     &enLinTrabajo = 0;
     &eaDeclaTraba = "";
|FIN;

|| ************************************************************************
|| PROCESOS DE CONSULTA DE PANTALLAS NORMALES
|| ************************************************************************

|PROCESO PintaPantalla;
     |SI nPanta < 0;   nPanta = 0;                         |FINSI;
     |SI nPanta = 0;   |PINPA #00#1003;  |PINDA #00#1003;  |FINSI;
     |SI nPanta = 1;   |PINPA #01#1003;  |PINDA #01#1003;  |FINSI;
|FPRC;

|PROCESO Tipo0C7w600;
     |SI #1003#6 > #1003#7;
          |MENAV "0000 ATENCION. Importe de retencion superior a la retribucion dineraria";
     |FINSI;
|FPRC;

|PROCESO Calculadora;
     |SI FSalida ! 13;  |ACABA;  |FINSI;

     |PUSHV 0501 2380;
     |CUADRO 1030 1672;
     |BLANCO 1131 1571;
     |PINTA 1131, "Seguridad Social ......:                ";
     |PINTA 1231, "Mutualidades ..........:                ";
     |PINTA 1331, "Colegios de Huerfanos .:                ";
     |PINTA 1431, "Montepios .............:                ";

     E_inf = -999999999.99;
     E_sup = 9999999999.99;

     nCampo1 = #1003Campo;
     nCampo2 = 0;
     nCampo3 = 0;
     nCampo4 = 0;

     |PINTA 1156, nCampo1;
     |PINTA 1256, nCampo2;
     |PINTA 1356, nCampo3;
     |PINTA 1456, nCampo4;

     |ET nCampo1;
         nCampo1 = 1156 ? 1;

     |ET nCampo2;
         nCampo2 = 1256 ? 1;
         |SI FSalida = 2;  |VETE nCampo1;  |FINSI;
         |SI FSalida = 1;  |VETE Abajo;    |FINSI;
         |SI FSalida = 7;  |VETE Abajo;    |FINSI;

     |ET nCampo3;
         nCampo3 = 1356 ? 1;
         |SI FSalida = 2;  |VETE nCampo2;  |FINSI;
         |SI FSalida = 1;  |VETE Abajo;    |FINSI;
         |SI FSalida = 7;  |VETE Abajo;    |FINSI;

     |ET nCampo4;
         nCampo4 = 1456 ? 1;
         |SI FSalida = 2;  |VETE nCampo3;  |FINSI;

     |ET Abajo;
         #1003Campo = nCampo1 + nCampo2 + nCampo3 + nCampo4;

     |POPV;

     |PINTA #1003Campo;
|FPRC;

|PROCESO Calculadora1;
     |SI FSalida ! 13;  |ACABA;  |FINSI;

     |PUSHV 0501 2380;
     |CUADRO 1030 1672;
     |BLANCO 1131 1571;

     |PINTA 1131, "Importe 1 .............:                ";
     |PINTA 1231, "Importe 2 .............:                ";
     |PINTA 1331, "Importe 3 .............:                ";
     |PINTA 1431, "Importe 4 .............:                ";
     |PINTA 1531, "Importe 5 .............:                ";

     E_inf = -999999999.99;
     E_sup = 9999999999.99;

     nCampo1 = #1003Campo;
     nCampo2 = 0;
     nCampo3 = 0;
     nCampo4 = 0;
     nCampo5 = 0;

     |ET ETCampo1;
         nCampo1 = 1156 ? 1;
         |SI FSalida = 1;  |VETE Abajo1;    |FINSI;
         |SI FSalida = 7;  |VETE Abajo1;    |FINSI;

     |ET ETCampo2;
         nCampo2 = 1256 ? 1;
         |SI FSalida = 2;  |VETE ETCampo1;  |FINSI;
         |SI FSalida = 1;  |VETE Abajo1;    |FINSI;
         |SI FSalida = 7;  |VETE Abajo1;    |FINSI;

     |ET ETCampo3;
         nCampo3 = 1356 ? 1;
         |SI FSalida = 2;  |VETE ETCampo2;  |FINSI;
         |SI FSalida = 1;  |VETE Abajo1;    |FINSI;
         |SI FSalida = 7;  |VETE Abajo1;    |FINSI;

     |ET ETCampo4;
         nCampo4 = 1456 ? 1;
         |SI FSalida = 2;  |VETE ETCampo3;  |FINSI;
         |SI FSalida = 1;  |VETE Abajo1;    |FINSI;
         |SI FSalida = 7;  |VETE Abajo1;    |FINSI;

     |ET ETCampo5;
         nCampo5 = 1556 ? 1;
         |SI FSalida = 2;  |VETE ETCampo4;  |FINSI;
         |SI FSalida = 1;  |VETE Abajo1;    |FINSI;
         |SI FSalida = 7;  |VETE Abajo1;    |FINSI;

     |ET Abajo1;
         #1003Campo = nCampo1 + nCampo2 + nCampo3 + nCampo4 + nCampo5;

     |POPV;

     |PINTA #1003Campo;
|FPRC;

|PROCESO Totalw1003;
     #1003#12 = #1003#9  + #1003#10 - #1003#11;

     |SI #1003#17 ! -1;
         |C_M #1003#18, 1, "N";
         #1003#18 = #1003#28 % #1003#17;
     |SINO;
         |C_M #1003#18, 1, "S";
     |FINSI;

     |SI #1003#27 ! -1;
         nLimite  = (#401#1 * #1003#27) % 40;
         |SI #1003#18 > nLimite;  #1003#18 = nLimite;  |FINSI;
     |FINSI;

     |SI #1003#21 > #401#2;  #1003#21 = #401#2;  |FINSI;
     |SI #1003#22 > #401#3;  #1003#22 = #401#3;  |FINSI;

     #1003#23 = #1003#19 + #1003#20 + #1003#21 + #1003#22;
     #1003#24 = #1003#7  + #1003#12 + #1003#34 + #1003#16 + #1003#28 - #1003#18 - #1003#23;
     #1003#36 = 0;

     |SI #1003#35 = "S";
         #1003#36 = #1003#24 % 65;
     |FINSI;

     #1003#37 = #1003#24 - #1003#36;

     |PINTA #1003#12;
     |PINTA #1003#16;
     |PINTA #1003#18;
     |PINTA #1003#21;
     |PINTA #1003#22;
     |PINTA #1003#23;
     |PINTA #1003#24;
     |PINTA #1003#25;
     |PINTA #1003#26;
     |PINTA #1003#36;
     |PINTA #1003#37;
|FPRC;

|PROCESO TraePensiones;
     |SI nSw = 0;
         #1003#13 = #53#11;
         #1003#16 = #1003#16 + #53#15;
         |SI #53#13 = "S";
             #1003#16 = #1003#16 + #53#18;
         |FINSI;

         #53#3 = #20#3;
         |SI #53#3 = "A";  #53#3 = "R";  |FINSI;
         |SI #53#3 = "P";  #53#3 = "R";  |FINSI;

         |GRABA 020#53a;
         |LIBERA #53;
     |SINO;
         |BORRA 020#53a;
         |LIBERA #53;

         eaPrograma   = "rem00053";
         eaPunto      = "  ";
         |HAZ ProgramaActivo;
     |FINSI;
|FPRC;

|DEFBUCLE rem00053;
     #53#1;
     21, 22;
     #20#0, 01, "S", #20#1;
     #20#0, 99, "S", #20#1;
     be;
     NULL, TraePensiones;
|FIN;

|PROCESO Pensiones;
     |SI FSalida = 10;
         enLinTrabajo = #1003#1;
         eaDeclaTraba = #1003#2;
         |SUB_EJECUTA_NP "rem00053";
     |FINSI;

     #1003#16 = 0;
     nSw      = 0;   |BUCLE rem00053;

     |PINTA #1003#13;
     |PINTA #1003#16;
|FPRC;

|PROCESO Irregular;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |C_M #1003#27, 1, "S";
     |C_M #1003#17, 1, "S";
     |SI #1003#28 = 0;
         |C_M #1003#27, 1, "N";  #1003#27 = 0;  |PINTA #1003#27;
         |C_M #1003#17, 1, "N";  #1003#17 = 0;  |PINTA #1003#17;
     |FINSI;
|FPRC;

|PROCESO Fantasma;
     |SI #20#3 = "X";  |Y nModo = 1;
         |ACABA;
     |FINSI;

     |PARA nCampos = nDesde;  |SI nCampos [ nNumCampos;  |HACIENDO nCampos = nCampos + 1;
           #1003nCampos = #20nCampos;
     |SIGUE;

     aEscape    = &255 + "806";
     aAvance    = &255 + "815";
     aRetroceso = &255 + "814";

     |SI nNumPanta ] 1;
         |MENSA "      Av.Pag Pantalla Siguiente. Re.Pag Pantalla Anterior. ESC Salir.";
     |SINO;
         |MENSA "      ESC Salir.";
     |FINSI;

     |PUSHV 0401 2380;
     |SI nModo ! 4;
         |PINPA #0#1003;
         |PINDA #0#1003;
         |ENDATOS #1#1003;
         |SI FSalida = 0;
             |PARA nCampos = nDesde;  |SI nCampos [ nNumCampos;  |HACIENDO nCampos = nCampos + 1;
                   |SI enSwLiquida = 1;
                       |SI #20nCampos ! #1003nCampos;
                           enSwRetorno = 1;
                       |FINSI;
                   |FINSI;
                   #20nCampos = #1003nCampos;
             |SIGUE;
         |FINSI;
     |SINO;
         |PINPA #0#1003;
         |PINDA #0#1003;
         nPanta = 0;
         |PARA;  |SI;  |HACIENDO;
              |LEETECLA aAlfa;
              |SI aAlfa = aEscape;  |SAL;  |FINSI;
              |SI aAlfa = aAvance;
                  |SI nPanta < nNumPanta;
                      nPanta = nPanta + 1;
                      |HAZ PintaPantalla;
                  |FINSI;
              |FINSI;

              |SI aAlfa = aRetroceso;
                  |SI nPanta [ nNumPanta;
                      nPanta = nPanta - 1;
                      |HAZ PintaPantalla;
                  |FINSI;
              |FINSI;
         |SIGUE;
     |FINSI;
     |POPV;
|FPRC;

|RUTINA ClaveBaja20;
     #20#0 = enEmpresa;
     #20#1 = 1;
|FRUT;

|RUTINA ClaveAlta20;
     #20#0 = enEmpresa;
     #20#1 = 99;
|FRUT;

|PROCESO PonDeclarante;  |TIPO 7;
     eaDeclarante = #20#2;
     enComunes    = 0;
     |HAZ SacaDeclarantes;
     #20#2 = eaDeclarante;
     |PINTA #20#2;

     |C_M #1003#29, 1, "S";
     |SI enMinusvalia < 33;
         #20#29 = "N";
         |C_M #1003#29, 1, "N";
     |FINSI;
|FPRC;

|PROCESO Fallece;  |TIPO 7;
     |SI eaFallece = "S";
         |C_M #20#4, 1, "S";
     |SINO;
         |C_M #20#4, 1, "N";  #20#4 = 0;  |PINTA #20#4;
     |FINSI;
|FPRC;

|PROCESO Tipo2m20;  |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 3;
         enSwRetorno = 1;
         nSw         = 1;   |BUCLE rem00053;
         |ACABA;
     |FINSI;

     nEntrada = 0 +. 1;
     |SI nEntrada = 1;
         nSw    = 0;   |BUCLE rem00053;
         nDesde = 5;
         |SI nModo = 1;  nDesde = 0;  |FINSI;
         nModo     = 1;

         |HAZ Fantasma;

         |SI enSwLiquida = 1;
             |PARA nCampos = 0;  |SI nCampos [ 4;  |HACIENDO nCampos = nCampos + 1;
                   |SI #20nCampos ! #1003nCampos;
                       enSwRetorno = 1;
                   |FINSI;
             |SIGUE;
         |FINSI;
     |SINO;
         |PARA nCampos = 0;  |SI nCampos [ 4;  |HACIENDO nCampos = nCampos + 1;
               #1003nCampos = #20nCampos;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO Tipo11m20;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     nModo  = 4;  nDesde = 0;  |HAZ Fantasma;
     |ERROR;
|FPRC;

|PROCESO rem00019;
     #19#0 = #20#0;
     #19#1 = #20#2;
     |LEE 101#19=;
     |SI FSalida ! 0;
         |DDEFECTO #19;
         #19#0 = #20#0;
         #19#1 = #20#2;
         |GRABA 020#19n;
     |FINSI;
     |LIBERA #19;
|FPRC;

|DEFBUCLE rem00020;
     #20#1;
     ;
     enEmpresa, 01;
     enEmpresa, 99;
     ;
     NULL, rem00019;
|FIN;

|PROCESO rem00020;
     #20#0  = #19#0;
     #20#2  = #19#1;
     |LEE 101#20=;
     |SI FSalida ! 0;
         |BORRA 020#19a;
     |FINSI;
     |LIBERA #19;
|FPRC;

|DEFBUCLE rem00019;
     #19#1;
     ;
     enEmpresa, "          ";
     enEmpresa, "zzzzzzzzzz";
     ;
     NULL, rem00020;
|FIN;

|PROCESO Tipo7m20;  |TIPO 7;
     |SI #20#31 = "S";
         |ATRI I;
         |PINTA 0721, "PROVIENE DEL PASE AUTOMATICO";
         |ATRI 0;
     |SINO;
         |PINTA 0721, "컴컴컴컴컴컴컴컴컴컴컴컴컴컴";
     |FINSI;
|FPRC;

|PROGRAMA;
     |ABRE #401;
     |LEE 040#401p;
     |SI FSalida ! 0; |O #401#1 = 0;  |O #401#2 = 0;  |O #401#3 = 0;
         |MENAV "      Instale Ficheros Patrones. No existe Tabla de Limites e Importes";
         |CIERRA #401;
         |ACABA;
     |FINSI;
     |CIERRA #401;

     enSwRetorno = 0;
     aQueQuiero  = "|NP";
     aNumPanta   = #1003#aQueQuiero#;
     nNumPanta   = aNumPanta;
     nNumPanta   = nNumPanta - 1;

     aQueQuiero = "|NC";
     aNumCampos = #1003#aQueQuiero#;
     nNumCampos = aNumCampos;
     nNumCampos = nNumCampos - 1;

     |ENTLINEAL #1#20, -2, enModo, 22, 1, ClaveBaja20, ClaveAlta20;

     eaPrograma   = "rem00020";
     eaPunto      = "  ";
     |HAZ ProgramaActivo;

     |ABRE #19;
     |BUCLE rem00020;
     |CIERRA #19;

     |ABRE #20;
     |SELECT #3#20;
     |BUCLE rem00019;
     |CIERRA #20;
|FPRO;
