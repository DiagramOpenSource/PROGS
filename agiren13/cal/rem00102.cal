|FICHEROS;
     rem00100 #10;
     rem00102 #20;

     rew00003;

     espejo@ #99;
|FIN;

|VARIABLES;
     aAlfa         = "";
     aAlfa1        = "";
     aLong         = "";
     aQueQuiero    = "";
     aNumCampos    = "";
     aNumPanta     = "";
     aPathRenta    = "";
     aDef          = "";
     aTecla        = "";

     nLong         = 0;
     nCaracter     = 0;
     nPosi         = 0;
     nPanta        = 0;
     nNumCampos    = 0;
     nNumPanta     = 0;
     nCampo        = 0;
     nModo         = 0;
     nEntrada      = 0;
     nContenido    = 0;
     nPorce        = 0;
     nVentana      = 0;

     &enEmpresa    = 0;
     &enModo       = 0;
     &enModoReg    = 0;
     &eaDeclarante = "";
     &enComunes    = 0;
     &enChequeo    = 0;
     &enMinusvalia = 0;
     &eaMinuAyuda  = "";
     &eaFallece    = "";
     &eaSwVarios   = "";
     &eaVarDni     = "";
     &enCalcCif    = 0;
     &enSwPresenta = 0;
     &enSwLiquida  = 0;
     &enSwRetorno  = 0;
     &eaPrograma   = "";
     &eaPunto      = "";
|FIN;

|| ************************************************************************
|| PROCESOS DE CONSULTA DE PANTALLAS NORMALES
|| ************************************************************************

|PROCESO LetraNif;  |TIPO 6;
     |SI FSalida = 10;
         eaVarDni  = #20Campo;
         enCalcCif = 1;
         |HAZ CalculoDNI;
         #20Campo = eaVarDni;
         |PINTA #20Campo;
         |ERROR;
         |ACABA;
     |FINSI;

     #rem00102#16 = "S";
     eaVarDni     = #20Campo;
     enCalcCif    = 0;
     |HAZ CalculoDNI;
     eaVarDni = (eaVarDni + "                    ") % 120;
     |SI #20Campo ! eaVarDni;
         |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
         |SI FSalida = 0;
             #20Campo = eaVarDni;  |PINTA #20Campo;
             |ERROR;
             |ACABA;
         |FINSI;

         |CONFI "0000NEs un N£mero de Identificaci¢n de otro pais";
         |SI FSalida = 0;
             #rem00102#16 = "N";
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO AyudaSituacionFm102;  |TIPO 0;
     |C_M #20#11, 1, "S";
     |SI #20#14 > 2;
         |C_M #20#11, 1, "N";  #20#11 = "";  |PINTA #20#11;
     |FINSI;

     |SI FSalida ! 10;  |ACABA;  |FINSI;

     |VENTANA 2220, 2889, -1, 1, "";
     nVentana = FSalida;

     |CONTROL_SIMPLE 0, "LABEL,{{15}}SITUACION", 2222, 2269, NULL;
     |CONTROL_SIMPLE 0, "LABEL,{{16}}1 .- Vivienda o plaza de garaje situada en cualquier punto del territorio espa¤ol,", 2323, 2389, NULL;
     |CONTROL_SIMPLE 0, "LABEL,{{16}}      con excepcion del Pais Vasco y Navarra", 2423, 2489, NULL;
     |CONTROL_SIMPLE 0, "LABEL,{{16}}2 .- Vivienda o plaza de garaje situada en el Pais Vasco y Navarra", 2523, 2589, NULL;
     |CONTROL_SIMPLE 0, "LABEL,{{16}}3 .- Vivienda o plaza de garaje situada en cualquier punto del territorio espa¤ol,", 2623, 2689, NULL;
     |CONTROL_SIMPLE 0, "LABEL,{{16}}     pero sin tener asignada referencia catastral.", 2723, 2789, NULL;
     |CONTROL_SIMPLE 0, "LABEL,{{16}}4 .- Vivienda o plaza de garaje situada en el extranjero. ", 2823, 2889, NULL;

     |PTEC 802;  |PAUSA;

     |LEETECLA aTecla;
     |FINVENTANA nVentana;
     |ERROR;
|FPRC;

|PROCESO Titularidad;
     |SI #20#9 = " ";  #20#10 = "                 ";  |FINSI;
     |SI #20#9 = "1";  #20#10 = "EN PROPIEDAD     ";  |FINSI;
     |SI #20#9 = "2";  #20#10 = "EN USUFRUCTO     ";  |FINSI;
     |SI #20#9 = "3";  #20#10 = "EN ARRENDAMIENTO ";  |FINSI;
     |SI #20#9 = "4";  #20#10 = "OTRAS SITUACIONES";  |FINSI;

     |PINTA #20#10;

     |C_M #20#15, 1, "S";
     |SI #20#9 ! "3";
         |C_M #20#15, 1, "N";  #20#15 = "";
     |FINSI;

     |PINTA #20#15;
|FPRC;

|RUTINA ClaveBaja20;
     #20#0 = enEmpresa;
     #20#1 = 1;
|FRUT;

|RUTINA ClaveAlta20;
     #20#0 = enEmpresa;
     #20#1 = 99;
|FRUT;

|PROCESO PonDeclarante;  |TIPO 7;
     |SI #20#12 ! 0;
         |MENAV "      La linea viene del Capital Inmobiliario, Modifique desde el Inmobiliario";
         |PTEC 807;
         |ACABA;
     |FINSI;

     enComunes   = 1;
     enChequeo   = 0;

     #rew00003#0 = #20#2;
     #rew00003#1 = "A";
     #rew00003#2 = "0";
     #rew00003#3 = #20#3;
     #rew00003#4 = #20#4;
     #rew00003#5 = #20#5;
     #rew00003#6 = #20#6;
     #rew00003#7 = #20#7;
     #rew00003#8 = #20#8;

     |C_M #rew00003#1, 1, "N";

     |HAZ PantaREW00003;

     #20#2 = #rew00003#0;
     #20#3 = #rew00003#3;
     #20#4 = #rew00003#4;
     #20#5 = #rew00003#5;
     #20#6 = #rew00003#6;
     #20#7 = #rew00003#7;
     #20#8 = #rew00003#8;

     |PINTA #20#2;
|FPRC;

|PROCESO Tipo2m20;  |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;  |O nModo = 3;
         |SI nModo = 3;  |Y #20#12 ! 0;
             |MENAV "      La linea viene del Capital Inmobiliario, Borre desde el Inmobiliario";
             |ERROR;
             |ACABA;
         |FINSI;

         enSwRetorno  = 1;
         |ACABA;
     |FINSI;

     nEntrada = 0 +. 1;
     |SI nEntrada ! 1;
         |PARA nCampo = 0;  |SI nCampo [ nNumCampos;  |HACIENDO nCampo = nCampo + 1;
               #99nCampo = #20nCampo;
         |SIGUE;
     |SINO;
         |PARA nCampo = 0;  |SI nCampo [ nNumCampos;  |HACIENDO nCampo = nCampo + 1;
               |SI #99nCampo ! #20nCampo;
                   enSwRetorno  = 1;
               |FINSI;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO Refer;
     |SI #20#14 > 2;  |ACABA;  |FINSI;

     aAlfa = #20#11;  |QBF aAlfa;
     |SI aAlfa = "";
         |MENAV "0000 Referencia Catastral Vacia";
     |FINSI;

     aAlfa = aAlfa % 0;
     |SI aAlfa ! "20";
         |MENAV "0000 La longitud de la Referencia Catastral deberia ser de 20 Caracteres.";
     |FINSI;
|FPRC;

|PROCESO Tipo80Fm102;  |TIPO 80;
     FSalida = 2699;
|FPRC;

|PROGRAMA;
     |HAZ CargaColores;

     enModoReg  = 0;
     aQueQuiero = "|NP";
     aNumPanta  = #20#aQueQuiero#;
     nNumPanta  = aNumPanta;
     nNumPanta  = nNumPanta - 1;

     aQueQuiero = "|NC";
     aNumCampos = #20#aQueQuiero#;
     nNumCampos = aNumCampos;
     nNumCampos = nNumCampos - 1;
     enSwPresenta = 0;

     |DBASE aPathRenta;  |QBT aPathRenta;
     aDef = aPathRenta + "def/rem00102.mas";
     |CARGA_DEF aDef, espejo@;
     |SI FSalida ! 0;
         aAlfa = "     Error en cargar el Def rem00102.mas";
         |MENAV aAlfa;
         |ERROR;
     |FINSI;

     |ABRE #rem00100;
     #rem00100#0 = enEmpresa;
     |LEE 000#rem00100.=;
     |SI FSalida ! 0; |DDEFECTO #rem00100; |FINSI;
     |CIERRA #rem00100;
     |SI #rem00100#91 = " ";
        |C_M #rem00102#13,1,"N";
     |SINO;
        |C_M #rem00102#13,1,"S";
     |FINSI;

     |ENTLINEAL #1#20, -2, enModo, 24, 1, ClaveBaja20, ClaveAlta20;

     |DESCARGA_DEF #espejo@;

     eaPrograma   = "rem00102";
     eaPunto      = "  ";
     |HAZ ProgramaActivo;
|FPRO;
