|FICHEROS;
     rem00101 #1,MANTE;
     rew00039 #939;
|FIN;

|VARIABLES;
      aAlfa         = "";
      aTecla        = "";
      nForma        = 0;
      nEdad         = 0;

     &eaVarDni      = "";
     &enCalcCif     = 0;

 {-1} aMenuFami     = "";
      aMenuFami1    = "1262";
      aMenuFami2    = "1";
      aMenuFami3    = "2";
      aMenuFami4    = "DESCENDIENTE";
      aMenuFami5    = "ASCENDIENTE ";
|FIN;

|PROCESO SacaAyuda;  |TIPO 7;
     |SI #1#4 !  "DESCENDIENTE";  |ACABA;  |FINSI;

     |PUSHV 0501 2380;
     |BLANCO 1210 2380;
     |CUADRO 1210 2380;
     |ATRI S;
     |PINTA 1311, "                         OTRAS SITUACIONES                           ";
     |ATRI s;

     |ATRI I;
     |PINTA 1411, "     En blanco ninguna de estas cuatro                               ";
     |PINTA 1511, " 1.- Descendiente distinto de los hijos (Nietos, bisnietos) o        ";
     |PINTA 1611, "     persona en tutela o acogimiento.                                ";
     |PINTA 1711, " 2.- Hijo/a mayor de edad incapacitado/a judicialmente sujeto/a a    ";
     |PINTA 1811, "     patria potestad prorrogada.                                     ";
     |PINTA 1911, " 3.- Hijo/a del declarante que conviva con otro progenitor sin matri-";
     |PINTA 2011, "     monio entre los padres.                                         ";
     |PINTA 2111, " 4.- Hijo/a del declarante cuando concurran simultaneamente las      ";
     |PINTA 2211, "     situaciones 2 y 3                                               ";
     |ATRI i;

     |LEETECLA aTecla;
     |POPV;
|FPRC;

|PROCESO VeriFecha101;
     |SI #1Campo = "01.01.0000";    |ACABA;  |FINSI;
     |SI #1#4    = "DESCENDIENTE";
         |SI #1Campo ] "01.01.2003";
             |MENAV "     Fecha Incorrecta.";
             |ERROR;
         |FINSI;
         |ACABA;
     |FINSI;

     |SI #1Campo ] "01.01.2000";
         aAlfa = "     Fecha Incorrecta. Cambie los dos primeros digitos del A" + &209 + "O (20) por (19)";
         |MENAV aAlfa;
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO LetraNifm101;  |TIPO 6;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     eaVarDni  = #1Campo;
     enCalcCif = 1;
     |HAZ CalculoDNI;
     #1Campo = eaVarDni;
     |PINTA #1Campo;
     |ERROR;
|FPRC;

|PROCESO Vincula;
     |SI #1#4 = "DESCENDIENTE";  |ACABA;  |FINSI;

     |SI #1Campo = " ";
         |MENAV "     Debe ser 1 o 2 dependiendo del Titular o del Conyuge.";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO PonTipo;
     |SI FSalida = 2;  |ACABA;  |FINSI;

     |SI #1Campo = "DESCENDIENTE";  aMenuFami2 = "1";  |FINSI;
     |SI #1Campo = "ASCENDIENTE ";  aMenuFami2 = "2";  |FINSI;

     |PUSHV 0501 2380;
     |PARA; |SI; |HACIENDO;
            |MENUG aMenuFami;
            aMenuFami2 = FSalida;

            |SI aMenuFami2 = "1";   #1Campo = aMenuFami4;  |SAL;  |FINSI;
            |SI aMenuFami2 = "2";   #1Campo = aMenuFami5;  |SAL;  |FINSI;
     |SIGUE;
     |POPV;

     |SI #1#4 = "            ";
         |MENAV "     Datos incorrectos. Informe ASCENDIENTE o DESCENDIENTE";
         |ERROR;
         |ACABA;
     |FINSI;

     |PINTA #1Campo;

     |SI #1#4 =  "DESCENDIENTE";
         |C_M #1#8,  1, "S";
         |C_M #1#12, 1, "S";
         |C_M #1#18, 1, "S";
         |C_M #1#9,  1, "N";  #1#9 = "";   |PINTA #1#9;
         |C_M #1#19, 1, "N";  #1#19 = "";  |PINTA #1#19;
     |SINO;
         |C_M #1#9,  1, "S";
         |C_M #1#8,  1, "N";  #1#8  = "";  |PINTA #1#8;
         |C_M #1#12, 1, "N";  #1#12 = "";  |PINTA #1#12;
         |C_M #1#18, 1, "N";  #1#18 = "00.00.0000";  |PINTA #1#18;
         |C_M #1#19, 1, "S";
         |SI #1#6 = 0;
             |C_M #1#19, 1, "N";  #1#19 = "";  |PINTA #1#19;
         |FINSI;

         |SI #1#2 = "            ";
             |MENAV "     Introduzca el NIF del Ascendiente";
             |ERROR;

             |PTEC 808;
             |PTEC 808;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO CtasCtesm101;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     #939#0 = #1Campo % 104;
     #939#1 = #1Campo % 504;
     #939#2 = #1Campo % 902;
     #939#3 = #1Campo % 1110;

     |PUSHV 0501 2380;
     |PINPA #0#939;

     |ATRI R;
     |PINTA 1116, "            CUENTA CORRIENTE                ";
     |ATRI r;

     |PINDA #0#939;
     |ENDATOS #1#939;
     |SI FSalida = 0;
         aAlfa     = #939#0 + #939#1 + #939#2 + #939#3;
         #1Campo = aAlfa;
     |FINSI;
     |POPV;
     |PINTA #1Campo;
|FPRC;

|PROCESO Mayusm101;
     #1Campo = #1Campo > #1Campo;
     |PINTA #1Campo;
|FPRC;

|PROCESO FechaNacimiento;
     |SI FSalida = 2;  |ACABA;  |FINSI;

     #1#5 = #1#17 % -104;

     |SI #1#5 = "0000";
         |MENAV "     Datos incorrectos. Informe la Fecha de Nacimiento ";
         |ERROR;
         |ACABA;
     |FINSI;

     |SI #1#4 =  "DESCENDIENTE";
         nEdad = 2002 - #1#5;
         |SI nEdad ] 16;
             |SI #1#2 = "            ";
                 |MENAV "     Descendiente > = a 16, Introduzca el NIF del Descendiente";
                 |ERROR;

                 |PTEC 808;
                 |PTEC 808;
             |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO MiraNombre;
     aAlfa = #1#3;  |QBF aAlfa;
     |SI aAlfa = "";
         |MENAV "     Introduzca Apellidos y Nombre.";
         |ERROR;
         |ACABA;
     |FINSI;
|FPRC;



|PROCESO Compartido;  |TIPO 7;
     |C_M #1#15, 1, "S";
     |SI #1#4 = "DESCENDIENTE";
         |C_M #1#15, 1, "N";
         #1#15 = "N";  |PINTA #1#15;
     |FINSI;
|FPRC;

|PROCESO NivelRentas;  |TIPO 7;
     |SI #1#4 = "DESCENDIENTE";
         |C_M #1#19, 1, "N";  #1#19 = "";  |PINTA #1#19;
     |SINO;
         |C_M #1#19, 1, "S";
         |SI #1#6 = 0;
             |C_M #1#19, 1, "N";  #1#19 = "";  |PINTA #1#19;
         |FINSI;
     |FINSI;
|FPRC;
