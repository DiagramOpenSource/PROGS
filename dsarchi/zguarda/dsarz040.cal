|FICHEROS;
     dsarz040 #0;
     dsarm001 #1;
     dsarm040 #40;

     dsarw071 #971;
     dsarm053 #53;
|FIN;

|VARIABLES;
     aBase    = "";
     nHandle  = 0;

     nSubVent = 0;
     nPanta = 0;

     ||AutoSeleccionTipoSegunPlantillasAutoRelleno
     &eaAutoSelPlantilla = "";

     ||Modo silencio
     &enSwModoSilencio = 0;
     &eaErrorDsEscan = "";
     &enNumRegDsEscan = 0;
     aHora = "";

     nBotonImp = 0;
     nBotonTiposAuto = 0;
     aInforme = "";
     nSwPie = 0;
     aMensaje = "";
     aSilencio = "";

     nPanta0             = 0;
     nPanta971           = 0;
     nBotonIniciar       = -1;
     nBotonSalir         = -1;
     nBotonAceptar       = -1;
     nBotonCancelar      = -1;
     nBotonAlta          = -1;
     nBotonBaja          = -1;
     nBotonModificar     = -1;
     nBotonCopiar        = -1;
     nBotonCarpeta       = -1;
     nBotonTipo          = -1;
     nSegundos           = 0;
     nSalir              = 0;
     nConta              = 0;
     nRango              = 0;
     nGrid40             = 0;
     nRedon              = 0;
     nFalta              = 0;
     nLabelTiempo        = -1;
     nLabelBarra         = -1;
     nVentTrab           = 0;
     nVent971            = 0;
     nModo               = 0;
     nTiempo             = 0;
     nHay                = 0;
     nLong               = 0;
     nCarac              = 0;
     nPos                = 0;
     nCopia              = 0;
     nCodBarra           = 0;

     aQueBoton           = "";
     aID                 = "";
     aAlfa               = "";
     aAlfa1              = "";
     aHora1              = "";
     aHora2              = "";
     aDesde              = "";
     aHasta              = "";
     aFichero            = "";
     aEjecuta            = "";
     aDir                = "";
     aDirCopia           = "";
     aDestino            = "";
     aLong               = "";
     aCarac              = "";
     aBarra              = "";

     fFecha              = @;

     {200}aPopup         = "";

     {-1}aGet            = 0;
         aGet1           = 0;
         aGet2           = 0;

     {-1}aVent           = "";
         aVent1          = 0;
         aVent2          = 0;
         aVent3          = 0;
         aVent4          = 0;
         aVent5          = "";

     &eaDSMAC            = "";
     &enDesdeCapturador  = 0;
     &enPrevisualizar    = 0;
     &eaCarpetaOrigen    = "";
     &eaDocCaptura       = "";
     &enGrupo            = 0;
     &enSubGrupo         = 0;
     &enTipo             = 0;
     &eaUsuArchivado     = "";
|FIN;

|PROCESO Tipo7C4_z040; |TIPO 7;
     |HAZ Tipo0C4_z040;
|FPRC;

|PROCESO Tipo0C4_z040; |TIPO 0;
/*
     |SI #dsarz040#4 = "S";
          |ESTADO_CONTROL nBotonImp, "ENABLE";
     |SINO;
          |ESTADO_CONTROL nBotonImp, "DISABLE";
     |FINSI;
*/
|FPRC;

|PROCESO dsarm053;
     |SI #dsarm053#3 = "N";
          #dsarm053#3 = "S";
          |GRABA 020#dsarm053.a;
          nSwPie = 1;
          |PRINT;
     |FINSI;
|FPRC;

|DEFBUCLE dsarm053;
 #dsarm053#1;
 ;
 INICIO;
 FINAL;
 be;
 NULL, dsarm053;
|FIN;

|PROCESO Imprimir;
     ||ARA23
     |IMPRE 0;
     |SI FSalida = 0;
          aInforme = "dsarm053";
          |INFOR aInforme;
          |SI FSalida = 0;
               |BUCLE dsarm053;
               |SI nSwPie = 1;
                    |PIEINF;
               |FINSI;
               |FININF;
          |SINO;
               aMensaje = "0000Informe " + aInforme + " inaccesible.";
               |MENAV aMensaje;
          |FINSI;
          |FINIMP;
     |SINO;
          |MENAV "0000Proceso cancelado.";
     |FINSI;
|FPRC;

|PROCESO ProcesaFichero;
     |QBF aFichero;
     aAlfa    = aFichero;
     aFichero = "";
     aLong    = aAlfa % 0;
     nLong    = aLong;
     |PARA nCarac = 1;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos   = (nCarac * 100) + 1;
           aCarac =  aAlfa % nPos;
           |SI aCarac = "/";
               aCarac = "\";
           |FINSI;

           aFichero = aFichero + aCarac;
     |SIGUE;

     aAlfa = aFichero < "";
     aAlfa = aAlfa - "thumbs.db";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI nLabelTiempo ! -1;
         |FIN_CONTROL_WINDOWS nLabelTiempo;
         nLabelTiempo = -1;
     |FINSI;

     |SI nLabelBarra ! -1;
         |FIN_CONTROL_WINDOWS nLabelBarra;
         nLabelBarra = -1;
     |FINSI;

     aAlfa  = "LABEL,{{15}}Procesando fichero " + aFichero;
     |CONTROL_SIMPLE 0, aAlfa, 1502, 1798, NULL;
     nLabelTiempo = FSalida;

     |SI #dsarz040#4 = "S";
          enSwModoSilencio = 1;
     |SINO;
          enSwModoSilencio = 0;
     |FINSI;
     eaErrorDsEscan = "";
     enNumRegDsEscan = 0;

     enGrupo           = #40#3;
     enSubGrupo        = #40#4;
     enTipo            = #40#5;
     enDesdeCapturador = 1;
     enPrevisualizar   = 1;
     eaCarpetaOrigen   = #40#1;
     eaDocCaptura      = aFichero;
     eaUsuArchivado    = #40#6;      |QBF eaUsuArchivado;
     eaAutoSelPlantilla = #40#7;
     aEjecuta          = "dsarz001;CAPTURAESTEDOC";
     |SUB_EJECUTA_NP aEjecuta;

     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;

     |QBF eaErrorDsEscan;
     |SI #dsarz040#4 = "S"; |Y eaErrorDsEscan ! "";
          |HORA aHora;
          |QBF aHora;
          |ABRE #dsarm053;
          |DDEFECTO #dsarm053;
          #dsarm053#0 = ~;
          #dsarm053#1 = aHora;
          #dsarm053#2 = enNumRegDsEscan;
          |LEE 101#dsarm053.=;
          |SI FSalida ! 0; |GRABA 020#dsarm053.b; |FINSI;
          #dsarm053#3 = "N";
          #dsarm053#4 = eaErrorDsEscan;
          #dsarm053#5 = aFichero;
          |GRABA 020#dsarm053.a;
          |LIBERA #dsarm053;
          |CIERRA #dsarm053;
     |FINSI;

     |SI nCopia    = 0;
          fFecha    = ~;
          |HORA aHora1;

          aAlfa     = (fFecha % -104) + (fFecha % 0402) + (fFecha % 102);
          aAlfa     = aAlfa + (aHora1 % 102) + (aHora1 % 402) + (aHora1 % 702);

          aDirCopia = #40#1;  |QBF aDirCopia;
          aDirCopia = aDirCopia + aAlfa;            |RMKDIR aDirCopia;
          aDirCopia = aDirCopia + "\";
     |FINSI;

     nCopia   = 1;
     aDestino = aDirCopia + (aFichero - aDir);
     |RRENOMBRA_FICHERO aFichero, aDestino;
|FPRC;

|PROCESO dsarm040Ejecuta;
     nCopia    = 0;
     aDir      = #40#1;  |QBF aDir;
     aFichero  = aDir + "*.*";
     |R_PDIR aFichero;
     |PARA;  |SI FSalida ] 0; |HACIENDO;
          |HAZ ProcesaFichero;

          |R_SDIR aFichero;
     |SIGUE;
|FPRC;

|DEFBUCLE dsarm040Ejecuta;
     #40#1;
     ;
     #0#0, aDesde;
     #0#0, aHasta;
     ;
     NULL, dsarm040Ejecuta;
|FIN;

|PROCESO EjecutaProceso;
     |ESTADO_CONTROL nGrid40, "DISABLE";

     |ESTADO_CONTROL nBotonSalir, "HIDE";

     aDesde    = "";
     aHasta    = "z" * 70;

     |BUCLE dsarm040Ejecuta;

     |ESTADO_CONTROL nBotonSalir, "SHOW";
     |ESTADO_CONTROL nGrid40, "ENABLE";
|FPRC;

|PROCESO Evento40;
     |SI FSalida = 1;  |O FSalida = 2;
         |ESTADO_CONTROL nBotonModificar, "DISABLE";
         |ESTADO_CONTROL nBotonBaja, "DISABLE";
         |ESTADO_CONTROL nBotonIniciar, "DISABLE";
         |ESTADO_CONTROL nBotonCopiar,  "ENABLE";

         |LEE 000#40=;
         |SI FSalida ! 0;   |ACABA;  |FINSI;
         |SI #40#0 ! #0#0;  |ACABA;  |FINSI;

         |ESTADO_CONTROL nBotonIniciar, "ENABLE";
         |ESTADO_CONTROL nBotonModificar, "ENABLE";
         |ESTADO_CONTROL nBotonBaja, "ENABLE";
         |ESTADO_CONTROL nBotonCopiar, "DISABLE";
     |FINSI;
|FPRC;

|PROCESO Baja40;
     #40#0 = #0#0;
     #40#1 = "";
|FPRC;

|PROCESO Alta40;
     #40#0 = #0#0;
     #40#1 = "z" * 50;
|FPRC;

|PROCESO Tipo12;  |TIPO 12;
     |SI FSalida = 7;  |ACABA;  |FINSI;

     |ERROR;
     |ACABA;
|FPRC;

|PROCESO LabelTiempo;
     |SI nLabelTiempo ! -1;
         |FIN_CONTROL_WINDOWS nLabelTiempo;  nLabelTiempo = -1;
     |FINSI;

     |SI nFalta > 1;
         aAlfa  = "LABEL,{{15}}Para la pr¢xima captura faltan " + nFalta + " minutos";
     |SINO;
         aAlfa  = "LABEL,{{15}}Para la pr¢xima captura falta menos de un minuto";
     |FINSI;

     |CONTROL_SIMPLE 0, aAlfa, 2420, 2478, NULL;
     nLabelTiempo = FSalida;
|FPRC;

|PROCESO LabelBarra;
     |SI nLabelBarra ! -1;
         |FIN_CONTROL_WINDOWS nLabelBarra;   nLabelBarra  = -1;
     |FINSI;

     nCodBarra = nCodBarra + 1;
     |SI nCodBarra > 4;  nCodBarra = 1;  |FINSI;

     |SI nCodBarra = 1;  aBarra = "|";  |FINSI;
     |SI nCodBarra = 2;  aBarra = "/";  |FINSI;
     |SI nCodBarra = 3;  aBarra = "--";  |FINSI;
     |SI nCodBarra = 4;  aBarra = "\";  |FINSI;

     aAlfa  = "LABEL,{{15}}" + aBarra;
     |CONTROL_SIMPLE 0, aAlfa, 2415, 2418, NULL;
|FPRC;

|PROCESO Salir;
     nSalir = 1;

     |PTEC 807;
|FPRC;

|PROCESO GetData;
     |REFERENCIA_DEF aGet1, #0;
     aGet2 = 1;
     IaGet = aGet1<-;
     |ESPECIFICA "GETDATACONTROL", aGet;
     #0#1 = FSalida;

     nSegundos = #0#1 * 60;
|FPRC;

|PROCESO Lee1000;
     |DBASE aBase;
     aFichero = aBase + "dsarchi.men";
     |XABRE "", aFichero, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     |PARA; |SI; |HACIENDO;
            |XLEE nHandle, 250, aAlfa;
            |SI FSalida < 0;  |SAL;  |FINSI;
     |SIGUE;
     |XCIERRA nHandle;
|FPRC;

|PROCESO Iniciar;
     |FIN_CONTROL_WINDOWS nBotonIniciar;
     nBotonIniciar = -1;

     |VENTANA 2401, 2599, nPanta0, -1, "";
     nVentTrab = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentTrab;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     nSalir    = 0;
     nConta    = 0;

     |PTEC 802;  |PAUSA;

     |HAZ EjecutaProceso;

     |HAZ GetData;
     nFalta    = #0#1;  |HAZ LabelTiempo;

     |PARA;  |SI;  |HACIENDO;
          |SI nSalir = 1;  |SAL;  |FINSI;

          |SI nConta = nSegundos;
              |HAZ EjecutaProceso;
              nConta    = 0;
              |HAZ GetData;
              nFalta    = #0#1;
              |HAZ LabelTiempo;
          |FINSI;

          |SLEEP 1;
          |PULSATECLA;

/*
          |HORA aHora1;
          |PARA;  |SI;  |HACIENDO;
                |HORA aHora2;
                |SI aHora1 ! aHora2;  |SAL;  |FINSI;

                |HAZ Lee1000;

                |PULSATECLA;  ||este
          |SIGUE;
*/

          nConta = nConta + 1;

          nRedon = nConta $ 60;
          |SI nRedon = 0;
              nFalta = ((#0#1 * 60) - nConta) / 60;
              |HAZ LabelTiempo;
          |FINSI;

          |HAZ LabelBarra;

          |PULSATECLA;
     |SIGUE;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentTrab;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVent971;

     |PULSATECLA;
     |PULSATECLA;
|FPRC;

|PROCESO Tipo7C1;   |TIPO 7;
     |SI nBotonSalir = -1;
         aQueBoton     = "|X000";
         aID           = #0#aQueBoton#;
         nBotonAceptar = aID;
         |ESTADO_CONTROL nBotonAceptar, "HIDE";

         aQueBoton      = "|X001";
         aID            = #0#aQueBoton#;
         nBotonCancelar = aID;
         |ESTADO_CONTROL nBotonCancelar, "HIDE";

         |CONTROL_SIMPLE 0, "BOTON,{{197}}Salir", 2688, 2798, Salir;
         nBotonSalir = FSalida;
     |FINSI;
|FPRC;

|PROCESO EntraDatos971
     |VENTANA 0501, 1999, -1, 16, "Carpeta contenedora";
     nVent971 = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent971;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |ABRE #1;
     |LEE 000#1p;
     |SI FSalida ! 0;  |DDEFECTO #1;  |FINSI;
     |CIERRA #1;

     |PINPA #0#971;  nPanta971 = FSalida;

     |DDEFECTO #971;

     |SI nModo = 1;
         |CONTROL_SIMPLE nPanta971, "BOTON,{{136}}", 0692, 0695, Carpeta;
         nBotonCarpeta = FSalida;

         |CONTROL_SIMPLE nPanta971, "BOTON,{{197}}Asignar carpeta", 1076, 1186, Ubicacion;
         nBotonTipo = FSalida;
     |FINSI;

     |SI nModo = 2;
         |CONTROL_SIMPLE nPanta971, "BOTON,{{197}}Cambiar carpeta", 1076, 1186, Ubicacion;
         nBotonTipo = FSalida;

         #971#0 = #40#1;
         #971#1 = #40#2;
         #971#2 = #40#3;
         #971#3 = #40#4;
         #971#4 = #40#5;
         #971#8 = #40#6;
         #971#9 = #40#7;

         |HAZ Tipo0C2Fw71;
         |SI #1#8 ! "N";
             |HAZ Tipo0C3Fw71;
             |HAZ Tipo0C4Fw71;
         |FINSI;
     |FINSI;

     |PINDA #0#971;

     |ENDATOS #1#971;
     |SI FSalida = 0;
         #40#0 = #0#0;
         #40#1 = #971#0;
         |LEE 101#40=;
         |SI FSalida ! 0;
             #40#0 = #0#0;
             #40#1 = #971#0;
             |GRABA 020#40b;
         |FINSI;
         #40#2 = #971#1;
         #40#3 = #971#2;
         #40#4 = #971#3;
         #40#5 = #971#4;
         #40#6 = #971#8;
         #40#7 = #971#9;
         |GRABA 020#40a;
         |LIBERA #40;

         |REFRESCACONTROL nGrid40;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent971;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVent971;
|FPRC;

|PROCESO Alta;
     nModo = 1;   |HAZ EntraDatos971;
|FPRC;

|PROCESO Modificar;
     nModo = 2;   |HAZ EntraDatos971;
|FPRC;

|PROCESO Baja;
     |CONFI "0000NSeguro que quiere dar de baja la Carpeta contenedora";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |LEE 101#40=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |BORRA 020#40a;
     |LIBERA #40;

     |REFRESCACONTROL nGrid40;

     FSalida = 1;  |HAZ Evento40;

     |FOCOTECLADO nGrid40;
|FPRC;

|PROCESO dsarm040Copiar;
     #40#0 = eaDSMAC;
     |GRABA 020#40n;
     |LIBERA #40;
|FPRC;

|DEFBUCLE dsarm040Copiar;
     #40#1;
     ;
     #0#0, aDesde;
     #0#0, aHasta;
     ;
     NULL, dsarm040Copiar;
|FIN;

|PROCESO Copiar;
     nTiempo  = #0#1;
     nHay     = 0;
     IaPopup  = aPopup2  <-;

     |LEE 000#0p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          IaPopup = IaPopup + 1;
          aAlfa   = #0#0 + " / " + #0#2 + " / " + #0#3;
          aPopup  = aAlfa;
          nHay    = nHay + 1;

          |LEE 000#0s;
     |SIGUE;

     |SI nHay = 0;
         #0#0 = eaDSMAC;
         #0#1 = nTiempo;

         |MENAV "0000No hay configuraci¢n en otra m quina";
         |ACABA;
     |FINSI;

     |MENAV "0000A continuaci¢n aparecer  todas las maquinas con la fecha y hora de la £ltima captura. Seleccione una m quina.";

     aPopup1      = nBotonCopiar;
     aPopup2      = nHay;
     aAlfa        = "";

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
         IaPopup  = aPopup3  <-;
         IaPopup  = IaPopup + (FSalida - 1);
         aAlfa    = aPopup;
     |FINSI;

     |SI aAlfa = "";
         #0#0 = eaDSMAC;
         #0#1 = nTiempo;

         |ACABA;
     |FINSI;

     aAlfa1 = aAlfa % 120;  #0#0   = aAlfa1;
     |LEE 000#0=;
     |SI FSalida = 0;
         nTiempo = #0#1;
         aDesde  = "";
         aHasta  = "z" * 70;

         |BUCLE dsarm040Copiar;
     |FINSI;

     #0#0 = eaDSMAC;
     #0#1 = nTiempo;   |PINTA #0#1;

     |REFRESCACONTROL nGrid40;
     |FOCOTECLADO nGrid40;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     FSalida = 2799;
|FPRC;

|PROCESO TiposAuto;
     |VENTANA 0501, 2799, nPanta, 17, "Configuraci¢n Tipos destino Archivado Autom tico";
     nSubVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |SUB_EJECUTA_NP "dsarz053";

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nSubVent;
|FPRC;

|PROGRAMA;
     |ABRET #0;
     |ABRET #40;

     |CLS;

     |HAZ CogeMac;

     #0#0 = eaDSMAC;
     |LEE 000#0=;
     |SI FSalida ! 0;
         |DDEFECTO #0;
         #0#0 = eaDSMAC;
     |FINSI;

     |PINPA #0#0;  nPanta0 = FSalida;
     |PINDA #0#0;

     |CONTROL_SIMPLE nPanta0, "BOTON,I N I C I A R", 2430, 2660, Iniciar;
     nBotonIniciar = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON,{{199}}Baja", 1887, 1898, Baja;
     nBotonBaja = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON,{{197}}Modificar", 1875, 1885, Modificar;
     nBotonModificar = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON,{{193}}Alta", 1863, 1873, Alta;
     nBotonAlta = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON,{{197}}Copiar configuraci¢n de otra m quina", 1802, 1832, Copiar;
     nBotonCopiar = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON,{{205}}", 2174, 2178, Imprimir;
     nBotonImp = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON,Configuraci¢n Tipos Automaticos", 2280, 2397, TiposAuto;
     nBotonTiposAuto = FSalida;

     |HAZ Tipo0C4_z040;

     |ESTADO_CONTROL nBotonModificar, "DISABLE";
     |ESTADO_CONTROL nBotonBaja, "DISABLE";
     |ESTADO_CONTROL nBotonIniciar, "DISABLE";

     nRango = 2 + 4 + 8 + 16 + 32 + 128;
     |LINEAL_SIMPLE #1#40, nRango, 0602, 1798, Baja40, Alta40, Evento40;
     nGrid40 = FSalida;

     |ENDATOS #1#0;

     nTiempo = #0#1;

     aSilencio = #0#4;

     |HORA aHora1;

     #0#0 = eaDSMAC;
     |LEE 101#0=;
     |SI FSalida ! 0;
         |DDEFECTO #0;
         #0#0 = eaDSMAC;
         |GRABA 020#0b;
     |FINSI;

     #0#1 = nTiempo;
     #0#2 = ~;
     #0#3 = aHora1;
     #0#4 = aSilencio;

     |GRABA 020#0a;
     |LIBERA #0;

     |CIERRAT #0;
     |CIERRAT #40;

     |FIN_CONTROL_WINDOWS nBotonIniciar;
     |FIN_CONTROL_WINDOWS nBotonSalir;
     |FIN_CONTROL_WINDOWS nBotonAlta;
     |FIN_CONTROL_WINDOWS nBotonModificar;
     |FIN_CONTROL_WINDOWS nBotonBaja;
     |FIN_CONTROL_WINDOWS nBotonCopiar;
     |FIN_CONTROL_WINDOWS nBotonImp;
     |FIN_CONTROL_WINDOWS nBotonTiposAuto;

     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
|FPRO;
