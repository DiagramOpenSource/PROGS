|FICHEROS;
     dsarz006 #99;
     dsarm001 #1;        ||Parametros;
     dsarm002 #2;        ||Grupos
     dsarm003 #3;        ||Subgrupos
     dsarm004 #4;        ||Tipos
     dsarm005 #0;        ||Registro documentos
     dsarm006 #6;        ||Entrada/Salidas Doc.
     dsarm007 #7;        ||Asociacion Documentos
     dsarw008 #908;

     dsarm005@ #905;

     flowm006 #506;

     ||ASO
     dsarm008 #8;          ||Vistas Arbol
     dsarm000 #100;        ||Usuarios
     dsarm043 #43;         ||Permisos Vistas Usu.
     dsarm044 #44;         ||Permisos VistasTipos
     dsarm059;             ||Permisos VistasTipos

     dpntm001 #801;

     :/integral/def/dsam0103 #103;
|FIN;

|VARIABLES;
     ||Arbol Asociar
     nBoton0_8 = 0;
     nBoton0_9 = 0;
     nBoton0_10 = 0;
     nBoton0_11 = 0;

     ||ASO
     aTipoVista = "";
     aNomVista = "";
     &enBusqueda = 0;
     nSwArbolActivo = 0;
     nEstadoGrid = 0;
     nBotonLabel = 0;

     aCodigo9 = "";
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     aAlfa4 = "";
     aAlfa5 = "";
     aBeta1 = "";
     aBeta2 = "";
     aBeta3 = "";
     aBeta4 = "";
     aBeta5 = "";
     nEntroPDatos = 0;
     aTexto = "";
     nCargar = 0;
     nPosi = 0;
     nNumCamp = 0;
     aRefresca = "";
     aBusca = "";
     aUsuarioVista = "";
     nOpcion = 0;
     nGrid = 0;
     aDetalle = "";
     aBeta = "";
     {200}aPopup      = "";
     {200}aOpcion     = "";

     ||Orden en ultimo nivel Arbol
     aOrden = "";
     nOrden = 0;
     nSwCampoNum = 0;
     nCampoOrden = 0;
     nSwCampoFecha = 0;
     aAuxOrden = "";
     aIniOrden = "";
     aFechaUno = "";
     fFechaUno = @;
     nFechaUno = 0;
     fFechaDos = @;
     aDia = "";
     aMes = "";
     aAnyo = "";
     aPilla = "";



     &enNumRegFlow = 0;
     &enSituFlow = 0;
     &eaDesSituFlow = "";

     aUrl = "";
     aClave = "";
     aAuxNom = "";

     aFicAux = "";
     nSwVerDocPDF = 0;
     nHandleX = 0;
     aAuxPath = "";

     nLinea = 0;
     nSitu = 0;
     aOriHis = "";
     aDesHis = "";
     aUsuario = "";
     aAsociado = "";
     aDocHis = "";

     nError       = 0;
     nHandleEjec  = 0;
     nCaracter    = 0;

     aUnidadSistema = "";
     aContenido     = "";

     &enSwIOCorrecta = 0;

     &eaErrorFirma   = "";
     &eaBaseFirmador = "";
     &eaFEOrigen     = "";
     &eaFEDestino    = "";
     nSwFirmado = 0;
     aAux = "";

     aGuarda1 = "";
     aGuarda2 = "";
     nSwFirmaOK = 0;
     {1}aLocal = "";
     aBaseLocal = "";
     aBaseBin = "";
     aBaseDll = "";

     &eaNomFirmador = "";
     aBorra = "";
     &enSwNuevoMD5 = 0;
     aEjecuta = "";


     aMensaje = "";
     nCampoD        = 0;
     nCampoH        = 0;
     nPanta0        = 0;
     nVentana       = 0;
     nVentz6        = -1;
     nPos1          = 0;
     nPos2          = 0;
     nRango         = 0;
     nBoton1        = 0;
     nPausa         = 0;
     nPosDRamas     = 0;
     nPosHRamas     = 0;
     nPosDGrid      = 0;
     nPosHGrid      = 0;
     nGridTree      = -1;
     nBtnGuarda     = -1;

     aPathRemoto    = "";
     aSystem        = "";
     aEsc1          = "";
     aEsc2          = "";
     aRetu          = "";
     aTecla         = "";
     aAlfa          = "";
     aHora          = "";

     aIPRemoto      = "";
     aBase          = "";
     aMas           = "";

     {-1}aVent      = "";
         aVent1     = 0;
         aVent2     = 0;
         aVent3     = 0;
         aVent4     = 0;
         aVent5     = "";

     &enModo        = 0;
     &eaClave       = "";
     &enPassword    = 0;
     &enDocumento   = 0;
     &eaFichero     = "";
     &enError       = 0;
     &enGrupo      = 0;
     &enSubGrupo   = 0;
     &enTipo       = 0;
     &eaOrigen        = "";
     &eaDestino       = "";
|FIN;

|INCLUYE incvista;

|PROCESO EnviaUno;
|FPRC;

|| ------------------------------------------------------------------------
|| PROCESOS DE ASIGNACION DE MENUS
|| ------------------------------------------------------------------------

|PROCESO BajaPanta0_1;
     |SELECT #1#0;
     #0#0  = 1;
|FPRC;

|PROCESO AltaPanta0_1;
     #0#0  = 999999999;
|FPRC;

|PROCESO TraeLocalExeFirmar;
   |ESPECIFICA "RBASS", aLocal;
   aBaseLocal = aLocal;

   aBaseBin = aBaseLocal + "bin/";
   eaDestino = aBaseBin + "aeatfact.dll";

   enSwNuevoMD5 = 0;
   |DBASE aBase;
   |QBF aBase;
   aBaseDll = aBase + "dlls/";
   eaOrigen = aBaseDll + "aeatfact.dll";
   |HAZ EnviaFicheroConMD5;
   |SI enSwNuevoMD5 = 1;
       nError = 1;
       |PARA nCaracter = 67;  |SI nCaracter [ 90;  |HACIENDO nCaracter = nCaracter + 1;
             aUnidadSistema = &nCaracter + ":";
             aAbre   = aUnidadSistema + "\windows\system32\regsvr32.exe";
             |XABRE "EC", aAbre, nHandleEjec;
             |SI FSalida ] 0;  nError = 0;  |SAL;  |FINSI;
       |SIGUE;

       |SI nError = 0;
           aEjecuta = aUnidadSistema + "\windows\system32\regsvr32.exe " + aBaseBin + "aeatfact.dll";
           |RSYSTEM aEjecuta;
       |FINSI;
   |FINSI;

   eaDestino = aBaseBin + "dsaeatfact.exe";
   eaOrigen = aBaseDll + "dsaeatfact.exe";
   |HAZ EnviaFicheroConMD5;

   eaDestino = aBaseBin + "dsabreextension.exe";
   eaOrigen = aBaseDll + "dsabreextension.exe";
   |HAZ EnviaFicheroConMD5;
|FPRC;

|PROCESO PresentaDocumento;
     |XABRE "EC", eaDestino, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     ||Historico
     |HORA aHora;
     aUsuario = Usuario % 810;
     |ABRE #flowm006;
     #flowm006#0  = #dsarm005#0;
     #flowm006#1  = 99999;
     |LEE 000#flowm006.];
     |SI FSalida = 0;
          |LEE 000#flowm006.a;
     |SINO;
          |LEE 000#flowm006.u;
     |FINSI;
     |SI FSalida = 0; |Y #flowm006#0  = #dsarm005#0;
          nLinea = #flowm006#1 + 1;
          nSitu = #flowm006#2;
     |SINO;
          nSitu = 0;
          nLinea = 1;
     |FINSI;
     |DDEFECTO #flowm006;
     #flowm006#0  = #dsarm005#0;
     #flowm006#1  = nLinea;
     |LEE 101#flowm006.=;
     |SI FSalida ! 0; |GRABA 020#flowm006.b; |FINSI;
     #flowm006#2  = nSitu;
     #flowm006#3  = ~;
     #flowm006#4  = aHora;
     #flowm006#5  = aUsuario;
     aDocHis = ("000000000" + #dsarm005#0) % -109;
     #flowm006#6  = "VISUALIZADO DOCUMENTO: " + aDocHis;

     enNumRegFlow = #flowm006#0;
     enSituFlow = #flowm006#2;
     |HAZ DimeDesFlow;
     #flowm006#7 = eaDesSituFlow;

     |GRABA 020#flowm006.a;
     |LIBERA #flowm006;
     |CIERRA #flowm006;

     aEjecuta = "START " + eaUnidad + ":\DOCUMENTOS\DSARCHI\dsabreextension ";
     aEjecuta = aEjecuta + eaDestino;
     aEjecuta = aEjecuta + " " + eaUnidad + ":\DOCUMENTOS\DSARCHI\" + "fin" + Usuario + ".txt";

     aAlfa  = eaUnidad + ":\DOCUMENTOS\DSARCHI\" + "fin" + Usuario + ".txt";
     |RFBORRA aAlfa;

     |RSYSTEM aEjecuta;
     aAlfa  = eaUnidad + ":\DOCUMENTOS\DSARCHI\" + "fin" + Usuario + ".txt";
     |PARA;  |SI;  |HACIENDO;
             |XABRE "EC", aAlfa, nHandle;
             |SI FSalida ] 0;
                 |SAL;
             |FINSI;

             |PARA nPausa = 1; |SI nPausa < 10000; |HACIENDO nPausa = nPausa + 1;
             |SIGUE;
             |PULSATECLA;
     |SIGUE;
     |PULSATECLA;

     aAlfa  = eaUnidad + ":\DOCUMENTOS\DSARCHI\" + "fin" + Usuario + ".txt";
     |RFBORRA aAlfa;
|FPRC;

|PROCESO EditaDocumento;
     |SI #dsarm001#7 = "N";
          |HAZ DimeUnidad;
     |SINO;
          eaUnidad = "c";
     |FINSI;

     |DBASE aBase;
     |QBF aBase;

     aAuxNom = #dsarm005#9;
     |QBF aAuxNom;
     |SI aAuxNom = "-DOCUMENTOWEB-";
          eaDestino = eaUnidad + ":\DOCUMENTOS\DSARCHI\" + "dsabreweb.exe";
          eaOrigen = aBase + "dlls/dsabreweb.exe";
          |HAZ EnviaFicheroConMD5;

          aUrl = #dsarm001#162;
          |QBF aUrl;
          |SI aUrl = "";
               aMensaje = "0000Url para llamadas Asterisk NO definido. Proceso cancelado.";
               |MENAV aMensaje;
               |ACABA;
          |FINSI;
          aClave = #dsarm005#23;
          aClave = aClave - "CLAVE: ";
          |QBF aClave;

          aEjecuta = "START " + eaUnidad + ":\DOCUMENTOS\DSARCHI\dsabreweb.exe " + aUrl + aClave;
          |RSYSTEM aEjecuta;

          |ACABA;
     |FINSI;

     eaDestino = eaUnidad + ":\DOCUMENTOS\DSARCHI\" + "dsabreextension.exe";
     eaOrigen = aBase + "dlls/dsabreextension.exe";
     |HAZ EnviaFicheroConMD5;


     nSwFirmado = 0;
     aAux = #0#9;
     |QBF aAux;
     aAux = aAux % -104;
     |SI aAux = ".fir"; |O aAux = ".FIR";
          nSwFirmado = 1;
     |FINSI;

     aFichero = #0#7;   |QBF aFichero;

     aFicAux = #dsarm005#96;
     |QBF aFicAux;
     |SI aFicAux ! "";
          nSwVerDocPDF = 1;
     |SINO;
          nSwVerDocPDF = 0;
     |FINSI;

     |SI nSwVerDocPDF = 1;
         aAuxPath = aFichero;

         aFichero = aFichero + aFicAux;
         ||Voy a verificar si existe o no el fichero.
         |SI #1#7 = "N";
               |XABRE "E", aFichero, nHandleX;
               |SI FSalida < 0;
                    nSwVerDocPDF = 0;
                    aFichero = aFichero + #0#9;
               |FINSI;
         |SINO;
               |XABRE "CE", aFichero, nHandleX;
               |SI FSalida < 0;
                    nSwVerDocPDF = 0;
                    aFichero = aFichero + #0#9;
               |FINSI;
         |FINSI;
     |SINO;
         aFichero = aFichero + #0#9;
     |FINSI;
     |QBF aFichero;


     eaOrigen  = aFichero;
     |SI nSwVerDocPDF = 1;
          eaDestino = aPathRemoto + "\" + aFicAux;
     |SINO;
          eaDestino = aPathRemoto + "\" + #0#9;
     |FINSI;
     |QBF eaDestino;

     aIPRemoto   = "";
     |IP_REMOTO aIPRemoto;

     |SI #1#7 = "N";
         |SI aIPRemoto ! "";
             |ENVIA_FICHERO eaOrigen, eaDestino;
         |SINO;
             |COPIA_FICHERO eaOrigen, eaDestino;
         |FINSI;
     |SINO;
         |RCOPIA_FICHERO eaOrigen, eaDestino;
     |FINSI;

     |SI aIPRemoto = "";
         |XABRE "E", eaDestino, nHandle;
     |SINO;
         |XABRE "EC", eaDestino, nHandle;
     |FINSI;

     |SI FSalida < 0;
         aMensaje = "0000No puedo localizar el documento. Debe haber sido borrado o movido de carpeta";
         |MENAV aMensaje;
         |ACABA;
     |FINSI;

     |SI nSwFirmado = 1;
          aGuarda1 = eaOrigen;
          aGuarda2 = eaDestino;

          |HAZ TraeLocalExeFirmar;
          nSwFirmaOK = 0;
          eaErrorFirma = "";
          eaNomFirmador = "";
          eaBaseFirmador = aBaseBin;
          |SI #1#7 = "N";
               |HAZ DimeUnidad;
               eaFEOrigen = eaUnidad + ":\documentos\dsarchi\" + #0#9;
          |SINO;
               eaFEOrigen = eaUnidadBasico + "\documentos\dsarchi\" + #0#9;
          |FINSI;
          |QBF eaFEOrigen;
          aAux = aAux % -104;
          |SI aAux = ".fir";
               eaFEDestino = eaFEOrigen - ".fir";
          |FINSI;
          |SI aAux = ".FIR";
               eaFEDestino = eaFEOrigen - ".FIR";
          |FINSI;
          |HAZ RecuperaDocFirmado;
          |SI eaErrorFirma ! "";
               aMensaje = "0000Documento Firmado." + &13;
               eaAlfa = eaErrorFirma;
               |HAZ QuitaRaros;
               eaErrorFirma = eaAlfa;
               aMensaje = aMensaje + "Error: " + eaErrorFirma;
               |MENAV aMensaje;
               |ACABA;
          |SINO;
               |SI eaNomFirmador ! "";

                    aAux = eaNomFirmador % 4;
                    aMensaje = "0000Documento Firmado." + &13;
                    aMensaje = aMensaje + "Emisor: " + aAux;
                    |MENAV aMensaje;
               |FINSI;
               nSwFirmaOK = 1;
               aGuarda2 = eaFEDestino;
          |FINSI;

          eaOrigen = aGuarda1;
          eaDestino = aGuarda2;
     |FINSI;


     |VENTANA 0501, 2999, -1, 16, "Visualizando Documento";
     nVentz6  = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentz6;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PINPA #0#908;
     |PINDA #0#908;
     |PTEC 802;
     |PAUSA;

     |HAZ PresentaDocumento;

     |PULSATECLA;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentz6;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVentz6;

     |SI nSwFirmado = 1;
          |SI #dsarm001#181 = "S";
               ||Ahora controlamos si borramos o no la cache.
               aBorra = eaDestino + ".fir";
               |RFBORRA aBorra;
          |FINSI;
     |SINO;
          |SI nSwVerDocPDF = 0;
               enSwIOCorrecta = 0;
               |HAZ RecibeConMD5;
               |SI enSwIOCorrecta = 0;
                    |MENAV "0000 Ha habido un error al transferir el documento al servidor. Revise Permisos y espacio disponible.";
                    |ACABA;
               |FINSI;

               aIPRemoto = "";
               |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

               |SI #1#7 = "N";  |Y aIPRemoto ! "";
                   |XABRE "E", eaOrigen, nHandle;
               |SINO;
                   |XABRE "EC", eaOrigen, nHandle;
               |FINSI;

               |SI FSalida < 0;
                   |MENAV "0000 Ha habido un error al transferir el documento al servidor. Intentelo de nuevo";
               |FINSI;
          |FINSI;
     |FINSI;

     |SI #dsarm001#181 = "S";
          ||Ahora controlamos si borramos o no la cache.
          |QBF eaDestino;
          |SI eaDestino ! "";
               |RFBORRA eaDestino;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO EventoPanta1;
     |SI #0#0 = 0;  |ACABA;  |FINSI;

     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#0=;
         |SI FSalida ! 0;  |DDEFECTO #0;  |FINSI;

         aFichero = #0#7;              |QBF aFichero;
         aFichero = aFichero + #0#9;   |QBF aFichero;
     |FINSI;
     |SI aFichero ! "";
          |ESTADO_CONTROL nBoton1,  "ENABLE";
     |SINO;
          |ESTADO_CONTROL nBoton1,  "DISABLE";
     |FINSI;

     |SI FSalida = 4;
         |LEE 000#0=;
         |SI FSalida = 0;
             |HAZ EditaDocumento;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO GridFicheroEntero;
     |SI #1#8 = "N";
         |C_V #0#2,  1, "N";
         |C_V #0#3,  1, "N";
         |C_V #0#45, 1, "N";
         |C_V #0#46, 1, "N";
     |FINSI;

     |ABRE #0;
     |LINEAL_SIMPLE #1#0, nRango, nPos1, nPos2, BajaPanta0_1, AltaPanta0_1, EventoPanta1;
     nGrid = FSalida;
     nFichero = 0;

     |FOCOTECLADO nGrid;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Inicio del Programa
|| ------------------------------------------------------

|PROCESO dsarm007;
     #905#0 = #7#1;
     |LEE 000#905=;
     |SI FSalida = 0;
         |PARA nCampo = 0;  |SI nCampo [ 98;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = #905nCampo;
         |SIGUE;
         |GRABA 020#0n;
         |LIBERA #0;
     |FINSI;
|FPRC;

|DEFBUCLE dsarm007;
     #7#1;
     ;
     enDocumento, 000000001;
     enDocumento, 999999999;
     be;
     NULL, dsarm007;
|FIN;

|PROCESO BorraAsociacionHis;
     ||Historico
     |HORA aHora;
     aUsuario = Usuario % 810;
     |ABRE #flowm006;
     #flowm006#0  = #7#0;
     #flowm006#1  = 99999;
     |LEE 000#flowm006.];
     |SI FSalida = 0;
          |LEE 000#flowm006.a;
     |SINO;
          |LEE 000#flowm006.u;
     |FINSI;
     |SI FSalida = 0; |Y #flowm006#0  = #7#0;
          nLinea = #flowm006#1 + 1;
          nSitu = #flowm006#2;
     |SINO;
          nSitu = 0;
          nLinea = 1;
     |FINSI;
     |DDEFECTO #flowm006;
     #flowm006#0  = #7#0;
     #flowm006#1  = nLinea;
     |LEE 101#flowm006.=;
     |SI FSalida ! 0; |GRABA 020#flowm006.b; |FINSI;
     #flowm006#2  = nSitu;
     #flowm006#3  = ~;
     #flowm006#4  = aHora;
     #flowm006#5  = aUsuario;
     aAsociado = ("000000000" + #7#1) % -109;
     #flowm006#6  = "BORRADA ASOCIACION CON " + aAsociado;

     enNumRegFlow = #flowm006#0;
     enSituFlow = #flowm006#2;
     |HAZ DimeDesFlow;
     #flowm006#7 = eaDesSituFlow;

     |GRABA 020#flowm006.a;
     |LIBERA #flowm006;
     |CIERRA #flowm006;
|FPRC;

|PROCESO Boton1;
     |CONFI "0000N Seguro que desea borrar la asociacion del documento";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |ABRE #7;
     #7#0 = enDocumento;
     #7#1 = #0#0;
     |LEE 101#7=;
     |SI FSalida = 0;
         |BORRA 020#7a;
         |LIBERA #7;
     |FINSI;
     |HAZ BorraAsociacionHis;

     #7#0 = #0#0;
     #7#1 = enDocumento;
     |LEE 101#7=;
     |SI FSalida = 0;
         |BORRA 020#7a;
         |LIBERA #7;
     |FINSI;
     |CIERRA #7;
     |HAZ BorraAsociacionHis;

     |BORRA 020#0a;
     |LIBERA #0;

     ||ARA
     |SI nSwArbolActivo = 0;
          |REFRESCACONTROL nGrid;
     |SINO;
          |HAZ Boton0_11;
     |FINSI;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     enTemporal = 1;
     |HORA aHora;
     aAlfa = ((aHora % 102) + (aHora % 402) + (aHora % 702) + Usuario) % 108;
     |NOME_DAT #0 aAlfa;
     |ABRE #0;  |CIERRA #0;  |DELINDEX #0;

     |DBASE aMas;  |QBF aMas;
     aMas = aMas + "def/dsarm005.mas";
     |CARGA_DEF aMas, dsarm005@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |ABRE #905;
     |ABRE #0;
     |BUCLE dsarm007;
     |CIERRA #905;

     |DESCARGA_DEF #dsarm005@;

     FSalida = 2999;
|FPRC;

|PROCESO Tipo90;  |TIPO 90;
     |CIERRA #0;
     |DELINDEX #0;

     |CIERRA #7;

     enTemporal = 0;
|FPRC;

|PROCESO LeeDocumento;
     |SI nFichero = 0;
         |SELECT #1#dsarm005;
         #dsarm005#0 = #dsarm055#0;
         |LEE 000#dsarm005.=;
         |SI FSalida = 0;
             |ESTADO_CONTROL nBoton1, "ENABLE";
         |SINO;
             |ESTADO_CONTROL nBoton1, "DISABLE";
             |DDEFECTO #dsarm005;
         |FINSI;

         |SELECT #8#dsarm005;
         |LEE 000#dsarm005.=;
         aFichero = #dsarm005#7;              |QBF aFichero;
         aFichero = aFichero + #dsarm005#9;   |QBF aFichero;
     |SINO;
         |SELECT #1#905;
         #905#0 = #dsarm055#0;
         |LEE 000#905=;
         |SI FSalida ! 0;  |DDEFECTO #905;  |FINSI;

         |SELECT #8#905;
         |LEE 000#905=;

         aFichero = #905#7;              |QBF aFichero;
         aFichero = aFichero + #905#9;   |QBF aFichero;
     |FINSI;

     |SI aFichero ! "";
         nEntroPDatos = 1;
         |ESTADO_CONTROL nBoton1,  "ENABLE";
     |SINO;
         |ESTADO_CONTROL nBoton1,  "DISABLE";
     |FINSI;
|FPRC;

|PROCESO EventoGridTree;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#dsarm055.=;

         |HAZ LeeDocumento;
     |FINSI;

     |SI FSalida = 4;
         |SI nEntroPDatos = 1;
             |HAZ EditaDocumento;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO BajaDSARM055_2;
     #dsarm055#1  = #dsarm056#5;
     #dsarm055#0  = 0;
|FPRC;

|PROCESO AltaDSARM055_2;
     #dsarm055#1  = #dsarm056#5;
     #dsarm055#0  = 999999999;
|FPRC;

|PROCESO BajaDSARM055_3;
     #dsarm055#1  = #dsarm056#5;
     #dsarm055#0  = 0;
     #dsarm055#2  = "";
|FPRC;

|PROCESO AltaDSARM055_3;
     #dsarm055#1  = #dsarm056#5;
     #dsarm055#0  = 999999999;
     #dsarm055#2  = "z" * 135;
|FPRC;

|PROCESO BajaDSARM055_4;
     #dsarm055#1  = #dsarm056#5;
     #dsarm055#0  = 0;
     #dsarm055#2  = "z" * 135;
|FPRC;

|PROCESO AltaDSARM055_4;
     #dsarm055#1  = #dsarm056#5;
     #dsarm055#0  = 999999999;
     #dsarm055#2  = "";
|FPRC;

|PROCESO CreaGrid;
     |DBASE aBase;  |QBF aBase;
     eaPathDef     = aBase + "def/";
     eaFichRegedit = "dsarm055";
     |HAZ BorraDelRegedit;

     |C_V #dsarm055#4,  1, "S";
     |C_V #dsarm055#5,  1, "S";
     |C_V #dsarm055#6,  1, "S";
     |C_V #dsarm055#7,  1, "S";

     nNumCamp = #8#12;
     |SI #dsarm056#6 ! "                    ";
         |ABRE #dsarm059;
         #dsarm059#0 = #dsarm056#6;
         |LEE 000#dsarm059.=;
         |CIERRA #dsarm059;

         nNumCamp = #dsarm059#1;
     |FINSI;

     |SI nNumCamp = 1;
         |C_V #dsarm055#4,  1, "N";
         |C_V #dsarm055#5,  1, "N";
         |C_V #dsarm055#6,  1, "N";
         |C_V #dsarm055#7,  1, "N";
     |FINSI;

     |SI nNumCamp = 2;
         |C_V #dsarm055#5,  1, "N";
         |C_V #dsarm055#6,  1, "N";
         |C_V #dsarm055#7,  1, "N";
     |FINSI;

     |SI nNumCamp = 3;
         |C_V #dsarm055#6,  1, "N";
         |C_V #dsarm055#7,  1, "N";
     |FINSI;

     |SI nNumCamp = 4;
         |C_V #dsarm055#7,  1, "N";
     |FINSI;
|FPRC;

|PROCESO EventoTree;
     |SI FEntrada = 0;
         nEntroPDatos = 0;
         aAlfa        = Contenido;  |QBF aAlfa;
         aTexto       = "";
         aLong        = aAlfa % 0;
         nLong        = aLong;
         nCargar      = 0;
         |PARA nPosi  = 1;  |SI nPosi [ nLong;  |HACIENDO nPosi = nPosi + 1;
               nPos = (nPosi * 100) + 1;
               aCarac = aAlfa % nPos;

               |SI nCargar = 1;
                   aTexto = aTexto + aCarac;
               |FINSI;

               |SI aCarac = ":";  nCargar = 1; |FINSI;
         |SIGUE;

         aContenido = aTexto;
     |FINSI;

     |ESTADO_CONTROL nBoton1,  "DISABLE";

     |SI nGridTree ! -1;
         |CIERRA #dsarm055;
         |DESTRUYECONTROL nGridTree;
         |FIN_CONTROL_WINDOWS nBtnGuarda;
         nGridTree = -1;
     |FINSI;

     aAlfa = aContenido;  |QBF aAlfa;
     |SI aAlfa = "";  |ACABA;  |FINSI;

     |ABRE #dsarm056;
     |SELECT #2#dsarm056;
     #dsarm056#5 = aContenido;
     |LEE 000#dsarm056.=;
     |SI FSalida = 0;
         eaIdent = #dsarm056#7; |QBF eaIdent;

         |SI eaIdent = "";
             |CIERRA #dsarm008;
             |CIERRA #dsarm056;

             eaCodVista = #dsarm008#0;
             |SUB_EJECUTA "dsarz050";

             |ABRE #dsarm008;
             |ABRE #dsarm056;

             #dsarm008#0 = eaCodVista;
             |LEE 000#dsarm008.=;

             |SELECT #2#dsarm056;
             #dsarm056#5 = aContenido;
             |LEE 000#dsarm056.=;
             |SI FSalida ! 0;  |DDEFECTO #dsarm056;  |FINSI;

             eaIdent = #dsarm056#7; |QBF eaIdent;
         |FINSI;

         |HAZ CreaGrid;

         |ABRE #dsarm055;
         nRango     = 4 + 8 + 16 + 32 + 128;

         |CONTROL_SIMPLE nPanta0, "BOTON,{{217}} ", 2102, 2103, GuardaGrid;
         nBtnGuarda = FSalida;

         |SI #dsarm008#30 = "N";
             |SI #dsarm008#28 = 0;
                 |LINEAL_SIMPLE #2#dsarm055, nRango, nPosDGrid, nPosHGrid, BajaDSARM055_2, AltaDSARM055_2, EventoGridTree;
             |SINO;
                 |LINEAL_SIMPLE #3#dsarm055, nRango, nPosDGrid, nPosHGrid, BajaDSARM055_3, AltaDSARM055_3, EventoGridTree;
             |FINSI;
         |SINO;
             |LINEAL_SIMPLE #4#dsarm055, nRango, nPosDGrid, nPosHGrid, BajaDSARM055_4, AltaDSARM055_4, EventoGridTree;
         |FINSI;
         nGridTree = FSalida;
     |FINSI;
     |CIERRA #dsarm056;

     |PULSATECLA;
|FPRC;


|PROCESO CreaArbol;
     nSwArbolActivo = 1;

     |ESTADO_CONTROL nBoton1, "HIDE";

     |SI nBoton0_8 ! -1;   |ESTADO_CONTROL nBoton0_8,  "HIDE";  |FINSI;
     |SI nBoton0_9 ! -1;   |ESTADO_CONTROL nBoton0_9,  "HIDE";  |FINSI;
     |SI nBoton0_10 ! -1;  |ESTADO_CONTROL nBoton0_10, "HIDE";  |FINSI;
     |SI nBoton0_11 ! -1;  |ESTADO_CONTROL nBoton0_11, "HIDE";  |FINSI;
     |SI nEstadoGrid ! -1;  |ESTADO_CONTROL nGrid,     "HIDE";  |FINSI;

     |HAZ QuitaBotonSalir;

     |VENTANA 0501, 0550, -1, 16, "Procesando trabajo. Esto puede tardar varios minutos.";
     nVentTrab = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentTrab;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;
     |PAUSA;

     |SI nGridTree ! -1;
         |CIERRA #dsarm055;
         |DESTRUYECONTROL nGridTree;
         |FIN_CONTROL_WINDOWS nBtnGuarda;
         nGridTree = -1;
     |FINSI;

     nTDocumentos = 0;
     |HAZ CargaTemporalVista_Z5;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentTrab;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVentTrab;

     |SI nTree ! -1;
         |FIN_CONTROL_WINDOWS nTree;
         nTree = -1;
     |FINSI;

     eaAlfa = "TREECTRL," + #8#0;  |QBF eaAlfa;
     eaAlfa = eaAlfa + " (" + nTDocumentos + " documentos)";
     eaAlfa = eaAlfa + " {{" + aAlfa + "}}";

     |CONTROL_SIMPLE nPanta0, eaAlfa, nPosDRamas, nPosHRamas, EventoTree;
     nTree = FSalida;

     |ESTADO_CONTROL nTree, "HIDE";

     |ESTADO_CONTROL nBoton1,  "SHOW";
     |SI nBoton0_8   ! -1;  |ESTADO_CONTROL nBoton0_8,  "SHOW";  |FINSI;
     |SI nBoton0_9   ! -1;  |ESTADO_CONTROL nBoton0_9,  "SHOW";  |FINSI;
     |SI nBoton0_10  ! -1;  |ESTADO_CONTROL nBoton0_10, "SHOW";  |FINSI;
     |SI nBoton0_11  ! -1;  |ESTADO_CONTROL nBoton0_11, "SHOW";  |FINSI;
     |SI nEstadoGrid ! -1;  |ESTADO_CONTROL nGrid,      "SHOW";  |FINSI;

     |HAZ PonBotonSalir;

     |HAZ CreaGrid;

     |ABRE #dsarm055;
|FPRC;

|PROCESO PresentaArbol;
     |PULSATECLA;

     |ESTADO_CONTROL nTree, "SHOW";

     |SI nGridTree ! -1;
         |ESTADO_CONTROL nGridTree, "SHOW";
         |ESTADO_CONTROL nBtnGuarda, "SHOW";
     |FINSI;

     |SI #dsarm001#184 = "S";
         #dsarm055#0 = aBusca;
         |LEE 000#dsarm055.=;
         |SI FSalida = 0;
              aBusca = #dsarm055#1;

              sT_nID   = nTree;
              sT_nOper = 3;
              sT_aData = "[" + aBusca;
              |ESPECIFICA "COMMANDCONTROL", sTree;
              aRefresca = ":" + FSalida;
         |FINSI;
     |FINSI;

     sT_nID   = nTree;
     sT_nOper = 2;
     |SI aRefresca = "";
         sT_aData = "1:";
     |SINO;
         sT_aData = aRefresca;
     |FINSI;

     |ESPECIFICA "COMMANDCONTROL", sTree;
     |PULSATECLA;

     |FOCOTECLADO nTree;
|FPRC;

||TODO CCC botones Arbol

|PROCESO Boton0_8;
     |SI nFichero = 0;
         aBusca = ("000000000" + #dsarm005#0) % -109;
     |SINO;
         aBusca = ("000000000" + #905#0) % -109;
     |FINSI;

     aAlfa  = #8#0;  |QBF aAlfa;
     |SI aAlfa = "";
         |HAZ Boton0_10;

         |SI nError [ 0;  |ACABA;  |FINSI;
     |SINO;
          aUsuarioVista = Usuario % 810;
          |QBF aUsuarioVista;
          |ABRE #dsarm043;
          |DDEFECTO #dsarm043;
          #dsarm043#0 = aUsuarioVista;
          #dsarm043#1 = #dsarm008#0;
          |LEE 000#dsarm043.=;
          |SI FSalida = 0; |Y #dsarm043#2 = "N";
               |CIERRA #dsarm043;
               |HAZ Boton0_10;

               |SI nError [ 0;
                    |ACABA;
               |FINSI;
          |SINO;
               |CIERRA #dsarm043;
               |SI enBusqueda = 1;
                   |HAZ CreaArbol;
               |FINSI;
          |FINSI;
     |FINSI;

     enBusqueda = 0;

     |ESTADO_CONTROL nGrid, "HIDE";
     nEstadoGrid = -1;

     aTipoVista = "A";

     |HAZ PresentaArbol;

     |SI nBoton0_8 ! -1;
         |FIN_CONTROL_WINDOWS nBoton0_8;
         nBoton0_8 = -1;

         |CONTROL_SIMPLE 0, "BOTON,{{155}} Vistas por Lineal", 2702, 2720, Boton0_9;
         nBoton0_9 = FSalida;

         |CONTROL_SIMPLE 0, "BOTON,{{137}} Cambiar Vistas", 2742, 2760, Boton0_10;
         nBoton0_10 = FSalida;

         |CONTROL_SIMPLE 0, "BOTON,{{197}} Actualizar Arbol", 2722, 2740, Boton0_11;
         nBoton0_11 = FSalida;
     |FINSI;
|FPRC;

|PROCESO Boton0_9;
     nSwArbolActivo = 0;

     aTipoVista = "L";
     aNomVista = "";

     |PULSATECLA;
     |SI nTree ! -1;
         |ESTADO_CONTROL nTree, "HIDE";
     |FINSI;

     |SI nGridTree ! -1;
         |ESTADO_CONTROL nGridTree, "HIDE";
         |ESTADO_CONTROL nBtnGuarda, "HIDE";
     |FINSI;

     |PULSATECLA;

     |ESTADO_CONTROL nGrid, "SHOW";
     nEstadoGrid = nGrid;

     |REFRESCACONTROL nGrid;
     |FOCOTECLADO nGrid;
     |PULSATECLA;

     |FIN_CONTROL_WINDOWS nBoton0_9;
     nBoton0_9 = -1;

     |FIN_CONTROL_WINDOWS nBoton0_10;
     nBoton0_10 = -1;

     |FIN_CONTROL_WINDOWS nBoton0_11;
     nBoton0_11 = -1;

     |CONTROL_SIMPLE 0, "BOTON,{{155}} Vistas por Arbol", 2702, 2720, Boton0_8;
     nBoton0_8 = FSalida;
|FPRC;

|PROCESO dsarm008;
     |DDEFECTO #dsarm043;
     #dsarm043#0 = aUsuarioVista;
     #dsarm043#1 = #dsarm008#0;
     |LEE 000#dsarm043.=;
     |SI FSalida = 0; |Y #dsarm043#2 = "N";
          |ACABA;
     |FINSI;

     nContador = nContador + 1;

     eaAlfa     = #8#0;  |QBF eaAlfa;
     aOpcion    = eaAlfa;

     |HAZ QuitaRaros;
     aPopup    = eaAlfa;

     IaPopup   = IaPopup  + 1;
     IaOpcion  = IaOpcion + 1;
|FPRC;

|DEFBUCLE dsarm008;
     #8#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dsarm008;
|FIN;


|PROCESO Boton0_10;
     nContador = 0;
     aPopup1   = nGrid;

     |ABRE #dsarm043;
     aUsuarioVista = Usuario % 810;
     |QBF aUsuarioVista;

     IaPopup  = aPopup3  <-;
     IaOpcion = aOpcion1 <-;
     |BUCLE dsarm008;
     aPopup2 = nContador;

     |CIERRA #dsarm043;

     |SI nContador = 0;
          aMensaje = "0000No tiene permiso para ver las vistas por  rbol";
          |MENAV aMensaje;
          nBoton0_9 = -1;
          aTipoVista = "L";
          |ACABA;
     |FINSI;

     IaPopup  = aPopup1  <-;
     IaOpcion = aOpcion1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     nError = FSalida;

     |SI nError > 0;
         IaPopup  = aPopup3  <-;
         IaOpcion = aOpcion1 <-;

         nOpcion  = nError - 1;
         IaPopup  = IaPopup  + nOpcion;
         IaOpcion = IaOpcion + nOpcion;
         aAlfa    = aOpcion;

         |PULSATECLA;

         #8#0 = aAlfa;
         |LEE 000#8=;
         |SI FSalida ! 0;  |ACABA;  |FINSI;

         aNomVista = aAlfa;
         |QBF aNomVista;

         |HAZ CreaArbol;
         enBusqueda = 0;
     |FINSI;

     |SI nBoton0_10 = -1;  |Y enBusqueda = 0;
         |ACABA;
     |FINSI;

     |SI enBusqueda = 1;
         |HAZ CreaArbol;
     |FINSI;

     enBusqueda = 0;

     |HAZ PresentaArbol;
|FPRC;

|PROCESO Boton0_11;
     enBusqueda = 0;
     |HAZ CreaArbol;
     |HAZ PresentaArbol;
|FPRC;

|PROGRAMA;
     |ABRE #dsarm008;
     |HAZ LeeUnidadBasico;

     aParametro = PARAMETRO;
     |LEE 000#0p;

     |ABRE #1;
     |LEE 000#1p;
     |SI FSalida ! 0;  |DDEFECTO #1;  |FINSI;
     |CIERRA #1;

     ||ASO
     |ABRE #100;
     #100#0 = Usuario % 810;
     |LEE 000#100=;
     |SI FSalida ! 0;
         |DDEFECTO #100;
     |FINSI;
     aTipoVista = #100#19; |QBF aTipoVista;
     aNomVista = #100#20;  |QBF aNomVista;

     aPathRemoto    = "";

     |SI #dsarm001#7 = "N";
          |HAZ DimeUnidad;
          aPathRemoto = eaUnidad + ":\DOCUMENTOS";              |RMKDIR aPathRemoto;
          aPathRemoto = eaUnidad + ":\DOCUMENTOS\DSARCHI";      |RMKDIR aPathRemoto;
     |SINO;
          aPathRemoto = eaUnidadBasico + "\DOCUMENTOS";              |RMKDIR aPathRemoto;
          aPathRemoto = eaUnidadBasico + "\DOCUMENTOS\DSARCHI";      |RMKDIR aPathRemoto;
     |FINSI;

     |PINPA #0#99;  nPanta0 = FSalida;

     |LEE 000#8p;
     |SI FSalida = 0;
          |CONTROL_SIMPLE 0, "BOTON,{{155}} Vistas por Arbol", 2702, 2720, Boton0_8;
          nBoton0_8 = FSalida;
     |FINSI;

     |CONTROL_SIMPLE 0, "BOTON,{{207}} Borrar Asociado", 2780, 2798, Boton1;
     nBoton1 = FSalida;

     nRango     = 4 + 8 + 16 + 32;
     nPos1      = 0602;
     nPos2      = 2698;
     nPosDRamas = 0602;
     nPosHRamas = 2098;
     nPosDGrid  = 2104;
     nPosHGrid  = 2698;

     |HAZ GridFicheroEntero;

     FSalida = 1;
     |HAZ EventoPanta1;

     #8#0 = aNomVista;
     |LEE 000#8=;
     |SI FSalida ! 0;  |DDEFECTO #8;  |FINSI;

     aNomVista = #8#0;
     |QBF aNomVista;
     |SI aTipoVista = "A"; |Y aNomVista ! "";
          |HAZ CreaArbol;
          |HAZ Boton0_8;
     |SINO;
          enBusqueda = 1;
     |FINSI;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::Salir," + nGrid;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |DESTRUYECONTROL nGrid;

     |PULSATECLA;

     ||ASO
     #100#0 = Usuario % 810;
     |LEE 101#100=;
     |SI FSalida ! 0;
         |DDEFECTO #100;
         #100#0 = Usuario % 810;
         |GRABA 020#100b;
     |FINSI;
     #100#19 = aTipoVista;
     #100#20 = aNomVista;
     |GRABA 020#100a;
     |LIBERA #100;
     |CIERRA #100;

     |FIN_CONTROL_WINDOWS nBoton1;
     |CIERRA #dsarm008;

     |HAZ BorraTempoVistaVS;
|FPRO;
