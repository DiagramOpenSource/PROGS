|FICHEROS;
     dsarz044;

     dsarm001;
     dpntm001;

     dpntm100;
     dpntm101;

     :contagen/canempre;

     linearep@;
     lineasop@;
|FIN;

|VARIABLES;
     aUsuario             = "";
     aEsc1                = "";
     aEsc2                = "";
     aRetu                = "";
     aTecla               = "";
     aAlfa                = "";
     aBase                = "";
     aPathNet             = "";
     aMas                 = "";
     aDef                 = "";
     aTempo1              = "";
     aTempo2              = "";

     nPanta939            = 0;
     nVentana             = 0;
     nVentTra             = 0;
     nCampo               = 0;
     nEmpresa             = 0;
     nPeriodo             = 0;
     nPdtesContR          = 0;
     nPdtesContS          = 0;
     nGridR               = -1;
     nGridS               = -1;
     nRango               = -1;
     nSalir               = -1;
     nSituacion           = 0;
     nLabelContR          = -1;
     nLabelContS          = -1;
     nLabel1              = -1;
     nLabel2              = -1;
     nLabel3              = -1;
     nLabel4              = -1;

     {-1}aVent            = "";
         aVent1           = 0;
         aVent2           = 0;
         aVent3           = 0;
         aVent4           = 0;
         aVent5           = "";

     &Tempo               = "";
     &eaTitulo            = "";
|FIN;

|PROCESO dpntm100;
     aAlfa = #dpntm100#15 % -102;

     #canempre#2  = #dpntm001#0;
     #canempre#26 = aAlfa;
     |LEE 000#canempre.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     #linearep@#0 = #canempre#2;
     #linearep@#1 = #canempre#3;
     |LEE 101#linearep@.=;
     |SI FSalida ! 0;
         |DDEFECTO #linearep@;
         #linearep@#0 = #canempre#2;
         #linearep@#1 = #canempre#3;
         #linearep@#2 = 2000 + #canempre#26;
         #linearep@#3 = #canempre#1;
         |GRABA 020#linearep@.b;
     |FINSI;
     #linearep@#4 = #linearep@#4 + 1;
     |GRABA 020#linearep@.a;
     |LIBERA #linearep@;

     nPdtesContR = nPdtesContR + 1;
|FPRC;

|DEFBUCLE dpntm100;
     #dpntm100#1;
     ;
          0;
     999999;
     ;
     NULL, dpntm100;
|FIN;

|PROCESO dpntm101;
     aAlfa = #dpntm101#15 % -102;

     #canempre#2  = #dpntm001#0;
     #canempre#26 = aAlfa;
     |LEE 000#canempre.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     #lineasop@#0 = #canempre#2;
     #lineasop@#1 = #canempre#3;
     |LEE 101#lineasop@.=;
     |SI FSalida ! 0;
         |DDEFECTO #lineasop@;
         #lineasop@#0 = #canempre#2;
         #lineasop@#1 = #canempre#3;
         #lineasop@#2 = 2000 + #canempre#26;
         #lineasop@#3 = #canempre#1;
         |GRABA 020#lineasop@.b;
     |FINSI;
     #lineasop@#4 = #lineasop@#4 + 1;
     |GRABA 020#lineasop@.a;
     |LIBERA #lineasop@;

     nPdtesContS = nPdtesContS + 1;
|FPRC;

|DEFBUCLE dpntm101;
     #dpntm101#1;
     ;
          0;
     999999;
     ;
     NULL, dpntm101;
|FIN;

|PROCESO dpntm001;
     |DBASS aBase;  |QBF aBase;

     aAlfa     = ("00000" + #dpntm001#0) % -105;
     aPathNet  = aBase + "despanet";           |MKDIR aPathNet;
     aPathNet  = aPathNet + "/" + aAlfa;       |MKDIR aPathNet;
     aPathNet  = aPathNet + "/fich";           |MKDIR aPathNet;
     aPathNet  = aPathNet + "/";

     |PATH_DAT #dpntm100#aPathNet#;
     |PATH_DAT #dpntm101#aPathNet#;

     |BUCLE dpntm100;
     |BUCLE dpntm101;
|FPRC;

|DEFBUCLE dpntm001;
     #dpntm001#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dpntm001;
|FIN;

|PROCESO Evento975;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#lineasop@.=;
     |FINSI;

     |SI FSalida = 4;
         |PULSATECLA;
         |PULSATECLA;
         |PULSATECLA;

         |ESTADO_CONTROL nGridR, "DISABLE";

         eaTitulo = "Empresa: " + (("00000" + #lineasop@#0) % -105) + " " + #lineasop@#3;
         aAlfa    = "|:contagen/gadz0002 canempr1 " + (("00000" + #lineasop@#0) % -105) + #lineasop@#1 + ";" + "S" + "GAD";
         |SUB_EJECUTA_NP aAlfa;

         |ESTADO_CONTROL nGridR, "ENABLE";

         nSalir   = 1;
         nEmpresa = #lineasop@#0;
         nPeriodo = #lineasop@#1;
         |PTEC 806;

         |PULSATECLA;
         |PULSATECLA;
         |PULSATECLA;
         |PULSATECLA;
     |FINSI;
|FPRC;

|PROCESO Evento974;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#linearep@.=;
     |FINSI;

     |SI FSalida = 4;
         |PULSATECLA;
         |PULSATECLA;
         |PULSATECLA;

         |ESTADO_CONTROL nGridR, "DISABLE";

         eaTitulo = "Empresa: " + (("00000" + #linearep@#0) % -105) + " " + #linearep@#3;
         aAlfa    = "|:contagen/gadz0002 canempr1 " + (("00000" + #linearep@#0) % -105) + #linearep@#1 + ";" + "R" + "GAD";
         |SUB_EJECUTA_NP aAlfa;

         |ESTADO_CONTROL nGridR, "ENABLE";

         nSalir   = 1;
         nEmpresa = #linearep@#0;
         nPeriodo = #linearep@#1;
         |PTEC 806;

         |PULSATECLA;
         |PULSATECLA;
         |PULSATECLA;
         |PULSATECLA;
     |FINSI;
|FPRC;

|PROCESO DibujaPantalla;
     |ABRE #dsarm001;
     |DDEFECTO #dsarm001;
     |LEE 000#dsarm001.p;
     |CIERRA #dsarm001;

     nPdtesContR = 0;
     nPdtesContS = 0;

     |SI nGridR ! -1;
        |DESTRUYECONTROL nGridR;
        |DESTRUYECONTROL nGridS;

        |FIN_CONTROL_WINDOWS nLabelContR;
        |FIN_CONTROL_WINDOWS nLabelContS;
        |FIN_CONTROL_WINDOWS nLabel1;
        |FIN_CONTROL_WINDOWS nLabel2;
        |FIN_CONTROL_WINDOWS nLabel3;
        |FIN_CONTROL_WINDOWS nLabel4;

        |CIERRA #linearep@;   |DELINDEX #linearep@;
        |CIERRA #lineasop@;   |DELINDEX #lineasop@;

        Tempo = aTempo1;  |HAZ BoraTempo;
        Tempo = aTempo2;  |HAZ BoraTempo;

        nGridR = -1;
        nGridS = -1;
     |FINSI;

     |HAZ CreaTempo;  |NOME_DAT #linearep@#Tempo#;  aTempo1 = Tempo;
     |HAZ CreaTempo;  |NOME_DAT #lineasop@#Tempo#;  aTempo2 = Tempo;

     |ABRE #linearep@;
     |ABRE #lineasop@;
     |ABRE #canempre;
     |SELECT #4#canempre;

     |BUCLE dpntm001;

     |CIERRA #canempre;

     #linearep@#0 = nEmpresa;
     #linearep@#1 = nPeriodo;
     |LEE 000#linearep@.=;

     |REFRESCACONTROL nGridR;
     |FOCOTECLADO nGridR;

     |CONTROL_SIMPLE 0, "LABEL,{{15}}Procesos facturas emitidas", 0502, 0560, NULL;
     nLabel1 = FSalida;

     |CONTROL_SIMPLE 0, "LABEL,{{7}}Pulse doble-click en cada Empresa para proceder a su contabilizaci¢n", 0852, 0899, NULL;
     nLabel2 = FSalida;

     |SI nPdtesContR ! 0;
         aAlfa = "LABEL,{{13}}" + nPdtesContR + " registros pendientes de contabilizar";
     |SINO;
         aAlfa = "LABEL,{{16}}No hay registros pendientes de contabilizar";
     |FINSI;

     |CONTROL_SIMPLE 0, aAlfa, 0702, 0760, NULL;
     nLabelContR = FSalida;


     |CONTROL_SIMPLE 0, "LABEL,{{15}}Procesos facturas recibidas", 1902, 1960, NULL;
     nLabel3 = FSalida;

     |CONTROL_SIMPLE 0, "LABEL,{{7}}Pulse doble-click en cada Empresa para proceder a su contabilizaci¢n", 2252, 2299, NULL;
     nLabel4 = FSalida;

     |SI nPdtesContS ! 0;
         aAlfa = "LABEL,{{13}}" + nPdtesContS + " registros pendientes de contabilizar";
     |SINO;
         aAlfa = "LABEL,{{16}}No hay registros pendientes de contabilizar";
     |FINSI;

     |CONTROL_SIMPLE 0, aAlfa, 2102, 2160, NULL;
     nLabelContR = FSalida;

     nRango  = 2 + 4 + 8 + 16 + 32 + 128;

     |SI #dsarm001#182 = "1";
         |LINEAL_SIMPLE #1#linearep@, nRango, 0902, 1798, NULL, NULL, Evento974;
         nGridR   = FSalida;

         |LINEAL_SIMPLE #1#lineasop@, nRango, 2302, 3198, NULL, NULL, Evento975;
         nGridS   = FSalida;
     |SINO;
         |CONTROL_SIMPLE 0, "LABEL,{{4}}Desactivado", 0562, 0598, NULL;
         |CONTROL_SIMPLE 0, "LABEL,{{4}}Desactivado", 1962, 1998, NULL;

         |LINEAL_SIMPLE #1#linearep@, nRango, 0902, 1798, NULL, NULL, NULL;
         nGridR   = FSalida;

         |LINEAL_SIMPLE #1#lineasop@, nRango, 2302, 3198, NULL, NULL, NULL;
         nGridS   = FSalida;
     |FINSI;
|FPRC;

|PROGRAMA;
     |VENTANA 0501, 3399, -1, 16, "Panel de control contabilizaci¢n DESPABOX";
     nVentana = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     |DDEF aDef;
     aMas = aDef + "dsarw074.mas";

     |CARGA_DEF aMas, linearep@;
     |CARGA_DEF aMas, lineasop@;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          |HAZ DibujaPantalla;

          FSalida = "::Salir," + nGridR;
          |LEETECLA aTecla;

          |SI nSalir = 1;  aTecla = "";  |FINSI;

          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
          |SI aTecla = aRetu; |SAL;  |FINSI;

          nSalir = 0;
          |PULSATECLA;
     |SIGUE;

     |DESTRUYECONTROL nGridR;
     |CIERRA #linearep@;  |DELINDEX #linearep@;

     |DESTRUYECONTROL nGridS;
     |CIERRA #lineasop@;  |DELINDEX #lineasop@;

     Tempo = aTempo1;  |HAZ BoraTempo;
     Tempo = aTempo2;  |HAZ BoraTempo;

     |DESCARGA_DEF #lineasop@;
     |DESCARGA_DEF #linearep@;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVentana;
|FPRO;
