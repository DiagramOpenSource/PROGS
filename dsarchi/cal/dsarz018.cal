|FICHEROS;
     dsarz010;
     dsarz018 #0;

     dsarm000 #100;
     dsarm001 #1;
     dsarm002 #2;
     dsarm003 #3;
     dsarm004 #4;
     dsarm005 #5;
     dsarm100 #100;

     dpntm001 #801;
     dpntm002 #802;

     dsarw062  #62;      |

     reflejo2@ #1002;
     reflejo3@ #1003;
     reflejo4@ #1004;
|FIN;

|VARIABLES;
     nUltimo = 0;
     nUltimoDoc = 0;          ||Contador

     aOrigen = "";
     aDestino = "";
     aDirArchivados = "";
     aNomDocArchivado = "";

     aBaseDll = "";
     nPausa = 0;


     nTipoLabel = 0;
     aExtension = "";

     aCadOri = "";
     aCadFin = "";
     aLong = "";
     nLong = 0;
     aCampo = "";
     nNumSaca = 0;
     aLetra = "";


     nNumDoc = 0;
     nBorrados = 0;
     nVoy = 0;
     nAntes = 0;
     nIdImagen = -1;
     aImagen = "";
     aPosicion = "";
     aBmp = "";
     coord = "";
     nPIX = 0;
     alfa = "";

     nSwYaTa = 0;
     aGuarda = "";
     aPinta = "";
     aEfe5 = "";
     aAvPag = "";
     aRePag = "";
     aInicio = "";
     aFin = "";
     aArriba = "";
     aAbajo = "";
     aCtrlArriba = "";
     aCtrlAbajo = "";

     aTempo62 = "";
     nTotal = 0;
     nSwHayPtes = 0;
     nImagen = 0;
     aNombre = "";
     aPtes = "";
     aFichero = "";


     aDirBase = "";
     aPrograma = "";
     {1}aLocal = "";
     aEjecuta = "";
     aAbre = "";
     aPath = "";


     aIntParametro = "";
     aBorra = "";
     aLinea = "";

     nSwParametroFichero = 0;
     aResto = "";

     nSwObligatorios = 0;
     nSwDameTres     = 0;              ||Sirve para preguntar GRUPO/SUBGRUPO/TIPO
     nUltDoc         = 0;
     nVentana        = -1;
     nVentz1         = 0;
     nEmpresa        = 0;
     nEjercicio      = 0;
     nHandle         = 0;
     nDesdeGrupo     = 0;
     nHastaGrupo     = 0;
     nDesdeSub       = 0;
     nHastaSub       = 0;
     nSwADConta      = 0;
     nRefresca       = 0;
     nSalida         = 0;
     nPanta0         = 0;
     nBotonFoto      = 0;
     nRango          = 0;
     nPos1           = 0;
     nPos2           = 0;
     nGrid1          = -1;
     nGrid2          = -1;
     nGrid3          = -1;
     nBoton1         = -1;
     nBoton2         = -1;
     nBoton3         = -1;
     nBoton4         = -1;
     nBoton5         = -1;
     nBoton7         = -1;
     nBoton8         = -1;
     nBtnBor         = -1;
     nIdSalir        = -1;
     nVentz3         = 0;
     nCampo          = 0;
     nNumCampos      = 0;
     nErrorDESPANET  = 0;

     aLabel          = "";
     aEmpresa        = "";
     aEjercicio      = "";
     aAux            = "";
     aParametro      = "";
     aDirLocal       = "";
     aMensaje        = "";
     aBase           = "";
     aFotoOrigen     = "";
     aFotoDestino    = "";
     aExt            = "";
     aDocRemoto      = "";
     aDocServidor    = "";
     aIPRemoto       = "";
     aFoto           = "";
     aEsc1           = "";
     aEsc2           = "";
     aRetu           = "";
     aTecla          = "";
     aAlfa           = "";
     aUsuario        = "";
     aQueQuiero      = "";
     aMas            = "";
     aHora           = "";

     {1}aContiene    = "";

     {2}nNivel       = 0;

     {-1}aVent       = "";
         aVent1      = 0;
         aVent2      = 0;
         aVent3      = 0;
         aVent4      = 0;
         aVent5      = "";

     &enGrupo         = 0;
     &enSubGrupo      = 0;
     &enTipo          = 0;
     &enEscaneo       = 0;
     &enDocumento     = 0;
     &enDESPANET      = 0;
     &enGrupoTres     = 0;
     &enSubGTres      = 0;
     &enTipoTres      = 0;
     &enSwADConta     = 0;
     &enEmpADConta    = 0;
     &enEjeADConta    = 0;
     &enMueveDoc      = 0;
     &enGrupoPV       = 0;
     &enSubGrupoPV    = 0;
     &enTipoPV        = 0;
     &eaUnidadBasico  = "";
     &eaUnidad        = "";
     &eaCarpetaOrigen = "";
     &enPrevisualizar = 0;
     &eaOrigen        = "";
     &eaDestino       = "";
     &enErrorCaptura  = 0;
     &eaDocCaptura    = "";
     &eaDocPC         = "";
     &Tempo           = "";
     &enSwDesde9      = 0;
     &eaSwBorroOrigen = "";
|FIN;

|PROCESO Baja2;
     #2#0 = nDesdeGrupo;
|FPRC;

|PROCESO Alta2;
     #2#0 = nHastaGrupo;
|FPRC;

|PROCESO Baja3;
     #3#0 = #2#0;
     #3#1 = nDesdeSub;
|FPRC;

|PROCESO Alta3;
     #3#0 = #2#0;
     #3#1 = nHastaSub;
|FPRC;

|PROCESO Baja4;
     #4#0 = #3#0;
     #4#1 = #3#1;
     #4#2 = 1;
|FPRC;

|PROCESO Alta4;
     #4#0 = #3#0;
     #4#1 = #3#1;
     #4#2 = 999;
|FPRC;

|PROCESO PintaUltimo;
     |ABRE #5;
     |LEE 000#5u;
     |SI FSalida ! 0;  |DDEFECTO #5;  |FINSI;
     |CIERRA #5;

     nUltimo = #5#0;

     |ABRE #dsarm100;
     |DDEFECTO #dsarm100;
     |LEE 000#dsarm100.p;
     |SI FSalida = 0;
          nUltimoDoc = #dsarm100#1 + 1;
     |SINO;
          nUltimoDoc = 1;
     |FINSI;
     |SI nUltimoDoc > nUltimo;
          nUltimo = nUltimoDoc;
     |FINSI;
     |CIERRA #dsarm100;

     #0#6 = nUltimo;  |PINTA #0#6;
|FPRC;

|PROCESO ActUltDocCarpeta;
     |ABRE #5;
     |SELECT #2#5;
     #5#1 = #0#0;
     #5#2 = #0#2;
     #5#3 = #0#4;
     #5#0 = 999999999;
     |LEE 000#5];
     |SI FSalida = 0;
         |LEE 000#5a;
     |SINO;
         |LEE 000#5u;
     |FINSI;

     nUltDoc = 0;
     |SI FSalida = 0;  |Y #5#1 = #0#0;  |Y #5#2 = #0#2; |Y #5#3 = #0#4;
         nUltDoc = #5#47;
     |FINSI;
     |SELECT #1#5;
     |CIERRA #5;

     #dsarz018#7 = nUltDoc;
     |PINTA #dsarz018#7;
|FPRC;

|PROCESO Evento2;
     |SI FSalida = 1;  |O FSalida = 2;
         nSalida = FSalida;
         |LEE 000#2=;
         |SI FSalida = 0;
             #0#0 = #2#0;  |PINTA #0#0;
             #0#1 = #2#1;  |PINTA #0#1;
         |SINO;
             #0#0 = 0;  |PINTA #0#0;
             #0#1 = ""; |PINTA #0#1;
         |FINSI;

         |SI #1#8 = "S";
             nRefresca = 1;
             FSalida   = 1;
             |HAZ Evento3;
         |FINSI;

         |HAZ PintaUltimo;
         |HAZ ActUltDocCarpeta;
     |FINSI;
|FPRC;

|PROCESO Evento3;
     |SI FSalida = 1;  |O FSalida = 2;
         |SI nRefresca = 0;
             |LEE 000#3=;
         |SINO;
             #3#0 = #2#0;
             #3#1 = 9999;
             |LEE 000#3];
             |SI FSalida = 0;
                 |LEE 000#3a;
             |SINO;
                 |LEE 000#3u;
             |FINSI;

             |REFRESCACONTROL nGrid2;
         |FINSI;

         nRefresca = 1;
         FSalida   = 1;
         |HAZ Evento4;

         |SI FSalida = 0;
             #0#2 = #3#1;  |PINTA #0#2;
             #0#3 = #3#2;  |PINTA #0#3;
         |SINO;
             #0#2 = 0;   |PINTA #0#2;
             #0#3 = "";  |PINTA #0#3;
         |FINSI;

         |HAZ ActUltDocCarpeta;

         |REFRESCACONTROL nGrid3;
     |FINSI;
|FPRC;

|PROCESO Evento4;
     |SI FSalida = 1;  |O FSalida = 2;
         |SI nRefresca = 0;
              |LEE 000#4=;
         |SINO;
             #4#0 = #3#0;
             #4#1 = #3#1;
             #4#2 = 9999;
             |LEE 000#4];
             |SI FSalida = 0;
                 |LEE 000#4a;
             |SINO;
                 |LEE 000#4u;
             |FINSI;

             nRefresca = 0;
         |FINSI;

         |SI FSalida = 0;
             #0#4 = #4#2;  |PINTA #0#4;
             #0#5 = #4#3;  |PINTA #0#5;
         |SINO;
             #0#4 = 0;   |PINTA #0#4;
             #0#5 = "";  |PINTA #0#5;
         |FINSI;

         |HAZ ActUltDocCarpeta;
     |FINSI;
|FPRC;

|PROCESO Evento4P;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#4=;
         |SI FSalida = 0;
             #2#0 = #4#0;
             |LEE 000#2=;

             #3#0 = #4#0;
             #3#1 = #4#1;
             |LEE 000#3=;

             #0#0 = #2#0;  |PINTA #0#0;
             #0#1 = #2#1;  |PINTA #0#1;
             #0#2 = #3#1;  |PINTA #0#2;
             #0#3 = #3#2;  |PINTA #0#3;
             #0#4 = #4#2;  |PINTA #0#4;
             #0#5 = #4#3;  |PINTA #0#5;
         |FINSI;

         |HAZ ActUltDocCarpeta;
     |FINSI;
|FPRC;

|PROCESO ObtenIdSalir;
     InNivel = nNivel1<-;
     nNivel1 = 0;
     |ESPECIFICA "IDBOTONTECLA", nNivel;
     nIdSalir = nNivel2;
|FPRC;

|PROCESO PonFoto;
     |DBASE aBase;  |QBF aBase;

     |SI enDESPANET = 0;
         aFotoOrigen = aBase + "logos/logods.jpg";
         aExt        = ".jpg";
     |SINO;
         aFotoOrigen = aBase + "logos/logodespanet.jpg";
         aExt        = ".jpg";
         |XABRE "E", aFotoOrigen, nHandle;
         |SI FSalida < 0;
             aFotoOrigen = aBase + "logos/logodespanet.bmp";
             aExt        = ".bmp";
             |XABRE "E", aFotoOrigen, nHandle;
             |SI FSalida < 0;
                 aFotoOrigen = aBase + "logos/insertarlogo.jpg";
                 aExt        = ".jpg";
                 |XABRE "E", aFotoOrigen, nHandle;
                 |SI FSalida < 0;
                     |ACABA;
                 |FINSI;
             |FINSI;
         |FINSI;
     |FINSI;

     aDirLocal    = eaUnidadBasico + "\DOCUMENTOS";                |RMKDIR aDirLocal;
     aDirLocal    = eaUnidadBasico + "\DOCUMENTOS\DSARCHI";        |RMKDIR aDirLocal;
     aDirLocal    = eaUnidadBasico + "\DOCUMENTOS\DSARCHI\LOGOS";  |RMKDIR aDirLocal;
     aFotoDestino = aDirLocal + "/logods" + aExt;

     aContiene1 = aFotoDestino;
     |ESPECIFICA "REMOTOMD5", aContiene;
     aDocRemoto = FSalida;  |QBF aDocRemoto;

     aContiene1 = aFotoOrigen;
     |ESPECIFICA "MD5", aContiene;
     aDocServidor = FSalida;  |QBF aDocServidor;

     |SI aDocRemoto ! aDocServidor;  |O aDocRemoto = "";
         aIPRemoto = "";
         |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aFotoOrigen, aFotoDestino;
         |SINO;
             |COPIA_FICHERO aFotoOrigen, aFotoDestino;
         |FINSI;
     |FINSI;

     aFoto = "IMAGEN,"  + aFotoDestino;
     |CONTROL_SIMPLE nPanta0, aFoto, 0501, 0923, NULL;
     nBotonFoto = FSalida;
|FPRC;

|PROCESO Boton7;
     |PTEC 815;
|FPRC;

|PROCESO Boton8;
     |PTEC 814;
|FPRC;

|PROCESO SeBorran;
     |FIN_CONTROL_WINDOWS nBtnBor;

     |SI #dsarz018#12 = "S";
         #dsarz018#12 = "N";
         |CONTROL_SIMPLE 0, "BOTON,{{207}}No se borran en origen los documentos una vez archivados.", 3902, 4030, SeBorran;
     |SINO;
         #dsarz018#12 = "S";
         |CONTROL_SIMPLE 0, "BOTON,{{206}}Se borran en origen los documentos una vez archivados.", 3902, 4030, SeBorran;
     |FINSI;
     nBtnBor = FSalida;
|FPRC;

|PROCESO BotonesPanta0;
     |PULSATECLA;
     |PULSATECLA;
     |HAZ PonFoto;

     |CONTROL_SIMPLE nPanta0, "BOTON,Seleccionar", 0842, 0852, Boton1;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON,Cambiar", 1167, 1177, Boton2;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON,[F5] Capturar", 1287, 1397, Boton3;
     nBoton3 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{202}}Av.Pag. siguiente", 3985, 4097, Boton7;
     nBoton7 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{201}}Re.Pag. anterior", 3971, 4083, Boton8;
     nBoton8 = FSalida;

     |SI #dsarz018#12 = "S";
         |CONTROL_SIMPLE 0, "BOTON,{{206}}Se borran en origen los documentos una vez archivados.", 3902, 4030, SeBorran;
     |SINO;
         |CONTROL_SIMPLE 0, "BOTON,{{207}}No se borran en origen los documentos una vez archivados.", 3902, 4030, SeBorran;
     |FINSI;
     nBtnBor = FSalida;
|FPRC;

|PROCESO dsarm005_z018;
     #2#0 = #5#1;
     |LEE 101#2=;
     |SI FSalida ! 0;
         |DDEFECTO #2;
         #2#0 = #5#1;
         |GRABA 020#2b;
     |FINSI;

     #2#3 = #2#3 + 1;
     |GRABA 020#2a;
     |LIBERA #2;

     #3#0 = #5#1;
     #3#1 = #5#2;
     |LEE 101#3=;
     |SI FSalida ! 0;
         |DDEFECTO #3;
         #3#0 = #5#1;
         #3#1 = #5#2;
         |GRABA 020#3b;
     |FINSI;

     #3#4 = #3#4 + 1;
     |GRABA 020#3a;
     |LIBERA #3;

     #4#0 = #5#1;
     #4#1 = #5#2;
     #4#2 = #5#3;
     |LEE 101#4=;
     |SI FSalida ! 0;
         |DDEFECTO #4;
         #4#0 = #5#1;
         #4#1 = #5#2;
         #4#2 = #5#3;
         |GRABA 020#4b;
     |FINSI;

     #4#25 = #4#25 + 1;
     |GRABA 020#4a;
     |LIBERA #4;

     #5#44 = ((#2#3  / #2#4)  + 0.48) ! 0;
     #5#45 = ((#3#4  / #3#5)  + 0.48) ! 0;
     #5#46 = ((#4#25 / #4#26) + 0.48) ! 0;

     |SI #5#44 = 0;  #5#44 = 1;  |FINSI;
     |SI #5#45 = 0;  #5#45 = 1;  |FINSI;
     |SI #5#46 = 0;  #5#46 = 1;  |FINSI;

     |GRABA 020#5a;
     |LIBERA #5;
|FPRC;

|DEFBUCLE dsarm005_z018;
     #5#1;
     ;
     000000001;
     999999999;
     be;
     NULL, dsarm005_z018;
|FIN;

|PROCESO reflejo3;
     #802#0 = #801#0;
     #802#1 = #1004#0;
     #802#2 = #1004#1;
     #802#3 = #1004#2;
     |LEE 000#802=;
     |SI FSalida = 0;  |ACABA;  |FINSI;

     #1002#0 = #1004#0;
     |LEE 000#1002=;

     #1003#0 = #1004#0;
     #1003#1 = #1004#1;
     |LEE 000#1003=;

     #2#0 = #1004#0;
     |LEE 101#2=;
     |SI FSalida ! 0;
         #2#0 = #1004#0;
         #2#1 = #1002#1;
         #2#2 = #1002#2;
         #2#3 = #1002#3;
         #2#4 = #1002#4;
         |GRABA 020#2n;
     |FINSI;
     |LIBERA #2;

     #3#0 = #1004#0;
     #3#1 = #1004#1;
     |LEE 101#3=;
     |SI FSalida ! 0;
         #3#0 = #1004#0;
         #3#1 = #1004#1;
         #3#2 = #1003#2;
         #3#3 = #1003#3;
         #3#4 = #1003#4;
         #3#5 = #1003#5;
         |GRABA 020#3n;
     |FINSI;
     |LIBERA #3;

     |PARA nCampo = 0;  |SI nCampo [ nNumCampos;  |HACIENDO nCampo = nCampo + 1;
           #4nCampo = #1004nCampo;
     |SIGUE;
     |GRABA 020#4n;
     |LIBERA #4;
|FPRC;

|DEFBUCLE reflejo3;
     #1004#2004;
     ;
     "S", 000, 000, 000;
     "S", 999, 999, 999;
     ;
     NULL, reflejo3;
|FIN;

|PROCESO TempoDESPANET;
     nErrorDESPANET = 0;
     aUsuario       = Usuario % 810;
     |ABRE #801;
     #801#0 = aUsuario;
     |LEE 000#801=;
     |SI FSalida ! 0;
         nErrorDESPANET = 1;
         |CIERRA #801;
         |ACABA;
     |FINSI;
     |CIERRA #801;

     enDESPANET = 1;

     |HORA aHora;
     aAlfa = "2" + ((aHora % 102) + (aHora % 402) + (aHora % 702) + Usuario) % 108;
     |NOME_DAT #2 aAlfa;

     aAlfa = "3" + ((aHora % 102) + (aHora % 402) + (aHora % 702) + Usuario) % 108;
     |NOME_DAT #3 aAlfa;

     aAlfa = "4" + ((aHora % 102) + (aHora % 402) + (aHora % 702) + Usuario) % 108;
     |NOME_DAT #4 aAlfa;

     |ABRE #2;  |CIERRA #2;  |DELINDEX #2;
     |ABRE #3;  |CIERRA #3;  |DELINDEX #3;
     |ABRE #4;  |CIERRA #4;  |DELINDEX #4;

     |DBASE aBase;  |QBF aBase;
     aMas = aBase + "def/dsarm002.mas";  |CARGA_DEF aMas, reflejo2@;
     aMas = aBase + "def/dsarm003.mas";  |CARGA_DEF aMas, reflejo3@;
     aMas = aBase + "def/dsarm004.mas";  |CARGA_DEF aMas, reflejo4@;

     aQueQuiero = "|NC";
     nNumCampos = #4#aQueQuiero#;
     nNumCampos = nNumCampos - 1;

     |ABRE #2;
     |ABRE #3;
     |ABRE #4;
     |ABRE #1002;
     |ABRE #1003;
     |ABRE #802;
     |BUCLE reflejo3;
     |CIERRA #1002;
     |CIERRA #1003;
     |CIERRA #2;
     |CIERRA #3;
     |CIERRA #4;
     |CIERRA #802;

     |DESCARGA_DEF #reflejo4@;
     |DESCARGA_DEF #reflejo3@;
     |DESCARGA_DEF #reflejo2@;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     aAux   = PARAMETRO % 108;
     |SI aAux = "DESPABOX";
         |HAZ TempoDESPANET;
         |SI nErrorDESPANET = 1;
             |ACABA;
         |FINSI;
     |SINO;
         aUsuario = Usuario % 810;
         |ABRE #801;
         #801#0 = aUsuario;
         |LEE 000#801=;
         |SI FSalida = 0;
             nErrorDESPANET = 1;
             |CIERRA #801;
             |ACABA;
         |FINSI;
         |CIERRA #801;
     |FINSI;

     |ABRET #2;
     |ABRET #3;
     |ABRET #4;

     |ABRE #5;
     |LEE 000#5p;
     |SI #5#44 = 0;  |Y #5#45 = 0;  |Y #5#46 = 0;
         |CIERRA #5;

         |BUCLE dsarm005_z018;
     |FINSI;
     |CIERRA #5;

     FSalida = 4299;
|FPRC;

|PROCESO ControlZero;
     |ABRE #dsarm002;
     #dsarm002#0 = 0;
     |LEE 101#dsarm002.=;
     |SI FSalida = 0;
          |BORRA 020#dsarm002.a;
          |LIBERA #dsarm002;
     |FINSI;
     |CIERRA #dsarm002;

     |ABRE #dsarm004;
     #dsarm004#0 = 0;
     #dsarm004#1 = 0;
     #dsarm004#2 = 0;
     |LEE 101#dsarm004.=;
     |SI FSalida = 0;
          |BORRA 020#dsarm004.a;
          |LIBERA #dsarm004;
     |FINSI;
     |CIERRA #dsarm004;
|FPRC;

|PROCESO Tipo90;  |TIPO 90;
     |CIERRAT #2;
     |CIERRAT #3;
     |CIERRAT #4;

     |SI enDESPANET = 1;
         |DELINDEX #2;
         |DELINDEX #3;
         |DELINDEX #4;
     |FINSI;
|FPRC;

|PROCESO Pondsbrowse;
     |DBASE aBase;
     |QBF aBase;
     aFotoOrigen = aBase + "dlls/dsbrowse.exe";
     |XABRE "E", aFotoOrigen, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     aDirLocal    = "C:\DOCUMENTOS";                       |RMKDIR aDirLocal;
     aDirLocal    = "C:\DOCUMENTOS\DSARCHI\";              |RMKDIR aDirLocal;
     aFotoDestino = aDirLocal + "\dsbrowse.exe";

     aContiene1 = aFotoDestino;
     |ESPECIFICA "REMOTOMD5", aContiene;
     aDocRemoto = FSalida;  |QBF aDocRemoto;

     aContiene1 = aFotoOrigen;
     |ESPECIFICA "MD5", aContiene;
     aDocServidor = FSalida;  |QBF aDocServidor;

     |SI aDocRemoto ! aDocServidor;  |O aDocRemoto = "";
         aIPRemoto = "";
         |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aFotoOrigen, aFotoDestino;
         |SINO;
             |COPIA_FICHERO aFotoOrigen, aFotoDestino;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Boton1;         ||Seleccionar directorio Local
     |SI nIdSalir = -1;
          |HAZ ObtenIdSalir;
     |FINSI;

     ||TODO CCC
     |VENTANA 0501, 2799, -1, -1, "";
     nVentana = FSalida;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |ABRE #100;
     #100#0 = Usuario % 810;
     |LEE 101#100=;
     |SI FSalida ! 0;
         |DDEFECTO #100;
         #100#0 = Usuario % 810;
         |GRABA 020#100b;
     |FINSI;
     |GRABA 020#100a;
     |LIBERA #100;
     |CIERRA #100;

     aDirBase = #100#8;
     |QBF aDirBase;
     |SI aDirBase = "";
          |HAZ DimeUnidad;
          aDirBase = eaUnidad + ":\";
     |FINSI;

     ||DSBROWSE.EXE.
     |HAZ Pondsbrowse;

     aAux = "C:\DOCUMENTOS\DSARCHI\";
     aPrograma = aAux + "dsbrowse.exe";

     aBorra = aAux + "dsbrowse.txt";
     |RFBORRA aBorra;

     aBorra = aAux + "dsbrowse.ini";
     |RFBORRA aBorra;

     aEjecuta = aAux + "dsbrowse.ini";
     |XABRE "CW", aEjecuta, nHandle;
     |SI FSalida < 0;
          aMensaje = "0000Error. Problemas al crear " + aEjecuta;
          |MENAV aMensaje;
          |ACABA;
     |SINO;
          aLinea = &34 + aDirBase + &34 + " " + &34 + aAux + "dsbrowse.txt" + &34;
          |XGRABA nHandle, aLinea;
          |XCIERRA nHandle;
     |FINSI;

     ||ARA. Para Prueba.
     ||El bueno es el RSYSTEM y el "CU"
     aEjecuta = aPrograma;
     |RSYSTEM aEjecuta;

     aAbre = aAux + "dsbrowse.txt";
     |XABRE "CU", aAbre, nHandle;
     |SI FSalida < 0;
          aPath = "";
          |FINVENTANA nVentana;
          |ACABA;
     |SINO;
          |XLEE nHandle, 250, aPath;
          |SI FSalida < 0
               aPath = "";
               |FINVENTANA nVentana;
               |ACABA;
          |FINSI;
     |FINSI;

     |XCIERRA nHandle;
     aBorra = aAux + "dsbrowse.txt";
     |RFBORRA aBorra;

     |FINVENTANA nVentana;

     |QBF aPath;
     |SI aPath ! "";
          |ABRE #100;
          #100#0 = Usuario % 810;
          |LEE 101#100=;
          |SI FSalida ! 0;
              |DDEFECTO #100;
              #100#0 = Usuario % 810;
              |GRABA 020#100b;
          |FINSI;
          #100#8 = aPath;
          |GRABA 020#100a;
          |LIBERA #100;
          |CIERRA #100;

          #dsarz018#8 = aPath;
          |PINTA #dsarz018#8;

          aPtes = #dsarz018#8;
          |QBF aPtes;
          |SI aPtes ! "";
               |HAZ MeteDocumentos;
          |SINO;
               |HAZ SinDocumentos;
          |FINSI;

          |FOCOTECLADO nIdSalir;
     |FINSI;
|FPRC;

|PROCESO Boton2;         ||Cambiar Grupo/Subgrupo/Tipo
     |SI #1#8 = "S";
          enGrupoTres = 0;
          enSubGTres = 0;
          enTipoTres = 0;
          aEjecuta = "dsarz003;DAMETRE";
          |SUB_EJECUTA_NP aEjecuta;
          |SI enGrupoTres ! 0;
               |ABRE #100;
               #100#0 = Usuario % 810;
               |LEE 101#100=;
               |SI FSalida ! 0;
                   |DDEFECTO #100;
                   #100#0 = Usuario % 810;
                   |GRABA 020#100b;
               |FINSI;
               #100#9 = enGrupoTres;
               #100#10 = enSubGTres;
               #100#11 = enTipoTres;
               |GRABA 020#100a;
               |LIBERA #100;
               |CIERRA #100;

               #dsarz018#0 = #100#9;
               #dsarz018#2 = #100#10;
               #dsarz018#4 = #100#11;
               |PINTA #dsarz018#0;
               |PINTA #dsarz018#2;
               |PINTA #dsarz018#4;
               |HAZ PonDescripciones;
          |FINSI;
     |SINO;
          |CONSULTA_CLAVE #1#2;
          |SI FSalida = 0;
               #dsarz018#0 = #2#0;
               #dsarz018#1 = #2#1;
               |PINTA #dsarz018#0;
               |PINTA #dsarz018#1;
               #dsarz018#2 = 1;
               #dsarz018#4 = 1;
          |FINSI;
          ||Solo selecciono Grupo.
     |FINSI;

     ||TODO CCC
|FPRC;

|PROCESO Boton3;         ||Capturar
     |SI nIdImagen ! -1;
          |ESTADO_CONTROL nIdImagen, "HIDE";
     |FINSI;
     |SI nIdSalir = -1;
          |HAZ ObtenIdSalir;
     |FINSI;
     |ESTADO_CONTROL nIdSalir, "HIDE";
     |ESTADO_CONTROL nBoton7, "HIDE";
     |ESTADO_CONTROL nBoton8, "HIDE";

     |VENTANA 0501, 3299, -1, 16, "Datos del documento";
     nVentana = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     enErrorCaptura = 0;

     eaDocCaptura = #dsarz018#8;
     |QBF eaDocCaptura;
     |SI eaDocCaptura = "";
          aMensaje = "0000Error: Carpeta local origen de documentos NO seleccionada.";
          |MENAV aMensaje;
          |ACABA;
     |FINSI;

     aDirArchivados = eaDocCaptura + "archivados/";

     aAux = #dsarz018#11;
     |QBF aAux;
     |SI aAux = "";
          aMensaje = "0000Error: No hay ningun documento seleccionado para capturar.";
          |MENAV aMensaje;
          |ACABA;
     |FINSI;
     eaDocCaptura = eaDocCaptura + aAux;
     |QBF eaDocCaptura;

     aNomDocArchivado = aDirArchivados + aAux;

     enGrupo    = #dsarz018#0;
     enSubGrupo = #dsarz018#2;
     enTipo     = #dsarz018#4;
     |SI enGrupo = 0; |O enSubGrupo = 0; |O enTipo = 0;
          |SI #1#8 = 1;
               aMensaje = "0000Error: Indique el Grupo/Subgrupo/Tipo destino de la captura.";
          |SINO;
               aMensaje = "0000Error: Indique el Grupo destino de la captura.";
          |FINSI;
          |MENAV aMensaje;
          |ACABA;
     |FINSI;

     |SI #dsarz018#12 = "N";
         |RMKDIR aDirArchivados;

         aOrigen = eaDocCaptura;
         aDestino = aNomDocArchivado;
         |RCOPIA_FICHERO aOrigen, aDestino;
     |FINSI;

     eaSwBorroOrigen = #dsarz018#12;
     eaCarpetaOrigen = #dsarz018#8;
     eaDocPC         = eaDocCaptura;
     aEjecuta        = "dsarz001;CAPTURAESTEDOC";
     enPrevisualizar = 1;
     |SUB_EJECUTA_NP aEjecuta;
     enPrevisualizar = 0;
     ||TODO CCC

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVentana;

     |SI nIdImagen ! -1;
          |ESTADO_CONTROL nIdImagen, "SHOW";
     |FINSI;
     |ESTADO_CONTROL nIdSalir, "SHOW";
     |ESTADO_CONTROL nBoton7, "SHOW";
     |ESTADO_CONTROL nBoton8, "SHOW";

     |SI enErrorCaptura = 0;
          ||He capturado este fichero. Tengo que refrescar.
          ||ARA77
          aPtes = #dsarz018#8;
          |QBF aPtes;
          |SI aPtes ! "";
               |HAZ MeteDocumentos;
          |SINO;
               |HAZ SinDocumentos;
          |FINSI;
     |SINO;
          ||NO ha capturado nada. Se queda todo como estaba. Lo borro de Archivados.

          |RFBORRA aNomDocArchivado;
     |FINSI;

     |FOCOTECLADO nIdSalir;
|FPRC;

|PROCESO Boton5;
     |HAZ DobleClickP;

     ||TODO CCC
     ||ARA66
|FPRC;

|PROCESO PonDescripciones;
     |SI #dsarz018#0 = 0;
          #dsarz018#1 = "";
     |SINO;
          #2#0 = #dsarz018#0;
          |LEE 000#2.=;
          |SI FSalida = 0;
               #dsarz018#1 = #2#1;
          |SINO;
               #dsarz018#1 = "";
          |FINSI;
     |FINSI;


     |SI #1#8 = "S";
          #3#0 = #dsarz018#0;
          #3#1 = #dsarz018#2;
          |LEE 000#3.=;
          |SI FSalida = 0;
               #dsarz018#3 = #3#2;
          |SINO;
               #dsarz018#3 = "";
          |FINSI;

          #4#0 = #dsarz018#0;
          #4#1 = #dsarz018#2;
          #4#2 = #dsarz018#4;
          |LEE 000#4.=;
          |SI FSalida = 0;
               #dsarz018#5 = #4#3;
          |SINO;
               #dsarz018#3 = "";
          |FINSI;
     |SINO;
          #dsarz018#3 = "";
          #dsarz018#5 = "";
     |FINSI;

     |PINTA #dsarz018#1;
     |PINTA #dsarz018#3;
     |PINTA #dsarz018#5;
|FPRC;

|PROCESO CambiaBarra; |TIPO 0;
     |QBF aCadOri;
     aCadFin = "";
     aLong = aCadOri % A0;     ||A0 == "0"
     nLong = aLong;
     |PARA nCampo = 1; |SI nCampo [ nLong; |HACIENDO nCampo = nCampo + 1;
          aCampo = nCampo + "01";
          nNumSaca = aCampo;
          aLetra = aCadOri % nNumSaca;
          |SI aLetra = "/";
               aLetra = "\";
          |FINSI;
          aCadFin = aCadFin + aLetra;
     |SIGUE;
|FPRC;


|PROCESO MeteDocumentos;
     #dsarz018#9 = 0;
     #dsarz018#10 = 0;
     #dsarz018#11 = "";
     |PINTA #dsarz018#9;
     |PINTA #dsarz018#10;
     |PINTA #dsarz018#11;

     nSwHayPtes = 0;
     nImagen = 0;
     nTotal = 0;
     aFichero = aPtes + "*.*";
     |R_PDIR aFichero;

     |PARA nImagen = 1; |SI FSalida ] 0; |HACIENDO nImagen = nImagen + 1;
          |QBF aFichero;

          |SI aFichero ! "";
               aCadOri = aFichero;
               |HAZ CambiaBarra;
               aFichero = aCadFin;

               aNombre = aFichero - aPtes;

               |SI nImagen = 1;
                    |HAZ CreaTempo;
                    aTempo62 = Tempo;
                    |NOME_DAT #62 aTempo62;
                    |DELINDEX #62;
                    |ABRE #62;

                    #dsarz018#9 = 1;
                    #dsarz018#11 = aNombre;
                    |PINTA #dsarz018#9;
                    |PINTA #dsarz018#11;
               |FINSI;

               |DDEFECTO #62;
               #62#0 = nImagen;
               |LEE 101#62=;
               |SI FSalida ! 0; |GRABA 020#62b; |FINSI;
               #62#1 = aNombre;
               |GRABA 020#62a;
               |LIBERA #62;

               nTotal = nTotal + 1;
               nSwHayPtes = 1;
          |FINSI;

          |R_SDIR aFichero;
     |SIGUE;

     #dsarz018#10 = nTotal;
     |PINTA #dsarz018#10;

     |SI nSwHayPtes ! 0;
          nVoy = 1;
          aPinta = "p";
          |HAZ PintaImagen;
     |SINO;
          |HAZ SinDocumentos;
     |FINSI;
|FPRC;

|PROCESO Boton5_22; ||Visualizar Factura
     |HAZ DobleClickP;
|FPRC;

|PROCESO Boton5_20; ||Imagen Anterior
     |HAZ Boton5_2;
|FPRC;

|PROCESO Boton5_21; ||Imagen Siguiente
     |HAZ Boton5_3;
|FPRC;

|PROCESO BotonPrimero; ||Imagen Primero. INICIO
     |HAZ Boton5_1;
|FPRC;

|PROCESO BotonUltimo; ||Imagen Ultima. FIN.
     |HAZ Boton5_4;
|FPRC;

|PROCESO Contador;
     nNumDoc = nTotal - nBorrados;
     #dsarz018#9  = nVoy;
     #dsarz018#10 = nNumDoc;
     |PINTA #dsarz018#9;
     |PINTA #dsarz018#10;
|FPRC;

|PROCESO RefrescaImagen;
     aImagen = #dsarz018#8;
     |QBF aImagen;
     aImagen = aImagen + #dsarz018#11;
     |QBF aImagen;

     aExtension = aImagen % -104;
     aExtension = aExtension > aExtension;
     |SI aExtension = ".BMP"; |O aExtension = ".JPG"; |O aExtension = ".JPEG"; |O aExtension = ".JPE";
          aPosicion = "PI:0,0:";
          aPosicion = "";
          aFotoDestino = aPosicion + "SCROLLV:" + aImagen;

          aBmp = "IMAGEN," + aFotoDestino;
          |CONTROL_SIMPLE nPanta0, aBmp, 1601, 4299, DobleClickP;
          nIdImagen = FSalida;

          coord = "0," + nPIX;
          alfa = "MOVE:"+coord;
          |ESTADO_CONTROL nIdImagen,alfa;
     |SINO;
          aImagen = "c:\documentos\dsarchi\vistaprevia.jpg";

          aPosicion = "PI:0,0:";
          aPosicion = "";
          aFotoDestino = aPosicion + "SCROLLV:" + aImagen;

          aBmp = "IMAGEN," + aFotoDestino;
          |CONTROL_SIMPLE nPanta0, aBmp, 1601, 4299, DobleClickP;
          nIdImagen = FSalida;

          coord = "0," + nPIX;
          alfa = "MOVE:"+coord;
          |ESTADO_CONTROL nIdImagen,alfa;
     |FINSI;
|FPRC;

|PROCESO PintaImagen;
     |ESTADO_CONTROL nBoton7, "HIDE";
     |ESTADO_CONTROL nBoton8, "HIDE";

     nSwYaTa = 0;

     |SI aPinta = "p";
          |DDEFECTO #62;
          |LEE 000#62.p;
     |FINSI;

     |SI aPinta = "u";
          |DDEFECTO #62;
          |LEE 000#62.u;
     |FINSI;

     |SI aPinta = "s";
          nAntes = nVoy;
          |DDEFECTO #62;
          #62#0 = nVoy;
          |LEE 000#62.=;
          |LEE 000#62.s;
          |SI #62#0 = nAntes;
               aMensaje = "0000Ultima Ficha";
               |MENAV aMensaje;
               nSwYaTa = 1;
          |FINSI;
     |FINSI;

     |SI aPinta = "a";
          |SI nVoy = 1;
               aMensaje = "0000Primera Ficha";
               |MENAV aMensaje;
               |DDEFECTO #62;
               |LEE 000#62.p;
               nSwYaTa = 1;
          |SINO;
               |DDEFECTO #62;
               #62#0 = nVoy;
               |LEE 000#62.=;
               |LEE 000#62.a;
          |FINSI;
     |FINSI;

     |SI nSwYaTa = 0;
          |SI nIdImagen ! -1;
               |DESTRUYECONTROL nIdImagen;
               nIdImagen = -1;
          |FINSI;


          #dsarz018#11 = #62#1;
          |PINTA #dsarz018#11;

          |HAZ PintaLabel;

          nVoy = #62#0;
          |HAZ Contador;

          |HAZ RefrescaImagen;
     |SINO;
          |HAZ PintaLabel;
     |FINSI;

     |ESTADO_CONTROL nBoton7, "SHOW";
     |ESTADO_CONTROL nBoton8, "SHOW";
|FPRC;

|PROCESO PintaLabel;
     nTipoLabel = 2;     ||Todo borrado. Sin Label

     aAux = #dsarz018#8;
     |QBF aAux;
     |SI aAux = "";
          nTipoLabel = 0;
     |SINO;
          |SI #dsarz018#10 = 0;
               nTipoLabel = 1;
          |SINO;
               aAux = #dsarz018#11;
               |QBF aAux;
               |SI aAux = "";
                    nTipoLabel = 1;
               |SINO;
                    aExtension = aAux % -104;
                    aExtension = aExtension > aExtension;
                    |SI aExtension = ".BMP"; |O aExtension = ".JPG"; |O aExtension = ".JPEG"; |O aExtension = ".JPE";
                         nTipoLabel = 2;     ||Todo borrado. Sin Label
                    |SINO;
                         nTipoLabel = 3;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;

     |SI nBoton5 ! -1;
          |FIN_CONTROL_WINDOWS nBoton5;
          nBoton5 = -1;
     |FINSI;

     |SI nTipoLabel = 0;
          |CONTROL_SIMPLE nPanta0, "LABEL,{{9}}Seleccione carpeta local ubicaci¢n documentos.", 2608, 2996, NULL;
     |FINSI;

     |SI nTipoLabel = 1;
          |CONTROL_SIMPLE nPanta0, "LABEL,{{9}}Carpeta Local sin documentos.", 2608, 2996, NULL;
     |FINSI;

     |SI nTipoLabel = 2;
          |CONTROL_SIMPLE nPanta0, "LABEL,{{9}}                                              ", 2608, 2796, NULL;

          |CONTROL_SIMPLE nPanta0, "LABEL,{{9}}                                              ", 2808, 2996, NULL;
     |FINSI;

     |SI nTipoLabel = 3;
          |CONTROL_SIMPLE nPanta0, "LABEL,{{9}}Documento sin vista previa. Pulse sobre este texto para", 2608, 2796, NULL;

          |CONTROL_SIMPLE nPanta0, "LABEL,{{9}}abrir el documento utilizando el programa asociado al mismo.", 2808, 2996, NULL;
     |FINSI;
|FPRC;

|PROCESO Boton5_1;
     aPinta = "p";
     |HAZ PintaImagen;
|FPRC;

|PROCESO Boton5_2;
     aPinta = "a";
     |HAZ PintaImagen;
|FPRC;

|PROCESO Boton5_3;
     aPinta = "s";
     |HAZ PintaImagen;
|FPRC;

|PROCESO Boton5_4;
     aPinta = "u";
     |HAZ PintaImagen;
|FPRC;

|PROCESO DobleClickP;
     |VENTANA 0501, 4299, -1, -1, "";
     nVentana = FSalida;

     aMensaje = "0000N¿Visualizar Documento?";
     |CONFI aMensaje;
     |SI FSalida = 0;
          aGuarda = aFotoDestino;
          aFotoDestino = #dsarz018#8;
          |QBF aFotoDestino;
          aFotoDestino = aFotoDestino + #dsarz018#11;
          |QBF aFotoDestino;
          aEjecuta = aFotoDestino;
          aFotoDestino = aGuarda;

          |DBASE aBase;
          |QBF aBase;
          aBaseDll = aBase + "dlls/";
          eaOrigen = aBaseDll + "dsabreextension.exe";

          |HAZ DimeUnidad;

          eaDestino = eaUnidad + ":\DOCUMENTOS\DSARCHI\" + "dsabreextension.exe";
          |HAZ EnviaFicheroConMD5;

          |HAZ PresentaDocumento;
     |FINSI;

     |FINVENTANA nVentana;

     |HAZ PintaLabel;
|FPRC;

|PROCESO PonVistaPrevia;
     |DBASE aBase;
     |QBF aBase;
     aBaseDll = aBase + "logos/";
     eaOrigen = aBaseDll + "vistaprevia.jpg";

     |HAZ DimeUnidad;

     eaDestino = eaUnidad + ":\DOCUMENTOS\DSARCHI\" + "vistaprevia.jpg";
     |HAZ EnviaFicheroConMD5;
|FPRC;

|PROCESO PresentaDocumento;
     |HAZ DimeUnidad;

     eaDestino = aEjecuta;
     |XABRE "EC", eaDestino, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     aAlfa  = eaUnidad + ":\DOCUMENTOS\DSARCHI\" + "fin" + Usuario + ".txt";
     |RFBORRA aAlfa;

     aAbre = eaUnidad + ":\DOCUMENTOS\DSARCHI\previsual.bat";
     |RFBORRA aAbre;

     aAbre = eaUnidad + ":\DOCUMENTOS\DSARCHI\previsual.bat";
     |XABRE "CW", aAbre, nHandle;
     |SI FSalida < 0;
          aMensaje = "0000Error. Problemas al crear " + aAbre;
          |MENAV aMensaje;
          |ACABA;
     |FINSI;

     aAlfa = "START " + eaUnidad + ":\DOCUMENTOS\DSARCHI\dsabreextension ";
     aAlfa = aAlfa + &34 + eaDestino + &34;
     aAlfa = aAlfa + " " + eaUnidad + ":\DOCUMENTOS\DSARCHI\" + "fin" + Usuario + ".txt";
     aAlfa = aAlfa + &13 + &10;
     |XGRABA nHandle, aAlfa;

     |XCIERRA nHandle;

     |RSYSTEM aAbre;

     aAlfa  = eaUnidad + ":\DOCUMENTOS\DSARCHI\" + "fin" + Usuario + ".txt";
     |PARA;  |SI;  |HACIENDO;
             |XABRE "EC", aAlfa, nHandle;
             |SI FSalida ] 0;
                 |SAL;
             |FINSI;

             |PARA nPausa = 1; |SI nPausa < 10000; |HACIENDO nPausa = nPausa + 1;
             |SIGUE;
             |PULSATECLA;
     |SIGUE;
     |PULSATECLA;

     aAlfa  = eaUnidad + ":\DOCUMENTOS\DSARCHI\" + "fin" + Usuario + ".txt";
     |RFBORRA aAlfa;
     |RFBORRA aAbre;
|FPRC;


|PROCESO SinDocumentos;
     |SI nIdImagen ! -1;
          |DESTRUYECONTROL nIdImagen;
          nIdImagen = -1;
     |FINSI;

     ||Borro el temporal 62.
     |HAZ CreaTempo;
     aTempo62 = Tempo;
     |NOME_DAT #62 aTempo62;
     |DELINDEX #62;
     |ABRE #62;

     nSwHayPtes = 0;
     nTotal = 0;
     ||Pinta la pantalla con imagen .. SIN DOCUMENTOS
     ||TODO CCC

     #dsarz018#9 = 0;
     #dsarz018#10 = nTotal;
     #dsarz018#11 = "";
     |PINTA #dsarz018#9;
     |PINTA #dsarz018#10;
     |PINTA #dsarz018#11;

     |HAZ PintaLabel;
|FPRC;

|PROGRAMA;
     |SI nErrorDESPANET = 1;
         |MENAV "0000No tiene permiso para entrar en esta opci¢n, no es un usuario DESPABOX";
         |ACABA;
     |FINSI;

     |HAZ ControlZero;
     |HAZ PonVistaPrevia;
     |HAZ LeeUnidadBasico;

     |CABEZA "Asistente de archivado 2010";

     |ABRE #1;
     |LEE 000#1p;
     |SI FSalida ! 0;  |DDEFECTO #1;  |FINSI;
     |CIERRA #1;

     aAvPag = &255 + "815";
     aRePag = &255 + "814";
     aInicio = &255 + "816";
     aFin = &255 + "812";
     aArriba = &255 + "808";
     aAbajo = &255 + "809";
     aCtrlArriba = &255 + "994";
     aCtrlAbajo = &255 + "992";
     aEfe5 = &255 + "827";

     nSwADConta  = 0;
     nDesdeGrupo = 1;
     nHastaGrupo = 999;
     nDesdeSub   = 1;
     nHastaSub   = 999;
     aParametro  = PARAMETRO;
     |QBF aParametro;

     aAux = aParametro % 107;

     nPanta0 = 0;

     |PINPA #0#0;  nPanta0 = FSalida;

     |ABRE #100;
     #100#0 = Usuario % 810;
     |LEE 101#100=;
     |SI FSalida ! 0;
         |DDEFECTO #100;
         #100#0 = Usuario % 810;
         |GRABA 020#100b;
     |FINSI;
     |GRABA 020#100a;
     |LIBERA #100;

     #dsarz018#8  = #100#8;
     #dsarz018#0  = #100#9;
     #dsarz018#12 = "N";
     |SI #dsarm001#217 = "S";  #dsarz018#12 = "S";  |FINSI;

     |SI #1#8 = "S";
          #dsarz018#2 = #100#10;
          #dsarz018#4 = #100#11;
     |SINO;
          #dsarz018#2 = 1;
          #dsarz018#4 = 1;

          |C_M #dsarz018#2, 1, "N";
          |C_V #dsarz018#2, 1, "N";
          |C_M #dsarz018#3, 1, "N";
          |C_V #dsarz018#3, 1, "N";
          |C_M #dsarz018#4, 1, "N";
          |C_V #dsarz018#4, 1, "N";
          |C_M #dsarz018#5, 1, "N";
          |C_V #dsarz018#5, 1, "N";

          |CONTROL_SIMPLE nPanta0, "LABEL,{{1}}          ", 1203, 1210, NULL;
          |CONTROL_SIMPLE nPanta0, "LABEL,{{1}}          ", 1303, 1310, NULL;
     |FINSI;
     |CIERRA #100;

     |HAZ PonDescripciones;

     |PINDA #0#0;
     |HAZ BotonesPanta0;

     aPtes = #dsarz018#8;
     |QBF aPtes;
     |SI aPtes ! "";
          |HAZ MeteDocumentos;
     |SINO;
          |HAZ SinDocumentos;
     |FINSI;

     nRango  = 2 + 4 + 8 + 16 + 32 + 128;

     aTecla = "";
     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::Salir";
          |LEETECLA aTecla;
          |SI aTecla = aRePag;
               |HAZ Boton5_20;
          |FINSI;
          |SI aTecla = aAvPag;
               |HAZ Boton5_21;
          |FINSI;
          |SI aTecla = aInicio;
               |HAZ BotonUltimo;
          |FINSI;
          |SI aTecla = aFin;
               |HAZ BotonPrimero;
          |FINSI;
          |SI aTecla = aEfe5;
               |HAZ Boton3;
          |FINSI;

          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
          |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |SI nSwHayPtes = 1;
          |CIERRA #62;
          |DELINDEX #62;
          Tempo = aTempo62;
          |HAZ BoraTempo;
     |FINSI;

     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;
     |SI nBoton5 ! -1;
          |FIN_CONTROL_WINDOWS nBoton5;
          nBoton5 = -1;
     |FINSI;

     |PULSATECLA;

     |SUB_EJECUTA "dpntz001;CLI";
|FPRO;
