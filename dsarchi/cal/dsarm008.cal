||Si se anyaden campos, en dsarm002,dsarm003, dsarm004 o dsarm008,
||hay que actualizar los patrones: para_asesorias.ptl

|FICHEROS;
     dsarm001 #1;

     dsarm008 #8;
     dsarw015 #915;
     dsarw016 #916;
     dsarz010;

     referen@ #999;
     dsarm043 #43;
     dsarm044 #44;
|FIN;

|VARIABLES;
     aVistaBorra = "";
     nSubVentZero = 0;
     nPanta915 = 0;
     nBotonPermisos = 0;
     aMensaje = "";

     &eaUnidad = "";

     aAbre          = "";
     aBase          = "";
     aAlfa          = "";
     aEsc1          = "";
     aEsc2          = "";
     aRetu          = "";
     aTecla         = "";
     aFichero       = "";
     aCarga         = "";
     aQueQuiero     = "";
     aTexto         = "";
     aLong          = "";
     aCarac         = "";
     aRefresca      = "";
     aBusca         = "";
     aDirVistas     = "";

     nBotonLabel    = 0;
     nHandle        = 0;
     nTree          = -1;
     nPanta         = 0;
     nLong          = 0;
     nCargar        = 0;
     nPos           = 0;
     nPosi          = 0;
     nBoton1        = 0;
     nBoton2        = 0;
     nBoton3        = 0;
     nSubVent       = 0;
     nCampo         = 0;
     nTCampo        = 0;
     nCarpeta0      = 0;
     nCarpeta1      = 0;
     nCarpeta2      = 0;
     nCarpeta3      = 0;
     nCarpeta4      = 0;
     nCarpeta5      = 0;
     nCarpeta6      = 0;
     FEstado        = 0;

     {-1}sTree      = 0;
     sT_nID         = 0;
     sT_nOper       = 0;
     sT_aData       = "";

     {-1}aVent      = "";
         aVent1     = 0;
         aVent2     = 0;
         aVent3     = 0;
         aVent4     = 0;
         aVent5     = "";

     &eaRutaOrigen   = "";
     &eaRutaDestino  = "";
     &eaFicheroTxt   = "";
     &eaFicheroTgz   = "";
     &eaFicheroTar   = "";
     &eaAlfa         = "";
     &eaUnidadBasico = "";
     &eaVista        = "";
     &eaCodVista     = "";
|FIN;

|PROCESO CargaTemporal;
     aFichero = ("16" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #916 aFichero;

     |ABRE #916;  |CIERRA #916;  |DELINDEX #916;

     |ABRE #916;

     |DBASE aBase;  |QBF aBase;
     aCarga    = aBase + "def/dsarm005.mas";  |QBF aCarga;
     |CARGA_DEF aCarga, referen@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aQueQuiero = "|NC";
     aAlfa      = #999#aQueQuiero#;
     nTCampo    = aAlfa;
     nTCampo    = nTCampo - 1;

     |PARA nCampo = 0;  |SI nCampo [ nTCampo;  |HACIENDO nCampo = nCampo + 1;
           aQueQuiero = "|C" + (("000" + nCampo) % -103) + "NOMBRE";
           aFichero   = (#999#aQueQuiero# + (" " * 20)) % 120;
           #916#0 = nCampo;
           #916#1 = aFichero;

           |GRABA 020#916n;
     |SIGUE;
     |CIERRA #916;

     |DESCARGA_DEF #referen@;
|FPRC;

|PROCESO BotonPermisos;
     eaVista = #915#0;
     |QBF eaVista;
     |SI eaVista = "";
          aMensaje = "0000Introduzca primero el nombre de la vista";
          |MENAV aMensaje;
          |ACABA;
     |FINSI;

     |HAZ Acotaciones;

     ||AL SALIR ... VUELVO A PONER EL MODAL.
     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;
|FPRC;

|PROCESO dsarz010;
     #dsarm043#0 = #dsarz010#0;
     #dsarm043#1 = #dsarm008#0;
     |LEE 000#dsarm043.=;
     |SI FSalida = 0;  |ACABA;  |FINSI;

     |DDEFECTO #dsarm043;
     #dsarm043#0 = #dsarz010#0;
     #dsarm043#1 = #dsarm008#0;

     |SI #dsarm008#32 = "1";  #dsarm043#2 = "S";  |FINSI;
     |SI #dsarm008#32 = "2";  #dsarm043#2 = "N";  |FINSI;

     |SI #dsarm043#2 = "S";
         #dsarm043#3 = "0,185,0;255,255,255";
     |SINO;
         #dsarm043#3 = "255,0,0;255,255,255";
     |FINSI;

     |GRABA 020#dsarm043.n;
     |LIBERA #dsarm043;
|FPRC;

|DEFBUCLE dsarz010;
     #dsarz010#1;
     ;
     "          ";
     "zzzzzzzzzz";
     ;
     NULL, dsarz010;
|FIN;

|PROCESO GrabaVistaUsu;
     |ABRE #dsarm043;
     |BUCLE dsarz010;
     |CIERRA #dsarm043;
|FPRC;

|PROCESO NuevaVista;
     |VENTANA 0501, 2899, -1, 17, "Nueva vista";
     nSubVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |DDEFECTO #915;
     |DDEFECTO #8;

     |PINPA #0#915;
     nPanta915 = FSalida;
     |PINDA #0#915;

     |C_M #915#0, 1, "S";
     |PINTA #915#0;

     ||ARA55
     |CONTROL_SIMPLE nPanta915, "BOTON,Acotaciones", 2702, 2716, BotonPermisos;
     nBotonPermisos = FSalida;

     |ENDATOS #1#915;
     FEstado = FSalida;
     |SI FSalida = 0;
         |PARA nCampo = 0;  |SI nCampo [ 33;  |HACIENDO nCampo = nCampo + 1;
               #8nCampo = #915nCampo;
         |SIGUE;
         |GRABA 020#8n;
         |LIBERA #8;

         |HAZ GrabaAcotaciones;
         |HAZ GrabaVistaUsu;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nSubVent;

     |SI FEstado = 0;
         aBusca = #915#0;
         |HAZ MontaArbol;
     |FINSI;

     |SI #1#191 = "S";  |Y eaCodVista ! "";
         |SUB_EJECUTA "dsarz050";
     |FINSI;
     eaCodVista = "";
|FPRC;

|PROCESO ModificarVista;
     #8#0 = aTexto;
     |LEE 101#8=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     eaCodVista = "";

     |VENTANA 0501, 2899, -1, 17, "Modificar vista";
     nSubVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PARA nCampo = 0;  |SI nCampo [ 33;  |HACIENDO nCampo = nCampo + 1;
           #915nCampo = #8nCampo;
     |SIGUE;

     |PINPA #0#915;
     nPanta915 = FSalida;
     |PINDA #0#915;

     |C_M #915#0, 1, "N";
     |PINTA #915#0;

     ||ARA55
     |CONTROL_SIMPLE nPanta915, "BOTON,Acotaciones", 2702, 2716, BotonPermisos;
     nBotonPermisos = FSalida;

     |ENDATOS #1#915;
     |SI FSalida = 0;
         |PARA nCampo = 0;  |SI nCampo [ 33;  |HACIENDO nCampo = nCampo + 1;
               #8nCampo = #915nCampo;
         |SIGUE;
     |FINSI;

     |GRABA 020#8a;
     |LIBERA #8;

     |HAZ GrabaAcotaciones;
     |HAZ GrabaVistaUsu;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nSubVent;

     aBusca = #915#0;
     |HAZ MontaArbol;

     |SI #1#191 = "S";  |Y eaCodVista ! "";
         |SUB_EJECUTA "dsarz050";
     |FINSI;
     eaCodVista = "";
|FPRC;

|PROCESO dsarm043_borra;
     |BORRA 020#dsarm043.a;
|FPRC;

|DEFBUCLE dsarm043_borra;
     #dsarm043#1;
     ;
     "          ", aVistaBorra;
     "zzzzzzzzzz", aVistaBorra;
     be;
     NULL, dsarm043_borra;
|FIN;

|PROCESO dsarm044_borra;
     |BORRA 020#dsarm044.a;
|FPRC;

|DEFBUCLE dsarm044_borra;
 #dsarm044#1;
 ;
 aVistaBorra, "         ";
 aVistaBorra, "zzzzzzzzz";
 be;
 NULL, dsarm044_borra;
|FIN;


|PROCESO BorrarVista;
     #8#0 = aTexto;
     |LEE 101#8=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |CONFI "0000NSeguro que quiere borrar la vista ";
     |SI FSalida ! 0;
          |LIBERA #8;
          |ACABA;
     |FINSI;

     aVistaBorra = #8#0;
     |BUCLE dsarm043_borra;
     |BUCLE dsarm044_borra;

     |BORRA 020#8a;
     |LIBERA #8;

     |SI #1#191 = "S";
         |DBASE aDirVistas;  |QBF aDirVistas;
         aDirVistas = aDirVistas + "vistas";
         aDirVistas = aDirVistas + "/";

         aFichero   = aDirVistas + "va" + (("000000" + #8#31) % -106) + ".dat";  |FBORRA aFichero;
         aFichero   = aDirVistas + "va" + (("000000" + #8#31) % -106) + ".ddx";  |FBORRA aFichero;
         aFichero   = aDirVistas + "va" + (("000000" + #8#31) % -106) + ".ixx";  |FBORRA aFichero;

         aFichero   = aDirVistas + "vb" + (("000000" + #8#31) % -106) + ".dat";  |FBORRA aFichero;
         aFichero   = aDirVistas + "vb" + (("000000" + #8#31) % -106) + ".ddx";  |FBORRA aFichero;
         aFichero   = aDirVistas + "vb" + (("000000" + #8#31) % -106) + ".ixx";  |FBORRA aFichero;
     |FINSI;

     eaCodVista = "";

     |LEE 000#8a;
     |SI FSalida ! 0;
         |LEE 000#8u;
     |FINSI;

     aBusca = #8#0;
     |HAZ MontaArbol;
|FPRC;

|PROCESO EventoTree;
     |SI FEntrada ! 0;  |ACABA;  |FINSI;

     aAlfa       = Contenido;  |QBF aAlfa;
     aTexto      = "";
     aLong       = aAlfa % 0;
     nLong       = aLong;
     nCargar     = 0;
     |PARA nPosi = 1;  |SI nPosi [ nLong;  |HACIENDO nPosi = nPosi + 1;
           nPos = (nPosi * 100) + 1;
           aCarac = aAlfa % nPos;

           |SI nCargar = 1;
               aTexto = aTexto + aCarac;
           |FINSI;

           |SI aCarac = ":";  nCargar = 1; |FINSI;
     |SIGUE;

     |ESTADO_CONTROL nBoton2, "ENABLE";
     |ESTADO_CONTROL nBoton3, "ENABLE";

     |SI aTexto = "";
         |ESTADO_CONTROL nBoton2, "DISABLE";
         |ESTADO_CONTROL nBoton3, "DISABLE";
     |FINSI;
|FPRC;

|PROCESO CargaArbol;
     |QBF aAlfa;
     aAlfa = aAlfa + &13 + &10;
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO Carpeta1;
     nCarpeta1 = nCarpeta1 + 1;
     aAlfa = nCarpeta0 + "," + nCarpeta1;
     aAlfa = aAlfa + ":(" + (("000" + #8#2) % -103) + ") " + #8#3;
     |HAZ CargaArbol;
|FPRC;

|PROCESO Carpeta2;
     nCarpeta2 = nCarpeta2 + 1;
     aAlfa = nCarpeta0 + "," + nCarpeta1 + "," + nCarpeta2;
     aAlfa = aAlfa + ":(" + (("000" + #8#4) % -103) + ") " + #8#5;
     |HAZ CargaArbol;
|FPRC;

|PROCESO Carpeta3;
     nCarpeta3 = nCarpeta3 + 1;
     aAlfa = nCarpeta0 + "," + nCarpeta1 + "," + nCarpeta2 + "," + nCarpeta3;
     aAlfa = aAlfa + ":(" + (("000" + #8#6) % -103) + ") " + #8#7;
     |HAZ CargaArbol;
|FPRC;

|PROCESO Carpeta4;
     nCarpeta4 = nCarpeta4 + 1;
     aAlfa = nCarpeta0 + "," + nCarpeta1 + "," + nCarpeta2 + "," + nCarpeta3 + "," + nCarpeta4;
     aAlfa = aAlfa + ":(" + (("000" + #8#8) % -103) + ") " + #8#9;
     |HAZ CargaArbol;
|FPRC;

|PROCESO Carpeta5;
     nCarpeta5 = nCarpeta5 + 1;
     aAlfa = nCarpeta0 + "," + nCarpeta1 + "," + nCarpeta2 + "," + nCarpeta3 + "," + nCarpeta4 + "," + nCarpeta5;
     aAlfa = aAlfa + ":(" + (("000" + #8#10) % -103) + ") " + #8#11;
     |HAZ CargaArbol;
|FPRC;

|PROCESO dsarm008;
     nCarpeta0 = nCarpeta0  + 1;
     nCarpeta1 = 0;
     nCarpeta2 = 0;
     nCarpeta3 = 0;
     nCarpeta4 = 0;
     nCarpeta5 = 0;

     aAlfa = nCarpeta0 + ":" + #8#0;  |QBF aAlfa;
     aAlfa = aAlfa + " {{" + #8#0 + "}}";
     |HAZ CargaArbol;

     |SI #8#1 = "1";
         |HAZ Carpeta1;

         nCarpeta2 = nCarpeta2 + 1;
         aAlfa = nCarpeta0 + "," + nCarpeta1 + "," + nCarpeta2;
         aAlfa = aAlfa + ":AQUI LOS DOCUMENTOS";
         |HAZ CargaArbol;
     |FINSI;

     |SI #8#1 = "2";
         |HAZ Carpeta1;
         |HAZ Carpeta2;

         nCarpeta3 = nCarpeta3 + 1;
         aAlfa = nCarpeta0 + "," + nCarpeta1 + "," + nCarpeta2 + "," + nCarpeta3;
         aAlfa = aAlfa + ":AQUI LOS DOCUMENTOS";
         |HAZ CargaArbol;
     |FINSI;

     |SI #8#1 = "3";
         |HAZ Carpeta1;
         |HAZ Carpeta2;
         |HAZ Carpeta3;

         nCarpeta4 = nCarpeta4 + 1;
         aAlfa = nCarpeta0 + "," + nCarpeta1 + "," + nCarpeta2 + "," + nCarpeta3 + "," + nCarpeta4;
         aAlfa = aAlfa + ":AQUI LOS DOCUMENTOS";
         |HAZ CargaArbol;
     |FINSI;

     |SI #8#1 = "4";
         |HAZ Carpeta1;
         |HAZ Carpeta2;
         |HAZ Carpeta3;
         |HAZ Carpeta4;

         nCarpeta5 = nCarpeta5 + 1;
         aAlfa = nCarpeta0 + "," + nCarpeta1 + "," + nCarpeta2 + "," + nCarpeta3 + "," + nCarpeta4 + "," + nCarpeta5;
         aAlfa = aAlfa + ":AQUI LOS DOCUMENTOS";
         |HAZ CargaArbol;
     |FINSI;

     |SI #8#1 = "5";
         |HAZ Carpeta1;
         |HAZ Carpeta2;
         |HAZ Carpeta3;
         |HAZ Carpeta4;
         |HAZ Carpeta5;

         nCarpeta6 = nCarpeta6 + 1;
         aAlfa = nCarpeta0 + "," + nCarpeta1 + "," + nCarpeta2 + "," + nCarpeta3 + "," + nCarpeta4 + "," + nCarpeta5 + "," + nCarpeta6;
         aAlfa = aAlfa + ":AQUI LOS DOCUMENTOS";
         |HAZ CargaArbol;
     |FINSI;
|FPRC;

|DEFBUCLE dsarm008;
     #8#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dsarm008;
|FIN;

|PROCESO MontaArbol;
     |SI nTree ! -1;
         |FIN_CONTROL_WINDOWS nTree;
         nTree = -1;
     |FINSI;

     |CONTROL_SIMPLE 0, "LABEL,{{5}}Cargando informaci¢n ...", 1315, 1680, NULL;
     nBotonLabel = FSalida;

     |PTEC 802;  |PAUSA;

     |DBASE aBase;  |QBF aBase;
     aAlfa = aBase + "tmp";          |MKDIR aAlfa;

     eaRutaOrigen  = aBase + "tmp/";

     |SI #1#7 = "N";
          |HAZ DimeUnidad;
          eaRutaDestino = eaUnidad + ":\documentos\dsarchi\";
     |SINO;
          |HAZ LeeUnidadBasico;
          eaRutaDestino = eaUnidadBasico + "\documentos\dsarchi\";
     |FINSI;

     eaFicheroTxt  = "dsarm00801" + Usuario + ".txt";
     eaFicheroTgz  = "dsarm00801" + Usuario + ".tgz";
     eaFicheroTar  = "dsarm00801" + Usuario + ".tar";

     aAbre = eaRutaOrigen + eaFicheroTxt;

     sT_nID   = nTree;
     sT_nOper = 0;

     |XABRE "WB", aAbre, nHandle;

     nCarpeta0 = 0;
     nCarpeta1 = 0;
     nCarpeta2 = 0;
     nCarpeta3 = 0;
     nCarpeta4 = 0;
     nCarpeta5 = 0;
     nCarpeta6 = 0;

     |BUCLE dsarm008;

     |XCIERRA nHandle;

     |HAZ TraeTxtComprimido;
     aAlfa = eaRutaDestino + eaFicheroTxt;

     eaAlfa = "TREECTRL,Vistas Arbol en Registros {{" + aAlfa + "}}";
     |CONTROL_SIMPLE nPanta, eaAlfa, 0602, 2598, EventoTree;
     nTree = FSalida;

     eaFicheroTxt  = "dsarm00801" + Usuario + ".txt";
     aAlfa         = eaRutaDestino + eaFicheroTxt;
     |RFBORRA aAlfa;

     |FIN_CONTROL_WINDOWS nBotonLabel;

     |SI aBusca ! "";
         sT_nID   = nTree;
         sT_nOper = 3;
         sT_aData = "[" + aBusca;
         |ESPECIFICA "COMMANDCONTROL", sTree;
         aRefresca = ":" + FSalida;
     |FINSI;

     sT_nID   = nTree;
     sT_nOper = 2;
     |SI aRefresca = "";
         sT_aData = "1:";
     |SINO;
         sT_aData = aRefresca;
     |FINSI;

     |ESPECIFICA "COMMANDCONTROL", sTree;
     |PULSATECLA;

     |FOCOTECLADO nTree;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     FSalida = 2799;
|FPRC;

|PROGRAMA;
     |VENTANA 0501, 2899, nPanta, 17, "Configuraci¢n Vista por Arbol en la Entrada de Registros";
     nSubVentZero = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVentZero;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;


     |ABRE #1;
     |LEE 000#1p;
     |SI FSalida ! 0;  |DDEFECTO #1;  |FINSI;
     |CIERRA #1;

     |ABRET #8;

     |CLS;
     |CABEZA "Vistas Arbol";

     |PINPA #0#8;
     nPanta = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}Nueva vista", 2602, 2615, NuevaVista;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Modificar vista", 2617, 2630, ModificarVista;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{199}}Borrar vista", 2632, 2645, BorrarVista;
     nBoton3 = FSalida;

     |HAZ CargaTemporal;
     |HAZ MontaArbol;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}Salir," + nTree;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nTree;
     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;

     |CIERRAT #8;

     |ABRE #916;  |CIERRA #916;  |DELINDEX #916;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVentZero;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nSubVentZero;
|FPRO;
