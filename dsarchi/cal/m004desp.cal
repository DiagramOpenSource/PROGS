|FICHEROS;
     dpntm001;
     dpntm004;
     dpntm005;

     dpntw007,MANTE;

     &labtraba@;
|FIN;

|VARIABLES;
     nUsuDespabox = 0;
     aUsuDespabox = "";
     nNumeroEnviados    = 0;
     nVentana           = 0;
     nBtnMT             = -1;
     nBtnDT             = -1;
     nBtnED             = -1;
     nBtnEA             = -1;
     nBtnEC             = -1;
     nBtnAD             = -1;
     nRango             = 0;
     nGrid              = 0;
     nTrabCapturados    = 0;
     nCorreosActuali    = 0;
     nPanW7             = 0;
     nOk                = 0;

     aEsc1              = "";
     aEsc2              = "";
     aRetu              = "";
     aTecla             = "";
     aBase              = "";
     aLaboral           = "";
     aDef               = "";
     aMensaje           = "";
     aAlfa              = "";

     aUsuario           = "";
     aCorreo            = "";
     nVentCorreo        = 0;
     aMarca             = "";
     aVerde             = "0,185,0;255,255,255";
     aRojo              = "255,80,80;255,255,255";
     aHora              = "";
     aFecha             = "";

     fFecha             = @;

     {-1}aVent          = "";
         aVent1         = 0;
         aVent2         = 0;
         aVent3         = 0;
         aVent4         = 0;
         aVent5         = "";

     &enEmpresa         = 0;
     &eaModoNet         = "";
     &eaUsuNet          = "";
     &eaFechaNet        = "";
     &eaAccesoNet       = "";
     &eaEmpNet          = "";
     &eaTraNet          = "";
     &eaAccNet          = "";
     &eaMaiNet          = "";
     &eaDesNet          = "";
     &eaLabNet          = "";
     &eaAcceso          = "";
     &eaDni             = "";
     &eaEmail           = "";
     &enSwHayRegistros  = 0;
|FIN;

|PROCESO PonColor_Despa;
     #dpntm004#9 = "";

     |SI #dpntm004#7 > "01.01.0000";
         #dpntm004#9 = aVerde;
     |FINSI;

     |SI #dpntm004#5 = "*";
         #dpntm004#9 = aVerde;
     |FINSI;

     eaEmail = #dpntm004#4;   |QBF eaEmail;
     |SI eaEmail = "";
         #dpntm004#9 = aRojo;
         #dpntm004#5 = "";
     |FINSI;

     |SI #dpntm004#7 = "01.01.0000";
         #dpntm004#9 = aRojo;
     |FINSI;
|FPRC;

|PROCESO LeeDPNTM005;
     |SI #dpntw007#1 = 0;
         #dpntw007#2 = "";  |PINTA #dpntw007#2;
     |FINSI;

     |ABRE #dpntm005;
     #dpntm005#0 = enEmpresa;
     #dpntm005#1 = #dpntw007#1;
     |LEE 000#dpntm005.=;
     |SI FSalida = 0;
         #dpntw007#2 = #dpntm005#2;  |PINTA #dpntw007#2;
     |FINSI;
     |CIERRA #dpntm005;
|FPRC;

|PROCESO Evento4_Despa;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#dpntm004.=;
         |SI FSalida = 0;
             |SI nBtnEC ! -1;  |ESTADO_CONTROL nBtnEC, "ENABLE";  |FINSI;

             |ESTADO_CONTROL nBtnED, "ENABLE";
             |ESTADO_CONTROL nBtnAD, "DISABLE";

             eaEmail = #dpntm004#4;   |QBF eaEmail;

             |SI #dpntm004#7 > "01.01.0000";  |Y eaEmail ! "";
                 |ESTADO_CONTROL nBtnAD, "ENABLE";
             |FINSI;
         |FINSI;
     |FINSI;

     |SI FSalida = 4;
         |HAZ MarcarDesmarcar;
     |FINSI;
|FPRC;

|PROCESO labtraba_Despa;
     eaDni = #labtraba@#7;   |QBF eaDni;

     #dpntm004#0 = enEmpresa;
     #dpntm004#1 = eaDni;
     |LEE 101#dpntm004.=;
     |SI FSalida ! 0;
         |DDEFECTO #dpntm004;
         #dpntm004#0  = enEmpresa;
         #dpntm004#1  = eaDni;
         #dpntm004#2  = #labtraba@#1;
         #dpntm004#3  = #labtraba@#2;
         #dpntm004#4  = #labtraba@#127;
         |SI #labtraba@#36 ! "          ";
             #dpntm004#11 = #labtraba@#36;
         |FINSI;

         #dpntm004#5 = "*";

         aUsuario = (("00000" + enEmpresa) % -105) + (("00000" + #labtraba@#1) % -105) ;
         #dpntm004#8 = aUsuario;
         |GRABA 020#dpntm004.b;

         nTrabCapturados = nTrabCapturados + 1;
     |FINSI;

     fFecha = #labtraba@#36;
     |SI #dpntm004#2 ! #labtraba@#1;
         |SI fFecha < #dpntm004#11;
             |ACABA;
         |FINSI;
     |FINSI;

     #dpntm004#2  = #labtraba@#1;
     #dpntm004#11 = fFecha;
     #dpntm004#4  = #labtraba@#127;

     |HAZ PonColor_Despa;

     |GRABA 020#dpntm004.a;
     |LIBERA #dpntm004;
|FPRC;

|DEFBUCLE labtraba_Despa;
     #labtraba@#1002;
     ;
     enEmpresa, 00001;
     enEmpresa, 99999;
     ;
     NULL, labtraba_Despa;
|FIN;

|PROCESO dpntm004_Despa;
     #dpntm004#9 = "";

     eaEmail = #dpntm004#4;   |QBF eaEmail;
     |SI eaEmail = "";  |O #dpntm004#7 = "01.01.0000";
         #dpntm004#5 = " ";
         #dpntm004#7 = "01.01.0000";
     |FINSI;

     |HAZ PonColor_Despa;

     nUsuDespabox = #dpntm004#8;
     |SI nUsuDespabox = 0;
          aUsuDespabox = (("00000" + #dpntm004#0) % -105) + (("00000" + #dpntm004#2) % -105) ;
          #dpntm004#8 = aUsuDespabox;
     |FINSI;

     |GRABA 020#dpntm004.a;
     |LIBERA #dpntm004;
|FPRC;

|DEFBUCLE dpntm004_Despa;
     #dpntm004#1;
     ;
     enEmpresa, "            ";
     enEmpresa, "zzzzzzzzzzzz";
     be;
     NULL, dpntm004_Despa;
|FIN;

|PROCESO CargaTrab_Despa;
     |BUCLE dpntm004_Despa;
     |BUCLE labtraba_Despa;
|FPRC;

|PROCESO MarcarDesmarcar;
     eaEmail = #dpntm004#4;   |QBF eaEmail;
     |SI eaEmail = "";
         |ACABA;
     |FINSI;

     |LEE 101#dpntm004.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aMarca = #dpntm004#5;
     |QBF aMarca;
     |SI aMarca = "*";
          #dpntm004#5 = " ";
     |SINO;
          #dpntm004#5 = "*";
     |FINSI;

     |GRABA 020#dpntm004.a;
     |LIBERA #dpntm004;

     |REFRESCACONTROL nGrid;
     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO Activa;
     |LEE 101#dpntm004.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI #dpntm004#7 > "01.01.0000";
         #dpntm004#7 = "01.01.0000";
         eaModoNet   = "Deshabilitar acceso trabajador en empresa";

         |CONFI "0000NEst  seguro de deshabilitar el trabajador";
     |SINO;
         #dpntm004#7 = ~;
         eaModoNet   = "Habilitar acceso trabajador en empresa";

         |CONFI "0000NEst  seguro de habilitar el trabajador";
     |FINSI;

     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |HAZ PonColor_Despa;

     |GRABA 020#dpntm004.a;
     |LIBERA #dpntm004;

     |HORA aHora;
     eaUsuNet      = Usuario % 810; |QBF eaUsuNet;
     fFecha        = ~;
     aFecha        = fFecha;
     eaFechaNet    = aFecha + " " + aHora;
     eaAccesoNet   = #dpntm001#1; |QBF eaAccesoNet;
     eaEmpNet      = ("00000" + #dpntm001#0) % -105;
     eaEmpNet      = eaEmpNet + " / " + #dpntm001#2;   |QBF eaEmpNet;
     eaEmpNet      = eaEmpNet + " / " + #dpntm001#10;  |QBF eaEmpNet;
     eaTraNet      = ("00000" + #dpntm004#2) % -105;
     eaTraNet      = eaTraNet + " / " + #dpntm004#3;   |QBF eaTraNet;
     eaAccNet      = #dpntm004#4;                      |QBF eaAccNet;
     eaDesNet      = "";
     eaLabNet      = "";

     |HAZ EnviaNotificacion;

     |REFRESCACONTROL nGrid;
     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO dpntm004Todos;
     #dpntm004#5 = aMarca;

     |HAZ PonColor_Despa;

     |GRABA 020#dpntm004.a;
     |LIBERA #dpntm004;
|FPRC;

|DEFBUCLE dpntm004Todos;
     #dpntm004#1;
     ;
     enEmpresa, "            ";
     enEmpresa, "zzzzzzzzzzzz";
     be;
     NULL, dpntm004Todos;
|FIN;

|PROCESO Todos004;
     |BUCLE dpntm004Todos;
|FPRC;

|PROCESO MarcarTodos;
     aMarca = "*";
     |HAZ Todos004;
     |REFRESCACONTROL nGrid;
     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO DesmarcarTodos;
     aMarca = " ";
     |HAZ Todos004;
     |REFRESCACONTROL nGrid;
     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO Edita_Despa;
     |VENTANA 0501, 1599, -1, 16, "Editar datos trabajador";
     nVentCorreo = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentCorreo;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     #dpntw007#0 = #dpntm004#4;
     #dpntw007#1 = #dpntm004#10;

     |PINPA #0#dpntw007;      nPanW7 = FSalida;

     |HAZ LeeDPNTM005;

     eaDni = #dpntm004#1;  |QBF eaDni;

     |PINDA #0#dpntw007;
     |ENDATOS #2#dpntw007;
     |SI FSalida = 0;
          |SI #dpntm004#4 ! #dpntw007#0;
              #dpntm004#4  = #dpntw007#0;
          |FINSI;

          #dpntm004#10 = #dpntw007#1;

          |HAZ PonColor_Despa;

          |GRABA 020#dpntm004.a;

          eaEmail = #dpntw007#0;
          |HAZ GrabaEmailTraba;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentCorreo;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVentCorreo;

     |REFRESCACONTROL nGrid;
     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO dpntm004Enviar;
     |SI #dpntm004#5 = "*";
          eaEmail = #dpntm004#4;
          |QBF eaEmail;
          |SI eaEmail ! "";
               nNumeroEnviados = nNumeroEnviados + 1;
               |HAZ EnvioCorreoActiva;

               #dpntm004#7 = ~;

               |HAZ PonColor_Despa;

               |GRABA 020#dpntm004.a;
          |FINSI;
     |FINSI;

     |LIBERA #dpntm004;
|FPRC;

|DEFBUCLE dpntm004Enviar;
     #dpntm004#1;
     ;
     enEmpresa, "            ";
     enEmpresa, "zzzzzzzzzzzz";
     be;
     NULL, dpntm004Enviar;
|FIN;

|PROCESO EnviarTodos;
     nNumeroEnviados = 0;

     |BUCLE dpntm004Enviar;

     aMarca = "";
     |BUCLE dpntm004Todos;

     |REFRESCACONTROL nGrid;
     |FOCOTECLADO nGrid;

     |SI nNumeroEnviados = 0;
          aMensaje = "0000Ningun correo enviado. Revise que los trabajadores marcados tengan direcci¢n de correo";
     |SINO;
          |SI nNumeroEnviados = 1;
               aMensaje = "00001 correo enviado.";
          |SINO;
               aMensaje = "0000" + nNumeroEnviados + " correos enviados.";
          |FINSI;
     |FINSI;
     |MENAV aMensaje;
|FPRC;

|PROCESO EnviarActual;
     eaEmail = #dpntm004#4;  |QBF eaEmail;
     |SI eaEmail = "";
          aMensaje = "0000Trabajador sin direcci¢n de correo. Env¡o cancelado.";
          |MENAV aMensaje;
          |CIERRA #dpntm001;
          |ACABA;
     |FINSI;

     |HAZ EnvioCorreoActiva;

     #dpntm004#5 = "";
     #dpntm004#7 = ~;

     |HAZ PonColor_Despa;
     |GRABA 020#dpntm004.a;
     |CIERRA #dpntm001;

     |MENAV "0000Correo acceso enviado.";

     |REFRESCACONTROL nGrid;
     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO Baja4_Despa;
     #dpntm004#0 = enEmpresa;
     #dpntm004#1 = "";
|FPRC;

|PROCESO Alta4_Despa;
     #dpntm004#0 = enEmpresa;
     #dpntm004#1 = "z" * 12;
|FPRC;

|PROCESO Despabox;
     nTrabCapturados    = 0;
     nCorreosActuali    = 0;
     |HAZ CargaTrab_Despa;
     |SI enSwHayRegistros = 0; |Y nTrabCapturados = 0;
         aMensaje = "0000Esta empresa NO tiene trabajadores definidos";
         |MENAV aMensaje;
     |SINO;
         |SI nTrabCapturados ! 0;
             |SI nTrabCapturados = 1;
                 aMensaje = "0000A¤adido y marcado 1 nuevo trabajador.";
             |SINO;
                 aMensaje = "0000A¤adidos y marcados " + nTrabCapturados + " nuevos trabajadores.";
             |FINSI;

             |SI nCorreosActuali ! 0;
                 |SI nCorreosActuali = 1;
                     aMensaje = aMensaje + " 1 direcci¢n de correo.";
                 |SINO;
                     aMensaje = aMensaje + " " + nCorreosActuali + " direcciones de correos.";
                 |FINSI;
             |FINSI;
             |MENAV aMensaje;
         |FINSI;
     |FINSI;

     |CLS;
     aAlfa = "Acceso Trabajadores " + eaAcceso;
     |CABEZA aAlfa;

     aAlfa = "Acceso " + eaAcceso + " Trabajadores Empresa: " + #dpntm001#2;  |QBF aAlfa;
     |VENTANA 0501, 2899, -1, 16, aAlfa;
     nVentana = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |CONTROL_SIMPLE 0, "LABEL,{{15}}Doble click en el registro para marcar o desmarcar", 0502, 0598, NULL;

     |CONTROL_SIMPLE 0, "BOTON,Marcar Todos", 2502, 2612, MarcarTodos;
     nBtnMT = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,Desmarcar Todos", 2514, 2628, DesmarcarTodos;
     nBtnDT = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,Enviar Activaci¢n Todos Marcados", 2530, 2648, EnviarTodos;
     nBtnEA = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,Enviar Correo Activaci¢n", 2550, 2668, EnviarActual;
     nBtnEC = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,Editar datos trabajador", 2570, 2682, Edita_Despa;
     nBtnED = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,Habilitar/Deshabilitar trabajador", 2584, 2698, Activa;
     nBtnAD = FSalida;

     |SI nBtnEC ! -1;  |ESTADO_CONTROL nBtnEC, "DISABLE";  |FINSI;

     |ESTADO_CONTROL nBtnED, "DISABLE";
     |ESTADO_CONTROL nBtnAD, "DISABLE";

     |C_V #dpntm004#5, 1, "S";

     nRango = 4 + 8 + 16 + 32 + 128;

     |LINEAL_SIMPLE #1#dpntm004, nRango, 0602, 2498, Baja4_Despa, Alta4_Despa, Evento4_Despa;
     nGrid = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}Salir," + nGrid;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |DESTRUYECONTROL nGrid;

     |FIN_CONTROL_WINDOWS nBtnMT;
     |FIN_CONTROL_WINDOWS nBtnDT;
     |FIN_CONTROL_WINDOWS nBtnEA;
     |FIN_CONTROL_WINDOWS nBtnEC;
     |FIN_CONTROL_WINDOWS nBtnED;
     |FIN_CONTROL_WINDOWS nBtnAD;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVentana;
|FPRC;
