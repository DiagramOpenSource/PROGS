|FICHEROS;
     dsarz053 #0;
     dsarm058 #58;       ||Ctrl. Archivado Auto
     dsarw085 #85,MANTE; ||Pan. Aux. Archi.Auto

     dsarm002 #2;
     dsarm003 #3;
     dsarm004 #4;

     dsarm001 #1;
|FIN;

|VARIABLES;
     aEjecuta = "";
     &enGrupoTres = 0;
     &enSubGTres  = 0;
     &enTipoTres  = 0;

     aQueBoton     = "";
     aID           = "";
     nBotonAceptar = 0;
     nBotonCancelar = 0;
     nUltimo = 0;
     nIdSalir = 0;
     {2}nNivel = 0;
     nVentana = 0;
     nPantaDos = 0;
     aCadena = "";
     aAlfa = "";
     nCampoHasta = 0;
     nCampoD = 0;
     nCampoH = 0;

     {-1}aVent      = "";
         aVent1     = 0;
         aVent2     = 0;
         aVent3     = 0;
         aVent4     = 0;
         aVent5     = "";

     aMensaje = "";

     nBoton1 = 0;
     nBoton2 = 0;
     nBoton3 = 0;
     nBotonSel = 0;

     nPanta = 0;
     nRango = 0;
     nPos1  = 0;
     nPos2  = 0;
     nGrid = 0;
     aEsc1  = "";
     aEsc2  = "";
     aRetu  = "";
     aTecla = "";

     nModo = 0;
|FIN;

|PROCESO Tipo0C4Fw85;
     |SI #1#8   = "N";  |ACABA;  |FINSI;
     |SI FSalida  = 2;  |ACABA;  |FINSI;

     |ABRE #4;
     #4#0 = #dsarw085#2;
     #4#1 = #dsarw085#3;
     #4#2 = #dsarw085#4;
     |LEE 000#4=;
     |SI FSalida ! 0;
          |MENAV "    Tipo inexistente"; |ERROR;
          #dsarw085#7 = "";
     |SINO;
          #dsarw085#7 = #4#3;
     |FINSI;
     |CIERRA #4;

     |PINTA #dsarw085#7;
|FPRC;

|PROCESO Tipo0C3Fw85;
     |SI #1#8   = "N";  |ACABA;  |FINSI;
     |SI FSalida  = 2;  |ACABA;  |FINSI;

     |ABRE #3;
     #3#0 = #dsarw085#2;
     #3#1 = #dsarw085#3;
     |LEE 000#3=;
     |SI FSalida ! 0;
          |MENAV "    Subgrupo inexistente"; |ERROR;
          #dsarw085#6 = "";
     |SINO;
          #dsarw085#6 = #3#2;
     |FINSI;
     |CIERRA #3;

     |PINTA #dsarw085#6;
|FPRC;

|PROCESO Tipo0C2Fw85;
     |SI FSalida  = 2;  |ACABA;  |FINSI;

     |ABRE #2;
     #2#0 = #dsarw085#2;
     |LEE 000#2=;
     |SI FSalida ! 0;
          |MENAV "    Grupo inexistente"; |ERROR;
          #dsarw085#5 = "";
     |SINO;
          #dsarw085#5 = #2#1;
     |FINSI;
     |CIERRA #2;

     |PINTA #dsarw085#5;
|FPRC;

|PROCESO Tipo12w85; |TIPO 12;
     |SI FSalida = 7;  |ACABA;  |FINSI;

     |ABRE #4;
     #4#0 = #dsarw085#2;
     #4#1 = #dsarw085#3;
     #4#2 = #dsarw085#4;
     |LEE 000#4=;
     |SI FSalida ! 0;
         |MENAV "    Datos de la ubicaci¢n en la Gesti¢n documental inexistente";
         |ERROR;
         |CIERRA #4;
         |ACABA;
     |FINSI;
     |CIERRA #4;
|FPRC;

|PROCESO BotonSel;
     ||ARA39
     |SI #1#8 = "S";
          enGrupoTres = 0;
          enSubGTres  = 0;
          enTipoTres  = 0;
          aEjecuta = "dsarz003;DAMETRE";
          |SUB_EJECUTA_NP aEjecuta;
          |SI enGrupoTres ! 0;
               #dsarw085#2 = enGrupoTres;    |PINTA #dsarw085#2;
               #dsarw085#3 = enSubGTres;     |PINTA #dsarw085#3;
               #dsarw085#4 = enTipoTres;     |PINTA #dsarw085#4;

               |HAZ Tipo0C2Fw85;
               |HAZ Tipo0C3Fw85;
               |HAZ Tipo0C4Fw85;
          |FINSI;
     |SINO;
          |ABRE #2;
          |CONSULTA_CLAVE #1#2;
          |SI FSalida = 0;
               #dsarw085#2 = #2#0;    |PINTA #dsarw085#2;
               #dsarw085#3 = 1;       |PINTA #dsarw085#3;
               #dsarw085#4 = 1;       |PINTA #dsarw085#4;

               |HAZ Tipo0C2Fw85;
          |FINSI;
          |CIERRA #2;
     |FINSI;
|FPRC;

|PROCESO ObtenIdSalir;
     InNivel = nNivel1<-;
     nNivel1 = 0;
     |ESPECIFICA "IDBOTONTECLA", nNivel;
     nIdSalir = nNivel2;
|FPRC;

|PROCESO Tipo0C0w85; |TIPO 0;
     |SI nModo = 1;
          #dsarm058#0 = #dsarw085#0;
          |LEE 000#dsarm058.=;
          |SI FSalida = 0;
               aMensaje = "0000Ya existe la configuracion para esta plantilla";
               |MENAV aMensaje;
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO ModiAlta;
     nIdSalir = 0;
     |HAZ ObtenIdSalir;
     |ESTADO_CONTROL nIdSalir, "HIDE";

     ||VENTANA 0501, 2999, -1, -1, "";
     |VENTANA 0501, 2999, -1, 17, "";
     nVentana = FSalida;

     aCadena ="|NC";
     aAlfa = #58#aCadena#;
     nCampoHasta = aAlfa;
     nCampoHasta = nCampoHasta - 1;

     |SI nModo ! 1;
          |PARA nCampoD = 0;  |SI nCampoD [ nCampoHasta; |HACIENDO nCampoD = nCampoD + 1;
                nCampoH = nCampoD;
                #85nCampoH = #58nCampoD;
          |SIGUE;

          |C_M #85#0, 1, "N";
     |SINO;
          |C_M #85#0, 1, "S";
          nUltimo = 0;
          |HAZ DameUltimo;
          |DDEFECTO #85;
          nUltimo = nUltimo + 1;
          #85#0 = nUltimo;
     |FINSI;

     |PINPA #0#85; nPantaDos = FSalida;
     |PINDA #0#85;

     |CONTROL_SIMPLE nPantaDos, "BOTON,{{197}}Asignar carpeta", 1176, 1286, BotonSel;
     nBotonSel = FSalida;

     |SI nModo = 1;  |ENDATOS #1#85;  |FINSI;
     |SI nModo = 2;  |ENDATOS #2#85;  |FINSI;

     |SI FSalida = 0;
          |HAZ GrabaDatos;
     |FINSI;

     |FINVENTANA nVentana;

     |ESTADO_CONTROL nIdSalir, "SHOW";
|FPRC;

|PROCESO DameUltimo;
     |LEE 000#dsarm058.u;
     |SI FSalida = 0;
          nUltimo = #dsarm058#0;
     |SINO;
          nUltimo = 0;
     |FINSI;
|FPRC;

|PROCESO GrabaDatos;
     #58#0 = #85#0;
     |LEE 101#58=;
     |SI FSalida ! 0;
         |DDEFECTO #58;
         #58#0 = #85#0;
         |GRABA 020#58b;
     |FINSI;

     |PARA nCampoD = 1;  |SI nCampoD [ nCampoHasta;  |HACIENDO nCampoD = nCampoD + 1;
           nCampoH   = nCampoD;
           #58nCampoD = #85nCampoH;
     |SIGUE;

     |GRABA 020#58a;
     |LIBERA #58;
|FPRC;


|PROCESO Tipo80C0; |TIPO 80;
     FSalida = 2999;
|FPRC;

|PROCESO BajaPanta;
     #dsarm058#0 = 01;
|FPRC;

|PROCESO AltaPanta;
     #dsarm058#0 = 99;
|FPRC;

|PROCESO EventoPanta;
     |SI FSalida = 1;  |O FSalida = 2;
          |LEE 000#dsarm058.=;
          |SI FSalida = 0;
               |ESTADO_CONTROL nBoton2, "ENABLE";
               |ESTADO_CONTROL nBoton3, "ENABLE";
          |SINO;
               |ESTADO_CONTROL nBoton2, "DISABLE";
               |ESTADO_CONTROL nBoton3, "DISABLE";
          |FINSI;
     |FINSI;

     |SI FSalida = 4;
          |HAZ Boton2;
     |FINSI;
|FPRC;

|PROCESO Boton1;
     nModo = 1;
     |HAZ ModiAlta;
     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO Boton2;
     |LEE 000#dsarm058.=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     nModo = 2;
     |HAZ ModiAlta;
     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO Boton3;
     |LEE 000#dsarm058.=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |LEE 101#dsarm058.=;
     |SI FSalida = 0;
          aMensaje = "0000NDesea Borrar " + #58#1;
          |QBF aMensaje;
          aMensaje = aMensaje + "?";
          |CONFI aMensaje;
          |SI FSalida = 0;
               |BORRA 020#dsarm058.a;
          |FINSI;
          |LIBERA #dsarm058;
     |FINSI;

     |REFRESCACONTROL nGrid;
|FPRC;

|PROGRAMA;
     |CLS;
     |CABEZA "Configuraci¢n Tipos destino Archivado Autom tico";

     |ABRE #dsarm001;
     |DDEFECTO #dsarm001;
     |LEE 000#dsarm001.=;
     |CIERRA #dsarm001;

     |ABRE #dsarm058;

     |PINPA #0#dsarz053;
     nPanta = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}Alta ", 2753, 2763, Boton1;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Modificar ", 2765, 2775, Boton2;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{199}}Baja ", 2777, 2787, Boton3;
     nBoton3 = FSalida;

     nRango = 4 + 8 + 16 + 32;
     nPos1  = 0702;
     nPos2  = 2698;

     |LINEAL_SIMPLE #1#dsarm058, nRango, nPos1, nPos2, BajaPanta, AltaPanta, EventoPanta;
     nGrid = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}Salir," + nGrid;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nGrid;

     |DESTRUYECONTROL nBoton1;
     |DESTRUYECONTROL nBoton2;
     |DESTRUYECONTROL nBoton3;

     |CIERRA #dsarm058;
|FPRO;
