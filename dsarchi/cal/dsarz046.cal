|FICHEROS;
     dsarz046 #0;        ||Este Def

     dsarm001 #1;        ||Parametros

     dsarm054 #54;       ||Band.exporta.DsEscan
     dsarm034;           ||Resultado dsEscan
     dsarm035 #35;       ||VAR. Resul. dsEscan
     dsarm005 #5;
|FIN;

|VARIABLES;
     aEtiqueta = "";
     aValor = "";
     aEstado = "";
     nSituacion = 0;
     aAlfa = "";
     aFicheroOCR = "";
     aAuxNom = "";
     aIPRemoto = "";
     nHandleTxt = 0;
     aLinea = "";
     nSalida = 0;
     nRegExpo = 0;

     aTexto = "";
     aPathExpo = "";
     aNomFic = "";
     aNomCompleto = "";
     nHandle = 0;
     nHayExpo = 0;

     aMensaje = "";
     nRango = 0;
     nPos1 = 0;
     nPos2 = 0;
     nGrid = 0;
     aEsc1 = "";
     aEsc2 = "";
     aRetu = "";
     aTecla = "";
     nBoton1 = 0;
     nBoton2 = 0;

     nSwAuto = 0;
     aModo = "";
     aParametro = "";

     nPanta = 0;
     aVacio20 = "";
     aVacio25 = "";
     aZeta20 = "";
     aZeta25 = "";

     aFechaDesde = "";
     aFechaHasta = "";
|FIN;

|PROCESO dsarm035;
     aEtiqueta = #dsarm035#10;
     aEtiqueta = aEtiqueta - ":";
     aValor = #dsarm035#4;
     |HAZ MeteValor;
|FPRC;

|DEFBUCLE dsarm035;
 #dsarm035#1;
 ;
 #dsarm034#0, #dsarm034#1, #dsarm034#2, aVacio25;
 #dsarm034#0, #dsarm034#1, #dsarm034#2, aZeta25;
 ;
 NULL, dsarm035;
|FIN;

|PROCESO ExportaUno;
     #dsarm034#0 = #54#0;
     #dsarm034#1 = #54#1;
     #dsarm034#2 = #54#2;
     |LEE 000#dsarm034.=;
     |SI FSalida ! 0;
          ||No exporta NADA, porque no existe el documento en la bandeja dsEscan
          |ACABA;
     |FINSI;

     aNomFic = ("000000000" + #54#3) % -109;
     aNomFic = "expo" + aNomFic + ".xml";

     aNomCompleto = aPathExpo + aNomFic;

     |XABRE "CW", aNomCompleto, nHandle;
     |SI FSalida < 0;
          aMensaje = "0000Problemas al crear el fichero " + aNomCompleto;
          |MENAV aMensaje;
          |ACABA;
     |FINSI;

     aEtiqueta = "NumeroRegistro";
     aValor = #54#3;
     |HAZ MeteValor;

     |HAZ DameEstado;
     aEtiqueta = "Estado";
     aValor = aEstado;
     |HAZ MeteValor;

     aEtiqueta = "FechaArchivado";
     aValor = #dsarm034#0;
     |HAZ MeteValor;

     aEtiqueta = "HoraArchivado";
     aValor = #dsarm034#1;
     |HAZ MeteValor;

     aEtiqueta = "NombreArchivo";
     aValor = #dsarm034#2;
     |HAZ MeteValor;

     aEtiqueta = "DirIPArchiva";
     aValor = #dsarm034#4;
     |HAZ MeteValor;

     aEtiqueta = "CarpetaPCArchiva";
     aValor = #dsarm034#5;
     |HAZ MeteValor;

     aEtiqueta = "UsuarioArchiva";
     aValor = #dsarm034#6;
     |HAZ MeteValor;

     aEtiqueta = "CodPlantilla";
     aValor = #dsarm034#7;
     |HAZ MeteValor;

     aEtiqueta = "DesPlantilla";
     aValor = #dsarm034#8;
     |HAZ MeteValor;

     aEtiqueta = "RGBColor";
     aValor = #dsarm034#15;
     |HAZ MeteValor;

     aEtiqueta = "ProcesoPI";
     aValor = #dsarm034#17;
     |HAZ MeteValor;

     aEtiqueta = "EmpContagen";
     aValor = #dsarm034#18;
     |HAZ MeteValor;

     aEtiqueta = "PeriodoContagen";
     aValor = #dsarm034#19;
     |HAZ MeteValor;

     aEtiqueta = "DirMAC";
     aValor = #dsarm034#20;
     |HAZ MeteValor;

     |BUCLE dsarm035;

     |HAZ MeteTextodsEscan;

     ||TODO CCC
     |XCIERRA nHandle;

     #54#4 = "S";
     |GRABA 020#54.a;
     nHayExpo = 1;

     nRegExpo = nRegExpo + 1;
|FPRC;

|PROCESO MeteTextodsEscan;
     #5#0 = #dsarm034#16;
     |LEE 000#5=;
     |SI FSalida ! 0;
          |ACABA;
     |FINSI;

     aAuxNom = #dsarm005#9;
     |QBF aAuxNom;

     aFicheroOCR  = #dsarm005#7;
     |QBF aFicheroOCR;
     aFicheroOCR  = aFicheroOCR + aAuxNom + ".txt";

     aIPRemoto   = "";
     |IP_REMOTO aIPRemoto;

     |SI #1#7 = "N";
          |XABRE "U", aFicheroOCR, nHandleTxt;
     |SINO;
          |XABRE "CU", aFicheroOCR, nHandleTxt;
     |FINSI;
     |SI FSalida < 0; |ACABA; |FINSI;

     aTexto = "<TEXTORESULTADODSESCAN>";
     |XGRABA nHandle, aTexto;

     nSalida = 0;
     |PARA; |SI; |HACIENDO;
          |SI nSalida < 0; |SAL; |FINSI;
          |XLEE nHandleTxt, 250, aLinea;
          nSalida = FSalida;
          |QBF aLinea;

          aTexto = aLinea;
          |QBF aTexto;
          |XGRABA nHandle, aTexto;
     |SIGUE;

     aTexto = "</TEXTORESULTADODSESCAN>";
     |XGRABA nHandle, aTexto;

     |XCIERRA nHandleTxt;
|FPRC;

|PROCESO DameEstado;
     aEstado = "Desconocido";

     nSituacion = 0;
     aAlfa      = #dsarm034#15;  |QBF aAlfa;

     |SI aAlfa = "110,110,110;255,255,255";    || Gris
         nSituacion = 1;
     |FINSI;

     |SI aAlfa = "192,192,192;255,255,255";    || Otro Gris
         nSituacion = 1;
     |FINSI;

     |SI #dsarm034#7 = 0;  |Y #dsarm034#9 ! "I";
         nSituacion = 1;
     |FINSI;

     |SI #dsarm034#9 = "N"; |Y #dsarm034#10 = "N";  |Y #dsarm034#7 ! 0;
         nSituacion = 2;
     |FINSI;

     |SI #dsarm034#9 = "S"; |Y #dsarm034#10 = "N";
         nSituacion = 3;
     |FINSI;

     |SI #dsarm034#9 = "S"; |Y #dsarm034#10 = "S";
         nSituacion = 4;

         #5#0 = #dsarm034#16;
         |LEE 000#5=;
         |SI FSalida ! 0;   |DDEFECTO #5;  |FINSI;

         |SI #5#71 ! 0;
             nSituacion = 5;
         |FINSI;
     |FINSI;

     |SI #dsarm034#9 = "I";
         nSituacion = 6;
     |FINSI;

     |SI nSituacion = 1;  aEstado = "Pend. Plantilla";    |FINSI;
     |SI nSituacion = 2;  aEstado = "Pend. Integridad";   |FINSI;
     |SI nSituacion = 3;  aEstado = "Pend. Validar";      |FINSI;
     |SI nSituacion = 4;  aEstado = "Pend. Contabilizar"; |FINSI;
     |SI nSituacion = 5;  aEstado = "Contabilizado";      |FINSI;
     |SI nSituacion = 6;  aEstado = "No Valido";          |FINSI;
|FPRC;

|PROCESO MeteValor;
     |QBF aEtiqueta;
     |QBF aValor;
     aTexto = "<" + aEtiqueta + ">";
     aTexto = aTexto + aValor;
     aTexto = aTexto + "</" + aEtiqueta + ">";
     |XGRABA nHandle, aTexto;
|FPRC;

|PROCESO Evento;
     |SI FSalida = 1; |O FSalida = 2;
          |LEE 000#54.=;
     |FINSI;

     |SI FSalida = 4;
          |LEE 101#54.=;
          |SI FSalida = 0;
               nHayExpo = 0;
               aMensaje = "0000NExportar este registro?";
               |CONFI aMensaje;
               |SI FSalida = 0;
                    |HAZ ExportaUno;
               |FINSI;
               |LIBERA #54;
               |SI nHayExpo = 1;
                    |REFRESCACONTROL nGrid;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO BajaPanta;
     #54#0 = "31.12.2099";
     #54#1 = "99:99";
     #54#2 = aVacio20;
|FPRC;

|PROCESO AltaPanta;
     #54#0 = "01.01.0000";
     #54#1 = "00:00";
     #54#2 = aZeta20;
|FPRC;

|PROCESO dsarm054;
     |SI aModo = "T";
          |HAZ ExportaUno;
     |SINO;
          |SI #dsarm054#4 = "N";
               |HAZ ExportaUno;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE dsarm054;
 #dsarm054#2;
 ;
 INICIO;
 FINAL;
 be;
 NULL, dsarm054;
|FIN;

|PROCESO Exportacion;
     |BUCLE dsarm054;
|FPRC;

|PROCESO Boton1;
     nHayExpo = 0;
     nRegExpo = 0;
     aModo = "P";
     |HAZ Exportacion;
     |SI nHayExpo = 1;
          |SI nRegExpo = 1;
               aMensaje = "0000Exportado 1 registro. Directorio: " + aPathExpo;
          |SINO;
               aMensaje = "0000Exportados " + nRegExpo + " registros. Directorio: " + aPathExpo;
          |FINSI;
          |MENAV aMensaje;
          |REFRESCACONTROL nGrid;
          |PTEC 806;
     |SINO;
          aMensaje = "0000No hay nada para exportar";
          |MENAV aMensaje;
     |FINSI;
|FPRC;

|PROCESO Boton2;
     aMensaje = "0000Atenci¢n: este proceso requiere cierto tiempo de proceso, dependiendo del n£mero de documentos.";
     |MENAV aMensaje;

     aMensaje = "0000NRealizar proceso?";
     |CONFI aMensaje;
     |SI FSalida = 0;
          nHayExpo = 0;
          nRegExpo = 0;
          aModo = "T";
          |HAZ Exportacion;
          |SI nHayExpo = 1;
               |SI nRegExpo = 1;
                    aMensaje = "0000Exportado 1 registro. Directorio: " + aPathExpo;
               |SINO;
                    aMensaje = "0000Exportados " + nRegExpo + " registros. Directorio: " + aPathExpo;
               |FINSI;
               |MENAV aMensaje;
               |REFRESCACONTROL nGrid;
               |PTEC 806;
          |SINO;
               aMensaje = "0000No hay nada para exportar";
               |MENAV aMensaje;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo80; |TIPO 80;
     FSalida = 2799;
|FPRC

|PROCESO dsarm034;
     ||Miro si existe en el 54, y sino la creo.

     |DDEFECTO #dsarm054;
     #dsarm054#0 = #dsarm034#0;
     #dsarm054#1 = #dsarm034#1;
     #dsarm054#2 = #dsarm034#2;
     |LEE 000#dsarm054.=;
     |SI FSalida ! 0;
          |DDEFECTO #dsarm054;
          #dsarm054#0 = #dsarm034#0;
          #dsarm054#1 = #dsarm034#1;
          #dsarm054#2 = #dsarm034#2;
          |LEE 101#dsarm054.=;
          #dsarm054#3 = #dsarm034#16;
          #dsarm054#4 = "N";
          |GRABA 020#dsarm054.n;
          |LIBERA #dsarm054;
     |FINSI;
|FPRC;

|DEFBUCLE dsarm034;
 #dsarm034#1;
 ;
 aFechaDesde, "00:00", aVacio20;
 aFechaHasta, "99:99", aZeta20;
 ;
 NULL, dsarm034;
|FIN;

|PROGRAMA;
     aVacio20 = " " * 20;
     aVacio25 = " " * 25;
     aZeta20 = "z" * 20;
     aZeta25 = "z" * 25;

     |ABRE #dsarm001;
     |DDEFECTO #dsarm001;
     |LEE 000#dsarm001.=;
     aPathExpo = #dsarm001#189;
     |QBF aPathExpo;
     |SI aPathExpo = "";
          aMensaje = "0000No ha especificado el directorio de exportaci¢n. Proceso cancelado";
          |MENAV aMensaje;
          |ACABA;
     |FINSI;
     |CIERRA #dsarm001;

     |RMKDIR aPathExpo;

     |ABRE #dsarm005;
     |ABRET #54;
     |ABRET #dsarm034;
     ||Proceso para actualizar esta tabla al entrar
     |SELECT #2#54;
     |LEE 000#54.p;
     |SI FSalida = 0;
          aFechaDesde = #54#0;
     |SINO;
          aFechaDesde = "01.01.0000";
     |FINSI;
     aFechaHasta = "31.12.2099";

     |BUCLE dsarm034;

     aParametro = PARAMETRO;
     |QBF aParametro;
     |SI aParametro = "AUTO";
          nSwAuto = 1;
          ||Para la exportacion de registro indicado.
          ||Atencion son 3 campos la clave.
     |SINO;
          nSwAuto = 0;


          |PINPA #0#dsarz046;
          nPanta = FSalida;

          nRango = 4 + 8 + 16 + 32;
          nPos1  = 0802;
          nPos2  = 2698;

          |LINEAL_SIMPLE #2#54, nRango, nPos1, nPos2, BajaPanta, AltaPanta, Evento;
          nGrid = FSalida;

          |CONTROL_SIMPLE nPanta, "BOTON,Exportar Pendientes", 2702, 2731, Boton1;
          nBoton1 = FSalida;

          |CONTROL_SIMPLE nPanta, "BOTON,Exportar Todos", 2733, 2762, Boton2;
          nBoton2 = FSalida;

          aEsc1  = &255 + "806";
          aEsc2  = &255 + "807";
          aRetu  = &255 + "802";
          |PARA; |SI;  |HACIENDO;
              FSalida = "::{{001}}Salir," + nGrid;
              |LEETECLA aTecla;
              |SI aTecla = aEsc1; |SAL;  |FINSI;
              |SI aTecla = aEsc2; |SAL;  |FINSI;
              |SI aTecla = aRetu; |SAL;  |FINSI;
          |SIGUE;
     |FINSI;
     |CIERRAT #54;
     |CIERRAT #dsarm034;
     |CIERRA #dsarm005;
|FPRO;
