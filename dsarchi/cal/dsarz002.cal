|| Para los acentos, usare los patrones .. concretamente los caracteres acentuados
|| los cambiare por "*."

|FICHEROS;
     dsarm001 #1;
     dsarm002 #2;
     dsarm003 #3;
     dsarm004 #4;
     dsarm005 #5;
     dsarz002 #902;

     dsarm005@ #905;
|FIN;

|VARIABLES;
     &eaFicAso = "";
     &enSwModoAsociar = 0;

     aSaca = "";
     nAscii = 0;
     aTextoBusqueda = "";
     aAbre = "";
     aBarra = "";
     aAuxBarra = "";
     aLong = "";
     nLong = 0;
     nVoy = 0;
     nSaca = 0;
     aLetra = "";

     aPathDlls = "";
     aSitu = "";
     aLinea = "";
     aNumero = "";
     nNumero = 0;
     aUsuario = "";
     aFicRespuesta = "";
     aFicResError = "";
     aEjecuta = "";
     aDirBaseServidor = "";
     aSistema = "";

     aLabel = "";
     aTexto = "";
     aMensaje = "";
     nPanta0      = 0;
     nCampo       = 0;
     nHandle      = 0;
     nNume        = 0;
     nVentz2      = 0;
     nBotonLabel  = 0;

     aAlfa        = "";
     aAlfa1       = "";
     aMas         = "";
     aFicheroSql  = "";
     aCadena      = "";
     aBase        = "";
     aOrigen      = "";
     aDestino     = "";
     aFichOri     = "";
     aFichDes     = "";

     {-1}aVent      = "";
         aVent1     = 0;
         aVent2     = 0;
         aVent3     = 0;
         aVent4     = 0;
         aVent5     = "";

     &enDocumento = 0;
     &eaFichero   = "";
     &enError     = 0;
     &enNumCampos = 0;
     &enLABORAL   = 0;
|FIN;

|PROCESO CambiaBarra;
     aAuxBarra = "";
     |QBF aBarra;
     aLong = aBarra % A0;
     nLong = aLong;
     |PARA nVoy = 1; |SI nVoy [ nLong; |HACIENDO nVoy = nVoy + 1;
          nSaca = (nVoy * 100) + 1;
          aLetra = aBarra % nSaca;
          |SI aLetra = "/";
               aLetra = "\";
          |FINSI;
          aAuxBarra = aAuxBarra + aLetra;
     |SIGUE;
     aBarra = aAuxBarra;
|FPRC;

|PROCESO ControlAcentos;
     aCadena = "";
     aLong = aTextoBusqueda % A0;
     nLong = aLong;

     |PARA nVoy = 1; |SI nVoy [ nLong; |HACIENDO nVoy = nVoy + 1;
          nSaca = (100 * nVoy) + 1;
          aSaca = nSaca;
          aLetra = aTextoBusqueda % aSaca;
          nAscii = &aLetra;
          |SI nAscii = 160; |O nAscii = 130; |O nAscii = 161; |O nAscii = 162; |O nAscii = 163;
               aCadena = aCadena + "*.";
          |SINO;
               aCadena = aCadena + aLetra;
          |FINSI;
     |SIGUE;
     aTextoBusqueda = aCadena;
|FPRC;

|PROCESO Tipo12Fz002;  |TIPO 12;
     |SI FSalida = 7;  enError = 1;   |ACABA;   |FINSI;

     |VENTANA 0501, 2699, -1, 16, "Buscando registros ...";
     nVentz2  = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentz2;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |CONTROL_SIMPLE 0, "LABEL,{{5}}Buscando registros ...", 1315, 1680, NULL;
     nBotonLabel = FSalida;

     |PTEC 802;   |PAUSA;
     |PULSATECLA;

     |SI enLABORAL = 1;
          aFichOri = "tm" + Usuario;
     |SINO;
          aFichOri = "dsarm005";
     |FINSI;

     aFichDes = eaFichero;
     |NOME_DAT #905 eaFichero;

     |ABRE #905;
     |LEE 000#905p;
     |SI FSalida = 0;
               || RPRIEGO - Tiquet: 002805-00265
               || NOME_DAT con el fichero abierto !!!
               ||/pongo que se cierre
         |CIERRA #905;

         aFichOri = eaFichero;
         |NOME_DAT #5 aFichOri;

         aFichDes = ("00" + Usuario + "zzzzzzzz") % 108;
         |NOME_DAT #905 aFichDes;
     |SINO;
          |SI enSwModoAsociar = 1;
              |CIERRA #905;

              aFichOri = eaFicAso;
              |NOME_DAT #5 aFichOri;

              aFichDes = ("00" + Usuario + "zzzzzzzz") % 108;
              |NOME_DAT #905 aFichDes;
          |FINSI;
     |FINSI;
     |CIERRA #905;

     |SI #dsarz002#11 = "S";
          |DBASE aBase;   |QBF aBase;

          aDirBaseServidor = aBase + "ocr";  |MKDIR aDirBaseServidor;
          aDirBaseServidor = aBase + "ocr/";

          |QUE_SISTEMA aSistema;
          |SI aSistema = "ESDOS";
               aSistema = "ESDOS";
          |SINO;
               aSistema = "ESUNIX";
          |FINSI;

          aTextoBusqueda = #dsarz002#0;
          |QBF aTextoBusqueda;
          |SI aTextoBusqueda = "";
               aMensaje = "0000No ha introducido la Cadena a buscar,";
               |MENAV aMensaje;

               enError = 1;
               aVent1 = -1;
               aVent2 = -1;
               aVent3 = 0;
               aVent4 = nVentz2;
               aVent5 = "MODELESS";
               |ESPECIFICA "ESTADO_VENTANA", aVent;

               |FINVENTANA nVentz2;

               |ACABA;
          |FINSI;
          |HAZ ControlAcentos;

          aUsuario = Usuario % 810;
          |QBF aUsuario;
          aFicRespuesta = aDirBaseServidor + "grep." + aUsuario + ".res";
          |FBORRA aFicRespuesta;
          aFicResError = aDirBaseServidor + "grep." + aUsuario + ".err";
          |FBORRA aFicResError;
          |SI aSistema = "ESUNIX";
               aEjecuta = "grep " + &34 + #dsarz002#0;
               |QBF aEjecuta;
               aEjecuta = aEjecuta + &34 + " " + aDirBaseServidor + "*.txt";
               aEjecuta = aEjecuta + " > " + aFicRespuesta + " 2> " + aFicResError;
               |SYSTEM aEjecuta;
          |SINO;
               aPathDlls = aBase + "dlls/";
               aEjecuta = aPathDlls + "grep.exe -o " + &34 + #dsarz002#0;
               |QBF aEjecuta;
               aEjecuta = aEjecuta + &34 + " " + aDirBaseServidor + "*.txt";
               aEjecuta = aEjecuta + " > " + aFicRespuesta + " 2> " + aFicResError;

               aBarra = aEjecuta;
               |HAZ CambiaBarra;
               aEjecuta = aBarra;

               aAbre = aDirBaseServidor + Usuario;
               |QBF aAbre;
               aAbre = aAbre + ".bat";
               |XABRE "W", aAbre, nHandle;
               |SI FSalida ] 0;
                    aLinea = aEjecuta;
                    |XGRABA nHandle, aLinea;
               |FINSI;
               |XCIERRA nHandle;

               ||aEjecuta = "cmd " + &34 + "/c start " + aAbre + &34;
               aEjecuta = aAbre;
               |SYSTEM aEjecuta;

               ||TODO CCC
          |FINSI;

          |ABRE #905;
          |ABRE #5;

          |XABRE "E", aFicRespuesta, 0;
          |SI FSalida ! 0;
               enError = 1;
          |SINO;
               |XABRE "U", aFicRespuesta, nHandle;
               |SI FSalida < 0;
                    enError = 1;
               |SINO;
                    |PARA; |SI; |HACIENDO;
                         |XLEE nHandle, 250, aLinea;
                         |SI FSalida < 0; |SAL; |FINSI;
                         |SI aSistema = "ESUNIX";
                              aLinea = aLinea - aDirBaseServidor;
                         |SINO;
                              aBarra = aDirBaseServidor;
                              |HAZ CambiaBarra;
                              aLinea = aLinea - aBarra;
                         |FINSI;
                         aNumero = aLinea % 109;
                         nNumero = aNumero;
                         |SI nNumero ! 0;
                              #905#0 = nNumero;
                              |LEE 000#905.=;
                              |SI FSalida = 0;
                                   ||OKIE
                              |SINO;
                                   #5#0 = nNumero;
                                   |LEE 000#5.=;
                                   |SI FSalida = 0;
                                        |SI #5#1 ] #dsarz002#2; |Y #5#1 [ #dsarz002#3;
                                         |Y #5#2 ] #dsarz002#4; |Y #5#2 [ #dsarz002#5;
                                         |Y #5#3 ] #dsarz002#6; |Y #5#3 [ #dsarz002#7;
                                             |SI #dsarz002#1 = "T";
                                                  |HAZ Mete905;
                                             |SINO;
                                                  aSitu = #5#48;
                                                  |QBF aSitu;
                                                  |SI #dsarz002#1 = "E"; |Y aSitu = "ENTRADA";
                                                       |HAZ Mete905;
                                                  |FINSI;
                                                  |SI #dsarz002#1 = "S"; |Y aSitu = "SALIDA";
                                                       |HAZ Mete905;
                                                  |FINSI;
                                             |FINSI;
                                        |FINSI;
                                   |FINSI;
                              |FINSI;
                         |FINSI;
                    |SIGUE;
               |FINSI;
               |XCIERRA nHandle;
          |FINSI;
          |CIERRA #5;
          |CIERRA #905;

          |SI aSistema = "ESDOS";
               aAbre = aDirBaseServidor + Usuario;
               |QBF aAbre;
               aAbre = aAbre + ".bat";
               |FBORRA aAbre;
          |FINSI;

          |SI aFichDes ! eaFichero;
              |ABRE #5; |CIERRA #5;  |DELINDEX #5;

              aOrigen  = aBase + "fich/" + aFichDes  + ".dat";
              aDestino = aBase + "fich/" + eaFichero + ".dat";
              |RENOMBRA_FICHERO aOrigen, aDestino;

              aOrigen  = aBase + "fich/" + aFichDes  + ".ddx";
              aDestino = aBase + "fich/" + eaFichero + ".ddx";
              |RENOMBRA_FICHERO aOrigen, aDestino;

              aOrigen  = aBase + "fich/" + aFichDes  + ".ixx";
              aDestino = aBase + "fich/" + eaFichero + ".ixx";
              |RENOMBRA_FICHERO aOrigen, aDestino;

              |NOME_DAT #905 eaFichero;
          |FINSI;

          aFicRespuesta = aDirBaseServidor + "grep." + aUsuario + ".res";
          |FBORRA aFicRespuesta;
          aFicResError = aDirBaseServidor + "grep." + aUsuario + ".err";
          |FBORRA aFicResError;

          aVent1 = -1;
          aVent2 = -1;
          aVent3 = 0;
          aVent4 = nVentz2;
          aVent5 = "MODELESS";
          |ESPECIFICA "ESTADO_VENTANA", aVent;

          |FINVENTANA nVentz2;

          |ACABA;
     |FINSI;

     |DBASE aBase;
     aAlfa = aBase + "tmp";  |MKDIR aAlfa;
     aFicheroSql = aAlfa  + "/sql" + Usuario + ".txt";
     |XABRE "WB", aFicheroSql, nHandle;

     aCadena = #902#0;  |QBF aCadena;
     aCadena = "'" + aCadena + "'";  |QBF aCadena;

     aAlfa = "* FROM " + aFichOri + " INTO " + aFichDes + " WHERE ";
     |XGRABA nHandle, aAlfa;

     |SI PARAMETRO ! "";
         |SI #902#1 = "E";
             aAlfa =  " 48 LIKE " + "'ENTRADA' AND ";
         |FINSI;

         |SI #902#1 = "S";
             aAlfa =  " 48 LIKE " + "'SALIDA' AND ";
         |FINSI;

         |SI #902#1 = "T";
             aAlfa =  " ";
         |FINSI;

         aAlfa1 = PARAMETRO % 0;
         |SI aAlfa1 = "5";
             nNume = PARAMETRO;
             aAlfa = aAlfa + "8 LIKE '" + nNume + "' AND (";
         |SINO;
             aAlfa = aAlfa + "10 LIKE '" + PARAMETRO + "' AND (";
         |FINSI;

         |XGRABA nHandle, aAlfa;
     |SINO;
         |SI #902#2 = 1; |Y #902#3 = 999; |Y #902#4 = 1; |Y #902#5 = 999;
          |Y #902#6 = 1; |Y #902#7 = 999;
              |SI #902#1 = "E";
                  aAlfa = " 48 LIKE " + "'ENTRADA' AND (";
                  |XGRABA nHandle, aAlfa;
              |FINSI;

              |SI #902#1 = "S";
                  aAlfa = " 48 LIKE " + "'SALIDA' AND (";
                  |XGRABA nHandle, aAlfa;
              |FINSI;
              |SI #902#1 = "T";
                  aAlfa = " (";
                  |XGRABA nHandle, aAlfa;
              |FINSI;
         |SINO;

              aAlfa = " 1 BETWEEN " + #902#2 + ", " + #902#3 + " AND ";
              |XGRABA nHandle, aAlfa;
              aAlfa = " 2 BETWEEN " + #902#4 + ", " + #902#5 + " AND ";
              |XGRABA nHandle, aAlfa;
              aAlfa = " 3 BETWEEN " + #902#6 + ", " + #902#7;
              |XGRABA nHandle, aAlfa;

              |SI #902#1 = "E";
                  aAlfa = " AND 48 LIKE " + "'ENTRADA' AND (";
                  |XGRABA nHandle, aAlfa;
              |FINSI;

              |SI #902#1 = "S";
                  aAlfa = " AND 48 LIKE " + "'SALIDA' AND (";
                  |XGRABA nHandle, aAlfa;
              |FINSI;
               ||TODO CCC
              |SI #902#1 = "T";
                  aAlfa =  " AND (";
                  |XGRABA nHandle, aAlfa;
              |FINSI;
         |FINSI;
     |FINSI;

     ||TODO CCC
     |PARA nCampo = 0;  |SI nCampo [ enNumCampos;  |HACIENDO nCampo = nCampo + 1;
           |SI nCampo = 0;
               aAlfa = nCampo + " LIKE " + aCadena;
           |SINO;
               aAlfa = " OR " + nCampo + " LIKE " + aCadena;
           |FINSI;
           |XGRABA nHandle, aAlfa;
     |SIGUE;

     aAlfa = ")";
     |XGRABA nHandle, aAlfa;

     |XCIERRA nHandle;

     |ABRE #5;

     aAlfa = "SELECT @" + aFicheroSql;
     |SQL aAlfa;
     |CIERRA #5;

     ||TODO CCC
     |||FBORRA aFicheroSql;
     |CIERRA #905;

     |SI aFichDes ! eaFichero;
         |ABRE #5; |CIERRA #5;  |DELINDEX #5;

         aOrigen  = aBase + "fich/" + aFichDes  + ".dat";
         aDestino = aBase + "fich/" + eaFichero + ".dat";
         |RENOMBRA_FICHERO aOrigen, aDestino;

         aOrigen  = aBase + "fich/" + aFichDes  + ".ddx";
         aDestino = aBase + "fich/" + eaFichero + ".ddx";
         |RENOMBRA_FICHERO aOrigen, aDestino;

         aOrigen  = aBase + "fich/" + aFichDes  + ".ixx";
         aDestino = aBase + "fich/" + eaFichero + ".ixx";
         |RENOMBRA_FICHERO aOrigen, aDestino;

         |NOME_DAT #905 eaFichero;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentz2;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVentz2;
|FPRC;


|PROCESO Mete905;
     ||No me fio del "|NC";

     |DDEFECTO #905;
     #905#0 = #5#0;
     |LEE 101#905=;
     |SI FSalida ! 0; |GRABA 020#905.b; |FINSI;
     |PARA nVoy = 1; |SI nVoy [ enNumCampos; |HACIENDO nVoy = nVoy + 1;
          #905#nVoy# = #5#nVoy#;
     |SIGUE;
     |GRABA 020#905.a;
     |LIBERA #905;
|FPRC;


|PROCESO Tipo80;  |TIPO 80;
               || RPRIEGO - Tiquet: 002805-00265
               ||/no se ejecutaba este proceso desde el def !!!
               ||/he quitado el "&" de la declaracion de la referencia
               ||/y ahora se carga y se descarga el def aqui y en origen

     |DBASE aMas;  |QBF aMas;

     |SI enLABORAL = 1;
         aMas = aMas + "def/tm" + Usuario + ".mas";
     |SINO;
         aMas = aMas + "def/dsarm005.mas";
     |FINSI;
     |CARGA_DEF aMas, dsarm005@;

     |SI FSalida ! 0;
          |MENAV "0000[dsarm005.mas] inaccesible. Consulte con su distribuidor.";
          |ACABA;
     |FINSI;

     eaFichero = ("01" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #905 eaFichero;

     FSalida = 2699;
|FPRC;

|PROCESO Tipo90;  |TIPO 90;
     |DESCARGA_DEF #dsarm005@;
|FPRC;

|PROCESO Tipo0C3_z002; |TIPO 0;
     |SI #dsarz002#2 = 1; |Y #dsarz002#3 = 999;
          #dsarz002#8 = "-TODOS-";
     |SINO;
          |ABRE #dsarm002;
          |DDEFECTO #dsarm002;
          #dsarm002#0 = #dsarz002#2;
          |LEE 000#dsarm002.=;
          aTexto = #dsarm002#1;
          |QBF aTexto;
          aTexto = aTexto + " - ";

          |DDEFECTO #dsarm002;
          #dsarm002#0 = #dsarz002#3;
          |LEE 000#dsarm002.=;
          aTexto = aTexto + #dsarm002#1;
          |QBF aTexto;

          #dsarz002#8 = aTexto;
     |FINSI;
     |PINTA #dsarz002#8;
|FPRC;

|PROCESO Tipo0C5_z002; |TIPO 0;
     |SI #dsarz002#4 = 1; |Y #dsarz002#5 = 999;
          #dsarz002#9 = "-TODOS-";
     |SINO;
          |SI #dsarz002#2 = #dsarz002#3;
               |ABRE #dsarm003;
               |DDEFECTO #dsarm003;
               #dsarm003#0 = #dsarz002#2;
               #dsarm003#1 = #dsarz002#4;
               |LEE 000#dsarm003.=;
               aTexto = #dsarm003#2;
               |QBF aTexto;
               aTexto = aTexto + " - ";

               |DDEFECTO #dsarm003;
               #dsarm003#0 = #dsarz002#2;
               #dsarm003#1 = #dsarz002#5;
               |LEE 000#dsarm003.=;
               aTexto = aTexto + #dsarm003#2;
               |QBF aTexto;

               #dsarz002#9 = aTexto;
          |SINO;
               #dsarz002#9 = "-MULTIPLE SELECCION-";
          |FINSI;
     |FINSI;
     |PINTA #dsarz002#9;
|FPRC;

|PROCESO Tipo0C7_z002; |TIPO 0;
     |SI #dsarz002#6 = 1; |Y #dsarz002#7 = 999;
          #dsarz002#10 = "-TODOS-";
     |SINO;
          |SI #dsarz002#2 = #dsarz002#3; |Y  #dsarz002#4 = #dsarz002#5;

               |ABRE #dsarm004;
               |DDEFECTO #dsarm004;
               #dsarm004#0 = #dsarz002#2;
               #dsarm004#1 = #dsarz002#4;
               #dsarm004#2 = #dsarz002#6;
               |LEE 000#dsarm004.=;
               aTexto = #dsarm004#3;
               |QBF aTexto;
               aTexto = aTexto + " - ";

               |DDEFECTO #dsarm004;
               #dsarm004#0 = #dsarz002#2;
               #dsarm004#1 = #dsarz002#4;
               #dsarm004#2 = #dsarz002#7;
               |LEE 000#dsarm004.=;
               aTexto = aTexto + #dsarm004#3;
               |QBF aTexto;

               #dsarz002#10 = aTexto;
          |SINO;
               #dsarz002#10 = "-MULTIPLE SELECCION-";
          |FINSI;
     |FINSI;
     |PINTA #dsarz002#10;
|FPRC;

|PROCESO Baja3;
     #dsarm003#0 = #dsarz002#2;
     #dsarm003#1 = 1;
|FPRC;

|PROCESO Alta3;
     #dsarm003#0 = #dsarz002#2;
     #dsarm003#1 = 999;
|FPRC;

|PROCESO Tipo66C4_z002; |TIPO 66;
     |SI #dsarz002#2 = #dsarz002#3;
          |ABRE #dsarm003;
          |CONSULTA_F_CLAVE #1#dsarm003, Baja3, Alta3;
          |SI FSalida = 0;
               #dsarz002#4 = #dsarm003#1;
               |PINTA #dsarz002#4;
          |FINSI;
          |CIERRA #dsarm003;
     |SINO;
          aMensaje = "0000Seleccion con F11 solo posible si Desde = Hasta Grupo";
          |MENAV aMensaje;
     |FINSI;
|FPRC;

|PROCESO Tipo66C5_z002; |TIPO 66;
     |SI #dsarz002#2 = #dsarz002#3;
          |ABRE #dsarm003;
          |CONSULTA_F_CLAVE #1#dsarm003, Baja3, Alta3;
          |SI FSalida = 0;
               #dsarz002#5 = #dsarm003#1;
               |PINTA #dsarz002#5;
          |FINSI;
          |CIERRA #dsarm003;
     |SINO;
          aMensaje = "0000Seleccion con F11 solo posible si Desde = Hasta Grupo";
          |MENAV aMensaje;
     |FINSI;
|FPRC;

|PROCESO Baja4;
     #dsarm004#0 = #dsarz002#2;
     #dsarm004#1 = #dsarz002#4;
     #dsarm004#2 = 1;
|FPRC;

|PROCESO Alta4;
     #dsarm004#0 = #dsarz002#2;
     #dsarm004#1 = #dsarz002#4;
     #dsarm004#2 = 999;
|FPRC;

|PROCESO Tipo66C6_z002; |TIPO 66;
     |SI #dsarz002#2 = #dsarz002#3; |Y  #dsarz002#4 = #dsarz002#5;
          |ABRE #dsarm004;
          |CONSULTA_F_CLAVE #1#dsarm004, Baja4, Alta4;
          |SI FSalida = 0;
               #dsarz002#6 = #dsarm004#2;
               |PINTA #dsarz002#6;
          |FINSI;
          |CIERRA #dsarm004;
     |SINO;
          aMensaje = "0000Seleccion con F11 solo posible si Desde = Hasta Grupo/SubGrupo";
          |MENAV aMensaje;
     |FINSI;
|FPRC;

|PROCESO Tipo66C7_z002; |TIPO 66;
     |SI #dsarz002#2 = #dsarz002#3; |Y  #dsarz002#4 = #dsarz002#5;
          |ABRE #dsarm004;
          |CONSULTA_F_CLAVE #1#dsarm004, Baja4, Alta4;
          |SI FSalida = 0;
               #dsarz002#7 = #dsarm004#2;
               |PINTA #dsarz002#7;
          |FINSI;
          |CIERRA #dsarm004;
     |SINO;
          aMensaje = "0000Seleccion con F11 solo posible si Desde = Hasta Grupo/SubGrupo";
          |MENAV aMensaje;
     |FINSI;
|FPRC;

|PROGRAMA;
     |ABRE #dsarm001;
     |LEE 000#dsarm001.p;
     |SI FSalida ! 0; |DDEFECTO #dsarm001; |FINSI;
     |CIERRA #dsarm001;

     |DDEFECTO #902;
     |PINPA #0#902;  nPanta0 = FSalida;

     |SI #dsarm001#45 = "S";
          |C_M #dsarz002#11, 1, "S";
          |C_V #dsarz002#11, 1, "S";
          aLabel = "LABEL,{{0}} B£squeda usando OCR";
          |CONTROL_SIMPLE nPanta0, aLabel, 2045, 2075,NULL;
          aLabel = "LABEL,{{0}} Formatos soportados.";
          |CONTROL_SIMPLE nPanta0, aLabel, 2145, 2195,NULL;
          aLabel = "LABEL,{{0}} TIF, BMP, JPG, PDF, DOC, XLS, XML, HTML, TXT.";
          |CONTROL_SIMPLE nPanta0, aLabel, 2245, 2295,NULL;
     |SINO;
          |C_M #dsarz002#11, 1, "N";
          |C_V #dsarz002#11, 1, "N";
     |FINSI;

     |PINDA #0#902;

     |ENDATOS #1#902;

     |PULSATECLA;
|FPRO;
