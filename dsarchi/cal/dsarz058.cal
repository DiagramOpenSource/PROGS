|FICHEROS;
     dsarz058 #1;

     dsarm065 #65;       ||Plantillas Auto-relleno
     dsarw094 #94,MANTE; ||Aux.Plan Auto-Relleno

     dsarw057 #57,MANTE; ||Aux. Copiar Plan.

     dsarw016 #916;
     dsarw095 #995;
     referen@ #999;
     dsarm004 #4;        ||Tipos Documentos
|FIN;

|VARIABLES;
     &enEstaComersan = 0;
     {1}aContiene     = "";
     nError = 0;
     aOrigen = "";
     nHandle = 0;
     aDestino = "";
     aLinea = "";

     &eaUnidad = "";
     nPlantilla = 0;
     nLabel = 0;

     {-1}aVent      = "";
         aVent1     = 0;
         aVent2     = 0;
         aVent3     = 0;
         aVent4     = 0;
         aVent5     = "";

     nSubVent = 0;
     &enNumRegla = 0;
     aEjecuta = "";
     aQueBoton     = "";
     aID           = "";
     nBotonAceptar = 0;
     nBotonCancelar = 0;

     aAux = "";
     nTCampo = 0;
     aQueQuiero = "";
     aFichero = "";
     aBase = "";
     aCarga = "";
     nCampo = 0;
     aImpre = "";

     nUltimo = 0;
     nIdSalir = 0;
     {2}nNivel = 0;
     nVentana = 0;
     nPantaDos = 0;
     aCadena = "";
     aAlfa = "";
     nSw = 0;
     nCampoHasta = 0;
     nCampoD = 0;
     nCampoH = 0;
     aMensaje = "";
     nBoton1 = 0;
     nBoton2 = 0;
     nBoton3 = 0;
     nBoton4 = 0;
     nPanta = 0;
     nRango = 0;
     nPos1  = 0;
     nPos2  = 0;
     nGrid = 0;
     aEsc1  = "";
     aEsc2  = "";
     aRetu  = "";
     aTecla = "";
     nModo = 0;
|FIN;

|PROCESO Tipo12w094; |TIPO 12;

|FPRC;

|PROCESO Tipo0C0w094; |TIPO 0;
     |SI FSalida ! 7;
          |SI nModo = 1;
               #dsarm065#0 = #dsarw094#0;
               |LEE 000#dsarm065.=;
               |SI FSalida = 0;
                    aMensaje = "0000Error: Plantilla YA definida previamente";
                    |MENAV aMensaje;
                    |ERROR;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo0C0w057; |TIPO 0;
     |SI FSalida ! 7;
          |SI #dsarw057#0 = #dsarw057#2;
               aMensaje = "0000Error: Plantilla Origen = Plantilla Destino";
               |MENAV aMensaje;
               |ERROR;
          |SINO;
               #dsarm065#0 = #dsarw057#2;
               |LEE 000#dsarm065.=;
               |SI FSalida = 0;
                    aMensaje = "0000Error: Plantilla YA definida previamente";
                    |MENAV aMensaje;
                    |ERROR;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo12w057; |TIPO 12;
|FPRC;

|PROCESO Tipo0Campos;  |TIPO 0;
     |SI Campo = 4; |O Campo = 9;
      |O Campo = 14; |O Campo = 19;
      |O Campo = 24; |O Campo = 29;
      |O Campo = 34; |O Campo = 39;
      |O Campo = 44; |O Campo = 49;

          |SI #94Campo ! -1;
              |ABRE #916;
              #916#0 = #94Campo;
              |LEE 000#916=;
              |SI FSalida ! 0;
                  |MENAV "0000No existe el campo";
                  |ERROR;
                  |CIERRA #916;
                  |ACABA;
              |FINSI;
              |CIERRA #916;

               nCampo = Campo + 1;
               #94Campo = #916#0;    |PINTA #94Campo;
               #94nCampo = #916#1;    |PINTA #94nCampo;
          |SINO;
               nCampo = Campo + 1;
               #94nCampo = "";    |PINTA #94nCampo;
          |FINSI;
     |SINO;
          |SI #94Campo ! -1;
              |ABRE #995;
              #995#0 = #94Campo;
              |LEE 000#995=;
              |SI FSalida ! 0;
                  |MENAV "0000No existe el campo";
                  |ERROR;
                  |CIERRA #995;
                  |ACABA;
              |FINSI;
              |CIERRA #995;

              nCampo = Campo + 1;
              #94Campo = #995#0;    |PINTA #94Campo;
              #94nCampo = #995#1;    |PINTA #94nCampo;
          |SINO;
               nCampo = Campo + 1;
               #94nCampo = "";    |PINTA #94nCampo;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO ObtenIdSalir;
     InNivel = nNivel1<-;
     nNivel1 = 0;
     |ESPECIFICA "IDBOTONTECLA", nNivel;
     nIdSalir = nNivel2;
|FPRC;

|PROCESO Tipo80z058; |TIPO 80;
     FSalida = 2999;
|FPRC;

|PROCESO Tipo80w094; |TIPO 80;
     FSalida = 3199;
|FPRC;

|PROCESO BajaPanta;
     #dsarm065#0 = 01;
|FPRC;

|PROCESO AltaPanta;
     #dsarm065#0 = 99;
|FPRC;

|PROCESO EventoPanta;
     |SI FSalida = 1;  |O FSalida = 2;
          |LEE 000#dsarm065.=;
          |SI FSalida = 0;
               |ESTADO_CONTROL nBoton2, "ENABLE";
               |ESTADO_CONTROL nBoton3, "ENABLE";
               |ESTADO_CONTROL nBoton4, "ENABLE";
          |SINO;
               |ESTADO_CONTROL nBoton2, "DISABLE";
               |ESTADO_CONTROL nBoton3, "DISABLE";
               |ESTADO_CONTROL nBoton4, "DISABLE";
          |FINSI;
     |FINSI;

     |SI FSalida = 4;
          |HAZ Boton2;
     |FINSI;
|FPRC;

|PROCESO Boton1;
     nModo = 1;
     |HAZ ModiAlta;
     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO Boton2;
     |LEE 000#dsarm065.=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     nModo = 2;
     |HAZ ModiAlta;
     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO Boton3;
     |LEE 000#dsarm065.=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |LEE 101#dsarm065.=;
     |SI FSalida = 0;
          aMensaje = "0000NDesea Borrar " + #65#1;
          |QBF aMensaje;
          aMensaje = aMensaje + "?";
          |CONFI aMensaje;
          |SI FSalida = 0;
               |BORRA 020#dsarm065.a;
          |FINSI;
          |LIBERA #dsarm065;
     |FINSI;

     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO Boton4;
     |LEE 000#dsarm065.=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     nPlantilla = #dsarm065#0;

     |VENTANA 1010, 1590, nPanta, 17, "Copiar Plantilla";
     nSubVent = FSalida;

     nLabel = -1;
     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |DDEFECTO #dsarw057;
     #dsarw057#0 = #dsarm065#0;
     #dsarw057#1 = #dsarm065#1;
     nUltimo = 0;
     |HAZ DameUltimo;
     nUltimo = nUltimo + 1;
     #dsarw057#2 = nUltimo;

     |PINPA #0#dsarw057;
     |PINDA #0#dsarw057;

     |ENDATOS #1#dsarw057;
     |SI FSalida = 0;
          #dsarm065#0 = nPlantilla;
          |LEE 000#dsarm065.=;
          |SI FSalida = 0;
               #dsarm065#0 = #dsarw057#2;
               #dsarm065#1 = #dsarw057#3;
               |GRABA 020#dsarm065.n;
               |LIBERA #dsarm065;
          |FINSI;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nSubVent;

     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO CargaTemporal;
     aFichero = ("18" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #916 aFichero;

     |ABRE #916;  |CIERRA #916;  |DELINDEX #916;

     |ABRE #916;

     |DBASE aBase;  |QBF aBase;
     aCarga    = aBase + "def/dsarm005.mas";  |QBF aCarga;
     |CARGA_DEF aCarga, referen@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aQueQuiero = "|NC";
     aAlfa      = #999#aQueQuiero#;
     nTCampo    = aAlfa;
     nTCampo    = nTCampo - 1;

     |PARA nCampo = 0;  |SI nCampo [ nTCampo;  |HACIENDO nCampo = nCampo + 1;
           aQueQuiero = "|C" + (("000" + nCampo) % -103) + "NOMBRE";
           aFichero   = (#999#aQueQuiero# + (" " * 20)) % 120;
           #916#0 = nCampo;
           #916#1 = aFichero;

           |GRABA 020#916n;
           |LIBERA #916;
     |SIGUE;
     |CIERRA #916;

     |DESCARGA_DEF #referen@;

     ||Campos Camaniva

     aFichero = ("19" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #995 aFichero;

     |ABRE #995;  |CIERRA #995;  |DELINDEX #995;

     |ABRE #995;

     |DBASS aBase;  |QBF aBase;
     aCarga = aBase + "contagen/def/camaniva.mas"; |QBF aCarga;
     |CARGA_DEF aCarga, referen@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aQueQuiero = "|NC";
     aAlfa      = #999#aQueQuiero#;
     nTCampo    = aAlfa;
     nTCampo    = nTCampo - 1;

     |PARA nCampo = 0;  |SI nCampo [ nTCampo;  |HACIENDO nCampo = nCampo + 1;
           aQueQuiero = "|C" + (("000" + nCampo) % -103) + "NOMBRE";
           aFichero   = (#999#aQueQuiero# + (" " * 20)) % 120;
           #995#0 = nCampo;
           #995#1 = aFichero;

           |GRABA 020#995n;
           |LIBERA #995;
     |SIGUE;

     enEstaComersan = 0;
     |HAZ EstaComersan;

     |SI enEstaComersan = 1;
          #995#0 = 99;
          #995#1 = "Num. Fra. Proveedor";
          |GRABA 020#995n;
          |LIBERA #995;
     |FINSI;

     |CIERRA #995;

     |DESCARGA_DEF #referen@;
|FPRC;


|PROCESO ModiAlta;
     nIdSalir = 0;
     |HAZ ObtenIdSalir;
     |ESTADO_CONTROL nIdSalir, "HIDE";

     |VENTANA 0501, 3199, -1, -1, "";
     nVentana = FSalida;

     aCadena ="|NC";
     aAlfa = #65#aCadena#;
     nCampoHasta = aAlfa;
     nCampoHasta = nCampoHasta - 1;

     |SI nModo ! 1;
          |PARA nCampoD = 0;  |SI nCampoD [ nCampoHasta; |HACIENDO nCampoD = nCampoD + 1;
                nCampoH = nCampoD;
                #94nCampoH = #65nCampoD;
          |SIGUE;

          |C_M #94#0, 1, "N";
     |SINO;
          |C_M #94#0, 1, "S";
          nUltimo = 0;
          |HAZ DameUltimo;
          |DDEFECTO #94;
          nUltimo = nUltimo + 1;
          #94#0 = nUltimo;
     |FINSI;

     |HAZ CargaTemporal;

     |PINPA #0#94; nPantaDos = FSalida;
     |PINDA #0#94;

     |SI nModo = 1;  |ENDATOS #1#94;  |FINSI;
     |SI nModo = 2;  |ENDATOS #2#94;  |FINSI;

     |SI FSalida = 0;
          |HAZ GrabaDatos;
     |FINSI;

     |ABRE #916;  |CIERRA #916;  |DELINDEX #916;

     |FINVENTANA nVentana;

     |ESTADO_CONTROL nIdSalir, "SHOW";
|FPRC;

|PROCESO DameUltimo;
     |LEE 000#dsarm065.u;
     |SI FSalida = 0;
          nUltimo = #dsarm065#0;
     |SINO;
          nUltimo = 0;
     |FINSI;
|FPRC;

|PROCESO GrabaDatos;
     #65#0 = #94#0;
     |LEE 101#65=;
     |SI FSalida ! 0;
         |DDEFECTO #65;
         #65#0 = #94#0;
         |GRABA 020#65b;
     |FINSI;

     |PARA nCampoD = 1;  |SI nCampoD [ nCampoHasta;  |HACIENDO nCampoD = nCampoD + 1;
           nCampoH   = nCampoD;
           #65nCampoD = #94nCampoH;
     |SIGUE;

     |GRABA 020#65a;
     |LIBERA #65;
|FPRC;

|PROCESO Tipo0C2w094; |TIPO 0;
     |SI #dsarw094#2 = "S";
          |C_M #dsarw094#3, 1, "S";
     |SINO;
          |C_M #dsarw094#3, 1, "N";
     |FINSI;
     |PINTA #dsarw094#3;
|FPRC;

|PROGRAMA;
     |DBASS aBase;  |QBF aBase;
     aCarga = aBase + "contagen/def/camaniva.mas";
     |QBF aCarga;

     |XABRE "E", aCarga, nHandle;
     |SI FSalida < 0;
          aMensaje = "0000Aplicacion CONTAGEN no instalada en el sistema";
          |MENAV aMensaje;
          |ACABA;
     |FINSI;

     |HAZ eCambioDeCero;

     |CLS;
     |CABEZA "Definici¢n Plantillas Auto-relleno Contagen";

     |ABRE #dsarm065;

     #dsarm065#0 = 0;
     |LEE 000#dsarm065.=;
     |SI FSalida ! 0;
          |DDEFECTO #dsarm065;
          #dsarm065#0 = 0;
          |LEE 101#dsarm065.=;
          |SI FSalida ! 0;
               #dsarm065#1 = "<SIN PLANTILLA>";
               |GRABA 020#dsarm065.n;
          |FINSI;
          |LIBERA #dsarm065;
     |FINSI;

     |PINPA #0#dsarz058;
     nPanta = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}Alta ", 2753, 2763, Boton1;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Modificar ", 2765, 2775, Boton2;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{199}}Baja ", 2777, 2787, Boton3;
     nBoton3 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,[+]   Copiar ", 2703, 2713, Boton4;
     nBoton4 = FSalida;

     ||Poner los 4 botones

     nRango = 4 + 8 + 16 + 32;
     nPos1  = 0702;
     nPos2  = 2698;

     |LINEAL_SIMPLE #1#dsarm065, nRango, nPos1, nPos2, BajaPanta, AltaPanta, EventoPanta;
     nGrid = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}Salir," + nGrid;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nGrid;

     |DESTRUYECONTROL nBoton1;
     |DESTRUYECONTROL nBoton2;
     |DESTRUYECONTROL nBoton3;
     |DESTRUYECONTROL nBoton4;

     |CIERRA #dsarm065;
|FPRO;
