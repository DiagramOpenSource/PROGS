|FICHEROS;
     dsarz040 #0;
     dsarm001 #1;
     dsarm040 #40;

     dsarw071 #971;
     dsarm053 #53;
|FIN;

|VARIABLES;
     &eaImpoAutoADConta = "";
     &enImpoEmpContagen = 0;
     &enImpoPercontagen = 0;
     &enNoMoverImpoADConta = 0;
     &enImpoNumDoc = 0;

     aExtension = "";
     aAuxNom = "";

     nHandle             = 0;
     nSubVent            = 0;
     nPanta              = 0;
     nSwPie              = 0;
     nPanta0             = 0;
     nPanta971           = 0;
     nBtnIniciar         = -1;
     nBtnSalir           = -1;
     nBtnAceptar         = -1;
     nBtnCancelar        = -1;
     nBtnAlta            = -1;
     nBtnBaja            = -1;
     nBtnModificar       = -1;
     nBtnCopiar          = -1;
     nBtnCarpeta         = -1;
     nBtn                = -1;
     nBtnConfig          = -1;
     nBtnCaptur          = -1;
     &enBtnTipo          = -1;
     nBtnImp             = 0;
     nBtnTiposAuto       = 0;
     nBtnFoto            = 0;
     nVentana            = 0;
     nVentConf           = 0;
     nSegundos           = 0;
     nSalir              = 0;
     nConta              = 0;
     nRango              = 0;
     nGrid40             = 0;
     nRedon              = 0;
     nFalta              = 0;
     nLabelTiempo        = -1;
     nLabelBarra         = -1;
     nVentTrab           = 0;
     nVent971            = 0;
     nModo               = 0;
     nTiempo             = 0;
     nHay                = 0;
     nLong               = 0;
     nCarac              = 0;
     nPos                = 0;
     nCopia              = 0;
     nCodBarra           = 0;
     nSeg                = 0;

     aHora               = "";
     aBase               = "";
     aInforme            = "";
     aMensaje            = "";
     aSilencio           = "";
     aQueBoton           = "";
     aID                 = "";
     aAlfa               = "";
     aAlfa1              = "";
     aHora1              = "";
     aHora2              = "";
     aDesde              = "";
     aHasta              = "";
     aFichero            = "";
     aEjecuta            = "";
     aDir                = "";
     aDirCopia           = "";
     aDestino            = "";
     aLong               = "";
     aCarac              = "";
     aBarra              = "";
     aEsc1               = "";
     aEsc2               = "";
     aRetu               = "";
     aTecla              = "";
     aFotoOrigen         = "";
     aFotoDestino        = "";
     aExt                = "";
     aDirLocal           = "";
     aDocRemoto          = "";
     aDocServidor        = "";
     aIPRemoto           = "";
     aFoto               = "";

     fFecha              = @;

     {200}aPopup         = "";

     {1}aContiene        = "";

     {-1}aGet            = 0;
         aGet1           = 0;
         aGet2           = 0;

     {-1}aVent           = "";
         aVent1          = 0;
         aVent2          = 0;
         aVent3          = 0;
         aVent4          = 0;
         aVent5          = "";

     &eaDSMAC            = "";
     &enDesdeCapturador  = 0;
     &enPrevisualizar    = 0;
     &eaCarpetaOrigen    = "";
     &eaDocCaptura       = "";
     &enGrupo            = 0;
     &enSubGrupo         = 0;
     &enTipo             = 0;
     &eaUsuArchivado     = "";
     &eaAutoSelPlantilla = "";
     &eaImpoAlbaVta      = "";
     &enSwModoSilencio   = 0;
     &eaErrorDsEscan     = "";
     &enNumRegDsEscan    = 0;
     &eaUnidadBasico     = 0;
|FIN;

|PROCESO dsarm053;
     |SI #dsarm053#3 = "N";
          #dsarm053#3 = "S";
          |GRABA 020#dsarm053.a;
          nSwPie = 1;
          |PRINT;
     |FINSI;
     |LIBERA #dsarm053;
|FPRC;

|DEFBUCLE dsarm053;
     #dsarm053#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL, dsarm053;
|FIN;

|PROCESO Imprimir;
     |IMPRE 0;
     |SI FSalida = 0;
          aInforme = "dsarm053";
          |INFOR aInforme;
          |SI FSalida = 0;
               |BUCLE dsarm053;
               |SI nSwPie = 1;
                    |PIEINF;
               |FINSI;
               |FININF;
          |SINO;
               aMensaje = "0000Informe " + aInforme + " inaccesible.";
               |MENAV aMensaje;
          |FINSI;
          |FINIMP;
     |SINO;
          |MENAV "0000Proceso cancelado.";
     |FINSI;
|FPRC;

|PROCESO ProcesaFichero;
     |QBF aFichero;
     aAlfa    = aFichero;
     aFichero = "";
     aLong    = aAlfa % 0;
     nLong    = aLong;
     |PARA nCarac = 1;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos   = (nCarac * 100) + 1;
           aCarac =  aAlfa % nPos;
           |SI aCarac = "/";
               aCarac = "\";
           |FINSI;

           aFichero = aFichero + aCarac;
     |SIGUE;

     aAlfa = aFichero < "";
     aAlfa = aAlfa - "thumbs.db";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI nLabelTiempo ! -1;
         |FIN_CONTROL_WINDOWS nLabelTiempo;
         nLabelTiempo = -1;
     |FINSI;

     |SI nLabelBarra ! -1;
         |FIN_CONTROL_WINDOWS nLabelBarra;
         nLabelBarra = -1;
     |FINSI;

     aAlfa  = "LABEL,{{15}}Procesando fichero " + aFichero;
     |CONTROL_SIMPLE 0, aAlfa, 1502, 1798, NULL;
     nLabelTiempo = FSalida;

     |SI #dsarz040#4 = "S";
          enSwModoSilencio = 1;
     |SINO;
          enSwModoSilencio = 0;
     |FINSI;
     eaErrorDsEscan = "";
     enNumRegDsEscan = 0;

     |SI nCopia    = 0;
          fFecha    = ~;
          |HORA aHora1;

          aAlfa     = (fFecha % -104) + (fFecha % 0402) + (fFecha % 102);
          aAlfa     = aAlfa + (aHora1 % 102) + (aHora1 % 402) + (aHora1 % 702);

          aDirCopia = #40#1;  |QBF aDirCopia;
          aDirCopia = aDirCopia + aAlfa;            |RMKDIR aDirCopia;
          aDirCopia = aDirCopia + "\";
     |FINSI;

     nCopia   = 1;
     aDestino = aDirCopia + (aFichero - aDir);
     |RCOPIA_FICHERO aFichero, aDestino;

     enGrupo           = #40#3;
     enSubGrupo        = #40#4;
     enTipo            = #40#5;
     enDesdeCapturador = 1;
     enPrevisualizar   = 1;
     eaCarpetaOrigen   = #40#1;
     eaDocCaptura      = aFichero;
     eaUsuArchivado    = #40#6;      |QBF eaUsuArchivado;
     eaAutoSelPlantilla = #40#7;
     eaImpoAlbaVta     = #40#8;
     eaImpoAutoADConta = #40#9;
     enImpoEmpContagen = #40#10;
     enImpoPercontagen = #40#11;
     enNoMoverImpoADConta = 0;
     aEjecuta          = "dsarz001;CAPTURAESTEDOC";


     |SI eaImpoAutoADConta = "S";
          aExtension = aFichero % -104;
          aAuxNom = (aFichero - aDir);
          aAuxNom = aAuxNom - aExtension;
          enImpoNumDoc = aAuxNom;
     |FINSI;

     |SUB_EJECUTA_NP aEjecuta;

     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;

     |QBF eaErrorDsEscan;
     |SI #dsarz040#4 = "S"; |Y eaErrorDsEscan ! "";
          |HORA aHora;
          |QBF aHora;
          |ABRE #dsarm053;
          |DDEFECTO #dsarm053;
          #dsarm053#0 = ~;
          #dsarm053#1 = aHora;
          #dsarm053#2 = enNumRegDsEscan;
          |LEE 101#dsarm053.=;
          |SI FSalida ! 0; |GRABA 020#dsarm053.b; |FINSI;
          #dsarm053#3 = "N";
          #dsarm053#4 = eaErrorDsEscan;
          #dsarm053#5 = aFichero;
          |GRABA 020#dsarm053.a;
          |LIBERA #dsarm053;
          |CIERRA #dsarm053;
     |FINSI;


     |SI enNoMoverImpoADConta = 1; |Y eaImpoAutoADConta = "S";
          |RFBORRA aDestino;

          ||Si la carpeta aDirCopia esta vacia borrarla.
          |RRMDIR aDirCopia;
     |SINO;
          |RFBORRA aFichero;
     |FINSI;
|FPRC;

|PROCESO dsarm040Ejecuta;

     |SI #dsarm040#8 = "S";
          |SI #dsarm001#203 = "N";
               aMensaje = "0000No esta activa la importaci¢n Albaranes Venta." + &13 + "Par metros. Conexi¢n Gesti¢n Comercial.";
               |MENAV aMensaje;
               |ACABA;
          |FINSI;
          |SI #dsarm001#204 = 0; |O #dsarm001#205 = 0; |O #dsarm001#206 = 0;
               aMensaje = "0000G/S/T para Alba. Vta. reconocidos por OCR, no configurados.";
               |MENAV aMensaje;
               |ACABA;
          |FINSI;
          |SI #dsarm001#207 = 0; |O #dsarm001#208 = 0; |O #dsarm001#209 = 0;
               aMensaje = "0000G/S/T para Alba. Vta. no reconocidos por OCR, no configurados.";
               |MENAV aMensaje;
               |ACABA;
          |FINSI;
     |FINSI;

     nCopia    = 0;
     aDir      = #40#1;  |QBF aDir;
     aFichero  = aDir + "*.*";
     |R_PDIR aFichero;
     |PARA;  |SI FSalida ] 0; |HACIENDO;
          |HAZ ProcesaFichero;

          |R_SDIR aFichero;
     |SIGUE;
|FPRC;

|DEFBUCLE dsarm040Ejecuta;
     #40#1;
     ;
     #0#0, aDesde;
     #0#0, aHasta;
     ;
     NULL, dsarm040Ejecuta;
|FIN;

|PROCESO EjecutaProceso;
     |ESTADO_CONTROL nGrid40, "DISABLE";

     |ESTADO_CONTROL nBtnSalir, "HIDE";

     aDesde    = "";
     aHasta    = "z" * 70;

     |BUCLE dsarm040Ejecuta;

     |ESTADO_CONTROL nBtnSalir, "SHOW";
     |ESTADO_CONTROL nGrid40, "ENABLE";
|FPRC;

|PROCESO Evento40;
     |SI FSalida = 1;  |O FSalida = 2;
         |ESTADO_CONTROL nBtnModificar, "DISABLE";
         |ESTADO_CONTROL nBtnBaja, "DISABLE";
         |ESTADO_CONTROL nBtnCopiar,  "ENABLE";

         |LEE 000#40=;
         |SI FSalida ! 0;   |ACABA;  |FINSI;
         |SI #40#0 ! #0#0;  |ACABA;  |FINSI;

         |ESTADO_CONTROL nBtnModificar, "ENABLE";
         |ESTADO_CONTROL nBtnBaja,      "ENABLE";
         |ESTADO_CONTROL nBtnCopiar,    "DISABLE";
     |FINSI;
|FPRC;

|PROCESO Baja40;
     #40#0 = #0#0;
     #40#1 = "";
|FPRC;

|PROCESO Alta40;
     #40#0 = #0#0;
     #40#1 = "z" * 50;
|FPRC;

|PROCESO Tipo12;  |TIPO 12;
|FPRC;

|PROCESO LabelTiempo;
     |SI nLabelTiempo ! -1;
         |FIN_CONTROL_WINDOWS nLabelTiempo;  nLabelTiempo = -1;
     |FINSI;

     |SI nFalta > 1;
         aAlfa  = "LABEL,{{15}}Para la pr¢xima captura faltan " + nFalta + " minutos";
     |SINO;
         aAlfa  = "LABEL,{{15}}Para la pr¢xima captura falta menos de un minuto";
     |FINSI;

     |CONTROL_SIMPLE 0, aAlfa, 1520, 1578, NULL;
     nLabelTiempo = FSalida;
|FPRC;

|PROCESO LabelBarra;
     nSeg = nSeg + 1;
     |SI nSeg > 60;  nSeg = 1;  |FINSI;

     nRedon = nSeg $ 1;
     |SI nRedon ! 0;  |ACABA;  |FINSI;

     |SI nLabelBarra ! -1;
         |FIN_CONTROL_WINDOWS nLabelBarra;   nLabelBarra  = -1;
     |FINSI;

     aBarra = ("00" + nSeg) % -102;
     aAlfa  = "LABEL,{{15}}" + aBarra;
     |CONTROL_SIMPLE 0, aAlfa, 1515, 1518, NULL;
     nLabelBarra = FSalida;
|FPRC;

|PROCESO Salir;
     nSalir = 1;
     nBtn   = 0;

     |PTEC 806;
|FPRC;

|PROCESO BtnNuevaCapt;
     nConta = #0#1 * 60;
|FPRC;

|PROCESO Capturar;
     |CONTROL_SIMPLE 0, "BOTON,{{197}}Pantalla principal", 2785, 2898, Salir;
     nBtnSalir = FSalida;

     |ESTADO_CONTROL nBtnCaptur, "ENABLE";

     nSalir    = 0;
     nConta    = 0;
     nSeg      = 0;

     |HAZ EjecutaProceso;

     nSegundos = #0#1 * 60;
     nFalta    = #0#1;  |HAZ LabelTiempo;

     |PARA;  |SI;  |HACIENDO;
          |SI nSalir = 1;  |SAL;  |FINSI;

          |SI nConta = nSegundos;
              |HAZ EjecutaProceso;
              nConta    = 0;
              nSegundos = #0#1 * 60;
              nFalta    = #0#1;
              nSeg      = 0;
              |HAZ LabelTiempo;
          |FINSI;

          |SLEEP 1;

          nConta = nConta + 1;

          nRedon = nConta $ 60;
          |SI nRedon = 0;
              nFalta = ((#0#1 * 60) - nConta) / 60;
              |HAZ LabelTiempo;
          |FINSI;

          |HAZ LabelBarra;

          |PULSATECLA;

          |SI nBtn = 1;  |SAL;  |FINSI;
     |SIGUE;

     |SI nBtn = 2;  nBtn = 3;  |FINSI;

     |FIN_CONTROL_WINDOWS nLabelTiempo;  nLabelTiempo = -1;
     |FIN_CONTROL_WINDOWS nLabelBarra;   nLabelBarra  = -1;
     |FIN_CONTROL_WINDOWS nBtnSalir;     nBtnSalir    = -1;

     |ESTADO_CONTROL nBtnCaptur, "DISABLE";

     |PULSATECLA;
     |PULSATECLA;
|FPRC;

|PROCESO Tipo7C1;   |TIPO 7;
     |SI nBtnSalir = -1;
         aQueBoton     = "|X000";
         aID           = #0#aQueBoton#;
         nBtnAceptar = aID;
         |ESTADO_CONTROL nBtnAceptar, "HIDE";

         aQueBoton      = "|X001";
         aID            = #0#aQueBoton#;
         nBtnCancelar = aID;
         |ESTADO_CONTROL nBtnCancelar, "HIDE";

         |CONTROL_SIMPLE 0, "BOTON,{{197}}Salir", 2688, 2798, Salir;
         nBtnSalir = FSalida;
     |FINSI;
|FPRC;

|PROCESO EntraDatos971
     |VENTANA 0501, 2199, -1, 16, "Carpeta contenedora";
     nVent971 = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent971;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PINPA #0#971;  nPanta971 = FSalida;

     |DDEFECTO #971;

     |SI nModo = 1;
         |CONTROL_SIMPLE nPanta971, "BOTON,{{136}}", 0692, 0695, Carpeta;
         nBtnCarpeta = FSalida;

         |CONTROL_SIMPLE nPanta971, "BOTON,{{197}}Asignar carpeta", 1076, 1186, Ubicacion;
         enBtnTipo = FSalida;

         |SI #dsarm001#203 = "N";      ||Impo. Albaranes.
              #971#10 = "N";
              |C_M #971#10, 1, "N";
         |FINSI;
     |FINSI;

     |SI nModo = 2;
         |CONTROL_SIMPLE nPanta971, "BOTON,{{197}}Cambiar carpeta", 1076, 1186, Ubicacion;
         enBtnTipo = FSalida;

         #971#0 = #40#1;
         #971#1 = #40#2;
         #971#2 = #40#3;
         #971#3 = #40#4;
         #971#4 = #40#5;
         #971#8 = #40#6;
         #971#9 = #40#7;
         #971#10 = #40#8;
         #971#11 = #40#9;
         #971#12 = #40#10;
         #971#13 = #40#11;

         |HAZ Tipo0C2Fw71;
         |SI #1#8 ! "N";
             |HAZ Tipo0C3Fw71;
             |HAZ Tipo0C4Fw71;
         |FINSI;
     |FINSI;

     |SI #dsarm001#203 = "N";      ||Impo. Albaranes.
          #971#10 = "N";
          |C_M #971#10, 1, "N";
     |FINSI;
     |HAZ Tipo0C10w071;

     |PINDA #0#971;

     |ENDATOS #1#971;
     |SI FSalida = 0;
         #40#0 = #0#0;
         #40#1 = #971#0;
         |LEE 101#40=;
         |SI FSalida ! 0;
             #40#0 = #0#0;
             #40#1 = #971#0;
             |GRABA 020#40b;
         |FINSI;
         #40#2 = #971#1;
         #40#3 = #971#2;
         #40#4 = #971#3;
         #40#5 = #971#4;
         #40#6 = #971#8;
         #40#7 = #971#9;
         #40#8 = #971#10;
         #40#9 = #971#11;
         #40#10 = #971#12;
         #40#11 = #971#13;
         |GRABA 020#40a;
         |LIBERA #40;

         |REFRESCACONTROL nGrid40;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent971;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVent971;
|FPRC;

|PROCESO Alta;
     nModo = 1;   |HAZ EntraDatos971;
|FPRC;

|PROCESO Modificar;
     nModo = 2;   |HAZ EntraDatos971;
|FPRC;

|PROCESO Baja;
     |CONFI "0000NSeguro que quiere dar de baja la Carpeta contenedora";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |LEE 101#40=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |BORRA 020#40a;
     |LIBERA #40;

     |REFRESCACONTROL nGrid40;

     FSalida = 1;  |HAZ Evento40;

     |FOCOTECLADO nGrid40;
|FPRC;

|PROCESO dsarm040Copiar;
     #40#0 = eaDSMAC;
     |GRABA 020#40n;
     |LIBERA #40;
|FPRC;

|DEFBUCLE dsarm040Copiar;
     #40#1;
     ;
     #0#0, aDesde;
     #0#0, aHasta;
     ;
     NULL, dsarm040Copiar;
|FIN;

|PROCESO Copiar;
     nTiempo  = #0#1;
     nHay     = 0;
     IaPopup  = aPopup2  <-;

     |LEE 000#0p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          IaPopup = IaPopup + 1;
          aAlfa   = #0#0 + " / " + #0#2 + " / " + #0#3;
          aPopup  = aAlfa;
          nHay    = nHay + 1;

          |LEE 000#0s;
     |SIGUE;

     |SI nHay = 0;
         #0#0 = eaDSMAC;
         #0#1 = nTiempo;

         |MENAV "0000No hay configuraci¢n en otra m quina";
         |ACABA;
     |FINSI;

     |MENAV "0000A continuaci¢n aparecer  todas las maquinas con la fecha y hora de la £ltima captura. Seleccione una m quina.";

     aPopup1      = nBtnCopiar;
     aPopup2      = nHay;
     aAlfa        = "";

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
         IaPopup  = aPopup3  <-;
         IaPopup  = IaPopup + (FSalida - 1);
         aAlfa    = aPopup;
     |FINSI;

     |SI aAlfa = "";
         #0#0 = eaDSMAC;
         #0#1 = nTiempo;

         |ACABA;
     |FINSI;

     aAlfa1 = aAlfa % 120;  #0#0   = aAlfa1;
     |LEE 000#0=;
     |SI FSalida = 0;
         nTiempo = #0#1;
         aDesde  = "";
         aHasta  = "z" * 70;

         |BUCLE dsarm040Copiar;
     |FINSI;

     #0#0 = eaDSMAC;
     #0#1 = nTiempo;   |PINTA #0#1;

     |REFRESCACONTROL nGrid40;
     |FOCOTECLADO nGrid40;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     FSalida = 2799;
|FPRC;

|PROCESO TiposAuto;
     |VENTANA 0501, 2799, nPanta, 17, "Configuraci¢n Tipos destino Archivado Autom tico";
     nSubVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |SUB_EJECUTA_NP "dsarz053";

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nSubVent;
|FPRC;

|PROCESO PantaConf;
     nBtnSalir = -1;

     |VENTANA 0501, 2799, -1, 16, "Configuraci¢n ";
     nVentConf = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentConf;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PINPA #0#0;  nPanta0 = FSalida;
     |PINDA #0#0;

     |CONTROL_SIMPLE nPanta0, "BOTON,{{199}}Baja", 1887, 1898, Baja;
     nBtnBaja = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON,{{197}}Modificar", 1875, 1885, Modificar;
     nBtnModificar = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON,{{193}}Alta", 1863, 1873, Alta;
     nBtnAlta = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON,{{197}}Copiar configuraci¢n de otra m quina", 1802, 1832, Copiar;
     nBtnCopiar = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON,{{205}}", 2174, 2178, Imprimir;
     nBtnImp = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON,Configuraci¢n Tipos Autom ticos", 2280, 2397, TiposAuto;
     nBtnTiposAuto = FSalida;

     |ESTADO_CONTROL nBtnModificar, "DISABLE";
     |ESTADO_CONTROL nBtnBaja,      "DISABLE";

     nRango = 2 + 4 + 8 + 16 + 32 + 128;
     |LINEAL_SIMPLE #1#40, nRango, 0602, 1798, Baja40, Alta40, Evento40;
     nGrid40 = FSalida;

     |ENDATOS #1#0;

     nTiempo = #0#1;

     aSilencio = #0#4;

     |HORA aHora1;

     #0#0 = eaDSMAC;
     |LEE 101#0=;
     |SI FSalida ! 0;
         |DDEFECTO #0;
         #0#0 = eaDSMAC;
         |GRABA 020#0b;
     |FINSI;

     #0#1 = nTiempo;
     #0#2 = ~;
     #0#3 = aHora1;
     #0#4 = aSilencio;

     |GRABA 020#0a;
     |LIBERA #0;

     |FIN_CONTROL_WINDOWS nBtnSalir;
     |FIN_CONTROL_WINDOWS nBtnAlta;
     |FIN_CONTROL_WINDOWS nBtnModificar;
     |FIN_CONTROL_WINDOWS nBtnBaja;
     |FIN_CONTROL_WINDOWS nBtnCopiar;
     |FIN_CONTROL_WINDOWS nBtnImp;
     |FIN_CONTROL_WINDOWS nBtnTiposAuto;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentConf;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVentConf;
|FPRC;

|PROCESO BtnConf;
     |SI nBtn ] 2;
         nBtn = 3;
     |SINO;
         nBtn = 1;
     |FINSI;

     nSalir = 1;
     |PTEC 806;
|FPRC;


|PROCESO BtnCapt;
     nBtn = 2;

     |PTEC 806;
|FPRC;

|PROCESO PonFoto;
     |DBASE aBase;  |QBF aBase;

     aFotoOrigen = aBase + "logos/logods.jpg";
     aExt        = ".jpg";

     |HAZ LeeUnidadBasico;

     aDirLocal    = eaUnidadBasico + "\DOCUMENTOS";                     |RMKDIR aDirLocal;
     aDirLocal    = eaUnidadBasico + "\DOCUMENTOS\DSARCHI";             |RMKDIR aDirLocal;
     aDirLocal    = eaUnidadBasico + "\DOCUMENTOS\DSARCHI\LOGOS";       |RMKDIR aDirLocal;
     aFotoDestino = aDirLocal + "/logods" + aExt;

     aContiene1 = aFotoDestino;
     |ESPECIFICA "REMOTOMD5", aContiene;
     aDocRemoto = FSalida;  |QBF aDocRemoto;

     aContiene1 = aFotoOrigen;
     |ESPECIFICA "MD5", aContiene;
     aDocServidor = FSalida;  |QBF aDocServidor;

     |SI aDocRemoto ! aDocServidor;  |O aDocRemoto = "";
         aIPRemoto = "";
         |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aFotoOrigen, aFotoDestino;
         |SINO;
             |COPIA_FICHERO aFotoOrigen, aFotoDestino;
         |FINSI;
     |FINSI;

     aFoto = "IMAGEN,"  + aFotoDestino;
     |CONTROL_SIMPLE 0, aFoto, 0501, 0928, NULL;
     nBtnFoto = FSalida;
|FPRC;

|PROGRAMA;
     |ABRET #0;
     |ABRET #40;

     |CLS;

     |ABRE #1;
     |LEE 000#1p;
     |SI FSalida ! 0;  |DDEFECTO #1;  |FINSI;
     |CIERRA #1;

     |HAZ CogeMac;

     #0#0 = eaDSMAC;
     |LEE 000#0=;
     |SI FSalida ! 0;
         |DDEFECTO #0;
         #0#0 = eaDSMAC;
     |FINSI;

     |VENTANA 0501, 2899, -1, 0, "";
     nVentana = FSalida;

     |HAZ PonFoto;

     |CONTROL_SIMPLE 0, "LABEL,{{9}}Capturador autom tico de documentos", 0535, 0998, NULL;


     |CONTROL_SIMPLE 0, "BOTON,{{197}}Capturar de nuevo", 2702, 2815, BtnNuevaCapt;
     nBtnCaptur  = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Configuraci¢n", 2720, 2834, BtnConf;
     nBtnConfig  = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA;  |SI;  |HACIENDO;
          |ESTADO_CONTROL nBtnCaptur,  "DISABLE";

          |CONTROL_SIMPLE 0, "BOTON,C A P T U R A R", 1540, 1760, BtnCapt;
          nBtnIniciar = FSalida;

          |ESTADO_CONTROL nBtnIniciar, "DISABLE";

          |HAZ CogeMac;
          #0#0 = eaDSMAC;
          |LEE 000#0=;
          |SI FSalida ! 0;
              |DDEFECTO #0;
              #0#0 = eaDSMAC;
          |FINSI;

          #40#0 = #0#0;
          #40#1 = "";
          |LEE 000#40];
          |SI FSalida = 0;  |Y  #40#0 = #0#0;
              |ESTADO_CONTROL nBtnIniciar, "ENABLE";
          |FINSI;

          FSalida = "::{{001}}Salir";
          |LEETECLA aTecla;

          |SI aTecla = aRetu;  |SAL;  |FINSI;

          |FIN_CONTROL_WINDOWS nBtnIniciar;

          |SI nBtn = 1;
              |HAZ PantaConf;
          |SINO;
              |SI nBtn = 2;
                  |HAZ Capturar;
              |SINO;
                  |SI nBtn = 3;
                       |HAZ PantaConf;
                       |HAZ Capturar;
                  |FINSI;
              |FINSI;
          |FINSI;
     |SIGUE;

     |CIERRAT #0;
     |CIERRAT #40;

     |FIN_CONTROL_WINDOWS nBtnIniciar;
     |FIN_CONTROL_WINDOWS nBtnConfig;
     |FIN_CONTROL_WINDOWS nBtnCaptur;

     |FINVENTANA nVentana;

     |PULSATECLA;
|FPRO;
