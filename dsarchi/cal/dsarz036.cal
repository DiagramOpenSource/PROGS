|FICHEROS;
     dsarz036 #0;

     dsarw060 #60;       ||Aux. Faxes Asterisk
     dsarm001 #1;        ||Parametros
     dsarm005 #5;        ||Registro documentos
     dsarm000 #100;      ||Usuarios
     dsarw018 #918,MANTE;||Desde Fecha
|FIN;

|VARIABLES;
     aNumHora = "";
     aNumMinu = "";
     aNumSeg = "";
     nNumHora = 0;
     nNumMin = 0;
     nNumSeg = 0;

     aHoraQuieroSeg = "";
     nSegundos = 0;
     nSegundos60 = 0;
     nSegundosReg = 0;
     nSegundosRegDesde = 0;
     nSegundosRegHasta = 0;

     fHasta = @;
     fDesde = @;

     aDFecha = "";
     aHFecha = "";
     fFecha = @;
     aTipoFecha = "";
     nBoton = 0;
     nNume = 0;
     nVentm5 = 0;
     aFechaGuarda = "";
     aAlfa = "";
     nPanta = 0;

     {-1}aVent        = "";
         aVent1       = 0;
         aVent2       = 0;
         aVent3       = 0;
         aVent4       = 0;
         aVent5       = "";

     nGrupo = 0;
     nSubGrupo = 0;
     nTipo = 0;
     aAux = "";
     aBeta = "";
     aFecha60 = "";
     aHora60 = "";
     aSrc60 = "";
     aFechaReg = "";
     aHoraReg = "";
     aSrcReg = "";
     aCanal = "";
     aCanal60 = "";

     aMensaje = "";
     aParametro = "";

     aCalldate = "";
     aClid = "";
     aSrc = "";
     aDst = "";
     aDcontext = "";
     aChannel = "";
     aDstchannel = "";
     aLastapp = "";
     aLastdata = "";
     nDuration = 0;
     nBillsec = 0;
     aDisposition = "";
     nAmaflags = 0;
     aAccountcode = "";
     aUniqueid = "";
     aUserfield = "";

     aFechaAst = "";
     aDia = "";
     aMes = "";
     aAnyo = "";
     aHoraAst = "";

     nSwEsta = 0;
     nNumRegistro = 0;

     fLogDia = @;
     aLogHor = "";
     aFicLog = "";
     nHandLog = 0;
     aMenLog = "";
     aPathImpo = "";
     aPathOks = "";
     aNombre = "";
     aFichero = "";
     aLong = "";
     nLong = 0;
     nContador = 0;
     aTextAux = "";
     nVoy = 0;
     aSaca = "";
     aLetra = "";
     aReturnUnix = "";
     aTab = "";

     nHandle = 0;
     nLinea = 0;
     aLinea = "";
     aOrigen = "";
     aDestino = "";


     aEsc1  = "";
     aEsc2  = "";
     aRetu  = "";
     nRango = 0;
     nPos1 = 0;
     nPos2 = 0;
     aTecla = "";
     nGrid = 0;

     aFecha = "";
     aHora = "";
     aBase = "";
     aDirectorio = "";
     nBotonLabel = 0;
|FIN;

|PROCESO Tipo80; |TIPO 80;
     |SI aParametro ! "AUTO";
          FSalida = 2999;
     |FINSI;
|FPRC;

|PROCESO MeteLog;
     fLogDia = ~;
     |HORA aLogHor;

     |XABRE "A", aFicLog, nHandLog;
     |SI FSalida ] 0;
          aMenLog = fLogDia + "#" + aLogHor + "#" + aMenLog;
          |XGRABA nHandLog, aMenLog;
          |XCIERRA nHandLog;
     |FINSI;
|FPRC;

|PROCESO SacaReg;
     aAux = #dsarm005#nVoy#;
     |QBF aAux;
     aBeta = aAux % 106;
     |SI aBeta = "HORA: ";
          aHoraReg = aAux - aBeta;
          |QBF aHoraReg;
          |ACABA;
     |FINSI;
     aBeta = aAux % 107;
     |SI aBeta = "LINEA: ";
          aSrcReg = aAux - aBeta;
          |QBF aSrcReg;
          |ACABA;
     |FINSI;
     |SI aBeta = "FECHA: ";
          aFechaReg = aAux - aBeta;
          |QBF aFechaReg;
          |ACABA;
     |FINSI;
     |SI aBeta = "CANAL: ";
          aCanal = aAux - aBeta;
          |QBF aCanal;
          |ACABA;
     |FINSI;
|FPRC;

|PROCESO DameSegundos;
     aNumHora = aHoraQuieroSeg % 102;
     aNumMinu = aHoraQuieroSeg % 402;
     aNumSeg = aHoraQuieroSeg % 702;

     nNumHora = aNumHora;
     nNumMin = aNumMinu;
     nNumSeg = aNumSeg;

     nSegundos = (nNumHora * 3600) + (nNumMin * 60) + nNumSeg;
|FPRC;

|PROCESO dsarm005;
     ||nSwEsta = 0;
     ||nNumRegistro = 0;

     aFechaReg = "";
     aHoraReg = "";
     aSrcReg = "";
     aCanal = "";
     |PARA nVoy = 23; |SI nVoy [ 37; |HACIENDO nVoy = nVoy + 1;
          |HAZ SacaReg;
     |SIGUE;
     |PARA nVoy = 60; |SI nVoy [ 64; |HACIENDO nVoy = nVoy + 1;
          |HAZ SacaReg;
     |SIGUE;

     |SI aFechaReg ! ""; |Y aHoraReg ! ""; |Y aCanal ! "";
          aFecha60 = #dsarw060#1;
          |QBF aFecha60;
          aHora60 = #dsarw060#2;
          |QBF aHora60;
          aSrc60 = #dsarw060#5;
          |QBF aSrc60;
          aCanal60 = #dsarw060#8;
          |QBF aCanal60;

          ||ARA .. Controlar las llamadas con un margen de 10 segundos.
          ||En el registro de Documentos, tenemos la hora fin llamada
          ||Y en el asterisk no llega la hora inicio llamada.
          ||TODO CCC

          aHoraQuieroSeg = aHora60;
          nSegundos = 0;
          |HAZ DameSegundos;
          nSegundos60 = nSegundos + #dsarw060#12;           ||Hora inicio + duracion

          aHoraQuieroSeg = aHoraReg;
          nSegundos = 0;
          |HAZ DameSegundos;
          nSegundosReg = nSegundos;
          nSegundosRegDesde = nSegundosReg - 10;
          nSegundosRegHasta = nSegundosReg + 10;

          |SI aFecha60 = aFechaReg; |Y aCanal60 = aCanal;
               |SI nSegundos60 > nSegundosRegDesde; |Y nSegundos60 < nSegundosRegHasta;
                    nSwEsta = 1;
                    nNumRegistro = #dsarm005#0;

                    aMenLog = " - dscoz036. Encontrado Fax en Registro. Num.: " + nNumRegistro;
                    |HAZ MeteLog;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE dsarm005;
 #dsarm005#2;
 ;
 nGrupo, nSubGrupo, nTipo, 000000001;
 nGrupo, nSubGrupo, nTipo, 999999999;
 ;
 NULL, dsarm005;
|FIN;

|PROCESO BuscaNumRegistro;
     |ABRE #dsarm001;
     |DDEFECTO #dsarm001;
     |LEE 000#dsarm001.=;
     |SI FSalida ! 0;
          |DDEFECTO #dsarm001;
     |FINSI;

     nGrupo = #dsarm001#67;
     nSubGrupo = #dsarm001#68;
     nTipo = #dsarm001#69;
     |BUCLE dsarm005;

     |SI nSwEsta = 0;
          nGrupo = #dsarm001#89;
          nSubGrupo = #dsarm001#90;
          nTipo = #dsarm001#91;
          |BUCLE dsarm005;
     |FINSI;

     |CIERRA #dsarm001;
|FPRC;

|PROCESO RegistroAsterisk;
     |DDEFECTO #dsarw060;
     #dsarw060#0 = aUniqueid;
     |LEE 101#dsarw060.=;
     |SI FSalida ! 0; |GRABA 020#dsarw060.b; |FINSI;
     #dsarw060#1 = aFechaAst;
     #dsarw060#2 = aHoraAst;
     #dsarw060#3 = aCalldate;
     #dsarw060#4 = aClid;
     #dsarw060#5 = aSrc;
     #dsarw060#6 = aDst;
     #dsarw060#7 = aDcontext;
     #dsarw060#8 = aChannel;
     #dsarw060#9 = aDstchannel;
     #dsarw060#10 = aLastapp;
     #dsarw060#11 = aLastdata;
     #dsarw060#12 = nDuration;
     #dsarw060#13 = nBillsec;
     #dsarw060#14 = aDisposition;
     #dsarw060#15 = nAmaflags;
     #dsarw060#16 = aAccountcode;
     #dsarw060#17 = aUserfield;

     nSwEsta = 0;
     nNumRegistro = 0;
     |HAZ BuscaNumRegistro;
     |SI nSwEsta = 1;
          #dsarw060#18 = nNumRegistro;
     |FINSI;

     |GRABA 020#dsarw060.a;
     |LIBERA #dsarw060;
|FPRC;

|PROCESO MeteRegistros;
     aFichero = aDirectorio;
     |XABRE "U", aFichero, nHandle;
     |SI FSalida < 0;
          aMenLog = " - dscoz036. Problemas al abrir " +  aFichero;
          |HAZ MeteLog;
          |ACABA;
     |SINO;
          aMenLog = " - dscoz036. Importando fichero" +  aFichero;
          |HAZ MeteLog;

          nLinea = 0;
          |PARA; |SI; |HACIENDO;
               |XLEE nHandle, 250, aLinea;
               |SI FSalida < 0;
                    |SAL;
               |SINO;
                    |QBF aLinea;
                    aLong = aLinea % A0;     ||A0 == "0"
                    nLong = aLong;
                    |SI nLong = 0;
                         |SAL;
                    |FINSI;

          ||Falta poner los campos.

                    ||ARA66

                    nLinea = nLinea + 1;
                    |SI nLinea ! 1;          ||La primera linea nos la saltamos

                         aCalldate = "";
                         aClid = "";
                         aSrc = "";
                         aDst = "";
                         aDcontext = "";
                         aChannel = "";
                         aDstchannel = "";
                         aLastapp = "";
                         aLastdata = "";
                         nDuration = 0;
                         nBillsec = 0;
                         aDisposition = "";
                         nAmaflags = 0;
                         aAccountcode = "";
                         aUniqueid = "";
                         aUserfield = "";

                         nContador = 0;
                         aTextAux = "";
                         |PARA nVoy = 1; |SI nVoy [ nLong; |HACIENDO nVoy = nVoy + 1;
                              aSaca = nVoy + "01";
                              aLetra = aLinea % aSaca;
                              |SI aLetra = aTab; |O aLetra = aReturnUnix;
                                   nContador = nContador + 1;
                                   |SI nContador = 1;
                                        aCalldate = aTextAux;
                                        aFechaAst = aTextAux % 110;
                                        aDia = aFechaAst % -102;
                                        aMes = aFechaAst % 602;
                                        aAnyo = aFechaAst % 104;
                                        aFechaAst = aDia + "." + aMes + "." + aAnyo;
                                        aHoraAst = aTextAux % 1208;
                                   |FINSI;
                                   |SI nContador = 2;
                                        aClid  = aTextAux;
                                   |FINSI;
                                   |SI nContador = 3;
                                        aSrc = aTextAux;
                                   |FINSI;
                                   |SI nContador = 4;
                                        aDst = aTextAux;
                                   |FINSI;
                                   |SI nContador = 5;
                                        aDcontext = aTextAux;
                                   |FINSI;
                                   |SI nContador = 6;
                                        aChannel = aTextAux;
                                   |FINSI;
                                   |SI nContador = 7;
                                        aDstchannel = aTextAux;
                                   |FINSI;
                                   |SI nContador = 8;
                                        aLastapp = aTextAux;
                                   |FINSI;
                                   |SI nContador = 9;
                                        aLastdata = aTextAux;
                                   |FINSI;
                                   |SI nContador = 10;
                                        nDuration = aTextAux;
                                   |FINSI;
                                   |SI nContador = 11;
                                        nBillsec = aTextAux;
                                   |FINSI;
                                   |SI nContador = 12;
                                        aDisposition = aTextAux;
                                   |FINSI;
                                   |SI nContador = 13;
                                        nAmaflags = aTextAux;
                                   |FINSI;
                                   |SI nContador = 14;
                                        aAccountcode = aTextAux;
                                   |FINSI;
                                   |SI nContador = 15;
                                        aUniqueid = aTextAux;
                                   |FINSI;
                                   |SI nContador = 16;
                                        aUserfield = aTextAux;
                                   |FINSI;
                                   aTextAux = "";
                              |SINO;
                                   aTextAux = aTextAux + aLetra;
                              |FINSI;
                         |SIGUE;
                         ||Al salir falta meter el resto de campos
                         |SI nContador = 14;
                              aUniqueid = aTextAux;
                              aUserfield = "";
                         |FINSI;
                         |SI nContador = 15;
                              aUserfield = aTextAux;
                         |FINSI;

                         |HAZ RegistroAsterisk;
                    |FINSI;
               |FINSI;
          |SIGUE;
          |XCIERRA nHandle;

          aOrigen = aPathImpo + aNombre;
          aDestino = aPathOks + aNombre;

          ||TODO CCC. Descomentar
          |RENOMBRA_FICHERO aOrigen, aDestino;


          aMenLog = " - dscoz036. Moviendo Fichero [" + aNombre + "] a directorio (Oks)";
          |HAZ MeteLog;
     |FINSI;
|FPRC;

|PROCESO RecogeDatos;
     |SI aParametro ! "AUTO";
          |CONTROL_SIMPLE 0, "LABEL,{{5}}Cargando informaci¢n ...", 1315, 1680, NULL;
          nBotonLabel = FSalida;
          |PTEC 802;  |PAUSA;
          ||Pantallazo. Por favor espere
     |FINSI;

     aPathImpo = aBase + "asterisk/";
     aPathOks = aPathImpo + "oks/";
     |MKDIR aPathOks;

     aDirectorio = aBase + "asterisk/*.txt";
     |_PDIR aDirectorio;
     |SI FSalida ] 0;
          |PARA; |SI FSalida ] 0; |HACIENDO;
               aNombre = aDirectorio - aPathImpo;
               |HAZ MeteRegistros;
               |_SDIR aDirectorio;
          |SIGUE;
     |FINSI;

     |SI aParametro ! "AUTO";
          |FIN_CONTROL_WINDOWS nBotonLabel;
          ||Final Pantallazo. Por favor espere
     |FINSI;
|FPRC;

|PROCESO Baja;
     #dsarw060#1 = aHFecha;
     #dsarw060#2 = "zzzzzzzz";
|FPRC;

|PROCESO Alta;
     #dsarw060#1 = aDFecha;
     #dsarw060#2 = "        ";
|FPRC;

|PROCESO Evento;
|FPRC;


|PROCESO Boton;
     |VENTANA 0501, 1354, -1, 16, "Pedir desde fecha";
     nVentm5 = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentm5;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     aFechaGuarda = aDFecha;
     #918#7 = aTipoFecha;
     #918#8 = aDFecha;

     |PINPA #0#918;
     |PINDA #0#918;
     |ENDATOS #1#918;
     |SI FSalida = 0;
         aDFecha    = #918#8;
         aTipoFecha = #918#7;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentm5;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVentm5;

     |SI aFechaGuarda ! aDFecha;
          fDesde = aDFecha;
          fHasta = aHFecha;
          |SI fDesde > fHasta;
               aDFecha = fHasta;
          |FINSI;
          |REFRESCACONTROL nGrid;
     |FINSI;

     |FOCOTECLADO nGrid;

     |HAZ BotonFecha;
|FPRC;

|PROCESO BotonFecha;
     |SI nBoton ! -1;
         |FIN_CONTROL_WINDOWS nBoton;
         nBoton = -1;
     |FINSI;

     |SI aTipoFecha = "1";
         aAlfa = "BOTON,Desde Fecha: SIN LIMITE";
     |FINSI;

     |SI aTipoFecha = "2";
         aAlfa = "BOTON,Desde Fecha: MES ACTUAL";
     |FINSI;

     |SI aTipoFecha = "3";
         aAlfa = "BOTON,Desde Fecha: A¥O ACTUAL";
     |FINSI;

     |SI aTipoFecha = "4";
         aAlfa = "BOTON,Desde Fecha: TRES MESES ATRAS";
     |FINSI;

     |SI aTipoFecha = "5";
         aAlfa = "BOTON,Desde Fecha: SEIS MESES ATRAS";
     |FINSI;

     |SI aTipoFecha = "6";
         aAlfa = "BOTON,Desde Fecha: UN A¥O ATRAS";
     |FINSI;

     |SI aTipoFecha = "7";
         aAlfa = "BOTON,Desde Fecha: " + aDFecha;
     |FINSI;

     |CONTROL_SIMPLE nPanta, aAlfa, 0670, 0698, Boton;
     nBoton = FSalida;
|FPRC;

|PROCESO CalculaFecha;
     fFecha  = ~;

     |SI aTipoFecha = "1";
         aDFecha = "01.01.0000";
     |FINSI;

     |SI aTipoFecha = "2";
         aAlfa   = fFecha % 0402;
         aAlfa   = "01." + aAlfa + (fFecha % -105);
         aDFecha = aAlfa;
     |FINSI;

     |SI aTipoFecha = "3";
         aAlfa   = "01.01." + (fFecha % -104);
         aDFecha = aAlfa;
     |FINSI;

     |SI aTipoFecha = "4";
         nNume   = fFecha;
         nNume   = (nNume - 92);
         fFecha  = nNume;
         aDFecha = fFecha;
     |FINSI;

     |SI aTipoFecha = "5";
         nNume  = fFecha;
         nNume  = (nNume - 184);
         fFecha  = nNume;
         aDFecha = fFecha;
     |FINSI;

     |SI aTipoFecha = "6";
         nNume  = fFecha;
         nNume  = (nNume - 365);
         fFecha  = nNume;
         aDFecha = fFecha;
     |FINSI;
|FPRC;



|PROGRAMA;
     |CLS;
     aParametro = PARAMETRO;
     |QBF aParametro;
     |SI aParametro ! "AUTO";
          |PINPA #0#dsarz036;
          nPanta = FSalida;
     |FINSI;

     aTab = &9;
     aReturnUnix = &10;

     |DBASE aBase;
     |QBF aBase;

     aFicLog = aBase + "logs/";
     |MKDIR aFicLog;
     aFicLog = aFicLog + "impo_asterisk.log";

     aMenLog = " - dscoz036. Inicio Recoger datos Asterisk";
     |HAZ MeteLog;

     |ABRE #dsarw060;
     |HAZ RecogeDatos;

     aMenLog = " - dscoz036. Final Recoger datos Asterisk";
     |HAZ MeteLog;


     |SI aParametro = "AUTO";
          |ACABA;
     |FINSI;

     |ABRE #dsarm000
     #dsarm000#0 = Usuario % 810;
     |LEE 000#dsarm000.=;
     |SI FSalida ! 0;
         |DDEFECTO #dsarm000;
     |FINSI;

     fFecha     = ~;
     aDFecha    = #dsarm000#6;
     aTipoFecha = #dsarm000#7;
     aHFecha = fFecha;
     |CIERRA #dsarm000;

     |HAZ CalculaFecha;
     |HAZ BotonFecha;

     nRango = 4 + 8 + 16 + 32;
     nPos1 = 0702;
     nPos2 = 2997;

     |LINEAL_SIMPLE #2#dsarw060, nRango, nPos1, nPos2, Baja, Alta, Evento;
     nGrid = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::Salir," + nGrid;
          |LEETECLA aTecla;
          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
          |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |CIERRA #dsarw060;

     |ABRE #dsarm000;
     #dsarm000#0 = Usuario % 810;
     |LEE 101#dsarm000.=;
     |SI FSalida ! 0;
         |DDEFECTO #dsarm000;
         #dsarm000#0 = Usuario % 810;
         |GRABA 020#dsarm000.b;
     |FINSI;
     #dsarm000#6 = aDFecha;
     #dsarm000#7 = aTipoFecha;
     |GRABA 020#dsarm000.a;
     |LIBERA #dsarm000;
     |CIERRA #dsarm000;
|FPRO;
