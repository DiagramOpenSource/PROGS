|FICHEROS;
     dsarm001;           ||Parametros
     dsarm002;           ||Grupos
     dsarm003;           ||Subgrupos
     dsarm004;           ||Tipos
     dsarm005;           ||Registro Documentos
     dsarm006;           ||Registro Documentos
     dsarm007;           ||Registro Documentos
     dsarm100;           ||Contador Reg.Doc.

     flowm006;

     referen@;             ||CARGA_DEF
|FIN;

|VARIABLES;
     &enEmpCorreo = 0;
     nUltimoDoc = 0;          ||Contador

     &enNumRegFlow = 0;
     &enSituFlow = 0;
     &eaDesSituFlow = "";

     &eaTipoCorreo = "";     ||Sirve para distinguir cuando el correo
                             ||es de tipo "nominas".

     nSalidaHis = 0;
     nSitu = 0;
     aHora = "";
     aUsuario = "";
     nLinea = 0;

     &eaSwEnvioCorreo = "";  ||Indica si se envia por correo al emitir
     &enErrorCorreo = 0;
     &enDocumento   = 0;

     aAux = "";

     &enSwDesde9 = 0;

     &eaFicLogVisor = "";
     &eaPathLogVisor = "";
     &eaNomLogVisor = "";
     &eaDesLogVisor = "";

     aAAAAMM = "";
     aFechaSis = "";
     fFechaSis = @;

     aAlfa1 = "";

     &eaNomTemporal = "";
     &enSwErrorRecDoc = 0;

     aFicLog = "";
     fLogDia = @;
     aLogHor = "";;
     nHandLog = 0;
     aMenLog = "";
     nSwTodoCorrecto = 0;

     aMensaje = "";

     aBase = "";
     aDirRecDoc = "";
     aMas = "";
     aPathDatos = "";

     aFicheroTGZ = "";
     aFicheroTAR = "";

     nGrupoDefecto = 0;
     nSubGrDefecto = 0;
     nTipoDefecto = 0;

     nGrupoAlm = 0;
     nSubGrAlm = 0;
     nTipoAlm = 0;

     aQueQuiero = "";
     aNumCampos = "";
     nNumCampos = 0;
     nVoy = 0;

     nUltimo = 0;

     nDocuCarpeta = 0;
     aIPRemoto = "";

     aUbicacionBase = "";
     aAlfa = "";
     aPathDestino = "";
     aOrigen = "";
     aDestino = "";
     nHandle = 0;
     aUltLetra = "";
     aExtension = "";
     aSinExtension = "";
     aNomDestino = "";
     &enSwContab = 0;
     &enSwNomina = 0;
     &enSwNominaEmpCen = 0;

     nRegRepe = 0;
     fFecRepe = @;
     nEmpRepe = 0;
     nTraRepe = 0;
     nCenRepe = 0;
     aUsuRepe = "";
     aNTrRepe = "";
     nRepe    = 0;
     nCli     = 0;
     nCentro  = 0;
     nTrab    = 0;
     nModelo  = 0;
     nAnyo    = 0;
     nMes     = 0;
     nTipo    = 0;
     aNTr     = "";
     aFichero = "";
     &enSwBorradoLaboral = 0;
     &eaFechaEnvio = "";
     &eaHoraEnvio = "";
     &eaEmailEnvio = "";
     &nDesdeMes = 0;
     &nHastaMes = 0;
|FIN;

|PROCESO DondeAlmaceno;
     ||TODO CCC.

     ||Pendiente.
     ||He pensado un mantenimiento, por tipo de documento.
     ||Pero por ahora, pilla estos datos de los parametros.

     nGrupoDefecto = #dsarm001#102;
     nSubGrDefecto = #dsarm001#103;
     nTipoDefecto = #dsarm001#104;

     |SI enSwNomina > 0;
          nGrupoDefecto = #dsarm001#109;
          nSubGrDefecto = #dsarm001#110;
          nTipoDefecto  = #dsarm001#111;
     |FINSI;

     || Tipo Documento.   Grupo + SubGrupo + Tipo
     || facturas          2       1          1
     || pedido            5       2          1
     || etc ... o un lugar comun para todo.

     || Comentar con Priego.
|FPRC;

|PROCESO MeteLog;
     fLogDia = ~;
     |HORA aLogHor;

     |XABRE "A", aFicLog, nHandLog;
     |SI FSalida ] 0;
          aMenLog = fLogDia + "#" + aLogHor + "#" + aMenLog;
          |XGRABA nHandLog, aMenLog;
          |XCIERRA nHandLog;
     |FINSI;
|FPRC;

|| PROCESOS PARA BORRADO DE DOCUMENTOS

|PROCESO dsarm006_borra;
     |BORRA 020#dsarm006.a;
|FPRC;

|DEFBUCLE dsarm006_borra;
     #dsarm006#1;
     ;
     #dsarm005#0, 001;
     #dsarm005#0, 999;
     be;
     NULL, dsarm006_borra;
|FIN;

|PROCESO dsarm007_1B;
     |BORRA 020#dsarm007.a;
     |LIBERA #dsarm007;
|FPRC;

|DEFBUCLE dsarm007_1B;
     #dsarm007#1;
     ;
     #dsarm005#0, 000000001;
     #dsarm005#0, 999999999;
     be;
     NULL, dsarm007_1B;
|FIN;

|PROCESO dsarm007_2B;
     |BORRA 020#dsarm007.a;
     |LIBERA #dsarm007;
|FPRC;

|DEFBUCLE dsarm007_2B;
     #dsarm007#2;
     ;
     #dsarm005#0, 000000001;
     #dsarm005#0, 999999999;
     be;
     NULL, dsarm007_2B;
|FIN;

|PROCESO BuscaRepe;
     nRegRepe = #dsarm005#0;
     fFecRepe = #dsarm005#4;
     nEmpRepe = #dsarm005#8;
     nTraRepe = #dsarm005#15;
     aNTrRepe = #dsarm005#95;
     aUsuRepe = #dsarm005#6;
     nCenRepe = #dsarm005#99;

     |SI enSwContab = 2;
         |SI aNTrRepe ! aNTr;
             |ACABA;
         |FINSI;
     |FINSI;
     nRepe    = nRepe + 1;

     aFichero = #dsarm005#7;              |QBF aFichero;
     aFichero = aFichero + #dsarm005#9;   |QBF aFichero;

     |SI #dsarm001#7 = "N";
          |FBORRA aFichero;
     |SINO;
          |RFBORRA aFichero;
     |FINSI;

     |BUCLE dsarm006_borra;
     |BUCLE dsarm007_1B;
     |BUCLE dsarm007_2B;

     |BORRA 020#dsarm005.a;
     |LIBERA #dsarm005;
|FPRC;

|DEFBUCLE BuscaRepe;
     #dsarm005#1;
     8, 15, 19, 17, 18, 20, 99;
     000000001, nCli, nTrab, nModelo, nAnyo, nMes, nTipo, nCentro;
     999999999, nCli, nTrab, nModelo, nAnyo, nMes, nTipo, nCentro;
     ;
     NULL, BuscaRepe;
|FIN;

|PROCESO Duplicados;
     nRegRepe = 0;
     fFecRepe = "";
     nEmpRepe = 0;
     nTraRepe = 0;
     nCenRepe = 0;
     aUsuRepe = "";
     aNTrRepe = "";

     nCli     = #referen@#8;
     nTrab    = #referen@#15;
     nModelo  = #referen@#19;
     nAnyo    = #referen@#17;
     nMes     = #referen@#18;
     nTipo    = #referen@#20;
     aNTr     = #referen@#95;
     nCentro  = #referen@#99;

     nRepe = 0;
     |BUCLE BuscaRepe;
|FPRC;

|PROCESO ActRegDocumentos;
     ||METE REGISTROS.
     ||TODO CCC
     ||ARA
     nGrupoAlm = #referen@#1;
     nSubGrAlm = #referen@#2;
     nTipoAlm = #referen@#3;
     |SI nGrupoAlm = 0; |Y nSubGrAlm = 0; |Y nTipoAlm = 0;
          nGrupoAlm = nGrupoDefecto;
          nSubGrAlm = nSubGrDefecto;
          nTipoAlm = nTipoDefecto;
     |FINSI;

     |SELECT #2#dsarm005;
     #dsarm005#1 = nGrupoAlm;
     #dsarm005#2 = nSubGrAlm;
     #dsarm005#3 = nTipoAlm;
     #dsarm005#0 = 999999999;
     |LEE 000#dsarm005.];
     |SI FSalida = 0;
         |LEE 000#dsarm005.a;
     |SINO;
         |LEE 000#dsarm005.u;
     |FINSI;
     nDocuCarpeta = 1;
     |SI FSalida = 0;  |Y  #dsarm005#1 = nGrupoAlm;
                       |Y  #dsarm005#2 = nSubGrAlm;
                       |Y  #dsarm005#3 = nTipoAlm;
         nDocuCarpeta =  #dsarm005#47 + 1;
     |FINSI;

     |ABRE #dsarm002;
     #dsarm002#0 = nGrupoAlm;
     |LEE 101#dsarm002.=;
     |SI FSalida ! 0;
          |DDEFECTO #dsarm002;
          #dsarm002#0 = nGrupoAlm;
          |GRABA 020#dsarm002.b;
     |FINSI;
     #dsarm002#3 = #dsarm002#3 + 1;
     |GRABA 020#dsarm002.a;
     |LIBERA #dsarm002;
     |CIERRA #dsarm002;
     aAlfa = #dsarm002#2;
     |QBF aAlfa;
     |SI aAlfa ! "";
          aPathDestino = aUbicacionBase + aAlfa + "/";
     |SINO;
          aPathDestino = aUbicacionBase;
     |FINSI;

     |SI #dsarm001#7 = "N";
          |MKDIR aPathDestino;
     |SINO;
          |RMKDIR aPathDestino;
     |FINSI;

     |SI #dsarm001#8 = "N";

          |ABRE #dsarm004;
          #dsarm004#0 = nGrupoAlm;
          #dsarm004#1 = 1;
          #dsarm004#2 = 1;
          |LEE 000#dsarm004.=;
          |CIERRA #dsarm004;

          |SI #dsarm004#75 = "S";
               fFechaSis = #referen@#4;
               aFechaSis = fFechaSis;
               aAAAAMM = (aFechaSis % -104) + (aFechaSis % 402);
               aPathDestino = aPathDestino + aAAAAMM + "/";
               |SI #dsarm001#7 = "N";
                    |MKDIR aPathDestino;
               |SINO;
                    |RMKDIR aPathDestino;
               |FINSI;
          |FINSI;
     |FINSI;

     |SI #dsarm001#8 = "S";

          |ABRE #dsarm003;
          #dsarm003#0 = nGrupoAlm;
          #dsarm003#1 = nSubGrAlm;
          |LEE 000#dsarm003.=;
          |SI FSalida ! 0;
               |DDEFECTO #dsarm003;
               #dsarm003#0 = nGrupoAlm;
               #dsarm003#1 = nSubGrAlm;
               |GRABA 020#dsarm003.b;
          |FINSI;
          #dsarm003#4 = #dsarm003#4 + 1;
          |GRABA 020#dsarm003.a;
          |LIBERA #dsarm003;
          |CIERRA #dsarm003;

          aAlfa = #dsarm003#3;
          |QBF aAlfa;
          |SI aAlfa ! "";
               aPathDestino = aPathDestino + aAlfa + "/";
          |FINSI;
          |SI #dsarm001#7 = "N";
               |MKDIR aPathDestino;
          |SINO;
               |RMKDIR aPathDestino;
          |FINSI;

          |ABRE #dsarm004;
          #dsarm004#0 = nGrupoAlm;
          #dsarm004#1 = nSubGrAlm;
          #dsarm004#2 = nTipoAlm;
          |LEE 000#dsarm004.=;
          |SI FSalida ! 0;
               #dsarm004#0 = nGrupoAlm;
               #dsarm004#1 = nSubGrAlm;
               #dsarm004#2 = nTipoAlm;
               |GRABA 020#dsarm004.b;
               |DDEFECTO #dsarm004;
          |FINSI;
          #dsarm004#25 = #dsarm004#25 + 1;
          |GRABA 020#dsarm004.a;
          |LIBERA #dsarm004;
          |CIERRA #dsarm004;
          aAlfa = #dsarm004#4;
          |QBF aAlfa;
          |SI aAlfa ! "";
               aPathDestino = aPathDestino + aAlfa + "/";
          |FINSI;
          |SI #dsarm001#7 = "N";
               |MKDIR aPathDestino;
          |SINO;
               |RMKDIR aPathDestino;
          |FINSI;

          |SI #dsarm004#75 = "S";
               fFechaSis = #referen@#4;
               aFechaSis = fFechaSis;
               aAAAAMM = (aFechaSis % -104) + (aFechaSis % 402);
               aPathDestino = aPathDestino + aAAAAMM + "/";
               |SI #dsarm001#7 = "N";
                    |MKDIR aPathDestino;
               |SINO;
                    |RMKDIR aPathDestino;
               |FINSI;
          |FINSI;
     |FINSI;

     || COMENTADO CON MIGUEL ANGEL.
     || Si esta repetida la nomina, se borra, para luego volverla a crear.

     || Si es un fichero de todas las nominas de un centro o de una empresa
     || lo borrare despues con el swBorradoLaboral


     |SI enSwNomina = 1; |Y enSwNominaEmpCen = 0;
          |SI #dsarm001#183 = "N";
               |HAZ Duplicados;
          |FINSI;
     |FINSI;

     |SI enSwContab ! 0;
          |SI #dsarm001#226 = "N";
               |HAZ Duplicados;
          |FINSI;
     |FINSI;

     |SELECT #1#dsarm005;
     |DDEFECTO #dsarm005;
     |LEE 000#dsarm005.u;
     |SI FSalida = 0;
          nUltimo = #dsarm005#0 + 1;
     |SINO;
          nUltimo = 1;
     |FINSI;

     |ABRE #dsarm100;
     |DDEFECTO #dsarm100;
     |LEE 000#dsarm100.p;
     |SI FSalida = 0;
          nUltimoDoc = #dsarm100#1 + 1;
     |SINO;
          nUltimoDoc = 1;
     |FINSI;
     |SI nUltimoDoc > nUltimo;
          nUltimo = nUltimoDoc;
     |FINSI;
     #dsarm100#0 = "1";
     |LEE 101#dsarm100.=;
     |SI FSalida ! 0; |GRABA 020#dsarm100.b; |FINSI;
     #dsarm100#1 = nUltimo;
     |GRABA 020#dsarm100.a;
     |LIBERA #dsarm100;
     |CIERRA #dsarm100;

     |DDEFECTO #dsarm005;
     #dsarm005#0 = nUltimo;
     |LEE 101#dsarm005.=;
     |SI FSalida = 0;
          |LIBERA #dsarm005;
          nUltimo = nUltimo +1;
          |DDEFECTO #dsarm005;
          #dsarm005#0 = nUltimo;
          |LEE 101#dsarm005.=;
          |SI FSalida = 0;
               aMenLog = "ERROR: Problemas al bloquear Registro Documentos";
               |HAZ MeteLog;
               |LIBERA #dsarm005;
               nSwTodoCorrecto = 0;
               |ACABA;
          |FINSI;
     |FINSI;
     |GRABA 020#dsarm005.b;

     aOrigen = aDirRecDoc + #referen@#9;
     |QBF aOrigen;

     aExtension = #referen@#9;
     |QBF aExtension;
     aExtension = aExtension % -104;
     aAlfa1 = aExtension % 101;
     |SI aAlfa1 ! ".";
          aExtension = #referen@#9;
          |QBF aExtension;
          aExtension = aExtension % -105;
     |FINSI;

     aSinExtension = (("000000000") + nUltimo) % -109;
     |QBF aSinExtension;
     aNomDestino = aSinExtension + aExtension;
     aDestino = aPathDestino + aNomDestino;
     |QBF aDestino;

     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |SI #dsarm001#7 = "N";
          |COPIA_FICHERO aOrigen, aDestino;
          nSalidaHis = FSalida;
     |SINO;
          |SI aIPRemoto ! "";
               |ENVIA_FICHERO aOrigen, aDestino;
          |SINO;
               |COPIA_FICHERO aOrigen, aDestino;
          |FINSI;
          nSalidaHis = FSalida;
     |FINSI;

     ||Historico
     |HORA aHora;
     aUsuario = Usuario % 810;
     |ABRE #flowm006;
     #flowm006#0  = #dsarm005#0;
     #flowm006#1  = 99999;
     |LEE 000#flowm006.];
     |SI FSalida = 0;
          |LEE 000#flowm006.a;
     |SINO;
          |LEE 000#flowm006.u;
     |FINSI;
     |SI FSalida = 0; |Y #flowm006#0  = #dsarm005#0;
          nLinea = #flowm006#1 + 1;
          nSitu = #flowm006#2;
     |SINO;
          nSitu = 0;
          nLinea = 1;
     |FINSI;
     |DDEFECTO #flowm006;
     #flowm006#0  = #dsarm005#0;
     #flowm006#1  = nLinea;
     |LEE 101#flowm006.=;
     |SI FSalida ! 0; |GRABA 020#flowm006.b; |FINSI;
     #flowm006#2  = nSitu;
     #flowm006#3  = ~;
     #flowm006#4  = aHora;
     #flowm006#5  = aUsuario;
     #flowm006#6  = "RESULTADO ALTA [" + nSalidaHis + "]";

     enNumRegFlow = #flowm006#0;
     enSituFlow = #flowm006#2;
     |HAZ DimeDesFlow;
     #flowm006#7 = eaDesSituFlow;

     |GRABA 020#flowm006.a;
     |LIBERA #flowm006;
     |CIERRA #flowm006;

     |SI #dsarm001#7 = "N";
          |XABRE "E", aDestino, nHandle;
     |SINO;
          |XABRE "EC", aDestino, nHandle;
     |FINSI;

     |SI FSalida < 0;
          nSwTodoCorrecto = 0;

          |BORRA 020#dsarm005.a;
          |LIBERA #dsarm005;
          aMenLog = "ERROR: Problemas al archivar el documento: " + #referen@#93;
          |QBF aMenLog;
          |HAZ MeteLog;
          |ACABA;
     |SINO;
          |FBORRA aOrigen;
     |FINSI;

     #dsarm005#1 = nGrupoAlm;
     #dsarm005#2 = nSubGrAlm;
     #dsarm005#3 = nTipoAlm;
     |PARA nVoy = 4;  |SI nVoy [ nNumCampos;  |HACIENDO nVoy = nVoy + 1;
          #dsarm005#nVoy# = #referen@#nVoy#;
     |SIGUE;

     || lo de siempre
     #dsarm005#44 = ((#dsarm002#3  / #dsarm002#4)  + 0.48) ! 0;
     |SI #dsarm001#8 = "S";
          #dsarm005#45 = ((#dsarm003#4  / #dsarm003#5)  + 0.48) ! 0;
          #dsarm005#46 = ((#dsarm004#25 / #dsarm004#26) + 0.48) ! 0;
     |FINSI;

     #dsarm005#47 = nDocuCarpeta;
     #dsarm005#9 = aNomDestino;
     #dsarm005#7 = aPathDestino;

     |SI #dsarm005#44 = 0;  #dsarm005#44 = 1;  |FINSI;
     |SI #dsarm001#8 = "S";
          |SI #dsarm005#45 = 0;  #dsarm005#45 = 1;  |FINSI;
          |SI #dsarm005#46 = 0;  #dsarm005#46 = 1;  |FINSI;
     |FINSI;

     |GRABA 020#dsarm005.a;
     |LIBERA #dsarm005;

     |SI eaSwEnvioCorreo = "S";
          aAux = #dsarm005#78;
          |QBF aAux;
          |SI aAux ! "";
               aAux = eaTipoCorreo;
               |QBF aAux;
               |SI aAux = "";
                    eaTipoCorreo = "nominas";
               |FINSI;

               |SI eaTipoCorreo = "nominas";
                    ||En Nominas EMPRESA == Campo Cliente
                    enEmpCorreo = #dsarm005#8;
               |SINO;
                    enEmpCorreo = 0;
               |FINSI;

               enDocumento = #dsarm005#0;
               enErrorCorreo = 1;
               |SUB_EJECUTA_NP "dsarw010;AUTODESDED9";
          |FINSI;
     |FINSI;

     |SI enSwBorradoLaboral = 1;
          || para el envio por correo de nominas de laboral
          || cuando se crea un documento que es el resultado de la union de
          || otros (por ejemplo todas las nominas de la empresa en un solo
          || PDF). Eso hay que crearlo, que lo hago en local y luego enviarlo a
          || dsarchi para mandarlo por correo, tengo que archivar el pdf
          || creado, mandarlo Y LUEGO BORRARLO, de ahi esta parte
          |LEE 101#dsarm005.=;

          eaFechaEnvio = #dsarm005#79;
          eaHoraEnvio = #dsarm005#80;
          eaEmailEnvio = #dsarm005#78;
          |BORRA 020#dsarm005.a;
          |LIBERA #dsarm005;

          aFichero = #dsarm005#7;              |QBF aFichero;
          aFichero = aFichero + #dsarm005#9;   |QBF aFichero;

          |SI #dsarm001#7 = "N";
               |FBORRA aFichero;
          |SINO;
               |RFBORRA aFichero;
          |FINSI;

          |ACABA;
     |FINSI;

     ||Tema del OCR.
     aAlfa = "dsarz013;" + #dsarm005#0;
     |SUB_EJECUTA aAlfa;
     aAlfa = "dsarz030;" + #dsarm005#0;
     |SUB_EJECUTA aAlfa;
|FPRC;

|DEFBUCLE ActRegDocumentos;
 #referen@#1001;
 ;
 000000001;
 999999999;
 ;
 NULL, ActRegDocumentos;
|FIN;

|PROCESO Principal;
     ||TODO CCC
     enSwErrorRecDoc = 1;

     |QBF eaNomTemporal;
     |SI eaNomTemporal = "";
          aMenLog = "Inicio Recepcion Documentos. ERROR: Temporal intercambio vacio.";
          |HAZ MeteLog;
          |ACABA;
     |FINSI;

     ||Control de Bloqueos, cortes, etc ..... (dsapunte ????)
     ||No lo necesito, puesto que ya controlo cada proceso.
     ||Y devuelvo un error si algo falla. Ademas hay un log del proceso

     ||Descomprime el tgz.

     |DBASE aBase;
     |QBF aBase;

     aDirRecDoc = aBase + "rec_doc/";
     |MKDIR aDirRecDoc;

     aFicheroTGZ = aDirRecDoc + eaNomTemporal + ".tgz";
     |DESCOMPRIME aFicheroTGZ;
     |SI FSalida = 0;
          |FBORRA aFicheroTGZ;
          aFicheroTAR = aDirRecDoc + eaNomTemporal + ".tar";
          |DETAR aFicheroTAR, aDirRecDoc;
          |FBORRA aFicheroTAR;
     |FINSI;

     aMas = aBase + "def/dsarm005";
     |CARGA_DEF aMas, referen@;
     |SI FSalida ! 0;
          aMenLog = "ERROR CargaDef " + aMas;
          |HAZ MeteLog;
          |ACABA;
     |FINSI;

     |NOME_DAT #referen@#eaNomTemporal#;

     ||Almacena (DONDE ??? Grupo, SubGrupo, Tipo)
     nGrupoDefecto = 0;
     nSubGrDefecto = 0;
     nTipoDefecto = 0;
     |HAZ DondeAlmaceno;

     |ABRE #dsarm005;
     aQueQuiero = "|NC";
     aNumCampos = #dsarm005#aQueQuiero#;
     nNumCampos = aNumCampos;
     nNumCampos = nNumCampos - 1;

     |BUCLE ActRegDocumentos;
     ||Actualiza Registro Documentos
     |CIERRA #dsarm005;

     |DESCARGA_DEF #referen@;

     ||Borra los Temporales
|FPRC;

|PROGRAMA;
     |SUB_EJECUTA_NP "dsarz057";

     ||TODO CCC. Para pruebas mias.
     ||eaNomTemporal = "CARLOSC";

     |SI enSwNomina = 0;
          |CLS;
     |FINSI;

     |ABRE #dsarm001;
     |LEE 000#dsarm001.p;
     |SI FSalida ! 0;
          aMensaje = "0000Parametros NO definidos en DSARCHI";
          |MENAV aMensaje;
          |ACABA;
     |FINSI;

     aUbicacionBase = #dsarm001#1;
     |QBF aUbicacionBase;
     aUltLetra = aUbicacionBase % -101;
     |SI aUltLetra ! "/";
          aUbicacionBase = aUbicacionBase + "/";
     |FINSI;

     |SI #dsarm001#7 = "N";
          |MKDIR aUbicacionBase;
     |SINO;
          |RMKDIR aUbicacionBase;
     |FINSI;

     |DBASE aBase;
     |QBF aBase;
     aFicLog = aBase + "logs/";
     |MKDIR aFicLog;
     aFicLog = aFicLog + "rec_doc.log";

     eaPathLogVisor = aBase + "logs/";
     eaNomLogVisor = "rec_doc.log";
     eaFicLogVisor = aFicLog;
     eaDesLogVisor = "Logs Recepcion Documentos. (dsarz032)";
     |HAZ MeteVisorLog;

     aMenLog = "Inicio Recepcion Documentos. Temporal intercambio: " + eaNomTemporal;
     |HAZ MeteLog;

     nSwTodoCorrecto = 1;
     |HAZ Principal;
     |SI nSwTodoCorrecto = 1;
          enSwErrorRecDoc = 0;
     |FINSI;

     aMenLog = "Final Recepcion Documentos.  Temporal intercambio: " + eaNomTemporal;
     |HAZ MeteLog;

     |CIERRA #dsarm001;

     |SI enSwNomina = 0;
         |SUB_EJECUTA "dpntz001;CLI";
     |FINSI;

     eaSwEnvioCorreo = "N";

     |DBASE aAlfa;   |QBF aAlfa;
     aAlfa = aAlfa + "ocr/";
     aAlfa = aAlfa + "Envio" + Usuario + ".txt";
     |XABRE "E", aAlfa, 0;
     |SI FSalida ] 0;
         |SUB_EJECUTA "dsarw010;ENVIAAVISODIAGRAM";
     |FINSI;
|FPRO;
