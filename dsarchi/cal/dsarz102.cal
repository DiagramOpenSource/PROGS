|FICHEROS;
     dsarm001;
     dsarm002;
     dsarm003;
     dsarm004;
     dsarm005;
     dsarm072;
     dsarm100;

     dsarw047;
     dsarw123;
     dsarw124;

     :basica/agitab99;
|FIN;

|VARIABLES;
|FIN;

|INCLUYE z1000var;

|PROCESO CambiaRaros;
     aAlfa    = eaFicRes;   |QBF aAlfa;
     aLong    = aAlfa % 0;
     nLong    = aLong;
     aCadena  = "";
     nPuntos  = 0;
     |PARA nCarac = 1;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos   = (nCarac * 100) + 1;
           aCarac =  aAlfa % nPos;
           |SI aCarac = ".";
               nPuntos = nPuntos + 1;
           |FINSI;

           |SI aCarac = " ";
               aCarac = "";
           |FINSI;

           aCadena = aCadena + aCarac;
     |SIGUE;

     |SI nPuntos ] 2;
         aAlfa   = aCadena;
         aCadena = "";
         |PARA nCarac = 1;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
               nPos   = (nCarac * 100) + 1;
               aCarac =  aAlfa % nPos;

               |SI aCarac = ".";  |Y nPuntos > 1;
                   aCarac  =  "_";
                   nPuntos = nPuntos - 1;
               |FINSI;

               aCadena = aCadena + aCarac;
         |SIGUE;
     |FINSI;

     |SI aCadena ! eaFicRes;
         aOrigen  = aFichero;
         aDestino = eaRuta + "\" + aCadena;
         |RRENOMBRA_FICHERO aOrigen, aDestino;

         eaFicRes = aCadena;
         aFichero = aDestino;
     |FINSI;
|FPRC;

|PROCESO CheqFacturas;
     enChequeo = 0;
     |HAZ MiraSiC001;  |SI enChequeo = 1;  |ACABA;  |FINSI;
|FPRC;

|PROCESO ConvierteTxt;
     aEjecuta = aDirBaseLocal + "pdftotext/pdftotext.exe -f 1 -l 3 -layout " + aFichero + " " + aAbre + ".txt";
     |RSYSTEM aEjecuta;
|FPRC;

|PROCESO Graba047;
     |SI PARAMETRO = "2";
         |SI aCadena = "";  |ACABA;  |FINSI;
     |FINSI;

     nConta047   = nConta047 + 1;
     #dsarw047#0 = nConta047;
     #dsarw047#1 = aCadena;
     |GRABA 020#dsarw047.n;
     |LIBERA #dsarw047;
|FPRC;

|PROCESO Graba123Vacio;
     |DDEFECTO #dsarw123;

     enConta      = enConta + 1;
     #dsarw123#0  = enConta;
     #dsarw123#1  = eaFicRes;
     #dsarw123#4  = "Formato documento no reconocido";
     #dsarw123#8  = eaRojo;

     |GRABA 020#dsarw123.n;
     |LIBERA #dsarw123;
|FPRC;

|PROCESO ModoPDFTOTEXT;
     aAbre  = aAlfa - ".pdf";
     |HAZ ConvierteTxt;

     aAlfa  = aFichero < " ";
     aAbre  = aAlfa - ".pdf" + ".txt";

     |DBASE aPathBase;
     |QBF aPathBase;

     eaDestino = aAbre;
     eaOrigen  = aPathBase + "tmp";         |MKDIR eaOrigen;
     eaOrigen  = eaOrigen + "/" + Usuario;  |MKDIR eaOrigen;
     eaOrigen  = eaOrigen + "/tempo.txt";
     |HAZ RecibeEnServidorMD5;

     aAbre = eaOrigen;
     |XABRE "BU", aAbre, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     aDat = "dsarw047" + Usuario;
     |NOME_DAT #dsarw047#aDat#;

     |ABRE #dsarw047;  |CIERRA #dsarw047;  |DELINDEX  #dsarw047;

     eaJusti            = "";
     eaElect            = "";
     aCR                = &10;
     aLF                = &13;
     aCadena            = "";
     eaEjercicio        = "";
     eaPeriodo          = "";
     eaDni              = "";
     eaDni2             = "";
     eaTributa          = "";
     enCompl            = 0;
     nConta047          = 0;

     |ABRE #dsarw047;
     |XLEE nHandle, 1, aAlfa;
     |PARA; |SI FSalida > 0; |HACIENDO;
            |SI aAlfa = aCR;
                |HAZ Graba047;
                aCadena = "";
            |SINO;
                |SI aAlfa ! aLF;
                    aCadena = aCadena + aAlfa;
                |FINSI;
            |FINSI;
            |XLEE nHandle, 1, aAlfa;
     |SIGUE;
     |XCIERRA nHandle;

     |HAZ CheqFacturas;

     |CIERRA #dsarw047;

     |SI enChequeo = 0;
         |HAZ Graba123Vacio;
     |FINSI;

     |DELINDEX  #dsarw047;

     aAlfa = aPathBase + "tmp/" + Usuario + "/tempo.txt"; |FBORRA aAlfa;
|FPRC;

|PROCESO LeeFicheroPDF;
     aAbre = aFichero;

     |SI nLabel2 ! -1;
         |FIN_CONTROL_WINDOWS nLabel2;
     |FINSI;

     aAlfa = "LABEL,{{0}} " + eaFicRes;
     |CONTROL_SIMPLE 0, aAlfa,  1020, 1098, NULL;
     nLabel2 = FSalida;

     aAlfa  = aFichero < " ";
     aAbre  = aAlfa - ".pdf" + ".txt";
     |XABRE "EC", aAbre, nHandle;
     |SI FSalida ] 0;
         |RFBORRA aAbre;
     |FINSI;

     |HAZ ModoPDFTOTEXT;
|FPRC;

|PROCESO EstaIntegral;
     enSwEstaIntegral = 0;
     |DBASS aBase;
     aMas = aBase + "/integral/def/dsam0000.mas";
     |XABRE "E", aMas, nHandle;
     |SI FSalida ] 0;
         enSwEstaIntegral = 1;
     |FINSI;
|FPRC;

|PROCESO EstaBasica;
     enSwEstaBasica = 0;

     |DBASS aBase;
     aMas = aBase + "/basica/def/agifigen.mas";
     |XABRE "E", aMas, nHandle;
     |SI FSalida ] 0;
         enSwEstaBasica = 1;
     |FINSI;
|FPRC;

|PROCESO LeeDocumentos;
     aAlfa = "0000A continuaci¢n seleccione el directorio donde se encuentran los PDF a tratar.";
     |MENAV aAlfa;

     nLabel1 = -1;
     nLabel2 = -1;
     eaRuta  = "D ";
     |BROWSE eaRuta;

     aAlfa  = eaRuta;   |QBF aAlfa;
     eaRuta = "";
     aLong  = aAlfa % 0;
     nLong  = aLong;
     |PARA nCarac = 2;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos = (nCarac * 100) + 1;
           eaRuta = eaRuta + (aAlfa % nPos);
     |SIGUE;

     |SI eaRuta = "";
         |MENAV "0000No ha seleccionado un directorio.";
         enErrorMod = 1;
         |ACABA;
     |FINSI;

     |HAZ EstaIntegral;
     |HAZ EstaBasica;

     |CONTROL_SIMPLE 0, "LABEL,{{9}} Leyendo documentos", 0610, 0798, NULL;
     nLabel1 = FSalida;

     |SI #dsarm001#7 = "N";
          |HAZ DimeUnidad;
     |SINO;
          |HAZ LeeUnidadBasico;

          eaUnidad = eaUnidadBasico % 101;
     |FINSI;

     aDirBaseLocal = eaUnidad + ":\DOCUMENTOS";              |RMKDIR aDirBaseLocal;
     aDirBaseLocal = eaUnidad + ":\DOCUMENTOS\DSARCHI";      |RMKDIR aDirBaseLocal;
     aDirBaseLocal = eaUnidad + ":\DOCUMENTOS\DSARCHI\";

     enConta = 0;
     aFicMod = eaRuta + "\*.pdf";
     |R_PDIR aFicMod;
     |PARA; |SI; |HACIENDO;
         |SI FSalida ! 0; |SAL; |FINSI;

         eaFicRes = (aFicMod - eaRuta) > "";
         eaFicRes = eaFicRes % 2;
         aFichero = aFicMod;

         |HAZ CambiaRaros;

         |HAZ LeeFicheroPDF;
         |PULSATECLA;

         |R_SDIR aFicMod;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nLabel1;
     |FIN_CONTROL_WINDOWS nLabel2;
|FPRC;

|PROCESO dsarw123M;
     |SI aMarca = "*";
         |SI #dsarw123#3  = 0;
             |ACABA;
         |FINSI;
     |FINSI;

     #dsarw123#7 = aMarca;

     |HAZ PonColor;

     |GRABA 020#dsarw123.a;
     |LIBERA #dsarw123;
|FPRC;

|DEFBUCLE dsarw123M;
     #dsarw123#1;
     ;
           1;
     9999999;
     be;
     NULL, dsarw123M;
|FIN;

|PROCESO Todo;
     aMarca = "*";
     |BUCLE dsarw123M;

     |SELECT #1#dsarw123;

     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO Ninguno;
     aMarca = " ";
     |BUCLE dsarw123M;

     |SELECT #1#dsarw123;

     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO CambiarValores;
     |LEE 101#dsarw123.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |VENTANA 0501, 2599, -1, 16, "Cambiar valores";
     nSubVentz1 = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVentz1;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     #dsarw124#0  = #dsarw123#1;
     #dsarw124#1  = #dsarw123#2;
     #dsarw124#2  = #dsarw123#3;
     #dsarw124#3  = #dsarw123#4;
     #dsarw124#4  = #dsarw123#5;
     #dsarw124#5  = #dsarw123#6;

     |PINPA #0#dsarw124;
     |PINDA #0#dsarw124;
     |ENDATOS #1#dsarw124;
     |SI FSalida = 0;
         #dsarw123#1  = #dsarw124#0;
         #dsarw123#2  = #dsarw124#1;
         #dsarw123#3  = #dsarw124#2;
         #dsarw123#4  = #dsarw124#3;
         #dsarw123#5  = #dsarw124#4;

         |HAZ PonColor;

         |GRABA 020#dsarw123.a;
     |FINSI;

     |LIBERA #dsarw123;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVentz1;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nSubVentz1;

     |REFRESCACONTROL nGrid;
     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO NoReconocidos;
|FPRC;

|PROCESO Recomendaciones;
     |VENTANA 0501, 1970, -1, 16, "Recomendaciones";
     nSubVentz1 = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVentz1;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     aAlfa = "LABEL,{{0}}1.- Guardar siempre los documentos Seguridad Social en las mismas carpetas por periodo/ejercicio"
     |CONTROL_SIMPLE 0, aAlfa, 0602, 0670, NULL;

     aAlfa = "LABEL,{{0}}    para una mayor agilizaci¢n del proceso.";
     |CONTROL_SIMPLE 0, aAlfa, 0702, 0770, NULL;

     aAlfa = "LABEL,{{0}}3.- Contacte con nuestro departamento de soporte para incluir formatos de ";
     |CONTROL_SIMPLE 0, aAlfa, 0902, 0970, NULL;

     aAlfa = "LABEL,{{0}}    documentos no reconocidos. En cualquier caso podr  rellenar los datos a ";
     |CONTROL_SIMPLE 0, aAlfa, 1002, 1070, NULL;

     aAlfa = "LABEL,{{0}}    trav‚s del bot¢n " + &34 + "Cambiar Valores" + &34 + ".";
     |CONTROL_SIMPLE 0, aAlfa, 1102, 1170, NULL;

     |PAUSA;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVentz1;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nSubVentz1;
|FPRC;

|PROCESO Colores;
     |VENTANA 0501, 1270, -1, 16, "Ayuda colores registros";
     nSubVentz1 = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVentz1;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     aAlfa = "LABEL,{{15}}Rojo.-   Documentos con datos no reconocidos"
     |CONTROL_SIMPLE 0, aAlfa, 0602, 0670, NULL;

     aAlfa = "LABEL,{{15}}Verde.- Documentos ya existentes en la gesti¢n documental";
     |CONTROL_SIMPLE 0, aAlfa, 0702, 0770, NULL;

     aAlfa = "LABEL,{{15}}Negro.- Documentos preparados para seleccionar"
     |CONTROL_SIMPLE 0, aAlfa, 0802, 0870, NULL;

     aAlfa = "LABEL,{{15}}Azul.-   Documentos seleccionados para archivar"
     |CONTROL_SIMPLE 0, aAlfa, 0902, 0970, NULL;

     |PAUSA;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVentz1;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nSubVentz1;
|FPRC;

|PROCESO Evento123;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#dsarw123.=;
     |FINSI;

     |SI FSalida = 4;
         |LEE 101#dsarw123.=;
         |SI FSalida ! 0;  |ACABA;  |FINSI;

         |SI #dsarw123#3 = 0;
             |LIBERA #dsarw123;
             |ACABA;
         |FINSI;

         |SI #dsarw123#7 = " ";
             aAlfa = #dsarw123#8;  |QBF aAlfa;
             |SI aAlfa = eaVerde;
                 |CONFI "0000NEste documento ya existe en la gesti¢n documental, quiere registrarlo de nuevo";
                 |SI FSalida ! 0;
                     |ACABA;
                 |FINSI;
             |FINSI;

             #dsarw123#7 = "*";
         |SINO;
             #dsarw123#7 = " ";
         |FINSI;

         |HAZ PonColor;

         |GRABA 020#dsarw123.a;
         |LIBERA #dsarw123;

         |PINTA #dsarw123#6;

         FSalida = "SKIPDEFAULT";
         |REFRESCACONTROL nGrid;
     |FINSI;
|FPRC;

|PROCESO LinealSimple;
     nRango = 4 + 8 + 16 + 32;
     |LINEAL_SIMPLE #1#dsarw123, nRango, 0601, 2899, NULL, NULL, Evento123;
     nGrid = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::{{230}}Continuar," + nGrid;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Pantalla;
     |CONTROL_SIMPLE 0, "LABEL,{{2}}Doble click en la l¡nea para MARCAR o DESMARCAR el registro", 0502, 0598, NULL;
     nId1 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{137}}Marcar todo", 2902, 3015, Todo;
     nId2 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{193}}Desmarcar todo", 2917, 3032, Ninguno;
     nId3 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Cambiar valores", 2951, 3061, CambiarValores;
     nId5 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Ver documento PDF", 2963, 3074, VerDocu;
     nId6 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,Ver documento TXT", 2976, 3086, VerDocuTXT;
     nId7 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Recomendaciones", 3202, 3220, Recomendaciones;
     nId8 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Ayuda colores registros", 3224, 3244, Colores;

     |PARA;  |SI;  |HACIENDO;
          |HAZ LinealSimple;
          |CONFI "0000SConforme con lo seleccionado";
          |SI FSalida = 0;  |SAL;  |FINSI;
     |SIGUE;
|FPRC;

|PROCESO SacaUbicacion;
     aPath = #dsarm001#1;  |QBF aPath;

     |DDEFECTO #dsarm002;
     #dsarm002#0 = enGrupoTres;
     |LEE 000#dsarm002.=;

     aPath = aPath + "/" + #dsarm002#2;  |QBF aPath;
     |SI #dsarm001#7 = "N";
         |MKDIR aPath;
     |SINO;
         |RMKDIR aPath;
     |FINSI;

     |SI #dsarm001#8 = "N";
         aPath = aPath + "/";
         aUbicacion = aPath;

         |DDEFECTO #dsarm004;
         #dsarm004#0 = enGrupoTres;
         #dsarm004#1 = 1;
         #dsarm004#2 = 1;
         |LEE 000#dsarm004.=;

         |SI #dsarm004#75 = "S";
              fFechaSis = ~;
              aFechaSis = fFechaSis;
              aAAAAMM = (aFechaSis % -104) + (aFechaSis % 402);
              aUbicacion = aUbicacion + aAAAAMM + "/";
              |SI #dsarm001#7 = "N";
                   |MKDIR aUbicacion;
              |SINO;
                   |RMKDIR aUbicacion;
              |FINSI;
         |FINSI;

         |ACABA;
     |FINSI;

     |DDEFECTO #dsarm003;
     #dsarm003#0 = enGrupoTres;
     #dsarm003#1 = enSubGTres;
     |LEE 000#dsarm003.=;

     aPath = aPath + "/" + #dsarm003#3;  |QBF aPath;
     |SI #dsarm001#7 = "N";
         |MKDIR aPath;
     |SINO;
         |RMKDIR aPath;
     |FINSI;

     |DDEFECTO #dsarm004;
     #dsarm004#0 = enGrupoTres;
     #dsarm004#1 = enSubGTres;
     #dsarm004#2 = enTipoTres;
     |LEE 000#dsarm004.=;

     aPath = aPath + "/" + #dsarm004#4;  |QBF aPath;
     |SI #dsarm001#7 = "N";
         |MKDIR aPath;
     |SINO;
         |RMKDIR aPath;
     |FINSI;

     aPath = aPath + "/";
     aUbicacion = aPath;

     |SI #dsarm004#75 = "S";
          fFechaSis = ~;
          aFechaSis = fFechaSis;
          aAAAAMM = (aFechaSis % -104) + (aFechaSis % 402);
          aUbicacion = aUbicacion + aAAAAMM + "/";
          |SI #dsarm001#7 = "N";
               |MKDIR aUbicacion;
          |SINO;
               |RMKDIR aUbicacion;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO dsarw123G;
     nDocu      = 1;
     |LEE 000#dsarm005.u;
     |SI FSalida = 0;
          nDocu = #dsarm005#0 + 1;
     |FINSI;

     |DDEFECTO #dsarm100;
     |LEE 000#dsarm100.p;
     |SI FSalida = 0;
          nUltimoDoc = #dsarm100#1 + 1;
     |SINO;
          nUltimoDoc = 1;
     |FINSI;
     |SI nUltimoDoc > nDocu;
          nDocu = nUltimoDoc;
     |FINSI;
     #dsarm100#0 = "1";
     |LEE 101#dsarm100.=;
     |SI FSalida ! 0; |GRABA 020#dsarm100.b; |FINSI;
     #dsarm100#1 = nDocu;
     |GRABA 020#dsarm100.a;
     |LIBERA #dsarm100;

     |DDEFECTO #dsarm005;
     #dsarm005#0   = nDocu;
     |GRABA 020#dsarm005.b;

     aAlfa  = #dsarw123#1;  |QBF aAlfa;
     aAlfa  = (aAlfa % -104) > "";
     aAlfa1 = aAlfa % 101;
     |SI aAlfa1 ! ".";
         aAlfa  = #dsarw123#1;  |QBF aAlfa;
         aAlfa  = (aAlfa % -105) > "";
     |FINSI;
     aFicModelo = (("000000000" + nDocu) % -109) + aAlfa;
     |QBT aFicModelo;

     aUbicacion = "";
     |HAZ SacaUbicacion;

     eaDestino      = eaRuta + "/" + #dsarw123#1;  |QBF eaDestino;
     eaOrigen       = aUbicacion + aFicModelo;
     eaDocPC        = eaDestino;
     enSwIOCorrecta = 0;
     |HAZ RecibeConMD5;
     |SI enSwIOCorrecta = 0;
         |MENAV "0000 Ha habido un error al transferir el documento al servidor. Revise Permisos y espacio disponible.";
         |BORRA 020#dsarm005.a;
         |LIBERA #dsarm005;
         |ACABA;
     |FINSI;

     |SI eaSwBorroOrigen = "S";
          aAlfa = eaRuta + "/" + #dsarw123#1;  |QBF aAlfa;
          aAlfa = aAlfa < " ";
          aAlfa = aAlfa - ".pdf" + ".txt";
          |RFBORRA aAlfa;
     |FINSI;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |SI #dsarm001#7 = "N";  |Y aIPRemoto ! "";
         |XABRE "E", eaOrigen, nHandle;
     |SINO;
         |XABRE "EC", eaOrigen, nHandle;
     |FINSI;

     |SI FSalida < 0;
         |MENAV "0000 Ha habido un error al transferir el documento al servidor. Intentelo de nuevo";
         |BORRA 020#dsarm005.a;
         |LIBERA #dsarm005;
         |ACABA;
     |FINSI;

     #dsarm005#1 = #dsarm004#0;
     #dsarm005#2 = #dsarm004#1;
     #dsarm005#3 = #dsarm004#2;

     #dsarm005#4 = ~;
     |HORA aHora;
     aHora = aHora % 105;
     #dsarm005#5 = aHora;
     aUsuario = Usuario % 810;
     #dsarm005#6  = aUsuario;
     #dsarm005#7  = aUbicacion;
     #dsarm005#9  = aFicModelo;

     #dsarm005#8  = #dsarw123#3;
     #dsarm005#10 = #dsarw123#2;
     #dsarm005#14 = #dsarw123#4;
     #dsarm005#38 = #dsarw123#4;
     #dsarm005#11 = "Factura: " + #dsarw123#6;
     #dsarm005#12 = "Fecha factura: " + #dsarw123#5;

     aAux = #dsarm004#5;   |QBF aAux;  |SI aAux ! "";  #dsarm005#23 = aAux + ":";  |FINSI;
     aAux = #dsarm004#6;   |QBF aAux;  |SI aAux ! "";  #dsarm005#24 = aAux + ":";  |FINSI;
     aAux = #dsarm004#7;   |QBF aAux;  |SI aAux ! "";  #dsarm005#25 = aAux + ":";  |FINSI;
     aAux = #dsarm004#8;   |QBF aAux;  |SI aAux ! "";  #dsarm005#26 = aAux + ":";  |FINSI;
     aAux = #dsarm004#9;   |QBF aAux;  |SI aAux ! "";  #dsarm005#27 = aAux + ":";  |FINSI;
     aAux = #dsarm004#27;  |QBF aAux;  |SI aAux ! "";  #dsarm005#28 = aAux + ":";  |FINSI;
     aAux = #dsarm004#28;  |QBF aAux;  |SI aAux ! "";  #dsarm005#29 = aAux + ":";  |FINSI;
     aAux = #dsarm004#29;  |QBF aAux;  |SI aAux ! "";  #dsarm005#30 = aAux + ":";  |FINSI;
     aAux = #dsarm004#30;  |QBF aAux;  |SI aAux ! "";  #dsarm005#31 = aAux + ":";  |FINSI;
     aAux = #dsarm004#31;  |QBF aAux;  |SI aAux ! "";  #dsarm005#32 = aAux + ":";  |FINSI;
     aAux = #dsarm004#10;  |QBF aAux;  |SI aAux ! "";  #dsarm005#33 = aAux + ":";  |FINSI;
     aAux = #dsarm004#11;  |QBF aAux;  |SI aAux ! "";  #dsarm005#34 = aAux + ":";  |FINSI;
     aAux = #dsarm004#12;  |QBF aAux;  |SI aAux ! "";  #dsarm005#35 = aAux + ":";  |FINSI;
     aAux = #dsarm004#13;  |QBF aAux;  |SI aAux ! "";  #dsarm005#36 = aAux + ":";  |FINSI;
     aAux = #dsarm004#14;  |QBF aAux;  |SI aAux ! "";  #dsarm005#37 = aAux + ":";  |FINSI;
     aAux = #dsarm004#15;  |QBF aAux;  |SI aAux ! "";  #dsarm005#60 = aAux + ":";  |FINSI;
     aAux = #dsarm004#16;  |QBF aAux;  |SI aAux ! "";  #dsarm005#61 = aAux + ":";  |FINSI;
     aAux = #dsarm004#17;  |QBF aAux;  |SI aAux ! "";  #dsarm005#62 = aAux + ":";  |FINSI;
     aAux = #dsarm004#18;  |QBF aAux;  |SI aAux ! "";  #dsarm005#63 = aAux + ":";  |FINSI;
     aAux = #dsarm004#19;  |QBF aAux;  |SI aAux ! "";  #dsarm005#64 = aAux + ":";  |FINSI;

     #dsarm005#93 = #dsarw123#1;

     |GRABA 020#dsarm005.a;
     |LIBERA #dsarm005;

     aAlfa = "dsarz013;" + #dsarm005#0;
     |SUB_EJECUTA aAlfa;

     aAlfa = "dsarz030;" + #dsarm005#0;
     |SUB_EJECUTA aAlfa;
|FPRC;

|DEFBUCLE dsarw123G;
     #dsarw123#2;
     ;
     "*",       1;
     "*", 9999999;
     be;
     NULL, dsarw123G;
|FIN;

|PROGRAMA;
     |HAZ Traete_pdftotext;
     |SI enError = 1;  |ACABA;  |FINSI;

     |MENAV "0000Seleccione la carpeta donde archivar las facturas.";

     enGrupoTres = 4;
     enSubGTres  = 4;
     enTipoTres  = 1;
     |SUB_EJECUTA_NP ":dsarchi/dsarz003;DAMETRE";
     |SI enGrupoTres = 0;  |ACABA;  |FINSI;

     aAlfa = "Capturar documentos Seguridad Social";
     |VENTANA 0501, 3299, -1, 16, aAlfa;
     nVentz1  = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentz1;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;
     |PAUSA;

     |HAZ LeeDocumentos;

     |LEE 000#dsarw123.p;
     |SI FSalida = 0;
         |HAZ Pantalla;
     |SINO;
         |MENAV "0000Carpeta sin documentos a archivar.";
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentz1;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVentz1;

     |SELECT #2#dsarw123;
     |LEE 000#dsarw123.p;
     |SI FSalida ! 0;  |O #dsarw123#7 = " ";
         |MENAV "0000Seleccion vac¡a";

         |ACABA;
     |FINSI;

     aAlfa = "Registrado documentos";
     |VENTANA 0501, 0540, -1, 16, aAlfa;
     nVentz1  = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentz1;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;
     |PAUSA;

     |HAZ CargaVarBorrado;

     |BUCLE dsarw123G;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentz1;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVentz1;
|FPRO;

|PROCESO T80;  |TIPO 80;
     |ABRE #dsarm001;
     |DDEFECTO #dsarm001;
     |LEE 000#dsarm001.p;
     |CIERRA #dsarm001;

     eaVerde = "0,185,0;255,255,255";
     eaAzul  = "18,28,245;255,255,255";
     eaRojo  = "255,80,80;255,255,255";

     aDat = "dsarw123" + Usuario;
     |NOME_DAT #dsarw123#aDat#;

     |ABRE #dsarw123;  |CIERRA #dsarw123;  |DELINDEX  #dsarw123;

     |ABRET #dsarw123;
     |ABRET #dsarm072;
     |ABRET #dsarm002;
     |ABRET #dsarm003;
     |ABRET #dsarm004;
     |ABRET #dsarm005;
     |ABRET #dsarm100;
|FPRC;

|PROCESO T90;  |TIPO 90;
     |CIERRAT #dsarm002;
     |CIERRAT #dsarm003;
     |CIERRAT #dsarm004;
     |CIERRAT #dsarm005;
     |CIERRAT #dsarm100;
     |CIERRAT #dsarm072;

     |CIERRAT #dsarw123;
     |DELINDEX #dsarw123;
|FPRC;
