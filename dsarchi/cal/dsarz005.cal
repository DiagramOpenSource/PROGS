|FICHEROS;
     dsarm005 #0;        ||Registro documentos

     dsarz005 #99;
     dsarm001 #1;        ||Parametros;
     dsarm002 #2;        ||Grupos
     dsarm003 #3;        ||Subgrupos
     dsarm004 #4;        ||Tipos
     dsarm006 #6;        ||Entrada/Salidas Doc.
     dsarm007 #7;        ||Asociacion Documentos

     reflejos@ #1005;

     flowm006 #506;

     dsarm008 #8;          ||Vistas Arbol
     dsarm000 #100;        ||Usuarios
     dsarm043 #43;         ||Permisos Vistas Usu.
     dsarm044 #44;         ||Permisos VistasTipos
     dsarm059;             ||Permisos VistasTipos

     dsarw111;

     dpntm001 #801;

     :/integral/def/dsam0103 #103;

     dsarm005@ #905;
     trabajo@;
|FIN;

|VARIABLES;
     &enSwFiltroAsociar = 0;       ||Asociar Avanzado
     &eaNomFicFiltroAso = "";

     nBtnAsocTodos   = 0;
     nBtnAsocPatron  = 0;

     aAuxFiltro = "";
     &enSwNoHazVistas = 0;

     ||Pedir Tipo al Asociar
     nSwPideTipo = 0;
     &enGrupoTres    = 0;
     &enSubGTres     = 0;
     &enTipoTres     = 0;
     aMostrarAso = "";
     nPideGrupo = 0;
     nPideSGrupo = 0;
     nPideTipo = 0;
     aEjecuta = "";
     aLabel = "";
     nBotonCon = 0;
     nBotonSin = 0;
     nBotonTodos = 0;
     nBotonAcota = 0;
     nSwBotonAcota = 0;
     &eaFicAso = "";
     &enSwModoAsociar = 0;

     ||Arbol Asociar
     nBoton0_8 = 0;
     nBoton0_9 = 0;
     nBoton0_10 = 0;
     nBoton0_11 = 0;

     ||ASO
     aTipoVista = "";
     aNomVista = "";
     &enBusqueda = 0;
     nSwArbolActivo = 0;
     nEstadoGrid = 0;

     aCodigo9         = "";
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     aAlfa4 = "";
     aAlfa5 = "";
     aBeta1 = "";
     aBeta2 = "";
     aBeta3 = "";
     aBeta4 = "";
     aBeta5 = "";
     nEntroPDatos = 0;
     aTexto = "";
     nCargar = 0;
     nPosi = 0;
     aRefresca = "";
     aBusca = "";
     aUsuarioVista = "";
     nOpcion = 0;
     nNumCamp = 0;
     aDetalle = "";
     aBeta = "";
     aAux = "";
     {200}aPopup      = "";
     {200}aOpcion     = "";

     ||Orden en ultimo nivel Arbol
     aOrden = "";
     nOrden = 0;
     nSwCampoNum = 0;
     nCampoOrden = 0;
     nSwCampoFecha = 0;
     aAuxOrden = "";
     aIniOrden = "";
     aFechaUno = "";
     fFechaUno = @;
     nFechaUno = 0;
     fFechaDos = @;
     aDia = "";
     aMes = "";
     aAnyo = "";
     aPilla = "";

     nError = 0;

     &enNumRegFlow = 0;
     &enSituFlow = 0;
     &eaDesSituFlow = "";

     nLinea = 0;
     nSitu = 0;
     aOriHis = "";
     aDesHis = "";
     aUsuario = "";
     aAsociado = "";

     &enNoClonarFlow = 0;

     aMensaje = "";
     nCampoD        = 0;
     nCampoH        = 0;
     nGrid1         = 0;
     nPanta0        = 0;
     nVentana       = 0;
     nVentz5        = 0;
     nVent          = -1;
     nPos1          = 0;
     nPos2          = 0;
     nRango         = 0;
     nBoton0_2      = 0;
     nBoton0_4      = 0;
     nBoton0_5      = 0;
     nPosDRamas     = 0;
     nPosHRamas     = 0;
     nPosDGrid      = 0;
     nPosHGrid      = 0;
     nGridTree      = -1;
     nBtnGuarda     = -1;
     nDCli          = 0;
     nHCli          = 0;
     nDTra          = 0;
     nHTra          = 0;
     nDMod          = 0;
     nHMod          = 0;
     nDPer          = 0;
     nHPer          = 0;
     nDEje          = 0;
     nHEje          = 0;

     aEsc1          = "";
     aEsc2          = "";
     aRetu          = "";
     aTecla         = "";
     aAlfa          = "";
     aQueQuiero     = "";
     aContenido     = "";

     aOrigen        = "";
     aDestino       = "";
     aIPRemoto      = "";
     aBase          = "";
     aMas           = "";
     aHora          = "";

     {-1}aVent      = "";
         aVent1     = 0;
         aVent2     = 0;
         aVent3     = 0;
         aVent4     = 0;
         aVent5     = "";

     &enModo        = 0;
     &eaClave       = "";
     &enPassword    = 0;
     &enDocumento   = 0;
     &enDocuPpal    = 0;
     &eaFichero     = "";
     &enError       = 0;
     &enGrupo       = 0;
     &enSubGrupo    = 0;
     &enTipo        = 0;
|FIN;

|INCLUYE incvista;

|PROCESO EnviaUno;
|FPRC;

|PROCESO HistoricoAsociar;
     ||Historico
     |HORA aHora;
     aUsuario = Usuario % 810;
     |ABRE #flowm006;
     #flowm006#0  = #7#0;
     #flowm006#1  = 99999;
     |LEE 000#flowm006.];
     |SI FSalida = 0;
          |LEE 000#flowm006.a;
     |SINO;
          |LEE 000#flowm006.u;
     |FINSI;
     |SI FSalida = 0; |Y #flowm006#0  = #7#0;
          nLinea = #flowm006#1 + 1;
          nSitu = #flowm006#2;
     |SINO;
          nSitu = 0;
          nLinea = 1;
     |FINSI;
     |DDEFECTO #flowm006;
     #flowm006#0  = #7#0;
     #flowm006#1  = nLinea;
     |LEE 101#flowm006.=;
     |SI FSalida ! 0; |GRABA 020#flowm006.b; |FINSI;
     #flowm006#2  = nSitu;
     #flowm006#3  = ~;
     #flowm006#4  = aHora;
     #flowm006#5  = aUsuario;
     aAsociado = ("000000000" + #7#1) % -109;
     #flowm006#6  = "ASOCIADO CON " + aAsociado;

     enNumRegFlow = #flowm006#0;
     enSituFlow = #flowm006#2;
     |HAZ DimeDesFlow;
     #flowm006#7 = eaDesSituFlow;

     |GRABA 020#flowm006.a;
     |LIBERA #flowm006;
     |CIERRA #flowm006;
|FPRC;

|PROCESO Asociacion;
     #7#0 = enDocuPpal;
     #7#1 = #0#0;
     |LEE 101#7=;
     |SI FSalida ! 0;
         #7#0 = enDocuPpal;
         #7#1 = #0#0;
         #7#2 = ~;
         #7#3 = Usuario % 810;
         |GRABA 020#7n;
         |LIBERA #7;
     |FINSI;

     |HAZ HistoricoAsociar;

     #7#0 = #0#0;
     #7#1 = enDocuPpal;
     |LEE 101#7=;
     |SI FSalida ! 0;
         #7#0 = #0#0;
         #7#1 = enDocuPpal;
         #7#2 = ~;
         #7#3 = Usuario % 810;
         |GRABA 020#7n;
         |LIBERA #7;
     |FINSI;

     |HAZ HistoricoAsociar;
|FPRC;

|| ------------------------------------------------------------------------
|| PROCESOS DE ASIGNACION DE MENUS
|| ------------------------------------------------------------------------

|PROCESO Baja905;
     #905#65 = " ";
     #905#0  = 1;
|FPRC;

|PROCESO Alta905;
     #905#65 = " ";
     #905#0  = 999999999;
|FPRC;

|PROCESO BajaPanta0_1;
     |SELECT #1#0;
     #0#65 = " ";
     #0#0  = 1;
|FPRC;

|PROCESO AltaPanta0_1;
     #0#65 = " ";
     #0#0  = 999999999;
|FPRC;

|PROCESO BajaPanta0_2;
     |SELECT #2#0;
     #0#65 = " ";
     #0#1  = enGrupo;
     #0#2  = enSubGrupo;
     #0#3  = enTipo;
     #0#0  = 1;
|FPRC;

|PROCESO AltaPanta0_2;
     #0#65 = " ";
     #0#1  = enGrupo;
     #0#2  = enSubGrupo;
     #0#3  = enTipo;
     #0#0  = 999999999;
|FPRC;

|PROCESO EventoPanta1;
     |SI #0#0 = 0;  |ACABA;  |FINSI;

     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#0=;
         |SI FSalida ! 0;  |DDEFECTO #0;  |FINSI;

         aFichero = #0#7;              |QBF aFichero;
         aFichero = aFichero + #0#9;   |QBF aFichero;

         |SI aFichero ! "";
             |ESTADO_CONTROL nBoton0_2, "ENABLE";
             |ESTADO_CONTROL nBoton0_4, "ENABLE";
         |SINO;
             |ESTADO_CONTROL nBoton0_2, "DISABLE";
             |ESTADO_CONTROL nBoton0_4, "DISABLE";
         |FINSI;
     |FINSI;

     |SI FSalida = 4;
         |SI #0#0 = enDocuPpal;
             |MENAV "0000 No puedo asociar el mismo documento.";
             |ACABA;
         |FINSI;

         |CONFI "0000SEsta seguro que quiere asociar el documento seleccionado";
         |SI FSalida ! 0;
             |ACABA;
         |FINSI;

         |LEE 101#0=;
         |SI FSalida = 0;
             |HAZ Asociacion;

             enSwNoHazVistas = 1;
             |BORRA 020#0a;
             |LIBERA #0;
             enSwNoHazVistas = 0;

             |REFRESCACONTROL nGrid1;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO GridFicheroEntero;
     |SI #1#8 = "N";
         |C_V #0#2,  1, "N";
         |C_V #0#3,  1, "N";
         |C_V #0#45, 1, "N";
         |C_V #0#46, 1, "N";
     |FINSI;

     |ABRE #0;
     |SI aParametro = "";
         |LINEAL_SIMPLE #1#0, nRango, nPos1, nPos2, BajaPanta0_1, AltaPanta0_1, EventoPanta1;
     |SINO;
         |LINEAL_SIMPLE #2#0, nRango, nPos1, nPos2, BajaPanta0_2, AltaPanta0_2, EventoPanta1;
     |FINSI;
     nGrid1   = FSalida;
     nEstadoGrid = nGrid1;
     nFichero = 0;

     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;

     |FOCOTECLADO nGrid1;
|FPRC;

|PROCESO EventoPanta905;
     |SI #905#0 = 0;  |ACABA;  |FINSI;

     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#0=;
         |SI FSalida ! 0;  |DDEFECTO #0;  |FINSI;

         aFichero = #905#7;              |QBF aFichero;
         aFichero = aFichero + #905#9;   |QBF aFichero;

         |SI aFichero ! "";
             |ESTADO_CONTROL nBoton0_2, "ENABLE";
             |ESTADO_CONTROL nBoton0_4, "ENABLE";
         |SINO;
             |ESTADO_CONTROL nBoton0_2, "DISABLE";
             |ESTADO_CONTROL nBoton0_4, "DISABLE";
         |FINSI;
     |FINSI;

     |SI FSalida = 4;
         |SI #905#0 = enDocuPpal;
             |MENAV "0000 No puedo asociar el mismo documento.";
             |ACABA;
         |FINSI;

         |CONFI "0000SEsta seguro que quiere asociar el documento seleccionado.";
         |SI FSalida ! 0;
             |ACABA;
         |FINSI;

         |LEE 101#905=;
         |SI FSalida = 0;
             #0#0  = #905#0;
             #0#1  = #905#1;
             #0#2  = #905#2;
             #0#3  = #905#3;
             #0#65 = #905#65;
             |LEE 101#0=;
             |SI FSalida = 0;
                 |HAZ Asociacion;

                 enSwNoHazVistas = 1;
                 |BORRA 020#0a;
                 |BORRA 020#905a;
                 enSwNoHazVistas = 0;

                 |REFRESCACONTROL nGrid1;
             |SINO;
                 |PARA nCampo = 0;  |SI nCampo [ enNumCampos;  |HACIENDO nCampo = nCampo + 1;
                       #0nCampo = #905nCampo;
                 |SIGUE;

                 |HAZ Asociacion;

                 enSwNoHazVistas = 1;
                 |BORRA 020#905a;
                 enSwNoHazVistas = 0;

                 |REFRESCACONTROL nGrid1;
             |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO GridFicheroBusqueda;
     |ABRE #905;
     |LEE 000#905p;

     |LINEAL_SIMPLE #6#905, nRango, nPos1, nPos2, NULL, NULL, EventoPanta905;
     nGrid1   = FSalida;
     nEstadoGrid = nGrid1;
     nFichero = 905;

     |FOCOTECLADO nGrid1;
     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Inicio del Programa
|| ------------------------------------------------------

|PROCESO FinVentanaz5;
     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentz5;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVentz5;
|FPRC;

|PROCESO Boton0_2;
     |SI nFichero = 0;
         enDocumento = #0#0;
     |SINO;
         enDocumento = #905#0;
     |FINSI;

     |VENTANA 0501, 2999, -1, 16, "Datos Documentos";
     nVentz5 = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentz5;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |SUB_EJECUTA_NP "dsarw006";

     |FINVENTANA nVentz5;
|FPRC;

|PROCESO Boton0_4;
     |CIERRA #0;
     |CIERRA #905;


     |VENTANA 0501, 2699, -1, 16, "Busqueda de Documentos";
     nVentz5 = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentz5;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |SI nSwPideTipo = 1;
          enSwModoAsociar = 1;
     |FINSI;

     aParametro = "";
     enError    = 0;
     |SUB_EJECUTA_NP "dsarz002";

     |FINVENTANA nVentz5;

     |SI enError = 1;
          |ABRE #0;
          |ABRE #905;
          |ACABA;
     |FINSI;

     |DESTRUYECONTROL nGrid1;   |PULSATECLA;
     |HAZ GridFicheroBusqueda;

     enBusqueda = 1;
     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;

     |SI nSwArbolActivo = 1;       ||Si el arbol esta activo, lo actualizo
          |ESTADO_CONTROL nGrid1, "HIDE";
          nEstadoGrid = -1;
          |HAZ Boton0_8;
     |FINSI;

     |ESTADO_CONTROL nBoton0_5, "ENABLE";

     |SI nSwPideTipo = 1;
          enSwModoAsociar = 0;
     |FINSI;
|FPRC;

|PROCESO Boton0_5;
     |CIERRA #905;  |DELINDEX #905;

     |SI nSwPideTipo = 1;
          |PINPA #0#99;  nPanta0 = FSalida;
          |HAZ NucleoReflejos;

          ||Recalculo y el refresco del grid o el arbol segun corresponda
          |DESTRUYECONTROL nGrid1;   |PULSATECLA;
          |HAZ GridFicheroEntero;
          enBusqueda = 1;
          |PULSATECLA;
          |PULSATECLA;
          |PULSATECLA;
          |SI nSwArbolActivo = 1;       ||Si el arbol esta activo, lo actualizo
               |ESTADO_CONTROL nGrid1, "HIDE";
               nEstadoGrid = -1;
               |HAZ Boton0_8;
          |FINSI;

          |PINPA #0#99;  nPanta0 = FSalida;
     |SINO;

          |DESTRUYECONTROL nGrid1;

          enBusqueda = 1;
          |HAZ GridFicheroEntero;

          |PULSATECLA;
          |PULSATECLA;
          |PULSATECLA;
          |SI nSwArbolActivo = 1;
               |ESTADO_CONTROL nGrid1, "HIDE";
               nEstadoGrid = -1;
               |HAZ Boton0_8;
          |FINSI;
     |FINSI;

     |ESTADO_CONTROL nBoton0_5, "DISABLE";
|FPRC;

|PROCESO BotonAcota;
     nSwBotonAcota = 1;
     |HAZ AcotarPorTipos;
     nSwBotonAcota = 0;

     |HAZ NucleoReflejos;

     ||Recalculo y el refresco del grid o el arbol segun corresponda
     |DESTRUYECONTROL nGrid1;   |PULSATECLA;
     |HAZ GridFicheroEntero;
     enBusqueda = 1;
     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
     |SI nSwArbolActivo = 1;       ||Si el arbol esta activo, lo actualizo
          |ESTADO_CONTROL nGrid1, "HIDE";
          nEstadoGrid = -1;
          |HAZ Boton0_8;
     |FINSI;

     |PINPA #0#99;  nPanta0 = FSalida;
     |ESTADO_CONTROL nBoton0_4, "ENABLE";
     |ESTADO_CONTROL nBoton0_5, "DISABLE";
|FPRC;

|PROCESO BotonSin;
     aMostrarAso = "S";
     |ABRE #100;
     #100#0 = Usuario % 810;
     |LEE 101#100=;
     |SI FSalida ! 0;
         |DDEFECTO #100;
         #100#0 = Usuario % 810;
         |GRABA 020#100b;
     |FINSI;
     #100#26 = aMostrarAso;
     |GRABA 020#100a;
     |LIBERA #100;
     |CIERRA #100;

     |HAZ NucleoReflejos;
     ||Recalculo y el refresco del grid o el arbol segun corresponda
     |DESTRUYECONTROL nGrid1;   |PULSATECLA;
     |HAZ GridFicheroEntero;
     enBusqueda = 1;
     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
     |SI nSwArbolActivo = 1;       ||Si el arbol esta activo, lo actualizo
          |ESTADO_CONTROL nGrid1, "HIDE";
          nEstadoGrid = -1;
          |HAZ Boton0_8;
     |FINSI;

     |ESTADO_CONTROL nBotonTodos, "ENABLE";
     |ESTADO_CONTROL nBotonSin, "DISABLE";
     |ESTADO_CONTROL nBotonCon, "ENABLE";
|FPRC;

|PROCESO BotonCon;
     aMostrarAso = "N";
     |ABRE #100;
     #100#0 = Usuario % 810;
     |LEE 101#100=;
     |SI FSalida ! 0;
         |DDEFECTO #100;
         #100#0 = Usuario % 810;
         |GRABA 020#100b;
     |FINSI;
     #100#26 = aMostrarAso;
     |GRABA 020#100a;
     |LIBERA #100;
     |CIERRA #100;

     |HAZ NucleoReflejos;
     ||Recalculo y el refresco del grid o el arbol segun corresponda
     |DESTRUYECONTROL nGrid1;   |PULSATECLA;
     |HAZ GridFicheroEntero;
     enBusqueda = 1;
     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
     |SI nSwArbolActivo = 1;       ||Si el arbol esta activo, lo actualizo
          |ESTADO_CONTROL nGrid1, "HIDE";
          nEstadoGrid = -1;
          |HAZ Boton0_8;
     |FINSI;
     |ESTADO_CONTROL nBotonTodos, "ENABLE";
     |ESTADO_CONTROL nBotonSin, "ENABLE";
     |ESTADO_CONTROL nBotonCon, "DISABLE";
|FPRC;

|PROCESO BotonTodos;
     aMostrarAso = "T";
     |ABRE #100;
     #100#0 = Usuario % 810;
     |LEE 101#100=;
     |SI FSalida ! 0;
         |DDEFECTO #100;
         #100#0 = Usuario % 810;
         |GRABA 020#100b;
     |FINSI;
     #100#26 = aMostrarAso;
     |GRABA 020#100a;
     |LIBERA #100;
     |CIERRA #100;


     |HAZ NucleoReflejos;
     ||Recalculo y el refresco del grid o el arbol segun corresponda
     |DESTRUYECONTROL nGrid1;   |PULSATECLA;
     |HAZ GridFicheroEntero;
     enBusqueda = 1;
     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
     |SI nSwArbolActivo = 1;       ||Si el arbol esta activo, lo actualizo
          |ESTADO_CONTROL nGrid1, "HIDE";
          nEstadoGrid = -1;
          |HAZ Boton0_8;
     |FINSI;
     |ESTADO_CONTROL nBotonTodos, "DISABLE";
     |ESTADO_CONTROL nBotonSin, "ENABLE";
     |ESTADO_CONTROL nBotonCon, "ENABLE";
|FPRC;

|PROCESO dsarm005Asocia;
     |SI #0#0 ! enDocuPpal;
         |HAZ Asociacion;
     |FINSI;
|FPRC;

|DEFBUCLE dsarm005Asocia;
 #dsarm005#1;
 ;
 INICIO;
 FINAL;
 ;
 NULL, dsarm005Asocia;
|FIN;

|PROCESO BtnAsocTodos;
     |BUCLE dsarm005Asocia;

     |PTEC 806;
|FPRC;

|PROCESO dsarm005Patron;
     |SI #trabajo@#0 = #dsarm005#0;
         |ACABA;
     |FINSI;

     #dsarm007#0 = #trabajo@#0;
     #dsarm007#1 = #dsarm005#0;
     |LEE 000#dsarm007.=;
     |SI FSalida = 0;  |ACABA;  |FINSI;

     |DDEFECTO #dsarm007;
     #dsarm007#0 = #trabajo@#0;
     #dsarm007#1 = #dsarm005#0;
     #dsarm007#2 = ~;
     #dsarm007#3 = Usuario % 810;
     |GRABA 020#dsarm007.n;
     |LIBERA #dsarm007;

     |HAZ HistoricoAsociar;
|FPRC;

|DEFBUCLE dsarm005Patron;
     #dsarm005#9;
     15;
     nDCli, nDMod, nDEje, nDPer, 00, 000000001, nDTra;
     nHCli, nHMod, nHEje, nHPer, 99, 999999999, nHTra;
     ;
     NULL, dsarm005Patron;
|FIN;

|PROCESO trabajo;
     |SI #dsarw111#0 = "S";  nDCli = #trabajo@#8;  nHCli = #trabajo@#8;  |FINSI;
     |SI #dsarw111#1 = "S";  nDTra = #trabajo@#15; nHTra = #trabajo@#15; |FINSI;
     |SI #dsarw111#2 = "S";  nDMod = #trabajo@#19; nHMod = #trabajo@#19; |FINSI;
     |SI #dsarw111#3 = "S";  nDPer = #trabajo@#18; nHPer = #trabajo@#18; |FINSI;
     |SI #dsarw111#4 = "S";  nDEje = #trabajo@#17; nHEje = #trabajo@#17; |FINSI;

     |BUCLE dsarm005Patron;
|FPRC;

|DEFBUCLE trabajo;
     #trabajo@#1001;
     ;
     000000001;
     999999999;
     ;
     NULL, trabajo;
|FIN;

|PROCESO BtnAsocPatron;
     |VENTANA 0501, 1475, -1, 16, "Patron"
     nVentz5 = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentz5;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |DDEFECTO #dsarw111;
     |PINPA #0#dsarw111;
     |PINDA #0#dsarw111;
     |ENDATOS #1#dsarw111;
     |SI FSalida = 0;
         |SI #dsarw111#0 = "N"; |Y #dsarw111#1 = "N";
          |Y #dsarw111#2 = "N"; |Y #dsarw111#3 = "N";
          |Y #dsarw111#4 = "N";
             |MENAV "0000Proceso cancelado por no haber criterio para asociar.";

             |HAZ FinVentanaz5;
             |ACABA;
         |FINSI;
     |FINSI;

     |HAZ FinVentanaz5;

     |DBASE aMas;  |QBF aMas;
     aMas = aMas + "def/dsarm005.mas";
     |CARGA_DEF aMas, trabajo@;
     |SI FSalida ! 0;
          |MENAV "0000[dsarm005.mas] inaccesible. Consulte con su distribuidor.";

          |ACABA;
     |FINSI;

     |VENTANA 0501, 0530, -1, 16, "Realizando trabajo"
     nVentz5 = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentz5;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     eaFicAso = aAlfa;

     |NOME_DAT #trabajo@#eaFicAso#;

     nDCli = 0;  nHCli = 99999;
     nDTra = 0;  nHTra = 99999;
     nDMod = 0;  nHMod = 999;
     nDPer = 0;  nHPer = 99;
     nDEje = 0;  nHEje = 9999;

     |BUCLE trabajo;

     |DESCARGA_DEF #trabajo@;

     |HAZ FinVentanaz5;

     |PTEC 806;
|FPRC;

|PROCESO BotonesPanta0;
     |CONTROL_SIMPLE 0, "BOTON,{{137}} Pantalla", 2588, 2598, Boton0_2;
     nBoton0_2 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{163}} Busqueda", 2502, 2512, Boton0_4;
     nBoton0_4 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{207}} Borrar Busqueda", 2514, 2530, Boton0_5;
     nBoton0_5 = FSalida;

     |SI nSwPideTipo = 1;
          |CONTROL_SIMPLE 0, "BOTON,Acotar por Tipo", 2538, 2550, BotonAcota;
          nBotonAcota = FSalida;

          aLabel = "LABEL,{{2}}Asociados..";
          |CONTROL_SIMPLE 0, aLabel, 2558, 2566,NULL;

          |CONTROL_SIMPLE 0, "BOTON,SIN", 2567, 2570, BotonSin;
          nBotonSin = FSalida;
          |CONTROL_SIMPLE 0, "BOTON,CON", 2571, 2574, BotonCon;
          nBotonCon = FSalida;
          |CONTROL_SIMPLE 0, "BOTON,*", 2575, 2578, BotonTodos;
          nBotonTodos = FSalida;

          |SI enSwFiltroAsociar = 1;
               |CONTROL_SIMPLE 0, "BOTON,ASOCIAR TODOS", 2785, 2898, BtnAsocTodos;
               nBtnAsocTodos = FSalida;

               |CONTROL_SIMPLE 0, "BOTON,ASOCIAR ENTRE DOCUMENTOS POR PATRON", 2760, 2883, BtnAsocPatron;
               nBtnAsocPatron = FSalida;
          |FINSI;

          |SI aMostrarAso = "T";
               |ESTADO_CONTROL nBotonTodos, "DISABLE";
               |ESTADO_CONTROL nBotonSin, "ENABLE";
               |ESTADO_CONTROL nBotonCon, "ENABLE";
          |SINO;
               |SI aMostrarAso = "S";
                    |ESTADO_CONTROL nBotonSin, "DISABLE";
                    |ESTADO_CONTROL nBotonCon, "ENABLE";
                    |ESTADO_CONTROL nBotonTodos, "ENABLE";
               |SINO;
                    |ESTADO_CONTROL nBotonCon, "DISABLE";
                    |ESTADO_CONTROL nBotonSin, "ENABLE";
                    |ESTADO_CONTROL nBotonTodos, "ENABLE";
               |FINSI;
          |FINSI;
     |SINO;
          |SI enSwFiltroAsociar = 1;
               aLabel = "LABEL,{{2}}Asociados..";
               |CONTROL_SIMPLE 0, aLabel, 2558, 2566,NULL;

               |CONTROL_SIMPLE 0, "BOTON,SIN", 2567, 2570, BotonSin;
               nBotonSin = FSalida;
               |CONTROL_SIMPLE 0, "BOTON,CON", 2571, 2574, BotonCon;
               nBotonCon = FSalida;
               |CONTROL_SIMPLE 0, "BOTON,*", 2575, 2578, BotonTodos;
               nBotonTodos = FSalida;

               |CONTROL_SIMPLE 0, "BOTON,ASOCIAR TODOS", 2785, 2898, BtnAsocTodos;
               nBtnAsocTodos = FSalida;

               |CONTROL_SIMPLE 0, "BOTON,ASOCIAR ENTRE DOCUMENTOS POR PATRON", 2760, 2883, BtnAsocPatron;
               nBtnAsocPatron = FSalida;

               |SI aMostrarAso = "T";
                    |ESTADO_CONTROL nBotonTodos, "DISABLE";
                    |ESTADO_CONTROL nBotonSin, "ENABLE";
                    |ESTADO_CONTROL nBotonCon, "ENABLE";
               |SINO;
                    |SI aMostrarAso = "S";
                         |ESTADO_CONTROL nBotonSin, "DISABLE";
                         |ESTADO_CONTROL nBotonCon, "ENABLE";
                         |ESTADO_CONTROL nBotonTodos, "ENABLE";
                    |SINO;
                         |ESTADO_CONTROL nBotonCon, "DISABLE";
                         |ESTADO_CONTROL nBotonSin, "ENABLE";
                         |ESTADO_CONTROL nBotonTodos, "ENABLE";
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;

     |ESTADO_CONTROL nBoton0_2, "DISABLE";
     |ESTADO_CONTROL nBoton0_4, "DISABLE";
     |ESTADO_CONTROL nBoton0_5, "DISABLE";
|FPRC;


|PROCESO AcotarPorTipos;
     |SI nSwBotonAcota = 1;
          |VENTANA 0501, 2699, -1, 16, "Acotar por Tipo"
          nVentz5 = FSalida;

          aVent1 = -1;
          aVent2 = -1;
          aVent3 = 0;
          aVent4 = nVentz5;
          aVent5 = "MODAL";
          |ESPECIFICA "ESTADO_VENTANA", aVent;

          enGrupoTres = 0;
          enSubGTres = 0;
          enTipoTres = 0;
          aEjecuta = "dsarz003;DAMETRE";
          |SUB_EJECUTA_NP aEjecuta;
          |SI enGrupoTres ! 0;
               nPideGrupo = enGrupoTres;
               nPideSGrupo = enSubGTres;
               nPideTipo = enTipoTres;
          |SINO;
               nPideGrupo = 0;
               nPideSGrupo = 0;
               nPideTipo = 0;
          |FINSI;

          |HAZ FinVentanaz5;

          |PULSATECLA;
          |PULSATECLA;
          |PULSATECLA;
     |SINO;
          |ABRE #100;
          #100#0 = Usuario % 810;
          |LEE 000#100=;
          |SI FSalida ! 0;
              |DDEFECTO #100;
          |FINSI;
          aTipoVista = #100#21; |QBF aTipoVista;
          aNomVista = #100#22;  |QBF aNomVista;
          aMostrarAso = #100#26; |QBT aMostrarAso;

          |SI nSwPideTipo = 1;
               nPideGrupo = #100#23;
               nPideSGrupo = #100#24;
               nPideTipo = #100#25;

               |SI nPideGrupo = 0; |O nPideSGrupo = 0; |O nPideTipo = 0;
                    enGrupoTres = 0;
                    enSubGTres = 0;
                    enTipoTres = 0;
                    aEjecuta = "dsarz003;DAMETRE";
                    |SUB_EJECUTA_NP aEjecuta;
                    |SI enGrupoTres ! 0;
                         nPideGrupo = enGrupoTres;
                         nPideSGrupo = enSubGTres;
                         nPideTipo = enTipoTres;
                    |SINO;
                         nPideGrupo = 0;
                         nPideSGrupo = 0;
                         nPideTipo = 0;
                    |FINSI;
               |FINSI;
          |FINSI;
          |CIERRA #100;
     |FINSI;
|FPRC;

|PROCESO reflejos;
     |SI enDocuPpal = #1005#0;
         |ACABA;
     |FINSI;

     |SI aMostrarAso = "T";        ||COMO HASTA AHORA
          #7#0 = enDocuPpal;
          #7#1 = #1005#0;
          |LEE 000#7=;
          |SI FSalida = 0;  |ACABA;  |FINSI;
     |SINO;
          |SI aMostrarAso = "S";
               ||Solo documentos que no tengan asociacion.
               #7#0 = #1005#0;
               #7#1 = 999999999;
               |LEE 000#7.];
               |SI FSalida = 0;
                    |LEE 000#7.a;
               |SINO;
                    |LEE 000#7.u;
               |FINSI;
               |SI FSalida = 0; |Y #7#0 = #1005#0;
                    |ACABA;
               |FINSI;
          |SINO;
               ||Solo documentos que tengan asociacion
               #7#0 = #1005#0;
               #7#1 = 999999999;
               |LEE 000#7.];
               |SI FSalida = 0;
                    |LEE 000#7.a;
               |SINO;
                    |LEE 000#7.u;
               |FINSI;
               |SI FSalida = 0; |Y #7#0 = #1005#0;
                    ||Todo correcto. Pasa el filtro
               |SINO;
                    |ACABA;
               |FINSI;
          |FINSI;
     |FINSI;

     |PARA nCampo = 0;  |SI nCampo [ enNumCampos;  |HACIENDO nCampo = nCampo + 1;
           #0nCampo = #1005nCampo;
     |SIGUE;

     |GRABA 020#0n;
     |LIBERA #0;
|FPRC;

|DEFBUCLE reflejos_1;
     #1005#1001;
     ;
             1;
     999999999;
     ;
     NULL, reflejos;
|FIN;

|DEFBUCLE reflejos_2;
     #1005#2004;
     ;
     enGrupo, enSubGrupo, enTipo,         1;
     enGrupo, enSubGrupo, enTipo, 999999999;
     ;
     NULL, reflejos;
|FIN;

|PROCESO NucleoReflejos;
     aQueQuiero  = "|NC";
     enNumCampos = #0#aQueQuiero#;
     enNumCampos = enNumCampos - 1;

     |HORA aHora;
     aAlfa = ((aHora % 102) + (aHora % 402) + (aHora % 702) + Usuario) % 108;
     eaFicAso = aAlfa;

     |NOME_DAT #0 aAlfa;
     |ABRE #0;  |CIERRA #0;  |DELINDEX #0;

     eaFichero = ("01" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #905 eaFichero;
     |ABRE #905;  |CIERRA #905;  |DELINDEX #905;

     |VENTANA 0501, 0550, -1, 16, "Procesando trabajo. Esto puede tardar varios minutos.";
     nVentTrab = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentTrab;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;
     |PAUSA;

     |ABRE #0;
     |ABRE #7;
     |SI nSwPideTipo = 0; |O nPideGrupo = 0;
          |BUCLE reflejos_1;
     |SINO;
          enGrupo = nPideGrupo;
          enSubGrupo = nPideSGrupo;
          enTipo = nPideTipo;
          |BUCLE reflejos_2;
     |FINSI;
     |CIERRA #0;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentTrab;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVentTrab;
|FPRC;



|PROCESO dsarm005;
     #0#65 = "";
     |GRABA 020#0a;
     |LIBERA #0;
|FPRC;

|DEFBUCLE dsarm005;
     #0#1;
     65;
     000000001, "*";
     999999999, "*";
     be;
     NULL, dsarm005;
|FIN;

|PROCESO dsarm007;
     #0#0 = #7#1;
     |LEE 101#0=;
     |SI FSalida = 0;
         #0#65 = "*";
         |GRABA 020#0a;
         |LIBERA #0;
     |FINSI;
|FPRC;

|DEFBUCLE dsarm007;
     #7#1;
     ;
     enDocuPpal, 000000001;
     enDocuPpal, 999999999;
     be;
     NULL, dsarm007;
|FIN;


|PROCESO LeeDocumento;
     |SI nFichero = 0;
         |SELECT #1#dsarm005;
         #dsarm005#0 = #dsarm055#0;
         |LEE 000#dsarm005.=;
         |SI FSalida = 0;
             |ESTADO_CONTROL nBoton0_2, "ENABLE";
             |ESTADO_CONTROL nBoton0_4, "ENABLE";
         |SINO;
             |ESTADO_CONTROL nBoton0_2, "DISABLE";
             |ESTADO_CONTROL nBoton0_4, "DISABLE";
             |DDEFECTO #dsarm005;
         |FINSI;

         |SELECT #8#dsarm005;
         |LEE 000#dsarm005.=;

         aFichero = #dsarm005#7;              |QBF aFichero;
         aFichero = aFichero + #dsarm005#9;   |QBF aFichero;
     |SINO;
         |SELECT #1#905;
         #905#0 = #dsarm055#0;
         |LEE 000#905=;
         |SI FSalida ! 0;  |DDEFECTO #905;  |FINSI;

         |SELECT #8#905;
         |LEE 000#905=;

         aFichero = #905#7;              |QBF aFichero;
         aFichero = aFichero + #905#9;   |QBF aFichero;
     |FINSI;

     |SI aFichero ! "";
         nEntroPDatos = 1;
         |ESTADO_CONTROL nBoton0_2,  "ENABLE";
         |ESTADO_CONTROL nBoton0_4,  "ENABLE";
     |SINO;
         |ESTADO_CONTROL nBoton0_2,  "DISABLE";
         |ESTADO_CONTROL nBoton0_4,  "DISABLE";
     |FINSI;
|FPRC;

|PROCESO EventoGridTree;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#dsarm055.=;

         |HAZ LeeDocumento;
     |FINSI;

     |SI FSalida = 4;
         |SI nEntroPDatos = 1;
              |SI #0#0 = enDocuPpal;
                  |MENAV "0000 No puedo asociar el mismo documento.";
                  |ACABA;
              |FINSI;

              |CONFI "0000SEsta seguro que quiere asociar el documento seleccionado..";
              |SI FSalida ! 0;
                  |ACABA;
              |FINSI;

              |LEE 101#0=;
              |SI FSalida = 0;
                  |HAZ Asociacion;

                  enSwNoHazVistas = 1;
                  |BORRA 020#0a;
                  |LIBERA #0;
                  enSwNoHazVistas = 0;

                  ||TODO CCC. En cierto casos peta de forma extranya
                  ||por eso como solucion, ahora al terminar de asociar
                  ||se sale, al arbol principal.
                  ||HAZ Boton0_11;
                  |PTEC 806;
              |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO BajaDSARM055_2;
     #dsarm055#1  = #dsarm056#5;
     #dsarm055#0  = 0;
|FPRC;

|PROCESO AltaDSARM055_2;
     #dsarm055#1  = #dsarm056#5;
     #dsarm055#0  = 999999999;
|FPRC;

|PROCESO BajaDSARM055_3;
     #dsarm055#1  = #dsarm056#5;
     #dsarm055#0  = 0;
     #dsarm055#2  = "";
|FPRC;

|PROCESO AltaDSARM055_3;
     #dsarm055#1  = #dsarm056#5;
     #dsarm055#0  = 999999999;
     #dsarm055#2  = "z" * 135;
|FPRC;

|PROCESO BajaDSARM055_4;
     #dsarm055#1  = #dsarm056#5;
     #dsarm055#0  = 0;
     #dsarm055#2  = "z" * 135;
|FPRC;

|PROCESO AltaDSARM055_4;
     #dsarm055#1  = #dsarm056#5;
     #dsarm055#0  = 999999999;
     #dsarm055#2  = "";
|FPRC;

|PROCESO CreaGrid;
     |DBASE aBase;  |QBF aBase;
     eaPathDef     = aBase + "def/";
     eaFichRegedit = "dsarm055";
     |HAZ BorraDelRegedit;

     |C_V #dsarm055#4,  1, "S";
     |C_V #dsarm055#5,  1, "S";
     |C_V #dsarm055#6,  1, "S";
     |C_V #dsarm055#7,  1, "S";

     nNumCamp = #8#12;
     |SI #dsarm056#6 ! "                    ";
         |ABRE #dsarm059;
         #dsarm059#0 = #dsarm056#6;
         |LEE 000#dsarm059.=;
         |CIERRA #dsarm059;

         nNumCamp = #dsarm059#1;
     |FINSI;

     |SI nNumCamp = 1;
         |C_V #dsarm055#4,  1, "N";
         |C_V #dsarm055#5,  1, "N";
         |C_V #dsarm055#6,  1, "N";
         |C_V #dsarm055#7,  1, "N";
     |FINSI;

     |SI nNumCamp = 2;
         |C_V #dsarm055#5,  1, "N";
         |C_V #dsarm055#6,  1, "N";
         |C_V #dsarm055#7,  1, "N";
     |FINSI;

     |SI nNumCamp = 3;
         |C_V #dsarm055#6,  1, "N";
         |C_V #dsarm055#7,  1, "N";
     |FINSI;

     |SI nNumCamp = 4;
         |C_V #dsarm055#7,  1, "N";
     |FINSI;
|FPRC;

|PROCESO EventoTree;
     |SI FEntrada = 0;
         nEntroPDatos = 0;
         aAlfa        = Contenido;  |QBF aAlfa;
         aTexto       = "";
         aLong        = aAlfa % 0;
         nLong        = aLong;
         nCargar      = 0;
         |PARA nPosi  = 1;  |SI nPosi [ nLong;  |HACIENDO nPosi = nPosi + 1;
               nPos = (nPosi * 100) + 1;
               aCarac = aAlfa % nPos;

               |SI nCargar = 1;
                   aTexto = aTexto + aCarac;
               |FINSI;

               |SI aCarac = ":";  nCargar = 1; |FINSI;
         |SIGUE;

         aContenido = aTexto;
     |FINSI;

     |ESTADO_CONTROL nBoton0_2,  "DISABLE";
     |ESTADO_CONTROL nBoton0_4,  "DISABLE";

     |SI nGridTree ! -1;
         |CIERRA #dsarm055;
         |DESTRUYECONTROL nGridTree;
         |FIN_CONTROL_WINDOWS nBtnGuarda;
         nGridTree = -1;
     |FINSI;

     aAlfa = aContenido;  |QBF aAlfa;
     |SI aAlfa = "";  |ACABA;  |FINSI;

     |ABRE #dsarm056;
     |SELECT #2#dsarm056;
     #dsarm056#5 = aContenido;
     |LEE 000#dsarm056.=;
     |SI FSalida = 0;
         eaIdent = #dsarm056#7; |QBF eaIdent;

         |SI eaIdent = "";
             |CIERRA #dsarm008;
             |CIERRA #dsarm056;

             eaCodVista = #dsarm008#0;
             |SUB_EJECUTA "dsarz050";

             |ABRE #dsarm008;
             |ABRE #dsarm056;

             #dsarm008#0 = eaCodVista;
             |LEE 000#dsarm008.=;

             |SELECT #2#dsarm056;
             #dsarm056#5 = aContenido;
             |LEE 000#dsarm056.=;
             |SI FSalida ! 0;  |DDEFECTO #dsarm056;  |FINSI;

             eaIdent = #dsarm056#7; |QBF eaIdent;
         |FINSI;

         |HAZ CreaGrid;

         |ABRE #dsarm055;
         nRango     = 4 + 8 + 16 + 32 + 128;

         |CONTROL_SIMPLE nPanta0, "BOTON,{{217}} ", 2102, 2103, GuardaGrid;
         nBtnGuarda = FSalida;

         |SI #dsarm008#30 = "N";
             |SI #dsarm008#28 = 0;
                 |LINEAL_SIMPLE #2#dsarm055, nRango, nPosDGrid, nPosHGrid, BajaDSARM055_2, AltaDSARM055_2, EventoGridTree;
             |SINO;
                 |LINEAL_SIMPLE #3#dsarm055, nRango, nPosDGrid, nPosHGrid, BajaDSARM055_3, AltaDSARM055_3, EventoGridTree;
             |FINSI;
         |SINO;
             |LINEAL_SIMPLE #4#dsarm055, nRango, nPosDGrid, nPosHGrid, BajaDSARM055_4, AltaDSARM055_4, EventoGridTree;
         |FINSI;
         nGridTree = FSalida;
     |FINSI;
     |CIERRA #dsarm056;

     |PULSATECLA;
|FPRC;


|PROCESO CreaArbol;
     |ESTADO_CONTROL nBoton0_2, "HIDE";
     |ESTADO_CONTROL nBoton0_4, "HIDE";
     |ESTADO_CONTROL nBoton0_5, "HIDE";

     |SI nBoton0_8 ! -1;   |ESTADO_CONTROL nBoton0_8,  "HIDE";  |FINSI;
     |SI nBoton0_9 ! -1;   |ESTADO_CONTROL nBoton0_9,  "HIDE";  |FINSI;
     |SI nBoton0_10 ! -1;  |ESTADO_CONTROL nBoton0_10, "HIDE";  |FINSI;
     |SI nBoton0_11 ! -1;  |ESTADO_CONTROL nBoton0_11, "HIDE";  |FINSI;
     |SI nEstadoGrid ! -1;  |ESTADO_CONTROL nGrid1,    "HIDE";  |FINSI;

     |HAZ QuitaBotonSalir;

     |VENTANA 0501, 0550, -1, 16, "Procesando trabajo. Esto puede tardar varios minutos.";
     nVentTrab = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentTrab;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;
     |PAUSA;

     |SI nGridTree ! -1;
         |CIERRA #dsarm055;
         |DESTRUYECONTROL nGridTree;
         |FIN_CONTROL_WINDOWS nBtnGuarda;
         nGridTree = -1;
     |FINSI;

     nTDocumentos = 0;
     |HAZ CargaTemporalVista_Z5;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentTrab;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVentTrab;

     |SI nTree ! -1;
         |FIN_CONTROL_WINDOWS nTree;
         nTree = -1;
     |FINSI;

     eaAlfa = "TREECTRL," + #8#0;  |QBF eaAlfa;
     eaAlfa = eaAlfa + " (" + nTDocumentos + " documentos)";
     eaAlfa = eaAlfa + " {{" + aAlfa + "}}";

     |CONTROL_SIMPLE nPanta0, eaAlfa, nPosDRamas, nPosHRamas, EventoTree;
     nTree = FSalida;

     |ESTADO_CONTROL nTree, "HIDE";

     |ESTADO_CONTROL nBoton0_2,  "SHOW";
     |ESTADO_CONTROL nBoton0_4,  "SHOW";
     |ESTADO_CONTROL nBoton0_5,  "SHOW";
     |SI nBoton0_8   ! -1;  |ESTADO_CONTROL nBoton0_8,  "SHOW";  |FINSI;
     |SI nBoton0_9   ! -1;  |ESTADO_CONTROL nBoton0_9,  "SHOW";  |FINSI;
     |SI nBoton0_10  ! -1;  |ESTADO_CONTROL nBoton0_10, "SHOW";  |FINSI;
     |SI nBoton0_11  ! -1;  |ESTADO_CONTROL nBoton0_11, "SHOW";  |FINSI;
     |SI nEstadoGrid ! -1;  |ESTADO_CONTROL nGrid1,     "SHOW";  |FINSI;

     |HAZ PonBotonSalir;

     |HAZ CreaGrid;

     |ABRE #dsarm055;
|FPRC;

|PROCESO PresentaArbol;
     nSwArbolActivo = 1;

     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;

     |ESTADO_CONTROL nTree, "SHOW";

     |SI nGridTree ! -1;
         |ESTADO_CONTROL nGridTree, "SHOW";
         |ESTADO_CONTROL nBtnGuarda, "SHOW";
     |FINSI;

     |SI #dsarm001#184 = "S";
         #dsarm055#0 = aBusca;
         |LEE 000#dsarm055.=;
         |SI FSalida = 0;
              aBusca = #dsarm055#1;

              sT_nID   = nTree;
              sT_nOper = 3;
              sT_aData = "[" + aBusca;
              |ESPECIFICA "COMMANDCONTROL", sTree;
              aRefresca = ":" + FSalida;
         |FINSI;
     |FINSI;

     sT_nID   = nTree;
     sT_nOper = 2;
     |SI aRefresca = "";
         sT_aData = "1:";
     |SINO;
         sT_aData = aRefresca;
     |FINSI;

     |ESPECIFICA "COMMANDCONTROL", sTree;
     |PULSATECLA;

     |FOCOTECLADO nTree;
|FPRC;

||TODO CCC botones Arbol

|PROCESO Boton0_8;
     nSwArbolActivo = 1;

     |SI nFichero = 0;
         aBusca = ("000000000" + #dsarm005#0) % -109;
     |SINO;
         aBusca = ("000000000" + #905#0) % -109;
     |FINSI;

     aAlfa  = #8#0;  |QBF aAlfa;
     |SI aAlfa = "";
         |HAZ Boton0_10;

         |SI nError [ 0;  |ACABA;  |FINSI;
     |SINO;
          aUsuarioVista = Usuario % 810;
          |QBF aUsuarioVista;
          |ABRE #dsarm043;
          |DDEFECTO #dsarm043;
          #dsarm043#0 = aUsuarioVista;
          #dsarm043#1 = #dsarm008#0;
          |LEE 000#dsarm043.=;
          |SI FSalida = 0; |Y #dsarm043#2 = "N";
               |CIERRA #dsarm043;
               |HAZ Boton0_10;

               |SI nError [ 0;
                    |ACABA;
               |FINSI;
          |SINO;
               |CIERRA #dsarm043;
               |SI enBusqueda = 1;
                   |HAZ CreaArbol;
               |FINSI;
          |FINSI;
     |FINSI;

     enBusqueda = 0;

     |ESTADO_CONTROL nGrid1, "HIDE";
     nEstadoGrid = -1;

     aTipoVista = "A";

     |HAZ PresentaArbol;

     |SI nBoton0_8 ! -1;
         |FIN_CONTROL_WINDOWS nBoton0_8;
         nBoton0_8 = -1;

         |CONTROL_SIMPLE 0, "BOTON,{{155}} Vistas por Lineal", 3002, 3020, Boton0_9;
         nBoton0_9 = FSalida;

         |CONTROL_SIMPLE 0, "BOTON,{{137}} Cambiar Vistas", 3042, 3060, Boton0_10;
         nBoton0_10 = FSalida;

         |CONTROL_SIMPLE 0, "BOTON,{{197}} Actualizar Arbol", 3022, 3040, Boton0_11;
         nBoton0_11 = FSalida;
     |FINSI;
|FPRC;

|PROCESO Boton0_9;
     nSwArbolActivo = 0;

     aTipoVista = "L";
     aNomVista = "";

     |PULSATECLA;
     |SI nTree ! -1;
         |ESTADO_CONTROL nTree, "HIDE";
     |FINSI;

     |SI nGridTree ! -1;
         |ESTADO_CONTROL nGridTree, "HIDE";
         |ESTADO_CONTROL nBtnGuarda, "HIDE";
     |FINSI;

     |PULSATECLA;

     |ESTADO_CONTROL nGrid1, "SHOW";
     nEstadoGrid = nGrid1;

     |REFRESCACONTROL nGrid1;
     |FOCOTECLADO nGrid1;
     |PULSATECLA;

     |FIN_CONTROL_WINDOWS nBoton0_9;
     nBoton0_9 = -1;

     |FIN_CONTROL_WINDOWS nBoton0_10;
     nBoton0_10 = -1;

     |FIN_CONTROL_WINDOWS nBoton0_11;
     nBoton0_11 = -1;

     |CONTROL_SIMPLE 0, "BOTON,{{155}} Vistas por Arbol", 3002, 3020, Boton0_8;
     nBoton0_8 = FSalida;
|FPRC;

|PROCESO dsarm008;
     |DDEFECTO #dsarm043;
     #dsarm043#0 = aUsuarioVista;
     #dsarm043#1 = #dsarm008#0;
     |LEE 000#dsarm043.=;
     |SI FSalida = 0; |Y #dsarm043#2 = "N";
          |ACABA;
     |FINSI;

     nContador = nContador + 1;

     eaAlfa     = #8#0;  |QBF eaAlfa;
     aOpcion    = eaAlfa;

     |HAZ QuitaRaros;
     aPopup    = eaAlfa;

     IaPopup   = IaPopup  + 1;
     IaOpcion  = IaOpcion + 1;
|FPRC;

|DEFBUCLE dsarm008;
     #8#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dsarm008;
|FIN;

|PROCESO Boton0_10;
     nContador = 0;
     aPopup1   = nGrid1;

     |ABRE #dsarm043;
     aUsuarioVista = Usuario % 810;
     |QBF aUsuarioVista;

     IaPopup  = aPopup3  <-;
     IaOpcion = aOpcion1 <-;
     |BUCLE dsarm008;
     aPopup2 = nContador;

     |CIERRA #dsarm043;

     |SI nContador = 0;
          aMensaje = "0000No tiene permiso para ver las vistas por  rbol";
          |MENAV aMensaje;
          nBoton0_9 = -1;
          aTipoVista = "L";
          |ACABA;
     |FINSI;

     IaPopup  = aPopup1  <-;
     IaOpcion = aOpcion1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     nError = FSalida;

     |SI nError > 0;
         IaPopup  = aPopup3  <-;
         IaOpcion = aOpcion1 <-;

         nOpcion  = nError - 1;
         IaPopup  = IaPopup  + nOpcion;
         IaOpcion = IaOpcion + nOpcion;
         aAlfa    = aOpcion;

         |PULSATECLA;

         #8#0 = aAlfa;
         |LEE 000#8=;
         |SI FSalida ! 0;  |ACABA;  |FINSI;

         aNomVista = aAlfa;
         |QBF aNomVista;

         |HAZ CreaArbol;
         enBusqueda = 0;
     |FINSI;

     |SI nBoton0_10 = -1;  |Y enBusqueda = 0;
         |ACABA;
     |FINSI;

     |SI enBusqueda = 1;
         |HAZ CreaArbol;
     |FINSI;

     enBusqueda = 0;

     |HAZ PresentaArbol;
|FPRC;

|PROCESO Boton0_11;
     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
     |HAZ CreaArbol;
     |HAZ PresentaArbol;
|FPRC;

|PROGRAMA;
     ||ASO

     |ABRE #dsarm008;

     aQueQuiero  = "|NC";
     enNumCampos = #0#aQueQuiero#;
     enNumCampos = enNumCampos - 1;

     aParametro = PARAMETRO;

     |BUCLE dsarm005;
     |BUCLE dsarm007;

     |LEE 000#0p;

     |PINPA #0#99;  nPanta0 = FSalida;

     |LEE 000#8p;
     |SI FSalida = 0;
          |CONTROL_SIMPLE 0, "BOTON,{{155}} Vistas por Arbol", 3002, 3020, Boton0_8;
          nBoton0_8 = FSalida;
     |FINSI;

     nRango = 2 + 4 + 8 + 16 + 32 + 128;
     nPos1      = 0602;
     nPos2      = 2698;
     nPosDRamas = 0602;
     nPosHRamas = 2098;
     nPosDGrid  = 2104;
     nPosHGrid  = 2698;

     |HAZ GridFicheroEntero;

     |HAZ BotonesPanta0;

     FSalida = 1;
     |HAZ EventoPanta1;

     #8#0 = aNomVista;
     |LEE 000#8=;
     |SI FSalida ! 0;  |DDEFECTO #8;  |FINSI;

     aNomVista = #8#0;
     |QBF aNomVista;
     |SI aTipoVista = "A"; |Y aNomVista ! "";
          |HAZ CreaArbol;
          |HAZ Boton0_8;
     |SINO;
          enBusqueda = 1;
     |FINSI;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::Salir," + nGrid1;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nBoton0_2;
     |FIN_CONTROL_WINDOWS nBoton0_4;
     |FIN_CONTROL_WINDOWS nBoton0_5;

     |SI nSwPideTipo = 1;
          |FIN_CONTROL_WINDOWS nBotonAcota;
          |FIN_CONTROL_WINDOWS nBotonSin;
          |FIN_CONTROL_WINDOWS nBotonCon;
          |FIN_CONTROL_WINDOWS nBotonTodos;
     |FINSI;

     |DESTRUYECONTROL nGrid1;

     |PULSATECLA;

     ||ASO
     |ABRE #100;
     #100#0 = Usuario % 810;
     |LEE 101#100=;
     |SI FSalida ! 0;
         |DDEFECTO #100;
         #100#0 = Usuario % 810;
         |GRABA 020#100b;
     |FINSI;
     #100#21 = aTipoVista;
     #100#22 = aNomVista;
     #100#26 = aMostrarAso;

     |SI enSwFiltroAsociar = 0;
          ||TODO CCC.  Descomentar OKIE
          #100#23 = nPideGrupo;
          #100#24 = nPideSGrupo;
          #100#25 = nPideTipo;
     |FINSI;

     |GRABA 020#100a;
     |LIBERA #100;
     |CIERRA #100;

     |BUCLE dsarm005;

     |CIERRA #dsarm008;
     |CIERRA #dsarm055;

     |HAZ BorraTempoVistaVS;
|FPRO;



|PROCESO Tipo80;  |TIPO 80;
     enTemporal  = 1;
     aParametro  = PARAMETRO;

     |ABRE #1;
     |LEE 000#1p;
     |SI FSalida ! 0;  |DDEFECTO #1;  |FINSI;
     |CIERRA #1;

     |SI enSwFiltroAsociar = 0;
          |SI #1#188 = "S";
               nSwPideTipo = 1;
          |SINO;
               nSwPideTipo = 0;
          |FINSI;
     |SINO;
          nSwPideTipo = 0;
          nPideGrupo = 0;
          nPideSGrupo = 0;
          nPideTipo = 0;
     |FINSI;

     |HAZ AcotarPorTipos;

     |DBASE aMas;  |QBF aMas;
     aMas = aMas + "def/dsarm005.mas";
     |CARGA_DEF aMas, dsarm005@;
     |SI FSalida ! 0;
          |MENAV "0000[dsarm005.mas] inaccesible. Consulte con su distribuidor.";
          |ACABA;
     |FINSI;

     |CARGA_DEF aMas, reflejos@;
     |SI FSalida ! 0;
          |DESCARGA_DEF #dsarm005@;
          |MENAV "0000[dsarm005.mas] inaccesible. Consulte con su distribuidor.";
          |ACABA;
     |FINSI;

     ||Esto es lo que voy a hacer si vienen del filtroAvanzado
     |SI enSwFiltroAsociar = 1;
          ||ARA40
          aAuxFiltro = eaNomFicFiltroAso;
          |NOME_DAT #1005 aAuxFiltro;
          |ABRE #1005;
          |CIERRA #1005;
     |FINSI;

     |HAZ NucleoReflejos;

     FSalida = 3099;
|FPRC;

|PROCESO Tipo90;  |TIPO 90;
     |SI aDestino ! "";
          |RFBORRA aDestino;
          aDestino = "";
     |FINSI;

     |CIERRA #0;  |DELINDEX #0;
     |CIERRA #7;

     |ABRE #905;  |CIERRA #905;  |DELINDEX #905;
     |DESCARGA_DEF #dsarm005@;
     |DESCARGA_DEF #reflejos@;

     enTemporal = 0;
|FPRC;
