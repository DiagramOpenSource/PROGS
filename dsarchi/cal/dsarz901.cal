|FICHEROS;
     dsarz901;

     dsarm005;
     dsarm017;
     dsarw100;

     Referen@;
|FIN;

|VARIABLES;
     nSwEsta      = 0;
     nHandle1     = 0;
     nLong        = 0;
     nConta       = 0;
     nHayReferen  = 0;

     aPathBase    = "";
     aAlfa        = "";
     aAlfa1       = "";
     aAlfa2       = "";
     aAlfa3       = "";
     aTel         = "";
     aTel2        = "";
     aDST         = "";
     aSRC         = "";
     aLong        = "";
     aTelefonoFax = "";
     aTlf         = "";
     aNif         = "";
     aNomApe      = "";
     aFich        = "";
     aOrigen      = "";
     aDestino     = "";
     aVacio40     = "";
     aZeta40      = "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz";
|FIN;

|PROCESO ReferenListin;
     aTel = #Referen@#4;
     |QBT aTel;
     |SI aTel = aTelefonoFax;
          nSwEsta = 1;
          aNomApe = #Referen@#0;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE ReferenListin;
     #Referen@#1002;
     4;
     aVacio40, 01, aTelefonoFax;
     aZeta40,  99, aTelefonoFax;
     ;
     NULL, ReferenListin;
|FIN;

|PROCESO BuscaEnListin;
     |SI nSwEsta ! 0;  |ACABA;  |FINSI;

     |SI nHayReferen = 0;  |ACABA;  |FINSI;

     |BUCLE ReferenListin;
|FPRC;

|PROCESO CargaDatos;
     aTel  = #dsarw100#8;   |QBT aTel;
     aTel2 = #dsarw100#17;  |QBT aTel2;
     |SI aTel = aTelefonoFax; |O aTel2 = aTelefonoFax;
          nSwEsta    = 1;
          aNif       = #dsarw100#0;
          aNomApe    = #dsarw100#1;
     |FINSI;
|FPRC;

|PROCESO BuscaDatos;
     nSwEsta = 0;

     |SELECT #3#dsarw100;
     #dsarw100#8 = aTelefonoFax;
     |LEE 000#dsarw100.=;
     |SI FSalida = 0;
         |HAZ CargaDatos;
     |FINSI;

     |SI nSwEsta = 1;  |ACABA;  |FINSI;

     |SELECT #4#dsarw100;
     #dsarw100#17 = aTelefonoFax;
     |LEE 000#dsarw100.=;
     |SI FSalida = 0;
         |HAZ CargaDatos;
     |FINSI;

     |SI nSwEsta = 1;  |ACABA;  |FINSI;

     |HAZ BuscaEnListin;
|FPRC;

|PROCESO LeeDatos;
     |SELECT #1#dsarw100;
     #dsarw100#0 = aNif;
     |LEE 000#dsarw100.=;
     |SI FSalida = 0;
         nSwEsta = 1;
         aNomApe = #dsarw100#1;
     |FINSI;

     |SI nSwEsta = 1;  |ACABA;  |FINSI;

     |HAZ BuscaEnListin;
|FPRC;

|PROCESO dsarm005;
     |PULSATECLA;

     aTlf = #dsarm005#55;  |QBT aTlf;
     aDST = #dsarm005#26;  |QBT aDST;
     aDST = aDST - "DST:";
     |SI FSalida ! 0;
         aSRC = #dsarm005#25;  |QBT aSRC;
         aSRC = aSRC - "SRC:";

         aLong = aSRC % 0;
         nLong = aLong;
         |SI nLong [ 5;
             aLong = aDST % 0;
             nLong = aLong;
             |SI nLong ] 7;
                 aTlf = aDST;
             |FINSI;
        |FINSI;
     |SINO;
        aAlfa = #dsarm005#23;  |QBT aAlfa;
        aAlfa = aAlfa - "TSI:";
        |SI FSalida = 0;  |ACABA;  |FINSI;
     |FINSI;

     aTelefonoFax = aTlf;
     |SI aTelefonoFax = "";  |ACABA;  |FINSI;

     aNif    = "000000000";
     aNomApe = "*DESCONOCIDO*";

     |ABRE #dsarm017;
     |SELECT #2#dsarm017;
     #dsarm017#2 = aTelefonoFax;
     |LEE 000#dsarm017.=;
     |SI FSalida = 0;
          aNif = #dsarm017#0;
     |FINSI;
     |CIERRA #dsarm017;

     |SI aNif = "000000000";       ||Lo buscamos en Integral/Basica
         |HAZ BuscaDatos;
     |SINO;
         |HAZ LeeDatos;
     |FINSI;

     aAlfa1 = #dsarm005#10;  |QBF aAlfa1;
     aAlfa2 = #dsarm005#14;  |QBF aAlfa2;
     aAlfa3 = #dsarm005#55;  |QBF aAlfa3;

     |QBF aNif;
     |QBF aNomApe;
     |QBF aTelefonoFax;

     |SI aAlfa1 = aNif;  |Y aAlfa2 = aNomApe; |Y aAlfa3 = aTelefonoFax;
         |ACABA;
     |FINSI;

     #dsarm005#10 = aNif;
     #dsarm005#14 = aNomApe;
     #dsarm005#38 = aNomApe;
     #dsarm005#55 = aTelefonoFax;

     |GRABA 020#dsarm005.a;
     |LIBERA #dsarm005;
|FPRC;

|DEFBUCLE dsarm005;
     #dsarm005#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL, dsarm005;
|FIN;

|PROCESO Tipo12;  |TIPO 12;
     |SI FSalida = 7;  |ACABA;  |FINSI;

     |SI #dsarz901#0 = "N";  |ACABA;  |FINSI;

     nHayReferen = 0;

     |DFICE aFich;

     |DBASS aPathBase;
     |QBF aPathBase;
     aAlfa = aPathBase + "integral/fich/dsam0103.dat";
     |XABRE "E", aAlfa, nHandle1;
     |SI FSalida ] 0;
         aOrigen  = aPathBase + "integral/fich/dsam0103.dat";
         aDestino = aFich + "dsarw100.dat";
         |COPIA_FICHERO aOrigen, aDestino;

         aOrigen  = aPathBase + "integral/fich/dsam0103.ddx";
         aDestino = aFich + "dsarw100.ddx";
         |COPIA_FICHERO aOrigen, aDestino;

         aOrigen  = aPathBase + "integral/fich/dsam0103.ixx";
         aDestino = aFich + "dsarw100.ixx";
         |COPIA_FICHERO aOrigen, aDestino;
     |SINO;
         aAlfa = aPathBase + "basica/fich/agim0003.dat";
         |XABRE "E", aAlfa, nHandle1;
         |SI FSalida ] 0;
             aOrigen  = aPathBase + "basica/fich/agim0003.dat";
             aDestino = aFich + "dsarw100.dat";
             |COPIA_FICHERO aOrigen, aDestino;

             aOrigen  = aPathBase + "basica/fich/agim0003.ddx";
             aDestino = aFich + "dsarw100.ddx";
             |COPIA_FICHERO aOrigen, aDestino;

             aOrigen  = aPathBase + "basica/fich/agim0003.ixx";
             aDestino = aFich + "dsarw100.ixx";
             |COPIA_FICHERO aOrigen, aDestino;
         |FINSI;
     |FINSI;

     |XABRE "E", aAlfa, nHandle1;
     |SI FSalida ] 0;
         aAlfa = aPathBase + "integral/def/dsam0107.mas";
         |CARGA_DEF aAlfa, Referen@;
         |SI FSalida = 0;
            aAlfa = aPathBase + "integral/fich/";
            |PATH_DAT #Referen@#aAlfa#;

            nHayReferen = 1;
         |FINSI;
     |FINSI;

     |ABRET #dsarw100;

     nConta = 0;
     |BUCLE dsarm005;

     |SI nHayReferen = 1;
         |DESCARGA_DEF #Referen@;
     |FINSI;

     |CIERRAT #dsarw100;
     |DELINDEX #dsarw100;
|FPRC;
