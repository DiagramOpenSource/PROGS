||Recalculo de NIF's en CEFAX. Fax ocultos/desconocidos

|FICHEROS;
     dsarz033 #0;

     dsarm001 #1;        ||Parametros
     dsarm002 #2;
     dsarm003 #3;
     dsarm004 #4;
     dsarm005 #5;        ||Registro Documentos
     dsarm017 #17;       ||Faxes Registro NIF

     Referen@  #100;
|FIN;

|VARIABLES;
     aFichero = "";
     aOrigen = "";
     aDestino = "";
     nContador = 0;

     aRespuesta = "";
     aMensaje = "";
     nExiste = 0;

     aLinea = "";
     aTelefonoFax = "";
     aNif = "";
     aNomApe = "";
     aDomicilio = "";
     aNumero = "";
     aAlfa = "";
     Handle1 = 0;

     nGrupoDes = 0;
     nSubGrupoDes = 0;
     nTipoDes = 0;
     nGrupoOri = 0;
     nSubGrupoOri = 0;
     nTipoOri = 0;

     nSwEsta = 0;
     aPathBase = "";
     aTel = "";
     aTel2 = "";
|FIN;

|PROCESO Referen;
     aTel = #Referen@#8;
     |QBT aTel;
     aTel2 = #Referen@#17;
     |QBT aTel2;
     |SI aTel = aTelefonoFax; |O aTel2 = aTelefonoFax;
          nSwEsta = 1;
          aNif = #Referen@#0;
          aNomApe = #Referen@#1;
          aDomicilio = #Referen@#2;
          aNumero = #Referen@#3;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE Referen;
 #Referen@#1001;
 ;
 "            ";
 "zzzzzzzzzzzz";
 ;
 NULL, Referen;
|FIN;

|PROCESO MueveDocumento;
     ||TODO CCC

     aMensaje = "0000Mover Documento " + #dsarm005#0;
     ||MENAV aMensaje;

     aFichero = #dsarm005#7;
     |QBF aFichero;
     aFichero = aFichero + #dsarm005#9;
     |QBF aFichero;
     aOrigen  = aFichero;

     aDestino = #1#1;
     |QBF aDestino;
     |ABRE #2;
     |DDEFECTO #2;
     #2#0 = nGrupoDes;
     |LEE 000#2.=;
     |SI FSalida = 0;
          aDestino = aDestino + "/" + #2#2;
          |QBF aDestino;
          |SI #1#7 = "N";
               |MKDIR aDestino;
          |SINO;
               |RMKDIR aDestino;
          |FINSI;
     |FINSI;
     |CIERRA #2;

     |SI #1#8 = "S"; ||Usan Grupo, Subgrupo y Tipo
          |ABRE #3;
          |DDEFECTO #3;
          #3#0 = nGrupoDes;
          #3#1 = nSubGrupoDes;
          |LEE 000#3.=;
          |SI FSalida = 0;
               aDestino = aDestino + "/" + #3#3;
               |QBF aDestino;
               |SI #1#7 = "N";
                    |MKDIR aDestino;
               |SINO;
                    |RMKDIR aDestino;
               |FINSI;
          |FINSI;
          |CIERRA #3;
          |ABRE #4;
          |DDEFECTO #4;
          #4#0 = nGrupoDes;
          #4#1 = nSubGrupoDes;
          #4#2 = nTipoDes;
          |LEE 000#4.=;
          |SI FSalida = 0;
               aDestino = aDestino + "/" + #4#4;
               |QBF aDestino;
               |SI #1#7 = "N";
                    |MKDIR aDestino;
               |SINO;
                    |RMKDIR aDestino;
               |FINSI;
          |FINSI;
          |CIERRA #4;
     |SINO;
          |ABRE #4;
          |DDEFECTO #4;
          #4#0 = nGrupoDes;
          #4#1 = 1;
          #4#2 = 1;
          |LEE 000#4.=;
          |CIERRA #4;
     |FINSI;

     aDestino = aDestino + "/";
     #dsarm005#7 = aDestino;

     aDestino = aDestino + #dsarm005#9;
     |QBF aDestino;

     |SI #1#7 = "N";
          |RENOMBRA_FICHERO aOrigen, aDestino;
     |SINO;
          |RRENOMBRA_FICHERO aOrigen, aDestino;
     |FINSI;

     #dsarm005#1 = nGrupoDes;
     #dsarm005#2 = nSubGrupoDes;
     #dsarm005#3 = nTipoDes;

     ||ARA .. Falta Numero NIF, nombre .. etc ...
     #dsarm005#10 = aNif;
     #dsarm005#14 = aNomApe;
     #dsarm005#38 = aNomApe;

     |GRABA 020#dsarm005.a;
     |LIBERA #dsarm005;

     nContador = nContador + 1;
|FPRC;

|PROCESO dsarm005;
     aLinea = #dsarm005#26;
     |QBF aLinea;
     aLinea = aLinea - "LINEA: ";
     |QBT aLinea;
     aTelefonoFax = aLinea;

     |SI aTelefonoFax = "";
          |ACABA;
     |FINSI;

     aNif = "000000000";
     aNomApe = "*DESCONOCIDO*";
     aDomicilio = "";
     aNumero = "";

     |ABRE #dsarm017;
     |SELECT #2#dsarm017;
     #dsarm017#2 = aTelefonoFax;
     |LEE 000#dsarm017.=;
     |SI FSalida = 0;
          aNif = #dsarm017#0;
     |FINSI;
     |CIERRA #dsarm017;

     |SI aNif = "000000000";       ||Lo buscamos en Integral/Basica

          nSwEsta = 0;
          ||TODO CCC. Buscamos el telefono
          |DBASS aPathBase;
          |QBF aPathBase;
          aAlfa = aPathBase + "integral/fich/dsam0103.dat";
          |XABRE "E", aAlfa, Handle1;
          |SI FSalida ] 0;
             aAlfa = aPathBase + "integral/def/dsam0103.mas";
             |CARGA_DEF aAlfa, Referen@;
             |SI FSalida = 0;
                aAlfa = aPathBase + "integral/fich/";
                |PATH_DAT #100 aAlfa;
                |BUCLE Referen;
                |DESCARGA_DEF #Referen@;
             |FINSI;
          |FINSI;


          |SI nSwEsta = 0;
               aAlfa = aPathBase + "basica/fich/agim0003.dat";
               |XABRE "E", aAlfa, Handle1;
               |SI FSalida ] 0;
                  aAlfa = aPathBase + "basica/def/agim0003.mas";
                  |CARGA_DEF aAlfa, Referen@;
                  |SI FSalida = 0;
                     aAlfa = aPathBase + "basica/fich/";
                     |PATH_DAT #100 aAlfa;
                     |BUCLE Referen;
                     |DESCARGA_DEF #Referen@;
                  |FINSI;
               |FINSI;
          |FINSI;
     |SINO;
          ||Buscamos el Nombre del CIF
          nSwEsta = 0;
          |DBASS aPathBase;
          |QBF aPathBase;
          aAlfa = aPathBase + "integral/fich/dsam0103.dat";
          |XABRE "E", aAlfa, Handle1;
          |SI FSalida ] 0;
             aAlfa = aPathBase + "integral/def/dsam0103.mas";
             |CARGA_DEF aAlfa, Referen@;
             |SI FSalida = 0;
                aAlfa = aPathBase + "integral/fich/";
                |PATH_DAT #100 aAlfa;
                |ABRE #100;
                #100#0 = aNif;
                |LEE 000#100.=;
                |SI FSalida = 0;
                    nSwEsta = 1;
                    aNomApe = #100#1;
                    aDomicilio = #100#2;
                    aNumero = #100#3;
                |FINSI;
                |CIERRA #100;
                |DESCARGA_DEF #Referen@;
             |FINSI;
          |FINSI;
          |SI nSwEsta = 0;
               aAlfa = aPathBase + "basica/fich/agim0003.dat";
               |XABRE "E", aAlfa, Handle1;
               |SI FSalida ] 0;
                  aAlfa = aPathBase + "basica/def/agim0003.mas";
                  |CARGA_DEF aAlfa, Referen@;
                  |SI FSalida = 0;
                     aAlfa = aPathBase + "basica/fich/";
                     |PATH_DAT #100 aAlfa;
                     |ABRE #100;
                     #100#0 = aNif;
                     |LEE 000#100.=;
                     |SI FSalida = 0;
                         aNomApe = #100#1;
                         aDomicilio = #100#2;
                         aNumero = #100#3;
                     |FINSI;
                     |CIERRA #100;
                     |DESCARGA_DEF #Referen@;
                  |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;

     |SI aNif ! "000000000";
          |HAZ MueveDocumento;
     |FINSI;
|FPRC;

|DEFBUCLE dsarm005;
 #dsarm005#2;
 ;
 nGrupoOri, nSubGrupoOri, nTipoOri, 000000001;
 nGrupoOri, nSubGrupoOri, nTipoOri, 999999999;
 be;
 NULL, dsarm005;
|FIN;

|PROCESO Principal;
     |ABRE #dsarm001;
     |DDEFECTO #dsarm001;
     |LEE 000#dsarm001.p;
     |CIERRA #dsarm001;

     |SI #dsarm001#66 = "N";       ||No esta activo CEFAX
          |ACABA;
     |FINSI;

     nGrupoDes = #dsarm001#67;
     nSubGrupoDes = #dsarm001#68;
     nTipoDes = #dsarm001#69;

     |SI nGrupoDes = 0; |Y nSubGrupoDes = 0; |Y nTipoDes = 0;
          |ACABA;
     |FINSI;

     nGrupoOri = #dsarm001#89;
     nSubGrupoOri = #dsarm001#90;
     nTipoOri = #dsarm001#91;

     |SI nGrupoOri = 0; |Y nSubGrupoOri = 0; |Y nTipoOri = 0;
          |ACABA;
     |FINSI;

     aMensaje = "0000NRealizar recalculo NIF's CEFAX?";
     |CONFI aMensaje;
     |SI FSalida = 0;
          nContador = 0;
          |BUCLE dsarm005;
          |SI nContador = 0;
               aMensaje = "0000No se ha encontrado ningun registro para actualizar.";
          |SINO;
               |SI nContador = 1;
                    aMensaje = "00001 registro actualizado.";
               |SINO;
                    aMensaje = "0000" + nContador + " registros actualizados.";
               |FINSI;
          |FINSI;
          |MENAV aMensaje;
     |FINSI;
|FPRC;

|PROGRAMA;
     |CLS;
     |ABRE #0;
     |DDEFECTO #0;
     |LEE 101#0p;
     nExiste = FSalida;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          aRespuesta = #dsarz033#1;
          #dsarz033#1 = "NO";
          |SI nExiste = 0;
               |GRABA 020#0a;
          |SINO;
               |GRABA 020#0n;
          |FINSI;
          |SI aRespuesta = "SI";
               |HAZ Principal;
          |FINSI;
     |FINSI;
     |LIBERA #0;
     |CIERRA #0;
|FPRO;
