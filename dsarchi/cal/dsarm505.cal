||Nuevo parametro:
||"DS9ALBAOCR" -> Tendre que averiguar el XXXYYYZZZ (207, 208 y 209 en dsarm001)
||Hace un filtro del grupo XXX subgrupo YYY y tipo ZZZ
||similar a como lo tenemos en ADCONTA.
||Tiquet: 1647.512

|FICHEROS;
     dsarm005;
     dsarm068;                ||Control.ErrorOCR DS9

     dsarw097;                ||Sel. AlbaDS9 a Aso.
     dsarw098;                ||Aux.Acotar por Fecha
     dsarw110;                ||Aux. Selecc.Alba.DS9

     agifa010@;               ||Albaranes dscomer9
     agifa041@;               ||Empresas dscomer9
     dsarm005@;
|FIN;

|VARIABLES;
     aTextoLabel = "";

     &enEmpDS9Mov = 0;
     &eaSerDS9Mov = "";
     &enNumDS9Mov = 0;
     &enSwDS9Mov = 0;

     nSwEstaDoc = 0;
     nSwAcotaFecha = 0;
     nSwContinuaBucle = 0;

     nBoton1 = 0;
     nBoton2 = 0;
     nBoton3 = 0;
     nBoton4 = 0;
     nBoton5 = 0;

     aDesde = "";
     aHasta = "";
     nRango = 0;

     aEsc1 = "";
     aEsc2 = "";
     aRetu = "";
     nGrid = 0;
     aTecla = "";


     nCampo = 0;
     aMensaje = "";

     nEmpresaDS9 = 0;

     aBase = "";
     aPathDef = "";
     aPathArchi = "";
     aPathFich = "";
     aPathEmp = "";
     aEmpresa = "";
     aNombreDef = "";

     {-1}aVent      = "";
         aVent1     = 0;
         aVent2     = 0;
         aVent3     = 0;
         aVent4     = 0;
         aVent5     = "";

     nVentana = 0;
     nVentanaDos = 0;
     nPantalla = 0;
|FIN;

|PROCESO dsarm005EstaDoc;
     |SI #dsarm005@#88 = nEmpresaDS9;
          nSwEstaDoc = 1;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE dsarm005EstaDoc;
 #dsarm005@#15003;
 ;
 "AV", #agifa010@#52, #agifa010@#0;
 "AV", #agifa010@#52, #agifa010@#0;
 ;
 NULL, dsarm005EstaDoc;
|FIN;

|PROCESO Albaranes;
     |DDEFECTO #dsarw110;
     #dsarw110#52 = #agifa010@#52;
     #dsarw110#0 = #agifa010@#0;
     |LEE 101#dsarw110.=;
     |SI FSalida ! 0; |GRABA 020#dsarw110.b; |FINSI;

     |PARA nCampo = 1; |SI nCampo [ 51; |HACIENDO nCampo = nCampo + 1;
          #dsarw110#nCampo# = #agifa010@#nCampo#;
     |SIGUE;
     |PARA nCampo = 53; |SI nCampo [ 97; |HACIENDO nCampo = nCampo + 1;
          #dsarw110#nCampo# = #agifa010@#nCampo#;
     |SIGUE;


     ||ARA39 .. Falta el campo de asociado S/N
     nSwEstaDoc = 0;
     |BUCLE dsarm005EstaDoc;
     |SI nSwEstaDoc = 1;
          #dsarw110#98 = "S";
     |FINSI;


     |GRABA 020#dsarw110.a;
     |LIBERA #dsarw110;
|FPRC;

|DEFBUCLE Albaranes;
     #agifa010@#7003;
     ;
      #dsarm068#2, "     ", 000000;
     "31.12.2099", "zzzzz", 999999;
     ;
     NULL, Albaranes;
|FIN;


|PROCESO Baja110;
     |SELECT #2#dsarw110;
     #dsarw110#52 = "     ";
     #dsarw110#0 = 000000;
     #dsarw110#98 = aDesde;
|FPRC;

|PROCESO Alta110;
     #dsarw110#52 = "zzzzz";
     #dsarw110#0 = 999999;
     #dsarw110#98 = aHasta;
|FPRC;

|PROCESO Evento110;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#dsarw110.=;
     |FINSI;

     |SI FSalida = 4;
         |CONFI "0000SEst  seguro del albar n seleccionado.";
         |SI FSalida = 0;
             enSwDS9Mov = 1;
             enEmpDS9Mov = nEmpresaDS9;
             eaSerDS9Mov = #dsarw110#52;
             enNumDS9Mov = #dsarw110#0;

             nSwContinuaBucle = 0;
             |PTEC 806;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Baja510;
     |SELECT #4#agifa010@;
     #agifa010@#52 = "     ";
     #agifa010@#0 = 000000;
     #agifa010@#1 = #dsarm068#2;
|FPRC;

|PROCESO Alta510;
     #agifa010@#52 = "zzzzz";
     #agifa010@#0 = 999999;
     #agifa010@#1 = "31.12.2099";
|FPRC;

|PROCESO PintaSinConDocumentos;
     nSwEstaDoc = 0;
     |BUCLE dsarm005EstaDoc;
     |SI nSwEstaDoc = 1;
          aTextoLabel = "Albaran CON Documentos Asociados";
     |SINO;
          aTextoLabel = "Albaran SIN Documentos Asociados";
     |FINSI;

     aTextoLabel = "LABEL,{{001}}" + aTextoLabel;
     |CONTROL_SIMPLE nPantalla, aTextoLabel, 0802, 0862, NULL;
|FPRC;

|PROCESO Evento510;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#agifa010@.=;
         |HAZ PintaSinConDocumentos;
     |FINSI;

     |SI FSalida = 4;
         |CONFI "0000SEst  seguro del albar n seleccionado.";
         |SI FSalida = 0;
             enSwDS9Mov = 1;
             enEmpDS9Mov = nEmpresaDS9;
             eaSerDS9Mov = #agifa010@#52;
             enNumDS9Mov = #agifa010@#0;

             nSwContinuaBucle = 0;
             |PTEC 806;
         |FINSI;
     |FINSI;
|FPRC;


|PROCESO Boton1;
     aDesde = "N";
     aHasta = "N";
     |ESTADO_CONTROL nBoton1, "DISABLE";
     |ESTADO_CONTROL nBoton2, "ENABLE";
     |ESTADO_CONTROL nBoton3, "ENABLE";

     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO Boton2;
     aDesde = "S";
     aHasta = "S";

     |ESTADO_CONTROL nBoton1, "ENABLE";
     |ESTADO_CONTROL nBoton2, "DISABLE";
     |ESTADO_CONTROL nBoton3, "ENABLE";
     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO Boton3;
     aDesde = "";
     aHasta = "z";

     |ESTADO_CONTROL nBoton1, "ENABLE";
     |ESTADO_CONTROL nBoton2, "ENABLE";
     |ESTADO_CONTROL nBoton3, "DISABLE";
     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO Boton4;         ||Cambio empresa
     nSwContinuaBucle = 1;
     nEmpresaDS9 = 0;
     |PTEC 806;
|FPRC;

|PROCESO Boton5;         ||Acotar por fecha
     |VENTANA 1501, 2099, -1, -1, "Acotar Albaranes por fecha DESDE";
     nVentanaDos = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentanaDos;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |DDEFECTO #dsarw098;
     #dsarw098#0 = #dsarm068#2;
     |PINPA #0#dsarw098;
     |ENDATOS #1#dsarw098;
     |SI FSalida = 0;
          aVent1 = -1;
          aVent2 = -1;
          aVent3 = 0;
          aVent4 = nVentanaDos;
          aVent5 = "MODELESS";
          |ESPECIFICA "ESTADO_VENTANA", aVent;
          |FINVENTANA nVentanaDos;

          |ABRE #dsarm068;
          |DDEFECTO #dsarm068;
          |LEE 101#dsarm068.p;
          |SI FSalida ! 0; |GRABA 020#dsarm068.b; |FINSI;
          #dsarm068#2 = #dsarw098#0;
          |GRABA 020#dsarm068.a;
          |CIERRA #dsarm068;

          nSwAcotaFecha = 1;
          nSwContinuaBucle = 1;
          |PTEC 806;
          ||ARA39
     |SINO;
          aVent1 = -1;
          aVent2 = -1;
          aVent3 = 0;
          aVent4 = nVentanaDos;
          aVent5 = "MODELESS";
          |ESPECIFICA "ESTADO_VENTANA", aVent;
          |FINVENTANA nVentanaDos;
          ||Me quedo donde estoy

          ||Vuelvo a activar el modal de la ventana principal.
          aVent1 = -1;
          aVent2 = -1;
          aVent3 = 0;
          aVent4 = nVentana;
          aVent5 = "MODAL";
          |ESPECIFICA "ESTADO_VENTANA", aVent;
     |FINSI;

|FPRC;

|PROCESO MoverBandejaErrOCR;
     |ABRE #dsarm068;
     |DDEFECTO #dsarm068;
     |LEE 000#dsarm068.p;
     nEmpresaDS9 = #dsarm068#1;
     |CIERRA #dsarm068;

     ||Bucle preguntando a que albaran se asocia
     ||es un bucle pq. puedo cambiar de empresa de gestion.

     ||una vez terminado se mueve al grupo de Alb. Correctos.

     |DBASS aBase;
     |QBF aBase;

     aPathArchi = aBase + "dsarchi/def/";
     aPathDef = aBase + "dscomer9/def/";
     aPathFich = aBase + "dscomer9/fich/";

     aNombreDef = aPathDef + "agifa041";
     |CARGA_DEF aNombreDef, agifa041@;
     |SI FSalida ! 0;
          aMensaje = "0000Error: Problemas con acceso [CARGA_DEF] al fichero " + aNombreDef;
          |MENAV aMensaje;
          |ACABA;
     |FINSI;

     |PATH_DAT #agifa041@#aPathFich#;

     aEmpresa = ("00000" + nEmpresaDS9) % -105;

     |ABRE #dsarw110;

     || La primera vez si hay empresa, debe acotar, por si hay albaranes nuevos
     nSwAcotaFecha = 1;
     |PARA; |SI; |HACIENDO;
          nSwContinuaBucle = 0;

          |SI nEmpresaDS9 = 0;
               ||SI no selecciono ninguna. Termina
               ||SI selecciona una empresa .. Actualizo el dsarm068

               ||Grid de empresas para seleccionar la empresa.
               |ABRE #agifa041@
               |CONSULTA_CLAVE #1#agifa041@;
               |SI FSalida = 0;
                    nEmpresaDS9 = #agifa041@#0;
                    aEmpresa = ("00000" + nEmpresaDS9) % -105;
               |SINO;
                    |SAL;
               |FINSI;
               |CIERRA #agifa041@;

               |ABRE #dsarm068;
               |DDEFECTO #dsarm068;
               |LEE 101#dsarm068.p;
               |SI FSalida ! 0; |GRABA 020#dsarm068.b; |FINSI;
               #dsarm068#1 = nEmpresaDS9;
               |GRABA 020#dsarm068.a;
               |CIERRA #dsarm068;
          |SINO;
               |SI nSwAcotaFecha = 1;
                    aEmpresa = ("00000" + nEmpresaDS9) % -105;
               |FINSI;
          |FINSI;

          ||Ahora ya muestro el grid de los albaranes de la empresa.
          ||Ya esta acotado por fecha.

          nSwAcotaFecha = 0;

          |VENTANA 0501, 3299, -1, -1, "Seleccion Albaran a Vincular DSARCHI";
          nVentana = FSalida;

          aVent1 = -1;
          aVent2 = -1;
          aVent3 = 0;
          aVent4 = nVentana;
          aVent5 = "MODAL";
          |ESPECIFICA "ESTADO_VENTANA", aVent;

          |PINPA #0#dsarw097;
          nPantalla = FSalida;

          |CONTROL_SIMPLE nPantalla, "BOTON,Cambio Empresa", 0680, 0698, Boton4;
          nBoton4 = FSalida;

          |CONTROL_SIMPLE nPantalla, "BOTON,Acotar por Fecha", 0780, 0798, Boton5;
          nBoton5 = FSalida;

          aDesde = "N";
          aHasta = "N";

          |ESTADO_CONTROL nBoton1, "DISABLE";

          aNombreDef = aPathDef + "agifa010";
          |CARGA_DEF aNombreDef, agifa010@;
          |SI FSalida ! 0;
               aMensaje = "0000Error: Problemas con acceso [CARGA_DEF] al fichero " + aNombreDef;
               |MENAV aMensaje;
               |SAL;
          |FINSI;
          aPathEmp = aBase + "dscomer9/fich/" + aEmpresa + "/";

          |PATH_DAT #agifa010@#aPathEmp#;

          aNombreDef = aPathArchi + "dsarm005";
          |CARGA_DEF aNombreDef, dsarm005@;
          |SI FSalida ! 0;
               |DESCARGA_DEF #agifa010@;
               aMensaje = "0000Error: Problemas con acceso [CARGA_DEF] al fichero " + aNombreDef;
               |MENAV aMensaje;
               |SAL;
          |FINSI;
          |ABRE #agifa010@;

          nRango  = 4 + 8 + 16 + 32 + 128;
          |LINEAL_SIMPLE #4#agifa010@, nRango, 0902, 3298, Baja510, Alta510, Evento510;
          nGrid = FSalida;

          ||Pongo el grid
          ||Y los botones
          ||Bucle para salir

          aEsc1  = &255 + "806";
          aEsc2  = &255 + "807";
          aRetu  = &255 + "802";
          |PARA; |SI;  |HACIENDO;
              FSalida = "::Salir," + nGrid;
              |LEETECLA aTecla;
              |SI aTecla = aEsc1; |SAL;  |FINSI;
              |SI aTecla = aEsc2; |SAL;  |FINSI;
              |SI aTecla = aRetu; |SAL;  |FINSI;
          |SIGUE;

          |FIN_CONTROL_WINDOWS nBoton1;
          |FIN_CONTROL_WINDOWS nBoton2;
          |FIN_CONTROL_WINDOWS nBoton3;
          |FIN_CONTROL_WINDOWS nBoton4;
          |FIN_CONTROL_WINDOWS nBoton5;

          aVent1 = -1;
          aVent2 = -1;
          aVent3 = 0;
          aVent4 = nVentana;
          aVent5 = "MODELESS";
          |ESPECIFICA "ESTADO_VENTANA", aVent;

          |FINVENTANA nVentana;

          |CIERRA #agifa010@;

          |DESCARGA_DEF #dsarm005@;
          |DESCARGA_DEF #agifa010@;

          ||Si seleccionan albaran.  Pillo los datos y muevo el documento

          ||Si quieren anular.  Termino y me salgo
          |SI nSwContinuaBucle = 0;
               |SAL;
          |FINSI;
     |SIGUE;
     |CIERRA #dsarw110;

     |DESCARGA_DEF #agifa041@;
|FPRC;
