|FICHEROS;
     dsarm050 #50;
     dsarm051 #51;
     dsarm052 #52;

     :integral/dsam0124;
     :integral/dsam0000;

     dsarw077 #977;
     dsarw078 #978,MANTE;
|FIN;

|VARIABLES;
     aAlfa           = "";
     aEsc1           = "";
     aEsc2           = "";
     aRetu           = "";
     aTecla          = "";
     aFichero        = "";
     aLista          = "";
     aHastaZ         = "";
     aBase           = "";
     aMas            = "";
     aAlfa1          = "";
     aAlfa2          = "";

     nVent51         = 0;
     nVent978        = 0;
     nRango          = 0;
     nModo           = 0;
     nPI             = 0;
     nHandle         = 0;
     nCalc           = 0;
     nCalc1          = 0;

     nGrid50         = 0;
     nGrid51         = 0;
     nGrid124        = 0;
     nGrid977        = 0;
     nBoton50_1      = 0;
     nBoton50_2      = 0;
     nBoton50_3      = 0;
     nBoton51_1      = 0;
     nBoton51_2      = 0;
     nBoton977_1     = 0;
     nBoton977_2     = 0;

     {-1}aVent       = "";
         aVent1      = 0;
         aVent2      = 0;
         aVent3      = 0;
         aVent4      = 0;
         aVent5      = "";

     &eaPathDef      = "";
     &eaFichRegedit  = "";
     &eaUnidadBasico = "";
     &eaUnidad       = "";
|FIN;

|PROCESO BorraDelRegedit;
     |HAZ DimeUnidad;
     eaUnidadBasico = eaUnidad + ":";

     aAlfa2 = "";
     aAlfa = eaUnidadBasico + "/documentos/dsarchi/tmp/"; |RMKDIR aAlfa;
     aAlfa = aAlfa + "fichero.reg";
     |XABRE "WC", aAlfa, nHandle;
     |SI FSalida ] 0;
         aAlfa  = "REGEDIT4" + &13 + &10; |XGRABA nHandle, aAlfa;
         aAlfa  = &13 + &10;              |XGRABA nHandle, aAlfa;

         aAlfa2 = "[HKEY_CURRENT_USER\Software\Diagram Software S.L.\Diagramx\GridDefaultTaman]" + &13 + &10;
         |XGRABA nHandle, aAlfa2;

         aAlfa1 = " " + eaPathDef;
         aAlfa1 = aAlfa1 - "/";
         |PARA; |SI FSalida ! 0; |HACIENDO;
              nCalc  = ((FSalida / 100) ! 0) + 99;
              nCalc1 = FSalida + 98;
              aAlfa1 = (aAlfa1 % nCalc) + "_" + (aAlfa1 % nCalc1);
              aAlfa1 = aAlfa1 - "/";
         |SIGUE;

         aAlfa = &34 + aAlfa1 + eaFichRegedit + &34 + "=-" + &13 + &10;
         |XGRABA nHandle, aAlfa;

         aAlfa2  = "[HKEY_CURRENT_USER\Software\Diagram Software S.L.\Diagramx\GridDefaultOrder]" + &13 + &10;
         |XGRABA nHandle, aAlfa2;

         aAlfa1 = " " + eaPathDef;
         aAlfa1 = aAlfa1 - "/";
         |PARA; |SI FSalida ! 0; |HACIENDO;
              nCalc  = ((FSalida / 100) ! 0) + 99;
              nCalc1 = FSalida + 98;
              aAlfa1 = (aAlfa1 % nCalc) + "_" + (aAlfa1 % nCalc1);
              aAlfa1 = aAlfa1 - "/";
         |SIGUE;
         aAlfa = &34 + aAlfa1 + eaFichRegedit + &34 + "=-" + &13 + &10;
         |XGRABA nHandle, aAlfa;
     |FINSI;
     |XCIERRA nHandle;

     aAlfa  = "cmd " + &34 + "/c regedit /C /s " + eaUnidadBasico + "/documentos/dsarchi/tmp/fichero.reg" + &34;
     |RSYSTEM aAlfa;
|FPRC;

|PROCESO ControlaBotones;
     |SALVA_DATOS #50;
     |LEE 000#50p;
     |SI FSalida = 0;
         |ESTADO_CONTROL nBoton50_2, "ENABLE";
         |ESTADO_CONTROL nBoton50_3, "ENABLE";
     |SINO;
         |ESTADO_CONTROL nBoton50_2, "DISABLE";
         |ESTADO_CONTROL nBoton50_3, "DISABLE";
     |FINSI;
     |REPON_DATOS #50;
     |LEE 000#50=;

     |SALVA_DATOS #51;
     |LEE 000#51p;
     |SI FSalida = 0;
         |ESTADO_CONTROL nBoton51_1, "ENABLE";
         |ESTADO_CONTROL nBoton51_2, "ENABLE";
         |ESTADO_CONTROL nBoton977_2, "ENABLE";
     |SINO;
         |ESTADO_CONTROL nBoton51_1, "DISABLE";
         |ESTADO_CONTROL nBoton51_2, "DISABLE";
         |ESTADO_CONTROL nBoton977_2, "DISABLE";
     |FINSI;
     |REPON_DATOS #51;
     |LEE 000#51=;

     |SALVA_DATOS #977;
     |LEE 000#977p;
     |SI FSalida = 0;
         |ESTADO_CONTROL nBoton977_1, "ENABLE";
     |SINO;
         |ESTADO_CONTROL nBoton977_1, "DISABLE";
     |FINSI;
     |REPON_DATOS #977;
     |LEE 000#977=;
|FPRC;

|PROCESO Panta978;
     |C_M #978#0, 1, "S";
     |SI nModo = 2;
         |C_M #978#0, 1, "N";

         |LEE 101#50=;
         |SI FSalida ! 0;  |ACABA;  |FINSI;
     |SINO;
         |DDEFECTO #50;
     |FINSI;

     |VENTANA 0501, 1999, -1, 16, "Mantenimiento de direcciones";
     nVent978 = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent978;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     #978#0  = #50#0;
     #978#1  = #50#1;
     #978#2  = #50#2;
     #978#3  = #50#3;

     |PINPA #0#978;
     |PINDA #0#978;
     |ENDATOS #1#978;
     |SI FSalida = 0;
         #50#0  = #978#0;
         |LEE 101#50=;
         |SI FSalida ! 0;
             |DDEFECTO #50;
             #50#0  = #978#0;
             |GRABA 020#50b;
         |FINSI;

         #50#1  = #978#1;
         #50#2  = #978#2;
         #50#3  = #978#3;
         |GRABA 020#50a;
         |LIBERA #50;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent978;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVent978;

     |REFRESCACONTROL nGrid50;

     |HAZ ControlaBotones;
|FPRC;

|PROCESO AltaDireccion;
     nModo = 1;  |HAZ Panta978;
|FPRC;

|PROCESO ModificarDireccion;
     nModo = 2;  |HAZ Panta978;
|FPRC;

|PROCESO BorrarDireccion;
     |CONFI "0000NSeguro que quiere borrar la direccion seleccionada";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |LEE 101#50=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |BORRA 020#50a;
     |LIBERA #50;

     |REFRESCACONTROL nGrid50;

     |HAZ ControlaBotones;
|FPRC;

|PROCESO dsarm052B;
     |BORRA 020#52a;
     |LIBERA #52;
|FPRC;

|DEFBUCLE dsarm052B;
     #52#1;
     ;
     #51#0, "";
     #51#0, aHastaZ;
     be;
     NULL, dsarm052B;
|FIN;

|PROCESO BorrarLista;
     |CONFI "0000NSeguro que quiere borrar la lista seleccionada y todas sus direcciones";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |LEE 101#51=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |BUCLE dsarm052B;

     |BORRA 020#51a;
     |LIBERA #51;

     |REFRESCACONTROL nGrid51;

     |HAZ ControlaBotones;
|FPRC;

|PROCESO Baja52;
     #52#0 = #51#0;
     #52#1 = "";
|FPRC;

|PROCESO Alta52;
     #52#0 = #51#0;
     #52#1 = "z" * 50;
|FPRC;

|PROCESO VerDirecciones;
     |C_V #52#0, 1, "N";
     |C_V #52#1, 1, "S";

     |ABRE #52;
     |CONSULTA_F_CLAVE #1#52, Baja52, Alta52;
     |CIERRA #52;
|FPRC;

|PROCESO dsarw077NLista;
     #52#0 = #51#0;
     #52#1 = #977#0;
     |GRABA 020#52n;
     |LIBERA #52;
|FPRC;

|DEFBUCLE dsarw077NLista;
     #977#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dsarw077NLista;
|FIN;

|PROCESO NuevaLista;
     |VENTANA 0501, 0755, -1, 16, "Introduzca el nombre de la nueva lista";
     nVent51 = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent51;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     E_inf   = "";
     E_sup   = "50";
     aLista  = 0602 ? 1;   |QBF aLista;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent51;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVent51;

     |SI aLista = "";  |ACABA;  |FINSI;

     #51#0 = aLista;
     |LEE 000#51=;
     |SI FSalida = 0;
         |MENAV "0000La lista ya existe.";
         |ACABA;
     |FINSI;

     #51#0 = aLista;
     |GRABA 020#51n;
     |LIBERA #51;

     |ABRE #52;
     |BUCLE dsarw077NLista;
     |CIERRA #52;

     |REFRESCACONTROL nGrid51;

     |HAZ ControlaBotones;
|FPRC;

|PROCESO ActualizarLista;
     |MENAV "0000A continuacion seleccione la lista que quiere actualizar";

     |CONSULTA_CLAVE #1#51;
     |SI FSalida ! 0;
         |MENAV "0000Proceso cancelado";
         |ACABA;
     |FINSI;

     |BUCLE dsarm052B;

     |ABRE #52;
     |BUCLE dsarw077NLista;
     |CIERRA #52;
|FPRC;

|PROCESO Evento50;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#50=;
     |FINSI;

     |SI FSalida = 4;
         #977#0 = #50#0;
         |LEE 000#977=;
         |SI FSalida ! 0;
             #977#0 = #50#0;
             #977#1 = 0;
             #977#2 = "";
             |GRABA 020#977n;
             |LIBERA #977;

             |REFRESCACONTROL nGrid977;

             |HAZ ControlaBotones;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Evento124;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#dsam0124.=;
     |FINSI;

     |SI FSalida = 4;
         #dsarw077#0 = #dsam0124#3;
         |LEE 000#dsarw077.=;
         |SI FSalida ! 0;
             |SELECT #2#dsam0000;
             #dsam0000#1 = #dsam0124#0;
             |LEE 000#dsam0000.=;
             |SI FSalida ! 0;  |DDEFECTO #dsam0000;  |FINSI;

             #dsarw077#0 = #dsam0124#3;
             #dsarw077#1 = #dsam0000#0;
             #dsarw077#2 = #dsam0000#1;

             |GRABA 020#dsarw077.n;
             |LIBERA #dsarw077;

             |REFRESCACONTROL nGrid977;

             |HAZ ControlaBotones;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO dsarm052;
     #977#0 = #52#1;
     |LEE 000#977=;
     |SI FSalida = 0;  |ACABA;  |FINSI;

     #977#0 = #52#1;
     |GRABA 020#977n;
     |LIBERA #977;
|FPRC;


|DEFBUCLE dsarm052;
     #52#1;
     ;
     #51#0, "";
     #51#0, aHastaZ;
     ;
     NULL, dsarm052;
|FIN;

|PROCESO dsam0124;
     aAlfa = #dsam0124#3; |QBF aAlfa;
     |SI aAlfa = "";  |ACABA;  |FINSI;

     #dsarw077#0 = #dsam0124#3;
     |LEE 000#dsarw077.=;
     |SI FSalida = 0;  |ACABA;  |FINSI;

     |SELECT #2#dsam0000;
     #dsam0000#1 = #dsam0124#0;
     |LEE 000#dsam0000.=;
     |SI FSalida ! 0;  |DDEFECTO #dsam0000;  |FINSI;

     #dsarw077#0 = #dsam0124#3;
     #dsarw077#1 = #dsam0000#0;
     #dsarw077#2 = #dsam0000#1;
     |GRABA 020#dsarw077.n;
     |LIBERA #dsarw077;
|FPRC;

|DEFBUCLE dsam0124;
     #dsam0124#2;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dsam0124;
|FIN;

|PROCESO TodasPI;
     |BUCLE dsam0124;

     |REFRESCACONTROL nGrid977;
     |HAZ ControlaBotones;
|FPRC;

|PROCESO Evento51;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#51=;
     |FINSI;

     |SI FSalida = 4;
         |BUCLE dsarm052;

         |REFRESCACONTROL nGrid977;

         |HAZ ControlaBotones;
     |FINSI;
|FPRC;

|PROCESO Evento977;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#977=;
     |FINSI;

     |SI FSalida = 4;
         |LEE 101#977=;
         |SI FSalida = 0;
             |BORRA 020#977a;
             |LIBERA #977;

             |REFRESCACONTROL nGrid977;

             |HAZ ControlaBotones;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO BotonesSinPI;
     |CONTROL_SIMPLE 0, "BOTON,{{193}}Alta ", 2011, 2021, AltaDireccion;
     nBoton50_1  = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Modificar ", 2023, 2033, ModificarDireccion;
     nBoton50_2  = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{199}}Borrar", 2035, 2045, BorrarDireccion;
     nBoton50_3  = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{199}}Borrar", 3135, 3145, BorrarLista;
     nBoton51_1  = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{199}}Ver direcciones", 3102, 3117, VerDirecciones;
     nBoton51_2  = FSalida;


     |CONTROL_SIMPLE 0, "BOTON,{{197}}Nueva lista", 3170, 3183, NuevaLista;
     nBoton977_1  = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Actualizar lista", 3185, 3198, ActualizarLista;
     nBoton977_2  = FSalida;

     |HAZ ControlaBotones;
|FPRC;

|PROCESO BotonesConPI;
     |CONTROL_SIMPLE 0, "BOTON,{{193}}Alta ", 1411, 1421, AltaDireccion;
     nBoton50_1  = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Modificar ", 1423, 1433, ModificarDireccion;
     nBoton50_2  = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{199}}Borrar", 1435, 1445, BorrarDireccion;
     nBoton50_3  = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{199}}Borrar", 3135, 3145, BorrarLista;
     nBoton51_1  = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{199}}Ver direcciones", 3102, 3117, VerDirecciones;
     nBoton51_2  = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Nueva lista", 3170, 3183, NuevaLista;
     nBoton977_1  = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Actualizar lista", 3185, 3198, ActualizarLista;
     nBoton977_2  = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{230}}Todas", 2535, 2545, TodasPI;

     |HAZ ControlaBotones;
|FPRC;

|PROCESO Graba77;
     |LEE 000#977p;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aAlfa = #977#0;   |QBF aAlfa;
     aAlfa = aAlfa - ";";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     #50#0 = #977#0;
     |LEE 000#50=;
     |SI FSalida = 0;  |ACABA;  |FINSI;

     |DDEFECTO #50;
     #50#0 = #977#0;
     |GRABA 020#50n;
     |LIBERA #50;
|FPRC;

|PROCESO PantaSinPI;
     aHastaZ  = "z" * 100;
     aFichero = ("77" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #977 aFichero;
     |ABRE #977;

     |CONTROL_SIMPLE 0, "LABEL,{{15}}Libreta de direcciones", 0502, 0545, NULL;
     |CONTROL_SIMPLE 0, "LABEL,{{15}}Lista de direcciones", 2202, 2245, NULL;
     |CONTROL_SIMPLE 0, "LABEL,{{15}}Direcciones asignadas", 0548, 0598, NULL;
     |CONTROL_SIMPLE 0, "LABEL,{{15}}Doble click en el registro para seleccionar o deseleccionar direcciones", 3302, 3380, NULL;

     nRango = 4 + 8 + 16 + 32 + 128;

     |ABRE #50;
     |ABRE #51;

     |HAZ Graba77;
     |HAZ BotonesSinPI;

     |LINEAL_SIMPLE #1#50, nRango, 0601, 1945, NULL, NULL, Evento50;
     nGrid50 = FSalida;

     |LINEAL_SIMPLE #1#51, nRango, 2301, 3045, NULL, NULL, Evento51;
     nGrid51 = FSalida;

     |LINEAL_SIMPLE #1#977, nRango, 0648, 3098, NULL, NULL, Evento977;
     nGrid977 = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::{{001}}Salir," + nGrid50;
          |LEETECLA aTecla;
          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
          |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |CIERRA #50;
     |CIERRA #51;
|FPRC;

|PROCESO Baja124;
     #dsam0124#0 = "";
     #dsam0124#3 = "!";
|FPRC;

|PROCESO Alta124;
     #dsam0124#0 = "z" * 40;
     #dsam0124#3 = "z" * 80;
|FPRC;

|PROCESO PantaConPI;
     aHastaZ  = "z" * 100;
     aFichero = ("77" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #977 aFichero;
     |ABRE #977;

     |CONTROL_SIMPLE 0, "LABEL,{{15}}Libreta de direcciones GAD", 0502, 0545, NULL;
     |CONTROL_SIMPLE 0, "LABEL,{{15}}Lista de direcciones", 2602, 2645, NULL;
     |CONTROL_SIMPLE 0, "LABEL,{{15}}Lista de direcciones PI", 1502, 1545, NULL;
     |CONTROL_SIMPLE 0, "LABEL,{{15}}Direcciones asignadas", 0548, 0598, NULL;
     |CONTROL_SIMPLE 0, "LABEL,{{15}}Doble click en el registro para seleccionar o deseleccionar direcciones", 3302, 3380, NULL;

     nRango = 4 + 8 + 16 + 32 + 128;

     |ABRE #50;
     |ABRE #51;
     |ABRET #dsam0124;
     |ABRET #dsam0000;

     |HAZ Graba77;
     |HAZ BotonesConPI;

     |DBASS aBase;  |QBF aBase;
     eaPathDef     = aBase + "integral/def/";
     eaFichRegedit = "dsam0124";
     |HAZ BorraDelRegedit;

     |C_V #dsam0124#0, 1, "S";
     |C_V #dsam0124#1, 1, "N";

     |LINEAL_SIMPLE #1#50, nRango, 0601, 1345, NULL, NULL, Evento50;
     nGrid50 = FSalida;

     |LINEAL_SIMPLE #2#dsam0124, nRango, 1601, 2445, Baja124, Alta124, Evento124;
     nGrid124 = FSalida;

     |LINEAL_SIMPLE #1#51, nRango, 2701, 3045, NULL, NULL, Evento51;
     nGrid51 = FSalida;

     |LINEAL_SIMPLE #1#977, nRango, 0648, 3098, NULL, NULL, Evento977;
     nGrid977 = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::{{001}}Salir," + nGrid50;
          |LEETECLA aTecla;
          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
          |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |CIERRA #50;
     |CIERRA #51;
     |CIERRAT #dsam0124;
     |CIERRAT #dsam0000;
|FPRC;

|PROGRAMA;
     |SI nPI = 0;
         |HAZ PantaSinPI;
     |FINSI;

     |SI nPI = 1;
         |HAZ PantaConPI;
     |FINSI;
|FPRO;

|PROCESO Tipo80;  |TIPO 80;
     nPI = 0;

     |DBASS aBase;
     aMas = aBase + "integral/def/dsam0124.mas";
     |XABRE "E", aMas, 0;
     |SI FSalida ] 0;
         |ABRE #dsam0124;
         |LEE 000#dsam0124.p;
         |SI FSalida = 0;
             nPI = 1;
         |FINSI;
         |CIERRA #dsam0124;
     |FINSI;

     FSalida = 3399;
|FPRC;
