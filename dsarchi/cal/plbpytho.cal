|FICHEROS;
|FIN;

|VARIABLES;
     nHandle         = 0;
     nHandle1        = 0;
     nLong           = 0;
     nVoy            = 0;
     nSaca           = 0;

     aBass           = "";
     aBase           = "";
     aLong           = "";
     aPathTmp        = "";
     aSystema        = "";
     aVersion        = "";
     aFicTmp         = "";
     aLee            = "";
     aLetra          = "";
     aPathPlu        = "";
     aAbreShell      = "";
     aBaseAux        = "";
     aEjecuta        = "";
     aPython         = "";

     &enErrorPy      = 0;
     &eaMensaPy      = "";
     &eaParamA       = "";
     &eaParamC       = "";
     &eaParamD       = "";
     &eaParamM       = "";
     &eaParamO       = "";
     &eaParamU       = "";
     &eaParamT       = "";
|FIN;

|PROCESO QuePython;
     aPython  = "python2.7";
|FPRC;

|PROCESO VersionPython;
     |HAZ QuePython;

     |DBASE aBase;
     aPathTmp = aBase + "tmp";            |MKDIR aPathTmp;
     aPathTmp = aPathTmp + "/" + Usuario;    |MKDIR aPathTmp;
     aPathTmp = aPathTmp + "/";
     aFicTmp  = aPathTmp + "versionpython.txt";

     aSystema = aPython + " --version 2> " + aFicTmp;
     |SYSTEM aSystema;

     enErrorPy = 0;
     eaMensaPy = "";
     |XABRE "E", aFicTmp, 0;
     |SI FSalida < 0;
         enErrorPy = 1;
         eaMensaPy = "El python no est  instalado en el servidor";
         |ACABA;
     |FINSI;

     |XABRE "", aFicTmp, nHandle;
     |XLEE nHandle, 255, aLee;
     |XCIERRA nHandle;

     aVersion = aLee - "Python ";  |QBT aVersion;
     |SI aVersion < "2.7.10";
         enErrorPy = 1;
         eaMensaPy = "Se requiere Python 2.7.10 (instalada " + aVersion + ")";
     |FINSI;
|FPRC;

|PROCESO QuitaBarras;
     |QBF aBass;
     eaParamC = aBass % -204;
|FPRC;

|PROCESO LanzaPush;
     |HAZ QuePython;

     |DBASS aBass;  |QBF aBass;
     |DBASE aBase;  |QBF aBase;

     aPathTmp = aBase + "tmp";               |MKDIR aPathTmp;
     aPathTmp = aPathTmp + "/" + Usuario;    |MKDIR aPathTmp;
     aPathTmp = aPathTmp + "/";

     aPathPlu = aBase + "plugins/";

     |HAZ QuitaBarras;

     eaParamO = aPathTmp + "salida.txt";

     aAbreShell = aPathTmp + "lanzapush.sh";
     |XABRE "WB", aAbreShell, nHandle;

     aEjecuta = aPython + " " + aPathPlu + "push.py";
     aEjecuta = aEjecuta + " -e cp437";

     |SI eaParamT ! "";  aEjecuta = aEjecuta + " -t=" + &34 + eaParamT + &34;  |FINSI;
     |SI eaParamC ! "";  aEjecuta = aEjecuta + " -c=" + &34 + eaParamC + &34;  |FINSI;
     |SI eaParamU ! "";  aEjecuta = aEjecuta + " -u=" + &34 + eaParamU + &34;  |FINSI;
     |SI eaParamA ! "";  aEjecuta = aEjecuta + " -a=" + &34 + eaParamA + &34;  |FINSI;
     |SI eaParamO ! "";  aEjecuta = aEjecuta + " -o=" + &34 + eaParamO + &34;  |FINSI;
     |XGRABA nHandle, aEjecuta;

     |SI eaParamD ! "";
         aEjecuta = " -d ";
         |XGRABA nHandle, aEjecuta;

         |XABRE "", eaParamD, nHandle1;
         |PARA;  |SI;  |HACIENDO;
              |XLEE nHandle1, 255, aLee;
              |SI FSalida [ 0;  |SAL;  |FINSI;

              aEjecuta = aLee;
              |XGRABA nHandle, aEjecuta;
         |SIGUE;
         |XCIERRA nHandle1;
     |FINSI;

     |SI eaParamM ! "";
         aEjecuta = " -m=" + &34 + eaParamM + &34;
         |XGRABA nHandle, aEjecuta;
     |FINSI;

     aEjecuta = &10;
     |XGRABA nHandle, aEjecuta;

     |XCIERRA nHandle;

     aEjecuta = "chmod 755 " + aAbreShell;
     |SYSTEM aEjecuta;

     |SYSTEM aAbreShell;

     aLee = "";
     |XABRE "", eaParamO, nHandle;
     |XLEE nHandle, 255, aLee;
     |XCIERRA nHandle;

     eaMensaPy = "Ultimo push: " + aLee;

     |FBORRA eaParamO;
     |FBORRA eaParamD;
|FPRC;
