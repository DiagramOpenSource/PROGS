|FICHEROS;
     dsarz042 #0;

     dsarm042 #42,MANTE;       ||Lin.Borra G/S/Tipo

     dsarm001 #1;
     dsarm002 #2;
     dsarm003 #3;
     dsarm004 #4;
     dsarm005 #5;
     dsarm006 #6;        ||Entrada/Salidas Doc.
     dsarm007 #7;        ||Entrada/Salidas Doc.
     dsarm030 #30;
     dsarm031 #31;
     dsarm034;           ||Resultado dsEscan
     dsarm035 #35;       ||VAR. Resul. dsEscan
|FIN;

|VARIABLES;
     &enPassword = 0;
     aUbiActual = "";
     aUbiAnterior = "";
     aAuxDos = "";

     aCarpeta = "";
     aAlfa = "";
     aLong = "";
     nLong = 0;
     nSaca = 0;
     aSaca = "";
     aFichero = "";
     aFicheroOcr = "";
     aVacio25 = "";
     aZeta25 = "";
     nBorrados = 0;
     aParametro = "";
     nSwSal = 0;

     nGrupo = 0;
     nDSubGrupo = 0;
     nHSubGrupo = 0;
     nDTipo = 0;
     nHTipo = 0;

     nExiste = 0;
     aMensaje = "";
     aRespuesta = "";

     aTexto = "";
|FIN;

|PROCESO min;
     #42#0 = 0;
     #42#1 = 0;
     #42#2 = 0;
|FPRC;

|PROCESO max;
     #42#0 = 999;
     #42#1 = 999;
     #42#2 = 999;
|FPRC;

|PROCESO dsarm030_borra;
     |BORRA 020#dsarm030.a;
|FPRC;

|DEFBUCLE dsarm030_borra;
     #dsarm030#1;
     ;
     #dsarm005#0,     0;
     #dsarm005#0, 99999;
     be;
     NULL, dsarm030_borra;
|FIN;

|PROCESO dsarm031_borra;
     |BORRA 020#dsarm031.a;
|FPRC;

|DEFBUCLE dsarm031_borra;
     #dsarm031#1;
     ;
     #dsarm005#0,   0;
     #dsarm005#0, 999;
     be;
     NULL, dsarm031_borra;
|FIN;

|PROCESO dsarm006_borra;
     |BORRA 020#dsarm006.a;
|FPRC;

|DEFBUCLE dsarm006_borra;
     #dsarm006#1;
     ;
     #dsarm005#0, 001;
     #dsarm005#0, 999;
     be;
     NULL, dsarm006_borra;
|FIN;

|PROCESO dsarm007_1B;
     |BORRA 020#7a;
     |LIBERA #7;
|FPRC;

|DEFBUCLE dsarm007_1B;
     #7#1;
     ;
     #5#0, 000000001;
     #5#0, 999999999;
     be;
     NULL, dsarm007_1B;
|FIN;

|PROCESO dsarm007_2B;
     |BORRA 020#7a;
     |LIBERA #7;
|FPRC;

|DEFBUCLE dsarm007_2B;
     #7#2;
     ;
     #5#0, 000000001;
     #5#0, 999999999;
     be;
     NULL, dsarm007_2B;
|FIN;


|PROCESO dsarm035_borra;
     |BORRA 020#dsarm035.a;
|FPRC;

|DEFBUCLE dsarm035_borra;
 #dsarm035#1;
 ;
 #dsarm034#0, #dsarm034#1, #dsarm034#2, aVacio25;
 #dsarm034#0, #dsarm034#1, #dsarm034#2, aZeta25;
 be;
 NULL, dsarm035_borra;
|FIN;

|PROCESO dsarm034_borra;
     |BUCLE dsarm035_borra;
     |BORRA 020#dsarm034.a;
|FPRC;

|DEFBUCLE dsarm034_borra;
 #dsarm034#2;
 ;
 #dsarm005#0;
 #dsarm005#0;
 be;
 NULL, dsarm034_borra;
|FIN;

|PROCESO dsarm005_borra;
     aFichero = #dsarm005#7;              |QBF aFichero;

     |SI aUbiActual ! "";
          aAuxDos = aFichero - aUbiAnterior;
          |SI aAuxDos ! aFichero;
               aFichero = aUbiActual + aAuxDos;
          |FINSI;
     |FINSI;

     aFichero = aFichero + #dsarm005#9;   |QBF aFichero;

     aFicheroOcr = aFichero + ".txt";
     |SI #1#7 = "N";
         |FBORRA aFichero;
         |FBORRA aFicheroOcr;
     |SINO;
         |RFBORRA aFichero;
         |RFBORRA aFicheroOcr;
     |FINSI;

     |BUCLE dsarm006_borra;
     |BUCLE dsarm007_1B;
     |BUCLE dsarm007_2B;
     |BUCLE dsarm030_borra;
     |BUCLE dsarm031_borra;

     aVacio25 = (" " * 25);
     aZeta25 = ("z" * 25);
     |BUCLE dsarm034_borra;    ||Borramos los restos del dsEscan

     |BORRA 020#5a;

     nBorrados = nBorrados + 1;
|FPRC;

|DEFBUCLE dsarm005_borra;
 #dsarm005#2;
 ;
 nGrupo, nDSubGrupo, nDTipo, 000000001;
 nGrupo, nHSubGrupo, nHTipo, 999999999;
 be;
 NULL, dsarm005_borra;
|FIN;

|PROCESO dsarm042;
     nGrupo = #dsarm042#0;

     nDSubGrupo = #dsarm042#1;
     |SI nDSubGrupo = 0;
          nDSubGrupo = 1;
          nHSubGrupo = 999;
     |SINO;
          nHSubGrupo = nDSubGrupo;
     |FINSI;

     nDTipo = #dsarm042#2;
     |SI nDTipo = 0;
          nDTipo = 1;
          nHTipo = 999;
     |SINO;
          nHTipo = nDTipo;
     |FINSI;

     |BUCLE dsarm005_borra;
|FPRC;

|DEFBUCLE dsarm042;
 #dsarm042#1;
 ;
 INICIO;
 FINAL;
 ;
 NULL, dsarm042;
|FIN;

|PROCESO Principal;
     |ABRE #dsarm001;
     |DDEFECTO #dsarm001;
     |LEE 000#dsarm001.p;
     |CIERRA #dsarm001;

     |LEE 000#dsarz042.=;
     aUbiActual = #dsarz042#1;
     |QBF aUbiActual;

     aUbiAnterior = #dsarm001#1;
     |QBF aUbiAnterior;

     nBorrados = 0;
     |BUCLE dsarm042;

     |SI aParametro ! "AUTO";
          |SI nBorrados ! 0;
               aMensaje = "0000Borrados " + nBorrados + " registros";
               |MENAV aMensaje;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo11C0; |TIPO 11;
     |SI FSalida = 7;
          nSwSal = 1;
     |FINSI;
|FPRC;

|PROCESO Tipo11C1; |TIPO 11;
     |SI FSalida = 7;
          nSwSal = 1;
     |FINSI;
|FPRC;

|PROCESO Tipo11C2; |TIPO 11;
     |SI FSalida = 7;
          nSwSal = 1;
     |FINSI;
|FPRC;

|PROCESO Tipo7C0; |TIPO 7;
     |PINTA #dsarz042#1;
|FPRC;

|PROCESO Tipo0C0; |TIPO 0;
     |SI #dsarm042#0 = 0;
          aMensaje = "0000No esta permitido el borrado de todos los grupos a la vez, por motivos de seguridad";
          |MENAV aMensaje;
          |ERROR;
     |SINO;
          |ABRE #dsarm002;
          |DDEFECTO #dsarm002;
          #dsarm002#0 = #dsarm042#0;
          |LEE 000#dsarm002.=;
          |SI FSalida = 0;
               #dsarm042#3 = #dsarm002#1;
          |SINO;
               aTexto = "*GRUPO " + #dsarm042#0;
               |QBF aTexto;
               aTexto = aTexto + "*";
               #dsarm042#3 = aTexto;
          |FINSI;
          |CIERRA #dsarm002;
          |PINTA #dsarm042#3;
     |FINSI;
|FPRC;

|PROCESO Tipo0C1_z042; |TIPO 0;
     aCarpeta = #dsarz042#1;
     |QBF aCarpeta;
     aAlfa = aCarpeta % -101;
     |SI aAlfa = "/"; |O aAlfa = "\";
          aLong = aCarpeta % A0;
          nLong = aLong;
          nSaca = nLong - 1;
          nSaca = 100 + nSaca;
          aSaca = nSaca;
          aCarpeta = aCarpeta % aSaca;
          #dsarz042#1 = aCarpeta;
          |PINTA #dsarz042#1;
     |FINSI;
|FPRC;

|PROCESO Tipo0C1; |TIPO 0;
     |SI #dsarm042#1 = 0;
          aTexto = "*TODOS*";
          #dsarm042#4 = aTexto;
     |SINO;
          |ABRE #dsarm003;
          |DDEFECTO #dsarm003;
          #dsarm003#0 = #dsarm042#0;
          #dsarm003#1 = #dsarm042#1;
          |LEE 000#dsarm003.=;
          |SI FSalida = 0;
               #dsarm042#4 = #dsarm003#2;
          |SINO;
               aTexto = "*SUBGRUPO " + #dsarm042#1;
               |QBF aTexto;
               aTexto = aTexto + "*";
               #dsarm042#4 = aTexto;
          |FINSI;
          |CIERRA #dsarm003;
     |FINSI;
     |PINTA #dsarm042#4;
     |HAZ Tipo0C0;
|FPRC;

|PROCESO Tipo0C2; |TIPO 0;
     |SI #dsarm042#2 = 0;
          aTexto = "*TODOS*";
          #dsarm042#5 = aTexto;
     |SINO;
          |ABRE #dsarm004;
          |DDEFECTO #dsarm004;
          #dsarm004#0 = #dsarm042#0;
          #dsarm004#1 = #dsarm042#1;
          #dsarm004#2 = #dsarm042#2;
          |LEE 000#dsarm004.=;
          |SI FSalida = 0;
               #dsarm042#5 = #dsarm004#3;
          |SINO;
               aTexto = "*TIPO " + #dsarm042#2;
               |QBF aTexto;
               aTexto = aTexto + "*";
               #dsarm042#5 = aTexto;
          |FINSI;
          |CIERRA #dsarm004;
     |FINSI;
     |PINTA #dsarm042#5;
     |HAZ Tipo0C0;
     |HAZ Tipo0C1;
|FPRC;

|PROGRAMA;
     aParametro = PARAMETRO;
     |QBF aParametro;
     |SI aParametro = "AUTO";
          |ABRE #0;
          |HAZ Principal;
          |CIERRA #0;
     |SINO;
          enPassword = 0;
          |HAZ PidePassword9;
          |SI enPassword = 1;
               aMensaje = "0000Password incorrecto. Proceso cancelado";
               |MENAV aMensaje;
               |ACABA;
          |FINSI;

          |CLS;
          |ABRE #0;
          |ABRE #42;
          |DDEFECTO #0;
          |LEE 101#0p;
          nExiste = FSalida;
          |PINPA #0#0;
          |PINDA #0#0;

          |ENDATOS #1#0;
          |SI FSalida ! 0;
               |LIBERA #0;
               |CIERRA #0;
               |CIERRA #42;
               |ACABA;
          |SINO;
               |SI nExiste = 0;
                    |GRABA 020#0a;
               |SINO;
                    |GRABA 020#0n;
               |FINSI;
               |LIBERA #0;
          |FINSI;

          |ENTLINEAL #1#42, 1, 2, 22, 1, min, max;
          |SI FSalida = 0;
               |SI nSwSal = 1;
                    ||Proceso Cancelado   Control + Q
               |SINO;
                    aMensaje = "0000NEste proceso borrara todos los registros que coincidan con el Grupo/Subgrupo/Tipo. Continuar?";
                    |CONFI aMensaje;
                    |SI FSalida = 0;
                         aMensaje = "0000NLos registros se borraran permanentemente. Esta Seguro?";
                         |CONFI aMensaje;
                         |SI FSalida = 0;
                              |HAZ Principal;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
          |CIERRA #0;
          |CIERRA #42;
     |FINSI;
|FPRO;
