|FICHEROS;
     dsarw029 #929;
     dsarm009 #9;

     dsarw037 #937,MANTE;
|FIN;

|VARIABLES;
     &eaPathAntes = "";
     aPathAhora = "";
     &enAdaptarPath = 0;

     aExiste = "";
     &enBotonXSL = 0;
     aLong = "";
     nLong = 0;
     nSaca = 0;
     aSaca = "";

     nVenCache = 0;
     aEjecuta = "";
     nError         = 0;
     nHandle        = 0;
     nBotonCache    = -1;
     nBotonPatrones = -1;
     nVentana       = -1;
     nRango         = -1;
     nPos1          = -1;
     nPos2          = -1;
     nGrid9         = -1;

     aOrigen        = "";
     aDestino       = "";
     aCarpeta       = "";
     aMensa         = "";
     aMensaje       = "";
     aAlfa          = "";
     aSistema       = "";
     aContenido1    = "";
     aContenido2    = "";
     aIP            = "";
     aAux           = "";
     aBase          = "";
     aAbre          = "";
     aLinea         = "";
     aUsuario       = "";
     aEsc1          = "";
     aEsc2          = "";
     aRetu          = "";
     aTecla         = "";

     {1}aBaseLocal  = "";

     {-1}get        = "";
         get1       = 0;
         get2       = 0;
         get3       = 0;
         get4       = 0;
         get5       = "";
|FIN;

|PROCESO CargaUsuarios;
     |DBASS aBase;  |QBF aBase;
     aAbre = aBase + "dev/usr/ds_sys.psw";

     |XABRE "U", aAbre, nHandle;
     |SI FSalida ] 0;
          |PARA; |SI; |HACIENDO;
               |XLEE nHandle, 250, aLinea;
               |SI FSalida < 0; |SAL; |FINSI;
               aUsuario = aLinea % 410;
               |QBF aUsuario;
               |SI aUsuario ! "";
                    |DDEFECTO #dsarm009;
                    #dsarm009#0 = aUsuario;
                    |LEE 101#dsarm009.=;
                    |SI FSalida ! 0;
                         |GRABA 020#dsarm009.n;
                    |FINSI;
                    |LIBERA #dsarm009;
               |FINSI;
          |SIGUE;
          |XCIERRA nHandle;
     |FINSI;
|FPRC;

|PROCESO Evento9;
     |SI FSalida = 4;

          |VENTANA 1655, 2095, -1, 1, "";
          nVenCache = FSalida;

          get1 = -1;
          get2 = -1;
          get3 = 0;
          get4 = nVenCache;
          get5 = "MODAL";
          |ESPECIFICA "ESTADO_VENTANA", get;

          #937#0 = #dsarm009#1;
          |PINPA #0#937;
          |PINDA #0#937;
          |ENDATOS #1#937;
          |SI FSalida = 0;
               #dsarm009#1 = #937#0;
               |GRABA 020#dsarm009.a;
               |LIBERA #dsarm009;
               |REFRESCACONTROL nGrid9;
          |FINSI;

          get1 = -1;
          get2 = -1;
          get3 = 0;
          get4 = nVenCache;
          get5 = "MODELESS";
          |ESPECIFICA "ESTADO_VENTANA", get;

          |FINVENTANA nVenCache;
     |FINSI;
|FPRC;

|PROCESO BotonCache;
     |VENTANA 0852, 2798, -1, 1, "";
     nVentana = FSalida;

     get1 = -1;
     get2 = -1;
     get3 = 0;
     get4 = nVentana;
     get5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", get;

     |ABRE #dsarm009;

     |HAZ CargaUsuarios;

     |PINPA #0#dsarm009;

     nRango = 1 + 4 + 8 + 16 + 32;
     nPos1 = 1053;
     nPos2 = 2795;

     |LINEAL_SIMPLE #1#dsarm009, nRango, nPos1, nPos2, NULL, NULL, Evento9;
     nGrid9 = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}Salir," + nGrid9;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |CIERRA #dsarm009;

     get1 = -1;
     get2 = -1;
     get3 = 0;
     get4 = nVentana;
     get5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", get;

     |FINVENTANA nVentana;
|FPRC;

|PROCESO Tipo12Fw29;  |TIPO 12;
     |SI FSalida ! 7;

          aPathAhora = #929#0;
          |QBF aPathAhora;
          |SI aPathAhora ! eaPathAntes;
               aMensaje = "0000NEl cambio de ubicaci¢n adaptar  el registro de documentos a la nueva ubicaci¢n. Cambiar?";
               |CONFI aMensaje;
               |SI FSalida = 0;
                    ||Okie. Hay que cambiar el path en el registro.
                    enAdaptarPath = 1;
               |SINO;
                    enAdaptarPath = 0;
                    #929#0 = eaPathAntes;
                    |PINTA #929#0;
                    |POSICIONATE #929#0;
                    |ERROR;
               |FINSI;
          |SINO;
               enAdaptarPath = 0;
          |FINSI;
     |FINSI;

     |FIN_CONTROL_WINDOWS nBotonCache;
     |FIN_CONTROL_WINDOWS nBotonPatrones;

     nBotonCache = -1;
     nBotonPatrones = -1;
|FPRC;

|PROCESO CompruebaServidor;
     nError = 0;
     |XABRE "E", aDestino, nHandle;
     |SI FSalida < 0;
         |MENAV "0000Ubicaci¢n err¢nea."
         |ERROR;
         nError = 1;
     |SINO;
         |FBORRA aDestino;
     |FINSI;
|FPRC;

|PROCESO CompruebaLocal;
     nError = 0;
     |XABRE "EC", aDestino, nHandle;
     |SI FSalida < 0;
         |MENAV "0000Ubicaci¢n err¢nea."
         |ERROR;
         nError = 1;
     |SINO;
         |RFBORRA aDestino;
     |FINSI;
|FPRC;

|PROCESO Tipo7C2Fw29;  |TIPO 7;
     |SI nBotonCache ! -1;  |ACABA;  |FINSI;

     |CONTROL_SIMPLE 0, "BOTON,Unidad Cache por Usuarios", 2702, 2725, BotonCache;
     nBotonCache = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,Actualizar Patrones FacturaE", 2729, 2754, BotonPatrones;
     nBotonPatrones = FSalida;

     |HAZ Tipo0C1Fw29;
|FPRC;

|PROCESO BotonPatrones;
     aEjecuta = "dsarz012";
     |SUB_EJECUTA_NP aEjecuta;
|FPRC;


|PROCESO Tipo0C0Fw29;  |TIPO 0;
     |SI FSalida = 2;  |ACABA;  |FINSI;

     aCarpeta = #929#0;
     |QBF aCarpeta;
     |SI aCarpeta = "";
          aMensa = "0000Por favor indique la ubicaci¢n."
          |MENAV aMensa;
          |ERROR;
          |ACABA;
     |FINSI;

     aAlfa = aCarpeta % -101;
     |SI aAlfa = "/"; |O aAlfa = "\";
          aLong = aCarpeta % A0;
          nLong = aLong;
          nSaca = nLong - 1;
          nSaca = 100 + nSaca;
          aSaca = nSaca;
          aCarpeta = aCarpeta % aSaca;
          #929#0 = aCarpeta;
          |PINTA #929#0;
     |FINSI;

     |SI Campo = 1;
         |SI #929#1 = "S";
             aAlfa = #929#0 % 102;
             |SI aAlfa ! "\\";
                 |MENAV "0000 Se deberia introducir una Ruta de la Red Local, ya que sino solo podra acceder este PC.";
             |FINSI;
         |SINO;
             |QUE_SISTEMA aSistema;
             |QBF aSistema;
             |SI aSistema ! "ESDOS";
                  aAlfa = #929#0 % 101;
                  |SI aAlfa ! "/";
                        aMensaje = "0000La ubicaci¢n de los documentos es local al servidor Unix.";
                        |MENAV aMensaje;
                        |ERROR;
                        |ACABA;
                  |FINSI;
             |FINSI;
         |FINSI;

         aContenido1 = Contenido;  |QBF aContenido1;
         aContenido2 = #929#0;       |QBF aContenido2;

         |SI aContenido1 ! aContenido2;
             |MENAV "0000 El cambio de ubicaci¢n implica el no poder visualizar los documentos ya existentes.";
         |FINSI;
     |FINSI;




     |SI #929#1 = "S";
          |RMKDIR aCarpeta;
     |SINO;
          |MKDIR aCarpeta;
     |FINSI;

     aBaseLocal1 = "";
     |ESPECIFICA "RBASS", aBaseLocal;  |QBF aBaseLocal1;

     aOrigen  = aBaseLocal1 + "logo.bmp";

     aDestino = #929#0;                             |QBF aDestino;
     aDestino = aDestino + "/" + "dstest.txt";

     |SI #929#1 = "N";
         aAlfa = #929#0 % 102;
         |SI aAlfa = "\\";
             |MENAV "0000 La ruta es para una Red Local, introduzca una Ruta valida.";
             #929#0 = "";  |PINTA #929#0;
             |ERROR;
             |ACABA;
         |FINSI;

         aIP = "";
         |IP_REMOTO aIP;
         |SI aIP ! "";
||             |RECIBE_FICHERO aOrigen, aDestino;
             |FBORRA aDestino;

             |XABRE "W", aDestino, nHandle;
             |SI FSalida ] 0;
                  aAux = "*Texto Test Escritura DS*";
                  |XGRABA nHandle, aAux;
                  |XCIERRA nHandle;
             |FINSI;

             |HAZ CompruebaServidor;
         |SINO;
||             |COPIA_FICHERO aOrigen, aDestino;
             |RFBORRA aDestino;

             |XABRE "CW", aDestino, nHandle;
             |SI FSalida ] 0;
                  aAux = "*Texto Test Escritura DS*";
                  |XGRABA nHandle, aAux;
                  |XCIERRA nHandle;
             |FINSI;
             |HAZ CompruebaLocal;
         |FINSI;
     |SINO;

||         |RCOPIA_FICHERO aOrigen, aDestino;
         |RFBORRA aDestino;

         |XABRE "CW", aDestino, nHandle;
         |SI FSalida ] 0;
              aAux = "*Texto Test Escritura DS*";
              |XGRABA nHandle, aAux;
              |XCIERRA nHandle;
         |FINSI;
         |HAZ CompruebaLocal;
     |FINSI;

     |SI nError = 1;
         |ERROR;
         |ACABA;
     |FINSI;
|FPRC;

|PROCESO Tipo0C1Fw29; |TIPO 0;
     |SI #929#1 = "S";
          |ESTADO_CONTROL nBotonCache, "DISABLE";
     |SINO;
          |ESTADO_CONTROL nBotonCache, "ENABLE";
     |FINSI;
|FPRC;

|PROCESO Tipo0C4Fw29; |TIPO 0;
     |SI #929#4 = "S";
         |C_M #929#5, 1, "S";
     |SINO;
         |C_M #929#5, 1, "N";
     |FINSI;
     |PINTA #929#5;
|FPRC;

|PROCESO Tipo0C9Fw29; |TIPO 0;
     |SI #929#9 = "S";
         |C_M #929#10, 1, "S";
     |SINO;
         |C_M #929#10, 1, "N";
     |FINSI;
     |PINTA #929#10;
|FPRC;

|PROCESO Tipo0C10Fw29; |TIPO 0;
     |SI FSalida = 7;  |ACABA;  |FINSI;

     |SI #929#9 = "S";
         aAux = #929#10;
         |QBF aAux;
         |SI aAux = "";
             aMensaje = "0000Introduzca la Carpeta Destino";
             |MENAV aMensaje;
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo12; |TIPO 12;
|FPRC;

|PROCESO Tipo0C6w029; |TIPO 0;
     aAux = #dsarw029#6;
     |QBF aAux;
     |SI aAux ! "3.0"; |Y aAux ! "3.1"; |Y aAux ! "3.2";
          aMensaje = "0000Soporte unicamente para Facturae versi¢n 3.0, 3.1 y 3.2";
          |MENAV aMensaje;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo0C7w029; |TIPO 0;
     |DBASE aBase;
     |QBF aBase;
     aExiste = aBase + "plugins/" + #dsarw029#7;
     |QBF aExiste;
     |XABRE "E", aExiste, 0;
     |SI FSalida < 0;
          aMensaje = "0000No existe fichero:" + &13 + aExiste;
          |MENAV aMensaje;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo0C8w029; |TIPO 0;
     |DBASE aBase;
     |QBF aBase;
     aExiste = aBase + "plugins/" + #dsarw029#8;
     |QBF aExiste;
     |XABRE "E", aExiste, 0;
     |SI FSalida < 0;
          aMensaje = "0000No existe fichero:" + &13 + aExiste;
          |MENAV aMensaje;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo7C0Fw29;  |TIPO 7;
     |SI #929#1 = "S";  |ACABA;  |FINSI;

     aAlfa = #929#0;  |QBF aAlfa;
     |SI aAlfa ! "";  |ACABA;  |FINSI;

     |DBASE aAlfa;  |QBF aAlfa;
     aAlfa = aAlfa + "gad";
     #929#0  = aAlfa;
     |PINTA #929#0;
|FPRC;
