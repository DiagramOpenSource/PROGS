|FICHEROS;
     dsarm001;
     dsarm002;
     dsarm003;
     dsarm004;

     dpntm001;
     dpntm003;

     dsarw115,MANTE;
|FIN;

|VARIABLES;
     nSwCopiaSubEnMismoGrupo = 0;

     nBotonLibresOn  = 0;
     nBotonLibresOff = 0;
     nVentmCL        = 0;
     nVoy            = 0;
     nFSalida        = 0;
     nSwCorrecto     = 0;
     aGuardaTexto    = "";
     nGrupoMov       = 0;
     nSubGrupoMov    = 0;
     nTipoMov        = 0;
     nVent           = 0;
     nPantaAux       = 0;
     nRango          = 0;
     nPos1           = 0;
     nPos2           = 0;
     nGrid1          = 0;
     nGrid2          = 0;
     nUltimo         = 0;
     nSwSeleccion    = 0;
     nSwRefresca     = 0;
     nGrupoOri       = 0;
     nGrupoDes       = 0;
     nSubGrupoOri    = 0;
     nSubGrupoDes    = 0;
     nTipoOri        = 0;
     nTipoDes        = 0;
     nVentTrab       = 0;
     nValor          = 0;
     nBusco          = 0;
     nCuentaX        = 0;
     nSwBorraTot     = 0;
     nPanta          = 0;
     nHandle         = 0;
     nTree           = 0;
     nBotonLabel     = 0;
     nCampoD         = 0;
     nCampoH         = 0;
     nCarpeta1       = 0;
     nCarpeta2       = 0;
     nCarpeta3       = 0;
     nBoton          = 0;
     nLong           = 0;
     nCargar         = 0;
     nPosi           = 0;
     nPos            = 0;
     nVentm2         = 0;
     nDSubGrupo      = 0;
     nHSubGrupo      = 0;
     nPanta904       = 0;
     nPanta104       = 0;
     nGrupo          = 0;
     nSubGrupo       = 0;
     nTipo           = 0;
     nDS             = 0;
     nHS             = 0;
     nDT             = 0;
     nHT             = 0;

     aFichero        = "";
     aPathRemoto     = "";
     aPath           = "";
     aUbicacion      = "";
     aFechaSis       = "";
     aAAAAMM         = "";
     aDesDes         = "";
     aCarpetaDes     = "";
     aDesSGDes       = "";
     aCarpetaSGDes   = "";
     aEsc1           = "";
     aEsc2           = "";
     aRetu           = "";
     aTecla          = "";
     aAlfa           = "";
     aAbre           = "";
     aBusca          = "";
     aRefresca       = "";
     aTexto          = "";
     aLong           = "";
     aCarac          = "";
     aAux            = "";
     aAux2           = "";
     aAux3           = "";
     aMensaje        = "";
     aAlfa1          = "";
     aDirectorio     = "";
     aBase           = "";
     aDirec          = "";
     aNom            = "";

     fFechaSis       = @;

     {-1}sTree       = 0;
     sT_nID          = 0;
     sT_nOper        = 0;
     sT_aData        = "";

     {-1}aVent       =  "";
         aVent1      = 0;
         aVent2      = 0;
         aVent3      = 0;
         aVent4      = 0;
         aVent5      = "";

     &enGrupo        = 0;
     &enSubGrupo     = 0;
     &enTipo         = 0;
     &enModo         = 0;
     &enPassword     = 0;
     &enSwBorraFis   = 0;
     &eaGrupoBorra   = 0;
     &eaSubBorra     = 0;
     &eaTipoBorra    = 0;
     &eaRutaOrigen   = "";
     &eaRutaDestino  = "";
     &eaFicheroTxt   = "";
     &eaFicheroTgz   = "";
     &eaFicheroTar   = "";
     &eaAlfa         = "";
     &enCalDESPANET  = 0;
     &eaUnidadBasico = "";
     &enCambio       = "";
     &enCamposDet    = 0;
     &enSwMueve      = 0;
     &eaCarpetaOri   = "";
     &eaOrigen       = "";
     &eaDestino      = "";
     &eaUnidad       = "";
     &enEmpresa      = 0;
|FIN;

|PROCESO dpntm003G;
     |SI nTipo = 1;
         #dpntm003#7 = #dsarw115#0;
     |FINSI;

     |SI nTipo = 2;
         #dpntm003#8 = #dsarw115#0;
     |FINSI;

     |SI nTipo = 3;
         #dpntm003#9  = #dsarw115#0;
         #dpntm003#10 = #dsarw115#1;
         #dpntm003#11 = #dsarw115#2;
         #dpntm003#12 = #dsarw115#3;
     |FINSI;

     |GRABA 020#dpntm003.a;
     |LIBERA #dpntm003;
|FPRC;

|DEFBUCLE dpntm003G;
     #dpntm003#1;
     ;
     enEmpresa, enGrupo, nDS, nDT;
     enEmpresa, enGrupo, nHS, nHT;
     be;
     NULL, dpntm003G;
|FIN;

|PROCESO CambiaDescri;
     |VENTANA 0501, 1865, -1, 16, "Descripci¢n en DESPABOX";
     nVentm2 = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentm2;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     |DDEFECTO #dsarw115;
     |PINPA #0#dsarw115;

     |C_V #dsarw115#1, 1, "S";  |C_M #dsarw115#1, 1, "S";
     |C_V #dsarw115#2, 1, "S";  |C_M #dsarw115#2, 1, "S";
     |C_V #dsarw115#3, 1, "S";  |C_M #dsarw115#3, 1, "S";

     nFSalida = 0;

     |SI nTipo = 1;
         #dpntm003#0 = enEmpresa;
         #dpntm003#1 = enGrupo;
         #dpntm003#2 = 1;
         #dpntm003#3 = 1;
         |LEE 000#dpntm003.];
         |SI FSalida ! 0;              nFSalida = 1;  |FINSI;
         |SI #dpntm003#0 ! enEmpresa;  nFSalida = 1;  |FINSI;
         |SI #dpntm003#1 ! enGrupo;    nFSalida = 1;  |FINSI;

         #dsarw115#0 = #dpntm003#7;

         |C_V #dsarw115#1, 1, "N";  |C_M #dsarw115#1, 1, "N";
         |C_V #dsarw115#2, 1, "N";  |C_M #dsarw115#2, 1, "N";
         |C_V #dsarw115#3, 1, "N";  |C_M #dsarw115#3, 1, "N";

         |BLANCO 0901 1560;
     |FINSI;

     |SI nTipo = 2;
         #dpntm003#0 = enEmpresa;
         #dpntm003#1 = enGrupo;
         #dpntm003#2 = enSubGrupo;
         #dpntm003#3 = 1;
         |LEE 000#dpntm003.];
         |SI FSalida ! 0;               nFSalida = 1;  |FINSI;
         |SI #dpntm003#0 ! enEmpresa;   nFSalida = 1;  |FINSI;
         |SI #dpntm003#1 ! enGrupo;     nFSalida = 1;  |FINSI;
         |SI #dpntm003#2 ! enSubGrupo;  nFSalida = 1;  |FINSI;

         #dsarw115#0 = #dpntm003#8;

         |C_V #dsarw115#1, 1, "N";  |C_M #dsarw115#1, 1, "N";
         |C_V #dsarw115#2, 1, "N";  |C_M #dsarw115#2, 1, "N";
         |C_V #dsarw115#3, 1, "N";  |C_M #dsarw115#3, 1, "N";

         |BLANCO 0901 1560;
     |FINSI;

     |SI nTipo = 3;
         #dpntm003#0 = enEmpresa;
         #dpntm003#1 = enGrupo;
         #dpntm003#2 = enSubGrupo;
         #dpntm003#3 = enTipo;
         |LEE 000#dpntm003.=;
         |SI FSalida ! 0;               nFSalida = 1;  |FINSI;

         #dsarw115#0 = #dpntm003#9;
         #dsarw115#1 = #dpntm003#10;
         #dsarw115#2 = #dpntm003#11;
         #dsarw115#3 = #dpntm003#12;
     |FINSI;

     |SI nFSalida = 0;
         |PINDA #0#dsarw115;
         |ENDATOS #2#dsarw115;
          nFSalida = FSalida;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentm2;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVentm2;

     |SI nFSalida ! 0;  |ACABA;  |FINSI;

     |SI nTipo = 1;
         nDS = 0;
         nHS = 999;
         nDT = 0;
         nHT = 999;
     |FINSI;

     |SI nTipo = 2;
         nDS = enSubGrupo;
         nHS = enSubGrupo;
         nDT = 0;
         nHT = 999;
     |FINSI;

     |SI nTipo = 3;
         nDS = enSubGrupo;
         nHS = enSubGrupo;
         nDT = enTipo;
         nHT = enTipo;
     |FINSI;

     |BUCLE dpntm003G;
|FPRC;

|PROCESO SacaPantalla;
     aAlfa = aTexto - "TIPO";
     |SI aAlfa ! aTexto;
         aAlfa     = aTexto % -103;  enTipo       = aAlfa;
         aAlfa     = aTexto % -403;  enSubGrupo   = aAlfa;
         aAlfa     = aTexto % -703;  enGrupo      = aAlfa;
         nTipo     = 3;

         |HAZ CambiaDescri;

         |ACABA;
     |FINSI;

     aAlfa = aTexto - "SUBGRUPO";
     |SI aAlfa ! aTexto;
         aAlfa     = aTexto % -103;  enSubGrupo   = aAlfa;
         aAlfa     = aTexto % -403;  enGrupo      = aAlfa;
         nTipo = 2;

         |HAZ CambiaDescri;

         |ACABA;
     |FINSI;

     aAlfa = aTexto - "GRUPO";
     |SI aAlfa ! aTexto;
         aAlfa     = aTexto % -103;
         enGrupo   = aAlfa;
         nTipo     = 1;
         |SI #dsarm001#8 = "N";
             enSubGrupo = 1;
             enTipo     = 1;
             nTipo      = 3;
         |FINSI;

         |HAZ CambiaDescri;

         |ACABA;
     |FINSI;
|FPRC;

|PROCESO EventoTree;
     |SI FEntrada = 0;
         aAlfa       = Contenido;  |QBF aAlfa;
         aTexto      = "";
         aLong       = aAlfa % 0;
         nLong       = aLong;
         nCargar     = 0;
         |PARA nPosi = 1;  |SI nPosi [ nLong;  |HACIENDO nPosi = nPosi + 1;
               nPos = (nPosi * 100) + 1;
               aCarac = aAlfa % nPos;

               |SI nCargar = 1;
                   aTexto = aTexto + aCarac;
               |FINSI;

               |SI aCarac = ":";  nCargar = 1; |FINSI;
         |SIGUE;

         |ACABA;
     |FINSI;

     |HAZ SacaPantalla;

     aBusca = aTexto;
     |HAZ MontaArbol;
|FPRC;

|PROCESO CargaArbol;
     |QBF aAlfa;
     aAlfa = aAlfa + &13 + &10;
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO dpntm003;
     |SI nGrupo ! #dpntm003#1;
         nCarpeta1 = nCarpeta1 + 1;
         nCarpeta2 = 0;
         nCarpeta3 = 0;
         nSubGrupo = 0;
         nTipo     = 0;

         aNom   = #dpntm003#7;  |QBF aNom;
         aAlfa  = nCarpeta1 + ":" + (("000" + #dpntm003#1) % -103);
         aAlfa  = aAlfa + " - " + aNom;
         aAlfa  = aAlfa + "{{GRUPO" + (("000" + #dpntm003#1) % -103) + "}}";
         |HAZ CargaArbol;
     |FINSI;

     |SI nSubGrupo ! #dpntm003#2;
         nCarpeta2 = nCarpeta2 + 1;
         nCarpeta3 = 0;
         nTipo     = 0;

         aNom   = #dpntm003#8;  |QBF aNom;
         aAlfa  = nCarpeta1 + "," + nCarpeta2 + ":";
         aAlfa  = aAlfa + (("000" + #dpntm003#2) % -103) + " - " + aNom;
         aAlfa  = aAlfa + "{{SUBGRUPO" + (("000" + #dpntm003#1) % -103);
         aAlfa  = aAlfa + (("000" + #dpntm003#2) % -103) + "}}";
         |HAZ CargaArbol;
     |FINSI;

     |SI nTipo ! #dpntm003#3;
         nCarpeta3 = nCarpeta3 + 1;

         aNom   = #dpntm003#9;  |QBF aNom;
         |SI #dpntm003#10 = "S";
             aNom = aNom + " (Se muestra en compartir como " + #dpntm003#11;  |QBF aNom;
             aNom = aNom + ")";
         |FINSI;

         aAlfa  = nCarpeta1 + "," + nCarpeta2 + "," + nCarpeta3 + ":";
         aAlfa  = aAlfa + (("000" + #dpntm003#3) % -103) + " - " + aNom;
         aAlfa  = aAlfa + "{{TIPO" + (("000" + #dpntm003#1) % -103);
         aAlfa  = aAlfa + (("000" + #dpntm003#2) % -103);
         aAlfa  = aAlfa + (("000" + #dpntm003#3) % -103) + "}}";
         |HAZ CargaArbol;
     |FINSI;

     nGrupo    = #dpntm003#1;
     nSubGrupo = #dpntm003#2;
     nTipo     = #dpntm003#3;
|FPRC;

|DEFBUCLE dpntm003;
     #dpntm003#1;
     ;
     enEmpresa, 000, 000, 000;
     enEmpresa, 999, 999, 999;
     ;
     NULL, dpntm003;
|FIN;

|PROCESO MontaArbol;
     |SI nTree ! -1;
         |FIN_CONTROL_WINDOWS nTree;
         nTree = -1;
     |FINSI;

     |CONTROL_SIMPLE 0, "LABEL,{{5}}Cargando informaci¢n ...", 1315, 1680, NULL;
     nBotonLabel = FSalida;

     |PTEC 802;  |PAUSA;

     |DBASE aBase;  |QBF aBase;
     aAlfa = aBase + "tmp";          |MKDIR aAlfa;

     eaRutaOrigen  = aBase + "tmp/";

     |SI #dsarm001#7 = "N";
          |HAZ DimeUnidad;
          eaRutaDestino = eaUnidad + ":\documentos\tmp";  |RMKDIR eaRutaDestino;
          eaRutaDestino = eaUnidad + ":\documentos\tmp\";
     |SINO;
          |HAZ LeeUnidadBasico;
          eaRutaDestino = eaUnidadBasico + "\documentos\tmp";   |RMKDIR eaRutaDestino;
          eaRutaDestino = eaUnidadBasico + "\documentos\tmp\";
     |FINSI;
     eaFicheroTxt  = "dpntm00301" + Usuario + ".txt";
     eaFicheroTgz  = "dpntm00301" + Usuario + ".tgz";
     eaFicheroTar  = "dpntm00301" + Usuario + ".tar";

     aAbre = eaRutaOrigen + eaFicheroTxt;

     sT_nID   = nTree;
     sT_nOper = 0;

     |XABRE "WB", aAbre, nHandle;

     nGrupo    = 0;
     nSubGrupo = 0;
     nTipo     = 0;
     nCarpeta1 = 0;
     nCarpeta2 = 0;
     nCarpeta3 = 0;

     |ABRE #dsarm002;
     |ABRE #dsarm003;
     |ABRE #dsarm004;

     |BUCLE dpntm003;

     |CIERRA #dsarm002;
     |CIERRA #dsarm003;
     |CIERRA #dsarm004;

     |XCIERRA nHandle;

     |HAZ TraeTxtComprimido;
     aAlfa = eaRutaDestino + eaFicheroTxt;

     eaAlfa = "TREECTRL,Configuraci¢n personalizada de la estructura de documentos";
     eaAlfa = eaAlfa +  "{{" + aAlfa + "}}";
     |CONTROL_SIMPLE nPanta, eaAlfa, 0502, 2698, EventoTree;
     nTree = FSalida;

     eaFicheroTxt  = "dpntm00301" + Usuario + ".txt";
     aAlfa         = eaRutaDestino + eaFicheroTxt;
     |RFBORRA aAlfa;

     |FIN_CONTROL_WINDOWS nBotonLabel;

     |SI aBusca ! "";
         sT_nID   = nTree;
         sT_nOper = 3;
         sT_aData = "[" + aBusca;
         |ESPECIFICA "COMMANDCONTROL", sTree;
         aRefresca = ":" + FSalida;
     |FINSI;

     sT_nID   = nTree;
     sT_nOper = 2;
     |SI aRefresca = "";
         sT_aData = "1:";
     |SINO;
         sT_aData = aRefresca;
     |FINSI;

     |ESPECIFICA "COMMANDCONTROL", sTree;
     |PULSATECLA;

     |FOCOTECLADO nTree;
|FPRC;

|PROCESO dsarm004;
     #dsarm002#0 = #dsarm004#0;
     |LEE 000#dsarm002.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     #dsarm003#0 = #dsarm004#0;
     #dsarm003#1 = #dsarm004#1;
     |LEE 000#dsarm003.=;
     |SI FSalida ! 0;  |DDEFECTO #dsarm003;  |FINSI;

     #dpntm003#0  = enEmpresa;
     #dpntm003#1  = #dsarm004#0;
     #dpntm003#2  = #dsarm004#1;
     #dpntm003#3  = #dsarm004#2;
     |LEE 101#dpntm003.=;
     |SI FSalida ! 0;
         |DDEFECTO #dpntm003;
         #dpntm003#0  = enEmpresa;
         #dpntm003#1  = #dsarm004#0;
         #dpntm003#2  = #dsarm004#1;
         #dpntm003#3  = #dsarm004#2;
         #dpntm003#7  = #dsarm002#5;
         #dpntm003#8  = #dsarm003#6;
         #dpntm003#9  = #dsarm004#93;
         #dpntm003#10 = #dsarm004#94;
         #dpntm003#11 = #dsarm004#95;
         #dpntm003#12 = #dsarm004#96;
         |GRABA 020#dpntm003.b;
     |FINSI;

     #dpntm003#4  = #dsarm002#1;
     #dpntm003#5  = #dsarm003#2;
     #dpntm003#6  = #dsarm004#3;

     aNom = #dpntm003#7;  |QBF aNom;
     |SI aNom = "";
         #dpntm003#7 = #dpntm003#4;
     |FINSI;

     aNom = #dpntm003#8;  |QBF aNom;
     |SI aNom = "";
         #dpntm003#8 = #dpntm003#5;
     |FINSI;

     aNom = #dpntm003#9;  |QBF aNom;
     |SI aNom = "";
         #dpntm003#9 = #dpntm003#6;
     |FINSI;

     |GRABA 020#dpntm003.a;
     |LIBERA #dpntm003;
|FPRC;

|DEFBUCLE dsarm004;
     #dsarm004#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dsarm004;
|FIN;

|PROCESO dpntm003B;
     |BORRA 020#dpntm003.a;
     |LIBERA #dpntm003;
|FPRC;

|DEFBUCLE dpntm003B;
     #dpntm003#1;
     ;
     enEmpresa, 000, 000, 000;
     enEmpresa, 999, 999, 999;
     be;
     NULL, dpntm003B;
|FIN;

|PROCESO Borrar;
     |CONFI "0000NSeguro que quiere borrar la estructura personalizada y dejar la predeterminada";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |BUCLE dpntm003B;

     |MENAV "0000Activada estructura predeterminada.";

     |PTEC 807;
|FPRC

|PROCESO dpntm003Act;
     #dsarm002#0 = #dpntm003#1;
     |LEE 000#dsarm002.=;
     |SI FSalida ! 0;
         |BORRA 020#dpntm003.a;
         |LIBERA #dpntm003;
         |ACABA;
     |FINSI;

     #dsarm004#0 = #dpntm003#1;
     #dsarm004#1 = #dpntm003#2;
     #dsarm004#2 = #dpntm003#3;
     |LEE 000#dsarm004.=;
     |SI FSalida ! 0;
         |BORRA 020#dpntm003.a;
         |LIBERA #dpntm003;
         |ACABA;
     |FINSI;
|FPRC;

|DEFBUCLE dpntm003Act;
     #dpntm003#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dpntm003Act;
|FIN;

|PROCESO dpntm001Act;
     enEmpresa = #dpntm001#0;

     #dpntm003#0 = enEmpresa;
     #dpntm003#1 = 0;
     #dpntm003#2 = 0;
     #dpntm003#3 = 0;
     |LEE 000#dpntm003.];
     |SI FSalida ! 0;  |O  #dpntm003#0 ! enEmpresa;
         |ACABA;
     |FINSI;

     |BUCLE dsarm004;
|FPRC;

|DEFBUCLE dpntm001Act;
     #dpntm001#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dpntm001Act;
|FIN;

|PROGRAMA;
     |SI PARAMETRO = "ACTUALIZAR";
         |ABRE #dsarm002;
         |ABRE #dsarm003;
         |ABRE #dsarm004;

         |BUCLE dpntm003Act;
         |BUCLE dpntm001Act;

         |CIERRA #dsarm002;
         |CIERRA #dsarm003;
         |CIERRA #dsarm004;

         |ACABA;
     |FINSI;

     #dpntm003#0 = enEmpresa;
     #dpntm003#1 = 0;
     #dpntm003#2 = 0;
     #dpntm003#3 = 0;
     |LEE 000#dpntm003.];
     |SI FSalida ! 0;  |O  #dpntm003#0 ! enEmpresa;
         |CONFI "0000NSeguro que quiere personalizar la estructura de documentos para esta empresa DESPABOX";
         |SI FSalida ! 0;  |ACABA;  |FINSI;
     |FINSI;

     |ABRE #dsarm002;
     |ABRE #dsarm003;
     |BUCLE dsarm004;
     |CIERRA #dsarm002;
     |CIERRA #dsarm003;

     |HAZ MontaArbol;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Borrar configuraci¢n", 2702, 2725, Borrar;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}Salir," + nTree;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nTree;
     |FIN_CONTROL_WINDOWS nBoton;

     |PULSATECLA;
|FPRO;

|PROCESO Tipo80;  |TIPO 80;
     |ABRET #dpntm003;

     |ABRE #dsarm001;
     |LEE 000#dsarm001.p;
     |SI FSalida ! 0;  |DDEFECTO #dsarm001;  |FINSI;
     |CIERRA #dsarm001;

     FSalida = 2899;
|FPRC;

|PROCESO Tipo90;  |TIPO 90;
     |CIERRAT #dpntm003;
|FPRC;
