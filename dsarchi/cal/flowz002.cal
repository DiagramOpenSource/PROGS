|FICHEROS;
     flowz002 #0;
     flowm007 #607;
|FIN;

|VARIABLES;
     nVentana     = 0;
     nRango       = 0;
     nGrid607     = 0;
     nIde         = 0;
     nCampo1      = 0;
     nCampo2      = 0;
     nPanta0      = 0;
     nBoton1      = 0;
     nBoton2      = 0;
     nBoton3      = 0;

     aEsc1        = "";
     aEsc2        = "";
     aRetu        = "";
     aTecla       = "";
     aAlfa        = "";
     aHora        = "";
     aUsuario     = "";

   {-1}aVent      = "";
       aVent1     = 0;
       aVent2     = 0;
       aVent3     = 0;
       aVent4     = 0;
       aVent5     = "";

     &enDocumento = 0;
|FIN;

|PROCESO Tipo12;  |TIPO 12;
|FPRC;

|PROCESO Baja607;
     #607#0 = enDocumento;
     #607#1 = 001;
|FPRC;

|PROCESO Alta607;
     #607#0 = enDocumento;
     #607#1 = 999;
|FPRC;

|PROCESO Evento607;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#607=;

         |PARA nCampo1 = 0;  |SI nCampo1 [ 19;  |HACIENDO nCampo1 = nCampo1 + 1;
               nCampo2   = nCampo1 + 5;
               #0nCampo1 = #607nCampo2;
         |SIGUE;

         |PINTA #0#0;

         |ESTADO_CONTROL nBoton2, "ENABLE";
         |ESTADO_CONTROL nBoton3, "ENABLE";

         aAlfa = #607#2;  |QBF aAlfa;
         |SI aUsuario ! aAlfa;
             |ESTADO_CONTROL nBoton2, "DISABLE";
             |ESTADO_CONTROL nBoton3, "DISABLE";
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Boton1;
     |HAZ QuitaBotonSalir;

     #607#0 = enDocumento;
     #607#1 = 999;
     |LEE 000#607];
     |SI FSalida = 0;
         |LEE 000#607a;
     |SINO;
         |LEE 000#607u;
     |FINSI;

     nIde = 1;
     |SI FSalida = 0;  |Y #607#0 = enDocumento;
         nIde = #607#1 + 1;
     |FINSI;

     |DDEFECTO #607;
     #607#0 = enDocumento;
     #607#1 = nIde;
     |GRABA 020#607b;

     |DDEFECTO #0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
         |PARA nCampo1 = 0;  |SI nCampo1 [ 19;  |HACIENDO nCampo1 = nCampo1 + 1;
               nCampo2     = nCampo1 + 5;
               #607nCampo2 = #0nCampo1;
         |SIGUE;

         |HORA aHora;

         #607#2 = aUsuario;
         #607#4 = aHora;

         |GRABA 020#607a;
     |SINO;
         |BORRA 020#607a;
     |FINSI;
     |LIBERA #607;

     |REFRESCACONTROL nGrid607;
     |FOCOTECLADO nGrid607;

     |HAZ PonBotonSalir;
|FPRC;

|PROCESO Boton2;
     |LEE 101#607=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |HAZ QuitaBotonSalir;

     |PARA nCampo1 = 0;  |SI nCampo1 [ 19;  |HACIENDO nCampo1 = nCampo1 + 1;
           nCampo2   = nCampo1 + 5;
           #0nCampo1 = #607nCampo2;
     |SIGUE;
     |PINTA #0#0;

     |ENDATOS #1#0;
     |SI FSalida = 0;
         |PARA nCampo1 = 0;  |SI nCampo1 [ 19;  |HACIENDO nCampo1 = nCampo1 + 1;
               nCampo2     = nCampo1 + 5;
               #607nCampo2 = #0nCampo1;
         |SIGUE;

         |HORA aHora;

         #607#2 = aUsuario;
         #607#3 = ~;
         #607#4 = aHora;

         |GRABA 020#607a;
     |FINSI;
     |LIBERA #607;

     |REFRESCACONTROL nGrid607;
     |FOCOTECLADO nGrid607;

     |HAZ PonBotonSalir;
|FPRC;

|PROCESO Boton3;
     |LEE 101#607=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |CONFI "0000NSeguro que quiere borrar la anotaci¢n";
     |SI FSalida ! 0;  |LIBERA #607;  |ACABA;  |FINSI;

     |BORRA 020#607a;
     |LIBERA #607;

     |REFRESCACONTROL nGrid607;
     |FOCOTECLADO nGrid607;
|FPRC;

|PROGRAMA;
     aUsuario = Usuario % 810;

     aAlfa = "Anotaci¢n del documento: " + enDocumento;
     |VENTANA 0501, 3299, -1, 16, aAlfa;
     nVentana = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |ABRE #607;
     |PINPA #0#0;   nPanta0 = FSalida;
     |PINDA #0#0;

     |CONTROL_SIMPLE nPanta0, "BOTON,{{193}}Alta", 0685, 0695, Boton1;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON,{{197}}Modificar", 0885, 0895, Boton2;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON,{{199}}Baja", 1085, 1095, Boton3;
     nBoton3 = FSalida;

     |ESTADO_CONTROL nBoton2, "DISABLE";
     |ESTADO_CONTROL nBoton3, "DISABLE";

     nRango     = 4 + 8 + 16 + 32 + 128;
     |LINEAL_SIMPLE #1#607, nRango, 0512, 1081, Baja607, Alta607, Evento607;
     nGrid607 = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::Salir," + nGrid607;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |CIERRA #607;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVentana;
|FPRO;
