||  ACTUALIZAR CARPETAS NUEVAS DE CONTABILIDAD Y MODELOS
||Crea las nuevas carpetas al poner en marcha
||archivar informes contables y certificados modelos
|| 29.11.2017

|FICHEROS;
     dsarz990;

     dsarm002;
     dsarm003;
     dsarm004;
     dsarm005;
     dsarm008;
     dsarm059;

     referen@;      ||Para los CARGA_DEF
|FIN;

|VARIABLES;
     aParam   = "";
     aDirBase = "";
     aDef     = "";
     nCampo   = 0;

     nDGrupoPatron  = 0;
     nHGrupoPatron  = 0;
     nDSGrupoPatron = 0;
     nHSGrupoPatron = 0;
     aMensaje        = "";

     nOk           = 0;
     aAlfa         = "";
     aRuta         = "";
     aLong         = "";
     aCaracter     = "";
     aNombreFic    = "";
     aNombreRea    = "";
     aExtension    = "";
     aEsc1         = "";
     aEsc2         = "";
     aRetu         = "";
     aTecla        = "";
     aBase         = "";
     aDir          = "";
     aDirDestino   = "";
     aOrigen       = "";
     aDestino      = "";
     aIPRemoto     = "";
     aFichero      = "";

     nHay          = 0;
     nHandle       = 0;
     nDatos        = 0;
     nLong         = 0;
     nCarac        = 0;
     nPos          = 0;
|FIN;

|PROCESO referen2;
     |DDEFECTO #dsarm002;
     #dsarm002#0 = #referen@#0;
     |LEE 101#dsarm002.=;
     |SI FSalida ! 0;
         #dsarm002#1 = #referen@#1;
         #dsarm002#2 = #referen@#2;
         #dsarm002#3 = #referen@#3;
         #dsarm002#4 = #referen@#4;
         |GRABA 020#dsarm002.b;
     |FINSI;
     |LIBERA #dsarm002;
|FPRC;

|DEFBUCLE referen2;
     #referen@#1001;
     ;
     nDGrupoPatron;
     nHGrupoPatron;
     ;
     NULL, referen2;
|FIN;

|PROCESO referen3;
     |DDEFECTO #dsarm003;
     #dsarm003#0 = #referen@#0;
     #dsarm003#1 = #referen@#1;
     |LEE 101#dsarm003.=;
     |SI FSalida ! 0;
         #dsarm003#2 = #referen@#2;
         #dsarm003#3 = #referen@#3;
         #dsarm003#4 = #referen@#4;
         #dsarm003#5 = #referen@#5;
         |GRABA 020#dsarm003.b;
     |FINSI;
     |LIBERA #dsarm003;
|FPRC;

|DEFBUCLE referen3;
     #referen@#1002;
     ;
     nDGrupoPatron, nDSGrupoPatron;
     nHGrupoPatron, nHSGrupoPatron;
     ;
     NULL, referen3;
|FIN;

|PROCESO referen4;
     nOk = 0;
     |SI #referen@#0 = 2; |Y #referen@#1 = 2;
         nOk = 1;
     |FINSI;
     |SI #referen@#0 = 3;
         |SI #referen@#1 = 3; |O #referen@#1 = 4; |O #referen@#1 = 6;
             nOk = 1;
         |FINSI;
     |FINSI;

     |SI nOk = 1;
         |DDEFECTO #dsarm004;
         #dsarm004#0 = #referen@#0;
         #dsarm004#1 = #referen@#1;
         #dsarm004#2 = #referen@#2;
         |LEE 101#dsarm004.=;
         |SI FSalida ! 0;
             |PARA nCampo = 3; |SI nCampo [ 90; |HACIENDO nCampo = nCampo + 1;
                  #dsarm004#nCampo# = #referen@#nCampo#;
             |SIGUE;
             |GRABA 020#dsarm004.b;
         |SINO;
             |SI aParam = "TODO";
                 |PARA nCampo = 3; |SI nCampo [ 90; |HACIENDO nCampo = nCampo + 1;
                      #dsarm004#nCampo# = #referen@#nCampo#;
                 |SIGUE;
                 |GRABA 020#dsarm004.a;
             |FINSI;
         |FINSI;
         |LIBERA #dsarm004;
     |FINSI;
|FPRC;

|DEFBUCLE referen4;
     #referen@#1003;
     ;
     nDGrupoPatron, 001, 001;
     nHGrupoPatron, 999, 999;
     ;
     NULL, referen4;
|FIN;

|PROCESO ActuaPatron;

     |DBASE aBase;  |QBF aBase;
     |DFICE aDirDestino;
     aDirDestino = aDirDestino + "tmp/";
     |MKDIR aDirDestino;

     aDestino = aDirDestino + "maestros.tgz";

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |COPIA_FICHERO aOrigen, aDestino;

     |XABRE "E", aDestino, nHandle;
     |SI FSalida < 0;
         |ACABA;
     |FINSI;

     |DESCOMPRIME aDestino;
     |DETAR aDestino, aDirDestino;

     aAlfa = aDestino;                      |FBORRA aAlfa;
     aAlfa = aDirDestino + "maestros.tar";  |FBORRA aAlfa;

     |DBASE aDirBase;
     |QBF aDirBase;

     ||dsarm002
     aDef = aDirBase + "def/dsarm002";
     |CARGA_DEF aDef, referen@;
     |SI FSalida ! 0;
          aMensaje = "0000Problema con CARGA_DEF" + aDef;
          || ... |MENAV aMensaje;
          |ACABA;
     |FINSI;

     |PATH_DAT #referen@#aDirDestino#;

     |ABRE #dsarm002;
     |BUCLE referen2;
     |CIERRA #dsarm002;
     |DESCARGA_DEF #referen@;

     ||dsarm003
     aDef = aDirBase + "def/dsarm003";
     |CARGA_DEF aDef, referen@;
     |SI FSalida ! 0;
          aMensaje = "0000Problema con CARGA_DEF" + aDef;
          || ... |MENAV aMensaje;
          |ACABA;
     |FINSI;

     nDSGrupoPatron = 6;
     nHSGrupoPatron = 6;
     |PATH_DAT #referen@#aDirDestino#;

     |ABRE #dsarm003;
     |BUCLE referen3;
     |CIERRA #dsarm003;
     |DESCARGA_DEF #referen@;

     ||dsarm004
     aDef = aDirBase + "def/dsarm004";
     |CARGA_DEF aDef, referen@;
     |SI FSalida ! 0;
          aMensaje = "0000Problema con CARGA_DEF" + aDef;
          || ... |MENAV aMensaje;
          |ACABA;
     |FINSI;

     |PATH_DAT #referen@#aDirDestino#;

     |ABRE #dsarm004;
     |BUCLE referen4;
     |CIERRA #dsarm004;
     |DESCARGA_DEF #referen@;

     |MENAV "0000Grupo/Subgrupo/Tipo y Vistas actualizados a partir de Patrones.";
|FPRC;

|PROGRAMA;
     aParam = PARAMETRO; |QBF aParam;
     nDGrupoPatron = 2;
     nHGrupoPatron = 3;

     |DBASE aBase;
     aDirDestino = aBase + "plugins/";
     aDir        = aBase + "plugins/para_asesorias.ptl";

     nHay    = 0;
     |_PDIR aDir;
     |PARA; |SI FSalida ] 0; |HACIENDO;
          nHay    = nHay + 1;
          |_SDIR aDir;
     |SIGUE;

     |SI nHay ! 0;
         aOrigen = aBase + "plugins/para_asesorias.ptl";
         |HAZ ActuaPatron;
     |FINSI;
|FPRO;
