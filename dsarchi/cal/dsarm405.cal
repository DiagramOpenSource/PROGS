||Para el control de la MULTISELECCION DE DOCUMENTOS PARA EL ENVIO DE CORREOS

||para los botones, se basa en el dsarm105

|FICHEROS;
     dsarm001;
     dsarm005;
     dsarm056;
     dsarm055;

     dsarw021;

     destino@;
|FIN;

|VARIABLES;
     aMas = "";
     aAux = "";
     aCadena = "";
     aAlfa = "";
     nCampoHasta = 0;
     nCampo = 0;
     aMensaje = "";
     n0Vent   = 0;
     n1Vent   = 0;
     nPanta21 = 0;
     nPos1 = 0;
     nPos2 = 0;
     nBotonQuita = 0;
     nBotonQuitaTodos = 0;
     nRango = 0;
     nGridSel = 0;

     nOk              = 0;
     nCmp             = 0;
     nSwTgzOk         = 0;

     aDCarp1          = "";
     aDCarp2          = "";
     aDCarp3          = "";
     aDCarp4          = "";
     aDCarp5          = "";
     aDCarp6          = "";
     aHCarp1          = "";
     aHCarp2          = "";
     aHCarp3          = "";
     aHCarp4          = "";
     aHCarp5          = "";
     aHCarp6          = "";
     aPaqBase         = "";
     aPaqLocal        = "";
     aPaqTar          = "";
     aPaq             = "";
     aHora            = "";
     aPaquete         = "";
     aIPRemoto        = "";
     aOrigen          = "";
     aDestino         = "";
     aFicTar          = "";
     aBorra           = "";
     aEjec            = "";
     aBase            = "";

     fFecha           = @;

 {-1}aVent = "";
     aVent1 = 0;
     aVent2 = 0;
     aVent3 = 0;
     aVent4 = 0;
     aVent5 = "";

     &enNumMultiSele     = 0;
     &enSwDefMultiSele   = 0;
     &eaFoco             = "";
     &eaUnidadBasico     = "";
     &enArbol            = 0;
|FIN;

|PROCESO dsarm055Paq;
     #destino@#0 = #dsarm055#0;
     |LEE 000#destino@.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI nOk = 0;
         aPaq = aPaqTar;

         |PARA nCmp = 0;  |SI nCmp [ 6;  |HACIENDO nCmp = nCmp + 1;
               |SI nCmp ! 5;
                   aAlfa = #dsarm056#nCmp#;  |QBF aAlfa;
                   |SI aAlfa ! "";
                       aPaq = aPaq + aAlfa;  |MKDIR aPaq;
                       aPaq = aPaq + "/";
                   |FINSI;
               |FINSI;
          |SIGUE;
     |FINSI;

     aOrigen  = #destino@#7;             |QBF aOrigen;
     aOrigen  = aOrigen + #destino@#9;   |QBF aOrigen;
     aDestino = aPaq + #destino@#9;      |QBF aDestino;

     |SI #dsarm001#7 = "N";
         |COPIA_FICHERO aOrigen, aDestino;
     |SINO;
         |SI aOrigen ! aDestino;
             |RCOPIA_FICHERO aOrigen, aDestino;
         |FINSI;
     |FINSI;

     nOk = 1;
|FPRC;

|DEFBUCLE dsarm055Paq;
     #dsarm055#2;
     ;
     #dsarm056#5, 000000000;
     #dsarm056#5, 999999999;
     ;
     NULL, dsarm055Paq;
|FIN;

|PROCESO dsarm056Paq;
     nOk = 0;
     |BUCLE dsarm055Paq;
|FPRC;

|DEFBUCLE dsarm056Paq;
     #dsarm056#1;
     ;
     aDCarp1, aDCarp2, aDCarp3, aDCarp4, aDCarp5, aDCarp6;
     aHCarp1, aHCarp2, aHCarp3, aHCarp4, aHCarp5, aHCarp6;
     ;
     NULL, dsarm056Paq;
|FIN;

|PROCESO SinCarpetas;
     aOrigen  = #destino@#7;             |QBF aOrigen;
     aOrigen  = aOrigen + #destino@#9;   |QBF aOrigen;
     aDestino = aPaqTar + #destino@#9;   |QBF aDestino;

     |SI #dsarm001#7 = "N";
         |COPIA_FICHERO aOrigen, aDestino;
     |SINO;
         |SI aOrigen ! aDestino;
             |RCOPIA_FICHERO aOrigen, aDestino;
         |FINSI;
     |FINSI;

     nOk = 1;
|FPRC;

|DEFBUCLE SinCarpetas;
     #destino@#1001;
     ;
     000000001;
     999999999;
     ;
     NULL, SinCarpetas;
|FIN;

|PROCESO BorraCarpeta;
     |DBASS aBase;
     aEjec = aBase + "bin/agirm -r " + aPaqBase + aPaquete;

     |SYSTEM aEjec;
|FPRC;

|PROCESO CrearPaquete;
     aAlfa =  "0000SLa creaci¢n del paquete puede tardar varios minutos ";
     aAlfa = aAlfa + "dependiendo la cantidad de documentos. Procedemos a la creaci¢n.";
     |CONFI aAlfa;
     |SI FSalida ! 0;
         |FOCOTECLADO nGridSel;
         |ACABA;
     |FINSI;

     |SI eaUnidadBasico = "";
         |HAZ LeeUnidadBasico;
     |FINSI;

     aPaqLocal    = eaUnidadBasico + "\DOCUMENTOS";                     |RMKDIR aPaqLocal;
     aPaqLocal    = eaUnidadBasico + "\DOCUMENTOS\DSARCHI";             |RMKDIR aPaqLocal;
     aPaqLocal    = eaUnidadBasico + "\DOCUMENTOS\DSARCHI\PAQUETES";    |RMKDIR aPaqLocal;
     aPaqLocal    = eaUnidadBasico + "\DOCUMENTOS\DSARCHI\PAQUETES\";

     aIPRemoto   = "";
     |IP_REMOTO aIPRemoto;

     fFecha = ~;
     |HORA aHora;

     aPaquete = (fFecha % -104) + (fFecha % 402) + (fFecha % -102);
     aPaquete = aPaquete + (aHora % 102) + (aHora % 402) + (aHora % 702);

     |DBASE aPaqBase;
     aPaqBase = aPaqBase + "tmp";  |MKDIR aPaqBase;
     aPaqBase = aPaqBase + "/" + Usuario;   |MKDIR aPaqBase;
     aPaqBase = aPaqBase + "/";

     aPaqTar  = aPaqBase + aPaquete;   |MKDIR aPaqTar;
     aPaqTar  = aPaqTar  + "/";

     aDCarp1 = "";
     aHCarp1 = "z" * 135;
     aDCarp2 = "";
     aHCarp2 = "z" * 135;
     aDCarp3 = "";
     aHCarp3 = "z" * 135;
     aDCarp4 = "";
     aHCarp4 = "z" * 135;
     aDCarp5 = "";
     aHCarp5 = "z" * 135;
     aDCarp6 = "";
     aHCarp6 = "z" * 135;

     |VENTANA 0501, 0550, -1, 16, "Creando paquete, espere por favor.";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     |SI enArbol = 1;
         |BUCLE dsarm056Paq;
     |SINO;
         |BUCLE SinCarpetas;
     |FINSI;

     nSwTgzOk = 0;
     aFicTar  = aPaqBase + aPaquete + ".tar" + " " + aPaquete + "/*";

     |ATAR aFicTar, aPaqBase;
     |SI FSalida = 0;
          aFicTar = aPaqBase + aPaquete + ".tar";
          |COMPRIME aFicTar;
          |SI FSalida = 0;
               aBorra = aPaqBase + aPaquete + ".tar";
               |FBORRA aBorra;

               nSwTgzOk = 1;
          |FINSI;
     |FINSI;

     |SI nSwTgzOk = 1;
         aOrigen  = aPaqBase  + aPaquete + ".tgz";
         aDestino = aPaqLocal + aPaquete + ".tgz";

         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aOrigen, aDestino;
         |SINO;
             |COPIA_FICHERO aOrigen, aDestino;
         |FINSI;

         |XABRE "EC", aDestino, 0;
         |SI FSalida ] 0;
             aAlfa = "0000Generado. La ubicaci¢n y nombre del paquete es la siguiente ";
             aAlfa = aAlfa + aDestino;
             |MENAV aAlfa;
         |SINO;
             |MENAV "0000Ha habido un error a generar el paquete. Vuelva a intentarlo.";
         |FINSI;

         |FBORRA aOrigen;
     |SINO;
         |MENAV "0000Ha habido un error a generar el paquete. Vuelva a intentarlo.";
     |FINSI;

     |HAZ BorraCarpeta;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;

     |FOCOTECLADO nGridSel;
|FPRC;

|PROCESO DesCargaDefMulti;
     |CIERRA #destino@;
     |DELINDEX #destino@;

     |DESCARGA_DEF #destino@;
|FPRC;

|PROCESO BotonMas;   ||+  Anyadir a Seleccinados.
     |SI enSwDefMultiSele = 0;
          |DBASE aMas;  |QBF aMas;
          aMas = aMas + "def/dsarm005.mas";
          |CARGA_DEF aMas, destino@;
          |SI FSalida ! 0;
               |MENAV "0000[dsarm005.mas] inaccesible. Consulte con su distribuidor.";
               |ACABA;
          |FINSI;

          enSwDefMultiSele = 1;

          aAux = ("ms" + Usuario + "zzzzzzzz") % 108;
          |NOME_DAT #destino@#aAux#;

          |DELINDEX #destino@;

          |ABRE #destino@;
     |FINSI;

     aCadena ="|NC";
     aAlfa = #destino@#aCadena#;
     nCampoHasta = aAlfa;
     nCampoHasta = nCampoHasta - 1;

     |SELECT #1#destino@;
     #destino@#0 = #dsarm005#0;
     |LEE 101#destino@.=;
     |SI FSalida ! 0;
          |PARA nCampo = 1;  |SI nCampo [ nCampoHasta;   |HACIENDO nCampo = nCampo + 1;
               #destino@#nCampo# = #dsarm005#nCampo#
          |SIGUE;
          |GRABA 020#destino@.n;
          enNumMultiSele = enNumMultiSele + 1;
     |SINO;
          aMensaje = "0000Este documento YA esta Seleccionado";

          |SI eaFoco ! "CARPETA";
              |MENAV aMensaje;
          |FINSI;
     |FINSI;
     |LIBERA #destino@;
|FPRC;

|PROCESO BotonQuitaMS;     || "-"  Deseleccionar
     |LEE 101#destino@.=;
     |SI FSalida = 0;
          |BORRA 020#destino@.a;
          |LIBERA #destino@;

          enNumMultiSele = enNumMultiSele - 1;
          |SI enNumMultiSele = 0;
               |ESTADO_CONTROL nBotonQuita, "DISABLE";
          |FINSI;

          |REFRESCACONTROL nGridSel;
     |FINSI;
|FPRC;

|PROCESO EventoMS;
     |SI FSalida = 1; |O FSalida = 2;
          |LEE 000#destino@.=;
          |SI FSalida = 0;
               |ESTADO_CONTROL nBotonQuita, "ENABLE";
          |SINO;
               |ESTADO_CONTROL nBotonQuita, "DISABLE";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO BotonQuitaTodos;
     enNumMultiSele = 0;
     |CIERRA #destino@;
     |DELINDEX #destino@;
     |ABRE #destino@;
     |ESTADO_CONTROL nBotonQuita, "DISABLE";
     |ESTADO_CONTROL nBotonQuitaTodos, "DISABLE";

     |REFRESCACONTROL nGridSel;
|FPRC;

|PROCESO BotonMulti;    ||Ver Seleccionados
     |SI enNumMultiSele = 0;
          aMensaje = "0000MultiSeleccion Vacia";
          |MENAV aMensaje;
     |SINO;
          |VENTANA 0501, 2699, -1, 16, "Ver Documentos Seleccionados";
          n0Vent = FSalida;

          aVent1 = -1;
          aVent2 = -1;
          aVent3 = 0;
          aVent4 = n0Vent;
          aVent5 = "MODAL";
          |ESPECIFICA "ESTADO_VENTANA", aVent;

          |PINPA #0#dsarw021;
          nPanta21 = FSalida;

          |CONTROL_SIMPLE nPanta21, "BOTON,Crear paquete en el PC", 2602, 2620, CrearPaquete;

          |CONTROL_SIMPLE nPanta21, "BOTON,Deseleccionar Todos", 2666, 2684, BotonQuitaTodos;
          nBotonQuitaTodos = FSalida;

          |CONTROL_SIMPLE nPanta21, "BOTON,Deseleccionar", 2686, 2698, BotonQuitaMS;
          nBotonQuita = FSalida;

          nRango = 2 + 4 + 8 + 16 + 32 + 128;

          |LINEAL_SIMPLE #1#destino@, nRango, 0802, 2599, NULL, NULL, EventoMS;
          nGridSel = FSalida;

          FSalida = 1;
          |HAZ EventoMS;

          |PAUSA;

          aVent1 = -1;
          aVent2 = -1;
          aVent3 = 0;
          aVent4 = n0Vent;
          aVent5 = "MODELESS";
          |ESPECIFICA "ESTADO_VENTANA", aVent;

          |FINVENTANA n0Vent;
     |FINSI;
|FPRC;
