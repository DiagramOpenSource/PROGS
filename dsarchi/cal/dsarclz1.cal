||NUCLEO

||aSwModo  .. INDICICA SI SE EJECUTA TODO, BASE (HISTORIAS Y EPISODIOS)
||            o los INCR (Incrementales)

|FICHEROS;
     dsarclz1 #0;

     dsarm001 #1;
     dsarm005 #5;

     dsarcl99 #99;       ||Control Act. CLINICA

     dsarcl01 #101;      ||Historias
     dsarcl02 #102;      ||Episodios
     dsarcl03 #103;      ||Sust.HistoriaCLINICA
     dsarcl05 #105;      ||C.Hist.Epi   CLINICA
     dsarcl06 #106;      ||Anul. Episo  CLINICA

     dsarcl10 #110;      ||Reg.Inciden. CLINICA

     ||El dsarcl04 .. No se utiliza. NO esta ni creado.
|FIN;

|VARIABLES;
     nModoHora = 0;
     fFechaMenosUna = @;
     aFechaMenosUna = "";
     nFechaSis = 0;
     aHoraMenosUna = "";
     nAux = 0;

     aHoraBBDD = "";
     aHisReg = "";
     aEpiReg = "";
     aCodHisBus = "";
     aCodEpiBus = "";
     nUltimo = 0;

     aDesModo = "";
     aSwModo = "";
     aAlfa1 = "";
     aTextoOld = "";
     aTextoNew = "";
     nTamTotal = 0;
     nSaca = 0;
     aTexto = "";
     aIni = "";
     aFin = "";

     aAux = "";
     aFechaAnt = "";
     aHoraAnt = "";
     fSistema = @;
     aSistema = "";
     aHoraSis = "";
     nDifMinutos = 0;
     nMinAnt = 0;
     nHoraAnt = 0;
     nMinSis = 0;
     nHoraSis = 0;

     aPacOld = "";
     aHisOld = "";
     aPacNew = "";
     aHisNew = "";
     aVarOrigen = "";
     aFechaReg = "";
     aHoraReg = "";
     aFechaAux = "";

     aFechaNorma = "";
     aDia = "";
     aMes = "";
     aAnyo = "";
     aFechaIni = "";
     aSit = "";
     aNumEpi = "";

     aTab = "";
     nSal = 0;
     nHandleErr = 0;
     nHandleRes = 0;
     nSwErrorSQL = 0;
     aNomTmp = "";
     aLong = "";
     nLong = 0;
     nContador = 0;
     aTextAux = "";
     nVoy = 0;
     aReturnUnix = "";
     aSaca = "";
     aLetra = "";
     aCodHis = "";
     aApel1 = "";
     aApel2 = "";
     aNombre = "";
     aSexo = "";
     aFechaNac = "";
     aDni = "";
     aTelefono = "";
     aNafss = "";
     aCip = "";
     aFechaGra = "";

     nSwAcaba = 0;
     aNomOri = "";
     aNomDes = "";
     aDesSql = "";
     aEjecuta = "";
     aFicRes = "";
     aFicErr = "";
     nSwHayRes = 0;
     nSwHayErr = 0;
     aFicheroRes = "";
     aFicheroErr = "";
     nPausa = 0;
     aAlfa = "";

     aMeteTxt = "";
     nHandle = 0;
     nHandle2 = 0;
     aLinea = "";
     aFicSQL = "";
     aOrigen = "";
     aDestino = "";

     &eaUnidad = "";
     &eaDestino = "";
     &eaOrigen = "";
     aPathBase = "";

     aMensaje = "";
     aParametro = "";
     nModoAuto = 0;
     aBase = "";
     aFicLog = "";
     fLogDia = @;
     aLogHor = "";
     nHandLog = 0;
     aMenLog = "";

     aUsuario = "";
     aPassword = "";
     aServidor = "";
     aNombreBBDD = "";
|FIN;

|PROCESO Tipo80; |TIPO 80;
     FSalida = 2799;
|FPRC;

|PROCESO MeteLog;
     fLogDia = ~;
     |HORA aLogHor;

     |XABRE "A", aFicLog, nHandLog;
     |SI FSalida ] 0;
          aMenLog = fLogDia + "#" + aLogHor + "#" + aMenLog;
          |XGRABA nHandLog, aMenLog;
          |XCIERRA nHandLog;
     |FINSI;
|FPRC;


|PROCESO Principal;
     |SI nModoHora = 1;
          ||Calculo la fecha del sistema menos 1 hora.
          ||aHoraSis;
          ||fSistema;

          aAux = aHoraSis % 102;
          nHoraSis = aAux;
          aAux = aHoraSis % 402;
          nMinSis = aAux;
          nMinSis = (60 * nHoraSis) + nMinSis;

          nFechaSis = fSistema;
          |SI nMinSis ] 61;        ||Le doy un margen de 1 minuto. 60 +1
               nMinSis = nMinSis - 61;
          |SINO;
               nMinSis = 1440 + nMinSis - 61;
               ||Le quito un dia a la fecha.
               nFechaSis = nFechaSis - 1;
          |FINSI;

          fFechaMenosUna = nFechaSis;
          aFechaMenosUna = fFechaMenosUna;

          nAux = nMinSis $ 60;          ||Numero de minutos

          nMinSis = nMinSis - nAux;     ||Numero de horas (aun en minutos)
          nHoraSis = nMinSis / 60;      ||Numero de horas
          nMinSis = nAux;               ||Numero minutos
          aHoraMenosUna = (("00" + nHoraSis) % -102) + ":" + (("00" + nMinSis) % -102);

/*
          aMensaje = "0000" + aSistema + "_" + aHoraSis + " MENOS 61 MIN. " + aFechaMenosUna + "_" + aHoraMenosUna;
          |MENAV aMensaje;
*/
     |FINSI;

     |SI aSwModo = "TODO";
          aDesModo = "TODO";
     |SINO;
          |SI aSwModo = "BASE";
               aDesModo = "HISTORIAS/EPISODIOS";
          |SINO;
               aDesModo = "INCIDENCIAS";
          |FINSI;
     |FINSI;

     aMenLog = "Fecha Ejecucion: " + aSistema + ". Hora: " + aHoraSis + " Modo Ejecucion: " + aDesModo;
     |HAZ MeteLog;

     |SI nModoAuto = 0;
           aMenLog = "Inicio. Conexion con clinicas. Modo Manual";
     |SINO;
           aMenLog = "Inicio. Conexion con clinicas. Modo Automatico";
     |FINSI;
     |HAZ MeteLog;

     |ABRE #dsarm001;
     |LEE 000#dsarm001.p;
     |SI FSalida ! 0;
          |SI nModoAuto = 0;
               aMensaje = "0000Fichero Parametros NO definido.";
               |MENAV aMensaje;
          |SINO;
               aMenLog = "Conexion con clinicas NO establecida.";
               |HAZ MeteLog;
          |FINSI;
          |ACABA;
     |FINSI;

     |SI #dsarm001#194 = "N";
          |SI nModoAuto = 0;
               aMensaje = "0000Conexi¢n con cl¡nicas NO establecida. [Par metros]";
               |MENAV aMensaje;
          |SINO;
               aMenLog = "Conexion con clinicas NO establecida. [Parametros]";
               |HAZ MeteLog;
          |FINSI;
          |ACABA;
     |FINSI;
     |CIERRA #dsarm001;

     aUsuario = #dsarm001#195;
     aPassword = #dsarm001#196;
     aServidor = #dsarm001#197;
     aNombreBBDD = #dsarm001#198;
     |QBF aUsuario;
     |QBF aPassword;
     |QBF aServidor;
     |QBF aNombreBBDD;

     |SI aUsuario = ""; |O aPassword = ""; |O aServidor = ""; |O aNombreBBDD = "";
          |SI nModoAuto = 0;
               aMensaje = "0000Par metros Conexi¢n con cl¡nicas incorrectos.";
               |MENAV aMensaje;
          |SINO;
               aMenLog = "Parametros Conexion con clinicas incorrectos";
               |HAZ MeteLog;
          |FINSI;
          |ACABA;
     |FINSI;

     ||NUCLEO

     |ABRE #dsarm005;

     ||Me traigo el programa de David al puesto. [dsclinica.exe]
     |HAZ DimeUnidad;

     |DBASE aPathBase;
     |QBF aPathBase;

     eaDestino = eaUnidad + ":\DOCUMENTOS\DSARCHI\" + "dsclinica.exe";
     eaOrigen = aPathBase + "dlls/dsclinica.exe";
     |HAZ EnviaFicheroConMD5;

     eaDestino = eaUnidad + ":\DOCUMENTOS\DSARCHI\" + "wait.exe";
     eaOrigen = aPathBase + "dlls/wait.exe";
     |HAZ EnviaFicheroConMD5;

     aMenLog = "Path Ejecucion datos: " + eaUnidad + ":\DOCUMENTOS\DSARCHI\";
     |HAZ MeteLog;

     ||Tengo que crear 4 txt, con cada una de las selects
     ||y luego ejecutar 4 veces el programita de David

     nSwAcaba = 0;

     |SI aSwModo = "TODO"; |O aSwModo = "BASE";
          aNomOri = "sqlcli01.txt";
          aNomDes = "sql1.txt";
          aDesSql = "SQL Historias";
          |HAZ TraeSQL;
          |SI nSwAcaba = 1;
               |ACABA;
          |FINSI;

          aNomOri = "sqlcli02.txt";
          aNomDes = "sql2.txt";
          aDesSql = "SQL Episodios";
          |HAZ TraeSQL;
          |SI nSwAcaba = 1;
               |ACABA;
          |FINSI;
     |FINSI;

     |SI aSwModo = "TODO"; |O aSwModo = "INCR";
          aNomOri = "sqlcli03.txt";
          aNomDes = "sql3.txt";
          aDesSql = "SQL Sustituaciones Historia";
          |HAZ TraeSQL;
          |SI nSwAcaba = 1;
               |ACABA;
          |FINSI;


          ||ESTO POR AHORA NO SE UTILIZA
||          aNomOri = "sqlcli04.txt";
||          aNomDes = "sql4.txt";
||          aDesSql = "SQL Cambio Numeros Historia";
||          |HAZ TraeSQL;
||          |SI nSwAcaba = 1;
||               |ACABA;
||          |FINSI;

          aNomOri = "sqlcli05.txt";
          aNomDes = "sql5.txt";
          aDesSql = "SQL Cambio de Historia de un episodio";
          |HAZ TraeSQL;
          |SI nSwAcaba = 1;
               |ACABA;
          |FINSI;

          aNomOri = "sqlcli06.txt";
          aNomDes = "sql6.txt";
          aDesSql = "SQL Anulacion de episodio";
          |HAZ TraeSQL;
          |SI nSwAcaba = 1;
               |ACABA;
          |FINSI;

          |SI aSwModo = "INCR";
               aNomOri = "sqlcli01.txt";
               aNomDes = "sql7.txt";
               aDesSql = "SQL Incrementales Historias";
               |HAZ TraeSQL;
               |SI nSwAcaba = 1;
                    |ACABA;
               |FINSI;

               aNomOri = "sqlcli02.txt";
               aNomDes = "sql8.txt";
               aDesSql = "SQL Incrementales Episodios";
               |HAZ TraeSQL;
               |SI nSwAcaba = 1;
                    |ACABA;
               |FINSI;
          |FINSI;
     |FINSI;

     ||Ahora ejecuto el proceso, y recogo el resultado.

     |SI aSwModo = "TODO"; |O aSwModo = "BASE";
          aNomDes = "sql1.txt";
          |HAZ EjecutaSQL;
          ||Espero a que termine .. y recogo el resultado.
          ||Actualizo las tablas que resulten implicadas

          aNomDes = "sql2.txt";
          |HAZ EjecutaSQL;
     |FINSI;

     |SI aSwModo = "TODO"; |O aSwModo = "INCR";
          |ABRE #dsarcl10;

          aNomDes = "sql3.txt";
          |HAZ EjecutaSQL;

          aNomDes = "sql5.txt";
          |HAZ EjecutaSQL;

          aNomDes = "sql6.txt";
          |HAZ EjecutaSQL;

          |SI aSwModo = "INCR";
               aNomDes = "sql7.txt";
               |HAZ EjecutaSQL;

               aNomDes = "sql8.txt";
               |HAZ EjecutaSQL;
          |FINSI;

          |CIERRA #dsarcl10;
     |FINSI;

     |SI nModoAuto = 0;
           aMenLog = "Final . Conexion con clinicas. Modo Manual";
     |SINO;
           aMenLog = "Final . Conexion con clinicas. Modo Automatico";
     |FINSI;
     |HAZ MeteLog;

     |CIERRA #dsarm005;

     ||Actualizar hora y fecha (del registro de ejecucion)
     |DDEFECTO #dsarcl99;
     #dsarcl99#0 = "1";
     |LEE 101#dsarcl99.=;
     |SI FSalida ! 0; |GRABA 020#dsarcl99.b; |FINSI;
     #dsarcl99#1 = aSistema;
     #dsarcl99#2 = aHoraSis;
     |GRABA 020#dsarcl99.a;
     |LIBERA #dsarcl99;
|FPRC;

|PROCESO EjecutaSQL;
     aMenLog = "Inicio Ejecucion [" + aNomDes + "]";
     |HAZ MeteLog;

     aFicRes = aNomDes + ".res";
     aFicErr = aNomDes + ".err";

     aAlfa  = eaUnidad + ":\DOCUMENTOS\DSARCHI\" + aFicRes;
     |RFBORRA aAlfa;

     aAlfa  = eaUnidad + ":\DOCUMENTOS\DSARCHI\" + aFicErr;
     |RFBORRA aAlfa;

     aEjecuta = eaUnidad + ":\DOCUMENTOS\DSARCHI\wait.exe ";
     aEjecuta = aEjecuta + eaUnidad + ":\DOCUMENTOS\DSARCHI\dsclinica.exe ";
     aEjecuta = aEjecuta + eaUnidad + ":\DOCUMENTOS\DSARCHI\" + aNomDes;
     aEjecuta = aEjecuta + " " + aPassword;

     |RSYSTEM aEjecuta;

     aMensaje = "0000Copia el .txt y el .res (" + aNomDes + ")";
     ||MENAV aMensaje;

     ||Me falta recoger el .res o el .err, esperando a que se termine.

     nSal = 0;
     |PARA; |SI; |HACIENDO;
          aFicheroRes = eaUnidad + ":\DOCUMENTOS\DSARCHI\" + aFicRes;
          |XABRE "EC", aFicheroRes, nHandle;
          |SI FSalida ] 0;
               nSwHayRes = 1;
               |SLEEP 5;      ||Le doy tiempo a que se termine de copiar
               nSal = 1;
          |FINSI;

          aFicheroErr = eaUnidad + ":\DOCUMENTOS\DSARCHI\" + aFicErr;
          |XABRE "EC", aFicheroErr, nHandle;
          |SI FSalida ] 0;
               nSwHayErr = 1;
               |SLEEP 1;      ||Le doy tiempo a que se termine de copiar
               nSal = 1;
          |FINSI;

          |SI nSal = 1;
               |SAL;
          |FINSI;

          |PARA nPausa = 1; |SI nPausa < 10000; |HACIENDO nPausa = nPausa + 1;
          |SIGUE;
          |PULSATECLA;
     |SIGUE;
     |PULSATECLA;

     nSwErrorSQL = 0;
     |SI nSwHayErr = 1;
          |HAZ ProcesaErr;
     |FINSI;

     |SI nSwHayRes = 1; |Y nSwErrorSQL = 0;
          |HAZ ProcesaRes;
     |FINSI;

     aMenLog = "Final Ejecucion [" + aNomDes + "]";
     |HAZ MeteLog;
|FPRC;

|PROCESO PonCampoSQL1;
     |QBF aTextAux;
     |SI nContador = 1;
          aCodHis = aTextAux;
     |FINSI;
     |SI nContador = 2;
          aApel1 = aTextAux;
     |FINSI;
     |SI nContador = 3;
          aApel2 = aTextAux;
     |FINSI;
     |SI nContador = 4;
          aNombre = aTextAux;
     |FINSI;
     |SI nContador = 5;
          aSexo = aTextAux;
     |FINSI;
     |SI nContador = 6;
          aFechaNac = aTextAux;
     |FINSI;
     |SI nContador = 7;
          aDni = aTextAux;
     |FINSI;
     |SI nContador = 8;
          aTelefono = aTextAux;
     |FINSI;
     |SI nContador = 9;
          aNafss = aTextAux;
     |FINSI;
     |SI nContador = 10;
          aCip = aTextAux;
     |FINSI;
     |SI nContador = 11;
          aFechaGra = aTextAux;
     |FINSI;
|FPRC;

|PROCESO MeteLineaSql1;
     aLong = aLinea % A0;
     nLong = aLong;

     ||ACERA VARIABLES.
     aCodHis = "";
     aApel1 = "";
     aApel2 = "";
     aNombre = "";
     aSexo = "";
     aFechaNac = "";
     aDni = "";
     aTelefono = "";
     aNafss = "";
     aCip = "";
     aFechaGra = "";

     nContador = 0;
     aTextAux = "";
     |PARA nVoy = 1; |SI nVoy [ nLong; |HACIENDO nVoy = nVoy + 1;
          aSaca = nVoy + "01";
          aLetra = aLinea % aSaca;
          |SI aLetra = aTab; |O aLetra = aReturnUnix;
               nContador = nContador + 1;

               |HAZ PonCampoSQL1;

               aTextAux = "";
          |SINO;
               aTextAux = aTextAux + aLetra;
          |FINSI;
     |SIGUE;

     ||ULTIMOS CAMPO. Parece que no mantiene el numero de campos.
     |SI nContador > 1;
          nContador = nContador + 1;
          |HAZ PonCampoSQL1;
     |FINSI;

     ||Falta normalizar la fecha
     aFechaNorma = aFechaNac;
     |HAZ NormalizaFecha;
     aFechaNac = aFechaNorma;

     ||GRABO EL REGISTRO 101
     |DDEFECTO #101;
     #101#0 = aCodHis;
     |LEE 101#101.=;
     |SI FSalida ! 0; |GRABA 020#101.b; |FINSI;
     #101#1 = aApel1;
     #101#2 = aApel2;
     #101#3 = aNombre;
     #101#4 = aSexo;
     #101#5 = aFechaNac;
     #101#6 = aDni;
     #101#7 = aTelefono;
     #101#8 = aNafss;
     #101#9 = aCip;
     |GRABA 020#101.a;
     |LIBERA #101;

     |SI aNomDes = "sql7.txt";
          |LEE 000#dsarcl10.u;
          |SI FSalida = 0;
               nUltimo = #dsarcl10#0 + 1;
          |SINO;
               nUltimo = 1;
          |FINSI;
          |DDEFECTO #dsarcl10;
          #dsarcl10#0 = nUltimo;
          |LEE 101#dsarcl10.=;
          |SI FSalida ! 0;
               #dsarcl10#1 = aSistema;
               #dsarcl10#2 = aHoraSis;
               #dsarcl10#3 = "sql7";

               aFechaNorma = aFechaGra % 110;
               |HAZ NormalizaFecha;
               #dsarcl10#4 = aFechaNorma;
               aHoraBBDD = aFechaGra % 1208;
               |QBF aHoraBBDD;
               #dsarcl10#5 = aHoraBBDD;

               #dsarcl10#9 = #dsarcl01#0;
               |GRABA 020#dsarcl10.n;
               |LIBERA #dsarcl10;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO MeteLineaSql2;
     aLong = aLinea % A0;
     nLong = aLong;

     ||ACERA VARIABLES.
     aCodHis = "";
     aNumEpi = "";
     aFechaIni = "";
     aSit = "";

     nContador = 0;
     aTextAux = "";
     |PARA nVoy = 1; |SI nVoy [ nLong; |HACIENDO nVoy = nVoy + 1;
          aSaca = nVoy + "01";
          aLetra = aLinea % aSaca;
          |SI aLetra = aTab; |O aLetra = aReturnUnix;
               nContador = nContador + 1;

               |HAZ PonCampoSQL2;
               aTextAux = "";
          |SINO;
               aTextAux = aTextAux + aLetra;
          |FINSI;
     |SIGUE;

     ||ULTIMOS CAMPO. Parece que no mantiene el numero de campos.
     |SI nContador > 1;
          nContador = nContador + 1;
          |HAZ PonCampoSQL2;
     |FINSI;

     ||Falta normalizar la fecha
     aFechaNorma = aFechaIni;
     |HAZ NormalizaFecha;
     aFechaIni = aFechaNorma;

     |SI aCodHis ! ""; |Y aNumEpi ! "";
          ||GRABO EL REGISTRO 102
          |DDEFECTO #102;
          #102#0 = aCodHis;
          #102#1 = aNumEpi;
          |LEE 101#102.=;
          |SI FSalida ! 0; |GRABA 020#102.b; |FINSI;
          #102#2 = aFechaIni;
          #102#3 = aSit;
          |GRABA 020#102.a;
          |LIBERA #102;

          |SI aNomDes = "sql8.txt";
               |LEE 000#dsarcl10.u;
               |SI FSalida = 0;
                    nUltimo = #dsarcl10#0 + 1;
               |SINO;
                    nUltimo = 1;
               |FINSI;
               |DDEFECTO #dsarcl10;
               #dsarcl10#0 = nUltimo;
               |LEE 101#dsarcl10.=;
               |SI FSalida ! 0;
                    #dsarcl10#1 = aSistema;
                    #dsarcl10#2 = aHoraSis;
                    #dsarcl10#3 = "sql8";

                    aFechaNorma = aFechaGra % 110;
                    |HAZ NormalizaFecha;
                    #dsarcl10#4 = aFechaNorma;
                    aHoraBBDD = aFechaGra % 1208;
                    |QBF aHoraBBDD;
                    #dsarcl10#5 = aHoraBBDD;

                    #dsarcl10#9 = #dsarcl02#0;
                    #dsarcl10#8 = #dsarcl02#1;
                    |GRABA 020#dsarcl10.n;
                    |LIBERA #dsarcl10;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO PonCampoSQL2;
     |QBF aTextAux;
     |SI nContador = 1;
          aNumEpi = aTextAux;
     |FINSI;
     |SI nContador = 2;
          aFechaIni = aTextAux;
     |FINSI;
     |SI nContador = 3;
          aSit = aTextAux;
     |FINSI;
     |SI nContador = 4;
          aCodHis = aTextAux;
     |FINSI;
     |SI nContador = 5;
          aFechaGra = aTextAux;
     |FINSI;
|FPRC;

|PROCESO MeteLineaSql3;
     aLong = aLinea % A0;
     nLong = aLong;

     ||ACERA VARIABLES.
     aPacOld = "";
     aHisOld = "";
     aPacNew = "";
     aHisNew = "";
     aVarOrigen = "";
     aFechaReg = "";
     aHoraReg = "";
     aFechaAux = "";

     nContador = 0;
     aTextAux = "";
     |PARA nVoy = 1; |SI nVoy [ nLong; |HACIENDO nVoy = nVoy + 1;
          aSaca = nVoy + "01";
          aLetra = aLinea % aSaca;
          |SI aLetra = aTab; |O aLetra = aReturnUnix;
               nContador = nContador + 1;

               |HAZ PonCampoSQL3;
               aTextAux = "";
          |SINO;
               aTextAux = aTextAux + aLetra;
          |FINSI;
     |SIGUE;

     ||ULTIMOS CAMPO. Parece que no mantiene el numero de campos.
     |SI nContador > 1;
          nContador = nContador + 1;
          |HAZ PonCampoSQL3;
     |FINSI;

     |QBF aFechaAux;
     |SI aFechaAux ! "";
          aFechaReg = aFechaAux % 110;
          |QBF aFechaReg;
          aHoraReg = aFechaAux % 1208;
          |QBF aHoraReg;
          aHoraReg = ("0" + aHoraReg) % -108;
     |FINSI;

     ||Falta normalizar la fecha
     aFechaNorma = aFechaReg;
     |HAZ NormalizaFecha;
     aFechaReg = aFechaNorma;

     |SI aFechaReg ! ""; |Y aHoraReg ! "";
          ||GRABO EL REGISTRO 103
          |DDEFECTO #103;
          #103#0 = aFechaReg;
          #103#1 = aHoraReg;
          |LEE 101#103.=;
          |SI FSalida ! 0;
               #103#2 = aPacOld;
               #103#3 = aHisOld;
               #103#4 = aPacNew;
               #103#5 = aHisNew;
               #103#6 = aVarOrigen;
               |GRABA 020#103.n;
          |FINSI;
          |LIBERA #103;
     |FINSI;
|FPRC;

|PROCESO PonCampoSQL3;
     |QBF aTextAux;
     |SI nContador = 1;
          aPacNew = aTextAux;
     |FINSI;
     |SI nContador = 2;
          aHisOld = aTextAux;
     |FINSI;
     |SI nContador = 3;
          aFechaAux = aTextAux;
     |FINSI;
     |SI nContador = 4;
          aPacOld = aTextAux;
     |FINSI;
     |SI nContador = 5;
          aHisNew = aTextAux;
     |FINSI;
     |SI nContador = 6;
          aVarOrigen = aTextAux;
     |FINSI;
|FPRC;

|PROCESO MeteLineaSql5;
     aLong = aLinea % A0;
     nLong = aLong;

     ||ACERA VARIABLES.
     aNumEpi = "";
     aHisOld = "";
     aHisNew = "";
     aVarOrigen = "";
     aFechaReg = "";
     aHoraReg = "";
     aFechaAux = "";

     nContador = 0;
     aTextAux = "";
     |PARA nVoy = 1; |SI nVoy [ nLong; |HACIENDO nVoy = nVoy + 1;
          aSaca = nVoy + "01";
          aLetra = aLinea % aSaca;
          |SI aLetra = aTab; |O aLetra = aReturnUnix;
               nContador = nContador + 1;

               |HAZ PonCampoSQL5;
               aTextAux = "";
          |SINO;
               aTextAux = aTextAux + aLetra;
          |FINSI;
     |SIGUE;

     ||ULTIMOS CAMPO. Parece que no mantiene el numero de campos.
     |SI nContador > 1;
          nContador = nContador + 1;
          |HAZ PonCampoSQL5;
     |FINSI;

     |QBF aFechaAux;
     |SI aFechaAux ! "";
          aFechaReg = aFechaAux % 110;
          |QBF aFechaReg;
          aHoraReg = aFechaAux % 1208;
          |QBF aHoraReg;
          aHoraReg = ("0" + aHoraReg) % -108;
     |FINSI;

     ||Falta normalizar la fecha
     aFechaNorma = aFechaReg;
     |HAZ NormalizaFecha;
     aFechaReg = aFechaNorma;

     |SI aFechaReg ! ""; |Y aHoraReg ! "";
          ||GRABO EL REGISTRO 105
          |DDEFECTO #105;
          #105#0 = aFechaReg;
          #105#1 = aHoraReg;
          |LEE 101#105.=;
          |SI FSalida ! 0;
               #105#2 = aHisOld;
               #105#3 = aHisNew;
               #105#4 = aNumEpi;
               #105#5 = aVarOrigen;
               |GRABA 020#105.n;
          |FINSI;
          |LIBERA #105;
     |FINSI;
|FPRC;

|PROCESO PonCampoSQL5;
     |QBF aTextAux;
     |SI nContador = 1;
          aNumEpi = aTextAux;
     |FINSI;
     |SI nContador = 2;
          aHisOld = aTextAux;
     |FINSI;
     |SI nContador = 3;
          aHisNew = aTextAux;
     |FINSI;
     |SI nContador = 4;
          aFechaAux = aTextAux;
     |FINSI;
     |SI nContador = 5;
          aVarOrigen = aTextAux;
     |FINSI;
|FPRC;

|PROCESO MeteLineaSql6;
     aLong = aLinea % A0;
     nLong = aLong;

     ||ACERA VARIABLES.
     aNumEpi = "";
     aCodHis = "";
     aVarOrigen = "";
     aFechaReg = "";
     aHoraReg = "";
     aFechaAux = "";

     nContador = 0;
     aTextAux = "";
     |PARA nVoy = 1; |SI nVoy [ nLong; |HACIENDO nVoy = nVoy + 1;
          aSaca = nVoy + "01";
          aLetra = aLinea % aSaca;
          |SI aLetra = aTab; |O aLetra = aReturnUnix;
               nContador = nContador + 1;

               |HAZ PonCampoSQL6;
               aTextAux = "";
          |SINO;
               aTextAux = aTextAux + aLetra;
          |FINSI;
     |SIGUE;

     ||ULTIMOS CAMPO. Parece que no mantiene el numero de campos.
     |SI nContador > 1;
          nContador = nContador + 1;
          |HAZ PonCampoSQL6;
     |FINSI;

     |QBF aFechaAux;
     |SI aFechaAux ! "";
          aFechaReg = aFechaAux % 110;
          |QBF aFechaReg;
          aHoraReg = aFechaAux % 1208;
          |QBF aHoraReg;
          aHoraReg = ("0" + aHoraReg) % -108;
     |FINSI;

     ||Falta normalizar la fecha
     aFechaNorma = aFechaReg;
     |HAZ NormalizaFecha;
     aFechaReg = aFechaNorma;

     |SI aFechaReg ! ""; |Y aHoraReg ! "";
          ||GRABO EL REGISTRO 106
          |DDEFECTO #106;
          #106#0 = aFechaReg;
          #106#1 = aHoraReg;
          |LEE 101#106.=;
          |SI FSalida ! 0;
               #106#2 = aCodHis;
               #106#3 = aNumEpi;
               #106#4 = aVarOrigen;
               |GRABA 020#106.n;
          |FINSI;
          |LIBERA #106;
     |FINSI;
|FPRC;

|PROCESO PonCampoSQL6;
     |QBF aTextAux;
     |SI nContador = 1;
          aNumEpi = aTextAux;
     |FINSI;
     |SI nContador = 2;
          aCodHis = aTextAux;
     |FINSI;
     |SI nContador = 3;
          aFechaAux = aTextAux;
     |FINSI;
     |SI nContador = 4;
          aVarOrigen = aTextAux;
     |FINSI;
|FPRC;

|PROCESO NormalizaFecha;
     |QBF aFechaNorma;
     |SI aFechaNorma = "";
          aFechaNorma = "01.01.0000";
     |SINO;
          aDia = aFechaNorma % 102;
          aMes = aFechaNorma % 402;
          aAnyo = aFechaNorma % -104;

          aFechaNorma = aDia + "." + aMes + "." + aAnyo;
     |FINSI;
|FPRC;

|PROCESO dsarm005_busca;
     aHisReg = #dsarm005#97;
     |QBF aHisReg;
     |SI aHisReg = "";
          |ACABA;
     |FINSI;
     |SI aCodHisBus ! aHisReg;
          |ACABA;
     |FINSI;

     aEpiReg = #dsarm005#98;
     |QBF aEpiReg;

     |SI aCodEpiBus ! "-1"; |Y aEpiReg ! aCodEpiBus;
          |ACABA;
     |FINSI;

     |LEE 000#dsarcl10.u;
     |SI FSalida = 0;
          nUltimo = #dsarcl10#0 + 1;
     |SINO;
          nUltimo = 1;
     |FINSI;
     |DDEFECTO #dsarcl10;
     #dsarcl10#0 = nUltimo;
     |LEE 101#dsarcl10.=;
     |SI FSalida ! 0;
          #dsarcl10#1 = aSistema;
          #dsarcl10#2 = aHoraSis;
          |SI aNomDes = "sql3.txt";
               #dsarcl10#3 = "sql3";
          |SINO;
               |SI aNomDes = "sql4.txt";
                    #dsarcl10#3 = "sql4";
               |SINO;
                    |SI aNomDes = "sql5.txt";
                         #dsarcl10#3 = "sql5";
                    |SINO;
                         |SI aNomDes = "sql6.txt";
                              #dsarcl10#3 = "sql6";
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
          |SI aNomDes = "sql3.txt";
               #dsarcl10#4 = #dsarcl03#0;
               #dsarcl10#5 = #dsarcl03#1;
               #dsarcl10#7 = #dsarcl03#3;
               #dsarcl10#8 = #dsarm005#98;
               #dsarcl10#9 = #dsarcl03#5;
          |FINSI;
          |SI aNomDes = "sql5.txt";
               #dsarcl10#4 = #dsarcl05#0;
               #dsarcl10#5 = #dsarcl05#1;
               #dsarcl10#7 = #dsarcl05#2;
               #dsarcl10#8 = #dsarcl05#4;
               #dsarcl10#9 = #dsarcl05#3;
          |FINSI;
          |SI aNomDes = "sql6.txt";
               #dsarcl10#4 = #dsarcl06#0;
               #dsarcl10#5 = #dsarcl06#1;
               #dsarcl10#7 = #dsarcl06#2;
               #dsarcl10#8 = #dsarcl06#3;
               #dsarcl10#11 = "S";
          |FINSI;

          #dsarcl10#6 = #dsarm005#0;

          |GRABA 020#dsarcl10.n;
          |LIBERA #dsarcl10;

          |SI aNomDes = "sql3.txt";
               #dsarm005#97 = #dsarcl03#5;
          |FINSI;
          |SI aNomDes = "sql5.txt";
               #dsarm005#97 = #dsarcl05#3;
          |FINSI;
          |SI aNomDes = "sql6.txt";
               aAux = #dsarm005#98;
               |QBF aAux;
               aAux = aAux + "[ANULADO]";
               #dsarm005#98 = aAux;
          |FINSI;
          |GRABA 000#dsarm005.a;
     |FINSI;
|FPRC;

|DEFBUCLE dsarm005_busca;
 #dsarm005#1;
 ;
 INICIO;
 FINAL;
 ;
 NULL, dsarm005_busca;
|FIN;

|PROCESO dsarcl03;
     aCodHisBus = #dsarcl03#3;
     |QBF aCodHisBus;
     aCodEpiBus = "-1";            ||TODOS
     |QBF aCodEpiBus;
     |BUCLE dsarm005_busca;

     #dsarcl03#7 = "S";
     |GRABA 020#dsarcl03.a;
|FPRC;

|DEFBUCLE dsarcl03;
 #dsarcl03#1;
 7;
 "01.01.0000", "        ", "N";
 "31.12.2099", "zzzzzzzz", "N";
 be;
 NULL, dsarcl03;
|FIN;

|PROCESO dsarcl05;
     aCodHisBus = #dsarcl05#2;
     |QBF aCodHisBus;
     aCodEpiBus = #dsarcl05#4;
     |QBF aCodEpiBus;
     |BUCLE dsarm005_busca;

     #dsarcl05#6 = "S";
     |GRABA 020#dsarcl05.a;
|FPRC;

|DEFBUCLE dsarcl05;
 #dsarcl05#1;
 6;
 "01.01.0000", "        ", "N";
 "31.12.2099", "zzzzzzzz", "N";
 be;
 NULL, dsarcl05;
|FIN;

|PROCESO dsarcl06;
     aCodHisBus = #dsarcl06#2;
     |QBF aCodHisBus;
     aCodEpiBus = #dsarcl06#3;
     |QBF aCodEpiBus;
     |BUCLE dsarm005_busca;

     #dsarcl06#5 = "S";
     |GRABA 020#dsarcl06.a;
|FPRC;

|DEFBUCLE dsarcl06;
 #dsarcl06#1;
 5;
 "01.01.0000", "        ", "N";
 "31.12.2099", "zzzzzzzz", "N";
 be;
 NULL, dsarcl06;
|FIN;

|PROCESO ProcesaRes;
     ||Segun la sql, haremos una cosa u otra.
     ||Pero en todos leeremos el fichero para actualizar los datos

     ||ARA3838 QUITAR. QUITADO
     ||aMensaje = "0000CCC. Copia de datos/" + aNomDes + ".res a documentos\dsarchi";
     ||MENAV aMensaje;

     |SI aNomDes = "sql1.txt";

          ||Lo creo con otro nombre y luego lo muevo.

          aNomTmp = "10000sql";

          |NOME_DAT #101 aNomTmp;

          |ABRE #101;
          |CIERRA #101;
          |DELINDEX #101;

          |ABRE #101;
          |XABRE "CU", aFicheroRes, nHandleRes;
          |SI FSalida < 0;
               aMenLog = "No puedo abrir " + aFicheroRes;
               |HAZ MeteLog;
          |SINO;
               |PARA; |SI; |HACIENDO;
                    |XLEE nHandleRes, 250, aLinea;
                    |SI FSalida < 0;
                         |SAL;
                    |SINO;
                         |SI FSalida ! 0;
                              |QBF aLinea;
                              |SI aLinea ! "";
                                   |HAZ MeteLineaSql1;
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |SIGUE;
               |XCIERRA nHandleRes;
          |FINSI;
          |CIERRA #101;

          |DBASE aBase;
          |QBF aBase;

          aOrigen = aBase + "fich/" + aNomTmp + ".dat";
          aDestino = aBase + "fich/dsarcl01.dat";
          |COPIA_FICHERO aOrigen, aDestino;
          |FBORRA aOrigen;

          aOrigen = aBase + "fich/" + aNomTmp + ".ddx";
          aDestino = aBase + "fich/dsarcl01.ddx";
          |COPIA_FICHERO aOrigen, aDestino;
          |FBORRA aOrigen;

          aOrigen = aBase + "fich/" + aNomTmp + ".ixx";
          aDestino = aBase + "fich/dsarcl01.ixx";
          |COPIA_FICHERO aOrigen, aDestino;
          |FBORRA aOrigen;
     |FINSI;

     |SI aNomDes = "sql2.txt";

          aNomTmp = "20000sql";

          |NOME_DAT #102 aNomTmp;

          |ABRE #102;
          |CIERRA #102;
          |DELINDEX #102;

          |ABRE #102;
          |XABRE "CU", aFicheroRes, nHandleRes;
          |SI FSalida < 0;
               aMenLog = "No puedo abrir " + aFicheroRes;
               |HAZ MeteLog;
          |SINO;
               |PARA; |SI; |HACIENDO;
                    |XLEE nHandleRes, 250, aLinea;
                    |SI FSalida < 0;
                         |SAL;
                    |SINO;
                         |SI FSalida ! 0;
                              |QBF aLinea;
                              |SI aLinea ! "";
                                   |HAZ MeteLineaSql2;
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |SIGUE;
               |XCIERRA nHandleRes;
          |FINSI;
          |CIERRA #102;

          |DBASE aBase;
          |QBF aBase;

          aOrigen = aBase + "fich/" + aNomTmp + ".dat";
          aDestino = aBase + "fich/dsarcl02.dat";
          |COPIA_FICHERO aOrigen, aDestino;
          |FBORRA aOrigen;

          aOrigen = aBase + "fich/" + aNomTmp + ".ddx";
          aDestino = aBase + "fich/dsarcl02.ddx";
          |COPIA_FICHERO aOrigen, aDestino;
          |FBORRA aOrigen;

          aOrigen = aBase + "fich/" + aNomTmp + ".ixx";
          aDestino = aBase + "fich/dsarcl02.ixx";
          |COPIA_FICHERO aOrigen, aDestino;
          |FBORRA aOrigen;
     |FINSI;

     |SI aNomDes = "sql3.txt";
          |ABRE #103;
          |XABRE "CU", aFicheroRes, nHandleRes;
          |SI FSalida < 0;
               aMenLog = "No puedo abrir " + aFicheroRes;
               |HAZ MeteLog;
          |SINO;
               |PARA; |SI; |HACIENDO;
                    |XLEE nHandleRes, 250, aLinea;
                    |SI FSalida < 0;
                         |SAL;
                    |SINO;
                         |SI FSalida ! 0;
                              |QBF aLinea;
                              |SI aLinea ! "";
                                   |HAZ MeteLineaSql3;
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |SIGUE;
               |XCIERRA nHandleRes;
          |FINSI;
          |CIERRA #103;

          ||Proceso los nuevos registros, actualizando y metiendo
          ||en el log. Al terminar Procesado = "S";
          |BUCLE dsarcl03;
     |FINSI;

     |SI aNomDes = "sql5.txt";
          |ABRE #105;
          |XABRE "CU", aFicheroRes, nHandleRes;
          |SI FSalida < 0;
               aMenLog = "No puedo abrir " + aFicheroRes;
               |HAZ MeteLog;
          |SINO;
               |PARA; |SI; |HACIENDO;
                    |XLEE nHandleRes, 250, aLinea;
                    |SI FSalida < 0;
                         |SAL;
                    |SINO;
                         |SI FSalida ! 0;
                              |QBF aLinea;
                              |SI aLinea ! "";
                                   |HAZ MeteLineaSql5;
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |SIGUE;
               |XCIERRA nHandleRes;
          |FINSI;
          |CIERRA #105;

          ||Proceso los nuevos registros, actualizando y metiendo
          ||en el log. Al terminar Procesado = "S";
          |BUCLE dsarcl05;
     |FINSI;

     |SI aNomDes = "sql6.txt";
          |ABRE #106;
          |XABRE "CU", aFicheroRes, nHandleRes;
          |SI FSalida < 0;
               aMenLog = "No puedo abrir " + aFicheroRes;
               |HAZ MeteLog;
          |SINO;
               |PARA; |SI; |HACIENDO;
                    |XLEE nHandleRes, 250, aLinea;
                    |SI FSalida < 0;
                         |SAL;
                    |SINO;
                         |SI FSalida ! 0;
                              |QBF aLinea;
                              |SI aLinea ! "";
                                   |HAZ MeteLineaSql6;
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |SIGUE;
               |XCIERRA nHandleRes;
          |FINSI;
          |CIERRA #106;

          ||Proceso los nuevos registros, actualizando y metiendo
          ||en el log. Al terminar Procesado = "S";
          |BUCLE dsarcl06;
     |FINSI;

     |SI aNomDes = "sql7.txt";          ||Incrementales de Historias
          ||ARA38
          |ABRE #101;
          |XABRE "CU", aFicheroRes, nHandleRes;
          |SI FSalida < 0;
               aMenLog = "No puedo abrir " + aFicheroRes;
               |HAZ MeteLog;
          |SINO;
               |PARA; |SI; |HACIENDO;
                    |XLEE nHandleRes, 250, aLinea;
                    |SI FSalida < 0;
                         |SAL;
                    |SINO;
                         |SI FSalida ! 0;
                              |QBF aLinea;
                              |SI aLinea ! "";
                                   |HAZ MeteLineaSql1;
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |SIGUE;
               |XCIERRA nHandleRes;
          |FINSI;
          |CIERRA #101;

          ||ARA38
          ||Faltaria meter en el log. especial.  Mod. Historia XXXX
     |FINSI;

     |SI aNomDes = "sql8.txt";          ||Incrementales de Episodios

          ||Idem para episodios.
          |ABRE #102;
          |XABRE "CU", aFicheroRes, nHandleRes;
          |SI FSalida < 0;
               aMenLog = "No puedo abrir " + aFicheroRes;
               |HAZ MeteLog;
          |SINO;
               |PARA; |SI; |HACIENDO;
                    |XLEE nHandleRes, 250, aLinea;
                    |SI FSalida < 0;
                         |SAL;
                    |SINO;
                         |SI FSalida ! 0;
                              |QBF aLinea;
                              |SI aLinea ! "";
                                   |HAZ MeteLineaSql2;
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |SIGUE;
               |XCIERRA nHandleRes;
          |FINSI;
          |CIERRA #102;
     |FINSI;
|FPRC;

|PROCESO ProcesaErr;
     ||Aqui lo que vamos a hacer es actualizar el log.
     ||Algo de este estilo.
     ||>INICIO ERROR EJECUCION SQL
     ||> sql.txt
     ||> ....
     ||> .err:
     ||> ......
     ||>FINAL  ERROR EJECUCION SQL

     |XABRE "CU", aFicheroErr, nHandleErr;
     |SI FSalida < 0;
          nSwErrorSQL = 1;
          aMenLog = "No puedo abrir " + aFicheroErr;
          |HAZ MeteLog;
     |SINO;
          |PARA; |SI; |HACIENDO;
               |XLEE nHandleErr, 250, aLinea;
               |SI FSalida < 0;
                    |SAL;
               |SINO;
                    |SI FSalida ! 0;
                         |QBF aLinea;
                         |SI aLinea = "OK";
                              nSwErrorSQL = 0;
                              aMenLog = " Ejecucion SQL [" + aNomDes + "]  realizada CORRECTAMENTE";
                              |HAZ MeteLog;
                         |SINO;
                              nSwErrorSQL = 1;
                              aMenLog = " " + aLinea;
                              |HAZ MeteLog;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |SIGUE;
          |XCIERRA nHandleErr;
     |FINSI;
|FPRC;

|PROCESO TraeSQL;
     aOrigen = aPathBase + "fich/" + aNomOri;
     |XABRE "E", aOrigen, nHandle;
     |SI FSalida < 0;
          aMenLog = "No existe " + aDesSql + " [" + aNomOri + "]";
          |HAZ MeteLog;
          |ACABA;
     |FINSI;
     |XABRE "U", aOrigen, nHandle;
     |SI FSalida ] 0;
          aFicSQL = aPathBase + "fich/" + aNomDes;
          |XABRE "W", aFicSQL, nHandle2;
          |SI FSalida < 0;
               aMenLog = "Error al crear " + aDesSql;
               |HAZ MeteLog;
               |ACABA;
          |FINSI;
          aMeteTxt = aUsuario;
          |XGRABA nHandle2, aMeteTxt;
          aMeteTxt = aServidor;
          |XGRABA nHandle2, aMeteTxt;
          aMeteTxt = aNombreBBDD;
          |XGRABA nHandle2, aMeteTxt;
          |PARA; |SI; |HACIENDO;
               |XLEE nHandle, 250, aLinea;
               |SI FSalida < 0; |SAL; |FINSI;
               aMeteTxt = aLinea;

               aAlfa1 = aMeteTxt - "*FECHAULTIMAEJECUCION*";
               |SI FSalida ! 0;
                    aTextoOld = "*FECHAULTIMAEJECUCION*";
                    |SI nModoHora = 0;
                         aDia = aFechaAnt % 102;
                         aMes = aFechaAnt % 402;
                         aAnyo = aFechaAnt % -104;
                         aTextoNew = aDia + "/" + aMes + "/" + aAnyo + " " + aHoraAnt + ":00";
                    |SINO;
                         aDia = aFechaMenosUna % 102;
                         aMes = aFechaMenosUna % 402;
                         aAnyo = aFechaMenosUna % -104;
                         aTextoNew = aDia + "/" + aMes + "/" + aAnyo + " " + aHoraMenosUna + ":00";
                    |FINSI;
                    |HAZ SustituyeTexto;
               |FINSI;

               |XGRABA nHandle2, aMeteTxt;
          |SIGUE;

          |SI aNomDes = "sql7.txt"; |O aNomDes = "sql8.txt";
               aMeteTxt = " where fecgra >= '*FECHAULTIMAEJECUCION*'";

               aTextoOld = "*FECHAULTIMAEJECUCION*";
               |SI nModoHora = 0;
                    aDia = aFechaAnt % 102;
                    aMes = aFechaAnt % 402;
                    aAnyo = aFechaAnt % -104;
                    aTextoNew = aDia + "/" + aMes + "/" + aAnyo + " " + aHoraAnt + ":00";
               |SINO;
                    aDia = aFechaMenosUna % 102;
                    aMes = aFechaMenosUna % 402;
                    aAnyo = aFechaMenosUna % -104;
                    aTextoNew = aDia + "/" + aMes + "/" + aAnyo + " " + aHoraMenosUna + ":00";
               |FINSI;
               |HAZ SustituyeTexto;

               |XGRABA nHandle2, aMeteTxt;
          |FINSI;

          |XCIERRA nHandle2;
          |XCIERRA nHandle;
     |FINSI;

     eaDestino = eaUnidad + ":\DOCUMENTOS\DSARCHI\" + aNomDes;
     |RFBORRA eaDestino;

     eaOrigen = aPathBase + "fich/" + aNomDes;
     |HAZ EnviaFicheroConMD5;
|FPRC;

|PROCESO SustituyeTexto;
     aLong = aMeteTxt % A0;
     nLong = aLong;
     nTamTotal = nLong;

     aLong = aTextoOld % A0;
     nLong = aLong;

     |PARA nVoy = 1; |SI nVoy [ nTamTotal; |HACIENDO nVoy = nVoy + 1;
          nSaca = (100 * nVoy) + nLong;
          aSaca = nSaca;
          aTexto = aMeteTxt % aSaca;
          |SI aTexto = aTextoOld;
               nSaca = 100 + nVoy - 1;
               aSaca = nSaca;
               aSaca = aMeteTxt % aSaca;
               aIni = aSaca;
               aFin = aMeteTxt - aIni - aTextoOld;

               aMeteTxt = aIni + aTextoNew + aFin;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO DiferenciaMinutos;
     ||Paso ambas HH:MM a minutos y resto.
     ||aHoraAnt
     ||aHoraSis

     aAux = aHoraAnt % 102;
     nHoraAnt = aAux;
     aAux = aHoraAnt % 402;
     nMinAnt = aAux;
     nMinAnt = (60 * nHoraAnt) + nMinAnt;

     aAux = aHoraSis % 102;
     nHoraSis = aAux;
     aAux = aHoraSis % 402;
     nMinSis = aAux;
     nMinSis = (60 * nHoraSis) + nMinSis;

     nDifMinutos = nMinSis - nMinAnt;
|FPRC;

|PROGRAMA;
     ||Ejecutar  TODO, BASE (HISTORIAS/EPISODIOS) o INCREMENTALES

     aTab = &9;
     aReturnUnix = &10;

     |CLS;
     aParametro = PARAMETRO;
     |QBF aParametro;

     nModoHora = 0;
     |SI aParametro ! "";
          |SI aParametro = "TODO";
               aSwModo = "TODO";
          |SINO;
               |SI aParametro = "BASE";
                    aSwModo = "BASE";
               |SINO;
                    |SI aParametro = "INCRHORA";
                         nModoHora = 1;
                         aSwModo = "INCR";
                    |SINO;
                         aSwModo = "INCR";
                    |FINSI;
               |FINSI;
          |FINSI;
          aParametro = "AUTO";
     |FINSI;

     |DBASE aBase;
     |QBF aBase;

     aFicLog = aBase + "logs/";
     |MKDIR aFicLog;
     aFicLog = aFicLog + "datos_clinica.log";

     |ABRE #dsarcl99;
     |DDEFECTO #dsarcl99;
     |LEE 000#dsarcl99.p;
     aFechaAnt = #dsarcl99#1;
     aHoraAnt = #dsarcl99#2;

     |HORA aHoraSis;
     |QBF aHoraSis;
     aHoraSis = aHoraSis % 105;

     fSistema = ~;
     aSistema = fSistema;
     |SI aFechaAnt = aSistema; |Y #dsarcl99#3 ! 0;
          ||CONTROL de la ultima ejecucion

          nDifMinutos = 0;
          |HAZ DiferenciaMinutos;

          |SI nDifMinutos [ #dsarcl99#3;
               aMenLog = "Proceso de actualizacion YA en marcha desde las " +  aHoraAnt;
               |HAZ MeteLog;

               |SI aParametro ! "AUTO";
                    aMensaje = "0000Proceso de actualizacion YA en marcha desde las " +  aHoraAnt;
                    |MENAV aMensaje;
               |FINSI;
               |ACABA;
          |FINSI;
     |FINSI;

     |SI aParametro = "AUTO";
          nModoAuto = 1;
          |HAZ Principal;
     |SINO;
          |ABRE #dsarclz1;
          |PINPA #0#dsarclz1;
          |ENDATOS #1#dsarclz1;
          |SI FSalida = 0;
               |SI #dsarclz1#1 = "T";
                    aSwModo = "TODO";
               |SINO;
                    |SI #dsarclz1#1 = "B";
                         aSwModo = "BASE";
                    |SINO;
                         aSwModo = "INCR";
                         |SI #dsarclz1#1 = "H";
                              nModoHora = 1;
                         |FINSI;
                    |FINSI;
               |FINSI;
               nModoAuto = 0;
               |HAZ Principal;
          |FINSI;
          |CIERRA #dsarclz1;
     |FINSI;
     |CIERRA #dsarcl99;
|FPRO;
