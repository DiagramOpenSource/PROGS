|FICHEROS;
     dsarz059;

     dsarm005;

     :/laboral/def/nomw0003;
|FIN;

|VARIABLES;
     n1Vent           = 0;
     nGrabo           = 0;

     aTecla           = "";
     aRetu            = "";
     aAlfa1           = "";
     aAlfa2           = "";

     {-1}aVent        = "";
         aVent1       = 0;
         aVent2       = 0;
         aVent3       = 0;
         aVent4       = 0;
         aVent5       = "";

     &enIdeCli        = 0;
     &eaNomCli        = "";
     &eaNifCli        = "";
     &eaMaiCli        = "";
     &eaTelCli        = "";
     &eaIdeNif        = "";
     &eaNomNif        = "";
     &eaCodNif        = "";
|FIN;

|PROCESO dsarm005;
     |PULSATECLA;

     |SI #dsarm005#8  = 0;
      |Y #dsarm005#10 = "            ";
      |Y #dsarm005#15 = 0;
         |ACABA;
     |FINSI;

     nGrabo = 0;
     |SI #dsarm005#8  ! 0;
         enIdeCli = #dsarm005#8;
         |HAZ LeeCliente;

         aAlfa1 = #dsarm005#14;  |QBF aAlfa1;
         aAlfa2 = eaNomCli;      |QBF aAlfa2;

         |SI aAlfa1 ! aAlfa2;
             #dsarm005#14 = eaNomCli;
             nGrabo        = 1;
         |FINSI;

         aAlfa1 = #dsarm005#10;  |QBF aAlfa1;
         aAlfa2 = eaNifCli;      |QBF aAlfa2;

         |SI aAlfa1 ! aAlfa2;
             #dsarm005#10 = eaNifCli;
             nGrabo        = 1;
         |FINSI;
     |FINSI;

     |SI #dsarm005#10 ! "            ";
          eaCodNif = #dsarm005#10;
          |HAZ LeeNifs;

          aAlfa1 = #dsarm005#38;  |QBF aAlfa1;
          aAlfa2 = eaNomNif;      |QBF aAlfa2;

          |SI aAlfa1 ! aAlfa2;
              #dsarm005#38 = eaNomNif;
              nGrabo        = 1;
          |FINSI;
     |FINSI;

     |SI #dsarm005#15 ! 0;
         |ABRE #nomw0003;
         #nomw0003#0 = #dsarm005#8;
         #nomw0003#1 = #dsarm005#15;
         |LEE 000#nomw0003.=;
         |SI FSalida ! 0;  |DDEFECTO #nomw0003;  |FINSI;
         |CIERRA #nomw0003;

         aAlfa1 = #dsarm005#16;  |QBF aAlfa1;
         aAlfa2 = #nomw0003#2;   |QBF aAlfa2;

         |SI aAlfa1 ! aAlfa2;
             #dsarm005#16 = #nomw0003#2;
             nGrabo        = 1;
         |FINSI;

         aAlfa1 = #dsarm005#95;  |QBF aAlfa1;
         aAlfa2 = #nomw0003#7;   |QBF aAlfa2;

         |SI aAlfa1 ! aAlfa2;
             #dsarm005#95 = #nomw0003#7;
             nGrabo        = 1;
         |FINSI;
     |FINSI;

     |SI nGrabo = 0;  |ACABA;  |FINSI;

     |GRABA 020#dsarm005.a;
     |LIBERA #dsarm005;
|FPRC;

|DEFBUCLE dsarm005;
     #dsarm005#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL, dsarm005;
|FIN;

|PROCESO Ptec807;
     |PTEC 807;
|FPRC;

|PROCESO Ptec802;
     |PTEC 802;
|FPRC;

|PROCESO Recalculo;
     |CONFI "0000SSeguros que quiere realizar el proceso.";
     |SI FSalida ! 0;
         |MENAV "0000Proceso cancelado.";
         |ACABA;
     |FINSI;

     |VENTANA 0501, 0540, -1, 16, "Procesando trabajo";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     |BUCLE dsarm005;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;
|FPRC;

|PROGRAMA;
     |CLS;
     |CABEZA "Recalculo de datos";

     |PINPA #0#dsarz059;

     |CONTROL_SIMPLE 0, "BOTON,{{207}}Salir", 2577, 2586, Ptec807;
     |CONTROL_SIMPLE 0, "BOTON,{{230}}Continuar", 2588, 2598, Ptec802;

     aRetu  = &255 + "802";

     FSalida = "::,";
     |LEETECLA aTecla;

     |SI aRetu = aTecla;
         |HAZ Recalculo;
     |FINSI;
|FPRO;

|PROCESO Tipo80;  |TIPO 80;
     FSalida = 2599;
|FPRC;
