||TODO CCC. Para boton:   ACTUALIZAR CON PATRONES
||Hasta ahora los patrones son del grupo al 1 al 5.
||si se anyaden patrones con un numero de grupo superior al 5, habria que
||modificar esta variable. nHGrupoPatron


|FICHEROS;
     dsarz999;

     dsarm002;
     dsarm003;
     dsarm004;
     dsarm005;
     dsarm008;
     dsarm059;

     referen@;      ||Para los CARGA_DEF
|FIN;

|VARIABLES;
     aDirBase = "";
     aDef = "";
     nCampo = 0;
     aVacio60 = "";
     aZeta60 = "";

     aLabel = "";
     nDGrupoPatron = 0;
     nHGrupoPatron = 0;
     nSwHayGrupos = 0;
     nBotonActua = 0;
     aMensaje = "";


     aAlfa         = "";
     aRuta         = "";
     aLong         = "";
     aCaracter     = "";
     aNombreFic    = "";
     aNombreRea    = "";
     aExtension    = "";
     aEsc1         = "";
     aEsc2         = "";
     aRetu         = "";
     aTecla        = "";
     aBase         = "";
     aDir          = "";
     aDirDestino   = "";
     aOrigen       = "";
     aDestino      = "";
     aIPRemoto     = "";
     aFichero      = "";

     nHay          = 0;
     nHandle       = 0;
     nDatos        = 0;
     nLong         = 0;
     nCarac        = 0;
     nPos          = 0;
     nVentana      = 0;
     nPanta0       = 0;
     nBotonImporta = 0;
     nBotonExporta = 0;
     nBotonPredet  = 0;
     nPredetermin  = 0;

     {-1}aVent     = "";
         aVent1    = 0;
         aVent2    = 0;
         aVent3    = 0;
         aVent4    = 0;
         aVent5    = "";

     {200}aPopup   = "";
|FIN;

|PROCESO MiraDatos;
     nDatos = 0;

     |ABRE #dsarm002;
     |LEE 000#dsarm002.p;
     |SI FSalida = 0;  nDatos = 1;  |FINSI;
     |CIERRA #dsarm002;

     |ABRE #dsarm003;
     |LEE 000#dsarm003.p;
     |SI FSalida = 0;  nDatos = 1;  |FINSI;
     |CIERRA #dsarm003;

     |ABRE #dsarm004;
     |LEE 000#dsarm004.p;
     |SI FSalida = 0;  nDatos = 1;  |FINSI;
     |CIERRA #dsarm004;

     |ABRE #dsarm008;
     |LEE 000#dsarm008.p;
     |SI FSalida = 0;  nDatos = 1;  |FINSI;
     |CIERRA #dsarm008;

     |ABRE #dsarm059;
     |LEE 000#dsarm059.p;
     |SI FSalida = 0;  nDatos = 1;  |FINSI;
     |CIERRA #dsarm059;
|FPRC;


|PROCESO InstalaFichero;
     |DBASE aBase;  |QBF aBase;
     |DFICE aDirDestino;

     aDestino = aDirDestino + "maestros.tgz";

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |SI nPredetermin = 0;
         |SI aIPRemoto ! "";
             |RECIBE_FICHERO aOrigen, aDestino;
         |SINO;
             |COPIA_FICHERO aOrigen, aDestino;
         |FINSI;
     |SINO;
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;

     |XABRE "E", aDestino, nHandle;
     |SI FSalida < 0;
         |MENAV "0000Hubo un error al copiar el fichero al servidor.";
         |ACABA;
     |FINSI;

     |ABRE #dsarm002;  |CIERRA #dsarm002;  |DELINDEX #dsarm002;
     |ABRE #dsarm003;  |CIERRA #dsarm003;  |DELINDEX #dsarm003;
     |ABRE #dsarm004;  |CIERRA #dsarm004;  |DELINDEX #dsarm004;
     |ABRE #dsarm008;  |CIERRA #dsarm008;  |DELINDEX #dsarm008;
     |ABRE #dsarm059;  |CIERRA #dsarm059;  |DELINDEX #dsarm059;

     aAlfa = aRuta < "";
     Pos   = -1;
     |DESCOMPRIME aDestino;
     |DETAR aDestino, aDirDestino;

     aAlfa = aDestino;                      |FBORRA aAlfa;
     aAlfa = aDirDestino + "maestros.tar";  |FBORRA aAlfa;

     |ABRE #dsarm002;  |CIERRA #dsarm002;
     |ABRE #dsarm003;  |CIERRA #dsarm003;
     |ABRE #dsarm004;  |CIERRA #dsarm004;
     |ABRE #dsarm008;  |CIERRA #dsarm008;
     |ABRE #dsarm059;  |CIERRA #dsarm059;

     |MENAV "0000Estructuras de carpetas y vistas instaladas.";
|FPRC;

|PROCESO Importa;
     |HAZ MiraDatos;
     |SI nDatos = 1;
         |CONFI "0000NYa existen datos introducidos. Reinstalamos?";
         |SI FSalida ! 0;  |ACABA;  |FINSI;
     |FINSI;

     |MENAV "0000A continuacion abra el fichero con extesi¢n PTL a importar. Debe estar situado en su PC.";

     aRuta = "F*.ptl";
     |BROWSE aRuta;

     aAlfa = aRuta;
     aRuta = "";
     aLong = aAlfa % 0;
     nLong = aLong;
     |PARA nCarac = 2;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos       = (nCarac * 100) + 1;
           aCaracter  = aAlfa % nPos;
           aRuta      = aRuta      + aCaracter;
           aNombreFic = aNombreFic + aCaracter;

           |SI aCaracter = "/";  |O aCaracter = "\";
               aNombreFic = "";
           |FINSI;
     |SIGUE;

     aNombreRea = aNombreFic < "";
     aNombreFic = aNombreFic > "";
     aExtension = (aRuta % -104) > "";
     |SI aExtension ! ".PTL";
         |MENAV "0000Fichero no v lido. La extensi¢n debe ser PTL.";
         |ACABA;
     |FINSI;

     aOrigen      = aRuta;
     nPredetermin = 0;
     |HAZ InstalaFichero;
|FPRC;

|PROCESO Exporta;
     |MENAV "0000Seleccione a continuaci¢n un directorio para ubicar las estructuras";

     aRuta   = "D ";
     |BROWSE aRuta;

     aAlfa = aRuta;   |QBF aAlfa;
     aRuta = "";
     aLong = aAlfa % 0;
     nLong = aLong;
     |PARA nCarac = 2;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos = (nCarac * 100) + 1;
           aRuta = aRuta + (aAlfa % nPos);
     |SIGUE;

     |SI aRuta = "";
         |MENAV "0000No ha seleccionado una ubicaci¢n.";
         |ACABA;
     |FINSI;

     |VENTANA 0501, 0735, -1, 16, "Introduzca nombre del fichero sin extensi¢n";
     nVentana = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PARA;  |SI;  |HACIENDO;
          E_inf    = "";
          E_sup    = "25";
          aFichero = 0605 ? 1;
          aAlfa    = aFichero - ".";
          |SI FSalida = 0;  |SAL;  |FINSI;

          |MENAV "0000Introduzca el nombre del fichero sin extensi¢n";
     |SIGUE;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVentana;

     |QBF aFichero;
     |SI aFichero = "";
         |MENAV "0000Proceso cancelado.";
         |ACABA;
     |FINSI;

     |DFICE aDirDestino;
     aAlfa = aDirDestino + aFichero + ".tar" + " " + "dsarm002.dat dsarm003.dat dsarm004.dat dsarm008.dat dsarm059.dat";
     |ATAR aAlfa, aDirDestino;

     aAlfa = aDirDestino + aFichero + ".tar";
     |COMPRIME aAlfa;

     aAlfa = aDirDestino + aFichero + ".tar";   |RFBORRA aAlfa;

     aOrigen  = aDirDestino + aFichero + ".tgz";
     aDestino = aRuta + "\" + aFichero + ".ptl";

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |SI aIPRemoto ! "";
         |ENVIA_FICHERO aOrigen, aDestino;
     |SINO;
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;

     |FBORRA aOrigen;

     |XABRE "EC", aDestino, nHandle;
     |SI FSalida ] 0;
         |MENAV "0000El fichero se ha generado en la ubicaci¢n seleccionada.";
     |SINO;
         |MENAV "0000Ha habido un error al generar el fichero";
     |FINSI;
|FPRC;

|PROCESO Predetermina;
     |HAZ MiraDatos;
     |SI nDatos = 1;
         |CONFI "0000NYa existen datos introducidos. Reinstalamos?";
         |SI FSalida ! 0;  |ACABA;  |FINSI;
     |FINSI;

     |DBASE aBase;
     aDirDestino = aBase + "plugins/";
     aDir        = aBase + "plugins/*.ptl";

     IaPopup  = aPopup2  <-;

     nHay    = 0;
     |_PDIR aDir;
     |PARA; |SI FSalida ] 0; |HACIENDO;
          IaPopup = IaPopup + 1;
          aPopup  = aDir - aDirDestino;
          nHay    = nHay + 1;

          |_SDIR aDir;
     |SIGUE;

     |SI nHay = 0;
         |MENAV "0000No hay plantillas predeterminadas instaladas.";
         |ACABA;
     |FINSI;

     aPopup1      = nBotonPredet;
     aPopup2      = nHay;
     nPredetermin = 1;

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
         IaPopup  = aPopup3  <-;
         IaPopup  = IaPopup + (FSalida - 1);

         aOrigen = aDirDestino + aPopup;
         |HAZ InstalaFichero;
     |FINSI;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     FSalida = 2899;
|FPRC;

|PROCESO referen2;
     |DDEFECTO #dsarm002;
     #dsarm002#0 = #referen@#0;
     |LEE 101#dsarm002.=;
     |SI FSalida ! 0; |GRABA 020#dsarm002.b; |FINSI;
     #dsarm002#1 = #referen@#1;
     #dsarm002#2 = #referen@#2;
     #dsarm002#3 = #referen@#3;
     #dsarm002#4 = #referen@#4;
     |GRABA 020#dsarm002.a;
     |LIBERA #dsarm002;
|FPRC;

|DEFBUCLE referen2;
     #referen@#1001;
     ;
     nDGrupoPatron;
     nHGrupoPatron;
     ;
     NULL, referen2;
|FIN;

|PROCESO referen3;
     |DDEFECTO #dsarm003;
     #dsarm003#0 = #referen@#0;
     #dsarm003#1 = #referen@#1;
     |LEE 101#dsarm003.=;
     |SI FSalida ! 0; |GRABA 020#dsarm003.b; |FINSI;
     #dsarm003#2 = #referen@#2;
     #dsarm003#3 = #referen@#3;
     #dsarm003#4 = #referen@#4;
     #dsarm003#5 = #referen@#5;
     |GRABA 020#dsarm003.a;
     |LIBERA #dsarm003;
|FPRC;

|DEFBUCLE referen3;
     #referen@#1002;
     ;
     nDGrupoPatron, 001;
     nHGrupoPatron, 999;
     ;
     NULL, referen3;
|FIN;

|PROCESO referen4;
     |DDEFECTO #dsarm004;
     #dsarm004#0 = #referen@#0;
     #dsarm004#1 = #referen@#1;
     #dsarm004#2 = #referen@#2;
     |LEE 101#dsarm004.=;
     |SI FSalida ! 0; |GRABA 020#dsarm004.b; |FINSI;

     |PARA nCampo = 3; |SI nCampo [ 90; |HACIENDO nCampo = nCampo + 1;
          #dsarm004#nCampo# = #referen@#nCampo#;
     |SIGUE;
     |GRABA 020#dsarm004.a;
     |LIBERA #dsarm004;
|FPRC;

|DEFBUCLE referen4;
     #referen@#1003;
     ;
     nDGrupoPatron, 001, 001;
     nHGrupoPatron, 999, 999;
     ;
     NULL, referen4;
|FIN;

|PROCESO referen8;
     |DDEFECTO #dsarm008;
     #dsarm008#0 = #referen@#0;
     |LEE 101#dsarm008.=;
     |SI FSalida ! 0; |GRABA 020#dsarm008.b; |FINSI;
     |PARA nCampo = 1; |SI nCampo [ 33; |HACIENDO nCampo = nCampo + 1;
          #dsarm008#nCampo# = #referen@#nCampo#;
     |SIGUE;
     |GRABA 020#dsarm008.a;
     |LIBERA #dsarm008;
|FPRC;

|DEFBUCLE referen8;
     #referen@#1001;
     ;
     aVacio60;
     aZeta60;
     ;
     NULL, referen8;
|FIN;

|PROCESO referen59;
     #dsarm059#0 = #referen@#0;
     |LEE 101#dsarm059.=;
     |SI FSalida ! 0;
         |DDEFECTO #dsarm059;
         #dsarm059#0 = #referen@#0;
         |GRABA 020#dsarm059.b;
     |FINSI;

     |PARA nCampo = 1; |SI nCampo [ 11; |HACIENDO nCampo = nCampo + 1;
          #dsarm059#nCampo# = #referen@#nCampo#;
     |SIGUE;

     |GRABA 020#dsarm059.a;
     |LIBERA #dsarm059;
|FPRC;

|DEFBUCLE referen59;
     #referen@#1001;
     ;
     "                    ";
     "zzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, referen59;
|FIN;

|PROCESO ActuaPatron;
     |DBASE aBase;  |QBF aBase;
     |DFICE aDirDestino;
     aDirDestino = aDirDestino + "tmp/";
     |MKDIR aDirDestino;

     aDestino = aDirDestino + "maestros.tgz";

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |COPIA_FICHERO aOrigen, aDestino;

     |XABRE "E", aDestino, nHandle;
     |SI FSalida < 0;
         |MENAV "0000Hubo un error a copiar el fichero al servidor.";
         |ACABA;
     |FINSI;

     |DESCOMPRIME aDestino;
     |DETAR aDestino, aDirDestino;

     aAlfa = aDestino;                      |FBORRA aAlfa;
     aAlfa = aDirDestino + "maestros.tar";  |FBORRA aAlfa;

     |DBASE aDirBase;
     |QBF aDirBase;

     ||TODO CCC.
     ||Ahora hago un cargadef del patron ... y lo copio al sitio donde corresponda.

     ||dsarm002
     aDef = aDirBase + "def/dsarm002";
     |CARGA_DEF aDef, referen@;
     |SI FSalida ! 0;
          aMensaje = "0000Problema con CARGA_DEF" + aDef;
          |MENAV aMensaje;
          |ACABA;
     |FINSI;

     |PATH_DAT #referen@#aDirDestino#;

     |ABRE #dsarm002;
     |BUCLE referen2;
     |CIERRA #dsarm002;
     |DESCARGA_DEF #referen@;

     ||dsarm003
     aDef = aDirBase + "def/dsarm003";
     |CARGA_DEF aDef, referen@;
     |SI FSalida ! 0;
          aMensaje = "0000Problema con CARGA_DEF" + aDef;
          |MENAV aMensaje;
          |ACABA;
     |FINSI;

     |PATH_DAT #referen@#aDirDestino#;

     |ABRE #dsarm003;
     |BUCLE referen3;
     |CIERRA #dsarm003;
     |DESCARGA_DEF #referen@;

     ||dsarm004
     aDef = aDirBase + "def/dsarm004";
     |CARGA_DEF aDef, referen@;
     |SI FSalida ! 0;
          aMensaje = "0000Problema con CARGA_DEF" + aDef;
          |MENAV aMensaje;
          |ACABA;
     |FINSI;

     |PATH_DAT #referen@#aDirDestino#;

     |ABRE #dsarm004;
     |BUCLE referen4;
     |CIERRA #dsarm004;
     |DESCARGA_DEF #referen@;

     ||dsarm008
     aDef = aDirBase + "def/dsarm008";
     |CARGA_DEF aDef, referen@;
     |SI FSalida ! 0;
          aMensaje = "0000Problema con CARGA_DEF" + aDef;
          |MENAV aMensaje;
          |ACABA;
     |FINSI;

     |PATH_DAT #referen@#aDirDestino#;

     |ABRE #dsarm008;
     |BUCLE referen8;
     |CIERRA #dsarm008;
     |DESCARGA_DEF #referen@;

     ||dsarm059
     aDef = aDirBase + "def/dsarm059";
     |CARGA_DEF aDef, referen@;
     |SI FSalida ! 0;
          aMensaje = "0000Problema con CARGA_DEF" + aDef;
          |MENAV aMensaje;
          |ACABA;
     |FINSI;

     |PATH_DAT #referen@#aDirDestino#;

     |ABRE #dsarm059;
     |BUCLE referen59;
     |CIERRA #dsarm059;
     |DESCARGA_DEF #referen@;

     |MENAV "0000Grupo/Subgrupo/Tipo y Vistas actualizados a partir de Patrones.";
|FPRC;

|PROCESO ActualizaPatrones;
     |DBASE aBase;
     aDirDestino = aBase + "plugins/";
     aDir        = aBase + "plugins/*.ptl";

     IaPopup  = aPopup2  <-;

     nHay    = 0;
     |_PDIR aDir;
     |PARA; |SI FSalida ] 0; |HACIENDO;
          IaPopup = IaPopup + 1;
          aPopup  = aDir - aDirDestino;
          nHay    = nHay + 1;

          |_SDIR aDir;
     |SIGUE;

     |SI nHay = 0;
         |MENAV "0000No hay plantillas predeterminadas instaladas.";
         |ACABA;
     |FINSI;

     aPopup1      = "-1";
     aPopup2      = nHay;
     nPredetermin = 1;

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
         IaPopup  = aPopup3  <-;
         IaPopup  = IaPopup + (FSalida - 1);

         aOrigen = aDirDestino + aPopup;
         |HAZ ActuaPatron;
     |FINSI;
|FPRC;

|PROCESO CtblesNuevos;
     |ABRE #dsarm003;
     #dsarm003#0 = 003;
     #dsarm003#1 = 006;
     |LEE 000#dsarm003.=;
     |SI FSalida ! 0;
         nSwHayGrupos = 0;
     |FINSI;
     |CIERRA #dsarm003;
|FPRC;

|PROCESO dsarm002_grupos;
     ||TODO CCC. Desconectar
     nSwHayGrupos = 1;
     |ERROR10;
|FPRC;

|DEFBUCLE dsarm002_grupos;
     #dsarm002#1;
     ;
     nDGrupoPatron;
     nHGrupoPatron;
     ;
     NULL, dsarm002_grupos;
|FIN;

|PROGRAMA;
     nDGrupoPatron = 1;
     nHGrupoPatron = 5;

     aVacio60 = " " * 60;
     aZeta60  = "z" * 60;

     |CLS;
     |CABEZA "Importaciones / Exportaciones";

     |PINPA #0#dsarz999;  nPanta0 = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON,I M P O R T A C I O N", 0703, 1035, Importa;
     nBotonImporta = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON,E X P O R T A C I O N", 1503, 1835, Exporta;
     nBotonExporta = FSalida;

     ||Formato correcto, cuando en default.fnt esta esta opcion: :FONTBOTON 17
     ||aAlfa = "BOTON,                                I N S T A L A R                  ";
     ||aAlfa = aAlfa + "                                               P R E D E T E R M I N A D O S";

     aAlfa = "BOTON,                    I N S T A L A R                  ";
     aAlfa = aAlfa + "               P R E D E T E R M I N A D O S";
     |CONTROL_SIMPLE nPanta0, aAlfa, 2303, 2635, Predetermina;
     nBotonPredet  = FSalida;

     |ABRE #dsarm005;
     |LEE 000#dsarm005.p;
     |SI FSalida = 0;
          |ESTADO_CONTROL nBotonImporta, "DISABLE";

          nSwHayGrupos = 0;
          |BUCLE dsarm002_grupos;
          || ... |SI nSwHayGrupos = 1; |HAZ CtblesNuevos; |FINSI; No activamos
          |SI nSwHayGrupos = 0;
               |FIN_CONTROL_WINDOWS nBotonPredet;

               ||Formato correcto, cuando en default.fnt esta esta opcion: :FONTBOTON 17
               ||aAlfa = "BOTON,                                A C T U A L I Z A R    C O N     ";
               ||aAlfa = aAlfa + "                                         P A T R O N E S ";

               aAlfa = "BOTON,                A C T U A L I Z A R    C O N     ";
               aAlfa = aAlfa + "                     P A T R O N E S ";
               |CONTROL_SIMPLE nPanta0, aAlfa, 2303, 2635, ActualizaPatrones;
               nBotonActua = FSalida;
               |ESTADO_CONTROL nBotonActua, "ENABLE";

               aLabel = "Actualiza los grupos, subgrupos y tipos a partir de los patrones. Tambi‚n se actualizan las vistas por  rbol.";
               aLabel = "LABEL,{{007}}" + aLabel;
               |CONTROL_SIMPLE nPanta0, aLabel, 2337, 2496, NULL;

               aLabel = "No deben de existir los grupos del 001 al 005.";
               aLabel = "LABEL,{{007}}" + aLabel;
               |CONTROL_SIMPLE nPanta0, aLabel, 2537, 2596, NULL;

               aLabel = "Esto permite instalar los patrones sin borrar el resto de grupos ya existentes.";
               aLabel = "LABEL,{{007}}" + aLabel;
               |CONTROL_SIMPLE nPanta0, aLabel, 2637, 2696, NULL;
          |SINO;
               |ESTADO_CONTROL nBotonPredet, "DISABLE";
          |FINSI;
     |FINSI;
     |CIERRA #dsarm005;

     ||Tiquet 1081.475.
     ||Ahora comprobaremos si existe los grupos 1,2,3,4 o 5. Si no existen
     ||se instalaran los patrones.

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::Salir";
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;
|FPRO;
