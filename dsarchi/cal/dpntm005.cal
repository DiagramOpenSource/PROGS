|FICHEROS;
     dpntm001;
     dpntm005;
|FIN;

|VARIABLES;
     n0Vent         = 0;
     nBtnNue        = -1;
     nBtnQui        = -1;
     nBtnMod        = -1;
     nBtnRee        = -1;
     nRango         = -1;
     nIde           = -1;
     nAccesos       = -1;

     aEsc1          = "";
     aEsc2          = "";
     aRetu          = "";
     aTecla         = "";
     aAlfa          = "";
     aHora          = "";
     aFecha         = "";

     fFecha         = @;

 {-1}aVent          = "";
     aVent1         = 0;
     aVent2         = 0;
     aVent3         = 0;
     aVent4         = 0;
     aVent5         = "";

     &enEmpresa       = 0;
     &enGrid5         = 0;
     &eaModoNet       = "";
     &eaUsuNet        = "";
     &eaFechaNet      = "";
     &eaAccesoNet     = "";
     &eaEmpNet        = "";
     &eaTraNet        = "";
     &eaAccNet        = "";
     &eaMaiNet        = "";
     &eaDesNet        = "";
     &eaLabNet        = "";
     &eaAcceso        = "";
|FIN;

|PROCESO QuitarAcceso;
     nIde = #dpntm005#1;

     nAccesos = 0;
     #dpntm005#0 = #dpntm001#0;
     #dpntm005#1 = 1;
     |LEE 000#dpntm005.];
     |PARA;  |SI FSalida = 0;  |HACIENDO;
          |SI #dpntm005#0 ! #dpntm001#0;
              |SAL;
          |FINSI;

          nAccesos = nAccesos + 1;

          |LEE 000#dpntm005.s;
     |SIGUE;

     #dpntm005#0 = #dpntm001#0;
     #dpntm005#1 = nIde;
     |LEE 000#dpntm005.=;

     |SI nAccesos [ 1;
         |MENAV "0000Acceso principal, no se puede quitar el acceso, puede cambiar el e_mail";
         |ACABA;
     |FINSI;

     |CONFI "0000NSeguro que quiere quitar el acceso";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |LEE 101#dpntm005.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |BORRA 020#dpntm005.a;
     |LIBERA #dpntm005;

     |SI eaAcceso = "PIBOX";
         |REFRESCACONTROL enGrid5;
         |FOCOTECLADO enGrid5;

         |ACABA;
     |FINSI;

     |HORA aHora;

     eaModoNet     = "Deshabilitar acceso a empresa";
     eaUsuNet      = Usuario % 810; |QBF eaUsuNet;
     fFecha        = ~;
     aFecha        = fFecha;
     eaFechaNet    = aFecha + " " + aHora;
     eaAccesoNet   = #dpntm001#1; |QBF eaAccesoNet;
     eaEmpNet      = ("00000" + #dpntm001#0) % -105;
     eaEmpNet      = eaEmpNet + " / " + #dpntm001#2;   |QBF eaEmpNet;
     eaEmpNet      = eaEmpNet + " / " + #dpntm001#10;  |QBF eaEmpNet;
     eaAccNet      = ("000" + #dpntm005#1) % -103;
     eaAccNet      = eaAccNet + " / " + #dpntm005#2;   |QBF eaAccNet;
     eaTraNet      = "";
     eaDesNet      = #dpntm005#5;
     eaLabNet      = #dpntm005#6;

     |HAZ EnviaNotificacion;

     |REFRESCACONTROL enGrid5;
     |FOCOTECLADO enGrid5;
|FPRC;

|PROCESO ReenviaAcceso;
     aAlfa = "0000SQuiere volver a mandar un acceso a  " + #dpntm005#2;  |QBF aAlfa;
     |CONFI aAlfa;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |HAZ EnviaAcceso;
|FPRC;

|PROCESO Evnt5;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#dpntm005.=;
         |SI FSalida = 0;
             |ESTADO_CONTROL nBtnQui, "ENABLE";
             |ESTADO_CONTROL nBtnMod, "ENABLE";

             |SI nBtnRee ! -1;  |ESTADO_CONTROL nBtnRee, "ENABLE";  |FINSI;
         |FINSI;
     |FINSI;

     |SI FSalida = 4;
         |HAZ ModificarDatos;
     |FINSI;
|FPRC;

|PROCESO Baja5;
     #dpntm005#0 = #dpntm001#0;
     #dpntm005#1 = 001;
|FPRC;

|PROCESO Alta5;
     #dpntm005#0 = #dpntm001#0;
     #dpntm005#1 = 999;
|FPRC;

|PROGRAMA;
     |CLS;
     aAlfa = "Accesos a la empresa " + eaAcceso;
     |CABEZA aAlfa;

     aAlfa = "Accesos a la Empresa " + eaAcceso;
     |VENTANA 0501, 2899, -1, 16, aAlfa;
     n0Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n0Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |CONTROL_SIMPLE 0, "BOTON,Nuevo acceso", 2502, 2614, NuevoAcceso;
     nBtnNue = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,Modificar datos", 2516, 2628, ModificarDatos;
     nBtnMod = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,Quitar acceso", 2530, 2644, QuitarAcceso;
     nBtnQui = FSalida;

     |SI eaAcceso ! "PIBOX";
         |CONTROL_SIMPLE 0, "BOTON,Reenviar acceso", 2546, 2658, ReenviaAcceso;
         nBtnRee = FSalida;
     |FINSI;

     |ESTADO_CONTROL nBtnQui, "DISABLE";
     |ESTADO_CONTROL nBtnMod, "DISABLE";

     |SI nBtnRee ! -1;  |ESTADO_CONTROL nBtnRee, "DISABLE";  |FINSI;

     nRango = 4 + 8 + 16 + 32 + 128;

     |LINEAL_SIMPLE #1#dpntm005, nRango, 0502, 2498, Baja5, Alta5, Evnt5;
     enGrid5 = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}Salir," + enGrid5;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |DESTRUYECONTROL enGrid5;

     |FIN_CONTROL_WINDOWS nBtnNue;
     |FIN_CONTROL_WINDOWS nBtnQui;
     |FIN_CONTROL_WINDOWS nBtnMod;

     |SI nBtnRee ! -1;  |FIN_CONTROL_WINDOWS nBtnRee;  |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n0Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n0Vent;
|FPRO;

|PROCESO Tipo80;  |TIPO 80;
     |ABRET #dpntm001;
     |ABRET #dpntm005;

     #dpntm001#0 = enEmpresa;
     |LEE 000#dpntm001.=;
|FPRC;

|PROCESO Tipo90;  |TIPO 90;
     #dpntm005#0 = enEmpresa;
     #dpntm005#1 = 1;
     |LEE 000#dpntm005.];
     |SI FSalida = 0;  |Y #dpntm005#0 = enEmpresa;
         #dpntm001#0 = enEmpresa;
         |LEE 101#dpntm001.=;
         |SI FSalida = 0;
             #dpntm001#9 = #dpntm005#2;

             |GRABA 020#dpntm001.a;
             |LIBERA #dpntm001;
         |FINSI;
     |FINSI;

     |CIERRAT #dpntm001;
     |CIERRAT #dpntm005;
|FPRC;
