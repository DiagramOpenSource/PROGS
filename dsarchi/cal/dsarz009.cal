||Falta aclarar el tema de direccion origen del correo
||y el tema del cuerpo.

||Para Log.  Grabacion Carpetas.
||VerificaGrabacion

||EJEMPLO.  LLAMADA ENTRANTE
||LLAMADA IN CLAVE:1262590999.62 HORA:2010-01-04 08:43:27 SRC:968341321 DST:101 DURACION:251


|FICHEROS;
     dsarz009 #0;
     dsarw010 #10;            ||Control Correos
     dsarm010 #110;           ||Registro Correos

     dsarm005 #5;             ||Registro Documentos

     dsarm002 #2;
     dsarm003 #3;
     dsarm004 #4;

     dsarm001 #1;             ||Parametros

     dsarm017 #17;            ||Faxes Registro NIF

     Referen@ #100;

     flowm006 #506;

     dsarz026 #26;

     dsarm100 #100;        ||Contador Reg.Doc.
|FIN;

|VARIABLES;
     aVacio40 = "";
     aZeta40 = "";

     nUltimoDoc = 0;          ||Contador

     &enNumRegFlow = 0;
     &enSituFlow = 0;
     &eaDesSituFlow = "";

     aNomFicheroFlag = "";
     aFicheroFlag = "";
     aCarpetaFlag = "";
     nSwGrabacionOk = 0;
     nSwMaquinaCliente = 0;
     aUltimo = "";
     aTmpUsuario = "";
     nHandleFlag = 0;
     aTmpGraba = "";
     aNomCarpetaFlag = "";

     aFicLog = "";
     aMenLog = "";
     fLogDia = @;
     aLogHor = "";
     nHandLog = 0;
     nCorreosRecibidos = 0;


     aDirSamba = "";
     aSamba = "";
     nSitu = 0;
     aUsuario = "";
     nLinea = 0;

     nSalidaHis = 0;
     aTipoLlamada = "";
     aClave = "";
     aHoraLlamada = "";
     aSrc = "";
     aDst = "";
     aDuracion = "";
     nDuracion = 0;

     aFicheroCuerpo = "";
     aReporteCuerpo = "";
     nHandle = 0;
     aBusco = "";

     aAlfa1 = "";
     aAAAAMM = "";
     aFechaSis = "";
     fFechaSis = @;

     aAux2 = "";
     aNomFix = "";
     aCreaDir = "";
     aAlfaAux = "";
     nGrupo = 0;
     nSubGrupo = 0;
     nTipo = 0;

     nSaca = 0;
     aSaca = "";
     aLetra = "";
     aBeta = "";

     aReporte = "";
     aTSI = "";
     aNUMTSI = "";
     aFecha = "";
     aHoraFax = "";
     aLinea = "";
     aCanal = "";
     aDame = "";
     aAnyo = "";
     aMes = "";
     aDia = "";

     &enSwDesde9 = 0;
     nSwContinua = 0;
     aExtension = "";
     aNomDestino = "";
     aTel = "";
     aTel2 = "";
     aIPRemoto = "";
     &eaOrigen = "";
     &eaDestino = "";
     nUltDoc = 0;
     aPathBase = "";
     Handle1 = 0;
     nSwEsta = 0;
     nUltimo = 0;
     aUbicacion = "";

     aNif = "";
     aNomApe = "";
     aDomicilio = "";
     aNumero = "";

     aTelefonoFax = "";

     aPatron = "";
     nNumDoc = 0;
     aAux = "";
     aBase = "";
     aContador = "";

     aBorra = "";
     nUltima = 0;
     aFichero = "";
     aIni = "";
     nIni = 0;
     aCarpeta = "";
     aDirSaca = "";
     aOrigen = "";
     aDestino = "";
     aCuerpo = "";
     aHora = "";
     aLong = "";
     nLong = 0;
     nConta = 0;
     aCampo = "";
     nCampo = 0;
     aValor = "";


     aMensaje = "";
     aAsuntoCorreo = "";
     aIPServ = "";
     aServCorreo = "";
     aFrom = "";
     aPwd = "";
     aNombre = "";
     aDirEntrantes = "";
     nUnError = 0;
     aAlfa = "";
     nPos = 0;

     &eaEntrada = "";
     &eaSalida = "";

     aParametro = "";
     &enSwImpUnix = 0;
     &enSoyDESPANET = 0;
|FIN;

|PROCESO RecogeCorreo;
     nUnError = 0;
     |PARA; |SI; |HACIENDO;
          |DSCORREO_RECIBE aIPServ, aServCorreo, aFrom, aPwd, aDirEntrantes, aFichero;
          aAsuntoCorreo = FSalida;
          aAlfa         = FSalida;
          aAlfa         = aAlfa % 101;
          |SI aAlfa ! "0";
              |SI nUnError = 0;
                  |SI aAlfa = "-";
                       aMenLog = "Error recibiendo correo: " + aAsuntoCorreo;
                       |HAZ MeteLog;
                       aMenLog = " IP DSCorreo: " + aIPServ;
                       |HAZ MeteLog;
                       aMenLog = " Servidor Correo: " + aServCorreo;
                       |HAZ MeteLog;
                       aMenLog = " Usuario: " + aFrom;
                       |HAZ MeteLog;
                       aMenLog = " Password: [*******]";
                       |HAZ MeteLog;
                       aMenLog = " Directorio entrante: " + aDirEntrantes;
                       |HAZ MeteLog;
                       aMenLog = " Fichero Adjunto: " + aFichero;
                       |HAZ MeteLog;
                  |FINSI;
                  nUnError = 1;
              |SINO;
                  |SAL;
              |FINSI;
          |SINO;
              nUnError = 0;
          |FINSI;

          aFichero = aFichero - ";;";

          |SI aAlfa = "0";
              nCorreosRecibidos = nCorreosRecibidos + 1;

              aAsuntoCorreo = aAsuntoCorreo % 02;  |QBF aAsuntoCorreo;
              aAsuntoCorreo = aAsuntoCorreo - "]";
              |SI FSalida ! 0;
                  aAlfa = FSalida;
                  nPos  = FSalida;
                  |SI nPos > 1000;
                      aAlfa = aAlfa % 102;
                  |SINO;
                      aAlfa = aAlfa % 101;
                  |FINSI;
                  nPos = aAlfa;
                  nPos = nPos + 1;
                  aAsuntoCorreo = aAsuntoCorreo % nPos;
                  |HAZ GrabaCorreos;
              |FINSI;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO GrabaCorreos;
     aDirSaca = aFichero;
     |HAZ SacaNombre;

     aNomFix = "";
     aAux2 = aNombre % 102;
     |SI aAux2 = "=_";
          aAux2 = aNombre % -102;
          |SI aAux2 = "_=";
               ||Nos llega de esta forma =_ISO-8859-1_Q_Formato_Documentaci=F3n.doc_=
               aNomFix = aNombre - "_=";
          |FINSI;
     |FINSI;

     |ABRE #dsarm010;
     |LEE 000#dsarm010.u;
     |SI FSalida = 0;
          nUltima = #dsarm010#0 + 1;
     |SINO;
          nUltima = 1;
     |FINSI;

     |HORA aHora;
     |DDEFECTO #dsarm010;
     #dsarm010#0 = nUltima;
     |LEE 101#dsarm010.=;
     |SI FSalida ! 0;
          #dsarm010#2 = aFrom;
          #dsarm010#3 = aAsuntoCorreo;
          #dsarm010#4 = ~;
          #dsarm010#5 = aHora;
          #dsarm010#6 = aNombre;

          aContador = ("000000" + #dsarm010#0) % -106;

          aCuerpo = aDirEntrantes + "_cuerpo_.txt";
          aDestino = aDirEntrantes + "cuerpo" + aContador + ".txt";
          |COPIA_FICHERO aCuerpo, aDestino;

          aCuerpo = "cuerpo" + aContador + ".txt";
          #dsarm010#9 = aCuerpo;

          nNumDoc = 0;
          aAux = aAsuntoCorreo - "Doc.Num.:";
          |SI aAux ! aAsuntoCorreo;
               |HAZ DameNumDoc;
               |SI nNumDoc ! 0;
                    #dsarm010#7 = nNumDoc;
                    #dsarm010#8 = "S";
               |FINSI;
          |SINO;
               aPatron = #dsarm001#73;
               |QBF aPatron;
               aAux = aAsuntoCorreo - aPatron;
               |SI aAux ! aAsuntoCorreo;
                    |HAZ RecepcionFax;
               |SINO;
                    aPatron = #dsarm001#142;
                    |QBF aPatron;
                    aAux = aAsuntoCorreo - aPatron;
                    |SI aAux ! aAsuntoCorreo;
                         aTipoLlamada = "OUT";
                         |HAZ RecepcionLlamada;
                    |SINO;
                         aPatron = #dsarm001#149;
                         |QBF aPatron;
                         aAux = aAsuntoCorreo - aPatron;
                         |SI aAux ! aAsuntoCorreo;
                              aTipoLlamada = "IN";
                              |HAZ RecepcionLlamada;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;

          |GRABA 020#dsarm010.n;
          |LIBERA #dsarm010;

          aBorra = aDirEntrantes + "_cuerpo_.txt";
          |FBORRA aBorra;

          |SI nNumDoc ! 0;
               |ABRE #dsarm005;
               |DDEFECTO #dsarm005;
               #dsarm005#0 = nNumDoc;
               |LEE 101#dsarm005.=;
               |SI FSalida = 0;
                    #dsarm005#83 = "S";
                    |GRABA 020#dsarm005.a;
                    |LIBERA #dsarm005;
               |FINSI;
               |CIERRA #dsarm005;
          |FINSI;
     |FINSI;
     |CIERRA #dsarm010;
|FPRC;

|PROCESO Referen;
     aTel = #Referen@#8;
     |QBT aTel;
     aTel2 = #Referen@#17;
     |QBT aTel2;
     |SI aTel = aTelefonoFax; |O aTel2 = aTelefonoFax;
          nSwEsta = 1;
          aNif = #Referen@#0;
          aNomApe = #Referen@#1;
          aDomicilio = #Referen@#2;
          aNumero = #Referen@#3;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE Referen;
 #Referen@#1001;
 ;
 "            ";
 "zzzzzzzzzzzz";
 ;
 NULL, Referen;
|FIN;

|PROCESO ReferenListin;
     aTel = #Referen@#4;
     |QBT aTel;
     |SI aTel = aTelefonoFax;
          nSwEsta = 1;
          aNomApe = #Referen@#0;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE ReferenListin;
 #Referen@#1002;
 ;
 aVacio40, 01;
 aZeta40,  99;
 ;
 NULL, ReferenListin;
|FIN;

|PROCESO BuscaEnListin;
     ||Buscamos en dsam0107.
     |SI nSwEsta = 0;
          aAlfa = aPathBase + "integral/fich/dsam0107.dat";
          |XABRE "E", aAlfa, Handle1;
          |SI FSalida ] 0;
             aAlfa = aPathBase + "integral/def/dsam0107.mas";
             |CARGA_DEF aAlfa, Referen@;
             |SI FSalida = 0;
                aAlfa = aPathBase + "integral/fich/";
                |PATH_DAT #100 aAlfa;
                |BUCLE ReferenListin;
                |DESCARGA_DEF #Referen@;
             |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO BuscaReporteCuerpo;
     aFicheroCuerpo = aDirEntrantes + aCuerpo;

     |XABRE "U", aFicheroCuerpo, nHandle;
     |SI FSalida ] 0;
          |PARA; |SI; |HACIENDO;
               |XLEE nHandle, 250, aAux;
               |SI FSalida [ 0;
                    |SAL;
               |FINSI;
               aBusco = aAux % 108;
               |SI aBusco = "REPORTE:";
                    aReporteCuerpo = aAux - "REPORTE:";
                    |QBF aReporteCuerpo;
                    |SAL;
               |FINSI;
          |SIGUE;
          |XCIERRA nHandle;
     |FINSI;
|FPRC;


|PROCESO RecepcionFax;
     ||Primero recupero los datos del FAX.
     aTSI = "";
     aNUMTSI = "";
     aFecha = "";
     aHoraFax = "";
     aLinea = "";
     aCanal = "";
     aDame = "";
     aReporte = "";

     aLong = aAux % A0;
     nLong = aLong;
     |PARA nConta = 1; |SI nConta [ nLong; |HACIENDO nConta = nConta + 1;
          nSaca = (100 * nConta) + 1;
          aSaca = nSaca;
          aLetra = aAux % aSaca;
          aDame = aDame + aLetra;
          |SI aLetra = ":";
               aBeta = aDame % -107;
               |SI aBeta = "NUMTSI:";
                    aBeta = aDame - "TSI:";
                    aBeta = aBeta - "NUMTSI:";
                    |QBT aBeta;
                    aTSI = aBeta;
                    aDame = "";
               |SINO;
                    aBeta = aDame % -106;
                    |SI aBeta = "FECHA:";
                         aBeta = aDame - "FECHA:";
                         aBeta = aBeta - "NUMTSI:";
                         |QBT aBeta;
                         aNUMTSI = aBeta;
                         aDame = "";
                    |SINO;
                         |SI aBeta = "LINEA:";
                              aBeta = aDame - "FECHA:";
                              aBeta = aBeta - "LINEA:";
                              |QBT aBeta;
                              aFecha = aBeta % 110;
                              aAnyo = aFecha % 104;
                              aMes = aFecha % 602;
                              aDia = aFecha % 902;
                              aFecha = aDia + "." + aMes + "." + aAnyo;
                              aHoraFax = aBeta % -108;
                              aDame = "";
                         |SINO;
                              |SI aBeta = "CANAL:";
                                   aBeta = aDame - "CANAL:";
                                   aBeta = aBeta - "LINEA:";
                                   |QBT aBeta;
                                   aLinea = aBeta;
                                   aDame = "";
                              |SINO;
                                   ||Falta Reporte:
                                   aBeta = aDame % -108;
                                   |SI aBeta = "REPORTE:";
                                        aBeta = aDame - "REPORTE:";
                                        aBeta = aBeta - "CANAL:";
                                        |QBT aBeta;
                                        aCanal = aBeta;
                                        aDame = "";
                                   |FINSI;
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
     |SIGUE;
     |QBT aDame;
     |SI aDame ! "";
          aReporte = aDame;
     |FINSI;

     aReporteCuerpo = "";
     |HAZ BuscaReporteCuerpo;
     |SI aReporteCuerpo ! "";
          aReporte = aReporteCuerpo;
     |FINSI;

     |SI aLinea = "";
          nGrupo = #dsarm001#89;
          nSubGrupo = #dsarm001#90;
          nTipo = #dsarm001#91;

          |HAZ MeteDocumentoCefax;
     |SINO;
          aTelefonoFax = aLinea;

          aNif = "000000000";
          aNomApe = "*DESCONOCIDO*";
          aDomicilio = "";
          aNumero = "";

          |ABRE #dsarm017;
          |SELECT #2#dsarm017;
          #dsarm017#2 = aTelefonoFax;
          |LEE 000#dsarm017.=;
          |SI FSalida = 0;
               aNif = #dsarm017#0;
          |FINSI;
          |CIERRA #dsarm017;

          |SI aNif = "000000000";       ||Lo buscamos en Integral/Basica

               nSwEsta = 0;
               ||TODO CCC. Buscamos el telefono
               |DBASS aPathBase;
               |QBF aPathBase;
               aAlfa = aPathBase + "integral/fich/dsam0103.dat";
               |XABRE "E", aAlfa, Handle1;
               |SI FSalida ] 0;
                  aAlfa = aPathBase + "integral/def/dsam0103.mas";
                  |CARGA_DEF aAlfa, Referen@;
                  |SI FSalida = 0;
                     aAlfa = aPathBase + "integral/fich/";
                     |PATH_DAT #100 aAlfa;
                     |BUCLE Referen;
                     |DESCARGA_DEF #Referen@;
                  |FINSI;
               |FINSI;

               |SI nSwEsta = 0;
                    aAlfa = aPathBase + "basica/fich/agim0003.dat";
                    |XABRE "E", aAlfa, Handle1;
                    |SI FSalida ] 0;
                       aAlfa = aPathBase + "basica/def/agim0003.mas";
                       |CARGA_DEF aAlfa, Referen@;
                       |SI FSalida = 0;
                          aAlfa = aPathBase + "basica/fich/";
                          |PATH_DAT #100 aAlfa;
                          |BUCLE Referen;
                          |DESCARGA_DEF #Referen@;
                       |FINSI;
                    |FINSI;
               |FINSI;

               ||Buscamos en dsam0107.
               |HAZ BuscaEnListin;
          |SINO;
               ||Buscamos el Nombre del CIF
               nSwEsta = 0;
               |DBASS aPathBase;
               |QBF aPathBase;
               aAlfa = aPathBase + "integral/fich/dsam0103.dat";
               |XABRE "E", aAlfa, Handle1;
               |SI FSalida ] 0;
                  aAlfa = aPathBase + "integral/def/dsam0103.mas";
                  |CARGA_DEF aAlfa, Referen@;
                  |SI FSalida = 0;
                     aAlfa = aPathBase + "integral/fich/";
                     |PATH_DAT #100 aAlfa;
                     |ABRE #100;
                     #100#0 = aNif;
                     |LEE 000#100.=;
                     |SI FSalida = 0;
                         nSwEsta = 1;
                         aNomApe = #100#1;
                         aDomicilio = #100#2;
                         aNumero = #100#3;
                     |FINSI;
                     |CIERRA #100;
                     |DESCARGA_DEF #Referen@;
                  |FINSI;
               |FINSI;
               |SI nSwEsta = 0;
                    aAlfa = aPathBase + "basica/fich/agim0003.dat";
                    |XABRE "E", aAlfa, Handle1;
                    |SI FSalida ] 0;
                       aAlfa = aPathBase + "basica/def/agim0003.mas";
                       |CARGA_DEF aAlfa, Referen@;
                       |SI FSalida = 0;
                          aAlfa = aPathBase + "basica/fich/";
                          |PATH_DAT #100 aAlfa;
                          |ABRE #100;
                          #100#0 = aNif;
                          |LEE 000#100.=;
                          |SI FSalida = 0;
                              aNomApe = #100#1;
                              aDomicilio = #100#2;
                              aNumero = #100#3;
                          |FINSI;
                          |CIERRA #100;
                          |DESCARGA_DEF #Referen@;
                       |FINSI;
                    |FINSI;
               |FINSI;
               ||Buscamos en dsam0107.
               |HAZ BuscaEnListin;
          |FINSI;

          |SI aNif = "000000000";
               |SI nSwEsta = 0;
                    nGrupo = #dsarm001#89;
                    nSubGrupo = #dsarm001#90;
                    nTipo = #dsarm001#91;
               |SINO;
                    nGrupo = #dsarm001#67;
                    nSubGrupo = #dsarm001#68;
                    nTipo = #dsarm001#69;
               |FINSI;

               |HAZ MeteDocumentoCefax;
          |SINO;
               nGrupo = #dsarm001#67;
               nSubGrupo = #dsarm001#68;
               nTipo = #dsarm001#69;

               |HAZ MeteDocumentoCefax;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO MeteDocumentoCefax;
     aSamba = "";
     |ABRE #dsarz026;
     |DDEFECTO #dsarz026;
     |LEE 000#dsarz026.p;
     |CIERRA #dsarz026;
     |SI enSwImpUnix = 1; |Y #1#7 = "S";
          aSamba = #dsarz026#34;
          |QBF aSamba;
          aDirSamba = aSamba;
          |MKDIR aDirSamba;

          ||LOG
          aCarpetaFlag = aDirSamba;
          nSwMaquinaCliente = 0;
          |HAZ VerificaGrabacion;
     |FINSI;

     |ABRE #2;
     #2#0 = nGrupo;
     |LEE 000#2=;
     |SI FSalida ! 0;  |DDEFECTO #2;  |FINSI;
     |CIERRA #2;

     |ABRE #3;
     #3#0 = nGrupo;
     #3#1 = nSubGrupo;
     |LEE 000#3=;
     |SI FSalida ! 0;  |DDEFECTO #3;  |FINSI;
     |CIERRA #3;

     |ABRE #4;
     #4#0 = nGrupo;
     |SI #1#8 = "S";
          #4#1 = nSubGrupo;
          #4#2 = nTipo;
     |SINO;
          #4#1 = 1;
          #4#2 = 1;
     |FINSI;

     |LEE 000#4=;
     |SI FSalida ! 0;  |DDEFECTO #4;  |FINSI;
     |CIERRA #4;

     aUbicacion = #1#1;
     |QBF aUbicacion;
     aUbicacion = aUbicacion + "/" + #2#2;
     |QBF aUbicacion;
     aCreaDir = aUbicacion;
     |SI #1#7 = "N";
          |MKDIR aCreaDir;
          ||LOG
          aCarpetaFlag = aCreaDir;
          nSwMaquinaCliente = 0;
          |HAZ VerificaGrabacion;
     |SINO;
         |RMKDIR aCreaDir;
         ||LOG
         aCarpetaFlag = aCreaDir;
         nSwMaquinaCliente = 1;
         |HAZ VerificaGrabacion;
         |SI aSamba ! "";
              aDirSamba = aDirSamba + "/" + #2#2;
              |QBF aDirSamba;
              |MKDIR aDirSamba;
              ||LOG
              aCarpetaFlag = aDirSamba;
              nSwMaquinaCliente = 0;
              |HAZ VerificaGrabacion;
         |FINSI;
     |FINSI;
     |SI #1#8 = "S";
          aUbicacion = aUbicacion + "/" + #3#3;
          |QBF aUbicacion;
          aCreaDir = aUbicacion;
          |SI #1#7 = "N";
              |MKDIR aCreaDir;
              ||LOG
              aCarpetaFlag = aCreaDir;
              nSwMaquinaCliente = 0;
              |HAZ VerificaGrabacion;
          |SINO;
              |RMKDIR aCreaDir;
              ||LOG
              aCarpetaFlag = aCreaDir;
              nSwMaquinaCliente = 1;
              |HAZ VerificaGrabacion;
              |SI aSamba ! "";
                   aDirSamba = aDirSamba + "/" + #3#3;
                   |QBF aDirSamba;
                   |MKDIR aDirSamba;
                   ||LOG
                   aCarpetaFlag = aDirSamba;
                   nSwMaquinaCliente = 0;
                   |HAZ VerificaGrabacion;
              |FINSI;
          |FINSI;
          aUbicacion = aUbicacion + "/" + #4#4;
          |QBF aUbicacion;
          aCreaDir = aUbicacion;
          |SI #1#7 = "N";
              |MKDIR aCreaDir;
              ||LOG
              aCarpetaFlag = aCreaDir;
              nSwMaquinaCliente = 0;
              |HAZ VerificaGrabacion;
          |SINO;
              |RMKDIR aCreaDir;
              ||LOG
              aCarpetaFlag = aCreaDir;
              nSwMaquinaCliente = 1;
              |HAZ VerificaGrabacion;
              |SI aSamba ! "";
                   aDirSamba = aDirSamba + "/" + #4#4;
                   |QBF aDirSamba;
                   |MKDIR aDirSamba;
                   ||LOG
                   aCarpetaFlag = aDirSamba;
                   nSwMaquinaCliente = 0;
                   |HAZ VerificaGrabacion;
              |FINSI;
          |FINSI;
     |FINSI;

     |SI #4#75 = "S";
          fFechaSis = ~;
          aFechaSis = fFechaSis;
          aAAAAMM = (aFechaSis % -104) + (aFechaSis % 402);
          aUbicacion = aUbicacion + "/" + aAAAAMM;
          |SI #1#7 = "N";
               |MKDIR aUbicacion;
               ||LOG
               aCarpetaFlag = aUbicacion;
               nSwMaquinaCliente = 0;
               |HAZ VerificaGrabacion;
          |SINO;
               |RMKDIR aUbicacion;
               ||LOG
               aCarpetaFlag = aUbicacion;
               nSwMaquinaCliente = 1;
               |HAZ VerificaGrabacion;
               |SI aSamba ! "";
                    aDirSamba = aDirSamba + "/" + aAAAAMM;
                    |QBF aDirSamba;
                    |MKDIR aDirSamba;
                    ||LOG
                    aCarpetaFlag = aDirSamba;
                    nSwMaquinaCliente = 0;
                    |HAZ VerificaGrabacion;
               |FINSI;
          |FINSI;
     |FINSI;

     |ABRE #5;
     |SELECT #2#5;
     #5#1 = nGrupo;
     #5#2 = nSubGrupo;
     #5#3 = nTipo;
     #5#0 = 999999999;
     |LEE 000#5];
     |SI FSalida = 0;
         |LEE 000#5a;
     |SINO;
         |LEE 000#5u;
     |FINSI;

     nUltDoc = 0;
     |SI FSalida = 0;  |Y #5#1 = nGrupo;  |Y #5#2 = nSubGrupo; |Y #5#3 = nTipo;
         nUltDoc = #5#47;
     |FINSI;

     |SELECT #1#5;
     |DDEFECTO #dsarm005;
     |LEE 000#dsarm005.u;
     |SI FSalida = 0;
          nUltimo = #dsarm005#0 + 1;
     |SINO;
          nUltimo = 1;
     |FINSI;

     |ABRE #dsarm100;
     |DDEFECTO #dsarm100;
     |LEE 000#dsarm100.p;
     |SI FSalida = 0;
          nUltimoDoc = #dsarm100#1 + 1;
     |SINO;
          nUltimoDoc = 1;
     |FINSI;
     |SI nUltimoDoc > nUltimo;
          nUltimo = nUltimoDoc;
     |FINSI;
     #dsarm100#0 = "1";
     |LEE 101#dsarm100.=;
     |SI FSalida ! 0; |GRABA 020#dsarm100.b; |FINSI;
     #dsarm100#1 = nUltimo;
     |GRABA 020#dsarm100.a;
     |LIBERA #dsarm100;
     |CIERRA #dsarm100;

     |DDEFECTO #dsarm005;
     #dsarm005#0 = nUltimo;
     |LEE 101#dsarm005.=;
     |SI FSalida = 0;
          |LIBERA #dsarm005;
          nUltimo = nUltimo + 1;
          |DDEFECTO #dsarm005;
          #dsarm005#0 = nUltimo;
          |LEE 101#dsarm005.=;
          |SI FSalida = 0;
               |LIBERA #dsarm005;
               |ACABA;
          |SINO;
               |GRABA 020#dsarm005.b;
          |FINSI;
     |SINO;
          |GRABA 020#dsarm005.b;
     |FINSI;

     |SI aNomFix = "";
          aExtension = (aNombre % -104) > "";
     |SINO;
          aExtension = (aNomFix % -104) > "";
     |FINSI;
     aAlfa1 = aExtension % 101;
     |SI aAlfa1 ! ".";
          |SI aNomFix = "";
               aExtension = (aNombre % -105) > "";
          |SINO;
               aExtension = (aNomFix % -105) > "";
          |FINSI;
     |FINSI;

     aNomDestino = (("000000000" + #dsarm005#0) % -109) + aExtension;
     aUbicacion = aUbicacion + "/";

     eaOrigen = aDirEntrantes + "/" + aNombre;

     ||Distinguir si estoy en unix. . y tengo el campo smb.
     |SI enSwImpUnix = 1; |Y #1#7 = "S";
          |SI aSamba = "";
               eaDestino = aUbicacion + aNomDestino;
          |SINO;
               eaDestino = aSamba + aDirSamba + "/" + aNomDestino;
          |FINSI;
     |SINO;
          eaDestino = aUbicacion + aNomDestino;
     |FINSI;

     |SI #1#7 = "N";
          |COPIA_FICHERO eaOrigen, eaDestino;
     |SINO;
          aIPRemoto = "";
          |IP_REMOTO aIPRemoto;
          |QBF aIPRemoto;
          |SI aIPRemoto ! "";
               |ENVIA_FICHERO eaOrigen, eaDestino;
          |SINO;
               |COPIA_FICHERO eaOrigen, eaDestino;
          |FINSI;
     |FINSI;
     nSalidaHis = FSalida;
     aMenLog = "Resultado <COPIAR DOCUMENTO>: " + nSalidaHis;
     |HAZ MeteLog;

     aMenLog = " Origen: " + eaOrigen;
     |HAZ MeteLog;

     aMenLog = " Destino: " + eaDestino;
     |HAZ MeteLog;


     ||Historico
     |HORA aHora;
     aUsuario = Usuario % 810;
     |ABRE #flowm006;
     #flowm006#0  = #dsarm005#0;
     #flowm006#1  = 99999;
     |LEE 000#flowm006.];
     |SI FSalida = 0;
          |LEE 000#flowm006.a;
     |SINO;
          |LEE 000#flowm006.u;
     |FINSI;
     |SI FSalida = 0; |Y #flowm006#0  = #dsarm005#0;
          nLinea = #flowm006#1 + 1;
          nSitu = #flowm006#2;
     |SINO;
          nSitu = 0;
          nLinea = 1;
     |FINSI;
     |DDEFECTO #flowm006;
     #flowm006#0  = #dsarm005#0;
     #flowm006#1  = nLinea;
     |LEE 101#flowm006.=;
     |SI FSalida ! 0; |GRABA 020#flowm006.b; |FINSI;
     #flowm006#2  = nSitu;
     #flowm006#3  = ~;
     #flowm006#4  = aHora;
     #flowm006#5  = aUsuario;
     #flowm006#6  = "RESULTADO ALTA [" + nSalidaHis + "]";

     enNumRegFlow = #flowm006#0;
     enSituFlow = #flowm006#2;
     |HAZ DimeDesFlow;
     #flowm006#7 = eaDesSituFlow;

     |GRABA 020#flowm006.a;
     |LIBERA #flowm006;
     |CIERRA #flowm006;

     #dsarm005#1 = nGrupo;
     #dsarm005#2 = nSubGrupo;
     #dsarm005#3 = nTipo;
     #dsarm005#4 = ~;
     |HORA aHora;
     aHora = aHora % 105;
     #dsarm005#5 = aHora;
     #dsarm005#6 = "CEFAX";
     #dsarm005#7 = aUbicacion;
     #dsarm005#9 = aNomDestino;
     #dsarm005#93 = aNomDestino;
     #dsarm005#10 = aNif;
     #dsarm005#14 = aNomApe;
     #dsarm005#38 = aNomApe;
     #dsarm005#47 = nUltDoc + 1;
     #dsarm005#55 = aTelefonoFax;
     |SI #dsarm005#44 = 0;  #dsarm005#44 = 1;  |FINSI;
     |SI #dsarm005#45 = 0;  #dsarm005#45 = 1;  |FINSI;
     |SI #dsarm005#46 = 0;  #dsarm005#46 = 1;  |FINSI;

     ||ARA
     aAlfaAux = #dsarm004#5;
     |QBF aAlfaAux;
     aAlfaAux = aAlfaAux + ": " + aTSI;
     |QBF aAlfaAux;
     #dsarm005#23 = aAlfaAux;

     aAlfaAux = #dsarm004#6;
     |QBF aAlfaAux;
     aAlfaAux = aAlfaAux + ": " + aNUMTSI;
     |QBF aAlfaAux;
     #dsarm005#24 = aAlfaAux;

     aAlfaAux = #dsarm004#7;
     |QBF aAlfaAux;
     aAlfaAux = aAlfaAux + ": " + aHoraFax;
     |QBF aAlfaAux;
     #dsarm005#25 = aAlfaAux;

     aAlfaAux = #dsarm004#8;
     |QBF aAlfaAux;
     aAlfaAux = aAlfaAux + ": " + aLinea;
     |QBF aAlfaAux;
     #dsarm005#26 = aAlfaAux;

     aAlfaAux = #dsarm004#9;
     |QBF aAlfaAux;
     aAlfaAux = aAlfaAux + ": " + aCanal;
     |QBF aAlfaAux;
     #dsarm005#27 = aAlfaAux;

     aAlfaAux = #dsarm004#27;
     |QBF aAlfaAux;
     aAlfaAux = aAlfaAux + ": " + aReporte;
     |QBF aAlfaAux;
     #dsarm005#28 = aAlfaAux;

     aAlfaAux = #dsarm004#15;
     |QBF aAlfaAux;
     aAlfaAux = aAlfaAux + ": " + aFecha;
     |QBF aAlfaAux;
     #dsarm005#29 = aAlfaAux;

     |GRABA 020#dsarm005.a;
     |LIBERA #dsarm005;
     |CIERRA #dsarm005;

     aMenLog = "*Subejecuta [dsarz013;" + #dsarm005#0;
     |QBF aMenLog;
     aMenLog = aMenLog + "]. Inicio";
     |HAZ MeteLog;

     enSwDesde9 = 1;
     aAlfa = "dsarz013;" + #dsarm005#0;
     |SUB_EJECUTA aAlfa;

     aMenLog = "*Subejecuta [dsarz013;" + #dsarm005#0;
     |QBF aMenLog;
     aMenLog = aMenLog + "]. Final";
     |HAZ MeteLog;

     aMenLog = "*Subejecuta [dsarz030;" + #dsarm005#0;
     |QBF aMenLog;
     aMenLog = aMenLog + "]. Inicio";
     |HAZ MeteLog;

     aAlfa = "dsarz030;" + #dsarm005#0;
     |SUB_EJECUTA aAlfa;
     enSwDesde9 = 0;

     aMenLog = "*Subejecuta [dsarz030;" + #dsarm005#0;
     |QBF aMenLog;
     aMenLog = aMenLog + "]. Final";
     |HAZ MeteLog;
|FPRC;


/*
************************************************************************
************ RECEPCION CORREOS LLAMADAS ASTERISK  **********************
************************************************************************
*/

|PROCESO DameDatosPI;
     ||TODO CCC
     ||ARA66
     ||COMO ES EL CONTROL DE LOS DESCONOCIDOS ????
     ||FALTA ESTUDIAR.

     aTelefonoFax = aLinea;

     aNif = "000000000";
     aNomApe = "*DESCONOCIDO*";

     |ABRE #dsarm017;
     |SELECT #2#dsarm017;
     #dsarm017#2 = aTelefonoFax;
     |LEE 000#dsarm017.=;
     |SI FSalida = 0;
          aNif = #dsarm017#0;
     |FINSI;
     |CIERRA #dsarm017;

     |SI aNif = "000000000";       ||Lo buscamos en Integral/Basica

          nSwEsta = 0;
          ||TODO CCC. Buscamos el telefono
          |DBASS aPathBase;
          |QBF aPathBase;
          aAlfa = aPathBase + "integral/fich/dsam0103.dat";
          |XABRE "E", aAlfa, Handle1;
          |SI FSalida ] 0;
             aAlfa = aPathBase + "integral/def/dsam0103.mas";
             |CARGA_DEF aAlfa, Referen@;
             |SI FSalida = 0;
                aAlfa = aPathBase + "integral/fich/";
                |PATH_DAT #100 aAlfa;
                |BUCLE Referen;
                |DESCARGA_DEF #Referen@;
             |FINSI;
          |FINSI;

          |SI nSwEsta = 0;
               aAlfa = aPathBase + "basica/fich/agim0003.dat";
               |XABRE "E", aAlfa, Handle1;
               |SI FSalida ] 0;
                  aAlfa = aPathBase + "basica/def/agim0003.mas";
                  |CARGA_DEF aAlfa, Referen@;
                  |SI FSalida = 0;
                     aAlfa = aPathBase + "basica/fich/";
                     |PATH_DAT #100 aAlfa;
                     |BUCLE Referen;
                     |DESCARGA_DEF #Referen@;
                  |FINSI;
               |FINSI;
          |FINSI;
          ||Buscamos en dsam0107.
          |HAZ BuscaEnListin;
     |SINO;
          ||Buscamos el Nombre del CIF
          nSwEsta = 0;
          |DBASS aPathBase;
          |QBF aPathBase;
          aAlfa = aPathBase + "integral/fich/dsam0103.dat";
          |XABRE "E", aAlfa, Handle1;
          |SI FSalida ] 0;
             aAlfa = aPathBase + "integral/def/dsam0103.mas";
             |CARGA_DEF aAlfa, Referen@;
             |SI FSalida = 0;
                aAlfa = aPathBase + "integral/fich/";
                |PATH_DAT #100 aAlfa;
                |ABRE #100;
                #100#0 = aNif;
                |LEE 000#100.=;
                |SI FSalida = 0;
                    nSwEsta = 1;
                    aNomApe = #100#1;
                |FINSI;
                |CIERRA #100;
                |DESCARGA_DEF #Referen@;
             |FINSI;
          |FINSI;
          |SI nSwEsta = 0;
               aAlfa = aPathBase + "basica/fich/agim0003.dat";
               |XABRE "E", aAlfa, Handle1;
               |SI FSalida ] 0;
                  aAlfa = aPathBase + "basica/def/agim0003.mas";
                  |CARGA_DEF aAlfa, Referen@;
                  |SI FSalida = 0;
                     aAlfa = aPathBase + "basica/fich/";
                     |PATH_DAT #100 aAlfa;
                     |ABRE #100;
                     #100#0 = aNif;
                     |LEE 000#100.=;
                     |SI FSalida = 0;
                         aNomApe = #100#1;
                     |FINSI;
                     |CIERRA #100;
                     |DESCARGA_DEF #Referen@;
                  |FINSI;
               |FINSI;
          |FINSI;
          ||Buscamos en dsam0107.
          |HAZ BuscaEnListin;
     |FINSI;
|FPRC;


|PROCESO RecepcionLlamada;
     ||Primero recupero los datos de la llamada.
     ||ARA66
     aClave = "";
     aFecha = "";
     aHoraLlamada = "";
     aSrc = "";
     aDst = "";
     aDuracion = "";
     aDame = "";

     aLong = aAux % A0;
     nLong = aLong;
     |PARA nConta = 1; |SI nConta [ nLong; |HACIENDO nConta = nConta + 1;
          nSaca = (100 * nConta) + 1;
          aSaca = nSaca;
          aLetra = aAux % aSaca;
          aDame = aDame + aLetra;
          |SI aLetra = ":";
               aBeta = aDame % -105;
               |SI aBeta = "HORA:";
                    aBeta = aDame - "CLAVE:";
                    aBeta = aBeta - "HORA:";
                    |QBT aBeta;
                    aClave = aBeta;
                    aDame = "";
               |SINO;
                    aBeta = aDame % -104;
                    |SI aBeta = "SRC:";
                         aBeta = aDame - "FECHA:";
                         aBeta = aBeta - "SRC:";
                         |QBT aBeta;
                         aFecha = aBeta % 110;
                         aAnyo = aFecha % 104;
                         aMes = aFecha % 602;
                         aDia = aFecha % 902;
                         aFecha = aDia + "." + aMes + "." + aAnyo;
                         aHoraLlamada = aBeta % -108;
                         aDame = "";
                    |SINO;
                         |SI aBeta = "DST:";
                              aBeta = aDame - "SRC:";
                              aBeta = aBeta - "DST:";
                              |QBT aBeta;
                              aSrc = aBeta;
                              aDame = "";
                         |SINO;
                              aBeta = aDame % -109;
                              |SI aBeta = "DURACION:";
                                   aBeta = aDame - "DST:";
                                   aBeta = aBeta - "DURACION:";
                                   |QBT aBeta;
                                   aDst = aBeta;
                                   aDame = "";
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
     |SIGUE;
     |QBT aDame;
     |SI aDame ! "";
          aDuracion = aDame;
          nDuracion = aDuracion;
          aDuracion = nDuracion
     |FINSI;

     ||ARA55
     |SI aTipoLlamada = "OUT";
          aLinea = aDst;
     |SINO;
          aLinea = aSrc;
     |FINSI;
     ||ARA38
     |HAZ DameDatosPI;

     |SI aClave ! "";
          |SI aNif = "000000000";
               |SI nSwEsta = 0;
                    ||Desconocido.
                    nGrupo = #dsarm001#156;
                    nSubGrupo = #dsarm001#157;
                    nTipo = #dsarm001#158;
               |SINO;
                    |SI aTipoLlamada = "OUT";
                         nGrupo = #dsarm001#143;
                         nSubGrupo = #dsarm001#144;
                         nTipo = #dsarm001#145;
                    |SINO;
                         nGrupo = #dsarm001#150;
                         nSubGrupo = #dsarm001#151;
                         nTipo = #dsarm001#152;
                    |FINSI;
               |FINSI;
          |SINO;
               |SI aTipoLlamada = "OUT";
                    nGrupo = #dsarm001#143;
                    nSubGrupo = #dsarm001#144;
                    nTipo = #dsarm001#145;
               |SINO;
                    nGrupo = #dsarm001#150;
                    nSubGrupo = #dsarm001#151;
                    nTipo = #dsarm001#152;
               |FINSI;
          |FINSI;
          |HAZ MeteDocumentoLlamada;
     |SINO;
          |SI aNif = "000000000";
               |SI nSwEsta = 0;
                    ||Desconocido.
                    nGrupo = #dsarm001#156;
                    nSubGrupo = #dsarm001#157;
                    nTipo = #dsarm001#158;
               |SINO;
                    |SI aTipoLlamada = "OUT";
                         nGrupo = #dsarm001#143;
                         nSubGrupo = #dsarm001#144;
                         nTipo = #dsarm001#145;
                    |SINO;
                         nGrupo = #dsarm001#150;
                         nSubGrupo = #dsarm001#151;
                         nTipo = #dsarm001#152;
                    |FINSI;
               |FINSI;

               |SI aTipoLlamada = "OUT";
                    nGrupo = #dsarm001#143;
                    nSubGrupo = #dsarm001#144;
                    nTipo = #dsarm001#145;
               |SINO;
                    nGrupo = #dsarm001#150;
                    nSubGrupo = #dsarm001#151;
                    nTipo = #dsarm001#152;
               |FINSI;
          |FINSI;
          |HAZ MeteDocumentoLlamada;
     |FINSI;
|FPRC;

|PROCESO MeteDocumentoLlamada;
     ||Realmente no guarda el documento fisicamente.
     ||Nos guardamos un enlace a una pagina web que es la que se encarga de
     ||permitirnos oir la llamada.

     |ABRE #2;
     #2#0 = nGrupo;
     |LEE 000#2=;
     |SI FSalida ! 0;  |DDEFECTO #2;  |FINSI;
     |CIERRA #2;

     |ABRE #3;
     #3#0 = nGrupo;
     #3#1 = nSubGrupo;
     |LEE 000#3=;
     |SI FSalida ! 0;  |DDEFECTO #3;  |FINSI;
     |CIERRA #3;

     |ABRE #4;
     #4#0 = nGrupo;
     |SI #1#8 = "S";
          #4#1 = nSubGrupo;
          #4#2 = nTipo;
     |SINO;
          #4#1 = 1;
          #4#2 = 1;
     |FINSI;

     |LEE 000#4=;
     |SI FSalida ! 0;  |DDEFECTO #4;  |FINSI;
     |CIERRA #4;

     aUbicacion = #1#1;
     |QBF aUbicacion;
     aUbicacion = aUbicacion + "/" + #2#2;
     |QBF aUbicacion;
     aCreaDir = aUbicacion;
     |SI #1#7 = "N";
         |MKDIR aCreaDir;
     |SINO;
         |RMKDIR aCreaDir;
     |FINSI;
     |SI #1#8 = "S";
          aUbicacion = aUbicacion + "/" + #3#3;
          |QBF aUbicacion;
          aCreaDir = aUbicacion;
          |SI #1#7 = "N";
              |MKDIR aCreaDir;
          |SINO;
              |RMKDIR aCreaDir;
          |FINSI;
          aUbicacion = aUbicacion + "/" + #4#4;
          |QBF aUbicacion;
          aCreaDir = aUbicacion;
          |SI #1#7 = "N";
              |MKDIR aCreaDir;
          |SINO;
              |RMKDIR aCreaDir;
          |FINSI;
     |FINSI;

     |SI #4#75 = "S";
          fFechaSis = ~;
          aFechaSis = fFechaSis;
          aAAAAMM = (aFechaSis % -104) + (aFechaSis % 402);
          aUbicacion = aUbicacion + "/" + aAAAAMM;
          |SI #1#7 = "N";
               |MKDIR aUbicacion;
          |SINO;
               |RMKDIR aUbicacion;
          |FINSI;
     |FINSI;

     |ABRE #5;
     |SELECT #2#5;
     #5#1 = nGrupo;
     #5#2 = nSubGrupo;
     #5#3 = nTipo;
     #5#0 = 999999999;
     |LEE 000#5];
     |SI FSalida = 0;
         |LEE 000#5a;
     |SINO;
         |LEE 000#5u;
     |FINSI;

     nUltDoc = 0;
     |SI FSalida = 0;  |Y #5#1 = nGrupo;  |Y #5#2 = nSubGrupo; |Y #5#3 = nTipo;
         nUltDoc = #5#47;
     |FINSI;

     |SELECT #1#5;
     |DDEFECTO #dsarm005;
     |LEE 000#dsarm005.u;
     |SI FSalida = 0;
          nUltimo = #dsarm005#0 + 1;
     |SINO;
          nUltimo = 1;
     |FINSI;

     |ABRE #dsarm100;
     |DDEFECTO #dsarm100;
     |LEE 000#dsarm100.p;
     |SI FSalida = 0;
          nUltimoDoc = #dsarm100#1 + 1;
     |SINO;
          nUltimoDoc = 1;
     |FINSI;
     |SI nUltimoDoc > nUltimo;
          nUltimo = nUltimoDoc;
     |FINSI;
     #dsarm100#0 = "1";
     |LEE 101#dsarm100.=;
     |SI FSalida ! 0; |GRABA 020#dsarm100.b; |FINSI;
     #dsarm100#1 = nUltimo;
     |GRABA 020#dsarm100.a;
     |LIBERA #dsarm100;
     |CIERRA #dsarm100;

     |DDEFECTO #dsarm005;
     #dsarm005#0 = nUltimo;
     |LEE 101#dsarm005.=;
     |SI FSalida = 0;
          |LIBERA #dsarm005;
          nUltimo = nUltimo + 1;
          |DDEFECTO #dsarm005;
          #dsarm005#0 = nUltimo;
          |LEE 101#dsarm005.=;
          |SI FSalida = 0;
               |LIBERA #dsarm005;
               |ACABA;
          |SINO;
               |GRABA 020#dsarm005.b;
          |FINSI;
     |SINO;
          |GRABA 020#dsarm005.b;
     |FINSI;

/*
     |SI aNomFix = "";
          aExtension = (aNombre % -104) > "";
     |SINO;
          aExtension = (aNomFix % -104) > "";
     |FINSI;
     aAlfa1 = aExtension % 101;
     |SI aAlfa1 ! ".";
          |SI aNomFix = "";
               aExtension = (aNombre % -105) > "";
          |SINO;
               aExtension = (aNomFix % -105) > "";
          |FINSI;
     |FINSI;

     aNomDestino = (("000000000" + #dsarm005#0) % -109) + aExtension;
*/


     aUbicacion = aUbicacion + "/";

     ||Historico
     |HORA aHora;
     aUsuario = Usuario % 810;
     |ABRE #flowm006;
     #flowm006#0  = #dsarm005#0;
     #flowm006#1  = 99999;
     |LEE 000#flowm006.];
     |SI FSalida = 0;
          |LEE 000#flowm006.a;
     |SINO;
          |LEE 000#flowm006.u;
     |FINSI;
     |SI FSalida = 0; |Y #flowm006#0  = #dsarm005#0;
          nLinea = #flowm006#1 + 1;
          nSitu = #flowm006#2;
     |SINO;
          nSitu = 1;
          nLinea = 1;
     |FINSI;
     |DDEFECTO #flowm006;
     #flowm006#0  = #dsarm005#0;
     #flowm006#1  = nLinea;
     |LEE 101#flowm006.=;
     |SI FSalida ! 0; |GRABA 020#flowm006.b; |FINSI;
     #flowm006#2  = nSitu;
     #flowm006#3  = ~;
     #flowm006#4  = aHora;
     #flowm006#5  = aUsuario;
     #flowm006#6  = "RESULTADO ALTA [CORRECTO]";

     enNumRegFlow = #flowm006#0;
     enSituFlow = #flowm006#2;
     |HAZ DimeDesFlow;
     #flowm006#7 = eaDesSituFlow;

     |GRABA 020#flowm006.a;
     |LIBERA #flowm006;
     |CIERRA #flowm006;

     #dsarm005#1 = nGrupo;
     #dsarm005#2 = nSubGrupo;
     #dsarm005#3 = nTipo;
     #dsarm005#4 = ~;
     |HORA aHora;
     aHora = aHora % 105;
     #dsarm005#5 = aHora;
     #dsarm005#6 = "LLAMADA";
     #dsarm005#7 = aUbicacion;
     #dsarm005#9 = "-DOCUMENTOWEB-";
     #dsarm005#10 = aNif;
     #dsarm005#14 = aNomApe;
     #dsarm005#38 = aNomApe;
     #dsarm005#47 = nUltDoc + 1;
     #dsarm005#55 = aTelefonoFax;
     |SI #dsarm005#44 = 0;  #dsarm005#44 = 1;  |FINSI;
     |SI #dsarm005#45 = 0;  #dsarm005#45 = 1;  |FINSI;
     |SI #dsarm005#46 = 0;  #dsarm005#46 = 1;  |FINSI;

     ||ARA
     aAlfaAux = #dsarm004#5;
     |QBF aAlfaAux;
     aAlfaAux = aAlfaAux + ": " + aClave;
     |QBF aAlfaAux;
     #dsarm005#23 = aAlfaAux;

     aAlfaAux = #dsarm004#6;
     |QBF aAlfaAux;
     aAlfaAux = aAlfaAux + ": " + aHoraLlamada;
     |QBF aAlfaAux;
     #dsarm005#24 = aAlfaAux;

     aAlfaAux = #dsarm004#7;
     |QBF aAlfaAux;
     aAlfaAux = aAlfaAux + ": " + aSrc;
     |QBF aAlfaAux;
     #dsarm005#25 = aAlfaAux;

     aAlfaAux = #dsarm004#8;
     |QBF aAlfaAux;
     aAlfaAux = aAlfaAux + ": " + aDst;
     |QBF aAlfaAux;
     #dsarm005#26 = aAlfaAux;

     aAlfaAux = #dsarm004#9;
     |QBF aAlfaAux;
     aAlfaAux = aAlfaAux + ": " + aDuracion;
     |QBF aAlfaAux;
     #dsarm005#27 = aAlfaAux;

     aAlfaAux = #dsarm004#15;
     |QBF aAlfaAux;
     aAlfaAux = aAlfaAux + ": " + aFecha;
     |QBF aAlfaAux;
     #dsarm005#28 = aAlfaAux;

     #dsarm005#93 = aAsuntoCorreo;

     |GRABA 020#dsarm005.a;
     |LIBERA #dsarm005;
     |CIERRA #dsarm005;

     aMenLog = "*Subejecuta [dsarz013;" + #dsarm005#0;
     |QBF aMenLog;
     aMenLog = aMenLog + "]. Inicio";
     |HAZ MeteLog;

     enSwDesde9 = 1;
     aAlfa = "dsarz013;" + #dsarm005#0;
     |SUB_EJECUTA aAlfa;

     aMenLog = "*Subejecuta [dsarz013;" + #dsarm005#0;
     |QBF aMenLog;
     aMenLog = aMenLog + "]. Final";
     |HAZ MeteLog;

     aMenLog = "*Subejecuta [dsarz030;" + #dsarm005#0;
     |QBF aMenLog;
     aMenLog = aMenLog + "]. Inicio";
     |HAZ MeteLog;

     aAlfa = "dsarz030;" + #dsarm005#0;
     |SUB_EJECUTA aAlfa;
     enSwDesde9 = 0;

     aMenLog = "*Subejecuta [dsarz030;" + #dsarm005#0;
     |QBF aMenLog;
     aMenLog = aMenLog + "]. Final";
     |HAZ MeteLog;
|FPRC;

/*
************************************************************************
************ FINAL RECEPCION CORREOS LLAMADAS ASTERISK  ****************
************************************************************************
*/


|PROCESO DameNumDoc;
     aLong = aAsuntoCorreo % A0;
     nLong = aLong;
     |PARA nConta = 1; |SI nConta [ nLong; |HACIENDO nConta = nConta + 1;
          nCampo = 100 * nConta;
          nCampo = nCampo + 10;
          aValor = aAsuntoCorreo % nCampo;
          |SI aValor = "Doc.Num.: "; |O aValor = "Doc.Num.:_";
               nCampo = 100 * (nConta + 10);
               nCampo = nCampo + 9;
               aValor = aAsuntoCorreo % nCampo;
               nNumDoc = aValor;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO SacaNombre;
     aNombre = "";
     aLong = aDirSaca % A0;
     nLong = aLong;
     |PARA nConta = 1; |SI nConta [ nLong; |HACIENDO nConta = nConta + 1;
          |SI nConta [ 99;
               aCampo = ("00" + nConta) % -102;
               aCampo = "-" + aCampo + "01";
               nCampo = aCampo;
               aValor = aDirSaca % nCampo;
               |SI aValor ! "/"; |Y aValor ! "\";
                    aNombre = aValor + aNombre;
               |SINO;
                    |SAL;
               |FINSI;
          |SINO;
               aNombre = "";
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO MeteLog;
     fLogDia = ~;
     |HORA aLogHor;

     |XABRE "A", aFicLog, nHandLog;
     |SI FSalida ] 0;
          |QBF aMenLog;
          |SI aMenLog ! "";
               aMenLog = fLogDia + "#" + aLogHor + "#" + aMenLog;
          |FINSI;
          |XGRABA nHandLog, aMenLog;
          |XCIERRA nHandLog;
     |FINSI;
|FPRC;

|PROCESO RecogerCefax;
     |SI #dsarm001#66 = "S";       ||RECOGER CEFAX
          aMenLog = "Inicio RecogerCefax";
          |HAZ MeteLog;

          |SI #dsarw010#23 = "S";
           |Y #dsarw010#24 = #dsarw010#27;
           |Y #dsarw010#25 = #dsarw010#28;
           |Y #dsarw010#26 = #dsarw010#29;
               |ACABA;        ||Ya hemos recogido las confirmaciones y es la misma cuenta
          |FINSI;

          aServCorreo = #dsarw010#27;
          |QBF aServCorreo;
          |SI aServCorreo = "";
               aMensaje = "0000Servidor Entrante POP3 NO configurado. (CEFAX)";
               |MENAV aMensaje;
               aMenLog = "Servidor Entrante POP3 NO configurado. (CEFAX)";
               |HAZ MeteLog;
               |ACABA;
          |FINSI;

          aIPServ     = #dsarw010#18;  |QBF aIPServ;

          aFrom       = #dsarw010#28;  |QBF aFrom;
          aPwd        = #dsarw010#29;  |QBF aPwd;
          aFichero    = "";
          |HAZ RecogeCorreo;

          aMenLog = "Final RecogerCefax";
          |HAZ MeteLog;
     |FINSI;
|FPRC;

|PROCESO RecogerAsterisk;
     |SI #dsarm001#141 = "S";       ||RECOGER CORREOS ASTERISK LLAMADAS
          aMenLog = "Inicio RecogerAsterisk";
          |HAZ MeteLog;

          |SI #dsarw010#23 = "S";
           |Y #dsarw010#24 = #dsarw010#30;
           |Y #dsarw010#25 = #dsarw010#31;
           |Y #dsarw010#26 = #dsarw010#32;
               |ACABA;        ||Ya hemos recogido las confirmaciones y es la misma cuenta
          |FINSI;

          |SI #dsarm001#66 = "S";
           |Y #dsarw010#27 = #dsarw010#30;
           |Y #dsarw010#28 = #dsarw010#31;
           |Y #dsarw010#29 = #dsarw010#32;
               |ACABA;        ||Ya hemos recogido los CEFAX y es la misma cuenta
          |FINSI;

          aServCorreo = #dsarw010#30;
          |QBF aServCorreo;
          |SI aServCorreo = "";
               aMensaje = "0000Servidor Entrante POP3 NO configurado. (Asterisk)";
               |MENAV aMensaje;
               aMenLog = "Servidor Entrante POP3 NO configurado. (Asterisk)";
               |HAZ MeteLog;
               |ACABA;
          |FINSI;

          aIPServ     = #dsarw010#18;  |QBF aIPServ;

          aFrom       = #dsarw010#31;  |QBF aFrom;
          aPwd        = #dsarw010#32;  |QBF aPwd;
          aFichero    = "";
          |HAZ RecogeCorreo;

          aMenLog = "Final RecogerAsterisk";
          |HAZ MeteLog;
     |FINSI;
|FPRC;

|PROCESO VerificaGrabacion;
     ||Entrada:
     || En el servidor o en el cliente (R*)    nSwMaquinaCliente = 0/1
     || Carpeta                                aCarpetaFlag

     ||Devuelve:
     || Si da error, se graba en el log.

     ||LOG
     aTmpUsuario = Usuario;
     |QBF aTmpUsuario;

     aNomFicheroFlag = "dsflagescritura" + aTmpUsuario + ".txt";

     aNomCarpetaFlag = aCarpetaFlag;
     aUltimo = aCarpetaFlag % -101;
     |SI aUltimo ! "/"; |Y aUltimo ! "\";
          aCarpetaFlag = aCarpetaFlag + "/";
     |FINSI;

     aFicheroFlag = aCarpetaFlag + aNomFicheroFlag;

     |SI nSwMaquinaCliente = 0;
          |XABRE "W", aFicheroFlag, nHandleFlag;
     |SINO;
          |XABRE "CW", aFicheroFlag, nHandleFlag;
     |FINSI;
     |SI FSalida ] 0;
          aTmpGraba = " --- Fichero control DS ---";
          |XGRABA nHandleFlag, aTmpGraba;
          |SI FSalida < 0;
               aMenLog = "Problemas al escribir en el directorio:" + aNomCarpetaFlag;
               |HAZ MeteLog;
          |FINSI;
     |SINO;
          aMenLog = "Problemas al escribir en el directorio:" + aNomCarpetaFlag;
          |HAZ MeteLog;
     |FINSI;
     |SI nSwMaquinaCliente = 0;
          |FBORRA aFicheroFlag;
     |SINO;
          |RFBORRA aFicheroFlag;
     |FINSI;
|FPRC;


|PROGRAMA;
     aVacio40 = " " * 40;
     aZeta40 = "z" * 40;

     nCorreosRecibidos = 0;
     aParametro = PARAMETRO;
     |QBF aParametro;

     |SI aParametro = "SERVIDORUNIX";
          enSwImpUnix = 1;
     |SINO;
          enSwImpUnix = 0;
     |FINSI;

     |DBASE aBase;
     |QBF aBase;
     aFicLog = aBase + "logs/";
     |MKDIR aFicLog;
     aFicLog = aFicLog + "recep_correos.log";

     aMenLog = "";
     |HAZ MeteLog;

     aMenLog = "Inicio Recepcion de Correos";
     |HAZ MeteLog;

     |SI aParametro = "";
          aMenLog = "Parametros: [--SIN PARAMETROS--]";
     |SINO;
          aMenLog = "Parametros: " + aParametro;
     |FINSI;
     |HAZ MeteLog;

     aMenLog = "Directorio Base: " + aBase;
     |HAZ MeteLog;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;
     |QBF aIPRemoto;
     |SI aIPRemoto ! "";
          aMenLog = "Ejecucion C/S. Direccion IP: " + aIPRemoto;
     |SINO;
          aMenLog = "Ejecucion en modo LOCAL";
     |FINSI;
     |HAZ MeteLog;

     |ABRE #dsarm001;
     |DDEFECTO #dsarm001;
     |LEE 000#dsarm001.p;
     |SI FSalida ! 0;
          aMenLog = "Parametros generales no definidos [dsarm001]";
          |HAZ MeteLog;
          |DDEFECTO #dsarm001;
     |FINSI;
     |CIERRA #dsarm001;

     |ABRE #dsarw010;
     |LEE 000#dsarw010.p;
     |SI FSalida ! 0;
          aMensaje = "0000Control de Correos no configurado.";
          |MENAV aMensaje;

          aMenLog = "Control de Correos no configurado [dsarw010]";
          |HAZ MeteLog;

          aMenLog = "Final Recepcion de Correos";
          |HAZ MeteLog;

          |ACABA;
     |FINSI;

     nSwContinua = 0;
     aAlfa = #dsarw010#23;
     |QBF aAlfa;
     |SI aAlfa = "S";
          nSwContinua = 1;
          aServCorreo = #dsarw010#24;
          |QBF aServCorreo;
          aAlfa = #dsarw010#23;
          |QBF aAlfa;
          |SI aAlfa = "";
               aMensaje = "0000Confirmacion de lectura NO activa.";
               |MENAV aMensaje;
               aMenLog = "Confirmacion de lectura NO activa";
               |HAZ MeteLog;

               nSwContinua = 0;
          |SINO;
               |SI aServCorreo = "";
                    aMensaje = "0000Servidor Entrante POP3 NO configurado.";
                    |MENAV aMensaje;
                    aMenLog = "Servidor Entrante POP3 NO configurado";
                    |HAZ MeteLog;
                    nSwContinua = 0;
               |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #dsarw010;

     |DBASE aBase;
     |QBF aBase;

     aDirEntrantes = aBase + "correos/";
     |QBF aDirEntrantes;
     |MKDIR aDirEntrantes;

     aMenLog = "Directorio entrada correos: " + aDirEntrantes;
     |HAZ MeteLog;

     ||LOG

     aMenLog = "Inicio VerificaGrabacion: " + aDirEntrantes;
     |QBF aMenLog;
     |HAZ MeteLog;

     aCarpetaFlag = aDirEntrantes;
     nSwMaquinaCliente = 0;
     |HAZ VerificaGrabacion;

     aMenLog = "Final VerificaGrabacion: " + aDirEntrantes;
     |QBF aMenLog;
     |HAZ MeteLog;

     |SI nSwContinua = 1;
          aIPServ     = #dsarw010#18;  |QBF aIPServ;

          aFrom       = #dsarw010#25;  |QBF aFrom;
          aPwd        = #dsarw010#26;  |QBF aPwd;
          aFichero    = "";

          aMenLog = "Inicio RecogeCorreo";
          |HAZ MeteLog;
          |HAZ RecogeCorreo;
          aMenLog = "Final RecogeCorreo";
          |HAZ MeteLog;
     |FINSI;

     |HAZ RecogerCefax;

     |HAZ RecogerAsterisk;

     aMenLog = "Numero de Correos recibidos: " + nCorreosRecibidos;
     |HAZ MeteLog;

     |SI nCorreosRecibidos ! 0;

          aIPRemoto = "";
          |IP_REMOTO aIPRemoto;
          |QBF aIPRemoto;
          |SI aIPRemoto = "";
               enSwDesde9 = 1;
          |FINSI;

          aMenLog = "*Subejecuta [dpntz001;CLI]. Inicio";
          |HAZ MeteLog;

          |SUB_EJECUTA "dpntz001;CLI";

          aMenLog = "*Subejecuta [dpntz001;CLI]. Final";
          |HAZ MeteLog;

          |SI enSwDesde9 = 1;
               enSwDesde9 = 0;
          |FINSI;
     |FINSI;

     aMenLog = "Final Recepcion de Correos";
     |HAZ MeteLog;
|FPRO;
