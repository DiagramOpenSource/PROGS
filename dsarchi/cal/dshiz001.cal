|FICHEROS;
     dshiz001;
     dshim001;

     dsarm001;
     dsarm002;
     dsarm003;
     dsarm004;
     dsarm005;

     dshim001@;
     dsarm005@;
|FIN;

|VARIABLES;
     n1Vent       = 0;
     nTCmp        = 0;
     nCmp         = 0;
     nRegis       = 0;
     nReg         = 0;
     nUltReg      = 0;
     nEj          = 0;
     nEjD         = 0;
     nEjH         = 0;

     aAlfa        = "";
     aQQ          = "";
     aPath        = "";
     aUbicacion   = "";
     aFechaSis    = "";
     aAAAAMM      = "";
     aPathOri     = "";
     aPathDes     = "";
     aFicOri      = "";
     aFicDes      = "";
     aExt         = "";
     aAbre        = "";
     aIPRemoto    = "";
     aOrigen      = "";
     aDestino     = "";
     aNome        = "";
     aBase        = "";
     aMas         = "";
     aEjAnt       = "";
     aEj          = "";
     aFic         = "";
     aOri         = "";
     aDes         = "";

     fFechaSis    = @;

     &enGrupoTres = 0;
     &enSubGTres  = 0;
     &enTipoTres  = 0;
     &enDocumento = 0;
     &enPasaHisto = 1;
     &eaEj        = "";
|FIN;

|PROCESO DAMETRE;
     |SUB_EJECUTA "dsarz003;DAMETRE";
     |SI enGrupoTres ! 0;
         #dshiz001#2 = enGrupoTres;
         #dshiz001#3 = enGrupoTres;
         #dshiz001#4 = enSubGTres;
         #dshiz001#5 = enSubGTres;
         #dshiz001#6 = enTipoTres;
         #dshiz001#7 = enTipoTres;

         |PINDA #0#dshiz001;
     |FINSI;
|FPRC;

|PROCESO SacaUbicacion;
     aPath = #dsarm001#1;  |QBF aPath;

     #dsarm002#0 = #dsarm005#1;
     |LEE 000#dsarm002.=;
     |SI FSalida ! 0;  |DDEFECTO #dsarm002;  |FINSI;

     aPath = aPath + "/" + #dsarm002#2;  |QBF aPath;
     |SI #dsarm001#7 = "N";
         |MKDIR aPath;
     |SINO;
         |RMKDIR aPath;
     |FINSI;

     |SI #dsarm001#8 = "N";
         aPath = aPath + "/";
         aUbicacion = aPath;

         #dsarm004#0 = #dsarm005#1;
         #dsarm004#1 = 1;
         #dsarm004#2 = 1;
         |LEE 000#dsarm002.=;
         |SI FSalida ! 0;  |DDEFECTO #dsarm004;  |FINSI;

         |SI #dsarm004#75 = "S";
              fFechaSis = ~;
              aFechaSis = fFechaSis;
              aAAAAMM = (aFechaSis % -104) + (aFechaSis % 402);
              aUbicacion = aUbicacion + aAAAAMM + "/";
              |SI #dsarm001#7 = "N";
                   |MKDIR aUbicacion;
              |SINO;
                   |RMKDIR aUbicacion;
              |FINSI;
         |FINSI;

         |ACABA;
     |FINSI;

     #dsarm003#0 = #dsarm005#1;
     #dsarm003#1 = #dsarm005#2;
     |LEE 000#dsarm003.=;
     |SI FSalida ! 0;  |DDEFECTO #dsarm003;  |FINSI;

     aPath = aPath + "/" + #dsarm003#3;  |QBF aPath;
     |SI #dsarm001#7 = "N";
         |MKDIR aPath;
     |SINO;
         |RMKDIR aPath;
     |FINSI;

     #dsarm004#0 = #dsarm005#1;
     #dsarm004#1 = #dsarm005#2;
     #dsarm004#2 = #dsarm005#3;
     |LEE 000#dsarm004.=;
     |SI FSalida ! 0;  |DDEFECTO #dsarm004;  |FINSI;

     aPath = aPath + "/" + #dsarm004#4;  |QBF aPath;
     |SI #dsarm001#7 = "N";
         |MKDIR aPath;
     |SINO;
         |RMKDIR aPath;
     |FINSI;

     |SI PARAMETRO ! "DEVOLVER";  |Y PARAMETRO ! "DESARCHIVAR";
         aPath = aPath + "/Historico";
         |SI #dsarm001#7 = "N";
             |MKDIR aPath;
         |SINO;
             |RMKDIR aPath;
         |FINSI;
     |FINSI;

     aUbicacion = aPath + "/";

     |SI #dsarm004#75 = "S";
          fFechaSis = ~;
          aFechaSis = fFechaSis;
          aAAAAMM = (aFechaSis % -104) + (aFechaSis % 402);
          aUbicacion = aUbicacion + aAAAAMM + "/";
          |SI #dsarm001#7 = "N";
               |MKDIR aUbicacion;
          |SINO;
               |RMKDIR aUbicacion;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CopiaDocumento;
     aAbre = aPathOri + aFicOri;

     |SI #dsarm001#7 = "N";  |Y aIPRemoto ! "";
         |XABRE "E", aAbre, 0;
     |SINO;
         |XABRE "EC", aAbre, 0;
     |FINSI;

     |SI FSalida ] 0;
         aOrigen  = aPathOri + aFicOri;
         aDestino = aPathDes + aFicDes;
         |SI #dsarm001#7 = "N";
             |RENOMBRA_FICHERO aOrigen, aDestino;
         |SINO;
             |RRENOMBRA_FICHERO aOrigen, aDestino;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Extension;
     aExt   = (aFicOri % -104);
     aAlfa  = aExt % 101;
     |SI aAlfa ! ".";
         aExt   = (aFicOri % -105);
         aAlfa = aExt % 101;
         |SI aAlfa ! ".";
             aExt = "";
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO TrataDocumentoHIS;
     |HAZ SacaUbicacion;

     #dshim001#7  = aUbicacion;
     aPathOri = #dsarm005#7;  |QBF aPathOri;
     aPathDes = #dshim001#7;  |QBF aPathDes;

     aFicOri  = #dsarm005#9;  |QBF aFicOri;

     |SI aFicOri = "-DOCUMENTOWEB-";  |ACABA;  |FINSI;

     |HAZ Extension;

     #dshim001#9 = (("000000000" + #dshim001#0) % -109) + aExt;

     aFicDes  = #dshim001#9;  |QBF aFicDes;

     |HAZ CopiaDocumento;

     aFicOri  = #dsarm005#92;  |QBF aFicOri;
     |SI aFicOri ! "";
         |HAZ Extension;

         #dshim001#92 = (("000000000" + #dshim001#0) % -109) + aExt;

         aFicDes  = #dshim001#92;  |QBF aFicDes;
         |HAZ CopiaDocumento;
     |FINSI;

     aFicOri  = #dsarm005#96;  |QBF aFicOri;
     |SI aFicOri ! "";
         |HAZ Extension;

         #dshim001#96 = (("000000000" + #dshim001#0) % -109) + aExt;

         aFicDes  = #dshim001#96;  |QBF aFicDes;
         |HAZ CopiaDocumento;
     |FINSI;
|FPRC;

|PROCESO dsarm005Pas;
     aEj = #dsarm005#4 % -104;
     |SI aEj ! aEjAnt;
         |SI aEjAnt ! "";
             |CIERRA #dshim001;
         |FINSI;

         aNome = "h" + aEj + "m01";
         |NOME_DAT #dshim001#aNome#;
         |ABRE #dshim001;
     |FINSI;

     nRegis = 1;

     |LEE 000#dshim001.u;
     |SI FSalida = 0;
         nRegis = #dshim001#0 + 1;
     |FINSI;

     |DDEFECTO #dshim001;
     #dshim001#0 = nRegis;
     |GRABA 020#dshim001.b;

     |PARA nCmp = 1; |SI nCmp [ nTCmp;  |HACIENDO nCmp = nCmp + 1;
           #dshim001#nCmp# = #dsarm005#nCmp#;
     |SIGUE;

     #dshim001#43 = #dsarm005#0;

     |HAZ TrataDocumentoHIS;

     |GRABA 020#dshim001.a;
     |LIBERA #dshim001;

     |BORRA 020#dsarm005.a;
     |LIBERA #dsarm005;

     aEjAnt = aEj;

     |PULSATECLA;
|FPRC;

|DEFBUCLE dsarm005Pas;
     #dsarm005#8;
     1, 2, 3;
     #dshiz001#0, "     ", 000000001, #dshiz001#2, #dshiz001#4, #dshiz001#6;
     #dshiz001#1, "zzzzz", 999999999, #dshiz001#3, #dshiz001#5, #dshiz001#7;
     be;
     NULL, dsarm005Pas;
|FIN;

|PROCESO PasaDSHISM01;
     aAlfa = "0000NBorrar  los registros del archivo y los crear  en el hist¢rico.";
     aAlfa = aAlfa + " Est  seguro de la selecci¢n.";
     |CONFI aAlfa;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |VENTANA 0501, 0540, -1, 16, "Realizando proceso ...";
     n1Vent = FSalida;

     |PTEC 802;  |PAUSA;

     |ABRE #dsarm002;
     |ABRE #dsarm003;
     |ABRE #dsarm004;

     aEj    = "";
     aEjAnt = "";
     |BUCLE dsarm005Pas;

     |CIERRA #dsarm002;
     |CIERRA #dsarm003;
     |CIERRA #dsarm004;
     |CIERRA #dshim001;

     |FINVENTANA n1Vent;
|FPRC;

|PROCESO TrataDocumentoARM;
     |HAZ SacaUbicacion;

     #dsarm005#7  = aUbicacion;
     aPathOri = #dshim001#7;  |QBF aPathOri;
     aPathDes = #dsarm005#7;  |QBF aPathDes;

     aFicOri  = #dshim001#9;  |QBF aFicOri;

     |SI aFicOri = "-DOCUMENTOWEB-";  |ACABA;  |FINSI;

     |HAZ Extension;

     #dsarm005#9 = (("000000000" + #dsarm005#0) % -109) + aExt;

     aFicDes  = #dsarm005#9;  |QBF aFicDes;

     |HAZ CopiaDocumento;

     aFicOri  = #dshim001#92;  |QBF aFicOri;
     |SI aFicOri ! "";
         |HAZ Extension;

         #dsarm005#92 = (("000000000" + #dsarm005#0) % -109) + aExt;

         aFicDes  = #dsarm005#92;  |QBF aFicDes;
         |HAZ CopiaDocumento;
     |FINSI;

     aFicOri  = #dshim001#96;  |QBF aFicOri;
     |SI aFicOri ! "";
         |HAZ Extension;

         #dsarm005#96 = (("000000000" + #dsarm005#0) % -109) + aExt;

         aFicDes  = #dsarm005#96;  |QBF aFicDes;
         |HAZ CopiaDocumento;
     |FINSI;
|FPRC;

|PROCESO dshim001Pas;
     nRegis = #dshim001#43;

     #dsarm005#0 = #dshim001#43;
     |LEE 000#dsarm005.=;
     |SI FSalida = 0;
         nRegis = 1;
         |LEE 000#dsarm005.u;
         |SI FSalida = 0;
             nRegis = #dsarm005#0 + 1;
         |FINSI;
     |FINSI;

     |DDEFECTO #dsarm005;
     #dsarm005#0 = nRegis;
     |GRABA 020#dsarm005.b;

     |PARA nCmp = 1; |SI nCmp [ nTCmp;  |HACIENDO nCmp = nCmp + 1;
           #dsarm005#nCmp# = #dshim001#nCmp#;
     |SIGUE;

     #dsarm005#43 = 0;

     |HAZ TrataDocumentoARM;

     |GRABA 020#dsarm005.a;
     |LIBERA #dsarm005;

     |BORRA 020#dshim001.a;
     |LIBERA #dshim001;
|FPRC;

|DEFBUCLE dshim001Pas;
     #dshim001#1;
     4, 1, 2, 3;
     000000001, #dshiz001#0, #dshiz001#2, #dshiz001#4, #dshiz001#6;
     999999999, #dshiz001#1, #dshiz001#3, #dshiz001#5, #dshiz001#7;
     be;
     NULL, dshim001Pas;
|FIN;

|PROCESO PasaDSARM005;
     aAlfa = "0000NBorrar  los registros del hist¢rico y los crear  en el regitro.";
     aAlfa = aAlfa + " Est  seguro de la selecci¢n.";
     |CONFI aAlfa;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |VENTANA 0501, 0540, -1, 16, "Realizando proceso ...";
     n1Vent = FSalida;

     |PTEC 802;  |PAUSA;

     |ABRE #dsarm002;
     |ABRE #dsarm003;
     |ABRE #dsarm004;
     |ABRE #dsarm005;

     nEjD = 2000;
     |SI #dshiz001#0 > "01.01.0000";
         aAlfa = #dshiz001#0 % -104;
         nEjD  = aAlfa;
     |FINSI;
     aAlfa = #dshiz001#1 % -104;
     nEjH  = aAlfa;

     |DFICE aFic;
     |PARA nEj = nEjD;  |SI nEj [ nEjH;  |HACIENDO nEj = nEj + 1;
           aAlfa = aFic + "h" + nEj + "m01.dat";
           |XABRE "E", aAlfa, 0;
           |SI FSalida ] 0;
               aNome = "h" + nEj + "m01";
               |NOME_DAT #dshim001#aNome#;

               |BUCLE dshim001Pas;
           |FINSI;
     |SIGUE;

     |CIERRA #dsarm002;
     |CIERRA #dsarm003;
     |CIERRA #dsarm004;
     |CIERRA #dsarm005;

     |FINVENTANA n1Vent;
|FPRC;

|PROCESO Tipo12;  |TIPO 12;
     |FINVENTANA n1Vent;

     |SI FSalida = 7;  |ACABA;  |FINSI;

     |SI PARAMETRO = "ARCHIVAR";
         |HAZ PasaDSHISM01;
     |FINSI;

     |SI PARAMETRO = "DESARCHIVAR";
         |HAZ PasaDSARM005;
     |FINSI;
|FPRC;

|PROCESO Archivar;
     |VENTANA 0501, 1935, -1, 16, "Archivar en el hist¢rico";
     n1Vent = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Selecci¢n Grupo/Subrgrupo/Tipo", 1702, 1730, DAMETRE;

     |DDEFECTO #dshiz001;
     |PINPA #0#dshiz001;
     |PINDA #0#dshiz001;
     |ENDATOS #1#dshiz001;

     enPasaHisto = 0;
|FPRC;

|PROCESO Desarchivar;
     |VENTANA 0501, 1935, -1, 16, "Desarchivar del hist¢rico";
     n1Vent = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Selecci¢n Grupo/Subrgrupo/Tipo", 1702, 1730, DAMETRE;

     |DDEFECTO #dshiz001;
     |PINPA #0#dshiz001;
     |PINDA #0#dshiz001;
     |ENDATOS #1#dshiz001;

     enPasaHisto = 0;
|FPRC;

|PROCESO Limpiar;
     aNome = "dsarm005" + Usuario;

     |DFICE aFic;

     aOri = aFic + "dsarm005.dat";
     aDes = aFic + aNome + ".dat";
     |COPIA_FICHERO aOri, aDes;

     aOri = aFic + "dsarm005.ddx";
     aDes = aFic + aNome + ".ddx";
     |COPIA_FICHERO aOri, aDes;

     aOri = aFic + "dsarm005.ixx";
     aDes = aFic + aNome + ".ixx";
     |COPIA_FICHERO aOri, aDes;

     |ABRE #dsarm005;
     |CIERRA #dsarm005;
     |DELINDEX #dsarm005;

     |DDEF aBase;
     aMas = aBase + "dsarm005.mas";
     |CARGA_DEF aMas, dsarm005@;

     |NOME_DAT #dsarm005@#aNome#;
     |ABRE #dsarm005@;
     |LEE 000#dsarm005@.u;
     |SI FSalida ! 0;  |DDEFECTO #dsarm005@;  |FINSI;

     nUltReg = #dsarm005@#0;

     |ABRE #dsarm005;
     |PARA nReg = nUltReg;  |SI nReg > 0;  |HACIENDO nReg = nReg - 1;
           #dsarm005@#0 = nReg;
           |LEE 000#dsarm005@.=;
           |SI FSalida = 0;
               |SI #dsarm005@#1 ! 0;  |O #dsarm005@#2 ! 0;  |O #dsarm005@#3 ! 0;
                   |PARA nCmp = 0;  |SI nCmp [ nTCmp;  |HACIENDO nCmp = nCmp + 1;
                         #dsarm005#nCmp# = #dsarm005@#nCmp#;
                   |SIGUE;

                   |GRABA 020#dsarm005.n;
                   |LIBERA #dsarm005;
               |FINSI;
           |FINSI;
     |SIGUE;
     |CIERRA #dsarm005;

     |CIERRA #dsarm005@;
     |DELINDEX #dsarm005@;
     |DESCARGA_DEF #dsarm005@;
|FPRC;

|PROCESO dshim001;
     aEj = #dshim001#4 % -104;
     |SI aEj ! aEjAnt;
         |SI aEjAnt ! "";
             |CIERRA #dshim001@;
         |FINSI;

         aNome = "h" + aEj + "m01";
         |NOME_DAT #dshim001@#aNome#;
         |ABRE #dshim001@;
     |FINSI;

     |PARA nCmp = 0; |SI nCmp [ nTCmp;  |HACIENDO nCmp = nCmp + 1;
           #dshim001@#nCmp# = #dshim001#nCmp#;
     |SIGUE;

     |GRABA 020#dshim001@.n;
     |LIBERA #dshim001@;

     aEjAnt = aEj;
|FPRC;

|DEFBUCLE dshim001;
     #dshim001#2;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dshim001;
|FIN;

|PROCESO SeparaPorEjer;
     aNome = "dshim001";
     |NOME_DAT #dshim001#aNome#;

     |ABRE #dshim001;
     |LEE 000#dshim001.p;
     |SI FSalida ! 0;
         |CIERRA #dshim001;
         |ACABA;
     |FINSI;
     |CIERRA #dshim001;

     |VENTANA 0501, 0590, -1, 16, "Cambio de versi¢n. Esto puede tardar unos minutos. Espere por favor";
     n1Vent = FSalida;

     |PTEC 802;  |PAUSA;

     |DDEF aBase;
     aMas = aBase + "dshim001.mas";
     |CARGA_DEF aMas, dshim001@;

     aEj    = "";
     aEjAnt = "";

     |BUCLE dshim001;
     |DELINDEX #dshim001;

     |CIERRA #dshim001@;
     |DESCARGA_DEF #dshim001@;

     |FINVENTANA n1Vent;
|FPRC;

|PROGRAMA;
     enPasaHisto = 1;
     aIPRemoto   = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     aQQ   = "|NC";
     nTCmp = #dshim001#aQQ#;
     nTCmp = nTCmp - 1;

     |ABRE #dsarm001;
     |LEE 000#dsarm001.p;
     |SI FSalida ! 0;  |DDEFECTO #dsarm001;  |FINSI;
     |CIERRA #dsarm001;

     |HAZ SeparaPorEjer;

     |SI PARAMETRO = "ARCHIVAR";
         |HAZ Archivar;
         |ACABA;
     |FINSI;

     |SI PARAMETRO = "DESARCHIVAR";
         |HAZ Desarchivar;
         |ACABA;
     |FINSI;

     |SI PARAMETRO = "DEVOLVER";
         aNome = "h" + eaEj + "m01";
         |NOME_DAT #dshim001#aNome#;

         |ABRE #dshim001;
         #dshim001#0 = enDocumento;
         |LEE 101#dshim001.=;
         |SI FSalida = 0;
             |HAZ dshim001Pas;
         |FINSI;
         |CIERRA #dshim001;

         enPasaHisto = 0;

         |ACABA;
     |FINSI;

     |SI PARAMETRO = "LIMPIAR";
         |HAZ Limpiar;
         |ACABA;
     |FINSI;
|FPRO;
