|FICHEROS;
     dsarm001 #1;
     dsarm005 #5;

     dsarz017 #0;
|FIN;

|VARIABLES;
     nVent           = 0;
     nEmpresa        = 0;
     nAnyo           = 0;
     nPeriodo        = 0;
     nModelo         = 0;
     nComplem        = 0;
     nGrid           = -1;
     nSalir          = 0;
     nRango          = 0;
     nDAnyo          = 0;
     nHAnyo          = 0;
     nDPeriodo       = 0;
     nHPeriodo       = 0;
     nBoton1         = 0;
     nBoton2         = 0;
     nBoton3         = 0;
     nBoton4         = 0;
     nHandle         = 0;
     nSubVent        = 0;
     nPausa          = 0;

     aAlfa           = "";
     aFichero        = "";
     aEsc1           = "";
     aEsc2           = "";
     aRetu           = "";
     aTecla          = "";
     aPathRemoto     = "";
     aPathBase       = "";
     aIPRemoto       = "";
     aMensaje        = "";
     aEjecuta        = "";

     {-1}aVent       = "";
         aVent1      = 0;
         aVent2      = 0;
         aVent3      = 0;
         aVent4      = 0;
         aVent5      = "";

     &eaUnidad       = "";
     &eaOrigen       = "";
     &eaDestino      = "";
|FIN;

|PROCESO Presentar;
     |SI #1#7 = "N";
          |HAZ DimeUnidad;
     |SINO;
          eaUnidad = "c";
     |FINSI;

     aPathRemoto = eaUnidad + ":\DOCUMENTOS";           |RMKDIR aPathRemoto;
     aPathRemoto = eaUnidad + ":\DOCUMENTOS\DSARCHI\";  |RMKDIR aPathRemoto;

     |DBASE aPathBase;
     |QBF aPathBase;

     eaOrigen  = aPathBase + "dlls/dsabreextension.exe";
     eaDestino = aPathRemoto + "dsabreextension.exe";
     |HAZ EnviaFicheroConMD5;

     aFichero = #0#7;             |QBF aFichero;
     aFichero = aFichero + #0#8;  |QBF aFichero;

     eaOrigen  = aFichero;
     eaDestino = aPathRemoto + "\" + #0#8;  |QBF eaDestino;

     aIPRemoto   = "";
     |IP_REMOTO aIPRemoto;

     |SI #1#7 = "N";
         |SI aIPRemoto ! "";
             |ENVIA_FICHERO eaOrigen, eaDestino;
         |SINO;
             |COPIA_FICHERO eaOrigen, eaDestino;
         |FINSI;
     |SINO;
         |RCOPIA_FICHERO eaOrigen, eaDestino;
     |FINSI;

     |SI aIPRemoto = "";
         |XABRE "E", eaDestino, nHandle;
     |SINO;
         |XABRE "EC", eaDestino, nHandle;
     |FINSI;

     |SI FSalida < 0;
         aMensaje = "0000No puedo localizar el documento. Debe haber sido borrado o movido de carpeta";
         |MENAV aMensaje;
         |ACABA;
     |FINSI;

     |VENTANA 0501, 2399, -1, 16, "Visualizando Documento";
     nSubVent  = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |XABRE "EC", eaDestino, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     aEjecuta = "START " + eaUnidad + ":\DOCUMENTOS\DSARCHI\dsabreextension ";
     aEjecuta = aEjecuta + eaDestino;
     aEjecuta = aEjecuta + " " + eaUnidad + ":\DOCUMENTOS\DSARCHI\" + "fin" + Usuario + ".txt";

     aAlfa  = eaUnidad + ":\DOCUMENTOS\DSARCHI\" + "fin" + Usuario + ".txt";
     |RFBORRA aAlfa;

     |RSYSTEM aEjecuta;
     aAlfa  = eaUnidad + ":\DOCUMENTOS\DSARCHI\" + "fin" + Usuario + ".txt";
     |PARA;  |SI;  |HACIENDO;
             |XABRE "EC", aAlfa, nHandle;
             |SI FSalida ] 0;
                 |SAL;
             |FINSI;

             |PARA nPausa = 1; |SI nPausa < 10000; |HACIENDO nPausa = nPausa + 1;
             |SIGUE;
             |PULSATECLA;
     |SIGUE;
     |PULSATECLA;

     aAlfa  = eaUnidad + ":\DOCUMENTOS\DSARCHI\" + "fin" + Usuario + ".txt";
     |RFBORRA aAlfa;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nSubVent;
|FPRC;

|PROCESO Evento0;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#0=;
         |SI FSalida = 0;
             |ESTADO_CONTROL nBoton4, "ENABLE";
         |FINSI;
     |FINSI;

     |SI FSalida = 4;
         |LEE 000#0=;
         |SI FSalida ! 0;  |ACABA;  |FINSI;

         |HAZ Presentar;

         FSalida = "SKIPDEFAULT";
     |FINSI;
|FPRC;

|PROCESO dsarm005M;
     #0#0  = #5#0;
     #0#1  = #5#19;
     #0#2  = #5#8;
     #0#3  = #5#17;
     #0#4  = #5#18;
     #0#5  = #5#20;
     #0#6  = #5#14;
     #0#7  = #5#7;
     #0#8  = #5#9;
     |GRABA 020#0n;
     |LIBERA #0;
|FPRC;

|DEFBUCLE dsarm005M;
     #5#9;
     ;
     nEmpresa, nModelo, nDAnyo, nDPeriodo, 00, 000000001;
     nEmpresa, nModelo, nHAnyo, nHPeriodo, 98, 999999999;
     ;
     NULL, dsarm005M;
|FIN;

|PROCESO LinealSimple;
     |ESTADO_CONTROL nBoton4, "DISABLE";

     |SI nGrid ! -1;
         |DESTRUYECONTROL nGrid;
     |FINSI;

     aFichero = ("md" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #0 aFichero;

     |ABRE #0;  |CIERRA #0;  |DELINDEX #0;

     |ABRE #0;
     |BUCLE dsarm005M;

     nRango = 4 + 8 + 16 + 32;
     |LINEAL_SIMPLE #2#0, nRango, 0601, 2199, NULL, NULL, Evento0;
     nGrid = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}Salir," + nGrid;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |CIERRA #0;
     |DELINDEX #0;
|FPRC;

|PROCESO DelPeriodo;
     nDAnyo    = nAnyo;
     nHAnyo    = nAnyo;
     nDPeriodo = nPeriodo;
     nHPeriodo = nPeriodo;

     |ESTADO_CONTROL nBoton1, "DISABLE";
     |ESTADO_CONTROL nBoton2, "ENABLE";
     |ESTADO_CONTROL nBoton3, "ENABLE";

     nSalir = 0;
     |PTEC 806;
|FPRC;

|PROCESO DelEjercicio;
     nDAnyo    = nAnyo;
     nHAnyo    = nAnyo;
     nDPeriodo = 00;
     nHPeriodo = 99;

     |ESTADO_CONTROL nBoton2, "DISABLE";
     |ESTADO_CONTROL nBoton1, "ENABLE";
     |ESTADO_CONTROL nBoton3, "ENABLE";

     nSalir = 0;
     |PTEC 806;
|FPRC;

|PROCESO Todo;
     nDAnyo    = 0;
     nHAnyo    = 9999;
     nDPeriodo = 00;
     nHPeriodo = 99;

     |ESTADO_CONTROL nBoton3, "DISABLE";
     |ESTADO_CONTROL nBoton1, "ENABLE";
     |ESTADO_CONTROL nBoton2, "ENABLE";

     nSalir = 0;
     |PTEC 806;
|FPRC;

|PROGRAMA;
     |ABRE #1;
     |LEE 000#1p;
     |SI FSalida ! 0;  |DDEFECTO #1;  |FINSI;
     |CIERRA #1;

     aAlfa = PARAMETRO % 105;    nEmpresa = aAlfa;
     aAlfa = PARAMETRO % 604;    nAnyo    = aAlfa;
     aAlfa = PARAMETRO % 1002;   nPeriodo = aAlfa;
     aAlfa = PARAMETRO % 1203;   nModelo  = aAlfa;
     aAlfa = PARAMETRO % 1502;   nComplem = aAlfa;

     aAlfa = "Modelo (" + nModelo + ")" + " Empresa: " + (PARAMETRO % 105) + "-" + (PARAMETRO % 1740);
     |VENTANA 0501, 2399, -1, 16, aAlfa;
     nVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |CONTROL_SIMPLE 0, "BOTON,Del periodo", 2202, 2215, DelPeriodo;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,Del ejercicio", 2217, 2230, DelEjercicio;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,Todos", 2232, 2245, Todo;
     nBoton3 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,Presentar documento", 2247, 2270, Presentar;
     nBoton4 = FSalida;

     |ESTADO_CONTROL nBoton1, "DISABLE";

     nDAnyo    = nAnyo;
     nHAnyo    = nAnyo;
     nDPeriodo = nPeriodo;
     nHPeriodo = nPeriodo;
     |PARA;  |SI;  |HACIENDO;
             nSalir = 1;
             |HAZ LinealSimple;
             |SI nSalir = 1;  |SAL;  |FINSI;
     |SIGUE;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVent;
|FPRO;
