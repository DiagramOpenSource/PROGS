|FICHEROS;
     flowm020;
     floww008,MANTE;
|FIN;

|VARIABLES;
     nBtnAlta       = 0;
     nBtnModi       = 0;
     nBtnBaja       = 0;
     nVentana       = 0;
     nVentTra       = 0;
     nRango         = 0;
     nGrid          = 0;
     nModo          = 0;

     aEsc1          = "";
     aEsc2          = "";
     aRetu          = "";
     aTecla         = "";
     aMensaje       = "";

     {-1}aVent      = "";
         aVent1     = 0;
         aVent2     = 0;
         aVent3     = 0;
         aVent4     = 0;
         aVent5     = "";
|FIN;

|PROCESO Tipo12;  |TIPO 12;
|FPRC;

|PROCESO Tipo0C0;  |TIPO 0;
     |SI nModo = 1;
         #flowm020#0 = #floww008#0;
         |LEE 000#flowm020.=;
         |SI FSalida = 0;
             |MENAV "0000El usuario ya existe.";
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO PantaFanta;
     |VENTANA 0501, 1336, -1, 16, "Usuario";
     nVentTra = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentTra;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |SI nModo = 1;
         |DDEFECTO #floww008;

         |C_M #floww008#0, 1, "S";
     |FINSI;

     |SI nModo = 2;
         #floww008#0 = #flowm020#0;
         #floww008#1 = #flowm020#1;
         #floww008#2 = #flowm020#2;
         #floww008#3 = #flowm020#3;

         |C_M #floww008#0, 1, "N";
     |FINSI;

     |PINPA #0#floww008;
     |PINDA #0#floww008;
     |SI nModo = 1;  |ENDATOS #1#floww008;  |FINSI;
     |SI nModo = 2;  |ENDATOS #2#floww008;  |FINSI;
     |SI FSalida = 0;
        #flowm020#0 = #floww008#0;
        |LEE 101#flowm020.=;
        |SI FSalida ! 0;
            |DDEFECTO #flowm020;
            #flowm020#0 = #floww008#0;
            |GRABA 020#flowm020.b;
        |FINSI;
        #flowm020#1 = #floww008#1;
        #flowm020#2 = #floww008#2;
        #flowm020#3 = #floww008#3;
        |GRABA 020#flowm020.a;
        |LIBERA #flowm020;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentTra;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVentTra;
|FPRC;

|PROCESO BtnAlta;
     nModo = 1;
     |HAZ PantaFanta;
     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO BtnModi;
     nModo = 2;
     |HAZ PantaFanta;
     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO BtnBaja;
     |LEE 000#flowm020.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aMensaje = "0000NDesea Borrar el usuario " + #flowm020#0;  |QBF aMensaje;
     aMensaje = aMensaje + "?";
     |CONFI aMensaje;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |LEE 101#flowm020.=;
     |SI FSalida = 0;
         |BORRA 020#flowm020.a;
         |LIBERA #flowm020;
     |FINSI;

     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO Evento;
     |SI FSalida = 1;  |O FSalida = 2;
          |LEE 000#flowm020.=;

          |ESTADO_CONTROL nBtnModi, "ENABLE";
          |ESTADO_CONTROL nBtnBaja, "ENABLE";
     |FINSI;

     |SI FSalida = 4;
          |HAZ BtnModi;
     |FINSI;
|FPRC;

|PROGRAMA;
     |VENTANA 0501, 2799, -1, 17, "Configuraci¢n de administradores";
     nVentana = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |CABEZA "Configuraci¢n de administradores";

     |ABRE #flowm020;

     |CONTROL_SIMPLE 0, "BOTON,{{193}}Alta ", 2744, 2754, BtnAlta;
     nBtnAlta = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Modificar ", 2756, 2766, BtnModi;
     nBtnModi = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{199}}Baja ", 2768, 2778, BtnBaja;
     nBtnBaja = FSalida;

     |ESTADO_CONTROL nBtnModi, "DISABLE";
     |ESTADO_CONTROL nBtnBaja, "DISABLE";

     nRango = 4 + 8 + 16 + 32;

     |LINEAL_SIMPLE #1#flowm020, nRango, 0622, 2678, NULL, NULL, Evento;
     nGrid = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
            FSalida = "::{{001}}Salir," + nGrid;
            |LEETECLA aTecla;
            |SI aTecla = aEsc1; |SAL;  |FINSI;
            |SI aTecla = aEsc2; |SAL;  |FINSI;
            |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nGrid;

     |DESTRUYECONTROL nBtnAlta;
     |DESTRUYECONTROL nBtnBaja;
     |DESTRUYECONTROL nBtnModi;

     |CIERRA #flowm020;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVentana;
|FPRO;
