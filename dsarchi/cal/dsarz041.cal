|FICHEROS;
     dsarm001 #1;

     dsarz041 #41;

     dsarw072 #972,MANTE;

     dsarw073 #973;

     dsarm041;       ||Tipos Doc. dsEscan

     referen@ #999;
|FIN;

|VARIABLES;
     aNulo = "";
     nTipoDocTempo = 0;
     nVoy = 0;
     aDef = "";
     aProcesoPI = "";

     &eaUnidad = "";

     aAbre          = "";
     aBase          = "";
     aAlfa          = "";
     aEsc1          = "";
     aEsc2          = "";
     aRetu          = "";
     aTecla         = "";
     aFichero       = "";
     aCarga         = "";
     aQueQuiero     = "";
     aTexto         = "";
     aLong          = "";
     aCarac         = "";
     aRefresca      = "";
     aBusca         = "";

     nBotonLabel    = 0;
     nHandle        = 0;
     nTree          = -1;
     nPanta         = 0;
     nLong          = 0;
     nCargar        = 0;
     nPos           = 0;
     nPosi          = 0;
     nBoton1        = 0;
     nBoton2        = 0;
     nBoton3        = 0;
     nSubVent       = 0;
     nCampo         = 0;
     nTCampo        = 0;
     nCarpeta0      = 0;
     nCarpeta1      = 0;
     nCarpeta2      = 0;
     nCarpeta3      = 0;
     nCarpeta4      = 0;
     nCarpeta5      = 0;
     nCarpeta6      = 0;
     FEstado        = 0;

     {-1}sTree      = 0;
     sT_nID         = 0;
     sT_nOper       = 0;
     sT_aData       = "";

     {-1}aVent      = "";
         aVent1     = 0;
         aVent2     = 0;
         aVent3     = 0;
         aVent4     = 0;
         aVent5     = "";

     &eaRutaOrigen  = "";
     &eaRutaDestino = "";
     &eaFicheroTxt  = "";
     &eaFicheroTgz  = "";
     &eaFicheroTar  = "";
     &eaAlfa        = "";
     &eaUnidadBasico = "";
     &eaCodVista     = "";
|FIN;

|PROCESO Tipo0C28w072;
     |SI nTipoDocTempo ! #dsarw072#28;
          nTipoDocTempo = #dsarw072#28;
          |HAZ CargaTemporal;
     |FINSI;
|FPRC;

|PROCESO CargaTemporal;
     aFichero = ("73" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #973 aFichero;

     |ABRE #973;  |CIERRA #973;  |DELINDEX #973;

     |ABRE #973;

     nVoy = 0;
     ||Primero los *custom*

     nVoy = nVoy + 1;
     |DDEFECTO #973;
     #973#0 = nVoy;
     #973#1 = "Estado Documento";
     #973#2 = "*custom*";
     #973#3 = 0;
     |GRABA 020#973n;
     |LIBERA #973;

     nVoy = nVoy + 1;
     |DDEFECTO #973;
     #973#0 = nVoy;
     #973#1 = "Emp.Periodo Contable";
     #973#2 = "*custom*";
     #973#3 = 0;
     |GRABA 020#973n;
     |LIBERA #973;

     nVoy = nVoy + 1;
     |DDEFECTO #973;
     #973#0 = nVoy;
     #973#1 = "Plantilla OCR";
     #973#2 = "*custom*";
     #973#3 = 0;
     |GRABA 020#973n;
     |LIBERA #973;

     ||Despues los campos del dsarm034
     ||Por ultimo los del def asociado al Proceso PI.

     |DBASE aBase;  |QBF aBase;

     aCarga    = aBase + "def/dsarm034.mas";  |QBF aCarga;
     |CARGA_DEF aCarga, referen@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aQueQuiero = "|NC";
     aAlfa      = #999#aQueQuiero#;
     nTCampo    = aAlfa;
     nTCampo    = nTCampo - 1;

     ||ARA55

     |PARA nCampo = 0;  |SI nCampo [ nTCampo;  |HACIENDO nCampo = nCampo + 1;
          |SI nCampo ! 3; |Y nCampo ! 7; |Y nCampo ! 18; |Y nCampo ! 19;
                aQueQuiero = "|C" + (("000" + nCampo) % -103) + "NOMBRE";
                aFichero   = (#999#aQueQuiero# + (" " * 20)) % 120;
                aNulo = aFichero;
                |QBF aNulo;
                |SI aNulo ! "~~";
                     nVoy = nVoy + 1;
                     #973#0 = nVoy;
                     #973#1 = aFichero;
                     #973#2 = "dsarm034";
                     #973#3 = nCampo;

                     |GRABA 020#973n;
                     |LIBERA #973;
                |FINSI;
           |FINSI;
     |SIGUE;
     |DESCARGA_DEF #referen@;

     |SI nTipoDocTempo ! 0;
          |ABRE #dsarm041;
          #dsarm041#0 = nTipoDocTempo;
          |LEE 000#dsarm041.=;
          |SI FSalida = 0;
               aDef = #dsarm041#3;
               |QBF aDef;
          |FINSI;
          |CIERRA #dsarm041;
     |SINO;
          aDef = "";
     |FINSI;

     |SI aDef ! "";
          aCarga    = aBase + "def/" + aDef;  |QBF aCarga;
          |CARGA_DEF aCarga, referen@;
          |SI FSalida ! 0;  |ACABA;  |FINSI;

          aQueQuiero = "|NC";
          aAlfa      = #999#aQueQuiero#;
          nTCampo    = aAlfa;
          nTCampo    = nTCampo - 1;

          |PARA nCampo = 0;  |SI nCampo [ nTCampo;  |HACIENDO nCampo = nCampo + 1;
               |SI nCampo ! 29; |Y nCampo ! 44; |Y nCampo ! 45; |Y nCampo ! 46; |Y nCampo ! 47; |Y nCampo ! 48; |Y nCampo ! 49; |Y nCampo ! 50;
                     nVoy = nVoy + 1;
                     aQueQuiero = "|C" + (("000" + nCampo) % -103) + "NOMBRE";
                     aFichero   = (#999#aQueQuiero# + (" " * 20)) % 120;
                     aNulo = aFichero;
                     |QBF aNulo;
                     |SI aNulo ! "~~";
                          nVoy = nVoy + 1;
                          #973#0 = nVoy;
                          #973#1 = aFichero;
                          #973#2 = aDef;
                          #973#3 = nCampo;

                          |GRABA 020#973n;
                          |LIBERA #973;
                     |FINSI;
                |FINSI;
          |SIGUE;
          |DESCARGA_DEF #referen@;
     |FINSI;

     |CIERRA #973;
|FPRC;

|PROCESO NuevaVista;
     |VENTANA 0501, 2499, -1, 16, "Nueva vista";
     nSubVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |DDEFECTO #972;
     |PINPA #0#972;
     |PINDA #0#972;

     |ENDATOS #1#972;
     FEstado = FSalida;
     |SI FSalida = 0;
         |PARA nCampo = 0;  |SI nCampo [ 40;  |HACIENDO nCampo = nCampo + 1;
               #41nCampo = #972nCampo;
         |SIGUE;
         |GRABA 020#41n;
         |LIBERA #41;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nSubVent;

     |SI FEstado = 0;
         aBusca = #972#0;
         |HAZ MontaArbol;
     |FINSI;
|FPRC;

|PROCESO ModificarVista;
     #41#0 = aTexto;
     |LEE 101#41=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |BORRA 020#41a;
     |LIBERA #41;

     |VENTANA 0501, 2499, -1, 16, "Modificar vista";
     nSubVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     eaCodVista = "";

     |PARA nCampo = 0;  |SI nCampo [ 40;  |HACIENDO nCampo = nCampo + 1;
           #972nCampo = #41nCampo;
     |SIGUE;

     |PINPA #0#972;
     |PINDA #0#972;

     |ENDATOS #1#972;
     |SI FSalida = 0;
         |PARA nCampo = 0;  |SI nCampo [ 40;  |HACIENDO nCampo = nCampo + 1;
               #41nCampo = #972nCampo;
         |SIGUE;
     |FINSI;

     |GRABA 020#41n;
     |LIBERA #41;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nSubVent;

     aBusca = #972#0;
     |HAZ MontaArbol;
|FPRC;

|PROCESO BorrarVista;
     #41#0 = aTexto;
     |LEE 101#41=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |CONFI "0000NSeguro que quiere borrar la vista ";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |BORRA 020#41a;
     |LIBERA #41;

     eaCodVista = "";

     |LEE 000#41a;
     |SI FSalida ! 0;
         |LEE 000#41u;
     |FINSI;

     aBusca = #41#0;
     |HAZ MontaArbol;
|FPRC;

|PROCESO EventoTree;
     |SI FEntrada ! 0;  |ACABA;  |FINSI;

     aAlfa       = Contenido;  |QBF aAlfa;
     aTexto      = "";
     aLong       = aAlfa % 0;
     nLong       = aLong;
     nCargar     = 0;
     |PARA nPosi = 1;  |SI nPosi [ nLong;  |HACIENDO nPosi = nPosi + 1;
           nPos = (nPosi * 100) + 1;
           aCarac = aAlfa % nPos;

           |SI nCargar = 1;
               aTexto = aTexto + aCarac;
           |FINSI;

           |SI aCarac = ":";  nCargar = 1; |FINSI;
     |SIGUE;

     |ESTADO_CONTROL nBoton2, "ENABLE";
     |ESTADO_CONTROL nBoton3, "ENABLE";

     |SI aTexto = "";
         |ESTADO_CONTROL nBoton2, "DISABLE";
         |ESTADO_CONTROL nBoton3, "DISABLE";
     |FINSI;
|FPRC;

|PROCESO CargaArbol;
     |QBF aAlfa;
     aAlfa = aAlfa + &13 + &10;
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO Carpeta1;
     nCarpeta1 = nCarpeta1 + 1;
     aAlfa = nCarpeta0 + "," + nCarpeta1;
     aAlfa = aAlfa + ":(" + (("000" + #41#2) % -103) + ") " + #41#3;
     |HAZ CargaArbol;
|FPRC;

|PROCESO Carpeta2;
     nCarpeta2 = nCarpeta2 + 1;
     aAlfa = nCarpeta0 + "," + nCarpeta1 + "," + nCarpeta2;
     aAlfa = aAlfa + ":(" + (("000" + #41#4) % -103) + ") " + #41#5;
     |HAZ CargaArbol;
|FPRC;

|PROCESO Carpeta3;
     nCarpeta3 = nCarpeta3 + 1;
     aAlfa = nCarpeta0 + "," + nCarpeta1 + "," + nCarpeta2 + "," + nCarpeta3;
     aAlfa = aAlfa + ":(" + (("000" + #41#6) % -103) + ") " + #41#7;
     |HAZ CargaArbol;
|FPRC;

|PROCESO Carpeta4;
     nCarpeta4 = nCarpeta4 + 1;
     aAlfa = nCarpeta0 + "," + nCarpeta1 + "," + nCarpeta2 + "," + nCarpeta3 + "," + nCarpeta4;
     aAlfa = aAlfa + ":(" + (("000" + #41#8) % -103) + ") " + #41#9;
     |HAZ CargaArbol;
|FPRC;

|PROCESO Carpeta5;
     nCarpeta5 = nCarpeta5 + 1;
     aAlfa = nCarpeta0 + "," + nCarpeta1 + "," + nCarpeta2 + "," + nCarpeta3 + "," + nCarpeta4 + "," + nCarpeta5;
     aAlfa = aAlfa + ":(" + (("000" + #41#10) % -103) + ") " + #41#11;
     |HAZ CargaArbol;
|FPRC;

|PROCESO dsarz041;
     nCarpeta0 = nCarpeta0  + 1;
     nCarpeta1 = 0;
     nCarpeta2 = 0;
     nCarpeta3 = 0;
     nCarpeta4 = 0;
     nCarpeta5 = 0;

     aAlfa = nCarpeta0 + ":" + #41#0;  |QBF aAlfa;
     aAlfa = aAlfa + " {{" + #41#0 + "}}";
     |HAZ CargaArbol;

     |SI #41#1 = "1";
         |HAZ Carpeta1;

         nCarpeta2 = nCarpeta2 + 1;
         aAlfa = nCarpeta0 + "," + nCarpeta1 + "," + nCarpeta2;
         aAlfa = aAlfa + ":AQUI LOS DOCUMENTOS";
         |HAZ CargaArbol;
     |FINSI;

     |SI #41#1 = "2";
         |HAZ Carpeta1;
         |HAZ Carpeta2;

         nCarpeta3 = nCarpeta3 + 1;
         aAlfa = nCarpeta0 + "," + nCarpeta1 + "," + nCarpeta2 + "," + nCarpeta3;
         aAlfa = aAlfa + ":AQUI LOS DOCUMENTOS";
         |HAZ CargaArbol;
     |FINSI;

     |SI #41#1 = "3";
         |HAZ Carpeta1;
         |HAZ Carpeta2;
         |HAZ Carpeta3;

         nCarpeta4 = nCarpeta4 + 1;
         aAlfa = nCarpeta0 + "," + nCarpeta1 + "," + nCarpeta2 + "," + nCarpeta3 + "," + nCarpeta4;
         aAlfa = aAlfa + ":AQUI LOS DOCUMENTOS";
         |HAZ CargaArbol;
     |FINSI;

     |SI #41#1 = "4";
         |HAZ Carpeta1;
         |HAZ Carpeta2;
         |HAZ Carpeta3;
         |HAZ Carpeta4;

         nCarpeta5 = nCarpeta5 + 1;
         aAlfa = nCarpeta0 + "," + nCarpeta1 + "," + nCarpeta2 + "," + nCarpeta3 + "," + nCarpeta4 + "," + nCarpeta5;
         aAlfa = aAlfa + ":AQUI LOS DOCUMENTOS";
         |HAZ CargaArbol;
     |FINSI;

     |SI #41#1 = "5";
         |HAZ Carpeta1;
         |HAZ Carpeta2;
         |HAZ Carpeta3;
         |HAZ Carpeta4;
         |HAZ Carpeta5;

         nCarpeta6 = nCarpeta6 + 1;
         aAlfa = nCarpeta0 + "," + nCarpeta1 + "," + nCarpeta2 + "," + nCarpeta3 + "," + nCarpeta4 + "," + nCarpeta5 + "," + nCarpeta6;
         aAlfa = aAlfa + ":AQUI LOS DOCUMENTOS";
         |HAZ CargaArbol;
     |FINSI;
|FPRC;

|DEFBUCLE dsarz041;
     #41#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dsarz041;
|FIN;

|PROCESO MontaArbol;
     |SI nTree ! -1;
         |FIN_CONTROL_WINDOWS nTree;
         nTree = -1;
     |FINSI;

     |CONTROL_SIMPLE 0, "LABEL,{{5}}Cargando informaci¢n ...", 1315, 1680, NULL;
     nBotonLabel = FSalida;

     |PTEC 802;  |PAUSA;

     |DBASE aBase;  |QBF aBase;
     aAlfa = aBase + "tmp";          |MKDIR aAlfa;

     eaRutaOrigen  = aBase + "tmp/";

     |SI #1#7 = "N";
          |HAZ DimeUnidad;
          eaRutaDestino = eaUnidad + ":\documentos\dsarchi\";
     |SINO;
          |HAZ LeeUnidadBasico;
          eaRutaDestino = eaUnidadBasico + "\documentos\dsarchi\";
     |FINSI;

     eaFicheroTxt  = "dsarz04101" + Usuario + ".txt";
     eaFicheroTgz  = "dsarz04101" + Usuario + ".tgz";
     eaFicheroTar  = "dsarz04101" + Usuario + ".tar";

     aAbre = eaRutaOrigen + eaFicheroTxt;

     sT_nID   = nTree;
     sT_nOper = 0;

     |XABRE "WB", aAbre, nHandle;

     nCarpeta0 = 0;
     nCarpeta1 = 0;
     nCarpeta2 = 0;
     nCarpeta3 = 0;
     nCarpeta4 = 0;
     nCarpeta5 = 0;
     nCarpeta6 = 0;

     |BUCLE dsarz041;

     |XCIERRA nHandle;

     |HAZ TraeTxtComprimido;
     aAlfa = eaRutaDestino + eaFicheroTxt;

     eaAlfa = "TREECTRL,Vistas Arbol dsEscan {{" + aAlfa + "}}";
     |CONTROL_SIMPLE nPanta, eaAlfa, 0602, 2598, EventoTree;
     nTree = FSalida;

     eaFicheroTxt  = "dsarz04101" + Usuario + ".txt";
     aAlfa         = eaRutaDestino + eaFicheroTxt;
     |RFBORRA aAlfa;

     |FIN_CONTROL_WINDOWS nBotonLabel;

     |SI aBusca ! "";
         sT_nID   = nTree;
         sT_nOper = 3;
         sT_aData = "[" + aBusca;
         |ESPECIFICA "COMMANDCONTROL", sTree;
         aRefresca = ":" + FSalida;
     |FINSI;

     sT_nID   = nTree;
     sT_nOper = 2;
     |SI aRefresca = "";
         sT_aData = "1:";
     |SINO;
         sT_aData = aRefresca;
     |FINSI;

     |ESPECIFICA "COMMANDCONTROL", sTree;
     |PULSATECLA;

     |FOCOTECLADO nTree;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     FSalida = 2799;
|FPRC;

|PROGRAMA;
     |ABRE #1;
     |LEE 000#1p;
     |SI FSalida ! 0;  |DDEFECTO #1;  |FINSI;
     |CIERRA #1;

     |ABRET #41;

     |CLS;
     |CABEZA "Vistas Arbol dsEscan";

     |PINPA #0#41;
     nPanta = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}Nueva vista", 2602, 2615, NuevaVista;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Modificar vista", 2617, 2630, ModificarVista;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{199}}Borrar vista", 2632, 2645, BorrarVista;
     nBoton3 = FSalida;

     ||He cambiado la carga del temporal de campos
     nTipoDocTempo = 1;          ||Por defecto son facturas recibidas
     |HAZ CargaTemporal;
     |HAZ MontaArbol;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}Salir," + nTree;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nTree;
     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;

     |CIERRAT #41;

     |ABRE #973;  |CIERRA #973;  |DELINDEX #973;
|FPRO;
