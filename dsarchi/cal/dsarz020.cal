|FICHEROS;
     dsarm001;
     dsarm002;
     dsarm003;
     dsarm004;
     dsarm005;
     dpntm003;

     dsarw002,MANTE;
     dsarw003,MANTE;
     dsarw004;
|FIN;

|VARIABLES;
     aAlfa          = "";
     aAlfa1         = "";
     aEsc1          = "";
     aEsc2          = "";
     aRetu          = "";
     aTecla         = "";
     aFichero       = "";
     aBase          = "";
     aMas           = "";
     aAbre          = "";
     aContenido1    = "";
     aContenido2    = "";
     aBusca1        = "";
     aBusca2        = "";
     aRefresca1     = "";
     aRefresca2     = "";
     aLong          = "";
     aTexto1        = "";
     aTexto2        = "";
     aTex1          = "";
     aTex2          = "";
     aCarac         = "";
     aIDE1_1        = "";
     aIDE1_2        = "";
     aIDE1_3        = "";
     aIDE2_1        = "";
     aIDE2_2        = "";
     aIDE2_3        = "";
     aMensa         = "";
     aAccion        = "";
     aPath          = "";
     aUbicacion     = "";
     aFechaSis      = "";
     aAAAAMM        = "";
     aOrigen        = "";
     aDestino       = "";
     aPathRemoto    = "";
     aDirectorio    = "";
     aDirec         = "";

     nSubVent       = 0;
     nVent          = 0;
     nVentTrab      = 0;
     nTree1         = 0;
     nTree2         = 0;
     nRango         = 0;
     nHandle        = 0;
     nLong          = 0;
     nCargar        = 0;
     nPosi          = 0;
     nPos           = 0;
     nOpcion1       = 0;
     nOpcion2       = 0;
     nCarpeta1      = 0;
     nCarpeta2      = 0;
     nCarpeta3      = 0;
     nAccion        = 0;
     nGrupoOri      = 0;
     nGrupoDes      = 0;
     nSubGrupoDes   = 0;
     nDSubGrupo     = 0;
     nHSubGrupo     = 0;
     nDTipo         = 0;
     nHTipo         = 0;
     nSubGrupo      = 0;
     nCampoD        = 0;
     nCampoH        = 0;
     nTipo          = 0;
     nUltimo        = 0;
     nBtnCopiar     = -1;
     nLblCopiar1    = -1;
     nLblCopiar2    = -1;
     nBtnMover      = -1;
     nLblMover1     = -1;
     nLblMover2     = -1;

     fFechaSis      = @;

     {-1}sTree      = 0;
     sT_nID         = 0;
     sT_nOper       = 0;
     sT_aData       = "";

     {-1}aVent      = "";
         aVent1     = 0;
         aVent2     = 0;
         aVent3     = 0;
         aVent4     = 0;
         aVent5     = "";

     &eaRutaOrigen   = "";
     &eaRutaDestino  = "";
     &eaUnidad       = "";
     &eaUnidadBasico = "";
     &eaFicheroTxt   = "";
     &eaFicheroTgz   = "";
     &eaAlfa         = "";
     &eaFicheroTar   = "";

     &enModo         = 0;
     &eaCarpetaOri   = "";
     &enSwMueve      = 0;
|FIN;

|PROCESO dsarm004_clona;
     #dsarm004#0 = nGrupoDes;
     #dsarm004#1 = nSubGrupoDes;
     |GRABA 020#dsarm004.n;
     |LIBERA #dsarm004;
|FPRC;

|DEFBUCLE dsarm004_clona;
     #dsarm004#1;
     ;
     #dsarm003#0, #dsarm003#1, 001;
     #dsarm003#0, #dsarm003#1, 999;
     be;
     NULL, dsarm004_clona;
|FIN;

|PROCESO dsarm003_clona;
     nSubGrupoDes = #dsarm003#1;
     |BUCLE dsarm004_clona;

     #dsarm003#0 = nGrupoDes;
     |GRABA 020#dsarm003.n;
     |LIBERA #dsarm003;
|FPRC;

|DEFBUCLE dsarm003_clona;
     #dsarm003#1;
     ;
     nGrupoOri, 001;
     nGrupoOri, 999;
     be;
     NULL, dsarm003_clona;
|FIN;

|PROCESO ClonaGrupo;
     nGrupoOri = aIDE1_1;

     #dsarm002#0 = aIDE1_1;
     |LEE 000#dsarm002.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     #dsarm002#0 = nGrupoDes;
     #dsarm002#1 = #dsarw002#1;
     #dsarm002#2 = #dsarw002#2;
     #dsarm002#3 = 0;
     #dsarm002#4 = #dsarw002#4;
     |GRABA 020#dsarm002.n;
     |LIBERA #dsarm002;

     |BUCLE dsarm003_clona;
|FPRC;

|PROCESO ClonaUnTipo;
     nGrupoOri = aIDE1_1;

     |DDEFECTO #dsarm004;
     #dsarm004#0 = nGrupoOri;
     #dsarm004#1 = 1;
     #dsarm004#2 = 1;
     |LEE 000#dsarm004.=;
     |SI FSalida = 0;
          #dsarm004#0 = nGrupoDes;
          #dsarm004#1 = 1;
          #dsarm004#2 = 1;
          |GRABA 020#dsarm004.n;
          |LIBERA #dsarm004;
     |FINSI;
|FPRC;

|PROCESO SacaUbicacion;
     aPath = #dsarm001#1;  |QBF aPath;

     #dsarm002#0 = #dsarm005#1;
     |LEE 000#dsarm002.=;
     |SI FSalida ! 0;  |DDEFECTO #dsarm002;  |FINSI;
     |CIERRA #dsarm002;

     aPath = aPath + "/" + #dsarm002#2;  |QBF aPath;
     |SI #dsarm001#7 = "N";
         |MKDIR aPath;
     |SINO;
         |RMKDIR aPath;
     |FINSI;

     |SI #dsarm001#8 = "N";
         aPath = aPath + "/";
         aUbicacion = aPath;

         #dsarm004#0 = #dsarm005#1;
         #dsarm004#1 = 1;
         #dsarm004#2 = 1;
         |LEE 000#dsarm004.=;
         |SI FSalida ! 0;  |DDEFECTO #dsarm004;  |FINSI;
         |CIERRA #dsarm004;

         |SI #dsarm004#75 = "S";
              fFechaSis = ~;
              aFechaSis = fFechaSis;
              aAAAAMM   = (aFechaSis % -104) + (aFechaSis % 402);
              aUbicacion = aUbicacion + aAAAAMM + "/";
              |SI #dsarm001#7 = "N";
                   |MKDIR aUbicacion;
              |SINO;
                   |RMKDIR aUbicacion;
              |FINSI;
         |FINSI;

         |ACABA;
     |FINSI;

     #dsarm003#0 = #dsarm005#1;
     #dsarm003#1 = #dsarm005#2;
     |LEE 000#dsarm003.=;
     |SI FSalida ! 0;  |DDEFECTO #dsarm003;  |FINSI;

     aPath = aPath + "/" + #dsarm003#3;  |QBF aPath;
     |SI #dsarm001#7 = "N";
         |MKDIR aPath;
     |SINO;
         |RMKDIR aPath;
     |FINSI;

     #dsarm004#0 = #dsarm005#1;
     #dsarm004#1 = #dsarm005#2;
     #dsarm004#2 = #dsarm005#3;
     |LEE 000#dsarm004.=;
     |SI FSalida ! 0;  |DDEFECTO #dsarm004;  |FINSI;

     aPath = aPath + "/" + #dsarm004#4;  |QBF aPath;
     |SI #dsarm001#7 = "N";
         |MKDIR aPath;
     |SINO;
         |RMKDIR aPath;
     |FINSI;

     aPath = aPath + "/";
     aUbicacion = aPath;

     |SI #dsarm004#75 = "S";
          fFechaSis  = ~;
          aFechaSis  = fFechaSis;
          aAAAAMM    = (aFechaSis % -104) + (aFechaSis % 402);
          aUbicacion = aUbicacion + aAAAAMM + "/";
          |SI #dsarm001#7 = "N";
               |MKDIR aUbicacion;
          |SINO;
               |RMKDIR aUbicacion;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO dsarm005_mueve;
     aFichero     = #dsarm005#7;   |QBF aFichero;
     aFichero     = aFichero + #dsarm005#9;
     |QBF aFichero;
     aOrigen  = aFichero;

                         #dsarm005#1 = nGrupoDes;
     |SI nSubGrupo ! 0;  #dsarm005#2 = nSubGrupo;   |FINSI;
     |SI nTipo     ! 0;  #dsarm005#3 = nTipo;       |FINSI;

     aUbicacion   = "";
     |HAZ SacaUbicacion;
     aPathRemoto  = aUbicacion;

     aDestino = aPathRemoto + #dsarm005#9;
     |QBF aDestino;

     |SI #dsarm001#7 = "N";
         |RENOMBRA_FICHERO aOrigen, aDestino;
     |SINO;
         |RRENOMBRA_FICHERO aOrigen, aDestino;
     |FINSI;

     #dsarm005#7 = aPathRemoto;

     |GRABA 020#dsarm005.a;
     |LIBERA #dsarm005;
|FPRC;

|DEFBUCLE dsarm005_mueve;
     #dsarm005#1;
     1, 2, 3;
     000000001, nGrupoOri, nDSubGrupo, nDTipo;
     999999999, nGrupoOri, nHSubGrupo, nHTipo;
     be;
     NULL, dsarm005_mueve;
|FIN;

|PROCESO dpntm003_mueve;
                         #dpntm003#1 = nGrupoDes;
     |SI nSubGrupo ! 0;  #dpntm003#2 = nSubGrupo;   |FINSI;
     |SI nTipo     ! 0;  #dpntm003#3 = nTipo;       |FINSI;

     |GRABA 120#dpntm003.n;
     |LIBERA #dpntm003;
|FPRC;

|DEFBUCLE dpntm003_mueve;
     #dpntm003#1;
     ;
     00001, nGrupoOri, nDSubGrupo, nDTipo;
     99999, nGrupoOri, nHSubGrupo, nHTipo;
     be;
     NULL, dpntm003_mueve;
|FIN;

|PROCESO dsarm004B;
     |BORRA 020#dsarm004.a;
     |LIBERA #dsarm004;
|FPRC;

|DEFBUCLE dsarm004B;
     #dsarm004#1;
     ;
     #dsarm003#0, #dsarm003#1, nDTipo;
     #dsarm003#0, #dsarm003#1, nHTipo;
     be;
     NULL, dsarm004B;
|FIN;

|PROCESO dsarm003B;
     |BUCLE dsarm004B;

     |BORRA 020#dsarm003.a;
     |LIBERA #dsarm003;
|FPRC;

|DEFBUCLE dsarm003B;
     #dsarm003#1;
     ;
     #dsarm002#0, nDSubGrupo;
     #dsarm002#0, nHSubGrupo;
     be;
     NULL, dsarm003B;
|FIN;

|PROCESO BorraGrupo;
     #dsarm002#0  =  aIDE1_1;
     |LEE 101#dsarm002.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aAlfa = #dsarm002#2;  |QBF aAlfa;
     |SI aAlfa = "";
         aMensa = "0000Error: La Ubicacion de los Documentos esta vacia. [Grupo]";
         |MENAV aMensa;
         |LIBERA #dsarm002;
         |ACABA;
     |FINSI;

     nDSubGrupo = 000;
     nHSubGrupo = 999;
     nDTipo     = 000;
     nHTipo     = 999;

     |BUCLE dsarm003B;

     |BORRA 020#dsarm002.a;
     |LIBERA #dsarm002;
|FPRC;

|PROCESO ElGrupo;
     enModo = 1;

     aAlfa  =  aAccion + " estructura a nuevo Grupo";
     |VENTANA 0501, 1263, -1, 16, aAlfa;
     nSubVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |SI nAccion = 2;
          #dsarm002#0 = aIDE1_1;
          |LEE 000#dsarm002.=;
          |SI FSalida = 0;
              eaCarpetaOri = #dsarm002#2;
              |QBF eaCarpetaOri;
          |FINSI;
     |FINSI;

     |C_M #dsarw002#0, 1, "S";

     |DDEFECTO #dsarw002;
     |PINPA #0#dsarw002;
     |PINDA #0#dsarw002;
     |ENDATOS #1#dsarw002;
     |SI FSalida = 0;
         aVent1 = -1;
         aVent2 = -1;
         aVent3 = 0;
         aVent4 = nSubVent;
         aVent5 = "MODELESS";
         |ESPECIFICA "ESTADO_VENTANA", aVent;

         |FINVENTANA nSubVent;

         |VENTANA 0501, 0540, -1, 16, "Procesando trabajo";
         nSubVent = FSalida;

         aVent1 = -1;
         aVent2 = -1;
         aVent3 = 0;
         aVent4 = nSubVent;
         aVent5 = "MODAL";
         |ESPECIFICA "ESTADO_VENTANA", aVent;

         |PTEC 802;  |PAUSA;

         nGrupoOri = aIDE1_1;
         nGrupoDes = #dsarw002#0;

         |SI #dsarm001#8 = "S";
             |HAZ ClonaGrupo;
         |SINO;
             #dsarm002#0 = aIDE1_1;
             |LEE 000#dsarm002.=;
             |SI FSalida = 0;
                 #dsarm002#0 = nGrupoDes;
                 #dsarm002#1 = #dsarw002#1;
                 #dsarm002#2 = #dsarw002#2;
                 #dsarm002#3 = 0;
                 #dsarm002#4 = #dsarw002#4;
                 |GRABA 020#dsarm002.n;
                 |LIBERA #dsarm002;

                 |HAZ ClonaUnTipo;
              |FINSI;
         |FINSI;

         |SI nAccion = 2;
             nDSubGrupo = 0;
             nHSubGrupo = 999;
             nDTipo     = 0;
             nHTipo     = 999;
             nSubGrupo  = 0;
             nTipo      = 0;

             |BUCLE dsarm005_mueve;
             |BUCLE dpntm003_mueve;

             |HAZ BorraGrupo;

             aBusca1 = "GRUPO" + (("000" + nGrupoDes) % -103);
             aBusca2 = "GRUPO" + (("000" + nGrupoDes) % -103);
         |FINSI;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nSubVent;
|FPRC;

|PROCESO ElSubGrupo;
     enModo = 1;

     aAlfa  =  aAccion + " estructura a nuevo Subgrupo";
     |VENTANA 0501, 1863, -1, 16, aAlfa;
     nSubVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     #dsarm002#0 = aIDE2_1;
     |LEE 000#dsarm002.=;
     |SI FSalida ! 0;  |DDEFECTO #dsarm002;  |FINSI;

     #dsarm003#0 = aIDE1_1;
     #dsarm003#1 = aIDE1_2;
     |LEE 000#dsarm003.=;
     |SI FSalida ! 0;  |DDEFECTO #dsarm003;  |FINSI;

     |DDEFECTO #dsarw003;

     nGrupoDes   = #dsarm002#0;

     #dsarw003#0 = #dsarm002#0;
     #dsarw003#1 = #dsarm002#1;
     #dsarw003#2 = #dsarm002#2;
     #dsarw003#4 = #dsarm003#2;
     #dsarw003#5 = #dsarm003#3;
     #dsarw003#6 = #dsarm003#5;

     #dsarm003#0 = nGrupoDes;
     #dsarm003#1 = 999;
     |LEE 000#dsarm003.];
     |SI FSalida = 0;
         |LEE 000#dsarm003.a;
     |SINO;
         |LEE 000#dsarm003.u;
     |FINSI;

     nUltimo = 1;
     |SI FSalida = 0; |Y #dsarm003#0 = nGrupoDes;
         nUltimo = #dsarm003#1 + 1;
     |FINSI;

     #dsarw003#3 = nUltimo;

     |SI aIDE1_1 = aIDE2_1;
         #dsarw003#4 = "";
         #dsarw003#5 = "";
     |FINSI;

     |PINDA #0#dsarm003;

     |ENDATOS #1#dsarw003;
     |SI FSalida = 0;
         aVent1 = -1;
         aVent2 = -1;
         aVent3 = 0;
         aVent4 = nSubVent;
         aVent5 = "MODELESS";
         |ESPECIFICA "ESTADO_VENTANA", aVent;

         |FINVENTANA nSubVent;

         |VENTANA 0501, 0540, -1, 16, "Procesando trabajo";
         nSubVent = FSalida;

         aVent1 = -1;
         aVent2 = -1;
         aVent3 = 0;
         aVent4 = nSubVent;
         aVent5 = "MODAL";
         |ESPECIFICA "ESTADO_VENTANA", aVent;

         |PTEC 802;  |PAUSA;

         #dsarm003#0 = nGrupoDes;
         #dsarm003#1 = nUltimo;
         |LEE 101#dsarm003.=;
         |SI FSalida ! 0;
             |DDEFECTO #dsarm003;
             #dsarm003#0 = nGrupoDes;
             #dsarm003#1 = nUltimo;
             |GRABA 020#dsarm003.b;
         |FINSI;

         #dsarm003#2 = #dsarw003#4;
         #dsarm003#3 = #dsarw003#5;
         #dsarm003#4 = 0;
         #dsarm003#5 = #dsarw003#6;
         |GRABA 020#dsarm003.a;
         |LIBERA #dsarm003;

         #dsarm003#0 = aIDE1_1;
         #dsarm003#1 = aIDE1_2;
         |LEE 000#dsarm003.=;
         |SI FSalida = 0;
             nSubGrupoDes = nUltimo;
             |BUCLE dsarm004_clona;
         |FINSI;

         |SI nAccion = 2;
              nGrupoOri  = aIDE1_1;
              nDSubGrupo = aIDE1_2;
              nHSubGrupo = aIDE1_2;
              nDTipo     = 0;
              nHTipo     = 999;
              nSubGrupo  = nUltimo;
              nTipo      = 0;
              |BUCLE dsarm005_mueve;
              |BUCLE dpntm003_mueve;

              #dsarm002#0 = aIDE1_1;
              |LEE 000#dsarm002.=;
              |SI FSalida = 0;
                  |BUCLE dsarm003B;
              |FINSI;

              aBusca1 = "GRUPO" + (("000" + nGrupoOri) % -103);
              aBusca2 = "SUBGRUPO" + (("000" + nGrupoDes) % -103) + (("000" + nUltimo) % -103);
          |FINSI;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nSubVent;
|FPRC;

|PROCESO ElTipo;
     enModo = 1;

     |VENTANA 0501, 2699, -1, 16, "Realizando proceso";
     nSubVent = FSalida;

     |PTEC 802;  |PAUSA;

     |PINPA #0#dsarw004;

     #dsarm002#0 = aIDE2_1;
     |LEE 000#dsarm002.=;
     |SI FSalida ! 0;  |DDEFECTO #dsarm002;  |FINSI;

     #dsarm003#0 = aIDE2_1;
     #dsarm003#1 = aIDE2_2;
     |LEE 000#dsarm003.=;
     |SI FSalida ! 0;  |DDEFECTO #dsarm003;  |FINSI;

     #dsarm004#0 = aIDE1_1;
     #dsarm004#1 = aIDE1_2;
     #dsarm004#2 = aIDE1_3;
     |LEE 000#dsarm004.=;
     |SI FSalida ! 0;  |DDEFECTO #dsarm004;  |FINSI;

     |DDEFECTO #dsarw004;

     nGrupoDes     = #dsarm003#0;
     nSubGrupoDes  = #dsarm003#1;

     |SI #dsarm001#8 = "N";
         |C_V #dsarw004#1, 1, "S";  |PINTA #dsarw004#1;
         |C_V #dsarw004#2, 1, "S";  |PINTA #dsarw004#2;
         |C_V #dsarw004#3, 1, "N";
         |C_V #dsarw004#4, 1, "N";
         |C_V #dsarw004#5, 1, "N";
         |C_V #dsarw004#6, 1, "N";
         |C_V #dsarw004#7, 1, "N";
         |C_V #dsarw004#8, 1, "N";

         |PINTA 0701, "                      ";
         |PINTA 0801, "                      ";
         |PINTA 0762, "                      ";
         |PINTA 0862, "                      ";
     |SINO;
         |C_V #dsarw004#1, 1, "S";
         |C_V #dsarw004#2, 1, "S";
         |C_V #dsarw004#3, 1, "S";
         |C_V #dsarw004#4, 1, "S";
         |C_V #dsarw004#5, 1, "S";
         |C_V #dsarw004#6, 1, "S";
         |C_V #dsarw004#7, 1, "S";
         |C_V #dsarw004#8, 1, "S";

         #dsarw004#0 = #dsarm002#0;
         #dsarw004#1 = #dsarm002#1;
         #dsarw004#2 = #dsarm002#2;
         #dsarw004#3 = #dsarm003#1;
         #dsarw004#4 = #dsarm003#2;
         #dsarw004#5 = #dsarm003#3;
     |FINSI;

     |SI #dsarm001#25 = "N";
         |C_V #dsarw004#38, 1, "N";
         |C_V #dsarw004#39, 1, "N";
         |C_V #dsarw004#40, 1, "N";
         |C_V #dsarw004#41, 1, "N";
         |C_V #dsarw004#42, 1, "N";
         |C_V #dsarw004#43, 1, "N";
         |C_V #dsarw004#44, 1, "N";
         |C_V #dsarw004#45, 1, "N";
         |C_V #dsarw004#46, 1, "N";
         |C_V #dsarw004#47, 1, "N";
         |C_V #dsarw004#48, 1, "N";
         |C_V #dsarw004#49, 1, "N";
         |C_V #dsarw004#50, 1, "N";
         |C_V #dsarw004#51, 1, "N";
         |C_V #dsarw004#52, 1, "N";
         |C_V #dsarw004#53, 1, "N";
         |C_V #dsarw004#54, 1, "N";
         |C_V #dsarw004#55, 1, "N";
         |C_V #dsarw004#56, 1, "N";
         |C_V #dsarw004#57, 1, "N";
     |SINO;
         |C_V #dsarw004#38, 1, "S";
         |C_V #dsarw004#39, 1, "S";
         |C_V #dsarw004#40, 1, "S";
         |C_V #dsarw004#41, 1, "S";
         |C_V #dsarw004#42, 1, "S";
         |C_V #dsarw004#43, 1, "S";
         |C_V #dsarw004#44, 1, "S";
         |C_V #dsarw004#45, 1, "S";
         |C_V #dsarw004#46, 1, "S";
         |C_V #dsarw004#47, 1, "S";
         |C_V #dsarw004#48, 1, "S";
         |C_V #dsarw004#49, 1, "S";
         |C_V #dsarw004#50, 1, "S";
         |C_V #dsarw004#51, 1, "S";
         |C_V #dsarw004#52, 1, "S";
         |C_V #dsarw004#53, 1, "S";
         |C_V #dsarw004#54, 1, "S";
         |C_V #dsarw004#55, 1, "S";
         |C_V #dsarw004#56, 1, "S";
         |C_V #dsarw004#57, 1, "S";
     |FINSI;

     |SI #dsarm001#95 = "N";  |PINTA #dsarw004#24;  |FINSI;
     |SI #dsarm001#96 = "N";  |PINTA #dsarw004#25;  |FINSI;
     |SI #dsarm001#97 = "N";  |PINTA #dsarw004#26;  |FINSI;
     |SI #dsarm001#98 = "N";  |PINTA #dsarw004#27;  |FINSI;
     |SI #dsarm001#99 = "N";  |PINTA #dsarw004#28;  |FINSI;

     #dsarw004#6  = #dsarm004#2;
     #dsarw004#7  = #dsarm004#3;
     #dsarw004#8  = #dsarm004#4;
     |PARA nCampoD = 5;  |SI nCampoD [ 23;  |HACIENDO nCampoD = nCampoD + 1;
           nCampoH   = nCampoD + 4;
           #dsarw004#nCampoH# = #dsarm004#nCampoD#;
     |SIGUE;

     #dsarw004#29 = #dsarm004#26;
     #dsarw004#30 = #dsarm004#27;
     #dsarw004#31 = #dsarm004#28;
     #dsarw004#32 = #dsarm004#29;
     #dsarw004#33 = #dsarm004#30;
     #dsarw004#34 = #dsarm004#31;
     #dsarw004#35 = #dsarm004#32;
     #dsarw004#36 = #dsarm004#33;
     #dsarw004#37 = #dsarm004#34;

     |PARA nCampoD = 35;  |SI nCampoD [ 89;  |HACIENDO nCampoD = nCampoD + 1;
           nCampoH   = nCampoD + 3;
           #dsarw004#nCampoH# = #dsarm004#nCampoD#;
     |SIGUE;

     #dsarm004#0 = nGrupoDes;
     #dsarm004#1 = nSubGrupoDes;
     #dsarm004#2 = 999;
     |LEE 000#dsarm004.];
     |SI FSalida = 0;
         |LEE 000#dsarm004.a;
     |SINO;
         |LEE 000#dsarm004.u;
     |FINSI;

     nUltimo = 1;
     |SI FSalida = 0; |Y #dsarm004#0 = nGrupoDes;
                      |Y #dsarm004#1 = nSubGrupoDes;
         nUltimo = #dsarm004#2 + 1;
     |FINSI;

     #dsarw004#6 = nUltimo;

     |PINDA #0#dsarw004;

     |VENTANA 0501, 0540, -1, 16, "Realizando proceso";
     nVentTrab = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentTrab;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     #dsarm004#0 = aIDE1_1;
     #dsarm004#1 = aIDE1_2;
     #dsarm004#2 = aIDE1_3;
     |LEE 000#dsarm004.=;
     |SI FSalida = 0;
         #dsarm004#0 = nGrupoDes;
         #dsarm004#1 = nSubGrupoDes;
         #dsarm004#2 = nUltimo;
         #dsarm004#3 = #dsarw004#7;
         #dsarm004#4 = #dsarw004#8;
         |GRABA 020#dsarm004.n;
     |FINSI;

     |SI nAccion = 2;
         nGrupoOri  = aIDE1_1;
         nDSubGrupo = aIDE1_2;
         nHSubGrupo = aIDE1_2;
         nDTipo     = aIDE1_3;
         nHTipo     = aIDE1_3;

         nSubGrupo  = nDSubGrupo;
         nTipo      = nUltimo;
         |BUCLE dsarm005_mueve;
         |BUCLE dpntm003_mueve;

         #dsarm004#0 = aIDE1_1;
         #dsarm004#1 = aIDE1_2;
         #dsarm004#2 = aIDE1_3;
         |LEE 101#dsarm004.=;
         |SI FSalida = 0;
              |HAZ dsarm004B;
         |FINSI;

         aBusca1 = "SUBGRUPO" + aIDE1_1 + aIDE1_2;
         aBusca2 = "TIPO" + (("000" + nGrupoDes) % -103) + (("000" + nSubGrupoDes) % -103) + (("000" + nUltimo) % -103);
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentTrab;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVentTrab;
     |FINVENTANA nSubVent;
|FPRC;

|PROCESO Procesalo;
     |SI nOpcion1 = 1;
          aMensa = "0000NSeguro que desea " + aAccion + " la estructura del grupo " + aIDE1_1 + " a un nuevo grupo?";
          aMensa = aMensa + " Si pulsa <S¡> se proceder  a la creaci¢n del mismo.";
     |FINSI;

     |SI nOpcion1 = 2;
          aMensa = "0000NSeguro que desea " + aAccion + " la estructura del subgrupo " + aIDE1_1 + "/" + aIDE1_2;
          aMensa = aMensa + " al grupo destino seleccionado.";
          aMensa = aMensa + " Si pulsa <S¡> se proceder  a la creaci¢n del mismo.";
     |FINSI;

     |SI nOpcion1 = 3;
          aMensa = "0000NSeguro que desea " + aAccion + " la estructura del tipo " + aIDE1_1 + "/" + aIDE1_2 + "/" + aIDE1_3;
          aMensa = aMensa + " al subgrupo destino seleccionado.";
          aMensa = aMensa + " Si pulsa <S¡> se proceder  a la creaci¢n del mismo.";
     |FINSI;

     |CONFI aMensa;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI nOpcion1 = 2;
         |SI aIDE1_1 = aIDE2_1;
             aMensa = "0000NEst  intentando " + aAccion + " un subgrupo en el mismo grupo. Est  seguro.";
             |CONFI aMensa;
             |SI FSalida ! 0;  |ACABA;  |FINSI;
         |FINSI;
     |FINSI;

     |SI nOpcion1 = 1;  |HAZ ElGrupo;     |FINSI;
     |SI nOpcion1 = 2;  |HAZ ElSubGrupo;  |FINSI;
     |SI nOpcion1 = 3;  |HAZ ElTipo;      |FINSI;

     |HAZ MontaArbol1;
     |HAZ MontaArbol2;
|FPRC;

|PROCESO Copiar;
     nAccion   = 1;
     aAccion   = "COPIAR";
     enSwMueve = 0;
     |HAZ Procesalo;
|FPRC;

|PROCESO Mover;
     nAccion   = 2;
     aAccion   = "MOVER";
     enSwMueve = 1;
     |HAZ Procesalo;
|FPRC;

|PROCESO EnciendeBotones;
     |ESTADO_CONTROL nBtnCopiar, "DISABLE";
     |ESTADO_CONTROL nBtnMover, "DISABLE";

     |SI nOpcion2 = 3;
         |ACABA;
     |FINSI;

     |SI nOpcion1 = 1;
         |ESTADO_CONTROL nBtnCopiar, "ENABLE";
         |ESTADO_CONTROL nBtnMover, "ENABLE";

         |ACABA;
     |FINSI;

     |SI nOpcion1 = 2;
         |SI nOpcion2 = 1;
             |ESTADO_CONTROL nBtnCopiar, "ENABLE";
             |ESTADO_CONTROL nBtnMover, "ENABLE";
         |FINSI;

         |ACABA;
     |FINSI;

     |SI nOpcion1 = 3;
         |SI nOpcion2 = 2;
             |SI aIDE1_1 ! aIDE2_1;  |O aIDE1_2 ! aIDE2_2;
                 |ESTADO_CONTROL nBtnCopiar, "ENABLE";
                 |ESTADO_CONTROL nBtnMover, "ENABLE";
             |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO EventoTree1;
     |SI FEntrada = 0;
         aAlfa       = Contenido;  |QBF aAlfa;
         aTexto1     = "";
         aLong       = aAlfa % 0;
         nLong       = aLong;
         nCargar     = 0;
         |PARA nPosi = 1;  |SI nPosi [ nLong;  |HACIENDO nPosi = nPosi + 1;
               nPos = (nPosi * 100) + 1;
               aCarac = aAlfa % nPos;

               |SI nCargar = 1;
                   aTexto1 = aTexto1 + aCarac;
               |FINSI;

               |SI aCarac = ":";  nCargar = 1; |FINSI;
         |SIGUE;

         aIDE1_1  = "";
         aIDE1_2  = "";
         aIDE1_3  = "";

         nOpcion1 = 1;

         aAlfa = aTexto1 - "GRUPO";
         |SI aTexto1 ! aAlfa;
             nOpcion1 = 1;
             aIDE1_1  = aAlfa % 103;
         |FINSI;

         aAlfa = aTexto1 - "SUBGRUPO";
         |SI aTexto1 ! aAlfa;
             nOpcion1 = 2;
             aIDE1_1  = aAlfa % 103;
             aIDE1_2  = aAlfa % 403;
         |FINSI;

         aAlfa = aTexto1 - "TIPO";
         |SI aTexto1 ! aAlfa;
             nOpcion1 = 3;
             aIDE1_1  = aAlfa % 103;
             aIDE1_2  = aAlfa % 403;
             aIDE1_3  = aAlfa % 703;
         |FINSI;

         |SI aTexto1 = "";
             nOpcion1 = 0;
         |FINSI;
     |FINSI;

     |SI nLblCopiar1 ! -1;  |FIN_CONTROL_WINDOWS nLblCopiar1;  |FINSI;
     |SI nLblCopiar2 ! -1;  |FIN_CONTROL_WINDOWS nLblCopiar2;  |FINSI;
     |SI nBtnCopiar  ! -1;  |FIN_CONTROL_WINDOWS nBtnCopiar;   |FINSI;
     |SI nLblMover1  ! -1;  |FIN_CONTROL_WINDOWS nLblMover1;   |FINSI;
     |SI nLblMover2  ! -1;  |FIN_CONTROL_WINDOWS nLblMover2;   |FINSI;
     |SI nBtnMover   ! -1;  |FIN_CONTROL_WINDOWS nBtnMover;   |FINSI;

     nLblCopiar1 = -1;
     nLblCopiar2 = -1;
     nBtnCopiar  = -1;
     nLblMover1  = -1;
     nLblMover2  = -1;
     nBtnMover   = -1;

     |SI nOpcion1 = 0;  |ACABA;  |FINSI;

     aBusca1 = aTexto1;

     |SI nOpcion1 = 1;
         aTex1 = "Copiar el grupo origen seleccionado a un nuevo grupo";
         aTex2 = "Mover el grupo origen seleccionado a un nuevo grupo y se borra en origen";
     |FINSI;

     |SI nOpcion1 = 2;
         aTex1 = "Copiar el subgrupo origen seleccionado ";
         aTex1 = aTex1 + "como un nuevo subgrupo en el grupo destino seleccionado";

         aTex2 = "Mover el subgrupo origen seleccionado ";
         aTex2 = aTex2 + "al grupo destino seleccionado y se borra en origen";
     |FINSI;

     |SI nOpcion1 = 3;
         aTex1 = "Copiar el tipo origen seleccionado ";
         aTex1 = aTex1 + "como un nuevo tipo en el grupo/subgrupo destino seleccionado";

         aTex2 = "Mover el tipo origen seleccionado ";
         aTex2 = aTex2 + "al grupo/subgrupo destino seleccionado y se borra en origen";
     |FINSI;

     |CONTROL_SIMPLE 0, "BOTON,{{202}}", 0648, 0752, Copiar;
     nBtnCopiar = FSalida;

     |CONTROL_SIMPLE 0, "LABEL,{{15}}COPIAR", 0847, 0857, NULL;
     nLblCopiar1 = FSalida;

     aAlfa = "LABEL,{{13}}" + aTex1;
     |CONTROL_SIMPLE 0, aAlfa, 0942, 1558, NULL;
     nLblCopiar2 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{202}}", 1848, 1952, Mover;
     nBtnMover = FSalida;

     |CONTROL_SIMPLE 0, "LABEL,{{15}}MOVER", 2047, 2057, NULL;
     nLblMover1  = FSalida;

     aAlfa = "LABEL,{{13}}" + aTex2;
     |CONTROL_SIMPLE 0, aAlfa, 2142, 2858, NULL;
     nLblMover2  = FSalida;

     |HAZ EnciendeBotones;
|FPRC;

|PROCESO EventoTree2;
     |SI FEntrada = 0;
         aAlfa       = Contenido;  |QBF aAlfa;
         aTexto2     = "";
         aLong       = aAlfa % 0;
         nLong       = aLong;
         nCargar     = 0;
         |PARA nPosi = 1;  |SI nPosi [ nLong;  |HACIENDO nPosi = nPosi + 1;
               nPos = (nPosi * 100) + 1;
               aCarac = aAlfa % nPos;

               |SI nCargar = 1;
                   aTexto2 = aTexto2 + aCarac;
               |FINSI;

               |SI aCarac = ":";  nCargar = 1; |FINSI;
         |SIGUE;

         aIDE2_1  = "";
         aIDE2_2  = "";
         aIDE2_3  = "";

         nOpcion2 = 1;

         aAlfa = aTexto2 - "GRUPO";
         |SI aTexto2 ! aAlfa;
             nOpcion2 = 1;
             aIDE2_1  = aAlfa % 103;
         |FINSI;

         aAlfa = aTexto2 - "SUBGRUPO";
         |SI aTexto2 ! aAlfa;
             nOpcion2 = 2;
             aIDE2_1  = aAlfa % 103;
             aIDE2_2  = aAlfa % 403;
         |FINSI;

         aAlfa = aTexto2 - "TIPO";
         |SI aTexto2 ! aAlfa;
             nOpcion2 = 3;
             aIDE2_1  = aAlfa % 103;
             aIDE2_2  = aAlfa % 403;
             aIDE2_3  = aAlfa % 703;
         |FINSI;

         |SI aTexto2 = "";
             nOpcion2 = 0;
         |FINSI;
     |FINSI;

     aBusca2 = aTexto2;

     |HAZ EnciendeBotones;
|FPRC;

|PROCESO CargaArbol;
     |QBF aAlfa;
     aAlfa = aAlfa + &13 + &10;
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO dsarm004;
     nCarpeta3 = nCarpeta3 + 1;
     aAlfa     = nCarpeta1 + "," + nCarpeta2 + "," + nCarpeta3 + ":";
     aAlfa     = aAlfa + (("000" + #dsarm004#2) % -103) + " - " + #dsarm004#3;  |QBF aAlfa;
     aAlfa     = aAlfa + "{{TIPO" + (("000" + #dsarm004#0) % -103) + (("000" + #dsarm004#1) % -103) + (("000" + #dsarm004#2) % -103) + "}}";
     |HAZ CargaArbol;
|FPRC;

|DEFBUCLE dsarm004;
     #dsarm004#1;
     ;
     #dsarm003#0, #dsarm003#1, 001;
     #dsarm003#0, #dsarm003#1, 999;
     ;
     NULL, dsarm004;
|FIN;

|PROCESO dsarm003;
     nCarpeta2 = nCarpeta2 + 1;
     aAlfa     = nCarpeta1 + "," + nCarpeta2 + ":" + (("000" + #dsarm003#1) % -103) + " - " + #dsarm003#2;  |QBF aAlfa;
     aAlfa     = aAlfa + "{{SUBGRUPO" + (("000" + #dsarm003#0) % -103) + (("000" + #dsarm003#1) % -103) + "}}";
     |HAZ CargaArbol;

     nCarpeta3 = 0;

     |BUCLE dsarm004;
|FPRC;

|DEFBUCLE dsarm003;
     #dsarm003#1;
     ;
     #dsarm002#0, 001;
     #dsarm002#0, 999;
     ;
     NULL, dsarm003;
|FIN;

|PROCESO dsarm002;
     nCarpeta1 = nCarpeta1 + 1;
     aAlfa     = nCarpeta1 + ":" + (("000" + #dsarm002#0) % -103) + " - " + #dsarm002#1;  |QBF aAlfa;
     aAlfa     = aAlfa + "{{GRUPO" + (("000" + #dsarm002#0) % -103) + "}}";
     |HAZ CargaArbol;

     nCarpeta2 = 0;
     nCarpeta3 = 0;

     |SI #dsarm001#8 = "S";
         |BUCLE dsarm003;
     |FINSI;
|FPRC;

|DEFBUCLE dsarm002;
     #dsarm002#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dsarm002;
|FIN;

|PROCESO MontaArbol1;
     |SI nTree1 ! -1;
         |FIN_CONTROL_WINDOWS nTree1;
         nTree1 = -1;
     |FINSI;

     |VENTANA 0501, 0540, -1, 16, "Procesando trabajo";
     nSubVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     |DBASE aBase;  |QBF aBase;
     aAlfa = aBase + "tmp";          |MKDIR aAlfa;

     eaRutaOrigen  = aBase + "tmp/";

     |SI #dsarm001#7 = "N";
          |HAZ DimeUnidad;
          eaRutaDestino = eaUnidad + ":\documentos\tmp";  |RMKDIR eaRutaDestino;
          eaRutaDestino = eaUnidad + ":\documentos\tmp\";
     |SINO;
          |HAZ LeeUnidadBasico;
          eaRutaDestino = eaUnidadBasico + "\documentos\tmp";   |RMKDIR eaRutaDestino;
          eaRutaDestino = eaUnidadBasico + "\documentos\tmp\";
     |FINSI;
     eaFicheroTxt  = "dsarz02001" + Usuario + ".txt";
     eaFicheroTgz  = "dsarz02001" + Usuario + ".tgz";
     eaFicheroTar  = "dsarz02001" + Usuario + ".tar";

     aAbre = eaRutaOrigen + eaFicheroTxt;

     sT_nID   = nTree1;
     sT_nOper = 0;

     |XABRE "WB", aAbre, nHandle;

     nCarpeta1 = 0;
     nCarpeta2 = 0;
     nCarpeta3 = 0;

     |BUCLE dsarm002;

     |XCIERRA nHandle;

     |HAZ TraeTxtComprimido;
     aAlfa = eaRutaDestino + eaFicheroTxt;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nSubVent;

     eaAlfa = "TREECTRL,Estructura origen {{" + aAlfa + "}}";
     |CONTROL_SIMPLE 0, eaAlfa, 0602, 3340, EventoTree1;
     nTree1 = FSalida;

     eaFicheroTxt  = "dsarz02001" + Usuario + ".txt";
     aAlfa         = eaRutaDestino + eaFicheroTxt;
     |RFBORRA aAlfa;

     |SI aBusca1 ! "";
         sT_nID   = nTree1;
         sT_nOper = 3;
         sT_aData = "[" + aBusca1;
         |ESPECIFICA "COMMANDCONTROL", sTree;
         aRefresca1 = ":" + FSalida;
     |FINSI;

     sT_nID   = nTree1;
     sT_nOper = 2;
     |SI aRefresca1 = "";
         sT_aData = "1:";
     |SINO;
         sT_aData = aRefresca1;
     |FINSI;

     |ESPECIFICA "COMMANDCONTROL", sTree;
     |PULSATECLA;

     |FOCOTECLADO nTree1;
|FPRC;

|PROCESO MontaArbol2;
     |SI nTree2 ! -1;
         |FIN_CONTROL_WINDOWS nTree2;
         nTree2 = -1;
     |FINSI;

     |VENTANA 0501, 0540, -1, 16, "Procesando trabajo";
     nSubVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     |DBASE aBase;  |QBF aBase;
     aAlfa = aBase + "tmp";          |MKDIR aAlfa;

     eaRutaOrigen  = aBase + "tmp/";

     |SI #dsarm001#7 = "N";
          |HAZ DimeUnidad;
          eaRutaDestino = eaUnidad + ":\documentos\tmp";  |RMKDIR eaRutaDestino;
          eaRutaDestino = eaUnidad + ":\documentos\tmp\";
     |SINO;
          |HAZ LeeUnidadBasico;
          eaRutaDestino = eaUnidadBasico + "\documentos\tmp";   |RMKDIR eaRutaDestino;
          eaRutaDestino = eaUnidadBasico + "\documentos\tmp\";
     |FINSI;
     eaFicheroTxt  = "dsarz02002" + Usuario + ".txt";
     eaFicheroTgz  = "dsarz02002" + Usuario + ".tgz";
     eaFicheroTar  = "dsarz02002" + Usuario + ".tar";

     aAbre = eaRutaOrigen + eaFicheroTxt;

     sT_nID   = nTree2;
     sT_nOper = 0;

     |XABRE "WB", aAbre, nHandle;

     nCarpeta1 = 0;
     nCarpeta2 = 0;
     nCarpeta3 = 0;

     |BUCLE dsarm002;

     |XCIERRA nHandle;

     |HAZ TraeTxtComprimido;
     aAlfa = eaRutaDestino + eaFicheroTxt;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nSubVent;

     eaAlfa = "TREECTRL,Estructura destino {{" + aAlfa + "}}";
     |CONTROL_SIMPLE 0, eaAlfa, 0660, 3398, EventoTree2;
     nTree2 = FSalida;

     eaFicheroTxt  = "dsarz02002" + Usuario + ".txt";
     aAlfa         = eaRutaDestino + eaFicheroTxt;
     |RFBORRA aAlfa;

     |SI aBusca2 ! "";
         sT_nID   = nTree2;
         sT_nOper = 3;
         sT_aData = "[" + aBusca2;
         |ESPECIFICA "COMMANDCONTROL", sTree;
         aRefresca2 = ":" + FSalida;
     |FINSI;

     sT_nID   = nTree2;
     sT_nOper = 2;
     |SI aRefresca2 = "";
         sT_aData = "1:";
     |SINO;
         sT_aData = aRefresca2;
     |FINSI;

     |ESPECIFICA "COMMANDCONTROL", sTree;
     |PULSATECLA;
|FPRC;

|PROGRAMA;
     |ABRE #dsarm001;
     |LEE 000#dsarm001.p;
     |SI FSalida ! 0;  |DDEFECTO #dsarm001;  |FINSI;
     |CIERRA #dsarm001;

     |VENTANA 0501, 3599, -1, 16, "Copiar/Mover estructuras";
     nVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     |HAZ MontaArbol1;
     |HAZ MontaArbol2;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}Salir," + nTree1;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVent;
|FPRO;

|PROCESO Tipo80;  |TIPO 80;
     |ABRET #dsarm002;
     |ABRET #dsarm003;
     |ABRET #dsarm004;
|FPRC;

|PROCESO Tipo90;  |TIPO 90;
     |CIERRAT #dsarm002;
     |CIERRAT #dsarm003;
     |CIERRAT #dsarm004;
|FPRC;
