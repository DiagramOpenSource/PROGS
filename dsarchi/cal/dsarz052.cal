|FICHEROS;
     dsarz052 #0;
     dsarm057 #57;       ||Reglas Automatizacio
     dsarw084 #84,MANTE; ||Aux.Reglas Automat.

     dsarm001 #1;
|FIN;

|VARIABLES;
     aPrefijo = "";
     aPathBase = "";
     aAuxVersion = "";
     &eaOrigen = "";
     &eaDestino = "";
     aPathRemoto = "";
     &eaUnidad = "";
     aEjecuta = "";
     &enSwIOCorrecta = 0;
     aIPRemoto = "";
     nHandle = 0;


     nEmpresa = 0;
     aQueBoton     = "";
     aID           = "";
     nBotonAceptar = 0;
     nBotonCancelar = 0;
     nUltimo = 0;
     nIdSalir = 0;
     {2}nNivel = 0;
     nVentana = 0;
     nPantaDos = 0;
     aCadena = "";
     aAlfa = "";
     nCampoHasta = 0;
     nCampoD = 0;
     nCampoH = 0;

     {-1}aVent      = "";
         aVent1     = 0;
         aVent2     = 0;
         aVent3     = 0;
         aVent4     = 0;
         aVent5     = "";

     aMensaje = "";

     nBoton1 = 0;
     nBoton2 = 0;
     nBoton3 = 0;
     nBotonEdit = 0;

     nPanta = 0;
     nRango = 0;
     nPos1  = 0;
     nPos2  = 0;
     nGrid = 0;
     aEsc1  = "";
     aEsc2  = "";
     aRetu  = "";
     aTecla = "";

     nModo = 0;
|FIN;

|PROCESO Tipo12w084; |TIPO 12;

|FPRC;

|PROCESO BotonEdit;
     ||ARA39

     nEmpresa = #dsarw084#0;
     aPrefijo = "emp" + nEmpresa;


     ||El nombre del fichero custom sera  nEmpresa + dscustom3[01]0.xsl

     ||TODO CCC.
     ||Miro si existe dscustom3[01]0.xsl, sino la creo a partir del dsfacturae3[10]0.xsl
     |DBASE aPathBase;
     |QBF aPathBase;
     aAuxVersion = #dsarm001#40;
     |QBF aAuxVersion;
     |SI aAuxVersion = "3.1";
          eaOrigen = aPathBase + "plugins/" + aPrefijo + "dscustom310.xsl";
     |SINO;
          |SI aAuxVersion = "3.2";
               eaOrigen = aPathBase + "plugins/" + aPrefijo + "dscustom320.xsl";
          |SINO;
               eaOrigen = aPathBase + "plugins/" + aPrefijo + "dscustom300.xsl";
          |FINSI;
     |FINSI;

     |XABRE "E", eaOrigen, nHandle;
     |SI FSalida ! 0;
          |SI aAuxVersion = "3.1";
               eaOrigen = aPathBase + "plugins/dsfacturae310.xsl";
               eaDestino = aPathBase + "plugins/" + aPrefijo + "dscustom310.xsl";
          |SINO;
               |SI aAuxVersion = "3.2";
                    eaOrigen = aPathBase + "plugins/dsfacturae320.xsl";
                    eaDestino = aPathBase + "plugins/" + aPrefijo + "dscustom320.xsl";
               |SINO;
                    eaOrigen = aPathBase + "plugins/dsfacturae300.xsl";
                    eaDestino = aPathBase + "plugins/" + aPrefijo + "dscustom300.xsl";
               |FINSI;
          |FINSI;

          |COPIA_FICHERO eaOrigen, eaDestino;
          eaOrigen = eaDestino;
     |FINSI;

     |HAZ DimeUnidad;
     aPathRemoto = eaUnidad + ":\DOCUMENTOS";              |RMKDIR aPathRemoto;
     aPathRemoto = eaUnidad + ":\DOCUMENTOS\DSARCHI";      |RMKDIR aPathRemoto;
     |SI aAuxVersion = "3.1";
          eaDestino = aPathRemoto + "\" + aPrefijo + "dscustom310.xsl";
     |SINO;
          |SI aAuxVersion = "3.2";
               eaDestino = aPathRemoto + "\" + aPrefijo + "dscustom320.xsl";
          |SINO;
               eaDestino = aPathRemoto + "\" + aPrefijo + "dscustom300.xsl";
          |FINSI;
     |FINSI;
     |HAZ EnviaFicheroConMD5;

     aEjecuta = eaDestino;
     |RSYSTEM aEjecuta;

     enSwIOCorrecta = 0;
     |HAZ RecibeConMD5;
     |SI enSwIOCorrecta = 0;
          |MENAV "0000 Ha habido un error al transferir el documento al servidor. Revise Permisos y espacio disponible.";
          |ACABA;
     |FINSI;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |SI #1#7 = "N";  |Y aIPRemoto ! "";
          |XABRE "E", eaOrigen, nHandle;
     |SINO;
          |XABRE "EC", eaOrigen, nHandle;
     |FINSI;

     |SI FSalida < 0;
          |MENAV "0000 Ha habido un error al transferir el documento al servidor. Intentelo de nuevo";
     |FINSI;
|FPRC;

|PROCESO ObtenIdSalir;
     InNivel = nNivel1<-;
     nNivel1 = 0;
     |ESPECIFICA "IDBOTONTECLA", nNivel;
     nIdSalir = nNivel2;
|FPRC;

|PROCESO Tipo0C0w084; |TIPO 0;
     |SI nModo = 1;
          #dsarm057#0 = #dsarw084#0;
          |LEE 000#dsarm057.=;
          |SI FSalida = 0;
               aMensaje = "0000Ya existe la definicion para esta empresa";
               |MENAV aMensaje;
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo0C5w084; |TIPO 0;
     |SI #dsarw084#5 = "S";
          |ESTADO_CONTROL nBotonEdit, "ENABLE";
     |SINO;
          |ESTADO_CONTROL nBotonEdit, "DISABLE";
     |FINSI;
|FPRC;

|PROCESO Tipo0C6w084; |TIPO 0;
     |SI #dsarw084#6 = "S";
          |C_M #dsarw084#7, 1, "S";
     |SINO;
          |C_M #dsarw084#7, 1, "N";
          #dsarw084#7 = "N";
     |FINSI;
     |PINTA #dsarw084#7;
|FPRC;

|PROCESO ModiAlta;
     nIdSalir = 0;
     |HAZ ObtenIdSalir;
     |ESTADO_CONTROL nIdSalir, "HIDE";

     ||VENTANA 0501, 2999, -1, -1, "";
     |VENTANA 0501, 2999, -1, 17, "";
     nVentana = FSalida;

     aCadena ="|NC";
     aAlfa = #57#aCadena#;
     nCampoHasta = aAlfa;
     nCampoHasta = nCampoHasta - 1;

     |SI nModo ! 1;
          |PARA nCampoD = 0;  |SI nCampoD [ nCampoHasta; |HACIENDO nCampoD = nCampoD + 1;
                nCampoH = nCampoD;
                #84nCampoH = #57nCampoD;
          |SIGUE;

          |C_M #84#0, 1, "N";
     |SINO;
          |C_M #84#0, 1, "S";
          nUltimo = 0;
          |HAZ DameUltimo;
          |DDEFECTO #84;
          nUltimo = nUltimo + 1;
          #84#0 = nUltimo;
     |FINSI;

     |PINPA #0#84; nPantaDos = FSalida;
     |PINDA #0#84;


     |CONTROL_SIMPLE nPantaDos, "BOTON,Editar XSL", 1434, 1444, BotonEdit;
     nBotonEdit = FSalida;

     |SI nModo = 1;  |ENDATOS #1#84;  |FINSI;
     |SI nModo = 2;  |ENDATOS #2#84;  |FINSI;

     |SI FSalida = 0;
          |HAZ GrabaDatos;
     |FINSI;

     |FINVENTANA nVentana;

     |ESTADO_CONTROL nIdSalir, "SHOW";
|FPRC;

|PROCESO Tipo7C0w084; |TIPO 7;
     |HAZ Tipo0C5w084;
     |HAZ Tipo0C6w084;
|FPRC;

|PROCESO DameUltimo;
     |LEE 000#dsarm057.u;
     |SI FSalida = 0;
          nUltimo = #dsarm057#0;
     |SINO;
          nUltimo = 0;
     |FINSI;
|FPRC;

|PROCESO GrabaDatos;
     #57#0 = #84#0;
     |LEE 101#57=;
     |SI FSalida ! 0;
         |DDEFECTO #57;
         #57#0 = #84#0;
         |GRABA 020#57b;
     |FINSI;

     |PARA nCampoD = 1;  |SI nCampoD [ nCampoHasta;  |HACIENDO nCampoD = nCampoD + 1;
           nCampoH   = nCampoD;
           #57nCampoD = #84nCampoH;
     |SIGUE;

     |GRABA 020#57a;
     |LIBERA #57;
|FPRC;


|PROCESO Tipo80C0; |TIPO 80;
     FSalida = 2999;
|FPRC;

|PROCESO BajaPanta;
     #dsarm057#0 = 01;
|FPRC;

|PROCESO AltaPanta;
     #dsarm057#0 = 99999;
|FPRC;

|PROCESO EventoPanta;
     |SI FSalida = 1;  |O FSalida = 2;
          |LEE 000#dsarm057.=;
          |SI FSalida = 0;
               |ESTADO_CONTROL nBoton2, "ENABLE";
               |ESTADO_CONTROL nBoton3, "ENABLE";
          |SINO;
               |ESTADO_CONTROL nBoton2, "DISABLE";
               |ESTADO_CONTROL nBoton3, "DISABLE";
          |FINSI;
     |FINSI;

     |SI FSalida = 4;
          |HAZ Boton2;
     |FINSI;
|FPRC;

|PROCESO Boton1;
     nModo = 1;
     |HAZ ModiAlta;
     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO Boton2;
     |LEE 000#dsarm057.=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     nModo = 2;
     |HAZ ModiAlta;
     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO Boton3;
     |LEE 000#dsarm057.=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |LEE 101#dsarm057.=;
     |SI FSalida = 0;
          aMensaje = "0000NDesea Borrar " + #57#1;
          |QBF aMensaje;
          aMensaje = aMensaje + "?";
          |CONFI aMensaje;
          |SI FSalida = 0;
               |BORRA 020#dsarm057.a;
          |FINSI;
          |LIBERA #dsarm057;
     |FINSI;

     |REFRESCACONTROL nGrid;
|FPRC;

|PROGRAMA;
     |CLS;
     |CABEZA "Configuraci¢n Factura_E por empresa";

     |ABRE #dsarm057;

     |PINPA #0#dsarz052;
     nPanta = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}Alta ", 2753, 2763, Boton1;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Modificar ", 2765, 2775, Boton2;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{199}}Baja ", 2777, 2787, Boton3;
     nBoton3 = FSalida;

     nRango = 4 + 8 + 16 + 32;
     nPos1  = 0702;
     nPos2  = 2698;

     |LINEAL_SIMPLE #1#dsarm057, nRango, nPos1, nPos2, BajaPanta, AltaPanta, EventoPanta;
     nGrid = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}Salir," + nGrid;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nGrid;

     |DESTRUYECONTROL nBoton1;
     |DESTRUYECONTROL nBoton2;
     |DESTRUYECONTROL nBoton3;

     |CIERRA #dsarm057;
|FPRO;
