|FICHEROS;
     dsarz034 #1;

     dsarm029 #29;       ||Plantillas Auto-relleno
     dsarw056 #56,MANTE; ||Aux.Plan Auto-Relleno
     dsarw057 #57,MANTE; ||Aux. Copiar Plan.AutoR
     dsarw016 #916;
     referen@ #999;

     dsarm004 #4;        ||Tipos Documentos
|FIN;

|VARIABLES;
     {1}aContiene     = "";
     nError = 0;
     aOrigen = "";
     nHandle = 0;
     aDestino = "";
     aLinea = "";

     &eaUnidad = "";
     nPlantilla = 0;
     nLabel = 0;

     {-1}aVent      = "";
         aVent1     = 0;
         aVent2     = 0;
         aVent3     = 0;
         aVent4     = 0;
         aVent5     = "";

     nSubVent = 0;
     &enNumRegla = 0;
     aEjecuta = "";
     aQueBoton     = "";
     aID           = "";
     nBotonAceptar = 0;
     nBotonCancelar = 0;

     aAux = "";
     nTCampo = 0;
     aQueQuiero = "";
     aFichero = "";
     aBase = "";
     aCarga = "";
     nCampo = 0;
     aImpre = "";

     nUltimo = 0;
     nIdSalir = 0;
     {2}nNivel = 0;
     nVentana = 0;
     nPantaDos = 0;
     aCadena = "";
     aAlfa = "";
     nCampoHasta = 0;
     nCampoD = 0;
     nCampoH = 0;
     aMensaje = "";
     nBoton1 = 0;
     nBoton2 = 0;
     nBoton3 = 0;
     nBoton4 = 0;
     nPanta = 0;
     nRango = 0;
     nPos1  = 0;
     nPos2  = 0;
     nGrid = 0;
     aEsc1  = "";
     aEsc2  = "";
     aRetu  = "";
     aTecla = "";
     nModo = 0;
|FIN;

|PROCESO Tipo12w056; |TIPO 12;

|FPRC;

|PROCESO Tipo0C0w056; |TIPO 0;
     |SI FSalida ! 7;
          |SI nModo = 1;
               #dsarm029#0 = #dsarw056#0;
               |LEE 000#dsarm029.=;
               |SI FSalida = 0;
                    aMensaje = "0000Error: Plantilla YA definida previamente";
                    |MENAV aMensaje;
                    |ERROR;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo0C0w057; |TIPO 0;
     |SI FSalida ! 7;
          |SI #dsarw057#0 = #dsarw057#2;
               aMensaje = "0000Error: Plantilla Origen = Plantilla Destino";
               |MENAV aMensaje;
               |ERROR;
          |SINO;

               #dsarm029#0 = #dsarw057#2;
               |LEE 000#dsarm029.=;
               |SI FSalida = 0;
                    aMensaje = "0000Error: Plantilla YA definida previamente";
                    |MENAV aMensaje;
                    |ERROR;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo12w057; |TIPO 12;
|FPRC;

|PROCESO Tipo0Campos;  |TIPO 0;
     |ABRE #916;
     #916#0 = #56Campo;
     |LEE 000#916=;
     |SI FSalida ! 0;
         |MENAV "0000No existe el campo";
         |ERROR;
         |CIERRA #916;
         |ACABA;
     |FINSI;
     |CIERRA #916;

     |SI #56Campo ! 0;
          nCampo = Campo + 1;
          #56Campo = #916#0;    |PINTA #56Campo;
          #56nCampo = #916#1;    |PINTA #56nCampo;
     |SINO;
          nCampo = Campo + 1;
          #56nCampo = "";    |PINTA #56nCampo;
     |FINSI;
|FPRC;

|PROCESO ObtenIdSalir;
     InNivel = nNivel1<-;
     nNivel1 = 0;
     |ESPECIFICA "IDBOTONTECLA", nNivel;
     nIdSalir = nNivel2;
|FPRC;

|PROCESO Tipo80z034; |TIPO 80;
     FSalida = 2999;
|FPRC;

|PROCESO Tipo80w056; |TIPO 80;
     FSalida = 3199;
|FPRC;

|PROCESO BajaPanta;
     #dsarm029#0 = 01;
|FPRC;

|PROCESO AltaPanta;
     #dsarm029#0 = 99;
|FPRC;

|PROCESO EventoPanta;
     |SI FSalida = 1;  |O FSalida = 2;
          |LEE 000#dsarm029.=;
          |SI FSalida = 0;
               |ESTADO_CONTROL nBoton2, "ENABLE";
               |ESTADO_CONTROL nBoton3, "ENABLE";
               |ESTADO_CONTROL nBoton4, "ENABLE";
          |SINO;
               |ESTADO_CONTROL nBoton2, "DISABLE";
               |ESTADO_CONTROL nBoton3, "DISABLE";
               |ESTADO_CONTROL nBoton4, "DISABLE";
          |FINSI;
     |FINSI;

     |SI FSalida = 4;
          |HAZ Boton2;
     |FINSI;
|FPRC;

|PROCESO Boton1;
     nModo = 1;
     |HAZ ModiAlta;
     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO Boton2;
     |LEE 000#dsarm029.=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     nModo = 2;
     |HAZ ModiAlta;
     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO Boton3;
     |LEE 000#dsarm029.=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |LEE 101#dsarm029.=;
     |SI FSalida = 0;
          aMensaje = "0000NDesea Borrar " + #29#1;
          |QBF aMensaje;
          aMensaje = aMensaje + "?";
          |CONFI aMensaje;
          |SI FSalida = 0;
               |BORRA 020#dsarm029.a;
          |FINSI;
          |LIBERA #dsarm029;
     |FINSI;

     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO Boton4;
     |LEE 000#dsarm029.=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     nPlantilla = #dsarm029#0;

     |VENTANA 1010, 1590, nPanta, 17, "Copiar Plantilla";
     nSubVent = FSalida;

     nLabel = -1;
     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |DDEFECTO #dsarw057;
     #dsarw057#0 = #dsarm029#0;
     #dsarw057#1 = #dsarm029#1;
     nUltimo = 0;
     |HAZ DameUltimo;
     nUltimo = nUltimo + 1;
     #dsarw057#2 = nUltimo;

     |PINPA #0#dsarw057;
     |PINDA #0#dsarw057;

     |ENDATOS #1#dsarw057;
     |SI FSalida = 0;
          #dsarm029#0 = nPlantilla;
          |LEE 000#dsarm029.=;
          |SI FSalida = 0;
               #dsarm029#0 = #dsarw057#2;
               #dsarm029#1 = #dsarw057#3;
               |GRABA 020#dsarm029.n;
               |LIBERA #dsarm029;
          |FINSI;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nSubVent;

     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO CargaTemporal;
     aFichero = ("18" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #916 aFichero;

     |ABRE #916;  |CIERRA #916;  |DELINDEX #916;

     |ABRE #916;

     |DBASE aBase;  |QBF aBase;
     aCarga    = aBase + "def/dsarm005.mas";  |QBF aCarga;
     |CARGA_DEF aCarga, referen@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aQueQuiero = "|NC";
     aAlfa      = #999#aQueQuiero#;
     nTCampo    = aAlfa;
     nTCampo    = nTCampo - 1;

     |PARA nCampo = 0;  |SI nCampo [ nTCampo;  |HACIENDO nCampo = nCampo + 1;
           aQueQuiero = "|C" + (("000" + nCampo) % -103) + "NOMBRE";
           aFichero   = (#999#aQueQuiero# + (" " * 20)) % 120;
           #916#0 = nCampo;
           #916#1 = aFichero;

           |GRABA 020#916n;
     |SIGUE;
     |CIERRA #916;

     |DESCARGA_DEF #referen@;
|FPRC;


|PROCESO ModiAlta;
     nIdSalir = 0;
     |HAZ ObtenIdSalir;
     |ESTADO_CONTROL nIdSalir, "HIDE";

     |VENTANA 0501, 3199, -1, -1, "";
     nVentana = FSalida;

     aCadena ="|NC";
     aAlfa = #29#aCadena#;
     nCampoHasta = aAlfa;
     nCampoHasta = nCampoHasta - 1;

     |SI nModo ! 1;
          |PARA nCampoD = 0;  |SI nCampoD [ nCampoHasta; |HACIENDO nCampoD = nCampoD + 1;
                nCampoH = nCampoD;
                #56nCampoH = #29nCampoD;
          |SIGUE;

          |C_M #56#0, 1, "N";
     |SINO;
          |C_M #56#0, 1, "S";
          nUltimo = 0;
          |HAZ DameUltimo;
          |DDEFECTO #56;
          nUltimo = nUltimo + 1;
          #56#0 = nUltimo;
     |FINSI;

     |HAZ CargaTemporal;

     |PINPA #0#56; nPantaDos = FSalida;
     |PINDA #0#56;

     |SI nModo = 1;  |ENDATOS #1#56;  |FINSI;
     |SI nModo = 2;  |ENDATOS #2#56;  |FINSI;

     |SI FSalida = 0;
          |HAZ GrabaDatos;
     |FINSI;

     |ABRE #916;  |CIERRA #916;  |DELINDEX #916;

     |FINVENTANA nVentana;

     |ESTADO_CONTROL nIdSalir, "SHOW";
|FPRC;

|PROCESO DameUltimo;
     |LEE 000#dsarm029.u;
     |SI FSalida = 0;
          nUltimo = #dsarm029#0;
     |SINO;
          nUltimo = 0;
     |FINSI;
|FPRC;

|PROCESO GrabaDatos;
     #29#0 = #56#0;
     |LEE 101#29=;
     |SI FSalida ! 0;
         |DDEFECTO #29;
         #29#0 = #56#0;
         |GRABA 020#29b;
     |FINSI;

     |PARA nCampoD = 1;  |SI nCampoD [ nCampoHasta;  |HACIENDO nCampoD = nCampoD + 1;
           nCampoH   = nCampoD;
           #29nCampoD = #56nCampoH;
     |SIGUE;

     |GRABA 020#29a;
     |LIBERA #29;
|FPRC;

|PROCESO Tipo0C2w056; |TIPO 0;
     |SI #dsarw056#2 = "S";
          |C_M #dsarw056#3, 1, "S";
     |SINO;
          |C_M #dsarw056#3, 1, "N";
     |FINSI;
     |PINTA #dsarw056#3;
|FPRC;

|PROGRAMA;
     |CLS;
     |CABEZA "Definici¢n Plantillas Auto-relleno";

     |ABRE #dsarm029;

     #dsarm029#0 = 0;
     |LEE 000#dsarm029.=;
     |SI FSalida ! 0;
          |DDEFECTO #dsarm029;
          #dsarm029#0 = 0;
          |LEE 101#dsarm029.=;
          |SI FSalida ! 0;
               #dsarm029#1 = "<SIN PLANTILLA>";
               |GRABA 020#dsarm029.n;
          |FINSI;
          |LIBERA #dsarm029;
     |FINSI;

     |PINPA #0#dsarz034;
     nPanta = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}Alta ", 2753, 2763, Boton1;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Modificar ", 2765, 2775, Boton2;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{199}}Baja ", 2777, 2787, Boton3;
     nBoton3 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,[+]   Copiar ", 2703, 2713, Boton4;
     nBoton4 = FSalida;

     ||Poner los 4 botones

     nRango = 4 + 8 + 16 + 32;
     nPos1  = 0702;
     nPos2  = 2698;

     |LINEAL_SIMPLE #1#dsarm029, nRango, nPos1, nPos2, BajaPanta, AltaPanta, EventoPanta;
     nGrid = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}Salir," + nGrid;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nGrid;

     |DESTRUYECONTROL nBoton1;
     |DESTRUYECONTROL nBoton2;
     |DESTRUYECONTROL nBoton3;
     |DESTRUYECONTROL nBoton4;

     |CIERRA #dsarm029;
|FPRO;
