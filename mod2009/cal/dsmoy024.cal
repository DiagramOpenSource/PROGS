|FICHEROS;
     dsmoy024 #0;
     dsmom004 #4;
     dsmom026 #26;
     dsm190ca #190;
     dsm190li #191;
     dsmow072 #999,MANTE;
     dsmow073 #998;

     :/basica/def/agifigen   #300;
     :/basica/def/agidanif   #301;

     :/laboral/def/labtraba  #200;
     :/laboral/def/nomhicca  #201;
     :/laboral/def/nomhiatr  #205;
|FIN;

|VARIABLES;
     nCampo            = 0;
     nHandle           = 0;
     nEjer             = 0;
     nEmpresa          = 0;
     nDPeriodo         = 0;
     nHPeriodo         = 0;
     nEjerAt           = 0;
     nDComple          = 0;
     nHComple          = 0;
     nComprueba        = 0;
     nSwHay            = 0;
     nTrabajador       = 0;
     nAutonomo         = 0;
     nLinea            = 0;
     nGrupos           = 0;

     aPathLaboral      = "";
     aBase             = "";
     aAlfa             = "";
     aDni              = "";
     aFichero          = "";

     &EURODB           = 0;

     &eaNif            = "";
     &eaNomNif         = "";
     &enEmpresa        = 0;
     &eaNomEmpresa     = "";
     &enImporte1       = 0;
     &enImporte2       = 0;

     {-1}aMenu         = "";
         aMenu1        = "2400";
         aMenu2        = "2";
         aMenu3        = "Elija opcion : ";
         aMenu4        = "RCA";
         aMenu5        = " Revisar ";
         aMenu6        = " Corregir ";
         aMenu7        = " cAncelar ";
|FIN;

|| /////////////////////// PROCESOS DEL LINEAL  \\\\\\\\\\\\\\\\\\\\

|PROCESO Tipo11;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     |LEE 101#999=;
     |SI #999#6 = "S";
         #999#6 = "N";
     |SINO;
         #999#6 = "S";
     |FINSI;
     |GRABA 020#999a;
     |LIBERA #999;

     |PINTA #999#6;
|FPRC;

|| /////////////////////// PROCESOS ENTRADA POR PANTALLA \\\\\\\\\\\\\\\\\\\\

|PROCESO Corregir;
     |C_M #0#17, 1, "S";
     |SI #0#16 = "N";
         |C_M #0#17, 1, "N";
         #0#17 = " ";  |PINTA #0#17;
     |SINO;
         #0#17 = "S";  |PINTA #0#17;
     |FINSI;
|FPRC;

|PROCESO Parrilla;
     |SI #0#1 = 0;
         #0#1 = 0;  |PINTA #0#1;
         #0#2 = 0;  |PINTA #0#2;  |C_M #0#4, 1, "N";
         |PARA nCampo = 3;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |C_M #0#1, 1, "S";
         |C_M #0#2, 1, "S";

         |PARA nCampo = 3;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|| /////////////////////// PROCESOS DE TRABAJO \\\\\\\\\\\\\\\\\\\\

|PROCESO nomhicca;
     enImporte2 = enImporte2 + (#201#48 + #201#49 + #201#50 + #201#51);
|FPRC;

|DEFBUCLE nomhicca;
     #201#1;
     ;
     nEmpresa, nTrabajador, nEjer, 01, 0;
     nEmpresa, nTrabajador, nEjer, 12, 9;
     ;
     NULL, nomhicca;
|FIN;

|DEFBUCLE nomhiccaA;
     #201#1;
     ;
     #205#0, nTrabajador, nEjerAt, 01, #205#2;
     #205#0, nTrabajador, nEjerAt, 12, #205#2;
     ;
     NULL, nomhicca;
|FIN;

|PROCESO nomhiatr;
     |SI #205#4 = 0;  |ACABA;  |FINSI;

     aAlfa   = #205#3;
     aAlfa   = aAlfa % -102;
     nEjerAt = aAlfa;
     |BUCLE nomhiccaA;
|FPRC;

|DEFBUCLE nomhiatr;
     #205#1;
     ;
     nEmpresa, #0#0, 5, 0000;
     nEmpresa, #0#0, 9, 9999;
     ;
     NULL, nomhiatr;
|FIN;

|PROCESO labtraba;
     nTrabajador = #200#1;
     nAutonomo   = 0;
     |SI #200#42 = "H";  |Y #0#15 = "N";
         nAutonomo = 1;
         |ACABA;
     |FINSI;

     |SI #190#4 = nDComple;
         |BUCLE nomhicca;

               || se lee todo el historico, incluyendo atrasos sin tener
               || en cuenta los de ejercicios anteriores, en el momento
               || de hacer este programa el 'nomhiatr' era nuevo y estaba
               || vacio (15.04.2003-RPC)
         ||nAtrasos = 1;  |BUCLE nomhiatr;
     |FINSI;
|FPRC;

|DEFBUCLE labtraba;
     #labtraba#5;
     ;
     nEmpresa, #191#6;
     nEmpresa, #191#6;
     ;
     NULL, labtraba;
|FIN;

|PROCESO dsmom026;
     nEmpresa   = #26#1;
     nGrupos    = 1;

     |BUCLE labtraba;
|FPRC;

|DEFBUCLE dsmom026;
     #26#1;
     ;
     #191#0, 00001;
     #191#0, 99999;
     ;
     NULL, dsmom026;
|FIN;

|PROCESO dsm190li;
     |SI eaNif ! #191#6;  |Y eaNif ! "";
         nGrupos    = 0;
         enImporte1 = enImporte1 ! EURODB;
         enImporte2 = enImporte2 ! EURODB;
         |SI enImporte1 ! enImporte2;
             |SI #0#16 = "N";
                 |PRINT;
             |SINO;
                 #999#0 = enEmpresa;
                 #999#1 = eaNif;
                 #999#2 = eaNomEmpresa;
                 #999#3 = eaNomNif;
                 #999#4 = enImporte1;
                 #999#5 = enImporte2;
                 #999#6 = "S";
                 |GRABA 020#999n;
             |FINSI;
             nSwHay = 1;
         |FINSI;

         enImporte1 = 0;
         enImporte2 = 0;
     |FINSI;

     nGrupos = 0;

     |ABRE #26;
     |SELECT #1#26;
     #26#0 = #191#0;
     #26#1 = 0;
     |LEE 040#26];
     |SI FSalida = 0;  |Y  #26#0 = #191#0;
         |CIERRA #26;
         enImporte2 = 0;
         |BUCLE dsmom026;
     |FINSI;
     |CIERRA #26;

     |SI nGrupos = 0;
         nAutonomo = 0;
         |BUCLE labtraba;
     |FINSI;

     |SI nAutonomo = 0;
         eaNif    = #191#6;
         eaNomNif = #191#12;

         enImporte1 = enImporte1 + #191#22;
     |FINSI;
|FPRC;

|DEFBUCLE dsm190li;
     #191#3;
     ;
     #190#0, #190#1, "A", "  ", "            ", #190#4;
     #190#0, #190#1, "A", "  ", "zzzzzzzzzzzz", #190#4;
     ;
     NULL, dsm190li;
|FIN;

|PROCESO dsm190ca;
     |SI nComprueba = 0;
         |SI #190#4 ! 0;
             |SI #190#10 = "S";
                 nDComple = #190#4;
                 nHComple = #190#4;
             |SINO;
                 nHComple = #190#4;
             |FINSI;
         |SINO;
             nDComple = #190#4;
             nHComple = #190#4;
         |FINSI;
         |ACABA;
     |FINSI;

     eaNif        = "";
     enImporte1   = 0;
     enImporte2   = 0;
     enEmpresa    = #190#0;
     eaNomEmpresa = #300#1;
     |BUCLE dsm190li;

     enImporte1 = enImporte1 ! EURODB;
     enImporte2 = enImporte2 ! EURODB;
     |SI enImporte1 ! enImporte2;
         |SI #0#16 = "N";
             |PRINT;
         |SINO;
             #999#0 = enEmpresa;
             #999#1 = eaNif;
             #999#2 = eaNomEmpresa;
             #999#3 = eaNomNif;
             #999#4 = enImporte1;
             #999#5 = enImporte2;
             #999#6 = "S";
            |GRABA 020#999n;
         |FINSI;
         nSwHay = 1;
     |FINSI;

     enImporte1 = 0;
     enImporte2 = 0;
|FPRC;

|DEFBUCLE dsm190ca;
     #190#1;
     ;
     #300#0, #0#0, 99, 190, nDComple, 00;
     #300#0, #0#0, 99, 190, nHComple, 99;
     ;
     NULL, dsm190ca;
|FIN;

|PROCESO agifigen;
     nDComple   = 0;
     nHComple   = 99;
     nEmpresa   = #300#0;
     nComprueba = 0;  |BUCLE dsm190ca;
     nComprueba = 1;  |BUCLE dsm190ca;
|FPRC;

|DEFBUCLE agifigen;
     #300#1;
     ;
     #0#1;
     #0#2;
     ;
     NULL, agifigen;
|FIN;

|PROCESO Sustitutoria191;
     #191#4 = nLinea;
     |GRABA 020#191n;
     |LIBERA #191;
|FPRC;

|DEFBUCLE dsm190liC;
     #191#1;
     ;
     #190#0,#190#1,#190#2,#190#3,#190#4,#190#5,"            ",0000," ","  ",0;
     #190#0,#190#1,#190#2,#190#3,#190#4,#190#5,"zzzzzzzzzzzz",9999,"z","zz",4;
     be;
     NULL, Sustitutoria191;
|FIN;

|PROCESO CreaComplementaria;
     |DDEFECTO #4;
     #4#0  = #999#0;
     #4#1  = #0#0;
     #4#2  = 99;
     #4#3  = 190;
     #4#4  = nLinea;
     #4#5  = "EMPRESA";
     #4#7  = "X";
     #4#8  = ~;
     #4#9  = " ";
     #4#10 = "01.01.0000";
     #4#11 = " ";
     #4#12 = "01.01.0000";
     #4#13 = " ";
     #4#15 = 0;
     #4#14 = "01.01.0000";
     #4#17 = "COMPLETADO";
     #4#18 = 0;

     |GRABA 020#4n;
     |LIBERA #4;

     |SI #0#17 = "S";
         #190#0 = #4#0;
         #190#1 = #4#1;
         #190#2 = #4#2;
         #190#3 = 190;
         #190#4 = nLinea - 1;
         #190#5 = 0;
         |LEE 040#190=;

         |BUCLE dsm190liC;

         #190#4  = nLinea;
         #190#10 = "S";
         |GRABA 020#190n;
         |LIBERA #190;
     |SINO;
         |DDEFECTO #190;
         #190#0  = #4#0;
         #190#1  = #4#1;
         #190#2  = #4#2;
         #190#3  = 190;
         #190#4  = nLinea;
         #190#5  = 0;
         #190#10 = "C";
         |GRABA 020#190n;
         |LIBERA #190;
     |FINSI;

     #998#0 = #4#0;
     |LEE 101#998=;
     |SI FSalida ! 0;
         |DDEFECTO #998;
         #998#0 = #4#0;
         #998#1 = #999#2;
         #998#3 = #4#4;
         |GRABA 020#998n;
     |FINSI;
     |LIBERA #998;
|FPRC;

|PROCESO Graba191;
     |SI #191#22 = #999#4;
         #191#22 = #999#5;
         |GRABA 020#191a;
     |FINSI;
     |LIBERA #191;
|FPRC;

|DEFBUCLE dsm190liG;
     #191#1;
     ;
     #190#0,#190#1,#190#2,#190#3,#190#4,#190#5,#999#1,0000,"A","  ",0;
     #190#0,#190#1,#190#2,#190#3,#190#4,#190#5,#999#1,9999,"A","zz",4;
     be;
     NULL, Graba191;
|FIN;

|PROCESO MiraTemporal;
     |SI enEmpresa = 0;  |O enEmpresa ! #999#0;
         #4#0 = #999#0;
         #4#1 = #0#0;
         #4#2 = 99;
         #4#3 = 190;
         #4#4 = 0;
         |PARA;  |SI;  |HACIENDO;
              |LEE 040#4=;
              |SI FSalida = 0;
                  #4#4 = #4#4 + 1;
              |SINO;
                  nLinea = #4#4;
                  |SAL;
              |FINSI;
         |SIGUE;

         |SI #0#17 = "C";  |O #0#17 = "S";
             |HAZ CreaComplementaria;
         |SINO;
             nLinea = nLinea - 1;
         |FINSI;
     |FINSI;

     |ABRE #301;
     #301#0  = #191#6;
     #301#1  = 1;
     |LEE 040#301=;
     |SI FSalida ! 0;  |DDEFECTO #301; |FINSI;
     |CIERRA #301;

     enEmpresa = #999#0;

     #190#0 = #4#0;
     #190#1 = #4#1;
     #190#2 = #4#2;
     #190#3 = 190;
     #190#4 = nLinea;
     #190#5 = 0;
     |LEE 101#190=;

     |SI #0#17 = "S";  |O #0#17 = "M";
         |BUCLE dsm190liG;
     |SINO;
         |ABRE #191;
         |DDEFECTO #191;
         #191#0  = #4#0;
         #191#1  = #4#1;
         #191#2  = #4#2;
         #191#3  = 190;
         #191#4  = nLinea;
         #191#5  = 0;
         #191#6  = #999#1;
         #191#7  = 0;
         #191#8  = "A";
         #191#9  = "";
         #191#10 = #301#36;
         #191#22 = #999#5;
         |GRABA 020#191n;
         |LIBERA #191;
         |CIERRA #191;

         #190#6 = #190#6 + 1;
         |GRABA 020#190a;
     |FINSI;
     |LIBERA #190;

     |PRINT;
     nSwHay = 1;
|FPRC;

|DEFBUCLE dsmow072;
     #999#2;
     ;
     "S", 00001, "            ";
     "S", 99999, "zzzzzzzzzzzz";
     ;
     NULL, MiraTemporal;
|FIN;

|PROCESO GrabaJusti;
     #190#0  = #998#0;
     #190#1  = #0#0;
     #190#2  = 99;
     #190#3  = 190;
     #190#4  = #998#3;
     #190#5  = 0;
     |LEE 101#190=;
     |SI FSalida = 0;
         #190#9 = #998#2;
         |GRABA 020#190a;
     |FINSI;
     |LIBERA #190;
|FPRC;

|DEFBUCLE dsmow073;
     #998#1;
     ;
     00001;
     99999;
     ;
     NULL, GrabaJusti;
|FIN;

|RUTINA ClaveBaja998;
     #998#0 = 1;
|FRUT;

|RUTINA ClaveAlta998;
     #998#0 = 99999;
|FRUT;

|PROCESO GrabaModelo;
     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |INFOR "dsmoyb24";
     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

     enEmpresa = 0;
     nSwHay    = 0;

     |ABRE #4;
     |ABRE #190;
     |BUCLE dsmow072;
     |CIERRA #4;

     |SI #0#17 ! "M";
         |ET Arriba998;
             |ENTLINEAL #1#998, -1, 2, 22, 1, ClaveBaja998, ClaveAlta998;

             aMenu4 = "RGA";
             aMenu6 = " Grabar Justificante ";
             |MENU aMenu;
             aMenu2 = FSalida;

             |SI FSalida < 0; aMenu2 = "1";        |FINSI;
             |SI aMenu2 = "1";  |VETE Arriba998;   |FINSI;
             |SI aMenu2 = "2";  |BUCLE dsmow073;   |FINSI;
     |FINSI;

     |CIERRA #190;

     |SI nSwHay = 1;  |PIEINF;  |FINSI;
     |FININF;
     |FINIMP;
|FPRC;

|RUTINA ClaveBaja999;
     #999#0 = 1;
     #999#1 = "            ";
|FRUT;

|RUTINA ClaveAlta999;
     #999#0 = 99999;
     #999#1 = "zzzzzzzzzzzz";
|FRUT;

|PROCESO Tipo3;  |TIPO 3;
     |SI #0#16 = "N";
         |IMPRE 0;
         |SI FSalida ! 0;  |ACABA;  |FINSI;

         |INFOR "dsmoy024";
         |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;
     |SINO;
         aFichero = ("72" + Usuario + "zzzzzzzz") % 108;
         |NOME_DAT #999 aFichero;

         aFichero = ("73" + Usuario + "zzzzzzzz") % 108;

         |ABRE #999;  |CIERRA #999;  |DELINDEX #999;
         |ABRE #998;  |CIERRA #998;  |DELINDEX #998;
         |ABRE #999;
         |ABRE #998;
     |FINSI;

     aAlfa   = #0#0;
     aAlfa   = aAlfa % -102;
     nEjer   = aAlfa;

     |ABRET #200;
     |SI #0#1 ! 0;
         |BUCLE agifigen;
     |SINO;
         |PARA nCampo = 3;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               |SI #0nCampo ! 0;
                   #0#1 = #0nCampo;
                   #0#2 = #0nCampo;
                   |BUCLE agifigen;
               |FINSI;
         |SIGUE;
     |FINSI;
     |CIERRAT #200;

     |SI #0#16 = "N";
         |SI nSwHay = 1;  |PIEINF;  |FINSI;
         |FININF;
         |FINIMP;
     |SINO;
         |CIERRA #999;
         |SI nSwHay = 1;
             |ET Arriba;
                 |ENTLINEAL #1#999, -2, 4, 22, 1, ClaveBaja999, ClaveAlta999;
                 |MENU aMenu;
                 aMenu2 = FSalida;

                 |SI FSalida < 0; aMenu2 = "1";        |FINSI;
                 |SI aMenu2 = "1";  |VETE Arriba;      |FINSI;
                 |SI aMenu2 = "2";  |HAZ GrabaModelo;  |FINSI;
         |SINO;
             |MENAV "     Seleccion Vacia.";
         |FINSI;
         |DELINDEX #999;
         |DELINDEX #998;
     |FINSI;
|FPRC;

|PROGRAMA;
     |DBASS aBase;  |QBF aBase;
     aPathLaboral = aBase + "laboral/def/nomhicca.mas";
     |XABRE "E", aPathLaboral, nHandle;
     |SI FSalida < 0;
         |MENAV "    No existe la Gestion Laboral. ....";
         |ACABA;
     |FINSI;

     aPathLaboral = aBase + "laboral/fich/";
     |PATH_DAT #200 aPathLaboral;
     |PATH_DAT #201 aPathLaboral;

     |MANTE #0;
|FPRO;
