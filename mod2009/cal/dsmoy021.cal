|FICHEROS;
    dsmoy021 #0;

    dsmom001 #1;
    dsmom004 #4;

    dsm180ca #180;
    dsm180li #181;
    dsmom107 #107;

    :/basica/def/agicen17 #17;
    :/basica/def/agim0024 #24;
    :/basica/def/agim0026 #26;
    :/basica/def/agim0029 #29;

    :/integral/def/dsam0003 #300;

    &regisnif@ #709;
    &clientes@ #715;

    dsmow014 #999;
|FIN;

|VARIABLES;
     &eaParam   = "";
     nSwGraba   = 0;
     nCampo     = 0;
     nDEmpresa  = 0;
     nHEmpresa  = 0;
     nEjer      = 0;

     informe    = "";
     aAlfa1     = "";
     aAlfa2     = "";
     aAlfa3     = "";
     nNume1     = 0;
     nNume2     = 0;
     nNume3     = 0;
     aFichero   = "";
     aPath      = "";
     eaMoneda   = "";
     aAlfa      = "";
     &eaTMoneda = "";

     &justi1   = 0;
     &justi2   = 0;
     &dia      = "";
     &mes      = "";
     &anyo     = "";

     aElDni    = "";

     &periodo = 0;

     &nomrep = "";
     &dnirep = "";
     &carrep = "";
     &codemp = "";
     &nomemp = "";
     &dniemp = "";
     &sgemp  = "";
     &domemp = "";
     &numemp = "";
     &escemp = "";
     &pisemp = "";
     &puremp = "";
     &telemp = "";
     &munemp = "";
     &cpoemp = "";
     &proemp = "";
     &delemp = "";
     &admemp = "";

     &nomtra = "";
     &dnitra = "";
     &domtra = "";
     &muntra = "";
     &cpotra = "";
     &protra = "";

     fecsys  = @;
     &fletra = "";
     &fprese = @;

     &enImp1 = 0;
     &enImp2 = 0;
     &enImp3 = 0;
     &enImp4 = 0;

     SwPrimer  = 0;
      enSwHay  = 0;
     sw_valido = 0;
     nVale     = 0;
     nHayRepre = 0;
     nLectura  = 0;
     nDComple  = 0;
     nHComple  = 0;
     &eaPathIntegral = "";
     &eaCodRenta     = "";
     aBase     = "";
     nCanal    = 0;
|FIN;

|INCLUYE i_variar;
|| /////////////////////// PROCESOS ENTRADA POR PANTALLA \\\\\\\\\\\\\\\\\\\\

|CALCULO CodigoRenta; |TIPO 0;
     |DBASS aBase;  |QBF aBase;
     aAlfa = aBase + "integral/def/dsam0000.mas";
     |XABRE "E", aAlfa, nCanal;
     |SI FSalida ] 0;
         eaPathIntegral  = aBase + "integral/";
     |FINSI;

     |C_M #0#34, 1, "S";
     |SI eaPathIntegral = "";
         |C_M #0#34, 1, "N";  #0#34 = "N";  |PINTA #0#34;
     |SINO;
         |SI #0#15 = "O";
             |C_M #0#34, 1, "N";  #0#34 = "N";  |PINTA #0#34;
         |FINSI;
     |FINSI;

     |C_M #0#35,1,"S";
     |SI #0#15 = "O";  |C_M #0#35,1,"N";  |FINSI;
|FCAL;

|CALCULO T7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#0 = enEmpresa;  |PINTA #0#0;
         #0#1 = enEmpresa;  |PINTA #0#1;

         |C_M #0#0, 1, "N";
         |C_M #0#1, 1, "N";
     |FINSI;
     aAlfa1 = #0#16; aAlfa1 = aAlfa1 % -104;
     #0#14 = aAlfa1;
     #0#14 = #0#14 - 1;
     |PINTA #0#14;
|FCAL;

|CALCULO Parrilla; |TIPO 0;
     |SI #0#0 = 0;
         #0#0 = 0;  |PINTA #0#0;
         #0#1 = 0;  |PINTA #0#1;  |C_M #0#1, 1, "N";
         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#0, 1, "S";
             |C_M #0#1, 1, "S";
         |FINSI;

         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|PROCESO DesdeDni;
     |SI #0#19 = "            ";
         #0#20 = "zzzzzzzzzzzz";  |C_M #0#20,1,"N"; |PINTA #0#20;
         |PARA nCampo = 21;  |SI nCampo [ 28;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |C_M #0#20,1,"S";
         |PARA nCampo = 21;  |SI nCampo [ 28;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = ""; |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasDni;
     |SI #0Campo = "            ";
         |PARA nCampo = Campo + 1;  |SI nCampo [ 28;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = "";  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 28;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO leocargo;
 |ABRE #29;
 #29#0 = #0Campo;
 |LEE 001#29=;
 |SI FSalida = 0;
     #0#33 = #29#1; |PINTA #0#33;
 |FINSI;
 |CIERRA #29;
|FPRC;

|| /////////////////////// //////////////////////////////////////////////////
|| /////////////////////// PROCESOS DE INFORMES \\\\\\\\\\\\\\\\\\\\\
|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO LaRentaSI;
 eaCodRenta = "";
 |ABRE #300;
 |SELECT #2#300;
 #300#1 = aElDni;
 |LEE 040#300=;
 |SI FSalida = 0;
     eaCodRenta = "Codigo de Renta : " + (("00000" + #300#0) % -105);
 |SINO;
    |SELECT #3#300;
    #300#35 = aElDni;
    |LEE 040#300=;
    |SI FSalida = 0;
        eaCodRenta = "Codigo de Renta : " + (("00000" + #300#0) % -105);
    |FINSI;
 |FINSI;
 |CIERRA #300;
|FPRC;

|PROCESO Imprimir;
  |SI enImp1 ! 0;|O enImp2 ! 0;|O enImp3 ! 0;|O enImp4 ! 0;

      eaCodRenta = "";
      |SI #0#34 = "S";  |HAZ LaRentaSI;  |FINSI;

      |PRINT;
      |PIEINF;
  |FINSI;
|FPRC;

|PROCESO FechaPres;
 |ABRE #4;
 #4#0 = #180#0;
 #4#1 = #180#1;
 #4#2 = #180#2;
 #4#3 = #180#3;
 #4#4 = #180#4;
 |LEE 000#4=;
 fprese = #4#10;
 |CIERRA #4;
|FPRC;


|PROCESO ElTrabajador;
 enImp1 = 0;
 enImp2 = 0;
 enImp3 = 0;
 enImp4 = 0;

 aElDni = #181#6;
 eaCodNif = #181#6;
 |HAZ LeeNifs;

 nomtra = #709#1;
 dnitra = aElDni;

 aAlfa1 = #709#9; |QBF aAlfa1; |SI aAlfa1 ! ""; aAlfa1 = aAlfa1 + " "; |FINSI;
 aAlfa2 = #709#2; |QBF aAlfa2; |SI aAlfa2 ! ""; aAlfa2 = aAlfa2 + " "; |FINSI;
 aAlfa3 = #709#3;
 domtra = aAlfa1 + aAlfa2 + aAlfa3;
 muntra = #709#6;
 protra = #709#7;

 aAlfa1 = #709#4; |QBF aAlfa1; aAlfa1 = ("00" + aAlfa1) % -102;
 aAlfa2 = #709#5; |QBF aAlfa2; aAlfa2 = ("000" + aAlfa2) % -103;
 cpotra = aAlfa1 + aAlfa2;
|FPRC;

|PROCESO Man180Li;
 |SI #0#19 = "            ";
     nVale = 0;
     |PARA nCampo = 21; |SI nCampo [ 28; |HACIENDO nCampo = nCampo + 1;
           |SI #0nCampo ! "            ";
               nVale = 1;
               |SI #0nCampo = #181#6;
                   nVale = 2;
                   |SAL;
               |FINSI;
           |FINSI;
     |SIGUE;
 |FINSI;
 |SI nVale = 1; |ACABA; |FINSI;

 |SI SwPrimer = 0;
     |HAZ ElTrabajador;
     SwPrimer = 1;
 |FINSI;

 |SI aElDni ! #181#6;
     |HAZ Imprimir;
     |HAZ ElTrabajador;
 |FINSI;

|| ............................. Sumo Cantidades donde van
  |SI #181#7 = 1;
      enImp1 = enImp1 + #181#13;
      enImp2 = enImp2 + #181#11;
  |FINSI;

  |SI #181#7 = 2;
      enImp3 = enImp3 + #181#13;
      enImp4 = enImp4 + #181#11;
  |FINSI;
|FPRC;

|DEFBUCLE Man180Li;
     #181#1;
     ;
     #180#0,#180#1,#180#2,#180#3,#180#4,#180#5,#0#19, 1, 0000;
     #180#0,#180#1,#180#2,#180#3,#180#4,#180#5,#0#20, 2, 9999;
     ;
     NULL, Man180Li;
|FIN;

|PROCESO BuscaCargo180;
   nomrep = #26#4;
   dnirep = #26#3;
   carrep = #26#17;
|FPRC;

|DEFBUCLE agim0026;
    #26#1;
    16;
    #180#0, 01, 01, #0#32;
    #180#0, 99, 99, #0#32;
    ;
    NULL, BuscaCargo180;
|FIN;

|PROCESO BuscaCargo180R;
     nomrep = #24#4;
     dnirep = #24#3;
     carrep = "REPRESENTANTE";
     nHayRepre = 1;
     |ERROR10;
|FPRC;

|DEFBUCLE agim0024;
     #24#2;
     ;
     #180#0, 99, 01;
     #180#0, 01, 99;
     ;
     NULL, BuscaCargo180R;
|FIN;

|PROCESO BuscaComuneros;
     aAlfa = "31.12." + #180#1;
     |SI #1#8 > aAlfa;  |ACABA;  |FINSI;

     aAlfa = "01.01." + #180#1;
     |SI #1#9 ! "01.01.0000";
         |SI #1#9 < aAlfa;  |ACABA;  |FINSI;
     |FINSI;

     nHayRepre = 1;

     enCodCli = #1#3;
     |HAZ LeePersona;

     |SI enSwDeDonde = 1;
         nomrep = #715#1;
         dnirep = #715#3;
         carrep = "REPRESENTANTE";
     |SINO;
         nomrep = #715#1;
         dnirep = #715#13;
         carrep = "REPRESENTANTE";
     |FINSI;

     |ERROR10;
|FPRC;

|DEFBUCLE dsmom001;
     #1#1;
     ;
     #180#0, 01, 0001;
     #180#0, 99, 9999;
     ;
     NULL, BuscaComuneros;
|FIN;

|PROCESO Man180C;
     |SI nLectura = 0;
         |SI #180#4 ! 0;
             |SI #180#10 = "S";
                 nDComple = #180#4;
                 nHComple = #180#4;
             |SINO;
                 nHComple = #180#4;
             |FINSI;
         |SINO;
             nDComple = #180#4;
             nHComple = #180#4;
         |FINSI;
         |ACABA;
     |FINSI;

     codemp = "";
     nomrep = "";
     dnirep = "";
     carrep = "";

     nomemp = "";
     dniemp = "";
     sgemp  = "";
     domemp = "";
     numemp = "";
     escemp = "";
     pisemp = "";
     puremp = "";
     telemp = "";
     munemp = "";
     cpoemp = "";
     proemp = "";
     delemp = "";
     admemp = "";

     |SI #107#1 = "S";
         |BUCLE agim0026;
     |SINO;
         nHayRepre = 0;
         |BUCLE agim0024;
         |SI nHayRepre = 0;
             |BUCLE dsmom001;
             |SI nHayRepre = 0;
                 nomrep  = #999#3;
                 carrep  = "TITULAR";
             |FINSI;
         |FINSI;
    |FINSI;

    codemp   = #180#0; |QBF codemp; codemp = ("00000" + codemp) % -105;
    enCodCli = #180#0;
    |HAZ LeePersona;

    |SI enSwDeDonde = 1;
          nomemp = #715#2;
          dniemp = #715#3;
          sgemp  = #715#29;
          domemp = #715#30;
          numemp = #715#31;
          escemp = #715#32;
          pisemp = #715#33;
          puremp = #715#34;
          telemp = #715#54;
          munemp = #715#37;
          aAlfa1 = #715#35; |QBF aAlfa1; aAlfa1 = ("00" + aAlfa1) % -102;
          aAlfa2 = #715#36; |QBF aAlfa2; aAlfa2 = ("000" + aAlfa2) % -103;
          cpoemp = aAlfa1 + aAlfa2;
          proemp = #715#38;
          delemp = #715#17;
          admemp = #715#18;
          |SI nomrep = "";
              nomrep = #715#1;
              |SI dnirep = ""; dnirep = #715#3; |FINSI;
          |FINSI;

     |SINO;
          nomemp = #715#1;
          dniemp = #715#13;
          sgemp  = #715#3;
          domemp = #715#4;
          numemp = #715#5;
          escemp = #715#6;
          pisemp = #715#7;
          puremp = #715#8;
          telemp = #715#14;
          munemp = #715#11;
          aAlfa1 = #715#9;  |QBF aAlfa1; aAlfa1 = ("00" + aAlfa1) % -102;
          aAlfa2 = #715#10; |QBF aAlfa2; aAlfa2 = ("000" + aAlfa2) % -103;
          cpoemp = aAlfa1 + aAlfa2;
          proemp = #715#12;
          delemp = #715#91;
          admemp = #715#92;
          |SI nomrep = "";
              nomrep = #715#2;
              |SI dnirep = ""; dnirep = #715#13; |FINSI;
          |FINSI;
     |FINSI;

     periodo = #0#14;

     fprese = #0#16;
     aAlfa1 = #0#16;
     fecsys = aAlfa1 % 2;    || pasa la fecha a letras
     fletra = fecsys % 11;   || coge la parte literal de la fecha
     aAlfa1 = munemp; |QBF aAlfa1;
     fletra = aAlfa1 + ", a " + fletra;  || une la localidad a la fecha literal
     |HAZ FechaPres;

     SwPrimer = 0;
     aElDni   = "";

     |BUCLE Man180Li;
     |SI SwPrimer = 1;
         |HAZ Imprimir;
     |FINSI;
     justi1 = justi1 + 1;
     justi2 = justi2 - 1;
     |SI justi2 = -1; justi2 = 6; |FINSI;
|FPRC;

|DEFBUCLE Man180C;
     #180#1;
     ;
     #999#0, #999#1, #999#2, #0#30, nDComple, 00;
     #999#0, #999#1, #999#2, #0#30, nHComple, 99;
     ;
     NULL, Man180C;
|FIN;

|PROCESO Imprime;
    enEmpresa = #999#0;
    enSwHay   = enSwHay + 1;
    nDComple  = 0;
    nHComple  = 99;

    nLectura  = 0;  |BUCLE Man180C;
    nLectura  = 1;  |BUCLE Man180C;
|FPRC;

|DEFBUCLE dsmow014M;
     #999#1;
     6;
     00000, 0000, 00, "S";
     99999, 9999, 99, "S";
     ;
     NULL, Imprime;
|FIN;

|| ///////////////////////////////////////////////////
|PROCESO GrabaTempo;
    enCodCli = #4#0;
    |HAZ LeePersona;

    #999#0  = #4#0;
    #999#1  = #4#1;
    #999#2  = #4#2;
    |LEE 101#999=;
    |SI FSalida ! 0;
        |DDEFECTO #999;
        #999#0  = #4#0;
        #999#1  = #4#1;
        #999#2  = #4#2;
        |GRABA 020#999b;
    |FINSI;

    #999#3  = #715#1;
    #999#4  = "ANUAL";
    #999#5  = #0#30;
    #999#6  = "S";
    #999#7  = #4#4;
    |GRABA 020#999a;
    |LIBERA #999;

     nSwGraba = 1;
|FPRC;

|DEFBUCLE dsmom004;
     #4#1;
     ;
     nDEmpresa, #0#14, #0#31, #0#30, 00;
     nHEmpresa, #0#14, #0#31, #0#30, 99;
     ;
     NULL, GrabaTempo;
|FIN;
|| ///////////////////////////////////////////////////

|PROCESO IT3;  |TIPO 3;

     |SI #0#29 = "PESETAS";
         eaTMoneda = "Ptas.";
     |SINO;
         eaTMoneda = "Euros";
     |FINSI;
     eaMoneda = #0#29;

     enEjer      = #0#14;        || A¤o Calculo
     aAlfa1      = #0#14;
     aAlfa1      = aAlfa1 % -102;
     nEjer       = aAlfa1;       || Dos ultimos digitos del A¤o

     aFichero = ("ea" + Usuario) % 108;
     |NOME_DAT #999 aFichero;
     |ABRE #999;  |CIERRA #999;  |DELINDEX #999;

     |SI #0#15 = "O";
         informe = "dsmoyo21";
     |SINO;
         informe = "dsmoy021";
         |SI #0#35 = "2"; informe = "dsmoys21"; |FINSI;
     |FINSI;

     justi1 = #0#17;
     justi2 = #0#18;
     dia    = #0#16 % 102;
     mes    = #0#16 % 402;
     anyo   = #0#16 % 704;
     |HAZ NomMes;

      nSwGraba = 0;
      |ABRE #999;
      |SI #0#0 ! 0;
          nDEmpresa = #0#0;
          nHEmpresa = #0#1;
          |BUCLE dsmom004;
      |SINO;
          |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
                nDEmpresa = #0nCampo;
                nHEmpresa = #0nCampo;
                |BUCLE dsmom004;
          |SIGUE;
      |FINSI;
      |CIERRA #999;

      |SI nSwGraba = 1;
          |IMPRE 0;
          |SI FSalida = 0;

              |INFOR informe;
              |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

              |ABRE #180;
              enSwHay = 0;
              |BUCLE dsmow014M;
              |CIERRA #180;
              |FININF;
              |FINIMP;

          |FINSI;
      |SINO;
          |MENAV "     Seleccion Vacia.";
          |DELINDEX #999;
          |ACABA;
      |FINSI;

      |DELINDEX #999;
|FPRC;


||............... PROGRAMA ......................................
|PROGRAMA;
     eaParam = PARAMETRO; |QBF eaParam;
     |SI eaParam = "";
         eaParam = "180";
     |FINSI;
     #0#30 = eaParam;
     #0#31 = 99;

     |ABRE #107;
     |LEE 040#107p;
     |SI FSalida ! 0;
         |CIERRA #107;
         |SUB_EJECUTA_NP "dsmom107";
         |ABRE #107;
     |FINSI;

     |LEE 040#107p;
     |SI FSalida ! 0;  |DDEFECTO #107;  |FINSI;
     |CIERRA #107;

     |SI enEmpresa ! 0;
         |DDEFECTO #0;
         #0#0  = enEmpresa;
         #0#1  = enEmpresa;
         #0#14 = enEjer;
         |HAZ IT3;
     |SINO;
         |CLS;
         |MANTE #0;
     |FINSI;
|FPRO;

|| **********************************************************************
____________________________________/ CALCULOS DE LECTURA E IMPRESION


|PROCESO NomMes;
 nNume1 = mes;
 |SI nNume1 = 1;  mes = "Enero     "; |FINSI;
 |SI nNume1 = 2;  mes = "Febrero   "; |FINSI;
 |SI nNume1 = 3;  mes = "Marzo     "; |FINSI;
 |SI nNume1 = 4;  mes = "Abril     "; |FINSI;
 |SI nNume1 = 5;  mes = "Mayo      "; |FINSI;
 |SI nNume1 = 6;  mes = "Junio     "; |FINSI;
 |SI nNume1 = 7;  mes = "Julio     "; |FINSI;
 |SI nNume1 = 8;  mes = "Agosto    "; |FINSI;
 |SI nNume1 = 9;  mes = "Septiembre"; |FINSI;
 |SI nNume1 = 10; mes = "Octubre   "; |FINSI;
 |SI nNume1 = 11; mes = "Noviembre "; |FINSI;
 |SI nNume1 = 12; mes = "Diciembre "; |FINSI;
|FPRC;
