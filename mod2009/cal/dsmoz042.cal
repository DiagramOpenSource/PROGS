|FICHEROS;
    dsmoz042 #0;
    dsmom041 #41;
    dsmom042 #42;

    dsmow041 #941;
|FIN;

|VARIABLES;
  nAnyV       = 0;
  fecalf      = "";
  nDEmpresa   = 0;
  nHEmpresa   = 0;
  &nDActividad = 0;
  &nHActividad = 0;
  nCampo      = 0;
  &nCamSel    = 23;
  &nCamTip    = 38;
  nLaEmpresa  = 0;
  swBueno     = 0;

  &enError    = 0;
  &eaError    = "";
  &eaRecup    = "";
  &eaTitulo   = "";
  &enAnyI     = 0;

  informe     = "dsmoz056";
  nSwAlgun    = 0;

  aAlfa1      = "";

  &ele1        = "";
  &ele2        = "";
  &nnum1       = 0;
  &nnum2       = 0;
  &enEjerA     = 0;
  &aTAmor      = "";
  &eFAmor      = @;
  aFichero     = "";
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;
|INCLUYE i_numamo;
|INCLUYE i_selcon;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\
|CALCULO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#7 = enEmpresa; |PINTA #0#7;
         #0#8 = enEmpresa; |PINTA #0#8;

         |C_M #0#7, 1, "N";
         |C_M #0#8, 1, "N";
     |FINSI;
|FCAL;

|CALCULO Parrilla;
     |SI #0#7 = 0;
         #0#7 = 0;  |PINTA #0#7;
         #0#8 = 0;  |PINTA #0#8;  |C_M #0#8, 1, "N";
         |PARA nCampo = 9;  |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#7, 1, "S";
             |C_M #0#8, 1, "S";
         |FINSI;

         |PARA nCampo = 9;  |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO numamo; |TIPO 0;
    x_alfa1 = #0Campo;
    |HAZ x_punto;
    #0Campo = x_alfa1;
    |PINTA #0Campo;
|FCAL;

|CALCULO ElPtec; |TIPO 7;
 |PTEC 816;
|FCAL;

|CALCULO porDefecto; |TIPO 7;
 fecalf = ~;
 fecalf = "31.12." + (fecalf % -104);
 #0#3 = fecalf;
|FCAL;

|CALCULO defecano;
  fecalf = #0#3;
  fecalf = fecalf % -104;
  #0#2 = fecalf;
|FCAL;

|| /////////////////////////// Procesos \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO ParaImprimir;
  enEmpresa = #941#0;
  ele1      = #0#0;
  ele2      = #0#1;
  nnum1     = #0#36;
  nnum2     = #0#37;
  aTAmor    = #0#6;
  eFAmor    = #0#3;
  |SUB_EJECUTA_NP "dsmoy059;Cal";
|FPRC;

|DEFBUCLE ParaImp;
 #941#1;
 ;
 00001;
 99999;
 ;
 NULL,ParaImprimir;
|FIN;

|PROCESO GrabaAuxEmp;
 #941#0 = enEmpresa;
 |LEE 040#941=;
 |SI FSalida ! 0;
     #941#0 = enEmpresa;
     |GRABA 020#941n;
 |FINSI;
|FPRC;

|PROCESO C_Amortiza;

  |SI nLaEmpresa ! #dsmom041#0;
      |SI nLaEmpresa ! -1;
          |SI nSwAlgun = 1;
              |SI enError ! 0; |Y enEjerA = 0; |PIEINF; |FINSI;
          |FINSI;
      |FINSI;
      nLaEmpresa = #dsmom041#0;
      enEmpresa  = #dsmom041#0;
      swBueno = 0;
      enError = 0;
      |SI enEjerA = 0; |HAZ LaSelEmpresa; |FINSI;
      |SI enSwSelEmp = 1; swBueno = 1;  |FINSI;
  |FINSI;
  |SI swBueno = 1; |ACABA; |FINSI;

  |SI #0#6 = "A";  |Y #dsmom041#24 ! "A";  |ACABA;  |FINSI;
  |SI #0#6 = "P";  |Y #dsmom041#24 ! "P";  |ACABA;  |FINSI;
  |SI #dsmom041#16 = 0;  |Y #dsmom041#22 = 0;  |ACABA;  |FINSI;
  |SI #dsmom041#35 = "S"; |ACABA; |FINSI;    || bien en curso

  |SI enEjerA = 0;
      |PINTA 2053, #dsmom041#0;
      |PINTA 2143, #dsmom041#2;
      |PINTA 2152, #dsmom041#3;
  |SINO;
      |PINTA 2252, #dsmom041#0;
      |PINTA 2268, #dsmom041#2;
      |PINTA 2277, #dsmom041#3;
  |FINSI;

  enEmpresa   = #dsmom041#0;
  enActividad = #dsmom041#1;
  eaElemento  = #dsmom041#2;
  enElemeNum  = #dsmom041#3;
  nAnyAmor    = #0#2;
  fFechaAmor  = #0#3;
  nConcpAmor  = #0#5;
  aConcpAmor  = #0#4;

  |SI #dsmom041#40 = "A";
      |HAZ CalculaAmor;
  |SINO;
      eaError   = "ELEMENTO INACTIVO CON VALOR RESIDUAL";
      swAmBuena = 1;
      |SI #41#26 > "01.01.1000";
          aAlfa1 = #41#26; aAlfa1 = aAlfa1 % -104;
          nAnyV  = aAlfa1;
          |SI nAnyV = nAnyAmor;
              eaError = "";
              |HAZ CalculaAmor;
          |FINSI;
      |FINSI;
  |FINSI;
  |SI swAmBuena = 0;
      eSWGrabar = 0;
      nLineaAmor = ultlin + 1;
      |HAZ GrabaAmor;
      |SI #0#39 = "S"; |HAZ GrabaAuxEmp; |FINSI;
  |SINO;
      |SI nSwAlgun = 0;
          nSwAlgun = 1;
          |SI enEjerA = 0;
              |IMPRE 0;
              |SI FSalida ! 0; nSwAlgun = 2; |ACABA; |FINSI;

              |INFOR informe;
              |SI FSalida ! 0; |FINIMP; nSwAlgun = 2; |ACABA; |FINSI;
          |FINSI;
      |FINSI;
      |SI nSwAlgun = 1;
          |SI enEjerA = 0; |PRINT; |FINSI;
          enError = enError + 1;
      |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE dsmoz042;
   #dsmom041#1;
   1;
   nDEmpresa,#0#0,#0#36,nDActividad;
   nHEmpresa,#0#1,#0#37,nHActividad;
   e;
   NULL, C_Amortiza;
|FIN;

|CALCULO Tipo3;  |TIPO 3;
  aFichero = ("4w" + Usuario) % 108;
  |NOME_DAT #941 aFichero;
  |DELINDEX #941;

  eaRecup  = "NO";
  eaTitulo = "RELACION DE ELEMENTOS QUE NO SE HAN PODIDO AMORTIZAR";
  aAlfa1   = #0#3;
  aAlfa1   = aAlfa1 % -104;
  enAnyI   = aAlfa1;
  efFechaI = ~;


  efFecTope = #0#3;
  nDActividad = #0#21;
  nHActividad = #0#22;
  |ABRE #941;
  |SI #0#7 ! 0;
         nDEmpresa = #0#7;
         nHEmpresa = #0#8;
         nLaEmpresa = -1;
         swBueno = 0;
         |BUCLE dsmoz042;
         |SI nSwAlgun = 1;
             |SI enError ! 0; |Y enEjerA = 0; |PIEINF; |FINSI;
         |FINSI;
     |SINO;
         |PARA nCampo = 9; |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
               |SI #0nCampo ! 0;
                   nDEmpresa = #0nCampo;
                   nHEmpresa = #0nCampo;
                   nLaEmpresa = -1;
                   swBueno = 0;
                   |BUCLE dsmoz042;
                   |SI nSwAlgun = 1;
                       |SI enError ! 0; |Y enEjerA = 0; |PIEINF; |FINSI;
                   |FINSI;
               |FINSI;
         |SIGUE;
     |FINSI;
     |CIERRA #941;

     |SI enEjerA = 0;

         |SI nSwAlgun = 1; |FININF; |FINSI;

         |SI #0#39 = "N";
             |SI nSwAlgun = 1;  |FINIMP; |FINSI;
         |SINO;
             |SI nSwAlgun = 0;
                 |IMPRE 0;
                 |SI FSalida ! 0;
                     |MENAV "    Proceso Interrumpido ";
                     |ACABA;
                 |FINSI;
             |FINSI;
             |BUCLE ParaImp;
             |FINIMP;
         |FINSI;
     |FINSI;
|FCAL;


|PROGRAMA;
 |SI enEmpresa = 0;
     |MANTE #0;
 |SINO;
     |DDEFECTO #0;
     #0#0 = ele1;
     #0#1 = ele2;
     #0#2 = enEjerA; aAlfa1 = enEjerA;
     aAlfa1 = "31.12." + aAlfa1;
     #0#3 = aAlfa1;
     #0#4 = "AMORTIZACION EJERCICIO/" + #0#2;
     #0#6 = "X";
     #0#7 = enEmpresa;
     #0#8 = enEmpresa;
     #0#21 = nDActividad;
     #0#22 = nHActividad;
     #0#23 = "S";
     #0#36 = nnum1;
     #0#37 = nnum2;
     #0#39 = "N";
     |HAZ Tipo3;
 |FINSI;
|FPRO;
