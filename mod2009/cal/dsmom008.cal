|FICHEROS;
     dsmom003 #3;
     dsmom004 #4,MANTE;
     dsmom008 #8,MANTE;
     dsmom107 #107;
     :/basica/def/agitab99 #99;
|FIN;

|VARIABLES;
     aAlfa    = "";

     nBoton1  = 0;
     nBoton2  = 0;
     nBoton3  = 0;
     nBoton4  = 0;
     nBoton5  = 0;
     nBoton6  = 0;
     nBoton7  = 0;
     nBoton8  = 0;
     nBoton9  = 0;
     nBoton10 = 0;
     nBoton12 = 0;

     nSwBoton2  = 0;
     nSwBoton3  = 0;
     nSwBoton4  = 0;
     nSwBoton5  = 0;
     nSwBoton6  = 0;
     nSwBoton7  = 0;
     nSwBoton8  = 0;
     nSwBoton9  = 0;
     nSwBoton10 = 0;
     nSwBoton12 = 0;

     nTecla   = 0;
     nSwPregunta = 0;

{-1} aMenu310 = "";
     aMenu3101     = "2400";
     aMenu3102     = "1";
     aMenu3103     = "";
     aMenu3104     = "MC";
     aMenu3105     = "Modulo  ";
     aMenu3106     = "  -Cuotas Devengadas-IVA Deducible- ";

{-1} aMenu131 = "";
     aMenu1311     = "2400";
     aMenu1312     = "1";
     aMenu1313     = "";
     aMenu1314     = "MC";
     aMenu1315     = "Modulo  ";
     aMenu1316     = "  Retenciones ";
|FIN;

|INCLUYE i_variar;

|| \\\\\ Alta o Modificacion Modelos ////////

|PROCESO AltaModelo8;
     |SI #8#7 = "COMUNIDAD ";
         |MENAV "     Comunero, Modifique la Comunidad.";
         |ACABA;
     |FINSI;

     |SI #8#3 = 349;  |O #8#3 = 110;  |O #8#3 = 115;  |O #8#3 = 123;
      |O #8#3 = 216;
         |LEE 101#8=;
         |SI FSalida = 0;
             |SI #8#7 = "PLANILLAS ";  #8#7 = "MANUAL";  |FINSI;
             |SI #8#7 = "MODULOS   ";  #8#7 = "MANUAL";  |FINSI;
             |GRABA 020#8a;
             |LIBERA #8;
         |FINSI;
     |FINSI;

     |ABRE #107;
     |LEE 040#107p;
     |SI FSalida ! 0;  |DDEFECTO #107;  |FINSI;
     |CIERRA #107;

     |SI #8#7 = "REGISTROS ";  |Y #107#3 = "N";
         |MENAV "     Los datos vienen de Contabilidad, No puedo dejarle entrar datos";
         |ACABA;
     |FINSI;

     |SI #4#11 = "X";
         |MENAV "     Modelo ya Remesado. No se puede Modificar";
         |ACABA;
     |FINSI;

     |SI #4#13 = "X";
         |MENAV "     Modelo ya Archivado. No se puede Modificar";
         |ACABA;
     |FINSI;

     enModi310 = 0;
     enModi131 = 0;
     |SI #8#7 ! "MANUAL    ";
         |SI #8#5 > 0;
             |SI #8#7 = "PLANILLAS ";
                 enEmpresa = #8#0;
                 enPeriodo = #8#2;
                 enEjer    = #8#1;
                 eaNomPer  = #3#3;
                 |SUB_EJECUTA_NP "dsmoz004";
                 |ACABA;
             |FINSI;

             |SI #8#7 = "MODULOS   ";
                 |SI #8#12 = 310;
                     |MENU aMenu310;
                     aMenu3102 = FSalida;
                     |SI aMenu3102 = "1";
                         enEmpresa = #8#0;
                         enEjer    = #8#1;
                         |SUB_EJECUTA_NP "dsmoz005";
                         |ACABA;
                     |FINSI;

                     |SI aMenu3102 = "2";  enModi310 = 1;  |FINSI;

                     |SI aMenu3102 ! "1";  |Y aMenu3102 ! "2";
                         |ACABA;
                     |FINSI;

                     |SI enModi310 = 0;
                         |MENAV "      No se puede dar de Alta o Modificar, viene de un Traspaso";
                         |ACABA;
                     |FINSI;
                 |FINSI;

                 |SI #8#12 = 131;
                     |MENU aMenu131;
                     aMenu1312 = FSalida;
                     |SI aMenu1312 = "1";
                         enEmpresa = #8#0;
                         enEjer    = #8#1;
                         |SUB_EJECUTA_NP "dsmoz005";
                         |ACABA;
                     |FINSI;

                     |SI aMenu1312 = "2";  enModi131 = 1;  |FINSI;

                     |SI aMenu1312 ! "1";  |Y aMenu1312 ! "2";
                         |ACABA;
                     |FINSI;

                     |SI enModi131 = 0;
                         |MENAV "      No se puede dar de Alta o Modificar, viene de un Traspaso";
                         |ACABA;
                     |FINSI;
                  |FINSI;
             |FINSI;
         |SINO;

             |MENAV "      No se puede dar de Alta o Modificar el Registro de Impresion";
             ||ACABA;
         |FINSI;
     |FINSI;

     |SI #8#3 = 349;  |Y #8#5 ! 0;
         ||MENAV "      No se puede dar de Alta o Modificar el Registro, hagalo desde registros de IVA";
         ||ACABA;
     |FINSI;

     enTipoEntrada = 1;

     |SI #8#7 = "REGISTROS ";  |Y #107#3 = "S";  |Y #8#3 = 130;
         enTipoEntrada = 111;
     |FINSI;

     |HAZ EntraModelo;    || Esta en el PLB

     |SI enEntrada = 1;
         |LEE 101#4=;
         #4#7  = "X";
         #4#8  = ~;
         #4#15 = enResultado;
         aAlfa = #4#16;  |QBT aAlfa;
         |SI aAlfa ! "";
             |HAZ UltimoPeriodo;
             #4#16 = eaUltimoPer;
         |FINSI;
         |GRABA 020#4a;
         |LIBERA #4;

         |LEE 101#8=;
         #8#11 = "COMPLETADO"
         |GRABA 020#8a;
         |LIBERA #8;
      |FINSI;
|FPRC;

|| \\\\\ Recoge Modelo ////////

|PROCESO RecogeModelo;
     |SI enAnterior = 0;
         |SI #8#3 ! 110;  |Y #8#3 ! 115;  |Y #8#3 ! 123; |Y #8#3 ! 216;
             |SI #8#5 = 99;
                 |SI #8#3 = 300;  |O #8#3 = 320;  |O #8#3 = 330;  |O #8#3 = 332; |O #8#3 = 370;
                     |SI #8#3 = 300;  |O #8#3 = 370;
                         |SI #8#2 = 3;  |ACABA;  |FINSI;
                     |SINO;
                         |SI #8#2 = 1;  |ACABA;  |FINSI;
                     |FINSI;
                 |SINO;
                     |SI #8#7 ! "REGISTROS ";  |ACABA;  |FINSI;
                 |FINSI;
             |SINO;
                 |SI #8#7 ! "REGISTROS ";  |Y #8#3 ! 310;  |ACABA;  |FINSI;
             |FINSI;
         |FINSI;
     |FINSI;

     |ABRE #99;
     #99#0 = enModelo;
     |LEE 040#99=;
     |SI FSalida ! 0;  |DDEFECTO #99;  |FINSI;
     |CIERRA #99;

     |SI #99#4 = "N";
         |MENAV "     Modelo sin Activar el Pase de Registro.";
         |ERROR;
     |FINSI;

     |SI #4#11 = "X";
         |MENAV "     Modelo ya Remesado. No se puede Recoger Datos";
         |ACABA;
     |FINSI;

     |SI #4#13 = "X";
         |MENAV "     Modelo ya Archivado. No se puede Recoger Datos";
         |ACABA;
     |FINSI;

     nSwPregunta = 0;
     |SI #4#9 = "X";
         |CONFI "0000NModelo ya Impreso. Desea realizar el Pase ";
         |SI FSalida ! 0;
             |ACABA;
         |FINSI;
         nSwPregunta = 1;
         FSalida = 0;
     |FINSI;

     |SI #8#3 = 300;  |Y enAnterior = 1;
         nSwPregunta = 1;
     |FINSI;

     |SI nSwPregunta = 0;
         |CONFI "0000SDesea Realizar el Pase en estos momentos ";
     |FINSI;

     |SI FSalida = 0;
         enModelo1 = #8#12;
         |SI #4#3 = "130";
             enTipoEntrada = 11;
             |HAZ EntraModelo;
             |LEE 101#8=;
             #8#11 = "COMPLETADO"
             |GRABA 020#8a;
             |LIBERA #8;
         |SINO;
             |SUB_EJECUTA_NP "dsmozpas";
         |FINSI;
     |FINSI;
|FPRC;

|| \\\\\ Baja del Modelo ////////

|PROCESO BajaModelo8;
     |SI #8#7 ! "MANUAL    ";  |Y #8#7 ! "REGISTROS ";
       |Y #8#3 ! 110;  |Y #8#3 ! 115; |Y #8#3 ! 123; |Y #8#3 ! 216;
      |Y #8#7 ! "MODULOS   ";
         |MENAV "     El registro proviene de un Traspaso, no puedo darlo de Baja";
         |ACABA;
     |FINSI;

     |SI #4#11 = "X";
         |MENAV "     Modelo ya Remesado. No se puede dar de Baja";
         |ACABA;
     |FINSI;

     |SI #4#13 = "X";
         |MENAV "     Modelo ya Archivado. No se puede dar de Baja";
         |ACABA;
     |FINSI;

     |SI #4#9 = "X";
         |CONFI "0000NModelo ya Impreso, Esta Seguro de la Baja : ";
         |SI FSalida ! 0;
             |ACABA;
         |FINSI;
     |SINO;
         |CONFI "0000NEsta seguro de dar de Baja el Modelo ";
         |SI FSalida ! 0;
             |ACABA;
         |FINSI;
     |FINSI;

     enTipoEntrada = 3;
     |HAZ EntraModelo;   || Esta en el PLB

     |LEE 101#8=;
     #8#11 = "SIN DATOS";
     |GRABA 020#8a;
     |LIBERA #8;
|FPRC;

|PROCESO Boton2m008;
     |PTEC 824;
|FPRC;

|PROCESO Boton3m008;
     |PTEC 825;
|FPRC;

|PROCESO Boton4m008;
     |PTEC 826;
|FPRC;

|PROCESO Boton5m008;
     |PTEC 827;
|FPRC;

|PROCESO Boton6m008;
     |PTEC 828;
|FPRC;

|PROCESO Boton7m008;
     |PTEC 829;
|FPRC;

|PROCESO Boton8m008;
     |PTEC 830;
|FPRC;

|PROCESO Boton9m008;
     |PTEC 831;
|FPRC;

|PROCESO Boton10m008;
     |PTEC 832;
|FPRC;

|PROCESO Boton12m008;
     |PTEC 834;
|FPRC;

|PROCESO FinBotonm008;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;
     |FIN_CONTROL_WINDOWS nBoton4;
     |FIN_CONTROL_WINDOWS nBoton5;
     |FIN_CONTROL_WINDOWS nBoton6;
     |FIN_CONTROL_WINDOWS nBoton7;
     |FIN_CONTROL_WINDOWS nBoton8;
     |FIN_CONTROL_WINDOWS nBoton9;
     |FIN_CONTROL_WINDOWS nBoton10;
     |FIN_CONTROL_WINDOWS nBoton12;

     |PULSATECLA;
|FPRC;

|PROCESO Tipo0m008;  |TIPO 0;
     |HAZ FinBotonm008;
|FPRC;

|PROCESO Tipo7m008;  |TIPO 7;
     |BLIN 04;

     |CONTROL_SIMPLE 0, "LBOTON,F2 Consulta Modelo             ", 0902, 0926, Boton2m008;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F3 Entrada Modelo              ", 1002, 1026, Boton3m008;
     nBoton3 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F4 Entrada Planilla/Modulos    ", 1102, 1126, Boton4m008;
     nBoton4 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F5 Recoger NIF's Regul.Anterior", 1202, 1226, Boton5m008;
     nBoton5 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F6 Compensar Regularizacion    ", 0928, 0952, Boton6m008;
     nBoton6 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F7 Recoger Datos               ", 1028, 1052, Boton7m008;
     nBoton7 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F8 Baja Datos                  ", 1128, 1152, Boton8m008;
     nBoton8 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F9 Datos Periodo Anterior      ", 1228, 1252, Boton9m008;
     nBoton9 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F10 C.Prorrata Definitiva ", 0954, 0980, Boton10m008;
     nBoton10 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F12 C.Prorrata Provisional", 1054, 1080, Boton12m008;
     nBoton12 = FSalida;

     |ABRE #107;
     |LEE 040#107p;
     |SI FSalida ! 0;  |DDEFECTO #107;  |FINSI;
     |CIERRA #107;

     |ESTADO_CONTROL nBoton2, "DISABLE";  nSwBoton2 = 0;
     |ESTADO_CONTROL nBoton3, "DISABLE";  nSwBoton3 = 0;
     |ESTADO_CONTROL nBoton4, "DISABLE";  nSwBoton4 = 0;
     |ESTADO_CONTROL nBoton5, "DISABLE";  nSwBoton5 = 0;
     |ESTADO_CONTROL nBoton6, "DISABLE";  nSwBoton6 = 0;
     |ESTADO_CONTROL nBoton7, "DISABLE";  nSwBoton7 = 0;
     |ESTADO_CONTROL nBoton8, "DISABLE";  nSwBoton8 = 0;
     |ESTADO_CONTROL nBoton9, "DISABLE";  nSwBoton9 = 0;
     |ESTADO_CONTROL nBoton10, "DISABLE"; nSwBoton10 = 0;
     |ESTADO_CONTROL nBoton12, "DISABLE"; nSwBoton12 = 0;

     |SI #8#3 = 300;  |O #8#3 = 370;  |O #8#3 = 320;
      |O #8#3 = 330;  |O #8#3 = 332;
         |ESTADO_CONTROL nBoton10,  "ENABLE";  nSwBoton10 = 1;
         |ESTADO_CONTROL nBoton12,  "ENABLE";  nSwBoton12 = 1;
     |FINSI;

     |SI #8#5 = 0;
         |ESTADO_CONTROL nBoton2, "ENABLE";  nSwBoton2 = 1;
         |ACABA;
     |FINSI;

     |SI #8#3 = 349;
         |ESTADO_CONTROL nBoton2, "ENABLE";  nSwBoton2 = 1;
         |ESTADO_CONTROL nBoton3, "ENABLE";  nSwBoton3 = 1;
         |ACABA;
     |FINSI;

     |SI #8#3 = 110;  |O #8#3 = 115;  |O #8#3 = 123; |O #8#3 = 216;
         |ESTADO_CONTROL nBoton2, "ENABLE";  nSwBoton2 = 1;
         |ESTADO_CONTROL nBoton3, "ENABLE";  nSwBoton3 = 1;

         |SI #8#5 ! 99;
             |ESTADO_CONTROL nBoton7, "ENABLE";  nSwBoton7 = 1;

             |SI #8#3 = 115;
                 |ESTADO_CONTROL nBoton9, "ENABLE";  nSwBoton9 = 1;
             |FINSI;
         |SINO;
             |ESTADO_CONTROL nBoton5, "ENABLE";  nSwBoton5 = 1;
         |FINSI;

         |ESTADO_CONTROL nBoton8, "ENABLE";  nSwBoton8 = 1;
         |ACABA;
     |FINSI;

     |SI #8#5 ! 0;
         |SI #8#7 = "COMUNIDAD ";
             |ESTADO_CONTROL nBoton2, "ENABLE";  nSwBoton2 = 1;
             |ACABA;
         |FINSI;

         |SI #8#7 = "MODULOS   ";
             |ESTADO_CONTROL nBoton2, "ENABLE";  nSwBoton2 = 1;
             |ESTADO_CONTROL nBoton4, "ENABLE";  nSwBoton4 = 1;
             |ESTADO_CONTROL nBoton8, "ENABLE";  nSwBoton8 = 1;

             |SI #8#3 = 310;
                 |ESTADO_CONTROL nBoton7, "ENABLE";  nSwBoton7 = 1;
             |FINSI;

             |ACABA;
         |FINSI;

         |SI #8#7 = "PLANILLAS ";
             |ESTADO_CONTROL nBoton2, "ENABLE";  nSwBoton2 = 1;
             |ESTADO_CONTROL nBoton4, "ENABLE";  nSwBoton4 = 1;

             |ACABA;
         |FINSI;

         |SI #8#7 = "MANUAL    ";
             |ESTADO_CONTROL nBoton2, "ENABLE";  nSwBoton2 = 1;
             |ESTADO_CONTROL nBoton3, "ENABLE";  nSwBoton3 = 1;
             |ESTADO_CONTROL nBoton8, "ENABLE";  nSwBoton8 = 1;

             |SI #8#5 = 99;
                 |SI #8#3 = 300;  |O #8#3 = 320;  |O #8#3 = 330;  |O #8#3 = 332;  |O #8#3 = 370;
                     |SI #8#3 = 300;  |O #8#3 = 370;
                         |SI #8#2 ! 3;
                             |ESTADO_CONTROL nBoton6, "ENABLE";  nSwBoton6 = 1;
                         |FINSI;
                     |SINO;
                         |SI #8#2 ! 1;
                             |ESTADO_CONTROL nBoton6, "ENABLE";  nSwBoton6 = 1;
                         |FINSI;
                     |FINSI;
                 |FINSI;
             |SINO;
                 |SI #8#3 = 300;
                     |ESTADO_CONTROL nBoton9, "ENABLE";  nSwBoton9 = 1;
                 |FINSI;
             |FINSI;

             |ACABA;
         |FINSI;

         |SI #8#7 = "REGISTROS ";
             |ESTADO_CONTROL nBoton2, "ENABLE";  nSwBoton2 = 1;
             |ESTADO_CONTROL nBoton7, "ENABLE";  nSwBoton7 = 1;
             |ESTADO_CONTROL nBoton8, "ENABLE";  nSwBoton8 = 1;

             |SI #107#3 = "S";
                 |ESTADO_CONTROL nBoton3, "ENABLE";  nSwBoton3 = 1;
                 |ACABA;
             |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo11m008;  |TIPO 11;
     nTecla = FSalida;

     |HAZ FinBotonm008;

     enEmpresa     = #8#0;
     enEjer        = #8#1;
     enPeriodo     = #8#2;
     enModelo      = #8#3;
     enComplem     = #8#4;
     enActividad   = #8#5;
     eaComunidad   = #8#9;
     eaComunero    = #8#10;
     eaCaptura     = #8#7;
     eaNombreAct   = #8#6;

|| \\\\\ Consulta Modelos ////////

     |SI nTecla = 10;  |Y nSwBoton2 = 1;
         enTipoEntrada = 4;
         |HAZ EntraModelo;    || Esta en el PLB
         |HAZ Refresco;
     |FINSI;

|| \\\\\ Alta o Modificacion Modelos ////////

     |SI nTecla = 11;  |Y nSwBoton3 = 1;
         |HAZ AltaModelo8;
         |HAZ Refresco;
     |FINSI;

|| \\\\\ Alta o Modificacion Planillas y Modulos ////////

     |SI nTecla = 12;  |Y nSwBoton4 = 1;
         |HAZ AltaModelo8;
         |HAZ Refresco;
     |FINSI;

|| \\\\\ Recoger NIF Regularizacion Anterior ////////

     |SI nTecla = 13;  |Y nSwBoton5 = 1;
         enAnterior = 1;
         |HAZ RecogeModelo;
         |HAZ Refresco;
         enAnterior = 0;
     |FINSI;

|| \\\\\ Compensar Regularizacion ////////

     |SI nTecla = 14;  |Y nSwBoton6 = 1;
         |HAZ RecogeModelo;
         |HAZ Refresco;
         enAnterior = 0;
     |FINSI;

|| \\\\\ Recoger Datos ////////

     |SI nTecla = 15;  |Y nSwBoton7 = 1;
         |HAZ RecogeModelo;
         |HAZ Refresco;
         enAnterior = 0;
     |FINSI;

|| \\\\\ Baja Modelos ////////

     |SI nTecla = 16;  |Y nSwBoton8 = 1;
         |HAZ BajaModelo8;
         |HAZ Refresco;
     |FINSI;

|| \\\\\ Recoger Datos Perido Anterior ////////

     |SI nTecla = 17;  |Y nSwBoton9 = 1;
         |SI #8#3 = 115;
             enAnterior = 1;
             |HAZ RecogeModelo;
             |HAZ Refresco;
             enAnterior = 0;
         |FINSI;

         |SI #8#3 = 300;  |Y  #8#7 = "MANUAL    ";
             |CONFI "0000NNo deben tener Adquisiciones ni Entregas Intracomunitarias, Continuar ";
             |SI FSalida ! 0;  |ACABA;  |FINSI;
             enAnterior = 1;
             |HAZ RecogeModelo;
             |HAZ Refresco;
             enAnterior = 0;
         |FINSI;
     |FINSI;

     |SI nTecla = 18; |Y nSwBoton10 = 1;
         |CONFI "    NCalcular la Prorrata Definitiva de Esta Empresa S/N";
         |SI FSalida = 0;
             enSinPantalla = 4;
             |SUB_EJECUTA_NP "dsmozp80";
             enSinPantalla = 0;
         |FINSI;
     |FINSI;
     |SI nTecla = 20; |O nTecla = 9;
         |SI nSwBoton12 = 1;
             |CONFI "    NIntroducir Prorrata Provisional S/N";
             |SI FSalida = 0;
                 |SUB_EJECUTA_NP "dsmozp82";
             |FINSI;
         |FINSI;
     |FINSI;
|FPRC;
