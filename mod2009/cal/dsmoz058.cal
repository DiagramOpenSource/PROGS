|FICHEROS;
     dsmoz058 #0;
     dsmom040 #40;
     dsmom041 #41;
     dsmom042 #42;
     dsmom044 #44;
     dsmom045 #45;
     dsmom046 #46;

     dsmom100 #100;
     :/basica/def/agifigen #1000;
     :/contagen/def/canempre #330;
     :/contagen/def/anm00014 #324;
     :/contagen/def/anm00015 #325;
     :/contagen/def/cafinanc #360;

     dsmow106 #906,MANTE;
     dsow106@ #900;
|FIN;

|VARIABLES;
  sw         = 0;
  aFichero   = "";
  Linea      = 0;
  total      = 0;
  fHFecha    = @;
  nEjer      = 0;
  aAlfa1     = "";
  nswerror   = 0;
  aParam     = "";
  nLaEmpresa = 0;
  nInkey     = 0;
  nErGrupo   = 0;
  nGrupo     = 0;
  nYaExiste  = 0;
  aDef       = "";
  aMas       = "";
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;
|INCLUYE i_numamo;
|| /////////////////////// PROCESOS DE TRABAJO \\\\\\\\\\\\\\\\\\\\
|CALCULO  VerEmpresa; |TIPO 0;  ||Desde Numero Empresa
                     ||Busco la empresa

   enEmpresa = #0#0;
   |HAZ LEmpresaBas;
   |SI nswerror = 1;   || No existe ninguna empresa
       |MENAV "    Error. No existe Empresa.";
       |ERROR;
   |SINO;
        #0#1 = #1000#1; |PINTA #0#1;
   |FINSI;
|FCAL;

|CALCULO numamo; |TIPO 0;
    x_alfa1 = #0Campo;
    |HAZ x_punto;
    #0Campo = x_alfa1;
    |PINTA #0Campo;
|FCAL;

|CALCULO cuenta; |TIPO 0;
     salidas  = FSalida;

     |ABRE #100;
     |LEE 040#100p;
     |SI FSalida ! 0;  |DDEFECTO #100;  |FINSI;
     |CIERRA #100;

     nLaEmpresa = enEmpresa;
     enEmpresa  = #100#1;
     enPerConta = #100#2;

     eaCuenta = #0Campo;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;
     enEmpresa = nLaEmpresa;
|FCAL;

|| ////////////////// Procesos  \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|RUTINA ClaveBaja906;
  #906#0 = enEmpresa;
  #906#2 = "        ";
  #906#3 = 00;
|FRUT;

|RUTINA ClaveAlta906;
  #906#0 = enEmpresa;
  #906#2 = "zzzzzzzz";
  #906#3 = 99;
|FRUT;

|| *************************************************************************
|PROCESO ElCambio; |TIPO 11;
  nInkey = FSalida;

  |SI nInkey = 10;
     |LEE 141#906=;

     |SI aParam = "C"; |ENCAMPO #14#906; |FINSI;
     |SI aParam = "G"; |ENCAMPO #15#906; |FINSI;

     #906#16 = "S"; |PINTA #906#16;
     |ENCAMPO #16#906;

     |GRABA 000#906a;
     |LIBERA #906;
     |ERROR;
 |FINSI;
 |SI nInkey = 11;
     |LEE 141#906=;
     |SI #906#16 = "N";
         |SI aParam = "C"; |Y #906#14 ! "        ";
             #906#16 = "S"; |PINTA #906#16;
         |FINSI;
         |SI aParam = "G"; |Y #906#15 ! 0;
             #906#16 = "S"; |PINTA #906#16;
         |FINSI;
     |SINO;
          #906#16 = "N"; |PINTA #906#16;
     |FINSI;
     |GRABA 000#906a;
     |LIBERA #906;
     |ERROR;
 |FINSI;
|FPRC;

|PROCESO inf41;
 #41#0 = #906#0;
 #41#2 = "        ";
 #41#3 = 00;
|FPRC;

|PROCESO sup41;
 #41#0 = #906#0;
 #41#2 = "zzzzzzzz";
 #41#3 = 99;
|FPRC;

|CALCULO Elemento; |TIPO 0;
   nInkey = FSalida;
   |C_M #906Campo, 0, aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   |SI nInkey = 10;
      |ABRE #41;
      #41#0 = #906#0;
      #41#2 = #906#2;
      #41#3 = #906#3;
      |CONSULTA_F_CLAVE #1#41, inf41, sup41;
      |CIERRA #41;
   |FINSI;

   |SI nInkey = 11;
       #906#14 = #906#2; |PINTA #906#14;
   |FINSI;

   x_alfa1 = #906Campo;
   |QBF x_alfa1; |SI x_alfa1 = ""; |ERROR; |ACABA; |FINSI;
   x_alfa1 = #906Campo;
   |HAZ x_punto;
   #906Campo = x_alfa1;
   |PINTA #906 Campo;

   |HAZ MiroSiYaEsta;
   |SI nYaExiste = 1;
       #906#14 = ""; |PINTA #906#14;
       |ERROR;
   |FINSI;
|FCAL;

|PROCESO inf40;
 #40#0 = #906#0;
 #40#2 = 0001;
|FPRC;

|PROCESO sup40;
 #40#0 = #906#0;
 #40#2 = 9999;
|FPRC;

|PROCESO VeoGrupo;
 nErGrupo = 0;
 |SI nInkey = 10;
     |ABRE #40;
     |CONSULTA_F_CLAVE #1#40, inf40, sup40;
     #906Campo = #40#2; |PINTA #906Campo;
     |CIERRA #40;
 |FINSI;

 |ABRE #40;
 |DDEFECTO #40;
 #40#0 = enEmpresa;
 #40#2 = nGrupo;
 |LEE 000#40=;
 |SI FSalida ! 0; nErGrupo = 1; |FINSI;
 |CIERRA #40;
|FPRC;

|CALCULO Grupo; |TIPO 0;
 |C_M #906Campo, 0, aAlfa1;
 |SI aAlfa1 = "N"; |ACABA; |FINSI;

 nInkey = FSalida;
 nGrupo = #906Campo;

 |HAZ VeoGrupo;
 |SI nErGrupo ! 0;
     |MENAV "    No existe Grupo de Amortizacion ";
     |ERROR;
 |FINSI;

|FCAL;
|| ***********************************************************************
|PROCESO LinGrupo;
     |BORRA 140#325a;
     |LIBERA #325;

     #325#0 = #906#0;
     #325#1 = #906#14;
     #325#2 = #906#3;
     |GRABA 020#325n;
|FPRC;

|DEFBUCLE LinGrupo;
     #325#1;
     ;
     #906#0, #906#2, #906#3, 000;
     #906#0, #906#2, #906#3, 999;
     be;
     NULL, LinGrupo;
|FIN;

|PROCESO Cambio41_grupo;

  |SI #906#15 = 0; |O #906#15 = #906#12; |ACABA; |FINSI;

  #41#0  = #906#0;
  #41#2  = #906#2;
  #41#3  = #906#3;
  |LEE 101#41=;
  |SI FSalida = 0;
      #41#18 = #906#15;
      |GRABA 020#41a;
  |FINSI;
  |LIBERA #41;
|FPRC;

|PROCESO dsmom046;
 |BORRA 020#46a;
 |LIBERA #46;
 #46#0 = #906#0;
 #46#2 = #906#14;
 #46#3 = #906#3;
 |GRABA 000#46n;
|FPRC;

|DEFBUCLE dsmom046;
 #46#1;
 ;
 #906#0, #906#2, #906#3, 01;
 #906#0, #906#2, #906#3, 99;
 be;
 NULL, dsmom046;
|FIN;

|PROCESO dsmom042;
 |BORRA 020#42a;
 |LIBERA #42;
 #42#0 = #906#0;
 #42#2 = #906#14;
 #42#3 = #906#3;
 |GRABA 000#42n;
|FPRC;

|DEFBUCLE dsmom042;
 #42#1;
 ;
 #906#0, #906#2, #906#3, 001;
 #906#0, #906#2, #906#3, 999;
 be;
 NULL, dsmom042;
|FIN;

|PROCESO Cambio41_codigo;
  |SI #906#14 = "        "; |O #906#14 = #906#2; |ACABA; |FINSI;

  #41#0 = #906#0;
  #41#2 = #906#14;
  #41#3 = #906#3;
  |LEE 001#41=;
  |SI FSalida = 0;
      |PUSHV 1010 1452;
      |PINTA 1111, "                                         ";
      |PINTA 1211, "                                         ";
      |PINTA 1311, "                                         ";
      |PINTA 1111, " NO SE HA CAMBIADO EL ELEMENTO ";
      |PINTA 1142, #906#2;
      |PINTA 1211, " EL NUEVO CODIGO ";
      |PINTA 1228, #906#14;
      |PINTA 1238, "YA EXISTE.";
      |CUADRO 1010 1451;
      |PAUSA;
      |POPV;
      |ACABA;
  |FINSI;

  #41#0  = #906#0;
  #41#2  = #906#2;
  #41#3  = #906#3;
  |LEE 101#41=;
  |SI FSalida = 0;
      #41#0 = #906#0;
      #41#2 = #906#14;
      #41#3 = #906#3;
      |GRABA 020#41n;

      #44#0 = #906#0;
      #44#2 = #906#2;
      #44#3 = #906#3;
      |LEE 041#44=;
      |SI FSalida = 0;
          |BORRA 020#44a;
          #44#0 = #906#0;
          #44#2 = #906#14;
          #44#3 = #906#3;
          |GRABA 000#44n;
      |FINSI;
      |LIBERA #44;

      #45#0 = #906#0;
      #45#2 = #906#2;
      #45#3 = #906#3;
      |LEE 041#45=;
      |SI FSalida = 0;
          |BORRA 020#45a;
          #45#0 = #906#0;
          #45#2 = #906#14;
          #45#3 = #906#3;
          |GRABA 000#45n;
      |FINSI;
      |LIBERA #45;

      |BUCLE dsmom042;
      |BUCLE dsmom046;

      aAlfa1 = #41#34; aAlfa1 = aAlfa1 % 103;
      |SI aAlfa1 = "GRU";
          |ABRE #324;
          #324#0 = #906#0;
          #324#1 = #906#2;
          #324#2 = #906#3;
          |LEE 101#324=;
          |SI FSalida = 0;
              |BORRA 020#324a;
              #324#0 = #906#0;
              #324#1 = #906#14;
              #324#2 = #906#3;
              |GRABA 000#324n;

              |BUCLE LinGrupo;
          |FINSI;
          |LIBERA #324;
          |CIERRA #324;
      |FINSI;
      |SI #41#36 ! 0;
          |ABRE #360;
          #360#0 = #41#0;
          #360#1 = #41#36;
          |LEE 101#360=;
          |SI FSalida = 0;
              #360#44 = #906#14;
              #360#45 = #906#3;
              |GRABA 020#360a;
          |FINSI;
          |LIBERA #360;
          |CIERRA #360;
       |FINSI;

      |LIBERA #41;
      #41#0 = #906#0;
      #41#2 = #906#2;
      #41#3 = #906#3;
      |LEE 101#41=;
      |SI FSalida = 0;
          |BORRA 020#41a;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO Cambio41;
 |SI aParam = "G"; |HAZ Cambio41_grupo; |FINSI;
 |SI aParam = "C"; |HAZ Cambio41_codigo; |FINSI;
|FPRC;

|DEFBUCLE Cambio41;
 #906#1;
 16;
 #0#0, "        ", 00, "S";
 #0#0, "zzzzzzzz", 99, "S";
 e;
 NULL, Cambio41;
|FIN;

|| ***********************************************************************
|PROCESO HagoElCambio;
 |ABRE #41;
 |ABRE #44;
 |BUCLE Cambio41;
 |CIERRA #44;
 |CIERRA #41;
|FPRC;

|| *************************************************************************
|PROCESO GrabaTemp1;

  |SI #0#13 = "N";
      |SI #41#35 = "S"; |ACABA; |FINSI;
  |FINSI;
  |SI #0#14 ! "T";
      |SI #0#14 ! #41#40; |ACABA; |FINSI;
  |FINSI;
  |SI #41#18 < #0#11; |O #41#18 > #0#12; |ACABA; |FINSI;

  #906#0 = #41#0;
  #906#1 = #41#1;
  #906#2 = #41#2;
  #906#3 = #41#3;
  |LEE 101#906=;
  |SI FSalida ! 0;
      #906#0 = #41#0;
      #906#1 = #41#1;
      #906#2 = #41#2;
      #906#3 = #41#3;
      #906#4 = #41#27;
      #906#13 = #41#4;
      |GRABA 020#906b;
  |FINSI;

  aAlfa1 = #41#27;
  aAlfa1 = #41#27; aAlfa1 = aAlfa1 % -104;
  enEjerCam  = aAlfa1;
  #906#5 = #41#5;
  #906#8 = #41#5;
  |SI enEjerCam < 2008; |Y enPer2008 ! -1;
      eaCuenta  = #906#5;
      |HAZ CambioCta;
      #906#8 = eaCta2008;
  |FINSI;
  #906#12 = #41#18;
  |SI aParam = "C";
      #906#15 = #41#18;
  |SINO;
      #906#14 = #41#2;
  |FINSI;
  #906#16 = "N";
  |GRABA 020#906a;
  |LIBERA #906;
  sw = 1;
|FPRC;

|DEFBUCLE dsmoz058;
 #41#1;
 5,1;
 #0#0, #0#5, #0#7, #0#3, #0#9;
 #0#0, #0#6, #0#8, #0#4, #0#10;
 ;
 NULL,GrabaTemp1;
|FIN;

|PROCESO Tipo3;  |TIPO 3;

   aFichero = ("6w" + Usuario) % 108;
   |NOME_DAT #906 aFichero;
   |DELINDEX #906;

   |HAZ AntCambioCta;
   enEmpresa = #0#0;

   |ABRE #906;
   sw = 0;
   |BUCLE dsmoz058;
   |CIERRA #906;

   |SI sw ! 0;
       |PINPA #0#906;
       |PINTA 0610, #0#0;
       |PINTA 0616, #0#1;
       |SI aParam = "C"; aAlfa1 = "CODIGO"; |FINSI;
       |SI aParam = "G"; aAlfa1 = " GRUPO "; |FINSI;
       |ATRI R; |PINTA 0867, aAlfa1;
       |ENTLINEAL #1#906, -2, 4, 22, 0, ClaveBaja906, ClaveAlta906;
       |SI FSalida = 0;
           |CONFI "    SSon correctos los datos Introducidos";
           |SI FSalida = 0;
               |HAZ HagoElCambio;
           |FINSI;
       |FINSI;
   |SINO;
       |MENAV "    Seleccion vacia";
   |FINSI;

   |DELINDEX #906;
|FPRC;

|| ....................... Situa la Empresa Contable
|PROCESO LEmpresaBas;
  nswerror = 0;
  |ABRE #1000;
  |DDEFECTO #1000;
  #1000#0 = #0#0;
  |LEE 001#1000=;
  |SI FSalida ! 0; nswerror = 1; |FINSI;
  |CIERRA #1000;
|FPRC;

|PROGRAMA;
 aParam = PARAMETRO;

 |CLS;
 |CABEZA "Cambio Datos Amortizacion";
 |PINPA #0#0;
 |SI aParam = "C";
     aAlfa1 = "Cambiar Codigo de los Elementos Amortizables";
     |C_M #906#15, 1, "N"; |C_V #906#15, 1, "N";
 |SINO;
     aAlfa1 = "Cambiar Grupo Amortizacion en los Elementos";
     |C_M #906#14, 1, "N"; |C_V #906#14, 1, "N";
 |FINSI;
 |ATRI R; |PINTA 0603, aAlfa1; |ATRI 0;
 |PINDA #0#0;

 |ENDATOS #1#0;
|FPRO;

|PROCESO LeoEspejo;
 |SI #906#2 ! #900#2; |Y #906#3 ! #900#3;
     nYaExiste = 1;
 |FINSI;
|FPRC;

|DEFBUCLE LeoEspejo;
 #900#1003;
 14;
 #906#0, "        ", 00, #906#14;
 #906#0, "zzzzzzzz", 99, #906#14;
 ;
 NULL, LeoEspejo;
|FIN;

|PROCESO MiroSiYaEsta;
 nYaExiste = 0;

 |SI #906#2 = #906#14; |ACABA; |FINSI;

 |DBASE aMas;
 aDef = aMas + "def/dsmow106";
 |CARGA_DEF aDef, dsow106@;
 |SI FSalida ! 0;
     aDef = "";
     |MENAV "    Error al Carga fichero Auxiliar de Copia";
     |ACABA;
 |FINSI;
 |NOME_DAT #900 aFichero;

 |ABRE #41;
 #41#0 = #906#0;
 #41#2 = #906#14;
 #41#3 = #906#3;
 |LEE 001#41=;
 |SI FSalida = 0;
     nYaExiste = 1;
     |ABRE #900;
     #900#0 = #41#0;
     #900#2 = #41#2;
     #900#3 = #41#3;
     |LEE 000#900=;
     |SI FSalida = 0;
         |SI #900#14 > "        "; |Y #900#14 ! #906#14;
             nYaExiste = 0;
         |FINSI;
     |FINSI;
     |CIERRA #900;
 |FINSI;
 |CIERRA #41;

 |SI nYaExiste = 1;
     |MENAV "    Error. Ya existe un Elemento con este Codigo";
     |VETE VeteFin;
 |FINSI;

 |BUCLE LeoEspejo;
 |SI nYaExiste = 1;
     |CONFI "    NCodigo Nuevo ya asignado a otro Elemento, ¨ Es correcto ?";
     |SI FSalida = 0; nYaExiste = 0; |FINSI;
 |FINSI;

|ET VeteFin;
 |SI aDef ! ""; |DESCARGA_DEF #900; aDef = "";|FINSI;
|FPRC;
