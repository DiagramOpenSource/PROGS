|FICHEROS;
    dsmoz053 #0;

    dsmom041 #41;
    dsmom042 #42;
|FIN;

|VARIABLES;
  nDEmpresa   = 0;
  nHEmpresa   = 0;
  nDActividad = 0;
  nHActividad = 0;
  nCampo      = 0;

  &nCamSel    = 18;
  &nCamTip    = 31;

  ele1        = "";
  ele2        = "";
  nnum1       = 0;
  nnum2       = 0;
  Dtipo       = "A";
  Htipo       = "A";

  aAlfa1      = "";

  afiscal     = 0;
  acontab     = 0;
  sw          = 0;
  nLaEmpresa  = 0;
  swBueno     = 0;
  nSwCambia   = 0;

  nAcumContab = 0;
  nVAnyo      = 0;
  aT          = "";
  nCamLin     = 0;
  fFechaD     = @;
  aSiCont     = "";
  fPrimLinea  = @;
  nPrimLinea  = 0;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;
|INCLUYE i_selcon;
|INCLUYE i_numamo;
|INCLUYE i_calamo;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\
|CALCULO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#4 = enEmpresa; |PINTA #0#4;
         #0#5 = enEmpresa; |PINTA #0#5;

         |C_M #0#4, 1, "N";
         |C_M #0#5, 1, "N";
     |FINSI;
|FCAL;

|CALCULO Parrilla;
     |SI #0#4 = 0;
         #0#4 = 0;  |PINTA #0#4;
         #0#5 = 0;  |PINTA #0#5;  |C_M #0#5, 1, "N";
         |PARA nCampo = 6;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#4, 1, "S";
             |C_M #0#5, 1, "S";
         |FINSI;

         |PARA nCampo = 6;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO SiRecalF; |TIPO 0;
 aAlfa1 = #0#32;
 |C_M #0#33,1,aAlfa1;
 |C_M #0#34,1,aAlfa1;
|FCAL;

|CALCULO SiRecalC; |TIPO 0;
 aAlfa1 = #0#35;
 |C_M #0#36,1,aAlfa1;
 |SI aAlfa1 = "S"; |Y #0#32 = "S";
     #0#36 = #0#33; |PINTA #0#36;
 |FINSI;
|FCAL;

|CALCULO numamo; |TIPO 0;
   x_alfa1 = #0Campo;
   |HAZ x_punto;
   #0Campo = x_alfa1;
   |PINTA #0Campo;
|FCAL;

|CALCULO ElModulo; |TIPO 0;
 |SI #0#42 = "S";
     |C_M #0#43, 1, "S";
 |SINO;
     |C_M #0#43, 1, "N";
     #0#43 = "0"; |PINTA #0#43;
 |FINSI;
|FCAL;

|CALCULO Elvisto; |TIPO 0;
 |SI FSalida = 2; |ACABA; |FINSI;
 |SI #0#32 = "N"; |Y #0#35 = "N";
     |MENAV "    ATENCION. No ha elegido recalcular ninguna Amortizacion";
     |ERROR;
 |FINSI;
|FCAL;

|| /////////////////////////// Procesos \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO CalLineas;
 |SI aT = "F";
     amorcon = #42#11;
 |FINSI;

 fFechaAmor = #42#9;
 nAnyAmor   = #42#5;
 nImpAmor   = 0;
 |SI nRest ! 0;
     |HAZ Calculo;
 |FINSI;
 |SI vfecha > "01.01.0000";
     |SI nAnyAmor = nVAnyo; |Y nAnyAmor ] 2005; |Y aT = "F";
         nImpAmor = (nAcumContab - #41#15);
      |FINSI;
 |FINSI;

 #42nCamLin = nImpAmor;
 #42#13     = #42#11 - #42#7;
 |GRABA 020#42a;
 |LIBERA #42;

 aUltLinea   = "A";
 ultFechaA   = #42#9;
 |SI aT = "F";
     |SI #42#5 < nAnyFin;
         enAcumFiscal = enAcumFiscal + #42nCamLin;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE CalLineas;
   #42#2;
   ;
   #41#0, #41#2, #41#3, "A", fPrimLinea, nPrimLinea;
   #41#0, #41#2, #41#3, "A", efFecTope,  999;
   be;
   NULL, CalLineas;
|FIN;

|PROCESO ParaSumar;
  nAcum = nAcum + #42nCamLin;
  |SI #42#9 ] ultFechaA;
      ultFechaA = #42#9;
      aUltLinea = "A";
  |FINSI;
  |SI aT = "F";
      |SI #42#5 < nAnyFin;
          enAcumFiscal = enAcumFiscal + #42nCamLin;
      |FINSI;
  |FINSI;
  |SI aT = "C"; |Y #42#5 [ nVAnyo;
      nAcumContab = nAcumContab + #42nCamLin;
  |FINSI;
|FPRC;

|PROCESO VerLineas;

 |SI nPrimLinea = 0;

     |SI #42#9 < fFechaD;
         |HAZ ParaSumar;
         |ACABA;
     |FINSI;

     |SI aSiCont ! "T";
         |SI aSiCont ! #42#8;
             |HAZ ParaSumar;
             |ACABA;
         |FINSI;
     |FINSI;

     fPrimLinea = #42#9;
     nPrimLinea = #42#4;

 |SINO;

     |SI aT = "C";
         |SI #42#8 = "S";
             |MENAV "    Error. Existen lineas contabilizadas mayor a la elegida";
             nPrimLinea = 0;
             |LIBERA #42;
             |ERROR10;
             |ACABA;
         |FINSI;
     |FINSI;

 |FINSI;
|FPRC;

|DEFBUCLE VerLineas;
   #42#2;
   ;
   #41#0, #41#2, #41#3, "A", "01.01.0000", 001;
   #41#0, #41#2, #41#3, "A", "31.12.2099", 999;
   e;
   NULL, VerLineas;
|FIN;

|PROCESO xRecalcula;

  cfecha  = #41#9;
  importe = #41#10;
  vfecha  = #41#26;
  aAlfa1  = #41#26; aAlfa1 = aAlfa1 % -104; nVAnyo = aAlfa1; || a¤o venta
 enCodOpe = #41#36;
 fFinPago = #41#37;
 enMulFis = #41#39;
 aAlfa1   = fFinPago;
 aAlfa1   = aAlfa1 % -104;
 nAnyFin  = aAlfa1;
 enAcumFiscal = 0;

  eaTipoA = #41#13;
  enTP100 = #41#14;
  enAnys  = #41#17;
  nCamLin = 7;
  fFechaD = #0#33;
  aSiCont = #0#34;
  |SI aT = "C";
      eaTipoA = #41#19;
      enTP100 = #41#20;
      enAnys  = #41#23;
      nCamLin = 11;
      fFechaD = #0#36;
      aSiCont = "N";
      nAcumContab = 0;
  |FINSI;

  ultFechaA  = cfecha;
  aUltLinea  = "C";
  nAcum      = 0;
  nRest      = 0;
  fPrimLinea = "01.01.0000";
  nPrimLinea = 0;

  |BUCLE VerLineas;
  |SI nPrimLinea ! 0;
      nRest = importe - nAcum;
      |BUCLE CalLineas;
      nSwCambia   = 1;
  |FINSI;
|FPRC;

|| ***************************************************
|PROCESO L_Amortiza;
   afiscal = afiscal + #42#7;
   acontab = acontab + #42#11;

   |SI ultFechaA < #42#9; ultFechaA = #42#9; |FINSI;
   sw = 1;
|FPRC;

|DEFBUCLE RLineas;
   #42#1;
   6;
   #41#0, #41#2, #41#3, 000, "A";
   #41#0, #41#2, #41#3, 999, "A";
   e;
   NULL, L_Amortiza;
|FIN;

|PROCESO C_Amortiza;

   |SI nLaEmpresa ! #41#0;
       nLaEmpresa = #41#0;
       enEmpresa  = #41#0;
       swBueno = 0;
       |HAZ LaSelEmpresa;
       |SI enSwSelEmp = 1; swBueno = 1;  |FINSI;
   |FINSI;
   |SI swBueno = 1; |ACABA; |FINSI;

   |SI #0#41 ! "X";   ||Seleccion Anual / Periodica
       |SI #41#24 ! #0#41; |ACABA; |FINSI;
   |FINSI;

   |SI #0#42 = "N";
       |SI #41#28 ! 0;  |ACABA; |FINSI;
   |FINSI;
   |SI #0#42 = "S";
       |SI #41#28 = 0;  |ACABA; |FINSI;
       |SI #0#43 ! "T";
           |SI #41#28 ! #0#43; |ACABA; |FINSI;
       |FINSI;
   |FINSI;

   |SI #41#9 [ "01.01.1900";  |ACABA; |FINSI;

   cfecha  = #41#9;
   importe = #41#10;
   vfecha  = #41#26;

   #0#3 = #41#0; |PINTA #0#3;
   #0#1 = #41#2; |PINTA #0#1;
   #0#2 = #41#3; |PINTA #0#2;

   nSwCambia = 0;
   aT = "C"; |SI #0#35 = "S"; |HAZ xRecalcula; |FINSI;
   aT = "F"; |SI #0#32 = "S"; |HAZ xRecalcula; |FINSI;

   |SI nSwCambia = 1;
       ultFechaA = "01.01.0000";
       afiscal = 0;
       acontab = 0;
       sw = 0;
       |BUCLE RLineas;
       |SI sw = 1;
           #41#15 = afiscal;
           #41#16 = importe - afiscal; |SI #41#16 < 0; #41#16 = 0; |FINSI;
           #41#21 = acontab;
           #41#22 = importe - acontab; |SI #41#22 < 0; #41#22 = 0; |FINSI;
           |SI #41#26 > "01.01.1900";
               |SI ultFechaA ] #41#26;
                   #41#16 = 0;
                   #41#22 = 0;
               |FINSI;
           |FINSI;
           |GRABA 020#41a;
      |FINSI;
   |FINSI;
   |LIBERA #41;
|FPRC;

|DEFBUCLE dsmoz053;
   #41#1;
   1,35;
   nDEmpresa, ele1, nnum1, nDActividad, "N";
   nHEmpresa, ele2, nnum2, nHActividad, "N";
   e;
   NULL, C_Amortiza;
|FIN;

|CALCULO Tipo3;  |TIPO 3;

  |SI #0#0  = "N"; |ACABA; |FINSI;
  |SI #0#32 = "N"; |Y #0#35 = "N";  |ACABA;  |FINSI;

  |HAZ LeeEURODBMOD;
  nT600 = 100000;
  |SI EURODBMOD = 2;
      nT600 = (nT600 / 166.386) ! EURODBMOD;
  |FINSI;

   efFecTope = "31.12.2099";
   nDActividad = 00;
   nHActividad = 99;
   ele1  = #0#37;
   ele2  = #0#38;
   nnum1 = #0#39;
   nnum2 = #0#40;

  |SI #0#4 ! 0;
         nDEmpresa = #0#4;
         nHEmpresa = #0#5;
         nLaEmpresa = -1;
         swBueno = 0;
         |BUCLE dsmoz053;
     |SINO;
         |PARA nCampo = 6; |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               nDEmpresa = #0nCampo;
               nHEmpresa = #0nCampo;
               nLaEmpresa = -1;
               swBueno = 0;
               |BUCLE dsmoz053;
         |SIGUE;
     |FINSI;
|FCAL;
