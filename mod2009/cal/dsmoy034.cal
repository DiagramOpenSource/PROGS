|FICHEROS;
   dsmoy034 #0;

   dsmom017 #17;
   dsmom018 #18;
   dsmom019 #19;
   dsmom020 #20;
   dsmom021 #21;

   :/basica/def/agicen14 #114;
   :/basica/def/agifigen #118;
   :/basica/def/agim0019 #119;
   :/basica/def/agidanif #120;

   :/laboral/def/labtraba  #200;
   :/laboral/def/nomir003  #201;
   :/laboral/def/nomhicca  #202;
   :/laboral/def/nomauto1  #203;
   :/laboral/def/nomauto2  #204;

   dsmow032 #900;
   dsmow096 #901;
|FIN;

|VARIABLES;
   &entrada = 1;
   &eSwSelec  = 0;
   &eaDDepar  = "";
   &eaHDepar  = "";
   &eaDSubde  = "";
   &eaHSubde  = "";
   &efDFecha  = @;
   &efHFecha  = @;

   inform1    = "dsmoy034";
   inform2    = "dsmoyi34";
   informe    = "";
   aFichero   = "";
   nCampo     = 0;
   nDEmpresa  = 0;
   nHEmpresa  = 0;
   nDActividad = 0;
   nHActividad = 0;

   nSPin1      = 0;

   aAlfa  = "";
   aAlfa1 = "";
   aAlfa2 = "";
   aAlfa3 = "";
   nPara1 = 0;
   nPara2 = 0;
   nNume1 = 0;
   nNume2 = 0;
   nNume3 = 0;
   nNume4 = 0;

   SwHayDatos = 0;

   &nPagina   = 0;
   &SwPrim    = 0;
   &SwLinea   = 0;
   &eaTituloL = "";
   &aTexto    = "";
   &aTexTotal = "";
   nTipo      = 0;
   sw         = 0;
   nSw        = 0;

   nPin1    = 0;
   aPin1    = "";
   efPn10   = @;
   efPn11   = @;
   swExiste = 0;
   swNoExis = 0;

   efHBaja  = @;
   fLaFecha = @;
   fDFecTra = @;
   fHFecTra = @;
   fFecNaci = @;
   nDBaja   = 0;
   nHora    = 0;
   nDias    = 0;
   fResta1  = @;
   fResta2  = @;
   nTrabaja = 0;

   nVHoSem   = 0;
   nVHoras   = 0;
   aVHoras   = "";
   aPathLaboral = "";
   aBase     = "";
   nHandle   = 0;
   diasanyo  = 0;
   nEjer     = 0;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;


|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\
|CALCULO Eano; |TIPO 7;
 |SI #0#1 = 1900;
     fLaFecha = ~;
     aAlfa1 = fLaFecha; aAlfa1 = aAlfa1 % -104;
     #0#1 = aAlfa1;
     |PINTA #0#1;
 |FINSI;
|FCAL;

|CALCULO ElEjer; |TIPO 0;
 aAlfa1 = #0#1;
 aAlfa2 = "01.01." + aAlfa1; #0#47 = aAlfa2;
 aAlfa3 = "31.12." + aAlfa1; #0#48 = aAlfa3;
|FCAL;

|CALCULO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#2  = enEmpresa;  |PINTA #0#2;
         #0#3  = enEmpresa;  |PINTA #0#3;

         |C_M #0#2, 1, "N";
         |C_M #0#3, 1, "N";
     |FINSI;
|FCAL;

|CALCULO Parrilla; |TIPO 0;
     |SI #0#2 = 0;
         #0#2 = 0;  |PINTA #0#2;
         #0#3 = 0;  |PINTA #0#3;  |C_M #0#3, 1, "N";
         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#2, 1, "S";
             |C_M #0#3, 1, "S";
         |FINSI;

         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas; |TIPO 0;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO YaSeCual; |TIPO 7;
 |SI enEmpresa ! 0;
     #0#46 = "S"; |PINTA #0#46;
     #0#24 = "S"; |PINTA #0#24;
     #0#25 = 00;  |PINTA #0#25;
     #0#26 = 99;  |PINTA #0#26;
     |C_M #0#24, 1, "N";
     |C_M #0#25, 1, "N";
     |C_M #0#26, 1, "N";
     |C_M #0#46, 1, "N";
 |FINSI;
|FCAL;

|CALCULO SelTipo0; |TIPO 0;
 |SI #0#46 = "N";
     #0#25 = 0;   |PINTA #0#25;  |C_M #0#25, 1, "N";
     #0#26 = 99;  |PINTA #0#26;  |C_M #0#26, 1, "N";
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |SINO;
     |SI enEmpresa = 0;
         |C_M #0#25, 1, "S";
         |C_M #0#26, 1, "S";
     |FINSI;
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "N";
           #0nCampo = 0;  |PINTA #0nCampo;
     |SIGUE;
 |FINSI;
|FCAL;

|CALCULO SelTipo1;
 |SI #0Campo = 0;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           #0nCampo = 0;  |C_M #0nCampo, 1, "N";
     |SIGUE;
 |SINO;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |FINSI;
|FCAL;

|| **********************************************************************************
|PROCESO Texto;
     aTexto    = "";
     aTexTotal = "";

     |SI nTipo = 1;
         aTexto = "Personal Menor de 19 a¤os";
         aTexTotal = "Suma total personal menor 19 a¤os";
     |FINSI;

     |SI nTipo = 2;
         aTexto = "Personal minusvalido con el 33% o mas";
         aTexTotal = "Suma total personal minusvalido";
     |FINSI;

     |SI nTipo = 3;
         aTexto = "Personal con Contrato de aprendizaje ";
         aTexTotal = "Suma total personal en aprendizaje";
     |FINSI;

     |SI nTipo = 4;
         aTexto = "Personal en Formacion academica      ";
         aTexTotal = "Suma total personal academico";
     |FINSI;

     |SI nTipo = 5;
         aTexto = "Resto personal  ";
         aTexTotal = "Suma total Resto de Personal";
      |FINSI;

      SwPrim = 0;
      #0#50 = 0;
      #0#51 = 0;
|FPRC;

|PROCESO Total;
  SwLinea = 1;
  |PRINT;
  SwPrim = 0;
  #0#50 = 0;
  #0#51 = 0;
|FPRC;

|PROCESO LeeAux0;
  |SI sw = 0;
      sw = 1;
      nTipo = #901#5;
      |HAZ Texto;
  |FINSI;

  |SI nTipo ! #901#5;
      |HAZ Total;
      nTipo = #901#5;
      |HAZ Texto;
  |FINSI;
  SwLinea = 0;
  |PRINT;
  SwPrim = 1;

  #0#50 = #0#50 + #901#12;
  #0#51 = #0#51 + #901#13;

  #0#63 = #0#63 + #901#12;
  #0#64 = #0#64 + #901#13;
|FPRC;

|DEFBUCLE LeeAux;
 #901#1;
 ;
 #17#0, #17#1, #17#2, #17#3, 00, 0001;
 #17#0, #17#1, #17#2, #17#3, 99, 9999;
 ;
 NULL, LeeAux0;
|FIN;

|PROCESO Emision;
 #0#50 = 0; #0#51 = 0;
 #0#63 = 0; #0#64 = 0;
 nTipo   = 0;
 nPagina = 1;
 sw      = 0;
 |BUCLE LeeAux;
 |SI sw = 1;
     |HAZ Total;
     |PIEINF;
 |FINSI;
|FPRC;

|| /////////////////////////////////////////////////////////////////
|PROCESO GrabaAux901;
     nTrabaja = nTrabaja  + 1;

     |DDEFECTO #901;
     #901#0  = #17#0;
     #901#1  = #17#1;
     #901#2  = #17#2;
     #901#3  = #17#3;
     #901#4  = nTrabaja;
     #901#5  = nTipo;
     |LEE 001#901=;
     |SI FSalida ! 0;
         |DDEFECTO #901;
         #901#0  = #17#0;
         #901#1  = #17#1;
         #901#2  = #17#2;
         #901#3  = #17#3;
         #901#4  = nTrabaja;
         #901#5  = nTipo;
         |GRABA 020#901c;
     |FINSI;

     #901#6  = #200#2;  || Nombre
     #901#14 = #200#7;  || NIF
     #901#10 = fDFecTra;
     #901#11 = fHFecTra;
     #901#15 = fFecNaci;

     #901#7 = nHora;
     #901#8 = nDias;

|| Regla3: Unidad Horas Semana  - Unidad Horas Anuales - Dias A¤o
||         Horas Semana Trabajador  -   X           - Dias Trabajados
||         ----------------------------------------------------------
||                nVHoSem (40)  - nVHoras (1800) - diasanyo (365)
||                 #901#7       -    X(#901#9)   -  #901#8
||

     |SI #901#7 = nVHoSem; |Y #901#8 = diasanyo;
         #901#9 = nVHoras;
     |SINO;
         |SI #901#7 = nVHoSem; |Y #901#8 ! diasanyo;
             #901#9 = ((#901#8 * nVHoras) / diasanyo) ! 2;
         |SINO;
             |SI #901#7 ! nVHoSem; |Y #901#8 = diasanyo;
                 #901#9 = ((#901#7 * nVHoras) / nVHoSem) ! 2;
             |SINO;
                 #901#9 = ( (#901#7 * nVHoras * #901#8) / ( nVHoSem * diasanyo) ) ! 2;
             |FINSI;
         |FINSI;
     |FINSI;
     #901#12 = ((#901#9 / nVHoras)) ! 2;

     #901#13 = 1;
     |SI fHFecTra > "01.01.0000";  |Y fHFecTra [ efHBaja;
         #901#13 = 0;
     |SINO;
         |SI nVHoSem ! #901#7;
             #901#13 = (#901#7 / nVHoSem) ! 2;
         |FINSI;
     |FINSI;

     |SI #0#66 = "S";
         #901#16 = nDBaja;
         #901#18 = #901#9;
         |SI #901#16 ! 0;
             |HAZ NuevoCalculo;
         |FINSI;
     |FINSI;

     |GRABA 020#901a;
     SwHayDatos = 1;
|FPRC;
|| **************************************************************************

|PROCESO NuevoCalculo;
     #901#17 = #901#8 - #901#16;  ||dias a¤o menos Enf.

|| Regla3: Unidad Horas Semana  - Unidad Horas Anuales - Dias A¤o
||         Horas Semana Trabajador  -   X           - Dias Trabajados
||         ----------------------------------------------------------
||                nVHoSem (40)  - nVHoras (1800) - diasanyo (365)
||                 #901#7       -    X(#901#9)   -  #901#17
||

     |SI #901#7 = nVHoSem; |Y #901#17 = diasanyo;
         #901#18 = nVHoras;
     |SINO;
         |SI #901#7 = nVHoSem; |Y #901#17 ! diasanyo;
             #901#18 = ((#901#17 * nVHoras) / diasanyo) ! 2;
         |SINO;
             |SI #901#7 ! nVHoSem; |Y #901#17 = diasanyo;
                 #901#18 = ((#901#7 * nVHoras) / nVHoSem) ! 2;
             |SINO;
                 #901#18 = ( (#901#7 * nVHoras * #901#17) / ( nVHoSem * diasanyo) ) ! 2;
             |FINSI;
         |FINSI;
     |FINSI;
     #901#12 = ((#901#18 / nVHoras)) ! 2;
|FPRC;

|PROCESO PorAutonomo;
 |ABRE #204;
 #204#0 = #200#0;
 #204#1 = #200#1;
 #204#2 = enEjer;
 |LEE 001#204=;
 |SI FSalida = 0;
     nDBaja = #204#33 + #204#34 + #204#35 + #204#36 + #204#37 + #204#38 + #204#39;
     nDBaja = nDBaja  + #204#40 + #204#41 + #204#42 + #204#43 + #204#44;
 |FINSI;
 |CIERRA #204;
|FPRC;

|PROCESO PorHistorico;
  nDBaja = nDBaja + (#202#31 + #202#32 + #202#65);
|FPRC;

|DEFBUCLE PorHistorico;
 #202#1;
 ;
 #200#0, #200#1, nEjer,  1, 0;
 #200#0, #200#1, nEjer, 12, 0;
 ;
 NULL, PorHistorico;
|FIN;
|| **************************************************************************
|PROCESO LeeTrabajador;

 fHFecTra = "01.01.0000";
 fDFecTra = #200#36;
 |SI fDFecTra > efHFecha; |ACABA; |FINSI;  || no esta de alta en periodo

 aAlfa1 = #200#37; |QBF aAlfa1;
 |SI aAlfa1 ! "";
     fHFecTra = #200#37;
     |SI fHFecTra < efDFecha; |ACABA; |FINSI;  || no esta de alta en periodo
 |FINSI;

 fFecNaci = #200#11;

 nHora = #200#234; |SI nHora = 0; nHora = nVHoSem; |FINSI;  || la unidad

 fResta1 = efDFecha;
 |SI fDFecTra > efDFecha; fResta1 = fDFecTra; |FINSI;
 fResta2 = efHFecha;
 |SI fHFecTra < efHFecha; |Y fHFecTra > "01.01.1900"; fResta2 = fHFecTra; |FINSI;
 nDias = fResta2 - fResta1 + 1;

 |ABRE #201;
 |DDEFECTO #201;
 #201#0 = #200#7;
 |LEE 001#201=;
 |CIERRA #201;

 nTipo  = 5;
 nNume1 = 99;
 aAlfa1 = fFecNaci; |QBF aAlfa1;
 |SI aAlfa1 ! "";
     aAlfa2 = aAlfa1 % -104;
     nNume1 = aAlfa2;
     nNume1 = enEjer - nNume1;
 |FINSI;

 |SI nNume1 < 19;
     nTipo = 1;
 |SINO;
     |SI #201#6 = "S";
         nTipo = 2;
     |SINO;
         nSw = 0;
         |PARA nPara1 = 52; |SI nPara1 [ 61; |HACIENDO nPara1 = nPara1 + 1;
               |SI #0nPara1 ! 0; |Y #0nPara1 = #200#45;
                    nSw = nPara1;
                    |SAL;
               |FINSI;
         |SIGUE;
         |SI nSw ! 0;
             nTipo = 3;
             |SI nSw > 56; nTipo = 4; |FINSI;
         |FINSI;
     |FINSI;
 |FINSI;

  nDBaja     = 0;
  |SI #0#66 = "S";
      |SI #200#42 = "H";
          |HAZ PorAutonomo;
      |SINO;
          |BUCLE PorHistorico;
      |FINSI;
  |FINSI;
  |HAZ GrabaAux901;
|FPRC;

|DEFBUCLE LeeTrabajadores;
 #200#1;
 ;
 #17#0, 00001;
 #17#0, 99999;
 ;
 NULL, LeeTrabajador;
|FIN;


|PROCESO Imprime;

 enEmpresa   = #17#0;
 enActividad = #17#3;
 |HAZ LeeDatosEmpresa;
 |SI swNoExis = 1;
     |ACABA;
 |FINSI;

 eaEstadoEmp = "S";
 |SI #agifigen#94 > "01.01.0000";
     eaEstadoEmp = "N";
     |SI #0#24 = "N"; |ACABA; |FINSI;
 |FINSI;

|| ... Seleccion de Tipo de Empresa
  nSPin1 = #agifigen#87;
  enSwSelEmp = 1;
  |SI #0#46 = "S";
      |SI nSPin1 ] #0#25; |Y nSPin1 [ #0#26;
          enSwSelEmp = 0;
      |FINSI;
  |SINO;
      |SI nSPin1 = #0#27;
          enSwSelEmp = 0;
      |SINO;
          |PARA nCampo = 28;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
                |SI nSPin1 = #0nCampo; |Y nSPin1 ! 0;
                    enSwSelEmp = 0;
                    |SAL;
                |FINSI;
          |SIGUE;
      |FINSI;
  |FINSI;
  |SI enSwSelEmp = 1; |ACABA; |FINSI;
|| ------------------------------------------------------------------------

  |DELINDEX #901;

  SwHayDatos = 0;
  nTrabaja   = 0;

  |ABRE #901;
  |BUCLE LeeTrabajadores;
  |CIERRA #901;

  |SI SwHayDatos = 1;
      |HAZ Emision;
  |FINSI;

|FPRC;

|DEFBUCLE Leem017;
 #17#1;
 ;
 nDEmpresa, #0#1, 00, #0#38;
 nHEmpresa, #0#1, 99, #0#39;
 ;
 NULL, Imprime;
|FIN;

||///////////////////////////////////////////////////////////////////
|CALCULO Tipo3;  |TIPO 3;
    #0#0  = "N"; ||  No es Inmovilizado

    |HAZ LeeEURODBMOD;

    aAlfa1 = #0#1; aAlfa1 = aAlfa1 % -102;
    nEjer  = aAlfa1;

    |PATH_DAT #202 aPathLaboral;
    |PATH_DAT #203 aPathLaboral;
    |PATH_DAT #204 aPathLaboral;

    nVHoSem   = #0#65;    || Unidad de horas a la semana = 40
    nVHoras   = #0#49;    || Unidad de horas al a¤o = 1800
    aVHoras   = nVHoras; |QBF aVHoras;
    enEjer    = #0#1;
    diasanyo = 365;       || Unidad de dias al a¤o
    |SI enEjer = 1980;  |O enEjer = 1984;  |O enEjer = 1988;  |O enEjer = 1992;
                        |O enEjer = 1996;  |O enEjer = 2000;  |O enEjer = 2004;
                        |O enEjer = 2008;  |O enEjer = 2012;  |O enEjer = 2016;
                        |O enEjer = 2020;  |O enEjer = 2024;  |O enEjer = 2028;
         diasanyo = 366;
    |FINSI;

    eaTituloL = #0#45;
    efDFecha  = #0#47;
    efHFecha  = #0#48;
    nNume1    = efHFecha;
    nNume1    = nNume1 + 1;
    efHBaja   = nNume1;    ||fecha para comparar la baja a principios de a¤o

    informe = inform1; |SI #0#66 = "S"; informe = inform2; |FINSI;

    |IMPRE 0;
    |SI FSalida ! 0; |MENAV "    Error en Impresora"; |ACABA; |FINSI;

    |INFOR informe;
    |SI FSalida ! 0;
        |MENAV "    Error en Informe. ";
        |FINIMP;
        |ACABA;
    |FINSI;

    |SI #0#2 ! 0;
        nDEmpresa = #0#2;
        nHEmpresa = #0#3;
        |BUCLE Leem017;
    |SINO;
        |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
              |SI #0nCampo ! 0;
                  nDEmpresa = #0nCampo;
                  nHEmpresa = #0nCampo;
                  |BUCLE Leem017;
              |FINSI;
        |SIGUE;
    |FINSI;

    |FININF;
    |FINIMP;
|FCAL;

|| *************************************************************************
|PROCESO LeeDatosEmpresa;

  |DDEFECTO #900;

  #900#0 = enEmpresa;
  nPin1  = 0;
  aPin1  = "";
  efPn10 = "01.01.0000";
  efPn11 = "01.01.0000";

  || Lee Primera Actividad
    |ABRE #agicen14;
    #agicen14#0 = #900#0; || Empresa
    #agicen14#1 = 00;
    |LEE 041#agicen14.];
    |SI FSalida = 0; |Y #agicen14#0 = #900#0;
        #900#5 = #agicen14#9;
        #900#6 = #agicen14#10;
        #900#7 = #agicen14#7;
        #900#8 = #agicen14#8;
        efPn10 = #agicen14#5;
        efPn11 = #agicen14#6;
    |FINSI;
    |CIERRA #agicen14;

 || Lee Ficha Sociedades
    swExiste = 0;
    |ABRE #agim0019;
    #agim0019#0 = #900#0; || Empresa
    #agim0019#1 = 99;
    |LEE 041#agim0019.];
    |SI FSalida = 0;
        |SI #agim0019#0 ! #900#0;
            |LEE 041#agim0019.a;
            |SI FSalida = 0; |Y #agim0019#0 = #900#0;
                swExiste = 1;
            |FINSI;
        |SINO;
            swExiste = 1;
        |FINSI;
    |FINSI;
    |SI swExiste = 1;
        efPn10 = #agim0019#7;      || Fecha Activa
        efPn11 = #agim0019#55;     || Fecha Inactiva
        nPin1 = #agim0019#4;
        aPin1 = #agim0019#5;
    |FINSI;
    |CIERRA #agim0019;

    |ABRE #agifigen;
    #agifigen#0 = #900#0; || Empresa
    |LEE 041#agifigen.=;
    |SI FSalida = 0;
        efPn10  = #agifigen#93;
        efPn11  = #agifigen#94;
        nPin1   = #agifigen#87;
        aPin1   = #agifigen#88;
        #900#1 = #agifigen#13;  || Nif
        #900#2 = #agifigen#1;   || Nombre
    |FINSI;
    |CIERRA #agifigen;
    |SI efPn10 [ "01.01.1900"; efPn10 = "01.01.0000"; |FINSI;
    |SI efPn11 [ "01.01.1900"; efPn11 = "01.01.0000"; |FINSI;
    #900#3 = nPin1;
    #900#4 = aPin1;
    #900#9 = efPn10;
    #900#10 = efPn11;

    #900#11 = "ACTIVA";
    |SI efPn11 > "01.01.0000"; #900#11 = "INACTIVA"; |FINSI;
    #900#12 = enEjer;
    #900#13 = efDFecha;
    #900#14 = efHFecha;

    #900#50 = eaTituloL; ||Titulo del Libro

  || Lee Actividad en concreto.
    |ABRE #agicen14;
    swNoExis = 0;
    #agicen14#0 = #900#0; || Empresa
    #agicen14#1 = enActividad;
    |LEE 041#agicen14.=;
    |SI FSalida = 0;
        #900#15 = #agicen14#1;   ||Act.
        #900#16 = #agicen14#2;   ||Tipo Epig.
        #900#17 = #agicen14#3;   ||Epigrafe
        #900#18 = #agicen14#4;   ||Descripcion Actividad
        #900#19 = #agicen14#5;   ||Fecha Inicio
        #900#20 = #agicen14#6;   ||Fecha Fin
        #900#21 = #agicen14#11;  ||Sector actividad
        #900#22 = #agicen14#12;  ||Sector actividad
        #900#23 = #agicen14#16;  ||Tipo Actividad
        #900#24 = #agicen14#17;  ||Descripcion Tipo Actividad
        #900#51 = #agicen14#20;  ||CNAE

        |SI #agicen14#6 > "01.01.0000";
            |SI #0#24 = "N";
                swNoExis = 1;
            |FINSI;
        |FINSI;
    |SINO;
        swNoExis = 1;
    |FINSI;
    |CIERRA #agicen14;
|FPRC;


|PROGRAMA;

  aPathLaboral = "";
  |DBASS aBase;   |QBF aBase;
  aAlfa1 = aBase + "laboral/def/labempre.mas";
  |XABRE "E", aAlfa1, nHandle;
  |SI FSalida ] 0;
      aPathLaboral = aBase + "laboral/fich/";
  |SINO;
      |MENAV "    No esta instalada la Aplicacion de Laboral. Proceso interumpido";
      |ACABA;
  |FINSI;

 |CLS;
 |CABEZA "Ficha Trabajadores Asalariados";

 |ABRE #0;
 |LEE 101#0p;
 |SI FSalida ! 0;
     |DDEFECTO #0;
     |GRABA 020#0b;
 |FINSI;

 |PINPA #0#0;
 |PINDA #0#0;
 |ENDATOS #1#0;
 |SI FSalida = 0;
     |GRABA 020#0a;
 |FINSI;
 |LIBERA #0;
 |CIERRA #0;
|FPRO;
