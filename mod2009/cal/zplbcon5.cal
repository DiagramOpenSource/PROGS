|| ... Rutina de Acceso a Analitica
|| ... Subrutinas de las cuentas analiticas

|FICHEROS;
  :/contagen/def/anm00000 #600;
  :/contagen/def/anm00016 #616;
  :/contagen/def/anm00001 #358;
  :/contagen/def/anm00007 #359;
|FIN;

|INCLUYE i_varcon;
|INCLUYE i_variar;

|VARIABLES;
    nNume1 = 0;
    aAlfa1 = "";
    aAlfa2 = "";
    aAlfa3 = "";
    y_guarda = 0;
    y_entra = 0;
    y_lugar = 0;
    y_cuan1 = 0;
    y_cuan2 = 0;
    y_cuan3 = 0;
    y_cuan4 = 0;
    y_cuan5 = 0;
    y_alfa1 = "";
    y_alfa2 = "";
    y_alfa3 = "";
    y_alfa4 = "";
    y_alfa5 = "";
    y_alfa6 = "";

    y_nume1 = 0;
    y_nume2 = 0;
    y_ceros = "";

    y_cuen111 = "";
    y_cuen211 = "";
    y_cuen311 = "";

    y_error = 0;
|FIN;

|PROCESO FPuntosAna; |TIPO 6;
  y_guarda = FSalida;
  y_alfa1  = #Fichero@#Campo#;

  y_lugar = 0;
  y_cuan1 = 0;
  y_cuan2 = 0;
  y_cuan3 = 0;
  y_cuan4 = 0;
  y_cuan5 = 7;
  y_alfa2 = "";
  y_alfa3 = "";
  y_alfa4 = "";
  y_alfa5 = "";
  y_alfa6 = "";

  y_alfa2 = y_alfa1 - ".";
  |SI FSalida ! 0;
      y_nume1 = FSalida;
      y_nume2 = 100 + (((y_nume1 / 100) ! 0) -1);
      y_alfa2 = y_alfa1 % y_nume2;

      y_nume2 = ((y_nume1 / 100) ! 0) * 100 + 199;
      y_alfa3 = y_alfa1 % y_nume2;
      |QBF y_alfa2;
      |QBF y_alfa3;
      FSalida = y_nume1;
      |PARA; |SI FSalida ! 0; |HACIENDO;
             y_alfa3 = y_alfa3 - ".";  || limpiar mas puntos (los ignoramos)
      |SIGUE;
      y_nume2 = MaxAnalitica - ( (A\y_alfa2 % 0) + (A\y_alfa3 % 0) );
      y_alfa4 = (A\ "0" * y_nume2);

      y_cuen111 = y_alfa3 % 101;
      y_cuen111 = y_cuen111 > " ";
      |SI y_cuen111 = "U";  |O y_cuen111 = "A";
          y_cuen311 = y_cuen111;
          |HAZ ult_anacta;
           #Fichero@#Campo# = y_alfa5;
      |SINO;
           #Fichero@#Campo# = (A\ y_alfa2 + y_alfa4 + y_alfa3);
      |FINSI;
      |PINTA #Fichero@#Campo#;
   |FINSI;
   FSalida = y_guarda;
|FPRC;

|PROCESO ult_anacta;
  y_cuan2   =  y_cuan3 + 100; || longitud de la 1¦ parte en Alfa.
  y_cuen211 =  y_cuan2; || longitud de la 1¦ parte en Alfa.
  y_cuan1 = (y_cuan5 - y_cuan3) + 100;
  y_cuan1 = - y_cuan1;
  y_cuen311 = y_cuan1; || longitud de la 2¦ parte en Alfa.

  y_alfa2 = y_alfa3 + "9999999";
  y_alfa2 = y_alfa2 % 107;

  |PATH_DAT #358 #aPathConta#;
  |PATH_PAN #358 #aPathPanConta#;

  |ABRE #358;
  #358#0 = y_alfa2;

  |LEE 000#358];
  |SI FSalida ! 0;
      |LEE 011#358u;
  |SINO;
      |LEE 011#358a;
      |SI FSalida ! 0;
          |LEE 011#358p;
      |FINSI;
  |FINSI;

  y_alfa2 = #358#0 % y_cuen211;
  y_alfa6 = #358#0 % y_cuen311;

  |SI y_cuen111 = "U";
      y_alfa5 = #358#0;
  |SINO;
      |SI y_alfa2 ! y_alfa3;
          y_nume1 = (y_cuan5 - y_cuan3 - 1);
          y_ceros = ("0" * y_nume1) + "1";
          y_alfa5 = y_alfa3 + y_ceros;
      |SINO;
          y_nume2 = y_alfa6;
          y_nume2 = y_nume2 + 1;
          y_alfa6 = y_nume2;
          y_alfa6 = "0000000" + y_alfa6;
          y_alfa6 = y_alfa6 % y_cuen311;
          y_alfa5 = y_alfa2 + y_alfa6;
      |FINSI;
  |FINSI;
  |DDEFECTO #358;
  |CIERRA #358;
|FPRC;

|PROCESO LeeTablaAna;

  |PATH_DAT #359 #aPathConta#;
  |PATH_PAN #359 #aPathPanConta#;

  |ABRE #359;
  |DDEFECTO #359;
  #359#0 = eaCuenta;
  |LEE 001#359=;
  |SI FSalida ! 0;
      swRelac = 1;
  |SINO;
      eaCtaAna = #359#3;
      eaAnaMod = #359#4;
  |FINSI;
  |CIERRA #359;
|FPRC;

|RUTINA inferAna;
  #358#0 = "       ";
|FRUT;

|RUTINA superAna;
  #358#0 = "zzzzzzz";
|FRUT;

|PROCESO LeeCtaAna;
  eaCtaAn1 = "";
  eswLect = 0;
  |PATH_DAT #358 #aPathConta#;
  |PATH_PAN #358 #aPathPanConta#;
  |ABRE #358;
  |SI eOpAna = 1;
      |CONSULTA_F_CLAVE #1#358,inferAna,superAna;
      eaCtaAna = #358#0;
  |FINSI;
  #358#0 = eaCtaAna;
  |LEE 000#358=;
  |SI FSalida ! 0;
      |DDEFECTO #358;
      eswLect = 1;
  |FINSI;
  eaCtaAn1 = #358#1;
  |CIERRA #358;
|FPRC


|PROCESO Analitica;
   |ABRE #600;
   |DDEFECTO #600;
   |LEE 101#600p;
   |SI FSalida ! 0;
       |GRABA 020#600b;
   |FINSI;

   enSwAna = 0;  || 0 = Sin Analitica / 1 = Contabilidad Analitica
   |SI #600#1 = "S";
       MaxAnalitica = 7;
       nAdig0 = #600#2;
       nAdig1 = #600#3;
       nAdig2 = #600#4;
       nAdig3 = #600#5;
       nAdig4 = #600#6;
       nAdig5 = #600#7;
       eaPAna = #600#9;
       eaGAna = #600#10;
       enSwAna = 1;
   |FINSI;
   eaRefer = #600#8;
   eaAnNiv = #600#11;
   |CIERRA #600;

   |SI eaAnNiv = "E"; |Y enEmpresa ! 0;
       |ABRE #616;
       #616#0 = enEmpresa;
       |LEE 001#616=;
       |SI FSalida ! 0;
           enSwAna = 0;
       |SINO;
            MaxAnalitica = 7;
            nAdig0 = #616#2;
            nAdig1 = #616#3;
            nAdig2 = #616#4;
            nAdig3 = #616#5;
            nAdig4 = #616#6;
            nAdig5 = #616#7;
            eaPAna = #616#9;
            eaGAna = #616#10;
            enSwAna = 1;
       |FINSI;
       |CIERRA #616;
   |FINSI;
|FPRC;

|CALCULO InicioAna; |TIPO 8;
  |HAZ Analitica;
  |SI  enSwAna = 0;
       aAlfa1 = "    CONTABILIDAD NO ANALITICA ";
       |SI eaAnNiv = "E";
           aAlfa1 = "    EMPRESA NO ANALITICA ";
       |FINSI;
       |MENAV aAlfa1;
       |PTEC 807;
  |FINSI;
|FCAL;
