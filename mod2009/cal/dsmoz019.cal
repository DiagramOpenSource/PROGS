|FICHEROS;
     dsmoz019 #0;
     dsmom002 #2;
     dsmom004 #4;
     dsmom008 #8;
     dsmow013 #999;

     :/basica/def/agitab99 #99;
     :/basica/def/agifige1 #1000,MANTE;

     espejo1@ #1300;

     dsmom026 #26;
     dsmoc110 #100;

     :/contagen/def/canempre #101;
|FIN;

|VARIABLES;
     sw = 0;
     aFichero   = "";
     aAlfa      = "";
     aAlfa1     = "";
     aAlfa2     = "";
     nCampo    = 0;
     nDEmpresa = 0;
     nHEmpresa = 0;
     nMes      = 0;
     nAnyo     = 0;

     fFecha    = @;
     enGraba   = 0;

     nSw               = 0;
     aTodoCero         = "";
     aClave            = "";
     aDni              = "";
     aDn               = "            ";
     aHn               = "zzzzzzzzzzzz";
     aPeriodicidad     = "";

     aPathModelo       = "";
     aPathLaboral      = "";
     aDef              = "";
     aTipo             = "";
     aBase             = "";

     nEjer             = 0;
     nEjerAt           = 0;
     nDPeriodo         = 0;
     nHPeriodo         = 0;
     nCampo1           = 0;
     nCampo11          = 0;
     nCampo2           = 0;
     nCampo22          = 0;
     nCampo3           = 0;
     nCampo33          = 0;
     nSwEsta           = 0;
     nGrupo            = 0;
     nSubGrupo         = 0;
     nDepar            = 0;
     nBaseImpo         = 0;
     nPorce            = 0;
     nCuota            = 0;
     nPeriodo          = 0;
     nModelo           = 0;
     nSwPasa           = 0;
     nLineaIrpf        = 0;
     nDComplem         = 0;
     nHComplem         = 0;
     nHandle           = 0;
     nHay              = 0;
     nPorGrupos        = 0;
     nEmpresa          = 0;
|FIN;

|INCLUYE i_variar;

|| /////////////////////// PROCESOS ENTRADA POR PANTALLA \\\\\\\\\\\\\\\\\\\\

|PROCESO Modelo;
     |ABRE #99;
     #99#0 = #0#0;
     |LEE 040#99=;
     |SI FSalida ! 0;  |DDEFECTO #99;  |FINSI;
     |CIERRA #99;

     |SI #99#4 = "N";
         |MENAV "     Modelo sin Activar.";
         |ERROR;
     |FINSI;

     |SI #99#8 ! "L";
         |MENAV "     Modelo sin Pase desde Registros Laboral, no necesita este Proceso";
         |PTEC 807;
     |FINSI;

     #0#1 = 0;
     fFecha = ~;
     aAlfa  = fFecha % 402;   nMes  = aAlfa;
     aAlfa  = fFecha % -104;  nAnyo = aAlfa;

     |SI #99#5 = "T";  |O #99#5 = "C";
         |SI nMes [ 3;
             #0#1 = 12;
             #0#3 = nAnyo - 1;
             |HAZ Periodo;
             |PINTA #0#1;
             |PINTA #0#3;
             |ACABA;
         |FINSI;

         |SI nMes [ 6;
             #0#1 = 3;
             #0#3 = nAnyo;
             |HAZ Periodo;
             |PINTA #0#3;
             |PINTA #0#1;
             |ACABA;
         |FINSI;

         |SI nMes [ 9;
             #0#1 = 6;
             #0#3 = nAnyo;
             |HAZ Periodo;
             |PINTA #0#3;
             |PINTA #0#1;
             |ACABA;
         |FINSI;

         |SI nMes [ 12;
             #0#1 = 9;
             #0#3 = nAnyo;
             |HAZ Periodo;
             |PINTA #0#3;
             |PINTA #0#1;
             |ACABA;
         |FINSI;
     |FINSI;

     |SI #99#5 = "A";
          #0#1 = 99;
          #0#3 = nAnyo - 1;
          |HAZ Periodo;
          |PINTA #0#3;
          |PINTA #0#1;
     |FINSI;

     |SI #99#5 = "M";
         |SI nMes = 1;
              #0#1 = 12;
              #0#3 = nAnyo - 1;
          |SINO;
              #0#1 = nMes - 1;
              #0#3 = nAnyo;
          |FINSI;
          |HAZ Periodo;
          |PINTA #0#1;
          |PINTA #0#3;
     |FINSI;
|FPRC;

|PROCESO Periodo;
     #0#2 = "          ";
     |SI #0#1 = 1;   #0#2 = "ENERO     ";  |FINSI;
     |SI #0#1 = 2;   #0#2 = "FEBRERO   ";  |FINSI;
     |SI #0#1 = 3;   #0#2 = "MARZO     ";  |FINSI;
     |SI #0#1 = 4;   #0#2 = "ABRIL     ";  |FINSI;
     |SI #0#1 = 5;   #0#2 = "MAYO      ";  |FINSI;
     |SI #0#1 = 6;   #0#2 = "JUNIO     ";  |FINSI;
     |SI #0#1 = 7;   #0#2 = "JULIO     ";  |FINSI;
     |SI #0#1 = 8;   #0#2 = "AGOSTO    ";  |FINSI;
     |SI #0#1 = 9;   #0#2 = "SEPTIEMBRE";  |FINSI;
     |SI #0#1 = 10;  #0#2 = "OCTUBRE   ";  |FINSI;
     |SI #0#1 = 11;  #0#2 = "NOVIEMBRE ";  |FINSI;
     |SI #0#1 = 12;  #0#2 = "DICIEMBRE ";  |FINSI;
     |SI #0#1 = 99;  #0#2 = "ANUAL     ";  |FINSI;

     |SI #0#2 = "          ";
         |MENAV "     Periodo Incorrecto.";
         |ERROR;
     |FINSI;
     |PINTA #0#2;
|FPRC;

|PROCESO Parrilla;
     |SI #0#4 = 0;
         #0#4 = 0;  |PINTA #0#4;
         #0#5 = 0;  |PINTA #0#5;  |C_M #0#5, 1, "N";
         |PARA nCampo = 6;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#4, 1, "S";
             |C_M #0#5, 1, "S";
         |FINSI;

         |PARA nCampo = 6;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;
|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\

|| /////////////////////// PROCESOS DE TRABAJO \\\\\\\\\\\\\\\\\\\\
|PROCESO MiraCampos110;
     aTodoCero = "S";
     |PARA nCampo1 = 10;  |SI nCampo1 [ 24;  |HACIENDO nCampo1 = nCampo1 + 1;
          |SI #100nCampo1 ! 0;
               aTodoCero = "N";
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Compensa110;

     nHay   = 0;
     aClave = #1300#8;
     |SI aClave = "A"; |O aClave = "L";  |ACABA; |FINSI;

     #100#0  = #1300#0;
     #100#1  = #1300#1;
     #100#2  = #999#3;
     #100#3  = #1300#3;
     #100#4  = #999#9;
     #100#5  = #1300#5;
     #100#6  = #1300#6;
     #100#7  = #1300#7;
     #100#8  = #1300#8;
     #100#9  = #1300#9;
     |LEE 101#100=;
     |SI FSalida ! 0;
         |DDEFECTO #100;
         #100#0  = #1300#0;
         #100#1  = #1300#1;
         #100#2  = #999#3;
         #100#3  = #1300#3;
         #100#4  = #999#9;
         #100#5  = #1300#5;
         #100#6  = #1300#6;
         #100#7  = #1300#7;
         #100#8  = #1300#8;
         #100#9  = #1300#9;
         #100#29 = #1300#29;
         |GRABA 020#100b;

         nHay = 1;
     |SINO;
         |HAZ MiraCampos110;
         |SI aTodoCero = "S";
             nHay = 1;
         |FINSI;
     |FINSI;

     |PARA nCampo1 = 10;  |SI nCampo1 [ 24;  |HACIENDO nCampo1 = nCampo1 + 1;
           #100nCampo1 = #100nCampo1 - #1300nCampo1;
     |SIGUE;

     |GRABA 020#100a;
     |LIBERA #100;

     |HAZ MiraCampos110;  || mira si son todos = 0 o no

     |SI aTodoCero = "S";
         |SI nHay = 0;
             |BORRA 020#100a;
         |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE Bdsmoc110;
     #1300#1010;
     ;
     #999#0, #999#2, nPeriodo, 110, nDComplem, #999#1, aDn, 0,    " ", "  ";
     #999#0, #999#2, nPeriodo, 110, nHComplem, #999#1, aHn, 9999, "z", "zz";
     ;
     NULL, Compensa110;
|FIN;

||/////////////////////////////////////////////////////////////////
|| .... Compruebo que existe en contabilidad alguna de las Empresas.
|PROCESO PorDonde;
     enEmpresa = nEmpresa;
     enSwOpPar = 0;
     nSw       = 0;
     |HAZ SituaEmpresa;
     |SI enSwPartido = 0;
         |ABRE #101;
         |SELECT #4#101;
         #101#2  = nEmpresa;
         #101#26 = nEjer;
         |LEE 040#101=;
         |SI FSalida = 0; nSw = 1; |FINSI;
         |CIERRA #101;
      |SINO;
          nSw = 1;
      |FINSI;
      |SI nSw = 1; sw = 1; |FINSI;
      enEmpresa = #999#0;
|FPRC;

|PROCESO dsmom026;
     nEmpresa = #26#1;
     |HAZ PorDonde;
|FPRC;

|DEFBUCLE dsmom026;
     #26#1;
     ;
     #999#0, 00001;
     #999#0, 99999;
     ;
     NULL, dsmom026;
|FIN;

|PROCESO MiroSiConta;
     sw = 0;
     nPorGrupos = 0;
     |ABRE #26;
     #26#0 = #999#0;
     #26#1 = 0;
     |LEE 040#26];
     |SI FSalida = 0;  |Y #26#0 = #999#0;
         nPorGrupos = 1;
     |FINSI;
     |CIERRA #26;

     |SI nPorGrupos = 0;
         nEmpresa = #999#0;
         |HAZ PorDonde;
     |SINO;
         |BUCLE dsmom026;
     |FINSI;
|FPRC;
||/////////////////////////////////////////////////////////////////

|PROCESO Pase110_Reg;

     || Miro si tiene contabilidad.
     |HAZ MiroSiConta;
     |SI sw = 0; |ACABA; |FINSI; ||no lo paso.

     |DDEFECTO #8;
     |ABRE #8;
     #8#0 = #999#0;
     #8#1 = #999#2;
     #8#2 = #999#3;
     #8#3 = #999#7;
     #8#4 = #999#9;
     #8#5 = #999#1;
     |LEE 101#8=;
     |SI FSalida = 0;
         #8#11 = "SIN DATOS";
         |GRABA 020#8a;

         enEmpresa     = #8#0;
         enEjer        = #8#1;
         enPeriodo     = #8#2;
         enModelo      = #8#3;
         enComplem     = #8#4;
         enActividad   = #8#5;
         enTipoEntrada = 97;
         |SUB_EJECUTA_NP "dsmoc110";
     |FINSI;
     |LIBERA #8;
     |CIERRA #8;

     |ABRET #100;

     aAlfa = #999#2;
     aAlfa = aAlfa % -102;
     nEjer = aAlfa;

     enEmpresa     = #999#0;
     enEjer        = #999#2;
     enPeriodo     = #999#3;
     enModelo      = #999#7;
     enComplem     = #999#9;
     enActividad   = #999#1;

     |SI #999#3 ! 1;
         |DBASE aPathModelo;  |QBT aPathModelo;
         aDef = aPathModelo + "def/dsmoc110.mas";
         |CARGA_DEF aDef, espejo1@;
         |SI FSalida = 0;
             nDComplem = 0;
             nHComplem = enComplem;

             |PARA nPeriodo = 1;  |SI nPeriodo < #999#3;  |HACIENDO nPeriodo = nPeriodo + 1;
                   |BUCLE Bdsmoc110;
             |SIGUE;

             |SI #999#9 ! 0;
                 nDComplem = 0;
                 nHComplem = enComplem - 1;
                 nPeriodo  = #999#3;
                 |BUCLE Bdsmoc110;
             |FINSI;
         |FINSI;
         |DESCARGA_DEF #espejo1@;
     |FINSI;

     |CIERRAT #100;

     enEmpresa     = #999#0;
     enEjer        = #999#2;
     enPeriodo     = #999#3;
     enModelo      = #999#7;
     enComplem     = #999#9;
     enActividad   = #999#1;
     enTipoEntrada = 98;
     eaNombreAct   = #999#6;
     |SUB_EJECUTA_NP "dsmoc110";

     |ABRE #4;
     #4#0 = #999#0;
     #4#1 = #999#2;
     #4#2 = #999#3;
     #4#3 = #999#7;
     #4#4 = #999#9;
     |LEE 101#4=;
     |SI FSalida = 0;
         |HAZ ChequeaSituacion;  || Esta en el PLB

         #4#9 = " ";
         |GRABA 020#4a;
         |LIBERA #4;
     |FINSI;
     |CIERRA #4;
|FPRC;

|PROCESO dsmom008;
     #999#0 = #8#0;
     #999#1 = #8#5;
     |LEE 101#999=;
     |SI FSalida ! 0;
         #1000#0 = #8#0;
         |LEE 040#1000=;
         |SI FSalida ! 0;  |DDEFECTO #1000;  |FINSI;

         #999#0  = #8#0;
         #999#1  = #8#5;
         #999#2  = #8#1;
         #999#3  = #8#2;
         #999#4  = #1000#1;
         #999#5  = #0#2;
         #999#6  = #8#6;
         #999#7  = #8#3;
         #999#8  = "S";
         #999#10 = #8#12;
         |GRABA 020#999n;

         enGraba = 1;
     |FINSI;
     |LIBERA #999;
|FPRC;

|DEFBUCLE dsmom008;
     #8#1;
     ;
     #4#0, #4#1, #4#2, #4#3, 00, 99;
     #4#0, #4#1, #4#2, #4#3, 00, 99;
     ;
     NULL, dsmom008;
|FIN;

|PROCESO GrabaTempo;
     |SI #4#9  = "X";  |ACABA;  |FINSI;
     |SI #4#11 = "X";  |ACABA;  |FINSI;
     |SI #4#13 = "X";  |ACABA;  |FINSI;

     |SI #4#5 = "ACTIVIDAD ";  |O #4#5 = "ACT.+ REG.";
         |BUCLE dsmom008;
     |FINSI;
|FPRC;

|DEFBUCLE dsmom004;
     #4#1;
     ;
     nDEmpresa, #0#3, #0#1, #0#0, 00;
     nHEmpresa, #0#3, #0#1, #0#0, 99;
     ;
     NULL, GrabaTempo;
|FIN;

|PROCESO RealizaPase;
      aAlfa1 = #0#3;
      aAlfa1 = "01.01." + aAlfa1; efFecIni = aAlfa1;
      aAlfa1 = #0#3;
      aAlfa1 = "31.12." + aAlfa1; efFecFin = aAlfa1;
      eaNombreEmp = #999#4;
      eaNomPer    = #0#2;
      eaNombreAct = #999#6;

      |HAZ Pase110_Reg;
|FPRC;

|DEFBUCLE dsmow013;
     #999#1;
     8;
     00001, 00, "S";
     99999, 99, "S";
     ;
     NULL, RealizaPase;
|FIN;

|PROCESO Tipo3;  |TIPO 3;
     aFichero = ("9z" + Usuario) % 108;
     |NOME_DAT #999 aFichero;
     |ABRE #999;  |CIERRA #999;  |DELINDEX #999;

     aAlfa1 = #0#3; aAlfa1 = aAlfa1 % -102;
     nEjer  = aAlfa1;
     |ABRE #999;
     |ABRE #1000;
     |SI #0#4 ! 0;
         nDEmpresa = #0#4;
         nHEmpresa = #0#5;
         |BUCLE dsmom004;
     |SINO;
         |PARA nCampo = 6;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               nDEmpresa = #0nCampo;
               nHEmpresa = #0nCampo;
               |BUCLE dsmom004;
         |SIGUE;
     |FINSI;
     |CIERRA #1000;
     |CIERRA #999;

     |SI enGraba = 1;
         |BUCLE dsmow013;
     |SINO;
         |MENAV "     Seleccion Vacia.";
     |FINSI;

     |DELINDEX #999;
|FPRC;

|PROGRAMA;
     enDesdePase = 1;
     |SI enEmpresa ! 0;

         aFichero = ("pas" + Usuario) % 108;
         |NOME_DAT #999 aFichero;
         |ABRE #999;  |CIERRA #999;  |DELINDEX #999;

         #0#0 = enModelo;
         #0#1 = enPeriodo;
         #0#3 = enEjer;
         #0#4 = enEmpresa;
         #0#5 = enEmpresa;

         |ABRE #999;
         #999#0  = enEmpresa;
         #999#1  = enActividad;
         #999#2  = enEjer;
         #999#3  = enPeriodo;
         #999#6  = eaNombreAct;
         #999#7  = enModelo;
         #999#8  = "S";
         #999#9  = enComplem;
         #999#10 = enModelo1;
         |GRABA 020#999n;
         |CIERRA #999;

         enMasivo = 0;
         |BUCLE dsmow013;

         |DELINDEX #999;
         |ACABA;
     |FINSI;
     |CLS;
     |DDEFECTO #0;

     #0#0 = 110;
     enMasivo = 1;
     |C_M #0#0, 1, "N";
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
|FPRO;
