|FICHEROS;
     dsmozpas #0;
     dsmom004 #4;
     dsmom008 #8;
     dsmom030 #30;
     dsmo2347 #1;
     dsmo3347 #2;

     :/contagen/def/canempre #101;
     :/contagen/def/camaniva #102;
     :/contagen/def/caivalin #103;
     :/contagen/def/caivaasi #104;

     :/basica/def/agicen17   #117;

     dsmow013 #999,MANTE;

     &regisnif@ #709;
|FIN;

|VARIABLES;
     aAlfa1         = "";
     aAlfa          = "";
     aPathModelo    = "";
     aDef           = "";
     aTipo          = "";
     aClave         = "";

     nEjer          = 0;
     nDPeriodo      = 0;
     nHPeriodo      = 0;
     nCampo1        = 0;
     nCampo11       = 0;
     nCampo2        = 0;
     nCampo22       = 0;
     nCampo3        = 0;
     nCampo33       = 0;
     nSwEsta        = 0;
     nGrupo         = 0;
     nSubGrupo      = 0;
     nDepar         = 0;
     nBaseImpo      = 0;
     nPorce         = 0;
     nCuota         = 0;
     nPeriodo       = 0;
     nModelo        = 0;
     nLinea         = 0;

     aDepar = "";
     aSubde = "";
|FIN;

|INCLUYE i_variar;

|PROCESO GrabaNormal;
     #1#0  = #4#0;
     #1#1  = #4#1;
     #1#2  = #4#2;
     #1#3  = #4#3;
     #1#4  = #4#4;
     #1#5  = 0;
     #1#6  = aClave;
     #1#7  = #102#14;
     #1#8  = "N";
     |SI #30#50 = "X";  #1#8 = "S";  |FINSI;
     #1#9  = "N";
     |LEE 101#1=;
     |SI FSalida ! 0;
         |DDEFECTO #1;
         #1#0  = #4#0;
         #1#1  = #4#1;
         #1#2  = #4#2;
         #1#3  = #4#3;
         #1#4  = #4#4;
         #1#5  = 0;
         #1#6  = aClave;
         #1#7  = #102#14;
         #1#8  = "N";
         |SI #30#50 = "X";  #1#8 = "S";  |FINSI;
         #1#9  = "N";
         #1#10 = "N";

         eaCodNif = #1#7;   |HAZ LeeNifs;
         #1#11 = #709#1;

         |GRABA 020#1b;
     |FINSI;
     #1#12 = #1#12 + ((((#103#6 + #103#8 + #103#10) / enConversor)) ! EURODBMOD);
     |GRABA 020#1a;
     |LIBERA #1;
|FPRC;

|PROCESO GrabaArrendador;
     |ABRE #117;
     #117#0 = #4#0;
     #117#1 = aDepar;
     #117#2 = aSubde;
     |LEE 040#117=;
     |SI FSalida ! 0;  |DDEFECTO #117;  |FINSI;
     |CIERRA #117;

     #2#0  = #4#0;
     #2#1  = #4#1;
     #2#2  = #4#2;
     #2#3  = #4#3;
     #2#4  = #4#4;
     #2#5  = aDepar;
     #2#6  = aSubde;
     |LEE 101#2=;
     |SI FSalida ! 0;
         |DDEFECTO #2;
         #2#0  = #4#0;
         #2#1  = #4#1;
         #2#2  = #4#2;
         #2#3  = #4#3;
         #2#4  = #4#4;
         #2#5  = aDepar;
         #2#6  = aSubde;
         #2#7  = #102#14;
         #2#9  = #117#3;

         eaCodNif = #2#7;  |HAZ LeeNifs;

         #2#8  = #709#1;

         |GRABA 020#2b;
     |FINSI;
     #2#10 = #2#10 + ((((#103#6 + #103#8 + #103#10) / enConversor)) ! EURODBMOD);
     |GRABA 020#2a;
     |LIBERA #2;
|FPRC;

|PROCESO LeeLinea347;
     #30#0 = #103#3;
     |LEE 040#30=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;
     |SI #30#51 = "X"; |ACABA;  |FINSI;

     aClave = "";
     |SI #30#44 = "X";  aClave = "A";  |FINSI;
     |SI #30#45 = "X";  aClave = "B";  |FINSI;
     |SI #30#46 = "X";  aClave = "C";  |FINSI;
     |SI #30#47 = "X";  aClave = "D";  |FINSI;
     |SI #30#48 = "X";  aClave = "E";  |FINSI;
     |SI #30#76 = "X";  aClave = "F";  |FINSI;
     |SI #30#77 = "X";  aClave = "G";  |FINSI;
     |SI #30#49 = "X";  aClave = "X";  |FINSI;

     |SI aClave = "";  |ACABA;  |FINSI;

     #102#0  = #103#0;
     #102#1  = #103#1;
     |LEE 040#102=;
     |SI FSalida ! 0;  |DDEFECTO #102;  |FINSI;

     aAlfa1 = #103#30; |QBF aAlfa1;
     |SI aAlfa1 = "";
         #103#30 = #102#10;
         aAlfa1 = #103#31; |QBF aAlfa1;
         |SI aAlfa1 = ""; #103#31 = #102#11; |FINSI;
     |FINSI;

     aDepar = #102#10;
     aSubde = #102#11;
     |SI aMulDep = "S";
         aDepar = #103#30;
         aSubde = #103#31;
     |FINSI;

     |SI aClave ! "X";
         |HAZ GrabaNormal;
     |SINO;
         |HAZ GrabaArrendador;
     |FINSI;
|FPRC;

|DEFBUCLE caivalin347;
     #103#1;
     ;
     00, 0000000000, 01;
     99, 9999999999, 99;
     ;
     NULL, LeeLinea347;
|FIN;

|PROCESO Pase347;
     enEmpresa   = #999#0;
     enEjer      = #999#2;
     enSwOpPar   = 0;
     |HAZ SituaEmpresa;
     |SI enSwPartido = 0;
         aAlfa = #999#2;
         aAlfa = aAlfa % -102;
         nEjer = aAlfa;

         |ABRE #101;
         |SELECT #4#101;
         #101#2  = #999#0;
         #101#26 = nEjer;
         |LEE 040#101=;
         |SI FSalida ! 0;  |CIERRA #101;  |ACABA;   |FINSI;
         |CIERRA #101;

         |DBASS aPathConta;   |QBT aPathConta;
         aPathConta = aPathConta + "contagen/fich/" + (("00000" + #101#2) % -105) + #101#3 + "/";

         enEmpresa  = #101#2;
         enPerConta = #101#3;
         eaLaMonEmp = #101#28;
     |SINO;
         enPerConta = enPeriodoPar;
         eaLaMonEmp = "E";
     |FINSI;
     |HAZ EnConversor;

     |ABRE #4;
     #4#0 = #999#0;
     #4#1 = #999#2;
     #4#2 = #999#3;
     #4#3 = #999#7;
     #4#4 = #999#9;
     |LEE 040#4=;
     |SI FSalida ! 0;  |CIERRA #4; |ACABA;   |FINSI;
     |CIERRA #4;

     enEmpresa     = #999#0;
     enEjer        = #999#2;
     enPeriodo     = #999#3;
     enModelo      = #999#7;
     enComplem     = #4#4;
     enActividad   = 0;
     enTipoEntrada = 3;
     enMensa       = 1;
     |SUB_EJECUTA_NP "dsmo1347";

     |PATH_DAT #102 aPathConta;
     |PATH_DAT #103 aPathConta;
     |PATH_DAT #104 aPathConta;
     |ABRE #caivaasi;
     |DDEFECTO #caivaasi;
     |LEE 000#caivaasi.p;
     |CIERRA #caivaasi;
     aMulDep = #caivaasi#132;

     |ABRE #30;
     |ABRE #102;
     |ABRE #1;
     |ABRE #2;
     |BUCLE caivalin347;
     |CIERRA #30;
     |CIERRA #102;
     |CIERRA #1;
     |CIERRA #2;

     |SI enSwPartido = 1;
         enSwOpPar   = 2;
         |HAZ FinalPartido;
     |FINSI;

     enEmpresa     = #999#0;
     enEjer        = #999#2;
     enPeriodo     = #999#3;
     enModelo      = #999#7;
     enComplem     = #4#4;
     enActividad   = 0;
     enTipoEntrada = 98;
     enMensa       = 1;
     |SUB_EJECUTA_NP "dsmo1347";

     |ABRE #4;
     #4#0 = #999#0;
     #4#1 = #999#2;
     #4#2 = #999#3;
     #4#3 = #999#7;
     #4#4 = #999#9;
     |LEE 101#4=;
     |SI FSalida = 0;
         #4#7  = "X";
         #4#8  = ~;
         #4#15 = 0;
         #4#17 = "COMPLETADO";

         |GRABA 020#4a;
         |LIBERA #4;
     |FINSI;
     |CIERRA #4;
|FPRC;
