|FICHEROS;
     dsmoy050 #0;
     dsmom050 #50;  ||Tabla cuentas
     dsmom051 #51;  ||Mant. Valores por Empresas
     dsmom052 #52;  ||Socios
     dsmom053 #53;  ||Comuneros
     dsmow061 #61;  ||Temporal Cuentas

     dsmom100 #100;
     :/contagen/def/cacuenta #300;
     :/contagen/def/canempre #330;

     :/integral/def/dsam0003 #103;

     dsmow030 #900;
|FIN;

|VARIABLES;
     nEstado   = 0;
     nCampo    = 0;
     nDEmpresa = 0;
     nHEmpresa = 0;
     nPeriodo  = 0;
     nEjer     = 0;
     nEjerEos  = 0;
     nEjerSoc  = 0;
     aAlfa1    = "";
     aAlfa2    = "";
     aFichero  = "";
     aPath     = "";

     &eaRelCta = "";
     nEmpres   = 0;
     nSaldo    = 0;
     nCapzac   = 0;
     nImpCap   = 0;
     fFecha    = @;
     &swLinea  = 0;
     informe   = "";
     nNume1    = 0;
     aCodR0          = "";
     &eaPathIntegral = "";
     &eaCodRenta     = "";
     &eaCodR2        = "";
     aBase     = "";
     aAlfa     = "";
     aElDni    = "";
     nCanal    = 0;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;
|INCLUYE i_varlib;
|INCLUYE i_masivo;

|| /////////////////////// PROCESOS ENTRADA POR PANTALLA \\\\\\\\\\\\\\\\\\\\
|CALCULO CodigoRenta; |TIPO 7;
     |DBASS aBase;  |QBF aBase;
     aAlfa = aBase + "integral/def/dsam0000.mas";
     |XABRE "E", aAlfa, nCanal;
     |SI FSalida ] 0;
         eaPathIntegral  = aBase + "integral/";
     |FINSI;

     |C_M #0#15, 1, "S";
     |SI eaPathIntegral = "";
         |C_M #0#15, 1, "N";  #0#15 = "N";  |PINTA #0#15;
     |FINSI;
|FCAL;

|CALCULO ElAny; |TIPO 7;
 fFecha = ~;
 aAlfa1 = fFecha;
 aAlfa1 = aAlfa1 % -104;
 #0#14 = aAlfa1;
 |PINTA #0#14;
|FCAL;

|PROCESO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#0 = enEmpresa;  |PINTA #0#0;
         #0#1 = enEmpresa;  |PINTA #0#1;

         |C_M #0#0, 1, "N";
         |C_M #0#1, 1, "N";
     |FINSI;
|FPRC;

|PROCESO Parrilla;
     |SI #0#0 = 0;
         #0#0 = 0;  |PINTA #0#0;
         #0#1 = 0;  |PINTA #0#1;  |C_M #0#1, 1, "N";
         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#0, 1, "S";
             |C_M #0#1, 1, "S";
         |FINSI;

         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|| /////////////////////// //////////////////////////////////////////////////
|| /////////////////////// PROCESOS DE INFORMES \\\\\\\\\\\\\\\\\\\\\
|PROCESO ImpComuneros0;
  eaCodR2 = "";
  aCodR0  = eaCodRenta;
  |SI #0#15 = "S"; aElDni = #53#5; |HAZ LaRentaSI;  |FINSI;
  eaCodRenta = aCodR0;
  |PRINT;
  swLinea=8
|FPRC;

|DEFBUCLE ImpComuneros;
  #53#1;
  ;
  #51#0, #51#1, 01, 00001;
  #51#0, #51#1, 99, 99999;
  e;
  NULL, ImpComuneros0;
|FIN;

|PROCESO ImpSocios0;
  eaCodR2 = "";
  aCodR0  = eaCodRenta;
  |SI #0#15 = "S"; aElDni = #52#4; |HAZ LaRentaSI;  |FINSI;
  eaCodRenta = aCodR0;
  |PRINT;
  swLinea=5
|FPRC;

|DEFBUCLE ImpSocios;
    #52#1;
    ;
    #51#0, #51#1, 001;
    #51#0, #51#1, 999;
    e;
    NULL, ImpSocios0;
|FIN;

|PROCESO ImpCuentas0;
  |SI #61#2 ! "2210";
      |PRINT;
      swLinea = 2;
  |FINSI;
|FPRC;

|DEFBUCLE ImpCuentas;
    #61#1;
    ;
    #51#0, #51#1, "    ";
    #51#0, #51#1, "zzzz";
    e;
    NULL, ImpCuentas0;
|FIN;
|| ....................................................................

|PROCESO LasCuentas0;
  #cacuenta#0 = #50#0;
  |LEE 001#cacuenta.=;
  |SI FSalida ! 0; |ACABA; |FINSI;
  nSaldo = ((#cacuenta#53 - #cacuenta#50) / enConversor) ! EURODBMOD;
                                         || Saldo Total - Saldo Ejercicio
  |SI nSaldo ! 0;
       #61#0 = #51#0;
       #61#1 = #51#1;
       #61#2 = #50#0;
       |LEE 101#61=;
       |SI FSalida ! 0;
           #61#0 = #51#0;
           #61#1 = #51#1;
           #61#2 = #50#0;
           |GRABA 020#61b;
       |FINSI;
       #61#3 = #50#1;
       #61#4 = #50#2;
       #61#5 = nSaldo; || Saldo Total - Saldo Ejercicio
       |GRABA 020#61a;
       |LIBERA #61;
   |FINSI;
|FPRC;

|DEFBUCLE LasCuentas;
 #50#1;
 ;
 enPlan, "    ";
 enPlan, "zzzz";
 e;
 NULL, LasCuentas0;
|FIN;

|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO LaRentaSI;
     eaCodR2    = "";
     eaCodRenta = "";
     |ABRE #103;
     |SELECT #2#103;
     #103#1 = aElDni;
     |LEE 040#103=;
     |SI FSalida = 0;
         eaCodRenta = "Codigo de Renta : " + (("00000" + #103#0) % -105);
         eaCodR2    = "Renta: " + (("00000" + #103#0) % -105);
     |SINO;
         |SELECT #3#103;
         #103#35 = aElDni;
         |LEE 040#103=;
         |SI FSalida = 0;
             eaCodRenta = "Codigo de Renta : " + (("00000" + #103#0) % -105);
             eaCodR2    = "Renta: " + (("00000" + #103#0) % -105);
         |FINSI;
     |FINSI;
     |CIERRA #103;
|FPRC;
|| .....................................................................
|PROCESO Imprimir0;

 enEmpresa = #51#0;
 |HAZ DatosEmpr_w30;
 |SI enEjerBajaM > 0; |Y enEjerBajaM [ #0#14; |ACABA; |FINSI; ||esta ya disuelta

 eaCodRenta = "";
 |SI #0#15 = "S"; aElDni = #51#34; |HAZ LaRentaSI;  |FINSI;
 eaCodR2 = "";

 |SI #51#6 = 2;  |O nRegis ! 0; ||Una Sociedad  |SI #51#4 = 2;
     nEjer   = nEjerSoc;
     |INFOR "dsmoys50";
 |SINO;
     nEjer   = nEjerEos;
     |INFOR "dsmoyc50";
 |FINSI;

 |SI FSalida ! 0; |ACABA; |FINSI;

 swLinea = 0;
 |PRINT;

  |SI eaRelCta = "";

      |SI enEjer = 2008;
          |SI #51#6 ! 2;
              enEjer      = #0#14;         || A¤o Calculo
          |SINO;
              enEjer = #0#14 - 1;
          |FINSI;
          eaPlanilla = "dsmom050";
          |HAZ PlanDePlanillas;
      |FINSI;
      enEjer = #0#14;        || A¤o Calculo

      || ... Relacion de Cuentas que determinan el Valor
      |ABRE #canempre;
      |SELECT #4#canempre;
      #canempre#2  = #51#0;
      #canempre#26 = nEjer;
      |LEE 001#canempre.=;
      |SI FSalida = 0;
          enEmpresa  = #330#2;
          enPerConta = #330#3;
          eaLaMonEmp = #330#28;
          |HAZ EnConversor;

          aAlfa1 = #canempre#2; aAlfa1 = ("00000" + aAlfa1) % -105;
          aAlfa2 = #canempre#3; aAlfa2 = ("0" + aAlfa2) % -101;
          |DBASS aPath; |QBT aPath;
          aPathConta = aPath + "contagen/fich/" + aAlfa1 + aAlfa2 + "/";
          |PATH_DAT #300 aPathConta;
          |ABRE #cacuenta;
          |ABRE #61;
          |BUCLE LasCuentas;
          |CIERRA #cacuenta;
          |CIERRA #61;
      |FINSI;
      |CIERRA #canempre;
  |FINSI;

  swLinea = 1;
  |BUCLE ImpCuentas;

  swLinea = 3;
  |PRINT;


|SI #51#6 = 2;  |O nRegis ! 0; ||Una Sociedad |SI #51#4 = 2;
    swLinea = 4;
    |BUCLE ImpSocios;
|SINO;
    |SI #51#33 = 1; #51#32 = 0; |FINSI;
    |SI #51#33 = 2; #51#30 = 0; #51#31 = 0; |FINSI;
    swLinea = 6;
    |PRINT;
    swLinea = 7;
    |BUCLE ImpComuneros;
|FINSI;
|PIEINF;
|FININF;
|FPRC;

|| .....................................................................
|DEFBUCLE Imprimir;
    #51#1;
    ;
    nDEmpresa, enEjer;
    nHEmpresa, enEjer;
    e;
    NULL, Imprimir0;
|FIN;

|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO ITipo3;  |TIPO 3;


  aAlfa1 = #0#14; aAlfa1 = "01.01." + aAlfa1; efDFecha = aAlfa1;
  aAlfa1 = #0#14; aAlfa1 = "31.12." + aAlfa1; efHFecha = aAlfa1;

  |SI enSwMvo = 0;
      |IMPRE 0;
      |SI FSalida ! 0; |ACABA; |FINSI;
  |FINSI;

 |QBF eaRelCta;     ||YA esta Calculado
 |SI eaRelCta = "";
     aFichero = ("61" + Usuario) % 108;
     |NOME_DAT #61 aFichero;
     |ABRE #61;  |CIERRA #61;  |DELINDEX #61;
 |SINO;
     aFichero = eaRelCta;
     |NOME_DAT #61 aFichero;
 |FINSI;

 enEjer      = #0#14;        || A¤o Calculo
 |SI enEjer ! 2008;
     eaPlanilla = "dsmom050";
     |HAZ PlanDePlanillas;
 |FINSI;

    || ... Albaproa 23.06.2006        || Ejercicio Contable a consultar
     nNume1      = #0#14;             || Paco Hellin 1243-1461
     aAlfa1      = nNume1;            || el mismo a¤o para las Estimaciones
     aAlfa1      = aAlfa1 % -102;
     nEjerEos    = aAlfa1;

     nNume1      = #0#14 - 1;         || El a¤o anterior para Sociedad
     aAlfa1      = nNume1;
     aAlfa1      = aAlfa1 % -102;
     nEjer       = aAlfa1;
     nEjerSoc    = nEjer;

     |SI #0#0 ! 0;
         nDEmpresa = #0#0;
         nHEmpresa = #0#1;
         |BUCLE Imprimir;
     |SINO;
         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               nDEmpresa = #0nCampo;
               nHEmpresa = #0nCampo;
               |BUCLE Imprimir;         || Imprimir Valor
         |SIGUE;
     |FINSI;

     |SI enSwMvo = 0;
         |FINIMP;
     |FINSI;
|FPRC;

|| =========================================================================
|PROGRAMA;
  |SI enSwMvo = 1;
      enEmpresa = enEmprM;
      enEjer    = enEjerM;
  |FINSI;

 |SI enEmpresa ! 0;
     #0#0 = enEmpresa;
     #0#1 = enEmpresa;
     #0#14 = enEjer;
     |HAZ ITipo3;
 |SINO;
     |CLS;
     |MANTE #0;
 |FINSI;
|FPRO;
