|FICHEROS;
   dsmoy048 #0;

   dsmom041 #41;
   dsmom040 #40;
   dsmom042 #42;
   dsmom100 #100;

   :/basica/def/agicen14 #114;
   :/basica/def/agim0019 #119;

   dsmow030 #900;      || Def para imprimir
   dsmow044 #998;

   :/contagen/def/cacuenta #310;
   :/contagen/def/caconimp #306;  || configuracion impresiones
|FIN;

|VARIABLES;
   &entrada    = 1;
   nEjer       = 0;
   aDCuenta    = "";
   aHCuenta    = "";

   &aEmpresa   = "";
   nLaEmpresa  = 0;
   informe     = "dsmoy048";

   nCampo      = 0;
   nDEmpresa   = 0;
   nHEmpresa   = 0;
   nDActividad = 0;
   nHActividad = 0;
   &nCamSel    = 23;
   &nCamTip    = 38;

   aFichero    = "";
   aAlfa1      = "";
   aAlfa2      = "";
   nNume1      = 0;
   nNume2      = 0;
   nNume3      = 0;
   nPara1      = 0;
   fFecsys     = @;

   aPath       = "";
   nEjercicio  = 0;

   nTipo    = 0;
   nTip     = 0;
   nImporte = 0;
   nSaldo   = 0;
   nCanti1  = 0;
   nCanti2  = 0;
   nSwSel   = 0;

   nAnyFin  = 0;
   nDias    = 0;
   nGrupo   = 0;
   nLinea   = 0;
   &enSwLin = 0;
   nSwNO    = 0;
   nCambio  = 0;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;
|INCLUYE i_varlib;
|INCLUYE i_selcon;
|INCLUYE i_numamo;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO DatosEmp; |TIPO 7;

   |DDEFECTO #306;

   |ABRE #100;
   |LEE 040#100p;
   |SI FSalida = 0;
       aAlfa1 = #100#1; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
       aAlfa2 = #100#2; |QBF aAlfa2; aAlfa2 = ("0"     + aAlfa2) % -101;
       |DBASS aPath; |QBT aPath;
       aPathConta = aPath + "contagen/fich/" + aAlfa1 + aAlfa2 + "/";

       |PATH_DAT #306 aPathConta;

       |ABRE #306;
       #306#0 = 1;
       |LEE 000#306=;
       |CIERRA #306;
   |FINSI;
   |CIERRA #100;

   #0#54  = #306#7; |PINTA #0#54;
   aAlfa1 = #306#8; |C_M #0#54,1,aAlfa1;
|FPRC;

|CALCULO Defecto; |TIPO 7;
   fFecsys =  ~;
   aAlfa1 = fFecsys;
   aAlfa1 = aAlfa1 % -104;
   #0Campo = aAlfa1;
   |PINTA #0Campo;
|FCAL;

|CALCULO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#7 = enEmpresa; |PINTA #0#7;
         #0#8 = enEmpresa; |PINTA #0#8;

         |C_M #0#7, 1, "N";
         |C_M #0#8, 1, "N";
     |FINSI;
|FCAL;

|CALCULO Parrilla;
     |SI #0#7 = 0;
         #0#7 = 0;  |PINTA #0#7;
         #0#8 = 0;  |PINTA #0#8;  |C_M #0#8, 1, "N";
         |PARA nCampo = 9;  |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#7, 1, "S";
             |C_M #0#8, 1, "S";
         |FINSI;

         |PARA nCampo = 9;  |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO numamo; |TIPO 0;
   x_alfa1 = #0Campo;
   |HAZ x_punto;
   #0Campo = x_alfa1;
   |PINTA #0Campo;
|FCAL;

|CALCULO cuenta; |TIPO 0;
     salidas  = FSalida;
     |C_M #0Campo, 0, aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     |ABRE #100;
     |LEE 040#100p;
     |SI FSalida ! 0;  |DDEFECTO #100;  |FINSI;
     |CIERRA #100;

     nLaEmpresa = enEmpresa;

     enEmpresa  = #100#1;
     enPerConta = #100#2;

     eaCuenta = #0Campo;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;
     enEmpresa = nLaEmpresa;
|FCAL;

|CALCULO Seleccion; |TIPO 0;
   |SI #0#39 = "S";
       |C_M #0#0,1,"S";
       |C_M #0#1,1,"S";
       |PARA nCampo = 40; |SI nCampo [ 51; |HACIENDO nCampo = nCampo + 1;
             |C_M #0nCampo,1,"N";
             #0nCampo = "";
             |PINTA #0nCampo;
       |SIGUE;
   |SINO;
      |C_M #0#0,1,"N"; #0#0 = "            "; |PINTA #0#0;
      |C_M #0#1,1,"N"; #0#1 = "zzzzzzzzzzzz"; |PINTA #0#1;
      |PARA nCampo = 40; |SI nCampo [ 51; |HACIENDO nCampo = nCampo + 1;
            |C_M #0nCampo,1,"S";
      |SIGUE;
   |FINSI;
|FCAL;

|CALCULO NoMasCtas; |TIPO 0;
     |SI #0Campo [ "            ";
         |PARA nCampo = Campo + 1;  |SI nCampo [ 51;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo,1,"N"; #0nCampo = "";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 51;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO elTitulo; |TIPO 0;
 #0#5 = "CHEQUEO DE CUENTAS DE INMOVILIZADO Y DE AMORTIZACION ACUMULADA";
 |SI #0#6 = "1";
     #0#5 = "CHEQUEO DE CUENTAS DE INMOVILIZADO ";
 |FINSI;
 |SI #0#6 = "2";
     #0#5 = "CHEQUEO DE CUENTAS DE AMORTIZACION ACUMULADA";
 |FINSI;
 |PINTA #0#5;
|FCAL;

|| ************************************************************************
|PROCESO LeeLaCuenta;
 nSaldo = 0;
 |ABRE #310;
 |DDEFECTO #310;
 #310#0 = eaCuenta;
 |LEE 041#310=;
 |CIERRA #310;
 nSaldo = #310#53 - #310#50;
|FPRC;

|PROCESO GrabaAuxiliar;
 |ABRE #998;
 |DDEFECTO #998;
 #998#0 = enEmpresa;
 #998#1 = nTipo;
 #998#2 = eaCuenta;
 |LEE 040#998=;
 |SI FSalida ! 0;
     |DDEFECTO #998;
     #998#0 = enEmpresa;
     #998#1 = nTipo;
     #998#2 = eaCuenta;
     |HAZ LeeLaCuenta;
     #998#3 = #310#2;
     #998#4 = eaNombreEmp;
     #998#5 = #0#52;
     #998#6 = (nSaldo / enConversor) ! EURODBMOD;
     |GRABA 020#998c;
 |FINSI;
 |SI #998#1 = 2; nImporte = - nImporte; |FINSI;
 #998#7 = #998#7 + nImporte;
 #998#8 = #998#6 - #998#7;
 |GRABA 020#998a;
 |CIERRA #998;
 nLinea = 1;
|FPRC;
|| ************************************************************************

|PROCESO SeleccionCta;
 nSwSel = 1;
 |SI #0#39 = "S";
     |SI #0#0 [ eaCuenta; |Y #0#1 ] eaCuenta;
         nSwSel = 0;
     |FINSI;
 |SINO;
     |PARA nPara1 = 40; |SI nPara1 [ 51; |HACIENDO nPara1 = nPara1 + 1;
           |SI #0nPara1 = eaCuenta; |Y #0nPara1 ! "            ";
               nSwSel = 0;
           |FINSI;
     |SIGUE;
 |FINSI;
|FPRC;

|PROCESO LasLineas;
      nCanti1 = 0;
      nCanti2 = 0;
      |SI nTip = 0;
          nCanti1 = #42#11;
      |SINO;
          |SI #42#5 ! nAnyFin;
              |SI nTip = 1; |Y #42#5 > nAnyFin;
                  nCanti1 = #42#11;
              |FINSI;

              |SI nTip = 2; |Y #42#5 < nAnyFin;
                  nCanti1 = #42#11;
              |FINSI;
          |SINO;
              |HAZ CalDiasAny;
              nNume1 = #41#37;  nNume2 = #42#9;
              nNume3 = nNume2 - nNume1;
              nCanti2 = ((#42#11  * nNume3) / nDias) ! EURODBMOD;

              |SI nTip = 1;         || desde el pago
                  nCanti1 = nCanti2;
              |FINSI;
              |SI nTip = 2;         || hasta el pago
                  nCanti1 = #42#11  - nCanti2;
              |FINSI;
          |FINSI;
      |FINSI;
      nImporte = nImporte + nCanti1;
|FPRC;

|DEFBUCLE LasLineas;
 #42#1;
 6,5;
 #41#0, #41#2, #41#3, 001, "A", 0000;
 #41#0, #41#2, #41#3, 999, "A", #0#52;
 e;
 NULL, LasLineas;
|FIN;

|PROCESO GrabaGrupo;
  |ABRE #40;
  #40#0 = #41#0;
  #40#1 = #41#1;
  #40#2 = nGrupo;
  |LEE 001#40=;
  |SI FSalida ! 0;  |CIERRA #40; |ACABA;   |FINSI;
  |CIERRA #40;

  |SI enEjer ] 2008;
      eaCuenta = #40#12;
  |SINO;
      eaCuenta = #40#5;
  |FINSI;
  |HAZ SeleccionCta;
  |SI nSwSel ! 0;  |ACABA; |FINSI;

  nImporte = 0;
  |BUCLE LasLineas;
  |SI nImporte ! 0;
      nTipo    = 2;
      |HAZ GrabaAuxiliar;
  |FINSI;
|FPRC;

|| ************* Proceso para cambiar las cuentas
|PROCESO ElAuxiliar;
  aAlfa1 = #41#27;
  aAlfa1 = #41#27; aAlfa1 = aAlfa1 % -104;
  nEjer  = aAlfa1;

  |SI nEjer ! enEjer;
      enEjerCam = nEjer;
      nCambio   = 0;
      |SI nEjer < 2008; |Y enEjer ] 2008;
          nCambio = 1;
      |FINSI;
      |SI nEjer ] 2008; |Y enEjer < 2008;
          nCambio = 1;
      |FINSI;
      |SI nCambio = 1;
          |HAZ CambioCta;
          |SI enEjer ] 2008; eaCuenta = eaCta2008; |FINSI;
          |SI enEjer < 2008; eaCuenta = eaCta1990; |FINSI;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO LeeLasAmor;
 |SI #41#27 > efHFecha; ||Comprado despues del ejercicio
     |ACABA;
 |FINSI;

 nSwNO = 0;
 |SI #41#26 > "01.01.1900";
     |SI #41#26 < efDFecha;
         |ACABA;
     |FINSI;
     |SI #0#53 = "N";
         |SI #41#26 [ efHFecha;
             |ACABA;
         |FINSI;
     |FINSI;
 |FINSI;

 nAnyFin = 0;
 |SI #41#36 ! 0;
     aAlfa1 = #41#37;
     aAlfa1 = aAlfa1 % -104;
     nAnyFin = aAlfa1;
 |FINSI;

 eaCuenta = #41#5;
 |HAZ ElAuxiliar;
 #41#5 = eaCuenta;

 |SI #0#6 ! 2;
     eaCuenta = #41#5;
     |HAZ SeleccionCta;
     |SI nSwSel = 0;
         nTipo    = 1;
         nImporte = #41#10;
         |HAZ GrabaAuxiliar;
     |FINSI;
 |FINSI;

 |SI #0#6 ! 1;
     |SI #41#36 = 0; |O #41#38 = 0;
         nTip   = 0;
         nGrupo = #41#18;
         |HAZ GrabaGrupo;
     |SINO;
         nTip   = 1;
         nGrupo = #41#18;
         |HAZ GrabaGrupo;
         nTip   = 2;
         nGrupo = #41#38;
         |HAZ GrabaGrupo;
    |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE LeeLasAmor;
 #41#1;
 ;
 enEmpresa, "        ", 00;
 enEmpresa, "zzzzzzzz", 99;
 e;
 NULL, LeeLasAmor;
|FIN;

|PROCESO ImprimeAuxiliar;
 |SI nTipo ! #998#1; |Y nTipo ! 0;
     enSwLin = 1;
 |FINSI;
 |PRINT;
 enSwLin = 0;
 nTipo = #998#1;
|FPRC;

|DEFBUCLE LeeAuxiliar;
 #998#1;
 ;
 INICIO;
 FINAL;
 ;
 NULL, ImprimeAuxiliar;
|FIN;

|| ....................... Situa la Empresa Contable
|PROCESO LEmpresaConta;

 enEmpresa = #agifigen#0;
 |HAZ AntCambioCta;  ||para situar la del 2008

 |ABRE #canempre;
 |SELECT #4#canempre;
 #canempre#2  = #agifigen#0;
 #canempre#26 = nEjercicio;
 |LEE 041#canempre.=;
 |SI FSalida ! 0;
     |SELECT #1#canempre;
     |CIERRA #canempre;
     |ACABA;
 |FINSI;
 |SELECT #1#canempre;
 |CIERRA #canempre;

  enEmpresa   = #canempre#2;
  enPerConta  = #canempre#3;   || Periodo Contable
  eaNombreEmp = #canempre#1;
  |HAZ EnConversor;

  eaEstadoEmp = "S"; |SI #canempre#21 = "I"; eaEstadoEmp = "N"; |FINSI;
  |HAZ LaSelEmpresa2;
  |SI enSwSelEmp = 1; swerror = 1; |FINSI;
|| ------------------------------------------------------------------------
  |DBASS aPath;
  aAlfa1 = enEmpresa;  aAlfa1 = ("00000" + aAlfa1) % -105;
  aAlfa2 = enPerConta; aAlfa2 = ("0" + aAlfa2) % -101;
  aPathConta  = aPath + "contagen/fich/" + aAlfa1 + aAlfa2 + "/";
  |PATH_DAT #310 aPathConta;

  enActividad = 0;
  |HAZ LeeDatosEmpresa;


  nLinea   = 0;
  |BUCLE LeeLasAmor;
  |SI nLinea ! 0;
      nTipo = 0;
      |BUCLE LeeAuxiliar;
      |PIEINF;
  |FINSI;

  |DELINDEX #998;
|FPRC;

|DEFBUCLE agifigen;
 #agifigen#1;
 ;
 nDEmpresa;
 nHEmpresa;
 ;
 NULL, LEmpresaConta;
|FIN;

|| //////////////////// Procesos desde el Tipo 3   \\\\\\\\\\\\\\\\\\\\\\\\\\
|CALCULO Tipo3;  |TIPO 3;
     enConversor = 1;

     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |INFOR informe;
     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

     aFichero = ("44w" + Usuario) % 108;
     |NOME_DAT #998 aFichero;
     |ABRE #998; |CIERRA #998; |DELINDEX #998;

     enEjer = #0#52;
     aAlfa1 = enEjer; |QBF aAlfa1;
     aAlfa1 = aAlfa1 % -102;
     nEjercicio = aAlfa1;
     aAlfa2 = #0#52;
     aAlfa1 = "01.01." + aAlfa2;
     aAlfa2 = "31.12." + aAlfa2;
     efDFecha  = aAlfa1;
     efHFecha  = aAlfa2;
     enDatos = 0; |SI #0#54 = "S"; enDatos = 1; |FINSI;

     nYaLeo = 1;
     |SI #0#7 ! 0;
         nDEmpresa = #0#7;
         nHEmpresa = #0#8;
         |BUCLE agifigen;
     |SINO;
         |PARA nCampo = 9; |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
               nDEmpresa = #0nCampo;
               nHEmpresa = #0nCampo;
               |BUCLE agifigen;
         |SIGUE;
     |FINSI;

     |FININF;
     |FINIMP;
|FCAL;

|| *********************************************************************
|PROCESO LeeDatosEmpresa;
  |DDEFECTO #900;
  eaInac = #0#23;
  enEjer = #0#52;
  eaTituloL = #0#5;
  |HAZ DatosEmpr_w30_SA;
|FPRC;

|PROCESO CalDiasAny;
  nDias = 365;
  |SI nAnyFin = 1980;  |O nAnyFin = 1984;  |O nAnyFin = 1988;  |O nAnyFin = 1992;
                       |O nAnyFin = 1996;  |O nAnyFin = 2000;  |O nAnyFin = 2004;
                       |O nAnyFin = 2008;  |O nAnyFin = 2012;  |O nAnyFin = 2016;
                       |O nAnyFin = 2020;  |O nAnyFin = 2024;  |O nAnyFin = 2028;
      nDias = 366;
  |FINSI;
|FPRC;
