|FICHEROS;
     dsmoz096 #96;
     dsmom002 #2;
     dsmom004 #4;

     dsmom093 #93,MANTE;

     dsmod131  #131;
     dsmod131@ #1131;

     dsmod130  #130;
     dsmod130@ #1130;

     :/basica/def/agifigen #1000;
     :/basica/def/agicen14 #114;
|FIN;

|VARIABLES;
     nCampo    = 0;

     aParam    = "";
     aBase     = "";
     aMas      = "";
     nDEmpresa = 0;
     nHEmpresa = 0;
     nHPer     = 0;
     nHComple  = 0;

     nSumaD    = 0;
     nSwNeg    = 0;

     nEmpresa  = 0;
     nPeriodo  = 0;
     nEjer     = 0;
     nModelo   = 0;
     nComplem  = 0;
     nImpAnt   = 0;
     nImpNuevo = 0;
     nEl33     = 0;
     nEl23     = 0;
     nImporte  = 0;

     aAlfa1    = "";
     errconta  = 0;
     nModo     = 0;
     nInkey    = 0;

     nSwConPro   = 0;
     fFecIni     = @;
     fFecFin     = @;
     aAplica     = "";
     nNoModi     = 0;
     nNume1      = 0;
|FIN;

|INCLUYE i_variar;

|| ////////////////////////////////////////////////////////////////////////
|PROCESO Actual_m004;
     #4#0 = nEmpresa;
     #4#1 = nEjer;
     #4#2 = nPeriodo;
     #4#3 = nModelo;
     #4#4 = nComplem;
     |LEE 101#4=;
     |SI FSalida = 0;
         #4#15 = #4#15 - nImpAnt + nImpNuevo;
         |GRABA 020#4a;
     |FINSI;
     |LIBERA #4;
|FPRC;

|PROCESO NoModificar;
  |SI #4#11 = "X"; nNoModi = 1; |FINSI;
  |SI #4#13 = "X"; nNoModi = 1; |FINSI;
|FPRC;

|DEFBUCLE NoModificar;
 #4#1;
 ;
 #93#2, #93#1, 04, #93#0, 00;
 #93#2, #93#1, 99, #93#0, 99;
 ;
 NULL, NoModificar;
|FIN;

|| ----------------------------------------------------------------------
|PROCESO dsmod131_neg;

     |SI #1131#33 < 0; nSwNeg = 1; |FINSI;

     |SI #1131#33 ] 0;  |Y nSwNeg = 0; |ACABA;  |FINSI;

     nSumaD = nSumaD + #1131#38;

     |SI #1131#33 < 0;
         #131#39 = #131#39 + -#1131#33;
     |SINO;
         #131#39 = #131#39 - #1131#38;
         |SI #131#39 [ 0; nSwNeg = 0; #131#39 = 0; nSumaD = 0; |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE dsmod131_neg;
     #1131#1006;
     ;
     #131#0, #131#1,     3, #131#3,  0, #131#5;
     #131#0, #131#1, nHPer, #131#3, 99, #131#5;
     ;
     NULL, dsmod131_neg;
|FIN;

|PROCESO CapturaNegativos131;
     |SI #131#38 [ 0;  |ACABA;  |FINSI;

     |DBASE aBase;  |QBF aBase;
     aMas = aBase + "def/dsmod131.mas";
     |CARGA_DEF aMas, dsmod131@;

     |ABRE #dsmod131@;
     nSwNeg   = 0;
     nSumaD   = 0;
     #131#39  = 0;
     nHPer    = #131#2 - 3;
     |PARA nNume1 = 3; |SI nNume1 [ nHPer; |HACIENDO nNume1 = nNume1 + 3;
           |DDEFECTO #1131;
           #1131#0 = #131#0;
           #1131#1 = #131#1;
           #1131#2 = nNume1;
           #1131#3 = #131#3;
           #1131#4 = 99;
           #1131#5 = #131#5;
           |LEE 040#1131];
           |SI FSalida = 0;
               |LEE 040#1131a;
           |SINO;
               |LEE 040#1131u;
           |FINSI;
           |SI FSalida = 0; |Y #1131#0 = #131#0;|Y #1131#1 = #131#1;
                       |Y #1131#2 = nNume1; |Y #1131#3 = #131#3;
               |HAZ dsmod131_neg;
           |FINSI;
     |SIGUE;
     |SI #131#39 > #131#38;  #131#39 = #131#38;  |FINSI;
     |CIERRA #dsmod131@;
     |DESCARGA_DEF #dsmod131@;
|FPRC;

|PROCESO dsmod131_2;
     |SI #1131#33 > 0;
         #131#41 = #131#41 + #1131#33;
     |FINSI;
|FPRC;

|DEFBUCLE dsmod131_2;
     #1131#1006;
     ;
     #131#0, #131#1, #131#2, #131#3,        0, #131#5;
     #131#0, #131#1, #131#2, #131#3, nHComple, #131#5;
     ;
     NULL, dsmod131_2;
|FIN;

|PROCESO CapturaOtros131;
     |DBASE aBase;  |QBF aBase;
     aMas = aBase + "def/dsmod131.mas";
     |CARGA_DEF aMas, dsmod131@;

     nHComple = #131#4 - 1;
     #131#41  = 0;

     |BUCLE dsmod131_2;

     |DESCARGA_DEF #dsmod131@;
|FPRC;

|PROCESO RecalModelo_131;
     nEmpresa  = #131#0;
     nEjer     = #131#1;
     nPeriodo  = #131#2;
     nComplem  = #131#4;

     nImpAnt = #131#33;
     #131#41 = 0;
     #131#42 = 0;
     |SI #131#1 = 2008; |Y aAplica ! "N";
         |SI #131#2 = 6;   #131#42 = 200;  |FINSI;
         |SI #131#2 = 9;   #131#42 = 100;  |FINSI;
         |SI #131#2 = 12;  #131#42 = 100;  |FINSI;
     |FINSI;

     #131#36 = #131#26 + #131#35 + #131#30;
     #131#37 = #131#27 + #131#31;
     #131#38 = #131#36 - #131#37 - #131#42;
     #131#39  = 0;
     |SI #131#2 ! 3;
         |HAZ CapturaNegativos131;
     |FINSI;

     #131#40 = #131#38 - #131#39;

     nEl33 = #131#40;
     |SI #131#4 > 0;
         |HAZ CapturaOtros131;
         nEl33 = nEl33 - #131#41;
     |FINSI;

     #131#33 = #131#40 - #131#41;
     |GRABA 020#131a;
     |LIBERA #131;


     nImpNuevo = #131#33;
     |HAZ Actual_m004;
|FPRC;

|DEFBUCLE dsmod131;
    #131#1;
    ;
    nEmpresa, nEjer, 01, nModelo, 00, 00;
    nEmpresa, nEjer, 12, nModelo, 99, 99;
    ;
    NULL, RecalModelo_131;
|FIN;

|| ---- 130
|PROCESO dsmod130_neg;

     |SI #1130#1 = 2008;  |Y #1130#2 [ 3;
          nImporte = #1130#17 - #1130#18;
     |SINO;
          nImporte = #1130#23;
     |FINSI;

     |SI nImporte < 0; nSwNeg = 1; |FINSI;

     |SI nImporte ] 0;  |Y nSwNeg = 0; |ACABA;  |FINSI;

     nSumaD = nSumaD + #1130#20;

     |SI nImporte < 0;
         #130#21 = #130#21 + -nImporte;
     |SINO;
         #130#21 = #130#21 - #1130#20;
         |SI #130#21 [ 0; nSwNeg = 0; #130#21 = 0; nSumaD = 0; |FINSI;
     |FINSI;
|FPRC;

/*
|DEFBUCLE dsmod130_neg;
     #1130#1006;
     ;
     #130#0, #130#1,     3, #130#3,  0, #130#5;
     #130#0, #130#1, nHPer, #130#3, 99, #130#5;
     ;
     NULL, dsmod130_neg;
|FIN;
*/

|PROCESO CapturaNegativos130;
     |SI #130#20 [ 0;  |ACABA;  |FINSI;

     |DBASE aBase;  |QBF aBase;
     aMas = aBase + "def/dsmod130.mas";
     |CARGA_DEF aMas, dsmod130@;

     |ABRE #dsmod130@;
     nSwNeg   = 0;
     nSumaD   = 0;
     #130#21  = 0;
     nHPer    = #130#2 - 3;
     |PARA nNume1 = 3; |SI nNume1 [ nHPer; |HACIENDO nNume1 = nNume1 + 3;
           |DDEFECTO #1130;
           #1130#0 = #130#0;
           #1130#1 = #130#1;
           #1130#2 = nNume1;
           #1130#3 = #130#3;
           #1130#4 = 99;
           #1130#5 = #130#5;
           |LEE 040#1130];
           |SI FSalida = 0;
               |LEE 040#1130a;
           |SINO;
               |LEE 040#1130u;
           |FINSI;
           |SI FSalida = 0; |Y #1130#0 = #130#0;|Y #1130#1 = #130#1;
                       |Y #1130#2 = nNume1; |Y #1130#3 = #130#3;
               |HAZ dsmod130_neg;
           |FINSI;
     |SIGUE;
     |SI #130#21 > #130#20;  #130#21 = #130#20;  |FINSI;
     |CIERRA #dsmod130@;

     |DESCARGA_DEF #dsmod130@;
|FPRC;

|PROCESO dsmod130_2;
     |SI #1130#1 = 2008;  |Y #1130#2 [ 3;
          nImporte = #1130#17 - #1130#18;
     |SINO;
          nImporte = #1130#23;
     |FINSI;
     |SI nImporte < 0; nImporte = 0; |FINSI;
     #130#18 = #130#18 + nImporte;
|FPRC;

|DEFBUCLE dsmod130_2;
     #1130#1006;
     ;
     #130#0, #130#1, #130#2, #130#3,        0, #130#5;
     #130#0, #130#1, #130#2, #130#3, nHComple, #130#5;
     ;
     NULL, dsmod130_2;
|FIN;

|PROCESO CapturaOtros130;
     |DBASE aBase;  |QBF aBase;
     aMas = aBase + "def/dsmod130.mas";
     |CARGA_DEF aMas, dsmod130@;

     nHComple = #130#4 - 1;
     #130#18  = 0;

     |BUCLE dsmod130_2;

     |DESCARGA_DEF #dsmod130@;
|FPRC;

|PROCESO RecalModelo_130;
     nEmpresa  = #130#0;
     nEjer     = #130#1;
     nPeriodo  = #130#2;
     nComplem  = #130#4;

     #130#19 = 0;
     |SI #130#1 = 2008; |Y aAplica ! "N";
         |SI #130#2 = 6;   #130#19 = 200;  |FINSI;
         |SI #130#2 = 9;   #130#19 = 100;  |FINSI;
         |SI #130#2 = 12;  #130#19 = 100;  |FINSI;
     |FINSI;

     nImpAnt   = #130#17 - #130#18;
     |SI #130#1 = 2008; |Y #130#2 ] 4;
         |SI #130#20 ! 0; |O #130#21 ! 0; |O #130#22 ! 0; |O #130#23 ! 0;
             nImpAnt = #130#23;  ||ya estaba calculado
         |FINSI;
     |FINSI;

     #130#18 = 0;
     #130#20 = #130#17 - #130#19;
     #130#21  = 0;
     |SI #130#2 ! 3;
         |HAZ CapturaNegativos130;
     |FINSI;

     #130#22 = #130#20 - #130#21;
     nEl23 = #130#22;

     |SI #130#4 > 0;
         |HAZ CapturaOtros130;
         nEl23 = nEl23 - #130#18;
     |FINSI;
     #130#23 = #130#22 - #130#18;

     |GRABA 020#130a;
     |LIBERA #130;

     nImpNuevo = #130#23;
     |HAZ Actual_m004;
|FPRC;

|DEFBUCLE dsmod130;
    #130#1;
    ;
    nEmpresa, nEjer, 01, nModelo, 00, 00;
    nEmpresa, nEjer, 12, nModelo, 99, 99;
    ;
    NULL, RecalModelo_130;
|FIN;

|PROCESO Actualiza;
    nModelo   = #93#0;
    nEjer     = #93#1;
    nEmpresa  = #93#2;
    |ABRE #4;
    |SI nModelo = 130; |BUCLE dsmod130; |FINSI;
    |SI nModelo = 131; |BUCLE dsmod131; |FINSI;
    |CIERRA #4;
|FPRC;

|CALCULO Tipo1_93; |TIPO 1;
   nModo = (FEntrada / 100) ! 0;
   |SI nModo = 3;
       |CONFI "    SConfirmacion Baja  (Aplicar Minoracion) ";
       |SI FSalida ! 0;
           |ERROR;
           |ACABA;
       |FINSI;

       aAplica   = "S";
       |HAZ Actualiza;
   |FINSI;
|FCAL;

|CALCULO Aplicar; |TIPO 0;
   aAplica   = #93#4;
   |HAZ Actualiza;
|FCAL;

|| ***************************************************************************
|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|| .... Mirar si hace el modelo de IVA
|PROCESO MiraSiD;
 |SI #2#8 > "01.01.0000";
     |SI #2#8 > fFecFin;  |ACABA;  |FINSI;
 |FINSI;
 |SI #2#9 > "01.01.0000";
     |SI #2#9 < fFecIni;  |ACABA;  |FINSI;
 |FINSI;
 nSwConPro = 0;
 |ERROR10;
|FPRC;

|DEFBUCLE MiraSiModelo;
 #2#1;
 ;
 #93#2, 01, #93#0;
 #93#2, 99, #93#0;
 ;
 NULL, MiraSiD;
|FIN;

|PROCESO LeeEmpresa;
  || Lee Empresa
    errconta = 0;
    |ABRE #1000;
    #1000#0 = nEmpresa;
    |LEE 041#1000=;
    |SI FSalida ! 0;
        errconta = 1;
    |FINSI;
    |CIERRA #1000;
|FPRC;

|PROCESO Tipo11_93; |TIPO 11;
 nInkey = FSalida;
 |SI nInkey = 11;
     |ERROR;
     eaElProm = "dsmom093";
     |HAZ ConsuCliente7;
     eaElProm = "";
     |PTEC 802;
     |ACABA;
 |FINSI;
|FPRC;

|PROCESO HazBorrar;
  nModo = (FEntrada / 100) ! 0;
  |SI nModo = 2;
      |LEE 041#93=;

      |BORRA 020#93a;
      |LIBERA #93;
      |PONCONTROL  23, "SI";
  |FINSI;
  |ERROR;
  |PINTA #93#2;
|FPRC;

|PROCESO Tipo0_93; |TIPO 0;

    nEmpresa = #93#2;
    |HAZ LeeEmpresa;
    |SI errconta = 1;
        |MENAV "    No existe Empresa ";
        |HAZ HazBorrar;
        |ACABA;
    |FINSI;
    #93#3 = #1000#1; |PINTA #93#3;

    nSwConPro = -1;
    |BUCLE MiraSiModelo;
    |SI nSwConPro = -1;
        aAlfa1 = "    Empresa sin Modelo " + #93#0 + " en el Planing";
        |MENAV aAlfa1;
        |HAZ HazBorrar;
        |ACABA;
    |FINSI;

    aCoBien = "N";
    |ABRE #114;
    #114#0 = nEmpresa;
    #114#1 = 00;
    |LEE 041#114];
    |SI FSalida = 0; |Y #114#0 = nEmpresa;
        aCoBien = #114#13;  ||Comunidad de Bienes
    |FINSI;
    |CIERRA #114;
    |SI aCoBien = "S";
        aAlfa1 = "    Empresa COMUNIDAD DE BIENES       ";
        |MENAV aAlfa1;
        |HAZ HazBorrar;
        |ACABA;
    |FINSI;

     nNoModi = 0;
     |BUCLE NoModificar;
     |SI nNoModi = 1;
         |MENAV "    Empresa con Modelos Remesados o Archivados, no puede modificarlos";
         |ERROR;
    |FINSI;
|FPRC;

|| **************************************************************************
|PROCESO ClaveBaja;
 #93#0 = #96#0;
 #93#1 = #96#1;
 #93#2 = 1;
|FPRC;

|PROCESO ClaveAlta;
 #93#0 = #96#0;
 #93#1 = #96#1;
 #93#2 = 99999;
|FPRC;

|CALCULO Tipo12; |TIPO 12;

  aAlfa1 = #96#1;
  aAlfa1 = "01.01." + aAlfa1; fFecIni = aAlfa1;
  aAlfa1 = #96#1;
  aAlfa1 = "31.12." + aAlfa1; fFecFin = aAlfa1;

 |ENTLINEAL #1#93, -3, 1, 20, 0, ClaveBaja, ClaveAlta;
|FCAL;

|PROGRAMA;
     aParam = PARAMETRO; |QBF aParam;
     |SI aParam = ""; aParam = "130"; |FINSI;

     |CLS;
     |CABEZA "Empresas sin Aplicar Minoracion";
     |PINPA #0#96;
     #96#0 = aParam;
     |PINDA #0#96;

     |ENDATOS #1#96;
|FPRO;
