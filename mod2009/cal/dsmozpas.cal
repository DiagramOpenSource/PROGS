|FICHEROS;
     dsmozpas #0;
     dsmom002 #2;
     dsmom003 #3;
     dsmom004 #4;
     dsmom005 #5;
     dsmom006 #6;
     dsmom008 #8;
     dsmow013 #999;

     :/basica/def/agitab99 #99;
     :/basica/def/agifige1 #1000,MANTE;
|FIN;

|VARIABLES;
     aFichero   = "";
     aParametro = "";
     aAlfa      = "";
     aAlfa1     = "";
     aAlfa2     = "";
     nCampo    = 0;
     nDEmpresa = 0;
     nHEmpresa = 0;
     nMes      = 0;
     nAnyo     = 0;

     fFecha    = @;

     enGraba   = 0;

     fFecha1     = @;
     fFecha2     = @;
     nNuevo      = 0;
     nPrimero    = 0;
     nSim006     = 0;
     &enEmpresaA = 0;
     &enAnyo     = 0;
     nFalta      = 0;
     nExiste     = 0;
|FIN;

|INCLUYE i_variar;

|| /////////////////////// PROCESOS ENTRADA POR PANTALLA \\\\\\\\\\\\\\\\\\\\

|PROCESO Compensar;  |TIPO 7;
     |C_M #0#21, 1, "S";

     |SI #0#0 ! 300;  |Y #0#0 ! 320;  |Y #0#0 ! 330;  |Y #0#0 ! 332;
         |C_M #0#21, 1, "N";
         #0#21 = "N";  |PINTA #0#21;
         |ACABA;
     |FINSI;

     |SI #0#0 = 300;
         |SI #0#1 = 3;
             |C_M #0#21, 1, "N";
             #0#21 = "N";  |PINTA #0#21;
         |FINSI;
     |SINO;
         |SI #0#1 = 1;
             |C_M #0#21, 1, "N";
             #0#21 = "N";  |PINTA #0#21;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Modelo;
     |SI aParametro = "311";  |O aParametro = "371";
         |ACABA;
     |FINSI;

     |ABRE #99;
     #99#0 = #0#0;
     |LEE 040#99=;
     |SI FSalida ! 0;  |DDEFECTO #99;  |FINSI;
     |CIERRA #99;

     |SI #0#0 = 180;  |O #0#0 = 190;  |O #0#0 = 390;
      |O #0#0 = 392;  |O #0#0 = 193;  |O #0#0 = 347;
      |O #0#0 = 296;
         #99#4 = "S";
         #99#8 = "S";
     |FINSI;

     |SI #99#4 = "N";
         |MENAV "     Modelo sin Activar.";
         |ERROR;
     |FINSI;

     |SI #99#8 = "N";
         |MENAV "     Modelo sin Pase desde Registros Contabilidad";
         |ERROR;
     |FINSI;

     |SI #99#0 = 110; |O #99#0 = 115;
         |PINTA 2314, "Emitir Informe de las Empresas Impreso Sin Actividad: ";
         |C_V #0#22,1,"S";
         |C_M #0#22,1,"S";
         |PINTA #0#22;
     |SINO;
         |PINTA 2314, "                                                       ";
         |C_V #0#22,1,"N";
         |C_M #0#22,1,"N";
     |FINSI;

     #0#1 = 0;
     |SI #0#0 = 180;  #0#1 = 99;  |FINSI;
     |SI #0#0 = 190;  #0#1 = 99;  |FINSI;
     |SI #0#0 = 193;  #0#1 = 99;  |FINSI;
     |SI #0#0 = 296;  #0#1 = 99;  |FINSI;
     |SI #0#0 = 390;  #0#1 = 99;  |FINSI;
     |SI #0#0 = 392;  #0#1 = 99;  |FINSI;
     |SI #0#0 = 347;  #0#1 = 99;  |FINSI;

     |SI #0#1 = 99;
         |C_M #0#1, 1, "N";
         |PINTA #0#1;
     |SINO;
         fFecha = ~;
         aAlfa  = fFecha % 402;   nMes  = aAlfa;
         aAlfa  = fFecha % -104;  nAnyo = aAlfa;
         |SI #99#5 = "T";  |O #99#5 = "C";
             |SI nMes [ 3;
                 #0#1 = 12;
                 #0#3 = nAnyo - 1;
                 |HAZ Periodo;
                 |PINTA #0#1;
                 |PINTA #0#3;
                 |ACABA;
             |FINSI;

             |SI nMes [ 6;
                 #0#1 = 3;
                 #0#3 = nAnyo;
                 |HAZ Periodo;
                 |PINTA #0#3;
                 |PINTA #0#1;
                 |ACABA;
             |FINSI;

             |SI nMes [ 9;
                 #0#1 = 6;
                 #0#3 = nAnyo;
                 |HAZ Periodo;
                 |PINTA #0#3;
                 |PINTA #0#1;
                 |ACABA;
             |FINSI;

             |SI nMes [ 12;
                 #0#1 = 9;
                 #0#3 = nAnyo;
                 |HAZ Periodo;
                 |PINTA #0#3;
                 |PINTA #0#1;
                 |ACABA;
             |FINSI;
         |FINSI;

         |SI #99#5 = "A";
             #0#1 = 99;
             #0#3 = nAnyo - 1;
             |HAZ Periodo;
             |PINTA #0#3;
             |PINTA #0#1;
         |FINSI;

         |SI #99#5 = "M";
             |SI nMes = 1;
                 #0#1 = 12;
                 #0#3 = nAnyo - 1;
             |SINO;
                 #0#1 = nMes - 1;
                 #0#3 = nAnyo;
             |FINSI;
             |HAZ Periodo;
             |PINTA #0#1;
             |PINTA #0#3;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Periodo;
     #0#2 = "          ";
     |SI #0#1 = 1;   #0#2 = "ENERO     ";  |FINSI;
     |SI #0#1 = 2;   #0#2 = "FEBRERO   ";  |FINSI;
     |SI #0#1 = 3;   #0#2 = "MARZO     ";  |FINSI;
     |SI #0#1 = 4;   #0#2 = "ABRIL     ";  |FINSI;
     |SI #0#1 = 5;   #0#2 = "MAYO      ";  |FINSI;
     |SI #0#1 = 6;   #0#2 = "JUNIO     ";  |FINSI;
     |SI #0#1 = 7;   #0#2 = "JULIO     ";  |FINSI;
     |SI #0#1 = 8;   #0#2 = "AGOSTO    ";  |FINSI;
     |SI #0#1 = 9;   #0#2 = "SEPTIEMBRE";  |FINSI;
     |SI #0#1 = 10;  #0#2 = "OCTUBRE   ";  |FINSI;
     |SI #0#1 = 11;  #0#2 = "NOVIEMBRE ";  |FINSI;
     |SI #0#1 = 12;  #0#2 = "DICIEMBRE ";  |FINSI;
     |SI #0#1 = 99;  #0#2 = "ANUAL     ";  |FINSI;

     |SI #0#2 = "          ";
         |MENAV "     Periodo Incorrecto.";
         |ERROR;
     |FINSI;
     |PINTA #0#2;
|FPRC;

|PROCESO Parrilla;
     |SI #0#4 = 0;
         #0#4 = 0;  |PINTA #0#4;
         #0#5 = 0;  |PINTA #0#5;  |C_M #0#5, 1, "N";
         |PARA nCampo = 6;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#4, 1, "S";
             |C_M #0#5, 1, "S";
         |FINSI;

         |PARA nCampo = 6;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|| /////////////////////// PROCESOS DE TRABAJO \\\\\\\\\\\\\\\\\\\\

|PROCESO dsmom008;
     |SI #8#5 = 99;
         |SI #0#21 = "N";  |ACABA;  |FINSI;
     |SINO;
         |SI #8#3 ! 110;  |Y #8#3 ! 115;  |O #8#3 ! 123; |O #8#3 ! 216;
             |SI #8#7 ! "REGISTROS ";  |ACABA;  |FINSI;
         |FINSI;
     |FINSI;

     #999#0 = #8#0;
     #999#1 = #8#5;
     |LEE 101#999=;
     |SI FSalida ! 0;
         #1000#0 = #8#0;
         |LEE 040#1000=;
         |SI FSalida ! 0;  |DDEFECTO #1000;  |FINSI;

         #999#0  = #8#0;
         #999#1  = #8#5;
         #999#2  = #8#1;
         #999#3  = #8#2;
         #999#4  = #1000#1;
         #999#5  = #0#2;
         #999#6  = #8#6;
         #999#7  = #8#3;
         #999#8  = "N";
         #999#10 = #8#12;
         |GRABA 020#999n;

         enGraba = 1;
     |FINSI;
     |LIBERA #999;
|FPRC;

|DEFBUCLE dsmom008;
     #8#1;
     ;
     #4#0, #4#1, #4#2, #4#3, 00, 01;
     #4#0, #4#1, #4#2, #4#3, 00, 99;
     ;
     NULL, dsmom008;
|FIN;

|PROCESO GrabaTempo;
     |SI #4#9  = "X";  |ACABA;  |FINSI;
     |SI #4#11 = "X";  |ACABA;  |FINSI;
     |SI #4#13 = "X";  |ACABA;  |FINSI;

     |SI #4#5 = "ACTIVIDAD ";  |O #4#5 = "ACT.+ REG.";
         |BUCLE dsmom008;
     |SINO;
         #999#0 = #4#0;
         #999#1 = 0;
         |LEE 101#999=;
         |SI FSalida ! 0;
             #1000#0 = #4#0;
             |LEE 040#1000=;
             |SI FSalida ! 0;  |DDEFECTO #1000;  |FINSI;

             #999#0  = #4#0;
             #999#1  = 0;
             #999#2  = #4#1;
             #999#3  = #4#2;
             #999#4  = #1000#1;
             #999#5  = #0#2;
             #999#6  = #1000#1;
             #999#7  = #4#3;
             #999#8  = "N";
             #999#9  = #4#4;
             #999#10 = #4#3;
             |GRABA 020#999n;

             enGraba = 1;
         |FINSI;
         |LIBERA #999;
     |FINSI;
|FPRC;

|DEFBUCLE dsmom004;
     #4#1;
     ;
     nDEmpresa, #0#3, #0#1, #0#0, 00;
     nHEmpresa, #0#3, #0#1, #0#0, 99;
     ;
     NULL, GrabaTempo;
|FIN;

|PROCESO RealizaPase;
      aAlfa1 = #0#3;
      aAlfa1 = "01.01." + aAlfa1; efFecIni = aAlfa1;
      aAlfa1 = #0#3;
      aAlfa1 = "31.12." + aAlfa1; efFecFin = aAlfa1;
      eaNombreEmp = #999#4;
      eaNomPer    = #0#2;
      eaNombreAct = #999#6;

     |SI #999#10 = "110";  |HAZ Pase110;  |FINSI;
     |SI #999#10 = "115";  |HAZ Pase115;  |FINSI;
     |SI #999#10 = "123";  |HAZ Pase123;  |FINSI;
     |SI #999#10 = "216";  |HAZ Pase216;  |FINSI;
     |SI #999#10 = "300";  |HAZ Pase300;  |FINSI;
     |SI #999#10 = "320";  |HAZ Pase300;  |FINSI;
     |SI #999#10 = "330";  |HAZ Pase300;  |FINSI;
     |SI #999#10 = "332";  |HAZ Pase300;  |FINSI;
     |SI #999#10 = "310";  |HAZ Pase310;  |FINSI;
     |SI #999#10 = "347";  |HAZ Pase347;  |FINSI;
     |SI #999#10 = "180";  |HAZ Pase180;  |FINSI;
     |SI #999#10 = "190";  |HAZ Pase190;  |FINSI;
     |SI #999#10 = "193";  |HAZ Pase193;  |FINSI;
     |SI #999#10 = "296";  |HAZ Pase296;  |FINSI;
     |SI #999#10 = "390";  |HAZ Pase390;  |FINSI;
     |SI #999#10 = "392";  |HAZ Pase392;  |FINSI;

     |SI #999#10 = 300; |O #999#10 = 310; |O #999#10 = 110;
         |O #999#10 = 115; |O #999#10 = 123;  |O #999#10 = 216;
            |HAZ ChequeoAnual;
     |FINSI;
|FPRC;

|DEFBUCLE dsmow013;
     #999#1;
     8;
     00001, 00, "S";
     99999, 99, "S";
     ;
     NULL, RealizaPase;
|FIN;

|RUTINA ClaveBaja999;
     #999#0 = 1;
     #999#1 = 00;
|FRUT;

|RUTINA ClaveAlta999;
     #999#0 = 99999;
     #999#1 = 99;
|FRUT;

|PROCESO Tipo3;  |TIPO 3;
     aFichero = ("pas" + Usuario) % 108;
     |NOME_DAT #999 aFichero;
     |ABRE #999;  |CIERRA #999;  |DELINDEX #999;

     |ABRE #999;
     |ABRE #1000;
     |SI #0#4 ! 0;
         nDEmpresa = #0#4;
         nHEmpresa = #0#5;
         |BUCLE dsmom004;
     |SINO;
         |PARA nCampo = 6;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               nDEmpresa = #0nCampo;
               nHEmpresa = #0nCampo;
               |BUCLE dsmom004;
         |SIGUE;
     |FINSI;
     |CIERRA #1000;
     |CIERRA #999;

     |SI enGraba = 1;
         |HAZ PonBoton;
         |ENTLINEAL #1#999, -1, 4, 22, 1, ClaveBaja999, ClaveAlta999;
         |HAZ QuitaBoton;
         |CONFI "0000SRealizamos el Pase en estos momentos : ";

         |SI #0#0 = 390;  |O #0#0 = 392;
             |CONFI "0000SChequear Emision de los Modelos Periodicos.";
             enSwCheq390 = FSalida;
         |FINSI;

         |SI #0#22 = "S"; |HAZ eIniciW103; |FINSI;

         |BUCLE dsmow013;

         |SI #0#22 = "S"; |Y enFichW103 = 1;
             |HAZ eListaW103;
         |FINSI;

         |SI #0#0 = 193;  |O #0#0 = 296;
             |MENAV "0000Revisar los importes de Percepciones, Reducciones y Datos Valores";
         |FINSI;
     |SINO;
         |MENAV "     Seleccion Vacia.";
     |FINSI;

     |DELINDEX #999;
|FPRC;


|PROCESO ChequeoAnual;

     |HAZ LeeCtrlLibro; |SI enSwAuAnual = 0; |ACABA; |FINSI;

     nPrimero = #999#10;
     |SI nPrimero = 300; |O nPrimero = 310; nNuevo = 390; |FINSI;
     |SI nPrimero = 110;  nNuevo = 190; |FINSI;
     |SI nPrimero = 115;  nNuevo = 180; |FINSI;
     |SI nPrimero = 123;  nNuevo = 193; |FINSI;
     |SI nPrimero = 216;  nNuevo = 296; |FINSI;

     nFalta = 0;
     |ABRE #2;
     |DDEFECTO #2;
     #2#0 = #999#0;
     #2#1 = #999#1;
     #2#2 = nNuevo;
     |LEE 001#2=;
     |SI FSalida ! 0; nFalta = 1; |FINSI;
     |CIERRA #2;

     nExiste = 0;
     |ABRE #4;
     #4#0 = #999#0;
     #4#1 = #999#2;
     #4#2 = 99;
     #4#3 = nNuevo;
     #4#4 = 00;
     |LEE 040#4=;
     |SI FSalida = 0;
         |LIBERA #4;
         |CIERRA #4;
         nExiste = 1;
         |SI nFalta = 0; |ACABA; |FINSI;       || si que existe sale
     |FINSI;

     |SI enMasivo = 1;
         aAlfa  = ("00000" + #999#0) % -105;
         aAlfa  = aAlfa + " " + eaNombreEmp; |QBF aAlfa;
         aAlfa1 = "    S"+ aAlfa + ". No configurado Planing " + nNuevo + ", Desea Configurarlo: ";
     |SINO;
         aAlfa1 = "    SNo configurado Planing del Modelo " + nNuevo + ", Desea Configurarlo : ";
     |FINSI;

     |SI nExiste = 0;
         |CONFI aAlfa1;
         |SI FSalida ! 0; |CIERRA #4; |ACABA; |FINSI;

         |DDEFECTO #4;
     |FINSI;

     nSim006 = 0;
     |ABRE #6;
     #6#0 = #999#0;
     #6#1 = nNuevo;
     |LEE 040#6=;
     |SI FSalida = 0;
         nSim006 = 1;
         #4#5  = #6#9;
         #4#16 = #6#12;
     |SINO;
         |ABRE #5;
         #5#0 = nNuevo;
         |LEE 040#5=;
         |SI FSalida ! 0;  |DDEFECTO #5;  |FINSI;
         #4#5  = #5#8;
         #4#16 = #5#11;
         |CIERRA #5;
     |FINSI;
     |CIERRA #6;

     |SI nExiste = 0;
         #4#0 = #999#0;
         #4#1 = #999#2;
         #4#2 = 99;
         #4#3 = nNuevo;
         #4#4 = 00;
         |GRABA 020#4n;
         |LIBERA #4;
         |CIERRA #4;
     |FINSI;

     |ABRE #3;
     #3#0 = #4#0;
     #3#1 = #4#1;
     #3#2 = #4#2;
     |LEE 101#3=;
     |SI FSalida ! 0;
         |DDEFECTO #3;
         #3#0 = #4#0;
         #3#1 = #4#1;
         #3#2 = #4#2;
         #3#3 = "ANUAL     ";
         |GRABA 020#3n;
     |FINSI;
     |LIBERA #3;
     |CIERRA #3;

     |ABRE #2;
     |DDEFECTO #2;
     #2#0 = #999#0;
     #2#1 = #999#1;
     #2#2 = nPrimero;
     |LEE 000#2=;
     fFecha1 = #2#8;
     fFecha2 = #2#9;

     #2#0 = #999#0;
     #2#1 = #999#1;
     #2#2 = nNuevo;
     |LEE 001#2=;
     |SI FSalida ! 0;
         #2#0  = #999#0;
         #2#1  = #999#1;
         #2#2  = nNuevo;
         #2#10 = 1;
         |GRABA 020#2b;
     |FINSI;
     |SI nSim006 = 1;
         #2#3 = #6#2;
         #2#4 = #6#3;
         #2#5 = #6#4;
     |SINO;
         #2#3 = #5#1;
         #2#4 = #5#2;
         #2#5 = #5#3;
     |FINSI;
     #2#7 = "ANUAL     ";
     #2#6 = eaNombreAct;
     #2#8 = fFecha1;
     #2#9 = fFecha2;
     |GRABA 020#2a;
     |LIBERA #2;
     |CIERRA #2;

     enEmpresaA = #999#0;
     enAnyo     = #999#2;
     |SUB_EJECUTA "dsmoz001";
|FPRC;

|PROGRAMA;
    |SI enEmpresa ! 0;
         aFichero = ("pas" + Usuario) % 108;
         |NOME_DAT #999 aFichero;
         |ABRE #999;  |CIERRA #999;  |DELINDEX #999;

         #0#0 = enModelo;
         #0#1 = enPeriodo;
         #0#3 = enEjer;
         #0#4 = enEmpresa;
         #0#5 = enEmpresa;

         |ABRE #999;
         #999#0  = enEmpresa;
         #999#1  = enActividad;
         #999#2  = enEjer;
         #999#3  = enPeriodo;
         #999#6  = eaNombreAct;
         #999#7  = enModelo;
         #999#8  = "S";
         #999#9  = enComplem;
         #999#10 = enModelo1;
         |GRABA 020#999n;
         |CIERRA #999;

         |SI #0#0 = 390;  |O #0#0 = 392;
             |CONFI "0000SChequear Emision de los Modelos Periodicos.";
             enSwCheq390 = FSalida;
         |FINSI;

         enMasivo = 0;
         |BUCLE dsmow013;

         |SI #0#0 = 193;  |O #0#0 = 296;
             |MENAV "0000Revisar los importes de Percepciones, Reducciones y Datos Valores";
         |FINSI;

         |DELINDEX #999;
         |ACABA;
     |FINSI;

     aParametro = PARAMETRO;  |QBF aParametro;
     |SI aParametro ! 0;
         |DDEFECTO #0;

         #0#0 = aParametro;
         |SI aParametro = "311";
             #0#0 = "310";
             #0#1 = "12";
             |C_M #0#1, 1, "N";
         |FINSI;

         |SI aParametro = "371";
             #0#0 = "370";
             #0#1 = "12";
             |C_M #0#1, 1, "N";
         |FINSI;

         enMasivo = 1;
         |C_M #0#0, 1, "N";
         |PINPA #0#0;
         |PINDA #0#0;
         |ENDATOS #1#0;
     |SINO;
         enSwCompensa = 0;
         enMasivo = 1;
         |MANTE #0;
     |FINSI;
|FPRO;
