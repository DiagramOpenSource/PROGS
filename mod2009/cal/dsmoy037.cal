|FICHEROS;
   dsmoy037 #0;

   dsmom001 #1;
   dsmom043 #43;

   :/integral/def/dsam0000 #2;   ||Clientes
   :/basica/def/agicen14 #114;
   :/basica/def/agim0019 #119;

   dsmow032 #900;          ||Def de impresion
   dsmow037 #901;          ||Def de impresion
   dsmow043 #999,MANTE;
|FIN;

|VARIABLES;
   nInkey      = 0;
   aFichero    = "";
   nCampo      = 0;
   nDEmpresa   = 0;
   nHEmpresa   = 0;
   nDActividad = 0;
   nHActividad = 0;
   fDFecha     = @;
   fHFecha     = @;
   nGTempo     = 0;

   &nCamSel    = 24;
   &nCamTip    = 49;

   &eaDElem = "";
   &eaHElem = "";
   &enDNEle = 0;
   &enHNEle = 0;

   swUnoSolo   = 0;

   aAlfa1 = "";
   aAlfa2 = "";
   aAlfa3 = "";
   nPara1 = 0;
   nPara2 = 0;
   nNume1 = 0;
   nNume2 = 0;
   nNume3 = 0;
   nNume4 = 0;
   fFecha1 = @;
   fFecha2 = @;

   &nPagina  = 0;
   &SwPrim   = 0;
   &enComunero = 0;

   nPin1    = 0;
   aPin1    = "";
   efPn10   = @;
   efPn11   = @;
   swExiste = 0;
   swNoExis = 0;
   aMonMod = "";
   &eaTex1 = "";
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;
|INCLUYE i_numamo;
|INCLUYE i_selcon;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\
|CALCULO losDefectos; |TIPO 7;
 #0#46 = "GANANCIAS Y PERDIDAS PATRIMONIALES DE ELEMENTOS AFECTOS A ACTIVIDADES ECONOMICAS";
 |SI enEjer = 0;
     fFecha1 = ~;
     aAlfa1 = fFecha1;
     aAlfa1 = aAlfa1 % -104;
     #0#1 = aAlfa1;
     #0#1 = #0#1 - 1;
 |SINO;
     #0#1 = enEjer;
 |FINSI;
 |PINTA #0#1;
 |PINTA #0#46;
 |HAZ LaFecha1;
|FCAL;

|CALCULO LaFecha1; |TIPO 7;
 aAlfa1 = #0#1; |QBT aAlfa1;
 aAlfa2 = "01.01."+ aAlfa1; #0#41 = aAlfa2; |PINTA #0#41;
 aAlfa2 = "31.12."+ aAlfa1; #0#42 = aAlfa2; |PINTA #0#42;
|FCAL;

|CALCULO LaFecha0; |TIPO 0;
 aAlfa1 = #0#1; |QBT aAlfa1;
 aAlfa2 = "01.01."+ aAlfa1; fFecha1 = aAlfa2;
 aAlfa3 = "31.12."+ aAlfa1; fFecha2 = aAlfa3;

 |SI #0Campo < fFecha1; |O #0Campo > fFecha2;
     |MENAV "    Error. Fecha fuera del Ejercicio ";
     |ERROR;
 |SINO;
     |SI Campo = 42; |Y #0#41 > #0#42;
         |MENAV "    Error. Fecha Hasta menor a la Fecha Desde ";
         |ERROR;
     |FINSI;
 |FINSI;
|FCAL;

|CALCULO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#2  = enEmpresa;  |PINTA #0#2;
         #0#3  = enEmpresa;  |PINTA #0#3;

         |C_M #0#2, 1, "N";
         |C_M #0#3, 1, "N";
     |FINSI;
|FCAL;

|CALCULO Parrilla; |TIPO 0;
     |SI #0#2 = 0;
         #0#2 = 0;  |PINTA #0#2;
         #0#3 = 0;  |PINTA #0#3;  |C_M #0#3, 1, "N";
         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#2, 1, "S";
             |C_M #0#3, 1, "S";
         |FINSI;

         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas; |TIPO 0;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO numamo; |TIPO 0;
   x_alfa1 = #0Campo;
   |HAZ x_punto;
   #0Campo = x_alfa1;
   |PINTA #0Campo;
|FCAL;


|| **********************************************************************************
|| **********************************************************************************
|| .......................Recoge Empresas a Imprimir
|PROCESO LosElementos1;
  enSwSelEmp = 0;
  |ERROR10;
|FPRC;

|DEFBUCLE LosElementos;
 #43#2;
 1,6;
 enEmpresa, enEjer, eaDElem, enDNEle, nDActividad, fDFecha;
 enEmpresa, enEjer, eaHElem, enHNEle, nHActividad, fHFecha;
 ;
 NULL, LosElementos1;
|FIN;

|PROCESO LEmpresaEOS;

 enEmpresa  = #agifigen#0;

 eaEstadoEmp = "S";
 |SI #agifigen#94 > "01.01.0000";
     eaEstadoEmp = "N";
     |SI #0#24 = "N"; |ACABA; |FINSI;
 |FINSI;

|| ... Seleccion de Tipo de Empresa
  nSPin1 = #agifigen#87;
  enSwSelEmp = 1;
  |SI #0#49 = "S";
      |SI nSPin1 ] #0#25; |Y nSPin1 [ #0#26;
          enSwSelEmp = 0;
      |FINSI;
  |SINO;
      |SI nSPin1 = #0#27;
          enSwSelEmp = 0;
      |SINO;
          |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
                |SI nSPin1 = #0nCampo; |Y nSPin1 ! 0;
                    enSwSelEmp = 0;
                    |SAL;
                |FINSI;
          |SIGUE;
      |FINSI;
  |FINSI;
  |SI enSwSelEmp = 1; |ACABA; |FINSI;
|| ------------------------------------------------------------------------

   enSwSelEmp = 1;
   |BUCLE LosElementos;
   |SI enSwSelEmp = 0;
       |DDEFECTO #999;
       nGTempo = nGTempo + 1;
       #999#0 = nGTempo;       || Guia
       #999#1 = enEmpresa;     || Empresa
       #999#2 = 0;             || Periodo Contable
       #999#3 = enEjer;        || A¤o
       #999#4 = 01;            || Desde Mes
       #999#12 = eaEstadoEmp;  || Estado
       #999#13 = aMonMod;      || Moneda
       #999#14 = #agifigen#1;  || Nombre Empresa
       #999#15 = "";          || Path
       #999#16 = #0#44;        || Emitir
       |GRABA 020#999n;
   |FINSI;
|FPRC;

|DEFBUCLE agifigen;
 #agifigen#1;
 ;
 nDEmpresa;
 nHEmpresa;
 e;
 NULL, LEmpresaEOS;
|FIN;

||///////////////////////////////////////////////////////////////////
|CALCULO Datos7; |TIPO 7;
 |MENSA "    <F3> Cambiar Emitir S/N";
 |ATRI R; |PINTA 1169, "Imp"; |ATRI 0;
|FCAL;

|CALCULO DatosManip; |TIPO 11;
   nInkey = FSalida;
   |BLIN 04;
   |SI nInkey = 11;
       |SI #999#16 = "S";
           #999#16 = "N";
       |SINO;
           #999#16 = "S";
       |FINSI;
       |GRABA 020#999a;
   |FINSI;
|FCAL;

|RUTINA inferior;
   #999#0 = 00001;
|FRUT;

|RUTINA superior;
   #999#0 = 99999;
|FRUT;

||///////////////////////////////////////////////////////////////////
|CALCULO Tipo3;  |TIPO 3;

    aMonMod = "P";
    |HAZ LeeEURODBMOD;
    |SI EURODBMOD = 2; aMonMod = "E"; |FINSI;

    enEjer      = #0#1;
    nDActividad = #0#37; nHActividad = #0#38;
    eaDElem     = #0#39; eaHElem     = #0#40;
    fDFecha     = #0#41; fHFecha     = #0#42;
    enDNEle     = #0#47; enHNEle     = #0#48;

    aFichero = ("43w" + Usuario) % 108;
    |NOME_DAT #999 aFichero;
    |ABRE #999;  |CIERRA #999;  |DELINDEX #999;

    |ABRE #999;
    nGTempo = 0;   ||Busco Empresas Contables
    |SI #0#2 ! 0;
        nDEmpresa = #0#2;
        nHEmpresa = #0#3;
        |BUCLE agifigen;
    |SINO;
        |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
              nDEmpresa = #0nCampo;
              nHEmpresa = #0nCampo;
              |BUCLE agifigen;
        |SIGUE;
    |FINSI;
    |CIERRA #999;

    |SI nGTempo = 0;    || No existe ninguna empresa
        |MENAV "    Error. No existe ninguna Empresa dentro de la Seleccion ";
        |DELINDEX #999;
        |ACABA;
    |FINSI;

    |SI swUnoSolo = 0;
        |C_V #999#12,1,"N"; |C_P #999#16,1,1270;
        |PUSHV 0509 2375;
        |ENTLINEAL #1#999, -1, 2, 22, 1, inferior, superior;
        |POPV;
    |FINSI;

    |HAZ Imprimir;

    |DELINDEX #999;
|FCAL;

||------------------------------------------------------------------------
|| *************************************************************************

|PROCESO dsmom001;
     |SI #1#8 ! "01.01.0000";
         |SI #1#8 > #0#42; |ACABA;  |FINSI;
     |FINSI;

     |SI #1#9 ! "01.01.0000";
         |SI #1#9 < #0#41;  |ACABA;  |FINSI;
     |FINSI;

     |DDEFECTO #901;

     |ABRE #2;
     #2#0 = #1#3;
     |LEE 040#2=;
     |SI FSalida ! 0;  |DDEFECTO #2;  |FINSI;
     |CIERRA #2;

     |ABRE #114;
     #114#0 = #2#0; || Empresa
     #114#1 = #1#4;
     |LEE 041#114=;
     |SI FSalida = 0;
         #901#14 = #114#18;  ||Codigo en Renta
     |FINSI;
     |CIERRA #114;

     #901#0 = #1#3;
     #901#1 = #1#5;   || Nombre
     #901#2 = #2#3;   || NIF
     #901#3 = #1#7;   || % Participacion

     enComunero = 1;
     |PRINT;
|FPRC;

|DEFBUCLE dsmom001;
     #1#1;
     ;
     #43#0, #43#1, 0001;
     #43#0, #43#1, 9999;
     ;
     NULL, dsmom001;
|FIN;

|PROCESO dsmom043_I;
   |SI SwPrim = 0;
       nPagina  = 1;
   |FINSI;
   eaTex1 = "GANANCIA PATRIMONIAL ";
   |SI #43#13 < 0; eaTex1 = "PERDIDA PATRIMONIAL "; |FINSI;

   enComunero = 0;

   |SI #114#13 = "S";
       |BUCLE dsmom001;
   |SINO;
     |PRINT;
   |FINSI;

   |PIEINF;
   SwPrim = 1;
|FPRC;


|DEFBUCLE dsmom043;
 #43#1;
 1,6;
 enEmpresa, eaDElem, enDNEle, enEjer, enActividad, fDFecha;
 enEmpresa, eaHElem, enHNEle, enEjer, enActividad, fHFecha;
 e;
 NULL, dsmom043_I;
|FIN;

||////////////////////////////////////////////////////////////////////////
|PROCESO Emision;
   enEmpresa   = #999#1;
   enEjer      = #999#3;   || A¤o
   eaNombreEmp = #999#14; || Nombre Empresa

   SwPrim = 0;
   |PARA nPara1 = nDActividad; |SI nPara1 [ nHActividad; |HACIENDO nPara1 = nPara1 + 1;
         enActividad = nPara1;
         |HAZ LeeDatosEmpresa;

         |SI  swNoExis = 0;       ||SI existe esta Actividad.
              |BUCLE dsmom043;
         |FINSI;
   |SIGUE;
|FPRC;

|DEFBUCLE dsmow043;
 #dsmow043#1;
 16;
 000001, "S";
 999999, "S";
 ;
 NULL, Emision;
|FIN;

|PROCESO Imprimir;
    |IMPRE 0;
    |SI FSalida ! 0;  |ACABA;  |FINSI;

    |INFOR "dsmoy037";
    |SI FSalida ! 0;  |ACABA;  |FINSI;

    |BUCLE dsmow043;

    |FININF;
    |FINIMP;
|FPRC;


|| *********************** Datos de la Empresa y de la Actividad.

|PROCESO LeeDatosEmpresa;
  |DDEFECTO #900;

  #900#0 = enEmpresa;
  nPin1 = 0;
  aPin1 = "";
  efPn10 = "01.01.0000";
  efPn11 = "01.01.0000";

  || Lee Primera Actividad
    |ABRE #agicen14;
    #agicen14#0 = #900#0; || Empresa
    #agicen14#1 = 00;
    |LEE 041#agicen14.];
    |SI FSalida = 0; |Y #agicen14#0 = #900#0;
        #900#5  = #agicen14#9;
        #900#6  = #agicen14#10;
        #900#7  = #agicen14#7;
        #900#8  = #agicen14#8;
        efPn10  = #agicen14#5;
        efPn11  = #agicen14#6;
    |FINSI;

  || Lee Actividad en concreto.
    swNoExis = 0;
    #agicen14#0 = #900#0; || Empresa
    #agicen14#1 = enActividad;
    |LEE 041#agicen14.=;
    |SI FSalida = 0;
        #900#15 = #agicen14#1;   ||Act.
        #900#16 = #agicen14#2;   ||Tipo Epig.
        #900#17 = #agicen14#3;   ||Epigrafe
        #900#18 = #agicen14#4;   ||Descripcion Actividad
        #900#19 = #agicen14#5;   ||Fecha Inicio
        #900#20 = #agicen14#6;   ||Fecha Fin
        #900#21 = #agicen14#11;  ||Sector actividad
        #900#22 = #agicen14#12;  ||Sector actividad
        #900#23 = #agicen14#16;  ||Tipo Actividad
        #900#24 = #agicen14#17;  ||Descripcion Tipo Actividad
        #900#51 = #agicen14#20;  ||CNAE
        #900#52 = #agicen14#18;  ||Codigo en Renta
    |SINO;
        |CIERRA #agicen14;
        swNoExis = 1;
        |ACABA;
    |FINSI;
    |CIERRA #agicen14;

 || Lee Ficha Sociedades
    swExiste = 0;
    |ABRE #agim0019;
    #agim0019#0 = #900#0; || Empresa
    #agim0019#1 = 99;
    |LEE 041#agim0019.];
    |SI FSalida = 0;
        |SI #agim0019#0 ! #900#0;
            |LEE 041#agim0019.a;
            |SI FSalida = 0; |Y #agim0019#0 = #900#0;
                swExiste = 1;
            |FINSI;
        |SINO;
            swExiste = 1;
        |FINSI;
    |FINSI;
    |SI swExiste = 1;
        efPn10 = #agim0019#7;      || Fecha Activa
        efPn11 = #agim0019#55;     || Fecha Inactiva
        nPin1 = #agim0019#4;
        aPin1 = #agim0019#5;
    |FINSI;
    |CIERRA #agim0019;

    |ABRE #agifigen;
    #agifigen#0 = #900#0; || Empresa
    |LEE 041#agifigen.=;
    |SI FSalida = 0;
        efPn10  = #agifigen#93;
        efPn11  = #agifigen#94;
        nPin1   = #agifigen#87;
        aPin1   = #agifigen#88;
        #900#1 = #agifigen#13;  || Nif
        #900#2 = #agifigen#1;   || Nombre
    |FINSI;
    |CIERRA #agifigen;
    |SI efPn10 [ "01.01.1900"; efPn10 = "01.01.0000"; |FINSI;
    |SI efPn11 [ "01.01.1900"; efPn11 = "01.01.0000"; |FINSI;
    #900#3 = nPin1;
    #900#4 = aPin1;
    #900#9 = efPn10;
    #900#10 = efPn11;

    #900#11 = "ACTIVA";
    |SI #999#12 = "N"; #900#11 = "INACTIVA"; |FINSI;
    #900#12 = #0#1;                                   || Ejercicio
    #900#13 = fDFecha;
    #900#14 = fHFecha;

    #900#50 = #0#46; ||Titulo del Libro
|FPRC;
|| **************************************************************************

|PROGRAMA;
 |SI enEmpresa ! 0;

     swUnoSolo = 1;
     |DDEFECTO #0;
     #0#1 = enEjer;
     #0#2 = enEmpresa;
     #0#3 = enEmpresa;
     #0#39 = eaDElem;
     #0#40 = eaHElem;
     #0#47 = enDNEle;
     #0#48 = enHNEle;

     aAlfa1 = #0#1; |QBT aAlfa1;
     aAlfa2 = "01.01."+ aAlfa1; #0#41 = aAlfa2;
     aAlfa2 = "31.12."+ aAlfa1; #0#42 = aAlfa2;
     #0#46 = "GANANCIAS Y PERDIDAS PATRIMONIALES DE ELEMENTOS AFECTOS A ACTIVIDADES ECONOMICAS";

     |HAZ Tipo3;
 |SINO;
     swUnoSolo = 0;
     |CLS;
     |MANTE #0;
 |FINSI;
|FPRO;
