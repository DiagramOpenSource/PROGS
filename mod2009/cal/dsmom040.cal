|FICHEROS;
   dsmom040 #40,MANTE;

   :/contagen/def/cacuenta #300;
   :/contagen/def/cacuebal #305;
   :/contagen/def/caparbal #306;
   :/contagen/def/caparexp #307;
   :/contagen/def/caagrupa #308;
   :/contagen/def/caepigra #309;
   :/contagen/def/cafanta1 #310;
   :/contagen/def/caclipro #311;
   :/contagen/def/anm00001 #313;
   :/contagen/def/anm00007 #314;

   :/basica/def/agicen14 #114;
   :/basica/def/agifigen #1000;
|FIN;

|VARIABLES;
   nSwEsta   = 0;
   nInkey    = 0;
   &errconta = 0;
   &entrada  = 1;
   alfa1     = "";
   alfa2     = "";
   alfa3     = "";
   alfa4     = "";
   nume1     = 0;
   nume2     = 0;
   nume3     = 0;
   nume4     = 0;

   inkey       = 0;
   ModoEntrLin = 0;
   aPath1990   = "";
   nPer1990    = 0;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;  || Variables de Contabilidad

**************************************************************************
LLAMADAS DESDE CUENTAS
**************************************************************************
|PROCESO elPath;
    |SI Campo = 3; |O Campo = 5;
        aPathConta = aPath1990;
        enPerConta = nPer1990;
    |SINO;
        aPathConta = eaPath2008;
        enPerConta = enPer2008;
    |FINSI;
    |PATH_DAT #300 #aPathConta#; |PATH_PAN #300 #aPathPanConta#;
    |PATH_DAT #305 #aPathConta#; |PATH_PAN #305 #aPathPanConta#;
    |PATH_DAT #306 #aPathConta#; |PATH_PAN #306 #aPathPanConta#;
    |PATH_DAT #307 #aPathConta#; |PATH_PAN #307 #aPathPanConta#;
    |PATH_DAT #308 #aPathConta#; |PATH_PAN #308 #aPathPanConta#;
    |PATH_DAT #309 #aPathConta#; |PATH_PAN #309 #aPathPanConta#;
    |PATH_DAT #310 #aPathConta#; |PATH_PAN #310 #aPathPanConta#;
    |PATH_DAT #311 #aPathConta#; |PATH_PAN #311 #aPathPanConta#;
    |PATH_DAT #313 #aPathConta#; |PATH_PAN #313 #aPathPanConta#;
    |PATH_DAT #314 #aPathConta#; |PATH_PAN #314 #aPathPanConta#;

|FPRC;

|PROCESO VoyCambio;
 |SI Campo = 10; eaCuenta = #40#3; |FINSI;
 |SI Campo = 12; eaCuenta = #40#5; |FINSI;
 |HAZ CambioCta;
 #40Campo = eaCta2008; |PINTA #40Campo;
|FPRC;

|CALCULO cuenta08; |TIPO 7;
  |C_M #40Campo,0,alfa1;
  |SI alfa1 = "N"; |ACABA; |FINSI;

  ModoEntrLin = (FEntrada / 100) ! 0;
  |SI ModoEntrLin = 1;
      |HAZ VoyCambio;
  |FINSI;
|FCAL;


|CALCULO cuentag; |TIPO 6;
     |C_M #40Campo,0,alfa1;
     |SI alfa1 = "N"; |ACABA; |FINSI;

     nInkey = FSalida;
     |SI nInkey = 10;
         |SI Campo = 10; |O Campo = 12;
             |HAZ VoyCambio;
             |SI Campo = 10;
                 Campo = 12;
                 |HAZ VoyCambio;
             |FINSI;
             |ERROR;
         |FINSI;
     |FINSI;

     |HAZ elPath;

     emCta = 1;        || Dar mensajes de error
     eaCuenta = #40Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #40Campo = eaCuenta;
     |PINTA #40Campo;

     |HAZ SacaNivel;
     nume1 = Campo + 1;
     #40nume1 = enNivel;
|FCAL;

|CALCULO refrescag; |TIPO 0;
   |C_M #40Campo,0,alfa1;
   |SI alfa1 = "N"; |ACABA; |FINSI;

   emCta = 0;        || no Dar mensajes de error
   eaCuenta = #40Campo;
   |HAZ LeeCuenta;
   |SI eswCta = 1;
        ||  |ERROR;
        |CONFI "    SError.! No existe Cuenta. Desea darla de alta ";
        |SI FSalida = 0;
            |SUB_EJECUTA_NP "dsmoz030";
            #40Campo = eaCuenta;
            |PINTA #40Campo;
            |SI eswCta = 1; |ERROR; |FINSI;
        |FINSI;
   |SINO;
        |SI eswCta = 2;
            |MENAV "0000Esta Cuenta NO tiene Pase Directo.";
            |ERROR;
        |FINSI;
   |FINSI;
|FCAL;

|PROCESO elTipo3g; |TIPO 3;
    #dsmom040#8 = #1000#1; ||A todas las Lineas Nombre empresa
|FPRC;
|| ============================================================

|RUTINA ClaveBaja0;
     #dsmom040#0 = enEmpresa;
     #dsmom040#2 = 0001;
|FRUT;

|RUTINA ClaveAlta0;
     #dsmom040#0 = enEmpresa;
     #dsmom040#2 = 9999;
|FRUT;

|CALCULO cuenGrupo; |TIPO 7;
|FCAL;

|| ----- Cuentas analiticas
|CALCULO AntesAnaG; |TIPO 7;
 ModoEntrLin = (FEntrada / 100) ! 0;
 |SI enSwAna = 1;
     eaCuenta = #dsmom040#3; |SI #40#10 > "            "; eaCuenta = #40#10; |FINSI;
     |HAZ LeeTablaAna;
     |SI swRelac = 0;
         #dsmom040#9 = eaCtaAna;
         |C_M #dsmom040#9,1,eaAnaMod;
     |SINO;
         |SI ModoEntrLin = 1; #dsmom040#9 = ""; |FINSI;
         |SI eaPAna = "S";
             |C_M #dsmom040#9,1,"N";
         |SINO;
             |C_M #dsmom040#9,1,"S";
         |FINSI;
     |FINSI;
     |PINTA #dsmom040#9;
 |FINSI;
|FCAL;

|CALCULO anactaG; |TIPO 0;
  inkey = FSalida;
  |SI enSwAna = 0; |ACABA; |FINSI;

    salidas = FSalida; |HAZ FPuntosAna;
    eOpAna = 0; || 1 = Consulta pantalla
    |SI inkey = 10; eOpAna = 1;  |FINSI;
    eaCtaAna = #dsmom040#9;
    |HAZ LeeCtaAna;
    |SI eswLect = 1;
        |CONFI "    SError.! No existe Cuenta. Desea darla de alta ";
        |SI FSalida = 0;
            |SUB_EJECUTA_NP "dsmoz035";
        |FINSI;
        #dsmom040#9 = eaCtaAna;
        |PINTA #dsmom040#9;
        |ERROR;
    |SINO;
        #dsmom040#9 = eaCtaAna;
        |PINTA #dsmom040#9;
   |FINSI;
|FCAL;

|PROCESO EntraAlaLineag;  |TIPO 5;
  |SI enModo ! 2; |ACABA; |FINSI;

  || Lee Empresa
    |ABRE #1000;
    #1000#0 = enEmpresa;
    |LEE 040#1000=;
    |SI FSalida ! 0;
        |MENAV "     Empresa Inexistente";
        |CIERRA #1000;
        |ACABA;
    |FINSI;
    #dsmom040#8 = #1000#1;
    |CIERRA #1000;

    |SI eaAnNiv = "E";
        |HAZ Analitica;
    |FINSI;

  |C_M #40#3, 1,"S"; |C_M #40#5, 1,"S";
  |C_M #40#10,1,"S"; |C_M #40#12,1,"S";

  enConcreto = 1;
  enPerConta = -1;
  enEjer     = 2007;
  enSwSinMen = 1;
  |HAZ SituaContagen;
  |SI swerror = 1;
     |HAZ SituaMaestra;
     |SI swerror = 1;
          |C_M #40#3,1,"N"; |C_M #40#5,1,"N";
     |SINO;
          aPath1990 = aPathConta;
          nPer1990  = enPerConta;
     |FINSI;
  |SINO;
     aPath1990 = aPathConta;
     nPer1990  = enPerConta;
  |FINSI;

  enPerConta = -1;
  eaPath2008 = "";
  enPer2008  = -1;
  enEjer     = 2008;
  enSwSinMen = 1;
  |HAZ SituaContagen;
  |SI swerror = 1;
      |C_M #40#10,1,"N"; |C_M #40#12,1,"N";
  |SINO;
      eaPath2008 = aPathConta;
      enPer2008  = enPerConta;
      nLongMax = MaximoDigitos;
  |FINSI;
  enConcreto = 0;
|FPRC;
