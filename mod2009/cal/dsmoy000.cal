|FICHEROS;
     dsmoy000 #0;

     dsmom001 #1;
     dsmom004 #4;
     dsmom008 #8;
     dsmom023 #23;
     dsmom024 #24;
     dsmom101 #101;
     dsmom102 #102;

     :/basica/def/agicen14 #114;
     :/basica/def/agitab99 #99;

     dsmow014 #999;
     dsmom107 #207; || control Modelos
     &clientes@ #715;
|FIN;

|VARIABLES;
      nMes      = 0;
      nAnyo     = 0;

      aComproba = "";

     &enEmpresa = 0;
     &enPeriodo = 0;
     &enEjer    = 0;
     &eaElProm  = "dsmoy000";
     &enComplem = 0;
     &aCoBien   = "";
     aContinua   = "";
     nSwIm       = 0;

     nDComple = 0;
     nHComple = 0;
|FIN;

|INCLUYE i_varemi;

|| /////////////////////// PROCESOS ENTRADA POR PANTALLA \\\\\\\\\\\\\\\\\\\\

|PROCESO MiraVarMoneda;
     nTecla = FSalida;
     |HAZ PintaMoneda;

     #0#37 = eaMoneda;
     |SI #0#37 = "PESETAS";
         |SI #0#0 = 180;  |O #0#0 = 190;  |O #0#0 = 390;  |O #0#0 = 392;
          |O #0#0 = 184;  |O #0#0 = 193;  |O #0#0 = 296;
             |MENAV "     Moneda incorrecta. Debe ser EUROS.";
             |ERROR;
         |FINSI;
         |SI #0#6 ] 2002;
             |SI #0#0 = 110;  |O #0#0 = 115;  |O #0#0 = 130;  |O #0#0 = 131;
              |O #0#0 = 300;  |O #0#0 = 310;  |O #0#0 = 311;
                 |MENAV "     Moneda incorrecta. Debe ser EUROS.";
                 |ERROR;
             |FINSI;
         |FINSI;
     |FINSI;
     |PINTA #0#37;
     FSalida =  nTecla;
|FPRC;

|PROCESO Periodo;
     |SI #0#4 = 1;   #0#5 = "ENERO     ";  |FINSI;
     |SI #0#4 = 2;   #0#5 = "FEBRERO   ";  |FINSI;
     |SI #0#4 = 3;   #0#5 = "MARZO     ";  |FINSI;
     |SI #0#4 = 4;   #0#5 = "ABRIL     ";  |FINSI;
     |SI #0#4 = 5;   #0#5 = "MAYO      ";  |FINSI;
     |SI #0#4 = 6;   #0#5 = "JUNIO     ";  |FINSI;
     |SI #0#4 = 7;   #0#5 = "JULIO     ";  |FINSI;
     |SI #0#4 = 8;   #0#5 = "AGOSTO    ";  |FINSI;
     |SI #0#4 = 9;   #0#5 = "SEPTIEMBRE";  |FINSI;
     |SI #0#4 = 10;  #0#5 = "OCTUBRE   ";  |FINSI;
     |SI #0#4 = 11;  #0#5 = "NOVIEMBRE ";  |FINSI;
     |SI #0#4 = 12;  #0#5 = "DICIEMBRE ";  |FINSI;
     |SI #0#4 = 99;  #0#5 = "ANUAL     ";  |FINSI;

     |SI aComproba ! "C";  |PINTA #0#5;  |FINSI;
     |SI aComproba = "C";  |PINTA #0#39;  |FINSI;
     |PINTA #0#6;
|FPRC;

|PROCESO Modelo;
     |SI eaParametro = "311";  |O eaParametro = "311C";
      |O eaParametro = "371";  |O eaParametro = "371C";
         |ACABA;
     |FINSI;

     |ABRE #99;
     #99#0 = #0#0;
     |LEE 040#99=;
     |SI FSalida ! 0;  |DDEFECTO #99;  |FINSI;
     |CIERRA #99;

     |SI #0#0 = 180;  |O #0#0 = 190;  |O #0#0 = 390;  |O #0#0 = 392;
      |O #0#0 = 184;  |O #0#0 = 193;  |O #0#0 = 296;
         #99#4 = "S";
     |FINSI;

     |SI #99#4 = "N";
         |MENAV "     Modelo sin Activar.";
         |ERROR;
     |FINSI;

     |SI aComproba ! "C";
         |C_M #0#40, 1, "S";
     |FINSI;

     #0#4 = 0;
     |SI #0#0 = 180;  #0#4 = 99;  |C_M #0#40, 1, "N";  #0#40 = "T";  |FINSI;
     |SI #0#0 = 184;  #0#4 = 99;  |C_M #0#40, 1, "N";  #0#40 = "T";  |FINSI;
     |SI #0#0 = 190;  #0#4 = 99;  |C_M #0#40, 1, "N";  #0#40 = "T";  |FINSI;
     |SI #0#0 = 193;  #0#4 = 99;  |C_M #0#40, 1, "N";  #0#40 = "T";  |FINSI;
     |SI #0#0 = 296;  #0#4 = 99;  |C_M #0#40, 1, "N";  #0#40 = "T";  |FINSI;
     |SI #0#0 = 390;  #0#4 = 99;  |C_M #0#40, 1, "N";  #0#40 = "T";  |FINSI;
     |SI #0#0 = 392;  #0#4 = 99;  |C_M #0#40, 1, "N";  #0#40 = "T";  |FINSI;
     |SI #0#0 = 347;  #0#4 = 99;  |C_M #0#40, 1, "N";  #0#40 = "T";  |FINSI;
     |SI #0#0 = 349;              |C_M #0#40, 1, "N";  #0#40 = "T";  |FINSI;

     |PINTA #0#40;

     #0#39 = #0#4;

     fFecha = ~;
     aAlfa  = fFecha % -104;
     #0#6   = aAlfa;
     |SI #0#4 = 99;
          #0#6   = #0#6  - 1;
     |FINSI;

     |SI #0#4 = 99;
         |C_M #0#4, 1, "N";
         |PINTA #0#4;
         |HAZ Periodo;
     |SINO;
         fFecha = ~;
         aAlfa  = fFecha % 402;   nMes  = aAlfa;
         aAlfa  = fFecha % -104;  nAnyo = aAlfa;
         |SI #99#5 = "T";  |O #99#5 = "C";
             |SI nMes [ 3;
                 #0#4  = 12;
                 #0#39 = 12;
                 #0#6 = nAnyo - 1;
                 |HAZ Periodo;
                 |PINTA #0#4;
                 |PINTA #0#6;
                 |ACABA;
             |FINSI;

             |SI nMes [ 6;
                 #0#4  = 3;
                 #0#39 = 3;
                 #0#6  = nAnyo;
                 |HAZ Periodo;
                 |PINTA #0#4;
                 |PINTA #0#6;
                 |ACABA;
             |FINSI;

             |SI nMes [ 9;
                 #0#4  = 6;
                 #0#39 = 6;
                 #0#6  = nAnyo;
                 |HAZ Periodo;
                 |PINTA #0#4;
                 |PINTA #0#6;
                 |ACABA;
             |FINSI;

             |SI nMes [ 12;
                 #0#4  = 9;
                 #0#39 = 9;
                 #0#6  = nAnyo;
                 |HAZ Periodo;
                 |PINTA #0#4;
                 |PINTA #0#6;
                 |ACABA;
             |FINSI;
         |FINSI;

         |SI #99#5 = "A";
             #0#4  = 99;
             #0#39 = 99;
             #0#6  = nAnyo - 1;
             |HAZ Periodo;
             |PINTA #0#4;
             |PINTA #0#6;
         |FINSI;

         |SI #99#5 = "M";
             |SI nMes = 1;
                 #0#4  = 12;
                 #0#39 = 12;
                 #0#6  = nAnyo - 1;
                 |HAZ Periodo;
             |SINO;
                 #0#4  = nMes - 1;
                 #0#39 = nMes - 1;
                 #0#6  = nAnyo;
                 |HAZ Periodo;
             |FINSI;
             |PINTA #0#4;
             |PINTA #0#6;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Planillas;
     |C_M #0#36, 1, "S";

     #0#10 = 1;      |PINTA #0#10;  |C_M #0#10, 1, "N";
     #0#11 = 99999;  |PINTA #0#11;  |C_M #0#11, 1, "N";
     |PARA nCampo = 12;  |SI nCampo [ 35;  |HACIENDO nCampo = nCampo + 1;
          |C_M #0nCampo, 1, "N";
     |SIGUE;
|FPRC;

|PROCESO Parrilla;
     #0#10 = 1;      |PINTA #0#10;  |C_M #0#10, 1, "N";
     #0#11 = 99999;  |PINTA #0#11;  |C_M #0#11, 1, "N";
     #0#36 = 0;  |PINTA #0#36;  |C_M #0#36, 1, "N";
     |PARA nCampo = 12;  |SI nCampo [ 35;  |HACIENDO nCampo = nCampo + 1;
          |C_M #0nCampo, 1, "S";
     |SIGUE;
|FPRC;

|PROCESO DesdeHasta;
     |C_M #0#10, 1, "S";
     |C_M #0#11, 1, "S";
     #0#36 = 0;  |PINTA #0#36;  |C_M #0#36, 1, "N";

     |PARA nCampo = 12;  |SI nCampo [ 35;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "N";
           #0nCampo = 0;  |PINTA #0nCampo;
     |SIGUE;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 35;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 35;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO BotonParrilla;
     |CONTROL_SIMPLE 0, "LBOTON,F2 Seleccion Empresas Desde-Hasta", 2007, 2039, Boton1;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F3 Seleccion Empresas Parrilla   ", 2107, 2139, Boton2;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F4 Seleccion por Planillas       ", 2207, 2239, Boton3;
     nBoton3 = FSalida;

     aF2 = &255 + "824";
     aF3 = &255 + "825";
     aF4 = &255 + "826";
     |PARA; |SI;  |HACIENDO;
         |LEETECLA aTecla;
         |SI aTecla = aF2;  |HAZ DesdeHasta;  |SAL;  |FINSI;
         |SI aTecla = aF3;  |HAZ Parrilla;    |SAL;  |FINSI;
         |SI aTecla = aF4;  |HAZ Planillas;   |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;

     |PULSATECLA;
|FPRC;

|PROCESO MiraPlanilla;  |TIPO 0;
     |SI #0#36 = 0;  |ACABA;  |FINSI;

     |ABRE #101;
     #101#0 = #0#36;
     |LEE 040#101=;
     |SI FSalida ! 0;
         |MENAV "     Registro de Planilla Inexistente.";
         |ERROR;
     |FINSI;
     |CIERRA #101;
|FPRC;

|PROCESO GrabaTempo;
     |SI #0#40 = "N";
         |SI #4#15 > 0;  |ACABA;  |FINSI;
     |FINSI;

     |SI #0#40 = "P";
         |SI #4#15 [ 0;  |ACABA;  |FINSI;
     |FINSI;

     |SI #0#9 = "N";
         |SI #4#9 = "X";   |ACABA;  |FINSI;
     |SINO;
         |SI #0#9 = "S";  |Y aComproba ! "C";
             |SI #4#13 = "X";  |ACABA;  |FINSI;
         |FINSI;
     |FINSI;

     nSwEsta = 1;

     |SI #0#36 ! 0;
         nSwEsta = 0;
         |ABRE #102;
         #102#0 = #0#36;
         #102#1 = #4#0;
         |LEE 040#102=;
         |SI FSalida = 0;
             nSwEsta = 1;
         |FINSI;
         |CIERRA #102;
     |FINSI;

     |SI #0#12 ! 0;
         nSwEsta = 0;
         |PARA nCampo = 12;  |SI nCampo [ 35;  |HACIENDO nCampo = nCampo + 1;
               |SI #0nCampo = #4#0;
                   nSwEsta = 1;
                   |SAL;
               |FINSI;
         |SIGUE;
     |FINSI;

     |SI nSwEsta = 0;  |ACABA;  |FINSI;

     enCodCli = #4#0;
     |HAZ LeePersona;

     #999#0  = #4#0;
     #999#1  = #4#1;
     #999#2  = #4#2;
     |LEE 101#999=;
     |SI FSalida ! 0;
         |DDEFECTO #999;
         #999#0  = #4#0;
         #999#1  = #4#1;
         #999#2  = #4#2;
         |GRABA 020#999b;
     |FINSI;

     #999#3  = #715#1;
     #999#4  = #0#5;
     #999#5  = #0#0;
     #999#6  = "S";
     #999#7  = #4#4;
     |GRABA 020#999a;
     |LIBERA #999;

     nSwGraba = 1;
|FPRC;

|DEFBUCLE dsmom004;
     #4#1;
     7;
     #0#10, #0#6, #0#4,  #0#0, nDComple, "X";
     #0#11, #0#6, #0#39, #0#0, nHComple, "X";
     ;
     NULL, GrabaTempo;
|FIN;

|PROCESO RelComuneros0;
     #0#10 = #1#3;
     #0#11 = #1#3;
     |ABRE #999;
     |BUCLE dsmom004;
     |CIERRA #999;
|FPRC;

|DEFBUCLE RelComuneros;
  #1#1;
  ;
  enEmpresa, 01, 0001;
  enEmpresa, 99, 9999;
  e;
  NULL, RelComuneros0;
|FIN;


|RUTINA ClaveBaja999;
     #999#0 = 1;
     #999#1 = 0000;
     #999#2 = 00;
|FRUT;

|RUTINA ClaveAlta999;
     #999#0 = 99999;
     #999#1 = 9999;
     #999#2 = 99;
|FRUT;

|PROCESO MiraCuantos;
     enSwHay = enSwHay + 1;
|FPRC;

|DEFBUCLE dsmow014M;
     #999#1;
     6;
     00000, 0000, 00, "S";
     99999, 9999, 99, "S";
     ;
     NULL, MiraCuantos;
|FIN;

|PROCESO Tipo3;  |TIPO 3;
     |SI aComproba ! "C";
         #0#39 = #0#4;
     |FINSI;

     |GRABA 020#0n;
     |LIBERA #0;

     eaMoneda = #0#37;

     nDComple = 0; nHComple = 99;
     |SI eaParametro = "PLANING";
         nDComple = enComplem;
         nHComple = enComplem;
     |FINSI;

     aFichero = ("imp" + Usuario) % 108;
     |NOME_DAT #999 aFichero;
     |ABRE #999;  |CIERRA #999;  |DELINDEX #999;

     nSwGraba = 0;

     |SI aCoBien = "S";
         |BUCLE RelComuneros;
     |SINO;
         |ABRE #999;
         |BUCLE dsmom004;
         |CIERRA #999;
     |FINSI;

     |SI nSwGraba = 1;
         |PINPA #0#999;
         |HAZ PonBoton;
         |ENTLINEAL #1#999, -1, 4, 21, 0, ClaveBaja999, ClaveAlta999;
         |HAZ QuitaBoton;

         enSwHay = 0;
         |BUCLE dsmow014M;
     |SINO;
         |MENAV "     Seleccion Vacia.";
         |DELINDEX #999;
         |ACABA;
     |FINSI;

     eaPathLanza = "C:\MODELOS";                       |RMKDIR eaPathLanza;
     eaPathLanza = eaPathLanza + "\" + #0#0;           |RMKDIR eaPathLanza;
     eaPathLanza = eaPathLanza + "\" + #0#6;           |RMKDIR eaPathLanza;
     eaPathLanza = eaPathLanza + "\";

     |DBASE eaPathEmision;
     eaPathEmision = eaPathEmision + "emisiones";                     |MKDIR eaPathEmision;
     eaPathEmision = eaPathEmision + "/" + #0#0;                      |MKDIR eaPathEmision;
     eaPathEmision = eaPathEmision + "/" + #0#6;                      |MKDIR eaPathEmision;
     eaPathEmision = eaPathEmision + "/" + (("00" + #0#4) % -102);    |MKDIR eaPathEmision;
     eaPathEmision = eaPathEmision + "/";

     |HAZ LeeUnidad;
     eaPathInternet = eaUnidad + "/AEAT";                             |RMKDIR eaPathInternet;
     eaPathInternet = eaPathInternet + "/INTERNET";                   |RMKDIR eaPathInternet;
     eaPathInternet = eaPathInternet + "/" + #0#0;                    |RMKDIR eaPathInternet;
     eaPathInternet = eaPathInternet + "/" + #0#6;                    |RMKDIR eaPathInternet;
     eaPathInternet = eaPathInternet + "/" + (("00" + #0#4) % -102);  |RMKDIR eaPathInternet;
     eaPathInternet = eaPathInternet + "/";

     |SI enSwHay = 0;
         |MENAV "      Seleccion Vacia";
     |SINO;
         aAlfa = "dsmoy" + #0#0;
         |SUB_EJECUTA_NP aAlfa;
         |SI aCoBien = "S";
             |HAZ ImpComunidad;
         |FINSI;
     |FINSI;

     |DELINDEX #999;

     |CIERRA #0;
     |DELINDEX #0;
|FPRC;

|PROCESO Boton1;
     |PTEC 824;
|FPRC;

|PROCESO Boton2;
     |PTEC 825;
|FPRC;

|PROCESO Boton3;
     |PTEC 826;
|FPRC;

|PROCESO VerComunidad;
  || Lee Primera Actividad
    aCoBien    = "N";
    aContinua  = "S";
    |ABRE #114;
    #114#0 = enEmpresa;
    #114#1 = 00;
    |LEE 041#114];
    |SI FSalida = 0; |Y #114#0 = enEmpresa;
        aCoBien = #114#13;  ||Comunidad de Bienes
    |FINSI;
    |CIERRA #114;

    |SI aCoBien = "S";
        |PUSHV 1225 1575;
        |CUADRO 1225 1575;
        |ATRI I;
        |PINTA 1327, "Esta Empresa es Una COMUNIDAD DE BIENES       ";
        |PINTA 1427, "Desea Imprimir los Pagos de sus Comuneros S/N ";
        E_sup  = 1;
        E_inf  = "SNsn";
        aContinua = "S";
        aContinua = 1473 ? 0;
        aContinua = aContinua > " ";
        |ATRI R; |PINTA 1473, aContinua; |ATRI 0;
        |POPV;
    |FINSI;
|FPRC;

|PROCESO dsmom004I0;
     |SI #4#9 ! "X";  nSwIm = 1;  |FINSI;
|FPRC;

|DEFBUCLE dsmom004I;
     #4#1;
     ;
     #0#10, #0#6, #0#4,  #0#0, 00;
     #0#11, #0#6, #0#39, #0#0, 99;
     ;
     NULL, dsmom004I0;
|FIN;

|PROCESO RelComun_I0;
 #0#10 = #1#3;
 #0#11 = #1#3;
 |BUCLE dsmom004I;
|FPRC;

|DEFBUCLE RelComun_I;
  #1#1;
  ;
  enEmpresa, 01, 0001;
  enEmpresa, 99, 9999;
  e;
  NULL, RelComun_I0;
|FIN;

|PROCESO ImpComunidad;
   nSwIm = 0;
   |BUCLE RelComun_I;
   |ABRE #4;
   #4#0  = enEmpresa;
   #4#1  = #0#6;
   #4#2  = #0#4;
   #4#3  = #0#0;
   #4#4  = enComplem;
   |LEE 101#4=;
   |SI FSalida = 0;
       |SI nSwIm = 0;
           #4#9  = "X";
           #4#10 = #0#7;
           |SI #207#15 = "S"; #4#13 = "X"; #4#14 = #0#7; |FINSI;
       |SINO;
           #4#9  = " ";
           #4#10 = "01.01.0000";
       |FINSI;
       |GRABA 020#4a;
       |LIBERA #4;
   |FINSI;
   |CIERRA #4;
|FPRC;

|PROGRAMA;
     |HAZ LeeUnidad;

     |ABRE #207;
     |DDEFECTO #207;
     |LEE 000#207p;
     |CIERRA #207;

     aCoBien  = "";
     aFichero = ("000" + Usuario) % 108;
     |NOME_DAT #0 aFichero;
     |ABRE #0;  |CIERRA #0;  |DELINDEX #0;

     |ABRE #0;

     enCalcCif   = 0;
     eaParametro = PARAMETRO;  |QBF eaParametro;
     |SI eaParametro = "PLANING";
         |DDEFECTO #0;
         #0#0  = enModelo;
         #0#4  = enPeriodo;  |HAZ Periodo;
         #0#39 = enPeriodo;
         #0#6  = enEjer;
         #0#9  = "X";
         #0#10 = enEmpresa;
         #0#11 = enEmpresa;
         #0#40 = "T";
         |SI enModelo = 130; |O enModelo = 131;
             |HAZ VerComunidad;
             |SI aContinua = "N"; |ACABA; |FINSI;
         |FINSI;

         |HAZ PintaMoneda;
         #0#37 = eaMoneda;

         |PUSHV 0501 2380;
         |BLANCO 1025 1244;
         |CUADRO 0925 1244;
         |ATRI R;
         |PINTA 1026, "FECHA PRESENTACION";
         |ATRI r;

         fFecha = ~;
         fFecha = 1130 ? 1;

         #0#7 = fFecha;

         |POPV;

         |HAZ Tipo3;

         |ACABA;
     |FINSI;

     aComproba   = eaParametro % -101;

     |SI aComproba = "C";
         |C_V #0#39, 1, "S";  |C_M #0#39, 1, "S";
         |C_V #0#5,  1, "N";  |C_M #0#5,  1, "N";
                              |C_M #0#40, 1, "N";  #0#40 = "T";
     |SINO;
         |C_V #0#39, 1, "N";  |C_M #0#39, 1, "N";
         |C_V #0#5,  1, "S";  |C_M #0#5,  1, "N";
     |FINSI;

     aModelo     = eaParametro % 103;
     |SI eaParametro ! "";
         |ABRE #99;
         #99#0 = aModelo;
         |LEE 040#99=;
         |SI FSalida ! 0;  |DDEFECTO #99;  |FINSI;
         |CIERRA #99;

         |SI aModelo ! 180;  |Y aModelo ! 190;  |Y aModelo ! 390;  |Y aModelo ! 193;
          |Y aModelo ! 311;  |Y aModelo ! 371;  |Y aModelo ! 392;  |Y aModelo ! 184;
          |Y aModelo ! 296;
             |SI #99#4 = "N";
                 |CIERRA #0;  |DELINDEX #0;
                 |MENAV "     Modelo sin Activar.";
                 |ACABA;
             |FINSI;
         |FINSI;
         |CIERRA #99;

         |DDEFECTO #0;

         aAlfa = eaParametro % 103;
         #0#0  = aAlfa;
         |SI eaParametro = "311";  |O eaParametro = "311C";
             #0#0  = "310";
             #0#4  = "12";
             #0#39 = "12";
             |C_M #0#4, 1, "N";
         |FINSI;

         |SI eaParametro = "371";  |O eaParametro = "371C";
             #0#0  = "370";
             #0#4  = "12";
             #0#39 = "12";
             |C_M #0#4, 1, "N";
         |FINSI;

         |C_M #0#0, 1, "N";
         |PINPA #0#0;
         |PINDA #0#0;
         |ENDATOS #1#0;
     |SINO;
         |PINPA #0#0;
         |PINDA #0#0;
         |ENDATOS #1#0;
     |FINSI;
     |PULSATECLA;
|FPRO;
