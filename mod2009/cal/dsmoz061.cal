|FICHEROS;
   dsmoz061 #0;
   dsmom040 #40;

   :/basica/def/agifigen #1000;
   :/contagen/def/ca8m0003 #403;
   :/contagen/def/ca8ctrle #400;  ||Control Empresa
   :/contagen/def/ca8eqmay #402;  ||Control Empresa

   :/contagen/def/canempre #300;
|FIN;

|VARIABLES;
  nCampo      = 0;
  nEmpresa    = 0;
  nDEmpresa   = 0;
  nHEmpresa   = 0;
  nPara1      = 0;

  &swUno      = 0;
  &enEmpreOri = 0;
  &enEmpreDes = 0;
  aAlfa1 = "";
  aAlfa2 = "";
  aAlfa3 = "";
  nNume1 = 0;
  nNume2 = 0;
  nNume3 = 0;
  period = 0;
  nHay   = 0;
  uno    = "";
  dos    = "";
  nLong1 = 0;
  nLong2 = 0;
  aPath  = "";
  aCtaNueva = "";
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;

|| =====================================================================
|CALCULO Parrilla;
     |SI #0#2 = 0;
         #0#2 = 0;  |PINTA #0#2;
         #0#3 = 0;  |PINTA #0#3;  |C_M #0#3, 1, "N";
         |PARA nCampo = 4;  |SI nCampo [ 15;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#2, 1, "S";
             |C_M #0#3, 1, "S";
         |FINSI;

         |PARA nCampo = 4;  |SI nCampo [ 15;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;
               |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 15;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 15;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

||******************************************************************************
|PROCESO Cambio;
  |QBF eaCuenta;
  aCtaNueva = eaCuenta;
  |SI eaCuenta = ""; |ACABA; |FINSI;

  nHay = 0;
  |SI period ! -1;
      |ABRE #403;
      #403#0 = eaCuenta;
      |LEE 000#403=;
      |SI FSalida = 0;
          aCtaNueva = #403#2;
          nHay      = 1;
      |FINSI;
      |CIERRA #403;
  |FINSI;

  |SI nHay = 0;  || Lo transformo ahora
      |ABRE #402;
      |DDEFECTO #402;

      uno = eaCuenta; |QBF uno;
      dos = uno + "000000000000";

      uno = dos % A104; nLong1 = 4;
      #402#0 = eNum2008;
      #402#1 = uno;
      |LEE 040#402=;
      |SI FSalida = 0;  |HAZ Datos;  |ACABA;  |FINSI;

      uno = dos % A103; nLong1 = 3;
      #402#0 = eNum2008;
      #402#1 = uno;
      |LEE 040#402=;
      |SI FSalida = 0;  |HAZ Datos;  |ACABA;  |FINSI;

      uno = dos % A102; nLong1 = 2;
      #402#0 = eNum2008;
      #402#1 = uno;
      |LEE 040#402=;
      |SI FSalida = 0;  |HAZ Datos;  |ACABA;  |FINSI;

      uno = dos % A101; nLong1 = 1;
      #402#0 = eNum2008;
      #402#1 = uno;
      |LEE 040#402=;
      |SI FSalida = 0;
          |HAZ Datos;
      |SINO;
          aCtaNueva = "PDTE";
          |CIERRA #402;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO Datos;
   eaPlan8  = #402#3;
   |QBF eaPlan8;
   |CIERRA #402;

   aAlfa1 = eaPlan8 % 0;
   nNume1 = aAlfa1;     || Longitud del Cambio
   nNume2 = nNume1 + 100; nLong2 = nNume1;

   |SI eaPlan8 = "PDTE";
       aCtaNueva = eaPlan8;
       |ACABA;
   |FINSI;

   nNume3 = 0 - ((nLongMax - nLong2) + 100)
   aAlfa1 = eaCuenta % nNume2;
   aAlfa2 = eaCuenta % nNume3;

   |SI aAlfa1 = eaPlan8;
       aCtaNueva = eaCuenta;
   |SINO;
       aCtaNueva = eaPlan8 + aAlfa2;
   |FINSI;
|FPRC;

|PROCESO GrupsAmor0;
   eaCuenta = #40#3; |HAZ Cambio; #40#10 = aCtaNueva;
   eaCuenta = #40#5; |HAZ Cambio; #40#12 = aCtaNueva;
   #40#8 = #1000#1;
   |GRABA 040#40a;
   |LIBERA #40;
|FPRC;

|DEFBUCLE GrupsAmor;
 #40#1;
 ;
 nEmpresa, 0001;
 nEmpresa, 9999;
 be;
 NULL, GrupsAmor0;
|FIN;

|PROCESO locreo;

  |DBASS aPath; |QBT aPath;
  aPath  = aPath + "contagen/fich/";

  nLongMax = 12;
  nEmpresa = #1000#0;
  period   = -1;

  |ABRE #300;
  |DDEFECTO #300;
  |SELECT #4#300;
  #300#2  = nEmpresa;
  #300#26 = 8;
  |LEE 001#300=;
  |SI FSalida = 0;
      period = #300#3;
  |FINSI;
  |SELECT #1#300;
  |CIERRA #300;

  |ABRE #400;
  |DDEFECTO #400;
  #400#0 = nEmpresa;
  #400#1 = period;
  |LEE 001#400=;
  |SI FSalida = 0;
      eNum2008 = #400#2;
  |SINO;
      #400#0 = nEmpresa;
      #400#1 = 0;
      |LEE 001#400];
      |SI FSalida = 0; |Y #400#0 = nEmpresa;
           eNum2008 = #400#2;
      |FINSI;
  |FINSI;
  |CIERRA #400;

  |SI period ! -1;
      |SI #300#13 = 1; nLongMax = #300#14; |FINSI;
      |SI #300#13 = 2; nLongMax = #300#15; |FINSI;
      |SI #300#13 = 3; nLongMax = #300#16; |FINSI;
      |SI #300#13 = 4; nLongMax = #300#17; |FINSI;
      |SI #300#13 = 5; nLongMax = #300#18; |FINSI;
      |SI #300#13 = 6; nLongMax = #300#19; |FINSI;

      aAlfa1 = nEmpresa; aAlfa1 = ("00000" + aAlfa1) % -105;
      aAlfa2 = period;   aAlfa2 = ("0"     + aAlfa2) % -101;
      aPath  = aPath + aAlfa1 + aAlfa2 + "/";
      |PATH_DAT #403 aPath;
  |FINSI;

  |BUCLE GrupsAmor;
|FPRC;

|DEFBUCLE locreo;
 #1000#1;
 ;
 nDEmpresa;
 nHEmpresa;
 ;
 NULL, locreo;
|FIN;

|CALCULO pasalo; |TIPO 3;
  |SI #0#2 ! 0;
      nDEmpresa = #0#2;
      nHEmpresa = #0#3;
      |BUCLE locreo;
  |SINO;
      |PARA nPara1 = 4; |SI nPara1 [ 15; |HACIENDO nPara1 = nPara1 + 1;
            nDEmpresa = #0nPara1;
            nHEmpresa = #0nPara1;
            |BUCLE locreo;
       |SIGUE;
  |FINSI;
|FCAL;

|| **************************************************************************
|PROGRAMA;
 |SI swUno = 0;
     |CLS;
     |MANTE #0;
 |SINO;
     |DDEFECTO #0;
     #0#2  = enEmpreOri;
     #0#3  = enEmpreDes;
     |HAZ pasalo;
 |FINSI;
|FPRO;
