|FICHEROS;
    dsmoz043 #0;
    dsmom040 #40;
    dsmom041 #41;
    dsmom042 #42;
    dsmom046 #46;

    dsmow123 #123;      || Temporal de Pase Contable
    dsmow046 #998;      || Temporal de Grupos / Cuentas
    dsmow043 #999;      || Temporal de Empresas Contables
|FIN;

|VARIABLES;
  ElPathPan   = "";
  aFichero    = "";
  fecalf      = "";
  nDEmpresa   = 0;
  nHEmpresa   = 0;
  nDActividad = 0;
  nHActividad = 0;
  nCampo      = 0;
  nGTempo     = 0;
  nEjercicio  = 0;
  aPath       = "";

  fecsys  = @;
  alfa1   = "";
  dos     = "";
  tres    = "";
  cuatro  = "";
  cinco   = "";
  cuenta  = "";
  digito  = 0;
  cuenta1 = "";
  digito1 = 0;
  conter  = 0;
  digit   = 0;
  signo   = "";
  dig_dos = 0;
  dig_tre = 0;

  sw_paso   = 0;
  nTotal    = 0;
  nImporte  = 0;
  sw_tmp    = 0;
  x         = 0;
  Lineas    = 0;
  Documento = 0;
  &nCamSel  = 24;
  &nCamTip  = 39;
  aAlfa1    = "";
  aAlfa2    = "";
  aCtaAna   = "";

   nGrupo   = 0;
   nTip     = 0;
   nAnyFin  = 0;
   nDias    = 0;
   nNume1   = 0;
   nNume2   = 0;
   nNume3   = 0;
   nCanti   = 0;
   &EURODB  = 0;
   nSwGraba = 0;
   nSwOpera = 0;

   nSwReparto = 0;
   nParteImp  = 0;
   nTp100     = 0;
   nTodasCon  = 0;
   nSwAlgun   = 0;
   aBlancs    = "";
   nBueno     = 0;

   fFecMensual = @;
   fFecMenahor = @;
   aDescrip   = "";
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;
|INCLUYE i_numamo;
|INCLUYE i_selcon;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\
|CALCULO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#8 = enEmpresa; |PINTA #0#8;
         #0#9 = enEmpresa; |PINTA #0#9;

         |C_M #0#8, 1, "N";
         |C_M #0#9, 1, "N";
     |FINSI;
|FCAL;

|CALCULO Parrilla;
     |SI #0#8 = 0;
         #0#8 = 0;  |PINTA #0#8;
         #0#9 = 0;  |PINTA #0#9;  |C_M #0#9, 1, "N";
         |PARA nCampo = 10;  |SI nCampo [ 21;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#8, 1, "S";
             |C_M #0#9, 1, "S";
         |FINSI;

         |PARA nCampo = 10;  |SI nCampo [ 21;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 21;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 21;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO numamo; |TIPO 0;
    x_alfa1 = #0Campo;
    |HAZ x_punto;
    #0Campo = x_alfa1;
    |PINTA #0Campo;
|FCAL;

|CALCULO ElPtec; |TIPO 7;
 |PTEC 816;
|FCAL;

|CALCULO defecto; |TIPO 7;
   fecsys = ~;
   alfa1 = fecsys % -104;
   #0Campo = alfa1;
   |PINTA #0Campo;
   enEmpresa = 0;
   |HAZ Analitica;
|FCAL;

|CALCULO SiesAna; |TIPO 7;
   |SI enSwAna = 1; |Y eaGAna = "N";
       #0#7 = "N"; |PINTA #0#7;
       |PUSHV 1444 1780;
       |CUADRO 1444 1780;
       |ATRI R; |PINTA 1445, " Contabilidad Analitica con reparto ";
                |PINTA 1545, " de  grupos  no fijo; Contabilizar  ";
                |PINTA 1645, " por elementos amortizables.        ";
       |ATRI 0;
       ||C_M #0#7,1,"N";
   |SINO;
       |C_M #0#7,1,"S";
   |FINSI;
|FCAL;

|CALCULO SiesAna0; |TIPO 0;
   |SI enSwAna = 1; |Y eaGAna = "N";
       |POPV;
   |FINSI;

   |C_M #0#40,1,"S";
   |SI #0#7 = "S";
       |C_M #0#40,1,"N"; #0#40 = " "; |PINTA #0#40;
   |FINSI;
|FCAL;

|CALCULO porDefecto; |TIPO 0;
 alfa1 = #0#2;
 fecalf = "31.12." + alfa1;
 #0#4 = fecalf; |PINTA #0#4;
|FCAL;

|CALCULO EhLafecha; |TIPO 0;
 alfa1 = #0#4;
 alfa1 = alfa1 % -104;
 nNume1 = alfa1;
 |SI nNume1 ! #0#2;
     |CONFI "    NAtencion. La Fecha no corresponde al Ejercicio de Amortizacion. Continuar S/N";
     |SI FSalida ! 0;
         |ERROR;
     |FINSI;
 |FINSI;
|FCAL;

|| /////////////////////////// Procesos \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO LeeLineas;
   nSwAlgun = 1;
   |ERROR10;
|FPRC;

|DEFBUCLE LeeLineas;
 #42#1;
 5,6,8;
 enEmpresa, #0#0, #0#37, 001, #0#2, "A", "N";
 enEmpresa, #0#1, #0#38, 999, #0#2, "A", "N";
 be;
 NULL, LeeLineas;
|FIN;

|PROCESO CalAnyFinPago;
  nImporte = 0;
  nDias    = 365;
  |SI nAnyFin = 1980;  |O nAnyFin = 1984;  |O nAnyFin = 1988;  |O nAnyFin = 1992;
                       |O nAnyFin = 1996;  |O nAnyFin = 2000;  |O nAnyFin = 2004;
                       |O nAnyFin = 2008;  |O nAnyFin = 2012;  |O nAnyFin = 2016;
                       |O nAnyFin = 2020;  |O nAnyFin = 2024;  |O nAnyFin = 2028;
      nDias = 366;
  |FINSI;


  nNume1 = fFecMenahor;
  nNume2 = #42#9;
  nDias = nNume2 - nNume1;

  |SI #41#37 ] fFecMenahor;
      nNume1 = #41#37;  nNume2 = #42#9;
      nNume3 = nNume2 - nNume1;
      |SI nNume3 < 0;
          nNume3 = 0;
      |FINSI;
      nCanti = ((nParteImp * nNume3) / nDias) ! EURODB;
  |SINO;
      nCanti = nParteImp;  || todo
  |FINSI;


  |SI nTip = 1;         || desde el pago
      nImporte = nCanti;
  |FINSI;
  |SI nTip = 2;         || hasta el pago
      nImporte = nParteImp  - nCanti;
  |FINSI;
|FPRC;

|PROCESO L_Amortiza;

   nParteImp = #42#11;
   |SI nTp100 ! 100;
       nParteImp = (#42#11 * nTp100 / 100 ) ! EURODBMOD;
   |FINSI;

   |SI nSwOpera = 0;

       cuenta  = dos;
       digito  = dig_dos;

       cuenta1 = tres;
       digito1 = dig_tre;

       nImporte = nParteImp;
       |SI nTip ! 0;
           |HAZ CalAnyFinPago;
       |FINSI;

       |SI nImporte ! 0;
           sw_paso = 1;
           nTotal = nTotal + nImporte;
           |SI #0#7 = "S"; |HAZ Graba_Cta; |FINSI;
           nSwGraba = 1;
       |FINSI;

       |SI #42#9 > fFecMenahor;
            fFecMenahor = #42#9;
       |FINSI;

   |SINO;

        #42#8 = "S";
        |GRABA 020#42a;
   |FINSI;

   |LIBERA #42;
|FPRC;

|DEFBUCLE LasLineas;
 #42#1;
 5,6,8;
 #41#0, #41#2, #41#3, 001, #0#2, "A", "N";
 #41#0, #41#2, #41#3, 999, #0#2, "A", "N";
 be;
 NULL, L_Amortiza;
|FIN;

|PROCESO GrabaElAuxi;

   #40#0 = #41#0;
   #40#2 = nGrupo;
   |LEE 001#40=;
   |SI FSalida ! 0;  |ACABA; |FINSI;

   |SI enEjer < 2008;
       dos     = #40#3;  || cta. amortiz
       dig_dos = #40#4;  || digito
       tres    = #40#5;  || cta. amortiz acum
       dig_tre = #40#6;  || digito
   |SINO;
       dos     = #40#10;  || cta. amortiz
       dig_dos = #40#11;  || digito
       tres    = #40#12;  || cta. amortiz acum
       dig_tre = #40#13;  || digito
   |FINSI;

   cuatro  = #40#9;
   cinco   = #40#9;

   nTotal   = 0;
   sw_paso  = 0;
   nSwOpera = 0;
   fFecMenahor = fFecMensual;
   |BUCLE LasLineas;
   |SI #0#7 = "N";
       |SI sw_paso = 1;
           nImporte = nTotal;
           cuatro   = #41#34;  ||Cuenta analitica
           cinco    = #41#34;  ||Cuenta analitica
           eaElemento = #41#2;
           enElemeNum = #41#3;
           |HAZ Act_Asiento;
       |FINSI;
   |FINSI;
|FPRC;

|PROCESO C_Amortiza;

   |PINTA 2243, #41#2;
   |PINTA 2252, #41#3;

   nAnyFin = 0;
   |SI #41#36 ! 0;
       aAlfa1 = #41#37;  aAlfa1 = aAlfa1 % -104;
       nAnyFin = aAlfa1;
       |SI nAnyFin = 0; #41#36 = 0; |FINSI;  || como si no existiese
   |FINSI;

   nTip     = 0;  ||total de la linea
   |SI #41#36 = 0; |O #41#38 = 0;  || No Operacion Financiera
       nGrupo = #41#18;
       |HAZ GrabaElAuxi;
   |SINO;
       |SI nAnyFin ! #0#2;  || no es el a¤o en el que termina el pago
           |SI nAnyFin > #0#2;     || pagando
               nGrupo = #41#38;
           |SINO;
               nGrupo = #41#18;    ||  ya esta pagado
           |FINSI;
           |HAZ GrabaElAuxi;
       |SINO;
           nTip   = 2; || pagando
           nGrupo = #41#38;
           |HAZ GrabaElAuxi;
           nTip   = 1; || pagado
           nGrupo = #41#18;
           |HAZ GrabaElAuxi;
       |FINSI;
   |FINSI;

|FPRC;

|PROCESO LeeReparto;
 nSwReparto = 1;  ||existe reparto en diferentes actividades

 |SI #46#1 ] #0#22; |Y  #46#1 [ #0#23;
     enActividad = #46#1;
     nTp100      = #46#5;
     |HAZ C_Amortiza;
 |SINO;
     nTodasCon = 1;
 |FINSI;
|FPRC;

|DEFBUCLE LeeReparto;
 #46#1;
 ;
 #41#0, #41#2, #41#3, 01;
 #41#0, #41#2, #41#3, 99;
 e;
 NULL, LeeReparto;
|FIN;


|PROCESO LeeContab;
 |SI #42#5 < #0#2;
     |SI #42#9 > fFecMensual;
         fFecMensual = #42#9;
     |FINSI;
 |FINSI;
 |SI #42#5 = #0#2; |Y #42#8 = "S";
     |SI #42#9 > fFecMensual;
         fFecMensual = #42#9;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE LeeContab;
 #42#1;
 5,6;
 #41#0, #41#2, #41#3, 001, 1900, "A";
 #41#0, #41#2, #41#3, 999, #0#2, "A";
 be;
 NULL, LeeContab;
|FIN;

|PROCESO LaDescripcion;
 aDescrip = #0#6; |QBF aDescrip;
 |SI aDescrip ! ""; aDescrip = aDescrip + " "; |FINSI;
 |SI #0#40 = "E";
     aDescrip = aDescrip + #41#2 + #41#3;
 |FINSI;
 |SI #0#40 = "D";
     aDescrip = aDescrip + #41#4;
 |FINSI;
 fFecMensual = #41#9; || aqui
 |BUCLE LeeContab;
|FPRC;

|PROCESO LaParteAct;
 |HAZ LaDescripcion;

 nTodasCon  = 0;
   nSwGraba = 0;
 nSwReparto = 0;
 |BUCLE LeeReparto;
 |SI nSwReparto = 0;
     |SI #41#1 ] #0#22; |Y  #41#1 [ #0#23;
         enActividad = #41#1;
         nTp100      = 100;
         |HAZ C_Amortiza;
     |FINSI;
 |FINSI;
 |SI nSwGraba = 1; |Y nTodasCon = 0;
     nSwOpera = 1;
     |BUCLE LasLineas;
 |FINSI;
|FPRC;

|DEFBUCLE LasCabeceras;
   #41#1;
   1;
   enEmpresa, #0#0, #0#37, nDActividad;
   enEmpresa, #0#1, #0#38, nHActividad;
   e;
   NULL, LaParteAct;
|FIN;

|| ********************************************************************
|PROCESO C_Temporal;
   cuenta      = #998#2;
   digito      = #998#3;
   nImporte    = #998#4;
   cuenta1     = #998#5;
   digito1     = #998#6;
   cuatro      = #998#7;
   cinco       = #998#8;
   enActividad = #998#9;
   eaElemento  = "";
   enElemeNum  = 0;
   aDescrip    = #0#6;
   |HAZ Act_Asiento;
|FPRC;

|DEFBUCLE ElTemporal;
   #998#1;
   ;
   0000, 01;
   9999, 99;
   e;
   NULL, C_Temporal;
|FIN;

|PROCESO Empresas;
    enEmpresa = #999#1;
    enPeriodo = #999#2;
    enEjer     = #999#3;   || A¤o
    dig1       = #999#4;   || Desde Mes
    dig3       = #999#5;   || N§ Digitos
    dig4       = #999#6;   || Digitos Nivel 1
    dig5       = #999#7;   || Digitos Nivel 2
    dig6       = #999#8;   || Digitos Nivel 3
    dig7       = #999#9;   || Digitos Nivel 4
    dig8       = #999#10;  || Digitos Nivel 5
    dig9       = #999#11;  || Digitos Nivel 6

   |SI dig3 = 1; MaximoDigitos = dig4; |FINSI;
   |SI dig3 = 2; MaximoDigitos = dig5; |FINSI;
   |SI dig3 = 3; MaximoDigitos = dig6; |FINSI;
   |SI dig3 = 4; MaximoDigitos = dig7; |FINSI;
   |SI dig3 = 5; MaximoDigitos = dig8; |FINSI;
   |SI dig3 = 6; MaximoDigitos = dig9; |FINSI;

    Lineas    = 0;
    Documento = 0;
    sw_tmp    = 0;
    fec3      = #999#23;

    |SI fec3 > "01.01.0000";
        nSwAlgun = 0;
        |BUCLE LeeLineas;
        |SI nSwAlgun = 1; |Y #0#4 [ fec3;
            |PUSHV 1108 1774;
            aBlancs = " " * 66;
            |PINTA 1108, aBlancs; |PINTA 1208, aBlancs;
            |PINTA 1308, aBlancs; |PINTA 1408, aBlancs;
            |PINTA 1508, aBlancs; |PINTA 1608, aBlancs;
            |PINTA 1708, aBlancs;

            |CUADRO 1108 1774;
            |CUADRO 1209 1673;
            |ATRI I;
            |PINTA 1310, " NO SE REALIZA LA CONTABILIZACION DE AMORTIZACIONES  ";
            |PINTA 1410, " EN LA EMPRESA:                                      ";
            |PINTA 1510, " Existe Fecha de Bloqueo igual a                     ";
            |ATRI P; |PINTA 1426, #999#1; |PINTA 1432, #999#14; |ATRI 0;
            |PINTA 1543, #999#23;
            |ATRI 0;
            |PAUSA;
            |POPV;
            |ACABA;
        |FINSI;
    |FINSI;


    aAlfa1 = enEmpresa; aAlfa1 = ("00000" + aAlfa1) % -105;
    |ATRI I; |PINTA 2153, aAlfa1; |ATRI 0;
    aFichero = ("46w" + Usuario) % 108;
    |NOME_DAT #998 aFichero;

    |ABRE #998;  |CIERRA #998;  |DELINDEX #998;
    |ABRE #40;

    |ABRE #998;
    |BUCLE LasCabeceras;
    |CIERRA #998;

    |SI sw_tmp = 1;
        |BUCLE ElTemporal;
    |FINSI;

    |CIERRA #40;

|FPRC;

|DEFBUCLE empreconta;
 #999#1;
 ;
 000000;
 999999;
 e;
 NULL, Empresas;
|FIN;

|PROCESO LeeEmpresas;

   nBueno = 0;
   |SI #canempre#11 = 1;
       |SI #canempre#26 = nEjercicio;
           nBueno = 1;
       |FINSI;
   |SINO;
        |HAZ LaFecha;
        |SI efFechaSit ] fec1; |Y efFechaSit [ fec2;
            nBueno = 1;
        |FINSI;
   |FINSI;
   |SI nBueno = 0; |ACABA; |FINSI;

   enEmpresa  = #canempre#2;
   enPerConta = #canempre#3;
   aAlfa1 = enEmpresa;  aAlfa1 = ("00000" + aAlfa1) % -105;
   aAlfa2 = enPerConta; aAlfa2 = ("0"     + aAlfa2) % -101;
   aPathConta   = aPath +  aAlfa1 + aAlfa2 + "/";

   eaEstadoEmp = "S";
   |SI #canempre#21 = "I"; eaEstadoEmp = "N"; |FINSI;

    enSwSelEmp = 0;
    |HAZ LaSelEmpresa2;
    |SI enSwSelEmp = 0;
        |HAZ GrabaTemporal;
    |FINSI;
|FPRC;

|DEFBUCLE LeeEmpresas;
 #canempre#1;
 ;
 nDEmpresa, 0;
 nHEmpresa, 9;
 ;
 NULL, LeeEmpresas;
|FIN;


|CALCULO Tipo3;  |TIPO 3;
     |HAZ LeeEURODBMOD;
     nDActividad = 1;
     nHActividad = 99;

     aFichero = ("123w" + Usuario) % 108;
     |NOME_DAT #123 aFichero;
     |ABRE #123;  |CIERRA #123;  |DELINDEX #123;
     eaFAsiento = aFichero;

     aFichero = ("043w" + Usuario) % 108;
     |NOME_DAT #999 aFichero;
     |ABRE #999;  |CIERRA #999;  |DELINDEX #999;
     eaFEmpresa = aFichero;

     enTipoAct   = 2;  || Tipo 2  Diario y Fecha de las Lineas, Varios Astos.
     enRelaIVA   = 0;  || Sin Relacionar con IVA
     swSinFich   = 1;  || Con Fichero Auxiliar Empresas
     swSinCheq   = 0;  || Sin Chequear Cuadre

     |DBASS aPath;
     aPath = aPath + "contagen/fich/";

     efFechaSit = #0#4;
     alfa1      = #0#4; alfa1 = alfa1 % -104;
     enEjer     = alfa1;
     alfa1      = alfa1 % -102;
     nEjercicio = alfa1;

     |ABRE #999;
     nGTempo = 0;   ||Busco Empresas Contables
     |SI #0#8 ! 0;
         nDEmpresa = #0#8;
         nHEmpresa = #0#9;
         |BUCLE LeeEmpresas;
     |SINO;
        |PARA nCampo = 10;;  |SI nCampo [ 21;  |HACIENDO nCampo = nCampo + 1;
              nDEmpresa = #0nCampo;
              nHEmpresa = #0nCampo;
              |BUCLE LeeEmpresas;
        |SIGUE;
     |FINSI;
     |CIERRA #999;


     aDescrip = #0#6;

     |SI nGTempo = 0;    || No existe ninguna empresa
         |MENAV "    Error. No existe ninguna Empresa Contable dentro de la Seleccion ";
     |SINO;
         |ABRE #123;
         |BUCLE empreconta;
         |CIERRA #123;
     |FINSI;

     |SUB_EJECUTA_NP "dsmoz123"; || Contabilizacion
|FCAL;

||****************************************

|| ....................... Situa la Empresa Contable

|PROCESO GrabaTemporal;
 |DDEFECTO #999;
 nGTempo = nGTempo + 1;
 #999#0 = nGTempo;      || Guia
 #999#1 = enEmpresa;    || Empresa
 #999#2 = enPerConta;   || Periodo Contable
 #999#3 = enEjer;       || A¤o
 #999#4 = #canempre#11;  || Desde Mes
 #999#5 = #canempre#13;  || N§ Digitos
 #999#6 = #canempre#14;  || Digitos Nivel 1
 #999#7 = #canempre#15;  || Digitos Nivel 2
 #999#8 = #canempre#16;  || Digitos Nivel 3
 #999#9 = #canempre#17;  || Digitos Nivel 4
 #999#10 = #canempre#18; || Digitos Nivel 5
 #999#11 = #canempre#19; || Digitos Nivel 6
 #999#12 = eaEstadoEmp;  || Activa / Inactiva
 #999#13 = #canempre#28; || Moneda
 #999#14 = #canempre#01; || Nombre Empresa
 #999#15 = aPathConta;   || Path
 #999#17 = #canempre#24; || Si/NO Departamentos
 #999#18 = #canempre#22; || Si/No subdepartamentos
 #999#19 = #canempre#30;
 #999#20 = #canempre#31;
 #999#21 = #canempre#32;
 #999#22 = #canempre#33;
 #999#23 = #canempre#34;

 |GRABA 020#999n;
|FPRC;


|PROCESO Graba_Cta;
   |DDEFECTO #998;
   #998#0 = #41#18;
   #998#9 = enActividad;
   |LEE 101#998=;
   |SI FSalida ! 0;
       |DDEFECTO #998;
       #998#0 = #41#18;
       #998#9 = enActividad;
       |GRABA 020#998b;
   |FINSI;
   #998#1 = #998#0;
   #998#2 = cuenta;
   #998#3 = digito;
   #998#4 = #998#4 + nImporte; || ............... cantidad
   #998#5 = cuenta1;
   #998#6 = digito1;
   #998#7 = cuatro;
   #998#8 = cinco;
   #998#9 = enActividad;
   |GRABA 021#998a;
   |LIBERA #998;
   sw_tmp = 1;
|FPRC;

|PROCESO Act_Asiento; || crea asiento contable por linea
   Documento = Documento + 1;

  |PARA x = 1; |SI x [ 2; |HACIENDO x = x + 1;
       |SI x = 1;
           conter = cuenta; digit = digito; signo = "D";
           aCtaAna = cuatro;
       |SINO;
           conter = cuenta1; digit = digito1; signo = "H";
           aCtaAna = cinco;
       |FINSI;

       |DDEFECTO #dsmow123;
        Lineas = Lineas + 1;
        #dsmow123#0  = enEmpresa;
        #dsmow123#1  = enPeriodo;
        #dsmow123#2  = Lineas;
        #dsmow123#3  = #0#3;    || diario
        #dsmow123#4  = #0#4;    || fecha asiento
        #dsmow123#5  = Documento;      || nro. de Documento
        #dsmow123#6  = conter;         || cuenta
        eaCuenta = #dsmow123#6; emCta = 0; |HAZ SacaNivel;
        #dsmow123#7  = enNivel;          || digito
        #dsmow123#8  = #0#5;    || concepto contable
        #dsmow123#9  = aDescrip;    || descripcion
        #dsmow123#10 = signo;          || signo
        #dsmow123#11 = nImporte;       || importe

        aAlfa2 = enActividad; |QBF aAlfa2;
        aAlfa2 = ("00" + aAlfa2) % -102;
        #dsmow123#12 = aAlfa2;    || N§ Actividad = Departamento Ctble.

        #dsmow123#28 = aCtaAna;
        #dsmow123#29 = eaElemento;
        #dsmow123#30 = enElemeNum;
        |GRABA 020#dsmow123.n;
   |SIGUE;
|FPRC;
