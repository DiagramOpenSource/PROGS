|FICHEROS;
     dsmozp80 #0;

     dsmom080 #80;
     dsmom081 #81,MANTE;
     dsmom082 #82;
     dsmom083 #83;

     dsmow082 #982,MANTE;
     dsmow083 #983,MANTE;

     :/basica/def/agifigen #1000;
     :/basica/def/agicen14 #1014;

     reflejo@ #182;
|FIN;

|VARIABLES;
     nNume1      = 0;
     nNume2      = 0;
     &nAny       = 0;
     &aTipoPr    = "";
     aAlfa       = "";
     aAlfa1      = "";
     aAlfa2      = "";
     aAlfa3      = "";

     fFecIni   = @;
     fFecFin   = @;
     nInkey    = 0;
     esalta    = 0;
     esaltaL   = 0;
     nSwSal    = 0;

     aBlanc      = "";

     &aFichw82   = "";
     &nSwLinea   = 0;
     &eaCam1     = "";
     &eaCam2     = "";
     &eaCam3     = "";
     &enCam4     = "";
     &eaCa4      = "";
     &eaCam8     = "";
     &enCam10    = 0;
     &enCam11    = 0;
     &enCam12    = 0;
     &enCam13    = 0;
     &eaCam15    = "";
     &enCam22    = 0;
     &eaTit      = "";
     &enEj82     = "";

     aBase = "";
     aDef  = "";
     nDEmp = 0;
     nHEmp = 0;
     nDAct = 0;
     nHAct = 0;
     informe = "";
     inform1 = "dsmow082";
     inform2 = "dsmow083";

     nBoton2    = 0;
     nBoton3    = 0;
     nBoton4    = 0;
     nBoton5    = 0;
     nBoton6    = 0;
     nBoton7    = 0;
     nBoton8    = 0;
     nBoton9    = 0;
     &aParam    = "";
     &enSw80    = 0;
     &nSwHay84  = 0;
|FIN;

|INCLUYE i_variar;

**************************************************************************
|PROCESO ClaveBajaL83;
  #983#0  = #0#0;
  #983#1  = enEmpresa;
  #983#2  = 00;
|FPRC;

|PROCESO ClaveAltaL83;
  #983#0  = #0#0;
  #983#1  = enEmpresa;
  #983#2  = 99;
|FPRC;

|PROCESO MTipo5;  |TIPO 5;
       |LEE 040#983p;
       #983#2 = 00; |LINEAS_BI_ATRI #983#2, P, D, -1;
|FPRC;

|CALCULO Siesgeneral; |TIPO 0;
 |SI #982#3 = "G";
     |C_M #982#4,1,"S";
 |SINO;
     |C_M #982#4,1,"N";
 |FINSI;
|FCAL;

|PROCESO PintaCab;
        aBlanc = " " * 79;
        |PINTA 0601, aBlanc;
        |PINTA 0701, aBlanc;
        |PINTA 0801, aBlanc;
        |PINTA 0702, "Empresa: "; |PINTA 0711, #982#1;
        |PINTA 0717, #982#2;
        |PINTA 0759, "NIF: "; |PINTA 0764, #982#15;
|FPRC;

|PROCESO FinBotonw082;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;
     |FIN_CONTROL_WINDOWS nBoton4;
     |FIN_CONTROL_WINDOWS nBoton5;
     |FIN_CONTROL_WINDOWS nBoton6;
     |FIN_CONTROL_WINDOWS nBoton7;
     |FIN_CONTROL_WINDOWS nBoton8;
     |FIN_CONTROL_WINDOWS nBoton9;
     |PULSATECLA;
|FPRC;

|PROCESO Tipo0w082;  |TIPO 0;
     |HAZ FinBotonw082;
|FPRC;

|CALCULO Tipo7w082;  |TIPO 7;
     |SI aParam = "P";
         #982#0 = #982#0 + 1;
         |PINTA #982#0;
         #982#0 = #982#0 - 1;
     |FINSI;

     |CONTROL_SIMPLE 0, "LBOTON,F2  Consulta Actividades ", 0732, 0755, Boton2w082;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F3  Vol.Operaciones de Actividades", 0832, 0855, Boton3w082;
     nBoton3 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F4  Consulta Prorrata Aplicadas ", 0932, 0955, Boton4w082;
     nBoton4 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F5  Modificar General / Especial ", 0557, 0580, Boton5w082;
     nBoton5 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F6  Imprimir Relacion Empresas  ", 0657, 0680, Boton6w082;
     nBoton6 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F7  Imprimir Datos de La Empresa ", 0757, 0780, Boton7w082;
     nBoton7 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F8  Resumen Totales Conceptos IVA ", 0857, 0880, Boton8w082;
     nBoton8 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F9  Informe de IVA Comun         ", 0957, 0980, Boton9w082;
     nBoton9 = FSalida;
|FCAL;

|CALCULO Tipo11w082; |TIPO 11;
    nInkey    = FSalida;
    |HAZ FinBotonw082;

    enEmpresa = #982#1;
    nAny      = #0#0;
    |SI nInkey <10; |O nInkey > 17;
        |ACABA;
    |FINSI;

    |PUSHV 0501 2380;
    |LEE 001#982=;

    |SI nInkey = 10; || (F2)
        |HAZ PintaCab;
        enModo    = 4;
        |SUB_EJECUTA_NP ":basica/agicen14";
    |FINSI;

    |SI nInkey = 11; || (F3)
        nSwSal = 0;
        |PINPA #0#983;
        |PINTA 0718, #982#2;  |PINTA 0767, #982#15;
        |PINTA 2217, #982#5;  |PINTA 2229, #982#6;
        |PINTA 2241, #982#22; |PINTA 2252, #982#9;
        |PINTA 2273, #982#3;  |PINTA 2275, #982#19;
        |ENTLINEAL #1#983, -3, 4, 20, 0, ClaveBajaL83, ClaveAltaL83;
    |FINSI;

    |SI nInkey = 12;  || (F4)
        |HAZ PintaCab;
        |HAZ ConsultaGE;
    |FINSI;

    |SI nInkey = 16; || (F8)
        |SI #982#20 = -1;
            |MENAV "0000Empresa Sin Contabilidad ";
            |ACABA;
        |FINSI;
        enPeriodo = #982#20;
        |SUB_EJECUTA_NP ":contagen/cay3ivas;modelos";
    |FINSI;

    |SI nInkey = 17; || (F9)
        |SI #982#20 = -1;
            |MENAV "0000Empresa Sin Contabilidad ";
            |ACABA;
        |FINSI;
        enPeriodo = #982#20;
        |SUB_EJECUTA_NP ":contagen/cay5ivas;modelos";
    |FINSI;

    |POPV;

    |SI nInkey = 13;

        |SI #982#17 = 1; |MENAV "    Empresa con una sola actividad. Prorrata General "; |ACABA; |FINSI;
        aAlfa1 = "General"; aAlfa2 = "Especial";
        |SI #982#3 = "E"; aAlfa2 = "General"; aAlfa1 = "Especial"; |FINSI;
        aAlfa  = "0000NATENCION!!! Cambiar el Tipo de Prorrata de " + aAlfa1 + " a " + aAlfa2 + ". Conforme ";
        |CONFI aAlfa;
        |SI FSalida ! 0; |ACABA; |FINSI;

        |SI #982#3 = "G";
            #982#3 = "E";
        |SINO;
            #982#3 = "G";
        |FINSI;
        |PINTA #982#3;
        aTipoPr = #982#3;
        |HAZ CalNueProrr;
        #982#19 = "";
        |SI #982#3 = "G";
            aAlfa1  = #982#4; |QBF aAlfa1;
        |SINO;
            aAlfa1  = #982#18; |QBF aAlfa1;
        |FINSI;
        aAlfa1  = ("   " + aAlfa1) % -103;
        #982#19 = aAlfa1;
        |PINTA #982#19;

        |GRABA 020#982a;
    |FINSI;

    |SI nInkey = 14; |O nInkey = 15;      || (F6/F7)
        |HAZ Imprimir;
    |FINSI;

|FCAL;


|CALCULO SalgoYa;  |TIPO 7;
     |SI nSwSal = 1;  |PTEC 807;  |FINSI;
|FCAL;

|| ************************************************************************

|PROCESO Leew82_Aux83;

  |SI #983#7 = 0; |Y #983#15 = 0;
      nNume2 = 100;
  |SINO;
      nNume1 = (( #983#6 / (#983#8 + #983#15 + #983#32) ) * 100) ! 2;
      nNume2 = nNume1 ! 0;
      |SI nNume2 < nNume1;
          nNume2 = nNume1 ! 0 + 1;
      |FINSI;
  |FINSI;
  |SI nNume2 > 100; nNume2 = 100; |FINSI;

  #983#5  = nNume2;

  |SI aTipoPr ! "E";
      #983#9 = #982#4;
  |SINO;
      #983#9 = #983#5;
  |FINSI;
  |GRABA 020#983a;
|FPRC;

|DEFBUCLE Leew82_Aux83;
 #983#1;
 ;
 #982#0,#982#1, 00;
 #982#0,#982#1, 99;
 ;
 NULL, Leew82_Aux83;
|FIN;

|PROCESO CalNueProrr;

    |SI #982#6 = 0; |Y #982#9 = 0;
         nNume2 = 100;
    |SINO;
         nNume1 = (( #982#5 / (#982#7 + #982#9 + #982#22) ) * 100) ! 2;
         nNume2 = nNume1 ! 0;
         |SI nNume2 < nNume1;
             nNume2 = nNume1 ! 0 + 1;
         |FINSI;
    |FINSI;
    |SI nNume2 > 100; nNume2 = 100; |FINSI;
    |SI nSwHay84 = 1; nNume2 = 100; |FINSI;

    #982#18 = nNume2;
    |SI aTipoPr = "G";
        #982#4 = #982#18;
    |SINO;
        #982#4 = #982#18;
    |FINSI;
    |BUCLE Leew82_Aux83;
|FPRC;

|| ********************** Imprimo Datos
|PROCESO LeoInfw83;
 |SI eaCa4 = ""; eaCa4 = #983#13; |FINSI;
 |PRINT;
|FPRC;

|DEFBUCLE LeoInfw83;
 #983#1;
 ;
 #0#0, enEmpresa, 00;
 #0#0, enEmpresa, 99;
 ;
 NULL, LeoInfw83;
|FIN;

|PROCESO LeoReflejo;

 enEmpresa = #182#1;
 nSwLinea  = 0;
 eaCam1    = #182#1; |QBF eaCam1;
 eaCam1    = ("00000" + eaCam1) % -105;
 eaCam2    = #182#2;
 eaCam3    = "ESPECIAL";
 enCam4    = "";
 |SI #182#3 = "G";
     eaCam3 = "GENERAL ";
 |FINSI;
 enCam4 = #182#19;
 enCam4 = enCam4 + "%";
 enCam4 = ("     " + enCam4) % -104;
 eaCam8    = "NO";
 |SI #182#8 = "S";
     eaCam8 = "SI";
 |FINSI;
 enCam10   = #182#5;
 enCam11   = #182#9;
 enCam13   = #182#6;
 enCam12   = #182#16;
 eaCam15   = #182#15;
 enCam22   = #182#22;

 |SI nInkey = 15;
     |BUCLE LeoInfw83;
 |SINO;
     |PRINT;
 |FINSI;
|FPRC;

|DEFBUCLE LeoReflejo;
 #182#1002;
 ;
 #0#0, nDEmp;
 #0#0, nHEmp;
 ;
 NULL, LeoReflejo;
|FIN;

|PROCESO Imprimir;
   eaTit  = "DEFINITIVA";
   enEj82 = #0#0;
   |SI aParam = "P";
       eaTit = "PROVISIONAL";
       enEj82 = #0#0 + 1;
   |FINSI;

   |DBASE aBase;  |QBT aBase;
   aDef = aBase + "def/dsmow082.mas";
   |CARGA_DEF aDef, reflejo@;
   |SI FSalida ! 0;
       aAlfa = "     Error en cargar el Def dsmow082";
       |MENAV aAlfa;
       |ACABA;
   |FINSI;
   |NOME_DAT #182 aFichw82;

   |IMPRE 0;
   |SI FSalida ! 0;
       |DESCARGA_DEF #reflejo@;
       |ACABA;
   |FINSI;

   informe = inform1;
   nDEmp   = 1;
   nHEmp   = 99999;
   |SI nInkey = 15;
       informe = inform2;
       nDEmp   = enEmpresa;
       nHEmp   = enEmpresa;
   |FINSI;

   |INFOR informe;
   |SI FSalida ! 0;
       |FINIMP;
       |DESCARGA_DEF #reflejo@;
       |ACABA;
   |FINSI;

   |BUCLE LeoReflejo;

   |PIEINF;
   |FININF;
   |FINIMP;
   |DESCARGA_DEF #reflejo@;
|FPRC;

|| ***********************************************************************
|PROCESO Baja81;
 #81#0 = enEmpresa;
 #81#1 = nAny;
 #81#2 = 00;
|FPRC;

|PROCESO Alta81;
 #81#0 = enEmpresa;
 #81#1 = nAny;
 #81#2 = 99;
|FPRC;

|PROCESO Baja82;
 #82#0 = enEmpresa;
 #82#1 = #0#0;
 #82#2 = 00;
|FPRC;

|PROCESO Alta82;
 #82#0 = enEmpresa;
 #82#1 = #0#0;
 #82#2 = 99;
|FPRC;

|PROCESO Baja83;
 #83#0 = enEmpresa;
 #83#1 = #0#0;
 #83#2 = #81#2;
 #83#3 = 00;
|FPRC;

|PROCESO Alta83;
 #83#0 = enEmpresa;
 #83#1 = #0#0;
 #83#2 = #81#2;
 #83#3 = 99;
|FPRC;

|PROCESO Verel83; |TIPO 7;
    |ENTLINEAL #1#83, -4, 7, 20, 0, Baja83, Alta83;
|FPRC;

|PROCESO Entrarel83; |TIPO 11;
  |SI FSalida = 10;
      |ENTLINEAL #1#83, -4, 4, 20, 0, Baja83, Alta83;
  |FINSI;
|FPRC;


|PROCESO ConsultaGE;

|SI #0#0 < enEjerPro;
    enTipoEntrada = 4;
    enEmpresa = #982#1;
    enEjer    = #982#0;
    enSw80    = 1;
    |SUB_EJECUTA_NP "dsmom027";
    enSw80    = 0;
    |ACABA;
|FINSI;

||  |SI #982#3 = "G";
      |ABRE #80;
      #80#0 = enEmpresa;
      #80#1 = #0#0;
      |LEE 001#80=;
      |SI FSalida ! 0;
          |DDEFECTO #80;
          #80#0 = enEmpresa;
          #80#1 = #0#0;
      |FINSI;
      #80#4  = #982#3;  || Definitivo
      #80#5  = #982#4;  || definitivo
      #80#6  = #982#5;
      #80#7  = #982#6;
      #80#9  = #982#9;
      #80#43 = #982#22;
      #80#8  = #80#6 + #80#7 + #80#9 + #80#43;

      |PINPA #0#80;
      |PINDA #0#80;
      |ENTLINEAL #1#82, -3, 4, 20, 0, Baja82, Alta82;
||  |FINSI;

/*
 |SI #982#3 = "E";
     |PINPA #0#81;
     |ENTLINEAL #1#81, -3, 4, 22, 0, Baja81, Alta81;
 |FINSI;
*/
|FPRC;

|PROCESO Boton2w082;
     |PTEC 824;
|FPRC;

|PROCESO Boton3w082;
     |PTEC 825;
|FPRC;

|PROCESO Boton4w082;
     |PTEC 826;
|FPRC;

|PROCESO Boton5w082;
     |PTEC 827;
|FPRC;

|PROCESO Boton6w082;
     |PTEC 828;
|FPRC;

|PROCESO Boton7w082;
     |PTEC 829;
|FPRC;

|PROCESO Boton8w082;
     |PTEC 830;
|FPRC;

|PROCESO Boton9w082;
     |PTEC 831;
|FPRC;
