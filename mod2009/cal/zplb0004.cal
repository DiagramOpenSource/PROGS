|FICHEROS;
     dsmom108 #108;
|FIN;

|VARIABLES;
        nLongitud    = 0;
        nCaracter    = 0;
        nCanal       = 0;

        aLongitud    = "";
        aIP          = "";

     {1}version      = "";
|FIN;

|INCLUYE i_varemi;

|PROCESO ExtraePath;
     eaPathAcrobat = version1;
     aLongitud     = eaPathAcrobat % 0;
     nLongitud     = aLongitud;
     |PARA nPos = 1; |SI nPos [ nLongitud; |HACIENDO nPos = nPos + 1;
          nCaracter = (nPos * 100) + 1;
          aCaracter = eaPathAcrobat % nCaracter;
          |SI aCaracter = ",";
               nPos          = nLongitud - nPos;
               nPos          = nPos + 100;
               nPos          = (-1) * nPos;
               eaPathAcrobat = eaPathAcrobat % nPos;
               |SAL;
          |FINSI;
     |SIGUE;

     eaPathAcrobat = eaPathAcrobat - "[";
     eaPathAcrobat = eaPathAcrobat - "]";
|FPRC;

|PROCESO CompruebaRuta;
     enError = 0;
     |QBF eaPathAcrobat;
     |XABRE "CE", eaPathAcrobat, nCanal;
     |SI eaPathAcrobat = ""; |O FSalida < 0;
          |XCIERRA nCanal;
          aAlfa = "    No se ha encontrado el directorio donde esta instalado ";
          aAlfa = aAlfa + "Adobe Acrobat Reader. ";
          |MENAV aAlfa;
          aAlfa = "    Debera instalarselo.";
          |MENAV aAlfa;

          enError = 1;
     |FINSI;
|FPRC;

|PROCESO LocalizaAcrobat;
     |IP_REMOTO aIP; |QBF aIP;

     |ABRE #108;
     #108#0 = aIP;
     |LEE 000#108=;
     |SI FSalida ! 0;  |DDEFECTO #108;  |FINSI;
     |CIERRA #108;

     eaPathAcrobat = #108#1;  |QBF eaPathAcrobat;

     enError  = 0;

     |SI eaPathAcrobat ! "";  |ACABA;  |FINSI;

     version1 = "AcroExch.Document";
     |ESPECIFICA "VERSION_PROGRAMA",version;
     |HAZ ExtraePath;
     |SI eaPathAcrobat = "NO LOCALIZADO";
          version1 = "AcroExch.App";
          |ESPECIFICA "VERSION_PROGRAMA",version;
          |HAZ ExtraePath;
     |FINSI;

     |SI eaPathAcrobat ! "NO LOCALIZADO";
          |HAZ CompruebaRuta;
          |SI enError = 1;          || todo ok
               |ACABA;              || hay Acrobat pero no lo encuentra
          |FINSI;
     |SINO;
          enError = 1;
     |FINSI;

     |SI enError = 0;
         |ABRE #108;
         #108#0 = aIP;
         |LEE 101#108=;
         |SI FSalida ! 0;
             |DDEFECTO #108;
             #108#0 = aIP;
             |GRABA 020#108b;
         |FINSI;
         #108#1 = eaPathAcrobat;
         |GRABA 020#108a;
         |LIBERA #108;
         |CIERRA #108;
    |FINSI;
|FPRC;
