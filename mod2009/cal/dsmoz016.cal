|FICHEROS;
     dsmoz016 #0;

     dsmom004 #4;
     dsmom008 #8;
     dsmow013 #999;

     :/basica/def/agifige1 #1000,MANTE;
|FIN;

|VARIABLES;
     aFichero   = "";
     aParametro = "";
     aAlfa      = "";

     nCampo    = 0;
     nDEmpresa = 0;
     nHEmpresa = 0;
     nMes      = 0;
     nAnyo     = 0;

     fFecha    = @;

     enGraba   = 0;
|FIN;

|INCLUYE i_variar;

|| /////////////////////// PROCESOS ENTRADA POR PANTALLA \\\\\\\\\\\\\\\\\\\\

|PROCESO Defecto; |TIPO 7;
    fFecha = ~;
    aAlfa  = fFecha % 402;   nMes  = aAlfa;
    aAlfa  = fFecha % -104;  nAnyo = aAlfa;
    |SI nMes [ 3;
        #0#1 = 4;
        #0#3 = nAnyo - 1;
        |HAZ Periodo;
        |PINTA #0#1;
        |PINTA #0#3;
        |ACABA;
    |FINSI;

    |SI nMes [ 6;
        #0#1 = 3;
        #0#3 = nAnyo;
        |HAZ Periodo;
        |PINTA #0#3;
        |PINTA #0#1;
        |ACABA;
    |FINSI;

    |SI nMes [ 9;
        #0#1 = 6;
        #0#3 = nAnyo;
        |HAZ Periodo;
        |PINTA #0#3;
        |PINTA #0#1;
        |ACABA;
    |FINSI;

    |SI nMes [ 12;
        #0#1 = 9;
        #0#3 = nAnyo;
        |HAZ Periodo;
        |PINTA #0#3;
        |PINTA #0#1;
        |ACABA;
    |FINSI;
|FPRC;

|PROCESO Periodo;
     #0#2 = "          ";
     |SI #0#1 = 1;   #0#2 = "ENERO     ";  |FINSI;
     |SI #0#1 = 2;   #0#2 = "FEBRERO   ";  |FINSI;
     |SI #0#1 = 3;   #0#2 = "MARZO     ";  |FINSI;
     |SI #0#1 = 4;   #0#2 = "ABRIL     ";  |FINSI;
     |SI #0#1 = 5;   #0#2 = "MAYO      ";  |FINSI;
     |SI #0#1 = 6;   #0#2 = "JUNIO     ";  |FINSI;
     |SI #0#1 = 7;   #0#2 = "JULIO     ";  |FINSI;
     |SI #0#1 = 8;   #0#2 = "AGOSTO    ";  |FINSI;
     |SI #0#1 = 9;   #0#2 = "SEPTIEMBRE";  |FINSI;
     |SI #0#1 = 10;  #0#2 = "OCTUBRE   ";  |FINSI;
     |SI #0#1 = 11;  #0#2 = "NOVIEMBRE ";  |FINSI;
     |SI #0#1 = 12;  #0#2 = "DICIEMBRE ";  |FINSI;
     |SI #0#1 = 99;  #0#2 = "ANUAL     ";  |FINSI;

     |SI #0#2 = "          ";
         |MENAV "     Periodo Incorrecto.";
         |ERROR;
     |FINSI;

     fFecha = ~;
     aAlfa  = fFecha % -104;
     #0#3   = aAlfa;
     |SI #0#1 = 99;
         #0#3 = #0#3 - 1;
     |FINSI;

     |PINTA #0#2;
     |PINTA #0#3;
|FPRC;

|PROCESO Parrilla;
     |SI #0#4 = 0;
         #0#4 = 0;  |PINTA #0#4;
         #0#5 = 0;  |PINTA #0#5;  |C_M #0#5, 1, "N";
         |PARA nCampo = 6;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#4, 1, "S";
             |C_M #0#5, 1, "S";
         |FINSI;

         |PARA nCampo = 6;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|| /////////////////////// PROCESOS DE TRABAJO \\\\\\\\\\\\\\\\\\\\

|PROCESO dsmom008;
     |SI #8#5 = 99;
         |ACABA;
     |SINO;
         |SI #8#7 ! "REGISTROS ";  |ACABA;  |FINSI;
     |FINSI;

     #999#0 = #8#0;
     #999#1 = #8#5;
     |LEE 101#999=;
     |SI FSalida ! 0;
         #1000#0 = #8#0;
         |LEE 040#1000=;
         |SI FSalida ! 0;  |DDEFECTO #1000;  |FINSI;

         #999#0  = #8#0;
         #999#1  = #8#5;
         #999#2  = #8#1;
         #999#3  = #8#2;
         #999#4  = #1000#1;
         #999#5  = #0#2;
         #999#6  = #8#6;
         #999#7  = #8#3;
         #999#8  = "N";
         |SI enEmpresa ! 0; #999#8  = "S"; |FINSI;
         #999#10 = #8#12;
         |GRABA 020#999n;

         enGraba = 1;
     |FINSI;
     |LIBERA #999;
|FPRC;

|DEFBUCLE dsmom008;
     #8#1;
     ;
     #4#0, #4#1, #4#2, #4#3, 00, 01;
     #4#0, #4#1, #4#2, #4#3, 00, 99;
     ;
     NULL, dsmom008;
|FIN;

|PROCESO GrabaTempo;
     |SI #4#3 ! 300; |Y #4#3 ! 320; |Y #4#3 ! 330;
         |Y #4#3 ! 332; |Y #4#3 ! 370; |Y #4#3 ! 371;
         |ACABA;
     |FINSI;

     |SI #4#9  = "X";  |ACABA;  |FINSI;
     |SI #4#11 = "X";  |ACABA;  |FINSI;
     |SI #4#13 = "X";  |ACABA;  |FINSI;

     |SI #4#5 = "ACTIVIDAD ";  |O #4#5 = "ACT.+ REG.";
         |BUCLE dsmom008;
     |SINO;
         #999#0 = #4#0;
         #999#1 = 0;
         |LEE 101#999=;
         |SI FSalida ! 0;
             #1000#0 = #4#0;
             |LEE 040#1000=;
             |SI FSalida ! 0;  |DDEFECTO #1000;  |FINSI;

             #999#0  = #4#0;
             #999#1  = 0;
             #999#2  = #4#1;
             #999#3  = #4#2;
             #999#4  = #1000#1;
             #999#5  = #0#2;
             #999#6  = #1000#1;
             #999#7  = #4#3;
             #999#8  = "N";
             |SI enEmpresa ! 0; #999#8  = "S"; |FINSI;
             #999#9  = #4#4;
             #999#10 = #4#3;
             |GRABA 020#999n;

             enGraba = 1;
         |FINSI;
         |LIBERA #999;
     |FINSI;
|FPRC;

|DEFBUCLE dsmom004;
     #4#1;
     ;
     nDEmpresa, #0#3, #0#1, #0#0,  00;
     nHEmpresa, #0#3, #0#1, #0#18, 99;
     ;
     NULL, GrabaTempo;
|FIN;

|PROCESO RealizaPase;
     |SI #999#10 = "300";  |HAZ Pase300_P;  |FINSI;
     |SI #999#10 = "320";  |HAZ Pase300_P;  |FINSI;
     |SI #999#10 = "330";  |HAZ Pase300_P;  |FINSI;
     |SI #999#10 = "332";  |HAZ Pase300_P;  |FINSI;
|FPRC;

|DEFBUCLE dsmow013;
     #999#1;
     8;
     00001, 00, "S";
     99999, 99, "S";
     ;
     NULL, RealizaPase;
|FIN;

|RUTINA ClaveBaja999;
     #999#0 = 1;
     #999#1 = 00;
|FRUT;

|RUTINA ClaveAlta999;
     #999#0 = 99999;
     #999#1 = 99;
|FRUT;

|| **********************************************************************
|PROCESO Tipo3;  |TIPO 3;

     #0#0    = 300; #0#18 = 371;
     enNPro1 = #0#19;

     |HAZ LeeCtrlProrr;
     |SI enSwProrr = 2;
         |CONFI "    NTIENE INFORMADO EN CONTROL NO AL CALCULO DE PRORRATA. Continuar con el Proceso ";
         |SI FSalida ! 0;
             |ACABA;
         |FINSI;
     |FINSI;

     aFichero = ("16z" + Usuario) % 108;
     |NOME_DAT #999 aFichero;
     |ABRE #999;  |CIERRA #999;  |DELINDEX #999;

     |ABRE #999;
     |ABRE #1000;
     |SI #0#4 ! 0;
         nDEmpresa = #0#4;
         nHEmpresa = #0#5;
         |BUCLE dsmom004;
     |SINO;
         |PARA nCampo = 6;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               |SI #0nCampo ! 0;
                   nDEmpresa = #0nCampo;
                   nHEmpresa = #0nCampo;
                   |BUCLE dsmom004;
               |FINSI;
         |SIGUE;
     |FINSI;
     |CIERRA #1000;
     |CIERRA #999;

     |SI enGraba = 1;
         |SI enEmpresa = 0;
             |HAZ PonBoton;
             |ENTLINEAL #1#999, -1, 4, 22, 1, ClaveBaja999, ClaveAlta999;
             |HAZ QuitaBoton;
             |CONFI "0000SRealizamos el Pase en estos momentos : ";
             |SI FSalida = 0;
                 |BUCLE dsmow013;
             |FINSI;
         |SINO;
             |BUCLE dsmow013;
         |FINSI;
     |SINO;
         |MENAV "     Seleccion Vacia.";
     |FINSI;

     |DELINDEX #999;
|FPRC;

|PROGRAMA;
     |SI enEmpresa ! 0;
         |DDEFECTO #0;
         #0#19 = -1;
         #0#1 = enPeriodo;
         #0#3 = enEjer;
         #0#4 = enEmpresa;
         #0#5 = enEmpresa;
         |HAZ Tipo3;
     |SINO;
         |MANTE #0;
     |FINSI;
|FPRO;
