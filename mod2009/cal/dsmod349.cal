|FICHEROS;
     dsmod349 #0;
     dsmoc349 #1;
|FIN;

|VARIABLES;
     nDPeriodo = 0;
     nHPeriodo = 0;
     nPeriodo  = 0;

     aEscape = "";
     aF2     = "";
     aF3     = "";
     aF4     = "";
     aF5     = "";
     aF6     = "";
     aF7     = "";
     aF8     = "";
     aF9     = "";
     aAlfa   = "";
     aTipo   = "";
|FIN;

|INCLUYE i_variar;

|RUTINA ClaveBaja1;
     #1#0  = enEmpresa;
     #1#1  = enEjer;
     #1#2  = nDPeriodo;
     #1#3  = 349;
     #1#4  = enComplem;
     #1#5  = 01;
     #1#6  = aTipo;
     #1#7  = "            ";
     #1#25 = 0000;
     #1#26 = 0;
|FRUT;

|RUTINA ClaveAlta1;
     #1#0  = enEmpresa;
     #1#1  = enEjer;
     #1#2  = nHPeriodo;
     #1#3  = 349;
     #1#4  = enComplem;
     #1#5  = 99;
     #1#6  = aTipo;
     #1#7  = "zzzzzzzzzzzz";
     #1#25 = 9999;
     #1#26 = 9;
|FRUT;

|RUTINA ClaveBaja11;
     #1#0 = enEmpresa;
     #1#1 = enEjer;
     #1#2 = nDPeriodo;
     #1#3 = 349;
     #1#4 = enComplem;
     #1#5 = 01;
     #1#6 = aTipo;
     #1#9 = "                                        ";
|FRUT;

|RUTINA ClaveAlta11;
     #1#0 = enEmpresa;
     #1#1 = enEjer;
     #1#2 = nHPeriodo;
     #1#3 = 349;
     #1#4 = enComplem;
     #1#5 = 99;
     #1#6 = aTipo;
     #1#9 = "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz";
|FRUT;

|PROCESO Lineal1;
     |PUSHV 0501 2380;
     |C_M #1#2, 1, "N";
     |C_M #1#7, 1, "N";
     |ENTLINEAL #1#1, -7, 4, 22, 1, ClaveBaja1, ClaveAlta1;
     |POPV;
|FPRC;

|PROCESO Lineal2;
     |PUSHV 0501 2380;
     |C_M #1#2, 1, "N";
     |C_M #1#9, 1, "N";
     |ENTLINEAL #2#1, -7, 4, 22, 1, ClaveBaja11, ClaveAlta11;
     |POPV;
|FPRC;

|PROGRAMA;
     eaNomDef = "dsmod349";

     |SI enPeriodo ] 1;  |Y enPeriodo [ 3;
         nPeriodo = 3;
     |FINSI;

     |SI enPeriodo ] 4;  |Y enPeriodo [ 6;
         nPeriodo = 6;
     |FINSI;

     |SI enPeriodo ] 7;  |Y enPeriodo [ 9;
         nPeriodo = 9;
     |FINSI;

     |SI enPeriodo ] 10;  |Y enPeriodo [ 12;
         nPeriodo = 12;
     |FINSI;

     |SI nPeriodo = 0;  nPeriodo = 99;  |FINSI;

|| \\\\\\ Consulta del Modelo ////////

     |SI enTipoEntrada = 4;              || Consulta del Modelo
         |CABEZA "Modelo 349";
         |ABRE #0;
         #0#0 = enEmpresa;
         #0#1 = enEjer;
         #0#2 = nPeriodo;
         #0#3 = enModelo;
         #0#4 = enComplem;
         #0#5 = enActividad;
         |LEE 040#0=;
         |SI FSalida ! 0;
             |MENAV "      No existe Registro.";
             |CIERRA #0;
             |ACABA;
         |FINSI;

         |SI #0#2 = 99;
             nDPeriodo = 99;
             nHPeriodo = 99;
         |SINO;
             |SI #0#2 = 3;   nDPeriodo = 1;   nHPeriodo = 3;   |FINSI;
             |SI #0#2 = 6;   nDPeriodo = 4;   nHPeriodo = 6;   |FINSI;
             |SI #0#2 = 9;   nDPeriodo = 7;   nHPeriodo = 9;   |FINSI;
             |SI #0#2 = 12;  nDPeriodo = 10;  nHPeriodo = 12;  |FINSI;
         |FINSI;

         |MENSA "     ESC Salir.";
         |PINPA #0#0;
         |PINDA #0#0;
         |HAZ PintaDatos;

         aEscape    = &255 + "806";
         aF2        = &255 + "824";
         aF3        = &255 + "825";
         aF4        = &255 + "826";
         aF5        = &255 + "827";
         aF6        = &255 + "828";
         aF7        = &255 + "829";
         aF8        = &255 + "830";
         aF9        = &255 + "831";

         |PARA;  |SI;  |HACIENDO;
                 |LEETECLA aAlfa;
                 |SI aAlfa = aEscape;  |SAL;  |FINSI;
                 |SI aAlfa = aF2;  aTipo = "E ";  |HAZ Lineal1;  |FINSI;
                 |SI aAlfa = aF3;  aTipo = "A ";  |HAZ Lineal1;  |FINSI;
                 |SI aAlfa = aF4;  aTipo = "ER";  |HAZ Lineal1;  |FINSI;
                 |SI aAlfa = aF5;  aTipo = "AR";  |HAZ Lineal1;  |FINSI;
                 |SI aAlfa = aF6;  aTipo = "E ";  |HAZ Lineal2;  |FINSI;
                 |SI aAlfa = aF7;  aTipo = "A ";  |HAZ Lineal2;  |FINSI;
                 |SI aAlfa = aF8;  aTipo = "ER";  |HAZ Lineal2;  |FINSI;
                 |SI aAlfa = aF9;  aTipo = "AR";  |HAZ Lineal2;  |FINSI;
         |SIGUE;
         |CIERRA #0;

         |ACABA;
     |FINSI;

|| \\\\\\ Comprobacion Existencia del Modelo

     |SI enTipoEntrada = 99;              || Existencia del Modelo ????
         |ABRE #0;
         #0#0 = enEmpresa;
         #0#1 = enEjer;
         #0#2 = nPeriodo;
         #0#3 = enModelo;
         #0#4 = enComplem;
         #0#5 = enActividad;
         |LEE 040#0=;
         |SI FSalida ! 0;  enExistencia = 1;  |DDEFECTO #0; |FINSI;
         |CIERRA #0;

         |SI enActividad = 0;
             enResultado = 0;
             |SI enDesdePase = 1;
                 enResultado = #0#7 + #0#9 + #0#11;
             |FINSI;
         |FINSI;
     |FINSI;
|FPRO;
