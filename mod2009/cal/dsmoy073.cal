|FICHEROS;
     dsmoy073 #0;

     dsmom002 #2;
     dsmom004 #4;

     dsmod309 #309;
     :/contasoc/def/isocie29 #202;

     :/integral/def/dsam0001 #1;
     :/integral/def/dsam0121 #121;
     :/integral/def/dsam0122 #122;
     :/integral/def/dsam0054 #54;

     :/basica/def/agitab99 #199;

     &clientes@ #715;
|FIN;

|VARIABLES;
     aPathDoc         = "";
     aAbre            = "";
     aAlfa            = "";
     aAlfa1           = "";
     aAlfa2           = "";
     aAlfa3           = "";
     aOrigen          = "";
     aDestino         = "";
     aDescri          = "";
     aIPRemoto        = "";
     aCadena          = "";
     aLen             = "";
     aAsigna          = "";
     aCaracter        = "";
     aIdenti          = "";
     aMenav           = "";
     aDocumento       = "";
     aPeriodo         = "";
     aPathSoc         = "";
     aEntero          = "";
     aDecima          = "";
     aLong            = "";
     aImpor1          = "";
     aImpor2          = "";
     aImpor3          = "";
     aImpor4          = "";
     aDImpreso        = "";
     aHImpreso        = "";

     nLong            = 0;
     nDecimal         = 0;
     nCaracter        = 0;
     nPeriodo         = 0;
     nEmpresa         = 0;
     nBanco           = 0;
     nHandle          = 0;
     nPara            = 0;
     nResultado       = 0;
     nWord            = 0;
     nLinea           = 0;
     nLen             = 0;
     nPos             = 0;
     nCampo           = 0;
     nMes             = 0;
     nAnyo            = 0;
     nPago            = 0;

     fFecha           = @;

     &empresa         = 0;
     &anyo            = 0;
     &enSwDeDonde     = 0;
     &enModelo        = 0;
     &enForzar        = 0;
     &enCodCli        = 0;

     &eaEntidad       = "";
     &eaOficina       = "";
     &eaDC            = "";
     &eaCuenta        = "";
     &eaRutaDocumento = "";
     &enFSalida       = 0;
     &enCodUbica      = 0;
     &eaNomUbicacion  = "";
     &eaAlfa          = "";
     &eaImporte       = "";
     &enBanco         = 0;
     &eaFormaPago     = "";
     &eaNomEnt        = "";
     &eaNomOfi        = "";
|FIN;

|PROCESO GuardaDocumento;
     |ABRE #54;
     #54#0  = nEmpresa;
     #54#1  = 9999;
     |LEE 040#54];
     |SI FSalida = 0;
         |LEE 040#54a;
     |SINO;
         |LEE 040#54u;
     |FINSI;
     |SI FSalida = 0;  |Y #54#0 = nEmpresa;
         nLinea = #54#1 + 1;
     |SINO;
         nLinea = 1;
     |FINSI;

     |DDEFECTO #54;
     |HORA aAlfa;
     aAlfa1 = aAlfa % 102;
     aAlfa2 = aAlfa % 402;

     #54#0  = nEmpresa;
     #54#1  = nLinea;
     #54#2  = aDescri;
     #54#3  = "DOC" + (("00000" + #54#0) % -105) + (("0000" + #54#1) % -104) + ".DOC";
     #54#4  = ~;
     #54#5  = aAlfa1;
     #54#6  = aAlfa2;
     #54#7  = ~;
     #54#8  = aAlfa1;
     #54#9  = aAlfa2;
     #54#10 = #0#37;
     #54#11 = "";
     #54#12 = "";
     |GRABA 020#54n;
     |LIBERA #54;
     |CIERRA #54;

     aOrigen  = aAbre;
     aDestino = eaRutaDocumento + "/" + "DOC" + (("00000" + nEmpresa) % -105) + (("0000" + nLinea) % -104) + ".DOC";
     |QBT aOrigen;
     |QBT aDestino;

     |RCOPIA_FICHERO aOrigen, aDestino;
     |SI FSalida ! 0;
         |MENAV "     No se ha podido copia el Documento al Cliente.";
     |FINSI;
|FPRC;

|PROCESO AbreWord;
     aAlfa = "C:/MODELOS";                   |RMKDIR aAlfa;
     aAlfa = "C:/MODELOS/DOCUMENTOS";        |RMKDIR aAlfa;

     aOrigen   = aPathDoc + aDocumento;
     aDestino  = "C:/MODELOS/DOCUMENTOS/" + aDocumento;
     aDescri   = #0#39;  |QBF aDescri;
     aDescri   = aDescri + " PERIODO " + #0#42 + " EJERCICIO " + #0#44;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |SI aIPRemoto ! "";
         |ENVIA_FICHERO aOrigen, aDestino;
     |SINO;
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;

     nWord  = 1;
     aAbre  = aDestino;

     |MENSA "      Abriendo el Documento, espere un Momento.";

     |WORD_ABRE nWord, aAbre;
     |WORD_PROPIEDADES nWord, "Visible", "NO";
|FPRC;

|PROCESO Asigna;
     |QBF aAsigna;
     aCadena = "";
     aLen    = aAsigna % 0;
     nLen    = aLen;
     |PARA nCampo = 1;  |SI nCampo [ nLen;  |HACIENDO nCampo = nCampo + 1;
           nPos = (100 * nCampo) + 1;
           aCaracter = aAsigna % nPos;
           |SI aCaracter = "¥";  aCaracter = &209; |FINSI;
           |SI aCaracter = "¤";  aCaracter = &241; |FINSI;
           |SI aCaracter = " ";  aCaracter = &225; |FINSI;
           |SI aCaracter = "‚";  aCaracter = &233; |FINSI;
           |SI aCaracter = "¡";  aCaracter = &237; |FINSI;
           |SI aCaracter = "¢";  aCaracter = &243; |FINSI;
           |SI aCaracter = "£";  aCaracter = &250; |FINSI;
           |SI aCaracter = "§";  aCaracter = &176; |FINSI;
           |SI aCaracter = "¦";  aCaracter = &170; |FINSI;
           aCadena = aCadena + aCaracter;
     |SIGUE;
     aAsigna = aCadena;

     |PARA;  |SI;  |HACIENDO;
          |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;
          |SI FSalida ! 0;  |SAL;  |FINSI;
     |SIGUE;
|FPRC;

|PROCESO SacaFecha;
     aAlfa1 = eaAlfa % 102;
     aAlfa2 = eaAlfa % 402;
     aAlfa3 = eaAlfa % 704;
     |SI aAlfa2 = "01";  aAlfa2 = "Enero";      |FINSI;
     |SI aAlfa2 = "02";  aAlfa2 = "Febrero";    |FINSI;
     |SI aAlfa2 = "03";  aAlfa2 = "Marzo";      |FINSI;
     |SI aAlfa2 = "04";  aAlfa2 = "Abril";      |FINSI;
     |SI aAlfa2 = "05";  aAlfa2 = "Mayo";       |FINSI;
     |SI aAlfa2 = "06";  aAlfa2 = "Junio";      |FINSI;
     |SI aAlfa2 = "07";  aAlfa2 = "Julio";      |FINSI;
     |SI aAlfa2 = "08";  aAlfa2 = "Agosto";     |FINSI;
     |SI aAlfa2 = "09";  aAlfa2 = "Septiembre"; |FINSI;
     |SI aAlfa2 = "10";  aAlfa2 = "Octubre";    |FINSI;
     |SI aAlfa2 = "11";  aAlfa2 = "Noviembre";  |FINSI;
     |SI aAlfa2 = "12";  aAlfa2 = "Diciembre";  |FINSI;

     eaAlfa = aAlfa1 + " de " + aAlfa2 + " de " + aAlfa3;
|FPRC;

|PROCESO Importe;    || Calculo para poner las puntos y comas
                     || en el importe

     aDecima   = "";
     aEntero   = "";
     aLong     = eaImporte % 0;
     nLong     = aLong;
     nDecimal  = 0;
     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPos      = (100 * nCaracter) + 1;
           aCaracter = eaImporte % nPos;
           |SI aCaracter = ".";
               nDecimal = 1;
           |SINO;
               |SI nDecimal = 0;
                   aEntero = aEntero + aCaracter;
               |SINO;
                   aDecima = aDecima + aCaracter;
               |FINSI;
           |FINSI;
     |SIGUE;
     aDecima = (aDecima + "00") % 102;
     eaImporte = aEntero;

     aLong = eaImporte % 0;
     nLong = aLong;
     eaImporte = "             " + eaImporte;
     eaImporte = eaImporte % -113;
     aImpor1   = eaImporte % -103;
     aImpor2   = eaImporte % -403;
     aImpor3   = eaImporte % -703;
     aImpor4   = eaImporte % -1004;
     |SI nLong ] 10;
         |SI aImpor4 ! "  -";
             eaImporte = aImpor4 + "." + aImpor3  + "." + aImpor2 + "." + aImpor1;
         |SINO;
             eaImporte = aImpor4 + aImpor3  + "." + aImpor2 + "." + aImpor1;
      |FINSI;
    |SINO;
        |SI nLong ] 7;
            |SI aImpor3 ! "  -";
                eaImporte = aImpor3  + "." + aImpor2 + "." + aImpor1;
            |SINO;
                eaImporte = aImpor3 + aImpor2 + "." + aImpor1;
            |FINSI;
        |SINO;
            |SI nLong ] 4;
                |SI aImpor2 ! "  -";
                    eaImporte = aImpor2 + "." + aImpor1;
                |SINO;
                    eaImporte = aImpor2 + aImpor1;
                |FINSI;
            |SINO;
                eaImporte = aImpor1;
            |FINSI;
        |FINSI;
     |FINSI;

     eaImporte = ("             " + eaImporte + "," + aDecima) % -113;

     |SI eaImporte = "            0";  eaImporte = "             ";  |FINSI;
     |SI eaImporte = "         0,00";  eaImporte = "             ";  |FINSI;
|FPRC;

|PROCESO CargaVariables;
     aAsigna = #715#1;  |HAZ QuitaRaros;
     aIdenti = "!#1#";
     |HAZ Asigna;

     |SI enSwDeDonde = 1;
         aAsigna = #715#3;
         aIdenti = "!#NIF#";
         |HAZ Asigna;

         aAsigna = #715#57;
         aIdenti = "!#ANAGRAMA#";
         |HAZ Asigna;

         aAsigna = "";
         |SI #715#39 ! "  ";  aAsigna = #715#39;  |FINSI;

         aAsigna = aAsigna + " " + #715#40;  |QBF aAsigna;

         |SI #715#41 ! "     ";  aAsigna = aAsigna + ", " + #715#41;  |QBF aAsigna;  |FINSI;
         |SI #715#42 ! "   ";    aAsigna = aAsigna + " "  + #715#42;  |QBF aAsigna;  |FINSI;
         |SI #715#43 ! "  ";     aAsigna = aAsigna + " "  + #715#43;  |QBF aAsigna;  |FINSI;
         |SI #715#44 ! "  ";     aAsigna = aAsigna + " "  + #715#44;  |QBF aAsigna;  |FINSI;

         aIdenti = "!#2#";
         |HAZ Asigna;

         aAsigna = "";
         aAsigna = (("00" + #715#45) % -102) + (("000" + #715#46) % -103);
         aAsigna = aAsigna + " " + #715#47;  |QBF aAsigna;

         aIdenti = "!#3#";
         |HAZ Asigna;

         aAsigna = "(" + #715#48;                  |QBF aAsigna;
         aAsigna = aAsigna + ")";
         |SI aAsigna = "()";  aAsigna = "";  |FINSI;
         aIdenti = "!#4#";
         |HAZ Asigna;
     |SINO;
         aAsigna = #715#13;
         aIdenti = "!#NIF#";
         |HAZ Asigna;

         aAsigna = "    ";
         aIdenti = "!#ANAGRAMA#";
         |HAZ Asigna;

         aAsigna = "";
         |SI #715#51 ! "  ";  aAsigna = #715#51;  |FINSI;

         aAsigna = aAsigna + " " + #715#52;  |QBF aAsigna;

         |SI #715#53 ! "     ";  aAsigna = aAsigna + ", " + #715#53;  |QBF aAsigna;  |FINSI;
         |SI #715#54 ! "   ";    aAsigna = aAsigna + " "  + #715#54;  |QBF aAsigna;  |FINSI;
         |SI #715#55 ! "  ";     aAsigna = aAsigna + " "  + #715#55;  |QBF aAsigna;  |FINSI;
         |SI #715#56 ! "  ";     aAsigna = aAsigna + " "  + #715#56;  |QBF aAsigna;  |FINSI;

         aIdenti = "!#2#";
         |HAZ Asigna;

         aAsigna = "";
         aAsigna = (("00" + #715#57) % -102) + (("000" + #715#58) % -103);
         aAsigna = aAsigna + " " + #715#59;  |QBF aAsigna;

         aIdenti = "!#3#";
         |HAZ Asigna;

         aAsigna = "(" + #715#60;                  |QBF aAsigna;
         aAsigna = aAsigna + ")";
         |SI aAsigna = "()";  aAsigna = "";  |FINSI;
         aIdenti = "!#4#";
         |HAZ Asigna;
     |FINSI;

     aAsigna = #1#4;  |QBF aAsigna;
     eaAlfa  = #0#34;  |HAZ SacaFecha;
     aAsigna = aAsigna + ", " + eaAlfa;

     aIdenti = "!#5#";
     |HAZ Asigna;

     aAsigna = #1#1;  |QBF aAsigna;
     aIdenti = "!#NOMASESORIA#";
     |HAZ Asigna;

     aAsigna = #1#2;  |QBF aAsigna;
     aIdenti = "!#DESASESORIA#";
     |HAZ Asigna;

     aAsigna = #0#44;
     aIdenti = "!#EJERCICIO#";
     |HAZ Asigna;

     aAsigna = aPeriodo;
     aIdenti = "!#PERIODO#";
     |HAZ Asigna;

     aAsigna = #0#35;
     aIdenti = "!#DIAFIN#";
     |HAZ Asigna;

     aAsigna = #0#51;
     aIdenti = "!#FECHATOPE#";
     |HAZ Asigna;

     eaImporte = nResultado;  |HAZ Importe;
     aAsigna   = eaImporte;
     aIdenti = "!#IMPORTE#";
     |HAZ Asigna;

     aAsigna   = "";
     |SI eaEntidad ! "";
         aAsigna   = "ENTIDAD..:   " + eaEntidad + " " + eaNomEnt;
     |FINSI;
     aIdenti = "!#ENTIDAD#";
     |HAZ Asigna;

     aAsigna   = "";
     |SI eaOficina ! "";
         aAsigna   = "SUCURSAL:   " + eaOficina + " " + eaNomOfi;
     |FINSI;
     aIdenti = "!#SUCURSAL#";
     |HAZ Asigna;

     aAsigna   = "";
     |SI eaCuenta ! "";
         aAsigna   = "CUENTA...:   " + eaCuenta;
     |FINSI;
     aIdenti = "!#CUENTA#";
     |HAZ Asigna;

     aAsigna = #0#40;
     aIdenti = "!#MODELO#";
     |HAZ Asigna;
|FPRC;

|PROCESO dsmom002;
     |SI #2#7 = "MENSUAL   ";
         aPeriodo = "M";
     |FINSI;

     |SI #2#7 = "TRIMESTRAL";
         aPeriodo = "T";
     |FINSI;
|FPRC;

|DEFBUCLE dsmom002;
     #2#1;
     ;
     nEmpresa, 01, #0#40;
     nEmpresa, 98, #0#40;
     ;
     NULL, dsmom002;
|FIN;

|PROCESO Imprime;
     |SI #0#40 ! 202;
         |BUCLE dsmom002;

         |SI aPeriodo = "T";
             |SI #0#42 = 3;    aPeriodo = "1§ Trimestre";  |FINSI;
             |SI #0#42 = 6;    aPeriodo = "2§ Trimestre";  |FINSI;
             |SI #0#42 = 9;    aPeriodo = "3§ Trimestre";  |FINSI;
             |SI #0#42 = 12;   aPeriodo = "4§ Trimestre";  |FINSI;
         |SINO;
             aPeriodo = "del mes " + (("00" + #0#42) % -102);
         |FINSI;
     |SINO;
         |SI #0#42 = 1;    aPeriodo = "1§ pago";  |FINSI;
         |SI #0#42 = 2;    aPeriodo = "2§ pago";  |FINSI;
         |SI #0#42 = 3;    aPeriodo = "3§ pago";  |FINSI;
     |FINSI;

     |HAZ AbreWord;

     |MENSA "      Asignando Campos, espere un Momento.";

     |HAZ CargaVariables;

     |MENSA "      Imprimiendo, espere un Momento.";

     |WORD_IMPRIME nWord, "";
     |WORD_PROPIEDADES nWord, "Visible", "NO";

     |SLEEP 10;

     |MENSA "      Cerrando Documento";

     |WORD_SALVA nWord, "";

     |SI #0#36 = "S";  |HAZ GuardaDocumento;  |FINSI;

     |RFBORRA aAbre;
|FPRC;

|PROCESO dsmom004;
     nBanco   = 1;
     enCodCli = #4#0;
     |HAZ LeePersona;

     enModelo = #4#3;  enForzar = 1;  |HAZ CogeDatosBancos;

     |SI enBanco    = 0;      nBanco = 0;  |FINSI;

     |SI #0#50 ! 0;
         nPago = eaFormaPago;
         |SI nPago ! #0#50;  nBanco = 0;  |FINSI;
     |FINSI;

     |SI #4#15  [ 0;  |ACABA;  |FINSI;
     |SI nBanco [ 0;  |ACABA;  |FINSI;

     nResultado = #4#15;
     aPeriodo   = #199#5;  |QBT aPeriodo;
     nEmpresa   = #4#0;

     |HAZ Imprime;
|FPRC;

|DEFBUCLE dsmom004;
     #4#1;
     9;
     #0#1, #0#44, #0#42, #0#40, 00, aDImpreso;
     #0#2, #0#44, #0#42, #0#40, 99, aHImpreso;
     ;
     NULL, dsmom004;
|FIN;

|PROCESO dsmod309;
     nBanco   = 1;
     enCodCli = #309#0;
     |HAZ LeePersona;

     enModelo = 309;  enForzar = 1;  |HAZ CogeDatosBancos;

     |SI enBanco    = 0;  nBanco = 0;  |FINSI;
     |SI #0#50 ! 0;
         nPago = eaFormaPago;
         |SI nPago ! #0#50;  nBanco = 0;  |FINSI;
     |FINSI;

     |SI #309#18 [ 0;  |ACABA;  |FINSI;
     |SI nBanco  [ 0;  |ACABA;  |FINSI;

     nResultado = #309#18;
     aPeriodo   = #199#5;  |QBT aPeriodo;
     nEmpresa   = #309#0;

     |HAZ Imprime;
|FPRC;

|DEFBUCLE dsmod309;
     #309#1;
     11;
     #0#1, 01, #0#44, #0#42, 00, 00, aDImpreso;
     #0#2, 98, #0#44, #0#42, 99, 99, aHImpreso;
     ;
     NULL, dsmod309;
|FIN;

|PROCESO isocie29;
     nResultado = 0;

     |SI #0#42 = 1;
         |SI PARAMETRO ! 1;
             fFecha = #202#9;
             |SI fFecha [ "01.01.1900";
                 |ACABA;
             |FINSI;
         |FINSI;

         nResultado = #202#6 + #202#24;
         |SI nResultado < 0;  nResultado = 0;  |FINSI;
     |FINSI;

     |SI #0#42 = 2;
         |SI PARAMETRO ! 1;
             fFecha = #202#10;
             |SI fFecha [ "01.01.1900";
                 |ACABA;
             |FINSI;
         |FINSI;

         nResultado = #202#7 + #202#25;
         |SI nResultado < 0;  nResultado = 0;  |FINSI;
     |FINSI;

     |SI #0#42 = 3;
         |SI PARAMETRO ! 1;
             fFecha = #202#11;
             |SI fFecha [ "01.01.1900";
                 |ACABA;
             |FINSI;
         |FINSI;

         nResultado = #202#8 + #202#26;
         |SI nResultado < 0;  nResultado = 0;  |FINSI;
     |FINSI;

     |SI nResultado [ 0;  |ACABA;  |FINSI;

     nBanco   = 1;
     enCodCli = #202#0;
     |HAZ LeePersona;

     enModelo = 202;  enForzar = 1;  |HAZ CogeDatosBancos;

     |SI enBanco    = 0;      nBanco = 0;  |FINSI;
     |SI #0#50 ! 0;
         nPago = eaFormaPago;
         |SI nPago ! #0#50;  nBanco = 0;  |FINSI;
     |FINSI;

     |SI nBanco  [ 0;  |ACABA;  |FINSI;

     aPeriodo   = #199#5;  |QBT aPeriodo;
     nEmpresa   = #202#0;

     |HAZ Imprime;
|FPRC;

|DEFBUCLE isocie29;
     #202#1;
     ;
     #0#1, #0#44;
     #0#2, #0#44;
     ;
     NULL, isocie29;
|FIN;

|PROCESO Tipo3;  |TIPO 3;
     |ABRE #1;
     |LEE 000#1p;
     |SI FSalida ! 0;  |DDEFECTO #1;  |FINSI;
     |CIERRA #1;

     |SI #0#40 = 202;
         |DBASS aPathSoc;  |QBF aPathSoc;
         aPathSoc = aPathSoc + "contasoc/fich/";
         |PATH_DAT #202 aPathSoc;
     |FINSI;

     |SI #0#0 = "S";
         |SI #0#40 ! 309;  |Y #0#40 ! 202;  |BUCLE dsmom004;  |FINSI;
         |SI #0#40 = 309;                   |BUCLE dsmod309;  |FINSI;
         |SI #0#40 = 202;                   |BUCLE isocie29;  |FINSI;
     |SINO;
         |PARA nPara = 3; |SI nPara [ 32; |HACIENDO nPara = nPara + 1;
               |SI #0nPara ! 0;
                  #0#1 = #0nPara;
                  #0#2 = #0nPara;

                  |SI #0#40 ! 309;  |Y #0#40 ! 202;  |BUCLE dsmom004;  |FINSI;
                  |SI #0#40 = 309;                   |BUCLE dsmod309;  |FINSI;
                  |SI #0#40 = 202;                   |BUCLE isocie29;  |FINSI;
               |FINSI;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO Periodo;
     |SI #0#40 ! 202;
         |SI #0#42 = 1;   #0#43 = "ENERO     ";  |FINSI;
         |SI #0#42 = 2;   #0#43 = "FEBRERO   ";  |FINSI;
         |SI #0#42 = 3;   #0#43 = "MARZO     ";  |FINSI;
         |SI #0#42 = 4;   #0#43 = "ABRIL     ";  |FINSI;
         |SI #0#42 = 5;   #0#43 = "MAYO      ";  |FINSI;
         |SI #0#42 = 6;   #0#43 = "JUNIO     ";  |FINSI;
         |SI #0#42 = 7;   #0#43 = "JULIO     ";  |FINSI;
         |SI #0#42 = 8;   #0#43 = "AGOSTO    ";  |FINSI;
         |SI #0#42 = 9;   #0#43 = "SEPTIEMBRE";  |FINSI;
         |SI #0#42 = 10;  #0#43 = "OCTUBRE   ";  |FINSI;
         |SI #0#42 = 11;  #0#43 = "NOVIEMBRE ";  |FINSI;
         |SI #0#42 = 12;  #0#43 = "DICIEMBRE ";  |FINSI;
         |SI #0#42 = 99;  #0#43 = "ANUAL     ";  |FINSI;
     |SINO;
         |SI #0#42 > 3;
             |MENAV "0000 Periodo erroneo";
             |ERROR;
             |ACABA;
         |FINSI;

         |SI #0#42 = 1;   #0#43 = "1 - PAGO  ";  |FINSI;
         |SI #0#42 = 2;   #0#43 = "2 - PAGO  ";  |FINSI;
         |SI #0#42 = 3;   #0#43 = "3 - PAGO  ";  |FINSI;
     |FINSI;

     |PINTA #0#42;
     |PINTA #0#43;
|FPRC;

|PROCESO Modelo;
     |ABRE #199;
     #199#0 = #0#40;
     |LEE 040#199=;
     |SI FSalida ! 0;  |DDEFECTO #199;  |FINSI;
     |CIERRA #199;

     |SI #0#40 = 180;  |O #0#40 = 190;  |O #0#40 = 390;  |O #0#40 = 392;
      |O #0#40 = 184;  |O #0#40 = 193;  |O #0#40 = 296;
         |MENAV "     Modelo no valido.";
         |ERROR;
     |FINSI;

     |SI #199#4 = "N";  |Y #199#0 ! 202;
         |MENAV "     Modelo sin Activar.";
         |ERROR;
     |FINSI;

     #0#42 = 0;

     fFecha = ~;
     aAlfa  = fFecha % -104;
     #0#44  = aAlfa;

     |SI #199#0 = 202;
         #0#42 = 1;
         |HAZ Periodo;
         |ACABA;
     |FINSI;

     fFecha = ~;
     aAlfa  = fFecha % 402;   nMes  = aAlfa;
     aAlfa  = fFecha % -104;  nAnyo = aAlfa;

     |SI #199#5 = "T";  |O #199#5 = "C";
         |SI nMes [ 3;
             #0#42  = 12;
             #0#44  = nAnyo - 1;
             |HAZ Periodo;
             |PINTA #0#42;
             |PINTA #0#44;
             |ACABA;
         |FINSI;

         |SI nMes [ 6;
             #0#42 = 3;
             #0#44 = nAnyo;
             |HAZ Periodo;
             |PINTA #0#42;
             |PINTA #0#44;
             |ACABA;
         |FINSI;

         |SI nMes [ 9;
             #0#42 = 6;
             #0#44 = nAnyo;
             |HAZ Periodo;
             |PINTA #0#42;
             |PINTA #0#44
             |ACABA;
         |FINSI;

         |SI nMes [ 12;
             #0#42 = 9;
             #0#44 = nAnyo;
             |HAZ Periodo;
             |PINTA #0#42;
             |PINTA #0#44
             |ACABA;
         |FINSI;
     |FINSI;

     |SI #199#5 = "M";
         |SI nMes = 1;
             #0#42 = 12;
             #0#44 = nAnyo - 1;
             |HAZ Periodo;
         |SINO;
             #0#42 = nMes - 1;
             #0#44 = nAnyo;
             |HAZ Periodo;
         |FINSI;
         |PINTA #0#42;
         |PINTA #0#44;
     |FINSI;
|FPRC;

|PROCESO Seleccion; |TIPO 0;
     |SI #0#0 = "S";
         |C_M #0#01, 1, "S";
         |C_M #0#02, 1, "S";

         |PARA nPara = 3; |SI nPara [ 32; |HACIENDO nPara = nPara + 1;
               #0nPara = 0; |PINTA #0nPara;
               |C_M #0nPara, 1, "N";
         |SIGUE;
     |SINO;
         #0#1 = 00001; |PINTA #0#1;    |C_M #0#01, 1, "N";
         #0#2 = 99999; |PINTA #0#2;    |C_M #0#02, 1, "N";

         |PARA nPara = 3; |SI nPara [ 32; |HACIENDO nPara = nPara + 1;
               |C_M #0nPara, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO Gestion;
     |C_M #0#37, 1, "S";
     |SI #0#36 = "N";
         |C_M #0#37, 1, "N";  #0#37 = 0;
                              #0#38 = "";
     |FINSI;

     |PINTA #0#37;
     |PINTA #0#38;
|FPRC;

|PROCESO LeeUbicaciones;
     |SI FSalida = 10;
         |ABRE #121;
         #121#0 = enCodUbica;
         |LEE 000#121=;
         |SI FSalida ! 0;  |DDEFECTO #121;  |FINSI;

         |CONSULTA_CLAVE #1#121;
         |SI FSalida = 0;
             enCodUbica = #121#0;
         |FINSI;
         |CIERRA #121;

         eaNomUbicacion = #121#1;
         |ERROR;
         |ACABA;
     |FINSI;

     enFSalida = 0;
     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;   |QBT aIPRemoto;
     |SI aIPRemoto = "";  aIPRemoto = "LOCAL";  |FINSI;

     aIPRemoto = (aIPRemoto + (" " * 20)) % 120;

     |ABRE #121;
     #121#0 = enCodUbica;
     |LEE 040#121=;
     enFSalida = FSalida;
     |SI FSalida ! 0;
         |MENAV "      No existe Ubicacion ";
         |CIERRA #121;
         |ACABA;
     |FINSI;
     |CIERRA #121;

     |ABRE #122;
     #122#0 = enCodUbica;
     #122#1 = aIPRemoto;
     |LEE 040#122=;
     enFSalida = FSalida;
     |SI FSalida ! 0;
         aMenav =  "      No existe Ubicacion en la Maquina : " + aIPRemoto;
         |MENAV aMenav;
         |CIERRA #122;
         |ACABA;
     |FINSI;
     |CIERRA #122;

     eaRutaDocumento = #122#3;  |QBF eaRutaDocumento;
     |SI eaRutaDocumento = "";
         aMenav = "      No esta configurada la Ruta en la Maquina " + aIPRemoto;
         |MENAV aMenav;
         enFSalida = 1;
         |ACABA;
     |FINSI;
     eaNomUbicacion = #121#1;
|FPRC;

|PROCESO BuscaUbicacion;
     |SI #0#36 = "N";  |ACABA;  |FINSI;

     enCodUbica = #0#37;
     |HAZ LeeUbicaciones;
     |SI enFSalida ! 0;  |ERROR;  |ACABA;  |FINSI;

     #0#37 = enCodUbica;      |PINTA #0#37;
     #0#38 = eaNomUbicacion;  |PINTA #0#38;
|FPRC;

|PROGRAMA;
     |CLS;

     |DDEFECTO #0;

     |SI PARAMETRO = 1;
         |CABEZA "Carta preaviso de domiciliaciones y adeudos con NCR";
         #0#39 = "Carta preaviso de domiciliaciones y adeudos con NCR";

         #0#45 = "Se emitir  una carta de todos aquellos  ";
         #0#46 = "clientes seleccionados que en la        ";
         #0#47 = "configuraci¢n de bancos tengan puesto en";
         #0#48 = "la forma de pago el pago que se pide por";
         #0#49 = "pantalla.                               ";

         |C_M #0#50, 1, "S";
         |C_M #0#51, 1, "S";

         aDocumento = "word0075.doc";
         aDImpreso  = " ";
         aHImpreso  = "z";
     |FINSI;

     |SI PARAMETRO = 2;
         |CABEZA "Carta aviso de domiciliaciones sin adeudos sin NCR";
         #0#39 = "Carta aviso de domiciliaciones sin adeudos sin NCR";

         #0#45 = "Se emitir  una carta de todos aquellos  ";
         #0#46 = "clientes seleccionados que en la        ";
         #0#47 = "configuraci¢n de bancos tengan puesto en";
         #0#48 = "la forma de pago del modelo un 3.       ";
         #0#49 = "El modelo deber  estar impreso.         ";

         #0#50 = 3;             |C_M #0#50, 1, "N";
         #0#51 = "01.01.0000";  |C_M #0#51, 1, "N";

         aDocumento = "word0076.doc";
         aDImpreso  = "X";
         aHImpreso  = "X";
     |FINSI;

     |SI PARAMETRO = 3;
         |CABEZA "Carta de solicitud a bancos del pago de adeudos con NCR";
         #0#39 = "Carta de solicitud a bancos del pago de adeudos con NCR";

         #0#45 = "Se emitir  una carta de todos aquellos  ";
         #0#46 = "clientes seleccionados que en la        ";
         #0#47 = "configuraci¢n de bancos tengan puesto en";
         #0#48 = "la forma de pago del modelo un 2.       ";
         #0#49 = "El modelo deber  estar impreso.         ";

         #0#50 = 2;             |C_M #0#50, 1, "N";
         #0#51 = "01.01.0000";  |C_M #0#51, 1, "N";

         aDocumento = "word0077.doc";
         aDImpreso  = "X";
         aHImpreso  = "X";
     |FINSI;

     |SUB_EJECUTA_NP ":basica/wordm000;CREALOS";

     |DBASS aPathDoc; |QBF aPathDoc;
     aPathDoc = aPathDoc + "basica/word/";

     aAbre = aPathDoc + "word0065.doc";
     |XABRE "E", aAbre, nHandle;
     |SI FSalida < 0;
         |MENAV "     No tiene instalado la plantilla de la Carta, instale ipbasica";
         |ACABA;
     |FINSI;

     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
|FPRO;
