|FICHEROS;
     dsmoy014 #0;

     dsmom001 #1;
     :/integral/def/dsam0000 #2;   ||Clientes
     dsmom004 #4;

     dsmoc130 #230;
     dsmoc131 #231;
     dsmod130 #430;
     dsmod131 #431;

    :/basica/def/agicen14 #114;
    :/basica/def/agifigen #118;
    :/basica/def/agim0019 #119;

     dsmow032 #900;          ||Def de impresion
     dsmow037 #901;          ||Comuneros auxiliar

     dsmow014 #999;
|FIN;

|VARIABLES;
     aFichero   = "";
     nCampo     = 0;
     nDEmpresa  = 0;
     nHEmpresa  = 0;
     nDActividad = 0;
     nHActividad = 0;
     nLaEmpresa  = 0;
     nGTempo     = 0;
     nEstado     = 0;

     nSPin1      = 0;

     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     nPara1 = 0;
     nPara2 = 0;
     nNume1 = 0;
     nNume2 = 0;
     nNume3 = 0;
     nNume4 = 0;

     SwHayDatos = 0;
     SwHayComun = 0;

     fFecha  = @;
     efDFecha  = @;
     efHFecha  = @;

     &enSwNo   = 0;
     &SwLinea  = 0;
     &aTp0     = "%";
     &aTp1     = "Pagos a Cta.";
     &aTp2     = "Retenciones ";
     &nTp3     = 0;
     &enFrase  = 0;

     nImport1  = 0;
     nImport2  = 0;
     nImp1    = 0;
     nImp2    = 0;

     nPin1    = 0;
     aPin1    = "";
     efPn10   = @;
     efPn11   = @;
     swExiste = 0;
     swNoExis = 0;
     nSwCom   = 0;

     nNumEmp  = 0;
     nDActEmp  = 0;
     nHActEmp  = 0;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO losDefectos; |TIPO 7;
 fFecha = ~; aAlfa1 = fFecha;
 aAlfa1 = aAlfa1 % -104;
 #0#1 = aAlfa1; |PINTA #0#1;
|FPRC;

|PROCESO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#2  = enEmpresa;  |PINTA #0#2;
         #0#3  = enEmpresa;  |PINTA #0#3;

         |C_M #0#2, 1, "N";
         |C_M #0#3, 1, "N";
     |FINSI;
|FPRC;

|PROCESO Parrilla; |TIPO 0;
     |SI #0#2 = 0;
         #0#2 = 0;  |PINTA #0#2;
         #0#3 = 0;  |PINTA #0#3;  |C_M #0#3, 1, "N";
         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#2, 1, "S";
             |C_M #0#3, 1, "S";
         |FINSI;

         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas; |TIPO 0;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO YaSeCual; |TIPO 7;
 |SI enEmpresa ! 0;
     #0#46 = "S"; |PINTA #0#46;
     #0#24 = "S"; |PINTA #0#24;
     #0#25 = 00;  |PINTA #0#25;
     #0#26 = 99;  |PINTA #0#26;
     |C_M #0#24, 1, "N";
     |C_M #0#25, 1, "N";
     |C_M #0#26, 1, "N";
     |C_M #0#46, 1, "N";
 |FINSI;
|FPRC;

|PROCESO SelTipo0; |TIPO 0;
 |SI #0#46 = "N";
     #0#25 = 0;  |PINTA #0#25;  |C_M #0#25, 1, "N";
     #0#26 = 99;  |PINTA #0#26;  |C_M #0#26, 1, "N";
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |SINO;
     |SI enEmpresa = 0;
         |C_M #0#25, 1, "S";
         |C_M #0#26, 1, "S";
     |FINSI;
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "N";
           #0nCampo = 0;  |PINTA #0nCampo;
     |SIGUE;
 |FINSI;
|FPRC;

|PROCESO SelTipo1;
 |SI #0Campo = 0;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           #0nCampo = 0;  |C_M #0nCampo, 1, "N";
     |SIGUE;
 |SINO;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |FINSI;
|FPRC;

|| **********************************************************************************
|| **********************************************************************************
|PROCESO ImpComuneros;
   |PRINT;
   SwLinea = 2;
|FPRC;

|DEFBUCLE ImpComuneros;
 #901#1;
 ;
 00001;
 99999;
 ;
 NULL, ImpComuneros;
|FIN;
|| **********************************************************************************

|PROCESO LaSuma2;
||  |SI #900#25 < 0; #900#25 = 0; |FINSI;
||  |SI #900#26 < 0; #900#26 = 0; |FINSI;
||  |SI #900#27 < 0; #900#27 = 0; |FINSI;
||  |SI #900#28 < 0; #900#28 = 0; |FINSI;

  #900#29 = #900#25 + #900#26 + #900#27 + #900#28;
|FPRC;

|PROCESO LaSuma;
 |SI enPeriodo = 3;   aAlfa1 = "01.01." + enEjer;  |FINSI;
 |SI enPeriodo = 6;   aAlfa1 = "01.04." + enEjer;  |FINSI;
 |SI enPeriodo = 9;   aAlfa1 = "01.07." + enEjer;  |FINSI;
 |SI enPeriodo = 12;  aAlfa1 = "01.10." + enEjer;  |FINSI;
 fFecha = aAlfa1;

 |SI enPeriodo [ 3;                   nNume1 = 25; nNume2 = 30; |FINSI;
 |SI enPeriodo > 3; |Y enPeriodo [ 6; nNume1 = 26; nNume2 = 31; |FINSI;
 |SI enPeriodo > 6; |Y enPeriodo [ 9; nNume1 = 27; nNume2 = 32; |FINSI;
 |SI enPeriodo > 9;                   nNume1 = 28; nNume2 = 33; |FINSI;

 #900nNume1 = #900nNume1 + nImp1;
 #900nNume2 = #900nNume2 + nImp2;
 #900#34    = #900nNume2;

 SwHayDatos = 1;
|FPRC;

|| ///////////////////////////////////////////////////////////////////////

|PROCESO LSComunero;
     |SI #0#1 = 2008;
         enFrase = 1;
     |FINSI;

     nEstado = 0;
     #901#0 = #1#3;
     |LEE 001#901=;
     |SI FSalida ! 0;
         nEstado = 1;
         |DDEFECTO #901;
         #901#0 = #1#3;

         |ABRE #2;
         #2#0 = #1#3;
         |LEE 040#2=;
         |SI FSalida ! 0;  |DDEFECTO #2;  |FINSI;
         |CIERRA #2;

         #901#1 = #1#5;   || Nombre
         #901#2 = #2#3;   || NIF

         |ABRE #114;
         #114#0 = #2#0; || Empresa
         #114#1 = #1#4;
         |LEE 041#114=;
         |SI FSalida = 0;
             #901#14 = #114#18;  ||Codigo en Renta
         |FINSI;
         |CIERRA #114;
     |FINSI;

     #901#3 = #1#7;   || % Participacion

     nImport1 = nImp1;
     nImport2 = nImp2;

     |SI enPeriodo [ 3;                   nNume1 = 4; nNume2 = 9;  |FINSI;
     |SI enPeriodo > 3; |Y enPeriodo [ 6; nNume1 = 5; nNume2 = 10; |FINSI;
     |SI enPeriodo > 6; |Y enPeriodo [ 9; nNume1 = 6; nNume2 = 11; |FINSI;
     |SI enPeriodo > 9;                   nNume1 = 7; nNume2 = 12; |FINSI;

     #901nNume1 = #901nNume1 + nImport1;
     #901nNume2 = #901nNume2 + nImport2;
     #901#8     = #901#8  + nImport1;
     #901#13    = nImport2;

     |SI nEstado = 0; |GRABA 020#901a; |FINSI;
     |SI nEstado ! 0; |GRABA 020#901n; |FINSI;
     SwHayComun = 1;
|FPRC;

|| ****************************************************
|PROCESO modelo131_0;
     enPeriodo = #231#2;
     nImp1     = #231#11 + #231#14;    ||  #231#33;
     nImp2     = #231#12 + #231#15;    ||  #231#27 + #231#31;

     |HAZ LaSuma;
     |SI nSwCom = 1;
         |HAZ LSComunero;
     |FINSI;
|FPRC;

|DEFBUCLE modelo131;
     #231#1;
     ;
     nNumEmp, #999#1, 00, 000, 00, nDActEmp;
     nNumEmp, #999#1, 99, 999, 99, nHActEmp;
     ;
     NULL, modelo131_0;
|FIN;

|PROCESO modelo130_0;
     enPeriodo = #230#2;
     nImp1     = #230#33;   || #230#17; del d130
     nImp2     = #230#31;   || #230#11 + #230#15;

     |HAZ LaSuma;
     |SI nSwCom = 1;
         |HAZ LSComunero;
     |FINSI;
|FPRC;

|DEFBUCLE modelo130;
     #230#1;
     35;
     nNumEmp, #999#1, 00, 000, 00, 00, nDActEmp;
     nNumEmp, #999#1, 99, 999, 99, 99, nHActEmp;
     ;
     NULL, modelo130_0;
|FIN;

|| -------------------------------------------------------------------------
|| ///////////////////////////////////////////////////////////
|| ... los def de impresion  d130 y d131

|PROCESO modelo431_0;
     enPeriodo = #431#2;
     nImp1     = #431#33;
     nImp2     = #431#27 + #431#31;

     |HAZ LaSuma;
|FPRC;

|DEFBUCLE modelo431;
     #431#1;
     ;
     nNumEmp, #999#1, 00, 000, 00, 00;
     nNumEmp, #999#1, 99, 999, 99, 99;
     ;
     NULL, modelo431_0;
|FIN;

|PROCESO modelo430_0;
     enPeriodo = #430#2;

     |SI #430#1 [ 2007;
         nImp1     = #430#17;
     |SINO;
         |SI #430#1 = 2008;  |Y #430#2 = 3;
             nImp1 = #430#17;
         |SINO;
             nImp1 = #430#23;
         |FINSI;
     |FINSI;

     nImp2     = #430#11 + #430#15;

     |HAZ LaSuma;
|FPRC;

|DEFBUCLE modelo430;
     #430#1;
     ;
     nNumEmp, #999#1, 00, 000, 00, 00;
     nNumEmp, #999#1, 99, 999, 99, 99;
     ;
     NULL, modelo430_0;
|FIN;
|| ***********************************************

|PROCESO RelComuneros0;
     |SI #1#8 ! "01.01.0000";
         |SI #1#8 > efHFecha; |ACABA;  |FINSI;
     |FINSI;
     |SI #1#9 ! "01.01.0000";
         |SI #1#9 < efDFecha;  |ACABA;  |FINSI;
     |FINSI;

     nSwCom   = 1;
     nNumEmp  = #1#3;
     nDActEmp = #1#4;
     nHActEmp = #1#4;
     |SI enModelo = 130;
         |BUCLE modelo130;
     |SINO;
         |BUCLE modelo131;
     |FINSI;
|FPRC;

|DEFBUCLE RelComumeros;
     #1#1;
     ;
     #999#0, 01, 0001;
     #999#0, 99, 9999;
      e;
     NULL, RelComuneros0;
|FIN;
|| ///////////////////////////////////////////////////////////


|PROCESO Busco131;
     enModelo = 131;
     |DELINDEX #901;
     SwHayDatos = 0;
     SwHayComun = 0;
     |PDEFECTO #900, 25, 50;

     |SI aCoBien = "N";
         nNumEmp = #999#0;
         nSwCom  = 0;
         nDActEmp = 1;
         nHActEmp = 99;
         |BUCLE modelo431;
     |SINO;
         |ABRE #901;
         nSwCom  = 1;
         |BUCLE RelComumeros;
         |CIERRA #901;
     |FINSI;

     |SI SwHayDatos = 1;
         |HAZ LaSuma2;
         |SI #900#25 ! 0; |O #900#26 ! 0; |O #900#27 ! 0; |O #900#28 ! 0;
             |O #900#30 ! 0; |O #900#31 ! 0; |O #900#32 ! 0; |O #900#33 ! 0;
                SwLinea = 0;
                nTp3    = 131;
                aAlfa1  = #900#50; |QBF aAlfa1;
                aAlfa1 = aAlfa1 + " " + nTp3 + " -";
                #900#50 = aAlfa1;
                |PRINT;
                SwLinea = 1;
                |SI SwHayComun ! 0;  |BUCLE ImpComuneros;  |FINSI;
                |PIEINF;
         |FINSI;
     |FINSI;
|FPRC;

|| -------------------------------------------------------------------------

|PROCESO Busco130;
     enModelo = 130;
     |DELINDEX #901;
     SwHayDatos = 0;
     SwHayComun = 0;
     enFrase    = 0;
     |PDEFECTO #900, 25, 50;

     |SI aCoBien = "N";
         nNumEmp = #999#0;
         nSwCom  = 0;
         nDActEmp = 1;
         nHActEmp = 99;
         |BUCLE modelo430;
     |SINO;
         |ABRE #901;
         nSwCom  = 1;
         |BUCLE RelComumeros;
         |CIERRA #901;
     |FINSI;

     |SI SwHayDatos = 1;
         |HAZ LaSuma2;
         |SI #900#25 ! 0; |O #900#26 ! 0; |O #900#27 ! 0; |O #900#28 ! 0;
             |O #900#30 ! 0; |O #900#31 ! 0; |O #900#32 ! 0; |O #900#33 ! 0;
                SwLinea = 0;
                nTp3    = 130;
                aAlfa1  = #900#50; |QBF aAlfa1;
                aAlfa1 = aAlfa1 + " " + nTp3 + " -";
                #900#50 = aAlfa1;
                |PRINT;
                SwLinea = 1;
                |SI SwHayComun ! 0;  |BUCLE ImpComuneros;  |FINSI;
                |PIEINF;
         |FINSI;
     |FINSI;

|FPRC;

|PROCESO LeeDatosEmpresa;
     |DDEFECTO #900;

     #900#0 = enEmpresa;
     nPin1 = 0;
     aPin1 = "";
     efPn10 = "01.01.0000";
     efPn11 = "01.01.0000";

     || Lee Primera Actividad

     swNoExis = 0;
     |ABRE #agicen14;
     #agicen14#0 = #900#0; || Empresa
     #agicen14#1 = 00;
     |LEE 041#agicen14.];
     |SI FSalida = 0; |Y #agicen14#0 = #900#0;
         #900#5 = #agicen14#9;
         #900#6 = #agicen14#10;
         #900#7 = #agicen14#7;
         #900#8 = #agicen14#8;
         efPn10 = #agicen14#5;
         efPn11 = #agicen14#6;
         enActividad = #agicen14#1;

         #900#15 = #agicen14#1;   ||Act.
         #900#16 = #agicen14#2;   ||Tipo Epig.
         #900#17 = #agicen14#3;   ||Epigrafe
         #900#18 = #agicen14#4;   ||Descripcion Actividad
         #900#19 = #agicen14#5;   ||Fecha Inicio
         #900#20 = #agicen14#6;   ||Fecha Fin
         #900#21 = #agicen14#11;  ||Sector actividad
         #900#22 = #agicen14#12;  ||Sector actividad
         #900#23 = #agicen14#16;  ||Tipo Actividad
         #900#24 = #agicen14#17;  ||Descripcion Tipo Actividad
         aCoBien = #agicen14#13;  ||Comunidad de Bienes
         #900#51 = #agicen14#20;  ||CNAE
         #900#52 = #agicen14#18;  ||Codigo en Renta
     |SINO;
         |CIERRA #agicen14;
         swNoExis = 1;
         |ACABA;
     |FINSI;
     |CIERRA #agicen14;

    ||Lee Ficha Sociedades

     swExiste = 0;
     |ABRE #agim0019;
     #agim0019#0 = #900#0; || Empresa
     #agim0019#1 = 99;
     |LEE 041#agim0019.];
     |SI FSalida = 0;
         |SI #agim0019#0 ! #900#0;
             |LEE 041#agim0019.a;
             |SI FSalida = 0; |Y #agim0019#0 = #900#0;
                 swExiste = 1;
             |FINSI;
         |SINO;
             swExiste = 1;
         |FINSI;
     |FINSI;

     |SI swExiste = 1;
         efPn10 = #agim0019#7;      || Fecha Activa
         efPn11 = #agim0019#55;     || Fecha Inactiva
         nPin1 = #agim0019#4;
         aPin1 = #agim0019#5;
     |FINSI;
     |CIERRA #agim0019;

     |ABRE #agifigen;
     #agifigen#0 = #900#0; || Empresa
     |LEE 041#agifigen.=;
     |SI FSalida = 0;
         efPn10  = #agifigen#93;
         efPn11  = #agifigen#94;
         nPin1   = #agifigen#87;
         aPin1   = #agifigen#88;
         #900#1 = #agifigen#13;  || Nif
         #900#2 = #agifigen#1;   || Nombre
     |FINSI;
     |CIERRA #agifigen;

     |SI efPn10 [ "01.01.1900"; efPn10 = "01.01.0000"; |FINSI;
     |SI efPn11 [ "01.01.1900"; efPn11 = "01.01.0000"; |FINSI;

     #900#3 = nPin1;
     #900#4 = aPin1;
     #900#9 = efPn10;
     #900#10 = efPn11;
     #900#11 = "ACTIVA";

     |SI #999#6 = "N"; #900#11 = "INACTIVA"; |FINSI;

     #900#12 = #0#1;                                   || Ejercicio
     #900#13 = efDFecha;
     #900#14 = efHFecha;
     #900#50 = #0#45; ||Titulo del Libro
|FPRC;

|PROCESO Imprime;
   enEmpresa = #999#0;

  |HAZ LeeDatosEmpresa;

  |SI aCoBien = "S"; |Y #0#47 = "N";
      |ACABA;
  |FINSI;

  |SI  swNoExis = 0;
       |HAZ Busco130;
       |HAZ Busco131;
  |FINSI;
|FPRC;

|DEFBUCLE dsmow014M;
     #999#1;
     ;
     00000, 0000, 00;
     99999, 9999, 99;
     ;
     NULL, Imprime;
|FIN;

|PROCESO Imprimir;
     aAlfa2 = #0#1;
     aAlfa1 = "01.01." + aAlfa2;
     aAlfa2 = "31.12." + aAlfa2;
     efDFecha  = aAlfa1;     efHFecha  = aAlfa2;

     nDActividad = #0#38;
     nHActividad = #0#39;

     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;
     |INFOR "dsmoy014";
     |SI FSalida ! 0;  |FINIMP; |ACABA;  |FINSI;

     |BUCLE dsmow014M;

     |FININF;
     |FINIMP;
|FPRC;

|| /////////////////////////////////////////////////////////////////////
|PROCESO GrabaTempo;
     enEmpresa  = #4#0;

   || ... Seleccion de Tipo de Empresa

     #agifigen#0 = enEmpresa;
     |LEE 041#agifigen.=;
     |SI FSalida ! 0;
         |DDEFECTO #agifigen;
     |FINSI;

     nSPin1 = #agifigen#87;

     eaEstadoEmp = "S";
     |SI #agifigen#94 > "01.01.0000";
         eaEstadoEmp = "N";
         |SI #0#24 = "N"; |ACABA; |FINSI;
     |FINSI;

     enSwSelEmp = 1;
     |SI #0#46 = "S";
         |SI nSPin1 ] #0#25; |Y nSPin1 [ #0#26;
             enSwSelEmp = 0;
         |FINSI;
     |SINO;
         |SI nSPin1 = #0#27;
             enSwSelEmp = 0;
         |SINO;
             |PARA nCampo = 28;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
                   |SI nSPin1 = #0nCampo; |Y nSPin1 ! 0;
                       enSwSelEmp = 0;
                       |SAL;
                   |FINSI;
             |SIGUE;
         |FINSI;
     |FINSI;

     |SI enSwSelEmp = 1; |ACABA; |FINSI;

   || ------------------------------------------------------------------------

     #999#0  = enEmpresa;
     #999#1  = #4#1;
     #999#2  = 99;   ||  uno solo por empresa #4#2;
     |LEE 001#999=;
     |SI FSalida ! 0;
         |DDEFECTO #999;
         #999#0  = #4#0;
         #999#1  = #4#1;
         #999#2  = 99; || uno solo por empresa  #4#2;
         #999#5  = #4#3;
         #999#6  = eaEstadoEmp;
         #999#7  = #4#4;
         |GRABA 020#999n;
         nGTempo = 1;
     |FINSI;
|FPRC;

|DEFBUCLE dsmom004;
     #4#1;
     ;
     nDEmpresa, #0#1, 00, 130, 00;
     nHEmpresa, #0#1, 99, 131, 99;
     ;
     NULL, GrabaTempo;
|FIN;

|PROCESO Tipo3;  |TIPO 3;
    enEjer = #0#1;
    enSwNo = 0;
    aFichero = ("w14" + Usuario) % 108;
    |NOME_DAT #999 aFichero;
    |ABRE #999;  |CIERRA #999;  |DELINDEX #999;
    aFichero = ("w37" + Usuario) % 108;
    |NOME_DAT #901 aFichero;
    |ABRE #901;  |CIERRA #901;  |DELINDEX #901;

    |ABRE #999;
    |ABRE #agifigen;
    nGTempo = 0;   ||Busco Empresas Contables
    |SI #0#2 ! 0;
        nDEmpresa = #0#2;
        nHEmpresa = #0#3;
        |BUCLE dsmom004;
    |SINO;
        |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
              |SI #0nCampo ! 0;
                  nDEmpresa = #0nCampo;
                  nHEmpresa = #0nCampo;
                  |BUCLE dsmom004;
              |FINSI;
        |SIGUE;
    |FINSI;
    |CIERRA #agifigen;
    |CIERRA #999;

    |SI nGTempo = 0;    || No existe ninguna empresa
        |MENAV "    Error. No existe ninguna Empresa dentro de la Seleccion ";
    |SINO;
        |HAZ Imprimir;
    |FINSI;
    |DELINDEX #901;
    |DELINDEX #999;
|FPRC;
