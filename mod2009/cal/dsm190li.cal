|FICHEROS;
     dsm190ca #190;
     dsm190li #191;
     dsmow078 #978,MANTE;
     dsmow079 #979,MANTE;
     :/basica/def/agidanif #3;

     &regisnif@ #709;
|FIN;

|VARIABLES;
     nCampo      = 0;
     nPtec       = 0;
     nCampoD     = 0;
     nCampoO     = 0;
     nForma      = 0;

     aMenav      = "";
     aPathModelo = "";
     aDef        = "";
     aAlfa       = "";
     aTecla      = "";

     &enError    = 0;
     &enBoton3   = 0;
|FIN;

|INCLUYE i_variar;

|PROCESO Tipo12;  |TIPO 12;
|FPRC;

|PROCESO PorEntero;
     nCampoD = Campo - 1;

     |SI #979Campo > #979nCampoD;
         |MENAV "     No puede ser superior al Campo Anterior.";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO TotalPersonas;
     #978#6 = #978#1 + #978#2 + #978#3 + #978#4 + #978#5;
     |PINTA #978#6;
|FPRC;

|PROCESO Boton3;
     nPtec = 825;
     |PTEC nPtec;
|FPRC;

|PROCESO PonDatosNif;  |TIPO 7;
     eaCodNif  = #191#6;
     |HAZ LeeNifs;

     |HAZ CalculaDatosNif;

     #191#12 = #709#1;   |PINTA #191#12;
     #191#13 = #709#4;   |PINTA #191#13;
                         |PINTA #191#14;
                         |PINTA #191#15;
                         |PINTA #191#25;
                         |PINTA #191#26;
                         |PINTA #191#27;

     |C_M #191#10, 1, "S";
     |C_M #191#14, 1, "S";
     |C_M #191#21, 1, "S";
     |C_M #191#22, 1, "S";
     |C_M #191#23, 1, "S";
     |C_M #191#24, 1, "S";
     |C_M #191#25, 1, "S";
     |C_M #191#26, 1, "S";

     |SI #191#8 ! "A"; |Y #191#8 ! "B";  |Y #191#8 ! "C";
      |Y #191#8 ! "D";  |Y  #191#8 ! "M";
         |C_M #191#14, 1, "N";   #191#14 = 0;   |PINTA #191#14;
         |C_M #191#21, 1, "N";   #191#21 = 0;   |PINTA #191#21;
         |C_M #191#22, 1, "N";   #191#22 = 0;   |PINTA #191#22;
         |C_M #191#23, 1, "N";   #191#23 = 0;   |PINTA #191#23;
         |C_M #191#24, 1, "N";   #191#24 = 0;   |PINTA #191#24;
         |C_M #191#25, 1, "N";   #191#25 = 0;   |PINTA #191#25;
         |C_M #191#26, 1, "N";   #191#26 = 0;   |PINTA #191#26;

         |PARA nCampoD = 28;  |SI nCampoD [ 38;  |HACIENDO nCampoD = nCampoD + 1;
               #191nCampoD = 0;
         |SIGUE;

         |PARA nCampoD = 41;  |SI nCampoD [ 60;  |HACIENDO nCampoD = nCampoD + 1;
               #191nCampoD = 0;
         |SIGUE;
     |FINSI;

     |SI #191#26 ! 2;
         |C_M #191#27, 1, "N";  #191#27 = "";  |PINTA #191#27;
     |FINSI;
|FPRC;

|PROCESO Tipo2m190li;  |TIPO 2;
     #190#6 = #190#6 +. 1;
     #190#7 = #190#7 +. (#191#16 + #191#18);
     #190#8 = #190#8 +. (#191#17 + #191#19);

     |SI enBoton3 = -1;  |ACABA;  |FINSI;

     nForma = 0 +. 1;
     |SI nForma = 1;
         |SI #191#8 ! "A"; |Y #191#8 ! "B";  |Y #191#8 ! "C";
          |Y #191#8 ! "D"; |Y #191#8 ! "M";
             |ACABA;
         |FINSI;

         |ESTADO_CONTROL enBoton3, "HIDE";

         |SI #191#1 [ 2002;
             |PUSHV 0501 2380;
             |PARA nCampoD = 28;  |SI nCampoD [ 38;  |HACIENDO nCampoD = nCampoD + 1;
                   nCampoO = nCampoD - 28;
                   #978nCampoO = #191nCampoD;
             |SIGUE;

             |PINPA #0#978;
             |PINDA #0#978;
             |ENDATOS #1#978;
             |SI FSalida = 0;
                 |PARA nCampoD = 28;  |SI nCampoD [ 38;  |HACIENDO nCampoD = nCampoD + 1;
                       nCampoO = nCampoD - 28;
                       #191nCampoD = #978nCampoO;
                 |SIGUE;
             |FINSI;
             |POPV;
         |FINSI;

         |SI #191#1 ] 2003;
             |PUSHV 0501 2380;

             |PARA nCampoD = 41;  |SI nCampoD [ 60;  |HACIENDO nCampoD = nCampoD + 1;
                   nCampoO = nCampoD - 41;
                   #979nCampoO = #191nCampoD;
             |SIGUE;

             |PINPA #0#979;
             |PINDA #0#979;
             |ENDATOS #1#979;
             |SI FSalida = 0;
                 |PARA nCampoD = 41;  |SI nCampoD [ 60;  |HACIENDO nCampoD = nCampoD + 1;
                       nCampoO = nCampoD - 41;
                       #191nCampoD = #979nCampoO;
                 |SIGUE;
             |FINSI;
             |POPV;
         |FINSI;

         |ESTADO_CONTROL enBoton3, "SHOW";
     |FINSI;
|FPRC;

|PROCESO Tipo11m190li;  |TIPO 11;
     |SI FSalida = 10;
         eaVarDni  = #191Campo;
         enCalcCif = 1;
         |HAZ CalculameDNI;
         #191Campo = eaVarDni;
         |PINTA #191Campo;
         |ERROR;
     |FINSI;

     |SI FSalida = 11;
         |SI #191#8 ! "A"; |Y #191#8 ! "B";  |Y #191#8 ! "C";
          |Y #191#8 ! "D"; |Y #191#8 ! "M";
             |ERROR;
             |ACABA;
         |FINSI;

         |ESTADO_CONTROL enBoton3, "HIDE";

         |SI #191#1 [ 2002;
             |PUSHV 0501 2380;
             |PARA nCampoD = 28;  |SI nCampoD [ 38;  |HACIENDO nCampoD = nCampoD + 1;
                   nCampoO = nCampoD - 28;
                   #978nCampoO = #191nCampoD;
             |SIGUE;

             |PINPA #0#978;
             |PINDA #0#978;
             |LEETECLA aTecla;
             |POPV;
         |FINSI;

         |SI #191#1 ] 2003;
             |PUSHV 0501 2380;

             |PARA nCampoD = 41;  |SI nCampoD [ 60;  |HACIENDO nCampoD = nCampoD + 1;
                   nCampoO = nCampoD - 41;
                   #979nCampoO = #191nCampoD;
             |SIGUE;

             |PINPA #0#979;
             |PINDA #0#979;
             |LEETECLA aTecla;
             |POPV;
         |FINSI;

         |ESTADO_CONTROL enBoton3, "SHOW";

         |ERROR;
     |FINSI;
|FPRC;

|PROCESO LetraNifConyuge;
     |SI FSalida = 10;
         eaVarDni  = #191Campo;
         enCalcCif = 1;
         |HAZ CalculameDNI;
         #191Campo = eaVarDni;
         |PINTA #191Campo;
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO MiraDni190li;  |TIPO 6;
     eaVarDni  = #191Campo;
     enCalcCif = 0;
     |HAZ CalculameDNI;
     eaVarDni = (eaVarDni + "             ") % 112;
     |SI #191Campo ! eaVarDni;
         |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
         |SI FSalida = 0;
             #191Campo = eaVarDni;  |PINTA #191Campo;
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO RecogeDatos;
     enError = 1;
     |HAZ GrabaDatosNif;

     |SI enError = 1;
         |MENAV "     Datos del NIF sin Actualizar o no hay Trabajador, PUEDE que los datos esten incompletos.";
         |ACABA;
     |FINSI;

     |ABRE #3;
     #3#0  = #191#6;
     #3#1  = 1;
     |LEE 040#3=;
     |SI FSalida ! 0;
         |MENAV "      NIF sin Datos Personales, Actualicelo.";
         |ERROR;
     |FINSI;
     |CIERRA #3;
|FPRC;

|PROCESO Tipo7;  |TIPO 7;
     |SI #191#8 ! "A"; |Y #191#8 ! "B";  |Y #191#8 ! "C";
      |Y #191#8 ! "D"; |Y #191#8 ! "M";
         |ESTADO_CONTROL enBoton3, "DISABLE";
     |SINO;
         |ESTADO_CONTROL enBoton3, "ENABLE";
     |FINSI;
|FPRC;

|PROCESO TipoRelacion;  |TIPO 7;
     |SI #191#8 ! "A";  |Y #191#8 ! "M";
         |C_M #191#10, 1, "N";
         #191#10 = 0;  |PINTA #191#10;
         |ACABA;
     |FINSI;

     |ABRE #3;
     #3#0  = #191#6;
     #3#1  = 1;
     |LEE 040#3=;
     |SI FSalida ! 0;  |DDEFECTO #3; |FINSI;
     |CIERRA #3;

     #191#10 = #3#36;
     |PINTA #191#10;
|FPRC;

|PROCESO SituacionFamiliar;
     |C_M #191#27, 1, "S";
     |SI #191#26 ! 2;
         |C_M #191#27, 1, "N";  #191#27 = "";  |PINTA #191#27;
     |FINSI;
|FPRC;
