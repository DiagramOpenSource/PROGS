|FICHEROS;
   dsmoy013 #0;

   dsmom060 #60;
   dsmom061 #61;

   :/basica/def/agicen14 #114;
   :/basica/def/agifigen #118;
   :/basica/def/agim0019 #119;

   :/contagen/def/canempre #300;
   :/contagen/def/cacuenta #304;
   :/contagen/def/cacuedep #305;
   :/contagen/def/caconimp #306;  || configuracion impresiones

   dsmom009 #09;
   dsmom010 #10;
   dsmom012 #12;
   dsmom013 #13;

   dsmow032 #900;          ||Def de impresion
   dsmow033 #901;
   dsmow043 #999,MANTE;
|FIN;

|VARIABLES;
   &entrada = 1;
   &eSwSelec  = 0;
   &eaDDepar  = "";
   &eaHDepar  = "";
   &efDFecha  = @;
   &efHFecha  = @;
   &eaNomFDep = "";  ||Nombre del Fichero Creado
   &dirfich0  = "";

   aMonMod    = "";
   informe = "";
   nInkey      = 0;
   aFichero    = "";
   nCampo      = 0;
   nDEmpresa   = 0;
   nHEmpresa   = 0;
   nDActividad = 0;
   nHActividad = 0;
   nLaEmpresa  = 0;
   nGTempo     = 0;

   nSPin1      = 0;
   aPath       = "";
   nEjercicio  = 0;
   nQueBusco   = 0;
   aQueBusco   = "";

   aAlfa1 = "";
   aAlfa2 = "";
   aAlfa3 = "";
   nPara1 = 0;
   nPara2 = 0;
   nNume1 = 0;
   nNume2 = 0;
   nNume3 = 0;
   nNume4 = 0;

   SwHayDatos = 0;

   fDFecha   = @;
   fHFecha   = @;

   &enI1     = 0;
   &enI2     = 0;
   &enI3     = 0;
   &enI4     = 0;
   &enI5     = 0;
   &nPagina  = 0;
   &SwPrim   = 0;
   &SwLinea  = 0;
   &eaTituloL = "";
   &eaTitul2  = "";
   &aTipIG    = "";

   nPin1 = 0;
   aPin1 = "";
   efPn10 = @;
   efPn11 = @;
   swExiste = 0;
   swNoExis = 0;

   aElDepar  = "";
   aDCuenta = "";
   aHCuenta = "";
   nSald1 = 0;
   nSald2 = 0;
   nSald3 = 0;
   nSald4 = 0;
   sw = 0;
   fLaFecha = @;
   nMixto   = 0;
   nDConta  = 0;
   nDPlani  = 0;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\
|CALCULO Eano; |TIPO 7;
 fLaFecha = ~;
 aAlfa1 = fLaFecha; aAlfa1 = aAlfa1 % -104;
 #0#1 = aAlfa1;
 |PINTA #0#1;
|FCAL;

|CALCULO losDefectos; |TIPO 0;
 aAlfa1 = #0#1;
|FCAL;

|CALCULO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#2  = enEmpresa;  |PINTA #0#2;
         #0#3  = enEmpresa;  |PINTA #0#3;

         |C_M #0#2, 1, "N";
         |C_M #0#3, 1, "N";
     |FINSI;
|FCAL;

|CALCULO Parrilla; |TIPO 0;
     |SI #0#2 = 0;
         #0#2 = 0;  |PINTA #0#2;
         #0#3 = 0;  |PINTA #0#3;  |C_M #0#3, 1, "N";
         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#2, 1, "S";
             |C_M #0#3, 1, "S";
         |FINSI;

         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas; |TIPO 0;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO YaSeCual; |TIPO 7;
 |SI enEmpresa ! 0;
     #0#46 = "S"; |PINTA #0#46;
     #0#24 = "S"; |PINTA #0#24;
     #0#25 = 00;  |PINTA #0#25;
     #0#26 = 99;  |PINTA #0#26;
     |C_M #0#24, 1, "N";
     |C_M #0#25, 1, "N";
     |C_M #0#26, 1, "N";
     |C_M #0#46, 1, "N";
 |FINSI;
|FCAL;

|CALCULO SelTipo0; |TIPO 0;
 |SI #0#46 = "N";
     #0#25 = 0;  |PINTA #0#25;  |C_M #0#25, 1, "N";
     #0#26 = 99; |PINTA #0#26;  |C_M #0#26, 1, "N";
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |SINO;
     |SI enEmpresa = 0;
         |C_M #0#25, 1, "S";
         |C_M #0#26, 1, "S";
     |FINSI;
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "N";
           #0nCampo = 0;  |PINTA #0nCampo;
     |SIGUE;
 |FINSI;
|FCAL;

|CALCULO SelTipo1;
 |SI #0Campo = 0;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           #0nCampo = 0;  |C_M #0nCampo, 1, "N";
     |SIGUE;
 |SINO;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |FINSI;
|FCAL;

|| **********************************************************************************
|| **********************************************************************************
|| ....................... Situa la Empresa Contable
|PROCESO LEmpresaPlan;
   nGTempo = nGTempo + 1;
   #999#0 = nGTempo;       || Guia
   #999#1 = enEmpresa;     || Empresa
   #999#2 = 0;             || Periodo Contable
   #999#3 = enEjer;        || A¤o
   #999#4 = 01;            || Desde Mes
   #999#5 = 1;             || N§ Digitos
   #999#6 = 4;             || Digitos Nivel 1
   #999#7 = 0;             || Digitos Nivel 2
   #999#8 = 0;             || Digitos Nivel 3
   #999#9 = 0;             || Digitos Nivel 4
   #999#10 = 0;            || Digitos Nivel 5
   #999#11 = 0;            || Digitos Nivel 6
   #999#12 = eaEstadoEmp;  || Estado
   #999#13 = aMonMod;     || Moneda
   #999#14 = #agifigen#1;  || Nombre Empresa
   #999#15 = "PLANILLAS";  || Path
   #999#16 = #0#43;        || Emitir
   |GRABA 020#999n;
|FPRC;
|PROCESO LeoDsmom009;
 nDPlani = 1;
 |ERROR10;
|FPRC;

|DEFBUCLE LeoDsmom009;
 #9#1;
 ;
 #agicen14#0, #0#1, 01, 00, #agicen14#1;
 #agicen14#0, #0#1, 99, 99, #agicen14#1;
 e;
 NULL, LeoDsmom009;
|FIN;

|PROCESO LaActividad;
 |SI #agicen14#6 > "01.01.0000";
     |SI #0#24 = "N"; |ACABA; |FINSI;
 |FINSI;

 aAlfa1 = #agicen14#15; |QBF aAlfa1;
 |SI aAlfa1 = "REGISTROS";  nDConta = 1; |FINSI;
 |SI aAlfa1 = "PLANILLAS";  nDPlani = 1; |FINSI;
 |SI aAlfa1 = "MANUAL"; |BUCLE LeoDsmom009; |FINSI;

 |SI nDConta = 0; |Y nDPlani = 1;
     enSwSelEmp = 0;
 |FINSI;
 |SI nDConta = 1; |Y nDPlani = 1;
     nMixto = 1;
     enSwSelEmp = 1;
 |FINSI;
|FPRC;

|DEFBUCLE LaActividad;
  #agicen14#1,
  ;
  enEmpresa, 01;
  enEmpresa, 99;
  e;
  NULL, LaActividad;
|FIN;

|| .................... graba Contabilidad
|PROCESO LEmpresaConta;

 enEmpresa  = #canempre#2;
 enPerConta = #canempre#3;   || Periodo Contable

 |SI #0#24 = "N"; |Y #canempre#21 = "I"; |ACABA; |FINSI;

|| ------------------------------------------------------------------------
   aAlfa1 = enEmpresa;  aAlfa1 = ("00000" + aAlfa1) % -105;
   aAlfa2 = enPerConta; aAlfa2 = ("0" + aAlfa2) % -101;
   aPathConta  = aPath + "contagen/fich/" + aAlfa1 + aAlfa2 + "/";
   |SI nMixto = 1;
       aPathConta = aPathConta + ".Mixto";
   |FINSI;

   nGTempo = nGTempo + 1;
   #999#0 = nGTempo;       || Guia
   #999#1 = enEmpresa;     || Empresa
   #999#2 = enPerConta;    || Periodo Contable
   #999#3 = enEjer;        || A¤o
   #999#4 = #canempre#11;  || Desde Mes
   #999#5 = #canempre#13;  || N§ Digitos
   #999#6 = #canempre#14;  || Digitos Nivel 1
   #999#7 = #canempre#15;  || Digitos Nivel 2
   #999#8 = #canempre#16;  || Digitos Nivel 3
   #999#9 = #canempre#17;  || Digitos Nivel 4
   #999#10 = #canempre#18; || Digitos Nivel 5
   #999#11 = #canempre#19; || Digitos Nivel 6
   eaEstadoEmp = "S"; |SI #canempre#21 = "I"; eaEstadoEmp = "N"; |FINSI;
   #999#12 = eaEstadoEmp;  || Estado
   #999#13 = #canempre#28; || Moneda
   #999#14 = #canempre#1;  || Nombre Empresa
   #999#15 = aPathConta;   || Path
   #999#16 = #0#43;        || Emitir
   |GRABA 020#999n;
|FPRC;

|PROCESO LEmpresaEOS;
 enEmpresa  = #agifigen#0;
 enPerConta = 0;

 eaEstadoEmp = "S";
 |SI #agifigen#94 > "01.01.0000";
     eaEstadoEmp = "N";
     |SI #0#24 = "N"; |ACABA; |FINSI;
 |FINSI;

|| ... Seleccion de Tipo de Empresa
  nSPin1 = #agifigen#87;
  enSwSelEmp = 1;
  |SI #0#46 = "S";
      |SI nSPin1 ] #0#25; |Y nSPin1 [ #0#26;
          enSwSelEmp = 0;
      |FINSI;
  |SINO;
      |SI nSPin1 = #0#27;
          enSwSelEmp = 0;
      |SINO;
          |PARA nCampo = 28;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
                |SI nSPin1 = #0nCampo; |Y nSPin1 ! 0;
                    enSwSelEmp = 0;
                    |SAL;
                |FINSI;
          |SIGUE;
      |FINSI;
  |FINSI;
  |SI enSwSelEmp = 1; |ACABA; |FINSI;
|| ------------------------------------------------------------------------

   nMixto     = 0;
   nDConta    = 0;
   nDPlani    = 0;
   enSwSelEmp = 1;
   |BUCLE LaActividad;
   |SI enSwSelEmp = 0;
       |HAZ LEmpresaPlan;
   |SINO;
       |SELECT #4#canempre;
       #canempre#2  = enEmpresa;
       #canempre#26 = nEjercicio;
       |LEE 001#canempre.=;
       |SI FSalida = 0;
           |HAZ LEmpresaConta;
       |SINO;
           |SI nMixto = 1;
               nMixto = 0;
               |HAZ LEmpresaPlan;
           |FINSI;
       |FINSI;
       |SELECT #1#canempre;
   |FINSI;
|FPRC;

|DEFBUCLE agifigen;
 #agifigen#1;
 ;
 nDEmpresa;
 nHEmpresa;
 e;
 NULL, LEmpresaEOS;
|FIN;

||///////////////////////////////////////////////////////////////////
|CALCULO Datos7; |TIPO 7;
 |MENSA "    <F3> Cambiar Emitir S/N";
 |ATRI R; |PINTA 1169, "Imp"; |ATRI 0;
|FCAL;

|CALCULO DatosManip; |TIPO 11;
   nInkey = FSalida;
   |BLIN 04;
   |SI nInkey = 11;
       |SI #999#16 = "S";
           #999#16 = "N";
       |SINO;
           #999#16 = "S";
       |FINSI;
       |GRABA 020#999a;
   |FINSI;
|FCAL;

|RUTINA inferior;
   #999#0 = 00001;
|FRUT;

|RUTINA superior;
   #999#0 = 99999;
|FRUT;

||///////////////////////////////////////////////////////////////////
|CALCULO Tipo3;  |TIPO 3;
    #0#0  = "S"; || Es Inmovilizado

    aMonMod = "P";
    |HAZ LeeEURODBMOD;
    |SI EURODBMOD = 2; aMonMod = "E"; |FINSI;

    enEjer = #0#1;
    eaPlanilla = "dsmom060";  |HAZ PlanDePlanillas;

    aAlfa1 = enEjer; |QBF aAlfa1;
    aAlfa1 = aAlfa1 % -102;
    nEjercicio = aAlfa1;
    |DBASS aPath; |QBT aPath;

    aFichero = ("032w" + Usuario) % 108;
    |NOME_DAT #901 aFichero;

    aFichero = ("043w" + Usuario) % 108;
    |NOME_DAT #999 aFichero;
    |ABRE #999;  |CIERRA #999;  |DELINDEX #999;
    eaFEmpresa = aFichero;

    |ABRE #999;
    nGTempo = 0;   ||Busco Empresas Contables
    |ABRE #canempre;
    |SI #0#2 ! 0;
        nDEmpresa = #0#2;
        nHEmpresa = #0#3;
        |BUCLE agifigen;
    |SINO;
        |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
              |SI #0nCampo ! 0;
                  nDEmpresa = #0nCampo;
                  nHEmpresa = #0nCampo;
                  |BUCLE agifigen;
              |FINSI;
        |SIGUE;
    |FINSI;
    |CIERRA #canempre;
    |CIERRA #999;

    |SI nGTempo = 0;    || No existe ninguna empresa
        |MENAV "    Error. No existe ninguna Empresa dentro de la Seleccion ";
        |DELINDEX #999;
        |ACABA;
    |FINSI;

    |C_V #999#12,1,"N"; |C_P #999#16,1,1270;

    |PUSHV 0509 2375;
    |ENTLINEAL #1#999, -1, 2, 22, 1, inferior, superior;
    |POPV;
    |HAZ Imprimir;

    |DELINDEX #999;
|FCAL;

|| *************************************************************************
|| *************************************************************************
|| *************************************************************************

|PROCESO LeeAux0;

 |SI #901#3 ! 0; |O #901#4 ! 0; |O #901#5 ! 0; |O #901#6 ! 0;

     #60#6 = enPlan;
     #60#0 = #901#0;
     #60#1 = #901#1;
     #60#2 = #901#2;
     |LEE 001#60=;
     |SI FSalida ! 0;
         |DDEFECTO #60;
     |FINSI;

     |PRINT;
     SwLinea = 1;
     SwPrim  = 1;
     sw      = 1;

     |SI #901#1 = "I";
         #900#25 = #900#25 + #901#3;
         #900#26 = #900#26 + #901#4;
         #900#27 = #900#27 + #901#5;
         #900#28 = #900#28 + #901#6;
         #900#29 = #900#29 + #901#7;
     |SINO;
         #900#30 = #900#30 + #901#3;
         #900#31 = #900#31 + #901#4;
         #900#32 = #900#32 + #901#5;
         #900#33 = #900#33 + #901#6;
         #900#34 = #900#34 + #901#7;
     |FINSI;

 |FINSI;
|FPRC;

|DEFBUCLE LeeAux;
 #901#1;
 ;
 #0#0, aTipIG, 01;
 #0#0, aTipIG, 99;
 ;
 NULL, LeeAux0;
|FIN;

|PROCESO ImpAuxiliar;    || ... Imprimir el balance en detalle

 |ABRE #60;
 SwPrim  = 0;
 nPagina = 1;
 |PDEFECTO #900,25,50;

 eaTitul2 = "INGRESOS PROCEDENTES DEL INMOVILIZADO";
 sw      = 0;
 SwLinea = 0;
 aTipIG = "I";
 |BUCLE LeeAux;
 |SI sw = 1;
     eaTitul2 = "Total Ingresos por ventas del inmovilizado";
     SwLinea = 2;
     |PRINT;
 |FINSI;

 eaTitul2 = "GASTOS PROCEDENTES DEL INMOVILIZADO";
 sw      = 0;
 SwLinea = 0;
 aTipIG  = "G";
 |BUCLE LeeAux;
 |SI sw = 1;
     eaTitul2 = "Total Gastos por ventas del inmovilizado";
     SwLinea = 2;
     |PRINT;
 |FINSI;

|| ... Rendimiento Neto
 #900#45 = #900#25 - #900#30;
 #900#46 = #900#26 - #900#31;
 #900#47 = #900#27 - #900#32;
 #900#48 = #900#28 - #900#33;
 #900#49 = #900#29 - #900#34;

 |PIEINF;
 |CIERRA #60;
|FPRC;

|| /////////////////////////////////////////////////////////////////
|PROCESO SumaSP13;
 SwHayDatos = 1;

 nNume1 = 1;
 |SI #13#7 = "-"; nNume1 = -1; |FINSI;
 #13#8 = #13#8 * nNume1;

 |SI #13#2 [ 3;                nSald1 = nSald1 + #13#8; |FINSI;
 |SI #13#2 ] 4; |Y #13#2 [ 6;  nSald2 = nSald2 + #13#8; |FINSI;
 |SI #13#2 ] 7; |Y #13#2 [ 9;  nSald3 = nSald3 + #13#8; |FINSI;
 |SI #13#2 ] 10;               nSald4 = nSald4 + #13#8; |FINSI;
|FPRC;

|DEFBUCLE dsmom013;
 #13#1;
 ;
 enEmpresa, #0#1, 01, 00, enActividad, aDCuenta;
 enEmpresa, #0#1, 99, 99, enActividad, aHCuenta;
 e;
 NULL, SumaSP13;
|FIN;

|PROCESO SumaSP12;
 SwHayDatos = 1;

 nNume1 = 1;
 |SI #12#7 = "-"; nNume1 = -1; |FINSI;
 #12#8 = #12#8 * nNume1;

 |SI #12#2 [ 3;                nSald1 = nSald1 + #12#8; |FINSI;
 |SI #12#2 ] 4; |Y #12#2 [ 6;  nSald2 = nSald2 + #12#8; |FINSI;
 |SI #12#2 ] 7; |Y #12#2 [ 9;  nSald3 = nSald3 + #12#8; |FINSI;
 |SI #12#2 ] 10;               nSald4 = nSald4 + #12#8; |FINSI;
|FPRC;

|DEFBUCLE dsmom012;
 #12#1;
 ;
 enEmpresa, #0#1, 01, 00, enActividad, aDCuenta;
 enEmpresa, #0#1, 99, 99, enActividad, aHCuenta;
 e;
 NULL, SumaSP12;
|FIN;

|PROCESO SumaSP10;
 SwHayDatos = 1;

 nNume1 = 1;
 |SI #10#7 = "-"; nNume1 = -1; |FINSI;
 #10#8 = #10#8 * nNume1;

 |SI #10#2 [ 3;                nSald1 = nSald1 + #10#8; |FINSI;
 |SI #10#2 ] 4; |Y #10#2 [ 6;  nSald2 = nSald2 + #10#8; |FINSI;
 |SI #10#2 ] 7; |Y #10#2 [ 9;  nSald3 = nSald3 + #10#8; |FINSI;
 |SI #10#2 ] 10;               nSald4 = nSald4 + #10#8; |FINSI;
|FPRC;

|DEFBUCLE dsmom010;
 #10#1;
 ;
 enEmpresa, #0#1, 01, 00, enActividad, aDCuenta;
 enEmpresa, #0#1, 99, 99, enActividad, aHCuenta;
 e;
 NULL, SumaSP10;
|FIN;

|PROCESO SumaSaldos;
 SwHayDatos = 1;
 |PARA nPara2 = 9; |SI nPara2 [ 53; |HACIENDO nPara2 = nPara2 + 1;
       #305nPara2  = (#305nPara2  / enConversor) ! EURODBMOD;
 |SIGUE;

 nSald1 = nSald1 + (#305#14 + #305#17 + #305#20);
 nSald2 = nSald2 + (#305#23 + #305#26 + #305#29);
 nSald3 = nSald3 + (#305#32 + #305#35 + #305#38);
 nSald4 = nSald4 + (#305#41 + #305#44 + #305#47);

|FPRC;

|DEFBUCLE LeeCuentas;
 #305#2;
 3;
 aElDepar, aDCuenta, "S";
 aElDepar, aHCuenta, "S";
 e;
 NULL, SumaSaldos;
|FIN;

|PROCESO dsmom061_0;
 #901#0 = #61#0;
 #901#1 = #61#1;
 #901#2 = #61#2;
 |LEE 001#901=;
 |SI FSalida ! 0;
     |DDEFECTO #901;
     #901#0 = #61#0;
     #901#1 = #61#1;
     #901#2 = #61#2;
     |GRABA 020#901b;
 |FINSI;
 aAlfa1 = #61#4; |QBF aAlfa1;              aDCuenta = aAlfa1;
 aAlfa1 = (aAlfa1 + "zzzzzzzzzzzz") % 112; aHCuenta = aAlfa1;

 nSald1 = 0;  nSald2 = 0;
 nSald3 = 0;  nSald4 = 0;

 |SI nQueBusco = 0;
     |BUCLE LeeCuentas;
 |SINO;
     aDCuenta = aDCuenta % 104;
     aHCuenta = aHCuenta % 104;
     |BUCLE dsmom010;
     |BUCLE dsmom012;
     |BUCLE dsmom013;
 |FINSI;

 |SI #61#5 = "-"; |Y nQueBusco = 0;
     nSald1 = - nSald1; nSald2 = - nSald2;
     nSald3 = - nSald3; nSald4 = - nSald4;
 |FINSI;

 #901#3 = #901#3 + nSald1;
 #901#4 = #901#4 + nSald2;
 #901#5 = #901#5 + nSald3;
 #901#6 = #901#6 + nSald4;
 #901#7 = #901#3 + #901#4 + #901#5 + #901#6;

 |GRABA 020#901a;
 |LIBERA #901;
|FPRC;

|DEFBUCLE dsmom061;
 #61#1;
 ;
 enPlan, #0#0, " ", 00, 001;
 enPlan, #0#0, "z", 99, 999;
 ;
 NULL, dsmom061_0;
|FIN;

||////////////////////////////////////////////////////////////////////////
|PROCESO Emision;
   aAlfa1 = #999#15; |QBF aAlfa1;
   nQueBusco = 0;
   |SI aAlfa1 = "PLANILLAS";
       nQueBusco = 1;
   |SINO;
       aAlfa2 = aAlfa1 - ".Mixto"
       |SI aAlfa2 ! aAlfa1;
           nQueBusco = 2;
           #999#15 = aAlfa2;
       |FINSI;
   |FINSI;

   enEmpresa  = #999#1;
   enPerConta = #999#2;
   eaLaMonEmp = #999#13;

   |SI nQueBusco = 1;   ||Por planillas
       enConversor = 1; ||la misma moneda
   |SINO;
       |HAZ EnConversor;
   |FINSI;
   enEjer     = #999#3;   || A¤o
   dig1       = #999#4;   || Desde Mes
   dig3       = #999#5;   || N§ Digitos
   dig4       = #999#6;   || Digitos Nivel 1
   dig5       = #999#7;   || Digitos Nivel 2
   dig6       = #999#8;   || Digitos Nivel 3
   dig7       = #999#9;   || Digitos Nivel 4
   dig8       = #999#10;  || Digitos Nivel 5
   dig9       = #999#11;  || Digitos Nivel 6
   eaEstadoEmp = #999#12; || Estado
   eaNombreEmp = #999#14; || Nombre Empresa
   aPathConta  = #999#15; |QBF aPathConta; || Path

   || Recoger informacion sobre los niveles ...
   |SI dig3 = 1; MaximoDigitos = dig4; |FINSI;
   |SI dig3 = 2; MaximoDigitos = dig5; |FINSI;
   |SI dig3 = 3; MaximoDigitos = dig6; |FINSI;
   |SI dig3 = 4; MaximoDigitos = dig7; |FINSI;
   |SI dig3 = 5; MaximoDigitos = dig8; |FINSI;
   |SI dig3 = 6; MaximoDigitos = dig9; |FINSI;

   eaTituloL = #0#45; ||Titulo del Libro

   |SI nQueBusco ! 1;
       |PATH_DAT #304 aPathConta;
       dirfich0 = aPathConta;
       eSwSelec  = 2;
       |ATRI I;
       |PINTA 2302, "Procesando Cuentas por Departamentos ... Espere ";
       |ATRI 0;
       |SUB_EJECUTA_NP ":contagen/cacuedep";
       |PINTA 2302, "                                                ";
       |NOME_DAT #305 eaNomFDep;

       |PATH_DAT #305 aPathConta;
       |PATH_DAT #306 aPathConta;
       |DDEFECTO #306;  ||Configuracion de Informes
       |ABRE #306;
       #306#0 = 1;
       |LEE 000#306=;
       |CIERRA #306;
   |SINO;
       #306#4 = "N";
   |FINSI;

   informe = "dsmoy013";
   |SI #306#4 = "N"; informe = "drmoy013"; |FINSI;

   |INFOR informe;
   |SI FSalida ! 0;  |ACABA;  |FINSI;

   |PARA nPara1 = #0#38; |SI nPara1 [ #0#39; |HACIENDO nPara1 = nPara1 + 1;
         enActividad = nPara1;
         |HAZ LeeDatosEmpresa;
         |SI swNoExis = 0;       ||SI existe esta Actividad.

              nQueBusco = 0;
              |SI aQueBusco = "PLANILLAS";  nQueBusco = 1; |FINSI;
              aElDepar = enActividad; |QBF aElDepar;
              aElDepar = ("00" + aElDepar) % -102;

              SwHayDatos = 0;
              |ABRE #901;
              |BUCLE dsmom061;
              |CIERRA #901;

              |SI SwHayDatos = 1;
                  |HAZ ImpAuxiliar;
              |FINSI;
         |FINSI;
   |SIGUE;
   |FININF;
|FPRC;

|DEFBUCLE dsmow043;
 #dsmow043#1;
 16;
 000001, "S";
 999999, "S";
 ;
 NULL, Emision;
|FIN;

|PROCESO Imprimir;

    eaDDepar  = #0#38; |QBF eaDDepar;
    eaDDepar  = ("00" + eaDDepar) % -102;
    eaHDepar  = #0#39; |QBF eaHDepar;
    eaHDepar  = ("00" + eaHDepar) % -102;

    aAlfa2 = #0#1;
    aAlfa1 = "01.01." + aAlfa2;
    aAlfa2 = "31.12." + aAlfa2;
    efDFecha  = aAlfa1;     efHFecha  = aAlfa2;

    |IMPRE 0;
    |SI FSalida ! 0;  |ACABA;  |FINSI;

    |BUCLE dsmow043;

    |FINIMP;
|FPRC;

|PROCESO LeeDatosEmpresa;
  |DELINDEX #901;

  |DDEFECTO #900;

  #900#0 = enEmpresa;
  nPin1 = 0;
  aPin1 = "";
  efPn10 = "01.01.0000";
  efPn11 = "01.01.0000";

  || Lee Primera Actividad
    |ABRE #agicen14;
    #agicen14#0 = #900#0; || Empresa
    #agicen14#1 = 00;
    |LEE 041#agicen14.];
    |SI FSalida = 0; |Y #agicen14#0 = #900#0;
        #900#5 = #agicen14#9;
        #900#6 = #agicen14#10;
        #900#7 = #agicen14#7;
        #900#8 = #agicen14#8;
        efPn10 = #agicen14#5;
        efPn11 = #agicen14#6;
    |FINSI;

  || Lee Actividad en concreto.
    swNoExis = 0;
    #agicen14#0 = #900#0; || Empresa
    #agicen14#1 = enActividad;
    |LEE 041#agicen14.=;
    |SI FSalida = 0;
        #900#15 = #agicen14#1;   ||Act.
        #900#16 = #agicen14#2;   ||Tipo Epig.
        #900#17 = #agicen14#3;   ||Epigrafe
        #900#18 = #agicen14#4;   ||Descripcion Actividad
        #900#19 = #agicen14#5;   ||Fecha Inicio
        #900#20 = #agicen14#6;   ||Fecha Fin
        #900#21 = #agicen14#11;  ||Sector actividad
        #900#22 = #agicen14#12;  ||Sector actividad
        #900#23 = #agicen14#16;  ||Tipo Actividad
        #900#24 = #agicen14#17;  ||Descripcion Tipo Actividad
        #900#51 = #agicen14#20;  ||CNAE
        |SI #agicen14#6 > "01.01.0000";
            |SI #0#24 = "N";
                |CIERRA #agicen14;
                swNoExis = 1;
                |ACABA;
            |FINSI;
        |FINSI;
        aQueBusco = #agicen14#15; |QBF aQueBusco;
        |SI aQueBusco = "MANUAL"; aQueBusco = "PLANILLAS"; |FINSI;
    |SINO;
        |CIERRA #agicen14;
        swNoExis = 1;
        |ACABA;
    |FINSI;
    |CIERRA #agicen14;

 || Lee Ficha Sociedades
    swExiste = 0;
    |ABRE #agim0019;
    #agim0019#0 = #900#0; || Empresa
    #agim0019#1 = 99;
    |LEE 041#agim0019.];
    |SI FSalida = 0;
        |SI #agim0019#0 ! #900#0;
            |LEE 041#agim0019.a;
            |SI FSalida = 0; |Y #agim0019#0 = #900#0;
                swExiste = 1;
            |FINSI;
        |SINO;
            swExiste = 1;
        |FINSI;
    |FINSI;
    |SI swExiste = 1;
        efPn10 = #agim0019#7;      || Fecha Activa
        efPn11 = #agim0019#55;     || Fecha Inactiva
        nPin1 = #agim0019#4;
        aPin1 = #agim0019#5;
    |FINSI;
    |CIERRA #agim0019;

    |ABRE #agifigen;
    #agifigen#0 = #900#0; || Empresa
    |LEE 041#agifigen.=;
    |SI FSalida = 0;
        efPn10  = #agifigen#93;
        efPn11  = #agifigen#94;
        nPin1   = #agifigen#87;
        aPin1   = #agifigen#88;
        #900#1 = #agifigen#13;  || Nif
        #900#2 = #agifigen#1;   || Nombre
    |FINSI;
    |CIERRA #agifigen;
    |SI efPn10 [ "01.01.1900"; efPn10 = "01.01.0000"; |FINSI;
    |SI efPn11 [ "01.01.1900"; efPn11 = "01.01.0000"; |FINSI;
    #900#3 = nPin1;
    #900#4 = aPin1;
    #900#9 = efPn10;
    #900#10 = efPn11;

    #900#11 = "ACTIVA";
    |SI #999#12 = "N"; #900#11 = "INACTIVA"; |FINSI;
    #900#12 = #0#1;                                   || Ejercicio
    #900#13 = efDFecha;
    #900#14 = efHFecha;

    #900#50 = #0#45; ||Titulo del Libro
|FPRC;
