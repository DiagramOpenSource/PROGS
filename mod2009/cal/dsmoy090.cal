|FICHEROS;
   dsmoy090 #0;

   dsmom090 #90;
   dsmom091 #91;
   dsmom092 #92;

   :/basica/def/agicen14 #114;
   :/basica/def/agim0019 #119;

   dsmow030 #900;
|FIN;

|VARIABLES;
   &entrada = 1;

   swBueno    = 0;
   &swerror   = 0;
   nCampo     = 0;
   aAlfa1     = "";
   aAlfa2     = "";
   informe    = "";
   inform1    = "dsmoy090";
   inform2    = "dsmoyn90";

   &enDMaqu   = 0;
   &enHMaqu   = 0;

   nDEmpresa   = 0;
   nHEmpresa   = 0;
   nDActividad = 0;
   nHActividad = 0;
   nLaEmpresa  = 0;
   &nCamSel    = 21;
   &nCamTip    = 34;
   nSw = 0;

   &eaTAlta = "";
   &eaTBaja = "";
   nTMov    = 0;
   aTMov    = "";
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varlib;
|INCLUYE i_selcon;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\
|CALCULO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#5  = enEmpresa;  |PINTA #0#5;
         #0#6  = enEmpresa;  |PINTA #0#6;

         |C_M #0#5, 1, "N";
         |C_M #0#6, 1, "N";
     |FINSI;
|FCAL;

|CALCULO Parrilla;
     |SI #0#5 = 0;
         #0#5 = 0;  |PINTA #0#5;
         #0#6 = 0;  |PINTA #0#6;  |C_M #0#6, 1, "N";
         |PARA nCampo = 7;  |SI nCampo [ 18;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#5, 1, "S";
             |C_M #0#6, 1, "S";
         |FINSI;

         |PARA nCampo = 7;  |SI nCampo [ 18;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 18;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 18;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|| **************************************************************
|| //////////////////// Procesos del Informe  \\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO imprelin;
 nSw = 1;
 |PRINT;
|FPRC;

|DEFBUCLE LasLineas;
 #91#1;
 ;
 #90#0, #90#1, 001;
 #90#0, #90#1, 999;
 e;
 NULL, imprelin;
|FIN;

|PROCESO LaImpresion;
   |SI nLaEmpresa ! #90#0;
       |SI nLaEmpresa ! -1;
           |SI #0#4 = "I"; |PIEINF; nSw = 0; |FINSI;
       |FINSI;
       nLaEmpresa = #90#0;
       enEmpresa  = #90#0;
       swBueno = 0;
       |HAZ LaSelEmpresa;
       |SI enSwSelEmp = 1;  swBueno = 1;  |FINSI;
   |FINSI;
   |SI swBueno = 1; |ACABA; |FINSI;

   |HAZ LeeDatosEmpresa;

   |SI #0#2 = "A";
       |SI #90#17 ] "01.01.1000"; |ACABA; |FINSI;
   |FINSI;
   |SI #0#2 = "B";
       |SI #90#17 < "01.01.1000"; |ACABA; |FINSI;
   |FINSI;

   nTMov = #90#9; |HAZ VerTipos; eaTAlta = aTMov;
   eaTBaja = "";
   |SI #90#17 > "01.01.1000";
       nTMov = #90#18;
       |HAZ VerTipos;
        eaTBaja = aTMov;
   |FINSI;

   |SI #0#4 = "F";
       |BUCLE LasLineas;
       |PIEINF;
        nSw = 0;
   |SINO;
       |PRINT; nSw = 1;
   |FINSI;
|FPRC;

|DEFBUCLE dsmoy090;
   #90#1;
   20;
   nDEmpresa,#0#0,nDActividad;
   nHEmpresa,#0#1,nHActividad;
   e;
   NULL, LaImpresion;
|FIN;

|PROCESO imprimir;
      nLaEmpresa = -1;
      swBueno = 0;
      |BUCLE dsmoy090;
|FPRC;

|CALCULO Tipo3;  |TIPO 3;

    |IMPRE 0;
    |SI FSalida ! 0;  |ACABA;  |FINSI;

    informe = inform1;
    |SI #0#4 = "I"; informe = inform2; |FINSI;

    |INFOR informe;
    |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

     nDActividad = #0#19;
     nHActividad = #0#20;
     nSw = 0;
     |SI #0#5 ! 0;
         nDEmpresa = #0#5;
         nHEmpresa = #0#6;
         |HAZ imprimir;
     |SINO;
         |PARA nCampo = 7; |SI nCampo [ 18;  |HACIENDO nCampo = nCampo + 1;
               nDEmpresa = #0nCampo;
               nHEmpresa = #0nCampo;
               |HAZ imprimir;
         |SIGUE;
     |FINSI;
     |SI nSw = 1;
         |PIEINF;
     |FINSI;
     |FININF;
     |FINIMP;
|FCAL;

|PROGRAMA;
 |HAZ LeeEURODBMOD;
 |SI enEmpresa ! 0;
     |DDEFECTO #0;
     #0#4 = "I";
     |SI enHMaqu = 0;
         enHMaqu = enDMaqu;
         #0#4 = "F";
     |FINSI;
     #0#0 = enDMaqu;
     #0#1 = enHMaqu;
     #0#2 = "T";
     #0#5 = enEmpresa;
     #0#6 = enEmpresa;
     #0#21 = "S";
     |HAZ Tipo3;
 |SINO;
     |CLS;
     |MANTE #0;
 |FINSI;
|FPRO;

|PROCESO LeeDatosEmpresa;
  |DDEFECTO #900;
  #900#0 = enEmpresa;
  eaInac = #0#21;
  eaTituloL = "";
  enActividad = #90#20;
  |HAZ DatosEmpr_w30;
|FPRC;


|PROCESO VerTipos;
   aTMov = "";
   |SI nTMov = 0; aTMov = "Cuota tributaria devengada en el ejercicio     "; |FINSI;
   |SI nTMov = 1; aTMov = "Maquina autorizada en a¤os anteriores          "; |FINSI;
   |SI nTMov = 2; aTMov = "Maquina de nueva autorizacion                  "; |FINSI;
   |SI nTMov = 3; aTMov = "Modificacion del precio autorizado por partida "; |FINSI;
   |SI nTMov = 4; aTMov = "Alta por sustitucion                           "; |FINSI;

   |SI nTMov = 5; aTMov = "Suspension temporal de la autorizacion         "; |FINSI;
   |SI nTMov = 6; aTMov = "Extincion o revocacion de la autorizacion      "; |FINSI;
   |SI nTMov = 7; aTMov = "Baja definitiva de la maquina sin sustitucion  "; |FINSI;
   |SI nTMov = 8; aTMov = "Baja definitiva de la maquina con sustitucion  "; |FINSI;
   |SI nTMov = 9; aTMov = "Otros                                          "; |FINSI;
|FPRC;
