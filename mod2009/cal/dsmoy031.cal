|FICHEROS;
     dsmoy031 #0;

     dsmom080 #80;
     dsmom081 #81;
     dsmom082 #82;
     dsmom083 #83;

     dsmow082 #982;
     dsmow083 #983;

     :/basica/def/agicen14 #114;
     :/basica/def/agifigen #118;
     :/basica/def/agim0019 #119;
     :/contagen/def/canempre #330;

     dsmow032 #900;
|FIN;

|VARIABLES;
     aParam = "";
     aPar1  = "";
     aPar2  = "";

     nDEmpresa  = 0;
     nHEmpresa  = 0;
     aDNom      = "";
     aHNom      = "";
     nEjer      = 0;
     nDPeriodo  = 0;
     nHPeriodo  = 0;
     nCampo     = 0;

     nSwAlgun    = 0;
     nSwAlg      = 0;
     aAlfa       = "";
     aAlfa1      = "";
     aAlfa2      = "";
     aAlfa3      = "";
     aAlfa4      = "";
     fLaFecha    = @;

     informe     = "";
     inform1 = "dsmow082";
     inform2 = "dsmow084";
     inform3 = "dsmoy031";

     nNume1      = 0;
     nNume2      = 0;
     nNume3      = 0;

     nSwSal      = 0;
     &nSwLinea   = 0;
     &eaCam1     = "";
     &eaCam2     = "";
     &eaCam3     = "";
     &eaCam31    = "";
     &enCam4     = "";
     &enCam41    = "";
     &eaCa4      = "";
     &eaCam8     = "";
     &enCam10    = 0;
     &enCam11    = 0;
     &enCam12    = 0;
     &enCam13    = 0;
     &eaCam15    = "";
     &eaTit      = "";
     &enEj82     = "";
     &eaTex      = "";
     &enC1       = 0;
     &enC2       = 0;
     &enC3       = 0;
     &enC4       = 0;
     &enCT1      = 0;
     &enCT2      = 0;

    nPin1    = 0;
    aPin1    = "";
    efPn10   = @;
    efPn11   = @;
    swExiste = 0;
    swNoExis = 0;
    nExiFin  = 0;
    efDFecha = @;
    efHFecha = @;
|FIN;

|INCLUYE i_variar;

|| /////////////////////// PROCESOS ENTRADA POR PANTALLA \\\\\\\\\\\\\\\\\\\\
|CALCULO Eano; |TIPO 7;
 |SI enEmpresa = 0;
     fLaFecha = ~;
     aAlfa1 = fLaFecha; aAlfa1 = aAlfa1 % -104;
     #0#3 = aAlfa1;
     |PINTA #0#3;
 |SINO;
     |PTEC 802;
 |FINSI;
|FCAL;

|PROCESO LaEmpresa7; |TIPO 7;
 |SI enEmpresa ! 0;
     |PTEC 802;
     |SI Campo = 21; enEmpresa = 0; |FINSI;
 |FINSI;
|FPRC;

|PROCESO Parrilla;
     |SI #0#4 = 0;
         #0#4 = 0;  |PINTA #0#4;
         #0#5 = 0;  |PINTA #0#5;  |C_M #0#5, 1, "N";
         |PARA nCampo = 6;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#4, 1, "S";
             |C_M #0#5, 1, "S";
         |FINSI;

         |PARA nCampo = 6;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|| /////////////////////// PROCESOS DE TRABAJO \\\\\\\\\\\\\\\\\\\\
|PROCESO ElComun;
       #983#2  = 99;
       #983#3  = "IVA DEDUCIBLE OPERACIONES COMUNES ";
       #983#4  = "";
       #983#5  = #982#18;
       #983#6  = #982#5;
       #983#7  = #982#6;
       #983#8  = #982#7;
       #983#9  = #982#18;
       #983#10 = "X";
       #983#11 = "COMUN";
       #983#15 = #982#9;

       #900#15 = 99;
       #900#16 = #983#3;

       |ABRE #agicen14;
       #114#0 = enEmpresa; || Empresa
       #114#1 = 00;
       |LEE 041#114];
       |SI FSalida = 0; |Y #114#0 = enEmpresa;
           #900#17 = #114#3;   ||Epigrafe
           #900#18 = #114#4;   ||Descripcion Actividad
           #900#19 = #118#93;   ||Fecha Inicio
           #900#20 = #118#94;   ||Fecha Fin
           #900#21 = #114#11;  ||Sector actividad
           #900#22 = #114#12;  ||Sector actividad
           #900#23 = #114#16;  ||Tipo Actividad
           #900#24 = #114#17;  ||Descripcion Tipo Actividad
           #900#51 = #114#20;  ||CNAE
       |FINSI;
       |CIERRA #114;
|FPRC;

|PROCESO LosDatosAct;
    |ABRE #114;
    #114#0 = enEmpresa;
    #114#1 = #81#2;
    |LEE 001#114=;
    |SI FSalida = 0;
        #983#3 = #114#4;
        #983#4 = #114#20;
        #983#12 = #114#1;
        #983#13 = #114#20;

        #983#14 = #114#7;
        #983#16 = "Z";
        #983#19 = #118#87;
        #983#20 = #118#88;
        #983#21 = #114#9;
        #983#22 = #114#10;
        #983#23 = #114#16;
        #983#24 = #114#17;
        |SI #114#5 > "01.01.0000"; #983#25 = #114#5; |FINSI;
        |SI #114#6 > "01.01.0000"; #983#26 = #114#6; |FINSI;
        #983#27 = #114#8;
        #983#30 = #114#15;
        #983#31 = #114#3;

        #900#15 = #114#1;   ||Act.
        #900#16 = #114#2;   ||Tipo Epig.
        #900#17 = #114#3;   ||Epigrafe
        #900#18 = #114#4;   ||Descripcion Actividad
        #900#19 = #114#5;   ||Fecha Inicio
        #900#20 = #114#6;   ||Fecha Fin
        #900#21 = #114#11;  ||Sector actividad
        #900#22 = #114#12;  ||Sector actividad
        #900#23 = #114#16;  ||Tipo Actividad
        #900#24 = #114#17;  ||Descripcion Tipo Actividad
        #900#51 = #114#20;  ||CNAE
   |FINSI;
   |CIERRA #114;
|FPRC;

|PROCESO LeoInfw83;
 |DDEFECTO #983;

 |PDEFECTO #900, 15, 25; #900#51 = "";

 #983#0 = enEjer;
 #983#1 = enEmpresa;
 #983#2 = #81#2;
 |SI #81#2 ! 99;
     |HAZ LosDatosAct;
 |SINO;
     |HAZ ElComun;
 |FINSI;
 #983#10 = #81#40;
 |SI #983#10 = "O"; #983#11 = "OTRA"; |FINSI;
 |SI #983#10 = "P"; #983#11 = "PRINCIPAL"; |FINSI;
 |SI #983#10 = "C"; #983#11 = "COMPLEMENTARIA"; |FINSI;
 #983#4 = #81#6;
 #983#6 = #81#7;
 #983#7 = #81#8;
 #983#8 = #81#9;
 #983#9 = #81#6;
 #983#15 = #81#10;
 #983#28 = #81#12;
 #983#29 = #81#29;

 eaCa4 = #81#4;
 |PRINT;
 nSwAlg = 1;
 |PIEINF;
|FPRC;

|DEFBUCLE LeoInf81;
 #81#1;
 ;
 enEmpresa, nEjer, 01;
 enEmpresa, nEjer, 99;
 ;
 NULL, LeoInfw83;
|FIN;

|PROCESO LeoInfw82;
  |PRINT;
|FPRC;

|DEFBUCLE LeoInf82;
 #82#1;
 ;
 enEmpresa, nEjer, 01;
 enEmpresa, nEjer, 99;
 ;
 NULL, LeoInfw82;
|FIN;

|PROCESO LEmpresa;
  nSwSal = 0;
  |SI #0#4 = 0;
      nSwSal = 1;
      |PARA nCampo = 6;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
            |SI #0nCampo ! 0;
                |SI #0nCampo = #118#0;
                    nSwSal = 0;
                    |SAL;
                |FINSI;
            |FINSI;
      |SIGUE;
  |FINSI;
  |SI nSwSal = 1; |ACABA; |FINSI;

  enEmpresa = #118#0;

  eaCam1  = #118#0; |QBF eaCam1;
  eaCam1  = ("00000" + eaCam1) % -105;
  eaCam2  = #118#1;
  eaCam15  = #118#13;

  enC1 = 0; enC2 = 0;
  enC3 = 0; enC4 = 0;
  enCT1 = 0;
  enCT2 = 0;

  |DDEFECTO #80;
  #80#0 = enEmpresa;
  #80#1 = enEjer -1;
  |LEE 001#80=;
  enC1 = #80#6; enC2 = #80#7;
  enC3 = #80#9; enC4 = #80#43;
  enCT2 = enC1 + enC2 + enC3 + enC4;

  nEjer = enEjer;
  |DDEFECTO #80;
  #80#0 = enEmpresa;
  #80#1 = enEjer;
  |LEE 001#80=;
  |SI FSalida ! 0;
      |ACABA;
  |FINSI;
  enCT1 = #80#6 + #80#7 + #80#9 + #80#43;

  nNume1 = 4; nNume2 = 5; nNume3 = 32;
  |SI aPar2 = "P";
      nNume1 = 2; nNume2 = 3; nNume3 = 31;
  |FINSI;

  nSwLinea  = 0;
  eaCam3    = "ESPECIAL";
  enCam4    = #80nNume3;
  |SI #80nNume1 ! "E"; eaCam3 = "GENERAL "; |FINSI;

  enCam4 = enCam4 + "%";
  enCam4 = ("     " + enCam4) % -104;
  |SI aPar2 = "I";
      eaCam31 = "ESPECIAL";
      enCam41 = #80#31;
      |SI #80#2 ! "E"; eaCam31 = "GENERAL "; |FINSI;
      enCam41 = enCam41 + "%";
      enCam41 = ("     " + enCam41) % -104;
 |FINSI;

 eaProrrat = #80nNume1;
 nProrrata = #80nNume2;
 aAlfa     = #80nNume3;
 |SI aPar2 = "P";
     nEjer = enEjer;
     #80#0 = enEmpresa;
     #80#1 = nEjer;
     |LEE 001#80=;
     |SI FSalida ! 0;
         |DDEFECTO #80;
     |FINSI;
 |FINSI;

  enCam10   = #80#6;
  enCam11   = #80#9;
  enCam13   = #80#7;
  enCam12   = #80#15;

  |SI aParam ! "FI";
      |SI nProrrata = 100; || ... ... |Y #80#9 = 0; |Y #80#15 = 0;
          |ACABA;
      |FINSI;
  |SINO;
      |SI #80#13 = 0; |Y #80#11 = 0;
          |ACABA;
      |FINSI;
  |FINSI;

  |SI aParam = "FI";
       |INFOR informe;
       |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;
  |FINSI;
  |SI aPar1 = "F";
      |DDEFECTO #982;
      #982#0 = enEjer;      #982#1 = enEmpresa;
      #982#2 = eaCam2;      #982#3 = eaProrrat;
      #982#4 = nProrrata;   #982#5 = #80#6;
      #982#6 = #80#7;       #982#7 = #80#8;
      #982#9 = #80#9;       #982#13 = #80#11;
      #982#15 = eaCam15;    #982#16 = #80#15;
      #982#17 = #80#16;     #982#18 = aAlfa;

      |HAZ DatosEmpresa;
      |SI aPar2 ! "I"; |BUCLE LeoInf81; |FINSI;
      |SI aPar2 = "I";
          |BUCLE LeoInf82;
          |PIEINF;
      |FINSI;
      |SI aParam = "FI";
          |FININF;
          |SI #0#22 = "S";
              enPeriodo = -1;
              |ABRE #330;
              |SELECT #4#330;
              #330#2  = enEmpresa;
              aAlfa1 = #0#3;
              aAlfa1 = aAlfa1 % -102;
              nNume1 = aAlfa1;
              #330#26 = nNume1;
              |LEE 000#330=;
              |SI FSalida = 0;
                  enPeriodo = #330#3;
              |FINSI;
              |SELECT #1#330;
              |CIERRA #330;
              |SI enPeriodo ! -1;
                  |SUB_EJECUTA_NP ":contagen/cay3ivas;modelosR";
                  || ... |SUB_EJECUTA_NP ":contagen/cay3ivas;modelosS"; no quiere soportado 28.12.05
              |FINSI;
          |FINSI;
      |FINSI;
  |SINO;
      |PRINT;
      nSwAlgun = 1;
  |FINSI;
|FPRC;


|DEFBUCLE agifigen1;
 #118#2;
 0;
 aDNom, nDEmpresa;
 aHNom, nHEmpresa;
 e;
 NULL, LEmpresa;
|FIN;

|DEFBUCLE agifigen;
 #118#1;
 ;
 nDEmpresa;
 nHEmpresa;
 e;
 NULL, LEmpresa;
|FIN;


|PROCESO Tipo3;  |TIPO 3;

     |HAZ LeeCtrlProrr;

     aDNom = " " * 40;  aHNom = "z" * 40;
     nDPeriodo = 01;    nHPeriodo = 99;
     enEjer   = #0#3;
     aAlfa    = enEjer;
     aAlfa1   = "01.01." + aAlfa;
     aAlfa2   = "31.12." + aAlfa;
     efDFecha = aAlfa1;
     efHFecha = aAlfa2;

     eaTit  = "DEFINITIVA";
     |SI aPar2 = "P";
         eaTit = "PROVISIONAL";
         eaTex = "Datos Economicos del Ejercicio Anterior";
     |FINSI;
     enEj82 = enEjer;

     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI aParam ! "FI";
         |INFOR informe;
         |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;
     |FINSI;

     nDEmpresa = 00001;
     nHEmpresa = 99999;
     |SI #0#4 ! 0;
         nDEmpresa = #0#4;
         nHEmpresa = #0#5;
     |FINSI;

     nSwAlgun = 0;
     |ABRE #80;
     |SI #0#21 = "C"; |BUCLE agifigen; |FINSI;
     |SI #0#21 = "N"; |BUCLE agifigen1; |FINSI;
     |CIERRA #80;

     |SI nSwAlgun = 1; |Y aParam ! "FI";
         |PIEINF;
     |FINSI;
     |SI aParam ! "FI";
         |FININF;
     |FINSI;
     |FINIMP;

|FPRC;
|| ************************************************************

|PROGRAMA;
  aParam = PARAMETRO; |QBF aParam; aParam = aParam > " ";
  |SI aParam = ""; aParam = "CD"; |FINSI;

  aPar1 = aParam % 101;
  aPar2 = aParam % 201;

  informe = inform1;   || aParam = "CD";
  aAlfa1 = "Informe Calculo Definitivo";
  aAlfa2 = "Informe Gral Empresas con Calculo de Porcentaje de Prorrata Definitiva";
  |SI aParam = "CP";
      informe = inform1;
      aAlfa1 = "Informe Calculo Provisional";
      aAlfa2 = "Informe Gral Empresas con Calculo de Porcentaje de Prorrata Provisional";
  |FINSI;
  |SI aParam = "FD";
      informe = inform2;
      aAlfa1 = "Ficha Calculo Definitivo";
      aAlfa2 = "Ficha de Empresas con Calculo de Porcentaje de Prorrata Definitiva";
  |FINSI;
  |SI aParam = "FP";
      informe = inform2;
      aAlfa1 = "Ficha Calculo Provisional";
      aAlfa2 = "Ficha de Empresas con Calculo de Porcentaje de Prorrata Provisional";
  |FINSI;
  |SI aParam = "FI";
      informe = inform3;
      aAlfa1 = "Ficha de la Prorrata Aplicada";
      aAlfa2 = "Ficha de Empresas con la Prorrata Aplicada";
  |FINSI;


  |CLS;
  |CABEZA aAlfa1;

  |PINPA #0#0;
  |SI aParam = "FI";
      |BLIN 21;
      |CUADRO 1709 2273;
      |PINTA 2111, "Resumen Totales Conceptos ..: ";
      |C_V #0#22,1,"S"; |C_M #0#22,1,"S";
      #0#22 = "S";
  |FINSI;
  |ATRI R;
  |PINTA 0706, aAlfa2;
  |ATRI 0;
  |SI enEmpresa ! 0;
      #0#3 = enEjer;
      #0#4 = enEmpresa;
      #0#5 = enEmpresa;
  |FINSI;

  aAlfa2 = aAlfa2 > " ";
  #0#20  = aAlfa2;
  |PINDA #0#0;
  |ENDATOS #1#0;
|FPRO;

|PROCESO DatosEmpresa;

  |DDEFECTO #900;

  #900#0 = enEmpresa;
  nPin1 = 0;
  aPin1 = "";
  efPn10 = "01.01.0000";
  efPn11 = "01.01.0000";

  || Lee Primera Actividad
    |ABRE #agicen14;
    #agicen14#0 = #900#0; || Empresa
    #agicen14#1 = 00;
    |LEE 041#agicen14.];
    |SI FSalida = 0; |Y #agicen14#0 = #900#0;
        #900#5 = #agicen14#9;
        #900#6 = #agicen14#10;
        #900#7 = #agicen14#7;
        #900#8 = #agicen14#8;
        efPn10 = #agicen14#5;
        efPn11 = #agicen14#6;
    |FINSI;
    |CIERRA #agicen14;

 || Lee Ficha Sociedades
    swExiste = 0;
    |ABRE #agim0019;
    #agim0019#0 = #900#0; || Empresa
    #agim0019#1 = 99;
    |LEE 041#agim0019.];
    |SI FSalida = 0;
        |SI #agim0019#0 ! #900#0;
            |LEE 041#agim0019.a;
            |SI FSalida = 0; |Y #agim0019#0 = #900#0;
                swExiste = 1;
            |FINSI;
        |SINO;
            swExiste = 1;
        |FINSI;
    |FINSI;
    |SI swExiste = 1;
        efPn10 = #agim0019#7;      || Fecha Activa
        efPn11 = #agim0019#55;     || Fecha Inactiva
        nPin1 = #agim0019#4;
        aPin1 = #agim0019#5;
    |FINSI;
    |CIERRA #agim0019;

        efPn10  = #agifigen#93;
        efPn11  = #agifigen#94;
        nPin1   = #agifigen#87;
        aPin1   = #agifigen#88;
        #900#1 = #agifigen#13;  || Nif
        #900#2 = #agifigen#1;   || Nombre

    |SI efPn10 [ "01.01.1900"; efPn10 = "01.01.0000"; |FINSI;
    |SI efPn11 [ "01.01.1900"; efPn11 = "01.01.0000"; |FINSI;
    #900#3 = nPin1;
    #900#4 = aPin1;
    #900#9 = efPn10;
    #900#10 = efPn11;

    #900#11 = "ACTIVA";
    |SI #900#10 > "01.01.1900"; #900#11 = "INACTIVA"; |FINSI;
    #900#12 = #0#3;                                   || Ejercicio
    #900#13 = efDFecha;
    #900#14 = efHFecha;

    #900#50 = #0#20; ||Titulo del Libro
|FPRC;
