|FICHEROS;
   dsmoy009 #0;

   :/basica/def/agicen14 #114;
   :/basica/def/agifigen #118;
   :/basica/def/agim0019 #119;
   :/basica/def/agitab99 #99;

   :/contagen/def/canempre #300;
   :/contagen/def/caivaasi #301;
   :/contagen/def/camaniva #304;
   :/contagen/def/caivalin #305;
   :/contagen/def/caconimp #306;  || configuracion impresiones

   dsmom030 #30;
   dsmow030 #900;          ||Def de impresion
   dsmow031 #901;
   dsmow049 #998,MANTE;
   dsmow043 #999,MANTE;
|FIN;

|VARIABLES;
   &entrada = 1;
   informe = "";

   &aTipo      = "DE COMPRA";
   nInkey      = 0;
   aFichero    = "";
   nCampo      = 0;
   nDEmpresa   = 0;
   nHEmpresa   = 0;
   nDActividad = 0;
   nHActividad = 0;
   nLaEmpresa  = 0;
   nGTempo     = 0;

   nSPin1      = 0;
   aPath       = "";
   nEjercicio  = 0;

   aAlfa1 = "";
   aAlfa2 = "";
   aAlfa3 = "";
   nPara1 = 0;
   nPara2 = 0;
   nNume1 = 0;
   nNume2 = 0;
   nNume3 = 0;
   nNume4 = 0;
   nNume5 = 0;
   nNume6 = 0;

   aDTipo = "";
   aHTipo = "";
   SwHayDatos = 0;

   fDFecha   = @;
   fHFecha   = @;
   fLaFecha  = @;
   &nFactura = 0;
   &nPagina  = 0;
   &nPagDup  = 0;
   &nSwPagDup = 0;
   &SwPrim   = 0;
   &SwLinea  = 0;
   &nLBase   = 0;
   &nLTipoI  = 0;
   &nLCuoI   = 0;
   &nLCuoIN  = 0;
   &nLTipoR  = 0;
   &nLCuoR   = 0;
   &nLCuoRN  = 0;

   &nLBasP   = 0;
   &nLTipP   = 0;
   &nLCuoP   = 0;

   nSwReten = 0;
   nElDepar = 0;
   nEstado = 0;

   aParam       = "";
   &enEjercicio = 0;
   &eaLegEmp    = "";
   &eaLegPer    = "";
   Comodin      = "";
   Comodin1     = "";
   aAlfa        = "";
   Handle       = 0;
   Numeracion   = 1;
   &efFecLeg    = @;

   nSigFactu    = 0;
   aClave       = "";
   aNomModelo   = "";
   &enBasImp    = 0;
   &eaNomImp    = "";

   nOpMdep      = 0;
   nAlSi        = 0;
   nAlNo        = 0;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;
|INCLUYE i_varlib;
|INCLUYE i_caliva;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\
|CALCULO Eano; |TIPO 7;
 |SI nEstado ! 0;
     fLaFecha = ~;
     aAlfa1 = fLaFecha; aAlfa1 = aAlfa1 % -104;
     #0#1  = aAlfa1; |PINTA #0#1;
 |FINSI;
 #0#52 = "LIBRO REGISTRO DE FACTURAS RECIBIDAS";
 |SI nSigFactu = 1;
     #0#52 = "LIBRO REGISTRO DE FACTURAS RECTIFICATIVAS RECIBIDAS ";
     |ATRI R; |PINTA 0531,"Emision Libro Reg.Fras Rectificativas Recibidas"; |ATRI 0;
 |FINSI;
 |SI aNomModelo ! "";
     #0#52 = aNomModelo;
 |FINSI;
 |PINTA #0#52;
|FCAL;

|CALCULO losDefectos; |TIPO 0;
 aAlfa1 = #0#1;
 aAlfa2 = #0#41; aAlfa2 = aAlfa2 % -104;
 |SI aAlfa1 ! aAlfa2; |O nEstado ! 0;
     aAlfa2 = "01.01." + aAlfa1; #0#41 = aAlfa2; |PINTA #0#41;
     aAlfa2 = "31.12." + aAlfa1; #0#42 = aAlfa2; |PINTA #0#42;
 |FINSI;
|FCAL;

|CALCULO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#2  = enEmpresa;  |PINTA #0#2;
         #0#3  = enEmpresa;  |PINTA #0#3;

         |C_M #0#2, 1, "N";
         |C_M #0#3, 1, "N";
     |FINSI;
|FCAL;

|CALCULO Parrilla; |TIPO 0;
     |SI #0#2 = 0;
         #0#2 = 0;  |PINTA #0#2;
         #0#3 = 0;  |PINTA #0#3;  |C_M #0#3, 1, "N";
         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#2, 1, "S";
             |C_M #0#3, 1, "S";
         |FINSI;

         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas; |TIPO 0;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO YaSeCual; |TIPO 7;
 |SI enEmpresa ! 0;
     #0#54 = "S";
     #0#24 = "S"; |PINTA #0#24;
     #0#25 = 00;  |PINTA #0#25;
     #0#26 = 99;  |PINTA #0#26;
     |C_M #0#24, 1, "N";
     |C_M #0#25, 1, "N";
     |C_M #0#26, 1, "N";
     |C_M #0#54, 1, "N";
 |FINSI;
|FCAL;

|CALCULO SelTipo0; |TIPO 0;
 |SI #0#54 = "N";
     #0#25 = 0;   |PINTA #0#25;  |C_M #0#25, 1, "N";
     #0#26 = 99;  |PINTA #0#26;  |C_M #0#26, 1, "N";
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |SINO;
     |SI enEmpresa = 0;
         |C_M #0#25, 1, "S";
         |C_M #0#26, 1, "S";
     |FINSI;
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "N";
           #0nCampo = 0;  |PINTA #0nCampo;
     |SIGUE;
 |FINSI;
|FCAL;

|CALCULO SelTipo1;
 |SI #0Campo = 0;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           #0nCampo = 0;  |C_M #0nCampo, 1, "N";
     |SIGUE;
 |SINO;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |FINSI;
|FCAL;

|CALCULO Cuadro; |TIPO 7;
   |PUSHV  1012 1770;
   |CUADRO 1012 1770;
   |ATRI R;
   |PINTA 1013, " ------------ FORMATO LIBRO FACTURAS ------------------- ";
   |PINTA 1113, " ( 1) Descr. Fra., Nombre y NIF, una linea s/recargo     ";
   |PINTA 1213, " ( 2) Descr. Fra., Nombre y NIF, varias lineas s/recargo ";
   |PINTA 1313, " ( 3) Descr. Fra., Nombre y NIF, una linea               ";
   |PINTA 1413, " ( 4) Descr. Fra., Nombre y NIF, varias lineas           ";
   |PINTA 1513, " ( 5) Nombre y NIF, varias lineas                        ";
   |PINTA 1613, " ( 6) Nombre y NIF, varias lineas con rayas              ";
   |ATRI r;
|FCAL;

|CALCULO SalCuadro; |TIPO 0;
   |POPV;
   |SI #0#43 = 6;
       #0#53 = "N"; |C_M #0#53,1,"N"; |PINTA #0#53;
   |SINO;
       |C_M #0#53,1,"S";
   |FINSI;
|FCAL;

|CALCULO fecha; |TIPO 0;
 aAlfa1 = #0#1;
 aAlfa2 = "01.01." + aAlfa1;
 aAlfa3 = "31.12." + aAlfa1;
 |SI #0Campo < aAlfa2; |O #0Campo > aAlfa3;
     |MENAV "    Error. Fecha Incorrecta ";
     |ERROR;
 |SINO;
     |SI Campo = 42; |Y #0#41 > #0#42;
         |MENAV "    Error. Fecha Incorrecta ";
         |ERROR;
     |FINSI;
 |FINSI;
|FCAL;

|CALCULO QueCalculo; |TIPO 0;
 |C_M #0#51,1,"S";
 |SI #0Campo = "N";
     |C_M #0#51,1,"N";
     #0#51 = 0; |PINTA #0#51;
 |SINO;
     |C_M #0#51,1,"S";
     #0#51 = 1; |PINTA #0#51;
 |FINSI;
|FCAL;

|CALCULO SelAct; |TIPO 0;
 aAlfa1 = #0Campo;
 |C_M #0#56,1, aAlfa1;
 |C_M #0#57,1, aAlfa1;
|FCAL;

|| **********************************************************************************
|| **********************************************************************************
|| ....................... Situa la Empresa Contable
|PROCESO LEmpresaConta;

 enEmpresa  = #canempre#2;
 enPerConta = #canempre#3;;   || Periodo Contable

 |SI #0#24 = "N"; |Y #canempre#21 = "I"; |ACABA; |FINSI;

|| ... Seleccion de Tipo de Empresa
  |ABRE #agifigen;
  #agifigen#0 = enEmpresa;
  |LEE 041#agifigen.=;
  |SI FSalida ! 0;
      |DDEFECTO #agifigen;
  |FINSI;
  |CIERRA #agifigen;
  nSPin1 = #agifigen#87;

  enSwSelEmp = 1;
  |SI #0#54 = "S";
      |SI nSPin1 ] #0#25; |Y nSPin1 [ #0#26;
          enSwSelEmp = 0;
      |FINSI;
  |SINO;
      |SI nSPin1 = #0#27;
          enSwSelEmp = 0;
      |SINO;
          |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
                |SI nSPin1 = #0nCampo; |Y nSPin1 ! 0;
                    enSwSelEmp = 0;
                   |SAL;
                |FINSI;
          |SIGUE;
      |FINSI;
  |FINSI;
  |SI enSwSelEmp = 1; |ACABA; |FINSI;
|| ------------------------------------------------------------------------
   aAlfa1 = enEmpresa;  aAlfa1 = ("00000" + aAlfa1) % -105;
   aAlfa2 = enPerConta; aAlfa2 = ("0" + aAlfa2) % -101;
   aPathConta  = aPath + "contagen/fich/" + aAlfa1 + aAlfa2 + "/";

   nGTempo = nGTempo + 1;
   #999#0 = nGTempo;       || Guia
   #999#1 = enEmpresa;     || Empresa
   #999#2 = enPerConta;    || Periodo Contable
   #999#3 = enEjer;        || A¤o
   #999#4 = #canempre#11;  || Desde Mes
   #999#5 = #canempre#13;  || N§ Digitos
   #999#6 = #canempre#14;  || Digitos Nivel 1
   #999#7 = #canempre#15;  || Digitos Nivel 2
   #999#8 = #canempre#16;  || Digitos Nivel 3
   #999#9 = #canempre#17;  || Digitos Nivel 4
   #999#10 = #canempre#18; || Digitos Nivel 5
   #999#11 = #canempre#19; || Digitos Nivel 6
   eaEstadoEmp = "S"; |SI #canempre#21 = "I"; eaEstadoEmp = "N"; |FINSI;
   #999#12 = eaEstadoEmp;  || Estado
   #999#13 = #canempre#28; || Moneda
   #999#14 = #canempre#1;  || Nombre Empresa
   #999#15 = aPathConta;   || Path
   #999#16 = #0#48;        || Emitir
   |GRABA 020#999n;

   |SI #0#50 = "S";
       |PARA nPara2 = 00; |SI nPara2 [ 99; |HACIENDO nPara2 = nPara2 + 1;
             #998#0  = enEmpresa;
             #998#11 = #0#49;
             #998#41 = nPara2;
             |LEE 101#998=;
             |SI FSalida = 0;
                 #998#2 = "S";
                 |SI #0#51 ! 0; #998#1 = #0#51; |FINSI;
                 |GRABA 020#998a;
             |FINSI;
       |SIGUE;
   |FINSI;
|FPRC;

|DEFBUCLE canempre;
 #300#4;
 ;
 nDEmpresa, nEjercicio;
 nHEmpresa, nEjercicio;
 ;
 NULL, LEmpresaConta;
|FIN;
||///////////////////////////////////////////////////////////////////
|CALCULO AceraDatos; |TIPO 0;
   |SI #998#2 = "S";
       #998#5 = 0; |PINTA #998#5; |C_M #998#5,1,"N";
       #998#6 = 0; |PINTA #998#6; |C_M #998#6,1,"N";
       #998#7 = 0; |PINTA #998#7; |C_M #998#7,1,"N";
       #998#8 = 0; |PINTA #998#8; |C_M #998#8,1,"N";
       #998#10 = 1; |PINTA #998#10; |C_M #998#10,1,"N";
       #998#39 = 0; |PINTA #998#39; |C_M #998#39,1,"N";
       #998#40 = 0; |PINTA #998#40; |C_M #998#40,1,"N";
       |PDEFECTO #998, 12, 39;
       |PARA nPara1 = 12; |SI nPara1 [ 26; |HACIENDO nPara1 = nPara1 + 1;
            #998nPara1 = 0; |PINTA #998nPara1;
            |C_M #998nPara1,1,"N";
       |SIGUE;
   |SINO;
       |C_M #998#5,1,"S"; |C_M #998#6,1,"S";
       |C_M #998#7,1,"S"; |C_M #998#8,1,"S";
       |C_M #998#39,1,"S"; |C_M #998#40,1,"S";
       |PARA nPara1 = 12; |SI nPara1 [ 26; |HACIENDO nPara1 = nPara1 + 1;
            |C_M #998nPara1,1,"S";
       |SIGUE;
   |FINSI;
|FCAL;

|CALCULO Datos7; |TIPO 7;
 |MENSA "    <F2> Modificar Datos de Acumulados Emision, <F3> Cambiar Emitir Libro S/N";
|FCAL;

|CALCULO DatosManip; |TIPO 11;
   nInkey = FSalida;
   |BLIN 04;
   |SI nInkey = 10; |Y #999#16 = "S";
       |SI #0#55 = "N";
           #998#0  = #999#1;  || Empresa
           #998#11 = #0#49;   || Modelo
           #998#41 = 00;      || Actividad
           |LEE 000#998=;
           |SI FSalida ! 0;
               |DDEFECTO #998;
               #998#0  = #999#1;
               #998#11 = #0#49;
               #998#41 = 00;
               |GRABA 020#998b;
           |FINSI;

           |PUSHV 0506 2375;
           |PINPA #0#998;
           |PINDA #0#998;
           |C_A #999#14,1,35;  |PINTA 0729, #999#14;  |C_A #999#14,1,80;

           |ENDATOS #1#998;
           |SI FSalida = 0;
               |GRABA 020#998a;
               |LIBERA #998;
           |FINSI;
           |POPV;
        |SINO;
           |MENAV "    NO SE PUEDE MODIFICAR. SELECCION POR ACTIVIDADES";
        |FINSI;
   |FINSI;
   |SI nInkey = 11;
       |SI #999#16 = "S";
           #999#16 = "N";
       |SINO;
           #999#16 = "S";
       |FINSI;
       |GRABA 020#999a;
   |FINSI;
|FCAL;

|RUTINA inferior;
   #999#0 = 00001;
|FRUT;

|RUTINA superior;
   #999#0 = 99999;
|FRUT;

||///////////////////////////////////////////////////////////////////
|CALCULO Tipo3;  |TIPO 3;
    #0#0  = "S";      ||Soportado
    |SI nSigFactu = 1; #0#0 = "s"; |FINSI;

    #0#49 = enModelo; ||Libro Oficial

    |HAZ LeeCtrlLibro;

    enEjer = #0#1;
    aAlfa1 = enEjer; |QBF aAlfa1;
    aAlfa1 = aAlfa1 % -102;
    nEjercicio = aAlfa1;
    |DBASS aPath; |QBT aPath;

    aFichero = ("2w" + Usuario) % 108;
    |NOME_DAT #901 aFichero;

    aFichero = ("5w" + Usuario) % 108;
    |NOME_DAT #999 aFichero;
    |ABRE #999;  |CIERRA #999;  |DELINDEX #999;
    eaFEmpresa = aFichero;

    |ABRE #998;
    |ABRE #999;
    nGTempo = 0;   ||Busco Empresas Contables
    |SI #0#2 ! 0;
        nDEmpresa = #0#2;
        nHEmpresa = #0#3;
        |BUCLE canempre;
    |SINO;
        |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
              nDEmpresa = #0nCampo;
              nHEmpresa = #0nCampo;
              |BUCLE canempre;
        |SIGUE;
    |FINSI;
    |CIERRA #998;
    |CIERRA #999;

    |SI nGTempo = 0;    || No existe ninguna empresa
        |SI aParam = "";
            |MENAV "    Error. No existe ninguna Empresa Contable dentro de la Seleccion ";
        |FINSI;
        |DELINDEX #999;
        |ACABA;
    |FINSI;

    |SI aParam = "";
        |PUSHV 0509 2375;
        |ABRE #998;
        |ENTLINEAL #1#999, -1, 2, 22, 1, inferior, superior;
        |CIERRA #998;
        |POPV;
    |FINSI;

    |HAZ Imprimir;

    |DELINDEX #901;
    |DELINDEX #999;
|FCAL;

|| *************************************************************************
|PROCESO LasReten;
 |SI enActividad ! 0;
     nElDepar = #304#10;
     |SI nElDepar = 0;
         nElDepar = 1;
     |FINSI;
     |SI enActividad ! nElDepar; |ACABA; |FINSI;
 |FINSI;
 |SI #camaniva#20 ! 0;
     nSwReten = 1;
     |ERROR10;
 |FINSI;
|FPRC;

|DEFBUCLE LasReten;
 #camaniva#1;
 36;
 #0#39,0000000001, aDTipo;
 #0#40,9999999999, aHTipo;
 ;
 NULL,LasReten;
|FIN;

|| *************************************************************************
|PROCESO VariasLineas;
 SwLinea = 0;
 nLBasP = #900#66;
 nLTipP = #900#67;
 nLCuoP = #900#68;
 |PARA nPara1 = 23; |SI nPara1 [ 27; |HACIENDO nPara1 = nPara1 + 1;
       nLBase = #900nPara1;
       nNume1 = nPara1 +  6; nLTipoI = #900nNume1;
       nNume2 = nPara1 + 11; nLCuoI  = #900nNume2;
       nNume3 = nPara1 + 17; nLTipoR = #900nNume3;
       nNume4 = nPara1 + 22; nLCuoR  = #900nNume4;
       nNume5 = nPara1 + 29; nLCuoIN = #900nNume5;
       nNume6 = nPara1 + 35; nLCuoRN = #900nNume6;
       |SI nLCuoI = 0; |Y nLCuoIN = 0; nLTipoI = 0; |FINSI;
       |SI nLCuoR = 0; |Y nLCuoRN = 0; nLTipoR = 0; |FINSI;
       |SI nLBase ! 0; |O nLCuoI ! 0; |O nLCuoR ! 0; |O nLCuoIN ! 0; |O nLCuoRN ! 0;
           |PRINT;
           SwLinea = 1;
       |FINSI;
 |SIGUE;
 |SI SwLinea = 0;
     |PRINT;
 |FINSI;
|FPRC;

|PROCESO LinLibro0;

  aAlfa1 = #caivalin#30; |QBF aAlfa1; |SI aAlfa1 = ""; #caivalin#30 = #camaniva#10; |FINSI;
  |SI enActividad ! 0;      ||quiere separar por actividades
      |SI aMulDep = "S";
          nElDepar = #caivalin#30;
          |SI nElDepar = 0;
              nElDepar = 1;
          |FINSI;
          |SI enActividad ! nElDepar; nAlNo = 1; |ACABA; |FINSI;
      |FINSI;
  |FINSI;
  nAlSi = 1;
  |SI nOpMdep = 0;  |ACABA;  |FINSI;  || Solo comprueba que exista

 #caivalin#6  = (#caivalin#6  / enConversor) ! EURODBMOD;
 #caivalin#8  = (#caivalin#8  / enConversor) ! EURODBMOD;
 #caivalin#10 = (#caivalin#10 / enConversor) ! EURODBMOD;
 #caivalin#20 = (#caivalin#20 / enConversor) ! EURODBMOD;
 #caivalin#22 = (#caivalin#22 / enConversor) ! EURODBMOD;

 |SI #caivalin#7 = 0;               ||  0%
     #900#27 = #900#27 + #caivalin#6;
     #900#28 = #900#28 + #caivalin#6;
 |SINO;
     #900#28 = #900#28 + #caivalin#6;
     |SI #caivalin#5 = "S";
          #900#39 = #900#39 + #caivalin#8;
          |SI #caivalin#7 = #900#29;         ||  4%
              #900#23 = #900#23 + #caivalin#6;
              #900#34 = #900#34 + #caivalin#8;
          |SINO;
              |SI #caivalin#7 = #900#30;           ||  7%
                  #900#24 = #900#24 + #caivalin#6;
                  #900#35 = #900#35 + #caivalin#8;
              |SINO;
                  |SI #caivalin#7 = #900#31;           || 16%
                      #900#25 = #900#25 + #caivalin#6;
                      #900#36 = #900#36 + #caivalin#8;
                  |SINO;
                      #900#26 = #900#26 + #caivalin#6;   || ??%
                      #900#32 = #caivalin#7;
                      #900#37 = #900#37 + #caivalin#8;
                  |FINSI;
              |FINSI;
          |FINSI;
      |SINO;                                    ||NO DEDUCIBLE
          #900#57 = #900#57 + #caivalin#8;
          |SI #caivalin#7 = #900#29;         ||  4%
              #900#23 = #900#23 + #caivalin#6;
              #900#52 = #900#52 + #caivalin#8;
          |SINO;
              |SI #caivalin#7 = #900#30;           ||  7%
                  #900#24 = #900#24 + #caivalin#6;
                  #900#53 = #900#53 + #caivalin#8;
              |SINO;
                  |SI #caivalin#7 = #900#31;           || 16%
                      #900#25 = #900#25 + #caivalin#6;
                      #900#54 = #900#54 + #caivalin#8;
                  |SINO;
                      #900#26 = #900#26 + #caivalin#6;   || ??%
                      #900#32 = #caivalin#7;
                      #900#55 = #900#55 + #caivalin#8;
                  |FINSI;
              |FINSI;
          |FINSI;
     |FINSI;
  |FINSI;

  |SI #caivalin#9 ! 0; |Y #caivalin#5 = "S";
      #900#50 = #900#50 + #caivalin#10;
      |SI #caivalin#9 = #900#40;        || 0.5%
          #900#45 = #900#45 + #caivalin#10;
      |SINO;
          |SI #caivalin#9 = #900#41;    || 1.5%
              #900#46 = #900#46 + #caivalin#10;
         |SINO;
              |SI #caivalin#9 = #900#42; || 4
                 #900#47 = #900#47 + #caivalin#10;
             |SINO;
                 #900#43 = #caivalin#9;
                 #900#48 = #900#48 + #caivalin#10; || %
             |FINSI;
         |FINSI;
     |FINSI;
 |FINSI;
 |SI #caivalin#9 ! 0; |Y #caivalin#5 = "N";
     #900#63 = #900#63 + #caivalin#10;
     |SI #caivalin#9 = #900#40;        || 0.5%
         #900#58 = #900#58 + #caivalin#10;
     |SINO;
         |SI #caivalin#9 = #900#41;    || 1.5%
             #900#59 = #900#59 + #caivalin#10;
         |SINO;
             |SI #caivalin#9 = #900#42; || 1.75%
                 #900#60 = #900#60 + #caivalin#10;
             |SINO;
                 #900#43 = #caivalin#9;
                 #900#61 = #900#61 + #caivalin#10; || %
             |FINSI;
         |FINSI;
     |FINSI;
 |FINSI;

 |SI #0#53 = "S";
     #900#51 = #900#51 + #caivalin#6 + #caivalin#8 + #caivalin#10 - #caivalin#22;
 |SINO;
     #900#51 = #900#51 + #caivalin#6 + #caivalin#8 + #caivalin#10;
 |FINSI;

 #900#66 = #900#66 + #caivalin#20;
 #900#67 = #caivalin#21;
 #900#68 = #900#68 + #caivalin#22;

|| ... Graba Resumen 1 por Conceptos
 #901#0 = #caivalin#3;
 #901#5 = "";
 |LEE 101#901=;
 |SI FSalida ! 0;
     |DDEFECTO #901;
     #901#0 = #caivalin#3;
     #901#5 = "";
     |GRABA 020#901b;
 |FINSI;
 #901#1 = #901#1 + #caivalin#6;
 #901#2 = #901#2 + #caivalin#8;
 #901#3 = #901#3 + #caivalin#10;
 #901#4 = #901#4 + (#caivalin#6 + #caivalin#8 + #caivalin#10);
 |GRABA 020#901a;
 |LIBERA #901;

|| ... Graba Resumen 2 por % de IVA
 |SI #0#38 ] 2; |HAZ sLiqIVA; |FINSI;

|FPRC;

|DEFBUCLE LinLibro;
 #caivalin#1;
 ;
 #camaniva#0,#camaniva#1,001;
 #camaniva#0,#camaniva#1,999;
 ;
 NULL, LinLibro0;
|FIN;

|PROCESO Facturas;

   |SI enActividad ! 0;      ||quiere separar por actividades
       |SI aMulDep ! "S";
           nElDepar = #304#10;
           |SI nElDepar = 0;
               nElDepar = 1;
           |FINSI;
           |SI enActividad ! nElDepar; |ACABA; |FINSI;
       |SINO;
            nOpMdep = 0;
            nAlSi   = 0;
            nAlNo   = 0;
            |BUCLE LinLibro;
            |SI nAlSi = 0; |ACABA; |FINSI;
       |FINSI;
   |FINSI;

   nOpMdep = 1;
   |PDEFECTO #900, 16, 64;
   #900#66 = 0; #900#67 = 0; #900#68 = 0;
   #900#79 = "01.01.0000";

   enConcepto = #camaniva#27;
   |SI enSwSelCp = 1;
       |HAZ CtrlConcepto;
       |SI enSwConcep = 1;
           |ACABA;
       |FINSI;
   |FINSI;

   |SI nSigFactu = 1;
       |SI #camaniva#21 ] 0;
           |ACABA;
       |FINSI;
   |FINSI;

   aAlfa1 = #998#3; |QBF aAlfa1;
   |SI aAlfa1 = "*";
       #900#16 = #camaniva#2;
   |SINO;
       #900#16 = #998#3;
   |FINSI;
   |SI #998#4 = 0;
       #900#17    = #camaniva#3;
   |SINO;
       #900#17    = nFactura;
   |FINSI;
   fLaFecha = #camaniva#4; |SI #0#44 = 2; fLaFecha = #camaniva#8; |FINSI;
   aAlfa1   = fLaFecha; aAlfa1   = aAlfa1 % 105;
   #900#18  = aAlfa1;
   #900#79  = fLaFecha;

   #900#19  = #camaniva#12;
   #900#20  = #camaniva#13;
   #900#21  = #camaniva#14;
   #900#22  = #camaniva#28;
   |SI #0#58 = "S";
       aAlfa1 = #900#22; |QBF aAlfa1;
       aAlfa1 = aAlfa1 + " " + #camaniva#22;
       #900#22 = aAlfa1;
   |FINSI;

   |BUCLE LinLibro;

   |SI fLaFecha ] #0#41;
       |SI #0#43 = 2; |O #0#43 = 4; |O #0#43 = 5; |O #0#43 = 6;
           |HAZ VariasLineas;
       |SINO;
           |PRINT;
       |FINSI;
       SwHayDatos = 1;
       SwPrim     = 1;
   |FINSI;
   nFactura = nFactura + 1;

|| .... Suma Parciales
   #998#12 = #998#12 + #900#23;  ||base imponible
   #998#13 = #998#13 + #900#24;
   #998#14 = #998#14 + #900#25;
   #998#15 = #998#15 + #900#26;
   #998#16 = #998#16 + #900#27;
   #998#5  = #998#5  + #900#28;

   #998#17 = #998#17 + #900#34;  ||IVA
   #998#18 = #998#18 + #900#35;
   #998#19 = #998#19 + #900#36;
   #998#20 = #998#20 + #900#37;
   #998#21 = #998#21 + #900#38;
   #998#6  = #998#6  + #900#39;

   #998#22 = #998#22 + #900#45;  ||Recargo
   #998#23 = #998#23 + #900#46;
   #998#24 = #998#24 + #900#47;
   #998#25 = #998#25 + #900#48;
   #998#26 = #998#26 + #900#49;
   #998#7  = #998#7  + #900#50;

   #998#39 = #998#39 + #900#66;   || Retencion
   #998#40 = #998#40 + #900#68;

   #998#8  = #998#8  + #900#51;

   #998#29 = #998#29 + #900#52;  ||IVA No deducibles
   #998#30 = #998#30 + #900#53;
   #998#31 = #998#31 + #900#54;
   #998#32 = #998#32 + #900#55;
   #998#33 = #998#33 + #900#56;
   #998#27 = #998#27 + #900#57;

   #998#34 = #998#34 + #900#58;  ||Recargo No Deducibles
   #998#35 = #998#35 + #900#59;
   #998#36 = #998#36 + #900#60;
   #998#37 = #998#37 + #900#61;
   #998#38 = #998#38 + #900#62;
   #998#28 = #998#28 + #900#63;
|FPRC;

|DEFBUCLE Facturas1;
 #camaniva#5;
 36;
 fDFecha, #0#39, 0000000001, aDTipo;
 fHFecha, #0#40, 9999999999, aHTipo;
 ;
 NULL, Facturas;
|FIN;

|DEFBUCLE Facturas2;
 #camaniva#1;
 8,36;
 #0#39,0000000001, fDFecha, aDTipo;
 #0#40,9999999999, fHFecha, aHTipo;
 ;
 NULL,NULL,NULL,NULL,NULL,Facturas;
 #camaniva#8, #camaniva#0, #camaniva#1;
|FIN;

|DEFBUCLE Facturas3;
 #camaniva#3;
 4,36;
 #0#39,"     ", 000001, fDFecha, aDTipo;
 #0#40,"zzzzz", 999999, fHFecha, aHTipo;
 ;
 NULL, Facturas;
|FIN;

|PROCESO Emision;
   enEmpresa  = #999#1;
   enPerConta = #999#2;
   eaLaMonEmp = #999#13;
   |HAZ EnConversor;
   enEjer     = #999#3;   || A¤o
   dig1       = #999#4;   || Desde Mes
   dig3       = #999#5;   || N§ Digitos
   dig4       = #999#6;   || Digitos Nivel 1
   dig5       = #999#7;   || Digitos Nivel 2
   dig6       = #999#8;   || Digitos Nivel 3
   dig7       = #999#9;   || Digitos Nivel 4
   dig8       = #999#10;  || Digitos Nivel 5
   dig9       = #999#11;  || Digitos Nivel 6
   eaEstadoEmp = #999#12; || Estado
   eaNombreEmp = #999#14; || Nombre Empresa
   aPathConta  = #999#15; |QBF aPathConta; || Path
   || Recoger informacion sobre los niveles ...
   |SI dig3 = 1; MaximoDigitos = dig4; |FINSI;
   |SI dig3 = 2; MaximoDigitos = dig5; |FINSI;
   |SI dig3 = 3; MaximoDigitos = dig6; |FINSI;
   |SI dig3 = 4; MaximoDigitos = dig7; |FINSI;
   |SI dig3 = 5; MaximoDigitos = dig8; |FINSI;
   |SI dig3 = 6; MaximoDigitos = dig9; |FINSI;

   eaTituloL = #0#52; ||Titulo del Libro
   |SI #0#37 = "S";
       |SUB_EJECUTA_NP ":basica/agiy0016";
   |FINSI;

   |PATH_DAT #301 aPathConta;
   |PATH_DAT #304 aPathConta;
   |PATH_DAT #305 aPathConta;
   |PATH_DAT #306 aPathConta;
   |ABRE #caivaasi;
   |DDEFECTO #caivaasi;
   |LEE 000#caivaasi.p;
   |CIERRA #caivaasi;
   aMulDep = #caivaasi#132;

   |DDEFECTO #306;  ||Configuracion de Informes
   |ABRE #306;
   #306#0 = 1;
   |LEE 000#306=;
   |CIERRA #306;

   nDActividad = 0; nHActividad = 0;
   |SI #0#55 = "S"; nDActividad = #0#56; nHActividad = #0#57;  |FINSI;

   |PARA enActividad = nDActividad; |SI enActividad [ nHActividad; |HACIENDO enActividad = enActividad + 1;

         |HAZ LeeDatosEmpresa;
         |SI  swNoExis = 0;       ||SI existe esta Actividad.
             #998#0  = enEmpresa;
             #998#11 = #0#49;
             #998#41 = enActividad;
             |LEE 101#998=;
             |SI FSalida ! 0;
                 |DDEFECTO #998;
                 #998#0  = enEmpresa;
                 #998#11 = #0#49;
                 #998#41 = enActividad;
                 |GRABA 020#998b;
             |FINSI;

             nSwReten = 0;
             |SI #0#53 = "S";  |BUCLE LasReten; |FINSI;

             |SI #0#43 = 1; informe = "dsmoya09"; |FINSI;
             |SI #0#43 = 2; informe = "dsmoyb09"; |FINSI;
             |SI #0#43 = 3; informe = "dsmoyc09"; |FINSI;
             |SI #0#43 = 4; informe = "dsmoyd09"; |FINSI;
             |SI #0#43 = 5; informe = "dsmoye09"; |FINSI;
             |SI #0#43 = 6; informe = "dsmoyf09"; |FINSI;
             |SI nSwReten = 1; |Y #0#43 ! 6;
                 aAlfa1  = informe % 603;
                 informe = "dsmor" + aAlfa1;
             |FINSI;
             |SI #306#4 = "N"; |Y #0#43 ! 6;
                 aAlfa1  = informe % 306;
                 informe = "dr" + aAlfa1;
             |FINSI;

             |INFOR informe;
             |SI FSalida ! 0;  |ACABA;  |FINSI;

             SwHayDatos = 0;
             SwPrim     = 0;
             fDFecha = #0#41;
             fHFecha = #0#42;
             |SI #998#2 = "S";
                 #998#5 = 0;  #998#6 = 0;
                 #998#7 = 0;  #998#8 = 0;
                 |PDEFECTO #998, 12, 41;
                 fDFecha  = "01.01.1900";
                 nFactura = #998#4;
             |SINO;
                 nFactura = #998#10;
             |FINSI;
             nPagina = #998#1;
             nPagDup = nPagina -1;
             nSwPagDup = 0;

             |ABRE #901;
             |SI #0#44 = 1; |BUCLE Facturas1; |FINSI;
             |SI #0#44 = 2; |BUCLE Facturas2; |FINSI;
             |SI #0#44 = 3;
                nNume1  = #0#41; nNume1 = nNume1 - 1;
                fHFecha = nNume1;
                |BUCLE Facturas3;   ||primero sumo anterior
                fDFecha = #0#41;
                fHFecha = #0#42;
                |BUCLE Facturas3;
             |FINSI;
             |CIERRA #901;

             |SI SwHayDatos = 1;
                 |PIEINF;
                 |FININF;
                 #998#1  = nPagina + 1;
                 #998#2  = "N";
                 #998#3  = "";
                 #998#10 = nFactura;
                 |GRABA 020#998a;

                 |SI #0#38 = 3; |O #0#38  = 1; |HAZ Resumen1; |FINSI;
                 |SI #0#38 = 3; |O #0#38  = 2; |HAZ Resumen2; |FINSI;
             |SINO;
                 |FININF;
             |FINSI;
             |LIBERA #998;
        |FINSI;
   |SIGUE;
|FPRC;

|DEFBUCLE dsmow043;
 #dsmow043#1;
 16;
 000001, "S";
 999999, "S";
 ;
 NULL, Emision;
|FIN;

|PROCESO Imprimir;

    |SI aParam ! "";
        Impresora = eaNomImp + "?c:\tmp1,,,?Facturas emitidas";
    |SINO;
        |SI #0#43 ! 4;
           |MENAV "0000 Recuerde seleccionar el papel de la impresora en Horizontal";
        |FINSI;
    |FINSI;

    |SI aParam ! ""; |Y enBasImp ! 0;
        |IMPRE -1;
    |SINO;
        |IMPRE 0;
    |FINSI;
    |SI FSalida ! 0;  |ACABA;  |FINSI;

    aDTipo = "R"; aHTipo = "S";
    |SI enSwSelCp = 0;
        aDTipo = #0#0; aDTipo = aDTipo > " ";
        aHTipo = #0#0; aHTipo = aHTipo > " ";
    |FINSI;

    |ABRE #998;
    |BUCLE dsmow043;
    |CIERRA #998;
    |FINIMP;
|FPRC;

|PROCESO LeeDatosEmpresa;
  |DELINDEX #901;

  |DDEFECTO #900;
  eaInac = #0#24;
  eaTituloL = #0#52;
  efDFecha  = #0#41;
  efHFecha  = #0#42;
  |HAZ DatosEmpr_w30;
|FPRC;


|PROCESO dsmow031;
 #30#0 = #901#0;
 |LEE 001#30=;
 |SI FSalida ! 0;
     |DDEFECTO #30;
 |FINSI;
 |PRINT;
|FPRC;

|DEFBUCLE dsmow031;
 #901#1;
 ;
 " ", 001;
 " ", 999;
 ;
 NULL, dsmow031;
|FIN;

|PROCESO Resumen1;
   |INFOR "dsmoyr08";
   |SI FSalida ! 0;  |ACABA;  |FINSI;

   |ABRE #30;
   |BUCLE dsmow031;
   |CIERRA #30;

   |PIEINF;
   |FININF;
|FPRC;

|PROCESO Resumen2;
   |HAZ iLiqIVA;
   |HAZ BAcum;
|FPRC;

|PROGRAMA;

  aParam = PARAMETRO; |QBF aParam;

  aClave    = "S";
  nSigFactu = 0;

  |DDEFECTO #0;
  |SI aParam = "rec";
      aParam    = "";
      nSigFactu = 1;
      aClave    = "s";
      #0#49     = 922;
  |FINSI;

  enModelo = #0#49;
  |ABRE #99;
  |DDEFECTO #99;
  #99#0 = enModelo;
  |LEE 041#99=;
  aNomModelo = #99#1; |QBF aNomModelo;
  |CIERRA #99;

  |ABRE #0;
  #0#0 = aClave;
  |LEE 101#0=;
  |SI FSalida ! 0;
      |DDEFECTO #0;
      #0#0  = aClave;
      #0#49 = enModelo;
      |GRABA 020#0b;
      nEstado = 1;
  |FINSI;

  |SI aParam = "";
      |CLS;
      |PINPA #0#0;
      |PINDA #0#0;

      |ENDATOS #1#0;
      |SI FSalida = 0;
          |GRABA 020#0a;
      |FINSI;
      |LIBERA #0;
      |CIERRA #0;
  |SINO;
      || ... Legalia
      |LIBERA #0;
      |CIERRA #0;

      #0#1 = enEjercicio;
      #0#2 = eaLegEmp;
      #0#3 = eaLegEmp;

      |PDEFECTO #0,4,41;
      #0#37 = "N";  || sin caratula
      #0#38 = 0;    || sin resumen
      aAlfa1 = #0#1;
      aAlfa2 = "01.01." + aAlfa1; #0#41 = aAlfa2;
      aAlfa2 = "31.12." + aAlfa1; #0#42 = aAlfa2;

      |PDEFECTO #0,45,51;
      #0#45 = efFecLeg;
      #0#51 = 1;
      #0#54 = "S";
      |HAZ Tipo3;

      |DBASS Comodin1; |QBF Comodin1;
      Comodin1 = Comodin1 + "contagen/PresLibr/" + eaLegEmp + eaLegPer + "/";
      Comodin1 = Comodin1 + "FAC_RECI*.xls";
      Numeracion = 1;
      |_PDIR Comodin1;
      |PARA; |SI FSalida = 0; |HACIENDO;
              Comodin = Comodin1 % -107; Comodin = Comodin % 0103;
              Numeracion = Comodin; Numeracion = Numeracion + 1;
              |_SDIR Comodin1;
      |SIGUE;

      Comodin = "c:/tmp1.xls";
      |XABRE "C", Comodin, Handle;
      |SI FSalida ] 0;
          |XCIERRA Handle;
          |DBASS Comodin1; |QBF Comodin1;
          Comodin1 = Comodin1 + "contagen/PresLibr/";
          |MKDIR Comodin1;
          Comodin1 = Comodin1 + eaLegEmp + eaLegPer + "/";
          |MKDIR Comodin1;
          Comodin1 = Comodin1 + "FAC_RECI_" + (("000" + Numeracion) % -103) + ".xls";
          aAlfa = "";
          |IP_REMOTO aAlfa; |QBF aAlfa;
          |SI aAlfa ! "";
             |RECIBE_FICHERO Comodin, Comodin1;
          |SINO;
             |COPIA_FICHERO Comodin, Comodin1;
          |FINSI;
          FSalida = 1;

          |PARA; |SI FSalida ! 0; |HACIENDO;
             |RFBORRA Comodin;
             |SI FSalida ! 0;
                |MENAV "    Termine la tarea de Excel que tiene activa";
             |FINSI;
          |SIGUE;
     |FINSI;

  |FINSI;
|FPRO;
