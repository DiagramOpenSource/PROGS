|FICHEROS;
     dsmom026 #26;

     dsm190ca #190;
     dsm190li #191,MANTE;

     :/basica/def/agidanif #3;

     :/laboral/def/labtraba  #200;
     :/laboral/def/nomhicca  #201;
     :/laboral/def/nomhicli  #202;
     :/laboral/def/labempre  #203;
     :/laboral/def/nomconli  #204;
     :/laboral/def/nomhiatr  #205;
     :/laboral/def/nomconca  #206;
     :/laboral/def/labcentr  #207;
     :/laboral/def/nomcenes  #208;
     :/laboral/def/nomnotay  #215;
     :/laboral/def/nomir003  #101;

     dsmow085 #985;
     dsmow014 #14;
     dsmow134;
|FIN;

|VARIABLES;
     nCampo            = 0;
     nHandle           = 0;
     nTipoRel          = 0;
     nDevengo          = 0;
     nImporteBaseI     = 0;
     nImporteRetencion = 0;
     nImporteSSTrabaja = 0;
     ded_not           = 0;
     nEmpresa          = 0;
     nCentro           = 0;
     notaria           = 0;
     nAtrasos          = 0;
     nEjer             = 0;
     nEjerAt           = 0;
     nHay              = 0;
     nGraba            = 0;
     nBoton5           = 0;
     nEl20             = 0;

     aBase             = "";
     aFichero          = "";
     aPathLaboral      = "";
     aClave            = "";
     aDni              = "";
     aSubClave         = "";
     aNombreTrabajador = "";
     aEspecie          = "";
     aRepercute        = "";
     aAlfa             = "";
     aClaveDia         = "";

     &nDiferencias     = 0;
     &nVEmp            = 0;
     &aVDNI            = "";
     &nVAny            = 0;
     &nCuotNueva       = 0;
     &nBaseHist        = 0;
     &nSSHist          = 0;

     &nSwMasivo = 0;
     &swCabecera = 0;
     nEmpAnt = 0;
     nComAnt = 0;
     nDCompl = 0;
     nHCompl = 0;
     nDActiv = 0;
     nHActiv = 0;

     nIRPFMayo = 0;
     nBaseJunio = 0;
     nRetJunio = 0;
     nIRPFJunio = 0;
     nIRPFJulio = 0;
     nDifJunio = 0;
     nAntEmp = 0;
     nAntTra = 0;
     swSigue = 0;
|FIN;

|RUTINA Baja985;
     #985#0  = #190#0;
     #985#1  = #190#1;
     #985#2  = #190#2;
     #985#3  = #190#3;
     #985#4  = #190#4;
     #985#5  = #190#5;
     #985#6  = "            ";
     #985#7  = 0000;
     #985#8  = " ";
     #985#9  = "  ";
     #985#10 = 0;
|FRUT;

|RUTINA Alta985;
     #985#0  = #190#0;
     #985#1  = #190#1;
     #985#2  = #190#2;
     #985#3  = #190#3;
     #985#4  = #190#4;
     #985#5  = #190#5;
     #985#6  = "zzzzzzzzzzzz";
     #985#7  = 9999;
     #985#8  = "z";
     #985#9  = "zz";
     #985#10 = 4;
|FRUT;

|| Carga del Modelo

|PROCESO dsm190liD;
     |DDEFECTO #985;
     |PARA nCampo = 0;  |SI nCampo [ 10;  |HACIENDO nCampo = nCampo + 1;
           #985nCampo = #191nCampo;
     |SIGUE;
     #985#11 = #191#12;
     #985#15 = #191#16 + #191#18;
     #985#16 = #191#17 + #191#19;
     #985#17 = #191#22;

     |GRABA 020#985n;
     |LIBERA #985;
|FPRC;

|DEFBUCLE dsm190liD;
     #191#1;
     ;
     #190#0,#190#1,#190#2,#190#3,nDCompl,nDActiv,"            ",0000,"A","  ", 0;
     #190#0,#190#1,#190#2,#190#3,nHCompl,nHActiv,"zzzzzzzzzzzz",9999,"A","zz", 9;
     || #190#0,#190#1,#190#2,#190#3,#190#4,#190#5,"            ",0000,"A","  ", 0;
     || #190#0,#190#1,#190#2,#190#3,#190#4,#190#5,"zzzzzzzzzzzz",9999,"A","zz", 9;
     be;
     NULL, dsm190liD;
|FIN;

|| Carga del Historico

|PROCESO TipoRel;
     nTipoRel = "0";
     |SI aClave ! "A";
         nTipoRel = 0;
         |ACABA;
     |FINSI;

     |ABRE #3;
     #3#0  = aDni;
     #3#1  = 1;
     |LEE 040#3=;
     |SI FSalida ! 0;  |DDEFECTO #3; |FINSI;
     |CIERRA #3;

     nTipoRel = #3#36;
|FPRC;

|PROCESO GrabaTemporal1;
     |SI aClave = " ";  |Y aSubClave = "  ";  |ACABA;  |FINSI;
     |SI aClave ! "A";                        |ACABA;  |FINSI;

     |HAZ TipoRel;

     #985#0     = #190#0;
     #985#1     = #190#1;
     #985#2     = #190#2;
     #985#3     = #190#3;
     #985#4     = #190#4;
     #985#5     = #190#5;
     #985#6     = aDni;
     #985#7     = nDevengo;
     #985#8     = aClave;
     #985#9     = aSubClave;
     #985#10    = nTipoRel;
     |LEE 101#985=;
     |SI FSalida ! 0;
         |DDEFECTO #985;
         #985#0     = #190#0;
         #985#1     = #190#1;
         #985#2     = #190#2;
         #985#3     = #190#3;
         #985#4     = #190#4;
         #985#5     = #190#5;
         #985#6     = aDni;
         #985#7     = nDevengo;
         #985#8     = aClave;
         #985#9     = aSubClave;
         #985#10    = nTipoRel;
         #985#11    = aNombreTrabajador;
         |GRABA 020#985b;
     |FINSI;

     #985#12 = #985#12 + nImporteBaseI;
     #985#13 = #985#13 + nImporteRetencion;
     #985#14 = #985#14 + nImporteSSTrabaja;

     |SI nTipoRel = 2; |O nTipoRel = 3;
          |SI #201#2 = 7; |Y #201#3 = 0;
               #985#18 = #985#18 - nDifJunio;
          |FINSI;
     |FINSI;

     #985#20 = nEl20;
     #985#21 = aClaveDia;
|| xxxxxxxxxxxxx
     |GRABA 020#985a;
     |LIBERA #985;

     nImporteBaseI     = 0;
     nImporteRetencion = 0;
     nImporteSSTrabaja = 0;
     ded_not           = 0;
|FPRC;

|PROCESO SSTrabaja;
     |ABRE #215;
     #215#0 = nEmpresa;
     #215#1 = nCentro;
     |LEE 000#215=;
     |SI FSalida ! 0;
         #215#2 = 0;       || (%) a cero
     |FINSI;
     notaria = #215#2;
     |CIERRA #215;

     ded_not           = (#201#48 < notaria) ! 2;
     nImporteSSTrabaja = (ded_not+#201#49+#201#50+#201#51);           || deduc.S.S.
|FPRC;

|PROCESO nomhicli;
     #204#0 = #200#87;
     #204#2 = #202#4;
     |LEE 101#204=;
     |SI FSalida = 0;
         |SI #204#8 = 0;  |ACABA;  |FINSI;

         |SI #204#13 = "  ";
             #204#13 = "01";
             |GRABA 020#204a;
         |FINSI;

         aClave            = "L";
         aSubClave         = #204#13;
         aEspecie          = "N";
         aRepercute        = "N";
         nImporteBaseI     = #202#11 * #202#10;
         nImporteRetencion = 0;
         |HAZ GrabaTemporal1;
     |FINSI;

     |LIBERA #204;
|FPRC;

|DEFBUCLE nomhicli;
     #202#1;
     ;
     #201#0, #201#1, #201#66, #201#2, #201#3, 411;
     #201#0, #201#1, #201#66, #201#2, #201#3, 499;
     ;
     NULL, nomhicli;
|FIN;

|PROCESO IniJun2008;
     nIRPFMayo = 0;
     nBaseJunio = 0;
     nRetJunio = 0;
     nIRPFJunio = 0;
     nIRPFJulio = 0;
     nDifJunio = 0;
|FPRC;

|PROCESO Jun2008;
     |SI nAntEmp ! #201#0; |O nAntTra ! #201#1;
          |HAZ IniJun2008;
     |FINSI;
     |SI #201#66 ! 08; |O #201#3 ! 0;
          |ACABA;
     |FINSI;
     |SI #201#2 = 5;
          nIRPFMayo = #201#13;
     |FINSI;
     |SI #201#2 = 6;
          nBaseJunio = #201#14 + #201#16;
          nRetJunio = #201#15 + #201#17;
          nIRPFJunio = #201#13;
     |FINSI;
     |SI #201#2 = 7;
          nIRPFJulio = #201#13;
          |HAZ TipoRel;
          swSigue = 0;
          |SI #3#36 = 2; |O #3#36 = 3;
               swSigue = 1;
          |FINSI;
          |SI nIRPFMayo = 2; |Y nIRPFJulio = 2;
               swSigue = swSigue + 1;
          |FINSI;
          |SI nIRPFMayo = 15; |Y nIRPFJulio = 15;
               swSigue = swSigue + 1;
          |FINSI;
          |SI nIRPFJunio < nIRPFMayo;
               swSigue = swSigue + 1;
          |FINSI;
          |SI swSigue ] 3;
               nDifJunio = ((nBaseJunio) % (nIRPFMayo - nIRPFJunio)) ! 2;
          |FINSI;
     |FINSI;
     nAntEmp = #201#0;
     nAntTra = #201#1;
|FPRC;

|PROCESO nomhicca;
     nDevengo = 0;

     |SI nAtrasos = 1;
         |SI #205#1 ! #205#3;
              nDevengo = #205#3;
         |FINSI;
     |FINSI;

     |HAZ Jun2008;

     #203#0 = #201#0;
     |LEE 000#203=;
     |SI FSalida ! 0;  |DDEFECTO #203; |FINSI;

     #200#0 = #201#0;
     #200#1 = #201#1;
     |LEE 000#200=;
     |SI FSalida ! 0;  |DDEFECTO #200; |FINSI;
     nCentro = #200#77;

     nEl20     = 0;
     aClaveDia = "";
     |ABRE #101;
     #101#0  = #200#7;
     #101#40 = #190#1;
     #101#41 = #200#0;
     |LEE 040#101=;
     |SI FSalida = 0;
         nEl20     = #101#11;
         aClaveDia = #200#42;
     |FINSI;
     |CIERRA #101;

     aNombreTrabajador = #200#2;
     aDni              = #200#7;

     aClave            = #200#99;
     aSubClave         = #200#102;
     aEspecie          = #200#105;
     aRepercute        = #200#108;
     nImporteBaseI     = #201#14 + #201#16;
     nImporteRetencion = #201#15 + #201#17;
     |HAZ SSTrabaja;
     |HAZ GrabaTemporal1;

     aClave            = #200#100;
     aSubClave         = #200#103;
     aEspecie          = #200#106;
     aRepercute        = #200#109;
     nImporteBaseI     = #201#108;
     nImporteRetencion = #201#110;
     |HAZ GrabaTemporal1;

     aClave            = #200#101;
     aSubClave         = #200#104;
     aEspecie          = #200#107;
     aRepercute        = #200#110;
     nImporteBaseI     = #201#109;
     nImporteRetencion = #201#111;
     |HAZ GrabaTemporal1;

     |SI #201#18 ! 0;
         |BUCLE nomhicli;
     |FINSI;
|FPRC;

|DEFBUCLE nomhicca;
     #201#1;
     ;
     nEmpresa, 00001, nEjer, 01, 0;
     nEmpresa, 99999, nEjer, 99, 4;
     ;
     NULL, nomhicca;
|FIN;

|DEFBUCLE nomhiccaA;
     #201#1;
     ;
     #205#0, 00001, nEjerAt, 01, #205#2;
     #205#0, 99999, nEjerAt, 12, #205#2;
     ;
     NULL, nomhicca;
|FIN;

|PROCESO nomhiatr;
     |SI #205#4 = 0;  |ACABA;  |FINSI;

     aAlfa   = #205#3;
     aAlfa   = aAlfa % -102;
     nEjerAt = aAlfa;
     |BUCLE nomhiccaA;
|FPRC;

|DEFBUCLE nomhiatr;
     #205#1;
     ;
     nEmpresa, #190#1, 5, 0000;
     nEmpresa, #190#1, 9, 9999;
     ;
     NULL, nomhiatr;
|FIN;

|PROCESO dsmom026;
     nEmpresa = #26#1;
     nAtrasos = 0;  |BUCLE nomhicca;
     nAtrasos = 1;  |BUCLE nomhiatr;
|FPRC;

|DEFBUCLE dsmom026;
     #26#1;
     ;
     #190#0, 00001;
     #190#0, 99999;
     ;
     NULL, dsmom026;
|FIN;

|PROCESO Historico;
     |ABRE #200;
     |ABRE #203;
     |ABRE #204;
     |ABRE #206;

     aAlfa   = #190#1;
     aAlfa   = aAlfa % -102;
     nEjer   = aAlfa;

     |SELECT #1#26;
     #26#0 = #190#0;
     #26#1 = 0;
     |LEE 040#26];
     |SI FSalida = 0;  |Y  #26#0 = #190#0;
         |BUCLE dsmom026;
         |ACABA;
     |FINSI;

     |SELECT #3#26;
     #26#1 = #190#0;
     |LEE 040#26=;
     |SI FSalida = 0;  |ACABA;  |FINSI;

     nEmpresa = #190#0;
     nAtrasos = 0;  |BUCLE nomhicca;
     nAtrasos = 1;  |BUCLE nomhiatr;

     |CIERRA #200;
     |CIERRA #203;
     |CIERRA #204;
     |CIERRA #206;
|FPRC;

|PROCESO dsmow085;
     nDiferencias     = 1;
     nVEmp            = #985#0;
     aVDNI            = #985#6;
     nVAny            = #985#1;
     nCuotNueva       = 0;
     nBaseHist        = #985#15;
     nSSHist          = #985#17;
     |SUB_EJECUTA_NP ":laboral/nomir011";

     nGraba  = 0;
     || #985#18 = nCuotNueva;
     #985#18 = #985#18 + nCuotNueva;
     #985#19 = #985#18 - #985#16;

     |SI #985#14 = 0;  |Y #985#21 = "H";
         #985#14 = #985#20;
     |FINSI;

     |SI #985#12 ! #985#15;  nGraba = 1;  nHay = 1;  |FINSI;
     |SI #985#13 ! #985#16;  nGraba = 1;  nHay = 1;  |FINSI;
     |SI #985#14 ! #985#17;  nGraba = 1;  nHay = 1;  |FINSI;
     |SI #985#19 ! 0;        nGraba = 1;  nHay = 1;  |FINSI;

     |SI nGraba = 1;
         |GRABA 020#985a;
     |SINO;
         |BORRA 020#985a;
     |FINSI;
     |LIBERA #985;
|FPRC;

|DEFBUCLE dsmow085;
     #985#1;
     ;
     #190#0,#190#1,#190#2,#190#3,nDCompl,nDActiv,"            ",0000,"A","  ", 0;
     #190#0,#190#1,#190#2,#190#3,nHCompl,nHActiv,"zzzzzzzzzzzz",9999,"A","zz", 9;
     || #190#0,#190#1,#190#2,#190#3,#190#4,#190#5,"            ",0000,"A","  ", 0;
     || #190#0,#190#1,#190#2,#190#3,#190#4,#190#5,"zzzzzzzzzzzz",9999,"A","zz", 9;
     be;
     NULL, dsmow085;
|FIN;

|PROCESO dsmow085I;
     |SI #985#19 [ #dsmow134#0; |O #985#19 ] #dsmow134#1;

     |SI #985#0 ! nEmpAnt; |O #985#4 ! nComAnt; |O nEmpAnt = 0;
          |SI nEmpAnt ! 0;
               |PIEINF;
          |FINSI;
          swCabecera = 1;
     |SINO;
          swCabecera = 0;
     |FINSI;

          |PRINT;
          nEmpAnt = #985#0;
          nComAnt = #985#4;
     |FINSI;
|FPRC;

|DEFBUCLE dsmow085I;
     #985#1;
     ;
     #190#0,#190#1,#190#2,#190#3,nDCompl,nDActiv,"            ",0000,"A","  ", 0;
     #190#0,#190#1,#190#2,#190#3,nHCompl,nHActiv,"zzzzzzzzzzzz",9999,"A","zz", 9;
     || #190#0,#190#1,#190#2,#190#3,#190#4,#190#5,"            ",0000,"A","  ", 0;
     || #190#0,#190#1,#190#2,#190#3,#190#4,#190#5,"zzzzzzzzzzzz",9999,"A","zz", 9;
     be;
     NULL, dsmow085I;
|FIN;

|PROCESO dsmow014I;
     #190#0 = #14#0;
     #190#1 = #14#1;
     #190#2 = #14#2;
     #190#3 = 190;
     nDCompl = 00;
     nHCompl = 99;
     nDActiv = 00;
     nHActiv = 99;

     |BUCLE dsmow085I;
|FPRC;

|DEFBUCLE dsmow014I;
     #14#1;
     6;
     00000, 0000, 00, "S";
     99999, 9999, 99, "S";
     ;
     NULL, dsmow014I;
|FIN;

|PROCESO Boton5;
     |PUSHV 16272280;
     |PINPA #0#dsmow134;
     |PINDA #0#dsmow134;
     |ENDATOS #1#dsmow134;
     |SI FSalida ! 0;
          |POPV;
          |ACABA;
     |FINSI;
     |POPV;

     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI nSwMasivo ! 1;
          |INFOR "dsmow085";
     |SINO;
          |INFOR "dsmow08m";
     |FINSI;

     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

     |SI nSwMasivo ! 1;
          nDCompl = #190#4;
          nHCompl = #190#4;
          nDActiv = #190#5;
          nHActiv = #190#5;

          |BUCLE dsmow085I;
     |SINO;
          |BUCLE dsmow014I;
     |FINSI;

     |PIEINF;
     |FININF;
     |FINIMP;
|FPRC;

|PROCESO dsmow014;
     #190#0 = #14#0;
     #190#1 = #14#1;
     #190#2 = #14#2;
     #190#3 = 190;

     nDCompl = 00;
     nHCompl = 99;
     nDActiv = 00;
     nHActiv = 99;

     |ABRE #985;
     |BUCLE dsm190liD;

     |ABRET #26;
     |HAZ Historico;
     |CIERRAT #26;

     |CIERRA #985;

     nHay = 0;

     nDCompl = 00;
     nHCompl = 99;
     nDActiv = 00;
     nHActiv = 99;
     |BUCLE dsmow085;
|FPRC;

|DEFBUCLE dsmow014;
     #14#1;
     6;
     00000, 0000, 00, "S";
     99999, 9999, 99, "S";
     ;
     NULL, dsmow014;
|FIN;

|PROCESO Diferencias;
     |DBASS aBase;  |QBF aBase;
     aPathLaboral = aBase + "laboral/def/nomhicca.mas";
     |XABRE "E", aPathLaboral, nHandle;
     |SI FSalida < 0;
         |MENAV "    No existe la Gestion Laboral. ....";
         |ACABA;
     |FINSI;

     aPathLaboral = aBase + "laboral/fich/";
     |PATH_DAT #201 aPathLaboral;
     |PATH_DAT #202 aPathLaboral;
     |PATH_DAT #204 aPathLaboral;
     |PATH_DAT #206 aPathLaboral;
     |PATH_DAT #215 aPathLaboral;

     aFichero = ("85" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #985 aFichero;
     |ABRE #985;  |CIERRA #985;  |DELINDEX #985;

     |SI nSwMasivo ! 1;
          |ABRE #985;

          nDCompl = #190#4;
          nHCompl = #190#4;

          nDActiv = #190#5;
          nHActiv = #190#5;

          |BUCLE dsm190liD;

          |ABRET #26;
          |HAZ Historico;
          |CIERRAT #26;

          |CIERRA #985;

          nHay = 0;
          nDCompl = #190#4;
          nHCompl = #190#4;
          nDActiv = #190#5;
          nHActiv = #190#5;
          |BUCLE dsmow085;

          |SI nHay = 0;
              |MENAV "0000 No existen diferencias entre el Historico y el Modelo.";
              |DELINDEX #985;
              |ACABA;
          |FINSI;

          |PUSHV 0501 2380;

          |PINPA #0#985;

          |CONTROL_SIMPLE 0, "BOTON,  Imprimir Diferencias ", 2235, 2265, Boton5;
          nBoton5 = FSalida;

          |ENTLINEAL #1#985, -7, 4, 22, 0, Baja985, Alta985;

          |FIN_CONTROL_WINDOWS nBoton5;

          |POPV;

          |DELINDEX #985;

     |SINO;

          aFichero = ("imp" + Usuario) % 108;
          |NOME_DAT #14 aFichero;

          |BUCLE dsmow014;
/*
          |SI nHay = 0;
              |MENAV "0000 No existen diferencias entre el Historico y el Modelo.";
              |DELINDEX #985;
              |ACABA;
          |FINSI;
*/
          |HAZ Boton5;
          |DELINDEX #985;
     |FINSI;
|FPRC;
