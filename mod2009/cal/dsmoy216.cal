|FICHEROS;
     dsmoy000 #0;
     dsmom002 #2;
     dsmom004 #4;
     dsmom107 #107;
     dsmow601 #601,MANTE;

     dsmod216 #216;
     dsmow014 #999;
     dsmow578 #578;

     &clientes@ #715;
     &datosper@ #718;
|FIN;

|VARIABLES;
|FIN;

|INCLUYE i_varemi;

|PROCESO Periodicidad216;
     |SI #2#7 = "TRIMESTRAL";
         aPeriodo = "T";
     |SINO;
         aPeriodo = "M";
     |FINSI;
|FPRC;

|DEFBUCLE dsmom002;
     #2#1;
     ;
     #4#0, 01, 216;
     #4#0, 98, 216;
     ;
     NULL, Periodicidad216;
|FIN;

|PROCESO TDeclaracion216;
     #578#2 = "";

     |SI #216#11 > 0;
         enModelo = 216;  enForzar = 0;  |HAZ CogeDatosBancos;

         #578#13 = eaEntidad;
         #578#14 = eaOficina;
         #578#15 = eaDC;
         #578#16 = eaCuenta;
     |FINSI;

     #578#2 = "N";
     |SI #216#11 > 0;
                                 #578#2 = "I";
         |SI eaFormaPago = "3";  #578#2 = "U";  |FINSI;
         |SI eaFormaPago = "4";  #578#2 = "A";  |FINSI;
         |SI eaFormaPago = "5";  #578#2 = "E";  |FINSI;
     |FINSI;
|FPRC;

|PROCESO Registro216;
     |ABRE #216;
     #216#0 = #4#0;
     #216#1 = #4#1;
     #216#2 = #4#2;
     #216#3 = #4#3;
     #216#4 = #4#4;
     #216#5 = 0;
     |LEE 040#216=;
     |SI FSalida ! 0;  |CIERRA #216;  |ACABA;  |FINSI;
     |CIERRA #216;

     enCodCli = #216#0;
     |HAZ LeePersona;

     |DDEFECTO #578;

     #578#1  = "216";

     |SI enSwDeDonde = 1;
         eaVarDni = #715#3;   |HAZ CalculameDNI;   #578#3   = eaVarDni;
         eaAlfa   = #715#57;  |HAZ QuitaRaros;     #578#7   = eaAlfa;
         eaAlfa   = #715#53;  |HAZ QuitaRaros;     #578#17  = eaAlfa;
         eaAlfa   = #715#54;  |HAZ QuitaRarosTfno; #578#18  = eaAlfa;

         eaDeleg = #715#17;
         eaAdmin = #715#18;
     |SINO;
         eaVarDni = #715#13;  |HAZ CalculameDNI;   #578#3  = eaVarDni;
         eaAlfa   = #715#1;   |HAZ QuitaRaros;     #578#17 = eaAlfa;
         eaAlfa   = #715#14;  |HAZ QuitaRarosTfno; #578#18 = eaAlfa;

         eaDeleg = #715#91;
         eaAdmin = #715#92;
     |FINSI;

     aPeriodo = "T";
     |BUCLE dsmom002;

     #578#5 = #216#1;

     |SI aPeriodo = "T";
         |SI #216#2 = 3;   #578#6 = "1T";  |FINSI;
         |SI #216#2 = 6;   #578#6 = "2T";  |FINSI;
         |SI #216#2 = 9;   #578#6 = "3T";  |FINSI;
         |SI #216#2 = 12;  #578#6 = "4T";  |FINSI;
     |SINO;
         #578#6 = ("00" + #216#2) % -102;
     |FINSI;

     enNume  = #216#6;  |HAZ Conver17;  #578#7  = eaAlfa;
     enNume  = #216#7;  |HAZ Conver17;  #578#8  = eaAlfa;
     enNume  = #216#8;  |HAZ Conver17;  #578#9  = eaAlfa;
     enNume  = #216#9;  |HAZ Conver17;  #578#10 = eaAlfa;
     enNume  = #216#10; |HAZ Conver17;  #578#11 = eaAlfa;
     enNume  = #216#11; |HAZ Conver17;  #578#12 = eaAlfa;

     |HAZ TDeclaracion216;

     #578#23 = &13 + &10;

     nTCampo = 22;

     |SI nTecla = 3;
         aAlfa = "";
         |PARA nCampo = 1;  |SI nCampo [ nTCampo;  |HACIENDO nCampo = nCampo + 1;
              aAlfa = #578nCampo;
              |XGRABA nHandle2, aAlfa;
         |SIGUE;
         |XCIERRA nHandle2;

         eaFOrigen  = eaPathEmision  + "216" + (("00000" + #999#0) % -105) + ".TXT";
         eaFDestino = eaPathInternet + "216" + (("00000" + #999#0) % -105) + ".TXT";
         |HAZ CopiaFichero;

         |FBORRA eaFOrigen;

         |HAZ GrabaImpreso;
     |FINSI;

     |SI nTecla = 4;
         |PRINT;
         |PIEINF;
     |FINSI;
|FPRC;

|PROCESO QueHacemos216;
     |ABRE #4;
     #4#0  = #999#0;
     #4#1  = #999#1;
     #4#2  = #999#2;
     #4#3  = #999#5;
     #4#4  = #999#7;
     |LEE 040#4=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;
     |CIERRA #4;

     |SI nTecla = 1;
         aAbre = eaPathEmision + "216" + (("00000" + #999#0) % -105) + ".TXT";
         |XABRE "BW", aAbre, nHandle2;
     |FINSI;

     |SI nTecla = 3;
         aAbre = eaPathEmision + "216" + (("00000" + #999#0) % -105) + ".TXT";
         |XABRE "BW", aAbre, nHandle2;
     |FINSI;

     enCodCli = #216#0;
     |HAZ LeePersona;

     |HAZ Registro216;
|FPRC;

|DEFBUCLE dsmow014C216;
     #999#1;
     6;
     00000, 0000, 00, "S";
     99999, 9999, 99, "S";
     ;
     NULL, QueHacemos216;
|FIN;

|DEFBUCLE dsmow014N216;
     #999#2;
     6;
     "                                        ", "S";
     "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz", "S";
     ;
     NULL, QueHacemos216;
|FIN;

|PROCESO Boton2161;
     |PTEC 824;
|FPRC;

|PROCESO Boton2162;
     |PTEC 825;
|FPRC;

|PROCESO Boton216Esc;
     |PTEC 806;
|FPRC;

|PROCESO Botones216;
     |BLANCO 0501 2380;

     |CONTROL_SIMPLE 0, "LBOTON, <F2> Presentacion Telematica              ", 0913, 1154, Boton2161;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON, <F3> Informe de Comprobacion              ", 1313, 1554, Boton2162;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,ESC Cancelar", 2365, 2378, Boton216Esc;
     nBoton3 = FSalida;

     aF2 = &255 + "824";
     aF3 = &255 + "825";
     aF4 = &255 + "806";
     |PARA; |SI;  |HACIENDO;
         |LEETECLA aTecla;
         |SI aTecla = aF2;  nTecla = 3;   |SAL;  |FINSI;
         |SI aTecla = aF3;  nTecla = 4;   |SAL;  |FINSI;
         |SI aTecla = aF4;  nTecla = 0;   |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;

     |PULSATECLA;
|FPRC;

|PROGRAMA;
     aFichero = ("000" + Usuario) % 108;
     |NOME_DAT #0 aFichero;

     aFichero = ("imp" + Usuario) % 108;
     |NOME_DAT #999 aFichero;

     |ABRE #107;
     |LEE 040#107p;
     |SI FSalida ! 0;
         |CIERRA #107;
         |SUB_EJECUTA_NP "dsmom107";
         |ABRE #107;
     |FINSI;

     |LEE 040#107p;
     |SI FSalida ! 0;  |DDEFECTO #107;  |FINSI;
     |CIERRA #107;

     |ABRE #0;
     |LEE 000#0p;
     |CIERRA #0;

     enSwTrata      = 0;
     enCambiaMoneda = 1;

     |SI eaParametro ! "216C";
         |HAZ Botones216;
     |SINO;
         nTecla = 4;
     |FINSI;

     |SI nTecla = 0;  |ACABA;  |FINSI;

     |SI nTecla = 4;
         |IMPRE 0;
         |SI FSalida ! 0;  |ACABA;  |FINSI;

         |INFOR "m216co01";

         |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;
     |FINSI;

     nSwLee = 0;
     |SI #0#8 = "C";
         |BUCLE dsmow014C216;
     |SINO;
         |BUCLE dsmow014N216;
     |FINSI;

     |SI nTecla = 4;
         |FININF;
         |FINIMP;
     |FINSI;

     |SI nTecla = 3;
         aAlfa =  "      Los ficheros generados estan en: " + eaPathInternet + " de su PC";
         |MENAV aAlfa;
     |FINSI;
|FPRO;
