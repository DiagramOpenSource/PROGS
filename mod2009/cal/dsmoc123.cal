|FICHEROS;
     dsmoc123 #0;
     dsmom002 #2;
     dsmod123 #123;
     dsmom004 #4;
     dsmom008 #8;

     dsmod123@ #1123;

     &regisnif@ #709;
|FIN;

|VARIABLES;
     nGrupo        = 0;
     i             = 0;
     nMod          = 123;
     aDn           = "            ";
     aHn           = "zzzzzzzzzzzz";
     nDd           = 0;
     nHd           = 9999;
     aTipoCaptura  ="";
     nSwEsta       = 0;
     nEjerAnt      = 0;
     nPer          = 0;
     nSwGraba      = 0;
     nTipo         = 0;
     nIngre        = 0;
     nPeriodo      = 0;
     nEl8          = 0;
     nHComple      = 0;

     aPeriodo      = "";
     aPathModelo   = "";
     aDef          = "";
     aAlfa         = "";
     aPeriodicidad = "";
     aDNIAnt       = "";
     aBase         = "";
     aMas          = "";
|FIN;

|INCLUYE i_variar;

|PROCESO PonNombre;  |TIPO 7;
     aAlfa = #0#21;  |QBF aAlfa;
     |SI aAlfa = "";
         eaCodNif = #0#6;
         |HAZ LeeNifs;
         #0#21 = #709#1;
         |PINTA #0#21;
     |FINSI;
|FPRC;

|PROCESO Naturaleza;
     |SI #0#8 = "A";
         |SI #0#9 > 6;
             |MENAV "     Codigo de Naturaleza erroneo. Parametro del 1 al 6";
             |ERROR;
         |FINSI;
     |FINSI;

     |SI #0#8 = "B";
         |SI #0#9 > 7;
             |MENAV "     Codigo de Naturaleza erroneo. Parametro del 1 al 7";
             |ERROR;
         |FINSI;
     |FINSI;

     |SI #0#8 = "D";
         |SI #0#9 > 7;
             |MENAV "     Codigo de Naturaleza erroneo. Parametro del 1 al 7";
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO TotRetencion;
     |SI #0Campo = Contenido;  |ACABA;  |FINSI;

     |SI Campo = 11;  |O Campo = 12;
         #0#13 = #0#11 % #0#12;  |PINTA #0#13;
     |FINSI;

     |SI Campo = 14;  |O Campo = 15;
         #0#16 = #0#14 % #0#15;  |PINTA #0#16;
     |FINSI;

     |SI Campo = 17;  |O Campo = 18;
         #0#19 = #0#17 % #0#18;  |PINTA #0#19;
     |FINSI;
|FPRC;

|PROCESO Modificables; |TIPO 7;
     |ABRE #2;
     #2#0 = enEmpresa;
     #2#1 = enActividad;
     #2#2 = enModelo;
     |LEE 000#2=;
     |SI FSalida = 0;
          aPeriodicidad = #2#7;
          |QBF aPeriodicidad;
     |SINO;
          aPeriodicidad = "TRIMESTRAL";
     |FINSI;
     |CIERRA #2;

     |SI aPeriodicidad ! "TRIMESTRAL";
          |PARA i = 14;|SI i [ 19; |HACIENDO i = i + 1;
               |CAMPO_MODIFICA #0i,1,"N";
               #0i = 0;  |PINTA #0i;
          |SIGUE;
     |FINSI;

     |C_M #0#20, 1, "S";
     |SI #0#7 = 0;  |Y #0#6 ! "PASEANTERIOR";  |Y #0#6 ! "MANUAL      ";
         |C_M #0#20, 1, "N";
         #0#20 = 0;  |PINTA #0#20;
     |FINSI;

     |SI #0#6 ! "PASEANTERIOR";  |Y #0#6 ! "MANUAL      ";
         |C_M #0#22, 1, "N";  |C_V #0#22, 1, "N";
     |SINO;
         |C_M #0#22, 1, "S";  |C_V #0#22, 1, "S";

         |PARA i = 14;|SI i [ 19; |HACIENDO i = i + 1;
               |CAMPO_MODIFICA #0i,1,"N";
               #0i = 0;  |PINTA #0i;
         |SIGUE;
     |FINSI;

     |PINTA #0#22;
|FPRC;

|PROCESO Tipo7;  |TIPO 7;
     |SI #0#6 ! "PASEANTERIOR";  |Y #0#6 ! "MANUAL      ";
         |C_M #0#22, 1, "N";  |C_V #0#22, 1, "N";
     |SINO;
         |C_M #0#22, 1, "S";  |C_V #0#22, 1, "S";
     |FINSI;

     |PINTA #0#22;
|FPRC;

|PROCESO Dnimoc123;  |TIPO 11;
     |SI FSalida = 10;
         eaVarDni  = #0Campo;
         enCalcCif = 1;
         |HAZ CalculameDNI;
         #0Campo = eaVarDni;
         |PINTA #0Campo;
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO MiraDnimoc123;  |TIPO 6;
     eaVarDni  = #0Campo;
     enCalcCif = 0;
     |HAZ CalculameDNI;
     eaVarDni = (eaVarDni + "             ") % 112;
     |SI #0Campo ! eaVarDni;
         |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
         |SI FSalida = 0;
             #0Campo = eaVarDni;  |PINTA #0Campo;
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO dsmod123Otros;
     #123#14 = #123#14 + #1123#11;
|FPRC;

|DEFBUCLE dsmod123Otros;
     #1123#1006;
     ;
     #123#0, #123#1, #123#2, #123#3,        0, #123#5;
     #123#0, #123#1, #123#2, #123#3, nHComple, #123#5;
     ;
     NULL, dsmod123Otros;
|FIN;

|PROCESO CapturaOtros123;
     |DBASE aBase;  |QBF aBase;
     aMas = aBase + "def/dsmod123.mas";
     |CARGA_DEF aMas, dsmod123@;

     nHComple = #123#4 - 1;
     #123#14    = 0;

     |BUCLE dsmod123Otros;

     |DESCARGA_DEF #dsmod123@;
|FPRC;

|PROCESO GrabaModelo;
     nSwGraba = 1;

     |SI #0#6 ! "            ";
         |SI #0#6 ! "PASEANTERIOR";  |Y #0#6 ! "MANUAL      ";
             #123#6 = #123#6 + 1;
         |FINSI;

         |SI #0#6 = "PASEANTERIOR";  |O #0#6 = "MANUAL      ";
             #123#6 = #0#22;
         |FINSI;
     |FINSI;

     #123#7  = #123#7 + #0#11 + #0#14 + #0#17;
     #123#8  = #123#8 + #0#13 + #0#16 + #0#19;
     #123#9  = #123#9 + #0#20;
     #123#12 = nIngre + #123#8;
     #123#10 = #123#9 - #123#12;
     |SI #123#10 < 0;  #123#10 = 0;  |FINSI;

     |SI #123#1 [ 2007;
         #123#11 = #123#8 + #123#10;
         #123#13 = 0;
         #123#14 = 0;
     |SINO;
         #123#13 = #123#8 + #123#10;
         #123#14 = 0;

         |SI #123#4 > 0;
             |HAZ CapturaOtros123;
             nEl8 = #123#13 - #123#14;
             |SI nEl8 < 0;
                 |MENAV "    RECUERDE. Si el resultado NO es a INGRESAR, NO procede declaracion complementaria";
             |FINSI;
         |FINSI;

         #123#11 = #123#13 - #123#14;
     |FINSI;
|FPRC;

|DEFBUCLE dsmoc123;
     #0#1;
     ;
     enEmpresa, enEjer, enPeriodo, nMod, enComplem, 01, aDn, nDd, " ", 00, 1;
     enEmpresa, enEjer, enPeriodo, nMod, enComplem, 99, aHn, nHd, "z", 99, 2;
     be;
     NULL, GrabaModelo;
|FIN;

|PROCESO GrabaRegistro0;
     |SI enModelo = 123;
         |ABRE #123;
         #123#0 = enEmpresa;
         #123#1 = enEjer;
         #123#2 = enPeriodo;
         #123#3 = enModelo;
         #123#4 = enComplem;
         #123#5 = 0;
         |LEE 101#123=;
         |SI FSalida = 0;
             |BORRA 020#123a;
         |FINSI;
         |LIBERA #123;
     |FINSI;

     |ABRE #8;
     #8#0 = enEmpresa;
     #8#1 = enEjer;
     #8#2 = enPeriodo;
     #8#3 = enModelo;
     #8#4 = enComplem;
     #8#5 = 0;
     |LEE 101#8=;
     |SI FSalida = 0;
         #8#11 = "SIN DATOS ";
         |GRABA 020#8a;
     |FINSI;
     |LIBERA #8;
     |CIERRA #8;
|FPRC;

|PROCESO BajaModelo;
     |BORRA 020#0a;
     |LIBERA #0;
|FPRC;

|DEFBUCLE dsmoc123B;
     #0#1;
     ;
     enEmpresa, enEjer, enPeriodo, nMod, enComplem, 01, aDn, nDd, " ", 00, 1;
     enEmpresa, enEjer, enPeriodo, nMod, enComplem, 99, aHn, nHd, "z", 99, 2;
     be;
     NULL, BajaModelo;
|FIN;

|PROCESO BorraRegistros;
     |BUCLE dsmoc123B;
     |HAZ GrabaRegistro0;

     |ABRE #8;
     #8#0 = enEmpresa;
     #8#1 = enEjer;
     #8#2 = nPer;
     #8#3 = 123;
     #8#4 = enComplem;
     #8#5 = enActividad;
     |LEE 101#8=;
     |SI FSalida = 0;
         #8#11 = "SIN DATOS";
         |GRABA 020#8a;
     |FINSI;
     |LIBERA #8;
     |CIERRA #8;
|FPRC;

|PROCESO BuscaIngresos;
     nIngre    = 0;

     |ABRE #2;
     #2#0 = enEmpresa;
     #2#1 = enActividad;
     #2#2 = enModelo;
     |LEE 000#2=;
     |SI FSalida ! 0;  |DDEFECTO #2;  |FINSI;
     |CIERRA #2;

     aPeriodo  = "T";
     |SI #2#7 = "MENSUAL   ";  aPeriodo = "M";  |FINSI;

     |SI aPeriodo = "T";
         |SI enPeriodo ! 3;
             |PARA nPeriodo = 3;  |SI nPeriodo < enPeriodo;  |HACIENDO nPeriodo = nPeriodo + 3;
                   #123#0 = enEmpresa;
                   #123#1 = enEjer;
                   #123#2 = nPeriodo
                   #123#3 = enModelo;
                   #123#4 = enComplem;
                   #123#5 = enActividad;
                   |LEE 040#123=;
                   |SI FSalida = 0;
                       |SI #123#11 > 0;
                           nIngre = nIngre + #123#8;
                       |FINSI;
                   |FINSI;
              |SIGUE;
         |FINSI;
     |SINO;
         |SI enPeriodo ! 1;
             |PARA nPeriodo = 1;  |SI nPeriodo < enPeriodo;  |HACIENDO nPeriodo = nPeriodo + 1;
                   #123#0 = enEmpresa;
                   #123#1 = enEjer;
                   #123#2 = nPeriodo
                   #123#3 = enModelo;
                   #123#4 = enComplem;
                   #123#5 = enActividad;
                   |LEE 040#123=;
                   |SI FSalida = 0;
                       |SI #123#11 > 0;
                           nIngre = nIngre + #123#8;
                       |FINSI;
                   |FINSI;
              |SIGUE;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO GrabaTodo;
     |ABRE #123;
     #123#0 = enEmpresa;
     #123#1 = enEjer;
     #123#2 = enPeriodo;
     #123#3 = enModelo;
     #123#4 = enComplem;
     #123#5 = 0;
     |LEE 101#123=;
     |SI FSalida = 0;
          |BORRA 020#123a;
     |FINSI;

     |HAZ BuscaIngresos;

     |DDEFECTO #123;
     #123#0 = enEmpresa;
     #123#1 = enEjer;
     #123#2 = enPeriodo;
     #123#3 = enModelo;
     #123#4 = enComplem;
     #123#5 = 0;

     nSwGraba = 0;
     |BUCLE dsmoc123;
     |SI nSwGraba = 0;
         |MENAV "0000 No hay Registros ";
     |SINO;
         |GRABA 020#123n;
         |LIBERA #123;

         |ABRE #8;
         #8#0 = enEmpresa;
         #8#1 = enEjer;
         #8#2 = enPeriodo;
         #8#3 = enModelo;
         #8#4 = enComplem;
         #8#5 = enActividad;
         |LEE 101#8=;
         |SI FSalida = 0;
             #8#11 = "COMPLETADO";
             |GRABA 020#8a;
         |FINSI;
         |LIBERA #8;

         #8#0 = enEmpresa;
         #8#1 = enEjer;
         #8#2 = enPeriodo;
         #8#3 = enModelo;
         #8#4 = enComplem;
         #8#5 = 0;
         |LEE 101#8=;
         |SI FSalida = 0;
             #8#11 = "EXISTENTE";
             |GRABA 020#8a;
         |FINSI;
         |LIBERA #8;
         |CIERRA #8;
     |FINSI;
     |CIERRA #123;
|FPRC;

|PROCESO AltaModelo;
     |HAZ PintaDatos;
     |ENTLINEAL #1#0, -7, 2, 22, 1, ClaveBaja0, ClaveAlta0;

     |HAZ GrabaTodo;
|FPRC;

|RUTINA ClaveBaja0;
     #0#0  = enEmpresa;
     #0#1  = enEjer;
     #0#2  = enPeriodo;
     #0#3  = enModelo;
     #0#4  = enComplem;
     #0#5  = enActividad;
     #0#6  = "            ";
     #0#7  = 0000;
     #0#8  = "A";
     #0#9  = 01;
     #0#10 = 1;
|FRUT;

|RUTINA ClaveAlta0;
     #0#0  = enEmpresa;
     #0#1  = enEjer;
     #0#2  = enPeriodo;
     #0#3  = enModelo;
     #0#4  = enComplem;
     #0#5  = enActividad;
     #0#6  = "zzzzzzzzzzzz";
     #0#7  = 9999;
     #0#8  = "D";
     #0#9  = 12;
     #0#10 = 2;
|FRUT;

|PROCESO TipoCaptura;
     |ABRE #8;
     #8#0 = enEmpresa;
     #8#1 = enEjer;
     #8#2 = enPeriodo;
     #8#3 = enModelo;
     #8#4 = enComplem;
     #8#5 = enActividad;
     |LEE 101#8=;
     |SI FSalida = 0;
          aTipoCaptura = #8#7;
          |QBF aTipoCaptura;
     |FINSI;
     |CIERRA #8;
|FPRC;

|PROGRAMA;
     eaNoVisu = "";
     eaNomDef = "dsmoc123";

|| \\\\\\ Consulta del Modelo ////////

     |SI enTipoEntrada = 4;              || Consulta del Modelo
          |HAZ PintaDatos;
          |ENTLINEAL #1#0, -11, 4, 22, 1, ClaveBaja0, ClaveAlta0;
          |ACABA;
     |FINSI;

|| \\\\\\ Baja del Modelo ////////

     |SI enTipoEntrada = 3;              ||Baja del Modelo
         |HAZ BorraRegistros;
         |ABRE #0;
         |HAZ GrabaTodo;
         |CIERRA #0;
         |ACABA;
     |FINSI;

|| \\\\\\ Baja del Modelo desde Modulo////////

     |SI enTipoEntrada = 97;              ||Baja del Modelo
         |HAZ BorraRegistros;
         |ACABA;
     |FINSI;

|| \\\\\\ Alta o Modificacion del Modelo ////////

     |SI enTipoEntrada = 1;              || Alta del Modelo
         |CABEZA "Modelo Trabajo 123";
         |HAZ AltaModelo;
         |ACABA;
     |FINSI;

|| \\\\\\ Grabacion del Modelo desde el Exterior

     |SI enTipoEntrada = 98;
         |ABRE #0;
         |HAZ GrabaTodo;
         |CIERRA #0;
         |ACABA;
     |FINSI;

|| \\\\\\ Comprobacion Existencia del Modelo

     |SI enTipoEntrada = 99;              || Existencia del Modelo ????
         |ABRE #0;
         #0#0  = enEmpresa;
         #0#1  = enEjer;
         #0#2  = enPeriodo;
         #0#3  = enModelo;
         #0#4  = enComplem;
         #0#5  = enActividad;
         #0#6  = "            ";
         #0#7  = 0;
         #0#8  = " ";
         #0#9  = 0;
         #0#10 = 0;
         |LEE 040#0];
         |SI FSalida ! 0;  |O #0#0 ! enEmpresa;  |O #0#1 ! enEjer; |O #0#2 ! enPeriodo;
          |O #0#3 ! enModelo;  |O #0#4 ! enComplem; |O #0#5 ! enActividad;
             enExistencia = 1;
         |FINSI;
         |CIERRA #0;
     |FINSI;
|FPRO;
