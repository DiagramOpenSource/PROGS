|FICHEROS;
     dsmoz001 #0;
     dsmom001 #1;
     dsmom002 #2;
     dsmom003 #3;
     dsmom004 #4;
     dsmom005 #5;
     dsmom006 #6;
     dsmom008 #8;

     dsmom009 #9;        || Planillas
     dsmom010 #10;       ||
     dsmom011 #11;       ||
     dsmom012 #12;       ||
     dsmom013 #13;       ||

     dsmom016 #16;       || Modulos
     dsmom017 #17;       ||
     dsmom018 #18;       ||
     dsmom019 #19;       ||

     dsmom026 #26;       || Empresas Asociadas

     dsmow003 #99;
     :/basica/def/agifigen #100;
     :/basica/def/agicen14 #114;
     :/basica/def/agitab11 #111;
|FIN;

|VARIABLES;
     nCampo    = 0;
     nDEmpresa = 0;
     nHEmpresa = 0;
     nPeriodo  = 0;
     nNume     = 0;
     nSwEsta1  = 0;
     nSwEsta2  = 0;
     nActivi   = 0;
     nPasada   = 0;
     nForzar   = 0;
     nEmpresa  = 0;

     aAlfa     = "";
     aAlfa1    = "";
     aAlfa2    = "";
     aFichero  = "";
     aMenav    = "";
     aDescri   = "";
     aEl300_9  = "";
     aEl310_7  = "";
     aEl310_9  = "";

     fHFecha   = @;
     fDFecha   = @;
     fFecha    = @;

     &enEmpresaA = 0;
     &enAnyo     = 0;
     &enSwEstar  = 0;
|FIN;

|INCLUYE i_variar;

|| /////////////////////// PROCESOS ENTRADA POR PANTALLA \\\\\\\\\\\\\\\\\\\\

|PROCESO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#0 = enEmpresa;  |PINTA #0#0;
         #0#1 = enEmpresa;  |PINTA #0#1;

         |C_M #0#0, 1, "N";
         |C_M #0#1, 1, "N";
     |FINSI;

     fFecha = ~;
     aAlfa  = fFecha % -104;
     #0#14  = aAlfa;

     |PINTA #0#14;
|FPRC;

|PROCESO Parrilla;
     |SI #0#0 = 0;
         #0#0 = 0;  |PINTA #0#0;
         #0#1 = 0;  |PINTA #0#1;  |C_M #0#1, 1, "N";
         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#0, 1, "S";
             |C_M #0#1, 1, "S";
         |FINSI;

         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|| /////////////////////// PROCESOS CALCULO DEL PLANING \\\\\\\\\\\\\\\\\\\\\

|PROCESO GrabaPlanilla;
     |ABRE #9;
     #9#0 =  #8#0;
     #9#1 =  #8#1;
     #9#2 =  #8#2;
     #9#3 =  #8#4;
     #9#4 =  #8#5;
     |LEE 101#9=;
     |SI FSalida ! 0;
         |DDEFECTO #9;
         #9#0 =  #8#0;
         #9#1 =  #8#1;
         #9#2 =  #8#2;
         #9#3 =  #8#4;
         #9#4 =  #8#5;
         |GRABA 020#9b;
     |FINSI;

     |SI #8#3 = 300;  #9#7  = "X";  #9#8 = " ";  #9#9  = " ";  #9#10 = " ";  #9#11 = " ";  |FINSI;
     |SI #8#3 = 370;  #9#7  = " ";  #9#8 = "X";  #9#9  = " ";  #9#10 = " ";  #9#11 = " ";  |FINSI;
     |SI #8#3 = 320;  #9#9  = "X";  #9#7 = " ";  #9#8  = " ";  #9#10 = " ";  #9#11 = " ";  |FINSI;
     |SI #8#3 = 330;  #9#10 = "X";  #9#7 = " ";  #9#8  = " ";  #9#9  = " ";  #9#11 = " ";  |FINSI;
     |SI #8#3 = 332;  #9#11 = "X";  #9#7 = " ";  #9#8  = " ";  #9#9  = " ";  #9#10 = " ";  |FINSI;
     |SI #8#3 = 349;  #9#12 = "X";  |FINSI;
     |SI #8#3 = 130;  #9#14 = "X";  |FINSI;
     |SI #8#3 = 131;  #9#15 = "X";  |FINSI;

     |ABRE #100;
     #100#0 = #9#0;
     |LEE 040#100=;
     |SI FSalida ! 0;  |DDEFECTO #100;  |FINSI;
     |CIERRA #100;

     #114#0 = #8#0;
     #114#1 = #8#5;
     |LEE 040#114=;
     |SI FSalida ! 0;  |DDEFECTO #114;  |FINSI;
     |SI #100#94 > "01.01.0000";
         |SI #114#6 < "01.01.1900";
             #114#6 = #100#94;
         |FINSI;
     |FINSI;

     #9#5   = #100#1;
     #9#6   = #8#6;
     #9#48  = #114#3;
     #9#49  = #114#8;
     #9#50  = #114#10;
     #9#51  = #114#17;
     #9#52  = #114#12;
     #9#53  = #114#5;
     #9#54  = #114#6;
     #9#55  = #114#11;
     #9#56  = #114#9;

     |GRABA 020#9a;
     |LIBERA #9;
     |CIERRA #9;
|FPRC;

|PROCESO GrabaModulos;
     |ABRE #17;
     #17#0 =  #8#0;
     #17#1 =  #8#1;
     #17#2 =  #8#4;
     #17#3 =  #8#5;
     |LEE 101#17=;
     |SI FSalida ! 0;
         |DDEFECTO #17;
         #17#0 =  #8#0;
         #17#1 =  #8#1;
         #17#2 =  #8#4;
         #17#3 =  #8#5;
         |GRABA 020#17b;
     |FINSI;

     |ABRE #100;
     #100#0 = #17#0;
     |LEE 040#100=;
     |SI FSalida ! 0;  |DDEFECTO #100;  |FINSI;
     |CIERRA #100;
     |SI #100#94 > "01.01.0000";
         |SI #114#6 < "01.01.1900";
             #114#6 = #100#94;
         |FINSI;
     |FINSI;

     #17#4  = #114#2;
     #17#5  = #114#3;
     #17#7  = #100#1;

     |ABRE #16;
     |SELECT #1#16;
     #16#0  = "E";
     #16#1  = #17#5;
     #16#41 = #17#6;
     #16#2  = #17#1;
     |LEE 040#16=;
     |SI FSalida ! 0;  |DDEFECTO #16;  |FINSI;
     |CIERRA #16;

     #17#8  = #16#3;

     |SI #114#11 = 100;  |O #114#11 = 200;
         #17#12 = "S";
     |FINSI;

     |SI #114#9 = 6;  |O #114#9 = 7;  |O #114#9 = 11;  |O #114#9 = 12;
         #17#12 = "S";
     |FINSI;

     |GRABA 020#17a;
     |LIBERA #17;
     |CIERRA #17;
|FPRC;

|PROCESO Borra10;
     |BORRA 020#10a;
     |LIBERA #10;
|FPRC;

|DEFBUCLE dsmom010;
     #10#1;
     ;
     #9#0, #9#1, #9#2, #9#3, #9#4, "    ";
     #9#0, #9#1, #9#2, #9#3, #9#4, "zzzz";
     be;
     NULL, Borra10;
|FIN;

|PROCESO Borra11;
     |BORRA 020#11a;
     |LIBERA #11;
|FPRC;

|DEFBUCLE dsmom011;
     #11#1;
     ;
     #9#0, #9#1, #9#2, #9#3, #9#4, "    ";
     #9#0, #9#1, #9#2, #9#3, #9#4, "zzzz";
     be;
     NULL, Borra11;
|FIN;

|PROCESO Borra12;
     |BORRA 020#12a;
     |LIBERA #12;
|FPRC;

|DEFBUCLE dsmom012;
     #12#1;
     ;
     #9#0, #9#1, #9#2, #9#3, #9#4, "    ";
     #9#0, #9#1, #9#2, #9#3, #9#4, "zzzz";
     be;
     NULL, Borra12;
|FIN;

|PROCESO Borra13;
     |BORRA 020#13a;
     |LIBERA #13;
|FPRC;

|DEFBUCLE dsmom013;
     #13#1;
     ;
     #9#0, #9#1, #9#2, #9#3, #9#4, "    ";
     #9#0, #9#1, #9#2, #9#3, #9#4, "zzzz";
     be;
     NULL, Borra13;
|FIN;

|PROCESO BorraModulos;
     |ABRE #18;
     #18#0 = #17#0;
     #18#1 = #17#1;
     #18#2 = #17#2;
     #18#3 = #17#3;
     |LEE 101#18=;
     |SI FSalida = 0;
         |BORRA 020#18a;
     |FINSI;
     |LIBERA #18;
     |CIERRA #18;

     |ABRE #19;
     #19#0 = #17#0;
     #19#1 = #17#1;
     #19#2 = #17#2;
     #19#3 = #17#3;
     |LEE 101#19=;
     |SI FSalida = 0;
         |BORRA 020#19a;
     |FINSI;
     |LIBERA #19;
     |CIERRA #19;
|FPRC;

|PROCESO Borra08;
     |SI nPasada = 1;
         |SI #8#3 = 370;  |Y #8#5 ! 0;
             #114#0 = #8#0;
             #114#1 = #8#5;
             |LEE 040#114=;
             |SI FSalida ! 0;  |DDEFECTO #114;  |FINSI;

             #8#12 = 310;

             |ABRE #111;
             #111#0 = #114#7;
             #111#1 = 310;
             |LEE 040#111=;
             |SI FSalida ! 0;  #8#12 = 300; |FINSI;
             |CIERRA #111;

             |ABRE #3;
             #3#0 = #4#0;
             #3#1 = #4#1;
             #3#2 = #4#2;
             |LEE 040#3=;
             |SI FSalida ! 0;  |DDEFECTO #3;  |FINSI;
             |CIERRA #3;

             #8#8  = "REGISTROS DEL MODELO " + #8#12 + " EJERCICIO " + #3#1 + " PERIODO " + #3#3;

             |GRABA 020#8a;
             |LIBERA #8;
         |FINSI;
         |ACABA;
     |FINSI;

     |SI #8#7 = "PLANILLAS ";
         |ABRE #9;
         #9#0 =  #8#0;
         #9#1 =  #8#1;
         #9#2 =  #8#2;
         #9#3 =  #8#4;
         #9#4 =  #8#5;
         |LEE 101#9=;
         |SI FSalida = 0;
             |SI #8#3 = 300;  #9#7  = " ";  |FINSI;
             |SI #8#3 = 370;  #9#8  = " ";  |FINSI;
             |SI #8#3 = 320;  #9#9  = " ";  |FINSI;
             |SI #8#3 = 320;  #9#9  = " ";  |FINSI;
             |SI #8#3 = 330;  #9#10 = " ";  |FINSI;
             |SI #8#3 = 332;  #9#11 = " ";  |FINSI;
             |SI #8#3 = 349;  #9#12 = " ";  |FINSI;
             |SI #8#3 = 130;  #9#14 = " ";  |FINSI;
             |SI #8#3 = 131;  #9#15 = " ";  |FINSI;

             |SI #9#7  = " "; |Y #9#8  = " "; |Y #9#9  = " "; |Y #9#10 = " ";
              |Y #9#11 = " "; |Y #9#12 = " "; |Y #9#13 = " "; |Y #9#14 = " ";
              |Y #9#15 = " ";
                 |BORRA 020#9a;

                 |BUCLE dsmom010;
                 |BUCLE dsmom011;
                 |BUCLE dsmom012;
                 |BUCLE dsmom013;
             |SINO;
                 |GRABA 020#9a;
             |FINSI;
         |FINSI;
         |LIBERA #9;
         |CIERRA #9;
     |FINSI;

     |SI #8#7 = "MODULOS   ";
         |SI nSwEsta2 = 0;
             |ABRE #17;
             #17#0 =  #8#0;
             #17#1 =  #8#1;
             #17#2 =  #8#4;
             #17#3 =  #8#5;
             |LEE 101#17=;
             |SI FSalida = 0;
                 |SI #17#19 = 0;  |Y #17#24 = 0;
                     |HAZ BorraModulos;
                     |BORRA 020#17a;
                 |FINSI;
             |FINSI;
             |LIBERA #17;
             |CIERRA #17;
         |FINSI;
     |FINSI;

     |BORRA 020#8a;
     |LIBERA #8;
|FPRC;

|DEFBUCLE dsmom008;
     #8#1;
     ;
     #4#0, #4#1, #4#2, #4#3, #4#4, 00;
     #4#0, #4#1, #4#2, #4#3, #4#4, 99;
     be;
     NULL, Borra08;
|FIN;

|PROCESO Borra04;
     |SI enSwEstar = 1;
         nPasada = 1;  |BUCLE dsmom008;
     |FINSI;

     nPasada = 0;

     |SI #4#3  = 310;
         |SI #4#7 = "X";  |Y nForzar = 0;  |ACABA;  |FINSI;
     |SINO;
         |SI #4#7  = "X";   |ACABA;   |FINSI;
     |FINSI;
     |SI #4#9  = "X";   |ACABA;   |FINSI;
     |SI #4#11 = "X";   |ACABA;   |FINSI;
     |SI #4#13 = "X";   |ACABA;   |FINSI;

     |BORRA 020#4a;
     |LIBERA #4;

     |BUCLE dsmom008;
|FPRC;

|DEFBUCLE dsmom004;
     #4#1;
     ;
     nDEmpresa, #0#14, 00, 000, 00;
     nHEmpresa, #0#14, 99, 999, 99;
     be;
     NULL, Borra04;
|FIN;

|PROCESO PasaAl370;
     |SALVA_DATOS #8;

     #8#3 = 370;
     |LEE 101#8=;
     |SI FSalida ! 0;
         #8#3  = 370;
         #8#12 = 300;
         |GRABA 020#8n;
         |LIBERA #8;
     |FINSI;

     |SI #8#7 = "PLANILLAS ";
         |HAZ GrabaPlanilla;
     |FINSI;

     |REPON_DATOS #8;
     |LEE 101#8=;
|FPRC;

|DEFBUCLE dsmom008P;
     #8#1;
     ;
     #4#0, #4#1, #4#2, #4#3, #4#4, 01;
     #4#0, #4#1, #4#2, #4#3, #4#4, 99;
     be;
     NULL, PasaAl370;
|FIN;

|PROCESO Filtros;
     nSwEsta1 = 0;
     nSwEsta2 = 0;
     nForzar  = 0;

     |ABRE #4;
     #4#0 = #3#0;
     #4#1 = #3#1;
     #4#2 = #3#2;
     #4#3 = 370;
     #4#4 = 0;
     |LEE 040#4=;
     |SI FSalida = 0;
         #4#0 = #3#0;
         #4#1 = #3#1;
         #4#2 = #3#2;
         #4#3 = 300;
         #4#4 = 0;
         |LEE 040#4=;
         |SI FSalida ! 0;   |DDEFECTO #4;  |FINSI;
         aEl300_9 = #4#7;

         #4#0 = #3#0;
         #4#1 = #3#1;
         #4#2 = #3#2;
         #4#3 = 310;
         #4#4 = 0;
         |LEE 040#4=;
         |SI FSalida ! 0;   |DDEFECTO #4;  |FINSI;
         aEl310_7 = #4#7;
         aEl310_9 = #4#9;

         |SI aEl300_9 = " ";  nSwEsta1 = 1;  |FINSI;
         |SI aEl310_9 = " ";  nSwEsta2 = 1;  |FINSI;

         |SI enSwEstar = 1;
             nSwEsta1 = 1;
             nSwEsta2 = 1;
         |FINSI;
     |FINSI;

     |SI nSwEsta1 = 1;  |Y nSwEsta2 = 1;
         |SI aEl310_7 = "X";  nForzar = 1;  |FINSI;

         #4#0 = #3#0;
         #4#1 = #3#1;
         #4#2 = #3#2;
         #4#3 = 300;
         #4#4 = 0;
         |LEE 101#4=;
         |SI FSalida = 0;
             |BUCLE dsmom008P;
             |HAZ Borra04;
         |FINSI;
         |LIBERA #4;

         #4#0 = #3#0;
         #4#1 = #3#1;
         #4#2 = #3#2;
         #4#3 = 310;
         #4#4 = 0;
         |LEE 101#4=;
         |SI FSalida = 0;
             |HAZ Borra04;
         |FINSI;
         |LIBERA #4;

         |SI nForzar = 1;
             aAlfa = "0000 Empresa " + #3#0 + " Ejercicio " + #3#1 + " Periodo " + #3#2;
             aAlfa = aAlfa + " Modelo 370 activado en este periodo. Vuelva a Revisar datos.";
             |MENAV aAlfa;

             nForzar = 0;
         |FINSI;
     |SINO;
         #4#0 = #3#0;
         #4#1 = #3#1;
         #4#2 = #3#2;
         #4#3 = 370;
         #4#4 = 0;
         |LEE 101#4=;
         |SI FSalida = 0;
             |HAZ Borra04;
         |FINSI;
         |LIBERA #4;
     |FINSI;

     #4#0 = #3#0;
     #4#1 = #3#1;
     #4#2 = #3#2;
     #4#3 = 200;
     #4#4 = 0;
     |LEE 040#4=;
     |SI FSalida = 0;
         #4#0 = #3#0;
         #4#1 = #3#1;
         #4#2 = #3#2;
         #4#3 = 201;
         #4#4 = 0;
         |LEE 101#4=;
         |SI FSalida = 0;  |HAZ Borra04;  |FINSI;
         |LIBERA #4;
     |FINSI;

     #4#0 = #3#0;
     #4#1 = #3#1;
     #4#2 = #3#2;
     #4#3 = 0;
     #4#4 = 0;
     |LEE 040#4];
     |SI FSalida ! 0; |O #4#0 ! #3#0; |O #4#1 ! #3#1; |O #4#2 ! #3#2;
         |BORRA 020#3a;
         |LIBERA #3;
     |FINSI;

     |CIERRA #4;
|FPRC;

|DEFBUCLE dsmom003;
     #3#2;
     ;
     nDEmpresa, #0#14, 00;
     nHEmpresa, #0#14, 99;
     be;
     NULL, Filtros;
|FIN;

|PROCESO GrabaTrabNormal;
     #8#0 = #4#0;
     #8#1 = #4#1;
     #8#2 = #4#2;
     #8#3 = #4#3;
     #8#4 = #4#4;
     #8#5 = 0;
     |LEE 101#8=;
     |SI FSalida ! 0;
         |DDEFECTO #8;
         #8#0  = #4#0;
         #8#1  = #4#1;
         #8#2  = #4#2;
         #8#3  = #4#3;
         #8#4  = #4#4;
         #8#5  = 0;
         #8#6  = "REGISTRO DE IMPRESION";
         #8#7  = "";
         #8#8  = "REGISTROS DEL MODELO " + #8#3 + " EJERCICIO " + #3#1 + " PERIODO " + #3#3;
         #8#9  = "N";
         #8#10 = "N";
         #8#11 = "";
         #8#12 = #8#3;
         |GRABA 020#8n;
     |FINSI;
     |LIBERA #8;

     nActivi = #114#1;
     aDescri = #114#4;
     |SI #4#3 = 110;  |O #4#3 = 115;  |O #4#3 = 123;  |O #4#3 = 216;
         nActivi = 1;
         aDescri = "REGISTRO DE TRABAJO";
     |FINSI;

     #8#0 = #4#0;
     #8#1 = #4#1;
     #8#2 = #4#2;
     #8#3 = #4#3;
     #8#4 = #4#4;
     #8#5 = nActivi;
     |LEE 101#8=;
     |SI FSalida ! 0;
         |DDEFECTO #8;

         #8#12 = #4#3;
         |SI #8#12 = 370;  #8#12 = 310;  |FINSI;

         #8#0  = #4#0;
         #8#1  = #4#1;
         #8#2  = #4#2;
         #8#3  = #4#3;
         #8#4  = #4#4;
         #8#5  = nActivi;
         #8#6  = aDescri;
         #8#7  = #114#15;
         #8#8  = "REGISTROS DEL MODELO " + #8#12 + " EJERCICIO " + #3#1 + " PERIODO " + #3#3;
         #8#9  = "N";
         #8#10 = "N";
         #8#11 = "SIN DATOS";

         |SI #8#3 = 347;  |O #8#3 = 110;  |O #8#3 = 115;
          |O #8#3 = 349;  |O #8#3 = 123;  |O #8#3 = 216;
             |SI #8#7 = "PLANILLAS ";  #8#7 = "MANUAL";  |FINSI;
             |SI #8#7 = "MODULOS   ";  #8#7 = "MANUAL";  |FINSI;
         |FINSI;

         |SI #8#3 = 216;  #8#7 = "MANUAL";  |FINSI;

         |GRABA 020#8n;

         |SI #8#7 = "PLANILLAS ";
             |HAZ GrabaPlanilla;
         |FINSI;

         |SI #8#7 = "MODULOS   ";
             |HAZ GrabaModulos;
         |FINSI;
     |FINSI;
     |LIBERA #8;

     |SI #4#5 = "ACT.+ REG.";
         #8#0 = #4#0;
         #8#1 = #4#1;
         #8#2 = #4#2;
         #8#3 = #4#3;
         #8#4 = #4#4;
         #8#5 = 99;
         |LEE 101#8=;
         |SI FSalida ! 0;
             |DDEFECTO #8;
             #8#0  = #4#0;
             #8#1  = #4#1;
             #8#2  = #4#2;
             #8#3  = #4#3;
             #8#4  = #4#4;
             #8#5  = 99;
             #8#6  = "REGISTRO DE REGULARIZACION";
             #8#7  = "MANUAL";
             #8#8  = "REGISTROS DEL MODELO " + #8#3 + " EJERCICIO " + #3#1 + " PERIODO " + #3#3;
             #8#9  = "N";
             #8#10 = "N";
             #8#11 = "SIN DATOS";
             #8#12 = #8#3;
             |GRABA 020#8n;
         |FINSI;
         |LIBERA #8;
     |FINSI;
|FPRC;

|PROCESO TipoNormal;
     |SI #2#2 = 110;  |O #2#2 = 190;    || Mira si es una empresa asociada.
         |ABRE #26;
         |SELECT #3#26;
         #26#1 = #3#0;
         |LEE 040#26=;
         |SI FSalida = 0;  |Y #26#0 ! #26#1;
             |CIERRA #26;
             |ACABA;
         |FINSI;
         |CIERRA #26;
     |FINSI;

     #4#0 = #3#0;
     #4#1 = #3#1;
     #4#2 = #3#2;
     #4#3 = #2#2;
     #4#4 = 0;
     |LEE 040#4=;
     |SI FSalida ! 0;
         |DDEFECTO #4;
         #4#0 = #3#0;
         #4#1 = #3#1;
         #4#2 = #3#2;
         #4#3 = #2#2;
         #4#4 = 0;

         #6#0 = #4#0;
         #6#1 = #4#3;
         |LEE 040#6=;
         |SI FSalida = 0;
             #4#5  = #6#9;
             #4#16 = #6#12;
         |SINO;
             #5#0 = #4#3;
             |LEE 040#5=;
             |SI FSalida ! 0;  |DDEFECTO #5;  |FINSI;

             #4#5  = #5#8;
             #4#16 = #5#11;
         |FINSI;

         |SI #4#3 = 180;  |O #4#3 = 190;  |O #4#3 = 390;  |O #4#3 = 392;
          |O #4#3 = 193;  |O #4#3 = 184;  |O #4#3 = 296;
             #4#5  = "EMPRESA";
         |FINSI;

         |GRABA 020#4n;
         |LIBERA #4;
     |FINSI;

     |SI #4#5 ! "EMPRESA   ";
         |HAZ GrabaTrabNormal;
     |FINSI;
|FPRC;

|PROCESO MiraEmpresas;
     #99#0 = #1#3;
     |LEE 101#99=;
     |SI FSalida ! 0;
         |DDEFECTO #99;
         #99#0 = #1#3;
         #99#1 = "N";
         |GRABA 020#99n;
     |FINSI;
     |LIBERA #99;
|FPRC;

|DEFBUCLE dsmom001;
    #1#1;
    ;
    #2#0, #2#1, 0000;
    #2#0, #2#1, 9999;
    ;
    NULL, MiraEmpresas;
|FIN;

|PROCESO GrabaTrabCom;
     |SI #114#13 = "N";
         #8#0 = #4#0;
         #8#1 = #4#1;
         #8#2 = #4#2;
         #8#3 = #4#3;
         #8#4 = #4#4;
         #8#5 = 0;
         |LEE 101#8=;
         |SI FSalida ! 0;
             |DDEFECTO #8;
             #8#0  = #4#0;
             #8#1  = #4#1;
             #8#2  = #4#2;
             #8#3  = #4#3;
             #8#4  = #4#4;
             #8#5  = 0;
             #8#6  = "REGISTRO DE IMPRESION";
             #8#7  = "";
             #8#8  = "REGISTROS DEL MODELO " + #8#3 + " EJERCICIO " + #3#1 + " PERIODO " + #3#3;
             #8#9  = "N";
             #8#10 = "N";
             #8#11 = "";
             #8#12 = #8#3;
             |GRABA 020#8n;
         |FINSI;
         |LIBERA #8;

         |SI #4#5 = "ACT.+ REG.";
             #8#0 = #4#0;
             #8#1 = #4#1;
             #8#2 = #4#2;
             #8#3 = #4#3;
             #8#4 = #4#4;
             #8#5 = 99;
             |LEE 101#8=;
             |SI FSalida ! 0;
                 |DDEFECTO #8;
                 #8#0  = #4#0;
                 #8#1  = #4#1;
                 #8#2  = #4#2;
                 #8#3  = #4#3;
                 #8#4  = #4#4;
                 #8#5  = 99;
                 #8#6  = "REGISTRO DE REGULARIZACION";
                 #8#7  = "MANUAL";
                 #8#8  = "REGISTROS DEL MODELO " + #8#3 + " EJERCICIO " + #3#1 + " PERIODO " + #3#3;
                 #8#9  = "N";
                 #8#10 = "N";
                 #8#11 = "SIN DATOS";
                 #8#12 = #8#3;
                 |GRABA 020#8n;
             |FINSI;
             |LIBERA #8;
         |FINSI;
     |FINSI;

     #8#0 = #4#0;
     #8#1 = #4#1;
     #8#2 = #4#2;
     #8#3 = #4#3;
     #8#4 = #4#4;
     #8#5 = #114#1;
     |LEE 101#8=;
     |SI FSalida ! 0;
         |DDEFECTO #8;
         #8#0  = #4#0;
         #8#1  = #4#1;
         #8#2  = #4#2;
         #8#3  = #4#3;
         #8#4  = #4#4;
         #8#5  = #114#1;
         #8#6  = #114#4;
         |SI #114#14 = "N";
             #8#7  = #114#15;
         |SINO;
             #8#7  = "COMUNIDAD";
         |FINSI;
         #8#8  = "REGISTROS DEL MODELO " + #8#3 + " EJERCICIO " + #3#1 + " PERIODO " + #3#3;
         #8#9  = #114#13;
         #8#10 = #114#14;
         #8#11 = "SIN DATOS";
         #8#12 = #8#3;
         |GRABA 020#8n;

         |SI #8#7 = "PLANILLAS ";
             |HAZ GrabaPlanilla;
         |FINSI;

         |SI #8#7 = "MODULOS   ";
             |HAZ GrabaModulos;
         |FINSI;
     |FINSI;
     |LIBERA #8;
|FPRC;

|PROCESO TipoComunidad;
     |SI #114#13 = "S";
         |BUCLE dsmom001;
     |FINSI;

     #4#0 = #3#0;
     #4#1 = #3#1;
     #4#2 = #3#2;
     #4#3 = #2#2;
     #4#4 = 0;
     |LEE 040#4=;
     |SI FSalida ! 0;
         |DDEFECTO #4;
         #4#0 = #3#0;
         #4#1 = #3#1;
         #4#2 = #3#2;
         #4#3 = #2#2;
         #4#4 = 0;

         #6#0 = #4#0;
         #6#1 = #4#3;
         |LEE 040#6=;
         |SI FSalida = 0;
             #4#5  = #6#9;
             #4#16 = #6#12;
         |SINO;
             #5#0 = #4#3;
             |LEE 040#5=;
             |SI FSalida ! 0;  |DDEFECTO #5;  |FINSI;

             #4#5  = #5#8;
             #4#16 = #5#11;
         |FINSI;

         |SI #4#3 = 180;  |O #4#3 = 190;  |O #4#3 = 390;  |O #4#3 = 392;
          |O #4#3 = 193;  |O #4#3 = 184;  |O #4#3 = 296;
             #4#5  = "EMPRESA";
         |FINSI;

         |GRABA 020#4n;
         |LIBERA #4;
     |FINSI;

     |SI #4#5 ! "EMPRESA   ";
         |HAZ GrabaTrabCom;
     |FINSI;
|FPRC;

|PROCESO CreaPlaning;
     |SI #2#2 = 309;  |ACABA;  |FINSI;

     |SI nPeriodo = 12;
         |SI #2#2 = 370;  |O #2#2 = 310;
             || Crea el 370 para el Ultimo Trimestre
         |SINO;
             fFecha = aAlfa1;
             |SI fHFecha < fFecha;  |ACABA;  |FINSI;

             fFecha = aAlfa2;
             |SI fDFecha > fFecha;  |ACABA;  |FINSI;
         |FINSI;
     |SINO;
         fFecha = aAlfa1;
         |SI fHFecha < fFecha;  |ACABA;  |FINSI;

         fFecha = aAlfa2;
         |SI fDFecha > fFecha;  |ACABA;  |FINSI;
     |FINSI;

     |ABRE #3;
     |ABRE #4;
     |ABRE #8;

     #3#0 = #2#0;
     #3#1 = #0#14;
     #3#2 = nPeriodo;
     |LEE 101#3=;
     |SI FSalida ! 0;
         #3#0 = #2#0;
         #3#1 = #0#14;
         #3#2 = nPeriodo;
         |GRABA 020#3b;
     |FINSI;

     |SI nPeriodo = 1;   #3#3 = "ENERO     ";  |FINSI;
     |SI nPeriodo = 2;   #3#3 = "FEBRERO   ";  |FINSI;
     |SI nPeriodo = 3;   #3#3 = "MARZO     ";  |FINSI;
     |SI nPeriodo = 4;   #3#3 = "ABRIL     ";  |FINSI;
     |SI nPeriodo = 5;   #3#3 = "MAYO      ";  |FINSI;
     |SI nPeriodo = 6;   #3#3 = "JUNIO     ";  |FINSI;
     |SI nPeriodo = 7;   #3#3 = "JULIO     ";  |FINSI;
     |SI nPeriodo = 8;   #3#3 = "AGOSTO    ";  |FINSI;
     |SI nPeriodo = 9;   #3#3 = "SEPTIEMBRE";  |FINSI;
     |SI nPeriodo = 10;  #3#3 = "OCTUBRE   ";  |FINSI;
     |SI nPeriodo = 11;  #3#3 = "NOVIEMBRE ";  |FINSI;
     |SI nPeriodo = 12;  #3#3 = "DICIEMBRE ";  |FINSI;
     |SI nPeriodo = 99;  #3#3 = "ANUAL     ";  |FINSI;
     |GRABA 020#3a;
     |LIBERA #3;

     |SI #2#2 ! 130;  |Y #2#2 ! 131;
         |HAZ TipoNormal;
     |SINO;
         |HAZ TipoComunidad;
     |FINSI;

     |CIERRA #3;
     |CIERRA #4;
     |CIERRA #8;
|FPRC;

|PROCESO Calculo;
     #99#0 = #2#0;
     |LEE 101#99=;
     |SI FSalida ! 0;
         |DDEFECTO #99;
         #99#0 = #2#0;
         |GRABA 020#99b;
     |FINSI;
     #99#1 = "S";
     |GRABA 020#99a;
     |LIBERA #99;

     |ABRE #100;
     #100#0 = #2#0;
     |LEE 040#100=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;
     |CIERRA #100;

     #114#0 = #2#0;
     #114#1 = #2#1;
     |LEE 040#114=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI #100#94 > "01.01.0000";
         |SI #114#6 < "01.01.1900";
             #114#6 = #100#94;
         |FINSI;
     |FINSI;

     aAlfa   = "31.12." + #0#14;
     fHFecha = aAlfa;

     aAlfa   = "01.01." + #0#14;
     fDFecha = aAlfa;

     aAlfa   = #2#8 % -104;
     |SI aAlfa ! "0000";
         nNume   = aAlfa;
         |SI nNume > #0#14;  |ACABA;  |FINSI;

         |SI #2#8 > fDFecha;  fDFecha = #2#8;  |FINSI;
     |FINSI;

     aAlfa   = #2#9 % -104;
     |SI aAlfa ! "0000";
         nNume   = aAlfa;
         |SI nNume < #0#14;  |ACABA;  |FINSI;

         |SI #2#9 < fHFecha;  fHFecha = #2#9;  |FINSI;
     |FINSI;

     |ABRE #5;
     |ABRE #6;

     |SI #2#7 = "TRIMESTRAL";
         nPeriodo = 3;
         aAlfa1 = "01.01." + #0#14;
         aAlfa2 = "31.03." + #0#14;
         |HAZ CreaPlaning;

         nPeriodo = 6;
         aAlfa1 = "01.04." + #0#14;
         aAlfa2 = "30.06." + #0#14;
         |HAZ CreaPlaning;

         nPeriodo = 9;
         aAlfa1 = "01.07." + #0#14;
         aAlfa2 = "30.09." + #0#14;
         |HAZ CreaPlaning;

         nPeriodo = 12;
         aAlfa1 = "01.10." + #0#14;
         aAlfa2 = "31.12." + #0#14;
         |HAZ CreaPlaning;
     |FINSI;

     |SI #2#7 = "MENSUAL   ";
         nPeriodo = 1;
         aAlfa1 = "01.01." + #0#14;
         aAlfa2 = "31.01." + #0#14;
         |HAZ CreaPlaning;

         nPeriodo = 2;
         aAlfa1 = "01.02." + #0#14;
         aAlfa2 = "29.02." + #0#14;
         |HAZ CreaPlaning;

         nPeriodo = 3;
         aAlfa1 = "01.03." + #0#14;
         aAlfa2 = "31.03." + #0#14;
         |HAZ CreaPlaning;

         nPeriodo = 4;
         aAlfa1 = "01.04." + #0#14;
         aAlfa2 = "30.04." + #0#14;
         |HAZ CreaPlaning;

         nPeriodo = 5;
         aAlfa1 = "01.05." + #0#14;
         aAlfa2 = "31.05." + #0#14;
         |HAZ CreaPlaning;

         nPeriodo = 6;
         aAlfa1 = "01.06." + #0#14;
         aAlfa2 = "30.06." + #0#14;
         |HAZ CreaPlaning;

         nPeriodo = 7;
         aAlfa1 = "01.07." + #0#14;
         aAlfa2 = "31.07." + #0#14;
         |HAZ CreaPlaning;

         nPeriodo = 8;
         aAlfa1 = "01.08." + #0#14;
         aAlfa2 = "31.08." + #0#14;
         |HAZ CreaPlaning;

         nPeriodo = 9;
         aAlfa1 = "01.09." + #0#14;
         aAlfa2 = "30.09." + #0#14;
         |HAZ CreaPlaning;

         nPeriodo = 10;
         aAlfa1 = "01.10." + #0#14;
         aAlfa2 = "31.10." + #0#14;
         |HAZ CreaPlaning;

         nPeriodo = 11;
         aAlfa1 = "01.11." + #0#14;
         aAlfa2 = "30.11." + #0#14;
         |HAZ CreaPlaning;

         nPeriodo = 12;
         aAlfa1 = "01.11." + #0#14;
         aAlfa2 = "31.12." + #0#14;
         |HAZ CreaPlaning;
     |FINSI;

     |SI #2#7 = "ANUAL     ";
         nPeriodo = 99;
         aAlfa1 = "01.01." + #0#14;
         aAlfa2 = "31.12." + #0#14;
         |HAZ CreaPlaning;
     |FINSI;

     |CIERRA #5;
     |CIERRA #6;
|FPRC;

|DEFBUCLE dsmom002;
     #2#1;
     ;
     nDEmpresa, 00, 000;
     nHEmpresa, 99, 999;
     ;
     NULL, Calculo;
|FIN;

|PROCESO Comuneros;
     nDEmpresa = #99#0;
     nHEmpresa = #99#0;
     |BUCLE dsmom004;    || Borrado del Planing
     |BUCLE dsmom002;    || Calculo del Planing
     |BUCLE dsmom003;    || Filtros del Planing
|FPRC;

|DEFBUCLE dsmow003;
     #99#1;
     1;
     00000, "N";
     99999, "N";
     ;
     NULL, Comuneros;
|FINSI;

|PROCESO RecalculaFechas;
     |ABRE #114;
     #114#0 = #2#0;
     #114#1 = #2#1;
     |LEE 040#114=;
     |SI FSalida ! 0;
         |BORRA 020#2a;
         |LIBERA #2;

         |CIERRA #114;
         |ACABA;
     |FINSI;
     |CIERRA #114;

     #2#8  = #114#5;
     #2#9  = #114#6;
     #2#10 = 1;
     |GRABA 020#2a;
     |LIBERA #2;
|FPRC;

|DEFBUCLE dsmom002R;
     #2#1;
     10;
     00000, 00, 000, 0;
     99999, 99, 999, 0;
     be;
     NULL, RecalculaFechas;
|FIN;

|PROCESO PonFechas;
     |ABRE #2;
     |LEE 040#2p;
     |SI FSalida ! 0;  |ACABA;  |FINSI;
     |CIERRA #2;

     |SI #2#10 = 0;
         |BUCLE dsmom002R;   || Recalculo de Fechas;
     |FINSI;
|FPRC;

|PROCESO Tipo3;  |TIPO 3;
     |HAZ PonFechas;

     aFichero = ("00" + Usuario) % 108;
     |NOME_DAT #99 aFichero;
     |ABRE #99;  |CIERRA #99;  |DELINDEX #99;

     |ABRE #114;
     |ABRE #99;
     |SI #0#0 ! 0;
         nDEmpresa = #0#0;
         nHEmpresa = #0#1;
         |BUCLE dsmom004;    || Borrado del Planing
         |BUCLE dsmom002;    || Calculo del Planing
         |BUCLE dsmom003;    || Filtros del Planing
     |SINO;
         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               nDEmpresa = #0nCampo;
               nHEmpresa = #0nCampo;
               |BUCLE dsmom004;    || Borrado del Planing
               |BUCLE dsmom002;    || Calculo del Planing
               |BUCLE dsmom003;    || Filtros del Planing
         |SIGUE;
     |FINSI;
     |CIERRA #99;

     |BUCLE dsmow003;

     |CIERRA #114;
     |DELINDEX #99;
|FPRC;

|PROGRAMA;
     |SI enEmpresaA ! 0;
         #0#0  = enEmpresaA;
         #0#1  = enEmpresaA;
         #0#14 = enAnyo;
         |HAZ Tipo3;
     |SINO;
          |MANTE #0;
     |FINSI;
|FPRO;
