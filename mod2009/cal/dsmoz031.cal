|FICHEROS;
  dsmoz031 #0;

  :/modelos/def/dsmom030 #30;
  :/modelos/def/dsmom031 #31;
  :/basica/def/agitab99  #99;

  dsmow077 #977,MANTE;
|FIN;

|VARIABLES;
  aFichero   = "";
  nInkey     = 0;
  aAlfa      = "";
  nEstado    = 0;
  nModificar = 0;
|FIN;


|INCLUYE i_variar;
|INCLUYE i_varcon;

|| ***************************************************************
|PROCESO LosConceptos;

 #977#0 = #0#0;
 #977#1 = #30#0;
 #977#2 = #30#1;
 #977#3 = "no";
 #977#4 = #0#1;

 enSwSelCp  = 1;
 enModelo   = #977#0;
 enConcepto = #977#1;
 |HAZ CtrlConcepto;
 |SI enSwConcep = 0;
     #977#3 = "SI";
 |FINSI;
 |GRABA 040#977n;
|FPRC;

|DEFBUCLE LosConceptos;
 #30#1;
 ;
 000;
 999;
 e;
 NULL, LosConceptos;
|FIN;

|PROCESO LeeLosMod;
 #0#0 = #99#0;
 #0#1 = #99#1;
 |GRABA 040#0n;

 |ABRE #977;
 |BUCLE LosConceptos;
 |CIERRA #977;
|FPRC;

|DEFBUCLE agitab99;
 #99#1;
 ;
 900;
 999;
 e;
 NULL, LeeLosMod;
|FIN;

|| *******************************************************************
|CALCULO Tipo5w77;  |TIPO 5;
       |LEE 040#977p;
       #977#3 = "no";
       |LINEAS_BI_ATRI #977#3, D, S, -1;
|FCAL;

|PROCESO Tipo11w77; |TIPO 11;
|FPRC;

|PROCESO Tipo7w77;  |TIPO 13;
  aAlfa = "    CONSULTA";
  |SI enModo = 2;
      |SI #977#3 = "SI";
          aAlfa = "    <F2> - Quitar de Libro";
      |SINO;
          aAlfa = "    <F2> - Incorporar al Libro";
      |FINSI;
  |FINSI;
  |MENSA aAlfa;
|FPRC;

|PROCESO Tipo0w77;  |TIPO 0;
  |SI enModo = 7; |ACABA; |FINSI;

  nInkey = FSalida;
  |SI nInkey = 10;
      |SI #977#3 = "SI";
          #977#3 = "no";
      |SINO;
          #977#3 = "SI";
      |FINSI;
      |PINTA #977#3;
      |GRABA 040#977a;
      nModificar = 1;
  |FINSI;
|FPRC;

|| *******************************************************************
|RUTINA ClaveBaja977;
 #977#0 = #0#0;
 #977#1 = 000;
 #977#3 = "  ";
|FRUT;

|RUTINA ClaveAlta977;
 #977#0 = #0#0;
 #977#1 = 999;
 #977#3 = "zz";
|FRUT;

|PROCESO Tipo7z31;  |TIPO 7;
     enModo = 7;
     |ENTLINEAL #2#977, -3, 7, 22, 0, ClaveBaja977, ClaveAlta977;
|FPRC;

|PROCESO Tipo11z31;  |TIPO 11;
   nInkey = FSalida;
   |SI nInkey =  10;
       enModo = 2;
       |ENTLINEAL #1#977, -2, 4, 22, 0, ClaveBaja977, ClaveAlta977;
   |FINSI;
|FPRC;

|PROCESO Tipo0z31;  |TIPO 0;
    enModo = 7;
    |ENTLINEAL #2#977, -3, 4, 22, 0, ClaveBaja977, ClaveAlta977;
|FPRC;

|| ****************************************************************
|PROCESO LeeElAuxiliar;
 #31#0 = #977#1;  || Concepto
 #31#1 = #977#0;  || Libro;
 |LEE 141#31=;
 nEstado = FSalida;

 |SI #977#3 = "SI";
     |SI nEstado ! 0;
         |DDEFECTO #31;
         #31#0 = #977#1;
         #31#1 = #977#0;
         #31#2 = #977#4;
         |GRABA 020#31n;
     |FINSI;
 |SINO;
     |SI nEstado = 0;
         |BORRA 020#31a;
     |FINSI;
 |FINSI;
 |LIBERA #31;
|FPRC;

|DEFBUCLE LeeElAuxiliar;
 #977#1;
 ;
 000, 000;
 999, 999;
 e;
 NULL, LeeElAuxiliar;
|FIN;

|PROCESO Cambiar;
 |ABRE #31;
 |BUCLE LeeElAuxiliar;
 |CIERRA #31;
|FPRC;

|| ****************************************************************
|RUTINA ClaveBaja0;
 #0#0 = 0;
|FRUT;

|RUTINA ClaveAlta0;
 #0#0 = 999;
|FRUT;

|PROGRAMA;

  aFichero = ("31z" + Usuario) % 108;
  |NOME_DAT #0 aFichero;
  aFichero = ("77w" + Usuario) % 108;
  |NOME_DAT #977 aFichero;

  |ABRE #0;   |CIERRA #0;   |DELINDEX #0;
  |ABRE #977; |CIERRA #977; |DELINDEX #977;

  |ABRE #0;
  |BUCLE agitab99;
  |CIERRA #0;

  nModificar = 0;
  |CLS;
  |PINPA #0#0;
  |ENTLINEAL #1#0, -1, 4, 22, 0, ClaveBaja0, ClaveAlta0;
  |SI nModificar = 1;
      |CONFI "    SConforme con los cambios realizados S/N";
      |SI FSalida = 0;
          |HAZ Cambiar;
      |FINSI;
  |FINSI;
  |DELINDEX #0;
  |DELINDEX #977;
|FPRO;
|| ****************************************************************
