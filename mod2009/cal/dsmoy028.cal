|FICHEROS;
     dsmoy028 #0;

     dsmom027 #27;
     dsmom029 #29;
     dsmom028 #28;
     dsmom047 #47;

     dsmom080 #80;
     dsmom082 #82;
     :/basica/def/agifigen #118;
|FIN;

|VARIABLES;
     nDEmpresa = 0;
     nHEmpresa = 0;
     aDNom     = "";
     aHNom     = "";
     nEjer          = 0;
     nDPeriodo      = 0;
     nHPeriodo      = 0;
     nDper          = 0;
     nHper          = 0;

     nCampo         = 0;

     nSwAlgun    = 0;
     nSwPrint    = 0;
     aAlfa       = "";
     aAlfa1      = "";
     aAlfa2      = "";
     aAlfa3      = "";
     aAlfa4      = "";
     fLaFecha    = @;
     &enImp1     = 0;
     &enImp2     = 0;
     &enImp3     = 0;
     &enImp4     = 0;
     &enImp5     = 0;
     &enImp6     = 0;
     &enImp7     = 0;
     &aEmpresa   = "";
     &eaNom      = "";
     &eaNif      = "";
     &nSwFich    = 0;
     aParam      = "";
     informe     = "";
     inform1     = "dsmoy028";
     inform2     = "dsmoym28";
     inform3     = "dsmoyt28";
     nSwSal      = 0;

|FIN;

|INCLUYE i_variar;

|| /////////////////////// PROCESOS ENTRADA POR PANTALLA \\\\\\\\\\\\\\\\\\\\
|CALCULO Eano; |TIPO 7;
 fLaFecha = ~;
 aAlfa1 = fLaFecha; aAlfa1 = aAlfa1 % -104;
 #0#3 = aAlfa1;
 |PINTA #0#3;
|FCAL;

|PROCESO Periodo;
     #0#2 = "          ";
     |SI #0#1 = 1;   #0#2 = "ENERO     ";  |FINSI;
     |SI #0#1 = 2;   #0#2 = "FEBRERO   ";  |FINSI;
     |SI #0#1 = 3;   #0#2 = "MARZO     ";  |FINSI;
     |SI #0#1 = 4;   #0#2 = "ABRIL     ";  |FINSI;
     |SI #0#1 = 5;   #0#2 = "MAYO      ";  |FINSI;
     |SI #0#1 = 6;   #0#2 = "JUNIO     ";  |FINSI;
     |SI #0#1 = 7;   #0#2 = "JULIO     ";  |FINSI;
     |SI #0#1 = 8;   #0#2 = "AGOSTO    ";  |FINSI;
     |SI #0#1 = 9;   #0#2 = "SEPTIEMBRE";  |FINSI;
     |SI #0#1 = 10;  #0#2 = "OCTUBRE   ";  |FINSI;
     |SI #0#1 = 11;  #0#2 = "NOVIEMBRE ";  |FINSI;
     |SI #0#1 = 12;  #0#2 = "DICIEMBRE ";  |FINSI;
     |SI #0#1 = 99;  #0#2 = "ANUAL     ";  |FINSI;

     |SI #0#2 = "          ";
         |MENAV "     Periodo Incorrecto.";
         |ERROR;
     |FINSI;
     |PINTA #0#2;
|FPRC;

|PROCESO Parrilla;
     |SI #0#4 = 0;
         #0#4 = 0;  |PINTA #0#4;
         #0#5 = 0;  |PINTA #0#5;  |C_M #0#5, 1, "N";
         |PARA nCampo = 6;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#4, 1, "S";
             |C_M #0#5, 1, "S";
         |FINSI;

         |PARA nCampo = 6;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|| /////////////////////// PROCESOS DE TRABAJO \\\\\\\\\\\\\\\\\\\\
|PROCESO Leem028;
   |SI #28#7 = "S";
       enImp6 = enImp6 + #28#6;
   |FINSI;
|FPRC;

|DEFBUCLE Leem028;
     #28#1;
     ;
     enEmpresa, enEjer, nDPeriodo, 000, 00, 00;
     enEmpresa, enEjer, nHPeriodo, 999, 99, 99;
     ;
     NULL, Leem028;
|FIN;

|PROCESO LeePrescrip;   ||Prorrata Nueva
    nSwFich = 3;
    |BUCLE Leem028;
    |SI enImp6 ! 0;
        nSwPrint = 1;
   |FINSI;
|FPRC;

|PROCESO Leem047;
   enImp5 = enImp5 + #47#14;
|FPRC;

|DEFBUCLE Leem047;
     #47#1;
     ;
     enEmpresa, enEjer, nDPeriodo, 000, 00, 00;
     enEmpresa, enEjer, nHPeriodo, 999, 99, 99;
     ;
     NULL, Leem047;
|FIN;

|PROCESO LeeManual;   ||Prorrata Nueva
    nSwFich = 2;
    |BUCLE Leem047;
    |SI enImp5 ! 0;
        nSwPrint = 1;
   |FINSI;
|FPRC;


|PROCESO Leem082;
   |SI #82#2 = 12; enImp1 = 0; |FINSI;
   enImp1 = enImp1 + #82#3;
   enImp2 = enImp2 + #82#39;
   enImp4 = enImp4 + #82#8;
|FPRC;

|DEFBUCLE Leem082;
     #82#1;
     ;
     enEmpresa, enEjer, nDper;
     enEmpresa, enEjer, nHper;
     ;
     NULL, Leem082;
|FIN;

|PROCESO Leem080;   ||Prorrata Nueva

    nSwFich = 1;
    |ABRE #80;
    |DDEFECTO #80;
    #80#0 = enEmpresa;
    #80#1 = enEjer;
    |LEE 001#80=;
    |SI FSalida = 0;
        |BUCLE Leem082;
        enImp3 = enImp1 - enImp2;
        |SI enImp3 ! 0; |O enImp4 ! 0;
            nSwPrint = 1;
        |FINSI;
   |FINSI;
   |CIERRA #80;
|FPRC;

|PROCESO Leem029;
     enImp1 = enImp1 + #29#3;
     enImp2 = enImp2 + #29#5;
     enImp4 = enImp4 + #29#8;
|FPRC;

|DEFBUCLE Leem029;
     #29#1;
     ;
     enEmpresa, enEjer, nDPeriodo, 00;
     enEmpresa, enEjer, nHPeriodo, 00;
     ;
     NULL, Leem029;
|FIN;

|PROCESO Leem027;   || Prorrata Anterior
    nSwFich = 0;
    |ABRE #27;
    |DDEFECTO #27;
    #27#0 = enEmpresa;
    #27#1 = enEjer;
    |LEE 001#27=;
    |SI FSalida = 0;
        |BUCLE Leem029;
        enImp3 = enImp1 - enImp2;
        |SI enImp3 ! 0; |O enImp4 ! 0;
             nSwPrint = 1;
        |FINSI;
    |FINSI;
    |CIERRA #27;
|FPRC;


|PROCESO LEmpresa;
  nSwSal = 0;
  |SI #0#4 = 0;
      nSwSal = 1;
      |PARA nCampo = 6;  |SI nCampo [ 17;  |HACIENDO nCampo = nCampo + 1;
            |SI #0nCampo ! 0;
                |SI #0nCampo = #118#0;
                    nSwSal = 0;
                    |SAL;
                |FINSI;
            |FINSI;
      |SIGUE;
  |FINSI;
  |SI nSwSal = 1; |ACABA; |FINSI;

  enEmpresa = #118#0;
  aEmpresa = #118#0; |QBF aEmpresa;
  aEmpresa = ("00000" + aEmpresa) % -105;
  eaNom    = #118#1;
  eaNif    = #118#13;

  nSwPrint = 0;
  enImp1 = 0;   || iva soportado deducible
  enImp2 = 0;   || iva deducido
  enImp3 = 0;   || diferencia
  enImp4 = 0;   || subvenciones
  enImp5 = 0;   || manual
  enImp6 = 0;   || prescripcion
  enImp7 = 0;   || total perdida de IVA

  |SI aParam = "P"; |O aParam = "T";
      |SI enEjer < enEjerPro; |HAZ Leem027; |FINSI;
      |SI enEjer ] enEjerPro; |HAZ Leem080; |FINSI;
  |FINSI;
  |SI aParam = "M"; |O aParam = "T";
      |HAZ LeeManual;
  |FINSI;
  |SI aParam = "C"; |O aParam = "T";
      |HAZ LeePrescrip;
  |FINSI;

  |SI nSwPrint = 1;
      enImp7 = enImp3 + enImp5 + enImp6 + enImp4;
      |PRINT;
      nSwAlgun = 1;
  |FINSI;
|FPRC;

|DEFBUCLE agifigen1;
 #118#2;
 0;
 aDNom, nDEmpresa;
 aHNom, nHEmpresa;
 e;
 NULL, LEmpresa;
|FIN;

|DEFBUCLE agifigen;
 #118#1;
 ;
 nDEmpresa;
 nHEmpresa;
 e;
 NULL, LEmpresa;
|FIN;

|PROCESO Tipo3;  |TIPO 3;

     |SI aParam = "T"; |O aParam = "P";
         |HAZ LeeCtrlProrr;
     |FINSI;

     aDNom = " " * 40;
     aHNom = "z" * 40;
     nDPeriodo = #0#1; nDper = #0#1;
     nHPeriodo = #0#1; nHper = #0#1;
     |SI #0#1 = 12; |O #0#1 = 99; nDper = 01; nHper = 12; |FINSI;
     |SI #0#1 = 99;  nDPeriodo = 01; nHPeriodo = 12; |FINSI;
     enEjer = #0#3;

     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |INFOR informe;
     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

     |SI #0#4 ! 0;
         nDEmpresa = #0#4;
         nHEmpresa = #0#5;
     |SINO;
         nDEmpresa = 00001;
         nHEmpresa = 99999;
     |FINSI;
     |SI #0#21 = "C"; |BUCLE agifigen; |FINSI;
     |SI #0#21 = "N"; |BUCLE agifigen1; |FINSI;

     |SI nSwAlgun = 1;
         |PIEINF;
     |FINSI;
     |FININF;
     |FINIMP;
|FPRC;
|| ************************************************************

|PROGRAMA;
  aParam = PARAMETRO; |QBF aParam;
  |SI aParam = ""; aParam = "P"; |FINSI;

  informe = inform1;
  aAlfa1 = "Informe Prorrata IVA";
  aAlfa2 = "Informe Empresas que han aplicado la Prorrata IVA";
  |SI aParam = "M";
      informe = inform2;
      aAlfa1 = "Informe Regularizacion Manual IVA";
      aAlfa2 = "Informe Empresas con Regularizacion Manual IVA";
  |FINSI;
  |SI aParam = "C";
      informe = inform2;
      aAlfa1 = "Informe Prescripcion IVA";
      aAlfa2 = "Informe Empresas con Prescripcion de IVA";
  |FINSI;
  |SI aParam = "T";
      informe = inform3;
      aAlfa1 = "Informe Variacion del Importe deducible de IVA";
      aAlfa2 = "Informe Empresas con Reg. Manual, Prorrata o/y Prescripcion IVA";
  |FINSI;

  |ABRE #0;
  |LEE 041#0p;
  |SI FSalida ! 0;
      |DDEFECTO #0;
      |GRABA 020#0c;
  |FINSI;

  |CLS;
  |CABEZA aAlfa1;

  |PINPA #0#0;
  |ATRI R;
  |SI aParam ! "T";
      |PINTA 0618, aAlfa2;
  |SINO;
      |PINTA 0609, aAlfa2;
  |FINSI;
  |ATRI 0;
  aAlfa2 = aAlfa2 > " ";
  #0#20 = aAlfa2;
  |PINDA #0#0;
  |ENDATOS #1#0;
  |CIERRA #0;
|FPRO;
