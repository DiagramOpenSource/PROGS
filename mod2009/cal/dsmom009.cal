|FICHEROS;
     dsmom009 #9,MANTE;
     dsmom001 #1;
     dsmom004 #4;
     dsmom008 #8;
     dsmom010 #10;
     dsmom011 #11;
     dsmom012 #12;
     dsmom013 #13;
     dsmociva #300;
     dsmoc130 #130;
     dsmow006 #998;
|FIN;

|VARIABLES;
     aPathModelo = "";
     aDef        = "";
     aAlfa       = "";
     aLong       = "";
     aCampo      = "";

     nCampo      = 0;
     nPosi       = 0;
     nPeriodoMin = 0;
     nLong       = 0;
     nConta      = 0;
     nIVA        = 0;
     nTri        = 0;
     nSwIva      = 0;
     nSw130      = 0;
     nTecla      = 0;
     nBoton1     = 0;
     nBoton2     = 0;
|FIN;

|INCLUYE i_variar;

|PROCESO Borra10;
     |BORRA 020#10a;
     |LIBERA #10;
|FPRC;

|DEFBUCLE dsmom010B;
     #10#1;
     ;
     #9#0, #9#1, #9#2, #9#3, #9#4, "    ";
     #9#0, #9#1, #9#2, #9#3, #9#4, "zzzz";
     be;
     NULL, Borra10;
|FIN;

|PROCESO Borra11;
     |BORRA 020#11a;
     |LIBERA #11;
|FPRC;

|DEFBUCLE dsmom011B;
     #11#1;
     ;
     #9#0, #9#1, #9#2, #9#3, #9#4, "    ";
     #9#0, #9#1, #9#2, #9#3, #9#4, "zzzz";
     be;
     NULL, Borra11;
|FIN;

|PROCESO Borra12;
     |BORRA 020#12a;
     |LIBERA #12;
|FPRC;

|DEFBUCLE dsmom012B;
     #12#1;
     ;
     #9#0, #9#1, #9#2, #9#3, #9#4, "    ";
     #9#0, #9#1, #9#2, #9#3, #9#4, "zzzz";
     be;
     NULL, Borra12;
|FIN;

|PROCESO Borra13;
     |BORRA 020#13a;
     |LIBERA #13;
|FPRC;

|DEFBUCLE dsmom013B;
     #13#1;
     ;
     #9#0, #9#1, #9#2, #9#3, #9#4, "    ";
     #9#0, #9#1, #9#2, #9#3, #9#4, "zzzz";
     be;
     NULL, Borra13;
|FIN;

|PROCESO BajaIva;
     |DDEFECTO #8;
     |ABRE #8;
     #8#0 = #9#0;
     #8#1 = #9#1;
     #8#2 = #9#2;
     |SI #9#7  = "X";  #8#3 = 300;  |FINSI;
     |SI #9#8  = "X";  #8#3 = 370;  |FINSI;
     |SI #9#9  = "X";  #8#3 = 320;  |FINSI;
     |SI #9#10 = "X";  #8#3 = 330;  |FINSI;
     |SI #9#11 = "X";  #8#3 = 332;  |FINSI;
     #8#4 = 0;
     #8#5 = #9#4;
     |LEE 101#8=;
     |SI FSalida = 0;
         #8#11 = "SIN DATOS";
         |GRABA 020#8a;

         enEmpresa     = #8#0;
         enEjer        = #8#1;
         enPeriodo     = #8#2;
         enModelo      = #8#3;
         enComplem     = #8#4;
         enActividad   = #8#5;
         eaComunidad   = #8#9;
         eaComunero    = #8#10;
         enTipoEntrada = 97;
         |SUB_EJECUTA_NP "dsmociva";

         |ABRE #4;
         #4#0 = #9#0;
         #4#1 = #9#1;
         #4#2 = #9#2;
         #4#3 = enModelo;
         #4#4 = 0;
         |LEE 101#4=;
         |SI FSalida = 0;
             |HAZ ChequeaSituacion;  || Esta en el PLB

             |GRABA 020#4a;
             |LIBERA #4;
         |FINSI;
         |CIERRA #4;
     |FINSI;
     |LIBERA #8;
     |CIERRA #8;
|FPRC;

|PROCESO Baja130;
     |DDEFECTO #8;
     |ABRE #8;
     #8#0 = #9#0;
     #8#1 = #9#1;
     #8#2 = #9#2;
     #8#3 = 130;
     #8#4 = 0;
     #8#5 = #9#4;
     |LEE 101#8=;
     |SI FSalida = 0;
         #8#11 = "SIN DATOS";
         |GRABA 020#8a;

         enEmpresa     = #8#0;
         enEjer        = #8#1;
         enPeriodo     = #8#2;
         enModelo      = #8#3;
         enComplem     = #8#4;
         enActividad   = #8#5;
         eaComunidad   = #8#9;
         eaComunero    = #8#10;
         enTipoEntrada = 3;
         |SUB_EJECUTA_NP "dsmoc130";

         |ABRE #4;
         #4#0 = #9#0;
         #4#1 = #9#1;
         #4#2 = #9#2;
         #4#3 = enModelo;
         #4#4 = 0;
         |LEE 101#4=;
         |SI FSalida = 0;
             |HAZ ChequeaSituacion;  || Esta en el PLB

             |GRABA 020#4a;
             |LIBERA #4;
         |FINSI;
         |CIERRA #4;
     |FINSI;
     |LIBERA #8;
     |CIERRA #8;
|FPRC;

|PROCESO BajaPlanilla;
     |SI nSw130 = 2;  |O nSwIva = 2;
         |CONFI "0000NModelos ya Impreso, Esta Seguro de la Baja : ";
         |SI FSalida ! 0;
             |ACABA;
         |FINSI;
     |SINO;
         |CONFI "0000NEsta seguro de dar de Baja la Planilla ";
         |SI FSalida ! 0;
             |ACABA;
         |FINSI;
     |FINSI;

     |BUCLE dsmom010B;
     |BUCLE dsmom011B;
     |BUCLE dsmom012B;
     |BUCLE dsmom013B;

     |LEE 101#9=;
     |SI FSalida = 0;
         |PARA nCampo = 17;  |SI nCampo [ 47;  |HACIENDO nCampo = nCampo + 1;
               |SI nCampo ! 29;  |Y nCampo ! 36;
                   #9nCampo = 0;
               |FINSI;
         |SIGUE;
         #9#57 = "SIN DATOS";
         #9#58 = 0;
         #9#59 = 0;
         |GRABA 020#9a;
     |FINSI;
     |LIBERA #9;

     |HAZ BajaIva;
     |HAZ Baja130;
|FPRC;

|PROCESO RecogeAnterior;
     #9#18 = 0;
     #9#21 = 0;
     #9#24 = 0;
     #9#27 = 0;
     #9#31 = 0;
     #9#34 = 0;
     #9#38 = 0;
     #9#41 = 0;

     |ABRE #130;
     #130#0 = #9#0;
     #130#1 = #9#1;
     #130#2 = #9#2 - 3;
     #130#3 = 130;
     #130#4 = 0;
     #130#5 = #9#4;
     |LEE 000#130=;
     |PARA;  |SI FSalida = 0;  |HACIENDO;  || Recoge de la ultima linea del
          #9#18 = #130#8;                  || periodo
          #9#21 = #130#11;
          #9#24 = #130#14;
          #9#27 = #130#17;
          #9#31 = #130#21;
          #9#34 = #130#24;
          #9#38 = #130#28;
          #9#41 = #130#31;

          #130#4 = #130#4 + 1;
          |LEE 000#130=;
     |SIGUE;

     #9#43 = #130#32 + #130#33;

     |CIERRA #130;
|FPRC;

|PROCESO NoVisualizar;
     |QBF eaNoVisu;
     |SI eaNoVisu ! "";
         aLong = eaNoVisu % 0;
         nLong = aLong;
         |PARA nConta = 1;  |SI nConta [ nLong;  |HACIENDO nConta = nConta + 3;
               nPosi  = (nConta * 100) + 3
               aCampo = eaNoVisu % nPosi;
               nCampo = aCampo;

               |C_V #998nCampo, 1, "N";
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO SiVisualizar;
     |QBF eaNoVisu;
     |SI eaNoVisu ! "";
         aLong = eaNoVisu % 0;
         nLong = aLong;
         |PARA nConta = 1;  |SI nConta [ nLong;  |HACIENDO nConta = nConta + 3;
               nPosi  = (nConta * 100) + 3
               aCampo = eaNoVisu % nPosi;
               nCampo = aCampo;

               |C_V #998nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO Boton1m009;
     |PTEC 824;
|FPRC;

|PROCESO Boton2m009;
     |PTEC 830;
|FPRC;

|PROCESO Tipo7m009;  |TIPO 7;
     |BLIN 04;
     |CONTROL_SIMPLE 0, "LBOTON,F2 Entrada de Datos ", 0460, 0480, Boton1m009;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F8 Baja Planilla    ", 0560, 0580, Boton2m009;
     nBoton2 = FSalida;
|FPRC;

|PROCESO Tipo0m009;  |TIPO 0;
     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |PULSATECLA;
|FPRC;

|PROCESO MiraComunero;
     #4#0 = #1#3;
     #4#1 = #9#1;
     #4#2 = #9#2;
     #4#3 = 130;
     #4#4 = 0;
     |LEE 101#4=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI #4#9  = "X";  nSw130 = 2;  |FINSI;
     |SI #4#11 = "X";  nSw130 = 1;  |FINSI;
     |SI #4#13 = "X";  nSw130 = 1;  |FINSI;

     |SI nSw130 ! 0;  |ERROR10;  |FINSI;
|FPRC;

|DEFBUCLE dsmom001;
     #1#1;
     ;
     #9#0, #9#4, 0001;
     #9#0, #9#4, 9999;
     ;
     NULL, MiraComunero;
|FIN;

|PROCESO Tipo11m009;  |TIPO 11;
     nTecla = FSalida;

     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |PULSATECLA;

     |SI nTecla ! 10;  |Y nTecla ! 16;  |ACABA;  |FINSI;

     nSw130 = 0;
     nSwIva = 0;

     |ABRE #4;
     |SI #9#56 ! 8;  |Y #9#56 ! 9;  |Y #9#56 ! 10;
      |Y #9#56 ! 11; |Y #9#56 ! 12; |Y #9#56 ! 18;
         #4#0 = #9#0;
         #4#1 = #9#1;
         #4#2 = #9#2;
         #4#3 = 130;
         #4#4 = 0;
         |LEE 101#4=;
         |SI FSalida ! 0;  |DDEFECTO #4;  |FINSI;
     |SINO;
         |BUCLE dsmom001;
     |FINSI;

     |SI #4#9  = "X";  nSw130 = 2;  |FINSI;
     |SI #4#11 = "X";  nSw130 = 1;  |FINSI;
     |SI #4#13 = "X";  nSw130 = 1;  |FINSI;

     #4#0 = #9#0;
     #4#1 = #9#1;
     #4#2 = #9#2;
     |SI #9#7  = "X";  #4#3 = 300;  |FINSI;
     |SI #9#8  = "X";  #4#3 = 370;  |FINSI;
     |SI #9#9  = "X";  #4#3 = 320;  |FINSI;
     |SI #9#10 = "X";  #4#3 = 330;  |FINSI;
     |SI #9#11 = "X";  #4#3 = 332;  |FINSI;
     #4#4 = 0;
     |LEE 101#4=;
     |SI FSalida ! 0;  |DDEFECTO #4;  |FINSI;

     |SI #4#9  = "X";  nSwIva = 2;  |FINSI;
     |SI #4#11 = "X";  nSwIva = 1;  |FINSI;
     |SI #4#13 = "X";  nSwIva = 1;  |FINSI;
     |CIERRA #4;

     |SI nSwIva = 2;  |O nSw130 = 2;
         |CONFI "0000NModelo Ya impresos, desea continuar";
         |SI FSalida ! 0;  |ACABA;   |FINSI;
     |FINSI;

     |SI nSwIva = 1;  |O nSw130 = 1;
         |MENAV "     Tenemos los modelos Remesados o Archivados. ";
         |ACABA;
     |FINSI;

     |SI nTecla = 16;
         |HAZ BajaPlanilla;
         |HAZ Refresco;
         |ACABA;
     |FINSI;

     nPeriodoMin = 1;
     |SI #9#7  = "X";  nPeriodoMin = 3;  |FINSI;
     |SI #9#8  = "X";  nPeriodoMin = 3;  |FINSI;

     |SI #9#2 ! 3; |Y #9#14 = "X";
         |HAZ RecogeAnterior;
     |FINSI;

     |PARA nCampo = 0;  |SI nCampo [ 59;  |HACIENDO nCampo = nCampo + 1;
           #998nCampo = #9nCampo;
     |SIGUE;

     eaNoVisu = "";
     |PARA nCampo = 17;  |SI nCampo [ 44;  |HACIENDO nCampo = nCampo + 1;
          eaNoVisu = eaNoVisu + (("000" + nCampo) % -103);
     |SIGUE;

     |SI #9#14 = " ";
         |HAZ NoVisualizar;
     |SINO;
         |HAZ SiVisualizar;
         |SI #998#56 = 7;  |O #998#56 = 12;  |O #998#56 = 17;
             eaNoVisu = "029030033036037027031034038041028032035039042043";
             |HAZ NoVisualizar;
         |SINO;
             |SI #998#56 = 3;  |O #998#56 = 8;  |O #998#56 = 13;
                 eaNoVisu = "029030031032";
                 |HAZ NoVisualizar;

                 #998#29 = 0;
                 #998#30 = 0;
                 #998#31 = 0;
                 #998#32 = 0;
             |SINO;
                 |SI #998#29 = 0;  #998#29 = 5;  |FINSI;
             |FINSI;
          |FINSI;
     |FINSI;

     |PUSHV 0501 2380;
     |PINPA #0#998;
     |PINDA #0#998;
     |ENDATOS #1#998;
     |SI FSalida = 0;
         |LEE 101#9=;
         |PARA nCampo = 0;  |SI nCampo [ 59;  |HACIENDO nCampo = nCampo + 1;
               #9nCampo = #998nCampo;
         |SIGUE;

         nIVA  = -1;
         nTri  = -1;

         |SI #9#7 = "X"; |O #9#8 = "X";  |O  #9#9 = "X";
          |O #9#10 = "X";  |O #9#11 = "X";
             nIVA = 0;
             |SI #9#45 ! 0;  |O #9#46 ! 0;
                 nIVA = 1;
             |FINSI;
         |FINSI;

         |SI #9#14 = "X";
             nTri = 0;

             |SI #9#17 ! 0;    nTri = 1;   |FINSI;
             |SI #9#20 ! 0;    nTri = 1;   |FINSI;
             |SI #9#58 ! 0;    nTri = 1;   |FINSI;
             |SI #9#23 ! 0;    nTri = 1;   |FINSI;
             |SI #9#40 ! 0;    nTri = 1;   |FINSI;
             |SI #9#44 ! 0;    nTri = 1;   |FINSI;
         |FINSI;

         |SI nTri = -1;  |Y nIVA = -1;
             #9#57 = "COMPLETADO";
         |FINSI;

         |SI nTri = 1;  |Y nIVA = 1;
             #9#57 = "COMPLETADO";
         |FINSI;

         |SI nTri = -1;  |Y nIVA = 1;
             #9#57 = "COMPLETADO";
         |FINSI;

         |SI nTri = 1;  |Y nIVA = -1;
             #9#57 = "COMPLETADO";
         |FINSI;

         |SI nTri = 0;  |Y nIVA = 0;
             #9#57 = "SIN DATOS";
         |FINSI;

         |SI nTri = 0;  |Y nIVA = 1;
             #9#57 = "SIN TRIBUTA.";
         |FINSI;

         |SI nTri = 1;  |Y nIVA = 0;
             #9#57 = "SIN IVA";
         |FINSI;

         |GRABA 020#9a;
         |LIBERA #9;

         |SI #9#14 = "X";
             |ABRE #8;
             #8#0 = #9#0;
             #8#1 = #9#1;
             #8#2 = #9#2;
             #8#3 = 130;
             #8#4 = 0;
             #8#5 = #9#4;
             |LEE 040#8=;
             |SI FSalida ! 0;  |DDEFECTO #8;  |FINSI;
             |CIERRA #8;

             enEmpresa     = #8#0;
             enEjer        = #8#1;
             enPeriodo     = #8#2;
             enModelo      = #8#3;
             enComplem     = #8#4;
             enActividad   = #8#5;
             eaComunidad   = #8#9;
             eaComunero    = #8#10;
             enTipoEntrada = 98;
             |SUB_EJECUTA_NP "dsmoc130";

             |ABRE #4;
             #4#0 = #9#0;
             #4#1 = #9#1;
             #4#2 = #9#2;
             #4#3 = 130;
             #4#4 = 0;
             |LEE 101#4=;
             |SI FSalida = 0;
                 |HAZ ChequeaSituacion;  || Esta en el PLB

                 |GRABA 020#4a;
                 |LIBERA #4;
             |FINSI;
             |CIERRA #4;
         |FINSI;
     |FINSI;
     |POPV;

     |HAZ Refresco;
|FPRC;
