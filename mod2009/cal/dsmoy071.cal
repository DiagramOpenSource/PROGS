|FICHEROS;
     dsmoy071 #0;

     dsmom002 #2;
     dsmom004 #4;
     dsmom070 #70;

     dsmod309 #309;
     :/contasoc/def/isocie29 #202;

     dsmow071 #900;

     :/integral/def/dsam0001 #1;
     :/basica/def/agitab99 #199;

     &clientes@ #715;
     &bancosc@ #720;
|FIN;

|VARIABLES;
     nBanco    = 0;
     nLinea    = 0;
     nChequeo  = 0;
     nMes      = 0;
     nAnyo     = 0;
     nImpor    = 0;

     aFichero  = "";
     aAlfa     = "";
     aPathSoc  = "";

     fFecha    = @;

     &eaCuenta = "";
     &eaDirec  = "";
|FIN;

|INCLUYE i_variar;

|PROCESO dsmow071;
     |SI nBanco ! #900#0;  |Y nBanco ! 0;
         |PIEINF;
     |FINSI;

     nBanco = #900#0;

     |DDEFECTO #70;
     #70#0 = #0#0;
     #70#1 = #0#1;
     #70#2 = #900#0;

     enCodBanco = #900#0;
     |HAZ LeeBancoCliente;

     #70#3  = #720#2;
     #70#4  = #720#3;
     #70#5  = #720#1;
     #70#6  = #720#5;

     |PRINT;
|FPRC;

|DEFBUCLE dsmow071;
     #900#1;
     ;
      -1, 00001, "   ", 00;
     999, 99999, "zzz", 99;
     ;
     NULL, dsmow071;
|FIN;

|PROCESO dsmom002;
     |SI #2#7 = "MENSUAL   ";
         #900#13 = "M";
     |FINSI;

     |SI #2#7 = "TRIMESTRAL";
         #900#13 = "T";
     |FINSI;
|FPRC;

|DEFBUCLE dsmom002;
     #2#1;
     ;
     #900#1, 01, #0#0;
     #900#1, 98, #0#0;
     ;
     NULL, dsmom002;
|FIN;

|PROCESO MiraModelos;
     nBanco   = 1;
     enCodCli = #4#0;
     |HAZ LeePersona;

     enModelo = #4#3;  enForzar = 1;  |HAZ CogeDatosBancos;

     |SI enBanco    = 0;     nBanco = 0;  |FINSI;
     |SI eaFormaPago ! "2";  nBanco = 0;  |FINSI;

     |SI #4#15  [ 0;  |ACABA;  |FINSI;
     |SI nBanco [ 0;  |ACABA;  |FINSI;

     |DDEFECTO #900;
     #900#0  = enBanco;
     #900#5  = eaDC;
     #900#6  = eaCuenta;

     |SI enSwDeDonde = 1;
         #900#4  = #715#1;
         #900#11 = #715#3;
     |SINO;
         #900#4  = #715#1;
         #900#11 = #715#13;
     |FINSI;

     #900#1 = #4#0;
     #900#2 = #4#3;
     #900#3 = #4#4;
     #900#7 = #4#1;
     #900#8 = #4#2;

     |ABRE #199;
     #199#0 = #900#2;
     |LEE 040#199=;
     |SI FSalida ! 0;  |DDEFECTO #199;  |FINSI;
     |CIERRA #199;

     #900#9  = #199#1;
     #900#10 = #4#15;

     aAlfa   = #900#7;
     #900#12 = aAlfa % -102;

     #900#13 = #199#5;
     |BUCLE dsmom002;

     |SI #900#13 = "T  ";
         |SI #0#4 = 3;    #900#13 = "1T";  |FINSI;
         |SI #0#4 = 6;    #900#13 = "2T";  |FINSI;
         |SI #0#4 = 9;    #900#13 = "3T";  |FINSI;
         |SI #0#4 = 12;   #900#13 = "4T";  |FINSI;
     |SINO;
         #900#13 = (("00" + #0#4) % -102) + "M";
     |FINSI;

     |GRABA 020#900n;
     |LIBERA #900;
|FPRC;

|DEFBUCLE dsmom004;
     #4#1;
     9;
     00001, #0#6, #0#4, #0#0, 00, "X";
     99999, #0#6, #0#4, #0#0, 99, "X";
     ;
     NULL, MiraModelos;
|FIN;

|PROCESO MiraModelos309;
     nBanco   = 1;
     enCodCli = #309#0;
     |HAZ LeePersona;

     enModelo = 309;  enForzar = 1;  |HAZ CogeDatosBancos;

     |SI enBanco     = 0;    nBanco = 0;  |FINSI;
     |SI eaFormaPago ! "2";  nBanco = 0;  |FINSI;

     |SI #309#18 [ 0;  |ACABA;  |FINSI;
     |SI nBanco  [ 0;  |ACABA;  |FINSI;

     #900#0 = enBanco;
     #900#1 = #309#0;
     #900#2 = 309;
     #900#3 = #309#4;
     |LEE 101#900=;
     |SI FSalida ! 0;
         |DDEFECTO #900;
         #900#0  = enBanco;

         |SI enSwDeDonde = 1;
             #900#11 = #715#3;
         |SINO;
             #900#11 = #715#13;
         |FINSI;

         #900#1 = #309#0;
         #900#2 = 309;
         #900#3 = #309#4;
         |GRABA 020#900b;
     |FINSI;

     |SI enSwDeDonde = 1;
         #900#4 = #715#1;
         #900#5 = eaDC;
         #900#6 = eaCuenta;
     |SINO;
         #900#4 = #715#1;
         #900#5 = #715#83;
         #900#6 = #715#84;
     |FINSI;

     #900#7 = #0#6;
     #900#8 = #0#4;

     |ABRE #199;
     #199#0 = #900#2;
     |LEE 040#199=;
     |SI FSalida ! 0;  |DDEFECTO #199;  |FINSI;
     |CIERRA #199;

     #900#9  = #199#1;
     #900#10 = #900#10 + #309#18;

     aAlfa   = #900#7;
     #900#12 = aAlfa % -102;

     #900#13 = #199#5;
     |BUCLE dsmom002;

     |SI #900#13 = "T  ";
         |SI #0#4 = 3;    #900#13 = "1T";  |FINSI;
         |SI #0#4 = 6;    #900#13 = "2T";  |FINSI;
         |SI #0#4 = 9;    #900#13 = "3T";  |FINSI;
         |SI #0#4 = 12;   #900#13 = "4T";  |FINSI;
     |SINO;
         #900#13 = (("00" + #0#4) % -102) + "M";
     |FINSI;

     |GRABA 020#900a;
     |LIBERA #900;
|FPRC;

|DEFBUCLE dsmod309;
     #309#1;
     11;
     00001, 01, #0#6, #0#4, 00, 00, "X";
     99999, 98, #0#6, #0#4, 99, 99, "X";
     ;
     NULL, MiraModelos309;
|FIN;

|PROCESO isocie29;
     nImpor = 0;

     |SI #0#4 = 1;
         fFecha = #202#9;
         |SI fFecha [ "01.01.1900";
             |ACABA;
         |FINSI;

         nImpor = #202#6 + #202#24;
         |SI nImpor < 0;  nImpor = 0;  |FINSI;
     |FINSI;

     |SI #0#4 = 2;
         fFecha = #202#10;
         |SI fFecha [ "01.01.1900";
             |ACABA;
         |FINSI;

         nImpor = #202#7 + #202#25;
         |SI nImpor < 0;  nImpor = 0;  |FINSI;
     |FINSI;

     |SI #0#4 = 3;
         fFecha = #202#11;
         |SI fFecha [ "01.01.1900";
             |ACABA;
         |FINSI;

         nImpor = #202#8 + #202#26;
         |SI nImpor < 0;  nImpor = 0;  |FINSI;
     |FINSI;

     |SI nImpor [ 0;  |ACABA;  |FINSI;

     nBanco   = 1;
     enCodCli = #202#0;
     |HAZ LeePersona;

     enModelo = 202;  enForzar = 1;  |HAZ CogeDatosBancos;

     |SI enBanco     = 0;    nBanco = 0;  |FINSI;
     |SI eaFormaPago ! "2";  nBanco = 0;  |FINSI;

     |SI nBanco  [ 0;  |ACABA;  |FINSI;

     #900#0 = enBanco;
     #900#1 = #202#0;
     #900#2 = 202;
     #900#3 = 0;
     |LEE 101#900=;
     |SI FSalida ! 0;
         |DDEFECTO #900;
         #900#0  = enBanco;

         |SI enSwDeDonde = 1;
             #900#11 = #715#3;
         |SINO;
             #900#11 = #715#13;
         |FINSI;

         #900#1 = #202#0;
         #900#2 = 202;
         #900#3 = 0;
         |GRABA 020#900b;
     |FINSI;

     |SI enSwDeDonde = 1;
         #900#4 = #715#1;
         #900#5 = eaDC;
         #900#6 = eaCuenta;
     |SINO;
         #900#4 = #715#1;
         #900#5 = #715#83;
         #900#6 = #715#84;
     |FINSI;

     #900#7 = #0#6;
     #900#8 = #0#4;

     |ABRE #199;
     #199#0 = #900#2;
     |LEE 040#199=;
     |SI FSalida ! 0;  |DDEFECTO #199;  |FINSI;
     |CIERRA #199;

     #900#9  = #199#1;
     #900#10 = #900#10 + nImpor;

     aAlfa   = #900#7;
     #900#12 = aAlfa % -102;
     #900#13 = (("00" + #0#4) % -102) + "P";

     |GRABA 020#900a;
     |LIBERA #900;
|FPRC;

|DEFBUCLE isocie29;
     #202#1;
     ;
     00000, #0#6;
     99999, #0#6;
     ;
     NULL, isocie29;
|FIN;

|PROCESO Modelo202;
     |DBASS aPathSoc;  |QBF aPathSoc;
     aPathSoc = aPathSoc + "contasoc/fich/";
     |PATH_DAT #202 aPathSoc;

     |BUCLE isocie29;
|FPRC;

|PROCESO Tipo3;  |TIPO 3;
     |ABRE #1;
     |LEE 040#1p;
     |SI FSalida ! 0;  |DDEFECTO #1;  |FINSI;
     |CIERRA #1;

     eaDirec = #1#3;  |QBF eaDirec;
     eaDirec = eaDirec + ", " + #1#4;  |QBF eaDirec;
     eaDirec = eaDirec + " (" + #1#5;  |QBF eaDirec;
     eaDirec = eaDirec + ")";
     eaDirec = ((" " * 72) + eaDirec) % -172

     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |INFOR "dsmoy071";
     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

     aFichero = ("71" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #900 aFichero;
     |ABRE #900;  |CIERRA #900;  |DELINDEX #900;

     |ABRE #900;
     |SI #0#0 ! 309;  |Y #0#0 ! 202;
          |BUCLE dsmom004;
     |SINO;
          |SI #0#0 = 309;
              |BUCLE dsmod309;
          |FINSI;

          |SI #0#0 = 202;
              |HAZ Modelo202;
          |FINSI;
     |FINSI;
     |CIERRA #900;

     nBanco = 0;

     |ABRE #4;
     |BUCLE dsmow071;
     |CIERRA #4;

     |PIEINF;
     |FININF;
     |FINIMP;

     |ABRE #900;  |CIERRA #900;  |DELINDEX #900;
|FPRC;

|PROCESO Periodo;
     |SI #0#0 ! 202;
         |SI #0#4 = 1;   #0#5 = "ENERO     ";  |FINSI;
         |SI #0#4 = 2;   #0#5 = "FEBRERO   ";  |FINSI;
         |SI #0#4 = 3;   #0#5 = "MARZO     ";  |FINSI;
         |SI #0#4 = 4;   #0#5 = "ABRIL     ";  |FINSI;
         |SI #0#4 = 5;   #0#5 = "MAYO      ";  |FINSI;
         |SI #0#4 = 6;   #0#5 = "JUNIO     ";  |FINSI;
         |SI #0#4 = 7;   #0#5 = "JULIO     ";  |FINSI;
         |SI #0#4 = 8;   #0#5 = "AGOSTO    ";  |FINSI;
         |SI #0#4 = 9;   #0#5 = "SEPTIEMBRE";  |FINSI;
         |SI #0#4 = 10;  #0#5 = "OCTUBRE   ";  |FINSI;
         |SI #0#4 = 11;  #0#5 = "NOVIEMBRE ";  |FINSI;
         |SI #0#4 = 12;  #0#5 = "DICIEMBRE ";  |FINSI;
         |SI #0#4 = 99;  #0#5 = "ANUAL     ";  |FINSI;
     |SINO;
         |SI #0#4 > 3;
             |MENAV "0000 Periodo erroneo";
             |ERROR;
             |ACABA;
         |FINSI;

         |SI #0#4 = 1;   #0#5 = "1 - PAGO  ";  |FINSI;
         |SI #0#4 = 2;   #0#5 = "2 - PAGO  ";  |FINSI;
         |SI #0#4 = 3;   #0#5 = "3 - PAGO  ";  |FINSI;
     |FINSI;

     |PINTA #0#5;
     |PINTA #0#6;
|FPRC;

|PROCESO Modelo;
     |ABRE #199;
     #199#0 = #0#0;
     |LEE 040#199=;
     |SI FSalida ! 0;  |DDEFECTO #199;  |FINSI;
     |CIERRA #199;

     |SI #0#0 = 180;  |O #0#0 = 190;  |O #0#0 = 390;  |O #0#0 = 392;
      |O #0#0 = 184;  |O #0#0 = 193;  |O #0#0 = 296;
         |MENAV "     Modelo no valido.";
         |ERROR;
     |FINSI;

     |SI #199#4 = "N";  |Y #199#0 ! 202;
         |MENAV "     Modelo sin Activar.";
         |ERROR;
     |FINSI;

     #0#4 = 0;

     fFecha = ~;
     aAlfa  = fFecha % -104;
     #0#6   = aAlfa;

     |SI #199#0 = 202;
         #0#4 = 1;
         |HAZ Periodo;
         |ACABA;
     |FINSI;

     fFecha = ~;
     aAlfa  = fFecha % 402;   nMes  = aAlfa;
     aAlfa  = fFecha % -104;  nAnyo = aAlfa;

     |SI #199#5 = "T";  |O #199#5 = "C";
         |SI nMes [ 3;
             #0#4  = 12;
             #0#6 = nAnyo - 1;
             |HAZ Periodo;
             |PINTA #0#4;
             |PINTA #0#6;
             |ACABA;
         |FINSI;

         |SI nMes [ 6;
             #0#4  = 3;
             #0#6  = nAnyo;
             |HAZ Periodo;
             |PINTA #0#4;
             |PINTA #0#6;
             |ACABA;
         |FINSI;

         |SI nMes [ 9;
             #0#4  = 6;
             #0#6  = nAnyo;
             |HAZ Periodo;
             |PINTA #0#4;
             |PINTA #0#6;
             |ACABA;
         |FINSI;

         |SI nMes [ 12;
             #0#4  = 9;
             #0#6  = nAnyo;
             |HAZ Periodo;
             |PINTA #0#4;
             |PINTA #0#6;
             |ACABA;
         |FINSI;
     |FINSI;

     |SI #199#5 = "M";
         |SI nMes = 1;
             #0#4  = 12;
             #0#6  = nAnyo - 1;
             |HAZ Periodo;
         |SINO;
             #0#4  = nMes - 1;
             #0#6  = nAnyo;
             |HAZ Periodo;
         |FINSI;
         |PINTA #0#4;
         |PINTA #0#6;
     |FINSI;
|FPRC;
