|FICHEROS;
     dsmoy001 #0;

     dsmom030 #30;
     dsmo1347 #347;
     dsmo2347 #348;
     dsmo3347 #349;

     dsmom100 #100;

     :/contagen/def/canempre #101;
     :/contagen/def/camaniva #102;
     :/contagen/def/caivalin #103;
     :/contagen/def/caclipro #104;
     :/contagen/def/caconimp #306;

     :/basica/def/agifigen   #115;
     :/basica/def/agicen17   #117;
     :/integral/def/dsam0001 #140;

     &regisnif@ #709;

     dsmow024 #999;
     dsmow102 #998;
     dsmow030 #900;
|FIN;

|VARIABLES;
     nSwSel = 0;
     &eaTit2    = "";
     &enDatos   = 0;
     aPath      = "";
     nCampo     = 0;
     nDEmpresa  = 0;
     nHEmpresa  = 0;
     nTopeA     = 0;
     nTopeB     = 0;
     nTopeC     = 0;
     nTopeD     = 0;
     nTopeE     = 0;
     nTopeAI    = 0;
     nTopeBI    = 0;
     nTopeCI    = 0;
     nTopeDI    = 0;
     nTopeEI    = 0;
     nSwPrint   = 0;
     nEjer      = 0;
     nHay       = 0;
     nDComple   = 0;
     nHComple   = 0;

     aClau      = "";
     aClave     = "";
     aFichero   = "";
     aAlfa      = "";
     aAlfa1     = "";
     aAlfa2     = "";
     aAlfa3     = "";

     &enTope    = 0;
     &eaDescri  = "";
     &eaDescri1 = "";
     &eaMoneda  = "";
     fLaFecha   = @;
     aBase      = "";
     nCanal     = 0;
     informe    = "";
     inform1    = "";

     nTopPan    = 0;
     nTopPa1    = 0;

    aCliPro = "";
    aDClPr  = "";
    aHClPr  = "";
    nLaEmpresa = 0;

    aDCta  = "";
    aHCta  = "";
    aDNif  = "";
    aHNif  = "";
    nSwInf = 0;
    &enSwLinea = 0;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;

|PROCESO elany; |TIPO 7;
     |HAZ MiraVarMoneda;
     fLaFecha = ~;
     aAlfa1 = fLaFecha; aAlfa1 = aAlfa1 % -104;
     #0Campo = aAlfa1;
     #0Campo = #0Campo - 1;  |PINTA #0Campo;

     |SI eaMoneda ! "EUROS  ";
         #0#82 = 500000;
    |SINO;
         #0#82 = 3005.06;
    |FINSI;
    |PINTA #0#82;
    |SI PARAMETRO = "MODELOS";
         |ATRI R;
         |PINTA 0502, "Carta de Aviso de Compras y Ventas Superiores desde el Propio Modelo 347";
         |ATRI r;

         #0#63 = "C";  |C_M #0#63, 1, "N";  |C_V #0#63,1,"N";
         #0#67 = "N";  |C_M #0#67, 1, "N";  |C_V #0#67,1,"N";
                       |C_M #0#82, 1, "N";
         #0#83 = 1;    |C_M #0#83, 1, "N";  |C_V #0#83,1,"N";
         #0#92 = "N";  |C_M #0#92, 1, "N";  |C_V #0#92,1,"N";
         #0#84 = "N";  |C_M #0#84, 1, "N";  |C_V #0#84,1,"N";
         |PINTA 0727, "                    ";
         |ATRI I; |PINTA 0804, "(Emision por NIF y claves de agrupacion del Modelo 347)                 "; |ATRI 0;
         |PINTA 1104, "                 ";
         |PINTA #0#82;
    |FINSI;

    |SI PARAMETRO = "C";
         #0#63 = "C";  |C_M #0#63, 1, "N";  |PINTA #0#63;
    |FINSI;

    |HAZ DatosEmp;
|FPRC;

|PROCESO DatosEmp;
   |DDEFECTO #306;
   |ABRE #100;
   |LEE 040#100p;
   |SI FSalida = 0;
       aAlfa1 = #100#1; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
       aAlfa2 = #100#2; |QBF aAlfa2; aAlfa2 = ("0"     + aAlfa2) % -101;
       |DBASS aPath; |QBT aPath;
       aPathConta = aPath + "contagen/fich/" + aAlfa1 + aAlfa2 + "/";
       |PATH_DAT #306 aPathConta;
       |ABRE #306;
       |DDEFECTO #306;
       #306#0 = 1;
       |LEE 000#306=;
       |CIERRA #306;

   |FINSI;
   |CIERRA #100;
   #0#84  = #306#7; |PINTA #0#84;
   aAlfa1 = #306#8; |C_M #0#84,1,aAlfa1;
|FPRC;

|PROCESO AntesD; |TIPO 7;
 |SI #0#63 = "C";
     aAlfa1 = "N";
 |SINO;
     aAlfa1 = #306#8;
 |FINSI;
 |C_M #0#84,1,aAlfa1;
|FPRC;

|PROCESO hagocarta; |TIPO 7;
 |C_M #0Campo, 1,"N";
 |SI #0#63 = "C";
     |C_M #0Campo, 1,"S";
 |FINSI;
|FPRC;


|PROCESO MiraVarMoneda;
     |HAZ PintaMoneda;

     #0#66 = eaMoneda;
     |PINTA #0#66;
|FPRC;

|PROCESO Parrilla;
     |SI #0#1 = 0;
         #0#1 = 0;  |PINTA #0#1;
         #0#2 = 0;  |PINTA #0#2;  |C_M #0#2, 1, "N";
         |PARA nCampo = 3;  |SI nCampo [ 27;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |C_M #0#2, 1, "S";
         |PARA nCampo = 3;  |SI nCampo [ 27;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 27;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 27;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO QueSelec;
 aAlfa1 = #0#85;
 aAlfa2 = #0#85; |SI PARAMETRO = "MODELOS"; aAlfa2 = "N"; |FINSI;

 |C_M #0#86,1,aAlfa2; |C_M #0#87,1,aAlfa2;
 |C_M #0#88,1,aAlfa2; |C_M #0#89,1,aAlfa2;
 |C_M #0#90,1,aAlfa1; |C_M #0#91,1,aAlfa1;

 |SI aAlfa1 = "N";
     |PDEFECTO #0, 86, 92;
     |PINTA #0#86; |PINTA #0#87;
     |PINTA #0#88; |PINTA #0#89;
     |PINTA #0#90; |PINTA #0#91;
 |FINSI;
|FPRC;

|CALCULO cuenta; |TIPO 0;
     salidas  = FSalida;
     |C_M #0Campo,0,aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     |ABRE #100;
     |LEE 040#100p;
     |SI FSalida ! 0;  |DDEFECTO #100;  |FINSI;
     |CIERRA #100;

     nLaEmpresa = enEmpresa;
     enEmpresa  = #100#1;
     enPerConta = #100#2;

     eaCuenta = #0Campo;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;
     enEmpresa = nLaEmpresa;
|FCAL;

|| =================================================================
|PROCESO ElDetalle;
 |PRINT;
 enSwLinea = 2;
|FPRC;

|DEFBUCLE ElDetalle;
 #998#1;
 ;
 #999#0, #999#1, 00, 0000000001;
 #999#0, #999#1, 99, 9999999999;
 ;
 NULL, ElDetalle;
|FIN;

|PROCESO LeeLinea;

     #30#0 = #103#3;
     |LEE 040#30=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;
     |SI #30#51 = "X"; |ACABA;  |FINSI;

     aClave = "";
     |SI #30#44 = "X";  aClave = "A";  |FINSI;
     |SI #30#45 = "X";  aClave = "B";  |FINSI;
     |SI #30#46 = "X";  aClave = "C";  |FINSI;
     |SI #30#47 = "X";  aClave = "D";  |FINSI;
     |SI #30#48 = "X";  aClave = "E";  |FINSI;
     |SI #30#49 = "X";  aClave = "X";  |FINSI;
     |SI aClave = "";  |ACABA;  |FINSI;

     |SI #0#67 = "N";
         aClau = #102#14;  || Nif
     |SINO;
         aClau = #102#12;  || Cuenta Contable
     |FINSI;

     #999#0 = aClau;
     #999#1 = aClave;
     |LEE 101#999=;
     |SI FSalida ! 0;
         |DDEFECTO #999;
         #999#0 = aClau;
         #999#1 = aClave;
         |GRABA 020#999b;
     |FINSI;

     #999#2   = #102#13;
     eaCodNif = #102#14;
     |HAZ LeeNifs;
     #999#4   = #709#9;
     aAlfa    = #709#2;  |QBF aAlfa;
     aAlfa    = aAlfa + ", " + #709#3;
     #999#5   = aAlfa;
     #999#6   = (("00" + #709#4) % -102) + (("000" + #709#5) % -103);
     #999#7   = #709#6;
     #999#8   = #999#8 + (((#103#6 + #103#8 + #103#10) / enConversor) ! EURODBMOD);
     #999#9   = #709#7;
     #999#10  = #102#14;
     #999#11  = #102#12;
     #999#12  = aCliPro;
     #999#13  = #709#8;
     |GRABA 020#999a;
     |LIBERA #999;

     |SI #0#92 = "S";
         |ABRE #998;
         #998#0 = #999#0;
         #998#1 = #999#1;
         #998#2 = #102#0;
         #998#3 = #102#1;
         |LEE 101#998=;
         |SI FSalida ! 0;
             |DDEFECTO #998;
             #998#0  = #999#0;
             #998#1  = #999#1;
             #998#2  = #102#0;
             #998#3  = #102#1;
             #998#4  = #102#2;
             #998#5  = #102#3;
             #998#6  = #102#4;
             #998#7  = #102#10;
             #998#8  = #102#11;
             #998#9  = #103#3;
             #998#10 = #103#4;
             #998#11 = #102#22;
             |GRABA 020#998b;
         |FINSI;
         #998#12  = #998#12 + (#103#6  / enConversor) ! EURODBMOD;
         #998#13  = #998#13 + (#103#8  / enConversor) ! EURODBMOD;
         #998#14  = #998#14 + (#103#10 / enConversor) ! EURODBMOD;
         #998#15  = #998#13 + #998#14;
         #998#16  = #998#12 + #998#13 + #998#14;
         |GRABA 020#998a;
         |LIBERA #998;
         |CIERRA #998;
     |FINSI;
|FPRC;

|DEFBUCLE caivalin;
     #103#1;
     ;
     #102#0, #102#1, 01;
     #102#0, #102#1, 99;
     ;
     NULL, LeeLinea;
|FIN;

|PROCESO LeeCabecera;

     aCliPro = "";
     |SI #102#36 = "S"; aCliPro = "P"; |FINSI;
     |SI #102#36 = "R"; aCliPro = "C"; |FINSI;

     nSwSel = 0;
     |SI #0#85 = "S";
         |SI aCliPro = "C";
             |SI #102#12 < #0#86; |O #102#12 > #0#87; nSwSel = 1; |FINSI;
         |SINO;
             |SI #102#12 < #0#88; |O #102#12 > #0#89; nSwSel = 1; |FINSI;
         |FINSI;
         |SI #102#14 < #0#90; |O #102#14 > #0#91; nSwSel = 1; |FINSI;
     |FINSI;
     |SI nSwSel = 1; |ACABA; |FINSI;

     |SI #0#67 = "N";
         aClau = #102#14;  || Nif
     |SINO;
         aClau = #102#12;  || Cuenta Contable
     |FINSI;

     |BUCLE caivalin;
|FPRC;

|DEFBUCLE camaniva;
     #102#1;
     12,14;
     00, 0000000000, aDCta, aDNif;
     99, 9999999999, aHCta, aHNif;
     ;
     NULL, LeeCabecera;
|FIN;

|PROCESO FechaBonita;
     aAlfa1 = #0#64 % 102;
     aAlfa2 = #0#64 % 402;
     aAlfa3 = #0#64 % 704;

     |SI aAlfa2 = "01";  aAlfa2 = "ENERO     ";  |FINSI;
     |SI aAlfa2 = "02";  aAlfa2 = "FEBRERO   ";  |FINSI;
     |SI aAlfa2 = "03";  aAlfa2 = "MARZO     ";  |FINSI;
     |SI aAlfa2 = "04";  aAlfa2 = "ABRIL     ";  |FINSI;
     |SI aAlfa2 = "05";  aAlfa2 = "MAYO      ";  |FINSI;
     |SI aAlfa2 = "06";  aAlfa2 = "JUNIO     ";  |FINSI;
     |SI aAlfa2 = "07";  aAlfa2 = "JULIO     ";  |FINSI;
     |SI aAlfa2 = "08";  aAlfa2 = "AGOSTO    ";  |FINSI;
     |SI aAlfa2 = "09";  aAlfa2 = "SEPTIEMBRE";  |FINSI;
     |SI aAlfa2 = "10";  aAlfa2 = "OCTUBRE   ";  |FINSI;
     |SI aAlfa2 = "11";  aAlfa2 = "NOVIEMBRE ";  |FINSI;
     |SI aAlfa2 = "12";  aAlfa2 = "DICIEMBRE ";  |FINSI;

     |SI #0#68 = 2;
         aAlfa = #0#72;
     |SINO;
         aAlfa = #115#59;
     |FINSI;
     |QBF aAlfa;
     |QBF aAlfa2;
     #0#65 = aAlfa + ", a " + aAlfa1 + " de " + aAlfa2 + " de " + aAlfa3;
|FPRC;

|PROCESO Imprime;
     enSwLinea = 0;
                        enTope = 0;
     |SI #999#1 = "A";  enTope = nTopeAI;  |FINSI;
     |SI #999#1 = "B";  enTope = nTopeBI;  |FINSI;
     |SI #999#1 = "C";  enTope = nTopeCI;  |FINSI;
     |SI #999#1 = "D";  enTope = nTopeDI;  |FINSI;
     |SI #999#1 = "E";  enTope = nTopeEI;  |FINSI;

     |SI #999#1 = "A";  |Y #999#8 < nTopeA;
         enTope = nTopeAI;
         |ACABA;
     |FINSI;

     |SI #999#1 = "B";  |Y #999#8 < nTopeB;
         enTope = nTopeBI;
         |ACABA;
     |FINSI;

     |SI #999#1 = "C";  |Y #999#8 < nTopeC;
         enTope = nTopeCI;
         |ACABA;
     |FINSI;

     |SI #999#1 = "D";  |Y #999#8 < nTopeD;
         enTope = nTopeDI;
         |ACABA;
     |FINSI;

     |SI #999#1 = "E";  |Y #999#8 < nTopeE;
         enTope = nTopeEI;
         |ACABA;
     |FINSI;

     |HAZ FechaBonita;

     |SI nSwInf = 0;
         |INFOR informe;
         |SI FSalida ! 0; |ACABA; |FINSI;
         nSwInf = 1;
     |FINSI;

     |PRINT;

     |SI #0#63 = "C";
         |PIEINF;
         |SI #0#92 = "S";
             |FININF;
             nSwInf = 0;
             |INFOR inform1;
             |SI FSalida ! 0; |ACABA; |FINSI;
             nSwInf = 1;
         |FINSI;
     |FINSI;

     nSwPrint = 1;
     |SI #0#92 = "S";
         enSwLinea = 1;
         |BUCLE ElDetalle;
         |SI #0#63 = "C";
             |PIEINF;
             |FININF;
             nSwInf = 0;
         |SINO;
             |SI enSwLinea = 2; enSwLinea = 3; |PRINT; |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE dsmow024;
     #999#1;
     ;
     "            ", " ";
     "zzzzzzzzzzzz", "z";
     ;
     NULL, Imprime;
|FIN;

|DEFBUCLE dsmow024_2;
     #999#2;
     ;
     aDClPr, "            ";
     aHClPr, "zzzzzzzzzzzz";
     ;
     NULL, Imprime;
|FIN;

|DEFBUCLE dsmow024_3;
     #999#3;
     ;
     aDClPr, "                                        ";
     aHClPr, "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, Imprime;
|FIN;

|PROCESO Selecciona;
     |ATRI S;
     |PINTA 2103, "Empresa : ";
     |PINTA 2113, #101#2;
     |PINTA 2119, #101#1;
     |ATRI s;

     enEmpresa  = #101#2;
     enPerConta = #101#3;
     eaLaMonEmp = #101#28;
     |HAZ EnConversor;

     |SI EURODBMOD ! 0;
         nTopeA     = nTopPan;  ||  3005.06;
         nTopeB     = nTopPan;  ||  3005.06;
         nTopeC     = nTopPa1;  ||  300.51;
         nTopeD     = nTopPan;  ||  3005.06;
         nTopeE     = nTopPan;  || 3005.06;
     |SINO;
         nTopeA    = nTopPan;   || 500000;
         nTopeB    = nTopPan;   || 500000;
         nTopeC    = nTopPa1;   || 50000;
         nTopeD    = nTopPa1;   || 50000;
         nTopeE    = nTopPa1;   || 50000;
     |FINSI;

     |SI #0#66 = "EUROS  ";
         eaDescri   = nTopPan; eaDescri = eaDescri + "  EUROS";
         eaDescri1  = "euros.";
         nTopeAI    = nTopPan; || 3005.06;
         nTopeBI    = nTopPan; || 3005.06;
         nTopeCI    = nTopPa1; || 300.51;
         nTopeDI    = nTopPan; || 3005.06;
         nTopeEI    = nTopPan; || 3005.06;
     |SINO;
         eaDescri  = nTopPan; eaDescri = eaDescri + "  PTAS";
         eaDescri1 = "pesetas.";
         nTopeAI   = nTopPan; || 500000;
         nTopeBI   = nTopPan; || 500000;
         nTopeCI   = nTopPa1; || 50000;
         nTopeDI   = nTopPa1; || 50000;
         nTopeEI   = nTopPa1; || 50000;
     |FINSI;

     aFichero = ("24" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #999 aFichero;
     |ABRE #999;  |CIERRA #999;  |DELINDEX #999;
     aFichero = ("23" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #998 aFichero;
     |ABRE #998;  |CIERRA #998;  |DELINDEX #998;

     |DBASS aPathConta;   |QBT aPathConta;
     aPathConta = aPathConta + "contagen/fich/" + (("00000" + #101#2) % -105) + #101#3 + "/";

     |PATH_DAT #102 aPathConta;
     |PATH_DAT #103 aPathConta;
     |PATH_DAT #104 aPathConta;

     |ABRE #999;
     |ABRE #30;
     |BUCLE camaniva;
     |CIERRA #30;
     |CIERRA #999;

     |ABRE #115;
     #115#0 = #101#2;
     |LEE 000#115=;
     |SI FSalida ! 0;  |DDEFECTO #115;  |FINSI;
     |CIERRA #115;

     #0#76 = #115#1;
     #0#77 = #115#13;
     aAlfa = #115#51;  |QBF aAlfa;
     |SI aAlfa ! "";  aAlfa = aAlfa + " ";  |FINSI;
     aAlfa = aAlfa + #115#52;  |QBF aAlfa;
     |SI #115#53 ! "    ";  aAlfa = aAlfa + ", " + #115#53;  |FINSI;
     |SI #115#55 ! "  ";    aAlfa = aAlfa + " "  + #115#55;  |FINSI;
     |SI #115#56 ! "  ";    aAlfa = aAlfa + " "  + #115#56;  |FINSI;
     #0#78 = aAlfa;
     #0#79 = (("00" + #115#57) % -102) + (("000" + #115#58) % -103);
     #0#80 = #115#59;
     #0#81 = #115#14;

     |SI enDatos = 1; enActividad = 0;  |HAZ DatosEmpr_w30; |FINSI;

     nSwPrint = 0;
     |SI #0#83 = 1;
         eaTit2 = "";
         |BUCLE dsmow024;
         |SI nSwPrint = 1;  |Y #0#63 = "I";  |PIEINF;  |FINSI;
     |SINO;
         nSwPrint = 0;
         aDClPr = "P"; aHClPr = "P"; eaTit2 = "PROVEEDORES Y ACREEDORES";
         |SI #0#83 = 2; |BUCLE dsmow024_2; |FINSI;
         |SI #0#83 = 3; |BUCLE dsmow024_3; |FINSI;
         |SI nSwPrint = 1;  |Y #0#63 = "I";  |PIEINF;  |FINSI;
         nSwPrint = 0;
         aDClPr = "C"; aHClPr = "C"; eaTit2 = "CLIENTES Y DEUDORES";
         |SI #0#83 = 2; |BUCLE dsmow024_2; |FINSI;
         |SI #0#83 = 3; |BUCLE dsmow024_3; |FINSI;
         |SI nSwPrint = 1;  |Y #0#63 = "I";  |PIEINF;  |FINSI;
     |FINSI;

     |DELINDEX #998;
     |DELINDEX #999;
|FPRC;

|DEFBUCLE canempre;
     #101#4;
     ;
     nDEmpresa, nEjer;
     nHEmpresa, nEjer;
     ;
     NULL, Selecciona;
|FIN;

|| ------------------- Procesos por Modelos

|PROCESO dsmo1347;
     nHay = 1;

     |SI #347#4 = 0;
         nDComple = 0;
         nHComple = 0;
     |SINO;
         |SI #347#19 = "C";
             nHComple = #347#4;
         |SINO;
             nDComple = #347#4;
             nHComple = #347#4;
         |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE dsmo1347;
     #347#1;
     ;
     #115#0, #0#0, 99, 347, 00, 00;
     #115#0, #0#0, 99, 347, 99, 00;
     ;
     NULL, dsmo1347;
|FIN;

|PROCESO dsmo2347;

     |DDEFECTO #999;

     #999#0  = #348#7;
     #999#1  = #348#6;
     #999#2  = #348#11;

     eaCodNif = #348#7;
     |HAZ LeeNifs;

     #999#4   = #709#9;
     aAlfa    = #709#2;  |QBF aAlfa;
     aAlfa    = aAlfa + ", " + #709#3;
     #999#5   = aAlfa;
     #999#6   = (("00" + #709#4) % -102) + (("000" + #709#5) % -103);
     #999#7   = #709#6;
     #999#8   = #348#12;
     #999#9   = #709#7;
     #999#10  = #348#7;
     #999#11  = "";

     eaDescri1  = "euros.";

                        enTope = 0;
     |SI #999#1 = "A";  enTope = nTopPan; |FINSI; || 3005.06;
     |SI #999#1 = "B";  enTope = nTopPan; |FINSI; || 3005.06;
     |SI #999#1 = "C";  enTope = nTopPa1; |FINSI; ||  300.51;
     |SI #999#1 = "D";  enTope = nTopPan; |FINSI; || 3005.06;
     |SI #999#1 = "E";  enTope = nTopPan; |FINSI; || 3005.06;

     |HAZ FechaBonita;

     |PRINT;
     |PIEINF;
|FPRC;

|DEFBUCLE dsmo2347;
     #348#1;
     ;
     #115#0, #0#0, 99, 347, nDComple, 00, " ", aDNif, " ", " ";
     #115#0, #0#0, 99, 347, nHComple, 00, "z", aHNif, "z", "z";
     ;
     NULL, dsmo2347;
|FIN;

|PROCESO dsmo3347;

     |SI #349#7 < aDNif; |O #349#7 > aHNif; |ACABA; |FINSI;
     |DDEFECTO #999;

     #999#0  = #349#7;
     #999#1  = "X";
     #999#2  = #349#8;

     eaCodNif = #349#7;
     |HAZ LeeNifs;

     #999#4   = #709#9;
     aAlfa    = #709#2;  |QBF aAlfa;
     aAlfa    = aAlfa + ", " + #709#3;
     #999#5   = aAlfa;
     #999#6   = (("00" + #709#4) % -102) + (("000" + #709#5) % -103);
     #999#7   = #709#6;
     #999#8   = #349#10;
     #999#9   = #709#7;
     #999#10  = #348#7;
     #999#11  = "";

     eaDescri1  = "euros.";
     enTope     = 0;

     |HAZ FechaBonita;

     |PRINT;
     |PIEINF;
|FPRC;

|DEFBUCLE dsmo3347;
     #349#1;
     ;
     #115#0, #0#0, 99, 347, nDComple, 00, 01;
     #115#0, #0#0, 99, 347, nHComple, 00, 99;
     ;
     NULL, dsmo3347;
|FIN;

|PROCESO agifigen;
     nHay = 0;
     |BUCLE dsmo1347;

     |SI nHay = 0;  |ACABA;  |FINSI;

     #0#76 = #115#1;
     #0#77 = #115#13;
     aAlfa = #115#51;  |QBF aAlfa;
     |SI aAlfa ! "";  aAlfa = aAlfa + " ";  |FINSI;
     aAlfa = aAlfa + #115#52;  |QBF aAlfa;
     |SI #115#53 ! "    ";  aAlfa = aAlfa + ", " + #115#53;  |FINSI;
     |SI #115#55 ! "  ";    aAlfa = aAlfa + " "  + #115#55;  |FINSI;
     |SI #115#56 ! "  ";    aAlfa = aAlfa + " "  + #115#56;  |FINSI;
     #0#78 = aAlfa;
     #0#79 = (("00" + #115#57) % -102) + (("000" + #115#58) % -103);
     #0#80 = #115#59;
     #0#81 = #115#14;

     |BUCLE dsmo2347;
     |BUCLE dsmo3347;
|FPRC;

|DEFBUCLE agifigen;
     #115#1;
     ;
     nDEmpresa;
     nHEmpresa;
     ;
     NULL, agifigen;
|FIN;

|PROCESO LeeDespacho;
     enSwDeDonde = 0;
     |DBASS aBase;   |QBT aBase;
     aBase = aBase + "integral/def/dsam0000.mas";
     |XABRE "E", aBase, nCanal;
     |SI FSalida ] 0;
         enSwDeDonde = 1;
     |FINSI;

     |SI enSwDeDonde = 0; |ACABA; |FINSI;

     |ABRE #140;
     |LEE 101#140p;
     |SI FSalida = 0;
         #0#69 = #140#1;
         #0#70 = #140#2; #0#70 = "DEPARTAMENTO FISCAL";
         #0#71 = #140#3;
         #0#72 = #140#4;
         #0#73 = #140#5;
         #0#74 = #140#6;
         #0#75 = #140#7;
     |FINSI;
     |CIERRA #140;
|FPRC;

|PROCESO Tipo3;  |TIPO 3;

     aDCta = "            "; aHCta = "zzzzzzzzzzzz";
     aDNif = "            "; aHNif = "zzzzzzzzzzzz";
     |SI #0#85 = "S";
         aDNif = #0#90; aHNif = #0#91;
         |SI PARAMETRO ! "MODELOS";
             aDCta = #0#86; |SI #0#88 < aDCta; aDCta = #0#88; |FINSI;
             aHCta = #0#87; |SI #0#89 > aHCta; aHCta = #0#89; |FINSI;
         |FINSI;
     |FINSI;

     aAlfa = #0#0;
     aAlfa = aAlfa % -102;
     nEjer = aAlfa;

     nTopPan = #0#82;
     nTopPa1 = (#0#82 / 10 ) ! EURODBMOD;

     enDatos = 0; |SI #0#84 = "S"; enDatos = 1; |FINSI;
     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     inform1 = "dsmoy501";
     |SI #0#63 = "I";
         informe = "dsmoy101";
         |SI #0#92 = "S";
             informe = "dsmoy401";
         |FINSI;
     |SINO;
         |SI #0#68 = 2;
             informe = "dsmoy301";
             |HAZ LeeDespacho;
         |SINO;
             informe = "dsmoy201";
         |FINSI;
     |FINSI;

     |SI PARAMETRO ! "MODELOS"; |Y #0#63 = "C"; |Y #0#92 = "S";
         nSwInf = 0;
     |SINO;
         nSwInf = 1;
         |INFOR informe;
         |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;
     |FINSI;

     |SI #0#1 ! 0;
         nDEmpresa = #0#1;
         nHEmpresa = #0#2;

         |SI PARAMETRO = "MODELOS";
             |BUCLE agifigen;
         |SINO;
             |BUCLE canempre;
         |FINSI;
     |SINO;
         |PARA nCampo = 3;  |SI nCampo [ 27;  |HACIENDO nCampo = nCampo + 1;
               |SI #0nCampo ! 0;
                   nDEmpresa = #0nCampo;
                   nHEmpresa = #0nCampo;
                   |SI PARAMETRO = "MODELOS";
                       |BUCLE agifigen;
                   |SINO;
                       |BUCLE canempre;
                   |FINSI;
               |FINSI;
         |SIGUE;
     |FINSI;

     |SI nSwInf = 1;
         |FININF;
         nSwInf = 0;
     |FINSI;
     |FINIMP;
|FPRC;
