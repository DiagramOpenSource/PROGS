|FICHEROS;
    dsmoz039 #0;

    dsmom041 #41;
    dsmom042 #42;

    dsmow035 #995,MANTE;      || Temporal de Fra. de Inversion
    dsmow034 #997,MANTE;      || Temporal de Grupos / Cuentas
    dsmow041 #998,MANTE;
    dsmow043 #999;            || Temporal de Empresas Contables

   :/contagen/def/camaniva #301;
   :/contagen/def/caivalin #302;
|FIN;

|VARIABLES;
  ElPathPan   = "";
  aFichero    = "";
  fecalf      = "";
  nDEmpresa   = 0;
  nHEmpresa   = 0;
  nDActividad = 0;
  nHActividad = 0;
  nCampo      = 0;
  nGTempo     = 0;

  &nCamSel = 24;
  &nCamTip = 37;
  aAlfa1 = "";
  aAlfa2 = "";

  nSwElem = 0;
  &errconta = 0;
  nInkey = 0;
  nElAny = 0;

  aPath  = "";
  fecsys = @;
  nDepar = 0;
  &aComVen = "";
  &aRepSop = "";
  nBueno = 0;
  fDFecha = @;
  fHFecha = @;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;
|INCLUYE i_numamo;
|INCLUYE i_selcon;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\
|CALCULO TComVen; |TIPO 8;
 aComVen = PARAMETRO; |QBT aComVen;
 aComVen = aComVen > " ";
 |SI aComVen = "";  |O aComVen ! "C"; aComVen = "V"; |FINSI;
 |SI aComVen = "C";  aRepSop = "S"; |FINSI;
 |SI aComVen = "V";  aRepSop = "R"; |FINSI;
|FCAL;

|CALCULO Antes; |TIPO 7;
     aAlfa1 = "Venta ";
     |SI aComVen = "C";
         aAlfa1 = "Compra";
     |FINSI;
     |ATRI R; |PINTA 0617, aAlfa1; |ATRI 0;
     |PINTA 1638, aAlfa1;
     fecsys = ~;
     aAlfa1 = fecsys % -104;
     #0#2 = aAlfa1;
     |PINTA #0#2;
|FCAL;

|CALCULO Tipo7_7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#8 = enEmpresa; |PINTA #0#8;
         #0#9 = enEmpresa; |PINTA #0#9;

         |C_M #0#8, 1, "N";
         |C_M #0#9, 1, "N";
     |FINSI;
|FCAL;

|CALCULO Parrilla;
     |SI #0#8 = 0;
         #0#8 = 0;  |PINTA #0#8;
         #0#9 = 0;  |PINTA #0#9;  |C_M #0#9, 1, "N";
         |PARA nCampo = 10;  |SI nCampo [ 21;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#8, 1, "S";
             |C_M #0#9, 1, "S";
         |FINSI;

         |PARA nCampo = 10;  |SI nCampo [ 21;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 21;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 21;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO numamo; |TIPO 0;
    x_alfa1 = #0Campo;
    |HAZ x_punto;
    #0Campo = x_alfa1;
    |PINTA #0Campo;
|FCAL;

|CALCULO ElPtec; |TIPO 7;
 |PTEC 816;
|FCAL;


|| /////////////////////////// Procesos \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\

|RUTINA ClaveBaja997;
 #997#0 = #998#0;
 #997#1 = "        ";
 #997#2 = 00;
|FRUT;

|RUTINA ClaveAlta997;
 #997#0 = #998#0;
 #997#1 = "zzzzzzzz";
 #997#2 = 99;
|FRUT;

||************************************************************************
|CALCULO LeelaEmpresa; |TIPO 0;
   enEmpresa = #999#0;
|FCAL;

|PROCESO Tipo1; |TIPO 1;
|FPRC;

|PROCESO Tipo7;  |TIPO 7;
     |MENSA "    <F2> Entrar en Elementos Amortizables, <F3> Consulta Cliente por Nombre";

     |PINTA  #998#0; |PINTA  #998#1;
     enModo = 7;
     enEmpresa   = #998#0;
     enPerConta  = -1;
     enEjer      = 0;

    |ABRE #997;
    #997#0 = enEmpresa;
    #997#1 = "        ";
    #997#2 = 00;
    |LEE 001#997];
    |SI FSalida ! 0; |O #997#0 ! enEmpresa;
        |DDEFECTO #997;
    |FINSI;
    |CIERRA #997;
    |PINDA #0#997;

    |ENTLINEAL #1#997, -3, 7, 22, 0, ClaveBaja997, ClaveAlta997;
|FPRC;

|PROCESO Tipo11;  |TIPO 11;
   nInkey = FSalida;
   |SI nInkey =  10;
         errconta = 0;
         enModo   = 2;
         enEmpresa = #998#0;
         |HAZ leetodo;
         |ENTLINEAL #1#997, -3, 2, 22, 0, ClaveBaja997, ClaveAlta997;
   |FINSI;
     |SI nInkey = 11;         || F3
         |ERROR;
         eaElProm = "dsmow041";
         |HAZ ConsuCliente;
         eaElProm = "";
         #0#0 = #999#0;
         |PTEC 802;
     |FINSI;

|FPRC;
||************************************************************************

|RUTINA ClaveBaja998;
 #998#0 = 00001;
|FRUT;

|RUTINA ClaveAlta998;
 #998#0 = 99999;
|FRUT;

||************************************************************************
|PROCESO Canempre0;

   enEmpresa = #canempre#2;
   enEjer    = #0#2;
   eaEstadoEmp = "S";
   |SI #canempre#21 = "I"; eaEstadoEmp = "N"; |FINSI;

   nBueno = 0;
   |SI #canempre#11 = 1;
       |SI #canempre#26 = nElAny;
           nBueno = 1;
       |FINSI;
   |SINO;
        |HAZ LaFecha;
        |SI fHFecha ] fec1; |Y fDFecha [ fec2;
            nBueno = 1;
        |FINSI;
   |FINSI;
   |SI nBueno = 0; |ACABA; |FINSI;

   |HAZ LaSelEmpresa2;
   |SI enSwSelEmp ! 1;
       |HAZ GrabaTemporal;
   |FINSI;
|FPRC;

|DEFBUCLE Canempre;
 #canempre#1;
 ;
 nDEmpresa, 0;
 nHEmpresa, 9;
 e;
 NULL, Canempre0;
|FIN;

|CALCULO Tipo3;  |TIPO 3;
     nDActividad = #0#22;
     nHActividad = #0#23;

     aAlfa1  = #0#2; |QBF aAlfa1;
     aAlfa2  = "01.01." + aAlfa1; fDFecha = aAlfa2;
     aAlfa2  = "31.12." + aAlfa1; fHFecha = aAlfa2;

     aAlfa1  = aAlfa1 % -102;
     nElAny  = aAlfa1;

     aFichero = ("35w" + Usuario) % 108;
     |NOME_DAT #995 aFichero;
     |ABRE #995;  |CIERRA #995;  |DELINDEX #995;

     aFichero = ("34w" + Usuario) % 108;
     |NOME_DAT #997 aFichero;
     |ABRE #997;  |CIERRA #997;  |DELINDEX #997;

     aFichero = ("41w" + Usuario) % 108;
     |NOME_DAT #998 aFichero;
     |ABRE #998;  |CIERRA #998;  |DELINDEX #998;

     aFichero = ("043w" + Usuario) % 108;
     |NOME_DAT #999 aFichero;
     |ABRE #999;  |CIERRA #999;  |DELINDEX #999;
     eaFEmpresa = aFichero;


     |ABRE #999;
     |ABRE #998;
     |ABRE #997;
     |ABRE #995;

     nGTempo = 0;   ||Busco Empresas Contables
     |SI #0#8 ! 0;
         nDEmpresa = #0#8;
         nHEmpresa = #0#9;
         |BUCLE Canempre;
     |SINO;
         |PARA nCampo = 10;;  |SI nCampo [ 21;  |HACIENDO nCampo = nCampo + 1;
               nDEmpresa = #0nCampo;
               nHEmpresa = #0nCampo;
               |BUCLE Canempre;
         |SIGUE;
      |FINSI;
      |CIERRA #999;
      |CIERRA #998;
      |CIERRA #997;
      |CIERRA #995;

      |SI nGTempo = 0;    || No existe ninguna empresa
          |MENAV "    Error. No existen Ventas de Elementos Amortizables dentro de la Seleccion ";
      |SINO;
          |C_P #998#0,1,0903; |C_P #998#1,1,0714;

          |PINPA #0#997;
          |ENTLINEAL #1#998, -1, 4, 22, 0, ClaveBaja998, ClaveAlta998;

          |CONFI "    SSon correctos los datos Introduccidos ?";
          |SI FSalida = 0;
              |HAZ Actualizacion;
          |FINSI;
     |FINSI;

     |DELINDEX #995;
     |DELINDEX #997;
     |DELINDEX #998;
     |DELINDEX #999;
|FCAL;

||***************************************************************************
|PROCESO LeeFacturas0;

 #301#0 = #302#0;
 #301#1 = #302#1;
 |LEE 001#301=;
 |SI FSalida ! 0;   |ACABA;  |FINSI;

 |SI #301#36 ! aRepSop; |ACABA; |FINSI;

 nDepar = #301#10;
 |SI nDepar < #0#22; |O nDepar > #0#23;   |ACABA;  |FINSI;

 #302#6 =(#302#6 / enConversor) ! EURODBMOD;
 #302#8 =(#302#8 / enConversor) ! EURODBMOD;
 #302#10 =(#302#10 / enConversor) ! EURODBMOD;

 #995#0 = enEmpresa;
 #995#1 = #301#0;
 #995#2 = #301#1;
 |LEE 101#995=;
 |SI FSalida ! 0;
     |DDEFECTO #995;
     #995#0 = enEmpresa;
     #995#1 = #301#0;
     #995#2 = #301#1;
     |GRABA 020#995b;
 |FINSI;
 #995#3  = #301#2;
 #995#17 = #301#3;
 #995#4  = #301#3;
 aAlfa1  = #301#4; aAlfa1 = (aAlfa1 % 102) + (aAlfa1 % 402);
 #995#5  = aAlfa1;
 #995#6  = #302#2;
 #995#7  = #301#10;
 #995#8  = #301#24;
 #995#9  = #301#12;
 #995#10 = #301#13;
 #995#11 = #302#6;
 #995#12 = #302#8;
 #995#13 = #302#6 + #302#8;
 #995#14 = #301#22;
 #995#15 = #302#3;
 #995#16 = #302#4;
 #995#17 = #302#12;
 |GRABA 020#995a;
 |LIBERA #995;
 nSwElem = 1;
|FPRC;

|DEFBUCLE LeeFacturas;
 #302#1;
 17,4;
 01,0000000001,01,"S",fDFecha;
 99,9999999999,99,"S",fHFecha;
 e;
 NULL, LeeFacturas0;
|FIN;

|PROCESO LeeVentas0;
   #41#0 = #42#0;
   #41#2 = #42#2;
   #41#3 = #42#3;
   |LEE 001#41=;
   |SI FSalida ! 0;  |ACABA; |FINSI;
   |SI #41#1 < nDActividad; |O #41#1 > nHActividad;  |ACABA; |FINSI;

   nSwElem = 1;
   #997#0 = #42#0;   || Empresa
   #997#1 = #42#2;   || Elemento
   #997#2 = #42#3;   || numero
   |LEE 101#997=;
   |SI FSalida ! 0;
       |DDEFECTO #997;
       #997#0 = #42#0;   || Empresa
       #997#1 = #42#2;   || Elemento
       #997#2 = #42#3;   || numero
       |GRABA 020#997b;
   |FINSI;

   #997#3 = #41#4;    || descripcion
   #997#4 = #42#4;    || Linea
   #997#5 = #42#5;    || A¤o Venta
   #997#6 = #42#9;    || Fecha Venta
   #997#7 = #42#7;    || Importe Venta
   #997#8 = #42#12;   || Concepto
   #997#9 = #42#10;   || Descripcion
   #997#10 = #42#14;  || Libro
   #997#11 = #42#15;  || Serie
   #997#12 = #42#16;  || Factura
   #997#13 = #42#1;   || Actividad
   #997#14 = #41#33;  || Actividad Nombre

   |GRABA 020#997a;
   |LIBERA #997;
|FPRC;

|DEFBUCLE LeeVentas;
   #dsmom042#1;
   5,6;
   enEmpresa,#0#0,#0#3,001, #0#2, aComVen;
   enEmpresa,#0#1,#0#4,999, #0#2, aComVen;
   e;
   NULL, LeeVentas0;
|FIN;

|PROCESO GrabaTemporal;
 |ABRE #41;
 nSwElem = 0;
 |BUCLE LeeVentas;
 |CIERRA #41;

 |SI nSwElem = 0; |ACABA; |FINSI;   || No hay venta/compra de elementos

 aAlfa1 = #canempre#2; |QBF aAlfa1;  aAlfa1 = ("00000" + aAlfa1) % -105;
 aAlfa2 = #canempre#3; |QBF aAlfa2;  aAlfa2 = ("0" + aAlfa2) % -101;

 enEmpresa  = #canempre#2;
 enPerConta = #canempre#3;
 eaLaMonEmp = #canempre#28;
 |HAZ EnConversor;

 |DBASS aPath; |QBT aPath;
 aPathConta    = aPath + "contagen/fich/" + aAlfa1 + aAlfa2 + "/";

 |PATH_DAT #301 aPathConta;
 |PATH_DAT #302 aPathConta;

 nSwElem = 0;
 |ABRE #301;
 |BUCLE LeeFacturas;
 |CIERRA #301;
 |SI nSwElem = 0; |ACABA; |FINSI;    ||No hay facturas de Inversion

 nGTempo = nGTempo + 1;
 #999#0 = nGTempo;      || Guia
 #999#1 = enEmpresa;    || Empresa
 #999#2 = #canempre#3;       || Periodo Contable
 #999#3 = enEjer;       || A¤o
 #999#4 = #canempre#11;         || Desde Mes
 #999#5 = #canempre#13;         || N§ Digitos
 #999#6 = #canempre#14;         || Digitos Nivel 1
 #999#7 = #canempre#15;         || Digitos Nivel 2
 #999#8 = #canempre#16;         || Digitos Nivel 3
 #999#9 = #canempre#17;         || Digitos Nivel 4
 #999#10 = #canempre#18;        || Digitos Nivel 5
 #999#11 = #canempre#19;        || Digitos Nivel 6
 #999#12 = eaEstadoEmp; || Activa / Inactiva
 #999#13 = #canempre#28;        || Moneda
 #999#14 = #canempre#1;         || Nombre Empresa
 #999#15 = aPathConta;      || Path
 |GRABA 020#999n;

 #998#0 = enEmpresa;
 |LEE 101#998=;
 |SI FSalida ! 0;
     #998#0 = enEmpresa;
     #998#1 = #canempre#1;
     |GRABA 020#998b;
 |FINSI;
 |LIBERA #998;
|FPRC;

|| ....................... Situa la Empresa Contable
|| ******************
|PROCESO elW34;
 #42#0 = #997#0;
 #42#2 = #997#1;
 #42#3 = #997#2;
 #42#4 = #997#4;
 |LEE 101#42=;
 |SI FSalida = 0;
     #42#14 = #997#10;  || Libro
     #42#15 = #997#11;  || Serie
     #42#16 = #997#12;  || Factura
     |GRABA 020#42a;
 |FINSI;
 |LIBERA #42;
|FPRC;

|DEFBUCLE elW34;
 #997#1;
 ;
 00001, "        ", 00;
 99999, "zzzzzzzz", 99;
 ;
 NULL, elW34;
|FIN;

|PROCESO Actualizacion;
  |ABRE #42;
  |BUCLE elW34;
  |CIERRA #42;
|FPRC;

| ***************************************************************************
|| Proceso Lee la Empresa
|PROCESO elW430;
 aPathConta    = #999#15;
 |QBF aPathConta;
|FPRC;

|DEFBUCLE elW43;
 #999#2;
 ;
 enEmpresa,0;
 enEmpresa,9;
 ;
 NULL, elW430;
|FIN;

|PROCESO leetodo;  || para situar empresa
  |BUCLE elW43;
  |PATH_DAT #301 aPathConta;
  |PATH_DAT #302 aPathConta;
|FPRC;
