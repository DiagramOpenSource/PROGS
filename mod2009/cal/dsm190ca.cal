|FICHEROS;
     dsm190ca #190;
     dsm190li #191;
     dsmom004 #4;
     dsmom037 #37;
|FIN;

|VARIABLES;
     nSwChequea = 0;
     nBorra     = 0;
     nComp      = 0;
     nSuma      = 0;
     nBoton1    = 0;
     nBoton2    = -1;
     nBoton3    = -1;
     aTecla     = "";
     aF2        = "";
     aF3        = "";
     aF4        = "";
     aEsc       = "";

     &eaExtension = "";
     &eaPrograma  = "";

     &enBoton3   = -1;
     &nSwMasivo = 0;
|FIN;

|INCLUYE i_variar;

|RUTINA ClaveBaja2;
     #191#0  = #190#0;
     #191#1  = #190#1;
     #191#2  = #190#2;
     #191#3  = #190#3;
     #191#4  = #190#4;
     #191#5  = #190#5;
     #191#6  = "            ";
     #191#7  = 0000;
     #191#8  = " ";
     #191#9  = "  ";
     #191#10 = 0;
|FRUT;

|RUTINA ClaveAlta2;
     #191#0  = #190#0;
     #191#1  = #190#1;
     #191#2  = #190#2;
     #191#3  = #190#3;
     #191#4  = #190#4;
     #191#5  = #190#5;
     #191#6  = "zzzzzzzzzzzz";
     #191#7  = 9999;
     #191#8  = "z";
     #191#9  = "zz";
     #191#10 = 4;
|FRUT;

|PROCESO Boton1;
     |PUSHV 0501 2380;
     |ESTADO_CONTROL nBoton1, "HIDE";
     |SI nBoton2 ! -1;  |ESTADO_CONTROL nBoton2, "HIDE";  |FINSI;
     |SI nBoton3 ! -1;  |ESTADO_CONTROL nBoton3, "HIDE";  |FINSI;

     |PINPA #0#191;

     |CONTROL_SIMPLE 0, "BOTON,<F3> Datos Familiares ", 1860, 1879, Boton3;
     enBoton3 = FSalida;

     |SI enTipoEntrada = 4;
         |ENTLINEAL #1#191, -7, 4, 22, 0, ClaveBaja2, ClaveAlta2;
     |SINO;
         |ENTLINEAL #1#191, -7, 2, 22, 0, ClaveBaja2, ClaveAlta2;
     |FINSI;

     |FIN_CONTROL_WINDOWS enBoton3;
     enBoton3 = -1;
     |PULSATECLA;

     |ESTADO_CONTROL nBoton1, "SHOW";
     |SI nBoton2 ! -1;  |ESTADO_CONTROL nBoton2, "SHOW";  |FINSI;
     |SI nBoton3 ! -1;  |ESTADO_CONTROL nBoton3, "SHOW";  |FINSI;
     |POPV;

     |PINTA #190#6;
     |PINTA #190#7;
     |PINTA #190#8;
|FPRC;

|PROCESO RecalculaLi;
     |SI nBorra = 1;
         |BORRA 020#191a;
         |LIBERA #191;
         |ACABA;
     |FINSI;

     |SI nSwChequea = 0;
         nSwChequea = 1;
         |ERROR10;
         |ACABA;
     |FINSI;

     enBoton3 = -1;
     |HAZ Tipo2m190li;
|FPRC;

|DEFBUCLE dsm190li;
     #191#1;
     ;
     #190#0,#190#1,#190#2,#190#3,#190#4,#190#5,"            ",0000," ","  ", 0;
     #190#0,#190#1,#190#2,#190#3,#190#4,#190#5,"zzzzzzzzzzzz",9999,"z","zz", 9;
     be;
     NULL, RecalculaLi;
|FIN;

|PROCESO Recalculo;
     nSwChequea = 0;  |BUCLE dsm190li;

     |SI nSwChequea = 0;  |ACABA;  |FINSI;

     #190#6  = 0;
     #190#7  = 0;
     #190#8  = 0;

     |BUCLE dsm190li;
|FPRC;

|PROCESO Traedsm190li;
     #191#4 = #190#4;
     |GRABA 020#191n;
     |LIBERA #191;
|FPRC;

|DEFBUCLE dsm190liT;
     #191#1;
     ;
     #190#0,#190#1,#190#2,#190#3,nComp,#190#5,"            ",0000," ","  ",0;
     #190#0,#190#1,#190#2,#190#3,nComp,#190#5,"zzzzzzzzzzzz",9999,"z","zz",9;
     be;
     NULL, Traedsm190li;
|FIN;

|PROCESO Justificante;
     |SI #190#4 = 0;  |ACABA;  |FINSI;

     |SI #190#10 ! "C";  |Y #190#10 ! "S";
         |MENAV "      Introduzca Complementaria o Sustitutoria";
         |ERROR;
     |FINSI;

     |C_M #190#9, 1, "N";
     |SI #190#10 = "S";  |O #190#10 = "C";
         |C_M #190#9, 1, "S";

         |SI #190#9 = "             ";
             |ABRE #4;
             #4#0 = #190#0;
             #4#1 = #190#1;
             #4#2 = #190#2;
             #4#3 = #190#3;
             #4#4 = #190#4 - 1;
             |LEE 000#4=;
             |SI FSalida ! 0;  |DDEFECTO #4;  |FINSI;
             |CIERRA #4;

             #190#9 = #4#19;  |PINTA #190#9;
         |FINSI;
     |SINO;
         #190#9 = "";  |PINTA #190#9;
     |FINSI;

     |SI #190#10 = "S";
         nSuma = 0;
         nSuma = #190#6 + #190#7 + #190#8;

         |SI nSuma = 0;
             nComp = #190#4 - 1;
             |BUCLE dsm190liT;

             |HAZ Recalculo;

             |PINPA #0#190;
             |PINDA #0#190;
             |HAZ PintaDatos;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Boton2;
     |ESTADO_CONTROL nBoton1, "DISABLE";
     |ESTADO_CONTROL nBoton2, "DISABLE";
     |ESTADO_CONTROL nBoton3, "DISABLE";

     |HAZ Recalculo;
     |PINTA #190#6;
     |PINTA #190#7;
     |PINTA #190#8;

     |ESTADO_CONTROL nBoton1, "ENABLE";
     |ESTADO_CONTROL nBoton2, "ENABLE";
     |ESTADO_CONTROL nBoton3, "ENABLE";
|FPRC;

|PROCESO Boton4;
     |ESTADO_CONTROL nBoton1, "HIDE";
     |ESTADO_CONTROL nBoton2, "HIDE";
     |ESTADO_CONTROL nBoton3, "HIDE";

     |HAZ Diferencias;

     |ESTADO_CONTROL nBoton1, "SHOW";
     |ESTADO_CONTROL nBoton2, "SHOW";
     |ESTADO_CONTROL nBoton3, "SHOW";
|FPRC;

|PROCESO Tipo12moc1190;  |TIPO 12;
|FPRC;

|PROCESO PulsoF2;
     |PTEC 824;
|FPRC;

|PROCESO PulsoF3;
     |PTEC 825;
|FPRC;

|PROCESO PulsoF4;
     |PTEC 826;
|FPRC;

|PROGRAMA;
     |ABRE #37;
     #37#0 = "L";
     #37#1 = "21";
     |LEE 000#37=;
     |SI FSalida ! 0;
         |MENAV "0000 Hay una nueva clave IRPF, se va a proceder a instalar el fichero";

         |CIERRA #37;
         eaExtension = "mod";
         eaPrograma  = "dsmom037";
         |SUB_EJECUTA_NP ":basica/dspatron";

         |ABRE #37;
         #37#0 = "L";
         #37#1 = "21";
         |LEE 000#37=;
         |SI FSalida = 0;
             |MENAV "0000 El fichero se ha instalado satisfactoriamente.";
         |SINO;
             |MENAV "0000 El fichero no se ha instalado, por favor instale la aplicacion ipbasica";
         |FINSI;
     |FINSI;
     |CIERRA #37;

     |HAZ LeeEURODBMOD;

     eaNomDef    = "dsm190ca";
     enResultado = 0;

|| \\\\\\ Alta del Modelo ////////

     |SI enTipoEntrada = 1;              || Consulta del Modelo
         |CABEZA "Modelo 190";
         |ABRE #190;
         #190#0 = enEmpresa;
         #190#1 = enEjer;
         #190#2 = enPeriodo;
         #190#3 = enModelo;
         #190#4 = enComplem;
         #190#5 = enActividad;
         |LEE 040#190=;
         |SI FSalida ! 0;
             |DDEFECTO #190;
             #190#0 = enEmpresa;
             #190#1 = enEjer;
             #190#2 = enPeriodo;
             #190#3 = enModelo;
             #190#4 = enComplem;
             #190#5 = enActividad;
             |GRABA 020#190b;
         |FINSI;

         |PINPA #0#190;
         |PINDA #0#190;
         |HAZ PintaDatos;

         |CONTROL_SIMPLE 0, "BOTON,<F2> Entrada Perceptores  ", 0955, 1179, PulsoF2;
         nBoton1 = FSalida;

         |CONTROL_SIMPLE 0, "BOTON,<F3> Recalculo Perceptores ", 1355, 1579, PulsoF3;
         nBoton2 = FSalida;

         |CONTROL_SIMPLE 0, "BOTON,<F4> Verificacion Calculo Acumulados de Retenciones CLAVE A", 1755, 1979, PulsoF4;
         nBoton3 = FSalida;

         |SI #190#4 ! 0;
             |C_M #190#10, 1, "S";
             |ENDATOS #1#190;
         |FINSI;

         aF2  = &255 + "824";
         aF3  = &255 + "825";
         aF4  = &255 + "826";
         aEsc = &255 + "806";
         |PARA; |SI;  |HACIENDO;
             |LEETECLA aTecla;
             |SI aTecla = aF2;   |HAZ Boton1;  |FINSI;
             |SI aTecla = aF3;   |HAZ Boton2;  |FINSI;
             |SI aTecla = aF4;   |HAZ Boton4;  |FINSI;
             |SI aTecla = aEsc;  |SAL;         |FINSI;
         |SIGUE;

         |FIN_CONTROL_WINDOWS nBoton1;
         |FIN_CONTROL_WINDOWS nBoton2;
         |FIN_CONTROL_WINDOWS nBoton3;

         |PULSATECLA;

         nSuma = #190#6  + #190#7  + #190#8;

         |SI nSuma = 0;
             |BORRA 020#190a;
             enEntrada = 0;
         |SINO;
             |GRABA 020#190a;
             enEntrada = 1;
         |FINSI;
         |LIBERA #190;
         |CIERRA #190;

         |ACABA;
     |FINSI;

|| \\\\\\ Consulta del Modelo ////////

     |SI enTipoEntrada = 4;              || Consulta del Modelo
         |CABEZA "Modelo 190";
         |ABRE #190;
         #190#0 = enEmpresa;
         #190#1 = enEjer;
         #190#2 = enPeriodo;
         #190#3 = enModelo;
         #190#4 = enComplem;
         #190#5 = enActividad;
         |LEE 040#190=;
         |SI FSalida ! 0;
             |MENAV "      No existe Registro.";
             |CIERRA #190;
             |ACABA;
         |FINSI;

         |PINPA #0#190;
         |PINDA #0#190;
         |HAZ PintaDatos;

         |CONTROL_SIMPLE 0, "BOTON,<F2> Entrada Perceptores  ", 1155, 1379, PulsoF2;
         nBoton1 = FSalida;

         aF2  = &255 + "824";
         aEsc = &255 + "806";
         |PARA; |SI;  |HACIENDO;
             |LEETECLA aTecla;
             |SI aTecla = aF2;   |HAZ Boton1;  |FINSI;
             |SI aTecla = aEsc;  |SAL;         |FINSI;
         |SIGUE;

         |FIN_CONTROL_WINDOWS nBoton1;

         |PULSATECLA;

         |CIERRA #190;

         |ACABA;
     |FINSI;

|| \\\\\\ Baja del Modelo ////////

     |SI enTipoEntrada = 3;              || Baja del Modelo
         |ABRE #190;
         #190#0 = enEmpresa;
         #190#1 = enEjer;
         #190#2 = enPeriodo;
         #190#3 = enModelo;
         #190#4 = enComplem;
         #190#5 = enActividad;
         |LEE 040#190=;
         |SI FSalida ! 0;
             |SI enMensa = 0;
                 |MENAV "      No existe Registro.";
             |FINSI;
             |CIERRA #190;
             |ACABA;
         |FINSI;

         nBorra = 1;  |BUCLE dsm190li;
         nBorra = 0;

         |BORRA 020#190a;
         |LIBERA #190;

         |CIERRA #190;

         |ACABA;
     |FINSI;

|| \\\\\\ Alta desde el Pase del Modelo

     |SI enTipoEntrada = 98;
         |ABRE #190;
         #190#0 = enEmpresa;
         #190#1 = enEjer;
         #190#2 = enPeriodo;
         #190#3 = enModelo;
         #190#4 = enComplem;
         #190#5 = enActividad;
         |LEE 040#190=;
         |SI FSalida ! 0;
             #190#0 = enEmpresa;
             #190#1 = enEjer;
             #190#2 = enPeriodo;
             #190#3 = enModelo;
             #190#4 = enComplem;
             #190#5 = enActividad;
             |GRABA 020#190b;
         |FINSI;

         |HAZ Recalculo;
         |GRABA 020#190a;

         |LIBERA #190;
         |CIERRA #190;

         |ACABA;
     |FINSI;

|| \\\\\\ Comprobacion Existencia del Modelo

     |SI enTipoEntrada = 99;              || Existencia del Modelo ????
         |ABRE #190;
         #190#0 = enEmpresa;
         #190#1 = enEjer;
         #190#2 = enPeriodo;
         #190#3 = enModelo;
         #190#4 = enComplem;
         #190#5 = enActividad;
         |LEE 040#190=;
         |SI FSalida ! 0;  enExistencia = 1;  |FINSI;
         |CIERRA #190;

         |SI enActividad = 0;  enResultado = 0;  |FINSI;
         enResultado = 0;
     |FINSI;

     |SI enTipoEntrada = 26;              || Informe de Comprobacion Masivo
          nSwMasivo = 1;
          |HAZ Diferencias;
     |FINSI;
|FPRO;
