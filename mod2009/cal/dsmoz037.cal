|FICHEROS;
     dsmoz037 #0;
     dsmom041 #41;
     dsmom042 #42;
     dsmom043 #43,MANTE;

|FIN;

|VARIABLES;
     &estado = 1;
     aFichero  = "";

     nEmpresa  = 0;
     nDEmpresa = 0;
     nHEmpresa = 0;
     enGraba   = 0;

     &errconta = 0;
     &esalta = 0;
     alfa1 = "";
     nume1 = 0;
     nInkey = 0;
     nAnyV = 0;
     nAnyC = 0;
     nImpVenta = 0;
     nImpAcumA = 0;
     nModoEntrada = 0;

     &nBoton2 = 0;
     &nBoton3 = 0;
     aF2     = "";
     aEsc    = "";
     &nTecla  = 0;
     &eaRelCta = "";
     nLnEstado = 0;
     nPara1  = 0;
     nNume1  = 0;
     &nDesdeAqui = 0;

     fFecha1 = @;
     aAlfa1  = "";
     nSwBorro = 0;
|FIN;

|INCLUYE i_variar;

|| /////////////////////// Calculos Fichero z037 \\\\\\\\\\\\\\\\\\\\
|RUTINA ClaveBaja43;
     #43#0 = #0#0;
     #43#2 = "        ";
     #43#3 = 00;
     #43#14 = enEjer;
|FRUT;

|RUTINA ClaveAlta43;
     #43#0 = #0#0;
     #43#2 = "zzzzzzzz";
     #43#3 = 99;
     #43#14 = enEjer;
|FRUT;

|PROCESO Tipo7;  |TIPO 7;

   |PINTA  #0#0; |PINTA  #0#1;
   enEmpresa   = #0#0;

   |ABRE #43;
   #43#0 = #0#0;
   #43#2 = "        ";
   #43#3 = 00;
   |LEE 001#43];
   |SI FSalida ! 0; |O #43#0 ! #0#0;
       |DDEFECTO #43;
   |FINSI;
   |CIERRA #43;
   |PINDA #0#43;

   enModo = 7;
   |ENTLINEAL #1#43, -2, 7, 12, 0, ClaveBaja43, ClaveAlta43;

   nDesdeAqui = 1;
   |HAZ MTipo7;
   nDesdeAqui = 0;
|FPRC;

|PROCESO Tipo11;  |TIPO 11;
 nTecla = FSalida;
 |SI nTecla = 11;
       |ERROR;
       eaElProm = "dsmoz037";
       |HAZ ConsuCliente;
       eaElProm = "";
       |PTEC 802;
       |ACABA;
  |FINSI;

 nDesdeAqui = 1;
 |HAZ MTipo0;
 nDesdeAqui = 0;
|FPRC;

|CALCULO LeelaEmpresa; |TIPO 0;
 nTecla = FSalida;
 |FIN_CONTROL_WINDOWS nBoton2;
 |FIN_CONTROL_WINDOWS nBoton3;
 |PULSATECLA;

|| ... Lee Control de Entrada
  errconta = 0;
  enEmpresa = #0#0;
  |ENTLINEAL #1#43, -2, 4, 12, 0, ClaveBaja43, ClaveAlta43;

|FCAL;

|| **********************************************************************
|| **********************************************************************
|| /////////////////////// PROCESOS DE TRABAJO \\\\\\\\\\\\\\\\\\\\

|PROCESO LeeLasLineas0;
 |SI #42#6 = "C";
     #43#17 = #42#4;
 |FINSI;
 |SI #42#6 = "A";
     nImpAcumA = nImpAcumA + #42#11;
 |FINSI;
 |SI #42#6 = "V";
     nImpVenta = nImpVenta + #42#11;
     #43#18 = #42#4;
 |FINSI;
|FPRC;

|DEFBUCLE LeeLasLineas;
 #42#1;
 ;
 #41#0, #41#2, #41#3, 001;
 #41#0, #41#2, #41#3, 999;
 e;
 NULL, LeeLasLineas0;
|FIN;

|PROCESO GrabaTempo;
  nSwBorro = 0;
  |DDEFECTO #43;
  #43#0  = #41#0;
  #43#2  = #41#2;
  #43#3  = #41#3;
  #43#14 = enEjer;
  |LEE 101#43=;
  nModoEntrada = FSalida;

  enEmpresa   = #41#0;
  enActividad = #41#1;
  eOpDep = 0;
  |HAZ LActividad;

  |SI #41#26 > "01.01.1900";

      #43#0 = #41#0; #43#1 = #41#1;
      #43#2 = #41#2; #43#3 = #41#3;
      #43#4 = #41#4; #43#5 = #41#5;

      #43#6 = #41#26;  ||Fecha de Transmision
      #43#7 = #41#27;  ||Fecha de Adquisicion

      alfa1 = #43#6; alfa1 = alfa1 % -104;
      nAnyV = alfa1;
      alfa1 = #43#7; alfa1 = alfa1 % -104;
      nAnyC = alfa1;

      |SI nAnyV = enEjer;

          |SI #41#26 ! efActFin;

              #43#8 = (nAnyV - nAnyC) + 1;  ||A¤os Generacion
              nImpVenta = 0;
              nImpAcumA = 0;
              |BUCLE LeeLasLineas;

              #43#9  = nImpVenta;       ||Valor transmision
              #43#10 = #41#10;          ||Importe adquisicion
              #43#11 = #43#9 - #43#10;  ||Diferencia
              #43#12 = nImpAcumA;
              #43#13 = #43#12 + #43#11; || Amortizacion acumulada + diferencia venta/compra

              #43#14 = nAnyV;
              #43#15 = #41#32;
              #43#16 = #41#33;

              |SI nModoEntrada = 0; |GRABA 020#43a; |FINSI;
              |SI nModoEntrada ! 0; |GRABA 020#43b; |FINSI;
              nSwBorro = 1;

              |LIBERA #43;

              #0#0 = #43#0;
              |LEE 001#0=;
              |SI FSalida ! 0;
                  #0#0 = #43#0;
                  #0#1 = #43#15;
                  #0#2 = enEjer;
                  |GRABA 020#0n;
                  enGraba = 1;
              |FINSI;

          |FINSI;

      |FINSI;

  |FINSI;

  |SI nSwBorro = 0; |Y nModoEntrada = 0;
      |BORRA 020#43a;
      |LIBERA #43;
  |SINO;
      |SI nEmpresa = 0;
          nEmpresa = #43#0;
      |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE dsmom041;
   #41#1;
   ;
   nDEmpresa, "        ", 00;
   nHEmpresa, "zzzzzzzz", 99;
   ;
   NULL, GrabaTempo;
|FIN;
|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO LosBorroPrimero;
 #41#0 = #43#0;
 #41#2 = #43#2;
 #41#3 = #43#3;
 |LEE 001#41=;
 |SI FSalida ! 0;
     |BORRA 020#43a;
 |FINSI;
 |LIBERA #43;
|FPRC;

|DEFBUCLE LosBorroPrimero;
   #43#1;
   ;
   nDEmpresa, "        ", 00, enEjer;
   nHEmpresa, "zzzzzzzz", 99, enEjer;
   be;
   NULL, LosBorroPrimero;
|FIN;
|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\

|RUTINA ClaveBaja0;
  |SI FSalida = "POSICION"; |Y enEmpresa ! 0;
      #0#0 = enEmpresa;
  |SINO;
      #0#0 = 1;
  |FINSI;
|FRUT;

|RUTINA ClaveAlta0;
     #0#0 = 99999;
|FRUT;

|PROGRAMA;
    |CLS;
    |CABEZA "Ganancias y Perdidas Patrimoniales de Elementos";

    |PINPA #0#0;
    fFecha1 = ~;
    aAlfa1 = fFecha1; aAlfa1 = aAlfa1 % -104;
    #0#2 = aAlfa1; #0#2 = #0#2 - 1; |PINTA #0#2;

    aFichero = ("z37" + Usuario) % 108;
    |NOME_DAT #0 aFichero;
    |ABRE #0;   |CIERRA #0;   |DELINDEX #0;

    |ENCAMPO #2#0;
    |SI FSalida = 7; |O FSalida = 1; |ACABA; |FINSI;

    enEjer    = #0#2;
    nDEmpresa = 00000;
    nHEmpresa = 99999;
    |ABRE #41;
    |BUCLE LosBorroPrimero;
    |CIERRA #41;

    |ABRE #0;
    |ABRE #43;
    |BUCLE dsmom041;
    |CIERRA #43;
    |CIERRA #0;
    enEmpresa = nEmpresa;

    |ENTLINEAL #1#0, -1, 4, 22, 0, ClaveBaja0, ClaveAlta0;
    |DELINDEX #0;
|FPRO;
