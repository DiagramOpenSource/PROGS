|FICHEROS;
   dsmoz090 #0;
   dsmom090 #90,MANTE;
   dsmom091 #91,MANTE;
   dsmom092 #92,MANTE;
   dsmow094 #94,MANTE;

   :/basica/def/agicen14 #114;
   :/basica/def/agifigen #1000;

   &imagen90@ #441;
   &imagen91@ #442;
|FIN;

|VARIABLES;
  &enDMaqu   = 0;
  &enHMaqu   = 0;
  inkey      = 0;
  aAlfa1     = "";
  aAlfa2     = "";
  nNume1     = 0;
  nSwSal     = 0;
  nSwEsta    = 0;
  nContenido = "";
  aAlfa      = "";
   esalta    = 0;
   esaltaL   = 0;
   elcon       = 0;
   aTMov       = "";
   nTMov       = 0;
   nSw         = 0;
   nAlta       = 0;
   nBaja       = 0;
   nTMovAnt    = 0;
   nHLin       = 0;
   aTABaja     = "";
   aTAAlta     = 0;
   fTAAlta     = @;
   fTABaja     = @;
   nEstaLin    = 0;

   fFecha1 = @;
   fFecha2 = @;
   nUltEjer = 0;
   nCuota   = 0;
   nEjer    = 0;
   nExiLin  = 0;
   nPara1   = 0;
   nSwExiste = 0;
   &snFilBa = "";
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;  || Variables de Contabilidad

**************************************************************************
|RUTINA ClaveBaja91;
     #91#0 = enEmpresa;
     #91#1 = #90#1;
     #91#2 = 001;
|FRUT;

|RUTINA ClaveAlta91;
     #91#0 = enEmpresa;
     #91#1 = #90#1;
     #91#2 = 999;
|FRUT;

**************************************************************************
|CALCULO elTipo0; |TIPO 0;
   esalta  = (FEntrada / 100) ! 0;
   |SI esalta = 1;
       #90#22 = #92#2; |PINTA #90#22;
   |FINSI;
|FCAL;

|PROCESO BorraLineas;
    |BORRA 020#91a;
    |LIBERA #91;
|FPRC;

|DEFBUCLE dsmom091;
     #91#1;
     ;
     #90#0, #91#1, 000;
     #90#0, #91#1, 999;
     be;
     NULL, BorraLineas;
|FIN;

|CALCULO Tipo2_1;  |TIPO 2;
     esalta  = (FEntrada / 100) ! 0;
     |SI esalta ! 3;  |ACABA;  |FINSI;

     |BUCLE dsmom091;
|FCAL;

|CALCULO elTipo3; |TIPO 3;
    esalta  = (FEntrada / 100) ! 0;
    |SI esalta = 1; #90#11 = "01.01.0000";|FINSI;

    #90#19 = #92#1; ||A todas las cabeceras Nombre empresa
    ||  #90#22 = #92#2;
    #90#21 = #114#4;  ||Actividad

    nSwSal = 0;
    |SI esalta = 1;
        |HAZ CalculaCompra; ||Primer Movimiento de Alta
    |FINSI;

    |ENTLINEAL #1#91, -3, 1, 22, 0, ClaveBaja91, ClaveAlta91;
    |LIBERA #90;
|FCAL;


|CALCULO PresentaLinea;  |TIPO 7;
     esalta = (FEntrada / 100) ! 0;
     |HAZ TipoAlta;
     |HAZ TipoBaja;
     nSwSal = 1;
     |ENTLINEAL #1#91, -3, 7, 22, 0, ClaveBaja91, ClaveAlta91;
|FCAL;

|PROCESO inf441;
 #441#0 = enEmpresa;
 #441#2 = "";
|FPRC;

|PROCESO sup441;
 #441#0 = enEmpresa;
 #441#2 = "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz";
|FPRC;

|CALCULO T11; |TIPO 11;
  inkey = FSalida;
  |SI inkey = 12;
      enEmpresa = #90#0;
      enDMaqu   = #90#1;
      enHMaqu   = 0;
      |SUB_EJECUTA_NP "dsmoy090";
      |ERROR;
  |FINSI;

  |SI inkey = 13;
      enEmpresa = #90#0;
      enDMaqu   = 1;
      enHMaqu   = 99999;
      |SUB_EJECUTA_NP "dsmoy090";
      |ERROR;
  |FINSI;

  |SI inkey = 10;
      enEmpresa  = #90#0;
      |ABRE #441;
      #441#0 = #90#0;
      #441#1 = #90#1;
      #441#2 = #90#2;
      |CONSULTA_F_CLAVE #2#441,inf441,sup441;
      #90#0 = #441#0;
      #90#1 = #441#1;
      |CIERRA #441;
      |PTEC 802;
  |FINSI;
|FCAL;

|CALCULO LeeLaActividad; |TIPO 0;
  inkey = FSalida;

  eOpDep = 0;
  |SI inkey = 10; eOpDep = 1; |FINSI;
  enActividad = #90#20;
  |HAZ LActividad;
  eOpDep = 0;
  |SI eswLect = 1;
      |MENAV "      Actividad Inexistente";
      |ERROR;
  |SINO;
      #90#20 = enActividad; |PINTA #90#20;
      #90#21 = eaNombreAct; |PINTA #90#21;
      |SI efActFin > "01.01.1900"; |Y snFilBa ! "N";
          aAlfa1 = "    NAtencion!!!. Actividad con Fecha de BAJA " + efActFin + ", Desea introducir Facturas ? ";
          |CONFI aAlfa1;
          |SI FSalida ! 0;
              |ERROR;
              |ACABA;
          |FINSI;
      |FINSI;
      |SI eaEsCom = "S";
          |CONFI "    NAtencion!!!. Actividad Comunera, ¨ Desea introducir Elementos ? ";
          |SI FSalida ! 0;
              |ERROR;
              |ACABA;
          |FINSI;
      |FINSI;
  |FINSI;
|FCAL;

|PROCESO LaAlta; |TIPO 7;
     esalta = (FEntrada / 100) ! 0;
     aAlfa1 = "N";
     |SI esalta = 1; aAlfa1 = "S"; |FINSI;
     |C_M #90#9, 1,aAlfa1;
     |C_M #90#10,1,aAlfa1;
     |C_M #90#11,1,aAlfa1;
     |C_M #90#23,1,aAlfa1;
|FPRC;

|PROCESO TipoAlta;
                aAlfa1 = "               ";
 |SI #90#9 = 1; aAlfa1 = "Aut.A¤os Anter."; |FINSI;
 |SI #90#9 = 2; aAlfa1 = "Nueva Autoriza."; |FINSI;
 |SI #90#9 = 4; aAlfa1 = "Por Sustitucion"; |FINSI;
 |PINTA 0765, aAlfa1;
|FPRC;

|PROCESO TipoBaja;
                aAlfa1 = "               ";
 |SI #90#18 = 5; aAlfa1 = "Suspen temporal"; |FINSI;
 |SI #90#18 = 6; aAlfa1 = "Extincion      "; |FINSI;
 |SI #90#18 = 7; aAlfa1 = "Baja Sin Susti."; |FINSI;
 |SI #90#18 = 8; aAlfa1 = "Baja Con Susti."; |FINSI;
 |SI #90#18 = 9; aAlfa1 = "Otros          "; |FINSI;
 |PINTA 0865, aAlfa1;
|FPRC;

|| ***************************************************************************
||   Calculos de las Lineas

|CALCULO AntFec; |TIPO 7;
  esaltaL = (FEntrada / 100) ! 0;
  |SI esaltaL = 1;
      fFecha1 = ~;
      aAlfa1 = fFecha1; aAlfa1 = aAlfa1 % -104;
      #91#13 = aAlfa1;  |PINTA #91#13;

      aAlfa1 = "01.01." + aAlfa1;
      #91#3  = aAlfa1; |PINTA #91#3;
  |FINSI;
|FCAL;

|CALCULO LimFecha; |TIPO 0;
 aAlfa1 = #91#13;
 aAlfa2 = "01.01." + aAlfa1;fFecha1 = aAlfa2;
 aAlfa2 = "31.12." + aAlfa1;fFecha2 = aAlfa2;
 |SI #91#3 < fFecha1; |O #91#3 > fFecha2;
     |MENAV "    Fecha incorrecta";
     |ERROR;
 |FINSI;
|FCAL;


|PROCESO BuscoEstado;

 |SI #442#2 = nEstaLin; |ACABA; |FINSI;
 |SI #442#4 = 1; |O #442#4 = 2; |O #442#4 = 4;
     nAlta = 1;
     nBaja = 0;
     aTAAlta = #442#4;
 |FINSI;
 |SI #442#4 ] 5; |Y #442#4 [ 9;
     nAlta = 0;
     nBaja = 1;
     aTABaja = #442#4;
 |FINSI;
 |SI #442#4 = 0;
     |SI nUltEjer < #442#13;
         nUltEjer = #442#13;
         nCuota   = #442#12;
     |FINSI;
     |SI nEjer = #442#13; nExiLin = #442#2; |FINSI;
 |FINSI;

|FPRC;

|DEFBUCLE BuscoEstado;
 #442#1003;
 ;
 #90#0, #90#1, 001;
 #90#0, #90#1, nHLin;
 ;
 NULL, BuscoEstado;
|FIN;

|CALCULO SalgoYa;  |TIPO 7;
     |SI nSwSal = 1;  |PTEC 807;  |FINSI;
|FCAL;

|CALCULO Tipo2_2;  |TIPO 2;
   esaltaL = (FEntrada / 100) ! 0;
|FCAL;

|CALCULO baja; |TIPO 1;
   |SI esalta = 3; |ACABA; |FINSI;

   esaltaL = (FEntrada / 100) ! 0;
   elcon = 0;
   |SI #91#2 = 1; |Y esaltaL = 3;
      |MENAV "0000*** NO PUEDE DAR DE BAJA ESTA LINEA ***";
      |ERROR;
      elcon = 1;
      |ACABA;
   |FINSI;

   |SI esaltaL ! 3; |ACABA; |FINSI;

   nExiLin  = 0;
   nEjer    = 0;
   nUltEjer = 0;
   nCuota   = 0;
   nEstaLin = #91#2;
   nTMovAnt = #91#4;
   nBaja = 0; aTAAlta = ""; fTAAlta = "01.01.0000";
   nAlta = 0; aTABaja = ""; fTABaja = "01.01.0000";
   nHLin = 999;
   |BUCLE BuscoEstado;

   |SI nTMovAnt = 2; |O nTMovAnt = 4; |O nTMovAnt = 1;
       #90#9  = aTAAlta;  |PINTA #90#9;
       #90#10 = fTAAlta;  |PINTA #90#10;
       |HAZ TipoAlta;
   |FINSI;
   |SI nTMovAnt ] 5; |Y nTMovAnt [ 9;
       #90#18 = aTABaja;  |PINTA #90#18;
       #90#17 = fTABaja;  |PINTA #90#17;
       |HAZ TipoBaja;
   |FINSI;
   |SI nTMovAnt = 0;
       #90#23 = nCuota; |PINTA #90#23;
   |FINSI;
|FCAL;

|PROCESO AntTipoMov; |TIPO 7;
   |PUSHV 12302380;
   |PINTA 1232, "                                                 ";
   |ATRI R;
   |PINTA 1332, "0.Cuota tributaria devengada en el ejercicio     ";
   |PINTA 1432, "1.Maquina autorizada en a¤os anteriores          ";
   |PINTA 1532, "2.Maquina de nueva autorizacion                  ";
   |PINTA 1632, "3.Modificacion del precio autorizado por partida ";
   |PINTA 1732, "4.Alta por sustitucion                           ";
   |PINTA 1832, "5.Suspension temporal de la autorizacion         ";
   |PINTA 1932, "6.Extincion o revocacion de la autorizacion      ";
   |PINTA 2032, "7.Baja definitiva de la maquina sin sustitucion  ";
   |PINTA 2132, "8.Baja definitiva de la maquina con sustitucion  ";
   |PINTA 2232, "9.Otros                                          ";
   |ATRI 0; |CUADRO 1231 2380;
   |ATRI P;
      |PINTA 1234, "OPERACIONES";
      |PINTA 1332, "0.";
      |PINTA 1432, "1.";
      |PINTA 1532, "2.";
      |PINTA 1632, "3.";
      |PINTA 1732, "4.";
      |PINTA 1832, "5.";
      |PINTA 1932, "6.";
      |PINTA 2032, "7.";
      |PINTA 2132, "8.";
      |PINTA 2232, "9.";
   |ATRI 0
|FPRC;

|PROCESO VerTipoMov;
 |POPV;
 nTMovAnt = Contenido;
 nTMov    = #91#4;
 |HAZ VerTipos0;
 |SI nSw = 0;
     #91#5 = aTMov; |PINTA #91#5;
     |HAZ PedirOtros;
 |SINO;
     |SI nSw = 1;
         |MENAV "     Codigo de Operacion Incorrecto";
         #91#4 = nTMovAnt; |PINTA #91#4;
         |ERROR;
     |SINO;
          aAlfa1 = nExiLin;
          aAlfa1 = "    NYa existe Cuota Anual en la Linea " + aAlfa1 + ". Desea continuar ";
          |CONFI aAlfa1;
          |SI FSalida ! 0;
              |ERROR;
          |FINSI;
     |FINSI;
 |FINSI;
|FPRC;


|PROCESO PedirOtros;
 |SI #91#4 ! 0; |Y #91#4 ! 3; |Y #91#4 ! 4; |Y #91#4 ! 8;
     |ACABA;
 |FINSI;

 |PUSHV 15302380;
 |PINPA #0#94;

 |PARA nPara1 = 0; |SI nPara1 [ 14; |HACIENDO nPara1 = nPara1 + 1;
       |SI nPara1 > 5; |C_V #94nPara1,1,"N"; |C_M #94nPara1,1,"N"; |FINSI;
       #94nPara1 = #91nPara1;
 |SIGUE;
 |C_V #94#15,1,"N"; |C_M #94#15,1,"N";

 |PINTA #94#5;
 |SI #91#4 = 0;
     |ATRI P; |PINTA 1934, "Nueva Cuota Tributaria "; |ATRI 0;
     |C_V #94#12,1,"S"; |C_M #94#12,1,"S";
     |PINTA #94#12;
 |FINSI;
 |SI #91#4 = 3;
     |ATRI P; |PINTA 2034, "Nuevo Precio por Partida"; |ATRI 0;
     |C_V #94#6,1,"S"; |C_M #94#6,1,"S";
     |PINTA #94#6;
 |FINSI;

 |SI #91#4 = 4; |O #91#4 = 8;
                    aAlfa1 = "Codigo Maquina a la que sustituye .: ";
     |SI #91#4 = 8; aAlfa1 = "Codigo Maquina que la sustituye .....: "; |FINSI;
     |ATRI P; |PINTA 1734, aAlfa1;
     |PINTA 1834, "Numero Guia de Circulacion..: ";
     |PINTA 1934, "Serie y n§ de fabricacion ..: ";
     |PINTA 2034, "N§ Autorizacion Explotacion : ";
     |PINTA 2134, "Fecha autorizacion/solicitud: ";
     |PINTA 2234, "                              CONFORME S/N: ";
     |C_V #94#7, 1,"S"; |C_M #94#7, 1,"S";  |PINTA #94#7;
     |C_V #94#8, 1,"S"; |C_M #94#8, 1,"S";  |PINTA #94#8;
     |C_V #94#9, 1,"S"; |C_M #94#9, 1,"S";  |PINTA #94#9;
     |C_V #94#10,1,"S"; |C_M #94#10,1,"S";  |PINTA #94#10;
     fFecha1 = ~;
     |SI #94#11 [ "01.01.0000"; #94#11 = fFecha1; |FINSI;
     |C_V #94#11,1,"S"; |C_M #94#11,1,"S"; |PINTA #94#11;
     |C_V #94#15,1,"S"; |C_M #94#15,1,"S";  #94#15 = "N"; |PINTA #94#15;
 |FINSI;

 |ENDATOS #1#94;
 |SI #94#15 = "N"; |O FSalida ! 0;
     |ERROR;
 |SINO;
     |SI FSalida = 0;
         |PARA nPara1 = 6; |SI nPara1 [ 14; |HACIENDO nPara1 = nPara1 + 1;
               #91nPara1 = #94nPara1;
         |SIGUE;
     |FINSI;
 |FINSI;
 |POPV;
|FPRC;

|CALCULO fecven; |TIPO 3;

   |BLIN 24;
   |SI #91#4 = 2; |O #91#4 = 4; |O #91#4 = 1;
       #90#9  = #91#4;  |PINTA #90#9;
       #90#10 = #91#3;  |PINTA #90#10;
       #90#11 = #91#3;  |PINTA #90#11;
       |HAZ TipoAlta;
   |FINSI;
   |SI #91#4 ] 5; |Y #91#4 [ 9;
       #90#18 = #91#4;  |PINTA #90#18;
       #90#17 = #91#3;  |PINTA #90#17;
       |HAZ TipoBaja;
   |FINSI;
   |SI #91#4 = 3;
       #90#4 = #91#6; |PINTA #90#4;
   |FINSI;
   |SI #91#4 = 0;
       nExiLin  = 0;
       nUltEjer = 0;
       nCuota   = 0;
       nEstaLin = 0;
       nHLin = 999;
       |BUCLE BuscoEstado;
       |SI nUltEjer [ #91#13; nCuota = #91#12; |FINSI;
       #90#23 = nCuota; |PINTA #90#23;
   |FINSI;
|FCAL;

|PROCESO EntraAlaLinea;  |TIPO 5;
  |SI enModo = 7;
      |SALVA_DATOS #90;
      #90#0 = enEmpresa;
      #90#1 = 00001;
      |LEE 000#90];
      |SI FSalida ! 0; |O #90#0 ! enEmpresa;
          |DDEFECTO #90;
      |FINSI;
      |PINDA #0#90;
      |REPON_DATOS #90;
  |FINSI;

  |SI enModo ! 2; |ACABA; |FINSI;
  || Lee Empresa
    |ABRE #1000;
    #1000#0 = enEmpresa;
    |LEE 041#1000=;
    |SI FSalida ! 0;
        |MENAV "     Empresa Inexistente";
        |CIERRA #1000;
        |ACABA;
    |FINSI;
    #90#19 = #1000#1;
    enTpEm = #1000#87;
    #92#1  = #1000#1;
||    #90#22 = #92#2;
    |CIERRA #1000;
|FPRC;

|| ***************************************************************************
|PROCESO CalculaCompra;
   || ........ ......    el PROCESO prim_lin
      aAlfa1 = #90#10;
      aAlfa1 = aAlfa1 % -104;
      |ABRE #91;
      |DDEFECTO #91;
      #91#0 = #90#0;
      #91#1 = #90#1;
      #91#2 = 1;
      #91#3 = #90#10;
      #91#4 = #90#9;
      nTMov = #91#4;
      |HAZ VerTipos;
      #91#5 = aTMov;
      #91#6 = #90#4;
      #91#13 = aAlfa1;
      |GRABA 021#91n;

      #91#0 = #90#0;
      #91#1 = #90#1;
      #91#2 = 2;
      #91#3 = #90#10;
      #91#4 = 0;
      nTMov = #91#4;
      |HAZ VerTipos;
      #91#5 = aTMov;
      #91#12 = #90#23;
      #91#13 = aAlfa1;
      |GRABA 021#91n;

      |CIERRA #91;
|FPRC;


|PROCESO VerTipos0;

  nSw   = 0;
  esaltaL = (FEntrada / 100) ! 0;

  |SI esaltaL ! 1;
      |SI nTMovAnt = 1; |O nTMovAnt = 2; |O nTMovAnt = 4;
          |SI nTMov ! 1; |Y nTMov ! 2; |Y nTMov ! 4;
              nSw = 1;
          |FINSI;
      |FINSI;
      |SI nTMovAnt = 3; |O nTMovAnt = 0;   || No cambia de tipo
          |SI nTMov ! nTMovAnt; nSw = 1; |FINSI;
      |FINSI;
      |SI nTMovAnt ] 5; |Y nTMovAnt [ 9;
          |SI nTMov < 5;
              nSw = 1;
          |FINSI;
      |FINSI;
  |SINO;
      || ... ... ... ... Alta de Linea
      |SI nTMov = 1;
          |SI #91#2 ! 1; nSw = 1; |FINSI;
      |SINO;
          |SI #91#2 = 1;
              |SI nTMov ] 5; |Y nTMov [ 9;
                  nSw = 1;
              |FINSI;
          |SINO;
              nExiLin  = 0;
              nEjer    = #91#3;
              nUltEjer = 0;
              nCuota   = 0;
              nEstaLin = #91#2;
              nTMovAnt = #91#4;
              nBaja = 0; aTAAlta = ""; fTAAlta = "01.01.0000";
              nAlta = 0; aTABaja = ""; fTABaja = "01.01.0000";
              nHLin = #91#2 - 1;
              |BUCLE BuscoEstado;
              |SI nAlta = 1;
                  |SI nTMov = 1; |O nTMov = 2; |O nTMov = 4;
                      nSw = 1;
                  |FINSI;
              |FINSI;
              |SI nBaja = 1;
                  |SI nTMov ] 5; |Y nTMov [ 9;
                      nSw = 1;
                  |FINSI;
                  |SI nTMov = 3;
                      nSw = 1;
                  |FINSI;
              |FINSI;
              |SI nExiLin ! 0; |Y nTMov = 0;
                  nSw = 2;
              |FINSI;
          |FINSI;
      |FINSI;
   |FINSI;

   aTMov = "";
   |SI nSw = 0;
       |HAZ VerTipos;
   |FINSI;
|FPRC;

|PROCESO VerTipos;
   aTMov = "";
   |SI nTMov = 0; aTMov = "Cuota tributaria devengada en el ejercicio     "; |FINSI;
   |SI nTMov = 1; aTMov = "Maquina autorizada en a¤os anteriores          "; |FINSI;
   |SI nTMov = 2; aTMov = "Maquina de nueva autorizacion                  "; |FINSI;
   |SI nTMov = 3; aTMov = "Modificacion del precio autorizado por partida "; |FINSI;
   |SI nTMov = 4; aTMov = "Alta por sustitucion                           "; |FINSI;

   |SI nTMov = 5; aTMov = "Suspension temporal de la autorizacion         "; |FINSI;
   |SI nTMov = 6; aTMov = "Extincion o revocacion de la autorizacion      "; |FINSI;
   |SI nTMov = 7; aTMov = "Baja definitiva de la maquina sin sustitucion  "; |FINSI;
   |SI nTMov = 8; aTMov = "Baja definitiva de la maquina con sustitucion  "; |FINSI;
   |SI nTMov = 9; aTMov = "Otros                                          "; |FINSI;

|FPRC;
