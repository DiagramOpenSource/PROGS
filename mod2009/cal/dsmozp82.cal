|FICHEROS;
     dsmozp82 #0;

     dsmom002 #2;
     dsmom004 #4;
     dsmom008 #8;

     dsmom027 #27;
     dsmom080 #80;
     dsmom081 #81;
     dsmom082 #82;
     dsmom083 #83;

     dsmom107 #107;
     dsmom109 #109;
     dsmociva #300;

     dsmow082 #982,MANTE;
     dsmow083 #983,MANTE;

     :/contagen/def/canempre #101;

     :/basica/def/agifigen #1000;
     :/basica/def/agicen14 #1014;
     :/basica/def/agicen22 #1022;
|FIN;

|VARIABLES;
     &eaPathBasica = "";
     aBase       = "";
     aPathModelo = "";
     aDef        = "";
     &aFichw82   = "";
     aFichero    = "";
     aAlfa       = "";
     aAlfa1      = "";
     aAlfa2      = "";
     aAlfa3      = "";
     nNume1      = 0;
     nNume2      = 0;
     nPara1      = 0;
     nPara2      = 0;
     fFecha      = @;

     nModp80       = 0;
     nCampo        = 0;
     nDEmpresa     = 0;
     nHEmpresa     = 0;
     enGraba       = 0;
     nBaseImpo     = 0;
     nCuota        = 0;
     nDPeriodo     = 0;
     nHPeriodo     = 0;
     nEjer         = 0;

     nSwConPro   = 0;
     fFecIni     = @;
     fFecFin     = @;

     nSwRegEsp   = 0;
     nSwSubv     = 0;
     nSwContab   = 0;
     nSwExentas  = 0;
     nSwVarios   = 0;
     nSw         = 0;
     nRegIVA     = 0;
     nReg109     = 0;
     nNumAct     = 0;
     nDepar      = 0;
     nVentaCon   = 0;
     nVentaSin   = 0;
     nVentaSub   = 0;
     nVentaSuI   = 0;
     nVentaNue   = 0;
     nSwOpm81    = 0;
     nSwOpe82    = 0;
     nSwOpe83    = 0;
     nImporteMas = 0;
     nIvaSop     = 0;
     nIvaSub     = 0;

     nSwCon = 0;
     nSwSin = 0;

     nActSuma    = 0;
     aTipo       = "";
     aLit        = "";
     nActivMas   = 0;
     aCNAE       = "";
     &aTipoPr    = "";
     &nTp100     = 0;
     &nAny       = 0;

     nIvaComun = 0;
      nSwBorro = 0;
     &aParam   = "";
     nProrr    = 0;
     nProrr1   = 0;
     aProrr    = "";
     nReg99    = 0;
|FIN;

|INCLUYE i_variar;

|| /////////////////////// PROCESOS ENTRADA POR PANTALLA \\\\\\\\\\\\\\\\\\\\

|PROCESO Ejercicio;  |TIPO 7;
     fFecha = ~;
     aAlfa  = fFecha % -104;
     #0#0   = aAlfa;
     |PINTA #0#0;
|FPRC;

|PROCESO Parrilla;
     |SI #0#1 = 0;
         #0#1 = 0;  |PINTA #0#1;
         #0#2 = 0;  |PINTA #0#2;  |C_M #0#2, 1, "N";
         |PARA nCampo = 3;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#1, 1, "S";
             |C_M #0#2, 1, "S";
         |FINSI;

         |PARA nCampo = 3;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|| ////////////////////////////////////////////////////////// CALCULOS
|| .........................................................................
|| ................DATOS DESDE CONTABILIDAD ................................
|| .........................................................................
|PROCESO dsmociva;

  |SI #300#6 = 1; |O #300#6 = 3;
     nSwCon = 0;
     nSwSin = 0;
     |SI #300#6 = #109#6;  |Y #300#7 = #109#7;   nSwCon = 1; |FINSI;
     |SI #300#6 = #109#8;  |Y #300#7 = #109#9;   nSwCon = 1; |FINSI;
     |SI #300#6 = #109#10; |Y #300#7 = #109#11;  nSwCon = 1; |FINSI;
     |SI #300#6 = #109#12; |Y #300#7 = #109#13;  nSwCon = 1; |FINSI;
     |SI #300#6 = #109#14; |Y #300#7 = #109#15;  nSwCon = 1; |FINSI;

     |SI #300#6 = #109#16; |Y #300#7 = #109#17;  nSwSin = 1; |FINSI;
     |SI #300#6 = #109#18; |Y #300#7 = #109#19;  nSwSin = 1; |FINSI;
     |SI #300#6 = #109#20; |Y #300#7 = #109#21;  nSwSin = 1; |FINSI;
     |SI #300#6 = #109#22; |Y #300#7 = #109#23;  nSwSin = 1; |FINSI;
     |SI #300#6 = #109#24; |Y #300#7 = #109#25;  nSwSin = 1; |FINSI;

     |SI nSwCon = 1;
         |SI #300#6 = 1;
             |SI #300#15 ! 0;  nVentaCon = nVentaCon + #300#14; |FINSI;
             |SI #300#18 ! 0;  nVentaCon = nVentaCon + #300#17; |FINSI;
             |SI #300#21 ! 0;  nVentaCon = nVentaCon + #300#20; |FINSI;
             |SI #300#24 ! 0;  nVentaCon = nVentaCon + #300#23; |FINSI;
         |SINO;
             nVentaCon = nVentaCon + #300#12;
         |FINSI;
     |FINSI;
     |SI nSwSin = 1;
         |SI #300#6 = 1;
             |SI #300#15 = 0;  nVentaSin = nVentaSin + #300#14; |FINSI;
             |SI #300#18 = 0;  nVentaSin = nVentaSin + #300#17; |FINSI;
             |SI #300#21 = 0;  nVentaSin = nVentaSin + #300#20; |FINSI;
             |SI #300#24 = 0;  nVentaSin = nVentaSin + #300#23; |FINSI;
         |SINO;
             nVentaSin = nVentaSin + #300#12;
         |FINSI;
     |FINSI;
   |FINSI;

   |SI #300#6 = 2; |Y #300#7 = 11;
       nVentaSuI =  nVentaSuI + #300#12;
       nIvaSub   =  nIvaSub   + #300#13;
   |FINSI;

   |SI enActividad = 99;  nReg99 = 1;  |FINSI;
|FPRC;

|DEFBUCLE dsmociva;
    #300#1;
    ;
    enEmpresa, nAny, 01, nModp80, 00, enActividad, 1, 00;
    enEmpresa, nAny, 12, nModp80, 99, enActividad, 3, 99;
    ;
    NULL, dsmociva;
|FIN;

|PROCESO LeeElModelo;
 |SI #2#8 > "01.01.0000";
     |SI #2#8 > fFecFin;  |ACABA;  |FINSI;
 |FINSI;
 |SI #2#9 > "01.01.0000";
     |SI #2#9 < fFecIni;  |ACABA;  |FINSI;
 |FINSI;

 nModp80 = #2#2;
 |SI nModp80 = 300; |O nModp80 = 320; |O nModp80 = 330; |O nModp80 = 332;
     |BUCLE dsmociva;
 |FINSI;
 |SI nModp80 = 310; |O nModp80 = 311;
     || ..falta el 310
 |FINSI;
 |SI nModp80 = 370;
     |BUCLE dsmociva;
     || ..falta el 310
 |FINSI;
|FPRC;

|DEFBUCLE LeeElModelo;
  #2#1;
  ;
  enEmpresa, enActividad, 300;
  enEmpresa, enActividad, 371;
  ;
  NULL, LeeElModelo;
|FIN;

|PROCESO ActivRegul99;  || Proceso nuevo para el calculo de la regul.

      nReg99   = 0;
      enActividad = 99;
      nVentaSub = 0;
      nVentaCon = 0;
      nVentaSin = 0;
      nVentaSuI = 0;
      nIvaSub   = 0;
      nVentaNue = 0;

      nModp80 = 300; |BUCLE dsmociva;
      nModp80 = 320; |BUCLE dsmociva;
      nModp80 = 330; |BUCLE dsmociva;
      nModp80 = 332; |BUCLE dsmociva;
      nModp80 = 370; |BUCLE dsmociva;

      |SI nReg99 = 1;
          |ABRE #983;
          |DDEFECTO #983;
          #983#0 = #0#0;
          #983#1 = enEmpresa;
          #983#2 = 99;
          |LEE 101#983=;
          |SI FSalida ! 0;
              |DDEFECTO #983;
              #983#0  = #0#0;
              #983#1  = enEmpresa;
              #983#2  = 99;
              #983#3  = "REGISTRO DE REGULARIZACION";
              #983#4  = "";
              #983#5  = #982#18;
              #983#9  = #982#18;
              #983#10 = "X";
              #983#11 = "REGULARIZACION";
              |GRABA 020#983b;
          |FINSI;
          #983#6  = #983#6  + nVentaCon;
          #983#7  = #983#7  + nVentaSin;
          #983#8  = #983#6  + #983#7;
          #983#15 = #983#15 + nVentaSub;
          #983#28 = #983#28 + nIvaSub;
          #983#29 = #983#29 + nVentaSuI;
          #983#32 = #983#32 + nVentaNue;
          |GRABA 020#983a;
          |CIERRA #983;

          #982#5  = #982#5  + nVentaCon;
          #982#6  = #982#6  + nVentaSin;
          #982#7  = #982#5  + #982#6;
          #982#9  = #982#9  + nVentaSub;
          #982#13 = #982#13 + nIvaSub;
          #982#16 = #982#16 + nVentaSuI;
          #982#22 = #982#22 + nVentaNue;
     |FINSI;
|FPRC;
|| ================================ Proceso Principal
|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO LeeElAux83;

  enActividad = #983#2;

  |SI nSwOpe83 = 0;   || = Calcular las Cuotas por Actividad

      |SI enActividad = 00; |O enActividad = 99; |ACABA; |FINSI;

      nVentaSub = 0;
      nVentaCon = 0;
      nVentaSin = 0;
      nVentaSuI = 0;
      nIvaSub   = 0;
      nVentaNue = 0;

      aAlfa1 = #983#30; |QBF aAlfa1;
      |SI aAlfa1 = "REGISTROS";
          nIvaComun = 1;  || Tiene Contabilidad
      |FINSI;

      nAny = #0#0 - 1;
      |SI enEjer < enEjerPro;
          |BUCLE LeeElModelo;
      |SINO;
          |ABRE #81;
          #81#0 = enEmpresa;
          #81#1 = nAny;
          #81#2 = enActividad;
          |LEE 001#81=;
          |SI FSalida = 0;
              nVentaCon = #81#7;
              nVentaSin = #81#8;
              nVentaSub = #81#10;
              nIvaSub   = #81#12;
              nVentaSuI = #81#15;
              nVentaNue = #81#41;
          |FINSI;
          |CIERRA #81;
      |FINSI;

      #983#6  = #983#6  + nVentaCon;
      #983#7  = #983#7  + nVentaSin;
      #983#8  = #983#6  + #983#7;
      #983#15 = #983#15 + nVentaSub;
      #983#28 = #983#28 + nIvaSub;
      #983#29 = #983#29 + nVentaSuI;
      #983#32 = #983#32 + nVentaNue;
      |GRABA 020#983a;

      #982#5  = #982#5  + nVentaCon;
      #982#6  = #982#6  + nVentaSin;
      #982#7  = #982#5  + #982#6;
      #982#9  = #982#9  + nVentaSub;
      #982#13 = #982#13 + nIvaSub;
      #982#16 = #982#16 + nVentaSuI;
      #982#22 = #982#22 + nVentaNue;

      nNumAct = nNumAct + 1;
  |FINSI;

  |SI nSwOpe83 = 1;   || = determina actividad Principal y otros detalles

      |SI enActividad = 00; |O enActividad = 99; |ACABA; |FINSI;
      |SI nSw = 0;
          nImporteMas = #983#8;
          nActivMas   = #983#2;
          nRegIVA     = #983#14;
          nSw = 1;
      |FINSI;
      |SI nImporteMas < #983#8;  || la actividad principal
          nImporteMas = #983#8;
          nActivMas   = #983#2;
      |FINSI;
      |SI nRegIVA ! #983#14;
          nSwVarios = 1;
          nRegIVA   = #983#14;
      |FINSI;

      |SI #983#14 = 3; |O #983#14 = 9; nSwRegEsp = 1; |FINSI;

      |SI #983#7 ! 0; |O #983#15 ! 0; nSwExentas = 1; |FINSI;

      |SI #983#28 ! 0; nSwSubv = 1; |FINSI;

      nNumAct = nNumAct + 1;        || todas las actividades

 |FINSI;

 |SI nSwOpe83 = 2;   || Busca todas las complementarias de la Principal

     |SI enActividad = 00; |O enActividad = 99; |ACABA; |FINSI;
     |SI #983#2 = nDepar; |ACABA; |FINSI;

     |SI #983#13 = aCNAE;
         #983#10 = aTipo;
         #983#11 = aLit;
         #983#12 = nDepar;
     |SINO;
         #983#10 = "O";
         #983#11 = "OTRA";
     |FINSI;
     |GRABA 020#983a;

 |FINSI;

 |SI nSwOpe83 = 3;   || mira acumulados
     |SI #983#9 ! 100;
         nSwBorro = 0;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE LeeElAux83;
 #983#1;
 ;
 #0#0, enEmpresa, 00;
 #0#0, enEmpresa, 99;
 ;
 NULL, LeeElAux83;
|FIN;

|PROCESO ActividadPrincipal;
 |ABRE #983;
 #983#0 = #0#0;
 #983#1 = enEmpresa;
 #983#2 = nDepar;
 |LEE 001#983=;
 |SI FSalida = 0;
     #983#10 = aTipo;
     #983#11 = aLit;
     #983#12 = nDepar;
     |GRABA 020#983a;
 |FINSI;
 |CIERRA #983;

 nSwOpe83 = 2;  ||Principal complementaria
 aCNAE   = #983#13;
 aLit    = "COMPLEMENTARIA";
 aTipo   = "C";
 |BUCLE LeeElAux83;
|FPRC;

|PROCESO GraboDatos;
    enEmpresa = #982#1;

    |SI nSwOpe82 = 0;   || Calculo de Los Importes de Ventas por actividad
        #982#20   = -1;  || sin contabilidad
        nNumAct   = 0;
        nIvaComun = 0;
        nSwOpe83  = 0;
        |BUCLE LeeElAux83;

        |HAZ ActivRegul99;

        |GRABA 020#982a;
        |LIBERA #982;

        |SI nIvaComun = 1; |Y nNumAct > 1;
            |HAZ GraboIvaComun;
        |FINSI;

        |ACABA;
    |FINSI;

    |SI nSwOpe82 = 1;   ||... Determinacion de la Prorrata

       || ... ... ... ...  Determinar la Actividad Principal, Si existe activ.
       ||                  en R.E.A.G Y P o R.E., y si ha cobrado subvenciones

        nSw         = 0;
        nSwRegEsp   = 0;  || Algun regimen especial
        nSwSubv     = 0;  || tiene subvenciones a bien concreto
        nSwExentas  = 0;  || tiene operaciones exentas
        nSwVarios   = 0;  || varios Regimenes de IVA
        nNumAct     = 0;  || Numero de Actividades
        nSwOpe83    = 1;
        nImporteMas = 0;
        nActivMas   = 0;  || actividad principal
        |BUCLE LeeElAux83;

        nDepar     = nActivMas;
        aTipo      = "P";
        aLit       = "PRINCIPAL";
        |HAZ ActividadPrincipal;

        || ...................... Hasta Aqui tiene clasificadas las activ.
        aTipoPr = #982#3;
        #982#17 = nNumAct;
        |HAZ CalNueProrr;
        #982#19 = "";
        #982#23 = " ESPECIAL";
        |SI #982#3 = "G";
            aAlfa1  = #982#4; |QBF aAlfa1;
            aAlfa1  = ("   " + aAlfa1) % -103;
            #982#19 = aAlfa1;
            #982#23 = " GENERAL ";
        |FINSI;
        |GRABA 020#982a;

    |FINSI;

|FPRC;

|DEFBUCLE dsmow082;
 #982#1;
 ;
 #0#0, 00001;
 #0#0, 99999;
 ;
 NULL, GraboDatos;
|FIN;

|PROCESO GraboIvaComun;
    |ABRE #983;
    |DDEFECTO #983;
    #983#0 = #0#0;
    #983#1 = enEmpresa;
    #983#2 = 00;
    |LEE 001#983=;
    |SI FSalida ! 0;
       |DDEFECTO #983;
       #983#0  = #0#0;
       #983#1  = enEmpresa;
       #983#2  = 00;
       #983#3  = "IVA DEDUCIBLE OPERACIONES COMUNES ";
       #983#4  = "";
       #983#5  = #982#18;
       #983#6  = #982#5;
       #983#7  = #982#6;
       #983#8  = #982#7;
       #983#9  = #982#18;
       #983#10 = "X";
       #983#11 = "COMUN";
       #983#15 = #982#9;
       #983#28 = #982#13;
       #983#29 = #982#16;
       #983#32 = #982#22;
       |GRABA 020#983c;
    |FINSI;
    |CIERRA #983;

|FPRC;
|| ========================================================================
|| ============= Primera Parte, ver que empresas tiene que aplicar Prorrata
|| ========================================================================
|| .... Busco en el ultimo registro del 036, si solicita la prorrata especial
|PROCESO Busco1022;
 aAlfa1 = #1022#142;
|FPRC;

|DEFBUCLE Busco1022;
 #1022#1;
 ;
 enEmpresa, 01;
 enEmpresa, 99;
 ;
 NULL, Busco1022;
|FIN;

|| ................. Grabo registro por Empresa
|PROCESO GraboAx82;
    |DDEFECTO #982;
    #982#0  = #0#0;
    #982#1  = enEmpresa;
    #982#2  = #1000#1;
    #982#3  = aProrr;
    #982#4  = nProrr;
    #982#8  = "S";
    #982#14 = "N";
    #982#15 = #1000#13;
    #982#17 = nNumAct;
    #982#18  = nProrr;
    |GRABA 000#982n;
    enGraba = 1;
|FPRC;

|| .................. Grabo Registro por Empresa-Actividad
|PROCESO GraboAux83;
    nProrr1 = nProrr;
    |SI aProrr = "E";
        |ABRE #81;
        #81#0 = enEmpresa;
        #81#1 = #0#0;
        #81#2 = nDepar;
        |LEE 001#81=;
        |SI FSalida = 0;
            nProrr = #81#5;
        |SINO;
            #81#0 = enEmpresa;
            #81#1 = #0#0 - 1;
            #81#2 = nDepar;
            |LEE 001#81=;
            |SI FSalida = 0;
                nProrr = #81#6;
            |FINSI;
         |FINSI;
         |CIERRA #81;
    |FINSI;
    |DDEFECTO #983;
    #983#0 = #0#0;
    #983#1 = enEmpresa;
    #983#2 = nDepar;
    |LEE 001#983=;
    |SI FSalida ! 0;
       |DDEFECTO #983;
       #983#0  = #0#0;
       #983#1  = enEmpresa;
       #983#2  = nDepar;
       #983#3  = #1014#4;
       #983#4  = #1014#20;
       #983#5  = nProrr;
       #983#9  = nProrr;
       #983#12 = nDepar;
       #983#13 = #1014#20;
       #983#14 = #1014#7;
       #983#16 = "Z";
       #983#19 = #1000#87;
       #983#20 = #1000#88;
       #983#21 = #1014#9;
       #983#22 = #1014#10;
       #983#23 = #1014#16;
       #983#24 = #1014#17;
       |SI #1014#5 > "01.01.0000"; #983#25 = #1014#5; |FINSI;
       |SI #1014#6 > "01.01.0000"; #983#26 = #1014#6; |FINSI;
       #983#27 = #1014#8;
       #983#30 = #1014#15;
       #983#31 = #1014#3;
       |GRABA 020#983c;
    |FINSI;
    nProrr = nProrr1;
|FPRC;

|| *************************************************************
|PROCESO PrimerFiltro;  || Mas de una Actividad
 |SI #1014#5 > "01.01.0000";
     |SI #1014#5 > fFecFin;  |ACABA;  |FINSI;
 |FINSI;
 |SI #1014#6 > "01.01.0000";
     |SI #1014#6 < fFecIni;  |ACABA; |FINSI; || Baja
 |FINSI;
 |SI #1014#14 = "S"; |ACABA; |FINSI;  || dia 08.06.2005 comunero
 nDepar = #1014#1;
 |HAZ GraboAux83;
 nNumAct = nNumAct + 1;
|FPRC;

|DEFBUCLE LeeActividades;
 #1014#1;
 ;
 enEmpresa, 01;
 enEmpresa, 99;
 ;
 NULL, PrimerFiltro;
|FIN;

|| .... Mirar si hace el modelo de IVA
|PROCESO MiraSiD;
 |SI #2#8 > "01.01.0000";
     |SI #2#8 > fFecFin;  |ACABA;  |FINSI;
 |FINSI;
 |SI #2#9 > "01.01.0000";
     |SI #2#9 < fFecIni;  |ACABA;  |FINSI;
 |FINSI;
 |SI #2#2 = 309; |ACABA; |FINSI;
 |SI #2#2 < 345; |O #2#2 > 350;
     nSwConPro = 0;
     |ERROR10;
 |FINSI;
|FPRC;

|DEFBUCLE MiraSiDIVA;
 #2#1;
 ;
 enEmpresa, 01, 300;
 enEmpresa, 99, 371;
 ;
 NULL, MiraSiD;
|FIN;

|| ..........................................

|PROCESO MirarFiltros;

     enEmpresa = #1000#0;

     nSwConPro = -1;
     |BUCLE MiraSiDIVA;  || Comprobar que hace algun modelo 300
     |SI nSwConPro = -1; |ACABA; |FINSI;

     nProrr = 100;
     aProrr = "G";
     |ABRE #27;
     #27#0 = enEmpresa;
     #27#1 = #0#0 - 1;
     |LEE 001#27=;
     |SI FSalida = 0;
         nProrr = #27#3;
     |FINSI;
     |CIERRA #27;

     |ABRE #80;
     #80#0 = enEmpresa;
     #80#1 = #0#0;
     |LEE 001#80=;
     |SI FSalida = 0;
         aProrr = #80#2;
         nProrr = #80#31;
     |SINO;
         #80#0 = enEmpresa;
         #80#1 = #0#0 - 1;
         |LEE 001#80=;
         |SI FSalida = 0;
             aProrr = #80#4;
             nProrr = #80#32;
         |FINSI;
     |FINSI;
     |CIERRA #80;

     nNumAct = 0;
     |ABRE #983;
     |BUCLE LeeActividades;   || Graba Actividades
     |CIERRA #983;

     |HAZ GraboAx82;
|FPRC;

|DEFBUCLE agifigen;
 #1000#1;
 ;
 nDEmpresa;
 nHEmpresa;
 ;
 NULL, MirarFiltros;
|FIN;

|| ========================================================================
|PROCESO Sup180;
 #982#0 = #0#0;
 #982#1 = 0;
|FPRC;

|PROCESO Inf180;
 #982#0 = #0#0;
 #982#1 = 99999;
|FPRC;

|PROCESO Tipo3;  |TIPO 3;

    aAlfa = #0#0;
    aAlfa = aAlfa % -102;
    nEjer = aAlfa;
    enEjer = #0#0;

    |HAZ LeeCtrlProrr;
    nAny   = #0#0 - 1;
    aAlfa1 = nAny;
    aAlfa1 = "01.01." + aAlfa1; fFecIni = aAlfa1;
    aAlfa1 = #0#0;
    aAlfa1 = "31.12." + aAlfa1; fFecFin = aAlfa1;

    |ABRE #982;
    |SI #0#1 ! 0;
        nDEmpresa = #0#1;
        nHEmpresa = #0#2;
        |BUCLE agifigen;
    |SINO;
        |PARA nCampo = 3;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
              nDEmpresa = #0nCampo;
              nHEmpresa = #0nCampo;
              |BUCLE agifigen;
        |SIGUE;
    |FINSI;
    |CIERRA #982;

    |SI enGraba = 1;

         nSwOpe82 = 0;
         |BUCLE dsmow082;  ||totales

         nSwOpe82 = 1;
         |BUCLE dsmow082;  ||Por actividades

         |SI enSinPantalla = 0;
             |PINPA #0#982;
             |ATRI R; |PINTA 0621, "Provisional"; |ATRI 0;
             |ATRI R; |PINTA 0955, "Datos Ejercicio Anterior "; |ATRI 0;
             |ENTLINEAL #1#982, -2, 4, 22, 0, Sup180, Inf180;
             |SI FSalida = 0;
                 |CONFI "    SCALCULOS CORRETOS S/N";
                 |SI FSalida = 0;
                     |HAZ PasaDatosBuenos;
                 |FINSI;
             |FINSI;
         |SINO;
              |HAZ PasaDatosBuenos;
         |FINSI;
     |SINO;
         |SI enSinPantalla = 0;
             |MENAV "     Seleccion Vacia.";
         |FINSI;
     |FINSI;

|FPRC;


|PROGRAMA;

     |ABRE #107;
     |DDEFECTO #107;
     |LEE 040#107p;
     |CIERRA #107;
     |SI #107#9 = "N";
         |ACABA;
         || .. acaba cuando no hay prorrata
     |FINSI;

     |DBASS aBase;  |QBF aBase;
     aAlfa = aBase + "basica/fich/";
     |PATH_DAT #1022 aAlfa;

     aFichw82 = ("82w" + Usuario) % 108;
     |NOME_DAT #982 aFichw82;
     aFichero = ("83w" + Usuario) % 108;
     |NOME_DAT #983 aFichero;

     |DELINDEX #982;
     |DELINDEX #983;

     |SI enEmpresa ! 0;
         |DDEFECTO #0;
         #0#0 = enEjer;
         #0#1 = enEmpresa;
         #0#2 = enEmpresa;
         |HAZ Tipo3;
    |SINO;
         |MANTE #0;
    |FINSI;

    |DELINDEX #982;
    |DELINDEX #983;

|FPRO;

|| .... .... ... .. .. ACTUALIZA FICHERO CORRECTO
|PROCESO Leem81;
 |SI nSwOpm81 = 0;
     |PDEFECTO #81, 6, 15;
     |GRABA 020#81a;
 |FINSI;
 |LIBERA #81;
|FPRC;

|DEFBUCLE Leem81;
 #81#1;
 ;
 #80#0, #80#1, 01;
 #80#0, #80#1, 99;
 be;
 NULL, Leem81;
|FIN;

|PROCESO ActActividad;
 #81#0 = #983#1;
 #81#1 = nAny;
 #81#2 = #983#2;
 |LEE 101#81=;
 |SI FSalida ! 0;
     |DDEFECTO #81;
     #81#0 = #983#1;
     #81#1 = nAny;
     #81#2 = #983#2;
     #81#3 = #983#3;
     #81#4 = #983#4;
     |GRABA 020#81b;
 |FINSI;

 |SI nSwOpm81 = 1;
     #81#5  = #983#9;
 |FINSI;

 #81#40 = #983#10;
 |GRABA 020#81a;
 |LIBERA #81;

|FPRC;

|DEFBUCLE ActActividad;
 #983#1;
 ;
 #982#0, #982#1, 00;
 #982#0, #982#1, 99;
 ;
 NULL, ActActividad;
|FIN;

|PROCESO Leew82_Aux82;

 nAny = #982#0;
 #80#0 = #982#1;  || cliente
 #80#1 = nAny;  || a¤o
 |LEE 101#80=;
 |SI FSalida ! 0;
     |DDEFECTO #80;
     #80#0 = #982#1;
     #80#1 = nAny;
     |GRABA 020#80b;
 |FINSI;

 #80#2 = #982#3;
 #80#3 = #982#4;
 #80#14 = #982#8;
 #80#31 = #982#18; ||para comun
 |GRABA 020#80a;
 |LIBERA #80;

 nSwOpm81 = 1;
 |ABRE #81;
 |BUCLE ActActividad;
 |CIERRA #81;
|FPRC;

|DEFBUCLE Leew82_Aux82;
 #982#1;
 ;
 #0#0, 00001;
 #0#0, 99999;
 ;
 NULL, Leew82_Aux82;
|FIN;

|PROCESO PasaDatosBuenos;
  |ABRE #80;
  |BUCLE Leew82_Aux82;
  |CIERRA #80;
|FPRC;
