|FICHEROS;
   dsmoy036 #0;

   :/basica/def/agim0002 #102;
   :/basica/def/agitab12 #112;
   :/basica/def/agicen14 #114;
   :/basica/def/agifigen #118;
   :/basica/def/agim0019 #119;

   dsmow030 #900;

   dsmom009 #09;  ||Ingresos Planillas
   dsmom010 #10;  ||Ingresos Planillas
   dsmom012 #12;  ||Consumo
   dsmom013 #13;  ||Gastos

   :/contagen/def/canempre #300;
   :/contagen/def/cacuenta #304;
   :/contagen/def/cacuedep #305;

   :/contasoc/def/xxx00001 #203;     || Impuesto Sociedades 1990
   :/contasoc/def/xxx00011 #204;     || Impuesto Sociedades 1991
   :/contasoc/def/isocie01 #205;     || Impuesto Sociedades 1992
   :/contasoc/def/isocie32 #206;     || Impuesto Sociedades 1993
   :/contasoc/def/isocie44 #207;     || Impuesto Sociedades 1994 y 1995
   :/contasoc/def/isocie57 #209;     || Impuesto Sociedades 1996
   :/contasoc/def/isocie65 #219;     || Impuesto Sociedades 1997
|FIN;

|VARIABLES;
   nInkey      = "";
   informe     = "";
   nCampo      = 0;
   nDEmpresa   = 0;
   nHEmpresa   = 0;
   fLaFecha    = @;

   aAlfa1 = "";
   aAlfa2 = "";
   aAlfa3 = "";
   nPara1 = 0;
   nPara2 = 0;
   nNume1 = 0;
   nNume2 = 0;
   nNume3 = 0;
   nNume4 = 0;

   nSPin1 = 0;
   &nSwCol1  = 0;
   &nSwCol2  = 0;
   &nSwCol3  = 0;
   &aTex5    = "";
   &aTex6    = "";
   &aTex7    = "";
   &aTex8    = "";
   &aTex9    = "";
   &nSwLinea = 0;
   &nSwPrint = 0;
   nSwHay    = 0;
   nPrint    = 0;
   aPath     = "";

   &eaPathContasoc  = "";
   aAlfa      = "";
   aBase      = "";
   nCanal     = 0;

   SwSocie    = 0;
   nSociedad  = 0;
   nSwOpera   = 0;
   nPorCtas   = 0;
   nSwRendi   = 0;
   nSwConta   = 0;
   nSwPlani   = 0;
   nNumAct    = 0;
   nTotAct    = 0;

   nEmpresa   = 0;
   nEjercicio = 0;
   nActividad = 0;
   aActividad = "";
   aDCta12    = "6000000000000";
   aHCta12    = "7099999999999";
   aDCta4C    = "6000";
   aHCta4C    = "6099";
   aDCta4V    = "7000";
   aHCta4V    = "7099";
   aCuenta    = "";
   nSaldo     = 0;

   &eSwSelec  = 0;
   &eaNomFDep = "";  ||Nombre del Fichero Creado
   &dirfich0  = "";
   &eaDDepar  = "";
   &eaHDepar  = "";

   nMiro      = 0;
   nImporte   = 0;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;
|INCLUYE i_varlib;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\
|CALCULO Eano; |TIPO 7;
 fLaFecha = ~;
 aAlfa1 = fLaFecha; aAlfa1 = aAlfa1 % -104;
|FCAL;

|CALCULO AntesOrdenar; |TIPO 7;
 |SI #0#2 = 0;
     |C_M #0Campo, 1, "N";
     #0Campo = 1; |PINTA #0Campo;
 |SINO;
     |C_M #0Campo, 1, "S";
 |FINSI;
|FCAL;

|CALCULO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#2  = enEmpresa;  |PINTA #0#2;
         #0#3  = enEmpresa;  |PINTA #0#3;

         |C_M #0#2, 1, "N";
         |C_M #0#3, 1, "N";
     |FINSI;
|FCAL;

|CALCULO Parrilla; |TIPO 0;
     |SI #0#2 = 0;
         #0#2 = 0;  |PINTA #0#2;
         #0#3 = 0;  |PINTA #0#3;  |C_M #0#3, 1, "N";
         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#2, 1, "S";
             |C_M #0#3, 1, "S";
         |FINSI;

         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas; |TIPO 0;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO YaSeCual; |TIPO 7;
 |SI enEmpresa ! 0;
     #0#41 = "S"; |PINTA #0#41;
     #0#24 = "S"; |PINTA #0#24;
     #0#25 = 00;  |PINTA #0#25;
     #0#26 = 99;  |PINTA #0#26;
     |C_M #0#24, 1, "N";
     |C_M #0#25, 1, "N";
     |C_M #0#26, 1, "N";
     |C_M #0#41, 1, "N";
 |FINSI;
|FCAL;

|CALCULO SelTipo0; |TIPO 0;
 |SI #0#41 = "N";
     #0#25 = 0;  |PINTA #0#25;  |C_M #0#25, 1, "N";
     #0#26 = 99; |PINTA #0#26;  |C_M #0#26, 1, "N";
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |SINO;
     |SI enEmpresa = 0;
         |C_M #0#25, 1, "S";
         |C_M #0#26, 1, "S";
     |FINSI;
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "N";
           #0nCampo = 0;  |PINTA #0nCampo;
     |SIGUE;
 |FINSI;
|FCAL;

|CALCULO SelTipo1;
 |SI #0Campo = 0;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           #0nCampo = 0;  |C_M #0nCampo, 1, "N";
     |SIGUE;
 |SINO;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |FINSI;
|FCAL;

|PROCESO ClaveAlta;
 #112#0 = 01;
|FPRC;

|PROCESO ClaveBaja;
 #112#0 = 99;
|FPRC;

|CALCULO MiraTributacion;
     nInkey = FSalida;
     |SI nInkey = 10;
         |ABRE #112;
         #112#0 = #0Campo;
         |CONSULTA_F_CLAVE #1#112, ClaveAlta, ClaveBaja;
         #0Campo = #112#0; |PINTA #0Campo;
         |CIERRA #112;
     |FINSI;

     nNume1 = 79;
     |SI Campo < 57; nNume1 = 56; |FINSI;
     |SI Campo < 51; nNume1 = 50; |FINSI;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ nNume1;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N"; |PINTA #0nCampo;
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ nNume1;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
     |SI #0Campo ! 0;
         |ABRE #112;
         #112#0 = #0Campo;
         |LEE 001#112=;
         |SI FSalida ! 0;
             |MENAV "    No existe Tipo Tributacion";
             |ERROR;
         |FINSI;
         |CIERRA #112;
     |FINSI;
|FCAL;

|| *************************************************************************
|| *************************************************************************

|PROCESO PreparaConta;

   nSwConta  = 0; ||compruebo si tiene contabilidad

   |ABRE #canempre;
   |SELECT #4#canempre;
   |DDEFECTO #canempre;
   #canempre#2  = enEmpresa;
   #canempre#26 = nEjercicio;
   |LEE 001#canempre.=;
   |SI FSalida = 0;
       enPerConta = #canempre#3;   || Periodo Contable
       aAlfa1 = enEmpresa;  aAlfa1 = ("00000" + aAlfa1) % -105;
       aAlfa2 = enPerConta; aAlfa2 = ("0" + aAlfa2) % -101;
       aPathConta  = aPath + "contagen/fich/" + aAlfa1 + aAlfa2 + "/";
       nSwConta = 1;
   |FINSI;
   |SELECT #1#canempre;
   |CIERRA #canempre;

   |SI nSwConta = 0; |ACABA; |FINSI;    || no tiene contabilidad

   |PATH_DAT #304 aPathConta;

   nPorCtas = 1;
   |SI #0#39 = "S";
       nPorCtas = 0;
       |SI nNumAct = 1; nPorCtas = 1; |FINSI;  ||solo una actividad
   |SINO;
       |SI nNumAct ! 1;   || si hay mas de una actividad
           nMiro = 0;
           |SI #0#68 ! 0; nMiro = nMiro + 1; |FINSI;
           |SI #0#69 ! 0; nMiro = nMiro + 1; |FINSI;
           |SI #0#83 ! 0; nMiro = nMiro + 1; |FINSI;
           |SI nMiro > 1;
               nPorCtas = 0;  ||Hay mas de un regimen
           |SINO;
               |SI nNumAct ! nTotAct; nPorCtas = 0; |FINSI;
           |FINSI;
       |FINSI;
   |FINSI;

   |SI #0#68 = 0; |Y #0#69 = 0; |Y #0#83 ! 0;
       |SI #0#85 = "S"; nPorCtas = 1; |FINSI;   ||sociedades por el impuesto
   |FINSI;

   |SI nPorCtas = 0;             || ver por actividades
       dirfich0 = aPathConta;
       eSwSelec  = 2;
       |ATRI I;
       |PINTA 2302, "Procesando Cuentas por Departamentos ... Espere ";
       |ATRI 0;
       |SUB_EJECUTA_NP ":contagen/cacuedep";
       |PINTA 2302, "                                                ";
       |NOME_DAT #305 eaNomFDep;
       |PATH_DAT #305 aPathConta;
   |FINSI;
|FPRC;

|| *************************************************************************
|PROCESO SumoCtas;
  |SI aCuenta ] aDCta4C; |Y aCuenta [ aHCta4C;
      #102#15 = #102#15 + nSaldo;
  |FINSI;
  |SI aCuenta ] aDCta4V; |Y aCuenta [ aHCta4V;
      #102#13 = #102#13 + nSaldo;
  |FINSI;
|FPRC;

|| ... Todas
|PROCESO PorPlanCtble;
 nSwHay = 1;
 aCuenta = #304#0 % 104;
 nSaldo  = #304#53 - #304#50;
 aAlfa1  = aCuenta % 101;
 |SI aAlfa1 = "7";  nSaldo = -nSaldo; |FINSI;
 |HAZ SumoCtas;
|FPRC;

|DEFBUCLE PorPlanCtble;
 #304#1;
 3;
 aDCta12, "S";
 aHCta12, "S";
 ;
 NULL, PorPlanCtble;
|FIN;

|PROCESO PorPlanCtble0;  || solo una actividad

 #102#13 = 0;
 #102#15 = 0;
 #102#16 = 0;
 |BUCLE PorPlanCtble;

 |SI nSwHay = 1;
     |SI #0#68 ! 0;
         #0#57 = #0#57 + #102#13;
         #0#58 = #0#57 - #0#42;
         |SI #0#58 < 0; #0#58 = 0; |FINSI;

         #0#59 = #0#59 + #102#15 + #102#16;    || en la misma
                                               || #0#60 = #0#60 + #102#16;
         #0#61 = #0#59 - #0#43;
         |SI #0#61 < 0; #0#61 = 0; |FINSI;

     |SINO;
         |SI #0#69 ! 0;
             #0#62 = #0#62 + #102#13;
             #0#63 = #0#62 - #0#44;
             |SI #0#63 < 0; #0#63 = 0; |FINSI;
         |SINO;
             |SI #0#83 ! 0;
                 #0#80 = #0#80 + #102#13;
                 #0#81 = #0#80 - #0#73;
                 |SI #0#81 < 0; #0#81 = 0; |FINSI;
             |FINSI;
         |FINSI;
     |FINSI;

     |SI #0#39 = "S"; |HAZ ImprDatos; |FINSI;
  |FINSI;
|FPRC;


|| ........ Por actividades
|PROCESO LeeCuentas;
 aCuenta = #305#0 % 104;
 nSaldo  = #305#53 - #305#50;
 aAlfa1 = aCuenta % 101;
 |SI aAlfa1 = "7";  nSaldo = -nSaldo; |FINSI;
 |HAZ SumoCtas;
|FPRC;

|DEFBUCLE LeeCuentas;
 #305#2;
 3;
 aActividad, aDCta12, "S";
 aActividad, aHCta12, "S";
 ;
 NULL, LeeCuentas;
|FIN;

|| *************************************************************************
|PROCESO SumaSP13;
 nSwPlani = 1;
 aCuenta = #13#5;
 nSaldo  = #13#8;
 |HAZ SumoCtas;
|FPRC;

|DEFBUCLE dsmom013;
 #13#1;
 ;
 enEmpresa, #0#1, 01, 00, enActividad, aDCta4C;
 enEmpresa, #0#1, 99, 99, enActividad, aHCta4C;
 e;
 NULL, SumaSP13;
|FIN;

|PROCESO SumaSP12;
 nSwPlani = 1;
 aCuenta = #12#5;
 nSaldo  = #12#8;
 |HAZ SumoCtas;
|FPRC;

|DEFBUCLE dsmom012;
 #12#1;
 ;
 enEmpresa, #0#1, 01, 00, enActividad, aDCta4C;
 enEmpresa, #0#1, 99, 99, enActividad, aHCta4C;
 e;
 NULL, SumaSP12;
|FIN;

|PROCESO SumaSP10;
 nSwPlani = 1;
 aCuenta = #10#5;
 nSaldo  = #10#8;
 |HAZ SumoCtas;
|FPRC;

|DEFBUCLE dsmom010;
 #10#1;
 ;
 enEmpresa, #0#1, 01, 00, nActividad, aDCta4V;
 enEmpresa, #0#1, 99, 99, nActividad, aHCta4V;
 e;
 NULL, SumaSP10;
|FIN;

|PROCESO Planilla;
 aAlfa1 = #agicen14#15; |QBF aAlfa1;
 |SI aAlfa1 = "MANUAL"; aAlfa1 = "PLANILLAS"; |FINSI;
 |SI aAlfa1 ! "PLANILLAS";  |ACABA; |FINSI;

 |BUCLE dsmom010;
 |BUCLE dsmom012;
 |BUCLE dsmom013;

|FPRC;
|| *************************************************************************
|| .... Leo el Impuesto de Sociedades

|PROCESO LeoImpuesto;
  || Lee Ficha Sociedades, lee la ficha
     swExiste = 0;
     nRegis   = 0;
     |ABRE #agim0019;
     #agim0019#0 = enEmpresa;
     #agim0019#1 = 99;
     |LEE 041#agim0019.];
     |SI FSalida = 0;
         |SI #agim0019#0 ! enEmpresa;
             |LEE 041#agim0019.a;
             |SI FSalida = 0; |Y #agim0019#0 = enEmpresa;
                 swExiste = 1;
             |FINSI;
         |SINO;
             swExiste = 1;
         |FINSI;
     |FINSI;
     |SI swExiste = 1;
         nRegis = #agim0019#1;  ||Registre
         |SI #agim0019#12 > "01.01.1000";
             aAlfa1 = #agim0019#12; aAlfa1 = aAlfa1 % -104; enEjerBajaM = aAlfa1;
             |SI enEjerBajaM [ #0#1; nRegis = 0; |FINSI;
         |FINSI;
     |FINSI;
     |CIERRA #agim0019;

     |SI nRegis ! 0; |Y eaPathContasoc ! "";
         nImporte = 0;
         |SI #0#1 [ 1996;
             |ABRE #209;
             #209#0  = enEmpresa;
             #209#1  = #0#1;
             |LEE 040#209=;
             |SI FSalida = 0;
                 nImporte = #209#180;
                 SwSocie  = 1;
             |FINSI;
             |CIERRA #209;
         |SINO;
             |ABRE #219;
             |DDEFECTO #219;
             #219#0  = enEmpresa;
             #219#1  = #0#1;
             |LEE 040#219=;
             |SI FSalida = 0;
                 nImporte = #219#166;
                 SwSocie  = 1;
             |FINSI;
             |CIERRA #219;
         |FINSI;
     |FINSI;

     |SI SwSocie = 1;
         #0#80 = #0#80 + nImporte;
         #0#81 = #0#80 - #0#73;
         |SI #0#81 < 0; #0#81 = 0; |FINSI;
         nSwHay = 1;

         |SI #0#39 = "S";
             |SI #0#81 > 0; |O #0#84 = "S";
                 nSwPrint = 3; |PRINT; nPrint = 1; nSwLinea = 1;
             |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|| *************************************************************************

|PROCESO PorActividad;

 |SI #114#5 > "01.01.0000";
     |SI #114#5 > efHFecha;  |ACABA;  |FINSI;
 |FINSI;
 |SI #114#6 > "01.01.0000";
     |SI #114#6 < efDFecha;  |ACABA; |FINSI; || Baja
 |FINSI;

  nActividad = #114#1;
  aActividad = nActividad;
  aActividad = ("00" + aActividad) % -102;

  nSwPrint = 0;
  |SI #0#45 ! 0; |Y #114#9 = #0#45; nSwPrint = 1; |FINSI;
  |SI #0#46 ! 0; |Y #114#9 = #0#46; nSwPrint = 1; |FINSI;
  |SI #0#47 ! 0; |Y #114#9 = #0#47; nSwPrint = 1; |FINSI;
  |SI #0#48 ! 0; |Y #114#9 = #0#48; nSwPrint = 1; |FINSI;
  |SI #0#49 ! 0; |Y #114#9 = #0#49; nSwPrint = 1; |FINSI;
  |SI #0#50 ! 0; |Y #114#9 = #0#50; nSwPrint = 1; |FINSI;

  |SI #0#51 ! 0; |Y #114#9 = #0#51; nSwPrint = 2; |FINSI;
  |SI #0#52 ! 0; |Y #114#9 = #0#52; nSwPrint = 2; |FINSI;
  |SI #0#53 ! 0; |Y #114#9 = #0#53; nSwPrint = 2; |FINSI;
  |SI #0#54 ! 0; |Y #114#9 = #0#54; nSwPrint = 2; |FINSI;
  |SI #0#55 ! 0; |Y #114#9 = #0#55; nSwPrint = 2; |FINSI;
  |SI #0#56 ! 0; |Y #114#9 = #0#56; nSwPrint = 2; |FINSI;

  |SI #0#74 ! 0; |Y #114#9 = #0#74; nSwPrint = 3; |FINSI;
  |SI #0#75 ! 0; |Y #114#9 = #0#75; nSwPrint = 3; |FINSI;
  |SI #0#76 ! 0; |Y #114#9 = #0#76; nSwPrint = 3; |FINSI;
  |SI #0#77 ! 0; |Y #114#9 = #0#77; nSwPrint = 3; |FINSI;
  |SI #0#78 ! 0; |Y #114#9 = #0#78; nSwPrint = 3; |FINSI;
  |SI #0#79 ! 0; |Y #114#9 = #0#79; nSwPrint = 3; |FINSI;

  nNumAct = nNumAct + 1;
  |SI nSwPrint = 0; |ACABA; |FINSI;  ||Esta actividad no esta en la seleccion

  nTotAct = nTotAct + 1;
  |SI nSwOpera = 0;
      |SI #0#66 = 0;
          #0#66 = #114#1;
          #0#67 = #114#4;
      |FINSI;
      |SI nSwPrint = 1;
          #0#64 = #114#10;
          #0#68 = #114#9;
      |FINSI;
      |SI nSwPrint = 2;
          #0#65 = #114#10;
          #0#69 = #114#9;
      |FINSI;
      |SI nSwPrint = 3;
          #0#82 = #114#10;
          #0#83 = #114#9;
          nSociedad = nSociedad + 1;
      |FINSI;
      nSwHay = 1;
      |ACABA;
  |FINSI;

  #102#13 = 0;   || Total Ingresos
  #102#15 = 0;   || Total Consumo
  #102#16 = 0;   || Total Gastos

  |SI #0#39 = "S"; |PDEFECTO #0, 57, 64; #0#80 = 0; #0#81 = 0; |FINSI;
  |SI nSwConta = 1;
      |BUCLE LeeCuentas;
  |SINO;
       nSwPlani = 0;
       |HAZ Planilla;
       |SI nSwPlani = 0;
           nSwRendi = 0;
           |ABRE #102;
           |DDEFECTO #102;
           #102#0 = #114#0;
           #102#1 = nActividad;
           #102#2 = #0#1;
           |LEE 001#102=;
           |SI FSalida = 0; nSwRendi = 1; |FINSI;
           |CIERRA #102;
       |FINSI;
  |FINSI;

  |SI nSwRendi = 0; |Y nSwPlani = 0; |Y nSwConta = 0; |ACABA; |FINSI;

  |SI nSwPrint = 1;
      #0#57 = #0#57 + #102#13;
      #0#58 = #0#57 - #0#42;
      |SI #0#58 < 0; #0#58 = 0; |FINSI;

      #0#59 = #0#59 + #102#15 + #102#16;    || en la misma
                                            || #0#60 = #0#60 + #102#16;
      #0#61 = #0#59 - #0#43;
      |SI #0#61 < 0; #0#61 = 0; |FINSI;

      #0#64 = #114#10;
      #0#68 = #114#9;
  |FINSI;

  |SI nSwPrint = 2;
      #0#62 = #0#62 + #102#13;
      #0#63 = #0#62 - #0#44;
      |SI #0#63 < 0; #0#63 = 0; |FINSI;

      #0#65 = #114#10;
      #0#69 = #114#9;
  |FINSI;

  |SI nSwPrint = 3;
      #0#80 = #0#80 + #102#13;
      #0#81 = #0#80 - #0#73;
      |SI #0#81 < 0; #0#81 = 0; |FINSI;

      #0#82 = #114#10;
      #0#83 = #114#9;
  |FINSI;

  |SI #0#39 = "S";
      #0#66 = #114#1;
      #0#67 = #114#4;
      |SI #0#84 = "S"; |O #0#58 ! 0; |O #0#61 ! 0; |O #0#63 ! 0; |O #0#81 ! 0;
          |PRINT; nPrint = 1;
          nSwLinea = 1;
     |FINSI;
  |FINSI;

  nSwHay = 1;
|FPRC;

|DEFBUCLE PorActividad;
  #114#1;
  ;
  enEmpresa, 00;
  enEmpresa, 99;
  e;
  NULL, PorActividad;
|FIN;

|PROCESO ImprDatos;
   |SI #0#68 ! 0;
       |SI #0#58 ! 0; |O #0#61 ! 0; |O #0#84 = "S";
            nSwPrint = 1; |PRINT; nPrint = 1; nSwLinea = 1;
       |FINSI;
   |FINSI;

   |SI #0#69 ! 0;
       |SI #0#63 > 0; |O #0#84 = "S";
           nSwPrint = 2; |PRINT; nPrint = 1; nSwLinea = 1;
       |FINSI;
   |FINSI;

   |SI #0#83 ! 0;
       |SI #0#81 > 0; |O #0#84 = "S";
           nSwPrint = 3; |PRINT; nPrint = 1; nSwLinea = 1;
       |FINSI;
   |FINSI;
|FPRC;


|PROCESO LEmpresa;

 enEmpresa  = #agifigen#0;

 |DDEFECTO #900;
 #900#0 = enEmpresa;
 #900#2 = #agifigen#1;
 #900#11 = "ACTIVA";
 |SI #agifigen#94 > "01.01.0000";
     #900#11 = "INACTIVA";
     |SI #0#24 = "N"; |ACABA; |FINSI;
 |FINSI;

|| ... Seleccion de Tipo de Empresa
  nSPin1 = #agifigen#87;
  enSwSelEmp = 1;
  |SI #0#41 = "S";
      |SI nSPin1 ] #0#25; |Y nSPin1 [ #0#26;
          enSwSelEmp = 0;
      |FINSI;
  |SINO;
      |SI nSPin1 = #0#27;
          enSwSelEmp = 0;
      |SINO;
          |PARA nCampo = 28;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
                |SI nSPin1 = #0nCampo; |Y nSPin1 ! 0;
                    enSwSelEmp = 0;
                    |SAL;
                |FINSI;
          |SIGUE;
      |FINSI;
  |FINSI;
  |SI enSwSelEmp = 1; |ACABA; |FINSI;
|| ------------------------------------------------------------------------
|| ------------------------------------------------------------------------
   SwSocie   = 0;
   nSociedad = 0;
   nSwConta  = 0;
   nSwPlani  = 0;
   nSwRendi  = 0;
   nPorCtas  = 0;

   |PDEFECTO #0, 57, 70;
   #0#80 = 0;  #0#81 = 0;
   #0#82 = ""; #0#83 = 0;

   nTotAct  = 0;
   nNumAct  = 0;
   nSwHay   = 0;
   nSwLinea = 0;
   nSwOpera = 0;            ||Comprobar las actidades de la Empresa

   |BUCLE PorActividad;

   |SI nSwHay = 1;
       |HAZ PreparaConta;   ||Fichero departamentos

       |PDEFECTO #0, 57, 64;
       #0#80 = 0; #0#81 = 0;

       nNumAct  = 0;
       nTotAct  = 0;
       nSwHay   = 0;
       nSwLinea = 0;
       nSwOpera = 1;

       SwSocie = 0;
       |SI #0#85 = "S"; |Y nSociedad ! 0;     ||Para sociedades
           |HAZ LeoImpuesto;
       |FINSI;

       |SI SwSocie = 0;
           |SI nSwConta = 1; |Y nPorCtas = 1;
               |HAZ PorPlanCtble0;
           |SINO;
               |BUCLE PorActividad;
           |FINSI;
       |FINSI;

       |SI nSwHay = 1;
           |SI #0#39 = "S";
               |SI nSwLinea = 1; nSwPrint = 4; |PRINT; nPrint = 1; |FINSI;
           |SINO;
               |HAZ ImprDatos;
           |FINSI;
       |FINSI;

   |FINSI;

|FPRC;

|DEFBUCLE agifigen;
  #agifigen#1;
  ;
  nDEmpresa;
  nHEmpresa;
  e;
  NULL, LEmpresa;
|FIN;

|DEFBUCLE agifigen_n;
  #agifigen#2;
  0;
  "                                        ", nDEmpresa;
  "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz", nHEmpresa;
  e;
  NULL, LEmpresa;
|FIN;

|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|CALCULO Tipo3;  |TIPO 3;
    eaInac = #0#24;

    |DBASS aPath; |QBT aPath;

    enEjer = #0#1;
    aAlfa2 = enEjer;
    aAlfa1 = aAlfa2 % -102; nEjercicio = aAlfa1;
    aAlfa1 = "01.01." + aAlfa2; efDFecha = aAlfa1;
    aAlfa1 = "31.12." + aAlfa2; efHFecha = aAlfa1;
    eaTituloL = #0#40; ||Titulo del Libro

    |SI #0#45 ! 0; |O #0#46 ! 0; |O #0#47 ! 0;
        |O #0#48 ! 0; |O #0#49 ! 0; |O #0#50 ! 0;
           nSwCol1 = 1;
           aTex5 = "VOLUMEN DE"; aTex6 = "COMPRAS";
           aTex7 = "GASTOS";     aTex8 = "DIFERENCIA";
           aTex9 = "-------------------------------------------";
    |FINSI;
    |SI #0#51 ! 0; |O #0#52 ! 0; |O #0#53 ! 0;
        |O #0#54 ! 0; |O #0#55 ! 0; |O #0#56 ! 0;
           nSwCol2 = 1;
    |FINSI;
    |SI #0#74 ! 0; |O #0#72 ! 0; |O #0#76 ! 0;
        |O #0#77 ! 0; |O #0#78 ! 0; |O #0#79 ! 0;
           nSwCol3 = 1;
           |SI #0#39 = "N";
               aTex9 = "-------------------------------------------";
           |FINSI;
    |FINSI;

    eaDDepar  = "  ";
    eaHDepar  = "zz";
    |MENAV "0000 Recuerde seleccionar el papel de la impresora en Horizontal";

    |IMPRE 0;
    |SI FSalida ! 0; |ACABA; |FINSI;

    informe = "dsmoy036"; |SI #0#39 = "S"; informe = "dsmoya36"; |FINSI;
    |SI nSwCol1 = 0; |Y nSwCol2 = 0; |Y nSwCol3 = 1;
        informe = "dsmoyb36"; |SI #0#39 = "S"; informe = "dsmoyc36"; |FINSI;
    |FINSI;

    |INFOR informe;
    |SI FSalida ! 0; |ACABA; |FINSI;

    nPrint = 0;
    |SI #0#2 ! 0;
        nDEmpresa = #0#2;
        nHEmpresa = #0#3;
        |SI #0#37 = 1;
            |BUCLE agifigen;
        |SINO;
            |BUCLE agifigen_n;
        |FINSI;
    |SINO;
        |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
              |SI #0nCampo ! 0;
                  nDEmpresa = #0nCampo;
                  nHEmpresa = #0nCampo;
                  |BUCLE agifigen;
              |FINSI;
        |SIGUE;
    |FINSI;
    |SI nPrint = 1;  |PIEINF; |FINSI;
    |FININF;
    |FINIMP;
|FCAL;


|PROGRAMA;

 |CLS;
 |CABEZA "Inf.Detalle Vol. Operaciones";

 |ABRE #0;
 |LEE 001#0p;
 |SI FSalida ! 0;
     |DDEFECTO #0;
     fLaFecha = ~;
     aAlfa1 = fLaFecha; aAlfa1 = aAlfa1 % -104;
     #0#1 = aAlfa1;
     |GRABA 020#0c;
 |FINSI;

 |HAZ BusquedaSocie;
 |SI eaPathContasoc  = ""; |C_M #0#85, 1, "N"; #0#85 = "N"; |FINSI;

 |PINPA #0#0;
 |PINDA #0#0;

 |SI enEmpresa ! 0;
     #0#2 = enEmpresa;
     #0#3 = enEmpresa;
     #0#1 = enEjer;
     |HAZ Tipo3;
 |SINO;
     |ENDATOS #1#0;
 |FINSI;
 |GRABA 020#0a;
 |CIERRA #0;
|FPRO;

|PROCESO BusquedaSocie;
     eaPathContasoc  = "";
     |DBASS aBase;   |QBF aBase;

     aAlfa = aBase + "contasoc/def/maempr10.mas";
     |XABRE "E", aAlfa, nCanal;
     |SI FSalida ] 0;
         eaPathContasoc = aBase + "contasoc/";
     |FINSI;
|FPRC;
