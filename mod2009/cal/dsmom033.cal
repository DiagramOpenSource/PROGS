|FICHEROS;
     dsmom033 #33;
|FIN;

|VARIABLES;
     aFichModelos = "";
     aAbre        = "";
     aAlfa        = "";
     aAlfaAnt     = "";
     aCaracter9   = "";
     aCaracter10  = "";
     aCaracter    = "";
     aCadena      = "";
     aLong        = "";
     aCampo       = "";
     aCampo1      = "";
     aDesdeTipo   = "";
     aHastaTipo   = "";
     aParametro   = "";

     nHandle      = 0;
     nLong        = 0;
     nLong1       = 0;
     nPos         = 0;
     nPaso        = 0;
     nCaracter    = 0;
     nRegistro    = 0;
     nError       = 0;
     nVar         = 0;
     nNume        = 0;

     &eaTipoCultivo = "";
     &enCultivo     = 0;
|FIN;

|PROCESO CogeCampo;
     nError    = 0;

     |SI nPaso = 2;
         |QBF aCampo;
         |SI aCampo = "";  nError = 1; |ACABA;  |FINSI;

         nRegistro = nRegistro + 1;
     |FINSI;

     #33#0 = nRegistro;
     |LEE 101#33=;
     |SI FSalida ! 0;
         |DDEFECTO #33;
         #33#0 = nRegistro;
         |GRABA 020#33b;
     |FINSI;

     |SI nPaso = 2;  #33#8   = aCampo > "";           |FINSI;
     |SI nPaso = 3;  #33#1   = aCampo > "";           |FINSI;
     |SI nPaso = 4;  #33#9   = aCampo > "";           |FINSI;
     |SI nPaso = 5;  #33#2   = aCampo > "";           |FINSI;
     |SI nPaso = 6;  #33#5   = aCampo;                |FINSI;
     |SI nPaso = 7;  #33#3   = aCampo;                |FINSI;
     |SI nPaso = 8;  #33#4   = aCampo > "";           |FINSI;
     |SI nPaso = 9;  #33#6   = aCampo;                |FINSI;
     |SI nPaso = 10; #33#7   = aCampo;                |FINSI;

     |GRABA 020#33a;
     |LIBERA #33;
|FPRC;

|PROCESO Descomponlo;
     aLong = aCadena % 0;
     nLong = aLong;
     nPaso = 0;
     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPos      = (nCaracter * 100) + 1;
           aCaracter = aCadena % nPos;
           |SI aCaracter = aCaracter9;
               nPaso = nPaso + 1;
               |SI nPaso ] 2;
                   |HAZ CogeCampo;
                   |SI nError = 1;  |SAL;  |FINSI;
               |FINSI;
               aCampo = "";
           |SINO;
               aCampo = aCampo + aCaracter;
           |FINSI;
     |SIGUE;

     |SI nPaso ] 2;  |Y nError = 0;
         nPaso = nPaso + 1;
         |HAZ CogeCampo;
     |FINSI;
|FPRC;

|PROCESO CreaCultivos;
     |DFICE aFichModelos;  |QBF aFichModelos;
     aAbre = aFichModelos + "zagricola.txt";
     |XABRE "AB", aAbre, nHandle;

     |PARA;  |SI  FSalida ] 0;  |HACIENDO;
             |XLEE nHandle, 1, aAlfa;
             |SI FSalida [ 0;  |SAL;  |FINSI;

             aCaracter9  = &9;
             aCaracter10 = &10;
             nNume       = &aAlfa;

             |SI aAlfa = aCaracter10;
                 |HAZ Descomponlo;
                 aCadena = "";
             |SINO;
                 aCadena = aCadena + aAlfa;
             |FINSI;
     |SIGUE;
     |XCIERRA  nHandle;
|FPRC;

|PROCESO Cultivos;
     |CLS;
     |ABRE #33;
     |LEE 040#33p;
     |SI FSalida ! 0;
         |HAZ CreaCultivos;
     |FINSI;
     |CIERRA #33;
|FPRC;

|RUTINA ClaveBaja33;
     #33#0 = 1;
     #33#1 = "";
     #33#8 = aDesdeTipo;
|FRUT;

|RUTINA ClaveAlta33;
     #33#0 = 999;
     #33#1 = aAlfa;
     #33#8 = aHastaTipo;
|FRUT;

|PROGRAMA;
     |HAZ Cultivos;

     aParametro = PARAMETRO;   |QBF aParametro;

     |SI aParametro = "ENTRADA";
         |ENTLINEAL #1#33, -1, 2, 22, 1, ClaveBaja33, ClaveAlta33;
         |ACABA;
     |FINSI;

     aAlfa = "z" * 140;

     aDesdeTipo = eaTipoCultivo;
     aHastaTipo = eaTipoCultivo;

     |SI eaTipoCultivo = ""; |O eaTipoCultivo = "T";
         aDesdeTipo = " ";
         aHastaTipo = "z";
     |FINSI;

     |CAMPO_ANCHO #33#1, 1, 100;
     |CAMPO_ANCHO #33#4, 1, 100;
     |CAMPO_ANCHO #33#8, 1, 1;

     |ABRE #33;
     |SELECT #2#33;
     |LEE 040#33p;
     |CONSULTA_F_CLAVE #2#33, ClaveBaja33, ClaveAlta33;
     |SI FSalida = 0;  enCultivo = #33#0;  |FINSI;
     |CIERRA #33;
|FPRO;
