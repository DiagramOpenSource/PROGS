|FICHEROS;
     dsmoc349 #0;
     dsmom002 #2;
     dsmom003 #3;
     dsmom004 #4;
     dsmom005 #5;
     dsmom006 #6;
     dsmom008 #8;
     dsmod349 #349;

     dsmow002 #99;
     dsmow081 #81,MANTE;

     &regisnif@ #709;
     reflejo@   #20;
|FIN;

|VARIABLES;
     aAlfa1      = "";
     aDTipo      = "";
     aHTipo      = "";
     aConfi      = "";
     aPathModelo = "";
     aDef        = "";
     aAlfa       = "";
     aDT         = "";
     aHT         = "zz";
     aDN         = "";
     aHN         = "zzzzzzzzzzzz";

     nError      = 0;
     nPer        = 0;
     nCampo1     = 0;
     nCampo11    = 0;
     nCampo2     = 0;
     nCampo22    = 0;
     nCampo3     = 0;
     nCampo33    = 0;
     nDPeriodo   = 0;
     nHPeriodo   = 0;
     nDPeriodoV  = 0;
     nHPeriodoV  = 0;
     nSwGraba    = 0;
     nMod        = 0;
     nDComple    = 0;
     nHComple    = 0;

 {-1} aMenu       = "";
      aMenu1      = "1260";
      aMenu2      = "1";
      aMenu3      = "2";
      aMenu4      = "TRIMESTRAL";
      aMenu5      = "ANUAL     ";
      &enEmpresaA = 0;
      &enAnyo     = 0;
      nSim006     = 0;

      fFecha1     = @;
      fFecha2     = @;
      nNoExiste   = 0;
|FIN;

|INCLUYE i_variar;

|| /////////////////// Procesos del Programa por Pantalla \\\\\\\\\\\\\\\\\\

|PROCESO Periodo;
     |SI enModelo349 = 300;  |O enModelo349 = 310; |O enModelo349 = 309;
         |SI #0#2 ! enPeriodo;
             |MENAV "     Periodo Incorrecto.";
             |ERROR;
         |FINSI;
     |SINO;
         |SI #0#2 < nDPeriodoV;  |O #0#2 > nHPeriodoV;
             |MENAV "     Periodo Incorrecto.";
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;


|PROCESO Tipo349;
     |HAZ Modificas;

     |SI eaTipo349 = "";  |ACABA;  |FINSI;

     |SI eaTipo349 = "A";
         |SI #0#6 = "E ";  |O #0#6 = "ER";
             |MENAV "      Introduzca solo Adquisiciones o Rectificaciones Adquisiciones";
             |ERROR;
         |FINSI;
     |FINSI;

     |SI eaTipo349 = "E";
         |SI #0#6 = "A ";  |O #0#6 = "AR";
             |MENAV "      Introduzca solo Entregas o Rectificaciones Entregas";
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|RUTINA ClaveBaja20;
     #20#0 = enEmpresa;
     #20#1 = enEjer;
     #20#2 = nDPeriodoV;
     #20#3 = 349;
     #20#4 = enComplem;
     #20#5 = enActividad;
     #20#6 = aDTipo;
     #20#9 = "";
|FRUT;

|RUTINA ClaveAlta20;
     #20#0 = enEmpresa;
     #20#1 = enEjer;
     #20#2 = nHPeriodoV;
     #20#3 = 349;
     #20#4 = enComplem;
     #20#5 = enActividad;
     #20#6 = aHTipo;
     #20#9 = "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz";
|FRUT;

|PROCESO Dnimoc349;  |TIPO 11;
     |SI FSalida = 10;
         eaVarDni  = #0Campo;
         enCalcCif = 1;
         |HAZ CalculameDNI;
         #0Campo = eaVarDni;
         |PINTA #0Campo;
     |FINSI;
|FPRC;

|PROCESO MiraDnimoc349;  |TIPO 6;
     eaVarDni  = #0Campo;
     enCalcCif = 0;
     |HAZ CalculameDNI;
     eaVarDni = (eaVarDni + "             ") % 112;
     |SI #0Campo ! eaVarDni;
         |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
         |SI FSalida = 0;
             #0Campo = eaVarDni;  |PINTA #0Campo;
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO AntesPer; |TIPO 7;
  aAlfa1 = "    Pulse <F2> Consulta Ordenado por Nombre";
  |SI enTipoEntrada = 4;
      |SI #0#6 = "AR"; |O #0#6 = "ER";
          aAlfa1 = "    <F2> Consulta Ord. Nombre. <F3> Introducir Ejer./Per. Retificacion";
       |FINSI;
  |FINSI;
  |MENSA aAlfa1;

  |SI #0#4 ! 0;
      aAlfa = ((" " * 80) + ("COMPLEMENTARIA: "  + #0#4) + "   ") % -180;
  |SINO;
      aAlfa = (" " * 80) % -180;
  |FINSI;

  |ATRI S;
  |PINTA 0601, aAlfa;
  |ATRI s;
|FPRC;

|PROCESO AntesBlanco; |TIPO 7;
  |MENSA "";
|FPRC;

|PROCESO OrdenaNombre;  |TIPO 11;
     |SI FSalida = 10;
         |DBASE aPathModelo;  |QBT aPathModelo;
         aDef = aPathModelo + "def/dsmoc349.mas";
         |CARGA_DEF aDef, reflejo@;
         |SI FSalida ! 0;
             aAlfa = "     Error en cargar el Def dsmoc349.mas";
             |MENAV aAlfa;
             |ERROR;
         |FINSI;

         |PUSHV 0501 2380;
         |ATRI P;
         |PINTA 0703, "MODO CONSULTA  <ORDENADO POR NOMBRE>";
         |ATRI p;

         |ENTLINEAL #2#20, -8, 4, 22, 0, ClaveBaja20, ClaveAlta20;
         |POPV;

         |DESCARGA_DEF #reflejo@;

         |ERROR;
     |SINO;
         |SI FSalida = 11;
             |SI enTipoEntrada = 4;
                 |SI #0#6 = "AR"; |O #0#6 = "ER";
                     |LEE 101#0=;
                     |SI FSalida = 0;
                         |BORRA 020#0a;
                         |ENCAMPO #24#0;
                         |ENCAMPO #25#0;
                         |ENCAMPO #26#0;
                         |GRABA 020#0n;
                         |LIBERA #0;
                     |FINSI;
                     |ERROR;
                 |FINSI;
             |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Modificas;
     |C_M #0#22, 1, "S";
     |C_M #0#23, 1, "S";
     |C_M #0#24, 1, "S";
     |C_M #0#25, 1, "S";
     |C_M #0#26, 1, "S";

     |SI #0#6 = "A ";
         |C_M #0#22, 1, "N";
         |C_M #0#23, 1, "N";  #0#23 = "";  |PINTA #0#23;
         |C_M #0#24, 1, "N";  #0#24 = "";  |PINTA #0#24;
         |C_M #0#25, 1, "N";  #0#25 = 0;   |PINTA #0#25;
         |C_M #0#26, 1, "N";  #0#26 = 0;   |PINTA #0#26;
     |FINSI;

     |SI #0#6 = "AR";
         |C_M #0#22, 1, "N";
         |C_M #0#23, 1, "N";  #0#23 = "";  |PINTA #0#23;
     |FINSI;

     |SI #0#6 = "E ";
         |C_M #0#24, 1, "N";  #0#24 = "";  |PINTA #0#24;
         |C_M #0#25, 1, "N";  #0#25 = 0;   |PINTA #0#25;
         |C_M #0#26, 1, "N";  #0#26 = 0;   |PINTA #0#26;
     |FINSI;
|FPRC;

|PROCESO PonDatosNif;  |TIPO 7;
     eaCodNif  = #0#7;
     |HAZ LeeNifs;

     |SI #709#12 !  "   ";
         #0#8  = #709#12;  |PINTA #0#8;
     |FINSI;
     #0#9  = #709#1;   |PINTA #0#9;
|FPRC;

|PROCESO FiltroPais;
     |SI #0#8 = "DE ";  |ACABA;  |FINSI;
     |SI #0#8 = "AT ";  |ACABA;  |FINSI;
     |SI #0#8 = "BE ";  |ACABA;  |FINSI;
     |SI #0#8 = "BG ";  |ACABA;  |FINSI;
     |SI #0#8 = "CY ";  |ACABA;  |FINSI;
     |SI #0#8 = "DK ";  |ACABA;  |FINSI;
     |SI #0#8 = "SI ";  |ACABA;  |FINSI;
     |SI #0#8 = "EE ";  |ACABA;  |FINSI;
     |SI #0#8 = "FI ";  |ACABA;  |FINSI;
     |SI #0#8 = "FR ";  |ACABA;  |FINSI;
     |SI #0#8 = "EL ";  |ACABA;  |FINSI;
     |SI #0#8 = "GB ";  |ACABA;  |FINSI;
     |SI #0#8 = "NL ";  |ACABA;  |FINSI;
     |SI #0#8 = "HU ";  |ACABA;  |FINSI;
     |SI #0#8 = "IT ";  |ACABA;  |FINSI;
     |SI #0#8 = "IE ";  |ACABA;  |FINSI;
     |SI #0#8 = "LV ";  |ACABA;  |FINSI;
     |SI #0#8 = "LT ";  |ACABA;  |FINSI;
     |SI #0#8 = "LU ";  |ACABA;  |FINSI;
     |SI #0#8 = "MT ";  |ACABA;  |FINSI;
     |SI #0#8 = "PL ";  |ACABA;  |FINSI;
     |SI #0#8 = "PT ";  |ACABA;  |FINSI;
     |SI #0#8 = "CZ ";  |ACABA;  |FINSI;
     |SI #0#8 = "SK ";  |ACABA;  |FINSI;
     |SI #0#8 = "RO ";  |ACABA;  |FINSI;
     |SI #0#8 = "SE ";  |ACABA;  |FINSI;

     |MENAV "     Codigo de Pais incorrecto. ";
     |ERROR;
|FPRC;

|PROCESO Importe;
     |SI #0#6 ! "A ";  |Y #0#6 ! "AR";
         |ACABA;
     |FINSI;

     #99#0 = "Introducir las Adquisiciones";
          |PARA nCampo1 = 10;  |SI nCampo1 [ 21;  |HACIENDO nCampo1 = nCampo1 + 1;
                nCampo2 = nCampo1 - 6;
                #99nCampo2 = #0nCampo1;
          |SIGUE;

          |PUSHV 0501 2380;
          |PINPA #0#99;
          |PINDA #0#99;
          |ENDATOS #1#99;
          |SI FSalida = 0;
              |PARA nCampo1 = 10;  |SI nCampo1 [ 21;  |HACIENDO nCampo1 = nCampo1 + 1;
                    nCampo2 = nCampo1 - 6;
                    #0nCampo1 = #99nCampo2;
              |SIGUE;
          |FINSI;

          |POPV;

          #0#22 = #0#10 + #0#13 + #0#16 + #0#19;
          |PINTA #0#22;
|FPRC;

|PROCESO ImporteAnterior;
     |SI #0#6 ! "AR";  |Y #0#6 ! "ER";
         |ACABA;
     |FINSI;

     |PUSHV 0501 2380;
     |CUADRO 0756 1080;
     |BLANCO 0857 0979;

     |ATRI R;
     |PINTA 0857, "Base Imponible Anterior";
     |ATRI r;

     |C_V #0#27, 1, "S";
     |C_M #0#27, 1, "S";
     |C_P #0#27, 1, 0965;
     |ENCAMPO #27#0;
     |C_V #0#27, 1, "N";
     |C_M #0#27, 1, "N";
     |POPV;
|FPRC;

|| ///////////////////// Procesos de Alta y Presentacion \\\\\\\\\\\\\\\\\\\

|PROCESO Alta4y8;
     aConfi = "";
     #4#0   = enEmpresa;
     #4#1   = enEjer;
     #4#2   = 3;
     #4#3   = 349;
     #4#4   = enComplem;
     |LEE 040#4];
     |SI FSalida = 0;  |Y #4#0 = enEmpresa;  |Y #4#1 = enEjer; |Y #4#3 = 349;
      |Y #4#4 = enComplem;
         aConfi = "T";
     |FINSI;

     |SI enTipoEntrada ! 96;
         |CONFI "       No configurado Planing del Modelo 349, Desea Configurarlo : ";
         |SI FSalida ! 0;
             nError = 1;
             |ACABA;
         |FINSI;
     |FINSI;

     nNoExiste = 1;
     |DDEFECTO #4;
     nSim006 = 0;
     |ABRE #6;
     |ABRE #5;
     #6#0 = enEmpresa;
     #6#1 = 349;
     |LEE 040#6=;
     |SI FSalida = 0;
         nSim006 = 1;
         |SI #6#6 = "T";
             |SI enPeriodo ] 1;  |Y enPeriodo [ 3;  nPer = 3;  |FINSI;
             |SI enPeriodo ] 4;  |Y enPeriodo [ 6;  nPer = 6;  |FINSI;
             |SI enPeriodo ] 7;  |Y enPeriodo [ 9;  nPer = 9;  |FINSI;
             |SI enPeriodo ] 10; |Y enPeriodo [ 12; nPer = 12; |FINSI;
         |FINSI;

         |SI #6#6 = "A";  nPer = 99;         |FINSI;

         #4#5  = #6#9;
         #4#16 = #6#12;
     |SINO;
         #5#0 = 349;
         |LEE 040#5=;
         |SI FSalida ! 0;  |DDEFECTO #5;  |FINSI;

         |SI #5#5 = "T";
             |SI enPeriodo ] 1;  |Y enPeriodo [ 3;  nPer = 3;  |FINSI;
             |SI enPeriodo ] 4;  |Y enPeriodo [ 6;  nPer = 6;  |FINSI;
             |SI enPeriodo ] 7;  |Y enPeriodo [ 9;  nPer = 9;  |FINSI;
             |SI enPeriodo ] 10; |Y enPeriodo [ 12; nPer = 12; |FINSI;
         |FINSI;
         |SI #5#5 = "A";  nPer = 99;         |FINSI;

         #4#5  = #5#8;
         #4#16 = #5#11;

     |FINSI;
     |CIERRA #6;
     |CIERRA #5;

     |SI nPer = -1;  |Y aConfi = "";  |Y enTipoEntrada = 95;
         |SI enPeriodo ] 1;  |Y enPeriodo [ 3;  nPer = 3;  |FINSI;
         |SI enPeriodo ] 4;  |Y enPeriodo [ 6;  nPer = 6;  |FINSI;
         |SI enPeriodo ] 7;  |Y enPeriodo [ 9;  nPer = 9;  |FINSI;
         |SI enPeriodo ] 10; |Y enPeriodo [ 12; nPer = 12; |FINSI;
     |FINSI;

     |SI nPer = -1;  |Y aConfi = ""; |Y enTipoEntrada ! 96;
         |PUSHV 0501 2380;
         |PARA; |SI; |HACIENDO;
                |CUADRO 0745 1780;
                |BLANCO 0846 1679;
                |ATRI S;
                |PINTA 0846, "Introduzca la Periodicidad del 349";
                |MENUG aMenu;
                aMenu2 = FSalida;

                |SI aMenu2 = "1";
                    |SI enPeriodo ] 1;  |Y enPeriodo [ 3;  nPer = 3;  |FINSI;
                    |SI enPeriodo ] 4;  |Y enPeriodo [ 6;  nPer = 6;  |FINSI;
                    |SI enPeriodo ] 7;  |Y enPeriodo [ 9;  nPer = 9;  |FINSI;
                    |SI enPeriodo ] 10; |Y enPeriodo [ 12; nPer = 12; |FINSI;

                    |SAL;
                |FINSI;
                |SI aMenu2 = "2";   nPer = 99;         |SAL;  |FINSI;
         |SIGUE;
         |POPV;
     |SINO;
         |SI nPer = -1;
             |SI enPeriodo ] 1;  |Y enPeriodo [ 3;  nPer = 3;  |FINSI;
             |SI enPeriodo ] 4;  |Y enPeriodo [ 6;  nPer = 6;  |FINSI;
             |SI enPeriodo ] 7;  |Y enPeriodo [ 9;  nPer = 9;  |FINSI;
             |SI enPeriodo ] 10; |Y enPeriodo [ 12; nPer = 12; |FINSI;
         |FINSI;
     |FINSI;

        #4#0 = enEmpresa;
        #4#1 = enEjer;
        #4#2 = nPer;
        #4#3 = 349;
        #4#4 = enComplem;
    |SI enTipoEntrada ! 96;
        |GRABA 020#4n;
    |FINSI;
    |LIBERA #4;

     |ABRE #3;
     #3#0 = #4#0;
     #3#1 = #4#1;
     #3#2 = #4#2;
     |LEE 101#3=;
     |SI FSalida ! 0;
         |DDEFECTO #3;
         #3#0 = #4#0;
         #3#1 = #4#1;
         #3#2 = #4#2;

         |SI #3#2 = 1;   #3#3 = "ENERO     ";  |FINSI;
         |SI #3#2 = 2;   #3#3 = "FEBRERO   ";  |FINSI;
         |SI #3#2 = 3;   #3#3 = "MARZO     ";  |FINSI;
         |SI #3#2 = 4;   #3#3 = "ABRIL     ";  |FINSI;
         |SI #3#2 = 5;   #3#3 = "MAYO      ";  |FINSI;
         |SI #3#2 = 6;   #3#3 = "JUNIO     ";  |FINSI;
         |SI #3#2 = 7;   #3#3 = "JULIO     ";  |FINSI;
         |SI #3#2 = 8;   #3#3 = "AGOSTO    ";  |FINSI;
         |SI #3#2 = 9;   #3#3 = "SEPTIEMBRE";  |FINSI;
         |SI #3#2 = 10;  #3#3 = "OCTUBRE   ";  |FINSI;
         |SI #3#2 = 11;  #3#3 = "NOVIEMBRE ";  |FINSI;
         |SI #3#2 = 12;  #3#3 = "DICIEMBRE ";  |FINSI;
         |SI #3#2 = 99;  #3#3 = "ANUAL     ";  |FINSI;

         |GRABA 020#3n;
     |FINSI;
     |LIBERA #3;
     |CIERRA #3;

     |SI enTipoEntrada ! 96; |HAZ LoPongoPlaning; |FINSI;
|FPRC;

|PROCESO LoPongoPlaning;

    |SI enTipoEntrada = 96;
         nError = 0;
         |CONFI "       No configurado Planing del Modelo 349, Desea Configurarlo : ";
         |SI FSalida ! 0;
             nError = 1;
             |ACABA;
         |FINSI;
         |ABRE #4;
         |DDEFECTO #4;
         #4#0 = enEmpresa;
         #4#1 = enEjer;
         #4#2 = nPer;
         #4#3 = 349;
         #4#4 = enComplem;
         |SI nSim006 = 1;
             #4#5  = #6#9;
             #4#16 = #6#12;
         |SINO;
             #4#5  = #5#8;
             #4#16 = #5#11;
         |FINSI;
         |GRABA 020#4n;
         |CIERRA #4;
     |FINSI;

     |ABRE #2;
     |DDEFECTO #2;
     #2#0 = #4#0;
     #2#1 = enActividad;
     #2#2 = enModelo349;
     |LEE 000#2=;
     fFecha1 = #2#8;
     fFecha2 = #2#9;

     #2#0 = #4#0;
     #2#1 = enActividad;
     #2#2 = 349;
     |LEE 001#2=;
     |SI FSalida ! 0;
         #2#0  = #4#0;
         #2#1  = enActividad;
         #2#2  = 349;
         #2#10 = 1;
         |GRABA 020#2b;
     |FINSI;
     |SI nSim006 = 1;
         #2#3 = #6#2;
         #2#4 = #6#3;
         #2#5 = #6#4;

         |SI #6#6 = "T";  #2#7 = "TRIMESTRAL";  |FINSI;
         |SI #6#6 = "A";  #2#7 = "ANUAL     ";  |FINSI;
         |SI #6#6 = "M";  #2#7 = "MENSUAL   ";  |FINSI;
         |SI #6#6 = "C";  #2#7 = "SELECCIONA";  |FINSI;
     |SINO;
         #2#3 = #5#1;
         #2#4 = #5#2;
         #2#5 = #5#3;

         |SI #5#5 = "T";  #2#7 = "TRIMESTRAL";  |FINSI;
         |SI #5#5 = "A";  #2#7 = "ANUAL     ";  |FINSI;
         |SI #5#5 = "M";  #2#7 = "MENSUAL   ";  |FINSI;
         |SI #5#5 = "C";  #2#7 = "SELECCIONA";  |FINSI;
     |FINSI;
     #2#6 = eaNombreAct;
     #2#8 = fFecha1;
     #2#9 = fFecha2;
     |GRABA 020#2a;
     |LIBERA #2;
     |CIERRA #2;

     enEmpresaA = enEmpresa;
     enAnyo     = enEjer;
     |SUB_EJECUTA "dsmoz001";
|FPRC;

|PROCESO Alta4y8Activ;
     |ABRE #4;
     #4#0 = enEmpresa;
     #4#1 = enEjer;
     #4#2 = nPer;
     #4#3 = 349;
     #4#4 = enComplem;
     |LEE 040#4=;
     |SI FSalida ! 0;  |DDEFECTO #4;  |FINSI;
     |CIERRA #4;

     |ABRE #8;
     #8#0 = #4#0;
     #8#1 = #4#1;
     #8#2 = #4#2;
     #8#3 = #4#3;
     #8#4 = #4#4;
     #8#5 = enActividad;
     |LEE 101#8=;
     |SI FSalida ! 0;
         |DDEFECTO #8;
         #8#0  = #4#0;
         #8#1  = #4#1;
         #8#2  = #4#2;
         #8#3  = #4#3;
         #8#4  = #4#4;
         #8#5  = enActividad;
         #8#6  = eaNombreAct;
         #8#7  = eaCaptura;
         #8#8  = "REGISTROS DEL MODELO " + #8#3 + " EJERCICIO " + #8#1 + " PERIODO " + #8#2;
         #8#9  = eaComunidad;
         #8#10 = eaComunero;
         #8#12 = #8#3;
         |GRABA 020#8b;
     |FINSI;
     #8#11 = "SIN DATOS";
     |GRABA 020#8a;
     |LIBERA #8;
     |CIERRA #8;
|FPRC;

|RUTINA ClaveBaja0;
     #0#0  = enEmpresa;
     #0#1  = enEjer;
     #0#2  = nDPeriodoV;
     #0#3  = 349;
     #0#4  = nDComple;
     #0#5  = enActividad;
     #0#6  = aDTipo;
     #0#7  = "            ";
     #0#25 = 0000;
     #0#26 = 0;
|FRUT;

|RUTINA ClaveAlta0;
     #0#0  = enEmpresa;
     #0#1  = enEjer;
     #0#2  = nHPeriodoV;
     #0#3  = 349;
     #0#4  = nHComple;
     #0#5  = enActividad;
     #0#6  = aHTipo;
     #0#7  = "zzzzzzzzzzzz";
     #0#25 = 9999;
     #0#26 = 9;
|FRUT;

|PROCESO Graba349;
     |SI #0#6 = "E ";
         #349#6 = #349#6 + 1;
         #349#7 = #349#7 + #0#22;
     |FINSI;

     |SI #0#6 = "A ";
         #349#8 = #349#8 + 1;
         #349#9 = #349#9 + #0#22;
     |FINSI;

     |SI #0#6 = "AR";  |O #0#6 = "ER";
         #349#10 = #349#10 + 1;
         #349#11 = #349#11 + #0#22;
     |FINSI;

     nSwGraba = 1;
|FPRC;

|DEFBUCLE dsmoc349;
     #0#1;
     ;
     enEmpresa, enEjer, nDPeriodo, 349, enComplem, 01, "  ", 0000, 0, "            ";
     enEmpresa, enEjer, nHPeriodo, 349, enComplem, 99, "zz", 9999, 9, "zzzzzzzzzzzz";
     ;
     NULL, Graba349;
|FIN;

|PROCESO Graba349_99;
     #0#0 = #20#0;
     #0#1 = #20#1;
     #0#2 = 99;
     #0#3 = #20#3;
     #0#4 = #20#4;
     #0#5 = #20#5;
     #0#6 = #20#6;
     #0#7 = #20#7;
     |LEE 101#0=;
     |SI FSalida ! 0;
         |DDEFECTO #0;
         #0#0 = #20#0;
         #0#1 = #20#1;
         #0#2 = 99;
         #0#3 = #20#3;
         #0#4 = #20#4;
         #0#5 = #20#5;
         #0#6 = #20#6;
         #0#7 = #20#7;
         #0#8 = #20#8;
         #0#9 = #20#9;
         |GRABA 020#0b;
     |FINSI;

     #0#22 = #0#22 + #20#22;

     |PARA nCampo2 = 11;  |SI nCampo2 [ 20;  |HACIENDO nCampo2 = nCampo2 + 3;
           nCampo1 = nCampo2 - 1;
           nCampo3 = nCampo2 + 1;
            |PARA nCampo22 = 11;  |SI nCampo22 [ 20;  |HACIENDO nCampo22 = nCampo22 + 3;
                  nCampo11 = nCampo22 - 1;
                  nCampo33 = nCampo22 + 1;
                  |SI #0nCampo22 = #20nCampo2;
                      #0nCampo11 = #0nCampo11 + #20nCampo1;
                      #0nCampo33 = #0nCampo33 + #20nCampo3;
                      |SAL;
                  |FINSI;
            |SIGUE;
     |SIGUE;

     |GRABA 020#0a;
     |LIBERA #0;
|FPRC;

|DEFBUCLE reflejo;
     #20#1010;
     ;
     enEmpresa, enEjer,  1, 349, enComplem, 01, "  ", 0000, 0, "            ";
     enEmpresa, enEjer, 12, 349, enComplem, 99, "zz", 9999, 9, "zzzzzzzzzzzz";
     ;
     NULL, Graba349_99;
|FIN;

|PROCESO Borra349_99;
     |BORRA 020#0a;
     |LIBERA #0;
|FPRC;

|DEFBUCLE dsmoc349_99;
     #0#1;
     ;
     enEmpresa, enEjer, 99, 349, enComplem, 01, "  ", 0000, 0, "            ";
     enEmpresa, enEjer, 99, 349, enComplem, 99, "zz", 9999, 9, "zzzzzzzzzzzz";
     ;
     NULL, Borra349_99;
|FIN;

|PROCESO Comple;
     |C_M #81#1, 1, "S";
     |SI #81#0 = "C";
         |C_M #81#1, 1, "N";  #81#1 = "";  |PINTA #81#1;
     |FINSI;
|FPRC;

|PROCESO GrabaRegistro0;
     |ABRE #8;
     #8#0 = enEmpresa;
     #8#1 = enEjer;
     #8#2 = nPer;
     #8#3 = 349;
     #8#4 = enComplem;
     #8#5 = 0;
     |LEE 101#8=;
     |SI FSalida = 0;
         #8#11 = "SIN DATOS";
         |GRABA 020#8a;
     |FINSI;
     |LIBERA #8;
     |CIERRA #8;

     nSwGraba = 0;

     |ABRE #349;

     #349#0 = enEmpresa;
     #349#1 = enEjer;
     #349#2 = nPer;
     #349#3 = 349;
     #349#4 = enComplem;
     #349#5 = 0;
     |LEE 101#349=;
     |SI FSalida ! 0;
         |DDEFECTO #349;
         #349#0 = enEmpresa;
         #349#1 = enEjer;
         #349#2 = nPer;
         #349#3 = 349;
         #349#4 = enComplem;
         #349#5 = 0;

         |GRABA 020#349b;
     |FINSI;

     |SI enComplem ! 0;  |Y enTipoEntrada ! 3;  |Y enTipoEntrada ! 97;
         |PUSHV 0501 2380;
         #81#0 = #349#12;
         #81#1 = #349#13;
         |PINPA #0#81;
         |PINDA #0#81;
         |ENDATOS #1#81;
         |SI FSalida = 0;
             #349#12 = #81#0;
             #349#13 = #81#1;
         |FINSI;
         |POPV;
     |FINSI;

     #349#6  = 0;
     #349#7  = 0;
     #349#8  = 0;
     #349#9  = 0;
     #349#10 = 0;
     #349#11 = 0;

     |SI #349#2 = 99;
         |BUCLE dsmoc349_99;

         |DBASE aPathModelo;  |QBT aPathModelo;
         aDef = aPathModelo + "def/dsmoc349.mas";
         |CARGA_DEF aDef, reflejo@;
         |SI FSalida ! 0;
             aAlfa = "     Error en cargar el Def dsmoc349.mas";
             |MENAV aAlfa;
             |ERROR;
         |FINSI;

         |ABRE #0;
         |BUCLE reflejo;
         |CIERRA #0;

         |DESCARGA_DEF #reflejo@;

         nDPeriodo = 99;
         nHPeriodo = 99;
         |BUCLE dsmoc349;
     |SINO;
         |BUCLE dsmoc349;
     |FINSI;

     |SI nSwGraba = 0;
         |BORRA 020#349a;
         |LIBERA #349;
     |SINO;
         |GRABA 020#349a;
         |LIBERA #349;
         |CIERRA #349;

         |ABRE #8;
         #8#0 = enEmpresa;
         #8#1 = enEjer;
         #8#2 = nPer;
         #8#3 = 349;
         #8#4 = enComplem;
         #8#5 = 0;
         |LEE 101#8=;
         |SI FSalida = 0;
             #8#11 = "EXISTENTE";
             |GRABA 020#8a;
         |FINSI;
         |LIBERA #8;
         |CIERRA #8;
     |FINSI;

     |ABRE #4;
     #4#0 = enEmpresa;
     #4#1 = enEjer;
     #4#2 = nPer;
     #4#3 = 349;
     #4#4 = enComplem;
     |LEE 040#4=;
     |SI FSalida = 0;
         nMod     = enModelo;
         enModelo = 349;
         |HAZ ChequeaSituacion;
         enModelo = nMod;

         |GRABA 020#4a;
     |FINSI;
     |LIBERA #4;
     |CIERRA #4;
|FPRC;

|PROCESO AltaModelo;
     |HAZ Alta4y8Activ;

     |SI enTipoEntrada ! 96;
         |HAZ PintaDatos;
         |ENTLINEAL #1#0, -3, 2, 22, 1, ClaveBaja0, ClaveAlta0;
     |FINSI;

     |ABRE #0;
     #0#0 = enEmpresa;
     #0#1 = enEjer;
     #0#2 = nDPeriodo;
     #0#3 = 349;
     #0#4 = enComplem;
     #0#5 = enActividad;
     #0#6 = aDTipo;
     #0#7 = "            ";
     |LEE 040#0];
     |SI FSalida = 0;    |Y #0#0 = enEmpresa; |Y #0#1 = enEjer;
        |Y  #0#3 = 349;  |Y #0#4 = enComplem; |Y #0#5 = enActividad;
            |SI #0#2 ] nDPeriodo;  |Y #0#2 [ nHPeriodo;
                |SI enTipoEntrada = 96; |Y nNoExiste = 1;
                    |HAZ LoPongoPlaning;
                |FINSI;
                |ABRE #8;
                #8#0 = #4#0;
                #8#1 = #4#1;
                #8#2 = #4#2;
                #8#3 = #4#3;
                #8#4 = #4#4;
                #8#5 = enActividad;
                |LEE 101#8=;
                |SI FSalida = 0;
                    #8#11 = "COMPLETADO";
                    |GRABA 020#8a;
                |FINSI;
                |LIBERA #8;
                |CIERRA #8;
            |FINSI;
     |FINSI;

     |HAZ GrabaRegistro0;
|FPRC;

|PROCESO Borra0;
     |BORRA 020#0a;
     |LIBERA #0;
|FPRC;

|DEFBUCLE dsmoc349B;
     #0#1;
     ;
     enEmpresa,enEjer,nDPeriodoV,349,enComplem,enActividad,aDT,0000,0,aDN;
     enEmpresa,enEjer,nHPeriodoV,349,enComplem,enActividad,aHT,9999,9,aHN;
     be;
     NULL, Borra0;
|FIN;

|PROCESO BorraRegistros;
     aDT       = "";
     aHT       = "zz";
     aDN       = "";
     aHN       = "zzzzzzzzzzzz";
     |BUCLE dsmoc349B;
     |HAZ GrabaRegistro0;

     |ABRE #8;
     #8#0 = enEmpresa;
     #8#1 = enEjer;
     #8#2 = nPer;
     #8#3 = 349;
     #8#4 = enComplem;
     #8#5 = enActividad;
     |LEE 101#8=;
     |SI FSalida = 0;
         #8#11 = "SIN DATOS";
         |GRABA 020#8a;
     |FINSI;
     |LIBERA #8;
     |CIERRA #8;
|FPRC;

|PROCESO MiraElPlaning;
     nPer = -1;

     |SI enPeriodo ] 1;  |Y enPeriodo [ 3;  nPer = 3;  |FINSI;
     |SI enPeriodo ] 4;  |Y enPeriodo [ 6;  nPer = 6;  |FINSI;
     |SI enPeriodo ] 7;  |Y enPeriodo [ 9;  nPer = 9;  |FINSI;
     |SI enPeriodo ] 10; |Y enPeriodo [ 12; nPer = 12; |FINSI;
     |ABRE #4;
     #4#0 = enEmpresa;
     #4#1 = enEjer;
     #4#2 = nPer;
     #4#3 = 349;
     #4#4 = enComplem;
     |LEE 040#4=;
     |SI FSalida = 0;
         nPer = #4#2;
     |SINO;
         nPer = -1;
     |FINSI;

     |SI nPer = -1;
         #4#0 = enEmpresa;
         #4#1 = enEjer;
         #4#2 = 99;
         #4#3 = 349;
         #4#4 = enComplem;
         |LEE 040#4=;
         |SI FSalida = 0;  nPer = #4#2;  |FINSI;
     |FINSI;
     |CIERRA #4;
|FPRC;

|PROCESO FiltrosProgram;
     |HAZ MiraElPlaning;

     |SI nPer = -1;
         |SI enTipoEntrada = 99;
             enExistencia = 1;
             |ACABA;
         |FINSI;

         |SI enTipoEntrada = 4;
             |MENAV "     No existe Modelo.";
             enExistencia = 1;
             |ACABA;
         |FINSI;

         |SI enTipoEntrada = 1; |Y enComplem = 0;
              nError = 0;
              |HAZ Alta4y8;
              |SI nError = 1;
                  |ACABA;
              |FINSI;
         |FINSI;

         |SI enTipoEntrada = 96; |Y enComplem = 0;
              nError = 0;
              |HAZ Alta4y8;
              |SI nError = 1;
                  |ACABA;
              |FINSI;
         |FINSI;
     |FINSI;

     |SI nPer = 99;
         nDPeriodo = 01;
         nHPeriodo = 12;
     |SINO;
         |SI nPer = 3;    nDPeriodo = 1;   nHPeriodo = 3;   |FINSI;
         |SI nPer = 6;    nDPeriodo = 4;   nHPeriodo = 6;   |FINSI;
         |SI nPer = 9;    nDPeriodo = 7;   nHPeriodo = 9;   |FINSI;
         |SI nPer = 12;   nDPeriodo = 10;  nHPeriodo = 12;  |FINSI;
     |FINSI;

/*
     |SI enModelo349 = 0;
         nDPeriodoV = nDPeriodo;
         nHPeriodoV = nHPeriodo;
     |SINO;
         nDPeriodoV = enPeriodo;
         nHPeriodoV = enPeriodo;
     |FINSI;
*/

     nDPeriodoV = nDPeriodo;
     nHPeriodoV = nHPeriodo;

     aDTipo = "  ";
     aHTipo = "zz";

     |SI eaTipo349 = "A";
         aDTipo = "A ";
         aHTipo = "Az";
     |FINSI;

     |SI eaTipo349 = "E";
         aDTipo = "E ";
         aHTipo = "Ez";
     |FINSI;
|FPRC;

|PROCESO Existencia;
     enExistencia = 0;
|FPRC;

|DEFBUCLE Existencia;
     #0#1;
     ;
     enEmpresa,enEjer,nDPeriodo,349,enComplem,enActividad,aDT,0000,0,aDN;
     enEmpresa,enEjer,nHPeriodo,349,enComplem,enActividad,aHT,9999,9,aHN;
     ;
     NULL, Existencia;
|FIN;

|PROGRAMA;
     |ABRE #4;
     |ABRE #8;

     nNoExiste    = 0;
     enExistencia = 0;
     |HAZ FiltrosProgram;

     |SI enExistencia = 1;  |O nError = 1; |O enTipoEntrada = 95;
         |CIERRA #4;
         |CIERRA #8;
         |ACABA;
     |FINSI;

     |CIERRA #4;
     |CIERRA #8;
     |SI nPer = -1; |Y enComplem ! 0;
         |SI enTipoEntrada = 96; |O enTipoEntrada = 1; |O enTipoEntrada = 95;
             |MENAV "    Antes de Introducir Datos del Modelo 349, cree la Complementaria ";
             |ACABA;
         |FINSI;
     |FINSI;

     eaNoVisu = "";
     eaNomDef = "dsmoc349";
     nDComple = enComplem;
     nHComple = enComplem;

|| \\\\\\ Consulta del Modelo ////////

     |SI enTipoEntrada = 4;              || Consulta del Modelo
         |HAZ PintaDatos;
         |ENTLINEAL #1#0, -3, 4, 22, 1, ClaveBaja0, ClaveAlta0;
     |FINSI;

|| \\\\\\ Baja del Modelo ////////

     |SI enTipoEntrada = 3;              ||Baja del Modelo
         |HAZ BorraRegistros;
     |FINSI;

|| \\\\\\ Baja del Modelo desde Modulo////////

     |SI enTipoEntrada = 97;              ||Baja del Modelo
         |HAZ BorraRegistros;
     |FINSI;

|| \\\\\\ Alta del Modelo desde Registros ////////

     |SI enTipoEntrada = 96;              || Alta del Modelo
         |HAZ AltaModelo;
     |FINSI;

|| \\\\\\ Alta o Modificacion del Modelo ////////

     |SI enTipoEntrada = 1;              || Alta del Modelo
         |CABEZA "Modelo Trabajo 349 ";

         |SI enHComple ! 0;
             nDComple = enDComple;
             nHComple = enHComple;
         |FINSI;

         |HAZ AltaModelo;
         enTipoEntrada = 0;

         |SI enComplem ! 0;
             |MENAV "0000 Revise lo declarado en el IVA con el Modelo 349.";
         |FINSI;
     |FINSI;

|| \\\\\\ Comprobacion Existencia del Modelo

     |SI enTipoEntrada = 99;              || Existencia del Modelo ????
         enExistencia = 1;
         aDT          = "";
         aHT          = "zz";
         aDN          = "";
         aHN          = "zzzzzzzzzzzz";
         |BUCLE Existencia;
    |FINSI;
|FPRO;


/*
||reserva calculo alta4y8
     |ABRE #8;
     #8#0 = #4#0;
     #8#1 = #4#1;
     #8#2 = #4#2;
     #8#3 = #4#3;
     #8#4 = #4#4;
     #8#5 = 0;
     |LEE 101#8=;
     |SI FSalida ! 0;
         |DDEFECTO #8;
         #8#0 = #4#0;
         #8#1 = #4#1;
         #8#2 = #4#2;
         #8#3 = #4#3;
         #8#4 = #4#4;
         #8#5 = 0;
         |GRABA 020#8b;
     |FINSI;

     #8#6  = "REGISTRO DE IMPRESION";
     #8#7  = "";
     #8#8  = "REGISTRO DEL MODELO " + #8#3 + " EJERCICIO " + #8#1 + " PERIODO " + #8#2;
     #8#9  = "N";
     #8#10 = "N";
     #8#11 = "";
     #8#12 = #8#3;
     |GRABA 020#8a;
     |LIBERA #8;

     |SI #4#5 = "ACT.+ REG.";
         #8#0 = #4#0;
         #8#1 = #4#1;
         #8#2 = #4#2;
         #8#3 = #4#3;
         #8#4 = #4#4;
         #8#5 = 99;
         |LEE 101#8=;
         |SI FSalida ! 0;
             |DDEFECTO #8;
             #8#0 = #4#0;
             #8#1 = #4#1;
             #8#2 = #4#2;
             #8#3 = #4#3;
             #8#4 = #4#4;
             #8#5 = 99;
             |GRABA 020#8b;
         |FINSI;

         #8#6  = "REGISTRO DE REGULARIZACION";
         #8#7  = "MANUAL";
         #8#8  = "REGISTROS DEL MODELO " + #8#3 + " EJERCICIO " + #8#1 + " PERIODO " + #8#2;
         #8#9  = "N";
         #8#10 = "N";
         #8#11 = "SIN DATOS";
         #8#12 = #8#3;
         |GRABA 020#8a;
     |FINSI;
     |CIERRA #8;
*/
