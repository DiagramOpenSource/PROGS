|FICHEROS;
   dsmoy011 #0;

   dsmom041 #41;
   dsmom042 #42;

   :/basica/def/agicen14 #114;
   :/basica/def/agifigen #118;
   :/basica/def/agim0019 #119;

   :/contagen/def/canempre #300;
   :/contagen/def/caivaasi #301;
   :/contagen/def/camaniva #304;
   :/contagen/def/caivalin #305;
   :/contagen/def/caconimp #306;  || configuracion impresiones

   dsmom030 #30;
   dsmow030 #900;          ||Def de impresion
   dsmow031 #901;
   dsmow043 #999,MANTE;
|FIN;

|VARIABLES;
   &entrada = 1;
   informe = "";

   nInkey      = 0;
   aFichero    = "";
   nCampo      = 0;
   nDEmpresa   = 0;
   nHEmpresa   = 0;
   nDActividad = 0;
   nHActividad = 0;
   nLaEmpresa  = 0;
   nGTempo     = 0;

   nSPin1      = 0;
   aPath       = "";
   nEjercicio  = 0;

   aAlfa1 = "";
   aAlfa2 = "";
   aAlfa3 = "";
   nPara1 = 0;
   nPara2 = 0;
   nNume1 = 0;
   nNume2 = 0;
   nNume3 = 0;
   nNume4 = 0;

   aDTipo = "";
   aHTipo = "";
   SwHayDatos = 0;

   fDFecha   = @;
   fHFecha   = @;
   fLaFecha  = @;
   &nFactura = 0;
   &nPagina  = 0;
   &nPagDup  = 0;
   &nSwPagDup = 0;
   &SwPrim   = 0;
   &SwLinea  = 0;
   &nLBase   = 0;
   &nLTipoI  = 0;
   &nLCuoI   = 0;
   &nLTipoR  = 0;
   &nLCuoR   = 0;

   &aTipo = "";
   &asTipo = "";
   sw = 0;
   nElDepar = 0;

   nOpMdep      = 0;
   nAlSi        = 0;
   nAlNo        = 0;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;
|INCLUYE i_varlib;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\

|CALCULO Eano; |TIPO 7;
 fLaFecha = ~;
 aAlfa1 = fLaFecha; aAlfa1 = aAlfa1 % -104;
 #0#1 = aAlfa1;
 |PINTA #0#1;
|FCAL;

|CALCULO losDefectos; |TIPO 0;
 aAlfa1 = #0#1;
 aAlfa2 = "01.01." + aAlfa1; #0#41 = aAlfa2; |PINTA #0#41;
 aAlfa2 = "31.12." + aAlfa1; #0#42 = aAlfa2; |PINTA #0#42;
|FCAL;

|CALCULO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#2  = enEmpresa;  |PINTA #0#2;
         #0#3  = enEmpresa;  |PINTA #0#3;

         |C_M #0#2, 1, "N";
         |C_M #0#3, 1, "N";
     |FINSI;
|FCAL;

|CALCULO Parrilla; |TIPO 0;
     |SI #0#2 = 0;
         #0#2 = 0;  |PINTA #0#2;
         #0#3 = 0;  |PINTA #0#3;  |C_M #0#3, 1, "N";
         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#2, 1, "S";
             |C_M #0#3, 1, "S";
         |FINSI;

         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas; |TIPO 0;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO YaSeCual; |TIPO 7;
 |SI enEmpresa ! 0;
     #0#54 = "S"; |PINTA #0#54;
     #0#24 = "S"; |PINTA #0#24;
     #0#25 = 00;  |PINTA #0#25;
     #0#26 = 99;  |PINTA #0#26;
     |C_M #0#24, 1, "N";
     |C_M #0#25, 1, "N";
     |C_M #0#26, 1, "N";
     |C_M #0#54, 1, "N";
 |FINSI;
|FCAL;

|CALCULO SelTipo0; |TIPO 0;
 |SI #0#54 = "N";
     #0#25 = 0;   |PINTA #0#25;  |C_M #0#25, 1, "N";
     #0#26 = 99;  |PINTA #0#26;  |C_M #0#26, 1, "N";
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |SINO;
     |SI enEmpresa = 0;
         |C_M #0#25, 1, "S";
         |C_M #0#26, 1, "S";
     |FINSI;
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "N";
           #0nCampo = 0;  |PINTA #0nCampo;
     |SIGUE;
 |FINSI;
|FCAL;

|CALCULO SelTipo1;
 |SI #0Campo = 0;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           #0nCampo = 0;  |C_M #0nCampo, 1, "N";
     |SIGUE;
 |SINO;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |FINSI;
|FCAL;

|CALCULO fecha; |TIPO 0;
 aAlfa1 = #0#1;
 aAlfa2 = "01.01." + aAlfa1;
 aAlfa3 = "31.12." + aAlfa1;
 |SI #0Campo < aAlfa2; |O #0Campo > aAlfa3;
     |MENAV "    Error. Fecha Incorrecta ";
     |ERROR;
 |SINO;
     |SI Campo = 42; |Y #0#41 > #0#42;
         |MENAV "    Error. Fecha Incorrecta ";
         |ERROR;
     |FINSI;
 |FINSI;
|FCAL;

|CALCULO SelAct; |TIPO 0;
 aAlfa1 = #0Campo;
 |C_M #0#56,1, aAlfa1;
 |C_M #0#57,1, aAlfa1;
|FCAL;

|CALCULO Cuadro; |TIPO 7;
   |PUSHV  1912 2270;
   |CUADRO 1912 2270;
   |ATRI R;
   |PINTA 1913, " ------------ FORMATO LIBRO FACTURAS ------------------- ";
   |PINTA 2013, " ( 1) Descr. Fra., Nombre y NIF, varias lineas           ";
   |PINTA 2113, " ( 2) Nombre y Nif, varias lineas con rayas              ";
   |ATRI r;
|FCAL;

|CALCULO SalCuadro; |TIPO 0;
   |POPV;
|FCAL;

|| **********************************************************************************
|| **********************************************************************************
|| ....................... Situa la Empresa Contable
|PROCESO LEmpresaConta;

 enEmpresa  = #canempre#2;
 enPerConta = #canempre#3;;   || Periodo Contable

 |SI #0#24 = "N"; |Y #canempre#21 = "I"; |ACABA; |FINSI;

|| ... Seleccion de Tipo de Empresa
  |ABRE #agifigen;
  #agifigen#0 = enEmpresa;
  |LEE 041#agifigen.=;
  |SI FSalida ! 0;
      |DDEFECTO #agifigen;
  |FINSI;
  |CIERRA #agifigen;
  nSPin1 = #agifigen#87;

  enSwSelEmp = 1;
  |SI #0#54 = "S";
      |SI nSPin1 ] #0#25; |Y nSPin1 [ #0#26;
          enSwSelEmp = 0;
      |FINSI;
  |SINO;
      |SI nSPin1 = #0#27;
          enSwSelEmp = 0;
      |SINO;
          |PARA nCampo = 28;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
                |SI nSPin1 = #0nCampo; |Y nSPin1 ! 0;
                    enSwSelEmp = 0;
                   |SAL;
                |FINSI;
          |SIGUE;
      |FINSI;
  |FINSI;
  |SI enSwSelEmp = 1; |ACABA; |FINSI;
|| ------------------------------------------------------------------------
   aAlfa1 = enEmpresa;  aAlfa1 = ("00000" + aAlfa1) % -105;
   aAlfa2 = enPerConta; aAlfa2 = ("0" + aAlfa2) % -101;
   aPathConta  = aPath + "contagen/fich/" + aAlfa1 + aAlfa2 + "/";

   nGTempo = nGTempo + 1;
   #999#0 = nGTempo;       || Guia
   #999#1 = enEmpresa;     || Empresa
   #999#2 = enPerConta;    || Periodo Contable
   #999#3 = enEjer;        || A¤o
   #999#4 = #canempre#11;  || Desde Mes
   #999#5 = #canempre#13;  || N§ Digitos
   #999#6 = #canempre#14;  || Digitos Nivel 1
   #999#7 = #canempre#15;  || Digitos Nivel 2
   #999#8 = #canempre#16;  || Digitos Nivel 3
   #999#9 = #canempre#17;  || Digitos Nivel 4
   #999#10 = #canempre#18; || Digitos Nivel 5
   #999#11 = #canempre#19; || Digitos Nivel 6
   eaEstadoEmp = "S"; |SI #canempre#21 = "I"; eaEstadoEmp = "N"; |FINSI;
   #999#12 = eaEstadoEmp;  || Estado
   #999#13 = #canempre#28; || Moneda
   #999#14 = #canempre#1;  || Nombre Empresa
   #999#15 = aPathConta;   || Path
   #999#16 = #0#48;        || Emitir
   |GRABA 020#999n;
|FPRC;

|DEFBUCLE canempre;
 #300#4;
 ;
 nDEmpresa, nEjercicio;
 nHEmpresa, nEjercicio;
 ;
 NULL, LEmpresaConta;
|FIN;
||///////////////////////////////////////////////////////////////////

|CALCULO Datos7; |TIPO 7;
 |MENSA "    <F3> Cambiar Emitir Libro S/N";
 |ATRI R; |PINTA 1169, "Imp"; |ATRI 0;
|FCAL;

|CALCULO DatosManip; |TIPO 11;
   nInkey = FSalida;
   |BLIN 04;
   |SI nInkey = 11;
       |SI #999#16 = "S";
           #999#16 = "N";
       |SINO;
           #999#16 = "S";
       |FINSI;
       |GRABA 020#999a;
   |FINSI;
|FCAL;

|RUTINA inferior;
   #999#0 = 00001;
|FRUT;

|RUTINA superior;
   #999#0 = 99999;
|FRUT;

||///////////////////////////////////////////////////////////////////
|CALCULO Tipo3;  |TIPO 3;
    #0#0  = "I";      ||Inversion
    #0#49 = enModelo; ||Libro Oficial

    |HAZ LeeCtrlLibro;

    enEjer = #0#1;
    aAlfa1 = enEjer; |QBF aAlfa1;
    aAlfa1 = aAlfa1 % -102;
    nEjercicio = aAlfa1;
    |DBASS aPath; |QBT aPath;

    aFichero = ("031w" + Usuario) % 108;
    |NOME_DAT #901 aFichero;

    aFichero = ("043w" + Usuario) % 108;
    |NOME_DAT #999 aFichero;
    |ABRE #999;  |CIERRA #999;  |DELINDEX #999;
    eaFEmpresa = aFichero;

    |ABRE #999;
    nGTempo = 0;   ||Busco Empresas Contables
    |SI #0#2 ! 0;
        nDEmpresa = #0#2;
        nHEmpresa = #0#3;
        |BUCLE canempre;
    |SINO;
        |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
              nDEmpresa = #0nCampo;
              nHEmpresa = #0nCampo;
              |BUCLE canempre;
        |SIGUE;
    |FINSI;
    |CIERRA #999;

    |SI nGTempo = 0;    || No existe ninguna empresa
        |MENAV "    Error. No existe ninguna Empresa Contable dentro de la Seleccion ";
        |DELINDEX #999;
        |ACABA;
    |FINSI;

    |C_V #999#12,1,"N"; |C_P #999#16,1,1270;

    |PUSHV 0509 2375;
    |ENTLINEAL #1#999, -1, 2, 22, 1, inferior, superior;
    |POPV;

    |HAZ Imprimir;

    |DELINDEX #901;
    |DELINDEX #999;
|FCAL;

|| *************************************************************************
|| ... Proceso para encontrar el nombre del Elemento
|PROCESO BElemento0;
 |SI #42#6 = "V"; |O #42#6 = "C";

     |ABRE #41;
     #41#0 = #42#0;
     #41#2 = #42#2;
     #41#3 = #42#3;
     |LEE 001#41=;
     |SI FSalida = 0;
         #900#65 = #41#4;
         |ERROR10;
     |FINSI;
     |CIERRA #41;

 |FINSI;
|FPRC;

|DEFBUCLE BElemento;
 #42#1;
 5, 14,15,16;
 enEmpresa, "        ", 00, 001, #0#1, #camaniva#0, #camaniva#2, #camaniva#3;
 enEmpresa, "zzzzzzzz", 99, 999, #0#1, #camaniva#0, #camaniva#2, #camaniva#3;
 b;
 NULL, BElemento0;
|FIN;

|| *************************************************************************
|| *************************************************************************
|PROCESO VariasLineas;
 SwLinea = 0;
 |PARA nPara1 = 23; |SI nPara1 [ 27; |HACIENDO nPara1 = nPara1 + 1;
       nLBase = #900nPara1;
       nNume1 = nPara1 +  6; nLTipoI = #900nNume1;
       nNume2 = nPara1 + 11; nLCuoI  = #900nNume2;
       nNume3 = nPara1 + 17; nLTipoR = #900nNume3;
       nNume4 = nPara1 + 22; nLCuoR  = #900nNume4;
       |SI nLCuoI = 0; nLTipoI = 0; |FINSI;
       |SI nLCuoR = 0; nLTipoR = 0; |FINSI;
       |SI nLBase ! 0; |O nLCuoI ! 0; |O nLCuoR ! 0;
           |PRINT;
           SwLinea = 1;
       |FINSI;
 |SIGUE;
|FPRC;

|PROCESO LinLibro0;
  aAlfa1 = #caivalin#30; |QBF aAlfa1; |SI aAlfa1 = ""; #caivalin#30 = #camaniva#10; |FINSI;
  |SI enActividad ! 0;      ||quiere separar por actividades
      |SI aMulDep = "S";
          nElDepar = #caivalin#30;
          |SI nElDepar = 0;
              nElDepar = 1;
          |FINSI;
          |SI enActividad ! nElDepar; nAlNo = 1; |ACABA; |FINSI;
      |FINSI;
  |FINSI;

  nAlSi = 1;
  |SI nOpMdep = 0;  |ACABA;  |FINSI;  || Solo comprueba que exista

 |SI enSwSelCp = 1;
     enConcepto = #caivalin#3;
     |HAZ CtrlConcepto;
     |SI enSwConcep = 1;
         |ACABA;
     |FINSI;
 |FINSI;

 SwLinea = 1;
 #caivalin#6  = (#caivalin#6  / enConversor) ! EURODBMOD;
 #caivalin#8  = (#caivalin#8  / enConversor) ! EURODBMOD;
 #caivalin#10 = (#caivalin#10 / enConversor) ! EURODBMOD;

 |SI #caivalin#7 = 0;               ||  0%
     #900#27 = #900#27 + #caivalin#6;
     #900#28 = #900#28 + #caivalin#6;
 |SINO;
     #900#28 = #900#28 + #caivalin#6;
     #900#39 = #900#39 + #caivalin#8;
     |SI #caivalin#7 = #900#29;         ||  4%
         #900#23 = #900#23 + #caivalin#6;
         #900#34 = #900#34 + #caivalin#8;
     |SINO;
         |SI #caivalin#7 = #900#30;           ||  7%
             #900#24 = #900#24 + #caivalin#6;
             #900#35 = #900#35 + #caivalin#8;
         |SINO;
             |SI #caivalin#7 = #900#31;           || 16%
                 #900#25 = #900#25 + #caivalin#6;
                 #900#36 = #900#36 + #caivalin#8;
             |SINO;
                 #900#26 = #900#26 + #caivalin#6;   || ??%
                 #900#37 = #900#37 + #caivalin#8;
             |FINSI;
         |FINSI;
     |FINSI;
  |FINSI;

 |SI #caivalin#9 ! 0;
     #900#50 = #900#50 + #caivalin#10;
     |SI #caivalin#9 = #900#40;        || 0.5%
         #900#45 = #900#45 + #caivalin#10;
     |SINO;
         |SI #caivalin#9 = #900#41;    || 1.5%
             #900#46 = #900#46 + #caivalin#10;
         |SINO;
             |SI #caivalin#9 = #900#42; || 1.75%
                 #900#47 = #900#47 + #caivalin#10;
             |SINO;
                 #900#48 = #900#48 + #caivalin#10; || %
             |FINSI;
         |FINSI;
     |FINSI;
 |FINSI;

 #900#51 = #900#51 + #caivalin#6 + #caivalin#8 + #caivalin#10;


|| ... Graba Resumen 1 por Conceptos
 #901#0 = #caivalin#3;
 #901#5 = #camaniva#36;
 |LEE 101#901=;
 |SI FSalida ! 0;
     |DDEFECTO #901;
     #901#0 = #caivalin#3;
     #901#5 = #camaniva#36;
     |GRABA 020#901b;
 |FINSI;
 #901#1 = #901#1 + #caivalin#6;
 #901#2 = #901#2 + #caivalin#8;
 #901#3 = #901#3 + #caivalin#10;
 #901#4 = #901#4 + (#caivalin#6 + #caivalin#8 + #caivalin#10);

 #901#5 = #camaniva#36;
 |GRABA 020#901a;
 |LIBERA #901;
|FPRC;

|DEFBUCLE LinLibro;
 #caivalin#1;
 17,5;
 #camaniva#0,#camaniva#1,001, "S", "S";
 #camaniva#0,#camaniva#1,999, "S", "S";
 ;
 NULL, LinLibro0;
|FIN;

|PROCESO Facturas;

   |SI enActividad ! 0;      ||quiere separar por actividades
       |SI aMulDep ! "S";
           nElDepar = #304#10;
           |SI nElDepar = 0;
               nElDepar = 1;
           |FINSI;
           |SI enActividad ! nElDepar; |ACABA; |FINSI;
       |SINO;
            nOpMdep = 0;
            nAlSi   = 0;
            nAlNo   = 0;
            |BUCLE LinLibro;
            |SI nAlSi = 0; |ACABA; |FINSI;
       |FINSI;
   |FINSI;

   nOpMdep = 1;
   |PDEFECTO #900, 16, 64;
   #900#65 = "";

   #900#16 = #camaniva#2;
   #900#17 = nFactura;

   fLaFecha = #camaniva#4; |SI #0#44 = 2; fLaFecha = #camaniva#8; |FINSI;
   aAlfa1   = fLaFecha; aAlfa1   = aAlfa1 % 105;
   #900#18  = aAlfa1;
   #900#19  = #camaniva#12;
   #900#20  = #camaniva#13;
   #900#21  = #camaniva#14;
   #900#22  = #camaniva#28;
   #900#79  = fLaFecha;

   SwLinea = 0;
   |BUCLE LinLibro;
   |SI SwLinea = 0;  |ACABA; |FINSI; || No hay lineas de Inversion

   |BUCLE BElemento;

   |SI fLaFecha ] #0#41;
       |HAZ VariasLineas;
       SwHayDatos = 1;
       SwPrim     = 1;
   |FINSI;
   nFactura = nFactura + 1;
|FPRC;

|DEFBUCLE Facturas1;
 #camaniva#5;
 36;
 fDFecha, #0#39, 0000000001, aDTipo;
 fHFecha, #0#40, 9999999999, aHTipo;
 ;
 NULL, Facturas;
|FIN;

|DEFBUCLE Facturas2;
 #camaniva#1;
 8,36;
 #0#39,0000000001, fDFecha, aDTipo;
 #0#40,9999999999, fHFecha, aHTipo;
 ;
 NULL,NULL,NULL,NULL,NULL,Facturas;
 #camaniva#8, #camaniva#0, #camaniva#1;
|FIN;

|PROCESO Emision;
   enEmpresa  = #999#1;
   enPerConta = #999#2;
   eaLaMonEmp = #999#13;
   |HAZ EnConversor;
   enEjer     = #999#3;   || A¤o
   dig1       = #999#4;   || Desde Mes
   dig3       = #999#5;   || N§ Digitos
   dig4       = #999#6;   || Digitos Nivel 1
   dig5       = #999#7;   || Digitos Nivel 2
   dig6       = #999#8;   || Digitos Nivel 3
   dig7       = #999#9;   || Digitos Nivel 4
   dig8       = #999#10;  || Digitos Nivel 5
   dig9       = #999#11;  || Digitos Nivel 6
   eaEstadoEmp = #999#12; || Estado
   eaNombreEmp = #999#14; || Nombre Empresa
   aPathConta  = #999#15; |QBF aPathConta; || Path
   || Recoger informacion sobre los niveles ...
   |SI dig3 = 1; MaximoDigitos = dig4; |FINSI;
   |SI dig3 = 2; MaximoDigitos = dig5; |FINSI;
   |SI dig3 = 3; MaximoDigitos = dig6; |FINSI;
   |SI dig3 = 4; MaximoDigitos = dig7; |FINSI;
   |SI dig3 = 5; MaximoDigitos = dig8; |FINSI;
   |SI dig3 = 6; MaximoDigitos = dig9; |FINSI;

   eaTituloL = #0#52; ||Titulo del Libro
   |SI #0#37 = "S";
       |SUB_EJECUTA_NP ":basica/agiy0016";
   |FINSI;

   |PATH_DAT #301 aPathConta;
   |PATH_DAT #304 aPathConta;
   |PATH_DAT #305 aPathConta;
   |PATH_DAT #306 aPathConta;
   |ABRE #caivaasi;
   |DDEFECTO #caivaasi;
   |LEE 000#caivaasi.p;
   |CIERRA #caivaasi;
   aMulDep = #caivaasi#132;

   |DDEFECTO #306;  ||Configuracion de Informes
   |ABRE #306;
   #306#0 = 1;
   |LEE 000#306=;
   |CIERRA #306;

   nDActividad = 0; nHActividad = 0;
   |SI #0#55 = "S"; nDActividad = #0#56; nHActividad = #0#57;  |FINSI;

   |PARA enActividad = nDActividad; |SI enActividad [ nHActividad; |HACIENDO enActividad = enActividad + 1;

         |HAZ LeeDatosEmpresa;
         |SI  swNoExis = 0;       ||SI existe esta Actividad.

              informe = "dsmoy011";
              |SI #306#4 = "N"; informe = "drmoy011"; |FINSI;
              |SI #0#43 = 2;
                  informe = "dsmoya11";
              |FINSI;

              |INFOR informe;
              |SI FSalida ! 0;  |ACABA;  |FINSI;

              SwHayDatos = 0;
              SwPrim     = 0;
              fDFecha = #0#41;
              fHFecha = #0#42;
              nPagina = #0#51;
              nPagDup = nPagina -1;
              nSwPagDup = 0;
              nFactura = 1;

              |ABRE #901;
              |SI #0#44 = 1; |BUCLE Facturas1; |FINSI;
              |SI #0#44 = 2; |BUCLE Facturas2; |FINSI;
              |CIERRA #901;

              |SI SwHayDatos = 1;
                  |PIEINF;
                  |FININF;
                  |SI #0#38 = "S"; |HAZ Resumen1; |FINSI;
              |SINO;
                  |FININF;
              |FINSI;
         |FINSI;
     |SIGUE;
|FPRC;

|DEFBUCLE dsmow043;
 #dsmow043#1;
 16;
 000001, "S";
 999999, "S";
 ;
 NULL, Emision;
|FIN;

|PROCESO Imprimir;

    |MENAV "0000 Recuerde seleccionar el papel de la impresora en Horizontal";

    |IMPRE 0;
    |SI FSalida ! 0;  |ACABA;  |FINSI;

    aDTipo = "R"; aHTipo = "S";
    |SI #0#0 ! "I";  aDTipo = #0#0; aHTipo = #0#0; |FINSI;

    |BUCLE dsmow043;
    |FINIMP;
|FPRC;

|PROCESO LeeDatosEmpresa;
  |DELINDEX #901;

  |DDEFECTO #900;
  eaInac = #0#24;
  eaTituloL = #0#52;
  efDFecha  = #0#41;
  efHFecha  = #0#42;
  enEjer = #0#1;                                   || Ejercicio
  #900#0 = enEmpresa;
  |HAZ DatosEmpr_w30;
|FPRC;

|PROCESO dsmow031;
 #30#0 = #901#0;
 |LEE 001#30=;
 |SI FSalida ! 0;
     |DDEFECTO #30;
 |FINSI;
 |PRINT;
  sw = 1;
|FPRC;

|DEFBUCLE dsmow031;
 #901#1;
 ;
 asTipo, 001;
 asTipo, 999;
 ;
 NULL, dsmow031;
|FIN;

|PROCESO Resumen1;
   |INFOR "dsmoyr08";
   |SI FSalida ! 0;  |ACABA;  |FINSI;

   |ABRE #30;
   sw = 0;
   aTipo = "DE COMPRA (INVERSION)";
   asTipo = "S";
   |BUCLE dsmow031;
   |SI sw = 1; |PIEINF; |FINSI;

   sw = 0;
   aTipo = "DE VENTA (INVERSION)";
   asTipo = "R";
   |BUCLE dsmow031;
   |SI sw = 1; |PIEINF; |FINSI;

   |CIERRA #30;

   |FININF;
|FPRC;


|PROGRAMA;

 |DDEFECTO #0;
 enModelo = #0#49;

 |ABRE #0;
 |LEE 101#0p;
 |SI FSalida ! 0;
     |DDEFECTO #0;
     |GRABA 020#0b;
 |FINSI;

 |CLS;
 |PINPA #0#0;
 |PINDA #0#0;

 |ENDATOS #1#0;
 |SI FSalida = 0;
     |GRABA 020#0a;
 |FINSI;
 |LIBERA #0;
 |CIERRA #0;

 |PULSATECLA;
|FPRO;
