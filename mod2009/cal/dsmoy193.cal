|FICHEROS;
     dsmoy000 #0;
     dsmom001 #1;
     dsmom004 #4;
     dsmom107 #107;

     dsm193ca #193;
     dsm193li #194;
     dsm193ga #195;

     dsmow014 #999;

     dsmow522 #522;
     dsmow525 #525;
     dsmow562 #562;
     dsmow563 #563;
     dsmow564 #564;

     :/basica/def/agicen17 #17;
     :/basica/def/agim0024 #24;
     :/basica/def/agim0026 #26;

     &regisnif@ #709;
     &clientes@ #715;
|FIN;

|VARIABLES;
     aNombre     = "";
     aPathAfegir = "";
     aIPRemoto   = "";
     aBue        = "";
|FIN;

|INCLUYE i_varemi;

|PROCESO Registro193_1;
     enCodCli = #193#0;
     |HAZ LeePersona;

     |DDEFECTO #562;

     #562#0  = "1";

     #562#1  = "193";

     #562#2  = #193#1;

     |SI enSwDeDonde = 1;
         eaVarDni = #715#3;  |HAZ CalculameDNI;
         #562#3   = eaVarDni;

         eaAlfa   = #715#1;  |HAZ QuitaRaros;
         #562#4   = eaAlfa;

         eaAlfa   = #715#1;  enLinea = 2;  |HAZ LeeTelefono;  |QBF eaAlfa;
         |SI eaAlfa = "";
             eaAlfa   = #715#1;  enLinea = 1;  |HAZ LeeTelefono;
         |FINSI;
         |HAZ QuitaRarosTfno;
         #562#6   = eaAlfa;

         eaAlfa   = #715#53;  |HAZ QuitaRaros;
         #562#7   = eaAlfa;

         eaDeleg     = #715#17;
         eaAdmin     = #715#18;
         eaProvincia = #715#38;
     |SINO;
         eaVarDni = #715#13;  |HAZ CalculameDNI;
         #562#3   = eaVarDni;

         eaAlfa   = #715#1;   |HAZ QuitaRaros;
         #562#4   = eaAlfa;

         eaAlfa   = #715#14;  |HAZ QuitaRarosTfno;
         #562#6   = eaAlfa;

         eaAlfa   = #715#2;   |HAZ QuitaRaros;
         #562#7   = eaAlfa;

         eaDeleg     = #715#91;
         eaAdmin     = #715#92;
         eaProvincia = #715#12;
     |FINSI;

     #562#5  = "T";

     #562#8 = "1730000000000";

     nNume   = #193#6;
     #562#12 = ("000000000" + nNume) % -109;

     enNume  = #193#7;  |HAZ Conver15Signo;
     #562#13 = eaAlfa;

     enNume  = #193#8;  |HAZ Conver15Signo;
     #562#14 = eaAlfa;

     enNume  = #193#9;  |HAZ Conver15Signo;
     #562#15 = eaAlfa;

     enNume  = #193#10; |HAZ Conver15Signo;
     #562#16 = eaAlfa;

     enNume  = #193#11; |HAZ Conver15Signo;
     #562#17 = eaAlfa;

     enNume  = #193#12; |HAZ Conver15Signo;
     #562#18 = eaAlfa;

     |SI #193#15 = "S";  #562#19 = "S";  |FINSI;

     |SI #193#14 = "C";  #562#9  = "C";  |FINSI;

     |SI #193#14 = "S";
         #562#10 = "S";
         #562#11 = #193#13;
     |FINSI;

     #562#21 = &13 + &10;

     |SI nTecla ! 5;
         aAlfa = "";
         |PARA nCampo = 0;  |SI nCampo [ 21;  |HACIENDO nCampo = nCampo + 1;
               aAlfa = #562nCampo;
               |XGRABA nHandle2, aAlfa;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO Registro193_2;
     enCodCli = #193#0;
     |HAZ LeePersona;

     |DDEFECTO #563;

     #563#0  = "2";

     #563#1  = "193";

     #563#2  = #193#1;

     |SI enSwDeDonde = 1;
         eaVarDni = #715#3;  |HAZ CalculameDNI;
         #563#3   = eaVarDni;
     |SINO;
         eaVarDni = #715#13;  |HAZ CalculameDNI;
         #563#3   = eaVarDni;
     |FINSI;

     eaVarDni = #194#6;  |HAZ CalculameDNI;
     |SI #194#11 = "N";
         #563#4 = eaVarDni;
     |SINO;
         #563#5 = eaVarDni;
     |FINSI;

     |SI #194#6  = "999999999999";
         #563#4  = "999999999";
         #563#5  = "999999999";
         #563#16 = "X";
     |FINSI;

     eaAlfa = #194#12;  |HAZ QuitaRaros;
     #563#6 = eaAlfa;

     |SI #194#13 = "S";  #563#7 = "X";  |FINSI;

     #563#8  = ("00" + #194#14) % -102;
     #563#9  = #194#15;
     #563#10 = #194#16;
     #563#11 = #194#8;
     #563#12 = #194#9;
     #563#13 = #194#17;
     #563#14 = #194#18;
     #563#15 = #194#19;
     #563#17 = ("0000" + #194#7) % -104;
     #563#18 = #194#10;

     enNume  = #194#20;  |HAZ Conver13Signo;
     #563#19 = eaAlfa;

     #563#20 = ("000" + #194#21) % -103;

     enNume  = #194#22;  |HAZ Conver13Signo;
     #563#21 = eaAlfa;

     enNume  = #194#23;  |HAZ Conver13Signo;
     #563#22 = eaAlfa;

     nNume   = #194#24 * 100;
     #563#23 = ("0000" + nNume) % -104;

     enNume  = #194#25;  |HAZ Conver13Signo;
     #563#24 = eaAlfa;

     enNume  = #194#27;  |HAZ Conver13Signo;
     #563#25 = eaAlfa;

     |SI #193#15 = "S";  #563#27 = "S";  |FINSI;

     |SI #0#6 [ 2003;
         #563#28 = "";
     |SINO;
         |SI #194#18 ! "P";
             #563#28 = "0000000000000000000000000000000000000000";
         |SINO;
             aAlfa1 = (#194#29 % -104) + (#194#29 % 0402) + (#194#29 % 102);
             aAlfa2 = (#194#30 % -104) + (#194#30 % 0402) + (#194#30 % 102);

             enNume = #194#31;  |HAZ Conver12Signo;
             aAlfa3 = eaAlfa;

             enNume = #194#32;  |HAZ Conver12Signo;
             aAlfa4 = eaAlfa;

             #563#28 = aAlfa1 + aAlfa2 + aAlfa3 + aAlfa4;
         |FINSI;
     |FINSI;

     #563#29 = &13 + &10;

     |SI nTecla ! 5;
         aAlfa = "";
         |PARA nCampo = 0;  |SI nCampo [ 29;  |HACIENDO nCampo = nCampo + 1;
               aAlfa = #563nCampo;
               |XGRABA nHandle2, aAlfa;
         |SIGUE;
     |SINO;
         |PRINT;
         nSwPrint = 1;
     |FINSI;
|FPRC;

|DEFBUCLE dsm193li;
     #194#1;
     ;
     #193#0,#193#1,#193#2,#193#3,#193#4,#193#5,"            ",0000," ","  ",1;
     #193#0,#193#1,#193#2,#193#3,#193#4,#193#5,"zzzzzzzzzzzz",9999,"z","zz",2;
     ;
     NULL, Registro193_2;
|FIN;

|PROCESO Registro193_3;
     enCodCli = #193#0;
     |HAZ LeePersona;

     |DDEFECTO #564;

     #564#0  = "2";

     #564#1  = "193";

     #564#2  = #193#1;

     |SI enSwDeDonde = 1;
         eaVarDni = #715#3;  |HAZ CalculameDNI;
         #564#3   = eaVarDni;
     |SINO;
         eaVarDni = #715#13;  |HAZ CalculameDNI;
         #564#3   = eaVarDni;
     |FINSI;

     eaVarDni = #195#6;  |HAZ CalculameDNI;
     |SI #195#7 = "N";
         #564#4 = eaVarDni;
     |SINO;
         #564#5 = eaVarDni;
     |FINSI;

     eaAlfa = #195#8;   |HAZ QuitaRaros;
     #564#6 = eaAlfa;

     enNume  = #195#9;  |HAZ Conver13Signo;
     #564#8  = eaAlfa;

     #564#10 = &13 + &10;

     |SI nTecla ! 5;
         aAlfa = "";
         |PARA nCampo = 0;  |SI nCampo [ 10;  |HACIENDO nCampo = nCampo + 1;
               aAlfa = #564nCampo;
               |XGRABA nHandle2, aAlfa;
         |SIGUE;
     |SINO;
         |PRINT;
         nSwPrint = 1;
     |FINSI;
|FPRC;

|DEFBUCLE dsm193ga;
     #195#1;
     ;
     #193#0,#193#1,#193#2,#193#3,#193#4,#193#5,"            ";
     #193#0,#193#1,#193#2,#193#3,#193#4,#193#5,"zzzzzzzzzzzz";
     ;
     NULL, Registro193_3;
|FIN;

|PROCESO BuscaCargo193;
     eaAlfa  = #26#4;  |HAZ QuitaRaros;
     #525#13 = eaAlfa;

     eaAlfa  = #26#17;  |HAZ QuitaRaros;
     #525#14 = eaAlfa;
|FPRC;

|DEFBUCLE agim0026;
     #26#1;
     16;
     #193#0, 01, 01, 3;
     #193#0, 99, 99, 3;
     ;
     NULL, BuscaCargo193;
|FIN;

|PROCESO BuscaCargo193R;
     eaAlfa  = #24#4;  |HAZ QuitaRaros;
     #525#13 = eaAlfa;

     eaAlfa  = "REPRESENTANTE";  |HAZ QuitaRaros;
     #525#14 = eaAlfa;

     nHayRepre = 1;
     |ERROR10;
|FPRC;

|DEFBUCLE agim0024;
     #24#2;
     ;
     #193#0, 99, 01;
     #193#0, 01, 99;
     ;
     NULL, BuscaCargo193R;
|FIN;

|PROCESO BuscaComuneros193;
     aAlfa = "31.12." + #193#1;
     |SI #1#8 > aAlfa;  |ACABA;  |FINSI;

     aAlfa = "01.01." + #193#1;
     |SI #1#9 ! "01.01.0000";
         |SI #1#9 < aAlfa;  |ACABA;  |FINSI;
     |FINSI;

     nHayRepre = 1;

     enCodCli = #1#3;
     |HAZ LeePersona;

     eaAlfa  = #715#1;           |HAZ QuitaRaros;   #525#13 = eaAlfa;
     eaAlfa  = "REPRESENTANTE";  |HAZ QuitaRaros;   #525#14 = eaAlfa;

     |ERROR10;
|FPRC;

|DEFBUCLE dsmom001;
     #1#1;
     ;
     #193#0, 01, 0001;
     #193#0, 99, 9999;
     ;
     NULL, BuscaComuneros193;
|FIN;

|PROCESO Lee1931;        || Creacion de Ficheros
     |ABRE #4;
     #4#0  = #999#0;
     #4#1  = #999#1;
     #4#2  = #999#2;
     #4#3  = #999#5;
     #4#4  = #999#7;
     |LEE 040#4=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;
     |CIERRA #4;

     |ABRE #193;
     #193#0  = #999#0;
     #193#1  = #999#1;
     #193#2  = #999#2;
     #193#3  = #999#5;
     #193#4  = #999#7;
     #193#5  = 0;
     |LEE 040#193=;
     |SI FSalida = 0;
         |SI #0#6 [ 2003;
             aAbre = eaPathEmision + "RC" + (("00000" + #999#0) % -105) + ".TXT";
         |SINO;
             aAbre = eaPathEmision + "RCA" + (("00000" + #999#0) % -105) + ".TXT";
         |FINSI;
         |XABRE "BW", aAbre, nHandle2;
     |FINSI;

     |HAZ Registro193_1;

     |BUCLE dsm193li;
     |BUCLE dsm193ga;

     |XCIERRA nHandle2;

     |SI #0#6 [ 2003;
         eaFOrigen  = eaPathEmision + "RC" + (("00000" + #999#0) % -105) + ".TXT";
         eaFDestino = eaPathLanza   +  "RC" + (("00000" + #999#0) % -105) + ".TXT";
     |SINO;
         eaFOrigen  = eaPathEmision + "RCA" + (("00000" + #999#0) % -105) + ".TXT";
         eaFDestino = eaPathLanza   + "RCA" + (("00000" + #999#0) % -105) + ".TXT";
     |FINSI;

     |HAZ CopiaFichero;
     |FBORRA eaFOrigen;

     |SI #0#6 [ 2005;
         aAlfa = "wait VI193 /E:RCA" + (("00000" + #999#0) % -105) + ".TXT ";
         aAlfa = aAlfa + "/R:RCC" + (("00000" + #999#0) % -105) + ".TXT ";
         aAlfa = aAlfa + &13 + &10;
         |XGRABA nHandle1, aAlfa;
     |SINO;
         eaTipoE =  "RCA" + (("00000" + #999#0) % -105) + ".TXT";
         eaTipoR =  "RCC" + (("00000" + #999#0) % -105) + ".TXT";
         eaTipoV =  "S";

         |HAZ LineaShellJava;
     |FINSI;
     |CIERRA #193;
|FPRC;

|PROCESO Auxiliar193;
     enCodCli = #193#0;
     |HAZ LeePersona;

     |DDEFECTO #525;

     #525#0  = "1";

     #525#1  = "193";

     #525#2  = #193#1;

     |SI enSwDeDonde = 1;
         eaVarDni = #715#3;  |HAZ CalculameDNI;
         #525#3   = eaVarDni;

         #525#4   = ("00" + #715#15) % -102;

         #525#5   = ("000" + #715#16) % -103;

         #525#6   = #715#29;

         eaAlfa   = #715#30;  |HAZ QuitaRaros;
         #525#7   = eaAlfa;

         nNume    = #715#31;
         #525#8   = ("00000" + nNume) % -105;

         #525#9   = #715#37;

         #525#10  = ("00" + #715#35) % -102;

         #525#11  = (("00" + #715#35) % -102) + (("000" + #715#36) % -103);
     |SINO;
         eaVarDni = #715#13;  |HAZ CalculameDNI;
         #525#3   = eaVarDni;

         #525#4   = ("00" + #715#63) % -102;

         #525#5   = ("000" + #715#64) % -103;

         #525#6   = #715#3;

         eaAlfa   = #715#4;  |HAZ QuitaRaros;
         #525#7   = eaAlfa;

         nNume    = #715#5;
         #525#8   = ("00000" + nNume) % -105;

         #525#9   = #715#11;

         #525#10  = ("00" + #715#9) % -102;

         #525#11  = (("00" + #715#9) % -102) + (("000" + #715#10) % -103);
     |FINSI;

     #525#12  = (#0#7 % -104) + (#0#7 % 402) + (#0#7 % 102);

     |SI #107#1 = "S";
         |BUCLE agim0026;
     |SINO;
         nHayRepre = 0;
         |BUCLE agim0024;
         |SI nHayRepre = 0;
             |BUCLE dsmom001;
             |SI nHayRepre = 0;
                 eaAlfa  = #999#3;  |HAZ QuitaRaros;
                 #525#13 = eaAlfa;

                 eaAlfa  = "TITULAR";  |HAZ QuitaRaros;
                 #525#14 = eaAlfa;
             |FINSI;
         |FINSI;
     |FINSI;

     #525#15 = ("00000" + #999#0) % -105;
|FPRC;

|PROCESO Lee193P;       || Emision Papel
     |ABRE #4;
     #4#0  = #999#0;
     #4#1  = #999#1;
     #4#2  = #999#2;
     #4#3  = #999#5;
     #4#4  = #999#7;
     |LEE 040#4=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;
     |CIERRA #4;

     |ABRE #193;
     #193#0  = #999#0;
     #193#1  = #999#1;
     #193#2  = #999#2;
     #193#3  = #999#5;
     #193#4  = #999#7;
     #193#5  = 0;
     |LEE 040#193=;
     |SI FSalida = 0;
         |HAZ Auxiliar193;
         |HAZ Registro193_1;
         enSwImpre = 1;  |BUCLE dsm193li;
         enSwImpre = 2;  |BUCLE dsm193ga;
         |SI nSwPrint = 0;
             |DDEFECTO #194;
             |PRINT;
         |FINSI;
         |PIEINF;
     |FINSI;
     |CIERRA #193;
|FPRC;

|PROCESO QueHacemos193;
     |SI nTeclaPapel ! 0;  |O nTecla = 5;   || En Papel Continuo
         |HAZ Lee193P;
         |ACABA;
     |FINSI;

     |HAZ Lee1931;          || Creacion de los Ficheros Planos.
|FPRC;

|DEFBUCLE dsmow014C193;
     #999#1;
     6;
     00000, 0000, 00, "S";
     99999, 9999, 99, "S";
     ;
     NULL, QueHacemos193;
|FIN;

|DEFBUCLE dsmow014N193;
     #999#2;
     6;
     "                                        ", "S";
     "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz", "S";
     ;
     NULL, QueHacemos193;
|FIN;

|PROCESO Boton1931;
     |PTEC 824;
|FPRC;

|PROCESO Boton1932;
     |PTEC 825;
|FPRC;

|PROCESO Boton1933;
     |PTEC 826;
|FPRC;

|PROCESO Boton1934;
     |PTEC 827;
|FPRC;

|PROCESO Boton1935;
     |PTEC 828;
|FPRC;

|PROCESO Boton1936;
     |PTEC 806;
|FPRC;

|PROCESO PapelContinuo193;
     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI nTeclaPapel = 0;
         |INFOR "m193co01";
         |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;
     |FINSI;

     |SI #0#8 = "C";
         |BUCLE dsmow014C193;
     |SINO;
         |BUCLE dsmow014N193;
     |FINSI;

     |FININF;
     |FINIMP;
|FPRC;

|PROCESO PapelOficial193;
     nTeclaPapel = 0;
     |HAZ PapelContinuo193;
|FPRC;

|PROCESO Botones193_2003;
     |BLANCO 0501 2380;
     |CUADRO 0501 2380;

     |ATRI I;
     |PINTA 0602, "Hacienda no ha proporcionado un programa para la emision en laser o";
     |PINTA 0702, "prevalidacion del modelo 193.";

     |PINTA 0902, "Para poder emitir o prevalidar deberemos bajarnos el programa que nos";
     |PINTA 1002, "proporciona Hacienda en su pagina web (www.aeat.es) en el apartado de";
     |PINTA 1102, "Descarga de Programas/Declaraciones Informativas/Modelo 193-2003 y ";
     |PINTA 1202, "con los ficheros generados en nuestro programa, importar los ficheros";
     |PINTA 1302, "desde el programa de Hacienda, para su emision o prevalidacion.";

     |PINTA 1502, "Emitiendo el informe de comprobacion podemos obtener los datos que nos";
     |PINTA 1602, "pudieran hacer falta.                                                 ";

     |ATRI i;

     |CONTROL_SIMPLE 0, "LBOTON, <F2> Generacion Fichero ", 1813, 1954, Boton1931;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON, <F5> Informe de Comprobacion              ", 2013, 2154, Boton1934;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,ESC Cancelar", 2165, 2178, Boton1936;
     nBoton3 = FSalida;

     aF2 = &255 + "824";
     aF5 = &255 + "827";
     aF7 = &255 + "806";

     |PARA; |SI;  |HACIENDO;
         |LEETECLA aTecla;
         |SI aTecla = aF2;  nTecla = 1;   |SAL;  |FINSI;
         |SI aTecla = aF5;
             nTecla = 5;
             |HAZ PapelOficial193;
         |FINSI;
         |SI aTecla = aF7;  nTecla = 0;   |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;

     |PULSATECLA;
|FPRC;

|PROCESO Botones193_2004;
     |BLANCO 0501 2380;

     |CONTROL_SIMPLE 0, "LBOTON, <F2> Generacion Fichero para Internet", 0513, 0754, Boton1931;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON, <F5> Informe de Comprobacion              ", 0913, 1154, Boton1934;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,ESC Cancelar", 2165, 2178, Boton1936;
     nBoton3 = FSalida;

     aF2 = &255 + "824";
     aF5 = &255 + "827";
     aF7 = &255 + "806";

     |PARA; |SI;  |HACIENDO;
         |LEETECLA aTecla;
         |SI aTecla = aF2;  nTecla = 1;   |SAL;  |FINSI;
         |SI aTecla = aF5;
             nTecla = 5;
             |HAZ PapelOficial193;
         |FINSI;
         |SI aTecla = aF7;  nTecla = 0;   |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;

     |PULSATECLA;
|FPRC;

|PROCESO Proceso193_2003;
     |SI #0#8 = "C";
         |BUCLE dsmow014C193;
     |SINO;
         |BUCLE dsmow014N193;
     |FINSI;

     |SI nTecla = 1;
         aAlfa = "0000 Los fichero generados esta en el Directorio " + eaPathLanza + " de su PC.";
         |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO CreaVarios;
     aIPRemoto   = "";
     |IP_REMOTO aIPRemoto;

     aPathInter  = eaPathLanza + "AInternet";
     aPathInter  = aPathInter + "/";
     aFichero    = aPathInter + "*.bue";
     |R_PDIR aFichero;
     |PARA; |SI; |HACIENDO;
            |SI FSalida ! 0; |SAL; |FINSI;

            aBue     = aFichero - aPathInter;
            aDestino = eaPathInternet + "193" + (aBue % 405) + ".txt";

            |RCOPIA_FICHERO aFichero, aDestino;

            |RFBORRA aFichero;

            |R_SDIR aFichero;
     |SIGUE;

     aAlfa =  "      Los ficheros generados estan: " + eaPathInternet + " de su PC";
     |MENAV aAlfa;
|FPRC;

|PROCESO Internet2004;
     eaFichError  = "rcc";
     eaFichDatos  = "rca";
     eaFichAuxil  = "";
     enValidacion = 1;

     |HAZ BuscaErroresL;

     |SI enBuenos = 0;
         |MENAV "      Ha habido Errores, arreglelos y vuelva a emitir.";
         |ACABA;
     |FINSI;

     |HAZ CreaVarios;
|FPRC;

|PROCESO EmiteEsto193;
     aAlfa = "DEL *.DLL" + &13 + &10;
     |XGRABA nHandle1, aAlfa;

     aAlfa = "DEL *.TAB" + &13 + &10;
     |XGRABA nHandle1, aAlfa;

     aAlfa = "DEL *.WMF" + &13 + &10;
     |XGRABA nHandle1, aAlfa;

     aAlfa = "DEL ERROR193.TXT"     + &13 + &10;
     |XGRABA nHandle1, aAlfa;

     aAlfa = "DEL *.EXE"         + &13 + &10;
     |XGRABA nHandle1, aAlfa;

     |XCIERRA nHandle1;

     |DBASS aModelos;  |QBT aModelos;
     aModelos = aModelos + "basica/";

     |SI #0#6 = 2004;
         eaFOrigen  = aModelos + "patrones/zpr04193.exe";
         eaFDestino = eaPathLanza + "zpr04193.exe";
         |HAZ CopiaFichero;
     |FINSI;

     aAlfa = eaPathLanza + "IMPRIME";

     |RSYSTEM aAlfa;

     |HAZ Internet2004;
|FPRC;

|PROCESO AbreImprime193;
     aAbre = eaPathLanza + "imprime.bat";
     |XABRE "CBW", aAbre, nHandle1;

     aAlfa = "CD " + eaPathLanza + &13 + &10;    |XGRABA nHandle1, aAlfa;

     |SI #0#6 = 2004;
         aAlfa = "ZPR04193.EXE -Y" + &13 + &10;  |XGRABA nHandle1, aAlfa;
     |FINSI;
|FPRC;

|PROCESO Proceso193_2004;
     |HAZ AbreImprime193;

     |SI #0#8 = "C";
         |BUCLE dsmow014C193;
     |SINO;
         |BUCLE dsmow014N193;
     |FINSI;

     |SI #0#6 [ 2005;
         |HAZ EmiteEsto193;
     |SINO;
         |XCIERRA nHandle1;

         |SI eaTipoV ! "S";
             eaFichError  = "rcc";
             eaFichDatos  = "rca";
             eaFichAuxil  = "rcb";
             enValidacion = 0;

             |HAZ BuscaErroresL;
         |SINO;
             |HAZ Internet2004;
         |FINSI;
     |FINSI;
|FPRC;

|PROGRAMA;
     aFichero = ("000" + Usuario) % 108;
     |NOME_DAT #0 aFichero;

     aFichero = ("imp" + Usuario) % 108;
     |NOME_DAT #999 aFichero;

     |ABRE #0;
     |LEE 000#0p;
     |CIERRA #0;

     |ABRE #107;
     |LEE 040#107p;
     |SI FSalida ! 0;
         |CIERRA #107;
         |SUB_EJECUTA_NP "dsmom107";
         |ABRE #107;
     |FINSI;

     |LEE 040#107p;
     |SI FSalida ! 0;  |DDEFECTO #107;  |FINSI;
     |CIERRA #107;

     |SI #0#6 = 2004;
         |DBASS aModelos;  |QBT aModelos;
         aModelos = aModelos + "basica/";
         aAlfa    = aModelos + "patrones/zpr04193.exe";
         |XABRE "E", aAlfa, nHandle1;
         |SI FSalida < 0;
             |MENAV "0000 No existe el Modulo de Hacienda zpr04193.exe, Instale ippatron.";
             |ACABA;
         |FINSI;
     |FINSI;

     enCambiaMoneda = 1;
     enSwTrata      = 0;

     |SI eaParametro ! "193C";
         |SI #0#6 [ 2003;  |HAZ Botones193_2003;  |FINSI;
         |SI #0#6 ] 2004;  |HAZ Botones193_2004;  |FINSI;
     |SINO;
         nTecla = 5;
     |FINSI;

     |SI nTecla = 0;  |ACABA;  |FINSI;

     |SI nTecla = 5;
         |HAZ PapelOficial193;
         |ACABA;
     |FINSI;

     |SI #0#6 [ 2003;
         |HAZ Proceso193_2003;
     |SINO;
         |HAZ Proceso193_2004;
     |FINSI;
|FPRO;
