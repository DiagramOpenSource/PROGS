|FICHEROS;
   dsmoy018 #0;

   dsmom060 #60;
   dsmom061 #61;
   dsmom100 #100;

   :/basica/def/agicen14 #114;
   :/basica/def/agifigen #118;
   :/basica/def/agim0019 #119;

   :/contagen/def/canempre #300;
   :/contagen/def/camaasil #301;
   :/contagen/def/camaniva #302;
   :/contagen/def/cacuenta #303;
   :/contagen/def/caconimp #306;  || configuracion impresiones

   dsmow055 #900;          ||Def de impresion
   dsmow033 #901;
   dsmow054 #902;
   dsmow043 #999,MANTE;
|FIN;

|VARIABLES;
   nSwSiHay = 0;
   aParam = "";
   &entrada = 1;
   &efDFecha  = @;
   &efHFecha  = @;

   aMonMod    = "";
   informe    = "";
   nInkey     = 0;
   aFichero   = "";
   nCampo     = 0;
   nDEmpresa  = 0;
   nHEmpresa  = 0;
   nDActividad = 0;
   nHActividad = 0;
   nLaEmpresa  = 0;
   nGTempo     = 0;

   nSPin1      = 0;
   aPath       = "";
   nEjercicio  = 0;

   aAlfa1 = "";
   aAlfa2 = "";
   aAlfa3 = "";
   nPara1 = 0;
   nPara2 = 0;
   nNume1 = 0;
   nNume2 = 0;
   nNume3 = 0;
   nNume4 = 0;

   SwHayDatos = 0;
   fDFecha   = @;
   fHFecha   = @;


   &nPagina  = 0;
   &SwPrim   = 0;
   &SwLinea  = 0;
   &eaTituloL = "";
   &eaTitul2  = "";

   nPin1    = 0;
   aPin1    = "";
   efPn10   = @;
   efPn11   = @;
   swExiste = 0;

   fLaFecha = @;
   aCta     = "";
   aDCta    = "";
   aHCta    = "";
   nSwCol   = 0;
   aSigno   = "";
   aNomCta  = "";
   aDesCon  = "";
   aCIF     = "";
   nConta   = 0;
   nImporte = 0;
   nEspDos  = 0;

   nElDepar = 0;
   swNoExis = 0;
|FIN;

|INCLUYE i_variar;
|INCLUYE i_varcon;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\
|| ***************************************************************
|CALCULO QueLibro; |TIPO 0;
 |SI #0#0 = "I";
     #0#44 = 910;
     #0#45 = "LIBRO REGISTROS DE VENTAS E INGRESOS";
     aAlfa1 = "Emision Libro Registro de Ventas e Ingresos";
 |SINO;
     #0#44 = 911;
     #0#45 = "LIBRO REGISTROS DE COMPRAS Y GASTOS";
     aAlfa1 = "Emision Libro Registro de Compras y Gastos";
 |FINSI;
 |PINTA 0631, "                                             ";
 |ATRI R; |PINTA 0631, aAlfa1; |ATRI 0;
 |PINTA #0#44;
 |PINTA #0#45;
|FCAL;

|CALCULO Eano; |TIPO 7;
 fLaFecha = ~;
 aAlfa1 = fLaFecha; aAlfa1 = aAlfa1 % -104;
 #0#1 = aAlfa1;
 |PINTA #0#1;
|FCAL;

|CALCULO losDefectos; |TIPO 0;
 aAlfa1 = #0#1;
 aAlfa2 = "01.01." + aAlfa1; #0#39 = aAlfa2; |PINTA #0#39;
 aAlfa2 = "31.12." + aAlfa1; #0#40 = aAlfa2; |PINTA #0#40;
|FCAL;

|CALCULO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#2  = enEmpresa;  |PINTA #0#2;
         #0#3  = enEmpresa;  |PINTA #0#3;

         |C_M #0#2, 1, "N";
         |C_M #0#3, 1, "N";
     |FINSI;
|FCAL;

|CALCULO Parrilla; |TIPO 0;
     |SI #0#2 = 0;
         #0#2 = 0;  |PINTA #0#2;
         #0#3 = 0;  |PINTA #0#3;  |C_M #0#3, 1, "N";
         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#2, 1, "S";
             |C_M #0#3, 1, "S";
         |FINSI;

         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas; |TIPO 0;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO YaSeCual; |TIPO 7;
 |SI enEmpresa ! 0;
     #0#48 = "S"; |PINTA #0#48;
     #0#24 = "S"; |PINTA #0#24;
     #0#25 = 00;  |PINTA #0#25;
     #0#26 = 99;  |PINTA #0#26;
     |C_M #0#24, 1, "N";
     |C_M #0#25, 1, "N";
     |C_M #0#26, 1, "N";
     |C_M #0#48, 1, "N";
 |FINSI;
|FCAL;

|CALCULO SelTipo0; |TIPO 0;
 |SI #0#48 = "N";
     #0#25 = 0;  |PINTA #0#25;  |C_M #0#25, 1, "N";
     #0#26 = 99;  |PINTA #0#26;  |C_M #0#26, 1, "N";
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |SINO;
     |SI enEmpresa = 0;
         |C_M #0#25, 1, "S";
         |C_M #0#26, 1, "S";
     |FINSI;
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "N";
           #0nCampo = 0;  |PINTA #0nCampo;
     |SIGUE;
 |FINSI;
|FCAL;

|CALCULO SelTipo1;
 |SI #0Campo = 0;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           #0nCampo = 0;  |C_M #0nCampo, 1, "N";
     |SIGUE;
 |SINO;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |FINSI;
|FCAL;

|CALCULO fecha; |TIPO 0;
 aAlfa1 = #0#1;
 aAlfa2 = "01.01." + aAlfa1;
 aAlfa3 = "31.12." + aAlfa1;
 |SI #0Campo < aAlfa2; |O #0Campo > aAlfa3;
     |MENAV "    Error. Fecha Incorrecta ";
     |ERROR;
 |SINO;
     |SI Campo = 40; |Y #0#39 > #0#40;
         |MENAV "    Error. Fecha Incorrecta ";
         |ERROR;
     |FINSI;
 |FINSI;
|FCAL;

|CALCULO SelAct; |TIPO 0;
 aAlfa1 = #0Campo;
 |C_M #0#50,1, aAlfa1;
 |C_M #0#51,1, aAlfa1;
|FCAL;

|CALCULO Cuadro; |TIPO 7;
   |PUSHV  1512 1859;
   |CUADRO 1512 1859;
   |ATRI R;
   |PINTA 1513, " -------------- FORMATO LIBRO --------------- ";
   |SI #0#0 = "I";
       |PINTA 1613, " (1) Descripcion de Factura y Cuenta Contable ";
   |SINO;
       |PINTA 1613, " (1) Descripcion de Factura                   ";
   |FINSI;
   |PINTA 1713, " (2) Descripcion apuntes.                     ";
   |ATRI r;
|FCAL;

|CALCULO SalCuadro; |TIPO 0;
   |POPV;
|FCAL;
|| **********************************************************************************
|| **********************************************************************************
|| .................... graba Contabilidad
|PROCESO LEmpresaConta;

 enEmpresa  = #canempre#2;
 enPerConta = #canempre#3;   || Periodo Contable

 |SI #0#24 = "N"; |Y #canempre#21 = "I"; |ACABA; |FINSI;

|| ------------------------------------------------------------------------
   aAlfa1 = enEmpresa;  aAlfa1 = ("00000" + aAlfa1) % -105;
   aAlfa2 = enPerConta; aAlfa2 = ("0" + aAlfa2) % -101;
   aPathConta  = aPath + "contagen/fich/" + aAlfa1 + aAlfa2 + "/";

   nGTempo = nGTempo + 1;
   #999#0 = nGTempo;       || Guia
   #999#1 = enEmpresa;     || Empresa
   #999#2 = enPerConta;    || Periodo Contable
   #999#3 = enEjer;        || A¤o
   #999#4 = #canempre#11;  || Desde Mes
   #999#5 = #canempre#13;  || N§ Digitos
   #999#6 = #canempre#14;  || Digitos Nivel 1
   #999#7 = #canempre#15;  || Digitos Nivel 2
   #999#8 = #canempre#16;  || Digitos Nivel 3
   #999#9 = #canempre#17;  || Digitos Nivel 4
   #999#10 = #canempre#18; || Digitos Nivel 5
   #999#11 = #canempre#19; || Digitos Nivel 6
   eaEstadoEmp = "S"; |SI #canempre#21 = "I"; eaEstadoEmp = "N"; |FINSI;
   #999#12 = eaEstadoEmp;  || Estado
   #999#13 = #canempre#28; || Moneda
   #999#14 = #canempre#1;  || Nombre Empresa
   #999#15 = aPathConta;   || Path
   #999#16 = #0#43;        || Emitir
   |GRABA 020#999n;
|FPRC;

|PROCESO LEmpresaEOS;
 enEmpresa  = #agifigen#0;
 enPerConta = 0;

 eaEstadoEmp = "S";
 |SI #agifigen#94 > "01.01.0000";
     eaEstadoEmp = "N";
     |SI #0#24 = "N"; |ACABA; |FINSI;
 |FINSI;

|| ... Seleccion de Tipo de Empresa
  nSPin1 = #agifigen#87;
  enSwSelEmp = 1;
  |SI #0#48 = "S";
      |SI nSPin1 ] #0#25; |Y nSPin1 [ #0#26;
          enSwSelEmp = 0;
      |FINSI;
  |SINO;
      |SI nSPin1 = #0#27;
          enSwSelEmp = 0;
      |SINO;
          |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
                |SI nSPin1 = #0nCampo; |Y nSPin1 ! 0;
                    enSwSelEmp = 0;
                    |SAL;
                |FINSI;
          |SIGUE;
      |FINSI;
  |FINSI;
  |SI enSwSelEmp = 1; |ACABA; |FINSI;
|| ------------------------------------------------------------------------

       |SELECT #4#canempre;
       #canempre#2  = enEmpresa;
       #canempre#26 = nEjercicio;
       |LEE 001#canempre.=;
       |SI FSalida = 0;
           |HAZ LEmpresaConta;
       |FINSI;
       |SELECT #1#canempre;
|FPRC;

|DEFBUCLE agifigen;
 #agifigen#1;
 ;
 nDEmpresa;
 nHEmpresa;
 e;
 NULL, LEmpresaEOS;
|FIN;

||///////////////////////////////////////////////////////////////////
|CALCULO Datos7; |TIPO 7;
 |MENSA "    <F3> Cambiar Emitir Libro S/N";
 |ATRI R; |PINTA 1169, "Imp"; |ATRI 0;
|FCAL;

|CALCULO DatosManip; |TIPO 11;
   nInkey = FSalida;
   |BLIN 04;

   |SI nInkey = 11;
       |SI #999#16 = "S";
           #999#16 = "N";
       |SINO;
           #999#16 = "S";
       |FINSI;
       |GRABA 020#999a;
   |FINSI;
|FCAL;

|RUTINA inferior;
   #999#0 = 00001;
|FRUT;

|RUTINA superior;
   #999#0 = 99999;
|FRUT;

||///////////////////////////////////////////////////////////////////
|CALCULO Tipo3;  |TIPO 3;
    aMonMod = "P";
    |HAZ LeeEURODBMOD;
    |SI EURODBMOD = 2; aMonMod = "E"; |FINSI;

    enModelo = #0#44;
    |HAZ LeeCtrlLibro;

    enEjer = #0#1;
    eaPlanilla = "dsmom060";  |HAZ PlanDePlanillas;

    aAlfa1 = enEjer; |QBF aAlfa1;
    aAlfa1 = aAlfa1 % -102;
    nEjercicio = aAlfa1;
    |DBASS aPath; |QBT aPath;

    aFichero = ("018y" + Usuario) % 108;
    |NOME_DAT #901 aFichero;
    aFichero = ("018i" + Usuario) % 108;
    |NOME_DAT #902 aFichero;

    aFichero = ("043w" + Usuario) % 108;
    |NOME_DAT #999 aFichero;
    |ABRE #999;  |CIERRA #999;  |DELINDEX #999;
    eaFEmpresa = aFichero;

    |ABRE #999;
    nGTempo = 0;   ||Busco Empresas Contables
    |ABRE #canempre;
    |SI #0#2 ! 0;
        nDEmpresa = #0#2;
        nHEmpresa = #0#3;
        |BUCLE agifigen;
    |SINO;
        |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
              nDEmpresa = #0nCampo;
              nHEmpresa = #0nCampo;
              |BUCLE agifigen;
        |SIGUE;
    |FINSI;
    |CIERRA #canempre;
    |CIERRA #999;

    |SI nGTempo = 0;    || No existe ninguna empresa
        |MENAV "    Error. No existe ninguna Empresa dentro de la Seleccion ";
        |DELINDEX #999;
        |ACABA;
    |FINSI;

    |C_V #999#12,1,"N"; |C_P #999#16,1,1270;

    |PUSHV 0509 2375;
    |ENTLINEAL #1#999, -1, 2, 22, 1, inferior, superior;
    |POPV;
    |HAZ Imprimir;

    |DELINDEX #999;
    |DELINDEX #901;
    |DELINDEX #902;
|FCAL;

|| *************************************************************************
|| *************************************************************************
|| *************************************************************************
|PROCESO Lee60;
 |ABRE #60;
 |DDEFECTO #60;
 #60#6 = enPlan;
 #60#0 = #0#46;
 #60#1 = #0#0;
 #60#2 = nSwCol;
 |LEE 001#60=;
 |SI FSalida = 0;
     aAlfa1 = #60#4; |QBF aAlfa1;
     |SI aAlfa1 = "";
         #60#4 = #60#3;
     |FINSI;
 |FINSI;
 |CIERRA #60;
|FPRC;

|PROCESO LeeAuxi;
  nConta = nConta + 1;
  #902#3 = nConta;
  |PRINT;
  |PARA nNume1 = 9; |SI nNume1 [ 22; |HACIENDO nNume1 = nNume1 + 1;
        nNume2 = nNume1 + 16;
        #900nNume2 = #900nNume2 + #902nNume1;
  |SIGUE;
  SwPrim = 1;
|FPRC;

|DEFBUCLE LeeAuxi;
 #902#1;
 ;
 INICIO;
 FINAL;
 ;
 NULL, LeeAuxi;
|FIN;

|PROCESO ImpAuxiliar;

 |PARA nNume1 = 39; |SI nNume1 [ 52; |HACIENDO nNume1 = nNume1 + 1;
       nSwCol = nNume1 - 38;
       nNume2 = nNume1 + 14;
       |HAZ Lee60;
       #900nNume1 = #60#4;
       #900nNume2 = #60#5;

       aAlfa1 = #900nNume2; |QBF aAlfa1;
       |SI aAlfa1 = "";
           #900nNume2 = #900nNume1; #900nNume1 = "";
       |FINSI;
       |SI #0#0 = "G"; |Y #0#47 = 1;
           aAlfa1 = #900nNume1; |QBF aAlfa1;
           aAlfa1 = aAlfa1 % 111;
           aAlfa1 = ("           " + aAlfa1 ) % -111;
           #900nNume1 = aAlfa1;
           aAlfa1 = #900nNume2; |QBF aAlfa1;
           aAlfa1 = aAlfa1 % 111;
           aAlfa1 = ("           " + aAlfa1 ) % -111;
           #900nNume2 = aAlfa1;
       |SINO;
           aAlfa1 = #900nNume1; |QBF aAlfa1;
           aAlfa1 = aAlfa1 % 114;
           aAlfa1 = ("               " + aAlfa1 ) % -114;
           #900nNume1 = aAlfa1;
           aAlfa1 = #900nNume2; |QBF aAlfa1;
           aAlfa1 = aAlfa1 % 114;
           aAlfa1 = ("               " + aAlfa1 ) % -114;
           #900nNume2 = aAlfa1;
       |FINSI;
 |SIGUE;

 |PARA nEspDos = 1; |SI nEspDos [ 2; |HACIENDO nEspDos = nEspDos + 1;

      |SI #0#0 = "I";
          informe = "dsmoyi18";
          |SI #0#52 = 2; informe = "dsmoyn18"; |FINSI;
      |SINO;
          informe = "dsmoyg18";
          |SI #0#47 = 2;
              informe = "dsmo1g18";
              |SI nEspDos = 2;
                  informe = "dsmo2g18";
              |FINSI;
          |FINSI;
      |FINSI;

      |SI #306#4 = "N";
           aAlfa1  = informe % 306;
          informe = "dr" + aAlfa1;
      |FINSI;

      |INFOR informe;
      |SI FSalida ! 0;  |ACABA;  |FINSI;

      SwPrim   = 0;
      nConta = 0;
      |PDEFECTO #900, 25, 39;

      |BUCLE LeeAuxi;
      |SI SwPrim = 1;
          |PIEINF;
          nPagina = nPagina + 1;
      |FINSI;
      |FININF;

      |SI #0#47 = 1; nEspDos = 2; |FINSI;
  |SIGUE;
|FPRC;

|| ////////////////////////////////
|PROCESO LeeAuxiR;
  nSwCol = #901#2;
  |HAZ Lee60;
  |PRINT;
  SwPrim = 1;
|FPRC;

|DEFBUCLE LeeAuxiR;
 #901#1;
 ;
 #0#46, #0#0, 00;
 #0#46, #0#0, 99;
 ;
 NULL, LeeAuxiR;
|FIN;

|PROCESO ImpAuxiliarR;

 informe = "dsmoyr18";
 |INFOR informe;
 |SI FSalida ! 0;  |ACABA;  |FINSI;

 eaTitul2 = "GASTOS FISCALMENTE DEDUCIBLES";
 |SI #0#0 = "I";
     eaTitul2 = "CONCEPTO DE VENTAS E INGRESOS";
 |FINSI;

 SwPrim = 0;
 |BUCLE LeeAuxiR;
 |SI SwPrim = 1;
     |PIEINF;
 |FINSI;
 |FININF;
|FPRC;

|| /////////////////////////////////////////////////////////////////
|PROCESO Graba_w33;
 #901#0 = #61#0;
 #901#1 = #61#1;
 #901#2 = #61#2;
 |LEE 001#901=;
 |SI FSalida ! 0;
     |DDEFECTO #901;
     #901#0 = #61#0;
     #901#1 = #61#1;
     #901#2 = #61#2;
     |GRABA 020#901c;
 |FINSI;

 #901#7 = #901#7 + nImporte;
 |GRABA 020#901a;
|FPRC;

|PROCESO BuscoFra;
     aNomCta = #302#13;
     aDesCon = #302#28;
     aCIF    = #302#14;
     nSwSiHay = 1;
|FPRC;

|DEFBUCLE BuscoFra;
 #302#3;
 7,8,9;
 #301#13, #301#15, #301#14, #301#0, #301#1, #301#2;
 #301#13, #301#15, #301#14, #301#0, #301#1, #301#2;
 e;
 NULL, BuscoFra;
|FIN;

|PROCESO LeeCta;
 aNomCta = "";
 aDesCon = "";
 aCIF    = "";
 nSwSiHay = 0;
 |BUCLE BuscoFra;
 |SI nSwSiHay = 0;
     |ABRE #303;
     #303#0  = #301#4;
     |LEE 001#303=;
     |SI FSalida = 0;
         aNomCta = #303#2;
     |FINSI;
     aDesCon = #301#7;
     |CIERRA #303;
 |FINSI;

 |SI #0#52 = 2;
     aDesCon = #301#7;
 |FINSI;
|FPRC;

|PROCESO Graba_w54;
  |DDEFECTO #902;
  nConta = nConta + 1;
  #902#0 = #301#1;
  #902#1 = nConta;

  aAlfa1 = #902#0;
  #902#2 = aAlfa1 % 105;

  |HAZ LeeCta;
  #902#4 = aNomCta;
  #902#5 = aCIF;
  #902#6 = aDesCon;
  #902#7 = #301#4;
  #902#8 = nSwCol;
  |SI nSwCol [ 13;
      nNume1 = nSwCol + 8;
 |SINO;
      nNume1 = 22;
  |FINSI;
  #902nNume1 = nImporte;
  |GRABA 020#902n;
|FPRC;

|PROCESO dsmom061_0;
 aAlfa1 = #61#4; |QBF aAlfa1;
 aAlfa2 = (aAlfa1 + "zzzz") % 104;
 |SI aAlfa1 [ aHCta; |Y aAlfa2 ] aHCta;
     nSwCol = #61#2;                        ||Columna Libro
     aSigno = #61#5;
     |ERROR10;
 |FINSI;
|FPRC;

|DEFBUCLE dsmom061;
 #61#1;
 4;
 enPlan, #0#46, #0#0, 00, 001, aDCta;
 enPlan, #0#46, #0#0, 99, 999, aHCta;
 ;
 NULL, dsmom061_0;
|FIN;

|PROCESO Camaasil_0;

 #301#9 = (#301#9  / enConversor) ! EURODBMOD;
 |SI enActividad ! 0;
     nElDepar = #301#21;
     |SI nElDepar = 0;
         nElDepar = 1;
     |FINSI;
     |SI enActividad ! nElDepar; |ACABA; |FINSI;
 |FINSI;

 |SI enSwSelCp = 1;
     enConcepto = #301#6;
     |HAZ CtrlConcepto;
     |SI enSwConcep = 1;
         |ACABA;
     |FINSI;
 |FINSI;

 nSwCol = 0;
 aCta = #301#4;
 aDCta = (aCta % 101) + "   ";
 aHCta = aCta % 104; |QBF aHCta;
 |BUCLE dsmom061;
 |SI nSwCol ! 0;
     nImporte = #301#9;
     |SI #301#8 = "D"; |Y aSigno = "-"; nImporte = - nImporte; |FINSI;
     |SI #301#8 = "H"; |Y aSigno = "+"; nImporte = - nImporte; |FINSI;

     |HAZ Graba_w54;
     |HAZ Graba_w33;
     SwHayDatos = 1;
 |FINSI;
|FPRC;

|DEFBUCLE Camaasil;
 #301#1;
 ;
 01, efDFecha, 000001, 0001;
 98, efHFecha, 999999, 9999;
 e;
 NULL, Camaasil_0;
|FIN;

||////////////////////////////////////////////////////////////////////////
|PROCESO Emision;

   enEmpresa  = #999#1;
   enPerConta = #999#2;
   eaLaMonEmp = #999#13;
   |HAZ EnConversor;

   enEjer     = #999#3;   || A¤o
   dig1       = #999#4;   || Desde Mes
   dig3       = #999#5;   || N§ Digitos
   dig4       = #999#6;   || Digitos Nivel 1
   dig5       = #999#7;   || Digitos Nivel 2
   dig6       = #999#8;   || Digitos Nivel 3
   dig7       = #999#9;   || Digitos Nivel 4
   dig8       = #999#10;  || Digitos Nivel 5
   dig9       = #999#11;  || Digitos Nivel 6
   eaEstadoEmp = #999#12; || Estado
   eaNombreEmp = #999#14; || Nombre Empresa
   aPathConta  = #999#15; |QBF aPathConta; || Path

   || Recoger informacion sobre los niveles ...
   |SI dig3 = 1; MaximoDigitos = dig4; |FINSI;
   |SI dig3 = 2; MaximoDigitos = dig5; |FINSI;
   |SI dig3 = 3; MaximoDigitos = dig6; |FINSI;
   |SI dig3 = 4; MaximoDigitos = dig7; |FINSI;
   |SI dig3 = 5; MaximoDigitos = dig8; |FINSI;
   |SI dig3 = 6; MaximoDigitos = dig9; |FINSI;

   eaTituloL = #0#45; ||Titulo del Libro
   |SI #0#37 = "S";
       |SUB_EJECUTA_NP ":basica/agiy0016";
   |FINSI;

  |PATH_DAT #301 aPathConta;
  |PATH_DAT #302 aPathConta;
  |PATH_DAT #303 aPathConta;
  |PATH_DAT #306 aPathConta;

  |DDEFECTO #306;  ||Configuracion de Informes
  |ABRE #306;
  #306#0 = 1;
  |LEE 000#306=;
  |CIERRA #306;

  nDActividad = 0; nHActividad = 0;
  |SI #0#49 = "S";  nDActividad = #0#50; nHActividad = #0#51;  |FINSI;

  |PARA enActividad = nDActividad; |SI enActividad [ nHActividad; |HACIENDO enActividad = enActividad + 1;

        |HAZ LeeDatosEmpresa;
        |SI  swNoExis = 0;       ||SI existe esta Actividad.

             SwHayDatos = 0;
             nConta     = 0;

             |ABRE #901;
             |ABRE #902;
             |BUCLE Camaasil;
             |CIERRA #902;
             |CIERRA #901;

            nPagina = 1;
            |SI SwHayDatos = 1;
                |SI #0#38 ! "R";
                    |HAZ ImpAuxiliar;
                |FINSI;

                |SI #0#38 ! "N";
                    |HAZ ImpAuxiliarR;
                |FINSI;
            |FINSI;
      |FINSI;
  |SIGUE;
|FPRC;

|DEFBUCLE dsmow043;
 #dsmow043#1;
 16;
 000001, "S";
 999999, "S";
 ;
 NULL, Emision;
|FIN;

|PROCESO Imprimir;
    efDFecha  = #0#39;
    efHFecha  = #0#40;

    |MENAV "0000 Recuerde seleccionar el papel de la impresora en Horizontal";

    |IMPRE 0;
    |SI FSalida ! 0;  |ACABA;  |FINSI;

    |BUCLE dsmow043;

    |FINIMP;
|FPRC;

|PROCESO LeeDatosEmpresa;
  |DELINDEX #901;
  |DELINDEX #902;

  |DDEFECTO #900;

  #900#0 = enEmpresa;
  nPin1 = 0;
  aPin1 = "";
  efPn10 = "01.01.0000";
  efPn11 = "01.01.0000";

  || Lee Primera Actividad
    |ABRE #agicen14;
    #agicen14#0 = #900#0; || Empresa
    #agicen14#1 = 00;
    |LEE 041#agicen14.];
    |SI FSalida = 0; |Y #agicen14#0 = #900#0;
        #900#5 = #agicen14#9;
        #900#6 = #agicen14#10;
        #900#7 = #agicen14#7;
        #900#8 = #agicen14#8;
        efPn10 = #agicen14#5;
        efPn11 = #agicen14#6;
    |FINSI;

  || Lee Actividad en concreto.
    swNoExis = 0;
    |SI enActividad ! 0;
        #agicen14#0 = #900#0; || Empresa
        #agicen14#1 = enActividad;
        |LEE 041#agicen14.=;
        |SI FSalida = 0;
            #900#15 = #agicen14#1;   ||Act.
            #900#16 = #agicen14#2;   ||Tipo Epig.
            #900#17 = #agicen14#3;   ||Epigrafe
            #900#18 = #agicen14#4;   ||Descripcion Actividad
            #900#19 = #agicen14#5;   ||Fecha Inicio
            #900#20 = #agicen14#6;   ||Fecha Fin
            #900#21 = #agicen14#11;  ||Sector actividad
            #900#22 = #agicen14#12;  ||Sector actividad
            #900#23 = #agicen14#16;  ||Tipo Actividad
            #900#24 = #agicen14#17;  ||Descripcion Tipo Actividad
       |SINO;
            |CIERRA #agicen14;
            swNoExis = 1;
            |ACABA;
       |FINSI;
    |FINSI;

    |CIERRA #agicen14;

 || Lee Ficha Sociedades
    swExiste = 0;
    |ABRE #agim0019;
    #agim0019#0 = #900#0; || Empresa
    #agim0019#1 = 99;
    |LEE 041#agim0019.];
    |SI FSalida = 0;
        |SI #agim0019#0 ! #900#0;
            |LEE 041#agim0019.a;
            |SI FSalida = 0; |Y #agim0019#0 = #900#0;
                swExiste = 1;
            |FINSI;
        |SINO;
            swExiste = 1;
        |FINSI;
    |FINSI;
    |SI swExiste = 1;
        efPn10 = #agim0019#7;      || Fecha Activa
        efPn11 = #agim0019#55;     || Fecha Inactiva
        nPin1 = #agim0019#4;
        aPin1 = #agim0019#5;
    |FINSI;
    |CIERRA #agim0019;

    |ABRE #agifigen;
    #agifigen#0 = #900#0; || Empresa
    |LEE 041#agifigen.=;
    |SI FSalida = 0;
        efPn10  = #agifigen#93;
        efPn11  = #agifigen#94;
        nPin1   = #agifigen#87;
        aPin1   = #agifigen#88;
        #900#1 = #agifigen#13;  || Nif
        #900#2 = #agifigen#1;   || Nombre
    |FINSI;
    |CIERRA #agifigen;
    |SI efPn10 [ "01.01.1900"; efPn10 = "01.01.0000"; |FINSI;
    |SI efPn11 [ "01.01.1900"; efPn11 = "01.01.0000"; |FINSI;
    #900#3 = nPin1;
    #900#4 = aPin1;
    #900#9 = efPn10;
    #900#10 = efPn11;

    #900#11 = "ACTIVA";
    |SI #999#12 = "N"; #900#11 = "INACTIVA"; |FINSI;
    #900#12 = #0#1;                                   || Ejercicio
    #900#13 = efDFecha;
    #900#14 = efHFecha;

    #900#67 = #0#45; ||Titulo del Libro
|FPRC;


|| ***************************************************************
|PROGRAMA;
  aParam = PARAMETRO; |QBF aParam;

  #0#46 = "N";  ||No inmovilizado
  |SI aParam = "I"; |O aParam = "G";
       #0#0 = aParam;
       |C_M #0#0,1,"N";
       |C_V #0#0,1,"N";
   |SINO;
       |C_M #0#0,1,"S";
       |C_V #0#0,1,"S";
   |FINSI;

   |CLS;
   |CABEZA "Libros Ingresos/Gastos";
   |PINPA #0#0;
   |PINDA #0#0;
   |ENDATOS #1#0;
|FPRO;
