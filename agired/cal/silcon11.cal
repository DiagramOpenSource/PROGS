|FICHEROS;
    silcon11 #0;    || emision TC1
    labempre #1;    || empresas
    labempax #2;    || aux. empresas
    nomconst #3;    || constantes
    nomtepig #4;    || aux. epigrafes
    tsilcon4 #7;    || campos de impresion
    labcentr #10;   || centros de trabajo
    nomcenes #13;   || centros 18/93
    nomtstc1 #14;   || tmp. para importes TC1
    nomcones #15;   || constante especial 18/93
    nomtraca;
    labtraba;
    nomtepcd;
    nomwepcd;
    nomepigr #2610;
     nomssfan;
|FIN;

|VARIABLES;
    menos = 0;
    nom_tmp = "";
    sgdonum = 0;
    total = 0;
    cont = 0;
    epi = 0;
    trab = 0;
    ilt = 0;
    ims = 0;
    bas = 0;
    imp1 = 0;
    imp2 = 0;
    ini = 0;
    x = 0;
    postal1 = "";   || Codigo postal1 alfa
    postal2 = "";   || Codigo postal2 alfa
    domi    = "";   || Variable para quitar espacios al domicilio
    afili   = "";   || Variable para quitar espacios al nro. de afiliacion
    sgdoalf = "";   || Variable para quitar espacios al nro. de afiliacion
    fecsys = @;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume0 = 0;
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    p_mes = "";
    desde = 0;
    hasta = 0;
    seleccio = 0;
    tip = 0;
    tip2 = 0;
    tip3 = 0;
    siono_cen = "";
    el_period = 0;
    runnom = "";
    notaria = 0;
    emp_ant = -1;
    cen_ant = -1;
    hay_algo = 0;
    &s_emp = 0;
    &s_any = 0;
    &s_mes = 0;
    &s_tip = "";
    &s_tipd = "";
    &s_cen = "";
    &s_pas = 0;
    &s_nom = "";
    &s_ori = "";
    &s_tat = 0;
    sw_red = 0;
    por_dfp = 0;
    por_mat = 0;

    &temp1 = "";
    &temp2 = "";
    &temp3 = "";
    &temp4 = "";
    &temp5 = "";
    cc64 = 0;
    cc65 = 0;
    nEl36 = 36;
    nBase36 = 0;
    bon_162001 = 0;

    &esAtrasos = 0;
    liqu = "";
    nCuota_mater = 0;
    nContEmp = 0;
    &EURODB = 0;
    nDS = 0;
    nFG = 0;
    nFP = 0;
    swCte = 0;
    nCteAnt = 0;
    nFPEmp = 0;
    nFPTra = 0;
    fFecha1 = @;
    fFecha2 = @;

    nBase = 0;
    nCuotaTot = 0;
    nCuota = 0;
    nAntPorc = 0;
    nAnyo = 0;
    nILTERE = 0;
    nIMSERE = 0;
    nBaseERE = 0;
    nBaseEREDes = 0;
     nExcFog = 0;
     swElPrimero = 0;
     &swFiniqRaro = 0;
    nCuotaFogTot = 0;
    nCuotaFog = 0;
    nPintaPorc = 0;
    nPorFOGASA = 0;
     &s_anyl = 0;
     &aTempEpig = "";
     aAlfa = "";
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     aCCC = "";
     nTotalSSEmp = 0;
|FIN;


|PROCESO nom_usu;
    nom_tmp = Usuario;
|QBF nom_tmp;
    nom_tmp = nom_tmp + "zzzzzzzzzz";
    nom_tmp = nom_tmp % 107;
    nom_tmp = "t" + nom_tmp;
|FPRC;

|PROCESO nomssfan;
     nTotalSSEmp = nTotalSSEmp + #nomssfan#9;
|FPRC;

|DEFBUCLE nomssfan;
     #nomssfan#2;
     ;
     aCCC, s_any, s_mes, 00;
     aCCC, s_any, s_mes, 00;
     ;
     NULL, nomssfan
|FIN;

|PROCESO SegSocEmp;
     aAlfa1 = #7#1; |QBF aAlfa1;
     aAlfa2 = #7#2; |QBT aAlfa2; aAlfa2 = aAlfa2 % 8;
     aAlfa3 = #7#3; |QBF aAlfa3;

     aCCC = "0111" + aAlfa1 + aAlfa2 + aAlfa3;

     nTotalSSEmp = 0;

     |BUCLE nomssfan;

     |SI nTotalSSEmp < 0;
          |SI #2#98 > 0;
               #2#98 = #2#98 + nTotalSSEmp;
          |FINSI;
     |FINSI;
|FPRC;

____________________________________/ PREPARACION DEL TC1
|PROCESO tc1;
|SI runnom = "nomyytc1";  notaria = #2#16;  |FINSI;
|SI runnom = "nomwwtc1";  notaria = #2#19;  |FINSI;
|SI tip = 3; notaria = 0; |FINSI;
    #7#0 = #1#2;                           || nombre de la empresa
|SI siono_cen = "N";   afili = #1#13;   |FINSI;
|SI siono_cen = "S";   afili = #10#10;  |FINSI;
|QBF afili;
    sgdoalf = afili % 0;
    sgdonum = sgdoalf;
    sgdonum = sgdonum - 4;
    sgdonum = sgdonum + 300;               || para coger 2do. digito
    #7#1 = afili % 102;                    || Primer digito S.S.
    #7#2 = afili % sgdonum;                || Segundo digito S.S.
    #7#3 = afili % -102;                   || Tercer digito S.S.
|SI tip = 0; |O tip = 3;
    |SI siono_cen = "S";                     || DATOS CENTRO
            domi = #10#3;
        |QBF domi;
            #7#4 = domi+" "+#10#4+" "+#10#5;
            postal1 =  "00" + #10#7;
            postal1 = postal1 % -102;
            postal2 = "000" + #10#8;
            postal2 = postal2 % -103;
            #7#5 = " " + postal1 + postal2 + " " + #10#6;
            #7#6 = #10#9;                         || provincia
            #7#9 = #10#14;                        || entidad
            #7#10 = #10#13;                       || clave entidad
    |SINO;                                   || DATOS EMPRESA
            domi = #1#3;
        |QBF domi;
            #7#4 = domi+" "+#1#4+" "+#1#5;        || domicilio
            postal1 = "00" + #1#7;
            postal1 = postal1 % -102;
            postal2 = "000" + #1#8;
            postal2 = postal2 % -103;
            #7#5 = " " + postal1 + postal2 + " " + #1#6;
            #7#6 = #1#9;                          || provincia
            #7#9 = #1#20;                         || entidad
            #7#10 = #1#19;                        || clave entidad
    |FINSI;
|SINO;
    |HAZ cen_esp;
        domi = #13#3;
    |QBF domi;
        #7#4 = domi+" "+#13#4+" "+#13#5;
        postal1 =  "00" + #13#7;
        postal1 = postal1 % -102;
        postal2 = "000" + #13#8;
        postal2 = postal2 % -103;
        #7#5 = " " + postal1 + postal2 + " " + #13#6;
        #7#6 = #13#9;                        || provincia
        #7#9 = #13#15;                       || entidad
        #7#10 = #13#14;                      || clave entidad
|FINSI;
|HAZ mes;                                  || Calculo convertir mes en letra
    alfa1 = #0#3;
    alfa1 = alfa1 % -102;
    #7#8 = alfa1;                          || A¤o hasta (TC1/92)
    #7#11 = #1#12;                         || Actividad empresa
    #7#12 = #1#11;                         || Clave actividad
    #7#13 = #1#10;                         || CIF empresa
|SI tip = 0; |O tip = 3;
        #7#14 = #7#14 + #2#3;              || Contingencias comunes
        #7#15 = #3#6 + #3#7;
        #7#16 = #7#14 % #7#15;

        #7#23 = #7#23 + #2#4;              || Contingencias comunes coef.red.
        #7#24 = #3#6;
        #7#25 = #7#23 % #7#24;

        #7#168 = #7#168 + #2#96;
        #7#169 = #7#169 + #2#97;
|FINSI;

|SI tip = 9;             || tiempo parcial <12 horas R.D.489/98
    |SI liqu = "T";
            cc64 = ((#3#6 * #15#21)!2) + ((#3#7 * #15#21)!2);
            cc65 = ((#3#6 * #15#22)!2) + ((#3#7 * #15#22)!2);
    |FINSI;
    |SI liqu = "O";
            cc64 = (#3#7 * #15#21) ! 2;
            cc65 = (#3#7 * #15#22) ! 2;
    |FINSI;
    |SI liqu = "P";
            cc64 = (#3#6 * #15#21) ! 2;
            cc65 = (#3#6 * #15#22) ! 2;
    |FINSI;

        #7#14 = #7#14 + (#2#48 + #2#49);     || Contingencias comunes
    |SI #7#16 = 0;
        |SI #2#48 ! 0;|Y #2#49 ! 0;
                #7#15 = 0;
        |SINO;
            |SI #2#48 ! 0;    #7#15 = cc64;  |FINSI;
            |SI #2#49 ! 0;    #7#15 = cc65;  |FINSI;
        |FINSI;
    |SINO;
        |SI #2#48 ! 0;|Y #7#15 = cc65;  #7#15 = 0;     |FINSI;
        |SI #2#49 ! 0;|Y #7#15 = cc64;  #7#15 = 0;     |FINSI;
    |FINSI;
        #7#16 = #7#16 + (#2#48 % cc64) + (#2#49 % cc65);

        #7#23 = 0;
        #7#24 = 0;
        #7#25 = 0;
|FINSI;

    #7#17 = #7#17 + #2#5;                  || Horas extras estruc.
    #7#18 = #3#8 + #3#9;
    #7#19 = #7#17 % #7#18;

    #7#20 = #7#20 + #2#6;                  || Horas extras no estruc.
    #7#21 = #3#10 + #3#11;
    #7#22 = #7#20 % #7#21;
|SI tip = 0;
          nBase36 = (#2#51 % #3#6) ! EURODB;
          #7#113 = #7#113 + (#2#3 % #3#74) + (nBase36 % nEl36); || cuota 106
          #7#147 = #7#147 + nBase36;                            || parcial base 106
          |SI #3#74 ! 0;|Y #7#147 ! 0;
               #7#112 = 0;                        || base 106 (conjunto)
               #7#114 = 0;                        || (%) 106 (conjunto)
               #7#146 = 8;                        || tipo 106 (conjunto)
          |SINO;
               |SI #3#74 ! 0;
                    #7#112 = #7#112 + #2#3;       || base 106 (notarias)
                    #7#114 = #3#74;               || (%) 106 (notarias)
                    #7#146 = 8;                   || tipo 106 (notarias)
               |FINSI;
               |SI #7#147 ! 0;
                    #7#112 = #7#112 + nBase36;    || base 106 (12/2001)
                    #7#114 = nEl36;               || (%) 106 (12/2001)
                    #7#146 = 6;                   || tipo 106 (12/2001)
               |FINSI;
          |FINSI;

        #7#119 = #7#16;                    || deducc.volunt.(122)
        #7#120 = #3#75;                    || (%) deduc.volunt.
        #7#121 = #7#119 % #7#120;          || cuota deduc.volunt. (132)
|FINSI;

    #7#26 = #7#16 + #7#19 + #7#22 + #7#25 + #7#113 + #7#169; || Suma de cuotas
    ||#7#27 = #7#27 + #2#7;                  || Plus familiar (*)
    ||#7#28 = #7#28 + #2#8;                  || Asistencia subnormal (*)
    #7#29 = #7#29 + #2#9;                  || Importe I.L.T. (*)
    #7#30 = #7#30 + #2#11 + #2#21 - #2#21; || Bonificaciones contingencias (*)
    #7#110 = notaria;                      || (%) deducc.notarias
    #7#111 = ((#7#16 + #7#25) % #7#110) ! EURODB;    || cuota notarias 131
    #7#31 = #7#27 + #7#28 + #7#29 + #7#30;   || Punto 210
    #7#32 = #7#26 - #7#31 - #7#111 - #7#121; || Punto 299

|HAZ leepitrab;
|SI tip ! 8;
        nDS = #3#12 +#3#13;                                 || DESEMPLEO
        nFG = #3#14 +#3#15;                                 || FG
        nFP = #3#16 +#3#17 +#3#18 +#3#19;                   || FP+FS
        nDS = (#7#80 % nDS) ! EURODB;   || la TGSS lo calcula asi,
        nFG = ((#7#80 - nExcFog) % nFG) ! EURODB;   ||/sino se provocan diferencias
        nFP = (#7#80 % nFP) ! EURODB;
        #7#86 = #3#12 +#3#13 +#3#14 +#3#15 +#3#16 +#3#17 +#3#18 +#3#19;
        #7#87 = nDS + nFG + nFP;
        #7#88 = #7#87;                     || Punto 510
        #7#149 = #3#12 + #3#14 + #3#16 + #3#18; || porc. en CP Maternidad
        #7#150 = (#7#148 + #7#158) % #7#149;    || cuota en CP Maternidad+ERE

        nCuota_mater = nCuota_mater ! EURODB;
        nume0 = #7#150 - nCuota_mater;
        |SI nume0 < 0; nume0 = -nume0; |FINSI;
        |SI nume0 > 0.03;
               |SI por_mat = -1; por_mat = 0; |FINSI;
               #7#149 = por_mat;
               #7#150 = nCuota_mater;
        |FINSI;

        #7#145 = nCuotaTot;   || ATENCION: 21.01.04: esta cuota es la que cal-
                              || culo mediante el proceso LaCuota, respecto a
                              || los importes grabados. Se cambia por las di-
                              || ferencias que se provocaban respecto a TGSS.
        || #7#145 = #7#145 + nFG + nFP;      RLMigue
        #7#145 = #7#145 + nCuotaFogTot + nFP;
        #7#145 = #7#145 ! EURODB;
        nume1 = #7#87 - #7#145;
        por_dfp = #7#86;
        |SI nume1 < 0;   nume1 = -nume1; |FINSI;
        |SI nume1 > 0.03;
               || #7#86 = 0;       RLMigue
               |SI nPintaPorc = -1;
                    #7#86 = 0;
               |SINO;
                    #7#86 = nPintaPorc + #3#16 + #3#17;
               |FINSI;
               #7#88 = #7#145;
        |FINSI;

        #7#87 = #7#80 + nBaseEREDes;    || el 301 (= 501 + 502) + base ERE
        #7#80 = #7#80 - #7#148;         || quito a la base las cont. empres.
        || #7#88 = #7#88 - (#7#148 % por_dfp);  || quito a la cuota las cont. empres.
        #7#88 = #7#88 - nContEmp;       || quito a la cuota las cont. empres.
        #7#80 = #7#80 - bon_162001;     || quito a la base la bon. 16/2001 > 65
        |SI #7#80 [ 0;   || es porque al quitarle el importe de la bon > 65
             #7#88 = 0;  || esta ocupa todo el mes, entonces no cotizo al desem
             #7#80 = 0;  || (ahora es posible que sea negativo)
        |SINO;           || sino es porque no me ocupa todo el mes, quito la parte
             #7#88 = #7#88 - ((bon_162001 - #2#96) % por_dfp);  || quito a la cuota la bon. 16/2001 > 6

               || ojo, que la coit. solidaridad no esta clara aqui en enom
        |FINSI;
        #7#148 = #7#148 + #7#158;  || acumulo al 148 el 158
|SINO;
     || como el campo 46 del labempax ya no se usa lo uso para grabar cuantos
     || de los trabajadores en formacion estan de maternidad, y a esos le tengo
     || que quitar la cuota obrera
        #7#14 = 0;
        #7#15 = 0;
        #7#80 = 0;
        #7#86 = 0;
             aAlfa = #2#34; |QBF aAlfa;
             |SI aAlfa = "422";
               #nomcones#18 = 0; #nomcones#20 = 0;
               #nomconst#23 = 0; #nomcones#24 = 0;
             |FINSI;
    |SI liqu = "T";      || total
            #7#16 = #7#16 + ((#15#16 + #15#19) * #2#47);
            #7#16 = #7#16 - ((#15#16 + #15#19) * #2#46);
            #7#25 = #7#25 + (#15#16 * #2#46);
    |FINSI;
    |SI liqu = "O";      || obrera
            #7#16 = #7#16 + (#15#19 * #2#47);
            #7#16 = #7#16 - (#15#19 * #2#46);
    |FINSI;
    |SI liqu = "P";      || patronal
            #7#16 = #7#16 + (#15#16 * #2#47);
            #7#16 = #7#16 - (#15#16 * #2#46);
            #7#25 = #7#25 + (#15#16 * #2#46);
    |FINSI;

    #7#32 = #7#16 + #7#19 + #7#22 + #7#25 - #7#31 - #7#111;     || casilla 299

    |SI liqu = "T";      || total
               #7#88 = #7#88 + ((#15#18 + #15#20) * #2#47);
               #7#88 = #7#88 - ((#15#18 + #15#20) * #2#46);
               #7#88 = #7#88 + ((#15#23 + #15#24) * #2#81);
               #7#88 = #7#88 - ((#15#23 + #15#24) * #2#82);
               #7#150 = #7#150 + (#15#18 * #2#46);
               #7#150 = #7#150 + (#15#23 * #2#82);
    |FINSI;
    |SI liqu = "O";      || obrera
            #7#88 = #7#88 + (#15#20 * #2#47);
            #7#88 = #7#88 - (#15#20 * #2#46);
            #7#88 = #7#88 + (#15#24 * #2#81);
            #7#88 = #7#88 - (#15#24 * #2#82);
    |FINSI;
    |SI liqu = "P";      || patronal
            #7#88 = #7#88 + (#15#18 * #2#47);
            #7#88 = #7#88 - (#15#18 * #2#46);
            #7#88 = #7#88 + (#15#23 * #2#81);
            #7#88 = #7#88 - (#15#23 * #2#82);
            #7#150 = #7#150 + (#15#18 * #2#46);
            #7#150 = #7#150 + (#15#23 * #2#82);
    |FINSI;
|FINSI;
    #7#89 = #7#89 + #2#12;                   || Bonificaciones inem (*)
    |HAZ SegSocEmp;
    #7#170 = #7#170 + #2#98;                 || Bonificaciones Gar. Juvenil
    #7#90 = #7#88 + #7#150 - #7#89;          || Diferencia
    #7#91 = #7#91 + #2#15;                   || Acumulados disponibles (*)
    #7#92 = #7#90 - #7#91 - #7#170;          || Punto 699
    #7#93 = #7#32 + #7#85 + #7#92;           || Base calculo recargo
    #7#94 = #1#127;                          || Recargo
    #7#97 = (#7#93 % #7#94);                 || TOTAL RECARGO
    ||SI #7#97 < 0; #7#94 = 0; #7#97 = 0; |FINSI;
    total = (#7#32 + #7#85 + #7#92 + #7#97) ! EURODB;    || TOTAL A PAGAR
|SI total > 0; #7#98 = total; #7#99 = 0;        |FINSI;  || A ingresar
|SI total < 0; #7#99 = total * (-1); #7#98 = 0; |FINSI;  || A percibir
    #7#100 = #1#0;                         || Codigo empresa (clave, segmento1)
    #7#101 = #1#30;                        || Codigo banco
    #7#102 = #1#31;                        || Nombre banco
    #7#103 = #0#2;                         || Mes hasta en numero (TC1/92)
    #7#104 = #7#89 + #7#91;                || Punto 610 (TC1/92)
|SI tip = 0;
    |SI siono_cen = "N";   #7#105 = #01#13;   |FINSI;    || Nro. S.S.
    |SI siono_cen = "S";   #7#105 = #10#10;   |FINSI;
|SINO;
        #7#105 = #13#10;
|FINSI;

    #7#106 = #7#103;                       || Mes desde en numero (TC1/92)
    #7#107 = #7#8;                         || A¤o desde (TC1/92)

    #7#108 = #7#108 + #2#17;               || Nro. de TC2/1 (*)
|SI tip2 = 0;
        #7#109 = "";
|SINO;
        #7#109 = "Exp.Reg.Empleo: " + #1#29 + "/" + #1#35;  || nro.exp.
|FINSI;
    #7#115 = #1#113;                       || cuenta corriente
    #7#116 = ~;                            || fecha impresion
    |SI #2#22 = "S";     #7#117 = "N";  |FINSI;   || abreviado NO
    |SI #2#22 = "N";     #7#117 = "S";  |FINSI;   || abreviado SI
    #7#133 = #2#18;                        || centro trabajo (clave,segmento2)
|FPRC;

|PROCESO cen_esp;
|ABRE #13;
|DDEFECTO #13;
    #13#0 = #1#0;
|SI siono_cen = "N";  #13#13 = 1;     |FINSI;  || centro 1 cuando va conjunto
|SI siono_cen = "S";  #13#13 = #2#18; |FINSI;
|SI tip = 8;  #13#1 = 87;  |FINSI;
|SI tip = 9;  #13#1 = 64;  |FINSI;
|LEE 000#13=;
|CIERRA #13;
|FPRC;

|PROCESO mes;
|SI #0#2 = 1;  #7#7 = "Enero     "; |FINSI;
|SI #0#2 = 2;  #7#7 = "Febrero   "; |FINSI;
|SI #0#2 = 3;  #7#7 = "Marzo     "; |FINSI;
|SI #0#2 = 4;  #7#7 = "Abril     "; |FINSI;
|SI #0#2 = 5;  #7#7 = "Mayo      "; |FINSI;
|SI #0#2 = 6;  #7#7 = "Junio     "; |FINSI;
|SI #0#2 = 7;  #7#7 = "Julio     "; |FINSI;
|SI #0#2 = 8;  #7#7 = "Agosto    "; |FINSI;
|SI #0#2 = 9;  #7#7 = "Septiembre"; |FINSI;
|SI #0#2 = 10; #7#7 = "Octubre   "; |FINSI;
|SI #0#2 = 11; #7#7 = "Noviembre "; |FINSI;
|SI #0#2 = 12; #7#7 = "Diciembre "; |FINSI;
|FPRC;

|PROCESO mira_cte;
     alfa1 = #labtraba#37;
     |QBF alfa1;
     |SI alfa1 = ".........."; alfa1 = ""; |FINSI;
     |SI alfa1 ! "";          || si tengo informada fecha de baja
          alfa2 = alfa1 % 402; || mes baja
          alfa3 = alfa1 % 704; || a¤o baja
          alfa1 = "01" + "." + alfa2 + "." + alfa3;

          alfa2 = s_mes;   || mes
          nume3 = s_any;   || a¤o
          alfa3 = nume3;
          alfa2 = "00" + alfa2;    alfa2 = alfa2 % -102;
          alfa4 = "01" + "." + alfa2 + "." + alfa3;

          |SI swFiniqRaro = 1;
               nume3 = nume3 - 1;
               alfa3 = nume3;
               alfa4 = "01" + "." + "12" + "." + alfa3;
          |FINSI;
          fFecha1 = alfa1;    || fecha baja trabajador
          fFecha2 = alfa4;    || fecha emision TC1
          |SI fFecha1 < fFecha2;
               |ACABA;        || trabajador de baja en el mes de emision
          |FINSI;             || no juega
     |FINSI;

     |SI #labtraba#42 = "H";  |ACABA;   |FINSI;   || Los autonomos no juegan
     |DDEFECTO #nomtraca;                         || a esto
     #nomtraca#0 = #labtraba#0;
     #nomtraca#1 = #labtraba#1;
     |LEE 000#nomtraca.=;
     |SI FSalida ! 0;|O #nomtraca#3 = 0;
          swCte = 1;
          |ERROR10;
     |SINO;
          |SI #nomtraca#3 ! nCteAnt; |Y nCteAnt ! 0;
               swCte = 1;
               |ERROR10;
          |FINSI;
          nCteAnt = #nomtraca#3;
     |FINSI;
|FPRC;

|DEFBUCLE trabajadores;
     #labtraba#1;
     77;
     #2#0,     1, #2#18;
     #2#0, 99999, #2#18;
     ;
     NULL, mira_cte;
|FIN;

|PROCESO cte_centro;
     swCte = 0;
     nCteAnt = 0;
     |ABRE #nomtraca;
     |BUCLE trabajadores;
     |CIERRA #nomtraca;
     |SI swCte ! 1;
          #3#0 = nCteAnt;
     |FINSI;
|FPRC;
____________________________________/ IMPRESION DE CADA TC1
|PROCESO sacalo;
|SI hay_algo ! 0;
        seleccio = 1;
    ||INFOR c_informe;
    ||PRINT;
    ||PIEINF;
    ||FININF;

    |GRABA 020#7n;

    |HAZ graba_total;

        cen_ant = #2#18;
        emp_ant = #2#0;
    |DDEFECTO #7;
|FINSI;
|FPRC;

|PROCESO busca_ctefor;
     |SI tip3 = 8;  || FORMACION
          |SI FSalida ! 0;
               alfa1 = "    ATENCION: Existen trabajadores en formacion ";
               alfa1 = alfa1 + "y la empresa no tiene definida la constante ";
               alfa1 = alfa1 + " especial.";
               |MENAV alfa1;
               alfa1 = "    Se pueden originar errores en el TC1 y Lotes RED.";
               |MENAV alfa1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO empresa;
    #1#0 = #2#0;
|LEE 000#1=;                  || lee la empresa
|SI FSalida = 0;                   || AQUI PREGUNTA POR CENTROS S/N TC2!!
    |SI #1#45 ! "S";|Y #1#45 ! "N";     #1#45 = "N";   |FINSI;
    |SI #1#45 ! siono_cen;    |ACABA;   |FINSI;

        #10#0 = #2#0;
        #10#1 = #2#18;
    |LEE 000#10=;
    |SI FSalida = 0;|O #2#18 = 99;
        #3#0 = #1#32;
        |SI siono_cen = "S";
            |HAZ cte_centro;
        |FINSI;
        |LEE 000#3=;          || lee la constante
        |SI FSalida = 0;
            |DDEFECTO #15;
                #15#0 = #1#32;
            |LEE 000#15=;          || lee la constante especial
            |HAZ busca_ctefor;
            |HAZ limpieza;    || prepara segun obrera, patronal o total
            |SI emp_ant = -1;
                    emp_ant = #2#0;
                    cen_ant = #2#18;
            |FINSI;
            |SI emp_ant ! #2#0;
                |HAZ sacalo;
            |SINO;
                |SI cen_ant ! #2#18;|Y siono_cen = "S";
                    |HAZ sacalo;
                |FINSI;
            |FINSI;
            |HAZ tc1;     || calculo del TC1
                hay_algo = 1;
            |HAZ graba_cero;
        |FINSI;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO graba_total;
|SI liqu = "T";
        #14#0 = emp_ant;
        #14#1 = cen_ant;
        #14#2 = #0#2;
        #14#3 = tip3;
        #14#5 = el_period;
        #14#4 = total;
    |GRABA 020#14n;
|FINSI;
|FPRC;

|PROCESO graba_cero;
|SI liqu = "T";
    |LEE 121#2=;
        #2#20 = 0;
    |GRABA 020#2a;
    |LIBERA #2;
|FINSI;
|FPRC;

|PROCESO limpieza;    || Pone a cero constantes segun patronal u obrera
|SI liqu ! "T";
    |SI liqu = "O";
            ini = 6;
            #2#7 = 0;   || plus familiar aux. empresas
            #2#8 = 0;   || asist. subnormal aux. empresas
            #2#9 = 0;   || importe I.L.T. aux. empresas
            #2#10 = 0;  || importe accidentes aux. empresas
            #2#11 = 0;  || bonificaciones contingencias
            #2#12 = 0;  || bonificaciones INEM
            #2#15 = 0;  || acumulados disponibles
            #2#21 = 0;  || maternidad
            #3#74 = 0;   || recargo notarias/ayuntamientos
    |SINO;
            ini = 7;
            #3#5 = 0;    || coeficiente reductor no en patronal
    |FINSI;
    |PARA x = ini; |SI x < 20; |HACIENDO x = x + 2;
            #3x = 0;
    |SIGUE;
|FINSI;
|FPRC;
____________________________________/ CALCULO PRINCIPAL
|PROCESO principal;
            alfa1 = Usuario;  |QBF alfa1;    alfa1 = "6" + alfa1;
        |NOME_DAT #14 alfa1;
        |DELINDEX #14;
        |ABRE #1;      || empresas
        |ABRE #3;      || constantes
        |ABRE #10;     || centros de trabajo
        |ABRE #14;     || tmp. importes
        |ABRE #15;     || constantes especiales 18/93
        |HAZ lecturas;
        |CIERRA #1;
        |CIERRA #3;
        |CIERRA #10;
        |CIERRA #14;
        |CIERRA #15;
        |SI seleccio = 0;
            ||MENAV "    Seleccion vacia ...";
        |SINO;
            |SI liqu = "T";
                |HAZ re_importe;
                |HAZ actua_empresa;
            |FINSI;
        |FINSI;
        |DELINDEX #14;
|FPRC;
____________________________________/ ACTUALIZA IMPORTE TC1 EN AUX.EMPRESAS
|PROCESO importes1;
    #2#0 = #14#0;
    #2#18 = #14#1;
    #2#1 = #14#2;
    #2#2 = #14#3;
|LEE 121#2=;
|SI FSalida = 0;
        #2#20 = #14#4;
    |GRABA 020#2a;
|FINSI;
|LIBERA #2;
|FPRC;

|DEFBUCLE importes;
 #14#1;
 ;
     0,  0,  0,  1,  0;
 99999, 99, 99, 12, 19;
 ;
 NULL, importes1;
|FIN;

|PROCESO re_importe;
|ABRE #2;
|BUCLE importes;
|CIERRA #2;
|FPRC;
____________________________________/ CALCULOS DE LECTURA DE EPIGRAFES
     / LA RUPTURA SE HACE EN EL CUARTO EPIGRAFE POR ADAPTACION AL TC1/16
|PROCESO losseis1;
    cont = 0;       || contador para saber que no pase de 4 (TC1/16)
    epi  = 33;
    trab = 34;
    ilt  = 35;
    ims  = 36;
    bas  = 37;
    imp1 = 38;
    imp2 = 39;
    nCuota_mater = 0;
    por_mat = 0;
    nFPEmp = 0;
    nFPTra = 0;
    nContEmp = 0;
    bon_162001 = 0;
    nBaseERE = 0;
    nILTERE = 0;
    nIMSERE = 0;
|FPRC;
/*
|PROCESO epigrafe_ERE;
     |SI #4#19 ! 0;
          |ABRE #nomepigr;
          |SI s_anyl [ 2006;
               #nomepigr#0 = 126;
          |SINO;
               #nomepigr#0 = 904;
          |FINSI;
          |LEE 000#nomepigr.=;
          |SI FSalida = 0;
               nBaseERE = nBaseERE + #4#19;
               nILTERE = nILTERE + (#4#19 % #nomepigr#1);
               nIMSERE = nIMSERE + (#4#19 % #nomepigr#2);
          |FINSI;
          |CIERRA #nomepigr;
     |FINSI;
|FPRC;
*/

|PROCESO epigrafe_ERE;
     || migue
     |SI #4#19 ! 0;
          nBaseERE = nBaseERE + #4#19;

          nILTERE = nILTERE + #4#22;
          nIMSERE = nIMSERE + #4#23;
     |FINSI;
|FPRC;

|PROCESO losseis2;
    cont = cont + 1;
|SI liqu = "O";                || si es obrera limpia importes epigrafes
        #4#7 = 0;              || importe I.L.T.
        #4#8 = 0;              || importe I.M.S.
|FINSI;
|SI cont [ 4;
        #7epi  = #4#2;                          || Epigrafe
        #7trab = #7trab + #4#6;                 || Numero de trabajadores (*)
        #7ilt  = #4#3;                          || % I.L.T.
        #7ims  = #4#4;                          || % I.M.S.
        #7bas  = #7bas + #4#5;                  || Bases (*)
        #7imp1 = #7imp1 + #4#7;                 || Importe I.L.T. (*)
        #7imp2 = #7imp2 + #4#8;                 || Importe I.M.S. (*)

        epi    = epi  + 7;
        trab   = trab + 7;
        ilt    = ilt  + 7;
        ims    = ims  + 7;   || acumula punteros
        bas    = bas  + 7;
        imp1   = imp1 + 7;
        imp2   = imp2 + 7;
|SINO;
        #7#75 = #7#75 + #4#6;                 || Numero de trabajadores
        #7#76 = #7#76 + #4#5;                 || Bases
        #7#77 = #7#77 + #4#7;                 || Importe I.L.T.
        #7#78 = #7#78 + #4#8;                 || Importe I.M.S.
|FINSI;
#7#148 = #7#148 + #4#14;   || Base Cont. Prof. Maternidad
#7#158 = #7#158 + #4#19;   || Base Cont. Prof. Maternidad
bon_162001 = bon_162001 + #4#15;   || Base bonif. RDL 16/2001 > 65 (2.2.1)
|HAZ epigrafe_ERE;
|SI #2#35 = "X";    || MARCO LA BONIF FORM. CONT SI EXISTE
     #7#118 = "X";
     #7#167 = #2#95;
|FINSI;
|SI #4#17 > 0;
     |SI por_mat ! -1;                || a -1 indica que hay bases pero con
          |SI por_mat > 0;            || porcentajes diferentes
               |SI #4#17 ! por_mat;
                    por_mat = #4#17;
               |FINSI;
          |SINO;
               por_mat = #4#17;
          |FINSI;
     |FINSI;
|FINSI;

nCuota_mater = nCuota_mater + #4#16 + #4#20; || Cuota cont. empres. calculada para
                                     || comparacion por diferencia d constantes
nFPEmp = nFPEmp + #4#12;
nFPTra = nFPTra + #4#13;

|SI liqu = "T";
     nContEmp = nContEmp + #4#16 + #4#18;
|FINSI;
|SI liqu = "O";
     nContEmp = nContEmp + #4#18;
|FINSI;
|SI liqu = "P";
     nContEmp = nContEmp + #4#16;
|FINSI;
|FPRC;

|PROCESO losseis3;
    #7#79 = #7#34 +#7#41 +#7#48 +#7#55 +#7#62 +#7#69 +#7#75; || trabajadores
    #7#80 = #7#37 +#7#44 +#7#51 +#7#58 +#7#65 +#7#72 +#7#76; || bases
    #7#81 = #7#38 +#7#45 +#7#52 +#7#59 +#7#66 +#7#73 +#7#77; || importe I.L.T
    #7#82 = #7#39 +#7#46 +#7#53 +#7#60 +#7#67 +#7#74 +#7#78; || importe I.M.S
    nBaseEREDes = nBaseEREDes + nBaseERE; || me la guardo para sumar a base
    #7#81 = #7#81 + nILTERE;              || epig. a imprimir
    #7#82 = #7#82 + nIMSERE;
    #7#83 = #7#81 + #7#82;                        || Punto 310
    #7#84 = #7#84 + #2#10;                        || Importe accidentes (*)
    #7#85 = #7#83 - #7#84;                        || Punto 499
    || #7#79 = #7#79 + #2#19;
|SI liqu = "P";     #7#145 = #7#145 + (nFPEmp ! EURODB); |FINSI;
|SI liqu = "O";     #7#145 = #7#145 + (nFPTra ! EURODB); |FINSI;
|SI liqu = "T";     #7#145 = #7#145 + (nFPEmp ! EURODB) + (nFPTra ! EURODB); |FINSI;
|FPRC;
____________________________________/ ACTUALIZACION IMPORTES TC1 EMPRESA
|PROCESO graba_tc12;
    #1#0 = emp_ant;
|LEE 101#1=;
|SI FSalida = 0;
        nume1 = #0#2 + 113;
        #1nume1 = total;
    |GRABA 020#1a;
|FINSI;
|LIBERA #1;

    total = 0;
    emp_ant = #2#0;
|FPRC;

|PROCESO graba_tc11;
|SI #2#2 !  5;|Y #2#2 !  6;|Y #2#2 !  7;     || solo los del mes
 |Y #2#2 ! 15;|Y #2#2 ! 16;|Y #2#2 ! 17;

    |SI emp_ant = -1;   emp_ant = #2#0;     |FINSI;

    |SI emp_ant ! #2#0;
        |HAZ graba_tc12;
    |FINSI;
        total = total + #2#20;

|FINSI;
|FPRC;

|PROCESO graba_tc10;
    emp_ant = -1;
    total = 0;
|FPRC;

|DEFBUCLE graba_tc1;
 #2#1;
 ;
 #0#0,  0, #0#2,  0;
 #0#1, 99, #0#2, 19;
 ;
 graba_tc10, graba_tc11, NULL, NULL, graba_tc12;
|FIN;

|PROCESO actua_empresa;
|ABRE #1;
|BUCLE graba_tc1;
|CIERRA #1;
|FPRC;
____________________________________/ CALCULO PRINCIPAL DE LECTURA DE AUX.
|DEFBUCLE auxiliar;
 #2#1;
 ;
 #0#0,  0, #0#2, tip3;
 #0#1, 99, #0#2, tip3;
 ;
 NULL, empresa, NULL, NULL, sacalo;
|FIN;

|PROCESO lecturas;
|NOME_DAT #7 temp1;
|DELINDEX #7;
|ABRE #7;
|PARA tip2 = 0; |SI tip2 [ 10; |HACIENDO tip2 = tip2 + 10;
        sw_red = 0;
    |SI s_pas [ 3;|Y tip2 = 0;                    sw_red = 1;    |FINSI;
    |SI s_pas ] 4;|Y tip2 = 10;|Y s_tipd ! "N";   sw_red = 1;    |FINSI;

    |SI sw_red = 1;
        |SI tip2 = 0;   liqu = s_tip;   |FINSI;
        |SI tip2 = 10;  liqu = s_tipd;  |FINSI;

        |SI s_pas = 1;|O s_pas = 4;
            emp_ant = -1;   cen_ant = -1;
            |SI s_ori ! "C";
                 tip = 0;
            |SINO;
                 tip = 3;
            |FINSI;
            tip3 = tip + tip2;
            hay_algo = 0;   |BUCLE auxiliar;     || todos
        |FINSI;

        |SI s_pas = 2;|O s_pas = 5;
            emp_ant = -1;   cen_ant = -1;
            tip = 8;    tip3 = tip + tip2;
            hay_algo = 0;   |BUCLE auxiliar;     || aprendizaje
        |FINSI;

        |SI s_pas = 3;|O s_pas = 6;
            emp_ant = -1;   cen_ant = -1;
            tip = 9;    tip3 = tip + tip2;
            hay_algo = 0;   |BUCLE auxiliar;     || tiempo parcial
        |FINSI;
    |FINSI;
|SIGUE;

|CIERRA #7;
|FPRC;

|PROGRAMA;
    #0#0 = s_emp;
    #0#1 = s_emp;
    #0#2 = s_mes;
    #0#3 = s_any;
    || s_tip y s_tipd se cargan en el bucle de lecturas

    siono_cen = s_cen;
    runnom = "nomyytc1";
    el_period = #0#3 $ 100;
    |NOME_DAT #2610 aTempEpig;
|NOME_DAT #2 temp4;      || aux.empresas
|NOME_DAT #4 temp5;      || aux.epigrafes
|HAZ principal;
|FPRO;
____________________________________/ LECTURA DE EPIGRAFES
|DEFBUCLE nomyytc11;
 #4#1;
 ;
 #2#0, #2#18, #0#2,   0, tip3;
 #2#0, #2#18, #0#2, 999, tip3;
 ;
 losseis1, losseis2, NULL, NULL, losseis3;
|FIN;

|PROCESO leepitrab;
     nBaseEREDes = 0;    || aqui acumulo base en ERE para uego sumar a la
     |BUCLE nomyytc11;   || que tiene que salir en el TC1
     |HAZ LaCuota;
|FPRC;

__________________/ LECTURA DE AUXILIARES PARA CUOTAS DESEMPLEO

|PROCESO sumacuotaH;
     |SI nAntPorc = #nomwepcd#21; |O swElPrimero = 0;
          nBase = nBase + #nomwepcd#5;
          |SI #nomwepcd#21 = 0;
               nExcFog = nExcFog + #nomwepcd#5;
          |FINSI;

          || RLMigue
          |SI nPintaPorc ! -1;
               nPintaPorc = #nomwepcd#21 + #nomwepcd#23;
          |FINSI;
          nPorFOGASA = #nomwepcd#23;
     |SINO;
          nCuota = (nBase * nAntPorc / 100) ! 2;
          nCuotaTot = nCuotaTot + nCuota;

          || RLMigue
          nCuotaFog = (nBase * #nomwepcd#23 / 100) ! 2;
          nCuotaFogTot = nCuotaFogTot + nCuotaFog;

          nPorFOGASA = #nomwepcd#23;
          nPintaPorc = -1;

          nBase = #nomwepcd#5;
     |FINSI;
     nAntPorc = #nomwepcd#21;
     swElPrimero = swElPrimero + 1;
     || los asimilados los contara primero siempre ya que la clave usada es
     || por el % desempleo; en la variable nExcFog (Excluido Fogasa) meto
     || las bases que van a estar excluidas de FOGASA (las de los asimilados)
     || que asumo que son los que tienen un 0% de desempleo, luego lo restare
|FPRC;

|DEFBUCLE nomwepcd;
     #nomwepcd#2;
     ;
       0, #2#0, nAnyo, #0#2, 00001, #2#18,   0, tip3; || %,Emp,A¤o,Mes,Tra,Cen,Epi,Tip
     100, #2#0, nAnyo, #0#2, 99999, #2#18, 999, tip3; ||
     ;
     NULL, sumacuotaH;
|FIN;

|PROCESO sumacuotaM;
     |SI nAntPorc = #nomtepcd#21; |O swElPrimero = 0;
          nBase = nBase + #nomtepcd#5;
          |SI #nomtepcd#21 = 0;
               nExcFog = nExcFog + #nomtepcd#5;
          |FINSI;
          || RLMigue
          |SI nPintaPorc ! -1;
               nPintaPorc = #nomtepcd#21 + #nomtepcd#23;
          |FINSI;
          nPorFOGASA = #nomtepcd#23;
     |SINO;
          nCuota = (nBase * nAntPorc / 100) ! 2;
          nCuotaTot = nCuotaTot + nCuota;

          || RLMigue
          nCuotaFog = (nBase * #nomtepcd#23 / 100) ! 2;
          nCuotaFogTot = nCuotaFogTot + nCuotaFog;

          nPorFOGASA = #nomtepcd#23;
          nPintaPorc = -1;

          nBase = #nomtepcd#5;
     |FINSI;
     nAntPorc = #nomtepcd#21;
     swElPrimero = swElPrimero + 1;
     || los asimilados los contara primero siempre ya que la clave usada es
     || por el % desempleo; en la variable nExcFog (Excluido Fogasa) meto
     || las bases que van a estar excluidas de FOGASA (las de los asimilados)
     || que asumo que son los que tienen un 0% de desempleo, luego lo restare
|FPRC;

|DEFBUCLE nomtepcd;
     #nomtepcd#2;
     ;
       0, #2#0, #0#2, 00001, #2#18,   0, tip3; || %,Emp,Mes,Tra,Cen,Epi,Tip
     100, #2#0, #0#2, 99999, #2#18, 999, tip3;
     ;
     NULL, sumacuotaM;
|FIN;

|PROCESO LaCuota;
     nBase = 0;
     nCuota = 0;
     nCuotaTot = 0;

     || RLMigue
     nCuotaFog = 0;
     nCuotaFogTot = 0;
     nPorFOGASA = 0;
     nPintaPorc = 0;

     nAntPorc = 0;
     nAnyo = #0#3 - 2000;
     swElPrimero = 0;
     nExcFog = 0;
     nume3 = tip3;
     |SI s_tat = 2;           || si no no salen bien los finiquitos
          tip3 = s_tat;
     |FINSI;
     |SI s_ori = "A"; |O s_ori = "T";        || si no no salen bien los atrasos
          tip3 = 5;
     |FINSI;
     |SI s_ori = "M"; |O s_ori = "A";
          |BUCLE nomtepcd;
     |SINO;
          |BUCLE nomwepcd;
     |FINSI;

     nCuota = (nBase * nAntPorc / 100) ! 2;
     nCuotaTot = nCuotaTot + nCuota;
     nCuotaFog = (nBase * nPorFOGASA / 100) ! 2;
     nCuotaFogTot = nCuotaFogTot + nCuotaFog;
     nCuotaFogTot = nCuotaFogTot - (nExcFog * nPorFOGASA / 100);

     nume1 = nCuotaTot - (nFPTra ! EURODB) - (nFPEmp ! EURODB);
     |SI liqu = "P";
          nCuotaTot = nCuotaTot - (nFPTra ! EURODB);
          nCuotaTot = nCuotaTot - nume1;
     |FINSI;
     |SI liqu = "O";
          nCuotaTot = nCuotaTot - (nFPEmp ! EURODB);
          nCuotaFogTot = 0;
     |FINSI;
     tip3 = nume3;       || lo dejo como estaba
|FPRC;
