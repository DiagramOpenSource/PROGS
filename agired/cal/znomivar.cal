|| ESTE ES EL PLB QUE CON TENDRA PROCESOS AUXILIARES PARA EL CALCULO DE NOMINAS
|| Y QUE TAMBIEN SON COMUNES A OTROS PROGRAMAS, AL IGUAL QUE EN LA iplabor2

|FICHEROS;
     nomconst;
     nompanta;
     nombonif;
     labempre;
     labtraba;
     nomepigr #2610;
     nomocupa;
     nomcnaec;
     labcentr;
     nomcnama; || CNAEs por a¤os
     nomocuma; || ocupaciones por a¤os
     nomcnahi;
     nomocuhi;

     silcon01;
     silcon06;
     labclean;
|FIN;

|VARIABLES;
     &nMes = 0;
     &nAny = 0;
     &nCodigoCons = 0;
     &swConsModif = 0;
     aCodigoCons = "";

     &swCambioDes = 0;
     &swCambioFOG = 0;
     aAlfa = "";
     aAlfa1 = "";
     aAlfa2 = "";
     aModulo = "";

     swCambioBon = 0;
     &nCodigoBon = 0;
     aCodigoBon = "";
     swBonModif = 0;
     swCambio = 0;

     nClaveEpig = 0;
     nLon = 0;
     aLon = "";
     &aCNAE = "";
     &nEpigConv = 0;
     aAvisos = "";
     nNume1 = 0;
     nNume2 = 0;
     nNume3 = 0;
     &aCNAEEmp = "";
     &nElMes = 0;
     &nElAny = 0;

     aMes = "";
     aAny = "";
     aDia = "";
     aFecha = "";
     fFecha = @;
     FEstado = 0;
     &aTempEpig = "";

     aMensa = "";
     aEmp = "";
     aTra = "";
     aCen = "";
     swSigue = 0;
     &fCFechaCot = @;
     &aCErrorCot = "";
     swLocalizado = 0;

     nHandle = 0;
     {1}aContiene = "";
     aOrigen = "";
     aDestino = "";
     aDocRemoto = "";
     aDocServidor = "";
     aIPRemoto = "";
     aAbre = "";
     aEjecuta = "";
     nPausa = 0;
     &eaDSMAC        = "";
     &eaUnidadBasico = "";
     &swQuitaTildes = 0;
|FIN;

|PROCESO CambioAct;
     || si no ha habido cambios es porque ya estan cambiados de antes, la fe-
     || cha de ultima revision debe ser 07/2006 para no dar lugar a errores
     || de que no te deje actualizar despues por detectar cambio de constante

     |SI swCambioDes = 0;
          #nomconst#77 = 2008;
          #nomconst#78 = 07;
          |GRABA 020#nomconst.a;
          |LIBERA #nomconst;
     |FINSI;
|FPRC;

|PROCESO VerifCons;
|ACABA;
     swCambioDes = 0;
     swCambioFOG = 0;
     aCodigoCons = nCodigoCons;
     aCodigoCons = "00" + aCodigoCons;
     aCodigoCons = aCodigoCons % -102;
     swConsModif = 0;

     |MODULO aModulo;
     |QBF aModulo;

     #nomconst#0 = nCodigoCons;
     |LEE 101#nomconst.=;
     |SI FSalida = 0;
          swCambio = 0;
          |SI nAny ] 2009;
               swCambio = 1;
          |SINO;
               |SI nAny = 2008; |Y nMes ] 7;
                    swCambio = 1;
               |FINSI;
          |FINSI;
          |SI swCambio = 1;
               |SI #nomconst#12 = 5.75;         || tipo cotiz por desempleo al 6%
                    swCambioDes = 1;
               |FINSI;
               |SI aModulo = "nomactca";
                    |HAZ CambioAct;
               |FINSI;
          |SINO;
               |SI #nomconst#12 = 5.50;      || tipo cotiz por desempleo al 5.75%
                    swCambioDes = -1;
               |FINSI;
          |FINSI;
     |FINSI;
     |SI aModulo ! "nomcalir";
          |SI swCambioDes = 1;
               |PUSHV 05012380;
               |BLANCO 10012380;
               |PINPA #4#nompanta;
               |ATRI S;
               |PINTA 1542, aCodigoCons;
               |ATRI 0;
               |SI swCambioDes = 1;
                    aAlfa1 = "*El tipo de cotización patronal por desempleo pasa del 5.75%";
                    aAlfa2 = " al 5.50%                                                  ";
                    |PINTA 1811, aAlfa1;
                    |PINTA 1911, aAlfa2;
                    |AVISO;
                    |PAUSA;
               |FINSI;

               |SI aModulo = "silcon10"; |O aAlfa = "g";
                    |SI aModulo ! "gomcalcu";     || solo cuando se vayan a
                         |AVISO;                  || lanzar seguros sociales
                         |PINPA #5#nompanta;      || reg. general o agrarios
                         |PAUSA;
                    |FINSI;
               |FINSI;
               |SI aModulo = "nomactca";
                    |AVISO;
                    |PINPA #5#nompanta;
                    |PINTA 1513, "actualizando las nóminas del mes";
                    |PINTA 1923, " y Seguros Sociales.";
                    |PAUSA;
               |FINSI;
               |POPV;
          |FINSI;
          |SI swCambioDes = 1;
               #nomconst#12 = 5.50;
          |FINSI;
          |SI aModulo ! "nomcalir";
               |GRABA 020#nomconst.a;
               swConsModif = 1;
               |LIBERA #nomconst;
          |FINSI;
     |FINSI;
     |SI swCambioDes = -1;
          |SI swCambioDes = -1;
               #nomconst#12 = 5.75;
          |FINSI;
          swConsModif = -1;
     |FINSI;
|FPRC;

|PROCESO VerifBonif;
|ACABA;
     swCambioBon = 0;
     aCodigoBon = nCodigoBon;
     aCodigoBon = "00" + aCodigoBon;
     aCodigoBon = aCodigoBon % -102;
     swBonModif = 0;

     |MODULO aModulo;
     |QBF aModulo;

     #nombonif#0 = nCodigoBon;
     |LEE 101#nombonif.=;
     |SI FSalida = 0;
          swCambio = 0;
          |SI nAny ] 2009;
               swCambio = 1;
          |SINO;
               |SI nAny = 2008; |Y nMes ] 7;
                    swCambio = 1;
               |FINSI;
          |FINSI;
          |SI swCambio = 1;
               |SI #nombonif#5 = 6.55;          || bonif. desempleo al 6.55% (des+FOG+FP)
                    swCambioBon = 1;
               |FINSI;
               |SI #nombonif#5 = 6.35;       || bonif. desempleo al 6.35% (des+FP)
                    swCambioBon = 2;
               |FINSI;
               |SI #nombonif#5 = 5.95;       || bonif. desempleo al 5.95% (Agrario des+FOG)
                    swCambioBon = 3;
               |FINSI;
               |SI #nombonif#5 = 5.75;       || bonif. desempleo al 5.75% (Agrario des.)
                    swCambioBon = 4;
               |FINSI;
          |SINO;
               |SI #nombonif#5 = 6.30;       || bonif. desempleo al 6.30% (des+FOG+FP)
                    swCambioBon = -1;
               |FINSI;
               |SI #nombonif#5 = 6.10;       || bonif. desempleo al 6.10% (des+FP)
                    swCambioBon = -2;
               |FINSI;
               |SI #nombonif#5 = 5.70;       || bonif. desempleo al 5.70% (Agrario des+FOG)
                    swCambioBon = -3;
               |FINSI;
               |SI #nombonif#5 = 5.50;       || bonif. desempleo al 5.50% (Agrario des.)
                    swCambioBon = -4;
               |FINSI;
          |FINSI;
     |FINSI;
     |SI aModulo ! "nomcalir";
          |SI swCambioBon > 0;
               |PUSHV 05012380;
               |BLANCO 10012380;
               |AVISO;
               |PINPA #6#nompanta;
               |ATRI S;
               |PINTA 1747, aCodigoBon;
               |SI swCambioBon = 1;
                    aAlfa1 = "pasa del 6.55% al 6.30%";
               |FINSI;
               |SI swCambioBon = 2;
                    aAlfa1 = "pasa del 6.35% al 6.10%";
               |FINSI;
               |SI swCambioBon = 3;
                    aAlfa1 = "pasa del 5.95% al 5.70%";
               |FINSI;
               |SI swCambioBon = 4;
                    aAlfa1 = "pasa del 5.75% al 5.50%";
               |FINSI;
               |PINTA 2024, aAlfa1;
               |ATRI 0;
               |PAUSA;
               |POPV;

               |SI swCambioBon = 1;
                    #nombonif#5 = 6.30;
               |FINSI;
               |SI swCambioBon = 2;
                    #nombonif#5 = 6.10;
               |FINSI;
               |SI swCambioBon = 3;
                    #nombonif#5 = 5.70;
               |FINSI;
               |SI swCambioBon = 4;
                    #nombonif#5 = 5.50;
               |FINSI;
               |GRABA 020#nombonif.a;
               |LIBERA #nombonif;
          |FINSI;
          |SI swCambioBon < 0;
               |SI swCambioBon = -1;
                    #nombonif#5 = 6.55;
               |FINSI;
               |SI swCambioBon = -2;
                    #nombonif#5 = 6.35;
               |FINSI;
               |SI swCambioBon = -3;
                    #nombonif#5 = 5.95;
               |FINSI;
               |SI swCambioBon = -4;
                    #nombonif#5 = 5.75;
               |FINSI;
               swBonModif = -1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO PasaOcupa;
     nClaveEpig = nClaveEpig + 1;
     #nomepigr#0 = nClaveEpig;
     #nomepigr#1 = #nomocupa#2;
     #nomepigr#2 = #nomocupa#3;
     |GRABA 020#nomepigr.n;
     #nomocupa#5 = nClaveEpig;
     |GRABA 020#nomocupa.a;
|FPRC;

|DEFBUCLE PasaOcupa;
     #nomocupa#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL, PasaOcupa;
|FIN;

|PROCESO PasaCNAE;
     nClaveEpig = nClaveEpig + 1;
     #nomepigr#0 = nClaveEpig;
     #nomepigr#1 = #nomcnaec#2;
     #nomepigr#2 = #nomcnaec#3;
     |GRABA 020#nomepigr.n;
     |LIBERA #nomepigr;
     #nomcnaec#5 = nClaveEpig;
     |GRABA 020#nomcnaec.a;
     |LIBERA #nomcnaec;
|FPRC;

|DEFBUCLE PasaCNAE;
     #nomcnaec#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL, PasaCNAE;
|FIN;

|PROCESO PaseTablas;
     nClaveEpig = 700;
     |BUCLE PasaCNAE;
     nClaveEpig = 900;
     |BUCLE PasaOcupa;
|FPRC;

|PROCESO CompruebaTablas;
     |MODULO aAlfa;
     |QBF aAlfa;

     |SI aAlfa ! "labtraba"; |Y aAlfa ! "labempre";
     |Y aAlfa ! "nomw0003"; |Y aAlfa ! "nomw0002";
          || para que el epigrafe se quede actualizado al a¤o en curso, pero
          || el nomepigr de verdad, para el resto de operaciones uso
          || los temporales, esto es para los procesos que no esten con-
          || templados y que accedan al nomepigr (simulacion de nomina y cosas
          || asi)
          aTempEpig = Usuario; |QBT aTempEpig; aTempEpig = ("EP" + aTempEpig) % 108;
          |NOME_DAT #2610 aTempEpig;
          |DELINDEX #2610;
     |FINSI;

     |ABRE #nomepigr;
     #nomepigr#0 = 701;
     |LEE 000#nomepigr.=;
     |SI FSalida ! 0;
          || no existe el epigrafe 700, no se han pasado aun las tablas
          |HAZ PaseTablas;
     |FINSI;
     |CIERRA #nomepigr;
|FPRC;

|PROCESO CNAEEmpHis;
     || BUSCO PRIMERO EL CNAE POR EMPRESA

     swSigue = 0;

     |ABRE #nomcnahi;
     #nomcnahi#0 = #labempre#0;
     #nomcnahi#2 = 2009;
     #nomcnahi#4 = 00;
     |LEE 000#nomcnahi.=
     |SI FSalida = 0;
          aAlfa1 = #nomcnahi#3;
          |QBF aAlfa1;
          |SI aAlfa1 ! "";
               swSigue = 1;
          |FINSI;
     |FINSI;

     |SI swSigue = 1;
          aAlfa = #nomcnahi#3;     || me quedo con el CNAE por empresa

          |SI #nomcnahi#3 ! #labempre#128; |Y aCNAEEmp = "";
          /*
               || no saco mensaje de momento

               aEmp = #labempre#0;
               |QBF aEmp;
               aEmp = ("00000" + aEmp) % -105;
               aMensa = "    ATENCION. El CNAE de la empresa " + aEmp;
               aMensa = aMensa + " para el año 2009 (" + aAlfa;
               aMensa = aMensa + ") es distinto al actual (" + #labempre#128 + ").";
               |MENAV aMensa;

               aMensa = "    Se usará el correspondiente al 2009.";
               aMensa = aMensa + " Verifique que el dato es correcto";
               |MENAV aMensa;
          */
          |FINSI;
     |FINSI;

     || BUSCO TAMBIEN POR SI HUBIERA CNAE POR CENTRO

     swSigue = 0;

     |ABRE #labcentr;
     #labcentr#0 = #labempre#0;
     #labcentr#1 = #labtraba#77;
     |LEE 000#labcentr.=;

     #nomcnahi#0 = #labempre#0;
     #nomcnahi#2 = 2009;
     #nomcnahi#4 = #labtraba#77;
     |LEE 000#nomcnahi.=
     |SI FSalida = 0;
          aAlfa1 = #nomcnahi#3;
          |QBF aAlfa1;
          |SI aAlfa1 ! "";
               swSigue = 1;
          |FINSI;
     |FINSI;

     |CIERRA #labcentr;

     |SI swSigue = 1;
          aAlfa = #nomcnahi#3;     || me quedo con el CNAE por centro

          |SI #nomcnahi#3 ! #labcentr#15; |Y aCNAEEmp = "";
          /*
               || no saco mensaje de momento

               aEmp = #labempre#0;
               |QBF aEmp;
               aEmp = ("00000" + aEmp) % -105;
               aCen = #labtraba#77;
               |QBF aCen;
               aCen = ("00" + aCen) % -102;

               aMensa = "    ATENCION. El CNAE de la empresa " + aEmp;
               aMensa = aMensa + ", centro " + aCen + " para el año 2009 (" + aAlfa;
               aMensa = aMensa + ") es distinto al actual (" + #labempre#128 + ").";
               |MENAV aMensa;

               aMensa = "    Se usará el correspondiente al 2009.";
               aMensa = aMensa + " Verifique que el dato es correcto";
               |MENAV aMensa;
          */
          |FINSI;
     |FINSI;
     |CIERRA #nomcnahi;
|FPRC;

|PROCESO CNAEHis;
     || PRIMERO POR TRABAJADOR

     aAlfa = "";    || OJO que en esa variable me guardare el CNAE u Ocup
                    || comprobar al usarla

     swSigue = 1;

     |ABRE #nomocuhi;
     #nomocuhi#2 = 2009;
     #nomocuhi#0 = #labtraba#0;
     #nomocuhi#4 = #labtraba#1;
     |LEE 000#nomocuhi.=
     |SI FSalida = 0;
          aAlfa1 = #nomocuhi#3;
          |QBF aAlfa1;
          |SI aAlfa1 ! "";
               aAlfa = #nomocuhi#3;     || me quedo con la ocupacion del trab.
               swSigue = 0;
               |SI #nomocuhi#3 ! #labtraba#204;
               /*
                    || no saco mensaje de momento
                    aEmp = #labtraba#0;
                    aTra = #labtraba#1;
                    |QBF aEmp;
                    |QBF aTra;
                    aEmp = ("00000" + aEmp) % -105;
                    aTra = ("00000" + aTra) % -105;
                    aMensa = "    ATENCION. La ocupación del trab. " + aEmp + "." + aTra;
                    aMensa = aMensa + " para el año 2009 (" + aAlfa;
                    aMensa = aMensa + ") es distinta a la actual (" + #labtraba#204 + ").";
                    |MENAV aMensa;

                    aMensa = "    Se usará la correspondiente al 2009.";
                    aMensa = aMensa + " Verifique que el dato es correcto";
                    |MENAV aMensa;
               */
               |FINSI;
          |FINSI;
     |FINSI;
     |SI swSigue = 1;
          || no he encontrado ocupacion por trabajador en el historico
          || busco en la ficha del trabajador antes de buscar en el historico
          || por CNAE de Empresa y Centro

          aAlfa = #labtraba#204;                  || si el trabajador tiene ocup
          |QBF aAlfa;                             || me quedo con ella
          |SI aAlfa ! "";
               || ya tengo mi ocupacion
               swSigue = 0;
          |FINSI;
     |FINSI;
     |SI swSigue = 1;
          || SI NO POR CENTRO O EMPRESA
          |HAZ CNAEEmpHis;
     |FINSI;
     |CIERRA #nomocuhi;

     |SI aAlfa ! "";
          aCNAE = aAlfa;
     |FINSI;
|FPRC;

|PROCESO BuscaEquivCNAE;
     swLocalizado = 0;

     aMes = nElMes; aMes = ("00" + aMes) % -102;
     aAny = nElAny;
     aDia = "01";
     aFecha = aDia + "." + aMes + "." + aAny;
     fFecha = aFecha;

     aCNAE = (aCNAE + "          ") % 110;
     |ABRE #nomcnama;
     |SELECT #2#nomcnama;
     #nomcnama#0 = aCNAE;
     #nomcnama#6 = fFecha;
     |LEE 000#nomcnama.];
     |SI FSalida = 0;
          |SI #nomcnama#0 = aCNAE; |Y #nomcnama#6 = fFecha;
               nEpigConv = #nomcnama#5;
               swLocalizado = 1;
          |SINO;
               |LEE 000#nomcnama.a;
               |SI #nomcnama#0 = aCNAE;
                    nEpigConv = #nomcnama#5;
                    swLocalizado = 1;
               |SINO;
                    || NO ENCONTRADO
               |FINSI;
          |FINSI;
     |SINO;
          |LEE 000#nomcnama.u;
          |SI #nomcnama#0 = aCNAE;
               nEpigConv = #nomcnama#5;
               swLocalizado = 1;
          |SINO;
               || NO ENCONTRADO
          |FINSI;
     |FINSI;

     |SI swLocalizado = 0;
          aAlfa2 = " ";
          |PARA nNume1 = 1; |SI aAlfa2 ! ""; |HACIENDO nNume1 = nNume1 + 5;
               nNume2 = (nNume1 * 100) + 5;
               aAlfa2 = aAvisos % nNume2;

               aAlfa1 = #labempre#0;
               aAlfa1 = ("00000" + aAlfa1) % -105;
               |SI aAlfa1 = aAlfa2;
                    aAlfa1 = "";
                    |ACABA;
               |FINSI;
          |SIGUE;
          |SI aAlfa1 ! "";
               aAlfa = "    ATENCION: El CNAE " + aCNAE + " de la empresa " + aAlfa1;
               aAlfa = aAlfa + " no se encuentra. Repase los datos y vuelva a calcular.";
               |SI aModulo ! "nomcalir"; |Y aModulo ! "gomcalc1";
                    |MENAV aAlfa;
               |FINSI;
               aAvisos = aAvisos + aAlfa1;
          |FINSI;
     |FINSI;

     |CIERRA #nomcnama;
|FPRC;

|PROCESO BuscaEquivOcupa;

     aMes = nElMes; aMes = ("00" + aMes) % -102;
     aAny = nElAny;
     aDia = "01";
     aFecha = aDia + "." + aMes + "." + aAny;
     fFecha = aFecha;

     |ABRE #nomocuma;
     |SELECT #2#nomocuma;
     #nomocuma#0 = aCNAE;
     #nomocuma#6 = fFecha;
     |LEE 000#nomocuma.];
     |SI FSalida = 0;
          |SI #nomocuma#0 = aCNAE; |Y #nomocuma#6 = fFecha;
               nEpigConv = #nomocuma#5;
               swLocalizado = 1;
          |SINO;
               |LEE 000#nomocuma.a;
               |SI #nomocuma#0 = aCNAE;
                    nEpigConv = #nomocuma#5;
                    swLocalizado = 1;
               |SINO;
                    || NO ENCONTRADO
               |FINSI;
          |FINSI;
     |SINO;
          |LEE 000#nomocuma.u;
          |SI #nomocuma#0 = aCNAE;
               nEpigConv = #nomocuma#5;
               swLocalizado = 1;
          |SINO;
               || NO ENCONTRADO
          |FINSI;
     |FINSI;

     |SI swLocalizado = 0;
          aAlfa = "    No se ha encontrado la ocupacion " + aCNAE + ".";
          |SI aModulo ! "nomcalir";
               |MENAV aAlfa;
          |FINSI;
     |FINSI;
     |CIERRA #nomocuma;
/*
          |ABRE #nomocupa;
          #nomocupa#0 = aCNAE;
          #nomocuma#6 = fFecha;
          |LEE 000#nomocuma.=;

          |SI FSalida = 0;
               nEpigConv = #nomocupa#5;
          |SINO;
               aAlfa = "    No se ha encontrado la ocupacion " + aCNAE + ".";
               |SI aModulo ! "nomcalir";
                    |MENAV aAlfa;
               |FINSI;
          |FINSI;
          |CIERRA #nomocupa;
*/
|FPRC;

|PROCESO VerifCNAE;
     |HAZ CompruebaTablas;

     aCNAE = "";
     aAlfa = #labtraba#204;                  || si el trabajador tiene ocup
     |QBF aAlfa;                             || me quedo con ella

     |SI aAlfa = "e"; |Y nElAny ] 2013;
          aAlfa = "";
     |FINSI;

     |SI aAlfa ! "";
          aCNAE = aAlfa;
     |SINO;
          aAlfa = #labempre#128;             || si no, en principio el CNAE
          |ABRE #nomcnahi;                   || de la empresa
          |SI nElAny [ 2008;
               #nomcnahi#0 = #labempre#0;    || 2008 o anterior, busco nomcnahi
               #nomcnahi#2 = 2008;
               #nomcnahi#4 = 00;
               |LEE 000#nomcnahi.=
               |SI FSalida = 0;
                    aAlfa1 = #nomcnahi#3;
                    |QBF aAlfa1;
                    |SI aAlfa1 ! "";         || existe y distinto de blanco
                         aAlfa = #nomcnahi#3;     || cojo esta
                    |FINSI;
               |FINSI;
          |FINSI;

          |QBF aAlfa;
          |SI aCNAEEmp ! "";
               aAlfa = aCNAEEmp;
          |FINSI;
          |SI aAlfa ! "";
               aCNAE = aAlfa;
          |FINSI;

          |ABRE #labcentr;
          #labcentr#0 = #labempre#0;
          #labcentr#1 = #labtraba#77;
          |LEE 000#labcentr.=;
          |SI FSalida = 0;
               aAlfa = #labcentr#15;
               |QBF aAlfa;
               |SI aAlfa ! "";
                    |SI nElAny [ 2008;
                         #nomcnahi#0 = #labempre#0;
                         #nomcnahi#2 = 2008;
                         #nomcnahi#4 = #labtraba#77;
                         |LEE 000#nomcnahi.=
                         |SI FSalida = 0;
                              aAlfa = #nomcnahi#3;
                              |QBF aAlfa;
                              aCNAE = aAlfa;
                         |FINSI;
                    |SINO;
                         aCNAE = aAlfa;
                    |FINSI;
               |FINSI;
          |FINSI;
          |CIERRA #labcentr;

          |CIERRA #nomcnahi;
     |FINSI;

     |SI nElAny = 2009;
          |HAZ CNAEHis;
     |FINSI;

     aAlfa2 = " ";
     |SI aCNAE = "";
          |PARA nNume1 = 1; |SI aAlfa2 ! ""; |HACIENDO nNume1 = nNume1 + 5;
               nNume2 = (nNume1 * 100) + 5;
               aAlfa2 = aAvisos % nNume2;

               aAlfa1 = #labempre#0;
               aAlfa1 = ("00000" + aAlfa1) % -105;
               |SI aAlfa1 = aAlfa2;
                    aAlfa1 = "";
                    |ACABA;
               |FINSI;
          |SIGUE;
          |SI aAlfa1 ! "";
               aAlfa = "    ATENCION: La empresa " + aAlfa1 + " no tiene CNAE. ";
               aAlfa = aAlfa + "Repase los datos y vuelva a calcular.";
               |MENAV aAlfa;
               aAvisos = aAvisos + aAlfa1;
               |ACABA;
          |FINSI;
     |FINSI;

     nEpigConv = #labtraba#32;
     |QBF aCNAE;
     |SI aCNAE = "52."; aCNAE = "52"; |FINSI;
     aLon = aCNAE % 0;
     nLon = aLon;

     |MODULO aModulo;
     |QBF aModulo;

     |SI nLon = 1;
          || miro en la tabla de ocupaciones
          |HAZ BuscaEquivOcupa;
     |SINO;
          || miro en la tabla de CNAEs
          |HAZ BuscaEquivCNAE;
     |FINSI;
|FPRC;
/*
|PROCESO nomocupa;
     #nomepigr#0 = #nomocupa#5;
     |LEE 101#nomepigr.=;
     FEstado = FSalida;

     #nomepigr#1 = #nomocupa#2;
     #nomepigr#2 = #nomocupa#3;

     |SI FEstado = 0;
          |GRABA 020#nomepigr.a;
     |SINO;
          |GRABA 020#nomepigr.n;
     |FINSI;
     |LIBERA #nomepigr;
|FPRC;

|DEFBUCLE nomocupa;
     #nomocupa#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, nomocupa;
|FIN;

|PROCESO nomcnaec;
     #nomepigr#0 = #nomcnaec#5;
     |LEE 101#nomepigr.=;
     FEstado = FSalida;

     #nomepigr#1 = #nomcnaec#2;
     #nomepigr#2 = #nomcnaec#3;

     |SI FEstado = 0;
          |GRABA 020#nomepigr.a;
     |SINO;
          |GRABA 020#nomepigr.n;
     |FINSI;
     |LIBERA #nomepigr;
|FPRC;

|DEFBUCLE nomcnaec;
     #nomcnaec#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, nomcnaec;
|FIN;
*/

|PROCESO nomocuma;
     || primero cargo el nomocupa, esta tabla me sirve para consulta desde
     || el labempre o el labtraba por ejemplo, ya no paso por aqui para
     || nomina o seguros sociales

     #nomocupa#0 = #nomocuma#0;
     |LEE 101#nomocupa.=;
     FEstado = FSalida;

     #nomocupa#1 = #nomocuma#1;
     #nomocupa#2 = #nomocuma#2;
     #nomocupa#3 = #nomocuma#3;
     #nomocupa#4 = #nomocuma#4;
     #nomocupa#5 = #nomocuma#5;

     |SI FEstado = 0;
          |GRABA 020#nomocupa.a;
     |SINO;
          |GRABA 020#nomocupa.n;
     |FINSI;
     |LIBERA #nomocupa;

     || y ahora directamente desde el nomocuma cargo la tabla de epigrafes
     || en la que voy a mirar para calculo de nomina y seguros sociales

     #nomepigr#0 = #nomocuma#5;
     |LEE 101#nomepigr.=;
     FEstado = FSalida;

     #nomepigr#1 = #nomocuma#2;
     #nomepigr#2 = #nomocuma#3;

     |SI FEstado = 0;
          |GRABA 020#nomepigr.a;
     |SINO;
          |GRABA 020#nomepigr.n;
     |FINSI;
     |LIBERA #nomepigr;
|FPRC;

|DEFBUCLE nomocuma;
     #nomocuma#1;
     ;
     fFecha, " ";
     fFecha, "z";
     ;
     NULL, nomocuma;
|FIN;

|PROCESO nomcnama;
     || primero cargo el nomcnaec, esta tabla me sirve para consulta desde
     || el labempre o el labtraba por ejemplo, ya no paso por aqui para
     || nomina o seguros sociales

     #nomcnaec#0 = #nomcnama#0;
     |LEE 101#nomcnaec.=;
     FEstado = FSalida;

     #nomcnaec#1 = #nomcnama#1;
     #nomcnaec#2 = #nomcnama#2;
     #nomcnaec#3 = #nomcnama#3;
     #nomcnaec#4 = #nomcnama#4;
     #nomcnaec#5 = #nomcnama#5;

     |SI FEstado = 0;
          |GRABA 020#nomcnaec.a;
     |SINO;
          |GRABA 020#nomcnaec.n;
     |FINSI;
     |LIBERA #nomcnaec;

     || y ahora directamente desde el nomcnama cargo la tabla de epigrafes
     || en la que voy a mirar para calculo de nomina y seguros sociales

     #nomepigr#0 = #nomcnama#5;
     |LEE 101#nomepigr.=;
     FEstado = FSalida;

     #nomepigr#1 = #nomcnama#2;
     #nomepigr#2 = #nomcnama#3;

     |SI FEstado = 0;
          |GRABA 020#nomepigr.a;
     |SINO;
          |GRABA 020#nomepigr.n;
     |FINSI;
     |LIBERA #nomepigr;
|FPRC;

|DEFBUCLE nomcnama;
     #nomcnama#1;
     ;
     fFecha, "          ";
     fFecha, "zzzzzzzzzz";
     ;
     NULL, nomcnama;
|FIN;

|PROCESO Actualizador;
     |MODULO aAlfa;
     |QBF aAlfa;

     |SI aAlfa ! "labtraba"; |Y aAlfa ! "labempre";
     |Y aAlfa ! "nomw0003"; |Y aAlfa ! "nomw0002";
          || para que el epigrafe se quede actualizado al a¤o en curso, pero
          || el nomepigr de verdad, para el resto de operaciones uso
          || los temporales, esto es para los procesos que no esten con-
          || templados y que accedan al nomepigr (simulacion de nomina y cosas
          || asi)
          aTempEpig = Usuario; |QBT aTempEpig; aTempEpig = ("EP" + aTempEpig) % 108;
          |NOME_DAT #2610 aTempEpig;
          |DELINDEX #2610;
     |FINSI;

     |ABRE #nomepigr;

     |DELINDEX #nomcnaec;
     |ABRE #nomcnaec;
     |BUCLE nomcnama;
     |CIERRA #nomcnaec;

     |DELINDEX #nomocupa;
     |ABRE #nomocupa;
     |BUCLE nomocuma;
     |CIERRA #nomocupa;

     /*
     |BUCLE nomcnaec;
     |BUCLE nomocupa;
     */

     |CIERRA #nomepigr;
|FPRC;

|PROCESO ValoresITIMS;
     |SI nElAny < 2007;
          |ACABA;
     |FINSI;

     aMes = nElMes; aMes = ("00" + aMes) % -102;
     aAny = nElAny;
     aDia = "01";
     aFecha = aDia + "." + aMes + "." + aAny;
     fFecha = aFecha;

     |ABRE #nomcnama;
     #nomcnama#0 = "          ";
     #nomcnama#6 = fFecha;
     |LEE 000#nomcnama.];
     |SI FSalida = 0;
          |SI #nomcnama#0 = "          "; |Y #nomcnama#6 = fFecha;
               |HAZ Actualizador;
          |SINO;
               |LEE 000#nomcnama.a;
               fFecha = #nomcnama#6;
               |HAZ Actualizador;
          |FINSI;
     |SINO;
          |LEE 000#nomcnama.u;
          fFecha = #nomcnama#6;
          |HAZ Actualizador;
     |FINSI;

     |CIERRA #nomcnama;
|FPRC;

|PROCESO LeeUnidadBasico;
     aContiene = "";
     |ESPECIFICA "RBASS", aContiene;
     eaUnidadBasico = (aContiene1 % 102) > "";

     aAlfa = eaUnidadBasico % -101;
     |SI aAlfa ! ":";
         eaUnidadBasico = "";
     |FINSI;
|FPRC;

|PROCESO CogeMac;
     |SI eaDSMAC ! "";  |ACABA;  |FINSI;

     |HAZ LeeUnidadBasico;

     aAlfa = eaUnidadBasico + "\DOCUMENTOS"; |RMKDIR aAlfa;
     aAlfa = aAlfa + "\DSMAC";               |RMKDIR aAlfa;

     |DBASE aAlfa;   |QBF aAlfa;
     aAlfa      = aAlfa + "ejecutables/";
     aOrigen   = aAlfa + "dsmac.exe";

     aDestino  = eaUnidadBasico + "\DOCUMENTOS\DSMAC\DSMAC.EXE";

     aContiene1 = aDestino;
     |ESPECIFICA "REMOTOMD5", aContiene;
     aDocRemoto = FSalida;  |QBF aDocRemoto;

     aContiene1 = aOrigen;
     |ESPECIFICA "MD5", aContiene;
     aDocServidor = FSalida;  |QBF aDocServidor;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |SI aDocRemoto ! aDocServidor;
         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aOrigen, aDestino;
         |SINO;
             |COPIA_FICHERO aOrigen, aDestino;
         |FINSI;
     |FINSI;

     |XABRE "EC", aDestino, nHandle;
     |SI FSalida < 0;
         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aOrigen, aDestino;
         |SINO;
             |COPIA_FICHERO aOrigen, aDestino;
         |FINSI;
     |FINSI;

     aAbre = eaUnidadBasico + "\DOCUMENTOS\DSMAC\DSMAC.TXT";  |RFBORRA aAbre;

     aEjecuta = "cmd " + &34 + "/c START /WAIT " + aDestino + " ";
     aEjecuta = aEjecuta +  eaUnidadBasico + "\DOCUMENTOS\DSMAC\DSMAC.TXT" + &34;
     |RSYSTEM aEjecuta;

     aAbre    = eaUnidadBasico + "\DOCUMENTOS\DSMAC\DSMAC.TXT";
     |XABRE "EC", aAbre, nHandle;
     |SI FSalida < 0;
         aEjecuta = "START "+ aDestino + " ";
         aEjecuta = aEjecuta +  eaUnidadBasico + "\DOCUMENTOS\DSMAC\DSMAC.TXT" + &34;
         |RSYSTEM aEjecuta;

         |PARA;  |SI;  |HACIENDO;
                 |PARA nPausa = 1; |SI nPausa [ 10000; |HACIENDO nPausa = nPausa + 1;
                 |SIGUE;

                 |XABRE "EC", aAbre, nHandle;
                 |SI FSalida ] 0;
                     |SAL;
                 |FINSI;

                 |PULSATECLA;
         |SIGUE;
     |FINSI;

     eaDSMAC   = "";
     aAbre    = eaUnidadBasico + "\DOCUMENTOS\DSMAC\DSMAC.TXT";
     |XABRE "CU", aAbre, nHandle;
     |SI FSalida < 0;   |ACABA;  |FINSI;

     |XLEE nHandle, 250, eaDSMAC;
     |SI FSalida < 0
         eaDSMAC   = "";
     |FINSI;
     |XCIERRA nHandle;

     |QBF eaDSMAC;
|FPRC;

|PROCESO silcon06;
     #silcon06#5 = #silcon01#0;
     |GRABA 020#silcon06.a;
     |LIBERA #silcon06;
|FPRC;

|DEFBUCLE silcon06;
     #silcon06#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL, silcon06;
|FIN;

|PROCESO PaseAutorizacion;
     |ABRE #silcon01;
     |ABRE #silcon06;
     |ABRE #labclean;

     #labclean#0 = "silcon06";
     #labclean#1 = 5;
     |LEE 101#labclean.=;
     |SI FSalida ! 0;
          || lo que tenga que hacer

          |LEE 000#silcon01.p;
          |SI FSalida = 0;
               |BUCLE silcon06;
          |FINSI;

          #labclean#0 = "silcon06";
          #labclean#1 = 5;
          #labclean#2 = "S";
          |GRABA 020#labclean.n;
          |LIBERA #labclean;
     |FINSI;
     |CIERRA #labclean;
     |CIERRA #silcon06;
     |CIERRA #silcon01;
|FPRC;
