|FICHEROS;
    silcon14 #0;
    silcon01 #1;         || control directorio
    silcon02 #2;         || lotes cabs.
    tsilcon0 #3;         || pantalla campos
    silcon04 #4;         || campos auxiliares
    silcon03 #5;         || lotes lins.
    silcon06 #6;         || empresas silcon
    silcon07 #7;         || registro lotes
    labempre #10;        || empresas
    labtraba #12;        || trabajadores
    nomcenes #13;        || centros especiales
    labcentr #14;        || centros
    labpadre #15;        || datos aux. trabajador
    tsilcon7 #20,MANTE;  || movimientos AFI seleccionados (temporal)
    silcon13 #21;        || movimientos AFI
    silcon15 #25;        || gestion centros RED
    nomcontr #36;

    nomequiv #101;       || tabla equivalencias contratos nuevos
     nomconca;
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    alfa5 = "";
    alfa6 = "";
    aAlfa = "";
    aAlfa1 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;
    p_mes = "";
    inkey = 0;

    dlote = 0;
    hlote = 0;

    ajuste1 = "";
    ajuste2 = "";
    ajuste3 = "";
    alfa = "";
    error = 0;
    ind = 0;
    long = 0;
    posi = 0;
    digito = "";
    texto = "";
    mascara = "";
    cadena = "";
    i = 0;
    nCampo = 0;
    nTipo = 0;

    fichero = "";
    extension = "";
    clave = "";
    sw_auxil = 0;
    linea = 0;
    for_fecha = "";
    for_hora = "";
    apell1 = "";
    apell2 = "";
    nombre = "";
    descom = "";
    entero = "";
    decima = "";
    tot_emp = 0;
    indica = "";
    ant_empre = 0;
    ant_ctacot = 0;
    sw_empresa = 0;
    lote = 0;
    sw_comprueba = 0;
    &incluido = "";
    &los_si = 0;
    &los_no = 0;
    &emp_com = 0;
    &sw_linea = 0;
    ctacot = 0;
    &ctacotalf = "";
    aCto = "";
    nSexo = 0;
    aContrato = "";
    nRegimen = 0;
    swNoGrabes = 0;
    swEsAgrario = 0;
    nVacios = 0;
    aValor = "";
    nValor = 0;
    nLimite = 0;
    nPara1 = 0;
     swSigue = 0;
     nNume = 0;
     nNume1 = 0;
     nNume2 = 0;
     aAlfa2 = "";
     nPosicion = 0;
     aAutorizacion = "";
     &eaCadena = "";
     &eaCaracter1 = "";
     &eaCaracter2 = "";
     &eaCadenaSal = "";
|FIN;

____________________________________/ GESTION PANTALLA
|PROCESO no_entres; |TIPO 7;
|SI #0#5 = "R";
        #0#4 = "S";
    |PINTA #0#4;
    |C_M #0#4, 1, "N";
|SINO;
    |C_M #0#4, 1, "S";
|FINSI;
|FPRC;

|PROCESO carga_seleccion1;
     |PARA para1 = 0; |SI para1 [ 65; |HACIENDO para1 = para1 + 1;
          #20para1 = #21para1;
          |SI para1 = 42;
               eaCadena = #20para1;
               eaCaracter1 = "X";
               eaCaracter2 = "S";
               |HAZ sustituye_plb;
               #20para1 = eaCadenaSal;
          |FINSI;
     |SIGUE;
     |SI #0#5 = "N";     #20#26 = "*";  |FINSI;   || normal
     |SI #0#5 = "R";     #20#26 = " ";  |FINSI;   || reenvio
     |GRABA 020#20n;
|FPRC;

|DEFBUCLE carga_seleccion;
 #21#3;
 ;
 dlote,     1;
 hlote, 99999;
 ;
 NULL, carga_seleccion1;
|FIN;

|PROCESO carga_inicial;
|MENSA "    Preparando movimientos ...";
|DELINDEX #20;
|ABRE #20;
    para1 = 0;
|BUCLE carga_seleccion;
|CIERRA #20;
|BLIN 4;
|SI para1 = 0;
    |SI #0#5 = "N";
        |MENAV "    No hay ningun movimiento pendiente de incluir en lotes ...";
    |SINO;
        |MENAV "    No hay ningun movimiento incluido en lotes ...";
    |FINSI;
|FINSI;
|FPRC;

|PROCESO min20;
    #20#14 = dlote;
    #20#0 = 1;
|FPRC;

|PROCESO max20;
    #20#14 = hlote;
    #20#0 = 99999;
|FPRC;

|PROCESO seleccion; |TIPO 0;
    alfa1 = Contenido;
    inkey = FSalida;

|SI #0#5 = "N"; dlote = 0;     hlote = 0;     |FINSI;
|SI #0#5 = "R"; dlote = 1;     hlote = 99999; |FINSI;

|SI elsw1 = 0;
    |HAZ carga_inicial;
        elsw1 = 1;
|FINSI;

|SI alfa1 ! #0#4;   || si ha cambiado
    |SI #0#4 = "N";
        |HAZ carga_inicial;
    |FINSI;
|FINSI;

|SI inkey ! 2;|Y #0#4 = "S";
    |ABRE #20;
    |LEE 000#20p;
        FEstado = FSalida;
    |CIERRA #20;

    |SI FEstado = 0;
        |PUSHV 0501 2380;
        |ENTLINEAL #3#20, 2, 4, 19, 4, min20, max20;
        |POPV;
    |SINO;
        |SI #0#5 = "N";
            |MENAV "    No hay ningun movimiento pendiente de incluir en lotes ...";
        |SINO;
            |MENAV "    No hay ningun movimiento incluido en lotes ...";
        |FINSI;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO tipo_envio; |TIPO 0;
    alfa1 = Contenido;
|SI alfa1 ! #0#5;|Y #0#4 = "S";
    |AVISO;
    |CONFI "    NSe invalidara la seleccion anterior. Continuar (S/N): ";
    |SI FSalida = 0;
        |SI #0#5 = "N"; dlote = 0;     hlote = 0;     |FINSI;
        |SI #0#5 = "R"; dlote = 1;     hlote = 99999; |FINSI;
        |HAZ carga_inicial;
    |SINO;
            #0#5 = Contenido;
        |PINTA #0#5;
        |ERROR;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO tipo8; |TIPO 8;
     |ABRE #1;
     |LEE 100#1p;
     |SI FSalida ! 0;
         |SI FSalida = 111;
             |MENAV "    Debera definir 'Maestros/Control R.E.D.'";
         |FINSI;
         |CIERRA #1;
         |PTEC 807;
     |FINSI;

     |HAZ PaseAutorizacion;
|FPRC;

|PROCESO tipo9; |TIPO 9;
|LIBERA #1;
|CIERRA #1;
|DELINDEX #20;
|FPRC;

|PROCESO control;
|DDEFECTO #36;
|ABRE #36;
|LEE 000#36p;
|SI FSalida ! 0;
    |MENAV "    No existe control, usando defectos ...";
|FINSI;
|CIERRA #36;
|FPRC;

|PROCESO defecto; |TIPO 7;
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % -104;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO mirames; |TIPO 7;
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % 402;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO pintames; |TIPO 0;
|SI #0Campo =  1;  p_mes = "ENERO     ";  |FINSI;
|SI #0Campo =  2;  p_mes = "FEBRERO   ";  |FINSI;
|SI #0Campo =  3;  p_mes = "MARZO     ";  |FINSI;
|SI #0Campo =  4;  p_mes = "ABRIL     ";  |FINSI;
|SI #0Campo =  5;  p_mes = "MAYO      ";  |FINSI;
|SI #0Campo =  6;  p_mes = "JUNIO     ";  |FINSI;
|SI #0Campo =  7;  p_mes = "JULIO     ";  |FINSI;
|SI #0Campo =  8;  p_mes = "AGOSTO    ";  |FINSI;
|SI #0Campo =  9;  p_mes = "SEPTIEMBRE";  |FINSI;
|SI #0Campo = 10;  p_mes = "OCTUBRE   ";  |FINSI;
|SI #0Campo = 11;  p_mes = "NOVIEMBRE ";  |FINSI;
|SI #0Campo = 12;  p_mes = "DICIEMBRE ";  |FINSI;
|PINTA 1043, p_mes;
|FPRC;

|PROCESO ult_lote; |TIPO 7;
|ABRE #2;
|LEE 000#2u;
|SI FSalida = 0;
        #0#2 = #2#0 + 1;
|SINO;
        #0#2 = 1;
|FINSI;
|PINTA #0#2;
|CIERRA #2;
|FPRC;

|PROCESO si_existe; |TIPO 0;
|ABRE #2;
    #2#0 = #0#2;
|LEE 000#2=;
|SI FSalida = 0;
    |MENAV "    Numero de Lote YA existente ...";
    |ERROR;
|FINSI;
|CIERRA #2;
|FPRC;

|PROCESO mayus; |TIPO 0;
    #0Campo = #0Campo > #0Campo;
|PINTA #0Campo;
|FPRC;

|PROCESO no_vacio; |TIPO 0;
    alfa1 = #0Campo;
|QBT alfa1;
|SI alfa1 = "";
    |MENAV "    Error de entrada ...";
    |ERROR;
|FINSI;
|FPRC;
____________________________________/ FORMATEO DE DATOS
|PROCESO errores;
|SI error = 1;
        alfa1 = "    "+i+" "+clave+":VALOR DE CAMPO NUMERICO INCORRECTO";
|FINSI;
|SI error = 2;
        alfa1 = "    "+i+" "+clave+":CAMPO OBLIGADO SIN RELLENO";
|FINSI;
|MENAV alfa1;
|FPRC;

|PROCESO comp1;               || COMPRUEBA SI ES NUMERICO.
     error = 0;
     |SI ajuste1 = "1";
          |PARA ind = 1; |SI ind [ long; |HACIENDO ind = ind + 1;
               posi = 100 * ind + 1;
               digito = texto % posi;
               |SI digito ! " ";
                    |SI digito < "0"; |O digito > "9";
                         error = 1;
                         |SAL;
                    |FINSI;
               |FINSI;
          |SIGUE;
     |FINSI;
|FPRC;

|PROCESO comp2;               || COMPRUEBA SI TIENE QUE ESTAR RELLENO.
     error = 0;
     |SI ajuste3 = "3";
          FSalida = 0;
          |QBF alfa;
          |PARA ind = 1; |SI FSalida = 0; |HACIENDO ind = ind + 1;
               posi = 100 * ind + 1;
               digito = texto % posi;
               |SI digito = "";
                    error = 2;
                    |SAL;
               |FINSI;
          |SIGUE;
     |FINSI;
|FPRC;

|PROCESO comp3;               || AJUSTA I, o D. Y RELLENA DE 0 o BLANCOS.
     |SI ajuste3 = "1";       || RELLENA BLANCOS.
          mascara = "                                                                    ";
     |FINSI;
     |SI ajuste3 = "2";       || RELLENA CEROS.
          mascara = "00000000000000000000000000000000000000000000000000000000000000000000";
     |FINSI;
     |SI ajuste2 = "1";       || AJUSTA DERECHA.
          posi = 100 + long;
          posi = posi * -1;
          |QBF texto;
          texto = mascara + texto;
          texto = texto % posi;
     |FINSI;
     |SI ajuste2 = "2";
          posi = 100 + long;
          |QBF texto;
          texto = texto + mascara;
          texto = texto % posi;
     |FINSI;
|FPRC;

|PROCESO monta;
     texto = "";
     cadena = "";
     |PARA i = 0; |SI i < 30; |HACIENDO i = i + 1;
          nCampo = i;
          texto = #3nCampo;
          nTipo = i * 6 + 2;                 || MODIFICABLE.
          alfa  = #4nTipo;
          nTipo = i * 6 + 2;                 || VISUAL.
          alfa1 = #4nTipo;
          |SI alfa = "S"; |O alfa1 = "S";
               nTipo = i * 6 + 6;            || Longitud.
               long  = #4nTipo;
               nTipo = i * 6 + 7;            || AJUSTE.
               alfa  = #4nTipo;
               ajuste1 = alfa % 101;
               ajuste2 = alfa % 201;
               ajuste3 = alfa % 301;
               |HAZ comp1;
               |SI error = 1; |HAZ errores; |ACABA; |FINSI;
               |HAZ comp2;
               |SI error = 2; |HAZ errores; |ACABA; |FINSI;
               |HAZ comp3;
               cadena = cadena + texto;
          |FINSI;
     |SIGUE;
|FPRC;
____________________________________/ RUTINAS DE APOYO
|PROCESO descompone;
    entero = "";
    decima = "";
    elsw1 = 0;
    alfa1 = descom % 0;   long = alfa1;
|PARA para1 = 1; |SI para1 [ long; |HACIENDO para1 = para1 + 1;
        nume1 = (para1 * 100) + 1;
        alfa1 = descom % nume1;
    |SI alfa1 = ".";
            elsw1 = 1;
    |SINO;
        |SI elsw1 = 0;   entero = entero + alfa1; |FINSI;
        |SI elsw1 = 1;   decima = decima + alfa1; |FINSI;
    |FINSI;
|SIGUE;

|SI entero = "";    entero = "0";  |FINSI;
|SI decima = "";    decima = "0";  |FINSI;
|FPRC;

|PROCESO separa_nom;
    alfa1 = #12#2; |QBF alfa1;
    alfa2 = alfa1 % 0;   para2 = alfa2;
    elsw4 = 0;
    apell1 = "";
    apell2 = "";
    nombre = "";
|PARA para1 = 1; |SI para1 [ para2; |HACIENDO para1 = para1 + 1;
        nume2 = (para1 * 100) + 1;
        alfa3 = alfa1 % nume2;
    |SI alfa3 = " ";|O alfa3 = ",";
        |SI apell1 ! "";
                elsw4 = elsw4 + 1;
        |FINSI;
        |SI elsw4 ] 2;|Y nombre ! "";
                nombre = nombre + alfa3;
        |FINSI;
    |SINO;
        |SI elsw4 = 0;  apell1 = apell1 + alfa3; |FINSI;
        |SI elsw4 = 1;  apell2 = apell2 + alfa3; |FINSI;
        |SI elsw4 ] 2;  nombre = nombre + alfa3; |FINSI;
    |FINSI;
|SIGUE;
|FPRC;

|PROCESO fecha;
    for_fecha = fech1;
    for_fecha = (for_fecha % -104) + (for_fecha % 402) + (for_fecha % 102);
|FPRC;

|PROCESO hora;
|HORA for_hora;
    for_hora = (for_hora % 102) + (for_hora % 402);
|FPRC;

|PROCESO ini_linea;
|DDEFECTO #5;
    linea = linea + 1;

    #5#0 = #2#0;
    #5#1 = linea;
    #5#2 = "A";
    #5#3 = clave;
|GRABA 020#5b;

    sw_auxil = 0;
    #4#0 = #5#2;
    #4#1 = #5#3;
|LEE 000#4=;
|SI FSalida ! 0;
        alfa1 = "    Descripcion [" + #4#0 + "-" + #4#1 + "] sin definir ...";
    |MENAV alfa1;
        sw_auxil = 1;
|FINSI;
|FPRC;

|PROCESO fin_linea;
    #5#4 = cadena;
|GRABA 020#5a;
|LIBERA #5;
|FPRC;

|PROCESO fin_linea2;
    linea = linea - 1;
|BORRA 020#5a;
|LIBERA #5;
|FPRC;
____________________________________/ CARGA DE LOS DIFERENTES REGISTROS
|PROCESO actua_registro;
|ABRE #7;
    #7#0 = ant_empre;    || empresa
    #7#1 = #0#2;         || nro.lote
    #7#2 = #0#5;         || indicador
|LEE 101#7=;
    FEstado = FSalida;

    #7#3 = #0#0;         || a¤o
    #7#4 = #0#1;         || mes
    #7#5 = ~;            || fecha envio
    #7#6 = "01.01.1900"; || fecha comprobacion
    #7#7 = "AFI";        || tipo lote

|SI FEstado = 0;    |GRABA 020#7a; |FINSI;
|SI FEstado ! 0;    |GRABA 020#7n; |FINSI;
|LIBERA #7;
|CIERRA #7;
|FPRC;

|PROCESO etf;
    clave = "ETF";
|HAZ ini_linea;
|SI sw_auxil = 0;
    |DDEFECTO #3;
        #3#0  = "AFI8";
        #3#1  = "3";
        #3#2  = "WS83";
        #3#3  = "0";
        #3#4  = aAutorizacion;         || clave autorizacion
        #3#5  = #1#1;         || clave SILCON
        fech1 = ~;
    |HAZ fecha;
        #3#4  = aAutorizacion;         || clave autorizacion
        #3#5  = #1#1;         || clave SILCON
        fech1 = ~;
    |HAZ fecha;
        #3#6  = for_fecha;    || fecha
        #3#7  = for_hora;     || hora
        #3#8  = fichero;      || fichero
        #3#9  = extension;    || sufijo
        #3#10 = "N";          || codigo prioridad
        #3#11 = indica;       || indicador
        #3#12 = tot_emp;      || total segmentos EMP
        #3#13 = linea;        || total segmentos
        #3#14 = "";           || reservado
    |HAZ monta;
    |HAZ fin_linea;
|SINO;
    |HAZ fin_linea2;
|FINSI;
|FPRC;

|PROCESO otd;
     |SI #20#3 ! "MA "; |Y #20#3 ! "ACT";
          |ACABA;
     |FINSI;
     clave = "OTD";
     |HAZ ini_linea;
     |SI sw_auxil = 0;
          |DDEFECTO #3;

          |ABRE #nomconca;

          #nomconca#0 = #labtraba#87;
          |LEE 000#nomconca.=;
          |SI FSalida = 0;
               aAlfa = #nomconca#142;
          |FINSI;

          |CIERRA #nomconca;

          nNume = aAlfa;
          #3#0 = nNume;       || convenio colectivo
          #3#1 = "00000000";  || fecha inicio excedencia
          #3#2 = "0000";      || CCC inicio excedencia: Regimen
          #3#3 = "00";        || CCC inicio excedencia: Provincia
          #3#4 = "000000000"; || CCC inicio excedencia: Numero
          #3#5 = "00000000";  || Fecha notificacion
          #3#6 = "";          || Indicativo readmision
          #3#7 = "00000000";  || Fecha prevista fin de contrato
          #3#8 = "";          || reservado

          |HAZ monta;
          |HAZ fin_linea;
     |SINO;
          |HAZ fin_linea2;
     |FINSI;
|FPRC;

|PROCESO dam;
     |SI #20#3 = "ACT";
          |ACABA;
     |FINSI;
     clave = "DAM";
     |HAZ ini_linea;
     |SI sw_auxil = 0;
          |DDEFECTO #3;
          |HAZ separa_nom;
          alfa1 = #20#5; |QBF alfa1;
          |SI alfa1 ! "";
               fech1 = alfa1;
               |HAZ fecha;
               #3#0  = for_fecha;                || fecha inicio contrato
          |SINO;
               #3#0  = "";                || fecha inicio contrato
          |FINSI;
          #3#1 = #20#9;             || fic especifico
          #3#2 = #20#27;            || relacion laboral especial
          #3#3 = #20#28;            || NAFSS trabajador sustituido
          #3#4 = #20#29;            || causa sustitucion
          #3#5 = #20#33;            || Parte entera coeficiente
          |SI #20#33 = 0;|Y #20#34 = 33;
               #20#34 = 0;                    || para la huelga del 20.06.2002 !!
          |FINSI;
          #3#6 = #20#34;            || Parte decimal coeficiente
          #3#7 = #20#35;            || Dias de trabajo
          #3#8 = #20#36;            || Dias que no se aplica coeficiente
          #3#9 = #20#32;            || Coeficiente reductor edad jubilacion
          |SI #20#44 = "N";
               #20#44 = " ";
          |FINSI;
          #3#10 = #20#44;            || relevo
          |SI #20#3 = "ADT"
               #3#11 = #20#63;            || dias trabajados
          |SINO;
               #3#11 = "00";
          |FINSI;
          #3#12 = #20#65;          || Sistema Especial (32) para altas citricos
          #3#13 = "     ";         || reservado (antes convenio colectivo)
          #3#14 = #20#53;            || cambio puesto trabajo
          #3#15 = "00";              || indicativo perdida beneficios
          #3#16 = #20#57;            || vinculo familiar
          #3#17 = #20#60;            || programa fomento empleo agrario

          |SI nRegimen = 613; |O nRegimen = 163;
               |SI #20#3 = "AMC"; |O #20#3 = "MA "; || modalidad cotizacion
                    #3#18 = #20#54;
               |FINSI;
          |FINSI;
          #3#19 = "00";              || beneficiario Sist. Nac. Gar. Juv.
          aAlfa = #20#52;
          |QBF aAlfa;
          #3#20 = aAlfa;             || Ocupacion
          #3#21 = #20#58;            || excedente sector industrial
          #3#22 = "000";               || reduccion de jornada
          #3#23 = "000";               || Coef TP Inicial
          swNoGrabes = 0;
          |SI aAlfa = "";
               |HAZ comp_seg;   || comprobacion para saber si hacer o no el DAM
          |FINSI;
          |SI swNoGrabes ! 0;
               |ACABA;
          |FINSI;
          |HAZ monta;
          |HAZ fin_linea;
     |SINO;
          |HAZ fin_linea2;
     |FINSI;
|FPRC;

|PROCESO comp_seg;
     nVacios = 0;
     swNoGrabes = 0;
     |SI clave = "DAM";
          nLimite = 23;
     |FINSI;
     |PARA nPara1 = 0; |SI nPara1 [ nLimite; |HACIENDO nPara1 = nPara1 + 1;
          aValor = #3nPara1;
          |QBF aValor;
          |SI aValor = ""; |O aValor = "N";
               nVacios = nVacios + 1;
          |SINO;
               nValor = aValor;
               |SI nValor = 0;
                    nVacios = nVacios + 1;
               |FINSI;
          |FINSI;
     |SIGUE;
     nVacios = nVacios - 1;
     |SI nVacios = nLimite;
          |HAZ fin_linea2;
          swNoGrabes = 1;
     |FINSI;
|FPRC;

|PROCESO fct;
     swSigue = 0;
     |SI #20#3 = "MB "; |O #20#3 = "ASA"; |O #20#3 = "MSA"; |O #20#3 = "ESA";
     |O #20#3 = "ADT";
          swSigue = 1;
     |FINSI;
     |SI swSigue = 0;
          |ACABA;
     |FINSI;
     clave = "FCT";
     |HAZ ini_linea;
     |SI sw_auxil = 0;
          |DDEFECTO #3;
          #3#0 = #20#55;      || tipo de accion (=001)
          #3#1 = 0;           || Porcentaje (no lo uso de momento)
          #3#2 = "";          || fecha fin de vacaciones
          |SI #20#3 = "MB ";
               #3#2 = #20#50;
          |FINSI;
          #3#3 = "";
          |SI #20#3 = "ASA"; |O #20#3 = "MSA"; |O #20#3 = "ESA";
               fech1 = #20#13;
               |HAZ fecha
               #3#3 = for_fecha;       || fecha desde
          |FINSI;
          |SI #20#3 = "ADT";
               #3#3 = #20#61;       || fecha desde
          |FINSI;
          #3#4 = "";          || fecha hasta
          |SI #20#3 = "ADT";
               #3#4 = #20#62;       || fecha hasta
          |FINSI;

          #3#5 = "";          || fecha desde nueva (NO SE USA DE MOMENTO)
          #3#6 = "";          || fecha hasta nueva (NO SE USA DE MOMENTO)
          #3#7 = "";          || Reservado
          |HAZ monta;
          |HAZ fin_linea;
     |SINO;
          |HAZ fin_linea2;
     |FINSI;
|FPRC;

|PROCESO dra;
     |SI #20#3 ! "MJR"; |Y #20#3 ! "MFR";
          |ACABA;
     |FINSI;
     clave = "DRA";
     |HAZ ini_linea;
     |SI sw_auxil = 0;
          |DDEFECTO #3;
          |SI #20#3 = "MJR";
               #3#0 = #20#40;
               #3#1 = #20#41;
               #3#2 = #20#42;
          |FINSI;
          |SI #20#3 = "MFR";
               #3#3 = #20#16;
               #3#4 = #20#39;
          |FINSI;
          |HAZ monta;
          |HAZ fin_linea;
     |SINO;
          |HAZ fin_linea2;
     |FINSI;
|FPRC;

|PROCESO fab;
     clave = "FAB";
     |HAZ ini_linea;
     |SI sw_auxil = 0;
          |DDEFECTO #3;
          #3#0 = #20#3;            || accion
          |SI #20#3 = "MCH";       || para huelga, es MC, aunque grabo antes
               #3#0 = "MC";        || MCH porque me servira para otras cosas
          |FINSI;
          #3#1  = #20#4;             || situacion
          |SI #20#3 ! "MJR"; |Y #20#3 ! "AMC";
          |Y #20#3 ! "ASA"; |Y #20#3 ! "MSA"; |Y #20#3 ! "ESA";
          |Y #20#3 ! "ADT"; |Y #20#3 ! "ACT";
               fech1 = #20#13;
               |HAZ fecha;
               #3#2  = for_fecha;         || fecha real
               #3#3  = #12#33;            || grupo cotizacion
               |SI #12#32 > 500; #12#32 = #12#32 - 500;   |FINSI;
               |SI #20#3 = "MA "; |O #20#3 = "MG ";
                    |SI #12#33 ] 08;
                         |SI #12#42 = "X"; |O #12#42 = "M";
                              #3#4  = "S";    || mensual grupo 08-09-12-11
                         |SINO;
                              #3#4  = "";    || epigrafe AT
                         |FINSI;
                    |FINSI;
               |FINSI;
               #3#5 = 0;                 || grado discapaidad (no lo hacemos)
               |SI #20#3 ! "MB ";   || en las bajas los tipos de contrato van vacios!!!
                    #3#6  = aCto;          || clave contrato
               |FINSI;
               #3#7 = #20#20;             || condicion desempleado
               #3#8 = #20#21;             || mujer subrepresentada
               #3#9 = #20#12;             || coeficiente tiempo parcial
               |SI #12#45 = "064";|O #12#45 = "065";
                    nume1 = #20#12;
                    |SI nume1 = 0;
                         alfa1 = "    Trabajador [" + #20#1 + "-" + #20#2 + "] SIN coeficiente de jornada ...";
                         |MENAV alfa1;
                    |FINSI;
               |FINSI;
               #3#10 = #20#10;             || colectivo trabajador (Regimen del Mar)
               #3#11 = "";                || indicador de impresion NO
               |SI #0#10 = "S"; |O #0#10 = "C";
                    #3#11  = #0#10;        || indicador de impresion SI
               |FINSI;

               #3#12 = 0;                || Categoria profesional reg.raro(no usado)
               fech1 = #12#11;
               |HAZ fecha;
               |SI #20#3 ! "MB "; |Y #20#3 ! "MG "; |Y #20#3 ! "MC "; |Y #20#3 ! "MT ";
               |Y #20#3 ! "MCH";       || para huelga tampoco
                    #3#13 = for_fecha;           ||carga fecha de nacimiento
               |FINSI;
               |SI #20#3 = "MJR";
                    #3#13 = "";
               |FINSI;    ||en acccion MJR tampoco
               #3#14 = nSexo;             || sexo
               |SI #20#3 = "MCH";       || para huelga no hace falta sexo
                    #3#14 = 0;
               |FINSI;
               #3#15 = #20#37;            || Inactividad
               |SI nRegimen = 613; |O nRegimen = 163;
                    |SI #20#3 = "MA ";
                         #3#16 = #20#38;            || Exclusion de Desempleo
                    |FINSI;
               |FINSI;
               #3#17 = #20#43;            || Coef.Huelga Parcial
               #3#18 = #20#47;            || Mujer reincorporada/Superp. por 2a. reinc.
               #3#19 = #20#22;            || incapacitado readmitido
               #3#20 = #20#23;            || 1er.trabajador autonomo

               #3#21 = #20#59;            || 5 Jor/mes
               #3#22 = "";                || reservado 1 pos
               #3#23 = "";                || fecha de control

               #3#24 = #20#25;            || trabajador en exclusion
               #3#25 = #20#30;            || renta activa de insercion
               #3#26 = #20#31;            || trab. contratadas en 24 meses tras parto
          |FINSI;
          |HAZ monta;
          |HAZ fin_linea;
     |SINO;
          |HAZ fin_linea2;
     |FINSI;
|FPRC;

|PROCESO ldd;
     |SI swEsAgrario = 1;
          |ACABA;
     |FINSI;
     |SI #20#3 = "ASA"; |O #20#3 = "MSA"; |O #20#3 = "ESA";
          |ACABA;
     |FINSI;
     |SI #20#3 = "MCH"; |O #20#3 = "ADT"; |O #20#3 = "ACT";
          |ACABA;
     |FINSI;
          clave = "LDD";
     |HAZ ini_linea;
     |SI sw_auxil = 0;
          |DDEFECTO #3;
          nume1 = (#12#8 * 100) + #12#9;
          #3#0  = nume1;             || codigo postal
          #3#1  = #12#6;             || localidad
          #3#2  = #12#8;             || provincia
          #3#3  = #12#244;           || telefono
          #3#3  = "";                || reservado
          |HAZ monta;
          |HAZ fin_linea;
     |SINO;
          |HAZ fin_linea2;
     |FINSI;
|FPRC;

|PROCESO dom;
|SI swEsAgrario = 1;
     |ACABA;
|FINSI;
|SI #20#3 = "ASA"; |O #20#3 = "MSA"; |O #20#3 = "ESA";
|O #20#3 = "ADT"; |O #20#3 = "ACT";
     |ACABA;
|FINSI;
|SI #20#3 = "MCH";
     |ACABA;
|FINSI;
    clave = "DOM";
|HAZ ini_linea;
|SI sw_auxil = 0;
    |DDEFECTO #3;
        #3#0  = "";                || tipo domicilio (futuro uso)
        #3#1  = "";                || tipo via (futuro uso)
        #3#2  = #12#3;             || nombre via
        nume1 = #12#4;
        #3#3  = nume1;             || numero (evita alfanumericos)
        #3#4  = "";                || bloque
        #3#5  = "";                || escalera
        #3#6  = #12#5;             || piso
        #3#7  = "";                || puerta
        ||nume1 = (#12#8 * 100) + #12#9;
        ||#3#8  = ""nume1;             || c.postal
        #3#8  = "";                || mensajes SMS
        #3#9  = "";                || clave municipio
    |HAZ monta;
    |HAZ fin_linea;
|SINO;
    |HAZ fin_linea2;
|FINSI;
|FPRC;

|PROCESO dap;
|SI swEsAgrario = 1;
     |ACABA;
|FINSI;
|SI #20#3 = "MG "; |O #20#3 = "MC "; |O #20#3 = "MT ";
     |ACABA;
|FINSI;
clave = "DAP";
|HAZ ini_linea;
|SI sw_auxil = 0;
    |DDEFECTO #3;
    |HAZ separa_nom;
        #3#0  = #20#17;            || nombre padre
        #3#1  = #20#18;            || nombre madre
        #3#2  = 0;                 || lugar nacimiento (futuro uso)
        #3#3  = #20#19;            || telefono
        #3#4  = 0;                 || reservado
    |HAZ monta;
    |HAZ fin_linea;
|SINO;
    |HAZ fin_linea2;
|FINSI;
|FPRC;

|PROCESO ayn;
    clave = "AYN";
|HAZ ini_linea;
|SI sw_auxil = 0;
    |DDEFECTO #3;
    |HAZ separa_nom;
        #3#0  = apell1;            || primer apellido
        #3#1  = apell2;            || segundo apellido
        #3#2  = nombre;            || nombre
        #3#3  = "";                || reservado
    |HAZ monta;
    |HAZ fin_linea;
|SINO;
    |HAZ fin_linea2;
|FINSI;
|FPRC;

|PROCESO TipoID;
     alfa1 = #12#7 % 101;
     alfa2 = #12#7;
     |QBF alfa2;
     alfa2 = alfa2 % -101;

     |SI alfa1 ] "A"; |Y alfa1 [ "z";
          |SI alfa1 = "X"; |O alfa1 = "Y"; |O alfa1 = "Z";
               |SI alfa2 ] "A"; |Y alfa2 [ "z";
                    #3#2 = "6";                  || tipo identif.(EXTRANJERO)
                    #3#3 = "   ";                || pais emisor
                    #3#7 = #12#76 % 103;         || nacionalidad
               |SINO;
                    #3#2 = "2";                  || tipo identif.(PASAPORTE)
                    #3#3 = #12#76 % 103;         || pais emisor
                    #3#7 = #12#76 % 103;         || nacionalidad
               |FINSI;
          |SINO;
               #3#2 = "2";                  || tipo identif.(PASAPORTE)
               #3#3 = #12#76 % 103;         || pais emisor
               #3#7 = #12#76 % 103;         || nacionalidad
          |FINSI;
     |FINSI;
     |SI #nomcontr#40 = 2;
          |SI #3#2 = "6";
               aAlfa = #12#7;
               |QBF aAlfa;
               aAlfa1 = aAlfa % 101;
               aAlfa = aAlfa - aAlfa1;
               alfa6 = aAlfa % 0;
               long = alfa6;
               |SI long ! 9;
                    aAlfa = aAlfa1 + "0" + aAlfa;
               |SINO;
                    aAlfa = aAlfa1 + aAlfa;
               |FINSI;
               #12#7 = aAlfa;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO tra;
    clave = "TRA";
|HAZ ini_linea;
|SI sw_auxil = 0;
    |DDEFECTO #3;
        #3#0  = #12#75 % 102;                || provincia NAFSS
        #3#1  = #12#75 % 3;                  || numero NAFSS
        #3#2  = "1";                         || tipo identificador (DNI)
        #3#3  = "   ";                       || pais emisor (Espa¤a)
        #3#7  = "   ";                       || nacionalidad (Espa¤a)
        |HAZ TipoID;     || atencion, este TipoID es distinto al del resto de silcones
        #3#4 = #12#7;                        || DNI
        #3#5 = "";                           || reservado respues.afiliac.
        #3#6 = "";                           || decodif., reservado recaud.
        #3#8 = "";                           || indicadores (futuro uso)
        #3#9 = "";                           || reservado
    |HAZ monta;
    |HAZ fin_linea;
|SINO;
    |HAZ fin_linea2;
|FINSI;
|FPRC;

|PROCESO exc;
|FPRC;

|PROCESO rzs;
    clave = "RZS";
|HAZ ini_linea;
|SI sw_auxil = 0;
    |DDEFECTO #3;
        #3#0  = "0";          || indicador razon social (FIJO 0)
        #3#1  = #6#4;         || tipo alfab. empresario
        #3#2  = #10#2;        || razon social
        #3#3  = "00000000";   || reservado
        #3#4  = "";           || reservado
    |HAZ monta;
    |HAZ fin_linea;
|SINO;
    |HAZ fin_linea2;
|FINSI;
|FPRC;

|PROCESO emp;
     clave = "EMP";
     |HAZ ini_linea;
     |SI sw_auxil = 0;
          |DDEFECTO #3;
          tot_emp = tot_emp + 1;
          #3#0  = nRegimen;          || RRSS
          #3#1  = ctacotalf % 102;   || provincia
          #3#2  = ctacotalf % 309;   || nro.cta.cotizacion
          #3#3  = "";                || tipo de identificacion
          |SI #6#3 = 11;   #6#3 = 724; |FINSI;   || no se acepta Espa¤a=11
          #3#4  = #6#3;              || pais
          #3#5  = #10#10;            || nro.identif.empresario
          #3#6  = "";                || calif.cod.empresario
          |SI #0#0 ] 2012; |Y #6#2 = 0613;
               #6#2 = 0163;
          |FINSI;
          |SI #0#0 [ 2011; |Y #6#2 = 0163;
               #6#2 = 0613;
          |FINSI;
          #3#7  = #6#2;              || RRSS
          #3#8  = #10#13 % 102;      || provincia
          #3#9  = #10#13 % 309;      || nro.cta.cotizacion
          #3#10 = "";                || reservado
          #3#11 = "";                || accion
          #3#12 = "";                || situacion (sin uso)
          |HAZ monta;
          |HAZ fin_linea;
     |SINO;
          |HAZ fin_linea2;
     |FINSI;
|FPRC;

|PROCESO lee_centros;
|DDEFECTO #14;
    #14#0 = #12#0;
    #14#1 = #12#77;
|LEE 000#14=;
|SI FSalida ! 0;
        alfa1 = "    Centro:[" + #14#0 + "][" + #14#1 + "] inexistente ...";
    |MENAV alfa1;
|FINSI;
|FPRC;

|PROCESO lee_cenespe;
|DDEFECTO #13;
    #13#0 = #12#0;
    #13#13= #12#77;
|SI #12#45 = "097";|O #12#45 = "085"; |O #12#45 = "421"; |O #12#45 = "422";
        #13#1 = "87";
|SINO;
     |SI #12#45 = "065";
          #13#1 = "64";
     |SINO;
          #13#1 = #12#45 % 2;
     |FINSI;
|FINSI;
|LEE 000#13=;            || lee el centro especial de cotizacion
|SI FSalida ! 0;
        alfa1 = "    Centro Especial:["+#13#0+"]["+#13#13+"]["+#13#1+"] inexistente ...";
    |MENAV alfa1;
|FINSI;
|FPRC;

|PROCESO lee_empresa;
|DDEFECTO #10;
    #10#0 = #20#1;
|LEE 000#10=;            || lee la empresa
|FPRC;

|PROCESO lee_datos_aux;
|DDEFECTO #15;
    #15#0 = #20#1;
    #15#1 = #20#2;
|LEE 000#15=;            || lee el trabajador

    nSexo = 1;
|SI #15#5 = "M";
     nSexo = 2;
|FINSI;
|FPRC;

|PROCESO CodigosNuevos;
     |QBF aContrato;
     aContrato = ("000" + aContrato) % -103;
     |ABRE #nomequiv;
     #nomequiv#0 = aContrato;
     |LEE 000#nomequiv.=;
     |SI FSalida = 0;
          |SI #labtraba#42 = "X";|O #labtraba#42 = "T";|O #labtraba#42 = "P";
               aContrato = #nomequiv#2;      || tiempo parcial
          |SINO;
               aContrato = #nomequiv#1;      || tiempo completo
          |FINSI;
     |FINSI;
     |CIERRA #nomequiv;
|FPRC;

|PROCESO lee_trabajador;
|DDEFECTO #12;
    #12#0 = #20#1;
    #12#1 = #20#2;
|LEE 000#12=;            || lee el trabajador

    aCto = #12#45;
    |SI #12#45 = "Z1 ";  aCto = "181";  |FINSI;
    |SI #12#45 = "Z2 ";  aCto = "182";  |FINSI;
    |SI #12#45 = "Z3 ";  aCto = "183";  |FINSI;
    |SI #12#45 = "Z4 ";  aCto = "184";  |FINSI;
    |SI #12#45 = "Z5 ";  aCto = "185";  |FINSI;
    aContrato = aCto;
    |HAZ CodigosNuevos;
    aCto = aContrato;
     |SI aCto = "422";        || becarios
          aCto = "000";
     |FINSI;

    |DDEFECTO #25;
    #25#0 = #12#0;
    #25#1 = #12#77;
    |LEE 000#25=;
    |SI FSalida = 0;
        nRegimen = #25#2;
    |SINO;
        nRegimen = #6#2;
    |FINSI;

     |SI #0#0 ] 2012; |Y nRegimen = 0613;
          nRegimen = 0163;
     |FINSI;
     |SI #0#0 [ 2011; |Y nRegimen = 0163;
          nRegimen = 0613;
     |FINSI;

    |SI #labtraba#42 = "A"; |O #labtraba#42 = "B"; |O #labtraba#42 = "E";
    |O #labtraba#42 = "e";
        swEsAgrario = 1;
    |SINO;
        swEsAgrario = 0;
    |FINSI;
|FPRC;

|PROCESO empresa_si;
    #6#0 = #20#1;
|LEE 000#6=;
    sw_empresa = FSalida;
|FPRC;

|PROCESO cambia_autoriz;
     #silcon03#0 = #silcon03#0;
     #silcon03#1 = 0001;
     |LEE 101#silcon03.=;
     |SI FSalida = 0;
          nNume1 = nPosicion;
          nNume1 = 100 + nNume1;
          aAlfa1 = #silcon03#4 % nNume1;
          nNume2 = nPosicion + 9;
          nNume2 = (nNume2 * 100) + 99;
          aAlfa2 = #silcon03#4 % nNume2;
          #silcon03#4 = aAlfa1 + #silcon06#5 + aAlfa2;
          |GRABA 020#silcon03.a;
     |FINSI;
|FPRC;

|PROCESO mtos_afi1;
|HAZ empresa_si;
|SI sw_empresa = 0;
    |PINTA 2305, #20#1;
    |PINTA 2315, #20#2;

    |HAZ ordena;
    |SI ant_ctacot ! ctacot;
        |SI ant_ctacot ! -1;
            |HAZ actua_registro;
        |FINSI;
        |HAZ lee_empresa;

          |SI aAutorizacion ! ""; |Y aAutorizacion ! #6#5;
               || ya ha hecho la primera empresa
               || si la autorizacion anterior es distinta a la de la empresa actual,
               || le meto la general.En caso de dos distintas,la del control RED siempre

               #6#5 = #1#0;
               aAutorizacion = #1#0;
          |SINO;
               aAutorizacion = #6#5;
          |FINSI;
          nPosicion = 10;
          |HAZ cambia_autoriz;
        |HAZ emp;
        |HAZ rzs;
        |HAZ exc;
            ant_empre = #20#1;
            ant_ctacot = ctacot;
    |FINSI;
    |HAZ lee_trabajador;
    |HAZ lee_datos_aux;
    |HAZ tra;
    |HAZ ayn;
    ||HAZ dap;
    |HAZ dom;
    |HAZ ldd;
    |HAZ fab;
    |HAZ dam;
    |HAZ otd;
    |HAZ dra;
    |HAZ fct;

        #21#0 = #20#0;
    |LEE 121#21=;                  || actualiza nro. de lote
    |SI FSalida = 0;
            #21#14 = #0#2;
        |GRABA 020#21a;
    |FINSI;
    |LIBERA #21;
|FINSI;
|LIBERA #20;
|FPRC;

|PROCESO ordena;
    ctacot = 0;
|HAZ lee_trabajador;     || lee trabajador
|SI #12#45 = "421"; |O #12#45 = "422";
    |HAZ lee_cenespe;         || lee centros especiales
        ctacot = #13#10;
        ctacotalf = #13#10;
|SINO;
    |HAZ lee_centros;         || lee centros normales
        ctacot = #14#10;
        ctacotalf = #14#10;
|FINSI;
|FPRC;

|DEFBUCLE mtos_afi;
 #20#2;
 26;
     1,     1, "*";
 99999, 99999, "*";
 be;
 NULL, ordena, NULL, NULL, NULL, mtos_afi1;
 ctacot, #20#2;
|FIN;

|PROCESO sil;
     |SI #20#3 = "AMC"; |ACABA; |FINSI;
    clave = "SIL";
|HAZ ini_linea;
|SI sw_auxil = 0;
    |DDEFECTO #3;
        #3#0  = "";
    |HAZ monta;
    |HAZ fin_linea;
|SINO;
    |HAZ fin_linea2;
|FINSI;
|FPRC;

|PROCESO eti;
    clave = "ETI";
|HAZ ini_linea;
|SI sw_auxil = 0;
    |DDEFECTO #3;
        #3#0  = "AFI8";
        #3#1  = "3";
        #3#2  = "WS83";
        #3#3  = "0";
        #3#4  = #1#0;         || clave autorizacion
        #3#5  = "243     ";   || proveedor nomina + 5 blancos (reservados)
        fech1 = ~;
    |HAZ fecha;
        #3#6  = for_fecha;    || fecha
        #3#7  = for_hora;     || hora
        #3#8  = fichero;      || fichero
        #3#9  = extension;    || sufijo
        #3#10 = "N";          || codigo prioridad
        #3#11 = indica;       || indicador
        #3#12 = #0#0 % -102;  || a¤o
        #3#13 = #0#1;         || mes
        #3#14 = #0#6;         || serie
        #3#15 = #0#7;         || nro. envio
        #3#16 = #0#8;         || nro. documento
        #3#17 = "";           || reservado
    |HAZ monta;
    |HAZ fin_linea;
|SINO;
    |HAZ fin_linea2;
|FINSI;
|FPRC;

|PROCESO graba_lote;
     #2#1 = fichero;       || nombre salida
     #2#2 = extension;     || extension
     #2#3 = ~;             || fecha creacion
     #2#4 = ~;             || fecha grabacion
     #2#5 = 0;
     #2#6 = "";
     #2#7 = "";
     |GRABA 020#2b;
|FPRC;

|PROCESO nombre_fichero;
    fech1 = ~;
    alfa1 = fech1;
    alfa2 = alfa1 % 102;      || dia
    alfa3 = alfa1 % 402;      || mes
|HAZ hora;
    fichero = alfa2 + alfa3 + for_hora;
    || extension = "afi";     || desde 07/2004
    extension = "AFI";
|FPRC;
____________________________________/ PROCESO PRINCIPAL
|PROCESO tipo3; |TIPO 3;
|ABRE #1;
|ABRE #2;
|ABRE #4;
|ABRE #5;
|ABRE #6;
|ABRE #10;
|ABRE #12;
|ABRE #13;
|ABRE #14;
|ABRE #15;
|ABRE #21;
|ABRE #25;
|SELECT #1#21;

    #2#0 = #0#2;
|LEE 000#2=;
|SI FSalida = 0;
    |MENAV "    Numero de Lote YA existente ...";
|SINO;
        ant_empre = -1;
        ant_ctacot = -1;
    |SI #0#5 = "N"; indica = " ";  |FINSI;
    |SI #0#5 = "R"; indica = "S";  |FINSI;
    |HAZ control;
    |HAZ comprobacion;
    |SI sw_comprueba = 0;
            ant_empre = -1;
            ant_ctacot = -1;
        |HAZ nombre_fichero;
        |HAZ graba_lote;
        |HAZ eti;
        ||HAZ sil;
        |BUCLE mtos_afi;
        |HAZ actua_registro;
        |HAZ etf;
    |FINSI;
|FINSI;

|LIBERA #2;

|CIERRA #1;
|CIERRA #2;
|CIERRA #4;
|CIERRA #5;
|CIERRA #6;
|CIERRA #10;
|CIERRA #12;
|CIERRA #13;
|CIERRA #14;
|CIERRA #15;
|CIERRA #21;
|FPRC;
____________________________________/ COMPROBACION ANTERIOR A LA CREACION
|PROCESO comprueba1;
|HAZ ordena;
|SI ant_ctacot ! ctacot;
    |HAZ lee_empresa;
        sw_linea = 1;
    |PRINT;
        ant_ctacot = ctacot;
|FINSI;

|HAZ empresa_si;
|SI sw_empresa = 0;
        incluido = "SI";
        los_si = los_si + 1;
|SINO;
        incluido = "NO";
        los_no = los_no + 1;
|FINSI;
|HAZ lee_trabajador;
    sw_linea = 0;
|PRINT;
|FPRC;

|DEFBUCLE comprueba;
 #20#2;
 26;
     1,     1, "*";
 99999, 99999, "*";
 be;
 NULL, ordena, NULL, NULL, NULL, comprueba1;
 ctacot, #20#2;
|FIN;

|PROCESO comprobacion;
|AVISO;
|CONFI "    NComprobacion impresa del contenido del lote (S/N): ";
|SI FSalida ! 0;    |ACABA;   |FINSI;

|MENSA "    <Comprobacion del contenido del Lote>";

|IMPRE 0;
|SI FSalida = 0;
    |INFOR "silcon14";
    |BUCLE comprueba;
    |PIEINF;
    |FININF;
    |FINIMP;
    |CONFI "    SContinuar con la creacion del Lote (S/N): ";
        sw_comprueba = FSalida;
|SINO;
        sw_comprueba = FSalida;
|FINSI;
|ATRI R;  |BLIN 4;  |ATRI 0;
|FPRC;
