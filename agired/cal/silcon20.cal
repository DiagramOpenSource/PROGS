|FICHEROS;
     silcon20 #0;
     silcon01 #1;        || control directorio
     silcon02 #2;        || lotes cabs.
     tsilcon0 #3;        || def ayuda carga lineas
     silcon04 #4;        || campos auxiliares
     silcon03 #5;        || lotes lins.
     silcon06 #6;        || empresas silcon
     silcon07 #7;        || registro lotes
     tsilcon5 #15,MANTE; || tmp. parrilla empresas
     ||tsilcon9,MANTE;     || tmp. centros
     tsilcon6 #23,MANTE; || tmp. parrilla trabajadores

     tsilco10 #100;
     tsilco11 #101;
     tsilco12 #102;
     tsilco13,MANTE;

     labempre #10;        || empresas
     labtraba #12;        || trabajadores
     labcentr #16;        || centros de trabajo
     nomcenes #17;        || centros de trabajo especiales

     nomcalca #18;        || calculos cabs. (proceso mensual)
     nomhicca #19;        || calculos cabs. (proceso historico)
     nomcalac #21;        || calculos cabs. (proceso atrasos)
     silcon15 #25;        || regimenes centros
     nomcontr #36;        || control aplicacion

     nomcones #56;        || constante especial (bonif.exp.reg.emp.)
     nomcalli #59;        || Mantenimiento Calculos Mensual   (Lineas)
     nomhicli #60;        || Mantenimiento Calculos Historico (Lineas)
     nomcalal;            || calculos lins. (proceso atrasos)

     nomcorli;
     nomconli;

     silcon09;
|FIN;

|VARIABLES;
     fech1 = @;
     alfa1 = "";
     alfa2 = "";
     alfa3 = "";
     nume1 = 0;
     nume2 = 0;
     enAny = 0;
     p_mes = 0;
     traba = 0;
     nMarcarT = 0;
     nMarcarE = 0;
     sw_parrilla = 0;
     aAviso = "";
     FEstado = 0;
     clave = "";
     sw_auxil = 0;
     linea = 0;
     aRegimen = "";
     aParam = "";
     aAlfa = "";
     aAlfa0 = "";
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     aAlfa4 = "";
     aLibre = "";
     aReser = "";
     for_fecha = "";
     for_hora = "";
     fichero = "";
     extension = "";
     indica = "";
     swStop = 0;
     nEmp = 0;
     nTra = 0;
     any_hist = 0;
     tipo_bot = 0;
     tipo_top = 0;
     nElAny = 0;
     nElMes = 0;
     enDesCen = 0;
     enHasCen = 0;
     eaFuente = "";

     ajuste1 = "";
     ajuste2 = "";
     ajuste3 = "";
     alfa = "";
     error = 0;
     ind = 0;
     long = 0;
     posi = 0;
     digito = "";
     texto = "";
     mascara = "";
     i = 0;
     j = 0;
     nCampo = 0;
     nTipo = 0;
     nTipoNominas = 0;
     descom = "";
     entero = "";
     decima = "";
     elsw1 = 0;
     para1 = 0;
     nlinea = 0;
     nNume = 0;
     cadena = "";
     swRepite = 0;
     aCCC = "";
     nConcepto = 0;
     nConceptoRED = 0;
     nImporte = 0;
     nConvenio = 0;
     aIndicativo = "";
     aDescripcion = "";
     aTipoPaga = "";
     nUnidades = 0;
     nUnidadesCal = 0;
     nPrecio = 0;
     nBrutoMenosPrest = 0;
     nMesAtrasos = 0;
     nDesdeMes = 0;
     nHastaMes = 0;
     nMesHist = 0;
     swSigue = 0;
     aAntNroAfil = "";
     aAntCCC ="";
     aAntCen ="";
     nNroEmpresas = 0;
     nNroTrabaj = 0;
     nAntEmp = 0;
     nBrutoTrab = 0;
     nPagasProrr = 0;

     &s_ori = "";
     &s_tat = 0;
     &s_ele = "";
     &s_any = 0;
     &s_mes = 0;
     &s_has = 0;
     &s_anyl = 0;
     &s_sel = "";
     &s_lotecra = 0;
     &s_fichcra = "";
     &swSelEmp = 0;
     &swSelTra = 0;

     nNroTras = 0;
     nAntEmpCon = 0;
     nTotEmp = 0;
     nImporteFin = 0;

     &s_num = 0;
     &s_fich = "";
     topemes = 0;
     &nMes = 0;
     &nAny = 0;
     &nTopemes = 0;
     aMes = "";
     aAny = "";
     aFecha = "";
     fIniMes = @;
     fFinMes = @;
     fFechaIniMes = @;
     fFechaFinMes = @;
     aFechaIniMes = "";
     aFechaFinMes = "";
     fFechaAlta = @;
     fFechaBaja = @;
     nNume1 = 0;
     nNume2 = 0;
     nDias = 0;
     swDic2013 = 0;
     nDiasAlta = 0;
     &enLote = 0;
     &eaFich = "";
     &eaExt = "";
     swPosibleError = 0;
     nDiasMesSig = 0;
     nBaseMesSig = 0;
     nDiasVac = 0;
     nImporteVac = 0;
     swBaseMesSig = 0;
     swTipoBucle11 = 0;
     nContTmp = 0;
     nDesdeMarca = 0;
     nHastaMarca = 0;
     nDiasMes = 0;
     nDiasIT = 0;
     nPosicion = 0;
     aAutorizacion = "";
|FIN;

|PROCESO Mensa18; |TIPO 7;
     |SI #0#12 = "M"; |O #0#12 = "H";
          |MENSA "    Introduzca el año de abono de las nominas";
     |FINSI;
     |SI #0#12 = "A"; |O #0#12 = "T";
          |MENSA "    Introduzca el año al que corresponden los atrasos.";
     |FINSI;
|FPRC;

|PROCESO ultidia;
     topemes = 31;
     |SI nMes = 4; |O  nMes = 6; |O  nMes = 9; |O  nMes = 11;
          topemes = 30;
     |FINSI;
     |SI nMes = 2;
          topemes = 28;
     |FINSI;
|FPRC;

|PROCESO AyudaTipos; |TIPO 7;
     |PUSHV 10552380;
     |ATRI R;
     ||            5    6         7         8
     ||            56789012345678901234567890
     |PINTA 1055, "       - Opciones -       ";
     |PINTA 1155, " 00 :Normal               ";
     |PINTA 1255, " 02 :Finiquitos           ";
     |PINTA 1355, "5-20:Atrasos              ";
     |PINTA 1455, " 98 :Salarios de tramita- ";
     |PINTA 1555, "     ci¢n                 ";
     |PINTA 1655, "                          ";
     |ATRI 0;
|FPRC;

|PROCESO AyudaEnvio; |TIPO 7;
     |PUSHV 19062380;
     |ATRI R;
     ||            0   1         2         3         4         5         6         7
     ||            6789012345678901234567890123456789012345678901234567890123456789012345
     |PINTA 1906, " Indicador ( ): en blanco. Primer env¡o o modificaci¢n de env¡o previo ";
     |PINTA 2006, " Indicador (M): permite MODIFICAR un  importe comunicado  previamente. ";
     |PINTA 2106, " Se sustituir  el importe existente por el nuevo. Se  obtiene el mismo ";
     |PINTA 2206, " resultado si se deja el campo en blanco.                              ";
     |PINTA 2306, " Indicador (B): permite ELIMINAR el importe de un concepto comunicado. ";
     |ATRI 0;
|FPRC;

|PROCESO Restaura; |TIPO 0;
     |POPV;
|FPRC;

|PROCESO defecto; |TIPO 7;
     fech1 = ~;
     alfa1 = fech1;
     alfa1 = alfa1 % 402;
     nume1 = alfa1;
     alfa2 = fech1;
     alfa2 = alfa2 % -104;
     nume2 = alfa2;

     |SI nume1 = 1;
         #0#0 = nume2 - 1;
     |SINO;
         #0#0 = nume2;
     |FINSI;
     #0#18 = #0#0;

     |PINTA #0#0;
     |PINTA #0#18;

     |HAZ mirames;
     |HAZ ActivaCampos;
|FPRC;

|PROCESO elAnyo; |TIPO 0;
     enAny = #0#0;
|FPRC;

|PROCESO mirames;
     |SI #0#12 ! "A";|Y #0#12 ! "T"; |Y #0#12 ! "C";
          fech1 = ~;
          alfa1 = fech1;
          alfa1 = alfa1 % 402;
          nume1 = alfa1;
          alfa2 = fech1;
          alfa2 = alfa2 % -104;
          nume2 = alfa2;

          |SI nume1 = 1;
              #0#1 = 12;
              #0#13 = 12;
          |SINO;
              #0#1 = nume1 - 1;
              #0#13 = nume1 - 1;
          |FINSI;

          |PINTA #0#1;
          |PINTA #0#13;
     |FINSI;
|FPRC;

|PROCESO pintames; |TIPO 0;
|SI #0Campo =  1;  p_mes = "ENERO     ";  |FINSI;
|SI #0Campo =  2;  p_mes = "FEBRERO   ";  |FINSI;
|SI #0Campo =  3;  p_mes = "MARZO     ";  |FINSI;
|SI #0Campo =  4;  p_mes = "ABRIL     ";  |FINSI;
|SI #0Campo =  5;  p_mes = "MAYO      ";  |FINSI;
|SI #0Campo =  6;  p_mes = "JUNIO     ";  |FINSI;
|SI #0Campo =  7;  p_mes = "JULIO     ";  |FINSI;
|SI #0Campo =  8;  p_mes = "AGOSTO    ";  |FINSI;
|SI #0Campo =  9;  p_mes = "SEPTIEMBRE";  |FINSI;
|SI #0Campo = 10;  p_mes = "OCTUBRE   ";  |FINSI;
|SI #0Campo = 11;  p_mes = "NOVIEMBRE ";  |FINSI;
|SI #0Campo = 12;  p_mes = "DICIEMBRE ";  |FINSI;
|PINTA 0843, p_mes;
|FPRC;

|PROCESO AnyLote;
     |SI #0#12 = "M"; |O #0#12 = "H";
          #0#18 = #0#0;
          |PINTA #0#18;
     |FINSI;
|FPRC;

|PROCESO ActivaCampos;
     |SI #0#12 = "A"; |O #0#12 = "T";
          |CAMPO_MODIFICA #0#13, 1, "S";
          |CAMPO_VISUAL #0#13, 1, "S";
          |PINTA 1148, "-    ";
          |PINTA #0#13;

          |CAMPO_MODIFICA #0#18, 1, "S";
          |CAMPO_VISUAL #0#18, 1, "S";
          |PINTA 1324, "ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´";
          |PINTA 1424, "³ A¤o Atrasos Lote.:          ³";
          |PINTA #0#18;
     |SINO;
          |CAMPO_MODIFICA #0#13, 1, "N";
          |CAMPO_VISUAL #0#13, 1, "N";
          |PINTA 1148, "     ";

          |CAMPO_MODIFICA #0#18, 1, "N";
          |CAMPO_VISUAL #0#18, 1, "N";
          |PINTA 1324, "ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´";
          |PINTA 1424, "³                             ³";
     |FINSI;
|FPRC;

____________________________________/ FORMATEO DE DATOS
|PROCESO errores;
    alfa1 = "    " + i + "." + clave;
|SI error = 1;
        alfa1 = alfa1 + ": VALOR DE CAMPO NUMERICO INCORRECTO";
|FINSI;
|SI error = 2;
        alfa1 = alfa1 + ": CAMPO OBLIGADO SIN RELLENO";
|FINSI;
     alfa1 = alfa1 + " EN LINEA " + linea;
|MENAV alfa1;
|FPRC;

|PROCESO comp1;               || COMPRUEBA SI ES NUMERICO.
     |SI ajuste1 = "1";
          error = 0;
          |PARA ind = 1; |SI ind [ long; |HACIENDO ind = ind + 1;
               posi = 100 * ind + 1;
               digito = texto % posi;
               |SI digito ! " ";|Y digito ! ".";|Y digito ! "-";
                    |SI digito < "0"; |O digito > "9";
                         error = 1;
                         |SAL;
                    |FINSI;
               |FINSI;
          |SIGUE;
     |FINSI;
|FPRC;

|PROCESO comp2;               || COMPRUEBA SI TIENE QUE ESTAR RELLENO.
     |SI ajuste3 = "3";
          error = 0;
          FSalida = 0;
          |QBF alfa;
          |PARA ind = 1; |SI FSalida = 0; |HACIENDO ind = ind + 1;
               posi = 100 * ind + 1;
               digito = texto % posi;
               |SI digito = "";
                    error = 2;
                    |SAL;
               |FINSI;
          |SIGUE;
     |FINSI;
|FPRC;

|PROCESO comp3;               || AJUSTA I, o D. Y RELLENA DE 0 o BLANCOS.
     |SI ajuste3 = "1";       || RELLENA BLANCOS.
          mascara = "                                                                    ";
     |FINSI;
     |SI ajuste3 = "2";       || RELLENA CEROS.
          mascara = "00000000000000000000000000000000000000000000000000000000000000000000";
     |FINSI;
     |SI ajuste2 = "1";       || AJUSTA DERECHA.
          posi = 100 + long;
          posi = posi * -1;
          |QBF texto;
          texto = mascara + texto;
          texto = texto % posi;
     |FINSI;
     |SI ajuste2 = "2";
          posi = 100 + long;
          |QBF texto;
          texto = texto + mascara;
          texto = texto % posi;
     |FINSI;
|FPRC;

|PROCESO monta;
     texto = "";
     cadena = "";
     |PARA i = 0; |SI i < 30; |HACIENDO i = i + 1;
          nCampo = i;
          texto = #3nCampo;
          nTipo = i * 6 + 2;                 || MODIFICABLE.
          alfa  = #4nTipo;
          nTipo = i * 6 + 2;                 || VISUAL.
          alfa1 = #4nTipo;
          |SI alfa = "S"; |O alfa1 = "S";
               nTipo = i * 6 + 6;            || Longitud.
               long  = #4nTipo;
               nTipo = i * 6 + 7;            || AJUSTE.
               alfa  = #4nTipo;
               ajuste1 = alfa % 101;
               ajuste2 = alfa % 201;
               ajuste3 = alfa % 301;
               |HAZ comp1;
               |SI error = 1; |HAZ errores; |ACABA; |FINSI;
               |HAZ comp2;
               |SI error = 2; |HAZ errores; |ACABA; |FINSI;
               |HAZ comp3;
               cadena = cadena + texto;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO descompone;
     entero = "";
     decima = "";
     elsw1 = 0;
     alfa1 = descom % 0;   long = alfa1;
     |PARA para1 = 1; |SI para1 [ long; |HACIENDO para1 = para1 + 1;
          nume1 = (para1 * 100) + 1;
          alfa1 = descom % nume1;
          |SI alfa1 = ".";
               elsw1 = 1;
          |SINO;
               |SI elsw1 = 0;   entero = entero + alfa1; |FINSI;
               |SI elsw1 = 1;   decima = decima + alfa1; |FINSI;
          |FINSI;
     |SIGUE;

     |SI entero = "";    entero = "0";  |FINSI;
     |SI decima = "";    decima = "0";  |FINSI;
|FPRC;

|PROCESO ini_linea;
     |DDEFECTO #5;
     linea = linea + 2;        || linea del fichero
     |SI linea > 99999;
          |MENAV "0000Numero de lineas excedido. Lote demasiado extenso.";
          |MENAV "0000Debera repetir el lote seleccionando menos empresas.";
     |FINSI;
     nlinea = nlinea + 1;      || numero de registros

     #5#0 = #2#0;
     #5#1 = linea;
     #5#2 = "C";
     #5#3 = clave;
     |GRABA 020#5b;

     sw_auxil = 0;
     #4#0 = #5#2;
     #4#1 = #5#3;
     |LEE 000#4=;
     |SI FSalida ! 0;
          alfa1 = "    Descripcion [" + #4#0 + "-" + #4#1 + "] sin definir ...";
          |MENAV alfa1;
          sw_auxil = 1;
     |FINSI;
|FPRC;

|PROCESO fin_linea;
     #5#4 = cadena;
     |GRABA 020#5a;
     |LIBERA #5;
|FPRC;

|PROCESO fin_linea2;
     linea = linea - 2;
     nlinea = nlinea - 1;
     |BORRA 020#5a;
     |LIBERA #5;
|FPRC;

|PROCESO fecha;
     for_fecha = fech1;
     aAlfa = for_fecha % -104;
     nNume = aAlfa;
     nNume = nNume $ 4;
     |SI nNume ! 0;      || no es bisiesto
          aAlfa = for_fecha % 105;
          |SI aAlfa = "29.02";
               for_fecha = "28.02" + (for_fecha % -105);
          |FINSI;
     |FINSI;
     for_fecha = (for_fecha % -104) + (for_fecha % 402) + (for_fecha % 102);
|FPRC;

|PROCESO hora;
     |HORA for_hora;
     for_hora = (for_hora % 102) + (for_hora % 402);
|FPRC;

|PROCESO no_vacio; |TIPO 0;
     alfa1 = #0Campo;
     |QBT alfa1;
     |SI alfa1 = "";
          |MENAV "    ATENCION. Es necesario indicar un nombre para el fichero CRA.";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Repetir;
     swRepite = 0;
     |SI #0#12 = "R";
          |LEE 000#0p;
          |SI FSalida = 0;
               swRepite = 1;
               #0#2 = #0#2 + 1;
               #0#3 = #0#2;
               |PINDA #0#0;
               alfa1 = Usuario; |QBT alfa1;    alfa1 = ("t" + alfa1) % 108;
               alfa2 = Usuario; |QBT alfa2;    alfa2 = ("u" + alfa2) % 108;
               alfa3 = Usuario; |QBT alfa3;    alfa3 = ("v" + alfa2) % 108;
               |NOME_DAT #15 alfa1;
               |NOME_DAT #23 alfa2;
               ||NOME_DAT #tsilcon9#alfa3#;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO ult_lote; |TIPO 7;
|ABRE #2;
|LEE 000#2u;
|SI FSalida = 0;
        #0#2 = #2#0 + 1;
|SINO;
        #0#2 = 1;
|FINSI;
|PINTA #0#2;
|CIERRA #2;
|FPRC;

|PROCESO si_existe; |TIPO 0;
|ABRE #2;
    #2#0 = #0#2;
|LEE 000#2=;
|SI FSalida = 0;
    |MENAV "    Numero de Lote YA existente ...";
    |ERROR;
|FINSI;
|CIERRA #2;
|FPRC;

|PROCESO borra_tra1;
|BORRA 020#23a;
|LIBERA #23;
|FPRC;

|DEFBUCLE borra_tra;
 #23#2;
 ;
 #15#0,     1;
 #15#0, 99999;
 be;
 NULL, borra_tra1;
|FIN;

|PROCESO parrilla_tra1;
     #23#1 = #12#0;
     #23#2 = #12#1;
     |LEE 000#23=;

     |SI #labtraba#247 = 138;      || regimen empleados hogar
          |ACABA;
     |FINSI;

     |SI FSalida ! 0;
/*
          #tsilcon9#0 = #labtraba#0;
          #tsilcon9#1 = #labtraba#77;
          |LEE 000#tsilcon9.=;
          |SI FSalida = 0;
               |SI #tsilcon9#3 ! "*";
                    |ACABA;
               |FINSI;
          |FINSI;
*/
          traba = traba + 1;

          |DDEFECTO #23;
          #23#0 = traba;       || linea
          #23#1 = #12#0;       || empresa
          #23#2 = #12#1;       || trabajador
          #23#3 = #12#2;       || nombre
          #23#4 = #12#45;      || contrato
          #23#5 = #12#77;      || centro trabajo
          #23#6 = #12#39;      || bonificacion

          |SI nMarcarT = 0;     #23#7 = "*";   |FINSI;   || marcado SI
          |SI nMarcarT ! 0;     #23#7 = " ";   |FINSI;   || marcado NO
          |GRABA 020#23n;
     |SINO;
     /*
          #tsilcon9#0 = #labtraba#0;
          #tsilcon9#1 = #labtraba#77;
          |LEE 000#tsilcon9.=;
          |SI FSalida = 0;
               |SI #tsilcon9#3 ! "*";
                    |BORRA 020#23a;;
               |FINSI;
          |FINSI;
     */
     |FINSI;
|FPRC;

|DEFBUCLE parrilla_tra;
 #12#1;
 ;
 #15#0,     1;
 #15#0, 99999;
 ;
 NULL, parrilla_tra1;
|FIN;

|PROCESO parrilla_emp1;
     |SI #15#2 = "*";|O sw_parrilla = 0;
          |SELECT #2#23;
          |AVISO;
          alfa1 = "0000SMarcar todos los trabajadores de la empresa [" + #15#0 + "]:";
          |CONFI alfa1;
          nMarcarT = FSalida;

          |BUCLE parrilla_tra;

          |SELECT #1#23;
     |SINO;
          |CIERRAT #23;
          |BUCLE borra_tra;
          |ABRET #23;
     |FINSI;
|FPRC;

|DEFBUCLE parrilla_emp;
 #15#1;
 ;
     1;
 99999;
 ;
 NULL, parrilla_emp1;
|FIN;

|PROCESO parrilla2;
     |ABRET #23;
     ||ABRE #tsilcon9;
     |LEE 000#23u;  |SI FSalida = 0; traba = #23#0; |SINO; traba = 0; |FINSI;

     |BUCLE parrilla_emp;

     |LEE 000#23u;  |SI FSalida = 0; traba = #23#0; |SINO; traba = 0; |FINSI;
     ||CIERRA #tsilcon9;
     |CIERRAT #23;
|FPRC;

|PROCESO reenvio1;
     #labempre#0 = #6#0;
     |LEE 000#labempre.=;
     |SI FSalida = 0;
          |DDEFECTO #15;
          #15#0 = #6#0;
          #15#1 = #labempre#2;
          |SI nMarcarE = 0;    #15#2 = "*";   |FINSI;
          |SI nMarcarE ! 0;    #15#2 = " ";   |FINSI;
          |GRABA 020#15n;
     |FINSI;
|FPRC;

|DEFBUCLE reenvio;
 #6#1;
 ;
     1;
 99999;
 ;
 NULL, reenvio1;
|FIN;

|PROCESO min23;
    #23#0 = 1;
|FPRC;

|PROCESO max23;
    #23#0 = 999999;
|FPRC;

|PROCESO min15;
    #15#0 = 1;
|FPRC;

|PROCESO max15;
    #15#0 = 99999;
|FPRC;

|PROCESO parrilla; |TIPO 0;
     |SI FSalida = 0;|Y #0#11 = "S";
          |SI sw_parrilla = 0;
               |ATRI P;   |MENSA "    Cargando seleccion ...";    |ATRI 0;
               alfa1 = Usuario; |QBT alfa1;    alfa1 = ("t" + alfa1) % 108;
               alfa2 = Usuario; |QBT alfa2;    alfa2 = ("u" + alfa2) % 108;
               alfa3 = Usuario; |QBT alfa3;    alfa3 = ("v" + alfa2) % 108;
               |NOME_DAT #15 alfa1;
               |NOME_DAT #23 alfa2;
               ||NOME_DAT #tsilcon9#alfa3#;
               |DELINDEX #15;
               |DELINDEX #23;
               ||DELINDEX #tsilcon9;
               |AVISO;
               |CONFI "0000NMarcar todas las empresas: ";
               nMarcarE = FSalida;
               |ABRE #labempre;
               |ABRE #15;
               ||ABRE #tsilcon9;
               |BUCLE reenvio;
               ||CIERRA #tsilcon9;
               |CIERRA #labempre;
               |CIERRA #15;
               |ATRI R;   |BLIN 4;                                |ATRI 0;
               sw_parrilla = 1;
          |FINSI;

          |PUSHV 0501 2380;
          |ENTLINEAL #1#15, 1, 4, 22, 1, min15, max15;
          |CONFI "    NSeleccion de Trabajadores (S/N): ";
          |SI FSalida = 0;
               swSelTra = 1;
               |HAZ parrilla2;
               |SI traba ! 0;
                    |PUSHV 0501 2380;
                    |ENTLINEAL #1#23, 1, 4, 22, 1, min23, max23;
                    |POPV;
               |SINO;
                    swSelTra = 0;
                    |MENAV "    No hay empresas seleccionadas ...";
               |FINSI;
          |SINO;
               swSelTra = 0;
          |FINSI;
          |POPV;
     |FINSI;
|FPRC;

|PROCESO notipo; |TIPO 7;
     |SI #0#12 ! "A";
||Y #0#12 ! "C";
          |C_M #0#17, 1, "S";
          |SI #0#12 = "M";|O #0#12 = "H";    #0#17 = 0;     |FINSI;
          |SI #0#12 = "T";                   #0#17 = 5;     |FINSI;
          |PINTA #0#17;
     |SINO;
          |C_M #0#17, 1, "N";
          #0#17 = 5;
          |PINTA #0#17;
     |FINSI;
|FPRC;

|PROCESO cheq_tipo; |TIPO 0;
     swSigue = 0;
     |SI #0#17 = 0; |O #0#17 = 2; |O #0#17 = 98;
          swSigue = 1;
     |FINSI;
     |SI 5 [ #0#17; |Y #0#17 [ 20;
          swSigue = 1;
     |FINSI;
     |SI swSigue = 0;
          |MENAV "    Tipo de nómina no permitido";
          |ERROR;
     |FINSI;

     |SI #0#12 = "M"; |O #0#12 = "H";
          |SI #0Campo ! 0; |Y #0Campo ! 2; |Y #0Campo ! 98;
               aAviso = "0000En Procesos Mensual o Histórico sólo se admiten "
               aAviso = aAviso + "los tipos 0, 2 y 98";
               |MENAV aAviso;
               |ERROR;
          |FINSI;
     |FINSI;
     |SI #0#12 = "T";
          |SI #0Campo < 5; |O #0Campo > 20;
               aAviso = "0000En Proceso Atrasos desde Histórico sólo se admiten "
               aAviso = aAviso + "los tipos del 5 al 20";
               |MENAV aAviso;
          |FINSI;
     |FINSI;

     |SI #0Campo = 2;
          #0#14 = "L13";
     |FINSI;
|FPRC;

____________________________________/ CARGA DE LOS DIFERENTES REGISTROS
|PROCESO actua_registro;
|ABRE #7;
    #7#0 = #6#0;         || empresa
    #7#1 = #0#2;         || nro.lote
    #7#2 = #0#5;         || indicador
|LEE 101#7=;
    FEstado = FSalida;

    #7#3 = #0#18;        || a¤o lote
    #7#4 = #0#1;         || mes
    #7#5 = ~;            || fecha envio
    #7#6 = "01.01.1900"; || fecha comprobacion
    #7#7 = "CRA";        || tipo lote

|SI FEstado = 0;    |GRABA 020#7a; |FINSI;



|SI FEstado ! 0;    |GRABA 020#7n; |FINSI;
|LIBERA #7;
|CIERRA #7;
|FPRC;

|PROCESO trb;
     clave = "TRB";
     |HAZ ini_linea;
     |SI sw_auxil = 0;
          |DDEFECTO #3;
          aAlfa1 = #101#4 % 102; |QBF aAlfa1;
          aAlfa2 = #101#4 % 3; |QBF aAlfa2;
          aAlfa2 = ("0000000000" + aAlfa2) % -110;
          #3#0  = aAlfa1 + aAlfa2;          || NAFSS
          #3#1 = "";
          aAlfa1 = #101#0; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
          aAlfa2 = #101#1; |QBF aAlfa2; aAlfa2 = ("00000" + aAlfa2) % -105;
          aAlfa3 = #labtraba#2; |QBF aAlfa3;
          #3#2 = aAlfa1 + "." + aAlfa2 + " " + aAlfa3;
          |HAZ monta;
          |HAZ fin_linea;
     |SINO;
          |HAZ fin_linea2;
     |FINSI;
|FPRC;

|PROCESO cambia_autoriz;
     #silcon03#0 = #silcon03#0;
     #silcon03#1 = 0002;
     |LEE 101#silcon03.=;
     |SI FSalida = 0;
          nNume1 = nPosicion;
          nNume1 = 100 + nNume1;
          aAlfa1 = #silcon03#4 % nNume1;
          nNume2 = nPosicion + 9;
          nNume2 = (nNume2 * 100) + 99;
          aAlfa2 = #silcon03#4 % nNume2;
          #silcon03#4 = aAlfa1 + #silcon06#5 + aAlfa2;
          |GRABA 020#silcon03.a;
     |FINSI;
|FPRC;

|PROCESO dde;
     |ABRE #6;
     #6#0 = #100#0;
     |LEE 000#6=;
     |CIERRA #6;
     |SI aAutorizacion ! ""; |Y aAutorizacion ! #6#5;
          || ya ha hecho la primera empresa
          || si la autorizacion anterior es distinta a la de la empresa actual,
          || le meto la general.En caso de dos distintas,la del control RED siempre

          #6#5 = #1#0;
          aAutorizacion = #1#0;
     |SINO;
          aAutorizacion = #6#5;
     |FINSI;
     nPosicion = 10;
     |HAZ cambia_autoriz;

     clave = "DDE";
     |HAZ ini_linea;
     |SI sw_auxil = 0;
          |DDEFECTO #3;
          aAlfa = aCCC;
          #3#0  = aAlfa;             || CCC
          nNume = #0#18;
          |SI swBaseMesSig = 1; |Y #0#1 = 12;
               nNume = nNume + 1;
          |FINSI;
          #3#1 = nNume;              || a¤o
          nNume = #0#1;
          |SI swBaseMesSig = 1;
               nNume = nNume + 1;
               |SI nNume = 13;
                    nNume = 01;
               |FINSI;
          |FINSI;
          |SI #0#12 = "M"; |O #0#12 = "H";      || mes
               #3#2 = nNume;
          |FINSI;
          |SI #0#12 = "A"; |O #0#12 = "T";
               #3#2 = nMesAtrasos;              || mes
          |FINSI;
          #3#3 = "";
          #3#4 = "";
          aAlfa1 = #labempre#0; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
          aAlfa2 = #labempre#2; aAlfa2 = aAlfa2 % 110;
          aAlfa3 = #labcentr#1; |QBF aAlfa3; aAlfa3 = ("00" + aAlfa3) % -102;
          aAlfa4 = #labcentr#2; aAlfa4 = aAlfa4 % 108;
          #3#5  = aAlfa1 + " " + aAlfa2 + " / " + aAlfa3 + " " + aAlfa4;
          |HAZ monta;
          |HAZ fin_linea;
     |SINO;
          |HAZ fin_linea2;
     |FINSI;
|FPRC;

|PROCESO limpia_var;
|FPRC;


|PROCESO eti;
     clave = "ETI";
     |HAZ ini_linea;

     |DDEFECTO #3;
     #3#0  = "CRA1";
     #3#1  = "2";
     #3#2  = "WS  ";
     #3#3  = "0";
     #3#4  = #1#0;         || clave autorizacion
     #3#5  = "243     ";   || proveedor nomina + 5 blancos (reservados)
     fech1 = ~;
     |HAZ fecha;
     #3#6  = for_fecha;    || fecha
     |HAZ hora;
     #3#7  = for_hora;     || hora
     #3#8  = fichero;      || fichero
     #3#9  = "CRA";        || sufijo
     #3#10 = "N";          || codigo prioridad
     #3#11 = "";           || indicador
     #3#12 = 0;            || a¤o lote
     #3#13 = 0;            || mes
     #3#14 = 0;            || serie
     #3#15 = 0;            || nro. envio
     #3#16 = 0;            || nro. documento
     #3#17 = "";           || reservado
     |HAZ monta;
     |HAZ fin_linea;
|FPRC;

|PROCESO graba_lote;
     #2#1 = fichero;       || nombre salida
     #2#2 = extension;     || extension
     #2#3 = ~;             || fecha creacion
     #2#4 = ~;             || fecha grabacion
     |SI s_num ! 0;
          #2#5 = s_num;
          #2#6 = s_fich;
          #2#7 ="fan";
     |SINO;
          #2#5 = 0;
          #2#6 = "";
          #2#7 = "";
     |FINSI;
     |GRABA 020#2b;
|FPRC;

|PROCESO cre;
     clave = "CRE";
     |HAZ ini_linea;
     |SI sw_auxil = 0; |Y #102#4 ! 0;
          |DDEFECTO #3;
          #3#0 = #102#2;            || Concepto
          #3#1 = #102#3;            || Incluido/Excluido
          #3#2 = #102#4;            || Importe
          #3#3 = #0#22;
          |SI #3#3 = "B";
               #3#2 = 0;            || Importe a cero
          |FINSI;
          #3#4 = "";                                   || reservado

          |HAZ monta;
          |HAZ fin_linea;
     |SINO;
          |HAZ fin_linea2;
     |FINSI;
|FPRC;

|DEFBUCLE Conceptos;
     #102#1;
     ;
     aAntCCC, aAntNroAfil, 0001, " ";
     aAntCCC, aAntNroAfil, 9999, "z";
     ;
     NULL, cre;
|FIN;

|PROCESO DiasDiciembre2013;
     nMes = 12;
     aMes = nMes;
     aMes = ("00" + aMes) % -102;
     aAny = 2013;

     aFecha = "22" + "." + aMes + "." + aAny;
     fIniMes = aFecha;

     |HAZ ultidia;
     aFecha = topemes;
     aFecha = ("00" + aFecha) % -102;
     aFecha = aFecha + "." + aMes + "." + aAny;
     fFinMes = aFecha;

     aAlfa = #labtraba#36;
     fFechaAlta = aAlfa;

     aAlfa = #nomhicca#5;
     ||aAlfa = #labtraba#37;
     |QBF aAlfa;
     |SI aAlfa = ""; |O aAlfa = "..........";
          aAlfa = "31.12.2999";
     |FINSI;
     fFechaBaja = aAlfa;

     |SI fFechaAlta [ fFinMes; |Y fFechaBaja ] fIniMes;
          |SI fIniMes > fFechaAlta;
               fFechaIniMes = fIniMes;
          |SINO;
               fFechaIniMes = fFechaAlta;
          |FINSI;
          |SI fFinMes < fFechaBaja;
               fFechaFinMes = fFinMes;
          |SINO;
               fFechaFinMes = fFechaBaja;
          |FINSI;

          nNume1 = fFechaIniMes;
          nNume2 = fFechaFinMes;

          aFechaIniMes = fFechaIniMes;
          aFechaFinMes = fFechaFinMes;
          nDias = nNume2 - nNume1 + 1;

          nDiasAlta = #nomhicca#8;
     |FINSI;
|FPRC;

|PROCESO AcumConcepto;
     #nomcorli#0 = nConvenio;
     #nomcorli#1 = nConcepto;
     |LEE 000#nomcorli.=;
     |SI FSalida = 0;
          nConceptoRED = #nomcorli#5;
     |FINSI;

     |SI nConceptoRED = 9999;
          |ACABA;
     |FINSI;

     |SI nConcepto ] 200; |Y nConcepto [ 299;          || ES PAGA
          |SI nConceptoRED = 4; |O nConceptoRED = 5;   || parametrizada como tal
               |SI aTipoPaga = "P";
                    || nConceptoRED = 5;
                    nConceptoRED = 0004;
               |FINSI;
          |FINSI;
     |FINSI;

     nImporte = nUnidadesCal * nPrecio;
     |SI nConcepto ] 200; |Y nConcepto [ 299;          || ES PAGA
          nImporte = nUnidades * nPrecio;
          |SI #0#17 = 0; |O #0#17 ] 5;
               || SOLO EN NOMINAS DEL MES Y ATRASOS, FINIQUITOS NO
               |SI nUnidades = 0; |Y nUnidadesCal ! 0; |Y aTipoPaga = "P";
                    nImporte = nUnidadesCal * nPrecio;

                    || raro, paga prorrateada que se cobra en un mes con IT
                    || pero que por no descontar en caso de IT, se cobra
                    || pero NO tiene que cotizar

                    || la pongo como como una mejora de prestaciones IT

                    nConceptoRED = 0055;
               |FINSI;
          |SINO;
               || arreglo cagada 20/03/2014
               |SI nUnidades = 0; |Y nUnidadesCal ! 0; |Y swPosibleError = 1;
                    |SI #0#17 = 2;
                         |SI #0#0 = 2013; |O #0#0 = 2014;
                              |SI #0#1 = 12; |O #0#1 = 1; |O #0#1 = 2;
                                   nConceptoRED = 0055;
                                   nImporte = nUnidadesCal * nPrecio;
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;

     nImporte = nImporte ! 2;
     nImporte = nImporte * 100;

     #nomconli#0 = nConvenio;
     #nomconli#2 = nConcepto;
     |LEE 000#nomconli.=;
     |SI FSalida = 0;
          aIndicativo = "I";
          |SI nConcepto ] 400; |Y nConcepto [ 499;
               |SI #nomconli#11 = "S"; |Y #nomconli#12 = "7";
                    aIndicativo = "I";
               |SINO;
                    aIndicativo = "E";
               |FINSI;
          |FINSI;
     |FINSI;
     |SI nConceptoRED = 9998;
          nConceptoRED = 0001;
          aIndicativo = "I";
     |FINSI;

     |SI #0#12 = "A"; |O #0#12 = "T";
          |SI nConceptoRED = 0005; |O nConceptoRED = 0008; |O nConceptoRED = 0010;
          |O nConceptoRED = 0011;
               || en lotes de ATRASOS, si es un concepto cualquiera por ser
               || nominas de atrasos de convenio, es decir que tienen salario
               || base, antiguedad, pluses, etc ... es siempre 0009
               || pero si es nomina de atrasos con un concepto de los de atrasos
               || especificamente, es decir el 0005, 0008, 0010 o 0011 que no
               || son de convenio, ha de salir el concepto
          |SINO;
               nConceptoRED = 0009;
          |FINSI;
     |FINSI;

     |SI #0#17 = 98;
          nConceptoRED = 0007;
     |FINSI;

     |SI swDic2013 = 1;
          nImporte = ((nImporte / 100) * #101#8) / #101#9;
          nImporte = nImporte * 100;
          nImporte = nImporte ! 0;
          |SI nConceptoRED = 0032; |Y aIndicativo = "E";
               aIndicativo = "I";
          |FINSI;
     |FINSI;

     |SI #0#17 = 2;
          |SI nConceptoRED = 0006;
               nImporte = nImporte - (#101#11 * 100);
          |FINSI;
     |FINSI;

     |SI nImporte ! 0;
          |SI nConceptoRED = 0000;
               aAlfa1 = nConvenio; |QBF aAlfa1; aAlfa1 = ("000" + aAlfa1) % -103;
               aAlfa2 = nConcepto; |QBF aAlfa2; aAlfa2 = ("000" + aAlfa2) % -103;
               aAlfa = "    ATENCION. Convenio " + aAlfa1 + ". Concepto " + aAlfa2;
               aAlfa = aAlfa + " sin equivalencia para conceptos CRA";
               |MENAV aAlfa;
          |FINSI;
          |DDEFECTO #102;
          #102#0 = #101#3;    || CCC Centro
          #102#1 = #101#4;    || Nro. Afiliacion Trabajador
          #102#2 = nConceptoRED;
          #102#3 = aIndicativo;
          |LEE 101#102=;
          FEstado = FSalida;
          #102#4 = #102#4 + nImporte;
          |SI FEstado ! 0;
               |GRABA 020#102n;
          |SINO;
               |GRABA 020#102a;
          |FINSI;
          nBrutoTrab = nBrutoTrab + (nImporte / 100);
          ||SI nConceptoRED = 5;
          |SI aTipoPaga = "P";
               nPagasProrr = nPagasProrr + (nUnidadesCal * nPrecio);
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO otros_nomcalac;
     nConcepto = 501;
     nConceptoRED = 0055;
     aDescripcion = "Complementos IT";
     aTipoPaga = "";
     nUnidades = 1;
     nUnidadesCal = 1;
     nPrecio = #nomcalac#59 + #nomcalac#60;
     aIndicativo = "E";

     |SI nPrecio > 0;
          |HAZ AcumConcepto;
     |FINSI;
|FPRC;

|PROCESO nomcalal;
     nConcepto = #nomcalal#4;
     nConceptoRED = 0;
     aDescripcion = #nomcalal#8;
     aTipoPaga = #nomcalal#5;
     nUnidades = #nomcalal#9;
     nUnidadesCal = #nomcalal#11;
     nPrecio = #nomcalal#10;

     |HAZ AcumConcepto;
|FPRC;

|DEFBUCLE nomcalal;
     #nomcalal#1;
     ;
     #nomcalac#0, #nomcalac#1, #nomcalac#2, #nomcalac#3, 101;
     #nomcalac#0, #nomcalac#1, #nomcalac#2, #nomcalac#3, 499;
     ;
     NULL, nomcalal;
|FIN;

|PROCESO otros_nomhicca;
     nConcepto = 501;
     nConceptoRED = 0055;
     aDescripcion = "Complementos IT";
     aTipoPaga = "";
     nUnidades = 1;
     nUnidadesCal = 1;
     nPrecio = #nomhicca#59 + #nomhicca#60;
     aIndicativo = "E";

     |SI nPrecio > 0;
          |HAZ AcumConcepto;
     |FINSI;
|FPRC;

|PROCESO nomhicli;
     nConcepto = #nomhicli#4;
     nConceptoRED = 0;
     aDescripcion = #nomhicli#8;
     aTipoPaga = #nomhicli#5;
     nUnidades = #nomhicli#9;
     nUnidadesCal = #nomhicli#11;
     nPrecio = #nomhicli#10;

     nDiasMes = #nomhicca#7;
     nDiasIT = #nomhicca#31 + #nomhicca#32 + #nomhicca#65 + #nomhicca#173;

     |HAZ AcumConcepto;
|FPRC;

|DEFBUCLE nomhicli;
     #nomhicli#1;
     ;
     #nomhicca#0, #nomhicca#1, #nomhicca#66, #nomhicca#2, #nomhicca#3, 101;
     #nomhicca#0, #nomhicca#1, #nomhicca#66, #nomhicca#2, #nomhicca#3, 499;
     ;
     NULL, nomhicli;
|FIN;

|PROCESO otros_nomcalca;
     nConcepto = 501;
     nConceptoRED = 0055;
     aDescripcion = "Complementos IT";
     aTipoPaga = "";
     nUnidades = 1;
     nUnidadesCal = 1;
     nPrecio = #nomcalca#59 + #nomcalca#60;
     aIndicativo = "E";

     |SI nPrecio > 0;
          |HAZ AcumConcepto;
     |FINSI;
|FPRC;

|PROCESO nomcalli;
     nConcepto = #nomcalli#4;
     nConceptoRED = 0;
     aDescripcion = #nomcalli#8;
     aTipoPaga = #nomcalli#5;
     nUnidades = #nomcalli#9;
     nUnidadesCal = #nomcalli#11;
     nPrecio = #nomcalli#10;
     nDiasMes = #nomcalca#7;
     nDiasIT = #nomcalca#31 + #nomcalca#32 + #nomcalca#65 + #nomcalca#131;

     |HAZ AcumConcepto;
|FPRC;

|DEFBUCLE nomcalli;
     #nomcalli#1;
     ;
     #nomcalca#0, #nomcalca#1, #nomcalca#2, #nomcalca#3, 101;
     #nomcalca#0, #nomcalca#1, #nomcalca#2, #nomcalca#3, 499;
     ;
     NULL, nomcalli;
|FIN;


|PROCESO tsilco11_L13;
     |SI #101#10 = 2;
          || si, porque entonces no lo ha puesto en el anterior

          |HAZ MensaTraba;
     |FINSI;

     #labtraba#0 = #101#0;
     #labtraba#1 = #101#1;
     |LEE 000#labtraba.=;
     |HAZ trb;

     #102#2 = 0006;
     #102#3 = "I";
     #102#4 = #101#11 * 100;
     |HAZ cre;
|FPRC;

|PROCESO MensaTraba;
     nNroTrabaj = nNroTrabaj + 1;
     aAlfa = nNroTrabaj;
     aAlfa1 = nNroTras;
     aAlfa = "Procesando trabajador " + aAlfa + " de " + aAlfa1;
     aAlfa = " " + aAlfa + "                                  ";
     aAlfa = aAlfa % 135;
     |ATRI R;
     |PINTA 2221, aAlfa;
     |ATRI I;
|FPRC;

|PROCESO tsilco11;
     |SI swTipoBucle11 = 1;
          || para hacer los finiquitos
          |HAZ tsilco11_L13;
          |ACABA;
     |FINSI;

     |SI swTipoBucle11 = 2;
          || para contar si hay trabajadores
          nContTmp = nContTmp + 1;
          |ACABA;
     |FINSI;

     #labtraba#0 = #101#0;
     #labtraba#1 = #101#1;
     |LEE 000#labtraba.=;
     |SI FSalida = 0;
          nConvenio = #labtraba#87;
     |FINSI;

     |HAZ MensaTraba;

     |SI aAntNroAfil ! #101#4; |O aAntCCC ! #101#3;
          |SI aAntCCC ! #101#3;
||O aAntCen ! #101#2;

          || migue: hay que comentar el "|O aAntCen ! #101#2"
          || cambiado ya definitivamente, el nuevo sistema: 01.09.2015
          |SINO;
               |BUCLE Conceptos;
          |FINSI;
          |SI #101#10 ! 2;
               |HAZ trb;
          |FINSI;
     |FINSI;

     nTipoNominas = #0#17;
     |SI nTipoNominas = 98;
          nTipoNominas = 00;
     |FINSI;

     |SI #0#12 = "M";
          nBrutoTrab = 0;
          nPagasProrr = 0;
          #nomcalca#0 = #101#0;
          #nomcalca#1 = #101#1;
          #nomcalca#2 = #0#1;
          #nomcalca#3 = nTipoNominas;
          |LEE 000#nomcalca.=;
          |SI FSalida = 0;
               |BUCLE nomcalli;
               |HAZ otros_nomcalca;
          |FINSI;
     |FINSI;

     |SI #0#12 = "H";
          nBrutoTrab = 0;
          nPagasProrr = 0;
          #nomhicca#0 = #101#0;
          #nomhicca#1 = #101#1;
          #nomhicca#2 = #0#1;
          #nomhicca#3 = nTipoNominas;
          #nomhicca#66 = #0#0 - 2000;
          |LEE 000#nomhicca.=;
          |SI FSalida = 0;
               |BUCLE nomhicli;
               |HAZ otros_nomhicca;
          |FINSI;
     |FINSI;

     |SI #0#12 = "T";
          nBrutoTrab = 0;
          nPagasProrr = 0;
          #nomhicca#0 = #101#0;
          #nomhicca#1 = #101#1;
          #nomhicca#2 = nMesAtrasos;
          #nomhicca#3 = nTipoNominas;
          #nomhicca#66 = #0#0 - 2000;
          |LEE 000#nomhicca.=;
          |SI FSalida = 0;
               |BUCLE nomhicli;
               |HAZ otros_nomhicca;
          |FINSI;
     |FINSI;

     |SI #0#12 = "A";
          nBrutoTrab = 0;
          nPagasProrr = 0;

          #nomcalac#0 = #101#0;
          #nomcalac#1 = #101#1;
          #nomcalac#2 = nMesAtrasos;
          #nomcalac#3 = #0#17;
          |LEE 000#nomcalac.=;
          |SI FSalida = 0;
               |BUCLE nomcalal;
               |HAZ otros_nomcalac;
          |FINSI;
     |FINSI;

     #101#5 = #101#5 - nPagasProrr;
     #101#6 = nBrutoTrab;
     #101#7 = #101#5 - #101#6;
     |GRABA 020#101a;
     |LIBERA #101;

     aAntCen = #101#2;
     aAntCCC = #101#3;
     aAntNroAfil = #101#4;
|FPRC;

|DEFBUCLE tsilco11;
     #101#1;
     10;
     || #100#0, #100#2, #100#1, "            ", 00001, nDesdeMarca;  || emp, ccc, cen, afil, tra
     || #100#0, #100#2, #100#1, "zzzzzzzzzzzz", 99999, nHastaMarca;
     #100#0, #100#2, 01, "            ", 00001, nDesdeMarca;  || emp, ccc, cen, afil, tra
     #100#0, #100#2, 99, "zzzzzzzzzzzz", 99999, nHastaMarca;
     || migue
     || hay que cambiar las comentadas por las otras
     || OJO, la clave del tsilco10 ha de tener solo dos campos:
     || el codigo de empresa y el CCC
     || esta todo cambiado definitivamente: 01.09.2015
     be;
     NULL, tsilco11;
|FIN;

|PROCESO regimen;
     |ABRE #silcon15;
     #silcon15#0 = #100#0;
     #silcon15#1 = #100#1;
     |LEE 000#silcon15.=;
     |SI FSalida = 0;
          aRegimen = #silcon15#2;
     |SINO;
          |ABRE #6;
          #6#0 = #100#0;
          |LEE 000#6=;
          |SI FSalida = 0;
               aRegimen = #6#2;
          |FINSI;
          |CIERRA #6;
     |FINSI;
     |CIERRA #silcon15;

     |QBF aRegimen;
     |SI aRegimen ! "";
          aRegimen = ("0000" + aRegimen) % -104;
     |FINSI;

     |SI aRegimen = "0613";
          aRegimen = "0163";
     |FINSI;
|FPRC;

|PROCESO L13MesSig;
     || Tipo bucle es 1 para el bucle tsilcon11
     || es decir que es solo para buscar los trabajadores a los que hay
     || que declararles el finiquito o bien estre el mes de calculo y el
     || siguiente o todo en el siguiente

     swTipoBucle11 = 1;
     nDesdeMarca = 1;
     nHastaMarca = 2;
     |BUCLE tsilco11;
|FPRC;

|PROCESO tsilco10;
     |SI #0#17 = 2;
          || en finiquito, cuento en primer lugar si hay algun trabajador que
          || tenga base el mes de calculo, que es nDesdeMarca con valor 0 y 1

          || tipo bucle 2 para el tsilco11, es decir, para contar solo
          || en este caso si hay finiquitos del primer mes

          swTipoBucle11 = 2;
          nDesdeMarca = 0;
          nHastaMarca = 1;
          nContTmp = 0;
          |BUCLE tsilco11;
     |SINO;
          || si no es finiquito el contrador lo pongo a uno, para que siga su
          || curso normal

          nContTmp = 1;
     |FINSI;

     |HAZ regimen;

     || swPosibleError = 0;
     || BUCLE silcon07;

     #labempre#0 = #100#0;
     |LEE 000#labempre.=;

     #labcentr#0 = #100#0;
     #labcentr#1 = #100#1;
     |LEE 000#labcentr.=;

     |SI nAntEmp ! #100#0;
          nNroEmpresas = nNroEmpresas + 1;
          aAlfa = nNroEmpresas;
          aAlfa1 = nTotEmp;
          aAlfa = "Procesando empresa " + aAlfa + " de " + aAlfa1;
          aAlfa = " " + aAlfa + "                                  ";
          aAlfa = aAlfa % 135;
          |ATRI R;
          |PINTA 2121, aAlfa;
          |ATRI I;
     |FINSI;

     |SI nContTmp ! 0;
          || si estoy en finiquito y el contador esta a cero quiere decir
          || que no tengo ningun trabajador con base para el mes de calculo
          || lo que haya estara el mes siguiente

          |SI aAntCCC ! #100#2;
               swBaseMesSig = 0;
               aCCC = aRegimen + #100#2;

               |HAZ dde;
          |FINSI;

          swTipoBucle11 = 0;
          nDesdeMarca = 0;
          nHastaMarca = 2;
          |BUCLE tsilco11;

          |BUCLE Conceptos;        || el ultimo
     |FINSI;

     |SI #0#17 = 2;
          || solo para los finiquitos, compruebo si tengo que construir seg-
          || mentos para el mes siguiente, estos son los que tienen la marca
          || con valor 1 (mes de calculo y siguiente) o 2 (solo mes siguiente)

          swTipoBucle11 = 2;
          nDesdeMarca = 1;
          nHastaMarca = 2;
          nContTmp = 0;
          |BUCLE tsilco11;

          |SI nContTmp ! 0;
               || si hay trabajadores en este caso, tengo que construir de
               || nuevo el DDE y despues hacer el bucle (en L13MesSig)

               swBaseMesSig = 1;
               aCCC = aRegimen + #100#2;
               |HAZ dde;

               |HAZ L13MesSig;
          |FINSI;
     |FINSI;

     aAntCCC = #100#2;
     nAntEmp = #100#0;
|FPRC;

|DEFBUCLE tsilco10;
     #100#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, tsilco10;
|FIN;


_________________/ TRASPASO DE nomcalca,nomcalac y nomhicca AL tsilcon1
|PROCESO CompCentro;
     #labtraba#0 = nEmp;
     #labtraba#1 = nTra;
     |LEE 000#labtraba.=;
     |SI FSalida = 0;
     /*
          #tsilcon9#0 = #labtraba#0;
          #tsilcon9#1 = #labtraba#77;
          |LEE 000#tsilcon9.=;
          |SI FSalida = 0;
               |SI #tsilcon9#3 ! "*";
                    swStop = 1;
               |FINSI;
          |FINSI;
     */
     |FINSI;
|FPRC;

|PROCESO cta_cotizacion;
     aCCC = #labempre#13;          || cta.cot. de la empresa
     |SI #labtraba#45 = "085"; |O #labtraba#45 = "087"; |O #labtraba#45 = "097";
     |O #labtraba#45 = "421"; |O #labtraba#45 = "422";
          |DDEFECTO #nomcenes;
          |ABRE #nomcenes;
          #nomcenes#0 = #labtraba#0;
          #nomcenes#13 = #labtraba#77;
          #nomcenes#1 = 87;
          |LEE 000#nomcenes.=;
          |SI FSalida = 0;
               aCCC = #nomcenes#10;      || cta.cto. del centro de trabajo especial
          |FINSI;
          |CIERRA #nomcenes;
     |SINO;
          |DDEFECTO #labcentr;
          |ABRE #labcentr;
          #labcentr#0 = #labtraba#0;
          #labcentr#1 = #labtraba#77;
          |LEE 000#labcentr.=;
          |SI FSalida = 0;
               aCCC = #16#10;      || cta.cot. centro de trabajo
          |FINSI;
          |CIERRA #labcentr;
     |FINSI;
     |QBF aCCC;
     |SI aCCC ! "";
          aCCC = ("00000000000" + aCCC) % -111;
     |FINSI;
|FPRC;

|PROCESO GrabaTemporal;
     || GRABACION DEL TEMPORAL

     #100#0 = #labtraba#0;
     |HAZ cta_cotizacion;
     #100#2 = aCCC;
     #100#1 = #labtraba#77;

     |LEE 101#100=;
     |SI FSalida ! 0;
          |SI nAntEmpCon ! #100#0;
               nTotEmp = nTotEmp + 1;
          |FINSI;
          |GRABA 020#100n;
          |LIBERA #100;
          nAntEmpCon = #100#0;
     |FINSI;

     |DDEFECTO #101;
     #101#0 = #labtraba#0;         || empresa
     #101#3 = aCCC;                || ccc
     #101#4 = #labtraba#75;        || nro. afil.
     #101#1 = #labtraba#1;         || codigo trabajador
     |LEE 101#101=;
     FEstado = FSalida;

     #101#2 = #labtraba#77;
     #101#5 = nBrutoMenosPrest;

     #101#8 = nDias;
     #101#9 = nDiasAlta;

     |SI #0#17 = 2; |Y nBaseMesSig ! 0;
          || en finiquitos, me guardo en estos dos campos, en el 10 la marca
          || de que tengo una parte de la base de vacaciones que declarar el
          || mes siguiente y en el 11 esa base

          #101#10 = 1;
          #101#11 = nBaseMesSig;
          nNume = nImporteFin - nBaseMesSig;
          nNume = nNume ! 0;
          |SI nNume = 0;
               || esto es un trabajador que es baja el ultimo dia de mes
               || por lo que la base de vacaciones le tiene que salir toda
               || al mes siguiente, lo marco poniendo el campo a 2, luego me
               || hara falta para no pintar DDE ni TRB ni cosas de esas si
               || no me hace falta

               #101#10 = 2;
          |FINSI;
     |FINSI;

     |SI FEstado ! 0;
          nNroTras = nNroTras + 1;
          |GRABA 020#101n;
          |LIBERA #101;
     |FINSI;
|FPRC;

|PROCESO nomcalac;
     #23#1 = #21#0;
     #23#2 = #21#1;
     |LEE 000#23=;
     |SI FSalida = 0;|Y #23#7 ! "*";    |ACABA;   |FINSI;

     #labtraba#0 = #nomcalac#0;
     #labtraba#1 = #nomcalac#1;
     |LEE 000#labtraba.=;
     |SI FSalida = 0;
          |SI #labtraba#247 = 121; |O #labtraba#247 = 138; |O #labtraba#42 = "H";
               |ACABA;
          |FINSI;
          |SI aParam = "silcon09";
               |SI #labtraba#247 = 163; |O #labtraba#247 = 613;
                    |ACABA;
               |FINSI;
          |FINSI;
          |SI aParam = "silcon16";
               |SI #labtraba#247 ! 163; |Y #labtraba#247 ! 613;
                    |ACABA;
               |FINSI;
          |FINSI;

          swStop = 0;
          nEmp = #nomcalac#0;
          nTra = #nomcalac#1;
          |HAZ CompCentro;
          |SI swStop = 1;
               |ACABA;
          |FINSI;

          |SI #nomcalac#8 = 0; |ACABA; |FINSI;
     |FINSI;

     nBrutoMenosPrest = #nomcalac#46 - #nomcalac#57 - #nomcalac#58 - #nomcalac#73 + #nomcalac#38;
     nNume = nBrutoMenosPrest ! 2;
     |SI nNume > 0;
          |HAZ GrabaTemporal;
     |FINSI;
|FPRC;

|DEFBUCLE nomcalac;
     #21#1;
     ;
     #6#0,     1, nMesAtrasos, 5;
     #6#0, 99999, nMesAtrasos, 5;
     ;
     NULL, nomcalac;
|FIN;

|PROCESO CompFinHis;
     |SI #nomhicli#4 < 200; |O #nomhicli#4 > 299;
          nImporteFin = nImporteFin + (#nomhicli#10 * #nomhicli#11);
     |FINSI;
     |SI #nomhicli#4 = 125; |O #nomhicli#4 = 421;
          nDiasVac = #nomhicli#11;
          nImporteVac = #nomhicli#10 * #nomhicli#11;
     |FINSI;
|FPRC;

|DEFBUCLE CompFinHis;
     #nomhicli#1;
     ;
     #nomhicca#0, #nomhicca#1, #nomhicca#66, #nomhicca#2, #nomhicca#3, 001;
     #nomhicca#0, #nomhicca#1, #nomhicca#66, #nomhicca#2, #nomhicca#3, 999;
     ;
     NULL, CompFinHis;
|FIN;

|PROCESO nomhicca;
     #23#1 = #19#0;
     #23#2 = #19#1;
     |LEE 000#23=;
     |SI FSalida = 0;|Y #23#7 ! "*";    |ACABA;   |FINSI;
     #labtraba#0 = #nomhicca#0;
     #labtraba#1 = #nomhicca#1;
     |LEE 000#labtraba.=;
     |SI FSalida = 0;
          |SI #labtraba#247 = 121; |O #labtraba#247 = 138; |O #labtraba#42 = "H";
               |ACABA;
          |FINSI;
          |SI aParam = "silcon09";
               |SI #labtraba#247 = 163; |O #labtraba#247 = 613;
                    |ACABA;
               |FINSI;
          |FINSI;
          |SI aParam = "silcon16";
               |SI #labtraba#247 ! 163; |Y #labtraba#247 ! 613;
                    |ACABA;
               |FINSI;
          |FINSI;

          nDias = 0;
          nDiasAlta = 0;
          |SI swDic2013 = 1;
               |HAZ DiasDiciembre2013;
               |SI nDias = 0;
                    |ACABA;
               |FINSI;
          |FINSI;

          |SI #nomhicca#8 = 0; |ACABA; |FINSI;
     |FINSI;

     swStop = 0;
     nEmp = #19#0;
     nTra = #19#1;
     |HAZ CompCentro;
     |SI swStop = 1;
          |ACABA;
     |FINSI;

     |SI #0#17 = 2;
          nBrutoMenosPrest = #nomhicca#46 + #nomhicca#38;
     |SINO;
          nBrutoMenosPrest = #nomhicca#46 - #nomhicca#57 - #nomhicca#58 - #nomhicca#112 + #nomhicca#38;
     |FINSI;

     nImporteFin = 0;
     nDiasMesSig = 0;
     nBaseMesSig = 0;
     nDiasVac = 0;
     nImporteVac = 0;

     |SI #nomhicca#3 = 2;     || solo en finiquitos, compruebo que haya algo
          |BUCLE CompFinHis;  || ademas de la paga

          aAlfa = #nomhicca#5;
          aAlfa = aAlfa % 102;
          nDiasAlta = aAlfa;
          nDiasVac = (nDiasVac + 0.49) ! 0;

          |SI nDiasVac ! 0;
               || en finiquitos solo, si tengo dias de vacaciones, compruebo que
               || estos los pueda cotizar durante el mes, si no es asi, pasaran
               || al mes siguiente, me guardare la base en nBaseMesSig

               nMes = #0#1;
               nAny = #0#0;
               |HAZ topemes_plb;
               nNume = nDiasAlta + nDiasVac;
               |SI nNume > nTopemes;
                    nDiasMesSig = nNume - nTopemes;
                    nBaseMesSig = (nImporteVac * nDiasMesSig / nDiasVac) ! 2;
               |FINSI;
          |FINSI;
     |SINO;
          nImporteFin = 1;
     |FINSI;

     nNume = nBrutoMenosPrest ! 0;
     |SI nNume > 0; |Y nImporteFin > 0;
          |HAZ GrabaTemporal;
     |FINSI;
|FPRC;

|DEFBUCLE nomhicca;
     #19#1;
     ;
     #6#0,     1, any_hist, nDesdeMes, tipo_bot;
     #6#0, 99999, any_hist, nHastaMes, tipo_top;
     ;
     NULL, nomhicca;
|FIN;

|DEFBUCLE nomhicca_at;
     #19#1;
     ;
     #6#0,     1, any_hist, nMesAtrasos, tipo_bot;
     #6#0, 99999, any_hist, nMesAtrasos, tipo_top;
     ;
     NULL, nomhicca;
|FIN;

|PROCESO CompFinMen;
     |SI #nomcalli#4 < 200; |O #nomcalli#4 > 299;
          nImporteFin = nImporteFin + (#nomcalli#10 * #nomcalli#11);
     |FINSI;
     |SI #nomcalli#4 = 125; |O #nomcalli#4 = 421;
          nDiasVac = #nomcalli#11;
          nImporteVac = #nomcalli#10 * #nomcalli#11;
     |FINSI;
|FPRC;

|DEFBUCLE CompFinMen;
     #nomcalli#1;
     ;
     #nomcalca#0, #nomcalca#1, #nomcalca#2, #nomcalca#3, 001;
     #nomcalca#0, #nomcalca#1, #nomcalca#2, #nomcalca#3, 999;
     ;
     NULL, CompFinMen;
|FIN;

|PROCESO nomcalca;
     #23#1 = #18#0;
     #23#2 = #18#1;
     |LEE 000#23=;
     |SI FSalida = 0;|Y #23#7 ! "*";    |ACABA;   |FINSI;

     #labtraba#0 = #nomcalca#0;
     #labtraba#1 = #nomcalca#1;
     |LEE 000#labtraba.=;
     |SI FSalida = 0;
          |SI #labtraba#247 = 121; |O #labtraba#247 = 138; |O #labtraba#42 = "H";
               |ACABA;
          |FINSI;
          |SI aParam = "silcon09";
               |SI #labtraba#247 = 163; |O #labtraba#247 = 613;
                    |ACABA;
               |FINSI;
          |FINSI;
          |SI aParam = "silcon16";
               |SI #labtraba#247 ! 163; |Y #labtraba#247 ! 613;
                    |ACABA;
               |FINSI;
          |FINSI;

          swStop = 0;
          nEmp = #nomcalca#0;
          nTra = #nomcalca#1;
          |HAZ CompCentro;
          |SI swStop = 1;
               |ACABA;
          |FINSI;

          |SI #nomcalca#8 = 0; |ACABA; |FINSI;
     |FINSI;

     |SI #0#17 = 2;
          nBrutoMenosPrest = #nomcalca#46 + #nomcalca#38;
     |SINO;
          nBrutoMenosPrest = #nomcalca#46 - #nomcalca#57 - #nomcalca#58 - #nomcalca#73 + #nomcalca#38;
     |FINSI;

     nImporteFin = 0;
     nDiasMesSig = 0;
     nBaseMesSig = 0;
     nImporteVac = 0;
     nDiasVac = 0;

     |SI #nomcalca#3 = 2;     || solo en finiquitos, compruebo que haya algo
          |BUCLE CompFinMen;  || ademas de la paga

          aAlfa = #nomcalca#5;
          aAlfa = aAlfa % 102;
          nDiasAlta = aAlfa;
          nDiasVac = (nDiasVac + 0.49) ! 0;

          |SI nDiasVac ! 0;
               || tengo vacaciones
               nMes = #0#1;
               nAny = #0#0;
               |HAZ topemes_plb;
               nNume = nDiasAlta + nDiasVac;
               |SI nNume > nTopemes;
                    nDiasMesSig = nNume - nTopemes;
                    nBaseMesSig = (nImporteVac * nDiasMesSig / nDiasVac) ! 2;
               |FINSI;
          |FINSI;
     |SINO;
          nImporteFin = 1;
     |FINSI;

     nNume = nBrutoMenosPrest ! 0;
     |SI nNume > 0; |Y nImporteFin > 0;
          |HAZ GrabaTemporal;
     |FINSI;
|FPRC;

|DEFBUCLE nomcalca;
     #18#1;
     ;
     #6#0,     1, #0#1, tipo_bot;
     #6#0, 99999, #0#1, tipo_top;
     ;
     NULL, nomcalca;
|FIN;

|PROCESO control;
     |DDEFECTO #36; |ABRE #36; |LEE 000#36p;
     |CIERRA #36;
|FPRC;

|PROCESO carga_temporal;
     |HAZ control;
     |ABRE #23;
     ||ABRE #tsilcon9;
     |SELECT #2#23;
     any_hist = #0#0 $ 100;    || a¤o historico

     tipo_bot = #0#17;         || limites de tipo para el bucle
     tipo_top = #0#17;         || si es el RED, lo que vengan del DEF

     |SI #0#17 = 98;
          tipo_bot = 0;
          tipo_top = 0;
     |FINSI;

     nDesdeMes = #0#1;
     |SI #0#12 = "A"; |O #0#12 = "T";
          nHastaMes = #0#13;
     |SINO;
          nHastaMes = nDesdeMes;
     |FINSI;

     |SI #0#12 = "M";    |BUCLE nomcalca;     |FINSI;
     |SI #0#12 = "H";    |BUCLE nomhicca;     |FINSI;
     |SI #0#12 = "A";    |BUCLE nomcalac;     |FINSI;
     |SI #0#12 = "T";    |BUCLE nomhicca_at;     |FINSI;

     ||CIERRA #tsilcon9;
     |CIERRA #23;
|FPRC;

|PROCESO empresas1;
     #labempre#0 = #6#0;
     |LEE 000#labempre.=;
     |SI FSalida = 0;
          |DDEFECTO #15;
          #15#0 = #labempre#0;
          |LEE 000#15=;
          |SI swSelEmp ! 0;
               |SI FSalida ! 0; |O #15#2 ! "*";
                    |ACABA;
               |FINSI;
          |FINSI;

          |ATRI 0;
          |PINTA 2101, "                                                                           ";
          |PINTA 2201, "                                                                           ";
          |PINTA 2301, "                                                                           ";
          |ATRI R;
          |SI #0#12 = "A"; |O #0#12 = "T";
               aAlfa = nMesAtrasos; |QBF aAlfa; aAlfa = ("00" + aAlfa) % -102;
               aAlfa = " ATRASOS. MES: " + aAlfa + " ";
               |PINTA 2030, aAlfa;
          |FINSI;
          |PINTA 2210, "Generando trabajadores a incluir en el lote CRA ...";
          |PINTA 2310, "Procesando ... ";
          |ATRI I;
          |PINTA 2326, #labempre#0;
          |PINTA 2332, #labempre#2;

          |HAZ carga_temporal;       || recarga del Tmp. TC2

          |HAZ actua_registro;
     |FINSI;
|FPRC;

|DEFBUCLE empresas;
     #6#1;
     ;
         1;
     99999;
     ;
     NULL, empresas1;
|FIN;

|PROCESO GeneracionCRA;
     |DELINDEX #100;
     |DELINDEX #101;
     |DELINDEX #102;

     |ABRE #100;
     |ABRE #101;
     |ABRE #102;

     |BUCLE empresas;

     ||CONSULTA_CLAVE #1#100;
     ||CONSULTA_CLAVE #1#101;

     nNroEmpresas = 0;
     aAntNroAfil = "";
     aAntCCC ="";
     aAntCen ="";
     nAntEmp = 0;
     nNroEmpresas = 0;

     |ATRI 0;
     |PINTA 2102, "                                                                         ";
     |PINTA 2202, "                                                                         ";
     |PINTA 2302, "                                                                         ";

     |BUCLE tsilco10;

     ||CONSULTA_CLAVE #1#101;

     |CIERRA #102;
     |CIERRA #101;
     |CIERRA #100;

     |DELINDEX #102;
     |DELINDEX #101;
     |DELINDEX #100;
|FPRC;

|PROCESO tipo3;
     |ABRE #1;
     |ABRE #2;
     |ABRE #4;
     |ABRE #5;
     |ABRE #labempre;
     |ABRE #12;
     |ABRE #15;
     |ABRE #labcentr;

     |ABRE #nomcalca;
     |ABRE #nomcalli;
     |ABRE #nomhicca;
     |ABRE #nomhicli;
     |ABRE #nomcalac;
     |ABRE #nomcalal;
     |ABRE #nomcorli;
     |ABRE #nomconli;

     #2#0 = #0#2;
     |LEE 000#2=;
     |SI FSalida = 0;|Y aParam ! "TC2";
          |MENAV "    Numero de Lote YA existente ...";
     |SINO;
          fichero = #0#3;
          extension = "cra";

          |HAZ graba_lote;
          |HAZ eti;

          aAlfa = Usuario; |QBT aAlfa; aAlfa = ("w" + aAlfa) % 108;
          |NOME_DAT #100 aAlfa;
          aAlfa = Usuario; |QBT aAlfa; aAlfa = ("x" + aAlfa) % 108;
          |NOME_DAT #101 aAlfa;
          aAlfa = Usuario; |QBT aAlfa; aAlfa = ("z" + aAlfa) % 108;
          |NOME_DAT #102 aAlfa;

          |SI #0#11 = "N";
               swSelEmp = 0;
          |SINO;
               swSelEmp = 1;
          |FINSI;

          |SI #0#12 = "M"; |O #0#12 = "H";
               |HAZ GeneracionCRA;
          |FINSI;
          |SI #0#12 = "A"; |O #0#12 = "T";
               |PARA nMesAtrasos = #0#1; |SI nMesAtrasos [ #0#13; |HACIENDO nMesAtrasos = nMesAtrasos + 1;
                    nNroTras = 0;
                    nNroTrabaj = 0;
                    nTotEmp = 0;
                    |HAZ GeneracionCRA;
               |SIGUE;
          |FINSI;
     |FINSI;

     |SI nNroEmpresas ! 0;
          aAlfa1 = nNroEmpresas; |QBF aAlfa1;
          aAlfa2 = nNroTrabaj; |QBF aAlfa2;
          aAlfa0 = #0#2; |QBF aAlfa0; aAlfa0 = ("00000" + aAlfa0) % -105;
          aAlfa = "    Se ha creado el lote CRA código " + aAlfa0 + ".";

          |SI #0#12 = "M"; |O #0#12 = "H";
               |SI nNroEmpresas > 1;
                    aAlfa = aAlfa + " Se incluyen " + aAlfa1 + " empresas ";
               |SINO;
                    aAlfa = aAlfa + " Se incluye 1 empresa ";
               |FINSI;
               aAlfa = aAlfa + " y " + aAlfa2;
               |SI nNroTrabaj > 1;
                    aAlfa = aAlfa + " trabajadores.";
               |SINO;
                    aAlfa = aAlfa + " trabajador.";
               |FINSI;
          |FINSI;

          |SI #0#12 = "A"; |O #0#12 = "T";
               aAlfa0 = #0#1; aAlfa0 = ("00" + aAlfa0) % -102;
               aAlfa1 = #0#13; aAlfa1 = ("00" + aAlfa1) % -102;
               aAlfa2 = #0#18;
               aAlfa = aAlfa - "." + " ";
               aAlfa = aAlfa + "correspondiente a Atrasos del mes " + aAlfa0;
               aAlfa = aAlfa + " al " + aAlfa1 + " de " + aAlfa2;
          |FINSI;

          |MENAV aAlfa;

          |SI aParam = "";
               |HAZ GrabaLote;
          |FINSI;
     |SINO;
          aAlfa = "    No se ha encontrado ningún trabajador con la selección realizada";
          aAlfa = aAlfa + " para generar el fichero CRA";
          |MENAV aAlfa;
     |FINSI;

     |LIBERA #2;

     |CIERRA #nomconli;
     |CIERRA #nomcorli;
     |CIERRA #nomcalal;
     |CIERRA #nomcalac;
     |CIERRA #nomhicli;
     |CIERRA #nomhicca;
     |CIERRA #nomcalli;
     |CIERRA #nomcalca;

     |CIERRA #labcentr;
     |CIERRA #1;
     |CIERRA #2;
     |CIERRA #4;
     |CIERRA #5;
     |CIERRA #labempre;
     |CIERRA #12;
     |CIERRA #15;

     |DBASS aAlfa; |QBF aAlfa;
     |SI aAlfa ! "/u/dsprofes/";
          ||DELINDEX #tsilcon9;
          |DELINDEX #15;      || parrilla empresas        se queda con la seleccion
          |DELINDEX #23;      || parrilla trabajadores    para volver a hacer el
     |FINSI;                                              || lote
|FPRC;

|PROCESO ElProceso;
     |GRABA 000#0a;
     |CIERRA #0;

     nElAny = #0#18;
     nElMes = #0#1;

     enDesCen = 1;
     enHasCen = 99;
     eaFuente = #0#12;

     |HAZ tipo3;

     |LIBERA #1;
     |CIERRA #1;
|FPRC;

|PROGRAMA;
     |HAZ PaseAutorizacion;
     aParam = PARAMETRO;
     |QBF aParam;
     |SI aParam = "";
          |CLS;
          |CABEZA "CREACION FICHERO CRA";

          |ABRE #0;
          |LEE 000#0p;
          |SI FSalida ! 0; |GRABA 020#0n; |FINSI;

          |DDEFECTO #0;
          |PINPA #0#0;
          |PINDA #0#0;
          |ENDATOS #1#0;
          |SI FSalida = 0;
               swDic2013 = 0;
               |SI #0#0 = 2013; |Y #0#1 = 12; |Y #0#12 = "H";
                    |SI #0#17 = 0; |O #0#17 = 2;
                         swDic2013 = 1;
                    |FINSI;
               |FINSI;
               |HAZ ElProceso;
          |FINSI;
     |SINO;
          #0#12 = s_ori;
          #0#17 = s_tat;
          |SI s_ele = "L02";
               #0#17 = 98;
          |FINSI;
          #0#0 = s_any;
          #0#1 = s_mes;
          #0#13 = s_has;
          #0#18 = s_anyl;
          #0#11 = s_sel;
          #0#2 = s_lotecra;
          #0#3 = s_fichcra;

          alfa1 = Usuario; |QBT alfa1;    alfa1 = ("t" + alfa1) % 108;
          alfa2 = Usuario; |QBT alfa2;    alfa2 = ("u" + alfa2) % 108;
          alfa3 = Usuario; |QBT alfa3;    alfa3 = ("v" + alfa2) % 108;
          |NOME_DAT #15 alfa1;
          |NOME_DAT #23 alfa2;
          ||NOME_DAT #tsilcon9#alfa3#;

          |HAZ ElProceso;
     |FINSI;
|FPRO;

|PROCESO GrabaLote;
     aAlfa1 = #0#2; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
     aAlfa2 = #0#3; |QBF aAlfa2;
     aAlfa3 = #0#4; |QBF aAlfa3;
     aAlfa = "    S¿Desea grabar el fichero " + aAlfa2 + "." + aAlfa3 + "?";
     |CONFI aAlfa;
     |SI FSalida = 0;
          enLote = #0#2;
          eaFich = #0#3; |QBF eaFich;
          eaExt = #0#4; |QBF eaExt;
          |SUB_EJECUTA "silcon05;silcon09";
     |FINSI;
|FPRC;
