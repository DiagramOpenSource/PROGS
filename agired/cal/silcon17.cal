|FICHEROS;
     silcon17 #0;
     silcon18;
     nomhicca;
     labtraba;
     labparma;
     labparhi;
|FIN;

|VARIABLES;
     nContador = 0;
     nCampo = 0;
     &nEmpExt = 0;
     &nTraExt = 0;
     &fFecExt = @;
     aParam = "";
     i = 0;
     nume1 = 0;
     nume2 = 0;
     nume3 = 0;
     nume4 = 0;
     BaseHE = 0;
     alfa1 = "";
     alfa2 = "";
     alfa3 = "";
     alfa4 = "";
     aAlfa = "";
     aAlfa1 = "";
     aAlfa2 = "";
     &aAccExt = "";
     aCambios = "";
     swNoGrabes = 0;
     nNume = 0;
     nMensaje = 0;
     nParte = 0;
     fFechaActual = @;
     nParteActual = 0;
|FIN;

/******************** PROCESOS DESDE LA PANTALLA ****************************/

|PROCESO AltaBaja;
     |C_M #0#24, 1, "N";
     |C_V #0#24, 1, "N";
     |C_M #0#25, 1, "N";
     |C_V #0#25, 1, "N";
     |C_M #0#26, 1, "N";
     |C_V #0#26, 1, "N";
     |C_M #0#27, 1, "N";
     |C_V #0#27, 1, "N";
     |C_M #0#28, 1, "N";
     |C_V #0#28, 1, "N";
     |C_M #0#29, 1, "N";
     |C_V #0#29, 1, "N";
     |PINTA 2112, "                                     ";
     |PINTA 2212, "                                                             ";
     |SI #0#3 = "A";
          |C_M #0#22, 1, "S";
          |C_V #0#22, 1, "S";
          |C_M #0#23, 1, "S";
          |C_V #0#23, 1, "S";

          |C_M #0#5, 1, "N";
          |C_M #0#8, 1, "N";
          |C_M #0#9, 1, "N";
          |C_M #0#10, 1, "N";
          |C_V #0#5, 1, "N";
          |C_V #0#8, 1, "N";
          |C_V #0#9, 1, "N";
          |C_V #0#10, 1, "N";
          |BLANCO 14111572;

          |C_M #0#14, 1, "N";
          |C_M #0#15, 1, "N";
          |C_M #0#16, 1, "N";
          |C_M #0#17, 1, "N";
          |C_V #0#14, 1, "N";
          |C_V #0#15, 1, "N";
          |C_V #0#16, 1, "N";
          |C_V #0#17, 1, "N";
          |BLANCO 17112272;

          |PINTA 1244, "Fecha del Alta:";
          |PINTA 1344, "Causa del Alta:";
          |PINTA #0#22;
          |PINTA #0#23;
     |FINSI;
     |SI #0#3 = "B";
          |C_M #0#22, 1, "N";
          |C_V #0#22, 1, "N";
          |C_M #0#23, 1, "N";
          |C_V #0#23, 1, "N";
          |PINTA 1244, "                           ";
          |PINTA 1344, "                           ";

          |PINTA 1412, "Recaída ........:               Categoría ....:";
          |PINTA 1512, "Días en baja ...:               Meses en baja.: "
          |ATRI I;
          |PINTA 1712, "- Contratos a tiempo parcial/fijos discontinuos -   ";
          |ATRI 0;
          |PINTA 1812, "Suma bases cotización:          Suma días cotizados: ";
          |ATRI I;
          |PINTA 1912, "- Resto de contratos - ";
          |ATRI 0;
          |PINTA 2012, "Base de cotización ..:          Días cotizados mes :";

          |C_M #0#5, 1, "S";
          |C_M #0#8, 1, "S";
          |C_M #0#9, 1, "S";
          |C_M #0#10, 1, "S";
          |C_V #0#5, 1, "S";
          |C_V #0#8, 1, "S";
          |C_V #0#9, 1, "S";
          |C_V #0#10, 1, "S";
          |PINTA #0#5;
          |PINTA #0#8;
          |PINTA #0#9;
          |PINTA #0#10;

          |C_M #0#14, 1, "S";
          |C_M #0#15, 1, "S";
          |C_M #0#16, 1, "S";
          |C_M #0#17, 1, "S";
          |C_V #0#14, 1, "S";
          |C_V #0#15, 1, "S";
          |C_V #0#16, 1, "S";
          |C_V #0#17, 1, "S";
          |PINTA #0#14;
          |PINTA #0#15;
          |PINTA #0#16;
          |PINTA #0#17;
     |FINSI;
     |SI #0#3 = "C";
          |C_M #0#4, 1, "N";
          |C_M #0#5, 1, "N";
          |C_M #0#8, 1, "N";
          |C_M #0#9, 1, "N";
          |C_M #0#10, 1, "N";

          |C_V #0#5, 1, "N";
          |C_V #0#8, 1, "N";
          |C_V #0#9, 1, "N";
          |C_V #0#10, 1, "N";
          |BLANCO 14111572;

          |C_M #0#14, 1, "N";
          |C_M #0#15, 1, "N";
          |C_M #0#16, 1, "N";
          |C_M #0#17, 1, "N";
          |C_V #0#14, 1, "N";
          |C_V #0#15, 1, "N";
          |C_V #0#16, 1, "N";
          |C_V #0#17, 1, "N";
          |BLANCO 17112272;

          |PINTA 1712, "Fecha Parte Conf:"
          |PINTA 1812, "Fecha Cambio Ent:"
          |PINTA 1744, "Nímero Parte..:"
          |PINTA 1844, "Nueva Entidad.:"

          |C_P #0#26, 1, 1730;
          |C_V #0#26, 1, "S";
          |C_M #0#26, 1, "S";
          |PINTA #0#26;
          |C_P #0#27, 1, 1760;
          |C_V #0#27, 1, "S";
          |C_M #0#27, 1, "S";
          |PINTA #0#27;
          |C_P #0#28, 1, 1830;
          |C_V #0#28, 1, "S";
          |C_M #0#28, 1, "S";
          |PINTA #0#28;
          |C_P #0#29, 1, 1860;
          |C_V #0#29, 1, "S";
          |C_M #0#29, 1, "S";
          |PINTA #0#29;
     |FINSI;
|FPRC;

|PROCESO Contingencia;
     |SI #0#3 = "B";
          |SI #0#4 = "3"; |O #0#4 = "4"; |O #0#4 = "5";
               |ATRI I;
               |PINTA 2112, "- Otras Cotizaciones A¤o Anterior -"
               |ATRI 0;
               |PINTA 2212, "Horas Extras ........:          Otros Conceptos ...:         ";
               |C_M #0#24, 1, "S";
               |C_V #0#24, 1, "S";
               |PINTA #0#24;
               |C_M #0#25, 1, "S";
               |C_V #0#25, 1, "S";
               |PINTA #0#25;

               |C_M #0#23, 1, "S";
               |C_V #0#23, 1, "S";
               |PINTA 1244, "Fecha AT/EP ..:";
               |PINTA #0#23;
          |SINO;
               |C_M #0#24, 1, "N";
               |C_V #0#24, 1, "N";
               |C_M #0#25, 1, "N";
               |C_V #0#25, 1, "N";
               |PINTA 2112, "                                     ";
               |PINTA 2212, "                                                             ";

               |C_M #0#23, 1, "N";
               |C_V #0#23, 1, "N";
               |PINTA 1244, "                           ";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO atep; |TIPO 7;
     |MENSA "     Indique la fecha de Alta";
     |SI #0#3 = "B";
          |SI #0#4 = "3"; |O #0#4 = "4"; |O #0#4 = "5";
               |MENSA "     Indique la fecha para AT/EP";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Restaura;
     |POPV;
|FPRC;

|PROCESO ayuda1; |TIPO 7;
     |PUSHV 14442380;
     |ATRI R;
     |PINTA 1444, "      -  Elija opción  -      ";
     |PINTA 1544, " 1. Enfermedad Común          ";
     |PINTA 1644, " 2. Accidente no Laboral      ";
     |PINTA 1744, " 3. Accidente de Trabajo      ";
     |PINTA 1844, " 4. Enfermedad Profesional    ";
     |PINTA 1944, " 5. Periodos de observación   ";
     |PINTA 2044, "    de enfermedad profesional ";
     |ATRI 0;
|FPRC;

|PROCESO ayuda2; |TIPO 7;
     |PUSHV 13122343;
     |ATRI R;
     |PINTA 1312, "      -  Elija opción  -      ";
     |PINTA 1412, " 01. Curación                 ";
     |PINTA 1512, " 02. Fallecimiento            ";
     |PINTA 1612, " 03. Inspección Médica        ";
     |PINTA 1712, " 04. Propuesta Incapacidad    ";
     |PINTA 1812, " 05. Agotamiento de Plazo     ";
     |PINTA 1912, " 06. Mejoría                  ";
     |PINTA 2012, " 07. Incomparecencia          ";
     |PINTA 2112, " 10. Control INSS duración 12m";
     |PINTA 2212, " 17. Recupera capacid. prof.  ";
     |PINTA 2312, " 18. Incomparecencia Formación";
     |ATRI 0;
|FPRC;

|PROCESO CompAlta;
     |SI #0#22 > 7;
          |SI #0#22 ! 17; |Y #0#22 ! 18; |Y #0#22 ! 10;
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO suma;
     BaseHE = 0;
     |PARA i = 1; |SI i [ 12; |HACIENDO i = i + 1;
          #nomhicca#0 = #0#0;
          #nomhicca#1 = #0#1;
          #nomhicca#66 = nume4;     || a¤o
          #nomhicca#2 = nume1;     || mes
          #nomhicca#3 = 0;         || tipo
          |LEE 000#nomhicca.=;
          |SI FSalida = 0;
               BaseHE = BaseHE + #nomhicca#41 + #nomhicca#42;
          |FINSI;
          |SI nume1 < 12;
               nume1 = nume1 + 1;
          |SINO;
               nume1 = 1;
               nume4 = nume4 + 1;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO LeeBases; |TIPO 7;
     |SI aParam = "MODIF";
          |ACABA;
     |FINSI;
     alfa1 = #0#7;            || fecha accidente
     alfa2 = alfa1 % 402;     || mes accidente
     alfa3 = alfa1 % 704;     || a¤o accidente
     nume2 = alfa2;
     nume3 = alfa3;
     nume2 = nume2 - 1;
     |SI nume2 = 0;           || si es enero, el mes anterior es Diciembre
          nume2 = 12;         || del a¤o anterior
          nume3 = nume3 - 1;
     |FINSI;
     nume3 = nume3 - 2000;
     |ABRE #nomhicca;
     #nomhicca#0 = #0#0;
     #nomhicca#1 = #0#1;
     #nomhicca#2 = nume2;
     #nomhicca#3 = 0;
     #nomhicca#66 = nume3;
     |LEE 000#nomhicca.=;
     |SI FSalida = 0;
          |SI #labtraba#42 ! "T"; |Y #labtraba#42 ! "X";
               #0#16 = #nomhicca#40;
               #0#14 = #nomhicca#8;
          |SINO;
               #0#17 = #nomhicca#40;
               #0#15 = #nomhicca#8;
          |FINSI;
     |FINSI;
     nume1 = nume2 + 1;  || mes desde (12 meses anteriores)
     nume4 = nume3 - 1;  || a¤o desde (12 meses anteriores)
     |SI nume1 = 13;
          nume1 = 1;
          nume4 = nume4 + 1;
     |FINSI;
     |HAZ suma;
     |SI #0#3 = "B";
          |SI #0#4 = "3"; |O #0#4 = "4"; |O #0#5 = "5";
               #0#24 = BaseHE;
          |FINSI;
     |SINO;
          #0#24 = 0;
     |FINSI;
     |SI #labtraba#42 ! "T"; |Y #labtraba#42 ! "X";
          |PINTA #0#14;
          |PINTA #0#16;
     |SINO;
          |PINTA #0#15;
          |PINTA #0#17;
     |FINSI;
     |PINTA #0#24;
     |CIERRA #nomhicca;
|FPRC;

|PROCESO BuscaDupli_F;
     |SI #silcon17#3 ! "C";
          |ACABA;
     |FINSI;

     |SI #0#26 = fFechaActual;     || debo permitir volver a poner la misma
          |ACABA;                  || fecha del parte que esoy editando
     |FINSI;

     |SELECT #5#silcon18;
     #silcon18#0 = #silcon17#0;
     #silcon18#1 = #silcon17#1;
     #silcon18#7 = #silcon17#7;
     #silcon18#3 = #silcon17#3;
     #silcon18#26 = #silcon17#26;
     |LEE 000#silcon18.=;
     |SI FSalida = 0;
          aAlfa1 = #silcon18#27;
          aAlfa = "    ATENCION: ya existe un parte de confirmacion en esta fecha:";
          aAlfa = aAlfa + " parte numero " + aAlfa1;
          |MENAV aAlfa;
          aAlfa1 = #silcon18#2; aAlfa1 = "000000" + aAlfa1; aAlfa1 = aAlfa1 % -106;
          |SI #silcon18#20 = "-";
               aAlfa2 = "PENDIENTES";
          |SINO;
               aAlfa2 = "ENVIADOS";
          |FINSI;
          aAlfa = "    Consulte los movimientos FDI " + aAlfa2 + ". ";
          aAlfa = aAlfa + "Linea " + aAlfa1;
          |MENAV aAlfa;
          |ERROR;
     |FINSI;

     |SELECT #1#silcon18;
|FPRC;

|PROCESO BuscaDupli_C;
     |SI #silcon17#3 ! "C";
          |ACABA;
     |FINSI;

     |SI #0#27 = nParteActual;     || debo permitir poner el mismo numero de
          |ACABA;                  || parte que el de el que estoy editando
     |FINSI;

     |SELECT #4#silcon18;
     #silcon18#0 = #silcon17#0;
     #silcon18#1 = #silcon17#1;
     #silcon18#7 = #silcon17#7;
     #silcon18#3 = #silcon17#3;
     #silcon18#27 = #silcon17#27;
     |LEE 000#silcon18.=;
     |SI FSalida = 0;
          aAlfa = #silcon18#27;
          aAlfa = "    ATENCION: el parte de confirmacion numero " + aAlfa;
          aAlfa = aAlfa + " ya esta creado para esta IT";
          |MENAV aAlfa;
          aAlfa1 = #silcon18#2; aAlfa1 = "000000" + aAlfa1; aAlfa1 = aAlfa1 % -106;
          |SI #silcon18#20 = "-";
               aAlfa2 = "PENDIENTES";
          |SINO;
               aAlfa2 = "ENVIADOS";
          |FINSI;
          aAlfa = "    Consulte los movimientos FDI " + aAlfa2 + ". ";
          aAlfa = aAlfa + "Linea " + aAlfa1;
          |MENAV aAlfa;
          |ERROR;
     |FINSI;

     |SELECT #1#silcon18;
|FPRC;

|PROCESO BuscaUltimo_C;
     |SI aParam = "MODIF"; |O #silcon17#3 ! "C";
          |ACABA;
     |FINSI;
     |SELECT #4#silcon18;
     FSalida = 0;
     |PARA nParte = 1; |SI FSalida = 0; |HACIENDO nParte = nParte + 1;
          #silcon18#0 = #silcon17#0;
          #silcon18#1 = #silcon17#1;
          #silcon18#7 = #silcon17#7;
          #silcon18#3 = #silcon17#3;
          #silcon18#27 = nParte;
          |LEE 000#silcon18.=;
     |SIGUE;
     #0#27 = nParte - 1;
     |PINTA #0#27;

     |SELECT #1#silcon18;
|FPRC;

|PROCESO BuscaDupli;
     |SI aParam = "MODIF"; |O #silcon17#3 = "C";
          |ACABA;
     |FINSI;
     |SELECT #2#silcon18;
     #silcon18#0 = #silcon17#0;
     #silcon18#1 = #silcon17#1;
     #silcon18#7 = #silcon17#7;
     #silcon18#3 = #silcon17#3;
     |LEE 000#silcon18.=
     |SI FSalida = 0;
          aAlfa = "    ATENCION: ya se ha creado un parte en esta fecha ";
          aAlfa = aAlfa + "para la accion '" + #silcon17#3 + "'.";
          |MENAV aAlfa;
          aAlfa1 = #silcon18#2; aAlfa1 = "000000" + aAlfa1; aAlfa1 = aAlfa1 % -106;
          |SI #silcon18#20 = "-";
               aAlfa2 = "PENDIENTES";
          |SINO;
               aAlfa2 = "ENVIADOS";
          |FINSI;
          aAlfa = "    Consulte los movimientos FDI " + aAlfa2 + ". ";
          aAlfa = aAlfa + "Linea " + aAlfa1;
          |MENAV aAlfa;
          |ERROR;
     |FINSI;
     |SELECT #1#silcon18;
|FPRC;

|PROCESO BuscaBaja;
     |SI aParam = "MODIF"; |O #0#3 ! "C";
          |ACABA;
     |FINSI;
     |SELECT #2#silcon18;
     #silcon18#0 = #silcon17#0;
     #silcon18#1 = #silcon17#1;
     #silcon18#7 = #silcon17#7;
     #silcon18#3 = "B";
     |LEE 000#silcon18.=
     |SI FSalida ! 0;
          aAlfa = "    ATENCION: no se ha encontrado el parte de baja asociado ";
          aAlfa = aAlfa + "en esta fecha. Por favor, seleccione uno:";
          |MENAV aAlfa;
          FSalida = 13;
          |HAZ Consulta;
          |POSICIONATE #0#7;
     |SINO;
          #0#4 = #silcon18#4;
          #0#11 = #silcon18#11;
          #0#12 = #silcon18#12;
          #0#13 = #silcon18#13;
          |PINTA #0#4;
          |PINTA #0#11;
          |PINTA #0#12;
          |PINTA #0#13;
          ||POSICIONATE #0#26;
     |FINSI;
     |SELECT #1#silcon18;
|FPRC;

|PROCESO Min;
     #silcon18#0 = #0#0;
     #silcon18#1 = #0#1;
     #silcon18#7 = "01.01.1999";
     #silcon18#3 = "B";
|FPRC;

|PROCESO Max;
     #silcon18#0 = #0#0;
     #silcon18#1 = #0#1;
     #silcon18#7 = "31.12.2999";
     #silcon18#3 = "B";
|FPRC;

|PROCESO Consulta;
     |SI FSalida = 13;
          |ABRE #silcon18;
          |CONSULTA_F_CLAVE #2#silcon18, Min, Max;
          |SI FSalida = 0;
               #0#7 = #silcon18#7;
               |PINTA #0Campo;
          |SINO;
               |ERROR;
          |FINSI;
          |CIERRA #silcon18;
     |FINSI;
|FPRC;

/******************** FIN PROCESOS DESDE LA PANTALLA ************************/


|PROCESO contador;
     |SELECT #3#silcon18;
     |LEE 000#silcon18.u;
     |SI FSalida = 0;
          nContador = #silcon18#2;
          nContador = nContador + 1;
     |SINO;
          nContador = 1;
     |FINSI;
|FPRC;

|PROCESO FechaAltaoATEP;
     #silcon18#23 = 0;
     |SI #silcon17#3 = "A";
          #silcon18#23 = #silcon17#23;
     |FINSI;
     |SI #silcon17#3 = "B";
          |SI #silcon17#4 = "3"; |O #silcon17#4 = "4"; |O #silcon17#4 = "5";
               #silcon18#23 = #silcon17#23;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CargaModif;
     |SELECT #2#silcon18;
     #silcon18#0 = nEmpExt;
     #silcon18#1 = nTraExt;
     #silcon18#7 = fFecExt;
     #silcon18#3 = aAccExt;
     |LEE 000#silcon18.=;
     |PARA nCampo = 3; |SI nCampo [ 17; |HACIENDO nCampo = nCampo + 1;
          #silcon17#nCampo# = #silcon18#nCampo#;
     |SIGUE;
     #silcon17#0 = #silcon18#0;
     #silcon17#1 = #silcon18#1;
     |PARA nCampo = 22; |SI nCampo [ 29; |HACIENDO nCampo = nCampo + 1;
          #silcon17#nCampo# = #silcon18#nCampo#;
     |SIGUE;
|FPRC;

|PROCESO GrabaNuevo;
     |HAZ contador;
     |DDEFECTO #silcon18;
     |SELECT #4#silcon18;
     #silcon18#0 = #silcon17#0;
     #silcon18#1 = #silcon17#1;
     #silcon18#7 = #silcon17#7;
     #silcon18#3 = #silcon17#3;
     #silcon18#27 = #silcon17#27;
     |LEE 000#silcon18.=;
     |SI FSalida = 0;
          aAlfa = "    ATENCION: Parte Duplicado";
          |MENAV aAlfa;
          |ACABA;
     |SINO;
          |SELECT #1#silcon18;
          |PARA nCampo = 3; |SI nCampo [ 17; |HACIENDO nCampo = nCampo + 1;
               #silcon18#nCampo# = #silcon17#nCampo#;
          |SIGUE;
          #silcon18#2 = nContador;
          #silcon18#6 = ~;
          #silcon18#18 = "";
          #silcon18#19 = "";
          #silcon18#20 = "-";
          #silcon18#21 = " ";
          |PARA nCampo = 22; |SI nCampo [ 29; |HACIENDO nCampo = nCampo + 1;
               #silcon18#nCampo# = #silcon17#nCampo#;
          |SIGUE;
          |HAZ FechaAltaoATEP;
          |GRABA 020#silcon18.n;
          |LIBERA #silcon18;
     |FINSI;
|FPRC;

|PROCESO CambioA;
     aAlfa = Contenido;
     |SI aAlfa ! #0Campo;
          aAlfa = Campo; aAlfa = "000" + aAlfa; aAlfa = aAlfa % -103;
          aAlfa = "|C" + aAlfa + "NOMBRE"; aAlfa = #silcon17#aAlfa#;
          |QBF aAlfa;
          aCambios = aCambios + ", " + aAlfa;
     |FINSI;
|FPRC;

|PROCESO CambioN;
     nNume = Contenido;
     |SI nNume ! #0Campo;
          aAlfa = Campo; aAlfa = "000" + aAlfa; aAlfa = aAlfa % -103;
          aAlfa = "|C" + aAlfa + "NOMBRE"; aAlfa = #silcon17#aAlfa#;
          |QBF aAlfa;
          aCambios = aCambios + ", " + aAlfa;
     |FINSI;
|FPRC;

|PROCESO mensajes;
     |SI nMensaje = 1;
          aAlfa = "    ATENCION. Ya se ha enviado un parte de accidente por el ";
          aAlfa = aAlfa + "sistema Delt@.";
          |MENAV aAlfa;
          aAlfa ="    Debera dejar el parte como PENDIENTE para poder modificarlo"
          aAlfa1 = #labparma#0; aAlfa1 = "000000" + aAlfa1; aAlfa1 = aAlfa1 % -106;
          aAlfa = "Consulte movimientos Delta PENDIENTES: linea " + aAlfa1;
          |MENAV aAlfa;
     |FINSI;
     |SI nMensaje = 2;
          aCambios = aCambios - ", ";
          aAlfa = "    Campos comunes modificados: ";
          aAlfa = aAlfa + aCambios;
          |MENAV aAlfa;
     |FINSI;
     |SI nMensaje = 3;
          aAlfa = "    ATENCION: Parte no encontrado";
          |MENAV aAlfa;
     |FINSI;
     |SI nMensaje = 4;
          aAlfa = "    Modificado el parte de accidente asociado en Delt@. ";
          aAlfa1 = #labparma#0; aAlfa1 = "000000" + aAlfa1; aAlfa1 = aAlfa1 % -106;
          aAlfa = aAlfa + "Movimientos Delt@ PENDIENTES: linea " + aAlfa1;
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO comprobaciones;
     swNoGrabes = 0;
     |ABRE #labparma;
     |ABRE #labparhi;
     #labparma#1 = #silcon17#0;    || empresa
     #labparma#2 = #silcon17#1;    || trabajador
     #labparma#3 = #silcon17#7;    || fecha de baja
     |LEE 000#labparma.=;
     |SI FSalida = 0;              || el parte de accidente existe
          |SI #labparma#6 = "*";    || y encima esta ya emitido
               |QBF aCambios;
               |SI aCambios ! "";
                    nMensaje = 1;
                    |HAZ mensajes;
                    nMensaje = 2;
                    |HAZ mensajes;
                    swNoGrabes = 1;
               |FINSI;
          |SINO;
               |SI aCambios ! "";
                    #labparhi#1 = #silcon17#0;    || empresa
                    #labparhi#2 = #silcon17#1;    || trabajador
                    #labparhi#15 = #silcon17#7;    || fecha de baja
                    |LEE 101#labparhi.=;
                    |SI FSalida = 0;
                         |SI #silcon17#5 = "S";        || recaida S/N
                              #labparhi#40 = "R";
                         |SINO;
                              #labparhi#40 = "A";
                         |FINSI;
                         #labparhi#37 = #silcon17#14;  || dias cotizados mes
                         #labparhi#36 = #silcon17#16;  || base cotizacion mes anterior
                         #labparhi#38 = #silcon17#24;  || base cotizacion horas extras
                         #labparhi#39 = #silcon17#25;  || base cotizacion otros
                         |GRABA 020#labparhi.a;
                         nMensaje = 4;
                         |HAZ mensajes;
                         nMensaje = 2;
                         |HAZ mensajes;
                    |SINO;
                         nMensaje = 3;
                         |HAZ mensajes;
                         nMensaje = 2;
                         |HAZ mensajes;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;

     |CIERRA #labparma;
     |CIERRA #labparhi;
|FPRC;

|PROCESO GrabaModif;
     |HAZ comprobaciones;
     |SI swNoGrabes = 0;
          |PARA nCampo = 3; |SI nCampo [ 17; |HACIENDO nCampo = nCampo + 1;
               #silcon18#nCampo# = #silcon17#nCampo#;
          |SIGUE;
          |PARA nCampo = 22; |SI nCampo [ 29; |HACIENDO nCampo = nCampo + 1;
               #silcon18#nCampo# = #silcon17#nCampo#;
          |SIGUE;
          |HAZ FechaAltaoATEP;
          |GRABA 020#silcon18.a;
          |LIBERA #silcon18;
     |FINSI;
|FPRC;

|PROGRAMA;
     aParam = PARAMETRO;
     |QBF aParam;
     |CLS;
     |PINPA #0#0;
     |ABRE #silcon18;
     |SI aParam = "MODIF";
          |HAZ CargaModif;
          fFechaActual = #0#26;
          nParteActual = #0#27;
          |PINDA #0#0;
          |CAMPO_MODIFICA #0#0, 1, "N";
          |CAMPO_MODIFICA #0#1, 1, "N";
          |CAMPO_MODIFICA #0#7, 1, "N";
          |CAMPO_MODIFICA #0#3, 1, "N";
          |ENDATOS #1#0;
          |SI FSalida = 0;
               |HAZ GrabaModif;
          |FINSI;
     |SINO;
          |PINDA #0#0;
          |ENDATOS #1#0;
          |SI FSalida = 0;
               |HAZ GrabaNuevo;
          |FINSI;
     |FINSI;
     |SI FSalida = 0;
     |FINSI;
     |CIERRA #silcon18;
|FPRO;
