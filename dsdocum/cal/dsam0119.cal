|FICHEROS;
     dsam0119 #119;
     dsam0120 #120,MANTE;
|FIN;

|VARIABLES;
     aIPRemoto = "";
     aNombreIP = "";
     aRuta     = "";
     aAlfa     = "";
     nModo     = 0;
|FIN;

|PROCESO CuadroTDocumento;  |TIPO 7;
     |PUSHV 0501 2380;

     |CUADRO 0628 1263;
     |BLANCO 0729 1162;

     |PINTA 0732, "-1- Documento en Word         ";
     |PINTA 0832, "-2- Documento en Excel        ";
     |PINTA 0932, "-3- Documento en Texto MS-DOS ";
     |PINTA 1032, "-4- Documento de Imagen       ";
     |PINTA 1132, "-5- Otros Documentos          ";
|FPRC;

|PROCESO TipoDocumento;  |TIPO 0;
     |POPV;

     |PINTA #119#2;

     |C_M #119#3, 1, "S";
     |SI #119#2 = 1;   #119#3 = "DOC";   |C_M #119#3, 1, "N";   |FINSI;
     |SI #119#2 = 2;   #119#3 = "XLS";   |C_M #119#3, 1, "N";   |FINSI;
     |SI #119#2 = 3;   #119#3 = "TXT";   |C_M #119#3, 1, "N";   |FINSI;
     |SI #119#2 = 4;   #119#3 = "TIF";                          |FINSI;

     |PINTA #119#3;
|FPRC;

|RUTINA ClaveBaja120;
     #120#0 = #119#0;
     #120#1 = "                    ";
|FRUT;

|RUTINA ClaveAlta120;
     #120#0 = #119#0;
     #120#1 = "zzzzzzzzzzzzzzzzzzzz";
|FRUT;

|PROCESO Baja120;
     |BORRA 020#120a;
     |LIBERA #120;
|FPRC;

|DEFBUCLE dsam0120;
     #120#1;
     ;
     #119#0, "                    ";
     #119#0, "zzzzzzzzzzzzzzzzzzzz";
     be;
     NULL, Baja120;
|FIN;

|PROCESO Tipo2m119;  |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo ! 3;  |ACABA;  |FINSI;

     |BUCLE dsam0120;
|FPRC;

|PROCESO Tipo12;  |TIPO 12;
     |ENTLINEAL #1#120, -2, 2, 22, 0, ClaveBaja120, ClaveAlta120;
|FPRC;

|PROCESO Tipo5m120;  |TIPO 5;
     |IP_REMOTO aIPRemoto;   |QBT aIPRemoto;
     |SI aIPRemoto = "";
         aIPRemoto = "LOCAL";
         aNombreIP = "MAQUINA LOCAL";
     |SINO;
         |NOMBRE_IP aIPRemoto, aNombreIP;
     |FINSI;

     aIPRemoto = (aIPRemoto + (" " * 20)) % 120;

     #120#0 = #119#0;
     #120#1 = aIPRemoto;
     |LEE 101#120=;
     |SI FSalida ! 0;
         |DDEFECTO #120;
         #120#0 = #119#0;
         #120#1 = aIPRemoto;
         #120#2 = aNombreIP;
         |SI #119#2 = 1;  #120#3 = "LLAMADA INTERNA DESDE PROGRAMA";  |FINSI;
         |GRABA 020#120n;
     |FINSI;
|FPRC;

|PROCESO DejoEntrar;
     |SI #120#1 ! aIPRemoto;
         |MENAV "      El Nombre de la Maquina no corresponde con su IP.";
         |ERROR;
         |ACABA;
     |FINSI;

     |C_M #120#3, 1, "S";
     |SI #119#2 = 1;
         |C_M #120#3, 1, "N";
     |FINSI;
|FPRC;

|PROCESO Ruta;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     aRuta = "F" + #120Campo;
     |BROWSE aRuta;
     #120Campo = aRuta % 280;
     |PINTA #120Campo;
     |ERROR;
|FPRC;

|PROCESO PintaLineal;  |TIPO 14;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo ! 4;  nModo = 7;  |FINSI;

     aAlfa = #119#3;
     |PINTA 1025, aAlfa;

     |ENTLINEAL #1#120, -2, nModo, 22, 0, ClaveBaja120, ClaveAlta120;
|FPRC;
