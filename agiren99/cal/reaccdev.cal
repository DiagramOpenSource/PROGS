|FICHEROS;
     reaccdev #0;
     reparame #1;
     rem00100 #2;

     agifigen #5;
     agicontr #98;
|FIN;

|VARIABLES;
     &enEmpresa   = 0;
     &eaNombre    = "";
     &eaNif       = "";
     &enModo      = 0;
     &eaVarDni    = "";
     &enSwHojas   = 0;

     agicli    = "";
     agi       = "";
     agi1      = "";
     agi2      = "";
     aste      = "";
     longi     = "";
     num       = 0;

     {-1}mGeneral = "";
     mGeneral1    = "1425";
     mGeneral2    = "1";
     mGeneral3    = "6";
     mGeneral4    = "Datos Personales (Modelo 104)  ";
     mGeneral5    = "Importes Rentas  (Opcional)    ";
     mGeneral6    = "Modelo 104                     ";
     mGeneral7    = "Modelo 105                     ";
     mGeneral8    = "Emision Modelos Hojas Sueltas  ";
     mGeneral9    = "Emision Modelos Hojas Continuo ";

     {-1}mDRenta  = "";
     mDRenta1     = "1850";
     mDRenta2     = "1";
     mDRenta3     = "2";
     mDRenta4     = "Importes Rentas                ";
     mDRenta5     = "Conclusiones                   ";
|FIN;

|INCLUYE i_sepnom;

|PROCESO Tipo8; |TIPO 8;
     |HAZ QueVersion;

     |DBASS agi;
     |PATH_DAT #98 agi;

     |ABRE #98;
     #98#0 = 1;
     |LEE 040#98=;
     |SI FSalida ! 0; aste = "*"; |CIERRA #98; |ACABA; |FINSI;
     |CIERRA #98;

     aste = #98#6;
     |QBF aste;
     |SI aste  = "*"; |ACABA; |FINSI;

     agi1 = #98#8; |QBF agi1;

     longi = agi1 % A0;
     num   = longi;
     num   = 100 + (num - 5);
     longi = num;

     agi2  = (agi1 % longi) + "pan/";

     |PATH_DAT #5 agi1;
     |PATH_PAN #5 agi2;
|FPRC;

|PROCESO Separa;
     Cadena = #2#5;
     |SI Cadena ! "";
         |HAZ SeparaNombre;
         #2#2 = Cadena1;
         #2#3 = Cadena2;
         #2#4 = Cadena3;
     |FINSI;
|FPRC;

|PROCESO CogeCliente;
     #2#0  = #0#0;
     |LEE 101#2=;
     |SI FSalida = 0;  |LIBERA #2;  |ACABA;  |FINSI;

     |DDEFECTO #2;
     #2#0  = #0#0;
     #2#5  = #5#2;  |HAZ Separa;
     #2#1  = #5#13;
     #2#14 = #5#3;
     #2#15 = #5#4;
     #2#16 = #5#5;
     #2#17 = #5#6;
     #2#18 = #5#7;
     #2#19 = #5#8;
     #2#20 = #5#9;
     #2#21 = #5#10;
     #2#22 = #5#11;
     #2#23 =  (("00" + #2#20) % -102) +  (("000" + #2#21) % -103);
     #2#12 = #5#14;
     #2#69 = #5#63;
     #2#70 = #5#64;
     #2#24 = #5#51;
     #2#25 = #5#52;
     #2#26 = #5#53;
     #2#27 = #5#54;
     #2#28 = #5#55;
     #2#29 = #5#56;
     #2#30 = #5#57;
     #2#31 = #5#58;
     #2#32 = #5#59;
     #2#33 =  (("00" + #2#31) % -102) +  (("000" + #2#32) % -103);

     |GRABA 020#2n;
     |LIBERA #2;
|FPRC;

|PROCESO MiraAgifigen;


     |ABRE #5;
     |ABRE #2;
     #5#0 = #0#0;
     |LIBERA #5;
     |LEE 110#5=;
     |SI FSalida = 0;                || Recoge variables del agicli
         #5#22 = "S";
         |GRABA 020#5a;                 || Graba el agicli

         |HAZ CogeCliente;

         #0#1  = #5#2;     |PINTA #0#1;
         #0#2  = #5#13;    |PINTA #0#2;
    |SINO;
         |CONFI "2400S Confirma el alta en Clientes General : ";
         |SI FSalida = 0;
             |PUSHV 0501 2380;
             |PINPA #0#5;
             |DDEFECTO #5;
             |PINDA #0#5;
             #5#0 = #0#0; |PINTA #5#0;
             |ENDATOS #1#5;
             |SI FSalida = 0;
                 #5#22 = "S";
                 |GRABA 121#5n;                 || Graba el agicli

                 |HAZ CogeCliente;

                 #0#1  = #5#2;     |PINTA #0#1;
                 #0#2  = #5#13;    |PINTA #0#2;
             |FINSI;
             |POPV;
        |FINSI;
     |FINSI;

     |CIERRA #2;
     |CIERRA #5;
|FPRC;

|PROCESO Altacli;  |TIPO 0;
     enModo = (FEntrada / 100) ! 0;
     |SI enModo ! 1;  |ACABA;  |FINSI;

     |SI aste  = "";  |HAZ Tipo8; |FINSI;
     |SI aste  = "*"; |ACABA;      |FINSI;

     |HAZ MiraAgifigen;
|FPRC;

|PROCESO Bajacli;
     |SI aste  = "";  |HAZ Tipo8; |FINSI;
     |SI aste  = "*"; |ACABA;     |FINSI;

     |ABRE #5;
     #5#0 = #0#0;
     |LIBERA #5;
     |LEE 110#5=;
     |SI FSalida = 0;                || Recoge variables del agicli
         #5#22 = "N";
         |GRABA 020#5a;                 || Graba el agicli
     |FINSI;
     |CIERRA #5;
|FPRC;

|PROCESO Modicli;
     |SI aste  = "*"; |ACABA; |FINSI;
     |SI aste  = "";  |HAZ Tipo8; |FINSI;

     |ABRE #5;
     #5#0 = #0#0;
     |LEE 040#5=;
     |SI FSalida = 0;                || Recoge variables del agicli
         |SI #0#2 ! #5#13;
            |MENAV "     D.N.I. del Cliente es distinto al D.N.I. del Cliente General";
         |FINSI;
     |SINO;
         |MENAV "      No existe Cliente General";
     |FINSI;
     |CIERRA #5;
|FPRC;

|PROCESO mDRenta;
     |PUSHV 0501 2380;
     |PARA; |SI; |HACIENDO;
            |MENUG mDRenta;
            mDRenta2 = FSalida;
            |SI mDRenta2 < "1";  |O mDRenta2 > "2";  |SAL;  |FINSI;

            |SI mDRenta2 = "1";   |HAZ ImportesRentas;  |FINSI;
            |SI mDRenta2 = "2";   |HAZ Conclusiones;    |FINSI;
     |SIGUE;
     |POPV;
|FPRC;

|PROCESO Pantalla;
     enEmpresa    = #0#0;
     eaNombre     = #0#1;
     eaNif        = #0#2;
     |PARA; |SI; |HACIENDO;
            |MENUG mGeneral;
            mGeneral2 = FSalida;
            |SI mGeneral2 < "1";  |O mGeneral2 > "6";  |SAL;  |FINSI;
            |SI mGeneral2 = "1";
                |SUB_EJECUTA "rem00100";
                #0#1 = eaNombre;  |PINTA #0#1;
            |FINSI;
            |SI mGeneral2 = "2";  |HAZ mDRenta;      |FINSI;
            |SI mGeneral2 = "3";  |HAZ Modelo104;    |FINSI;
            |SI mGeneral2 = "4";  |HAZ Modelo105;    |FINSI;
            |SI mGeneral2 = "5"; enSwHojas = 0; |HAZ EmiteModelos; |FINSI;
            |SI mGeneral2 = "6"; enSwHojas = 1; |HAZ EmiteModelos; |FINSI;
     |SIGUE;
     |PINPA #0#0;
|FPRC;

|PROCESO LetraNif;  |TIPO 6;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     eaVarDni = #0Campo;
     |HAZ CalculoDNI;
     #0Campo = eaVarDni;
     |PINTA #0Campo;
     |ERROR;
|FPRC;

|PROCESO Contador; |TIPO 7;
     enModo = (FEntrada / 100) ! 0;
     |SI enModo ! 1;  |ACABA;  |FINSI;

     |ABRE #1;
     |LEE 101#1p;
     |SI FSalida ! 0; |DDEFECTO #1; #1#9 = 1; |GRABA 020#1b; |FINSI;
     |LIBERA #1;
     |CIERRA #1;

     |SI #1#8 = "A"; #0#0 = #1#9; |PINTA #0#0; |FINSI;
|FPRC;

|PROCESO Acceso;  |TIPO 3;
     enModo = (FEntrada / 100) ! 0;

     |SI enModo = 2; |HAZ Modicli; |FINSI;
     |SI enModo = 1; |O enModo = 2;
         |HAZ Pantalla;
     |FINSI;

     |SI enModo = 1;
         |ABRE #1;
         |LEE 101#1p;
         |SI #1#8 = "A";
             #1#9 = #0#0 + 1;
             |GRABA 020#1a;
         |FINSI;
         |LIBERA #1;
         |CIERRA #1;
     |FINSI;
|FPRC;

|PROCESO Consulta; |TIPO 13;
     |SI #0#0 = 0;  |ACABA;  |FINSI;

     enModo = (FEntrada / 100) ! 0;
     |SI enModo ! 4;  |ACABA;  |FINSI;
     |PINTA #0#0;
     |PINTA #0#1;
     |PINTA #0#2;
     |HAZ Pantalla;
|FPRC;

|PROCESO Borrado; |TIPO 2;
     enEmpresa = #0#0;
     eaNombre  = #0#1;
     enModo = (FEntrada / 100) ! 0;
     |SI enModo ! 3;  |ACABA;  |FINSI;

     |PINTA 2430,"Borrando Registros ....";

     |SUB_EJECUTA "reborra1";

     |PINTA 2430,"                       ";
|FPRC;
