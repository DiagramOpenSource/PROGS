|FICHEROS;
     reinfplz #0;

     reacceso #1;
     rem00100 #2;
     rem00101 #3;
     rem01001 #4;
     repoblac #5;
|FIN;

|VARIABLES;
     nParrilla = 0;
     nLinea    = 0;

     aFichero     = "";

     aAlfa      = "";
     aAlfa1     = "";
     aAlfa2     = "";
     aOrigen    = "";
     aDestino   = "";
     aWord      = "";
     aAbre      = "";
     aAsigna    = "";
     aIdenti    = "";
     aPath01    = "";
     aIPRemoto  = "";
     aDef       = "";
     aDModelo   = "";
     aHModelo   = "";
     aCaracter  = "";
     aCadena    = "";
     aLen       = "";
     aTipo      = "";
     aDescri    = "";
     aPathDoc   = "";
     aDecima    = "";
     aEntero    = "";
     aLong      = "";
     aImpor1    = "";
     aImpor2    = "";
     aImpor3    = "";
     aImpor4    = "";

     nSwDialogo = 0;
     nWord      = 0;
     nContador  = 0;
     nCampo     = 0;
     nCampoDes  = 0;
     nCampoDes2 = 0;
     nHandle    = 0;
     nNume      = 0;
     nLen       = 0;
     nPos       = 0;
     nImporte   = 0;
     nLong      = 0;
     nDecimal   = 0;
     nCaracter  = 0;

     &enSwDeDonde     = 0;
     &enCodProv       = 0;
     &enCodPobl       = 0;
     &eaRutaDocumento = "";
     &enFSalida       = 0;
     &enCodUbica      = 0;
     &eaNomUbicacion  = "";
     &eaImporte       = "";
     &eaUnidadBasico  = "";
|FIN;

|PROCESO Importe;    || Calculo para poner las puntos y comas
                     || en el importe

     aDecima   = "";
     aEntero   = "";
     aLong     = eaImporte % 0;
     nLong     = aLong;
     nDecimal  = 0;
     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPos      = (100 * nCaracter) + 1;
           aCaracter = eaImporte % nPos;
           |SI aCaracter = ".";
               nDecimal = 1;
           |SINO;
               |SI nDecimal = 0;
                   aEntero = aEntero + aCaracter;
               |SINO;
                   aDecima = aDecima + aCaracter;
               |FINSI;
           |FINSI;
     |SIGUE;
     aDecima = (aDecima + "00") % 102;
     eaImporte = aEntero;

     aLong = eaImporte % 0;
     nLong = aLong;
     eaImporte = "             " + eaImporte;
     eaImporte = eaImporte % -113;
     aImpor1   = eaImporte % -103;
     aImpor2   = eaImporte % -403;
     aImpor3   = eaImporte % -703;
     aImpor4   = eaImporte % -1004;
     |SI nLong ] 10;
         |SI aImpor4 ! "  -";
             eaImporte = aImpor4 + "." + aImpor3  + "." + aImpor2 + "." + aImpor1;
         |SINO;
             eaImporte = aImpor4 + aImpor3  + "." + aImpor2 + "." + aImpor1;
      |FINSI;
    |SINO;
        |SI nLong ] 7;
            |SI aImpor3 ! "  -";
                eaImporte = aImpor3  + "." + aImpor2 + "." + aImpor1;
            |SINO;
                eaImporte = aImpor3 + aImpor2 + "." + aImpor1;
            |FINSI;
        |SINO;
            |SI nLong ] 4;
                |SI aImpor2 ! "  -";
                    eaImporte = aImpor2 + "." + aImpor1;
                |SINO;
                    eaImporte = aImpor2 + aImpor1;
                |FINSI;
            |SINO;
                eaImporte = aImpor1;
            |FINSI;
        |FINSI;
     |FINSI;

     eaImporte = ("             " + eaImporte + "," + aDecima) % -113;

     |SI eaImporte = "            0";  eaImporte = "             ";  |FINSI;
     |SI eaImporte = "         0,00";  eaImporte = "             ";  |FINSI;
|FPRC;

|PROCESO Seleccion;
   |SI #0#1 = "S";
      |C_M #0#2,1,"S";
      |C_M #0#3,1,"S";
      |PARA nParrilla = 4; |SI nParrilla [ 24; |HACIENDO nParrilla = nParrilla + 1;
         |C_M #0nParrilla, 1, "N";
      |SIGUE;
   |SINO;
      |C_M #0#2,1,"N";
      |C_M #0#3,1,"N";
      |PARA nParrilla = 4; |SI nParrilla [ 24; |HACIENDO nParrilla = nParrilla + 1;
         |C_M #0nParrilla, 1, "S";
      |SIGUE;
   |FINSI;
|FPRC;

|PROCESO Asigna;
     aCadena = "";
     aLen    = aAsigna % 0;
     nLen    = aLen;
     |PARA nCampo = 1;  |SI nCampo [ nLen;  |HACIENDO nCampo = nCampo + 1;
           nPos = (100 * nCampo) + 1;
           aCaracter = aAsigna % nPos;
           |SI aCaracter = "¥";  aCaracter = &209; |FINSI;
           |SI aCaracter = "¤";  aCaracter = &241; |FINSI;
           |SI aCaracter = " ";  aCaracter = &225; |FINSI;
           |SI aCaracter = "‚";  aCaracter = &233; |FINSI;
           |SI aCaracter = "¡";  aCaracter = &237; |FINSI;
           |SI aCaracter = "¢";  aCaracter = &243; |FINSI;
           |SI aCaracter = "£";  aCaracter = &250; |FINSI;
           |SI aCaracter = "§";  aCaracter = &176; |FINSI;
           |SI aCaracter = "¦";  aCaracter = &170; |FINSI;
           aCadena = aCadena + aCaracter;
     |SIGUE;
     aAsigna = aCadena;

     |PARA;  |SI;  |HACIENDO;
          |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;
          |SI FSalida ! 0;  |SAL;  |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Mes;
   |SI aAlfa1 = "01";  aAlfa = "Enero";      |FINSI;
   |SI aAlfa1 = "02";  aAlfa = "Febrero";    |FINSI;
   |SI aAlfa1 = "03";  aAlfa = "Marzo";      |FINSI;
   |SI aAlfa1 = "04";  aAlfa = "Abril";      |FINSI;
   |SI aAlfa1 = "05";  aAlfa = "Mayo";       |FINSI;
   |SI aAlfa1 = "06";  aAlfa = "Junio";      |FINSI;
   |SI aAlfa1 = "07";  aAlfa = "Julio";      |FINSI;
   |SI aAlfa1 = "08";  aAlfa = "Agosto";     |FINSI;
   |SI aAlfa1 = "09";  aAlfa = "Septiembre"; |FINSI;
   |SI aAlfa1 = "10"; aAlfa = "Octubre";     |FINSI;
   |SI aAlfa1 = "11"; aAlfa = "Noviembre";   |FINSI;
   |SI aAlfa1 = "12"; aAlfa = "Diciembre";   |FINSI;
|FPRC;

|PROCESO AbreWord;
     aAlfa = eaUnidadBasico + "/MODELOS";                   |RMKDIR aAlfa;
     aAlfa = eaUnidadBasico + "/MODELOS/DOCUMENTOS";        |RMKDIR aAlfa;

     |SI #0#26 = "D";
         aOrigen   = aPathDoc + "w2plazo1.doc";
         aDestino  = eaUnidadBasico + "/MODELOS/DOCUMENTOS/word0033.doc";
         aDescri   = "CARTA 2§ PAGO DOMICILIADOS RENTA 2012";
     |SINO;
         aOrigen   = aPathDoc + "w2plazo2.doc";
         aDestino  = eaUnidadBasico + "/MODELOS/DOCUMENTOS/word0034.doc";
         aDescri   = "CARTA 2§ PAGO NO DOMICILIADOS RENTA 2012";
     |FINSI;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |SI aIPRemoto ! "";
         |ENVIA_FICHERO aOrigen, aDestino;
     |SINO;
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;

     nWord  = 1;
     aAbre  = aDestino;

     |MENSA "      Abriendo el Documento, espere un Momento.";

     |WORD_ABRE nWord, aAbre;
     |WORD_PROPIEDADES nWord, "Visible", "NO";
|FPRC;

|PROCESO CargaFamiliar;
     aAlfa     = #4#2 % 101;
     nNume     = aAlfa;
     nContador = nContador + 1;
     |SI nContador = nNume;
         aAsigna = #3#3;  |QBF aAsigna;
         aIdenti = "!#1#";
         |HAZ Asigna;
     |FINSI;
|FPRC;

|DEFBUCLE rem00101;
     #3#1;
     13;
     #1#0, 01, "S";
     #1#0, 99, "S";
     ;
     NULL, CargaFamiliar;
|FIN;

|PROCESO CargaPersonal;
     |SI #4#2 = "CONJUNTA  ";  |O #4#2 = "CONJUNTAE ";  |O #4#2 = "TITULAR   ";
         aAsigna = #2#5;
         aIdenti = "!#1#";
         |HAZ Asigna;
     |FINSI;

     |SI #4#2 = "CONYUGE   ";
         aAsigna = #2#39;
         aIdenti = "!#1#";
         |HAZ Asigna;
     |FINSI;

     nContador = 0;
     |SI #4#2 = "1 FAMILIAR";  |BUCLE rem00101; |FINSI;
     |SI #4#2 = "2 FAMILIAR";  |BUCLE rem00101; |FINSI;
     |SI #4#2 = "3 FAMILIAR";  |BUCLE rem00101; |FINSI;
     |SI #4#2 = "4 FAMILIAR";  |BUCLE rem00101; |FINSI;

     aAsigna = #2#24;  |QBF aAsigna;
     aAsigna = aAsigna + " " + #2#25;   |QBF aAsigna;
     |SI #2#26 ! "    ";
         aAsigna = aAsigna + ", " + #2#26;   |QBF aAsigna;

         |SI #2#28 ! "  ";
             aAsigna = aAsigna + " " + #2#28;   |QBF aAsigna;

             |SI #2#29 ! "  ";
                 aAsigna = aAsigna + " " + #2#29;   |QBF aAsigna;
             |FINSI;
         |FINSI;
     |FINSI;
     aIdenti = "!#2#";
     |HAZ Asigna;

     aAsigna = (("00" + #2#30) % -102) + (("000" + #2#31) % -103);
     aAsigna = aAsigna + " " + #2#32;
     aIdenti = "!#3#";
     |HAZ Asigna;

     |ABRE #5;
     #5#0 = #2#30;
     #5#1 = #2#31;
     |LEE 000#5=;
     |SI FSalida ! 0;  |DDEFECTO #5;  |FINSI;
     |CIERRA #5;

     aAsigna = "(" + #5#2;    |QBF aAsigna;
     aAsigna = aAsigna + ")";
     aIdenti = "!#4#";
     |HAZ Asigna;
|FPRC;

|PROCESO CargaCampos;
     |HAZ AbreWord;

     |MENSA "      Asignando Campos, espere un Momento.";

     #2#0 = #1#0;
     |LEE 040#2=;
     |SI FSalida ! 0;  |DDEFECTO #2;  |FINSI;

     |HAZ CargaPersonal;

     aAsigna = #0#29;  |QBF aAsigna;
     aAsigna = aAsigna + ",";
     aAlfa   = #0#25 % 0102;   aAsigna = aAsigna + " " + aAlfa + " de ";
     aAlfa1  = #0#25 % 0402;   |HAZ Mes;
                               aAsigna = aAsigna + aAlfa + " de ";
     aAlfa   = #0#25 % 0704;   aAsigna = aAsigna + aAlfa;

     aIdenti = "!#5#";
     |HAZ Asigna;

     aAsigna = #0#27;  |QBF aAsigna;
     aAsigna = aAsigna;
     aIdenti = "!#ATENCION#";
     |HAZ Asigna;

     aAsigna = #0#28;  |QBF aAsigna;
     aAsigna = aAsigna + ",";
     aIdenti = "!#NOMASESORIA#";
     |HAZ Asigna;

     nImporte  = #4#16 % 40;
     eaImporte = nImporte;  |HAZ Importe;
     aAsigna   = eaImporte + " Euros.";
     aIdenti   = "!#IMPORTE#";
     |HAZ Asigna;

     |MENSA "      Imprimiendo, espere un Momento.";

     |WORD_IMPRIME nWord, "";
     |WORD_PROPIEDADES nWord, "Visible", "NO";

     |SLEEP 10;

     |MENSA "      Cerrando Documento";

     |WORD_SALVA nWord, "";

     |RFBORRA aAbre;
|FPRC;

|DEFBUCLE rem01001;
     #4#1;
     5, 14, 8, 10;
     #1#0, 00, "          ", "100", "N", "S", aTipo;
     #1#0, 99, "zzzzzzzzzz", "101", "N", "S", aTipo;
     ;
     NULL, CargaCampos;
|FIN;

|PROCESO Emision;
     aDModelo = "100";
     aHModelo = "101";
     |SI #1#14 ! "IMPRESO   ";  |ACABA;  |FINSI;

     |BUCLE rem01001;
|FPRC

|DEFBUCLE reacceso;
     #1#1;
     ;
     #0#2;
     #0#3;
     ;
     NULL, Emision;
|FIN;

|PROCESO Tipo3;  |TIPO 3;
     aTipo = "N";
     |SI #0#26 = "D";  aTipo = "S";  |FINSI;

     |ABRE #2;
     |SI #0#1 = "S";
         |BUCLE reacceso;
     |SINO;
         |PARA nParrilla = 4; |SI nParrilla [ 24; |HACIENDO nParrilla = nParrilla + 1;
               |SI #0nParrilla ! 0;
                   #0#2 = #0nParrilla;
                   #0#3 = #0nParrilla;
                   |BUCLE reacceso;
               |FINSI;
         |SIGUE;
     |FINSI;
     |CIERRA #2;
|FPRC;

|PROGRAMA;
     |HAZ LeeUnidadBasico;

     |CLS;
     |CABEZA "Emision Carta Medios Telematicos";

     |DBASE aPathDoc; |QBF aPathDoc;
     aPathDoc = aPathDoc + "word/";

     aAbre = aPathDoc + "w2plazo1.doc";
     |XABRE "E", aAbre, nHandle;
     |SI FSalida < 0;
         |MENAV "     Instale Ficheros patrones (Documentos en Word)";
         |ACABA;
     |FINSI;

     |ABRET #0;
     |LEE 101#0p;
     |SI FSalida ! 0;
         |DDEFECTO #0;
         |GRABA 020#0b;
     |FINSI;

     |PDEFECTO #0, 1, 25;

     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |GRABA 020#0a;
     |LIBERA #0;
|FPRO;

|PROCESO Tipo80;  |TIPO 80;
     FSalida = 2499;
|FPRC;
