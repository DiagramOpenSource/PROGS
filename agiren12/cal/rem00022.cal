|FICHEROS;
     reparame #1;
     rem00022 #20;
     rem00041 #41;
     rem00059 #59;
     retabla7 #407;

     rem00043 #43;

     rew00622 #622,MANTE;
     rew00643 #643,MANTE;

     rew00003;
     rew00005;

     rew99999 #999;
|FIN;

|VARIABLES;
     aAlfa         = "";
     aAlfa1        = "";
     aAlfa2        = "";
     aAlfa3        = "";
     aLong         = "";
     aQueQuiero    = "";
     aNumCampos    = "";
     aNumPanta     = "";
     aPathRenta    = "";
     aDef          = "";
     aDescri       = "";

     aEsc1         = "";
     aEsc2         = "";
     aRetu         = "";
     aTecla        = "";
     aID           = "";

     nVentana       = 0;
     nCampo         = 0;
     nRango         = 0;
     nGrid          = -1;
     nGrid43        = -1;
     nBotonAlta     = 0;
     nBotonModi     = 0;
     nBotonBaja     = 0;
     nBotonCntr     = 0;
     nBotonRdto     = 0;
     nBotonCalc1    = 0;
     nBotonCalc2    = 0;
     nBotonCalc3    = 0;
     nBotonCalc43_1 = 0;
     nBotonCalc43_2 = 0;
     nBotonCalc43_3 = 0;
     nBotonCalc43_4 = 0;
     nBotonAlta43   = 0;
     nBotonModi43   = 0;
     nBotonBaja43   = 0;
     nEBotonModi43  = 0;
     nEBotonBaja43  = 0;
     nPanta         = 0;
     nPanta643      = 0;
     nID            = 0;

     nLong         = 0;
     nCaracter     = 0;
     nPosi         = 0;
     nNumCampos    = 0;
     nNumPanta     = 0;
     nModo         = 0;
     nEntrada      = 0;
     nPtas         = 0;
     nRetencion    = 0;
     nCampo15      = 0;
     nCampo16      = 0;
     nCampo17      = 0;
     nCampo20      = 0;
     nCampo27      = 0;
     nValor        = 0;
     nValor1       = 0;
     nValorPat     = 0;
     nDias         = 0;
     nAnyoVenta    = 0;
     nAnyoCompra   = 0;
     nDiferencia   = 0;
     nCoefi        = 0;
     nLinea        = 0;
     nContenido    = 0;
     nCas732       = 0;
     nCas733       = 0;
     nCas735       = 0;
     nCas736       = 0;
     nCas737       = 0;
     nCas745       = 0;
     nCas746       = 0;
     nCas747       = 0;
     nCas748       = 0;
     nAnyo         = 0;
     nSwHay        = 0;
     nCampo1       = 0;
     nCampo2       = 0;
     nCampo3       = 0;
     nCampo4       = 0;
     nCampo5       = 0;
     nFallecido    = 0;

     nTodosDias    = 0;
     nParciDias    = 0;

     fFechaLim     = @;

     fFechaAdq     = @;
     fFechaTra     = @;
     fFecha1       = @;
     fFecha2       = @;

     &enEmpresa    = 0;
     &enModo       = 0;
     &enModoReg    = 0;
     &eaDeclarante = "";
     &enComunes    = 0;
     &enChequeo    = 0;
     &enMinusvalia = 0;
     &eaMinuAyuda  = "";
     &eaFallece    = "";
     &eaSwVarios   = "";
     &eaVarDni     = "";
     &enSwLiquida  = 0;
     &enSwRetorno  = 0;
     &eaPrograma   = "";
     &eaPunto      = "";

     &eaLetra      = "";
     &eaDeDonde    = "";
     &eaLinDonde   = "";
     &eaAnyoUsu    = "";
     &eaEdadDec    = "";
     &eaPatr06     = "";
     &eaPatr07     = "";
     &eaPatr08     = "";
     &eaPatr09     = "";
     &eaPatr10     = "";
     &eaPatr11     = "";
     &eaPatr12     = "";
     &eaPatr13     = "";
     &eaPatr14     = "";
     &eaPatr15     = "";
     &eaPatr16     = "";
     &eaPatr17     = "";
     &eaPatr18     = "";
     &eaPatr19     = "";
     &eaPatr20     = "";
     &eaPatr21     = "";

     &eaAzul       = "";
     &eaVerde      = "";
     &eaRojo       = "";
     &eaRosa       = "";
|FIN;

|| ---- PROCESOS GRABACIONES EXTERIORES

|PROCESO Calculo411;
     |SI #41#14 ] "31.12.1994";  |ACABA;  |FINSI;

     |ABRE #999;
     |SELECT #2#999;
     #999#1 = #41#2;
     |LEE 000#999=;
     |SI FSalida ! 0;  |DDEFECTO #999;  |FINSI;

     |SELECT #1#999;
     |CIERRA #999;

     |SI #999#5 = "S";
         #41#39 = #41#24;
         |ACABA;
     |FINSI;

     |SI #41#41 < #41#16;
         #41#39 = #41#41 - #41#21;
     |SINO;
         #41#39 = #41#24;
     |FINSI;
|FPRC;

|PROCESO FechaLimite;
     fFechaLim  = "20.01.2006";

     |SI #41#15 [ fFechaLim;  |ACABA;  |FINSI;

     |ABRE #999;
     |SELECT #2#999;
     #999#1 = #41#2;
     |LEE 000#999=;
     |SI FSalida ! 0;  |DDEFECTO #999;  |FINSI;

     |SELECT #1#999;
     |CIERRA #999;

     |SI #999#5 = "S";
         #41#39 = #41#24;
         |ACABA;
     |FINSI;

     nTodosDias = (#41#15    - #41#14);
     nParciDias = (fFechaLim - #41#14);

     #41#39     = ((#41#24 / nTodosDias) * nParciDias) ! 2;
|FPRC;

|PROCESO Total41;
     aAlfa1      = #41#14 % -104;
     aAlfa3      = #41#14 % 0105;
     aAlfa2      = #41#15 % -104;
     nAnyoCompra = aAlfa1;
     nAnyoVenta  = aAlfa2;
     nDiferencia = #41#15 - #41#14

     |SI #41#12 = "6";  |O #41#12 = "7";
         |SI #41#14      < "01.01.1984";    nAnyoCompra = 1983;  |FINSI;

         |ABRE #407;
         #407#0 = 2012;
         #407#1 = nAnyoCompra;
         |LEE 040#407=;
         |SI FSalida ! 0;  |DDEFECTO #407;  |FINSI;
         |CIERRA #407;

         #41#18 = #407#3;

         |SI nDiferencia < 366;             #41#18 = 1;      |FINSI;
         |SI #41#13      ! "I";             #41#18 = 1;      |FINSI;
     |FINSI;

     |SI #41#12 = "1";
         |SI #41#14      < "31.12.1994";    nAnyoCompra = 1994;  |FINSI;
         |SI #41#14      = "31.12.1994";    nAnyoCompra = 1995;  |FINSI;

         |ABRE #407;
         #407#0 = 2012;
         #407#1 = nAnyoCompra;
         |LEE 040#407=;
         |SI FSalida ! 0;  |DDEFECTO #407;  |FINSI;
         |CIERRA #407;

         #41#18 = #407#2;
         |SI nDiferencia [ 366;             #41#18 = 1;      |FINSI;
         |SI #41#13      ! "I";             #41#18 = 1;      |FINSI;
     |FINSI;

     |SI #41#12 = "2";  |O #41#12 = "3";  |O #41#12 = "4";  |O #41#12 = "5";
         #41#18 = 1;
     |FINSI;

     nAnyoCompra = aAlfa1;
     |SI aAlfa3 = "31.12";  nAnyoCompra = nAnyoCompra + 1;  |FINSI;

     #41#19 = #41#17 * #41#18;
     #41#21 = #41#19 - #41#20;
     #41#40 = 0;

     nValor = #41#16 - #41#21;
     |SI nValor < 0;
         #41#22 = -nValor;
         |SI #43#14 = "S";  #41#23 = #41#22;  |FINSI;

         |PARA nCampo = 24;  |SI nCampo [ 33;  |HACIENDO nCampo = nCampo + 1;
               #41nCampo = 0;
         |SIGUE;

         #41#38 = "N";
         #41#39 = 0;
         #41#40 = 0;
     |SINO;
         #41#22 = 0;
         #41#23 = 0;

         #41#24 = nValor;

         |SI #41#12 ! "6";  |Y #41#12 ! "7";
             #41#25 = ((1996 - nAnyoCompra) + 1) - 2;
             nAnyo  = (1996 - nAnyoCompra) + 1;
             |SI #41#25 < 0;  #41#25 = 0;  |FINSI;

             nCoefi     = 0;
             |SI nAnyo ] 3;
                 |SI #41#13 = "I";  nCoefi = #41#25 * 11.11; |FINSI;
                 |SI #41#13 = "O";  nCoefi = #41#25 * 14.28;  |FINSI;

                 |SI #41#13 = "A";
                                        nCoefi = #41#25 * 25;
                     |SI #41#12 = "2";  nCoefi = #41#25 * 11.11;  |FINSI;
                 |FINSI;
             |FINSI;

             |SI nCoefi > 99; nCoefi = 100; |FINSI;

             #41#39     = #41#24;

             |SI #41#12 ! "4";
                 |SI #41#12 = "2";  |Y #41#13 = "A";
                     |HAZ Calculo411;
                 |SINO;
                     |HAZ FechaLimite;
                 |FINSI;
             |SINO;
                 |HAZ Calculo411;
             |FINSI;

             |SI #41#14 ] "31.12.1994";
                 #41#39 = 0;
             |FINSI;

             |SI #41#39 < 0;  #41#39 = 0;  |FINSI;

             #41#40 = #41#39 % nCoefi;
             #41#26 = #41#24 - #41#40;

             |SI #41#27 > #41#26;
                 #41#27 = #41#26;
             |FINSI;

             #41#28 = #41#26 - #41#27;
             |SI #43#14 = "S";  #41#29 = #41#28;  |FINSI;

             |PARA nCampo = 30;  |SI nCampo [ 33;  |HACIENDO nCampo = nCampo + 1;
                   #41nCampo = 0;
             |SIGUE;
         |SINO;
             #41#31 = #41#24 - #41#30;
             #41#32 = #41#31;
             #41#33 = #41#31 - #41#32;

             |PARA nCampo = 25;  |SI nCampo [ 29;  |HACIENDO nCampo = nCampo + 1;
                   #41nCampo = 0;
             |SIGUE;

             #41#38 = "N";
             #41#39 = 0;
             #41#40 = 0;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO El41;
     #41#0 = #20#0;
     #41#1 = 99;
     |LEE 040#41];
     |SI FSalida = 0;
         |LEE 040#41a;
     |SINO;
         |LEE 040#41u;
     |FINSI;

     |SI FSalida = 0; |Y #41#0 = #20#0;
         nLinea = #41#1 + 1;
     |SINO;
         nLinea = 1;
     |FINSI;

     |DDEFECTO #41;
     #41#0  = #20#0;
     #41#1  = nLinea;
     |PARA nCampo = 2;  |SI nCampo [ 10;  |HACIENDO nCampo = nCampo + 1;
           #41nCampo = #20nCampo;
     |SIGUE;
     #41#3  = "R";
     |SI #20#3 = "X";  #41#3 = "X";  |FINSI;
     #41#11 = aDescri;

                         #41#12 = "3";
     |SI #20#11 = "E1";  #41#12 = "3";  |FINSI;
     |SI #20#11 = "F2";  #41#12 = "3";  |FINSI;
     |SI #20#11 = "G1";  #41#12 = "3";  |FINSI;
     |SI #20#11 = "G3";  #41#12 = "3";  |FINSI;
     |SI #20#11 = "G4";  #41#12 = "3";  |FINSI;
     |SI #20#11 = "H2";  #41#12 = "3";  |FINSI;

     |SI #20#11 = "F1";  #41#12 = "4";  |FINSI;
     |SI #20#11 = "G2";  #41#12 = "4";  |FINSI;
     |SI #20#11 = "H1";  #41#12 = "4";  |FINSI;

     |SI #41#12 = "3";  #41#13 = "O";  |FINSI;
     |SI #41#12 = "4";  #41#13 = "A";  |FINSI;

     #41#14 = fFechaAdq;
     #41#15 = fFechaTra;
     #41#16 = nCampo15;
     #41#17 = nCampo16;
     #41#41 = #43#15;

     |HAZ Total41;

     #41#34 = "C1";
     #41#35 = #20#1;
     |GRABA 020#41n;
     |LIBERA #41;

     eaPrograma   = "rem00041";
     eaPunto      = "  ";
     |HAZ ProgramaActivo;
|FPRC;

|PROCESO Graba59;
     #59#0 = #20#0;
     #59#1 = 99;
     |LEE 040#59];
     |SI FSalida = 0;
         |LEE 040#59a;
     |SINO;
         |LEE 040#59u;
     |FINSI;

     |SI FSalida = 0; |Y #59#0 = #20#0;
         nLinea = #59#1 + 1;
     |SINO;
         nLinea = 1;
     |FINSI;

     |DDEFECTO #59;
     #59#0  = #20#0;
     #59#1  = nLinea;
     |PARA nCampo = 2;  |SI nCampo [ 10;  |HACIENDO nCampo = nCampo + 1;
           #59nCampo = #20nCampo;
     |SIGUE;
     #59#3  = "R";
     |SI #20#3 = "X";  #59#3 = "X";  |FINSI;

     #59#13 = #20#17;
     #59#14 = nCas732;
     #59#15 = nCas733;
     #59#16 = nCas735 + nCas745;
     #59#17 = nCas746;
     #59#18 = nCas736 + nCas747;
     #59#19 = nCas737 + nCas748;

     #59#11 = "C1";
     #59#12 = #20#1;
     |GRABA 020#59n;
     |LIBERA #59;

     eaPrograma   = "rem00059";
     eaPunto      = "  ";
     |HAZ ProgramaActivo;
|FPRC;

|PROCESO Calculo411_59;
     |ABRE #999;
     |SELECT #2#999;
     #999#1 = #20#2;
     |LEE 000#999=;
     |SI FSalida ! 0;  |DDEFECTO #999;  |FINSI;

     |SELECT #1#999;
     |CIERRA #999;

     |SI #999#5 = "S";
         nValor1 = nValor;
         |ACABA;
     |FINSI;

     |SI #43#2 < "20.01.2006";  |Y #43#15 = 0;
         nValor1 = nValor;
         |ACABA;
     |FINSI;

     |SI nValorPat < nCampo15;
         nValor1 = nValorPat - nCampo16;
     |SINO;
         nValor1 = nValor;
     |FINSI;
|FPRC;

|PROCESO Total59;
     aAlfa1      = #43#6 % -104;
     aAlfa3      = #43#6 %  105;
     aAlfa2      = #43#9 % -104;
     nAnyoCompra = aAlfa1;
     nAnyoVenta  = aAlfa2;
     nDiferencia = nAnyoVenta - nAnyoCompra;

     |SI aAlfa3 = "31.12";  nAnyoCompra = nAnyoCompra + 1;  |FINSI;

     nCas732 = nCas732 + nCampo15;
     nCas733 = nCas733 + nCampo16;

     nValor = nCampo15 - nCampo16;
     |SI nDias [ 366;
         |SI nValor ] 0;
             nCas735 = nCas735 + nValor;
         |SINO;
             nValor     = -nValor;
             nCas736    = nCas736 + nValor;
             |SI #43#14 = "S";  nCas737 = nCas737 + nValor;  |FINSI;
         |FINSI;
         nCas746 = nCas735 + nCas745;
     |SINO;
         |SI nValor ] 0;
             nCas745 = nCas745 + nValor;
             nAnyo   = (1996 - nAnyoCompra) + 1;
             nCoefi  = 0;
             |SI nAnyo ] 3;   nCoefi = (nAnyo - 2) * 25;  |FINSI;
             |SI nCoefi > 99; nCoefi = 100;               |FINSI;
             |SI nCoefi [ 100;
                 nValor1 = nValor;
                 |HAZ Calculo411_59;
                 |SI nValor1 < 0;  nValor1 = 0;  |FINSI;

                 |SI #43#6 ] "31.12.1994";
                     nValor1 = 0;
                 |FINSI;

                 nCas746 = nCas746 + (nValor - (nValor1 % nCoefi));
             |FINSI;
         |SINO;
             nValor     = -nValor;
             nCas747    = nCas747 + nValor;
             |SI #43#14 = "S";  nCas748 = nCas748 + nValor;  |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO PuntosG;
     |SI #43#11 = 0;  |ACABA;  |FINSI;

     nSwHay     = 1;
     nDias      = #43#9 - #43#6;
     aDescri    = #20#17;
     fFechaAdq  = #43#6;
     fFechaTra  = #43#9;
     nCampo15   = #43#11 - #43#13;
     nCampo16   = #43#8  + #43#12;
     nValorPat  = #43#15;

     |SI #20#11 ! "G2";
         |HAZ El41;
     |SINO;
         |HAZ Total59;
     |FINSI;
|FPRC;

|DEFBUCLE rem00043;
     #43#1;
     4;
     #20#0, #20#1, "01.01.1900", 01, "V";
     #20#0, #20#1, "31.12.2999", 99, "V";
     be;
     NULL, PuntosG;
|FIN;

|PROCESO Borra41;
     |BORRA 020#41a;
     |LIBERA #41;

     eaPrograma   = "rem00041";
     eaPunto      = "  ";
     |HAZ ProgramaActivo;
|FPRC;

|DEFBUCLE rem00041;
     #41#1;
     34, 35;
     #20#0, 01, "C1", #20#1;
     #20#0, 99, "C1", #20#1;
     be;
     NULL, Borra41;
|FIN;

|PROCESO Borra59;
     |BORRA 020#59a;
     |LIBERA #59;

     eaPrograma   = "rem00059";
     eaPunto      = "  ";
     |HAZ ProgramaActivo;
|FPRC;

|DEFBUCLE rem00059;
     #59#1;
     11, 12;
     #20#0, 01, "C1", #20#1;
     #20#0, 99, "C1", #20#1;
     be;
     NULL, Borra59;
|FIN;

|PROCESO Borra43;
     |BORRA 020#43a;
     |LIBERA #43;
|FPRC;

|DEFBUCLE rem00043B;
     #43#1;
     ;
     #20#0, #20#1, "01.01.1900", 01;
     #20#0, #20#1, "31.12.2999", 99;
     be;
     NULL, Borra43;
|FIN;

|PROCESO Patrimonio;
     |SI #20#3  = "R";           |ACABA;  |FINSI;
     |SI #20#3  = "X";           |ACABA;  |FINSI;
     |SI #20#2  = "1 FAMILIAR";  |ACABA;  |FINSI;
     |SI #20#2  = "2 FAMILIAR";  |ACABA;  |FINSI;
     |SI #20#2  = "3 FAMILIAR";  |ACABA;  |FINSI;
     |SI #20#2  = "4 FAMILIAR";  |ACABA;  |FINSI;
     |SI #20#15 = 0;             |ACABA;  |FINSI;

     |HAZ LimpiaPatrim;

     eaDeclarante = #20#2;

     eaLetra    = #20#11;
     eaDeDonde  = "C0";
     eaLinDonde = #20#1;
     eaAnyoUsu  = #20#14;
     eaPatr06   = #20#17;
     eaPatr16   = #20#13;
     eaPatr10   = #20#15;
     |SI #20#11 = "E1";  eaPatr13 = #20#18;  |FINSI;
     eaPatr20   = #20#5;
     eaPatr21   = #20#6;

     |HAZ GrabaPatrimonio;
|FPRC;

|PROCESO GrabaLoDemas;
     nCas732 = 0;
     nCas733 = 0;
     nCas735 = 0;
     nCas736 = 0;
     nCas737 = 0;
     nCas745 = 0;
     nCas746 = 0;
     nCas747 = 0;
     nCas748 = 0;
     nSwHay  = 0;

     |ABRE #41;
     |BUCLE rem00043;
     |CIERRA #41;

     |SI #20#11 = "G2";  |Y nSwHay = 1;
         |ABRE #59;
         |HAZ Graba59;
         |CIERRA #59;
     |FINSI;

     |HAZ Patrimonio;
|FPRC;

|| ---- PROCESOS DEL REM00043

|PROCESO Tipo12Fw643;  |TIPO 12;
|FPRC;

|PROCESO TotalFw643;
     #643#8  = #643#5 * #643#7;
     #643#11 = #643#5 * #643#10;

     |PINTA #643#8;
     |PINTA #643#11;
|FPRC;

|PROCESO Tipo0C6Fw643;   |TIPO 0;
     |SI #643#6 > #643#2;
         |MENAV "      Fecha Compra es Superior a la Fecha Operacion.";
         |ERROR;
     |FINSI;

     |C_M #643#15, 1, "S";

     |SI #622#11 ! "F1";  |Y #622#11 ! "G1";  |Y #622#11 ! "H1";
      |Y #622#11 ! "G2";
         #643#15 = 0;
         |C_M #643#15, 1, "N";
     |FINSI;

     |SI #643#6 ] "31.12.1994";
         #643#15 = 0;
         |C_M #643#15, 1, "N";
     |FINSI;

     |SI #622#11 = "G2";
         |SI #643#2 < "20.01.2006";
             #643#15 = 0;
             |C_M #643#15, 1, "N";
         |FINSI;
     |FINSI;

     |PINTA #643#15;

     |SI #643#4 ! "V";           |ACABA;  |FINSI;
     |SI #643#6 ] "31.12.1994";  |ACABA;  |FINSI;

     |SI #622#2 = "GANANCIAL ";  |O #622#2 = "VARIOS    ";
         nFallecido = 0;

         |ABRE #999;
         |SELECT #2#999;
         #999#1 = "TITULAR   ";
         |LEE 000#999=;
         |SI FSalida ! 0;  |DDEFECTO #999;  |FINSI;
         |SI #999#5 = "S";  nFallecido = 1;  |FINSI;

         |SELECT #2#999;
         #999#1 = "CONYUGE   ";
         |LEE 000#999=;
         |SI FSalida ! 0;  |DDEFECTO #999;  |FINSI;
         |SI #999#5 = "S";  nFallecido = 1;  |FINSI;

         |SELECT #1#999;
         |CIERRA #999;

         |SI nFallecido = 1;
             |MENAV "0000 Venta con un fallecido con fecha de adquisicion inferior a 31.12.1994, por favor introduzcalos individuales";
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo0C4Fw643;   |TIPO 0;
     |C_M #643#6,  1, "S";
     |C_M #643#7,  1, "S";
     |C_M #643#10, 1, "S";

     |SI #643#4 = "C";
         |C_M #643#6,  1, "N";
         |C_M #643#10, 1, "N";  #643#10 = 0;
         |C_M #643#11, 1, "N";  #643#11 = 0;

         #643#9 = "01.01.0000";
         #643#6 = #643#2;
     |FINSI;

     |SI #643#4 = "V";
         #643#9 = #643#2;
         |SI #643#9 < "01.01.2012";  |O #643#9 > "31.12.2012";
             |MENAV "    No puede ser una Venta. La Fecha no corresponde al Ejercicio";
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;

     |PINTA #643#6;
     |PINTA #643#9;
     |PINTA #643#10;
     |PINTA #643#11;
|FPRC;

|PROCESO BotonCalc43;
     |DDEFECTO #rew00005;

     |SI FSalida = nBotonCalc43_1; nCampo = 7;   |FINSI;
     |SI FSalida = nBotonCalc43_2; nCampo = 10;  |FINSI;
     |SI FSalida = nBotonCalc43_3; nCampo = 12;  |FINSI;
     |SI FSalida = nBotonCalc43_4; nCampo = 13;  |FINSI;

     #rew00005#1  = #643nCampo;
     #rew00005#20 = #643nCampo;

     |HAZ PantaREW00005;

     #643nCampo = #rew00005#20;  |PINTA #643nCampo;

     aAlfa = "|C" + (("000" + nCampo) % -103) + "WID";
     aID   = #643aAlfa;
     nID   = aID;
     |FOCOTECLADO nID;
|FPRC;

|PROCESO Tipo0C3Fw643;  |TIPO 0;
     |C_M #643#2, 0, aAlfa;
     |SI aAlfa = "N";  |ACABA;  |FINSI;

     #43#0 = #643#0;
     #43#1 = #643#1;
     #43#2 = #643#2;
     #43#3 = #643#3;
     |LEE 000#43=;
     |SI FSalida = 0;
         |MENAV "0000El registro ya existe.";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo0C2Fw643;  |TIPO 0;
     |SI #643#2 ] "31.12.2012";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO CargaFantasma643;
     |PARA nCampo = 0;  |SI nCampo [ 15;  |HACIENDO nCampo = nCampo + 1;
           #643nCampo = #43nCampo;
     |SIGUE;

     |SI #43#14 = "S";  #643#14 = "1";  |FINSI;
     |SI #43#14 = "N";  #643#14 = "2";  |FINSI;

     |CONTROL_SIMPLE nPanta643, "BOTON,&4", 1628, 1629, BotonCalc43;
     nBotonCalc43_1 = FSalida;

     |CONTROL_SIMPLE nPanta643, "BOTON,&5", 2028, 2029, BotonCalc43;
     nBotonCalc43_2 = FSalida;

     |CONTROL_SIMPLE nPanta643, "BOTON,&6", 1660, 1661, BotonCalc43;
     nBotonCalc43_3 = FSalida;

     |CONTROL_SIMPLE nPanta643, "BOTON,&7", 2060, 2061, BotonCalc43;
     nBotonCalc43_4 = FSalida;
|FPRC;

|PROCESO DescargaFantasma643;
     |PARA nCampo = 0;  |SI nCampo [ 15;  |HACIENDO nCampo = nCampo + 1;
           #43nCampo = #643nCampo;
     |SIGUE;

     |SI #643#14 = "1";  #43#14 = "S";  |FINSI;
     |SI #643#14 = "2";  #43#14 = "N";  |FINSI;
|FPRC;

|PROCESO Alta43;
     |SI enModoReg = 4;  |ACABA;  |FINSI;

     |VENTANA 0501, 2575, -1, 0, "";
     nVentana = FSalida;

     |PTEC 802;  |PAUSA;

     |DDEFECTO #43;
     |DDEFECTO #643;

     #43#0 = #622#0;
     #43#1 = #622#1;

     |PINPA #0#643;  nPanta643 = FSalida;

     |C_M #643#2, 1, "S";
     |C_M #643#3, 1, "S";

     |HAZ CargaFantasma643;
     |PINDA #0#643;

     |ENDATOS #1#643;
     |SI FSalida = 0;
         |HAZ DescargaFantasma643;

         |GRABA 020#43n;
         |LIBERA #43;

         enSwRetorno = 1;
         |REFRESCACONTROL nGrid43;
     |FINSI;

     |FINVENTANA nVentana;

     |PULSATECLA;
|FPRC;

|PROCESO Modificar43;
     |SI nEBotonModi43 = 0;  |ACABA;  |FINSI;

     |LEE 101#43=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |VENTANA 0501, 2575, -1, 0, "";
     nVentana = FSalida;

     |PTEC 802;  |PAUSA;

     |PINPA #0#643;  nPanta643 = FSalida;

     |C_M #643#2, 1, "N";
     |C_M #643#3, 1, "N";

     |HAZ CargaFantasma643;
     |PINDA #0#643;

     |ENDATOS #1#643;
     |SI FSalida = 0;
         |HAZ DescargaFantasma643;

         |GRABA 020#43a;
         |LIBERA #43;
         enSwRetorno = 1;

         |REFRESCACONTROL nGrid43;
     |FINSI;

     |FINVENTANA nVentana;

     |PULSATECLA;
|FPRC;

|PROCESO Baja43;
     |SI nEBotonBaja43 = 0;  |ACABA;  |FINSI;

     |CONFI "0000 Confirma la baja del registro seleccionado";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |LEE 101#43=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |BORRA 020#43a;
     |LIBERA #43;

     enSwRetorno = 1;

     |REFRESCACONTROL nGrid43;
     |FOCOTECLADO nGrid43;
|FPRC;

|| ---- PROCESOS DEL REW00622

|PROCESO Tipo12Fw622;  |TIPO 12;
     |SI FSalida = 7;  |ACABA;  |FINSI;

     aAlfa = #622#2;  |QBF aAlfa;
     |SI aAlfa = "";
         |MENAV "0000No se puede validar la ficha, no hay contribuyente introducido";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo0C11Fw622;  |TIPO 0;
     aAlfa = "  E1F1F2G1G2G3G4H1H2";
     aAlfa = aAlfa - #622Campo;
     |SI FSalida = 0;
         |ERROR;
         |ACABA;
     |FINSI;
|FPRC;

|PROCESO Tipo0C1Fw622;  |TIPO 0;
     |C_M #622#1, 0, aAlfa;
     |SI aAlfa = "N";  |ACABA;  |FINSI;

     #20#0 = #622#0;
     #20#1 = #622#1;
     |LEE 000#20=;
     |SI FSalida = 0;
         |MENAV "0000El registro ya existe.";
         |ERROR;
         |ACABA;
     |FINSI;

     |HAZ CreaLineal43;
|FPRC;

|PROCESO Usufructuario;
     |C_M #622#14, 1, "S";
     |SI #622#13 ! "N";
         |C_M #622#14, 1, "N";  #622#14 = 0;  |PINTA #622#14;
     |FINSI;
|FPRC;

|PROCESO nContenido;
     nContenido = Contenido;
     |SI #622Campo ! nContenido;
         #622#21 = #622#19 % #622#20;   |PINTA #622#21;
         #622#24 = #622#19 % #622#23;   |PINTA #622#24;
     |FINSI;
|FPRC;

|PROCESO Incremento;
     |SI #622#20 ! 40;  |Y #622#20 ! 25;  |Y #622#20 ! 0;
         |MENAV  "     Porcentaje incremento incoherente, el 25% o 40%";
         |ERROR;
         |ACABA;
     |FINSI;

     nContenido = Contenido;
     |SI #622Campo ! nContenido; |O #622#21 = 0;
         #622#21 = #622#19 % #622#20;
     |FINSI;

     |ABRE #1;
     |LEE 040#1p;
     |SI FSalida ! 0;  |DDEFECTO #1;  |FINSI;
     |CIERRA #1;
|FPRC;

|PROCESO Tipo0C28;
     |C_M #622#20, 1, "S";
     |C_M #622#21, 1, "S";
     |SI #622#28 = "S";
         |C_M #622#20, 1, "N";  #622#20 = 0;  |PINTA #622#20;
         |C_M #622#21, 1, "N";  #622#21 = 0;  |PINTA #622#21;
     |FINSI;
|FPRC;

|PROCESO Retencion;
     nContenido = Contenido;
     |SI #622Campo = nContenido;  |Y #622#24 ! 0;  |ACABA;  |FINSI;

     #622#24 = #622#19 % #622#23;
|FPRC;

|PROCESO Totalm020;
     #622#22 = #622#19 + #622#21;

     |PINTA #622#21;
     |PINTA #622#22;
     |PINTA #622#24;
|FPRC;


|PROCESO Baja_43;
     #43#0 = #622#0;
     #43#1 = #622#1;
     #43#2 = "01.01.1900";
     #43#3 = 1;
|FPRC;

|PROCESO Alta_43;
     #43#0 = #622#0;
     #43#1 = #622#1;
     #43#2 = "31.12.2999";
     #43#3 = 99;
|FPRC;

|PROCESO Evento43;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#43=;

         |SI enModoReg = 4;
             |ESTADO_CONTROL nBotonModi43, "DISABLE";  nEBotonModi43 = 0;
             |ESTADO_CONTROL nBotonBaja43, "DISABLE";  nEBotonBaja43 = 0;
         |SINO;
             |ESTADO_CONTROL nBotonModi43, "ENABLE";   nEBotonModi43 = 1;
             |ESTADO_CONTROL nBotonBaja43, "ENABLE";   nEBotonBaja43 = 1;
         |FINSI;

         |PULSATECLA;
     |FINSI;
|FPRC;

|PROCESO CreaLineal43;
     |SI nGrid43 ! -1;
         |DESTRUYECONTROL nGrid43;
     |FINSI;

     |LINEAL_SIMPLE #1#43, nRango, 2802, 3498, Baja_43, Alta_43, Evento43;
     nGrid43 = FSalida;
|FPRC;

|PROCESO CargaFantasma;
     |C_M #622#11, 1, "S";
     |C_M #622#13, 1, "S";
     |C_M #622#14, 1, "S";
     |C_M #622#15, 1, "S";
     |C_M #622#16, 1, "S";
     |C_M #622#17, 1, "S";
     |C_M #622#18, 1, "S";
     |C_M #622#19, 1, "S";
     |C_M #622#20, 1, "S";
     |C_M #622#21, 1, "S";
     |C_M #622#23, 1, "S";
     |C_M #622#24, 1, "S";
     |C_M #622#27, 1, "S";
     |C_M #622#28, 1, "S";
     |C_M #622#29, 1, "S";

     |PARA nCampo = 0;  |SI nCampo [ nNumCampos;  |HACIENDO nCampo = nCampo + 1;
           #622nCampo = #20nCampo;

           |C_V #622nCampo, 0, aAlfa;
           |SI aAlfa = "S";  |PINTA #622nCampo;  |FINSI;
     |SIGUE;

     |BUCLE rem00041;
     |BUCLE rem00059;

     eaDeDonde  = "C0";
     eaLinDonde = #20#1;
     eaLetra    = #20#11;
     |HAZ BorraPatrimonio;

     |HAZ CreaLineal43;
|FPRC;

|PROCESO DescargaFantasma;
     |PARA nCampo = 0;  |SI nCampo [ nNumCampos;  |HACIENDO nCampo = nCampo + 1;
           |SI enSwLiquida = 1;
               |SI #20nCampo = #622nCampo;
                   enSwRetorno = 1;
               |FINSI;
           |FINSI;

           #20nCampo = #622nCampo;
     |SIGUE;

                        #20#30 = "";
     |SI #20#3  = "X";  #20#30 = eaRojo;  |FINSI;

     |HAZ PintaDatosPantalla;
|FPRC;

|PROCESO Alta;
     |ESTADO_CONTROL nBotonAlta,   "DISABLE";
     |ESTADO_CONTROL nBotonModi,   "DISABLE";
     |ESTADO_CONTROL nBotonBaja,   "DISABLE";
     |ESTADO_CONTROL nBotonRdto,   "DISABLE";
     |ESTADO_CONTROL nBotonCalc1,  "ENABLE";
     |ESTADO_CONTROL nBotonCalc2,  "ENABLE";
     |ESTADO_CONTROL nBotonCalc3,  "ENABLE";
     |ESTADO_CONTROL nBotonAlta43, "ENABLE";

     |HAZ QuitaBotonSalir;

     |ESTADO_CONTROL nGrid, "DISABLE";

     |PULSATECLA;

     #20#0 = enEmpresa;
     #20#1 = 99;
     |LEE 000#20];
     |SI FSalida = 0;
         |LEE 000#20a;
     |SINO;
         |LEE 000#20u;
     |FINSI;

     |DDEFECTO #622;
     #622#0 = enEmpresa;
     #622#1 = 1;
     |SI FSalida = 0;  |Y  #20#0 = enEmpresa;
         #622#1 = #20#1 + 1;
     |FINSI;

     |DDEFECTO #20;

     #20#0 = #622#0;
     #20#1 = #622#1;

     enModoReg  = 1;
     |HAZ CargaFantasma;

     |C_M #622#1, 1, "S";
     |ENDATOS #1#622;
     |SI FSalida = 0;
         |HAZ DescargaFantasma;

         |GRABA 020#20n;
         |LIBERA #20;

         |HAZ GrabaLoDemas;
     |FINSI;

     |SI enModo ! 4;
         |ESTADO_CONTROL nBotonAlta, "ENABLE";
     |FINSI;

     |ESTADO_CONTROL nBotonRdto,  "ENABLE";
     |ESTADO_CONTROL nBotonCalc1, "DISABLE";
     |ESTADO_CONTROL nBotonCalc2, "DISABLE";
     |ESTADO_CONTROL nBotonCalc3, "DISABLE";
     |ESTADO_CONTROL nBotonAlta43, "DISABLE";
     |ESTADO_CONTROL nBotonModi43, "DISABLE";  nEBotonModi43 = 0;
     |ESTADO_CONTROL nBotonBaja43, "DISABLE";  nEBotonBaja43 = 0;

     |HAZ PonBotonSalir;

     enModoReg = 4;
     |ESTADO_CONTROL nGrid, "ENABLE";
     |REFRESCACONTROL nGrid;

     |SI #20#0 ! enEmpresa;
         |DDEFECTO #20;
         #20#0 = enEmpresa;
     |FINSI;

     FSalida = 1;  |HAZ Evento20;

     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO Modificar;
     |LEE 101#20=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |ESTADO_CONTROL nBotonAlta,  "DISABLE";
     |ESTADO_CONTROL nBotonModi,  "DISABLE";
     |ESTADO_CONTROL nBotonBaja,  "DISABLE";
     |ESTADO_CONTROL nBotonRdto,  "DISABLE";
     |ESTADO_CONTROL nBotonCalc1, "ENABLE";
     |ESTADO_CONTROL nBotonCalc2, "ENABLE";
     |ESTADO_CONTROL nBotonCalc3, "ENABLE";
     |ESTADO_CONTROL nBotonAlta43, "ENABLE";

     |HAZ QuitaBotonSalir;

     |ESTADO_CONTROL nGrid, "DISABLE";

     enModoReg = 2;
     |C_M #622#1, 1, "N";

     |HAZ CargaFantasma;
     |ENDATOS #2#622;
     |SI FSalida = 0;
         |HAZ DescargaFantasma;

         |GRABA 020#20a;
     |FINSI;
     |LIBERA #20;

     |HAZ GrabaLoDemas;

     |ESTADO_CONTROL nBotonAlta, "ENABLE";
     |ESTADO_CONTROL nBotonModi, "ENABLE";
     |ESTADO_CONTROL nBotonBaja, "ENABLE";
     |ESTADO_CONTROL nBotonRdto, "ENABLE";
     |ESTADO_CONTROL nBotonCalc1, "DISABLE";
     |ESTADO_CONTROL nBotonCalc2, "DISABLE";
     |ESTADO_CONTROL nBotonCalc3, "DISABLE";
     |ESTADO_CONTROL nBotonAlta43, "DISABLE";
     |ESTADO_CONTROL nBotonModi43, "DISABLE";  nEBotonModi43 = 0;
     |ESTADO_CONTROL nBotonBaja43, "DISABLE";  nEBotonBaja43 = 0;

     |HAZ PonBotonSalir;

     enModoReg = 4;
     |ESTADO_CONTROL nGrid, "ENABLE";
     |REFRESCACONTROL nGrid;
     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO Baja;
     |CONFI "0000 Confirma la baja del registro seleccionado";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |LEE 101#20=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |BORRA 020#20a;
     |LIBERA #20;

     |BUCLE rem00041;
     |BUCLE rem00059;
     |BUCLE rem00043B;

     eaDeDonde  = "C0";
     eaLinDonde = #20#1;
     eaLetra    = #20#11;
     |HAZ BorraPatrimonio;

     enSwRetorno = 1;

     |ESTADO_CONTROL nBotonModi,  "DISABLE";
     |ESTADO_CONTROL nBotonBaja,  "DISABLE";

     |REFRESCACONTROL nGrid;
     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO BotonCntr;
     enComunes   = 1;
     enChequeo   = 0;

     #rew00003#0 = #622#2;
     #rew00003#1 = #622#3;
     #rew00003#2 = #622#4;
     #rew00003#3 = #622#5;
     #rew00003#4 = #622#6;
     #rew00003#5 = #622#7;
     #rew00003#6 = #622#8;
     #rew00003#7 = #622#9;
     #rew00003#8 = #622#10;

     |HAZ PantaREW00003;

     #622#2  = #rew00003#0;    |PINTA #622#2;
     #622#3  = #rew00003#1;
     #622#4  = #rew00003#2;
     #622#5  = #rew00003#3;
     #622#6  = #rew00003#4;
     #622#7  = #rew00003#5;
     #622#8  = #rew00003#6;
     #622#9  = #rew00003#7;
     #622#10 = #rew00003#8;

     aAlfa = "|C013WID";
     aID   = #622aAlfa;
     nID   = aID;
     |FOCOTECLADO nID;

     |SI #622#3 = "X";  |Y enModoReg ! 4;
         |PTEC 806;
     |FINSI;
|FPRC;

|PROCESO Tipo7C2Fw622; |TIPO  7;
     aAlfa = #622#2;  |QBF aAlfa;
     |SI aAlfa ! "";  |Y #622#3 ! "X";
         |ACABA;
     |FINSI;

     |HAZ BotonCntr;
|FPRC;

|PROCESO BotonCalc;
     |SI enModoReg = 4;  |ACABA;  |FINSI;

     |DDEFECTO #rew00005;

     |SI FSalida = nBotonCalc1; nCampo = 15;  |FINSI;
     |SI FSalida = nBotonCalc2; nCampo = 19;  |FINSI;
     |SI FSalida = nBotonCalc3; nCampo = 27;  |FINSI;

     #rew00005#1  = #622nCampo;
     #rew00005#20 = #622nCampo;

     |HAZ PantaREW00005;

     #622nCampo = #rew00005#20;  |PINTA #622nCampo;

     aAlfa = "|C" + (("000" + nCampo) % -103) + "WID";
     aID   = #622aAlfa;
     nID   = aID;
     |FOCOTECLADO nID;
|FPRC;

|PROCESO Rendimientos;
     |SI enModoReg ! 4;  |ACABA;  |FINSI;

     |HAZ QuitaBotonSalir;

     |SUB_EJECUTA_NP "rew00551";

     |HAZ PonBotonSalir;

     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO PintaDatosPantalla;
     |DDEFECTO #622;
     |PARA nCampo = 0;  |SI nCampo [ nNumCampos;  |HACIENDO nCampo = nCampo + 1;
           #622nCampo = #20nCampo;

           |C_V #622nCampo, 0, aAlfa;
           |SI aAlfa = "S";  |PINTA #622nCampo;  |FINSI;

           |C_M #622nCampo, 1, "N";
     |SIGUE;

     |HAZ CreaLineal43;
|FPRC;

|PROCESO Evento20;
     |SI enModoReg ! 4;  |ACABA;  |FINSI;

     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#20=;

         |SI enModo = 4;
             |ESTADO_CONTROL nBotonAlta, "DISABLE";
             |ESTADO_CONTROL nBotonModi, "DISABLE";
             |ESTADO_CONTROL nBotonBaja, "DISABLE";
         |SINO;
             |ESTADO_CONTROL nBotonModi, "ENABLE";
             |ESTADO_CONTROL nBotonBaja, "ENABLE";
         |FINSI;

         |PULSATECLA;

         enModoReg = 4;
         |HAZ PintaDatosPantalla;
     |FINSI;

     |SI FSalida = 4;
         |SI enModo = 4;  |ACABA;  |FINSI;

         |HAZ Modificar;
     |FINSI;
|FPRC;

|PROCESO BotonesPanta;
     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}&Alta ", 1564, 1574, Alta;
     nBotonAlta = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}&Modificar ", 1576, 1586, Modificar;
     nBotonModi = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{199}}&Borrar", 1588, 1598, Baja;
     nBotonBaja = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}&Rendimientos", 0580, 0598, Rendimientos;
     nBotonRdto = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}} &Y", 1540, 1544, BotonCntr;
     nBotonCntr = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,&1", 1849, 1850, BotonCalc;
     nBotonCalc1 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,&2", 2314, 2315, BotonCalc;
     nBotonCalc2 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,&3", 2375, 2376, BotonCalc;
     nBotonCalc3 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}al&Ta ", 3564, 3574, Alta43;
     nBotonAlta43 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}mo&Dificar ", 3576, 3586, Modificar43;
     nBotonModi43 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{199}}b&Orrar", 3588, 3598, Baja43;
     nBotonBaja43 = FSalida;

     |ESTADO_CONTROL nBotonModi,   "DISABLE";
     |ESTADO_CONTROL nBotonBaja,   "DISABLE";
     |ESTADO_CONTROL nBotonCalc1,  "DISABLE";
     |ESTADO_CONTROL nBotonCalc2,  "DISABLE";
     |ESTADO_CONTROL nBotonCalc3,  "DISABLE";
     |ESTADO_CONTROL nBotonAlta43, "DISABLE";
     |ESTADO_CONTROL nBotonModi43, "DISABLE";  nEBotonModi43 = 0;
     |ESTADO_CONTROL nBotonBaja43, "DISABLE";  nEBotonBaja43 = 0;

     |SI enModo = 4;
         |ESTADO_CONTROL nBotonAlta, "DISABLE";
     |FINSI;
|FPRC;

|PROCESO Baja20;
     #20#0 = enEmpresa;
     #20#1 = 1;
|FPRC;

|PROCESO Alta20;
     #20#0 = enEmpresa;
     #20#1 = 99;
|FPRC;

|PROGRAMA;
     |HAZ CargaColores;

     enSwRetorno = 0;
     aQueQuiero  = "|NC";
     aNumCampos  = #20#aQueQuiero#;
     nNumCampos  = aNumCampos;
     nNumCampos  = nNumCampos - 1;
     nRango      = 4 + 8 + 16 + 32 + 128;
     enModoReg   = 4;

     |PINPA #0#622;  nPanta = FSalida;

     |HAZ BotonesPanta;

     |DDEFECTO #20;
     |HAZ PintaDatosPantalla;

     |ABRET #20;
     |ABRET #43;

     |LINEAL_SIMPLE #1#20, nRango, 0602, 1498, Baja20, Alta20, Evento20;
     nGrid = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::{{001}}sa&Lir," + nGrid;
          |LEETECLA aTecla;
          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
          |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |CIERRAT #20;
     |CIERRAT #43;

     eaPrograma   = "rem00022";
     eaPunto      = "  ";
     |HAZ ProgramaActivo;
|FPRO;
