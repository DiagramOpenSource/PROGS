|FICHEROS;
     rez00001 #0;
     refere1@ #999;
|FIN;

|VARIABLES;
     nHandle         = 0;
     nCampo          = 0;
     nDTexto         = 0;
     nHTexto         = 0;

     aExtension      = "";
     aAplicacion     = "";
     aNombAplica     = "";
     aPathFich       = "";
     aPathDef        = "";
     aDirecBuscar    = "";
     aDirPatrones    = "";
     aFicheroOrigen  = "";
     aQueQuiero      = "";
     aTitulo         = "";
     aDef            = "";
     aFich           = "";
     aInstalar       = "";
     aOrigen         = "";
     aDestino        = "";
     aFichero        = "";
     aAlfa           = "";
     aBase           = "";
     aParametro      = "";
|FIN;

|PROCESO Tipo11;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     |LEE 101#0=;
     |SI #0#4 = "N";
         #0#4 = "S";
     |SINO;
         #0#4 = "N";
     |FINSI;

     |PINTA #0#4;

     |GRABA 020#0a;
     |LIBERA #0;
|FPRC;

|PROCESO GrabaLineal;
     |SI aExtension ! "tgz";
         aDef = aPathDef + (aFicheroOrigen % 108) + ".mas";  |QBT aDef;
         |CARGA_DEF aDef, refere1@;
         |SI FSalida ! 0;  |ACABA;  |FINSI;

         aQueQuiero = "|TITULO";
         aTitulo    = #999#aQueQuiero#;

         |DESCARGA_DEF #refere1@;

         aInstalar = "S";
         aFich     = aPathFich + (aFicheroOrigen % 108) + ".dat";  |QBT aFich;
         |XABRE "E", aFich, nHandle;
         |SI FSalida ] 0;  aInstalar = "N";  |FINSI;

         |DDEFECTO #0;
         #0#0 = (aFicheroOrigen % -103) < "";
         #0#1 = (aFicheroOrigen % 108) < "";
         #0#2 = aTitulo > "";
         #0#3 = aNombAplica;
         #0#4 = aInstalar;
         |SI aParametro ! "";  #0#4 = "S";  |FINSI;
         |GRABA 020#0n;
      |SINO;
         aInstalar = "S";
         aFich = aBase + "word/wcompro0.doc";
         |XABRE "E", aFich, nHandle;
         |SI FSalida ] 0;  aInstalar = "N";  |FINSI;

         |DDEFECTO #0;
         #0#0 = "tgz";
         #0#1 = "wpatron";
         #0#2 = "DOCUMENTOS EN WORD";
         #0#3 = aNombAplica;
         #0#4 = aInstalar;
         |SI aParametro ! "";  #0#4 = "S";  |FINSI;
         |GRABA 020#0n;
     |FINSI;
|FPRC;

|PROCESO CogeFicheros;
     aDirecBuscar = aDirPatrones + "*." + aExtension;
     |_PDIR aDirecBuscar;
     |PARA; |SI FSalida = 0; |HACIENDO;
            aFicheroOrigen = aDirecBuscar - aDirPatrones;

            |HAZ GrabaLineal;

            |_SDIR aDirecBuscar;
     |SIGUE;
|FPRC;

|PROCESO CopiaFicheros;
     aFichero = #0#1 + "." + "ddx";  |QBT aFichero;
     aDestino = aPathFich + aFichero;
     |FBORRA aDestino;

     aFichero = #0#1 + "." + "ixx";  |QBT aFichero;
     aDestino = aPathFich + aFichero;
     |FBORRA aDestino;

     aFichero = #0#1 + "." + #0#0;   |QBT aFichero;
     aOrigen  = aDirPatrones + aFichero;

     aFichero = #0#1 + "." + "dat";  |QBT aFichero;
     aDestino = aPathFich    + aFichero;
     |COPIA_FICHERO aOrigen, aDestino;

     aDef = aPathDef + #0#1 + ".mas";  |QBT aDef;
     |CARGA_DEF aDef, refere1@;
     |SI FSalida ! 0;
         aAlfa = "     Error en Cargar el Def " + aDef;
         |MENAV aAlfa;
         |ACABA;
     |FINSI;

     |PATH_DAT #999 aPathFich;

     |ABRE #999;   |CIERRA #999;

     |DESCARGA_DEF #refere1@;
|FPRC;

|PROCESO Instalar;
     |SI #0#0 = "ddd";
         |HAZ CopiaFicheros;
     |FINSI;

     |SI #0#0 = "tgz";
         |SUB_EJECUTA_NP "rez00011";
     |FINSI;
|FPRC;

|DEFBUCLE dspatron;
     #0#1;
     4;
     "   ", "        ", "S";
     "zzz", "zzzzzzzz", "S";
     ;
     NULL, Instalar;
|FIN;

|RUTINA ClaveBaja0;
     #0#0 = "   ";
     #0#1 = "        ";
|FRUT;

|RUTINA ClaveAlta0;
     #0#0 = "zzz";
     #0#1 = "zzzzzzzz";
|FRUT;

|PROGRAMA;
     aParametro = PARAMETRO;  |QBT aParametro;

     |MENSA "     Gestionando Ficheros";

     |ABRE #0;

     |DBASE aBase;      |QBT aBase;
     |DFICE aPathFich;  |QBF aPathFich;
     aPathDef     = aBase + "def/";
     aDirPatrones = aBase + "patrones/";

     aExtension  = "ddd";
     aAplicacion = "agiren02";
     aNombAplica = "RENTA 2002";
     |HAZ CogeFicheros;

     aExtension  = "tgz";
     aAplicacion = "agiren02";
     aNombAplica = "RENTA 2002";
     |HAZ CogeFicheros;

     |CIERRA #0;

     |SI aParametro = "";
         |ENTLINEAL #1#0, -2, 4, 22, 1, ClaveBaja0, ClaveAlta0;

         |CONFI "0000SDesea Realizar el pase en estos momentos : ";
         |SI FSalida = 0;
             |BUCLE dspatron;
         |FINSI;
     |SINO;
         |BUCLE dspatron;
     |FINSI;

     |DELINDEX #0;
|FPRO;
