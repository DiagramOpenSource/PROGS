|FICHEROS;
     rem00044 #20;
     rem00021 #6;
     rem00007 #7,MANTE;
     rem00008 #8;
     rem00100 #100;
     rem00101 #101;
     retabla1 #401;
     espejo@ #99;
|FIN;

|VARIABLES;
     aAlfa         = "";
     aAlfa1        = "";
     aLong         = "";
     aQueQuiero    = "";
     aNumCampos    = "";
     aNumPanta     = "";
     aPathRenta    = "";
     aDef          = "";
     aPerceptor    = "";
     aEdad         = "";
     aEdadPer      = "";
     aAnyoAdop     = "";

     nLong         = 0;
     nCaracter     = 0;
     nPosi         = 0;
     nPanta        = 0;
     nNumCampos    = 0;
     nNumPanta     = 0;
     nCampo        = 0;
     nModo         = 0;
     nEntrada      = 0;
     nComunidad    = 0;
     nTipo         = 0;
     nLinea        = 0;
     nNumero       = 0;
     nInversion    = 0;
     nDeduccion    = 0;
     nHijosAnt     = 0;
     nHijosDed     = 0;
     nHijosDed1    = 0;
     nHijosDed2    = 0;
     nAscend       = 0;
     nAscend70     = 0;
     nMinusv       = 0;
     nMinusv65     = 0;
     nMinusD       = 0;
     nMinusD65     = 0;
     nEdadMayor    = 0;
     nTotalHijos   = 0;
     nHijos        = 0;
     nHijosTres    = 0;
     nHijosTres1   = 0;
     nHijosTres2   = 0;
     nCantidad     = 0;
     nPorce        = 0;
     nSwMensa      = 0;

     &enEmpresa    = 0;
     &enModo       = 0;
     &eaDeclarante = "";
     &enComunes    = 0;
     &enMinusvalia = 0;
     &eaMinuAyuda  = "";
     &eaFallece    = "";
     &eaSwVarios   = "";
     &eaVarDni     = "";
     &enSwLiquida  = 0;
     &enSwRetorno  = 0;
     &eaPrograma   = "";
     &eaPunto      = "";
     &enAutonomia  = 0;
|FIN;

|| ************************************************************************
|| PROCESOS DE CONSULTA DE PANTALLAS NORMALES
|| ************************************************************************

|PROCESO GrabaLinea;
     |ABRE #7;
     #7#0 = nComunidad;
     |LEE 040#7=;
     |SI FSalida ! 0;  |CIERRA #7;  |ACABA;  |FINSI;
     |CIERRA #7;

     |ABRE #8;
     #8#0 = nComunidad;
     #8#1 = nTipo;
     |LEE 040#8=;
     |SI FSalida ! 0;  |CIERRA #8;  |ACABA;  |FINSI;
     |CIERRA #8;

     |DDEFECTO #20;
     nLinea = nLinea + 1;
     #20#0   = enEmpresa;
     #20#1   = nLinea;
     #20#2   = aPerceptor;
     #20#3   = "A";
     #20#11  = #7#0;
     #20#12  = #7#1;
     #20#13  = #8#1;
     #20#14  = #8#2;
     #20#15  = #8#3;
     #20#16  = #8#4;
     #20#17  = nNumero;
     #20#18  = nInversion;
     #20#19  = nDeduccion;

     |GRABA 020#20n;
     |LIBERA #20;
|FPRC;

|PROCESO CalcuHijos;
     |SI #101#4 = "DESCENDIENTE";
         |SI #101#7 = " ";
             nTotalHijos = nTotalHijos + 1;
         |FINSI;

         |SI #101#7 = "1";  |Y aPerceptor = "TITULAR";
             nTotalHijos = nTotalHijos + 1;
         |FINSI;

         |SI #101#7 = "2";  |Y aPerceptor = "CONYUGE";
             nTotalHijos = nTotalHijos + 1;
         |FINSI;

         |SI #101#5 < "2002"; |Y #101#4 ! "    ";  |Y #101#4 ! "0000";
             |SI #101#7 = " ";
                 nHijosAnt = nHijosAnt + 1;
             |FINSI;
             |SI #101#7 = "1";  |Y aPerceptor = "TITULAR";
                 nHijosAnt = nHijosAnt + 1;
             |FINSI;
             |SI #101#7 = "2";  |Y aPerceptor = "CONYUGE";
                 nHijosAnt = nHijosAnt + 1;
             |FINSI;
         |FINSI;

         aAnyoAdop = #101#18 % -104;
         |SI #101#5 = "2002";  |O aAnyoAdop = "2002";
             |SI #101#7 = " ";
                 nHijosDed  = nHijosDed  + 1;
                 nHijosDed1 = nHijosDed1 + 1;
             |FINSI;
             |SI #101#7 = "1";  |Y aPerceptor = "TITULAR";
                 nHijosDed  = nHijosDed  + 1;
                 nHijosDed2 = nHijosDed2 + 1;
             |FINSI;
             |SI #101#7 = "2";  |Y aPerceptor = "CONYUGE";
                 nHijosDed  = nHijosDed  + 1;
                 nHijosDed2 = nHijosDed2 + 1;
             |FINSI;
         |FINSI;

         |SI #101#5 ] "2000";
             |SI #101#7 = " ";
                 |SI #101#6 < 65;
                     nHijosTres  = nHijosTres  + 1;
                     nHijosTres1 = nHijosTres1 + 1;
                 |FINSI;
             |FINSI;
             |SI #101#7 = "1";  |Y aPerceptor = "TITULAR";
                 |SI #101#6 < 65;
                     nHijosTres  = nHijosTres  + 1;
                     nHijosTres2 = nHijosTres2 + 1;
                 |FINSI;
             |FINSI;
             |SI #101#7 = "2";  |Y aPerceptor = "CONYUGE";
                 |SI #101#6 < 65;
                     nHijosTres  = nHijosTres  + 1;
                     nHijosTres2 = nHijosTres2 + 1;
                 |FINSI;
             |FINSI;
         |FINSI;
     |FINSI;

     |SI #101#4 = "ASCENDIENTE ";
         |SI #101#5 [ aEdad;  |Y #101#5 ! "    ";  |Y #101#5 ! "0000";
             |SI #101#7 = " ";
                 nAscend = nAscend + 1;
                 |SI #101#6 < 65;
                     nAscend70  = nAscend70  + 1;
                 |FINSI;
             |FINSI;
             |SI #101#7 = "1";  |Y aPerceptor = "TITULAR";
                 nAscend = nAscend + 1;
                 |SI #101#6 < 65;
                     nAscend70  = nAscend70  + 1;
                 |FINSI;
             |FINSI;
             |SI #101#7 = "2";  |Y aPerceptor = "CONYUGE";
                 nAscend = nAscend + 1;
                 |SI #101#6 < 65;
                     nAscend70  = nAscend70  + 1;
                 |FINSI;
             |FINSI;
         |FINSI;
     |FINSI;

     |SI #101#6 ] 33;
         |SI #101#7 = " ";
             nMinusv  = nMinusv  + 1;
         |FINSI;

         |SI #101#7 = "1";  |Y aPerceptor = "TITULAR";
             nMinusv  = nMinusv  + 1;
         |FINSI;
         |SI #101#7 = "2";  |Y aPerceptor = "CONYUGE";
             nMinusv  = nMinusv  + 1;
         |FINSI;
     |FINSI;

     |SI #101#6 ] 65;
         |SI #101#7 = " ";
             nMinusv65  = nMinusv65  + 1;
         |FINSI;

         |SI #101#7 = "1";  |Y aPerceptor = "TITULAR";
             nMinusv65  = nMinusv65  + 1;
         |FINSI;

         |SI #101#7 = "2";  |Y aPerceptor = "CONYUGE";
             nMinusv65  = nMinusv65 + 1;
         |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE rem00101;
     #101#1;
     ;
     #20#0, 01;
     #20#0, 99;
     ;
     NULL, CalcuHijos;
|FIN;

|PROCESO LeeFamiliar;
     nTotalHijos = 0;
     nHijosAnt   = 0;   || Numero de Hijos nacidos antes de este ejercicio
     nHijosDed   = 0;   || Numero de Hijos nacidos en este ejercicio
     nHijosDed1  = 0;   || Numero de Hijos nacidos en este ejercicio
     nHijosDed2  = 0;   || Numero de Hijos nacidos en este ejercicio
     nHijosTres  = 0;   || Numero de Hijos menores de Tres a¤os
     nHijosTres1 = 0;   || Numero de Hijos menores de Tres a¤os
     nHijosTres2 = 0;   || Numero de Hijos menores de Tres a¤os
     nEdadMayor  = 0;   || Sujetos Pasivos de ....A¤os o mas
     nAscend     = 0;   || Ascendientes de ... A¤os  o mas
     nAscend70   = 0;   || Ascendientes de ... A¤os  o mas
     nMinusv     = 0;   || Minusvalidos
     nMinusv65   = 0;   || Minusvalidos
     nMinusD     = 0;   || Minusvalidos
     nMinusD65   = 0;   || Minusvalidos

     |BUCLE rem00101;

     |SI aPerceptor = "TITULAR";
         |SI #100#8 ] 33;                           nMinusv    = nMinusv + 1; |FINSI;
         |SI #100#8 ] 33;                           nMinusD    = 1;           |FINSI;
         |SI #100#8 ] 65;                           nMinusD65  = 1;           |FINSI;
         |SI #100#7 [ aEdad;  |Y #100#7  ! "    ";  |Y #100#7 ! "0000";
             nEdadMayor = 1;
         |FINSI;
     |FINSI;

     |SI aPerceptor = "CONYUGE";
         |SI #100#41 ] 33;                           nMinusv = nMinusv + 1; |FINSI;
         |SI #100#41 ] 33;                           nMinusD    = 1;        |FINSI;
         |SI #100#41 ] 65;                           nMinusD65  = 1;        |FINSI;
         |SI #100#40 [ aEdad;  |Y #100#40 ! "    ";  |Y #100#40 ! "0000";
             nEdadMayor = 1;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO MiraVivienda;
     |SI #6#2 = "TITULAR   ";  |Y aPerceptor = "CONYUGE";  |ACABA;  |FINSI;
     |SI #6#2 = "CONYUGE   ";  |Y aPerceptor = "TITULAR";  |ACABA;  |FINSI;

     |SI #100#11 = 13;
         nCantidad    = #6#23 + #6#29;   || Murcia
     |SINO;
         nCantidad    = #6#23 - #6#22;   || Valencia
     |FINSI;

     |SI #6#2 = "TITULAR   ";  nPorce = 100;  |FINSI;
     |SI #6#2 = "CONYUGE   ";  nPorce = 100;  |FINSI;
     |SI #6#2 = "GANANCIAL ";  nPorce = 50;   |FINSI;
     |SI #6#2 = "VARIOS    ";
         |SI aPerceptor = "TITULAR";  nPorce = #6#5;  |FINSI;
         |SI aPerceptor = "CONYUGE";  nPorce = #6#6;  |FINSI;
     |FINSI;
     |ERROR10;
|FPRC;

|DEFBUCLE rem00021;
     #6#1;
     ;
     enEmpresa, 01;
     enEmpresa, 99;
     ;
     NULL, MiraVivienda;
|FIN;

|PROCESO Aragon;
     |HAZ LeeFamiliar;

     |SI nHijosDed ! 0;  |Y nTotalHijos > 2;
         nHijos      = nTotalHijos - 2;
         |SI nHijosDed > nHijos;  nHijosDed = nHijos;    |FINSI;

         nNumero     = nHijosDed;

         nTipo       = 149;
         nDeduccion  = nNumero * #401#92;
         nInversion  = 0;
         |HAZ GrabaLinea;

         |SI nHijosDed2 ! 0;  nSwMensa = 1;  |FINSI;
     |FINSI;
|FPRC;

|PROCESO Baleares;
     aEdad = "1937";
     |HAZ LeeFamiliar;

     |SI nEdadMayor > 0;
         nTipo       = 150;
         nNumero     = nEdadMayor;
         nInversion  = 0;
         nDeduccion  = nNumero * #401#39;
         |HAZ GrabaLinea;
     |FINSI;

     |SI nMinusv > 0;
         nTipo       = 151;
         nNumero     = nMinusv;
         nDeduccion  = nNumero * #401#40;
         nInversion  = 0;
         |HAZ GrabaLinea;
     |FINSI;
|FPRC;

|PROCESO CastillaLaMancha;
     aEdad = "1932";
     |HAZ LeeFamiliar;
     |SI nHijosTres ! 0;
         nTipo       = 156;
         nNumero     = nHijosTres;
         nInversion  = 0;
         nDeduccion  = (nHijosTres1 * #401#97) + (nHijosTres2 * (#401#97 * 2));

         |HAZ GrabaLinea;
     |FINSI;

     |SI nAscend70 ! 0;
         nTipo       = 157;
         nNumero     = nAscend70;
         nInversion  = 0;
         nDeduccion  = nNumero * (#401#100 * 2);

         |HAZ GrabaLinea;
     |FINSI;

     |SI nMinusv65 ! 0;
         nTipo       = 158;
         nNumero     = nMinusv65;
         nInversion  = 0;
         nDeduccion  = nNumero * #401#103;

         |HAZ GrabaLinea;
     |FINSI;

     |SI nMinusD65 ! 0;
         nTipo       = 159;
         nNumero     = nMinusD65;
         nInversion  = 0;
         nDeduccion  = nNumero * #401#106;

         |HAZ GrabaLinea;
     |FINSI;
|FPRC;

|PROCESO CastillaLeon;
     aEdad = "1937";
     |HAZ LeeFamiliar;

     |SI nTotalHijos ] 3;
         nTipo       = 162;
         nNumero     = 0;
         |SI nTotalHijos ] 4;
             nNumero     = nTotalHijos - 3;
         |FINSI;
         nInversion  = 0;
         |SI nMinusv = 0;
             nDeduccion  = #401#41 + (nNumero * #401#42);
         |SINO;
             nDeduccion  = #401#109 + (nNumero * #401#42);
         |FINSI;

         |HAZ GrabaLinea;

         |SI nHijosDed2 ! 0;  nSwMensa = 1;  |FINSI;
     |FINSI;

     |SI nHijosDed > 0;
         nTipo       = 163;
         nNumero     = nHijosDed;
         nInversion  = 0;
         nDeduccion  = 0;
         |SI nNumero = 1;  nDeduccion = #401#43;             |FINSI;
         |SI nNumero = 2;  nDeduccion = #401#43 + #401#44;   |FINSI;
         |SI nNumero ] 3;
             nDeduccion = (#401#43 + #401#44) + ((nNumero - 2) * #401#45);
         |FINSI;
         |HAZ GrabaLinea;

         |SI nHijosDed2 ! 0;  nSwMensa = 1;  |FINSI;
     |FINSI;
|FPRC;

|PROCESO Catalunya;
     aEdad = "1937";
     |HAZ LeeFamiliar;

     |SI nHijosDed ! 0;  |Y nTotalHijos > 1;
         nHijos  = nTotalHijos - 1;
         |SI nHijosDed > nHijos;  nHijosDed = nHijos;    |FINSI;
         nNumero     = nHijosDed;

         nTipo       = 166;
         nDeduccion  = nNumero * #401#46;
         nInversion  = 0;
         |HAZ GrabaLinea;
     |FINSI;
|FPRC;

|PROCESO Extremadura;
     nCantidad = 0;
     nPorce    = 100;
     |BUCLE rem00021;
     |SI nCantidad ! 0;  |Y aEdadPer ] "1967";
         nTipo       = 168;
         nNumero     = 0;
         nInversion  = nCantidad % nPorce;
         |SI nInversion > #401#110;
             nInversion = #401#110;
         |FINSI;
         nDeduccion  = nInversion % 3;
         |HAZ GrabaLinea;
     |FINSI;
|FPRC;

|PROCESO Galicia;
     aEdad = "1937";
     |HAZ LeeFamiliar;

     |SI nHijosDed > 0;
         nTipo       = 172;
         nNumero     = nHijosDed;
         nInversion  = 0;
         nDeduccion  = 0;
         |SI nNumero = 1;  nDeduccion = #401#47;                                 |FINSI;
         |SI nNumero = 2;  nDeduccion = #401#47 + #401#48;                       |FINSI;
         |SI nNumero = 3;  nDeduccion = #401#47 + #401#48 + #401#49;             |FINSI;
         |SI nNumero = 4;  nDeduccion = #401#47 + #401#48 + #401#49 + #401#50;   |FINSI;
         |SI nNumero ] 5;
             nDeduccion = #401#47 + #401#48 + #401#49 + #401#50;
             nDeduccion = nDeduccion + ((nNumero - 5) * #401#51);
         |FINSI;

         |HAZ GrabaLinea;

         |SI nHijosDed2 ! 0;  nSwMensa = 1;  |FINSI;
     |FINSI;
|FPRC;

|PROCESO Madrid;
     aEdad = "1937";
     |HAZ LeeFamiliar;

     |SI nHijosDed > 0;
         nTipo       = 175;
         nNumero     = nHijosDed;
         nInversion  = 0;
         nDeduccion  = (nHijosDed1 * #401#52) + (nHijosDed2 * (#401#52 * 2));
         |HAZ GrabaLinea;
     |FINSI;
|FPRC;

|PROCESO Murcia;
     aEdad = "1937";
     |HAZ LeeFamiliar;

     nCantidad = 0;
     nPorce    = 100;
     |BUCLE rem00021;
     |SI nCantidad ! 0;  |Y aEdadPer ] "1972";
         nTipo       = 180;
         nNumero     = 0;
         nInversion  = nCantidad % nPorce;
         nDeduccion  = nInversion % 3;
         |HAZ GrabaLinea;
     |FINSI;
|FPRC;

|PROCESO LaRioja;
     |HAZ LeeFamiliar;

     |SI nTotalHijos [ 1;  |ACABA;  |FINSI;

     |SI nHijosDed = nTotalHijos;
         nHijosDed = nHijosDed - 1;
     |FINSI;

     |SI nHijosDed [ 0;  |ACABA;  |FINSI;

     nTipo       = 183;
     nNumero     = nHijosDed;
     nInversion  = 0;
     nDeduccion  = 0;
     |SI nNumero = 1;  nDeduccion = #401#117;   |FINSI;
     |SI nNumero ] 2;
         nDeduccion = #401#117 + ((nNumero - 1) * #401#118);
     |FINSI;

     |HAZ GrabaLinea;

     |SI nHijosDed2 ! 0;  nSwMensa = 1;  |FINSI;
|FPRC;

|PROCESO Valencia;
     aEdad = "1937";
     |HAZ LeeFamiliar;

     |SI nHijosDed ! 0;  |Y nTotalHijos > 2;
         nHijos  = nTotalHijos - 2;
         |SI nHijosDed > nHijos;  nHijosDed = nHijos;    |FINSI;
         nNumero     = nHijosDed;
         nTipo       = 186;
         nDeduccion  = nNumero * #401#54;
         nInversion  = 0;
         |HAZ GrabaLinea;
     |FINSI;

     |SI nEdadMayor > 0; |Y nMinusD > 0;
         nTipo       = 188;
         nNumero     = nEdadMayor;
         nInversion  = 0;
         nDeduccion  = nNumero * #401#55;
         |HAZ GrabaLinea;
     |FINSI;

     nCantidad = 0;
     nPorce    = 100;
     |BUCLE rem00021;
     |SI nCantidad ! 0;  |Y aEdadPer ] "1967";
         nTipo       = 189;
         nNumero     = 0;
         nInversion  = nCantidad % nPorce;
         nDeduccion  = nInversion % 3;
         |HAZ GrabaLinea;
     |FINSI;
|FPRC;

|PROCESO Tipo11;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     #20#0 = enEmpresa;
     #20#1 = 1;
     |LEE 040#20];
     |SI FSalida = 0;  |Y #20#0 = enEmpresa;
         |MENAV "      Ya existen deducciones en esta empresa, debe estar sin deducciones.";
         |ERROR;
         |ACABA;
     |FINSI;

     nLinea = 0;

     #20#0 = enEmpresa;

     |ABRE #100;
     #100#0 = enEmpresa;
     |LEE 040#100=;
     |SI FSalida ! 0;  |DDEFECTO #100;  |FINSI;
     |CIERRA #100;

     aPerceptor = "TITULAR";
     nComunidad = #100#11;
     aEdadPer   = #100#7;
     nSwMensa   = 0;

     |SI #100#11 = 2;   |HAZ Aragon;            |FINSI;
     |SI #100#11 = 4;   |HAZ Baleares;          |FINSI;
     |SI #100#11 = 7;   |HAZ CastillaLaMancha;  |FINSI;
     |SI #100#11 = 8;   |HAZ CastillaLeon;      |FINSI;
     |SI #100#11 = 9;   |HAZ Catalunya;         |FINSI;
     |SI #100#11 = 10;  |HAZ Extremadura;       |FINSI;
     |SI #100#11 = 11;  |HAZ Galicia;           |FINSI;
     |SI #100#11 = 12;  |HAZ Madrid;            |FINSI;
     |SI #100#11 = 13;  |HAZ Murcia;            |FINSI;
     |SI #100#43 = 16;  |HAZ LaRioja;           |FINSI;
     |SI #100#11 = 17;  |HAZ Valencia;          |FINSI;

     aPerceptor = "CONYUGE";
     nComunidad = #100#43;
     aEdadPer   = #100#40;

     |SI #100#11 = 2;   |HAZ Aragon;            |FINSI;
     |SI #100#43 = 4;   |HAZ Baleares;          |FINSI;
     |SI #100#11 = 7;   |HAZ CastillaLaMancha;  |FINSI;
     |SI #100#43 = 8;   |HAZ CastillaLeon;      |FINSI;
     |SI #100#43 = 9;   |HAZ Catalunya;         |FINSI;
     |SI #100#11 = 10;  |HAZ Extremadura;       |FINSI;
     |SI #100#43 = 11;  |HAZ Galicia;           |FINSI;
     |SI #100#43 = 12;  |HAZ Madrid;            |FINSI;
     |SI #100#43 = 13;  |HAZ Murcia;            |FINSI;
     |SI #100#43 = 16;  |HAZ LaRioja;           |FINSI;
     |SI #100#43 = 17;  |HAZ Valencia;          |FINSI;

     |SI nSwMensa = 1;
         |MENAV "     Revise Deduccion de Hijos ya que tiene vinculaciones al Declarante o al Conyuge";
     |FINSI;

     |ERROR;
     |PTEC 802;
     |PTEC 807;
|FPRC;

|RUTINA ClaveBaja8;
     |SI FSalida = "POSICION";  |Y #20#13 ! 999;
         #8#1 = #20#13;
         |ACABA;
     |FINSI;
     #8#0 = #20#11;
     #8#1 = 149;
|FRUT;

|RUTINA ClaveAlta8;
     #8#0 = #20#11;
     #8#1 = 196;
|FRUT;

|PROCESO Consulta;
     |SI FSalida = 10;
         |PUSHV 0501 2380;
         |PINPA #0#7;
         |PINDA #0#7;
         |ENTLINEAL #1#8, 2, 4, 16, 0, ClaveBaja8, ClaveAlta8;
         #20#13 = #8#1;
         |POPV;
     |FINSI;

     |ABRE #8;
     #8#0 = #20#11;
     #8#1 = #20#13;
     |LEE 040#8=;
     |SI FSalida ! 0;
         |MENAV "    No existe Deduccion.";
         |CIERRA #8;
         |ERROR;
         |ACABA;
     |FINSI;
     |CIERRA #8;

     |C_M #20#17, 1, "S";
     |C_M #20#18, 1, "S";
     |C_M #20#19, 1, "S";
     |C_M #20#20, 1, "S";

     |SI #8#5 = "S";
         |C_M #20#18, 1, "N";   #20#18 = 0;
         |C_M #20#20, 1, "N";   #20#20 = 0;
     |FINSI;

     |SI #8#5 = "N";
         |C_M #20#17, 1, "N";   #20#17 = 0;
         |C_M #20#18, 1, "N";   #20#17 = 0;
         |C_M #20#20, 1, "N";   #20#20 = 0;
     |FINSI;

     |SI #8#5 = "I";
         |C_M #20#17, 1, "N";   #20#17 = 0;
         #20#20 = #8#6;
         |SI #20#20 = 0;
             |C_M #20#20, 1, "N";  #20#20 = 0;
             |C_M #20#19, 1, "N";  #20#19 = 0;
         |FINSI;
     |FINSI;

     #20#14 = #8#2;
     #20#15 = #8#3;
     #20#16 = #8#4;

     |PINTA #20#13;
     |PINTA #20#14;
     |PINTA #20#15;
     |PINTA #20#16;
     |PINTA #20#17;
     |PINTA #20#18;
     |PINTA #20#19;
     |PINTA #20#20;
|FPRC;

|PROCESO Totalm020;
     |SI #20#20 ! 0;
         #20#19 = #20#18 % #20#20;
         |SI #8#7 ! 0;
             |SI #20#19 > #8#7;
                 #20#19 = #8#7;
             |FINSI;
        |FINSI;
     |FINSI;
     |PINTA #20#19;
|FPRC;

|PROCESO Varios;  |TIPO 7;
     |C_M #20#5,  1, "N";
     |C_M #20#6,  1, "N";
     |C_M #20#7,  1, "N";
     |C_M #20#8,  1, "N";
     |C_M #20#9,  1, "N";
     |C_M #20#10, 1, "N";

     aAlfa = eaSwVarios;
     aLong = aAlfa % 0;
     nLong = aLong;
     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPosi = (100 * nCaracter) + 1;
           aAlfa1 = aAlfa % nPosi;
           |SI aAlfa1 = "T";  |C_M #20#5,  1, "S";  |FINSI;
           |SI aAlfa1 = "C";  |C_M #20#6,  1, "S";  |FINSI;
           |SI aAlfa1 = "1";  |C_M #20#7,  1, "S";  |FINSI;
           |SI aAlfa1 = "2";  |C_M #20#8,  1, "S";  |FINSI;
           |SI aAlfa1 = "3";  |C_M #20#9,  1, "S";  |FINSI;
           |SI aAlfa1 = "4";  |C_M #20#10, 1, "S";  |FINSI;
     |SIGUE;

     |SI #20#2 ! "VARIOS    ";
         |C_M #20#5,  1, "N";  #20#5  = 0;  |PINTA #20#5;
         |C_M #20#6,  1, "N";  #20#6  = 0;  |PINTA #20#6;
         |C_M #20#7,  1, "N";  #20#7  = 0;  |PINTA #20#7;
         |C_M #20#8,  1, "N";  #20#8  = 0;  |PINTA #20#8;
         |C_M #20#9,  1, "N";  #20#9  = 0;  |PINTA #20#9;
         |C_M #20#10, 1, "N";  #20#10 = 0;  |PINTA #20#10;
     |FINSI;
|FPRC;

|RUTINA ClaveBaja20;
     #20#0 = enEmpresa;
     #20#1 = 1;
|FRUT;

|RUTINA ClaveAlta20;
     #20#0 = enEmpresa;
     #20#1 = 99;
|FRUT;

|PROCESO PonDeclarante;  |TIPO 7;
     eaDeclarante = #20#2;
     enComunes    = 1;
     |HAZ SacaDeclarantes;
     #20#2  = eaDeclarante;
     #20#11 = enAutonomia;
     |PINTA #20#2;

     |ABRE #8;
     #8#0 = #20#11;
     #8#1 = 1;
     |LEE 040#8];
     |SI FSalida ! 0;
         |CIERRA #8;
         |MENAV "     No existe Autonomia en el Declarante.";
         |PTEC 807;
         |ACABA;
     |FINSI;
     |SI #8#1 = 99;
         |CIERRA #8;
         |MENAV "     No hay deducciones Autonomicas en la Comunidad";
         |PTEC 807;
         |ACABA;
     |FINSI;
     |CIERRA #8;

     |ABRE #7;
     #7#0 = #20#11;
     |LEE 040#7=;
     |SI FSalida ! 0;  |DDEFECTO #7;  |FINSI;
     |CIERRA #7;

     #20#12 = #7#1;

     |PINTA #20#11;
     |PINTA #20#12;
|FPRC;

|PROCESO Fallece;  |TIPO 7;
     |SI eaFallece = "S";
         |C_M #20#4, 1, "S";
     |SINO;
         |C_M #20#4, 1, "N";  #20#4 = 0;  |PINTA #20#4;
     |FINSI;
|FPRC;

|PROCESO Tipo2m20;  |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;  |O nModo = 3;
         enSwRetorno  = 1;
         |ACABA;
     |FINSI;

     nEntrada = 0 +. 1;
     |SI nEntrada ! 1;
         |PARA nCampo = 0;  |SI nCampo [ nNumCampos;  |HACIENDO nCampo = nCampo + 1;
               #99nCampo = #20nCampo;
         |SIGUE;
     |SINO;
         |PARA nCampo = 0;  |SI nCampo [ nNumCampos;  |HACIENDO nCampo = nCampo + 1;
               |SI #99nCampo ! #20nCampo;
                   enSwRetorno  = 1;
               |FINSI;
         |SIGUE;
     |FINSI;
|FPRC;

|PROGRAMA;
     |ABRE #401;
     |LEE 040#401p;
     |SI FSalida ! 0;  |DDEFECTO #401;  |FINSI;
     |CIERRA #401;

     aQueQuiero = "|NP";
     aNumPanta  = #20#aQueQuiero#;
     nNumPanta  = aNumPanta;
     nNumPanta  = nNumPanta - 1;

     aQueQuiero = "|NC";
     aNumCampos = #20#aQueQuiero#;
     nNumCampos = aNumCampos;
     nNumCampos = nNumCampos - 1;

     |DBASE aPathRenta;  |QBT aPathRenta;
     aDef = aPathRenta + "def/rem00044.mas";
     |CARGA_DEF aDef, espejo@;
     |SI FSalida ! 0;
         aAlfa = "     Error en cargar el Def rem00044.mas";
         |MENAV aAlfa;
         |ERROR;
     |FINSI;

     |ENTLINEAL #1#20, -2, enModo, 22, 1, ClaveBaja20, ClaveAlta20;

     |DESCARGA_DEF #espejo@;

     eaPrograma   = "rem00044";
     eaPunto      = "  ";
     |HAZ ProgramaActivo;
|FPRO;
