|FICHEROS;
     copidat1 #0;
     referen@;
|FIN;

|VARIABLES;
     aPath = "";
     aAlfa = "";
     aAlfa1 = "";
     nModo = 0;
     aDef = "";
     nNum = 0;
     nCal = 0;
     aClave = "";
     aCamClave = "";
     aLonClave = "";
     nCampo = 0;
     nBtn = -1;
     nError = 0;
     nCampos = 0;
     i = 0;
     j = 0;
     nLon = 0;
     aPathFic = "";
     nPos = 0;
     aTar = "";
     a1 = "";
     a2 = "";
     aEmp = "";
     fHoy = @;
     aFic = "";
     aIP = "";
|FIN;

|PROCESO Destino7; |TIPO 7;
     |ESTADO_CONTROL nBtn, "ENABLE";
|FPRC;

|PROCESO Destino0; |TIPO 0;
     |SI FSalida = 13;
          aAlfa = "D*.*";
          |BROWSE aAlfa;
          aAlfa = aAlfa % 2; |QBF aAlfa;
          |SI aAlfa = "D"; aAlfa = ""; |FINSI;
          |QBF aAlfa;
          |SI aAlfa = "";
               |ERROR;
          |SINO;
               #0#5 = aAlfa;
               |PINTA #0#5;
          |FINSI;
     |FINSI;
     |ESTADO_CONTROL nBtn, "DISABLE";
|FPRC;

|PROCESO F5;
     |PTEC 827;
|FPRC;

|PROCESO Tipo9; |TIPO 9;
     |FIN_CONTROL_WINDOWS nBtn;
|FPRC;

|PROCESO Tipo13; |TIPO 13;
     |SI nBtn = -1;
          |CONTROL_SIMPLE 0, "BOTON,...", 1474, 1477, F5;
          nBtn = FSalida;
          |ESTADO_CONTROL nBtn, "DISABLE";
     |FINSI;
|FPRC;

|PROCESO Conta; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          |LEE 000#0u;
          |SI FSalida = 0;
               nNum = #0#0 + 1;
          |SINO;
               nNum = 1;
          |FINSI;
          |DDEFECTO #0;
          #0#0 = nNum;
     |FINSI;
|FPRC;

|PROCESO Aplica; |TIPO 0;
     |DBASS aPath;
     aPath = aPath + #0#2; |QBF aPath;

     aAlfa = aPath;
     |_PDIR aAlfa;
     |SI FSalida ! 0;
          |MENAV "0000No existe aplicación...";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO DefEmp; |TIPO 0;
     aAlfa = #0#3; |QBF aAlfa;
     |SI aAlfa ! "";
          |C_M #0#4, 1, "S";
          |DBASS aPath;
          aPath = aPath + #0#2; |QBF aPath;
          aPath = aPath + "/def/" + #0#3; |QBF aPath;
          aDef = aPath + ".mas";
          |XABRE "E", aDef, 0;
          |SI FSalida ! 0;
               |MENAV "0000No existe def de empresa...";
               |ERROR;
          |SINO;
               |CARGA_DEF aDef, referen@;
               |SI FSalida ! 0;
                    |MENAV "0000Error al cargar def empresa...";
                    |ERROR;
               |SINO;
                    |DESCARGA_DEF #referen@;
               |FINSI;
          |FINSI;
     |SINO;
          |C_M #0#4, 1, "N";
          #0#4 = 0; |PINTA #0#4;
     |FINSI;
|FPRC;

|PROCESO EmpCop66; |TIPO 66;
     |DBASS aPath;
     aPath = aPath + #0#2; |QBF aPath;
     aDef = aPath + "/def/" + #0#3; |QBF aPath;
     aDef = aDef + ".mas";
     |CARGA_DEF aDef, referen@;
     |SI FSalida ! 0;
          |MENAV "0000Error al cargar def empresa...";
          |ERROR;
     |SINO;
          aAlfa = aPath + "/fich/";
          |PATH_DAT #referen@#aAlfa#;
          |ABRE #referen@;
          aAlfa = "|K000";
          aAlfa = #referen@#aAlfa#;
          aCamClave = aAlfa % -103;
          nCampo = aCamClave;
          #referen@#nCampo# = #0#4;
          |LEE 000#referen@.];
          |CONSULTA_CLAVE #1#referen@;
          |SI FSalida = 0;
               #0#4 = #referen@#nCampo#; |PINTA #0#4;
          |SINO;
               |ERROR;
          |FINSI;
          |CIERRA #referen@;
          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;

|PROCESO EmpCop0; |TIPO 0;
     aAlfa = #0#3; |QBF aAlfa;
     |SI aAlfa ! "";
          |DBASS aPath;
          aPath = aPath + #0#2; |QBF aPath;
          aDef = aPath + "/def/" + #0#3; |QBF aPath;
          aDef = aDef + ".mas";
          |CARGA_DEF aDef, referen@;
          |SI FSalida ! 0;
               |MENAV "0000Error al cargar def empresa...";
               |ERROR;
          |SINO;
               aAlfa = aPath + "/fich/";
               |PATH_DAT #referen@#aAlfa#;
               |ABRE #referen@;
               aAlfa = "|K000";
               aAlfa = #referen@#aAlfa#;
               aCamClave = aAlfa % -103;
               nCampo = aCamClave;
               #referen@#nCampo# = #0#4;
               |LEE 000#referen@.=;
               |SI FSalida ! 0;
                    |MENAV "0000No existe empresa...";
                    |ERROR;
               |FINSI;
               |CIERRA #referen@;
               |DESCARGA_DEF #referen@;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO PideClave0;
     |PUSHV 0501 2380;
     |PINTA 1714, "Introduzca la clave de la empresa:"
     E_inf = "";
     E_sup = nLon;
     aAlfa = "";
     aAlfa = 1749 ? -1;
     |POPV;
     |QBF aAlfa;
|FPRC;

|PROCESO Desencripta;
     aAlfa1 = "";
     aAlfa = aClave; |QBF aAlfa;
     aAlfa = aAlfa % 0; nNum = aAlfa;
     |PARA j = 1; |SI j [ nNum; |HACIENDO j = j + 1;
          nCal = (j * 100) + 1; aAlfa = aClave % nCal;
          nCal = &aAlfa;
          |SI nCal > 79;
               nCal = 79 - (nCal - 79);
          |SINO;
               nCal = 79 + (79 - nCal);
          |FINSI;
          aAlfa1 = &nCal + aAlfa1;
     |SIGUE;
     aClave = aAlfa1;
     |QBF aClave;
|FPRC;

|PROCESO PideClave;
     nError = 1;
     aAlfa = #0#3; |QBF aAlfa;
     |SI aAlfa ! "";
          |DBASS aPath;
          aPath = aPath + #0#2; |QBF aPath;
          aDef = aPath + "/def/" + #0#3; |QBF aPath;
          aDef = aDef + ".mas";
          |CARGA_DEF aDef, referen@;
          |SI FSalida ! 0;
               |MENAV "0000Error al cargar def empresa...";
          |SINO;
               aAlfa = aPath + "/fich/";
               |PATH_DAT #referen@#aAlfa#;
               |ABRE #referen@;
               aAlfa = "|NC";
               aAlfa = #referen@#aAlfa#;
               nCampos = aAlfa;
               |PARA i = 0; |SI i < nCampos; |HACIENDO i = i + 1;
                    aAlfa = "|C" + (("000" + i) % -103) + "NOMBRE";
                    aAlfa = #referen@#aAlfa#;
                    aAlfa = aAlfa > "";
                    aAlfa = aAlfa - "CLAVE";
                    |SI FSalida > 0; |SAL; |FINSI;
               |SIGUE;
               |SI i < nCampos;
                    aAlfa = "|K000";
                    aAlfa = #referen@#aAlfa#;
                    aCamClave = aAlfa % -103;
                    nCampo = aCamClave;
                    #referen@#nCampo# = #0#4;
                    |LEE 000#referen@.=;
                    |SI FSalida ! 0;
                         |MENAV "0000No existe empresa...";
                    |SINO;
                         aClave = #referen@#i#;
                         |HAZ Desencripta;
                         aAlfa = "|C" + (("000"+i)%-103) + "ILON";
                         aAlfa = #referen@#aAlfa#;
                         nLon = aAlfa;
                         |HAZ PideClave0;
                         |SI aAlfa = aClave;
                              nError = 0;
                         |FINSI;
                    |FINSI;
               |SINO;
                    nError = 0;
               |FINSI;
               |CIERRA #referen@;
               |DESCARGA_DEF #referen@;
          |FINSI;
     |SINO;
          nError = 0;
     |FINSI;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |HAZ PideClave;
     |SI nError ! 0;
          |MENAV "0000Clave incorrecta, no se realiza la copia...";
          |ACABA;
     |FINSI;

     nError = 1;
     aAlfa = #0#3; |QBF aAlfa;
     |SI aAlfa ! "";
          |DBASS aPath;
          aPath = aPath + #0#2; |QBF aPath;
          aDef = aPath + "/def/" + #0#3; |QBF aPath;
          aDef = aDef + ".mas";
          |CARGA_DEF aDef, referen@;
          |SI FSalida ! 0;
               |MENAV "0000Error al cargar def empresa...";
          |SINO;
               aAlfa = aPath + "/fich/";
               |PATH_DAT #referen@#aAlfa#;
               |ABRE #referen@;
               aAlfa = "|K000";
               aAlfa = #referen@#aAlfa#;
               aCamClave = aAlfa % -103;
               aAlfa = "|C" + aCamClave + "MAXIMO";
               aAlfa = #referen@#aAlfa#;
               aLonClave = aAlfa % A0;
               nCampo = aCamClave;
               #referen@#nCampo# = #0#4;
               |LEE 000#referen@.=;
               |SI FSalida ! 0;
                    |MENAV "0000No existe empresa...";
               |SINO;
                    nPos = aLonClave;
                    nPos = -1 * (nPos + 100);
                    aEmp = (("0000000" + #0#4) % nPos);
                    aPathFic = aPath + "/fich/" + aEmp + "/";
                    nError = 0;
               |FINSI;
               |CIERRA #referen@;
               |DESCARGA_DEF #referen@;
          |FINSI;
     |SINO;
          |DBASS aPath;
          aPath = aPath + #0#2; |QBF aPath;
          aPathFic = aPath + "/fich/";
          aAlfa = aPathFic;
          |_PDIR aAlfa;
          |SI FSalida ! 0;
               |MENAV "0000No existe o esta vacio el directorio 'fich'";
          |SINO;
               nError = 0;
          |FINSI;
          aEmp = "";
     |FINSI;
     |SI nError = 0;
          nError = 1;
          fHoy = ~;
          aFic = (fHoy % -102) + (fHoy % 402) + (fHoy % 102);
          aTar = aFic + aEmp ;    ||->Nombre del Fichero
          |DFICE aPath;
          a1 = aPath + aTar + " *.dat *.ixx *.ddx";
          a2 = aPathFic;
          |ATAR a1, a2;

          a1 = aPath + aTar;
          |COMPRIME a1;
          |FBORRA a1;

          a1 = a1 + ".gz";
          |XABRE "E", a1, 0;
          |SI FSalida ! 0;
               |MENAV "0000Error no se puede compactar...";
          |SINO;
               aIP = "";
               |IP_REMOTO aIP;
               a2 = #0#5; |QBF a2;
               a2 = a2 + "/" + aTar + ".tgz";
               |SI aIP = "";
                    |COPIA_FICHERO a1, a2;
               |SINO;
                    |ENVIA_FICHERO a1, a2;
               |FINSI;
               |SI FSalida ! 0;
                    |MENAV "0000Error al copiar 'tgz' al destino...";
               |SINO;
                    nError = 0;
               |FINSI;
               |FBORRA a1;
          |FINSI;
     |FINSI;
     |SI nError = 1;
          |MENAV "0000Se ha producido algún error, no se ha realizado la copia...";
     |SINO;
          aAlfa = "0000Fichero compactado:" + &13 + aTar + ".tgz";
          |MENAV aAlfa;
     |FINSI;
|FPRC;
