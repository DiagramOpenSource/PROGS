|FICHEROS;
     dsexcel0 #0;
     dsexcel1 #1;
     dsexcel2 #2;
     dsexcel3 #3,MANTE;
     dsexcel4 #4;
     dsexcel5 #5,MANTE;
     dsexcel6 #6,MANTE;
     dsexcel7 #7;
     dsexcel8 #8;

     referen@ #100;
|FIN;

|VARIABLES;
     aNom = "";
     aEmpresa = "";
     aPathMasiva = "";
     aOrg = "";
     aDes = "";
     nGuarda   = 0;
     nCarac    = 0;
     nReg      = 0;
     nRegGraba = 0;
     nRegDupli = 0;
     nXls      = 0;
     aApli     = "";
     aAlfa     = "";
     aAlf1     = "";
     aAlf2     = "";
     aPath     = "";
     aDir      = "";
     aFich     = "";
     nHandle   = 0;
     aDef      = "";
     nCampo    = 0;
     nCampos   = 0;
     i         = 0;
     aEmp      = "";
     aFichero  = "";
     aDat      = "";
     aExcel    = "";
     nSal      = 0;
     nSal2     = 0;
     nBoton1   = 0;
     nBoton2   = 0;
     nBoton3   = 0;
     aRuta     = "";

{300}aDato     = "";
{300}aCamp     = "";
{300}aValor = "";

 {-1}aMenu     = "";
     aMenu1    = "2400";
     aMenu2    = "1";
     aMenu3    = "";
     aMenu4    = "RPC";
     aMenu5    = " Repasar ";
     aMenu6    = " Procesar ";
     aMenu7    = " Cancelar ";

     aCol      = "";
     aLon      = "";
     aColum    = "";
     nNum1     = 0;
     nNum2     = 0;
     aSigno    = "";
     aSignoAnt = "";
     nPos      = 0;
     aRelle    = "";
     aCar      = "";
     aCompo    = "";
     nPosLon   = 0;
     nLon      = 0;
     nLongi    = 0;
     nSwDefe   = 0;
     aCadena   = "";
     aLen      = "";
     aCaracter = "";
     aCarac    = "";
     nLen      = 0;
     aLetras = "";
     aIP = "";
     aTmp = "";
     aCR = "";
     aLF = "";
     nColumna = 0;
     nColumna1 = 0;
     nColumna2 = 0;
     aCol1 = "";
     aCol2 = "";
     aComi = "";
|FIN;
ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
|PROCESO SacaEmpresa;
     aLon = aPathMasiva % A0;
     aNom = "";
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = -(i * 100 + 1)
          aCar = aPathMasiva % nPos;
          |SI aCar = "\"; |O aCar = "/"; |SAL; |FINSI;
          aNom = aCar + aNom;
     |SIGUE;

     aEmpresa = "";
     aLon = aNom % A0;
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aCar = aNom % nPos;
          |SI aCar = "."; |SAL; |FINSI;
          aEmpresa = aEmpresa + aCar;
     |SIGUE;
|FPRC;

|PROCESO Carpeta7; |TIPO 7;
     |ESTADO_CONTROL nBoton3, "ENABLE";
|FPRC;

|PROCESO Carpeta6; |TIPO 6;
     |ESTADO_CONTROL nBoton3, "DISABLE";
|FPRC;

|PROCESO Boton3;
     |PTEC 824;
|FPRC;

|PROCESO MasivaF2; |TIPO 0;
     |SI FSalida = 10;
          aRuta = "D" + aRuta;
          |BROWSE aRuta;
          aRuta = aRuta % 299;
          |QBF aRuta;
          #1#7 = aRuta; |PINTA #1#7;
     |FINSI;
|FPRC;

|PROCESO Masiva; |TIPO 0;
     |SI #1#6 = "S";
          |C_M #1#3, 1, "N";
          |C_M #1#7, 1, "S";
          #1#3 = ""; |PINTA #1#3;
     |SINO;
          |C_M #1#3, 1, "S";
          |C_M #1#7, 1, "N";
          #1#7 = ""; |PINTA #1#7;
     |FINSI;
|FPRC;

|PROCESO Tipo; |TIPO 0;
     |SI #1#4 = "E";
          |C_M #1#5, 1, "N";
     |SINO;
          |C_M #1#5, 1, "S";
     |FINSI;
     |PINTA #1#5;
|FPRC;

|PROCESO QuitaComillas;
     aComi = &34;
     aAlfa = aValor % 101;
     |SI aAlfa = aComi;
          aValor = aValor % 2;
     |FINSI;
     aAlfa = aValor % -101;
     |SI aAlfa = aComi;
          aAlfa = aValor % A0;
          nPos = (aAlfa - 1) + 100;
          aValor = aValor % nPos;
     |FINSI;
|FPRC;

|PROCESO NumColum;
     aLetras = "ABCDEFGHIJKLMNOPQRSTUVWXYZ";
     aAlfa = aCol % A0;
     |SI aAlfa = 1;
          aAlfa = aLetras - aCol;
          aAlfa = (("0" + FSalida) % -104) % 102;
          nColumna = aAlfa - 1;
     |SINO;
          |SI aAlfa = 2;
               aCol1 = aCol % 101;
               aCol2 = aCol % 201;

               aAlfa = aLetras - aCol1;
               aAlfa = (("0" + FSalida) % -104) % 102;
               nColumna1 = aAlfa - 1;

               aAlfa = aLetras - aCol2;
               aAlfa = (("0" + FSalida) % -104) % 102;
               nColumna2 = aAlfa - 1;

               nColumna = 26 * (nColumna1 + 1) + nColumna2;
          |SINO;
               nColumna = 255;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Formato;
     aAlfa = ("0000" + nPosLon) % -102;
     nLon = aAlfa;
     aAlfa = ("0000" + nPosLon) % -302;
     nPos = aAlfa;
     aLon = aDato % A0;
     aAlfa = "";
     |SI aRelle = "N";
          |SI nLon ! 0; |Y nPos ! 0;
               aDato = aDato % nPosLon;
          |FINSI;
     |SINO;
          |SI nPosLon = 0;     || Logitud segun el def
               nNum1 = nLongi - aLon;
               |SI nNum1 > 0;
                    aAlfa = aCar * nNum1;
               |FINSI;
          |SINO;               || Longitud segun la definicion
               |SI nPos = 0;
                    nNum1 = nLon - aLon;
                    |SI nNum1 > 0;
                         aAlfa = aCar * nNum1;
                    |FINSI;
               |SINO;
                    aDato = aDato % nPosLon;
                    aLon = aDato % A0;
                    |SI nLon > aLon;
                         nNum1 = nLon - aLon;
                         |SI nNum1 > 0;
                              aAlfa = aCar * nNum1;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
          |SI aRelle = "D"; aDato = aDato + aAlfa; |FINSI;
          |SI aRelle = "I"; aDato = aAlfa + aDato; |FINSI;
     |FINSI;
     aAlfa = aDato;
     || aAlfa retorna el valor
|FPRC;

|PROCESO Una;
     |SI #1#4 = "E";
          ||| aCol contiene la columna, "A", "B" y nXls la fila
          aAlfa = aCol + nXls;
          |QBF aAlfa;
          |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa;
          |SI FSalida ! 0; nSal = 1; |FINSI;
     |SINO;
          |HAZ NumColum;
          |SI nColumna ] 250;
               nSal = 1;
               |MENAV "0000Se supera el maximo de campos (250)...";
          |SINO;
               IaValor = aValor1<-;
               IaValor = IaValor + nColumna;
               |HAZ QuitaComillas;
               aAlfa = aValor;
          |FINSI;
     |FINSI;
     |QBF aAlfa;
|FPRC;

|PROCESO Columna;
     aLon = aColum % A0;
     aCol = "";
     nNum1 = 0;
     nNum2 = 0;
     aSigno = "";
     aSignoAnt = "";
     aAlfa = "";
     |PARA i = 1; |SI i [ aLon; |Y nSal = 0; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aAlfa = aColum % nPos;
          |SI aAlfa = "*"; |O aAlfa = "+"; |O aAlfa = "/"; |O aAlfa = "-";
               aSigno = aAlfa;
               |HAZ Una;
               nNum1 = aAlfa;
               |SI aSignoAnt = "";
                    nNum2 = nNum1;
               |SINO;
                    |SI aSignoAnt = "*"; nNum2 = nNum2 * nNum1; |FINSI;
                    |SI aSignoAnt = "+"; nNum2 = nNum2 + nNum1; |FINSI;
                    |SI aSignoAnt = "/"; nNum2 = nNum2 / nNum1; |FINSI;
                    |SI aSignoAnt = "-"; nNum2 = nNum2 - nNum1; |FINSI;
               |FINSI;
               aSignoAnt = aSigno;
               aCol = "";
          |SINO;
               aCol = aCol + aAlfa;
          |FINSI;
     |SIGUE;
     |SI aCol ! ""; |Y nSal = 0;
          |HAZ Una;
          |SI aSignoAnt ! "";
               nNum1 = aAlfa;
               |SI aSignoAnt = "*"; nNum2 = nNum2 * nNum1; |FINSI;
               |SI aSignoAnt = "+"; nNum2 = nNum2 + nNum1; |FINSI;
               |SI aSignoAnt = "/"; nNum2 = nNum2 / nNum1; |FINSI;
               |SI aSignoAnt = "-"; nNum2 = nNum2 - nNum1; |FINSI;
               aAlfa = nNum2;
               |SI nNum2 = 0; aAlfa = ""; |FINSI;
          |FINSI;
     |FINSI;
     || aAlfa retorna el valor
|FPRC;

|PROCESO dsexcel6;
     aColum = #6#4; |QBF aColum;
     |HAZ Columna;
     aAlf1 = aAlfa; |QBT aAlf1;
     |SI aAlf1 ! ""; nSal2 = 0; |FINSI;
     aDato = aAlfa;
     nPosLon = #6#5;
     aRelle = #6#6;
     aCar = #6#7;
     nLongi = 0;
     |HAZ Formato;
     aCompo = aCompo + aAlfa;
|FPRC;

|DEFBUCLE dsexcel6;
     #6#1;
     ;
     #3#0, #3#4, #3#1, 01;
     #3#0, #3#4, #3#1, 99;
     ;
     NULL, dsexcel6;
|FIN;
ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
|PROCESO Grabalo;
     |GRABA 000#100n;
     nReg = nReg + 1;
     |SI FSalida = 0;
          nRegGraba = nRegGraba + 1;
     |FINSI;
|FPRC;

|PROCESO MiraRaros;
     nGuarda = nCampo;
     |QBF aAlf1;
     aCadena = "";
     aLen    = aAlf1 % 0;
     nLen    = aLen;
     |PARA nCampo = 1;  |SI nCampo [ nLen;  |HACIENDO nCampo = nCampo + 1;
           nPos = (100 * nCampo) + 1;
           aCaracter = aAlf1 % nPos;

           aCarac = &209;  |SI aCaracter = aCarac;  aCaracter = "¥"; |FINSI;
           aCarac = &241;  |SI aCaracter = aCarac;  aCaracter = "¤"; |FINSI;
           aCarac = &186;  |SI aCaracter = aCarac;  aCaracter = "§"; |FINSI;
           aCarac = &170;  |SI aCaracter = aCarac;  aCaracter = "¦"; |FINSI;
           aCarac = &225;  |SI aCaracter = aCarac;  aCaracter = " "; |FINSI;
           aCarac = &233;  |SI aCaracter = aCarac;  aCaracter = "‚"; |FINSI;
           aCarac = &237;  |SI aCaracter = aCarac;  aCaracter = "¡"; |FINSI;
           aCarac = &243;  |SI aCaracter = aCarac;  aCaracter = "¢"; |FINSI;
           aCarac = &250;  |SI aCaracter = aCarac;  aCaracter = "£"; |FINSI;

           aCadena = aCadena + aCaracter;
     |SIGUE;
     aAlf1 = aCadena;
     nCampo = nGuarda;
|FPRC;

|PROCESO PasaCampos;
     |SI nSwDefe = 0; |DDEFECTO #100; |FINSI;
     IaDato = aDato1<-;
     IaCamp = aCamp1<-;
     |PARA i = 0; |SI i [ nCampos; |HACIENDO i = i + 1;
          |SI aCamp ! "";
               nCampo = aCamp;
               |SI #5#4 = "X"; |O #5#4 = "D";
                    aAlf1 = aDato; |QBT aAlf1;
                    |SI aAlf1 ! "";
                         aAlf1 = aDato; |QBF aAlf1;
                         |HAZ MiraRaros;
                         #100nCampo = aAlf1;
                    |FINSI;
               |SINO;
                    aAlf1 = aDato;  |QBF aAlf1;
                    |HAZ MiraRaros;
                    #100nCampo = aAlf1;
               |FINSI;
          |FINSI;
          IaDato = IaDato + 1;
          IaCamp = IaCamp + 1;
     |SIGUE;
|FPRC;

|PROCESO dsexcel3;
     aAlfa = #3#3; |QBT aAlfa;
     |SI aAlfa = ""; |Y #3#7 = "N";
          aDato = "";
          aCamp = "";
     |SINO;
          |SI #3#7 = "S";
               aCompo = "";
               |BUCLE dsexcel6;
               aAlfa = aCompo;
          |SINO;
               aColum = #3#3; |QBF aColum;
               |HAZ Columna;
               aAlf1 = aAlfa; |QBT aAlf1;
               |SI aAlf1 ! ""; nSal2 = 0; |FINSI;
          |FINSI;
          aDato = aAlfa;
          nPosLon = #3#5;
          aRelle = #3#6;
          aCar = #3#8;
          nLongi = #3#9;
          |HAZ Formato;
          aDato = aAlfa;
          aCamp = #3#1;
     |FINSI;
     IaDato = IaDato + 1;
     IaCamp = IaCamp + 1;

     |SI #1#4 = "E";
          aAlfa = " campo:" + #3#1 + "           ";
          |PINTA 2430, aAlfa;
     |FINSI;
|FPRC;

|DEFBUCLE dsexcel3;
     #3#1;
     ;
     #5#0, #5#1, 000;
     #5#0, #5#1, 999;
     ;
     NULL, dsexcel3;
|FIN;

|PROCESO Vacia;
     IaDato = aDato1<-;
     IaCamp = aCamp1<-;
     |PARA i = 1; |SI i [ 255; |HACIENDO i = i + 1;
          aDato = "";
          aCamp = "";
          IaDato = IaDato + 1;
          IaCamp = IaCamp + 1;
     |SIGUE;
|FPRC;

|PROCESO CargaHoja;
     |PARA nXls = #dsexcel1#8; |SI nXls < 65536; |HACIENDO nXls = nXls + 1;
          aAlfa = #5#1 + ", linea: " + nXls + "  ";
          |PINTA 2408, aAlfa;
          |HAZ Vacia;
          IaDato = aDato1<-;
          IaCamp = aCamp1<-;
          nSal2 = 1;
          |BUCLE dsexcel3;
          |SI nSal ! 0; |SAL; |FINSI;
          |SI nSal2 ! 0; |SAL; |FINSI;
          |HAZ PasaCampos;
          |LEE 101#100=;
          |SI FSalida = 0;
               |SI #5#4 = "S"; |O #5#4 = "X"; |O #5#4 = "D";
                    |BORRA 000#100a;
                    nSwDefe = 0;
                    |SI #5#4 = "X"; nSwDefe = 1; |FINSI;
                    |SI #5#4 = "D"; nSwDefe = 1; |FINSI;
                    |HAZ PasaCampos;
                    nSwDefe = 0;
                    |HAZ Grabalo;
               |SINO;
                    nReg = nReg + 1;
               |FINSI;
               nRegDupli = nRegDupli + 1;
          |SINO;
               |SI #5#4 ! "D";
                    |HAZ Grabalo;
               |FINSI;
          |FINSI;
          |LIBERA #100;
     |SIGUE;
|FPRC;

|PROCESO FinalLinea;
     nXls = nXls + 1;
     |SI nXls < #dsexcel1#8;  |ACABA;  |FINSI;

     aAlfa = #5#1 + ", linea: " + nXls + "  ";
     |PINTA 2408, aAlfa;
     |HAZ Vacia;

     IaDato = aDato1<-;
     IaCamp = aCamp1<-;
     nSal2 = 1;
     |BUCLE dsexcel3;
     |SI nSal ! 0;  |ACABA; |FINSI;
     |SI nSal2 ! 0; |ACABA; |FINSI;

     |HAZ PasaCampos;
     |LEE 101#100=;
     |SI FSalida = 0;
         |SI #5#4 = "S"; |O #5#4 = "X"; |O #5#4 = "D";
             |BORRA 000#100a;
             nSwDefe = 0;
             |SI #5#4 = "X"; nSwDefe = 1; |FINSI;
             |SI #5#4 = "D"; nSwDefe = 1; |FINSI;
             |HAZ PasaCampos;
             nSwDefe = 0;
             |HAZ Grabalo;
         |SINO;
             nReg = nReg + 1;
         |FINSI;
         nRegDupli = nRegDupli + 1;
     |SINO;
         |SI #5#4 ! "D";
             |HAZ Grabalo;
         |FINSI;
     |FINSI;
     |LIBERA #100;
|FPRC;

|PROCESO Caracter;
     |SI aCar = aLF; |ACABA; |FINSI;
     |SI aCar = aCR;
          aValor = aDato;
          |HAZ FinalLinea;
          nCampo = 0;
          aDato = "";
          IaValor = aValor1<-;
          |PARA i = 0; |SI i [ 250; |HACIENDO i = i + 1;
               aValor = "";
               IaValor = IaValor + 1;
          |SIGUE;
          IaValor = aValor1<-;
     |SINO;
          |SI aCar = #1#5;
               aValor = aDato;
               IaValor = IaValor + 1;
               nCampo = nCampo + 1;
               aDato = "";
               |SI nCampo ] 250;
                    |MENAV "0000Se supera el maximo de campos (250)...";
                    nSal = 1;
               |FINSI;
          |SINO;
               aDato = aDato + aCar;
          |FINSI;
     |FINSI;
     || |PULSATECLA;
|FPRC;

|PROCESO CargaCSV;
     aLF = &13;
     aCR = &10;
     nCampo = 0; aDato = ""; IaValor = aValor1<-; nXls = 0;
     |XLEE nHandle, 250, aTmp;
     |PARA; |SI FSalida > 0; |HACIENDO;
          |PARA; |SI aTmp ! ""; |HACIENDO;
               aCar = aTmp % 101;
               |SI aTmp = aCar;
                    aTmp = ""; ||con un caracter falla: "aTmp = aTmp % 2"
               |SINO;
                    aTmp = aTmp % 2;
               |FINSI;
               |HAZ Caracter;
               |SI nSal ! 0; |SAL; |FINSI;
          |SIGUE;

          |XLEE nHandle, 250, aTmp;
          |SI nSal ! 0; |SAL; |FINSI;
     |SIGUE;
     |SI nSal = 0;
          |HAZ FinalLinea;
     |FINSI;
|FPRC;

|PROCESO dsexcel5;
     aExcel = #5#2; |QBF aExcel;
     |SI aExcel = ""; |ACABA; |FINSI;

     |SI #1#6 = "S";
          aExcel = aPathMasiva;
          |HAZ SacaEmpresa;
          #1#3 = aEmpresa;
     |FINSI;

     |XABRE "CE", aExcel, 0;
     |SI FSalida < 0;
          aAlfa = "0000No existe " + aExcel;
          |MENAV aAlfa;
          |ACABA;
     |FINSI;

     |SI #1#4 = "E";
          |ALFACALLDLL "agixl97.dll", "carga_book", aExcel;
          |ALFACALLDLL "agixl97.dll", "selecciona_hoja", "Hoja1";
     |SINO;
          |DFICE aAlfa;
          aAlfa = aAlfa + Usuario;
          |SI aIP = "";
               |COPIA_FICHERO aExcel, aAlfa;
          |SINO;
               |RECIBE_FICHERO aExcel, aAlfa;
          |FINSI;
          |SI FSalida = 0;
               aExcel = aAlfa;
               |XABRE "B", aAlfa, nHandle;
               |SI FSalida ] 0; FSalida = 0; |FINSI;
          |FINSI;
     |FINSI;

     |SI FSalida = 0;
          aPath = #1#2; |QBF aPath;
          aApli = #1#1; |QBF aApli;
          aEmp = #1#3; |QBF aEmp;
          |SI aEmp = "";
              aDat = aPath + aApli + "/fich/";
          |SINO;
               aDat = aPath + aApli + "/fich/" + aEmp + "/";
          |FINSI;

          aDef = #5#1; |QBF aDef;

          aDef = aPath + aApli + "/def/" + aDef;
          |CARGA_DEF aDef, referen@;
          |SI FSalida ! 0;
               aAlfa = "0000Error al cargar " + aDef;
               |MENAV aAlfa;
               |ACABA;
          |FINSI;

          aAlfa = "|NC";
          aAlfa = #referen@#aAlfa#;
          nCampos = aAlfa;
          |PATH_DAT #100 aDat;

          |ABRE #100;
          |SI #1#4 = "E";
              |HAZ CargaHoja;
          |SINO;
              |HAZ CargaCSV;
              |XCIERRA nHandle;
              |FBORRA aExcel;
          |FINSI;
          |CIERRA #100;

          |DESCARGA_DEF #referen@;
     |FINSI;

     |SI #1#4 = "E";
          ||peta en vista
          |ALFACALLDLL "agixl97.dll", "fin_book", "salva";
     |FINSI;
|FPRC;

|DEFBUCLE dsexcel5;
     #5#1;
     ;
     #1#0, "        ";
     #1#0, "zzzzzzzz";
     ;
     NULL, dsexcel5;
|FIN;
ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
****************************************************
ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
|PROCESO Min6;
     #6#0 = #3#0;
     #6#1 = #3#4;
     #6#2 = #3#1;
     #6#3 = 01;
|FPRC;

|PROCESO Max6;
     #6#0 = #3#0;
     #6#1 = #3#4;
     #6#2 = #3#1;
     #6#3 = 99;
|FPRC;

|PROCESO Campo3; |TIPO 0;
     |SI #3Campo = Contenido;
       |ET E37;
          |ENCAMPO #7#3;
          |SI FSalida ! 0; |VETE Fin3; |FINSI;
       |ET E33;
          |ENCAMPO #3#3;
          |SI FSalida = 2; |VETE E37; |FINSI;
          |SI FSalida ! 0; |VETE Fin3; |FINSI;
       |ET E35;
          |ENCAMPO #5#3;
          |SI FSalida = 2; |VETE E33; |FINSI;
          |SI FSalida ! 0; |VETE Fin3; |FINSI;
       |ET E36;
          |ENCAMPO #6#3;
          |SI FSalida = 2; |VETE E35; |FINSI;
          |SI FSalida ! 0; |VETE Fin3; |FINSI;
       |ET E38;
          |ENCAMPO #8#3;
          |SI FSalida = 2; |VETE E36; |FINSI;
          |SI FSalida = 0;
               |SI #3#7 = "S";
                    |PUSHV 0501 2380;
                    |ENTLINEAL #1#6, 4, 2, 22, 1, Min6, Max6;
                    |POPV;
                    |ABRE #6;
                    #6#0 = #3#0;
                    #6#1 = #3#4;
                    #6#2 = #3#1;
                    #6#3 = 0;
                    |LEE 000#6];
                    |SI #6#0 = #3#0; |Y #6#1 = #3#4; |Y #6#2 = #3#1;
                         #3#7 = "S";
                    |SINO;
                         #3#7 = "N";
                    |FINSI;
                    |CIERRA #6;
               |FINSI;
               |GRABA 020#3a;
          |FINSI;
       |ET Fin3;
     |FINSI;
|FPRC;
ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
|PROCESO Boton1;
     |PTEC 824;
|FPRC;

|PROCESO Browser; |TIPO 6;
     |SI FSalida ! 10; |ACABA; |FINSI;

     aRuta = #5#2;  |QBF aRuta;
     |SI aRuta = "";
          |SI #1#4 = "E";
               aRuta = "*.xls";
          |SINO;
               aRuta = "*.csv";
          |FINSI;
     |FINSI;
     aRuta = "F" + aRuta;

     |BROWSE aRuta;

     aRuta = aRuta % 299;
     #5#2 = aRuta;
     |PINTA #5#2;
|FPRC;

|PROCESO Min;
     #3#0 = #1#0;
     #3#1 = 0;
|FPRC;

|PROCESO Max;
     #3#1 = #1#0;
     #3#1 = 999;
|FPRC;

|PROCESO Min5;
     #5#0 = #1#0;
     #5#1 = "                    ";
|FPRC;

|PROCESO Max5;
     #5#0 = #1#0;
     #5#1 = "zzzzzzzzzzzzzzzzzzzz";
|FPRC;

|PROCESO Min3;
     #3#0 = #5#0;
     #3#4 = #5#1;
     #3#1 = 0;
|FPRC;

|PROCESO Max3;
     #3#0 = #5#0;
     #3#4 = #5#1;
     #3#1 = 999;
|FPRC;

|PROCESO LibroExcel6; |TIPO 6;
     |SI #1#6 = "N";
          |ESTADO_CONTROL nBoton1, "DISABLE";
     |FINSI;
|FPRC;

|PROCESO LibroExcel7; |TIPO 7;
     |SI #1#6 = "N";
          |MENSA "0000Pinchar el boton "Fichero" o pulsar "F2" para buscar el fichero a importar.";
          |ESTADO_CONTROL nBoton1, "ENABLE";
     |SINO;
          |MENSA "0000Indicar 'MASIVA' para realizar la importacion del fichero.";
     |FINSI;
|FPRC;

|PROCESO LibroExcel; |TIPO 0;
     |SI #1#6 = "S";
          aAlfa = #5#2; |QBF aAlfa;
          |SI aAlfa ! "";
               aAlfa = aAlfa > "";
               |SI aAlfa ! "MASIVA";
                    |MENAV "0000Indicar 'MASIVA' para seleccionar este fichero a importar";
                    |ERROR;
               |SINO;
                    #5#2 = aAlfa; |PINTA #5#2;
               |FINSI;
          |FINSI;
     |SINO;
          aExcel = #5#2; |QBF aExcel;
          |SI aExcel ! "";
               |XABRE "CE", aExcel, 0;
               |SI FSalida < 0;
                    |MENAV "0000No encuentro el fichero a importar";
                    ||ERROR;
               |SINO;
                    aAlf1 = #5#2;  |QBF aAlf1;
                    aAlf2 = aAlf1 % -104;
                    aAlf2 = aAlf2 < aAlf2;
                    |SI #1#4 = "E";
                         |SI aAlf2 ! ".xls";
                              |MENAV "0000Es obligado el formato EXCEL para importar.";
                              |ERROR;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO LeeCampos;
     aPath = #1#2; |QBF aPath;
     aApli = #1#1; |QBF aApli;
     aDef = #5#1; |QBF aDef;
     aDef = aPath + aApli + "/def/" + aDef;
     |CARGA_DEF aDef, referen@;
     |SI FSalida ! 0;
          aAlfa = "0000Error al cargar " + aDef;
          |MENAV aAlfa;
          |ACABA;
     |FINSI;

     aAlfa = "|NC";
     aAlfa = #referen@#aAlfa#;
     nCampos = aAlfa;

     |ABRE #3;
     |PARA i = 0 ;|SI i < nCampos; |HACIENDO i = i + 1;
           |DDEFECTO #3;
           #3#0 = #1#0;
           #3#4 = #5#1;
           #3#1 = i;
           |LEE 101#3=;
           |SI FSalida ! 0; |GRABA 020#3b; |FINSI;

           aAlfa = "|C" + (("000" + i) % -103) + "NOMBRE";
           aAlfa = #referen@#aAlfa#;
           #3#2 = aAlfa;
           aAlfa = "|C" + (("000" + i) % -103) + "LONGITUD";
           aAlfa = #referen@#aAlfa#;
           #3#9 = aAlfa;
           |GRABA 020#3a;
           |LIBERA #3;
     |SIGUE;

     |PARA; |SI #3#0 = #1#0; |Y #3#4 = #5#1; |Y FSalida = 0; |HACIENDO i = i + 1;
            #3#0 = #1#0;
            #3#4 = #5#1;
            #3#1 = i;
            |LEE 101#3];
            |SI FSalida = 0; |Y #3#0 = #1#0; |Y #3#4 = #5#1;
                i = #3#1;
                |BORRA 020#3a;
            |FINSI;
            |LIBERA #3;
     |SIGUE;

     |PARA; |SI #6#0 = #1#0; |Y #6#1 = #5#1; |Y FSalida = 0; |HACIENDO;
            #6#0 = #1#0;
            #6#1 = #5#1;
            #6#2 = 0;
            #6#3 = 0;
            |LEE 101#6];
            |SI FSalida = 0; |Y #6#0 = #1#0; |Y #6#1 = #5#1;
                |BORRA 020#6a;
            |FINSI;
            |LIBERA #6;
            |LEE 000#6s;
     |SIGUE;

     |CIERRA #6;
     |DESCARGA_DEF #referen@;
|FPRC;

|PROCESO Def0; |TIPO 0;
     |SI #5#1 = Contenido;
        |ET E1;
          |ENCAMPO #2#5;
          |SI FSalida ! 0; |Y FSalida ! 10; |VETE Sal; |FINSI;
          |ENCAMPO #4#5;
          |SI FSalida = 2; |VETE E1; |FINSI;
          |SI FSalida = 0;
               |GRABA 020#5a;
               |PUSHV 05012380;
               |HAZ LeeCampos;
               |ENTLINEAL #1#3, 1, 4, 22, 1, Min3, Max3;
               |POPV;
          |FINSI;
        |ET Sal;
     |FINSI;
|FPRC;

|PROCESO Def66; |TIPO 66;
     |ABRE #4;
     #4#0 = #5#1;
     |LEE 000#4];
     |CONSULTA_CLAVE #1#4;
     |SI FSalida = 0;
          #5#1 = #4#0;  |PINTA #5#1;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #4;
|FPRC;

ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
|PROCESO BorraCom;
     |BORRA 020#6a;
|FPRC;

|DEFBUCLE BorraCom;
     #6#1;
     ;
     #3#0, #3#4, #3#1, 01;
     #3#0, #3#4, #3#1, 99;
     be;
     NULL, BorraCom;
|FIN;

|PROCESO Compuesto3; |TIPO 0;
     |SI #3#7 = "S";
          |C_M #3#3, 1, "N";
          #3#3 = ""; |PINTA #3#3;
     |SINO;
          |C_M #3#3, 1, "S";
          |BUCLE BorraCom;
     |FINSI;
|FPRC;

|PROCESO Relle3; |TIPO 7;
     |SI #3#6 = "N";
          |C_M #3#8, 1, "N";
          #3#8 = ""; |PINTA #3#8;
     |SINO;
          |C_M #3#8, 1, "S";
     |FINSI;
|FPRC;

|PROCESO Relle6; |TIPO 7;
     |SI #6#6 = "N";
          |C_M #6#7, 1, "N";
          #6#7 = ""; |PINTA #6#7;
     |SINO;
          |C_M #6#7, 1, "S";
     |FINSI;
|FPRC;

|PROCESO Pos3; |TIPO 0;
|ACABA;
     |SI #3#5 = 0;
          |C_M #3#6, 1, "N";
          |C_M #3#8, 1, "N";
          #3#6 = "N"; |PINTA #3#6;
          #3#8 = ""; |PINTA #3#7;
     |SINO;
          |C_M #3#6, 1, "S";
          |C_M #3#8, 1, "S";
     |FINSI;
|FPRC;

|PROCESO Pos6; |TIPO 0;
|ACABA;
     |SI #6#5 = 0;
          |C_M #6#6, 1, "N";
          |C_M #6#7, 1, "N";
          #6#6 = "N"; |PINTA #6#6;
          #6#7 = ""; |PINTA #6#7;
     |SINO;
          |C_M #6#6, 1, "S";
          |C_M #6#7, 1, "S";
     |FINSI;
|FPRC;

|PROCESO Procesa;
     |SI #1#4 = "E";
          |CARGADLL "agixl97.dll";
          |SI FSalida < 0;
               |MENAV "0000No se puede usar agixl97.dll";
          |SINO;
              |BUCLE dsexcel5;
              |DESCARGADLL "agixl97.dll";
          |FINSI;
     |SINO;
          |BUCLE dsexcel5;
     |FINSI;
|FPRC;

|PROCESO Tipo12; |TIPO 12;
     |SI FSalida = 7; |ACABA; |FINSI;
     |SI aDat = "";
          |MENAV "0000Configuracion de importacion incompleta.";
          |ACABA;
     |FINSI;
     |ESTADO_CONTROL nBoton2, "DISABLE";
     |ESTADO_CONTROL nBoton3, "DISABLE";

     |PUSHV 0501 2380;
     |PINPA #0#7;
     |PAUSA;
     |POPV;

     |HAZ LeeDir;

     |ABRE #2;
     #2#0 = #1#0;
     |LEE 101#2=;
     |SI FSalida ! 0; |GRABA 020#2b; |FINSI;
     #2#1 = #1#1;
     #2#2 = #1#2;
     #2#3 = #1#3;
     #2#5 = #1#4;
     #2#6 = #1#5;
     #2#7 = #1#6;
     #2#8 = #1#7;
     #2#9 = #1#8;
     |GRABA 020#2a;
     |CIERRA #2;

     |PARA; |SI aMenu2 = 1; |HACIENDO;
          |ENTLINEAL #1#5, -1, 4, 22, 0, Min5, Max5;
          |MENU aMenu;
          aMenu2 = FSalida;
     |SIGUE;
     |ESTADO_CONTROL nBoton1, "DISABLE";

     |SI aMenu2 = 2;
          |SI #1#6 = "S";
               aPathMasiva = #1#7; |QBF aPathMasiva;
               aAlfa = aPathMasiva % -101;
               |SI aAlfa ! "/"; |Y aAlfa ! "\";
                    aPathMasiva = aPathMasiva + "/";
               |FINSI;
               |SI aIP = "";
                    |_PDIR aPathMasiva;
                    |PARA; |SI FSalida = 0; |HACIENDO;
                         |HAZ Procesa;
                         |_SDIR aPathMasiva;
                    |SIGUE;
               |SINO;
                    |R_PDIR aPathMasiva;
                    |PARA; |SI FSalida = 0; |HACIENDO;
                         |HAZ Procesa;
                         |R_SDIR aPathMasiva;
                    |SIGUE;
               |FINSI;
          |SINO;
               |HAZ Procesa;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO empresa0; |TIPO 0;
     |SI FSalida = 2; |ACABA; |FINSI;
     aPath = #1#2; |QBF aPath;
     aApli = #1#1; |QBF aApli;
     aEmp = #1#3; |QBF aEmp;
     |SI aEmp = "";
         aDat = aPath + aApli + "/fich/";
     |SINO;
          aDat = aPath + aApli + "/fich/" + aEmp + "/";
     |FINSI;

     |MKDIR aDat;
     |SI FSalida = 0;
          |RMDIR aDat;
          |MENAV "0000La empresa no existe...";
          |ERROR;
          aDat = "";
     |FINSI;
|FPRC;

|PROCESO DefPath7; |TIPO 7;
     aAlfa = #1#2; |QBF aAlfa;
     |SI aAlfa = "";
          |DBASS aAlfa;
          #1#2 = aAlfa;
     |FINSI;
|FPRC;

|PROCESO PonBarra; |TIPO 0;
     aAlfa = #1#2; |QBF aAlfa;
     |SI aAlfa ! "";
          aAlf1 = aAlfa % -101;
          |SI aAlf1 ! "/";|Y aAlf1 ! "\";
               aAlfa = aAlfa + "/";
               #1#2 = aAlfa;
               |PINTA #1#2;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Corrige5;
     #4#0 = #5#1;
     |LEE 000#4=;
     |SI FSalida ! 0;
          |BORRA 020#5a;
     |FINSI;
|FPRC;

|DEFBUCLE Corrige5;
     #5#1;
     ;
     #1#0, "        ";
     #1#0, "zzzzzzzz";
     be;
     NULL, Corrige5;
|FIN;

|PROCESO LeeDir;
     |MENSA "0000Cargando....";
     aPath = #1#2; |QBF aPath;
     aApli = #1#1; |QBF aApli;
     aDir = aPath + "/" + aApli + "/fich/" + aApli + ".dir";
     |XABRE "U", aDir, nHandle;
     |SI FSalida < 0;
          aAlfa = "0000[" + aDir + "] inaccesible o inexistente.";
          |MENAV aAlfa;
     |SINO;
          |DELINDEX #4;
          |ABRE #4;
          |ABRE #5;
          |DDEFECTO #5;
          |PARA; |SI FSalida ] 0 ;|HACIENDO;
               |XLEE nHandle, 255, aAlf1; |SI FSalida < 0; |SAL; |FINSI;
               |XLEE nHandle, 255, aAlf2; |SI FSalida < 0; |SAL; |FINSI;
               |XLEE nHandle, 255, aAlfa; |SI FSalida < 0; |SAL; |FINSI;
               |XLEE nHandle, 255, aAlfa; |SI FSalida < 0; |SAL; |FINSI;
               |XLEE nHandle, 255, aAlfa; |SI FSalida < 0; |SAL; |FINSI;
               #5#0 = #1#0;
               #5#1 = aAlf1;
               #5#3 = aAlf2;
               #5#4 = "N";
               |GRABA 000#5n;
               #4#0 = aAlf1;
               #4#1 = aAlf2;
               |GRABA 000#4n;
          |SIGUE;
          |CIERRA #5;
          |BUCLE Corrige5;
          |CIERRA #4;
     |FINSI;
|FPRC;

|PROCESO Codi0; |TIPO 0;
     |SI #1Campo = Contenido; |ACABA; |FINSI;

     |ABRE #2;
     #2#0 = #1#0;
     |LEE 000#2=;
     |SI FSalida = 0;
          #1#1 = #2#1; |PINTA #1#1;
          #1#2 = #2#2; |PINTA #1#2;
          #1#3 = #2#3; |PINTA #1#3;
          #1#4 = #2#5; |PINTA #1#4;
          #1#5 = #2#6; |PINTA #1#5;
          #1#6 = #2#7; |PINTA #1#6;
          #1#7 = #2#8; |PINTA #1#7;
          #1#8 = #2#9; |PINTA #1#8;
          |HAZ Masiva;
     |FINSI;
     |CIERRA #2;

     |ENTLINEAL #1#5, -1, 7, 22, 0, Min5, Max5;
|FPRC;

|PROCESO MensaAct0; |TIPO 0;
     |SI FSalida = 13;
          |PUSHV 0501 2380;
          |PINPA #0#7;
          |PAUSA;
          |POPV;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Boton2;
     |PUSHV 0501 2380;

     |ABRE #8;
     |LEE 101#8p;
     |SI FSalida ! 0; |GRABA 020#8b; |FINSI;

     |PINPA #0#8;
     |PINDA #0#8;
     |ENDATOS #1#8;
     |SI FSalida = 0;
          |GRABA 020#8a;

          aRuta = #8#1; |QBF aRuta;
          aAlfa = aRuta % -101;
          |SI aAlfa ! "/"; |Y aAlfa ! "\"; aRuta = aRuta + "/"; |FINSI;
          aOrg = aRuta + "dsexcel0.dat";
          |XABRE "E", aOrg, 0;
          |SI FSalida ] 0;
               |DFICE aPath;
               aOrg = aRuta + "dsexcel0.dat"; aDes = aPath + "dsexcel0.dat"; |COPIA_FICHERO aOrg, aDes;
               aOrg = aRuta + "dsexcel0.ixx"; aDes = aPath + "dsexcel0.ixx"; |COPIA_FICHERO aOrg, aDes;
               aOrg = aRuta + "dsexcel0.ddx"; aDes = aPath + "dsexcel0.ddx"; |COPIA_FICHERO aOrg, aDes;

               aOrg = aRuta + "dsexcel2.dat"; aDes = aPath + "dsexcel2.dat"; |COPIA_FICHERO aOrg, aDes;
               aOrg = aRuta + "dsexcel2.ixx"; aDes = aPath + "dsexcel2.ixx"; |COPIA_FICHERO aOrg, aDes;
               aOrg = aRuta + "dsexcel2.ddx"; aDes = aPath + "dsexcel2.ddx"; |COPIA_FICHERO aOrg, aDes;

               aOrg = aRuta + "dsexcel3.dat"; aDes = aPath + "dsexcel3.dat"; |COPIA_FICHERO aOrg, aDes;
               aOrg = aRuta + "dsexcel3.ixx"; aDes = aPath + "dsexcel3.ixx"; |COPIA_FICHERO aOrg, aDes;
               aOrg = aRuta + "dsexcel3.ddx"; aDes = aPath + "dsexcel3.ddx"; |COPIA_FICHERO aOrg, aDes;

               aOrg = aRuta + "dsexcel4.dat"; aDes = aPath + "dsexcel4.dat"; |COPIA_FICHERO aOrg, aDes;
               aOrg = aRuta + "dsexcel4.ixx"; aDes = aPath + "dsexcel4.ixx"; |COPIA_FICHERO aOrg, aDes;
               aOrg = aRuta + "dsexcel4.ddx"; aDes = aPath + "dsexcel4.ddx"; |COPIA_FICHERO aOrg, aDes;

               aOrg = aRuta + "dsexcel5.dat"; aDes = aPath + "dsexcel5.dat"; |COPIA_FICHERO aOrg, aDes;
               aOrg = aRuta + "dsexcel5.ixx"; aDes = aPath + "dsexcel5.ixx"; |COPIA_FICHERO aOrg, aDes;
               aOrg = aRuta + "dsexcel5.ddx"; aDes = aPath + "dsexcel5.ddx"; |COPIA_FICHERO aOrg, aDes;

               aOrg = aRuta + "dsexcel6.dat"; aDes = aPath + "dsexcel6.dat"; |COPIA_FICHERO aOrg, aDes;
               aOrg = aRuta + "dsexcel6.ixx"; aDes = aPath + "dsexcel6.ixx"; |COPIA_FICHERO aOrg, aDes;
               aOrg = aRuta + "dsexcel6.ddx"; aDes = aPath + "dsexcel6.ddx"; |COPIA_FICHERO aOrg, aDes;

               |DBASS aAlfa;
               |ABRE #2;
               |LEE 101#2p;
               |PARA; |SI FSalida = 0; |HACIENDO;
                    #2#2 = aAlfa;
                    |GRABA 020#2a;
                    |LIBERA #2;
                    |LEE 101#2s;
               |SIGUE;
               |CIERRA #2;
          |SINO;
               |MENAV "0000Carpeta incorrecta...";
          |FINSI;
     |FINSI;
     |CIERRA #8;
     |POPV;
|FPRC;

|PROGRAMA;
     aIP = ""; |IP_REMOTO aIP;
     aAlfa = Usuario; |QBF aAlfa;
     |NOME_DAT #4 aAlfa;

     |CONTROL_SIMPLE 0, "BOTON, Fichero       ", 1249, 1268, Boton1;
     nBoton1 = FSalida;
     |ESTADO_CONTROL nBoton1, "DISABLE";

     |CONTROL_SIMPLE 0, "BOTON,Importar definici¢n otra carpeta", 0649, 0668, Boton2;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,...", 1022, 1024, Boton3;
     nBoton3 = FSalida;
     |ESTADO_CONTROL nBoton3, "DISABLE";

     |SI PARAMETRO = "";
          |CLS;
          |PINPA #0#1;
          |PINDA #0#1;
          |ENTLINEAL #1#5, -1, 7, 22, 0, Min5, Max5;
          |ENDATOS #1#1;
     |SINO;
          |ABRE #2;
          #2#0 = PARAMETRO;
          |LEE 000#2=;
          |SI FSalida = 0;
               #1#0 = #2#0;
               #1#1 = #2#1;
               #1#2 = #2#2;
               #1#3 = #2#3;
               #1#4 = #2#5;
               #1#5 = #2#6;
               |HAZ LeeDir;
               |SI #1#4 = "E";
                    |CARGADLL "agixl97.dll";
                    |SI FSalida ] 0;
                         |BUCLE dsexcel5;
                         |DESCARGADLL "agixl97.dll";
                    |FINSI;
               |SINO;
                    |BUCLE dsexcel5;
               |FINSI;
          |FINSI;
          |CIERRA #2;
     |FINSI;

     |DELINDEX #4;

     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;
     |PULSATECLA;
|FPRO;
