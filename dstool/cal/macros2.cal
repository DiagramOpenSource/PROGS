|| Este programa carga en el buffer interno de Teclado las pulsaciones que
|| previamente hayamos grabado en agifa900
|| Es un buffer tipo LIFO por lo que le tendremos que dar la vuelta primero
|| y luego escribirlo.
|| Para ello se monta una segunda clave en el fichero de lineas de Macro
|| con el numero de linea en orden descendente
|| Lorca 20/07/94

|| La idea es que sea interactivo y que haga una serie de pulsaciones que se
|| pare que pregunte y que despues siga. Eso ahora se debe hacer con dos macros:
|| Una hasta la pregunta y otra despues de la pregunta.
|| Hay una solucion bastante buena y es preguntar primero los datos que voy
|| a necesitar y meterlos en el buffer en el sitio que le corresponda. Para ello
|| en la definicion de Macros tendremos la posibilidad mediante ? de determinar
|| que es una PETICION DE DATOS Y ademas para no pasarnos tambien sabemos la
|| longitud del campo.
|| Lorca 29/07/94

|| Si ponen $CX . Siendo "X" un numero del 1 al 9 se pondra en marcha un contador
|| hasta un total de 9 a la vez.
|| Lorca 25/05/95

|| Se incluye la posibilidad de descargar el valor introducido en una peticion
|| de datos por parte de usuario en una memoria. Para ello se introduce un campo
|| nuevo en agifa901 donde se informa del numero de memoria donde descargar
|| del 1-30 , 0 no descarga.

|| Se crean 30 campos de 80 posiciones y se dejan 30 mas numericos para posibles
|| modificaciones. Se debera hacer mdpase de agifa901. los campos a¤adidos en
|| agifa902 no importan ya que antes este fichero no existia.
|| Poniendo $V1 cogera la memoria numero tal y la descargara en donde le corresponda
|| Lorca 16/06/95


|FICHEROS;
    defmacrc #0;  || Macros cab.disponibles
    defmacrl #1;  || Lineas de macros
    macros   #2;  || Pantalla de eleccion de macros
    macrotmp #3;  || Temporal
|FIN;

|VARIABLES;

   clave=2;
   entmodo=5;
   limite=21;
   pantalla=0;

   &entradalineal=1;
   &controlc=0;
   mensaje="";
   tecla=0;
   orden="";
   tipo = "";
   ffecha = @;
   afecha="";
   ascii=0;
   longitud="";
   i=0;
   acortar="";
   cortar=0;
   puente="";
   cadena="";
   amenos="";
   nfecha=0;
   acuantos="";
   ncuantos=0;
   afecha2="";
   hora="";
   programa="";
   pulsa1="";
   pulsa2="";
   nantmacro=0;
   &proceso=0;
   &nmacro=0;
   path="";
   acontador="";
   ncontador=0;
   {9}conta=0;
   argv1 = "";
   argv2 = "";
   numvar      = 0;
   ncampo      = 0;
   alfa1       = "";
   ncortar     = 0;
   atmp = "";
   adigito ="";
|FIN;


|PROCESO lineal902;
   |PUSHV 0657 1875;
   || Ya que se esta presentando un fichero que no esta pensado como lineal
   || en formato lineal se deben hacer los ajustes pertinentes.
   |CAMPO_LINEAL #0#0,1,"S";
   |CAMPO_LINEAL #0#1,1,"S";
   |CAMPO_POSICION #0#0,1,0804;
   |CAMPO_POSICION #0#1,1,0809;
   |CAMPO_VISUAL #0#2,1,"N";
   |CAMPO_VISUAL #0#3,1,"N";

   |PINPA #0#2;

   || Si hemos puesto como argumento TODOS cogera no solo las macros
   || definidas por usuario sino todas
   argv1 = PARAMETRO;
   |QBF argv1;
   argv2 = argv1 - "TODOS";
   |SI argv2 ! argv1;   || Existe argumento
       |ENTLINEAL #3#0,clave,entmodo,limite,pantalla,prim9023,ult9023;
   |SINO;
       |ENTLINEAL #3#0,clave,entmodo,limite,pantalla,prim902,ult902;
   |FINSI;

   |POPV;
|FPRC;


|PROCESO prim902;
   #0#0=001;
   #0#2=Usuario;
|FPRC;

|PROCESO ult902;
   #0#0=999;
   #0#2=Usuario;
|FPRC;

|PROCESO prim9022;
    #1#0=#0#0;
    #1#1=001;
    #1#2="?                             ";
|FPRC;
|PROCESO ult9022;
    #1#0=#0#0;
    #1#1=999;
    #1#2="?                             ";
|FPRC;

|PROCESO prim9023;
   #0#0=001;
   #0#2="          ";
|FPRC;

|PROCESO ult9023;
   #0#0=999;
   #0#2="zzzzzzzzzz";
|FPRC;


|PROCESO pidedatos;
   |PUSHV 1201 2380;
   || Ya que se esta presentando un fichero en una pantalla diferente a
   || su mantenimiento se deben hacer los ajustes pertinentes.
   |CAMPO_POSICION #1#1,1,1503;
   |CAMPO_POSICION #1#3,1,1508;

   |CAMPO_VISUAL #1#5,1,"S";
   || Este solo es modificable aqui

   |CAMPO_MODIFICA #1#5,1,"S";
   |CAMPO_POSICION #1#5,1,1539;

   || Estos no aparecen aqui
   |CAMPO_VISUAL #1#2,1,"N";
   |CAMPO_VISUAL #1#4,1,"N";
   |CAMPO_VISUAL #1#7,1,"N";      || Numero de memoria

   |CAMPO_MODIFICA #1#3,1,"N";


   |PINPA #1#2;

   limite=21;
   entmodo=2;
   clave=3;
   |ENTLINEAL #3#1,clave,entmodo,limite,pantalla,prim9022,ult9022;
   |POPV;
|FPRC;


|| Este proceso escribe en el buffer interno de.runun lo informado en las lineas
|| de las macros.
|PROCESO componemacro;
     || Compruebo que no sea una tecla de control
     || Para ello en el primer digito debe haber un [
     orden = #1#2;
     |QBF orden;

     tipo = orden % "101";
     |SI tipo = "[";
         orden = orden % "203";      ||  Por ejemplo [802]
         |HAZ ptec;
     |SINO;
          || Ahora si se le ha indicado que coja la fecha o contadores
          tipo = orden % "101";
          |SI tipo ! "$";
              |SI tipo = "?";
               tipo = orden % "101";
                   orden=#1#5;  || Entrada de datos por parte del usuario
                   |HAZ filtra;
                   cadena=atmp;
                   |HAZ literal;
               |SINO;
                || Si no es ni [ ni $ ni ? es un literal
                 cadena=orden;
                |HAZ literal;
            |FINSI;
          |SINO;
             tipo = orden % "102";
             ffecha = ~;

             |SI tipo = "$D";      || Descarga dia del mes
                |HAZ dia;
             |FINSI;
             |SI tipo = "$M";      || Descarga mes
                |HAZ mes;
             |FINSI;
             |SI tipo = "$A";
                |HAZ anyo;         || Descarga anyo
             |FINSI;

             || Tambien es posible informar la fecha entera
             |SI tipo = "$T";
                 || O bien la fecha menos nnn dias.
                 amenos = orden % "301";
                 |SI amenos = "-";
                     nfecha=ffecha;
                     acuantos = orden % "403";
                     ncuantos=acuantos;
                     nfecha=nfecha-ncuantos;
                     ffecha = nfecha;
                     afecha=ffecha;
                 |SINO;              || fecha de hoy
                     afecha=ffecha;
                 |FINSI;
                     || Como siempre al reves
                     |HAZ anyo;
                     |HAZ mes;
                     |HAZ dia;
             |FINSI;
             |SI tipo = "$C";    || Contadores
                || Es un contador. Determino el numero de contador que  es
                   acontador= orden % "301";
                   ncontador = acontador;
                   || Aumento el contador que le toca
                   |SI ncontador < 0;|O ncontador > 9;  || Por si hay alguna aberracion
                       ncontador = 1;
                   |FINSI;
                   #macrotmp#ncontador# = #macrotmp#ncontador# +1;
                   cadena = #macrotmp#ncontador#;
                   |HAZ literal;
               |FINSI;

             |SI tipo = "$V";    || Memorias
                || Es una memoria. Determino el numero de memoria
                   acontador= orden % "302";
                   ncontador = acontador;
                   |HAZ leevar;
                   cadena = orden;
                   |HAZ literal;
               |FINSI;
         |FINSI;
    |FINSI;
|FPRC;


|| Lo hago con la clave que tiene orden descendente de las lineas
|DEFBUCLE 9020;
#1#2;
;
#0#0,999;
#0#0,001;
e;
NULL,componemacro;
|FIN;



|PROCESO prin_902;
    |SI proceso = 0;   || Si viene del programa agifa905 Ejecucion de
                       || procesos, no hace falta que el usuario elija macro


        || Si hemos puesto como PARAMETRO ->DIRECTO no presentara la pantalla
        || de eleccion de macros sino que se ejecutara directamente
        argv1= PARAMETRO;
        |QBF argv1;
        argv2 = argv1 - "DIRECTO";
        |SI argv2 ! argv1;   || Ejecuta una macro directamente
            |HAZ entramacro;
        |SINO;

           |BLIN 4;
           mensaje="0000<RETURN> Valida Macro , <ESC> o <CTRL>+<C> No ejecuta ninguna";
           |MENSA mensaje;
           |HAZ lineal902;
           |BLIN 4;
        |FINSI;
    |SINO;   || Si viene el numero de macro le viene dado
        #0#0 = nmacro;
    |FINSI;

    || A hora ya se si ha elegido una linea con ESC o bien ha salido con
    || <CTRL>+<C>. Mediante un calculo del DEFMACRC que se ejecuta desde este
    || proceso.

    |SI controlc=0;
       || por cada linea de macro
       || No puedo hacerlo con buclelin, porque es un buffer LIFO y se ha
       || tenido que montar una clave descendente

       || Se ejecutaran 2 bucles:
       || 1.- Comprobara que haya alguna PETICION DE DATOS "?" y con eso
       ||     creara un registro en un temporal que sera enseñado posterior
       ||     mente al usuario para que rellene los datos variables.
       || 2.- Un bucle que lea todas las lineas de la macro en orden inverso
       ||     para componer el buffer interno. Si se encuentra alguna linea con
       ||     "?" se ira al temporal y cargara la primera linea que alli encuentre
       ||     y la informacion introducida entrara al buffer como si se tratara
       ||     de cualquier otra pulsacion premeditada.

       || 1.-
       || Primero compruebo que haya algun dato de PETICION DE DATOS en esta
       || macro que visualizar y evitaremos desplegamientos innecesarios de ventanas.


       |ABRE #1;         || Lineas de macro
       |DDEFECTO #1;
       |SELECT #3#1;     || Selecciono clave 3-> Numero macro + Pulsacion + Linea
       #1#0=#0#0;        || Numero de macro
       nantmacro= #0#0;  || Cargo el valor de macro para comprobaciones
       #1#2="?                             ";
       #1#1=0;
       |LEE 001#1];

       || La busqueda ] es bastante imperfecta. Tampoco puedo buscar por = ya
       || que no se el numero de linea donde va a estar el primero.
       |SI FSalida = 0;|Y #1#0 = nantmacro;
          || Esta comprobacion falla cuando el registro es el ultimo en el
          || que nantmacro sigue siendo el mismo. Comprobaremos que #1#2 todavia
          || siga siendo ?.
          |SI #1#2 = "?                             ";
             |HAZ pidedatos;
          |FINSI;
       |FINSI;
       |CIERRA #1;


       || 2.-
       |HAZ abre906;      || Se abre fichero temporal de contadores
       |BUCLE 9020;

       || Graba estado de contadores
       |GRABA 021#macrotmp.a;
       |LIBERA #macrotmp;
       |CIERRAT #macrotmp;

    |FINSI;
|FPRC;

|| Carga en buffer
|PROCESO ptec;
     tecla = orden;
     |PTEC tecla;
|FPRC;


|| Carga en buffer dias de la semana
|PROCESO dia;
   afecha2 =afecha % "102";
   orden = afecha2 % "201";   || Primer digito dia
   |HAZ ascii;
   |HAZ ptec;
   orden = afecha2 % "101";   || segundo digito dia
   |HAZ ascii;
   |HAZ ptec;
|FPRC;

|| Carga en buffer mes
|PROCESO mes;
   afecha2 =afecha % "402";
   orden = afecha2 % "201";   || Primer digito mes
   |HAZ ascii;
   |HAZ ptec;
   orden = afecha2 % "101";   || segundo digito mes
   |HAZ ascii;
   |HAZ ptec;
|FPRC;


|| Carga en buffer mes
|PROCESO anyo;
   afecha2 =afecha % "902";
   orden = afecha2 % "201";   || Primer digito anyo
   |HAZ ascii;
   |HAZ ptec;
   orden = afecha2 % "101";   || segundo digito anyo
   |HAZ ascii;
   |HAZ ptec;
|FPRC;


|PROCESO ascii;
    ascii = &orden;   || Calculo el ascii
    orden = ascii;
|FPRC;


|| Coje una cadena y le da la vuelta
|PROCESO literal;
    |QBF cadena;
    longitud = cadena % 0;    || Averiguo longitud de la cadena
    |PARA i = 1;|SI i [ longitud;|HACIENDO i=i+1;

         cortar = i*100+1;
         acortar= cortar;
         acortar = "-" + acortar;

         orden =  cadena % acortar;
         |HAZ ascii;
         |HAZ ptec;
    |SIGUE;
|FPRC;


|| Leo de la variable
|PROCESO leevar;
     numvar = ncontador;
     ncampo = numvar +10; || Numero del campo donde se guarda la variables
     orden = #macrotmp#ncampo#;
     |QBF orden;
|FPRC;

|PROCESO abre906;
    |ABRET #macrotmp;
    #macrotmp#0 ="A";
    |LEE 101#macrotmp.=;
    || si no existe lo creo
    |SI FSalida ! 0;
        |GRABA 021#macrotmp.b;
    |FINSI;
|FPRC;

|| Pregunto macro directamente
|PROCESO entramacro;
         |BLIN 24;
         |PINTA 2420 , " Numero de macro a ejecutar :     " ;
         alfa1 = "";
         E_sup = 3;
         E_inf = "";
         alfa1 = 2453 ? 1;
         |BLIN 24;
         #0#0 = alfa1;
|FPRC;

|PROCESO filtra;
     || Quito el "|" si lo hubiere
     atmp = "";
     |PARA i = 1;|SI i [ #defmacrl#4;|HACIENDO i= i+1;
          ncortar = i*100 + 1;
          acortar = ncortar;
          adigito = #defmacrl#5 % acortar;
          atmp = atmp + adigito;
     |SIGUE;
|FPRC;
