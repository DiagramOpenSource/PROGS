|| Ver si me devuelve la barra en los directorios
|| hacer a los directorios un XABRE "E" ...


|FICHEROS;
     dstool02 #0;
     dstool03,MANTE #3;   ||Entlineal Resultado proceso

     dsw00004 #4;   ||Aux. Consu.Aplicac 1
     dsw00005 #5;   ||Aux. Consu.Aplicac 2
|FIN;

|VARIABLES;
     &enNumApli = 0;
     nSwNoEntran = 0;
     nLinea = 0;
     aSistema = "";
     aLinea = "";
     nHandle = 0;
     nHandleFic = 0;
     aFic = "";
     aAlfa = "";
     nSwHay = 0;
     nExiste = 0;
     aMensaje = "";
     aRespuesta = "";
     aInforme = "";
     nSwPie = 0;
     aBase = "";
     aMira = "";
     aAux = "";
     aDirectorio = "";
     &eaMenu = "";
     &eaVersion = "";
     &eaBase = "";
     aFicImp = "";
|FIN;

|PROGRAMA;
     aFicImp = PARAMETRO;
     |SI aFicImp ! "";
          |HAZ Principal;
          |ACABA;
     |FINSI;

     |CLS;
     |ABRE #0;
     |DDEFECTO #0;
     |LEE 101#0p;
     nExiste = FSalida;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          aRespuesta = #dstool02#1;
          #dstool02#1 = "NO";
          |SI nExiste = 0;
               |GRABA 020#0a;
          |SINO;
               |GRABA 020#0n;
          |FINSI;
          |SI aRespuesta = "SI";
               |HAZ Principal;
          |FINSI;
     |FINSI;
     |LIBERA #0;
     |CIERRA #0;
|FPRO;

|PROCESO Aux1;
     aMira = #dsw00004#0;
     |QBF aMira;
     |HAZ MiraDir;
     |BORRA 020#dsw00004.a;
|FPRC;

|DEFBUCLE Aux1;
 #dsw00004#1;
 ;
 INICIO;
 FINAL;
 be;
 NULL, Aux1;
|FIN;

|PROCESO Aux2;
     #dsw00004#0 = #dsw00005#0;
     |LEE 101#dsw00004.=;
     |SI FSalida ! 0;
          |GRABA 020#dsw00004.n;
          |LIBERA #dsw00004;
     |FINSI;
     |BORRA 020#dsw00005.a;
|FPRC;

|DEFBUCLE Aux2;
 #dsw00005#1;
 ;
 INICIO;
 FINAL;
 ;
 NULL, Aux2;
|FIN;

|PROCESO inf;
     #dstool03#0 = 001;
|FPRC;

|PROCESO sup;
     #dstool03#0 = 999;
|FPRC;

|PROCESO Principal;
     |QUE_SISTEMA aSistema;
     |SI aSistema = "ESDOS";
          aSistema = "ESDOS";
     |SINO;
          aSistema = "ESUNIX";
     |FINSI;

     |DELINDEX #dsw00004;
     |DELINDEX #dsw00005;
     |DELINDEX #dstool03;
     |DBASS aBase;
     |QBF aBase;
     eaBase = aBase;

     |ABRE #dstool03;
     nLinea = 1;
     nSwHay = 0;
     |ABRE #dsw00005;
     aMira = aBase;
     nSwNoEntran = 1;
     |HAZ MiraDir;
     |CIERRA #dsw00005;
     |PARA; |SI nSwHay = 1; |HACIENDO;
          nSwNoEntran = 0;
          nSwHay = 0;
          |ABRE #dsw00004;
          |BUCLE Aux2;
          |CIERRA #dsw00004;
          |ABRE #dsw00005;
          |BUCLE Aux1;
          |CIERRA #dsw00005;
     |SIGUE;
     |CIERRA #dstool03;

     |SI aFicImp = "";
          |ENTLINEAL #1#dstool03, -1, 4, 22, 1, inf, sup;

          aMensaje = "0000SDesea imprimir el resultado?";
          |CONFI aMensaje;
          |SI FSalida = 0;
               |HAZ Imprime;
          |FINSI;
     |SINO;
          |HAZ Imprime;
     |FINSI;
|FPRC;

|PROCESO PintaBase; |TIPO 7;
     aMensaje = "Directorio Base: [" + aBase + "]";
     |QBF aMensaje;
     |PINTA 0641, aMensaje;
|FPRC;

|PROCESO MiraDir;
     aDirectorio = aMira + "*";
     |_PDIR aDirectorio;
     |PARA; |SI FSalida ] 0; |HACIENDO;
          aAux = aDirectorio % -104;
          |SI aAux = ".men";
               |HAZ SacaMenu;
          |SINO;
               |SI aSistema = "ESUNIX";
                    aFic = aDirectorio + "/";
                    |XABRE "E", aFic, nHandle;
                    |SI FSalida ] 0;
                         #dsw00005#0 = aFic;
                         |LEE 101#dsw00005.=;
                         |SI FSalida ! 0;
                              |GRABA 020#dsw00005.n;
                              |LIBERA #dsw00005;
                              nSwHay = 1;
                         |FINSI;
                    |FINSI;
               |SINO;
                    ||En MSDOS .. no funciona muy fino lo de abrir carpetas.
                    aFic = aDirectorio;
                    |XABRE "E", aFic, nHandle;
                    |SI FSalida < 0;
                         aFic = aFic + "/";
                         #dsw00005#0 = aFic;
                         |LEE 101#dsw00005.=;
                         |SI FSalida ! 0;
                              |GRABA 020#dsw00005.n;
                              |LIBERA #dsw00005;
                              nSwHay = 1;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
          |_SDIR aDirectorio;
     |SIGUE;
|FPRC;

|PROCESO SacaMenu;
     |SI nSwNoEntran = 0;
          |XABRE "U", aDirectorio, nHandleFic;
          |SI FSalida < 0;
               eaVersion = "-Error- Al intentar abrir el Menu.";
          |SINO;
               |XLEE nHandleFic, 250, aLinea;
               |SI FSalida < 0;
                    eaVersion = "-Error- No se puede leer el Menu.";
               |SINO;
                    eaVersion = aLinea;
               |FINSI;
               |XCIERRA nHandleFic;
          |FINSI;
          eaMenu = aDirectorio;

          enNumApli = nLinea;
          |DDEFECTO #dstool03;
          #dstool03#0 = nLinea;
          |LEE 101#dstool03.=;
          |SI FSalida ! 0;
               eaMenu = eaMenu - aBase;
               #dstool03#1 = eaMenu;
               #dstool03#2 = eaVersion;
               |GRABA 020#dstool03.n;
               |LIBERA #dstool03;
               nLinea = nLinea + 1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO dstool03;
     |PRINT;
     nSwPie = 1;
|FPRC;

|DEFBUCLE dstool03;
 #dstool03#1;
 ;
 INICIO;
 FINAL;
 ;
 NULL, dstool03;
|FIN;

|PROCESO Imprime;
     |SI aFicImp ! "";
          Impresora = aFicImp;
          |IMPRE -77;
     |SINO;
          |IMPRE 0;
     |FINSI;
     |SI FSalida = 0;
          aInforme = "dstool02";
          |INFOR aInforme;
          |SI FSalida = 0;
               |BUCLE dstool03;
               |SI nSwPie = 1;
                    |PIEINF;
               |FINSI;
               |FININF;
          |SINO;
               aMensaje = "0000Informe " + aInforme + " inaccesible.";
               |MENAV aMensaje;
          |FINSI;
          |FINIMP;
     |SINO;
          |MENAV "0000Proceso cancelado.";
     |FINSI;
|FPRC;
