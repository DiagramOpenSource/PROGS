|FICHEROS;
     instalar;
     instala1;
     dicorreo; || direcciones
     cocorreo; || configuracion correo
     ds_confi; || configuracion ds.ini
|FIN;

|VARIABLES;
     aDBase = "";
     aOrigen = "";
     aDestin = "";
     aMensaje = "";
     nSwEnvio = 0;
     nSwCompr = 0;
     nSwBucle = 0;
     aIPdscorreo = "";
     aClave = "";
     aUsuario = "";
     aDireccion = "";
     aDirOrigen = "";
     aDirDestino = "";
     aServidor = "";
     aAlfa1 = "";
     nNume1 = 0;

  alfa1   ="";
  alfa2   ="";
  i       = 0;
  descrip = "";
  codigo  = "";
  version = "";
  estado  = 0;
  linea   = 0;
  &nDeci_1 = 0;
|FIN;

|RUTINA limite1;
  |SI FSalida = "POSICION";
     #instalar#linea = linea;
  |SINO;
     #instalar#linea = 1;
  |FINSI;
|FRUT;

|RUTINA limite2;
  #instalar#linea = 99;
|FRUT;

|RUTINA rellena;
     alfa1 = Usuario;
     |NOME_DAT #instalar#alfa1#;
     |DELINDEX #instalar;
     |ABRE #instalar;
     |DDEFECTO #instalar;
     #instalar#linea = 1;
     #instalar#descrip = "Nuevo Paquete a Instalar";
     |GRABA 020#instalar.n;
     |SI FSalida < 0;
          |MENAV "0000AVISO INSTALACION A001";
          |CIERRA #instalar;
          |ACABA;
     |FINSI;
     linea = 2;
     |PARA i = 0; |SI; |HACIENDO i = i + 1;
          |DDEFECTO #instalar;
          |DATOS_PAQUETE i, descrip, codigo, version, estado;
          |SI FSalida ] 0;
               |SI estado ] 0;
                    #instalar#linea = linea;
                    #instalar#descrip = descrip;
                    #instalar#codigo = codigo;
                    #instalar#version = version;
                    #instalar#estado = estado;
                    |SI estado = 0;
                         #instalar#testado = "Instalado";
                    |SINO;
                         |SI estado = 1;
                              #instalar#testado = "Por Actualizar";
                         |SINO;
                              |SI estado = 2;
                                   #instalar#testado = "Por Instalar";
                              |SINO;
                                   #instalar#testado = "?";
                              |FINSI;
                         |FINSI;
                    |FINSI;
                    |GRABA 020#instalar.n;
                    |SI FSalida < 0;
                         |MENAV "0000AVISO INSTALACION A002";
                         |CIERRA #instalar;
                         |ACABA;
                    |FINSI;
                    linea = linea + 1;
               |FINSI;
          |SINO;
               |SAL;
          |FINSI;
     |SIGUE;
     |CIERRA #instalar;
|FRUT;

|RUTINA nueva;
   |PUSHV 1109 1473;
   |PINPA #0#instala1;
   |ENDATOS #1#instala1;
   |SI FSalida ] 0;
      |BLOQUEA_MASTER;
      |SI FSalida < 1;
        |MENAV "0400ATENCION! NO DEBE HABER OTROS PROGRAMAS EN EJECUCION!!";
        |ACABA;
      |FINSI;
      alfa1 = #instala1#medio;
      |COPIAINSTALACION alfa1;
      |DESBLOQUEA_MASTER;
      i = 9999;
      |PTEC 806;
   |FINSI;
   |POPV;
|FRUT;

|RUTINA reinstala;
   |CONFI "2400N¨Seguro que desea REINSTALAR? ";
   |SI FSalida = 0;
      alfa1 = #instalar#codigo;
      |HAZ instala;
   |FINSI;
|FRUT;

|RUTINA actualiza;
   |CONFI "2400N¨Seguro que desea ACTUALIZAR? ";
   |SI FSalida = 0;
      alfa1 = #instalar#codigo;
      |HAZ instala;
   |FINSI;
|FRUT;

|PROCESO dicorreo;
     |SI nSwBucle = 0;
          nSwCompr = 1;
     |FINSI;
     |SI nSwBucle = 1;

          |ABRE #cocorreo;
          |LEE 000#cocorreo.p;
          |CIERRA #cocorreo;

          nNume1 = #cocorreo#1 + #cocorreo#2 + #cocorreo#3 + #cocorreo#4;
          |SI nNume1 ! 0;
               aIPdscorreo = #cocorreo#1 + "." + #cocorreo#2 + "." + #cocorreo#3 + "." + #cocorreo#4;
          |SINO;
               aIPdscorreo = #cocorreo#10;
               |QBF aIPdscorreo;
          |FINSI;
          aClave = #cocorreo#7;         |QBF aClave;
          aUsuario = #cocorreo#6;       |QBF aUsuario;
          aDireccion = #cocorreo#8;     |QBF aDireccion;
          |SI #cocorreo#9 = "S";
               aDirOrigen = aClave + ":" + aUsuario + ":" + aDireccion;
          |SINO;
               aDirOrigen = aDireccion;
          |FINSI;
          aDirDestino = #dicorreo#2;    |QBF aDirDestino;
          aServidor = #cocorreo#5;      |QBF aServidor;

          aAlfa1 = "0000Enviando a [" + aDirDestino + "]";
          |MENSA aAlfa1;

          aAlfa1 = #instalar#descrip;
          |DSCORREO_ENVIA aIPdscorreo, aServidor, aDirOrigen, aDirDestino, aAlfa1, aAlfa1, aDestin;
          |SI FSalida < 0;
               |MENAV "0000No se pudo enviar.";
          |SINO;
               |MENAV "0000Correo enviado.";
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE dicorreo;
     #dicorreo#1;
     3;
       1, "S";
     999, "S";
     ;
     NULL, dicorreo;
|FIN;

|PROCESO EjecutaEnvio;
     |DDEFECTO #ds_confi;
     |ABRE #ds_confi;
     |LEE 000#ds_confi.p;
     |CIERRA #ds_confi;
     |SI #ds_confi#87# = "N";
          |ACABA;
     |FINSI;

     |SI nSwEnvio = 1;
          |SUB_EJECUTA "dicorreo.tab";
          nSwCompr = 0;
          nSwBucle = 0;
          |BUCLE dicorreo;
          |SI nSwCompr = 1;
               |CONFI "0000NEnviar correo";
               |SI FSalida = 0;
                    |SUB_EJECUTA "cocorreo.tab";
                    |CONFI "0000NConfirme el envio";
                    |SI FSalida = 0;
                         nSwBucle = 1;
                         |BUCLE dicorreo;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO PreparaEnvio;
     |DDEFECTO #ds_confi;
     |ABRE #ds_confi;
     |LEE 000#ds_confi.p;
     |CIERRA #ds_confi;
     |SI #ds_confi#87# = "N";
          |ACABA;
     |FINSI;

     |DBASS aDBase;
     aOrigen = aDBase + "master/" + alfa1 + ".tgz";
     aDestin = aDBase + "tmp/";
     |MKDIR aDestin;
     aDestin = aDestin + alfa1 + ".tgz";
     |COPIA_FICHERO aOrigen, aDestin;
     |SI FSalida = 0;
          nSwEnvio = 1;
     |SINO;
          nSwEnvio = 0;
          aMensaje = "0000No se pudo copiar [" + aDestin + "]";
          |MENAV aMensaje;
     |FINSI;
|FPRC;

|RUTINA instala;
   |BLOQUEA_MASTER;
   |SI FSalida < 1;
      |MENAV "0400ATENCION! NO DEBE HABER OTROS PROGRAMAS EN EJECUCION!!";
      |ACABA;
   |FINSI;
   |QBF alfa1;

   |HAZ PreparaEnvio;

   |INSTALA alfa1;

   |HAZ EjecutaEnvio;

   |DESBLOQUEA_MASTER;
   i = 9999;
   |PTEC 806;
|FRUT;

|PROCESO seleccion;
|SI FSalida = 0;
        linea = #instalar#linea;
        |SI linea = 1;
            |HAZ nueva;
        |SINO;
        |SI #instalar#estado = 0;
            |HAZ reinstala;
        |SINO;
        |SI #instalar#estado = 1;
            |HAZ actualiza;
        |SINO;
        |SI #instalar#estado = 2;
            alfa1 = #instalar#codigo;
            |HAZ instala;
        |SINO;
            |MENAV "0000SELECCION ERRONEA!";
        |FINSI;
        |FINSI;
        |FINSI;
        |FINSI;
        |ERROR;
|FINSI;
|FPRC;

|PROGRAMA;
  |CLS;
  |CABEZA "Instalaciones";
  |PINPA #0#instalar;
  |ATRI P;
  |MENSA "0400Preparando Lista de Paquetes Instalados";
  |ATRI 0;
  |HAZ rellena;
  |BLIN 4;
  |PARA;|SI;|HACIENDO;
     |CLS;
     |CABEZA "Instalaciones";
     |PINPA #0#instalar;
     |MENSA "0400Seleccione con INTRO, ESC o CTRLC para SALIR";
     linea = 1;
     i = 0;
     |ENTLINEAL #1#instalar,1,4,22,0,limite1,limite2;
     |BLIN 4;
     |SI i = 9999;
        |DELINDEX #instalar;
        |HAZ rellena;
     |SINO;
        |SAL;
     |FINSI;
  |SIGUE;
  |DELINDEX #instalar;
|FPRO;
