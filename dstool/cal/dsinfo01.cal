|FICHEROS;
     dsinfo01 #0;            ||Ctrl AplicacionesCAB
     dsinfo02 #2;            ||Ctrl AplicacionesLIN

     referen@ #100;          ||Para los carga def
|FIN;

|VARIABLES;
     aMensaje = "";
     aSistema = "";
     aAux = "";
     aDef = "";
     aQueQuiero = "";
     aCadena = "";
     aCaracter = "";
     aCaracter10 = "";
     aCaracter13 = "";
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     aAlfa4 = "";
     aAlfa5 = "";

     aFicPet = "";
     nSwPie = 0;
     aInforme = "";


     aFicLog = "";
     aVarFecha = "";
     aDia = "";
     aMes = "";
     aAny = "";
     aBase = "";
     aPathApli = "";
     aPathDef = "";
     Comodin = "";
     Comodin1 = "";
     Comodin2 = "";
     fFecha = @;
     aHora = "";

     aAlfa = "";
     aAplica = "";
     nModo = 0;
     aBeta = "";
     aFicMenu = "";
     nHandle = 0;
     &eaTitulo = "";
     &enTamanyo = "";
     &enCampos = 0;
     &enClaves = 0;
     &enPan = 0;
     &enRel = 0;
     &eaLinea = "";
     &eaResto = "";
     aLinea = "";
     Cont = 0;

     &eaDef = "";
     &eaNomDef = "";
     nTotal = 0;
     aDefAnt = "";
     &eaFic = "";
     &eaPath = "";
     &enDos = 0;
     &eaTres = "";
     nCont = 0;
     nTCompo = 0;
     nCompo = 0;
     nPosi = 0;
     nNume = 0;
     nTClaves = 0;
     nRealCla = 0;
     nHandleDir = 0;
|FIN;

|PROCESO PonDatos; |TIPO 5;
     aAplica = #dsinfo01#0;
     |QBF aAplica;
     |SI aAplica ! "";
          |DBASS aBase;
          |QBF aBase;
          aPathApli = aBase + aAplica + "/";
          aPathDef = aPathApli + "def/";

          nModo = (FEntrada / 100) ! 0;
          |SI nModo ! 4;
               |HAZ MeteLineas;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO MeteLineas;
     Comodin1 = aPathDef + "*.mas";
     |_PDIR Comodin1;
     |PARA; |SI FSalida = 0; |HACIENDO;
        |CARGA_DEF Comodin1, referen@;
        |SI FSalida = 0;
           aAlfa1 = Comodin1 - aPathDef;
           |QBF aAlfa1;
           aAlfa1 = aAlfa1 - ".mas";
           aAlfa1 = aAlfa1 - "/";
           Comodin = aAlfa1;
           ||Fallaba para ficheros que no tengan 8 + 3 caracteres.
           ||Ahora ya no, porque va directamente. Antes cogia 12 y 8 carac.
           |DDEFECTO #dsinfo02;
           #dsinfo02#0 = #dsinfo01#0;
           #dsinfo02#1 = Comodin;
           |LEE 101#dsinfo02.=;
           |SI FSalida ! 0;        ||Si no esta lo introducimos
                Comodin ="|TITULO";
                #dsinfo02#2 = #referen@#Comodin#;
                |SI #dsinfo01#1 = "S";
                     #dsinfo02#3 = "S";
                |FINSI;
                |GRABA 020#dsinfo02.n;
           |FINSI;
           |DESCARGA_DEF #referen@;
           Comodin = "";
           |LIBERA #dsinfo02;
        |FINSI;
        |_SDIR Comodin1;
     |SIGUE;
|FPRC;

|PROCESO DatosDef;
     aDef = aPathDef + #dsinfo02#1;
     |QBF aDef;
     |CARGA_DEF aDef, referen@;
     |SI FSalida = 0;
          aQueQuiero = "|LONGITUD";
          aQueQuiero = #referen@#aQueQuiero#;
          enTamanyo = aQueQuiero;
          enTamanyo = enTamanyo;

          aQueQuiero = "|NC";
          enCampos = #referen@#aQueQuiero#;

          aQueQuiero = "|NK";
          enClaves = #referen@#aQueQuiero#;

          aQueQuiero = "|NP";
          enPan = #referen@#aQueQuiero#;

          aQueQuiero = "|NR";
          enRel = #referen@#aQueQuiero#;

          aQueQuiero = "|FICHERO";
          eaFic = #referen@#aQueQuiero#;

          aQueQuiero = "|DEF";
          eaPath = #referen@#aQueQuiero#;
          |QBF eaPath;
          |SI eaPath ! "";
               eaPath = eaPath + ".mas";
          |FINSI;


          nTotal = enCampos - 1;
          |PARA Cont = 0; |SI Cont [ nTotal; |HACIENDO Cont = Cont + 1;
               Comodin = ("000" + Cont) % -103;
               eaLinea = Comodin;
               Comodin = "|C" + Comodin;
               Comodin1 = Comodin + "NOMBRE";
               aBeta = #referen@#Comodin1#;
               |QBF aBeta;
               aBeta = (aBeta + "                    ") % 120;
               eaLinea = eaLinea + " " + aBeta;

               Comodin1 = Comodin + "TIPO"; |QBF Comodin1;
               Comodin1 = #referen@#Comodin1#;
               |SI Comodin1 = "F"; aBeta = "Fecha"; |FINSI;
               |SI Comodin1 = "A"; aBeta = "Alfa "; |FINSI;
               |SI Comodin1 = "N"; aBeta = "Nume "; |FINSI;
               |SI Comodin1 = "T"; aBeta = "Texto"; |FINSI;
               eaLinea = eaLinea + " " + aBeta;

               Comodin1 = Comodin + "LONGITUD";
               Comodin1 = #referen@#Comodin1#; |QBF Comodin1;
               Comodin1 = ("000" + Comodin1) % -103;
               eaLinea = eaLinea + " " + Comodin1;

               Comodin1 = Comodin + "ILON";
               Comodin1 = #referen@#Comodin1#; |QBF Comodin1;
               Comodin1 = ("000" + Comodin1) % -103;
               eaLinea = eaLinea + "          " + Comodin1;

               Comodin2 = Comodin + "MINIMO";
               aBeta = #referen@#Comodin2#;
               |QBF aBeta;
               aBeta = (aBeta + "                    ") % 120;
               eaLinea = eaLinea + "        " + aBeta;
               Comodin2 = Comodin + "MAXIMO";
               aBeta = #referen@#Comodin2#;
               |QBF aBeta;
               aBeta = (aBeta + "                    ") % 120;
               eaLinea = eaLinea + " " + aBeta;

               Comodin1 = Comodin + "IDCAMPO";
               Comodin1 = #referen@#Comodin1#; |QBF Comodin1;
               Comodin1 = (Comodin1 + "          ") % 110;
               eaLinea = eaLinea + " " + Comodin1;

               Comodin1 = Comodin + "DEFECTO";
               Comodin1 = #referen@#Comodin1#; |QBF Comodin1;
               Comodin1 = (Comodin1 + "                                                                            ") % 177;
               eaLinea = eaLinea + " " + Comodin1;

               Comodin1 = Comodin + "MENSAJE";
               Comodin1 = #referen@#Comodin1#; |QBF Comodin1;
               Comodin1 = (Comodin1 + "                                                                            ") % 177;
               eaResto = Comodin1;
               |QBF eaResto;

               |SI eaDef ! aDefAnt;
                    |SI aDefAnt ! "";
                         FSalida = "PAGINA";
                    |FINSI;
                    aDefAnt = eaDef;
               |FINSI;

               enDos = 0;
               |PRINT;
               |SI eaResto ! "";
                    enDos = 1;
                    eaResto = "Mensaje: " + eaResto;
                    |PRINT;
               |FINSI;
          |SIGUE;

          |SI enClaves > 0;
               eaTres = "";
               enDos = 2;
               |PRINT;
               eaTres = "{{N}}Relacion Campos en Claves{{n}}";
               enDos = 2;
               |PRINT;
               eaTres = "--------------------------------------";
               enDos = 2;
               |PRINT;
               nTClaves = enClaves - 1;
               |PARA nCont = 0; |SI nCont [ nTClaves; |HACIENDO nCont = nCont + 1;
                    ||ARA

                    nRealCla = nCont + 1;
                    aAux = (nRealCla + "a") % 102;
                    eaTres = aAux + ". CLAVE:";

                    aQueQuiero = "|K" + (("000" + nCont) % -103);
                    aAlfa1     = #referen@#aQueQuiero# % 103;
                    nTCompo    = aAlfa1;
                    aAux = (nTCompo + " ") % 102;
                    || eaTres = eaTres + " (" + aAux + " Campos).";
                    |PARA nCompo = 1;  |SI nCompo [ nTCompo;  |HACIENDO nCompo = nCompo + 1;
                          nPosi    = (nCompo * 300) + 103;
                          aAlfa1   = #referen@#aQueQuiero# % nPosi;
                          nNume    = aAlfa1;
                          aAlfa1   = ("000" + nNume) % -103;
                          eaTres   = eaTres + " " + aAlfa1;
                    |SIGUE;
                    enDos = 2;
                    |PRINT;
               |SIGUE;
          |FINSI;
          |DESCARGA_DEF #referen@;
          enDos = 0;
     |FINSI;
     nSwPie = 1;
|FPRC;

|PROCESO dsinfo02;
     |SI #dsinfo02#3 = "S";
          eaDef = #dsinfo02#1;
          |QBF eaDef;
          eaNomDef = #dsinfo02#2;
          |QBF eaNomDef;
          |HAZ DatosDef;
     |FINSI;
|FPRC;

|DEFBUCLE dsinfo02;
 #dsinfo02#1;
 ;
 #dsinfo01#0, "        ";
 #dsinfo01#0, "zzzzzzzz";
 ;
 NULL, dsinfo02;
|FIN;

|PROCESO SacaMenu;
     aFicMenu = aPathApli + aAplica + ".men";
     |XABRE "U", aFicMenu, nHandle;
     |SI FSalida ] 0;
          |XLEE nHandle, 250, aLinea;
          |SI FSalida ] 0;
               eaTitulo = aLinea % 0399;
               |QBF eaTitulo;
          |FINSI;
          |XCIERRA nHandle;
     |SINO;
          aFicMenu = aPathApli + "*.men";
          |_PDIR aFicMenu;
          |SI FSalida ] 0;
               |XABRE "U", aFicMenu, nHandle;
               |SI FSalida ] 0;
                    |XLEE nHandle, 250, aLinea;
                    |SI FSalida ] 0;
                         eaTitulo = aLinea % 0899;
                         |QBF eaTitulo;
                    |FINSI;
                    |XCIERRA nHandle;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Imprimir; |TIPO 4;
     |IMPRE 0;
     |SI FSalida = 0;
          aInforme = "dsinfo01";
          |INFOR aInforme;
          |SI FSalida = 0;
               |HAZ SacaMenu;
               |BUCLE dsinfo02;
               |SI nSwPie = 1;
                    |PIEINF;
               |FINSI;
               |FININF;
          |SINO;
               aMensaje = "0000Informe " + aInforme + " inaccesible.";
               |MENAV aMensaje;
          |FINSI;
          |FINIMP;
     |SINO;
          |MENAV "0000Proceso cancelado.";
     |FINSI;
|FPRC;

|PROCESO EstaApli; |TIPO 0;
     ||Ver si existe el directorio ... sino |ERROR
     ||ARA
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          aAplica = #dsinfo01#0;
          |QBF aAplica;
          |SI aAplica ! "";
               |QUE_SISTEMA aSistema;
               |SI aSistema = "ESDOS";
                    aSistema = "ESDOS";
               |SINO;
                    aSistema = "ESUNIX";
               |FINSI;
               |DBASS aBase;
               |QBF aBase;

               |SI aSistema = "ESUNIX";
                    aPathApli = aBase + aAplica + "/";
                    |XABRE "E", aPathApli, nHandleDir;
                    |SI FSalida ! 0;
                         #dsinfo01#2 = "";
                         |PINTA #dsinfo01#2;
                         aMensaje = "0000No existe la Aplicacion: " + aAplica;
                         |MENAV aMensaje;
                         |ERROR;
                    |SINO;
                         |HAZ SacaMenu;
                         #dsinfo01#2 = eaTitulo;
                         |PINTA #dsinfo01#2;
                    |FINSI;
               |SINO;
                    aPathApli = aBase + aAplica + "/";
                    eaTitulo = "";
                    |HAZ SacaMenu;
                    #dsinfo01#2 = eaTitulo;
                    |PINTA #dsinfo01#2;
                    |QBF eaTitulo;
                    |SI eaTitulo = "";
                         aAlfa5 = #dsinfo01#0;
                         |QBF aAlfa5;
                         aMensaje = "0000NNo encuentro Descripcion Aplicacion. Posiblemente no exista la aplicacion. Continuar?";
                         |CONFI aMensaje;
                         |SI FSalida ! 0;
                              |PTEC 807;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |SINO;
               #dsinfo01#2 = "";
               |PINTA #dsinfo01#2;
               aMensaje = "0000No ha indicado ninguna Aplicacion.";
               |MENAV aMensaje;
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     aMensaje = "0000SImprimir S/N?";
     |CONFI aMensaje;
     |SI FSalida = 0;
          |HAZ Imprimir;
     |FINSI;
|FPRC;
