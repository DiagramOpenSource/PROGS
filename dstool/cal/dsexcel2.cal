|FICHEROS;
     dspm0000 #0;
     dspm0003 #3;
     dspm0004 #4;
     dspm0015 #15;
     dspw0008 #99,MANTE;
|FIN;

|VARIABLES;
     aBase       = "";
     aIPServ     = "";
     aServCorreo = "";
     aFrom       = "";
     aPwd        = "";
     aNombre     = "";
     aAlfa       = "";
     aFichero    = "";
     aOrigen     = "";
     aDestino    = "";
     aCampoA     = "";
     aCampoB     = "";
     aCampoC     = "";
     aCampoD     = "";
     aUsuario    = "";
     aFecha      = "";
     aTo         = "";
     aMensaje    = "";
     aAsunto     = "";

     nLinea      = 0;
     nCorreos    = 0;
     nCampo      = 0;
     nSegundo    = 0;
     nTipoBucle  = 0;
     nBorra      = 0;

     &eaUsuario     = "";
     &eaIPUsuario   = "";
     &eaMailInterno = "";
|FIN;

|PROCESO Tipo11w08;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     |LEE 101#99=;

     |SI #99#9 = "BORRA";
         #99#9 = #99#10;
     |SINO;
         #99#9 = "BORRA";
     |FINSI;

     |PINTA #99#9;
     |GRABA 020#99a;
     |LIBERA #99;
|FPRC;

|PROCESO CargaTempo1;
     |ABRE #4;
     |SELECT #4#4;
     #4#10 = aCampoA;
     |LEE 040#4=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;
     |CIERRA #4;

     |ABRE #3;
     aAlfa  = (aCampoB % 102) + "." + (aCampoB % 402) + "." + (aCampoB % -104);
     #3#0   = aAlfa;
     #3#1   = #4#0;

     aAlfa  = aCampoC % 102;  #3#2  = aAlfa;
     |SI #3#2 > 23;  #3#2 = 0;  |FINSI;

     aAlfa  = aCampoC % 402;  #3#3  = aAlfa;
     |SI #3#3 > 59;  #3#3 = 59;  |FINSI;

     aAlfa  = aCampoC % 702;  #3#4  = aAlfa;
     nSegundo = #3#4;
     |PARA;  |SI;  |HACIENDO;
          #3#4 = nSegundo;
          |LEE 040#3=;
          |SI FSalida ! 0;  |SAL;  |FINSI;

          nSegundo = nSegundo + 1;
     |SIGUE;
     |CIERRA #3;

     #99#0    = #3#0;
     #99#1    = #4#0;
     #99#2    = #3#2;
     #99#3    = #3#3;
     #99#4    = nSegundo;
     nSegundo = #99#4;
     |LEE 040#99=;
     |PARA;  |SI;  |HACIENDO;
          #99#4 = nSegundo;
          |LEE 040#99=;
          |SI FSalida ! 0;  |SAL;  |FINSI;

          nSegundo = nSegundo + 1;
     |SIGUE;

     |DDEFECTO #99;
     #99#0  = #3#0;
     #99#1  = #4#0;
     #99#2  = #3#2;
     #99#3  = #3#3;
     #99#4  = nSegundo;
     #99#5  = aCampoD;
     |SI #99#5 = 1;  #99#5 = 0;  |FINSI;

     |SI #99#5 = 0;   #99#6 = "Presente";   |FINSI;
     |SI #99#5 = 3;   #99#6 = "No est ";    |FINSI;
     |SI #99#5 = 4;   #99#6 = "Ocioso";     |FINSI;
     |SI #99#5 = 5;   #99#6 = "Vacaciones"; |FINSI;
     |SI #99#5 = 6;   #99#6 = "Enfermedad"; |FINSI;
     |SI #99#5 = 7;   #99#6 = "Otros";      |FINSI;
     |SI #99#5 = 10;  #99#6 = "Fin Tiquet"; |FINSI;

     |SI #99#5 = 98;
         #99#5 = 4;
         #99#6 = "Comiendo";
     |FINSI;

     |SI #99#5 = 99;
         #99#5 = 4;
         #99#6 = "Fin de Comida";
     |FINSI;

     eaUsuario = #4#0;
     |HAZ BuscaIPUsuario;

     #99#7  = eaIPUsuario;
     #99#8  = #0#20 + "." + #0#21 + "." + #0#22 + "." + #0#23;
     #99#9  = "EXCEL";
     #99#10 = "EXCEL";
     |GRABA 020#99n;
     |LIBERA #99;
|FPRC;

|PROCESO CargaTempo2;
     #99#0  = #3#0;
     #99#1  = #3#1;
     #99#2  = #3#2;
     #99#3  = #3#3;
     #99#4  = #3#4;
     #99#5  = #3#5;
     #99#6  = #3#6;
     #99#7  = #3#7;
     #99#8  = #3#8;
     #99#9  = "CALCU";
     #99#10 = "CALCU";
     |GRABA 020#99n;
     |LIBERA #99;
|FPRC;

|DEFBUCLE dspm0003;
     #3#1;
     ;
     #99#0, #99#1, 00, 00, 00;
     #99#0, #99#1, 99, 99, 99;
     ;
     NULL, CargaTempo2;
|FIN;

|PROCESO CargaRectifica;
     |SI nTipoBucle = 1;
         |SI aFecha ! #99#0;  |O aUsuario ! #99#1;
             |BUCLE dspm0003;
             aFecha   = #99#0;
             aUsuario = #99#1;
         |FINSI;
     |FINSI;

     |SI nTipoBucle = 2;
         #3#0 = #99#0;
         #3#1 = #99#1;
         #3#2 = #99#2;
         #3#3 = #99#3;
         #3#4 = #99#4;
         |LEE 101#3=;
         |SI FSalida ! 0;
             |SI #99#9 = "BORRA";  |ACABA;  |FINSI;
             |DDEFECTO #3;
             #3#0 = #99#0;
             #3#1 = #99#1;
             #3#2 = #99#2;
             #3#3 = #99#3;
             #3#4 = #99#4;
             |GRABA 020#3b;
         |SINO;
             |SI #99#9 = "BORRA";
                 |BORRA 020#3a;
                 |LIBERA #3;
                 |ACABA;
             |FINSI;
         |FINSI;

         #3#5 = #99#5;
         #3#6 = #99#6;
         #3#7 = #99#7;
         #3#8 = #99#8;
         |GRABA 020#3a;
         |LIBERA #3;
     |FINSI;
|FPRC;

|DEFBUCLE dspw0008;
     #99#1;
     ;
     "01.01.0000", "          ", 00, 00, 00;
     "31.12.2999", "zzzzzzzzzz", 99, 99, 99;
     ;
     NULL, CargaRectifica;
|FIN;

|RUTINA ClaveBaja99;
     #99#0 = "01.01.0000";
     #99#1 = "          ";
     #99#2 = 00;
     #99#3 = 00;
     #99#4 = 00;
|FRUT;

|RUTINA ClaveAlta99;
     #99#0 = "31.12.2999";
     #99#1 = "zzzzzzzzzz";
     #99#2 = 99;
     #99#3 = 99;
     #99#4 = 99;
|FRUT;

|PROCESO CargaHoja;
     nCampo = 2;
     |PARA; |SI; |HACIENDO;
           aCampoA = "A" + nCampo;
           |ALFACALLDLL "agixl97.dll", "peek_dato", aCampoA;
           |QBF aCampoA;
           |SI aCampoA = "";  |SAL;  |FINSI;

           aCampoB = "B" + nCampo;
           |ALFACALLDLL "agixl97.dll", "peek_dato", aCampoB;
           |QBF aCampoB;

           aCampoC = "C" + nCampo;
           |ALFACALLDLL "agixl97.dll", "peek_dato", aCampoC;
           |QBF aCampoC;

           aCampoD = "D" + nCampo;
           |ALFACALLDLL "agixl97.dll", "peek_dato", aCampoD;
           |QBF aCampoD;

           |HAZ CargaTempo1;

           nCampo = nCampo + 1;
     |SIGUE;

     aFecha     = "";
     aUsuario   = "";
     nTipoBucle = 1;

     |BUCLE dspw0008;

     |PUSHV 0501 2380;
     |ENTLINEAL #1#99, -1, 4, 22, 1, ClaveBaja99, ClaveAlta99;
     |POPV;

     nBorra = 0;
     |CONFI "0000NConforme con lo introducido";
     |SI FSalida = 0;
         nTipoBucle = 2;
         |ABRE #3;
         |BUCLE dspw0008;
         |CIERRA #3;
         nBorra = 1;
     |FINSI;
|FPRC;

|PROCESO MandaCorreoAura;
     eaUsuario   = "MARIA";
     |HAZ LeeElUsuario;
     aFrom       = eaMailInterno;

     eaUsuario   = "AURA";
     |HAZ LeeElUsuario;
     aTo         = eaMailInterno;

     aIPServ     = #0#8 + "." + #0#9 + "." + #0#10 + "." + #0#11;
     aServCorreo = #0#12;
     aAsunto     = "Hoja Excel de Rectificaciones";
     aMensaje    = "Hoja excel de rectificaciones actualizada en la base de datos.";

     |QBT aIPServ;
     |QBF aServCorreo;
     |QBF aFrom;
     |QBF aAsunto;
     |QBF aMensaje;
     |QBF aTo;

     |DSCORREO_ENVIA aIPServ, aServCorreo, aFrom, aTo, aAsunto, aMensaje, aOrigen;
|FPRC;

|PROCESO Tipo11;  |TIPO 11;
     |SI #15#1 = "               ";  |ACABA;  |FINSI;

     |SI FSalida = 10;
         aOrigen  = aBase + #15#1;           |QBF aOrigen;
         aDestino = "C:\DOCUMENTOS";         |RMKDIR aDestino;
         aDestino = aDestino + "\" + #15#1;  |QBF aDestino;
         |ENVIA_FICHERO aOrigen, aDestino;

         |CARGADLL "agixl97.dll";
         |SI FSalida < 0;
             |MENAV "0000No se puede usar agixl97.dll";
             |ACABA;
         |FINSI;

         |ALFACALLDLL "agixl97.dll", "carga_book", aDestino;
         |ALFACALLDLL "agixl97.dll", "selecciona_hoja", "Hoja1";

         nBorra   = 0;
         aUsuario = ("08" + Usuario + "zzzzzzzz") % 108;
         |NOME_DAT #99 aUsuario;
         |ABRE #99;

         |HAZ CargaHoja;

         |CIERRA #99;
         |DELINDEX #99;

         ||ALFACALLDLL "agixl97.dll", "fin_book", "salva";
         |DESCARGADLL "agixl97.dll";

         |RFBORRA aDestino;

         |SI nBorra = 1;
             |HAZ MandaCorreoAura;

             |FBORRA aOrigen;

             |LEE 101#15=;
             |SI FSalida = 0;
                 |BORRA 020#15a;
                 |LIBERA #15;

                 |PONCONTROL 23, "SI";
                 |PTEC 808;
             |FINSI;
         |FINSI;
     |FINSI;

     |SI FSalida = 13;
         |CONFI "0000NSeguro de Borrar el Correo.";
         |SI FSalida ! 0;  |ACABA;  |FINSI;

         aOrigen  = aBase + #15#1;           |QBF aOrigen;
         |FBORRA aOrigen;

         |LEE 101#15=;
         |SI FSalida = 0;
             |BORRA 020#15a;
             |LIBERA #15;

             |PONCONTROL 23, "SI";
             |PTEC 808;
         |FINSI;
     |FINSI;
|FPRC;

|RUTINA ClaveBaja15;
     #15#0 = 1;
|FRUT;

|RUTINA ClaveAlta15;
     #15#0 = 99999999;
|FRUT;

|PROGRAMA;
     |ABRE #0;
     |LEE 040#0p;
     |SI FSalida ! 0;  |DDEFECTO #0;  |FINSI;
     |CIERRA #0;

     aBase = "";
     |DBASE aBase;  |QBF aBase;
     aBase = aBase + "rectific/";  |MKDIR aBase;

     aIPServ     = #0#8 + "." + #0#9 + "." + #0#10 + "." + #0#11;
     aServCorreo = #0#12;
     aFrom       = "dsp";
     aPwd        = "dsp";
     aNombre     = "";
     nCorreos    = 0;

     |QBF aServCorreo;

     nLinea = 0;
     |ABRE #15;
     |LEE 040#15u;
     |SI FSalida = 0; nLinea = #15#0;  |FINSI;

     |PARA; |SI; |HACIENDO;
          |DSCORREO_RECIBE aIPServ, aServCorreo, aFrom, aPwd, aBase, aNombre;
          |SI FSalida ! 0;  |SAL;  |FINSI;

          aFichero = aNombre - aBase;
          aAlfa = (aFichero % -103) > " ";
          |SI aAlfa = "XLS";
              nCorreos = nCorreos + 1;
              nLinea   = nLinea + 1;

              #15#0 = nLinea;
              #15#1 = "xls" + (("00000000" + nLinea) % -108) + ".xls";
              |GRABA 020#15n;
              |LIBERA #15;

              aOrigen  = aNombre;
              aDestino = aBase + #15#1;  |QBF aDestino;
              |RENOMBRA_FICHERO aOrigen, aDestino;
          |FINSI;
     |SIGUE;
     |CIERRA #15;

     |SI nCorreos ! 0;
         aAlfa = "0000 Recibidos " + nCorreos + " correos nuevos.";
     |SINO;
         aAlfa = "0000 No han habido correos nuevos.";
     |FINSI;

     |MENAV aAlfa;

     |ENTLINEAL #1#15, -1, 4, 22, 1, ClaveBaja15, ClaveAlta15;
|FPRO;
