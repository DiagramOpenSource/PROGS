|FICHEROS;
     :/agitool/def/xrwindex #0;
     :/agitool/def/xrwintmp #1;
     referen@;
|FIN;

|VARIABLES;
     nPanta = 0;
     aTecla = "";
     aAlfa = "";
     aEsc1 = "";
     aEsc2 = "";
     aRetu = "";
     nGrid = 0;
     nRango = 0;
     nLbl = 0;
     nBtnSalBus = 0;
     nBtnSigue = 0;
     nBtnIniFich = 0;
     nBtnIndFich = 0;
     nBtnCopFich = 0;
     nBtnBusca = 0;
     nBtnIniTodo = 0;
     nBtnIndTodo = 0;
     nBtnCopTodo = 0;
 {-1}aVent = "";
     aVent1 = 0;
     aVent2 = 0;
     aVent3 = 0;
     aVent4 = 0;
     aVent5 = "";
     nVentana = 0;
     nBytes = 0;
     aClave = "";
     aDirEmp = "";
     aDirDat = "";
     aDirBas = "";
     aDirDef = "";
     aEmp = "";
     aDef = "";
     aApli = "";
     aEmpDes = "";
     aNomEmp = "";
     nNume = 0;
     nPos = 0;
     aOrg = "";
     aDes = "";
     aExt = "";
     aFic = "";
     nSal = 0;
     aBus = "";
     aActual = "";
     nHay = 0;
|FIN;

|PROCESO FicDatos;
     aDef = #1#0; |QBF aDef;
     aAlfa = aDirDef + aDef;
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          aAlfa = "|FICHERO";
          aFic = #referen@#aAlfa#;
          aFic = aFic - aDirDat;
          |SI FSalida [ 0;
               aFic = "";
               || El fichero de datos es de otra aplicacion
               || esta definido como ":/aplicacion/fichero"
          |FINSI;
          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;

|PROCESO QuitaRepe;
     |HAZ FicDatos;
     aDef = #1#0; |QBF aDef;
     |SI aFic ! ""; |Y aFic ! aDef;
          |SALVA_FICHA #1;
          #1#0 = aFic;
          |LEE 000#1=;
          nSal = FSalida;
          |REPON_FICHA #1;
          |SI nSal = 0;
               |BORRA 020#1a;
               #0#0 = #0#0 - #1#2;
               #0#1 = #0#1 - 1;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE QuitaRepe;
     #1#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, QuitaRepe;
|FIN;

|PROCESO CopiaFic;
     aOrg = aDirDat + aDef + aExt;
     aDes = aDirEmp + aEmpDes + "/" + aDef + aExt;
     |COPIA_FICHERO aOrg, aDes;
|FPRC;

|PROCESO Borra;
     aAlfa = aDirEmp + aEmpDes + "/" + aDef + ".ixx"; |FBORRA aAlfa;
     aAlfa = aDirEmp + aEmpDes + "/" + aDef + ".dat"; |FBORRA aAlfa;
     aAlfa = aDirEmp + aEmpDes + "/" + aDef + ".dbf"; |FBORRA aAlfa;
     aAlfa = aDirEmp + aEmpDes + "/" + aDef + ".ddx"; |FBORRA aAlfa;
|FPRC;

|PROCESO Copia;
     |HAZ FicDatos;
     |SI aFic = ""; |ACABA; |FINSI;

     aDef = aFic;                || se copia el fichero de datos
||     aDef = #1#0; |QBF aAlfa;  || y no el def

     |HAZ Borra;
     aExt = ".ixx"; |HAZ CopiaFic;
     |SI FSalida = 0;
          aExt = ".dat"; |HAZ CopiaFic;
          |SI FSalida = 0;
               aExt = ".ddx"; |HAZ CopiaFic;
          |SINO;
               aExt = ".dbf"; |HAZ CopiaFic;
          |FINSI;
     |FINSI;
     |SI FSalida ! 0;
          |HAZ Borra;
          aAlfa = "0000Error al copiar: '" + aDef + "'";
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|DEFBUCLE Copia;
     #1#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Copia;
|FIN;

|PROCESO Indexa;
     aDef = #1#0; |QBF aDef;
     |GRABAENDEF aDef, "";
|FPRC;

|DEFBUCLE Indexa;
     #1#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Indexa;
|FIN;

|PROCESO Inicializa;
     aDef = #1#0; |QBF aDef;
     aAlfa = aDirDat + aDef + ".dat"; |FBORRA aAlfa;
     aAlfa = aDirDat + aDef + ".dbf"; |FBORRA aAlfa;
     aAlfa = aDirDat + aDef + ".ixx"; |FBORRA aAlfa;
     aAlfa = aDirDat + aDef + ".ddx"; |FBORRA aAlfa;
     #0#0 = #0#0 - #1#2;
     nBytes = 0;
     |LEEDEDEF aDef, "";
     |SI FSalida ] 0;
          aAlfa = FSalida % 375;
          nBytes = (A\aAlfa % 115);
     |FINSI;
     #1#2 = nBytes;
     #1#3 = 0;
     #0#0 = #0#0 + #1#2;
     |GRABA 020#1a;
|FPRC;

|DEFBUCLE Inicializa;
     #1#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Inicializa;
|FIN;

|PROCESO NomEmp;
     aNomEmp = "***";
     aDef = "";
     aAlfa = aApli % 107;
     |SI aAlfa = "dscomer";
          aDef = "agifa041";
     |SINO;
          |SI aAlfa = "agicont"; aDef = "canempre"; |FINSI;
     |FINSI;
     |SI aDef ! "";
          aAlfa = aDirBas + "def/" + aDef;
          |CARGA_DEF aAlfa, referen@;
          |SI FSalida = 0;
               aAlfa = aDirBas + "fich/";
               |PATH_DAT #referen@#aAlfa#;
               |ABRE #referen@;
               aAlfa = aApli % 107;
               |SI aAlfa = "dscomer";
                    #referen@#0 = aEmpDes;
               |SINO;
                    aEmpDes = ("000000" + aEmpDes) % -106;
                    aAlfa = aEmpDes % 105; #referen@#2 = aAlfa;
                    aAlfa = aEmpDes % -101; #referen@#3 = aAlfa;
               |FINSI;
               |LEE 000#referen@.=;
               |SI FSalida = 0;
                    aNomEmp = #referen@#1; |QBF aNomEmp;
               |FINSI;
               |CIERRA #referen@;
               |DESCARGA_DEF #referen@;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO BtnIniFich;
     |HAZ Clave;
     |SI FSalida = 0;
          aAlfa = "0000N¨Esta seguro de inicializar el fichero: '" + #1#0;
          |QBF aAlfa;
          aAlfa = aAlfa + "'?";
          |CONFI aAlfa;
          |SI FSalida = 0;
               |HAZ Inicializa;
               |PINTA #0#0;
               |REFRESCACONTROL nGrid;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO BtnIndFich;
     |HAZ Indexa;
|FPRC;

|PROCESO BtnCopFich;
     |HAZ Empresa;
     |SI aEmpDes ! "";
          |HAZ NomEmp;
          |SI aNomEmp = "***";
               FSalida = 0;
          |SINO;
               |SI aNomEmp = "";
                    aAlfa = "0000NC¢digo inaccesible." + &13 + "¨Es correcto?";
               |SINO;
                    aAlfa = "0000N-" + aNomEmp + "-" + &13 + "¨Es correcto?";
               |FINSI;
               |CONFI aAlfa;
          |FINSI;
          |SI FSalida = 0;
               |CONFI "0000N¨Esta seguro de eliminar el fichero en el destino?";
               |SI FSalida = 0;
                    |HAZ Copia;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO BtnIniTodo;
     |HAZ Clave;
     |SI FSalida = 0;
          aAlfa = "0000N¨Esta seguro de inicializar todo?";
          |CONFI aAlfa;
          |SI FSalida = 0;
               |PULSATECLA;
               |BUCLE Inicializa;
               |PINTA #0#0;
               |REFRESCACONTROL nGrid;
               |MENAV "0000Inicializaci¢n de ficheros terminada...";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO BtnIndTodo;
     |CONFI "0000N¨Esta seguro de indexar todos los ficehros?";
     |SI FSalida = 0;
          |PULSATECLA;
          |BUCLE Indexa;
          |MENAV "000oIndexaci¢n de ficheros terminada...";
     |FINSI;
|FPRC;

|PROCESO BtnCopTodo;
     |HAZ Empresa;
     |SI aEmpDes ! "";
          |HAZ NomEmp;
          |SI aNomEmp = "***";
               FSalida = 0;
          |SINO;
               |SI aNomEmp = "";
                    aAlfa = "0000NC¢digo inaccesible." + &13 + "¨Es correcto?";
               |SINO;
                    aAlfa = "0000N-" + aNomEmp + "-" + &13 + "¨Es correcto?";
               |FINSI;
               |CONFI aAlfa;
          |FINSI;
          |SI FSalida = 0;
               |CONFI "0000N¨Esta seguro de eliminar los ficheros en el destino?";
               |SI FSalida = 0;
                    |PULSATECLA;
                    |BUCLE Copia;
                    |MENAV "0000Copia de ficheros terminada...";
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Empresa;
     |VENTANA 1236, 1661, -1, 17, "Empresa a copiar";
     nVentana = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |CONTROL_SIMPLE 0, "LABEL,{{013}}Empresa:", 1437, 1447, NULL;
     nLbl = FSalida;
     aEmpDes = "";
     nNume = (N\ (A\ aEmp % 0) - 1);

     |PARA; |SI; |HACIENDO;
          E_sup = nNume;
          E_inf = "";
          aEmpDes = 1449 ? 1; |QBF aEmpDes;
          |SI FSalida ! 0; aEmpDes = ""; |SAL; |FINSI;
          nPos = -(100 + nNume)
          aAlfa = ("0" * nNume) + aEmpDes;
          aEmpDes = aAlfa % nPos;
          aAlfa = aEmpDes + "/";
          |SI aAlfa = aEmp;
               |MENAV "0000No se puede copiar a la empresa actual...";
          |SINO;
               |SAL;
          |FINSI;
     |SIGUE;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FIN_CONTROL_WINDOWS nLbl;
     |FINVENTANA nVentana;
     |PULSATECLA;
|FPRC;

|PROCESO Clave;
     |SI aClave = "<MYPASWD> ";
          FSalida = 0;
     |SINO;
          |VENTANA 1236, 1661, -1, 17, "Introduzca la Clave";
          nVentana = FSalida;

          aVent1 = -1;
          aVent2 = -1;
          aVent3 = 0;
          aVent4 = nVentana;
          aVent5 = "MODAL";
          |ESPECIFICA "ESTADO_VENTANA", aVent;

          |CONTROL_SIMPLE 0, "LABEL,{{013}}Clave:", 1437, 1443, NULL;
          nLbl = FSalida;

          aClave = "";
          E_sup = 10;
          E_inf = "";
          aClave = 1445 ? -1;

          aVent1 = -1;
          aVent2 = -1;
          aVent3 = 0;
          aVent4 = nVentana;
          aVent5 = "MODELESS";
          |ESPECIFICA "ESTADO_VENTANA", aVent;

          |FIN_CONTROL_WINDOWS nLbl;
          |FINVENTANA nVentana;
          |PULSATECLA;

          |SI aClave = "<MYPASWD> ";
               FSalida = 0;
          |SINO;
               |MENAV "0000Clave incorrecta...";
               FSalida = 1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO BtnSalBus;
     |PTEC 807;
|FPRC;

|PROCESO BtnSigue;
     |PTEC 806;
|FPRC;

|PROCESO BtnBusca;
     |VENTANA 1230, 1766, -1, 17, "Buscar fichero";
     nVentana = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |CONTROL_SIMPLE 0, "LABEL,{{013}}Fichero:", 1430, 1440, NULL;
     nLbl = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,{{207}}Salir", 1655, 1665, BtnSalBus;
     nBtnSalBus = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,{{202}}Siguiente", 1644, 1654, BtnSigue;
     nBtnSigue = FSalida;
  |ET Otro;
     |SI nHay = 0;
          |ESTADO_CONTROL nBtnSigue, "DISABLE";
     |SINO;
          |ESTADO_CONTROL nBtnSigue, "ENABLE";
     |FINSI;
     E_sup = 20;
     E_inf = "";
     aBus = 1442 ? 1; |QBF aBus;
     |SI FSalida ! 7;
          aDef = #1#0;
          aActual = #1#0;
          nHay = 0;
          #1#0 = aBus;
          |LEE 000#1=;
          |SI FSalida = 0;
               nHay = 1;
          |SINO;
               #1#0 = aDef;
               |LEE 000#1=;
               aDef = aBus > "";
               |LEE 000#1s;
               |PARA FSalida = 0; |SI FSalida = 0; |HACIENDO;
                    aAlfa = #1#0; aAlfa = aAlfa > "";
                    aAlfa = aAlfa - aDef;
                    |SI FSalida > 0;
                         nHay = 1;
                         |SAL;
                    |SINO;
                         aAlfa = #1#1; aAlfa = aAlfa > "";
                         aAlfa = aAlfa - aDef;
                         |SI FSalida > 0;
                              nHay = 1;
                              |SAL;
                         |FINSI;
                    |FINSI;
                    |LEE 000#1s;
               |SIGUE;
               |SI nHay = 0;
                    |LEE 000#1p;
                    |PARA FSalida = 0; |SI FSalida = 0; |HACIENDO;
                         aAlfa = #1#0; aAlfa = aAlfa > "";
                         aAlfa = aAlfa - aDef;
                         |SI FSalida > 0;
                              nHay = 1;
                              |SAL;
                         |SINO;
                              aAlfa = #1#1; aAlfa = aAlfa > "";
                              aAlfa = aAlfa - aDef;
                              |SI FSalida > 0;
                                   nHay = 1;
                                   |SAL;
                              |FINSI;
                         |FINSI;
                         |LEE 000#1s;
                    |SIGUE;
               |FINSI;
          |FINSI;
          |SI nHay = 0;
               #1#0 = aActual;
               |LEE 000#1=;
          |FINSI;
          |REFRESCACONTROL nGrid;
          |VETE Otro;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FIN_CONTROL_WINDOWS nLbl;
     |FIN_CONTROL_WINDOWS nBtnSalBus;
     |FIN_CONTROL_WINDOWS nBtnSigue;
     |FINVENTANA nVentana;
     |PULSATECLA;
|FPRC;

|PROCESO Evt;
     |SI FSalida = 1; |O FSalida = 2;
          |LEE 000#1=;
     |FINSI;
|FPRC;

|PROCESO T80; |TIPO 80;
     FSalida = 3099;
|FPRC;

|PROGRAMA;
     |DBASE aDirBas;
     |DFICB aDirEmp;
     |DFICE aDirDat;
     |DDEF aDirDef;
     |NOMAP aApli;
     aEmp = aDirDat - aDirEmp;

     |DBASS aAlfa; aAlfa = aAlfa + "/agitool/pan/";
     |PATH_PAN #0 aAlfa;
     |PINPA #0#0; nPanta = FSalida;
     |PINDA #0#0;

     |CONTROL_SIMPLE nPanta, "LABEL,{{014}}Cargando datos...", 1526, 1870, NULL;
     nLbl = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{137}}Inicializa Fichero", 0802, 0816, BtnIniFich;
     nBtnIniFich = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{137}}Indexa Fichero", 0817, 0831, BtnIndFich;
     nBtnIndFich = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{137}}Copia Fichero", 0832, 0846, BtnCopFich;
     nBtnCopFich = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{155}}", 0849, 0852, BtnBusca;
     nBtnBusca = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{137}}Inicializa Todo", 0855, 0869, BtnIniTodo;
     nBtnIniTodo = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{137}}Indexa Todo", 0870, 0884, BtnIndTodo;
     nBtnIndTodo = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{137}}Copia Todo", 0885, 0899, BtnCopTodo;
     nBtnCopTodo = FSalida;

     |SI aEmp = "";
          |ESTADO_CONTROL nBtnCopFich, "DISABLE";
          |ESTADO_CONTROL nBtnCopTodo, "DISABLE";
     |FINSI;

     |PULSATECLA;

     aAlfa = Usuario;
     |NOME_DAT #1 aAlfa; |DELINDEX #1; |ABRET #1;
     |HAZ CargaFic;

     |BUCLE QuitaRepe;
     |LEE 000#1p;
     |SI FSalida = 0;
          |PINTA #0#0;
          |PINTA #0#1;
          nRango = 4 + 8 + 16 + 32;
          |LINEAL_SIMPLE #1#1, nRango, 1002, 2798, NULL, NULL, Evt;
          nGrid = FSalida;
          |FIN_CONTROL_WINDOWS nLbl;
          aEsc1 = &255 + "806";
          aEsc2 = &255 + "807";
          aRetu = &255 + "802";
          aAlfa = "::{{001}}Salir,";
          |PARA; |SI;  |HACIENDO;
               FSalida = aAlfa + nGrid;
               |LEETECLA aTecla;
               |SI aTecla = aEsc1; |SAL;  |FINSI;
               |SI aTecla = aEsc2; |SAL;  |FINSI;
               |SI aTecla = aRetu; |SAL; |FINSI;
          |SIGUE;
          |DESTRUYECONTROL nGrid;
          |FIN_CONTROL_WINDOWS nBtnIniFich;
          |FIN_CONTROL_WINDOWS nBtnIndFich;
          |FIN_CONTROL_WINDOWS nBtnCopFich;
          |FIN_CONTROL_WINDOWS nBtnBusca;
          |FIN_CONTROL_WINDOWS nBtnIniTodo;
          |FIN_CONTROL_WINDOWS nBtnIndTodo;
          |FIN_CONTROL_WINDOWS nBtnCopTodo;
     |SINO;
          |MENAV "0000No hay ficheros a procesar...";
     |FINSI;
     |CIERRAT #1; |DELINDEX #1;
     |PULSATECLA;
|FPRO;
