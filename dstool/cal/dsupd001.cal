|FICHEROS;
     dsupd001 #0;
     dsupd002 #2,MANTE;
     dsupd003 #3,MANTE;
     dsupd004 #4,MANTE;

     dsparm01 #100;      ||Control Aplicacion
     dsparm03 #103;      ||Aplicaciones
     agifa024 #24;       ||Clientes
     ma00001@ #1;        ||AGIsys Cabs. en DSMAN9
|FIN;

|VARIABLES;
     aMensaje = "";
     aPath = "";
     aPathCom = "";
     nEmp = 0;
     aEmp = "";
     aEmpCom = "";
     aFic = "";
     aDatos = "";
     aDatosCom = "";
     nSwCargaDef = 0;
     nModo = 0;
|FIN;

|PROCESO F11; |TIPO 66;
     ||Mostrando el F11 del AGISYS
     |ABRE #ma00001@;
     |CONSULTA_CLAVE #1#1;
     |SI FSalida = 0;
          #dsupd001#0 = #1#0;
          |PINTA #dsupd001#0;
          |LEE 000#dsupd001.=;
          |SI FSalida = 0;
               |PINDA #0#0;
          |FINSI;
     |FINSI;
     |CIERRA #ma00001@;
|FPRC;

|PROCESO Inicio; |TIPO 8;
     |ABRE #dsparm01;
     |DDEFECTO #dsparm01;
     |LEE 000#dsparm01.=;
     |SI FSalida = 0;
          aPath = #dsparm01#5;
          |QBF aPath;
          aPathCom = #dsparm01#3;
          |QBF aPathCom;
          nEmp = #dsparm01#4;
          aEmp = ("00" + nEmp) % -102;
          aEmpCom = ("00000" + #dsparm01#1) % -105;
          aDatos = aPath + "fich/" + aEmp + "/";
          aDatosCom =  aPathCom + "fich/" + aEmpCom + "/";
          aFic = aPath + "def/ma000001";
          |CARGA_DEF aFic, ma00001@;
          |SI FSalida = 0;
               |PATH_DAT #1 aDatos;
               |PATH_DAT #24 aDatosCom;
               nSwCargaDef = 1;
          |SINO;
               aMensaje = "0000Error con CARGA_DEF " + aFic;
               |MENAV aMensaje;
               |PTEC 807;
          |FINSI;
     |SINO;
          aMensaje = "0000Control Aplicacion no definido";
          |MENAV aMensaje;
          |PTEC 807;
     |FINSI;
     |CIERRA #dsparm01;
|FPRC;

|PROCESO Final; |TIPO 9;
     |SI nSwCargaDef = 1;
          |DESCARGA_DEF #ma00001@;
     |FINSI;
|FPRC;

|PROCESO Cliente; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1; |O nModo = 2;
          |SI nSwCargaDef = 1;
               |ABRE #ma00001@;
               #ma00001@#0 = #dsupd001#0;
               |LEE 000#ma00001@.=;
               |SI FSalida = 0;
                    #dsupd001#1 = #ma00001@#3;
                    |PINTA #dsupd001#1;
                    |ABRE #agifa024;
                    #agifa024#0 = #dsupd001#1;
                    |LEE 000#agifa024.=;
                    |SI FSalida = 0;
                         #dsupd001#2 = #agifa024#1;
                         |PINTA #dsupd001#2;
                    |FINSI;
                    |CIERRA #agifa024;
               |SINO;
                    aMensaje = "0000 [" + #dsupd001#0 + "] No existe en Control AGIsys en DSMAN9.";
                    |MENAV aMensaje;
                    |ERROR;
               |FINSI;
               |CIERRA #ma00001@;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO minimo;
     #dsupd003#0 = #dsupd002#0;
     #dsupd003#1 = #dsupd002#1;
     #dsupd003#2 = "         ";
|FPRC;

|PROCESO maximo;
     #dsupd003#0 = #dsupd002#0;
     #dsupd003#1 = #dsupd002#1;
     #dsupd003#2 = "zzzzzzzzz";
|FPRC;

|PROCESO Aplica; |TIPO 0;
     |ABRE #dsparm03;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1; |O nModo = 2;
          #dsparm03#0 = #dsupd002#1;
          |LEE 000#dsparm03.=;
          |SI FSalida = 0;
               #dsupd002#2 = #dsparm03#1;
               |PINTA #dsupd002#2;
               #dsupd002#3 = #dsparm03#2;
          |SINO;
               aMensaje = "0000No existe la aplicacion [" + #dsupd002#1 + "]" + ":" + nModo;
               |MENAV aMensaje;
               |ERROR;
          |FINSI;
     |SINO;
          |SI nModo = 4;
               |PUSHV  0501 2380;
               |ENTLINEAL #1#3, 3, 4, 22, 1, minimo, maximo;
               |POPV;
          |FINSI;
     |FINSI;
     |CIERRA #dsparm03;
|FPRC;

|PROCESO ModAlta; |TIPO 3;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1; |O nModo = 2;
          |PUSHV  0501 2380;
          |ENTLINEAL #1#3, 3, 2, 22, 1, minimo, maximo;
          |POPV;
     |FINSI;
|FPRC;

|PROCESO minObs;
     #dsupd004#0 = #dsupd003#0;
     #dsupd004#1 = #dsupd003#1;
     #dsupd004#2 = #dsupd003#2;
     #dsupd004#3 = 01;
|FPRC;

|PROCESO maxObs;
     #dsupd004#0 = #dsupd003#0;
     #dsupd004#1 = #dsupd003#1;
     #dsupd004#2 = #dsupd003#2;
     #dsupd004#3 = 99;
|FPRC;

|PROCESO Obs; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1; |O nModo = 2;
     |SINO;
          |SI nModo = 4;
               |PUSHV  0501 2380;
               |ENTLINEAL #1#4, 4, 4, 15, 1, minObs, maxObs;
               |POPV;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO ObsMod; |TIPO 3;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1; |O nModo = 2;
          |PUSHV  0501 2380;
          |ENTLINEAL #1#4, 4, 2, 15, 1, minObs, maxObs;
          |POPV;
     |FINSI;
|FPRC;
