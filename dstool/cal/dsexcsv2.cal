          AcotaCampos

|FICHEROS;
     dsexcsv0 #0;
     dsexcsv4 #4;
     dsexcsv7 #7,MANTE;
     refere1@;
|FIN;

|VARIABLES;
     nSal = 0;
 {-1}aMenu   = "";
     aMenu1  = "2400";
     aMenu2  = "1";
     aMenu3  = "";
     aMenu4  = "AC";
     aMenu5  = " Aceptar ";
     aMenu6  = " Cancelar ";

     aH200 = "";
     aD200 = "";
     nLin = 0;
     nForma = 0;
     nLon = 0;
     aAlfa = "";
     aDesde = "";
     aHasta = "";
     nModo = 0;
     aMas = "";
     aPath = "";
     aApli = "";
     aDef = "";
     nNume = 0;
     aCampos = "";
     aCampo = "";
     i = 0;
|FIN;

|PROCESO Acota0; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          |ERROR; |ACABA;
     |FINSI;

     aDesde = #7#12; |QBF aDesde;
     aHasta = #7#13; |QBF aHasta;

     nLon = #7#5;
     |SI nLon > 20; nLon = 20; |FINSI;
     |SI #7#4 = "A";
          |CAMPO_ANCHO #7#6, 1, nLon;
          |CAMPO_ANCHO #7#7, 1, nLon;
          |CAMPO_MAXIMO #7#6, 1, "#";
          |CAMPO_MAXIMO #7#7, 1, "#";
          |C_V #7#8, 1, "N";
          |C_V #7#9, 1, "N";
          |C_V #7#10, 1, "N";
          |C_V #7#11, 1, "N";

          |C_V #7#6, 1, "S";
          |C_V #7#7, 1, "S";
          |PINTA #7#6;
          |PINTA #7#7;
     |FINSI;
     |SI #7#4 = "N";
          |C_V #7#6, 1, "N";
          |C_V #7#7, 1, "N";
          |C_V #7#10, 1, "N";
          |C_V #7#11, 1, "N";

          |C_V #7#8, 1, "S";
          |C_V #7#9, 1, "S";
          |PINTA #7#8;
          |PINTA #7#9;
     |FINSI;
     |SI #7#4 = "F";
          |C_V #7#6, 1, "N";
          |C_V #7#7, 1, "N";
          |C_V #7#8, 1, "N";
          |C_V #7#9, 1, "N";

          |C_V #7#10, 1, "S";
          |C_V #7#11, 1, "S";
          |PINTA #7#10;
          |PINTA #7#11;
     |FINSI;
|FPRC;

|PROCESO Acota2; |TIPO 2;
     nForma = 0 +. 1;
     |SI nForma = 1;
          |SI #7#4 = "A";
               #7#12 = #7#6;
               #7#13 = #7#7;
          |FINSI;
          |SI #7#4 = "N";
               #7#12 = #7#8;
               #7#13 = #7#9;
          |FINSI;
          |SI #7#4 = "F";
               #7#12 = #7#10;
               #7#13 = #7#11;
          |FINSI;

          #4#0 = #7#1;
          #4#1 = #7#2;
          |LEE 121#4=;
          |SI FSalida = 0;
               #4#7 = #7#12;
               #4#8 = #7#13;
               |GRABA 020#4a;
               |LIBERA #4;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Graba7;
     aDesde = #4#7; |QBF aDesde;
     aHasta = #4#8; |QBF aHasta;
     nLin = nLin + 1;
     |DDEFECTO #7;
     #7#0 = nLin;
     #7#1 = #4#0;
     #7#2 = #4#1;
     #7#3 = #4#2;
     #7#4 = #4#5;
     #7#5 = #4#6;
     |SI #4#5 = "A";
          #7#6 = aDesde;
          #7#7 = aHasta;
     |FINSI;
     |SI #4#5 = "N";
          #7#8 = aDesde;
          #7#9 = aHasta;
     |FINSI;
     |SI #4#5 = "F";
          |SI aDesde ! ""; #7#10 = aDesde; |FINSI;
          |SI aHasta ! ""; #7#11 = aHasta; |FINSI;
     |FINSI;
     #7#12 = aDesde;
     #7#13 = aHasta;
     |GRABA 020#7n;
|FPRC;

|PROCESO Acotar;
     |SI #4#4 = "*";
          |HAZ Graba7;
     |FINSI;
|FPRC;

|DEFBUCLE Acotar;
     #4#1;
     ;
     #0#0, aD200;
     #0#0, aH200;
     ;
     NULL, Acotar;
|FIN;

|PROCESO AcotaClave;
     |ABRE #4;
     aMas = #0#5; |QBF aMas;
     aPath = #0#2; |QBF aPath;
     aApli = #0#1; |QBF aApli;
     aDef = aPath + "/" + aApli + "/def/" + aMas; |QBF aDef;
     |CARGA_DEF aDef, refere1@;
     |SI FSalida = 0;
          nNume = #0#7 - 1;
          aAlfa = "|K" + (("000" + nNume) % -103);
          aAlfa = #refere1@#aAlfa#;
          aCampos = aAlfa % 103;
          aAlfa = aAlfa % 4;
          |PARA i = 0; |SI i < aCampos; |HACIENDO i = i + 1;
               aCampo = aAlfa % 103;
               aAlfa = aAlfa % 4;
               |DDEFECTO #4;
               #4#0 = #0#0;
               #4#1 = aCampo;
               |LEE 000#4=;
               |SI FSalida = 0;
                    |HAZ Graba7;
               |SINO;
                    |MENAV "0000Error en seleccion, borre el registro y vuelva a crearlo...";
               |FINSI;
          |SIGUE;
          |DESCARGA_DEF #refere1@;
     |FINSI;
     |CIERRA #4;
|FPRC;

|PROCESO Min7;
     #7#0 = 1;
|FPRC;

|PROCESO Max7;
     #7#0 = 999;
|FPRC;

|PROCESO AcotaCampos;
     aD200 = " " * 200;
     aH200 = "z" * 200;
     nLin = 0;
     |PUSHV 0501 2380;

     |DELINDEX #7;
     |ABRE #7;
     |SI #0#7 ! 0; |HAZ AcotaClave; |FINSI;
     |BUCLE Acotar;
     |CIERRA #7;
     |ABRE #4;
     |PINPA #0#7;
     |ENTLINEAL #1#7, -1, 2, 22, 0, Min7, Max7;
     |CIERRA #4;

     |MENU aMenu;
     nSal = FSalida;
     |POPV;

     FSalida = nSal;
|FPRC;
