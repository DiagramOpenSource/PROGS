|FICHEROS;
     dsconver #0;
     dsconve1 #1,MANTE;
     dsconve2 #2,MANTE;
     dsconve3 #4;
     zmonedas #3;

     fichero@ #100;
|FIN;

|VARIABLES;
     aAlfa        = "";
     aAlfa1       = "";
     aAlfa2       = "";
     aBase        = "";
     aQueQuiero   = "";
     aTitulo      = "";
     aDefFichero  = "";
     aFicFichero  = "";
     aNumCampo    = "";
     aMinCampo    = "";
     aDef         = "";
     aDirDef      = "";
     aDirDat      = "";
     aFichDat     = "";
     aTipoCampo   = "";
     aPath        = "";
     aNombreCampo = "";
     aEsc         = "";
     aTecla       = "";
     aZip         = "";
     aSeguir      = "";
     aParametro1  = "";
     aParametro2  = "";
     aLista       = "";
     aAplica      = "";
     aEmpresa     = "";
     aCopiaOrigen = "";
     aCopiaDestin = "";
     aClave       = "";
     aCadena      = "";
     aLong        = "";
     aCaracter    = "";
     aMensaje     = "";
     aDefAntes    = "";
     aFichero     = "";
     aSalvaMoneda = "";

     nFSalida1    = 0;
     nFSalida2    = 0;
     nHandle      = 0;
     nSwHay       = 0;
     nSwGraba     = 0;
     nPesetas     = 0;
     nEuros       = 0;
     nCampo       = 0;
     nNumCampos   = 0;
     nBoton1      = 0;
     nBoton2      = 0;
     nBoton3      = 0;
     nModo        = 0;
     nNume        = 0;
     nPulso       = 0;
     nGrabaCampo  = 0;
     nOpcion      = 0;
     nValor       = 0;
     nClave       = 0;
     nContador    = 0;
     nLong        = 0;
     nPos         = 0;
     nCaracter    = 0;
     nHayDatos    = 0;
     nImprimir    = 0;
     nEspecial    = 0;
     nAplica      = 0;

     &EURO      = 1;
     &EURODB    = 0;
     &EURODV    = 0;
     &EUROTEXTO = "Cambio de Moneda";

     &eaBase        = "";
     &eaAplica      = "";
     &eaEmpresa     = "";
     &eaMoneda      = "";
     &enDesdeConver = 1;
     &enForzar      = 0;
|FIN;

|PROCESO Usuario;  |TIPO 7;
     aAlfa = #0#1;  |QBF aAlfa;
     |SI aAlfa ! "";  |ACABA;  |FINSI;

     |DBASS aBase;
     #0#0 = aBase;
     |PINTA #0#0;

     #0#1 = "";  |PINTA #0#1;
     #0#2 = "";  |PINTA #0#2;
|FPRC;

|PROCESO MiraAplicacion;
     aAlfa = #0#0;  |QBF aAlfa;
     aAlfa = aAlfa + #0#1;  |QBF aAlfa;
     aAlfa = aAlfa + "/";

     #0#3 = aAlfa;

     nAplica = 0;
     aLista  = aAlfa + "/*.men";
     |_PDIR aLista;
     |PARA;  |SI FSalida = 0;  |HACIENDO;
         nAplica = 1;
         |_SDIR aLista;
     |SIGUE;

     |SI nAplica = 0;
         |MENAV "     No existe Aplicacion";
         |ERROR;
         |ACABA;
     |FINSI;
|FPRC;

|PROCESO MiraEmpresa;
     aAlfa = #0#3;  |QBF aAlfa;

     |SI #0#2 = "      ";
         aAlfa = aAlfa + "fich/";
     |SINO;
         aAlfa = aAlfa + "fich/" + #0#2;  |QBF aAlfa;
         aAlfa = aAlfa + "/";
     |FINSI;

     #0#4 = aAlfa;

     nAplica = 0;
     aLista  = aAlfa + "/*.dat";
     |_PDIR aLista;
     |PARA;  |SI FSalida = 0;  |HACIENDO;
         nAplica = 1;
         |_SDIR aLista;
     |SIGUE;

     |SI nAplica = 0;
         |MENAV "     No existe Directorio de Datos";
         |ERROR;
         |ACABA;
     |FINSI;
|FPRC;

|PROCESO GrabaElDos;
     #2#0 = #1#0;
     #2#1 = #1#1;
     #2#2 = #1#2;
     #2#3 = #1#3;
     #2#4 = nCampo;
     |LEE 101#2=;
     nFSalida2 = FSalida;
     |SI FSalida ! 0;
         |DDEFECTO #2;
         #2#0 = #1#0;
         #2#1 = #1#1;
         #2#2 = #1#2;
         #2#3 = #1#3;
         #2#4 = nCampo;
         |GRABA 020#2b;
     |FINSI;

     |SI enForzar = 1;  nHayDatos = 1;  |FINSI;

     |SI nHayDatos ! 0;
         |SI eaMoneda = "P";
             #2#6 = "PESETA";
         |SINO;
             #2#6 = "EURO";
         |FINSI;
     |FINSI;

     |SI #2#6 = "PESETA";
         |SI #0#5 = "EURO";
             |SI nFSalida1 ! 0;
                 #2#6 = "EURO";
             |SINO;
                 |SI #1#6 < #2#4;
                     #2#6 = "EURO";
                 |FINSI;
             |FINSI;
         |FINSI;
     |FINSI;

     #2#5 = aNombreCampo;
     |GRABA 020#2a;
     |LIBERA #2;

     |SI #2#6 = "PESETA";
         #1#7 = "PESETA";
     |FINSI;

     nSwHay = 1;

     |SI #1#8 = 0;
         |SI #2#6 = "PESETA";
             nPesetas = 1;
             #0#7     = #0#7 + 1;   |PINTA #0#7;
         |SINO;
             nEuros   = 1;
             #0#9     = #0#9 + 1;   |PINTA #0#9;
         |FINSI;
     |SINO;
         |SI nFSalida2 ! 0;
             aMensaje = "Mas = " + aDefAntes + " contra Mas = " + aDefFichero + " Campo : " + #2#4;
             |SI nImprimir ] 0;  |IMPRIME aMensaje;  |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO MiraElDos;
     #2#0 = #1#0;
     #2#1 = #1#1;
     #2#2 = #1#2;
     #2#3 = #1#3;
     #2#4 = nCampo;
     |LEE 101#2=;
     |SI FSalida = 0;
         aMensaje = "Mas = " + aDefAntes + " contra Mas = " + aDefFichero + " Campo : " + #2#4;
         |SI nImprimir ] 0;  |IMPRIME aMensaje;  |FINSI;

         |BORRA 020#2a;
     |FINSI;
     |LIBERA #2;
|FPRC;

|PROCESO BuscaCampos;
     aQueQuiero = "|TITULO";
     aTitulo    = #100#aQueQuiero#;
     #1#5       = aTitulo;
     aQueQuiero = "|NC";
     aAlfa      = #100#aQueQuiero#;
     nNumCampos = aAlfa;
     nNumCampos = nNumCampos - 1;
     |SI nFSalida1 ! 0;
         #1#6   = nNumCampos;
     |FINSI;

     |SI #1#4 = "        ";
         aDefAntes = aDefFichero - ".mas";
     |SINO;
         aDefAntes = #1#4;
     |FINSI;

     #1#4  = aDefFichero - ".mas";
     aAlfa = #0#1 % 106;
     |SI aAlfa = "agieos";
         aAlfa = (aDefFichero % 101) < "";
         |SI aAlfa = "v";
             #1#4 = "eoslinve";
         |FINSI;
         |SI aAlfa = "c";
             #1#4 = "eoslinco";
         |FINSI;
     |FINSI;

     nSwHay     = 0;
     nPesetas   = 0;
     nEuros     = 0;
     #1#7       = "EURO"

     |PARA nCampo = 0; |SI nCampo [ nNumCampos;  |HACIENDO nCampo = nCampo + 1;
         aQueQuiero = "|C" + (("000" + nCampo) % -103) + "TIPO";
         aTipoCampo  = #100#aQueQuiero#;
         |SI aTipoCampo = "N";
             aQueQuiero   = "|C" + (("000" + nCampo) % -103) + "NOMBRE";
             aNombreCampo = (#100#aQueQuiero# + (" " * 20)) % 120;
             aQueQuiero   = "|C" + (("000" + nCampo) % -103) + "MINIMO";
             aMinCampo    = #100#aQueQuiero#;
             aAlfa        = aMinCampo - "EURO";

             |SI aMinCampo ! aAlfa;
                 |HAZ GrabaElDos;
             |SINO;
                 |HAZ MiraElDos;
             |FINSI;
         |SINO;
             aQueQuiero   = "|C" + (("000" + nCampo) % -103) + "NOMBRE";
             aNombreCampo = (#100#aQueQuiero# + (" " * 20)) % 120;
             aAlfa        = aNombreCampo % 103;
             |SI aAlfa    = "*E*";
                 |HAZ GrabaElDos;
             |FINSI;
         |FINSI;
     |SIGUE;
|FPRC;

|PROCESO NombreFichero;
     |QBF aFicFichero;
     aLong    = aFicFichero % 0;
     nLong    = aLong;
     aCadena  = "";
     |PARA nCaracter = 1; |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPos      = (nCaracter * 100) + 1;
           aCaracter = aFicFichero % nPos;

           |SI aCaracter = "/"; |O aCaracter = "/";
               aCadena = "";
           |SINO;
               aCadena = aCadena + aCaracter;
           |FINSI;
     |SIGUE;
     aFicFichero = aCadena;
|FPRC;

|PROCESO Configura;
     |CARGA_DEF aDirDef, fichero@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aQueQuiero   = "|NK";
     aClave       = #100#aQueQuiero#;
     nClave       = aClave;
     aQueQuiero   = "|FICHERO";
     aFicFichero  = #100#aQueQuiero#;
     |HAZ NombreFichero;

     |DESCARGA_DEF #fichero@;

     |SI nClave = 0;  |ACABA;  |FINSI;

     aDefFichero = aDirDef - aDef;
     |CARGA_DEF aDirDef, fichero@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aPath = #0#4;  |QBF aPath;

     |PATH_DAT #100 aPath;
     |ABRE #100;
     |LEE 101#100p;
     nHayDatos = FSalida;
     |CIERRA #100;

     |ATRI S;  |PINTA 1055, aDefFichero;  |ATRI s;

     |DDEFECTO #1;
     #1#0 = #0#0;
     #1#1 = #0#1;
     #1#2 = #0#2;
     #1#3 = aFicFichero;
     |LEE 101#1=;
     nFSalida1 = FSalida;

     |SI nFSalida1 ! 0;
         |DDEFECTO #1;
         #1#0 = #0#0;
         #1#1 = #0#1;
         #1#2 = #0#2;
         #1#3 = aFicFichero;
         |GRABA 020#1b;
     |FINSI;

     |HAZ BuscaCampos;

     |SI #1#8 = 0;
         |SI nPesetas = 1;
             #0#6 = #0#6 + 1;   |PINTA #0#6;
         |FINSI;

         |SI nEuros = 1;
             #0#8 = #0#8 + 1;   |PINTA #0#8;
         |FINSI;
     |FINSI;

     |SI nSwHay = 1;
         #1#8 = 1;
         |GRABA 020#1a;
         nSwGraba = 1;
     |SINO;
         |BORRA 020#1a;
     |FINSI;
     |LIBERA #1;

     |DESCARGA_DEF #fichero@;

     |PINTA 1055, "              ";
|FPRC;

|PROCESO ConfiguraEos;
     aDefFichero = aDirDat - aFichDat;
     aDefFichero = aDefFichero - ".dat" + ".mas";
     aDef     = #0#3;  |QBF aDef;
     aDef     = aDef + "def/" + aDefFichero;

     aAlfa = (aDefFichero % 101) < "";
     |SI aAlfa = "v";
         aDef  = (aDef - aDefFichero) + "eoslinve.mas";
     |FINSI;
     |SI aAlfa = "c";
         aDef  = (aDef - aDefFichero) + "eoslinco.mas";
     |FINSI;

     |CARGA_DEF aDef, fichero@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aAlfa = #0#1 % 106;
     aAlfa = (aDefFichero % 101) < "";
     |SI aAlfa = "v";  |O aAlfa = "c";
         aAlfa = aDefFichero - ".mas";
         |NOME_DAT #100 aAlfa;
     |FINSI;

     |ATRI S;  |PINTA 1055, aDefFichero;  |ATRI s;

     aPath = #0#4;  |QBF aPath;
     |PATH_DAT #100 aPath;
     |ABRE #100;
     |LEE 101#100p;
     nHayDatos = FSalida;
     |CIERRA #100;

     #1#0 = #0#0;
     #1#1 = #0#1;
     #1#2 = #0#2;
     #1#3 = aDefFichero - ".mas";
     |LEE 101#1=;
     nFSalida1 = FSalida;

     |SI nFSalida1 ! 0;
         |DDEFECTO #1;
         #1#0 = #0#0;
         #1#1 = #0#1;
         #1#2 = #0#2;
         #1#3 = aDefFichero - ".mas";
         |GRABA 020#1b;
     |FINSI;

     aSalvaMoneda = eaMoneda;
     |SI #1#9 = 0;
         eaMoneda  = "E";
         nHayDatos = 1;
     |FINSI;

     |HAZ BuscaCampos;

     eaMoneda = aSalvaMoneda;

     |LIBERA #1;

     |SI #1#8 = 0;
         |SI nPesetas = 1;
             #0#6 = #0#6 + 1;   |PINTA #0#6;
         |FINSI;

         |SI nEuros = 1;
             #0#8 = #0#8 + 1;   |PINTA #0#8;
         |FINSI;
     |FINSI;

     |SI nSwHay = 1;
         #1#8  = 1;
         #1#9  = 1;
         |GRABA 020#1a;
         nSwGraba = 1;
     |SINO;
         |BORRA 020#1a;
     |FINSI;

     |DESCARGA_DEF #fichero@;

     |PINTA 1055, "              ";
|FPRC;

|PROCESO PonACero;
     #1#8 = 0;
     |GRABA 020#1a;
     |LIBERA #1;
|FPRC;

|DEFBUCLE dsconve1_0;
     #1#1;
     ;
     #0#0, #0#1, #0#2, "        ";
     #0#0, #0#1, #0#2, "zzzzzzzz";
     be;
     NULL, PonACero;
|FIN;

|PROCESO BuscaDefFicheros;
     |BUCLE dsconve1_0;

     aDef  = #0#3;  |QBF aDef;
     aDef  = aDef + "def/";
     aDirDef = aDef + "*.mas";
     |_PDIR aDirDef;
     |PARA;  |SI FSalida ] 0; |HACIENDO;
          |HAZ Configura;
          |_SDIR aDirDef;
     |SIGUE;

     |SI #0#1 = "agieos    ";
         aFichDat    = #0#4;  |QBF aFichDat;
         aDirDat     = aFichDat + "c*.dat";
         |_PDIR aDirDat;
         |PARA;  |SI FSalida ] 0; |HACIENDO;
                 |HAZ ConfiguraEos;
                 |_SDIR aDirDat;
         |SIGUE;

         aFichDat    = #0#4;  |QBF aFichDat;
         aDirDat     = aFichDat + "v*.dat";
         |_PDIR aDirDat;
         |PARA;  |SI FSalida ] 0; |HACIENDO;
                 |HAZ ConfiguraEos;
                 |_SDIR aDirDat;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO LeeMonedas;
     eaMoneda = "";
     aPath    = #0#4;  |QBF aPath;
     |SI #0#1 = "integral  ";  |O #0#1 = "modelos   ";
      |O #0#1 = "contanex  ";  |O #0#1 = "contasoc  ";
      |O #0#1 = "basica    ";  |O #0#1 = "laboral   ";
         |DBASS aPath;   |QBT aPath;
         aPath = aPath + "basica/fich/";

         |DBASS aDef;    |QBT aDef;
         aDef  = aDef + "basica/def/zmonedas";

         |CARGA_DEF aDef, fichero@;
         |SI FSalida ! 0;  |ACABA;  |FINSI;
         |PATH_DAT #100 aPath;

         |ABRE #100;
         |LEE 040#100p;
         |SI FSalida ! 0;  |DDEFECTO #100;  |FINSI;

         |SI #0#1 = "integral  ";  eaMoneda = #100#1;  |FINSI;
         |SI #0#1 = "basica    ";  eaMoneda = #100#3;  |FINSI;
         |SI #0#1 = "laboral   ";  eaMoneda = #100#5;  |FINSI;
         |SI #0#1 = "contasoc  ";  eaMoneda = #100#7;  |FINSI;
         |SI #0#1 = "modelos   ";  eaMoneda = #100#9;  |FINSI;
         |SI #0#1 = "contanex  ";  eaMoneda = #100#11;  |FINSI;

         |CIERRA #100;

         |DESCARGA_DEF #fichero@;
     |SINO;
         |PATH_DAT #3 aPath;
         |ABRE #3;
         |LEE 040#3p;
         |SI FSalida = 0;
             eaMoneda = #3#1;
         |FINSI;
         |CIERRA #3;
     |FINSI;

     |SI #0#1 ! "agicont   "; |Y #0#1 ! "contagen  ";
         |ACABA;
     |FINSI;

     |SI eaMoneda = "";
         aPath = #0#3;           |QBF aPath;
         aPath = aPath + "fich/000000/";

         aDef  = #0#3;  |QBF aDef;
         aDef  = aDef + "def/camoneda.mas";   |QBT aDef;

         |CARGA_DEF aDef, fichero@;
         |SI FSalida ! 0;  |ACABA;  |FINSI;
         |PATH_DAT #100 aPath;

         aAlfa1 = #0#2 % 105;
         aAlfa2 = #0#2 % 601;

         |ABRE #100;
         #100#0 = aAlfa1;
         #100#1 = aAlfa2;
         |LEE 040#100=;
         |SI FSalida = 0;
             eaMoneda = #100#2;
         |FINSI;
         |CIERRA #100;

         |DESCARGA_DEF #fichero@;
     |FINSI;
|FPRC;

|PROCESO CargaPath;
     aAlfa = #0#0;  |QBF aAlfa;
     aAlfa = aAlfa + #0#1;  |QBF aAlfa;
     aAlfa = aAlfa + "/";
     #0#3 = aAlfa;

     aAlfa = #0#3;  |QBF aAlfa;
     |SI #0#2 = "      ";
         aAlfa = aAlfa + "fich/";
     |SINO;
         aAlfa = aAlfa + "fich/" + #0#2;  |QBF aAlfa;
         aAlfa = aAlfa + "/";
     |FINSI;
     #0#4 = aAlfa;
|FPRC;

|PROCESO Tipo3;  |TIPO 3;
     |HAZ CargaPath;

     eaMoneda = "";
     |HAZ LeeMonedas;
     |SI eaMoneda = "";
         |MENAV "      Aplicacion sin configuracion de Monedas.";
         |ACABA;
     |FINSI;

     |SI nImprimir ] 0;
         |IMPRE 0;
         |SI FSalida ! 0;  |ACABA;  |FINSI;
     |FINSI;

     |SI eaMoneda = "P";
         #0#5 = "PESETA";
     |SINO;
         #0#5 = "EURO";
     |FINSI;

     |PINTA #0#5;

     |MENSA "      Configurando";

     |ESTADO_CONTROL nBoton3, "DISABLE";

     #0#6 = 0;  |PINTA #0#6;
     #0#7 = 0;  |PINTA #0#7;
     #0#8 = 0;  |PINTA #0#8;
     #0#9 = 0;  |PINTA #0#9;

     nSwGraba = 0;

     |ABRE #1;
     |ABRE #2;
     |HAZ BuscaDefFicheros;
     |CIERRA #1;
     |CIERRA #2;

     |MENSA "         ";

     |SI nImprimir ] 0;  |FINIMP;  |FINSI;

     |ESTADO_CONTROL nBoton3, "ENABLE";

     |SI nSwGraba = 0;
         |MENAV "      No hay Campos a Convertir";
         |ERROR;
         |ACABA;
     |FINSI;

     |SI #0#5 = "PESETA";
         |SI #0#8 = 0;  |Y #0#9 = 0;
             |ACABA;
         |FINSI;
     |FINSI;

     |SI #0#5 = "EURO  ";
         |SI #0#6 = 0;  |Y #0#7 = 0;
             |ACABA;
         |FINSI;
     |FINSI;

     |SI #0#5 = "PESETA";
         |SI #0#9 ! 0;
             |ESTADO_CONTROL nBoton2, "ENABLE";
         |FINSI;
     |SINO;
         |SI #0#7 ! 0;
             |ESTADO_CONTROL nBoton1, "ENABLE";
         |FINSI;
     |FINSI;

     |SI enForzar = 0;
         aEsc = &255 + "806";
         |PARA; |SI;  |HACIENDO;
              |LEETECLA aTecla;
              |SI aTecla = aEsc;  |SAL;  |FINSI;
         |SIGUE;
     |FINSI;

     |ESTADO_CONTROL nBoton1, "DISABLE";
     |ESTADO_CONTROL nBoton2, "DISABLE";
|FPRC;

|PROCESO Borra1;
     |BORRA 020#1a;
     |LIBERA #1;
|FPRC;

|DEFBUCLE dsconve1B;
     #1#1;
     ;
     #0#0, #0#1, #0#2, "        ";
     #0#0, #0#1, #0#2, "zzzzzzzz";
     be;
     NULL, Borra1;
|FIN;

|PROCESO Borra2;
     |BORRA 020#2a;
     |LIBERA #2;
|FPRC;

|DEFBUCLE dsconve2B;
     #2#1;
     ;
     #0#0, #0#1, #0#2, "        ", 000;
     #0#0, #0#1, #0#2, "zzzzzzzz", 999;
     be;
     NULL, Borra2;
|FIN;

|PROCESO Tipo2;  |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo ! 3;  |ACABA;  |FINSI;

     |BUCLE dsconve1B;
     |BUCLE dsconve2B;
|FPRC;

|RUTINA ClaveBaja0;
     #0#0 = "                         ";
     #0#1 = "          ";
     #0#2 = "      ";
|FRUT;

|RUTINA ClaveAlta0;
     #0#0 = "zzzzzzzzzzzzzzzzzzzzzzzzz";
     #0#1 = "zzzzzzzzzz";
     #0#2 = "zzzzzz";
|FRUT;

|RUTINA ClaveBaja1;
     #1#0 = #0#0;
     #1#1 = #0#1;
     #1#2 = #0#2;
     #1#3 = "        ";
|FRUT;

|RUTINA ClaveAlta1;
     #1#0 = #0#0;
     #1#1 = #0#1;
     #1#2 = #0#2;
     #1#3 = "zzzzzzzz";
|FRUT;

|RUTINA ClaveBaja2;
     #2#0 = #1#0;
     #2#1 = #1#1;
     #2#2 = #1#2;
     #2#3 = #1#3;
     #2#4 = 0;
|FRUT;

|RUTINA ClaveAlta2;
     #2#0 = #1#0;
     #2#1 = #1#1;
     #2#2 = #1#2;
     #2#3 = #1#3;
     #2#4 = 999;
|FRUT;

|PROCESO MiraEspecialEuro;
     |SI #2#1 ! "basica    ";  |Y #2#1 ! "modelos   ";
      |Y #2#1 ! "agicli    ";
         |ACABA;
     |FINSI;

     |SI #1#4 = "agim0019";
         |SI #2#4 = 16;  |O #2#4 = 20;  |O #2#4 = 24; |O #2#4 = 14;
             nEspecial = 1;

             |SI #2#4 = 14;
                 |ABRE #4;
                 |DDEFECTO #4;
                 #4#0 = #100#0;
                 #4#1 = #100#1;
                 |LEE 101#4=;
                 |SI FSalida ! 0;
                     |GRABA 020#4b;
                 |FINSI;
                 #4#2 = #100#51;
                 |GRABA 020#4a;
                 |LIBERA #4;
                 |CIERRA #4;
             |FINSI;

             |SI #4#2 = "E";  |ACABA;  |FINSI;

             #100#51    = "E";
             nCampo     = #2#4;
             nValor     = #100nCampo;
             |SI nValor ! 0;
                 |SI #2#4 ! 14;
                     nValor = (nValor  / 166.386) ! 6;
                 |SINO;
                     nValor = nValor  / 166.386;
                 |FINSI;
                 #100nCampo = nValor;
             |FINSI;
             nEuros = 1;
        |FINSI;
     |FINSI;

     |SI #1#4 = "agim0025";
         |SI #2#4 = 20;
             nEspecial = 1;

             |ABRE #4;
             #4#0 = #100#0;
             #4#1 = #100#1;
             |LEE 101#4=;
             |SI FSalida ! 0;  |DDEFECTO #4;  |FINSI;
             |CIERRA #4;

             |SI #4#2 = "E";  |ACABA;  |FINSI;

             nCampo     = #2#4;
             nValor     = #100nCampo;
             |SI nValor ! 0;
                 nValor = nValor  / 166.386;
                 #100nCampo = nValor;
             |FINSI;
             nEuros = 1;
         |FINSI;
     |FINSI;

     |SI #1#4 = "dsmociva";
         |SI #100#6 = 5;  |Y #100#7 = 1;
             nEspecial = 1;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO MiraEspecialPeseta;
     |SI #2#1 ! "basica    ";  |Y #2#1 ! "modelos   ";
      |Y #2#1 ! "agicli    ";
         |ACABA;
     |FINSI;

     |SI #1#4 = "agim0019";
         |SI #2#4 = 16;  |O #2#4 = 20;  |O #2#4 = 24; |O #2#4 = 14;
             nEspecial = 1;

             |SI #2#4 = 14;
                 |ABRE #4;
                 |DDEFECTO #4;
                 #4#0 = #100#0;
                 #4#1 = #100#1;
                 |LEE 101#4=;
                 |SI FSalida ! 0;
                     |GRABA 020#4b;
                 |FINSI;
                 #4#2 = #100#51;
                 |GRABA 020#4a;
                 |LIBERA #4;
                 |CIERRA #4;
             |FINSI;

             |SI #4#2 = "P";  |ACABA;  |FINSI;

             #100#51    = "P";
             nCampo     = #2#4;
             nValor     = #100nCampo;
             |SI nValor ! 0;
                 nValor = nValor  * 166.386;
                 #100nCampo = nValor;
             |FINSI;
             nPesetas = 1;
        |FINSI;
     |FINSI;

     |SI #1#4 = "agim0025";
         |SI #2#4 = 20;
             nEspecial = 1;

             |ABRE #4;
             #4#0 = #100#0;
             #4#1 = #100#1;
             |LEE 101#4=;
             |SI FSalida ! 0;  |DDEFECTO #4;  |FINSI;
             |CIERRA #4;

             |SI #4#2 = "P";  |ACABA;  |FINSI;

             nCampo     = #2#4;
             nValor     = #100nCampo;
             |SI nValor ! 0;
                 nValor = nValor  * 166.386;
                 #100nCampo = nValor;
             |FINSI;
             nPesetas = 1;
         |FINSI;
     |FINSI;

     |SI #1#4 = "dsmociva";
         |SI #100#6 = 5;  |Y #100#7 = 1;
             nEspecial = 1;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO CambiaMoneda;
     |SI nGrabaCampo = 1;
         |SI nPulso = 1;
             |SI #2#6 = "EURO  ";  |ACABA;  |FINSI;

                                   || Esto se puede simplificar pero pueden
                                   || entrar campos alfanumericos.

             nEspecial = 0;
             |HAZ MiraEspecialEuro;
             |SI nEspecial = 1;  |ACABA;  |FINSI;

             nCampo     = #2#4;
             nValor     = #100nCampo;
             |SI nValor ! 0;
                 nValor     = nValor  / 166.386;
                 #100nCampo = nValor;
             |FINSI;
             nEuros = 1;
         |FINSI;

         |SI nPulso = 2;
             |SI #2#6 = "PESETA";  |ACABA;  |FINSI;

                                   || Esto se puede simplificar pero pueden
                                   || entrar campos alfanumericos.

             nEspecial = 0;
             |HAZ MiraEspecialPeseta;
             |SI nEspecial = 1;  |ACABA;  |FINSI;

             nCampo     = #2#4;
             nValor     = #100nCampo;
             |SI nValor ! 0;
                 nValor     = nValor  * 166.386;
                 #100nCampo = nValor;
             |FINSI;
             nPesetas = 1;
         |FINSI;
     |SINO;
         |SI nPulso = 1;
             |SI #2#6 = "PESETA";
                 #2#6 = "EURO";
                 #0#7 = #0#7 - 1;   |PINTA #0#7;
                 #0#9 = #0#9 + 1;   |PINTA #0#9;
             |FINSI;
         |FINSI;

         |SI nPulso = 2;
             |SI #2#6 = "EURO  ";
                 #2#6 = "PESETA";
                 #0#7 = #0#7 + 1;   |PINTA #0#7;
                 #0#9 = #0#9 - 1;   |PINTA #0#9;
             |FINSI;
         |FINSI;

         |GRABA 020#2a;
         |LIBERA #2;
     |FINSI;
|FPRC;

|DEFBUCLE dsconve2;
     #2#1;
     ;
     #1#0, #1#1, #1#2, #1#3, 000;
     #1#0, #1#1, #1#2, #1#3, 999;
     be;
     NULL, CambiaMoneda;
|FIN;

|PROCESO LeeCampos;
     |SI nPulso = 1;
         |SI #1#7 = "EURO  ";  |ACABA;  |FINSI;
     |FINSI;

     |SI nPulso = 2;
         |SI #1#7 = "PESETA";  |ACABA;  |FINSI;
     |FINSI;

     aDefFichero = #1#4;  |QBF aDefFichero;
     aDefFichero = aDefFichero + ".mas";
     aDef        = #0#3;  |QBF aDef;
     aDef        = aDef + "def/" + aDefFichero;

     |CARGA_DEF aDef, fichero@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI #1#3 ! #1#4;  |Y #1#1 = "agieos    ";
         aAlfa = #1#3;  |QBF aAlfa;
         |NOME_DAT #100 aAlfa;
     |FINSI;

     aPath = #0#4;  |QBF aPath;
     |PATH_DAT #100 aPath;

     nEuros      = 0;
     nPesetas    = 0;
     nGrabaCampo = 1;

     |ATRI S;  |PINTA 2258, #1#3;  |ATRI s;

     |PINTA 2270, "         ";

     |ABRE #100;
     |LEE 101#100p;
     |SI FSalida ! 0;
         |BUCLE dsconve2;
     |SINO;
         nContador = 0;
         |PARA;  |SI FSalida = 0;  |HACIENDO;
              nContador = nContador + 1;
              |PINTA 2270, "         ";
              |ATRI I;  |PINTA 2270, nContador;  |ATRI i;
              |BUCLE dsconve2;
              |GRABA 020#100a;
              |LIBERA #100;
              |LEE 101#100s;
         |SIGUE;
     |FINSI;
     |CIERRA #100;
     |DESCARGA_DEF #fichero@;

     |SI nEuros ! 0;  |O nPesetas ! 0;
         nGrabaCampo = 0;
         |BUCLE dsconve2;

         |SI nPulso = 1;
             #0#6 = #0#6 - 1;   |PINTA #0#6;
             #0#8 = #0#8 + 1;   |PINTA #0#8;
             #1#7 = "EURO";
         |FINSI;

         |SI nPulso = 2;
             #0#6 = #0#6 + 1;   |PINTA #0#6;
             #0#8 = #0#8 - 1;   |PINTA #0#8;
             #1#7 = "PESETA";
         |FINSI;
     |FINSI;

     |PINTA 2258, "                 ";
     |GRABA 020#1a;
|FPRC;

|DEFBUCLE dsconve1;
     #1#1;
     ;
     #0#0, #0#1, #0#2, "        ";
     #0#0, #0#1, #0#2, "zzzzzzzz";
     be;
     NULL, LeeCampos;
|FIN;

|PROCESO CopiaSeguridad;
     |PUSHV 0501 2380;
     |CUADRO 1110 1558;
     |BLANCO 1211 1457;
     |ATRI S;
     |PINTA 1321, "HACIENDO COPIA DE SEGURIDAD";
     |ATRI s;
     |PULSATECLA;
     |SLEEP 1;

     aPath       = #0#4;  |QBF aPath;
     aParametro1 = aPath + "zdatodat" + " *.dat";
     aParametro2 = aPath;
     |ATAR aParametro1, aParametro2;

     aParametro1 = aPath + "zdatodat";
     |COMPRIME aParametro1;

     |FBORRA aParametro1;

     aPath       = #0#4;  |QBF aPath;
     aParametro1 = aPath + "zdatoddx" + " *.ddx";
     aParametro2 = aPath;
     |ATAR aParametro1, aParametro2;

     aParametro1 = aPath + "zdatoddx";
     |COMPRIME aParametro1;

     |FBORRA aParametro1;

     aPath       = #0#4;  |QBF aPath;
     aParametro1 = aPath + "zdatoixx" + " *.ixx";
     aParametro2 = aPath;
     |ATAR aParametro1, aParametro2;

     aParametro1 = aPath + "zdatoixx";
     |COMPRIME aParametro1;

     |FBORRA aParametro1;

     |POPV;
|FPRC;

|PROCESO RestauraCopia;
     |PUSHV 0501 2380;
     |CUADRO 1110 1558;
     |BLANCO 1211 1457;
     |ATRI S;
     |PINTA 1321, "RESTAURANDO COPIA DE SEGURIDAD";
     |ATRI s;
     |PULSATECLA;
     |SLEEP 1;

     aPath        = #0#4;  |QBF aPath;
     aCopiaOrigen = aPath + "zmonedas.dat";
     aCopiaDestin = aPath + "zmonedas.ddd";
     |COPIA_FICHERO aCopiaOrigen, aCopiaDestin;

     aCopiaOrigen = aPath + "zmonedas.ddx";
     aCopiaDestin = aPath + "zmonedas.xxx";
     |COPIA_FICHERO aCopiaOrigen, aCopiaDestin;

     aCopiaOrigen = aPath + "zmonedas.ixx";
     aCopiaDestin = aPath + "zmonedas.iii";
     |COPIA_FICHERO aCopiaOrigen, aCopiaDestin;

     aParametro1 = aPath + "zdatodat.gz";
     |DESCOMPRIME aParametro1;

     aParametro1 = aPath + "zdatodat";
     aParametro2 = aPath;
     |DETAR aParametro1, aParametro2;

     |FBORRA aParametro1;

     aPath       = #0#4;  |QBF aPath;
     aParametro1 = aPath + "zdatoddx.gz";
     |DESCOMPRIME aParametro1;

     aParametro1 = aPath + "zdatoddx";
     aParametro2 = aPath;
     |DETAR aParametro1, aParametro2;

     |FBORRA aParametro1;

     aPath       = #0#4;  |QBF aPath;
     aParametro1 = aPath + "zdatoixx.gz";
     |DESCOMPRIME aParametro1;

     aParametro1 = aPath + "zdatoixx";
     aParametro2 = aPath;
     |DETAR aParametro1, aParametro2;

     |FBORRA aParametro1;

     aCopiaOrigen = aPath + "zmonedas.ddd";
     aCopiaDestin = aPath + "zmonedas.dat";
     |COPIA_FICHERO aCopiaOrigen, aCopiaDestin;
     |FBORRA aCopiaOrigen;

     aCopiaOrigen = aPath + "zmonedas.xxx";
     aCopiaDestin = aPath + "zmonedas.ddx";
     |COPIA_FICHERO aCopiaOrigen, aCopiaDestin;
     |FBORRA aCopiaOrigen;

     aCopiaOrigen = aPath + "zmonedas.iii";
     aCopiaDestin = aPath + "zmonedas.ixx";
     |COPIA_FICHERO aCopiaOrigen, aCopiaDestin;
     |FBORRA aCopiaOrigen;

     |POPV;
|FPRC;

|PROCESO Boton1;
     EURO   = 166.386;
     EURODB = 2;
     EURODV = 0;

     |ESTADO_CONTROL nBoton1, "HIDE";
     |ESTADO_CONTROL nBoton2, "HIDE";
     |ESTADO_CONTROL nBoton3, "HIDE";

     |PUSHV 0501 2380;
     |BLANCO 1447 2279;

     |ATRI I;
     |PINTA 1447, " Es aconsejable tener hecha una  ";
     |PINTA 1547, "      COPIA DE SEGURIDAD         ";
     |ATRI i;

     |ATRI S;
     |PINTA 1747, "Desea una copia formato ZIP en el";
     |PINTA 1847, "disco duro para una posible      ";
     |PINTA 1947, "restauracion automatica .....:   ";

     |PINTA 2147, "Desea continuar con la opcion:   ";
     |ATRI s;

     aZip    = "SI";   |PINTA 1978, aZip;
     aSeguir = "NO";   |PINTA 2178, aSeguir;

     |ET Campo1;
         E_inf  = "SINOsino";
         E_sup  = "2";
         aZip   = 1978 ? 1;
         |SI FSalida = 2;  |VETE Campo1;  |FINSI;
         aZip   = aZip > aZip;

     |ET Campo2;
         E_inf   = "SINOsino";
         E_sup   = "2";
         aSeguir = 2178 ? 1;
         |SI FSalida = 2;  |VETE Campo1;  |FINSI;
         aSeguir = aSeguir > aSeguir;

     |SI aSeguir ! "SI";
         |ESTADO_CONTROL nBoton1, "SHOW";
         |ESTADO_CONTROL nBoton2, "SHOW";
         |ESTADO_CONTROL nBoton3, "SHOW";
         |POPV;
         |PTEC 806;
         |ACABA;
     |FINSI;

     |SI aZip = "SI";  |HAZ CopiaSeguridad;  |FINSI;

     nPulso = 1;
     |BUCLE dsconve1;

     |ESTADO_CONTROL nBoton1, "SHOW";
     |ESTADO_CONTROL nBoton2, "SHOW";
     |ESTADO_CONTROL nBoton3, "SHOW";

     |POPV;
     |PTEC 806;
|FPRC;

|PROCESO Boton2;
     EURO   = 1 / 166.386;
     EURODB = 0;
     EURODV = 2;

     |ESTADO_CONTROL nBoton1, "HIDE";
     |ESTADO_CONTROL nBoton2, "HIDE";
     |ESTADO_CONTROL nBoton3, "HIDE";

     aPath       = #0#4;  |QBF aPath;
     aParametro1 = aPath + "zdatodat.gz";
     |XABRE "E", aParametro1, nHandle;
     |SI FSalida ] 0;
         |PUSHV 0501 2380;
         |BLANCO 1147 2279;
         |ATRI I;
         |PINTA 1147, "La Opcion (2) puede generar la   ";
         |PINTA 1247, "p‚rdida de algunas pesetas res-  ";
         |PINTA 1347, "pecto al importe original. Seria ";
         |PINTA 1447, "conveniente usar la opcion (1)   ";
         |PINTA 1547, "Desde Copia Seguridad.           ";

         |ATRI S;
         |PINTA 1747, "Tipo de Opcion 1 / 2 ........:   ";
         |ATRI s;

         |ATRI I;
         |PINTA 1947, "  (1) Desde Copia Seguridad      ";
         |PINTA 2047, "  (2) Importe Actual X 166.386   ";
         |ATRI i;

         |ATRI S;
         |PINTA 2247, "      CONTINUAR CON LA OPCION:   ";
         |ATRI s;

         |ET nOpcion;
             nOpcion = 1;
             E_inf   = "1";
             E_sup   = "2";
             nOpcion = 1778 ? 1;
             |SI FSalida = 2;  |VETE nOpcion;  |FINSI;

             aSeguir = "NO";
             E_inf  = "SINOsino";
             E_sup  = "2";
             aSeguir = 2278 ? 1;
             aSeguir = aSeguir > aSeguir;
             |SI FSalida = 2;  |VETE nOpcion;  |FINSI;

             |POPV;

             |SI nOpcion = 1;  |Y aSeguir = "SI";
                  |HAZ RestauraCopia;
                  aSeguir = "NO";
                  |BUCLE dsconve1B;
                  |BUCLE dsconve2B;

                  #0#6 = #0#8;
                  #0#7 = #0#9;
                  #0#8 = 0;
                  #0#9 = 0;
             |FINSI;
     |SINO;
         |PUSHV 0501 2380;
         |BLANCO 1447 2279;
         |ATRI I;
         |PINTA 1447, "La Reconversi¢n Pesetas puede ge-";
         |PINTA 1547, "nerar la p‚rdida de algunas pese-";
         |PINTA 1647, "tas respecto al importe original.";
         |PINTA 1747, "Mejor que usar esta opci¢n seria ";
         |PINTA 1847, "conveniente restaurar la Copia de";
         |PINTA 1947, "Seguridad realizada en el Cambio ";
         |PINTA 2047, "a Euro. ";
         |ATRI i;

         |ATRI S;
         |PINTA 2147, "      CONTINUAR CON LA OPCION:   ";
         |ATRI s;

         aSeguir = "NO";
         E_inf  = "SINOsino";
         E_sup  = "2";
         aSeguir = 2178 ? 1;
         aSeguir = aSeguir > aSeguir;

         |POPV;
     |FINSI;

     |SI aSeguir ! "SI";
         |ESTADO_CONTROL nBoton1, "SHOW";
         |ESTADO_CONTROL nBoton2, "SHOW";
         |ESTADO_CONTROL nBoton3, "SHOW";
         |PTEC 806;
         |ACABA;
     |FINSI;

     nPulso = 2;
     |BUCLE dsconve1;

     |ESTADO_CONTROL nBoton1, "SHOW";
     |ESTADO_CONTROL nBoton2, "SHOW";
     |ESTADO_CONTROL nBoton3, "SHOW";

     |PTEC 806;
|FPRC;

|PROCESO Tipo7eu1;  |TIPO 7;
     |ENTLINEAL #1#2, -5, 7, 22, 0, ClaveBaja2, ClaveAlta2;
|FPRC;

|PROCESO Tipo11eu1;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     |ENTLINEAL #1#2, -5, 4, 22, 0, ClaveBaja2, ClaveAlta2;
|FPRC;

|PROCESO Boton3;
     nNume = #0#6 + #0#7 + #0#8 + #0#9;
     |SI nNume = 0;  |ACABA;  |FINSI;

     |ESTADO_CONTROL nBoton1, "HIDE";
     |ESTADO_CONTROL nBoton2, "HIDE";
     |ESTADO_CONTROL nBoton3, "HIDE";

     |PUSHV 0501 2380;
     |ENTLINEAL #1#1, -4, 4, 11, 1, ClaveBaja1, ClaveAlta1;
     |POPV;

     |ESTADO_CONTROL nBoton1, "SHOW";
     |ESTADO_CONTROL nBoton2, "SHOW";
     |ESTADO_CONTROL nBoton3, "SHOW";
|FPRC;

|PROCESO DesdeFuera;
     |ABRE #0;
     #0#0 = eaBase;
     #0#1 = eaAplica;
     #0#2 = eaEmpresa;
     |LEE 101#0=;
     |SI FSalida ! 0;
         |DDEFECTO #0;
         #0#0 = eaBase;
         #0#1 = eaAplica;
         #0#2 = eaEmpresa;
         |HAZ MiraAplicacion;
         |HAZ MiraEmpresa;
         |GRABA 020#0b;

         |PINTA #0#0;  |PINTA #0#1;
         |PINTA #0#2;

         |SI eaMoneda = "P";
             #0#6 = 0;  |PINTA #0#6;
             #0#7 = 0;  |PINTA #0#7;
             #0#8 = 0;  |PINTA #0#8;
             #0#9 = 0;  |PINTA #0#9;

             nSwGraba = 0;

            |ABRE #1;
            |ABRE #2;
            |HAZ BuscaDefFicheros;
            |CIERRA #1;
            |CIERRA #2;
         |SINO;
            |HAZ Tipo3;
         |FINSI;

         |GRABA 020#0a;
     |SINO;
         |SI eaMoneda = "E";
             |SI #0#5 = "PESETA";
                 |CONFI "0000SDesea hacer el cambio de moneda en estos momentos ";
                 |SI FSalida = 0;
                     |PINTA #0#0;  |PINTA #0#1;
                     |PINTA #0#2;  |PINTA #0#5;

                     |HAZ Tipo3;

                     |GRABA 020#0a;
                 |FINSI;
             |FINSI;
         |FINSI;

         |SI eaMoneda = "P";
             |SI #0#5 = "EURO  ";
                 |CONFI "0000SDesea hacer el cambio de moneda en estos momentos ";
                 |SI FSalida = 0;
                     |PINTA #0#0;  |PINTA #0#1;
                     |PINTA #0#2;  |PINTA #0#5;

                     |HAZ Tipo3;

                     |GRABA 020#0a;
                 |FINSI;
             |FINSI;
         |FINSI;
     |FINSI;
     |LIBERA #0;
     |CIERRA #0;
|FPRC;

|PROCESO SacaContabilidad;
     aPath = #0#3;           |QBF aPath;
     aPath = aPath + "fich/000000/";

     aDef  = #0#3;  |QBF aDef;
     aDef  = aDef + "def/camoneda.mas";   |QBT aDef;

     |CARGA_DEF aDef, fichero@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;
     |PATH_DAT #100 aPath;

     aAlfa1 = #0#2 % 105;
     aAlfa2 = #0#2 % 601;

     |ABRE #100;
     #100#0 = aAlfa1;
     #100#1 = aAlfa2;
     |LEE 101#100=;
     |SI FSalida ! 0;
         |DDEFECTO #100;
         #100#0 = aAlfa1;
         #100#1 = aAlfa2;
         |GRABA 020#100b;
     |FINSI;

     #3#1 = #100#2;
     |SI #3#1 = "P";  #3#2 = "PESETAS";  |FINSI;
     |SI #3#1 = "E";  #3#2 = "EUROS  ";  |FINSI;

     |PUSHV 0501 2380;
     |PINPA #0#3;
     |PINDA #0#3;
     |ENDATOS #1#3;
     |SI FSalida = 0;
         #100#2 = #3#1;
     |FINSI;
     |POPV;

     |GRABA 020#100a;
     |LIBERA #100;
     |CIERRA #100;

     |DESCARGA_DEF #fichero@;
|FPRC;

|PROCESO Tipo11eu0;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     eaMoneda = "";
     aPath    = #0#4;  |QBF aPath;

     |ESTADO_CONTROL nBoton1, "HIDE";
     |ESTADO_CONTROL nBoton2, "HIDE";
     |ESTADO_CONTROL nBoton3, "HIDE";

     |SI #0#1 = "agicont   ";  |O #0#1 = "contagen  ";
         |HAZ SacaContabilidad;
     |SINO;
         |SI #0#1 = "integral  ";  |O #0#1 = "modelos   ";
          |O #0#1 = "contanex  ";  |O #0#1 = "contasoc  ";
          |O #0#1 = "basica    ";  |O #0#1 = "laboral   ";
             eaAplica = #0#1;  |QBF aAplica;
             aAlfa    = ":basica/zmonedas;"
             |SUB_EJECUTA_NP aAlfa;
         |SINO;
             aAplica       = #0#1;  |QBF aAplica;
             aEmpresa      = #0#2;  |QBF aEmpresa;
             aAlfa         = ":agitool/zmonedas;*" + aAplica + "*" + aEmpresa + "*ALTA";
             enDesdeConver = 1;
             |SUB_EJECUTA_NP aAlfa;
         |FINSI;
     |FINSI;

     |ESTADO_CONTROL nBoton1, "SHOW";
     |ESTADO_CONTROL nBoton2, "SHOW";
     |ESTADO_CONTROL nBoton3, "SHOW";

     |PTEC 802;
     |PTEC 802;
     |PTEC 802;
|FPRC;

|PROGRAMA;
     aFichero = ("e04" + (Usuario % 108) + "zzzzzzzz") % 108;
     |NOME_DAT #4 aFichero;
     |ABRE #4;  |CIERRA #4;  |DELINDEX #4;

     |DFICE aAlfa;
     aAlfa = aAlfa + "chequeomas.txt";
     |XABRE "E", aAlfa, nHandle;
     nImprimir = FSalida;

     |CABEZA "      Conversion de Moneda";
     |PINPA #0#0;
     |PINDA #0#0;

     |CONTROL_SIMPLE 0, "BOTON,Convertir a EURO", 1447, 1679, Boton1;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,Reconvertir a PESETA", 1747, 1979, Boton2;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,Visualizar Configuracion", 2047, 2279, Boton3;
     nBoton3 = FSalida;

     |ESTADO_CONTROL nBoton1, "DISABLE";
     |ESTADO_CONTROL nBoton2, "DISABLE";

     |SI eaBase ! "";
         |HAZ DesdeFuera;

         |FIN_CONTROL_WINDOWS nBoton1;
         |FIN_CONTROL_WINDOWS nBoton2;
         |FIN_CONTROL_WINDOWS nBoton3;
         |PULSATECLA;

         |ACABA;
     |FINSI;

     |ENTLINEAL #1#0, -1, 2, 22, 0, ClaveBaja0, ClaveAlta0;

     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;

     |ABRE #4;  |CIERRA #4;  |DELINDEX #4;
     |PULSATECLA;
|FPRO;
