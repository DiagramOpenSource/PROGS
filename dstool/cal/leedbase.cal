|FICHEROS;
    leedbase #0;         || def. principal
    leedbftm #1;         || tmp. para campos
    leedbfay #2,MANTE;   || def. peticion datos
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    alfa5 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    fabre = "";
    fcanl = "";
    field = "";
    any = 0;
    mes = 0;
    dia = 0;
    bytes = 0;
    campo = 0;
    campoalf = "";
    pos_lee = 0;
    pos_gra = 0;
    pos_gra_ant = 0;
    fabre2 = "";
    fcanl2 = "";
    field2 = "";
    graba = 0;
    leido = 0;
    regis = 0;
    borra = 0;
    desde = "";
    hasta = "";
    sw_seleccio = 0;

    nhexa = "";
    ndeci = 0;
    nalfa1 = "";
    nalfa2 = "";
    nnume1 = 0;
    nnume2 = 0;
    npara1 = 0;
    npara2 = 0;
    npara3 = 0;
    nlongi = 0;

    vuelt0 = "";
    vuelt1 = "";
    vuelt2 = "";
    longa = "";
    longn = 0;
    vuelta = 0;
    coger = 0;
    flg_bor = 0;
|FIN;
____________________________________/
|PROCESO deci_hexa;
    nhexa = "";
|PARA npara1 = 2; |SI npara1 ] 1; |HACIENDO npara1 = npara1 - 1;
    |SI npara1 = 1; nnume1 = ndeci $ 16;          |FINSI;
    |SI npara1 = 2; nnume1 = (ndeci / 16) $ 16;   |FINSI;

    |SI nnume1 < 10;     nhexa = nhexa + nnume1;  |FINSI;
    |SI nnume1 = 10;     nhexa = nhexa + "A";     |FINSI;
    |SI nnume1 = 11;     nhexa = nhexa + "B";     |FINSI;
    |SI nnume1 = 12;     nhexa = nhexa + "C";     |FINSI;
    |SI nnume1 = 13;     nhexa = nhexa + "D";     |FINSI;
    |SI nnume1 = 14;     nhexa = nhexa + "E";     |FINSI;
    |SI nnume1 = 15;     nhexa = nhexa + "F";     |FINSI;
|SIGUE;
|FPRC;

|PROCESO hexa_deci;
    nalfa1 = nhexa % 0;
    nlongi = nalfa1;
    ndeci = 0;
|PARA npara1 = nlongi; |SI npara1 ] 1; |HACIENDO npara1 = npara1 - 1;

        nnume1 = (((nlongi - npara1) + 1) * 100) + 1; || pos.en la cadena
        nalfa2 = nhexa % nnume1;    || contenido de esa posicion
        nnume2 = nalfa2;            || contenido numerico
    |SI nalfa2 = "A";     nnume2 = 10;   |FINSI;
    |SI nalfa2 = "B";     nnume2 = 11;   |FINSI;
    |SI nalfa2 = "C";     nnume2 = 12;   |FINSI;
    |SI nalfa2 = "D";     nnume2 = 13;   |FINSI;
    |SI nalfa2 = "E";     nnume2 = 14;   |FINSI;
    |SI nalfa2 = "F";     nnume2 = 15;   |FINSI;

        nnume1 = 1;
        npara3 = npara1 - 1;
    |PARA npara2 = 1; |SI npara2 [ npara3; |HACIENDO npara2 = npara2 + 1;
            nnume1 = nnume1 * 16;
    |SIGUE;
        ndeci = ndeci + (nnume1 * nnume2);

|SIGUE;
|FPRC;

|PROCESO da_vuelta;
    longa = vuelt1 % 0;   longn = longa;
    longn = longn - 1;
    vuelt2 = "";
|PARA vuelta = longn; |SI vuelta ] 1; |HACIENDO vuelta = vuelta - 2;
        coger = (vuelta * 100) + 2;
        vuelt0 = vuelt1 % coger;
        vuelt2 = vuelt2 + vuelt0;
|SIGUE;
|FPRC;

|PROCESO leyendo_asc;
    vuelt1 = "";
|PARA para1 = 1; |SI para1 [ bytes; |HACIENDO para1 = para1 + 1;
        field = fcanl + " 1";
    |FLEE field;
        alfa5 = field % 0;
        nume1 = alfa5;
    |SI nume1 = 1;                      || el caracter cero lo desprecia
            vuelt1 = vuelt1 + field;
    |FINSI;
|SIGUE;
|FPRC;

|PROCESO leyendo_hex;
    vuelt1 = "";
|PARA para1 = 1; |SI para1 [ bytes; |HACIENDO para1 = para1 + 1;
        field = fcanl + " 1";
    |FLEE field;
        alfa5 = field % 0;
        nume1 = alfa5;
    |SI nume1 > 1;
            ndeci = 0;        || caracter cero (lo transforma a 2 bytes)
    |SINO;
            ndeci = &field;
    |FINSI;
    |HAZ deci_hexa;
        vuelt1 = vuelt1 + nhexa;
|SIGUE;
|HAZ da_vuelta;
    nhexa = vuelt2;
|HAZ hexa_deci;
|FPRC;
____________________________________/ DBase III
|PROCESO campos3;
    field = fcanl + " 20"; |FLEE field; || espacio reservado DBase I

    alfa1 = Usuario;     |QBF alfa1;
|NOME_DAT #1 alfa1;
|DELINDEX #1;
|ABRE #1;
|PARA campo = 1; |SI campo [ #0#6; |HACIENDO campo = campo + 1;
    |DDEFECTO #1;
        #1#0 = campo;                   || nro.campo
        bytes = 11; |HAZ leyendo_asc;
        #1#1 = vuelt1;                  || descripcion
        bytes = 1;  |HAZ leyendo_asc;
    |SI vuelt1 = "C";    #1#4 = "ALFANUMERI"; |FINSI;
    |SI vuelt1 = "N";    #1#4 = "NUMERICO  "; |FINSI;  || tipo de campo
    |SI vuelt1 = "L";    #1#4 = "LOGICO    "; |FINSI;
    |SI vuelt1 = "D";    #1#4 = "FECHA     "; |FINSI;
    |SI vuelt1 = "M";    #1#4 = "MEMORIA   "; |FINSI;
        bytes = 4;  |HAZ leyendo_asc;   || (basura)
        bytes = 1;  |HAZ leyendo_hex;
        #1#2 = ndeci;                   || longitud campo
        bytes = 1;  |HAZ leyendo_hex;
        #1#3 = ndeci;                   || nro. decimales
        bytes = 14;  |HAZ leyendo_asc;  || (basura)
    |GRABA 020#1n;
|SIGUE;
|CIERRA #1;
|FPRC;

|PROCESO lon_regis3;
    bytes = 2; |HAZ leyendo_hex;  #0#5 = ndeci;
|FPRC;

|PROCESO lon_cabec3;
    bytes = 2; |HAZ leyendo_hex;  #0#4 = ndeci;

    #0#6 = (#0#4 - 33) / 32;  || nro.campos
|FPRC;

|PROCESO nro_regis3;
    bytes = 4; |HAZ leyendo_hex;  #0#3 = ndeci;
|FPRC;

|PROCESO ult_acceso3;
    field = fcanl + " 1";     |FLEE field;   any = &field;  || lee a¤o
    field = fcanl + " 1";     |FLEE field;   mes = &field;  || lee mes
    field = fcanl + " 1";     |FLEE field;   dia = &field;  || lee dia

    alfa1 = dia;    alfa1 = ("00" + alfa1) % -102;
    alfa2 = mes;    alfa2 = ("00" + alfa2) % -102;
|SI any < 80;  any = any + 2000;   |SINO;    any = any + 1900;   |FINSI;
    alfa3 = alfa1 + "." + alfa2 + "." + any;
    #0#2 = alfa3;
|FPRC;
____________________________________/ DBase II
|PROCESO lon_cabec2;
    #0#4 = 521;          || cabecera fija
|FPRC;

|PROCESO lon_regis2;
    bytes = 2; |HAZ leyendo_hex;  #0#5 = ndeci;
|FPRC;

|PROCESO ult_acceso2;
    field = fcanl + " 1";     |FLEE field;   mes = &field;  || lee mes
    field = fcanl + " 1";     |FLEE field;   dia = &field;  || lee dia
    field = fcanl + " 1";     |FLEE field;   any = &field;  || lee any

    alfa1 = dia;    alfa1 = ("00" + alfa1) % -102;
    alfa2 = mes;    alfa2 = ("00" + alfa2) % -102;
|SI any < 80;  any = any + 2000;   |SINO;    any = any + 1900;   |FINSI;
    alfa3 = alfa1 + "." + alfa2 + "." + any;
    #0#2 = alfa3;
|FPRC;

|PROCESO nro_regis2;
    bytes = 2; |HAZ leyendo_hex;  #0#3 = ndeci;
|FPRC;
____________________________________/ DBase II/III
|PROCESO ent_min;
    #1#0 = 1;
|FPRC;

|PROCESO ent_max;
    #1#0 = 9999;
|FPRC;

|PROCESO version;
    field = fcanl + " 1";     |FLEE field;        || lee version

    nume1 = &field;
|SI nume1 = 2;  #0#1 = "II";   |FINSI;
|SI nume1 = 3;  #0#1 = "III";  |FINSI;
|FPRC;

|PROCESO tipo3; |TIPO 3;
    fabre = #0#0;
|QBF fabre;
    fabre = "rb  " + fabre;
|FABRE fabre;
|SI FSalida ] 0;
        fcanl = FSalida;
    |HAZ version;
    |SI #0#1 = "II   ";       || DBase II
        |HAZ nro_regis2;
        |HAZ ult_acceso2;
        |HAZ lon_regis2;
        |HAZ lon_cabec2;
    |FINSI;
    |SI #0#1 = "III  ";       || DBase III
        |HAZ ult_acceso3;
        |HAZ nro_regis3;
        |HAZ lon_cabec3;
        |HAZ lon_regis3;
        |HAZ campos3;
    |FINSI;
    |FCIERRA fcanl;
        Pos = -1;
    |SI #0#1 = "III  ";       || solamente en DBase III (de momento)
        |HAZ lo_demas;
    |SINO;
        |MENAV "    No estoy preparado para exportar el formato DBase II ...";
    |FINSI;
    |DELINDEX #1;
|SINO;
        alfa1 = "    [" + (fabre % 5) + "] no accesible intentando lectura ...";
    |MENAV alfa1;
|FINSI;
|FPRC;
____________________________________/ EXPORTACION A SECUENCIAL
|PROCESO graba_fin;
|SI flg_bor = 32;
        Pos = pos_gra;
        field2 = fcanl2 + " " + &13 + &10;
    |FGRABA field2;
        pos_gra = Pos;
|FINSI;

    leido = leido + 1;
    alfa1 = "Registros Leidos .: [" + leido + "]";
|PINTA 1804, alfa1;
|FPRC;

|PROCESO graba_campo;
|SI flg_bor = 32;
    |SI #2#4 = "S";
            desde = #1#5;   |QBF desde;
            hasta = #1#6;   |QBF hasta;
        |SI desde > campoalf;|O hasta < campoalf;
                sw_seleccio = 1;
        |FINSI;
    |FINSI;

    |SI #1#4 = "FECHA     ";
            campoalf = (campoalf % 7) + "." + (campoalf % 502) + "." + (campoalf % 104);
            #1#2 = 10;
    |FINSI;

        Pos = pos_gra;
        field2 = fcanl2 + " " + campoalf;
    |FGRABA field2;
        pos_gra = Pos;
|FINSI;
|FPRC;

|PROCESO lee_campos;
    Pos = pos_lee;
    field = fcanl + " 1";  |FLEE field;
    pos_lee = Pos;

    alfa5 = field % 0;  nume1 = alfa5;
|SI nume1 > 1;
        field = " ";     || caracter cero (lo transforma a 2 bytes)
|FINSI;
    campoalf = campoalf + field;
|FPRC;

|PROCESO quita_cabecera;
|PARA para1 = 1; |SI para1 [ #0#4; |HACIENDO para1 = para1 + 1;
        field = fcanl + " 1";
    |FLEE field;
|SIGUE;
    pos_lee = Pos;
|FPRC;

|PROCESO exporta;
|ABRE #1;
    fabre = #0#0;   |QBF fabre;    fabre = "rb  " + fabre;
|FABRE fabre;
|SI FSalida ] 0;
        fcanl = FSalida;

    |HAZ quita_cabecera;

        Pos = -1;
        fabre2 = #2#1;   |QBF fabre2;   fabre2 = "wb  " + fabre2;
    |FABRE fabre2;
    |SI FSalida ] 0;
            fcanl2 = FSalida;
            pos_gra = 0;

        |PARA regis = 1;|SI regis [ #0#3;|HACIENDO regis = regis + 1;
                sw_seleccio = 0;
                pos_gra_ant = pos_gra;
                campoalf = "";
            |HAZ lee_campos;
                flg_bor = &campoalf;
            |PARA campo = 1;|SI campo [ #0#6;|HACIENDO campo = campo + 1;
                    #1#0 = campo;
                |LEE 020#1=;
                |SI FSalida = 0;
                        campoalf = "";
                    |PARA longn = 1;|SI longn [ #1#2;|HACIENDO longn = longn + 1;
                        |HAZ lee_campos;
                    |SIGUE;
                    |HAZ graba_campo;
                |FINSI;
            |SIGUE;
            |HAZ graba_fin;
            |SI sw_seleccio ! 0;|O flg_bor ! 32;
                    pos_gra = pos_gra_ant;
                |SI flg_bor ! 32;
                        borra = borra + 1;
                        alfa1 = "Registros Borrados: [" + borra + "]";
                     |PINTA 2004, alfa1;
                |FINSI;
            |SINO;
                    graba = graba + 1;
                    alfa1 = "Registros Grabados: [" + graba + "]";
                |PINTA 1904, alfa1;
            |FINSI;
        |SIGUE;
        |MENAV "    < Proceso Finalizado >";

        |FCIERRA fcanl2;
            Pos = -1;
    |SINO;
            alfa1 = "    [" + (fabre % 5) + "] no accesible intentando escritura ...";
        |MENAV alfa1;
    |FINSI;
    |FCIERRA fcanl;
        Pos = -1;
|SINO;
        alfa1 = "    [" + (fabre % 5) + "] no accesible intentando lectura ...";
    |MENAV alfa1;
|FINSI;
|CIERRA #1;
|FPRC;
____________________________________/ CREACION DEL .DEE
|PROCESO graba_dee;
    field2 = fcanl2 + " " + field2 + &13 + &10;
|FGRABA field2;
|FPRC;

|PROCESO para_agipase;
|PINTA 2004, "Grabando fichero .DEE";

|ABRE #1;
    fabre2 = #2#3;   |QBF fabre2;   fabre2 = "wb  " + fabre2;
|FABRE fabre2;
|SI FSalida ] 0;
        fcanl2 = FSalida;

    |DDEFECTO #1;
    |PARA para1 = 1;|SI para1 [ 3;|HACIENDO para1 = para1 + 1;
            field2 = "";
        |HAZ graba_dee;
    |SIGUE;

        nume1 = #0#6 + 2;     field2 = nume1;     |HAZ graba_dee;
    |PARA campo = 1;|SI campo [ #0#6;|HACIENDO campo = campo + 1;
            #1#0 = campo;
        |LEE 020#1=;
        |SI FSalida = 0;
            |SI #1#4 = "FECHA     "; #1#2 = 10;     |FINSI;
                field2 = #1#1 + ",A," + #1#2;
            |HAZ graba_dee;
        |FINSI;
    |SIGUE;

        field2 = "<CARAC. 13>,A,1";     |HAZ graba_dee;
        field2 = "<CARAC. 10>,A,1";     |HAZ graba_dee;

    |FCIERRA fcanl2;
        Pos = -1;
|SINO;
        alfa1 = "    [" + (fabre % 5) + "] no accesible intentando escritura ...";
    |MENAV alfa1;
|FINSI;
|CIERRA #1;
|FPRC;
____________________________________/ PROCESOS DE EDICION Y EXPORTACION
|PROCESO no_exporta; |TIPO 7;
    alfa1 = #2#0;
|C_M #2#1, 1, alfa1;
|C_M #2#4, 1, alfa1;
|FPRC;

|PROCESO no_dee; |TIPO 7;
    alfa1 = #2#2;
|C_M #2#3, 1, alfa1;
|FPRC;

|PROCESO mayus; |TIPO 0;
    #2Campo = #2Campo > #2Campo;
|PINTA #2Campo;
|FPRC;

|PROCESO lo_demas;
|PINDA #0#0;

|PUSHV 0501 2380;
|ENTLINEAL #1#1, 1, 3, 22, 1, ent_min, ent_max;
|POPV;

|PUSHV 0501 2380;
|DDEFECTO #2;
|PINPA #0#2;
|PINDA #0#2;
|ENDATOS #1#2;
|SI FSalida = 0;
    |POPV;
    |SI #2#0 = "S"; |HAZ exporta;       |FINSI;   || exportacion secuencial
    |SI #2#2 = "S"; |HAZ para_agipase;  |FINSI;   || creacion fichero .dee
|SINO;
    |POPV;
|FINSI;
|FPRC;
