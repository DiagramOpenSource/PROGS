|FICHEROS;
     dsb2jz01 #0;
     dsb2jm01 #1;
|FIN;

|VARIABLES;
     aUsuario = "";
     nExiste = 0;
     aMensaje = "";
     aRespuesta = "";

     &PRMNT_comando = "";               ||Por ahora solo tenemos "WSNOTAS"
                                        ||Buscara la URL en agitool.ini
                                        ||NO SE ESTA UTILIZANDO

     &PRMNT_aplicacion = "";            ||Por ejemplo.   dspartes
     &PRMNT_empresa = "";               ||Codigo Empresa (relleno con ceros o null)
     &PRMNT_programa = "";              ||Nombre Programa (lo que queramos)
     &PRMNT_iddocumento = "";           ||ID del documento (Debe ser unico)

     aToken1 = "";
     aToken2 = "";
     aToken3 = "";
     aToken4 = "";
     aToken5 = "";
     aUrlMemos = "";

     fFecha = @;
     aFecha = "";
     nSinToken = 0;
     aError = "";

     rutaLogin = "";
     urlb2j = "";
     userb2j = "";
     passb2j = "";
     urlwsnotas = "";

     aBase = "";
     aAbre = "";
     aIP = "";
     nHandle = 0;
     aCadena = "";
     aBusca = "";
     aPython = "";
     aQS = "";
     aEmp = "";
     aAplicacion = "";
     aPrograma = "";
     aID = "";

     aPathPlugins = "";
     aPyScript = "";
     aRes = "";
     aEjecuta = "";
     aAcceso = "";

     aLetra = "";
     aUrl = "";
     nSeg = 0;
     aTexto = "";
     nTrozo = 0;
     aTrozo = "";
     nVoy = 0;
     nHandleRes = 0;

     nVentPLB = 0;
     aBorra = "";

     {1}aLocal = "";
     aBaseLocal = "";
     aFicheroHtml = "";
     aPathTemporal = "";
     aNombreHtml = "";
     aLinea = "";

     {1}version = "";
     aInfo = "";
     aVersion = "";
|FIN;

|PROCESO ObtenerToken;
     ||Obtener Token B2J

     aToken1 = "";
     aToken2 = "";
     aToken3 = "";
     aToken4 = "";
     aToken5 = "";
     aUrlMemos = "";

     aEmp = PRMNT_empresa;
     aUsuario = ("rt" + Usuario) % 108

     urlb2j = "";
     userb2j = "";
     passb2j = "";
     urlwsnotas = "";
     aPython = "";

     |DBASE aBase;  |QBF aBase;
     aAbre = aBase + "agitool.ini";

     aAcceso = (aBase % 703) + "_" + (aBase % 1001);

     aIP = ""; |IP_REMOTO aIP;
     |SI aIP = "";       || modo local
          |XABRE "C", aAbre, nHandle;
     |SINO;              || modo C/S
          |XABRE "", aAbre, nHandle;
     |FINSI;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     aCadena    = "";

     |PARA;  |SI;  |HACIENDO;
          |XLEE nHandle, 250, aCadena;
          |SI FSalida < 0;  |SAL;  |FINSI;
          |QBF aCadena;

          ||urlb2j
          aBusca = aCadena % 107;
          |SI aBusca = "URLB2J=";
               urlb2j = aCadena % 8;
          |FINSI;

          ||userb2j
          aBusca = aCadena % 108;
          |SI aBusca = "USERB2J=";
               userb2j = aCadena % 9;
          |FINSI;

          ||passb2j
          aBusca = aCadena % 108;
          |SI aBusca = "PASSB2J=";
               passb2j = aCadena % 9;
          |FINSI;

          ||urlwsnotas
          aBusca = aCadena % 111;
          |SI aBusca = "URLWSNOTAS=";
               urlwsnotas = aCadena % 12;
               aUrlMemos = urlwsnotas;
          |FINSI;

          ||python
          aBusca = aCadena % 107;
          |SI aBusca = "python=";
               aPython = aCadena % 12;
          |FINSI;
     |SIGUE;

     |XCIERRA nHandle;

     |SI urlb2j = ""; |O urlwsnotas = "";
          aError = "Error con los parametros del agitool.ini";
          |ACABA;
     |SINO;
          aUrl = urlb2j + rutaLogin;
     |FINSI;

     |SI aPython = "";
         aPython  = "python2.7";

         aIP = ""; |IP_REMOTO aIP;
         aQS = ""; |QUE_SISTEMA aQS;
         |SI aIP = ""; |O aQS = "ESDOS";
             aPython  = "python";
         |FINSI;
     |FINSI;

     aPathPlugins = aBase + "plugins/";
     aPyScript = aPathPlugins + "dameTokenB2J.py";
     aRes = aBase + aUsuario + ".txt";
     |FBORRA aRes;

     aEjecuta = aPython;
     aEjecuta = aEjecuta + " " + aPyScript;
     aEjecuta = aEjecuta + " " + aUrl;
     aEjecuta = aEjecuta + " " + userb2j;
     aEjecuta = aEjecuta + " " + passb2j;
     aEjecuta = aEjecuta + " " + aAcceso;
     aEjecuta = aEjecuta + " " + aEmp;
     aEjecuta = aEjecuta + " " + aRes;

||TODO:: alba. Comentar este trozo y descomentar la parte de abajo.
||             previamente se ha de copiar a /tmp/ccc/ara.txt la parte del token

     |SYSTEM aEjecuta;

     nSeg = 0;
     |PARA;  |SI;  |HACIENDO;
          |XABRE "E", aRes, 0;
          |SI FSalida ] 0;  |SAL;  |FINSI;

          |SLEEP 1;

          nSeg = nSeg + 1;
          |SI nSeg ] 3;  |SAL;  |FINSI;

          |PULSATECLA;
     |SIGUE;

     ||TODO:: @alba.  Tengo que copiar previamente en /tmp/ccc el
     || resultado de
     ||aRes = "/tmp/ccc/ara.txt";

     aTexto = "";
     nTrozo = 0;
     aTrozo = "";
     nVoy = 0;
     |XABRE "BU", aRes, nHandleRes;
     |SI FSalida ] 0;
          |PARA; |SI; |HACIENDO;
               |XLEE nHandleRes, 200, aTrozo;

               |SI FSalida > 0;
                    |QBF aTrozo;
                    nTrozo = nTrozo + 1;
                    |SI nTrozo = 1;
                         aBusca = aTrozo % 105;
                         aBusca = aBusca > aBusca;
                         |SI aBusca = "ERROR";
                              aError = aTrozo;
                              |SAL;
                         |SINO;
                              aToken1 = aTrozo;
                         |FINSI;
                    |SINO;
                         |SI nTrozo = 2;
                              aToken2 = aTrozo;
                         |FINSI;
                         |SI nTrozo = 3;
                              aToken3 = aTrozo;
                         |FINSI;
                         |SI nTrozo = 4;
                              aToken4 = aTrozo;
                         |FINSI;
                         |SI nTrozo = 5;
                              aToken5 = aTrozo;
                         |FINSI;
                         |SI nTrozo = 6;
                              |SAL;
                         |FINSI;
                    |FINSI;
               |FINSI;
               |SI FSalida [ 0;
                    |SAL;
               |FINSI;
          |SIGUE;
          |XCIERRA nHandleRes;
     |SINO;
          aError = "Error. Fichero respuesta B2J no accesible";
     |FINSI;
|FPRC;

|PROCESO CreaLanzadera;
     |ESPECIFICA "RBASS", aLocal;
     aBaseLocal = aLocal;

     aPathTemporal = aBaseLocal + "tmp/";
     |RMKDIR aPathTemporal;

     aUsuario = Usuario % 810;
     |QBF aUsuario;
     aNombreHtml = aUsuario + ".html";

     aFicheroHtml = aPathTemporal + aNombreHtml;

     |RFBORRA aFicheroHtml;

     |XABRE "CWB", aFicheroHtml, nHandle;
     |SI FSalida ] 0;
          aLinea = "<!DOCTYPE html PUBLIC " + &34 + "-//W3C//DTD XHTML 1.0 Strict//EN" + &34 + " " + &34 + "http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd" + &34 + ">";
          |XGRABA nHandle, aLinea;

          aLinea = "<html xmlns=" + &34 + "http://www.w3.org/1999/xhtml" + &34 + ">";
          |XGRABA nHandle, aLinea;

          aLinea = "  <head>";
          |XGRABA nHandle, aLinea;

          aLinea = "    <meta http-equiv=" + &34 + "content-type" + &34 + " content=" + &34 + "text/html; charset=utf-8" + &34 + "/>";
          |XGRABA nHandle, aLinea;

          aLinea = "    <script type=" + &34 + "text/javascript" + &34 + ">";
          |XGRABA nHandle, aLinea;

          aLinea = "        window.onload = function() {";
          |XGRABA nHandle, aLinea;

          aLinea = "                   document.forms[" + &34 + "newdata" + &34 + "].submit();";
          |XGRABA nHandle, aLinea;

          aLinea = "        }";
          |XGRABA nHandle, aLinea;

          aLinea = "    </script>";
          |XGRABA nHandle, aLinea;

          aLinea = "  </head>";
          |XGRABA nHandle, aLinea;

          aLinea = "  <body>";
          |XGRABA nHandle, aLinea;

          aLinea = "    <form id=" + &34 + "newdata" + &34 + " name=" + &34 + "newdata" + &34 + " method=" + &34 + "post" + &34 + " action=" + &34 + aUrlMemos + &34 + ">";
          |XGRABA nHandle, aLinea;

          aLinea = "      <input type=" + &34 + "hidden" + &34 + " name=" + &34 + "b2j_token" + &34 + " value=" + &34;
          |XGRABA nHandle, aLinea;

          aLinea = aToken1; |QBF aLinea;
          |SI aLinea ! "";
               |XGRABA nHandle, aLinea;
          |FINSI;
          aLinea = aToken2; |QBF aLinea;
          |SI aLinea ! "";
               |XGRABA nHandle, aLinea;
          |FINSI;
          aLinea = aToken3; |QBF aLinea;
          |SI aLinea ! "";
               |XGRABA nHandle, aLinea;
          |FINSI;
          aLinea = aToken4; |QBF aLinea;
          |SI aLinea ! "";
               |XGRABA nHandle, aLinea;
          |FINSI;
          aLinea = aToken5; |QBF aLinea;
          |SI aLinea ! "";
               |XGRABA nHandle, aLinea;
          |FINSI;
          aLinea = &34 + " />";
          |XGRABA nHandle, aLinea;

          aAplicacion = PRMNT_aplicacion;
          aLinea = "      <input type=" + &34 + "hidden" + &34 + " name=" + &34 + "aplicacion" + &34 + " value=" + &34 + aAplicacion + &34 + " />";
          |XGRABA nHandle, aLinea;

          aEmp = PRMNT_empresa;
          aLinea = "      <input type=" + &34 + "hidden" + &34 + " name=" + &34 + "empresa" + &34 + " value=" + &34 + aEmp + &34 + " />";
          |XGRABA nHandle, aLinea;

          aPrograma = PRMNT_programa;
          aLinea = "      <input type=" + &34 + "hidden" + &34 + " name=" + &34 + "programa" + &34 + " value=" + &34 + aPrograma + &34 + " />";
          |XGRABA nHandle, aLinea;

          aID = PRMNT_iddocumento;
          aLinea = "      <input type=" + &34 + "hidden" + &34 + " name=" + &34 + "iddocumento" + &34 + " value=" + &34 + aID + &34 + " />";
          |XGRABA nHandle, aLinea;

          aLinea = "    </form>";
          |XGRABA nHandle, aLinea;

          aLinea = "  </body>";
          |XGRABA nHandle, aLinea;

          aLinea = "</html>";
          |XGRABA nHandle, aLinea;

          |XCIERRA nHandle;
     |FINSI;
|FPRC;

|PROCESO LLamadaWSMemos;
     |SI aVersion = "DiagramX";
          |VENTANA 0501, 0560, -1, 16, "Procesando datos web (se abrir  una nueva pesta¤a en el navegador)";
          nVentPLB = FSalida;

          |PTEC 802;  |PAUSA;
          |CLS;
     |SINO;
          |CLS;
          |PINPA #0#dsb2jz01;
          |PTEC 802;  |PAUSA;
     |FINSI;


     |HAZ CreaLanzadera;

     ||Descomentar
     aEjecuta = "cmd " + &34 + "/c start " + aFicheroHtml + &34;
     |RSYSTEM aEjecuta;

     ||Nos esperamos 12 segundos y borramos el lanzador
     |SLEEP 12;

     || Esto del borrado es poco chapuza, pero por ahora no se me ocurre otra
     || forma
     aBorra = aFicheroHtml;
     |RFBORRA aBorra;

     |SI aVersion = "DiagramX";
          |FINVENTANA nVentPLB;
     |FINSI;
|FPRC;

|PROGRAMA;
     |ESPECIFICA "VERSIONBASICO", version;
     aInfo = version1;
     |QBF aInfo;
     aInfo = aInfo % 9;

     aVersion = aInfo % 101;
     |SI aVersion = "9";
          aVersion = "Diagram9";
     |SINO;
          aVersion = "DiagramX";
     |FINSI;

     rutaLogin = "/api/v2/geconet/login/"
     nSinToken = 1;
     fFecha = ~;
     aFecha = fFecha;
     |CLS;
     |ABRE #dsb2jm01;
     |DDEFECTO #dsb2jm01;
     |LEE 000#dsb2jm01.p;
     nExiste = FSalida;
     |SI FSalida = 0;
          |SI #dsb2jm01#1 = aFecha;
               aToken1 = #dsb2jm01#2;   |QBF aToken1;
               aToken2 = #dsb2jm01#3;   |QBF aToken2;
               aToken3 = #dsb2jm01#4;   |QBF aToken3;
               aToken4 = #dsb2jm01#5;   |QBF aToken4;
               aToken5 = #dsb2jm01#6;   |QBF aToken5;
               aUrlMemos = #dsb2jm01#7;   |QBF aUrlMemos;
               nSinToken = 0;
          |FINSI;
     |FINSI;

     |SI nSinToken = 1;
          |HAZ ObtenerToken;
          |SI aToken1 ! "";
               |DDEFECTO #dsb2jm01;
               #dsb2jm01#0 = 1;
               |LEE 101#dsb2jm01.=;
               |SI FSalida ! 0; |GRABA 020#dsb2jm01.b; |FINSI;
               #dsb2jm01#1 = fFecha;
               #dsb2jm01#2 = aToken1;
               #dsb2jm01#3 = aToken2;
               #dsb2jm01#4 = aToken3;
               #dsb2jm01#5 = aToken4;
               #dsb2jm01#6 = aToken5;
               #dsb2jm01#7 = aUrlMemos;
               |GRABA 020#dsb2jm01.a;
               |LIBERA #dsb2jm01;
          |FINSI;
     |FINSI;

     |SI aToken1 = ""; |O aError ! "";
          |SI aError ! "";
               aMensaje = "0000" + aError;
          |SINO;
               aMensaje = "0000Problemas para obtener el Token";
          |FINSI;
          |MENAV aMensaje;
     |SINO;
          |HAZ LLamadaWSMemos;
     |FINSI;
     |CIERRA #dsb2jm01;
|FPRO;
