||dsactual.    Actualizacion de las aplicaciones de forma automatica.
||desde www.diagram.es/clientes/diagram/aplica/

||Resultado del registro de las paginas web.
||www.geconet.net/adm/actualiza/dsactualiza.log

||Bloquear la pantalla con un lee o un graba de forma que
||solo pueda entrar cada vez un unico usuario, tal y como
||ocurre en el programa master.         OK

||Basicamente consiste en:
||Bajar el nuevo paquete/s
||Comparar version actual y nueva.
||Si se decide instalar.
||Reinstalar este nuevo paquete.

||Vamos a utilizar el commando "wget", disponible en Unix i Windows.
||Junto con tres peque¤as shell scripts que lo utilizan
||dswcopia
||dswlista
||dswocupa

||En desarrollo:
||Para que funcione hay que cambiar todas las referencias de agitool por
||dstool:
||1. En los menus de cada aplicacion . Update
||2. En el cal.  (parte FICHEROS)
||3. En el cal.  (cambiar el literal)
||4. En la definicion de la pantalla
||5. En el dsactua2. En el cal. (literal)

||Bajamos los paquetes desde
||www.diagram.es/clientes/diagram/aplica/


|FICHEROS;
     :/agitool/def/agiparam #100;      ||Parametros Configuracion  (para Proxy)
||En el cliente
     :/agitool/def/dsactual #0;
||En desarrollo
||     :/dstool/def/dsactual #0;
|FIN;

|VARIABLES;
     {-1}aSerie = "";
     nIni = 0;


     aReturnDos = "";
     aDirFtp = "";
     aPathPlugins = "";
     {1}aContiene = "";
     aDocRemoto = "";
     aDocServidor = "";
     aAbre = "";
     aDirRecoge = "";
     aFichero = "";

     aBasico = "";
     aVersion = "";
     nHandleVer = 0;
     aEco = "";
     aBorra = "";
     aSistema = "";
     aEjecuta = "";
     aBaseServer = "";
     aAux = "";
     fFecha = @;
     aFecha = "";
     aHora = "";
     aTxtLog = "";
     nHandleLog = 0;

     aBase = "";
     aDirLog = "";
     aLogs = "";

     nPtec = 0;
     &eaNumIni = "";
     aIni = "";

     &enSwSilencio = 0;

     aAlfa = "";
     aIP = "";
     aPuerto = "";
     aDirLocal = "";
     {1}aLocal = "";
     aMensaje = "";
     aParametro = "";
     aPaq = "";
     aAlfa1 = "";
     aAlfa2 = "";
     aUsu = "";
     aPw = "";
     aR = "";
     aA = "";
     aY = "";
     aB = "";
     aX = "";
     aL = "";
     aG = "";
     aJ = "";
     aS = "";
     aTmp = "";
     aTmpServer = "";
     aLogWget = "";
     nSwSal = 0;
     nHandle = 0;
     nCanal = 0;
     aLinea = "";
     aLineaAnt = "";
     aLineaAnt2 = "";
     aDatos = "";
     aCaracter = "";
     aSigno = "";
     aPtge = "";
     nContador = 0;
     nContTime = 0;      ||Numero de segundos para que termine el proceso
     aDirBase = "";
     aLong = "";
     nLong = 0;
     aSubCadena = "";
     nSubCadena = 0;
     aNumero = "";
     aLonguitud = "";
     nNumLin = 0;
     nNumLinAnt = 0;
     nBucle = 0;
     aBucle = "";
     aSaved = "";
     aStr = "";
     aBarra = "";
     nPorcien = 0;
     nSwOk = 0;
     aPar1 = "";    ||Parametro 1: Paquete
     aPar2 = "";    ||Paremetro 2: Nombre del menu.
     aVerAct = "";  ||Numero version actual
     aVerNue = "";  ||Numero version nueva
     aPath = "";
     aMenu = "";
     aDes = "";
     nSoloDir = 0;
     aFicRwi = "";
     aNomDir = "";
     nSwNuevoPaq = 0;
     nSwEncontrado = 0;
     nSwCopyIns = 0;
     aPathDswget = "";
     nNume1 = 0;
     aDirectorio = "";
     nSwNotFound = 0;
     aNotFound = "";
     aOrigen = "";
     aDestino = "";
     aIPRemoto = "";
|FIN;

|PROCESO Logwget;
     |QBF aPar2;
     |QBF aVerAct;
     |QBF aVerNue;
     aAlfa1 = aPathDswget + "/dsweblog " + aPathDswget + " " + aIni + "@" + aPar2 + "@" + aVerAct+ "@" + aVerNue;
     |SI #agiparam#1 = "N";
          aAlfa1 = aAlfa1 + " " + "N";
     |SINO;
          aIP = #agiparam#2;
          |QBF aIP;
          aIP = aIP + "." + #agiparam#3;
          |QBF aIP;
          aIP = aIP + "." + #agiparam#4;
          |QBF aIP;
          aIP = aIP + "." + #agiparam#5;
          aPuerto = #agiparam#6;
          |QBF aPuerto;
          aAlfa1 = aAlfa1 + " " + "S" + " " + aIP + ":" + aPuerto;
     |FINSI;

     aTxtLog = aAlfa1;
     |HAZ MeteLog;

     |SYSTEM aAlfa1;
|FPRC;

|PROCESO WebLog;
     aAlfa = "start www.geconet.net/adm/dsactualiza.php?Ini=" + aIni + "@" + aPar2 + "@" + aVerAct+ "@" + aVerNue ;
     aOrigen = "dsactualiza.bat";
     |XABRE "WC", aOrigen, nCanal;
     |SI FSalida ] 0;
          |XGRABA nCanal, "@echo off";
          |XGRABA nCanal, aAlfa;
          |XCIERRA nCanal;
          |RSYSTEM aOrigen;
     |FINSI;
|FPRC;

|PROCESO AveriguaINI;
     aSerie = "";
     |ESPECIFICA "SERIALIZACION", aSerie;
     aSerie = FSalida;
     nIni = aSerie;
     aIni = ("000000" + nIni) % -106;
|FPRC;

|PROGRAMA;
     |SI PARAMETRO ! "";
        aAux = PARAMETRO;
        |QBF aAux;
        aAux = aAux % -108;
        |SI aAux ! "gesracor";
             |SUB_EJECUTA "|:dstool/dstool07.tab";
             |ACABA;
        |FINSI;
     |FINSI;

     |QUE_SISTEMA aSistema;
     |QBF aSistema;

     ||En ciertos casos NO me pilla bien el numero de INI.
     ||Pero si se llama a este programa desde otra aplicacion.
     ||Da un error de aplicacion NO encontrada.
     ||La mejor solucion es intentar obtener el INI, y sino, llamar al
     ||sub_ejecuta, para que lo obtenga

     aIni = "";
     |HAZ AveriguaINI;
     eaNumIni = aIni;

     |SI eaNumIni = "000000";
          |SUB_EJECUTA "numerini";
          aIni = eaNumIni;
     |FINSI;

     |SI enSwSilencio = 0;
          FSalida = 0;
     |SINO;
          |DBASE aBase;
          |QBF aBase;
          aDirLog = aBase + "logs/";
          |MKDIR aDirLog;
          aLogs = aDirLog + "dsactual.log";

          aParametro = PARAMETRO;
          |QBF aParametro;
          aTxtLog = "Iniciando Actualizacion desde DSCOPIAS (" + aParametro + ")";
          |HAZ MeteLog;

          fFecha = ~;
          aFecha = fFecha;
          |HORA aHora;
          aTxtLog = aFecha + " " + aHora;
          |HAZ MeteLog;

          FSalida = 0;
     |FINSI;
     |SI FSalida=0;
          |ABRE #dsactual;
          |LEE 101#dsactual.p;
          |SI FSalida ! 0; |GRABA 020#dsactual.b; |FINSI;
          aL = "n";
          aJ = "a";
          aX = "c";
          aS = "u";
          aB = "2";
          aG = "1";
          aR = "j";
          aA = "l";
          aY = "a";
          aPw = aG + aB + aG + aJ;
          aParametro = PARAMETRO;
          aUsu = aR + aS + aY + aL;
          aPw = aPw + aA + aX + aG + aB;
          |QBF aParametro;
          |SI aParametro = "";     ||Pedidos el nombre del paquete (tgz) a actualizar
               ||NO hacer nada, necesito el parametro
          |SINO;                   ||En parametro nos viene el nombre del paquete + menu
               aLong = aParametro % A0;
               aLong = ("00" + aLong) % -102;
               nLong = aLong;
               |PARA nContador = 1; |SI nContador < nLong; |HACIENDO nContador = nContador + 1;
                    aSubCadena = nContador + "01";
                    nSubCadena = aSubCadena;
                    aCaracter = aParametro % nSubCadena;
                    |SI aCaracter ! " "; |Y aCaracter ! "+";
                         aPar1 = aPar1 + aCaracter;
                    |SINO;
                         nContador = nContador + 1;
                         aSubCadena = nContador + aLong;
                         nSubCadena = aSubCadena;
                         aPar2 = aParametro % nSubCadena;
                         |SAL;
                    |FINSI;
               |SIGUE;
               |CLS;

               |SI enSwSilencio = 0;
                    |ESPECIFICA "RBASS", aLocal;
                    aAux = aLocal;
                    |QBF aAux;
               |SINO;
                    |DBASS aAux;
                    |QBF aAux;
               |FINSI;
               aDirLocal = aAux;

               aPathDswget = aDirLocal + "/agitool";
               |SI enSwSilencio = 0;
                    |RMKDIR aPathDswget;
               |SINO;
                    |MKDIR aPathDswget;
               |FINSI;

               |PINPA #0#dsactual;
               #dsactual#1 = aPar1;
               |PINTA #dsactual#1;
               aPaq = aPar1;

               |DIRECTORIO aDirBase;
               |QBF aDirBase;


               |DBASS aBaseServer;
               |QBF aBaseServer;
               |SI aSistema ! "ESDOS";
                    aTmpServer = aBaseServer + "agitool/tmp2";
               |SINO;
                    aTmpServer = aBaseServer + "agitool\tmp2";
               |FINSI;
               |MKDIR aTmpServer;

               |SI enSwSilencio = 0;
                    aTmp = aPathDswget + "tmp2";
                    |RMKDIR aTmp;
               |SINO;
                    aTmp = aTmpServer;
               |FINSI;

               |HAZ Principal;
               |SI nSwOk = 1;
                    |HAZ Actualiza;
               |FINSI;
          |FINSI;
          |LIBERA #dsactual;
          |CIERRA #dsactual;
     |FINSI;
     |SI enSwSilencio = 1;
          aTxtLog =  "Finalizando Actualizacion desde DSCOPIAS (" + aParametro + ")";
          |HAZ MeteLog;

          fFecha = ~;
          aFecha = fFecha;
          |HORA aHora;
          aTxtLog = aFecha + " " + aHora;
          |HAZ MeteLog;

          aTxtLog = "";
          |HAZ MeteLog;

          |SI aPar1 ! "dscopbas.tgz";
               |SI aVersion ! "";
                    |DBASS aBase;
                    |QBF aBase;
                    aEco = aBase + "dscopias/shells/" + aIni + ".ver";

                    |XABRE "W", aEco, nHandleVer;
                    |SI FSalida ] 0;
                         |XGRABA nHandleVer, aVersion;
                         |XCIERRA nHandleVer;
                    |FINSI;
               |FINSI;
          |SINO;
               ||ARA5
               |SI aBasico ! "";
                    |DBASS aBase;
                    |QBF aBase;
                    aEco = aBase + "dscopias/shells/" + aIni + ".bas";

                    |XABRE "W", aEco, nHandleVer;
                    |SI FSalida ] 0;
                         |XGRABA nHandleVer, aBasico;
                         |XCIERRA nHandleVer;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRO;

|PROCESO Principal;
     |ABRE #agiparam;
     |DDEFECTO #agiparam;
     |LEE 000#agiparam.p;
     |CIERRA #agiparam;

     aMensaje = "Descargando <" + aPaq + "> , Por favor espere ...";
     #dsactual#2 = aMensaje;
     |PINTA #dsactual#2;
     |PTEC 802;
     |ENCAMPO #1#dsactual;

     |SI enSwSilencio = 1;
          aTxtLog = "Descargando <" + aPaq + ">";
          |HAZ MeteLog;
     |FINSI;

     ||ARA
     ||TODO CCC. QUITAR
     ||aSistema = "ESDOS";

     |SI aSistema = "ESUNIX"; |O #agiparam#7 = "S";

          aLogWget = aTmp + "/dswget.log";

          ||Primero averiguo el tama¤o del fichero y si este es accesible.
          aAlfa1 = aPathDswget + "/dswocupa " + aPathDswget + " " + aTmp + " " + aPaq;

          |SI #agiparam#1 = "N";
               aAlfa1 = aAlfa1 + " " + "N";
          |SINO;
               aIP = #agiparam#2;
               |QBF aIP;
               aIP = aIP + "." + #agiparam#3;
               |QBF aIP;
               aIP = aIP + "." + #agiparam#4;
               |QBF aIP;
               aIP = aIP + "." + #agiparam#5;
               aPuerto = #agiparam#6;
               |QBF aPuerto;
               aAlfa1 = aAlfa1 + " " + "S" + " " + aIP + ":" + aPuerto;
          |FINSI;

          aNumero = "";
          #dsactual#3 = "Averiguando tama¤o del fichero: ";
          |PINTA #dsactual#3;
          |PTEC 802;
          |ENCAMPO #1#dsactual;

          |SI enSwSilencio = 1;
               aTxtLog = aAlfa1;
               |HAZ MeteLog;
          |FINSI;

          |SI enSwSilencio = 0;
               |RSYSTEM aAlfa1;
          |SINO;
               |SYSTEM aAlfa1;
          |FINSI;

          nSwNotFound = 0;
          nHandle=1;
          |SI enSwSilencio = 0;
               |XABRE "C", aLogWget, nHandle;
          |SINO;
               |XABRE "U", aLogWget, nHandle;
          |FINSI;
          |PARA; |SI; |HACIENDO;
               |XLEE nHandle, 250, aLinea;
               |SI FSalida < 0;
                    |SAL;
               |SINO;
                    aLong = aLinea % 107;
                    |SI aLong = "Length:";
                         |PARA nContador = 9; |SI nContador < 22; |HACIENDO nContador = nContador + 1;
                              nSubCadena = 100 + nContador;
                              aNumero = aLinea % nSubCadena;
                              aNumero = aNumero % -101;
                              |SI aNumero ! " ";
                                   aLonguitud = aLonguitud + aNumero;
                              |SINO;
                                   |SAL;
                              |FINSI;
                         |SIGUE;
                         |SAL;
                    |SINO;
                         aNotFound = aLinea % -109;
                         |SI aNotFound = "Not Found";
                              nSwNotFound = 1;
                              |SAL;
                         |SINO;
                              aNotFound = aLinea % -110;
                              |SI aNotFound = "Not Found.";
                                   nSwNotFound = 1;
                                   |SAL;
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |SIGUE;
          |XCIERRA nHandle;
          #dsactual#3 = "Averiguando tama¤o del fichero: " + aLonguitud;
          |PINTA #dsactual#3 ;
          |PTEC 802;
          |ENCAMPO #1#dsactual;
          |SI enSwSilencio = 0;
               |RFBORRA aLogWget;
          |SINO;
               |FBORRA aLogWget;
          |FINSI;

          |SI enSwSilencio = 1;
               aTxtLog = "Averiguando tama¤o del fichero: " + aLonguitud;
               |HAZ MeteLog;
          |FINSI;

          ||EL PROCESO DE LA DESCARGA EN SI.
          |QBF aLonguitud;
          |SI aLonguitud ! "";
               aAlfa1 = aPathDswget + "/dswcopia " + aPathDswget + " " + aTmp + " " + aPaq;

               |SI #agiparam#1 = "N";
                    aAlfa1 = aAlfa1 + " " + "N";
               |SINO;
                    aIP = #agiparam#2;
                    |QBF aIP;
                    aIP = aIP + "." + #agiparam#3;
                    |QBF aIP;
                    aIP = aIP + "." + #agiparam#4;
                    |QBF aIP;
                    aIP = aIP + "." + #agiparam#5;
                    aPuerto = #agiparam#6;
                    |QBF aPuerto;
                    aAlfa1 = aAlfa1 + " " + "S" + " " + aIP + ":" + aPuerto;
               |FINSI;

               |SI enSwSilencio = 0;
                    |RSYSTEM aAlfa1;
               |SINO;
                    aTxtLog = aAlfa1;
                    |HAZ MeteLog;

                    |SYSTEM aAlfa1;
               |FINSI;

               ||Ahora abro el log y averiguo los datos relativos a la descarga
               |SLEEP 5;
               nHandle=1;
               nContTime = 0;
               nNumLinAnt = 0;
               |PARA; |SI; |HACIENDO;
                    |SI enSwSilencio = 0;
                         |XABRE "C", aLogWget, nHandle;
                    |SINO;
                         |XABRE "U", aLogWget, nHandle;
                    |FINSI;
                    aLinea = "";
                    aLineaAnt = "";
                    aLineaAnt2 = "";
                    nNumLin = 0;
                    |PARA; |SI; |HACIENDO;
                         |XLEE nHandle, 250, aLinea;
                         nNumLin = nNumLin + 1;
                         |SI FSalida < 0;
                              ||Se ha terminado de leer el fichero de datos
                              |HAZ ObtenPor;
                              #dsactual#4 = "Porcentaje : " + aPtge + " %";
                              |PINTA #dsactual#4;
                              |PTEC 802;
                              |ENCAMPO #1#dsactual;
                              |SAL;
                         |FINSI;
                         aLineaAnt2 = aLineaAnt;
                         aLineaAnt = aLinea;
                    |SIGUE;
                    |XCIERRA nHandle;
                    |SI nSwSal = 1;
                         |SAL;
                    |FINSI;
                    |SI nNumLin = nNumLinAnt;
                         nContTime = nContTime + 1;
                    |SINO;
                         nContTime = 0;
                    |FINSI;
                    |SI nContTime = 60;      ||60 segundos de timeout
                         |SI enSwSilencio = 1;
                              aTxtLog = "Error: TimeOut en la descarga del paquete " + aPaq;
                              |HAZ MeteLog;
                         |SINO;
                              aMensaje = "0000Error: TimeOut en la descarga del paquete " + aPaq;
                              |MENAV aMensaje;
                         |FINSI;
                         |SAL;
                    |FINSI;
                    |MENSA "2327Procesando .......";
                    nNumLinAnt = nNumLin;
                    |SLEEP 1;
               |SIGUE;
          |SINO;
               |SI nSwNotFound = 1;
                    |SI enSwSilencio = 1;
                         aTxtLog = "Fichero <" + aPaq + "> no encontrado en el servidor.";
                         |HAZ MeteLog;
                    |SINO;
                         aMensaje = "0000Fichero <" + aPaq + "> no encontrado en el servidor.";
                         |MENAV aMensaje;
                    |FINSI;
               |SINO;
                    |SI enSwSilencio = 1;
                         aTxtLog = "Imposible establecer la conexion. Revise la conexion a internet.";
                         |QUE_SISTEMA aSistema;
                         |SI aSistema ! "ESDOS";
                              aTxtLog = aTxtLog + " Y los permisos de las shells dsw*.";
                         |FINSI;
                         |HAZ MeteLog;
                    |SINO;
                         aMensaje = "0000Imposible establecer la conexion.";
                         |MENAV aMensaje;
                    |FINSI;
               |FINSI;
          |FINSI;
     |SINO;
          ||ARA
          ||TODO CCC
          ||Usaremos el dsftp

          |DBASS aBase;
          |QBF aBase;
          aBase = aBase + "agitool/";
          aPathPlugins = aBase + "plugins/";

          aReturnDos = &13 + &10;       ||Return en maquina Local. Siempre Windows

          aAlfa      = "C:\DOCUMENTOS";                     |RMKDIR aAlfa;
          aAlfa      = "C:\DOCUMENTOS\AGITOOL";             |RMKDIR aAlfa;
          aDirFtp    = aAlfa;
          aDirRecoge = aAlfa + "\";

          aFichero = "dsftp.exe";
          aOrigen = aPathPlugins + aFichero;
          aDestino = aDirRecoge + aFichero;

          aContiene1 = aOrigen;
          |ESPECIFICA "MD5", aContiene;
          aDocServidor = FSalida;  |QBF aDocServidor;

          aContiene1 = aDestino;
          |ESPECIFICA "REMOTOMD5", aContiene;
          aDocRemoto = FSalida;  |QBF aDocRemoto;

          |SI aDocRemoto ! aDocServidor;  |O aDocRemoto = "";
               aIPRemoto = "";
               |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

               |SI aIPRemoto ! "";
                    |ENVIA_FICHERO aOrigen, aDestino;
               |SINO;
                    |COPIA_FICHERO aOrigen, aDestino;
               |FINSI;
          |FINSI;

          aAbre =  aDirRecoge + "dsftp.bat";
          |XABRE "CWB", aAbre, nHandle;
          |SI FSalida ] 0;
               aAlfa = "@ECHO OFF"                                          + aReturnDos;  |XGRABA nHandle, aAlfa;
               aAlfa = ":: crear un archivo temporal llamado script.ftp"    + aReturnDos;  |XGRABA nHandle, aAlfa;
               aAlfa = ":: el signo > y >> es para canalizar el texto."     + aReturnDos;  |XGRABA nHandle, aAlfa;
               aAlfa = ">"  + aDirRecoge + "script.ftp ECHO agi" + aReturnDos;  |XGRABA nHandle, aAlfa;
               aAlfa = ">>" + aDirRecoge + "script.ftp ECHO %1" + aReturnDos;  |XGRABA nHandle, aAlfa;
               aAlfa = ">>" + aDirRecoge + "script.ftp ECHO lcd " + aTmp + aReturnDos;  |XGRABA nHandle, aAlfa;
               aAlfa = ">>" + aDirRecoge + "script.ftp ECHO bin"            + aReturnDos;  |XGRABA nHandle, aAlfa;
               aAlfa = ">>" + aDirRecoge + "script.ftp ECHO cd public_html/clientes/diagram/aplica" + aReturnDos;  |XGRABA nHandle, aAlfa;
               aAlfa = ">>" + aDirRecoge + "script.ftp ECHO get " + aPar1 + aReturnDos;  |XGRABA nHandle, aAlfa;
               aAlfa = ">>" + aDirRecoge + "script.ftp ECHO bye"            + aReturnDos;  |XGRABA nHandle, aAlfa;
               aAlfa = ":: Usamos el archivo recien creado:"                 + aReturnDos;  |XGRABA nHandle, aAlfa;
               aAlfa = aDirRecoge + "dsftp.exe " + aDirRecoge + "script.ftp ftp.diagram.es" + aReturnDos; |XGRABA nHandle, aAlfa;
               aAlfa = ":: sobreescribimos el fichero temporal y lo borramos" + aReturnDos;  |XGRABA nHandle, aAlfa;
               aAlfa = "TYPE NUL > " + aDirRecoge + "script.ftp"           + aReturnDos;  |XGRABA nHandle, aAlfa;
               aAlfa = "DEL " + aDirRecoge + "script.ftp"                  + aReturnDos;  |XGRABA nHandle, aAlfa;
               aAlfa = "GOTO End"                                          + aReturnDos;  |XGRABA nHandle, aAlfa;
               aAlfa = ":Ayuda"                                            + aReturnDos;  |XGRABA nHandle, aAlfa;
               aAlfa = "ECHO Uso: %0 password"                             + aReturnDos;  |XGRABA nHandle, aAlfa;
               aAlfa = ":End"                                              + aReturnDos;  |XGRABA nHandle, aAlfa;

          |SINO;
               |SI enSwSilencio = 0;
                    aMensaje = "0000Problemas al crear dsftp.bat para obtener los datos del servidor";
                    |MENAV aMensaje;
               |SINO;
                    aTxtLog = "Problemas al crear dsftp.bat para obtener los datos del servidor";
                    |HAZ MeteLog;
               |FINSI;
               |XCIERRA nHandle;
               |ACABA;
          |FINSI;

          |XCIERRA nHandle;

          #dsactual#3 = " - - - Descarga en curso - - - ";
          |PINTA #dsactual#3;
          |PTEC 802;
          |ENCAMPO #1#dsactual;

          aAlfa = "cmd " + &34 + "/c " + aDirRecoge + "dsftp.bat mantenerlimpio2009" + &34;
          |RSYSTEM aAlfa;

          |SLEEP 1;
          aAbre = aDirRecoge + "dsftp.bat";
          |RFBORRA aAbre;

          aAbre = aDirRecoge + "DsFtp.log";
          |RFBORRA aAbre;

          aAbre = aTmp + "/" + aPar1;
          |XABRE "CE", aAbre, nHandle;
          |SI FSalida ] 0;
               nSwOk = 1;
          |SINO;
               nSwOk = 0;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO ObtenPor;
     ||Obtengo el porcentage de aLinea i si no hay lo obtengo de aLineaAnt
     aDatos = aLinea;
     |QBF aLinea;
     aCaracter = aDatos % -101;
     |SI aCaracter = "]";
          aSigno = aDatos % -201;
          |SI aSigno = "%";
               aPtge = aDatos % -303;
          |SINO;
               |HAZ MiraSiFinal;
          |FINSI;
     |SINO;
          aDatos = aLineaAnt;
          |QBF aLinea;
          aCaracter = aDatos % -101;
          |SI aCaracter = "]";
               aSigno = aDatos % -201;
               |SI aSigno = "%";
                    aPtge = aDatos % -303;
               |SINO;
                    |HAZ MiraSiFinal;
               |FINSI;
          |SINO;
               aDatos = aLineaAnt2;
               |QBF aLinea;
               aCaracter = aDatos % -101;
               |SI aCaracter = "]";
                    aSigno = aDatos % -201;
                    |SI aSigno = "%";
                         aPtge = aDatos % -303;
                    |SINO;
                         |HAZ MiraSiFinal;
                    |FINSI;
               |SINO;
                    |HAZ MiraSiFinal;
               |FINSI;
          |FINSI;
     |FINSI;
     |HAZ PintaPtge;
|FPRC;

|PROCESO MiraSiFinal;
     |PARA nBucle = 1; |SI nBucle < 80; |HACIENDO nBucle = nBucle +1;
          aBucle = nBucle;
          aStr = aBucle + "05";
          aSaved = aLineaAnt2 % aStr;
          |SI aSaved = "saved";
               nSwOk = 1;
               aPtge = 100;
               |HAZ PintaPtge;
               #dsactual#5 = "Descarga completada correctamente";

               |SI enSwSilencio = 1;
                    aTxtLog = "Descarga completada correctamente";
                    |HAZ MeteLog;
               |FINSI;

               |PINTA #dsactual#5;
               |PTEC 802;
               |ENCAMPO #1#dsactual;
               nSwSal = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO PintaPtge;
     |QBF aPtge;
     |SI aPtge ! "";
          nPorcien = aPtge;
          nPorcien = nPorcien * 0.6;
          aBarra = "Þ" * nPorcien;
          aBarra = aBarra + ( " " * 60);
          aBarra = aBarra % 160;
          |ATRI I;
          |PINTA 1907, aBarra;
          |ATRI 0;
     |FINSI;
|FPRC;

|PROCESO Actualiza;
     |HAZ VerNueva;      ||Version nueva a intalar
     |HAZ VerActual;     ||Version actual
     |HAZ Pregunta;      ||Consultamos si realizamos la instalacion
     |HAZ BorraBasura;   ||Borra los ficheros utilizados
|FPRC;

|PROCESO VerActual;     ||Version actual
     |DBASS aPath;
     |QBF aPath;
     aPath = aPath + aNomDir + "/";
     aMenu = aPath + aPar2 + ".men";
     nHandle = 1;
     |XABRE "U", aMenu, nHandle;

     |XLEE nHandle, 250, aLinea;
     |SI FSalida < 0;
          |SI enSwSilencio = 1;
               |SI aPaq ! "dscopbas.tgz";
                    aTxtLog = ".No he podido localizar el menu <" + aMenu + "> ";
                    |HAZ MeteLog;
               |FINSI;
          |SINO;
               aMensaje = "0000.No he podido localizar el menu <" + aMenu + "> ";
               |MENAV aMensaje;
          |FINSI;

          nSwNuevoPaq = 1;
     |SINO;
          nSwNuevoPaq = 0;
          |QBF aLinea;
          aAlfa1 = aLinea % -109;
          |QBF aAlfa1;
          aVerAct = aAlfa1;
     |FINSI;
     |XCIERRA nHandle;
|FPRC;

|PROCESO VerNueva;      ||Version nueva a instalar
     ||TODO CCC
     |HAZ BorraRwi;

     ||Lo envio al servidor, que es donde se actualiza realmente ...

     |SI enSwSilencio = 0;
          aOrigen = aTmp + "/" + aPaq;
          aDestino = aTmpServer + "/" + aPaq;
          |HAZ EnviaFichero;
     |SINO;
          aDestino = aTmpServer + "/" + aPaq;
     |FINSI;

     aDes = aDestino;

     |DESCOMPRIME aDes;
     |SI FSalida = 0;
          aDirectorio = aTmpServer + "/";
          |DETAR aDes, aDirectorio;
          |SI FSalida = 0;
               aMensaje = "0000[" + aPaq + "] instalado correctamente.";
               |HAZ NomMenu;
          |SINO;
               aMensaje = "0000[" + aPaq + "] no instalado (DETAR).";
          |FINSI;
     |SINO;
          aMensaje = "    [" + aPaq + "] no instalado (COMPRIME).";
     |FINSI;
     |SI aPar1 ! "dscopbas.tgz";
          |SI nSwEncontrado = 1;
               aMenu = aTmpServer + "/" + aNomDir + "/" + aPar2 + ".men";
               nHandle = 1;
               |XABRE "U", aMenu, nHandle;
               |XLEE nHandle, 250, aLinea;
               |SI FSalida < 0;
                    |SI enSwSilencio = 1;
                         aTxtLog = "No he podido localizar el menu <" + aMenu + "> ";
                         |HAZ MeteLog;
                    |SINO;
                         aMensaje = "0000No he podido localizar el menu <" + aMenu + "> ";
                         |MENAV aMensaje;
                    |FINSI;

               |SINO;
                    |QBF aLinea;
                    aAlfa1 = aLinea % -109;
                    |QBF aAlfa1;
                    aVerNue = aAlfa1;
               |FINSI;
               |XCIERRA nHandle;
               aAlfa2 = aTmpServer + "/" + aNomDir + "/";     ||Directorio a borrar
               aAlfa1 = aDirBase + "bin/agirm -r " + aAlfa2;
               |SYSTEM aAlfa1;

               aAlfa1 = aFicRwi;
               |FBORRA aAlfa1;
          |FINSI;
     |SINO;
          aBasico = "";
          aMenu = aTmpServer + "/ztgz/runtime.txt";
          nHandle = 1;
          |XABRE "U", aMenu, nHandle;
          |XLEE nHandle, 250, aLinea;
          |SI FSalida < 0;
               aTxtLog = "No he podido localizar el runtime.txt (BASICO)";
               |HAZ MeteLog;
          |SINO;
               |QBF aLinea;
               aBasico = aLinea;
          |FINSI;
          |XCIERRA nHandle;
     |FINSI;
|FPRC;

|PROCESO BorraRwi;
     ||Primero borramos todos los *.rwi del directorio
     aAlfa1 = aTmp + "/*.rwi";
     |_PDIR aAlfa1;
     |PARA; |SI FSalida = 0; |HACIENDO;
          |SI aAlfa1 ! "/";
               |FBORRA aAlfa1;
          |FINSI;
          |_SDIR aAlfa1
     |SIGUE;
|FPRC;

|PROCESO NomMenu;
     nSoloDir = 0;
     aAlfa1 = aTmpServer + "/*.rwi";
     |_PDIR aAlfa1;
     |PARA; |SI FSalida = 0; |HACIENDO;
          |SI nSoloDir = 0;
               aFicRwi = aAlfa1;
               nSoloDir = 1;
          |FINSI;
          |_SDIR aAlfa1;
     |SIGUE;
     |SI nSoloDir = 0;
          |SI enSwSilencio = 1;
               |SI aPaq ! "dscopbas.tgz";
                    aTxtLog = "Error: paquete en mal estado";
                    |HAZ MeteLog;
               |FINSI;
          |SINO;
               aMensaje = "0000Error: paquete en mal estado";
               |MENAV aMensaje;
          |FINSI;
     |SINO;
          nHandle = 1;
          nSwEncontrado = 0;
          |XABRE "U", aFicRwi, nHandle;
          |PARA; |SI; |HACIENDO;
               |XLEE nHandle, 250, aLinea;
               |SI FSalida < 0;
                    |SAL;
               |SINO;
                    |SI nSwEncontrado = 1;
                         |QBF aLinea;
                         aNomDir = aLinea - "/*";
                         |SAL;
                    |FINSI;
                    aSubCadena = aLinea % 107;
                    |SI aSubCadena = "[Fijos]";
                         nSwEncontrado = 1;
                    |FINSI;
               |FINSI;
          |SIGUE;
          |XCIERRA nHandle;
     |FINSI;
|FPRC;

|PROCESO Pregunta;      ||Consultamos si realizamos la instalacion
     |SI aVerNue < aVerAct;
          |SI enSwSilencio = 0;
               aMensaje = "0000Version Actual: " + aVerAct + &10;
               aMensaje = aMensaje + "Version Nueva: " + aVerNue + &10;
               aMensaje = aMensaje + "Error: La nueva version es inferior.";
               |MENAV aMensaje;
          |SINO;
               aTxtLog = "Version Actual: " + aVerAct;
               |HAZ MeteLog;
               aTxtLog = "Version Nueva: " + aVerNue;
               |HAZ MeteLog;
               aTxtLog = "Error: La nueva version es inferior.";
               |HAZ MeteLog;
          |FINSI;
     |SINO;
          |SI enSwSilencio = 0;
               aMensaje = "0000SVersion Actual: " + aVerAct + &10;
               aMensaje = aMensaje + "Version Nueva: " + aVerNue + &10;
               aMensaje = aMensaje + "Desea actualizar?";
               |CONFI aMensaje;
          |SINO;
               |SI aPaq ! "dscopbas.tgz";
                    aTxtLog = "Version Actual: " + aVerAct;
                    |HAZ MeteLog;
                    aTxtLog = "Version Nueva: " + aVerNue;
                    |HAZ MeteLog;
               |FINSI;
               FSalida = 0;
          |FINSI;
          |SI FSalida = 0;
               |SI enSwSilencio = 0;
                    |HAZ WebLog;
               |SINO;
                    |HAZ Logwget;
               |FINSI;

               ||genero el fichero que se llama volume.ds
               ||y que contiene el nombre del paquete a instalar.
               nSwCopyIns = 0;
               nHandle = 1;
               aAlfa1 = aTmpServer + "/volume.ds";
               |XABRE "W", aAlfa1, nHandle;
               |SI FSalida ] 0;
                    aLinea = aPaq;
                    |XGRABA nHandle, aLinea;
                    |SI FSalida ] 0;
                         nSwCopyIns = 1;
                    |FINSI;
               |FINSI;
               |XCIERRA nHandle;
               |SI nSwCopyIns = 1;
                    aAlfa1 = aTmpServer;

                    |SI enSwSilencio = 1;
||                         nPtec  = &"S";
||                         |PTEC nPtec;
                         aTxtLog = "Copia Instalacion " + aAlfa1;
                         |HAZ MeteLog;

                         aOrigen = aTmpServer + "/" + aPaq;
                         aDestino = aBaseServer + aPaq;
                         |COPIA_FICHERO aOrigen, aDestino;

                         ||Descomprimo y detaro el paquete.
                         |DESCOMPRIME aDestino;
                         |SI FSalida = 0;
                              aDirectorio = aBaseServer;
                              |DETAR aDestino, aDirectorio;
                              |SI FSalida = 0;
                                   aTxtLog = "Paquete Instalado correctamente. (" + aPaq + ")";
                                   |HAZ MeteLog;

                                   aVersion = aVerNue;
                                   |QBF aVersion;

                                   ||SI es UNIX chmod -R 777 de tot
                                   |QUE_SISTEMA aSistema;
                                   |SI aSistema ! "ESDOS";
                                        aEjecuta = "chmod -R 777 " + aBaseServer + aPar2 + "/";
                                        |SYSTEM aEjecuta;
                                        aTxtLog = aSistema + " " + aEjecuta;
                                        |HAZ MeteLog;
                                   |FINSI;

                                   aBorra = aBaseServer + aPaq;
                                   |FBORRA aBorra;

                                   aAux = aPaq - ".tgz";
                                   aBorra = aBaseServer + aAux + ".tar";
                                   |FBORRA aBorra;

                                   aAux = aPaq - ".tgz";
                                   aBorra = aBaseServer + aAux + ".rwi";
                                   |FBORRA aBorra;
                              |SINO;
                                   aTxtLog = "Error al hacer el DETAR " + aDestino;
                                   |HAZ MeteLog;
                              |FINSI;
                         |SINO;
                              aTxtLog = "Error al descomprimir " + aDestino;
                              |HAZ MeteLog;
                         |FINSI;
                    |SINO;
                         |COPIAINSTALACION aAlfa1;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO BorraBasura;
     aAlfa1 = aTmpServer + "/" + aPaq;
     |FBORRA aAlfa1;
     aPaq = aPaq - ".tgz";
     aPaq = aPaq + ".tar";
     aAlfa1 = aTmpServer + "/" + aPaq;
     |FBORRA aAlfa1;
     aAlfa1 = aTmpServer + "/volume.ds";
     |FBORRA aAlfa1;
     aAlfa1 = aLogWget;
     |SI enSwSilencio = 0;
          |RFBORRA aAlfa1;
     |SINO;
          |FBORRA aAlfa1;
     |FINSI;
|FPRC;

|PROCESO EnviaFichero;
     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |SI aIPRemoto ! "";
         |RECIBE_FICHERO aOrigen, aDestino;
     |SINO;
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;
|FPRC;

|PROCESO MeteLog;
     |XABRE "A", aLogs, nHandleLog;
     |SI FSalida ] 0;
          |XGRABA nHandleLog, aTxtLog;
          |XCIERRA nHandleLog;
     |FINSI;
|FPRC;
