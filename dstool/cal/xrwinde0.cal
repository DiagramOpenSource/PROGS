|FICHEROS;
     :/agitool/def/xrwindex #0;
     :/agitool/def/xrwintmp #1;
|FIN;

|VARIABLES
  nConta = 0;
  aHora = "";
  aTecla = "";
  aFichero = "";
  aplicacion = "";
  dirbase = "";
  dirempresa = "";
  dirfichero = "";
  empresa = "";
  total = 0;
  ri = 0;
  temporal1 = "";
  temporal2 = "";
  temporal3 = "";
  temporal4 = "";
  temporal5 = "";
  handle = "";
  i = 0;
  total_lineas = 0;
  total_campos = 0;
  linea = 0;
  ant_linea = 0;
  posicion = 0;
  pintar = 0;
  {4500}fichero = "";

  dolar1 = "";
  dolar2 = "";
  alfa1 = "";
  para1 = 0;
  elsw1 = 0;
  topep = 0;
  punto_coma = "";
  primero = "";
  sw_paso = 0;
  traba = "";
  bytes = 0;
  quebusco = "";
  hebuscado = "";
  desbus = 0;
  estoesloquehay = "";
  encontrado = 0;
  esfichero = 0;
  unavez = 0;
  rafa1 = "";
  rafa2 = "";
  nSwOk = 0;
  aDef = "";
  aAplica = "";
  aMensaje = "";
  aDirApli = "";
  aPathDef = "";
  aPathDat = "";
  aAlfa = "";
  aEmpresa = "";
  aNomEmp = "";
  nSwCargado = 0;
  aPeriodo = "";
  aExtension = "";
  {3}reparar = "";
  aAlfa1 = "";
  aPass = "";
|FIN;

|PROCESO quitabarra;
  rafa1 = temporal1 % -108;
|ET arriba;
  rafa2 = rafa1 - "/";
  |SI rafa1 = rafa2;  |ACABA;  |FINSI;
  rafa1 = rafa1 % 207;
  |VETE arriba;
|FPRC;

|RUTINA SP;
|VARIABLES;
   pos_fichero = 0;
|FIN;
   pos_fichero = Pos;
   Pos = -1;
|FRUT;

|RUTINA RP;
   Pos = pos_fichero;
|FRUT;

|RUTINA parametros;
    alfa1 = PARAMETRO % 0;    topep = alfa1;
    dolar1 = "";
    dolar2 = "";
    elsw1 = 0;
    punto_coma = &59;
|PARA para1 = 1; |SI para1 [ topep; |HACIENDO para1 = para1 + 1;
        alfa1 = PARAMETRO % (N\para1 * 100 + 1);
    |SI alfa1 = punto_coma;
        |SI elsw1 = 0;
                elsw1 = 1;
        |SINO;
                elsw1 = 99;
            |MENAV "    Exceso de Parametros (maximo 2) ...";
        |FINSI;
    |SINO;
        |SI elsw1 = 0;   dolar1 = dolar1 + alfa1; |FINSI;
        |SI elsw1 = 1;   dolar2 = dolar2 + alfa1; |FINSI;
    |FINSI;
|SIGUE;
|FRUT;

|PROCESO CargaFic;
   |DBASS dirbase;
   |DBASE aDirApli;
   |QBF aDirApli;
   |NOMAP aplicacion;
   |DFICB dirempresa;
   |DFICE dirfichero;
   empresa = dirfichero - dirempresa;
   |HAZ parametros;

|| ********** Carga del .dir **************************
   total = 0;
   |SI dolar2 = "";
       temporal1 = "rt  "+dirempresa+aplicacion+".dir";
   |SINO;
       temporal1 = "rt  "+dirempresa+dolar2+".dir";
       |SI dolar1 = dolar2;  dolar1 = "";  dolar2 = "";  |FINSI;
       |SI dolar2 = "aginom";  dolar2 = "";  |FINSI;
   |FINSI;
   |FABRE temporal1;
   handle = FSalida;
   |SI handle ] 0;
      |HAZ SP;
      Ifichero = fichero1<-;
      |PARA i = 0;|SI total < 4500;|HACIENDO i = i + 1;
         temporal1 = handle+" 200";
         |HAZ RP;
         |FLEE temporal1;
         rafa1 = temporal1 % 101;
         |SI rafa1 = ":";
             |HAZ quitabarra;
             temporal1 = rafa1;
         |FINSI;
         |HAZ SP;
         |SI FSalida < 0;
            |SAL;
         |FINSI;
         ri = i $ 5;
         |SI ri = 0;  || 1
            fichero = temporal1;
         |SINO;
         |SI ri = 1;  || 2
            || ojo con las variables "apuntador"
            temporal2 =  fichero + "        ";
            temporal2 = temporal2 % 108;
            temporal2 = (temporal2+temporal1+"                             ") % 133;
            fichero = temporal2;
         |SINO;
         |SI ri = 3;  || 3
            || ***** Solo cuenta si tiene marca de indexado
            temporal1 = temporal1 % 101;
            |SI temporal1 = 1;|O temporal1 = 2; || ficheros con indice
                |SI total = 0;  |Y dolar2 ! "";  |Y sw_paso = 0;
                    sw_paso =  1;
                    primero = fichero % 108;
                    |QBF primero;
                |SINO;
|| Los ficheros que comienzan por "SQ" no se tratan
|| estos son los generados por la sentencia |SQL en la dscomer9
                    aAlfa = fichero % 102; aAlfa = aAlfa > "";
                    |SI aAlfa = "SQ";
                         fichero = "";
                    |SINO;
                         Ifichero = Ifichero + 1;
                         total    = total + 1;
                    |FINSI;
                |FINSI;
            |FINSI;
         |FINSI;      || 3
         |FINSI;      || 2
         |FINSI;      || 1
      |SIGUE;
      |FCIERRA handle;
   |FINSI;
   |SI dolar2 ! "";
       total = total - 1;
       Ifichero = Ifichero + 1;
   |FINSI;
   |SI total [ 0;
      |SI dolar2 = "";
          temporal1 = "0000No hay ficheros en "+dirempresa + aplicacion + ".dir";
      |SINO;
          temporal1 = "0000No hay ficheros en "+dirempresa + dolar2 + ".dir";
      |FINSI;
      |MENAV temporal1;
|| ************ abandona: nada que hacer ***********
      |ACABA;
|| *************************************************
   |FINSI;


||***************** Carga de los ficheros ********************
   #0#tficheros = total;
   #0#ttamano = 0;

   |ATRI R;
   |MENSA "0401Cargando informacion sobre los ficheros ...";
   |ATRI 0;
   Ifichero = fichero1<-;
   |PARA i = 0;|SI i < total;|HACIENDO i = i + 1;
      temporal1 = fichero % 108;
      |PINTA 0465,temporal1;
      |QBF temporal1;
      |SI dolar1 = "INDEXACION";
         |GRABAENDEF temporal1,"";
      |SINO;
         |SI dolar2 = "";
             |LEEDEDEF temporal1,"";
         |SINO;
             |LEEDEDEF primero,temporal1;
         |FINSI;
      |FINSI;
      |SI FSalida ] 0;
          temporal1 = FSalida % 375;
          #0#ttamano = #0#ttamano + (A\temporal1 % 115);
      |SINO;
          temporal1 = " " * 80;
          temporal1 = temporal1 % 175;
      |FINSI;
      fichero = fichero + temporal1;
      Ifichero = Ifichero + 1;
   |SIGUE;

   Ifichero = fichero1<-;
   |PARA i = 1;|SI i < total;|HACIENDO i = i + 1;
         temporal2 = fichero;
         |HAZ carga_campos;
         Ifichero = Ifichero + 1;
         |GRABA 020#1n;
   |SIGUE;
   |MENSA "0000...";
|FPRC;

|RUTINA carga_campos;
   ||****** en temporal2 vienen los datos
   || formato: 8 fichero 25 descripcion 8 ocupacion 8 registros 5 lenreg
   ||          2 n.claves 5 bloque 8 n.bloques
   |DDEFECTO #1;
   #1#fichero     = (A\temporal2 % 108);
   #1#descripcion = (A\temporal2 % 925);
   #1#tama¤o      = (A\temporal2 % 3415);
   #1#registros   = (A\temporal2 % 4915);
   #1#tamreg      = (A\temporal2 % 6405);
   #1#claves      = (A\temporal2 % 6903);
   #1#bloque      = (A\temporal2 % 7205);
   #1#nbloques    = (A\temporal2 % 7715);
   #1#segmentos   = (A\temporal2 % 9203);
|FRUT;
