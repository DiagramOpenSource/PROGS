|FICHEROS;
     dschin02 #0;
     dschintm #100,MANTE;

     defchino@ #998;
     empresas@ #999;
|FIN;

|VARIABLES;
     aBase       = "";
     aPathAplica = "";
     aPathDef    = "";
     aPathFich   = "";
     aPathChinos = "";
     aMasEmpresa = "";
     aQueQuiero  = "";
     aLong       = "";
     aDir        = "";
     aMas        = "";
     aAlfa       = "";

     {3}aBuscador = "";

     nHandle     = 0;
     nDEmpresa   = 0;
     nHEmpresa   = 0;
     nLong       = 0;
     nPosi       = 0;

     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     aValor = "";
     nNroCampos = 0;
     nCampo = 0;
     nCampo2 = 0;
     &aDato = "";
     FEstado = 0;
     aLon = "";
     nLon = 0;
     swChino = 0;
     nPos = 0;
     nCar = 0;
     aCar = "";
     nAscii = 0;
     aNroCamPri = "";
     nNroCamPri = 0;
     nNume = 0;
     aCamCla = "";
     nCamCla = 0;
     aFicheros = "";
     nFicheros = 0;
     aFich = "";
     nFich = 0;
     aMensa = "";
     nRegis = 0;
     aRegis = "";
     nLong1 = 0;
     swRegCorr = 0;

     &aValClave = "";
     &nRegistro = 0;
     &aFichero = "";
     &aNroCampo = "";
     &aCampoChino = "";
     &aBorra = "";
     &aAplica = "";
     &aEmpresa = "";

     fFecha = @;
     aDirDes = "";
     aFecha = "";
     aFecha2 = "";
     aHora = "";
     aHora2 = "";

     aIPServ = "";
     aServCorreo = "";
     aFrom = "";
     aAsunto = "";
     aMensaje = "";
     aTo = "";

     nCanal = 0;
     nPaso = 0;
     aDirLog = "";
     aLinea = "";

     aParam = "";
|FIN;

|PROCESO Log;
     |SI nPaso = 1;
          |XABRE "WB", aDirLog, nCanal;
          |HORA aHora;
          |QBF aHora;
          aLinea = "Inicio del proceso - " + aHora + &13 + &10;
          |XGRABA nCanal, aLinea;
     |FINSI;
     |SI nPaso = 2;
          |HORA aHora;
          |QBF aHora;
          aLinea = aMas - aPathDef - ".mas" + ".dat - " + aHora;
          aLinea = "Rastreando: " + aPathChinos + aLinea + &13 + &10;
          |XGRABA nCanal, aLinea;
     |FINSI;
     |SI nPaso = 3;
          |HORA aHora;
          |QBF aHora;
          aLinea = "Proceso Concluido, enviando correos - " + aHora + &13 + &10;
          |XGRABA nCanal, aLinea;
     |FINSI;
     |SI nPaso = 4; |O nPaso = 5;
          |HORA aHora;
          |QBF aHora;
          aLinea = aAlfa + " - " + aHora + &13 + &10;
          |XGRABA nCanal, aLinea;
     |FINSI;
     |SI nPaso = 6;
          |HORA aHora;
          |QBF aHora;
          aLinea = "Proceso finalizado con exito - " + aHora + &13 + &10;
          |XGRABA nCanal, aLinea;
          |XCIERRA nCanal;
     |FINSI;
|FPRC;

|PROCESO SiBorra;
     |SI #0#4 = "E";
          |CAMPO_VISUAL #0#5, 1, "N";
          |CAMPO_MODIFICA #0#5, 1, "N";
          |CAMPO_VISUAL #0#8, 1, "N";
          |CAMPO_MODIFICA #0#8, 1, "N";

          |PINTA 2121, " Borrado con confirmacion ............:   ";
          |PINTA 2221, "                                          ";

          |CAMPO_VISUAL #0#7, 1, "S";
          |CAMPO_MODIFICA #0#7, 1, "S";
          |PINTA #0#7;
     |FINSI;

     |SI #0#4 = "I";
          |CAMPO_VISUAL #0#5, 1, "N";
          |CAMPO_MODIFICA #0#5, 1, "N";
          |CAMPO_VISUAL #0#8, 1, "N";
          |CAMPO_MODIFICA #0#8, 1, "N";
          |CAMPO_VISUAL #0#7, 1, "N";
          |CAMPO_MODIFICA #0#7, 1, "N";

          |PINTA 2121, "                                          ";
          |PINTA 2221, "                                          ";
     |FINSI;

     |SI #0#4 = "C";
          |CAMPO_VISUAL #0#7, 1, "N";
          |CAMPO_MODIFICA #0#7, 1, "N";

          |PINTA 2121, " e-mail:                                  ";
          |PINTA 2221, " IP DSCorreo ...........:                 ";

          |CAMPO_VISUAL #0#5, 1, "S";
          |CAMPO_MODIFICA #0#5, 1, "S";
          |CAMPO_VISUAL #0#8, 1, "S";
          |CAMPO_MODIFICA #0#8, 1, "S";
          |PINTA #0#5
          |PINTA #0#8
     |FINSI;
|FPRC;

|PROCESO CorreoAct; |TIPO 7;
     |SI #0#4 = "C";
          |CAMPO_MODIFICA #0#5, 1, "S";
     |SINO;
          |CAMPO_MODIFICA #0#5, 1, "N";
     |FINSI;
|FPRC;

|PROCESO CorreoBla;
     aAlfa = #0#5;
     |QBF aAlfa;
     |SI aAlfa = ""; |Y #0#4 = "C";
          |MENAV "    ATENCION: Es necesario especificar la direccion de correo";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO SiCero;
     |SI nRegistro = 0;
          aFichero = "No se han encontrado registros corruptos";
          |PRINT;
     |FINSI;
|FPRC;

|PROCESO ImprimeChino;
     nRegistro = nRegistro + 1;
     aFichero = aDir;
     aValClave = aValClave;
     aNroCampo = nCampo;
     aNroCampo = ("000" + aNroCampo) % -103;
     aAlfa1 = "|C" + aNroCampo + "NOMBRE";
     aCampoChino = #998aAlfa1;
     aDato = aDato;
     |SI #0#4 = "E";
          aBorra = "ELIMINADO";
     |SINO;
          aBorra = "ACTIVO";
     |FINSI;
     |PRINT;
|FPRC;

|PROCESO TrataChino;
     aAlfa1 = "|K000";
     aAlfa2 = #998aAlfa1;
     aValClave = "";
     aNroCamPri = aAlfa2 % 103;
     nNroCamPri = aNroCamPri;      || nro campos clave primaria
     |PARA nCamCla = 1; |SI nCamCla [ nNroCamPri; |HACIENDO nCamCla = nCamCla + 1;
          nNume = (nCamCla * 3) + 1;       || nCamCla: campo clave primaria
          nNume = (nNume * 100) + 3;
          aCamCla = aAlfa2 % nNume;
          nCampo2 = aCamCla;       || aCamCla: campo clave primaria
          aValor = #998nCampo2;
          aValClave = aValClave + aValor + " # ";
     |SIGUE;
     |HAZ ImprimeChino;
|FPRC;

|PROCESO Comprueba;
     aLon = aDato % 0;
     nLon = aLon;
     swChino = 0;        || un voto de confianza
     |PARA nCar = 1; |SI nCar [ nLon; |HACIENDO nCar = nCar + 1;
          nPos = (nCar * 100) + 1;
          aCar = aDato % nPos;
          nAscii = &aCar;
          |SI nAscii < 32; |O nAscii > 126;
               |SI nAscii < 160;
               |Y nAscii ! 128; |Y nAscii ! 129; |Y nAscii ! 130;
               |Y nAscii ! 133; |Y nAscii ! 135; |Y nAscii ! 138;
               |Y nAscii ! 149; |Y nAscii ! 137; |Y nAscii ! 154;
                    swChino = 1;
               |FINSI;
               |SI nAscii > 175;
               |Y nAscii ! 224; |Y nAscii ! 225; |Y nAscii ! 231;
               |Y nAscii ! 232; |Y nAscii ! 233; |Y nAscii ! 236;
               |Y nAscii ! 240; |Y nAscii ! 241; |Y nAscii ! 242;
               |Y nAscii ! 243; |Y nAscii ! 249; |Y nAscii ! 250;
               |Y nAscii ! 252; |Y nAscii ! 178; |Y nAscii ! 218;
               |Y nAscii ! 183; |Y nAscii ! 207; |Y nAscii ! 211;
               |Y nAscii ! 209; |Y nAscii ! 180; |Y nAscii ! 193;
               |Y nAscii ! 199; |Y nAscii ! 201; |Y nAscii ! 205;
               |Y nAscii ! 220; |Y nAscii ! 237; |Y nAscii ! 200;
                    swChino = 1;
               |FINSI;
          |FINSI;

          |SI swChino = 1;
               |HAZ TrataChino;
               swRegCorr = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO CuentaFicheros;
     nFicheros = 0;
     aDir = aPathChinos + "*.dat";
     |_PDIR aDir;
     |PARA;  |SI;  |HACIENDO;
          nFicheros = nFicheros + 1;
          |SI FSalida ! 0;  |SAL;  |FINSI;
          |_SDIR aDir;
     |SIGUE;
     aFicheros = nFicheros;
|FPRC;


|PROCESO ElDir;
     |HAZ CuentaFicheros;

     aDir = aPathChinos + "*.dat";

     |_PDIR aDir;
     nFich = 0;
     |PARA;  |SI;  |HACIENDO;
          |SI FSalida ! 0;  |SAL;  |FINSI;
          nFich = nFich + 1;
          aFich = nFich;

          aAlfa = aDir - ".dat" + ".chn";  |FBORRA aAlfa;
          aAlfa = aDir - ".dat" + ".log";  |FBORRA aAlfa;

          aMas = aDir - aPathChinos - ".dat" + ".mas";

          nRegis = 0;
          aMensa = "Rastreando fichero " + aFich + " de " + aFicheros + ":" + aMas;
          |SI aParam ! "AUTO";
                         ||  2       3         4         5         6
                         ||  23456789012345678901234567890123456789012
               |PINTA 2122, "                                         ";
               |PINTA 2222, "                                         ";

               |PINTA 2122, aMensa;
          |FINSI;
          aMas = aPathDef + aMas;
          |CARGA_DEF aMas, defchino@;
          |SI FSalida = 0;
               nPaso = 2;
               |HAZ Log;
               |LEEDEDEF aMas, aPathChinos;
               aAlfa3 = FSalida % 1815; nLong1 = aAlfa3; aAlfa3 = nLong1;

               |PATH_DAT #998 aPathChinos;
               |ABRE #998;

               || migue, aqui empiezo yo a meter mano
               || primero busco numero de campos;

               aAlfa1 = "|NC";
               aAlfa2 = #998aAlfa1;
               nNroCampos = aAlfa2;

               |LEE 101#998p;
               |SI FSalida = 0;
                    |PARA nCampo = 0; |SI nCampo [ nNroCampos; |HACIENDO nCampo = nCampo + 1;
                         aAlfa1 = nCampo;
                         aAlfa1 = ("000" + nCampo) % -103;
                         aAlfa1 = "|C" + aAlfa1 + "TIPO";
                         aAlfa2 = #998aAlfa1;
                         |SI aAlfa2 = "A";
                              aDato = #998nCampo;
                              |HAZ Comprueba;
                         |FINSI;
                    |SIGUE;
                    |LIBERA #998;
                    FEstado = 0;
                    |PARA; |SI FEstado = 0; |HACIENDO;
                         |LEE 101#998s;
                         |QBF aMas;
                         FEstado = FSalida;
                         |SI FEstado = 0;
                              swRegCorr = 0;
                              nRegis = nRegis + 1;
                              aRegis = nRegis;
                              aMensa = "       Registro: " + aRegis + " de " + aAlfa3;
                              |SI aParam ! "AUTO";
                                   |PINTA 2222, aMensa;
                              |FINSI;
                              |PARA nCampo = 0; |SI nCampo [ nNroCampos; |HACIENDO nCampo = nCampo + 1;
                                   aAlfa1 = nCampo;
                                   aAlfa1 = ("000" + nCampo) % -103;
                                   aAlfa1 = "|C" + aAlfa1 + "TIPO";
                                   aAlfa2 = #998aAlfa1;
                                   |SI aAlfa2 = "A";
                                        aDato = #998nCampo;
                                        |HAZ Comprueba;
                                   |FINSI;
                              |SIGUE;
                              |SI swRegCorr = 1; |Y #0#4 = "E";
                                   |SI #0#7 = "N";
                                        |BORRA 020#998a;
                                   |SINO;
                                        aAlfa1 = aMas - aPathDef - ".mas" + ".dat";
                                        aAlfa1 = "    NEncontrado registro corrupto en " + aAlfa1;
                                        aAlfa1 = aAlfa1 + ". Desea eliminarlo?";
                                        |CONFI aAlfa1;
                                        |SI FSalida = 0;
                                             |BORRA 020#998a;
                                        |FINSI;
                                   |FINSI;
                              |FINSI;
                              |LIBERA #998;
                         |FINSI;
                    |SIGUE;
               |FINSI;

               |CIERRA #998;
               |DESCARGA_DEF #defchino@;
          |FINSI;

          |_SDIR aDir;
     |SIGUE;

     |PULSATECLA;
|FPRC;

|PROCESO TrataEmpresa1;
     aQueQuiero = "|C000LONGITUD";
     aLong      = #999#aQueQuiero#;
     nLong      = aLong;
     nPosi      = -(100 + nLong);

     aPathChinos = aPathFich + ((("0" * nLong) + #999#0) % nPosi) + "/";
     aEmpresa = ((("0" * nLong) + #999#0) % nPosi);

     |HAZ ElDir;

     |PIEINF;
|FPRC;

|DEFBUCLE empresas1;
     #999#1001;
     ;
     nDEmpresa;
     nHEmpresa;
     ;
     NULL, TrataEmpresa1;
|FIN;

|PROCESO TrataEmpresa2;
     aPathChinos = aPathFich + (("00000" + #999#2) % -105) + #999#3 + "/";
     aEmpresa = (("00000" + #999#2) % -105) + " - " + #999#3;

     |HAZ ElDir;

     |PIEINF;
|FPRC;

|DEFBUCLE empresas2;
     #999#1002;
     ;
     nDEmpresa, 0;
     nHEmpresa, 9;
     ;
     NULL, TrataEmpresa2;
|FIN;

|PROCESO ABuscar;
     nPaso = 1;
     |HAZ Log;

     |DBASS aBase;  |QBF aBase;
     || aBase = "/u/dsempres/";

     aAplica = #100#1;

     aPathAplica = aBase + #100#1;            |QBF aPathAplica;
     aPathDef    = aPathAplica + "/def/";
     aPathFich   = aPathAplica + "/fich/";

     |SI #100#2 ! "        ";
          aMasEmpresa = aPathDef + #100#2;         |QBF aMasEmpresa;
          aMasEmpresa = aMasEmpresa + ".mas";
          |XABRE "E", aMasEmpresa, nHandle;
          |SI FSalida < 0;
               aAlfa = "    ATENCION: Aplicacion " + #100#1;
               aAlfa = aAlfa + ". No encuentro el Def de Acceso: " + #100#2;
               |MENAV aAlfa;
               |ACABA;
          |FINSI;
     |SINO;
          aMasEmpresa = aPathFich + #100#1;         |QBF aMasEmpresa;
          aMasEmpresa = aMasEmpresa + ".dir";
          |XABRE "E", aMasEmpresa, nHandle;
          |SI FSalida < 0;
               aAlfa = "    ATENCION: No encuentro la Aplicacion: " + #100#1;
               |MENAV aAlfa;
               |ACABA;
          |FINSI;
     |FINSI;

     |SI #100#2 ! "        ";
          |CARGA_DEF aMasEmpresa, empresas@;
          |SI FSalida ! 0;
               |MENAV "0000 No puedo cargar el Def de Empresas";
               |ACABA;
          |FINSI;

          |PATH_DAT #999 aPathFich;

          nDEmpresa = #100#3;
          nHEmpresa = #100#4;

          |SI #100#1 = "agicont ";  |O #100#1 = "contagen";
               |BUCLE empresas2;
          |SINO;
               |BUCLE empresas1;
          |FINSI;

          |DESCARGA_DEF #empresas@;
     |SINO;
          aPathChinos = aPathFich;
          |HAZ ElDir;
     |FINSI;
     |HAZ SiCero;
     |PIEINF;
|FPRC;

|DEFBUCLE ABuscar;
     #100#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, ABuscar;
|FIN;

|PROCESO inferior;
     #100#0 = 01;
|FPRC;

|PROCESO superior;
     #100#0 = 99;
|FPRC;

|PROCESO Correo;
     nPaso = 3;
     |HAZ Log;

     aTo = #0#5;  |QBF aTo;

     aIPServ     = #0#8;
     aServCorreo = "asmtp.diagram.es";
     aAsunto     = "Rastreador de registros corruptos. " + aFecha2 + "_" + aHora2;
     aMensaje    = "Se adjunta el fichero generado por el ";
     aMensaje    = aMensaje + "rastreador de registros corruptos el dia ";
     aMensaje    = aMensaje +  aFecha2 + " a las " + aHora2;
     aFrom       = "soporte@diagram.es";

     |QBT aIPServ;
     |QBF aServCorreo;
     |QBF aFrom;
     |QBF aAsunto;
     |QBF aMensaje;
     |QBF aTo;

     |DSCORREO_ENVIA aIPServ, aServCorreo, aFrom, aTo, aAsunto, aMensaje, aDirDes;
     |SI FSalida ! 0;
          aAlfa = "Error en el envio del correo." + " Error " + FSalida;
     |SINO;
         aAlfa = "Correo enviado con exito: resultado del rastreo";
     |FINSI;

     nPaso = 4;
     |HAZ Log;

     aAsunto     = "LOG del " + aAsunto;
     aMensaje    = aMensaje - "Se adjunta el "
     aMensaje    = "Se adjunta LOG del " + aMensaje;
     |DSCORREO_ENVIA aIPServ, aServCorreo, aFrom, aTo, aAsunto, aMensaje, aDirLog;
     |SI FSalida ! 0;
          aAlfa = "Error en el envio del correo." + " Error " + FSalida;
     |SINO;
          aAlfa = "Correo enviado con exito: LOG";
     |FINSI;

     nPaso = 5;
     |HAZ Log;
|FPRC;

|PROGRAMA;
     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0;
          |DDEFECTO #0;
          |GRABA 020#0b;
     |FINSI;

     aParam = PARAMETRO;
     |QBF aParam;
     |SI aParam = "AUTO";
          FSalida = 0;
     |SINO;
          |CLS;
          |PINPA #0#0;
          |PINDA #0#0;

/*
          aAlfa = Usuario;  |QBT aAlfa;   aAlfa = ("c" + aAlfa) % 108;
          |NOME_DAT #100 aAlfa;
          |DELINDEX #100;
*/

          |ENTLINEAL #1#100, -1, 2, 18, 0, inferior, superior;

          |ENDATOS #1#0;
     |FINSI;
     |SI FSalida = 0;
          |GRABA 020#0a;
          |SI #0#4 = "C";
               |DBASE aDirDes;
               |QBF aDirDes;
               fFecha = ~;
               aFecha = fFecha;
               aFecha2 = aFecha;
               aFecha = aFecha - "." - ".";
               |QBF aFecha;
               |HORA aHora;
               aHora2 = aHora;
               |QBF aHora;
               aHora = aHora - ":" - ":";
               aDirDes = aDirDes + "fich/" + aFecha + "_" + aHora + "_Rastreo.txt"
               aDirLog = aDirDes - ".txt" + ".log";
               Impresora = aDirDes;
               |IMPRE -77;
               aAlfa = "chmod 777 " + Impresora;
               |SYSTEM aAlfa1;
          |SINO;
               |IMPRE 0;
          |FINSI;

          |INFOR "dschin02";

          |SI FSalida ! 0;
               |ACABA;
          |FINSI;

          |BUCLE ABuscar;

          |FININF;
          |FINIMP;

          |SI #0#4 = "C";
               |HAZ Correo;
          |FINSI;

          nPaso = 6;
          |HAZ Log;
     |FINSI;
     |GRABA 020#0a;
     |LIBERA #0;
     |CIERRA #0;
|FPRO;
