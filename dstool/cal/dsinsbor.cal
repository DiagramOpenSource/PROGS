|FICHEROS;
     :/agitool/def/dsinsbor #0;
     :/agitool/def/agiuser1 #1;
     :/agitool/def/dsw00001 #90;
|FIN;

|VARIABLES;
     aDirUsuar     = "";
     aDirUsuarE    = "";
     aDirFichero   = "";
     aBase         = "";
     aAlfa         = "";
     aAlfa1        = "";
     aFichero      = "";

     nContador     = 0;
     nBorra        = 0;
|FIN;

|PROCESO BorraUsuarioEmpresa;
     aAlfa       = #90#1;  |QBT aAlfa;
     aDirUsuarE  = aDirUsuar + aAlfa + "/";
     aDirFichero = aDirUsuarE + "*";  |QBT aDirFichero;

     |BORRA 020#90a;
     |LIBERA #90;

     |_PDIR aDirFichero;
     |PARA;  |SI FSalida ] 0; |HACIENDO;
             |FBORRA aDirFichero;
             |_SDIR aDirFichero;
     |SIGUE;

     |RMDIR aDirUsuarE;
|FPRC;

|DEFBUCLE dsw00001E;
     #90#1;
     ;
     000001;
     999999;
     ;
     NULL, BorraUsuarioEmpresa;
|FIN;

|PROCESO BorraUsuario;
     aAlfa       = #90#1;  |QBT aAlfa;
     aDirUsuar   = aBase + "dev/usr/" + (("000" + #1#0) % -103) + "/" + aAlfa + "/";  |QBT aDirUsuar;
     aDirFichero = aDirUsuar + "*";  |QBT aDirFichero;
     nBorra      = 0;

     |BORRA 020#90a;
     |LIBERA #90;

     |_PDIR aDirFichero;
     |PARA;  |SI FSalida ] 0; |HACIENDO;
             aAlfa  = aDirFichero < " ";
             aAlfa1 = aAlfa - ".pds";
             |SI aAlfa ! aAlfa1;
                 |FBORRA aDirFichero;
             |SINO;
                 nBorra    = 1;
                 nContador = nContador + 1;
                 aAlfa     = aDirFichero - aDirUsuar;
                 #90#0     = nContador;
                 #90#1     = aAlfa;
                 |GRABA 020#90n;
                 |LIBERA #90;
             |FINSI;

             |_SDIR aDirFichero;
     |SIGUE;

     |SI nBorra = 0;
         |RMDIR aDirUsuar;
     |SINO;
         |BUCLE dsw00001E;
         |RMDIR aDirUsuar;
     |FINSI;
|FPRC;

|DEFBUCLE dsw00001;
     #90#1;
     ;
     000001;
     999999;
     ;
     NULL, BorraUsuario;
|FIN;

|PROCESO MiraUsuarios;
     |ABRE #90;  |CIERRA #90;  |DELINDEX #90;

     |DBASS aBase;  |QBF aBase;
     aDirUsuar = aBase + "dev/usr/" + (("000" + #1#0) % -103) + "/";
     nContador = 0;

     |ABRE #90;
     aDirFichero = aDirUsuar + "*.prm";
     |_PDIR aDirFichero;
     |PARA;  |SI FSalida ] 0; |HACIENDO;
             nContador = nContador + 1;
             #90#0     = nContador;
             aAlfa     = aDirFichero - aDirUsuar;
             aAlfa     = aAlfa - ".prm";
             #90#1     = aAlfa;
             |GRABA 020#90n;
             |LIBERA #90;
             |_SDIR aDirFichero;
     |SIGUE;
     |CIERRA #90;

     |SI nContador ! 0;
         |BUCLE dsw00001;
     |FINSI;

     |DELINDEX #90;
|FPRC;

|DEFBUCLE agiuser1;
     #1#1;
     ;
     000;
     999;
     ;
     NULL, MiraUsuarios;
|FIN;

|PROCESO Tipo12;  |TIPO 12;
     |SI #0#0 ! "SI";  |ACABA;  |FINSI;

     aFichero = ("w3" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #90 aFichero;

     |BUCLE agiuser1;
|FPRC;


|PROGRAMA;
     |SI PARAMETRO = "";
         |MANTE #0;
     |SINO;
         #0#0 = "SI";
         |HAZ Tipo12;
     |FINSI;
|FPRO;
