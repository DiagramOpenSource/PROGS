|FICHEROS;
     vtofactu #0;
     vtofatmp #1;
     agifa041@ #41;

     agifa024@ #24;
     agifa084@ #84;
     agifa091@ #91;
     dsm00130@ #130;
     agifa133@ #133;
     agifa134@ #134;

     dscam003@ #30;
     dscam045@ #45;
|FIN;

|VARIABLES;
     nEmpresa = 0;
     nEmpCarte = 0;
     nHayCarte = 0;
     aEmp = "";
     nHay = 0;
     nSwFebrero = 0;
     aAlfa = "";
     aPath = "";
     aFicGes = "";
     aFicCar = "";
     aDefGes = "";
     aDefCar = "";
     aPago = "";
     nEsVta = 0;
     eaProg = "";
     nDia1 = 0;
     nDia2 = 0;
     nDia3 = 0;
     aCli = "";
     fFechaFac = @;
     nMesNoPago = 0;
     nVtos = 0;
     nCamFPago = 0;
     aAno = "";
     fFecVto = @;
     aDFecha = "";
     aHFecha = "";
     fDFecha = @;
     fHFecha = @;
     fFecha = @;
     fFecha1 = @;
     aDia = "";
     aMes = "";
     nDiaPago = 0;
     nMesPago = 0;
     fAntes = @
     nMesAnt = 0;
     nNume = 0;
     nDias = 0;
     i = 0;
     nMult = 0;
     nImpVto = 0;
     nImpo = 0;
     nAbono = 0;
     nResto = 0;
     nEuro = 0;
     nPrimeImpre = 0;
     nImpre = 0;
     nInfor = 0;
     aAlfa1 = "";
     fFechaOK = @;
|FIN;

|PROCESO Tmp;
     |PRINT;
|FPRC;

|DEFBUCLE Tmp;
     #1#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tmp;
|FIN;

|PROCESO CtrlRecibos;
     |SI nEmpCarte ! #45#6;
          |SI #45#0 = nEmpresa;
               nEmpCarte = #45#6;
               aEmp = ("00000" + #45#6) % -105;
               aAlfa = aPath + "dscarter/fich/" + aEmp + "/";
               |PATH_DAT #30#aAlfa#;
               |ABRE #30;
               |SELECT #12#30;
               |DDEFECTO #30;
               #30#0 = #133#4;
               #30#1 = #133#0;
               #30#2 = #133#1;
               |LEE 000#30];
               |SI FSalida = 0; |Y #30#0 = #133#4;  |Y #30#1 = #133#0;
                         |Y #30#2 = #133#1;
                    |DDEFECTO #1;
                    #1#0 = nEmpresa;
                    #1#1 = #30#0;
                    #1#2 = #30#1;
                    #1#3 = #30#2;
                    #1#4 = nEmpCarte;
                    #1#5 = #30#14;
                    #1#6 = #30#15;
                    #1#7 = #30#40;      || nro. documento
                    #1#8 = #30#17;      || nro. remesa
                    #1#9 = fFechaOK;    || fecha vto. correcta
                    #1#10 = #30#64;     || importe
                    |GRABA 020#1n;
                    nHay = nHay + 1;
                    nHayCarte = 1;
               |FINSI;
          |FINSI;
          |CIERRA #30;
     |FINSI;
|FPRC;

|DEFBUCLE CtrlRecibos;
     #45#2001;
     ;
     00000;
     99999;
     ;
     NULL, CtrlRecibos;
|FIN;

|PROCESO ReciboErr;
     |SI eaProg = "agifa084";
          #133#4 = #84#48;
          #133#0 = #84#0;
          #133#1 = i;
     |SINO;
          #133#4 = #134#49;
          #133#0 = #134#0;
          #133#1 = i;
     |FINSI;
     |LEE 000#133=;
     |SI FSalida = 0; |Y #133#3 = "28.02.2010";
          nEmpCarte = 0;
          nHayCarte = 0;
          |BUCLE CtrlRecibos;
          |SI nHayCarte = 0;
               |DDEFECTO #1;
               #1#0 = nEmpresa;
               #1#1 = #133#4;
               #1#2 = #133#0;
               #1#3 = #133#1;
               #1#4 = 0;
               #1#5 = "";
               #1#6 = "No esta en cartera";
               #1#7 = 0;
               #1#8 = 0;
               #1#9 = "01.01.2000";
               #1#10 = 0;
               |GRABA 020#1n;
               nHay = nHay + 1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO dsm00130;
     aAno = fFecVto; aAno = aAno % -104;
     aDFecha = (("00" + #130#2) % -102) +"."+(("00" + #130#3) % -102) +"."+aAno;
     aHFecha = (("00" + #130#4) % -102) +"."+(("00" + #130#5) % -102) +"."+aAno;
     fDFecha = aDFecha;
     fHFecha = aHFecha;
     |SI fFecVto ] fDFecha; |Y fFecVto [ fHFecha;
          fFecha = fHFecha + 1;
          |HAZ DiasFijos;
          fFecVto = fFecha;
     |FINSI;
|FPRC;

|DEFBUCLE dsm00130;
     #130#1002;
     ;
     aCli, 01;
     aCli, 99;
     ;
     NULL, dsm00130;
|FIN;

|PROCESO Cambios;
     fFecha1 = fFecha;
     aDia = fFecha1 % 102;
     aMes = fFecha1 % 402;
     nDiaPago = aDia;
     nMesPago = aMes;
|FPRC;

|PROCESO DiasFijos;
     |SI nDia1 = 0;
          |VETE FinFijos;
     |FINSI;
     |HAZ Cambios;
     |SI nDiaPago > nDia1;
          |VETE  Fijos2;
     |FINSI;

  |ET Fijos1;
     |SI nDiaPago = nDia1;
          |VETE FinFijos;
     |FINSI;

     |HAZ Cambios;
     fAntes = fFecha;
     nMesAnt = nMesPago;

     fFecha = fFecha + 1;
     |HAZ Cambios;

     || SI el dia fijo=30 y el vto es en Febrero y la fp a 30 dias
     nNume = nDias $ 30;
     |SI nDia1 = 30; |Y nMesAnt = 2; |Y nMesPago = 3; |Y nNume = 0;
          fFecha = fAntes;
          |VETE FinFijos;
     |FINSI;

     |VETE Fijos1;

  |ET Fijos2;
     |SI nDia2 = 0; |O nDiaPago [ nDia1;
          |VETE Fijos1;
     |FINSI;
     |SI nDiaPago > nDia2;
          |VETE Fijos3;
     |FINSI;
     |SI nDiaPago = nDia2;
          |VETE FinFijos;
     |FINSI;

     |HAZ Cambios;
     fAntes = fFecha;
     nMesAnt = nMesPago;

     fFecha = fFecha + 1;
     |HAZ Cambios;

     || SI el dia fijo=30 y el vto es en Febrero y la fp a 30 dias
     nNume = nDias $ 30;
     |SI nDia2 = 30; |Y nMesAnt = 2; |Y nMesPago = 3; |Y nNume = 0;
          fFecha = fAntes;
          |VETE FinFijos;
     |FINSI;

     |VETE Fijos2;

  |ET Fijos3;
     |SI nDia3 = 0;|O nDiaPago > nDia3; |O nDiaPago [ nDia1; |O nDiaPago [ nDia2;
          |VETE Fijos1;
     |FINSI;
     |SI nDiaPago = nDia3;
         |VETE FinFijos;
     |FINSI;

     |HAZ Cambios;
     fAntes = fFecha;
     nMesAnt = nMesPago;

     fFecha = fFecha + 1;
     |HAZ Cambios;

     || SI el dia fijo=30 y el vto es en Febrero y la fp a 30 dias
     nNume = nDias $ 30;
     |SI nDia3 = 30; |Y nMesAnt = 2; |Y nMesPago = 3; |Y nNume = 0;
          fFecha = fAntes;
          |VETE FinFijos;
     |FINSI;

     |VETE Fijos3;

  |ET FinFijos;
     fFecha1 = fFecha;
|FPRC;

|RUTINA Febrero;
     |SI i = 1;
          fFecha = fFechaFac;
          |HAZ Cambios;
          nMesAnt = nMesPago;
          fAntes = fFecha;
     |FINSI;
     fFecha = fFechaFac + nDias;
     |HAZ Cambios;
     |SI nMesPago = 3; |Y nMesAnt = 1;
          nSwFebrero = 1;
          fFechaOK = fFecha;       || la guarda para el informe
          fFecha = fAntes;
          |PARA; |SI; |HACIENDO;
               fAntes = fFecha;
               fFecha = fFecha + 1;
               |HAZ Cambios;
               |SI nMesPago = 3; fFecha = fAntes; |SAL; |FINSI;
          |SIGUE;
     |FINSI;
     nMesAnt = nMesPago;
     fAntes = fFecha;
|FRUT;

|PROCESO Vencimientos;
     nDias = #91nCamFPago / nMult;
     |SI nDias ! 0; |O nDia1 ! 0; |O nDia2 ! 0; |O nDia3 ! 0;
          nNume = nDias $ 30;
          |SI nDia1 = 0; |Y nNume = 0;
               ||Si no hay dias de pago y los vto son a 30 dias
               ||y el vto es el supuesto 29, 30 de febrero se pasa al 28
               |HAZ Febrero;
          |SINO;
               fFecha = fFechaFac + nDias;
          |FINSI;
          |HAZ DiasFijos;
          aMes = fFecha % 402;
          |SI aMes = nMesNoPago;          ||Si es mes de no pago...
               fFecha = fFecha + .001;
               fFechaFac = fFechaFac + .001;
               fFecha1 = fFecha;
          |FINSI;
          fFecVto = fFecha1;
          nImpVto = nImpo * nAbono;
          |SI i = nVtos;
               nImpVto = nImpVto + (nResto * nAbono);
          |FINSI;
     |SINO;
          fFecVto = fFechaFac;
          nImpVto = nImpo * nAbono;
     |FINSI;
     nImpVto = nImpVto / nEuro;
     |SI nEsVta = 1;
          |BUCLE dsm00130;
     |FINSI;
|FPRC;

|PROCESO LeePago;
     |ABRE #91;
     |DDEFECTO #91;
     #91#0 = aPago;
     |LEE 000#91=;
     |CIERRA #91;
|FPRC;

|PROCESO FacDir;
     |DDEFECTO #24;
     #24#0 = #84#3;
     |LEE 000#24=;

     nEsVta = 1;
     eaProg = "agifa084";
     nDia1 = #84#88;
     nDia2 = #84#89;
     nDia3 = #84#90;
     aCli = #84#3;
     fFechaFac = #84#1;
     nMesNoPago = #24#21;
     aPago = #84#8;
     |HAZ LeePago;
     nVtos = #91#3;
     |SI #91#5 = "N";
          nMult = 1;
     |SINO;
          nMult = 1000;
     |FINSI;

     nSwFebrero = 0;
     |PARA i = 1; |SI i [ nVtos; |HACIENDO i = i + 1;
          nCamFPago = i + 5;
          |SI nSwFebrero = 0; |HAZ Vencimientos; |FINSI;
          |SI nSwFebrero ! 0; |HAZ ReciboErr; |FINSI;
     |SIGUE;
|FPRC;

|PROCESO FacVta;
     |DDEFECTO #24;
     #24#0 = #134#2;
     |LEE 000#24=;

     nEsVta = 1;
     eaProg = "agifa134";
     nDia1 = #134#14;
     nDia2 = #134#15;
     nDia3 = #134#16;
     aCli = #134#2;
     fFechaFac = #134#1;
     nMesNoPago = #24#21;
     aPago = #134#11;
     |HAZ LeePago;
     nVtos = #91#3;
     |SI #91#5 = "N";
          nMult = 1;
     |SINO;
          nMult = 1000;
     |FINSI;

     nSwFebrero = 0;
     |PARA i = 1; |SI i [ nVtos; |HACIENDO i = i + 1;
          nCamFPago = i + 5;
          |SI nSwFebrero = 0; |HAZ Vencimientos; |FINSI;
          |SI nSwFebrero ! 0; |HAZ ReciboErr; |FINSI;
     |SIGUE;
|FPRC;

|DEFBUCLE FacDir;
     #84#3004;
     ;
     "      ", "01.01.2010", "     ", 000001;
     "zzzzzz", "31.01.2010", "zzzzz", 999999;
     ;
     NULL, FacDir;
|FIN;

|DEFBUCLE FacVta;
     #134#3004;
     ;
     "      ", "01.01.2010", "     ", 000001;
     "zzzzzz", "31.01.2010", "zzzzz", 999999;
     ;
     NULL, FacVta;
|FIN;

|PROCESO Empresas;
     nEmpresa = #41#0;
     aFicGes = aPath + "dscomer9/fich/" + (("00000" + #41#0)%-105) + "/";
     |PATH_DAT #24 aFicGes;
     |PATH_DAT #84 aFicGes;
     |PATH_DAT #91 aFicGes;
     |PATH_DAT #130 aFicGes;
     |PATH_DAT #133 aFicGes;
     |PATH_DAT #134 aFicGes;
     |ABRE #133;
     |BUCLE FacVta;
     |BUCLE FacDir;
     |CIERRA #133;
|FPRC;

|DEFBUCLE Empresas;
     #41#1001;
     ;
     00000;
     99999;
     ;
     NULL, Empresas;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     aAlfa1 = Usuario;   |QBF aAlfa1;
     |NOME_DAT #1 aAlfa1;
     |DELINDEX #1;
     |ABRE #1;

     aPath = #0#1; |QBF aPath;
     aAlfa = aPath % -101;
     |SI aAlfa ! "/"; |Y aAlfa ! "\"; aPath = aPath + "/"; |FINSI;

     aFicGes = aPath + "dscomer9/fich/";
     aDefGes = aPath + "dscomer9/def/";
     aFicCar = aPath + "dscarter/fich/";
     aDefCar = aPath + "dscarter/def/";

     aAlfa = aDefGes + "agifa041"; |CARGA_DEF aAlfa, agifa041@;
     |SI FSalida ! 0;
          |MENAV "0000Error al cargar 'def'";
          |ACABA;
     |FINSI;
     aAlfa = aDefGes + "agifa024"; |CARGA_DEF aAlfa, agifa024@;
     aAlfa = aDefGes + "agifa084"; |CARGA_DEF aAlfa, agifa084@;
     aAlfa = aDefGes + "agifa091"; |CARGA_DEF aAlfa, agifa091@;
     aAlfa = aDefGes + "dsm00130"; |CARGA_DEF aAlfa, dsm00130@;
     aAlfa = aDefGes + "agifa133"; |CARGA_DEF aAlfa, agifa133@;
     aAlfa = aDefGes + "agifa134"; |CARGA_DEF aAlfa, agifa134@;

     aAlfa = aDefCar + "dscam045"; |CARGA_DEF aAlfa, dscam045@;
     aAlfa = aDefCar + "dscam003"; |CARGA_DEF aAlfa, dscam003@;

     |PATH_DAT #41 aFicGes;
     |PATH_DAT #45 aFicCar;
     |BUCLE Empresas;

     |DESCARGA_DEF #dscam003@;
     |DESCARGA_DEF #dscam045@;

     |DESCARGA_DEF #agifa134@;
     |DESCARGA_DEF #agifa133@;
     |DESCARGA_DEF #dsm00130@;
     |DESCARGA_DEF #agifa091@;
     |DESCARGA_DEF #agifa084@;
     |DESCARGA_DEF #agifa024@;
     |DESCARGA_DEF #agifa041@;

     |CIERRA #1;
     |SI nHay ! 0;
          |IMPRE 0;
          |SI FSalida = 0;
               |INFOR "vtofactu";
               |SI FSalida = 0;
                    |BUCLE Tmp;
                    |PIEINF;
                    |FININF;
               |FINSI;
               |FINIMP;
               aAlfa1 = "0000Encontrados [" + nHay + "] recibos con las condiciones previstas.";
               |MENAV aAlfa1;
          |FINSI;
     |SINO;
          |MENAV "0000No se encuentran recibos con las condiciones previstas.";
     |FINSI;

     |DELINDEX #1;
|FPRC;

|PROGRAMA;
     |DBASS aAlfa;
     #0#1 = aAlfa;
     |ABRE #0;
     |LEE 101#0p; |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |GRABA 020#0a;
     |FINSI;
     |LIBERA #0;
     |CIERRA #0;
|FPRO;
