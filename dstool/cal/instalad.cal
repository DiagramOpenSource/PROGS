|FICHEROS;
    instalad #0;
|FIN;

|VARIABLES;
    sis = "";
    tra = "";
    tra1 = "";
    directo = "";
    fiche = "";
    handle = "";
    field = "";
    field2 = "";
    num = 0;
    num1 = 0;
    idirec = "";
    iversi = "";
    inume = "";
    ifich = "";
    ibasi = "";
    basico = "";
    bueno1 = "";
    bueno2 = "";
    bueno3 = "";
    bueno4 = "";
    camuf = 0;
    camufz = "";
    par1 = "";
    par2 = 0;
    par3 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    FEstado = 0;
    serial_sal = 0;
    tope = 0;
    sw_pregunta = 0;
    copiados = 0;
    buffer = 0;
    elemen = 0;

    buf_opc = 0;
    buf_men = 0;
    ele_opc = 0;
    ele_men = 0;
    var0 = "";
    longi = 0;
    aplic = "";
    tope1 = 0;
    encue = 0;
    hasta = 0;
    nombr = "";
    direc = "";
    si_esta = 0;
    opciones = 0;
    submenus = 0;
    cual1 = 0;
    cual2 = 0;
    veces = 0;

    acc_menu = "";
    acc_sino = "";
    acc_posi = 0;
    acc_tope = 0;
    acc_gpos = 0;
    acc_hand = "";
    acc_sali = 0;
|FIN;

***************************** MAPA DE PROCESOS ******************************
> recogida del 'infopaq.agi'
> copiar discos, descomprimir, instalar
> gestion de claves
> inclusion del opc en el menu
> confirmacion de menus con acceso directo
> calculos desde pantalla
*****************************************************************************

|PROGRAMA;
|CLS;
|PINPA #0#0;
|CABEZA "Instalacion Aplicaciones";
|QUE_SISTEMA sis;
|ABRE #0;
    bueno4 = "N";
    #0#0 = 1;
|LEE 140#0=;
|SI FSalida = 107;
    |MENAV "    Otro usuario esta instalando ...";
    |LIBERA #0;
    |CIERRA #0;
    |ACABA;
|FINSI;
|SI FSalida ! 0;
    |DDEFECTO #0;
    |HAZ parasistema;
        Pos = -1;
    |HAZ medio;
        #0#0 = 1;
    |GRABA 020#0b;
|FINSI;
|PINDA #0#0;
|ENDATOS #1#0;
    FEstado = FSalida;
|SI bueno4 = "S";
    |DDEFECTO #0;
        #0#0 = 1;
        #0#1 = bueno1;
        #0#2 = bueno2;
        #0#3 = bueno3;
    |GRABA 020#0a;
|FINSI;
|LIBERA #0;
|CIERRA #0;


|SI FEstado ! 0;|Y basico < ibasi;
        alfa1 = "AGIsys instalado ["+basico+"] inferior al requerido ["+ibasi+"] ...";
    |AVISO;
    |PINTA 1904, alfa1;
    |PINTA 2004, "Instalacion no posible ...";
    |PAUSA;
|FINSI;
|FPRO;


____________________________________/ RECOGIDA EL 'infopaq.agi'
|PROCESO correc1; |TIPO 0;
    #0#5 = #0#5 > " ";   |PINTA #0#5;

|SI #0#5 = "N";
    |CAMPO_MODIFICA #0#9, 1, "N";
    |ACABA;
|FINSI;

    #0#4 = "Copiando Informacion Sobre el Paquete a Instalar ...";
|PINTA #0#4;
    tra1 = "infopaq.agi";
|HAZ sistema;
    |SYSTEM tra;

    directo = "";
|DIRECTORIO directo;
    directo = directo + "infopaq.agi"
    fiche = "r   " + directo;
|QBF fiche;
|FABRE fiche;
|SI FSalida < 0;
    |MENAV "     DISCO INCORRECTO - COLOQUE DISCO CORRECTO";
    |HAZ mensaje1;
    |ERROR;
    |ACABA;
|FINSI;
    #0#4 = "INFORMACION APLICACION A INSTALAR";
|PINTA #0#4;
    handle = FSalida;
    field = handle + " 254";  |FLEE field;   idirec = field;
    field = handle + " 254";  |FLEE field;   iversi = field;
    field = handle + " 254";  |FLEE field;   inume = field;
    field = handle + " 254";  |FLEE field;   #0#8 = field;
    field = handle + " 254";  |FLEE field;   #0#6 = field;
    field = handle + " 254";  |FLEE field;   #0#7 = field;
    field = handle + " 254";  |FLEE field;
    field = handle + " 254";  |FLEE field;   ifich = field;
    field = handle + " 254";  |FLEE field;   ibasi = field;
|FCIERRA handle;
    Pos = -1;

|PINTA #0#8;
|PINTA #0#6;
|PINTA #0#7;
    camufz = iversi;          |HAZ base_10;  #0#13 = camuf;
|PINTA #0#13;
    bueno1 = #0#1;
    bueno2 = #0#2;
    bueno3 = #0#3;
    bueno4 = "S";

|VERSIONRTME basico;
|SI basico < ibasi;
        #0#9 = "N";
    |PINTA #0#9;
    |PTEC 807;
|FINSI;
|FPRC;

____________________________________/ COPIAR DISCOS, DESCOMPRIMIR, INSTALAR
|PROCESO correc2; |TIPO 0;
|SI #0#5 ! "S";  |PTEC 802;  |ACABA;  |FINSI;

    directo = "";
|DIRECTORIO directo;
    tra = directo + "bin/agirm " + directo + "infopaq.agi";
|SYSTEM tra;
    #0#9 = #0#9 > " ";    |PINTA #0#9;
|SI #0#9 = "S";                               || proceder a instalar SI
        sw_pregunta = 0;
        copiados = 0;
    |PARA num = 1; |SI num [ #0#8; |HACIENDO num = num + 1;
        |ET pide;
                tra = num;
                tra1 = "00" + tra;
                tra = tra1 % -102;
            |SI sw_pregunta = 1;
                    tra1 = "Coloque Disco -";
                    tra1 = tra1 + tra + "- En unidad de Instalacion";
                    #0#10 = tra1;
                |PINTA #0#10;
                |CAMPO_MODIFICA #0#11,1,"S";
                |AVISO;
                |ENCAMPO #11#0;
                |CAMPO_MODIFICA #0#11,1,"N";
                |SI #0#11 = "N";
                    |HAZ deshacer;
                    |SAL;
                |FINSI;
            |FINSI;
            |SI #0#1 = 1;
                    tra1 = "Copiando Disco -";
                    tra1 = tra1 + tra + "- Espere Por Favor ...";
                    #0#10 = tra1;
            |SINO;
                    #0#10 = "Copiando Informacion desde Cinta - Espere Por Favor ...";
            |FINSI;
            |PINTA #0#10;
                sw_pregunta = 1;
                tra1 = ifich;      |QBF tra1;     tra1 = tra1 + ".Z" + tra;
            |HAZ sistema;
            |SYSTEM tra;
                directo = "";
            |DIRECTORIO directo;
                directo = directo + tra1;
                fiche = "rb  " + directo;
            |FABRE fiche;
            |SI FSalida < 0;
                |MENAV "    ATENCION - DISCO INCORRECTO";
                |VETE pide;
            |SINO;
                |FCIERRA FSalida;
                    copiados = copiados + 1;
                    #0#11 = "S";
            |FINSI;
                tra = #0#3 % 105;
            |SI #0#1 = 1; |Y tra = "/dev/";
                    tra = "eject " + #0#3;
                |SYSTEM tra;
            |FINSI;
    |SIGUE;
    |SI #0#11 = "S";
        |HAZ la_clave;
        |SI serial_sal ! -2;
            |HAZ descomprime;
        |FINSI;
    |FINSI;
|FINSI;
|PTEC 802;
|FPRC;

|PROCESO sistema;
    tra = "";
    directo = "";
|DIRECTORIO directo;

    tra = #0#3 % 105;
|SI #0#1 = 2;|O tra = "/dev/";
        tra = "tar xf " + #0#3;    |QBF tra;
        tra = tra + " " + tra1;
|SINO;
        tra = #0#3 % 201;
    |SI tra = " ";
            tra = directo + "bin/agcopiaa";
            tra = tra + " " + tra1 + " " + directo + tra1;
            tra = tra + " " + #0#3;                    |QBF tra;
            tra = tra + ":";
    |SINO;
            tra = "tar xf " + directo + "agitar/" + #0#3;    |QBF tra;
            tra = tra + " " + tra1;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO deshacer;
|PARA num1 = 1; |SI num1 [ copiados;  |HACIENDO num1 = num1 + 1;
        tra = num1;
        tra1 = "000" + tra;
        tra = tra1 % -102;
        directo = "";
    |DIRECTORIO directo;
        tra1 = directo + ifich;
    |QBF tra1;
        tra1 = tra1 + ".Z" + tra;
        tra = directo + "bin/agirm " + tra1;
    |SYSTEM tra;
|SIGUE;
|FPRC;

|PROCESO descomprime;
    #0#10 = "Descomprimiendo Aplicacion - Espere Por Favor ...";
|PINTA #0#10;
    directo = "";
|DIRECTORIO directo;
    tra1 = directo + ifich;
|DESCOMPRIME tra1;

    fiche = "rb  " + tra1;
|FABRE fiche;
|SI FSalida < 0;
    |MENAV "    ATENCION - DESCOMPRESION NO REALIZADA CORRECTAMENTE";
|SINO;
    |FCIERRA FSalida;
        tope = #0#8;   |HAZ deshacer;
    |HAZ detar;
    |HAZ el_opc;
    |HAZ el_acceso;
|FINSI;
|FPRC;

|PROCESO detar;
    #0#10 = "Instalando Aplicacion - Espere Por Favor ...";
|PINTA #0#10;
    directo = "";
|DIRECTORIO directo;
    tra1 = directo + ifich;   |QBF tra1;
|DETAR tra1, directo;
|BLIN 5;
|BLIN 6;

    #0#10 = "Borrando Temporales - Espere Por Favor ...";
|PINTA #0#10;
    directo = "";
|DIRECTORIO directo;
    tra1 = directo + "bin/agirm " + directo + ifich;   |QBF tra1;
|SYSTEM tra1;
|FPRC;


____________________________________/ GESTION DE LA CLAVE
|PROCESO la_clave;
  veces = 1;
  #0#10 = "";
  |PINTA #0#10;

  camufz = inume;  |HAZ base_10;  par2 = camuf;      || nro.interno
  camufz = iversi; |HAZ base_10;  par3 = camuf;      || version

  par1 = "";
  |PONSERIAL par1, par2, par3;
  serial_sal = FSalida;
  |SI serial_sal = -1;
      |PARA; |SI; |HACIENDO;
             #0#14 = "Introduzca Clave de Instalacion (distinguiendo minusculas y mayusculas).";
             |AVISO;
             |PINTA #0#14;
             |CAMPO_MODIFICA #0#12, 1, "S";
             |ENCAMPO #12#0;
             |CAMPO_MODIFICA #0#12, 1, "N";
             par1 = #0#12;    |QBF par1;
             |PONSERIAL par1, par2, par3;
             serial_sal = FSalida;
             |SI serial_sal = -2;
                 alfa1 = "    Instalacion no posible. Fichero de configuracion para depurar ...";
                 |SAL;
             |FINSI;
             |SI serial_sal = -1;
                 alfa1 = "    Instalando en modo DEMO ...";
                 veces = veces + 1;
                 |SI veces > 3;
                     |CONFI "2300SConfirme Instalacion en modo DEMO (S/N): ";
                     |SI FSalida ! 0;
                         serial_sal = -2;
                     |FINSI;
                     |SAL;
                 |SINO;
                      alfa2 = "2300Clave Incorrecta. (" + veces + "/3)";
                      |ATRI P;
                      |MENSA alfa2;
                      |ATRI 0;
                 |FINSI;
             |FINSI;
             |SI serial_sal = 0;
                 alfa1 = "    Instalando ...";
                 |SAL;
             |FINSI;
      |SIGUE;
  |SINO;
      alfa1 = "    Reinstalando ...";
  |FINSI;

  #0#14 = "";  |PINTA #0#14;
  #0#12 = "";  |PINTA #0#12;

  |BLIN 23;
  |ATRI P;
  |MENSA alfa1;
  |ATRI 0;
|FPRC;

|PROCESO base_10;
  camuf = 0;
  |PARA para1 = 4; |SI para1 ] 1; |HACIENDO para1 = para1 - 1;
        nume1 = (para1 * 100) + 1;
        alfa1 = camufz % nume1;
        nume1 = &alfa1 - 65;
        |SI para1 = 1;  nume1 = nume1*26*26*26;  |FINSI;
        |SI para1 = 2;  nume1 = nume1*26*26;     |FINSI;
        |SI para1 = 3;  nume1 = nume1*26;        |FINSI;
        camuf = camuf + nume1;
  |SIGUE;
|FPRC;

____________________________________/ INCLUSION DEL OPC
|PROCESO el_opc;
|HAZ lee_opc;
|FINDIM buf_opc;
|FINDIM buf_men;
|FPRC;

|PROCESO lee_opc;
    directo = "";
|DIRECTORIO directo;
    fiche = directo + idirec + "/fich/" + idirec + ".opc";
|LEESECUENCIAL fiche, buf_opc, ele_opc, 1, 0;
|SI ele_opc > 0;
        ele_opc = ele_opc + 1;
    |HAZ lee_men;
|FINSI;
|FPRC;

|PROCESO lee_men;
    directo = "";
|DIRECTORIO directo;
    fiche = directo + "agi.men";
|LEESECUENCIAL fiche, buf_men, ele_men, 1, 5;
|SI ele_men > 0;
        ele_men = ele_men + 1;
    |HAZ si_esta;
    |SI si_esta = 0;
        |HAZ pon_opc;
        |HAZ gra_men;
    |FINSI;
|SINO;
        alfa1 = "    [" + fiche + "] NO accesible intentando lectura.";
    |MENAV alfa1;
|FINSI;
|FPRC;

|PROCESO dos_coma;       || busca si ya esta
|QBF aplic;
    alfa2 = aplic % 0;    tope1 = alfa2;
    encue = 0;
|PARA para1 = 1; |SI para1 [ tope1; |HACIENDO para1 = para1 + 1;
        nume1 = (para1 * 100) + 1;
        alfa1 = aplic % nume1;
    |SI alfa1 = ",";
        |SI encue = 0;
                encue = para1;
                hasta = 100 + para1 - 1;
        |SINO;
                nombr = aplic % hasta;  || saca nombre

                encue = para1 + 1;
                aplic = aplic % encue;  || saca resto

            |SAL;
        |FINSI;
    |FINSI;
|SIGUE;
|FPRC;

|PROCESO si_esta;
    si_esta = 0;
|PARA para4 = 1; |SI para4 [ ele_men; |HACIENDO para4 = para4 + 1;
    |DEL_BUF buf_men, para4, aplic;
        alfa2 = aplic - ",-1,0 ";       || si es entrada a menu
    |SI aplic ! alfa2;
        |HAZ dos_coma;
        |DEL_BUF buf_opc, 1, var0;
        |SI aplic = var0;               || si es la misma
                si_esta = 1;
            |SAL;
        |FINSI;
    |FINSI;
|SIGUE;
|FPRC;

|PROCESO una_coma;
|QBF aplic;
    alfa2 = aplic % 0;    tope1 = alfa2;
    encue = 0;
|PARA para1 = 1; |SI para1 [ tope1; |HACIENDO para1 = para1 + 1;
        nume1 = (para1 * 100) + 1;
        alfa1 = aplic % nume1;
    |SI alfa1 = ",";
            encue = para1;
            hasta = 100 + para1 - 1;

            nombr = aplic % hasta;  || saca nombre

            encue = para1 + 1;
            aplic = aplic % encue;  || saca resto

        |SAL;
    |FINSI;
|SIGUE;
|FPRC;

|PROCESO pon_opc;
    submenus = -1;
|DEL_BUF buf_opc, 1, aplic;        || si tiene empresa se modifica el
    alfa1 = aplic - "NULL";        || nro. actual de submenu
|SI alfa1 = aplic;
    |DEL_BUF buf_men, 2, aplic;
    |HAZ una_coma;
        submenus = aplic;
        nume1 = aplic;   nume1 = nume1 + 1;
        aplic = nombr + "," + nume1;
    |AL_BUF buf_men, 2, aplic;
|FINSI;

|DEL_BUF buf_men, 3, aplic;        || se modifica el nro. actual de opciones
|HAZ una_coma;
    opciones = nombr;
    nume1 = nombr;  nume1 = nume1 + 1;
    aplic = nume1 + "," + aplic;
|AL_BUF buf_men, 3, aplic;

    camufz = inume;  |HAZ base_10;  par2 = camuf;      || nro.interno
    cual1 = -1;
|SI par2 ] 10;|Y par2 [ 3100;
    |SI par2 = 10;                      cual1 = 1;  |FINSI;  || agicli
    |SI par2 = 100;                     cual1 = 2;  |FINSI;  || aginom
    |SI par2 = 900;                     cual1 = 3;  |FINSI;  || agilab
    |SI par2 ] 1000;|Y par2 [ 1030;     cual1 = 4;  |FINSI;  || agicont
    |SI par2 = 2300;                    cual1 = 5;  |FINSI;  || agieos
    |SI par2 ] 1990;|Y par2 [ 1999;     cual1 = 6;  |FINSI;  || agiren
    |SI par2 = 2250;                    cual1 = 7;  |FINSI;  || socie
    |SI par2 ] 2190;|Y par2 [ 2199;     cual1 = 8;  |FINSI;  || sociedad
    |SI par2 ] 3000;|Y par2 [ 3050;     cual1 = 9;  |FINSI;  || facges
    |SI par2 = 3100;                    cual1 = 10; |FINSI;  || seguros
|FINSI;

|SI cual1 ! -1;
    |PARA para1 = 2; |SI para1 [ opciones; |HACIENDO para1 = para1 + 1;
            para2 = para1 + 2;
        |DEL_BUF buf_men, para2, aplic;
            alfa1 = aplic % 108;
        |HAZ traduce;
        |SI cual1 < cual2;
            |SAL;
        |FINSI;
    |SIGUE;
|SINO;
        para2 = opciones + 2;
|FINSI;
|PARA para1 = ele_men; |SI para1 ] para2; |HACIENDO para1 = para1 - 1;
        para3 = para1 + 1;
    |DEL_BUF buf_men, para1, alfa1;
    |AL_BUF buf_men, para3, alfa1;
|SIGUE;
|DEL_BUF buf_opc, 2, alfa1;
|SI submenus ! -1;
        alfa1 = alfa1 + submenus + ",";
|FINSI;
|AL_BUF buf_men, para2, alfa1;     ele_men=ele_men+1;

|SI submenus ! -1;
    |DEL_BUF buf_opc,3,alfa1; |AL_BUF buf_men,ele_men,alfa1;  ele_men=ele_men+1;
    |DEL_BUF buf_opc,4,alfa1; |AL_BUF buf_men,ele_men,alfa1;  ele_men=ele_men+1;
    |DEL_BUF buf_opc,5,alfa1; |AL_BUF buf_men,ele_men,alfa1;  ele_men=ele_men+1;
|FINSI;
|FPRC;

|PROCESO traduce;
    cual2 = 999;
|SI alfa1 = "AGICLI -";  cual2 = 1;     |FINSI;   || agicli
|SI alfa1 = "AGINOM1-";  cual2 = 2;     |FINSI;   || aginom
|SI alfa1 = "AGINOM2-";  cual2 = 3;     |FINSI;   || agilab
|SI alfa1 = "AGICONT-";  cual2 = 4;     |FINSI;   || agicont
|SI alfa1 = "AGIFISC-";  cual2 = 5;     |FINSI;   || agieos
|SI alfa1 = "AGIRENT-";  cual2 = 6;     |FINSI;   || agiren
|SI alfa1 = "AGIFISO-";  cual2 = 7;     |FINSI;   || socie
|SI alfa1 = "AGISOCI-";  cual2 = 8;     |FINSI;   || sociedad
|SI alfa1 = "AGIPROF-";  cual2 = 9;     |FINSI;   || facges
|SI alfa1 = "AGISEG -";  cual2 = 10;    |FINSI;   || seguros
|FPRC;

|PROCESO gra_men;        || regraba el menu
    fiche = directo + "agi.men";
|GRABASECUENCIAL fiche, buf_men, 1, ele_men;
|SI FSalida < 0;
        alfa1 = "    [" + fiche + "] NO accesible intentando grabacion.";
    |MENAV alfa1;
|FINSI;
|FPRC;


____________________________________/ ACCESO DIRECTO MENUS
|PROCESO el_acceso;
|AVISO;
|CONFI "2300N¨Menus de Aplicacion con Acceso Directo (S/N)?: ";
|SI FSalida = 0;
        acc_sino = "S";
|SINO;
        acc_sino = "N";
|FINSI;
|HAZ nom_menu1;
|HAZ nom_menu2;
|FPRC;

|PROCESO nom_menu1;           || saca el nombre del menu
    Pos = -1;
    directo = "";
|DIRECTORIO directo;
    fiche = "r   " + directo + idirec + "/fich/" + idirec + ".opc";
|FABRE fiche;
|SI FSalida ] 0;
        handle = FSalida;
        field = handle + " 254";
    |FLEE field;
    |SI FSalida > 0;
            field2 = field;
        |HAZ gra_menu;
    |FINSI;
    |FCIERRA handle;
        Pos = -1;
|FINSI;
|FPRC;

|PROCESO nom_menu2;           || busca si cuelgan menus
    directo = "";
|DIRECTORIO directo;
    fiche = "rb  " + directo + idirec + "/" + acc_menu;
|FABRE fiche;
|SI FSalida ] 0;
        handle = FSalida;
        field = handle + " 1";
    |FLEE field;    acc_sali = FSalida;
    |PARA; |SI acc_sali > 0; |HACIENDO;
            nume1 = &field;
        |SI nume1 = 10;
            |HAZ nom_menu3;
                field2 = "";
        |SINO;
                field2 = field2 + field;
        |FINSI;
            field = handle + " 1";
        |FLEE field;     acc_sali = FSalida;
    |SIGUE;
    |FCIERRA handle;
        Pos = -1;
|FINSI;
|FPRC;

|PROCESO nom_menu3;
    alfa1 = field2 - ",-1,0 ";
|SI alfa1 ! field2;
    |HAZ gra_menu;
|FINSI;
|FPRC;

|PROCESO gra_menu;            || regraba la posicion 0 del menu en cuestion
    acc_gpos = Pos;

    alfa1 = field2 % 0;   acc_tope = alfa1;
|PARA para1 = acc_tope; |SI para1 ] 1; |HACIENDO para1 = para1 - 1;
        nume1 = (para1 * 100) + 1;
        alfa2 = field2 % nume1;
    |SI alfa2 = " ";
            acc_posi = para1 + 1;
        |SAL;
    |FINSI;
|SIGUE;

    acc_menu = (field2 % acc_posi) + ".men";
|QBF acc_menu;

    Pos = -1;
    directo = "";
|DIRECTORIO directo;
    fiche = "r+b " + directo + idirec + "/" + acc_menu;
|FABRE fiche;
|SI FSalida ] 0;
        acc_hand = FSalida;
    |SI acc_sino = "S";  field = "1";   |FINSI;
    |SI acc_sino = "N";  field = "0";   |FINSI;
        Pos = 0;
        field = acc_hand + " " + field;
    |FGRABA field;
    |FCIERRA acc_hand;
|FINSI;

    Pos = acc_gpos;
|FPRC;


____________________________________/ CALCULOS DE PANTALLA
|PROCESO medio; |TIPO 0;
|SI #0#1 = 1;
        #0#2 = "DISKETTE";
|SINO;
        #0#2 = "CINTA,DAT";
|FINSI;
|PINTA #0#2;
|FPRC;

|PROCESO unidad; |TIPO 0;
    tra = #0#3;     |QBF tra;
|SI tra = "a";  tra = "A"; |FINSI;
|SI tra = "b";  tra = "B"; |FINSI;
|SI tra = "c";  tra = "C"; |FINSI;
    #0#3 = tra;
|PINTA #0#3;
|HAZ mensaje1;

    alfa1 = #0#3 % 201;
    alfa2 = #0#3 % 105;
|SI alfa1 ! " ";|Y alfa2 ! "/dev/";
        directo = "";
    |DIRECTORIO directo;
        directo = directo + "agitar/" + #0#3;
        fiche = "r   " + directo;
    |QBF fiche;
    |FABRE fiche;
    |SI FSalida < 0;
            alfa1 = fiche + ": NO ACCESIBLE O INEXISTENTE ...";
        |MENAV alfa1;
        |ERROR;
    |FINSI;
    |FCIERRA fiche;
        Pos = -1;
|FINSI;
|FPRC;

|PROCESO mensaje1;
    #0#4 = "Prepare el Medio de Instalacion ... ";
|PINTA #0#4;
|FPRC;

|PROCESO correc3; |TIPO 0;
    #0#11 = #0#11 > " ";
|PINTA #0#11;
|FPRC;

|PROCESO parasistema;         || recoge defectos para la primera vez
|SI sis = "ESDOS";
        #0#1 = 1;
    |PINTA #0#1;
    |CAMPO_MODIFICA #0#1,1,"N";
        #0#3 = "A";
    |ACABA;
|FINSI;

    directo = "";
|DIRECTORIO directo;
    directo = directo + "/dev/drive"
    fiche = "r+  " + directo;
|QBF fiche;
|FABRE fiche;
|SI FSalida < 0;
        #0#1 = 1;
        #0#3 = "A";
    |ACABA;
|FINSI;

    handle = FSalida;
    field = handle + " 254";
|FLEE field;
|SI FSalida = 0;
        #0#1 = 1;
        #0#3 = "A";
    |FCIERRA handle;
    |ACABA;
|FINSI;

    tra = "";
    num1 = 1;
|PARA num = 1; |SI num [ FSalida;  |HACIENDO num = num + 1;
        num1 = num1 + 100;
        tra1 = field % num1;
    |SI tra1 ! " ";
            tra = tra + tra1;
    |SINO;
        |SAL;
    |FINSI;
|SIGUE;
    tra1 = tra - "fd0";
|SI tra1 ! tra;
        #0#1 = 1;
        #0#3 = "A";
|SINO;
        tra1 = tra - "fd1"
    |SI tra1 ! tra;
            #0#1 = 1;
            #0#3 = "B";
    |SINO;
            #0#1 = 2;
            #0#3 = tra;
    |FINSI;
|FINSI;
|FCIERRA handle;
|FPRC;
