||Que el fichero final quede en el cliente.  OK
||Utilizacion de "BROWSE" para seleccionar el directorio local. Ok.
||Creacion de un clave y guardar diferentes configuraciones. OK
||Preguntando si se quiere guardar configuracion. OK
||Reubicar los mensajes en la pantalla. OK

|FICHEROS;
     dspaquet #0; ||Este Def.

     dstool01 #1; ||Mensaje;
|FIN;

|VARIABLES;
     nSwAutorizado = 0;
     nHandle = 0;
     aUsuMin = "";
     aNombre = "";
     aNomMin = "";
     aUsuario = "";
     aBase = "";
     aControl = "";
     aMinuscula = "";
     aComMinus = "";
     aAuxMinus = "";
     aSistema = "";
     aMensaje = "";
     nSwDatos = 0;
     aDComodin = "";
     aDTrab2 = "";
     aComodin = "";
     aDirectorio = "";
     aNomSin = "";
     aFicOri = "";
     aFicDes = "";

     aDTrab    = "";
     aDBase    = "";
     aDTempo   = "";
     aAlfa     = "";
     aAlfa1    = "";
     aAlfa2    = "";
     aAlfa3    = "";
     aAlfa4    = "";
     aAlfa5    = "";
     dire      = "";
     aDesPaq   = "";
     aInsPaq   = "";
     aOriPaq   = "";
     aAdmira   = "";
     aCreaDir  = "";
     aDirPaq   = "";
     sistem    = "";
     nPara1    = 0;
     nSalParte = 0;
     nSwCopia  = 0;
     nNume1    = 0;
     aIPRemoto = "";
     aOrigen   = "";
     aDestino  = "";
|FIN;

|PROCESO ElTar;
     |QUE_SISTEMA sistem;
     |PARA nPara1 = 0; |SI nPara1 < nSalParte; |HACIENDO nPara1 = nPara1 + 1;
         |SI nSalParte = 1;
              aAlfa1 = aDesPaq + ".tgz";
              aAlfa2 = aDirPaq + ".tgz";
              aAlfa3 = "0000Copiando Volumen .tgz";
         |SINO;
              aAlfa1 = aDesPaq + "." + nPara1;
              aAlfa2 = aDirPaq + "." + nPara1;
              aAlfa3 = "0000Copiando Volumen " + nPara1;
         |FINSI;
         |SI #0#5 = "N";
              |MENSA aAlfa3;
              aOrigen = aAlfa1;
              aDestino = aAlfa2;
              |HAZ EnviaFichero;
         |SINO;
              aAlfa4 = "0000Prepare Disco en Unidad A";
              |MENAV aAlfa4;
              |MENSA aAlfa3;
              aOrigen = aAlfa1;
              aDestino = aAlfa2;
              |HAZ EnviaFichero;
         |FINSI;
     |SIGUE;
|FPRC;

|PROCESO EnviaFichero;
     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |SI aIPRemoto ! "";
         |ENVIA_FICHERO aOrigen, aDestino;
     |SINO;
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;
|FPRC;

|PROCESO Borralo;
     aDTrab = aDBase + aDTempo;
     aAlfa1 = "0000Borrando [" + aDTrab + "] ...";
     |MENSA aAlfa1;
     aAlfa1 = "";
     |DIRECTORIO aAlfa1;
     aAlfa1 = aAlfa1 + "bin/agirm -r " + aDTrab;
     |SYSTEM aAlfa1;
     |BLIN 4;

     |SI #dspaquet#8 = "N";
          aDTrab = aDBase + aDComodin;
          aAlfa1 = "0000Borrando [" + aDTrab + "] ...";
          |MENSA aAlfa1;
          aAlfa1 = "";
          |DIRECTORIO aAlfa1;
          aAlfa1 = aAlfa1 + "bin/agirm -r " + aDTrab;
          |SYSTEM aAlfa1;
          |BLIN 4;
     |FINSI;
|FPRC;

|PROCESO Fragmentos;
     aAlfa1 = aDesPaq + ".tgz";
     nNume1 = #0#4 * 1000;
     aAlfa2 = aDesPaq + ".";
     |PARTE_FICHERO aAlfa1, nNume1, aAlfa2;
     nSalParte = FSalida;
     |SI nSalParte = -1;
          aAlfa1 = "0000No se pudo leer [" + aDesPaq + ".tgz]";
          |MENAV aAlfa1;
          nSwCopia = 1;
     |SINO;
          |SI nSalParte = -2;
               aAlfa1 = "0000No se pudo grabar [" + aDesPaq + ".*]";
               |MENAV aAlfa1;
               nSwCopia = 1;
          |SINO;
               |SI nSalParte = 1;
                    aAlfa1 = aDesPaq + ".0";
                    |FBORRA aAlfa1;
                    aAlfa1 = "Instalador creado con exito.";
                    |PINTA 2225, aAlfa1;
               |SINO;
                    aAlfa1 = "Instalador y [" + nSalParte + "] fragmentos creados con exito.";
                    |PINTA 2225, aAlfa1;
               |FINSI;

          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Paquete;
     |PINTA 2225, "Agrupando en formato 'tar'";
     aDesPaq = aDesPaq + aInsPaq;
     aAlfa1 = aDesPaq + ".tar " + ".";
     aAlfa2 = aOriPaq;
     |ATAR aAlfa1, aAlfa2;
     aAlfa1 = "rb  " + aDesPaq + ".tar";
     |FABRE aAlfa1;
     |SI FSalida ] 0;
          |FCIERRA FSalida;
          |PINTA 2225, "Comprimiendo en formato 'gzip'";
          aAlfa2 = aDesPaq + ".tar";
          |COMPRIME aAlfa2;
          aAlfa1 = "rb  " + aDesPaq + ".tgz";
          |FABRE aAlfa1;
          |SI FSalida ] 0;
               |FCIERRA FSalida;
               aAlfa1 = aDesPaq + ".tar";
               |FBORRA aAlfa1;
               |SI #0#3 = "S";
                    |HAZ Fragmentos;
               |SINO;
                    nSalParte = 1;
               |FINSI;
          |SINO;
               aAlfa1 = "0000No se pudo crear [" + aDesPaq + ".tgz]";
               |MENAV aAlfa1;
               nSwCopia = 1;
          |FINSI;
     |SINO;
          aAlfa1 = "0000No se pudo crear [" + aDesPaq + "]";
          |MENAV aAlfa1;
          nSwCopia = 1;
     |FINSI;
|FPRC;

|PROCESO CreaDirectorio;
     nSwCopia = 0;
     |MKDIR aCreaDir;
     |SI FSalida ! 0;
          aAlfa1 = "0000Error creando [" + aCreaDir + "] ...";
          |MENAV aAlfa1;
          nSwCopia = 1;
     |FINSI;
|FPRC;

|PROCESO ejec;
     |SI #0#10 = "S";
          |DBASE aDBase;
          aDTempo = "tempo";
          aDTrab = aDBase + aDTempo;
          aAlfa1 = "";
          |DIRECTORIO aAlfa1;
          aAlfa1 = aAlfa1 + "bin/agirm -r " + aDTrab;
          |SYSTEM aAlfa1;
          |RMDIR aDTrab;
     |FINSI;


     aAlfa1 = #0#0;
     |QBF aAlfa1;
     aAlfa2 = aAlfa1 % -101;
     |SI aAlfa2 ! "/";
          aAlfa1 = aAlfa1 + "/";
          #0#0 = aAlfa1;
          |PINTA #0#0;
     |FINSI;
     aAlfa1 = #0#1;
     |QBF aAlfa1;
     aAlfa2 = aAlfa1 % -101;
     |SI aAlfa2 ! "/"; |Y aAlfa2 ! "\";
          aAlfa1 = aAlfa1 + "/";
          #0#1 = aAlfa1;
          |PINTA #0#1;
     |FINSI;
     |DBASE aDBase;
     aDTempo = "tempo";
     ||---- Crea Directorio Trabajo.
     aDTrab = aDBase + aDTempo;
     aCreaDir = aDTrab;  |HAZ CreaDirectorio; |SI nSwCopia ! 0; |ACABA; |FINSI;


     ||---- Monta Destino Fichero. Temporal
     aDesPaq = aDBase + aDTempo + "/";
     ||---- Monta Origen Ficheros.
     aOriPaq = #0#0;
     |QBF aOriPaq;
     ||---- Monta Nombre Fichero Destino de Tar.
     aInsPaq = #0#2;
     |QBF aInsPaq;
     ||---- Montar Destino Final Fichero Creado.
     aDirPaq = #0#1;
     |QBF aDirPaq;
     aDirPaq = aDirPaq + aInsPaq;
     ||---- Ejecuta Montaje Paquete.

     |SI #dspaquet#8 = "S";        ||Carpeta entera
          nSwDatos = 0;
          aDirectorio = aOriPaq + "*.*";
          |_PDIR aDirectorio;
          |PARA; |SI FSalida ] 0; |HACIENDO;
               |SI FSalida ] 0;
                    nSwDatos = 1;
               |SINO;
                    nSwDatos = 0;
               |FINSI;
               |SAL;
          |SIGUE;
          |SI nSwDatos = 1;
               |HAZ Paquete;
               |SI nSwCopia = 0;
                    |HAZ ElTar;
               |FINSI;
          |SINO;
               aMensaje = "0000No se ha encontrado ningun fichero.";
               |MENAV aMensaje;
          |FINSI;
          |HAZ Borralo;
     |SINO;                  ||Solo ficheros segun comodin

          nSwDatos = 0;
          ||---- Crea Directorio Trabajo copia ficheros comodin.
          aDComodin = "tempo2";
          aDTrab2 = aDBase + aDComodin + "/";
          aCreaDir = aDTrab2;  |HAZ CreaDirectorio; |SI nSwCopia ! 0; |ACABA; |FINSI;
          aComodin = #dspaquet#9;
          |QBF aComodin;

          |QUE_SISTEMA aSistema;
          |QBF aSistema;
          |SI aSistema = "ESDOS";
               aDirectorio = aOriPaq + "*";
               |_PDIR aDirectorio;
               |PARA; |SI FSalida ] 0; |HACIENDO;
                    aNomSin = aDirectorio - aOriPaq;
                    aMinuscula = aNomSin < aNomSin;
                    aComMinus = aComodin < aComodin;
                    aComMinus = aComMinus - "*";
                    aAuxMinus = aMinuscula - aComMinus;
                    |SI aAuxMinus ! aMinuscula;
                         aFicOri = aDirectorio;
                         aFicDes = aDTrab2 + aNomSin;
                         |COPIA_FICHERO aFicOri, aFicDes;
                         nSwDatos = 1;
                    |FINSI;
                    |_SDIR aDirectorio;
               |SIGUE;
          |SINO;
               aDirectorio = aOriPaq + aComodin;
               |_PDIR aDirectorio;
               |PARA; |SI FSalida ] 0; |HACIENDO;
                    aNomSin = aDirectorio - aOriPaq;
                    aFicOri = aDirectorio;
                    aFicDes = aDTrab2 + aNomSin;
                    |COPIA_FICHERO aFicOri, aFicDes;
                    nSwDatos = 1;
                    |_SDIR aDirectorio;
               |SIGUE;
          |FINSI;

          |SI nSwDatos = 1;
               aOriPaq = aDTrab2;
               |HAZ Paquete;
               |SI nSwCopia = 0;
                    |HAZ ElTar;
               |FINSI;
          |SINO;
               aMensaje = "0000No se ha encontrado ningun fichero.";
               |MENAV aMensaje;
          |FINSI;
          |HAZ Borralo;
     |FINSI;
|FPRC;

|PROCESO compro; |TIPO 0;
     |SI #0#3 = "N";
          |C_M #0#4 , 1 , "N";
          |C_M #0#5 , 1 , "N";
     |SINO;
          |C_M #0#4 , 1 , "S";
          |C_M #0#5 , 1 , "S";
     |FINSI;
|FPRC;

|PROCESO copia;
    dire = "";
    |DIRECTORIO dire;
    aAlfa5 = dire + "bin/agcopiaa " + aAlfa1 + " " + "A: ";
    |SYSTEM aAlfa5;
|FPRC;

|PROGRAMA;
     |DBASS aBase;
     |QBF aBase;
     aControl = aBase + "empaqueta.txt";

     aUsuario = Usuario % 810;
     |XABRE "U", aControl, nHandle;
     |SI FSalida ] 0;
          nSwAutorizado = 0;
          |PARA; |SI; |HACIENDO;
               |XLEE nHandle, 250, aNombre;
               |SI FSalida < 0;
                    |SAL;
               |FINSI;
               |QBF aNombre;
               aNomMin = aNombre < aNombre;
               aUsuMin = aUsuario < aUsuario;
               |SI aNomMin = aUsuMin;
                    nSwAutorizado = 1;
                    |SAL;
               |FINSI;
          |SIGUE;
          |XCIERRA nHandle;
     |SINO;
          nSwAutorizado = 0;
     |FINSI;

     |SI nSwAutorizado = 0;
          |HAZ SacaMensaje;
          |ACABA;
     |FINSI;

     |CLS;
     |CABEZA "Exportacion Ficheros";
     |PINPA #0#dspaquet;
     |ABRE #dspaquet;
     |ENCAMPO #6#dspaquet;    ||Introduce codigo del paquete a crear
     |SI FSalida ! 1;|Y FSalida ! 7;    || escape o Ctrl-C
          |LEE 101#dspaquet.=;
          |SI FSalida ! 0;    ||Si no existe previamente, ponemos el directorio de trabajo
               aAlfa1 = "";
               |DIRECTORIO aAlfa1;
               #dspaquet#0 = aAlfa1;
               |GRABA 020#dspaquet.b;
          |FINSI;
          |PINDA #0#dspaquet;
          |ENDATOS #1#dspaquet;
          |SI FSalida = 0;
               |HAZ ejec;
               |CONFI "0000SGuardar configuracion: ";
               |SI FSalida = 0;
                    |GRABA 020#dspaquet.a;
               |SINO;
                    |BORRA 020#dspaquet.a;
               |FINSI;
          |FINSI;
          |LIBERA #dspaquet;
     |FINSI;
     |CIERRA #dspaquet;
|FPRO;

|PROCESO PaqueteBlanco; |TIPO 7;
     aAlfa1 = #dspaquet#1;
     |QBF aAlfa1;
     |SI aAlfa1 = "";
          |HAZ ElBrowse;
     |FINSI;
|FPRC;

|PROCESO Browse; |TIPO 0;     ||Selector de fichero de Windows
     |SI FSalida ! 10; |ACABA; |FINSI;
     |HAZ ElBrowse;
|FPRC;

|PROCESO ElBrowse;
     aAlfa = "D" + #0#1;
     |BROWSE aAlfa;
     aAlfa = (aAlfa % 270) + "\"; |QBF aAlfa;
     #0#1 = aAlfa;
     |PINTA #0#1;
     |ERROR;
|FPRC;

|PROCESO Comodin; |TIPO 0;
     |SI #dspaquet#8 = "S";
          |C_M #dspaquet#9, 1, "N";
          |C_V #dspaquet#9, 1, "N";
          |PINTA 1336, "                                ";
     |SINO;
          |C_M #dspaquet#9, 1, "S";
          |C_V #dspaquet#9, 1, "S";
          |PINTA 1336, "Comodin Seleccion:";
     |FINSI;
|FPRC;


|PROCESO SacaMensaje;
     |PUSHV 0501 2327;
          |PINPA #0#dstool01;
          aMensaje = "El usuario [" + aUsuario + "] no dispone del acceso adecuado.";
          |PINTA 1107, aMensaje;
          aMensaje = "Incluya " + &34 + aUsuario + &34 + " en el fichero: ";
          |PINTA 1307, aMensaje;

          aIPRemoto = "";
          |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;
          |SI aIPRemoto ! "";
               aMensaje = &34 + aControl + &34 + " del servidor.";
          |SINO;
               aMensaje = &34 + aControl + &34 + " de su PC.";
          |FINSI;
          |PINTA 1407, aMensaje;

          |PINTA 1607, "Con cualquier editor de texto estandar.";
          |PAUSA;
     |POPV;
|FPRC;
