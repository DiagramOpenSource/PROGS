|FICHEROS;
     dstray01 #0;
     dstray02 #1,MANTE;
|FIN;

|VARIABLES;
{10} dstray = "";
     aUsudest = "";
     aUsuario = "";
     aTodos = "";
     nEnviado = 0;
     fHora = "";
     fFecha = @;

     nro_opcion=1;
     top_men = 0;
     mayor = 0;
     Pos = 0;
     buf_men = 0;
     buf_usr = 0;
     ele_usr = 0;
     para1 = 0;
     alfa1 = "";
     alfa2 = "";
     dbase = ""

|FIN;

|| --------------------------- RUTINAS PARA LIMITES DEL BUCLE ---------
|RUTINA inferior;
||     #1#0 = 000;
     #1#1 = "          ";
|FRUT;

|RUTINA superior;
     ||#1#0 = 999;
     #1#1 = "ZZZZZZZZZZ";
|FRUT;
|| ----------------------- FIN DE RUTINAS BUCLE ---------------------


|| ---------- montamos un lineal con los usuarios ---------------------
|PROCESO menu_opcion;
     |ABRET #1;
     |ENTLINEAL #2#1, 1, 4, 20, 1, inferior, superior;
     |CIERRAT #1;

|FPRC;
||----------- fin de la rutina que pinta el lineal con los usuarios ----


||--------- cargamos los usuarios -------------------------------------
|RUTINA carga_inicial;
     aUsuario      = Usuario % 810;  |QBF aUsuario; ||Este es el usuario actual

     mayor = -1;
     Pos = -1;
|DIMENSIONA 1000, buf_men, 0; ||Creamos un buffer para almacenar datos
|NOME_DAT #1 Usuario;
|DELINDEX #1;
|ABRET #1;
     |DIRECTORIO dbase;
     ||dbase = "u/tutorial/";
     alfa1 = dbase + "dev/usr/ds_sys.psw";
|LEESECUENCIAL alfa1, buf_usr, ele_usr, 0, 0;
|PARA para1 = 0; |SI para1 < ele_usr; |HACIENDO para1 = para1 + 1;
     |DEL_BUF buf_usr, para1, alfa1; ||saca el contenido de alfa1 del buffer
     |DDEFECTO #1; ||Carga los valores por defecto definidos en el def
          ||ABRE #0;
          #1#0 = para1 + 1;               || nro.opcion
          #1#1 = alfa1 % 410;             || nombre
          #1#2 = alfa1 % 1420;            || clave
          #1#3 = alfa1 % 3401;            || tipo
          #1#4 = (A\alfa1 % 103);         || nro.usuario
          |SI aTodos = "S";
               #1#5 = "S";
          |SINO;
               #1#5 = "N";
          |FINSI;


     |GRABA 020#1n;
     |SI FSalida = 0;
             alfa2 = ("000" + #1#0) % -103;
             alfa1 = alfa2 + "-" + #1#1 + "-" + (("0000" + #1#2) % -104);

          |SI mayor < #1#4;     mayor = #1#4;  |FINSI;
     |FINSI;
|SIGUE;
|CIERRAT #1;
|FRUT;
|| --------------------------FIN DE RUTINA CARGA USUARIOS ----------------



||------------- Cambia la letra de envio ---------------------------
|PROCESO dstray02;|TIPO 0;
     |SI FSalida = 0;
          |SI #1#5 = "S";
               #1#5 = "N"; ||Al principio si esta vacio lo pondra a 'n' tambien
          |SINO;
               #1#5 = "S";
          |FINSI;
          |GRABA 020#1a;
     |FINSI;
|FPRC;
|| ------------------------------------------------------------------



|| ------------------------- Proceso que envia los mensajes --------
|PROCESO envia;
     dstray2 = #1#1;
     |QBF dstray2;
     |ESPECIFICA "DSTRAY", dstray;   ||comando para enviar
     nEnviado = 1;
|FPRC;


|DEFBUCLE envia;
     #1#1;
     5;
       1, "S";
     999, "S";
     ;
     NULL, envia;
|FIN:

|PROGRAMA;
     |CLS;
     |CABEZA " Envio de avisos a usuarios.";
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
     aUsuario      = Usuario % 810;  |QBF aUsuario; ||Este es el usuario actual
     |HORA fHora;
     fFecha = ~;

          dstray1 = "0";
          dstray3 = "4";
          dstray4 = #0#0;
          dstray5 = #0#1;
          dstray6 = #0#2;
          dstray7 = "Mensaje enviado por: " + aUsuario +"-" + fHora + "/" + fFecha;

          |CONFI "0000S    ¨Quiere marcar todos los usuarios?";
          |SI FSalida = 0; ||Si dice que si
                aTodos = "S";
          |FINSI;
          |HAZ carga_inicial;
          |PINPA #0#1;
          |LIBERA #1;
          |HAZ menu_opcion;
          |BUCLE envia;    ||Este bucle recorre el lineal y envia segun 'S'
          |SI nEnviado  = 1;
               |MENAV "0000 El mensaje ha sido enviado ";
          |FINSI;
     |FINSI;
|FPRO;
