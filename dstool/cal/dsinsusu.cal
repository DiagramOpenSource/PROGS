|FICHEROS;
     :/agitool/def/dsinsusu #0;
     :/agitool/def/dsinsw01;
     :/agitool/def/agiuser1 #1;
     :/agitool/def/dsw00001 #90;

|| DSPREVEN
     dsprem02@ #902;
     dsprem03@ #903;
     dsprem04@ #904;
     maesm011@ #911;
     maesm035@ #935;
     maesm036@ #936;
     maesm037@ #937;

     ctrlfilt@ #900;

     pycm0015@;
     ctrlspas@;

|| PI

     dsficher@;
     pntm0001@;
     pntm0002@;
     pntm0003@;
     pntm0004@;

|| Gecochek

     gecom011@;
     gecom014@;
     gecom019@;
|FIN;

|VARIABLES;
     aBase        = "";
     aBaseSpa     = "";
     aDirGrupo    = "";
     aDirUsuar    = "";
     aDirFichero  = "";
     aFichero     = "";
     aDestino     = "";
     aAbre        = "";
     aAlfa        = "";
     aAlfa1       = "";
     aAlfa2       = "";
     aAlfa3       = "";
     aAlfa4       = "";
     aCaracter    = "";
     aLong        = "";
     aPrograma    = "";
     aQuitado     = "";
     aParte1      = "";
     aParte2      = "";
     aParte3      = "";
     aParametro   = "";
     aMas         = "";
     aPathDSOPORTE = "";
     aDefChek      = "";
     aPathChek     = "";
     aZonaAdmi     = "";
     aFic          = "";
     aBaseFiltro   = "";
     aCadena       = "";
     aAplicacion   = "";
     aInd          = "";
     aBaseConta    = "";
     aBaseComer    = "";
     aCod          = "";
     aBass         = "";

     nPrevencion   = 0;
     nPI           = 0;
     nSwHay        = 0;
     nHandle       = 0;
     nHandle2      = 0;
     nContador90   = 0;
     nCaracter     = 0;
     nLong         = 0;
     nBueno        = 0;
     nPos          = 0;
     nQuita        = 0;
     nDesdeGrupo   = 0;
     nHastaGrupo   = 0;
     nSwDsMen      = 0;
     nSuperUsuario = 0;
     nHay          = 0;
     nPasada       = 0;
     nTablasTotal  = 0;
     nBarra        = 0;
     nVerifi       = 0;
     nSoloEmpresa  = 0;
     nSPA          = 0;
     nHayColab     = 0;
     nEmpresa      = 0;
     nUsuario      = 0;
     nNumEmp       = 0;
     nNumDef       = 0;
     nContador     = 0;
     nCampoCli     = 0;
     nPrimero      = 0;
     nHayLabor     = 0;
     nPRMLabor     = 0;
     nArea         = 0;
     nNivel        = 0;
     nGraba        = 0;
     nNume         = 0;
|FIN;

|PROCESO pycm0015;
     |SI #pycm0015@#0 = "     ";  |ACABA;  |FINSI;
     |SI #pycm0015@#0 = "00   ";  |ACABA;  |FINSI;

     |SI nPasada = 0;
         |SI #900#6 ! - 1;  nTablasTotal = nTablasTotal + 1;  |FINSI;
         |SI #900#7 ! - 1;  nTablasTotal = nTablasTotal + 1;  |FINSI;
         |SI #900#8 ! - 1;  nTablasTotal = nTablasTotal + 1;  |FINSI;

         nHayColab = 1;

         |ACABA;
     |FINSI;

     |SI #900#6 ! -1;
         |SI #900#9 ! -1;
             aAlfa = #900#1 + "+002#-000";    |QBT aAlfa;
             aAlfa = aAlfa + &13 + &10;
             |XGRABA nHandle, aAlfa;

             aAlfa = (("000" + #900#9) % -103) + "=" + &34 + aZonaAdmi + &34 + &13 + &10;
             |XGRABA nHandle, aAlfa;

             aAlfa = (("000" + #900#6) % -103) + "=" + &34 + #pycm0015@#1 + &34 + &13 + &10;
             |XGRABA nHandle, aAlfa;
         |SINO;
             aAlfa = #900#1 + "+001#-000";    |QBT aAlfa;
             aAlfa = aAlfa + &13 + &10;
             |XGRABA nHandle, aAlfa;

             aAlfa = (("000" + #900#6) % -103) + "=" + &34 + #pycm0015@#1 + &34 + &13 + &10;
             |XGRABA nHandle, aAlfa;
         |FINSI;

         nHayColab = 1;
     |FINSI;

     |SI #900#7 ! -1;
         |SI #900#9 ! -1;
             aAlfa = #900#1 + "+002#-000";    |QBT aAlfa;
             aAlfa = aAlfa + &13 + &10;
             |XGRABA nHandle, aAlfa;

             aAlfa = (("000" + #900#9) % -103) + "=" + &34 + aZonaAdmi + &34 + &13 + &10;
             |XGRABA nHandle, aAlfa;

             aAlfa = (("000" + #900#7) % -103) + "=" + &34 + #pycm0015@#1 + &34 + &13 + &10;
             |XGRABA nHandle, aAlfa;
         |SINO;
             aAlfa = #900#1 + "+001#-000";    |QBT aAlfa;
             aAlfa = aAlfa + &13 + &10;
             |XGRABA nHandle, aAlfa;

             aAlfa = (("000" + #900#7) % -103) + "=" + &34 + #pycm0015@#1 + &34 + &13 + &10;
             |XGRABA nHandle, aAlfa;
         |FINSI;

         nHayColab = 1;
     |FINSI;

     |SI #900#8 ! -1;
         |SI #900#9 ! -1;
             aAlfa = #900#1 + "+002#-000";    |QBT aAlfa;
             aAlfa = aAlfa + &13 + &10;
             |XGRABA nHandle, aAlfa;

             aAlfa = (("000" + #900#9) % -103) + "=" + &34 + aZonaAdmi + &34 + &13 + &10;
             |XGRABA nHandle, aAlfa;

             aAlfa = (("000" + #900#8) % -103) + "=" + &34 + #pycm0015@#1 + &34 + &13 + &10;
             |XGRABA nHandle, aAlfa;
         |SINO;
             aAlfa = #900#1 + "+001#-000";    |QBT aAlfa;
             aAlfa = aAlfa + &13 + &10;
             |XGRABA nHandle, aAlfa;

             aAlfa = (("000" + #900#8) % -103) + "=" + &34 + #pycm0015@#1 + &34 + &13 + &10;
             |XGRABA nHandle, aAlfa;
         |FINSI;

         nHayColab = 1;
     |FINSI;
|FPRC;

|DEFBUCLE pycm0015;
     #pycm0015@#3002;
     ;
     "     ", #1#1;
     "zzzzz", #1#1;
     ;
     NULL, pycm0015;
|FIN;

|PROCESO maesm035;
     |SI #900#3 = -1;  |Y #900#4 = -1;
         |ACABA;
     |FINSI;

     |SI nPasada = 0;
         nTablasTotal = nTablasTotal + 1;

         |ACABA;
     |FINSI;

     aAlfa = #900#1 + "+001#-000";    |QBT aAlfa;
     aAlfa = aAlfa + &13 + &10;
     |XGRABA nHandle, aAlfa;

     |SI #900#3 ! -1;
         aAlfa = (("000" + #900#3) % -103) + "=" + &34 + #935#1 + &34 + &13 + &10;
     |SINO;
         aAlfa = (("000" + #900#4) % -103) + "=" + &34 + #935#1 + &34 + &13 + &10;
     |FINSI;

     |XGRABA nHandle, aAlfa;
|FPRC;

|DEFBUCLE maesm035;
     #935#1002;
     ;
     #1#1, "     ";
     #1#1, "zzzzz";
     ;
     NULL, maesm035;
|FIN;

|PROCESO maesm036;
     |SI nPasada = 0;
         |SI #900#5 ! -1;
             nTablasTotal = nTablasTotal + 1;
         |FINSI;

         |ACABA;
     |FINSI;

     |SI #900#5 ! -1;
         aAlfa = #900#1 + "+001#-000";    |QBT aAlfa;
         aAlfa = aAlfa + &13 + &10;
         |XGRABA nHandle, aAlfa;

         aAlfa = (("000" + #900#5) % -103) + "=" + &34 + (("000000" + #936#1) % -106) + &34 + &13 + &10;
         |XGRABA nHandle, aAlfa;
     |FINSI;
|FPRC;

|DEFBUCLE maesm036;
     #936#1002;
     ;
     #1#1,      1;
     #1#1, 999999;
     ;
     NULL, maesm036;
|FIN;

|PROCESO maesm037;
     |SI nPasada = 0;
         |SI #900#5 ! -1;
             nTablasTotal = nTablasTotal + 1;
         |FINSI;

         |ACABA;
     |FINSI;

     |SI #900#5 ! -1;
         aAlfa = #900#1 + "+001#-000";    |QBT aAlfa;
         aAlfa = aAlfa + &13 + &10;
         |XGRABA nHandle, aAlfa;

         aAlfa = (("000" + #900#5) % -103) + "=" + &34 + (("000000" + #937#1) % -106) + &34 + &13 + &10;
         |XGRABA nHandle, aAlfa;
     |FINSI;
|FPRC;

|DEFBUCLE maesm037;
     #937#1002;
     ;
     #1#1,      1;
     #1#1, 999999;
     ;
     NULL, maesm037;
|FIN;

|PROCESO Pasada0;
     |SI nSoloEmpresa = 1;
         |BUCLE maesm037;
         |ACABA;
     |FINSI;

     |SI nArea = 89;  |Y nNivel = 1;  |Y #900#1 = "pycm0015";
         nArea  = 0;
         #900#6 = -1;
         #900#7 = -1;
         #900#8 = -1;
     |FINSI;

     |SI nArea ! 89;
         |SI #900#3 = -1;  |Y #900#4 = -1;
             |ACABA;
         |FINSI;

         #900#6 = -1;
         #900#7 = -1;
         #900#8 = -1;
     |SINO;
         |SI #900#6 ! -1;  |O #900#7 ! -1;  |O #900#8 ! -1;
             nHayColab = 0;
             |BUCLE pycm0015;

             |SI nHayColab = 0;
                 nTablasTotal = nTablasTotal + 1;
             |FINSI;

             |ACABA;
         |FINSI;
     |FINSI;

     |SI #900#3 ! -1;  |O #900#4 ! -1;
         nTablasTotal = nTablasTotal + 1;

         |BUCLE maesm035;

         aAlfa = #900#1;  |QBF aAlfa;
         |SI aAlfa = "prevm060";
             |DBASE aBaseSpa;
             aAlfa = aBaseSpa - "ase1016";
             |SI FSalida ! 0;
                 nTablasTotal = nTablasTotal + 1;
             |FINSI;
         |FINSI;
     |FINSI;

     |SI #900#5 ! -1;
         |BUCLE maesm036;
     |FINSI;
|FPRC;

|PROCESO Pasada1;
     |SI nSoloEmpresa = 1;
         |BUCLE maesm037;
         |ACABA;
     |FINSI;

     |SI nArea = 89;  |Y nNivel = 1;  |Y #900#1 = "pycm0015";
         nArea  = 0;
         #900#6 = -1;
         #900#7 = -1;
         #900#8 = -1;
     |FINSI;

     |SI nArea ! 89;
         |SI #900#3 = -1;  |Y #900#4 = -1;
             |ACABA;
         |FINSI;

         #900#6 = -1;
         #900#7 = -1;
         #900#8 = -1;
     |SINO;
         |SI #900#6 ! -1;  |O #900#7 ! -1;  |O #900#8 ! -1;
             nHayColab = 0;
             |BUCLE pycm0015;

             |SI nHayColab = 0;
                 aAlfa = #900#1 + "+001#-000";    |QBT aAlfa;
                 aAlfa = aAlfa + &13 + &10;
                 |XGRABA nHandle, aAlfa;

                 aAlfa = (("000" + #900#6) % -103) + "=" + &34 + "VACIO" + &34 + &13 + &10;
                 |XGRABA nHandle, aAlfa;
             |FINSI;

             |ACABA;
         |FINSI;
     |FINSI;

     |SI #900#3 ! -1;  |O #900#4 ! -1;
         aAlfa = #900#1 + "+001#-000";    |QBT aAlfa;
         aAlfa = aAlfa + &13 + &10;
         |XGRABA nHandle, aAlfa;

         |SI #911#10 = "S";
             |SI #900#4 ! -1;
                 aAlfa = (("000" + #900#4) % -103) + "=" + &34 + aZonaAdmi + &34 + &13 + &10;
             |SINO;
                 |SI #900#3 ! -1;
                     aAlfa = (("000" + #900#3) % -103) + "=" + &34 + #911#11 + &34 + &13 + &10;
                 |FINSI;
             |FINSI;
         |FINSI;

         |SI #911#10 = "N";
             |SI #900#3 ! -1;
                 aAlfa = (("000" + #900#3) % -103) + "=" + &34 + #911#11 + &34 + &13 + &10;
             |SINO;
                 aAlfa = (("000" + #900#4) % -103) + "=" + &34 + aZonaAdmi + &34 + &13 + &10;
             |FINSI;
         |FINSI;

         |XGRABA nHandle, aAlfa;

         |BUCLE maesm035;

         aAlfa = #900#1;  |QBF aAlfa;
         |SI aAlfa = "prevm060";
             |DBASE aBaseSpa;
             aAlfa = aBaseSpa - "ase1016";
             |SI FSalida ! 0;
                 aAlfa = "prevm060+001#-000" + &13 + &10;
                 |XGRABA nHandle, aAlfa;

                 aAlfa = "000]" + &34 + "500000" + &34 + &13 + &10;
                 |XGRABA nHandle, aAlfa;
             |FINSI;
         |FINSI;
     |FINSI;

     |SI #900#5 ! -1;
         |BUCLE maesm036;
     |FINSI;
|FPRC;

|PROCESO  ctrlfilt;
     nArea  = #904#3;
     nNivel = #904#4;

     |SI nPasada = 0;
         |HAZ Pasada0;
     |SINO;
         |HAZ Pasada1;
     |FINSI;
|FPRC;

|DEFBUCLE ctrlfilt;
    #900#1002;
    ;
    aAplicacion, "        ";
    aAplicacion, "zzzzzzzz";
    ;
    NULL, ctrlfilt;
|FIN;

|PROCESO CargaPath;
     |DBASS aBaseFiltro;  |QBF aBaseFiltro;
     aLong   = aBaseFiltro % 0;
     nLong   = aLong;
     aCadena = "";
     nBarra  = 0;
     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPos      = (100 * nCaracter) + 1;
           aCaracter = aBaseFiltro % nPos;
           |SI aCaracter = "/";  |O aCaracter = "\";
               nBarra = nBarra + 1;
           |FINSI;

           aCadena = aCadena + aCaracter;

           |SI nBarra = 2;  |SAL;  |FINSI;
     |SIGUE;

     aBaseFiltro = aCadena + "asespas/";

     |PATH_DAT #900 aBaseFiltro;
     |PATH_DAT #ctrlspas@#aBaseFiltro#;

     aFic = aBase + "dspreven/fich/";
     |PATH_DAT #911 aFic;
     |PATH_DAT #935 aFic;
     |PATH_DAT #936 aFic;
     |PATH_DAT #937 aFic;

     |DBASS aAlfa;  |QBF aAlfa;
     aAlfa = aAlfa % -204;
     nSPA  = aAlfa;
     |SI nSPA = 0;  nSPA = 9999;  |FINSI;

     |ABRE #ctrlspas@;
     #ctrlspas@#0 = nSPA;
     |LEE 000#ctrlspas@.=;
     |SI FSalida ! 0;
         |DDEFECTO #ctrlspas@;
     |FINSI;
     |CIERRA #ctrlspas@;

     aFic = aBase + "dspreven/fich/" + (("00000" + #ctrlspas@#2) % -105) + "/";
     |PATH_DAT #pycm0015@#aFic#;
|FPRC;

|PROCESO Filtra;
     nTablasTotal = 0;
     nPasada      = 0;
     |BUCLE ctrlfilt;

     nHay  = 1;
     aAbre = aDirUsuar + aAplicacion + ".prm";
     |XABRE "E", aAbre, nHandle;
     |SI FSalida < 0;
         nHay = 0;
     |FINSI;

     aAbre = aDirUsuar + aAplicacion + ".prm";
     |XABRE "AB", aAbre, nHandle;

     |SI nHay = 0;
         aAlfa = "-0000" + &13 + &10;
         |XGRABA nHandle, aAlfa;
     |FINSI;

     aAlfa = "-" + (("0000" + nTablasTotal) % -104) + &13 + &10;
     |XGRABA nHandle, aAlfa;

     nPasada = 1;  |BUCLE ctrlfilt;

     |XCIERRA nHandle;
|FPRC;

|PROCESO FiltrosPrevencion;
     #911#0 = #1#1;
     |LEE 000#911=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI #911#12 = "S";  |ACABA;  |FINSI;

     aZonaAdmi = #911#11;
     aMas      = aBase + "dsoporte/def/dsprem02.mas";
     aAlfa1    = aZonaAdmi % 101;  #902#0 = aAlfa1;
     aAlfa1    = aZonaAdmi % 202;  #902#1 = aAlfa1;
     aAlfa1    = aZonaAdmi % 402;  #902#2 = aAlfa1;
     |LEE 000#902=;
     |SI FSalida = 0;
         aZonaAdmi = ("00000" + #902#16) % -105;
     |FINSI;

     nSoloEmpresa = 0;

     |ABRE #937;
     #937#0 = #1#1;
     #937#1 = 1;
     |LEE 000#937];
     |SI FSalida = 0;  |Y  #937#0 = #1#1;
         nSoloEmpresa = 1;
     |FINSI;
     |CIERRA #937;

     aAplicacion = "contagen";  |HAZ Filtra;
     aAplicacion = "dscomer9";  |HAZ Filtra;
     aAplicacion = "dscarter";  |HAZ Filtra;
     aAplicacion = "dspreven";  |HAZ Filtra;
|FPRC;

|PROCESO pntm0002;
     |SI nPasada = 0;
         nNumEmp = nNumEmp + 1;
         |ACABA;
     |FINSI;

     aCaracter = "#";

     |SI #pntm0002@#3 = "N";  |Y #pntm0002@#5 = "N";
      |Y #pntm0002@#4 = "N";  |Y #pntm0002@#6 = "S";
         aCaracter = "@";
     |FINSI;

     |SI #pntm0002@#3 = "S";  |Y #pntm0002@#5 = "N";
      |Y #pntm0002@#4 = "N";
         aCaracter = "A";
     |FINSI;

     |SI #pntm0002@#3 = "N";  |Y #pntm0002@#5 = "S";
      |Y #pntm0002@#4 = "N";
         aCaracter = "B";
     |FINSI;

     |SI #pntm0002@#3 = "N";  |Y #pntm0002@#5 = "N";
      |Y #pntm0002@#4 = "S";
         aCaracter = "C";
     |FINSI;

     |SI #pntm0002@#3 = "S";  |Y #pntm0002@#5 = "S";
      |Y #pntm0002@#4 = "N";
         aCaracter = "D";
     |FINSI;

     |SI #pntm0002@#3 = "S";  |Y #pntm0002@#5 = "N";
      |Y #pntm0002@#4 = "S";
         aCaracter = "E";
     |FINSI;

     |SI #pntm0002@#3 = "N";  |Y #pntm0002@#5 = "S";
      |Y #pntm0002@#4 = "S";
         aCaracter = "F";
     |FINSI;

     nGraba = nGraba + 1;
     |SI nGraba > 9999;
         |ERROR10;
         |ACABA;
     |FINSI;

     aAlfa = aMas + "+001" + aCaracter + "-000";
     aAlfa = aAlfa + &13 + &10;
     |XGRABA nHandle, aAlfa;

     aAlfa = (("000" + #dsficher@#4) % -103) + "=" + &34 + (("00000" + #pntm0002@#1) % -105) + &34 + &13 + &10;
     |XGRABA nHandle, aAlfa;
|FPRC;

|DEFBUCLE pntm0002;
     #pntm0002@#1002;
     ;
     nUsuario, 00001;
     nUsuario, 99999;
     ;
     NULL, pntm0002;
|FIN;

|PROCESO dsficher_Aplica;
     #pntm0004@#0 = #dsficher@#0;
     #pntm0004@#1 = #dsficher@#1;
     |LEE 000#pntm0004@.=;
     |SI FSalida ! 0;  |DDEFECTO #pntm0004@;  |FINSI;

     |SI #pntm0004@#3 = "N";
         |ACABA;
     |FINSI;

     nPasada  = 1;

     |SI nPrimero = 0;  |Y aAplicacion ! "basica";
         nCampoCli    = #dsficher@#4;
         aMas         = "agifigen";

         #dsficher@#4 = 0;
         |BUCLE pntm0002;

         #dsficher@#4 = nCampoCli;
     |FINSI;

     aMas = #dsficher@#1 - ".mas";  |QBF aMas;

     |BUCLE pntm0002;
     nPrimero = 1;
|FPRC;

|DEFBUCLE dsficher_Aplica;
     #dsficher@#1002;
     ;
     #dsinsw01#0, "            ";
     #dsinsw01#0, "zzzzzzzzzzzz";
     ;
     NULL, dsficher_Aplica;
|FIN;

|PROCESO dsinsw01;
     #pntm0003@#0 = #pntm0001@#0;
     #pntm0003@#1 = #dsinsw01#0;
     |LEE 000#pntm0003@.=;
     |SI FSalida = 0;  |ACABA;  |FINSI;

     aAplicacion = #dsinsw01#0;  |QBF aAplicacion;

     nHay  = 1;
     aAbre = aDirUsuar + aAplicacion + ".prm";
     |XABRE "E", aAbre, nHandle;
     |SI FSalida < 0;
         nHay = 0;
     |FINSI;

     aAbre = aDirUsuar + aAplicacion + ".prm";
     |XABRE "AB", aAbre, nHandle;

     |SI nHay = 0;
         aAlfa = "-0000" + &13 + &10;
         |XGRABA nHandle, aAlfa;
     |FINSI;

     nNumEmp = 0;
     nPasada = 0;  |BUCLE pntm0002;

     nNume   = nNumEmp;
     nNumEmp = #dsinsw01#1 * nNumEmp;

     |SI nNumEmp > 9999;
         aAlfa = "0000Se ha excedido el numero de tablas a restringir en un PRM";
         |MENAV aAlfa;

         aAlfa = "0000Usuario " + #agiuser1#1;  |QBF aAlfa;
         aAlfa = aAlfa + " Aplicacion " + aAplicacion; |QBF aAlfa;
         |MENAV aAlfa;

         aAlfa = "0000Tablas " + #dsinsw01#1;
         aAlfa = aAlfa + " Empresas " + nNume;
         aAlfa = aAlfa + " Total " + nNumEmp;
         aAlfa = aAlfa + " No se puede exceder de 9.999 en total";
         |MENAV aAlfa;

         aAlfa = "0000Se ha restringido a 9.999. Pongase en contacto con su administrador";
         |MENAV aAlfa;

         nNumEmp = 9999;
     |FINSI;

     aAlfa = "-" + (("0000" + nNumEmp) % -104) + &13 + &10;
     |XGRABA nHandle, aAlfa;

     nPrimero = 0;
     nGraba   = 0;
     |BUCLE dsficher_Aplica;

     |XCIERRA nHandle;
|FPRC;

|DEFBUCLE dsinsw01;
     #dsinsw01#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dsinsw01;
|FIN;

|PROCESO pntm0002Conta;
     aCaracter = "#";

     |SI #pntm0002@#3 = "N";  |Y #pntm0002@#5 = "N";
      |Y #pntm0002@#4 = "N";  |Y #pntm0002@#6 = "S";
         aCaracter = "@";
     |FINSI;

     |SI #pntm0002@#3 = "S";  |Y #pntm0002@#5 = "N";
      |Y #pntm0002@#4 = "N";
         aCaracter = "A";
     |FINSI;

     |SI #pntm0002@#3 = "N";  |Y #pntm0002@#5 = "S";
      |Y #pntm0002@#4 = "N";
         aCaracter = "B";
     |FINSI;

     |SI #pntm0002@#3 = "N";  |Y #pntm0002@#5 = "N";
      |Y #pntm0002@#4 = "S";
         aCaracter = "C";
     |FINSI;

     |SI #pntm0002@#3 = "S";  |Y #pntm0002@#5 = "S";
      |Y #pntm0002@#4 = "N";
         aCaracter = "D";
     |FINSI;

     |SI #pntm0002@#3 = "S";  |Y #pntm0002@#5 = "N";
      |Y #pntm0002@#4 = "S";
         aCaracter = "E";
     |FINSI;

     |SI #pntm0002@#3 = "N";  |Y #pntm0002@#5 = "S";
      |Y #pntm0002@#4 = "S";
         aCaracter = "F";
     |FINSI;

     aAlfa = aMas + "-000" + aCaracter + "-000" + &13 + &10;
     |XGRABA nHandle, aAlfa;
|FPRC;

|DEFBUCLE pntm0002Conta;
     #pntm0002@#1002;
     ;
     nUsuario, 00001;
     nUsuario, 99999;
     ;
     NULL, pntm0002Conta;
|FIN;

|PROCESO MeteDef;
     |BUCLE pntm0002Conta;
|FPRC;

|PROCESO TrataContagen;
     nNumDef = 0;

     |DBASS aBaseConta;  |QBF aBaseConta;

     aAbre = aBaseConta + "contagen/fich/contagen.dir";
     |XABRE "U", aAbre, nHandle2;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     nContador = 0;
     |PARA;  |SI;  |HACIENDO;
          |XLEE nHandle2, 250, aAlfa;
          |SI FSalida < 0;  |SAL;  |FINSI;

          nContador = nContador + 1;

          |SI nContador = 1;  aMas = aAlfa;  |FINSI;

          |SI nContador = 4;
              aInd = aAlfa % 101;

              |SI aInd = "1";  |O aInd = 2;
                  |SI nPasada = 0;
                       nNumDef = nNumDef + 1;
                  |FINSI;

                  |SI aMas ! "canempre";  |Y aMas ! "canempr1";  |Y aMas ! "canempr2";
                       |SI nPasada = 1;
                           |HAZ MeteDef;
                       |FINSI;
                  |FINSI;
              |FINSI;
          |FINSI;

          |SI nContador = 5;  nContador = 0;  |FINSI;
     |SIGUE;

     |XCIERRA nHandle2;
|FPRC;

|PROCESO MeteContagen;
     #pntm0003@#0 = #pntm0001@#0;
     #pntm0003@#1 = "contagen";
     |LEE 000#pntm0003@.=;
     |SI FSalida = 0;  |ACABA;  |FINSI;

     nPasada = 0;  |HAZ TrataContagen;

     aAbre = aDirUsuar + "contagen.prm";
     |XABRE "WB", aAbre, nHandle;

     aAlfa = "-0000" + &13 + &10;
     |XGRABA nHandle, aAlfa;

     aAlfa = "-" + (("0000" + nNumDef) % -104) + &13 + &10;
     |XGRABA nHandle, aAlfa;

     #dsficher@#4 = 2;
     aMas = "canempre";  nPasada = 1;  |BUCLE pntm0002;
     aMas = "canempr1";  nPasada = 1;  |BUCLE pntm0002;
     aMas = "canempr2";  nPasada = 1;  |BUCLE pntm0002;

     nPasada = 1;  |HAZ TrataContagen;

     |XCIERRA nHandle;
|FPRC;

|PROCESO TrataCartera;
     nNumDef = 0;

     |DBASS aBaseConta;  |QBF aBaseConta;

     aAbre = aBaseConta + "dscarter/fich/dscarter.dir";
     |XABRE "U", aAbre, nHandle2;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     nContador = 0;
     |PARA;  |SI;  |HACIENDO;
          |XLEE nHandle2, 250, aAlfa;
          |SI FSalida < 0;  |SAL;  |FINSI;

          nContador = nContador + 1;

          |SI nContador = 1;  aMas = aAlfa;  |FINSI;

          |SI nContador = 4;
              aInd = aAlfa % 101;

              |SI aInd = "1";  |O aInd = 2;
                  |SI nPasada = 0;
                       nNumDef = nNumDef + 1;
                  |FINSI;

                  |SI aMas ! "dscam000";  |Y aMas ! "dscam005";
                       |SI nPasada = 1;
                           |HAZ MeteDef;
                       |FINSI;
                  |FINSI;
              |FINSI;
          |FINSI;

          |SI nContador = 5;  nContador = 0;  |FINSI;
     |SIGUE;

     |XCIERRA nHandle2;
|FPRC;

|PROCESO MeteCartera;
     #pntm0003@#0 = #pntm0001@#0;
     #pntm0003@#1 = "dscarter";
     |LEE 000#pntm0003@.=;
     |SI FSalida = 0;  |ACABA;  |FINSI;

     nPasada = 0;  |HAZ TrataCartera;

     aAbre = aDirUsuar + "dscarter.prm";
     |XABRE "WB", aAbre, nHandle;

     aAlfa = "-0000" + &13 + &10;
     |XGRABA nHandle, aAlfa;

     aAlfa = "-" + (("0000" + nNumDef) % -104) + &13 + &10;
     |XGRABA nHandle, aAlfa;

     #dsficher@#4 = 0;
     aMas = "dscam000";  nPasada = 1;  |BUCLE pntm0002;
     aMas = "dscam005";  nPasada = 1;  |BUCLE pntm0002;

     nPasada = 1;  |HAZ TrataCartera;

     |XCIERRA nHandle;
|FPRC;

|PROCESO TrataDscomer9;
     nNumDef = 0;

     aAbre = aBaseComer + "dscomer9/fich/dscomer9.dir";
     |XABRE "U", aAbre, nHandle2;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     nContador = 0;
     |PARA;  |SI;  |HACIENDO;
          |XLEE nHandle2, 250, aAlfa;
          |SI FSalida < 0;  |SAL;  |FINSI;

          nContador = nContador + 1;

          |SI nContador = 1;  aMas = aAlfa;  |FINSI;

          |SI nContador = 4;
              aInd = aAlfa % 101;

              |SI aInd = "1";  |O aInd = 2;
                  |SI nPasada = 0;
                       nNumDef = nNumDef + 1;
                  |FINSI;

                  |SI aMas ! "agifa041";  |Y aMas ! "agif0041";  |Y aMas ! "coima040";
                       |SI nPasada = 1;
                           |HAZ MeteDef;
                       |FINSI;
                  |FINSI;
              |FINSI;
          |FINSI;

          |SI nContador = 5;  nContador = 0;  |FINSI;
     |SIGUE;

     |XCIERRA nHandle2;
|FPRC;

|PROCESO MeteDscomer9;
     #pntm0003@#0 = #pntm0001@#0;
     #pntm0003@#1 = "dscomer9";
     |LEE 000#pntm0003@.=;
     |SI FSalida = 0;  |ACABA;  |FINSI;

     |DBASS aBaseComer;  |QBF aBaseComer;

     aAbre = aBaseComer + "dscomer9/fich/dscomer9.dir";
     |XABRE "E", aAbre, nHandle2;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     nPasada = 0;  |HAZ TrataDscomer9;

     aAbre = aDirUsuar + "dscomer9.prm";
     |XABRE "WB", aAbre, nHandle;

     aAlfa = "-0000" + &13 + &10;
     |XGRABA nHandle, aAlfa;

     aAlfa = "-" + (("0000" + nNumDef) % -104) + &13 + &10;
     |XGRABA nHandle, aAlfa;

     #dsficher@#4 = 0;
     aMas = "agifa041";  nPasada = 1;  |BUCLE pntm0002;
     aMas = "agif0041";  nPasada = 1;  |BUCLE pntm0002;
     aMas = "coima040";  nPasada = 1;  |BUCLE pntm0002;

     nPasada = 1;  |HAZ TrataDscomer9;

     |XCIERRA nHandle;
|FPRC;

|PROCESO TrataDsarchi;
     nNumDef = 0;

     aAbre = aBass + "dsarchi/fich/dsarchi.dir";
     |XABRE "U", aAbre, nHandle2;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     nContador = 0;
     |PARA;  |SI;  |HACIENDO;
          |XLEE nHandle2, 250, aAlfa;
          |SI FSalida < 0;  |SAL;  |FINSI;

          nContador = nContador + 1;

          |SI nContador = 1;  aMas = aAlfa;  |FINSI;

          |SI nContador = 4;
              aInd = aAlfa % 101;

              |SI aInd = "1";  |O aInd = 2;
                  |SI nPasada = 0;
                       nNumDef = nNumDef + 1;
                  |FINSI;

                  |SI nPasada = 1;
                      |HAZ MeteDef;
                  |FINSI;
              |FINSI;
          |FINSI;

          |SI nContador = 5;  nContador = 0;  |FINSI;
     |SIGUE;

     |XCIERRA nHandle2;
|FPRC;

|PROCESO MeteDsarchi;
     #pntm0003@#0 = #pntm0001@#0;
     #pntm0003@#1 = "dsarchi";
     |LEE 000#pntm0003@.=;
     |SI FSalida = 0;  |ACABA;  |FINSI;

     |DBASS aBass;  |QBF aBass;

     aAbre = aBass + "dsarchi/fich/dsarchi.dir";
     |XABRE "E", aAbre, nHandle2;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     nPasada = 0;  |HAZ TrataDsarchi;

     aAbre = aDirUsuar + "dsarchi.prm";
     |XABRE "WB", aAbre, nHandle;

     aAlfa = "-0000" + &13 + &10;
     |XGRABA nHandle, aAlfa;

     aAlfa = "-" + (("0000" + nNumDef) % -104) + &13 + &10;
     |XGRABA nHandle, aAlfa;

     #dsficher@#4 = 0;
     aMas = "dsam0000";  nPasada = 1;  |BUCLE pntm0002;

     nPasada = 1;  |HAZ TrataDsarchi;

     |XCIERRA nHandle;
|FPRC;

|PROCESO FiltrosPI;
     nEmpresa = 0;

     #pntm0001@#1 = #agiuser1#1;
     |LEE 000#pntm0001@.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     nUsuario = #pntm0001@#0;
     |BUCLE dsinsw01;

     |HAZ MeteDsarchi;
     |HAZ MeteContagen;
     |HAZ MeteCartera;
     |HAZ MeteDscomer9;
|FPRC;

|PROCESO QuitaParte1;
     |QBF aAlfa1;
     aAlfa1 = aAlfa1 > aAlfa1;

     aAlfa2 = #90#1;   |QBF aAlfa2;
     aAlfa2 = aAlfa2 > aAlfa2;

     aLong     = aAlfa2 % 0;
     nLong     = aLong;
     aPrograma = "";
     nBueno    = 0;
     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPos      = (nCaracter * 100) + 1;
           aCaracter = aAlfa2 % nPos;
           |SI aCaracter = "{"; |O aCaracter = " "; |O aCaracter = ".";
            |O aCaracter = "("; |O aCaracter = ")"; |O aCaracter = ":";
            |O aCaracter = "/"; |O aCaracter = "}"; |O aCaracter = "_";
            |O aCaracter = "-";
               |SI aPrograma = aAlfa1;  nBueno = 1;  |SAL;  |FINSI;
               aPrograma = "";
           |SINO;
               aPrograma = aPrograma + aCaracter;
           |FINSI;
     |SIGUE;

     |SI nBueno = 1;
         aQuitado = #90#1;
         |BORRA 020#90a;
     |FINSI;
     |LIBERA #90;
|FPRC;

|PROCESO CalculaMenu;
     |QBT aQuitado;
     aLong     = aQuitado % 0;
     nLong     = aLong;
     aPrograma = "";
     nBueno    = 0;
     |PARA nCaracter = nLong;  |SI nCaracter ] 1;  |HACIENDO nCaracter = nCaracter - 1;
           nPos      = (nCaracter * 100) + 1;
           aCaracter = aQuitado % nPos;
           |SI aCaracter = ",";  |Y nBueno = 0;
               nBueno = 1;
           |SINO;
               |SI nBueno = 1;
                   aPrograma = aPrograma + aCaracter;
               |FINSI;
           |FINSI;
     |SIGUE;

     |SI aPrograma = "";  nBueno = 0;  |ACABA;  |FINSI;

     aQuitado = aPrograma;
     |QBT aQuitado;
     aLong     = aQuitado % 0;
     nLong     = aLong;
     aPrograma = "";
     |PARA nCaracter = nLong;  |SI nCaracter ] 1;  |HACIENDO nCaracter = nCaracter - 1;
           nPos      = (nCaracter * 100) + 1;
           aCaracter = aQuitado % nPos;
           aPrograma = aPrograma + aCaracter;
     |SIGUE;
     aPrograma = aPrograma + ":";
     nBueno    = 1;
|FPRC;

|PROCESO QuitaParte2;
     aParte1   = #90#1;   |QBT aParte1;
     aParte1   = aParte1 > aParte1;

     aLong     = aPrograma % 0;
     nLong     = aLong;
     nLong     = 100 + (nLong - 1);
     aParte2   = aParte1 % nLong;
     aParte3   = aPrograma % nLong;

     |SI aParte2 = aParte3;
         |BORRA 020#90a;
     |FINSI;
     |LIBERA #90;
|FPRC;

|PROCESO QuitaPrograma;
     |SI nQuita = 0;  |HAZ QuitaParte1;  |FINSI;
     |SI nQuita = 1;  |HAZ QuitaParte2;  |FINSI;
|FPRC;

|DEFBUCLE dsw00001B;
     #90#1;
     ;
     000001;
     999999;
     be;
     NULL, QuitaPrograma;
|FIN;

|PROCESO CreaMenu;
     nSwHay = 1;
     aAlfa1 = #90#1;
     |XGRABA nHandle, aAlfa1;
|FPRC;

|DEFBUCLE dsw00001G;
     #90#1;
     ;
     000001;
     999999;
     ;
     NULL, CreaMenu;
|FIN;

|PROCESO QuitaDelDS;
     |ABRE #90;  |CIERRA #90;  |DELINDEX #90;

     |SI nSwDsMen = 0;
         aAbre = aBase + "ds.men";  |QBT aAbre;
     |SINO;
         aAbre = aDirUsuar + "ds.men";  |QBT aAbre;
     |FINSI;
     nContador90 = 0;

     |ABRE #90;
     |XABRE "U", aAbre, nHandle;
     |SI FSalida ] 0;
         |PARA; |SI; |HACIENDO;
               |XLEE nHandle, 250, aAlfa1;
               |SI FSalida < 0;  |SAL;  |FINSI;

               nContador90 = nContador90 + 1;
               #90#0       = nContador90;
               #90#1       = aAlfa1;
               |GRABA 020#90n;
               |LIBERA #90;
         |SIGUE;
     |FINSI;
     |XCIERRA nHandle;
     |CIERRA #90;

     aAbre = aDirFichero;  |QBT aAbre;
     |XABRE "U", aAbre, nHandle;
     |SI FSalida ] 0;
         |PARA; |SI; |HACIENDO;
               |XLEE nHandle, 250, aAlfa1;
               |SI FSalida < 0;  |SAL;  |FINSI;

               aQuitado = "";
               nQuita   = 0;
               |BUCLE dsw00001B;
               |SI aQuitado ! "";
                   nQuita   = 1;
                   |HAZ CalculaMenu;
                   |SI nBueno = 1;
                       |BUCLE dsw00001B;
                   |FINSI;
               |FINSI;
         |SIGUE;
     |FINSI;
     |XCIERRA nHandle;

     aAbre = aDirUsuar + "ds.men";  |QBT aAbre;
     |XABRE "W", aAbre, nHandle;
     |BUCLE dsw00001G;
     |XCIERRA nHandle;
|FPRC;

|PROCESO BorraUsuarios;
     aDirUsuar = aBase + "dev/usr/" + (("000" + #1#0) % -103) + "/";

     aDirFichero = aDirUsuar + "*";
     |_PDIR aDirFichero;
     |PARA;  |SI FSalida ] 0; |HACIENDO;
          |PULSATECLA;
          aAlfa3 = aDirFichero < "";
          aAlfa4 = aAlfa3 - ".flt";
          |SI aAlfa3 = aAlfa4;
              |FBORRA aDirFichero;
          |FINSI;
          |_SDIR aDirFichero;
     |SIGUE;
|FPRC;

|PROCESO Laboral;
     aAlfa = aDirUsuar + "laboral.prm";
     |XABRE "E", aAlfa, 0;
     |SI FSalida ] 0;  |ACABA;  |FINSI;

     aDefChek  = aBase + "gecochek/def/";
     aPathChek = aBase + "gecochek/fich/";

     aMas      = aDefChek + "gecom014.mas";
     |CARGA_DEF aMas, gecom014@;
     |PATH_DAT #gecom014@#aPathChek#;

     |ABRE #gecom014@;
     |SELECT #2#gecom014@;
     #gecom014@#1 = #agiuser1#1;
     |LEE 000#gecom014@.=;
     |SI FSalida ! 0;
         |CIERRA #gecom014@;
         |DESCARGA_DEF #gecom014@;
         |ACABA;
     |FINSI;
     |CIERRA #gecom014@;

     aMas      = aDefChek + "gecom019.mas";
     |CARGA_DEF aMas, gecom019@;
     |PATH_DAT #gecom019@#aPathChek#;
     |ABRE #gecom019@;
     #gecom019@#0 = #gecom014@#0;
     #gecom019@#1 = 1;
     |LEE 000#gecom019@.];
     |SI FSalida ! 0;  |O #gecom019@#0 ! #gecom014@#0;
         |CIERRA #gecom019@;
         |DESCARGA_DEF #gecom019@;
         |DESCARGA_DEF #gecom014@;
         |ACABA;
     |FINSI;
     |CIERRA #gecom019@;

     aMas      = aDefChek + "gecom011.mas";
     |CARGA_DEF aMas, gecom011@;
     |PATH_DAT #gecom011@#aPathChek#;
     |ABRE #gecom011@;
     #gecom011@#0 = #gecom014@#0;
     |LEE 000#gecom011@.=;
     |SI FSalida ! 0;
         |CIERRA #gecom011@;
         |DESCARGA_DEF #gecom011@;
         |DESCARGA_DEF #gecom019@;
         |DESCARGA_DEF #gecom014@;
         |ACABA;
     |FINSI;
     |CIERRA #gecom011@;

/*   Tiquet 003237/2302
     aCod = ("00000" + #gecom011@#0) % -105;

     |SI #gecom011@#19 ! "S";
         #dsprem02@#0 = #dsprem03@#0;
         #dsprem02@#1 = #dsprem03@#1;
         #dsprem02@#2 = #dsprem03@#2;
         |LEE 000#dsprem02@.=;

         aCod = ("00000" + #dsprem02@#16) % -105;
     |FINSI;

*/
     |SELECT #2#dsprem04@;
     #dsprem04@#5 = #gecom014@#1;
     |LEE 000#dsprem04@.=;

     aCod = #dsprem04@#0 + (("00" + #dsprem04@#1) % -102) + (("00" + #dsprem04@#2) % -102);

     |DESCARGA_DEF #gecom011@;
     |DESCARGA_DEF #gecom019@;
     |DESCARGA_DEF #gecom014@;

     aAbre = aDirUsuar + "laboral.prm";
     |XABRE "WB", aAbre, nHandle;

     aAlfa = "-0000" + &13 + &10;
     |XGRABA nHandle, aAlfa;

     aAlfa = "-0001" + &13 + &10;
     |XGRABA nHandle, aAlfa;

     aAlfa = "nomempre+001#-000" + &13 + &10;
     |XGRABA nHandle, aAlfa;

     aAlfa = "000=" + &34 + aCod + &34 + &13 + &10;
     |XGRABA nHandle, aAlfa;

     |XCIERRA nHandle;
|FPRC;

|PROCESO CreaUsuarios;
     |HAZ BorraUsuarios;

     nSuperUsuario = 0;

     |SELECT #2#904;
     #904#5 = #1#1;
     |LEE 000#904=;
     |SI FSalida = 0;  |Y #904#10 = "S";
         #903#0 = #904#0;
         #903#1 = #904#1;
         #903#2 = #904#2;
         #903#3 = #904#3;
         #903#4 = #904#4;
         |LEE 000#903=;
         |SI FSalida = 0;  |Y #1#2 = 0;
             #1#2          = #903#11;
             nSuperUsuario = 1;
         |FINSI;
     |FINSI;

     aDirGrupo = aBase + "dsoporte/fich/grupos/" + Usuario + "/" + (("0000" + #1#2) % -104) + "/";
     aDirUsuar = aBase + "dev/usr/" + (("000" + #1#0) % -103);  |MKDIR aDirUsuar;
     aDirUsuar = aDirUsuar + "/";
     nSwDsMen  = 0;

     aDirFichero = aDirGrupo + "ds.men";
     |XABRE "E", aDirFichero, nVerifi;
     |SI FSalida ] 0;
         |PULSATECLA;
         aDestino = aDirUsuar + "ds.men";
         |COPIA_FICHERO aDirFichero, aDestino;

         nSwDsMen = 1;
     |FINSI;

     aDirFichero = aDirGrupo + "*";
     |_PDIR aDirFichero;
     |PARA;  |SI FSalida ] 0; |HACIENDO;
          |PULSATECLA;
          aFichero = aDirFichero - aDirGrupo;  |QBF aFichero;
          aFichero = aFichero < aFichero;
          |SI aFichero ! "";  |Y aFichero ! "ds.men";
              |SI aFichero = "ds.txt";
                  |HAZ QuitaDelDS;
                  nSwHay = 1;
              |SINO;
                  |SI nSuperUsuario = 0;
                      aDestino = aDirUsuar + aFichero;
                      |COPIA_FICHERO aDirFichero, aDestino;
                      nSwHay = 1;
                  |SINO;
                      aAlfa = (aFichero % -104) < "";
                      |SI aAlfa = ".men";
                          aDestino = aDirUsuar + aFichero;
                          |COPIA_FICHERO aDirFichero, aDestino;
                          nSwHay = 1;
                      |FINSI;
                  |FINSI;
              |FINSI;
          |FINSI;
          |_SDIR aDirFichero;
     |SIGUE;

     nPRMLabor = nHayLabor;

     |SI nPI ! 0;
         |HAZ FiltrosPI;

         #pntm0003@#0 = #pntm0001@#0;
         #pntm0003@#1 = "laboral";
         |LEE 000#pntm0003@.=;
         |SI FSalida = 0;
             nPRMLabor = 0;
         |FINSI;
     |FINSI;

     |SI nPRMLabor = 1;
         |HAZ Laboral;
     |FINSI;

     |SI nPI ! 0;  |ACABA;  |FINSI;

     |SI nPrevencion ! 0;
         |HAZ FiltrosPrevencion;
     |FINSI;
|FPRC;

|DEFBUCLE agiuser1;
     #1#1;
     2;
     000, nDesdeGrupo;
     999, nHastaGrupo;
     ;
     NULL, CreaUsuarios;
|FIN;

|PROCESO CargaDefPrevencion;
     nPrevencion = 0;

     aMas = aBase + "dspreven/def/maesm011.mas";
     |XABRE "E", aMas, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     aMas = aBase + "dspreven/def/maesm011.mas";
     |CARGA_DEF aMas, maesm011@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aMas = aBase + "dspreven/def/maesm035.mas";
     |CARGA_DEF aMas, maesm035@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #maesm011@;
         |ACABA;
     |FINSI;

     aMas = aBase + "dspreven/def/maesm036.mas";
     |CARGA_DEF aMas, maesm036@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #maesm035@;
         |DESCARGA_DEF #maesm011@;
         |ACABA;
     |FINSI;

     aMas = aBase + "dspreven/def/maesm037.mas";
     |CARGA_DEF aMas, maesm037@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #maesm036@;
         |DESCARGA_DEF #maesm035@;
         |DESCARGA_DEF #maesm011@;
         |ACABA;
     |FINSI;

     aMas = aBase + "dspreven/def/ctrlfilt.mas";
     |CARGA_DEF aMas, ctrlfilt@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #maesm037@;
         |DESCARGA_DEF #maesm036@;
         |DESCARGA_DEF #maesm035@;
         |DESCARGA_DEF #maesm011@;
         |ACABA;
     |FINSI;

     aMas = aBase + "dspreven/def/pycm0015.mas";
     |CARGA_DEF aMas, pycm0015@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #ctrlfilt@;
         |DESCARGA_DEF #maesm037@;
         |DESCARGA_DEF #maesm036@;
         |DESCARGA_DEF #maesm035@;
         |DESCARGA_DEF #maesm011@;
         |ACABA;
     |FINSI;

     aMas = aBase + "dspreven/def/ctrlspas.mas";
     |CARGA_DEF aMas, ctrlspas@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #pycm0015@;
         |DESCARGA_DEF #ctrlfilt@;
         |DESCARGA_DEF #maesm037@;
         |DESCARGA_DEF #maesm036@;
         |DESCARGA_DEF #maesm035@;
         |DESCARGA_DEF #maesm011@;
         |ACABA;
     |FINSI;

     nPrevencion = 1;

     |HAZ CargaPath;

     |ABRE #911;
|FPRC;

|PROCESO dsficher;
     |SI #dsficher@#0 = "contagen";  |ACABA;  |FINSI;

     #pntm0004@#0 = #dsficher@#0;
     #pntm0004@#1 = #dsficher@#1;
     |LEE 000#pntm0004@.=;
     |SI FSalida ! 0;  |DDEFECTO #pntm0004@;  |FINSI;

     |SI #pntm0004@#3 = "N";
         |ACABA;
     |FINSI;

     #dsinsw01#0 = #dsficher@#0;
     |LEE 101#dsinsw01.=;
     |SI FSalida ! 0;
         |DDEFECTO #dsinsw01;
         #dsinsw01#0 = #dsficher@#0;
         #dsinsw01#1 = 0;

         |SI #dsinsw01#0 ! "basica  ";
             #dsinsw01#1 = 1;
         |FINSI;

         |GRABA 020#dsinsw01.b;
     |FINSI;

     #dsinsw01#1 = #dsinsw01#1 + 1;

     |GRABA 020#dsinsw01.a;
     |LIBERA #dsinsw01;
|FPRC;

|DEFBUCLE dsficher;
     #dsficher@#1002;
     ;
     "        ", "            ";
     "zzzzzzzz", "zzzzzzzzzzzz";
     ;
     NULL, dsficher;
|FIN;

|PROCESO CargaDefPI;
     nPI = 0;

     aMas = aBase + "integral/def/pntm0001.mas";
     |XABRE "E", aMas, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     aMas = aBase + "integral/def/pntm0001.mas";
     |CARGA_DEF aMas, pntm0001@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aMas = aBase + "integral/def/pntm0002.mas";
     |CARGA_DEF aMas, pntm0002@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #pntm0001@;
         |ACABA;
     |FINSI;

     aMas = aBase + "integral/def/pntm0003.mas";
     |CARGA_DEF aMas, pntm0003@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #pntm0002@;
         |DESCARGA_DEF #pntm0001@;
         |ACABA;
     |FINSI;

     aMas = aBase + "integral/def/pntm0004.mas";
     |CARGA_DEF aMas, pntm0004@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #pntm0003@;
         |DESCARGA_DEF #pntm0002@;
         |DESCARGA_DEF #pntm0001@;
         |ACABA;
     |FINSI;

     aMas = aBase + "basica/def/dsficher.mas";
     |CARGA_DEF aMas, dsficher@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #pntm0003@;
         |DESCARGA_DEF #pntm0002@;
         |DESCARGA_DEF #pntm0001@;
         |ACABA;
     |FINSI;

     nPI = 1;

     aFic = aBase + "integral/fich/";
     |PATH_DAT #pntm0001@#aFic#;
     |PATH_DAT #pntm0002@#aFic#;
     |PATH_DAT #pntm0003@#aFic#;
     |PATH_DAT #pntm0004@#aFic#;

     aFic = aBase + "basica/fich/";
     |PATH_DAT #dsficher@#aFic#;

     |ABRE #pntm0001@;  |SELECT #2#pntm0001@;
     |ABRE #pntm0002@;  |SELECT #2#pntm0002@;
     |ABRE #pntm0003@;
     |ABRE #pntm0004@;

     aFichero = Usuario + "dsinsw01";
     |NOME_DAT #dsinsw01#aFichero#;
     |ABRE #dsinsw01;  |CIERRA #dsinsw01;  |DELINDEX #dsinsw01;

     |ABRE #dsinsw01;
     |BUCLE dsficher;
     |CIERRA #dsinsw01;
|FPRC;

|PROCESO Tipo3;  |TIPO 3;
     |SI #0#0 ! "SI";  |ACABA;  |FINSI;

     |DBASS aBase;

     aMas = aBase + "dsoporte/def/dsprem04.mas";
     |CARGA_DEF aMas, dsprem04@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aMas = aBase + "dsoporte/def/dsprem03.mas";
     |CARGA_DEF aMas, dsprem03@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #dsprem04@;
         |ACABA;
     |FINSI;

     aMas = aBase + "dsoporte/def/dsprem02.mas";
     |CARGA_DEF aMas, dsprem02@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #dsprem03@;
         |DESCARGA_DEF #dsprem04@;
         |ACABA;
     |FINSI;

     nHayLabor = 0;
     aDefChek  = aBase + "gecochek/def/";
     aPathChek = aBase + "gecochek/fich/";
     aMas      = aDefChek + "gecom019.mas";
     |XABRE "E", aMas, 0;
     |SI FSalida ] 0;
         |CARGA_DEF aMas, gecom019@;
         |PATH_DAT #gecom019@#aPathChek#;

         |ABRE #gecom019@;
         |LEE 000#gecom019@.p;
         |SI FSalida = 0;  nHayLabor = 1;  |FINSI;
         |CIERRA #gecom019@;
         |DESCARGA_DEF #gecom019@;
     |FINSI;

     aPathDSOPORTE = aBase + "dsoporte/fich/";
     |PATH_DAT #902 aPathDSOPORTE;
     |PATH_DAT #903 aPathDSOPORTE;
     |PATH_DAT #904 aPathDSOPORTE;

     |ABRE #902;
     |ABRE #903;
     |ABRE #904;

     |HAZ CargaDefPrevencion;
     |HAZ CargaDefPI;

     aFichero = ("w3" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #90 aFichero;
     |ABRE #90;  |CIERRA #90;  |DELINDEX #90;

     nSwHay      = 0;
     nDesdeGrupo = 0;
     nHastaGrupo = 9999;
     |SI #0#1 ! 0;
         nDesdeGrupo = #0#1;
         nHastaGrupo = #0#1;
     |FINSI;

     |BUCLE agiuser1;

     |SI nSwHay = 0;
         |MENAV "     Generacion Vacia";
     |SINO;
         |MENAV "     Generacion creada con exito.";
     |FINSI;

     |ABRE #90;  |CIERRA #90;  |DELINDEX #90;

     |CIERRA #902;
     |CIERRA #903;
     |CIERRA #904;

     |SI nPI = 1;
         |DELINDEX #dsinsw01;

         |CIERRA #pntm0001@;
         |CIERRA #pntm0002@;
         |CIERRA #pntm0003@;
         |CIERRA #pntm0004@;

         |DESCARGA_DEF #dsficher@;
         |DESCARGA_DEF #pntm0004@;
         |DESCARGA_DEF #pntm0003@;
         |DESCARGA_DEF #pntm0002@;
         |DESCARGA_DEF #pntm0001@;
     |FINSI;

     |SI nPrevencion = 1;
         |CIERRA #911;

         |DESCARGA_DEF #ctrlspas@;
         |DESCARGA_DEF #pycm0015@;
         |DESCARGA_DEF #ctrlfilt@;
         |DESCARGA_DEF #maesm037@;
         |DESCARGA_DEF #maesm036@;
         |DESCARGA_DEF #maesm035@;
         |DESCARGA_DEF #maesm011@;
     |FINSI;

     |DESCARGA_DEF #dsprem02@;
     |DESCARGA_DEF #dsprem03@;
     |DESCARGA_DEF #dsprem04@;
|FPRC;

|PROGRAMA;
     aParametro = PARAMETRO;  |QBF aParametro;
     |SI aParametro = "";
         |MANTE #0;
     |SINO;
         #0#0 = "SI";

         |SI aParametro = "AUTOMATICO";
             #0#1 = 0;
         |SINO;
             #0#1 = aParametro;
         |FINSI;

         |HAZ Tipo3;
     |FINSI;

     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
|FPRO;
