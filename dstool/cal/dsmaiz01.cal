|FICHEROS;
     dsmaiz01 #0;
     dsmail01 #10;
     dsmaiw05 #11,MANTE;
     dsmail03 #12,MANTE;
     dsmail02 #13;
     dsmail04 #14;

     dsmaiw06 #50;
     dsmaiw07 #51;
     dsmaiw08 #52;
     dsmaiw09 #53;

     Empresa@ #100;
     FMaster@ #101;
     FEsclav@ #102;
     Referen@ #103;
     Refere1@ #104;
|FIN;

|VARIABLES;
   aIP = "";
   nSwBasico = 0;
   nLinea    = 0;
   nExiste   = 0;
   aDef = "";
   aOrg = "";
   i = 0;
   aDirec  = "";
   aAlfa   = "";
   aAlfa1  = "";
   aAlfa2  = "";
   aAlfa3  = "";
   aRels   = "";
   aRels1  = "";
   aDir    = "";
   aApp    = "";
   aDefEmp = "";
   aLong   = "";
   aEmpresa = "";
   aPath   = "";
   aClave1 = "";
   aClave2 = "";
   aFichMaster  = "";
   aFichEsclavo = "";
   aFichRelac   = "";
   aNombreRel   = "";
   aSQL         = "";
   aFic         = "";
   aCadena      = "";
   TipoDato     = "";

   aAlfaComp  = ""; aAlfaComp1 = ""; aAlfaComp2 = "";
   nNumeComp  = 0;  nNumeComp1 = 0;  nNumeComp2 = 0;
   fFechComp  = @;  fFechComp1 = @;  fFechComp2 = @;

   nNume     = 0;
   nCmp      = 0;
   nNumRels  = 0;
   nNumRels1 = 0;
   nContador = 0;
   nValido   = 0;
   nNumClave = 0;
   nWord    = 0;
   nCalculo = 0;
   nSw     = 0;
   nMaxLin = 0;
   nFlag   = 0;
   nHandle = 0;
   nCalc   = 0;
   nCalc1  = 0;
   nCalc2  = 0;
   nLong   = 0;
   nCont   = 0;
   nCont1  = 0;
   nCont2  = 0;
   nNumInd = 0;
   nModo   = 0;
   nLin1   = 0;
   nLin2   = 0;
   nCampo  = 0;
   nOpcion = 0;
   nCmpClave1 = 0;
   nCmpClave2 = 0;
   nNumCmps   = 0;
   nLinRel    = 0;
   Calc1      = 0;
   nPos       = 0;
   nPunte     = 0;

{100}aArg = "";

{-1}Menu  = "";
    Menu1 = "2400";
    Menu2 = "1";
    Menu3 = "";
    Menu4 = "REC";
    Menu5 = " Revisar ";
    Menu6 = " Emitir ";
    Menu7 = " Cancelar ";

     nMail            = 0;
     nHastaCampo      = 0;
     nCaracter        = 0;
     nLlave           = 0;

     aAbre            = "";
     aDocTxt          = "";
     aDocDoc          = "";
     aDocRes          = "";
     aDocPdf          = "";
     aAsigna          = "";
     aConten          = "";
     aPathTrabajo     = "";
     aPathLocal       = "";
     aBaseMensaje     = "";
     aAbreCorreo      = "";
     aFichero         = "";
     aDSCorreo        = "";
     aProxy           = "";
     aDirOri          = "";
     aDirDes          = "";
     aTitulo          = "";
     aTexto           = "";
     aEnviado         = "";
     aBueno           = "";
     aCaracter        = "";
     aPara1           = "";
     aPara2           = "";
     aPara3           = "";
     aPara4           = "";
     aPara5           = "";

     &eaBase          = "";
     &eaFicheroTra    = "";
     &eaDirPlantilla  = "";
     &eaUnidadBasico  = "";
     &eaOrigen        = "";
     &eaDestino       = "";
     &eaAlfa          = "";
     &eaImpresora     = "";
     &eaParametro1    = "";
     &eaParametro2    = "";
     &eaParametro3    = "";
     &eaParametro4    = "";
     &eaParametro5    = "";
|FIN;

|PROCESO CuerpoCorreo;
     |DBASE aBaseMensaje;  |QBF aBaseMensaje;
     aBaseMensaje = aBaseMensaje + "correos";  |MKDIR aBaseMensaje;
     aBaseMensaje = aBaseMensaje + "/";
     aAbreCorreo  = aBaseMensaje + "adjunto.txt";
     |XABRE "WB", aAbreCorreo, nHandle;

     nHastaCampo = 0;
     |PARA nCampo = 17;  |SI nCampo ] 4;  |HACIENDO nCampo = nCampo - 1;
           aAlfa = #14nCampo;   |QBF aAlfa;
           |SI aAlfa ! "";
               nHastaCampo = nCampo;
               |SAL;
           |FINSI;
     |SIGUE;

     |PARA nCampo = 4;  |SI nCampo [ nHastaCampo;  |HACIENDO nCampo = nCampo + 1;
           eaAlfa = #14nCampo;  |QBF eaAlfa;
           |HAZ QuitaRaros;

           |SI eaAlfa = "";
               eaAlfa = &10;
           |SINO;
               eaAlfa = eaAlfa + &13 + &10;
           |FINSI;

           |XGRABA nHandle, eaAlfa;
     |SIGUE;

     |XCIERRA nHandle;
|FPRC;

|PROCESO EnviaCorreo;
     |SI aAbreCorreo = "";
         |HAZ CuerpoCorreo;
     |FINSI;

     aDSCorreo = "xdscorreo.dv.diagram.net";
     |SI #14#2 = "H";
          aDSCorreo = "192.168.14.101";
     |FINSI;

     eaOrigen = aPathLocal + aDocPdf;
     eaDestino = aBaseMensaje + aDocPdf;
     |SI aIP = "";
          |COPIA_FICHERO eaOrigen, eaDestino;
     |SINO;
          |RECIBE_FICHERO eaOrigen, eaDestino;
     |FINSI;

     aProxy  = "asmtp.diagram.es";
     aDirOri = #14#1;
     aDirDes = #53#2;
     aTitulo = #14#3;
     aTexto  = "$$$$" + aAbreCorreo;
     aFic    = eaDestino;

     |QBF aDirOri;
     |QBF aDirDes;
     |QBF aTitulo;

     aEnviado = "No enviado: la cuenta destino puede ser err¢nea";
     |DSCORREO_ENVIA aDSCorreo, aProxy, aDirOri, aDirDes, aTitulo, aTexto, aFic;
     |SI FSalida ] 0;
         aEnviado = "Enviado";
     |FINSI;

     |FBORRA aFic;

     #53#0 = nMail;
     |LEE 101#53=;
     |SI FSalida = 0;
         #53#3 = aEnviado;

         |GRABA 020#53a;
         |LIBERA #53;
     |FINSI;

     |PULSATECLA;
|FPRC;

|PROCESO Cambia;
     aAsigna = #dsmail02#6; |QBF aAsigna;
     aAsigna = "#" + aAsigna + "#";
     #dsmaiw07#0 = #dsmail02#2;
     #dsmaiw07#1 = #dsmail02#4;
     |LEE 000#dsmaiw07.=;
     |SI FSalida ! 0; |DDEFECTO #dsmaiw07; |FINSI;
     |SI #dsmaiw07#2 = "A"; aConten = #dsmaiw07#3; |FINSI;
     |SI #dsmaiw07#2 = "N";
         |SI #dsmail02#7 = "S";
             nPos   = -(100 + #dsmaiw07#6);
             aConten = (("0" * #dsmaiw07#6) + #dsmaiw07#4) % nPos;
         |SINO;
             aConten = #dsmaiw07#4;
         |FINSI;
     |FINSI;
     |SI #dsmaiw07#2 = "F"; aConten = #dsmaiw07#5; |FINSI;
     |QBF aConten;

     eaAlfa = aConten; |HAZ QuitaRaros; aConten = eaAlfa;

     aAlfa = "V"     + &13 + &10;   |XGRABA nHandle, aAlfa;
     aAlfa = aAsigna + &13 + &10;   |XGRABA nHandle, aAlfa;
     aAlfa = aConten + &13 + &10;   |XGRABA nHandle, aAlfa;
|FPRC;

|DEFBUCLE Cambia;
     #dsmail02#1;
     ;
     #dsmail01#0,001;
     #dsmail01#0,999;
     ;
     NULL,Cambia;
|FIN;

|PROCESO CreaSQL;
     |SI #dsmaiw05#2 ! aApp;  |ACABA;  |FINSI;

     |SI #dsmaiw05#8 = "A";
         |SI #dsmaiw05#9 ! #dsmaiw05#10;
             aSQL = aSQL + aAlfa3 + #dsmaiw05#4 + " BETWEEN ";
             aAlfa = #dsmaiw05#9; |QBF aAlfa;
             nCalc = 100 + #dsmaiw05#15;
             aAlfa = (aAlfa + (" " * #dsmaiw05#15)) % nCalc;
             aSQL = aSQL + "'" + aAlfa + "',";
             aAlfa = #dsmaiw05#10; |QBF aAlfa;
             nCalc = 100 + #dsmaiw05#15;
             aAlfa = (aAlfa + (" " * #dsmaiw05#15)) % nCalc;
             aSQL = aSQL + "'" + aAlfa + "'";
          |SINO;
             aSQL = aSQL + aAlfa3 + #dsmaiw05#4 + " == ";
             aAlfa = #dsmaiw05#9; |QBF aAlfa;
             nCalc = 100 + #dsmaiw05#15;
             aAlfa = (aAlfa + (" " * #dsmaiw05#15)) % nCalc;
             aSQL = aSQL + "'" + aAlfa + "'";
          |FINSI;

          aAlfa3 = " AND ";
     |FINSI;

     |SI #dsmaiw05#8 = "N";
         |SI #dsmaiw05#11 ! #dsmaiw05#12;
             aSQL = aSQL + aAlfa3 + #dsmaiw05#4 + " BETWEEN ";
             aSQL = aSQL + #dsmaiw05#11 + "," + #dsmaiw05#12;
         |SINO;
             aSQL = aSQL + aAlfa3 + #dsmaiw05#4 + " == ";
             aSQL = aSQL + #dsmaiw05#11;
         |FINSI;

         aAlfa3 = " AND ";
     |FINSI;

     |SI #dsmaiw05#8 = "F";
         |SI #dsmaiw05#13 ! #dsmaiw05#14;
             aSQL = aSQL + aAlfa3 + #dsmaiw05#4 + " BETWEEN ";
             aSQL = aSQL + #dsmaiw05#13 + "," + #dsmaiw05#14;
         |SINO;
             aSQL = aSQL + aAlfa3 + #dsmaiw05#4 + " == ";
             aSQL = aSQL + #dsmaiw05#13;
         |FINSI;

         aAlfa3 = " AND ";
     |FINSI;
|FPRC;

|DEFBUCLE CreaSQL;
     #dsmaiw05#1;
     ;
     01;
     99;
     ;
     NULL, CreaSQL;
|FIN;

|PROCESO CopiaMas;
     |DDEF aDef;
     aDef = aDef + #dsmail01#2; |QBF aDef;
     aDef = aDef + ".mas";
     |XABRE "E", aDef, 0;
     nExiste = FSalida;
     |SI nExiste < 0;
          |COPIA_FICHERO aOrg, aDef;
     |FINSI;
|FPRC;

|PROCESO BorraMas;
     |SI nExiste < 0;
          |FBORRA aDef;
     |FINSI;
|FPRC;

|PROCESO SentSQL;
     aAlfa = #dsmail01#6; |QBF aAlfa;
     aAlfa = aAlfa + "def/" + #dsmail01#2; |QBF aAlfa;
     aAlfa = aAlfa + ".mas";
     aOrg = aAlfa;

     |CARGA_DEF aAlfa, Referen@;
     |SI FSalida = 0;
        aPath = #dsmail01#6; |QBF aPath;
        aFichRelac = #dsmail01#2;
        aDir = aPath + "fich/" + aEmpresa + "/";
        |PATH_DAT #103 aDir;
        aSQL = "SELECT * FROM " + #dsmail01#2 + " INTO ml" + Usuario + ".def WHERE ";
        aApp = #dsmail01#2; aAlfa3 = "";
        |BUCLE CreaSQL;
        |SQL aSQL;
        nSwBasico = FSalida; || Dependiendo del basico el def debe de
        |SI FSalida ! 0;     || estar en otro lugar.
           |HAZ CopiaMas;    || Y el temporal que crea tambien cambia
           |SQL aSQL;        || La variable 'nSwBasico' es la que controla esto.
           |HAZ BorraMas;    || Juan Tue Nov 29 11:55:08 CET 2005
        |FINSI;

        |DESCARGA_DEF #Referen@;
     |FINSI;
|FPRC;

|| **************************************************************************
|| **************************************************************************
|| ****                     RESOLUCION DE RELACIONES                     ****
|| **************************************************************************
|| **************************************************************************


  La llamada al proceso principal 'ResuelveRels' toma el valor de la variable
  'FichRelac' que contiene el nombre del fichero del cual se resolveran las
  relaciones. En base al registro corriente del fichero 'FichRelac', se
  rellena el fichero 'dsmaiw07' con los registros correspondientes de los
  ficheros relacionados con el indicado.


|PROCESO CargaRegAct;
   aAlfa3 = "Cargando registro : " + nCont + "  /" + aFichMaster;
   |ATRI I; |PINTA 2207, aAlfa3; |ATRI N; nCont = nCont + 1;
   nNume = #dsmaiw07#1;
   |SI #dsmaiw07#2 = "A"; #FMaster@#nNume# = #dsmaiw07#3; |FINSI;
   |SI #dsmaiw07#2 = "N"; #FMaster@#nNume# = #dsmaiw07#4; |FINSI;
   |SI #dsmaiw07#2 = "F"; #FMaster@#nNume# = #dsmaiw07#5; |FINSI;
|FPRC;

|DEFBUCLE CargaRegAct;
     #dsmaiw07#1;
     ;
     #dsmaiw06#1,000;
     #dsmaiw06#1,255;
     ;
     NULL,CargaRegAct;
|FIN;

|PROCESO Comprueba;
     nValido = 0;

     |SELECT #2#dsmail02;
     #dsmail02#2 = aFic;
     #dsmail02#4 = nCmp;
     |LEE 000#dsmail02.=;
     |SI FSalida = 0; nValido = 1; |FINSI;
     |SELECT #1#dsmail02;
     |SI nValido = 1; |ACABA; |FINSI;

     |SELECT #2#dsmail03;
     #dsmail03#2 = aFic;
     #dsmail03#4 = nCmp;
     |LEE 000#dsmail03.=;
     |SI FSalida = 0; nValido = 1; |FINSI;
     |SELECT #1#dsmail03;
     |SI nValido = 1; |ACABA; |FINSI;

     aDirec = #dsmail01#6; |QBF aDirec;
     aDirec = aDirec + "def/" + aFic + ".mas";
     |CARGA_DEF aDirec, Refere1@;
     |SI FSalida = 0;
        aRels = "|K000"; aRels = #Refere1@#aRels#;
        aRels = aRels % 0499;
        |PARA; |SI aRels ! ""; |HACIENDO;
           aRels1 = aRels % 0103; aRels = aRels % 0499;
           nNume  = aRels1;
           |SI nNume = nCmp; nValido = 1; |SAL; |FINSI;
        |SIGUE;
        |SI nValido = 1; |DESCARGA_DEF #Refere1@; |ACABA; |FINSI;

        aRels = "|NR"; aRels = #Refere1@#aRels#;
        nNumRels = aRels;
        |PARA nContador = 1; |SI nContador [ nNumRels; |HACIENDO nContador = nContador + 1;
           aRels = "|R" + (("000" + nContador) % -103); aRels = #Refere1@#aRels#;
           aRels = aRels - "|"; nCalc = FSalida + 98; aRels = aRels % nCalc;
           |PARA; |SI aRels ! ""; |HACIENDO;
              aRels1 = aRels % 0103; aRels = aRels % 0499;
              nNume = aRels1;
              |SI nNume = nCmp; nValido = 1; |SAL; |FINSI;
           |SIGUE;
           |SI nValido = 1; |SAL; |FINSI;
        |SIGUE;
        |DESCARGA_DEF #Refere1@;
     |FINSI;
|FPRC;

|PROCESO SacaRels;
   aAlfa1 = #dsmaiw06#1; |QBF aAlfa1; aFichMaster = aAlfa1;
   aAlfa = #dsmail01#6; |QBF aAlfa; aAlfa = aAlfa + "def/" + aAlfa1 + ".mas";
   |CARGA_DEF aAlfa, FMaster@;
   |SI FSalida = 0;
      aDir = aPath + "fich/" + aEmpresa + "/";
      |PATH_DAT #101 aDir;
      |ABRE #FMaster@;

      nCont = 0; |BUCLE CargaRegAct;

      aAlfa = "|NR";
      nNumRels1 = #FMaster@#aAlfa#;

      |ATRI I; |PINTA 2207, "Resolviendo relaciones           "; |ATRI N;

      |PARA nCont = 0; |SI nCont < nNumRels1; |HACIENDO nCont = nCont + 1;
         aAlfa = ("000" + nCont) % -103;
         aAlfa = "|R" + aAlfa;
         aAlfa = #FMaster@#aAlfa#;
         aAlfa1 = aAlfa % 108; aNombreRel = aAlfa1;
         aAlfa = aAlfa - aAlfa1;
         aAlfa = aAlfa - "|";
         aAlfa = aAlfa - "-";
         |SI FSalida = 0;
            aFichEsclavo = aAlfa1; |QBF aFichEsclavo;
            aAlfa1 = aPath + "def/" + aAlfa1 + ".mas";
            |CARGA_DEF aAlfa1, FEsclav@;
            |SI FSalida = 0;
               aDir = aPath + "fich/" + aEmpresa + "/";
               |PATH_DAT #102 aDir;
               |ABRE #FEsclav@;

               || Cargo la clave del fichero nuevo y lo busco

               aAlfa1 = aAlfa % 103;
               aAlfa = aAlfa - aAlfa1;
               nCalc2 = aAlfa1;

               aClave1 = aAlfa;
               aAlfa = "|K000";
               aAlfa = #FEsclav@#aAlfa#;
               aClave2 = aAlfa % 0499;

               |PARA nCont1 = 0; |SI nCont1 < nCalc2; |HACIENDO nCont1 = nCont1 + 1;
                  aAlfa = aClave1 % 0103; aClave1 = aClave1 - aAlfa; nCmpClave1 = aAlfa;
                  nCmpClave1 = nCmpClave1 - 1;
                  aAlfa = aClave2 % 0103; aClave2 = aClave2 - aAlfa; nCmpClave2 = aAlfa;
                  #FEsclav@#nCmpClave2# = #FMaster@#nCmpClave1#;
               |SIGUE;

               |LEE 000#FEsclav@.=;

               || Si se encuentra , grabo los datos en el temporal de datos relaccionados
               |SI FSalida = 0;
                  aAlfa1 = "|NC";
                  nNumCmps = #FEsclav@#aAlfa1# - 1;
                  |PARA nCont2 = 0; |SI nCont2 [ nNumCmps; |HACIENDO nCont2 = nCont2 + 1;
                     aFic = aNombreRel; nCmp = nCont2; |HAZ Comprueba;
                     |SI nValido = 1;
                        #dsmaiw07#0 = aNombreRel;
                        #dsmaiw07#1 = nCont2;
                        |LEE 000#dsmaiw07.=;
                        |SI FSalida ! 0;
                           |DDEFECTO #dsmaiw07;
                           #dsmaiw07#0 = aNombreRel;
                           #dsmaiw07#1 = nCont2;
                           aAlfa1 = ("000" + nCont2) % -103;
                           aAlfa1 = "|C" + aAlfa1 + "TIPO";
                           aAlfa1 = #FEsclav@#aAlfa1#;
                           #dsmaiw07#2 = aAlfa1;
                           |SI aAlfa1 = "A"; #dsmaiw07#3 = #FEsclav@#nCont2#; |FINSI;
                           |SI aAlfa1 = "N"; #dsmaiw07#4 = #FEsclav@#nCont2#; |FINSI;
                           |SI aAlfa1 = "F"; #dsmaiw07#5 = #FEsclav@#nCont2#; |FINSI;
                           aAlfa1 = ("000" + nCont2) % -103;
                           aAlfa1 = "|C" + aAlfa1 + "LONGITUD";
                           #dsmaiw07#6 = #FEsclav@#aAlfa1#;
                           |GRABA 020#dsmaiw07.n;
                        |FINSI;
                     |FINSI;
                  |SIGUE;
               |FINSI;

               |SALVA_FICHA #dsmaiw06;
               |SELECT #2#dsmaiw06;
               #dsmaiw06#1 = aNombreRel;
               |LEE 101#dsmaiw06.=;
               |SI FSalida ! 0;
                  |DDEFECTO #dsmaiw06;
                  #dsmaiw06#0 = nLinRel; nLinRel = nLinRel + 1;
                  #dsmaiw06#1 = aNombreRel;
                  |GRABA 020#dsmaiw06.n; |LIBERA #dsmaiw06;
               |FINSI;
               |SELECT #1#dsmaiw06;
               |REPON_FICHA #dsmaiw06;
               |CIERRA #FEsclav@; |DESCARGA_DEF #FEsclav@;
            |FINSI;
         |FINSI;
      |SIGUE;
      |CIERRA #FMaster@; |DESCARGA_DEF #FMaster@;
   |FINSI;

   #dsmaiw06#2 = "*"; |GRABA 020#dsmaiw06.a;
|FPRC;

|PROCESO ResuelveRels;

  || Esta rutina rellena un temporal con el registro del fichero principal que
  ||   se esta tratando, ademas de rellenarlo con los registros de todos los
  ||   ficheros que estan relacionados con el. Para esto se apoya sobre un
  ||   temporal que indica los ficheros relacionados con el fichero principal
  ||   y los relacionados entre si, comenzando luego escribiendo el registro
  ||   actual del fichero principal y buscando los registros relacionados a
  ||   partir de ahi


  ||  Primero, rellenamos el temporal para saber todos los ficheros que
  ||    tendremos que leer ....

     |DELINDEX #dsmaiw06; |DELINDEX #dsmaiw07;
     |ABRE #dsmaiw06;
     nLinRel = 1;

     |DDEFECTO #dsmaiw06;
     #dsmaiw06#0 = nLinRel; nLinRel = nLinRel + 1;
     #dsmaiw06#1 = aFichRelac;
     |GRABA 020#dsmaiw06.n;

     |ABRET #dsmaiw07;
     aFichMaster = aFichRelac;
     aAlfa = aPath + "def/" + aFichRelac + ".mas";
     |CARGA_DEF aAlfa, FMaster@;
     |SI FSalida = 0;
        aDir = aPath + "fich/" + aEmpresa + "/";
        |PATH_DAT #101 aDir;
        |ABRE #FMaster@;

        aAlfa1 = "|NC";
        nNumCmps = #FMaster@#aAlfa1#; nNumCmps = nNumCmps - 1;
        |PARA nCont = 0; |SI nCont [ nNumCmps; |HACIENDO nCont = nCont + 1;
           #FMaster@#nCont# = #Referen@#nCont#;
        |SIGUE;
        |LEE 000#FMaster@.=;

        |PARA nCont = 0; |SI nCont [ nNumCmps; |HACIENDO nCont = nCont + 1;
           aFic = aFichRelac; nCmp = nCont; |HAZ Comprueba;
           |SI nValido = 1;
              #dsmaiw07#0 = aFichRelac;
              #dsmaiw07#1 = nCont;
              |LEE 101#dsmaiw07.=;
              |SI FSalida ! 0;
                 |DDEFECTO #dsmaiw07;
                 #dsmaiw07#0 = aFichRelac;
                 #dsmaiw07#1 = nCont;
                 |GRABA 020#dsmaiw07.b;
              |FINSI;
              aAlfa1 = ("000" + nCont) % -103;
              aAlfa1 = "|C" + aAlfa1 + "TIPO";
              aAlfa1 = #FMaster@#aAlfa1#;
              #dsmaiw07#2 = aAlfa1;
              |SI aAlfa1 = "A"; #dsmaiw07#3 = #FMaster@#nCont#; |FINSI;
              |SI aAlfa1 = "N"; #dsmaiw07#4 = #FMaster@#nCont#; |FINSI;
              |SI aAlfa1 = "F"; #dsmaiw07#5 = #FMaster@#nCont#; |FINSI;
              aAlfa1 = ("000" + nCont) % -103;
              aAlfa1 = "|C" + aAlfa1 + "LONGITUD";
              #dsmaiw07#6 = #FMaster@#aAlfa1#;
              |GRABA 020#dsmaiw07.a; |LIBERA #dsmaiw07;
           |FINSI;
        |SIGUE;

        aAlfa = "|NR";
        nCalc1 = #FMaster@#aAlfa#;

        |PARA nCont = 0; |SI nCont < nCalc1; |HACIENDO nCont = nCont + 1;
           aAlfa = ("000" + nCont) % -103;
           aAlfa = "|R" + aAlfa;
           aAlfa = #FMaster@#aAlfa#;
           aAlfa = aAlfa % 108;
           |DDEFECTO #dsmaiw06;
           #dsmaiw06#0 = nLinRel; nLinRel = nLinRel + 1;
           #dsmaiw06#1 = aAlfa;
           |GRABA 020#dsmaiw06.n;
        |SIGUE;

        |CIERRA #FMaster@; |DESCARGA_DEF #FMaster@;
     |FINSI;

  ||  ... y una vez lo tenemos , vamos obteniendo los registros relacionados
  ||   con el registro actual del fichero principal o con cualquiera
  ||   relacionado con este

     |LEE 000#dsmaiw06.p;
     |PARA; |SI FSalida = 0; |HACIENDO;
        |HAZ SacaRels;
        |LEE 000#dsmaiw06.p;
        |PARA; |SI FSalida = 0; |HACIENDO;
           |SI #dsmaiw06#2 = " "; |SAL; |FINSI;
           |LEE 000#dsmaiw06.s;
        |SIGUE;
     |SIGUE;

     |CIERRAT #dsmaiw07;
     |CIERRA #dsmaiw06;
|FPRC;

|PROCESO Acotaciones;
   #dsmaiw07#0 = #dsmaiw05#2;
   #dsmaiw07#1 = #dsmaiw05#4;
   |LEE 000#dsmaiw07.=;
   |SI FSalida ! 0; nSw = 1; |ERROR10; |FINSI;
   |SI #dsmaiw05#8 = "A"; aAlfaComp = #dsmaiw07#3; |FINSI;
   |SI #dsmaiw05#8 = "F"; aAlfaComp = #dsmaiw07#5; |FINSI;

   |SI #dsmaiw05#8 = "A";
      aAlfaComp  = #dsmaiw07#3;  |QBF aAlfaComp;
      aAlfaComp1 = #dsmaiw05#9;  |QBF aAlfaComp1;
      aAlfaComp2 = #dsmaiw05#10; |QBF aAlfaComp2;
      nCalculo = 100 + #dsmaiw05#15;
      aAlfaComp = (aAlfaComp + (" " * #dsmaiw05#15)) % nCalculo;
      aAlfaComp1 = (aAlfaComp1 + (" " * #dsmaiw05#15)) % nCalculo;
      aAlfaComp2 = (aAlfaComp2 + (" " * #dsmaiw05#15)) % nCalculo;

      |SI aAlfaComp1 > aAlfaComp; |O aAlfaComp > aAlfaComp2;
         nSw = 1; |ERROR10;
      |FINSI;
   |FINSI;
   |SI #dsmaiw05#8 = "N";
      nNumeComp = #dsmaiw07#4;
      nNumeComp1 = #dsmaiw05#11; nNumeComp2 = #dsmaiw05#12;
      |SI nNumeComp1 > nNumeComp; |O nNumeComp > nNumeComp2;
         nSw = 1; |ERROR10;
      |FINSI;
   |FINSI;
   |SI #dsmaiw05#8 = "F";
      fFechComp  = #dsmaiw07#5;
      fFechComp1 = #dsmaiw05#13;
      fFechComp2 = #dsmaiw05#14;
      |SI fFechComp1 > fFechComp; |O fFechComp > fFechComp2;
         nSw = 1; |ERROR10;
      |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE Acotaciones;
    #dsmaiw05#1;
    ;
    INICIO;
    FINAL;
    ;
    NULL, Acotaciones;
|FIN;

|PROCESO Documento;
     |SI #10#4 = "C";
         nMail = nMail + 1;
         #53#0 = nMail;
         nCont = #10#9;   #53#1 = #Referen@#nCont#;
         nCont = #10#10;  #53#2 = #Referen@#nCont#;
                          #53#3 = "No enviado: sin cuenta correo destino";
         |GRABA 020#53n;
         |LIBERA #53;

         aAlfa = #53#2;  |QBF aAlfa;
         |SI aAlfa = "";  |ACABA;  |FINSI;
     |FINSI;

     aPathTrabajo = eaBase + "tmp";               |MKDIR aPathTrabajo;
     aPathTrabajo = aPathTrabajo + "/" + Usuario; |MKDIR aPathTrabajo;
     aPathTrabajo = aPathTrabajo + "/";

     aAbre = aPathTrabajo + aDocTxt;
     |RFBORRA aAbre;

     |XABRE "WB", aAbre, nHandle;

     aAlfa = "[FICHERO]#" + aPathLocal + eaFicheroTra + aDocDoc + "#" + aPathLocal + aDocRes + &13 + &10;
     |XGRABA nHandle, aAlfa;

     |BUCLE Cambia;

     aAlfa = "[LIMPIA FINAL]" + &13 + &10;
     |XGRABA nHandle, aAlfa;

     |SI #dsmail01#4 = "I";
         |SI eaImpresora = "";
             eaParametro1 = aPathLocal + "dsbrowserprint";
             eaParametro2 = aPathLocal + "impresora.sel";
             eaParametro3 = eaDirPlantilla;
             eaParametro4 = aPathLocal;
             eaParametro5 = aPathLocal;
             |HAZ SeleccionaImpresora;
         |FINSI;

         aAlfa = "[IMPRESORA]#I#0#" + eaImpresora + &13 + &10;
         |XGRABA nHandle, aAlfa;
     |FINSI;

     |SI #dsmail01#4 = "E";
         aAlfa = "[IMPRESORA]#P"   + &13 + &10;
         |XGRABA nHandle, aAlfa;
     |FINSI;

     |SI #dsmail01#4 = "C";
         aAlfa = "[IMPRESORA]#I#2#dspdf#2" + &13 + &10;
         |XGRABA nHandle, aAlfa;
     |FINSI;

     |XCIERRA nHandle;

     eaOrigen  = aAbre;
     eaDestino = aPathLocal + aDocTxt;
     |HAZ EnviaFicheroSinMD5;
     |FBORRA eaOrigen;

     eaParametro1 = aPathLocal + "dsxml";
     eaParametro2 = aPathLocal + aDocTxt;
     eaParametro3 = eaDirPlantilla;
     eaParametro4 = aPathLocal;
     eaParametro5 = aPathLocal;

     |HAZ EjecutaDSXML;

     |SI #10#4 = "C";
         |HAZ EnviaCorreo;
     |FINSI;

     aAlfa = aPathLocal + "impresora.sel";  |RFBORRA aAlfa;
     aAlfa = aPathLocal + aDocRes;          |RFBORRA aAlfa;
     aAlfa = aPathLocal + aDocPdf;          |RFBORRA aAlfa;
|FPRC;

|PROCESO Emite;
     |HAZ LeeUnidadBasico;
     |DBASE eaBase;  |QBF eaBase;
     eaDirPlantilla = eaBase + "plugins/";

     aPathLocal = eaUnidadBasico + "\DOCUMENTOS";   |RMKDIR aPathLocal;
     aPathLocal = aPathLocal + "\AGITOOL";           |RMKDIR aPathLocal;
     aPathLocal = aPathLocal + "\";

     |SI #10#4 = "C";
         |HAZ InstalaImpre;

         aAlfa = "|:agitool/dsmail04;" + #0#0;
         |SUB_EJECUTA aAlfa;

         |ABRE #14;
         #14#0 = #0#0;
         |LEE 000#14=;
         |SI FSalida ! 0;  |DDEFECTO #14;  |FINSI;
         |CIERRA #14;

         |CONFI "0000SEnviamos los correos";
         |SI FSalida ! 0;  |ACABA;  |FINSI;

         aFichero = ("09" + Usuario + "zzzzzzzz") % 108;
         |NOME_DAT #53 aFichero;
         |ABRE #53;  |CIERRA #53;  |DELINDEX #53;

         |ABRE #53;
     |FINSI;

     aDocDoc = "mai" + (("00000" + #0#0) % -105) + ".doc";
     aDocTxt = "mai" + (("00000" + #0#0) % -105) + ".txt";
     aDocRes = "adjunto.doc";
     aDocPdf = "adjunto.pdf";

     eaOrigen   =  #dsmail01#5; |QBF eaOrigen;
     eaDestino  = aPathLocal + aDocDoc;
     |HAZ EnviaFicheroSinMD5;

     |HAZ SentSQL;

     aAlfa = #dsmail01#6; |QBF aAlfa;
     aAlfa = aAlfa + "def/ml" + Usuario + ".mas";
     |SI nSwBasico ! 0;
         aAlfa = "ml" + Usuario + ".mas";
     |FINSI;

     |CARGA_DEF aAlfa, Referen@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI nSwBasico = 0;
         |DFICB aAlfa; |QBF aAlfa; |PATH_DAT #103 aAlfa;
     |FINSI;

     nMail = 0;

     |ABRE #Referen@;
     |LEE 000#Referen@.p;
     |PARA; |SI FSalida = 0; |HACIENDO;
            |HAZ ResuelveRels;
            |ABRE #dsmaiw07;

            nSw = 0;
            |BUCLE Acotaciones;

            |SI nSw = 0;
                |HAZ Documento;
            |FINSI;

            |CIERRA #dsmaiw07;

            |LEE 000#Referen@.s;
     |SIGUE;

     aAlfa = aPathLocal + aDocDoc;  |RFBORRA aAlfa;

     |DELINDEX #Referen@;

     |SI #10#4 = "C";
         |CONSULTA_CLAVE #1#53;

         |CIERRA #53;
         |DELINDEX #53;

         |SI aAbreCorreo ! "";
             |FBORRA aAbreCorreo;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO BorraDef;
     aAlfa = #dsmail01#6; |QBF aAlfa;
     aAlfa = aAlfa + "def/ml" + Usuario + ".mas";
     |SI nSwBasico ! 0;
          |DDEF aDef;
          aAlfa =  aDef + "ml" + Usuario + ".mas";
     |FINSI;
     |FBORRA aAlfa;
|FPRC;

|PROCESO FiltraCampos;
     |SI #dsmaiw05#8 = "A";
         #dsmaiw05#11 = -9999999.99999; #dsmaiw05#12 = 99999999.99999
         #dsmaiw05#13 = "01.01.0000";   #dsmaiw05#14 = "31.12.9999";

         |C_M #dsmaiw05#11,1,"N"; |C_M #dsmaiw05#12,1,"N";
         |C_V #dsmaiw05#11,1,"N"; |C_V #dsmaiw05#12,1,"N";
         |C_M #dsmaiw05#13,1,"N"; |C_M #dsmaiw05#14,1,"N";
         |C_V #dsmaiw05#13,1,"N"; |C_V #dsmaiw05#14,1,"N";

         |C_M #dsmaiw05#9,1,"S";  |C_M #dsmaiw05#10,1,"S";
         |C_V #dsmaiw05#9,1,"S";  |C_V #dsmaiw05#10,1,"S";

         aAlfa = "#" + #dsmaiw05#15 + "#";
         |CAMPO_MAXIMO #dsmaiw05#9,1,aAlfa;
         |CAMPO_MAXIMO #dsmaiw05#10,1,aAlfa;

         #dsmaiw05#6 = #dsmaiw05#9;
         #dsmaiw05#7 = #dsmaiw05#10;
         |PINTA #dsmaiw05#6;
         |PINTA #dsmaiw05#7;
     |FINSI;

     |SI #dsmaiw05#8 = "N";
         #dsmaiw05#9  = " " * 40; #dsmaiw05#10 = "z" * 40;
         #dsmaiw05#13 = "01.01.0000";   #dsmaiw05#14 = "31.12.9999";

         |C_M #dsmaiw05#9,1,"N";  |C_M #dsmaiw05#10,1,"N";
         |C_V #dsmaiw05#9,1,"N";  |C_V #dsmaiw05#10,1,"N";
         |C_M #dsmaiw05#13,1,"N"; |C_M #dsmaiw05#14,1,"N";
         |C_V #dsmaiw05#13,1,"N"; |C_V #dsmaiw05#14,1,"N";

         |C_M #dsmaiw05#11,1,"S"; |C_M #dsmaiw05#12,1,"S";
         |C_V #dsmaiw05#11,1,"S"; |C_V #dsmaiw05#12,1,"S";

         nCalc = #dsmaiw05#15 - 3;
         aAlfa  = "-" + ("9" * nCalc) + ".*";
         |CAMPO_MINIMO #dsmaiw05#11,1,aAlfa;
         |CAMPO_MINIMO #dsmaiw05#12,1,aAlfa;

         aAlfa = "9" * #dsmaiw05#15;
         |CAMPO_MAXIMO #dsmaiw05#11,1,aAlfa;
         |CAMPO_MAXIMO #dsmaiw05#12,1,aAlfa;

         #dsmaiw05#6 = #dsmaiw05#11;  |PINTA #dsmaiw05#6;
         #dsmaiw05#7 = #dsmaiw05#12;  |PINTA #dsmaiw05#7;
     |FINSI;

     |SI #dsmaiw05#8 = "F";
         #dsmaiw05#9  = " " * 40;       #dsmaiw05#10 = "z" * 40;
         #dsmaiw05#11 = -9999999.99999; #dsmaiw05#12 = 99999999.99999

         |C_M #dsmaiw05#9,1,"N";  |C_M #dsmaiw05#10,1,"N";
         |C_V #dsmaiw05#9,1,"N";  |C_V #dsmaiw05#10,1,"N";
         |C_M #dsmaiw05#11,1,"N"; |C_M #dsmaiw05#12,1,"N";
         |C_V #dsmaiw05#11,1,"N"; |C_V #dsmaiw05#12,1,"N";

         |C_M #dsmaiw05#13,1,"S"; |C_M #dsmaiw05#14,1,"S";
         |C_V #dsmaiw05#13,1,"S"; |C_V #dsmaiw05#14,1,"S";

         aAlfa  = "01.01.0000";
         |CAMPO_MINIMO #dsmaiw05#13,1,aAlfa;
         |CAMPO_MINIMO #dsmaiw05#14,1,aAlfa;

         aAlfa  = "31.12.9999";
         |CAMPO_MAXIMO #dsmaiw05#13,1,aAlfa;
         |CAMPO_MAXIMO #dsmaiw05#14,1,aAlfa;

         #dsmaiw05#6 = "  " + #dsmaiw05#13;  |PINTA #dsmaiw05#6;
         #dsmaiw05#7 = "  " + #dsmaiw05#14;  |PINTA #dsmaiw05#7;
     |FINSI;
|FPRC;

|PROCESO Tipo07w05; |TIPO 7;
     |HAZ FiltraCampos;

     |SI #dsmaiw05#1 > nMaxLin;
         #dsmaiw05#1 = nMaxLin;
         |PINTA #dsmaiw05#1;
     |FINSI;
|FPRC;

|PROCESO Tipo0w05; |TIPO 0;
     |SI #dsmaiw05#1 > nMaxLin;
         #dsmaiw05#1 = nMaxLin;
         |PINTA #dsmaiw05#1;
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo02w05; |TIPO 2;
   nModo = (FEntrada / 100) ! 0;
   nFlag = 0 +. 1;
   |SI nFlag < 0;
       |HAZ FiltraCampos;
   |FINSI;
|FPRC;

|PROCESO SacaEmpresa;
     aAlfa1 = aCadena - "}}";
     |SI FSalida = 0;  |ACABA;  |FINSI;

     aAlfa1 = aCadena - aApp;
     |SI FSalida = 0;  |ACABA;  |FINSI;

     aBueno = "";
     aAlfa1 = aCadena;   |QBF aAlfa1;
     aLong  = aAlfa1 % 0;
     nLong  = aLong;
     nLlave = 0;
     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPos      = (nCaracter * 100) + 1;
           aCaracter = aAlfa1 % nPos;

           |SI aCaracter = "{";  |O aCaracter = "}";
               nLlave = nLlave + 1;
           |FINSI;

           |SI aCaracter = "}";  |O aCaracter = "{";
               aCaracter = "";
           |FINSI;

           |SI nLlave = 2;
               |SI aCaracter = "[";
                   nLlave = nLlave - 1;
               |FINSI;
           |FINSI;

           |SI nLlave = 2;
               aBueno = aBueno + aCaracter;
           |FINSI;

           |SI nLlave = 1;
              |SI aCaracter = "]";
                   nLlave = nLlave + 1;
               |FINSI;
           |FINSI;
     |SIGUE;

     aAlfa1 = aBueno;   |QBF aAlfa1;
     aLong  = aAlfa1 % 0;
     nLong  = aLong;
     aPara1 = "";
     aPara2 = "";
     aPara3 = "";
     aPara4 = "";
     aPara5 = "";
     nPunte = 1;
     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPos      = (nCaracter * 100) + 1;
           aCaracter = aAlfa1 % nPos;

           |SI aCaracter = " ";
               |SI aPara1 ! "";
                   nPunte = nPunte + 1;
               |FINSI;
           |SINO;
               |SI nPunte = 1;  aPara1 = aPara1 + aCaracter;  |FINSI;
               |SI nPunte = 2;  aPara2 = aPara2 + aCaracter;  |FINSI;
               |SI nPunte = 3;  aPara3 = aPara3 + aCaracter;  |FINSI;
               |SI nPunte = 4;  aPara4 = aPara4 + aCaracter;  |FINSI;
               |SI nPunte = 5;  aPara5 = aPara5 + aCaracter;  |FINSI;
           |FINSI;
     |SIGUE;

     |SI aPara1 ! "0";  |ACABA;  |FINSI;

     aDefEmp = aPara4;
|FPRC;

|PROCESO ExaminaCod;
     |ABRE #dsmail01;
     #dsmail01#0 = #dsmaiz01#0;
     |LEE 000#dsmail01.=;
     |SI FSalida ! 0; |DDEFECTO #dsmail01; |FINSI;

     aAlfa = #dsmail01#6; |QBF aAlfa;
     aAlfa1 = aAlfa % -101;
     aLong = aAlfa % 0; nLong = aLong;
     |SI aAlfa1 = "/"; |O aAlfa1 = "\";
        nCalc = 100 + (nLong - 1);
        aAlfa = aAlfa % nCalc;
     |FINSI;

     aDir   = "";
     aApp   = "";
     aAlfa2 = "";
     |PARA nCont = 1; |SI nCont [ nLong; |HACIENDO nCont = nCont + 1;
        nCalc = - ((nCont * 100) + 1);
        aAlfa1 = aAlfa % nCalc;
        |SI aAlfa1 = "/"; |O aAlfa1 = "\";
           |SI aApp = "";
               aApp = aAlfa2; aLong = aApp % 0;  nCalc  = aLong;
                              aLong = aAlfa % 0; nCalc1 = aLong;
               nCalc = 100 + (nCalc1 - nCalc);
               aDir = aAlfa % nCalc;
               aAlfa2 = "";
               |SAL;
           |FINSI;
        |SINO;
           aAlfa2 = aAlfa1 + aAlfa2;
        |FINSI;
     |SIGUE;

     |SI #dsmail01#7 = "A";
        || Edito el ds.men para buscar la linea de llamada a la aplicacion en
        || cuestion y coger (si lo hay) cual es el def de acceso a empresas

        aDefEmp = "";
        aAlfa   = aDir + "ds.men";
        |XABRE "U", aAlfa, nHandle;
        |SI FSalida < 0;
           aAlfa = "    No encuentro el menu general del basico [" + aDir + "]";
           |MENAV aAlfa;
           |ERROR;
           |ACABA;
        |FINSI;

        |PARA; |SI; |HACIENDO;
             |XLEE nHandle, 255, aCadena;
             |SI FSalida < 0;   |SAL; |FINSI;

             |HAZ SacaEmpresa;
             |SI aDefEmp ! "";  |SAL;  |FINSI;
        |SIGUE;

        |XCIERRA nHandle;

        |SI aDefEmp = "";  aDefEmp = "NULL";  |FINSI;
     |FINSI;

     |SI #dsmail01#7 = "N";
         aDefEmp = "NULL";             ||NO es multiempresa
     |FINSI;

     |SI #dsmail01#7 = "S";
         aDefEmp = #dsmail01#8;
     |FINSI;

     |SI aDefEmp ! "NULL";
        aAlfa = #dsmail01#6; |QBF aAlfa;
        aAlfa = aAlfa + "def/" + aDefEmp + ".mas";
        |CARGA_DEF aAlfa, Empresa@;
        |SI FSalida = 0;
           aAlfa = #dsmail01#6; |QBF aAlfa;
           aAlfa = aAlfa + "fich/"; |PATH_DAT #100 aAlfa;
           |ABRE #Empresa@;
           |ATRI R; |PINTA 0711, " Seleccionar empresa "; |ATRI N;
           nModo = 2 + 4 + 8 + 16; nLin1 = 0811; nLin2 = 1869;
           |LINEAL_SIMPLE #1#Empresa@, nModo, nLin1, nLin2, NULL,NULL,NULL;
           |SI FSalida = 0;
              aAlfa = "|K000"; aAlfa = #Empresa@#aAlfa#;
              aAlfa1 = aAlfa % 0103; aAlfa = aAlfa % 0499;
              #dsmaiz01#1 = aAlfa1;
              aAlfa1 = aAlfa % 0103; aAlfa = aAlfa % 0499;
              #dsmaiz01#2 = aAlfa1; aAlfa2 = "|C" + (("000" + aAlfa1) % -103) + "LONGITUD";
              #dsmaiz01#3 = #Empresa@#aAlfa2#;
              |SI aAlfa ! "";
                 aAlfa1 = aAlfa % 0103; aAlfa = aAlfa % 0499;
                 #dsmaiz01#4 = aAlfa1;
                 aAlfa2 = "|C" + (("000" + #dsmaiz01#4) % -103) + "LONGITUD";
                 #dsmaiz01#5 = #Empresa@#aAlfa2#;
              |FINSI;
              |SI aAlfa ! "";
                 aAlfa1 = aAlfa % 0103; aAlfa = aAlfa % 0499;
                 #dsmaiz01#6 = aAlfa1;
                 aAlfa2 = "|C" + (("000" + #dsmaiz01#6) % -103) + "LONGITUD";
                 #dsmaiz01#7 = #Empresa@#aAlfa2#;
              |FINSI;
           |FINSI;
           |PINTA 0711, "                      ";
           nCampo = 2; aAlfa1 = ""; aAlfa = "";
           |PARA nCont = 0; |SI nCont < #dsmaiz01#1; |HACIENDO nCont = nCont + 1;
              nCalc = 2 + (nCont * 2); nCalc1 = nCalc + 1;
              nCalc = #dsmaiz01#nCalc#; aAlfa3 = "0" * #dsmaiz01#nCalc1#; nCalc1 = -(100 + #dsmaiz01#nCalc1#);
              aEmpresa = aEmpresa + ((aAlfa3 + #Empresa@#nCalc#) % nCalc1);
           |SIGUE;
           |CIERRA #Empresa@; |DESCARGA_DEF #Empresa@;
        |SINO;
           aAlfa = "    No puedo encontrar el fichero de empresas [" + aAlfa + "]";
           |MENAV aAlfa;
        |FINSI;
     |SINO;
        aEmpresa = "";
     |FINSI;
|FPRC;

|PROCESO RellenaTempo;
     |DDEFECTO #dsmaiw05;
     #dsmaiw05#1 = #dsmail03#1;
     #dsmaiw05#2 = #dsmail03#2;
     #dsmaiw05#3 = #dsmail03#3;
     #dsmaiw05#4 = #dsmail03#4;
     #dsmaiw05#5 = #dsmail03#5;
     #dsmaiw05#6 = #dsmail03#6;
     #dsmaiw05#7 = #dsmail03#7;
     #dsmaiw05#8 = #dsmail03#8;
     #dsmaiw05#9 = #dsmail03#9;
     #dsmaiw05#10 = #dsmail03#10;
     #dsmaiw05#11 = #dsmail03#11;
     #dsmaiw05#12 = #dsmail03#12;
     #dsmaiw05#13 = #dsmail03#13;
     #dsmaiw05#14 = #dsmail03#14;
     #dsmaiw05#15 = #dsmail03#15;
     |GRABA 020#dsmaiw05.n;

     nMaxLin = #dsmaiw05#1;
|FPRC;

|DEFBUCLE RellenaTempo;
     #dsmail03#1;
     ;
     #dsmaiz01#0, 01;
     #dsmaiz01#0, 99;
     ;
     NULL,RellenaTempo;
|FIN;


|RUTINA infw05;
   #dsmaiw05#1 = 01;
|FRUT;

|RUTINA supw05;
   #dsmaiw05#1 = 99;
|FRUT;

|PROGRAMA;
   aIP = ""; |IP_REMOTO aIP;
   |ABRE #dsmail01;
   |ABRE #dsmail02;
   |ABRE #dsmail03;

   |CLS;
   |CABEZA " Emision mailings ";
   |PINDA #0#0; |PINPA #0#0; |PINTA  0631, #dsmail01#1;
   |ENDATOS #1#0;
   |SI FSalida = 0;
      |DELINDEX #dsmaiw05; |ABRE #dsmaiw05;
      |BUCLE RellenaTempo;
      |CIERRA #dsmaiw05;
      |PARA;  |SI;  |HACIENDO;
           |PINPA #0#dsmaiw05;
           |ENTLINEAL #1#dsmaiw05,-1, 2, 21, 0, infw05, supw05;

                             Menu6 = " Emitir ";
           |SI #10#4 = "C";  Menu6 = " Enviar ";  |FINSI;

           |MENU Menu;
           nOpcion = FSalida;

           |SI nOpcion < 1; |O nOpcion > 3;
               |SAL;
           |FINSI;

           |SI nOpcion = 2;
               |HAZ Emite;
               |HAZ BorraDef;
               |SAL;
           |FINSI;

           |SI nOpcion = 3;
               |MENAV "    Proceso abortado ";
               |SAL;
           |FINSI;
      |SIGUE;
   |FINSI;

   |CIERRA #dsmail03;
   |CIERRA #dsmail02;
   |CIERRA #dsmail01;
|FPRO;
