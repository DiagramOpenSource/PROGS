|| Este proceso programa tareas para que se ejecute todos los dias
|| a las HH:MM programados.

|| Primero miro si ya existe la tarea, si existe me salgo, sino la crea
|| una vez creada actualiza el tareas.srv, para que la tenga en cuenta

|FICHEROS;
     crontcom;

     dsapum02@;
|FIN;

|VARIABLES;
     nHandleLista   = 0;
     nSwCreaTareas  = 0;
     nSwModoUBasico = 0;
     nSwEsta        = 0;
     nNumero        = 0;
     nMayor         = 0;
     nCanalLista    = 0;
     nAprobacion    = 0;
     nSwEstaTarea   = 0;
     nLinea         = 0;
     nExiste        = 0;
     nContador      = 0;
     nRegis         = 0;
     nHandle        = 0;
     nLong          = 0;
     nPos           = 0;
     nCanal         = 0;
     nBufUsr        = 0;
     nEleUsr        = 0;

     aBusca         = "";
     aTab           = "";
     aTexto         = "";
     aSistema       = "";
     aReturn        = "";
     aReturnDos     = "";
     aMensaje       = "";
     aBaseBasico    = "";
     aComandoDOS    = "";
     aPathSh        = "";
     aFicBat        = "";
     aFicLanzaBat   = "";
     aTitulo        = "";
     aDBass         = "";
     aShell         = "";
     aAlfa          = "";
     aDirBase       = "";
     aLong          = "";
     aAplicacion    = "";
     aDefEmpre      = "";
     aAlfa2         = "";
     aAlfa3         = "";
     aNumEmpre      = "";
     aTAB           = "";
     aParam         = "";
     aAbre          = "";
     aMin           = "";
     aHora          = "";
     aDia_mes       = "";
     aMes           = "";
     aDia_Sem       = "";
     aComando       = "";
     aBase          = "";
     aAlfa1         = "";
     aLinea         = "";
     aLista         = "";
     aCR            = "";
     aLF            = "";
     aCadena        = "";
     aPrograma      = "";
     aTabProg       = "";
     aAplProg       = "";
     aMinProg       = "";
     aHorProg       = "";
     aDiaProg       = "";
     aMesProg       = "";
     aDSeProg       = "";
     aParProg       = "";
     aAse           = "";
     aMas           = "";
     aBasApli       = "";

     &enLin         = 0;
     &eaCmp8        = "";
|FIN;

|PROCESO crontcom_tarea;
     |SI #crontcom#23 ! "S";  |ACABA;  |FINSI;

     aTab   = #crontcom#20;  |QBF aTab;
     aTexto = #crontcom#22;  |QBF aTexto;

     |SI aTab = aTabProg; |Y aTexto = "Tarea Generada por DSTOOL (Est.).";
         nSwEstaTarea = 1;
         |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE crontcom_tarea;
     #crontcom#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, crontcom_tarea;
|FIN;

|PROCESO MiraSiTarea;
     |BUCLE crontcom_tarea;
|FPRC;

|PROCESO DameLineaLibre;
     || Este proceso ira buscando una a una las lineas libres
     || para ir ocupandolas

     |PARA nContador = nLinea; |SI nContador [ 999; |HACIENDO nContador = nContador + 1;
           #crontcom#0 = 0;
           #crontcom#1 = nContador;
           |LEE 000#crontcom.=;
           |SI FSalida ! 0;
               nLinea = nContador;
               |SAL;
           |FINSI;
     |SIGUE;

     |SI nContador ] 999;
          aMensaje = "0000Error: Contador de Líneas en el crontcom, ha llegado a las 999.";
          |MENAV aMensaje;
     |FINSI;
|FPRC;


|| -------------- CONSTRUIMOS UNA SHELL O UN BAT

|PROCESO PorDSTOOL;
     |SI #crontcom#23 = "S"; |Y aTab = aTabProg; |Y aTexto = "Tarea Generada por DSTOOL (Est.).";
          aLista = "/u/basico/lista_esta.txt";
          |XABRE "U", aLista, nCanalLista;
          |SI FSalida ] 0;
               |PARA; |SI; |HACIENDO;
                    |XLEE nCanalLista, 250, aLinea;
                    |SI FSalida < 0;
                         |SAL;
                    |FINSI;
                    aDirBase = aLinea;
                    aLong = aDirBase % 0;
                    nLong = aLong;
                    aAlfa = aDirBase;
                    nPos = 100 + nLong - 1;
                    aDirBase = aAlfa % nPos;

                    aAlfa = aDBass + "bin/serverds @" + aDirBase + " " + &34 + "/!:" + aAplicacion +"/"+ aTAB;
                    |SI aDefEmpre ! "";
                         aAlfa = aAlfa + " " + aDefEmpre;
                    |FINSI;
                    |SI aNumEmpre ! "";
                         aAlfa = aAlfa + " " + aNumEmpre;
                    |FINSI;
                    |SI aParam ! "";
                         aAlfa = aAlfa + ";" + aParam;
                    |FINSI;
                    aAlfa = aAlfa + &34 + aAlfa3;
                    |XGRABA nHandle, aAlfa;
               |SIGUE;
               |XCIERRA nCanalLista;
          |SINO;
               aAlfa = aDBass + "bin/serverds @" + aDirBase + " " + &34 + "/!:" + aAplicacion +"/"+ aTAB;
               |SI aDefEmpre ! "";
                    aAlfa = aAlfa + " " + aDefEmpre;
               |FINSI;
               |SI aNumEmpre ! "";
                    aAlfa = aAlfa + " " + aNumEmpre;
               |FINSI;
               |SI aParam ! "";
                    aAlfa = aAlfa + ";" + aParam;
               |FINSI;
               aAlfa = aAlfa + &34 + aAlfa3;
               |XGRABA nHandle, aAlfa;
          |FINSI;
     |SINO;
          aAlfa = aDBass + "bin/serverds @" + aDirBase + " " + &34 + "/!:" + aAplicacion +"/"+ aTAB;
          |SI aDefEmpre ! "";
               aAlfa = aAlfa + " " + aDefEmpre;
          |FINSI;
          |SI aNumEmpre ! "";
               aAlfa = aAlfa + " " + aNumEmpre;
          |FINSI;
          |SI aParam ! "";
               aAlfa = aAlfa + ";" + aParam;
          |FINSI;
          aAlfa = aAlfa + &34 + aAlfa3;
          |XGRABA nHandle, aAlfa;
     |FINSI;
|FPRC;

|PROCESO PorAplica;
     |DBASS aBasApli;
     aLong = aBasApli % 0;
     nLong = aLong;
     aAlfa = aBasApli;
     nPos  = 100 + nLong - 1;
     aBasApli = aAlfa % nPos;

     aAlfa = aDBass + "bin/serverds @" + aBasApli + " " + &34 + "/!:" + aAplicacion +"/"+ aTAB;
     |SI aDefEmpre ! "";
          aAlfa = aAlfa + " " + aDefEmpre;
     |FINSI;

     |SI aNumEmpre ! "";
         aAlfa = aAlfa + " " + aNumEmpre;
     |FINSI;

     |SI aParam ! "";
         aAlfa = aAlfa + ";" + aParam;
     |FINSI;

     aAlfa = aAlfa + &34 + aAlfa3;
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO ShellLinux;
     aTitulo     = #crontcom#22; |QBF aTitulo;

     aDBass = aBaseBasico;
     aShell = aDBass + "bin/lanza" + nRegis + ".sh";
     |XABRE "WB", aShell, nHandle;
     aAlfa = "#" + aTitulo + &10;
     |XGRABA nHandle, aAlfa;

     aAlfa = aDBass + "bin/cron" + nRegis  + ".sh &" + &10;
     |XGRABA nHandle, aAlfa;
     |XCIERRA nHandle;
     aAlfa = "chmod 777 " + aShell;
     |SYSTEM aAlfa;

     aShell = aDBass + "bin/cron" + nRegis  + ".sh";
     |XABRE "WB", aShell, nHandle;
     aDirBase    = #crontcom#16; |QBF aDirBase;
     aLong = aDirBase % 0;
     nLong = aLong;
     aAlfa = aDirBase;
     nPos = 100 + nLong - 1;
     aDirBase    = aAlfa % nPos;
     aAplicacion = #crontcom#17; |QBF aAplicacion;
     aDefEmpre   = #crontcom#18; |QBF aDefEmpre;
     aNumEmpre   = #crontcom#19; |QBF aNumEmpre;
     aTAB        = #crontcom#20; |QBF aTAB;
     aParam      = #crontcom#21; |QBF aParam;

     aAlfa = "#" + aTitulo + &10;
     |XGRABA nHandle, aAlfa;
     aAlfa2 = &10;
     aAlfa3 = &10;
     |SI #crontcom#25 = "S";
          aAlfa = "echo Iniciando la ejecucion:";

          ||aAlfa2 Resetea los logs
          aAlfa2 = " >" + aDBass + "bin/cron" + nRegis + ".log 2>" + aDBass + "bin/cron" + nRegis + ".err" + &10;

          ||aAlfa3 Anyade info a los logs
          aAlfa3 = " >>" + aDBass + "bin/cron" + nRegis + ".log 2>>" + aDBass + "bin/cron" + nRegis + ".err" + &10;

          aAlfa = aAlfa + aAlfa2;
          |XGRABA nHandle, aAlfa;

          aAlfa = "date";
          aAlfa = aAlfa + aAlfa3;
          |XGRABA nHandle, aAlfa;
     |FINSI;

     aTab = #crontcom#20;
     |QBF aTab;
     aTexto = #crontcom#22
     |QBF aTexto;

     |SI aTabProg = "dsapuz02";
         |HAZ PorAplica;
     |SINO;
         |HAZ PorDSTOOL;
     |FINSI;

     |SI #crontcom#25 = "S";
          aAlfa = "echo Finalizada la ejecucion:";
          aAlfa = aAlfa + aAlfa3;
          |XGRABA nHandle, aAlfa;

          aAlfa = "date";
          aAlfa = aAlfa + aAlfa3;
          |XGRABA nHandle, aAlfa;
     |FINSI;
     |XCIERRA nHandle;
     aAlfa = "chmod 777 " + aShell;
     |SYSTEM aAlfa;

     aShell = aDBass + "bin/lanza" + nRegis + ".sh";
|FPRC;

|PROCESO Bat;
     |DBASS aDBass; |QBF aDBass;
     aShell = aDBass + "bin/cron" + nRegis  + ".bat";
     |XABRE "WB", aShell, nHandle;
     aDirBase    = #crontcom#16; |QBF aDirBase;
     aLong = aDirBase % 0;
     nLong = aLong;
     aAlfa = aDirBase;
     nPos = 100 + nLong - 1;
     aDirBase    = aAlfa % nPos;
     aAplicacion = #crontcom#17; |QBF aAplicacion;
     aDefEmpre   = #crontcom#18; |QBF aDefEmpre;
     aNumEmpre   = #crontcom#19; |QBF aNumEmpre;
     aTAB        = #crontcom#20; |QBF aTAB;
     aParam      = #crontcom#21; |QBF aParam;

     aAlfa = "@echo off" + &13 + &10;
     |XGRABA nHandle, aAlfa;

     aAlfa = "echo Iniciando la ejecucion:";

     ||aAlfa2 Resetea los logs
     aAlfa2 = " >" + aDBass + "bin/cron" + nRegis + ".log 2>" + aDBass + "bin/cron" + nRegis + ".err" + &13 + &10;

     ||aAlfa3 Anyade info a los logs
     aAlfa3 = " >>" + aDBass + "bin/cron" + nRegis + ".log 2>>" + aDBass + "bin/cron" + nRegis + ".err" + &13 + &10;

     aAlfa = aAlfa + aAlfa2;
     |XGRABA nHandle, aAlfa;

     aAlfa = "time /T";
     aAlfa = aAlfa + aAlfa3;
     |XGRABA nHandle, aAlfa;

     aAlfa = aDBass + "bin/diagramx.exe /DS:" + &34 + "@" + aDirBase + " $$$batch,batch# //(" + aAplicacion +")"+ aTAB;
     |SI aDefEmpre ! "";
          aAlfa = aAlfa + "&" + aDefEmpre;
     |FINSI;
     |SI aNumEmpre ! "";
          aAlfa = aAlfa + "&" + aNumEmpre;
     |FINSI;
     |SI aParam ! "";
          aAlfa = aAlfa + ";" + aParam;
     |FINSI;
     aAlfa = aAlfa + &34 + aAlfa3;
     |XGRABA nHandle, aAlfa;

     aAlfa = "echo Finalizada la ejecucion:";
     aAlfa = aAlfa + aAlfa3;
     |XGRABA nHandle, aAlfa;

     aAlfa = "time /T";
     aAlfa = aAlfa + aAlfa3;
     |XGRABA nHandle, aAlfa;

     |XCIERRA nHandle;
|FPRC;


|| -------------- ESCRIBE EN EL FICHERO

|PROCESO Escribe;
     aMin     = #crontcom#2;    |QBF aMin;
     aHora    = #crontcom#3;    |QBF aHora;
     aDia_mes = #crontcom#4;    |QBF aDia_mes;
     aMes     = #crontcom#5;    |QBF aMes;
     aDia_Sem = #crontcom#6;    |QBF aDia_Sem;
     aComando = #crontcom#7;    |QBF aComando;
     aTitulo  = #crontcom#22;   |QBF aTitulo;


     aAlfa = "#" + aTitulo + &10;
     |XGRABA nCanal, aAlfa;

     |SI #crontcom#8 = "S";
          aAlfa = aMin + " " + aHora + " " + aDia_mes + " " + aMes + " " + aDia_Sem + " " + aComando + &13 + &10;
     |SINO;
          aAlfa = "#" + aMin + " " + aHora + " " + aDia_mes + " " + aMes + " " + aDia_Sem + " " + aComando + &13 + &10;
     |FINSI;
     |XGRABA nCanal, aAlfa;
|FPRC;

|DEFBUCLE crontcom;
     #crontcom#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Escribe;
|FIN;

|PROCESO crontcomShells;
     |SI #crontcom#8  = "N"; |ACABA; |FINSI;
     |SI #crontcom#15 = "N"; |ACABA; |FINSI;

     nRegis   = #crontcom#1;
     aTabProg = #crontcom#20;   |QBF aTabProg;

     |SI aSistema = "ESDOS";
          |HAZ Bat;
     |FINSI;

     |SI aSistema = "ESUNIX"; |O aSistema = "ESBSD";
          |HAZ ShellLinux;
     |FINSI;
|FPRC;

|DEFBUCLE crontcomShells;
     #crontcom#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, crontcomShells;
|FIN;

|| -------------- GRABA EL FICHERO "TAREAS.SRV" -----------------

|PROCESO GeneraTareasSrv;
     aAbre = aBaseBasico;
     aAbre = aAbre + "bin/tareas.srv";
     |XABRE "WB", aAbre, nCanal;
     aDBass = aBaseBasico;
     aAlfa = aDBass + "cron";
     |MKDIR aAlfa;
     |BUCLE crontcom;
     |XCIERRA nCanal;

     ||Regenera las shells a tab's. por si hay cambios (p.e. el tema de los logs y >> o >)

     |BUCLE crontcomShells;
|FPRC;


||-------------- CREACIO DE USUARIO BATCH -------------------

|PROCESO BuscaSecuencial;
     aAlfa = aBase + "dev/usr/ds_sys.psw"
     |LEESECUENCIAL aAlfa, nBufUsr, nEleUsr, 0, 0;
     |PARA nRegis = 0; |SI nRegis < nEleUsr; |HACIENDO nRegis = nRegis + 1;
           |DEL_BUF nBufUsr, nRegis, aAlfa;

           aAlfa1 = aAlfa % 410;
           |QBF aAlfa1;
           |SI aAlfa1 = "batch";
                 nSwEsta = 1;
                 |SAL;
           |FINSI;

           aAlfa1 = aAlfa % 103;
           nNumero = aAlfa1;

           |SI nMayor < nNumero;
               nMayor = nNumero;
           |FINSI;
     |SIGUE;

     |FINDIM nBufUsr;
|FPRC;

|PROCESO MeteBatch;
     aAbre = aBase + "dev/usr/ds_sys.psw"
     |XABRE "A", aAbre, nHandle;

     aAlfa = ("000" + nMayor) % -103;
     aAlfa = aAlfa + "batch     HOHMIYHQIACMCMCMCMCMN";
     |XGRABA nHandle, aAlfa;

     |XCIERRA nHandle;
|FPRC;

|PROCESO ControlBatch;
     |DBASS aBase;  |QBF aBase;
     |HAZ BuscaSecuencial;
     |SI nSwEsta = 0;
          nMayor = nMayor + 1;
          |HAZ MeteBatch;
     |FINSI;
|FPRC;

|PROCESO EstaYaEnLista;
     |XABRE "AB", aLista, nHandleLista;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     aCR        = &10;
     aLF        = &13;
     aCadena    = "";

     |XLEE nHandleLista, 1, aAlfa;
     |PARA; |SI FSalida > 0; |HACIENDO;
            |SI aAlfa = aCR;
                |SI aCadena = aPrograma;
                    nAprobacion = 1;
                    |SAL;
                |FINSI;

                aCadena = "";
            |SINO;
                |SI aAlfa ! aLF;
                    aCadena = aCadena + aAlfa;
                |FINSI;
            |FINSI;
            |XLEE nHandleLista, 1, aAlfa;
     |SIGUE;

     ||PARA QUE DETECTE TB. SI NO SE TERMINA LA LINEA
     |SI aCadena = aPrograma;
          nAprobacion = 1;
     |FINSI;

     |XCIERRA nHandleLista;
|FPRC;

|PROCESO MeteEnListaEsta;
     aLista = "/u/basico/ctrlversion.txt";
     |XABRE "E", aLista, 0;
     |SI FSalida < 0;
         |XABRE "W", aLista, nHandleLista;
         aLinea = "NO BORRAR";
         |XGRABA nHandleLista, aLinea;
         |XCIERRA nHandleLista;

          nSwCreaTareas = 1;
     |FINSI;

     |DBASS aPrograma;
     |QBF aPrograma;
     aLista = "/u/basico/lista_esta.txt";

     nAprobacion = 0;
     |HAZ EstaYaEnLista;
     |SI nAprobacion = 0;
          |XABRE "A", aLista, nHandleLista;
          |SI FSalida ] 0;
               aLinea = aPrograma;
               |XGRABA nHandleLista, aLinea;
          |FINSI;
          |XCIERRA nHandleLista;
          nSwCreaTareas = 1;
     |FINSI;
|FPRC;

|PROCESO GuardaTarea;
     aTab = &9;

     |SI nSwModoUBasico = 0;
         |SI aSistema = "ESDOS";
             |HAZ ControlBatch;
         |FINSI;

         |DBASS aBaseBasico;
         |QBF aBaseBasico;
     |FINSI;

     |ABRE #crontcom;

     ||Controlar el numero de linea.
     nLinea = 1;
     |HAZ DameLineaLibre;

     |DDEFECTO #crontcom;
     #crontcom#0 = 0;
     #crontcom#1 = nLinea;
     |LEE 101#crontcom.=;
     |SI FSalida ! 0;
         #crontcom#2  = aMinProg;
         #crontcom#3  = aHorProg;
         #crontcom#4  = aDiaProg;
         #crontcom#5  = aMesProg;
         #crontcom#6  = aDSeProg;
         #crontcom#8  = "S";
         #crontcom#9  = #crontcom#2;
         #crontcom#10 = #crontcom#3;
         #crontcom#11 = #crontcom#4;
         #crontcom#12 = #crontcom#5;
         #crontcom#13 = #crontcom#6;
         #crontcom#14 = #crontcom#7;
         #crontcom#15 = "S";
         #crontcom#16 = aBaseBasico;
         #crontcom#17 = aAplProg;
         #crontcom#20 = aTabProg;
         #crontcom#21 = aParProg;
         #crontcom#22 = "Tarea Generada por DSTOOL (Est.).";
         #crontcom#23 = "S";

         nRegis = #crontcom#1;
         |SI aSistema = "ESDOS";
             |HAZ Bat;
         |SINO;
             |HAZ ShellLinux;
         |FINSI;
         #crontcom#7 = aShell;

         |GRABA 020#crontcom.n;
         |LIBERA #crontcom;

         |HAZ GeneraTareasSrv;
     |FINSI;
     |CIERRA #crontcom;
|FPRC;

|PROCESO GeneraTarea;
     nSwEstaTarea = 0;
     |HAZ MiraSiTarea;

     |SI nSwEstaTarea = 0;
         |HAZ GuardaTarea;

         |ACABA;
     |FINSI;

     |SI nSwCreaTareas = 1;
         |HAZ GeneraTareasSrv;
     |FINSI;
|FPRC;

|PROCESO MeteDSAPUZ01;
     |DBASS aBasApli;

     aAplProg = "contagen";
     aTabProg = "dsapuz02";
     aMinProg = #dsapum02@#2;
     aHorProg = #dsapum02@#1;
     aDiaProg = #dsapum02@#4;
     aMesProg = #dsapum02@#3;
     aDSeProg = #dsapum02@#5;
     aParProg = "";
     nLinea   = enLin;

     |ABRE #crontcom;
     #crontcom#0 = 0;
     #crontcom#1 = nLinea;
     |LEE 000#crontcom.=;
     |SI FSalida ! 0;
         nLinea = 1;
         |HAZ DameLineaLibre;
     |FINSI;

     #crontcom#0 = 0;
     #crontcom#1 = nLinea;
     |LEE 101#crontcom.=;
     |SI FSalida ! 0;
         |DDEFECTO #crontcom;
         #crontcom#0 = 0;
         #crontcom#1 = nLinea;
         #crontcom#8 = "S";
         |GRABA 020#crontcom.b;
     |FINSI;

     #crontcom#2  = aMinProg;
     #crontcom#3  = aHorProg;
     #crontcom#4  = aDiaProg;
     #crontcom#5  = aMesProg;
     #crontcom#6  = aDSeProg;
     #crontcom#9  = #crontcom#2;
     #crontcom#10 = #crontcom#3;
     #crontcom#11 = #crontcom#4;
     #crontcom#12 = #crontcom#5;
     #crontcom#13 = #crontcom#6;
     #crontcom#14 = #crontcom#7;
     #crontcom#15 = "S";
     #crontcom#16 = aBasApli;
     #crontcom#17 = aAplProg;
     #crontcom#20 = aTabProg;
     #crontcom#21 = aParProg;
     #crontcom#22 = "Tarea Generada por contagen (desde dsapum02).";
     #crontcom#23 = "N";

     nRegis = #crontcom#1;
     |SI aSistema = "ESDOS";
         |HAZ Bat;
     |SINO;
         |HAZ ShellLinux;
     |FINSI;

     #crontcom#7 = aShell;

     |GRABA 020#crontcom.a;
     |LIBERA #crontcom;

     |HAZ GeneraTareasSrv;
     |CIERRA #crontcom;
|FPRC;

|PROCESO TareaDSAPUZ01;
     |DBASS aDBass; |QBF aDBass;
     aMas = aDBass + "contagen/def/dsapum02.mas";
     |XABRE "E", aMas, 0;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     |CARGA_DEF aMas, dsapum02@;
     |ABRE #dsapum02@;
     |LEE 000#dsapum02@.p;
     |SI FSalida = 0;
         |HAZ MeteDSAPUZ01;

         #dsapum02@#6 = 0;
         #dsapum02@#7 = nLinea;
         |GRABA 020#dsapum02@.a;
         |LIBERA #dsapum02@;
     |FINSI;
     |CIERRA #dsapum02@;
     |DESCARGA_DEF #dsapum02@;
|FPRC;

|PROGRAMA;
     aSistema = "ESDOS";
     |QUE_SISTEMA aSistema;
     |SI aSistema = "ESDOS";
          aSistema = "ESDOS";
          aReturn = &13 + &10;     ||DOS.
     |SINO;
          aSistema = "ESUNIX";
          aReturn = &10;           ||Unix
     |FINSI;

     nSwCreaTareas = 0;

     nSwModoUBasico = 0;
     |SI aSistema = "ESUNIX";
          aBusca = "/u/basico/ds.ini";
          |XABRE "E", aBusca, nHandle;
          |SI FSalida ] 0;
               nSwModoUBasico = 1;
          |FINSI;
     |FINSI;

     |SI nSwModoUBasico = 1;
          aAlfa = "/u/basico/agitool/fich/";
          |PATH_DAT #crontcom#aAlfa#;

          aBaseBasico = "/u/basico/";
     |FINSI;

     |SI nSwModoUBasico = 1;
          |HAZ MeteEnListaEsta;
     |FINSI;

     |SI PARAMETRO = "VDSAPUZ01";
         |ABRE #crontcom;
         #crontcom#0 = 0;
         #crontcom#1 = enLin;
         |LEE 000#crontcom.=;
         |SI FSalida ! 0;  |DDEFECTO #crontcom;  |FINSI;
         |CIERRA #crontcom;

         eaCmp8 = #crontcom#8;

         |ACABA;
     |FINSI;

     |SI PARAMETRO = "ADSAPUZ01";
         |ABRE #crontcom;
         #crontcom#0 = 0;
         #crontcom#1 = enLin;
         |LEE 000#crontcom.=;
         |SI FSalida = 0;
             |SI #crontcom#8 = "S";
                 #crontcom#8 = "N";
             |SINO;
                 #crontcom#8 = "S";
             |FINSI;

             |GRABA 020#crontcom.a;
             |LIBERA #crontcom;
         |FINSI;
         |CIERRA #crontcom;

         eaCmp8 = #crontcom#8;

         |HAZ TareaDSAPUZ01;

         |ACABA;
     |FINSI;

     |SI PARAMETRO = "GDSAPUZ01";
         |HAZ TareaDSAPUZ01;

         |ACABA;
     |FINSI;

     aAplProg = "agitool";
     aTabProg = "dsctdz01";
     aMinProg = "00";
     aHorProg = "12";
     aDiaProg = "*";
     aMesProg = "*";
     aDSeProg = "*";
     aParProg = "";
     |HAZ GeneraTarea;

     aAplProg = "agitool";
     aTabProg = "regcam01";
     aMinProg = "30";
     aHorProg = "11";
     aDiaProg = "*";
     aMesProg = "*";
     aDSeProg = "*";
     aParProg = "ACTUALIZA";
     |HAZ GeneraTarea;
|FPRO;
