|FICHEROS;
     dstraf01 #0;
     dstraf02 #2;
|FIN;

|VARIABLES;
     aBase      = "";
     aAbre      = "";
     aAlfa      = "";
     aAlfa1     = "";
     aPathLog   = "";
     aDIP       = "";
     aHIP       = "";
     aParametro = "";

     nHandle    = 0;
     nConta     = 0;
     nSwHay     = 0;

     &eaIP      = "";
     &efFecha   = @;
     &enTotal1  = 0;
     &enTotal2  = 0;
     &enTotal3  = 0;
     &enTotal4  = 0;
|FIN;

|PROCESO TipoOrden;
     |C_M #0#11, 1, "S";
     |SI #0#10 = "F";
         |C_M #0#11, 1, "N";
         #0#11 = "D";
         |PINTA #0#11;
     |FINSI;
|FPRC;

|PROCESO dstraf02;
     |SI #0#11 = "D";
         nSwHay = 1;
         |PRINT;
         |ACABA;
     |FINSI;

     |SI eaIP ! #2#0;  |Y nSwHay = 1;
         |PRINT;

         enTotal1 = 0;
         enTotal2 = 0;
         enTotal3 = 0;
         enTotal4 = 0;
     |FINSI;

     enTotal1 = enTotal1 + #2#4;
     enTotal2 = enTotal2 + #2#5;
     enTotal3 = enTotal3 + #2#6;
     enTotal4 = enTotal4 + #2#7;

     eaIP     = #2#0;
     efFecha  = #2#1;

     nSwHay   = 1;
|FPRC;

|DEFBUCLE dstraf02I;
     #2#1;
     ;
     aDIP, #0#0, "     ", 001;
     aHIP, #0#1, "zzzzz", 999;
     ;
     NULL, dstraf02;
|FIN;

|DEFBUCLE dstraf02F;
     #2#2;
     ;
     #0#0, "     ", aDIP, 001;
     #0#1, "zzzzz", aHIP, 999;
     ;
     NULL, dstraf02;
|FIN;

|PROCESO Tipo3;  |TIPO 3;
     |DBASS aBase;  |QBF aBase;
     aAbre = aBase + "ds.ini";

     |XABRE "", aAbre, nHandle;

     aPathLog = "";
     |PARA;  |SI  FSalida ] 0;  |HACIENDO;
             |XLEE nHandle, 254, aAlfa;
             |SI FSalida < 0;  |SAL;  |FINSI;
             aAlfa    = aAlfa < aAlfa;
             aAlfa1   = aAlfa - "log_trafico_tcp=";
             |SI aAlfa1 ! aAlfa;
                 aPathLog = aAlfa1;
                 |SAL;
             |FINSI;
     |SIGUE;
     |XCIERRA  nHandle;

     |SI aPathLog = "";
         |MENAV "      No esta configurado el LOG_TRAFICO_TCP en el DS.INI";
         |ACABA;
     |FINSI;

     |ABRE #2;  |CIERRA #2;  |DELINDEX #2;

     |ABRE #2;
     aAbre = aPathLog;  |QBF aAbre;
     |XABRE "", aAbre, nHandle;

     |PARA;  |SI  FSalida ] 0;  |HACIENDO;
             |XLEE nHandle, 254, aAlfa;
             |SI FSalida < 0;  |SAL;  |FINSI;

             aAlfa1 = aAlfa % 2215;    #2#0 = aAlfa1;
             aAlfa1 = (aAlfa % 3902) + "." + (aAlfa % 4202) + ".20" + (aAlfa % 4602);
                                       #2#1 = aAlfa1;
             aAlfa1 = aAlfa % 4905;    #2#2 = aAlfa1;

             nConta = 1;
             |PARA;  |SI;  |HACIENDO;
                  #2#3 = nConta;
                  |LEE 040#2=;
                  |SI FSalida ! 0;  |SAL;  |FINSI;
                  nConta = nConta + 1;
             |SIGUE;

             |DDEFECTO #2;
             aAlfa1 = aAlfa % 2215;    #2#0 = aAlfa1;
             aAlfa1 = (aAlfa % 3902) + "." + (aAlfa % 4202) + ".20" + (aAlfa % 4602);
                                       #2#1 = aAlfa1;
             aAlfa1 = aAlfa % 4905;    #2#2 = aAlfa1;
                                       #2#3 = nConta;
             aAlfa1 = aAlfa % 5910;    #2#4 = aAlfa1;
             aAlfa1 = aAlfa % 7010;    #2#5 = aAlfa1;
             aAlfa1 = aAlfa % 8610;    #2#6 = aAlfa1;
             aAlfa1 = aAlfa % 9710;    #2#7 = aAlfa1;

             |GRABA 020#2n;
             |LIBERA #2;
     |SIGUE;
     |XCIERRA  nHandle;
     |CIERRA #2;

     |SI aParametro ! "";
         |ABRE #2;
         |SELECT #2#2;
         |LEE 040#2p;
         |CONSULTA_CLAVE #2#2;
         |CIERRA #2;
         |ACABA;
     |FINSI;

     nSwHay = 0;
     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI #0#10 = "I";  |Y #0#11 = "D";  |INFOR "dstraf01";  |FINSI;
     |SI #0#10 = "I";  |Y #0#11 = "R";  |INFOR "dstraf02";  |FINSI;
     |SI #0#10 = "F";  |Y #0#11 = "D";  |INFOR "dstraf03";  |FINSI;

     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

     aDIP =        (("000" + #0#2) % -103) + ".";
     aDIP = aDIP + (("000" + #0#3) % -103) + ".";
     aDIP = aDIP + (("000" + #0#4) % -103) + ".";
     aDIP = aDIP + (("000" + #0#5) % -103);

     aHIP =        (("000" + #0#6) % -103) + ".";
     aHIP = aHIP + (("000" + #0#7) % -103) + ".";
     aHIP = aHIP + (("000" + #0#8) % -103) + ".";
     aHIP = aHIP + (("000" + #0#9) % -103);

     |SI #0#10 = "I";
         |BUCLE dstraf02I;
     |SINO;
         |BUCLE dstraf02F;
     |FINSI;

     |SI nSwHay ! 0;
         |SI #0#11 = "R";
             |PRINT;
         |FINSI;

         |PIEINF;
     |SINO;
         |MENAV "      Seleccion vacia.";
     |FINSI;

     |FININF;
     |FINIMP;
|FPRC;

|PROGRAMA;
     aParametro = PARAMETRO;  |QBT aParametro;
     |SI aParametro ! "";
         |HAZ Tipo3;
     |SINO;
         |MANTE #0;
     |FINSI;
|FPRO;
