|| ademas de los parametros que puede recibir como hasta ahora
|| ahora podra recibir 2 nuevos parametros

|| AUTOSQL
|| AUTOSQLINDEXACION

|| AUTOSQL. No muestra pantalla ni similares y crea las .sql que le correspondan
|| AUTOSQLINDEXACION. Es una INDEXACION, sin mostrar pantalla


|FICHEROS;
  :/agitool/def/rwindex #0;
  :/agitool/def/rwindlog #1;
  fichero@ #100;
|FIN;

|VARIABLES;
  nModoOculto = "";
  aParametro = "";
  aBusco = "";
  nConta = 0;
  aHora = "";
  aTecla = "";
  aFichero = "";
  aplicacion = "";
  dirbase = "";
  dirempresa = "";
  dirfichero = "";
  empresa = "";
  total = 0;
  ri = 0;
  temporal1 = "";
  temporal2 = "";
  temporal3 = "";
  temporal4 = "";
  temporal5 = "";
  handle = "";
  i = 0;
  total_lineas = 0;
  total_campos = 0;
  linea = 0;
  ant_linea = 0;
  posicion = 0;
  pintar = 0;
  {4500}fichero = "";

  dolar1 = "";
  dolar2 = "";
  alfa1 = "";
  para1 = 0;
  elsw1 = 0;
  topep = 0;
  punto_coma = "";
  primero = "";
  sw_paso = 0;
  traba = "";
  bytes = 0;
  quebusco = "";
  hebuscado = "";
  desbus = 0;
  estoesloquehay = "";
  encontrado = 0;
  esfichero = 0;
  unavez = 0;
  rafa1 = "";
  rafa2 = "";
  nSwOk = 0;
  aDef = "";
  aAplica = "";
  aMensaje = "";
  aDirApli = "";
  aPathDef = "";
  aPathDat = "";
  aAlfa = "";
  aEmpresa = "";
  aNomEmp = "";
  nSwCargado = 0;
  aPeriodo = "";
  aExtension = "";
  {3}reparar = "";
  aAlfa1 = "";
  aPass = "";
|FIN;


|RUTINA SP;
|VARIABLES;
   pos_fichero = 0;
|FIN;
   pos_fichero = Pos;
   Pos = -1;
|FRUT;

|RUTINA RP;
   Pos = pos_fichero;
|FRUT;

|RUTINA carga_campos;
   ||****** en temporal2 vienen los datos
   || formato: 8 fichero 25 descripcion 8 ocupacion 8 registros 5 lenreg
   ||          2 n.claves 5 bloque 8 n.bloques
   |DDEFECTO #rwindex;
   #rwindex#fichero     = (A\temporal2 % 108);
   #rwindex#descripcion = (A\temporal2 % 925);
   #rwindex#tamano      = (A\temporal2 % 3415);
   #rwindex#registros   = (A\temporal2 % 4915);
   #rwindex#tamreg      = (A\temporal2 % 6405);
   #rwindex#claves      = (A\temporal2 % 6903);
   #rwindex#bloque      = (A\temporal2 % 7205);
   #rwindex#nbloques    = (A\temporal2 % 7715);
   #rwindex#segmentos   = (A\temporal2 % 9203);
|FRUT;

|PROCESO borrando_fichero;
     temporal5 = temporal3 + aExtension;
     |FBORRA temporal5;
|FPRC;

|PROCESO copiando_fichero;
     temporal4 = temporal2 + aExtension;
     temporal5 = temporal3 + aExtension;
     |PINTA 1626, temporal4;
     |PINTA 1726, temporal5;
     |COPIA_FICHERO temporal4, temporal5;
     |SI FSalida ! 0;
          ri = 1;
     |FINSI;
|FPRC;

|RUTINA copia_fichero;
     ri = 0;

     temporal3 = fichero % 108;
     |QBF temporal3;
     temporal2 = dirfichero + temporal3;
     temporal3 = dirempresa + temporal1 + temporal3;

     aExtension = ".ixx";     |HAZ copiando_fichero;
     |SI ri = 0;
          aExtension = ".dat";     |HAZ copiando_fichero;
          |SI ri = 0;
               aExtension = ".ddx";
               |HAZ copiando_fichero;
               |SI ri ! 0;
                    aExtension = ".ixx";     |HAZ borrando_fichero;
                    aExtension = ".dat";     |HAZ borrando_fichero;
               |FINSI;
          |SINO;
               ri = 0;
               aExtension = ".dbf";     |HAZ copiando_fichero;  || los dbf no
               |SI ri ! 0;                                      ||/llevan ddx
                    aExtension = ".ixx";     |HAZ borrando_fichero;
               |FINSI;
          |FINSI;
     |FINSI;

/*
  temporal4 = temporal2 + ".ixx";
  temporal5 = temporal3 + ".ixx";
  |PINTA 1626,temporal4;
  |PINTA 1726,temporal5;
  |COPIA_FICHERO temporal4,temporal5;
  |SI FSalida ! 0;
     ri = 1;
  |SINO;
     temporal4 = temporal2 + ".dat";
     temporal5 = temporal3 + ".dat";
     |PINTA 1626,temporal4;
     |PINTA 1726,temporal5;
     |COPIA_FICHERO temporal4,temporal5;
     |SI FSalida ! 0;
        ri = 1;
        || Borrar indexado destino para evitar males mayores
        temporal5 = temporal3 + ".ixx";
        |FBORRA temporal5;
     |SINO;
        temporal4 = temporal2 + ".ddx";
        temporal5 = temporal3 + ".ddx";
        |PINTA 1626,temporal4;
        |PINTA 1726,temporal5;
        |COPIA_FICHERO temporal4,temporal5;
        |SI FSalida ! 0;
           ri = 1;
           || Borrar indexado y dat destino para evitar males mayores
           temporal5 = temporal3 + ".ixx";
           |FBORRA temporal5;
           temporal5 = temporal3 + ".dat";
           |FBORRA temporal5;
        |FINSI;
     |FINSI;
  |FINSI;
*/

     |SI ri = 1;
          ||AVISO;
          ||PINTA 1230,"FICHERO NO COPIADO";

          || la variable ri servia para no seguir copiando, si se queda con 1
          ||/esto se quito ...

          ri = 0;
     |FINSI;
|FRUT;

|RUTINA parametros;
    alfa1 = PARAMETRO % 0;    topep = alfa1;
    dolar1 = "";
    dolar2 = "";
    elsw1 = 0;
    punto_coma = &59;
|PARA para1 = 1; |SI para1 [ topep; |HACIENDO para1 = para1 + 1;
        alfa1 = PARAMETRO % (N\para1 * 100 + 1);
    |SI alfa1 = punto_coma;
        |SI elsw1 = 0;
                elsw1 = 1;
        |SINO;
                elsw1 = 99;
            |MENAV "    Exceso de Parametros (maximo 2) ...";
        |FINSI;
    |SINO;
        |SI elsw1 = 0;   dolar1 = dolar1 + alfa1; |FINSI;
        |SI elsw1 = 1;   dolar2 = dolar2 + alfa1; |FINSI;
    |FINSI;
|SIGUE;
|FRUT;

|PROCESO quitabarra;
  rafa1 = temporal1 % -108;
|ET arriba;
  rafa2 = rafa1 - "/";
  |SI rafa1 = rafa2;  |ACABA;  |FINSI;
  rafa1 = rafa1 % 207;
  |VETE arriba;
|FPRC;

|PROGRAMA;
   aParametro = PARAMETRO;
   |QBF aParametro;

   aBusco = aParametro % 107; |QBF aBusco;
   |SI aBusco = "AUTOSQL";
       nModoOculto = 1;
       aBusco = aParametro % 8;
       |QBF aBusco;
       |SI aBusco = "INDEXACION";
          dolar1 = aBusco;
       |FINSI;
       aParametro = "AUTOSQL";          ||Para que termine antes
   |SINO;
       nModoOculto = 0;
       |HAZ parametros;
   |FINSI;

   |CLS;
   |CABEZA "Control de Ficheros";
   quebusco = "";
   |SI nModoOculto = 0;
        |PINPA #0#rwindex;
   |FINSI;
|| ********** Inicializacion de variables *************
   |DBASS dirbase;
   |DBASE aDirApli;
   |QBF aDirApli;
   |NOMAP aplicacion;
   |DFICB dirempresa;
   |DFICE dirfichero;
   empresa = dirfichero - dirempresa;

|| ********** Carga del .dir **************************
   total = 0;
   |SI dolar2 = "";
       temporal1 = "rt  "+dirempresa+aplicacion+".dir";
   |SINO;
       temporal1 = "rt  "+dirempresa+dolar2+".dir";
       |SI dolar1 = dolar2;  dolar1 = "";  dolar2 = "";  |FINSI;
       |SI dolar2 = "aginom";  dolar2 = "";  |FINSI;
   |FINSI;
   |FABRE temporal1;
   handle = FSalida;
   |SI handle ] 0;
      |HAZ SP;
      Ifichero = fichero1<-;
      |PARA i = 0;|SI total < 4500;|HACIENDO i = i + 1;
         temporal1 = handle+" 200";
         |HAZ RP;
         |FLEE temporal1;
         rafa1 = temporal1 % 101;
         |SI rafa1 = ":";
             |HAZ quitabarra;
             temporal1 = rafa1;
         |FINSI;
         |HAZ SP;
         |SI FSalida < 0;
            |SAL;
         |FINSI;
         ri = i $ 5;
         |SI ri = 0;  || 1
            fichero = temporal1;
         |SINO;
         |SI ri = 1;  || 2
            || ojo con las variables "apuntador"
            temporal2 =  fichero + "        ";
            temporal2 = temporal2 % 108;
            temporal2 = (temporal2+temporal1+"                             ") % 133;
            fichero = temporal2;
         |SINO;
         |SI ri = 3;  || 3
            || ***** Solo cuenta si tiene marca de indexado
            temporal1 = temporal1 % 101;
            |SI temporal1 = 1;|O temporal1 = 2; || ficheros con indice
                |SI total = 0;  |Y dolar2 ! "";  |Y sw_paso = 0;
                    sw_paso =  1;
                    primero = fichero % 108;
                    |QBF primero;
                |SINO;
|| Los ficheros que comienzan por "SQ" no se tratan
|| estos son los generados por la sentencia |SQL en la dscomer9
                    aAlfa = fichero % 102; aAlfa = aAlfa > "";
                    |SI aAlfa = "SQ";
                         fichero = "";
                    |SINO;
                         Ifichero = Ifichero + 1;
                         total    = total + 1;
                    |FINSI;
                |FINSI;
            |FINSI;
         |FINSI;      || 3
         |FINSI;      || 2
         |FINSI;      || 1
      |SIGUE;
      |FCIERRA handle;
   |FINSI;
   |SI dolar2 ! "";
       total = total - 1;
       Ifichero = Ifichero + 1;
   |FINSI;
   |SI total [ 0;
      |SI dolar2 = "";
          temporal1 = "0000No hay ficheros en "+dirempresa + aplicacion + ".dir";
      |SINO;
          temporal1 = "0000No hay ficheros en "+dirempresa + dolar2 + ".dir";
      |FINSI;
      |MENAV temporal1;
|| ************ abandona: nada que hacer ***********
      |ACABA;
|| *************************************************
   |FINSI;


   aAlfa = Usuario; |NOME_DAT #1 aAlfa; |DELINDEX #1;

||***************** Carga de los ficheros ********************
   #rwindex#tficheros = total;
   #rwindex#ttamano = 0;

   |SI nModoOculto = 0;
      |ATRI R;
      |MENSA "0401Cargando informacion sobre los ficheros ...";
      |ATRI 0;
   |FINSI;

   Ifichero = fichero1<-;
   |PARA i = 0;|SI i < total;|HACIENDO i = i + 1;
      temporal1 = fichero % 108;
      |SI nModoOculto = 0;
          |PINTA 0465,temporal1;
      |FINSI;
      |QBF temporal1;
      |SI dolar1 = "INDEXACION";
         |GRABAENDEF temporal1,"";
      |SINO;
         |SI dolar2 = "";
             |LEEDEDEF temporal1,"";
         |SINO;
             |LEEDEDEF primero,temporal1;
         |FINSI;
      |FINSI;
      |SI FSalida ] 0;
          temporal1 = FSalida % 375;
          #rwindex#ttamano = #rwindex#ttamano + (A\temporal1 % 115);
      |SINO;
          temporal1 = " " * 80;
          temporal1 = temporal1 % 175;
      |FINSI;
      fichero = fichero + temporal1;
      Ifichero = Ifichero + 1;
   |SIGUE;
   |SI nModoOculto = 0;
       |BLIN 4;
   |FINSI;

   |SI aParametro = "AUTOSQL";
      ||SOLO SE UTILIZA PARA QUE SE GENEREN LAS SENTENCIAS SQL PARA EL ENGINE
      |DELINDEX #1;
      |ACABA;
   |FINSI;

||************************ Presentar resultados ***************
   |MENSA "0000F3=Inicializa;F4=Indexa;F5=Copia;F7=Inicializa TODO;F8=Indexa TODO;F9=Copia TODO";
   |PINTA 2501,"  (AvPg, RePg, Cursor)=Selecc. Fich.; (Esc o Ctrl-C)=Terminar;F10=Buscar Fich.";
   total_campos = 9;
   total_lineas = 15;
   linea = 1;
   posicion = 0;
   pintar=1;
   |PINTA #rwindex#tficheros;
   |PINTA #rwindex#ttamano;
   bytes = #rwindex#ttamano;
   |PARA;|SI;|HACIENDO;
      |SI pintar = 1;
         || La pantalla siempre esta completamente pintada en caso
         || de haber suficientes "registros" en caso contrario
         || el resto ya esta "blanco"
         |PARA i = 0;|SI i < total_campos;|HACIENDO i = i + 1;
            |CAMPO_ATRIBUTO #rwindex#i#,I,0,0;
         |SIGUE;

         Ifichero = fichero1<-;
         Ifichero = Ifichero + posicion;
         ri = posicion;
         |PARA i = 1;|SI i [ total_lineas;|Y ri < total;|HACIENDO i = i + 1;
             temporal2 = fichero;
             |HAZ carga_campos;
             Pos = i * 100;
             |PINDA #0#rwindex;
             Pos = -1;       || Siempre hay que acordarse de resetear Pos
             ri = ri + 1;
             Ifichero = Ifichero + 1;
         |SIGUE;

         |PARA i = 0;|SI i < total_campos;|HACIENDO i = i + 1;
            |CAMPO_ATRIBUTO #rwindex#i#,R,0,0;
         |SIGUE;

         pintar = 0;
         ant_linea = 0;
      |FINSI;

      || **** pintar linea activa
      |SI ant_linea ! linea;
         |SI ant_linea ! 0;
            |PARA i = 0;|SI i < total_campos;|HACIENDO i = i + 1;
               |CAMPO_ATRIBUTO #rwindex#i#,I,0,0;
            |SIGUE;
            Ifichero = fichero1<-;
            Ifichero = Ifichero + posicion + ant_linea - 1;
            temporal2 = fichero;
            |HAZ carga_campos;
            Pos = ant_linea * 100;
            |PINDA #0#rwindex;
            Pos = -1;
            |PARA i = 0;|SI i < total_campos;|HACIENDO i = i + 1;
               |CAMPO_ATRIBUTO #rwindex#i#,R,0,0;
            |SIGUE;
         |FINSI;
         Ifichero = fichero1<-;
         Ifichero = Ifichero + posicion + linea - 1;
         temporal2 = fichero;
         |HAZ carga_campos;
         Pos = linea * 100;
         |PINDA #0#rwindex;
         Pos = -1;
         ant_linea = linea;
      |FINSI;

      |LEETECLA temporal1;
      i = (A\temporal1 % 203);
      |SI i = 806;|O i = 807;
          |SAL; || fin por escape o ctrl-c
      |FINSI;
      |SI i = 814; || pgup
          ri = posicion - total_lineas;
          |SI ri ] 0;
              posicion = posicion - total_lineas;
              pintar = 1;
          |FINSI;
          i = 0;
      |FINSI;
      |SI i = 815; || pgdn
         ri = total - posicion;
         |SI ri > total_lineas;
            posicion = posicion + total_lineas;
            ri = ri - total_lineas;
            |SI ri < total_lineas;
               ri = total_lineas - ri;
               posicion = posicion - ri;
            |FINSI;
            pintar = 1;
         |FINSI;
         i = 0;
      |FINSI;
      |SI i = 808;  || arriba
         |SI linea > 1;
            linea = linea - 1;
         |SINO;
            |SI posicion > 0;
               posicion = posicion - 1;
               pintar = 1;
            |FINSI;
         |FINSI;
         i = 0;
      |FINSI;
      |SI i = 809;  || abajo
         ri = total - posicion;
         |SI linea < ri;
            |SI linea < total_lineas;
               linea = linea + 1;
            |SINO;
               posicion = posicion + 1;
               pintar = 1;
            |FINSI;
         |FINSI;
         i = 0;
      |FINSI;
      |SI i = 827;|O i = 831;
         |SI empresa ! "";
            |SI i = 827; aTecla = "F5"; aFichero = temporal2 % 108; |HAZ GrabaLog; |FINSI;
            |SI i = 831; aTecla = "F9"; aFichero = "todos"; |HAZ GrabaLog; |FINSI;

            |PUSHV 1010 2070;
            |PINPA #2#rwindex;
            temporal1 = "";
            E_sup = (N\ (A\ empresa % 0) - 1);
            E_inf = "";
            nSwOk = 0;
            ||TODO CCC
            aDef = "";
            aAplica = aplicacion % 107;
            |SI aAplica = "dscomer";
               aDef = "agifa041";
            |SINO;
               |SI aAplica = "agicont";
                  aDef = "canempre";
               |FINSI;
            |FINSI;
            |SI aDef ! "";
                aPathDef = aDirApli + "def/";
                aPathDat = aDirApli + "fich/";
                aAlfa = aPathDef + aDef;
                |CARGA_DEF aAlfa, fichero@;
                |SI FSalida ! 0;
                    aMensaje = "2405Error al cargar Def: " + aDef;
                    ||MENSA aMensaje;
                    |MENAV aMensaje;
                |SINO;
                    |PATH_DAT #100 aPathDat;
                    nSwCargado = 1;
                |FINSI;
            |FINSI;
            |PARA; |SI; |HACIENDO;
                 temporal1 = 1443 ? 0;
                 |QBF temporal1;
                 |SI nSwCargado = 1;
                    aNomEmp = "";
                    |ABRE #fichero@;
                    |SI aDef = "agifa041";
                         #fichero@#0 = temporal1;
                         |LEE 000#fichero@.=;
                         |SI FSalida = 0;
                              aNomEmp = #fichero@#1;
                              |QBF aNomEmp;
                         |FINSI;
                    |SINO;
                         aEmpresa = ("000000" + temporal1) % -106;
                         aEmpresa = aEmpresa % 105;
                         aPeriodo =  temporal1 % -101;
                         #fichero@#2 = aEmpresa;
                         #fichero@#3 = aPeriodo;
                         |LEE 000#fichero@.=;
                         |SI FSalida = 0;
                              aNomEmp = #fichero@#1;
                              |QBF aNomEmp;
                         |FINSI;
                    |FINSI;
                    |CIERRA #fichero@;
                 |FINSI;
                 |SI aDef ! "";
                     |SI aNomEmp = "";
                          aMensaje = "0000SCodigo Inaccesible." + &10 + "Es Correcto ?";
                     |SINO;
                          aMensaje = "0000S - " + aNomEmp + " - " + &10 + "Empresa Correcta ?";
                     |FINSI;
                     |CONFI aMensaje;
                     |SI FSalida = 0;
                        |SAL;
                     |FINSI;
                     |PINTA 1618, "                                                  ";
                 |SINO;
                     |SAL;
                 |FINSI;
            |SIGUE;
            |SI nSwCargado = 1;
               |DESCARGA_DEF #fichero@;
            |FINSI;
            |SI temporal1 ! "";
               temporal1 = temporal1 + "/";
               |SI temporal1 = empresa;
                  |PUSHV 1010 2070;
                  |PINPA #3#rwindex;
                  |PAUSA;
                  |POPV;
               |SINO;
                  |CONFI "2400NESTA SEGURO DE QUERER ELIMINAR LOS FICHEROS EN DESTINO? ";
                  |SI FSalida = 0;|Y i = 831;
                     |AVISO;
                     |CONFI "2400NVA A ELIMINAR TODOS LOS FICHEROS, ESTA SEGURO? ";
                  |FINSI;
                  |SI FSalida = 0;
                     |PINTA 1613,"COPIANDO...";
                     |PINTA 1720,"A...";
                     Ifichero = fichero1<-;
                     |SI i = 827;
                        Ifichero = Ifichero + posicion + linea - 1;
                        |HAZ copia_fichero;
                     |SINO;
                        ri = 0;
                        |PARA i = 0;|SI i < total;|Y ri = 0;|HACIENDO i = i + 1;
                          |HAZ copia_fichero;
                          Ifichero = Ifichero + 1;
                        |SIGUE;
                     |FINSI;
                  |FINSI;
               |FINSI;
            |FINSI;
            |POPV;
         |SINO;
            |PUSHV 1010 2070;
            |PINPA #1#rwindex;
            |PAUSA;
            |POPV;
         |FINSI;
         i = 0;
      |FINSI;
      |SI i = 826;
         Ifichero = fichero1<-;
         Ifichero = Ifichero + posicion + linea - 1;
         temporal1 = fichero % 108;
         |QBF temporal1;

         aTecla = "F4"; aFichero = temporal1; |HAZ GrabaLog;

         |SI dolar2 = "";
             |GRABAENDEF temporal1,"";
         |SINO;
             |GRABAENDEF primero,temporal1;
         |FINSI;
         i = 0;
      |FINSI;
      |SI i = 830;
         aTecla = "F8"; aFichero = "todos"; |HAZ GrabaLog;

         Ifichero = fichero1<-;
         |PARA i = 0;|SI i < total;|HACIENDO i = i + 1;
            temporal1 = fichero % 108;
            |QBF temporal1;
            |SI dolar2 = "";
                |GRABAENDEF temporal1,"";
            |SINO;
                |GRABAENDEF primero,temporal1;
            |FINSI;
            Ifichero = Ifichero + 1;
         |SIGUE;
         i = 0;
      |FINSI;
      |SI i = 825;  |O i = 829;
         |SI i = 825;
            Ifichero = fichero1<-;
            Ifichero = Ifichero + posicion + linea - 1;
            temporal1 = fichero % 108;
            |QBF temporal1;
            |HAZ Clave;
            |SI FSalida = 0;
               traba = "2400NESTA SEGURO DE INICIALIZAR EL FICHERO " + temporal1 + " ";
               |CONFI traba;
            |FINSI;
         |SINO;
            |HAZ Clave;
            |SI FSalida = 0;
               |CONFI "2400NESTA SEGURO DE INICIALIZAR TODOS LOS FICHEROS: ";
               |SI FSalida = 0;
                   |CONFI "2400NVA A INICIALIZAR TODOS LOS FICHEROS, ESTA SEGURO? ";
               |FINSI;
            |FINSI;
         |FINSI;
         |SI FSalida = 0;
            |SI i = 825;
               aTecla = "F3"; aFichero = temporal1; |HAZ GrabaLog;

               traba = dirfichero + temporal1 + ".dat";
               |FBORRA traba;
               traba = dirfichero + temporal1 + ".dbf";
               |FBORRA traba;
               traba = dirfichero + temporal1 + ".ixx";
               |FBORRA traba;
               traba = dirfichero + temporal1 + ".ddx";
               |FBORRA traba;
               |HAZ refresca;
            |SINO;
               aTecla = "F7"; aFichero = "todos"; |HAZ GrabaLog;

               Ifichero = fichero1<-;
               |PARA i = 0;|SI i < total;|HACIENDO i = i + 1;
                  temporal1 = fichero % 108;
                  |QBF temporal1;
                  traba = dirfichero + temporal1 + ".dat";
                  |FBORRA traba;
                  traba = dirfichero + temporal1 + ".dbf";
                  |FBORRA traba;
                  traba = dirfichero + temporal1 + ".ixx";
                  |FBORRA traba;
                  traba = dirfichero + temporal1 + ".ddx"
                  |FBORRA traba;
                  Ifichero = Ifichero + 1;
               |SIGUE;
               |SAL;
            |FINSI;
         |FINSI;
         i = 0;
      |FINSI;
      |SI i = 832;
          |SI total > total_lineas;
              |PUSHV 1325 1655;
              |PINPA #4#rwindex;
              quebusco = hebuscado;
              |PINTA 1528,quebusco;
              E_sup = 25;
              E_inf = "";
              quebusco = 1528 ? 0;
              |QBF quebusco;
              quebusco = quebusco > " ";
              |SI quebusco ! "";
                  |HAZ encuentra;
              |FINSI;
              hebuscado = quebusco;
              |POPV;
         |FINSI;
      |FINSI;
      |SI i = 836;
          |AVISO;
          E_sup = 10;
          E_inf = "";
          aPass = "";
          aPass = 2420 ? -1;
          |QBF aPass;
          |SI aPass = "LIMPIAR";
            Ifichero = fichero1<-;
            Ifichero = Ifichero + posicion + linea - 1;
            temporal1 = fichero % 108;
            |QBF temporal1;
            |DDEF reparar1;
            reparar1 = reparar1 + temporal1;
            |DFICE reparar2;
            reparar2 = reparar2 + temporal1;
            reparar3 = "?CHINOS";
            |ESPECIFICA "REPARA_FICHERO",reparar;
            |SI FSalida > 0;
                aAlfa1 = "0000Revisados [" + FSalida + "] registros con errores.";
                |MENAV aAlfa1;
            |FINSI;
            |SI FSalida < 0;
                |MENAV "0000DEF o datos inaccesibles.";
            |FINSI;
            |SI FSalida = 0;
                |MENAV "0000Ningun registro con errores.";
            |FINSI;
         |SINO;
            |MENAV "0000Clave incorrecta.";
         |FINSI;
         |BLIN 24;
         i = 0;
      |FINSI;
      |SI i = 840;
          |AVISO;
          E_sup = 10;
          E_inf = "";
          aPass = "";
          aPass = 2420 ? -1;
          |QBF aPass;
          |SI aPass = "LIMPIAR";
             Ifichero = fichero1<-;
             |PARA i = 0;|SI i < total;|HACIENDO i = i + 1;
                temporal1 = fichero % 108;
                |QBF temporal1;
                |DDEF reparar1;
                reparar1 = reparar1 + temporal1;
                |DFICE reparar2;
                reparar2 = reparar2 + temporal1;
                reparar3 = "?CHINOS";
                |ESPECIFICA "REPARA_FICHERO",reparar;
                |SI FSalida > 0;
                    aAlfa1 = "0000Revisados [" + FSalida + "] registros con errores de [" + temporal1 + "].";
                    |MENAV aAlfa1;
                |FINSI;
                |SI FSalida < 0;
                    aAlfa1 = "0000Error en def/datos de [" + temporal1 + "].";
                    |MENAV aAlfa1;
                |FINSI;
                Ifichero = Ifichero + 1;
             |SIGUE;
         |SINO;
            |MENAV "0000Clave incorrecta.";
         |FINSI;
         |BLIN 24;
         i = 0;
      |FINSI;

      i = 0;
   |SIGUE;

   |||ABRE #1; |CONSULTA_CLAVE #1#1; |CIERRA #1;
   |DELINDEX #1;

   || Eso es todo amigos!
|FPRO;

|RUTINA refresca;
  bytes = bytes - (A\fichero % 3408);
  #rwindex#ttamano = bytes;
  |PINTA #rwindex#ttamano;
  |SI dolar2 = "";
      |LEEDEDEF temporal1,"";
  |SINO;
      |LEEDEDEF primero,temporal1;
  |FINSI;
  |SI FSalida ] 0;
      temporal1 = FSalida % 375;
      #rwindex#ttamano = #rwindex#ttamano + (A\temporal1 % 115);
      bytes = bytes + (A\temporal1 % 115);
      #rwindex#ttamano = bytes;
      |PINTA #rwindex#ttamano;
  |FINSI;
  pintar = 1;
  traba = fichero % 133;
  fichero = traba + temporal1;
  temporal2 = fichero;
  |HAZ carga_campos;
  Pos = linea * 100;
  |PINDA #0#rwindex;
  Pos = -1;
|FRUT;

|RUTINA encuentra;
  unavez = 0;
|ET haempezar;
  unavez = unavez + 1;
  esfichero = 0;
  |HAZ buscando;
  |SI encontrado = 0;
      esfichero = 1;
      |HAZ buscando;
  |FINSI;
  |SI hebuscado = quebusco;  |Y  encontrado = 0;  |Y unavez ! 2;
      |VETE haempezar;
  |FINSI;
  |SI encontrado = 0;  |ACABA;  |FINSI;
  posicion = i;
  ri = total - posicion;
  |SI ri < total_lineas;
      posicion = total - total_lineas;
      linea = i - posicion + 1;
  |SINO;
      linea = 1;
  |FINSI;
  pintar = 1;
|FRUT;

|RUTINA buscando;
  encontrado = 0;
  Ifichero = fichero1<-;
  |SI hebuscado = quebusco;
      |SI unavez ! 2;
          desbus = posicion + linea;
      |SINO;
          desbus = 0;
      |FINSI;
  |SINO;
      desbus = 0;
  |FINSI;
  Ifichero = Ifichero + desbus;
  |PARA i = desbus; |SI i < total; |HACIENDO i = i + 1;
        |SI esfichero = 0;
            temporal1 = fichero % 108;
        |SINO;
            temporal1 = fichero % 925;
        |FINSI;
        |QBF temporal1;
        temporal1 = temporal1 > " ";
        estoesloquehay = temporal1 - quebusco;
        |SI temporal1 ! estoesloquehay;
            encontrado = 1;
            |SAL;
        |FINSI;
        Ifichero = Ifichero + 1;
  |SIGUE;
|FRUT;

|PROCESO GrabaLog;
     |ABRE #1;
     |LEE 000#1u;
     |SI FSalida = 0;
          nConta = #1#0 + 1;
     |SINO;
          nConta = 1;
     |FINSI;

     |HORA aHora;
     |DDEFECTO #1;
     #1#0 = nConta;
     #1#1 = Usuario % 810;
     #1#2 = ~;
     #1#3 = aHora;
     #1#4 = aTecla;
     #1#5 = aFichero;
     |GRABA 020#1n;
     |CIERRA #1;
|FPRC;

|PROCESO Clave;
     |PUSHV 0501 2480;
     |BLIN 24;
     |PINTA 2425, "Entre la Clave:";
     aAlfa = "";
     E_sup = 10;
     E_inf = "";
     aAlfa = 2441 ? -1;
     |POPV;
     |SI aAlfa = "<MYPASWD> ";
          FSalida = 0;
     |SINO;
          |MENAV "0000Clave incorrecta...";
          FSalida = 1;
     |FINSI;
|FPRC;
