|FICHEROS;
     dschin01 #0;

     defchino@ #998;
     empresas@ #999;
|FIN;

|VARIABLES;
     aBase       = "";
     aPathAplica = "";
     aPathDef    = "";
     aPathFich   = "";
     aPathChinos = "";
     aMasEmpresa = "";
     aQueQuiero  = "";
     aLong       = "";
     aDir        = "";
     aMas        = "";
     aAlfa       = "";

     {3}aBuscador = "";

     nHandle     = 0;
     nDEmpresa   = 0;
     nHEmpresa   = 0;
     nLong       = 0;
     nPosi       = 0;
|FIN;

|PROCESO ElDir;
     aDir = aPathChinos + "*.dat";

     |_PDIR aDir;
     |PARA;  |SI;  |HACIENDO;
          |SI FSalida ! 0;  |SAL;  |FINSI;

          aAlfa = aDir - ".dat" + ".chn";  |FBORRA aAlfa;
          aAlfa = aDir - ".dat" + ".log";  |FBORRA aAlfa;

          aMas = aDir - aPathChinos - ".dat" + ".mas";
          aMas = aPathDef + aMas;
          |CARGA_DEF aMas, defchino@;
          |SI FSalida = 0;
              |PATH_DAT #998 aPathChinos;
              |ABRE #998;
              |CIERRA #998;

              aBuscador1 = aMas - ".mas";
              aBuscador2 = aDir - ".dat";
              |SI #0#4 = "S";
                  aBuscador3 = "?CHINOS";
                  |ESPECIFICA "REPARA_FICHERO", aBuscador;
              |SINO;
                  aBuscador3 = "REVISACHINOS";
                  |ESPECIFICA "REPARA_FICHERO", aBuscador;
                  |SI FSalida > 0;
                      aAlfa = aDir + " [" + FSalida + "] Registros con errores.";
                      |IMPRIME aAlfa;
                  |FINSI;

                  |SI FSalida < 0;
                      aAlfa = aDir + " DEF o datos inaccesibles. FSalida=" + FSalida;
                      |IMPRIME aAlfa;
                  |FINSI;
              |FINSI;

              |DESCARGA_DEF #defchino@;

              aAlfa = aDir - aPathChinos - ".dat" + ".chn";  |FBORRA aAlfa;
              aAlfa = aDir - aPathChinos - ".dat" + ".log";  |FBORRA aAlfa;
          |SINO;
              |SI #0#4 = "N";
                  aAlfa = aDir + " DEF no encontrado o erroneo. |CARGA_DEF -> FSalida=" + FSalida;
                  |IMPRIME aAlfa;
              |FINSI;
          |FINSI;

          |_SDIR aDir;
     |SIGUE;

     |PULSATECLA;
|FPRC;

|PROCESO TrataEmpresa1;
     aQueQuiero = "|C000LONGITUD";
     aLong      = #999#aQueQuiero#;
     nLong      = aLong;
     nPosi      = -(100 + nLong);

     aPathChinos = aPathFich + ((("0" * nLong) + #999#0) % nPosi) + "/";

     |HAZ ElDir;
|FPRC;

|DEFBUCLE empresas1;
     #999#1001;
     ;
     nDEmpresa;
     nHEmpresa;
     ;
     NULL, TrataEmpresa1;
|FIN;

|PROCESO TrataEmpresa2;
     aPathChinos = aPathFich + (("00000" + #999#2) % -105) + #999#3 + "/";

     |HAZ ElDir;
|FPRC;

|DEFBUCLE empresas2;
     #999#1002;
     ;
     nDEmpresa, 0;
     nHEmpresa, 9;
     ;
     NULL, TrataEmpresa2;
|FIN;

|PROCESO Tipo3;  |TIPO 3;
     |DBASS aBase;  |QBF aBase;

     aPathAplica = aBase + #0#0;            |QBF aPathAplica;
     aPathDef    = aPathAplica + "/def/";
     aPathFich   = aPathAplica + "/fich/";
     |SI #0#1 ! "        ";
         aMasEmpresa = aPathDef + #0#1;         |QBF aMasEmpresa;
         aMasEmpresa = aMasEmpresa + ".mas";
         |XABRE "E", aMasEmpresa, nHandle;
         |SI FSalida < 0;
             |MENAV "0000 No encuentro el Def de Acceso.";
             |ACABA;
         |FINSI;
     |SINO;
         aMasEmpresa = aPathFich + #0#0;         |QBF aMasEmpresa;
         aMasEmpresa = aMasEmpresa + ".dir";
         |XABRE "E", aMasEmpresa, nHandle;
         |SI FSalida < 0;
             |MENAV "0000 No encuentro la Aplicacion.";
             |ACABA;
         |FINSI;
     |FINSI;

     |SI #0#1 ! "        ";
         |CARGA_DEF aMasEmpresa, empresas@;
         |SI FSalida ! 0;
             |MENAV "0000 No puedo cargar el Def de Empresas";
             |ACABA;
         |FINSI;

         |PATH_DAT #999 aPathFich;

         nDEmpresa = #0#2;
         nHEmpresa = #0#3;

         |SI #0#4 = "N";
             |IMPRE 0;
             |SI FSalida ! 0;  |ACABA;  |FINSI;
         |FINSI;

         |SI #0#0 = "agicont ";  |O #0#0 = "contagen";
             |BUCLE empresas2;
         |SINO;
             |BUCLE empresas1;
         |FINSI;

         |SI #0#4 = "N";
             |FINIMP;
         |FINSI;

         |DESCARGA_DEF #empresas@;
     |SINO;
         |SI #0#4 = "N";
             |IMPRE 0;
             |SI FSalida ! 0;  |ACABA;  |FINSI;
         |FINSI;

         aPathChinos = aPathFich;
         |HAZ ElDir;

         |SI #0#4 = "N";
             |FINIMP;
         |FINSI;
     |FINSI;
|FPRC;
