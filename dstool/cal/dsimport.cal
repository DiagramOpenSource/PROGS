|FICHEROS;
     dsimport #0;
     dsimpor1 #101,MANTE;
     dsimpor2 #102,MANTE;
     dsw00002 #1;
     dsw00003 #2;
     referen@ #900;
|FIN;

|VARIABLES;
     aAplicacion = "";
     aEmpresa    = "";
     aDirAplica  = "";
     aBase       = "";
     aAlfa        = "";
     aAlfa1       = "";
     aDirDef      = "";
     aBaseDef     = "";
     aFichero     = "";
     aAplica      = "";
     aDef         = "";
     aMas         = "";
     aQueQuiero   = "";
     aBaseFich    = "";
     aTitulo      = "";
     aDSoporte    = "";
     aPassword    = "";
     aDDef        = "";
     aHDef        = "";
     aDAplica     = "";
     aHAplica     = "";
     aHayDatos    = "";
     aRuta        = "";
     aCaracter9   = "";
     aCaracter10  = "";
     aCaracter13  = "";
     aCampo       = "";
     aAbre        = "";
     aComilla     = "";
     aCaracter    = "";
     aTraba       = "";
     aLongitud    = "";
     aTecla       = "";
     aF2          = "";
     aF3          = "";

     nPos         = 0;
     nBusca       = 0;
     nTCampo      = 0;
     nTCompo      = 0;
     nCampo       = 0;
     nCampoBue    = 0;
     nCampo1      = 0;
     nCompo       = 0;
     nNume        = 0;
     nPosi        = 0;
     nHandle      = 0;
     nMalo        = 0;
     nContador    = 0;
     nImpresion   = 0;
     nBoton1      = 0;
     nBoton2      = 0;
     nBoton3      = 0;
     nImportacion = 0;
     nRegistro    = 0;
     nChequeo     = 0;
     nInicializar = 0;
     nTotalCampo  = 0;
     nHayComillas = 0;
     nCaracter    = 0;
     nLongitud    = 0;
     nBytes       = 0;
     nBloqueByte  = 0;
     nCampoByte   = 0;
     nTotalBytes  = 0;
     nBytesLeidos = 0;
     nPorce       = 0;
     nSwHay      = 0;
     nSal = 0;
     aFicOrg = "";
     aIP = "";
|FIN;

|FPRC;

|| ‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
|| PROCESOS DE LOS BOTONES
|| ‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹

|PROCESO Boton1;
     |PTEC 824;
|FPRC;

|PROCESO Boton2;
     |PTEC 825;
|FPRC;

|| ‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
|| PROCESOS DATOS ENTRADA PANTALLA PRINCIPAL
|| ‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹

|PROCESO Aplicacion;
     nSwHay      = 0;
     aAplicacion = #0#1;  |QBF aAplicacion;
     |DBASS aBase;        |QBF aBase;

     aDirAplica = aBase + aAplicacion + "/*.men";
     |_PDIR aDirAplica;
     |PARA;  |SI FSalida ] 0; |HACIENDO;
             nSwHay = 1;
             |SAL;
     |SIGUE;

     |SI nSwHay = 0;
         |MENAV "      No existe Aplicacion.";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Empresa;
     nSwHay      = 0;
     aAplicacion = #0#1;  |QBF aAplicacion;
     aEmpresa    = #0#2;  |QBF aEmpresa;

     |SI aEmpresa = "";  |ACABA;  |FINSI;

     |DBASS aBase;        |QBF aBase;

     aDirAplica = aBase + aAplicacion + "/fich/" + aEmpresa + "/*";
     |_PDIR aDirAplica;
     |PARA;  |SI FSalida ] 0; |HACIENDO;
             nSwHay = 1;
             |SAL;
     |SIGUE;

     |SI nSwHay = 0;
         |MENAV "      No existe Empresa.";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO SacaBotones;  |TIPO 7;
     |CONTROL_SIMPLE 0, "BOTON,Texto delimitado por Tabulaciones", 0841, 0879, Boton1;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,Texto delimitado por Caracteres", 0941, 0979, Boton2;
     nBoton2 = FSalida;

     aF2 = &255 + "824";
     aF3 = &255 + "825";
     |PARA; |SI;  |HACIENDO;
         |LEETECLA aTecla;

         |SI aTecla = aF2;
             #0#3 = "ASCCI9";
             |PINTA #0#3;
             |C_M #0#3, 1, "N";
             |SAL;
         |FINSI;

         |SI aTecla = aF3;
             |C_M #0#3, 1, "S";
             |SAL;
         |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;

     |PULSATECLA;
|FPRC;

|| ‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
|| PROCESOS LA PROPIA IMPORTACION
|| ‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹

|PROCESO GrabaCampo;
     |SI nChequeo = 0;
         nChequeo = 1;
         |ERROR10;
         |ACABA;
     |FINSI;

     #1#0 = #102#6;
     |LEE 040#1=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aAlfa1 = #1#1;  |QBT aAlfa1;
     |SI aAlfa1 ! "";
         aAlfa1 = aAlfa1 > aAlfa1;
         |SI aAlfa1 = "TRUE";    #1#1 = "S";  |FINSI;
         |SI aAlfa1 = "FALSE";   #1#1 = "N";  |FINSI;

         nCampoBue       = #102#3;
         #900nCampoBue   = #1#1;
     |FINSI;

     nImportacion = 1;
|FPRC;

|DEFBUCLE dsimpor2;
     #102#1;
     6;
     #101#0, #101#1, #101#2, 000, 001;
     #101#0, #101#1, #101#2, 999, 999;
     ;
     NULL, GrabaCampo;
|FIN;

|PROCESO GrabaRegistro;
  |||   |SI nRegistro = 0;  |ACABA;  |FINSI;

     nImportacion = 0;
     nChequeo     = 1;

     |DDEFECTO #900;
     |ABRE #1;
     |BUCLE dsimpor2;
     |CIERRA #1;
     |SI nImportacion = 0;  |ACABA;  |FINSI;

     |SALVA_DATOS #900;
     |LEE 101#900=;
     |SI FSalida ! 0;
         |REPON_DATOS #900;
         |GRABA 020#900n;
     |SINO;
         |REPON_DATOS #900;
         |GRABA 020#900a;
     |FINSI;

     |LIBERA #900;
|FPRC;

|PROCESO MiraRaros;
     nCaracter = &aAlfa;
     aCaracter = &209; |SI aAlfa = aCaracter;  aAlfa = "•"; |FINSI;
     aCaracter = &186; |SI aAlfa = aCaracter;  aAlfa = "ß"; |FINSI;
     aCaracter = &170; |SI aAlfa = aCaracter;  aAlfa = "¶"; |FINSI;
     aCaracter = &225; |SI aAlfa = aCaracter;  aAlfa = "†"; |FINSI;
     aCaracter = &233; |SI aAlfa = aCaracter;  aAlfa = "Ç"; |FINSI;
     aCaracter = &237; |SI aAlfa = aCaracter;  aAlfa = "°"; |FINSI;
     aCaracter = &243; |SI aAlfa = aCaracter;  aAlfa = "¢"; |FINSI;
     aCaracter = &250; |SI aAlfa = aCaracter;  aAlfa = "£"; |FINSI;
|FPRC;

|PROCESO BuscaBytes;
     aAbre = aFicOrg;
     |XABRE "B", aAbre, nHandle;

     nTotalBytes = 0;
     |XPOSICION nHandle, nTotalBytes, 2;

     |XCIERRA nHandle;

     nBloqueByte  = nTotalBytes / 25;
     nBytes       = 0;
     nCampoByte   = 2;
|FPRC;

|PROCESO Importacion;
     nChequeo     = 0;

     |BUCLE dsimpor2;
     |SI nChequeo = 0;
         |MENAV "     Los Campos no tienen equivalencia con el TXT.";
         |ACABA;
     |FINSI;

     aFicOrg = #101#5; |QBF aFicOrg;
     |HAZ FicAlServidor;
     |SI aFicOrg = "";
          aAlfa = "Error al copiar:" + #101#5; |QBF aAlfa;
          |MENAV aAlfa;
          |ACABA;
     |FINSI;

     aMas = aBaseDef + #101#2 + ".mas";  |QBT aMas;
     |CARGA_DEF aMas, referen@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |PATH_DAT #900 aBaseFich;

     |SI nInicializar = 0;
         |ABRE #900;  |CIERRA #900;  |DELINDEX #900;
     |FINSI;

     aAlfa = ("02" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #1 aAlfa;
     |ABRE #1;  |CIERRA #1;  |DELINDEX #1;


     |HAZ BuscaBytes;

     aAbre = aFicOrg;
     |XABRE "B", aAbre, nHandle;

     aCampo       = "";
     nCampo       = 0;
     nRegistro    = 0;
     nTotalCampo  = 0;  || Variable que controla el numero de campos porque
                        || hay registros con campos con caracteres 10

     |DDEFECTO #2;
     #2#0 = #101#2;
     #2#1 = #101#3;
     |PUSHV 0501 2380;

     |PINPA #0#2;
     |PINDA #0#2;

     aCaracter9  = #0#3;  |QBF aCaracter9;
     aCaracter9  = aCaracter9 - "ASCCI";
     |SI FSalida > 0;
         nNume      = aCaracter9;
         aCaracter9 = &nNume;
     |FINSI;

     aCaracter10  = &10;
     aCaracter13  = &13;
     aComilla     = &34;
     nBytesLeidos = 0;

     |ABRE #900;
     |PARA;  |SI  FSalida ] 0;  |HACIENDO;
             |XLEE nHandle, 1, aAlfa;
             |SI FSalida [ 0;  |SAL;  |FINSI;

             nPorce       = #2#28;
             nBytesLeidos = nBytesLeidos + 1;
             #2#28 = (nBytesLeidos * 100) / nTotalBytes;
             |SI nPorce ! #2#28;
                 |PINTA #2#28;
             |FINSI;

             nBytes = nBytes + 1;
             |SI nBytes ] nBloqueByte;
                 nBytes       = 0;
                 nCampoByte   = nCampoByte + 1;

                 |CAMPO_ATRIBUTO #2nCampoByte, R, 0, 0;
                 |PINTA #2nCampoByte;
                 |CAMPO_ATRIBUTO #2nCampoByte, D, 0, 0;
             |FINSI;

             |SI aAlfa = aCaracter10;
                 nCampo = nCampo + 1;
                 |SI nRegistro = 0;
                     nTotalCampo = nTotalCampo + 1;
                 |FINSI;

                 |SI nCampo = nTotalCampo;
                     |ABRE #1;
                     #1#0   = nCampo;
                     aTraba = aCampo % -101;
                     |SI aTraba = aComilla;
                         aLongitud = aCampo % 0;
                         nLongitud = aLongitud;
                         nLongitud = 100 + aLongitud - 1;
                         aTraba = aCampo % nLongitud;
                         aCampo = aTraba;
                     |FINSI;
                     #1#1   = aCampo;
                     |GRABA 020#1n;
                     |LIBERA #1;
                     |CIERRA #1;

                     |HAZ GrabaRegistro;
                     nHayComillas = 0;

                     aCampo = "";
                     nCampo = 0;
                     |ABRE #1;  |CIERRA #1;  |DELINDEX #1;

                     nRegistro = nRegistro + 1;
                     #2#2      = nRegistro;
                     |PINTA #2#2;
                 |SINO;
                     nCampo = nCampo - 1;
                     aAlfa = " ";
                 |FINSI;
             |FINSI;

             |SI aAlfa = aCaracter9;
                 nCampo = nCampo + 1;
                 |SI nRegistro = 0;  nTotalCampo = nTotalCampo + 1;  |FINSI;

                 |ABRE #1;
                 #1#0   = nCampo;
                 aTraba = aCampo % -101;
                 |SI aTraba = aComilla;
                     aLongitud = aCampo % 0;
                     nLongitud = aLongitud;
                     nLongitud = 100 + aLongitud - 1;
                     aTraba = aCampo % nLongitud;
                     aCampo = aTraba;
                 |FINSI;
                 #1#1   = aCampo;

                 |GRABA 020#1n;
                 |LIBERA #1;
                 |CIERRA #1;

                 aCampo = "";
                 nHayComillas = 0;
             |FINSI;

             |SI aAlfa ! aCaracter10;  |Y aAlfa ! aCaracter9; |Y aAlfa ! aCaracter13;
                 |HAZ MiraRaros;

                 |SI aAlfa = aComilla;
                     |SI nHayComillas = 0;
                         nHayComillas = 1;
                         aAlfa        = "";
                     |SINO;
                         nHayComillas = 0;
                     |FINSI;
                 |FINSI;

                 aCampo       = aCampo + aAlfa;
             |FINSI;
     |SIGUE;
     |XCIERRA  nHandle;
     |CIERRA #900;
     |DELINDEX #1;
     |POPV;

     |FBORRA aFicOrg;
|FPRC;

|PROCESO ImportacionLineal;
     aAlfa = #101#5;  |QBT aAlfa;
     |SI aAlfa = "";
         |MENAV "     Introduzca nombre del Fichero TXT para la Importacion.";
         |ACABA;
     |FINSI;

     nInicializar = 1;
     |SI #101#4 = "S";
          |SI #101#6 = "N"; |ACABA; |FINSI;
          |SI #101#7 = "S"; nInicializar = 0; |FINSI;
     |FINSI;

     |CONFI "0000S Procedemos a la Importacion ";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |LEE 101#101=;

     |HAZ Importacion;

     |SI nImportacion = 1;
         #101#4 = "S";
         |PINTA #101#2;
         |GRABA 020#101a;
     |FINSI;

     |LIBERA #101;
|FPRC;

|PROCESO Masivo;
     aAlfa = #101#5;  |QBT aAlfa;
     |SI aAlfa = "";
         |ACABA;
     |FINSI;

     nInicializar = 1;
     |SI #101#4 = "S";
          |SI #101#6 = "N"; |ACABA; |FINSI;
          |SI #101#7 = "S"; nInicializar = 0; |FINSI;
     |FINSI;

     |HAZ Importacion;

     |SI nImportacion = 1;
         #101#4 = "S";
         |GRABA 020#101a;
     |FINSI;

     |LIBERA #101;
|FPRC;

|DEFBUCLE dsimpor1;
     #101#1;
     ;
     #0#1, #0#2, "        ";
     #0#1, #0#2, "zzzzzzzz";
     be;
     NULL, Masivo;
|FIN;

|PROCESO ImportacionTotal;
     |CONFI "0000SDesea hacer el pase en estos momentos.";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SALVA_DATOS #101;
     |BUCLE dsimpor1;
     |REPON_DATOS #101;
     |LEE 000#101=;

||     |PONCONTROL 23, "SI";
|FPRC;

|| ‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
|| PROCESOS DE LOS FICHEROS ASOCIADOS
|| ‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹

|PROCESO Tipo3m1;  |TIPO 3;
     |ENTLINEAL #1#102, -4, 2, 22, 0, ClaveBaja102, ClaveAlta102;
|FPRC;

|PROCESO Tipo11;  |TIPO 11;
     |SI FSalida = 10;  |HAZ ImportacionLineal;  |ERROR;  |FINSI;
     |SI FSalida = 11;  |HAZ ImportacionTotal;   |ERROR;  |FINSI;
|FPRC;

|PROCESO Tipo7;  |TIPO 7;
     |ENTLINEAL #1#102, -4, 7, 22, 0, ClaveBaja102, ClaveAlta102;
|FPRC;

|PROCESO SacaBoton;  |TIPO 7;
     |CONTROL_SIMPLE 0, "BOTON,...", 0777, 0779, Boton1;
     nBoton3 = FSalida;
|FPRC;

|PROCESO Browse;
     |FIN_CONTROL_WINDOWS nBoton3;
     nSal = FSalida;

     |PULSATECLA;

     |SI nSal ! 10;  |ACABA;  |FINSI;

     aRuta = #101#5;  |QBF aRuta;
     aRuta = "F" + aRuta;

     |BROWSE aRuta;

     #101#5 = aRuta % 299;
     #101#5 = #101#5 > #101#5;
     |PINTA #101#5;
|FPRC;

|| ‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
|| PROCESOS GRABACION DE LOS PROGRAMAS Y SUS CAMPOS
|| ‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹


|PROCESO GrabaDef;
     aMas = aDirDef;  |QBF aMas;
     |CARGA_DEF aMas, referen@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aQueQuiero = "|NK";
     aAlfa      = #900#aQueQuiero#;
     nTCampo    = aAlfa;
     nTCampo    = nTCampo - 1;
     |SI nTCampo < 0;
         |DESCARGA_DEF #referen@;
         |ACABA;
     |FINSI;

     |PATH_DAT #900 aBaseFich;
     |ABRE #900;
     |LEE 040#900p;
     aHayDatos = "N";
     |SI FSalida = 0;  aHayDatos = "S";  |FINSI;
     |CIERRA #900;

     aQueQuiero = "|FICHERO";
     aFichero   = #900#aQueQuiero#;
     aFichero   = aFichero - aBaseFich;
     |SI aFichero ! aDef;
         |DESCARGA_DEF #referen@;
         |ACABA;
     |FINSI;

     aQueQuiero = "|TITULO";
     aTitulo    = #900#aQueQuiero#;

     aQueQuiero = "|NC";
     aFichero   = #900#aQueQuiero#;
     nTCampo    = aFichero;
     nTCampo    = nTCampo - 1;
     |PARA nCampo = 0;  |SI nCampo [ nTCampo;  |HACIENDO nCampo = nCampo + 1;
           #102#0     = #0#1;
           #102#1     = #0#2;
           #102#2     = aDef;
           #102#3     = nCampo;
           |LEE 040#102=;
           |SI FSalida ! 0;
               |DDEFECTO #102;
               #102#0     = #0#1;
               #102#1     = #0#2;
               #102#2     = aDef;
               #102#3     = nCampo;

               aQueQuiero = "|C" + (("000" + nCampo) % -103) + "NOMBRE";
               aFichero   = (#900#aQueQuiero# + (" " * 20)) % 120;
               #102#4     = aFichero;

               aQueQuiero = "|C" + (("000" + nCampo) % -103) + "TIPO";
               aFichero   = #900#aQueQuiero#;
               #102#5     = aFichero;

               |GRABA 020#102n;
               |LIBERA #102;
           |FINSI;
     |SIGUE;

     |DESCARGA_DEF #referen@;

     |DDEFECTO #101;
     #101#0 = #0#1;
     #101#1 = #0#2;
     #101#2 = aDef;
     |LEE 101#101=;
     |SI FSalida ! 0;
         |DDEFECTO #101;
         #101#0 = #0#1;
         #101#1 = #0#2;
         #101#2 = aDef;
         #101#3 = aTitulo;
         |GRABA 020#101b;
     |FINSI;
     #101#4 = aHayDatos;
     |GRABA 020#101a;
     |LIBERA #101;
|FPRC;

|PROCESO CargaDatos;
     nBusca  = 0;

     |DBASS aBase;  |QBT aBase;
     aBaseDef  = aBase + aAplicacion + "/def/";
     aBaseFich = aBase + aAplicacion + "/fich/" + #0#2 + "/";
     |QBT aBaseFich;

     aDirDef  = aBaseDef + "*.mas";
     |_PDIR aDirDef;
     |PARA;  |SI FSalida ] 0; |HACIENDO;
          aDef = aDirDef - aBaseDef;
          aDef = aDef - ".mas";
          |HAZ GrabaDef;
          |_SDIR aDirDef;
     |SIGUE;
|FPRC;

|RUTINA ClaveBaja101;
     #101#0 = #0#1;
     #101#1 = #0#2;
     #101#2 = "        ";
|FRUT;

|RUTINA ClaveAlta101;
     #101#0 = #0#1;
     #101#1 = #0#2;
     #101#2 = "zzzzzzzz";
|FRUT;

|RUTINA ClaveBaja102;
     #102#0 = #101#0;
     #102#1 = #101#1;
     #102#2 = #101#2;
     #102#3 = 000;
|FRUT;

|RUTINA ClaveAlta102;
     #102#0 = #101#0;
     #102#1 = #101#1;
     #102#2 = #101#2;
     #102#3 = 999;
|FRUT;

|PROCESO Tipo3;  |TIPO 3;
     |MENSA "      Actualizando Datos";

     |ABRE #101;
     |ABRE #102;
     |HAZ CargaDatos;
     |CIERRA #101;
     |CIERRA #102;

     |MENSA "                        ";

     |CABEZA "Importacion de Datos";
     |PINPA #0#0;

     |CONTROL_SIMPLE 0,"BOTON, <F2> Importacion Linea", 0502, 0530, Boton1;
     nBoton1  = FSalida;

     |CONTROL_SIMPLE 0,"BOTON, <F3> Importacion Masiva", 0532, 0560, Boton2;
     nBoton2  = FSalida;

     |ENTLINEAL #1#101, -3, 2, 22, 1, ClaveBaja101, ClaveAlta101;

     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;

     |PULSATECLA;
|FPRC;

|| ‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
|| DESDE AQUI EMPEZAMOS ...
|| ‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹

|PROGRAMA;
     |CLS;
     |CABEZA "Importacion de Datos";

     |ABRET #0;
     |LEE 101#0p;
     |SI FSalida ! 0;
         |DDEFECTO #0;
         |GRABA 020#0b;
     |FINSI;

     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
         |GRABA 020#0a;
     |FINSI;
     |LIBERA #0;
     |CIERRAT #0;

     |PULSATECLA;
|FPRO;

|PROCESO FicAlServidor;
     |DFICE aAlfa;
     aAlfa = aAlfa + Usuario;

     aIP = "";
     |IP_REMOTO aIP;
     |SI aIP = "";
          |COPIA_FICHERO aFicOrg, aAlfa;
     |SINO;
          |RECIBE_FICHERO aFicOrg, aAlfa;
     |FINSI;
     |SI FSalida = 0;
          aFicOrg = aAlfa;
     |SINO;
          aFicOrg = "";
     |FINSI;
|FPRC;
