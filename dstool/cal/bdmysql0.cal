|| -----------------------------------------------------------------------||
|| dsvisorx.cal                                                [dstool]   ||
||                                                                        ||
|| Utilidad encargada de visionar los datos de cualquier dat, para su     ||
|| filtrado, exportacion, consulta, etc ...                               ||
|| Solo para DIAGRAM.X                                                    ||
||                                                                        ||
|| Ini: 23feb2006                                                         ||
|| Ult: 24feb2006                                           dsPRODUCCION  ||
|| -----------------------------------------------------------------------||

|FICHEROS;
     :/dstool/def/dsvisorx       #000;
     :/dstool/def/dsw00006       #100;

     refer@                      #200;
|FIN;

|VARIABLES;
     aTecla    = "";
     aEsc1     = "";
     aEsc2     = "";
     aRetu     = "";
     nRango    = 0;
     nPosIni   = 0;
     nPosFin   = 0;

     nPanta0   = 0;
     nGrid01   = 0;

     aAlfa     = "";
     aVal      = "";

     aDirDats  = "";
     aDirDefs  = "";
     aLosDefs  = "";

     aDef      = "";
     aDef2     = "";
     nClaves   = 0;
     aNombDef  = "";

|FIN;








|| ----------------------------------------------------------------
|| Arranques/finalizaciones y tipos raros
|| ----------------------------------------------------------------
|PROCESO Tipo80; |TIPO 80;
     FSalida = 2999;
|FPRC;



|| -----------------------------------------------------------------
|| El programa.
|| -----------------------------------------------------------------
|PROGRAMA;

     |HAZ CargaTMP_Def;
     |ABRET    #100;
     |HAZ InicioX;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::{{001}}Salir," + nGrid01;
          |LEETECLA aTecla;
          |SI aTecla = aRetu; |SAL;  |FINSI;
          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
     |SIGUE;

     |CIERRAT  #100;
     |HAZ BorraTMPs;

|FPRO;






|| -----------------------------------------------------------------
|| Procesos de eventos
|| -----------------------------------------------------------------
|PROCESO evGrid01;
     |SI FSalida ! 4; |ACABA; |FINSI;

     aAlfa = #100#00; |QBT aAlfa;
     aAlfa = aDirDefs + aAlfa;
     |CARGA_DEF aAlfa, refer@;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |PATH_DAT #refer@#aDirDats#;
     |ABRE     #refer@
     |CONSULTA_CLAVE #1#refer@;
     |CIERRA   #refer@;

     |DESCARGA_DEF #refer@;
|FPRC;







|| -----------------------------------------------------------------
|| Programacion GRAFICA
|| -----------------------------------------------------------------

|PROCESO InicioX;
     |PINPA #0#00;
     nPanta0 = FSalida

     nRango  = 4 + 8 + 16 + 32;
     nPosIni = 1020;
     nPosFin = 2850;
     |LINEAL_SIMPLE #2#100, nRango, nPosIni, nPosFin, NULL, NULL, evGrid01;
     nGrid01 = FSalida;
|FPRC;









|| -----------------------------------------------------------------
|| Programacion CLASICA
|| -----------------------------------------------------------------

|PROCESO CargaTMP_Def;

     || temporal donde vamos a colocar los defs
     |ABRE #100; |CIERRA #100; |DELINDEX #100;
     aAlfa = "01" + Usuario;
     |NOME_DAT #100#aAlfa#;

     |ABRE #100;

     || path de los datos.
     |DFICE aDirDats; |QBF aDirDats

     || path de los defs.
     |DDEF aDirDefs; |QBF aDirDefs;
     aLosDefs = aDirDefs + "*.mas";
     |_PDIR aLosDefs;

     |PARA; |SI FSalida]0; |HACIENDO;
          aDef = aLosDefs - ".mas";
          |HAZ InfoDEF;
          |SI nClaves > 0;
               |DDEFECTO #100;
               #100#00 = aDef2;
               #100#01 = aNombDef;
               |GRABA 040#100.n;
          |FINSI;
          |_SDIR aLosDefs;
     |SIGUE;

     |CIERRA   #100;

|FPRC;







|PROCESO BorraTMPs;
     |ABRE #100; |CIERRA #100; |DELINDEX #100;
|FPRC;









|PROCESO InfoDEF;
     nClaves  = 0;
     aNombDef = "";
     aDef2    = "";

     |CARGA_DEF aDef, refer@;
     |SI FSalida ! 0; |ACABA; |FINSI;

     aVal = "|TITULO";
     aNombDef = #refer@#aVal#;

     aVal = "|NK";
     aAlfa = #refer@#aVal#;
     nClaves= aAlfa;

     aDef2 = aDef - aDirDefs;
     aDef2 = aDef2 - ".mas";

     |DESCARGA_DEF #refer@;
|FPRC;






























|| ----------------------------------------------------------------------
|| ----------------------------------------------------------------------

/*
|| PRUEBA lineal independiente + boton de salida.

|FICHEROS;
     zz000001            #00;
     dslpm500            #10;      || provincias
|FIN;

|VARIABLES;
     aTecla   = "";
     aEsc1    = "";
     aEsc2    = "";
     aAlfa    = "";

     nRango   = 0;
     nPosIni  = 0;
     nPosFin  = 0;

     nGrid01  = 0;
|FIN;

|PROCESO Tipo80; |TIPO 80;
     FSalida = 3099;
|FPRC;


|PROGRAMA;

     |PINPA #0#00;

     |ABRET #10;

     nRango  = 4 + 8 + 16 + 32;
     nPosIni = 1020;
     nPosFin = 2880;
     |LINEAL_SIMPLE #1#10, nRango, nPosIni, nPosFin, NULL, NULL, evGrid01;
     nGrid01 = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     |PARA; |SI;  |HACIENDO;
           FSalida = "::{{001}}Salir," + nGrid01;
           |LEETECLA aTecla;
           |SI aTecla = aEsc1; |SAL;  |FINSI;
           |SI aTecla = aEsc2; |SAL;  |FINSI;
     |SIGUE;

     |CIERRAT #10;

|FPRO;






|PROCESO evGrid01;
     |SI FSalida = 1;
          aAlfa = "0000" + #10#01;
          |MENSA aAlfa;
     |FINSI;
|FPRC;

*/
