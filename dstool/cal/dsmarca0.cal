|FICHEROS;
     dsmarca0 #0;
     dsmarca1 #1,MANTE;
     dsmarca2 #2,MANTE;
     dsmarca3 #3;

     dsmarca0@ #900;
     dsmarca1@ #901;
     referen@ #999;
|FIN;

|VARIABLES;
     nBoton1     = 0;
     nBoton2     = 0;
     nBoton3     = 0;
     nBoton4     = 0;
     nBoton10    = 0;
     nBoton20    = 0;
     nBoton21    = 0;
     nLong       = 0;
     nCaracter   = 0;
     nPos        = 0;
     nClave      = 0;
     nCampo      = 0;
     nCampoOtro  = 0;
     nTCampo     = 0;
     nHay        = 0;
     n9012       = 0;
     nEnabla21   = 0;
     nSemilla    = 0;
     nVuelta     = 0;
     nBucle      = 0;
     nPI         = 3.141592;
     nProducto   = 0.000000;
     nRnd        = 0;
     nLinea      = 0;
     nRedondeo   = 0;

     aBase       = "";
     aPath       = "";
     aLista      = "";
     aLong       = "";
     aCadena     = "";
     aCaracter   = "";
     aAlfa       = "";
     aBaseDef    = "";
     aDirDef     = "";
     aPathFich   = "";
     aQueQuiero  = "";
     aTitulo     = "";
     aDef        = "";
     aFichero    = "";
     aDescri     = "";
     aMarca      = "";
     aDesde      = "";
     aHasta      = "";
     a9010       = "";
     a9011       = "";
     aHora       = "";
     aBeta       = "";
     aPunto      = "";
|FIN;

|| ---------------------------------------
|| PROCESOS DEL PASE
|| ---------------------------------------

||-------------------- Proceso que genera aleatorios (variable 'rnd')-------
|| El numero aleatorio se devuelve en la variable 'rnd'
|| Ej: obtener un numero comprendido entre 1 y 100:
||  |HAZ Random;  rnd = rnd $  100; rnd = rnd + 1;  --->   1 ] rnd [ 100

|PROCESO Random;
     nVuelta = nVuelta + 1;
     |SI nSemilla [ 0; |O nVuelta > 200;
          |HORA aHora;
          aBeta    = aHora % 702;
          nSemilla = aBeta;
          nVuelta   = 0;
     |FINSI;

     nProducto = nSemilla / nPI;
     aBeta     = nProducto;
     |PARA nBucle = 101; |SI nBucle < 1000;|HACIENDO nBucle = nBucle + 100;
          aPunto = aBeta % nBucle;
          |SI aPunto = "."; |SAL; |FINSI;
     |SIGUE;

     nBucle   = nBucle + 104;
     aBeta    = aBeta % nBucle;
     nSemilla = aBeta;
     nRnd     = nSemilla;
|FPRC;
--------------------------------------

|PROCESO dsmarca1P;
     |SI #901#5 = "I";  |ACABA;  |FINSI;

     nCampo = #901#2;
     |SI #901#5 = "F";
         #999nCampo = #901#6;
     |FINSI;

     |SI #901#5 = "O";  |Y #901#7 ! -1;
         nCampoOtro = #901#7;
         #999nCampo = #999nCampoOtro;
     |FINSI;

     |SI #901#5 = "A";
         |ABRE #3
         #3#0 = #901#0;
         #3#1 = #901#1;
         #3#2 = #901#2;
         #3#3 = 9999;
         |LEE 000#3];
         |SI FSalida = 0;
             |LEE 000#3a;
         |SINO;
             |LEE 000#3u;
         |FINSI;

         nRedondeo = 0;
         |SI FSalida = 0;  |Y #3#0 = #901#0; |Y #3#1 = #901#1; |Y #3#2 = #901#2;
             nRedondeo = #3#3;
         |FINSI;

         |SI nRedondeo = 0;
             |CIERRA #3;
             |ACABA;
         |FINSI;

         |PARA;  |SI;  |HACIENDO;
              |HAZ Random;

              nLinea = nRnd $ nRedondeo;
              |SI nLinea > nRedondeo;  nLinea = nRedondeo;  |FINSI;
              |SI nLinea = 0;          nLinea = 1;          |FINSI;

              #3#0 = #901#0;
              #3#1 = #901#1;
              #3#2 = #901#2;
              #3#3 = nLinea;
              |LEE 000#3=;
              |SI FSalida = 0;
                  #999nCampo = #3#4;
                  |SAL;
              |FINSI;
         |SIGUE;
         |CIERRA #3;
     |FINSI;
|FPRC;

|DEFBUCLE dsmarca1P;
     #901#1003;
     ;
     #2#0, #2#1, 000;
     #2#0, #2#1, 999;
     ;
     NULL, dsmarca1P;
|FIN;

|PROCESO dsmarca2;
     aPathFich = #2#0;

     aDirDef   = aBaseDef + #2#1;  |QBF aDirDef;
     aDirDef   = aDirDef + ".mas";
     aPathFich = #2#0;             |QBF aPathFich;

     |CARGA_DEF aDirDef, referen@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |PATH_DAT #999 aPathFich;

     |ABRE #999;
     |LEE 101#999p;
     |PARA;  |SI FSalida = 0;  |HACIENDO;
             |BUCLE dsmarca1P;

             |GRABA 020#999a;
             |LIBERA #999;

             |LEE 101#999s;
     |SIGUE;
     |CIERRA #999;

     |DESCARGA_DEF #referen@;
|FPRC;

|DEFBUCLE dsmarca2;
     #2#2;
     ;
     "*", #0#0, "        ";
     "*", #0#0, "zzzzzzzz";
     ;
     NULL, dsmarca2;
|FIN;

|PROCESO Boton20;
     |ESTADO_CONTROL nBoton20, "DISABLE";
     |ESTADO_CONTROL nBoton21, "DISABLE";

     |HAZ dsmarca2;

     |ESTADO_CONTROL nBoton20, "ENABLE";
     |ESTADO_CONTROL nBoton21, "ENABLE";

     |MENAV "0000 Pase Realizado.";
|FPRC;

|PROCESO Boton21;
     |ESTADO_CONTROL nBoton20, "DISABLE";
     |ESTADO_CONTROL nBoton21, "DISABLE";

     |BUCLE dsmarca2;

     |ESTADO_CONTROL nBoton20, "ENABLE";
     |ESTADO_CONTROL nBoton21, "ENABLE";

     |MENAV "0000 Pase Realizado.";

     |SELECT #1#2;
     |PONCONTROL 23, "SI";
     |PTEC 812;
|FPRC;

|| ---------------------------------------

|PROCESO Baja0;
     #0#0 = "";
|FPRC;

|PROCESO Alta0;
     #0#0 = "z" * 100;
|FPRC;

|PROCESO Baja2;
     #2#0 = #0#0;
     #2#1 = "";
|FPRC;

|PROCESO Alta2;
     #2#0 = #0#0;
     #2#1 = "zzzzzzzz";
|FPRC;

|PROCESO Baja1;
     #1#0 = #2#0;
     #1#1 = #2#1;
     #1#2 = 0;
|FPRC;

|PROCESO Alta1;
     #1#0 = #2#0;
     #1#1 = #2#1;
     #1#2 = 999;
|FPRC;

|PROCESO Baja3;
     #3#0 = #1#0;
     #3#1 = #1#1;
     #3#2 = #1#2;
     #3#3 = 1;
|FPRC;

|PROCESO Alta3;
     #3#0 = #1#0;
     #3#1 = #1#1;
     #3#2 = #1#2;
     #3#3 = 9999;
|FPRC;

|PROCESO dsmarca3B;
     |BORRA 020#3a;
     |LIBERA #3;
|FPRC;

|DEFBUCLE dsmarca3B;
     #3#1;
     ;
     #1#0, #1#1, #1#2, 0001;
     #1#0, #1#1, #1#2, 9999;
     be;
     NULL, dsmarca3B;
|FIN;

|PROCESO dsmarca1B;
     |BUCLE dsmarca3B;

     |BORRA 020#1a;
     |LIBERA #1;
|FPRC;

|DEFBUCLE dsmarca1B;
     #1#1;
     ;
     #0#0, "        ", 001;
     #0#0, "zzzzzzzz", 999;
     be;
     NULL, dsmarca1B;
|FIN;

|PROCESO dsmarca3A;
     #3#0 = #0#0;
     |GRABA 020#3n;
     |LIBERA #3;
|FPRC;

|DEFBUCLE dsmarca3A;
     #3#1;
     ;
     #1#0, #1#1, #1#2, 0001;
     #1#0, #1#1, #1#2, 9999;
     be;
     NULL, dsmarca3A;
|FIN;

|PROCESO dsmarca1A;
     |BUCLE dsmarca3A;

     #1#0 = #0#0;
     |GRABA 020#1n;
     |LIBERA #1;
|FPRC;

|DEFBUCLE dsmarca1A;
     #1#1;
     ;
     #900#0, "        ", 001;
     #900#0, "zzzzzzzz", 999;
     be;
     NULL, dsmarca1A;
|FIN;

||--------------------------------------------------------------------------
|| PROCESOS DEL DSMARCA1
||--------------------------------------------------------------------------

|PROCESO Tipo7_1;  |TIPO 7;
     |SI #1#5 = "A";
         |ESTADO_CONTROL nBoton10, "ENABLE";
     |SINO;
         |ESTADO_CONTROL nBoton10, "DISABLE";
     |FINSI;

     |PINTA 1127, "                                                     ";
     nLong = #1#10;
     |SI nLong > 53;  nLong = 53;  |FINSI;

     |C_A #1#6, 1, nLong;  |PINTA #1#6;

     |SI nLong > 48;  nLong = 48;  |FINSI;
     |C_A #3#4, 1, nLong;

     |ENTLINEAL #1#3, -4, 7, 22, 1, Baja3, Alta3;
|FPRC;

|PROCESO PulsoIntro1;
     |LEE 101#1=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |ENDATOS #1#1;
     |SI FSalida = 0;
         |GRABA 020#1a;
     |FINSI;
     |LIBERA #1;
|FPRC;

|PROCESO TipoCampo;
     |C_M #1#6, 1, "N";
     |C_M #1#7, 1, "N";

     |SI #1#5 = "F";
         |C_M #1#6, 1, "S";
         |ACABA;
     |FINSI;

     #1#6 = "";  |PINTA #1#6;

     |SI #1#5 = "O";
         |C_M #1#7, 1, "S";
         |ACABA;
     |FINSI;

     #1#7 = -1;  |PINTA #1#7;
     #1#8 = "";  |PINTA #1#8;
|FPRC;

|PROCESO OtroCampo;
     |SI #1#5 ! "O";  |ACABA;  |FINSI;
     |SI #1#7 = -1;
         |MENAV "0000 Campo Incorrecto.";
         |ERROR;
     |FINSI;

     |LIBERA #1;
     |SALVA_FICHA #1;
     #1#2 = #1#7;
     |LEE 000#1=;
     |SI FSalida = 0;
        aDescri = #1#4;
     |SINO;
        aDescri = "";
     |FINSI;
     |REPON_FICHA #1;

     #1#8 = aDescri;

     |SI aDescri = "";
         |MENAV "0000 Campo Incorrecto.";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo12_1;  |TIPO 12;
     |SI #1#5 = "A";
         |ESTADO_CONTROL nBoton10, "ENABLE";
         |ENTLINEAL #1#3, -4, 2, 22, 0, Baja3, Alta3;
     |SINO;
         |ESTADO_CONTROL nBoton10, "DISABLE";
         |BUCLE dsmarca3B;
     |FINSI;

     |SELECT #1#901;
     #901#0 = #1#0;
     #901#1 = #1#1;
     #901#2 = #1#2;
     |LEE 101#901=;
     |SI FSalida ! 0;
         |DDEFECTO #901;
         #901#0 = #1#0;
         #901#1 = #1#1;
         #901#2 = #1#2;
         |GRABA 020#901b;
     |FINSI;

     |SI #1#5 ! "I";
         #901#3  = #1#3;
         #901#4  = #1#4;
         #901#5  = #1#5;
         #901#6  = #1#6;
         #901#7  = #1#7;
         #901#8  = #1#8;
         #901#9  = #1#9;
         #901#10 = #1#10;
         #901#11 = #1#11;
         #901#12 = #1#12;
         |GRABA 020#901a;
     |SINO;
         |BORRA 020#901a;
     |FINSI;
     |LIBERA #901;
|FPRC;

||--------------------------------------------------------------------------
|| PROCESOS DEL DSMARCA2
||--------------------------------------------------------------------------

|PROCESO Baja901;
     #901#5 = "A";
     #901#0 = "";
     #901#1 = "";
     #901#2 = 0;
|FPRC;

|PROCESO Alta901;
     #901#5 = "A";
     #901#0 = "z" * 100;
     #901#1 = "zzzzzzzz";
     #901#2 = 999;
|FPRC;

|PROCESO dsmarca3G;
     #3#0 = #1#0;
     #3#1 = #1#1;
     #3#2 = #1#2;
     |GRABA 020#3n;
     |LIBERA #3;
|FPRC;

|DEFBUCLE dsmarca3G;
     #3#1;
     ;
     a9010, a9011, n9012, 0001;
     a9010, a9011, n9012, 9999;
     be;
     NULL, dsmarca3G;
|FIN;

|PROCESO Boton10;
     |CONFI "0000NEste proceso copia de otro Campo el Contenido del Lineal de Aleatorios. Continuar";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SELECT #2#901;
     #901#5 = "A";
     #901#0 = "";
     #901#1 = "";
     #901#2 = 0;
     |LEE 000#901];
     |SI FSalida ! 0;  |O #901#5 ! "A";
         |MENAV "0000 No existe otros aleatorios";
         |SELECT #1#901;
         |ACABA;
     |FINSI;

     |CONSULTA_F_CLAVE #2#901, Baja901, Alta901;
     |SI FSalida = 0;
         |SI #1#0 = #901#0;  |Y #1#1 = #901#1;
          |Y #1#2 = #901#2;
             |MENAV "0000 Has elejido el mismo Campo.";
             |ACABA;
         |FINSI;

         a9010 = #901#0;
         a9011 = #901#1;
         n9012 = #901#2;

         |BUCLE dsmarca3B;
         |BUCLE dsmarca3G;

         |MENAV "0000 Copia realizada con exito.";

         |PONCONTROL 23, "SI";
         |PTEC 812;
     |FINSI;
|FPRC;

|PROCESO PulsoIntro2;
     aFichero = ("M1" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #1 aFichero;
     |ABRE #1;  |CIERRA #1;  |DELINDEX #1;

     |ABRE #1;

     aBaseDef  = #0#1;  |QBF aBaseDef;
     aDirDef   = aBaseDef + #2#1;  |QBF aDirDef;
     aDirDef   = aDirDef + ".mas";

     aDef = (aDirDef - aBaseDef) - ".mas";

     |CARGA_DEF aDirDef, referen@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aQueQuiero = "|NC";
     aFichero   = #999#aQueQuiero#;
     nTCampo    = aFichero;
     nTCampo    = nTCampo - 1;
     |PARA nCampo = 0;  |SI nCampo [ nTCampo;  |HACIENDO nCampo = nCampo + 1;
           |DDEFECTO #1;

           #1#0       = #2#0;
           #1#1       = #2#1;
           #1#2       = nCampo;
           #1#3       = #2#2;

           aQueQuiero = "|C" + (("000" + nCampo) % -103) + "NOMBRE";
           aFichero   = (#999#aQueQuiero# + (" " * 20)) % 120;
           #1#4       = aFichero;

           aQueQuiero = "|C" + (("000" + nCampo) % -103) + "TIPO";
           aFichero   = #999#aQueQuiero#;
           #1#9       = aFichero;

           aQueQuiero = "|C" + (("000" + nCampo) % -103) + "LONGITUD";
           aFichero   = ((#999#aQueQuiero# + (" " * 3)) % 103);
           #1#10      = aFichero;

           aQueQuiero = "|C" + (("000" + nCampo) % -103) + "MINIMO";
           aFichero   = ((#999#aQueQuiero# + (" " * 20)) % 120);
           #1#11      = aFichero;

           aQueQuiero = "|C" + (("000" + nCampo) % -103) + "MAXIMO";
           aFichero   = ((#999#aQueQuiero# + (" " * 20)) % 120);
           #1#12      = aFichero;

           |SELECT #1#901;
           #901#0 = #1#0;
           #901#1 = #1#1;
           #901#2 = #1#2;
           |LEE 101#901=;
           |SI FSalida = 0;
               #1#5 = #901#5;
               #1#6 = #901#6;
               #1#7 = #901#7;
               #1#8 = #901#8;
           |FINSI;

           |GRABA 020#1n;
           |LIBERA #1;
     |SIGUE;
     |CIERRA #1;

     |PUSHV 0501 2380;

     |PINPA #0#1;

     |CONTROL_SIMPLE 0, "BOTON,{{217}} Copiar Aleatorios", 0860, 0979, Boton10;
     nBoton10 = FSalida;

     |ESTADO_CONTROL nBoton10, "DISABLE";

     |ENTLINEAL #1#1, -3, 4, 22, 0, Baja1, Alta1;

     |FIN_CONTROL_WINDOWS nBoton10;

     |POPV;

     |PULSATECLA;

     |DELINDEX #1;

     |LEE 101#2=;
     |SI FSalida = 0;
         |SELECT #1#901;
         #901#0 = #2#0;
         #901#1 = #2#1;
         #901#2 = 0;
         |LEE 101#901];
         |SI FSalida = 0;  |Y  #901#0 = #2#0;  |Y #901#1 = #2#1;
             #2#3 = "*";
         |SINO;
             #2#3 = "";
         |FINSI;
         |GRABA 020#2a;
         |LIBERA #2;
     |FINSI;

     |PINTA #2#3;
|FPRC;

|PROCESO Tipo7_2;  |TIPO 7;
     |SI #2#3 = "*";
         |ESTADO_CONTROL nBoton20, "ENABLE";
         |ESTADO_CONTROL nBoton21, "ENABLE";
     |SINO;
         |ESTADO_CONTROL nBoton20, "DISABLE";
     |FINSI;
|FPRC;

||--------------------------------------------------------------------------
|| PROCESOS DEL DSMARCA0
||--------------------------------------------------------------------------

|PROCESO Tipo7;  |TIPO 7;
     |ESTADO_CONTROL nBoton2, "ENABLE";
     |ESTADO_CONTROL nBoton3, "ENABLE";
     |ESTADO_CONTROL nBoton4, "ENABLE";

     |LEE 000#0=;
     |SI FSalida ! 0;
         |ESTADO_CONTROL nBoton2, "DISABLE";
         |ESTADO_CONTROL nBoton3, "DISABLE";
         |ESTADO_CONTROL nBoton4, "DISABLE";
     |FINSI;
|FPRC;

|PROCESO MiraDef;
     |CARGA_DEF aDirDef, referen@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aDef = (aDirDef - aBaseDef) - ".mas";

     aQueQuiero = "|NK";
     aAlfa      = #999#aQueQuiero#;
     nClave     = aAlfa;
     |SI nClave = 0;
         |DESCARGA_DEF #referen@;
         |ACABA;
     |FINSI;

     aQueQuiero = "|FICHERO";
     aFichero   = #999#aQueQuiero#;
     aAlfa      = aFichero - aDef;
     |SI FSalida = 0;
         |DESCARGA_DEF #referen@;
         |ACABA;
     |FINSI;

     aQueQuiero = "|TITULO";
     aTitulo    = #999#aQueQuiero#;
     aMarca     = "";

     |DDEFECTO #2;
     #2#0  = #0#0;
     #2#1  = aDef;
     #2#2  = aTitulo;

     |SELECT #1#901;
     #901#0 = #2#0;
     #901#1 = #2#1;
     #901#2 = 1;
     |LEE 000#901];
     |SI FSalida = 0;  |Y #901#0 = #2#0;  |Y #901#1 = #2#1;
         aMarca    = "*";
         nEnabla21 = 1;
     |FINSI;

     #2#3  = aMarca;
     |GRABA 020#2n;
     |LIBERA #2;

     aAlfa = "0000 Grabando Tabla: " + #2#1;
     |MENSA aAlfa;

     |DESCARGA_DEF #referen@;
|FPRC;

|PROCESO CargaTablas;
     aFichero = ("M2" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #2 aFichero;
     |ABRE #2;  |CIERRA #2;  |DELINDEX #2;

     |ABRE #2;
     aBaseDef  = #0#1;  |QBF aBaseDef;
     aDirDef   = aBaseDef + "*.mas";
     nEnabla21 = 0;
     |_PDIR aDirDef;
     |PARA;  |SI FSalida ] 0; |HACIENDO;
          |HAZ MiraDef;
          |_SDIR aDirDef;
     |SIGUE;
     |CIERRA #2;

     aAlfa = "0000   ";
     |MENSA aAlfa;

     |PUSHV 0501 2380;
     |PINPA #0#2;

     |CONTROL_SIMPLE 0, "BOTON,{{217}} Ejecutar esta Tabla", 0860, 0979, Boton20;
     nBoton20 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{217}} Ejecutar todas las Tablas", 1160, 1279, Boton21;
     nBoton21 = FSalida;

     |ESTADO_CONTROL nBoton20, "DISABLE";

     |SI nEnabla21 = 0;
         |ESTADO_CONTROL nBoton21, "DISABLE";
     |FINSI;

     |ENTLINEAL #1#2, -2, 4, 22, 0, Baja2, Alta2;

     |FIN_CONTROL_WINDOWS nBoton20;
     |FIN_CONTROL_WINDOWS nBoton21;

     |PULSATECLA;

     |POPV;

     |DELINDEX #2;
|FPRC;

|PROCESO Boton1;
     |PUSHV 0501 2380;
     |BLANCO 0501 2380;
     |ATRI S;
     |PINTA 1301, "Introduzca el Directorio donde tenemos los datos.";
     |ATRI s;

     E_inf = "";
     E_sup = 100;
     aPath = 1401 ? 1;  |QBF aPath;
     |POPV;

     #0#0 = aPath;
     |LEE 000#0=;
     |SI FSalida = 0;
         |MENAV "0000 El Directorio ya existe.";
         |ACABA;
     |FINSI;

     aLista  = aPath + "*.dat";
     |_PDIR aLista;
     |SI FSalida ! 0;
         |MENAV "0000 Directorio no valido.";
         |ACABA;
     |FINSI;

     aLong   = aPath % 0;
     nLong   = aLong;
     aCadena = "";
     aBase   = "";
     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPos      = (nCaracter * 100) + 1;
           aCaracter = aPath % nPos;
           aCadena   = aCadena + aCaracter;
           aAlfa     = aCadena - "fich/";
           |SI FSalida ! 0;
               aBase = aAlfa;
               |SAL;
           |FINSI;
     |SIGUE;

     |SI aBase = "";
         |MENAV "0000 Aplicacion No Conocida.";
         |ACABA;
     |FINSI;

     |DDEFECTO #0;
     #0#0 = aPath;
     #0#1 = aBase + "def/";
     |GRABA 020#0n;
     |LIBERA #0;

     |HAZ CargaTablas;

     |PONCONTROL 23, "SI";
     |PTEC 812;
|FPRC;

|PROCESO Boton2;
     |ESTADO_CONTROL nBoton1, "DISABLE";
     |ESTADO_CONTROL nBoton2, "DISABLE";
     |ESTADO_CONTROL nBoton3, "DISABLE";
     |ESTADO_CONTROL nBoton4, "DISABLE";

     |HAZ CargaTablas;

     |ESTADO_CONTROL nBoton1, "ENABLE";
     |ESTADO_CONTROL nBoton2, "ENABLE";
     |ESTADO_CONTROL nBoton3, "ENABLE";
     |ESTADO_CONTROL nBoton4, "ENABLE";
|FPRC;

|PROCESO Boton3;
     |CONFI "0000NSeguros de dar de Baja la Marcacion";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |LEE 101#0=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |BUCLE dsmarca1B;

     |BORRA 020#0a;
     |LIBERA #0;

     |PONCONTROL 23, "SI";
     |PTEC 812;
|FPRC;

|PROCESO dsmarca0;
     |SI #900#0 = #0#0;  |ACABA;  |FINSI;

     nHay = 1;
|FPRC;

|DEFBUCLE dsmarca0;
     #900#2002;
     ;
     #0#1, aDesde;
     #0#1, aHasta;
     ;
     NULL, dsmarca0;
|FIN;

|PROCESO Baja900;
     #900#1 = #0#1;
     #900#0 = "";
|FPRC;

|PROCESO Alta900;
     #900#1 = #0#1;
     #900#0 = "z" * 100;
|FPRC;

|PROCESO Boton4;
     |CONFI "0000NEste proceso copia todo de otra Marcacion y borra lo que habia en esta. Continuar";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     nHay   = 0;
     aDesde = "";
     aHasta = "z" * 100;
     |BUCLE dsmarca0;
     |SI nHay = 0;
         |MENAV "0000 No hay otra Marcacion con la misma aplicacion configurada.";
         |ACABA;
     |FINSI;

     |ABRE #900;
     |SELECT #2#900;
     #900#1 = #0#1;
     #900#0 = "";
     |LEE 000#900];
     |CONSULTA_F_CLAVE #2#900, Baja900, Alta900;
     |SI FSalida = 0;
         |SI #0#0 = #900#0;
             |MENAV "0000 Has elejido la misma marcacion.";
             |CIERRA #900;
             |ACABA;
         |FINSI;

         |ESTADO_CONTROL nBoton1, "DISABLE";
         |ESTADO_CONTROL nBoton2, "DISABLE";
         |ESTADO_CONTROL nBoton3, "DISABLE";
         |ESTADO_CONTROL nBoton4, "DISABLE";

         |BUCLE dsmarca1B;
         |BUCLE dsmarca1A;

         |ESTADO_CONTROL nBoton1, "ENABLE";
         |ESTADO_CONTROL nBoton2, "ENABLE";
         |ESTADO_CONTROL nBoton3, "ENABLE";
         |ESTADO_CONTROL nBoton4, "ENABLE";

         |MENAV "0000 Copia realizada con exito.";
     |FINSI;
     |CIERRA #900;
|FPRC;

|PROGRAMA;
     |DBASE aBase;  |QBF aBase;

     aDef = aBase + "def/dsmarca1.mas";
     |CARGA_DEF aDef, dsmarca1@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aDef = aBase + "def/dsmarca0.mas";
     |CARGA_DEF aDef, dsmarca0@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |ABRET #901;

     |CLS;
     |CABEZA "Marcador de Campos";

     |PINPA #0#0;

     |CONTROL_SIMPLE 0, "BOTON,{{166}} Alta", 0764, 0879, Boton1;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{193}} Modificacion", 1064, 1179, Boton2;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{199}} Baja", 1364, 1479, Boton3;
     nBoton3 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{217}} Copiar ", 1664, 1779, Boton4;
     nBoton4 = FSalida;

     |ENTLINEAL #1#0, -1, 4, 22, 0, Baja0, Alta0;

     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;
     |FIN_CONTROL_WINDOWS nBoton4;

     |CIERRAT #901;

     |DESCARGA_DEF #dsmarca0@;
     |DESCARGA_DEF #dsmarca1@;

     |PULSATECLA;
|FPRO;
