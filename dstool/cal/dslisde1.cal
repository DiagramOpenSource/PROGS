|FICHEROS;
     dslisde1 #1;
     dslisde2 #2,MANTE;
     dslisde3 #3;
     dslisde4 #4;
     dslisde5 #5,MANTE;
     dslisde6 #6;   ||RELACIONES

     referen@ #900;
|FIN;

|VARIABLES;
     aIPRemoto = "";
     aCadena = "";
     aLen = "";
     nLen = 0;
     aCaracter = "";
     nCaracter = 0;
     aSinRaros = "";
     nCabLin = 0;


     aAlfa        = "";
     aAlfa1       = "";
     aAlfa2       = "";
     aAlfa3       = "";
     aAlfa4       = "";
     aDirAplica   = "";
     aDirDef      = "";
     aBase        = "";
     aBaseDef     = "";
     aFichero     = "";
     aAplica      = "";
     aDef         = "";
     aMas         = "";
     aQueQuiero   = "";
     aBaseFich    = "";
     aTitulo      = "";
     aDSoporte    = "";
     aPassword    = "";
     aDDef        = "";
     aHDef        = "";
     aDAplica     = "";
     aHAplica     = "";
     aDirLocal   = "";
     aDirRemoto   = "";

     nPos         = 0;
     nBusca       = 0;
     nTCampo      = 0;
     nTCompo      = 0;
     nCampo       = 0;
     nCampo1      = 0;
     nCompo2      = 0;
     nCompo       = 0;
     nNume        = 0;
     nPosi        = 0;
     nHandle      = 0;
     nMalo        = 0;
     nContador    = 0;
     nImpresion   = 0;
     aLongi = "";
     nLongi = 0;
     aRuta = "";
     aRutaBase = "";
     nPara = 0;
     nDeci = 0;
     aAsc = "";
     aFich = "";
     aRutaFich = "";
     aDefecto = "";

     &enSw        = 0;

     &eaA1        = "";
     &eaB1        = "";
     &eaC1        = "";
     &eaD1        = "";
     &eaE1        = "";
     &eaF1        = "";
     &eaA2        = "";
     &eaB2        = "";
     &eaC2        = "";
     &eaD2        = "";
     &eaE2        = "";
     &eaF2        = "";
     &eaA3        = "";
     &eaB3        = "";
     &eaC3        = "";
     &eaD3        = "";
     &eaE3        = "";
     &eaF3        = "";
     &eaG1 = "";
     &eaG2 = "";
     &eaG3 = "";
|FIN;

|RUTINA ClaveBaja1;
    #1#0 = aDAplica;
|FRUT;

|RUTINA ClaveAlta1;
    #1#0 = aHAplica;
|FRUT;

|RUTINA ClaveBaja2;
    #2#0 = #1#0;
    #2#1 = "        ";
|FRUT;

|RUTINA ClaveAlta2;
    #2#0 = #1#0;
    #2#1 = "zzzzzzzz";
|FRUT;

|RUTINA ClaveBaja3;
    #3#0 = #2#0;
    #3#1 = #2#1;
    #3#2 = 000;
|FRUT;

|RUTINA ClaveAlta3;
    #3#0 = #2#0;
    #3#1 = #2#1;
    #3#2 = 999;
|FRUT;

|RUTINA ClaveBaja4;
    #4#0 = #2#0;
    #4#1 = #2#1;
    #4#2 = 01;
|FRUT;

|RUTINA ClaveAlta4;
    #4#0 = #2#0;
    #4#1 = #2#1;
    #4#2 = 99;
|FRUT;

|RUTINA ClaveBaja5;
    #5#0 = "        ";
|FRUT;

|RUTINA ClaveAlta5;
    #5#0 = "zzzzzzzz";
|FRUT;

|RUTINA ClaveBaja6;
    #6#0 = #2#0;
    #6#1 = #2#1;
    #6#2 = 01;
|FRUT;

|RUTINA ClaveAlta6;
    #6#0 = #2#0;
    #6#1 = #2#1;
    #6#2 = 99;
|FRUT;

|PROCESO Emite3;
     nContador = nContador + 1;

     |SI nContador = 1;
         eaA1 = ("000" + #3#2) % -103;
         eaB1 = #3#3;
         eaC1 = #3#4;
         eaD1 = #3#5;
         eaE1 = #3#6;
         eaF1 = #3#7;
         eaG1 = #3#8;
     |FINSI;

     |SI nContador = 2;
         eaA2 = ("000" + #3#2) % -103;
         eaB2 = #3#3;
         eaC2 = #3#4;
         eaD2 = #3#5;
         eaE2 = #3#6;
         eaF2 = #3#7;
         eaG2 = #3#8;
     |FINSI;

     |SI nContador = 3;
         eaA3 = ("000" + #3#2) % -103;
         eaB3 = #3#3;
         eaC3 = #3#4;
         eaD3 = #3#5;
         eaE3 = #3#6;
         eaF3 = #3#7;
         eaG3 = #3#8;

         nContador = 0;
         |PRINT;

         eaA1 = ""; eaB1 = ""; eaC1 = ""; eaD1 = ""; eaE1 = ""; eaF1 = "";
         eaA2 = ""; eaB2 = ""; eaC2 = ""; eaD2 = ""; eaE2 = ""; eaF2 = "";
         eaA3 = ""; eaB3 = ""; eaC3 = ""; eaD3 = ""; eaE3 = ""; eaF3 = "";
     |FINSI;
|FPRC;

|DEFBUCLE dslisde3I;
     #3#1;
     ;
     #2#0, #2#1, 000;
     #2#0, #2#1, 999;
     ;
     NULL, Emite3;
|FIN;

|PROCESO Emite4;
     |PRINT;
|FPRC;

|DEFBUCLE dslisde4I;
     #4#1;
     ;
     #2#0, #2#1, 00;
     #2#0, #2#1, 99;
     be;
     NULL, Emite4;
|FIN;

|PROCESO Emite6;
     |PRINT;
|FPRC;

|DEFBUCLE dslisde6I;
     #6#1;
     ;
     #2#0, #2#1, 00;
     #2#0, #2#1, 99;
     be;
     NULL, Emite6;
|FIN;

|PROCESO Emite2;
     |SI nImpresion = 1;
         nContador = 0;
         enSw = 1;  |BUCLE dslisde4I;
         enSw = 3;  |BUCLE dslisde6I;
         enSw = 2;  |BUCLE dslisde3I;
         |SI nContador ! 0;  |PRINT;  |FINSI;

        |PIEINF;
     |SINO;
        |PRINT;
     |FINSI;
|FPRC;

|DEFBUCLE dslisde2I;
     #2#1;
     ;
     #1#0, aDDef;
     #1#0, aHDef;
     ;
     NULL, Emite2;
|FIN;

|DEFBUCLE dslisde2R;
     #2#2;
     ;
     #1#0, "                    ";
     #1#0, "zzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, Emite2;
|FIN;

|PROCESO Impresion;
     nImpresion = 1;

     ||MENAV "0000 Recuerde seleccione impresora DS-LASER y formato HORIZONTAL";

     Impresora = "Crystal Reports";
     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |INFOR "dslisdef";
     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

     |BUCLE dslisde2I;

     |FININF;
     |FINIMP;
|FPRC;

|PROCESO QuitaRaros;
     aAlfa1 = aAlfa2;
     |QBF aAlfa1;
     aCadena = "";
     aLen    = aAlfa1 % 0;
     nLen    = aLen;
     |PARA nCampo = 1;  |SI nCampo [ nLen;  |HACIENDO nCampo = nCampo + 1;
           nPosi = (100 * nCampo) + 1;
           aCaracter = aAlfa1 % nPosi;
           |SI aCaracter = "¥";  aCaracter = &209; |FINSI;
           |SI aCaracter = "¤";  aCaracter = &241; |FINSI;
           |SI aCaracter = " ";  aCaracter = &225; |FINSI;
           |SI aCaracter = "‚";  aCaracter = &233; |FINSI;
           |SI aCaracter = "¡";  aCaracter = &237; |FINSI;
           |SI aCaracter = "¢";  aCaracter = &243; |FINSI;
           |SI aCaracter = "£";  aCaracter = &250; |FINSI;
           |SI aCaracter = "§";  aCaracter = "_"; |FINSI;
           |SI aCaracter = "¦";  aCaracter = "_"; |FINSI;
           |SI aCaracter = "/";  aCaracter = "_"; |FINSI;
           |SI aCaracter = "-";  aCaracter = "_"; |FINSI;
           |SI aCaracter = " ";  aCaracter = "_"; |FINSI;
           |SI aCaracter = "~";  aCaracter = #3#2 + "~"; |FINSI;
           |SI aCaracter = "ñ";  aCaracter = "_"; |FINSI;
           |SI aCaracter = "í";  aCaracter = "i"; |FINSI;
           |SI aCaracter = "ú";  aCaracter = "u"; |FINSI;
           |SI aCaracter = "ó";  aCaracter = "o"; |FINSI;

           nCaracter = &aCaracter;
           |SI nCaracter = 192;  aCaracter = "A";  |FINSI;
           |SI nCaracter = 193;  aCaracter = "A";  |FINSI;
           |SI nCaracter = 200;  aCaracter = "E";  |FINSI;
           |SI nCaracter = 201;  aCaracter = "E";  |FINSI;
           |SI nCaracter = 204;  aCaracter = "I";  |FINSI;
           |SI nCaracter = 205;  aCaracter = "I";  |FINSI;
           |SI nCaracter = 209;  aCaracter = "¥";  |FINSI;
           |SI nCaracter = 210;  aCaracter = "O";  |FINSI;
           |SI nCaracter = 211;  aCaracter = "O";  |FINSI;
           |SI nCaracter = 217;  aCaracter = "U";  |FINSI;
           |SI nCaracter = 218;  aCaracter = "U";  |FINSI;
           |SI nCaracter = 186;  aCaracter = "§";  |FINSI;

           aCadena = aCadena + aCaracter;
     |SIGUE;
     aAlfa1 = aCadena;
     aSinRaros = aAlfa1;
     aAlfa2 = aAlfa1;
|FPRC;

|PROCESO Emite3SQL;
     |SI #3#2 = 0;
          aAlfa = "DROP TABLE IF EXISTS `" + aRuta  + "`;" + &10 + &13;
          ||XGRABA nHandle, aAlfa;
          aAlfa = "CREATE TABLE "+ &34 + aRuta + &34 +" ("
          |XGRABA nHandle, aAlfa;
     |FINSI;
     aAlfa2 = #3#3;      |QBF aAlfa2;
     |HAZ QuitaRaros;
     aAlfa2 = #3#2 + aAlfa2;
     aLongi = #3#5;      |QBF aLongi;
     aDefecto = #3#8;    |QBF aDefecto;

     |SI #3#4 = "A";
          |SI aDefecto = "";
               aDefecto = "NULL";
          |SINO;
               aDefecto = "'" + aDefecto + "'";
          |FINSI;
          aAlfa = &34 + aAlfa2 + &34 + " VARCHAR(" + aLongi + ") default " + aDefecto + "," + &10 + &13;
     |FINSI;

     |SI #3#4 = "N";
          |QBT aDefecto;
          |SI aDefecto = "";
               aDefecto = "NULL";
          |FINSI;
          aAlfa = #3#6 - ".";
          aAlfa3 = FSalida;
          aAlfa = #3#7 - ".";
          aAlfa4 = FSalida;
          |SI aAlfa4 ! 0; |O aAlfa3 ! 0;
               |SI aAlfa3 ! 0;
                   ||SISCO: Me quiero quedar en deci con los decimales que harian falta
                    aAlfa3 = aAlfa3  - (aAlfa3 % -102);
                    nDeci = #3#5 - aAlfa3;
               |SINO;
                    aAlfa4 = aAlfa4  - (aAlfa4 % -102);
                    nDeci = #3#5 - aAlfa4;
               |FINSI;
               aAlfa = #3#6 - ".?";
               aAlfa3 = FSalida;
               aAlfa = #3#7 - ".?";
               aAlfa4 = FSalida;
               |SI aAlfa3 ! 0; |O aAlfa4 ! 0;
                    nDeci = #3#5 + 6;
                    aLongi = nDeci;
                    nDeci = 6;
               |FINSI;
               aAlfa = &34 + aAlfa2 + &34 +" REAL default " + aDefecto + "," + &10 + &13;
          |SINO;
               aAlfa = &34 + aAlfa2 + &34 +" INT default " + aDefecto + "," + &10 + &13;
          |FINSI;
     |FINSI;

     |SI #3#4 = "F";
          |SI aDefecto = "";
               aDefecto = "NULL";
          |SINO;
               aDefecto = "'" + aDefecto +"'";
          |FINSI;
          aAlfa =&34 + aAlfa2 + &34 + " SMALLDATETIME default " + aDefecto + "," + &10 + &13;
     |FINSI;
     aAlfa = &9 + aAlfa;
     |XGRABA nHandle, aAlfa;
|FPRC;

|DEFBUCLE dslisde3SQL;
     #3#1;
     ;
     #2#0, #2#1, 00;
     #2#0, #2#1, 99;
     ;
     NULL, Emite3SQL;
|FIN;

|PROCESO ClavePrimaria;
     |ABRE #3;
     |ABRE #4;
     #4#0 = #2#0;
     #4#1 = #2#1;
     #4#2 = 1;
     |LEE 101#4=;
     |SI FSalida = 0;
          |PARA nPara = 1; |SI nPara [ 16; |HACIENDO nPara = nPara + 1;
               nCompo = nPara + 2;
               aAlfa3 = #4nCompo;  |QBF aAlfa3;
               |SI aAlfa3 ! "";
                    aAlfa2 = #4nCompo;  |QBF aAlfa2;
                    nLongi = aAlfa2;
                    |SI nLongi < 0;
                         nLongi = nLongi * -1;
                         aAsc = 0;
                    |SINO;
                         aAsc = 1;
                    |FINSI;
                    #3#0 = #2#0;
                    #3#1 = #2#1;
                    #3#2 = nLongi;
                    |LEE 101#3=;
                    |SI FSalida = 0;
                         aAlfa2 = #3#3;      |QBF aAlfa2;
                         |HAZ QuitaRaros;
                         aAlfa2 = #3#2 + aAlfa2;
                         |SI nPara > 1;
                              aAlfa = aAlfa + ","+ &34 + aAlfa2;
                         |SINO;
                              aAlfa = "PRIMARY KEY ("+ &34 + aAlfa2; |QBF aAlfa;
                         |FINSI;
                    |SINO;
                         |ACABA;
                    |FINSI;
                    |SI aAsc = 1;
                         aAlfa = aAlfa + &34 +" ASC";
                    |SINO;
                         aAlfa = aAlfa + &34 + " DESC";
                    |FINSI;
               |FINSI;
          |SIGUE;
     |SINO;
          |ACABA;
     |FINSI;
     aAlfa = aAlfa + ")";
     |XGRABA nHandle, aAlfa;
     |LIBERA #3;
     |LIBERA #4;
     |CIERRA #3;
     |CIERRA #4;
|FPRC;

|PROCESO dslisde6SQL;
     |SI #6#2 = 1;
          aAlfa = ", FOREIGN KEY (";
     |SINO;
          aAlfa = ", FOREIGN KEY (";
     |FINSI;
     aFich = #6#3;  |QBF aFich;
     aRutaFich = aRutaBase + #2#0;         |QBF aRutaFich;
     aRutaFich = aRutaFich + "_" +  aFich;
     aAlfa4 = "CREATE INDEX Rel" + #6#2 + " ON " + aRuta+ "(";
     |PARA nPara = 1; |SI nPara [ 16; |HACIENDO nPara = nPara + 1;
          nCompo = nPara + 3;
          aAlfa3 = #6nCompo;  |QBF aAlfa3;
          nLongi = aAlfa3;
          |SI aAlfa3 ! "";
               #3#0 = #2#0;
               #3#1 = #2#1;
               |SI nLongi < 0;
                    nLongi = nLongi * -1;
                    |ACABA;
               |FINSI;
               #3#2 = nLongi;
               |LEE 000#3=;
               |SI FSalida = 0;
                    aAlfa2 = #3#3;      |QBF aAlfa2;
                    |HAZ QuitaRaros;
                    aAlfa2 = #3#2 + aAlfa2;
                    |SI nPara = 1;
                         aAlfa = aAlfa + &34 + aAlfa2;
                         aAlfa4 = aAlfa4 + &34 + aAlfa2;
                    |SINO;
                         aAlfa = aAlfa + ","+&34 + aAlfa2;
                         aAlfa4 = aAlfa4 + ","+&34 + aAlfa2;
                    |FINSI;
                    |QBF aAlfa;
                    |QBF aAlfa4;
                    aAlfa = aAlfa + &34;
                    aAlfa4 = aAlfa4 + &34;
               |FINSI;
          |FINSI;
     |SIGUE;
     aAlfa = aAlfa + ")";
     aAlfa4 = aAlfa4 + ");";
     |XGRABA nHandle, aAlfa4;
     ||XGRABA nHandle, aAlfa;
     aAlfa = &9 + "REFERENCES " + aRutaFich + "(";

     #4#0 = #2#0;
     #4#1 = aFich;
     #4#2 = 1;
     |LEE 101#4=;
     |SI FSalida = 0;
          |PARA nPara = 1; |SI nPara [ 16; |HACIENDO nPara = nPara + 1;
               nCompo = nPara + 2;
               aAlfa3 = #4nCompo;  |QBF aAlfa3;
               |SI aAlfa3 ! "";
                    aAlfa2 = #4nCompo;  |QBF aAlfa2;
                    nLongi = aAlfa2;
                    |SI nLongi < 0;
                         nLongi = nLongi * -1;
                         aAsc = 0;
                    |SINO;
                         aAsc = 1;
                    |FINSI;
                    #3#0 = #2#0;
                    #3#1 = aFich;
                    #3#2 = nLongi;
                    |LEE 101#3=;
                    |SI FSalida = 0;
                         aAlfa2 = #3#3;      |QBF aAlfa2;
                         |HAZ QuitaRaros;
                         aAlfa2 = #3#2 + aAlfa2;
                         |SI nPara > 1;
                              aAlfa = aAlfa + ","+&34 + aAlfa2;     |QBF aAlfa;
                         |SINO;
                              aAlfa = aAlfa + &34 + aAlfa2;      |QBF aAlfa;
                         |FINSI;
                    |SINO;
                         |ACABA;
                    |FINSI;
                    |SI aAsc = 0;
                         aAlfa = aAlfa + " DESC";
                    |FINSI;
                    aAlfa = aAlfa + &34;
               |FINSI;
          |SIGUE;
     |SINO;
          |ACABA;
     |FINSI;
     aAlfa = aAlfa + ")";
     ||XGRABA nHandle, aAlfa;
|FPRC;

|DEFBUCLE dslisde6SQL;
     #6#1;
     ;
     #2#0, #2#1, 00;
     #2#0, #2#1, 99;
     ;
     NULL, dslisde6SQL;
|FIN;

|PROCESO CreaIndexRel;
     |ABRE #3;
     |ABRE #4;
     |BUCLE dslisde6SQL;
     |LIBERA #3;
     |LIBERA #4;
     |CIERRA #3;
     |CIERRA #4;
|FPRC;

|PROCESO dslisde4SQL;
     aAlfa = "CREATE INDEX clave" + #4#2;    |QBF aAlfa;
     aAlfa = aAlfa + " ON " + aRuta + "(";
     |PARA nPara = 1; |SI nPara [ 16; |HACIENDO nPara = nPara + 1;
          nCompo = nPara + 2;
          aAlfa3 = #4nCompo;  |QBF aAlfa3;
          |SI aAlfa3 ! "";
               nLongi = aAlfa3;
               |SI nLongi < 0;
                    nLongi = nLongi * -1;
                    aAsc = 0;
               |SINO;
                    aAsc = 1;
               |FINSI;
               #3#0 = #2#0;
               #3#1 = #2#1;
               #3#2 = nLongi;
               |LEE 000#3=;
               |SI FSalida = 0;
                    aAlfa2 = #3#3;      |QBF aAlfa2;
                    |HAZ QuitaRaros;
                    aAlfa2 = #3#2 + aAlfa2;
                    |SI nPara > 1;
                         aAlfa = aAlfa + "," + &34 + aAlfa2;     |QBF aAlfa;
                    |SINO;
                         aAlfa = aAlfa + &34 + aAlfa2; |QBF aAlfa;
                    |FINSI;
               |SINO;
                    |ACABA;
               |FINSI;
               |SI aAsc ! 1;
                    aAlfa = aAlfa + &34 +" DESC";
               |SINO;
                    aAlfa = aAlfa + &34;
               |FINSI;
          |FINSI;
     |SIGUE;
     aAlfa = aAlfa + ");";
     |XGRABA nHandle, aAlfa;
|FPRC;

|DEFBUCLE dslisde4SQL;
     #4#1;
     ;
     #2#0, #2#1, 02;
     #2#0, #2#1, 99;
     ;
     NULL, dslisde4SQL;
|FIN;

|PROCESO CreaIndex;
     |ABRE #3;
     |ABRE #4;
     |BUCLE dslisde4SQL;
     |LIBERA #3;
     |LIBERA #4;
     |CIERRA #3;
     |CIERRA #4;
|FPRC;

|PROCESO Emite2SQL;
     aRuta = aRutaBase + #2#0; |QBF aRuta;
     aRuta = aRuta + "_" + #2#1;     |QBF aRuta;
     ||CREATE TABLE y CAMPOS
     |BUCLE dslisde3SQL;
     ||CLAVE PRIMARIA
     |HAZ ClavePrimaria;

     aAlfa = ")";
     |XGRABA nHandle, aAlfa;

     ||CREAR INDICES A PARTIR DE LAS CLAVES SECUNDARIAS "CREATE INDEX"
     |HAZ CreaIndex;

     ||CREAR LOS INDICES DE LAS FOREIGN KEYS
     |HAZ CreaIndexRel;
|FPRC;

|DEFBUCLE dslisde2SQL;
     #2#1;
     ;
     #1#0, aDDef;
     #1#0, aHDef;
     ;
     NULL, Emite2SQL;
|FIN;

|PROCESO ImpreForKeys;
     aRuta = aRutaBase + #2#0;     |QBF aRuta;
     aRuta = aRuta +"_" +  #2#1;     |QBF aRuta;
     aAlfa = "ALTER TABLE " + &34 + aRuta + &34 + " ADD FOREIGN KEY (";
     aFich = #6#3;  |QBF aFich;
     aRutaFich = aRutaBase + #2#0;         |QBF aRutaFich;
     aRutaFich = aRutaFich + "_" +  aFich;
     nCabLin = 0;
     |PARA nPara = 1; |SI nPara [ 16; |HACIENDO nPara = nPara + 1;
          nCompo = nPara + 3;
          aAlfa3 = #6nCompo;  |QBF aAlfa3;
          nLongi = aAlfa3;
          |SI aAlfa3 ! "";
               #3#0 = #2#0;
               #3#1 = #2#1;
               |SI nLongi < 0;
                    nLongi = nLongi * -1;
                    nCabLin = 1;
                    |ACABA;
               |FINSI;
               #3#2 = nLongi;
               |LEE 000#3=;
               |SI FSalida = 0;
                    aAlfa2 = #3#3;      |QBF aAlfa2;
                    |HAZ QuitaRaros;
                    aAlfa2 = #3#2 + aAlfa2;
                    |SI nPara = 1;
                         aAlfa = aAlfa + &34 + aAlfa2;
                         aAlfa4 = aAlfa4 + &34 + aAlfa2;
                    |SINO;
                         aAlfa = aAlfa + ","+&34 + aAlfa2;
                         aAlfa4 = aAlfa4 + ","+&34 + aAlfa2;
                    |FINSI;
                    |QBF aAlfa;
                    |QBF aAlfa4;
                    aAlfa = aAlfa + &34;
                    aAlfa4 = aAlfa4 + &34;
               |FINSI;
          |FINSI;
     |SIGUE;
     aAlfa = aAlfa + ")";
     aAlfa4 = aAlfa4 + ");";
     ||XGRABA nHandle, aAlfa4;
     |XGRABA nHandle, aAlfa;
     aAlfa = &9 + "REFERENCES " + aRutaFich + "(";

     #4#0 = #2#0;
     #4#1 = aFich;
     #4#2 = 1;
     |LEE 101#4=;
     |SI FSalida = 0;
          |PARA nPara = 1; |SI nPara [ 16; |HACIENDO nPara = nPara + 1;
               nCompo = nPara + 2;
               |SI nCabLin = 1;
                    nCompo2 = nPara + 3;
                    aAlfa3 = #4nCompo2;  |QBF aAlfa3;
                    |SI aAlfa3 = "";    |SAL;   |FINSI;
               |FINSI;
               aAlfa3 = #4nCompo;  |QBF aAlfa3;
               |SI aAlfa3 ! "";
                    aAlfa2 = #4nCompo;  |QBF aAlfa2;
                    nLongi = aAlfa2;
                    |SI nLongi < 0;
                         nLongi = nLongi * -1;
                         aAsc = 0;
                    |SINO;
                         aAsc = 1;
                    |FINSI;
                    #3#0 = #2#0;
                    #3#1 = aFich;
                    #3#2 = nLongi;
                    |LEE 101#3=;
                    |SI FSalida = 0;
                         aAlfa2 = #3#3;      |QBF aAlfa2;
                         |HAZ QuitaRaros;
                         aAlfa2 = #3#2 + aAlfa2;
                         |SI nPara > 1;
                              aAlfa = aAlfa + ","+&34 + aAlfa2;     |QBF aAlfa;
                         |SINO;
                              aAlfa = aAlfa + &34 + aAlfa2;      |QBF aAlfa;
                         |FINSI;
                    |SINO;
                         |ACABA;
                    |FINSI;
                    |SI aAsc = 0;
                         aAlfa = aAlfa + " DESC";
                    |FINSI;
                    aAlfa = aAlfa + &34;
               |FINSI;
          |SIGUE;
     |SINO;
          |ACABA;
     |FINSI;
     aAlfa = aAlfa + ")";
     |XGRABA nHandle, aAlfa;
|FPRC;

|DEFBUCLE ImpreForKeys;
     #6#1;
     ;
     #2#0, #2#1, 00;
     #2#0, #2#1, 99;
     ;
     NULL, ImpreForKeys;
|FIN;

|PROCESO CreaForKeys;
     |ABRE #3;
     |ABRE #4;
     |BUCLE ImpreForKeys;
     |LIBERA #3;
     |LIBERA #4;
     |CIERRA #3;
     |CIERRA #4;
|FPRC;

|DEFBUCLE CreaForKeys;
     #2#1;
     ;
     #1#0, aDDef;
     #1#0, aHDef;
     ;
     NULL, CreaForKeys;
|FIN;


|PROCESO ImprimeSQL;
     aAlfa = "D" + "\";
     |BROWSE aAlfa;
     aAlfa = (aAlfa % 270) + "\";  |QBF aAlfa;
     aDirRemoto = aAlfa + "exporta.sql";
     |HORA aAlfa;
     aAlfa = aAlfa - ":";
     aAlfa = aAlfa - ":";
     aDirLocal = "/tmp/" + Usuario + aAlfa + ".sql";
     |XABRE "W", aDirLocal, nHandle;
     |SI FSalida ] 0;
          aRutaBase = "";
          |DBASS aBase;
          aLongi = aBase % 0;
          nLongi = aLongi;
          |PARA nPara = 1; |SI nPara [ nLongi; |HACIENDO nPara = nPara + 1;
               nPosi = nPara * 100 + 1;
               aAlfa = aBase % nPosi;
               |SI aAlfa = "/"; |O aAlfa = "\";
                    aAlfa = "_";
               |FINSI;
               aRutaBase = aRutaBase + aAlfa;
          |SIGUE;

          aAlfa = "/*!40101 SET @OLD_CHARACTER_SET_CLIENT=@@CHARACTER_SET_CLIENT */";
          |XGRABA nHandle, aAlfa;

          aAlfa = "/*!40101 SET @OLD_CHARACTER_SET_RESULTS=@@CHARACTER_SET_RESULTS */";
          |XGRABA nHandle, aAlfa;

          aAlfa = "/*!40101 SET @OLD_COLLATION_CONNECTION=@@COLLATION_CONNECTION */";
          |XGRABA nHandle, aAlfa;

          aAlfa = "/*!40101 SET NAMES utf8 */";
          |XGRABA nHandle, aAlfa;

          aAlfa = "/*!40014 SET @OLD_UNIQUE_CHECKS=@@UNIQUE_CHECKS, UNIQUE_CHECKS=0 */";
          |XGRABA nHandle, aAlfa;

          aAlfa = "/*!40014 SET @OLD_FOREIGN_KEY_CHECKS=@@FOREIGN_KEY_CHECKS, FOREIGN_KEY_CHECKS=0 */";
          |XGRABA nHandle, aAlfa;

          aAlfa = "/*!40101 SET @OLD_SQL_MODE=@@SQL_MODE, SQL_MODE=`NO_AUTO_VALUE_ON_ZERO` */";
          |XGRABA nHandle, aAlfa;

          aAlfa = "SET FOREIGN_KEY_CHECKS = 0;";
          ||XGRABA nHandle, aAlfa;

          |BUCLE dslisde2SQL;
          |BUCLE CreaForKeys;

          aAlfa = "SET FOREIGN_KEY_CHECKS = 1;";
          ||XGRABA nHandle, aAlfa;

          aAlfa = "/*!40101 SET SQL_MODE=@OLD_SQL_MODE */";
          |XGRABA nHandle, aAlfa;

          aAlfa = "/*!40014 SET FOREIGN_KEY_CHECKS=@OLD_FOREIGN_KEY_CHECKS */";
          |XGRABA nHandle, aAlfa;

          aAlfa = "/*!40014 SET UNIQUE_CHECKS=@OLD_UNIQUE_CHECKS */";
          |XGRABA nHandle, aAlfa;

          aAlfa = "/*!40101 SET CHARACTER_SET_CLIENT=@OLD_CHARACTER_SET_CLIENT */";
          |XGRABA nHandle, aAlfa;

          aAlfa = "/*!40101 SET CHARACTER_SET_RESULTS=@OLD_CHARACTER_SET_RESULTS */";
          |XGRABA nHandle, aAlfa;

          aAlfa = "/*!40101 SET COLLATION_CONNECTION=@OLD_COLLATION_CONNECTION */";
          |XGRABA nHandle, aAlfa;
          |XCIERRA nHandle;

          aIPRemoto = "";
          |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;
          |SI aIPRemoto ! "";
               |ENVIA_FICHERO aDirLocal, aDirRemoto;
          |SINO;
               |COPIA_FICHERO aDirLocal, aDirRemoto;
          |FINSI;

     |FINSI;
|FPRC;

|PROCESO ImpresionRes;
     nImpresion = 2;

     Impresora = "Crystal Reports";
     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |INFOR "dslisde1";
     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

     |BUCLE dslisde2R;

     |PIEINF;
     |FININF;
     |FINIMP;
|FPRC;

|PROCESO Borra3;
     |BORRA 020#3a;
     |LIBERA #3;
|FPRC;

|DEFBUCLE dslisde3B;
     #3#1;
     ;
     #2#0, #2#1, 000;
     #2#0, #2#1, 999;
     be;
     NULL, Borra3;
|FIN;

|PROCESO Borra4;
     |BORRA 020#4a;
     |LIBERA #4;
|FPRC;

|DEFBUCLE dslisde4B;
     #4#1;
     ;
     #2#0, #2#1, 00;
     #2#0, #2#1, 99;
     be;
     NULL, Borra4;
|FIN;

|PROCESO Tipo0lisde5;  |TIPO 0;
     aAplica = #5#0;
     |PTEC 807;
|FPRC;

|PROCESO Tipo7lisde2;  |TIPO 7;
     |ENTLINEAL #1#3, -3, 7, 22, 0, ClaveBaja3, ClaveAlta3;
|FPRC;

|PROCESO Tipo11lisde2;  |TIPO 11;
     |SI FSalida = 10;
         |ENTLINEAL #1#3, -3, 4, 22, 0, ClaveBaja3, ClaveAlta3;
     |FINSI;

     |SI FSalida = 11;
         |PUSHV 0501 2380;
         |ENTLINEAL #1#4, -3, 4, 21, 1, ClaveBaja4, ClaveAlta4;
         |POPV;
         |ERROR;
     |FINSI;

     |SI FSalida = 12;
         |CONFI "0000NEsta seguro de Excluir el Def ";
         |SI FSalida ! 0;  |ERROR;  |ACABA;  |FINSI;

         |DFICE aDSoporte;   |QBF aDSoporte;
         aDSoporte = aDSoporte + "dslisdef.txt";
         |XABRE "A", aDSoporte, nHandle;
         |SI FSalida ] 0;
             aAlfa = #2#0 + #2#1 + &13 + &10;
             |XGRABA nHandle, aAlfa;

             |LEE 101#2=;
             |BUCLE dslisde3B;
             |BUCLE dslisde4B;
             |BORRA 020#2a;
             |LIBERA #2;

             |PONCONTROL 23, "SI";
         |FINSI;
         |XCIERRA nHandle;
     |FINSI;

     |SI FSalida = 13;
         aDDef = #2#1;
         aHDef = #2#1;
         |HAZ Impresion;
         |PONCONTROL 23, "SI";
     |FINSI;

     |SI FSalida = 14;
         |PUSHV 0501 2380;
         |ENTLINEAL #1#6, 3, 4, 21, 1, ClaveBaja6, ClaveAlta6;
         |POPV;
         |ERROR;
     |FINSI;

     |SI FSalida = 15;
          aDDef = #2#1;
          aHDef = #2#1;
          |HAZ ImprimeSQL;
          |PONCONTROL 23, "SI";
     |FINSI;
|FPRC;

|PROCESO Tipo7lisde1;  |TIPO 7;
     |ENTLINEAL #1#2, -2, 7, 22, 0, ClaveBaja2, ClaveAlta2;

     |ABRE #2;
     #2#0 = #1#0;
     #2#1 = "        ";
     |LEE 040#2];
     |CIERRA #2;

     |ENTLINEAL #1#3, -3, 7, 22, 0, ClaveBaja3, ClaveAlta3;
|FPRC;

|PROCESO Tipo11lisde1;  |TIPO 11;
     |SI FSalida = 10;
         |ENTLINEAL #1#2, -2, 4, 22, 0, ClaveBaja2, ClaveAlta2;
     |FINSI;

     |SI FSalida = 11;
         aDDef = "        ";
         aHDef = "zzzzzzzz";
         |HAZ Impresion;
     |FINSI;

     |SI FSalida = 12;
         aDDef = "        ";
         aHDef = "zzzzzzzz";
         |HAZ ImpresionRes;
     |FINSI;

     |SI FSalida = 13;
          aDDef = "        ";
          aHDef = "zzzzzzzz";
          |HAZ ImprimeSQL;
          |PONCONTROL 23, "SI";
     |FINSI;

|FPRC;

|PROCESO GrabaDef;
     nMalo = 0;
     aAlfa = ((aAplica + "        ") % 108) + ((aDef + "        ") % 108);
     |DFICE aDSoporte;   |QBF aDSoporte;
     aDSoporte = aDSoporte + "dslisdef.txt";
     |XABRE "U", aDSoporte, nHandle;
     |SI FSalida ] 0;
         |PARA; |SI; |HACIENDO;
              |XLEE nHandle, 250, aAlfa1;
              |SI FSalida < 0;  |SAL;  |FINSI;

              |QBF aAlfa1;
              |SI aAlfa = aAlfa1;  nMalo = 1;  |SAL;  |FINSI;
         |SIGUE;
     |FINSI;
     |XCIERRA nHandle;

     |SI nMalo = 1;  |ACABA;  |FINSI;

     aMas = aDirDef;  |QBF aMas;
     |CARGA_DEF aMas, referen@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aQueQuiero = "|NK";
     aAlfa      = #900#aQueQuiero#;
     nTCampo    = aAlfa;
     nTCampo    = nTCampo - 1;
     |SI nTCampo < 0;
         |DESCARGA_DEF #referen@;
         |ACABA;
     |FINSI;

     |PATH_DAT #900 aBaseFich;

     aQueQuiero = "|FICHERO";
     aFichero   = #900#aQueQuiero#;
     aFichero   = aFichero - aBaseFich;
     |SI aFichero ! aDef;
         |DESCARGA_DEF #referen@;
         |ACABA;
     |FINSI;

     aQueQuiero = "|TITULO";
     aTitulo    = #900#aQueQuiero#;

     aQueQuiero = "|NC";
     aFichero   = #900#aQueQuiero#;
     nTCampo    = aFichero;
     nTCampo    = nTCampo - 1;
     |PARA nCampo = 0;  |SI nCampo [ nTCampo;  |HACIENDO nCampo = nCampo + 1;
           #3#0       = #1#0;
           #3#1       = aDef;
           #3#2       = nCampo;

           aQueQuiero = "|C" + (("000" + nCampo) % -103) + "NOMBRE";
           aFichero   = (#900#aQueQuiero# + (" " * 20)) % 120;
           #3#3       = aFichero;

           aQueQuiero = "|C" + (("000" + nCampo) % -103) + "TIPO";
           aFichero   = #900#aQueQuiero#;
           #3#4       = aFichero;

           aQueQuiero = "|C" + (("000" + nCampo) % -103) + "LONGITUD";
           aFichero   = ((#900#aQueQuiero# + (" " * 3)) % 103);
           #3#5       = aFichero;

           aQueQuiero = "|C" + (("000" + nCampo) % -103) + "MINIMO";
           aFichero   = ((#900#aQueQuiero# + (" " * 20)) % 120);
           #3#6       = aFichero;

           aQueQuiero = "|C" + (("000" + nCampo) % -103) + "MAXIMO";
           aFichero   = ((#900#aQueQuiero# + (" " * 20)) % 120);
           #3#7       = aFichero;

           aQueQuiero = "|C" + (("000" + nCampo) % -103) + "DEFECTO";
           aFichero   = ((#900#aQueQuiero# + (" " * 20)) % 120);
           #3#8       = aFichero;

           |GRABA 020#3n;
           |LIBERA #3;
     |SIGUE;

     aQueQuiero = "|NK";
     aFichero   = #900#aQueQuiero#;
     nTCampo    = aFichero;
     nTCampo    = nTCampo - 1;
     |PARA nCampo = 0;  |SI nCampo [ nTCampo;  |HACIENDO nCampo = nCampo + 1;
           aQueQuiero = "|K" + (("000" + nCampo) % -103);
           aFichero   = (#900#aQueQuiero# % 103);
           nTCompo    = aFichero;

           |DDEFECTO #4;
           #4#0 = #1#0;
           #4#1 = aDef;
           #4#2 = nCampo + 1;
           |PARA nCompo = 1;  |SI nCompo [ nTCompo;  |HACIENDO nCompo = nCompo + 1;
                 nCampo1   = nCompo + 2;
                 nPosi     = (nCompo * 300) + 103;
                 aAlfa     = #900#aQueQuiero# % nPosi;
                 #4nCampo1 = aAlfa;
           |SIGUE;
           |GRABA 020#4n;
           |LIBERA #4;
     |SIGUE;

     aQueQuiero = "|NR";
     aFichero   = #900#aQueQuiero#;
     nTCampo    = aFichero;
     nTCampo    = nTCampo - 1;
     |PARA nCampo = 0;  |SI nCampo [ nTCampo;  |HACIENDO nCampo = nCampo + 1;
           aQueQuiero = "|R" + (("000" + nCampo) % -103);
           aFichero   = #900#aQueQuiero#;

           |DDEFECTO #6;
           #6#0 = #1#0;
           #6#1 = aDef;
           #6#2 = nCampo + 1;
           aAlfa = aFichero - "|";
           aAlfa = FSalida % 101;
           nPosi = aAlfa;
           nPosi = nPosi + 100 - 1;
           aAlfa = aFichero % nPosi;
           #6#3 = aAlfa;

           aFichero = aFichero - aAlfa - "|";
           aAlfa = aFichero % 103;
           nTCompo = aAlfa;

           |PARA nCompo = 1;  |SI nCompo [ nTCompo;  |HACIENDO nCompo = nCompo + 1;
                 nPosi     = (nCompo * 300) + 103;
                 nCampo1 = nCompo + 3;
                 aAlfa = aFichero % nPosi;
                 nLongi = aAlfa;
                 |SI nLongi < 0;
                    nLongi = nLongi + 1;
                    nLongi = nLongi * (-1);
                    aAlfa = nLongi;
                    aAlfa = ("00" + aAlfa) % -102;
                    aAlfa = "-" + aAlfa;
                 |SINO;
                    nLongi = nLongi - 1;
                    aAlfa = nLongi;
                    aAlfa = ("000" + aAlfa) % -103;
                 |FINSI;
                 #6nCampo1 = aAlfa;
           |SIGUE;
           |GRABA 020#6n;
           |LIBERA #6;
     |SIGUE;

     |DESCARGA_DEF #referen@;

     nBusca = 1;
     #2#0 = #1#0;
     #2#1 = aDef;
     #2#2 = aTitulo;
     |GRABA 020#2n;
     |LIBERA #2;
|FPRC;

|PROCESO BuscaDef;
     nBusca  = 0;
     aAplica = #1#0;  |QBF aAplica;
     aBaseDef  = aBase + aAplica + "/def/";
     aBaseFich = aBase + aAplica + "/fich/";
     aDirDef  = aBaseDef + "*.mas";
     |_PDIR aDirDef;
     |PARA;  |SI FSalida ] 0; |HACIENDO;
          aDef = aDirDef - aBaseDef;
          aDef = aDef - ".mas";
          |HAZ GrabaDef;
          |_SDIR aDirDef;
     |SIGUE;

     |SI nBusca = 0;
         |BORRA 020#1a;
     |FINSI;
     |LIBERA #1;
|FPRC;

|DEFBUCLE dslisde1;
     #1#1;
     ;
     aDAplica;
     aHAplica;
     be;
     NULL, BuscaDef;
|FIN;

|PROCESO GrabaEl1;
     #1#0 = aAlfa;
     aTitulo   = "";
     aDSoporte = aBase + #1#0 + "/" + #1#0 + ".men";  |QBT aDSoporte;
     |XABRE "U", aDSoporte, nHandle;
     |SI FSalida ] 0;
         |XLEE nHandle, 250, aAlfa1;
         |SI FSalida ] 0;
             aTitulo = aAlfa1 % 350;
         |FINSI;
     |FINSI;
     |XCIERRA nHandle;

     |SI aTitulo ! "";
         #1#1 = aTitulo;
         |GRABA 020#1n;

         #5#0 = #1#0;
         #5#1 = #1#1;
         |GRABA 020#5n;
     |FINSI;
|FPRC;

|PROCESO RecogeDatos;
     |ABRE #1;
     |ABRE #2;
     |ABRE #3;
     |ABRE #4;
     |ABRE #5;
     |ABRE #6;

     |DBASS aBase;  |QBF aBase;
     aDirAplica = aBase + "*";
     |_PDIR aDirAplica;
     |PARA;  |SI FSalida ] 0; |HACIENDO;
             aAlfa = aDirAplica - ".";
             |SI aAlfa = aDirAplica;
                 aAlfa = aDirAplica - aBase;
                 |HAZ GrabaEl1;
             |FINSI;
             |_SDIR aDirAplica;
     |SIGUE;

     #5#0 = "  TODAS ";
     #5#1 = "Listar Todas";
     |GRABA 020#5n;
     |LIBERA #5;
     |CIERRA #5;

     |PUSHV 0501 2380;
     |CLS;
     aAplica = "";
     |ENTLINEAL #1#5, -1, 4, 22, 1, ClaveBaja5, ClaveAlta5;
     |PULSATECLA;
     |POPV;

     |SI aAplica = "";  |ACABA;  |FINSI;

     aDAplica = aAplica;
     aHAplica = aAplica;
     |SI aAplica = "  TODAS ";
         aDAplica = "        ";
         aHAplica = "zzzzzzzz";
     |FINSI;

     |BUCLE dslisde1;

     |CIERRA #1;
     |CIERRA #2;
     |CIERRA #3;
     |CIERRA #4;
     |CIERRA #6;
|FPRC;

|PROGRAMA;
     |CLS;
     |CUADRO 0501 2380;
     |BLANCO 0602 2279;
     |PINTA 1320, "Introduzca Password : ";
     E_inf = "";
     E_sup = "20";
     aPassword = 1342 ? -1;
     |SI aPassword ! "<MYPASWD>     ";
         |MENAV "     Permiso denegado.";
         |ACABA;
     |FINSI;

     |CLS;
     |CUADRO 1110 1570;
     |ATRI S;
     |PINTA 1325, "PROCESANDO, ESPERESE POR FAVOR";
     |ATRI s;

     aFichero = ("de1" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #1 aFichero;
     |ABRE #1;  |CIERRA #1;  |DELINDEX #1;

     aFichero = ("de2" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #2 aFichero;
     |ABRE #2;  |CIERRA #2;  |DELINDEX #2;

     aFichero = ("de3" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #3 aFichero;
     |ABRE #3;  |CIERRA #3;  |DELINDEX #3;

     aFichero = ("de4" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #4 aFichero;
     |ABRE #4;  |CIERRA #4;  |DELINDEX #4;

     aFichero = ("de5" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #5 aFichero;
     |ABRE #5;  |CIERRA #5;  |DELINDEX #5;

     aFichero = ("de6" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #6 aFichero;
     |ABRE #6;  |CIERRA #6;  |DELINDEX #6;

     |HAZ RecogeDatos;

     |CLS;
     |ENTLINEAL #1#1, -1, 4, 22, 1, ClaveBaja1, ClaveAlta1;

     |DELINDEX #1;
     |DELINDEX #2;
     |DELINDEX #3;
     |DELINDEX #4;
     |DELINDEX #5;
     |DELINDEX #6;
|FPRO;
