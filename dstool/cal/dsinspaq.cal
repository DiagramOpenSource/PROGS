|FICHEROS;
     dsinspaq;
|FIN;

|VARIABLES;
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     aAlfa4 = "";
     nNume1 = 0;
     nNume2 = 0;
     nNume3 = 0;
     nNume4 = 0;
     nPara1 = 0;
     nPara2 = 0;
     nPara3 = 0;
     nPara4 = 0;
     fFech1 = @;
     fFech2 = @;
     fFech3 = @;
     fFech4 = @;
     nFEstado = 0;
     nSeleccio = 0;
     nModo = 0;

     aIniOri = "";
     aIniDes = "";
     nHandle = 0;
     nClave = 0;
     nSwLectura = 0;
     nInsPaq = 0;
     nCuenta = 0;
     nImporta = 0;

|FIN;
____________________________________/ DESDE PANTALLA
|PROCESO Browse7; |TIPO 7;
     aAlfa1 = "A*.ini";
     |BROWSE aAlfa1;
     aAlfa1 = (aAlfa1 % 270); |QBF aAlfa1;
     #dsinspaq#1 = aAlfa1;
     |PINTA #dsinspaq#1;
     |ERROR;
|FPRC;

|PROCESO Browse0; |TIPO 0;
     aAlfa1 = #dsinspaq#1;
     |QBF aAlfa1;
     |XABRE "E", aAlfa1, 0;
     |SI FSalida ! 0;
          |MENAV "0000Fichero inaccesible.";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO IniSistema7; |TIPO 7;
     aIniDes = "";
     |DBASS aIniDes;
     aIniDes = aIniDes + "ds.ini";

     #dsinspaq#2 = aIniDes;
     |PINTA #dsinspaq#2;

     |PAUSA;
|FPRC;
____________________________________/ DESDE TIPO3
|PROCESO CargaDes;
     |XABRE "A", aIniDes, nHandle;

     |PARA nPara1 = 3; |SI nPara1 [ 52; |HACIENDO nPara1 = nPara1 + 1;
          aAlfa1 = #dsinspaq#nPara1#;
          |QBF aAlfa1;
          |SI aAlfa1 ! "";
               nInsPaq = nInsPaq + 1;
               aAlfa2 = nInsPaq;
               aAlfa2 = ("00" + nInsPaq) % -102;
               aAlfa3 = "InsPaq" + aAlfa2 + "=" + aAlfa1;
               |XGRABA nHandle, aAlfa3;
               nCuenta = nCuenta + 1;
          |FINSI;
     |SIGUE;
     |XCIERRA nHandle;
|FPRC;

|PROCESO CuentaDes;
     |XABRE "U", aIniDes, nHandle;
     nSwLectura = FSalida;
     |PARA; |SI nSwLectura ] 0; |HACIENDO;
          |XLEE nHandle, 250, aAlfa1;
          nSwLectura = FSalida;
          aAlfa2 = aAlfa1 - "InsPaq";
          |SI aAlfa1 ! aAlfa2;
               aAlfa2 = aAlfa1 % 1013;
               |PARA nPara1 = 2; |SI nPara1 [ 52; |HACIENDO nPara1 = nPara1 + 1;
                    aAlfa3 = #dsinspaq#nPara1#;
                    |QBF aAlfa3;
                    |SI aAlfa3 = aAlfa2;
                         #dsinspaq#nPara1# = "";
                    |FINSI;
               |SIGUE;
               aAlfa2 = aAlfa1 % 702;
               nNume1 = aAlfa2;
               |SI nNume1 > nInsPaq;
                    nInsPaq = nNume1;
               |FINSI;
          |FINSI;
     |SIGUE;
     |XCIERRA nHandle;
|FPRC;

|PROCESO CargaOri;
     nClave = 2;
     |XABRE "CU", aIniOri, nHandle;
     nSwLectura = FSalida;
     |PARA; |SI nSwLectura ] 0; |HACIENDO;
          |XLEE nHandle, 250, aAlfa1;
          nSwLectura = FSalida;
          aAlfa2 = aAlfa1 - "InsPaq";
          |SI aAlfa1 ! aAlfa2;
               nClave = nClave + 1;
               |SI nClave [ 52;
                    #dsinspaq#nClave# = aAlfa1 % 1013;
                    nImporta = nImporta + 1;
               |SINO;
                    |MENAV "0000Exceso de claves. Superadas 50 claves.";
               |FINSI;
          |FINSI;
     |SIGUE;
     |XCIERRA nHandle;
|FPRC;

|PROCESO Tipo3;
     aIniOri = #dsinspaq#1;
     |QBF aIniOri;

     aIniOri = aIniOri < aIniOri;
     aIniDes = aIniDes < aIniDes;

     |SI aIniOri ! aIniDes;
          |HAZ CargaOri;
          |SI nImporta ! 0;
               |HAZ CuentaDes;
               |HAZ CargaDes;
               |SI nCuenta = 0;
                    |MENAV "0000No se ha importado ninguna clave.";
                    |MENAV "0000Ya existian en la configuracion del sistema.";
               |SINO;
                    |SI nCuenta = 1;
                         aAlfa1 = "0000Importada 1 clave.";
                    |SINO;
                         aAlfa1 = "0000Importadas " + nCuenta + " claves.";
                    |FINSI;
                    |MENAV aAlfa1;
               |FINSI;
          |SINO;
               |MENAV "0000No se encuentran claves para importar.";
          |FINSI;
     |SINO;
          |MENAV "0000Ha seleccionado el fichero de configuracion del sistema.";
          |MENAV "0000Debera seleccionar el fichero proporcionado por DS.";
     |FINSI;
|FPRC;

|PROGRAMA;
     |CLS;
     |CABEZA "Importar Claves";
     |PINPA #0#dsinspaq;
     |ABRE #dsinspaq;
     |LEE 101#dsinspaq.p;
     |SI FSalida ! 0;    |GRABA 020#dsinspaq.b;   |FINSI;
     |PINDA #0#dsinspaq;
     |ENDATOS #1#dsinspaq;
     |SI FSalida = 0;
          |HAZ Tipo3;
          |GRABA 020#dsinspaq.a;
     |FINSI;
     |LIBERA #dsinspaq;
     |CIERRA #dsinspaq;
|FPRO;
