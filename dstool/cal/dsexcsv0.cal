 |FICHEROS;
     dsexcsv0 #0;
     dsexcsv1 #1,MANTE;
     dsexcsv2 #2,MANTE;
     dsexcsv3 #3,MANTE;
     dsexcsv4 #4; ||Campos seleccionados
     dsexcsv5 #5; ||Codigo
     dsexcsv6 #6; ||Acota Clave
     dsexcsv7 #7,MANTE; ||Acotacion
     refere1@;
     refere2@;
|FIN;

|VARIABLES;
  {50}Relacion = "";
     nRelacion = 0;
     aPath = "";
     aApli = "";
     aEmp = "";
     aAlf1 = "";
     aAlf2 = "";
     aAlfa = "";
     aDat = "";
     aDir = "";
     nHandle = 0;
     nCampos = 0;
     aDef = "";
     aMas = "";
     i = 0;
     j = 0;
     nRela = 0;
     nPos = 0;
     nNume = 0;
     aTitulo = "";
     aSelRela = "";
     aMasActual = "";
     aCampo = "";
 {-1}aMenu   = "";
     aMenu1  = "2400";
     aMenu2  = "1";
     aMenu3  = "";
     aMenu4  = "RPCSB";
     aMenu5  = " Repasar ";
     aMenu6  = " Procesar ";
     aMenu7  = " Cancelar ";
     aMenu8  = " Seleccion ";
     aMenu9  = " Baja ";
     aD4 = "";
     aH4 = "";
     nHay = 0;
     aReg = "";
     aLon = "";
     aRuta = "";
     nCampo = 0;
     nClave = -1;
     nClaves = 0;
     aCampos = "";
|FIN;

|PROCESO MarcaClave;
     |ABRE #4;
     #4#0 = #0#0
     #4#1 = "";
     |LEE 000#4];
     |PARA; |SI FSalida = 0; |Y #4#0 = #0#0; |HACIENDO;
          aAlfa = #4#1; |QBF aAlfa;
          aLon = aAlfa % A0;
          |SI aLon = 3;
               #4#4 = "";
          |FINSI;
          |SI #4#3 = " "; |Y #4#4 = " ";
               |BORRA 020#4a;
          |SINO;
               |GRABA 020#4a;
          |FINSI;
          |LEE 000#4s;
     |SIGUE;

     |SI #0#7 > 0;
          aAlfa = #0#5;
          aPath = #0#2; |QBF aPath;
          aApli = #0#1; |QBF aApli;
          aDef = aPath + "/" + aApli + "/def/" + #0#5; |QBF aDef;
          |CARGA_DEF aDef, refere1@;
          |SI FSalida ! 0;
               aAlfa = "0000Error al cargar " + aDef;
               |MENAV aAlfa;
          |SINO;
               nNume = #0#7 - 1;
               aAlfa = "|K" + (("000" + nNume) % -103);
               aAlfa = #refere1@#aAlfa#;
               aCampos = aAlfa % 103;
               aAlfa = aAlfa % 4;
               |PARA i = 0; |SI i < aCampos; |HACIENDO i = i + 1;
                    aCampo = aAlfa % 103;
                    aAlfa = aAlfa % 4;
                    |DDEFECTO #4;
                    #4#0 = #0#0;
                    #4#1 = aCampo;
                    |LEE 101#4=;
                    |SI FSalida ! 0; |GRABA 020#4b; |FINSI;
                    #4#4 = "C";
                    |GRABA 020#4a;
                    |LIBERA #4;
               |SIGUE;
               |DESCARGA_DEF #refere1@;
          |FINSI;
     |FINSI;

     |CIERRA #4;
|FPRC;

|PROCESO CargaClaves;
     aAlfa = #0#5;
     aPath = #0#2; |QBF aPath;
     aApli = #0#1; |QBF aApli;
     aDef = aPath + "/" + aApli + "/def/" + #0#5; |QBF aDef;
     |CARGA_DEF aDef, refere1@;
     |SI FSalida ! 0;
          aAlfa = "0000Error al cargar " + aDef;
          |MENAV aAlfa;
     |SINO;
|DEBUG;
          aAlfa = "|NK";
          aAlfa = #refere1@#aAlfa#;
          nClaves = aAlfa;
          |PARA i = 0; |SI i < nClaves; |HACIENDO i = i + 1;
               |DDEFECTO #6;
               aAlfa = "|K" + (("000" + i) % -103)
               aAlfa = #refere1@#aAlfa#;
               aCampos = aAlfa % 103;
               aAlfa = aAlfa % 4;
               |PARA j = 0; |SI j < aCampos; |HACIENDO j = j + 1;
                    aCampo = aAlfa % 103;
                    aAlfa = aAlfa % 4;
                    aAlf1 = "|C" + aCampo + "NOMBRE";
                    aAlf2 = #refere1@#aAlf1#;

                    nCampo = j + 2;
                    #6nCampo = aAlf2;
               |SIGUE;
               #6#0 = i + 1;
               #6#1 = aCampos;
               |GRABA 020#6n;
          |SIGUE;
          |DDEFECTO #6;
          #6#0 = 0;
          #6#1 = "Eliminar Clave";
          |GRABA 020#6n;
          |DESCARGA_DEF #refere1@;
     |FINSI;
|FPRC;

|PROCESO Browse; |TIPO 0;
     |SI FSalida ! 13; |ACABA; |FINSI;

     aRuta = #0#6;  |QBF aRuta;
     |SI aRuta = "";
         aRuta = "\*.csv";
     |FINSI;
     aRuta = "F" + aRuta;

     |BROWSE aRuta;

     aRuta = aRuta % 299; |QBF aRuta;
     |SI aRuta = "";
          |ERROR;
     |SINO;
          #0#6 = aRuta;
          |PINTA #0#6;
     |FINSI;
|FPRC;

|PROCESO Marcados;
     aCampo = #4#1; |QBF aCampo;
     aLon = aCampo % A0;
     |SI aReg = "";
          |SI aLon = 3;
               #2#0 = aCampo;
               |LEE 101#2=;
               |SI FSalida = 0;
                    |SI #4#3 ! " "; #2#2 = #4#3; |FINSI;
                    |SI #4#4 ! " "; #2#3 = #4#4; |FINSI;
                    |GRABA 020#2a;
                    |LIBERA #2;
               |FINSI;
          |FINSI;
     |SINO;
          aAlfa = aCampo - aReg;
          |SI aAlfa ! aCampo;
               aLon = aAlfa % A0;
               |SI aLon = 3;
                    #2#0 = aAlfa;
                    |LEE 101#2=;
                    |SI FSalida = 0;
                         |SI #4#3 ! " "; #2#2 = #4#3; |FINSI;
                         |SI #4#4 ! " "; #2#3 = #4#4; |FINSI;
                         |GRABA 020#2a;
                         |LIBERA #2;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO IniMarcado;
     aReg = "";
     IRelacion = Relacion1<-;
     |PARA i = 1; |SI i [ nRelacion; |HACIENDO i = i + 1;
          IRelacion = IRelacion + 1;
          aReg = aReg + Relacion + "|";
     |SIGUE;
|FPRC;

|DEFBUCLE Marcados;
     #4#1;
     ;
     #0#0, aD4;
     #0#0, aH4;
     ;
     IniMarcado, Marcados;
|FIN;

|PROCESO Borra4;
     |BORRA 020#4a;
|FPRC;

|DEFBUCLE Borra4;
     #4#1;
     ;
     #0#0, aD4;
     #0#0, aH4;
     be;
     NULL, Borra4;
|FIN;

|PROCESO PintaRelacion;
     nPos = 1266;
     IRelacion = Relacion1<-;
     |PARA i = 0; |SI i [ nRelacion; |HACIENDO i = i + 1;
          aAlfa = Relacion % 108;
          |PINTA nPos, aAlfa;
          nPos = nPos + 100;
          IRelacion = IRelacion + 1;
     |SIGUE;
     |PARA; |SI nPos < 2214; |HACIENDO;
          |PINTA nPos, "        ";
          nPos = nPos + 100;
     |SIGUE;
|FPRC;

|PROCESO Relaciones;
     |QBF aMasActual;
     aPath = #0#2; |QBF aPath;
     aApli = #0#1; |QBF aApli;
     aDef = aPath + "/" + aApli + "/def/" + aMasActual; |QBF aDef;
     |CARGA_DEF aDef, refere1@;
     |SI FSalida ! 0;
          aAlfa = "0000Error al cargar " + aDef;
          |MENAV aAlfa;
     |SINO;
          aAlfa = "|NR";
          aAlfa = #refere1@#aAlfa#;
          nRela = aAlfa;
          |PARA i = 0; |SI i < nRela; |HACIENDO i = i + 1;
               aAlfa = "|R" + (("000" + i) % -103)
               aAlfa = #refere1@#aAlfa#;
               aAlf1 = aAlfa - "|";
               aAlf1 = FSalida % 101; nNume = aAlf1 - 1;
               nPos = nNume + 100;
               aMas = aAlfa % nPos;
               nNume = nNume + 2;
               aAlf1 = aAlfa % nNume;
               #3#1 = aMas;   || mas
               #3#2 = aAlf1;  || num componentes + campos
               |HAZ TituloMas;
               #3#0 = aTitulo;
               |GRABA 020#3n;
          |SIGUE;
          |DESCARGA_DEF #refere1@;
     |FINSI;
|FPRC;

|PROCESO MarcaCampo; |TIPO 0;
     |SI aSelRela ! ""; |ACABA; |FINSI;

     |SI FSalida = 0;
          |SI #2#2 = " " ;
               #2#2 = "*";
          |SINO;
               #2#2 = "";
          |FINSI;
          |GRABA 020#2a; |PINTA #2#2;

          aAlfa = "";
          IRelacion = Relacion1<-;
          |PARA i = 1; |SI i [ nRelacion; |HACIENDO i = i + 1;
               IRelacion = IRelacion + 1;
               aAlfa = aAlfa + Relacion + "|";
          |SIGUE;
          aCampo = ("000" + #2#0) % -103;
          aAlfa = aAlfa + aCampo;

          |DDEFECTO #4;
          #4#0 = #0#0;
          #4#1 = aAlfa;
          |LEE 000#4=;
          |SI FSalida ! 0; |GRABA 020#4b; |FINSI;
          #4#3 = #2#2;
          |SI #4#3 = " "; |Y #4#4 = " ";
               |BORRA 020#4c;
          |SINO;
               |GRABA 020#4a;
          |FINSI;
          |LIBERA #4;
     |FINSI;
|FPRC;

|PROCESO AcotaCampo; |TIPO 11;
     |SI FSalida = 13;        || F5
          |SI #2#3 = " ";
               #2#3 = "*";
          |SINO;
               |SI #2#3 = "C";
                    |MENAV "0000Campo de acotacion por clave...";
                    |ERROR;
               |SINO;
                    #2#3 = "";
               |FINSI;
          |FINSI;
          |GRABA 020#2a; |PINTA #2#3;

          aAlfa = "";
          IRelacion = Relacion1<-;
          |PARA i = 1; |SI i [ nRelacion; |HACIENDO i = i + 1;
               IRelacion = IRelacion + 1;
               aAlfa = aAlfa + Relacion + "|";
          |SIGUE;
          aCampo = ("000" + #2#0) % -103;
          aAlfa = aAlfa + aCampo;

          |DDEFECTO #4;
          #4#0 = #0#0;
          #4#1 = aAlfa;
          |LEE 000#4=;
          |SI FSalida ! 0; |GRABA 020#4b; |FINSI;
          #4#4 = #2#3;
          |SI #4#3 = " "; |Y #4#4 = " ";
               |BORRA 020#4c;
          |SINO;
               |GRABA 020#4a;
          |FINSI;
          |LIBERA #4;
          ||PTEC 809;
     |FINSI;
     |SI FSalida = 14;   || F6
          |SI nRelacion > 0;
               |MENAV "0000Acotacion por clave solo para el fichero principal...";
               |ERROR;
          |SINO;
               |DELINDEX #6;
               |ABRE #6;
               |HAZ CargaClaves;
               |LEE 000#6p;
               |CONSULTA_CLAVE #1#6;
               |SI FSalida = 0;
                    nClave = #6#0;
                    |PTEC 807;
               |SINO;
                    |ERROR;
               |FINSI;
               |CIERRA #6;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Min2;
     #2#0 = 0;
|FPRC;

|PROCESO Max2;
     #2#0 = 999;
|FPRC;

|PROCESO LeeCampos;
     |QBF aMasActual;
     aPath = #0#2; |QBF aPath;
     aApli = #0#1; |QBF aApli;
     aDef = aPath + "/" + aApli + "/def/" + aMasActual; |QBF aDef;
     |CARGA_DEF aDef, refere1@;
     |SI FSalida ! 0;
          aAlfa = "0000Error al cargar " + aDef;
          |MENAV aAlfa;
     |SINO;
          aAlfa = "|NC";
          aAlfa = #refere1@#aAlfa#;
          nCampos = aAlfa;

          |ABRE #2;
          |PARA i = 0 ;|SI i < nCampos; |HACIENDO i = i + 1;
               |DDEFECTO #2;
               #2#0 = i;
               |LEE 101#2=;
               |SI FSalida ! 0; |GRABA 020#2b; |FINSI;
               aAlfa = "|C" + (("000" + i) % -103) + "NOMBRE";
               aAlfa = #refere1@#aAlfa#;
               #2#1 = aAlfa;
               |GRABA 020#2a;
               |LIBERA #2;
          |SIGUE;
          |DESCARGA_DEF #refere1@;
     |FINSI;
|FPRC;

|PROCESO Relacion; |TIPO 66;
     aSelRela = "";

     |DELINDEX #3;
     |ABRE #3;
     |HAZ Relaciones;
     |LEE 000#3p;
     |SI FSalida ! 0;
          |MENAV "0000No hay relaciones...";
          |ERROR;
     |SINO;
          |CONSULTA_CLAVE #1#3;
          |SI FSalida = 0;
               aSelRela = #3#1 + #3#2; |QBF aSelRela;
               |PTEC 807;
          |SINO;
               |ERROR;
          |FINSI;
     |FINSI;
     |CIERRA #3;
|FPRC;

|PROCESO MeolloCampo;
     |PUSHV 0501 2380;
     |DELINDEX #2;
     |ABRE #2;
     |HAZ LeeCampos;
     |CIERRA #2;

     |ATRI R;
     |PINTA 1235, " Campos                  M A ";
     |ATRI 0;

     |HAZ PintaRelacion;

     |PARA; |SI; |HACIENDO;
          |BUCLE Marcados;
          |ENTLINEAL #1#2, -1, 4, 22, 0, Min2, Max2;
          |SI aSelRela ! "";
               IRelacion = Relacion1<-;
               nRelacion = nRelacion + 1;
               IRelacion = IRelacion + nRelacion;
               Relacion = aSelRela;
               |HAZ PintaRelacion;

               aMasActual = aSelRela % 108;
               aSelRela = "";
          |SINO;
               |SI nClave ! -1;
                    #0#7 = nClave;
                    |HAZ MarcaClave;
                    nClave = -1;
               |SINO;
                    nRelacion = nRelacion - 1;
                    |SI nRelacion < 0; |SAL; |FINSI;

                    IRelacion = Relacion1<-;
                    IRelacion = IRelacion + nRelacion;
                    aMasActual = Relacion % 108;

                    |HAZ PintaRelacion;
               |FINSI;
          |FINSI;

          |DELINDEX #2;
          |ABRE #2;
          |HAZ LeeCampos;
          |CIERRA #2;
     |SIGUE;
     |POPV;
|FPRC;

|PROCESO HayCampos;
     #4#0 = #0#0;
     #4#1 = "";
     |LEE 000#4];
     |SI FSalida = 0; |Y #4#0 = #0#0;
          FSalida = "0";
     |SINO;
          FSalida = "1";
     |FINSI;
|FPRC;

|PROCESO Campo; |TIPO 0;
     |SI FSalida = 0; |Y #1Campo = Contenido;
          #0#5 = #1#0;
          aMasActual = #1#0;
          nRelacion = 0;
          IRelacion = Relacion1<-;
          Relacion = aMasActual;

          |HAZ MeolloCampo;

          || Si ha grabado campos lo saco fuera
          |HAZ HayCampos;
          |SI FSalida = 0;
               |PTEC 807;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO TituloMas;
     |QBF aMas;
     aPath = #0#2; |QBF aPath;
     aApli = #0#1; |QBF aApli;
     aDef = aPath + "/" + aApli + "/def/" + aMas; |QBF aDef;
     |CARGA_DEF aDef, refere2@;
     |SI FSalida = 0;
          aAlfa = "|TITULO";
          aTitulo = #refere2@#aAlfa#;
          |DESCARGA_DEF #refere2@;
     |FINSI;
|FPRC;

|PROCESO LeeDir;
     |MENSA "0000Cargando....";
     aDir = aPath + "/" + aApli + "/fich/" + aApli + ".dir";
     |XABRE "U", aDir, nHandle;
     |SI FSalida < 0;
          aAlfa = "0000[" + aDir + "] inaccesible o inexistente.";
          |MENAV aAlfa;
     |SINO;
          |DDEFECTO #1;
          |PARA; |SI FSalida ] 0 ;|HACIENDO;
               |XLEE nHandle, 255, aAlf1; |SI FSalida < 0; |SAL; |FINSI;
               |XLEE nHandle, 255, aAlf2; |SI FSalida < 0; |SAL; |FINSI;
               |XLEE nHandle, 255, aAlfa; |SI FSalida < 0; |SAL; |FINSI;
               |XLEE nHandle, 255, aAlfa; |SI FSalida < 0; |SAL; |FINSI;
               |XLEE nHandle, 255, aAlfa; |SI FSalida < 0; |SAL; |FINSI;
               aMas = aAlf1;
               |HAZ TituloMas;
               #1#0 = aMas;
               #1#1 = aTitulo;
               |GRABA 000#1n;
          |SIGUE;
     |FINSI;
|FPRC;

|PROCESO empresa0; |TIPO 0;
     |SI FSalida = 2; |ACABA; |FINSI;

     |SI aEmp = "";
         aDat = aPath + aApli + "/fich/";
     |SINO;
          aDat = aPath + aApli + "/fich/" + aEmp + "/";
     |FINSI;

     |MKDIR aDat;
     |SI FSalida = 0;
          |RMDIR aDat;
          |MENAV "0000La empresa no existe...";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO DefPath7; |TIPO 7;
     aAlfa = #0#2; |QBF aAlfa;
     |SI aAlfa = "";
          |DBASS aAlfa;
          #0#2 = aAlfa;
     |FINSI;
|FPRC;

|PROCESO PonBarra; |TIPO 0;
     aAlfa = #0#2; |QBF aAlfa;
     |SI aAlfa ! "";
          aAlf1 = aAlfa % -101;
          |SI aAlf1 ! "/";|Y aAlf1 ! "\";
               aAlfa = aAlfa + "/";
               #0#2 = aAlfa;
               |PINTA #0#2;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Min4;
     #4#0 = #0#0;
     #4#1 = aD4;
|FPRC;

|PROCESO Max4;
     #4#0 = #0#0;
     #4#1 = aH4;
|FPRC;

|PROCESO Min1;
     #1#0 = "        ";
|FPRC;

|PROCESO Max1;
     #1#0 = "zzzzzzzz";
|FPRC;

|PROCESO Graba5;
     |ABRE #5;
     #5#0 = #0#0;
     |LEE 101#5=;
     |SI FSalida ! 0; |GRABA 020#5b; |FINSI;
     #5#1 = #0#1;
     #5#2 = #0#2;
     #5#3 = #0#3;
     #5#4 = #0#4;
     #5#5 = #0#5;
     #5#6 = #0#6;
     #5#7 = #0#7;
     |GRABA 020#5a;
     |CIERRA #5;
|FPRC;

|PROCESO Graba1;
     aMas = #0#5;
     |HAZ TituloMas;
     #1#0 = aMas;
     #1#1 = aTitulo;
     |GRABA 020#1n;
|FPRC;

|PROCESO Codigo; |TIPO 0;
     |ABRE #5;
     #5#0 = #0#0;
     |LEE 000#5=;
     |SI FSalida = 0;
          #0#1 = #5#1; |PINTA #0#1;
          #0#2 = #5#2; |PINTA #0#2;
          #0#3 = #5#3; |PINTA #0#3;
          #0#4 = #5#4; |PINTA #0#4;
          #0#5 = #5#5; |PINTA #0#5;
          #0#6 = #5#6; |PINTA #0#6;
          #0#7 = #5#7;
     |FINSI;
     |CIERRA #5;
|FPRC;

|PROGRAMA;
     aAlfa = "1" + Usuario; |NOME_DAT #1 aAlfa; |DELINDEX #1;
     aAlfa = "2" + Usuario; |NOME_DAT #2 aAlfa; |DELINDEX #2;
     aAlfa = "3" + Usuario; |NOME_DAT #3 aAlfa; |DELINDEX #3;
     aAlfa = "4" + Usuario; |NOME_DAT #6 aAlfa; |DELINDEX #6;
     aAlfa = "5" + Usuario; |NOME_DAT #7 aAlfa; |DELINDEX #7;

     aD4 = " " * 200;
     aH4 = "z" * 200;

     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENTLINEAL #1#1, -1, 7, 22, 0, Min1, Max1;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          aPath = #0#2; |QBF aPath;
          aApli = #0#1; |QBF aApli;
          aEmp = #0#3; |QBF aEmp;
          |SI aEmp = "";
              aDat = aPath + aApli + "/fich/";
          |SINO;
               aDat = aPath + aApli + "/fich/" + aEmp + "/";
          |FINSI;

          |ABRE #5;
          #5#0 = #0#0;
          |LEE 000#5=;
          |SI FSalida ! 0;
               |ABRE #1;
               |ABRE #4;
               |HAZ LeeDir;
               |ENTLINEAL #1#1, -1, 4, 22, 0, Min1, Max1;
               |HAZ HayCampos; nHay = FSalida
               |CIERRA #4;
               |CIERRA #1;
          |SINO;
               |ABRE #1;
               |HAZ Graba1;
               |ENTLINEAL #1#1, -1, 7, 22, 0, Min1, Max1;
               |CIERRA #1;
               nHay = 0;
          |FINSI;
          |CIERRA #5;

          |SI nHay = 0;
               |HAZ Graba5;
               |HAZ ProcesarTitulos;
               |PARA; |SI; |HACIENDO;
                    |MENU aMenu;
                    aMenu2 = FSalida;

                    |SI aMenu2 = 1;     || Repasar
                         |ABRE #4;
                         aMasActual = #0#5;
                         nRelacion = 0;
                         IRelacion = Relacion1<-;
                         Relacion = aMasActual;
                         |HAZ MeolloCampo;
                         |CIERRA #4;
                         |HAZ ProcesarTitulos;
                    |FINSI;
                    |SI aMenu2 = 2;     || Procesar
                         |HAZ AcotaCampos;
                         |SI FSalida = 1;
                              |HAZ Procesar; |SAL;
                         |FINSI;
                    |FINSI;
                    |SI aMenu2 = 3; |O aMenu2 = 0;     || Cancelar
                         |SAL;
                    |FINSI;
                    |SI aMenu2 = 4;     || Seleccion
                         |ABRE #4;
                         #4#0 = #0#0;
                         #4#1 = "";
                         |LEE 000#4];
                         |CONSULTA_F_CLAVE #1#4, Min4, Max4;
                         |CIERRA #4;
                    |FINSI;
                    |SI aMenu2 = 5;
                         |BUCLE Borra4;

                         |ABRE #5;
                         #5#0 = #0#0;
                         |BORRA 020#5c;
                         |CIERRA #5;
                         |SAL;
                    |FINSI;
               |SIGUE;
          |FINSI;
     |FINSI;

     |DELINDEX #1;
     |DELINDEX #2;
     |DELINDEX #3;
     |DELINDEX #6;
     |DELINDEX #7;
|FPRO;
