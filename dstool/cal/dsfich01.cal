|FICHEROS;
     dsfich01 #0;
|FIN;

|VARIABLES;
     aNombreFich = "";
     aDirectorio = "";
     aLong = "";
     nLong = 0;
     nConta = 0;
     aCampo = "";
     nCampo = 0;
     aDatos = "";
     aValor = "";

     aBase = "";
     aCajon = "";

     nExiste = 0;
     aMensaje = "";

     aPath1 = "";
     aNombre = "";
     aPath2 = "";
     aFichero = "";
     aOrigen = "";
     aDestino = "";
     aIPRemoto = "";

     aFich = "";
     nSwPara = 0;
     aParametro = "";
|FIN;

|PROGRAMA;
     |DBASE aBase;
     |QBF aBase;
     aCajon = aBase + "dscajonx/";
     |MKDIR aCajon;

     aParametro = PARAMETRO;
     |QBF aParametro;
     |SI aParametro = "enviar";
          nSwPara = 1;
     |SINO;
          nSwPara = 0;
     |FINSI;

     |CLS;
     |ABRE #0;
     |DDEFECTO #0;
     |LEE 101#0p;
     nExiste = FSalida;
     |PINPA #0#0;

     #0#1 = aCajon;

     |SI nSwPara = 1;
          |ATRI R;
          |PINTA 0604, " Envio de fichero desde la máquina local al servidor central ";
          |ATRI r;

          |C_M #dsfich01#2, 1, "N";
          |C_V #dsfich01#2, 1, "N";
          |C_M #dsfich01#3, 1, "N";
          |C_V #dsfich01#3, 1, "N";
          |C_M #dsfich01#4, 1, "N";
          |C_V #dsfich01#4, 1, "N";

          |C_M #dsfich01#6, 1, "S";
          |C_V #dsfich01#6, 1, "S";
          |C_P #dsfich01#6, 1, 1115;

          |PINTA 1706, "Fichero:";
          |PINTA 1806, "        ";

          |C_P #dsfich01#5, 1, 1715;
     |SINO;
          |C_M #dsfich01#6, 1, "N";
          |C_V #dsfich01#6, 1, "N";
          |C_M #dsfich01#5, 1, "N";
          |C_V #dsfich01#5, 1, "N";
     |FINSI;

     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |SI nExiste = 0;
               |GRABA 020#0a;
          |SINO;
               |GRABA 020#0n;
          |FINSI;
          |HAZ Principal;
     |FINSI;
     |LIBERA #0;
     |CIERRA #0;
|FPRO;

|PROCESO Browse; |TIPO 0;
     |SI FSalida = 13;
          aFich = "XD" + "/";
          |BROWSE aFich;
          |QBF aFich;
          |SI aFich ! "";
               #dsfich01#1 = aFich;
               |PINTA #dsfich01#1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO BrowseN; |TIPO 0;
     |SI FSalida = 13;
          aNombre = #dsfich01#1;
          |QBF aNombre;
          |SI aNombre = "";
               aMensaje = "0000Por favor indique antes el Path";
               |MENAV aMensaje;
               |ACABA;
          |FINSI;
          aFich = "XA" + aNombre;
          |BROWSE aFich;
          |QBF aFich;
          |SI aFich ! "";
               aFich = aFich - aNombre;
               #dsfich01#2 = aFich;
               |PINTA #dsfich01#2;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Browse2; |TIPO 0;
     |SI FSalida = 13;        ||F5
          aFich = "DC:\*";
          |BROWSE aFich;
          aFich = (aFich % 270);
          |QBF aFich;
          |SI aFich ! "";
               #dsfich01#3 = aFich;
               |PINTA #dsfich01#3;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Nombre; |TIPO 0;
     |HAZ BrowseN;

     |SI nSwPara = 0;
          aNombre = #dsfich01#2;
          |QBF aNombre;
          |SI aNombre ! "";
               #dsfich01#4 = #dsfich01#2;
               |PINTA #dsfich01#4;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO SacaNombreFich;
     aDatos = aFich;
     aLong = aDatos % A0;
     nLong = aLong;
     |PARA nConta = 1; |SI nConta [ nLong; |HACIENDO nConta = nConta + 1;
          |SI nConta [ 99;
               aCampo = ("00" + nConta) % -102;
               aCampo = "-" + aCampo + "01";
               nCampo = aCampo;
               aValor = aDatos % nCampo;
               |SI aValor ! "/"; |Y aValor ! "\";
                    aNombreFich = aValor + aNombreFich;
               |SINO;
                    |SAL;
               |FINSI;
          |SINO;
               aNombreFich = "";
               |SAL;
          |FINSI;
     |SIGUE;
     aDirectorio = aFich - aNombreFich;
|FPRC;


|PROCESO NombreLocal; |TIPO 0;
     |SI nSwPara = 1; |Y FSalida = 13;
          aFich = "AC:\*";
          |BROWSE aFich;
          |QBF aFich;
          |SI aFich ! "";
               aFich = aFich % 270;
               #dsfich01#6 = aFich;
               |PINTA #dsfich01#6;
          |FINSI;
     |FINSI;

     |SI nSwPara = 1;
          aNombreFich = "";
          aFich = #dsfich01#6;
          |QBF aFich;
          |HAZ SacaNombreFich;
          aNombre = aNombreFich;
          |QBF aNombre;
          |SI aNombre ! "";
               #dsfich01#5 = aNombre;
               |PINTA #dsfich01#5;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Principal;
     aPath1 = #dsfich01#1;
     |QBF aPath1;

     |SI nSwPara = 0;
          aNombre = #dsfich01#2;
     |SINO;
          aNombre = #dsfich01#5;
     |FINSI;
     |QBF aNombre;
     aPath2 = #dsfich01#3;
     |QBF aPath2;
     |SI nSwPara = 0;
          aFichero = #dsfich01#4;
     |SINO;
          aFichero = #dsfich01#6;
     |FINSI;
     |QBF aFichero;

     |SI nSwPara = 0;
          |SI aPath1 = ""; |O aNombre = ""; |O aPath2 = ""; |O aFichero = "";
               aMensaje = "0000Proceso cancelado. Datos incompletos";
               |MENAV aMensaje;
               |ACABA;
          |FINSI;
     |SINO;
          |SI aPath1 = ""; |O aNombre = ""; |O aFichero = "";
               aMensaje = "0000Proceso cancelado. Datos incompletos";
               |MENAV aMensaje;
               |ACABA;
          |FINSI;
     |FINSI;

     aOrigen = aPath1 + "/" + aNombre;
     |SI nSwPara = 0;
          aDestino = aPath2 + "/" + aFichero;
     |SINO;
          aDestino = aFichero;
     |FINSI;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |SI nSwPara = 0;
          |SI aIPRemoto ! "";
               |ENVIA_FICHERO aOrigen, aDestino;
          |SINO;
               |COPIA_FICHERO aOrigen, aDestino;
          |FINSI;
     |SINO;
          |SI aIPRemoto ! "";
               |RECIBE_FICHERO aDestino, aOrigen;
          |SINO;
               |COPIA_FICHERO aDestino, aOrigen;
          |FINSI;
     |FINSI;
|FPRC;
