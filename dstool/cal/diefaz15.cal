|FICHEROS;
     diefaz15 #0;
     referen@ #1;
     diefaw19 #19;
|FIN;

|VARIABLES;
     &enSwImp = 0;
     &eaApli = "";
     nPrime = 0;
     nInfor = -1;
     nImpre = -1;

     aNombre = "";
     aCar = "";
     nOk = 0;
     i = 0;
     nLonCam = 0;
     nCamClave = 0;
     nPosCam = 0;
     nNume = 0;
     nPos = 0;
     nCampo = 0;
     aValor = "";
     aCamClave = "";
     aValClave = "";
     aCampo = "";
     aBase = "";
     aPath = "";
     aDes = "";
     aFic = "";
     aAlfa = "";
     aOrg = "";
     aEmpAnt = "";
     aDefAnt = "";
     aEmp = "";
     aDef = "";
     aLon = "";
 {3}aPathApli = "";
     aFicDef = "";
     nSwCarga = 0;
     nConta = 0;
|FIN;

|PROCESO SacaNombre;
     aLon = aFic % A0;
     aNombre = "";
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = -100 * i - 1;
          aCar = aFic % nPos;
          |SI aCar = "/"; |O aCar = "\";
               |SAL;
          |FINSI;
          aNombre = aCar + aNombre;
     |SIGUE;
|FPRC;

|PROCESO Imprime;
     |SI nPrime = 0;
          |IMPRE 0;
          nImpre = FSalida;
          |SI FSalida = 0;
               |INFOR "logrecup";
               nInfor = FSalida;
          |FINSI;
          nPrime = 1;
     |FINSI;
     |SI nInfor = 0; |Y nImpre = 0;
          eaApli = aPathApli1;
          |PRINT;
     |FINSI;
|FPRC;

|PROCESO LeeReg;
     aValClave = #19#7;
     aAlfa = "|K000";
     aCamClave = #1aAlfa;
     aAlfa = aCamClave % 103;
     nCamClave = aAlfa;
     nPos = 4;
     nPosCam = 1;
     |PARA i = 1; |SI i [ nCamClave; |HACIENDO i = i + 1;
          nNume = nPos * 100 + 3;
          aCampo = aCamClave % nNume;
          nCampo = aCampo;

          aAlfa = "|C" + aCampo + "LONGITUD";
          aAlfa = #1aAlfa;
          nLonCam = aAlfa;
          nNume = nPosCam * 100 + nLonCam;
          aValor = aValClave % nNume;

          #1nCampo = aValor;
          nPos = nPos + 3;
          nPosCam = nPosCam + nLonCam + 1;
     |SIGUE;
     |LEE 101#1=;
     |SI FSalida ! 0; |GRABA 000#1b; |FINSI;
|FPRC;

|PROCESO CargaDef;
     aAlfa = aPathApli1 + "def/" +  aFicDef;
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida ! 0; |Y aPathApli2 ! "";
          aPathApli1 = aPathApli2;
          aAlfa = aPathApli1 + "def/" + aFicDef;
          |CARGA_DEF aAlfa, referen@;
          |SI FSalida ! 0; |Y aPathApli3 ! "";
               aPathApli1 = aPathApli3;
               aAlfa = aPathApli1 + "def/" + aFicDef;
               |CARGA_DEF aAlfa, referen@;
          |FINSI;
     |FINSI;
     nSwCarga = FSalida;

     |BLIN 21; |PINTA 2102, aAlfa;
|FPRC;

|PROCESO Log;
     nOk = 0;
     |SI #19#3 = "C"; nOk = 1; |FINSI;
     |SI #19#3 = "R"; |Y #19#4 = "32"; nOk = 1; |FINSI;
     |SI nOk = 0; |ACABA; |FINSI;

     aFicDef = #19#5; |QBF aFicDef;
     aEmp = #19#9; |QBF aEmp;

     aLon = aEmp % A0;
     aPathApli1 = "";
     aPathApli2 = "";
     aPathApli3 = "";
     IaPathApli = aPathApli1<-;
     |SI aLon = #0#2;
          aAlfa = #0#3; |HAZ Path;
          aPathApli = aAlfa;
          IaPathApli = IaPathApli + 1;
     |FINSI;
     |SI aLon = #0#4;
          aAlfa = #0#5; |HAZ Path;
          aPathApli = aAlfa;
          IaPathApli = IaPathApli + 1;
     |FINSI;
     |SI aLon = #0#6;
          aAlfa = #0#7; |HAZ Path;
          aPathApli = aAlfa;
          IaPathApli = IaPathApli + 1;
     |FINSI;

     |SI aEmp ! aEmpAnt; |O aFicDef ! aDefAnt;
          |SI nSwCarga = 0;
               |CIERRA #1;
               |DESCARGA_DEF #1;
          |FINSI;
          aEmpAnt = aEmp;
          aDefAnt = aFicDef;
          |HAZ CargaDef;
          |SI nSwCarga = 0;
               aAlfa = aPathApli1 + "fich/" + aEmp + "/";
               |PATH_DAT #1 aAlfa;
               |ABRE #1;
               |BLIN 22; |PINTA 2202, aAlfa;
          |SINO;
               enSwImp = 2;
               |HAZ Imprime;
          |FINSI;
     |FINSI;
     |SI nSwCarga ! 0; |ACABA; |FINSI;

     |DDEFECTO #1;
     |HAZ LeeReg;
     |SI #19#3 = "C";               || Modifica campo
          aAlfa = "|NC";
          aAlfa = #1aAlfa;
          nCampo = #19#6;
          |SI nCampo ] aAlfa;
               enSwImp = 1;
               |HAZ Imprime;
          |SINO;
               #1nCampo = #19#12;
               |GRABA 000#1a;
          |FINSI;
     |FINSI;
     |SI #19#3 = "R";               || Baja
          |BORRA 000#1a;
     |FINSI;
     |LIBERA #1;
|FPRC;

|DEFBUCLE Log;
     #19#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, NULL, NULL, NULL, NULL, Log;
     #19#9, #19#5, #19#1, #19#2; ||Empresa, Def, Fecha, Hora
|FIN;

|PROCESO Descomprime;
     aDes = aBase + "spool/ficlog.gz";
     |COPIA_FICHERO aFic, aDes;
     |DESCOMPRIME aDes;
     aFic = aDes - ".gz";
     aDes = aBase + "spool/ficlog.dat";
     |RENOMBRA_FICHERO aFic, aDes;
|FPRC;

|PROCESO Copia;
     aDes = aBase + "spool/ficlog";
     |COPIA_FICHERO aFic, aDes;
     aFic = aDes;
     aDes = aBase + "spool/ficlog.dat";
     |RENOMBRA_FICHERO aFic, aDes;
|FPRC;

|PROCESO Path;
     |QBF aAlfa;
     aCar = aAlfa % -101;
     |SI aCar ! "/"; |Y aCar ! "\";
          aAlfa = aAlfa + "/";
     |FINSI;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |DBASS aBase;
     aAlfa = #0#0; |HAZ Path;
     aPath = aAlfa;

     |_PDIR aPath;
     |PARA; |SI FSalida = 0; |HACIENDO;
          aFic = aPath; |QBF aFic;
          |HAZ SacaNombre;
          aAlfa = aFic % -103;
          |SI aAlfa ! "tar";
               |BLIN 20; |PINTA 2002, aFic;
               |SI aAlfa = ".gz";
                    |HAZ Descomprime;
               |SINO;
                    |HAZ Copia;
               |FINSI;

               aAlfa = aBase + "spool/";
               |PATH_DAT #19 aAlfa;
               |NOME_DAT #19 "ficlog";
               aEmpAnt = "";
               nSwCarga = -1;

               |ABRE #19;
               |CONSULTA_CLAVE #1#19;
               |CIERRA #19;

               |SI #0#1 = "S";
                    aAlfa = "0000NProcesar fichero? " + &13 + aNombre;
                    |CONFI aAlfa;
               |SINO;
                    FSalida = 0;
               |FINSI;
               |SI FSalida = 0;
                    |BUCLE Log;
               |FINSI;

               |DELINDEX #19;
               |SI nSwCarga = 0;
                    |CIERRA #1;
                    |DESCARGA_DEF #1;
               |FINSI;
          |FINSI;
          |_SDIR aPath;
          nConta = nConta + 1;
          ||SI nConta ] 3; |ACABA; |FINSI;
     |SIGUE;

     |SI nPrime ! 0;
          |SI nInfor = 0; |PIEINF; |FININF; |FINSI;
          |SI nImpre = 0; |FINIMP; |FINSI;
     |FINSI;
|FPRC;

|PROGRAMA;
     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |GRABA 020#0a;
     |FINSI;
     |CIERRA #0;
|FPRO;
