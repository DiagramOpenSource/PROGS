|FICHEROS;
     dsfich02 #0;
|FIN;

|VARIABLES;
     nExiste = 0;
     aMensaje = "";
     aBase = "";
     aCajon = "";
     aFich = "";

     aDatos = "";
     aLong = "";
     nLong = 0;
     nConta = 0;
     aCampo = "";
     nCampo = 0;
     aValor = "";
     aNombreFich = "";
     aNombre = "";
     aBorra = "";
|FIN;

|PROGRAMA;
     |DBASE aBase;
     |QBF aBase;
     aCajon = aBase + "dscajonx/";
     |MKDIR aCajon;

     |CLS;
     |ABRE #0;
     |DDEFECTO #0;
     |LEE 101#0p;
     nExiste = FSalida;
     |PINPA #0#0;

     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |SI nExiste = 0;
               |GRABA 020#0a;
          |SINO;
               |GRABA 020#0n;
          |FINSI;
          |HAZ Principal;
     |FINSI;
     |LIBERA #0;
     |CIERRA #0;
|FPRO;

|PROCESO Browse; |TIPO 0;
     |SI FSalida = 13;
          aFich = "XA" + aCajon;
          |BROWSE aFich;
          |QBF aFich;
          |SI aFich ! "";
               aNombreFich = "";
               |QBF aFich;
               |HAZ SacaNombreFich;
               aNombre = aNombreFich;
               |QBF aNombre;
               |SI aNombre ! "";
                    #dsfich02#1 = aNombre;
                    |PINTA #dsfich02#1;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO SacaNombreFich;
     aDatos = aFich;
     aLong = aDatos % A0;
     nLong = aLong;
     |PARA nConta = 1; |SI nConta [ nLong; |HACIENDO nConta = nConta + 1;
          |SI nConta [ 99;
               aCampo = ("00" + nConta) % -102;
               aCampo = "-" + aCampo + "01";
               nCampo = aCampo;
               aValor = aDatos % nCampo;
               |SI aValor ! "/"; |Y aValor ! "\";
                    aNombreFich = aValor + aNombreFich;
               |SINO;
                    |SAL;
               |FINSI;
          |SINO;
               aNombreFich = "";
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Principal;
     aBorra = #dsfich02#1;
     |QBF aBorra;
     |SI aBorra = ""; |O aBorra = "vacio";
          aMensaje = "0000No ha seleccionado nada para borrar";
          |MENAV aMensaje;
          |ACABA;
     |FINSI;

     aMensaje = "0000N¿Desea borrar del directorio de intercambio el fichero: " + aBorra + "?";
     |CONFI aMensaje;
     |SI FSalida = 0;
          aBorra = aCajon + aBorra;
          |FBORRA aBorra;
     |FINSI;
|FPRC;
