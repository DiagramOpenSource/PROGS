|VARIABLES;
     nVentana         = 0;
     nVentTrab        = -1;
     nHandle          = 0;
     nHandleTxt       = 0;
     nHandle1         = 0;
     nHandle2         = 0;
     nTree            = -1;
     nLong            = 0;
     nPos             = 0;
     nPosi            = 0;
     nError           = 0;
     nIndice          = 0;
     nConta           = 0;
     nHayIdioma       = 0;
     nEntro           = 0;
     nCargar          = 0;
     nImagen          = 0;
     nConfirmar       = 0;
     nIdSalir         = 0;
     nSalir           = 0;

     aEsc1            = "";
     aEsc2            = "";
     aRetu            = "";
     aTecla           = "";
     aAlfa            = "";
     aAlfa1           = "";
     aAlfa2           = "";
     aAlfa3           = "";
     aAlfa4           = "";
     aLee             = "";
     aBase            = "";
     aAbre            = "";
     aAbre1           = "";
     aAbre2           = "";
     aBusca           = "";
     aRefresca        = "";
     aLong            = "";
     aCarac           = "";
     aTexto           = "";
     aContenido       = "";
     aContenido1      = "";
     aIdioma          = "";
     aIdiomaConf      = "";
     aImagenApa       = "";
     aImagenIdi       = "";
     aLookAndFeel     = "";
     aLookAndFeelPath = "";
     aApariencia      = "";
     aAparienciaConf  = "";
     aPathConf        = "";
     aOrigen          = "";
     aDestino         = "";
     aIP              = "";
     aEjec            = "";

     {2}nNivel        = 0;
     {1}aPathDSJ      = "";

     {-1}sTree        = 0;
     sT_nID           = 0;
     sT_nOper         = 0;
     sT_aData         = "";

     &eaUnidadBasico  = "";
     &eaRutaOrigen    = "";
     &eaRutaDestino   = "";
     &eaFicheroTxt    = "";
     &eaFicheroTgz    = "";
     &eaFicheroTar    = "";
     &eaAlfa          = "";
|FIN;

|PROCESO QuitaBotonSalir;
     nIdSalir = 0;
     InNivel   = nNivel1<-;
     nNivel1   = 0;
     |ESPECIFICA "IDBOTONTECLA", nNivel;

     nIdSalir = nNivel2;
     |FIN_CONTROL_WINDOWS nIdSalir;
|FPRC;

|PROCESO Confirmar;
     aAlfa = aEjec % 101;
     |SI aAlfa = 1;
         sT_nOper = 0;
         sT_aData = "1:Apariencia: " + aTexto + "{{" + aEjec + "}}";
         |ESPECIFICA "COMMANDCONTROL", sTree;
         aApariencia = aTexto;   |QBF aApariencia;
     |SINO;
         sT_nOper = 0;
         sT_aData = "2:Idioma: " + (aTexto % 103) + "{{" + aEjec + "}}";
         |ESPECIFICA "COMMANDCONTROL", sTree;
         aIdioma = aTexto % 103;   |QBF aIdioma;
     |FINSI;
|FPRC;

|PROCESO MontaContenido;
     aAlfa       = aContenido;  |QBF aAlfa;
     aContenido1 = "";
     aLong       = aAlfa % 0;
     nLong       = aLong;
     nCargar     = 0;
     |PARA nPosi = 1;  |SI nPosi [ nLong;  |HACIENDO nPosi = nPosi + 1;
           nPos = (nPosi * 100) + 1;
           aCarac = aAlfa % nPos;

           |SI nCargar = 1;
               aContenido1 = aContenido1 + aCarac;
           |FINSI;

           |SI aCarac = ":";  nCargar = 1; |FINSI;
     |SIGUE;
|FPRC;

|PROCESO EventoTree;
     |SI nSalir = 0;
         |HAZ  QuitaBotonSalir;
         nSalir = 1;
     |FINSI;

     |SI nEntro   = 0;  |ACABA;  |FINSI;
     |SI FEntrada ! 0;  |ACABA;  |FINSI;

     aContenido = Contenido;

     |SI FEntrada = 0;
         aAlfa = "";
         aLong = aContenido % 0;
         nLong = aLong;
         |PARA nPosi = 1;  |SI nPosi [ nLong;  |HACIENDO nPosi = nPosi + 1;
               nPos = (nPosi * 100) + 1;
               aCarac = aContenido % nPos;
               |SI aCarac = ":";  |SAL;  |FINSI;
               aAlfa  = aAlfa + aCarac;
         |SIGUE;

         |QBT aAlfa;

         sT_nID   = nTree;
         sT_nOper = 4;
         sT_aData = ":" + aAlfa;
         |ESPECIFICA "COMMANDCONTROL", sTree;
         aTexto = Contenido;

         sT_nID   = nTree;
         sT_nOper = 3;
         sT_aData = ":" + aAlfa;
         |ESPECIFICA "COMMANDCONTROL", sTree;
         aEjec    = Contenido;
     |FINSI;

     |SI nVentTrab ! -1;
         |SI nImagen ! -1;
             |FIN_CONTROL_WINDOWS nImagen;
             |FIN_CONTROL_WINDOWS nConfirmar;
             nImagen    = -1;
             nConfirmar = -1;
         |FINSI;

         |FINVENTANA nVentTrab;
         nVentTrab = -1;
     |FINSI;

     |HAZ MontaContenido;
     |SI aContenido1 = "";  |ACABA;  |FINSI;

     |SI aContenido1 = "Salir";
         |PTEC 806;
         |ACABA;
     |FINSI;

     |VENTANA 0225, 2588, -1, 1, "";
     nVentTrab = FSalida;

     |PTEC 802;  |PAUSA;

     aAlfa = "IMAGEN,"  + aPathConf + (aContenido1 % 320);

     aAlfa1 = aContenido1 % 101;
     |SI aAlfa1 = "1";
         |CONTROL_SIMPLE 0, aAlfa, 0225, 2388, NULL;
     |SINO;
         |CONTROL_SIMPLE 0, aAlfa, 1035, 1665, NULL;
     |FINSI;
     nImagen = FSalida;

     |FOCOTECLADO nTree;
     |PULSATECLA;

     aAlfa = aTexto - "Idioma:";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aAlfa = aTexto - "Apariencias:";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Confirmar", 2578, 2588, Confirmar;
     nConfirmar = FSalida;
|FPRC;

|PROCESO CargaArbol;
     |QBF aAlfa;
     aAlfa = aAlfa + &13 + &10;
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO SacaLaApariencia;
     aAlfa1  = "";
     aAlfa2  = "";
     aAlfa3  = "";
     aAlfa4  = "";
     nIndice = 1;
     aLong   = aLee % 0;
     nLong   = aLong;
     |PARA nPosi = 1;  |SI nPosi [ nLong;  |HACIENDO nPosi = nPosi + 1;
           nPos   = (nPosi * 100) + 1;
           aCarac = aLee % nPos;
           |SI aCarac = ";";
               nIndice = nIndice + 1;
           |SINO;
               |SI nIndice = 1;  aAlfa1 = aAlfa1 + aCarac;  |FINSI;
               |SI nIndice = 2;  aAlfa2 = aAlfa2 + aCarac;  |FINSI;
               |SI nIndice = 3;  aAlfa3 = aAlfa3 + aCarac;  |FINSI;
               |SI nIndice = 4;  aAlfa4 = aAlfa4 + aCarac;  |FINSI;
           |FINSI;
     |SIGUE;

     nConta = nConta + 1;
     aAlfa  = "1," + nConta + ":" + aAlfa1 + " {{1 " + aAlfa4 + "}}";
     |OEM_ANSI aAlfa, aAlfa;

     |HAZ CargaArbol;

     |SI aAlfa1 = aAparienciaConf;
         aImagenApa = aAlfa4;
     |FINSI;
|FPRC;

|PROCESO RamasApariencias;
     nConta = 0;
     aAbre  = aPathConf + "lookandfeel.txt";

     |XABRE "C", aAbre, nHandleTxt;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     |PARA;  |SI;  |HACIENDO;
          |XLEE nHandleTxt, 255, aLee;
          |SI FSalida < 0;  |SAL;  |FINSI;

          |HAZ SacaLaApariencia;
     |SIGUE;
     |XCIERRA nHandleTxt;
|FPRC;

|PROCESO SacaElIdioma;
     aAlfa1  = "";
     aAlfa2  = "";
     aAlfa3  = "";
     nIndice = 1;
     aLong   = aLee % 0;
     nLong   = aLong;
     |PARA nPosi = 1;  |SI nPosi [ nLong;  |HACIENDO nPosi = nPosi + 1;
           nPos   = (nPosi * 100) + 1;
           aCarac = aLee % nPos;
           |SI aCarac = ";";
               nIndice = nIndice + 1;
           |SINO;
               |SI nIndice = 1;  aAlfa1 = aAlfa1 + aCarac;  |FINSI;
               |SI nIndice = 2;  aAlfa2 = aAlfa2 + aCarac;  |FINSI;
               |SI nIndice = 3;  aAlfa3 = aAlfa3 + aCarac;  |FINSI;
           |FINSI;
     |SIGUE;

     nConta = nConta + 1;
     aAlfa  = "2," + nConta + ":" + aAlfa1 + " - " + aAlfa2 + " {{2 "  + aAlfa3 + "}}";
     |OEM_ANSI aAlfa, aAlfa;

     |HAZ CargaArbol;

     |SI aAlfa1 = aIdiomaConf;
         aImagenIdi = aAlfa3;
     |FINSI;
|FPRC;

|PROCESO RamasIdiomas;
     nConta = 0;
     aAbre  = aPathConf + "idioma.txt";

     |XABRE "C", aAbre, nHandleTxt;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     |PARA;  |SI;  |HACIENDO;
          |XLEE nHandleTxt, 255, aLee;
          |SI FSalida < 0;  |SAL;  |FINSI;

          |HAZ SacaElIdioma;
     |SIGUE;
     |XCIERRA nHandleTxt;
|FPRC;

|PROCESO MontaArbol;
     |DBASE aBase;  |QBF aBase;
     aAlfa = aBase + "tmp";          |MKDIR aAlfa;

     eaRutaOrigen  = aBase + "tmp/";
     eaRutaDestino = eaUnidadBasico + "\documentos";           |RMKDIR eaRutaDestino;
     eaRutaDestino = eaUnidadBasico + "\documentos\tmp";       |RMKDIR eaRutaDestino;
     eaRutaDestino = eaUnidadBasico + "\documentos\tmp\";
     eaFicheroTxt  = "javaz00101" + Usuario + ".txt";
     eaFicheroTgz  = "javaz00101" + Usuario + ".tgz";
     eaFicheroTar  = "javaz00101" + Usuario + ".tar";

     aAbre = eaRutaOrigen + eaFicheroTxt;

     sT_nID   = nTree;
     sT_nOper = 0;

     |XABRE "WB", aAbre, nHandle;

     aAlfa = "1: Apariencia: " + aAparienciaConf + " {{1 " + aImagenApa + "}}";
     |HAZ CargaArbol;

     |HAZ RamasApariencias;

     aAlfa = "2: Idioma: " + aIdiomaConf + " {{2 " + aImagenIdi + "}}";
     |HAZ CargaArbol;

     |HAZ RamasIdiomas;

     aAlfa = "3: Salir {{Salir}}";
     |HAZ CargaArbol;

     |XCIERRA nHandle;

     |HAZ TraeTxtComprimido;
     aAlfa = eaRutaDestino + eaFicheroTxt;

     eaAlfa = "TREECTRL,Opciones {{" + aAlfa + "}}";
     |CONTROL_SIMPLE 0, eaAlfa, 0602, 3330, EventoTree;
     nTree = FSalida;

     eaFicheroTxt  = "javaz00101" + Usuario + ".txt";
     aAlfa         = eaRutaDestino + eaFicheroTxt;
     |RFBORRA aAlfa;

     sT_nID   = nTree;
     sT_nOper = 0;

     sT_aData = "1:Apariencias: " + aAparienciaConf + " {{1 " + aImagenApa + "}}";
     |ESPECIFICA "COMMANDCONTROL", sTree;

     sT_aData = "2:Idioma: " + aIdiomaConf + " {{2 " + aImagenIdi + "}}";
     |ESPECIFICA "COMMANDCONTROL", sTree;

     sT_nOper = 2;
     sT_aData = "1:";
     |ESPECIFICA "COMMANDCONTROL", sTree;

     sT_nID   = nTree;
     sT_nOper = 2;
     sT_aData = "0:";
     |ESPECIFICA "COMMANDCONTROL", sTree;

     |PULSATECLA;
|FPRC;

|PROCESO LeeConfiguracion;
     aAbre = aPathDSJ + "perfiles.cfg";
     |XABRE "EC", aAbre, nHandle;
     |SI FSalida < 0;
         aAlfa = "0000Instalaci¢n incompleta. Consulte con su Administrador: ";
         aAlfa = aAlfa + "falta el archivo perfiles.cfg";
         |MENAV aAlfa;

         nError = 1;

         |ACABA;
     |FINSI;

     |XABRE "C", aAbre, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     |PARA;  |SI;  |HACIENDO;
          |XLEE nHandle, 255, aAlfa;
          |SI FSalida < 0;  |SAL;  |FINSI;

          aAlfa1 = aAlfa - ":LOOKANDFEEL ";
          |SI FSalida ! 0;
              aLookAndFeel = aAlfa1;      |QBF aLookAndFeel;
          |FINSI;

          aAlfa1 = aAlfa - ":LOOKANDFEELPATH ";
          |SI FSalida ! 0;
              aLookAndFeelPath = aAlfa1;  |QBF aLookAndFeelPath;
          |FINSI;

          aAlfa1 = aAlfa - ":IDIOMA ";
          |SI FSalida ! 0;
              aIdiomaConf = aAlfa1;   |QBF aIdiomaConf;
              nHayIdioma  = 1;
          |FINSI;

          |SI aLookAndFeel ! "";  |Y aLookAndFeelPath ! "";  |Y aIdiomaConf ! "";
              |SAL;
          |FINSI;
     |SIGUE;
     |XCIERRA nHandle;

     |SI aLookAndFeel = "";  |O aLookAndFeelPath = "";
         aAlfa = "0000Instalaci¢n incompleta. Consulte con su Administrador: ";
         aAlfa = aAlfa + "perfiles.cfg incompleto";
         |MENAV aAlfa;
         nError = 1;

         |ACABA;
     |FINSI;

     aTexto = ";" + aLookAndFeel + ";" +  aLookAndFeelPath;

     aAbre = aPathConf + "lookandfeel.txt";

     |XABRE "C", aAbre, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     |PARA;  |SI;  |HACIENDO;
          |XLEE nHandle, 255, aAlfa;
          |SI FSalida < 0;  |SAL;  |FINSI;

          aAlfa1 = aAlfa - aTexto;
          |SI FSalida ! 0;
              aLong           = aAlfa % 0;
              nLong           = aLong;
              aAparienciaConf = "";
              |PARA nPosi = 1;  |SI nPosi [ nLong;  |HACIENDO nPosi = nPosi + 1;
                    nPos   = (nPosi * 100) + 1;
                    aCarac = aAlfa % nPos;
                    |SI aCarac = ";";  |SAL;  |FINSI;

                    aAparienciaConf = aAparienciaConf + aCarac;
              |SIGUE;

              |SAL;
          |FINSI;
     |SIGUE;
     |XCIERRA nHandle;

     |SI aIdiomaConf = "";  aIdiomaConf = "SPA";  |FINSI;
|FPRC;

|PROCESO SacaElLook;
     aTexto = "";
     aAbre  = aPathConf + "lookandfeel.txt";

     |XABRE "C", aAbre, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     |PARA;  |SI;  |HACIENDO;
          |XLEE nHandle, 255, aAlfa;
          |SI FSalida < 0;  |SAL;  |FINSI;

          aAlfa1 = aAlfa - aApariencia;
          |SI FSalida ! 0;
              aTexto = aAlfa;   |QBF aTexto;
              |SAL;
          |FINSI;
     |SIGUE;
     |XCIERRA nHandle;

     |SI aTexto = "";  |ACABA;  |FINSI;

     aLookAndFeel     = "";
     aLookAndFeelPath = "";
     nIndice          = 0;
     aLong            = aTexto % 0;
     nLong            = aLong;
     |PARA nPosi = 1;  |SI nPosi [ nLong;  |HACIENDO nPosi = nPosi + 1;
           nPos   = (nPosi * 100) + 1;
           aCarac = aTexto % nPos;
           |SI aCarac = ";";
               nIndice = nIndice + 1;
           |SINO;
               |SI nIndice = 1;  aLookAndFeel     = aLookAndFeel     + aCarac;  |FINSI;
               |SI nIndice = 2;  aLookAndFeelPath = aLookAndFeelPath + aCarac;  |FINSI;
           |FINSI;
     |SIGUE;
     |XCIERRA nHandle;
|FPRC;

|PROCESO GrabaConfiguracion;
     |SI aApariencia = aAparienciaConf;  |Y aIdioma = aIdiomaConf;
         |ACABA;
     |FINSI;

     aOrigen  = aPathDSJ + "perfiles.cfg";
     aDestino = aPathDSJ + "perfiles.cfg.old";
     |RCOPIA_FICHERO aOrigen, aDestino;

     |SI aApariencia ! aAparienciaConf;
         |HAZ SacaElLook;
     |FINSI;

     aAbre1 = aPathDSJ + "perfiles.cfg.old";
     aAbre2 = aPathDSJ + "perfiles.cfg";

     |XABRE "C", aAbre1, nHandle1;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     |XABRE "WCB", aAbre2, nHandle2;

     |PARA;  |SI;  |HACIENDO;
          |XLEE nHandle, 255, aAlfa;
          |SI FSalida < 0;  |SAL;  |FINSI;

          |SI aApariencia ! aAparienciaConf;
              aAlfa1 = aAlfa - ":LOOKANDFEEL ";
              |SI FSalida ! 0;
                  aAlfa = ":LOOKANDFEEL " + aLookAndFeel;
              |FINSI;

              aAlfa1 = aAlfa - ":LOOKANDFEELPATH ";
              |SI FSalida ! 0;
                  aAlfa = ":LOOKANDFEELPATH " + aLookAndFeelPath;
              |FINSI;
          |FINSI;

          |SI aIdioma ! aIdiomaConf;
              aAlfa1 = aAlfa - ":IDIOMA ";
              |SI FSalida ! 0;
                  aAlfa = ":IDIOMA " + aIdioma;
              |FINSI;
          |FINSI;

          aAlfa = aAlfa + &13 + &10;
          |XGRABA nHandle2, aAlfa;

          |SI nHayIdioma = 0;
              aAlfa1 = aAlfa - ":LOOKANDFEELPATH ";
              |SI FSalida ! 0;
                  aAlfa = &13 + &10;
                  |XGRABA nHandle2, aAlfa;

                  aAlfa = ":IDIOMA " + aIdioma + &13 + &10;
                  |XGRABA nHandle2, aAlfa;

                  nHayIdioma = 1;
              |FINSI;
          |FINSI;
     |SIGUE;

     |XCIERRA nHandle1;
     |XCIERRA nHandle2;
|FPRC;

|PROGRAMA;
     |ESPECIFICA "RBASS", aPathDSJ;
     aPathConf = aPathDSJ + "configuracion\";

     aApariencia = "";
     aIdiomaConf = "";
     aIdioma     = "";
     nHayIdioma  = 0;
     nSalir      = -1;

     |HAZ LeeConfiguracion;
     |SI nError = 1;  |ACABA;  |FINSI;

     |CLS;
     |CABEZA "Configuraci¢n jdiagramx";

     |VENTANA 0501, 3599, -1, 16, "Configuraci¢n jdiagramx";
     nVentana = FSalida;

     |PTEC 802;  |PAUSA;

     |HAZ LeeUnidadBasico;

     |HAZ MontaArbol;

     nEntro = 1;
     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         nSalir  = 0;
         FSalida = "::{{001}}Salir," + nTree;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nTree;

     |SI aApariencia = "";  aApariencia = aAparienciaConf;  |FINSI;
     |SI aIdioma     = "";  aIdioma     = aIdiomaConf;      |FINSI;

     |FINVENTANA nVentana;

     |VENTANA 0501, 0520, -1, 16, "Guardando configuraci¢n";
     nVentana = FSalida;

     |PTEC 802;  |PAUSA;

     |HAZ GrabaConfiguracion;

     |FINVENTANA nVentana;
|FPRO;
