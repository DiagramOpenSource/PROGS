|FICHEROS;
     mnprpm01;
     mnprpw01,MANTE;
|FIN;

|VARIABLES;
     n0Vent            = 0;
     n1Vent            = 0;
     nGrid             = 0;
     nRango            = 0;
     nBtnAlt           = 0;
     nBtnMod           = 0;
     nBtnBaj           = 0;
     nBtnIns           = 0;
     nLin              = 0;
     nPan              = 0;

     aEsc1             = "";
     aEsc2             = "";
     aRetu             = "";
     aTecla            = "";
     aRut              = "";
     aAlfa             = "";
     aOri              = "";
     aDes              = "";
     aBase             = "";
     aPathMen          = "";
     aIP               = "";

 {1}aLoc               = "";

 {-1}aVent             = "";
     aVent1            = 0;
     aVent2            = 0;
     aVent3            = 0;
     aVent4            = 0;
     aVent5            = "";

     &eaNomFic         = "";
|FIN;

|PROCESO BtnIns;
     |CONFI "0000NSeguro que quiere instalar el menu seleccionado";
     |SI FSalida ! 0;
         |FOCOTECLADO nGrid;
         |ACABA;
     |FINSI;

     |DBASE aBase;
     aOri = aBase + "plugins/dsmenupropio.exe";

     aIP = ""; |IP_REMOTO aIP; |QBF aIP;
     |SI aIP = "";
         |DBASS aBase;
         aLoc = aBase + "bin/";
     |SINO;
         |ESPECIFICA "RBASS", aLoc;
         aLoc = aLoc + "bin/";
     |FINSI;

     aDes  = aLoc + "dsmenupropio.exe";
     |ENVIA_FICHERO aOri, aDes;

     |XABRE "EC", aDes, 0;
     |SI FSalida < 0;
         |MENAV "0000Problemas con el instalador. Pongase en contacto con su administrador";

         |FOCOTECLADO nGrid;
         |ACABA;
     |FINSI;

     |DBASE aBase;
     aOri = aBase + "menupropio/" + (("0000" + #mnprpm01#0) % -104) + ".xmp";
     aDes = aLoc + (("0000" + #mnprpm01#0) % -104) + ".xmp";
     |ENVIA_FICHERO aOri, aDes;

     |XABRE "EC", aDes, 0;
     |SI FSalida < 0;
         |MENAV "0000Problemas con el fichero XMP. Pongase en contacto con su administrador";

         |FOCOTECLADO nGrid;
         |ACABA;
     |FINSI;

     aAlfa = aLoc + "dsmenupropio.exe" + " " + aLoc + (("0000" + #mnprpm01#0) % -104) + ".xmp";
     |RSYSTEM aAlfa;

     aAlfa = aLoc + (("0000" + #mnprpm01#0) % -104) + ".xmp";
     |RFBORRA aAlfa;

     |MENAV "0000Men£ instalado. Abre nueva sesi¢n de trabajo para ver los cambios";
     |PTEC 807;
|FPRC;

|PROCESO Adjuntar;
     |HAZ DsBrowse;
     aRut = eaNomFic < "";  |QBF aRut;

     aAlfa = aRut - ".xmp";
     |SI aAlfa = aRut;
         |MENAV "0000Se adjuntar  un fichero .xmp";
         aRut = "";
         |ACABA;
     |FINSI;

     aRut = eaNomFic;  |QBF aRut;
|FPRC;

|PROCESO BtnAlt;
     aRut = "";
     |SELECT #1#mnprpm01;
     |PARA nLin = 1;  |SI nLin [ 9999;  |HACIENDO nLin = nLin + 1;
           #mnprpm01#0 = nLin;
           |LEE 000#mnprpm01.=;
           |SI FSalida ! 0;
               |SAL;
           |FINSI;
     |SIGUE;

     |DDEFECTO #mnprpm01;
     #mnprpm01#0 = nLin;
     #mnprpm01#1 = "";

     |GRABA 020#mnprpm01.b;

     |VENTANA 0501, 1783, -1, 16, "Nuevo men£";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |DDEFECTO #mnprpw01;
     |PINPA #0#mnprpw01;  nPan = FSalida;

     |CONTROL_SIMPLE nPan, "BOTON,{{197}}Adjuntar ", 1342, 1555, Adjuntar;

     #mnprpw01#0 = "";

     |PINDA #0#mnprpw01;
     |ENDATOS #1#mnprpw01;
     |SI FSalida = 0;
         #mnprpm01#1 = #mnprpw01#0;
     |SINO;
         aRut  = "";
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;

     |SELECT #2#mnprpm01;

     |SI aRut = "";
         |MENAV "0000Proceso cancelado. No se ha adjuntado men£.";

         |BORRA 020#mnprpm01.a;
         |LIBERA #mnprpm01;
         |ACABA;
     |FINSI;

     |DBASE aBase;
     aPathMen = aBase + "menupropio";  |MKDIR aPathMen;
     aPathMen = aBase + "menupropio/";

     aOri = aRut;
     aDes = aPathMen + (("0000" + #mnprpm01#0) % -104) + ".xmp";
     |RECIBE_FICHERO aOri, aDes;

     |GRABA 020#mnprpm01.a;
     |LIBERA #mnprpm01;

     |REFRESCACONTROL nGrid;  |PULSATECLA;
     |FOCOTECLADO nGrid;

     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
|FPRC;

|PROCESO BtnMod;
     |SELECT #1#mnprpm01;
     |LEE 101#mnprpm01.=;
     |SI FSalida ! 0;
         |SELECT #2#mnprpm01;
         |ACABA;
     |FINSI;

     |VENTANA 0501, 1783, -1, 16, "Modificar descripci¢n";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |DDEFECTO #mnprpw01;
     #mnprpw01#0 = #mnprpm01#1;
     |PINPA #0#mnprpw01;  nPan = FSalida;

     |CONTROL_SIMPLE nPan, "LABEL,{{15}} ", 0902, 1274, NULL;

     |PINDA #0#mnprpw01;
     |ENDATOS #1#mnprpw01;
     |SI FSalida = 0;
         #mnprpm01#1 = #mnprpw01#0;
         |GRABA 020#mnprpm01.a;
         |LIBERA #mnprpm01;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;

     |SELECT #2#mnprpm01;

     |REFRESCACONTROL nGrid;  |PULSATECLA;
     |FOCOTECLADO nGrid;

     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
|FPRC;

|PROCESO BtnBaj;
     |CONFI "0000NSeguro que quiere borrar el menu seleccionado";
     |SI FSalida ! 0;
         |FOCOTECLADO nGrid;
         |ACABA;
     |FINSI;

     |SELECT #1#mnprpm01;
     |LEE 101#mnprpm01.=;
     |SI FSalida ! 0;
         |SELECT #2#mnprpm01;
         |ACABA;
     |FINSI;

     |BORRA 020#mnprpm01.a;
     |LIBERA #mnprpm01;

     |DBASE aBase;
     aPathMen = aBase + "menupropio/";

     aAlfa = aPathMen + (("0000" + #mnprpm01#0) % -104) + ".xmp";
     |FBORRA aAlfa;

     |ESTADO_CONTROL nBtnIns, "DISABLE";
     |ESTADO_CONTROL nBtnMod, "DISABLE";
     |ESTADO_CONTROL nBtnBaj, "DISABLE";

     |REFRESCACONTROL nGrid;  |PULSATECLA;
     |FOCOTECLADO nGrid;

     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
|FPRC;

|PROCESO Evnt;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#mnprpm01.=;

         |ESTADO_CONTROL nBtnIns, "ENABLE";
         |ESTADO_CONTROL nBtnMod, "ENABLE";
         |ESTADO_CONTROL nBtnBaj, "ENABLE";
     |FINSI;

     |SI FSalida = 4;
         |HAZ BtnMod;
     |FINSI;
|FPRC;

|PROGRAMA;
     |CABEZA "Conf.Menus Propio";

     |CONTROL_SIMPLE 0, "LABEL,{{10}}Configuraci¢n Menus Propio", 0502, 0550, NULL;

     |CONTROL_SIMPLE 0, "BOTON,{{193}}Alta", 3464, 3474, BtnAlt;
     nBtnAlt = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Modificar", 3476, 3486, BtnMod;
     nBtnMod = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{199}}Borrar", 3488, 3498, BtnBaj;
     nBtnBaj  = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Instalar en este PC", 3402, 3420, BtnIns;
     nBtnIns  = FSalida;

     |ESTADO_CONTROL nBtnIns, "DISABLE";
     |ESTADO_CONTROL nBtnMod, "DISABLE";
     |ESTADO_CONTROL nBtnBaj, "DISABLE";

     nRango = 4 + 8 + 16 + 32 + 128;

     |LEE 000#mnprpm01.p;

     |LINEAL_SIMPLE #1#mnprpm01, nRango, 0602, 3398, NULL, NULL, Evnt;
     nGrid = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}Salir," + nGrid;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |PULSATECLA;
|FPRO;

|PROCESO T80;  |TIPO 80;
     |ABRET #mnprpm01;

     FSalida = 3699;
|FPRC;

|PROCESO T90;  |TIPO 90;
     |CIERRAT #mnprpm01;
|FPRC;
