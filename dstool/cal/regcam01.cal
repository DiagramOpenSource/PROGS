|FICHEROS;
     regcam01;
     regcam02;
     regcam03;

     agicontm;
     dsw00010;

     referen@;
|FIN;

|VARIABLES;
     aEsTado3 = "";
     aSistema = "";
     nHay = 0;
     aApli = "";
     aLon = "";
     i = 0;
     nPos = 0;
     aFic  = "";

     aDirectorio = "";
     nHandle      = 0;
     nContador    = 0;
     nLogger      = 0;
     nLoggerSize  = 0;
     nDiasLogger  = 0;
     nListaLogger = 0;
     nBufUsr      = 0;
     nRegis       = 0;
     nRango       = 0;
     nEleUsr      = 0;
     nGrid2       = 0;
     nGrid3       = 0;
     nBoton2      = 0;
     nBoton3      = 0;
     nPanta       = 0;
     nCampo       = 0;
     nVentana     = 0;
     nID          = 0;
     nPaso        = 0;
     nDiasCiclo   = 0;
     nActualizo   = 0;
     nDias        = 0;

     aBase        = "";
     aAbre        = "";
     aAlfa        = "";
     aLogger      = "";
     aCadena      = "";
     aFichero     = "";
     aFiltro      = "";
     aPath        = "";
     aDir         = "";
     aMas         = "";
     aOrigen      = "";
     aDestino     = "";
     aIPRemoto    = "";
     aID          = "";

     fFecActiva   = @;
     fUltFecha    = @;
     fHoy         = @;

     {-1}aGet     = 0;
         aGet1    = 0;
         aGet2    = 0;

     {-1}aVent    = "";
         aVent1   = 0;
         aVent2   = 0;
         aVent3   = 0;
         aVent4   = 0;
         aVent5   = "";
|FIN;

|PROCESO MarcaApli;
     |SI #regcam03#1 = "*";
          nHay = 1;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE MarcaApli;
     #regcam03#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, MarcaApli;
|FIN;

|PROCESO LeeAplica;
     aPath = aBase + aAlfa + "/def/";
     aDir  = aPath + "*.mas";
     |_PDIR aDir;
     |PARA;  |SI FSalida = 0;  |HACIENDO;
          |CARGA_DEF aDir, referen@;
          |SI FSalida = 0;
               aAlfa = "|NK";
               aAlfa = #referen@#aAlfa#;
               |SI aAlfa > 0;
                    aMas = aDir - aPath;
                    aMas = aMas - ".mas";
                    aMas = aMas - ".MAS";
                    #dsw00010#0 = aMas;
                    |LEE 101#dsw00010.=;
                    |SI FSalida ! 0;
                        |DDEFECTO #dsw00010;
                        #dsw00010#0 = aMas;
                        |GRABA 020#dsw00010.n;
                    |FINSI;
                    |LIBERA #dsw00010;
               |FINSI;
               |DESCARGA_DEF #referen@;
          |FINSI;
          |_SDIR aDir;
     |SIGUE;
|FPRC;

|PROCESO dsw00010;
     aAlfa = #dsw00010#0;  |QBF aAlfa;
     aAlfa = aAlfa + &13;
     |XGRABA nHandle, aAlfa;
|FPRC;

|DEFBUCLE dsw00010;
     #dsw00010#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dsw00010;
|FIN;

|PROCESO xBoton1;
     nPaso = 1;
     aAlfa = "|C003WID";
     aID   = #regcam01#aAlfa#;
     nID   = aID;
     |FOCOTECLADO nID;
|FPRC;

|PROCESO CargaFiltro;
     |REFERENCIA_DEF aGet1, #regcam01;
     |PARA nCampo = 0;  |SI nCampo [ 5;  |HACIENDO nCampo = nCampo + 1;
           aGet2 = nCampo;
           IaGet = aGet1<-;
           |ESPECIFICA "GETDATACONTROL", aGet;
           #regcam01#nCampo# = FSalida;
     |SIGUE;

     |VENTANA 1610, 2080, -1, 16, "Procesando trabajo";
     nVentana = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |CONTROL_SIMPLE 0, "LABEL,{{14}}Procesando trabajo ...", 1720, 1879, NULL;
     |CONTROL_SIMPLE 0, "LABEL,{{15}}Esto puede tardar varios minutos", 1930, 1979, NULL;

     |PTEC 802;  |PAUSA;
     |PULSATECLA;

     aFichero = ("91" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #dsw00010#aFichero#;
     |ABRE #dsw00010;  |CIERRA #dsw00010;  |DELINDEX #dsw00010;

     |ABRE #dsw00010;
     |LEE 000#regcam03.p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          |SI #regcam03#1 = "*";
               aAlfa = #regcam03#0; |QBF aAlfa;
               |HAZ LeeAplica;
          |FINSI;

          |LEE 000#regcam03.s;
     |SIGUE;
     |CIERRA #dsw00010;

     |XABRE "W", aFiltro, nHandle;
     |BUCLE dsw00010;
     |XCIERRA nHandle;

     |DELINDEX #dsw00010;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVentana;

     |PULSATECLA;
|FPRC;

|PROCESO Boton2;
     |XABRE "E", aFiltro, nHandle;
     |SI FSalida < 0;
         |XABRE "W", aFiltro, nHandle;
         aAlfa = "   " + &13;
         |XGRABA nHandle, aAlfa;
         |XCIERRA nHandle;
     |FINSI;

     aAlfa   = "\DOCUMENTOS";           |RMKDIR aAlfa;
     aAlfa   = "\DOCUMENTOS\AGITOOL";   |RMKDIR aAlfa;

     aOrigen  = aFiltro;
     aDestino = aAlfa + "\filtro.txt";

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |SI aIPRemoto ! "";
         |ENVIA_FICHERO aOrigen, aDestino;
     |SINO;
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;

     aAlfa  = "cmd " + &34 + "/c START /WAIT " + aDestino + &34;
     |RSYSTEM aAlfa;

     |CONFI "0000SActualizar modificaciones en la lista de tablas?";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI aIPRemoto ! "";
         |RECIBE_FICHERO aDestino, aOrigen;
     |SINO;
         |COPIA_FICHERO aDestino, aOrigen;
     |FINSI;
|FPRC;

|PROCESO regcam02B;
     |SI #regcam02#1 = " ";  |O #regcam02#1 = "*";
         |BORRA 020#regcam02.a;
         |LIBERA #regcam02;

         |ACABA;
     |FINSI;

     |SI #regcam02#1 = "S";
         #regcam02#1 = "*";
         #regcam02#2 = "0,0,0;0,0,0";
     |FINSI;

     |SI #regcam02#1 = "N";
         #regcam02#1 = "";
         #regcam02#2 = "90,90,90;255,255,255";
     |FINSI;

     |GRABA 020#regcam02.a;
     |LIBERA #regcam02;
|FPRC;

|DEFBUCLE regcam02B;
     #regcam02#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL, regcam02B;
|FIN;

|PROCESO TraeUsuarios;
     aAlfa = aBase + "dev/usr/ds_sys.psw"
     |LEESECUENCIAL aAlfa, nBufUsr, nEleUsr, 0, 0;

     |PARA nRegis = 0; |SI nRegis < nEleUsr; |HACIENDO nRegis = nRegis + 1;
           |DEL_BUF nBufUsr, nRegis, aAlfa;

           #regcam02#0 = aAlfa % 410;
           |LEE 101#regcam02.=;
           |SI FSalida ! 0;
               |DDEFECTO #regcam02;
               #regcam02#0 = aAlfa % 410;
               |GRABA 020#regcam02.b;
           |FINSI;

           |SI #regcam02#1 = "*"; #regcam02#1 = "S";  |FINSI;
           |SI #regcam02#1 = " "; #regcam02#1 = "N";  |FINSI;

           |GRABA 020#regcam02.a;
           |LIBERA #regcam02;
     |SIGUE;

     |BUCLE regcam02B;

     |FINDIM nBufUsr;
|FPRC;

|PROCESO agicontm;
     aAlfa = #agicontm#1;  |QBF aAlfa;
     aAlfa = aAlfa + &13;
     |XGRABA nHandle, aAlfa;
|FPRC;

|DEFBUCLE agicontm;
     #agicontm#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, agicontm;
|FIN;

|PROCESO GrabaTmp;
     nContador = nContador + 1;
     #agicontm#0    = nContador;
     #agicontm#1    = aAlfa;
     |GRABA 020#agicontm.n;
     |LIBERA #agicontm;
|FPRC;

|PROCESO TrataCadena;
     aCadena = aAlfa - "LOGGERSIZE=";
     |SI FSalida ! 0;
         nLoggerSize = 1;

         |SI #regcam01#1 = "N";  |ACABA;  |FINSI;

         aAlfa = "LOGGERSIZE=" + #regcam01#2;
         |HAZ GrabaTmp;
         |ACABA;
     |FINSI;

     aCadena = aAlfa - "DIASLOGGER=";
     |SI FSalida ! 0;
         nDiasLogger = 1;

         |SI #regcam01#1 = "N";  |ACABA;  |FINSI;

         aAlfa = "DIASLOGGER=" + #regcam01#3;
         |HAZ GrabaTmp;
         |ACABA;
     |FINSI;

     aCadena = aAlfa - "LISTALOGGER=";
     |SI FSalida ! 0;
         nListaLogger = 1;

         |SI #regcam01#1 = "N";  |ACABA;  |FINSI;

         |SI #regcam01#4 = "T";  |ACABA;  |FINSI;

         aAlfa = "LISTALOGGER=" + aFiltro;
         |HAZ GrabaTmp;
         |ACABA;
     |FINSI;

     aCadena = aAlfa - "LOGGER=";
     |SI FSalida ! 0;
         nLogger = 1;

         |SI #regcam01#1 = "N";  |ACABA;  |FINSI;

         aAlfa = "LOGGER=" + aLogger;
         |HAZ GrabaTmp;
         |ACABA;
     |FINSI;

     aAlfa = aAlfa - &13;
     |HAZ GrabaTmp;
|FPRC;

|PROCESO regcam02;
     |SI #regcam02#1 = " ";  |ACABA;  |FINSI;

     aAlfa = #regcam02#0;  |QBF aAlfa;
     aAlfa = aAlfa + &13;
     |XGRABA nHandle, aAlfa;
|FPRC;

|DEFBUCLE regcam02;
     #regcam02#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, regcam02;
|FIN;

|PROCESO TrataINI;
     nContador    = 0;
     nLogger      = 0;
     nLoggerSize  = 0;
     nDiasLogger  = 0;
     nListaLogger = 0;

     aFichero = ("90" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #agicontm#aFichero#;
     |ABRE #agicontm;  |CIERRA #agicontm;  |DELINDEX #agicontm;

     |ABRE #agicontm;
     aAbre = aBase + "ds.ini";
     |XABRE "U", aAbre, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     |PARA;  |SI;  |HACIENDO;
         |XLEE nHandle, 250, aAlfa;
         |SI FSalida < 0;  |SAL;  |FINSI;

         |HAZ TrataCadena;
     |SIGUE;

     |SI #regcam01#1 = "S";
         |SI nLogger = 0;
             aAlfa = "LOGGER=" + aLogger;
             |HAZ GrabaTmp;
         |FINSI;

         |SI nLoggerSize  = 0;
             aAlfa = "LOGGERSIZE=" + #regcam01#2;
             |HAZ GrabaTmp;
         |FINSI;

         |SI nDiasLogger  = 0;
             aAlfa = "DIASLOGGER=" + #regcam01#3;
             |HAZ GrabaTmp;
         |FINSI;

         |SI nListaLogger = 0;  |Y #regcam01#4 = "C";
             aAlfa = "LISTALOGGER=" + aFiltro;
             |HAZ GrabaTmp;
         |FINSI;
     |FINSI;
     |CIERRA #agicontm;
     |XCIERRA nHandle;

     aAbre = aBase + "ds.ini";
     |XABRE "W", aAbre, nHandle;

     |BUCLE agicontm;

     |XCIERRA nHandle;

     |DELINDEX #agicontm;

     aAbre = aBase + "dev/usr/ds_log.usr";  |FBORRA aAbre;
     |SI #regcam01#1 = "S";
         |XABRE "W", aAbre, nHandle;

         |BUCLE regcam02;

         |XCIERRA nHandle;
     |FINSI;
|FPRC;

|PROCESO Tipo0C1;  |TIPO 0;
     aAlfa = Contenido; |QBF aAlfa;
     |SI aAlfa = ""; |ACABA; |FINSI;

     |C_M #regcam01#2, 1, "S";
     |C_M #regcam01#3, 1, "S";
     |C_M #regcam01#4, 1, "S";
     |C_M #regcam01#5, 1, "S";

     |ESTADO_CONTROL nGrid2,  "ENABLE";
     |ESTADO_CONTROL nGrid3,  "ENABLE";
     |ESTADO_CONTROL nBoton2, "ENABLE";
     |ESTADO_CONTROL nBoton3, "ENABLE";

     aEsTado3 = "ENABLE";

     |SI #regcam01#4 = "T";
         |ESTADO_CONTROL nBoton2, "DISABLE";
         |ESTADO_CONTROL nBoton3, "DISABLE";

         aEsTado3 = "DISABLE";
     |FINSI;

     |SI #regcam01#1 = "N";
         |C_M #regcam01#2, 1, "N";
         |C_M #regcam01#3, 1, "N";
         |C_M #regcam01#4, 1, "N";
         |C_M #regcam01#5, 1, "N";

         |ESTADO_CONTROL nGrid2, "DISABLE";
         |ESTADO_CONTROL nGrid3, "DISABLE";
         |ESTADO_CONTROL nBoton2, "DISABLE";
         |ESTADO_CONTROL nBoton3, "DISABLE";

         aEsTado3 = "DISABLE";
     |FINSI;

     |PINTA #regcam01#2;
     |PINTA #regcam01#3;
     |PINTA #regcam01#4;
     |PINTA #regcam01#5;

     |SI #regcam01#Campo# ! Contenido; |Y #regcam01#4 = "C";
      |Y #regcam01#1 = "S"; |Y nPaso = 1;
          |HAZ CargaApli;
     |FINSI;

     |SI #regcam01#Campo# ! Contenido; |Y nPaso = 1;
          |SI #regcam01#4 = "T"; |O #regcam01#1 = "N";
               |HAZ DescargaApli;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo0C4;  |TIPO 0;
     aAlfa = Contenido; |QBF aAlfa;
     |SI aAlfa = ""; |ACABA; |FINSI;

     |SI #regcam01#1 = "N";  |ACABA;  |FINSI;

     |C_M #regcam01#5, 1, "S";

     |ESTADO_CONTROL nBoton2, "ENABLE";
     |ESTADO_CONTROL nBoton3, "ENABLE";
     aEsTado3 = "ENABLE";

     |SI #regcam01#4 = "T";
         |ESTADO_CONTROL nBoton2, "DISABLE";
         |ESTADO_CONTROL nBoton3, "DISABLE";
         aEsTado3 = "DISABLE";
         |C_M #regcam01#5, 1, "N";
     |FINSI;

     |PINTA #regcam01#5;

     |SI #regcam01#Campo# ! Contenido; |Y #regcam01#4 = "C";
      |Y #regcam01#1 = "S";
          |HAZ CargaApli;
     |FINSI;

     |SI #regcam01#Campo# ! Contenido;
          |SI #regcam01#4 = "T"; |O #regcam01#1 = "N";
               |HAZ DescargaApli;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Evento2;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#regcam02.=;
     |FINSI;

     |SI FSalida = 4;
         |LEE 101#regcam02.=;
         |SI FSalida ! 0;  |ACABA;  |FINSI;

         |SI #regcam02#1 = "*";
             #regcam02#1 = " ";
             #regcam02#2 = "90,90,90;255,255,255";
         |SINO;
             #regcam02#1 = "*";
             #regcam02#2 = "0,0,0;0,0,0";
         |FINSI;

         |GRABA 020#regcam02.a;
         |LIBERA #regcam02;

         |REFRESCACONTROL nGrid2;
     |FINSI;
|FPRC;

|PROCESO Evento3;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#regcam03.=;
     |FINSI;

     |SI FSalida = 4;
         |LEE 101#regcam03.=;
         |SI FSalida ! 0;  |ACABA;  |FINSI;

         |SI #regcam03#1 = "*";
             #regcam03#1 = " ";
             #regcam03#2 = "90,90,90;255,255,255";
         |SINO;
             #regcam03#1 = "*";
             #regcam03#2 = "0,0,0;0,0,0";
         |FINSI;

         |GRABA 020#regcam03.a;
         |LIBERA #regcam03;
         |SALVA_FICHA #regcam03;
         |HAZ CargaFiltro;
         |REPON_FICHA #regcam03;
         |REFRESCACONTROL nGrid3;
     |FINSI;
|FPRC;

|PROCESO SacaApli;
     aApli = "";
     aLon = aDir % A0;
     |PARA i = aLon; |SI i > 0; |HACIENDO i = i - 1;
          nPos = i * 100 + 1;
          aAlfa = aDir % nPos;
          |SI aAlfa = "/"; |O aAlfa = "\";
               |SAL;
          |FINSI;
          aApli = aAlfa + aApli;
     |SIGUE;
|FPRC;

|PROCESO CargaApli;
     |DESTRUYECONTROL nGrid3;

     |DBASS aBase;
     aDirectorio = aBase + "*";

     |QUE_SISTEMA aSistema;
     |_PDIR aDirectorio;
     |PARA; |SI FSalida ] 0; |HACIENDO;
          aDir = aDirectorio;
          |SI aSistema = "ESDOS";
               aFic = aDirectorio;
               |XABRE "E", aFic, 0;
               |SI FSalida ] 0; aDir = ""; |FINSI;
          |SINO;
               aFic = aDirectorio + "/";
               |XABRE "E", aFic, 0;
               |SI FSalida < 0; aDir = ""; |FINSI;
          |FINSI;

          |SI aDir ! "";
               aDir = aDirectorio;
               |HAZ SacaApli;
               #regcam03#0 = aApli;
               #regcam03#1 = "";
               #regcam03#2 = "90,90,90;255,255,255";
               |GRABA 000#regcam03.n;
          |FINSI;

          |_SDIR aDirectorio;
     |SIGUE;
     |LEE 000#regcam03.p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          aDirectorio = aBase + #regcam03#0; |QBF aDirectorio;
          aDirectorio = aDirectorio + "/*men";
          |_PDIR aDirectorio;
          |SI FSalida < 0;
               |BORRA 020#regcam03.a;
          |FINSI;
          |LEE 000#regcam03.s;
     |SIGUE;
     |LEE 000#regcam03.p;
     |LINEAL_SIMPLE #1#regcam03, nRango, 1406, 2635, NULL, NULL, Evento3;
     nGrid3 = FSalida;

     |SALVA_FICHA #regcam03;
     |HAZ CargaFiltro;
     |REPON_FICHA #regcam03;
|FPRC;

|PROCESO DescargaApli;
     |DESTRUYECONTROL nGrid3;

     |CIERRA #regcam03;
     |DELINDEX #regcam03;
     |ABRE #regcam03;

     |LINEAL_SIMPLE #1#regcam03, nRango, 1406, 2635, NULL, NULL, Evento3;
     nGrid3 = FSalida;
     |ESTADO_CONTROL nBoton3, aEsTado3;
|FPRC;

|PROCESO Boton3;
     |HAZ CargaApli;
|FPRC;

|PROCESO EntraPorPantalla;
     |PINPA #0#regcam01;  nPanta = FSalida;

     |HAZ TraeUsuarios;

     nRango = 4 + 8+ 16 + 32;

     |LINEAL_SIMPLE #1#regcam02, nRango, 1160, 2690, NULL, NULL, Evento2;
     nGrid2 = FSalida;

     |LINEAL_SIMPLE #1#regcam03, nRango, 1406, 2635, NULL, NULL, Evento3;
     nGrid3 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Actualizar lista de aplicaciones", 1438, 1558, Boton3;
     nBoton3 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Editar lista de tablas  (uso avanzado)", 1838, 1958, Boton2;
     nBoton2 = FSalida;

     Contenido = "_";
     |HAZ Tipo0C1;
     nPaso = 1;

     |PINDA #0#regcam01;
     |PARA; |SI; |HACIENDO;
          |ENDATOS #1#regcam01;
          |SI FSalida ! 0;  |SAL;  |FINSI;

          nHay = 0;
          |BUCLE MarcaApli;
          |SI #regcam01#1 = "S"; |Y #regcam01#4 = "C"; |Y nHay = 0;
              |MENAV "0000Deber  seleccionar al menos una aplicaci¢n.";
          |SINO;
              |HAZ TrataINI;
              |GRABA 020#regcam01.a;
              |SAL;
          |FINSI;
     |SIGUE;
     |LIBERA #regcam01;

     |PULSATECLA;
|FPRC;

|PROCESO MiraSiActualizo;
     nDiasCiclo = 30;
     fFecActiva = "29.09.2016";
     fUltFecha  = #regcam01#35;
     fHoy       = ~;
     nActualizo = 0;

     |SI fHoy ] fFecActiva;
         |SI fUltFecha < fFecActiva;
             nActualizo = 1;
             |ACABA;
         |FINSI;

         nDias = fHoy - fUltFecha;
         |SI nDias ] nDiasCiclo;
             nActualizo = 1;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Actualiza;
     |HAZ MiraSiActualizo;
     |SI nActualizo = 0;  |ACABA;  |FINSI;

     |HAZ TraeUsuarios;

     |DBASS aBase;
     aDirectorio = aBase + "*";

     |QUE_SISTEMA aSistema;
     |_PDIR aDirectorio;
     |PARA; |SI FSalida ] 0; |HACIENDO;
          aDir = aDirectorio;
          |SI aSistema = "ESDOS";
               aFic = aDirectorio;
               |XABRE "E", aFic, 0;
               |SI FSalida ] 0; aDir = ""; |FINSI;
          |SINO;
               aFic = aDirectorio + "/";
               |XABRE "E", aFic, 0;
               |SI FSalida < 0; aDir = ""; |FINSI;
          |FINSI;

          |SI aDir ! "";
               aDir = aDirectorio;
               |HAZ SacaApli;

               #regcam03#0 = aApli;
               #regcam03#1 = "";
               #regcam03#2 = "90,90,90;255,255,255";

               |GRABA 000#regcam03.n;
          |FINSI;

          |_SDIR aDirectorio;
     |SIGUE;

     |LEE 000#regcam03.p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          aDirectorio = aBase + #regcam03#0; |QBF aDirectorio;
          aDirectorio = aDirectorio + "/*men";
          |_PDIR aDirectorio;
          |SI FSalida < 0;
               |BORRA 020#regcam03.a;
          |FINSI;

          |LEE 000#regcam03.s;
     |SIGUE;

     aFichero = ("91" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #dsw00010#aFichero#;
     |ABRE #dsw00010;  |CIERRA #dsw00010;  |DELINDEX #dsw00010;

     |ABRE #dsw00010;
     |LEE 000#regcam03.p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          |SI #regcam03#1 = "*";
               aAlfa = #regcam03#0; |QBF aAlfa;
               |HAZ LeeAplica;
          |FINSI;

          |LEE 000#regcam03.s;
     |SIGUE;
     |CIERRA #dsw00010;

     |XABRE "W", aFiltro, nHandle;
     |BUCLE dsw00010;
     |XCIERRA nHandle;

     |DELINDEX #dsw00010;

     nHay = 0;
     |BUCLE MarcaApli;
     |SI #regcam01#1 = "S"; |Y #regcam01#4 = "C"; |Y nHay = 0;
         |ACABA;
     |FINSI;

     |HAZ TrataINI;

     fHoy         = ~;
     #regcam01#35 = fHoy;

     |GRABA 020#regcam01.a;
     |LIBERA #regcam01;
|FPRC;

|PROGRAMA;
     |DBASS aBase;
     aLogger   = aBase + "logger/logger.spo";
     aFiltro   = aBase + "dev/usr/filtro.txt";

     |SI PARAMETRO = "";
         |HAZ EntraPorPantalla;
     |FINSI;

     |SI PARAMETRO = "ACTUALIZA";
         |HAZ Actualiza;
     |FINSI;
|FPRO;

|PROCESO Tipo80;  |TIPO 80;
     |ABRET #regcam01;
     |LEE 101#regcam01.p;
     |SI FSalida ! 0;
         |DDEFECTO #regcam01;
         |GRABA 020#regcam01.b;
     |FINSI;

     |ABRET #regcam02;
     |ABRET #regcam03;

     |LEE 000#regcam02.p;
     |LEE 000#regcam03.p;

     FSalida = 2799;
|FPRC;

|PROCESO Tipo90;  |TIPO 90;
     |CIERRAT #regcam01;
     |CIERRAT #regcam02;
     |CIERRAT #regcam03;
|FPRC;
