|FICHEROS;
     crontcom;
     crontcom@;
|FIN;

|VARIABLES;
     nSwNuevo    = 0;
     nCanalLista = 0;

     aTitulo = "";
     aTexto  = "";
     aLista  = "";
     aLinea  = "";
     aCar    = "";
     aLon    = "";

     i = 0;
     fFecha = @;
     nDia = 0;
     nMes = 0;
     nNume = 0;
     aBase = "";
     nBufUsr = 0;
     nEleUsr = 0;
     aAlfa1 = "";
     nSwEsta = 0;
     nNumero = 0;
     nMayor = 0;
     nClave = 0;
     nPos = 0;
     aAbre   = "";
     nHandle = 0;
     nCanal  = 0;
     aChar   = "";
     nCampo  = 0;
     aAscii  = "";
     nCodAsc = 0;
     nRegis  = 0;
     aAlfa   = "";
     aAlfa2  = "";
     aAlfa3  = "";
     nTecla  = 0;
     nLong   = 0;
     aLong   = "";
     aComent = "";
     nPara   = 0;
     nPara2  = 0;
     nSw     = 0;
     nSw2     = 0;
     nSw3     = 0;
     aDBass  = "";
     aDirBase    = "";
     aAplicacion = "";
     aDefEmpre   = "";
     aNumEmpre   = "";
     aTAB        = "";
     aParam      = "";
     aShell      = "";
     aMin     = "";
     aHora    = "";
     aDia_mes = "";
     aMes     = "";
     aDia_Sem = "";
     aComando = "";
|FIN;

|PROCESO ClaveBaja;
     #crontcom#0 = 00;
     |SI FSalida="POSICION";
          #crontcom#1 = nClave;
     |SINO;
          #crontcom#1=000;
     |FINSI;
|FPRC;

|PROCESO ClaveAlta;
     #crontcom#0 = 99;
     #crontcom#1 = 999;
|FPRC;

|| -------------- CONSTRUIMOS UNA SHELL O UN BAT

|PROCESO ShellLinux;
     aTitulo     = #crontcom#22; |QBF aTitulo;

     |DBASS aDBass; |QBF aDBass;
     aShell = aDBass + "bin/lanza" + nRegis + ".sh";
     |XABRE "WB", aShell, nHandle;
     aAlfa = "#" + aTitulo + &10;
     |XGRABA nHandle, aAlfa;

     aAlfa = aDBass + "bin/cron" + nRegis  + ".sh &" + &10;
     |XGRABA nHandle, aAlfa;
     |XCIERRA nHandle;
     aAlfa = "chmod 777 " + aShell;
     |SYSTEM aAlfa;

     aShell = aDBass + "bin/cron" + nRegis  + ".sh";
     |XABRE "WB", aShell, nHandle;
     aDirBase    = #crontcom#16; |QBF aDirBase;
     aLong       = aDirBase % 0;
     nLong       = aLong;
     aAlfa       = aDirBase;
     nPos        = 100 + nLong - 1;

     aDirBase    = aAlfa % nPos;
     aAplicacion = #crontcom#17; |QBF aAplicacion;
     aDefEmpre   = #crontcom#18; |QBF aDefEmpre;
     aNumEmpre   = #crontcom#19; |QBF aNumEmpre;
     aTAB        = #crontcom#20; |QBF aTAB;
     aParam      = #crontcom#21; |QBF aParam;

     aAlfa = "#" + aTitulo + &10;
     |XGRABA nHandle, aAlfa;
     aAlfa2 = &10;
     aAlfa3 = &10;
     |SI #crontcom#25 = "S";
          aAlfa = "echo Iniciando la ejecucion:";

          ||aAlfa2 Resetea los logs
          aAlfa2 = " >" + aDBass + "bin/cron" + nRegis + ".log 2>" + aDBass + "bin/cron" + nRegis + ".err" + &10;

          ||aAlfa3 Anyade info a los logs
          aAlfa3 = " >>" + aDBass + "bin/cron" + nRegis + ".log 2>>" + aDBass + "bin/cron" + nRegis + ".err" + &10;

          aAlfa = aAlfa + aAlfa2;
          |XGRABA nHandle, aAlfa;

          aAlfa = "date";
          aAlfa = aAlfa + aAlfa3;
          |XGRABA nHandle, aAlfa;
     |FINSI;

     aTexto = #crontcom#22;   |QBF aTexto;

     |SI #crontcom#23 = "S"; |Y aTexto = "Tarea Generada por DSTOOL (Est.).";
          aLista = "/u/basico/lista_esta.txt";
          |XABRE "U", aLista, nCanalLista;
          |SI FSalida ] 0;
               |PARA; |SI; |HACIENDO;
                    |XLEE nCanalLista, 250, aLinea;
                    |SI FSalida < 0;
                         |SAL;
                    |FINSI;
                    aDirBase = aLinea;
                    aLong    = aDirBase % 0;
                    nLong    = aLong;
                    aAlfa    = aDirBase;
                    nPos     = 100 + nLong - 1;
                    aDirBase = aAlfa % nPos;

                    aAlfa = aDBass + "bin/serverds @" + aDirBase + " " + &34 + "/!:" + aAplicacion +"/"+ aTAB;
                    |SI aDefEmpre ! "";
                         aAlfa = aAlfa + " " + aDefEmpre;
                    |FINSI;
                    |SI aNumEmpre ! "";
                         aAlfa = aAlfa + " " + aNumEmpre;
                    |FINSI;
                    |SI aParam ! "";
                         aAlfa = aAlfa + ";" + aParam;
                    |FINSI;
                    aAlfa = aAlfa + &34 + aAlfa3;
                    |XGRABA nHandle, aAlfa;
               |SIGUE;
               |XCIERRA nCanalLista;
          |SINO;
               aAlfa = aDBass + "bin/serverds @" + aDirBase + " " + &34 + "/!:" + aAplicacion +"/"+ aTAB;
               |SI aDefEmpre ! "";
                    aAlfa = aAlfa + " " + aDefEmpre;
               |FINSI;
               |SI aNumEmpre ! "";
                    aAlfa = aAlfa + " " + aNumEmpre;
               |FINSI;
               |SI aParam ! "";
                    aAlfa = aAlfa + ";" + aParam;
               |FINSI;
               aAlfa = aAlfa + &34 + aAlfa3;
               |XGRABA nHandle, aAlfa;
          |FINSI;
     |SINO;
          aAlfa = aDBass + "bin/serverds @" + aDirBase + " " + &34 + "/!:" + aAplicacion +"/"+ aTAB;
          |SI aDefEmpre ! "";
               aAlfa = aAlfa + " " + aDefEmpre;
          |FINSI;
          |SI aNumEmpre ! "";
               aAlfa = aAlfa + " " + aNumEmpre;
          |FINSI;
          |SI aParam ! "";
               aAlfa = aAlfa + ";" + aParam;
          |FINSI;
          aAlfa = aAlfa + &34 + aAlfa3;
          |XGRABA nHandle, aAlfa;
     |FINSI;

     |SI #crontcom#25 = "S";
          aAlfa = "echo Finalizada la ejecucion:";
          aAlfa = aAlfa + aAlfa3;
          |XGRABA nHandle, aAlfa;

          aAlfa = "date";
          aAlfa = aAlfa + aAlfa3;
          |XGRABA nHandle, aAlfa;
     |FINSI;
     |XCIERRA nHandle;
     aAlfa = "chmod 777 " + aShell;
     |SYSTEM aAlfa;

     aShell = aDBass + "bin/lanza" + nRegis + ".sh";
|FPRC;

|PROCESO Shell;
     |DBASS aDBass; |QBF aDBass;
     aShell = aDBass + "bin/cron" + nRegis  + ".sh";
     |XABRE "WB", aShell, nHandle;
     aDirBase    = #crontcom#16; |QBF aDirBase;
     aLong = aDirBase % 0;
     nLong = aLong;
     aAlfa = aDirBase;
     nPos = 100 + nLong - 1;
     aDirBase    = aAlfa % nPos;
     aAplicacion = #crontcom#17; |QBF aAplicacion;
     aDefEmpre   = #crontcom#18; |QBF aDefEmpre;
     aNumEmpre   = #crontcom#19; |QBF aNumEmpre;
     aTAB        = #crontcom#20; |QBF aTAB;
     aParam      = #crontcom#21; |QBF aParam;

     aAlfa2 = &10;
     aAlfa3 = &10;
     |SI #crontcom#25 = "S";
          aAlfa = "echo Iniciando la ejecucion:";

          ||aAlfa2 Resetea los logs
          aAlfa2 = " >" + aDBass + "bin/cron" + nRegis + ".log 2>" + aDBass + "bin/cron" + nRegis + ".err" + &10;

          ||aAlfa3 Anyade info a los logs
          aAlfa3 = " >>" + aDBass + "bin/cron" + nRegis + ".log 2>>" + aDBass + "bin/cron" + nRegis + ".err" + &10;

          aAlfa = aAlfa + aAlfa2;
          |XGRABA nHandle, aAlfa;

          aAlfa = "date";
          aAlfa = aAlfa + aAlfa3;
          |XGRABA nHandle, aAlfa;
     |FINSI;

     aAlfa = aDBass + "bin/serverds @" + aDirBase + " " + &34 + "/!:" + aAplicacion +"/"+ aTAB;
     |SI aDefEmpre ! "";
          aAlfa = aAlfa + " " + aDefEmpre;
     |FINSI;
     |SI aNumEmpre ! "";
          aAlfa = aAlfa + " " + aNumEmpre;
     |FINSI;
     |SI aParam ! "";
          aAlfa = aAlfa + ";" + aParam;
     |FINSI;
     aAlfa = aAlfa + &34 + aAlfa3;
     |XGRABA nHandle, aAlfa;
     |SI #crontcom#25 = "S";
          aAlfa = "echo Finalizada la ejecucion:";
          aAlfa = aAlfa + aAlfa3;
          |XGRABA nHandle, aAlfa;

          aAlfa = "date";
          aAlfa = aAlfa + aAlfa3;
          |XGRABA nHandle, aAlfa;
     |FINSI;
     |XCIERRA nHandle;
     aAlfa = "chmod 777 " + aShell;
     |SYSTEM aAlfa;
|FPRC;

|PROCESO Bat;
     |DBASS aDBass; |QBF aDBass;
     aShell = aDBass + "bin/cron" + nRegis  + ".bat";
     |XABRE "WB", aShell, nHandle;
     aDirBase    = #crontcom#16; |QBF aDirBase;
     aLong = aDirBase % 0;
     nLong = aLong;
     aAlfa = aDirBase;
     nPos = 100 + nLong - 1;
     aDirBase    = aAlfa % nPos;
     aAplicacion = #crontcom#17; |QBF aAplicacion;
     aDefEmpre   = #crontcom#18; |QBF aDefEmpre;
     aNumEmpre   = #crontcom#19; |QBF aNumEmpre;
     aTAB        = #crontcom#20; |QBF aTAB;
     aParam      = #crontcom#21; |QBF aParam;

     aAlfa = "@echo off" + &13 + &10;
     |XGRABA nHandle, aAlfa;

     aAlfa = "echo Iniciando la ejecucion:";

     ||aAlfa2 Resetea los logs
     aAlfa2 = " >" + aDBass + "bin/cron" + nRegis + ".log 2>" + aDBass + "bin/cron" + nRegis + ".err" + &13 + &10;

     ||aAlfa3 Anyade info a los logs
     aAlfa3 = " >>" + aDBass + "bin/cron" + nRegis + ".log 2>>" + aDBass + "bin/cron" + nRegis + ".err" + &13 + &10;

     aAlfa = aAlfa + aAlfa2;
     |XGRABA nHandle, aAlfa;

     aAlfa = "time /T";
     aAlfa = aAlfa + aAlfa3;
     |XGRABA nHandle, aAlfa;

     aAlfa = aDBass + "bin/diagramx.exe /DS:" + &34 + "@" + aDirBase + " $$$batch,batch# //(" + aAplicacion +")"+ aTAB;
     |SI aDefEmpre ! "";
          aAlfa = aAlfa + "&" + aDefEmpre;
     |FINSI;
     |SI aNumEmpre ! "";
          aAlfa = aAlfa + "&" + aNumEmpre;
     |FINSI;
     |SI aParam ! "";
          aAlfa = aAlfa + ";" + aParam;
     |FINSI;
     aAlfa = aAlfa + &34 + aAlfa3;
     |XGRABA nHandle, aAlfa;

     aAlfa = "echo Finalizada la ejecucion:";
     aAlfa = aAlfa + aAlfa3;
     |XGRABA nHandle, aAlfa;

     aAlfa = "time /T";
     aAlfa = aAlfa + aAlfa3;
     |XGRABA nHandle, aAlfa;

     |XCIERRA nHandle;
|FPRC;


|| -------------- EDITA UNA SHELL A UN TAB
|PROCESO ComTAB; |TIPO 0;
     |SI #crontcom#15 = "S"; |Y nSw2 = 0;
          |PUSHV 0501 2380;
          nSw2 = 1;
          |CLS;
          |CUADRO 1112 2060;
          |ATRI R;
          |PINTA 1016, " Comando al TAB ";
          |ATRI 0;
          |PINTA 1214, "Dir. Base";
          |PINTA 1314, "Aplicacion";
          |PINTA 1414, "Def. Empresa";
          |PINTA 1514, "Num. Empresa";
          |PINTA 1714, "TAB";
          |PINTA 1814, "Parametros";
          |PINTA 1914, "Titulo";
          |CAMPO_POSICION #crontcom#22, 1, 1929;
          |C_M #crontcom#22, 1, "N";
          |C_M #crontcom#25, 1, "N";
          |PARA nPara = 9; |SI nPara [ 15; |HACIENDO nPara = nPara + 1;
               |C_V #crontcom#nPara#,1,"N";
               |C_M #crontcom#nPara#,1,"N";
          |SIGUE;
          |C_M #crontcom#1,1,"N";
          |PARA nPara = 16; |SI nPara [ 22; |HACIENDO nPara = nPara + 1;
               |C_V #crontcom#nPara#,1,"S";
               |C_M #crontcom#nPara#,1,"S";
               |PINTA #crontcom#nPara#;
          |SIGUE;
          |SI nSwNuevo = 1;
               |DBASS aAlfa;
               #crontcom#16 = aAlfa;
          |SINO;
               aAlfa = #crontcom#16;
               |QBF aAlfa;
               |SI aAlfa = "";
                    |DBASS aAlfa;
                    #crontcom#16 = aAlfa;
               |FINSI;
          |FINSI;
          |ENDATOS #1#crontcom;
          |SI FSalida = 0;
               |QUE_SISTEMA aAlfa;
               |SI aAlfa = "ESDOS";
                    |HAZ Bat;
               |FINSI;
               |SI aAlfa = "ESUNIX"; |O aAlfa = "ESBSD";
                    |HAZ ShellLinux;
               |FINSI;
               nSw3 = 1;
               #crontcom#14 = aShell;
          |FINSI;
          |POPV;
          |ATRI R;
          |SI nTecla = 14;
               |PINTA 2015, " NUEVO COMANDO ";
          |FINSI;
          |SI nTecla = 15;
               |PINTA 2015, " MODIF. COMANDO ";
          |FINSI;
          |SI nTecla = 16;
               |PINTA 2015, " COPIA COMANDO ";
          |FINSI;
          |ATRI 0;

          |CAMPO_POSICION #crontcom#22, 1, 2310;
          |C_M #crontcom#22, 1, "S";
          |C_M #crontcom#25, 1, "S";
          |PARA nPara = 9; |SI nPara [ 14; |HACIENDO nPara = nPara + 1;
               |C_V #crontcom#nPara#,1,"S";
               |C_M #crontcom#nPara#,1,"S";
               |PINTA #crontcom#nPara#;
          |SIGUE;
          |SI nSw3 = 1;
               |C_M #crontcom#14,1,"N";
               |C_V #crontcom#14,1,"S";
          |FINSI;
          |PARA nPara = 16; |SI nPara [ 21; |HACIENDO nPara = nPara + 1;
               |C_V #crontcom#nPara#,1,"N";
               |C_M #crontcom#nPara#,1,"N";
          |SIGUE;
     |FINSI;
|FPRC;


|| -------------- ESCRIBE EN EL FICHERO
|PROCESO Escribe;
     aMin     = #crontcom#2;    |QBF aMin;
     aHora    = #crontcom#3;    |QBF aHora;
     aDia_mes = #crontcom#4;    |QBF aDia_mes;
     aMes     = #crontcom#5;    |QBF aMes;
     aDia_Sem = #crontcom#6;    |QBF aDia_Sem;
     aComando = #crontcom#7;    |QBF aComando;
     aTitulo = #crontcom#22;    |QBF aTitulo;


     aAlfa = "#" + aTitulo + &10;
     |XGRABA nCanal, aAlfa;

     |SI #crontcom#8 = "S";
          aAlfa = aMin + " " + aHora + " " + aDia_mes + " " + aMes + " " + aDia_Sem + " " + aComando + &13 + &10;
     |SINO;
          aAlfa = "#" + aMin + " " + aHora + " " + aDia_mes + " " + aMes + " " + aDia_Sem + " " + aComando + &13 + &10;
     |FINSI;
     |XGRABA nCanal, aAlfa;
|FPRC;

|DEFBUCLE crontcom;
     #crontcom#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Escribe;
|FIN;

|| ------------- COLOREA ACTIVOS
|PROCESO Tipo5; |TIPO 5;
     #crontcom#8 = "N";
     |LINEAS_BI_ATRI #crontcom#8,S,D,-1;
     |MENSA "0000<F5>Act./Desact. <F6>Nuevo <F7>Modificar <F8>Copiar <F9>Borrar";
|FPRC;


|| ------------NUEVO, COPIA O MODIFCA COMANDO
|PROCESO Nuevo;
     |BLIN 20;
     |BLIN 21;
     |BLIN 22;
     |BLANCO 2010 2280;
     |ATRI R;
     |SI nTecla = 14;
          |PINTA 2015, " NUEVO COMANDO ";
     |FINSI;
     |SI nTecla = 15;
          |PINTA 2015, " MODIF. COMANDO ";
     |FINSI;
     |SI nTecla = 16;
          |PINTA 2015, " COPIA COMANDO ";
     |FINSI;
     |ATRI 0;
     |SI nTecla = 15; |O nTecla = 16;
          |PARA nPara = 9; |SI nPara [ 14; |HACIENDO nPara = nPara + 1;
               nPara2 = nPara -7;
               #crontcom#nPara# = #crontcom#nPara2#;
          |SIGUE;
     |FINSI;
     |SI nTecla = 14;
          |PARA nPara = 9; |SI nPara [ 14; |HACIENDO nPara = nPara + 1;
               #crontcom#nPara# = "";
          |SIGUE;
          #crontcom#11 = "*";
          #crontcom#13 = "*";
     |FINSI;
     |SI nTecla = 15;
          nRegis = #crontcom#1;
     |SINO;
          |DDEF aAlfa;
          aAlfa = aAlfa + "crontcom";
          |CARGA_DEF aAlfa, crontcom@;
          |SI FSalida = 0;
               |ABRE #crontcom@;
               |LEE 000#crontcom@.u;
               |SI FSalida = 0;
                    nRegis = #crontcom@#1 + 1;
               |SINO;
                    nRegis = 1;
               |FINSI;
               |CIERRA #crontcom@;
               |DESCARGA_DEF #crontcom@;
          |FINSI;
     |FINSI;
     |PINTA 2101, "TAB";
     |PINTA 2105, "L/E";
     |PINTA 2111, "Hora";
     |PINTA 2119, "Minuto";
     |PINTA 2130, "Mes";
     |PINTA 2137, "Dia M.";
     |PINTA 2147, "Dia S.";
     |PINTA 2155, "Comando";
     |PARA nPara = 9; |SI nPara [ 15; |HACIENDO nPara = nPara + 1;
          |C_V #crontcom#nPara#,1,"S";
          |C_M #crontcom#nPara#,1,"S";
          |PINTA #crontcom#nPara#;
     |SIGUE;
     |C_M #crontcom#22,1,"S";
     |C_M #crontcom#1,1,"N";
     |C_V #crontcom#25,1,"S"; #crontcom#25 = #crontcom#24;|PINTA #crontcom#25;
     |ENDATOS #1#crontcom;
     |SI FSalida = 0;
          #crontcom#24 = #crontcom#25;
          |PARA nPara = 2; |SI nPara [ 7; |HACIENDO nPara = nPara + 1;
               nPara2 = nPara + 7;
               #crontcom#nPara# = #crontcom#nPara2#;
          |SIGUE;
          |SI nTecla = 14; |O nTecla = 16;
               #crontcom#8 = "S";
               #crontcom#1 = nRegis;
               #crontcom#23 = "N";
               |GRABA 020#crontcom.n;
          |FINSI;
          |SI nTecla = 15;
               |GRABA 020#crontcom.a;
          |FINSI;
     |FINSI;
     |PARA nPara = 9; |SI nPara [ 15; |HACIENDO nPara = nPara + 1;
          |C_V #crontcom#nPara#,1,"N";
          |C_M #crontcom#nPara#,1,"N";
     |SIGUE;
     |C_V #crontcom#25,1,"N";
     |C_M #crontcom#1,1,"S";
     |C_M #crontcom#22,1,"N";
     |HAZ GrabaFich;
     |PTEC 807;
|FPRC;

|PROCESO Tecla; |TIPO 11;
   |SI FSalida = 1;
        nSw = 1;
        |SI nTecla = 17;
          nSw = 0;
        |FINSI;
   |SINO;
     |SI FEntrada = 3;
          |PTEC 807;
     |FINSI;
     nTecla = FSalida;
     |SI nTecla = 13;
          || ------------- ACTIVA / DESAC. COMANDO
          |LEE 101#crontcom.=;
          |SI FSalida = 0;
               |SI #crontcom#8 = "S";
                    #crontcom#8 = "N";
               |SINO;
                   #crontcom#8 = "S";
               |FINSI;
               |GRABA 020#crontcom.a;
               |PTEC 807;
          |SINO;
               |ERROR;
          |FINSI;
     |FINSI;
     |SI nTecla = 14;
          || ----- ALTA COMANDO
          nSwNuevo = 1;
          |HAZ Nuevo;
     |FINSI;
     |SI nTecla = 15;
          || ------- MODIF. COMANDO
          nSwNuevo = 0;
          |LEE 000#crontcom.=;
          |SI FSalida = 0;
               |HAZ Nuevo;
          |SINO;
               |ERROR;
          |FINSI;
     |FINSI;
     |SI nTecla = 16;
          || ------- COPIA COMANDO
          nSwNuevo = 0;
          |LEE 000#crontcom.=;
          |SI FSalida = 0;
               |HAZ Nuevo;
          |SINO;
               |ERROR;
          |FINSI;
     |FINSI;
     |SI nTecla = 17;
          |BORRA 000#crontcom.a;
          |SI FSalida = 0; |PTEC 807; |FINSI;
     |FINSI;
   |FINSI;
   nClave = #crontcom#1;
|FPRC;

|PROCESO crontcomShells;
     |SI #crontcom#8 = "N"; |ACABA; |FINSI;

     |SI #crontcom#15 = "N"; |ACABA; |FINSI;

     nRegis = #crontcom#1;

     |QUE_SISTEMA aAlfa;
     |SI aAlfa = "ESDOS";
          |HAZ Bat;
     |FINSI;
     |SI aAlfa = "ESUNIX"; |O aAlfa = "ESBSD";
          |HAZ ShellLinux;
     |FINSI;
|FPRC;

|DEFBUCLE crontcomShells;
     #crontcom#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, crontcomShells;
|FIN;

|| -------------- GRABA EL FICHERO "TAREAS.SRV" -----------------
|PROCESO GrabaFich;
     |DBASS aAbre;
     aAbre = aAbre + "bin/tareas.srv";
     |XABRE "WB", aAbre, nCanal;
     |DBASS aDBass; |QBF aDBass;
     aAlfa = aDBass + "cron";
     |MKDIR aAlfa;
     |BUCLE crontcom;
     |XCIERRA nCanal;

     ||Regenera las shells a tab's. por si hay cambios (p.e. el tema de los logs y >> o >)

     |BUCLE crontcomShells;
|FPRC;

||-------------- CREACIO DE USUARIO BATCH -------------------
|PROCESO BuscaSecuencial;
     aAlfa = aBase + "dev/usr/ds_sys.psw"
     |LEESECUENCIAL aAlfa, nBufUsr, nEleUsr, 0, 0;
     |PARA nRegis = 0; |SI nRegis < nEleUsr; |HACIENDO nRegis = nRegis + 1;
           |DEL_BUF nBufUsr, nRegis, aAlfa;

           aAlfa1 = aAlfa % 410;
           |QBF aAlfa1;
           |SI aAlfa1 = "batch";
                 nSwEsta = 1;
                 |SAL;
           |FINSI;

           aAlfa1 = aAlfa % 103;
           nNumero = aAlfa1;

           |SI nMayor < nNumero;
               nMayor = nNumero;
           |FINSI;
     |SIGUE;
     |FINDIM nBufUsr;
|FPRC;


|PROCESO MeteBatch;
     aAbre = aBase + "dev/usr/ds_sys.psw"
     |XABRE "A", aAbre, nHandle;

     aAlfa = ("000" + nMayor) % -103;
     aAlfa = aAlfa + "batch     HOHMIYHQIACMCMCMCMCMN";
     |XGRABA nHandle, aAlfa;

     |XCIERRA nHandle;
|FPRC;

||-------------- PROGRAMA PRINCIPAL -------------------------

|PROGRAMA;
     |DBASS aBase;  |QBF aBase;
     |HAZ BuscaSecuencial;
     |SI nSwEsta = 0;
          nMayor = nMayor + 1;
          |HAZ MeteBatch;
     |FINSI;

     |CLS;
     |CABEZA "Tareas programadas";

     |PARA; |SI nSw ! 1; |HACIENDO;
          nSw = 0;
          nSw2 = 0;
          nSw3 = 0;
          |PINPA #0#crontcom;
          |ENTLINEAL #1#crontcom,2,4,22,1,ClaveBaja, ClaveAlta;
     |SIGUE;
     |HAZ GrabaFich;
|FPRO;

|PROCESO Chequeo;
     aAlfa = #crontcom#Campo#; |QBF aAlfa;
     |SI aAlfa = "*"; |ACABA; |FINSI;

     aLon = aAlfa % A0;
     |PARA i = 0; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aCar = aAlfa % nPos;
          |SI aCar = "/"; |ACABA; |FINSI;
          |SI aCar = "-"; |ACABA; |FINSI;
     |SIGUE;

     nNume = #crontcom#Campo#;
     #crontcom#Campo# = nNume; |PINTA #crontcom#Campo#;

     |SI Campo = 10;
          |SI #crontcom#10 < 0; |O #crontcom#10 > 23;
               |MENAV "0000Hora incorrecta...";
               |ERROR;
          |FINSI;
     |FINSI;
     |SI Campo = 9;
          |SI #crontcom#9 < 0; |O #crontcom#9 > 59;
               |MENAV "0000Minutos incorrectos...";
               |ERROR;
          |FINSI;
     |FINSI;
     |SI Campo = 12;
          |SI #crontcom#12 < 0; |O #crontcom#12 > 12;
               |MENAV "0000Mes incorrecto...";
               |ERROR;
          |FINSI;
     |FINSI;
     |SI Campo = 13;
          |SI #crontcom#13 < 0; |O #crontcom#13 > 7;
               |MENAV "0000Minutos incorrectos...";
               |ERROR;
          |FINSI;
     |FINSI;
     |SI Campo = 11;
          |SI #crontcom#11 < 0; |O #crontcom#11 > 31;
               |MENAV "0000Dia incorrecto...";
               |ERROR;
          |SINO;
               aAlfa = #crontcom#12; |QBF aAlfa;
               |SI aAlfa ! "*";
                    aAlfa = #crontcom#11; |QBF aAlfa;
                    |SI aAlfa ! "*";
                         nMes = #crontcom#12;
                         nDia = #crontcom#11;
                         aAlfa = (("00" + nDia) % -102) +"."+(("00" + nMes) % -102)+".2000";
                         fFecha = aAlfa;
                         nNume = fFecha;
                         fFecha = nNume;
                         |SI fFecha ! aAlfa;
                              |MENAV "0000Dia incorrecto...";
                              |ERROR;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;
