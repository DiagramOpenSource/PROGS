|| -----------------------------------------------------------------------||
|| dsvisor2.cal                                                [dstool]   ||
||                                                                        ||
|| Esta parte del codigo es la encargada de visualizar el asistente       ||
|| para la creacion de una VISTA. Nos basamos en los ficheros dsdatXXX    ||
|| donde se encuentra los que hay instalado en el servidor.               ||
|| Solo para DIAGRAM.X                                                    ||
||                                                                        ||
|| Ini: --                                                                ||
|| Ult: 15may2006                                           dsPRODUCCION  ||
|| -----------------------------------------------------------------------||

|FICHEROS;
     :/agitool/def/dsvisorx       #000;
     :/agitool/def/dsvispan       #001;
     :/agitool/def/dsvispa2       #002;

     :/agitool/def/dsdatapl       #011;  || aplicaciones
     :/agitool/def/dsdatemp       #012;  || aplicaciones
     :/agitool/def/dsdatdef       #013;  || aplicaciones

     :/agitool/def/dsw00006       #101;  || tmp.aplicaciones
     :/agitool/def/dsw00007       #102;  || tmp.empresas
     :/agitool/def/dsw00008       #103;  || tmp.defs

     refer@                        #200;
|FIN;

|VARIABLES;
     aREFRESCA = ":/agitool/bin/dsdatzzz";

     aTecla    = "";
     aEsc1     = "";
     aEsc2     = "";
     aRetu     = "";
     nRango    = 0;

     nVent0    = 0;
     nPanta1   = 0;
     nPanta2   = 0;
     nGridAPL  = 0;
     nGridEMP  = 0;
     nGridDEF  = 0;

     aDef      = "";
     nClaves   = 0;
     aNombDef  = "";

     aDBASS    = "";

     aAlfa     = "";
     aAlfa01   = "";
     aAlfa02   = "";

     afApl     = "";
     afMen     = "";
     aNombApl  = "";
     afEmp     = "";
     aNombEmp  = "";
     aData     = "";
     afDef     = "";
     aFich     = "";

     aIniEMP   = "";
     aFinEMP   = "";
     aIniDEF   = "";
     aFinDEF   = "";

     aMsg       = "";
     nLectura   = 0;
     nWidGuarda = 0;
     nIdGuarda  = 0;
     nSidGuarda = 0;
     nWid       = 0;
     nId        = 0;
     nSid       = 0;
|FIN;

|VARIABLES;
     &_swDesdeTool = 0;

     &_swMulti = "";
     &_aCodApl = "";
     &_aNomApl = "";
     &_aCodEmp = "";
     &_aNomEmp = "";
     &_aDef    = "";
     &_aDesDef = "";
     &_aTabla  = "";
     &_error   = 0;
|FIN;








|| -----------------------------------------------------------------
|| El ASISTENTE.
|| -----------------------------------------------------------------
|PROCESO _AsistenteX;

     _error = 100;
     |DBASS aDBASS; |QBT aDBASS;

     |HAZ CargaTMPS;

     _error = 100;
     |DBASS aDBASS; |QBT aDBASS;

     |ABRET #101;
     |ABRET #102;
     |ABRET #103;

     |SI _swDesdeTool = 0;
          |DDEFECTO #101;
          #101#00 = _aCodApl;
          |LEE 040#101.=;
          _aNomApl = #101#01;
          afEmp    = #101#03;

          |DDEFECTO #102;
          #102#00 = _aCodApl;
          #102#01 = _aCodEmp;
          |LEE 040#102.=;
          _aNomEmp = #102#02;
     |FINSI;


     |HAZ Inicio2X;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::{{001}}Salir," + nGridDEF;
          |LEETECLA aTecla;
          |SI aTecla = aRetu; |SAL;  |FINSI;
          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
     |SIGUE;

     |CIERRAT #101;
     |CIERRAT #102;
     |CIERRAT #103;

     |HAZ BorraTMPS;

|FPRC;






|| -----------------------------------------------------------------
|| Procesos de eventos
|| -----------------------------------------------------------------
|PROCESO evBtnGraba;
     |SI _error ! 10;
          |MENAV "0000Seleccion incompleta.";
          |ACABA;
     |FINSI;

     |VENTANA 0501, 2999, -1, -1, "";
     nVent0 = FSalida;

     |HAZ Asistente_2_X;

     |FINVENTANA nVent0;

     |PTEC 806;

|FPRC;

|PROCESO evBtnSalir;
     |PTEC 806;
|FPRC;


|PROCESO evRecargaDats;
     aAlfa = "0000NSolo utilice este boton tras instalar nuevas aplicaciones";
     aAlfa = aAlfa + " o abrir nuevas empresas.";
     |CONFI aAlfa;
     |SI FSalida ! 0;
          |ACABA;
     |FINSI;


     |ESTADO_CONTROL nGridAPL, "HIDE";
     |ESTADO_CONTROL nGridEMP, "HIDE";
     |ESTADO_CONTROL nGridDEF, "HIDE";
     |REFRESCACONTROL nGridAPL;
     |REFRESCACONTROL nGridEMP;
     |REFRESCACONTROL nGridDEF;

     |CIERRAT  #101;
     |CIERRAT  #102;
     |CIERRAT  #103;

     |HAZ BorraTMPS

     |HAZ IniEspera;
     aAlfa = aREFRESCA
     |SUB_EJECUTA aREFRESCA;
     |HAZ FinEspera;

     |HAZ CargaTMPS;

     |ABRET #101;
     |ABRET #102;
     |ABRET #103;

      aData   = "";
      afDef   = "";
      aIniEMP = "";
      aFinEMP = "";
      aIniDEF = "";
      aFinDEF = "";
      _error  = 1;

     |ESTADO_CONTROL nGridAPL, "SHOW";
     |ESTADO_CONTROL nGridEMP, "SHOW";
     |ESTADO_CONTROL nGridDEF, "SHOW";

     |REFRESCACONTROL nGridAPL;
     |REFRESCACONTROL nGridEMP;
     |REFRESCACONTROL nGridDEF;

|FPRC;






|PROCESO evGridAPL;
     |SI FSalida ! 4; |ACABA; |FINSI;

     aMsg = #101#01; |QBF aMsg;
     |HAZ PintaX;

     afApl = #101#00; |QBT afApl;
     afEmp = #101#03; |QBT afEmp;
     afMen = #101#04; |QBT afMen;
     aData = "";
     afDef = "";
     aIniEMP ="          ";
     aFinEMP ="zzzzzzzzzz";
     aIniDEF ="";
     aFinDEF ="";

     FSalida = "SKIPDEFAULT";

     |REFRESCACONTROL nGridEMP;
     |REFRESCACONTROL nGridDEF;

     _error   = 1;       ||   Nos quedan dos selecciones mas

     _aCodApl = #101#00;
     _aNomApl = #101#01;

|FPRC;

|PROCESO evGridEMP;
     |SI FSalida ! 4; |ACABA; |FINSI;

     aData = #102#01; |QBT aData;
     afDef = "";
     aIniDEF ="        ";
     aFinDEF ="zzzzzzzz";

     FSalida = "SKIPDEFAULT";

     |REFRESCACONTROL nGridDEF;

     aMsg = #101#01; |QBF aMsg;
     aMsg = aMsg + " - " + #102#02; |QBF aMsg;
     |HAZ PintaX;

     _error   = 2;       || Nos queda una seleccion mas

     _aCodEmp = #102#01;
     _aNomEmp = #102#02;

|FPRC;

|PROCESO evGridDEF;

     |SI FSalida ! 4; |ACABA; |FINSI;

     |SI afApl = ""; |O aData = "";
          |ACABA;
     |FINSI;

     aMsg = #101#01; |QBF aMsg;
     aMsg = aMsg + " - " + #102#02; |QBF aMsg;
     aMsg = aMsg + " - " + #103#02; |QBF aMsg;
     |HAZ PintaX;

     afDef = #103#01; |QBT afDef;
     aAlfa = aDBASS + afApl + "/def/" + afDef;

     |CARGA_DEF aAlfa, refer@;
     |SI FSalida ! 0; |ACABA; |FINSI;

     aAlfa = aDBASS + afApl + "/fich/";
     |SI afEmp ! "NULL";
          aAlfa = aAlfa + aData + "/";
     |FINSI;
     |PATH_DAT #refer@#aAlfa#;

     |ABRE     #refer@
     |CONSULTA_CLAVE #1#refer@;
     |CIERRA   #refer@;

     |DESCARGA_DEF #refer@;

     || Hemos hecho la seleccion correcta. (podemos pasar al GRABA)
     _aDef    = #103#01;
     _aDesDef = #103#02;
     _error   = 10;

|FPRC;








|| -----------------------------------------------------------------
|| Limites .....
|| -----------------------------------------------------------------
|PROCESO infEMP;
     #102#00 = afApl;
     #102#01 = aIniEMP;
|FPRC;
|PROCESO supEMP;
     #102#00 = afApl;
     #102#01 = aFinEMP;
|FPRC;

|PROCESO infDEF;
     #103#00 = afApl;
     #103#01 = aIniDEF;
|FPRC;
|PROCESO supDEF;
     #103#00 = afApl;
     #103#01 = aFinDEF;
|FPRC;








|| -----------------------------------------------------------------
|| Programacion GRAFICA.
|| -----------------------------------------------------------------

|PROCESO Inicio2X;

     nLectura = 0;
     |PCONTEXTO nLectura, nWidGuarda, nIdGuarda, nSidGuarda;

     |PINPA #0#001;
     nPanta1 = FSalida;

     || Solo se puede resfrescar si estamos en dstool
     |SI _swDesdeTool ! 0;
           |CONTROL_SIMPLE nPanta1, "BOTON,{{137}} Refresca ", 0685, 0695, evRecargaDats;
     |FINSI;
     |CONTROL_SIMPLE nPanta1, "BOTON,{{193}} >>Graba ",  2885, 2895, evBtnGraba;

     nRango  = 4 + 8 + 16 + 32 + 128;

     |SI _swDesdeTool ! 0;

          |LINEAL_SIMPLE #1#101, nRango, 1102, 2632, NULL, NULL, evGridAPL;
          nGridAPL = FSalida;

          |LINEAL_SIMPLE #1#102, nRango, 1135, 2665, infEMP, supEMP, evGridEMP;
          nGridEMP = FSalida;

          |LINEAL_SIMPLE #1#103, nRango, 0967, 2697, infDEF, supDEF, evGridDEF;
          nGridDEF = FSalida;

     |SINO;

          |LINEAL_SIMPLE #1#103, nRango, 1135, 2665, infDEF, supDEF, evGridDEF;
          nGridDEF = FSalida;

     |FINSI;

     |SI _swDesdeTool ! 0;
          aData   = "";
          afDef   = "";
          aIniEMP = "";
          aFinEMP = "";
          aIniDEF = "";
          aFinDEF = "";
          |REFRESCACONTROL nGridAPL;
          |REFRESCACONTROL nGridEMP;
          |REFRESCACONTROL nGridDEF;
     |SINO;
          afApl   = _aCodApl;
          aData   = _aCodEmp;
          afDef   = "";
          aIniEMP = _aCodEmp;
          aFinEMP = _aCodEmp;
          aIniDEF = "        ";
          aFinDEF = "zzzzzzzz";
          |REFRESCACONTROL nGridDEF;
     |FINSI;

|FPRC;


|PROCESO IniEspera;
     aMsg = "P R O C E S A N D O    D A T O S  ...";
     |HAZ PintaX;
|FPRC;
|PROCESO FinEspera;
     aMsg = "                                     ";
     |HAZ PintaX;
|FPRC;



|PROCESO PintaX;
     nLectura = 1;
     nWid     = nWidGuarda;
     nId      = nIdGuarda;
     nSid     = nSidGuarda;
     |PCONTEXTO nLectura, nWid, nId, nSid;

     |ATRI I;
     |PINTA 2805, aMsg;
     |ATRI i;
|FPRC;





|| ----------------------------------------------------------------------
|| segunda pantalla de vista nueva
|| ----------------------------------------------------------------------
|PROCESO Asistente_2_X;
     |DDEFECTO #002;

     #002#00 = _aCodApl;
     #002#01 = _aNomApl;
     #002#02 = _aCodEmp;
     #002#03 = _aNomEmp;
     #002#04 = _aDef;
     #002#05 = _aDesDef;

     |PINPA #0#002;
     nPanta2 = FSalida;

     |PINDA   #0#002;
     |ENDATOS #1#002;

     _error   = 200;
     |SI FSalida = 0; |Y #002#07 > "A";
          _swMulti = #002#06;
          _aTabla  = #002#07;
          _error = 0;
     |FINSI;
|FPRC;
















|| ------------------------------------------------------------------------
|| como trabajamos con temporales ... pasamos los datos con SQL
|| y luego un CARGA_DEF
|| ------------------------------------------------------------------------

|PROCESO CargaTMPS;

     || comprobamos si es la primera vez que entramos para cargar "lo instalado"
     || Si no hay datos en dsdatapl ....
     |ABRE     #dsdatapl;
     |LEE 040#dsdatapl.p;
     |SI FSalida ! 0;
          |CIERRA   #dsdatapl;
          aAlfa = aREFRESCA;
          |SUB_EJECUTA aAlfa;
     |FINSI;
     |CIERRA   #dsdatapl;

     || Duplicando los datos --> TMP
     aAlfa = "v1" + Usuario; |NOME_DAT #101#aAlfa#;
     |ABRE #101; |CIERRA #101; |DELINDEX #101;
     |ABRE     #011;
     |ABRE     #101;
     |LEE 010#011.p;
     |PARA; |SI FSalida=0; |HACIENDO;
          |SALVA_DATOS #011;
          |REPON_DATOS #101;
          |GRABA 010#101.n;
          |LEE 010#011.s
     |SIGUE;
     |CIERRA #011;
     |CIERRA #101;

     aAlfa = "v2" + Usuario; |NOME_DAT #102#aAlfa#;
     |ABRE #102; |CIERRA #102; |DELINDEX #102;
     |ABRE     #012;
     |ABRE     #102;
     |LEE 010#012.p;
     |PARA; |SI FSalida=0; |HACIENDO;
          |SALVA_DATOS #012;
          |REPON_DATOS #102;
          |GRABA 010#102.n;
          |LEE 010#012.s
     |SIGUE;
     |CIERRA #012;
     |CIERRA #102;


     aAlfa = "v3" + Usuario; |NOME_DAT #103#aAlfa#;
     |ABRE #103; |CIERRA #103; |DELINDEX #103;
     |ABRE     #013;
     |ABRE     #103;
     |LEE 010#013.p;
     |PARA; |SI FSalida=0; |HACIENDO;
          |SALVA_DATOS #013;
          |REPON_DATOS #103;
          |GRABA 010#103.n;
          |LEE 010#013.s
     |SIGUE;
     |CIERRA #013;
     |CIERRA #103;



|FPRC;


|PROCESO BorraTMPS;
     |ABRE #101; |CIERRA #101; |DELINDEX #101;
     |ABRE #102; |CIERRA #102; |DELINDEX #102;
     |ABRE #103; |CIERRA #103; |DELINDEX #103;
|FPRC;
