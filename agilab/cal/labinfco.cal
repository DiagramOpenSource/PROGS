|FICHEROS;
    labinfco #0;
    labcatip #1;         || iae cabs.
    lablitip #2;         || iae lins.
    labdesca #3;         || desglose cabs.
    labdesli #4;         || desglose lins.
    labdestm #5;         || tmp. desgloses
    lablitmp #12;         || tmp iae lins.
    labdcotm #15;        || tmp. desgloses para informe comparativo
    labempre #6;         || empresas
    labtraba #7;         || trabajadores
|FIN;

|VARIABLES;
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    seleccio = 0;

    &linea = 0;
    sw = 0;
    empresa = 0;
    ejercicio = 0;
    sw_calculo = 0;
    FEstado = 0;
    sw_repe = 0;
     &nEjer1 = 0;
     &nEjer2 = 0;
     nEmpresas = 0;
     aAlfa = "";
     fFinal = @;

     swSinPlanilla = 0;
     nCampo = 0;
     nNume = 0;
     aFijo = "";
|FIN;
____________________________________/ CALCULOS DE PANTALLA
|PROCESO PlanillasSN;
     aAlfa = #0#26;
     |PARA nCampo = 4; |SI nCampo [ 23; |HACIENDO nCampo = nCampo + 1;
          |CAMPO_MODIFICA #0nCampo, 1, aAlfa;
     |SIGUE;
|FPRC;

|PROCESO pin_desglo; |TIPO 7;
|SI #0Campo ! 0;
    |DDEFECTO #3;
    |ABRE #3;
        #3#0 = #0Campo;
    |LEE 000#3=;
    |CIERRA #3;

    |ATRI I;
    |PINTA 1938, #3#1;
    |ATRI 0;
|SINO;
    |PINTA 1938, "                                        ";
|FINSI;
|FPRC;

|PROCESO lee_desglo; |TIPO 0;
|SI #0Campo ! 0;
    |DDEFECTO #3;
    |ABRE #3;
        #3#0 = #0Campo;
    |LEE 000#3=;
    |SI FSalida ! 0;
        |MENAV "    Desglose inexistente ...";
        |ERROR;
    |SINO;
            elsw1 = 0;
        |PARA para1 = 4; |SI para1 [ 23; |HACIENDO para1 = para1 + 1;
            |SI para1 ! Campo;
                |SI #0para1 = #0Campo;  elsw1 = 1;     |FINSI;
            |FINSI;
        |SIGUE;
        |SI elsw1 = 1;
            |MENAV "    Codigo de Desglose repetido ...";
            |ERROR;
        |FINSI;
    |FINSI;
    |CIERRA #3;
|FINSI;
|FPRC;
____________________________________/
|PROCESO lee_tmp1;
     #1#0 = #15#0;
     #1#1 = #15#1 - 2000;
     |LEE 000#1=;
     |SI FSalida = 0;
          linea = 2;
          |PRINT;
     |FINSI;
|FPRC;

|DEFBUCLE lee_tmp;
     #15#1;
     ;
     empresa, ejercicio, 00;
     empresa, ejercicio, 99;
     ;
     NULL, lee_tmp1;
|FIN;

|PROCESO imprime_detalle;
     |DDEFECTO #7;          || trabajadores
     #7#0 = #12#0;
     #7#1 = #12#5;
     |LEE 000#7=;
     linea = 0;
     |PRINT;
|FPRC;

|PROCESO detalle;
|SI swSinPlanilla = 1;
     |HAZ imprime_detalle;
     |ACABA;
|FINSI;
     |PARA para1 = 4; |SI para1 [ 23; |HACIENDO para1 = para1 + 1;
          |SI #0para1 ! 0;
               #4#0 = #0para1;
               alfa1 = #12#4;     |QBF alfa1;
               alfa1 = ("000" + alfa1) % -103;
               #4#2 = alfa1;
               |LEE 000#4=;
               |SI FSalida = 0;                          || si existe desglose
                    |SI #12#3 ] #4#7;|Y #12#3 [ #4#8;       || grupos de tarifa
                         |SI #12#6 ] #4#5;|Y #12#6 [ #4#6;   || fechas de alta
                              |HAZ imprime_detalle;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |SINO;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO imprime;
     |SI sw = 0;
          sw = 1;
          empresa = #12#0;
          ejercicio = #12#1;
          |DDEFECTO #6;   #6#0 = #12#0;   |LEE 000#6=;   || empresas
          linea = 1;
          |PRINT;
     |FINSI;
     |SI empresa ! #12#0;
          |BUCLE lee_tmp;
          empresa = #12#0;
          ejercicio = #12#1;
          |DDEFECTO #6;   #6#0 = #12#0;   |LEE 000#6=;   || empresas
          linea = 1;
          || migue
          |PIEINF;
          |PRINT;
     |FINSI;

     |HAZ detalle;
|FPRC;

|DEFBUCLE imprime_detalle_Orden2;
     #12#2;
     ;
     INICIO;
     FINAL;
     ;
     NULL, imprime;
|FIN;

|DEFBUCLE imprime_detalle_Orden1;
     #12#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, imprime;
|FIN;

|PROCESO GrabaTrabTotales;
          |DDEFECTO #12;
          #12#0 = #2#0;
          #12#5 = #2#5;
          |LEE 101#12=;
          FEstado = FSalida;
          #12#1 = #0#2 + 2000;
          #12#2 = 00;         || la planilla de momento no la se, no
                              || es que me importe, pero por si algun
                              || dia tuviera que sacar los trabaadores por
                              || planilla, que para mi seria lo normal
          #12#3 = #2#3;       || grupo de tarifa
          #12#4 = #2#4;       || contrato
          #12#6 = #2#6;       || fecha de alta
          #12#7 = #2#7;       || fecha de baja

          |SI #0#2 > #0#3;
               nNume = #0#2;
          |SINO;
               nNume = #0#3;
          |FINSI;
          |SI #2#1 = nNume;
               #12#8 = #2#8;       || dias de alta total
               #12#21 = #2#15;     || Discapacitado (S/N)
          |FINSI;

          alfa1 = #2#11;
          |QBF alfa1;
          |SI alfa1 ! "";
               alfa1 = alfa1 - "proporcion ";
               alfa1 = alfa1 + " h.";
          |FINSI;
          #12#11 = alfa1;     || observaciones
          |SI #2#1 = #0#2;
               #12#9 = #2#9;            || dias alta ejercicio 1
               #12#10 = #2#10;          || % jornada ejerc 1
          |FINSI;
          #12#17 = #0#3 + 2000;
          |SI #2#1 = #0#3;
               #12#13 = #2#9;           || dias alta ejercicio 2
               #12#14 = #2#10;          || % jornada ejerc 2
          |FINSI;
          #12#15 = #12#13 - #12#9;      || diferencia dias ej2 - ej1
          #12#16 = #12#14 - #12#10;     || diferencia jornadas ej2 - ej1
||migue
          #6#0 = #12#0; |LEE 000#6=;
          #12#18 = #6#2;                || nombre empresa

          aFijo = #2#12; |QBF aFijo;
          #12#19 = aFijo;               || fijo (S/N)
          #12#20 = #2#14;               || sexo

          |SI FEstado ! 0;   |GRABA 020#12n; |FINSI;
          |SI FEstado = 0;   |GRABA 020#12a; |FINSI;
          |LIBERA #12;

          aFijo = #2#12; |QBF aFijo;

          |DDEFECTO #15;
          #15#0 = #2#0;          || empresa
          #15#1 = #0#2 + 2000;   || ejercicio 1
          #15#2 = #3#0;          || codigo desglose
          |LEE 101#15=;
          FEstado = FSalida;
          |SI swSinPlanilla = 1;
               #15#3 = "Distribuci¢n de trabajadores";
          |SINO;
               #15#3 = #3#1;          || descripcion
          |FINSI;
          |SI #2#1 = #0#2;
               #15#4 = #15#4 + #2#9;   || dias totales
               #15#5 = #15#5 + #2#10;  || jornadas totales
               aAlfa = #15#1;
               aAlfa = "31.12." + aAlfa;
               fFinal = aAlfa;
               |SI fFinal [ #2#7;                 || de alta el 31/12
                    |SI aFijo = "S";              || fijos
                         |SI #2#14 = "H";         || hombre
                              #15#11 = #15#11 + 1;
                         |FINSI;
                         |SI #2#14 = "M";         || mujer
                              #15#12 = #15#12 + 1;
                         |FINSI;
                    |FINSI;
                    |SI aFijo = "N";              || eventuales
                         |SI #2#14 = "H";         || hombre
                              #15#13 = #15#13 + 1;
                         |FINSI;
                         |SI #2#14 = "M";         || mujer
                              #15#14 = #15#14 + 1;
                         |FINSI;
                    |FINSI;
               |FINSI;
               |SI #2#15 = "S";
                    #15#15 = #15#15 + #2#10;
               |FINSI;
                                             || media total
               |SI aFijo = "S";              || fijos ejercicio 1
                    #15#21 = #15#21 + #2#10;
               |FINSI;
               |SI aFijo = "N";              || eventuales ejercicio 1
                    #15#22 = #15#22 + #2#10;
               |FINSI;
          |FINSI;

          #15#6 = #0#3 + 2000;          || ejercicio 2
          |SI #2#1 = #0#3;
               #15#7 = #15#7 + #2#9;   || dias totales
               #15#8 = #15#8 + #2#10;  || jornadas totales

               aAlfa = #15#6;
               aAlfa = "31.12." + aAlfa;
               fFinal = aAlfa;
               |SI fFinal [ #2#7;                 || de alta el 31/12
                    |SI aFijo = "S";              || fijos
                         |SI #2#14 = "H";         || hombre
                              #15#16 = #15#16 + 1;
                         |FINSI;
                         |SI #2#14 = "M";         || mujer
                              #15#17 = #15#17 + 1;
                         |FINSI;
                    |FINSI;
                    |SI aFijo = "N";              || eventuales
                         |SI #2#14 = "H";         || hombre
                              #15#18 = #15#18 + 1;
                         |FINSI;
                         |SI #2#14 = "M";         || mujer
                              #15#19 = #15#19 + 1;
                         |FINSI;
                    |FINSI;
               |FINSI;
               |SI #2#15 = "S";
                    #15#20 = #15#20 + #2#10;
               |FINSI;
                                             || media total
               |SI aFijo = "S";              || fijos ejercicio 1
                    #15#23 = #15#23 + #2#10;
               |FINSI;
               |SI aFijo = "N";              || eventuales ejercicio 1
                    #15#24 = #15#24 + #2#10;
               |FINSI;
          |FINSI;
          #15#9 = #15#7 - #15#4;        || diferencia dias
          #15#10 = #15#8 - #15#5;       || diferencia jornadas

          |SI FEstado ! 0;   |GRABA 020#15n; |FINSI;
          |SI FEstado = 0;   |GRABA 020#15a; |FINSI;
          |LIBERA #15;
          seleccio = 1;
|FPRC;

|PROCESO graba_tmp_comp;
     |SI swSinPlanilla = 1;
          #3#0 = 00;
          |HAZ GrabaTrabTotales;
          |ACABA;
     |FINSI;

          #3#0 = #0para1;
          |LEE 000#3=;                  || cabecera desglose
          |SI FSalida = 0;
               |HAZ GrabaTrabTotales;
          |FINSI;
|FPRC;

|PROCESO calcula;
     |SI swSinPlanilla = 1;
          |HAZ graba_tmp_comp;
          |ACABA;
     |FINSI;

     sw_repe = 0;
     |PARA para1 = 4; |SI para1 [ 23; |HACIENDO para1 = para1 + 1;
          |SI #0para1 ! 0;
               #4#0 = #0para1;
               alfa1 = #2#4;     |QBF alfa1;
               alfa1 = ("000" + alfa1) % -103;
               #4#2 = alfa1;
               |LEE 000#4=;
               |SI FSalida = 0;                          || si existe desglose
                    |SI #2#3 ] #4#7;|Y #2#3 [ #4#8;       || grupos de tarifa
                         |SI #2#6 ] #4#5;|Y #2#6 [ #4#6;   || fechas de alta
                              |SI sw_repe = 0;
                                   sw_repe = 1;
                                   |HAZ graba_tmp_comp;
                              |SINO;
                                   alfa1 = "    Contrato [" + #4#2 + "] repetido en desgloses ...";
                                   |MENAV alfa1;
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |SINO;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO lee_iae1;
     |SI sw_calculo = 0;
          |HAZ imprime;
     |SINO;
          |HAZ calcula;
     |FINSI;
|FPRC;

|DEFBUCLE lee_iae_per2;
     #2#1;
     ;
     #0#0, #0#3,    1;
     #0#1, #0#3, 9999;
     ;
     NULL, lee_iae1;
|FIN;

|DEFBUCLE lee_iae_per1;
     #2#1;
     ;
     #0#0, #0#2,    1;
     #0#1, #0#2, 9999;
     ;
     NULL, lee_iae1;
|FIN;

|PROCESO tipo3;
     |IMPRE 0;

     |SI FSalida = 0;
          alfa1 = Usuario; |QBF alfa1;
          |NOME_DAT #5 alfa1;
          |DELINDEX #5;
          alfa1 = Usuario; |QBF alfa1; alfa1 = "b" + alfa1;
          |NOME_DAT #12 alfa1;
          |DELINDEX #12;
          alfa1 = Usuario; |QBF alfa1; alfa1 = "c" + alfa1;
          |NOME_DAT #15 alfa1;
          |DELINDEX #15;
          |ABRE #1;
          |ABRE #3;
          |ABRE #4;
          |SELECT #2#4;
          |ABRE #12;
          |ABRE #15;
||migue
          |ABRE #6;
          sw_calculo = 1;

          || informe comparativo
          |BUCLE lee_iae_per1;               || calcula y graba temporal
          |SI #0#2 ! #0#3;
               |BUCLE lee_iae_per2;               || calcula y graba temporal
          |FINSI;

          ||SI aParam = "";
               |SI seleccio ! 0;
                    |INFOR "labinfco";
                    sw_calculo = 0;
                    |ABRE #7;
                    || migue
                    |SI #0#25 = 1;
                         |BUCLE imprime_detalle_Orden1;     || imprime detalle
                    |SINO;
                         |BUCLE imprime_detalle_Orden2;     || imprime detalle
                    |FINSI;
                    |CIERRA #7;
                    |BUCLE lee_tmp;
                    |PIEINF;
               |SINO;
                    |MENAV "    Seleccion vacia ...";
               |FINSI;
               |FININF;
          ||FINSI;

          |CIERRA #1;
          |CIERRA #3;
          |CIERRA #4;
          |CIERRA #6;
          |CIERRA #12;
          |CIERRA #15;
          |DELINDEX #12;
          |DELINDEX #15;
     |SINO;
          |MENAV "    Proceso abortado ...";
     |FINSI;
     |FINIMP;
|FPRC;

|PROGRAMA;
          |CLS;
          |CABEZA "Informe Desgloses";
          |PINPA #0#0;
          |DDEFECTO #0;
          |ABRE #0;
          |LEE 101#0p;
          |SI FSalida ! 0;    |GRABA 020#0b; |FINSI;
          |PINDA #0#0;
          |ENDATOS #1#0;
     |SI #0#26 = "S";
          swSinPlanilla = 0;
     |SINO;
          swSinPlanilla = 1;
     |FINSI;
          nEjer1 = #0#2 + 2000;
          nEjer2 = #0#3 + 2000;
          |SI FSalida = 0;
               |HAZ tipo3;
               |GRABA 020#0a;
          |FINSI;
          |LIBERA #0;
          |CIERRA #0;
|FPRO;
