|FICHEROS;
     labllxml #0;

     labllaca #1;
     labllali #2;
     labllenv,MANTE;
     /*
     labcer01 #1;
     labcer02 #2;
     labcer03 #3;
     labcer05 #5,MANTE;
     labcer06;
     */

     labempre;
     labtraba;
     labcontr;
     labcentr;
     labpadre;
|FIN;

|VARIABLES;
     aParam = "";
     nEmpresa = 0;
     nCampanya = 0;
     aMarca = "";
     aHora = "";
     aFecha = "";
     fFecha = @;
     aEmpresa = "";
     aCentro = "";

     aAlfa       = "";
     aAlfa1      = "";
     aAlfa2      = "";
     aApel1      = "";
     aApel2      = "";
     aNom        = "";
     aNume       = "";
     aLong       = "";
     aCampo      = "";
     nInd = 0;
     nLong = 0;
     nCalc = 0;
     nHayComa = 0;
     SwSalir = 0;

     nNivel = 0;
     aFichero = "";

     swHayMov = 1;
     nTrabajador = 0;
     aDesdeSel = "";
     aHastaSel = "";
     aRegimen = "";

     nNumTra = 0;
     nNumMov = 0;

     aFechaIniPer = "";
     aFechaFinPer = "";
     aInfComSit = "";
     aRepre = "";
     aNifRepre = "";

     &enEmpresa = 0;
     &enCentro = 0;
     &efFechaIni = @;
     &efFechaFin = @;
     &enCodLlama = 0;

     &aCadena = "";
     &eaNom   = "";
     &eaApel1 = "";
     &eaApel2 = "";
     &aCodPos = "";
     &nContMuni = 0;
     &aCodMuni = "";
     nPaisTrabaja = 0;
     &nOpcion = 0;
     nDesdeTra = 0;
     nHastaTra = 0;
     &eaVengoDe = "";
     aRutaBAT = "";
     nHandleBAT = 0;
     aUnidad = "";
     aResto = "";
     {1}aContiene = "";

     &eaCadena = "";
     &eaCadenaSal = "";
     &eaCaracter1 = "";
     &eaCaracter2 = "";
|FIN;

/****************************************************************************/
/***********   PROCESOS VARIOS PARA ENVIO A NETCONTRATA   *******************/
/****************************************************************************/

|VARIABLES;
     aRutaErrores = "";
     aOrigen = "";
     aDestino = "";
     nSwEtiq = 0;
     aEtiqu = "";
     aError = "";
     nNumChars = 0;
     aRutaSal = "";
     aLon = "";
     nLon = 0;
     nPunt = 0;
     nPos = 0;
     aCaracter = "";
     aRuta = "";
     aFich = "";
     nHandle = 0;
     aPath = "";
     eaFichero = "";
     &swSigue = 0;
|FIN;

|PROCESO CompRuta;
     aRutaSal = "";
     |QBF aRuta;
     aLon = aRuta % 0;
     nLon = aLon;
     |PARA nPunt = 1; |SI nPunt [ nLon; |HACIENDO nPunt = nPunt + 1;
          nPos = (nPunt * 100) + 1;
          aCaracter = aRuta % nPos;
          |SI aCaracter = "/";
               aRutaSal = aRutaSal + "\";
          |SINO;
               aRutaSal = aRutaSal + aCaracter;
          |FINSI;
     |SIGUE;
     aCaracter = aRutaSal % -101;
     |SI aCaracter ! "\";
          aRutaSal = aRutaSal + "\";
     |FINSI;
     aRuta = aRutaSal;

     eaCadena = aRuta;
     eaCaracter1 = "/";
     eaCaracter2 = "\";
     |HAZ sustituye_plb;
     aRuta = eaCadenaSal;
|FPRC;

|PROCESO EnvioNetContrata;
     || A PARTIR DE AQUI, ENVIO A NETCONTRATA

     |ABRE #labcontr; |LEE 000#labcontr.p; |CIERRA #labcontr;

     swSigue = 0;
     |SUB_EJECUTA "labnct00";
     |SI swSigue = 0;
          |ACABA;
     |FINSI;

     |SI swSigue = 2;
          aContiene = "";
          |ESPECIFICA "RBASS", aContiene;
          aAlfa = aContiene1;
          |QBF aAlfa;
          aAlfa = aAlfa + "conectass";
          #labcontr#63 = aAlfa;
     |FINSI;

     aRuta = #labcontr#51;
     |QBF aRuta;
     |HAZ CompRuta;

     aFich = aRuta + "Llamamientos\" + eaFichero;

     || ruta para errores
     aAlfa1 = #labcontr#63;
     |QBF aAlfa1;
     aAlfa = aAlfa1 % -101;
     |SI aAlfa ! "\"; |Y aAlfa ! "/";        || ruta de los XML
          aAlfa1 = aAlfa1 + "\";
     |FINSI;

     aAlfa = aAlfa1 + "ERRORES";
     aRutaErrores = aAlfa;

     aAlfa = aRutaErrores + "/ERRORESNET.XML";
     |RFBORRA aAlfa;

     |DBASE aOrigen; |QBF aOrigen;
     aOrigen = aOrigen + "ejecutables/rdn.exe";
     aDestino = #labcontr#63; |QBF aDestino; aDestino = aDestino + "rdn.exe";

     aAlfa = #labcontr#63; |QBF aAlfa;
     aRuta = aAlfa; |HAZ CompRuta; aAlfa = aRuta;
     aAlfa1 = #labcontr#62; |QBF aAlfa1;
     aAlfa2 = aAlfa + "ERRORES";
     aAlfa = aAlfa + "rdn.exe " + aAlfa1 + " " + aFich + " " + aRutaErrores;

     aAlfa = "start " + aAlfa;
     |RSYSTEM aAlfa;
|FPRC;

/****************************************************************************/

|PROCESO Tipo5; |TIPO 5;
     #labllenv#11 = "S";
     |LINEAS_BI_ATRI #labllenv#11, S, I, -1;
|FPRC;


|PROCESO Fichero;
     |SI #0#0 = 1; |O #0#0 = 3;
          |CAMPO_MODIFICA #0#1, 1, "N";
          |CAMPO_VISUAL #0#1, 1, "N";
          |PINTA 2012, "                                                         ";
     |FINSI;
     |SI #0#0 = 2; |O #0#0 = 3;
          aEmpresa = enEmpresa;
          aEmpresa = ("00000" + aEmpresa) % -105;

          |HORA aHora;
          fFecha = ~;
          aFecha = fFecha;

          aHora = aHora - ":" - ":";
          aFecha = aFecha - "." - ".";
          aFichero = aEmpresa + "_" + aFecha + "_" + aHora + "_lla.xml"

          |SI #0#0 = 2;
               |ATRI I;
               |PINTA 2112, " Nombre de Fichero:                                      ";
               |ATRI 0;
               |CAMPO_MODIFICA #0#1, 1, "S";
               |CAMPO_VISUAL #0#1, 1, "S";
               #0#1 = aFichero;
               |PINTA #0#1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO LeeEmpresa;
     |ABRE #labempre;
     #labempre#0 = enEmpresa;
     |LEE 000#labempre.=;
     |CIERRA #labempre;
|FPRC;

|PROCESO LeeTrab;
     |ABRE #labtraba;
     #labtraba#0 = nEmpresa;
     #labtraba#1 = nTrabajador;
     |LEE 000#labtraba.=;
     |CIERRA #labtraba;
|FPRC;

|PROCESO Linea;
     |QBF aAlfa;
     aCadena = aAlfa;
     |HAZ QuitaRaros;
     |SI nNivel = 1;
          aCadena = aCadena;
     |FINSI;
     |SI nNivel = 2;
          || aCadena = &09 + aCadena;
          aCadena = "  " + aCadena;
     |FINSI;
     |SI nNivel = 3;
          aCadena = "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 4;
          aCadena = "  " + "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 5;
          aCadena = "  " + "  " + "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 6;
          aCadena = "  " + "  " + "  " + "  " + "  " + aCadena;
     |FINSI;
     aCadena = aCadena + &13 + &10;
     aAlfa = aCadena;
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO DatosLlamamiento;
     nNivel = 3;
     aAlfa = "<DATOS_LLAMAMIENTO>";
     |HAZ Linea;

     aAlfa = #labllali#8;
     |QBF aAlfa;
     aAlfa = (aAlfa % -104) + (aAlfa % 402) + (aAlfa % 102);
     nNivel = 4;
     aAlfa = "<FECHA_INICIO>" + aAlfa + "</FECHA_INICIO>";
     |HAZ Linea;

     aAlfa = #labllali#9;
     |QBF aAlfa;
     |SI aAlfa ! ""; |Y aAlfa ! ".."; |Y aAlfa ! "01.01.0000";
          aAlfa = (aAlfa % -104) + (aAlfa % 402) + (aAlfa % 102);
          nNivel = 4;
          aAlfa = "<FECHA_FIN>" + aAlfa + "</FECHA_FIN>";
          |HAZ Linea;
     |FINSI;

     aAlfa = #labllali#10;
     |QBF aAlfa;
     |SI aAlfa ! "";
          nNivel = 4;
          /*
          |SI #labtraba#45 = "189"; |O #labtraba#45 = "289"; |O #labtraba#45 = "389";
               aAlfa1 = "55";
          |SINO;
               aAlfa1 = "00";
          |FINSI;
          aAlfa = (aAlfa % 114) + aAlfa1;
          */
          aAlfa = (aAlfa % 116);
          aAlfa = "<CLAVE_CONTRATO_TRANS>" + aAlfa + "</CLAVE_CONTRATO_TRANS>";
          |HAZ Linea;
     |FINSI;

     aAlfa = #labtraba#73;
     |QBF aAlfa;
     aAlfa = ("00" + aAlfa) % -102;
     nNivel = 4;
     aAlfa = "<NIVEL_FORMATIVO>" + aAlfa + "</NIVEL_FORMATIVO>";
     |HAZ Linea;

     nNivel = 3;
     aAlfa = "</DATOS_LLAMAMIENTO>";
     |HAZ Linea;
|FPRC;

|PROCESO DatosTrabajador;
     nNivel = 3;
     aAlfa = "<DATOS_TRABAJADOR>";
     |HAZ Linea;

     aAlfa = #labtraba#7;
     |QBF aAlfa;
     aAlfa1 = aAlfa % 101;
     |SI aAlfa1 ] "0";|Y aAlfa1 [ "9";
          aAlfa1 = "D";            || tipo identificacion DNI
     |SINO;
          |SI aAlfa1 = "X"; |O aAlfa1 = "Y"; |O aAlfa1 = "Z";
               aAlfa1 = "E";        || tipo identificacion EXTRANJERO
          |SINO;
               |SI aAlfa1 ] "a";|Y aAlfa1 [ "z";
                    aAlfa1 = "U";    || tipo identificacion OTROS
               |FINSI;
          |FINSI;
     |FINSI;

     nNivel = 4;
     aAlfa = aAlfa1 + aAlfa;
     aAlfa = "<IDENTIFICADORPFISICA>" + aAlfa + "</IDENTIFICADORPFISICA>";
     |HAZ Linea;

     nNivel = 4;
     aAlfa = "<NOMBRE_APELLIDOS>";
     |HAZ Linea;

     aCadena = #labtraba#2;
     |HAZ SeparaNom_plb;

     nNivel = 5;
     eaNom = eaNom % 115;
     aAlfa = "<NOMBRE>" + eaNom + "</NOMBRE>";
     |HAZ Linea;

     nNivel = 5;
     eaApel1 = eaApel1 % 120;
     aAlfa = "<PRIMER_APELLIDO>" + eaApel1 + "</PRIMER_APELLIDO>";
     |HAZ Linea;

     nNivel = 5;
     eaApel2 = eaApel2 % 120;
     aAlfa = "<SEGUNDO_APELLIDO>" + eaApel2 + "</SEGUNDO_APELLIDO>";
     |HAZ Linea;

     nNivel = 4;
     aAlfa = "</NOMBRE_APELLIDOS>";
     |HAZ Linea;

     aAlfa = "";
     #labpadre#0 = #labtraba#0;
     #labpadre#1 = #labtraba#1;
     |LEE 000#labpadre.=;
     |SI FSalida = 0;
          aAlfa = #labpadre#5;
          |QBF aAlfa;
     |FINSI;
     |SI aAlfa = "M";
          aAlfa = "2";
     |SINO;
          aAlfa = "1";
     |FINSI;
     aAlfa = "<SEXO>" + aAlfa + "</SEXO>";
     |HAZ Linea;

     nNivel = 4;
     aAlfa = #labtraba#11;
     aAlfa = (aAlfa % -104) + (aAlfa % 402) + (aAlfa % 102);
     aAlfa = "<FECHA_NACIMIENTO>" + aAlfa + "</FECHA_NACIMIENTO>";
     |HAZ Linea;

     nNivel = 4;
     aAlfa = #labtraba#76; aAlfa = aAlfa % 0101;
     |SI aAlfa ! "1"; |Y aAlfa ! "2"; |Y aAlfa ! "3"; |Y aAlfa ! "4"; |Y aAlfa ! "5";
     |Y aAlfa ! "6"; |Y aAlfa ! "7"; |Y aAlfa ! "8"; |Y aAlfa ! "9"; |Y aAlfa ! "0";
          nPaisTrabaja = 724;
     |SINO;
          aAlfa = #labtraba#76; aAlfa = aAlfa % 0103;
          nPaisTrabaja = aAlfa;
     |FINSI;
     aAlfa = nPaisTrabaja;
     |QBF aAlfa;
     aAlfa = ("000" + aAlfa) % -103;
     aAlfa = "<NACIONALIDAD>" + aAlfa + "</NACIONALIDAD>";
     |HAZ Linea;

     aAlfa1 = #labtraba#8; |QBF aAlfa1;
     aAlfa1 = ("00" + aAlfa1) % -102;
     aAlfa2 = #labtraba#9; |QBF aAlfa2;
     aAlfa2 = ("000" + aAlfa2) % -103;
     aCodPos = aAlfa1 + aAlfa2;

     |HAZ CodigoMuni_plb;

     nNivel = 4;
     aAlfa = "<MUNICIPIO_RESIDENCIA>" + aCodMuni + "</MUNICIPIO_RESIDENCIA>";
     |HAZ Linea;

     nNivel = 4;
     aAlfa = "<PAIS_RESIDENCIA>" + "724" + "</PAIS_RESIDENCIA>";
     |HAZ Linea;

     nNivel = 3;
     aAlfa = "</DATOS_TRABAJADOR>";
     |HAZ Linea;
|FPRC;

|PROCESO Empresa;
     nNivel = 3;
     aAlfa = "<DATOS_EMPRESA>";
     |HAZ Linea;

     nNivel = 4;
     aAlfa = "<CIF_NIF_EMPRESA>";
     |HAZ Linea;

     nNivel = 5;
     aAlfa = #labempre#10;
     |QBF aAlfa;
     aAlfa = "<CIF_NIF>" + aAlfa + "</CIF_NIF>";
     |HAZ Linea;

     nNivel = 4;
     aAlfa = "</CIF_NIF_EMPRESA>";
     |HAZ Linea;

     nNivel = 4;
     aRegimen = #labtraba#247;
     |QBF aRegimen;
     aRegimen = ("0000" + aRegimen) % -104;

     #labcentr#0 = #labtraba#0;
     #labcentr#1 = #labtraba#77;
     |LEE 000#labcentr.=;
     aAlfa = #labcentr#10;

     |QBF aAlfa;
     aAlfa = aRegimen + aAlfa;
     aAlfa = "<CCC>" + aAlfa + "</CCC>";
     |HAZ Linea;

     nNivel = 3;
     aAlfa = "</DATOS_EMPRESA>";
     |HAZ Linea;
|FPRC;

|PROCESO Trabajadores;
     |SI nOpcion = 2;
          |SI #0#4 = "N"; |Y #labllali#11 = "S";
               |ACABA;
          |FINSI;
          |SI #0#4 = "S"; |Y #labllali#12 ! "*";
               |ACABA;
          |FINSI;
     |FINSI;

     nNumTra = nNumTra + 1;

     nNivel = 2;
     aAlfa = "<LLAMAMIENTO_TIPO>";
     |HAZ Linea;

     #labtraba#0 = #labllali#0;    || LEO TRABAJADOR
     #labtraba#1 = #labllali#5;
     |LEE 000#labtraba.=;

     |HAZ Empresa;

     |HAZ DatosTrabajador;

     |HAZ DatosLlamamiento;

     nNivel = 2;
     aAlfa = "</LLAMAMIENTO_TIPO>";
     |HAZ Linea;

     |SI nOpcion = 2; |O nOpcion = 3;
          #labllali#11 = "S";
          #labllali#12 = " ";
          |GRABA 020#labllali.a;
          |LIBERA #labllali;
     |FINSI;
|FPRC;

|DEFBUCLE Trabajadores;
     #2#1;
     ;
     enEmpresa, enCentro, efFechaIni, efFechaFin, enCodLlama, nDesdeTra;
     enEmpresa, enCentro, efFechaIni, efFechaFin, enCodLlama, nHastaTra;
     ;
     NULL, Trabajadores;
|FIN;

|PROCESO ElProceso;
     |ABRE #1;
     |ABRE #2;
     |ABRE #labtraba;
     |ABRE #labcentr;
     |ABRE #labpadre;

     |ABRE #labcontr;
     |LEE 000#labcontr.p;
     |CIERRA #labcontr;
     |HAZ LeeEmpresa;

     aRuta = #labcontr#51;
     |QBF aRuta;
     aAlfa = aRuta % -101;
     |SI aAlfa ! "\"; |Y aAlfa ! "/";
          aRuta = aRuta + "/";
     |FINSI;
     aRuta = aRuta + "Llamamientos/";

     |RMKDIR aRuta;

     aRuta = aRuta + aFichero;
     |XABRE "CBW", aRuta, nHandle;
     |SI FSalida ] 0;
          nNivel = 1;
          aAlfa = "<?xml version=" + &34 + "1.0" + &34 + " encoding=" + &34 + "ISO-8859-1" + &34 + " ?>";
          |HAZ Linea;

          nNivel = 1;
          aAlfa = "<LLAMAMIENTOS>";
          |HAZ Linea;

          || DATOS TRABAJADOR

          |SI nOpcion = 3;
               nDesdeTra = #labllenv#5;
               nHastaTra = #labllenv#5;
          |SINO;
               nDesdeTra = 00001;
               nHastaTra = 99999;
          |FINSI;

          |BUCLE Trabajadores;

          nNivel = 1;
          aAlfa = "</LLAMAMIENTOS>";
          |HAZ Linea;
     |FINSI;
     |XCIERRA nHandle;

     |SI nOpcion = 2;
          |SI nNumTra ! 0;
               aRuta = aRuta - aFichero;
               aAlfa = "    El fichero se ha generado correctamente en " + aRuta;
               |MENAV aAlfa;

               aAlfa1 = nNumTra;
               aAlfa2 = nNumMov;

               aAlfa = "    Se incluyen un total de " + aAlfa1 + " llamamientos ";
               aAlfa = aAlfa + "de trabajadores.";
               |MENAV aAlfa;
          |SINO;
               aAlfa = "    No se han encontrado trabajadores para enviar.";
               |MENAV aAlfa;
          |FINSI;
     |FINSI;
     |CIERRA #labpadre;
     |CIERRA #labcentr;
     |CIERRA #labtraba;
     |CIERRA #2;
     |CIERRA #1;
|FPRC;

|PROCESO LimpiaSeleccion;
     #labllali#12 = " ";
     |GRABA 020#labllali.a;
     |LIBERA #labllali;
|FPRC;

|DEFBUCLE LimpiaSeleccion;
     #2#1;
     ;
     enEmpresa, enCentro, efFechaIni, efFechaFin, enCodLlama, 00001;
     enEmpresa, enCentro, efFechaIni, efFechaFin, enCodLlama, 99999;
     ;
     NULL, LimpiaSeleccion;
|FIN;

|PROCESO FuncOpc2;
     |SI FSalida = 13;
          |SI #labllenv#12 = "*";
               #labllenv#12 = " ";
               |PINTA #labllenv#12;
          |SINO;
               #labllenv#12 = "*";
               |PINTA #labllenv#12;
          |FINSI;
          |GRABA 020#labllenv.a;
          |LIBERA #labllenv;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO FuncOpc3;
     |SI FSalida = 13;
          |SI #labllenv#11 = "N";
               |HAZ ElProceso;
               eaFichero = aFichero;
               |HAZ EnvioNetContrata;
               #labllenv#11 = "S";
               |PINTA #labllenv#11;
               |GRABA 020#labllenv.a;
               |ERROR;
          |SINO;
               aAlfa = "    ATENCION. El llamamiento de este trabajador ya ha sido ";
               aAlfa = aAlfa + "marcado como enviado";
               |MENAV aAlfa;
               aAlfa = "    S¿Desea volver a enviarlo?";
               |CONFI aAlfa;
               |SI FSalida = 0;
                    |HAZ ElProceso;
                    eaFichero = aFichero;
                    |HAZ EnvioNetContrata;
                    #labllenv#11 = "S";
                    |PINTA #labllenv#11;
                    |GRABA 020#labllenv.a;
                    |ERROR;
               |FINSI;
          |FINSI;
     |FINSI;
     |SI FSalida = 14;
          |SI #labllenv#11 = "N";
               #labllenv#11 = "S";
               |PINTA #labllenv#11;
          |SINO;
               #labllenv#11 = "N";
               |PINTA #labllenv#11;
          |FINSI;
          |GRABA 020#labllenv.a;
          |LIBERA #labllenv;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Funciones; |TIPO 11;
     |SI #0#0 = 2;
          |HAZ FuncOpc2;
     |FINSI;
     |SI #0#0 = 3;
          |HAZ FuncOpc3;
     |FINSI;
|FPRC;

|PROCESO Mensajes; |TIPO 7;
     |SI #0#0 = 2;
          |MENSA "    Pulse < F5 > para marcar o desmarcar el trabajador para su envio";
     |FINSI;
     |SI #0#0 = 3;
          |MENSA "    Funciones: < F5 > Enviar Online, < F6 > Marcar como enviado o no enviado";
     |FINSI;
|FPRC;

|PROCESO inf_env;
     #labllenv#0 = enEmpresa;
     #labllenv#1 = enCentro;
     #labllenv#2 = efFechaIni;
     #labllenv#3 = efFechaFin;
     #labllenv#4 = enCodLlama;
     #labllenv#5 = 00001;
|FPRC;

|PROCESO sup_env;
     #labllenv#0 = enEmpresa;
     #labllenv#1 = enCentro;
     #labllenv#2 = efFechaIni;
     #labllenv#3 = efFechaFin;
     #labllenv#4 = enCodLlama;
     #labllenv#5 = 99999;
|FPRC;

|PROCESO Activa4;
     |SI #0#0 = 2;
          |CAMPO_MODIFICA #0#4, 1, "S";
          |CAMPO_VISUAL #0#4, 1, "S";
          |PINTA 2013, "Enviar Todos, solo los No enviados o Seleccionar ...:   ";
          |ATRI I;
          |PINTA 2020, "T";
          |PINTA 2036, "N";
          |PINTA 2050, "S";
          |ATRI 0;
          |PINTA #0#4;
     |SINO;
          |CAMPO_MODIFICA #0#4, 1, "N";
          |CAMPO_VISUAL #0#4, 1, "N";
          |PINTA 2013, "                                                        ";
     |FINSI;
|FPRC;

|PROGRAMA;
     aEmpresa = enEmpresa;
     aEmpresa = ("00000" + aEmpresa) % -105;
     #0#2 = aEmpresa;

     aCentro = enCentro;
     aCentro = ("00" + aCentro) % -102;
     #0#3 = aCentro;

     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          nOpcion = #0#0;
          |SI nOpcion = 1;         || en PDF, como siempre
               |ACABA;
          |FINSI;
          |SI nOpcion = 2;         || genero fichero xml
               swSigue = 1;
               |SI #0#4 = "S";
                    |ENTLINEAL #1#labllenv, -2, 4, 22, 1, inf_env, sup_env;
                    aAlfa = "    S¿Desea continuar con la generación del fichero xml ";
                    aAlfa = aAlfa + "con los trabajadores seleccionados?";
                    |CONFI aAlfa;
                    |SI FSalida ! 0;
                         swSigue = 0;
                         |BUCLE LimpiaSeleccion;
                    |FINSI;
               |FINSI;
               |SI swSigue = 1;
                    |HAZ ElProceso;
               |FINSI;
          |FINSI;
          |SI nOpcion = 3;         || genero fichero xml y envia a Netcontrata
               |ENTLINEAL #1#labllenv, -2, 4, 22, 1, inf_env, sup_env;
               /*
               || esto sera cuando se pueda enviar mas de uno
               |HAZ ElProceso;
               eaFichero = aFichero;
               |HAZ EnvioNetContrata;
               */
          |FINSI;
     |FINSI;
|FPRO;
