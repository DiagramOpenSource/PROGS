|FICHEROS;
     labta611 #0;
     labempre #1;   || empresas
     labtraba #2;   || trabajadores
     labregis #3;   || registro contratos
     labcentr #4;   || centros
     nomcenes #5;   || centros especiales
     labpadre #6;   || datos auxiliares (nombre padre y madre)
     nomconca #7;   || convenios cabs.
     nomir006 #8;   || datos IRPF
     nomir003 #9;   || datos IRPF empresa
     nomregim #10;  || descripcion regimenes Seg. Social
     gomexplo;      || datos explotacion agraria
     gomdetjr;      || detalle de dias jornadas realizadas

     silcon06 #20;  || gestion empresas AGIRED
     silcon13 #21;  || movimientos AFI AGIRED

     nomequiv #101; || tabla equivalencias contratos nuevos
     nomocupa;
|FIN;

|VARIABLES;
     alfa1 = "";
     alfa2 = "";
     alfa3 = "";
     alfa4 = "";
     nume1 = 0;
     nume2 = 0;
     nume3 = 0;
     nume4 = 0;
     para1 = 0;
     para2 = 0;
     para3 = 0;
     para4 = 0;
     fech1 = @;
     fech2 = @;
     fech3 = @;
     fech4 = @;
     elsw1 = 0;
     elsw2 = 0;
     elsw3 = 0;
     elsw4 = 0;
     FEstado = 0;
     seleccio = 0;
     modo = 0;
     sw_rup = 0;

     dia1   = "";
     mes1   = "";
     any1   = "";
     fec    = "";
     sw     = 0;
     dia    = 0;
     mes    = 0;
     any    = 0;
     tope   = 0;
     mod    = 0;

     &nombre = "";
     &apell1 = "";
     &apell2 = "";
     &ctacot = "";
     &codcon = "";
     &dianac = "";
     &mesnac = "";
     &anynac = "";
     &gradom = "";
     &tipodc = "";
     &diapar = "";
     &mespar = "";
     &anypar = "";
     &diasuc = "";
     &messuc = "";
     &anysuc = "";

     informe = "";
     impre = "";
     confi_ant = "S";
     red_ultimo = 0;
     red_baja = 0;
     red_esta = 0;
     horas_a = 0;
     horas_c = 0;
     minut_a = 0;
     minut_c = 0;
     aContrato = "";
     grado2 = 0;

     aCoefper = ""
     nPartentera = 0;
     nPartedecimal = 0;
     nDiastrab = 0;
     nDiasIT = 0;

     &aDia1 = "";
     &aMes1 = "";
     &aAny1 = "";
     &aDia2 = "";
     &aMes2 = "";
     &aAny2 = "";
     &aDoc1 = "";
     &aDoc2 = "";
     &aDoc3 = "";
     &aDatosExp = "";
     aCalendario = "";
     nCampo = 0;
     aJornada = "";
     aParam = "";
     aAlfa = "";
|FIN;
____________________________________/ CALCULOS DESDE CAMPOS
|PROCESO Pantalla; |TIPO 7;
     |SI aParam = "AUTO";      |ACABA;      |FINSI;
     |CAMPO_VISUAL #0#22, 1, "N";
     |CAMPO_MODIFICA #0#22, 1, "N";
     |PINTA 0661, "     ";
     |CAMPO_MODIFICA #0#100, 1, "N";
     |CAMPO_VISUAL #0#100, 1, "N";
     |CAMPO_MODIFICA #0#101, 1, "N";
     |CAMPO_VISUAL #0#101, 1, "N";
     |CAMPO_MODIFICA #0#102, 1, "N";
     |CAMPO_VISUAL #0#102, 1, "N";
     |CAMPO_MODIFICA #0#103, 1, "N";
     |CAMPO_VISUAL #0#103, 1, "N";
     |CAMPO_MODIFICA #0#104, 1, "N";
     |CAMPO_VISUAL #0#104, 1, "N";
     #0#22 = 00;
     #0#100 = "  ";
     #0#101 = 0;
     #0#102 = 0;
     #0#103 = " ";
     #0#104 = " ";
     |PINTA 0743, "                                     ";
|FPRC;

|PROCESO coeficiente; |TIPO 0;
     |SI #0#7 = "P";
          nume1 = (#0#9 + (#0#64/10)) / #0#10;
          nume1 = nume1 ! 3;
          #0#14 = nume1 * 1000;
          |SI #0#14 > 1000;
               #0#14 = 0;
          |FINSI;
          |PINTA #0#14;
     |FINSI;
|FPRC;

|PROCESO comp_reg;
     |SI #2#42 ! "A"; |Y #2#42 ! "B"; |Y #2#42 ! "E"; |Y #2#42 ! "e";
          |MENAV "    El modelo TA.0611 solo admite trabajadores del Regimen Agrario";
          |POSICIONATE #0#0;
     |FINSI;
|FPRC;

|PROCESO contrato; |TIPO 0;
/*
     |SI #0#7 = "P";
          alfa1 = "S";
     |SINO;
          alfa1 = "N";
          |PDEFECTO #0, 8, 11;
          |PDEFECTO #0, 14, 15;
          |PINTA #0#8;
          |PINTA #0#9;
          |PINTA #0#10;
          |PINTA #0#14;
     |FINSI;
     |C_M #0#8, 1, alfa1;
     |C_M #0#9, 1, alfa1;
     |C_M #0#10, 1, alfa1;
     |C_M #0#14, 1, alfa1;
*/
|FPRC;

|PROCESO mayus; |TIPO 0;
     #0Campo = #0Campo > #0Campo;
     |PINTA #0Campo;
|FPRC;

|PROCESO defecto; |TIPO 7;
     |SI #0#8 = " ";|Y #0#7 = "P";
          #0#8 = "S";    |PINTA #0#8;
          #0#9 = 25;     |PINTA #0#9;
          #0#10 = 40;    |PINTA #0#10;
     |FINSI;
|FPRC;

/*
|PROCESO AyudaCond; |TIPO 7;
     |ATRI R;
     |PUSHV 09022337;
     |BLANCO 09022337;
     ||                    1         2         3
     ||            234567890123456789012345678901234567
     |PINTA 0902, "1.Desemp. inscrito en Oficina Empleo";
     |PINTA 1002, "2.Idem. durante mas de 12 meses     ";
     |PINTA 1102, "3.Desemp. subsidio Regimen Agrario  ";
     |PINTA 1202, "4.Beneficiario prestacion + de 1 a¤o";
     |PINTA 1302, "5.Desemp. inscrito Ofi. Emp. +6 mes.";
     |PINTA 1402, "6.Benef. Desempleo al que le falta  ";
     |PINTA 1502, "  mas de 1 a¤o por percibir prestac.";
     |PINTA 1602, "7.Benef. Subsidio Desemp. > 52 a¤os ";
     |PINTA 1702, "8.Desemp.excedente Textil al que le ";
     |PINTA 1802, "  falta mas de 1 a¤o de prestacion  ";
     |PINTA 1902, "U.Desemp. +6 meses excedente textil ";
     |PINTA 2002, "T.Desempleado excedente sect. textil";
     |PINTA 2102, "9.No inscrito en Oficina de Empleo  ";
     |PINTA 2202, "A.Beneficiario prestación desempleo ";
     |PINTA 2302, "M.Desempleado contrato CTP<333      ";
     |ATRI 0;
|FPRC;
*/

|PROCESO AyudaExc; |TIPO 7;
     |PUSHV 09401280;
     |ATRI R;
               ||         5         6         7         8
               ||  34567890123456789012345678901234567890
     |PINTA 0943, "   Opciones: Exclusion de Desempleo   ";
     |PINTA 1043, "1. Familiar del titular de explotacion";
     |PINTA 1143, "2. Extranjero con permiso de trabajo  ";
|FPRC;

|PROCESO sistema; |TIPO 7;
     ||SI FSalida = 0;
          |ABRE #2;
          #2#0 = #0#0;
          #2#1 = #0#1;
          |LEE 020#2=;
          |CIERRA #2;
          fech1 = ~;
          |SI #0#2 = "A"; #0#16 = #2#36; |FINSI;
          |SI #0#2 = "B"; #0#16 = #2#37; |FINSI;
          |SI #0#2 = "V"; #0#16 = fech1; |FINSI;
          |PINTA #0#16;
     ||FINSI;
|FPRC;

|PROCESO QueEs;
     |SI aParam = "AUTO";      |ACABA;      |FINSI;
     |ABRE #20;
     |LEE 000#20p;
     |CIERRA #20;

     ||CAMPO_MODIFICA #0#74, 1, "N";
     ||CAMPO_VISUAL #0#74, 1, "N";
     ||PINTA 1060, "                    ³";
     |CAMPO_MODIFICA #0#100, 1, "N";
     |CAMPO_VISUAL #0#100, 1, "N";
     |CAMPO_MODIFICA #0#101, 1, "N";
     |CAMPO_VISUAL #0#101, 1, "N";
     |CAMPO_MODIFICA #0#102, 1, "N";
     |CAMPO_VISUAL #0#102, 1, "N";
     |CAMPO_MODIFICA #0#103, 1, "N";
     |CAMPO_VISUAL #0#103, 1, "N";
     |CAMPO_MODIFICA #0#104, 1, "N";
     |CAMPO_VISUAL #0#104, 1, "N";
     |PINTA 0743, "                                     ";

     |SI #0#2 = "B";
          |CAMPO_VISUAL #0#22, 1, "S";
          |CAMPO_MODIFICA #0#22, 1, "S";
/*

          |CAMPO_MODIFICA #0#74, 1, "S";
          |CAMPO_VISUAL #0#74, 1, "S";
          |PINTA 1060, "Fecha Vac:";
          |PINTA #0#74;
          aAlfa = #labtraba#184;
          |QBF aAlfa;
          #0#22 = aAlfa % 102;
          |PINTA #0#22;
*/
     |FINSI;

     |SI #0#2 = "V";
          |CAMPO_POSICION #0#100, 1, 0662;
          |CAMPO_MODIFICA #0#100, 1, "S";
          |CAMPO_VISUAL #0#100, 1, "S";
          |PINTA #0#100;
     |FINSI;
|FPRC;

|PROCESO AyudaVaria; |TIPO 7;
     |SI #0#2 = "V";
          |PUSHV 09372380;
          |ATRI R;
                     || 34         5         6         7         8
                     || 901234567890123456789012345678901234567890
          |PINTA 1039, " Elija tipo de variacion:                ";
          |PINTA 1139, " MG - Cambio de grupo de cotizacion      ";
          |PINTA 1239, " MC - Cambio de contrato                 ";
          |PINTA 1339, " MT - Cambio de ocupacion                ";
          |ATRI 0;
     |FINSI;
|FPRC;

|PROCESO TipoVaria;
     |SI #0#2 = "V";
                     || 4         5         6         7
                     || 3456789012345678901234567890123456789
          |PINTA 0743, "                                     ";
          |SI #0#100 = "MG";
               |PINTA 0743, "Cambio Gr.Cotiz:     -->";
               |SI aParam ! "AUTO";
                    #0#101 = #labtraba#33;
               |FINSI;
          |FINSI;
          |SI #0#100 = "MC";
               |PINTA 0743, "Cambio contrato:     -->";
               |SI aParam ! "AUTO";
                    #0#101 = #labtraba#45;
               |FINSI;
          |FINSI;
          |SI #0#100 = "MT";
               |PINTA 0743, "Cambio ocupacion:    -->";
               |SI aParam ! "AUTO";
                    #0#103 = #labtraba#204;
               |FINSI;
          |FINSI;
          |SI #0#100 = "MG"; |O #0#100 = "MC";
               |CAMPO_MODIFICA #0#103, 1, "N";
               |CAMPO_VISUAL #0#104, 1, "N";
               |CAMPO_MODIFICA #0#103, 1, "N";
               |CAMPO_VISUAL #0#104, 1, "N";

               |CAMPO_MODIFICA #0#101, 1, "N";
               |CAMPO_VISUAL #0#101, 1, "S";
               |PINTA #0#101;
               |SI aParam ! "AUTO";
                     |CAMPO_MODIFICA #0#102, 1, "S";
               |FINSI;
               |CAMPO_VISUAL #0#102, 1, "S";
               |PINTA #0#102;
          |SINO;
               |CAMPO_MODIFICA #0#101, 1, "N";
               |CAMPO_VISUAL #0#101, 1, "N";
               |CAMPO_MODIFICA #0#102, 1, "N";
               |CAMPO_VISUAL #0#102, 1, "N";

               |CAMPO_MODIFICA #0#103, 1, "N";
               |CAMPO_VISUAL #0#103, 1, "S";
               |SI aParam ! "AUTO";
                     |CAMPO_MODIFICA #0#104, 1, "S";
               |FINSI;
               |CAMPO_VISUAL #0#104, 1, "S";

               |CAMPO_POSICION #0#103, 1, 0760;
               |CAMPO_POSICION #0#104, 1, 0768;

               |PINTA #0#103;
               |PINTA #0#104;
          |FINSI;
     |SINO;
          |PINTA 0743, "                                     ";
          |CAMPO_MODIFICA #0#100, 1, "N";
          |CAMPO_VISUAL #0#100, 1, "N";
          |CAMPO_MODIFICA #0#101, 1, "N";
          |CAMPO_VISUAL #0#101, 1, "N";
     |FINSI;
|FPRC;

|PROCESO MensaVaria; |TIPO 7;
     |SI #0#100 = "MG";
          |MENSA "    Introduzca nuevo grupo de cotizacion del trabajador";
     |FINSI;
     |SI #0#100 = "MC";
          |MENSA "    Introduzca nuevo tipo de contrato (para cambio jornada rellene campos izda.)";
     |FINSI;
     |SI #0#100 = "MT";
          |MENSA "    Introduzca nuevo epigrafe de A.T. del trabajador";
     |FINSI;
|FPRC;

|PROCESO verifech; |TIPO 0;
     fec = #0Campo;
     sw  = 0;

     |SI fec = "          "; |ACABA; |FINSI;
     |SI fec = ".........."; |ACABA; |FINSI;

     dia1 = fec % 102;
     mes1 = fec % 402;
     any1 = fec % 704;

     dia  = dia1;
     mes  = mes1;
     any  = any1;

     |SI dia ! 0; |Y mes ! 0; |Y any ! 0;
          |SI mes [ 12; |Y mes ] 1;
              |SI mes=1;|O mes=3;|O mes=5;|O mes=7;|O mes=8;|O mes=10;|O mes=12;
                   tope = 31;
              |SINO;
                   |SI mes = 2;
                        mod = any $ 4;
                        |SI mod = 0; tope = 29; |SINO; tope = 28; |FINSI;
                   |SINO;
                        tope = 30;
                   |FINSI;
              |FINSI;
              |SI dia [ tope; sw = 1; |FINSI;
          |FINSI;
     |FINSI;

     |SI sw = 0;
          |MENAV "0000ATENCION !!! Fecha erronea ...";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO no_grupo; |TIPO 7;
     |SI #0#2 = "V";
          |CAMPO_MODIFICA #0#23, 1, "S";
     |SINO;
          |CAMPO_MODIFICA #0#23, 1, "N";
     |FINSI;
|FPRC;

|PROCESO si_grupo; |TIPO 0;
     |SI #0#2 = "V";|Y FSalida = 0;
          |ABRE #2;
          #2#0 = #0#0;
          #2#1 = #0#1;
          |LEE 121#2=;
          |SI FSalida = 0;|Y #2#33 ! #0#23;
               #2#33 = #0#23;           || actualiza el grupo de tarifa
               |GRABA 020#2a;
          |FINSI;
          |LIBERA #2;
          |CIERRA #2;
     |FINSI;
|FPRC;

|PROCESO si_cambio;
     |SI #0#2 = "V";|Y FSalida = 0;
          |ABRE #2;
          #2#0 = #0#0;
          #2#1 = #0#1;
          |LEE 121#2=;
          |SI FSalida = 0;
               |SI #0#100 = "MG";
                    #labtraba#33 = #0#102;
               |FINSI;
               |SI #0#100 = "MC";
                    |SI #0#77 ! 0;
                         #labtraba#45 = #0#102;
                    |SINO;
                         |MENAV "    ATENCION: No se ha indicado nuevo tipo de contrato";
                    |FINSI;
               |FINSI;
               |SI #0#100 = "MT"; |Y #0#104 ! "c";
                    #labtraba#204 = #0#104;
                    #labtraba#205 = #nomocupa#1;
               |FINSI;
          |FINSI;
          |GRABA 020#2a;
          |LIBERA #2;
          |CIERRA #2;
     |FINSI;
|FPRC;

|PROCESO Coefi; |TIPO 7;
     |SI #0#19 ! "    ";  |ACABA;   |FINSI;

     |ABRE #2;
     #2#0 = #0#0;
     #2#1 = #0#1;
     |LEE 020#2=;
     |CIERRA #2;

     |SI #2#42 = "C";    #0#19 = "1.33";     |PINTA #0#19;  |FINSI;
     |SI #2#42 = "F";    #0#19 = "1.61";     |PINTA #0#19;  |FINSI;
|FPRC;

|PROCESO regimen; |TIPO 7;
     |ABRE #10;
     #10#0 = #2#247;
     |LEE 020#10=;
     |CIERRA #10;
     #0#17 = #10#1
     |PINTA #0#17;
|FPRC;

|PROCESO casilla10; |TIPO 0;
     nume1 = #0#21;
     nume2 = #0#19;
     nume3 = (nume1 / nume2) ! 0;
     |SI nume3 ! 0;
          #0#18 = nume3;
          |PINTA #0#18;
     |FINSI;
|FPRC;

|PROCESO solicitud; |TIPO 7;
/*                  || de momento no hacemos aqui nada con las acciones
     |SI #0#2 = "J";
          |PUSHV 10421580;
          |CAMPO_MODIFICA #0#89, 1, "S";
          |CUADRO 10421480;
          |ATRI R;
          ||            3456789012345678901234567890123456789
          |PINTA 1043, "        Seleccione Opcion            ";
          |PINTA 1143, "1.Notificacion de Jornadas Reales    ";
          |PINTA 1243, "2.Solicitud Informe Jornadas Reales  ";
          |PINTA 1343, "3.Modificacion de fecha real de alta ";
          |ATRI 0;
     |SINO;
          |CAMPO_MODIFICA #0#89, 1, "N";
          #0#89 = " ";
     |FINSI;
*/
|FPRC;

|PROCESO restaura;
     |SI #0#89 = " "; |Y Campo = 89;
          |ERROR;
     |SINO;
          |POPV;
     |FINSI;
|FPRC;

|PROCESO AyudaIna; |TIPO 7;
     |PUSHV 09401480;
     |ATRI R;
     ||                   5         6        7         8
     ||            3456789012345678902345678901234567890
     |PINTA 0943, "        Tipos de Inactividad         ";
     |PINTA 1043, "1. Periodo Inactividad               ";
     |PINTA 1143, "2. Huelga Total                      ";
     |PINTA 1243, "3. Huelga Parcial                    ";
     |PINTA 1343, "4. Trienios Funcionarios             ";
     |PINTA 1443, "5. Ausencia del Trabajo (Reg.Min.Car)";
     |ATRI 0;
|FPRC;

|PROCESO HuelgaParcial;
     |SI #0#87 = "3";
          |CAMPO_MODIFICA #0#93, 1, "S";
     |SINO;
          |CAMPO_MODIFICA #0#93, 1, "N";
     |FINSI;
|FPRC;

|PROCESO fechas;
     |SI #0#89 = "1";
          |BLANCO 1043 1179;
          |PINTA 1043, "A¤o Jornadas .....:";
          |PINTA 1143, "Mes Jornadas .....:";
          |CAMPO_MODIFICA #0#5, 1, "N";
          |CAMPO_VISUAL #0#5, 1, "N";
          |CAMPO_MODIFICA #0#16, 1, "N";
          |CAMPO_VISUAL #0#16, 1, "N";
          |CAMPO_MODIFICA #0#91, 1, "S";
          |CAMPO_VISUAL #0#91, 1, "S";
          |CAMPO_POSICION #0#91, 1, 1063;
          |CAMPO_MODIFICA #0#92, 1, "S";
          |CAMPO_VISUAL #0#92, 1, "S";
          |CAMPO_POSICION #0#92, 1, 1163;
          |PINTA #0#91;
          |PINTA #0#92;
     |FINSI;

     |SI #0#89 = "3";
          |BLANCO 1043 1179;
          |PINTA 1043, "Fecha consolidada :";
          |PINTA 1143, "Fecha real alta ..:";
          |CAMPO_VISUAL #0#5, 1, "N";
          |CAMPO_MODIFICA #0#5, 1, "N";
          |CAMPO_MODIFICA #0#90, 1, "S";
          |CAMPO_VISUAL #0#90, 1, "S";
          |CAMPO_POSICION #0#90, 1, 1163;
          |PINTA #0#16;
          |PINTA #0#90;
     |FINSI;

     |SI #0#89 ! "1"; |Y #0#89 ! "3";
          |BLANCO 1043 1179;
          |PINTA 1043, "Fecha ............:";
          |PINTA 1143, "Causa ............:";
          |CAMPO_MODIFICA #0#90, 1, "N";
          |CAMPO_VISUAL #0#90, 1, "N";
          |CAMPO_MODIFICA #0#91, 1, "N";
          |CAMPO_VISUAL #0#91, 1, "N";
          |CAMPO_MODIFICA #0#92, 1, "N";
          |CAMPO_VISUAL #0#92, 1, "N";
          |CAMPO_MODIFICA #0#5, 1, "S";
          |CAMPO_VISUAL #0#5, 1, "S";
          |CAMPO_MODIFICA #0#16, 1, "S";
          |CAMPO_VISUAL #0#16, 1, "S";
          |PINTA #0#5;
          |PINTA #0#16;
     |FINSI;
|FPRC;

|PROCESO Comp105;
     aAlfa = #0#105;
     |SI aAlfa ! "  "; |Y aAlfa ! "01"; |Y aAlfa ! "02";
          |ERROR;
     |FINSI;
|FPRC;

____________________________________/ CALCULOS DE LECTURA E IMPRESION
|PROCESO DesglosaSucesiva;
     fech1 = #0#52;
     nume1 = fech1;
     |SI nume1 > 0;
          diasuc = #0#52 % 102;
          messuc = #0#52 % 402;
          anysuc = #0#52 % -104;
     |SINO;
          diasuc = "";
          messuc = "";
          anysuc = "";
     |FINSI;
|FPRC;

|PROCESO DesglosaFecha;
     fech1 = #0#16;
     nume1 = fech1;
     |SI nume1 > 0;
          diapar = #0#16 % 102;
          mespar = #0#16 % 402;
          anypar = #0#16 % -104;
     |SINO;
          diapar = "";
          mespar = "";
          anypar = "";
     |FINSI;
|FPRC;

|PROCESO TipoDocumento;
     alfa1 = #2#7 % 101;

     tipodc = "";
     |SI alfa1 = "X"; |O alfa1 = "Y"; |O alfa1 = "Z";       || extranjero
          tipodc = "E";
     |FINSI;
     |SI alfa1 ] "A";|Y alfa1 [ "z";
     |Y alfa1 ! "X"; |Y alfa1 ! "Y"; |Y alfa1 ! "Z";         || pasaporte
          tipodc = "P";
     |FINSI;
     |SI alfa1 ] "0";|Y alfa1 [ "9";                        || espa¤ol
          tipodc = "D";
     |FINSI;
|FPRC;

|PROCESO GradoMinusvalia;
     gradom = "";
     grado2 = 0;

     fech1 = ~;     alfa1 = fech1; alfa1 = alfa1 % -104;    || ejercicio

     |ABRE #nomir006;
     #nomir006#0 = #labtraba#7;         || nif
     #nomir006#40 = alfa1;              || ejercicio
     |LEE 000#nomir006.=;
     |SI FSalida = 0;
          |SI #nomir006#6 = "S";
               |SI #nomir006#7 = "1";   gradom = "33-65"; grado2 = 33; |FINSI;
               |SI #nomir006#7 = "2";   gradom = ">65  "; grado2 = 66; |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #nomir006;

     |ABRE #nomir003;
     #nomir003#0 = #labtraba#7;         || nif
     #nomir003#40 = alfa1;              || ejercicio
     #nomir003#41 = #labtraba#0;        || empresa
     |LEE 000#nomir003.=;
     |SI FSalida = 0;
          gradom = "";
          |SI #nomir003#6 = "S";
               |SI #nomir003#7 = "1";   gradom = "33-65"; grado2 = 33; |FINSI;
               |SI #nomir003#7 = "2";   gradom = ">65  "; grado2 = 66; |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #nomir003;
|FPRC;

|PROCESO FechaNacimiento;
     fech1 = #labtraba#11;
     nume1 = fech1;
     |SI nume1 > 0;
          dianac = #labtraba#11 % 102;
          mesnac = #labtraba#11 % 402;
          anynac = #labtraba#11 % -104;
     |SINO;
          dianac = "";
          mesnac = "";
          anynac = "";
     |FINSI;
|FPRC;

|PROCESO ActualizaBaja;
     |SI #0#2 = "B";
          alfa1 = "    " + confi_ant + "Actualizar fecha de baja en el Trabajador y en el Registro (S/N): ";
          |AVISO;
          |CONFI alfa1;
          |SI FSalida = 0;
               confi_ant = "S";
               |ABRE #2;
               #2#0 = #0#0;
               #2#1 = #0#1;
               |LEE 121#2=;
               |SI FSalida = 0;
                    #2#37 = #0#16;   || fecha baja
                    |GRABA 020#2a;
               |FINSI;
               |LIBERA #2;
               |CIERRA #2;

               |ABRE #3;
               |SELECT #2#3;
               #3#1 = #0#0;
               #3#2 = #0#1;
               |LEE 101#3=;
               |SI FSalida = 0;
                    |SI #3#15 = 0;
                         #3#8 = #0#16;           || fin contrato
                         |SI #0#16 ! "..........";|Y #0#16 ! "          ";
                              #3#24 = #0#16;
                         |FINSI;
                    |SINO;
                         #3#20 = #0#16;          || fin prorroga
                         |SI #0#16 ! "..........";|Y #0#16 ! "          ";
                              #3#26 = #0#16;
                         |FINSI;
                    |FINSI;
                    |GRABA 020#3a;
               |FINSI;
               |LIBERA #3;
               |CIERRA #3;
          |SINO;
               confi_ant = "N";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CodigoConvenio;
     |DDEFECTO #nomconca;
     |ABRE #nomconca;
     #nomconca#0 = #labtraba#87;
     |LEE 000#nomconca.=;
     |CIERRA #nomconca;

     codcon = #nomconca#142;
|FPRC;

|PROCESO Permanencias;
|SI #2#42 = "C"; |O #2#42 = "F";
     |SI #0#2 = "A"
          aCoefper = #0#19;
          aCoefper = aCoefper % -102;
          |SI aCoefper ! "  ";
               nPartentera = 1;
               nPartedecimal = aCoefper;
          |FINSI;
     |FINSI;

     |SI #0#2 = "B";
          nDiastrab = #0#18;
          nDiasIT = #0#26;
     |FINSI;
|FINSI;
|FPRC;

|PROCESO CuentaCotizacion;
     |SI #2#45 = "087";|O #2#45 = "097";|O #2#45 = "085";|O #2#45 = "421";
                       |O #2#45 = "064";|O #2#45 = "065";
          ctacot = #5#10;
     |SINO;
          ctacot = #4#10;
     |FINSI;
|FPRC;

|PROCESO SeparaNombre;
     alfa1 = #2#2;   |QBF alfa1;
     alfa2 = alfa1 % 0;   para2 = alfa2;
     elsw4 = 0;
     apell1 = "";
     apell2 = "";
     nombre = "";
     |PARA para1 = 1; |SI para1 [ para2; |HACIENDO para1 = para1 + 1;
          nume2 = (para1 * 100) + 1;
          alfa3 = alfa1 % nume2;
          |SI alfa3 = " ";|O alfa3 = ",";
               |SI apell1 ! "";
                    elsw4 = elsw4 + 1;
               |FINSI;
               |SI elsw4 ] 2;|Y nombre ! "";
                    nombre = nombre + alfa3;
               |FINSI;
          |SINO;
               |SI elsw4 = 0;  apell1 = apell1 + alfa3; |FINSI;
               |SI elsw4 = 1;  apell2 = apell2 + alfa3; |FINSI;
               |SI elsw4 ] 2;  nombre = nombre + alfa3; |FINSI;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO CentroEspecial;
     |DDEFECTO #5;
     |ABRE #5;
     #5#0  = #2#0;   || empresa
     #5#13 = #2#77;  || centro
     |SI #2#45 = "097";|O #2#45 = "085";|O #2#45 = "421";
          #5#1 = "87";
     |SINO;
          |SI #2#45 = "065";
               #5#1  = "64";
          |SINO;
               #5#1  = #2#45 % 2;
          |FINSI;
     |FINSI;
     |LEE 000#5=;
     |SI FSalida ! 0;
          |SI #2#45 = "087";|O #2#45 = "097";|O #2#45 = "085";|O #2#45 = "421";
                            |O #2#45 = "064";|O #2#45 = "065";
               |MENAV "    AVISO! Centro Especial de trabajo INEXISTENTE ...";
          |FINSI;
     |FINSI;
     |CIERRA #5;
|FPRC;

|PROCESO Centro;
     |DDEFECTO #4;
     |ABRE #4;
     #4#0 = #2#0;
     #4#1 = #2#77;
     |LEE 000#4=;
     |SI FSalida ! 0;|Y #2#45 ! "064";|Y #2#45 ! "065";
                     |Y #2#45 ! "087";|Y #2#45 ! "097";|Y #2#45 ! "085";|Y #2#45 ! "421";
          |MENAV "    AVISO! Centro de trabajo INEXISTENTE ...";
     |FINSI;
     |CIERRA #4;
|FPRC;

|PROCESO CodigosNuevos;
     |QBF aContrato;
     aContrato = ("000" + aContrato) % -103;
     |ABRE #nomequiv;
     #nomequiv#0 = aContrato;
     |LEE 000#nomequiv.=;
     |SI FSalida = 0;
          |SI #labtraba#42 = "X";|O #labtraba#42 = "T";|O #labtraba#42 = "P";
               aContrato = #nomequiv#2;      || tiempo parcial
          |SINO;
               aContrato = #nomequiv#1;      || tiempo completo
          |FINSI;
     |FINSI;
     |CIERRA #nomequiv;
|FPRC;

|PROCESO Trabajador;
     |DDEFECTO #2;
     |ABRE #2;
     #2#0 = #0#0;
     #2#1 = #0#1;
     |LEE 020#2=;
     |CIERRA #2;

     #0#34 = #2#45;
     |SI #2#45 = "Z1 ";  #0#34 = "181";  |FINSI;
     |SI #2#45 = "Z2 ";  #0#34 = "182";  |FINSI;
     |SI #2#45 = "Z3 ";  #0#34 = "183";  |FINSI;
     |SI #2#45 = "Z4 ";  #0#34 = "184";  |FINSI;
     |SI #2#45 = "Z5 ";  #0#34 = "185";  |FINSI;
     aContrato = #0#34;
     |HAZ CodigosNuevos;
     #0#34 = aContrato;
|FPRC;

|PROCESO Empresa;
     |DDEFECTO #1;
     |ABRE #1;
     #1#0 = #0#0;
     |LEE 020#1=;
     |CIERRA #1;
|FPRC;

|PROCESO agrarios;
     aDia1 = #0#65 % 102;          || fecha de las actividades realizadas
     aMes1 = #0#65 % 402;          || aparte de la agraria
     aAny1 = #0#65 % -104;
     aDia2 = #0#67 % 102;
     aMes2 = #0#67 % 402;
     aAny2 = #0#67 % -104;

     aDoc1 = #0#71;                || para marcar las cruces de los documentos
     aDoc2 = #0#72;                || presentados
     aDoc3 = #0#73;
     |QBF aDoc1;
     |QBF aDoc2;
     |QBF aDoc3;
     |SI aDoc1!""; aDoc1 = "X"; |FINSI;
     |SI aDoc2!""; aDoc2 = "X"; |FINSI;
     |SI aDoc3!""; aDoc3 = "X"; |FINSI;

     |ABRE #gomexplo;                   || Datos de la explotacion agraria
     #gomexplo#0 = #labempre#0;
     |LEE 000#gomexplo.=;
     |SI FSalida = 0;
          aDatosExp = #gomexplo#1 + " - " + #gomexplo#2 + "; ";
          alfa1 = #gomexplo#4;
          alfa2 = #gomexplo#5;
          alfa1 = "00" + alfa1;
          alfa2 = "000" + alfa2;
          alfa1 = alfa1 % -102;
          alfa2 = alfa2 % -103;
          aDatosExp = aDatosExp + alfa1 + alfa2 + "  " + #gomexplo#3;
          aDatosExp = aDatosExp +  " (" + #gomexplo#6 + ")";
     |FINSI;
     |CIERRA #gomexplo;
|FPRC;

|PROCESO imprime;
     |HAZ Empresa;
     |HAZ Trabajador;
     |HAZ Centro;
     |HAZ CentroEspecial;
     |HAZ SeparaNombre;
     |HAZ CuentaCotizacion;
     |HAZ CodigoConvenio;
     |HAZ ActualizaBaja;
     |HAZ FechaNacimiento;
     |HAZ GradoMinusvalia;
     |HAZ TipoDocumento;
     |HAZ DesglosaFecha;
     |HAZ DesglosaSucesiva;
     |HAZ Permanencias;
     |HAZ agrarios;

     |SI #0#20 = 1; informe = "labta611";    |FINSI;
     |SI #0#20 = 2;
          informe = "labta61z";
          #0#17 = "Regimen Agrario";
     |FINSI;
     |SI #0#2 ! "J";
          |INFOR informe;
          |SI FSalida = 0;
               |PRINT;
               |PIEINF;
          |SINO;
               alfa1 = "0000Informe [" + informe + "] inaccesible.";
               |MENAV alfa1;
          |FINSI;
          |FININF;
     |FINSI;
     |SI #0#24 = "S";
          |HAZ el_red;
     |FINSI;
|FPRC;

|PROGRAMA;
     |CLS;
     |CABEZA "Partes TA-2/S";
     |DDEFECTO #0;
     impre = "ibm80";
     |PARA; |SI; |HACIENDO;
          |PINPA #0#0;
          |PINDA #0#0;
          |ENDATOS #1#0;
          |SI FSalida = 0;
               |SI elsw1 ! 0;
                    |PTEC 802;
               |SINO;
                    elsw1 = 1;
               |FINSI;
               |HAZ si_cambio;
               Impresora = impre;
               |SI #0#2 ! "J";
                    |IMPRE 0;
                    |SI FSalida = 0;
                         impre = Impresora;
                         |HAZ imprime;
                    |FINSI;
               |SINO;
                    |HAZ imprime;
               |FINSI;
               |SI #0#2 ! "J";
                    |FINIMP;
               |FINSI;
          |SINO;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRO;
____________________________________/ ACTUALIZACION DE R.E.D.
|PROCESO ayuda; |TIPO 7;
     |ABRE #20;
     |LEE 000#20p;
     |SI FSalida = 0;|Y #0#2 = "B";
          |CAMPO_MODIFICA #0#22, 1, "S";

/*
          |PUSHV 09352276;
          |CUADRO 09 35 22 76;
          |ATRI R;
          ||            1234567890123456789012345678901234567890
          |PINTA 1036, " -------- SITUACIONES DE BAJA --------- ";
          |PINTA 1136, "                                        ";
          |PINTA 1236, " (1) Baja Volut. Trabajad. Cta. Ajena.  ";
          |PINTA 1336, " (2) Baja no Voluntaria.                ";
          |PINTA 1436, " (3) Baja por fallecimiento.            ";
          |PINTA 1536, " (4) Baja por pase situa. pensionista.  ";
          |PINTA 1636, " (5) Baja por servicio militar.         ";
          |PINTA 1736, " (6) Excedencia voluntaria.             ";
          |PINTA 1836, " (7) Excedencia por huelga o cierre.    ";
          |PINTA 1936, " (8) Baja por agotamiento I.T.          ";
          |PINTA 2036, " (9) Baja por otras causas de suspension";
          |PINTA 2136, "                                        ";
          |ATRI 0;
*/
     |SINO;
          |CAMPO_MODIFICA #0#22, 1, "N";
     |FINSI;
     |CIERRA #20;
|FPRC;

|PROCESO fin_ayuda; |TIPO 0;
    red_baja = #0#22;
/*
     |SI #0#2 = "B";
          ||POPV;
          |SI #0#22 = 1; red_baja = 51; |FINSI;
          |SI #0#22 = 2; red_baja = 54; |FINSI;
          |SI #0#22 = 3; red_baja = 56; |FINSI;
          |SI #0#22 = 4; red_baja = 58; |FINSI;
          |SI #0#22 = 5; red_baja = 61; |FINSI;
          |SI #0#22 = 6; red_baja = 63; |FINSI;
          |SI #0#22 = 7; red_baja = 64; |FINSI;
          |SI #0#22 = 8; red_baja = 65; |FINSI;
          |SI #0#22 = 9; red_baja = 99; |FINSI;
     |FINSI;
*/
|FPRC;

|PROCESO ayuda59; |TIPO 7;
     |ABRE #20;
     |LEE 000#20p;
     |SI FSalida = 0;
          |PUSHV 09352276;
          |CUADRO 09 35 22 76;
          |ATRI R;
          ||            1234567890123456789012345678901234567890
          |PINTA 1036, " -- RELACIONES LABORALES ESPECIALES --- ";
          |PINTA 1136, " 0100 Personal Alta Direccion           ";
          |PINTA 1236, " 0300 Penados en inst. penitenciarias   ";
          |PINTA 1336, " 0304 Penados en inst. penit. -menores- ";
          |PINTA 1436, " 0401 Jugadores de futbol               ";
          |PINTA 1536, " 0402 Jugadores de baloncesto           ";
          |PINTA 1636, " 0403 Jugadores de balonmano            ";
          |PINTA 1736, " 0404 Ciclistas                         ";
          |PINTA 1836, " 0600 Minusvalidos en Centros Especiales";
          |PINTA 1936, " 0800 Artistas en espectac. publicos    ";
          |PINTA 2036, " 0900 Abogados en despachos de abogados ";
          |PINTA 2136, " 9909 Personal investigador en formacion";
          |ATRI 0;
     |FINSI;
     |CIERRA #20;
|FPRC;

|PROCESO chequea59; |TIPO 0;
     |SI #0Campo = "    ";
          |POPV;
          |ACABA;
     |FINSI;

     |SI #0Campo ! "0100"; |Y #0Campo ! "0300"; |Y #0Campo ! "0304";
     |Y  #0Campo ! "0401"; |Y #0Campo ! "0402"; |Y #0Campo ! "0403";
     |Y  #0Campo ! "0404"; |Y #0Campo ! "0600"; |Y #0Campo ! "0700";
     |Y  #0Campo ! "0800"; |Y #0Campo ! "0900"; |Y #0Campo ! "9909";
          |MENAV "0000Codigo de relacion especial incorrecto";
          |ERROR;
     |SINO;
          |POPV;
     |FINSI;
|FPRC;

|PROCESO ayuda60; |TIPO 7;
     |ABRE #20;
     |LEE 000#20p;
     |SI FSalida = 0;
          |PUSHV 15032344;
          |CUADRO 15 03 23 44;
          |ATRI R;
          ||            0      1         2         3         4
          ||            456789012345678901234567890123456789012345
          |PINTA 1504, " ----- CAUSAS DE SUSTITUCION ---------- ";
          |PINTA 1604, " 1 Por excedencia por cuidado familiares";
          |PINTA 1704, " 2 Por descanso por maternidad          ";
          |PINTA 1804, " 3 Jubilado anticipadamente a los 64    ";
          |PINTA 1904, " 4 Minusv lido                          ";
          |PINTA 2004, " 5 Sustitucion minusv lido en IT        ";
          |PINTA 2104, " 6 V¡ctimas violencia de g‚nero         ";
          |PINTA 2204, " 7 Cobertura vacante disminuci¢n e. fijo";
          |ATRI 0;
     |FINSI;
     |CIERRA #20;
|FPRC;

|PROCESO chequea60; |TIPO 0;
     |SI #0Campo = 0;    |ACABA;   |FINSI;

     |SI #0Campo ! 1;|Y #0Campo ! 2;|Y #0Campo ! 3;|Y #0Campo ! 4;|Y #0Campo ! 5;
     |Y  #0Campo ! 6;|Y #0Campo ! 7;
          |MENAV "0000Codigo de causa de sustitucion incorrecto";
     |FINSI;
|FPRC;

|PROCESO habitual; |TIPO 0;
     |SI #0#68 ! "X";
          |CAMPO_MODIFICA #0#64, 1, "S";
          |CAMPO_MODIFICA #0#65, 1, "S";
          |CAMPO_MODIFICA #0#66, 1, "S";
          |CAMPO_MODIFICA #0#67, 1, "S";
     |SINO;
          |CAMPO_MODIFICA #0#64, 1, "N";
          |CAMPO_MODIFICA #0#65, 1, "N";
          |CAMPO_MODIFICA #0#66, 1, "N";
          |CAMPO_MODIFICA #0#67, 1, "N";
     |FINSI;
|FPRC;

|PROCESO temporada; |TIPO 0;
     |SI #0#69 = "X";
          |CAMPO_MODIFICA #0#70, 1, "S";
     |SINO;
          |CAMPO_MODIFICA #0#70, 1, "N";
     |FINSI;
|FPRC;

|PROCESO domicilio; |TIPO 0;
     |SI #0#79 = "2";
          |CAMPO_MODIFICA #0#80, 1, "S";
          |CAMPO_MODIFICA #0#81, 1, "S";
          |CAMPO_MODIFICA #0#82, 1, "S";
          |CAMPO_MODIFICA #0#83, 1, "S";
          |CAMPO_MODIFICA #0#84, 1, "S";
          |CAMPO_MODIFICA #0#85, 1, "S";
          |CAMPO_MODIFICA #0#86, 1, "S";
     |SINO;
          |CAMPO_MODIFICA #0#80, 1, "N";
          |CAMPO_MODIFICA #0#81, 1, "N";
          |CAMPO_MODIFICA #0#82, 1, "N";
          |CAMPO_MODIFICA #0#83, 1, "N";
          |CAMPO_MODIFICA #0#84, 1, "N";
          |CAMPO_MODIFICA #0#85, 1, "N";
          |CAMPO_MODIFICA #0#86, 1, "N";
     |FINSI;
|FPRC;

|PROCESO chequeaExt; |TIPO 7;
     alfa1 = #labtraba#7 % 101;
     |SI alfa1 = "X"; |O alfa1 = "Y"; |O alfa1 = "Z";       || extranjero
          |CAMPO_MODIFICA #0#97, 1, "S";
     |SINO;
          #0#97 = "   ";
          |PINTA #0#97;
          |CAMPO_MODIFICA #0#97, 1, "N";
     |FINSI;
|FPRC;

|PROCESO chequeaExt2; |TIPO 0;
     |SI alfa1 = "X"; |O alfa1 = "Y"; |O alfa1 = "Z";       || extranjero
          alfa2 = #0#97;
          |QBF alfa2;
          |SI alfa2 = "";
               alfa3 = "    Trabajador extranjero. Es necesario indicar";
               alfa3 = alfa3 + " el tipo de autorizacion";
               |MENAV alfa3;
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO chequeaVig; |TIPO 7;
     alfa1 = #labtraba#7 % 101;
     |SI alfa1 = "X"; |O alfa1 = "Y"; |O alfa1 = "Z";       || extranjero
          |CAMPO_MODIFICA #0#98, 1, "S";
     |SINO;
          #0#98 = "        ";
          |PINTA #0#98;
          |CAMPO_MODIFICA #0#98, 1, "N";
     |FINSI;
|FPRC;

|PROCESO agired; |TIPO 7;
     |ABRE #20;
     |LEE 000#20p;
     |SI FSalida = 0;
          alfa1 = "S";
     |SINO;
          alfa1 = "N";
     |FINSI;
     |CIERRA #20;
     |C_M #0Campo, 1, alfa1;
|FPRC;

|PROCESO los_minutos;
     horas_a = #0#9  - ( ( (#0#9  * 100) $ 100) / 100);
     horas_c = #0#10 - ( ( (#0#10 * 100) $ 100) / 100);

     horas_a = horas_a ! 0;    || si no se redondean no pasan bien al fichero
     horas_c = horas_c ! 0;    || ejemplo (horas=1.66)

     minut_a = (#0#9 * 100) $ 100;
     minut_a = ((minut_a * 60) / 100) ! 0;

     minut_c = (#0#10 * 100) $ 100;
     minut_c = ((minut_c * 60) / 100) ! 0;
|FPRC;

|PROCESO borralo1;
     |BORRA 020#21a;
     |LIBERA #21;
|FPRC;

|DEFBUCLE borralo;
     #21#2;
     14;
     #2#0, #2#1, 0;
     #2#0, #2#1, 0;
     be;
     NULL, borralo1;
|FIN;

|PROCESO el_red;
     |ABRE #20;
     #20#0 = #2#0;
     |LEE 000#20=;
     |SI FSalida = 0;

          |DDEFECTO #labpadre;
          |ABRE #labpadre;
          #labpadre#0 = #labtraba#0;
          #labpadre#1 = #labtraba#1;
          |LEE 000#labpadre.=;
          |CIERRA #labpadre;

          |ABRE #21;
          |LEE 000#21u;
          |SI FSalida ! 0;
               red_ultimo = 1;
          |SINO;
               red_ultimo = #21#0 + 1;
          |FINSI;
          |CIERRA #21;

          |BUCLE borralo;

          |ABRE #21;
          |DDEFECTO #21;
          #21#0 = red_ultimo;
          |GRABA 020#21b;

          #21#1 = #2#0;         || empresa
          #21#2 = #2#1;         || trabajador
          |SI #0#2 = "A"; #21#3 = "MA"; #21#4 = "01";     |FINSI; || accion/situac.
          |SI #0#2 = "B"; #21#3 = "MB"; #21#4 = red_baja; |FINSI;
          |SI #0#2 = "V"; #21#3 = #0#100; #21#4 = "00";     |FINSI;
          |SI #0#2 = "J";
               |SI #0#89 = "1"; #21#3 = "MJR"; |FINSI;
               |SI #0#89 = "2"; #21#3 = "CJR"; |FINSI;
               |SI #0#89 = "3"; #21#3 = "MFR"; |FINSI;
          |FINSI;
          #21#5 = "";           || mod.duracion 2104/84
          |SI #2#45 ] "014";|Y #2#45 [ "017";   #21#5 = #2#45; |FINSI;
          #21#6 = #0#8;         || calif. per. horas
          |HAZ los_minutos;
          #21#7 = horas_a;      || nro. horas contrato (A)
          #21#8 = horas_c;      || nro. horas empresa/convenio (B)
          |SI #0#14 ] 1000;
               #21#12 = 0;
          |SINO;
               #21#12 = #0#14;
          |FINSI;
          #21#13 = #0#16;     || fecha incidencia
          #21#14 = 0;         || nro. de lote
          #21#15 = minut_a;   || nro. minutos contrato (A)
          #21#16 = minut_c;   || nro. minutos empresa/convenio (B)
          #21#17 = #labpadre#2;    || nombre padre
          #21#18 = #labpadre#3;    || nombre madre
          #21#19 = #labtraba#244;  || telefono
          #21#20 = #0#35;
          /*
          #21#20 = "0";                           || desempleado: no inscrito
          |SI #0#35 ! " "; #21#20 = "1"; |FINSI;  || desempleado:
          |SI #0#36 ! " "; #21#20 = "2"; |FINSI;  || desempleado: >12 meses
          |SI #0#37 ! " "; #21#20 = "3"; |FINSI;  || desempleado: REA
          |SI #0#58 ! " "; #21#20 = "4"; |FINSI;  || desempleado: benefic.
          */
          #21#21 = "N";
          |SI #0#38 ! " "; #21#21 = "S"; |FINSI;  || mujer subrepresentada
          #21#22 = "N";
          |SI #0#41 ! " "; #21#22 = "S"; |FINSI;  || incapac.readmitido
          #21#23 = "N";
          |SI #0#40 ! " "; #21#23 = "S"; |FINSI;  || 1er.trabaj.autonomo
          #21#24 = grado2;                        || grado minusvalia
          #21#25 = " ";
          |SI #0#39 ! " "; #21#25 = #0#39; |FINSI;  || exc.soc./violen.domest.
          #21#27 = #0#59;                         || cod.relacion especial
          #21#28 = #0#43;                         || NAFSS sustituido
          #21#29 = #0#60;                         || cod.causa sustitucion
          #21#30 = "N";
          |SI #0#61 ! " "; #21#30 = "S"; |FINSI;   || renta activa insercion
          #21#31 = "N";
          |SI #0#62 ! " "; #21#31 = "S"; |FINSI;   || trab.contrat.24meses parto
          #21#32 = #0#63;                          || coef.reductor edad jubil.

          #21#33 = nPartentera;        || parte entera coeficiente (sit.esp. reg 0132)
          #21#34 = nPartedecimal;      || parte decimal coeficiente (sit.esp. reg 0132)
          #21#35 = nDiastrab;          || dias de trabajo (sit.esp. reg 0132)
          #21#36 = nDiasIT;            || dias en IT (sit.esp. reg 0132)

          |SI #0#87 ! " ";    || Inactividad
               #21#37 = #0#87;
          |SINO;
               #21#37 = " ";
          |FINSI;

          |SI #0#87 = "3";      || Coef. Huelga Parcial en caso de
               #21#43 = #0#93;  || Tipo Inactividad = 3
          |FINSI;

          |SI #0#88 ! " ";
               #21#38 = #0#88;  || Exclusion Desempleo
          |SINO;
               #21#38 = "0";
          |FINSI;
          |SI #21#3 = "MJR";
               #21#40 = #0#91;     || A¤o Jornadas Reales
               #21#41 = #0#92;     || Mes Jornadas Reales
               |HAZ dias_mes;
               #21#42 = aCalendario;
          |FINSI;
          |SI #21#3 = "MFR";
               #21#13 = #0#16;
               #21#39 = #0#90;
          |FINSI;
          #21#12 = 0;                  || Coef. T.P. no admisible en agrarios
          #21#44 = #0#94;          || relevo (DAM)
          #21#45 = #0#95;          || fecha desde (DAM)
          #21#46 = #0#96;          || fecha hasta (DAM)

          alfa1 = #0#97;           || Tipo Autorizacion trabajo extranjeros
          |QBF alfa1;
          |SI alfa1 ! "";
               #21#48 = alfa1;
          |FINSI;
          alfa1 = #0#98;           || Vigencia Autorizacion trabajo extranjeros
          |QBF alfa1;
          |SI alfa1 ! "";
               alfa1 = alfa1 - ".";
               alfa1 = alfa1 - ".";
               #21#49 = alfa1;
          |FINSI;
          |SI #0#2 = "A";
               #21#51 = #0#99;          || causa exclusion en Censo agrario
          |FINSI;
          |SI #0#2 = "A"; |Y #labtraba#204 ! " ";
               #0#104 = #labtraba#204;
          |FINSI;
          #21#52 = #0#104;
          #21#53 = #0#105;
          |SI #0#2 = "A";
               |SI #labtraba#42 = "A"; |O #labtraba#42 = "e";
                    #21#54 = 1;
               |FINSI;
               |SI #labtraba#42 = "E";
                    #21#54 = 2;
               |FINSI;
          |FINSI;
          #21#57 = #0#107;
          #21#58 = #0#108;
          #21#59 = #0#109;
          |SI #0#110 = "S";
               #21#60 = "1";
          |SINO;
               #21#60 = "0";
          |FINSI;

          |GRABA 020#21a;
          |LIBERA #21;
          |CIERRA #21;
     |FINSI;
     |CIERRA #20;
|FPRC;

|PROCESO dias_mes;
     |ABRE #gomdetjr;
     #gomdetjr#0 = #0#0;
     #gomdetjr#1 = #0#1;
     #gomdetjr#2 = #0#91;
     #gomdetjr#3 = #0#92;
     |LEE 000#gomdetjr.=;
     aCalendario = "";
     |SI FSalida = 0;
          |PARA nCampo = 4; |SI nCampo [ 33; |HACIENDO nCampo = nCampo + 1;
               |SI #gomdetjr#nCampo# = "X";
                    aJornada = "S";
               |SINO;
                    aJornada = "N";
               |FINSI;
               aCalendario = aCalendario + aJornada;
          |SIGUE;
     |SINO;
          aCalendario = "NNNNNNNNNNNNNNNNNNNNNNNNNNNNNNN";
     |FINSI;
     |CIERRA #gomdetjr;
|FPRC;
