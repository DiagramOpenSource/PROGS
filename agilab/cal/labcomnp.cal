|FICHEROS;
   labempre #0;
   labcomnp #1;
   labcontr #2;
   labtraba #3;
   labcentr #4;
   labregis #5;
|FIN;

|VARIABLES;
 x = 0;
 mod = 0;
    men0 = "0000ATENCION !!! Fecha erronea ...";
    dia1 = "";
    mes1 = "";
    any1 = "";
    dia = 0;
    mes = 0;
    any = 0;
    tope = 0;
    sw = 0;
    fec = "";
    blanco = "          ";
    puntos = "..........";
    informe = "labcomnp";
    men1 = "     NO EXITE REGISTRO DE EMPRESA";
    men2 = "     NO EXITE REGISTRO DE TRABAJADORES";
    men3 = "     LA FECHA DEBE SER SUPERIOR A 15 DIAS";
    men4 = "     NO EXISTE IMPRESORA";
    men5 = "     ATENCION !!! Fichero de Parametros de Control inexistente ...";
    men6 = "     No existe trabajador con ese codigo ...";
    men7 = "     ATENCION !!! Este Trabajador no pertenece al Centro dado ...";
    men8 = "     ATENCION !!! No existe Centro de Trabajo de este Trabajador ...";
    men9 = "     ATENCION !!! No existe ficha en Registro de Contratos ...";
    men10 = "     ATENCION !!! No existe Prorroga registrada de este Trabajador ...";
    fechanum = 0;
    &coma = ",";
    fechacom = "";
    loca = "";
    loca1 = "";
    &local = "";
    &fcom = "";
    diacom = "";
    mescom = "";
    anycom = "";
    espacio  = " "
    uno =       " Enero de ";
    dos =       " Febrero de ";
    tres =      " Marzo de ";
    cuatro =    " Abril de ";
    cinco =     " Mayo de ";
    seis =      " Junio de ";
    siete =     " Julio de ";
    ocho =      " Agosto de ";
    nueve =     " Septiembre de ";
    diez =      " Octubre de ";
    once =      " Noviembre de ";
    doce =      " Diciembre de ";
    calle1 = "";
    &calle = "";
    numca = "";
    piso = "";
    comaespa = ", "
    espa = " ";
    varcampo = 0;
    &domicen = "";
     dominum = "";
    varcamp2 = 0;
    varcamp3 = 0;
    varcamp4 = 0;
|FIN;

|INCLUYE i_comun;

|PROCESO compru1;|TIPO 0;
|ABRE #0;
    #0#0 = #1#0;
     |LEE 040#0=;
       |SI FSalida ! 0;
          |MENAV men1;
          |ERROR;
       |FINSI;
|CIERRA #0;
|FPRC;

|PROCESO leecontr; |TIPO 7;  || lee fichero de control
|ABRE #2;
|LEE 040#2p;
|SI FSalida ! 0;
    |MENAV men5;
       #2#38 = "";
|FINSI;
|CIERRA #2;
|FPRC;


|PROCESO varios;
    fechacom = #1#2;
    diacom = fechacom % A102;
    mescom = fechacom % A402;
    anycom = fechacom % A704;
  |SI mescom = 01;
    fechacom =  uno;
  |FINSI;
  |SI mescom = 02;
    fechacom =  dos ;
  |FINSI;
  |SI mescom = 03;
    fechacom = tres;
  |FINSI;
  |SI mescom = 04;
    fechacom = cuatro;
  |FINSI;
  |SI mescom = 05;
    fechacom = cinco;
  |FINSI;
  |SI mescom = 06;
    fechacom = seis;
  |FINSI;
  |SI mescom = 07;
    fechacom = siete;
  |FINSI;
  |SI mescom = 08;
    fechacom = ocho;
  |FINSI;
  |SI mescom = 09;
    fechacom = nueve;
  |FINSI;
  |SI mescom = 10;
    fechacom = diez;
  |FINSI;
  |SI mescom = 11;
    fechacom = once;
  |FINSI;
  |SI mescom = 12;
    fechacom = doce;
  |FINSI;
    fcom = diacom + fechacom;
    fcom = fcom + anycom;
    loca = #0#6;
    |QBF loca;
    loca1 = #0#6 - loca;
    local = loca1 + loca;
    calle1 = #0#3;
    numca = #0#4;
    piso = #0#5;
    |QBF calle1;
    calle = calle1 + comaespa;
    calle = calle + numca;
    calle = calle + espa;
    calle = calle + piso;
|FPRC;

|PROCESO impre;|TIPO 3;
|HAZ r_regis0;   r_emp = #1#0;  |HAZ r_regis1;
|IMPRE 0;
|SI FSalida = 0;
    |PARA x = 1;|SI x [ #1#15;|HACIENDO x = x + 1;
        |INFOR informe;
        |PRINT;
        |PIEINF;
        |FININF;
    |SIGUE;
|SINO;
    |MENAV men4;
|FINSI;
|FINIMP;
|FPRC;

|PROCESO leetrab; |TIPO 0;
    varcampo = Campo - 13;
    varcamp2 = Campo - 7;
    varcamp3 = Campo + 6;
    varcamp4 = Campo + 12;
|SI #1Campo ! 0;
    |ABRE #3;
    |ABRE #4;
    |ABRE #5;
        #3#0 = #1#0;
        #3#1 = #1Campo;
    |LEE 040#3=;
    |SI FSalida ! 0;
        #1varcampo = "";
        |MENAV men6;
        |ERROR;
    |SINO;
        |SI #3#77 ! #1#34;
            |MENAV men7;
            |ERROR;
        |SINO;
            #4#0 = #3#0;
            #4#1 = #3#77;
            |LEE 040#4=;
            |SI FSalida ! 0;
                |MENAV men8;
                |ERROR;
                 domicen = "";
            |SINO;
                 domicen = #4#3;
                 dominum = #4#4;
                 |QBF domicen;
                 domicen = domicen + ", " + dominum;
            |FINSI;
        |FINSI;
        #1varcampo = #3#2;
    |FINSI;
    |PINTA #1varcampo;
    |SELECT #2#5;
       #5#1 = #3#0;
       #5#2 = #3#1;
    |LEE 040#5=;
    |SI FSalida ! 0;
        |MENAV men9;
        #1varcamp2 = "";
        #1varcamp3 = "";
        #1varcamp4 = "";
        |ERROR;
    |SINO;
        |SI #5#15 = 0;
            |MENAV men10;
            |ERROR;
        |SINO;
            #1varcamp2 = #5#7;
            #1varcamp3 = #5#16;
            #1varcamp4 = #5#17;
        |FINSI;
    |FINSI;
    |PINTA #1varcamp2;
    |PINTA #1varcamp3;
    |PINTA #1varcamp4;
|SINO;
   #1varcampo = "";
   #1varcamp2 = "";
   #1varcamp3 = "";
   #1varcamp4 = "";
|FINSI;
|CIERRA #3;
|CIERRA #4;
|CIERRA #5;
|FPRC;

|PROCESO verifech; |TIPO 0;
        fec = #1Campo; sw = 0;
|SI fec ! blanco; |Y fec ! puntos;
  dia1 = fec % A102;
  mes1 = fec % A402;
  any1 = fec % A704;
  dia = dia1; mes = mes1; any = any1;
  |SI dia ! 0; |Y mes ! 0; |Y any ! 0;
     |SI mes [ 12; |Y mes ] 1;
       |SI mes = 1; |O mes = 3; |O mes = 5; |O mes = 7; |O mes = 8; |O mes = 10; |O mes = 12;
           tope = 31;
       |SINO;
           |SI mes = 2;
               mod = any $ 4;
               |SI mod = 0;
                   tope = 29;
               |SINO;
                   tope = 28;
               |FINSI;
            |SINO;
               tope = 30;
            |FINSI;
        |FINSI;
        |SI dia [ tope;
            sw = 1;
        |FINSI;
     |FINSI;
  |FINSI;
|SINO;
  sw = 1;
|FINSI;
|SI sw = 0;
    |MENAV men0;
    |ERROR;
|FINSI;
|FPRC;
