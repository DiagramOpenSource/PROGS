|FICHEROS;
    labempre #0;         || empresas
    labcentr #1;         || centros de trabajo

    agicontr #10;        || control AGICLI
    agifigen #11;        || clientes AGICLI
    agihacie #12;        || datos hacienda AGICLI

    silcon01 #20;        || control AGIRED
    silcon06 #30;        || gestion empresas AGIRED

    eosm0030 #130;       || datos IRPF
     nomcnaec;
     nomcnahi;
     nomw0021,MANTE;
     nomconca;
|FIN;

|VARIABLES;
    nume1 = 0;
    nume2 = 0;
    lugar = 0;
    nifnum = 0;
    resto = 0;
    mod = 0;
    corta = 0;
    nume3 = 0;
    jg = 0;
    sitio = 0;
    con = 0;
    swmod = 0;
    baja = 0;
    men1 = "0000ATENCION Fecha erronea";
    dia1 = "";
    mes1 = "";
    any1 = "";
    dia = 0;
    mes = 0;
    any = 0;
    tope = 0;
    sw = 0;
    fec = "";
    blanco = "          ";
    puntos = "..........";
    letra = "";
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    final = "";
    x = "";
    men6 = "0000S Confirma el alta en Clientes General : ";
    men7 = "      D.N.I. del Cliente es distinto al D.N.I. del Cliente General";
    men8 = "      No existe Cliente General";
    aste = "";
    agi = "";
    agi1 = "";
    longi = "";
    num   = 0;
    agi2 = "";
    dni1 = "";
    dni2 = "";
    trabacod1 = "";
    trabacod2 = "";
    &em_borr = 0;
    &tr_borr = 0;
    FEstado = 0;
    FEstado2 = 0;
    runnom = "";
    ant_runnom = "";
    aAlfa1 = "";
    aAlfa2 = "";
    &orden = "";
    &pasa = "";
     aAlfa = "";
     fFecha = @;
     &nElAny = 0;
     &nElMes = 0;

     aNifQbf1   = "";
     aNifQbf2   = "";

     &eaVarDni  = "";
     &enCalcCif = 0;

     aCNAE = "";
     aDesCNAE = "";
     nModo = 0;
     &eaVengoDe = "";
     swBotonSiNo = 0;
     nOpcion = 0;
     enBoton = 0;

     &eaProcedencia = "labempre";
     &eaMensaCad    = "";
     &enErrorCad    = 0;
|FIN;

|PROCESO letranif; |TIPO 0; || CALCULA LETRA PARA EL NIF CUANDO SE PULSA -F1-
     |SI FSalida = 9;
         eaVarDni   = #0Campo;
         enCalcCif  = 1;
         |HAZ CalculoDNI;
         #0Campo = eaVarDni;
         |PINTA #0Campo;
         |ERROR;
         |ACABA;
     |FINSI;

     eaVarDni  = #0Campo;
     enCalcCif = 0;
     |HAZ CalculoDNI;

     aNifQbf1 = #0Campo;   |QBF aNifQbf1;
     aNifQbf2 = eaVarDni;  |QBF aNifQbf2;

     |SI aNifQbf1 ! aNifQbf2; |Y aNifQbf1 ! "";
         |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
         |SI FSalida = 0;
             #0Campo = eaVarDni;  |PINTA #0Campo;
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO fecempre; |TIPO 0;
        fec = #0Campo; sw = 0;
|SI fec ! blanco; |Y fec ! puntos;
  dia1 = fec % A102;
  mes1 = fec % A402;
  any1 = fec % A704;
  dia = dia1; mes = mes1; any = any1;
  |SI dia ! 0; |Y mes ! 0; |Y any ! 0;
     |SI mes [ 12; |Y mes ] 1;
       |SI mes = 1; |O mes = 3; |O mes = 5; |O mes = 7; |O mes = 8; |O mes = 10; |O mes = 12;
           tope = 31;
       |SINO ;           |SI mes = 2;
               mod = any $ 4;
               |SI mod = 0;
                   tope = 29;
               |SINO;
                   tope = 28;
               |FINSI;
            |SINO;
               tope = 30;
            |FINSI;
        |FINSI;
        |SI dia [ tope;
            sw = 1;
        |FINSI;
     |FINSI;
  |FINSI;
|SINO;
  sw = 1;
|FINSI;
|SI sw = 0;
    |MENAV men1;
    |ERROR;
|FINSI;
|FPRC;

|PROCESO control1; |TIPO 0;
|SI FSalida = 9;
        alfa1 = #0Campo;
    |QBF alfa1;
        alfa2 = alfa1 % 102; alfa3 = alfa1 % -107;
        alfa4 = alfa3 % 101;
    |SI alfa4 = "0";
            alfa3 = alfa3 % 206;
            alfa1 = alfa2 + alfa3;
    |FINSI;
        nume1 = alfa1;
        nume2 = nume1 / 97;
    |HAZ trunca1;
    |SI nume1 ] 0; |Y nume1 [ 99;
            alfa1 = #0Campo;
        |QBF alfa1;
            alfa2 = nume1;
            alfa2 = "00" + alfa2;
            alfa2 = alfa2 % -102;
            alfa1 = alfa1 + alfa2;
            #0Campo = alfa1;
        |PINTA #0Campo;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO trunca1;
    corta = 0;
    alfa1 = nume2;  alfa2 = alfa1 % 0;  nume3 = alfa2;
|PARA jg = nume3; |SI jg ] 1;|Y corta = 0; |HACIENDO jg = jg - 1;
        sitio = (jg * 100) + 1;
        alfa2 = alfa1 % sitio;
    |SI alfa2 = ".";  corta = jg;  |FINSI;
|SIGUE;
|SI corta ! 0;
        sitio = 100 + corta - 1;
        alfa1 = alfa1 % sitio;
|FINSI;
    nume3 = alfa1;
    nume1 = nume1 - (nume3 * 97);
|FPRC;

/*
|PROCESO distinta; |TIPO 0;
|SI #0Campo ! "E";|Y #0Campo ! " ";
    |SI Campo = 37;
        |SI #0Campo = #0#38;
            |MENAV "    ATENCION!!! Claves iguales ...";
            |ERROR;
        |FINSI;
    |SINO;
        |SI #0Campo = #0#37;
            |MENAV "    ATENCION!!! Claves iguales ...";
            |ERROR;
        |FINSI;
    |FINSI;
|SINO;
    |SI #0Campo = "E";
        |MENAV "    ATENCION!!! Clave gestionada con conceptos 41? ...";
        |ERROR;
    |FINSI;
|FINSI;
|FPRC;
*/

|PROCESO tipo3; |TIPO 3;
    con = (FEstado2 / 100) ! 0;
|HAZ grabacen;
|HAZ modicli;
|HAZ grabared;
|FPRC;

|PROCESO grabacen;
|SI con = 1;             || alta del centro numero 1
    |ABRE #1;
        #1#0 = #0#0;
        #1#1 = 1;
    |LEE 000#1=;
    |SI FSalida ! 0;
        |DDEFECTO #1;
            #1#0  =  #0#0;               || codigo empresa
            #1#1  =  1;                  || codigo centro
            #1#2  =  "PROPIA EMPRESA";   || descripcion centro
            #1#3  =  #0#3;               || domicilio
            #1#4  =  #0#4;               || numero
            #1#5  =  #0#5;               || piso
            #1#6  =  #0#6;               || localidad
            #1#7  =  #0#7;               || codigo postal 1
            #1#8  =  #0#8;               || codigo postal 2
            #1#9  =  #0#9;               || provincia
            #1#10 = #0#13;               || nro. S.S. centro/empresa
            #1#11 = #0#14;               || plantilla centro/empresa
            #1#13 = #0#19;               || clave entidad
            #1#14 = #0#20;               || entidad
        |GRABA 020#1n;
    |FINSI;
    |CIERRA #1;
|FINSI;
|FPRC;

|PROCESO grabared;
|SI con = 1;                       || alta en AGIRED
    |ABRE #20;
    |LEE 000#20p;
    |SI FSalida = 0;
        |CONFI "    NDesea Alta en AGIRED (S/N): ";
        |SI FSalida = 0;
            |DDEFECTO #30;
            |ABRE #30;
                #30#0 = #0#0;
            |LEE 000#30=;
            |SI FSalida ! 0;
                |PUSHV 05012380;
                |PINPA #0#30;
                |PINDA #0#30;
                |ENDATOS #1#30;
                |SI FSalida = 0;
                    |GRABA 020#30n;
                |FINSI;
                |POPV;
            |FINSI;
            |CIERRA #30;
        |FINSI;
    |FINSI;
    |CIERRA #20;
|FINSI;
|FPRC;
____________________________________/ GESTION DEL AGICLI
|PROCESO modicli;
|HAZ tipo8;
    con = (FEstado2 / 100) ! 0;
|SI con = 2;|Y aste ! "*";         || modo modificacion (desde el tipo3)
    |ABRE #11;
        #11#0 = #0#0;
    |LEE 000#11=;
    |SI FSalida = 0;
            dni1 = #0#10;  |QBF dni1;
            dni2 = #11#13; |QBF dni2;
        |SI dni1 ! dni2;
            |MENAV men7;
        |FINSI;
    |SINO;
        |MENAV men8;
    |FINSI;
    |CIERRA #11;
|FINSI;
|FPRC;

|PROCESO bajacli; |TIPO 1;
|HAZ tipo8;
    con = (FEstado2 / 100) ! 0;
|SI con = 3;|Y aste ! "*";         || modo baja
    |ABRE #11;
        #11#0 = #0#0;
    |LEE 101#11=;
    |SI FSalida = 0;
            #11#18 = "N";
        |GRABA 020#11a;
    |FINSI;
    |LIBERA #11;
    |CIERRA #11;
|FINSI;
|FPRC;

|PROCESO altacli; |TIPO 0;
     FEstado2 = FEntrada;
     con = (FEstado2 / 100) ! 0;
|HAZ tipo8;
|MODULO runnom;
|SI con = 1;|Y aste ! "*";|Y runnom ! ant_runnom; || modo alta
        ant_runnom = runnom;
    |ABRE #11;
        #11#0 = #0#0;
    |LEE 101#11=;
        FEstado = FSalida;
    |SI FEstado = 0;
        |HAZ carga_campos;
    |SINO;
        |CONFI men6;
        |SI FSalida = 0;
            |PUSHV 0501 2380;
            |PINPA #0#11;
            |DDEFECTO #11;
                #11#0 = #0#0;
            |PINDA #0#11;
            |ENDATOS #1#11;
            |SI FSalida = 0;
                |POPV;
                |HAZ postal;
                |HAZ carga_campos;
            |SINO;
                |ERROR;
                |POPV;
            |FINSI;
        |FINSI;
    |FINSI;
    |LIBERA #11;
    |CIERRA #11;
|FINSI;
|FPRC;

|PROCESO carga_campos;
    #11#18 = "S";
|SI FEstado ! 0;    |GRABA 020#11n;     |FINSI;
|SI FEstado = 0;    |GRABA 020#11a;     |FINSI;

    #0#2  = #11#1;       || nombre
    #0#3  = #11#42;      || domicilio
    #0#4  = #11#43;      || numero
    #0#5  = #11#45;      || piso
    #0#6  = #11#49;      || localidad
    #0#7  = #11#47;      || cod.postal 1
    #0#8  = #11#48;      || cod.postal 2
    #0#9  = #11#50;      || provincia
    #0#10 = #11#13;      || cif
    #0#21 = (#11#63 * 1000) + #11#64;   || cod.admon.hacienda
|HAZ lee_hacienda;
    #0#111= #11#14;      || telefono 1
    #0#112= #11#15;      || telefono 2 (fax)
|MODULO runnom;
|SI runnom = "labempre";
    |PINDA #0#0;
|FINSI;
|FPRC;

|PROCESO lee_hacienda;
|ABRE #12;
    #12#0 = #11#63;
    #12#1 = #11#64;
|LEE 000#12=;
    #0#22 = #12#3;       || administracion
    #0#24 = #12#2;       || delegacion
|CIERRA #12;

|PINTA #0#22;
|PINTA #0#24;
|FPRC;

|PROCESO postal;
    #11#16 = "";
    trabacod1 = #11#9;
    trabacod1 = "00" + trabacod1;
    trabacod1 = trabacod1 % -102;
    trabacod2 = #11#10;
    trabacod2 = "000" + trabacod2;
    trabacod2 = trabacod2 % -103;
    #11#16 = trabacod1 + trabacod2;
|FPRC;

|PROCESO tipo8;
|CAMPO_ANCHO #nomconca#1, 1, 20;
|QUE_SISTEMA alfa1;
|DBASS agi;
|PATH_DAT #10 agi;

|ABRE #10;
    #10#0 = 1;
|LEE 000#10=;

|SI FSalida ! 0;
        aste = "*";
|SINO;
        aste = #10#2;
    |QBF aste;
    |SI aste ! "*";
            agi1 = #10#8; |QBF agi1;

            longi = agi1 % 0;
            num   = longi;
            num   = 100 + (num - 5);
            longi = num;
            agi2  = (agi1 % longi) + "pan/";

        |PATH_DAT #11 agi1;
        |PATH_PAN #11 agi2;

        |PATH_DAT #12 agi1;
        |PATH_PAN #12 agi2;
    |FINSI;
|FINSI;
|CIERRA #10;
|FPRC;
____________________________________/ BORRADO DE RELACIONES
|PROCESO tipo2; |TIPO 2;
    baja = FEntrada / 100;
    baja = baja ! 0;
|SI baja = 3;
    |HAZ aviso_em;
        em_borr = #0#0;
        tr_borr = 0;
    |SUB_EJECUTA "nomborr1.run";
    |SUB_EJECUTA "nomborr2.run";
    |HAZ e_aviso9;
|FINSI;
|FPRC;

|PROCESO aviso_em;
|PUSHV 05012380;
|HAZ e_color_f;
|CUADRO 09 30 11 71;
||            1234567890123456789012345678901234567890
|HAZ e_color_c;
|PINTA 1031, "         Borrando Relaciones ...        ";
|ATRI 0;
|FPRC;

|PROCESO e_color_c;        || activa el color caracter en avisos
|COLOR 14, 0;
|ATRI 0;
|FPRC;

|PROCESO e_color_f;        || activa el color graficos en avisos
|COLOR 4, 0;
|ATRI 0;
|FPRC;

|PROCESO e_aviso9;
|POPV;
|FPRC;

|PROCESO def45; |TIPO 7;
|SI #0#45 ! "S";|Y #0#45 ! "N";
        #0#45 = "N";
    |PINTA #0#45;
|FINSI;
|FPRC;

|PROCESO def46; |TIPO 7;
|SI #0#46 ! "S";|Y #0#46 ! "N";
        #0#46 = "N";
    |PINTA #0#46;
|FINSI;
|FPRC;

|PROCESO def47; |TIPO 7;
|SI #0#47 ! "1";|Y #0#47 ! "2";
        #0#47 = "1";
    |PINTA #0#47;
|FINSI;
|FPRC;

|PROCESO def48; |TIPO 7;
|SI #0#48 ! "1";|Y #0#48 ! "2";
        #0#48 = "2";
    |PINTA #0#48;
|FINSI;
|FPRC;

|PROCESO def49; |TIPO 7;
|SI #0#49 ! "S";|Y #0#49 ! "N";
        #0#49 = "S";
    |PINTA #0#49;
|FINSI;
|FPRC;

|PROCESO abreviado; |TIPO 0;
    alfa1 = Contenido;
|SI #0#45 = "S";|Y alfa1 ! #0Campo;
    |MENAV "    Revise las cuentas de cotizacion de los centros. Deberan ser unicas.";
|FINSI;
/*
|SI #0#49 = "S";|Y #0#45 = "S";|Y #0#46 = "N";|Y alfa1 ! #0Campo;
    |MENAV "    Si TC2 y TC2/1 se emite por centros y TC1 no: NO SE PODRA ABREVIAR EL TC2 ...";
|FINSI;
*/
|FPRC;

|PROCESO tc1_centros; |TIPO 7;
    #0#46 = #0#45;
    |PINTA #0#46;
    |C_M #0#46, 1, "N";
/*
|C_M #0#46, 1, "S";
|SI #0#45 = "N";
        #0#46 = "N";
    |PINTA #0#46;
    |C_M #0#46, 1, "N";
|FINSI;
*/
|FPRC;

|PROCESO tc1_atrasos; |TIPO 7;
|C_M #0#48, 1, "S";
|SI #0#47 = "2";
        #0#48 = "2";
    |PINTA #0#48;
    |C_M #0#48, 1, "N";
|FINSI;
|FPRC;

|PROCESO imagenes1; |TIPO 0;
     |SI FSalida = 13;
          aAlfa1 = #0#0; aAlfa1 = ("00000" + aAlfa1) % -105;
          aAlfa2 = "00000";
          orden = aAlfa1 + aAlfa2;
          pasa = "E";
          |SUB_EJECUTA "nombmp01";
     |FINSI;
|FPRC;

|PROCESO TipoIRPF; |TIPO 0;
     |SI #0#28 ! 0;|Y #0#28 ! 3;|Y #0#28 ! 4;
          |MENAV "    Error de entrada ...";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO eosm0030_1;
     |SI Campo = 2;
          #eosm0030#2 = #0Campo;
     |FINSI;
     |SI Campo = 15;
          #eosm0030#17 = #0Campo;
     |FINSI;
     |SI Campo = 18;
          #eosm0030#18 = #0Campo;
     |FINSI;
     |SI Campo = 111;
          #eosm0030#16 = #0Campo;
     |FINSI;
     |GRABA 020#eosm0030.a;
     |LIBERA #eosm0030;
|FPRC;

|DEFBUCLE eosm0030;
 #eosm0030#1;
 ;
 #0#0,    1;
 #0#0, 9999;
 be;
 NULL, eosm0030_1;
|FIN;

|PROCESO Cambia30; |TIPO 0;
     aAlfa1 = Contenido;
     |SI #0Campo ! aAlfa1;
          |BUCLE eosm0030;
     |FINSI;
     eaVengoDe = "labempre";
|FPRC;

|PROCESO EnBlanco; |TIPO 7;
     |SI #0Campo ! "S";|Y #0Campo ! "N";
          #0Campo = "N";
          |PINTA #0Campo;
     |FINSI;
|FPRC;

|PROCESO Aviso_Finiquito; |TIPO 0;
     |SI #0#39 = "1";
          alfa1 = "    La elaboracion de finiquito junto con el recibo del mes ";
          alfa1 = alfa1 + "puede ocasionar errores en las bases de cotizacion.";
          |MENAV alfa1;
          alfa1 = "    Se recomienda utilizar la opcion '2. Finiquito separado en recibo'";
          |MENAV alfa1;
     |FINSI;
|FPRC;

|PROCESO CambioCNAE;
     nModo = (FEntrada / 100) ! 0;
     aAlfa = #0Campo;
     |QBF aAlfa;
     |SI nModo = 2; |Y aAlfa ! aCNAE;
          |ABRE #nomcnahi;
          #nomcnahi#0 = #0#0;
          #nomcnahi#2 = 2009;
          #nomcnahi#4 = 00;
          |LEE 000#nomcnahi.=;
          FEstado = FSalida;
          aAlfa = #nomcnahi#3;
          |QBF aAlfa;
          |SI FEstado ! 0; |O aAlfa = "";
               |PUSHV 13392275;
               |ATRI R;
                         ||  3 4        5         6         7
                         ||  9012345678901234567890123456789012345
               |PINTA 1339, " ATENCION: la modificación del  CNAE ";
               |PINTA 1439, " para adaptarlo a las tablas de 2009 ";
               |PINTA 1539, " ha de realizarse mediante el proce- ";
               |PINTA 1639, " ceso  [Maestros/Principales/Tarifas ";
               |PINTA 1739, " de  cotización  AT/EP/Tranformación ";
               |PINTA 1839, " a CNAE 2009] para guardar los valo- ";
               |PINTA 1939, " res de 2008. Si se  modifica direc- ";
               |PINTA 2039, " tamente  este campo se pueden gene- ";
               |PINTA 2139, " rar  errores al calcular  atrasos y ";
               |PINTA 2239, " seguros sociales desde el histórico ";
               |ATRI 0;

               |PAUSA;
               |POPV;
               #0Campo = aCNAE;
               |PINTA #0Campo;
               #0#129 = aDesCNAE;
               |PINTA #0#129;

               aAlfa1 = #0Campo;
               aAlfa1 = aAlfa1 % 102;
               #0#11 = aAlfa1;
          |FINSI;
     |FINSI;
     |CIERRA #nomcnahi;
|FPRC;

|PROCESO CNAE;
     |ABRE #nomcnaec;
     aAlfa1 = #0Campo;
     |QBF aAlfa1;
     |SI aAlfa1 = "";
          #0#129 = "";
          |PINTA #0#129;
          aAlfa1 = "    Es obligatorio introducir el CNAE ";
          aAlfa1 = aAlfa1 + "para la correcta elaboracion de los Seguros Sociales.";
          |MENAV aAlfa1;
          |ERROR;
     |SINO;
          /*
          |SI aAlfa1 = "01";
               aAlfa1 = "    ATENCION: el codigo 01 no tiene asociada cotizacion. "
               aAlfa1 = aAlfa1 + "Debe elegir una de sus actividades asociadas";
               |MENAV aAlfa1;
               |ERROR;
          |SINO;
          */
               #nomcnaec#0 = #0#128;
               |LEE 000#nomcnaec.=;
               |SI FSalida ! 0;
                    aAlfa1 = "    ATENCION: el CNAE introducido no es válido ";
                    aAlfa1 = aAlfa1 + "o no está vigente.";
                    |MENAV aAlfa1;
                    |ERROR;
               |SINO;
                    aAlfa1 = aAlfa1 % 102;
                    #0#11 = aAlfa1;
               |FINSI;
          ||FINSI;
     |FINSI;
     |CIERRA #nomcnaec;
|FPRC;

|PROCESO RellenaCNAE; |TIPO 7;
     |ABRE #nomcnaec;
     aAlfa1 = #0#128;
     |QBF aAlfa1;
     |SI aAlfa1 = "52.";
          aAlfa1 = "52";
          #0#128 = "52";
          |PINTA #0#128;
     |FINSI;
     aCNAE = #0#128;
     |QBF aCNAE;
     aDesCNAE = #0#129;
/*
     || quito esta parte que con el cmabio de CNAEs de 2009 puede dar
     || problemas
     #nomcnaec#0 = aAlfa1;
     |LEE 000#nomcnaec.=;
     |SI FSalida ! 0; |O aAlfa1 = "";
          |SI #0#11 > 0; |Y #0#11 < 100;
               aAlfa1 = #0#11;
               aAlfa1 = ("000" + aAlfa1) % -102;

               #nomcnaec#0 = aAlfa1;
               |LEE 000#nomcnaec.=;
               |SI FSalida = 0;
                    #0#128 = #nomcnaec#0;
                    #0#129 = #nomcnaec#1;
                    |PINTA #0#128;
                    |PINTA #0#129;
               |FINSI;
          |FINSI;
     |FINSI;
*/
     |CIERRA #nomcnaec;
|FPRC;

|PROCESO Actividad; |TIPO 66;
     |HAZ SacaVentana;
|FPRC;

|PROCESO PintaActiv;
     alfa1 = #0#12;
     |QBF alfa1;
     |SI alfa1 = "";
          #0#12 = #0#129 % 140;
          |PINTA #0#12;
     |FINSI;
|FPRC;

|PROCESO CompActiv;
     alfa1 = #0#11;      || actividad
     alfa1 = "00" + alfa1;
     alfa1 = alfa1 % -102;

     alfa2 = #0#128;     || CNAE
     alfa2 = alfa2 % 102;

     |SI alfa1 ! alfa2;
          alfa3 =  "    ATENCION: La actividad de la empresa no puede per";
          alfa3 = alfa3 + "tenecer a un grupo distinto del CNAE";
          |MENAV alfa3;
          alfa3 = "    Pulse F11 y elija una actividad del grupo especificado";
          alfa3 = alfa3 + " en el CNAE de la empresa (" + alfa2 + ")";
          |MENAV alfa3;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo88emp; |TIPO 88;
     eaMensaCad = "9";
     |HAZ ControlCaducidad;
     |SI enErrorCad = 1;
         |PTEC 807;
         |ACABA;
     |FINSI;

     |HAZ ITIMSemp;
|FPRC;

|PROCESO ITIMSemp;
     |MODULO runnom; |QBF runnom; |SI runnom ! "labempre"; |ACABA; |FINSI;

     fFecha = ~;
     aAlfa = fFecha; aAlfa = aAlfa % -104;
     nElAny = aAlfa;
     aAlfa = fFecha; aAlfa = aAlfa % 402;
     nElMes = aAlfa;
     |HAZ ValoresITIMS;
|FPRC;

|PROCESO Activa64;
     |SI #0#127 > 0;
          |CAMPO_MODIFICA #0#64, 1, "S";
     |SINO;
          |CAMPO_MODIFICA #0#64, 1, "N";
     |FINSI;
|FPRC;

|PROCESO SSOnlineEmp;
     |SI nOpcion = 0;
          |ACABA;
     |FINSI;
     aAlfa1 = #0#0; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
     aAlfa2 = "00001";        || centro 1
     |SI nOpcion = 1;
          aAlfa = "labnct07;"+ "06" + aAlfa1 + aAlfa2;
     |FINSI;
     |SI nOpcion = 2;
          aAlfa = "labnct07;"+ "09" + aAlfa1 + aAlfa2;
     |FINSI;
     |SI nOpcion = 3;
          aAlfa = "labnct07;"+ "11" + aAlfa1 + aAlfa2;
     |FINSI;
     |SI nOpcion = 4;
          aAlfa = "labnct07;"+ "13" + aAlfa1 + aAlfa2;
     |FINSI;
     |SI nOpcion = 5;
          aAlfa = "labnct07;"+ "15" + aAlfa1 + aAlfa2;
     |FINSI;

     |SUB_EJECUTA aAlfa;
|FPRC;

|PROCESO MenuREDEmp;
     eaVengoDe = "labempre";
     |VARIABLES; {20} aPopup = ""; |FINSI;
     aPopup1 = "-1";  || Si fuera -1 en la posicion
     aPopup2 = 4;
     aPopup3 = "1. Situación de la empresa";
     aPopup4 = "2. Certificado situación de cotización";
     aPopup5 = "3. Afiliados en el Cod. Cta. Coticación";
     aPopup6 = "4. Consulta de trabajadores en alta";
     aPopup7 = "5. Consulta de Convenios";
     IaPopup = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     nOpcion = FSalida;
     |HAZ SSOnlineEmp;
|FPRC;

|PROCESO BotonREDEmp; |TIPO 8;
     |MODULO runnom; |QBF runnom; |SI runnom ! "labempre"; |ACABA; |FINSI;

     |HAZ tipo8;

     ||CONTROL_SIMPLE 0, "BOTON,Seguridad Social Online", 0760, 0777, MenuREDEmp;
     |CONTROL_SIMPLE 0, "BOTON,Seguridad Social Online", 0760, 0777, ConectaSS;
     enBoton = FSalida;
     |ESTADO_CONTROL enBoton, "ENABLE";
     swBotonSiNo = 1;
|FPRC;

|PROCESO FinBotonREDEmp; |TIPO 9;
     |MODULO runnom; |QBF runnom; |SI runnom ! "labempre"; |ACABA; |FINSI;

     |FIN_CONTROL_WINDOWS enBoton;
     |PULSATECLA;
     swBotonSiNo = 0;
|FPRC;

|PROCESO PonBotonREDEmp; |TIPO 14;
     |MODULO runnom; |QBF runnom; |SI runnom ! "labempre"; |ACABA; |FINSI;

     |SI swBotonSiNo = 0;
          |HAZ BotonREDEmp;
     |FINSI;
|FPRC;

|PROCESO QuitaBotonEmp; |TIPO 14;
     |MODULO runnom; |QBF runnom; |SI runnom ! "labempre"; |ACABA; |FINSI;

     |SI swBotonSiNo = 1;
          |HAZ FinBotonREDEmp;
     |FINSI;
|FPRC;

|PROCESO Tipo7C113FLab;   |TIPO 7;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |PUSHV 0501 2380;

     #nomw0021#0 = #labempre#113 % 104;
     #nomw0021#1 = #labempre#113 % 504;
     #nomw0021#2 = #labempre#113 % 1110;
     #nomw0021#3 = #labempre#113 % 0902;
     #nomw0021#4 = #labempre#134;
     #nomw0021#5 = #labempre#135;
     #nomw0021#6 = #labempre#136;

     |PINPA #0#nomw0021;
     |PINDA #0#nomw0021;
     |ENDATOS #1#nomw0021;
     |SI FSalida = 0;
         #labempre#113 = #nomw0021#0 + #nomw0021#1 + #nomw0021#3 + #nomw0021#2;
         #labempre#134 = #nomw0021#4;
         #labempre#135 = #nomw0021#5;
         #labempre#136 = #nomw0021#6;
         |PINTA #labempre#113;
     |FINSI;

     |POPV;

     |PINTA #labempre#113;
|FPRC;
