|FICHEROS;
    labparan #0;
    labempre #10;        || empresas AGINOM
    labcentr #11;        || centros
    labtraba #14;        || trabajadores AGINOM
    nomcenes #15;        || centros especiales
    nomequiv;            || tabla equivalencia contratos nuevos
    nomhicca;            || para leer bases y dias cotizacion del trabajador
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    &nombre = "";
    &apell1 = "";
    &apell2 = "";
    &ssesp = "";
    &aEsp = "";
    &aExt = "";

     &aIPF = "";
     fFecAcc = @;
     fFecAnt = @;
     nFecAcc = 0;
     nFecAnt = 0;
     nFecDif = 0;
     fFecDif = @;
     aFecDif = "";
     &nMeses = 0;
     &nDias = 0;
     &aDiaSemana = "";
     &aTestigos = "";
     &p_mes = "";

     men1 = "0000ATENCION !!! Fecha erronea";
     dia1 = "";
     mes1 = "";
     any1 = "";
     dia = 0;
     mes = 0;
     any = 0;
     tope = 0;
     sw = 0;
     fec = "";
     blanco = "          ";
     puntos = "..........";
     mod = 0;
     &aContrato = "";
     &aRegimen = "";
     BaseHE = 0;
     i = 0;
|FIN;

|PROCESO LeeTrab;
     |ABRE #14;
     #14#0 = #0#1;
     #14#1 = #0#2;
     |LEE 000#14=;
     |CIERRA #14;
|FPRC;

|PROCESO suma;
     |PARA i = 1; |SI i [ 12; |HACIENDO i = i + 1;
          #nomhicca#0 = #0#1;
          #nomhicca#1 = #0#2;
          #nomhicca#66 = nume4;     || a¤o
          #nomhicca#2 = nume1;     || mes
          #nomhicca#3 = 0;         || tipo
          |LEE 000#nomhicca.=;
          |SI FSalida = 0;
               BaseHE = BaseHE + #nomhicca#41 + #nomhicca#42;
          |FINSI;
          |SI nume1 < 12;
               nume1 = nume1 + 1;
          |SINO;
               nume1 = 1;
               nume4 = nume4 + 1;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO LeeBases; |TIPO 7;
     alfa1 = #0#3;            || fecha accidente
     alfa2 = alfa1 % 402;     || mes accidente
     alfa3 = alfa1 % 704;     || a¤o accidente
     nume2 = alfa2;
     nume3 = alfa3;
     nume2 = nume2 - 1;
     |SI nume2 = 0;           || si es enero, el mes anterior es Diciembre
          nume2 = 12;         || del a¤o anterior
          nume3 = nume3 - 1;
     |FINSI;
     nume3 = nume3 - 2000;
     |ABRE #nomhicca;
     #nomhicca#0 = #0#1;
     #nomhicca#1 = #0#2;
     #nomhicca#2 = nume2;
     #nomhicca#3 = 0;
     #nomhicca#66 = nume3;
     |LEE 000#nomhicca.=;
     |SI FSalida = 0;
          #0#36 = #nomhicca#40;
          #0#37 = #nomhicca#8;
     |FINSI;
     nume1 = nume2 + 1;  || mes desde (12 meses anteriores)
     nume4 = nume3 - 1;  || a¤o desde (12 meses anteriores)
     |SI nume1 = 13;
          nume1 = 1;
          nume4 = nume4 + 1;
     |FINSI;
     |HAZ suma;
     #0#38 = BaseHE;
     |PINTA #0#36;
     |PINTA #0#37;
     |PINTA #0#38;
     |CIERRA #nomhicca;
|FPRC;

|PROCESO si4; |TIPO 0;
|SI #0#4 = 4;
        alfa1 = "S";
|SINO;
        alfa1 = "N";
|FINSI;
|C_M #0#5, 1, alfa1;
|C_M #0#6, 1, alfa1;
|C_M #0#7, 1, alfa1;
|FPRC;
____________________________________/
|PROCESO mayus; |TIPO 0;
    #0Campo = #0Campo > #0Campo;
|PINTA #0Campo;
|FPRC;
____________________________________/
|PROCESO CodigosNuevos;
     aContrato = #14#45;
     |QBF aContrato;
     aContrato = ("000" + aContrato) % -103;
     |ABRE #nomequiv;
     #nomequiv#0 = aContrato;
     |LEE 000#nomequiv.=;
     |SI FSalida = 0;
          |SI #labtraba#42 = "X";|O #labtraba#42 = "T";|O #labtraba#42 = "P";
               aContrato = #nomequiv#2;      || tiempo parcial
          |SINO;
               aContrato = #nomequiv#1;      || tiempo completo
          |FINSI;
     |FINSI;
     |CIERRA #nomequiv;
|FPRC;

|PROCESO Pais;
||             NACIONALIDAD

     alfa1 = #14#7;
     alfa1 = alfa1 % 101;
     |SI alfa1 ! "X"; |Y alfa1 ! "Y"; |Y alfa1 ! "Z";
          aEsp = "X";
     |SINO;
          aExt = "X";
     |FINSI;
     |SI alfa1 ] "0";|Y alfa1 [ "9";
          aIPF = "1";            || tipo identificacion DNI
     |SINO;
          |SI alfa1 ! "X"; |Y alfa1 ! "Y"; |Y alfa1 ! "Z";
               aIPF = "6";        || tipo identificacion EXTRANJERO
          |SINO;
               |SI alfa1 ] "a";|Y alfa1 [ "z";
                    aIPF = "2";    || tipo identificacion PASAPORTE
               |FINSI;
          |FINSI;
     |FINSI;

     |SI aExt = "X";
          #0#95 = #14#76;
          |PINTA #0#95;
     |FINSI;
|FPRC;

|PROCESO varios;

     |HAZ CodigosNuevos;

     ||   REGIMEN

     aRegimen = "";
     |SI #14#247 = 111; aRegimen = "1"; |FINSI;
     |SI #14#247 = 611; aRegimen = "6"; |FINSI;
     |SI #14#247 = 613; |O #14#247 = 163;
           aRegimen = "7";
     |FINSI;
     |SI #14#247 = 800; aRegimen = "8"; |FINSI;
     |SI #14#247 = 911; aRegimen = "9"; |FINSI;
     |SI aRegimen = ""; aRegimen = "1"; |FINSI;

     |ABRE #11;
     |DDEFECTO #11;
     #11#0 = #14#0;
     #11#1 = #14#77; || lee centro
     |LEE 000#11=;
     ||ssesp = #11#10;   || s.s. del centro
     #10#13 = #11#10;    || s.s. del centro
     #10#3 = #11#3;      || domicilio
     #10#4 = #11#4;      || numero
     #10#5 = #11#5;      || piso
     #10#6 = #11#6;      || localidad
     #10#7 = #11#7;      || c.p. provincia
     #10#8 = #11#8;      || c.p. localidad
     #10#9 = #11#9;      || provincia
     #10#14 = #11#11;    || nro. trab. centro
     |CIERRA #11;


     |SI #14#45 = "087";|O #14#45 = "097";|O #14#45 = "085";|O #14#45 = "421";
                        |O #14#45 = "064";|O #14#45 = "065";
         |ABRE #15;
         |DDEFECTO #15;
             #15#0 = #14#0;
             #15#13= #14#77;
         |SI #14#45 = "097";|O #14#45 = "085";|O #14#45 = "421";
                 #15#1 = "87";
         |SINO;
             |SI #14#45 = "065";
                     #15#1 = "64";
             |SINO;
                     #15#1 = #14#45 % 2;
             |FINSI;
         |FINSI;
         |LEE 000#15=;
             ssesp = #15#10;
         |CIERRA #15;
     |FINSI;

         alfa1 = #14#2;  |QBF alfa1;
         alfa2 = alfa1 % 0;   para2 = alfa2;
         elsw4 = 0;
         apell1 = "";
         apell2 = "";
         nombre = "";
     |PARA para1 = 1; |SI para1 [ para2; |HACIENDO para1 = para1 + 1;
             nume2 = (para1 * 100) + 1;
             alfa3 = alfa1 % nume2;
         |SI alfa3 = " ";|O alfa3 = ",";
             |SI apell1 ! "";
                     elsw4 = elsw4 + 1;
             |FINSI;
             |SI elsw4 ] 2;|Y nombre ! "";
                     nombre = nombre + alfa3;
             |FINSI;
         |SINO;
             |SI elsw4 = 0;  apell1 = apell1 + alfa3; |FINSI;
             |SI elsw4 = 1;  apell2 = apell2 + alfa3; |FINSI;
             |SI elsw4 ] 2;  nombre = nombre + alfa3; |FINSI;
         |FINSI;
     |SIGUE;

||        ANTIGUEDAD EN PUESTO DE TRABAJO (MESES Y DIAS)
     fFecAcc = #0#3;
     fFecAnt = #0#93;
     nFecAcc = fFecAcc;
     nFecAnt = fFecAnt;
     nFecDif = nFecAcc - nFecAnt;
     fFecDif = nFecDif;
     aFecDif = fFecDif;

     alfa1 = aFecDif % 102;
     alfa2 = aFecDif % 402;
     alfa3 = aFecDif % 704;

     nume1 = alfa1;
     nume2 = alfa2;
     nume3 = alfa3;

     nume3 = nume3 * 12;
     nMeses = nume2 + nume3 - 1;
     nDias = nume1;

     |SI nDias ] 30;
          nMeses = nMeses + 1;
          nDias = nDias - 30;
     |FINSI;

     ||        DIA DE LA SEMANA

     fech1 = #0#3;
     fech2 = fech1 % 1;
     alfa2 = fech2;
     alfa2 = alfa2 % 1102;
     |SI alfa2 = "Lu";   aDiaSemana = "Lunes";    |FINSI;
     |SI alfa2 = "Ma";   aDiaSemana = "Martes";   |FINSI;
     |SI alfa2 = "Mi";   aDiaSemana = "Miercoles";|FINSI;
     |SI alfa2 = "Ju";   aDiaSemana = "Jueves";   |FINSI;
     |SI alfa2 = "Vi";   aDiaSemana = "Viernes";  |FINSI;
     |SI alfa2 = "Sa";   aDiaSemana = "Sabado";   |FINSI;
     |SI alfa2 = "Do";   aDiaSemana = "Domingo";  |FINSI;

     ||        TESTIGOS

     aTestigos = #0#10;
     |QBF aTestigos;
     |SI aTestigos = ""; aTestigos = "N";    |FINSI;

     ||        MES EN LETRA

     fech1 = ~;
     alfa1 = fech1;
     alfa1 = alfa1 % 402;
     nume1 = alfa1;
     |SI nume1 =  1;  p_mes = "ENERO     ";  |FINSI;
     |SI nume1 =  2;  p_mes = "FEBRERO   ";  |FINSI;
     |SI nume1 =  3;  p_mes = "MARZO     ";  |FINSI;
     |SI nume1 =  4;  p_mes = "ABRIL     ";  |FINSI;
     |SI nume1 =  5;  p_mes = "MAYO      ";  |FINSI;
     |SI nume1 =  6;  p_mes = "JUNIO     ";  |FINSI;
     |SI nume1 =  7;  p_mes = "JULIO     ";  |FINSI;
     |SI nume1 =  8;  p_mes = "AGOSTO    ";  |FINSI;
     |SI nume1 =  9;  p_mes = "SEPTIEMBRE";  |FINSI;
     |SI nume1 = 10;  p_mes = "OCTUBRE   ";  |FINSI;
     |SI nume1 = 11;  p_mes = "NOVIEMBRE ";  |FINSI;
     |SI nume1 = 12;  p_mes = "DICIEMBRE ";  |FINSI;



|FPRC;

|PROCESO situacion; |TIPO 7;
     |PUSHV 1216 1679;
     |CUADRO 1216 1679;
     |ATRI R;
     ||             8901234567890123456789012345678901234567890123456789012345678
     |PINTA 1317, "              -     Situacion Profesional     -               ";
     |PINTA 1417, " 1 - Asalariado sector publico   3 - Autonomo sin asalariados ";
     |PINTA 1517, " 2 - Asalariado sector privado   4 - Autonomo con asalariados ";
     |ATRI N;
|FPRC;

|PROCESO modalidades; |TIPO 7;
     |PUSHV 1530 2379;
     |CUADRO 1530 2379;
     |ATRI R;
     ||            3        4         5         6         7
     ||            123456789012345678901234567890123456789012345678
     |PINTA 1631, "  -  Modalidades de organizacion preventiva  -  ";
     |PINTA 1731, "1 - Asuncion presonal por el empresario         ";
     |PINTA 1831, "2 - Servicio de prevencion propio               ";
     |PINTA 1931, "3 - Servicio de prevencio ajeno                 ";
     |PINTA 2031, "4 - Trabajador(es) designado(s)                 ";
     |PINTA 2131, "5 - Servicio de prevencion mancomunado          ";
     |PINTA 2231, "6 - Ninguna                                     ";
     |ATRI N;
|FPRC;

|PROCESO restaura;
     |POPV;
|FPRC;

|PROCESO verifech; |TIPO 0;
     fec = #0Campo; sw = 0;
     |SI fec ! blanco; |Y fec ! puntos;
          dia1 = fec % A102;
          mes1 = fec % A402;
          any1 = fec % A704;
          dia = dia1; mes = mes1; any = any1;
          |SI dia ! 0; |Y mes ! 0; |Y any ! 0;
               |SI mes [ 12; |Y mes ] 1;
                    |SI mes = 1; |O mes = 3; |O mes = 5; |O mes = 7; |O mes = 8; |O mes = 10; |O mes = 12;
                         tope = 31;
                    |SINO;
                         |SI mes = 2;
                              mod = any $ 4;
                                   |SI mod = 0;
                                        tope = 29;
                                   |SINO;
                                        tope = 28;
                                   |FINSI;
                         |SINO;
                              tope = 30;
                         |FINSI;
                    |FINSI;
                    |SI dia [ tope;
                         sw = 1;
                    |FINSI;
               |FINSI;
          |FINSI;
     |SINO;
          sw = 1;
     |FINSI;
     |SI sw = 0;
          |MENAV men1;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO imprime;
|IMPRE 0;
|SI FSalida = 0;
    |HAZ varios;
    ||SI #0#40=1;
          |INFOR "labparan";
          ||INFOR "regla";
          |SI FSalida = 0;
               |PRINT;
               |PIEINF;
          |SINO;
               |MENAV "    Problemas con el informe 'labparan' ...";
          |FINSI;
          |FININF;
/*
    |SINO;
          |INFOR "labpara1";
          |SI FSalida = 0;
               |PRINT;
               |PIEINF;
          |SINO;
               |MENAV "    Problemas con el informe 'labpara1' ...";
          |FINSI;
          |FININF;
    |FINSI;
*/
|SINO;
    |MENAV "    Proceso abortado ...";
|FINSI;
|FINIMP;
|FPRC;

|PROGRAMA;
|CLS;
|CABEZA "Partes Accidente";
|PINPA #0#0;
|SI elsw1 = 0;
    |DELINDEX #0;
    |DDEFECTO #0;
    |ABRE #0;
    |LEE 101#0p;
    |SI FSalida ! 0;    |GRABA 020#0b; |FINSI;
    |PINDA #0#0;
    |ENDATOS #1#0;
    |SI FSalida = 0;
        |HAZ imprime;
        |GRABA 020#0a;
    |FINSI;
    |LIBERA #0;
    |CIERRA #0;
|FINSI;
|FPRO;
