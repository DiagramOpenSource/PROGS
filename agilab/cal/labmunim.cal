|FICHEROS;
    labmunim #0;
    labmunic #1;
     labprovi;
|FIN;

|VARIABLES;
     nCanal = 0;
     i      = 0;
     j      = 0;
     nCont  = 0;
     nLongi = 0;

     aDirFich = "";
     aLinea   = "";
     aCodProv = "";
     aNomProv = "";
     aCodMuni = "";
     aNomMuni = "";
     aCP      = "";
     aLongi   = "";
     aCadena  = "";
     aPos     = "";
     aCaracter= "";

     nCargados = 0;
     aAlfa = "";
     FEstado = 0;
|FIN;

|PROCESO meteMunicipios; |TIPO 3;
     |ABRE #labprovi;
     |SI #0#0 = "S";
          |DELINDEX #labmunic;
          |ABRE #labmunic;

          aDirFich = "";
          |DBASE aDirFich;
          aDirFich = aDirFich + "/municipios.txt";
          |XABRE "U", aDirFich, nCanal;

          ||nos quedamos con las cabeceras del txt
          |XLEE nCanal, 200, aLinea;

          |XLEE nCanal, 200, aLinea;
          |PARA; |SI FSalida ] 0; |HACIENDO;
                    |QBF aLinea;
                    aCodProv = aLinea % 102;
                    aCP = aLinea % -105;

                    |HAZ SacaCampos;

                    ||Ahora ya podemos ir metiendo en la tabla los campos del txt municipios

                    |DDEFECTO #labmunic;
                    #labmunic#4 = aCP;
                    #labmunic#1 = aCodProv + aCodMuni;

                    |LEE 101#labmunic.=;
                    FEstado = FSalida;


                    aNomProv = "";
                    #labprovi#0 = aCodProv;
                    |LEE 000#labprovi.=
                    |SI FSalida = 0;
                         aNomProv = #labprovi#1;
                    |FINSI;

                    #labmunic#2 = aNomProv;
                    #labmunic#3 = aNomMuni;
                    |SI FEstado = 0;
                         |GRABA 020#labmunic.a;
                    |SINO;
                         |GRABA 020#labmunic.n;
                    |FINSI;
                    |LIBERA #labmunic;

                    nCargados = nCargados + 1;
                    aAlfa = nCargados;
                    aAlfa = "Progreso: " + aAlfa + " de 14716";
                    |PINTA 1927, aAlfa;

                    |XLEE nCanal, 200, aLinea;
               |SIGUE;
         |XCIERRA nCanal;
    |FINSI;
    |CIERRA #labprovi;
    |CIERRA #1;
|FPRC;

|PROCESO SacaCampos;
     aLongi = aLinea % 0;
     nLongi = aLongi;

     aCadena = "";

     nCont = 1;

     ||hacemos un for que vaya cogiendo todos los caracteres del fichoer menos los 3 primeros (codProv + "#")
     |PARA j = 4; |SI j [ nLongi; |HACIENDO j = j + 1;
          aPos = j + "01";
          aCaracter = aLinea % aPos;
          |SI aCaracter ! "#";
               aCadena = aCadena + aCaracter;
          |SINO;
               |SI nCont = 1;
                    aNomProv = aCadena;
                    aCadena = "";
               |FINSI;
               |SI nCont = 2;
                    aCodMuni = aCadena;
                    aCadena = "";
               |FINSI;
               |SI nCont = 3;
                    aNomMuni = aCadena;
                    aCadena = "";
               |FINSI;
               nCont = nCont + 1;
          |FINSI;
     |SIGUE;
|FPRC;
