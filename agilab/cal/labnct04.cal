|FICHEROS;
     labnct04 #0;        || pantalla
     labnct05;           || registro netcontrata

     nomgruec;           || grupos de empresas Cabs.
     nomgruel;           || grupos de empresas Lins.
     labtraba #2;
     labnct14 #100,MANTE;
     labempre;
     labcontr;
     nomconca;
     labmunic #6;

     &regisnif@ #712;
|FIN;

|VARIABLES;
     avisa_mes = 0;
     htipo = 0;
     meses = "ENERO     FEBRERO   MARZO     ABRIL     MAYO      JUNIO     JULIO     AGOSTO    SEPTIEMBREOCTUBRE   NOVIEMBRE DICIEMBRE ";
     salte_1 = 0;
     salte_2 = 0;

     &nElMesIRPF = 0;         || para 'nomir008'
     &nElAnyIRPF = 0;         || para 'nomir008'
     &nDiferencias = 0;   || para 'nomir008'
     &nElMes = 0;         || para la nomina
     &nElAny = 0;         || para la nomina
     swActiva = 0;
     swPorGrupos = 0;
     nGrupo = 0;
     nCampoG = 0;
     &eaVengoDe = "";

     &nEmpAUTO = 0;
     &nEmpAUTO_D = 0;
     &nEmpAUTO_H = 0;
     &nTraAUTO = 0;
     &nTraAUTO_D = 0;
     &nTraAUTO_H = 0;
     &nMesAUTO = 0;
     &nAnyAUTO = 0;
     &aParam = "";
     &nEnEmpresa = 0;
     &nPaso = 0;
     &swCompAjuste = 0;
     &swSegPasada = 0;
     &nRetEstAnu = 0;    || retencion estimada anual
     &swCalPagas = "S";
     aAlfa1 = "";
     aAlfa2 = "";
     alfa1 = "";
     alfa2 = "";
     nume1 = 0;
     nume2 = 0;
     para1 = 0;
     swfuera = 0;
     &nMes = 0;
     &nAny = 0;
     &nDesdeEmp = 0;
     &nHastaEmp = 0;
     &nDesdeTra = 0;
     &nHastaTra = 0;
     seleccio = 0;
     aAlfa = "";
     p_mes = "";
     fecsys = @;
     mesnum = 0;
     anynum = 0;
     guarda = 0;
     topemes = 0;
     aFabre = "";
     aBase = "";
     aMas = "";
     aFich = "";
     &nTopemes = 0;
     &nEmp = 0;
     &nTra = 0;
     &aHora = "";
     aDef = "";
     FCargaDef = "";
     nCampo1 = 0;
     &swEdicion = 0;

     nOpcion = 0;
     {-1}menu = "";
     menu1 = "2400";
     menu2 = "1";
     menu3 = "";
     menu4 = "VACI";
     menu5 = "Volver a incid.";
     menu6 = "Aceptar y continuar";
     menu7 = "Cancelar proceso";
     menu8 = "Imprimir informe";

     aEstadoNomgirli = "";
     nNroIncid = 0;
     &swStopIRPF = 0;
     nNume1 = 0;
     &nIRPFMes = 0;
     &swBajaEsteMes = 0;
     nMesBaja = 0;
     nAnyBaja = 0;
     FEstado = 0;

     aFecha = "";
     aFechaAlta = "";
     fFechaLimite = @;
     fFechaAlta = @;
     swSigue = 0;
     &aCadena = "";
     &eaNom   = "";
     &eaApel1 = "";
     &eaApel2 = "";
     &eaCodNif = "";
     nHorasMax = 0;
     nNume = 0;
     &aCodPos = "";
     &nContMuni = 0;
     &aCodMuni = "";
     nHandle = 0;
     aRuta = "";
     aFichero = "";
     nNivel = 0;
     nNumMov = 0;
     nNumEmp = 0;
     aMuni = "";
     nMensajeF5 = 1;
     aCodigoMunicipio = "";
     nJornada = 0;
     aUsuario = "";
     fFecha = @;
     &runnom = "labnct04";
|FIN;

|INCLUYE nomcal_4;

|PROCESO proceso;
     || por compatibilidad con el incluye nomcal_4
|FPRC;

|PROCESO CuentaMunicipio;
     nContMuni = nContMuni + 1;
     aCodMuni = #6#1;
|FPRC;

|DEFBUCLE CuentaMunicipio;
     #6#1;
     ;
     aCodPos, "00000";
     aCodPos, "99999";
     ;
     NULL,CuentaMunicipio;
|FIN;

|PROCESO BuscaMunicipio;
     aAlfa2 = #6#3;            || aAlfa2 = municipio de labmunic
     |QBT aAlfa2;
     aMuni = #labempre#6;   || aMuniEmp = municipio de labempre
     aMuni = aMuni > "";
     |QBT aMuni;

     aAlfa = aMuni - aAlfa2;
     |SI aAlfa ! aMuni;
          ||aAlfa2 esta en aMuni
          aCodMuni = #6#1;
          nContMuni = nContMuni + 1;
     |FINSI;
|FPRC;

|DEFBUCLE BuscaMunicipio;
     #6#1;
     ;
     aCodPos, "00000";
     aCodPos, "99999";
     ;
     NULL,BuscaMunicipio;
|FIN;

|PROCESO min_labmunic;
     #6#4 = aCodPos;
     #6#1 = "00000";
|FPRC;

|PROCESO max_labmunic;
     #6#4 = aCodPos;
     #6#1 = "99999";
|FPRC;

|PROCESO CodigoMuni;
     nContMuni = 0;
     |BUCLE CuentaMunicipio;

     |SI nContMuni > 1;
          nContMuni = 0;
          |BUCLE BuscaMunicipio;
          |SI nContMuni > 1;
               nMensajeF5 = 1;
               aCodigoMunicipio = "";
               |SUB_EJECUTA "labmunic";
               |SI aCodigoMunicipio ! "";
                    aCodMuni = aCodigoMunicipio;
                    nContMuni = 1;
               |FINSI;
          |FINSI;
     |FINSI;

     |SI nContMuni = 0;
          nMensajeF5 = 1;
          aCodigoMunicipio = "";
          |SUB_EJECUTA "labmunic";
          |SI aCodigoMunicipio ! "";
               aCodMuni = aCodigoMunicipio;
               nContMuni = 1;
          |FINSI;
     |FINSI;
     ||ahora se supone que en aCodMuni tenemos el codigo del municipio
|FPRC;

|PROCESO TipoDocumento;
     aAlfa = #labempre#10; |QBF aAlfa;
     aAlfa1 = aAlfa % 0101;
     |SI aAlfa1 ] "0"; |Y aAlfa1 [ "9";
          aAlfa = "NF";
     |SINO;
          |SI aAlfa1 = "X"; |O aAlfa1 = "Y"; |O aAlfa1 = "Z";
               aAlfa = "NI";
          |SINO;
               aAlfa = "C";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Registro;
     #labnct05#0 = #labempre#0;
     |LEE 101#labnct05.=;
     FEstado = FSalida;

     #labnct05#1 = #labempre#10;
     #labnct05#2 = #labempre#13;

     aUsuario = Usuario % 810;
     |QBF aUsuario;
     #labnct05#7 = aUsuario;       || usuario

     fFecha = ~;
     aFecha = fFecha;
     #labnct05#8 = fFecha;         || fecha

     |HORA aHora;
     |QBF aHora;
     aHora = aHora % 105;
     #labnct05#9 = aHora;          || hora
     #labnct05#10 = "A";           || para el alta
     |SI aParam = "";
          #labnct05#11 = "labnct04";    || desde este proceso
     |SINO;
          #labnct05#11 = "labempre";    || desde alta de empresas
     |FINSI;
     #labnct05#12 = aFichero;      || Nombre de fichero desde el que fue generado
     |SI FEstado = 0;
          |GRABA 020#labnct05.a;
     |SINO;
          |GRABA 020#labnct05.n;
     |FINSI;
     |LIBERA #labnct05;
|FPRC;

|PROCESO Linea;
     |QBF aAlfa;
     aCadena = aAlfa;
     |HAZ QuitaRaros;
     |SI nNivel = 1;
          aCadena = aCadena;
     |FINSI;
     |SI nNivel = 2;
          aCadena = "  " + aCadena;
     |FINSI;
     |SI nNivel = 3;
          aCadena = "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 4;
          aCadena = "  " + "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 5;
          aCadena = "  " + "  " + "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 6;
          aCadena = "  " + "  " + "  " + "  " + "  " + aCadena;
     |FINSI;
     aCadena = aCadena + &13 + &10;
     aAlfa = aCadena;
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO DatosAlta;
     nHorasMax = 40;
     |ABRE #nomconca;
     #nomconca#0 = #labtraba#87;
     |LEE 000#nomconca.=;
     |SI FSalida = 0;
          |SI #nomconca#162 ! 0;
               nHorasMax = #nomconca#162;
          |FINSI;
     |FINSI;
     |CIERRA #nomconca;

     nNivel = 3;
     aAlfa = "<DATOS_ALTA_AFILIACION>";
     |HAZ Linea;

     nNivel = 4;
     aAlfa1 = #labtraba#247; |QBF aAlfa1;
     aAlfa1 = ("0000" + aAlfa1) % -104;
     aAlfa2 = #labempre#13; |QBF aAlfa2;
     aAlfa = "<CCC>" + aAlfa1 + aAlfa2 + "</CCC>";
     |HAZ Linea;

     aAlfa = #labtraba#45;
     aAlfa = "<CONTRATO>" + aAlfa + "</CONTRATO>";
     |HAZ Linea;

     aAlfa1 = #labtraba#36; |QBF aAlfa1;
     |SI aAlfa1 ! ""; |Y aAlfa1 ! "          "; |Y aAlfa1 ! "..........";
          aAlfa = (aAlfa1 % 102) + "/" + (aAlfa1 % 402) + "/" + (aAlfa1 % -104);
     |FINSI;
     aAlfa = "<FECHA_ALTA>" + aAlfa + "</FECHA_ALTA>";
     |HAZ Linea;

     aAlfa = #labtraba#33;
     |QBF aAlfa;
     aAlfa = ("00" + aAlfa) % -102;
     aAlfa = "<GRUPO_TARIFA>" + aAlfa + "</GRUPO_TARIFA>";
     |HAZ Linea;

     nNume = #labtraba#234;
     nJornada = 0;
     |SI nNume ! 0;
          nJornada = nNume / nHorasMax;
          nJornada = (nJornada * 1000) ! 0;
          aAlfa = nJornada;
     |SINO;
          aAlfa = "";
     |FINSI;
     |SI aAlfa ! "";
          aAlfa = "<COEFICIENTE>" + aAlfa + "</COEFICIENTE>";
          |HAZ Linea;
     |FINSI;

     nNivel = 3;
     aAlfa = "</DATOS_ALTA_AFILIACION>";
     |HAZ Linea;
|FPRC;

|PROCESO Domicilio_Social;
     eaCodNif = #labempre#10;
     |HAZ LeeNifs;

     nNivel = 3;
     aAlfa = "<DOMICILIO_SOCIAL>";
     |HAZ Linea;

     nNivel = 4;

     aAlfa = #712#3;  |QBF aAlfa;
     aAlfa = "<NUMERO>" + aAlfa + "</NUMERO>";
     |HAZ Linea;

     aAlfa = #712#16;  |QBF aAlfa;
     aAlfa = "<ESCALERA>" + aAlfa + "</ESCALERA>";
     |HAZ Linea;

     aAlfa = #712#11;  |QBF aAlfa;
     aAlfa = "<PUERTA>" + aAlfa + "</PUERTA>";
     |HAZ Linea;

     aAlfa = #712#9;  |QBF aAlfa;
     aAlfa = "<TIPO_VIA>" + aAlfa + "</TIPO_VIA>";
     |HAZ Linea;

     aCodPos = (("00" + #labtraba#8) % -102) + (("000" + #labtraba#9) % -103);
     |HAZ CodigoMuni;
     |SI nContMuni = 0;
          aAlfa1 = "";
     |SINO;
          aAlfa1 = aCodMuni;
     |FINSI;
     aAlfa = "<CODIGO_MUNICIPIO>" + aAlfa1 +  "</CODIGO_MUNICIPIO>";
     |HAZ Linea;

     aAlfa = #712#10;  |QBF aAlfa;
     aAlfa = "<PISO>" + aAlfa + "</PISO>";
     |HAZ Linea;

     aAlfa = #712#2;  |QBF aAlfa;
     aAlfa = "<VIA>" + aAlfa + "</VIA>";
     |HAZ Linea;

     nNivel = 3;
     aAlfa = "</DOMICILIO_SOCIAL>";
     |HAZ Linea;
|FPRC;

|PROCESO Representante;
     nNivel = 3;
     aAlfa = "<REPRESENTANTE>";
     |HAZ Linea;

     nNivel = 4;
     aAlfa = #labempre#17;
     aAlfa1 = aAlfa % 103;
     |SI aAlfa1 = "...";
          aAlfa = "";
     |FINSI;
     aAlfa = "<DNI>" + aAlfa + "</DNI>";
     |HAZ Linea;

     aAlfa = #labempre#15;
     aAlfa1 = aAlfa % 103;
     |SI aAlfa1 = "...";
          aAlfa = "";
     |FINSI;
     aAlfa = "<NOMBRE>" + aAlfa + "</NOMBRE>";
     |HAZ Linea;

     aAlfa = #labempre#18;
     aAlfa1 = aAlfa % 103;
     |SI aAlfa1 = "...";
          aAlfa = "";
     |FINSI;
     aAlfa = "<CARGO>" + aAlfa + "</CARGO>";
     |HAZ Linea;

     nNivel = 3;
     aAlfa = "</REPRESENTANTE>";
     |HAZ Linea;
|FPRC;

|PROCESO BuscaRegimen;
     |SI #labtraba#247 ! 0;
          aAlfa = #labtraba#247;
          |QBF aAlfa;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE BuscaRegimen;
     #labtraba#1;
     ;
     #labempre#0, 00001;
     #labempre#0, 99999;
     ;
     NULL, BuscaRegimen;
|FIN;

|PROCESO Empresas;
     |SI #labnct14#6 ! "*";
          |ACABA;
     |FINSI;

     #labempre#0 = #labnct14#0;
     |LEE 000#labempre.=;

     nNivel = 2;
     aAlfa = "<EMPRESA>";
     |HAZ Linea;

     nNivel = 3;

     aAlfa = #labempre#13; |QBF aAlfa;
     aAlfa = "<CCC>" + aAlfa + "</CCC>";
     |HAZ Linea;

     aAlfa = "";
     |BUCLE BuscaRegimen;
     |QBF aAlfa;
     |SI aAlfa = "";
          aAlfa = "0111";
     |FINSI;
     aAlfa = ("0000" + aAlfa) % -104
     aAlfa = "<REGIMEN>" + aAlfa + "</REGIMEN>";
     |HAZ Linea;

     |HAZ TipoDocumento;
     aAlfa = "<TIPO_DOCUMENTACION>" + aAlfa + "</TIPO_DOCUMENTACION>";
     |HAZ Linea;

     aAlfa = #labempre#10;
     |QBF aAlfa;
     aAlfa = "<CIF_NIF>" + aAlfa + "</CIF_NIF>";
     |HAZ Linea;

     aAlfa = #labempre#2;
     |QBF aAlfa;
     aAlfa = "<NOMBRE>" + aAlfa + "</NOMBRE>";
     |HAZ Linea;

     aAlfa = #labempre#111;
     |QBF aAlfa;
     aAlfa = "<TELEFONO>" + aAlfa + "</TELEFONO>";
     |HAZ Linea;

     aAlfa = "";
     |QBF aAlfa;
     aAlfa = "<EMAIL>" + aAlfa + "</EMAIL>";
     |HAZ Linea;

     aAlfa = "";
     |QBF aAlfa;
     aAlfa = "<FAX>" + aAlfa + "</FAX>";
     |HAZ Linea;

     aAlfa = #labempre#128;
     |QBF aAlfa;
     aAlfa = "<ACTIVIDAD>" + aAlfa + "</ACTIVIDAD>";
     |HAZ Linea;

     |ABRE #nomconca;
     #nomconca#0 = #labempre#1;
     |LEE 000#nomconca.=;
     |SI FSalida = 0;
          aAlfa = #nomconca#142;
     |FINSI;
     |CIERRA #nomconca;

     aAlfa = "<CONVENIO>" + aAlfa + "</CONVENIO>";
     |HAZ Linea;

     aAlfa = #labcontr#64;
     |QBF aAlfa;
     aAlfa = ("0000000" + aAlfa) % -107;
     aAlfa = "<OFICINA>" + aAlfa + "</OFICINA>";
     |HAZ Linea;

     aAlfa = "";
     |QBF aAlfa;
     aAlfa = "<CLAUSULAS>" + aAlfa + "</CLAUSULAS>";
     |HAZ Linea;

     aAlfa = "";
     |QBF aAlfa;
     aAlfa = "<FIRMA>" + aAlfa + "</FIRMA>";
     |HAZ Linea;

     |HAZ Domicilio_Social;

     |HAZ Representante;


     nNivel = 2;
     aAlfa = "</EMPRESA>";
     |HAZ Linea;

     |HAZ Registro;

     nNumEmp = nNumEmp + 1;
|FPRC;

|DEFBUCLE Empresas;
     #labnct14#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Empresas;
|FIN;

|PROCESO GeneraXML;
     |ABRE #labcontr;
     |LEE 000#labcontr.p;
     |CIERRA #labcontr;

     |ABRE #labempre;
     |ABRE #labnct05;

     aRuta = #labcontr#63;
     |QBF aRuta;
     aAlfa = aRuta % -101;
     |SI aAlfa ! "\"; |Y aAlfa ! "/";
          aRuta = aRuta + "/";
     |FINSI;
     aRuta = aRuta + "DIAGRAMXML/";

     |RMKDIR aRuta;

     aFichero = "EMP_";
     fFecha = ~;
     aFecha = fFecha;
     aFecha = (aFecha % 102) + (aFecha % 402) + (aFecha % 904);
     aFichero = aFichero + aFecha + "_";
     |HORA aHora;
     |QBF aHora;
     aHora = aHora - ":" - ":";
     aFichero = aFichero + aHora + ".xml";

     aRuta = aRuta + aFichero;
     |XABRE "CBW", aRuta, nHandle;
     |SI FSalida ] 0;
          nNivel = 1;
          aAlfa = "<?xml version=" + &34 + "1.0" + &34 + " encoding=" + &34 + "ISO-8859-1" + &34 + " ?>";
          |HAZ Linea;

          aAlfa = "<EMPRESAS>";
          |HAZ Linea;

          |BUCLE Empresas;

          nNivel = 1;
          aAlfa = "</EMPRESAS>";
          |HAZ Linea;

          |CIERRA #labnct05;
          |CIERRA #labempre;
     |FINSI;
     |XCIERRA nHandle;

     |SI nNumEmp ! 0;
          aRuta = aRuta - aFichero;
          aAlfa = "    El fichero se ha generado correctamente en " + aRuta;
          |MENAV aAlfa;

          aAlfa1 = nNumEmp;

          |SI aParam = "";
               |SI nNumEmp = 1;
                    aAlfa = "    Se incluye 1 empresa.";
               |SINO;
                    aAlfa = "    Se incluyen un total de " + aAlfa1;
                    aAlfa = aAlfa + " empresas.";
               |FINSI;
               |MENAV aAlfa;
          |FINSI;
     |SINO;
          aAlfa = "    No se han seleccionado empresas para exportar.";
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO sel_min;
     #100#0 = 00001;
|FPRC;

|PROCESO sel_max;
     #100#0 = 99999;
|FPRC;

|PROCESO labempre;
     || este sera el proceso del pase

     swSigue = 1;        || por defecto SI

     #labnct05#0 = #labempre#0;
     |LEE 000#labnct05.=;
     |SI FSalida = 0; |Y #0#14 = "N";
          swSigue = 0;
     |FINSI;

     || aqui se seleccionaran las empresas y grabare el temporal

     |SI swSigue = 1;
          seleccio = 1;

          #100#0 = #labempre#0;
          #100#2 = #labempre#2;
          #100#6 = "*";
          |GRABA 020#100n;
          |LIBERA #100;
     |FINSI;
|FPRC;

|PROCESO salteada_labempre;
     |PARA salte_1 = 21; |SI salte_1 [ 41; |HACIENDO salte_1 = salte_1 + 1;
          salte_2 = salte_1 + 21;
          #labempre#0 = #0salte_1;   || empresa
          |LEE 000#labempre.=;
          |SI FSalida = 0;
               |HAZ labempre;
          |FINSI;
     |SIGUE;
|FPRC;

|DEFBUCLE labempre;
     #labempre#1;
     ;
     #0#0;
     #0#1;
     ;
     NULL, labempre;
|FIN;

|PROCESO lectura;             || INICIO DEL CALCULO
     |ABRE #labempre;
     |ABRE #labnct05;

     |SI #0#21 = 0;
          |BUCLE labempre;
     |SINO;
          |HAZ salteada_labempre;
     |FINSI;

     |SI seleccio = 0; |Y aParam = "labnct04";
          |ATRI P;
          |MENAV "    Seleccion vacia ...";
          |ATRI 0;
     |FINSI;

     |CIERRA #labnct05;
     |CIERRA #labempre;
|FPRC;

|PROGRAMA;
     |HORA aHora;
     |QBF aHora;
     aHora = aHora % 105;
     swEdicion = 0;

     aAlfa = Usuario;         |QBF aAlfa;
     aAlfa = "u" +  aAlfa;
     |NOME_DAT #100 aAlfa;
     |DELINDEX #100;

     aParam = PARAMETRO;
     |QBF aParam;

     |SI aParam ! "";
          || vengo de otro sitio, se supone que de la pantalla de empresas
          aAlfa1 = aParam % 105;
          #0#21 = aAlfa1;
          #0#14 = "S";
          FSalida = 0;
     |SINO;
          || PINTO PANTALLA Y PIDO DATOS, DESDE EL MENU

          |CLS;
          |CABEZA "Exportacion de Empresas a NetContrata@";
          |PINPA #0#0;
          |ABRE #0;
          |DDEFECTO #0;
          |LEE 000#0p;
          |SI FSalida = 0;  |BORRA 020#0a;  |FINSI;
          |CIERRA #0;
          |PINDA #0#0;
          |ENDATOS #1#0;
     |FINSI;

     |SI FSalida = 0;
          |ABRE #100;
          |HAZ lectura;
          |CIERRA #100;
          |SI aParam = "";
               |ABRE #0;
               |GRABA 000#0n;
               |CIERRA #0;
          |FINSI;
     |SINO;
          |ABRE #0;
          |GRABA 000#0n;
          |CIERRA #0;
     |FINSI;

     || si se va a presentar un lineal con la seleccion este es el momento

     |SI seleccio = 0; |Y aParam = "";
          |MENAV "    Selección vacía";
          |ACABA;
     |FINSI;

     |SI aParam = "";
          |ENTLINEAL #1#100, -1, 4, 22, 1, sel_min, sel_max;
     |FINSI;

     || despues se generara el xml y se grabara el registro

     |SI aParam = "";
          aAlfa = "    S¿Desea continuar con la generación del fichero?";
     |SINO;
          aAlfa = "    S¿Generar el fichero xml con los datos de ";
          aAlfa = aAlfa + "la empresa para Netcontrat@?"
     |FINSI;

     |CONFI aAlfa;
     |SI FSalida = 0;
          |HAZ GeneraXML;
     |SINO;
          aAlfa = "    El proceso ha sido cancelado por el usuario";
          |MENAV aAlfa;
     |FINSI;
     |DELINDEX #100;
     |CIERRA #100;
|FPRO;
