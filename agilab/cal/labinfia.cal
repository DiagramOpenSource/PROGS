|FICHEROS;
    labinfia #0;
    labcatip #1;         || iae cabs.
    lablitip #2;         || iae lins.
    labdesca #3;         || desglose cabs.
    labdesli #4;         || desglose lins.
    labdestm #5;         || tmp. desgloses
    labempre #6;         || empresas
    labtraba #7;         || trabajadores
|FIN;

|VARIABLES;
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    seleccio = 0;

    &linea = 0;
    sw = 0;
    empresa = 0;
    ejercicio = 0;
    sw_calculo = 0;
    FEstado = 0;
    sw_repe = 0;
     aDesde = "";
     aHasta = "";
|FIN;
____________________________________/ CALCULOS DE PANTALLA
|PROCESO pin_desglo; |TIPO 7;
|SI #0Campo ! 0;
    |DDEFECTO #3;
    |ABRE #3;
        #3#0 = #0Campo;
    |LEE 000#3=;
    |CIERRA #3;

    |ATRI I;
    |PINTA 1738, #3#1;
    |ATRI 0;
|SINO;
    |PINTA 1738, "                                        ";
|FINSI;
|FPRC;

|PROCESO lee_desglo; |TIPO 0;
|SI #0Campo ! 0;
    |DDEFECTO #3;
    |ABRE #3;
        #3#0 = #0Campo;
    |LEE 000#3=;
    |SI FSalida ! 0;
        |MENAV "    Desglose inexistente ...";
        |ERROR;
    |SINO;
            elsw1 = 0;
        |PARA para1 = 4; |SI para1 [ 23; |HACIENDO para1 = para1 + 1;
            |SI para1 ! Campo;
                |SI #0para1 = #0Campo;  elsw1 = 1;     |FINSI;
            |FINSI;
        |SIGUE;
        |SI elsw1 = 1;
            |MENAV "    Codigo de Desglose repetido ...";
            |ERROR;
        |FINSI;
    |FINSI;
    |CIERRA #3;
|FINSI;
|FPRC;
____________________________________/
|PROCESO lee_tmp1;
    #1#0 = #5#0;
    #1#1 = #5#1;
|LEE 020#1=;
|SI FSalida = 0;
        linea = 2;
    |PRINT;
|FINSI;
|FPRC;

|DEFBUCLE lee_tmp;
 #5#1;
 ;
 empresa, ejercicio, 01;
 empresa, ejercicio, 99;
 ;
 NULL, lee_tmp1;
|FIN;

|PROCESO imprime_detalle;
|DDEFECTO #7;  #7#0 = #2#0;   #7#1 = #2#5;   |LEE 000#7=;   || trabajadores
    linea = 0;
|PRINT;
|FPRC;

|PROCESO detalle;
|PARA para1 = 4; |SI para1 [ 23; |HACIENDO para1 = para1 + 1;
    |SI #0para1 ! 0;
            #4#0 = #0para1;
            alfa1 = #2#4;     |QBF alfa1;
            alfa1 = ("000" + alfa1) % -103;
            #4#2 = alfa1;
        |LEE 000#4=;
        |SI FSalida = 0;                          || si existe desglose
            |SI #2#3 ] #4#7;|Y #2#3 [ #4#8;       || grupos de tarifa
                |SI #2#6 ] #4#5;|Y #2#6 [ #4#6;   || fechas de alta
                    |HAZ imprime_detalle;
                |FINSI;
            |FINSI;
        |FINSI;
    |SINO;
        |SAL;
    |FINSI;
|SIGUE;
|FPRC;

|PROCESO imprime;
|SI sw = 0;
        sw = 1;
        empresa = #2#0;
        ejercicio = #2#1;
    |DDEFECTO #6;   #6#0 = #2#0;   |LEE 000#6=;   || empresas
        linea = 1;
    |PRINT;
|FINSI;
|SI empresa ! #2#0;|O ejercicio ! #2#1;
    |BUCLE lee_tmp;
        empresa = #2#0;
        ejercicio = #2#1;
    |DDEFECTO #6;   #6#0 = #2#0;   |LEE 000#6=;   || empresas
        linea = 1;
    |PIEINF;
    |PRINT;
|FINSI;

|HAZ detalle;
|FPRC;

|PROCESO graba_tmp;
    #3#0 = #0para1;
|LEE 000#3=;                  || cabecera desglose
|SI FSalida = 0;
    |DDEFECTO #5;
        #5#0 = #2#0;          || empresa
        #5#1 = #2#1;          || ejercicio
        #5#2 = #3#0;          || codigo desglose
    |LEE 101#5=;
        FEstado = FSalida;

        #5#3 = #3#1;          || descripcion
        #5#4 = #5#4 + #2#9;   || dias totales
        #5#5 = #5#5 + #2#10;  || jornadas totales

    |SI FEstado ! 0;   |GRABA 020#5n; |FINSI;
    |SI FEstado = 0;   |GRABA 020#5a; |FINSI;
    |LIBERA #5;
        seleccio = 1;
|FINSI;
|FPRC;

|PROCESO calcula;
    sw_repe = 0;
|PARA para1 = 4; |SI para1 [ 23; |HACIENDO para1 = para1 + 1;
    |SI #0para1 ! 0;
            #4#0 = #0para1;
            alfa1 = #2#4;     |QBF alfa1;
            alfa1 = ("000" + alfa1) % -103;
            #4#2 = alfa1;
        |LEE 000#4=;
        |SI FSalida = 0;                          || si existe desglose
            |SI #2#3 ] #4#7;|Y #2#3 [ #4#8;       || grupos de tarifa
                |SI #2#6 ] #4#5;|Y #2#6 [ #4#6;   || fechas de alta
                    |SI sw_repe = 0;
                            sw_repe = 1;
                        |HAZ graba_tmp;
                    |SINO;
                            alfa1 = "    Contrato [" + #4#2 + "] repetido en desgloses ...";
                        |MENAV alfa1;
                    |FINSI;
                |FINSI;
            |FINSI;
        |FINSI;
    |SINO;
        |SAL;
    |FINSI;
|SIGUE;
|FPRC;

|PROCESO lee_iae1;
|SI sw_calculo = 0;
    |HAZ imprime;
|SINO;
    |HAZ calcula;
|FINSI;
|FPRC;

|DEFBUCLE lee_iae_orden2;
 #2#2;
 ;
 aDesde, #0#0, #0#2,    1;
 aHasta, #0#1, #0#3, 9999;
 ;
 NULL, lee_iae1;
|FIN;

|DEFBUCLE lee_iae_orden1;
 #2#1;
 ;
 #0#0, #0#2,    1;
 #0#1, #0#3, 9999;
 ;
 NULL, lee_iae1;
|FIN;

|PROCESO tipo3;
|IMPRE 0;
|SI FSalida = 0;
        alfa1 = Usuario; |QBF alfa1;
    |NOME_DAT #5 alfa1;
    |DELINDEX #5;
    |ABRE #1;
    |ABRE #3;
    |ABRE #4;
    |SELECT #2#4;
    |ABRE #5;
        sw_calculo = 1;
    |BUCLE lee_iae_orden1;                || calcula y graba temporal
    |CIERRA #5;

    |SI seleccio ! 0;
        |INFOR "labinfia";
            sw_calculo = 0;
        |ABRE #6;
        |ABRE #7;
        |SI #0#25 = 1;
             |BUCLE lee_iae_orden1;            || imprime detalle
        |SINO;
             aDesde = " " * 40;
             aHasta = "z" * 40;
             |BUCLE lee_iae_orden2;            || imprime detalle
        |FINSI;
        |CIERRA #6;
        |CIERRA #7;
        |BUCLE lee_tmp;
        |PIEINF;
    |SINO;
        |MENAV "    Seleccion vacia ...";
    |FINSI;
    |FININF;
    |CIERRA #1;
    |CIERRA #3;
    |CIERRA #4;
    |DELINDEX #5;
|SINO;
    |MENAV "    Proceso abortado ...";
|FINSI;
|FINIMP;
|FPRC;

|PROGRAMA;
|CLS;
|CABEZA "Informe Desgloses";
|PINPA #0#0;
|DDEFECTO #0;
|ABRE #0;
|LEE 101#0p;
|SI FSalida ! 0;    |GRABA 020#0b; |FINSI;
|PINDA #0#0;
|ENDATOS #1#0;
|SI FSalida = 0;
    |HAZ tipo3;
    |GRABA 020#0a;
|FINSI;
|LIBERA #0;
|CIERRA #0;
|FPRO;
