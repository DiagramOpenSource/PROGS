|FICHEROS;
    labvenci #0;
    labtraba #2;
    labregis #3;
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    &venci = "";
    &orden = "";
    orden_ant = "";
    &sw_lin = 0;
    alfa_orden = "";
|FIN;
____________________________________/
|PROCESO mayus; |TIPO 0;
    #0Campo = #0Campo > #0Campo;
|PINTA #0Campo;
|FPRC;

|PROCESO def_fechas; |TIPO 7;
    fech1 = ~;
    alfa1 = fech1;
    alfa2 = alfa1 % 402;      || recoge el mes actual
    alfa3 = alfa1 % -104;     || recoge el a¤o actual
    nume1 = alfa2;
    nume2 = alfa3;

|SI nume1 = 12;
        nume1 = 1;
        nume2 = nume2 + 1;
|SINO;
        nume1 = nume1 + 1;    || calcula el mes siguiente al actual
|FINSI;

    nume4 = nume2 $ 4;        || calcula el a¤o bisiesto
|SI nume4 = 0;
        nume4 = 1;
|SINO;
        nume4 = 0;
|FINSI;

|SI nume1 = 1;      nume3 = 31;         |FINSI;
|SI nume1 = 2;      nume3 = 28 + nume4; |FINSI;
|SI nume1 = 3;      nume3 = 31;         |FINSI;   || calcula el ultimo dia
|SI nume1 = 4;      nume3 = 30;         |FINSI;   ||/del mes siguiente al
|SI nume1 = 5;      nume3 = 31;         |FINSI;   ||/actual
|SI nume1 = 6;      nume3 = 30;         |FINSI;
|SI nume1 = 7;      nume3 = 31;         |FINSI;
|SI nume1 = 8;      nume3 = 31;         |FINSI;
|SI nume1 = 9;      nume3 = 30;         |FINSI;
|SI nume1 = 10;     nume3 = 31;         |FINSI;
|SI nume1 = 11;     nume3 = 30;         |FINSI;
|SI nume1 = 12;     nume3 = 31;         |FINSI;

    alfa4 = nume1;  alfa4 = ("00" + alfa4) % -102;     || mes alfanumerico

    alfa1 = "01." + alfa4 + "." + nume2;
    #0#4 = alfa1;                                      || desde fecha

    alfa1 = nume3;
    alfa1 = alfa1 + "." + alfa4 + "." + nume2;
    #0#5 = alfa1;                                      || hasta fecha

|PINTA #0#4;
|PINTA #0#5;
|FPRC;

|PROCESO no_entres; |TIPO 7;
|SI #0#7 = 3;|O #0#7 = 4;
        alfa1 = "S";
|SINO;
        alfa1 = "N";
|FINSI;
|CAMPO_MODIFICA #0#8, 1, alfa1;
|CAMPO_MODIFICA #0#14, 1, alfa1;
|FPRC;
____________________________________/
|PROCESO printa;
    sw_lin = 0;
|PRINT;
    seleccio = 1;
    orden_ant = orden;
|FPRC;

|PROCESO leyendo;
    #2#0 = #3#1;
    #2#1 = #3#2;
|LEE 000#2=;
|SI FSalida = 0;|Y #0#10 [ #2#62;|Y #0#11 ] #2#62;|Y #0#12 [ #2#63;|Y #0#13 ] #2#63;
        orden = "-----";
    |SI #0#7 = 3;
            orden = #2#62 + "-" + #2#63;
    |FINSI;
    |SI #0#7 = 4;
            orden = #2#77;    orden = ("00" + orden) % -102;
    |FINSI;
    |SI orden ! orden_ant;|Y orden_ant ! "";
            sw_lin = 1;
        |PRINT;
        |SI #0#14 = "S";
                FSalida = "PAGINA";
            |PRINT;
        |FINSI;
    |FINSI;
    |SI #0#6 = "C";|O #0#6 = "A";
            venci = "CONTRATO";
        |SI #3#24 ] #0#4;|Y #3#24 [ #0#5;    |HAZ printa;   |FINSI;
    |FINSI;
    |SI #0#6 = "P";|O #0#6 = "A";
            venci = "PRORROGA";
        |SI #3#26 ] #0#4;|Y #3#26 [ #0#5;    |HAZ printa;   |FINSI;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO ordena;
|DDEFECTO #2;
    #2#0 = #3#1;
    #2#1 = #3#2;
|LEE 000#2=;
|SI FSalida = 0;|Y #0#10 [ #2#62;|Y #0#11 ] #2#62;|Y #0#12 [ #2#63;|Y #0#13 ] #2#63;
    |SI #0#7 = 3;|O #0#7 = 4;
            alfa_orden = "";
        |SI #0#8 = 1;                  || por vencimiento + codigo
            |SI #3#24 ] #0#4;|Y #3#24 [ #0#5;    alfa1 = #3#24; |FINSI;
            |SI #3#26 ] #0#4;|Y #3#26 [ #0#5;    alfa1 = #3#26; |FINSI;
                alfa_orden = (alfa1 % -104) + (alfa1 % 402) + (alfa1 % 102);
        |FINSI;
        |SI #0#8 = 1;|O #0#8 = 2;      || por codigo
                alfa1 = #2#0;     alfa1 = ("00000" + alfa1) % -105;
                alfa2 = #2#1;     alfa2 = ("00000" + alfa2) % -105;
                alfa_orden = alfa_orden + alfa1 + alfa2;
        |FINSI;
        |SI #0#8 = 3;                  || por nombre
                alfa_orden = #2#2;
        |FINSI;
            #0#9 = alfa_orden;
    |FINSI;
|FINSI;
|FPRC;

|DEFBUCLE leyendo1;      || por empresa, trabajador
 #3#2;
 ;
 #0#0, #0#2;
 #0#1, #0#3;
 ;
 NULL, leyendo;
|FIN;

|DEFBUCLE leyendo2;      || por nombre, empresa, trabajador
 #3#2;
 ;
 #0#0, #0#2;
 #0#1, #0#3;
 ;
 NULL, ordena, NULL, NULL, NULL, leyendo;
 #2#2, #2#0, #2#1;
|FIN;

|DEFBUCLE leyendo3;      || por area-seccion + orden en grupos
 #3#2;
 ;
 #0#0, #0#2;
 #0#1, #0#3;
 ;
 NULL, ordena, NULL, NULL, NULL, leyendo;
 #2#62, #2#63, #0#9;
|FIN;

|DEFBUCLE leyendo4;      || por centros + orden en grupos
 #3#2;
 ;
 #0#0, #0#2;
 #0#1, #0#3;
 ;
 NULL, ordena, NULL, NULL, NULL, leyendo;
 #2#77, #0#9;
|FIN;

|PROCESO tipo3; |TIPO 3;
|IMPRE 0;
|SI FSalida = 0;
    |INFOR "labvenci";
    |ABRE #2;
    |SI #0#7 = 1;   |BUCLE leyendo1;    |FINSI;
    |SI #0#7 = 2;   |BUCLE leyendo2;    |FINSI;
    |SI #0#7 = 3;   |BUCLE leyendo3;    |FINSI;
    |SI #0#7 = 4;   |BUCLE leyendo4;    |FINSI;
    |CIERRA #2;
    |SI seleccio ! 0;
        |PIEINF;
    |SINO;
        |MENAV "    Seleccion vacia ...";
    |FINSI;
    |FININF;
|SINO;
    |MENAV "    Proceso abortado ...";
|FINSI;
|FINIMP;
|FPRC;
