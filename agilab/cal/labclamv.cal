|FICHEROS;
     labclamv #0;
     labclamt #100,MANTE;
     labclaus;
|FIN;

|VARIABLES;
 {-1}menu = "";
     menu1 = "2400";
     menu2 = "1";
     menu3 = " ";
     menu4 = "RPC";
     menu5 = " Repasar Lista ";
     menu6 = " Procesar";
     menu7 = " Cancelar ";
     opcion = 0;

     nCodAntiguo = 0;
     aDescripcion = "";
     nCodNuevo = 0;
     aAlfa1 = "";
     aAlfa2 = "";
     nNume1 = 0;
     nElUltimo = 0;
     nElAnterior = 0
     nRevisa1 = 0;
     nRevisa2 = 0;
|FIN;

|PROCESO graba_labclaus;
     #labclaus#1 = nCodAntiguo;
     |LEE 101#labclaus.=;
     #labclaus#1 = nCodNuevo;
     |GRABA 020#labclaus.n;
     |SI FSalida = 0;
          #labclaus#1 = nCodAntiguo;
          |LEE 101#labclaus.=;
          |BORRA 020#labclaus.a;
     |FINSI;
|FPRC;

|PROCESO ElProceso;
     nCodAntiguo = #labclamt#0;
     aDescripcion = #labclamt#1;
     nCodNuevo = #labclamt#2;
     |HAZ graba_labclaus;
|FPRC;

|DEFBUCLE ElBucle;
     #labclamt#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, ElProceso;
|FIN;

|PROCESO mueve; |TIPO 3;
     |SI opcion = 3; |O opcion = 0; |O FSalida ! 0; |O FEntrada = 302;
          |ACABA;
     |SINO;
          |ABRE #labclaus;
          |BUCLE ElBucle;
          |CIERRA #labclaus;
     |FINSI;
|FPRC;

|PROCESO revisa2;
     |SI #labclamt#2 = nElAnterior;
          nRevisa2 = #labclamt#2;
          |ERROR10;
     |FINSI;
     nElAnterior = #labclamt#2;
|FPRC;

|DEFBUCLE revisa2;  || revisa que no se repita ninguno de los nuevos
     #labclamt#2;
     ;
     INICIO;
     FINAL;
     ;
     NULL, revisa2;
|FIN;

|PROCESO revisa1;
     #labclaus#1 = #labclamt#2;
     |LEE 000#labclaus.=;
     |SI FSalida = 0;
          nRevisa1 = #labclamt#2;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE revisa1;  || revisa que ninguno de los nuevos exista
     #labclamt#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, revisa1;
|FIN;

|PROCESO carga;
     #labclamt#0 = #labclaus#1;
     #labclamt#1 = #labclaus#2;
     #labclamt#2 = #labclaus#1 + #labclamv#1;
     |GRABA 020#labclamt.n;
|FPRC;

|DEFBUCLE carga;
     #labclaus#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, carga;
|FIN;

|PROCESO minimo;
     #100#0 = 1;
|FPRC;

|PROCESO maximo;
     #100#0 = 9999;
|FPRC;

|PROCESO lineal;
     |ABRE #labclaus;
     aAlfa1 = Usuario; |QBF aAlfa1;
     |NOME_DAT #100 aAlfa1;
     |SI #0#0 = "S";
          |DELINDEX #labclamt;
          |ABRE #labclamt;
          |BUCLE carga;
          |CIERRA #labclamt;
     |SINO;
          |DELINDEX #labclamt;
     |FINSI;
     |PARA; |SI; |HACIENDO;
          |ENTLINEAL #1#100, 1, 3, 21, 1, minimo, maximo;
          |MENU menu;
          opcion = FSalida;
          |SI opcion ! 1;
               |SI opcion = 2;
                    nElAnterior = 2.2; || me evito problemas con los codigos 0
                    nRevisa1 = 0;
                    nRevisa2 = 0;
                    |BUCLE revisa1;
                    |BUCLE revisa2;
                    |SI nRevisa1 ! 0;
                         aAlfa1 = nRevisa1;
                         aAlfa1 = "     ATENCION: El codigo " + aAlfa1 + " ya existe.";
                         aAlfa1 = aAlfa1 +  " Es necesario revisar la lista.";
                         |MENAV aAlfa1;
                    |FINSI;
                    |SI nRevisa2 ! 0;
                         aAlfa1 = nRevisa2;
                         aAlfa1 = "     ATENCION: El codigo " + aAlfa1 + " esta duplicado.";
                         aAlfa1 = aAlfa1 +  " Es necesario revisar la lista.";
                         |MENAV aAlfa1;
                    |FINSI;
                    |SI nRevisa1 = 0; |Y nRevisa2 = 0;
                         |SAL;
                    |FINSI;
               |SINO;
                    |SAL;
               |FINSI;
          |FINSI;
     |SIGUE;
     |CIERRA #labclaus;
|FPRC;

------------------------------------------------------------------------------
|PROCESO factor; |TIPO 7;
     |SI #0#0 = "S";
          |CAMPO_MODIFICA #0#1, 1, "S";
     |SINO;
          |CAMPO_MODIFICA #0#1, 1, "N";
     |FINSI;
|FPRC;

|PROCESO comprueba;
     #labclaus#1 = #labclamt#0;
     |LEE 000#labclaus.=;
     |SI FSalida ! 0;
          aAlfa1 = #labclaus#1;
          aAlfa1 = "0000" + aAlfa1;
          aAlfa1 = aAlfa1 % -104;
          aAlfa1 = "    ATENCION: La Clausula " + aAlfa1 + " no existe.";
          |MENAV aAlfa1;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO comprueba2;
     #labclaus#1 = #labclamt#2;
     |LEE 000#labclaus.=;
     |SI FSalida = 0;
          aAlfa1 = #labclaus#1;
          aAlfa1 = "0000" + aAlfa1;
          aAlfa1 = aAlfa1 % -104;
          aAlfa1 = "    ATENCION: La Clausula " + aAlfa1 + " ya existe.";
          |MENAV aAlfa1;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO descripcion;
     #labclaus#1 = #labclamt#0;
     |LEE 000#labclaus.=;
     |SI FSalida = 0;
          #labclamt#1 = #labclaus#2;
          |PINTA #labclamt#1;
     |FINSI;
|FPRC;

|PROCESO elnuevo; |TIPO 7;
     |SI FEntrada ! 303;  || si no es modificacion
          #100Campo = #100#0;
          |PINTA #100Campo;
     |FINSI;
|FPRC;

|PROCESO comprueba0;
     |SI #0#0 = "S";
          |ABRE #labclaus;
          |LEE 000#labclaus.u;
          nElUltimo = #labclaus#0;
          |CIERRA #labclaus;

          nNume1 = nElUltimo + #0#1;
          |SI nNume1 > 9999;
               nNume1 = 9999 - nElUltimo;
               aAlfa2 = nNume1;
               aAlfa1 = "    El factor maximo segun la codificacion de Clausulas ";
               aAlfa1 = aAlfa1 + "no puede superar el " + aAlfa2;
               |MENAV aAlfa1;
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;
