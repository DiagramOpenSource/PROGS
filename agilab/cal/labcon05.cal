|FICHEROS;
     labcon05 #0;
     labtraba #2;
     labcentr #3;
|FIN;

|VARIABLES;
     aCadena="";
     nNumero=0;
     dia=0;
     mes=0;
     any=0;
     alfa1="";
     fFecha=@;
     fFecha1=@;
     fFecha2=@;
     fFechaSys=@;
     &titulo1="";
     &titulo2="";
     &aCodigoC1="";
     &aCodigoC2="";
     &aCodigoC3="";
     &trabacat = "";
     &trabafec = "";
     &trabanom = "";
     &trabapro = "";
     &mensaL = "    ATENCION! Dato erroneo, codigos de trabajador iguales ...";
     &mensa2 = "    No existe trabajador !!!";
     &trabacen = 0;
     &direcc = "";
     &numero = "";
     &piso   = "";
     &cod1   = "";
     &cod2   = "";
     &locali = "";
     &centrnom = "";
     &compactado = "";
     &mensa3 = "    No existe centro trabajo !!!";
     &mensaK = "    ATENCION! Dato erroneo, centro de trabajo distinto al del trabajador ...";
     &setenta = "";
     &espa = " ";
     &coma = ",";
|FIN;

|PROCESO trabajub; |TIPO 0;       || EN CONTRATOS 05 Y 25 VARIABLES DEL
|SI #0#27 ! #0#2;                 || TRABAJADOR JUBILADO
    |ABRE #2;
        #2#0 = #0#1;
        #2#1 = #0#27;
    |LEE 040#2=;
    |SI FSalida = 0
        |PINTA 1425,#2#2;
        trabanom = #2#2;
        trabafec = #2#11;
        trabapro = #2#19;
        trabacat = #2#18;
        trabacen = #2#77; #0#28 = trabacen; |PINTA #0#28;
        #2#0 = #0#1;
        #2#1 = #0#2;
        |LEE 040#2=;              || vuelve a cargar el trabajador a contratar
    |SINO;
        |MENAV mensa2;
        #0#27 = 0;
        |ERROR;
    |FINSI;
    |CIERRA #2;
|SINO;
    |MENAV mensaL;
    |ERROR;
|FINSI;
|FPRC;

|PROCESO centrjub; |TIPO 0;     || EN CONTRATOS 05 Y 25 VARIABLES DEL
|ABRE #3;                       || CENTRO DE TRABAJO DEL TRABAJO
    #3#0 = #0#1;                || JUBILADO
    #3#1 = #0#28;
|LEE 040#3=;
|SI FSalida = 0;
    |PINTA 1527,#3#2;
        direcc = #3#3;
        numero = #3#4;
        piso = #3#5;
        cod1 = #3#7;
        cod2 = #3#8;
        locali = #3#6;
    |HAZ compac_05;
        centrnom = compactado;
|SINO;
    |MENAV mensa3;
        #0#28 = 0;
    |ERROR;
|FINSI;
|CIERRA #3;
|SI #0#28 ! trabacen;
    |MENAV mensaK;
    |ERROR;
|FINSI;
|FPRC;

|PROCESO compac_05;                      || SUBRUTINA DE RUTINA domicili
    setenta = espa * 70;
    compactado = "";                  || DE COMPACTACION DE CAMPOS
|QBF direcc;
|QBF numero;
|QBF piso;
|QBF cod1; cod1 = "00" + cod1;   cod1 = cod1 % -102;
|QBF cod2; cod2 = "000" + cod2;  cod2 = cod2 % -103;
|QBF locali;
    compactado = direcc + coma;
    compactado = compactado + numero;
    compactado = compactado + "  ";
    compactado = compactado + piso;
    compactado = compactado + " (";
    compactado = compactado + cod1;
    compactado = compactado + cod2;
    compactado = compactado + ") ";
    compactado = compactado + locali;
    compactado = compactado + setenta;
    compactado = compactado % 170;
|FPRC;

|PROCESO TipoJornada;|TIPO 0;
     |SI #0#56="C";
          #0#0="441";
          |PINTA 2028,"1er. dia semana ..:     Ult. dia semana ..:   ";
          |CAMPO_VISUAL #0#80,1,"N"; |CAMPO_MODIFICA #0#80,1,"N";
          |CAMPO_VISUAL #0#33,1,"S";
          |CAMPO_VISUAL #0#34,1,"S";
          |PINTA #0#33;
          |PINTA #0#34;

          |CAMPO_MODIFICA #0#31,1,"N";
          #0#31="S";

          |CAMPO_VISUAL #0#53,1,"N";
          |PINTA 1958,"                     ";

          |CAMPO_MODIFICA #0#65,1,"N";
          |CAMPO_MODIFICA #0#66,1,"N";
          |CAMPO_MODIFICA #0#67,1,"N";
     |SINO;
          #0#0="541";
          |CAMPO_VISUAL #0#33,1,"N";
          |CAMPO_VISUAL #0#34,1,"N";
          |CAMPO_VISUAL #0#80,1,"S"; |CAMPO_MODIFICA #0#80,1,"S";
          |PINTA 2028,"                             Jornada inferior :   ";
          |PINTA #0#80;

|| ESTO ES PARA QUE SI ES PARCIAL SIEMPRE SE PONE HORAS A LA SEMANA.
          |CAMPO_MODIFICA #0#31,1,"S";
          #0#31="S";
          |PINTA #0#31;

          |CAMPO_VISUAL #0#53,1,"S";
          |PINTA 1958,"Maximo Horas:";
          |PINTA #0#53;

          |CAMPO_MODIFICA #0#65,1,"S";
          |CAMPO_MODIFICA #0#66,1,"S";
          |CAMPO_MODIFICA #0#67,1,"S";
     |FINSI;
|FPRC;


|PROCESO DesglosaFecha;     || DESGLOSA EN DIA, MES Y ANYO UNA FECHA DD.MM.AAAA
    alfa1 = fFecha %  102;   dia = alfa1;
    alfa1 = fFecha %  402;   mes = alfa1;
    alfa1 = fFecha % -104;   any = alfa1;
|FPRC;

|PROCESO DurContrato;|TIPO 0;
     |ABRE #2;
     #2#0=#0#1;
     #2#1=#0#27;
     |LEE 000#2=;
     |CIERRA #2;

     aCadena=#2#11;
     |QBF aCadena;
     |SI aCadena="";
          #0#40=0;
          #0#49=0;
     |SINO;
          fFechaSys=~;
          nNumero=#0#30/1000000;
          aCadena=nNumero;
          fFecha1=#2#11+aCadena;
          fFecha2=fFecha1-fFechaSys;

          fFecha=fFecha2;
          |HAZ DesglosaFecha;

          #0#40=(any*12)+mes;
          #0#49=dia;

          |SI #0#40>48;
               #0#40=48;
               #0#49=0;
          |SINO;
               |SI #0#40=48;|Y #0#49>0;
                    #0#40=48;
                    #0#49=0;
               |FINSI;
          |FINSI;
     |FINSI;

     |PINTA #0#40;
     |PINTA #0#49;

     |ABRE #2;
     #2#0=#0#1;
     #2#1=#0#2;
     |LEE 000#2=;
     |CIERRA #2;
|FPRC;


|PROCESO Comienzo; |TIPO 3;
     |SI #0#56="C";
          #0#0="441";
          titulo1="Tiempo Completo";
          aCodigoC1="4";
          aCodigoC2="4";
          aCodigoC3="1";
     |SINO;
          #0#0="541";
          titulo1="Tiempo Parcial";
          aCodigoC1="5";
          aCodigoC2="4";
          aCodigoC3="1";

          |SI #0#31="D";
               titulo2="al d¡a";
          |FINSI;
          |SI #0#31="S";
               titulo2="a la semana";
          |FINSI;
          |SI #0#31="M";
               titulo2="al mes";
          |FINSI;
          |SI #0#31="A";
               titulo2="al a¤o";
          |FINSI;

     |FINSI;

     |HAZ LeePaises;
|FPRC;

|PROCESO PintaPantalla;|TIPO 7;
     |HAZ TipoJornada;
|FPRC;

|PROCESO MiraEstado;
   |SI #0#78 = "A";
      |C_M #0#62,1,"N"; |C_M #0#63,1,"N";
      #0#62 = ""; |PINTA #0#62;
      #0#63 = ""; |PINTA #0#63;
   |SINO;
      |C_M #0#62,1,"S"; |C_M #0#63,1,"S";
   |FINSI;
|FPRC;
