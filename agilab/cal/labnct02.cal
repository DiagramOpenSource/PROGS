|FICHEROS;
     labnct02 #0;        || pantalla
     labnct03;           || registro netcontrata

     nomgruec;           || grupos de empresas Cabs.
     nomgruel;           || grupos de empresas Lins.
     labtraba #2;
     labnct12 #100,MANTE;
     labempre;
     labcontr;
     nomconca;
     labmunic #6;

     &regisnif@ #712;
|FIN;

|VARIABLES;
    avisa_mes = 0;
    htipo = 0;
    meses = "ENERO     FEBRERO   MARZO     ABRIL     MAYO      JUNIO     JULIO     AGOSTO    SEPTIEMBREOCTUBRE   NOVIEMBRE DICIEMBRE ";
    salte_1 = 0;
    salte_2 = 0;

    &nElMesIRPF = 0;         || para 'nomir008'
    &nElAnyIRPF = 0;         || para 'nomir008'
    &nDiferencias = 0;   || para 'nomir008'
    &nElMes = 0;         || para la nomina
    &nElAny = 0;         || para la nomina
    swActiva = 0;
    swPorGrupos = 0;
    nGrupo = 0;
    nCampoG = 0;
     &eaVengoDe = "";

     &nEmpAUTO = 0;
     &nEmpAUTO_D = 0;
     &nEmpAUTO_H = 0;
     &nTraAUTO = 0;
     &nTraAUTO_D = 0;
     &nTraAUTO_H = 0;
     &nMesAUTO = 0;
     &nAnyAUTO = 0;
     &aParam = "";
     &nEnEmpresa = 0;
     &nPaso = 0;
     &swCompAjuste = 0;
     &swSegPasada = 0;
     &nRetEstAnu = 0;    || retencion estimada anual
     &swCalPagas = "S";
     aAlfa1 = "";
     aAlfa2 = "";
     alfa1 = "";
     alfa2 = "";
     nume1 = 0;
     nume2 = 0;
     para1 = 0;
     swfuera = 0;
     &nMes = 0;
     &nAny = 0;
     &nDesdeEmp = 0;
     &nHastaEmp = 0;
     &nDesdeTra = 0;
     &nHastaTra = 0;
     seleccio = 0;
     aAlfa = "";
     p_mes = "";
     fecsys = @;
     mesnum = 0;
     anynum = 0;
     guarda = 0;
     topemes = 0;
     aFabre = "";
     aBase = "";
     aMas = "";
     aFich = "";
     &nTopemes = 0;
     &nEmp = 0;
     &nTra = 0;
     &aHora = "";
     aDef = "";
     FCargaDef = "";
     nCampo1 = 0;
     &swEdicion = 0;

     nOpcion = 0;
     {-1}menu = "";
     menu1 = "2400";
     menu2 = "1";
     menu3 = "";
     menu4 = "VACI";
     menu5 = "Volver a incid.";
     menu6 = "Aceptar y continuar";
     menu7 = "Cancelar proceso";
     menu8 = "Imprimir informe";

     aEstadoNomgirli = "";
     nNroIncid = 0;
     &swStopIRPF = 0;
     nNume1 = 0;
     &nIRPFMes = 0;
     &swBajaEsteMes = 0;
     nMesBaja = 0;
     nAnyBaja = 0;
     FEstado = 0;

     aFecha = "";
     aFechaAlta = "";
     fFechaLimite = @;
     fFechaAlta = @;
     swSigue = 0;
     &aCadena = "";
     &eaNom   = "";
     &eaApel1 = "";
     &eaApel2 = "";
     &eaCodNif = "";
     nHorasMax = 0;
     nNume = 0;
     &aCodPos = "";
     &nContMuni = 0;
     &aCodMuni = "";
     nHandle = 0;
     aRuta = "";
     aFichero = "";
     nNivel = 0;
     nNumMov = 0;
     nNumTra = 0;
     aMuni = "";
     nMensajeF5 = 1;
     aCodigoMunicipio = "";
     nJornada = 0;
     aUsuario = "";
     fFecha = @;
     &runnom = "labnct02";
     nUltimo = 0;
|FIN;

|INCLUYE nomcal_4;

|PROCESO proceso;
     || por compatibilidad con el incluye nomcal_4
|FPRC;

|PROCESO CuentaMunicipio;
     nContMuni = nContMuni + 1;
     aCodMuni = #6#1;
|FPRC;

|DEFBUCLE CuentaMunicipio;
     #6#1;
     ;
     aCodPos, "00000";
     aCodPos, "99999";
     ;
     NULL,CuentaMunicipio;
|FIN;

|PROCESO BuscaMunicipio;
     aAlfa2 = #6#3;            || aAlfa2 = municipio de labmunic
     |QBT aAlfa2;
     aMuni = #labempre#6;   || aMuniEmp = municipio de labempre
     aMuni = aMuni > "";
     |QBT aMuni;

     aAlfa = aMuni - aAlfa2;
     |SI aAlfa ! aMuni;
          ||aAlfa2 esta en aMuni
          aCodMuni = #6#1;
          nContMuni = nContMuni + 1;
     |FINSI;
|FPRC;

|DEFBUCLE BuscaMunicipio;
     #6#1;
     ;
     aCodPos, "00000";
     aCodPos, "99999";
     ;
     NULL,BuscaMunicipio;
|FIN;

|PROCESO min_labmunic;
     #6#4 = aCodPos;
     #6#1 = "00000";
|FPRC;

|PROCESO max_labmunic;
     #6#4 = aCodPos;
     #6#1 = "99999";
|FPRC;

|PROCESO CodigoMuni;
     nContMuni = 0;
     |BUCLE CuentaMunicipio;

     |SI nContMuni > 1;
          nContMuni = 0;
          |BUCLE BuscaMunicipio;
          |SI nContMuni > 1;
               nMensajeF5 = 1;
               aCodigoMunicipio = "";
               |SUB_EJECUTA "labmunic";
               |SI aCodigoMunicipio ! "";
                    aCodMuni = aCodigoMunicipio;
                    nContMuni = 1;
               |FINSI;
          |FINSI;
     |FINSI;

     |SI nContMuni = 0;
          nMensajeF5 = 1;
          aCodigoMunicipio = "";
          |SUB_EJECUTA "labmunic";
          |SI aCodigoMunicipio ! "";
               aCodMuni = aCodigoMunicipio;
               nContMuni = 1;
          |FINSI;
     |FINSI;
     ||ahora se supone que en aCodMuni tenemos el codigo del municipio
|FPRC;

|PROCESO TipoDocumento;
     aAlfa = #labtraba#7; |QBF aAlfa;
     aAlfa1 = aAlfa % 0101;
     |SI aAlfa1 ] "0"; |Y aAlfa1 [ "9";
          aAlfa = "D";
     |SINO;
          |SI aAlfa1 = "X"; |O aAlfa1 = "Y"; |O aAlfa1 = "Z";
               aAlfa = "E";
          |SINO;
               aAlfa1 = #labtraba#76; |QBF aAlfa1;
               aAlfa2 = aAlfa1 % 0599; aAlfa1 = aAlfa1 % 0103;
               |SI aAlfa1 = "ESP"; |O aAlfa1 = "724"; |O aAlfa1 = "250";
                |O aAlfa1 = "276"; |O aAlfa1 = "056"; |O aAlfa1 = "442";
                |O aAlfa1 = "380"; |O aAlfa1 = "208"; |O aAlfa1 = "372";
                |O aAlfa1 = "826"; |O aAlfa1 = "300"; |O aAlfa1 = "620";
                |O aAlfa1 = "040"; |O aAlfa1 = "246"; |O aAlfa1 = "752";
                |O aAlfa1 = "233"; |O aAlfa1 = "428"; |O aAlfa1 = "440";
                |O aAlfa1 = "616"; |O aAlfa1 = "203"; |O aAlfa1 = "703";
                |O aAlfa1 = "705"; |O aAlfa1 = "348"; |O aAlfa1 = "470";
                |O aAlfa1 = "196"; |O aAlfa2 = "HOLANDA";
                    aAlfa = "U";
               |SINO;
                    aAlfa = "W";
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Ultimo;
     nUltimo = nUltimo + 1;
|FPRC;

|DEFBUCLE Ultimo;
     #labnct03#1;
     ;
     #labtraba#0, #labtraba#1, "A", 01;
     #labtraba#0, #labtraba#1, "A", 99;
     ;
     NULL, Ultimo;
|FIN;

|PROCESO Registro;
/*
     || de momento paso, me quedo con el ultimo registro

     nUltimo = 00;
     |BUCLE Ultimo;
*/

     #labnct03#0 = #labtraba#0;
     #labnct03#1 = #labtraba#1;
     #labnct03#10 = "A";           || para el alta
     #labnct03#16 = 00;
     |LEE 101#labnct03.=;
     FEstado = FSalida;

     #labnct03#2 = #labtraba#45;
     #labnct03#3 = #labtraba#36;
     #labnct03#4 = #labtraba#37;
     #labnct03#5 = nJornada;
     #labnct03#6 = #labtraba#33;

     aUsuario = Usuario % 810;
     |QBF aUsuario;
     #labnct03#7 = aUsuario;       || usuario

     fFecha = ~;
     aFecha = fFecha;
     #labnct03#8 = fFecha;         || fecha

     |HORA aHora;
     |QBF aHora;
     aHora = aHora % 105;
     #labnct03#9 = aHora;          || hora
     #labnct03#10 = "A";           || para el alta
     |SI aParam = "";
          #labnct03#11 = "labnct02";    || desde este proceso
     |SINO;
          #labnct03#11 = "labtraba";    || desde el labtraba
     |FINSI;
     #labnct03#12 = aFichero;      || Nombre de fichero desde el que fue generado

     |SI FEstado = 0;
          |GRABA 020#labnct03.a;
     |SINO;
          |GRABA 020#labnct03.n;
     |FINSI;
     |LIBERA #labnct03;
|FPRC;

|PROCESO Linea;
     |QBF aAlfa;
     aCadena = aAlfa;
     |HAZ QuitaRaros;
     |SI nNivel = 1;
          aCadena = aCadena;
     |FINSI;
     |SI nNivel = 2;
          aCadena = "  " + aCadena;
     |FINSI;
     |SI nNivel = 3;
          aCadena = "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 4;
          aCadena = "  " + "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 5;
          aCadena = "  " + "  " + "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 6;
          aCadena = "  " + "  " + "  " + "  " + "  " + aCadena;
     |FINSI;
     aCadena = aCadena + &13 + &10;
     aAlfa = aCadena;
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO DatosAlta;
     nHorasMax = 40;
     |ABRE #nomconca;
     #nomconca#0 = #labtraba#87;
     |LEE 000#nomconca.=;
     |SI FSalida = 0;
          |SI #nomconca#162 ! 0;
               nHorasMax = #nomconca#162;
          |FINSI;
     |FINSI;
     |CIERRA #nomconca;

     nNivel = 3;
     aAlfa = "<DATOS_ALTA_AFILIACION>";
     |HAZ Linea;

     nNivel = 4;
     aAlfa1 = #labtraba#247; |QBF aAlfa1;
     aAlfa1 = ("0000" + aAlfa1) % -104;
     aAlfa2 = #labempre#13; |QBF aAlfa2;
     aAlfa = "<CCC>" + aAlfa1 + aAlfa2 + "</CCC>";
     |HAZ Linea;

     aAlfa = #labtraba#45;
     aAlfa = "<CONTRATO>" + aAlfa + "</CONTRATO>";
     |HAZ Linea;

     aAlfa1 = #labtraba#36; |QBF aAlfa1;
     |SI aAlfa1 ! ""; |Y aAlfa1 ! "          "; |Y aAlfa1 ! "..........";
          aAlfa = (aAlfa1 % 102) + "/" + (aAlfa1 % 402) + "/" + (aAlfa1 % -104);
     |FINSI;
     aAlfa = "<FECHA_ALTA>" + aAlfa + "</FECHA_ALTA>";
     |HAZ Linea;

     aAlfa = #labtraba#33;
     |QBF aAlfa;
     aAlfa = ("00" + aAlfa) % -102;
     aAlfa = "<GRUPO_TARIFA>" + aAlfa + "</GRUPO_TARIFA>";
     |HAZ Linea;

     nNume = #labtraba#234;
     nJornada = 0;
     |SI nNume ! 0;
          nJornada = nNume / nHorasMax;
          nJornada = (nJornada * 1000) ! 0;
          aAlfa = nJornada;
     |SINO;
          aAlfa = "";
     |FINSI;

     aAlfa = "<COEFICIENTE>" + aAlfa + "</COEFICIENTE>";
     |HAZ Linea;

     nNivel = 3;
     aAlfa = "</DATOS_ALTA_AFILIACION>";
     |HAZ Linea;
|FPRC;

|PROCESO Domicilio;
     eaCodNif = #labtraba#7;
     |HAZ LeeNifs;

     nNivel = 3;
     aAlfa = "<DOMICILIO>";
     |HAZ Linea;

     nNivel = 4;

     aAlfa = #712#3;  |QBF aAlfa;
     aAlfa = "<NUMERO>" + aAlfa + "</NUMERO>";
     |HAZ Linea;

     aAlfa = #712#16;  |QBF aAlfa;
     aAlfa = "<ESCALERA>" + aAlfa + "</ESCALERA>";
     |HAZ Linea;

     aAlfa = #712#11;  |QBF aAlfa;
     aAlfa = "<PUERTA>" + aAlfa + "</PUERTA>";
     |HAZ Linea;

     aAlfa = #712#9;  |QBF aAlfa;
     aAlfa = "<TIPO_VIA>" + aAlfa + "</TIPO_VIA>";
     |HAZ Linea;

     aCodPos = (("00" + #labtraba#8) % -102) + (("000" + #labtraba#9) % -103);
     |HAZ CodigoMuni;
     |SI nContMuni = 0;
          aAlfa1 = "";
     |SINO;
          aAlfa1 = aCodMuni;
     |FINSI;
     aAlfa = "<CODIGO_MUNICIPIO>" + aAlfa1 +  "</CODIGO_MUNICIPIO>";
     |HAZ Linea;

     aAlfa = #712#10;  |QBF aAlfa;
     aAlfa = "<PISO>" + aAlfa + "</PISO>";
     |HAZ Linea;

     aAlfa = #712#2;  |QBF aAlfa;
     aAlfa = "<VIA>" + aAlfa + "</VIA>";
     |HAZ Linea;

     nNivel = 3;
     aAlfa = "</DOMICILIO>";
     |HAZ Linea;
|FPRC;

|PROCESO Representante;
     nNivel = 3;
     aAlfa = "<REPRESENTANTE>";
     |HAZ Linea;

     nNivel = 4;
     aAlfa = #labtraba#22;
     aAlfa1 = aAlfa % 103;
     |SI aAlfa1 = "...";
          aAlfa = "";
     |FINSI;
     aAlfa = "<DNI>" + aAlfa + "</DNI>";
     |HAZ Linea;

     aAlfa = #labtraba#20;
     aAlfa1 = aAlfa % 103;
     |SI aAlfa1 = "...";
          aAlfa = "";
     |FINSI;
     aAlfa = "<NOMBRE>" + aAlfa + "</NOMBRE>";
     |HAZ Linea;

     aAlfa = #labtraba#23;
     aAlfa1 = aAlfa % 103;
     |SI aAlfa1 = "...";
          aAlfa = "";
     |FINSI;
     aAlfa = "<PARENTESCO>" + aAlfa + "</PARENTESCO>";
     |HAZ Linea;

     nNivel = 3;
     aAlfa = "</REPRESENTANTE>";
     |HAZ Linea;
|FPRC;

|PROCESO Trabajadores;
     |SI #labnct12#6 ! "*";
          |ACABA;
     |FINSI;

     #labtraba#0 = #labnct12#0;
     #labtraba#1 = #labnct12#1;
     |LEE 000#labtraba.=;

     #labempre#0 = #labtraba#0;
     |LEE 000#labempre.=;

     nNivel = 2;
     aAlfa = "<TRABAJADOR>";
     |HAZ Linea;

     aCadena = #labtraba#2;
     |HAZ SeparaNom_plb;

     nNivel = 3;

     aAlfa = "<NOMBRE>" + eaNom + "</NOMBRE>";
     |HAZ Linea;
     aAlfa = "<PRIMER_APELLIDO>" + eaApel1 + "</PRIMER_APELLIDO>";
     |HAZ Linea;
     aAlfa = "<SEGUNDO_APELLIDO>" + eaApel2 + "</SEGUNDO_APELLIDO>";
     |HAZ Linea;

     aAlfa1 = #labtraba#11; |QBF aAlfa1;
     |SI aAlfa1 ! ""; |Y aAlfa1 ! "          "; |Y aAlfa1 ! "..........";
          aAlfa = (aAlfa1 % 102) + "/" + (aAlfa1 % 402) + "/" + (aAlfa1 % -104);
     |FINSI;
     aAlfa = "<FECHA_NACIMIENTO>" + aAlfa + "</FECHA_NACIMIENTO>";
     |HAZ Linea;

     aAlfa = #labtraba#7;  |QBF aAlfa;
     aAlfa = "<NUMERO_DOCUMENTO>" + aAlfa + "</NUMERO_DOCUMENTO>";
     |HAZ Linea;

     |HAZ TipoDocumento;
     aAlfa = "<TIPO_DOCUMENTO>" + aAlfa + "</TIPO_DOCUMENTO>";
     |HAZ Linea;

     aAlfa = "";
     |SI #labtraba#252 = "V"; aAlfa = "1"; |FINSI;
     |SI #labtraba#252 = "M"; aAlfa = "2"; |FINSI;
     aAlfa = "<SEXO>" + aAlfa + "</SEXO>";
     |HAZ Linea;

     aAlfa = #labtraba#244; |QBF aAlfa;
     aAlfa = "<TELEFONO>" + aAlfa + "</TELEFONO>";
     |HAZ Linea;

     aAlfa = #labtraba#75; |QBF aAlfa;
     aAlfa = "<NUMERO_SEGURIDAD_SOCIAL>" + aAlfa + "</NUMERO_SEGURIDAD_SOCIAL>";
     |HAZ Linea;

     aAlfa = #labtraba#76; |QBF aAlfa;
     aAlfa1 = aAlfa % 0599; aAlfa = aAlfa % 0103;
     |SI aAlfa = "ESP";                   aAlfa = "724"; |FINSI;
     |SI aAlfa < "000"; |O aAlfa > "999"; aAlfa = "724"; |FINSI;
     aAlfa = "<NACIONALIDAD>" +  aAlfa + "</NACIONALIDAD>";
     |HAZ Linea;

     aAlfa = "<PAIS>" + "724" + "</PAIS>";
     |HAZ Linea;

     aAlfa = #labtraba#73; |QBF aAlfa;
     aAlfa = "<ESTUDIOS>" + aAlfa + "</ESTUDIOS>";
     |HAZ Linea;

     |HAZ Domicilio;

     |HAZ Representante;

     |HAZ DatosAlta;

     nNivel = 2;
     aAlfa = "</TRABAJADOR>";
     |HAZ Linea;

     |HAZ Registro;

     nNumTra = nNumTra + 1;
|FPRC;

|DEFBUCLE Trabajadores;
     #labnct12#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Trabajadores;
|FIN;

|PROCESO GeneraXML;
     |ABRE #labcontr;
     |LEE 000#labcontr.p;
     |CIERRA #labcontr;

     |ABRE #labempre;
     |ABRE #labtraba;
     |ABRE #labnct03;

     aRuta = #labcontr#63;
     |QBF aRuta;
     aAlfa = aRuta % -101;
     |SI aAlfa ! "\"; |Y aAlfa ! "/";
          aRuta = aRuta + "/";
     |FINSI;
     aRuta = aRuta + "DIAGRAMXML/";

     |RMKDIR aRuta;

     aFichero = "TRA_";
     fFecha = ~;
     aFecha = fFecha;
     aFecha = (aFecha % 102) + (aFecha % 402) + (aFecha % 904);
     aFichero = aFichero + aFecha + "_";
     |HORA aHora;
     |QBF aHora;
     aHora = aHora - ":" - ":";
     aFichero = aFichero + aHora + ".xml";

     aRuta = aRuta + aFichero;
     |XABRE "CBW", aRuta, nHandle;
     |SI FSalida ] 0;
          nNivel = 1;
          aAlfa = "<?xml version=" + &34 + "1.0" + &34 + " encoding=" + &34 + "ISO-8859-1" + &34 + " ?>";
          |HAZ Linea;

          aAlfa = "<TRABAJADORES>";
          |HAZ Linea;

          |BUCLE Trabajadores;

          nNivel = 1;
          aAlfa = "</TRABAJADORES>";
          |HAZ Linea;

          |CIERRA #labnct03;
          |CIERRA #labtraba;
          |CIERRA #labempre;
     |FINSI;
     |XCIERRA nHandle;

     |SI nNumTra ! 0;
          aRuta = aRuta - aFichero;
          aAlfa = "    El fichero se ha generado correctamente en " + aRuta;
          |MENAV aAlfa;

          aAlfa1 = nNumTra;

          |SI aParam = "";
               |SI nNumTra = 1;
                    aAlfa = "    Se incluye 1 trabajador.";
               |SINO;
                    aAlfa = "    Se incluyen un total de " + aAlfa1;
                    aAlfa = aAlfa + " trabajadores.";
               |FINSI;
               |MENAV aAlfa;
          |FINSI;
     |SINO;
          aAlfa = "    No se han seleccionado trabajadores para exportar.";
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO sel_min;
     #100#0 = 00001;
     #100#1 = 00001;
|FPRC;

|PROCESO sel_max;
     #100#0 = 99999;
     #100#1 = 99999;
|FPRC;

|PROCESO labtraba;
     || este sera el proceso del pase

     swSigue = 1;        || por defecto SI

     #labempre#0 = #labtraba#0;
     |LEE 000#labempre.=;
     |SI FSalida ! 0;
          aAlfa = #labtraba#0;
          aAlfa = ("00000" + aAlfa) % -105;
          aAlfa = "    ATENCION: Empresa " + aAlfa + " inexistente.";
          |MENAV aAlfa;
          swSigue = 0;
     |FINSI;

     aFecha = #0#13;
     |QBF aFecha;
     |SI aFecha ! "";
          fFechaLimite = aFecha;

          aFechaAlta = #labtraba#36;
          fFechaAlta = aFechaAlta;
          |SI fFechaAlta < fFechaLimite;
               swSigue = 0;
          |FINSI;
     |FINSI;

     #labnct03#0 = #labtraba#0;
     #labnct03#1 = #labtraba#1;
     #labnct03#2 = #labtraba#45;
     #labnct03#10 = "A";
     |LEE 000#labnct03.=;
     |SI FSalida = 0; |Y #0#14 = "N";
          swSigue = 0;
     |FINSI;

     || aqui se seleccionaran los trabajadores y grabare el temporal

     |SI swSigue = 1;
          seleccio = 1;

          #100#0 = #labtraba#0;
          #100#1 = #labtraba#1;
          #100#2 = #labempre#2;
          #100#3 = #labtraba#2;
          #100#4 = #labtraba#36;
          #100#5 = #labtraba#45;
          #100#6 = "*";
          |GRABA 020#100n;
          |LIBERA #100;
     |FINSI;
|FPRC;

|PROCESO salteada_labtraba
     |PARA salte_1 = 21; |SI salte_1 [ 41; |HACIENDO salte_1 = salte_1 + 1;
          salte_2 = salte_1 + 21;
          #labtraba#0 = #0salte_1;   || empresa
          #labtraba#1 = #0salte_2;   || trabajador
          |LEE 000#labtraba.=;
          |SI FSalida = 0;
               |HAZ labtraba;
          |FINSI;
     |SIGUE;
|FPRC;

|DEFBUCLE labtraba;
     #labtraba#1;
     ;
     #0#0, #0#2;
     #0#1, #0#3;
     ;
     NULL, labtraba;
|FIN;

|PROCESO lectura;             || INICIO DEL CALCULO
     |ABRE #labempre;
     |ABRE #labtraba;
     |ABRE #labnct03;

     |SI #0#21 = 0;
          |BUCLE labtraba;
     |SINO;
          |HAZ salteada_labtraba;
     |FINSI;
     |CIERRA #labtraba;

     |SI seleccio = 0; |Y aParam = "labnct02";
          |ATRI P;
          |MENAV "    Seleccion vacia ...";
          |ATRI 0;
     |FINSI;

     |CIERRA #labnct03;
     |CIERRA #labtraba;
     |CIERRA #labempre;
|FPRC;

|PROGRAMA;
     |HORA aHora;
     |QBF aHora;
     aHora = aHora % 105;
     swEdicion = 0;

     aAlfa = Usuario;         |QBF aAlfa;
     aAlfa = "t" +  aAlfa;
     |NOME_DAT #100 aAlfa;
     |DELINDEX #100;

     aParam = PARAMETRO;
     |QBF aParam;

     |SI aParam ! "";
          || vengo de otro sitio, se supone que de la pantalla de trabajadores
          aAlfa1 = aParam % 105;
          aAlfa2 = aParam % -105;
          #0#21 = aAlfa1;
          #0#42 = aAlfa2;
          #0#13 = "";
          #0#14 = "S";
          FSalida = 0;
     |SINO;
          || PINTO PANTALLA Y PIDO DATOS, DESDE EL MENU

          |CLS;
          |CABEZA "Exportacion de Trabajadores a NetContrata@";
          |PINPA #0#0;
          |ABRE #0;
          |DDEFECTO #0;
          |LEE 000#0p;
          |SI FSalida = 0;  |BORRA 020#0a;  |FINSI;
          |CIERRA #0;
          |PINDA #0#0;
          |ENDATOS #1#0;
     |FINSI;

     |SI FSalida = 0;
          |ABRE #100;
          |HAZ lectura;
          |CIERRA #100;
          |SI aParam = "";
               |ABRE #0;
               |GRABA 000#0n;
               |CIERRA #0;
          |FINSI;
     |SINO;
          |ABRE #0;
          |GRABA 000#0n;
          |CIERRA #0;
     |FINSI;

     || si se va a presentar un lineal con la seleccion este es el momento

     |SI seleccio = 0; |Y aParam = "";
          |ATRI P;
          |MENAV "    Seleccion vacia ...";
          |ATRI 0;
     |FINSI;

     |SI aParam = "";
          |ENTLINEAL #1#100, -1, 4, 22, 1, sel_min, sel_max;
     |FINSI;

     || despues se generara el xml y se grabara el registro

     |SI aParam = "";
          aAlfa = "    S¿Desea continuar con la generación del fichero?";
     |SINO;
          aAlfa = "    S¿Generar el fichero xml con los datos del ";
          aAlfa = aAlfa + "trabajador para Netcontrat@?"
     |FINSI;

     |CONFI aAlfa;
     |SI FSalida = 0;
          |HAZ GeneraXML;
     |SINO;
          aAlfa = "    El proceso ha sido cancelado por el usuario";
          |MENAV aAlfa;
     |FINSI;
     |DELINDEX #100;
     |CIERRA #100;
|FPRO;
