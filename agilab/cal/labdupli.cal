|FICHEROS;
    labdupli #0;
    labregis #1;    || registro
    labtraba #2;    || trabajadores
    labduptm #10;   || temporal de duplicados
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    ant_emp = 0;
    ant_tra = 0;
    cuantos = 0;
    pasada = 0;
    borra = 0;
|FIN;

|INCLUYE i_tempo;
____________________________________/ LEE EL TEMPORAL Y BORRA DEL REGISTRO
|PROCESO bor_regis1;
    #0#1 = #1#0;    |PINTA #0#1;

|SI pasada = 1;
        cuantos = cuantos + 1;
|FINSI;
|SI pasada = 2;
        cuantos = cuantos - 1;
    |SI cuantos ! 0;
        |BORRA 020#1a;
        |SI FSalida = 0; borra = borra + 1;  |FINSI;
    |FINSI;
|FINSI;
|FPRC;

|DEFBUCLE bor_regis;
 #1#1;
 1, 2;
      1, #10#0, #10#1;
 999999, #10#0, #10#1;
 ;
 NULL, bor_regis1;
|FIN;

|PROCESO lee_tmp1;
    cuantos = 0;
    pasada = 1;     |BUCLE bor_regis;
    pasada = 2;     |BUCLE bor_regis;

    seleccio = 1;
|FPRC;

|DEFBUCLE lee_tmp;
 #10#1;
 ;
     1,     1;
 99999, 99999;
 ;
 NULL, lee_tmp1;
|FIN;
____________________________________/ LEE EL REGISTRO Y GRABA EL TEMPORAL
|PROCESO lee_regis1;
    #0#1 = #1#0;    |PINTA #0#1;

    #2#0 = #1#1;    || empresa
    #2#1 = #1#2;    || trabajador
|LEE 000#2=;                            || lee en el maestro
|SI FSalida = 0;
    |SI ant_emp = #1#1;|Y ant_tra = #1#2;
        |DDEFECTO #10;
            #10#0 = ant_emp;
            #10#1 = ant_tra;
        |LEE 000#10=;
        |SI FSalida ! 0;
            |GRABA 020#10n;
        |FINSI;
    |FINSI;

        ant_emp = #1#1;
        ant_tra = #1#2;
|SINO;
    |BORRA 020#1a;                 || lo borra si no existe en el maestro
|FINSI;
|FPRC;

|DEFBUCLE lee_regis;
 #1#2;
 ;
     1,     1;
 99999, 99999;
 ;
 NULL, lee_regis1;
|FIN;

|PROCESO tipo3; |TIPO 3;
|SI #0#0 = "SI";
    |HAZ nom_usu;
        alfa1 = nom_tmp;
    |NOME_DAT #10 alfa1;
    |DELINDEX #10;
    |ABRE #2;
    |ABRE #10;
    |BUCLE lee_regis;
    |CIERRA #2;
    |CIERRA #10;
    |BUCLE lee_tmp;
    |SI seleccio = 0;
        |MENAV "    ENHORABUENA!!! No habian duplicados ...";
    |SINO;
        |SI borra = 1;
                alfa1 = "    Borrado [" + borra + "] registro duplicado ...";
        |SINO;
                alfa1 = "    Borrados [" + borra + "] registros duplicados ...";
        |FINSI;
        |MENAV alfa1;
    |FINSI;
    |DELINDEX #10;
|FINSI;
|FPRC;
