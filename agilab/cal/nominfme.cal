|FICHEROS;
     nominfme #0;
     labtraba;
     labempre;
     nomanmel;
     nomconca;
     labpadre;
     nominfm1;
|FIN;

|VARIABLES;
     aDesde = "";
     aHasta = "";
     fDesde = @;
     fHasta = @;
     fech1 = @;
     fech2 = @;
     alfa1 = "";
     nume1 = 0;
     nume2 = 0;
     nume3 = 0;
     &nDiasAlta = 0;
     &nDiasAlta0 = 0;
     &nCodCat = 0;
     &aNomCat = "";
     &nPeriodo = 0;
     &nPeriodo0 = 0;
     nHorasSemana = 0;
     &aSexo = "";
     aAlfa = "";
     swSigue = 0;
     nCampo = 0;
     nCampo1 = 0;
     nCampo2 = 0;
     nContrato = 0;
|FIN;

|PROCESO Defectos;
     fech1 = ~;
     alfa1 = fech1;
     alfa1 = alfa1 % -104;
     nume1 = alfa1;
     #0#2 = nume1 - 1;
     |PINTA #0#2;
|FPRC;

|PROCESO Dias;
     nume3 = 0;

     #labempre#0 = #labtraba#0;
     |LEE 000#labempre.=;

     #labpadre#0 = #labtraba#0;
     #labpadre#1 = #labtraba#1;
     |LEE 000#labpadre.=;
     |SI FSalida = 0;
          aSexo = #labpadre#5;
     |SINO;
          aSexo = " ";
     |FINSI;

     |SI #0#3 = "S";
          #nomanmel#0 = #labtraba#87;
          #nomanmel#1 = #labtraba#17;
          |LEE 000#nomanmel.=;
          |SI FSalida = 0;
               nCodCat = #nomanmel#3;
               aNomCat = #nomanmel#4;
          |SINO;
               nCodCat = 999;
               aNomCat = "Otros";
          |FINSI;
     |SINO;
          nCodCat = #labtraba#17;
          aNomCat = #labtraba#18;
     |FINSI;

     fech1 = #labtraba#36;    || fecha de alta
     fech2 = #labtraba#37;    || fecha de baja
     alfa1 = #labtraba#37;
     |QBF alfa1;
     |SI alfa1 = ""; |O alfa1 = ".........."; |O fech2 > fHasta;
          fech2 = fHasta;
     |FINSI;
     |SI fech1 < fDesde;
          fech1 = fDesde;
     |FINSI;

     |SI fech2 < fDesde;
          |ACABA;
     |FINSI;
     |SI fech1 > fHasta;
          |ACABA;
     |FINSI;

     nume1 = fech1;
     nume2 = fech2;
     nume3 = nume2 - nume1 + 1;

     nHorasSemana = 40;
/*
     #nomconca#0 = #labtraba#87;
     |LEE 000#nomconca.=;
     |SI FSalida = 0;
          |SI #nomconca#162 ! 0;
               nHorasSemana = #nomconca#162;
          |FINSI;
     |FINSI;
*/
     |SI #labtraba#42 = "T"; |O #labtraba#42 = "X";
          |SI #labtraba#234 ! 0;
               nume3 = nume3 * (#labtraba#234 / nHorasSemana);
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Exclusiones;
     nContrato = #labtraba#45;

     |PARA nCampo1 = 1; |SI nCampo1 [ 7; |HACIENDO nCampo1 = nCampo1 + 2;
          nCampo2 = nCampo1 + 1;
          |SI #nominfm1#nCampo1# [ nContrato; |Y nContrato [ #nominfm1#nCampo2#;
               swSigue = 0;
               |SAL;
          |FINSI;
     |SIGUE;

     |PARA nCampo = 9; |SI nCampo [ 20; |HACIENDO nCampo = nCampo + 1;
          |SI nContrato = #nominfm1#nCampo#
               swSigue = 0;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO buscarencia;
     aAlfa = #labtraba#36;    || fecha de alta
     |QBF aAlfa;
     |SI aAlfa = ""; |O aAlfa = "..........";
          |ACABA;
     |FINSI;

     swSigue = 1;
     |HAZ Exclusiones;
     |SI swSigue = 0;
          |ACABA;
     |FINSI;

     nDiasAlta = 0;
     nPeriodo = #0#2;
     aDesde = nPeriodo;
     aDesde = "01.01." + aDesde;
     aHasta = nPeriodo;
     aHasta = "31.12." + aHasta;

     fDesde = aDesde;
     fHasta = aHasta;

     |HAZ Dias;
     nDiasAlta = nume3;

     nDiasAlta0 = 0;
     nPeriodo0 = #0#2 - 1;
     aDesde = nPeriodo0;
     aDesde = "01.01." + aDesde;
     aHasta = nPeriodo0;
     aHasta = "31.12." + aHasta;

     fDesde = aDesde;
     fHasta = aHasta;

     |HAZ Dias;
     nDiasAlta0 = nume3;

     |SI nDiasAlta ! 0; |O nDiasAlta0 ! 0;
          |PRINT;
     |FINSI;
|FPRC;

|DEFBUCLE buscarencia;
     #labtraba#1;
     ;
     #0#0, 00001;
     #0#1, 99999;
     ;
     NULL, buscarencia;
     #labtraba#36;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |ABRE #labempre;
     |ABRE #nomanmel;
     |ABRE #nomconca;
     |ABRE #labpadre;
     |ABRE #nominfm1;

     |LEE 000#nominfm1.p;
     |BUCLE buscarencia;

     |CIERRA #nominfm1;
     |CIERRA #labpadre;
     |CIERRA #nomconca;
     |CIERRA #nomanmel;
     |CIERRA #labempre;
|FPRC;

|PROGRAMA;
     |CLS;
     |HAZ Defectos;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          Impresora = "CrystalReport";
          |IMPRE -1;
          |INFOR "nominfme";
          |HAZ Tipo3;
          |PIEINF;
          |FININF;
          |FINIMP;
     |FINSI;
|FPRO;
