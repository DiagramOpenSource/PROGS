|FICHEROS;
    nomconca #0;
    nomconax #1;
    nomcateg #2;
    nomcatca #3;
    nomcatli #4;

    nomconli;

    nomcorca;
    nomcorli;
    nomdesam;
    nomlisco;
|FIN;

|VARIABLES;
    paga = 0;
    top = 0;
    otro = 0;
    campodef = 0;
    campodia = 0;
    mes = 0;
    topemes = 0;
    baja = 0;
    xxxxfiac = "  ";
    xxaponer = "  ";
    xrelleno = "                              ";
    varcampo = 0;
    swcheck = 0;
    mensaje0 = "0000ATENCION !!! Concepto fuera de rango ...";
    mensaje1 = "0000ATENCION !!! Error de entrada ...";
    mensaje2 = "0000ATENCION !!! Imposible calculo antiguedad, tipos iguales ...";
     aAlfa = "";
     nCampo = 0;
     nCodigoRED = 0;
     nConcepto = 0;
     nDesdeConvenio = 0;
     nHastaConvenio = 0;
     nModo = 0;

     &eaMensaCad = "";
     &enErrorCad = 0;
|FIN;

||**************************************************************************
||                     LLAMADAS DESDE EL MANTENIMIENTO
||**************************************************************************

|PROCESO checconf; |TIPO 0;   || chequea la CONFECCION
|SI #0Campo > 12;|Y #0Campo < 99;
    |MENAV "    Error de entrada ...";
    |ERROR;
|FINSI;
|FPRC;

|PROCESO nomodias; |TIPO 7;    || segun tipo hace no modificable num. dias
    varcampo = Campo - 8;
|SI #0varcampo = "P";
    |HAZ nomodifi;
|FINSI;
|FPRC;

|PROCESO nomoporc; |TIPO 7;    || segun tipo hace no modificable porcentaje
    varcampo = Campo - 16;
|SI #0varcampo = "N";
    |HAZ nomodifi;
|FINSI;
|FPRC;

|PROCESO nomodifi;             || rutina de conversion a no modificables
    xxaponer = Anono;
    xxxxfiac = Control % A1001;
    Control = Control + xxxxfiac;
    Control = Control + xrelleno;
    Control = Control % A120;
    Control = Control + xxaponer;
|FPRC;

|PROCESO checcon1; |TIPO 0;  || chequea rangos de conceptos y paga dentro paga
|SI Campo=52;|O Campo=60;|O Campo=68;|O Campo=76;|O Campo=84;  paga=0; |FINSI;
|SI Campo=53;|O Campo=61;|O Campo=69;|O Campo=77;|O Campo=85;  paga=1; |FINSI;
|SI Campo=54;|O Campo=62;|O Campo=70;|O Campo=78;|O Campo=86;  paga=2; |FINSI;
|SI Campo=55;|O Campo=63;|O Campo=71;|O Campo=79;|O Campo=87;  paga=3; |FINSI;
|SI Campo=56;|O Campo=64;|O Campo=72;|O Campo=80;|O Campo=88;  paga=4; |FINSI;
|SI Campo=57;|O Campo=65;|O Campo=73;|O Campo=81;|O Campo=89;  paga=5; |FINSI;
|SI Campo=58;|O Campo=66;|O Campo=74;|O Campo=82;|O Campo=90;  paga=6; |FINSI;
|SI Campo=59;|O Campo=67;|O Campo=75;|O Campo=83;|O Campo=91;  paga=7; |FINSI;
    top = paga + 200;    || calcula el concepto maximo a incluir en cada paga
    swcheck = 0;
|SI #0Campo =   0; swcheck = 1; |FINSI;
|SI #0Campo = 100; swcheck = 1; |FINSI;
|SI #0Campo = 400; swcheck = 1; |FINSI;
|SI #0Campo ] 101; |Y #0Campo [ 199; swcheck = 1; |FINSI;
|SI #0Campo ] 201; |Y #0Campo [ top; swcheck = 1; |FINSI;
|SI #0Campo ] 301; |Y #0Campo [ 310; swcheck = 1; |FINSI;
|SI #0Campo ] 401; |Y #0Campo [ 420; swcheck = 1; |FINSI;
|SI swcheck = 0;
    |MENAV mensaje0;
    |ERROR;
|FINSI;
|FPRC;

|PROCESO mismo; |TIPO 0;      || chequea que claves de ant.y plus no sean
|SI Campo = 2;                || las dos = 3
    otro = 3;
|SINO;
    otro = 2;
|FINSI;
|SI #0Campo = 3; |Y #0otro = 3;
    |MENAV mensaje2;
    |ERROR;
|SINO;
    |POPV;
|FINSI;
|FPRC;

|PROCESO desdedi1; |TIPO 0;
    campodia = Campo + 104;
|SI #0campodia = 0;|Y #0Campo ! 0;
        #0campodia = 1;
    |PINTA #0campodia;
|FINSI;
|FPRC;

|PROCESO hastadi1; |TIPO 0;
    campodia = Campo + 104;
|SI #0campodia = 0;|Y #0Campo ! 0;
        mes = #0Campo;
    |HAZ ulti_dia;
        #0campodia = topemes;
    |PINTA #0campodia;
|FINSI;
|FPRC;

|PROCESO desdedi2; |TIPO 0;
    campodia = Campo + 32;
|SI #0campodia = 0;|Y #0Campo ! 0;
        #0campodia = 1;
    |PINTA #0campodia;
|FINSI;
|FPRC;

|PROCESO hastadi2; |TIPO 0;
    campodia = Campo + 32;
|SI #0campodia = 0;|Y #0Campo ! 0;
        mes = #0Campo;
    |HAZ ulti_dia;
        #0campodia = topemes;
    |PINTA #0campodia;
|FINSI;
|FPRC;

|PROCESO ulti_dia;
|SI mes =  1;  topemes = 31;  |FINSI;
|SI mes =  2;  topemes = 28;  |FINSI;
|SI mes =  3;  topemes = 31;  |FINSI;
|SI mes =  4;  topemes = 30;  |FINSI;
|SI mes =  5;  topemes = 31;  |FINSI;
|SI mes =  6;  topemes = 30;  |FINSI;
|SI mes =  7;  topemes = 31;  |FINSI;
|SI mes =  8;  topemes = 31;  |FINSI;
|SI mes =  9;  topemes = 30;  |FINSI;
|SI mes = 10;  topemes = 31;  |FINSI;
|SI mes = 11;  topemes = 30;  |FINSI;
|SI mes = 12;  topemes = 31;  |FINSI;
|FPRC;

|PROCESO a2; |TIPO 7;
|SI FSalida = 5;|O FSalida = 4;    |ACABA;   |FINSI;

|PUSHV 11121871;
|CUADRO 11 12 18 71;
|ATRI R;
||           "3456789012345678901234567890123456789012345678901234567890"
|PINTA 1213, " --------- CLAVES DE ANTIGUEDAD(102) POSIBLES ----------- ";
|PINTA 1313, " (1) Importe segun unidades y valor del concepto 102      ";
|PINTA 1413, " (2) Porcentaje s/a¤os antiguedad sobre [S.Base]          ";
|PINTA 1513, " (2.bis) Importe por Categorias s/a¤os antiguedad         ";
|PINTA 1613, " (3) Porcentaje s/a¤os antiguedad sobre [S.Base+Plus]     ";
|PINTA 1713, " (4) Porcentaje s/a¤os antiguedad sobre [Tabla Bases]     ";
|ATRI 0;
|FPRC;

|PROCESO a3; |TIPO 7;
|SI FSalida = 5;|O FSalida = 4;    |ACABA;   |FINSI;

|PUSHV 13151874;
|CUADRO 13 15 18 74;
|ATRI R;
||           "3456789012345678901234567890123456789012345678901234567890"
|PINTA 1416, " ----------- CLAVES DE PLUS(103) POSIBLES --------------- ";
|PINTA 1516, " (1) Importe segun unidades y valor del concepto 103      ";
|PINTA 1616, " (2) Porcentaje desde unidades sobre [S.Base]             ";
|PINTA 1716, " (3) Porcentaje desde unidades sobre [S.Base+Antiguedad]  ";
|ATRI 0;
|FPRC;

||**************************************************************************
||              CALCULOS DE BORRADO DE FICHEROS RELACIONADOS
||**************************************************************************

|PROCESO auxiliar;   || auxiliar convenios
|BORRA 020#1a;
|LIBERA #1;
|FPRC;

|DEFBUCLE nomconca0;    || (0)
 #1#1;
 ;
 #0#0;
 #0#0;
 be;
 NULL, auxiliar;
|FIN;

|PROCESO catego;  || categorias
|BORRA 020#2a;
|LIBERA #2;
|FPRC;

|DEFBUCLE nomconca1;    || (1)
 #2#1;
 ;
 #0#0,  0;
 #0#0, 99;
 be;
 NULL, catego;
|FIN;

|PROCESO catcabs;  || auxiliar categorias cabeceras
|BORRA 020#3a;
|BUCLELIN 1NULL#3;
|LIBERA #3;
|FPRC;

|DEFBUCLE nomconca2;   ||  (2)
 #3#1;
 ;
 #0#0,  0;
 #0#0, 99;
 be;
 NULL, catcabs;
|FIN;

|PROCESO borracon; |TIPO 2;
    baja = FEntrada / 100;
    baja = baja ! 0;
|SI baja = 3;
    |BUCLE nomconca0;   || auxiliar convenios
    |BUCLE nomconca1;   || categorias
    |BUCLE nomconca2;   || auxiliar categorias
|FINSI;
|FPRC;

|PROCESO Agitool_nomconca; |TIPO 8;
     eaMensaCad = "9";
     |HAZ ControlCaducidad;
     |SI enErrorCad = 1;
         |PTEC 807;
         |ACABA;
     |FINSI;
|FPRC;

/****************************************************************************/
/**** PROCESOS PARA REALIZAR LA RECARGA DE UNO O DE TODOS LOS CONVENIOS  ****/
/****************************************************************************/

|PROCESO Pagas;
     #nomconca#0 = #nomcorli#0;
     |LEE 000#nomconca.=;
     |SI FSalida = 0;
          nCampo = #nomcorli#1 - 200 + 19;
          |SI #nomconca#nCampo# = 99;
               nCodigoRED = 0005;
          |SINO;
               nCodigoRED = 0004;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO RecargaCon;
     |DDEFECTO #nomcorli;

     nConcepto = #nomconli#2;
     #nomcorli#0 = #nomconli#0;
     #nomcorli#1 = #nomconli#2;
     |LEE 000#nomcorli.=;
     |SI FSalida ! 0;
          #nomcorli#0 = #nomconli#0;
          #nomcorli#1 = #nomconli#2;
          #nomcorli#2 = #nomconli#6;

          #nomdesam#0 = #nomconli#0;
          #nomdesam#1 = #nomconli#2;
          |LEE 000#nomdesam.=;
          |SI FSalida = 0;
               #nomcorli#3 = #nomdesam#2;
          |FINSI;

          |SI #nomconli#2 < 400;
               #nomcorli#4 = "S";
          |SINO;
               |SI #nomconli#2 ] 400; |Y #nomconli#2 [ 499;
                    |SI #nomconli#11 = "S"; |Y #nomconli#12 = "7";
                         #nomcorli#4 = "S";
                    |SINO;
                         #nomcorli#4 = "N";
                    |FINSI;
               |SINO;
                    #nomcorli#4 = "S";
               |FINSI;
          |FINSI;

          nCodigoRED = 0001;
          |SI nConcepto = 125;
               nCodigoRED = 0006;
          |FINSI;
          |SI 301 [ nConcepto; |Y nConcepto [ 305;
               nCodigoRED = 0003;
          |FINSI;
          |SI 306 [ nConcepto; |Y nConcepto [ 310;
               nCodigoRED = 0002;
          |FINSI;
          |SI nConcepto = 125;
               nCodigoRED = 0006;
          |FINSI;
          |SI 201 [ nConcepto; |Y nConcepto [ 299;
               |HAZ Pagas;
          |FINSI;
          |SI nConcepto = 410; |O nConcepto = 415;
               nCodigoRED = 0054;
          |FINSI;

          #nomcorli#5 = nCodigoRED;
          #nomlisco#0 = #nomcorli#5;
          |LEE 000#nomlisco.=;
          |SI FSalida = 0;
               #nomcorli#6 = #nomlisco#1;
               #nomcorli#7 = #nomlisco#2;
               #nomcorli#8 = #nomlisco#3;
          |FINSI;


          |SI nCodigoRED = 0001;
          |Y nConcepto ! 101; |Y nConcepto ! 102;
               #nomcorli#9 = "*";
          |SINO;
               #nomcorli#9 = "";
          |FINSI;

          |SI nCodigoRED = 0054;
               #nomcorli#9 = "*";
          |FINSI;

          |GRABA 020#nomcorli.n;
          |LIBERA #nomcorli;
     |FINSI;
|FPRC;

|DEFBUCLE RecargaCon;
     #nomconli#1;
     ;
     nDesdeConvenio, 000;
     nHastaConvenio, 999;
     be;
     NULL, RecargaCon;
|FIN;

|PROCESO Recarga;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 3; |O nModo = 4;
          |ACABA;
     |FINSI;

     |ABRE #nomcorca;
     |ABRE #nomcorli;
     |ABRE #nomdesam;
     |ABRE #nomlisco;

     #nomcorca#0 = #0#0;
     |LEE 000#nomcorca.=;
     |SI FSalida ! 0;
          #nomcorca#0 = #0#0;
          #nomcorca#1 = #0#1;
          |GRABA 020#nomcorca.n;

          nDesdeConvenio = #0#0;
          nHastaConvenio = #0#0;
          |BUCLE RecargaCon;
     |FINSI;

     |CIERRA #nomlisco;
     |CIERRA #nomdesam;
     |CIERRA #nomcorli;
     |CIERRA #nomcorca;
|FPRC;

|PROCESO Convenios;
     #nomcorca#0 = #nomconca#0;
     #nomcorca#1 = #nomconca#1;
     |GRABA 020#nomcorca.n;
     |LIBERA #nomcorca;
|FPRC;

|DEFBUCLE Convenios;
     #nomconca#1;
     ;
     000;
     999;
     ;
     NULL, Convenios;
|FIN;

|PROCESO RecargaInicial; |TIPO 7;
     || migue

     |ABRE #nomconca;
     |ABRE #nomcorli;
     |ABRE #nomdesam;
     |ABRE #nomlisco;

     |ABRE #nomcorca;
     |LEE 000#nomcorca.p;
     |SI FSalida ! 0;
          |BUCLE Convenios;

          nDesdeConvenio = 000;
          nHastaConvenio = 999;
          |BUCLE RecargaCon;

          #0#0 = 001;
          |PINTA #0#0;
     |FINSI;

     |CIERRA #nomlisco;
     |CIERRA #nomdesam;
     |CIERRA #nomcorli;
     |CIERRA #nomcorca;
     |CIERRA #nomconca;

|FPRC;
