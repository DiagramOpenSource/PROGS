|FICHEROS;
    labcomsm #0;
    labempre #1;
    labtraba #2;
    labregis #3;
|FIN;

|VARIABLES;
    nume1 = 0;
    nume2 = 0;
    v = 0;
    mod = 0;
    verrfec = "0000ATENCION Fecha erronea";
    vdia1 = "";
    vme1 = "";
    vany1 = "";
    vdia = 0;
    vme = 0;
    vany = 0;
    vtope = 0;
    vsw = 0;
    vfec = "";
    vblanco = "          ";
    vpuntos = "..........";
    informe = "labcomsm";
    mensa1 = "    No existe empresa";
    mensa2 = "    No existe trabajador";
    mensa3 = "    No existe contrato en Registro";
    mensa9 = "    AVISO!!! Periodo superior a 4 a¤os !!!";
    &dura = "";
    direcc = "";
    numero = "";
    piso = "";
    &domemp = "";
    linepunt = "";
    punt = ".";
    barra = "  /";
    compactado = "";
    diaprimer = 0;
    mesprimer = 0;
    duracio = 0;
    nada = "";
    z = 0;
    &coma = ",";
    &dia = "";             || fecha presentacion desglosada
    &mes = "";
    &anyo = "";
    dia1 = "";            || variables trabajo fecha
    any1 = "";
    fec1 = "";
    fec2 = 0;
    &prorro = " ";
    &duracion = " ";
    &contra = " ";
    c = "";
    menos = 0;
    xfecreal = @;
    xfecnume = 0;
    xfecalfa = "";
    xfectope = "";
    xdiaalfa = "";
    xmesalfa = "";
    xanyalfa = "";
    xdianume = 0;
    xmesnume = 0;
    xanynume = 0;
    xtopemes = 0;
    xdiashas = 0;
    xdiastop = 0;
    xmeses = " meses";
    xdias  = " dias";
    xy     = " y ";
    xmod   = 0;
    xcheck = 0;
    mesestra = "";
    diastra = "";
    duratra = "";
|FIN;

|INCLUYE i_comun;

|PROCESO leeempre;|TIPO 0;    || CALCULO PARA ASEGURAR EFECTIVIDAD
|ABRET #1;                    || DE LA RELACION CON EL FICHERO DE
        #1#0 = #0#0;          || EMPRESAS
|LEE 040#1=;
|SI FSalida = 0;
    |PINTA 0728,#1#2;
|SINO;
    |MENAV mensa1;
        #0#0 = 0;
    |ERROR;
|FINSI;
|CIERRAT #1;
|FPRC;

|PROCESO leetraba;|TIPO 0;    || CALCULO PARA ASEGURAR EFECTIVIDAD
|ABRET #2;                    || EN LA RELACION CON EL FICHERO DE
    #2#0 = #0#0;              || TRABAJADORES
    #2#1 = #0#1;
|LEE 040#2=;
|SI FSalida = 0;
    |PINTA 0828,#2#2;
|SINO;
    |MENAV mensa2;
        #0#1 = 0;
    |ERROR;
|FINSI;
|CIERRAT #2;
|FPRC;

|PROCESO impresio; |TIPO 3;
|HAZ r_regis0;   r_emp = #0#0;  |HAZ r_regis1;
|IMPRE 0;
|SI FSalida = 0;
    |HAZ decodifi;
    |HAZ domicili;
    |PARA v = 1; |SI v [ #0#13; |HACIENDO v = v + 1;
        |INFOR informe;
        |PRINT;
        |PIEINF;
    |FININF;
    |SIGUE;
|SINO;
    |MENAV "    Proceso Abortado ...";
|FINSI;
|FINIMP;
|FPRC;

|PROCESO fechacon; |TIPO 0;
        vfec = #0Campo; vsw = 0;
|SI vfec ! vblanco; |Y vfec ! vpuntos; |Y vfec ! Aindefinido;
  vdia1 = vfec % A102;
  vme1 = vfec % A402;
  vany1 = vfec % A704;
  vdia = vdia1; vme = vme1; vany = vany1;
  |SI vdia ! 0; |Y vme ! 0; |Y vany ! 0;
     |SI vme [ 12; |Y vme ] 1;
       |SI vme = 1; |O vme = 3; |O vme = 5; |O vme = 7; |O vme = 8; |O vme = 10; |O vme = 12;
           vtope = 31;
       |SINO;
           |SI vme = 2;
               mod = vany $ 4;
               |SI mod = 0;
                   vtope = 29;
               |SINO;
                   vtope = 28;
               |FINSI;
            |SINO;
               vtope = 30;
            |FINSI;
        |FINSI;
        |SI vdia [ vtope;
            vsw = 1;
        |FINSI;
     |FINSI;
  |FINSI;
|SINO;
  vsw = 1;
|FINSI;
|SI vsw = 0;
    |MENAV verrfec;
    |ERROR;
|FINSI;
|FPRC;

|PROCESO decodifi;             || RUTINA DE DEGLOSE FECHA PRESENTACION
|SI #0#5 ! vpuntos; |Y #0#5 ! vblanco;
        fec1 = #0#5;
        dia1 = fec1 % 102;
        dia  = dia1;
        any1 = fec1 % 704;
        anyo = any1;
        fec1 = fec1 % 402;
        fec2 = fec1;
    |SI fec2 = 1;
        mes = "Enero ....";
    |FINSI;
    |SI fec2 = 2;
        mes = "Febrero ..";
    |FINSI;
    |SI fec2 = 3;
        mes = "Marzo ....";
    |FINSI;
    |SI fec2 = 4;
        mes = "Abril ....";
    |FINSI;
    |SI fec2 = 5;
        mes = "Mayo .....";
    |FINSI;
    |SI fec2 = 6;
        mes = "Junio ....";
    |FINSI;
    |SI fec2 = 7;
        mes = "Julio ....";
    |FINSI;
    |SI fec2 = 8;
        mes = "Agosto ...";
    |FINSI;
    |SI fec2 = 9;
        mes = "Septiembre";
    |FINSI;
    |SI fec2 = 10;
        mes = "Octubre ..";
    |FINSI;
    |SI fec2 = 11;
        mes = "Noviembre ";
    |FINSI;
    |SI fec2 = 12;
        mes = "Diciembre ";
    |FINSI;
|SINO;
    dia = "..";
    mes = "..........";
    anyo = "....";
|FINSI;
|FPRC;

|PROCESO domicili;                    || RUTINA DEL CALCULO actualiz
    direcc = #1#3;                    || PARA CONSTRUCCION DOMICILIOS
    numero = #1#4;
    piso   = #1#5;
|HAZ compac;
    domemp = compactado;              || domicilio empresa
|FPRC;

|PROCESO compac;                      || SUBRUTINA DE RUTINA domicili
    compactado = "";                  || DE COMPACTACION DE CAMPOS
    |QBF direcc;                      || USADA TAMBIEN EN CONT. 05 Y 25
    |QBF numero;
    |QBF piso;
    compactado = direcc + coma;
    compactado = compactado + numero;
    compactado = compactado + "-";
    compactado = compactado + piso;
    linepunt = punt * 40;
    compactado = compactado + linepunt;
    compactado = compactado % 140;
|FPRC;

|PROCESO siexiste; |TIPO 0;
|ABRET #3;
|SELECT #2#3;
    #3#1 = #0#0;
    #3#2 = #0#1;
|LEE 040#3=;
|SI FSalida = 0;
        #0#6  = #3#6;
        #0#7  = #3#9;
        #0#8  = #3#10;
        #0#9  = #3#7;
        #0#10 = #3#8;
        #0#11 = #3#12;
        #0#12 = #3#14;
    |PINTA #0#6;
    |PINTA #0#7;
    |PINTA #0#8;
    |PINTA #0#9;
    |PINTA #0#10;
    |PINTA #0#11;
    |PINTA #0#12;
|SINO;
        #0#6 = "";
        #0#7 = "";
        #0#8 = "";
        #0#9 = "";
        #0#10 = "";
        #0#11 = "";
        #0#12 = "";
    |PINTA #0#6;
    |PINTA #0#7;
    |PINTA #0#8;
    |PINTA #0#9;
    |PINTA #0#10;
    |PINTA #0#11;
    |PINTA #0#12;
    |MENAV mensa3;
|FINSI;
|CIERRAT #3;
|FPRC;

|PROCESO duracion; |TIPO 0;                 || CALCULA DURACION DE CONTRATOS
    #0#10 = "..........";
    xcheck = #0#7 + #0#8;
|SI xcheck ! 0; |Y #0#9 ! vblanco; |Y #0#9 ! vpuntos;
        nume1 = #0#7 / 1000;            || meses a sumar en formato suma
        nume2 = (#0#8 - 1) + nume1;     || fecha a sumar en formato suma
        xfecreal = #0#9;
        xfecreal = xfecreal + nume2;
        #0#10 = xfecreal;
    |PINTA #0#10;
        nume2 = (-1) + (4 / 1000000);  || suma 4 a¤os para saber el tope
        xfecreal = #0#9;
        xfecreal = xfecreal + nume2;
        nume1 = xfecreal;   || dias hasta (tope)
        xfecreal = #0#10;
        nume2 = xfecreal;   || dias hasta
    |SI nume2 > nume1;|Y FSalida ! 2;  || evita el segundo mensaje
        |MENAV mensa9;      || chequea los 4 a¤os
    |FINSI;
|FINSI;

    mesestra = #0#7;
    diastra  = #0#8;
|HAZ litedura;        || calcula literal de la duracion
    dura = duratra;
|FPRC;

|PROCESO litedura;              || CONSTRUYE LITERAL DURACION PARA IMPRESION
    duratra = "";
    |SI mesestra ! 0;
        duratra = mesestra;
        duratra = duratra + xmeses;
    |SINO;
        duratra = "...........";
    |FINSI;
    |SI diastra ! 0;
        |SI mesestra ! 0;
            duratra = duratra + xy;
        |FINSI;
        duratra = duratra + diastra;
        duratra = duratra + xdias;
    |SINO;
        duratra = duratra + "............";
    |FINSI;
|FPRC;
