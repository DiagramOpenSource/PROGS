|FICHEROS;
|FIN;

|VARIABLES;
     aNombreFich  = "";
     aNombreTexto = "";
     aBase        = "";
     aPlantilla   = "";
     aDirMenu     = "";
     aOrigen      = "";
     aDestino     = "";
     aExtension   = "";
     aCadena1     = "";
     aCadena2     = "";
     aAlfa        = "";
     aAlfa1       = "";
     aAlfa2       = "";
     aAlfa3       = "";
     aAlfa4       = "";
     aLen         = "";
     aImpor1      = "";
     aImpor2      = "";
     aImpor3      = "";
     aImpor4      = "";
     aCadena      = "";
     aCaracter    = "";
     aIPRemoto    = "";
     aDecima      = "";
     aEntero      = "";
     aLong        = "";
     aMenav       = "";
     aLetra       = "";
     aDocRemoto   = "";
     aDocServidor = "";
     aBasePlant   = "";
     aFicheroLCK  = "";
     aFicheroIMA  = "";
     aAbre        = "";
     aEjecuta     = "";

     nSwBueno      = 0;
     nContaPuntero = 0;
     nConta        = 0;
     nEmpresas     = 0;
     nLen          = 0;
     nCampo        = 0;
     nPos          = 0;
     nLong         = 0;
     nDecimal      = 0;
     nCaracter     = 0;
     nVeces        = 0;
     nPausa        = 0;
     nHandle       = 0;

     {1}aContiene     = "";

     &eaContador      = "";
     &eaImporte       = "";
     &eaAlfa          = "";
     &eaOrigen        = "";
     &eaDestino       = "";
     &eaNomUbicacion  = "";
     &eaRutaDocumento = "";

     &enFSalida       = 0;
     &enCodUbica      = 0;

     &enHandleTxt     = 0;
     &eaAsigna        = "";
     &eaConten        = "";
     &eaFicheroTra    = "";
     &eaParametro1    = "";
     &eaParametro2    = "";
     &eaParametro3    = "";
     &eaParametro4    = "";
     &eaParametro5    = "";
     &eaUnidadBasico  = "";
     &eaDSMAC         = "";
|FIN;

|PROCESO EnviaFichero;
     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |SI aIPRemoto ! "";
         |ENVIA_FICHERO eaOrigen, eaDestino;
     |SINO;
         |COPIA_FICHERO eaOrigen, eaDestino;
     |FINSI;
|FPRC;

|PROCESO RecibeFichero;
     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |SI aIPRemoto ! "";
         |RECIBE_FICHERO eaOrigen, eaDestino;
     |SINO;
         |COPIA_FICHERO eaOrigen, eaDestino;
     |FINSI;
|FPRC;

|PROCESO EnviaFicheroConMD5;
     aContiene1 = eaDestino;
     |ESPECIFICA "REMOTOMD5", aContiene;
     aDocRemoto = FSalida;  |QBF aDocRemoto;

     aContiene1 = eaOrigen;
     |ESPECIFICA "MD5", aContiene;
     aDocServidor = FSalida;  |QBF aDocServidor;

     |PULSATECLA;

     |SI aDocRemoto ! aDocServidor;
         aIPRemoto = "";
         |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

         |SI aIPRemoto ! "";
             |ENVIA_FICHERO eaOrigen, eaDestino;
         |SINO;
             |COPIA_FICHERO eaOrigen, eaDestino;
         |FINSI;

         aContiene1 = eaDestino;
         |ESPECIFICA "REMOTOMD5", aContiene;
         aDocRemoto = FSalida;  |QBF aDocRemoto;
     |FINSI;

     |PULSATECLA;
|FPRC;

|PROCESO CogeMac;
     |SI eaDSMAC ! "";  |ACABA;  |FINSI;

     |HAZ LeeUnidadBasico;

     aAlfa = eaUnidadBasico + "\MODELOS";  |RMKDIR aAlfa;
     aAlfa = aAlfa + "\DSMAC";             |RMKDIR aAlfa;

     |DBASE aAlfa;   |QBF aAlfa;
     aAlfa      = aAlfa + "plugins/";
     aOrigen   = aAlfa + "dsmac.exe";
     aDestino  = eaUnidadBasico + "\MODELOS\DSMAC\DSMAC.EXE";

     aContiene1 = aDestino;
     |ESPECIFICA "REMOTOMD5", aContiene;
     aDocRemoto = FSalida;  |QBF aDocRemoto;

     aContiene1 = aOrigen;
     |ESPECIFICA "MD5", aContiene;
     aDocServidor = FSalida;  |QBF aDocServidor;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |SI aDocRemoto ! aDocServidor;
         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aOrigen, aDestino;
         |SINO;
             |COPIA_FICHERO aOrigen, aDestino;
         |FINSI;
     |FINSI;

     |XABRE "EC", aDestino, nHandle;
     |SI FSalida < 0;
         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aOrigen, aDestino;
         |SINO;
             |COPIA_FICHERO aOrigen, aDestino;
         |FINSI;
     |FINSI;

     aAbre    = eaUnidadBasico + "\MODELOS\DSMAC\DSMAC.TXT";  |RFBORRA aAbre;

     aEjecuta = "cmd " + &34 + "/c START /WAIT " + aDestino + " ";
     aEjecuta = aEjecuta +  eaUnidadBasico + "\MODELOS\DSMAC\DSMAC.TXT" + &34;
     |RSYSTEM aEjecuta;

     aAbre    = eaUnidadBasico + "\MODELOS\DSMAC\DSMAC.TXT";
     |XABRE "EC", aAbre, nHandle;
     |SI FSalida < 0;
         aEjecuta = "START "+ aDestino + " ";
         aEjecuta = aEjecuta +  eaUnidadBasico + "\MODELOS\DSMAC\DSMAC.TXT" + &34;
         |RSYSTEM aEjecuta;

         |PARA;  |SI;  |HACIENDO;
                 |PARA nPausa = 1; |SI nPausa [ 10000; |HACIENDO nPausa = nPausa + 1;
                 |SIGUE;

                 |XABRE "EC", aAbre, nHandle;
                 |SI FSalida ] 0;
                     |SAL;
                 |FINSI;

                 |PULSATECLA;
         |SIGUE;
     |FINSI;

     eaDSMAC   = "";
     aAbre    = eaUnidadBasico + "\MODELOS\DSMAC\DSMAC.TXT";
     |XABRE "CU", aAbre, nHandle;
     |SI FSalida < 0;   |ACABA;  |FINSI;

     |XLEE nHandle, 250, eaDSMAC;
     |SI FSalida < 0
         eaDSMAC   = "";
     |FINSI;
     |XCIERRA nHandle;

     |QBF eaDSMAC;
|FPRC;

|PROCESO CopiaFichero;
     aAlfa = "";
     |IP_REMOTO aAlfa;  |QBT aAlfa;
     |SI aAlfa ! "";
         |ENVIA_FICHERO eaOrigen, eaDestino;
     |SINO;
         |COPIA_FICHERO eaOrigen, eaDestino;
     |FINSI;
|FPRC;

|PROCESO TraeDsAbreExtension;
     |HAZ LeeUnidadBasico;

     |DBASE aAlfa;   |QBF aAlfa;
     aAlfa      = aAlfa + "plugins/";
     eaOrigen   = aAlfa + "dsabreextension.exe";
     eaDestino  = eaUnidadBasico + "/MODELOS/DOCUMENTOS";
     eaDestino  = eaUnidadBasico + "/MODELOS/DOCUMENTOS/dsabreextension.exe";

     |HAZ EnviaFicheroConMD5;
|FPRC;

|PROCESO GrabaContenido;
     aAlfa = eaAsigna % 101;
     |SI aAlfa ! "V";  |Y aAlfa ! "M";
      |Y aAlfa ! "I";  |Y aAlfa ! "T";
      |Y aAlfa ! "X";  |Y aAlfa ! "P";
      |Y aAlfa ! "C";
         |ACABA;
     |FINSI;

     |SI aAlfa = "V";
         aAlfa = "W" + &13 + &10;  |XGRABA enHandleTxt, aAlfa;
     |SINO;
         |SI aAlfa ! "C";
             aAlfa = (eaAsigna % 101) + &13 + &10;  |XGRABA enHandleTxt, aAlfa;
         |SINO;
             aAlfa = "V" + &13 + &10;  |XGRABA enHandleTxt, aAlfa;
         |FINSI;
     |FINSI;

     aAlfa = (eaAsigna % 240) + &13 + &10;  |XGRABA enHandleTxt, aAlfa;
     aAlfa = eaConten + &13 + &10;          |XGRABA enHandleTxt, aAlfa;
|FPRC;

|PROCESO EjecutaDSXML;
     || eaParametro1 = Path y fichero exe
     || eaParametro2 = Path y fichero txt
     || eaParametro3 = Origen  del dsxml
     || eaParametro4 = Destino 1 del dsxml
     || eaParametro5 = Destino 2 del dsxml

     |XABRE "EC", eaParametro2, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     aFicheroLCK  = eaParametro2 < "";
     aFicheroLCK  = aFicheroLCK - ".txt" + ".lck";
     aFicheroIMA  = eaParametro5 < "";
     aFicheroIMA  = aFicheroIMA + "~$" + (eaFicheroTra % 215);  |QBF aFicheroIMA;

     |SI eaParametro3 ! "";
         eaOrigen       = eaParametro3 + "dsxml.exe";
         eaDestino      = eaParametro4 + "dsxml.exe";
         |HAZ EnviaFicheroConMD5;
     |FINSI;

     |RFBORRA aFicheroLCK;

     aAlfa  = "START " + eaParametro1 + " " + eaParametro2;
     |RSYSTEM aAlfa;

     |PARA;  |SI;  |HACIENDO;
             |PARA nPausa = 1; |SI nPausa [ 10000; |HACIENDO nPausa = nPausa + 1;
             |SIGUE;

             |XABRE "EC", aFicheroLCK, nHandle;
             |SI FSalida ] 0;
                 |SAL;
             |FINSI;

             |PULSATECLA;
     |SIGUE;

     |PARA nVeces = 0;  |SI nVeces [ 20;  |HACIENDO nVeces = nVeces + 1;
             |XABRE "EC", aFicheroIMA, nHandle;
             |SI FSalida < 0;  |SAL;  |FINSI;

             |MENSA "0000 Esperando cierre del documento.";
             |SLEEP 1;
             |PULSATECLA;
     |SIGUE;

     |RFBORRA eaParametro2;
     |RFBORRA aFicheroLCK;

     |PULSATECLA;
     |MENSA "0000  ";
|FPRC;

|PROCESO Importe;    || Calculo para poner las puntos y comas
                     || en el importe

     aDecima   = "";
     aEntero   = "";
     aLong     = eaImporte % 0;
     nLong     = aLong;
     nDecimal  = 0;
     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPos      = (100 * nCaracter) + 1;
           aCaracter = eaImporte % nPos;
           |SI aCaracter = ".";
               nDecimal = 1;
           |SINO;
               |SI nDecimal = 0;
                   aEntero = aEntero + aCaracter;
               |SINO;
                   aDecima = aDecima + aCaracter;
               |FINSI;
           |FINSI;
     |SIGUE;
     aDecima = (aDecima + "00") % 102;
     eaImporte = aEntero;

     aLong = eaImporte % 0;
     nLong = aLong;
     eaImporte = "             " + eaImporte;
     eaImporte = eaImporte % -113;
     aImpor1   = eaImporte % -103;
     aImpor2   = eaImporte % -403;
     aImpor3   = eaImporte % -703;
     aImpor4   = eaImporte % -1004;
     |SI nLong ] 10;
         |SI aImpor4 ! "  -";
             eaImporte = aImpor4 + "." + aImpor3  + "." + aImpor2 + "." + aImpor1;
         |SINO;
             eaImporte = aImpor4 + aImpor3  + "." + aImpor2 + "." + aImpor1;
      |FINSI;
    |SINO;
        |SI nLong ] 7;
            |SI aImpor3 ! "  -";
                eaImporte = aImpor3  + "." + aImpor2 + "." + aImpor1;
            |SINO;
                eaImporte = aImpor3 + aImpor2 + "." + aImpor1;
            |FINSI;
        |SINO;
            |SI nLong ] 4;
                |SI aImpor2 ! "  -";
                    eaImporte = aImpor2 + "." + aImpor1;
                |SINO;
                    eaImporte = aImpor2 + aImpor1;
                |FINSI;
            |SINO;
                eaImporte = aImpor1;
            |FINSI;
        |FINSI;
     |FINSI;

     eaImporte = ("             " + eaImporte + "," + aDecima) % -113;

     |SI eaImporte = "            0";  eaImporte = "             ";  |FINSI;
     |SI eaImporte = "         0,00";  eaImporte = "             ";  |FINSI;
|FPRC;

|PROCESO QuitaRaros;
     |QBF eaAlfa;
     aCadena = "";
     aLen    = eaAlfa % 0;
     nLen    = aLen;
     |PARA nCampo = 1;  |SI nCampo [ nLen;  |HACIENDO nCampo = nCampo + 1;
           nPos = (100 * nCampo) + 1;
           aCaracter = eaAlfa % nPos;
           |SI aCaracter = "¥";  aCaracter = &209; |FINSI;
           |SI aCaracter = "¤";  aCaracter = &241; |FINSI;
           |SI aCaracter = " ";  aCaracter = &225; |FINSI;
           |SI aCaracter = "‚";  aCaracter = &233; |FINSI;
           |SI aCaracter = "¡";  aCaracter = &237; |FINSI;
           |SI aCaracter = "¢";  aCaracter = &243; |FINSI;
           |SI aCaracter = "£";  aCaracter = &250; |FINSI;
           |SI aCaracter = "§";  aCaracter = &176; |FINSI;
           |SI aCaracter = "¦";  aCaracter = &170; |FINSI;
           aCadena = aCadena + aCaracter;
     |SIGUE;
     eaAlfa = aCadena;
|FPRC;
