|FICHEROS;
     rem99998;
|FIN;

|VARIABLES;
     n0Vent           = 0;
     nGrid            = 0;
     nRango           = 0;
     nPausa           = 0;
     nHandle          = 0;

     aEsc1            = "";
     aEsc2            = "";
     aRetu            = "";
     aTecla           = "";
     aPathInci        = "";
     aPath            = "";
     aFic             = "";
     aFicLCK          = "";
     aProg            = "";
     aAlfa            = "";

     {-1}aVent        = "";
         aVent1       = 0;
         aVent2       = 0;
         aVent3       = 0;
         aVent4       = 0;
         aVent5       = "";

     &eaUnidadBasico  = "";
     &eaOrigen        = "";
     &eaDestino       = "";
|FIN;

|PROCESO EditaIncidencias;
     |DBASE aPathInci;
     aPathInci = aPathInci + "incidencias/";

     |HAZ LeeUnidadBasico;
     aPath = eaUnidadBasico + "/MODELOS";                   |RMKDIR aPath;
     aPath = eaUnidadBasico + "/MODELOS/DOCUMENTOS";        |RMKDIR aPath;
     aPath = eaUnidadBasico + "/MODELOS/DOCUMENTOS/";

     aFic  = #rem99998#2;  |QBF aFic;
     aFic  = aFic + (#rem99998#0 % -104) + (#rem99998#0 % 402) + (#rem99998#0 % 102);
     aFic  = aFic + (#rem99998#1 % 102) + (#rem99998#1 % 402) + (#rem99998#1 % 702);
     aFic  = aFic + ".pdf";

     eaOrigen  = aPathInci + aFic;
     eaDestino = aPath + aFic;
     |HAZ CopiaFichero;

     |HAZ TraeDsAbreExtension;

     aFicLCK = aPath + Usuario + ".txt";
     aProg   = aPath + "dsabreextension.exe";
     |XABRE "EC", aProg, 0;
     |SI FSalida ] 0;
         aAlfa  = "START " + aProg + " " + aPath + aFic + " " + aFicLCK;
         |RSYSTEM aAlfa;

         |PARA;  |SI;  |HACIENDO;
                 |PARA nPausa = 1; |SI nPausa [ 10000; |HACIENDO nPausa = nPausa + 1;
                 |SIGUE;

                 |XABRE "EC", aFicLCK, nHandle;
                 |SI FSalida ] 0;
                     |SAL;
                 |FINSI;

                 |PULSATECLA;
         |SIGUE;
     |SINO;
         |MENAV "0000No se ha podido copiar el dsabreextension, pongase en contacto con su administrador.";
     |FINSI;

     |RFBORRA aFicLCK;

     aAlfa  = aPath + aFic;
     |RFBORRA aAlfa;

     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO Evt;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#rem99998.=;
     |FINSI;

     |SI FSalida = 4;
         |HAZ EditaIncidencias;
     |FINSI;
|FPRC;

|PROGRAMA;
     |VENTANA 0501, 3599, -1, 16, "Hist¢rico incidencias de la versi¢n";
     n0Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n0Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     nRango = 4 + 8 + 16 + 32 + 128;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Editar incidencias", 3402, 3420, EditaIncidencias;

     |LINEAL_SIMPLE #1#rem99998, nRango, 0502, 3398, NULL, NULL, Evt;
     nGrid  = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}Salir," + nGrid;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n0Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n0Vent;
     |PULSATECLA;
|FPRO;

|PROCESO T80;  |TIPO 80;
     |ABRE #rem99998;
|FPRC;

|PROCESO T90;  |TIPO 90;
     |CIERRA #rem99998;
|FPRC;
