|FICHEROS;
     reacceso;
     reparame;
     rem99999;

     rem00100;
     rem00101;
     rem00102;
     rem00021;
     rem00044;
     rem00900;
     rem01001;

     agifigen;
     agicontr;
     agiconpa;
|FIN;

|VARIABLES;
     &eaExtr           = "";

     &enEmpresa        = 0;
     &eaNombre         = "";
     &eaNif            = "";
     &enModo           = 0;
     &eaVarDni         = "";
     &enCalcCif        = 0;
     &enComplementaria = 0;
     &eaLetra          = "";
     &eaPrograma       = "";
     &eaExtension      = "";
     &eaPunto          = "";
     &enCodCli         = 0;
     &enSwDeDonde      = 0;
     &enError          = 0;
     &enDesdeRenta     = 0;
     &eaUnidadBasico   = "";
     &enTipoEntrada    = 0;
     &enVentGnrl       = 0;
     &enVentTrab       = 0;
     &eaCta            = "";
     &eaIBAN           = "";
     &eaContri         = "";

     aVariable = "";
     aAlfa     = "";
     aGrupo1   = "";
     aGrupo2   = "";
     aSiNo     = "";
     aTipo     = "";
     aPathRenta = "";
     aDef       = "";
     aAlfa1     = "";
     aAlfa2     = "";
     aUsuario1  = "";
     aUsuario2  = "";
     aBase      = "";
     aCadena    = "";
     aEsc1      = "";
     aEsc2      = "";
     aRetu      = "";
     aTecla     = "";
     aOrigen    = "";
     aDestino   = "";
     aIPRemoto  = "";
     aQueBoton  = "";
     aBoton     = "";
     aID        = "";
     aContenido = "";
     aLineaMenu = "";
     aCaracter  = "";
     aPrograma  = "";
     aTitulo    = "";
     aAbre      = "";
     aCap       = "";
     aFichero   = "";
     aAste      = "";
     aLongi     = "";
     aAgicli    = "";
     aLanza     = "";


     nNum       = 0;
     nHandle1   = 0;
     nHandle2   = 0;
     nPos       = 0;
     nLLave     = 0;
     nBoton     = 0;
     nSwGrupo1  = 0;
     nSwGrupo2  = 0;
     nOpcion    = 0;
     nEsArrend  = 0;
     nSwRecoge  = 0;
     nSwActiv   = 0;
     nDCodigo   = 0;
     nHCodigo   = 0;
     nCampo     = 0;
     nCampo1    = 0;
     nRegraba   = 0;
     nCompara   = 0;
     nPatrones  = 0;
     nContinuar = 0;
     nError     = 0;
     nHandle    = 0;
     nLong      = 0;
     nCaracter  = 0;
     nPosi      = 0;
     nPtec      = 0;
     nTree      = 0;
     nPanta900  = 0;
     nLinea     = 0;
     nLiquida   = 0;
     nHayArrend = 0;
     nVentana   = 0;

     nBoton1    = 0;
     nBoton2    = 0;
     nBoton3    = 0;
     nBoton4    = 0;
     nBoton5    = 0;
     nBoton6    = 0;
     nBoton7    = 0;
     nBoton8    = 0;
     nBoton9    = 0;
     nBoton10   = 0;
     nBoton11   = 0;
     nBoton12   = 0;
     nBoton13   = 0;
     nBoton14   = -1;

     {-1}sTree    = 0;
     sT_nID       = 0;
     sT_nOper     = 0;
     sT_aData     = "";

     &enEmprEos   = 0;
     &enActiEos   = 0;
     &enEmprRen   = 0;
     &enActiRen   = 0;
     &enLineaArre = 0;
     &eaTitCony   = "";

     &eaAzul       = "";
     &eaVerde      = "";
     &eaRojo       = "";
     &eaRosa       = "";
     &eaMarron     = "";
|FIN;

|INCLUYE i_sepnom;

|PROCESO MiraQue;
     enEmprEos  = #agiconpa#5;
     enActiEos  = #agiconpa#6;
     enEmprRen  = #reacceso#0;
     enActiRen  = #agiconpa#6;
     eaTitCony  = #agiconpa#11;

     |SI #agiconpa#2 = "R1";
         nEsArrend = 0;
         |SI #agiconpa#7 = "PLA";
             |SI nSwRecoge = 0; |SUB_EJECUTA "recappla";  |FINSI;
             nSwActiv = 1;
         |FINSI;

         |SI #agiconpa#7 = "RLI";
             |SI nSwRecoge = 0; |SUB_EJECUTA "recapreo";  |FINSI;
             nSwActiv = 1;
         |FINSI;

         |SI #agiconpa#7 = "13X";
             |SI nSwRecoge = 0; |SUB_EJECUTA "recapmod";  |FINSI;
             nSwActiv = 1;
         |FINSI;
     |FINSI;

     |SI #agiconpa#2 = "R2";
         nEsArrend   = 1;
         enLineaArre = #agiconpa#17;
         |SI nSwRecoge = 0; |SUB_EJECUTA "recapreo";  |FINSI;
         nSwActiv = 1;
     |FINSI;
|FPRC;

|DEFBUCLE agiconpa;
     #agiconpa#1;
     2;
     #reacceso#0, 00, aTipo;
     #reacceso#0, 99, aTipo;
     ;
     NULL, MiraQue;
|FIN;

|PROCESO RecogeReo;
     |SALVA_FICHA #reacceso;

     |DBASS aAgicli;

     |PATH_DAT #agicontr#aAgicli#;
     |ABRE #agicontr;
     |LEE 040#agicontr.p;
     |SI FSalida ! 0; |DDEFECTO #agicontr; |FINSI;
     |CIERRA #agicontr;

     aAgicli = #agicontr#3; |QBF aAgicli;
     |SI aAgicli = "*"; |ACABA; |FINSI;

     aAgicli = #agicontr#8; |QBF aAgicli;
     |PATH_DAT #agiconpa#aAgicli#;

     |CIERRAT #reacceso;
     |BUCLE agiconpa;
     |ABRET #reacceso;

     |REPON_FICHA #reacceso;
|FPRC;

|PROCESO CogeActiv;
     nSwActiv  = 0;
     aTipo     = "R1";
     nSwRecoge = 1;
     |HAZ RecogeReo;
     nSwRecoge = 0;
     |SI nSwActiv = 0;  |ACABA;  |FINSI;

     |VENTANA 0501, 3599, -1, 0, "";
     nVentana = FSalida;

     |PTEC 802;  |PAUSA;

     |CONTROL_SIMPLE 0, "LABEL,{{15}}DETECTADO ENLACE RENTA CON R.E.O.", 1322, 1398, NULL;
     |CONTROL_SIMPLE 0, "LABEL,{{16}}Desea hacer el pase en este momento", 1522, 1555, NULL;

     E_sup = 1;
     E_inf = "SNsn";
     aSiNo = "S";
     aSiNo = 1557 ? 1;
     aSiNo = aSiNo > "";

     |SI aSiNo = "S"; |HAZ RecogeReo; |FINSI;

     |FINVENTANA nVentana;
|FPRC;

|PROCESO CogeArrend;
     nSwActiv   = 0;
     aTipo      = "R2";
     nSwRecoge = 1;  |HAZ RecogeReo;
     nSwRecoge = 0;
     |SI nSwActiv = 0; |ACABA; |FINSI;

     |VENTANA 0501, 3599, -1, 0, "";
     nVentana = FSalida;

     |PTEC 802;  |PAUSA;

     |CONTROL_SIMPLE 0, "LABEL,{{15}}DETECTADO ENLACE RENTA CON R.E.O. ARRENDAMIENTOS", 1322, 1398, NULL;
     |CONTROL_SIMPLE 0, "LABEL,{{16}}Desea hacer el pase en este momento", 1522, 1555, NULL;

     E_sup = 1;
     E_inf = "SN";
     aSiNo  = "S";
     aSiNo  = 1561 ? 1;

     |SI aSiNo = "S"; |HAZ RecogeReo; |FINSI;

     |FINVENTANA nVentana;
|FPRC;

|PROCESO CogeReo;
     |HAZ CogeActiv;
     |HAZ CogeArrend;
|FPRC;

|PROCESO Separa;
     Cadena = #rem00100#5;
     |SI Cadena ! "";
         |HAZ SeparaNombre;
         #rem00100#2 = Cadena1;
         #rem00100#3 = Cadena2;
         #rem00100#4 = Cadena3;
     |FINSI;
|FPRC;

|| ----------------------------------------------------------------------------

|PROCESO Lectura;
     aAlfa = "";
     #rem99999#0  = #reacceso#0;
     #rem99999#2  = aVariable % 902;
     #rem99999#1  = aVariable % 108;
     |SI #rem99999#2 ! "  ";
         |SI #rem99999#1 ! "rem00058";  |Y  #rem99999#1 ! "rem00065";  |Y  #rem99999#1 ! "rem00056";
             #rem99999#1 = "rem00099";
         |FINSI;
     |FINSI;

     |LEE 000#rem99999.=;
     |SI FSalida = 0;
         aAlfa = " [*]";

         |SI #rem99999#1 = "rem00100";  #reacceso#3  = "*";  |FINSI;
         |SI #rem99999#1 = "rem00020";  #reacceso#4  = "*";  |FINSI;
         |SI aCap = "REN-B ";    #reacceso#5  = "*";  |FINSI;
         |SI #rem99999#1 = "rem00021";  #reacceso#6  = "*";  |FINSI;
         |SI #rem99999#1 = "rem01001";  #reacceso#12 = "*";  |FINSI;

         |SI aCap = "REN-E ";    #reacceso#7  = "*";  |FINSI;
         |SI aCap = "REN-F ";    #reacceso#8  = "*";  |FINSI;
         |SI aCap = "REN-I ";    #reacceso#8  = "*";  |FINSI;
         |SI aCap = "REN-G1";    #reacceso#9  = "*";  |FINSI;
         |SI aCap = "REN-G2";    #reacceso#9  = "*";  |FINSI;  || Falta G3 y G4
         |SI aCap = "REN-G4";    #reacceso#9  = "*";  |FINSI;  || Falta G3 y G4
         |SI aCap = "REN-N ";    #reacceso#10 = "*";  |FINSI;
         |SI aCap = "PAT-TO";    #reacceso#11 = "*";  |FINSI;
     |FINSI;
|FPRC;

|PROCESO PonAsterisco;
     |ABRE #rem99999;

     aCap      = aContenido % 103;
     aVariable = aContenido;
     aAlfa     = aVariable % 108;

     |SI aAlfa = "DATPER  ";  aVariable = "rem00100  ";  |FINSI;
     |SI aAlfa = "rezcalcu";  aVariable = "rem01001  ";  |FINSI;

     |SI aCap ! "REN";  |Y aCap ! "PAT";
         |HAZ Lectura;
         |ACABA;
     |FINSI;

     aCap  = aContenido % 106;

     |SI aCap = "REN-A ";
                           aVariable = "rem00020  ";  |HAZ Lectura;
     |FINSI;

     |SI aCap = "REN-B ";
                           aVariable = "rem00022  ";  |HAZ Lectura;
          |SI aAlfa = "";  aVariable = "rem00023  ";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00024  ";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00051  ";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00052  ";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00055  ";  |HAZ Lectura;  |FINSI;
     |FINSI;

     |SI aCap = "REN-E ";
                           aVariable = "rem00025  ";  |HAZ Lectura;
          |SI aAlfa = "";  aVariable = "rem00026  ";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00027  ";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00028  ";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00031  ";  |HAZ Lectura;  |FINSI;
     |FINSI;

     |SI aCap = "REN-F ";
                           aVariable = "rem000581 ";  |HAZ Lectura;
          |SI aAlfa = "";  aVariable = "rem000582 ";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem000583 ";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem000584 ";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00032  ";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00033  ";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00034  ";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00035  ";  |HAZ Lectura;  |FINSI;
     |FINSI;

     |SI aCap = "REN-G1";
                           aVariable = "rem00037  ";  |HAZ Lectura;
          |SI aAlfa = "";  aVariable = "rem00030  ";  |HAZ Lectura;  |FINSI;
     |FINSI;

     |SI aCap = "REN-G2";
                           aVariable = "rem00036  ";  |HAZ Lectura;
          |SI aAlfa = "";  aVariable = "rem00059  ";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00041  ";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00056G2";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00042  ";  |HAZ Lectura;  |FINSI;
     |FINSI;

     |SI aCap = "REN-I ";
                           aVariable = "rem00061  ";  |HAZ Lectura;
          |SI aAlfa = "";  aVariable = "rem00053  ";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00054  ";  |HAZ Lectura;  |FINSI;
     |FINSI;

     |SI aCap = "REN-N ";
                           aVariable = "rem00046  ";  |HAZ Lectura;
          |SI aAlfa = "";  aVariable = "rem00045  ";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00047  ";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00048  ";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00063  ";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00056  ";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00044  ";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00062  ";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem000651 ";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem000652 ";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem000653 ";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem000654 ";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem000655 ";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem000656 ";  |HAZ Lectura;  |FINSI;
     |FINSI;

     |SI aCap = "PAT-C ";
                           aVariable = "rem00099C1";  |HAZ Lectura;
          |SI aAlfa = "";  aVariable = "rem00099C2";  |HAZ Lectura;  |FINSI;
     |FINSI;

     |SI aCap = "PAT-F ";
                           aVariable = "rem00099F1";  |HAZ Lectura;
          |SI aAlfa = "";  aVariable = "rem00099F2";  |HAZ Lectura;  |FINSI;
     |FINSI;

     |SI aCap = "PAT-G ";
                           aVariable = "rem00099G1";  |HAZ Lectura;
          |SI aAlfa = "";  aVariable = "rem00099G2";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099G3";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099G4";  |HAZ Lectura;  |FINSI;
     |FINSI;

     |SI aCap = "PAT-H ";
                           aVariable = "rem00099H1";  |HAZ Lectura;
          |SI aAlfa = "";  aVariable = "rem00099H2";  |HAZ Lectura;  |FINSI;
     |FINSI;

     |SI aCap = "PAT-V ";
                           aVariable = "rem00099V1";  |HAZ Lectura;
          |SI aAlfa = "";  aVariable = "rem00099V2";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099V3";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099V4";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099V5";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099V6";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099V7";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099V8";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099V9";  |HAZ Lectura;  |FINSI;
     |FINSI;

     |SI aCap = "PAT-TO";
                           aVariable = "rem00099A1";  |HAZ Lectura;
          |SI aAlfa = "";  aVariable = "rem00099B1";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099C1";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099C2";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099D1";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099E1";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099F1";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099F2";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099G1";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099G2";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099G3";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099G4";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099H1";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099H2";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099I1";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099J1";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099K1";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099L1";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099M1";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099N1";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099O1";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099P1";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099Q1";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099U1";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099V1";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099V2";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099V3";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099V4";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099V5";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099V6";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099V7";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099V8";  |HAZ Lectura;  |FINSI;
          |SI aAlfa = "";  aVariable = "rem00099V9";  |HAZ Lectura;  |FINSI;
     |FINSI;

     |CIERRA #rem99999;
|FPRC;

|PROCESO rem00900;
     |SI #rem00900#0 = 5;  |Y #rem00900#1 = 6;
         |SI #reacceso#7 = " ";  |ACABA; |FINSI;
     |FINSI;

     aAlfa      = "";

     aContenido = #rem00900#4 + #rem00900#5;
     |HAZ PonAsterisco;

                       aAlfa2 = "{{" + aContenido + "}}";
                       aAlfa1 = #rem00900#0;
     |SI #rem00900#1 ! 0;  aAlfa1 = aAlfa1 + "," + #rem00900#1;  |FINSI;
     |SI #rem00900#2 ! 0;  aAlfa1 = aAlfa1 + "," + #rem00900#2;  |FINSI;

     aAlfa1 = aAlfa1 + ":" + #rem00900#3;  |QBF aAlfa1;

     aAlfa1 = aAlfa1 + " " + aAlfa + " " + aAlfa2 + &13 + &10;
     |XGRABA nHandle2, aAlfa1;
|FPRC;

|DEFBUCLE rem00900;
     #rem00900#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, rem00900;
|FIN;

|PROCESO TraeTxtComprimido;
     aAlfa = aPathRenta + aFichero + ".tar " + aFichero + ".txt";
     |ATAR aAlfa, aPathRenta;

     aAlfa = aPathRenta + aFichero + ".tar";
     |COMPRIME aAlfa;

     aAlfa = aPathRenta + aFichero + ".tar";  |FBORRA aAlfa;
     aAlfa = aPathRenta + aFichero + ".txt";  |FBORRA aAlfa;

     aOrigen  = aPathRenta + aFichero + ".tgz";
     aDestino = eaUnidadBasico + "\MODELOS\TMP\"  + aFichero + ".tgz";

     |RFBORRA aDestino;
     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |SI aIPRemoto ! "";
         |ENVIA_FICHERO aOrigen, aDestino;
     |SINO;
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;

     Pos    = -1;
     aAlfa  = eaUnidadBasico + "\MODELOS\TMP\" + aFichero + ".tgz";
     aAlfa1 = eaUnidadBasico + "\MODELOS\TMP\";
     |RDESCOMPRIME aAlfa;
     |RDETAR aAlfa, aAlfa1;

     aAlfa = aPathRenta + aFichero + ".tgz";         |FBORRA aAlfa;
     aAlfa = eaUnidadBasico + "\MODELOS\TMP\"  + aFichero + ".tgz";  |RFBORRA aAlfa;
     aAlfa = eaUnidadBasico + "\MODELOS\TMP\"  + aFichero + ".tar";  |RFBORRA aAlfa;
|FPRC;

|PROCESO CreaFicheros;
     aFichero = "001" + (("00000" + #reacceso#0) % -105);

     |DBASE aPathRenta;  |QBT aPathRenta;
     aPathRenta = aPathRenta + "tmp";  |MKDIR aPathRenta;
     aPathRenta = aPathRenta + "/";

     aOrigen    = aPathRenta + aFichero + ".txt";

     #reacceso#3  = "";
     #reacceso#4  = "";
     #reacceso#5  = "";
     #reacceso#6  = "";
     #reacceso#7  = "";
     #reacceso#8  = "";
     #reacceso#9  = "";
     #reacceso#10 = "";
     #reacceso#11 = "";
     #reacceso#12 = "";

     |XABRE "WB", aOrigen, nHandle2;
     |BUCLE rem00900;
     |XCIERRA nHandle2;

     |HAZ TraeTxtComprimido;

     aDestino = eaUnidadBasico + "\MODELOS";      |RMKDIR aDestino;
     aDestino = eaUnidadBasico + "\MODELOS\TMP";  |RMKDIR aDestino;
     aDestino = eaUnidadBasico + "\MODELOS\TMP\"  + aFichero + ".TXT";
|FPRC;

|PROCESO rem00101Cheq;
     |SI #rem00101#17 = "01.01.0000";  |Y #rem00101#18 = "01.01.0000";
         nContinuar = 1;
         |ERROR10;
     |FINSI;

     |SI #rem00101#21 ] "01.01.2000";
         |SI #rem00101#21 < "01.01.2017";
             nContinuar = 2;
             |ERROR10;
             |ACABA;
         |FINSI;

         |SI #rem00101#21 > "31.12.2017";
             nContinuar = 2;
             |ERROR10;
             |ACABA;
         |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE rem00101Cheq;
     #rem00101#1;
     ;
     #reacceso#0, 00;
     #reacceso#0, 99;
     ;
     NULL, rem00101Cheq;
|FIN;

|PROCESO rem00102Arrend;
     nHayArrend = 1;
     |ERROR10;
|FPRC;

|DEFBUCLE rem00102Arrend;
     #rem00102#1;
     9;
     #reacceso#0, 01, "3";
     #reacceso#0, 99, "3";
     ;
     NULL,  rem00102Arrend;
|FIN;

|PROCESO rem00021Cheq;
     |SI #rem00021#30 = 0;  |ACABA;  |FINSI;

     nError = 0;
     |ERROR10;
|FPRC;

|DEFBUCLE rem00021Cheq;
     #rem00021#1;
     14;
     #reacceso#0, 01, #rem00102#11;
     #reacceso#0, 99, #rem00102#11;
     ;
     NULL, rem00021Cheq;
|FIN;

|PROCESO rem00102Cheq;
     nError = 1;
     |BUCLE rem00021Cheq;

     |SI nError = 0;  |ACABA;  |FINSI;


     aAlfa = "LABEL,{{15}}No existe la Referencia Catastral: " + #rem00102#11;  |QBF aAlfa;
     aAlfa = aAlfa + " informada";
     |CONTROL_SIMPLE 0, aAlfa, 1215, 1298, NULL;

     |CONTROL_SIMPLE 0, "LABEL,{{15}}en los datos adicionales de la vivienda habitual en el inmobiliario.", 1315, 1398, NULL;
     |CONTROL_SIMPLE 0, "LABEL,{{15}}Por favor inf¢rmelo.", 1415, 1498, NULL;

     |LEETECLA  aAlfa;

     nContinuar = 1;
|FPRC;

|DEFBUCLE rem00102Cheq;
     #rem00102#1;
     9;
     #reacceso#0, 01, "1";
     #reacceso#0, 99, "2";
     ;
     NULL,  rem00102Cheq;
|FIN;

|PROCESO ChequeaLiquidacion;
     |ABRE #rem00100;
     #rem00100#0 = #reacceso#0;
     |LEE 040#rem00100.=;
     |SI FSalida ! 0;  |DDEFECTO #rem00100;  |FINSI;
     |CIERRA #rem00100;

     nContinuar = 0;

     |SI #rem00100#103 = "01.01.0000";
         |MENAV "0000 Fecha nacimiento contribuyente sin informar.";
         nContinuar = 1;
         |ACABA;
     |FINSI;

     |SI #rem00100#6 = "C";
         |SI #rem00100#104 = "01.01.0000";
             |MENAV "0000 Fecha nacimiento conyuge sin informar.";
             nContinuar = 1;
             |ACABA;
         |FINSI;
     |FINSI;

     |SI #rem00100#92 ] "01.01.2000";
         |SI #rem00100#92 < "01.01.2017";
             |MENAV "0000 Fecha fallecimiento err¢nea.";
             nContinuar = 1;
             |ACABA;
         |FINSI;

         |SI #rem00100#92 > "31.12.2017";
             |MENAV "0000 Fecha fallecimiento err¢nea.";
             nContinuar = 1;
             |ACABA;
         |FINSI;
     |FINSI;

     |BUCLE rem00101Cheq;

     |SI nContinuar = 1;
         |MENAV "0000 Fecha nacimiento de familiares sin informar.";
         |ACABA;
     |FINSI;

     |SI nContinuar = 2;
         |MENAV "0000 Fecha defunci¢n de familiar err¢nea.";
         |ACABA;
     |FINSI;

     |ABRE #rem00044;
     #rem00044#0 = #reacceso#0;
     #rem00044#1 = 0;
     |LEE 000#rem00044.];
     |SI FSalida ! 0;  |O #rem00044#0 ! #reacceso#0;
         |CONFI "0000SNo hay deducciones Autonomicas. Continuar.";
         |SI FSalida ! 0;
             |CIERRA #rem00044;
             nContinuar = 1;
             |ACABA;
         |SINO;
             |CONFI "0000SMostrar la pregunta anterior la pr¢xima vez que se liquide esta renta";
             |SI FSalida ! 0;
                 #rem00044#0 = #reacceso#0;
                 #rem00044#1 = 0;
                 |GRABA 020#rem00044.n;
                 |LIBERA #rem00044;
             |FINSI;
         |FINSI;
     |FINSI;
     |CIERRA #rem00044;

     |SI #rem00100#94 = "S";  |ACABA;  |FINSI;

     |ABRE #rem00102;
     #rem00102#0 = #reacceso#0;
     #rem00102#1 = 1;
     |LEE 000#rem00102.];
     |SI FSalida ! 0;  |O #rem00102#0 ! #reacceso#0;
         |CIERRA #rem00102;
         |MENAV "0000 No hay Datos Adicionales de la Vivienda, por favor introduzcalas.";
         nContinuar = 1;
         |ACABA;
     |FINSI;
     |CIERRA #rem00102;

     |BUCLE rem00102Cheq;
|FPRC;

|PROCESO LanzaPrograma;
     |PULSATECLA;

     aAlfa  = aPrograma % 108;
     aAlfa1 = aPrograma % 902;
     |SI aAlfa1 ! "  ";
         eaLetra = aAlfa1;
     |FINSI;

     |SI aAlfa ! "rezcalcu";
         |SUB_EJECUTA_NP aAlfa;

         |SI aAlfa = "rew00200";
             |ABRE #rem00100;
             #rem00100#0 = #reacceso#0;
             |LEE 040#rem00100.=;
             |SI FSalida ! 0;  |DDEFECTO #rem00100;  |FINSI;
             |CIERRA #rem00100;

             #reacceso#1 = #rem00100#5;

             aTitulo   = (("00000" + #reacceso#0) % -105) + " " + #reacceso#1;
         |FINSI;

         |HAZ CreaFicheros;
     |SINO;
         nContinuar = 0;
         |SI enModo ! 4;
             |VENTANA 0501, 3599, -1, 0, "";
             nVentana = FSalida;

             |PTEC 802;  |PAUSA;

             |HAZ ChequeaLiquidacion;

             |SI nContinuar = 0;  |SUB_EJECUTA_NP "rezcalcu";  |FINSI;

             |FINVENTANA nVentana;

             |PULSATECLA;
         |FINSI;

         |SI nContinuar = 0;
             |SUB_EJECUTA "rem01001";
             aPrograma = "";
         |FINSI;
     |FINSI;

     eaContri = "";
|FPRC;

|PROCESO CreaMenu;
     |HAZ CreaFicheros;

     aPrograma = "";
     aTitulo   = (("00000" + #reacceso#0) % -105) + " " + #reacceso#1;
     |PARA;  |SI;  |HACIENDO;
          aAbre = eaUnidadBasico + "\MODELOS\TMP\"  + "000" + (("00000" + #reacceso#0) % -105) + ".TXT";
          |XABRE "WCB", aAbre, nHandle;

          aAlfa = ((aDestino + (" " * 99)) % 198) + &13 + &10;
          |XGRABA nHandle, aAlfa;

          aAlfa = ((aTitulo + (" " * 99)) % 198) + &13 + &10;
          |XGRABA nHandle, aAlfa;

          aAlfa = ((aPrograma + (" " * 99)) % 198) + &13 + &10;
          |XGRABA nHandle, aAlfa;
          |XCIERRA nHandle;

          aAlfa    = "rez99999;" + aAbre;
          |SUB_EJECUTA_NP aAlfa;

          nLinea = 1;
          |XABRE "ABC",  aAbre,  nHandle;
          |PARA;  |SI;  |HACIENDO;
              |XLEE nHandle, 100, aAlfa;
              |SI FSalida [ 0;  |SAL;  |FINSI;

              |SI nLinea = 1;  aAbre     = aAlfa % 198;  |FINSI;
              |SI nLinea = 2;  aTitulo   = aAlfa % 198;  |FINSI;
              |SI nLinea = 3;  aPrograma = aAlfa % 198;  |FINSI;

              nLinea = nLinea + 1;
          |SIGUE;
          |XCIERRA nHandle;

          |QBF aAbre;
          |QBF aTitulo;
          |QBF aPrograma;

          |SI aPrograma = "";  |SAL;  |FINSI;

          |SI aPrograma ! "rem05000";
              |HAZ LanzaPrograma;
          |FINSI;

          |SI aPrograma = "rem05000";
              aAlfa     = "rem05000;" + (("00000" + #reacceso#0) % -105) + enModo;
              |SUB_EJECUTA_NP aAlfa;

              enCodCli  = #reacceso#0;
              |SUB_EJECUTA_NP "rezprogr";
              |HAZ CreaFicheros;
          |FINSI;

          |SI aPrograma = "";
              |HAZ CreaFicheros;
              |SAL;
          |FINSI;
     |SIGUE;

     aAbre = eaUnidadBasico + "\MODELOS\TMP\"  + "000" + (("00000" + #reacceso#0) % -105) + ".TXT";
     |RFBORRA aAbre;

     aAbre = eaUnidadBasico + "\MODELOS\TMP\"  + "001" + (("00000" + #reacceso#0) % -105) + ".TXT";
     |RFBORRA aAbre;
|FPRC;

|PROCESO Pantalla;
     enEmpresa        = #reacceso#0;
     eaNombre         = #reacceso#1;
     eaNif            = #reacceso#2;
     enComplementaria = 0;

     |ABRE #rem00100;
     #rem00100#0 = #reacceso#0;
     |LEE 040#rem00100.=;
     |SI FSalida ! 0;  |DDEFECTO #rem00100;  |FINSI;
     |CIERRA #rem00100;

     |SI #rem00100#10 = "S";
        eaExtr = "N";
     |SINO;
        eaExtr = "S";
     |FINSI;

     |SI #reparame#20 = "S";  #reacceso#16 = Usuario % 810;  |FINSI;

     |HAZ CreaMenu;
|FPRC;

|PROCESO MiraImpre;
     |SI #rem01001#5 = "   ";  |ACABA;  |FINSI;

     |SI #rem01001#5 ! "714";
         |SI #reacceso#14 = "INACTIVO  ";
             #reacceso#14 = "NO IMPRESO";
         |FINSI;

         |SI #rem01001#6 = "S";  |Y #reacceso#14 = "NO IMPRESO";
             #reacceso#14 = "IMPRESO";
         |FINSI;

         |SI #rem01001#6 = "N";  |Y #reacceso#14 = "IMPRESO   ";
              #reacceso#14 = "PARCIAL";
         |FINSI;
     |FINSI;

     |SI #rem01001#5 = "714";
         |SI #reacceso#15 = "INACTIVO  ";
             #reacceso#15 = "NO IMPRESO";
         |FINSI;

         |SI #rem01001#6 = "S";  |Y #reacceso#15 = "NO IMPRESO";
             #reacceso#15 = "IMPRESO";
         |FINSI;

         |SI #rem01001#6 = "N";  |Y #reacceso#15 = "IMPRESO   ";
             #reacceso#15 = "PARCIAL";
         |FINSI;
     |FINSI;

                                       #reacceso#20 = "";
     |SI #reacceso#12 = "*";           #reacceso#20 = eaAzul;   |FINSI;
     |SI #reacceso#14 = "IMPRESO   ";  #reacceso#20 = eaVerde;  |FINSI;
     |SI #reacceso#19 = "S";           #reacceso#20 = eaRosa;   |FINSI;
     |SI #reacceso#23 = "S";           #reacceso#20 = eaRojo;   |FINSI;
     |SI #reacceso#24 = "S";           #reacceso#20 = eaMarron; |FINSI;
|FPRC;

|DEFBUCLE rem01001;
     #rem01001#1;
     ;
     #reacceso#0, 00, "          ";
     #reacceso#0, 00, "zzzzzzzzzz";
     ;
     NULL, MiraImpre;
|FIN;

|PROCESO DatosPantalla;
     |ABRE #rem00100;
     #rem00100#0 = #reacceso#0;
     |LEE 040#rem00100.=;
     |SI FSalida ! 0;  |DDEFECTO #rem00100;  |FINSI;
     |CIERRA #rem00100;

     |SI #rem00100#10 = "S";
        eaExtr = "N";
     |SINO;
        eaExtr = "S";
     |FINSI;

     |SI #rem00100#56 = "X";  #reacceso#13 = "INDIVIDUAL";  |FINSI;
     |SI #rem00100#57 = "X";  #reacceso#13 = "CONJUNTA  ";  |FINSI;
     |SI #rem00100#58 = "X";  #reacceso#13 = "SEPARADA  ";  |FINSI;

     #reacceso#14 = "INACTIVO";
     #reacceso#15 = "INACTIVO";
     #reacceso#21  = #rem00100#35;
     #reacceso#22  = #rem00100#39;

     |BUCLE rem01001;
|FPRC;

|PROCESO Agicli;
     |DBASS aAlfa;
     |PATH_DAT #agicontr#aAlfa#;

     |ABRE #agicontr;
     #agicontr#0 = 1;
     |LEE 040#agicontr.=;
     |SI FSalida ! 0; aAste = "*"; |CIERRA #agicontr; |ACABA; |FINSI;
     |CIERRA #agicontr;

     aAste = #agicontr#6;
     |QBF aAste;
     |SI aAste  = "*"; |ACABA; |FINSI;

     aAlfa1 = #agicontr#8; |QBF aAlfa1;

     aLongi  = aAlfa1 % A0;
     nNum    = aLongi;
     nNum    = 100 + (nNum - 5);
     aLongi  = nNum;

     aAlfa2  = (aAlfa1 % aLongi) + "pan/";

     |PATH_DAT #agifigen#aAlfa1#;
     |PATH_PAN #agifigen#aAlfa2#;
|FPRC;

|PROCESO Modicli;
     |SI aAste  = "*"; |ACABA; |FINSI;

     |HAZ Agicli;

     |ABRE #agifigen;
     #agifigen#0 = #reacceso#0;
     |LEE 040#agifigen.=;
     |SI FSalida = 0;                || Recoge variables del agicli
         |SI #reacceso#2 ! #agifigen#13;
            |MENAV "     D.N.I. del Cliente es distinto al D.N.I. del Cliente General";
         |FINSI;
     |SINO;
         |MENAV "      No existe Cliente General";
     |FINSI;
     |CIERRA #agifigen;

     |ABRE #rem00100;
     #rem00100#0 = #reacceso#0;
     |LEE 040#rem00100.=;
     |SI FSalida ! 0;  |DDEFECTO #rem00100;  |FINSI;
     |CIERRA #rem00100;

     |SI #rem00100#90 = "S";
         |SI #rem00100#92 < "01.01.2017";
             |SI #rem00100#91 = "T";
                 |MENAV "0000El TITULAR esta fallecido antes del 01.01.2017 por favor revise los datos.";
             |SINO;
                 |MENAV "0000El CONYUGE esta fallecido antes del 01.01.2017 por favor revise los datos.";
             |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Modificar;
     #reacceso#0 = enEmpresa;
     |LEE 101#reacceso.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aUsuario1  = #reacceso#16;  |QBF aUsuario1;
     |SI #reparame#20 = "S";  |O aUsuario1 ! "";
         aUsuario2 = Usuario % 810;  |QBF aUsuario2;

         |SI aUsuario1 ! aUsuario2;  |Y aUsuario1 ! "";
             |MENAV "0000 No tiene permiso para entrar en esta Renta.";
             |LIBERA #reacceso;
             |ACABA;
         |FINSI;
     |FINSI;

     enEmpresa = #reacceso#0;
     enModo    = 2;

     |ABRE #rem99999;
     #rem99999#0 = #reacceso#0;
     #rem99999#1 = "rem01001";
     #rem99999#2 = "";
     |LEE 000#rem99999.=;
     nLiquida = FSalida;
     |CIERRA #rem99999;

     |SI nLiquida = 0;  |Y enModo ! 4;
         |CONFI "0000NRenta ya calculada, desea anular calculo ";
         |SI FSalida ! 0;
             |LIBERA #reacceso;
             |ACABA;
         |FINSI;

         |HAZ BorraLiquidacion;
         eaPrograma   = "rem01001";
         |HAZ ProgramaActivo;
     |FINSI;

     |HAZ Modicli;

     |HAZ CogeReo;

     enCodCli  = #reacceso#0;
     |SUB_EJECUTA_NP "rezprogr";

     |SI enModo = 1; |O enModo = 2;
         #reacceso#23 = "N";
         #reacceso#24 = "N";
         #reacceso#25 = "";
         #reacceso#26 = "01.01.0000";
         #reacceso#27 = "";

         |GRABA 020#reacceso.a;
         |LIBERA #reacceso;

         |LEE 101#reacceso.=;
         |HAZ Pantalla;
         |HAZ DatosPantalla;
     |FINSI;

                                       #reacceso#20 = "";
     |SI #reacceso#12 = "*";           #reacceso#20 = eaAzul;   |FINSI;
     |SI #reacceso#14 = "IMPRESO   ";  #reacceso#20 = eaVerde;  |FINSI;
     |SI #reacceso#19 = "S";           #reacceso#20 = eaRosa;   |FINSI;
     |SI #reacceso#23 = "S";           #reacceso#20 = eaRojo;   |FINSI;
     |SI #reacceso#24 = "S";           #reacceso#20 = eaMarron; |FINSI;

     |GRABA 020#reacceso.a;
     |LIBERA #reacceso;
|FPRC;

|PROCESO Consultar;
     enModo = 4;

     #reacceso#0 = enEmpresa;
     |LEE 000#reacceso.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |HAZ Pantalla;
|FPRC;

|PROCESO Baja;
     |CONFI "0000NEsta Seguro de dar de baja la Renta ";
     |SI FSalida ! 0; |ACABA;  |FINSI;

     #reacceso#0 = enEmpresa;
     |LEE 101#reacceso.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |HAZ BorraRenta;

     |BORRA 020#reacceso.a;
     |LIBERA #reacceso;
|FPRC;

|PROCESO CogeCliente;
     #rem00100#0  = #reacceso#0;
     |LEE 000#rem00100.=;
     |SI FSalida = 0; |ACABA;  |FINSI;

     |DDEFECTO #rem00100;
     #rem00100#0  = #reacceso#0;
     #rem00100#5  = #agifigen#2;  |HAZ Separa;
     #rem00100#1  = #agifigen#13;
     #rem00100#14 = #agifigen#3;
     #rem00100#15 = #agifigen#4;
     #rem00100#16 = #agifigen#5;
     #rem00100#17 = #agifigen#6;
     #rem00100#18 = #agifigen#7;
     #rem00100#19 = #agifigen#8;
     #rem00100#20 = #agifigen#9;
     #rem00100#21 = #agifigen#10;
     #rem00100#22 = #agifigen#11;
     #rem00100#23 =  (("00" + #rem00100#20) % -102) +  (("000" + #rem00100#21) % -103);
     #rem00100#12 = #agifigen#14;
     #rem00100#69 = #agifigen#63;
     #rem00100#70 = #agifigen#64;
     #rem00100#24 = #agifigen#51;
     #rem00100#25 = #agifigen#52;
     #rem00100#26 = #agifigen#53;
     #rem00100#27 = #agifigen#54;
     #rem00100#28 = #agifigen#55;
     #rem00100#29 = #agifigen#56;
     #rem00100#30 = #agifigen#57;
     #rem00100#31 = #agifigen#58;
     #rem00100#32 = #agifigen#59;
     #rem00100#33 =  (("00" + #rem00100#31) % -102) +  (("000" + #rem00100#32) % -103);

     eaCta = #rem00100#53;  |HAZ CalcuIBAN;  #rem00100#147 = eaIBAN;
     eaCta = #rem00100#54;  |HAZ CalcuIBAN;  #rem00100#148 = eaIBAN;
     eaCta = #rem00100#55;  |HAZ CalcuIBAN;  #rem00100#149 = eaIBAN;

     |GRABA 020#rem00100.n;
     |LIBERA #rem00100;
|FPRC;

|PROCESO Alta;
     |HAZ Agicli;

     |SI aAste  = "*"; |ACABA;      |FINSI;

     |ABRE #agifigen;
     |ABRE #rem00100;
     #agifigen#0 = #reacceso#0;
     |LEE 101#agifigen.=;
     |SI FSalida = 0;                || Recoge variables del agicli
         #agifigen#22 = "S";
         |GRABA 020#agifigen.a;                 || Graba el agicli
         |LIBERA #agifigen;

         |HAZ CogeCliente;

         #reacceso#1    = #agifigen#2;
         #reacceso#2    = #agifigen#13;
    |SINO;
         |CONFI "2400S Confirma el alta en Clientes General : ";
         |SI FSalida = 0;
             |CIERRA #agifigen;

             aLanza = "*:agiren17/zacceso9;" + (("00000" + #reacceso#0) % -105) + "ALTACLIENTE";
             |SUB_EJECUTA aLanza;

             |ABRE #agifigen;
             #agifigen#0 = #reacceso#0;
             |LEE 101#agifigen.=;
             |SI FSalida = 0;
                 #agifigen#22 = "S";
                 |GRABA 020#agifigen.a;                 || Graba el agicli
                 |LIBERA #agifigen;

                 #reacceso#1    = #agifigen#2;
                 #reacceso#2    = #agifigen#13;
             |SINO;
                 |DDEFECTO #agifigen;

                 #agifigen#0  = #reacceso#0;
                 #agifigen#2  = #reacceso#1;
                 #agifigen#13 = #reacceso#2;
             |FINSI;

             |HAZ CogeCliente;
        |SINO;
             #agifigen#0  = #reacceso#0;
             #agifigen#2  = #reacceso#1;
             #agifigen#13 = #reacceso#2;
             |HAZ CogeCliente;
        |FINSI;
     |FINSI;

     |CIERRA #rem00100;
     |CIERRA #agifigen;

     |GRABA 020#reacceso.a;
     |LIBERA #reacceso;
|FPRC;
