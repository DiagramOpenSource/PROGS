|FICHEROS;
 agim0051 #0;
 agifigen #1;

 agim0002 #2;
 agim0050 #50;
|FIN;

|VARIABLES;
 &enOperar    = 0;
 &enEjercicio = 0;
 &nDEjercicio = 0;
 &nHEjercicio = 0;
 &nDEmpresa   = 0;
 &nHEmpresa   = 0;
 &enEmpresa   = 0;
 nSwAlgun     = 0;
 nImporte     = 0;
 nPara1       = 0;
 &enTipSoc    = 0;
 &eaTipSoc    = "";
 nSwExi       = 0;

 fFecha       = @;
 aAlfa1       = "";
|FIN;

|| -------------------------------------

|PROCESO LeeTipSoc;
 nSwExi   = 0;
 enTipSoc = 0;
 eaTipSoc = "";
 |ABRE #0;
 #0#0 = enEmpresa;
 #0#1 = enEjercicio;
 |LEE 001#0=;
 |SI FSalida ! 0;
      nSwExi = 1;
 |SINO;
      enTipSoc = #0#2;
      eaTipSoc = #0#3;
 |FINSI;
 |CIERRA #0;
|FPRC;

|PROCESO LeeControl;
 |ABRE #50;
 #50#0 = enEjercicio;
 |LEE 001#50=;
 |SI FSalida ! 0;
     |DDEFECTO #50;
     #50#0 = enEjercicio;
     #50#4 = enEjercicio - 1;
     |GRABA 020#50n;
 |FINSI;
 |CIERRA #50;
|FPRC;

|PROCESO ElVolumen;
 nSwAlgun = 1;
 nImporte = nImporte + #2#13;
|FPRC;

|DEFBUCLE ElVolumen;
 #2#1;
 ;
 #1#0, 00, #50#4;
 #1#0, 99, #50#4;
 e;
 NULL, ElVolumen;
|FIN;

|PROCESO GrabaEmpresa;

  |PARA nPara1 = nDEjercicio; |SI nPara1 [ nHEjercicio; |HACIENDO nPara1 = nPara1 + 1;
        enTipSoc    = 0;
        eaTipSoc    = "";
        enEjercicio = nPara1;
        |HAZ LeeControl;

        nSwAlgun = 0;
        nImporte = 0;
        |BUCLE ElVolumen;
        |SI nSwAlgun = 1;
            |ABRE #0;
            #0#0 = #1#0;
            #0#1 = enEjercicio;
            |LEE 101#0=;
            |SI FSalida ! 0;
                |DDEFECTO #0;
                #0#0 = #1#0;
                #0#1 = enEjercicio;
                |GRABA 020#0b;
            |FINSI;

           |SI nImporte [ #50#1;
               #0#2 = 1;
           |SINO;
               |SI nImporte > #50#2; #0#2 = 2; |FINSI;
               |SI nImporte > #50#3; #0#2 = 3; |FINSI;
           |FINSI;
           |SI #0#2 = 1; #0#3 = "EMPRESA PYME";    |FINSI;
           |SI #0#2 = 2; #0#3 = "EMPRESA NO PYME"; |FINSI;
           |SI #0#2 = 3; #0#3 = "GRAN EMPRESA";    |FINSI;

           #0#4 = nImporte;
           #0#5 = #1#1;
           |GRABA 020#0a;
           |LIBERA #0;
           enTipSoc = #0#2;
           eaTipSoc = #0#3;
           |CIERRA #0;
        |FINSI;
   |SIGUE;

|FPRC;

|DEFBUCLE agifigen;
 #1#1;
 87;
 nDEmpresa, 02;
 nHEmpresa, 02;
 e;
 NULL, GrabaEmpresa;
|FIN;


|RUTINA min0;
 #0#0 = enEmpresa;
 #0#1 = 1900;
|FRUT;

|RUTINA max0;
 #0#0 = enEmpresa;
 #0#1 = 2999;
|FRUT;

|PROGRAMA;

     enTipSoc = 0;
     eaTipSoc = "";
     |SI nDEjercicio = 0; |Y nHEjercicio = 0;
         nDEjercicio = enEjercicio;
         nHEjercicio = enEjercicio;
     |FINSI;

     |SI enOperar = 10;   ||Calcula todas la empresas
         |BUCLE agifigen;
     |FINSI;

     |SI enOperar = 1;
         |HAZ LeeTipSoc;
         |SI nSwExi = 1;
             nDEmpresa = enEmpresa;
             nHEmpresa = enEmpresa;
             |BUCLE agifigen;
         |FINSI;
     |FINSI;

     |SI enOperar > 100;
         |SI enOperar < 104;
             nDEmpresa = enEmpresa; nHEmpresa = enEmpresa;
             fFecha = ~;
             aAlfa1 = fFecha % -104;
             nDEjercicio = 1997; nHEjercicio = aAlfa1;
             |BUCLE agifigen;
         |FINSI;

         |C_V #0#0, 1, "N";
         |C_V #0#1, 1, "S";
         |C_V #0#5, 1, "N";
         |C_L #0#1, 1, "S";

         |C_P #0#1, 1, 1318;
         |C_P #0#2, 1, 1326;
         |C_P #0#3, 1, 1328;
         |C_P #0#4, 1, 1348;

         |PINPA #1#0;
         |ENTLINEAL #1#0, -2, 4, 19, 0, min0, max0;
     |FINSI;

|FPRO;
