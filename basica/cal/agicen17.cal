|FICHEROS;
     :/basica/def/agicen17 #0;
     :/basica/def/agicen18 #18,MANTE;

     :/basica/def/agimunic #100;

     &regisnif@ #709;
     reflejo@   #900;
     agiw0018   #948,MANTE;

     :/contagen/def/caarm001 #111;
     :/contagen/def/canempre #330;

     &provinc@ #700;
      agiprovi #701;
     :/integral/def/dsam0100 #702;

     &poblaci@ #703;
      agipobla #704;
     :/integral/def/dsam0101 #705;
|FIN;

|VARIABLES;
     &enEmpresa   = 0;
     &enPeriodo   = 0;
     &enActividad = 0;
     &enLinInm    = 0;
     &eaCodNif    = "";
     &eaVarDni    = "";
     &enCalcCif   = 0;

     nSwSal  = 0;
     fFecha  = @;
     aAlfa   = "";
     aAlfa1  = "";
     aAlfa2  = "";
     nNume1  = 0;
     nAny    = 0;
     &EURODB = 0;

     aBase   = "";
     aMas    = "";
     nCampo1 = 0;
     nCampo2 = 0;
     nCampo3 = 0;
     nPara1  = 0;

     &eaPathContagen = "";
     &eaPathIntegral = "";
     &enDesContrato  = 0;
     nReserva       = 0;
     &enContrato    = 0;
     &eaActContra   = "";
     &eaSubContra   = "";
     &enDesBasica   = 0;
     &enDesModelo   = 0;

     nBoton3         = 0;
     nSwBoton3       = 0;
     nTecla          = 0;
     esalta          = 0;
     aEmpresaPeriodo = "";
|FIN;

|PROCESO VeoContrato;
    enContrato = #111#1;
|FPRC;

|DEFBUCLE VeoContrato;
    #111#1;
    11, 12;
    enEmpresa, 00001, eaActContra, eaSubContra;
    enEmpresa, 99999, eaActContra, eaSubContra;
    e;
    NULL, VeoContrato;
|FIN;

|PROCESO ElUltim;
    |SI enPeriodo =  -1;
        enPeriodo = #330#3;
        nAny      = #330#26 + 2000;
    |FINSI;
    nNume1 = #330#26 + 2000;
    |SI nNume1 > nAny;
        enPeriodo = #330#3;
        nAny  = #330#26 + 2000;
    |FINSI;
|FPRC;

|DEFBUCLE ElUltim;
    #330#1;
    ;
    enEmpresa, 0;
    enEmpresa, 9;
    ;
    NULL, ElUltim;
|FIN;

|PROCESO VerContratos;
     enPeriodo = -1;
     |BUCLE ElUltim;
     |SI enPeriodo = -1;
         |MENAV "0000No existe Empresa en Contabilidad";
         |ACABA;
     |FINSI;
     aEmpresaPeriodo = ("00000" + enEmpresa) % -105 + ("0" + enPeriodo) % -101;

     enContrato  = 0;
     eaActContra = ("00"  + #0#1) % -102;
     eaSubContra = "   ";
     |BUCLE VeoContrato;
     nReserva    = enContrato;

     eaSubContra = ("000" + #0#2) % -103;
     enContrato  = 0;
     |BUCLE VeoContrato;
     |SI enContrato = 0;
         |MENAV "0000No existe Contrato";
         |SI enDesModelo = 1; |ACABA; |FINSI;

         |SI nReserva ! 0;
             aAlfa1 =  "0000NDar de Alta un Nuevo Contrato (Hay algun contrato sin referencia informada)";
         |SINO;
             aAlfa1 =  "0000SDar de Alta un Nuevo Contrato";
         |FINSI;

         |CONFI aAlfa1;
         |SI FSalida ! 0;
             |SI nReserva = 0;
                 |ACABA;
             |SINO;
                 |CONFI "0000SDesea asignarlo a un Contrato ya existente";
                 |SI FSalida ! 0;
                     |ACABA;
                 |FINSI;
                 enContrato = nReserva;
             |FINSI;
         |FINSI;
     |FINSI;

     aAlfa =":contagen/caarz001 canempr1 " + aEmpresaPeriodo;
     enDesBasica = 1;
     |SUB_EJECUTA_NP aAlfa;

     |PULSATECLA;
|FPRC;

|PROCESO FinBoton;
     |SI eaPathContagen ! "";
         |FIN_CONTROL_WINDOWS nBoton3;
     |FINSI;
|FPRC;

|PROCESO MiraLocalidad;
     |SI eaPathIntegral ! "";
         |REFERENCIA_DEF provinc@,  #702;
         |REFERENCIA_DEF poblaci@,  #705;
     |SINO;
         |REFERENCIA_DEF poblaci@,  #704;
         |REFERENCIA_DEF provinc@,  #701;
     |FINSI;

     |ABRE #703;
     #703#0 = #agicen17#6;
     #703#1 = #agicen17#7;
     |LEE 040#703=;
     |SI FSalida = 0;
        |SI eaPathIntegral ! "";
            #agicen17#8 = #703#3;
        |SINO;
            #agicen17#8 = #703#2;
        |FINSI;
         |PINTA #agicen17#8;
     |SINO;
         |MENAV "    No existe Poblacion.";
         |ERROR;
     |FINSI;
     |CIERRA #703;

     |ABRE #700;
     #700#0 = #agicen17#6;
     |LEE 040#700=;
     |SI FSalida = 0;
         #agicen17#9 = #700#1;
         |PINTA #agicen17#9;
     |SINO;
         |MENAV "    No existe Provincia";
         |ERROR;
     |FINSI;
     |CIERRA #700;
|FPRC;

|PROCESO elTipo11; |TIPO 11;

    nTecla = FSalida;
    |HAZ FinBoton;

    |SI nTecla = 11; |Y nSwBoton3 = 1;
        |HAZ VerContratos;
        |ERROR;
    |FINSI;
|FPRC;

|PROCESO elTipo0; |TIPO 0;
    |HAZ FinBoton;
|FPRC;

|PROCESO Catastral;

     |SI FSalida = 10;
         |HAZ Copiar;
         |ACABA;
     |FINSI;

     aAlfa = #0#3;  |QBF aAlfa;
     |SI aAlfa = "";  |ACABA;  |FINSI;

     aAlfa = aAlfa % 0;
     |SI aAlfa ! "20";
         |MENAV "0000 La longitud de la Referencia Catastral deberia ser de 20 Caracteres.";
     |FINSI;
|FPRC;

|PROCESO LetraDnicen17;  |TIPO 6;
     |SI FSalida = 10;
         eaVarDni  = #0Campo;
         enCalcCif = 1;
         |HAZ CalculoDNI;
         #0Campo = eaVarDni;
         |PINTA #0Campo;
         |ERROR;
         |ACABA;
     |FINSI;

     eaVarDni  = #0Campo;
     enCalcCif = 0;
     |HAZ CalculoDNI;
     eaVarDni = (eaVarDni + "            ") % 112;
     |SI #0Campo ! eaVarDni;
         |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
         |SI FSalida = 0;
             #0Campo = eaVarDni;  |PINTA #0Campo;
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO NombreNif;
     eaCodNif = #0#4;

     |HAZ LeeNifs;

     #0#5 = #709#1;  |PINTA #0#5;
|FPRC;

|RUTINA ClaveBaja18;
 #18#0 = #0#0;
 #18#1 = #0#1;
 #18#2 = #0#2;
 |SI FSalida = "POSICION";
     fFecha = ~;
     aAlfa1 = fFecha;
     aAlfa1 = aAlfa1 % -104;
     nNume1 = aAlfa1;
     nNume1 = nNume1 - 1;
     #18#3 = nNume1;
 |SINO;
     #18#3 = 1900;
 |FINSI;
|FRUT;

|RUTINA ClaveAlta18;
 #18#0 = #0#0;
 #18#1 = #0#1;
 #18#2 = #0#2;
 #18#3 = 2099;
|FRUT;

|PROCESO elTipo3; |TIPO 3;
    nSwSal = 0;
    |ENTLINEAL #1#18, -4, 2, 22, 0, ClaveBaja18, ClaveAlta18;

    enLinInm = #0#2;
|FPRC;

|PROCESO PresLinea18;  |TIPO 7;

    nSwBoton3 = 0;
    |SI eaPathContagen ! "";
        |CONTROL_SIMPLE 0, "LBOTON, F3 Contrato Arrendamiento", 0766, 0879, Boton3n17;
        nBoton3 = FSalida;

        |ESTADO_CONTROL nBoton3,  "DISABLE";  nSwBoton3  = 0;

        esalta = (FEntrada / 100) ! 0;

        |SI enDesContrato = 0; |Y  esalta > 1;
            |ESTADO_CONTROL nBoton3, "ENABLE";  nSwBoton3 = 1;
        |FINSI;
    |FINSI;

    nSwSal = 1;
    |ENTLINEAL #1#18, -4, 7, 22, 0, ClaveBaja18, ClaveAlta18;
|FPRC;

|PROCESO SalYa18;  |TIPO 7;
     |SI nSwSal = 1;  |PTEC 807;  |FINSI;
|FPRC;

|PROCESO Tipo0C5; |TIPO 0;
     |SI #18#5 > #18#6;
         |CONFI "0000NValor del IBI superior al valor del terreno. Continuar.";
         |SI FSalida ! 0;
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo0C6; |TIPO 0;
     |SI FSalida = 10;
         #18#6 = (#18#4 % 20) ! EURODB;
         |PINTA #18#6;
         |ACABA;
     |FINSI;

     |SI #18#6 > #18#4;
         |CONFI "0000NValor del terreno superior al valor catastral. Continuar.";
         |SI FSalida ! 0;
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Cal20pc; |TIPO 0;
     |SI FSalida = 10;
         #18#6 = (#18#4 % 20) ! EURODB;
         |PINTA #18#6;
         |ACABA;
     |FINSI;

     |SI #18#6 > #18#4;
         |CONFI "0000 Valor del terreno superior al valor catastral. Continuar.";
         |SI FSalida ! 0;
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Infmunic;
    #100#0 = "00000";
    #100#1 = "00000";
|FPRC;

|PROCESO Supmunic;
    #100#0 = "99999";
    #100#1 = "99999";
|FPRC;

|PROCESO Tipo66C18; |TIPO 66;
    |ABRE #100;
    aAlfa1 = #0#6; aAlfa1 = ( "00"  + aAlfa1) % -102;
    aAlfa2 = #0#7; aAlfa2 = ( "000" + aAlfa2) % -103;
    aAlfa1 = aAlfa1 + aAlfa2;
    #100#0 = aAlfa1;
    #100#1 = #0#18;

    |CONSULTA_F_CLAVE #1#100, Infmunic, Supmunic;

    aAlfa1 = #100#0; |QBF aAlfa1;
    aAlfa1 = ("00000" + aAlfa1) % -105;
    aAlfa2 = aAlfa1 % 102; #0#6  = aAlfa2; |PINTA #0#6;
    aAlfa2 = aAlfa1 % 303; #0#7  = aAlfa2; |PINTA #0#7;

    #0#18 = #100#1; |PINTA #0#18;
    |CIERRA #100;
|FPRC;

|| **********************************************************
|PROCESO Tipo12w0018; |TIPO 12;
  || Para que no pregunte
|FPRC;

|PROCESO Inf900;
  #900#0 = #948#0;
  #900#1 = 01;
  #900#2 = 01;
|FPRC;

|PROCESO Sup900;
  #900#0 = #948#0;
  #900#1 = 99;
  #900#2 = 99;
|FPRC;

|PROCESO LeeReferencia;
    |SI FSalida = 10;
        |ABRE #900;
        #900#0 = #948#0;
        #900#1 = #948#1;
        #900#2 = #948#2;
        |CONSULTA_F_CLAVE #1#900, Inf900, Sup900;
        #948#1 = #900#1;
        #948#2 = #900#2; |PINTA #948#2;
        |CIERRA #900;
    |FINSI;

    |SI #948#0 = #0#0; |Y #948#1 = #0#1; |Y #948#2 = #0#2;
        |MENAV "    Error. Esta es la Referencia Actual";
        |ERROR;
        |ACABA;
    |FINSI;

    |ABRE #900;
    #900#0 = #948#0;
    #900#1 = #948#1;
    #900#2 = #948#2;
    |LEE 041#900=;
    |SI FSalida ! 0;
        |MENAV "    Error. No Existe Referencia";
        |ERROR;
    |SINO;
        #948#3 = #900#3; |PINTA #948#3;
    |FINSI;
    |CIERRA #900;
|FPRC;

|PROCESO BorroEstos;
    |BORRA 020#18a;
    |LIBERA #18;
|FPRC;

|DEFBUCLE BorroEstos;
    #18#1;
    ;
    #0#0, #0#1, #0#2, 1900;
    #0#0, #0#1, #0#2, 2999;
    be;
    NULL, BorroEstos;
|FIN;

|PROCESO CopioNuevos;
    #18#0 = #0#0;
    #18#1 = #0#1;
    #18#2 = #0#2;
    |GRABA 020#18n;
|FPRC;

|DEFBUCLE CopioNuevos;
    #18#1;
    ;
    nCampo1, nCampo2, nCampo3, 1900;
    nCampo1, nCampo2, nCampo3, 2999;
    e;
    NULL, CopioNuevos;
|FIN;

|PROCESO CopioCampos;
    |PARA nPara1 = 3; |SI nPara1 [ 18; |HACIENDO nPara1 = nPara1 + 1;
          #0nPara1 = #900nPara1; |PINTA #0nPara1;
    |SIGUE;

    |BUCLE BorroEstos;
    nCampo1 = #900#0;
    nCampo2 = #900#1;
    nCampo3 = #900#2;
    |BUCLE CopioNuevos;
|FPRC;

|PROCESO Copiar; ||*********** Proceso para duplicar Referencia

 |DBASE aBase; |QBF aBase;
 aMas = aBase + "def/agicen17";
 |CARGA_DEF aMas, reflejo@;
 |SI FSalida ! 0;
     |MENAV "    Error al cargar Referencias";
     |ACABA;
 |FINSI;

 |PUSHV 0915 1459;
 |PINPA #0#948;

 |DDEFECTO #948;
 #948#0 = #0#0;
 #948#1 = #0#1;
 |PINDA #0#948;
 |ENDATOS #1#948;
 |POPV;
 |SI #948#4 = "SI";
     |ABRE #900;
     #900#0 = #948#0;
     #900#1 = #948#1;
     #900#2 = #948#2;
     |LEE 041#900=;
     |SI FSalida = 0;
         |HAZ CopioCampos;
     |FINSI;
     |CIERRA #900;
 |FINSI;
 |DESCARGA_DEF #900;
 |PONCONTROL  23, "SI";
|FPRC;
|| ......................

|RUTINA ClaveBaja0;
     #0#0 = enEmpresa;
     #0#1 = enActividad;
     #0#2 = 01;
     |SI FSalida = "POSICION"; |Y enLinInm ! 0;
         #0#2 = enLinInm;
     |FINSI;
|FRUT;

|RUTINA ClaveAlta0;
     #0#0 = enEmpresa;
     #0#1 = enActividad;
     #0#2 = 99;
|FRUT;

|PROCESO Boton3n17;
     |PTEC 825;
|FPRC;

|PROGRAMA;
     |HAZ BusquedaApli;
     |ENTLINEAL #1#0, -3, 2, 22, 1, ClaveBaja0, ClaveAlta0;
|FPRO;
