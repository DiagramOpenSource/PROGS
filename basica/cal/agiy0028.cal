|FICHEROS;
     may00001 #0;
     agim0033 #1;
     agim0034 #2;
     agifigen #3;
     agicen14 #14;
|FIN;

|VARIABLES;
     Fecsys    = @;
     VAlfa     = "";
     VNume     = 0;
     Contador  = 0;
     DEmpresa  = 0;
     HEmpresa  = 0;
     Ultima    = 0;

     informe   = "";
     &Dia      = "";
     &Mes      = "";
     &Ejer     = "";

     &enEmpresa = 0;
     &enAnyo    = 0;
     &enDRef    = 0;
     &enHRef    = 0;

     aParam     = "";
|FIN;

|PROCESO Seleccion; |TIPO 0;
  |SI #0#1 = "N";
      |C_M #0#2, 1, "S";
      |C_M #0#3, 1, "S";

      |PARA Contador = 4; |SI Contador [ 23; |HACIENDO Contador = Contador + 1;
            |C_M #0Contador, 1, "N";
            #0Contador = 0; |PINTA #0Contador;
      |SIGUE;
  |SINO;
      |C_M #0#2, 1, "N";  #0#2 = 1;      |PINTA #0#2;
      |C_M #0#3, 1, "N";  #0#3 = 99999;  |PINTA #0#3;

      |PARA Contador = 4; |SI Contador [ 23; |HACIENDO Contador = Contador + 1;
            |C_M #0Contador, 1, "S";
      |SIGUE;
  |FINSI;
|FPRC;

|PROCESO Mayor; |TIPO 0;
  #0Campo = #0Campo > #0Campo;
|FPRC;

|PROCESO Defectos; |TIPO 7;
  Fecsys  = ~;
  VAlfa   = Fecsys;
  VAlfa   = VAlfa % -104;
  VNume   = VAlfa;
  #0#0    = VNume - 1;   |PINTA #0#0;
|FPRC;

|PROCESO agim0034;
  |PRINT;
  |SI aParam = ""; |PIEINF; |FINSI;
|FPRC;


|DEFBUCLE Linagim0034;
  #2#1;
  ;
  #1#0, #1#1, enDRef;
  #1#0, #1#1, enHRef;
  e;
  NULL, agim0034;
|FIN;

|PROCESO Lineas;

  #3#0 = #1#0;
  |LEE 040#3=;
  |SI FSalida ! 0; |DDEFECTO #3;  |FINSI;

  || Lee Primera Actividad
  #14#0 = #1#0;
  #14#1 = 00;
  |LEE 041#14];
  |SI FSalida ! 0; |O #14#0 ! #1#0;
      |DDEFECTO #14;
  |FINSI;

  #0#27 = "ACTIVA";
  |SI #3#94 > "01.01.1900";  #0#27 = "INACTIVA";  |FINSI;

  |BUCLE Linagim0034;
  |SI aParam = "I"; |PIEINF; |FINSI;
|FPRC;

|DEFBUCLE agim0033;
  #1#1;
  ;
  DEmpresa, #0#0;
  HEmpresa, #0#0;
  ;
  NULL, Lineas;
|FIN;

|PROCESO Tipo3; |TIPO 3;
  |HAZ Fecha;

  |IMPRE 0;
  |SI FSalida ! 0; |ACABA; |FINSI;

  informe = "agiy0028";
  |SI aParam = "I"; informe = "agiy1028"; |FINSI;

  |INFOR informe;
  |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

  |SI enEmpresa = 0; |O aParam = "I";
      enDRef = 001;
      enHRef = 999;
  |FINSI;

  |ABRE #3;
  |ABRE #14;
  |SI #0#1 = "N";
      DEmpresa = #0#2;
      HEmpresa = #0#3;
      |BUCLE agim0033;
  |SINO;
      |PARA Contador = 4; |SI Contador [ 23; |HACIENDO Contador = Contador + 1;
            |SI #0Contador ! 0;
                DEmpresa = #0Contador;
                HEmpresa = #0Contador;
                |BUCLE agim0033;
            |FINSI;
      |SIGUE;
  |FINSI;

  |CIERRA #14;
  |CIERRA #3;
  |FININF;
  |FINIMP;
|FPRC;

|PROCESO Fecha;
  Dia  = #0#24 % 102;
  Mes  = #0#24 % 402;
  Ejer = #0#24 % 704;

  |SI Mes = "01"; Mes = "Enero     "; |FINSI;
  |SI Mes = "02"; Mes = "Febrero   "; |FINSI;
  |SI Mes = "03"; Mes = "Marzo     "; |FINSI;
  |SI Mes = "04"; Mes = "Abril     "; |FINSI;
  |SI Mes = "05"; Mes = "Mayo      "; |FINSI;
  |SI Mes = "06"; Mes = "Junio     "; |FINSI;
  |SI Mes = "07"; Mes = "Julio     "; |FINSI;
  |SI Mes = "08"; Mes = "Agosto    "; |FINSI;
  |SI Mes = "09"; Mes = "Septiembre"; |FINSI;
  |SI Mes = "10"; Mes = "Octubre   "; |FINSI;
  |SI Mes = "11"; Mes = "Noviembre "; |FINSI;
  |SI Mes = "12"; Mes = "Diciembre "; |FINSI;

  VAlfa = #0#0; VAlfa = "01.01." + VAlfa; #0#25 = VAlfa;
  VAlfa = #0#0; VAlfa = "31.12." + VAlfa; #0#26 = VAlfa;
|FPRC;

|PROGRAMA;
     aParam = PARAMETRO; |QBF aParam;
     |SI enEmpresa = 0;
         |CLS;
         |SI aParam = "":
             |CABEZA "Emision Certificados Socios";
         |SINO;
             |CABEZA "Inf.Ficha Cert. Socios";
         |FINSI;
         |CLS;
         |PINPA #0#0;
         |SI aParam = "I";
             |ATRI R; |PINTA 0709, "Informe Fichas de Certificado de Socios"; |ATRI 0;
             |PINTA 0944, "Fecha Informe ";
         |FINSI;
         |PINDA #0#0;
         |ENDATOS #1#0;
     |SINO;
         |DDEFECTO #0;
         #0#0 = enAnyo;
         #0#1 = "N";
         #0#2 = enEmpresa;
         #0#3 = enEmpresa;
         |HAZ Tipo3;
     |FINSI;
|FPRO;
