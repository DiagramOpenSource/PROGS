|FICHEROS;
     dspase00 #0;
     dspase89 #89;
     dspase90 #90,MANTE;
     refer00@ #900;
     refer01@ #901;
|FIN;

|VARIABLES;
     aPathDefFiscal  = "";
     aPathFichFiscal = "";
     aPathAnterior   = "";
     aDef            = "";
     aAlfa           = "";
     aBase           = "";
     aPathPases      = "";
     aOrigen         = "";
     aDestino        = "";
     &eaCta          = "";
|FIN;

|PROCESO MCta_A; |TIPO 0;
     eaCta = #89Campo;
     |SI Campo = 3;
         |QBF eaCta; aAlfa = eaCta % -101;
         |SI aAlfa = "*"; |ACABA; |FINSI;
     |FINSI;
     |HAZ MFiltraPtos;
     #89Campo = eaCta; |PINTA #89Campo;
|FPRC;

|PROCESO MCta_M; |TIPO 0;
     eaCta = #90Campo;
     |SI Campo = 4;
         |QBF eaCta; aAlfa = eaCta % -101;
         |SI aAlfa = "*"; |ACABA; |FINSI;
     |FINSI;
     |HAZ MFiltraPtos;
     #90Campo = eaCta; |PINTA #90Campo;
|FPRC;

|PROCESO PonGrupo90;
     aAlfa = #90#3 % 103;
     #90#5 = aAlfa;  |PINTA #90#5;
     #90#6 = #90#2;  |PINTA #90#6;
|FPRC;

|PROCESO Tipo1190;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     |LEE 101#90=;
     |SI FSalida = 0;
         |ENDATOS #1#90;
         |SI FSalida = 0;
             |GRABA 020#90a;
         |FINSI;
     |FINSI;
     |LIBERA #90;
|FPRC;

|PROCESO PonGrupo89;
     aAlfa = #89#2 % 103;
     #89#4 = aAlfa;  |PINTA #89#4;
     #89#5 = #89#1;  |PINTA #89#5;
|FPRC;

|PROCESO Tipo1189;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     |LEE 101#89=;
     |SI FSalida = 0;
         |ENDATOS #1#89;
         |SI FSalida = 0;
             |GRABA 020#89a;
         |FINSI;
     |FINSI;
     |LIBERA #89;
|FPRC;

|PROCESO PonConcepto;
     |SI #900#17 = "      ";  |ACABA;  |FINSI;

     #89#0 = #900#17;
     |LEE 101#89=;
     |SI FSalida ! 0;
         |DDEFECTO #89;
         #89#0 = #900#17;
         #89#1 = #900#15;
         |GRABA 020#89n;
     |FINSI;
     |LIBERA #89;
|FPRC;

|DEFBUCLE eosamorc;
     #900#1003;
     ;
     00001, 00, 00000;
     99999, 99, 99999;
     ;
     NULL, PonConcepto;
|FIN;

|PROCESO PonConceptoM;
     |SI #900#11 = "      ";  |ACABA;  |FINSI;

     #901#0 = #900#11;
     |LEE 040#901=;
     |SI FSalida ! 0;  |DDEFECTO #901;  |FINSI;

     #90#0 = #900#4;
     #90#1 = #900#11;
     |LEE 101#90=;
     |SI FSalida ! 0;
         |DDEFECTO #90;
         #90#0 = #900#4;
         #90#1 = #900#11;
         #90#2 = #901#1;
         |SI #90#0 = 0;  #90#10 = "INMOVILIZADO MATERIAL NUEVO             ";  |FINSI;
         |SI #90#0 = 1;  #90#10 = "EDIFICIOS Y OTRAS CONSTRUCCIONES        ";  |FINSI;
         |SI #90#0 = 2;  #90#10 = "UTILES, HERRAMIENTAS Y EQUIPOS          ";  |FINSI;
         |SI #90#0 = 3;  #90#10 = "ELEMENTOS DE TRANSPORTES Y RESTO        ";  |FINSI;
         |SI #90#0 = 4;  #90#10 = "INMOVILIZADO INMATERIAL                 ";  |FINSI;
         |SI #90#0 = 5;  #90#10 = "VACUNO, PORCINO, OVINO Y CAPRINO        ";  |FINSI;
         |SI #90#0 = 6;  #90#10 = "EQUINO Y FRUTALES NO CITRICOS           ";  |FINSI;
         |SI #90#0 = 7;  #90#10 = "FRUTALES CITRICOS Y VI¥EDOS             ";  |FINSI;
         |SI #90#0 = 8;  #90#10 = "OLIVAR                                  ";  |FINSI;
         |GRABA 020#90n;
     |FINSI;
     |LIBERA #90;
|FPRC;

|DEFBUCLE eosm0023;
     #900#1003;
     ;
     00001, 1, 00000;
     99999, 6, 99999;
     ;
     NULL, PonConceptoM;
|FIN;

|PROCESO TraeConceptos;
     aPathAnterior   = #0#0;  |QBT aPathAnterior;

     |HAZ BusquedaApli;
     aPathDefFiscal  = aPathAnterior + "agieos/def/";
     aPathFichFiscal = aPathAnterior + "agieos/fich/";

     aDef = aPathDefFiscal + "eosamorc.mas";
     |CARGA_DEF aDef, refer00@;
     |SI FSalida ! 0;
         |MENAV "     Error en Cargar el Def eosamorc.mas";
         |ACABA;
     |FINSI;

     |PATH_DAT #900 aPathFichFiscal;

     |BUCLE eosamorc;
     |DESCARGA_DEF #refer00@;

     aDef = aPathDefFiscal + "eosm0023.mas";
     |CARGA_DEF aDef, refer00@;
     |SI FSalida ! 0;
         |MENAV "     Error en Cargar el Def eosm0023.mas";
         |ACABA;
     |FINSI;

     aDef = aPathDefFiscal + "eosconc6.mas";
     |CARGA_DEF aDef, refer01@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #refer00@;
         |MENAV "     Error en Cargar el Def eosconc6.mas";
         |ACABA;
     |FINSI;

     |PATH_DAT #900 aPathFichFiscal;
     |PATH_DAT #901 aPathFichFiscal;

     |ABRE #901;
     |BUCLE eosm0023;
     |CIERRA #901;

     |DESCARGA_DEF #refer00@;
     |DESCARGA_DEF #refer01@;
|FPRC;

|RUTINA ClaveBaja89;
     #89#0 = 00;
|FRUT;

|RUTINA ClaveAlta89;
     #89#0 = 99;
|FRUT;

|RUTINA ClaveBaja90;
     #90#0 = 0;
     #90#1 = 00;
|FRUT;

|RUTINA ClaveAlta90;
     #90#0 = 9;
     #90#1 = 99;
|FRUT;

|PROGRAMA;
     |ABRE #0;
     |LEE 040#0p;
     |SI FSalida ! 0;
         |MENAV "     Pase de Ficheros Generales no Realizado";
         |PTEC 807;
         |CIERRA #0;
         |ACABA;
     |FINSI;
     |CIERRA #0;

     |DBASE aBase;  |QBF aBase;
     aPathPases = aBase + "pases";  |MKDIR aPathPases;
     aPathPases = aPathPases + "/";

     |PATH_DAT #89 aPathPases;
     |PATH_DAT #90 aPathPases;

     |ABRE #89;
     |LEE 040#89p;
     |SI FSalida ! 0;
         |CIERRA #89;
         aOrigen  = aBase + "fich/dspase89.dat";
         aDestino = aPathPases + "dspase89.dat";
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;
     |CIERRA #89;

     |ABRE #90;
     |LEE 040#90p;
     |SI FSalida ! 0;
         |CIERRA #90;
         aOrigen  = aBase + "fich/dspase90.dat";
         aDestino = aPathPases + "dspase90.dat";
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;
     |CIERRA #90;

     |ABRE #89;
     |LEE 040#89p;
     |SI FSalida ! 0;
         |ABRE #90;  |CIERRA #90;  |DELINDEX #90;

         |ABRE #90;
         |HAZ TraeConceptos;
         |CIERRA #90;
     |FINSI;
     |CIERRA #89;

     |PINPA #0#89;
     |ENTLINEAL #1#90, -2, 7, 22, 1, ClaveBaja90, ClaveAlta90;
     |ENTLINEAL #1#89, -1, 4, 12, 0, ClaveBaja89, ClaveAlta89;
     |ENTLINEAL #1#89, -1, 7, 12, 0, ClaveBaja89, ClaveAlta89;
     |ENTLINEAL #1#90, -2, 4, 22, 0, ClaveBaja90, ClaveAlta90;
|FPRO;
