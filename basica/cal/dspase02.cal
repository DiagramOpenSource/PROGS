|FICHEROS;
     dspase00   #0;
     dspase15   #15;
     agicen01   #1101;
     agicen05   #1105;
     agicen03   #1103;
     agicen04   #1104;
     agicen05   #1105;
     agicen06   #1106;
     agicen07   #1107;
     agicen08   #1108;
     agicen09   #1109;
     agicen10   #1110;
     agicen11   #1111;
     agicen12   #1112;
     agicen13   #1113;
     agicen14   #1114;
     agicen15   #1115;
     agicen16   #1116;

     agim0002   #1120;

     refer00@ #900;
     refer01@ #901;
     refer02@ #902;
     refer03@ #903;
     refer04@ #904;
     refer05@ #905;
     refer06@ #906;
|FIN;

|VARIABLES;
     nEmpresa        = 0;
     nSwPasada       = 0;
     nDActi          = 0;
     nHActi          = 0;
     nDEmpre         = 0;
     nHEmpre         = 0;
     nConta          = 0;
     nLinea          = 0;
     nCampo          = 0;
     nSwEsta         = 0;
     nActiv          = 0;
     nSwCoger        = 0;
     nLineaA         = 0;
     nLineaC         = 0;
     nEmpresaDef     = 0;
     nAgrupa         = 0;
     nFactor         = 0;
     nAnyo           = 0;
     nAnyoAhora      = 0;
     nEjercicio      = 0;
     nCodigoRenta    = 0;
     nCodigoNue      = 0;
     nCodigoMadre    = 0;
     nCodigoComunero = 0;
     nCodigoReo      = 0;
     nAcumula        = 0;

     aEpigrafe       = "";
     aEpi1           = "";
     aEpi2           = "";
     aDef            = "";
     aPathOrigen     = "";
     aPathAnterior   = "";
     aAlfa           = "";
     aNombreActiv    = "";

     &eaPathAntes    = "";
     &eaPathBasica   = "";
     &eaPathDsmodelo = "";
     &enAnyo         = 0;
|FIN;

|PROCESO CogeCodigo;
     |SI #15#22 = "S";  |ACABA;  |FINSI;
     nCodigoNue = #15#3;
     |ERROR10;
|FPRC;

|DEFBUCLE dspase15;
     #15#4;
     ;
     nCodigoReo, "            ", 000;
     nCodigoReo, "zzzzzzzzzzzz", 999;
     ;
     NULL, CogeCodigo;
|FIN;

               ||Calculo Anterior refundiendo Actividades

|PROCESO PonActividad;
     nSwEsta = 0;

     #1103#0 = #1116#0;
     #1103#1 = #1116#1;
     |LEE 040#1103=;
     |SI FSalida = 0;  nSwEsta = 1;  |FINSI;

     #1104#0 = #1116#0;
     #1104#1 = #1116#1;
     |LEE 040#1104=;
     |SI FSalida = 0;  nSwEsta = 1;  |FINSI;

     #1106#0 = #1116#0;
     #1106#1 = #1116#1;
     #1106#2 = 1;
     |LEE 040#1106];
     |SI FSalida = 0;  |Y  #1106#0 = #1116#0; |Y #1106#1 = #1116#1;
         nSwEsta = 1;
     |FINSI;

     #1107#0 = #1116#0;
     #1107#1 = #1116#1;
     |LEE 040#1107=;
     |SI FSalida = 0;  nSwEsta = 1;  |FINSI;

     #1111#0 = #1116#0;
     #1111#1 = #1116#1;
     |LEE 040#1111=;
     |SI FSalida = 0;   nSwEsta = 1;  |FINSI;

     #1112#0 = #1116#0;
     #1112#1 = #1116#1;
     |LEE 040#1112];
     |SI FSalida = 0;   nSwEsta = 1;  |FINSI;

     #1113#0 = #1116#0;
     #1113#1 = #1116#1;
     #1113#2 = 1;
     |LEE 040#1113];
     |SI FSalida = 0;  |Y  #1113#0 = #1116#0;  |Y #1113#1 = #1116#1;
         nSwEsta = 1;
     |FINSI;

     |SI nSwEsta = 0;
         |BORRA 020#1116a;
         |LIBERA #1116;
         |ACABA;
     |FINSI;

     #1114#0 = #1116#0;
     #1114#1 = 1;
     |LEE 040#1114];
     |SI FSalida = 0;  |Y  #1114#0 = #1116#0;
         #1116#5 = #1114#3;
         #1116#6 = #1114#4;
         |GRABA 020#1116a;
         |LIBERA #1116;
     |FINSI;
|FPRC;

|DEFBUCLE agicen16;
     #1116#1;
     ;
     00000, 00;
     99999, 99;
     be;
     NULL, PonActividad;
|FIN;

|| ******* CREA EL FICHERO DE ACTIVIDADES

|PROCESO Rdtos;
     |SI nSwPasada = 0;
         |SALVA_DATOS #1120;

         #1120#1  = #1120#1 + 50;
         |GRABA 020#1120n;
         |LIBERA #1120;

         |REPON_DATOS #1120;
         |LEE 101#1120=;
         |SI FSalida = 0;
            |BORRA 020#1120a;
            |LIBERA #1120;
         |FINSI;
     |FINSI;

     |SI nSwPasada = 1;
         |SALVA_DATOS #1120;

         #1120#0  = #1114#0;
         #1120#1  = #1114#1;
         |LEE 101#1120=;
         |SI FSalida ! 0;
             |GRABA 020#1120n;
             |LIBERA #1120;
         |FINSI;

         |REPON_DATOS #1120;
         |LEE 101#1120=;
         |SI FSalida = 0;
            |BORRA 020#1120a;
            |LIBERA #1120;
         |FINSI;
     |FINSI;

     |SI nSwPasada = 2;
         |BORRA 020#1120a;
         |LIBERA #1120;
     |FINSI;
|FPRC;

|DEFBUCLE agim0002;
     #1120#1;
     ;
     nDEmpre, nDActi, 0000;
     nHEmpre, nHActi, 2999;
     be;
     NULL, Rdtos;
|FIN;

|PROCESO agicen05;
     #1115#0 = #1105#0;
     |LEE 101#1115=;
     |SI FSalida ! 0;
         #1115#0 = #1105#0;
         |GRABA 020#1115b;
     |FINSI;

     |PARA nCampo = 2;  |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
           #1115nCampo = #1105nCampo;
     |SIGUE;
     |GRABA 020#1115a;
     |LIBERA #1115;
|FPRC;

|DEFBUCLE agicen05;
     #1105#1;
     ;
     #1101#0, 00;
     #1101#0, 99;
     be;
     NULL, agicen05;
|FIN;

|PROCESO agicen08;
     |SALVA_DATOS #1108;
     #1108#1   = nConta;
     #1108#114 = #1114#1;
     |GRABA 020#1108n;
     |LIBERA #1108;

     |REPON_DATOS #1108;
     |LEE 101#1108=;
     |SI FSalida = 0;
         |BORRA 020#1108a;
         |LIBERA #1108;
     |FINSI;
|FPRC;

|DEFBUCLE agicen08;
     #1108#1;
     ;
     #1101#0, 0, #1101#1;
     #1101#0, 0, #1101#1;
     be;
     NULL, agicen08;
|FIN;

|PROCESO agicen09;
     |SALVA_DATOS #1109;
     #1109#1   = nConta;
     #1109#50  = #1114#1;
     |GRABA 020#1109n;
     |LIBERA #1109;

     |REPON_DATOS #1109;
     |LEE 101#1109=;
     |SI FSalida = 0;
         |BORRA 020#1109a;
         |LIBERA #1109;
     |FINSI;
|FPRC;

|DEFBUCLE agicen09;
     #1109#1;
     ;
     #1101#0, 0, #1101#1;
     #1101#0, 0, #1101#1;
     be;
     NULL, agicen09;
|FIN;

|PROCESO agicen10;
     |SALVA_DATOS #1110;
     #1110#1  = nConta;
     #1110#42 = #1114#1;
     |GRABA 020#1110n;
     |LIBERA #1110;

     |REPON_DATOS #1110;
     |LEE 101#1110=;
     |SI FSalida = 0;
         |BORRA 020#1110a;
         |LIBERA #1110;
     |FINSI;
|FPRC;

|DEFBUCLE agicen10;
     #1110#1;
     ;
     #1101#0, 0, #1101#1, 01;
     #1101#0, 0, #1101#1, 99;
     be;
     NULL, agicen10;
|FIN;

|PROCESO CreaActividad;
     nSwEsta = 0;

     |ABRE #1108;
     #1108#0   = #1101#0;
     #1108#114 = #1101#17;
     #1108#1   = 1;
     |LEE 040#1108];
     |SI FSalida = 0;  |Y #1108#0   = #1101#0; |Y #1108#114 = #1101#17;
         nSwEsta = 1;
     |FINSI;
     |CIERRA #1108;

     |ABRE #1109;
     #1109#0   = #1101#0;
     #1109#50  = #1101#17;
     #1109#1   = 1;
     |LEE 040#1109];
     |SI FSalida = 0;  |Y #1109#0   = #1101#0; |Y #1109#50 = #1101#17;
         nSwEsta = 1;
     |FINSI;
     |CIERRA #1109;

     |ABRE #1110;
     #1110#0   = #1101#0;
     #1110#42  = #1101#17;
     #1110#1   = 1;
     #1110#2   = 1;
     |LEE 040#1110];
     |SI FSalida = 0;  |Y #1110#0   = #1101#0; |Y #1110#42 = #1101#17;
         nSwEsta = 1;
     |FINSI;
     |CIERRA #1110;

     #1116#0 = #1101#0;
     #1116#1 = #1101#1;
     |SI #1101#6 = 1;
         #1116#2 = "REGISTRO MODELO 036 ANTERIOR A¥O 1998";
     |FINSI;
     |SI #1101#6 = 2;
         #1116#2 = "REGISTRO MODELO 037 ANTERIOR A¥O 1998";
     |FINSI;
     |SI #1101#6 = 3;
         #1116#2 = "REGISTRO MODELO 036 MODELO ACTUAL";
     |FINSI;
     |SI #1101#6 = 4;
         #1116#2 = "REGISTRO MODELO 037 MODELO ACTUAL";
     |FINSI;
     #1116#3 = #1101#5;
     #1116#4 = #1101#6;
     |GRABA 020#1116n;
     |LIBERA #1116;

     |SELECT #2#1114;
     #1114#0 = #1101#0;
     #1114#2 = #1101#2;
     #1114#3 = #1101#3;
     |LEE 040#1114=;
     |SI FSalida ! 0;
         |SELECT #1#1114;
         #1114#0 = #1101#0;
         #1114#1 = 99;
         |LEE 040#1114];
         |SI FSalida = 0;
             |LEE 040#1114a;
         |SINO;
             |LEE 040#1114u;
         |FINSI;

         |SI FSalida = 0;  |Y #1114#0 = #1101#0;
             nLinea = #1114#1 + 1;
         |SINO;
             nLinea = 1;
         |FINSI;

         |DDEFECTO #1114;
         #1114#0  = #1101#0;
         #1114#1  = nLinea;
         #1114#2  = #1101#2;
         #1114#3  = #1101#3;
         #1114#4  = #1101#4;
         #1114#5  = #1101#5;
         #1114#6  = #1101#14;
         #1114#13 = #1101#15;
         #1114#14 = #1101#16;
         |GRABA 020#1114n;
         |LIBERA #1114;
     |SINO;
         nLinea = #1114#1;
     |FINSI;

     |SELECT #1#1114;
     #1114#0 = #1101#0;
     #1114#1 = nLinea;
     |LEE 101#1114=;
     |SI FSalida = 0;
         #1114#6  = #1101#14;
         |GRABA 020#1114a;
         |LIBERA #1114;
     |FINSI;

     |SI nEmpresa ! #1101#0; |O aEpigrafe ! #1101#3;
         nConta    = 0;
         nEmpresa  = #1101#0;
         aEpigrafe = #1101#3;
     |FINSI;

     |SI nSwEsta = 1;
         |SALVA_DATOS #1101;

         |SELECT #1#1101;
         nConta = nConta + 1;
         #1101#0  = #1114#0;
         #1101#17 = #1114#1;
         #1101#1  = nConta;
         #1101#18 = "REGISTRO " + (("00" + #1101#1) % -102);
         |GRABA 020#1101n;
         |LIBERA #1101;

         |REPON_DATOS #1101;
         |SELECT #2#1101;
         |LEE 101#1101=;
         |SI FSalida = 0;
             nDEmpre = #1101#0;
             nHEmpre = #1101#0;
             nDActi  = #1101#1 + 50;
             nHActi  = #1101#1 + 50;
             nSwPasada = 1;
             |BUCLE agim0002;
             |BUCLE agicen08;
             |BUCLE agicen09;
             |BUCLE agicen10;
         |FINSI;
     |FINSI;

     |BORRA 020#1101a;
     |LIBERA #1101;
|FPRC;

|DEFBUCLE agicen01;
     #1101#2;
     ;
     00000, " ", "    ", 0,  1;
     99999, "z", "zzzz", 0, 99;
     be;
     NULL, CreaActividad;
|FIN;

|PROCESO DesdeCenso;
     |ABRE #1120;

     nDEmpre = 0;
     nHEmpre = 99999;
     nDActi  = 0;
     nHActi  = 50;
     nSwPasada = 0; |BUCLE agim0002;

     |ABRE #1114;
     |ABRE #1116;
     |BUCLE agicen01;
     |CIERRA #1116;
     |CIERRA #1114;
     |CIERRA #1120;

     |ABRE #1115;  |BUCLE agicen05;  |CIERRA #1115;

     nDEmpre = 0;
     nHEmpre = 99999;
     nDActi = 51;
     nHActi = 99;
     nSwPasada = 2; |BUCLE agim0002;

     |ABRE #1103;
     |ABRE #1104;
     |ABRE #1106;
     |ABRE #1107;
     |ABRE #1111;
     |ABRE #1112;
     |ABRE #1113;
     |ABRE #1114;
     |BUCLE agicen16;
     |CIERRA #1103;
     |CIERRA #1104;
     |CIERRA #1106;
     |CIERRA #1107;
     |CIERRA #1111;
     |CIERRA #1112;
     |CIERRA #1113;
     |CIERRA #1114;
|FPRC;

|| ________________________________________________________________________
|| Aplicacion REO
|| ________________________________________________________________________

|PROCESO PonTiposActiv;
     #900#7  = 1;
     |SI #901#29 = 0;  #900#7 = 1;  |FINSI;
     |SI #901#29 = 1;  #900#7 = 2;  |FINSI;
     |SI #901#29 = 2;  #900#7 = 5;  |FINSI;
     |SI #901#29 = 3;  #900#7 = 5;  |FINSI;
     |SI #901#29 = 4;  #900#7 = 7;  |FINSI;
     |SI #901#29 = 5;  #900#7 = 4;  |FINSI;
     |SI #901#29 = 6;  #900#7 = 10; |FINSI;
     |SI #901#29 = 7;  #900#7 = 2;  |FINSI;
     |SI #901#29 = 8;  #900#7 = 3;  |FINSI;
     |SI #901#29 = 9;  #900#7 = 3;  |FINSI;

     #900#9 = 1;
     |SI #901#22 = "A";   #900#9 = 5;  |FINSI;
     |SI #901#22 = "B";   #900#9 = 4;  |FINSI;
     |SI #901#22 = "C";   #900#9 = 3;  |FINSI;

     |SI #901#22 = "A";   |Y #901#41 = "S";
         #900#9 = 6;
     |FINSI;

     |SI #900#9 = 1;   |Y #901#41 = "S";
         #900#9 = 7;
     |FINSI;

     #902#0 = #900#7;
     |LEE 040#902=;
     |SI FSalida ! 0;  |DDEFECTO #902;  |FINSI;

     #903#0 = #900#9;
     |LEE 040#903=;
     |SI FSalida ! 0;  |DDEFECTO #903;  |FINSI;

     #900#8  = #902#5;
     #900#10 = #903#5;
|FPRC;

|PROCESO ChequeoActiv;
     nCodigoNue = 0;
     nCodigoReo = #901#0;
     |BUCLE dspase15;
     |SI nCodigoNue = 0;  |ACABA;  |FINSI;

     nSwEsta  = 0;
     nAcumula = 0;

     |ABRE #900;
     #900#0 = nCodigoNue;
     #900#1 = #901#1;
     |LEE 040#900=;
     |SI FSalida = 0;
         nSwEsta  = 1;
     |FINSI;
     |LIBERA #900;
     |CIERRA #900;

     |SI nSwEsta = 1;  |ACABA;  |FINSI;

     |ABRE #900;

     |DDEFECTO #900;
     #900#0  = nCodigoNue;
     #900#1  = #901#1;
     #900#2  = #901#4;
     #900#3  = #901#5;
     #900#4  = #901#6;
     #900#5  = #901#48;
     #900#6  = #901#49;

     |SI #901#2 ! nAnyoAhora;  |Y #900#6 = "01.01.1900";
         |SI #901#2 [ 80;
             aAlfa  = "31.12." + "20" + (("00" + #901#2) % -102);
         |SINO;
             aAlfa  = "31.12." + "19" + (("00" + #901#2) % -102);
         |FINSI;
         #900#6 = aAlfa;
     |FINSI;

     |SI #900#6 = "01.01.1900";  #900#6 = "01.01.0000";  |FINSI;
     #900#13 = #901#46;
     #900#14 = #901#14;

     |HAZ PonTiposActiv;

     |GRABA 020#900n;
     |LIBERA #900;
     |CIERRA #900;
|FPRC;

|DEFBUCLE eosactiv;
     #901#1003;
     ;
     00000, 0, nEjercicio;
     99999, 9, nEjercicio;
     ;
     NULL, ChequeoActiv;
|FIN;

|PROCESO Actividades;
     aDef = eaPathBasica + "def/agicen14.mas";
     |CARGA_DEF aDef, refer00@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aDef = aPathOrigen + "def/eosactiv.mas";
     |CARGA_DEF aDef, refer01@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #refer00@;
         |ACABA;
     |FINSI;

     aDef = eaPathBasica + "def/agitab10.mas";
     |CARGA_DEF aDef, refer02@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #refer01@;
         |DESCARGA_DEF #refer00@;
         |ACABA;
     |FINSI;

     aDef = eaPathBasica + "def/agitab12.mas";
     |CARGA_DEF aDef, refer03@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #refer02@;
         |DESCARGA_DEF #refer01@;
         |DESCARGA_DEF #refer00@;
         |ACABA;
     |FINSI;

     |PATH_DAT #901 aPathAnterior;

     aAlfa      = enAnyo;
     aAlfa      = aAlfa % -102;
     nAnyoAhora = aAlfa;
     |ABRE #902;
     |ABRE #903;
     |PARA nCampo = 115;  |SI nCampo ] 80;  |HACIENDO nCampo = nCampo - 1;
           nEjercicio = nCampo;
           |SI nCampo ] 100;  nEjercicio = nCampo - 100;  |FINSI;

           |BUCLE eosactiv;
     |SIGUE;
     |CIERRA #902;
     |CIERRA #903;

     |DESCARGA_DEF #refer03@;
     |DESCARGA_DEF #refer02@;
     |DESCARGA_DEF #refer01@;
     |DESCARGA_DEF #refer00@;
|FPRC;

|PROCESO LeeAgicen14C;
     |SI #902#3  = #903#5;
         |SI nSwCoger = 0;  nLineaA = #902#1;  |FINSI;
         |SI nSwCoger = 1;  nLineaC = #902#1;  |FINSI;
         aNombreActiv = #902#4;
         |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE agicen14C;
     #902#1002;
     ;
     nEmpresaDef, 01;
     nEmpresaDef, 99;
     ;
     NULL, LeeAgicen14C;
|FIN;

|PROCESO GrabaComunero;
     nCodigoNue = 0;
     nCodigoReo = #901#0;
     |BUCLE dspase15;
     |SI nCodigoNue = 0;  |ACABA;  |FINSI;

     nCodigoMadre = nCodigoNue;

     nLineaA = 0;
     #903#0  = #901#0;
     #903#1  = #901#1;
     #903#2  = #901#7;
     |LEE 040#903=;
     |SI FSalida ! 0;  |DDEFECTO #903;  |FINSI;

     |ABRE #902;
     #902#0  =  nCodigoMadre;
     #902#1  = #901#1;
     |LEE 040#902=;
     |SI FSalida = 0;
         |SI #902#3 = #903#5;  nLineaA = #902#1; |FINSI;
     |FINSI;
     |CIERRA #902;

     nSwCoger    = 0;
     nEmpresaDef = nCodigoMadre;
     |SI nLineaA = 0;  |BUCLE agicen14C;  |FINSI;
     |SI nLineaA = 0;  |ACABA;            |FINSI;

     nCodigoNue = 0;
     nCodigoReo = #901#3;
     |BUCLE dspase15;
     |SI nCodigoNue = 0;  |ACABA;  |FINSI;

     nCodigoComunero = nCodigoNue;

     nLineaC = 0;
     #903#0  = #901#3;
     #903#1  = #901#5;
     #903#2  = #901#7;
     |LEE 040#903=;
     |SI FSalida ! 0;  |DDEFECTO #903;  |FINSI;

     |ABRE #902;
     #902#0  = nCodigoComunero;
     #902#1  = #901#5;
     |LEE 040#902=;
     |SI FSalida = 0;
         |SI #902#3 = #903#5;
             nLineaC = #902#1;
             aNombreActiv = #902#4;
         |FINSI;
     |FINSI;
     |CIERRA #902;

     nSwCoger    = 1;
     nEmpresaDef = nCodigoComunero;
     |SI nLineaC = 0;  |BUCLE agicen14C;  |FINSI;
     |SI nLineaC = 0;  |ACABA;            |FINSI;

     nLinea = 1;
     #900#0 = nCodigoMadre;
     #900#1 = nLineaA;
     #900#2 = 9999;
     |LEE 040#900];
     |SI FSalida = 0;
         |LEE 040#900a;
     |SINO;
         |LEE 040#900u;
     |FINSI;
     |SI FSalida = 0;  |Y #900#0 = nCodigoMadre; |Y #900#1 = nLineaA;
         nLinea = #900#2 + 1;
     |FINSI;

     |DDEFECTO #900;
     #900#0 = nCodigoMadre;
     #900#1 = nLineaA;
     #900#2 = nLinea;
     #900#3 = nCodigoComunero;
     #900#4 = nLineaC;
     #900#5 = #901#4;
     #900#6 = aNombreActiv;
     #900#7 = #901#6;
     |SI #901#7 [ 80;
         nAnyo = 2000 + #901#7;
     |SINO;
         nAnyo = 1900 + #901#7;
     |FINSI;

     aAlfa  = "01.01." + nAnyo;  #900#8 = aAlfa;
     aAlfa  = "31.12." + nAnyo;  #900#9 = aAlfa;

     |GRABA 020#900n;
     |LIBERA #900;
|FPRC;

|DEFBUCLE eoscomul;
     #901#1004;
     ;
     00000, 0, nEjercicio, 00;
     99999, 9, nEjercicio, 99;
     ;
     NULL, GrabaComunero;
|FIN;

|PROCESO ArreglaComunero;
     |SI nEmpresa ! #900#0;  |O nActiv ! #900#1;
         nEmpresa   = #900#0;
         nActiv     = #900#1;
         nAnyoAhora = enAnyo;
     |FINSI;

     aAlfa = #900#9 % -104;
     nAnyo = aAlfa;
     |SI nAnyo = nAnyoAhora;
         #900#9 = "01.01.0000";
         |GRABA 020#900a;
     |FINSI;
     |LIBERA #900;
|FPRC;

|DEFBUCLE dsmom001;
     #900#1003;
     ;
     00000, 00, 0000;
     99999, 99, 9999;
     be;
     NULL, ArreglaComunero;
|FIN;

|PROCESO Comuneros;
     aDef = eaPathDsmodelo + "def/dsmom001.mas";
     |CARGA_DEF aDef, refer00@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aDef = aPathOrigen + "def/eoscomul.mas";
     |CARGA_DEF aDef, refer01@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #refer00@;
         |ACABA;
     |FINSI;

     aDef = eaPathBasica + "def/agicen14.mas";
     |CARGA_DEF aDef, refer02@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #refer01@;
         |DESCARGA_DEF #refer00@;
         |ACABA;
     |FINSI;

     aDef = aPathOrigen + "def/eosactiv.mas";
     |CARGA_DEF aDef, refer03@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #refer02@;
         |DESCARGA_DEF #refer01@;
         |DESCARGA_DEF #refer00@;
         |ACABA;
     |FINSI;

     |ABRE #900;  |CIERRA #900;  |DELINDEX #900;

     |PATH_DAT #901 aPathAnterior;
     |PATH_DAT #903 aPathAnterior;

     |ABRE #900;
     |ABRE #903;
     |PARA nCampo = 115;  |SI nCampo ] 80;  |HACIENDO nCampo = nCampo - 1;
           nEjercicio = nCampo;
           |SI nCampo ] 100;  nEjercicio = nCampo - 100;  |FINSI;

           |BUCLE eoscomul;
     |SIGUE;
     |CIERRA #900;
     |CIERRA #903;

     nEmpresa   = 0;
     nActiv     = 0;
     nAnyoAhora = 0;
     |BUCLE dsmom001;

     |DESCARGA_DEF #refer03@;
     |DESCARGA_DEF #refer02@;
     |DESCARGA_DEF #refer01@;
     |DESCARGA_DEF #refer00@;
|FPRC;

|| Chequeo Ultimo de las Actividades

|PROCESO ChequeaAgicen14;
     |SI #900#3 = "8612";
         #900#2 = "E";
     |FINSI;

     |SI #900#2 ! "E";  |Y #900#2 ! "P";
      |Y #900#2 ! "A";  |Y #900#2 ! " ";
         #900#2 = "O";
     |FINSI;

     nAgrupa = 0;
     |SI #900#2   = "E";
         aAlfa    = #900#3 % 101;
         nAgrupa = aAlfa;
         nAgrupa = nAgrupa * 100;
     |FINSI;

     aEpigrafe = #900#3;  |QBT aEpigrafe;
     aAlfa      = aEpigrafe % 0;
     |SI aAlfa  = "4";
         aEpigrafe = (aEpigrafe % 103) + "." + (aEpigrafe % 401);
     |FINSI;

     |SI #900#2 = "O";
         aEpigrafe = "9999";
         #900#11   = 999;
     |SINO;
         #901#0 = #900#2;
         #901#1 = nAgrupa;
         #901#2 = aEpigrafe;
         |LEE 040#901=;
         |SI FSalida ! 0;  |DDEFECTO #901;  |FINSI;

         #900#4  = #901#3;
         #900#16 = #901#5;

         |SI #900#2 = "E";
             aAlfa   = #900#3 % 101;
             nFactor = aAlfa;
             nFactor = 100 + (nFactor * 10);
         |FINSI;

         |SI #900#2 = "P";
             aAlfa   = #900#3 % 101;
             nFactor = aAlfa;
             nFactor = 200 + (nFactor * 10);
         |FINSI;

         |SI #900#2 = "A";
             aAlfa   = #900#3 % 102;
             nFactor = aAlfa;
             nFactor = 200 + nFactor;
         |FINSI;

         #900#11 = nFactor;
     |FINSI;

     aAlfa = aEpigrafe % 0;
     |SI aAlfa  = "5";
         aAlfa  = (aEpigrafe % 103) + (aEpigrafe % 501);
         #900#3 = aAlfa;
     |SINO;
         #900#3 = aEpigrafe;
     |FINSI;

     #902#0 = #900#16;
     |LEE 040#902=;
     |SI FSalida ! 0;  |DDEFECTO #902;  |FINSI;

     #903#0 = #900#11;
     |LEE 040#903=;
     |SI FSalida ! 0;  |DDEFECTO #903;  |FINSI;

     #900#17 = #902#1;
     #900#12 = #903#1;

     #900#13 = "N";
     #900#14 = "N";

     |SELECT #1#904;
     #904#0  = #900#0;
     #904#1  = #900#1;
     #904#2  = 1;
     |LEE 040#904];
     |SI FSalida = 0;  |Y #904#0 = #900#0;  |Y #904#1 = #900#1;
         #900#13 = "S";
     |SINO;
         |SELECT #2#904;
         #904#3  = #900#0;
         #904#4  = #900#1;
         |LEE 040#904=;
         |SI FSalida = 0;
             #900#14 = "S";
         |FINSI;
     |FINSI;

     |SI #900#13 = "S";
         |SI #900#9 = 5;  #900#9 = 10;  |FINSI;
         |SI #900#9 = 4;  #900#9 = 9;   |FINSI;
         |SI #900#9 = 3;  #900#9 = 8;   |FINSI;
         |SI #900#9 = 6;  #900#9 = 11;  |FINSI;
         |SI #900#9 = 7;  #900#9 = 12;  |FINSI;
         |SI #900#9 = 1;  #900#9 = 8;   |FINSI;
         |SI #900#9 = 0;  #900#9 = 8;   |FINSI;
     |FINSI;

     |SI #900#14 = "S";
         |SI #900#9 = 5;  #900#9 = 15;  |FINSI;
         |SI #900#9 = 4;  #900#9 = 14;  |FINSI;
         |SI #900#9 = 3;  #900#9 = 13;  |FINSI;
         |SI #900#9 = 6;  #900#9 = 16;  |FINSI;
         |SI #900#9 = 7;  #900#9 = 17;  |FINSI;
         |SI #900#9 = 1;  #900#9 = 13;  |FINSI;
         |SI #900#9 = 0;  #900#9 = 13;  |FINSI;
     |FINSI;

     #905#0 = #900#9;
     |LEE 040#905=;
     |SI FSalida ! 0;  |DDEFECTO #905;  |FINSI;

     #906#0 = #900#7;
     |LEE 040#906=;
     |SI FSalida ! 0;  |DDEFECTO #906;  |FINSI;

     #900#8  = #906#5;
     #900#10 = #905#5;

     nCodigoRenta = 0;
     |SI #900#3 = "8612";  nCodigoRenta = 1;  |FINSI;

     |SI #900#9 ! 1;  |Y #900#9 ! 2;  |Y #900#9 ! 8;  |Y #900#9 ! 9;
      |Y #900#9 ! 10; |Y #900#9 ! 11; |Y #900#9 ! 12;
         nCodigoRenta = 1;
     |FINSI;

     |SI nCodigoRenta = 1;
         |SI #900#18 = 0;
             #900#18 = #900#0;
         |FINSI;
     |FINSI;

     |GRABA 020#900a;
     |LIBERA #900;
|FPRC;

|DEFBUCLE agicen14Cheq;
     #900#1002;
     ;
     00000, 01;
     99999, 99;
      be;
     NULL, ChequeaAgicen14;
|FIN;

|PROCESO ChequeaActividad;
     aDef = eaPathBasica + "def/agicen14.mas";
     |CARGA_DEF aDef, refer00@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aDef = eaPathBasica + "def/agiepim2.mas";
     |CARGA_DEF aDef, refer01@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #refer00@;
         |ACABA;
     |FINSI;

     aDef = eaPathBasica + "def/agitab14.mas";
     |CARGA_DEF aDef, refer02@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #refer01@;
         |DESCARGA_DEF #refer00@;
         |ACABA;
     |FINSI;

     aDef = eaPathBasica + "def/agitab05.mas";
     |CARGA_DEF aDef, refer03@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #refer02@;
         |DESCARGA_DEF #refer01@;
         |DESCARGA_DEF #refer00@;
         |ACABA;
     |FINSI;

     aDef = eaPathDsmodelo + "def/dsmom001.mas";
     |CARGA_DEF aDef, refer04@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #refer03@;
         |DESCARGA_DEF #refer02@;
         |DESCARGA_DEF #refer01@;
         |DESCARGA_DEF #refer00@;
         |ACABA;
     |FINSI;

     aDef = eaPathBasica + "def/agitab12.mas";
     |CARGA_DEF aDef, refer05@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #refer04@;
         |DESCARGA_DEF #refer03@;
         |DESCARGA_DEF #refer02@;
         |DESCARGA_DEF #refer01@;
         |DESCARGA_DEF #refer00@;
         |ACABA;
     |FINSI;

     aDef = eaPathBasica + "def/agitab10.mas";
     |CARGA_DEF aDef, refer06@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #refer05@;
         |DESCARGA_DEF #refer04@;
         |DESCARGA_DEF #refer03@;
         |DESCARGA_DEF #refer02@;
         |DESCARGA_DEF #refer01@;
         |DESCARGA_DEF #refer00@;
         |ACABA;
     |FINSI;

     |ABRE #901;
     |ABRE #902;
     |ABRE #903;
     |ABRE #904;
     |ABRE #905;
     |ABRE #906;
     |BUCLE agicen14Cheq;
     |CIERRA #901;
     |CIERRA #902;
     |CIERRA #903;
     |CIERRA #904;
     |CIERRA #905;
     |CIERRA #906;

     |DESCARGA_DEF #refer06@;
     |DESCARGA_DEF #refer05@;
     |DESCARGA_DEF #refer04@;
     |DESCARGA_DEF #refer03@;
     |DESCARGA_DEF #refer02@;
     |DESCARGA_DEF #refer01@;
     |DESCARGA_DEF #refer00@;
|FPRC;

|PROCESO PasaBasica;
     nCodigoNue = 0;
     nCodigoReo = #901#0;
     |BUCLE dspase15;
     |SI nCodigoNue = 0;  |ACABA;  |FINSI;

     #900#0 = nCodigoNue;
     |LEE 101#900=;
     |SI FSalida ! 0;
         |DDEFECTO #900;
         #900#0 = nCodigoNue;
         |GRABA 020#900b;
     |SINO;
         |LIBERA #900;
         |ACABA;
     |FINSI;

     #900#13 = #901#1;
     #900#2  = #901#2;
     #900#1  = #901#3;
     #900#3  = #901#4;
     #900#4  = #901#5;
     #900#5  = #901#6;
     #900#6  = #901#7;
     #900#7  = #901#8;
     #900#8  = #901#9;
     #900#9  = #901#10;
     #900#10 = #901#11;
     #900#12 = #901#12;
     #900#11 = #901#13;
     #900#14 = #901#14;
     #900#15 = #901#15;
     #900#88 = #901#16;
     #900#63 = #901#18;
     #900#64 = #901#19;
     #900#16 = #901#21;
     #901#2  = #901#63;
     #900#52 = #901#52;
     #900#53 = #901#53;
     #900#55 = #901#54;
     #900#57 = #901#55;
     #900#58 = #901#56;
     #900#60 = #901#57;
     #900#59 = #901#58;
     #900#65 = #901#64;

     |GRABA 020#900a;
     |LIBERA #900;
|FPRC;

|DEFBUCLE eosclien;
     #901#1001;
     ;
     00001;
     99999;
     be;
     NULL, PasaBasica;
|FIN;

|PROCESO CargaClienteEos;
     aDef = eaPathBasica + "def/agifigen.mas";
     |CARGA_DEF aDef, refer00@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aDef = aPathOrigen + "def/eosclien.mas";
     |CARGA_DEF aDef, refer01@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #refer00@;
         |ACABA;
     |FINSI;

     |PATH_DAT #901 aPathAnterior;

     |ABRE #900;
     |BUCLE eosclien;
     |CIERRA #900;

     |DESCARGA_DEF #refer01@;
     |DESCARGA_DEF #refer00@;
|FPRC;

|PROCESO DesdeFiscal;
     aPathOrigen    = eaPathAntes;  |QBF aPathOrigen;
     aPathOrigen    = aPathOrigen + "agieos/";
     aPathAnterior  = aPathOrigen + "fich/";

     |HAZ CargaClienteEos;
     |HAZ Actividades;
     |HAZ Comuneros;
|FPRC;

|PROGRAMA;
     |SI eaPathBasica = "";   |ACABA;  |FINSI;

     |ABRE #0;
     |LEE 040#0p;
     |SI FSalida ! 0;
         |MENAV "     Pase de Ficheros Generales no Realizado";
         |CIERRA #0;
         |ACABA;
     |FINSI;
     |CIERRA #0;

     eaPathAntes = #0#0;  |QBT eaPathAntes;

     |ABRE #1114;
     |LEE 040#1114p;
     |SI FSalida = 0;  |CIERRA #1114;  |ACABA;  |FINSI;
     |CIERRA #1114;

     |MENSA "           Reconstruyendo Ficheros Actividades";

     |HAZ DesdeFiscal;
     |HAZ ChequeaActividad;
     |HAZ DesdeCenso;
     |HAZ ChequeaActividad;
|FPRO;
