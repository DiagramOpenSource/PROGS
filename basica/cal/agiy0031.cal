|FICHEROS;
     agiy0031 #0;
     agim0019 #1;
     agifigen #2;
     agiw0035 #99;
|FIN;

|VARIABLES;
     nCampo   = 0;
     nEmpresa = 0;
     nSwHay   = 0;
     nTipo    = 0;

     aFichero = "";
     aAlfa    = "";
|FIN;

|INCLUYE i_consul;

|PROCESO Parrilla;
     |SI #0#2 = 0;
         #0#2 = 0;  |PINTA #0#2;
         #0#3 = 0;  |PINTA #0#3;  |C_M #0#3, 1, "N";
         |PARA nCampo = 4;  |SI nCampo [ 21;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |C_M #0#3, 1, "S";
         |PARA nCampo = 4;  |SI nCampo [ 21;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasTipos;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 21;  |HACIENDO nCampo = nCampo + 1;
               #0Campo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 21;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO GrabaTempo;
     #99#0 = #1#4;
     #99#1 = #1#0;
     #99#2 = #1#5;
     #99#3 = #2#1;
     aAlfa = #2#62;  |QBF aAlfa;
     |SI aAlfa = "ACTIVA";  |O aAlfa = "INACTIVA";
         #99#4 = aAlfa;
         #99#5 = #2#93;
         #99#6 = #2#94;
     |SINO;
         #99#4 = #1#6;
         #99#5 = #1#7;
         #99#6 = #1#55;
     |FINSI;
     #99#7 = #2#13;
     |SI #99#6 = "00.00.0000";  #99#6 = "01.01.0000";  |FINSI;
     |GRABA 020#99n;
     |LIBERA #99;
     nSwHay = 1;
|FPRC;

|PROCESO MiraSociedad;
     |SI nEmpresa = #1#0;  |ACABA;  |FINSI;

     #2#0 = #1#0;
     |LEE 040#2=;
     |SI FSalida ! 0;  |DDEFECTO #2;  |FINSI;

     |SI #0#2 ! 0;
         |SI #1#4 ] #0#2;  |Y #1#4 [ #0#3;
             |HAZ GrabaTempo;
         |FINSI;
     |SINO;
         |PARA nCampo = 4;  |SI nCampo [ 21;  |HACIENDO nCampo = nCampo + 1;
               |SI #0nCampo ! 0;
                   |SI #0nCampo = #1#4;  |HAZ GrabaTempo;  |FINSI;
               |FINSI;
         |SIGUE;
     |FINSI;

     nEmpresa = #1#0;
|FPRC;

|DEFBUCLE agim0019;
     #1#2;
     ;
     #0#0, 99;
     #0#1, 01;
     ;
     NULL, MiraSociedad;
|FIN;

|PROCESO Imprime;
     |SI nTipo ! 0;
         |SI nTipo ! #99#0;  |PIEINF;  |FINSI;
     |FINSI;
     |PRINT;
     nTipo = #99#0;
|FPRC;

|DEFBUCLE agiw0035;
     #99#1;
     ;
     01, 00001;
     99, 99999;
     ;
     NULL, Imprime;
|FIN;

|PROCESO Tipo3;  |TIPO 3;
     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aFichero = ("35" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #99 aFichero;
     |ABRE #99;  |CIERRA #99;  |DELINDEX #99;

     nSwHay = 0;
     |ABRE #2;
     |ABRE #99;
     |BUCLE agim0019;
     |CIERRA #2;
     |CIERRA #99;

     |SI nSwHay = 0;
         |MENAV "      Seleccion Vacia ...";
         |FINIMP;
         |ABRE #99;  |CIERRA #99;  |DELINDEX #99;
         |ACABA;
     |FINSI;

     |INFOR "agiy0031";
     |SI FSalida ! 0;
         |FINIMP;
         |ABRE #99;  |CIERRA #99;  |DELINDEX #99;
         |ACABA;
     |FINSI;

     |BUCLE agiw0035;

     |PIEINF;
     |FININF;
     |FINIMP;

     |ABRE #99;  |CIERRA #99;  |DELINDEX #99;
|FPRC;
