|FICHEROS;
     socz0003;

     socm0005;
     socw0008,MANTE;

     agifigen;
     agitab04;

     clientes@;
|FIN;

|VARIABLES;
     nPanta             = 0;
     nRango             = 0;
     nPos1              = 0;
     nPos2              = 0;
     nGridCli           = 0;
     nGrid              = 0;
     n0Vent             = 0;
     n1Vent             = 0;
     nBoton             = 0;
     nBtnSal            = -1;
     nHay               = -1;
     nDesdeEmp          = -1;
     nHastaEmp          = -1;
     nDesdeAny          = -1;
     nHastaAny          = -1;
     nCampo             = -1;
     nBtnAlt            = -1;
     nBtnMod            = -1;
     nBtnBaj            = -1;
     nBtnAdj1           = -1;
     nBtnAdj2           = -1;
     nModo              = -1;
     nNume              = 0;
     nIde               = 0;
     nHandle            = 0;
     nPausa             = 0;
     nTree              = 0;

     aQueBoton          = "";
     aID                = "";
     aFichero           = "";
     aTecla             = "";
     aEsc1              = "";
     aEsc2              = "";
     aRetu              = "";
     aAlfa              = "";
     aAlfa1             = "";
     aDef               = "";
     aMas               = "";
     aAbre              = "";
     aQueQuiero         = "";
     aNumCampos         = "";
     aOrigen            = "";
     aDestino           = "";
     aBaseBasica        = "";
     aPathActas         = "";
     aPathLocal         = "";
     aPathBase          = "";
     aEjecuta           = "";
     aParaBor           = "";
     aParaIni           = "";
     aContenido         = "";
     aExt               = "";

     fFecha             = @;

     {-1}aVent          = "";
         aVent1         = 0;
         aVent2         = 0;
         aVent3         = 0;
         aVent4         = 0;
         aVent5         = "";

     {-1}sTree          = 0;
     sT_nID             = 0;
     sT_nOper           = 0;
     sT_aData           = "";

     {-1}aGet           = 0;
         aGet1          = 0;
         aGet2          = 0;

     &enBtnRetG1        = 0;
     &enBtnDatG1        = 0;
     &enNumCampos       = 0;
     &enInforme         = 0;
     &enPrint           = 0;
     &enCapPdte         = 0;
     &enRecPdte         = 0;
     &eaUnidadBasico    = "";
     &eaOrigen          = "";
     &eaDestino         = "";

     &enEmpresa         = 0;
|FIN;

|PROCESO PtecFinalFz3;  |TIPO 0;
     |POSICIONATE #socz0003#0;
|FPRC;

|PROCESO Tipo66C3Fz3;  |TIPO 66;
     |ABRE #agitab04;
     |CONSULTA_CLAVE #2#agitab04;
     |SI FSalida = 0;
         #socz0003#3 = #agitab04#1;
         #socz0003#4 = #agitab04#0;

         |PINTA #socz0003#3;
     |FINSI;
     |CIERRA #agitab04;
|FPRC;

|PROCESO Tipo0C3Fz3;  |TIPO 0;
     aAlfa = #socz0003#3;  |QBF aAlfa;
     |SI aAlfa = "";  |ACABA;  |FINSI;

     |ABRE #agitab04;
     |SELECT #2#agitab04;
     #agitab04#1 = #socz0003#3;
     |LEE 000#agitab04.=;
     |SI FSalida ! 0;
         |CIERRA #agitab04;
         |MENAV "0000No existe el tipo de empresa";
         |ERROR;
         |ACABA;
     |FINSI;

     #socz0003#3 = #agitab04#1;
     #socz0003#4 = #agitab04#0;

     |PINTA #socz0003#3;
     |CIERRA #agitab04;
|FPRC;

|PROCESO Tipo7C0Fz3;  |TIPO 7;
     |SI nBtnSal ! -1;  |ACABA;  |FINSI;

     aQueBoton = "|X000";
     aID       = #socz0003#aQueBoton#;
     nBoton    = aID;
     |ESTADO_CONTROL nBoton, "HIDE";

     aQueBoton = "|X001";
     aID       = #socz0003#aQueBoton#;
     nBoton    = aID;
     |ESTADO_CONTROL nBoton, "HIDE";

     |CONTROL_SIMPLE 0, "BOTON,{{230}}Continuar", 3686, 3696, Continuar;
     |CONTROL_SIMPLE 0, "BOTON,{{001}}Salir", 3673, 3683, Salir;

     nBtnSal = 0;
|FPRC;

|PROCESO Tipo0C0Fz3;  |TIPO 0;
     |SI #socz0003#0 = 0;
         #socz0003#1 = "Todas";
     |SINO;
         |ABRE #agifigen;
         #agifigen#0 = #socz0003#0;
         |LEE 000#agifigen.=;
         |SI FSalida ! 0;
             |MENAV "0000No exite la empresa.";
             |ERROR;
             |CIERRA #agifigen;
             |ACABA;
         |FINSI;
         |CIERRA #agifigen;

         #socz0003#1 = #agifigen#1;
     |FINSI;

     |PINTA #socz0003#1;
|FPRC;

|| -------------------------------------------------------------------------


|PROCESO EntraDatos;
     aAlfa = ("00000" + #clientes@#0) % -105;
     aAlfa = aAlfa + " / " + #clientes@#1;  |QBF aAlfa;

     |VENTANA 0501, 999, -1, 16, aAlfa;
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |C_M #socw0008#1, 1, "N";

     |SI nModo = 1;
         |DDEFECTO #socw0008;

         |SI #socz0003#2 ! 0;
             #socw0008#1 = #socz0003#2;
         |SINO;
             fFecha = ~;
             aAlfa  = fFecha % -104;
             nNume  = aAlfa;
             nNume  = nNume - 1;
             #socw0008#1 = nNume;
             |C_M #socw0008#1, 1, "S";
         |FINSI;
     |SINO;
         #socw0008#0 = #socm0005#0;
         #socw0008#1 = #socm0005#1;
         #socw0008#2 = #socm0005#2;
         #socw0008#3 = #socm0005#3;
         #socw0008#4 = #socm0005#4;
         #socw0008#5 = #socm0005#5;
         #socw0008#6 = #socm0005#6;
     |FINSI;

     |PULSATECLA;

     |PTEC 802;  |PAUSA;

     |PINPA #0#socw0008;
     |PINDA #0#socw0008;

     |ENDATOS #2#socw0008;

     |SI FSalida = 0;
         |SI nModo = 1;
             #socm0005#0  = #clientes@#0;
             #socm0005#1  = #socw0008#1;
             #socm0005#2  = 9999;
             |LEE 000#socm0005.];
             |SI FSalida = 0;
                 |LEE 000#socm0005.a;
             |SINO;
                 |LEE 000#socm0005.u;
             |FINSI;

             nIde = 2;
             |SI FSalida = 0; |Y #socm0005#0 = #clientes@#0;  |Y #socm0005#1  = #socw0008#1;
                 nIde = #socm0005#2 + 1;
             |FINSI;

             |DDEFECTO #socm0005;
             #socm0005#0  = #clientes@#0;
             #socm0005#1  = #socw0008#1;
             #socm0005#2  = nIde;
             #socm0005#3  = #socw0008#3;
             #socm0005#4  = #socw0008#4;
             |GRABA 020#socm0005.n;
             |LIBERA #socm0005;
         |SINO;
             |LEE 101#socm0005.=;
             |SI FSalida = 0;
                 #socm0005#3  = #socw0008#3;
                 #socm0005#4  = #socw0008#4;
                 |GRABA 020#socm0005.a;
                 |LIBERA #socm0005;
             |FINSI;
         |FINSI;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;
     |PULSATECLA;

     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO Alta;
     nModo = 1;
     |HAZ EntraDatos;

     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO Modifica;
     nModo = 2;

     |HAZ EntraDatos;

     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO Baja;
     |CONFI "0000NSeguro que quiere BORRAR el registro";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |LEE 101#socm0005.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |BORRA 020#socm0005.a;
     |LIBERA #socm0005;

     aAlfa = aPathActas  + ("00000" + #socm0005#0) % -105;
     aAlfa = aAlfa       + ("0000"  + #socm0005#1) % -104;
     aAlfa = aAlfa       + ("0000"  + #socm0005#2) % -104;
     aAlfa = aAlfa       + "1." + #socm0005#5;  |QBF aAlfa;
     |FBORRA aAlfa;

     aAlfa = aPathActas  + ("00000" + #socm0005#0) % -105;
     aAlfa = aAlfa       + ("0000"  + #socm0005#1) % -104;
     aAlfa = aAlfa       + ("0000"  + #socm0005#2) % -104;
     aAlfa = aAlfa       + "2." + #socm0005#6;  |QBF aAlfa;
     |FBORRA aAlfa;

     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO PresentaDocumento;
     |DBASE aPathBase;
     |QBF aPathBase;

     eaOrigen  = aPathBase  + "plantillas/dsabreextension.exe";
     eaDestino = aPathLocal + "dsabreextension.exe";
     |HAZ EnviaFicheroConMD5;
     |XABRE "EC", eaDestino, 0;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     eaOrigen  = aPathActas + aFichero;
     eaDestino = aPathLocal + aFichero;
     |HAZ EnviaFicheroSinMD5;

     |XABRE "EC", eaDestino, 0;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     aEjecuta = "START " + aPathLocal + "dsabreextension ";
     aEjecuta = aEjecuta + aPathLocal + aFichero;
     aEjecuta = aEjecuta + " " + aPathLocal + "fin" + Usuario + ".txt";

     aAlfa    = aPathLocal + "fin" + Usuario + ".txt";
     |RFBORRA aAlfa;

     |RSYSTEM aEjecuta;
     aAlfa  = aPathLocal + "fin" + Usuario + ".txt";
     |PARA;  |SI;  |HACIENDO;
             |XABRE "EC", aAlfa, nHandle;
             |SI FSalida ] 0;
                 |SAL;
             |FINSI;

             |PARA nPausa = 1; |SI nPausa < 10000; |HACIENDO nPausa = nPausa + 1;
             |SIGUE;
             |PULSATECLA;
     |SIGUE;
     |PULSATECLA;

     aAlfa   = aPathLocal + "fin" + Usuario + ".txt";
     |RFBORRA aAlfa;

     aAlfa   = aPathLocal + aFichero;
     |RFBORRA aAlfa;
|FPRC;

|PROCESO Browse;
     |DBASE aPathBase;
     |QBF aPathBase;

     eaOrigen  = aPathBase  + "plantillas/dsselect.exe";
     eaDestino = aPathLocal + "dsselect.exe";
     |HAZ EnviaFicheroConMD5;
     |XABRE "EC", eaDestino, 0;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     aParaBor = aPathLocal + "dsselect.txt";
     aParaIni = aPathLocal + "dsselect.ini";

     |RFBORRA aParaIni;

     aAlfa = &34 + eaUnidadBasico + &34 + " " + &34 + aParaBor + &34;
     |XABRE "CW", aParaIni, nHandle;
     |SI FSalida ] 0;
          |XGRABA nHandle, aAlfa;
     |FINSI;
     |XCIERRA nHandle;

     aEjecuta = aPathLocal + "dsselect.exe";
     |RSYSTEM aEjecuta;

     aAbre = aPathLocal + "dsselect.txt";
     |XABRE "CU", aAbre, nHandle;
     |SI FSalida < 0;
         |RFBORRA aParaBor;
         |RFBORRA aParaIni;

         |ACABA;
     |FINSI;

     |XLEE nHandle, 250, aFichero;
     |XCIERRA nHandle;

     |QBF aFichero;
     |SI aFichero  = "";
         |RFBORRA aParaBor;
         |RFBORRA aParaIni;

         |ACABA;
     |FINSI;

     aExt = (aFichero % -104) < "";
     aAlfa1 = aExt % 101;
     |SI aAlfa1 ! ".";
          aExt = (aFichero % -105) < "";
     |FINSI;
     aAlfa1 = aExt % 101;
     |SI aAlfa1 ! ".";
         |RFBORRA aParaBor;
         |RFBORRA aParaIni;

         |MENAV "0000Formato del documento inv lido.";
         |ACABA;
     |FINSI;

     |LEE 101#socm0005.=;

     |SI nCampo = 5;
         aAlfa = aPathActas  + ("00000" + #socm0005#0) % -105;
         aAlfa = aAlfa       + ("0000"  + #socm0005#1) % -104;
         aAlfa = aAlfa       + ("0000"  + #socm0005#2) % -104;
         aAlfa = aAlfa       + "1." + #socm0005#5;  |QBF aAlfa;
         |XABRE "E", aAlfa, 0;
         |SI FSalida ] 0;
             |FBORRA aAlfa;
         |FINSI;
     |FINSI;

     |SI nCampo = 6;
         aAlfa = aPathActas  + ("00000" + #socm0005#0) % -105;
         aAlfa = aAlfa       + ("0000"  + #socm0005#1) % -104;
         aAlfa = aAlfa       + ("0000"  + #socm0005#2) % -104;
         aAlfa = aAlfa       + "2." + #socm0005#6;  |QBF aAlfa;
         |XABRE "E", aAlfa, 0;
         |SI FSalida ] 0;
             |FBORRA aAlfa;
         |FINSI;
     |FINSI;

     #socm0005#nCampo# = aExt - ".";

     eaOrigen  = aFichero;
     eaDestino = aPathActas;
     eaDestino = eaDestino + ("00000" + #socm0005#0) % -105;
     eaDestino = eaDestino + ("0000"  + #socm0005#1) % -104;
     eaDestino = eaDestino + ("0000"  + #socm0005#2) % -104;

     |SI nCampo = 5;
         eaDestino = eaDestino + "1." + #socm0005#5;  |QBF eaDestino;
     |FINSI;

     |SI nCampo = 6;
         eaDestino = eaDestino + "2." + #socm0005#6;  |QBF eaDestino;
     |FINSI;

     |HAZ RecibeFicheroSinMD5;

     |GRABA 020#socm0005.a;
     |LIBERA #socm0005;
|FPRC;

|PROCESO Adjuntar1;
     aAlfa = aPathActas  + ("00000" + #socm0005#0) % -105;
     aAlfa = aAlfa       + ("0000"  + #socm0005#1) % -104;
     aAlfa = aAlfa       + ("0000"  + #socm0005#2) % -104;
     aAlfa = aAlfa       + "1." + #socm0005#5;  |QBF aAlfa;
     |XABRE "E", aAlfa, 0;
     |SI FSalida ] 0;
         |CONFI "0000NEl documento ya existe, quiere adjuntarlo de nuevo";
         |SI FSalida ! 0;
             |ACABA;
         |FINSI;
     |FINSI;

     nCampo = 5;  |HAZ Browse;
     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO Abrir1;
     aAlfa = aPathActas  + ("00000" + #socm0005#0) % -105;
     aAlfa = aAlfa       + ("0000"  + #socm0005#1) % -104;
     aAlfa = aAlfa       + ("0000"  + #socm0005#2) % -104;
     aAlfa = aAlfa       + "1." + #socm0005#5;  |QBF aAlfa;
     |XABRE "E", aAlfa, 0;
     |SI FSalida < 0;
         |MENAV "0000El documento no existe, adjunte el documento.";
         |ACABA;
     |FINSI;

     aFichero = ("00000" + #socm0005#0) % -105;
     aFichero = aFichero + ("0000"  + #socm0005#1) % -104;
     aFichero = aFichero + ("0000"  + #socm0005#2) % -104;
     aFichero = aFichero + "1." + #socm0005#5;  |QBF aFichero;

     |HAZ PresentaDocumento;
|FPRC;

|PROCESO Adjuntar2;
     aAlfa = aPathActas  + ("00000" + #socm0005#0) % -105;
     aAlfa = aAlfa       + ("0000"  + #socm0005#1) % -104;
     aAlfa = aAlfa       + ("0000"  + #socm0005#2) % -104;
     aAlfa = aAlfa       + "2." + #socm0005#6;  |QBF aAlfa;
     |XABRE "E", aAlfa, 0;
     |SI FSalida ] 0;
         |CONFI "0000NEl documento ya existe, quiere adjuntarlo de nuevo";
         |SI FSalida ! 0;
             |ACABA;
         |FINSI;
     |FINSI;

     nCampo = 6;  |HAZ Browse;
     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO Abrir2;
     aAlfa = aPathActas  + ("00000" + #socm0005#0) % -105;
     aAlfa = aAlfa       + ("0000"  + #socm0005#1) % -104;
     aAlfa = aAlfa       + ("0000"  + #socm0005#2) % -104;
     aAlfa = aAlfa       + "2." + #socm0005#6;  |QBF aAlfa;
     |XABRE "E", aAlfa, 0;
     |SI FSalida < 0;
         |MENAV "0000El documento no existe, adjunte el documento.";
         |ACABA;
     |FINSI;

     aFichero = ("00000" + #socm0005#0) % -105;
     aFichero = aFichero + ("0000"  + #socm0005#1) % -104;
     aFichero = aFichero + ("0000"  + #socm0005#2) % -104;
     aFichero = aFichero + "2." + #socm0005#6;  |QBF aFichero;

     |HAZ PresentaDocumento;
|FPRC;

|PROCESO EvntTree;
     aContenido = Contenido;  |QBF aContenido;

     |SI FEntrada = 0;  |ACABA;  |FINSI;

     |ESTADO_CONTROL nTree, "DISABLE";

     |SI aContenido = "Adjuntar1";  |HAZ Adjuntar1;  |FINSI;
     |SI aContenido = "Adjuntar2";  |HAZ Adjuntar2;  |FINSI;
     |SI aContenido = "Abrir1";     |HAZ Abrir1;     |FINSI;
     |SI aContenido = "Abrir2";     |HAZ Abrir2;     |FINSI;

     |ESTADO_CONTROL nTree, "ENABLE";
|FPRC;

|PROCESO GrabaArbol;
     aAlfa = aAlfa + &13 + &10;
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO ArbolDoc;
     aAbre = aPathLocal + "arbol.txt";
     |XABRE "WCB", aAbre, nHandle;

     aAlfa   = "1:Abrir documento acta sin firmar {{Abrir1}}";       |HAZ GrabaArbol;
     aAlfa   = "2:Abrir documento acta firmada {{Abrir2}}";           |HAZ GrabaArbol;
     aAlfa   = "3:Adjuntar documento acta sin firmar {{Adjuntar1}}";  |HAZ GrabaArbol;
     aAlfa   = "4:Adjuntar documento acta firmada {{Adjuntar2}}";     |HAZ GrabaArbol;

     |XCIERRA nHandle;

     |VENTANA 0501, 1550, -1, 16, "Procesos";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     aAlfa = "TREECTRL,Procesos {{" + aAbre + "}}";

     |CONTROL_SIMPLE 0, aAlfa, 0602, 1348, EvntTree;
     nTree = FSalida;

     aAlfa = aPathLocal + "arbol.txt";
     |RFBORRA aAlfa;

     sT_nID   = nTree;
     sT_nOper = 2;
     sT_aData = "1:";
     |ESPECIFICA "COMMANDCONTROL", sTree;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::{{001}}Salir," + nTree;
          |LEETECLA aTecla;
          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
          |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;

     |PULSATECLA;

     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO EventoGrid5;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#socm0005.=;

         |ESTADO_CONTROL nBtnMod, "ENABLE";
         |ESTADO_CONTROL nBtnBaj, "ENABLE";

         |PULSATECLA;
     |FINSI;

     |SI FSalida = 4;
         |HAZ ArbolDoc;
     |FINSI;
|FPRC;

|PROCESO Baja5;
     #socm0005#0 = #clientes@#0;
     #socm0005#1 = nDesdeAny;
     #socm0005#2 = 1;
|FPRC;

|PROCESO Alta5;
     #socm0005#0 = #clientes@#0;
     #socm0005#1 = nHastaAny;
     #socm0005#2 = 9999;
|FPRC;

|PROCESO EventoGridCli;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#clientes@.=;

         |ESTADO_CONTROL nBtnMod, "DISABLE";
         |ESTADO_CONTROL nBtnBaj, "DISABLE";

         |REFRESCACONTROL nGrid;
     |FINSI;

     |PULSATECLA;
|FPRC;

|PROCESO BtnsGrid1;
     |CONTROL_SIMPLE 0, "BOTON,{{193}}Alta ", 3464, 3474, Alta;
     nBtnAlt = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Modificar ", 3476, 3486, Modifica;
     nBtnMod = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{199}}Borrar", 3488, 3498, Baja;
     nBtnBaj = FSalida;

     |ESTADO_CONTROL nBtnMod, "DISABLE";
     |ESTADO_CONTROL nBtnBaj, "DISABLE";

     |CONTROL_SIMPLE 0, "LABEL,{{16}}Doble click en el registro para abrir o adjuntar documentos", 2102, 2180, NULL;
|FPRC;


|PROCESO MuestraActas;
     |VENTANA 0501, 3699, -1, 16, "Registros de actas";
     n0Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n0Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |HAZ BtnsGrid1;

     nRango = 4 + 8 + 16 + 32 + 128;
     |LINEAL_SIMPLE #1#socm0005, nRango, 2201, 3399, Baja5, Alta5, EventoGrid5;
     nGrid = FSalida;

     nRango = 4 + 8 + 16 + 32 + 128;
     |LINEAL_SIMPLE #1#clientes@, nRango, 0501, 1999, NULL, NULL, EventoGridCli;
     nGridCli = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::{{001}}Salir," + nGridCli;
          |LEETECLA aTecla;
          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
          |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n0Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n0Vent;

     |PULSATECLA;
|FPRC;

|| ---------------------------------------------------------------------------

|PROCESO agifigen;
     aAlfa = #socz0003#3;  |QBF aAlfa;
     |SI aAlfa ! "";
         |SI #socz0003#4 ! #agifigen#87;
             |ACABA;
         |FINSI;
     |FINSI;

     |SI #socz0003#5 = 1;
         #socm0005#0  = #agifigen#0;
         #socm0005#1  = #socz0003#2;
         #socm0005#2  = 1;
         |LEE 000#socm0005.];
         |SI FSalida ! 0;  |ACABA;  |FINSI;

         |SI #socm0005#0 ! #agifigen#0;
             |ACABA;
         |FINSI;

         |SI #socz0003#2 ! 0;
             |SI #socm0005#1 ! #socz0003#2;
                 |ACABA;
             |FINSI;
         |FINSI;
     |FINSI;

     |SI #socz0003#5 = 2;
         #socm0005#0  = #agifigen#0;
         #socm0005#1  = #socz0003#2;
         #socm0005#2  = 1;
         |LEE 000#socm0005.];
         |SI FSalida = 0;  |Y #socm0005#0 = #agifigen#0;
             |SI #socz0003#2 = 0;
                 |ACABA;
             |FINSI;

             |SI #socm0005#1 = #socz0003#2;
                 |ACABA;
             |FINSI;
         |FINSI;
     |FINSI;

     |PARA nCampo = 0;  |SI nCampo [ 105;  |HACIENDO nCampo = nCampo + 1;
           #clientes@#nCampo# = #agifigen#nCampo#;
     |SIGUE;

     |GRABA 020#clientes@.n;
     |LIBERA #clientes@;
|FPRC;

|DEFBUCLE agifigen;
     #agifigen#1;
     ;
     nDesdeEmp;
     nHastaEmp;
     ;
     NULL, agifigen;
|FIN;

|PROCESO Continuar;
     |REFERENCIA_DEF aGet1, #socz0003;
     |PARA nCampo = 0;  |SI nCampo [ 5;  |HACIENDO nCampo = nCampo + 1;
           |C_V #socz0003#nCampo#, 0, aAlfa;
           |SI aAlfa = "S";
               aGet2 = nCampo;
               IaGet = aGet1<-;
               |ESPECIFICA "GETDATACONTROL", aGet;
               #socz0003#nCampo# = FSalida;
           |FINSI;
     |SIGUE;

     |ABRE #clientes@;  |CIERRA #clientes@;  |DELINDEX #clientes@;

     |VENTANA 0501, 0540, -1, 16, "Procesando trabajo";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     nDesdeEmp = #socz0003#0;
     nHastaEmp = #socz0003#0;

     |SI #socz0003#0 = 0;
         nDesdeEmp = 1;
         nHastaEmp = 99999;
     |FINSI;

     nDesdeAny = #socz0003#2;
     nHastaAny = #socz0003#2;

     |SI #socz0003#2 = 0;
         nDesdeAny = 1;
         nHastaAny = 9999;
     |FINSI;

     aAlfa = #socz0003#3;  |QBF aAlfa;
     |SI #socz0003#0 = 0;  |Y aAlfa = "";  |Y  #socz0003#5 = "3";
         |DFICE aAlfa;
         aOrigen  = aAlfa + "agifigen.dat";
         aDestino = aAlfa + "clientes" + Usuario + ".dat";
         |COPIA_FICHERO aOrigen, aDestino;

         aOrigen  = aAlfa + "agifigen.ddx";
         aDestino = aAlfa + "clientes" + Usuario + ".ddx";
         |COPIA_FICHERO aOrigen, aDestino;

         aOrigen  = aAlfa + "agifigen.ixx";
         aDestino = aAlfa + "clientes" + Usuario + ".ixx";
         |COPIA_FICHERO aOrigen, aDestino;
     |SINO;
         |ABRE #socm0005;
         |ABRE #clientes@;
         |BUCLE agifigen;
         |CIERRA #socm0005;
         |CIERRA #clientes@;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;


     |ABRET #clientes@;
     |LEE 000#clientes@.p;
     |SI FSalida ! 0;
         |MENAV "0000Selecci¢n vac¡a.";

         |CIERRAT #clientes@;  |DELINDEX #clientes@;

         |POSICIONATE #socz0003#0;
         |ACABA;
     |FINSI;

     |HAZ MuestraActas;

     |CIERRAT #clientes@;  |DELINDEX #clientes@;

     |POSICIONATE #socz0003#0;
|FPRC;

|PROCESO Tipo12Fz3;  |TIPO 12;
     |SI FSalida = 7;  |ACABA;  |FINSI;

     |ERROR;
|FPRC;

|PROCESO Salir;
     |PTEC 807;
|FPRC;

|PROGRAMA;
     |HAZ BusquedaApli;

     |CLS;

     |PINPA #0#socz0003; nPanta = FSalida;
     |PINDA #0#socz0003;

     |PARA;  |SI;  |HACIENDO;
          |ENDATOS #1#socz0003;
          |SI FSalida ! 0;  |SAL;  |FINSI;
     |SIGUE;
|FPRO;

|PROCESO Tipo80Fz3;  |TIPO 80;
     |DBASE aBaseBasica;

     aPathActas = aBaseBasica + "documentos";        |MKDIR aPathActas;
     aPathActas = aBaseBasica + "documentos/actas";  |MKDIR aPathActas;
     aPathActas = aBaseBasica + "documentos/actas/";

     |HAZ LeeUnidadBasico;
     aPathLocal = eaUnidadBasico + "\MODELOS";        |RMKDIR aPathLocal;
     aPathLocal = eaUnidadBasico + "\MODELOS\ACTAS";  |RMKDIR aPathLocal;
     aPathLocal = eaUnidadBasico + "\MODELOS\ACTAS\";

     aQueQuiero  = "|NC";
     aNumCampos  = #socm0005#aQueQuiero#;
     enNumCampos = aNumCampos;
     enNumCampos = enNumCampos - 1;

     |DDEF aDef;
     aMas = aDef + "agifigen.mas";  |CARGA_DEF aMas, clientes@;

     aFichero = "clientes" + Usuario;  |NOME_DAT #clientes@#aFichero#;

     |ABRET #socm0005;

     FSalida = 3699;
|FPRC;

|PROCESO Tipo90Fz3;  |TIPO 90;
     |CIERRAT #socm0005;

     |DESCARGA_DEF #clientes@;
|FPRC;
