|FICHEROS;
     agiy0029 #0;
     agim0026 #26;
     agim0024 #24;

     agim0019 #19;
     agifigen #2;
|FIN;

|VARIABLES;
     Empresa  = 0;
     Registro = 0;
     &nRegis   = 0;
     swExiste = 0;

     nSwAlgun = 0;
     nCampo   = 0;
     nSw      = 0;
     nHay     = 0;
     aAlfa1   = "";
     &eSwFich = 0;
|FIN;

|| ************************************************************************
|CALCULO NoMasTipos; |TIPO 0;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |PINTA #0nCampo;
               |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO Tipo0C15; |TIPO 0;
     |SI #0#15 = "S";
         #0#2 = "00.00.0000"; |PINTA #0#2;
         #0#3 = "00.00.0000"; |PINTA #0#3;
         #0#16= "INFORME DE CARGOS INDEFINIDOS"; |PINTA #0#16;
         |PINTA 0936, "INDEFINIDO";
         |PINTA 0949, "INDEFINIDO";
         aAlfa1 = "N";
     |SINO;
         |SI #0#2 = "00.00.0000"; |PDEFECTO #0, 2, 3; |FINSI;
         |SI #0#3 = "00.00.0000"; |PDEFECTO #0, 3, 4; |FINSI;
         aAlfa1 = #0#16; |QBF aAlfa1;
         |SI aAlfa1 = ""; |O aAlfa1 = "INFORME DE CARGOS INDEFINIDOS";
             |PDEFECTO #0, 16, 17;
         |FINSI;
         |PINTA #0#2;
         |PINTA #0#3;
         |PINTA #0#16;
         aAlfa1 = "S";
     |FINSI;
     |C_M #0#2, 1, aAlfa1;
     |C_M #0#3, 1, aAlfa1;
|FCAL;
|| ************************************************************************

|PROCESO Imprime;
  |SI #26#15 < "01.01.1900"; #26#15 = "00.00.0000"; |FINSI;

  |SI #0#2 > #26#15; |O #0#3 < #26#15;
      |ACABA;
  |FINSI;

  eSwFich = 0;
  |PRINT;
  nSwAlgun = 1;
|FPRC;

|DEFBUCLE agim0026;
  #26#1;
  ;
  Empresa, nRegis, 00;
  Empresa, nRegis, 99;
  ;
  NULL, Imprime;
|FIN;

|PROCESO Imprime24;
  |SI #24#17 < "01.01.1900"; #24#17 = "00.00.0000"; |FINSI;

  |SI #0#2 > #24#17; |O #0#3 < #24#17;
      |ACABA;
  |FINSI;

  #26#0 = #24#0;
  |SI #24#15 = "R"; #26#17 = "REPRESENTANTE"; |FINSI;
  |SI #24#15 = "A"; #26#17 = "ADMINISTRADOR"; |FINSI;
  |SI #24#15 = "D"; #26#17 = "REPRES/ADMINIS."; |FINSI;
  eSwFich = 1;
  |PRINT;
  nSwAlgun = 1;
|FPRC;

|DEFBUCLE agim0024;
  #24#1;
  ;
  Empresa, nRegis, 00;
  Empresa, nRegis, 99;
  ;
  NULL, Imprime24;
|FIN;

|PROCESO LeeCliente;
  Empresa = #2#0;

 || Lee Ficha Sociedades
  swExiste = 0;
  nRegis   = 0;

  #19#0 = #2#0; || Empresa
  #19#1 = 99;
  |LEE 041#19];
  |SI FSalida = 0;
      |SI #19#0 ! #2#0;
          |LEE 041#19a;
          |SI FSalida = 0; |Y #19#0 = #2#0;
              swExiste = 1;
          |FINSI;
      |SINO;
          swExiste = 1;
      |FINSI;
  |FINSI;

  |SI swExiste = 1;
      nRegis = #19#1;  ||Registre

      |SI #0#14 ! "T";
          |SI #0#14 ! #19#54; swExiste = 0; |FINSI; ||Activa/Inactiva
      |FINSI;

      |SI nHay = 1;
          nSw  = 0;
          |PARA nCampo = 4; |SI nCampo [ 13; |HACIENDO nCampo = nCampo + 1;
                |SI #0nCampo ! 0; |Y #0nCampo = #19#4;
                    nSw = 1;
                |FINSI;
          |SIGUE;
          |SI nSw = 0; swExiste = 0; |FINSI;
      |FINSI;
  |FINSI;

  |SI swExiste = 1;
      |SI #0#17 ! "R"; |BUCLE agim0026; |FINSI;
      |SI #0#17 ! "C"; |BUCLE agim0024; |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE LeeCliente;
  #2#1;
  ;
  #0#0;
  #0#1;
  e;
  NULL, LeeCliente;
|FIN;

|PROCESO Tipo3;  |TIPO 3;
  |IMPRE 0;
  |SI FSalida ! 0;  |ACABA;  |FINSI;

  |INFOR "agiy0029";
  |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

  nHay = 0;
  |PARA nCampo = 4; |SI nCampo [ 13; |HACIENDO nCampo = nCampo + 1;
        |SI #0nCampo ! 0; nHay = 1; |FINSI;  ||si que hay seleccion
  |SIGUE;

  |ABRE #19;
  |BUCLE LeeCliente;
  |CIERRA #19;
  |SI nSwAlgun = 1;  |PIEINF;  |FINSI;

  |FININF;
  |FINIMP;
|FPRC;
