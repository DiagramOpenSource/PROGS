|FICHEROS;
     agiy0016 #0;        || Este Def
     agicen14 #2;        || Acceso Censo Lineas
     :/contagen/def/canempre #100;
|FIN;

|VARIABLES;
     || Esta variable indica cuando se llama a este proceso mediante un
     ||   SUB_EJECUTA si la impresora que se ha elegido es Crystal o no
     &eaImpresora = "";

     aAlfaX = "";
     nHandTxt = 0;

     informe = "";
     inform1 = "agiyn016";
     inform2 = "agiy0016";

     swAlgun     = 0;
     aAlfa1      = "";
     enEjercicio = 0;
     XPara       = 0;
     nDesdeFuera = 0;
     aAlfa       = "";

     &enEmpresa  = 0;
     &enEjer     = 0;
     &eaTituloL  = "";
     &eaSitua    = "";
     &swLinea    = 0;
     &eaDomicili = "";

     fDFecha = @;
     fHFecha = @;
|FIN;

|INCLUYE i_consul;
|INCLUYE i_masivo;

|FICHEROS;
     agifigen #1;     || Ficha de Clientes
|FIN;

|PROCESO Mayor; |TIPO 0;
  #0Campo = #0Campo > #0Campo;
  |PINTA #0Campo;
|FPRC;

|PROCESO SelecEmpre;
  |SI #0#2 = "S";
      |C_M #0#3, 1, "S";
      |C_M #0#4, 1, "S";
      |PARA XPara = 5; |SI XPara [ 24; |HACIENDO XPara = XPara + 1;
            |C_M #0XPara, 1, "N";  #0XPara = 0; |PINTA #0XPara;
      |SIGUE;
  |SINO;
      |C_M #0#3, 1, "N";  #0#3 =     1;  |PINTA #0#3;
      |C_M #0#4, 1, "N";  #0#4 = 99999;  |PINTA #0#4;
      |PARA XPara = 5; |SI XPara [ 24; |HACIENDO XPara = XPara + 1;
            |C_M #0XPara, 1, "S";
      |SIGUE;
  |FINSI;
|FPRC;

|PROCESO SelecTEmpre;
  |SI #0#25 = "S";
      |C_M #0#26, 1, "S";
      |C_M #0#27, 1, "S";
      |C_M #0#28, 1, "N";  #0#28 = 0;  |PINTA #0#28;
      |C_M #0#29, 1, "N";  #0#29 = 0;  |PINTA #0#29;
      |C_M #0#30, 1, "N";  #0#30 = 0;  |PINTA #0#30;
  |SINO;
      |C_M #0#26, 1, "N";  #0#26 = 00; |PINTA #0#26;
      |C_M #0#27, 1, "N";  #0#27 = 99; |PINTA #0#27;
      |C_M #0#28, 1, "S";
      |C_M #0#29, 1, "S";
      |C_M #0#30, 1, "S";
  |FINSI;
|FPRC;

|PROCESO SelecIVA;
  |SI #0#31 = "S";
      |C_M #0#32, 1, "S";
      |C_M #0#33, 1, "S";
      |C_M #0#34, 1, "N";  #0#34 = 0;  |PINTA #0#34;
      |C_M #0#35, 1, "N";  #0#35 = 0;  |PINTA #0#35;
      |C_M #0#36, 1, "N";  #0#36 = 0;  |PINTA #0#36;
  |SINO;
      |C_M #0#32, 1, "N";  #0#32 = 00; |PINTA #0#32;
      |C_M #0#33, 1, "N";  #0#33 = 99; |PINTA #0#33;
      |C_M #0#34, 1, "S";
      |C_M #0#35, 1, "S";
      |C_M #0#36, 1, "S";
  |FINSI;
|FPRC;

|PROCESO SelecTributa;
  |SI #0#37 = "S";
      |C_M #0#38, 1, "S";
      |C_M #0#39, 1, "S";
      |C_M #0#40, 1, "N";  #0#40 = 0;  |PINTA #0#40;
      |C_M #0#41, 1, "N";  #0#41 = 0;  |PINTA #0#41;
      |C_M #0#42, 1, "N";  #0#42 = 0;  |PINTA #0#42;
  |SINO;
      |C_M #0#38, 1, "N";  #0#38 = 00; |PINTA #0#38;
      |C_M #0#39, 1, "N";  #0#39 = 99; |PINTA #0#39;
      |C_M #0#40, 1, "S";
      |C_M #0#41, 1, "S";
      |C_M #0#42, 1, "S";
  |FINSI;
|FPRC;

|PROCESO SelecActivi;
  |SI #0#43 = "S";
      |C_M #0#44, 1, "S";
      |C_M #0#45, 1, "S";
      |C_M #0#46, 1, "N";  #0#46 = 0;  |PINTA #0#46;
      |C_M #0#47, 1, "N";  #0#47 = 0;  |PINTA #0#47;
      |C_M #0#48, 1, "N";  #0#48 = 0;  |PINTA #0#48;
  |SINO;
      |C_M #0#44, 1, "N";  #0#44 = 00; |PINTA #0#44;
      |C_M #0#45, 1, "N";  #0#45 = 99; |PINTA #0#45;
      |C_M #0#46, 1, "S";
      |C_M #0#47, 1, "S";
      |C_M #0#48, 1, "S";
  |FINSI;
|FPRC;

|PROCESO Imprime;
  |SI #0#31 = "S";
      |SI #2#8 < #0#32; |O #2#8 > #0#33; |ACABA; |FINSI;
  |SINO;
      |SI #2#8 ! #0#34; |Y #2#8 ! #0#35; |Y #2#8 ! #0#36;  |ACABA;  |FINSI;
  |FINSI;

  |SI #0#37 = "S";
      |SI #2#10 < #0#38; |O #2#10 > #0#39; |ACABA; |FINSI;
  |SINO;
      |SI #2#10 ! #0#40; |Y #2#10 ! #0#41; |Y #2#10 ! #0#42;  |ACABA;  |FINSI;
  |FINSI;

  |SI #0#43 = "S";
      |SI #2#12 < #0#44; |O #2#12 > #0#45; |ACABA; |FINSI;
  |SINO;
      |SI #2#12 ! #0#46; |Y #2#12 ! #0#47; |Y #2#12 ! #0#48;  |ACABA;  |FINSI;
  |FINSI;

  |SI #2#5 > fHFecha;                         |ACABA; |FINSI; || no es de este periodo
  |SI #2#6 < fDFecha; |Y #2#6 > "01.01.1900"; |ACABA; |FINSI; || no es de este periodo

  |SI nDesdeFuera = 0; |O eaImpresora ! "CrystalReport";
     |PRINT;
     |SI #0#61 = 2;   |PIEINF;  |FINSI;
  |SINO;
     |SI swLinea = 0; |O swLinea = 2;
        aAlfaX = "2" + &9 + #agicen14#1;
        aAlfaX = aAlfaX + &9 + #agicen14#2;
        aAlfaX = aAlfaX + &9 + #agicen14#3;
        aAlfaX = aAlfaX + &9 + #agicen14#4;
        aAlfaX = aAlfaX + &9 + #agicen14#5;
        |SI #agicen14#6 ! "01.01.0000";
           aAlfaX = aAlfaX + &9 + #agicen14#6;
        |SINO;
           aAlfaX = aAlfaX + &9 + "          ";
        |FINSI;
        aAlfaX = aAlfaX + &9 + #agicen14#12;
        aAlfaX = aAlfaX + &13 + &10;
        |XGRABA nHandTxt, aAlfaX;
     |FINSI;
     |SI swLinea = 1; |O swLinea = 3;
        aAlfaX = "3" + &9 + #agicen14#1;
        aAlfaX = aAlfaX + &9 + #agicen14#8;
        aAlfaX = aAlfaX + &9 + #agicen14#10;
        aAlfaX = aAlfaX + &9 + #agicen14#5;         || tique 516_454
        |SI #agicen14#6 ! "01.01.0000";
           aAlfaX = aAlfaX + &9 + #agicen14#6;
        |SINO;
           aAlfaX = aAlfaX + &9 + "          ";
        |FINSI;
        aAlfaX = aAlfaX + &9 + #agicen14#12;
        aAlfaX = aAlfaX + &13 + &10;
        |XGRABA nHandTxt, aAlfaX;
     |FINSI;
  |FINSI;
  swAlgun = 1;
  |SI swLinea < 2; swLinea = swLinea + 2; |FINSI;
|FPRC;

|DEFBUCLE agicen14;
  #2#1;
  ;
  #1#0, 00;
  #1#0, 99;
  ;
  NULL, Imprime;
|FIN;

|PROCESO FichaCliente;
||  |SI nDesdeFuera = 0;
      eaSitua    = "";
      eaDomicili = "";
      |ABRE #100;
      |SELECT #4#100;
      #100#2 = #1#0;         || Numero de Empresa
      #100#26 = enEjercicio; || A¤o
      |LEE 001#100=;
      |SI FSalida = 0;
          eaSitua = "ACTIVA";
          |SI #100#21 = "I"; eaSitua = "INACTIVA"; |FINSI;
      |FINSI;
      |SELECT #1#100;
      |CIERRA #100;
||  |FINSI;
    eaSitua = "ACTIVA"; |SI #1#94 > "01.01.1000"; eaSitua = "INACTIVA"; |FINSI;
    eaDomicili = #agifigen#4; |QBF eaDomicili;
    |SI eaDomicili ! "";
        eaDomicili = eaDomicili + ", " + #agifigen#5;
    |FINSI;

  |SI #0#25 = "S";
      |SI #1#87 < #0#26; |O #1#87 > #0#27; |ACABA; |FINSI;
  |SINO;
      |SI #1#87 ! #0#28; |Y #1#87 ! #0#29; |Y #1#87 ! #0#30;  |ACABA;  |FINSI;
  |FINSI;

  |SI nDesdeFuera ! 0; |Y eaImpresora = "CrystalReport";
     aAlfaX = "1" + &9 + #agifigen#13;
     aAlfaX = aAlfaX + &9 + #agifigen#0;
     aAlfaX = aAlfaX + &9 + #agifigen#1;
     aAlfaX = aAlfaX + &9 + #agifigen#3;
     aAlfaX = aAlfaX + &9 + eaDomicili;
     aAlfaX = aAlfaX + &9 + #agifigen#9;
     aAlfaX = aAlfaX + &9 + #agifigen#10;
     aAlfaX = aAlfaX + &9 + #agifigen#11;
     aAlfaX = aAlfaX + &9 + #agifigen#12;
     aAlfaX = aAlfaX + &9 + #agifigen#14;
     aAlfaX = aAlfaX + &9 + #agifigen#15;
     aAlfaX = aAlfaX + &9 + #agifigen#88;
     aAlfaX = aAlfaX + &9 + #agifigen#62;
     aAlfaX = aAlfaX + &13 + &10;
     |XGRABA nHandTxt, aAlfaX;
  |FINSI;
  swAlgun = 0;
  swLinea = 0;
  |BUCLE agicen14;
  |SI #0#61 = 1;
      swLinea = 1;
      |BUCLE agicen14;
      |SI nDesdeFuera = 0; |O eaImpresora ! "CrystalReport";
         |SI swAlgun = 1; |PIEINF; |FINSI;
      |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE agifigen;
  #1#1;
  ;
  #0#3;
  #0#4;
  ;
  NULL, FichaCliente;
|FIN;

|PROCESO Tipo3; |TIPO 3;
  aAlfa1 = #0#1; |QBF aAlfa1;
  aAlfa1 = aAlfa1 % -102;
  enEjercicio = aAlfa1;

  aAlfa1 = #0#1; aAlfa1 = "01.01." + aAlfa1; fDFecha = aAlfa1;
  aAlfa1 = #0#1; aAlfa1 = "31.12." + aAlfa1; fHFecha = aAlfa1;

  |SI nDesdeFuera = 0;
      |IMPRE 0;
      |SI FSalida ! 0; |ACABA; |FINSI;
  |FINSI;

  informe = inform1; |SI #0#61 = 2; informe = inform2; |FINSI;

  |SI nDesdeFuera ! 0; |Y eaImpresora = "CrystalReport";
     |DFICB aAlfaX; |QBF aAlfaX;
     aAlfaX = aAlfaX + "port" + Usuario + ".txt";
     |XABRE "WB", aAlfaX, nHandTxt;
     |SI FSalida ] 0;
        aAlfaX = "0" + &9 + #agiy0016#0; |QBF aAlfaX;
        aAlfaX = aAlfaX + &9 + #agiy0016#1;  |QBF aAlfaX;
        aAlfaX = aAlfaX + &13 + &10;
        |XGRABA nHandTxt, aAlfaX;
        |SI #0#2 = "S";
            |BUCLE agifigen;
        |SINO;
            |PARA XPara = 5; |SI XPara [ 24; |HACIENDO XPara = XPara + 1;
                  |SI #0XPara ! 0;
                      #0#3  = #0XPara;
                      #0#4  = #0XPara;
                      |BUCLE agifigen;
                  |FINSI;
            |SIGUE;
        |FINSI;
        |XCIERRA nHandTxt;
     |FINSI;
  |SINO;
     |INFOR informe;
     |SI FSalida ! 0;
         |SI nDesdeFuera = 0; |FINIMP; |FINSI;
         |ACABA;
     |FINSI;

     |SI #0#2 = "S";
         |BUCLE agifigen;
     |SINO;
         |PARA XPara = 5; |SI XPara [ 24; |HACIENDO XPara = XPara + 1;
               |SI #0XPara ! 0;
                   #0#3  = #0XPara;
                   #0#4  = #0XPara;
                   |BUCLE agifigen;
               |FINSI;
         |SIGUE;
     |FINSI;
     |FININF;
     |SI nDesdeFuera = 0; |FINIMP; |FINSI;
  |FINSI;

|FPRC;

|PROGRAMA;

 |SI enSwMvo = 1;
     enEmpresa   = enEmprM;
     enEjer      = enEjerM;
     eaTituloL   = "";
     eaImpresora = eaImMvo;
 |FINSI;
 |SI eaImpresora ! "";
     eaImpresora = eaImpresora % 113;
 |FINSI;

 nDesdeFuera = enEmpresa;
 |SI nDesdeFuera = 0;
     |MANTE #0;
 |SINO;
     #0#0  = eaTituloL;
     #0#1  = enEjer;
     #0#2  = "S";   #0#3  = nDesdeFuera;  #0#4  = nDesdeFuera;
     #0#25 = "S";   #0#26 = 00;         #0#27 = 99;
     #0#31 = "S";   #0#32 = 00;         #0#33 = 99;
     #0#37 = "S";   #0#38 = 00;         #0#39 = 99;
     #0#43 = "S";   #0#44 = 00;         #0#45 = 99;
     #0#61 = 1;
     |HAZ Tipo3;
 |FINSI;
|FPRO;
