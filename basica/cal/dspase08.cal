|FICHEROS;
     refer00@ #900;
     refer01@ #901;
     refer02@ #902;
     refer03@ #903;
|FIN;

|VARIABLES;
     nDig1           = 0;
     nDig3           = 0;
     nDig4           = 0;
     nDig5           = 0;
     nDig6           = 0;
     nDig7           = 0;
     nDig8           = 0;
     nDig9           = 0;
     nDigito         = 0;
     nSw             = 0;
     nLong           = 0;
     nPos            = 0;
     nCliente        = 0;
     aCuenta         = "";

     &aPathAnterior   = "";
     &aPathDefFiscal  = "";
     &aPathFichFiscal = "";
     &aPathDestino    = "";

     aDef            = "";
     aDirEmpresa     = "";
     aDirFichDestino = "";
     aDirFichOrigen  = "";
     aAlfa           = "";
     aAlfa1          = "";
     aAlfa2          = "";
     aAlfa3          = "";
     aAlfa4          = "";
     aAlfa5          = "";

     &enEmprEos      = 0;
     &enEmpresa      = 0;
     &enPerConta     = 0;
     nEstado         = 0;

     uno = "";
     dos = "";
     mascara = "";
     aQueQuiero = "";
     aNumCampos = "";
     nNumCampos = 0;
|FIN;


|PROCESO DefectoCta;
   |ABRE #903;
   uno = #902#0;  |QBF uno;               || quita los espacios blancos

   dos = uno + "000000000000";
   |SI #902#1 < 4;
       |VETE a_nivel_3;
   |FINSI;

   uno = dos % 104;
   #903#0 = uno;
   |LEE 040#903=;
   |SI FSalida = 0;
       |HAZ saca_defecto;
       |VETE findefecto_cuenta;
   |FINSI;

|ET a_nivel_3;
   |SI #902#1 < 3;
       |VETE a_nivel_2;
   |FINSI;
   uno = dos % 103;
   #903#0 = uno;
   |LEE 040#903=;
   |SI FSalida = 0;
       |HAZ saca_defecto;
       |VETE findefecto_cuenta;
   |FINSI;

|ET a_nivel_2;
   |SI #902#1 < 2;
       |VETE a_nivel_1;
   |FINSI;
   uno = dos % 102;
   #903#0 = uno;
   |LEE 040#903=;
   |SI FSalida = 0;
       |HAZ saca_defecto;
       |VETE findefecto_cuenta;
   |FINSI;

|ET a_nivel_1;
   uno = dos % 101;
   #903#0 = uno;
   |LEE 040#903=;
   |SI FSalida = 0;
       |HAZ saca_defecto;
   |FINSI;

|ET findefecto_cuenta;
   mascara = #902#0;
   |QBF mascara;
   mascara = mascara +"zzzzzzzzzzzz";
   mascara = mascara % 112;
   #902#54 = mascara;
   #902#55 = #902#1;
   |CIERRA #903;
|FPRC;

|RUTINA saca_defecto;
   #902#4 = #903#1;
   #902#5 = #903#2;
   #902#6 = #903#3;
   #902#7 = #903#4;
   #902#8 = #903#5;
|FRUT;

|PROCESO CalculaCuenta;
     aAlfa1  = aCuenta;  |QBF aAlfa1;
     aAlfa1  = aAlfa1 % 0;
     nLong   = aAlfa1;
     nSw     = 0;
     nDigito = 0;
     |SI nLong = nDig4; |Y nDig4 ! 0; nSw = 1; nDigito = 1; |FINSI;
     |SI nLong = nDig5; |Y nDig5 ! 0; nSw = 1; nDigito = 2; |FINSI;
     |SI nLong = nDig6; |Y nDig6 ! 0; nSw = 1; nDigito = 3; |FINSI;
     |SI nLong = nDig7; |Y nDig7 ! 0; nSw = 1; nDigito = 4; |FINSI;
     |SI nLong = nDig8; |Y nDig8 ! 0; nSw = 1; nDigito = 5; |FINSI;
     |SI nLong = nDig9; |Y nDig9 ! 0; nSw = 1; nDigito = 6; |FINSI;
     |SI nSw = 0;  |Y nLong ! 0;
         |SI nDig4 ! 0; nDigito = 1; nLong = nDig4;  |FINSI;
         |SI nDig5 ! 0; nDigito = 2; nLong = nDig5;  |FINSI;
         |SI nDig6 ! 0; nDigito = 3; nLong = nDig6;  |FINSI;
         |SI nDig7 ! 0; nDigito = 4; nLong = nDig7;  |FINSI;
         |SI nDig8 ! 0; nDigito = 5; nLong = nDig8;  |FINSI;
         |SI nDig9 ! 0; nDigito = 6; nLong = nDig9;  |FINSI;

         aAlfa   = aCuenta;  |QBF aAlfa;
         nPos    = 100 + nLong;
         aCuenta = (aAlfa + "000000000000") % nPos;
     |FINSI;
|FPRC;

|PROCESO CuentaConAste;
     aAlfa1  = aCuenta;  |QBF aAlfa1;
     aAlfa2  = aAlfa1 % -101;
     aAlfa3  = aAlfa1 % -212;
     |SI aAlfa2 = "*";
         |SI nDig4 ! 0; nDigito = 1; nLong = nDig4;  |FINSI;
         |SI nDig5 ! 0; nDigito = 2; nLong = nDig5;  |FINSI;
         |SI nDig6 ! 0; nDigito = 3; nLong = nDig6;  |FINSI;
         |SI nDig7 ! 0; nDigito = 4; nLong = nDig7;  |FINSI;
         |SI nDig8 ! 0; nDigito = 5; nLong = nDig8;  |FINSI;
         |SI nDig9 ! 0; nDigito = 6; nLong = nDig9;  |FINSI;

         nPos    = 100 + (nLong - 5);
         aCuenta = ((aAlfa3 + "000000000000") % nPos) + (("00000" + nCliente) % -105);
     |SINO;
         |HAZ CalculaCuenta;
     |FINSI;
|FPRC;

|PROCESO AltaCuenta;

 |ABRE #902;
 #902#0 = #900#10;
 |LEE 040#902=;       ||Y Existe
 |SI FSalida ! 0;
     |DDEFECTO #902;
      #902#0 = #900#10;
      #902#1 = #900#11;
      #902#2 = #900#1;
      |SI #902#1 = nDig3; #902#3 = "S"; |FINSI;

      |HAZ DefectoCta;
      |GRABA 020#902b;
 |FINSI;
 #902#2 = #900#1;
 |GRABA 020#902a;
 |LIBERA #902;

 |CIERRA #902;
|FPRC;

|PROCESO CreaCliPro;

 |SELECT #4#900;

 #900#9 = #901#3;
 |LEE 040#900=;
 nEstado = FSalida;

 |SELECT #1#900;
 |SI nEstado ! 0;

     |DDEFECTO #900;
     #900#0 = #901#1; nCliente = #900#0;
     #900#1 = #901#4;                     || Nombre

     aAlfa1 = #901#5; |QBF aAlfa1;
     |SI aAlfa1 ! ""; aAlfa1 = aAlfa1 + " "; |FINSI;
     aAlfa2 = #901#6; |QBF aAlfa2;
     aAlfa3 = #901#7; |QBF aAlfa3;
     |SI aAlfa3 ! ""; aAlfa3 = ", " + aAlfa3; |FINSI;
     aAlfa4 = #901#8; |QBF aAlfa4;
     |SI aAlfa4 ! ""; aAlfa4 = " " + aAlfa4; |FINSI;
     aAlfa5 = #901#8; |QBF aAlfa5;
     |SI aAlfa5 ! ""; aAlfa5 = " " + aAlfa5; |FINSI;
     aAlfa  = aAlfa1 + aAlfa2 + aAlfa3 + aAlfa4 + aAlfa5;
     #900#2 = aAlfa;                                       ||Direccion
     #900#3 = #901#10;                                     || C.P 1
     #900#4 = #901#11;                                     || C.P.2
     #900#5 = #901#12;                                     || Provincia
     #900#6 = #901#13;                                     || Poblacion
     #900#9 = #901#3;                                      || C.I.F.
     |SI #901#2 = 1;
         #900#23 = "C";
         aCuenta = "4300*";
     |SINO;
         #900#23 = "P";
         aCuenta = "4000*";
     |FINSI;
     |HAZ CuentaConAste;
     #900#10 = aCuenta;
     #900#11 = nDigito;
     |SI nNumCampos ] 23; #900#24 = #901#23; |FINSI;
     |SI #900#24 = "E  "; #900#24 = "ES "; |FINSI;
     |SALVA_DATOS #900;
     |LEE 040#900=;
     |SI FSalida ! 0;
         |REPON_DATOS #900;
         |GRABA 020#900n;
     |FINSI;
     |HAZ AltaCuenta;
 |FINSI;
|FPRC;

|DEFBUCLE LeeCliPro;
     #901#1003;
     ;
     enEmprEos, 1, 00001;
     enEmprEos, 2, 99999;
     e;
     NULL, CreaCliPro;
|FIN;

|PROGRAMA;

     aDirFichDestino = aPathDestino + "fich/";
     aDef            = aPathDestino + "def/canempre";
     |CARGA_DEF aDef, refer00@;
     |SI FSalida ! 0;
         |MENAV "     Error en Cargar el Def canempre.mas";
         |ACABA;
     |FINSI;
     |PATH_DAT #900 aDirFichDestino;

     |ABRE #900;
     #900#2 = enEmpresa;
     #900#3 = enPerConta;
     |LEE 040#900=;
     |SI FSalida ! 0;
         |CIERRA #900;
         |ACABA;
     |FINSI;
     |CIERRA #900;

     nDig1 = #900#11;       || desde mes
     nDig3 = #900#13;       || nro. niveles
     nDig4 = #900#14;       || 1 nivel
     nDig5 = #900#15;       || 2 nivel
     nDig6 = #900#16;       || 3 nivel
     nDig7 = #900#17;       || 4 nivel
     nDig8 = #900#18;       || 5 nivel
     nDig9 = #900#19;       || 6 nivel

     |DESCARGA_DEF #refer00@;

     aAlfa1 = enEmpresa;  |QBF aAlfa1;
     aAlfa2 = enPerConta; |QBF aAlfa2;
     aDirEmpresa     = (("00000" + aAlfa1) % -105) + aAlfa2 + "/";
     aDirFichDestino = aPathDestino + "fich/" + aDirEmpresa;

     aDef            = aPathDestino + "def/caclipro";
     |CARGA_DEF aDef, refer00@;
     |SI FSalida ! 0;
         |MENAV "     Error en Cargar el Def caclipro.mas";
         |ACABA;
     |FINSI;

     aDef            = aPathDestino + "def/cacuenta";
     |CARGA_DEF aDef, refer02@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #refer00@;
         |MENAV "     Error en Cargar el Def cacuenta.mas";
         |ACABA;
     |FINSI;

     aDef            = aPathDestino + "def/cacuebal";
     |CARGA_DEF aDef, refer03@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #refer02@;
         |DESCARGA_DEF #refer00@;
         |MENAV "     Error en Cargar el Def cacuebal.mas";
         |ACABA;
     |FINSI;

     |PATH_DAT #900 aDirFichDestino;
     |PATH_DAT #902 aDirFichDestino;
     |PATH_DAT #903 aDirFichDestino;

     aDef = aPathDefFiscal + "eosclpre.mas";
     |CARGA_DEF aDef, refer01@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #refer03@;
         |DESCARGA_DEF #refer02@;
         |DESCARGA_DEF #refer00@;
         |MENAV "     Error en Cargar el Def eosclpre.mas";
         |ACABA;
     |FINSI;

     |PATH_DAT #901 aPathFichFiscal;
     aQueQuiero = "|NC";
     aNumCampos = #refer01@#aQueQuiero#;
     nNumCampos = aNumCampos;
     nNumCampos = nNumCampos - 1;

     |ABRE #900;
     |BUCLE LeeCliPro;
     |CIERRA #900;

     |DESCARGA_DEF #refer01@;
     |DESCARGA_DEF #refer03@;
     |DESCARGA_DEF #refer02@;
     |DESCARGA_DEF #refer00@;
|FPRO;
