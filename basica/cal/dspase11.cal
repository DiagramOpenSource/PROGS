|FICHEROS;
     dspase00 #0;
     dspase11 #11;
     dspase15 #15;

     refer00@ #900;     || labempre
     refer01@ #901;     || labtraba
     refer02@ #902;     || eosm0032
     refer03@ #903;     || nomfusli

     :/basica/def/zmonedas #200;
     :/integral/def/dsam0003 #3;
     :/integral/def/dsam0004 #4;

     :/modelos/def/dsmom105 #105;
|FIN;

|VARIABLES;
     nEmpresa          = 0;
     nTBases           = 0;
     nTReten           = 0;
     nTEspba           = 0;
     nTEspre           = 0;
     nTEsprr           = 0;
     nTSegur           = 0;
     nCodCon           = 0;
     nCodigoNue        = 0;
     nCodigoLab        = 0;
     nEstaCambio       = 0;
     aTipCon           = "";
     aNomCon           = "";

     aPathAnterior     = "";
     aPathDefLaboral   = "";
     aPathFichLaboral  = "";
     aDef              = "";
     aMonedaLaboral    = "";

     &eaPathLaboral  = "";
     &eaPathDsmodelo = "";
     &eaPathAgitool  = "";
|FIN;

|PROCESO GrabaTemporal;
     |SI nTBases ! 0;|O nTReten ! 0;|O nTSegur ! 0;|O nTEspba ! 0;
      |O nTEspre ! 0;|O nTEsprr ! 0;
          |DDEFECTO #105;
          #105#0 = #901#7;      || D.N.I.
          #105#1 = nCodigoNue;  || Codigo
          |LEE 101#105=;
          |SI FSalida ! 0;
              |DDEFECTO #105;
              #105#0 = #901#7;      || D.N.I.
              #105#1 = nCodigoNue;  || Codigo
              |GRABA 020#105b;
          |SINO;
              |LIBERA #105;
              |ACABA;
          |FINSI;

          #105#2  = #901#1;         || trabajador
          #105#3  = nTBases;        || total bases
          #105#4  = nTReten;        || total retenciones
          #105#5  = nTSegur;        || total seg.social
          #105#6  = nTEspba;        || total bases en especie
          #105#7  = nTEspre;        || total retenciones en especie
          #105#8  = #901#2;         || nombre
          #105#9  = nCodCon;        || codigo contribuyente
          #105#10 = aTipCon;        || titular
          #105#11 = nTEsprr;        || total retenciones en especie repercutidos
          #105#12 = aNomCon;        || Nombre contribuyente
          #105#13 = #900#2;         || Nombre Entidad Pagadora
          #105#14 = #11#0;
          |GRABA 020#105a;
          |LIBERA #105;
     |FINSI;
|FPRC;

|PROCESO AcumIRPF;
     |SI #902#3 ! "A"; |Y #902#3 ! "B"; |Y #902#3 ! "E"; |Y #902#3 ! "F";
          |ACABA;
     |FINSI;   ||

     nTBases = nTBases + #902#7;     || base dineraria
     nTReten = nTReten + #902#9;     || retencion dineraria
     nTEspba = nTEspba + #902#10;    || base en especie
     nTEspre = nTEspre + #902#12;    || retencion en especie
     |SI #902#13 = "S";
          nTEsprr = nTEsprr + #902#12;    || retencion en especie reperc.
     |FINSI;
     nTSegur = nTSegur + #902#15;    || gtos. seg. social
|FPRC;

|DEFBUCLE eosm0032;
     #902#1007;
     ;
     nEmpresa, #11#0, #901#7, " ", "  ", 0,    1;
     nEmpresa, #11#0, #901#7, "z", "zz", 9, 9999;
     ;
     NULL, AcumIRPF;
|FIN;


|PROCESO MiraTrabajador;
     nEmpresa = #900#0;

     |SELECT #2#903;
     #903#1 = #900#0;
     #903#0 = 0;
     |LEE 000#903];
     |SI FSalida = 0;  |Y #903#1 = #900#0;
          nEmpresa = #903#0;
     |FINSI;

     nTBases = 0;
     nTReten = 0;
     nTEspba = 0;
     nTEspre = 0;
     nTEsprr = 0;
     nTSegur = 0;

     |BUCLE eosm0032;

     nCodCon = 0;
     aTipCon = "T";
     aNomCon = "";
     |SELECT #2#3;
     #3#1 = #901#7;
     |LEE 000#3=;               || lee por DNI
     |SI FSalida = 0;
          nCodCon = #3#0;
          aNomCon = #3#5;
     |SINO;
          |SELECT #3#3;
          #3#35 = #901#7;
          |LEE 000#3=;
          |SI FSalida = 0;
               nCodCon = #3#0;
               aTipCon = "C";
               aNomCon = #3#5;
          |SINO;
               |SELECT #2#4;
               #4#2 = #901#7;
               |LEE 000#4=;
               |SI FSalida = 0;
                   nCodCon = #4#0;
                   aTipCon = #4#1;
                   aNomCon = #3#5;
               |FINSI;
          |FINSI;
     |FINSI;

     |HAZ GrabaTemporal;
|FPRC;

|DEFBUCLE labtraba;
     #901#2001;
     0;
     "            ", #900#0;
     "zzzzzzzzzzzz", #900#0;
     ;
     NULL, MiraTrabajador;
|FIN;

|PROCESO CogeCodigo;
     |SI #15#22 = "S";  |ACABA;  |FINSI;

     nCodigoNue = #15#3;
     |ERROR10;
|FPRC;

|DEFBUCLE dspase15;
     #15#6;
     ;
     nCodigoLab, "            ", 000;
     nCodigoLab, "zzzzzzzzzzzz", 999;
     ;
     NULL, CogeCodigo;
|FIN;

|PROCESO Trabajadores;
     |SI nEstaCambio = 1;
         nCodigoNue = 0;
         nCodigoLab = #900#0;
         |BUCLE dspase15;
         |SI nCodigoNue = 0;  |ACABA;  |FINSI;
     |SINO;
         nCodigoNue = #900#0;
     |FINSI;

     |BUCLE labtraba;
|FPRC;

|DEFBUCLE labempre;
     #900#1001;
     ;
     00001;
     99999;
     ;
     NULL, Trabajadores;
|FIN;

|PROCESO Tipo3;  |TIPO 3;
     aDef = aPathDefLaboral + "labempre.mas";
     |CARGA_DEF aDef, refer00@;
     |SI FSalida ! 0;
         |MENAV "     Error en Cargar el Def labempre.mas";
         |ACABA;
     |FINSI;

     aDef = aPathDefLaboral + "labtraba.mas";
     |CARGA_DEF aDef, refer01@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #refer00@;
         |MENAV "     Error en Cargar el Def labtraba.mas";
         |ACABA;
     |FINSI;

     aDef = aPathDefLaboral + "eosm0032.mas";
     |CARGA_DEF aDef, refer02@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #refer01@;
         |DESCARGA_DEF #refer00@;
         |MENAV "     Error en Cargar el Def eosm0032.mas";
         |ACABA;
     |FINSI;

     aDef = aPathDefLaboral + "nomfusli.mas";
     |CARGA_DEF aDef, refer03@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #refer02@;
         |DESCARGA_DEF #refer01@;
         |DESCARGA_DEF #refer00@;
         |MENAV "     Error en Cargar el Def 903.mas";
         |ACABA;
     |FINSI;

     |PATH_DAT #900 aPathFichLaboral;
     |PATH_DAT #901 aPathFichLaboral;
     |PATH_DAT #902 aPathFichLaboral;
     |PATH_DAT #903 aPathFichLaboral;

     |ABRE #105;  |CIERRA #105;  |DELINDEX #105;

     |ABRE #105;
     |ABRE #903;
     |ABRE #3;
     |BUCLE labempre;
     |CIERRA #903;
     |CIERRA #105;
     |CIERRA #3;

     |DESCARGA_DEF #refer03@;
     |DESCARGA_DEF #refer02@;
     |DESCARGA_DEF #refer01@;
     |DESCARGA_DEF #refer00@;
|FPRC;

|PROGRAMA;
     |ABRE #0;
     |LEE 040#0p;
     |SI FSalida ! 0;
         |MENAV "     Pase de Ficheros Generales no Realizado";
         |CIERRA #0;
         |ACABA;
     |FINSI;
     |CIERRA #0;

     |ABRE #15;
     |LEE 040#15p;
     |SI FSalida ! 0;
         nEstaCambio = 0;
         |DDEFECTO #15;
     |SINO;
         nEstaCambio = 1;
         |SI #15#15 ! "S";
             |MENAV "      Unificacion de Codigos no Realizada.";
             |CIERRA #15;
             |ACABA;
         |FINSI;
     |FINSI;
     |CIERRA #15;

     |HAZ BusquedaApli;
     eaPathDsmodelo   = eaPathDsmodelo + "fich/";

     aPathAnterior    = #0#0;  |QBT aPathAnterior;

     aPathDefLaboral  = aPathAnterior + "aginom/def/";
     aPathFichLaboral = aPathAnterior + "aginom/fich/";

     aDef = eaPathAgitool + "def/zmonedas.mas";
     |CARGA_DEF aDef, refer00@;
     |SI FSalida ! 0;
         aMonedaLaboral = "P";
     |SINO;
         |PATH_DAT #900 aPathFichLaboral;
         |ABRE #900;
         |LEE 040#900p;
         |SI FSalida ! 0;  |DDEFECTO #900;  |FINSI;
         |CIERRA #900;

         aMonedaLaboral = #900#1;

         |DESCARGA_DEF #refer00@;
     |FINSI;

     |ABRE #200;
     |LEE 040#200p;
     |SI FSalida ! 0;  |DDEFECTO #200;  |FINSI;
     |CIERRA #200;

     |SI aMonedaLaboral ! #200#9;
         |MENAV "    Moneda Gestion de Laboral anterior distinta a la de esta Gestion.";
         |ACABA;
     |FINSI;

     |PINPA #0#11;
     |PINDA #0#11;
     |ENDATOS #1#11;
|FPRO;
