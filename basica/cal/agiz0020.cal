|FICHEROS;
     agiz0020 #0;
     agifigen #1;
     agicen14 #114;
     agim0002 #102;

     :/modelos/def/dsmom001 #11;

     rem01011@ #211;
     rem00025@ #225;
     rem00026@ #226;
     rem00027@ #227;
     rem00028@ #228;

     :/contasoc/def/isocie65 #165;
     :/contasoc/def/isom0003 #163;  || Balance de Resultados
|FIN;

|VARIABLES;
     aBase      = "";
     aPath      = "";
     aPathS     = "";
     aDef       = "";
     aMas       = "";
     aAlfa      = "";

     nCampo     = 0;
     nIngreso   = 0;
     nGasto     = 0;
     nConsumo   = 0;
     nEmpresa   = 0;
     nActivid   = 0;

     aCasilla   = "";
     nValor     = 0;
|FIN;

|PROCESO GrabaRendimiento;
     |ABRE #102;
     #102#0 = nEmpresa;
     #102#1 = nActivid;
     #102#2 = #0#0;
     |LEE 101#102=;
     |SI FSalida ! 0;
         |DDEFECTO #102;
         #102#0  = nEmpresa;
         #102#1  = nActivid;
         #102#2  = #0#0;
         #102#3  = #1#1;
         #102#4  = #114#2;
         #102#5  = #114#3;
         #102#6  = #114#4;
         #102#7  = #114#9;
         #102#8  = #114#10;
         #102#9  = #114#7;
         #102#10 = #114#8;
         #102#11 = #114#13;
         #102#12 = 1;
         #102#13 = nIngreso;
         #102#15 = nConsumo;
         #102#16 = nGasto;

         #102#14 = #102#13 - #102#15 - #102#16;
         #102#17 = (#102#14 * 100) / #102#13;
         #102#18 = (#102#15 * 100) / #102#13;
         #102#19 = (#102#16 * 100) / #102#13;
         #102#20 = ((#102#15 + #102#16) * 100) / #102#13;
         #102#21 = (#102#15 * 100) / (#102#15 + #102#16);
         #102#22 = (#102#16 * 100) / (#102#15 + #102#16);
         #102#23 = 100 - (((#102#13 - #102#15) * 100) / #102#13);
         #102#24 = #102#21 + #102#22 + #102#23;
         #102#25 = 12;

         |GRABA 020#102n;
     |FINSI;
     |LIBERA #102;
     |CIERRA #102;
|FPRC;

|PROCESO MiraComunero;
     |SI #11#8 ! "01.01.0000";
         |SI #11#8 > "31.12.2001";  |ACABA;  |FINSI;
     |FINSI;

     |SI #11#9 ! "01.01.0000";
         |SI #11#9 < "01.01.2001";  |ACABA;  |FINSI;
     |FINSI;

     nEmpresa = #11#0;
     nActivid = #11#1;
|FPRC;

|DEFBUCLE dsmom001;
     #11#2;
     ;
     #114#0, #114#1;
     #114#0, #114#1;
     ;
     NULL, MiraComunero;
|FIN;

|| ... Para repasar por Manolo, ha cambiado el fichero rem01011
|| ... del ejercicio 2007. Ver si el calculo de Ingresos y gastos es correcto.


|PROCESO PasaRendi011_2007;
     |SI #114#19 = "T";  |Y #211#2 ! "TITULAR   ";
         |ACABA;
     |FINSI;

     |SI #114#19 = "C";  |Y #211#2 ! "CONYUGE   ";
         |ACABA;
     |FINSI;

     |SI #211#13 = 0;  |Y #211#14 = 0; |Y #211#15 = 0; |Y #211#16 = 0;
         |ACABA;
     |FINSI;

     nIngreso = #211#13;
     nConsumo = 0;
     nGasto   = #211#14 + #211#15 + #211#16;

     |HAZ GrabaRendimiento;
|FPRC;

|DEFBUCLE rem01011_2007;
     #211#1004;
     ;
     #114#18, 00, "          ", 001;
     #114#18, 99, "zzzzzzzzzz", 999;
     ;
     NULL, PasaRendi011_2007;
|FIN;

|PROCESO PasaRendi011;
     |SI #114#19 = "T";  |Y #211#2 ! "TITULAR   ";
         |ACABA;
     |FINSI;

     |SI #114#19 = "C";  |Y #211#2 ! "CONYUGE   ";
         |ACABA;
     |FINSI;

     |SI #211#5 = 0;  |Y #211#10 = 0;
         |ACABA;
     |FINSI;

     nIngreso = #211#5;
     nConsumo = 0;
     nGasto   = #211#10;

     |HAZ GrabaRendimiento;
|FPRC;

|DEFBUCLE rem01011;
     #211#1003;
     ;
     #114#18, 00, "          ";
     #114#18, 99, "zzzzzzzzzz";
     ;
     NULL, PasaRendi011;
|FIN;

|PROCESO PasaRendi25;
     |SI #225#19 = "    ";  #225#19 = "9999";  |FINSI;

     |SI #225#19 = #114#3;
         nIngreso = #225#29;
         nConsumo = #225#30;
         nGasto   = #225#44 - #225#30;

         |HAZ GrabaRendimiento;
     |FINSI;
|FPRC;

|DEFBUCLE rem00025;
     #225#1002;
     ;
     #114#18, 01;
     #114#18, 99;
     ;
     NULL, PasaRendi25;
|FIN;

|PROCESO PasaRendi26;
     |SI #226#19 = "    ";  #226#19 = "9999";  |FINSI;

     |SI #226#19 = #114#3;
         nIngreso = #226#29;
         nConsumo = #226#30;
         nGasto   = #226#42 - #226#30 + #226#44;

         |HAZ GrabaRendimiento;
     |FINSI;
|FPRC;

|DEFBUCLE rem00026;
     #226#1002;
     ;
     #114#18, 01;
     #114#18, 99;
     ;
     NULL, PasaRendi26;
|FIN;

|PROCESO PasaRendi27;
     |SI #227#17 = "    ";  #227#17 = "9999";  |FINSI;

     |SI #227#17 = #114#3;
         nIngreso = #227#117;
         nConsumo = 0;
         nGasto   = 0;

         |HAZ GrabaRendimiento;
     |FINSI;
|FPRC;

|DEFBUCLE rem00027;
     #227#1002;
     ;
     #114#18, 01;
     #114#18, 99;
     ;
     NULL, PasaRendi27;
|FIN;

|PROCESO PasaRendi28;
     |SI #228#19 = "    ";  #228#19 = "9999";  |FINSI;

     |SI #228#19 = #114#3;
         nIngreso = #228#52;
         nConsumo = 0;
         nGasto   = 0;

         |HAZ GrabaRendimiento;
     |FINSI;
|FPRC;

|DEFBUCLE rem00028;
     #228#1002;
     ;
     #114#18, 01;
     #114#18, 99;
     ;
     NULL, PasaRendi28;
|FIN;

|PROCESO CogeRenta;
     nEmpresa = #114#0;
     nActivid = #114#1;
     |SI #114#14 = "S";
         nEmpresa = 0;
         |BUCLE dsmom001;
     |FINSI;

     |SI nEmpresa = 0;  |ACABA;  |FINSI;

     |SI #114#3 = "8612";   || comprobar si es correcto 1243_1493
         |SI #0#0 < 2007;
             |BUCLE rem01011;
         |SINO;
             |BUCLE rem01011_2007;
         |FINSI;
     |SINO;
         |BUCLE rem00025;
         |BUCLE rem00026;
         |BUCLE rem00027;
         |BUCLE rem00028;
     |FINSI;
|FPRC;

|DEFBUCLE agicen14R;
     #114#3;
     ;
     00001, #1#0;
     99999, #1#0;
     ;
     NULL, CogeRenta;
|FIN;

|PROCESO CogeSociedad;
     aAlfa = #114#6 % -104;
     |SI aAlfa ! "0000";
         |SI #114#6 < "01.01.2001";  |ACABA;  |FINSI;
     |FINSI;

     nEmpresa = #114#0;
     nActivid = #114#1;
     |HAZ GrabaRendimiento;

     |ERROR10;
|FPRC;

|DEFBUCLE agicen14S;
     #114#1;
     9;
     #1#0, 01, 02;
     #1#0, 99, 02;
     ;
     NULL, CogeSociedad;
|FIN;

|PROCESO LeeCas103;
     #163#0  = #1#0;
     #163#1  = #0#0;
     #163#3  = aCasilla;
     |LEE 040#163=;
     |SI FSalida ! 0; |DDEFECTO #163; |FINSI;

     |SI #163#6 ! "S"; #163#5 = 0; #163#7 = " "; |FINSI;
|FPRC;

|PROCESO LeeEmpresa;
     |BUCLE agicen14R;

     |SI #0#0 < 2007;
         |ABRE #165;
         #165#0 = #1#0;
         #165#1 = #0#0;
         |LEE 040#165=;
         |SI FSalida = 0;
             nIngreso = #165#166 + #165#167 + #165#168 + #165#172 + #165#176;
             nIngreso = nIngreso + #165#180 + #165#185 + #165#186 + #165#187;
             nIngreso = nIngreso + #165#188 + #165#189 + #165#190 + #165#191;
             nConsumo = #165#129 + #165#133;
             nGasto   = #165#136 + #165#137 + #165#141 + #165#146 + #165#151;
             nGasto   = nGasto   + #165#152 + #165#153 + #165#154 + #165#155;
             nGasto   = nGasto   + #165#156 + #165#157 + #165#158 + #165#159;
             nGasto   = nGasto   + #165#160;

            |BUCLE agicen14S;
         |FINSI;
         |CIERRA #165;
     |SINO;

         |ABRE #163;
         |DDEFECTO #163;
         #163#0  = #1#0;
         #163#1  = #0#0;
         #163#2  = 1;
         |LEE 040#163];
         |SI FSalida = 0; |Y #163#0 = #1#0; |Y #163#1 = #0#0;
             |SELECT #2#163;

             nIngreso = 0;
             aCasilla = "255"; |HAZ LeeCas103; nIngreso = nIngreso + #163#5;
             aCasilla = "259"; |HAZ LeeCas103; nIngreso = nIngreso + #163#5;
             aCasilla = "265"; |HAZ LeeCas103; nIngreso = nIngreso + #163#5;
             aCasilla = "285"; |HAZ LeeCas103; nIngreso = nIngreso + #163#5;
             aCasilla = "286"; |HAZ LeeCas103; nIngreso = nIngreso + #163#5;
             aCasilla = "290"; |HAZ LeeCas103; nIngreso = nIngreso + #163#5;
             aCasilla = "292"; |HAZ LeeCas103; nIngreso = nIngreso + #163#5;
             aCasilla = "294"; |HAZ LeeCas103; nIngreso = nIngreso + #163#5;
             aCasilla = "297"; |HAZ LeeCas103; nIngreso = nIngreso + #163#5;
             aCasilla = "317"; |HAZ LeeCas103; nIngreso = nIngreso + #163#5;
             aCasilla = "318"; |HAZ LeeCas103; nIngreso = nIngreso + #163#5;
             aCasilla = "320"; |HAZ LeeCas103; nIngreso = nIngreso + #163#5;
             aCasilla = "321"; |HAZ LeeCas103; nIngreso = nIngreso + #163#5;

             nConsumo = 0;
             aCasilla = "258"; |HAZ LeeCas103; nConsumo = nConsumo - #163#5;
             aCasilla = "260"; |HAZ LeeCas103; nConsumo = nConsumo - #163#5;

             nGasto = 0;
             aCasilla = "270"; |HAZ LeeCas103; nGasto = nGasto - #163#5;
             aCasilla = "279"; |HAZ LeeCas103; nGasto = nGasto - #163#5;
             aCasilla = "284"; |HAZ LeeCas103; nGasto = nGasto - #163#5;
             aCasilla = "289"; |HAZ LeeCas103; nGasto = nGasto - #163#5;
             aCasilla = "293"; |HAZ LeeCas103; nGasto = nGasto - #163#5;
             aCasilla = "305"; |HAZ LeeCas103; nGasto = nGasto - #163#5;
             aCasilla = "315"; |HAZ LeeCas103; nGasto = nGasto - #163#5;
             aCasilla = "316"; |HAZ LeeCas103; nGasto = nGasto - #163#5;
             aCasilla = "322"; |HAZ LeeCas103; nGasto = nGasto - #163#5;
             aCasilla = "323"; |HAZ LeeCas103; nGasto = nGasto - #163#5;
             aCasilla = "326"; |HAZ LeeCas103; nGasto = nGasto - #163#5;

             aCasilla = "295"; |HAZ LeeCas103;
             |SI #163#5 < 0;
                 nGasto   = nGasto   - #163#5;
             |SINO;
                 nIngreso = nIngreso + #163#5;
             |FINSI;
             aCasilla = "309"; |HAZ LeeCas103;
             |SI #163#5 < 0;
                 nGasto   = nGasto   - #163#5;
             |SINO;
                 nIngreso = nIngreso + #163#5;
             |FINSI;
             aCasilla = "312"; |HAZ LeeCas103;
             |SI #163#5 < 0;
                 nGasto   = nGasto   - #163#5;
             |SINO;
                 nIngreso = nIngreso + #163#5;
             |FINSI;


             |SELECT #1#163;
             |BUCLE agicen14S;
         |FINSI;
         |CIERRA #163;
     |FINSI;
|FPRC;

|DEFBUCLE agifigen;
     #1#1;
     ;
     00001;
     99999;
     ;
     NULL, LeeEmpresa;
|FIN;

|PROCESO Borra102;
     |BORRA 020#102a;
     |LIBERA #102;
|FPRC;

|DEFBUCLE agim0002;
     #102#1;
     ;
     00001, 01, #0#0;
     99999, 99, #0#0;
     be;
     NULL, Borra102;
|FIN;

|PROCESO Tipo3;  |TIPO 3;
     |BUCLE agim0002;

     |DBASS aBase;  |QBF aBase;
     aDef   = aBase + "ren" + #0#0 + "/def/";
     aPath  = aBase + "ren" + #0#0 + "/fich/";
     aPathS = aBase + "contasoc/fich/";

     aMas = aDef + "rem01011.mas";
     |CARGA_DEF aMas, rem01011@;
     |SI FSalida ! 0;
         |ACABA;
     |FINSI;

     aMas = aDef + "rem00025.mas";
     |CARGA_DEF aMas, rem00025@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #rem01011@;
         |ACABA;
     |FINSI;

     aMas = aDef + "rem00026.mas";
     |CARGA_DEF aMas, rem00026@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #rem00025@;
         |DESCARGA_DEF #rem01011@;
         |ACABA;
     |FINSI;

     aMas = aDef + "rem00027.mas";
     |CARGA_DEF aMas, rem00027@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #rem00026@;
         |DESCARGA_DEF #rem00025@;
         |DESCARGA_DEF #rem01011@;
         |ACABA;
     |FINSI;

     aMas = aDef + "rem00028.mas";
     |CARGA_DEF aMas, rem00028@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #rem00027@;
         |DESCARGA_DEF #rem00026@;
         |DESCARGA_DEF #rem00025@;
         |DESCARGA_DEF #rem01011@;
         |ACABA;
     |FINSI;

     |PATH_DAT #211 aPath;
     |PATH_DAT #225 aPath;
     |PATH_DAT #226 aPath;
     |PATH_DAT #227 aPath;
     |PATH_DAT #228 aPath;
     |PATH_DAT #163 aPathS;

     |BUCLE agifigen;

     |DESCARGA_DEF #rem00028@;
     |DESCARGA_DEF #rem00027@;
     |DESCARGA_DEF #rem00026@;
     |DESCARGA_DEF #rem00025@;
     |DESCARGA_DEF #rem01011@;
|FPRC;
