|FICHEROS;
     dspase00 #0;
     dspase22 #22;
     dspase23 #23;
     dspase15 #15;

     dspase96 #96;
     dspase97 #97;

     :/modelos/def/dsmom100 #100;

     &referen@ #999;
      refecon@ #998;
      refebue@ #997;
|FIN;

|VARIABLES;
     &enEmpresa        = 0;
     &enEmpresaV       = 0;
     &enPeriodo        = 0;
     &enPeriodoV       = 0;
     nDEmpresa         = 0;
     nHEmpresa         = 0;
     nDPeriodo         = 0;
     nHPeriodo         = 0;
     nSwGraba          = 0
     aConcepto         = "";
     nEmpresa          = 0;
     aDirEmpresaV      = "";
     aPathAnterior     = "";
     aPathOrigen       = "";
     aDef              = "";
     nEstaCambio       = 0;
     aPathDestino      = "";
     &eaPathContagen   = "";
     aBase             = "";
     aOrigen           = "";
     aDestino          = "";
     aPathPases        = "";

     aAlfa1            = "";
     aFichero          = "";
|FIN;

|PROCESO SiNivel;
 |SI #22#1 = "S";
     |C_M #22#2,1,"S";
 |SINO;
     |C_M #22#2,1,"N";
     #22#2 = "N"; |PINTA #22#2;
 |FINSI;
|FPRC;

|PROCESO PasaConcepto;
     #96#0 = 0;
     #96#1 = 0;
     #96#2 = #999#0;
     |LEE 101#96=;
     |SI FSalida ! 0;
         #96#0 = 0;
         #96#1 = 0;
         #96#2 = #999#0;
         #96#3 = #999#1;
         |GRABA 020#96n;
         nSwGraba = 1;
     |SINO;
         aConcepto = #96#3;
         |SI #96#3 ! #999#1;  aConcepto = "";  nEmpresa = 1;  |FINSI;
     |FINSI;
     |LIBERA #96;
|FPRC;

|DEFBUCLE caconcep;
     #999#1001;
     ;
     000;
     999;
     ;
     NULL, PasaConcepto;
|FIN;

|PROCESO Pasalibros;
     #23#0 = #997#0;
     |LEE 101#23=;
     |SI FSalida ! 0;
         #23#0 = #997#0;
         #23#1 = #997#1;
         #23#2 = #997#2;
         |GRABA 020#23n;
     |FINSI;
     |LIBERA #23;
|FPRC;

|DEFBUCLE calibros;
     #997#1001;
     ;
     00;
     99;
     ;
     NULL, Pasalibros;
|FIN;

|RUTINA ClaveBaja96;
      #96#0 = nDEmpresa;
      #96#1 = nDPeriodo;
      #96#2 = 001;
|FRUT;

|RUTINA ClaveAlta96;
      #96#0 = nHEmpresa;
      #96#1 = nHPeriodo;
      #96#2 = 999;
|FRUT;

|PROCESO GrabaTemporal;
     aDirEmpresaV    = (("00000" + #998#2) % -105) + #998#3 + "/";

     aPathOrigen     = #0#0;  |QBF aPathOrigen;
     aPathOrigen     = aPathOrigen + "agicont/";
     aPathAnterior   = aPathOrigen + "fich/" + aDirEmpresaV;

     enEmpresaV = #998#2;
     enPeriodo  = #998#3;

     aDef = aPathOrigen + "def/caconcep.mas"
     |CARGA_DEF aDef, referen@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aDef = aPathOrigen + "def/calibros.mas"
     |CARGA_DEF aDef, refebue@;
     |SI FSalida ! 0;  |DESCARGA_DEF #referen@; |ACABA;  |FINSI;

     |PATH_DAT #999 aPathAnterior;
     |PATH_DAT #997 aPathAnterior;

     |ABRE #96;
     nEmpresa = 0;
     |BUCLE caconcep;
     |CIERRA #96;

     |ABRE #23;
     nEmpresa = 0;
     |BUCLE calibros;
     |CIERRA #23;

     |DESCARGA_DEF #refebue@;
     |DESCARGA_DEF #referen@;
|FPRC;

|DEFBUCLE canempre;
     #998#1002;
     ;
     00000, 0;
     99999, 9;
     ;
     NULL, GrabaTemporal;
|FIN;

|PROCESO Tipo3; |TIPO 3;

     |SI #22#4 = "N"; |ACABA; |FINSI;

     aFichero = "li" + Usuario;
     |NOME_DAT #23 aFichero;
     |DELINDEX #23;

     aPathOrigen     = #0#0;  |QBF aPathOrigen;
     aPathOrigen     = aPathOrigen + "agicont/";
     aPathAnterior   = aPathOrigen + "fich/";

     aDef            = aPathOrigen + "def/canempre";
     |CARGA_DEF aDef, refecon@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |PATH_DAT #998 aPathAnterior;

     nSwGraba = 0;
     |BUCLE canempre;

     |DESCARGA_DEF #refecon@;

         nDEmpresa = 0;
         nHEmpresa = 0;
         nDPeriodo = 0;
         nHPeriodo = 0;

         |DBASE aAlfa1;
         aDef = aAlfa1 + "def/dspase23.mas"
         |CARGA_DEF aDef, referen@;
         |SI FSalida ! 0;  |ACABA;  |FINSI;

         |NOME_DAT #999 aFichero;

         |PUSHV  0501 2380
         |PARA; |SI; |HACIENDO;
              |ENTLINEAL #1#96, -3, 4, 22, 1, ClaveBaja96, ClaveAlta96;
              |CONFI "0000NLos Conceptos estan Correctos ";
              |SI FSalida = 0;  |SAL;  |FINSI;
         |SIGUE;
         |POPV;

         |DESCARGA_DEF #referen@;
     |DELINDEX #23;
|FPRC;

|PROGRAMA;
     |ABRE #0;
     |LEE 040#0p;
     |SI FSalida ! 0;
         |MENAV "    Recuerde: No se ha realizado el Pase de Aplicaciones Restantes";
     |FINSI;
     |CIERRA #0;

     |HAZ BusquedaApli;

     |ABRE #15;
     |LEE 040#15p;
     |SI FSalida ! 0;
         nEstaCambio = 0;
         |DDEFECTO #15;
     |SINO;
         nEstaCambio = 1;
         |SI #15#15 ! "S";
             |MENAV "      Recuerde: Unificacion de Codigos no Realizada.";
         |FINSI;
     |FINSI;
     |CIERRA #15;

     aPathDestino    = eaPathContagen - "fich/";
     |SI eaPathContagen = "";
         |MENAV "    No esta Instalada la Contabilidad";
         |ACABA;
     |FINSI;

     |ABRE #100;
     |LEE 040#100p;
     |SI FSalida ! 0;
         |MENAV "    Recuerde: No esta Definida la Empresa Maestra ";
     |FINSI;
     |CIERRA #100;

     |CLS;
     |CABEZA "     Control Pase de Ficheros Contabilidad";

     |ABRE #22;
     #22#0 = "A";
     |LEE 001#22=;
     |SI FSalida ! 0;
         |DDEFECTO #22;
         |GRABA 020#22b;
     |FINSI;

     |PINPA #0#22;
     |PINDA #0#22;

     |DBASE aBase;  |QBF aBase;
     aPathPases = aBase + "pases";  |MKDIR aPathPases;
     aPathPases = aPathPases + "/";

     |PATH_DAT #96 aPathPases;
     |PATH_DAT #97 aPathPases;

     |ABRE #96;
     |LEE 040#96p;
     |SI FSalida ! 0;
         |CIERRA #96;
         aOrigen  = aBase + "fich/dspase96.dat";
         aDestino = aPathPases + "dspase96.dat";
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;
     |CIERRA #96;

     |ABRE #97;
     |LEE 040#97p;
     |SI FSalida ! 0;
         |CIERRA #97;
         aOrigen  = aBase + "fich/dspase97.dat";
         aDestino = aPathPases + "dspase97.dat";
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;
     |CIERRA #97;
     |ENDATOS #1#22;
     |SI FSalida = 0;
         |GRABA 020#22a;
     |FINSI;
     |LIBERA #22;
     |CIERRA #22;
|FPRO;
