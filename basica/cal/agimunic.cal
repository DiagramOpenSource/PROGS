|FICHEROS;
    agimunic #1;
|FIN;

|VARIABLES;
     nCanal = 0;
     i      = 0;
     j      = 0;
     nCont  = 0;
     nLongi = 0;

     aDirFich = "";
     aLinea   = "";
     aCodProv = "";
     aNomProv = "";
     aCodMuni = "";
     aNomMuni = "";
     aCP      = "";
     aLongi   = "";
     aCadena  = "";
     aPos     = "";
     aCaracter= "";
     aAlfa = "";

     nNume1 = 0;
     aCodPos = "";
     aCodigoMunicipio = "";
     nMensajeF5 = 0;
|FIN;

|PROCESO meteMunicipios;
     |ABRE #1;
     |LEE 000#1p;
     |SI FSalida ! 0;
          aDirFich = "";
          |DFICE aDirFich;
          aDirFich = aDirFich + "/municipios.txt";
          |XABRE "U", aDirFich, nCanal;

          ||nos quedamos con las cabeceras del txt
          |XLEE nCanal, 200, aLinea;

          |XLEE nCanal, 200, aLinea;
          |PARA; |SI FSalida ] 0; |HACIENDO;
                    |QBF aLinea;
                    aCodProv = aLinea % 102;
                    aCP = aLinea % -105;

                    |HAZ SacaCampos;

                    ||Ahora ya podemos ir metiendo en la tabla los campos del txt municipios

                    |DDEFECTO #agimunic;
                    #agimunic#0 = aCP;
                    #agimunic#1 = aCodProv + aCodMuni;
                    |LEE 101#agimunic.=;
                    |SI FSalida ! 0;    |GRABA 020#agimunic.b;   |FINSI;
                    #agimunic#2 = aNomProv;
                    #agimunic#3 = aNomMuni;
                    |GRABA 020#agimunic.a;
                    |LIBERA #agimunic;

                    |XLEE nCanal, 200, aLinea;
               |SIGUE;
         |XCIERRA nCanal;
     |FINSI;
     |CIERRA #1;
|FPRC;

|PROCESO SacaCampos;
     aLongi = aLinea % 0;
     nLongi = aLongi;

     aCadena = "";

     nCont = 1;

     ||hacemos un for que vaya cogiendo todos los caracteres del fichoer menos los 3 primeros (codProv + "#")
     |PARA j = 4; |SI j [ nLongi; |HACIENDO j = j + 1;
          aPos = j + "01";
          aCaracter = aLinea % aPos;
          |SI aCaracter ! "#";
               aCadena = aCadena + aCaracter;
          |SINO;
               |SI nCont = 1;
                    aNomProv = aCadena;
                    aCadena = "";
               |FINSI;
               |SI nCont = 2;
                    aCodMuni = aCadena;
                    aCadena = "";
               |FINSI;
               |SI nCont = 3;
                    aNomMuni = aCadena;
                    aCadena = "";
               |FINSI;
               nCont = nCont + 1;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO min_agimunic;
     #agimunic#0 = "00000";
     #agimunic#1 = "00000";
|FPRC;

|PROCESO max_agimunic;
     #agimunic#0 = "99999";
     #agimunic#1 = "99999";
|FPRC;

|PROGRAMA;
     aAlfa = PARAMETRO;
     |QBF aAlfa;
     |SI aAlfa = "MANTE";
          |CLS;
          |CABEZA "Relacion Munic./Cod.Pos.";
          |ABRE #agimunic;

          |PINPA #0#agimunic;
          |PINDA #0#agimunic;
          |ENTLINEAL #1#agimunic, 1, 2, 20, 1, min_agimunic, max_agimunic;
     |SINO;
          |CLS;
          |CUADRO 12241658;
          ||            2    3         4         5      5
          ||            567890123456789012345678901234567
          |ATRI I;
          |PINTA 1325, " - CARGA INICIAL DE MUNICIPIOS - ";
          |ATRI 0;
          |PINTA 1425, " Espere unos  segundos  mientras ";
          |PINTA 1525, "   se realiza la carga inicial   ";
          |HAZ meteMunicipios;
     |FINSI;
|FPRO;
