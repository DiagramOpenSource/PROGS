|FICHEROS;
   dsinfcoc #0;
   dsinfcol #1;

   dsinform #2;
   agifigen #100;

   dsinfcs1 #11,MANTE;
   dsinfcs2 #12,MANTE;
   dsinfcs3 #13,MANTE;
   dsinfcs4 #14,MANTE;
   imagen@  #900;
   image1@  #901;
|FIN;

|VARIABLES;
   aID         = "";
   nID         = 0;
   aAlfa       = "";
   aAlfa1      = "";
   aAlfa2      = "";
   aAlfa3      = "";
   nNume1      = 0;
   nNume2      = 0;
   nNume3      = 0;
   nInkey      = 0;
   nModo       = 0;
   nModoCab    = 0;
   nModoLin    = 0;
   nSwOrden    = 0;
   nOpera      = 0;
   aExt        = "";
   aPro        = "";
   nNum        = 0;
   nSwExiste   = 0;
   nLinea      = 0;
   nOrden      = 0;
   nOrdLin     = 0;
   nNumLin     = 0;
   nNumOrd     = 0;
   nAlguno     = 0;

   &enEmpresa = 0;
   &enSwalgun = 0;
   nCampo     = 0;
   aBlanc     = "";
   aZetas     = "";
   aMas       = "";
   aDefL      = "";
   aDefC      = "";
   aFichero   = "";
   nTipoS     = 0;
   nNumero    = 0;

   &nSwMal    = 0;
   &eaFecha   = "";
   &NumMes    = 0;
   &NombreMes = "";
   {10}aPopup = "";
|FIN;

|INCLUYE i_consul;
|INCLUYE i_masivo;  || Variables Utilizadas en Procesos Masivos

|PROCESO Tipo8; |TIPO 8;
  aBlanc = " " * 80;
  aZetas = "z" * 80;

  |SUB_EJECUTA_NP "agiz0023;CON";
  |SI enSwalgun = 0;
      |MENAV "    Error en instalacion de Informes Masivos.";
      |PTEC 807;
      |ACABA;
  |FINSI;

  aFichero = ("lc" + Usuario) % 108;
  |NOME_DAT #2 aFichero;

  |HAZ BusquedaApli;

  |DBASE aMas;
  aDefL = aMas + "def/dsinfcol.mas";
  |CARGA_DEF aDefL, imagen@;
  |SI FSalida ! 0;
      aDefL = "";
      |MENAV "0000Error al cargar def dsinfcol";
      |PTEC 807;
      |ACABA;
  |FINSI;

  |DBASE aMas;
  aDefC = aMas + "def/dsinfcoc.mas";
  |CARGA_DEF aDefC, image1@;
  |SI FSalida ! 0;
      aDefC = "";
      |MENAV "0000Error al cargar def dsinfcol";
      |PTEC 807;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO Tipo9; |TIPO 9;
  |SI aDefC ! ""; |DESCARGA_DEF #image1@; |FINSI;
  |SI aDefL ! ""; |DESCARGA_DEF #imagen@; |FINSI;
|FPRC;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO Tipo7C00; |TIPO 7;
  nModoCab = (FEntrada / 100) ! 0;
  |SI nModoCab = 1;
      nNumero = 0
      |ABRE #901;
      |LEE 040#901u;
      |SI FSalida = 0;
          nNumero = #901#0;
      |FINSI;
      |CIERRA #901;
      #dsinfcoc#0 = nNumero + 1;
      |PINTA #dsinfcoc#0;
  |FINSI;
|FPRC;

|PROCESO Tipo0; |TIPO 0;
  nModoCab = (FEntrada / 100) ! 0;
|FPRC;

|PROCESO PulsaAlgo; |TIPO 0;
  nInkey = FSalida;
  |SI nInkey = 14;
      |SI nModoCab ! 1; |Y Campo [ 1
          |HAZ Tipo3;
      |SINO;
          |ERROR;
      |FINSI;
  |FINSI;
  |SI nInkey = 13;
      |SI Campo ! 0;
          nTipoS = 0;
          |HAZ Seleccion;
      |SINO;
          |ERROR;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO ModLimites;
         aAlfa1 = #dsinfcoc#30 % -102; #dsinfcoc#32 = aAlfa1;
         NumMes = #dsinfcoc#32; |HAZ NombreMes; #dsinfcoc#34 = NombreMes;
         aAlfa1 = #dsinfcoc#31 % -102; #dsinfcoc#33 = aAlfa1;
         NumMes = #dsinfcoc#33; |HAZ NombreMes; #dsinfcoc#35 = NombreMes;
   || ... mes camps de la seleccio
|FPRC;

|PROCESO VerFecha; |TIPO 0;
  aAlfa   = Contenido;

  eaFecha = #dsinfcoc#Campo#;
  |HAZ VerLaFecha;

  |SI nSwMal = 1;
      |MENAV "0000Fecha Erronea";
      |ERROR;
  |SINO;
      |SI nModoCab = 1;
          |HAZ ModLimites;
      |SINO;
          |SI aAlfa ! #dsinfcoc#Campo#;
              |CONFI "0000SModificada Fecha. Cambiar Seleccion en Balances";
              |SI FSalida = 0;
                  |HAZ ModLimites;
              |FINSI;
          |FINSI;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #dsinfcoc#2  = enEmpresa;  |PINTA #dsinfcoc#2;
         #dsinfcoc#3  = enEmpresa;  |PINTA #dsinfcoc#3;

         |C_M #dsinfcoc#2, 1, "N";
         |C_M #dsinfcoc#3, 1, "N";
     |FINSI;
|FPRC;

|PROCESO Parrilla; |TIPO 0;
     |SI #dsinfcoc#2 = 0;
         #dsinfcoc#2 = 0;  |PINTA #dsinfcoc#2;
         #dsinfcoc#3 = 0;  |PINTA #dsinfcoc#3;  |C_M #dsinfcoc#3, 1, "N";
         |PARA nCampo = 4;  |SI nCampo [ 15;  |HACIENDO nCampo = nCampo + 1;
               |C_M #dsinfcoc#nCampo#, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #dsinfcoc#2, 1, "S";
             |C_M #dsinfcoc#3, 1, "S";
         |FINSI;

         |PARA nCampo = 4;  |SI nCampo [ 15;  |HACIENDO nCampo = nCampo + 1;
               |C_M #dsinfcoc#nCampo#, 1, "N";
               #dsinfcoc#nCampo# = 0;  |PINTA #dsinfcoc#nCampo#;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas; |TIPO 0;
     |SI #dsinfcoc#Campo# = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 15;  |HACIENDO nCampo = nCampo + 1;
               #dsinfcoc#nCampo# = 0;  |C_M #dsinfcoc#nCampo#, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 15;  |HACIENDO nCampo = nCampo + 1;
               |C_M #dsinfcoc#nCampo#, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO YaSeCual; |TIPO 7;
 |SI enEmpresa ! 0;
     #dsinfcoc#16 = "S"; |PINTA #dsinfcoc#16;
     #dsinfcoc#17 = "S"; |PINTA #dsinfcoc#17;
     #dsinfcoc#18 = 00;  |PINTA #dsinfcoc#18;
     #dsinfcoc#19 = 99;  |PINTA #dsinfcoc#19;
     |C_M #dsinfcoc#16, 1, "N";
     |C_M #dsinfcoc#17, 1, "N";
     |C_M #dsinfcoc#18, 1, "N";
     |C_M #dsinfcoc#19, 1, "N";
 |FINSI;
|FPRC;

|PROCESO SelTipo0; |TIPO 0;
 |SI #dsinfcoc#17 = "N";
     #dsinfcoc#18 = 0;   |PINTA #dsinfcoc#18;  |C_M #dsinfcoc#18, 1, "N";
     #dsinfcoc#19 = 99;  |PINTA #dsinfcoc#19;  |C_M #dsinfcoc#19, 1, "N";
     |PARA nCampo = 20;  |SI nCampo [ 29;  |HACIENDO nCampo = nCampo + 1;
           |C_M #dsinfcoc#nCampo#, 1, "S";
     |SIGUE;
 |SINO;
     |SI enEmpresa = 0;
         |C_M #dsinfcoc#18, 1, "S";
         |C_M #dsinfcoc#19, 1, "S";
     |FINSI;
     |PARA nCampo = 20;  |SI nCampo [ 29;  |HACIENDO nCampo = nCampo + 1;
           |C_M #dsinfcoc#nCampo#, 1, "N";
           #dsinfcoc#nCampo# = 0;  |PINTA #dsinfcoc#nCampo#;
     |SIGUE;
 |FINSI;
|FPRC;

|PROCESO SelTipo1;
 |SI #dsinfcoc#Campo# = 0;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 29;  |HACIENDO nCampo = nCampo + 1;
           #dsinfcoc#nCampo# = 0;  |C_M #dsinfcoc#nCampo#, 1, "N";
     |SIGUE;
 |SINO;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 29;  |HACIENDO nCampo = nCampo + 1;
           |C_M #dsinfcoc#nCampo#, 1, "S";
     |SIGUE;
 |FINSI;
|FPRC;

|| *********************************************************
/*
|PROCESO MTipo5;  |TIPO 5;
       |LEE 040#dsinform.p;
       #dsinform#4 = "S";
       |LINEAS_BI_ATRI #dsinform#4, D, S, -1;
|FPRC;

|PROCESO Tipo11;
|FPRC;

|PROCESO Tipo11P;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     |SELECT #2#dsinform;
     |LEE 101#dsinform.=;
     |SI #dsinform#4 = "N"; |O #dsinform#4 = "S";
         |SI #dsinform#4 = "N";
             #dsinform#4 = "S";
         |SINO;
             #dsinform#4 = "N";
         |FINSI;
     |FINSI;
     |PINTA #dsinform#4;

     |GRABA 020#dsinform.a;
     |LIBERA #dsinform;
|FPRC;
*/

|| *********************************************************
|PROCESO VerExistencia;
   nSwExiste = 0;
   |SELECT #3#900;
   #900#0 = #dsinfcoc#0;
   #900#2 = aExt;
   #900#3 = aPro;
   #900#4 = nNum;
   |LEE 040#900=;
   |SI FSalida = 0;
       nSwExiste = 1;
   |FINSI;
   |SELECT #1#900;
|FPRC;

|PROCESO Recoger;
   aExt = #dsinform#0;
   aPro = #dsinform#1;
   nNum = #dsinform#9;
   |HAZ VerExistencia;
   |SI nSwExiste = 1; |Y  #900#6 = "S";
       #dsinform#4 = "X";
   |SINO;
       #dsinform#4 = "N";
   |FINSI;
   |GRABA 020#dsinform.a;
|FPRC;

|DEFBUCLE Recoger;
   #dsinform#1;
   ;
   "   ", "        ", 1;
   "zzz", "zzzzzzzz", 9;
   ;
   NULL, Recoger;
|FIN;

|PROCESO ParaRecoger;
   |ABRE #900;
   |BUCLE Recoger;
   |CIERRA #900;
|FPRC;

|PROCESO MiroLosQueHay;
   nNumLin = #900#1;
   |SI nNumOrd < #900#9;
       nNumOrd = #900#9;
   |FINSI;
|FPRC;

|DEFBUCLE MiroLosQueHay;
   #900#1002;
   ;
   #dsinfcoc#0, 01;
   #dsinfcoc#0, 99;
   ;
   NULL, MiroLosQueHay;
|FIN;

|PROCESO Pasar;
   aExt = #dsinform#0;
   aPro = #dsinform#1;
   nNum = #dsinform#9;
   |HAZ VerExistencia;
   |SI nSwExiste = 1;
       nLinea  = #900#1;
       nOrden  = #900#9;
   |SINO;
       |SI nOpera = 1;
           nLinea  = #dsinfcol#1;
       |SINO;
           nNumLin = nNumLin + 1;
           nLinea  = nNumLin;
       |FINSI;
       nNumOrd = nNumOrd + 1;
       nOrden  = nNumOrd;
   |FINSI;

   #900#0  = #dsinfcoc#0;
   #900#1  = nLinea;
   |LEE 041#900=;
   |SI FSalida ! 0;
       |DDEFECTO #900;
       #900#0  = #dsinfcoc#0;
       #900#1  = nLinea;
       |GRABA 020#900c;
   |FINSI;
   #900#2  = #dsinform#0;
   #900#3  = #dsinform#1;
   #900#4  = #dsinform#9;
   #900#5  = #dsinform#3;
   #900#6  = #dsinform#4;
   #900#7  = #dsinform#5;
   #900#8  = #dsinform#6;
   #900#9  = nOrden;
   #900#10 = #dsinform#8;
   |GRABA 040#900a;
   nAlguno = 1;
|FPRC;

|DEFBUCLE Pasar;
   #dsinform#1;
   4;
   "   ", "        ", 1, "S";
   "zzz", "zzzzzzzz", 9, "S";
   ;
   NULL, Pasar;
|FIN;

|PROCESO VerLosQueHay;
   nAlguno = 0;
   nNumLin = 0;
   nNumOrd = 0;
   |BUCLE MiroLosQueHay;
|FPRC;

|PROCESO ParaPasar;
   |HAZ VerLosQueHay;

   |ABRE #900;
   |SI nOpera = 0;
       |BUCLE Pasar;
   |SINO;
       |SI nOpera = 1;
           #dsinform#4 = "S";
           |HAZ Pasar;
       |FINSI;
   |FINSI;
   |CIERRA #900;
   |SI nAlguno = 0;
   |FINSI;
|FPRC;

|| *********************************************************
|PROCESO ClaveBaja2;
  #dsinform#4 = "N";
  #dsinform#8 = " ";
  #dsinform#6 = aBlanc;
|FPRC;

|PROCESO ClaveAlta2;
  #dsinform#4 = "N";
  #dsinform#8 = "z";
  #dsinform#6 = aZetas;
|FPRC;

|PROCESO CargaProgramas;

    |HAZ ParaRecoger;
    |C_V #dsinform#0,1,"N"; |C_V #dsinform#1,1,"N";
    |C_V #dsinform#2,1,"N"; |C_V #dsinform#3,1,"N";
    |C_V #dsinform#4,1,"S"; |C_P #dsinform#4,1,1076;
    |C_V #dsinform#5,1,"N";

    |C_V #dsinform#6, 1,"S";   |C_L #dsinform#6,1,"S";
    |C_P #dsinform#6, 1, 1009; |C_A #dsinform#6,1,65;
    |C_M #dsinform#6,1,"S";

    |C_V #dsinform#7,1,"S"; |C_P #dsinform#7, 1, 1006;
    |C_V #dsinform#8,1,"N";

    |SI nOpera = 0;
        |PUSHV 0501 2380;
        |BLANCO 0905 1878;
        |CUADRO 0905 1878;
        |ATRI I;
        |PINTA 0906, "Relacion de Informes a Lanzar";
        |PINTA 0966, "Seleccionar";
        |ATRI 0;

        |ENTLINEAL #3#dsinform, -3, 4, 17, 0, ClaveBaja2, ClaveAlta2;
        |POPV;
    |FINSI;
    |SI nOpera = 1;
        |ABRE #dsinform;
        |CONSULTA_F_CLAVE #3#dsinform, ClaveBaja2, ClaveAlta2;
        |SI FSalida ! 0;
            nOpera = 2;
        |FINSI;
        |CIERRA #dsinform;
    |FINSI;

    |HAZ ParaPasar;
    |PONCONTROL 23, "SI";
|FPRC;

|| *********************************************************
|PROCESO  MiroElTipo;
     aAlfa = ("000" + Campo) % -103;
     aAlfa = "|C" + aAlfa + "WID";
     aID   = #dsinfcoc#aAlfa#;
     nID   = aID;

     aPopup1  = nID;  || Si fuera -1 en la posicion
     aPopup2  = 5;
     aPopup3  = "1 para Balances  ";
     aPopup4  = "2 para Extractos ";
     aPopup5  = "3 para anotacines";
     aPopup6  = "4 para Embargos  ";
     aPopup7  = "5 para socios    ";

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida = 0;  |ACABA;  |FINSI;

     IaPopup  = aPopup3  <-;
     IaPopup  = IaPopup + (FSalida - 1);

     aAlfa = aPopup % 101;
     nTipoS = aAlfa;
|FPRC;

|PROCESO Seleccion;
    |SI nTipoS = 0;
        |HAZ MiroElTipo;
        |SI nTipoS = 0; |ACABA; |FINSI;
    |FINSI;

    |SI nTipoS = 1;
        |PARA nNume1 = 0; |SI nNume1 [ 27; |HACIENDO nNume1 = nNume1 + 1;
              nNume2 = nNume1 + 32;
              #dsinfcs1#nNume1# = #dsinfcoc#nNume2#;
        |SIGUE;

        |PUSHV 0501 2380;
        |PINPA #0#11;
        |PINDA #0#11;
        |SI nModo ! 4;
            |ENDATOS #2#11;
            |SI FSalida = 0;
                |PARA nNume1 = 0; |SI nNume1 [ 27; |HACIENDO nNume1 = nNume1 + 1;
                      nNume2 = nNume1 + 32;
                      #dsinfcoc#nNume2# = #dsinfcs1#nNume1#;
                |SIGUE;
            |FINSI;
        |SINO;
            |PAUSA;
        |FINSI;
        |POPV;
    |FINSI;

    |SI nTipoS = 2;
        #dsinfcs2#0 = #dsinfcoc#60;
        #dsinfcs2#1 = #dsinfcoc#61;
        #dsinfcs2#2 = #dsinfcoc#30;
        #dsinfcs2#3 = #dsinfcoc#31;
        |PARA nNume1 = 4; |SI nNume1 [ 51; |HACIENDO nNume1 = nNume1 + 1;
              nNume2 = nNume1 + 58;
              #dsinfcs2#nNume1# = #dsinfcoc#nNume2#;
        |SIGUE;

        |PUSHV 0501 2380;
        |PINPA #0#12;
        |PINDA #0#12;
        |SI nModo ! 4;
            |ENDATOS #2#12;
            |SI FSalida = 0;
                #dsinfcoc#60 = #dsinfcs2#0;
                #dsinfcoc#61 = #dsinfcs2#1;
                #dsinfcoc#30 = #dsinfcs2#2;
                #dsinfcoc#31 = #dsinfcs2#3;
                |PARA nNume1 = 4; |SI nNume1 [ 51; |HACIENDO nNume1 = nNume1 + 1;
                      nNume2 = nNume1 + 58;
                      #dsinfcoc#nNume2# = #dsinfcs2#nNume1#;
                |SIGUE;
            |FINSI;
        |SINO;
            |PAUSA;
        |FINSI;
        |POPV;
    |FINSI;

    |SI nTipoS = 3;
        #dsinfcs3#0 = #dsinfcoc#110;
        #dsinfcs3#1 = #dsinfcoc#111;
        #dsinfcs3#2 = #dsinfcoc#30;
        #dsinfcs3#3 = #dsinfcoc#31;
        |PARA nNume1 = 4; |SI nNume1 [ 12; |HACIENDO nNume1 = nNume1 + 1;
              nNume2 = nNume1 + 108;
              #dsinfcs3#nNume1# = #dsinfcoc#nNume2#;
        |SIGUE;

        |PUSHV 0501 2380;
        |PINPA #0#13;
        |PINDA #0#13;
        |SI nModo ! 4;
            |ENDATOS #2#13;
            |SI FSalida = 0;
                #dsinfcoc#110 = #dsinfcs3#0;
                #dsinfcoc#111 = #dsinfcs3#1;
                #dsinfcoc#30  = #dsinfcs3#2;
                #dsinfcoc#31  = #dsinfcs3#3;
                |PARA nNume1 = 4; |SI nNume1 [ 12; |HACIENDO nNume1 = nNume1 + 1;
                      nNume2 = nNume1 + 108;
                      #dsinfcoc#nNume2# = #dsinfcs3#nNume1#;
                |SIGUE;
            |FINSI;
        |SINO;
            |PAUSA;
        |FINSI;
        |POPV;
    |FINSI;

    |SI nTipoS = 4;
        |PARA nNume1 = 0; |SI nNume1 [ 15; |HACIENDO nNume1 = nNume1 + 1;
              nNume2 = nNume1 + 120;
          ||    #dsinfcs4#nNume1# = #dsinfcoc#nNume2#;
        |SIGUE;

        |PUSHV 0501 2380;
        |PINPA #0#14;
        |PINDA #0#14;
        |SI nModo ! 4;
            |PAUSA;
            || |ENDATOS #2#14;
            |SI FSalida = 0;
                |PARA nNume1 = 0; |SI nNume1 [ 15; |HACIENDO nNume1 = nNume1 + 1;
                      nNume2 = nNume1 + 120;
              ||        #dsinfcoc#nNume2# = #dsinfcs4#nNume1#;
                |SIGUE;
            |FINSI;
        |SINO;
            |PAUSA;
        |FINSI;
        |POPV;
    |FINSI;
|FPRC;

|| *********************************************************
|PROCESO MTipo5col;  |TIPO 5;

       |LEE 040#dsinfcol.p;
       #dsinfcol#6 = "S";
       |LINEAS_BI_ATRI #dsinfcol#6, D, S, -1;

||       |SI nModoCab = 2;
||           |HAZ VerLosQueHay;
||       |FINSI;
|FPRC;

|PROCESO Tipo7L; |TIPO 7;
     nModoLin = (FEntrada / 100) ! 0;
     |SI nModoLin = 2; aAlfa1 = "0000F2-Activar/Desact.Informe, F3-Entrar Orden, F4-Ver Orden, F5-Entrar Seleccion"; |FINSI;
     |SI nModoLin = 1; aAlfa1 = "0000<F4> Ver Orden, <F12> Ver Informes para incorporar"; |FINSI;
     |SI nModoLin = 4; aAlfa1 = "0000<F4> Ver Orden. <F5> Ver Campos Seleccion"; |FINSI;
     |MENSA aAlfa1;
|FPRC;

|PROCESO Clave1;
    #900#0 = #dsinfcol#0;
    #900#9 = 1;
|FPRC;

|PROCESO Clave2;
    #900#0 = #dsinfcol#0;
    #900#9 = 99;
|FPRC;

|PROCESO Tipo11col;  |TIPO 11;
     nInkey   = FSalida;
     nModoLin = (FEntrada / 100) ! 0;

     |SI nInkey = 12;
         |PUSHV 1401 2380;
         |ATRI I;|PINTA 1426, "ORDEN DE EMISION DE LOS INFORMES";|ATRI 0;
         |ENTLINEAL #2#900, -2, 4, 22, 0, Clave1, Clave2;
         |POPV;
         |ERROR;
         |ACABA;
     |FINSI;

     |SI nModoLin = 1;
         |SI nInkey = 9;
             nOpera = 1;
             |HAZ CargaProgramas;
         |SINO;
             |SI nInkey ] 10; |Y nInkey [ 20; |Y nInkey ! 13;
                 |ERROR;
             |FINSI;
         |FINSI;
     |FINSI;
     |SI nModoLin = 2;
         |SI nInkey = 10; |O nInkey = 11;
             |LEE 041#dsinfcol.=;
             |SI FSalida = 0;
                 |SI nInkey = 10;
                     |SI #dsinfcol#6 = "N";
                         #dsinfcol#6 = "S";
                     |SINO;
                         #dsinfcol#6 = "N";
                     |FINSI;

                     |PINTA #dsinfcol#6;
                 |FINSI;
                 |SI nInkey = 11;
                     |HAZ PideCampo9;
                 |FINSI;
                 |GRABA 020#dsinfcol.a;
                 |LIBERA #dsinfcol;
                 |ERROR;
              |FINSI;
        |FINSI;
    |FINSI;
    |SI nInkey = 13;
        nModo = nModoLin;
        |SI nModoLin ! 1;
            nTipoS = #dsinfcol#10;
        |SINO;
            nTipoS = 0;
        |FINSI;
        |HAZ Seleccion;
        |ERROR;
    |FINSI;
|FPRC;

|PROCESO Tipo0col; |TIPO 0;
    |BLIN 4;
    nModoLin = (FEntrada / 100) ! 0;
    |SI nModoLin = 1;
        nOpera = 1;
        |HAZ CargaProgramas;
    |FINSI;
    nOrdLin = #dsinfcol#9;
|FPRC;

|PROCESO PideCampo9;
    nOrdLin = #dsinfcol#9;
    |ENCAMPO #9#dsinfcol;
    |HAZ Tipo3L;
|FPRC;

|PROCESO ElOrden;
    nSwOrden = 0;
    |SI #dsinfcol#9 ! nOrdLin;
        nSwOrden = 1;
    |FINSI;
|FPRC;

|PROCESO ElCambio;
        |ABRE #900;
        |SELECT #2#900;
        #900#0 = #dsinfcol#0;
        #900#9 = #dsinfcol#9;
        |LEE 141#900=;
        |SI FSalida = 0; |Y #900#1 ! #dsinfcol#1;
            #900#9 = nOrdLin;
            |GRABA 020#900.a;
        |FINSI;
        |LIBERA #900;
        |CIERRA #900;
|FPRC;

|PROCESO Tipo3L; |TIPO 3;
    |SI nSwOrden = 1;
        |HAZ ElCambio;
        |PONCONTROL 23, "SI";
        |PTEC 809;
    |FINSI;
    nSwOrden = 0;
|FPRC;

|| ------------------------------------------------------------------------

|PROCESO Tipo3; |TIPO 30;

  |CONFI "0000SLanzar Informes seleccionados";
  |SI FSalida = 0;
      aAlfa1 = ("0000" + #dsinfcoc#0) % -104;
      aAlfa1 = ":basica/dsinfcoy;" + aAlfa1;
      |SUB_EJECUTA_NP aAlfa1;
  |FINSI;
|FPRC;
