|FICHEROS;
     dspase00 #0;
     dspase14 #14;
     dspase15 #15;
     dspase80 #80;
     dspase91 #91,MANTE;
     dspase92 #92,MANTE;

     :/basica/def/agicen14   #114;
     :/basica/def/zmonedas   #204;

     :/modelos/def/dsmom003 #3;
     :/modelos/def/dsmom004 #4;
     :/modelos/def/dsmom008 #8;

     :/modelos/def/dsmom009 #9;
     :/modelos/def/dsmom010 #10;
     :/modelos/def/dsmom011 #11;
     :/modelos/def/dsmom012 #12;
     :/modelos/def/dsmom013 #13;
     :/modelos/def/dsmom014 #214;
     :/modelos/def/dsmociva #300;
     :/modelos/def/dsmoc130 #130;

     agifige@ #330;
     refer00@ #900;
     refer01@ #901;
     refer02@ #902;

     dsmom09@ #909;
|FIN;

|VARIABLES;
     nAnyo           = 0;
     nSw             = 0;
     nSw130          = 0;
     nTecla          = 0;
     aAlfa           = "";
     aAlfa1          = "";
     aAlfa2          = "";
     aAlfa3          = "";
     nCodigoNue      = 0;
     nCodigoReo      = 0;
     nEstaCambio     = 0;

     aPathAnterior   = "";
     aPathDefFiscal  = "";
     aPathFichFiscal = "";
     aPathDestino    = "";
     aDef            = "";
     aDirEmpresa     = "";
     aDirFichDestino = "";
     aDirFichOrigen  = "";
     aFichero        = "";

     &eaPathContagen = "";
     &eaPathBasica   = "";
     &eaPathDsmodelo = "";
     &eaPathIntegral = "";
     &eaPathAgitool  = "";
     &eaFAsiento     = "";
     aPathdefModelo  = "";

     aDef330         = "";

     nDEmpresa       = 0;
     nHEmpresa       = 0;
     nPeriodo        = 0;
     aPathRec        = "";
     aDirTempo       = "";
     aFich           = "";
     aFichTar        = "";
     aDirOrigen      = "";
     aDirDestino     = "";

     nSwError   = 0;
     aMensa     = "";
     nSwLocal   = 0;
     aPath      = "";
     aBase      = "";
     aPathPases = "";
     aOrigen    = "";
     aDestino   = "";
     aDirBase   = "";
     aBasLocal  = "";
     aIPRemoto  = "";
     aPathDef   = "";
     fFecha     = @;

     &enPlan    = 0;
     nNume1     = 0;
     nNume2     = 0;
     nNume3     = 0;
     nImporte   = 0;
     aCuenta    = "";
     aGasCon    = "";
     nCampo     = 0;
     nIVA       = 0
     nTri       = 0;
     nUno       = 0;
     &enEmpresa   = 0;
     &enEjer      = 0;
     &enPeriodo   = 0;
     &enModelo    = 0;
     &enComplem   = 0;
     &enActividad = 0;
     &nGrupo      = 0;
     &nSubGrupo   = 0;
|FIN;

|RUTINA ClaveBaja92;
     #92#0 = nAnyo;
     #92#1 = 99999;
     #92#3 = 6;
|FRUT;

|RUTINA ClaveAlta92;
     #92#0 = nAnyo;
     #92#1 = 00000;
     #92#3 = 1;
|FRUT;

|RUTINA ClaveBaja91;
     #91#0 = nAnyo;
     #91#1 = 00000;
     #91#4 = 1;
|FRUT;

|RUTINA ClaveAlta91;
     #91#0 = nAnyo;
     #91#1 = 99999;
     #91#4 = 6;
|FRUT;

|| PROCESOS DEL DSPASE92 *************************************

|PROCESO Tipo11w92;  |TIPO 11;
     nTecla = FSalida;

     |SI nTecla = 10;
         #91#0 = #92#0;
         #91#1 = #92#1;
         #91#2 = #92#2;
         #91#3 = "N";
         #91#4 = #92#3;
         #91#5 = #92#4;
         |GRABA 020#91n;
         |LIBERA #91;

         |LEE 101#92=;
         |BORRA 020#92a;
         |LIBERA #92;

         |PONCONTROL 23, "SI";
     |FINSI;
|FPRC;

|| PROCESOS DEL DSPASE91 *************************************

|PROCESO PonAlfa;
     #91#3 = aAlfa;
     |GRABA 020#91a;
     |LIBERA #91;
|FPRC;

|DEFBUCLE dspase91E;
     #91#1;
     ;
     nAnyo, 00000, 0;
     nAnyo, 99999, 9;
     be;
     NULL, PonAlfa;
|FIN;

|PROCESO Tipo11w91;  |TIPO 11;
     nTecla = FSalida;

     |SI nTecla = 10;
         |LEE 101#91=;
         |SI #91#3 = "S";
             #91#3 = "N";
         |SINO;
             #91#3 = "S";
         |FINSI;
         |PINTA #91#3;

         |GRABA 020#91a;
         |LIBERA #91;
     |FINSI;

     |SI nTecla = 11;
         aAlfa       = "S";
         |BUCLE dspase91E;
         |PONCONTROL 23, "SI";
         |PTEC 812;
     |FINSI;

     |SI nTecla = 12;
         aAlfa      = "N";
         |BUCLE dspase91E;
         |PONCONTROL 23, "SI";
         |PTEC 812;
     |FINSI;

     |SI nTecla = 13;
         |ENTLINEAL #1#92, -2, 4, 22, 0, ClaveBaja92, ClaveAlta92;
         |ENTLINEAL #1#91, -2, 7, 22, 0, ClaveBaja91, ClaveAlta91;
     |FINSI;
|FPRC;

|| ______________________ PROCESOS DE DATOS POR PANTALLA  ___________________

|PROCESO Tipo12;  |TIPO 12;
|FPRC;

|CALCULO PonBarra; |TIPO 0;
     nTecla = FSalida;
     |C_M #14#6,0, aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     |SI nTecla = 10;
        aAlfa1 = "D c:\";
        |BROWSE aAlfa1;
        aAlfa1 = aAlfa1 % 280;
        #14Campo = aAlfa1;
        |PINTA #14Campo
    |FINSI;

     aAlfa1 = #14Campo;
     |QBF aAlfa1;
     aAlfa2 = aAlfa1 % -101;
     |SI aAlfa2 ! "/";|Y aAlfa2 ! "\";
          #14Campo = aAlfa1 + "/";
          |PINTA #14Campo;
     |FINSI;

     nSwError = 0;
     |HAZ CargaEmpReo;
     |SI nSwError ! 0;
         |MENAV aMensa;
         |C_M #14#6,1, "S";
         |C_M #14#7,1, "S";
     |SINO;
         |C_M #14#6,1, "N";
         |C_M #14#7,1, "N";
     |FINSI;
|FCAL;

|PROCESO Plantilla7; |TIPO 7;
 fFecha = ~;
 aAlfa1 = fFecha;
 aAlfa1 = aAlfa1 % -104;
 #14Campo = aAlfa1;
 |PINTA #14Campo;
|FPRC;

|PROCESO Plantilla;
 |ABRE #80;
 #80#0 = #14#2;
 |LEE 001#80=;
 |SI FSalida ! 0;
     |MENAV "    Error, no existe plantilla para el Pase";
     |ERROR;
 |FINSI;
 |CIERRA #80;
|FPRC;
|| ////////////////////////////////////////////////////////////////////
|| ////////////////////////////////////////////////////////////////////

|PROCESO CogeCodigo;
     |SI #15#22 = "S";  |ACABA;  |FINSI;

     nCodigoNue = #15#3;
     |ERROR10;
|FPRC;

|DEFBUCLE dspase15;
     #15#4;
     ;
     nCodigoReo, "            ", 000;
     nCodigoReo, "zzzzzzzzzzzz", 999;
     ;
     NULL, CogeCodigo;
|FIN;

|| //////////////////////////////////////
|PROCESO Borra10;
     |BORRA 020#10a;
     |LIBERA #10;
|FPRC;

|DEFBUCLE dsmom010B;
     #10#1;
     ;
     #9#0, #9#1, #9#2, #9#3, #9#4, "    ";
     #9#0, #9#1, #9#2, #9#3, #9#4, "zzzz";
     be;
     NULL, Borra10;
|FIN;

|PROCESO Borra11;
     |BORRA 020#11a;
     |LIBERA #11;
|FPRC;

|DEFBUCLE dsmom011B;
     #11#1;
     ;
     #9#0, #9#1, #9#2, #9#3, #9#4, "    ";
     #9#0, #9#1, #9#2, #9#3, #9#4, "zzzz";
     be;
     NULL, Borra11;
|FIN;

|PROCESO Borra12;
     |BORRA 020#12a;
     |LIBERA #12;
|FPRC;

|DEFBUCLE dsmom012B;
     #12#1;
     ;
     #9#0, #9#1, #9#2, #9#3, #9#4, "    ";
     #9#0, #9#1, #9#2, #9#3, #9#4, "zzzz";
     be;
     NULL, Borra12;
|FIN;

|PROCESO Borra13;
     |BORRA 020#13a;
     |LIBERA #13;
|FPRC;

|DEFBUCLE dsmom013B;
     #13#1;
     ;
     #9#0, #9#1, #9#2, #9#3, #9#4, "    ";
     #9#0, #9#1, #9#2, #9#3, #9#4, "zzzz";
     be;
     NULL, Borra13;
|FIN;

|PROCESO PasaIngresos;
     |DDEFECTO #10;
     #10#0  = #9#0;
     #10#1  = #9#1;
     #10#2  = #9#2;
     #10#3  = #9#3;
     #10#4  = #9#4;
     #10#5  = #214#0;
     #10#6  = #214#1;
     #10#7  = #214#3;
     |GRABA 020#10n;
     |LIBERA #10;
|FPRC;

|DEFBUCLE dsmom014Iw6;
     #214#1;
     2;
     enPlan, "    ", "I";
     enPlan, "zzzz", "I";
     ;
     NULL, PasaIngresos;
|FIN;

|PROCESO PasaConsumo;
     |DDEFECTO #12;
     #12#0  = #9#0;
     #12#1  = #9#1;
     #12#2  = #9#2;
     #12#3  = #9#3;
     #12#4  = #9#4;
     #12#5  = #214#0;
     #12#6  = #214#1;
     #12#7  = #214#3;
     |GRABA 020#12n;
     |LIBERA #12;
|FPRC;

|DEFBUCLE dsmom014Cw6;
     #214#1;
     2;
     enPlan, "    ", "C";
     enPlan, "zzzz", "C";
     ;
     NULL, PasaConsumo;
|FIN;

|PROCESO PasaExistencias;
     |DDEFECTO #11;
     #11#0  = #9#0;
     #11#1  = #9#1;
     #11#2  = #9#2;
     #11#3  = #9#3;
     #11#4  = #9#4;
     #11#5  = #214#0;
     #11#6  = #214#1;
     #11#7  = #214#3;
     #11#10 = #214#5;
     |GRABA 020#11n;
     |LIBERA #11;
|FPRC;

|DEFBUCLE dsmom014Ew6;
     #214#1;
     2;
     enPlan, "    ", "E";
     enPlan, "zzzz", "E";
     ;
     NULL, PasaExistencias;
|FIN;

|PROCESO PasaGastos;
     |DDEFECTO #13;
     #13#0  = #9#0;
     #13#1  = #9#1;
     #13#2  = #9#2;
     #13#3  = #9#3;
     #13#4  = #9#4;
     #13#5  = #214#0;
     #13#6  = #214#1;
     #13#7  = #214#3;
     |GRABA 020#13n;
     |LIBERA #13;
|FPRC;

|DEFBUCLE dsmom014Gw6;
     #214#1;
     2;
     enPlan, "    ", "G";
     enPlan, "zzzz", "G";
     ;
     NULL, PasaGastos;
|FIN;
|| ========================================================================
|PROCESO LeoM014;
    |ABRE #214;
    |DDEFECTO #214;
    #214#10 = enPlan;
    #214#0  = aCuenta;
    |LEE 040#214=;
    |CIERRA #214;
|FPRC;

|PROCESO Ingresos;
     nNume2  = nNume1 + 48;
     nNume3  = nNume1 + 103;
     aCuenta = #80nNume2; |QBF aCuenta;
     |SI aCuenta = "";
         |ACABA;  || no lo paso
     |FINSI;

     |DDEFECTO #10;
     #10#0  = #9#0;
     #10#1  = #9#1;
     #10#2  = #9#2;
     #10#3  = #9#3;
     #10#4  = #9#4;
     #10#5  = aCuenta;
     |LEE 141#10=;
     |SI FSalida ! 0;
         |DDEFECTO #10;
         #10#0  = #9#0;
         #10#1  = #9#1;
         #10#2  = #9#2;
         #10#3  = #9#3;
         #10#4  = #9#4;
         #10#5  = aCuenta;
         |HAZ LeoM014;
         #10#6  = #214#1;
         #10#7  = #214#3;
         |GRABA 020#10b;
     |FINSI;
     #10#8 = #10#8 + nImporte;
     |GRABA 020#10a;
     |LIBERA #10;
|FPRC;

|PROCESO GastoConsumo;
     |SI #901#22 = "B";
         nNume2  = nNume1 + 61;
         nNume3  = nNume1 + 116;
     |SINO;
         nNume2  = nNume1 + 82;
         nNume3  = nNume1 + 137;
     |FINSI;
     aCuenta = #80nNume2; |QBF aCuenta;
     aGasCon = #80nNume3;
     |SI aCuenta = "";
         |ACABA;  || no lo paso
     |FINSI;

     |SI aGasCon = "C";
         |DDEFECTO #12;
         #12#0  = #9#0;
         #12#1  = #9#1;
         #12#2  = #9#2;
         #12#3  = #9#3;
         #12#4  = #9#4;
         #12#5  = aCuenta;
         |LEE 141#12=;
         |SI FSalida ! 0;
             |DDEFECTO #12;
             #12#0  = #9#0;
             #12#1  = #9#1;
             #12#2  = #9#2;
             #12#3  = #9#3;
             #12#4  = #9#4;
             #12#5  = aCuenta;
             |HAZ LeoM014;
             #12#6  = #214#1;
             #12#7  = #214#3;
             |GRABA 020#12b;
         |FINSI;
         #12#8 = #12#8 + nImporte;
         |GRABA 020#12a;
         |LIBERA #12;
     |FINSI;
     |SI aGasCon = "G";
         |DDEFECTO #13;
         #13#0  = #9#0;
         #13#1  = #9#1;
         #13#2  = #9#2;
         #13#3  = #9#3;
         #13#4  = #9#4;
         #13#5  = aCuenta;
         |LEE 141#13=;
         |SI FSalida ! 0;
             |DDEFECTO #13;
             #13#0  = #9#0;
             #13#1  = #9#1;
             #13#2  = #9#2;
             #13#3  = #9#3;
             #13#4  = #9#4;
             #13#5  = aCuenta;
             |HAZ LeoM014;
             #13#6  = #214#1;
             #13#7  = #214#3;
             |GRABA 020#13b;
         |FINSI;
         #13#8 = #13#8 + nImporte;
         |GRABA 020#13a;
         |LIBERA #13;
     |FINSI;
|FPRC;

|PROCESO Existencias;
     aCuenta = #80#61; |QBF aCuenta;
     |SI aCuenta = "";
         |ACABA;  || no lo paso
     |FINSI;

     |DDEFECTO #11;
     #11#0  = #9#0;
     #11#1  = #9#1;
     #11#2  = #9#2;
     #11#3  = #9#3;
     #11#4  = #9#4;
     #11#5  = aCuenta;
     |LEE 141#11=;
     |SI FSalida ! 0;
         |DDEFECTO #11;
         #11#0  = #9#0;
         #11#1  = #9#1;
         #11#2  = #9#2;
         #11#3  = #9#3;
         #11#4  = #9#4;
         #11#5  = aCuenta;
         |HAZ LeoM014;
         #11#6  = #214#1;
         #11#7  = #214#3;
         #11#10 = #214#5;
         |GRABA 020#10b;
     |FINSI;
     #11#8 = #11#8 + #902#48;
     #11#9 = #11#9 + #902#49;
     |GRABA 020#11a;
     |LIBERA #11;
|FPRC;
|| ========================================================================
|PROCESO LeeCasilla;
     #300#0  = enEmpresa;
     #300#1  = enEjer;
     #300#2  = enPeriodo;
     #300#3  = enModelo;
     #300#4  = enComplem;
     #300#5  = enActividad;
     #300#6  = nGrupo;
     #300#7  = nSubGrupo;
     |LEE 040#300=;
     |SI FSalida ! 0;  |DDEFECTO #300;  |FINSI;
|FPRC;

|PROCESO Totales;

     #9#19 = #9#17 + #9#18;
     #9#22 = #9#20 + #9#21;
     #9#59 = #9#58;
     #9#25 = #9#23 + #9#24;

     |SI #9#56 ! 7;  |Y #9#56 ! 12;  |Y #9#56 ! 17;
         #9#26 = #9#17 - (#9#20 + #9#58) - #9#23;
         #9#30 = #9#26 % #9#29;
         |SI #9#30 < 0;  #9#30 = 0;  |FINSI;

         #9#33 = #9#26 - #9#30;

         #9#37 = #9#33 % #9#36;
         |SI #9#37 < 0;  #9#37 = 0;  |FINSI;

         #9#28 = #9#19 - (#9#22 + #9#59) - #9#25;

         #9#32 = #9#28 % #9#29;
         |SI #9#32 < 0;  #9#32 = 0;  |FINSI;

         #9#35 = #9#28 - #9#32;

         #9#39 = #9#35 % #9#36;
         |SI #9#39 < 0;  #9#39 = 0;  |FINSI;

         #9#42 = #9#40 + #9#41;
         #9#44 = #9#39 - #9#42 - #9#43;
         |SI #9#44 < 0;  #9#44 = 0;  |FINSI;

    |SINO;
         |PARA nCampo = 26;  |SI nCampo [ 39;  |HACIENDO nCampo = nCampo + 1;
               #9nCampo = 0;
         |SIGUE;

         #9#41 = 0;
         #9#42 = 0;
         #9#26 = #9#17 % 2;
         #9#44 = 0;
         |SI #9#26 > 0;
             #9#44 = #9#26 - #9#40;
         |FINSI;
         |SI #9#44 < 0;  #9#44 = 0;  |FINSI;
    |FINSI;

     enEmpresa     = #9#0;
     enEjer        = #9#1;
     enPeriodo     = #9#2;
     |SI #9#7    = "X";  enModelo = 300;  |FINSI;
     |SI #9#8    = "X";  enModelo = 370;  |FINSI;
     |SI #9#9    = "X";  enModelo = 320;  |FINSI;
     |SI #9#10   = "X";  enModelo = 330;  |FINSI;
     |SI #9#11   = "X";  enModelo = 332;  |FINSI;
     enComplem     = #9#3;
     enActividad   = #9#4;

    |ABRE #300;
    nGrupo    = 1;
    nSubGrupo = 13;
    |HAZ LeeCasilla;
    #9#45 = #300#13;

    nGrupo    = 2;
    nSubGrupo = 12;
    |HAZ LeeCasilla;
    #9#46 = #300#13;
    |CIERRA #300;

    #9#47 = #9#45 - #9#46;
|FPRC;

|PROCESO RecogeAnterior;
     #9#18 = 0;
     #9#21 = 0;
     #9#24 = 0;
     #9#27 = 0;
     #9#31 = 0;
     #9#34 = 0;
     #9#38 = 0;
     #9#41 = 0;

     nSw130 = 0;
     |ABRE #130;
     #130#0 = #9#0;
     #130#1 = #9#1;
     #130#2 = #9#2 - 3;
     #130#3 = 130;
     #130#4 = 0;
     #130#5 = #9#4;
     |LEE 000#130=;
     |PARA;  |SI FSalida = 0;  |HACIENDO;  || Recoge de la ultima linea del
          #9#18 = #130#8;                  || periodo
          #9#21 = #130#11;
          #9#24 = #130#14;
          #9#27 = #130#17;
          #9#31 = #130#21;
          #9#34 = #130#24;
          #9#38 = #130#28;
          #9#41 = #130#31;

          #130#4 = #130#4 + 1;
          nSw130 = 1;
          |LEE 000#130=;
     |SIGUE;

     #9#43 = #130#32 + #130#33;

     |CIERRA #130;

     |SI nSw130 = 0;
         |ABRE #909;
         #909#0 = #9#0;
         #909#1 = #9#1;
         #909#2 = #9#2 - 3;
         #909#3 = 0;
         #909#4 = #9#4;
         |LEE 000#909=;
         |PARA;  |SI FSalida = 0;  |HACIENDO;  || Recoge de la ultima linea del
                 #9#18 = #909#19;                 || periodo
                 #9#21 = #909#22;
                 #9#24 = #909#25;
                 #9#27 = #909#28;
                 #9#31 = #909#32;
                 #9#34 = #909#35;
                 #9#38 = #909#39;
                 #9#41 = #909#42;

                 #909#3 = #909#3 + 1;
                 |LEE 000#909=;
          |SIGUE;

          |CIERRA #909;
     |FINSI;
|FPRC;

|PROCESO CalculaIngresos;
     |SI #10#7 = "+";
         #9#17 = #9#17 + #10#8;
     |SINO;
         #9#17 = #9#17 - #10#8;
     |FINSI;
|FPRC;

|DEFBUCLE dsmom010w6;
     #10#1;
     ;
     #9#0, #9#1, #9#2, #9#3, #9#4, "    ";
     #9#0, #9#1, #9#2, #9#3, #9#4, "zzzz";
     ;
     NULL, CalculaIngresos;
|FIN;

|PROCESO CalculaConsumo;
     |ABRE #214;
     |SELECT #2#214;
     #214#5 = #12#5;
     |LEE 040#214=;
     |SI FSalida = 0;
          |SI #12#7 = "+";
              #9#58 = #9#58 + #12#8;
          |SINO;
              #9#58 = #9#58 - #12#8;
          |FINSI;
     |SINO;
          |SI #12#7 = "+";
              #9#20 = #9#20 + #12#8;
          |SINO;
              #9#20 = #9#20 - #12#8;
          |FINSI;
     |FINSI;
     |CIERRA #214;
|FPRC;

|DEFBUCLE dsmom012w6;
     #12#1;
     ;
     #9#0, #9#1, #9#2, #9#3, #9#4, "    ";
     #9#0, #9#1, #9#2, #9#3, #9#4, "zzzz";
     ;
     NULL, CalculaConsumo;
|FIN;

|PROCESO CalculaGastos;
     |SI #13#7 = "+";
         #9#23 = #9#23 + #13#8;
     |SINO;
         #9#23 = #9#23 - #13#8;
     |FINSI;
|FPRC;

|DEFBUCLE dsmom013w6;
     #13#1;
     ;
     #9#0, #9#1, #9#2, #9#3, #9#4, "    ";
     #9#0, #9#1, #9#2, #9#3, #9#4, "zzzz";
     ;
     NULL, CalculaGastos;
|FIN;

|PROCESO Recalculo;
       |PARA nCampo = 17;  |SI nCampo [ 47;  |HACIENDO nCampo = nCampo + 1;
              |SI nCampo ! 29;  |Y nCampo ! 36; |Y nCampo ! 40;
                   #9nCampo = 0;
              |FINSI;
        |SIGUE;
        #9#57 = "SIN DATOS";
        #9#58 = 0;
        #9#59 = 0;

        |SI #9#2 ! 3; |Y #9#14 ! " ";
             |HAZ RecogeAnterior;
         |FINSI;

        |SI #9#56 ! 7;  |Y #9#56 ! 12;  |Y #9#56 ! 17;
            |SI #9#56 = 3;  |O #9#56 = 8;  |O #9#56 = 13;
                #9#29 = 0;
                #9#30 = 0;
                #9#31 = 0;
                #9#32 = 0;
            |SINO;
                |SI #9#29 = 0;  #9#29 = 5;  |FINSI;
            |FINSI;
        |FINSI;

||  Ingresos;
     #9#17 = 0;
     |BUCLE dsmom010w6;

||  Consumos;
     #9#20 = 0;
     #9#58 = 0;
     |BUCLE dsmom012w6;

||  Gastos;
     #9#23 = 0;
     |BUCLE dsmom013w6;

        |HAZ Totales;
        nIVA  = -1;
        nTri  = -1;

         |SI #9#7 = "X"; |O #9#8 = "X";  |O  #9#9 = "X";
          |O #9#10 = "X";  |O #9#11 = "X";
             nIVA = 0;
             |SI #9#45 ! 0;  |O #9#46 ! 0;
                 nIVA = 1;
             |FINSI;
         |FINSI;

         |SI #9#14 = "X";
             nTri = 0;

             |SI #9#17 ! 0;    nTri = 1;   |FINSI;
             |SI #9#20 ! 0;    nTri = 1;   |FINSI;
             |SI #9#58 ! 0;    nTri = 1;   |FINSI;
             |SI #9#23 ! 0;    nTri = 1;   |FINSI;
             |SI #9#40 ! 0;    nTri = 1;   |FINSI;
             |SI #9#44 ! 0;    nTri = 1;   |FINSI;
         |FINSI;

         |SI nTri = -1;  |Y nIVA = -1;
             #9#57 = "COMPLETADO";
         |FINSI;

         |SI nTri = 1;  |Y nIVA = 1;
             #9#57 = "COMPLETADO";
         |FINSI;

         |SI nTri = -1;  |Y nIVA = 1;
             #9#57 = "COMPLETADO";
         |FINSI;

         |SI nTri = 1;  |Y nIVA = -1;
             #9#57 = "COMPLETADO";
         |FINSI;

         |SI nTri = 0;  |Y nIVA = 0;
             #9#57 = "SIN DATOS";
         |FINSI;

         |SI nTri = 0;  |Y nIVA = 1;
             #9#57 = "SIN TRIBUTA.";
         |FINSI;

         |SI nTri = 1;  |Y nIVA = 0;
             #9#57 = "SIN IVA";
         |FINSI;

         |GRABA 020#9a;
         |LIBERA #9;

     |ABRE #130;
     |DDEFECTO #130;
     #130#0 = #9#0;
     #130#1 = #9#1;
     #130#2 = #9#2;
     #130#3 = 130;
     #130#4 = 0;
     #130#5 = #9#4;
     |LEE 141#130=;
     |SI FSalida = 0;
         #130#9  = #9#20;
         #130#10 = #9#21;
         #130#11 = #9#22;

         #130#12 = #9#23;
         #130#13 = #9#24;
         #130#14 = #9#25;

         |GRABA 020#130a;
     |FINSI;
     |LIBERA #130;
     |CIERRA #130;
|FPRC;

|DEFBUCLE Recalculo;
 #9#1;
 ;
 #91#1, #14#2,   00, 00, #91#4;
 #91#1, #14#2, nUno, 99, #91#4;
 ;
 NULL, Recalculo;
|FIN;

|PROCESO PaseDatos;
     |SI #902#2 = 1;  nPeriodo = 3;   |FINSI;
     |SI #902#2 = 2;  nPeriodo = 6;   |FINSI;
     |SI #902#2 = 3;  nPeriodo = 9;   |FINSI;
     |SI #902#2 = 4;  nPeriodo = 12;  |FINSI;

     #9#0 = #91#1;
     #9#1 = #14#2;
     #9#2 = nPeriodo;
     #9#3 = 0;
     #9#4 = #91#4;
     |LEE 141#9=;
     |SI FSalida ! 0;
         |DDEFECTO #9;
         #9#0 = #91#1;          ||Empresa
         #9#1 = #14#2;          ||Ejercicio
         #9#2 = nPeriodo;       ||Periodo
         #9#3 = 0;              ||Complemento
         #9#4 = #91#4;          ||Actividad
         #9#5 = #91#2;          ||Nombre Empresa
         #9#6 = #114#4;         ||Nombre Actividad
         #9#14 = "X";
         #9#48  = #114#3;
         #9#49  = #114#8;
         #9#50  = #114#10;
         #9#51  = #114#17;
         #9#52  = #114#12;
         #9#53  = #114#5;
         #9#54  = #114#6;
         #9#55  = #114#11;
         #9#56  = #114#9;
         |GRABA 020#9b;
     |FINSI;

     |BUCLE dsmom010B;
     |BUCLE dsmom011B;
     |BUCLE dsmom012B;
     |BUCLE dsmom013B;

     |ABRE #10;
     |ABRE #11;
     |ABRE #12;
     |ABRE #13;

     |BUCLE dsmom014Iw6;
     |BUCLE dsmom014Cw6;
     |BUCLE dsmom014Ew6;
     |BUCLE dsmom014Gw6;

  || .... Ingresos
     |SI #902#19  ! 0; nNume1 = 1;  nImporte = #902#19;  |HAZ Ingresos; |FINSI;
     |SI #902#20  ! 0; nNume1 = 2;  nImporte = #902#20;  |HAZ Ingresos; |FINSI;
     |SI #902#21  ! 0; nNume1 = 3;  nImporte = #902#21;  |HAZ Ingresos; |FINSI;
     |SI #902#22  ! 0; nNume1 = 4;  nImporte = #902#22;  |HAZ Ingresos; |FINSI;
     |SI #902#150 ! 0; nNume1 = 5;  nImporte = #902#150; |HAZ Ingresos; |FINSI;
     |SI #902#23  ! 0; nNume1 = 6;  nImporte = #902#23;  |HAZ Ingresos; |FINSI;
     |SI #902#24  ! 0; nNume1 = 7;  nImporte = #902#24;  |HAZ Ingresos; |FINSI;
     |SI #902#85  ! 0; nNume1 = 8;  nImporte = #902#85;  |HAZ Ingresos; |FINSI;
     |SI #902#154 ! 0; nNume1 = 9;  nImporte = #902#154; |HAZ Ingresos; |FINSI;
     |SI #902#88  ! 0; nNume1 = 10; nImporte = #902#88;  |HAZ Ingresos; |FINSI;
     |SI #902#152 ! 0; nNume1 = 11; nImporte = #902#152; |HAZ Ingresos; |FINSI;
     |SI #902#25  ! 0; nNume1 = 12; nImporte = #902#25;  |HAZ Ingresos; |FINSI;

  || .... Gastos y Consumo
     |SI #902#26  ! 0; nNume1 = 1;  nImporte = #902#26;   |HAZ GastoConsumo; |FINSI;
     |SI #902#27  ! 0; nNume1 = 2;  nImporte = #902#27;   |HAZ GastoConsumo; |FINSI;
     |SI #902#28  ! 0; nNume1 = 3;  nImporte = #902#28;   |HAZ GastoConsumo; |FINSI;
     |SI #902#29  ! 0; nNume1 = 4;  nImporte = #902#29;   |HAZ GastoConsumo; |FINSI;
     |SI #902#30  ! 0; nNume1 = 5;  nImporte = #902#30;   |HAZ GastoConsumo; |FINSI;
     |SI #902#31  ! 0; nNume1 = 6;  nImporte = #902#31;   |HAZ GastoConsumo; |FINSI;
     |SI #902#32  ! 0; nNume1 = 7;  nImporte = #902#32;   |HAZ GastoConsumo; |FINSI;
     |SI #902#33  ! 0; nNume1 = 8;  nImporte = #902#33;   |HAZ GastoConsumo; |FINSI;
     |SI #902#34  ! 0; nNume1 = 9;  nImporte = #902#34;   |HAZ GastoConsumo; |FINSI;
     |SI #902#108 ! 0; nNume1 = 10; nImporte = #902#108;  |HAZ GastoConsumo; |FINSI;
     |SI #902#109 ! 0; nNume1 = 11; nImporte = #902#109;  |HAZ GastoConsumo; |FINSI;
     |SI #902#110 ! 0; nNume1 = 12; nImporte = #902#110;  |HAZ GastoConsumo; |FINSI;
     |SI #902#111 ! 0; nNume1 = 13; nImporte = #902#111;  |HAZ GastoConsumo; |FINSI;
     |SI #902#112 ! 0; nNume1 = 14; nImporte = #902#112;  |HAZ GastoConsumo; |FINSI;
     |SI #902#113 ! 0; nNume1 = 15; nImporte = #902#113;  |HAZ GastoConsumo; |FINSI;
     |SI #902#114 ! 0; nNume1 = 16; nImporte = #902#114;  |HAZ GastoConsumo; |FINSI;
     |SI #902#115 ! 0; nNume1 = 17; nImporte = #902#115;  |HAZ GastoConsumo; |FINSI;
     |SI #902#116 ! 0; nNume1 = 18; nImporte = #902#116;  |HAZ GastoConsumo; |FINSI;
     |SI #902#117 ! 0; nNume1 = 19; nImporte = #902#117;  |HAZ GastoConsumo; |FINSI;
     |SI #902#118 ! 0; nNume1 = 20; nImporte = #902#118;  |HAZ GastoConsumo; |FINSI;
     |SI #902#35  ! 0; nNume1 = 21; nImporte = #902#35 ;  |HAZ GastoConsumo; |FINSI;
     |SI #902#36  ! 0; #9#40 = #902#36; |FINSI;

  || .... Existencias
     |SI #902#48 ! 0; |O #902#49 ! 0; |O #902#47 ! 0;
         |HAZ Existencias;
     |FINSI;

     |CIERRA #13;
     |CIERRA #12;
     |CIERRA #11;
     |CIERRA #10;

    ||  #9#57 = "COMPLETADO";
     |GRABA 020#9a;
     |LIBERA #9;
     nUno = nPeriodo;
|FPRC;

|DEFBUCLE LasPlanillas;
     #902#1004;
     ;
     #901#0, #901#1, #901#2, #14#3;
     #901#0, #901#1, #901#2, #14#4;
     e;
     NULL, PaseDatos;
|FIN;

|PROCESO Pasando;
     #900#0 = #91#5;
     |LEE 040#900=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     #901#0 = #91#5;
     #901#1 = #91#4;
     #901#2 = #91#0;
     |LEE 040#901=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     #114#0 = #91#1;
     #114#1 = #91#4;
     |LEE 040#114=;
     |SI FSalida ! 0;  |CIERRA #114;  |ACABA;  |FINSI;

     nUno = -1;
     |ABRE   #9;
     |BUCLE LasPlanillas;
     |CIERRA #9;
     |SI nUno ! -1;
         |BUCLE Recalculo;
     |FINSI;

     |ABRE #92;
     #92#0 = #91#0;
     #92#1 = #91#1;
     #92#2 = #91#2;
     #92#3 = #91#4;
     #92#4 = #91#5;
     |GRABA 020#92n;
     |LIBERA #92;
     |CIERRA #92;

     |BORRA 020#91a;
     |LIBERA #91;

     |ENTLINEAL #1#92, -2, 7, 22, 1, ClaveBaja92, ClaveAlta92;
     |ENTLINEAL #1#91, -2, 7, 22, 0, ClaveBaja91, ClaveAlta91;
|FPRC;

|DEFBUCLE dspase91;
     #91#1;
     3;
     nAnyo, 00000, 1, "S";
     nAnyo, 99999, 9, "S";
     be;
     NULL, Pasando;
|FIN;

|PROCESO MiraPlanilla;

     |SI nEstaCambio = 1;
         nCodigoNue = 0;
         nCodigoReo = #901#0;
         |BUCLE dspase15;
         |SI nCodigoNue = 0;  |ACABA;  |FINSI;
     |SINO;
          nCodigoNue = #901#0;
     |FINSI;

     |SI  #901#22 ! "B"; |Y #901#22 ! "C";
          |ACABA;
     |FINSI;

     nSw  = 0;
     #902#0  = #901#0;
     #902#83 = #901#1;
     #902#1  = #901#2;
     #902#2  = #14#3 - 1;
     |LEE 040#902];
     |SI FSalida = 0; |Y #902#0 = #901#0;  |Y #902#83 = #901#1; |Y #902#1 = #901#2;
          nSw = 1;
     |FINSI;
     |SI nSw = 0; |ACABA; |FINSI;

     #8#0 = nCodigoNue;
     #8#1 = #14#2;
     #8#2 = 0;
     #8#3 = 0;
     #8#4 = 0;
     #8#5 = #901#1;
     |LEE 040#8];
     |PARA;  |SI;  |HACIENDO;
         |SI FSalida ! 0;  |SAL;  |FINSI;
         |SI #8#0 = nCodigoNue;  |Y #8#1 = #14#2; |Y #8#5 = #901#1;
             |SI #8#3 = 130;
                 |SI #8#7 ! "PLANILLAS "; |Y #8#7 ! "MANUAL    "; |Y #8#7 = "          ";
                     nSw = 0;
                     |SAL;
                 |FINSI;
              |FINSI;
         |SINO;
              |SI #8#0 ! nCodigoNue; |SAL; |FINSI;
         |FINSI;
         |LEE 040#8s;
     |SIGUE;

     #91#0 = nAnyo;
     #91#1 = nCodigoNue;
     #91#4 = #901#1;
     |LEE 040#91=;
     |SI FSalida = 0;  |ACABA;  |FINSI;

     #92#0 = nAnyo;
     #92#1 = nCodigoNue;
     #92#3 = #901#1;
     |LEE 040#92=;
     |SI FSalida = 0;  |ACABA;  |FINSI;

     #91#0 = nAnyo;
     #91#1 = nCodigoNue;
     #91#2 = #901#3;
     #91#3 = "N";
     #91#4 = #901#1;
     #91#5 = #901#0;
     |GRABA 020#91n;
     |LIBERA #91;
|FPRC;

|DEFBUCLE eosactiv;
     #901#1003;
     ;
     nDEmpresa, 1, nAnyo;
     nHEmpresa, 9, nAnyo;
     ;
     NULL, MiraPlanilla;
|FIN;

|PROCESO Reo;

     aPathAnterior   = #14#6;  |QBT aPathAnterior;
     aPathDefFiscal  = aPathAnterior + "agieos/def/";
     aPathFichFiscal = aPathAnterior + "agieos/fich/";

     aDef = aPathdefModelo + "dsmom009.mas";
     |CARGA_DEF aDef, dsmom09@;
     |SI FSalida ! 0;
         |MENAV "     Error en Cargar el Def dsmom009.mas";
         |ACABA;
     |FINSI;

     |PATH_DAT #9  eaPathDsmodelo;
     |PATH_DAT #10 eaPathDsmodelo;
     |PATH_DAT #11 eaPathDsmodelo;
     |PATH_DAT #12 eaPathDsmodelo;
     |PATH_DAT #13 eaPathDsmodelo;
     |PATH_DAT #909 eaPathDsmodelo;

     nDEmpresa = #14#0;
     nHEmpresa = #14#1;

     |NOME_DAT #91 "pase1491";
     |NOME_DAT #92 "pase1492";

     |ABRE #91;
     |ABRE #92;

     aDef = aPathDefFiscal + "eosclien.mas";
     |CARGA_DEF aDef, refer00@;
     |SI FSalida ! 0;
         |MENAV "     Error en Cargar el Def eosclien.mas";
         |DESCARGA_DEF #dsmom09@;
         |ACABA;
     |FINSI;

     aDef = aPathDefFiscal + "eosactiv.mas";
     |CARGA_DEF aDef, refer01@;
     |SI FSalida ! 0;
         |MENAV "     Error en Cargar el Def eosactiv.mas";
         |DESCARGA_DEF #refer00@;
         |DESCARGA_DEF #dsmom09@;
         |ACABA;
     |FINSI;

     aDef = aPathDefFiscal + "eosplan1.mas";
     |CARGA_DEF aDef, refer02@;
     |SI FSalida ! 0;
         |MENAV "     Error en Cargar el Def eosplan1.mas";
         |DESCARGA_DEF #refer01@;
         |DESCARGA_DEF #refer00@;
         |DESCARGA_DEF #dsmom09@;
         |ACABA;
     |FINSI;

     |PATH_DAT #900 aPathFichFiscal;
     |PATH_DAT #901 aPathFichFiscal;
     |PATH_DAT #902 aPathFichFiscal;

     aAlfa   = #14#2;
     aAlfa   = aAlfa % -102;
     nAnyo   = aAlfa;

     enPlan  = 2008; |SI #14#2 < 2008; enPlan = 1990; |FINSI;

     |ABRE #8;
     |ABRE #902;
     |BUCLE eosactiv;
     |CIERRA #902;
     |CIERRA #8;

     |CIERRA #91;
     |CIERRA #92;

     |ENTLINEAL #1#92, -2, 7, 22, 0, ClaveBaja92, ClaveAlta92;
     |ENTLINEAL #1#91, -2, 4, 22, 0, ClaveBaja91, ClaveAlta91;

     |CONFI "0000SRealizamos el pase en estos Momentos";
     |SI FSalida = 0;
         |ABRE #900;
         |ABRE #901;
         |ABRE #902;
         |ABRE #114;
         |BUCLE dspase91;
         |CIERRA #114;
         |CIERRA #902;
         |CIERRA #901;
         |CIERRA #900;
     |FINSI;

     |DESCARGA_DEF #refer02@;
     |DESCARGA_DEF #refer01@;
     |DESCARGA_DEF #refer00@;
     |DESCARGA_DEF #dsmom09@;
|FPRC;


|| //////////////////////////////////////////////////////////////////////////

|PROGRAMA;

    |ABRE #0;
    |LEE 040#0p;
    |CIERRA #0;

    |ABRE #15;
    |DDEFECTO #15;
    |LEE 040#15p;
    |SI FSalida ! 0;
        nEstaCambio = 0;
        |DDEFECTO #15;
    |SINO;
        nEstaCambio = 1;
        |SI #15#15 ! "S"; nEstaCambio = 0; |FINSI;
    |FINSI;
    |CIERRA #15;

    |DBASE aBase;  |QBF aBase;
    aPathPases = aBase + "pases";  |MKDIR aPathPases;
    aPathPases = aPathPases + "/";

    |HAZ BusquedaApli;
    aPathdefModelo = eaPathDsmodelo + "def/";
    eaPathDsmodelo = eaPathDsmodelo + "fich/";

    |CLS;
    |CABEZA "     Pase de Planillas de Reo";
    |PINPA #0#14;

    #14#6  = #0#0;
    |PINDA #0#14;

    |ET Pido;
    |C_M #14#6, 1, "S";
    |C_M #14#7, 1, "S";

    |ENCAMPO #7#14;
    |SI FSalida = 7; |O nSwError = 1; |HAZ DesCargaDef; |ACABA; |FINSI;
    |ENCAMPO #6#14;
    |SI FSalida = 7; |O nSwError = 1; |HAZ DesCargaDef; |ACABA; |FINSI;
    |SI nSwLocal = 2; |VETE Pido; |FINSI;

    |ENDATOS #1#14;
    |SI FSalida ! 0;   |HAZ DesCargaDef; |ACABA;  |FINSI;
    |SI #14#5 ! "SI";  |HAZ DesCargaDef; |ACABA;  |FINSI;

    |HAZ DesCargaDef;
    |HAZ Reo;

    |SI #14#7 = "S"; |HAZ LimpioAuxi; |FINSI;
|FPRO;
|| *************************************************************************

|PROCESO DesCargaDef;
     |SI aDef330 ! "";   |DESCARGA_DEF #agifige@; aDef330 = ""; |FINSI;
|FPRC;

|PROCESO CargaEmpReo;
     nSwError = 0;
     aMensa   = "";
     nSwLocal = 0;

     aDirBase = #14#6; |QBF aDirBase;
     |SI #14#7 = "S";
         aBasLocal = aDirBase;
         |HAZ TraeAplic;
         #14#6 = aDirBase;
     |FINSI;

     |SI nSwLocal = 2; |ACABA; |FINSI;

     aPathDef = aDirBase + "agieos/def/";

     aDef330 = aPathDef + "eosclien";
     |CARGA_DEF aDef330, agifige@;
     |SI FSalida ! 0;
         aMensa   = "    Error en:" + aDef330;
         aDef330  = "";
         nSwError = 1;
         |ACABA;
     |FINSI;
|FPRC;

|PROCESO LimpioAuxi;
   |MENSA "0000Borrando Datos Auxiliares. Espere, por favor...";

   aPath = aDirBase + "agieos/fich/";
   |HAZ PreparaDir;
   aPath = aDirBase + "agieos/def/";
   |HAZ PreparaDir;
   aPath = aDirBase + "agieos/"
   |HAZ PreparaDir;

   |BLIN 4;
|FPRC;

|PROCESO PreparaDir;
  aDirTempo  = aPath + "*";
  |_PDIR aDirTempo;
  |PARA; |SI; |HACIENDO;
         |SI FSalida ! 0; |SAL; |FINSI;

         |FBORRA aDirTempo;
         |_SDIR aDirTempo;
  |SIGUE;
|FPRC;

|PROCESO TraeAplic;
   |MENSA "0000Traspasando aplicacion. Espere, por favor...";

   nSwLocal = 0;
   |DBASS aBase;
   aAlfa1 = #14#6; |QBF aAlfa1;
   |SI aBase = aAlfa1;
       #14#7 = "N"; |PINTA #14#7;
       aBasLocal = "";
       nSwLocal = 1;
       |ACABA;
   |FINSI;

   aIPRemoto = "";
   |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;
   |SI aIPRemoto =  "";
       aBasLocal = "";
       #14#7     = "N"; |PINTA #14#7;
       nSwLocal  = 1;
       |ACABA;
   |FINSI;

   |HAZ VeoPosible;
   |SI nSwLocal ! 0; |ACABA; |FINSI;

   aPath = aBase + "star3/agieos";
   |MKDIR aPath;
   aPath = aPath + "/";
   |HAZ PreparaDir;

   aPath = aBase + "star3/agieos/def";
   |MKDIR aPath;
   aPath = aPath + "/";
   |HAZ PreparaDir;

   aPath = aBase + "star3/agieos/fich";
   |MKDIR aPath;
   aPath = aPath + "/";
   |HAZ PreparaDir;


|| Paso Datos desde C al Servidor
   aPath = aBase + "star3/";
   aDirBase = aPath;

   aDirOrigen  = aBasLocal + "agieos/def/";
   aDirDestino = aPath     + "agieos/def/";
   aAlfa = "mas";  |HAZ PasaFicheros;

   aDirOrigen = aBasLocal + "agieos/fich/";
   aDirDestino = aPath    + "agieos/fich/";
   aAlfa = "dat";  |HAZ PasaFicheros;

   |BLIN 4;
|FPRC;

|PROCESO VeoPosible;
  nSwLocal = 2;
  aDirTempo  = aBasLocal + "agieos/fich/";
  |R_PDIR aDirTempo;
  |SI FSalida = 0;
      nSwLocal = 0;
  |SINO;
      |_PDIR aDirTempo;
      |SI FSalida = 0; nSwLocal = 1; |FINSI;
  |FINSI;
  |SI nSwLocal = 1; aBasLocal = ""; #14#7 = "N"; |PINTA #14#7; |FINSI;
  |SI nSwLocal = 2;
      |MENAV "    ERROR. NO EXISTEN DATOS EN EL DIRECTORIO INDICADO";
  |FINSI;
|FPRC;

|PROCESO CopiaFichero;
     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;
     |SI aIPRemoto ! "";
         |RECIBE_FICHERO aOrigen, aDestino;
     |SINO;
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;
|FPRC;

|PROCESO PreparaDir_local;  || Borra directorio recdatos y quidatos
  aAlfa1 =  aBasLocal + "agieos/pase12"
  |RMKDIR aAlfa1;
  aPathRec   = aAlfa1 + "/";
  aDirTempo  = aPathRec + "*";
  |R_PDIR aDirTempo;
  |PARA; |SI; |HACIENDO;
         |SI FSalida ! 0; |SAL; |FINSI;

         |RFBORRA aDirTempo;
         |R_SDIR aDirTempo;
  |SIGUE;
|FPRC;

|PROCESO PasaFicheros;

   |HAZ PreparaDir_local;

   aFich    = "zLaReo";
   |SI aAlfa ! "dat";
       aFichTar = aPathRec + aFich + ".tar *." + aAlfa;
   |SINO;
       aFichTar = aPathRec + aFich + ".tar eos* agi*";
   |FINSI;

   aOrigen = aDirOrigen;
   |RATAR aFichTar, aOrigen;

   aFichTar  = aPathRec + aFich + ".tar";
   |RCOMPRIME aFichTar;

   |RFBORRA aFichTar;

   aOrigen  = aPathRec + aFich + ".tgz";
   aDestino = aDirDestino + aFich + ".tgz";
   |HAZ CopiaFichero;

   aFichTar   = aDestino;
   |DESCOMPRIME aFichTar;
   |SI FSalida = 0;
        aFichTar  = aDirDestino + aFich + ".tar";
        |DETAR aFichTar, aDirDestino;
        |FBORRA aFichTar;

        aFichTar   = aDestino;
        |FBORRA aFichTar;
   |FINSI;
|FPRC;
