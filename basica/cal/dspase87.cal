|FICHEROS;
     dspase00 #0;
     dspase87 #87;
     dspase88 #88,MANTE;
     refer00@ #900;
     refer01@ #901;
|FIN;

|VARIABLES;
     aPathDefFiscal  = "";
     aPathFichFiscal = "";
     aPathAnterior   = "";
     aDef            = "";
     aAlfa           = "";
     aBase           = "";
     aPathPases      = "";
     aOrigen         = "";
     aDestino        = "";
     &eaCta          = "";
|FIN;

|PROCESO PonGrupo88;
     aAlfa = #88#3 % 103;
     #88#5 = aAlfa;  |PINTA #88#5;
     #88#6 = #88#2;  |PINTA #88#6;
|FPRC;

|PROCESO MCta_A; |TIPO 0;
     eaCta = #87Campo;
     |SI Campo = 3;
         |QBF eaCta; aAlfa = eaCta % -101;
         |SI aAlfa = "*"; |ACABA; |FINSI;
     |FINSI;
     |HAZ MFiltraPtos;
     #87Campo = eaCta; |PINTA #87Campo;
|FPRC;

|PROCESO MCta_M; |TIPO 0;
     eaCta = #88Campo;
     |SI Campo = 4;
         |QBF eaCta; aAlfa = eaCta % -101;
         |SI aAlfa = "*"; |ACABA; |FINSI;
     |FINSI;
     |HAZ MFiltraPtos;
     #88Campo = eaCta; |PINTA #88Campo;
|FPRC;

|PROCESO Tipo1188;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     |LEE 101#88=;
     |SI FSalida = 0;
         |ENDATOS #1#88;
         |SI FSalida = 0;
             |GRABA 020#88a;
         |FINSI;
     |FINSI;
     |LIBERA #88;
|FPRC;

|PROCESO PonGrupo87;
     aAlfa = #87#2 % 103;
     #87#4 = aAlfa;  |PINTA #87#4;
     #87#5 = #87#1;  |PINTA #87#5;
|FPRC;

|PROCESO Tipo1187;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     |LEE 101#87=;
     |SI FSalida = 0;
         |ENDATOS #1#87;
         |SI FSalida = 0;
             |GRABA 020#87a;
         |FINSI;
     |FINSI;
     |LIBERA #87;
|FPRC;

|PROCESO PonConcepto;
     #87#0 = #900#9;
     |LEE 101#87=;
     |SI FSalida ! 0;
         |DDEFECTO #87;
         #87#0 = #900#9;
         #87#1 = #900#15;
         |GRABA 020#87n;
     |FINSI;
     |LIBERA #87;
|FPRC;

|DEFBUCLE eosamorc;
     #900#1003;
     ;
     00001, 00, 00000;
     99999, 99, 99999;
     ;
     NULL, PonConcepto;
|FIN;

|PROCESO PonConceptoM;
     #901#0 = #900#10;
     |LEE 040#901=;
     |SI FSalida ! 0;  |DDEFECTO #901;  |FINSI;

     #88#0 = #900#4;
     #88#1 = #900#10;
     |LEE 101#88=;
     |SI FSalida ! 0;
         |DDEFECTO #88;
         #88#0 = #900#4;
         #88#1 = #900#10;
         #88#2 = #901#1;
         |SI #88#0 = 0;  #88#10 = "INMOVILIZADO MATERIAL NUEVO             ";  |FINSI;
         |SI #88#0 = 1;  #88#10 = "EDIFICIOS Y OTRAS CONSTRUCCIONES        ";  |FINSI;
         |SI #88#0 = 2;  #88#10 = "UTILES, HERRAMIENTAS Y EQUIPOS          ";  |FINSI;
         |SI #88#0 = 3;  #88#10 = "ELEMENTOS DE TRANSPORTES Y RESTO        ";  |FINSI;
         |SI #88#0 = 4;  #88#10 = "INMOVILIZADO INMATERIAL                 ";  |FINSI;
         |SI #88#0 = 5;  #88#10 = "VACUNO, PORCINO, OVINO Y CAPRINO        ";  |FINSI;
         |SI #88#0 = 6;  #88#10 = "EQUINO Y FRUTALES NO CITRICOS           ";  |FINSI;
         |SI #88#0 = 7;  #88#10 = "FRUTALES CITRICOS Y VI¥EDOS             ";  |FINSI;
         |SI #88#0 = 8;  #88#10 = "OLIVAR                                  ";  |FINSI;
         |GRABA 020#88n;
     |FINSI;
     |LIBERA #88;
|FPRC;

|DEFBUCLE eosm0023;
     #900#1003;
     ;
     00001, 1, 00000;
     99999, 6, 99999;
     ;
     NULL, PonConceptoM;
|FIN;

|PROCESO TraeConceptos;
     aPathAnterior   = #0#0;  |QBT aPathAnterior;

     |HAZ BusquedaApli;
     aPathDefFiscal  = aPathAnterior + "agieos/def/";
     aPathFichFiscal = aPathAnterior + "agieos/fich/";

     aDef = aPathDefFiscal + "eosamorc.mas";
     |CARGA_DEF aDef, refer00@;
     |SI FSalida ! 0;
         |MENAV "     Error en Cargar el Def eosamorc.mas";
         |ACABA;
     |FINSI;

     |PATH_DAT #900 aPathFichFiscal;

     |BUCLE eosamorc;
     |DESCARGA_DEF #refer00@;

     aDef = aPathDefFiscal + "eosm0023.mas";
     |CARGA_DEF aDef, refer00@;
     |SI FSalida ! 0;
         |MENAV "     Error en Cargar el Def eosm0023.mas";
         |ACABA;
     |FINSI;

     aDef = aPathDefFiscal + "eosconce.mas";
     |CARGA_DEF aDef, refer01@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #refer00@;
         |MENAV "     Error en Cargar el Def eosconce.mas";
         |ACABA;
     |FINSI;

     |PATH_DAT #900 aPathFichFiscal;
     |PATH_DAT #901 aPathFichFiscal;

     |ABRE #901;
     |BUCLE eosm0023;
     |CIERRA #901;

     |DESCARGA_DEF #refer00@;
     |DESCARGA_DEF #refer01@;
|FPRC;

|RUTINA ClaveBaja87;
     #87#0 = 00;
|FRUT;

|RUTINA ClaveAlta87;
     #87#0 = 99;
|FRUT;

|RUTINA ClaveBaja88;
     #88#0 = 0;
     #88#1 = 00;
|FRUT;

|RUTINA ClaveAlta88;
     #88#0 = 9;
     #88#1 = 99;
|FRUT;

|PROGRAMA;
     |ABRE #0;
     |LEE 040#0p;
     |SI FSalida ! 0;
         |MENAV "     Pase de Ficheros Generales no Realizado";
         |PTEC 807;
         |CIERRA #0;
         |ACABA;
     |FINSI;
     |CIERRA #0;

     |DBASE aBase;  |QBF aBase;
     aPathPases = aBase + "pases";  |MKDIR aPathPases;
     aPathPases = aPathPases + "/";

     |PATH_DAT #87 aPathPases;
     |PATH_DAT #88 aPathPases;

     |ABRE #87;
     |LEE 040#87p;
     |SI FSalida ! 0;
         |CIERRA #87;
         aOrigen  = aBase + "fich/dspase87.dat";
         aDestino = aPathPases + "dspase87.dat";
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;
     |CIERRA #87;

     |ABRE #88;
     |LEE 040#88p;
     |SI FSalida ! 0;
         |CIERRA #88;
         aOrigen  = aBase + "fich/dspase88.dat";
         aDestino = aPathPases + "dspase88.dat";
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;
     |CIERRA #88;

     |ABRE #87;
     |LEE 040#87p;
     |SI FSalida ! 0;
         |ABRE #88;  |CIERRA #88;  |DELINDEX #88;

         |ABRE #88;
         |HAZ TraeConceptos;
         |CIERRA #88;
     |FINSI;
     |CIERRA #87;

     |PINPA #0#87;
     |ENTLINEAL #1#88, -2, 7, 22, 1, ClaveBaja88, ClaveAlta88;
     |ENTLINEAL #1#87, -1, 4, 12, 0, ClaveBaja87, ClaveAlta87;
     |ENTLINEAL #1#87, -1, 7, 12, 0, ClaveBaja87, ClaveAlta87;
     |ENTLINEAL #1#88, -2, 4, 22, 0, ClaveBaja88, ClaveAlta88;
|FPRO;
