|FICHEROS;
     dsborrpa #0;
     agifigen #1;

     espejo@  #900;
     dsam0000@;
|FIN;

|VARIABLES;
     nInkey       = 0;
     nBoton1      = 0;
     nBoton2      = 0;
     nBoton3      = 0;
     nCampo       = 0;
     nHandle      = 0;
     nLong        = 0;
     nCaracter    = 0;
     nPos         = 0;

     aAlfa        = "";
     aFichero     = "";
     aFichCSV     = "";
     aPassword    = "";
     aMas         = "";
     aBase        = "";
     aRuta        = "";
     aCampoA      = "";
     aCampoB      = "";
     aCadena      = "";
     aLong        = "";
     aTab         = "";
     aCarac1      = "";

     nDCliente    = 0;
     nHCliente    = 0;
     nSw          = 0;
     nQueHago     = 0;

     &enEmpresa   = 0;
     &enBajaCli   = 0;
     &enBorraTodo = 0;
|FIN;

|PROCESO agifigen;
     |PINTA 0917, #0#0;
     |PINTA 0925, #0#1;

     enEmpresa   = #1#0;
     enBajaCli   = 1;
     enBorraTodo = 1;

     |SUB_EJECUTA "dsborrad";

     |BORRA 020#0a;
|FPRC;

|PROCESO dsborrpa;
     #1#0 = #0#0;
     |LEE 000#1=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |HAZ agifigen;
|FPRC;

|DEFBUCLE dsborrpa;
     #0#1;
     ;
     00001;
     99999;
     ;
     NULL, dsborrpa;
|FIN;

|| ////////////////////////////////////////////////////////////////////////
|PROCESO LeeClientes;
     |SI nQueHago = 2;
         #dsam0000@#0 = #agifigen#0;
         |LEE 000#dsam0000@.=;
         |SI FSalida ! 0;          |ACABA;  |FINSI;
         |SI #dsam0000@#85 ! "I";  |ACABA;  |FINSI;
     |FINSI;

     #900#0 = #1#0;
     |LEE 041#900=;
     |SI FSalida ! 0;
         #900#0 = #1#0;
         #900#1 = #1#1;
         |GRABA 020#900n;
     |FINSI;
|FPRC;

|DEFBUCLE LeeClientes;
     #1#1;
     ;
     nDCliente;
     nHCliente;
     ;
     NULL, LeeClientes;
|FIN;

|PROCESO Boton1m004;
     |PTEC 825;
|FPRC;

|PROCESO Boton2m004;
     |PTEC 824;
|FPRC;

|PROCESO Boton3m004;
     |PTEC 826;
|FPRC;

|PROCESO IniBotonpa;
     |BLIN 04;
     |CONTROL_SIMPLE 0, "LBOTON,F2 Recoger Codigos Clientes", 0860, 0880, Boton2m004;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F3 Recoger clientes inactivos", 1060, 1080, Boton1m004;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F4 Recoger de fichero CSV", 1260, 1280, Boton3m004;
     nBoton3 = FSalida;
|FPRC;

|PROCESO FinBotonpa;
     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;
     |PULSATECLA;
|FPRC;

|PROCESO Tipo7pa;  |TIPO 7;
     |ESTADO_CONTROL nBoton1,  "ENABLE";
     |ESTADO_CONTROL nBoton2,  "ENABLE";
     |ESTADO_CONTROL nBoton3,  "ENABLE";
|FPRC;

|PROCESO Tipo0pa;  |TIPO 0;
     |ESTADO_CONTROL nBoton1,  "DISABLE";
     |ESTADO_CONTROL nBoton2,  "DISABLE";
     |ESTADO_CONTROL nBoton3,  "DISABLE";
|FPRC;

|PROCESO DesdeHasta;
         nSw = 0;
         |PUSHV 0653 1077;
         |PINTA 0653, "                        ";
         |PINTA 0753, "                        ";
         |PINTA 0853, "                        ";
         |PINTA 0953, "                        ";
         |CUADRO 0653 0977;
         |PINTA 0754, " Desde Cliente:       ";
         |PINTA 0854, " Hasta Cliente:       ";

         |SI nDCliente = 0; nDCliente = 1; |FINSI;

         |ET CampoDesde;
             E_inf = "00001";
             E_sup = "99999";
             nDCliente = 0770 ? 1;

             E_inf = "00001";
             E_sup = "99999";
             nHCliente = 0870 ? 1;
             |SI nDCliente > nHCliente;
                 |VETE CampoDesde;
             |FINSI;

         |CONFI "    SES CORRECTO";
         |SI FSalida = 0;
             nSw = 1;
         |FINSI;

         |POPV;
|FPRC;

|PROCESO LeeCSV;
     |XABRE "C", aFichCSV, nHandle;
     |PARA;  |SI FSalida ] 0;  |HACIENDO;
          |XLEE nHandle, 250, aAlfa;
          |SI FSalida < 0;  |SAL;  |FINSI;

          aCampoA  = "";
          aCampoB  = "";

          aCadena = aAlfa;  |QBF aCadena;
          aLong   = aCadena % 0;
          nLong   = aLong;
          nCampo  = 1;
          aTab    = ";";

          |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
                nPos    = (100 * nCaracter) + 1;
                aCarac1 = aCadena % nPos;
                |SI aCarac1 = aTab;
                    nCampo = nCampo + 1;
                |SINO;
                    |SI nCampo = 1;  aCampoA  = aCampoA  + aCarac1;  |FINSI;
                    |SI nCampo = 2;  aCampoB  = aCampoB  + aCarac1;  |FINSI;

                    |SI nCampo ] 3;
                        |SAL;
                    |FINSI;
                |FINSI;
          |SIGUE;

          |QBF aCampoA;

          |SI aCampoA ! "";
              #900#0 = aCampoA;
              |LEE 000#900=;
              |SI FSalida ! 0;
                  |DDEFECTO #900;
                  #900#0 = aCampoA;
                  #900#1 = aCampoB;
                  |GRABA 020#900n;
                  |LIBERA #900;
              |FINSI;
          |FINSI;
     |SIGUE;
     |XCIERRA nHandle;

     |PULSATECLA;
|FPRC;

|PROCESO Tipo11pa; |TIPO 11;
     nInkey = FSalida;

     |ESTADO_CONTROL nBoton1,  "DISABLE";
     |ESTADO_CONTROL nBoton2,  "DISABLE";
     |ESTADO_CONTROL nBoton3,  "DISABLE";

     |SI nInkey = 10;
         |HAZ DesdeHasta;
         |SI nSw = 0; |ACABA; |FINSI;

         |DBASE aBase;  |QBF aBase;
         aMas = aBase + "def/dsborrpa.mas";
         |CARGA_DEF aMas, espejo@
         |SI FSalida ! 0;
             |MENAV "    Error al Cargar def dsborrpa";
             |ACABA;
         |FINSI;
         |NOME_DAT #900 aFichero;

         |ABRE #900;
         nQueHago = 1;  |BUCLE LeeClientes;
         |CIERRA #900;
         |DESCARGA_DEF #espejo@;

         |PONCONTROL  23, "SI";
     |FINSI;

     |SI nInkey = 11;
         |DBASS aBase;  |QBF aBase;
         aMas = aBase + "integral/def/dsam0000.mas";
         |CARGA_DEF aMas, dsam0000@
         |SI FSalida ! 0;
             |MENAV "    Debe tener instalada la ipintegr";
             |ACABA;
         |FINSI;

         |DBASE aBase;  |QBF aBase;
         aMas = aBase + "def/dsborrpa.mas";
         |CARGA_DEF aMas, espejo@
         |SI FSalida ! 0;
             |MENAV "    Error al Cargar def dsborrpa";
             |ACABA;
         |FINSI;
         |NOME_DAT #900 aFichero;

         nDCliente = 1;
         nHCliente = 99999;

         |ABRE #900;
         |ABRE #dsam0000@;
         nQueHago = 2;  |BUCLE LeeClientes;
         |CIERRA #dsam0000@;
         |CIERRA #900;


         |DESCARGA_DEF #dsam0000@;
         |DESCARGA_DEF #espejo@;

         |PONCONTROL  23, "SI";
     |FINSI;

     |SI nInkey = 12;
         |MENAV "0000Fichero CSV: (Columna A - Codigo)  (Columna B - Nombre cliente)";
         |MENAV "0000A continuacion busque el fichero CSV.";

         aRuta = "F*.csv";
         |BROWSE aRuta;  |QBF aRuta;
         |SI aRuta = "F";
             |MENAV "0000Importacion cancelada.";
             |ACABA;
         |FINSI;

         aFichCSV = aRuta % 280;
         |XABRE "EC", aFichCSV, 0;
         |SI FSalida < 0;
             |MENAV "0000Importacion cancelada.";
             |ACABA;
         |FINSI;

         aAlfa = (aFichCSV % -103) < "";
         |SI aAlfa ! "csv";
             |MENAV "0000El fichero debe tener extension CSV.";
             |ACABA;
         |FINSI;

         |DBASS aBase;  |QBF aBase;
         aMas = aBase + "integral/def/dsam0000.mas";
         |CARGA_DEF aMas, dsam0000@
         |SI FSalida ! 0;
             |MENAV "    Debe tener instalada la ipintegr";
             |ACABA;
         |FINSI;

         |DBASE aBase;  |QBF aBase;
         aMas = aBase + "def/dsborrpa.mas";
         |CARGA_DEF aMas, espejo@
         |SI FSalida ! 0;
             |MENAV "    Error al Cargar def dsborrpa";
             |ACABA;
         |FINSI;
         |NOME_DAT #900 aFichero;

         |ABRE #900;
         |HAZ LeeCSV;
         |CIERRA #900;

         |DESCARGA_DEF #espejo@;

         |PONCONTROL  23, "SI";
     |FINSI;
|FPRC;

|PROCESO Baja0;
     #0#0 = 1;
|FPRC;

|PROCESO Alta0;
     #0#0 = 99999;
|FPRC;

|PROGRAMA;
     |PUSHV 0501 2380;
     |CUADRO 0501 2380;
     |BLANCO 0602 2279;
     |PINTA 1320, "Introduzca Password : ";
     E_inf = "";
     E_sup = "20";
     aPassword = 1342 ? -1;
     |SI aPassword ! "<MYPASWD>           ";
         |MENAV "     Permiso denegado.";
         |POPV;
         |ACABA;
     |FINSI;
     |POPV;

     |CLS;
     |CABEZA "Borrado de Empresas";

     aFichero = ("BB" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #0aFichero;

     |HAZ IniBotonpa;

     |ENTLINEAL #1#0, -1, 1, 19, 1, Baja0, Alta0;

     |HAZ FinBotonpa;

     |CONFI "0000NProcedemos a borrar los codigos seleccionados.";
     |SI FSalida = 0;
         |ABRE #1;
         |BUCLE dsborrpa;
         |CIERRA #1;
         |DELINDEX #0;
     |SINO;

         |CONFI "0000NBorrar Tabla de Relacion de Codigos a Borrar";
         |SI FSalida = 0;
             |DELINDEX #0;
         |FINSI;

     |FINSI;
|FPRO;
