|FICHEROS;
     agiy0012 #0;   || Este
     agifigen #1;   || Clientes
     agim0002 #2;   || Rendimientos
     agicen14 #14;  || Actividades
     agim0015 #95;  || Salario Minimo
     agiw0021 #96;
|FIN;

|VARIABLES;
     i          = 0;
     SwPrint    = 0;
     nActividad = 0;
     nCampo     = 0;
     aEstadoEmp = "";
     nSPin1     = 0;
     nSwSelEmp  = 0;
     aDSit      = "";
     aHSit      = "";
     nDTEm      = 0;
     nHTEm      = 0;
|FIN;

|INCLUYE i_consul;
|INCLUYE i_masivo;

|PROCESO selec; |TIPO 0;
     |SI #0#0 = "S";
          |C_M #0#1, 1, "S";
          |C_M #0#2, 1, "S";
          |PARA nCampo = 3; |SI nCampo [ 22; |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo,1,"N";
               #0nCampo = 0; |PINTA #0nCampo;
          |SIGUE;
     |SINO;
          |C_M #0#1, 1, "N"; #0#1 = 00001; |PINTA #0#1;
          |C_M #0#2, 1, "N"; #0#2 = 99999; |PINTA #0#2;
          |PARA nCampo = 3; |SI nCampo [ 22; |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo,1,"S";
          |SIGUE;
     |FINSI;
|FPRC;
|CALCULO NoMasEmpresas; |TIPO 0;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 22;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 22;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO SelTipo0; |TIPO 0;
 |SI #0#23 = "N";
     #0#24 = 0;  |PINTA #0#24;  |C_M #0#24, 1, "N";
     #0#25 = 99; |PINTA #0#25;  |C_M #0#25, 1, "N";
     |PARA nCampo = 26;  |SI nCampo [ 37;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |SINO;
     |C_M #0#24, 1, "S";
     |C_M #0#25, 1, "S";
     |PARA nCampo = 26;  |SI nCampo [ 37;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "N";
           #0nCampo = -1;  |PINTA #0nCampo;
     |SIGUE;
 |FINSI;
|FCAL;

|CALCULO SelTipo1;
 |SI #0Campo = -1;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 37;  |HACIENDO nCampo = nCampo + 1;
           #0nCampo = -1;  |C_M #0nCampo, 1, "N";
     |SIGUE;
 |SINO;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 37;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |FINSI;
|FCAL;

||---------------------------------------------------------------
|PROCESO Rendimiento;
     |SI nActividad ! 0;  |Y nActividad ! #2#1;
         |PIEINF;
     |FINSI;

     nActividad = #2#1;

     #14#0 = #2#0;
     #14#1 = #2#1;
     |LEE 040#14=;
     |SI FSalida ! 0;  |DDEFECTO #14;  |FINSI;

     SwPrint = 1;
     |PRINT;
|FPRC;

|DEFBUCLE BRendimiento;
     #2#1;
     ;
     #1#0, 01, 1900;
     #1#0, 99, 2999;
     ;
     NULL, Rendimiento;
|FIN;

|PROCESO Cliente;
     aEstadoEmp = "S"; |SI #1#94 > "01.01.0000";  aEstadoEmp = "N";  |FINSI;
     |SI aEstadoEmp < aDSit; |O aEstadoEmp > aHSit;
         |ACABA;
     |FINSI;

   || ... Seleccion de Tipo de Empresa
     nSPin1 = #1#87;
     nSwSelEmp = 0;
     |SI #0#23 ! "S";
         nSwSelEmp = 1;
         |SI nSPin1 = #0#26;
             nSwSelEmp = 0;
         |SINO;
             |PARA nCampo = 27;  |SI nCampo [ 37;  |HACIENDO nCampo = nCampo + 1;
                   |SI nSPin1 = #0nCampo;
                       nSwSelEmp = 0;
                       |SAL;
                   |FINSI;
             |SIGUE;
         |FINSI;
     |FINSI;
     |SI nSwSelEmp = 1; |ACABA; |FINSI;

     SwPrint    = 0;
     nActividad = 0;
     |ABRE #14;
     |BUCLE  BRendimiento;
     |CIERRA #14;
     |SI SwPrint = 1; |PIEINF; |FINSI;
|FPRC;

|DEFBUCLE BCliente;
     #1#1;
     87;
     #0#1, nDTEm;
     #0#2, nHTEm;
     ;
     NULL, Cliente;
|FIN;

|PROCESO imprime; |TIPO 3;

     |SI enSwMvo = 0;
         |IMPRE 0;
         |SI FSalida ! 0; |ACABA; |FINSI;
     |FINSI;

 || ...  Situacion Activa/Inactiva
     |SI #0#38 = "X";
         aDSit = "A"; aHSit = "Z";
     |SINO;
          aDSit = #0#38; aHSit = #0#38;
     |FINSI;

 || ... Tipo Empresa
      |SI #0#23 = "S";
          nDTEm = #0#24; nHTEm = #0#25;
      |SINO;
          nDTEm = 0; nHTEm = 99;
      |FINSI;

     |ABRE #95;
     |INFOR "agiy0012";
     |SI FSalida = 0;
         |SI #0#0 = "S";
             |BUCLE BCliente;
         |SINO;
             |PARA i = 3; |SI i [ 22; |HACIENDO i = i + 1;
                   |SI #0i > 0;
                       #0#1 = #0i;
                       #0#2 = #0i;
                       |BUCLE BCliente;
                   |FINSI;
              |SIGUE;
         |FINSI;
         |FININF;
     |FINSI;
     |CIERRA #95;

     |SI enSwMvo = 0;
        |FINIMP;
     |FINSI;
|FPRC;

|PROGRAMA;
 |SI enSwMvo = 0;
     |MANTE #0;
 |SINO;
     |DDEFECTO #0;
     #0#0 = "S";
     #0#1 = enEmprM;
     #0#2 = enEmprM;
     |HAZ imprime;
 |FINSI;
|FPRO;
