|FICHEROS;
     agiz0011 #0;         || Este Def
     agim0013 #1,MANTE;   || Entrevistas
     agim0014 #100,MANTE; || Textos Entrevistas
     &usuarios@ #712;
     &clientes@ #715;
|FIN;

|VARIABLES;
     nDEmpresa  = 0;
     nHEmpresa  = 99999;
     fDFecha    = @;
     fHFecha    = @;
     Modo       = 0;
     nModo      = 0;

     &enEmpresa = 0;
     &eaCodUsu  = "";
     &enCodCli  = 0;
|FIN;

|RUTINA ClaveBaja1;
  #1#0 = fDFecha;
  #1#1 = nDEmpresa;
|FRUT;

|RUTINA ClaveAlta1;
  #1#0 = fHFecha;
  #1#1 = nHEmpresa;
|FRUT;

|PROCESO Tipo3; |TIPO 3;
  fDFecha = #0#0;
  fHFecha = #0#0;
  |PINPA #0#1;
  |ENTLINEAL #1#1, -2, 1, 22, 0, ClaveBaja1, ClaveAlta1;
|FPRC;

|| ************************************************************************
||                     PROCESO DEL FICHERO AGIM0013
|| ************************************************************************

|PROCESO PintaUsuario;
     eaCodUsu = #1#5;
     |HAZ LeeUsuario;

     #1#6 = #712#1;
     |PINTA #1#6;
|FPRC;

|PROCESO PintaCliente;
     enCodCli = #1#1;
     |HAZ LeePersona;

     #1#2 = #715#1;
     |PINTA #1#2;
|FPRC;

|PROCESO Borra100;
  |BORRA 140#100a;
  |LIBERA #100;
|FPRC;

|DEFBUCLE agim0014;
  #100#1;
  ;
  #1#0, #1#1, 1;
  #1#0, #1#1, 200;
  be;
  NULL, Borra100;
|FIN;

|PROCESO Tipo2; |TIPO 2;
  Modo = (FEntrada / 100) ! 0;
  |SI Modo ! 3; |ACABA; |FINSI;

  |BUCLE agim0014;
|FPRC;

|| ************************************************************************
||                     PROCESO DEL FICHERO AGIM0014
|| ************************************************************************

|VARIABLES;
     XPara = 0;
     Tecla = 0;
     VAlfa = "";
|FIN;

|RUTINA ClaveBaja100;
  #100#0 = #1#0;
  #100#1 = #1#1;
  #100#2 = 1;
|FRUT;

|RUTINA ClaveAlta100;
  #100#0 = #1#0;
  #100#1 = #1#1;
  #100#2 = 200;
|FRUT;

|PROCESO Texto;
  |PUSHV 0501 2380;
  |ABRET #100;
  |PARA XPara = 1; |SI XPara [ 200; |HACIENDO XPara = XPara + 1;
        #100#0 = #1#0;
        #100#1 = #1#1;
        #100#2 = XPara;
        |LIBERA #100;
        |LEE 140#100=;
        |SI FSalida ! 0;
            #100#0 = #1#0;
            #100#1 = #1#1;
            #100#2 = XPara;
            #100#3 = "";
            |GRABA 140#100n;
        |FINSI;
  |SIGUE;

  |ENTLINEAL #1#100, -1, 4, 22, 1, ClaveBaja100, ClaveAlta100;

  |PARA XPara = 200; |SI XPara ] 1; |HACIENDO XPara = XPara - 1;
        #100#0 = #1#0;
        #100#1 = #1#1;
        #100#2 = XPara;
        |LIBERA #100;
        |LEE 140#100=;
        |SI FSalida = 0;
            VAlfa = #100#3;  |QBF VAlfa;
            |SI VAlfa ! "";  |SAL;  |FINSI;

            |BORRA 140#100a;
        |FINSI;
  |SIGUE;

  |CIERRAT #100;
  |POPV;
|FPRC;

|PROCESO PulsaTecla;  |TIPO 11;
  |SI FSalida ! 10;  |ACABA;  |FINSI;

  |PUSHV 0501 2380;
  |ENTLINEAL #1#100, -1, 4, 22, 1, ClaveBaja100, ClaveAlta100;
  |POPV;

  |ERROR;
|FPRC;

|PROCESO Tipo11; |TIPO 7;
  |ENCAMPO #3#100;
  Tecla = FSalida;

  |GRABA 140#100a;

  |SI Tecla = 1;  |PTEC 807;  |FINSI;
  |SI Tecla = 7;  |PTEC 807;  |FINSI;
  |SI Tecla = 0;  |PTEC 809;  |FINSI;
  |SI Tecla = 2;  |PTEC 808;  |FINSI;
  |SI Tecla = 3;  |PTEC 809;  |FINSI;
  |SI Tecla = 5;  |PTEC 815;  |FINSI;
  |SI Tecla = 4;  |PTEC 814;  |FINSI;
|FPRC;

|PROCESO GrabaTempo;
     #0#0 = #1#0;
     |LEE 101#0=;
     |SI FSalida ! 0;
         #0#0 = #1#0;
         |GRABA 020#0n;
     |FINSI;
     |LIBERA #0;
|FPRC;

|DEFBUCLE agim0013;
     #1#1;
     ;
     "01.01.0000", nDEmpresa;
     "31.12.2999", nHEmpresa;
     ;
     NULL, GrabaTempo;
|FIN;

|PROCESO Tipo7z0011;  |TIPO 7;
     fDFecha = #0#0;
     fHFecha = #0#0;
     |ENTLINEAL #1#1, -2, 7, 22, 1, ClaveBaja1, ClaveAlta1;
|FPRC;

|PROCESO BorraEntrevista;
     |BORRA 101#1a;
     |LIBERA #1;
|FPRC;

|DEFBUCLE agim0013B;
     #1#1;
     ;
     #0#0, nDEmpresa;
     #0#0, nHEmpresa;
     ;
     NULL, BorraEntrevista;
|FIN;

|PROCESO Tipo2z0011;  |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 3;
         |BUCLE agim0013B;
     |FINSI;
|FPRC;

|RUTINA ClaveBaja0;
     #0#0 = "01.01.0000";
|FRUT;

|RUTINA ClaveAlta0;
     #0#0 = "31.12.2999";
|FRUT;

|PROGRAMA;
     |SI enEmpresa = 0;
         nDEmpresa = 0;
         nHEmpresa = 99999;

         |ABRE #0
         |LEE 040#0p;
         |SI FSalida ! 0;
             |BUCLE agim0013;
         |FINSI;
         |CIERRA #0;

         |CABEZA "Entrevistas";
         |ENTLINEAL #1#0, -1, 1, 22, 1, ClaveBaja0, ClaveAlta0;
     |SINO;
         nDEmpresa = enEmpresa;
         nHEmpresa = enEmpresa;
         fDFecha   = "01.01.1900";
         fHFecha   = "31.12.2999";
         |PINPA #0#1;
         |ENTLINEAL #2#1, -1, 4, 22, 0, ClaveBaja1, ClaveAlta1;
     |FINSI;
|FPRO;
