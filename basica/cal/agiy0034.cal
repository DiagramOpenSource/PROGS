|FICHEROS;
     agiy0034 #0;

     agirefer #1;
|FIN;

|VARIABLES;
     nSwHay    = 0;
     nPara1    = 0;
     informe   = "agiy0034";
     aDNom     = "";
     aHNom     = "";
     aDRaz     = "";
     aHRaz     = "";
     &eOrden   = "";
     &eNombre  = "";
     &tNombre  = "";
     &nLinea   = 0;
     &eObserva = "";
|FIN;

|| ************************************************************************

|PROCESO Imprimelos;
     |SI #0#6 = "R";
         eNombre = #1#1;
     |SINO;
         eNombre = #1#2;
     |FINSI;

     nLinea = 0;
     |PRINT;
     |PARA nPara1 = 18; |SI nPara1 [ 32; |HACIENDO nPara1 = nPara1 + 1;
           eObserva = #1nPara1; |QBF eObserva;
           |SI eObserva ! "";
               nLinea = 1;
               |PRINT;
           |FINSI;
     |SIGUE;
     || ... |SI nLinea = 1; eObserva = ""; |PRINT; |FINSI;
     nSwHay = 1;
|FPRC;

|DEFBUCLE porNIF;
     #1#4;
     0,1,2;
     "            ", #0#0, aDRaz, aDNom;
     "zzzzzzzzzzzz", #0#1, aHRaz, aHNom;
     ;
     NULL, Imprimelos;
|FIN;

|DEFBUCLE porNombre;
     #1#3;
     0;
     aDNom, #0#0;
     aHNom, #0#1;
     ;
     NULL, Imprimelos;
|FIN;

|DEFBUCLE porRazon;
     #1#2;
     0;
     aDRaz, #0#0;
     aHRaz, #0#1;
     ;
     NULL, Imprimelos;
|FIN;

|DEFBUCLE porCodigo;
     #1#1;
     1,2;
     #0#0, aDRaz, aDNom;
     #0#1, aHRaz, aHNom;
     ;
     NULL, Imprimelos;
|FIN;

|PROCESO Tipo3;  |TIPO 3;

     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |INFOR informe;
     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

     |SI #0#6 = "R";
         tNombre = "Razon Social del posible Cliente";
         aDRaz = #0#2;
         aHRaz = #0#3;
         aDNom = " " * 40;
         aHNom = "z" * 40;
     |SINO;
         tNombre = "Nombre del posible Cliente";
         aDNom = #0#2;
         aHNom = #0#3;
         aDRaz = " " * 40;
         aHRaz = "z" * 40;
     |FINSI;

      nSwHay = 0;
      |SI #0#4 = 1;
          eOrden = "Ordenado por Codigo";
          |BUCLE porCodigo;
      |FINSI;
      |SI #0#4 = 2;
          |SI #0#6 = "R";
              eOrden = "Ordenado por Razon Social";
              |BUCLE porRazon;
          |SINO;
              eOrden = "Ordenado por Nombre";
              |BUCLE porNombre;
         |FINSI;
      |FINSI;
      |SI #0#4 = 3;
          eOrden = "Ordenado por CIF/NIF";
          |BUCLE porNIF;
      |FINSI;

      |SI  nSwHay = 1;
           |PIEINF;
      |FINSI;

      |FININF;
      |FINIMP;
|FPRC;
