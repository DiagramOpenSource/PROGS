|FICHEROS;
     dscamni1 #0;
     regisnif@ #1;
|FIN;

|VARIABLES;
     aMas       = "";
     aFic       = "";
     aFichero   = "";
     aAlfa      = "";
     aLong      = "";
     aTecla     = "";

     nSwHay     = 0;
     nHandle    = 0;
     nLong      = 0;
     nTecla     = 0;

     &eaVarDni     = "";
     &enCalcCif    = 0;
     &eaNifOrigen  = "";
     &eaNifDestino = "";
     &eaContab     = "";
     &enEjerc      = 0;
|FIN;

|PROCESO Contabilidad;
     |C_M #0#5, 1, "S";
     |SI #0#4 = "A";
         |C_M #0#5, 1, "N";  #0#5 = 0000;  |PINTA #0#5;
     |FINSI;
|FPRC;

|PROCESO Tipo11;  |TIPO 11;
     nTecla = FSalida;
     |LEE 101#0=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI nTecla = 10;
         #0#3 = "RESPETA";
         #0#4 = "";
         #0#5 = 0;
         |PINTA #0#3;
         |PINTA #0#4;
         |PINTA #0#5;
     |FINSI;

     |SI nTecla = 11;
         #0#3 = "CAMBIAR";  |PINTA #0#3;

         |ET Campo1;
             |ENCAMPO #1#0;
             |SI FSalida = 2;  |VETE Campo1;  |FINSI;

         |ET Campo4;
             |ENCAMPO #4#0;
             |SI FSalida = 2;  |VETE Campo1;  |FINSI;

         |ET Campo5;
             |ENCAMPO #5#0;
             |SI FSalida = 2;  |VETE Campo4;  |FINSI;
     |FINSI;

     |SI nTecla = 12;
         #0#3 = "BORRAR";
         #0#4 = "";
         #0#5 = 0;
         |PINTA #0#3;
         |PINTA #0#4;
         |PINTA #0#5;
     |FINSI;

     |GRABA 020#0a;
     |LIBERA #0;
|FPRC;

|PROCESO Baja0;
     #0#0 = "";
|FPRC;

|PROCESO Alta0;
     #0#0 = "zzzzzzzzzzzz";
|FPRC;

|PROCESO regisnif;
     |SI #1#12 ! "ES ";  |Y #1#12 ! "ESP";  |Y #1#12 ! "   ";
         |ACABA;
     |FINSI;

     eaVarDni  = #1#0 > "";
     enCalcCif = 2;
     |HAZ CalculoDNI;

     |SI eaVarDni ! #1#0;
         |DDEFECTO #0;
         #0#0 = #1#0;
         #0#1 = eaVarDni;
         #0#2 = #1#1;
         #0#3 = "RESPETA";
         |GRABA 020#0n;
         |LIBERA #0;

         nSwHay = 1;
     |FINSI;
|FPRC;

|DEFBUCLE regisnif;
     #1#1001;
     ;
     "            ";
     "zzzzzzzzzzzz";
     ;
     NULL, regisnif;
|FIN;

|PROCESO dscamni1;
     |SI #0#3 = "RESPETA";  |ACABA;  |FINSI;

     |SI #0#3 = "BORRAR ";
         |ABRE #1;
         #1#0 = #0#0;
         |LEE 101#1=;
         |SI FSalida = 0;
             |BORRA 020#1a;
             |LIBERA #1;
         |FINSI;
         |CIERRA #1;
     |FINSI;

     |SI #0#3 = "CAMBIAR";
         eaNifOrigen  = #0#0;
         eaNifDestino = #0#1;
         eaContab     = #0#4;
         enEjerc      = #0#5;
         |SUB_EJECUTA_NP "dscamnif;AUTO";
     |FINSI;
|FPRC;

|DEFBUCLE dscamni1;
     #0#1;
     ;
     "            ";
     "zzzzzzzzzzzz";
     ;
     NULL, dscamni1;
|FIN;

|PROGRAMA;
     |DBASS aMas;  |QBF aMas;
     aFic = aMas + "integral/fich/";
     aMas = aMas + "integral/def/dsam0103.mas";
     |XABRE "E", aMas, nHandle;
     |SI FSalida < 0;
         |DBASE aMas;  |QBF aMas;
         aFic = aMas + "fich/";
         aMas = aMas + "def/agim0003.mas";
     |FINSI;

     |CARGA_DEF aMas, regisnif@;
     |SI FSalida ! 0;
         aAlfa = "0000 No puedo cargar el Def " + aMas;
         |MENAV aAlfa;
         |ACABA;
     |FINSI;

     |PATH_DAT #1 aFic;

     aFichero = ("ni" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #0 aFichero;
     |ABRE #0;  |CIERRA #0;  |DELINDEX #0;

     |CLS;
     |CUADRO 0501 2380;

     |ATRI I;
     |PINTA 1110, "Chequea todos los Registros cuyo Pais tenga los codigos ";
     |PINTA 1210, "<ES>, <ESP> o Codigo en Blanco, los demas registros los ";
     |PINTA 1310, "toma como extranjeros y saltan el chequeo.";
     |PINTA 2210, "Pulse una tecla para continuar";
     |ATRI i;

     |LEETECLA aTecla;

     |MENSA "0000 Buscando Nif Erroneos";
     |CABEZA "Cambio Masivo";
     |PINPA #0#0;

     nSwHay = 0;
     |ABRE #0;
     |BUCLE regisnif;
     |CIERRA #0;

     |BLIN 04;

     |SI nSwHay = 1;
         |ENTLINEAL #1#0, -1, 4, 22, 1, Baja0, Alta0;
         |CONFI "0000SRealizar el Cambio o Borrado de NIF";
         |SI FSalida = 0;
             |BUCLE dscamni1;
         |FINSI;
     |SINO;
         |MENAV "0000 No hay registros Erroneos.";
     |FINSI;

     |DESCARGA_DEF #regisnif@;
     |ABRE #0;  |CIERRA #0;  |DELINDEX #0;
|FPRO;
