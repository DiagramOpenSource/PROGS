|FICHEROS;
     agidanif;
     :/laboral/def/labtraba;
     :/laboral/def/nomir003;
     :/laboral/def/nomir001;

     agiw0033;
     agiw0043,MANTE;

     agidanif@;
     &regisnif@;
|FIN;

|VARIABLES;
     &eaNif         = "";
     &eaCodNif      = "";
     &enModoNif     = 0;
     &enEmpresa     = 0;
     &enAnyo        = 0;
     &eaVarDni      = "";
     &enCalcCif     = 0;
     &eaVengoDe     = "";

      aPathCliente  = "";
      aDef          = "";
      aAlfa         = "";
      aAlfa1        = "";
      aAlfa2        = "";
      aAlfa3        = "";
      aComa         = "";
      aTecla        = "";

      aCadena       = "";
      aCadena1      = "";
      aCadena2      = "";
      aCadena3      = "";
      aCaracter     = "";
      aLongAlfa     = "";
      aParaQuitar   = "";

      nBlanco1      = 0;
      nBlanco2      = 0;
      nComa         = 0;
      nPara         = 0;
      nPosi         = 0;
      nLongNume     = 0;
      nCampo        = 0;
      nCampo1       = 0;
      nCampo2       = 0;
      nCampo3       = 0;
      nCampo4       = 0;
      nCampo5       = 0;
      nModoEntrada  = 0;
      nAnyo         = 0;
      nNume         = 0;

      nDia1 = 0;
      nDia2 = 0;
      nMes1 = 0;
      nMes2 = 0;
      nAny1 = 0;
      nAny2 = 0;

      fFecha        = @;
      fFecha1       = @;
      fHoy          = @;
      nSwAlta       = 0;
      nOpera        = 0;
      nEmpresa      = 0;

 {-1} aMenuFami     = "";
      aMenuFami1    = "1262";
      aMenuFami2    = "1";
      aMenuFami3    = "3";
      aMenuFami4    = "CONYUGE     ";
      aMenuFami5    = "DESCENDIENTE";
      aMenuFami6    = "ASCENDIENTE ";

     swHayAlguno = 0;
     &eaProcedencia = "";
     &enTrabajador  = 0;
     aPathLaboral   = "";
     swHijos        = 0;
     nPara1         = 0;
     nPara2         = 0;
     nEdad          = 0;
     nLaboral       = 0;
     aBase          = "";
     nCanal         = 0;
     swSal = 0;
     swComprueba = 0;
|FIN;

|VARIABLES;
|FIN;

|PROCESO Tipo7C23Fdanif;  |TIPO 7;
     |C_M #agidanif#24, 1, "S";
     |C_M #agidanif#25, 1, "S";
     |C_M #agidanif#26, 1, "S";
     |C_M #agidanif#27, 1, "S";
     |C_M #agidanif#28, 1, "S";
     |C_M #agidanif#29, 1, "S";
     |C_M #agidanif#30, 1, "S";
     |C_M #agidanif#31, 1, "S";
     |C_M #agidanif#32, 1, "S";

     |SI #agidanif#1  ! 1;    |ACABA;  |FINSI;
     |SI #agidanif#20 ! "N";  |ACABA;  |FINSI;
     |SI #agidanif#21 ! "S";  |ACABA;  |FINSI;

     |C_M #agidanif#24, 1, "N";  #agidanif#24 = "";  |PINTA #agidanif#24;
     |C_M #agidanif#25, 1, "N";  #agidanif#25 = "";  |PINTA #agidanif#25;
     |C_M #agidanif#26, 1, "N";  #agidanif#26 = "";  |PINTA #agidanif#26;
     |C_M #agidanif#27, 1, "N";  #agidanif#27 = "";  |PINTA #agidanif#27;
     |C_M #agidanif#28, 1, "N";  #agidanif#28 = "";  |PINTA #agidanif#28;
     |C_M #agidanif#29, 1, "N";  #agidanif#29 = "";  |PINTA #agidanif#29;
     |C_M #agidanif#30, 1, "N";  #agidanif#30 = 0;   |PINTA #agidanif#30;
     |C_M #agidanif#31, 1, "N";  #agidanif#31 = 0;   |PINTA #agidanif#31;
     |C_M #agidanif#32, 1, "N";  #agidanif#32 = "";  |PINTA #agidanif#32;

     |PUSHV 0501 2380;

     #agiw0043#0  = #agidanif#45;
     #agiw0043#1  = #agidanif#46;
     #agiw0043#2  = #agidanif#47;
     #agiw0043#3  = #agidanif#48;
     #agiw0043#4  = #agidanif#49;
     #agiw0043#5  = #agidanif#50;
     #agiw0043#6  = #agidanif#51;
     #agiw0043#7  = #agidanif#52;
     #agiw0043#8  = #agidanif#53;
     #agiw0043#9  = #agidanif#54;
     #agiw0043#10 = #agidanif#55;
     #agiw0043#11 = #agidanif#56;

     |PINPA #0#agiw0043;
     |PINDA #0#agiw0043;
     |ENDATOS #1#agiw0043;
     |SI FSalida = 0;
         #agidanif#45 = #agiw0043#0;
         #agidanif#46 = #agiw0043#1;
         #agidanif#47 = #agiw0043#2;
         #agidanif#48 = #agiw0043#3;
         #agidanif#49 = #agiw0043#4;
         #agidanif#50 = #agiw0043#5;
         #agidanif#51 = #agiw0043#6;
         #agidanif#52 = #agiw0043#7;
         #agidanif#53 = #agiw0043#8;
         #agidanif#54 = #agiw0043#9;
         #agidanif#55 = #agiw0043#10;
         #agidanif#56 = #agiw0043#11;
     |FINSI;

     |POPV;
|FPRC;

|PROCESO SeparaNombre;
     || Busca una coma en la aCadena

     aCadena1 = "";
     aCadena2 = "";
     aCadena3 = "";

     nBlanco1 = 0;
     nBlanco2 = 0;
     nComa    = 0;

     |PARA nPara = 1; |SI nPara [ 40; |HACIENDO nPara = nPara + 1;
           nPosi = nPara * 100 + 1;
           aCaracter = aCadena % nPosi;

           |SI aCaracter = ","; nComa = nPara; |SAL; |FINSI;
     |SIGUE;

     |SI nComa = 0;
         || Si no hay coma busca el primer blanco empezando por detras

         aLongAlfa = aCadena % 0;
         nLongNume = aLongAlfa;

         |PARA nPara = nLongNume; |SI nPara ] 1; |HACIENDO nPara = nPara - 1;
               nPosi = nPara * 100 + 1;
               aCaracter = aCadena % nPosi;
               |SI aCaracter = " "; nComa = nPara; |SAL; |FINSI;
         |SIGUE;
     |FINSI;

     || Coge los dos primeros aCaracteres del segundo apellido

     nBlanco1  = nComa - 1;
     nPosi     = nBlanco1 + 100;
     aCadena2  = aCadena % nPosi;
     aLongAlfa = aCadena2 % 0;
     nLongNume = aLongAlfa;

     |PARA nPara = nLongNume; |SI nPara ] 1; |HACIENDO nPara = nPara - 1;
           nPosi = nPara * 100 + 1;
           aCaracter = aCadena2 % nPosi;
           |SI aCaracter = " "; nBlanco1 = nPara; |SAL; |FINSI;
     |SIGUE;

     nBlanco1 = nBlanco1 + 1;
     nPosi    = nBlanco1 * 100 + nLongNume;
     aCadena2 = aCadena2 % nPosi;

     || Coge las dos primeros caracteres del primer apellido

     nBlanco1 = nBlanco1 - 1;
     nPosi    = 100 + nBlanco1;
     aCadena1 = aCadena % nPosi;

     || Recoge los dos primero aCaracteres de Nombre

     nPosi    = ((nComa + 1) * 100) + 40;
     aCadena3 = aCadena % nPosi;

     aParaQuitar = aCadena1; |HAZ QuitaBlanco; aCadena1 = aParaQuitar;
     aParaQuitar = aCadena2; |HAZ QuitaBlanco; aCadena2 = aParaQuitar;
     aParaQuitar = aCadena3; |HAZ QuitaBlanco; aCadena3 = aParaQuitar;

     |QBF aCadena1;
     |QBF aCadena2;
     |QBF aCadena3;
|FPRC;

|PROCESO QuitaBlanco;
     |ET aCadena;
         |QBF aParaQuitar;
         |SI aParaQuitar ! "";
             aCaracter = aParaQuitar % 101;
             |SI aCaracter = " ";
                 aParaQuitar = aParaQuitar % 240;
                 |VETE aCadena;
             |FINSI;
         |FINSI;
|FPRC;

|PROCESO PonTipodanif;
     nModoEntrada = (FEntrada / 100) ! 0;

     |SELECT #1#agidanif@;
     #agidanif@#0 = #agidanif#0;
     #agidanif@#1 = 1;
     |LEE 040#agidanif@.=;
     |SI FSalida ! 0;  |DDEFECTO #agidanif@;  |FINSI;

     |SI #agidanif#1 ! 1;
         |SI #agidanif#Campo# = "CONYUGE     ";  aMenuFami2 = "1";  |FINSI;
         |SI #agidanif#Campo# = "DESCENDIENTE";  aMenuFami2 = "2";  |FINSI;
         |SI #agidanif#Campo# = "ASCENDIENTE ";  aMenuFami2 = "3";  |FINSI;

         |PUSHV 0501 2380;
         |PARA; |SI; |HACIENDO;
                |MENUG aMenuFami;
                aMenuFami2 = FSalida;

                |SI aMenuFami2 = "1";   #agidanif#Campo# = aMenuFami4;  |FINSI;
                |SI aMenuFami2 = "2";   #agidanif#Campo# = aMenuFami5;  |FINSI;
                |SI aMenuFami2 = "3";   #agidanif#Campo# = aMenuFami6;  |FINSI;

                |SI #agidanif#Campo# = "CONYUGE     ";
                    |SI #agidanif@#14 ! "C";
                        |MENAV "      No podemos seleccionar Conyuge, ya que el Titular no esta casado.";
                        aMenuFami2 = "0";
                    |FINSI;
                |FINSI;

                |SI aMenuFami2 = "1";   |SAL;  |FINSI;
                |SI aMenuFami2 = "2";   |SAL;  |FINSI;
                |SI aMenuFami2 = "3";   |SAL;  |FINSI;
         |SIGUE;
         |POPV;

         |PINTA #agidanif#Campo#;
     |SINO;
         #agidanif#Campo# = "TITULAR     "; |PINTA #agidanif#Campo#;
     |FINSI;

     |C_M #agidanif#3,  1, "S";
     |C_M #agidanif#4,  1, "S";
     |C_M #agidanif#5,  1, "S";
     |C_M #agidanif#6,  1, "S";
     |C_M #agidanif#7,  1, "S";
     |C_M #agidanif#14, 1, "S";
     |C_M #agidanif#15, 1, "S";
     |C_M #agidanif#16, 1, "S";
     |C_M #agidanif#23, 1, "S";
     |C_M #agidanif#38, 1, "S";
     |C_M #agidanif#39, 1, "S";
     |C_M #agidanif#40, 1, "S";
     |C_M #agidanif#41, 1, "S";
     |C_M #agidanif#42, 1, "S";

     |SI #agidanif#2 =  "TITULAR     "; |O #agidanif#1 = 01;
         |C_M #agidanif#3,  1, "N";  #agidanif#3 = "";  |PINTA #agidanif#3;
         |C_M #agidanif#4,  1, "N";  #agidanif#4 = "";  |PINTA #agidanif#4;
         |C_M #agidanif#5,  1, "N";  #agidanif#5 = "";  |PINTA #agidanif#5;
         |C_M #agidanif#6,  1, "N";  #agidanif#6 = "";  |PINTA #agidanif#6;
         |C_M #agidanif#7,  1, "N";  #agidanif#7 =  0;   |PINTA #agidanif#7;
         |C_M #agidanif#41, 1, "N";  #agidanif#41=  0;   |PINTA #agidanif#41;
         |SI #agidanif#15 = 0;
              #agidanif#15 = 3; |PINTA #agidanif#15;
         |FINSI;
         #agidanif#16 = "A"; |PINTA #agidanif#16;
         #agidanif#42 = "0";
     |FINSI;

     |SI #agidanif#2 =  "CONYUGE     ";
         |C_M #agidanif#3,  1, "N";  #agidanif#3  = "";   |PINTA #agidanif#3;
         |C_M #agidanif#4,  1, "N";  #agidanif#4  = "";   |PINTA #agidanif#4;
         |C_M #agidanif#5,  1, "N";  #agidanif#5  = "";   |PINTA #agidanif#5;
         |C_M #agidanif#6,  1, "N";  #agidanif#6  = "";   |PINTA #agidanif#6;
         |C_M #agidanif#7,  1, "N";  #agidanif#7  = 0;    |PINTA #agidanif#7;
         |C_M #agidanif#14, 1, "N";  #agidanif#14 = "C";  |PINTA #agidanif#14;
         |C_M #agidanif#15, 1, "N";  #agidanif#15 = "";   |PINTA #agidanif#15;
         |C_M #agidanif#16, 1, "N";  #agidanif#16 = "";   |PINTA #agidanif#16;
         |C_M #agidanif#20, 1, "N";  #agidanif#20 = "";   |PINTA #agidanif#20;
         |C_M #agidanif#21, 1, "N";  #agidanif#21 = "";   |PINTA #agidanif#21;
         |C_M #agidanif#41, 1, "N";  #agidanif#41=  0;   |PINTA #agidanif#41;
         |C_M #agidanif#42, 1, "N";  #agidanif#42=  "0";   |PINTA #agidanif#42;
     |FINSI;

     |SI #agidanif#2 =  "DESCENDIENTE";
         |C_M #agidanif#5,  1, "N";  #agidanif#5  = "";   |PINTA #agidanif#5;
         |C_M #agidanif#6,  1, "N";  #agidanif#6  = "";   |PINTA #agidanif#6;
         |C_M #agidanif#14, 1, "N";  #agidanif#14 = "";   |PINTA #agidanif#14;
         |C_M #agidanif#15, 1, "N";  #agidanif#15 = "";   |PINTA #agidanif#15;
         |C_M #agidanif#16, 1, "N";  #agidanif#16 = "";   |PINTA #agidanif#16;
         |C_M #agidanif#20, 1, "N";  #agidanif#20 = "";   |PINTA #agidanif#20;
         |C_M #agidanif#21, 1, "N";  #agidanif#21 = "";   |PINTA #agidanif#21;
         |C_M #agidanif#20, 1, "N";  #agidanif#20 = "";   |PINTA #agidanif#20;
         |C_M #agidanif#21, 1, "N";  #agidanif#21 = "";   |PINTA #agidanif#21;
         |C_M #agidanif#38, 1, "N";  #agidanif#38 = "N";  |PINTA #agidanif#38;
         |C_M #agidanif#39, 1, "N";  #agidanif#39 = "N";  |PINTA #agidanif#39;
         |C_M #agidanif#40, 1, "N";  #agidanif#40 = "";   |PINTA #agidanif#40;
         |C_M #agidanif#42, 1, "N";  #agidanif#42=  "0";   |PINTA #agidanif#42;
     |FINSI;

     |SI #agidanif#2 =  "ASCENDIENTE ";
         |C_M #agidanif#4,  1, "N";  #agidanif#4  = "";   |PINTA #agidanif#4;
         |C_M #agidanif#14, 1, "N";  #agidanif#14 = "";   |PINTA #agidanif#14;
         |C_M #agidanif#15, 1, "N";  #agidanif#15 = "";   |PINTA #agidanif#15;
         |C_M #agidanif#16, 1, "N";  #agidanif#16 = "";   |PINTA #agidanif#16;
         |C_M #agidanif#20, 1, "N";  #agidanif#20 = "";   |PINTA #agidanif#20;
         |C_M #agidanif#21, 1, "N";  #agidanif#21 = "";   |PINTA #agidanif#21;
         |C_M #agidanif#23, 1, "N";  #agidanif#23 = "";   |PINTA #agidanif#23;
         |C_M #agidanif#38, 1, "N";  #agidanif#38 = "N";  |PINTA #agidanif#38;
         |C_M #agidanif#39, 1, "N";  #agidanif#39 = "N";  |PINTA #agidanif#39;
         |C_M #agidanif#40, 1, "N";  #agidanif#40 = "";   |PINTA #agidanif#40;
         |C_M #agidanif#41, 1, "N";  #agidanif#41=  0;   |PINTA #agidanif#41;
         |C_M #agidanif#42, 1, "N";  #agidanif#42=  "0";   |PINTA #agidanif#42;
     |FINSI;

     |SI nModoEntrada = 1;
         |PARA nCampo = 24;  |SI nCampo [ 35;  |HACIENDO nCampo = nCampo + 1;
               #agidanif#nCampo# = #agidanif@#nCampo#;  |PINTA #agidanif#nCampo#;
         |SIGUE;
         #agidanif#19 = #agidanif@#19;  |PINTA #agidanif#19;
     |FINSI;

     #agidanif#37 = "S";
|FPRC;

|PROCESO Tipo14C2; |TIPO 14;
     |DBASS aBase;   |QBT aBase;
     aBase = aBase + "integral/def/dsam0000.mas";
     |XABRE "E", aBase, nCanal;
     |SI FSalida ] 0;
         ||Desactivado
         |MODO_RELACION #agidanif, 1, "agim0003", "N", "N";
         |MODO_RELACION #agidanif, 1, "agim0003", "X", "X";
         || Activado
         |MODO_RELACION #agidanif, 0, "dsam0103", "X", "X";

         |SI #agidanif#2 =  "TITULAR     ";
             |MODO_RELACION #agidanif, 1, "dsam0103", "S", "S";
         |SINO;
             |MODO_RELACION #agidanif, 1, "dsam0103", "S", "N";
         |FINSI;
     |SINO;
         |MODO_RELACION #agidanif, 1, "dsam0103", "N", "N";
         |MODO_RELACION #agidanif, 1, "dsam0103", "X", "X";

         |MODO_RELACION #agidanif, 0, "agim0003", "X", "X";
         |SI #agidanif#2 =  "TITULAR     ";
             |MODO_RELACION #agidanif, 1, "agim0003", "S", "S";
         |SINO;
             |MODO_RELACION #agidanif, 1, "agim0003", "S", "N";
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO NombreEntero;
     aAlfa     = "";
     nCampo1   = Campo - 3;  aAlfa1 = #agidanif#nCampo1#;  |QBF aAlfa1;
     nCampo1   = Campo - 2;  aAlfa2 = #agidanif#nCampo1#;  |QBF aAlfa2;
     nCampo1   = Campo - 1;  aAlfa3 = #agidanif#nCampo1#;  |QBF aAlfa3;

     aAlfa     = aAlfa1;
     |SI aAlfa2 ! "";
         |SI aAlfa1 ! "";
             aAlfa = aAlfa + " " + aAlfa2;
         |SINO;
             aAlfa = aAlfa2;
         |FINSI;
     |FINSI;

     aComa = "";
     |SI aAlfa1 ! "";  |O aAlfa2 ! "";
         |SI aAlfa3 ! "";
            aComa = ", ";
         |FINSI;
     |FINSI;

     aAlfa = aAlfa + aComa + aAlfa3;
     #agidanif#Campo# = aAlfa;
     |PINTA #agidanif#Campo#;
|FPRC;

|PROCESO AyudaVincula;
     |PUSHV 0501 2380;
     |BLANCO 0910 1680;
     |CUADRO 0910 1680;

     |SI #agidanif#2 =  "DESCENDIENTE";
         |ATRI S;
         |PINTA 1011, "                         VINCULACION DESCENDIENTES                   ";
         |ATRI s;

         |ATRI I;
         |PINTA 1111, "     En blanco de ambos.                                             ";
         |PINTA 1211, " 1.- Del declarante si no convive con el otro progenitor.            ";
         |PINTA 1311, " 2.- Del conyuge si no convive con el otro progenitor.               ";
         |PINTA 1411, " 3.- Del declarante si convive con el otro progenitor.               ";
         |PINTA 1511, " 4.- Del conyuge si convive con el otro progenitor.                  ";
         |ATRI i;
     |SINO;
         |ATRI S;
         |PINTA 1011, "                         VINCULACION ASCENDIENTES                    ";
         |ATRI s;

         |ATRI I;
         |PINTA 1111, " 1.- Del declarante";
         |PINTA 1211, " 2.- Del conyuge";
         |ATRI i;
     |FINSI;

     |LEETECLA aTecla;
     |POPV;
|FPRC;

|PROCESO Vinculadanif;  |TIPO 0;
     |SI FSalida = 10;
         |HAZ AyudaVincula;
         |ERROR;
         |ACABA;
     |FINSI;

     |SI #agidanif#2 ! "ASCENDIENTE ";  |ACABA;  |FINSI;

     |SELECT #1#agidanif@;
     #agidanif@#0 = #agidanif#0;
     #agidanif@#1 = 1;
     |LEE 040#agidanif@.=;
     |SI FSalida ! 0;  |DDEFECTO #agidanif@;  |FINSI;

     |SI #agidanif#Campo# = "3";  |O #agidanif#Campo# = "4";
         |ERROR;
         |ACABA;
     |FINSI;

     |SI #agidanif#Campo# = "2";  |Y #agidanif@#14 ! "C";
         #agidanif#Campo# = "1";  |PINTA #agidanif#Campo#;
     |FINSI;

     |SI #agidanif#Campo# = " ";
         |MENAV "     Debe ser 1 o 2 dependiendo del Titular o del Conyuge.";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO CambiaEstado;
     |SI #agidanif#2 ! "TITULAR     ";  |O #agidanif#1 ! 01; |ACABA;  |FINSI;

     |SELECT #2#agidanif@;
     #agidanif@#0 = #agidanif#0;
     #agidanif@#2 = "CONYUGE     ";
     |LEE 040#agidanif@.=;
     |SI FSalida = 0;
         #agidanif#14 = "C";  |PINTA #agidanif#14;
     |FINSI;
|FPRC;

|PROCESO LetraNifdanif;  |TIPO 6;
     |SI FSalida = 10;
         eaVarDni  = #agidanif#Campo#;
         enCalcCif = 1;
         |HAZ CalculoDNI;
         #agidanif#Campo# = eaVarDni;
         |PINTA #agidanif#Campo#;
         |ERROR;
         |ACABA;
     |FINSI;

     eaVarDni  = #agidanif#Campo#;
     enCalcCif = 0;
     |HAZ CalculoDNI;
     eaVarDni = (eaVarDni + "            ") % 112;
     |SI #agidanif#Campo# ! eaVarDni;
         |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
         |SI FSalida = 0;
             #agidanif#Campo# = eaVarDni;  |PINTA #agidanif#Campo#;
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO CtasCtesdanif;
     ||SI FSalida ! 0;  |ACABA;  |FINSI;

     #agiw0033#0 = #agidanif#Campo# % 104;
     #agiw0033#1 = #agidanif#Campo# % 504;
     #agiw0033#2 = #agidanif#Campo# % 902;
     #agiw0033#3 = #agidanif#Campo# % 1110;

     |PUSHV 0501 2380;
     |PINPA #0#agiw0033;

     |ATRI R;
     |PINTA 1116, "            CUENTA CORRIENTE                ";
     |ATRI r;

     |PINDA #0#agiw0033;
     |ENDATOS #1#agiw0033;
     |SI FSalida = 0;
         aAlfa     = #agiw0033#0 + #agiw0033#1 + #agiw0033#2 + #agiw0033#3;
         #agidanif#Campo# = aAlfa;
     |FINSI;
     |POPV;
     |PINTA #agidanif#Campo#;
|FPRC;

|PROCESO labtraba2;
     swHayAlguno = 1;
     |ERROR10;
|FPRC;

|DEFBUCLE labtraba2;
     #labtraba#2;
     ;
     eaNif;
     eaNif;
     ;
     NULL, labtraba2;
|FIN;

|PROCESO CogeOtro;
     |SI #agidanif#8 = "            ";
         eaCodNif  = #agidanif#0;
         |HAZ LeeNifs;

         #agidanif#24 = #regisnif@#9;
         #agidanif#25 = #regisnif@#2;
         #agidanif#26 = #regisnif@#3;
         #agidanif#27 = #regisnif@#16;
         #agidanif#28 = #regisnif@#10;
         #agidanif#29 = #regisnif@#11;
         #agidanif#30 = #regisnif@#4;
         #agidanif#31 = #regisnif@#5;
         #agidanif#32 = #regisnif@#6;
         #agidanif#33 = #regisnif@#7;

         |PINTA #agidanif#24;
         |PINTA #agidanif#25;
         |PINTA #agidanif#26;
         |PINTA #agidanif#28;
         |PINTA #agidanif#29;
         |PINTA #agidanif#30;
         |PINTA #agidanif#31;
         |PINTA #agidanif#32;
         |PINTA #agidanif#33;

         |ACABA;
     |FINSI;

     eaCodNif  = #agidanif#8;
     |HAZ LeeNifs;

     #agidanif#12 = #regisnif@#1;
     #agidanif#19 = #regisnif@#14;
     #agidanif#24 = #regisnif@#9;
     #agidanif#25 = #regisnif@#2;
     #agidanif#26 = #regisnif@#3;
     #agidanif#27 = #regisnif@#16;
     #agidanif#28 = #regisnif@#10;
     #agidanif#29 = #regisnif@#11;
     #agidanif#30 = #regisnif@#4;
     #agidanif#31 = #regisnif@#5;
     #agidanif#32 = #regisnif@#6;
     #agidanif#33 = #regisnif@#7;
     #agidanif#34 = #regisnif@#8;

     aCadena = #agidanif#12;
     |HAZ SeparaNombre;
     #agidanif#9    = aCadena1;
     #agidanif#10   = aCadena2;
     #agidanif#11   = aCadena3;

     |PINTA #agidanif#12;
     |PINTA #agidanif#19;
     |PINTA #agidanif#24;
     |PINTA #agidanif#25;
     |PINTA #agidanif#26;
     |PINTA #agidanif#28;
     |PINTA #agidanif#29;
     |PINTA #agidanif#30;
     |PINTA #agidanif#31;
     |PINTA #agidanif#32;
     |PINTA #agidanif#33;
     |PINTA #agidanif#34;
     |PINTA #agidanif#9;
     |PINTA #agidanif#10;
     |PINTA #agidanif#11;
|FPRC;

|PROCESO Refresca;
     |HAZ CogeOtro;
     |GRABA 020#agidanif.a;
     |LIBERA #agidanif;
|FPRC;

|DEFBUCLE agidanifR;
     #agidanif#1;
     ;
     eaNif, 01;
     eaNif, 99;
     be;
     NULL, Refresca;
|FIN;

|PROCESO Tipo5danif;  |TIPO 5;
     #agidanif#0 = eaNif;
     #agidanif#1 = 1;
     |LEE 040#agidanif.=;
     |SI FSalida = 0;
         |BUCLE agidanifR;
         |ACABA;
     |FINSI;

     eaCodNif  = eaNif;
     |HAZ LeeNifs;

     |DDEFECTO #agidanif;
     #agidanif#0  = eaNif;
     #agidanif#1  = 1;
     #agidanif#2  = "TITULAR";
     #agidanif#8  = eaNif;
     #agidanif#12 = #regisnif@#1;
     #agidanif#15 = 3;          || situacion familiar del titular, por defecto a 3
                         || por favor al darse de alta esta ficha

     |SI nLaboral = 1;
         swHayAlguno = 0;
         |BUCLE labtraba2;
         |SI swHayAlguno = 1;
             #agidanif#13 = #labtraba#11;
         |FINSI;
     |FINSI;

     #agidanif#19 = #regisnif@#14;
     #agidanif#24 = #regisnif@#9;
     #agidanif#25 = #regisnif@#2;
     #agidanif#26 = #regisnif@#3;
     #agidanif#27 = #regisnif@#16;
     #agidanif#28 = #regisnif@#10;
     #agidanif#29 = #regisnif@#11;
     #agidanif#30 = #regisnif@#4;
     #agidanif#31 = #regisnif@#5;
     #agidanif#32 = #regisnif@#6;
     #agidanif#33 = #regisnif@#7;
     #agidanif#34 = #regisnif@#8;

     aCadena = #agidanif#12;
     |HAZ SeparaNombre;
     #agidanif#9    = aCadena1;
     #agidanif#10   = aCadena2;
     #agidanif#11   = aCadena3;

     |GRABA 020#agidanif.n;
     |LIBERA #agidanif;
|FPRC;

|PROCESO SinMov; |TIPO 7;
     |SI #agidanif#39 = "N";
         |C_M #agidanif#40, 1, "N";  #agidanif#40 = "";   |PINTA #agidanif#40;
     |SINO;
         |C_M #agidanif#40, 1, "S";  |PINTA #agidanif#40;
     |FINSI;
|FPRC;

|PROCESO ayudasino; |TIPO 7;
     |SI #agidanif#17 ] 33; |Y #agidanif#17 < 65;
         |C_M #agidanif#18, 1, "S";                 |PINTA #agidanif#18;
     |SINO;
         |C_M #agidanif#18, 1, "N";  #agidanif#18 = "N";   |PINTA #agidanif#18;
     |FINSI;
|FPRC;

|PROCESO Restaura;
     |POPV;
     |ATRI 0;
|FPRC;

|PROCESO AyudaTipo; |TIPO 7;
     |PUSHV 16262380;
     |ATRI R;
     |PINTA 1630, " Tipo de Relacion Laboral: ";
     |PINTA 1730, " 1. General                                        ";
     |PINTA 1830, " 2. Duracion inferior a un a¤o                     ";
     |PINTA 1930, " 3. Relaciones laborales especiales de caracter de-"
     |PINTA 2030, "    pendiente (salvo penados y discapacitados)     ";
     |PINTA 2130, " 4. Esporadica propia de retribuciones por peonadas";
     |PINTA 2230, "    o jornales diarios                             ";
     |ATRI 0;
|FPRC;

|RUTINA ClaveBaja0;
     #agidanif#0 = eaNif;
     #agidanif#1 = 1;
|FRUT;

|RUTINA ClaveAlta0;
     #agidanif#0 = eaNif;
     #agidanif#1 = 99;
|FRUT;

|PROCESO CargaDatos;
     |SI #agidanif#2 = "TITULAR     "; |O #agidanif#1 = 01;
          || siempre deberia ser TITULAR cuando fuera 01, pero ha pasado algun
          || caso en que estaba en blanco
         aAlfa = #agidanif#13 % -104;
         nNume = aAlfa;
         |SI nNume = 0;
              nNume = #nomir003#40;
         |FINSI;
         #nomir003#1 = #nomir003#40 - nNume;
         #nomir003#3 = #agidanif#15;
         #nomir003#7 = " ";
         |SI #agidanif#17 ] 33;
             #nomir003#6 = "S";
             #nomir003#7 = "1";
             |SI #agidanif#17 ] 65;  #nomir003#7 = "2";  |FINSI;
             #nomir003#8 = #agidanif#18;
         |FINSI;
         #nomir003#2 = #agidanif#16;
         || #nomir003#5  = #agidanif#36;
                         || a partir de 2011, este campo no se mete por aqui
                         || se mete en la ficha del trabajador
                         || aqui no se graba nada
         |SI #agidanif#23 = " ";  #nomir003#14 = "N";  |FINSI;
         |SI #agidanif#23 = "1";  #nomir003#14 = "S";  |FINSI;
         |SI #agidanif#23 = "2";  #nomir003#14 = "S";  |FINSI;
         #nomir003#72 = #agidanif#38;
         #nomir003#73 = #agidanif#39;
         #nomir003#74 = #agidanif#40;
         #nomir003#122 = #agidanif#42;
         #nomir003#124 = #agidanif#43;
         #nomir003#126 = #agidanif#44;
         #nomir003#129 = #agidanif#57;
         #nomir003#130 = #agidanif#58;
     |FINSI;

     |SI #agidanif#2 = "CONYUGE     ";
         |SI #nomir003#3 = "2";
             #nomir003#4 = #agidanif#8;
         |FINSI;
     |FINSI;

     |SI #agidanif#2 = "DESCENDIENTE";
         aAlfa = #agidanif#13 % -104;
         |PARA nCampo1 = 20;  |SI nCampo1 [ 29;  |HACIENDO nCampo1 = nCampo1 + 1;
               nCampo2 = nCampo1 + 22;
               nCampo3 = nCampo1 + 10;
               nCampo4 = nCampo1 + 55;
               nCampo5 = nCampo1 + 65;
               |SI #nomir003#nCampo1# = 0;
                   #nomir003#nCampo1# = aAlfa;
                   |SI #agidanif#3 = "1";
                       #nomir003#nCampo2# = "S";
                   |FINSI;
                   #nomir003#nCampo3# = " ";
                   |SI #agidanif#17 ] 33;
                       #nomir003#nCampo3#  = "1";
                       |SI #agidanif#17 ] 65;  #nomir003#nCampo3# = "2";  |FINSI;
                   |FINSI;
                   #nomir003#nCampo4# = #agidanif#41;
                   #nomir003#nCampo5# = #agidanif#18;
                   |SAL;
               |FINSI;
         |SIGUE;
     |FINSI;

     |SI #agidanif#2 = "ASCENDIENTE ";
         |PARA nCampo1 = 52;  |SI nCampo1 [ 61;  |HACIENDO nCampo1 = nCampo1 + 1;
               nCampo2 = nCampo1 + 10;
               nCampo3 = nCampo1 + 53;
               nCampo4 = nCampo1 + 43;
               |SI #nomir003#nCampo4# = 0;
                   |SI #agidanif#3 = " ";  |O #agidanif#3 = "1";
                       #nomir003#nCampo1# = #agidanif#5;
                       #nomir003#nCampo2# = "S";

                       #nomir003#nCampo2# = " ";
                       |SI #agidanif#17 ] 33;
                           #nomir003#nCampo2# = "1";
                           #nomir003#nCampo3# = #agidanif#18;   || Movilidad Reducida
                           |SI #agidanif#17 ] 65;
                               #nomir003#nCampo2# = "2";
                               #nomir003#nCampo3# = "N";
                           |FINSI;
                       |FINSI;

                       aAlfa = #agidanif#13;            || a¤o nacimiento descend.
                       aAlfa = aAlfa % 704;
                       nNume = aAlfa;
                       #nomir003#nCampo4# = nNume;
                   |FINSI;
                   |SAL;
               |FINSI;
         |SIGUE;
     |FINSI;
|FPRC;

|DEFBUCLE agidanif;
     #agidanif#1;
     ;
     eaNif, 01;
     eaNif, 99;
     ;
     NULL, CargaDatos;
|FIN;

|PROCESO labtraba;
     fFecha = "01.01.0000";
     |SI #labtraba#37 ! "          ";|Y #labtraba#37 ! "..........";
         fFecha = #labtraba#37;
     |FINSI;

     nSwAlta = 0;
     |SI #labtraba#44 = "B"; |Y fFecha < "01.01.1900"; nSwAlta = 1; |FINSI;
     |SI fFecha < fFecha1;   |Y fFecha > "01.01.1900"; nSwAlta = 1; |FINSI;

     |SI nSwAlta = 0;
         |SI nOpera = 0;
             swHayAlguno = 1;
             |ERROR10;
         |SINO;
             enEmpresa = #labtraba#0;
             enTrabajador = #labtraba#1;
             |SI nEmpresa = 0; |O nEmpresa = enEmpresa;
                 |HAZ ActuaTodos;
             |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE labtraba;
     #labtraba#2;
     ;
     eaNif;
     eaNif;
     ;
     NULL, labtraba;
|FIN;

|PROCESO comp_hijos;
     swHijos = 0;
     |PARA nPara1 = 20; |SI nPara1 [ 29; |HACIENDO nPara1 = nPara1 + 1;
          nEdad = #nomir003#40 - #nomir003#nPara1#;
          |SI #nomir003#nPara1# ! 0;
               nPara2 = nPara1 + 10;    || grado minusvalia

               || solo tienen reduccion menores de 25 a¤os o mayores c/minusv.
               |SI nEdad < 25;
               |O #nomir003#nPara2# = "1"; |O #nomir003#nPara2# = "2";
                   swHijos = 1;
               |FINSI;
          |FINSI;
     |SIGUE;
     |SI #nomir003#3 = 1; |Y swHijos ! 1;
          aAlfa1 = "    ATENCION. Para la situación familiar tipo 1 es obligatorio tener ";
          aAlfa1 = aAlfa1 + " des- cendientes con derecho a reducción";
          |MENAV aAlfa1;
          #nomir003#3 = 3;
          aAlfa = "    Se ha cambiado la situación familiar a tipo 3. Si no es correcto ";
          aAlfa = aAlfa + "cámbielo al 1 e introduzca los datos de los desc.";
          |MENAV aAlfa;

          |ABRE #agidanif;
          #agidanif#0 = #nomir003#0;
          #agidanif#1 = 01;
          |LEE 101#agidanif.=;
          |SI FSalida = 0;
               #agidanif#15 = 3;
               |GRABA 020#agidanif.a;
               |LIBERA #agidanif;
          |FINSI;
          |CIERRA #agidanif;
     |FINSI;
|FPRC;

|PROCESO comp_conyuge;
     aAlfa1 = #nomir003#4;
     |QBF aAlfa1;
     |SI aAlfa1 = ""; |Y #nomir003#3 = 2;
          aAlfa1 = "    ATENCION. Para la situación familiar tipo 2 es obligatorio tener informado";
          aAlfa1 = aAlfa1 + " el NIF del cónyuge.";
          |MENAV aAlfa1;
          #nomir003#3 = 3;
          aAlfa = "    Se ha cambiado la situación familiar a tipo 3. Si no es correcto ";
          aAlfa = aAlfa + "cámbielo al 2 e introduzca el NIF del cónyuge";
          |MENAV aAlfa;

          |ABRE #agidanif;
          #agidanif#0 = #nomir003#0;
          #agidanif#1 = 01;
          |LEE 101#agidanif.=;
          |SI FSalida = 0;
               #agidanif#15 = 3;
               |GRABA 020#agidanif.a;
               |LIBERA #agidanif;
          |FINSI;
          |CIERRA #agidanif;
     |FINSI;
|FPRC;

|PROCESO comp_sitfam;
     |ABRE #agidanif;
     #agidanif#0 = eaNif;
     #agidanif#1 = 01;
     |LEE 101#agidanif.=;
     |SI FSalida = 0;
          |SI #agidanif#15 = 0;
               #agidanif#15 = 3;
               |PINTA #agidanif#15;
               |GRABA 020#agidanif.a;
               |LIBERA #agidanif;

               aAlfa = "    ATENCION: la situación familiar del trabajador está a "
               aAlfa = aAlfa + "cero. Se ha cambiado por defecto a 3.";
               |MENAV aAlfa;
               aAlfa = "    Corrija el dato si procede";
               |MENAV aAlfa;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO comp_fechanac;
     |ABRE #agidanif;
     #agidanif#0 = eaNif;
     #agidanif#1 = 01;
     |LEE 101#agidanif.=;
     |SI FSalida = 0;
          |SI #agidanif#13 = "01.01.0000";
               swHayAlguno = 0;
               |BUCLE labtraba2;
               |SI swHayAlguno = 1;
                    aAlfa = #labtraba#11;
                    |QBF aAlfa;
               |FINSI;
               |SI swHayAlguno = 1; |Y aAlfa ! ""; |Y aAlfa ! "01.01.0000";
                    #agidanif#13 = #labtraba#11;
                    |PINTA #agidanif#13;
                    aAlfa = "    ATENCION: la fecha del nacimiento no se ha rellenado. "
                    aAlfa = aAlfa + "Se ha actualizado con la de la ficha trabajador.";
                    |MENAV aAlfa;
                    |GRABA 020#agidanif.a;
                    |LIBERA #agidanif;
               |SINO;
                    aAlfa = "    ATENCION: la fecha del nacimiento no se ha rellenado. "
                    aAlfa = aAlfa + "Este dato es necesario para el cálculo de IRPF.";
                    |MENAV aAlfa;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO RestauraViv;
     |SI #agidanif#43 = "S"; |Y #agidanif#44 = "S";
          || no restaures que si no se jode la pantalla
     |SINO;
          |ATRI 0;
          |POPV;
     |FINSI;
|FPRC;

|PROCESO AyudaViv; |TIPO 7;
                 ||     3        4         5         6         7
                 ||     012345678901234567890123456789012345678901234567


     |SI Campo = 43;
          |PUSHV 09021379;
          |ATRI R;
          |PINTA 1030, " Introduzca 'S' si el trabajador se acoge  a la ";
          |PINTA 1130, " deducción por pagos de préstamos  destinados a ";
          |PINTA 1230, " la vivienda habitual del perceptor adquirida o ";
          |PINTA 1330, " rehabilitada CON ANTERIORIDAD A 01/01/2011.    ";
     |FINSI;
     |SI Campo = 44;
          |PUSHV 07021379;
          |ATRI R;
          |PINTA 0830, " Introduzca 'S' si el trabajador se acoge  a la ";
          |PINTA 0930, " deducción por pagos de préstamos  destinados a ";
          |PINTA 1030, " la vivienda habitual del perceptor adquirida o ";
          |PINTA 1130, " rehabilitada  ENTRE EL d¡a 01/01/2011 y el d¡a ";
          |PINTA 1230, " 31/12/2012. NO ES VALIDO PARA VIVIENDAS ADQUI- ";
          |PINTA 1330, " RIDAS O REHABILITADAS A PARTIR DEL 01/01/2013. ";
     |FINSI;
|FPRC;

|PROCESO CompViv;
     |SI #agidanif#43 = "S"; |Y #agidanif#44 = "S";
          aAlfa = "    ATENCION: no se pueden marcar las dos casillas de ";
          aAlfa = aAlfa + "deducción por vivienda. Debe elegir una.";
          |MENAV aAlfa;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO AvisoViv2013;
     |SI enAnyo ! 0;          || desde labtraba enAnyo = 0, doy mensaje siempre
     |Y enAnyo < 2013;        || desde datos IRPF, solo a partir de 2013;
          |ACABA;
     |FINSI;
     |SI #agidanif#Campo# = "S"; |Y Contenido = "N";
          aAlfa = "    ATENCION. La deducción sólo es aplicable a viviendas ";
          aAlfa = aAlfa + "adquiridas o rehabilitación HASTA el 31/12/2012";
          |MENAV aAlfa;

          aAlfa = "    Compruebe que el trabajador tiene derecho a esta ";
          aAlfa = aAlfa + "deducción a partir del ejercicio 2013";
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO ActivaLaboral; |TIPO 7;
     |SI enAnyo = 0;
         fHoy     = ~;
         aAlfa    = fHoy;
         aAlfa    = aAlfa % -104;
     |SINO;
         aAlfa = enAnyo;
     |FINSI;
     aAlfa   = "01.01." + aAlfa;
     fFecha1 = aAlfa;

     swHayAlguno = 0;
     |SI eaProcedencia = "labtraba"; |O eaProcedencia = "nomw0004";
     |O eaProcedencia = "labempre";
     |O eaProcedencia = "nomir003";
          swHayAlguno = 1;
     |SINO;
          |SI nLaboral = 1;
              |BUCLE labtraba;
          |FINSI;
     |FINSI;

     |SI swHayAlguno = 0;
          |CAMPO_MODIFICA #agidanif#36, 1, "S";
          |CAMPO_VISUAL #agidanif#36, 1, "S";
          |PINTA 1562, "Tipo Relaci¢n :";
          |PINTA #agidanif#36;
     |SINO;
          |CAMPO_MODIFICA #agidanif#36, 1, "N";
          |CAMPO_VISUAL #agidanif#36, 1, "N";
          |PINTA 1562, "                  ";
     |FINSI;

     |CAMPO_MODIFICA #agidanif#8, 1, "S";
     |CAMPO_MODIFICA #agidanif#9, 1, "S";
     |CAMPO_MODIFICA #agidanif#10, 1, "S";
     |CAMPO_MODIFICA #agidanif#11, 1, "S";
     |CAMPO_MODIFICA #agidanif#12, 1, "S";

     |SI #agidanif#1 = 01;
          |SI eaProcedencia = "labtraba"; |O eaProcedencia = "nomw0004";
          |O eaProcedencia = "labempre";
          |O eaProcedencia = "nomir003";
               |CAMPO_MODIFICA #agidanif#8, 1, "N";
               |CAMPO_MODIFICA #agidanif#9, 1, "N";
               |CAMPO_MODIFICA #agidanif#10, 1, "N";
               |CAMPO_MODIFICA #agidanif#11, 1, "N";
               |CAMPO_MODIFICA #agidanif#12, 1, "N";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO PonSiglo20;
     aAlfa1    = (#agidanif#Campo# % 106) + "19" + (#agidanif#Campo# % -102);
     #agidanif#Campo# = aAlfa1;
     |PINTA #agidanif#Campo#;
|FPRC;

|PROCESO CumpleAnyos;
     aAlfa1 = fHoy;             aAlfa1 = aAlfa1 % -104; nAny1 = aAlfa1;
     aAlfa1 = #agidanif#Campo#; aAlfa1 = aAlfa1 % -104; nAny2 = aAlfa1;
     nEdad  = nAny1 - nAny2;

     |SI nEdad ! 18; |ACABA; |FINSI;

     aAlfa1 = fHoy;             aAlfa1 = aAlfa1 % 402; nMes1 = aAlfa1;
     aAlfa1 = #agidanif#Campo#; aAlfa1 = aAlfa1 % 402; nMes2 = aAlfa1;

     |SI nMes2 > nMes1;  nEdad = nEdad - 1; |FINSI;
     |SI nMes1 ! nMes2;  |ACABA;            |FINSI;

     aAlfa1 = fHoy;             aAlfa1 = aAlfa1 % 102; nDia1 = aAlfa1;
     aAlfa1 = #agidanif#Campo#; aAlfa1 = aAlfa1 % 102; nDia2 = aAlfa1;

     |SI nDia2 > nDia1;  nEdad = nEdad - 1; |FINSI;
|FPRC;

|PROCESO Tipo0C13;  |TIPO 0;
     |SI #agidanif#2 = "DESCENDIENTE";  |ACABA;  |FINSI;

     fHoy = ~;

     |SI #agidanif#Campo# > fHoy;    || nacimientos futuros como que no
          |HAZ PonSiglo20;    ||/se cambia sin preguntar
     |FINSI;


     |SI #agidanif#Campo# ] "01.01.2000"; |Y #agidanif#Campo# [ fHoy;     || presuntos ni¤os,
                                                            ||/se pregunta
         |HAZ CumpleAnyos;
         |SI nEdad < 18;
             aAlfa = "0000SLa fecha puede ser incorrecta. Ha introducido un contribuyente menor de edad. Corregir fecha?";
             |CONFI aAlfa;
             |SI FSalida = 0;
                 |ERROR;
                 || |HAZ PonSiglo20;
             |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO ActualizaLaboral;
     |SI enAnyo = 0;
         fHoy     = ~;
         aAlfa    = fHoy;
         aAlfa    = aAlfa % -104;
     |SINO;
         aAlfa = enAnyo;
     |FINSI;
     aAlfa   = "01.01." + aAlfa;
     fFecha1 = aAlfa;

     |ABRE #labtraba;
     swHayAlguno = 0;
     |SI eaProcedencia = "nomir001";
         swHayAlguno = 1;
     |SINO;
         |SI eaProcedencia = "labtraba"; |O eaProcedencia = "nomw0004";
         |O eaProcedencia = "labempre";
             swHayAlguno = 1;
         |SINO;
             |BUCLE labtraba;
         |FINSI;
     |FINSI;

     |SI swHayAlguno = 0;
         |CIERRA #labtraba;
         |ACABA;
     |FINSI;

     |SI enAnyo = 0;
         |SI eaProcedencia = "labtraba"; |O eaProcedencia = "nomw0004";
         |O eaProcedencia = "labempre";
         |O eaProcedencia = "regularizacion";
         |O eaProcedencia = "";    || del propio mantenimiento de NIF
             || ya tengo empresa cargada del labtraba o del nomw0004,
             || el a¤o lo cojo de la fecha del sistema
             || si es un vengo del propio mantenimiento de NIF la empresa
             || la cojo

             fFecha = ~;
             aAlfa  = fFecha % -104;
             nAnyo  = aAlfa;
         |FINSI;
     |SINO;
         nAnyo  = enAnyo;
     |FINSI;

     nEmpresa = enEmpresa;
     |SI enEmpresa ! 0; |Y enTrabajador ! 0;
         |HAZ ActuaTodos;
     |SINO;
         nOpera = 1; |BUCLE labtraba; nOpera = 0;
     |FINSI;

     |SI PARAMETRO = "DESDELABORBOX";
         |ACABA;
     |FINSI;

     aAlfa = nAnyo;
     |SI eaProcedencia = "nomir001";
     |O eaProcedencia = "labtraba"; |O eaProcedencia = "nomw0004";
     |O eaProcedencia = "labempre";
     |O eaProcedencia = "";    || del propio mantenimiento de NIF
         aAlfa1 = "    Actualizados los parámetros de cálculo de ";
         aAlfa1 = aAlfa1 + "IRPF para el año " + aAlfa + ". ";
         |MENAV aAlfa1;
     |FINSI;
|FPRC;

|PROCESO ActuaTodos;
||     |SI enEmpresa    = 0;  enEmpresa    = #labtraba#0;  |FINSI;
||     |SI enTrabajador = 0;  enTrabajador = #labtraba#1;  |FINSI;

     || si no existe la cabecera y vengo del labtraba puedo grabar
     || la cabecera (el nomir001), eso es al dar de alta una ficha

     |SI eaProcedencia = "labtraba"; |O eaProcedencia = "nomw0004";
     |O eaProcedencia = "labempre";
     |O eaProcedencia = "regularizacion";
     |O eaProcedencia = "";    || del propio mantenimiento de NIF
         |ABRE #nomir001;
         #nomir001#0 = eaNif;
         #nomir001#2 = nAnyo;
         #nomir001#3 = enEmpresa;
         |LEE 101#nomir001.=;
         |SI FSalida ! 0;
             |DDEFECTO #nomir001;
             #nomir001#0 = eaNif;
             |SI eaProcedencia = "regularizacion";
                 || vengo de la regularizacion del calculo de nominas
                 || no tengo cargado el nombre

                 #nomir001#1 = #labtraba#2;
             |SINO;
                 #nomir001#1 = #agidanif#12;
             |FINSI;
             #nomir001#2 = nAnyo;
             #nomir001#3 = enEmpresa;
             #nomir001#4 = enTrabajador;
             #nomir001#5 = "S";
             |GRABA 020#nomir001.n;
             |LIBERA #nomir001;
         |FINSI;
         |CIERRA #nomir001;
     |FINSI;

     |ABRE #nomir003;
     #nomir003#0  = eaNif;
     #nomir003#40 = nAnyo;
     #nomir003#41 = enEmpresa;
     |LEE 101#nomir003.=;
     |SI FSalida ! 0;
         |DDEFECTO #nomir003;
         #nomir003#0  = eaNif;
         #nomir003#40 = nAnyo;
         #nomir003#41 = enEmpresa;
         |GRABA 020#nomir003.b;
     |FINSI;

     #nomir003#14 = "N";
     |PDEFECTO #nomir003,  1, 5;
     |PDEFECTO #nomir003,  6, 9;
     |PDEFECTO #nomir003, 20, 40;
     |PDEFECTO #nomir003, 42, 114;

     |SI eaProcedencia ! "regularizacion";
         || estos dos datos los compruebo antes, para que pasen bien al
         || nomir003
         |HAZ comp_fechanac;
         |HAZ comp_sitfam;
     |FINSI;

     |BUCLE agidanif;

     |SI #nomir003#2 = " ";  #nomir003#2 = "A";  |FINSI;

     |SI eaProcedencia ! "regularizacion";
         || si estoy regularizando la nomina no me interesa que lo haga
         || aqui, porque asi se hay un error saltara en las incidencias
         || de la nomina y desde la propia nomina me lo pasar a tipo 3,
         || en el nomir003 y en el agidanif despues

         |HAZ comp_hijos;
         |HAZ comp_conyuge;
     |FINSI;

     |SI eaProcedencia = "nomir001";
     |O eaProcedencia = "labtraba"; |O eaProcedencia = "nomw0004";
     |O eaProcedencia = "labempre";
     |O eaProcedencia = "";
             #nomir003#5 = #labtraba#111;
             #nomir003#121 = #labtraba#130;
     |FINSI;

     |GRABA 020#nomir003.a;
     |LIBERA #nomir003;
     |CIERRA #nomir003;
|FPRC;

|PROCESO comp_fechanac_fin;
     swComprueba = 0;
     |SI enModoNif ! 4;
          |SI enEmpresa ! 0; |Y enTrabajador ! 0;
               swComprueba = 1;
          |FINSI;
          |SI eaProcedencia = "nomir001";
               |SI enEmpresa ! 0;
                    swComprueba = 1;
               |FINSI;
          |FINSI;
     |FINSI;
     |SI swComprueba = 1;
          |ABRE #agidanif;
          swSal = 0;
          |PARA; |SI swSal = 0; |HACIENDO;
               #agidanif#0 = eaNif;
               #agidanif#1 = 01;
               |LEE 000#agidanif.=;
               |SI FSalida = 0;
                    |SI #agidanif#13 = "01.01.0000";
                         aAlfa = "    ATENCION. Es obligatorio rellenar la fecha de ";
                         aAlfa = aAlfa + "nacimiento del TITULAR";
                         |MENAV aAlfa;
                         |ENTLINEAL #1#agidanif, -2, 2, 22, 1, ClaveBaja0, ClaveAlta0;
                    |SINO;
                         swSal = 1;
                    |FINSI;
               |FINSI;
          |SIGUE;
          |CIERRA #agidanif;
     |FINSI;
|FPRC;

|PROCESO EntradaConLaboral;
     |SI enModoNif ! -1;
         |DBASE aPathCliente;  |QBT aPathCliente;
         aDef = aPathCliente + "def/agidanif.mas";
         |CARGA_DEF aDef, agidanif@;
         |SI FSalida ! 0;
             aAlfa = "     Error en cargar el Def agidanif.mas";
             |MENAV aAlfa;
             |ERROR;
         |FINSI;

         |DBASS aPathLaboral;  |QBT aPathLaboral;
         aPathLaboral = aPathLaboral + "laboral/fich/";
         |PATH_DAT #nomir001#aPathLaboral#;

         |ABRE #agidanif@;
         |SI enModoNif ! 4;
             |ENTLINEAL #1#agidanif, -2, 2, 22, 1, ClaveBaja0, ClaveAlta0;
         |SINO;
             |ENTLINEAL #1#agidanif, -2, 4, 22, 1, ClaveBaja0, ClaveAlta0;
         |FINSI;

          |HAZ comp_fechanac_fin;

          |CIERRA #agidanif@;

          |DESCARGA_DEF #agidanif@;
     |FINSI;

     |SI enModoNif = 4;
         |ACABA;
     |FINSI;

     |HAZ ActualizaLaboral;
|FPRC;

|PROCESO EntradaSinLaboral;
     |SI enModoNif = -1;
         |ACABA;
     |FINSI;

     |DBASE aPathCliente;  |QBT aPathCliente;
     aDef = aPathCliente + "def/agidanif.mas";
     |CARGA_DEF aDef, agidanif@;
     |SI FSalida ! 0;
         aAlfa = "     Error en cargar el Def agidanif.mas";
         |MENAV aAlfa;
         |ERROR;
     |FINSI;

     |ABRE #agidanif@;

     |SI enModoNif ! 4;
         |ENTLINEAL #1#agidanif, -2, 2, 22, 1, ClaveBaja0, ClaveAlta0;
     |SINO;
         |ENTLINEAL #1#agidanif, -2, 4, 22, 1, ClaveBaja0, ClaveAlta0;
     |FINSI;

     |CIERRA #agidanif@;

     |DESCARGA_DEF #agidanif@;
|FPRC;

|PROGRAMA;
     nLaboral = 0;
     |DBASS aBase;   |QBF aBase;
     aAlfa1 = aBase + "laboral/def/labempre.mas";
     |XABRE "E", aAlfa1, nCanal;
     |SI FSalida ] 0;
         nLaboral = 1;
     |FINSI;

     eaVengoDe = "agidanif";
     |SI PARAMETRO = "DESDELABORBOX";
         |DBASS aPathLaboral;  |QBT aPathLaboral;
         aPathLaboral = aPathLaboral + "laboral/fich/";
         |PATH_DAT #nomir001#aPathLaboral#;

         |HAZ ActualizaLaboral;

         |ACABA;
     |FINSI;

     |SI nLaboral = 1;
         |HAZ EntradaConLaboral;
         |ACABA;
     |FINSI;

     |SI nLaboral = 0;
         |HAZ EntradaSinLaboral;
     |FINSI;
     eaVengoDe = "";
|FPRO;
