|FICHEROS;
     agiz0015 #0;

     agim0008 #1;        || Control de Visitas
     agim0009 #2;        || Llamadas
     agim0013 #3;        || Entrevistas
     agim0012 #4;        || Archivos

     agifigen #10;       || Ficha de Clientes
     agicen14 #11;       || Actividades
     agim0004 #12;       || Representantes
     agiz0014 #13;       || Ayuda Conceptos Contratados
     agim0001 #14;       || Conceptos Contratados
     agim0015 #16;       || Salarios

     agiw0024 #99;
     agiw0021 #95;       || Pantalla Totales Rendimientos

     &clientes@ #715;
|FIN;

|VARIABLES;
     &Cliente     = 0;
     &enEmpresa   = 0;
     &enActividad = 0;
     &enCodCli    = 0;
     &enSwDeDonde = 0;

     Fichero    = "";
     GrabaTempo = 0;
     NMovi      = 0;

 {-1} Menu      = "";
      Menu1     = "1514";
      Menu2     = "1";
      Menu3     = "3";
      Menu4     = "Datos Generales         ";
      Menu5     = "Entrevistas             ";
      Menu6     = "Historico del Cliente   ";

 {-1} MenuGnr   = "";
      MenuGnr1  = "1751";
      MenuGnr2  = "1";
      MenuGnr3  = "5";
      MenuGnr4  = "Datos Clientes          ";
      MenuGnr5  = "Actividades             ";
      MenuGnr6  = "Representantes          ";
      MenuGnr7  = "Conceptos Contratados   ";
      MenuGnr8  = "Consulta Rendimientos   ";
|FIN;

|PROCESO PintaCliente;
     enCodCli = #0#0;
     |HAZ LeePersona;
     |SI enSwDeDonde = 1;
         #0#1 = #715#1;
         #0#2 = #715#10;
         #0#3 = #715#11;
         #0#4 = #715#13;
         #0#5 = #715#14;
     |SINO;
         #0#1 = #715#1;
         #0#2 = #715#88;
         #0#3 = #715#89;
         #0#4 = #715#90;
     |FINSI;

     enCodCli = #0#4;
     |HAZ LeePersona;
     #0#5 = #715#1;

     |PINTA #0#1;
     |PINTA #0#2;
     |PINTA #0#3;
     |PINTA #0#4;
     |PINTA #0#5;
|FPRC;


|| *************************************************************************
||                  VISUALIZACION DEL HISTORICO
|| *************************************************************************

|| ****************************************** Visitas

|PROCESO Visitas;
  |DDEFECTO #99;
  #99#0 = 01;              || Programa;
  #99#1 = #1#0;            || Fecha
  #99#2 = #1#2;            || Hora
  #99#3 = #1#3;            || Minuto
  #99#4 = 0;               || Referencia
  #99#5 = "Visita. " + #1#7;
  #99#6 = #1#1;            || Usuario
  #99#7 = #1#4;
  |GRABA 140#99n;
  |LIBERA #99;
  GrabaTempo = 1;
|FPRC;

|DEFBUCLE agim0008;
  #1#3;
  ;
  #0#0, "01.01.0000", "          ", 00, 00;
  #0#0, "31.12.2999", "zzzzzzzzzz", 99, 99;
  ;
  NULL, Visitas;
|FIN;

|| ****************************************** Llamadas

|PROCESO Llamadas;
  |DDEFECTO #99;
  #99#0 = 02;              || Programa;
  #99#1 = #2#0;            || Fecha
  #99#2 = #2#1;            || Hora
  #99#3 = #2#2;            || Minuto
  #99#4 = 0;               || Referencia
  #99#5 = "Llamada. " + #2#7;
  #99#6 = #2#3;            || Usuario
  #99#7 = #2#4;
  |GRABA 140#99n;
  |LIBERA #99;
  GrabaTempo = 1;
|FPRC;

|DEFBUCLE agim0009;
  #2#2;
  ;
  #0#0, "01.01.0000", 00, 00;
  #0#0, "31.12.2999", 99, 99;
  ;
  NULL, Llamadas;
|FIN;

|| ****************************************** Entrevistas

|PROCESO Entrevista;
  |DDEFECTO #99;
  #99#0 = 03;              || Programa;
  #99#1 = #3#0;            || Fecha
  #99#2 = #3#3;            || Hora
  #99#3 = #3#4;            || Minuto
  #99#4 = 0;               || Referencia
  #99#5 = "Entrevista con el cliente ";
  #99#6 = #3#5;            || Usuario
  #99#7 = #3#6;            || Nombre del Usuario
  |GRABA 140#99n;
  |LIBERA #99;
  GrabaTempo = 1;
|FPRC;

|DEFBUCLE agim0013;
  #3#1;
  ;
  "00.00.0000", #0#0;
  "31.12.2999", #0#0;
  ;
  NULL, Entrevista;
|FIN;

|| ****************************************** Archivos

|PROCESO Archivos;
  |DDEFECTO #99;
  #99#0 = 04;              || Programa;
  #99#1 = #4#7;            || Fecha
  #99#2 = 00;              || Hora
  #99#3 = 00;              || Minuto
  #99#4 = #4#1;            || Referencia
  #99#5 = "Archivo " + #4#6 + " " + #4#3;
  #99#6 = #4#8;            || Usuario
  #99#7 = #4#9;            || Nombre del Usuario
  |GRABA 140#99n;
  |LIBERA #99;
  GrabaTempo = 1;
|FPRC;

|DEFBUCLE agim0012;
  #4#1;
  ;
  #0#0, 0000;
  #0#0, 9999;
  ;
  NULL, Archivos;
|FIN;

|RUTINA ClaveBaja99;
  #99#0 = 00;
  #99#1 = "00.00.0000";
  #99#2 = 00;
  #99#3 = 00;
  #99#4 = 0;
|FRUT;

|RUTINA ClaveAlta99;
  #99#0 = 99;
  #99#1 = "31.12.9999";
  #99#2 = 99;
  #99#3 = 99;
  #99#4 = 9999;
|FRUT;

|PROCESO Historico;
  Fichero = Usuario % 808;  |QBT Fichero;
  Fichero = ("24" + Fichero + "zzzzzzzz") % 108;
  |NOME_DAT #99 Fichero;
  |ABRE #99;  |CIERRA #99;  |DELINDEX #99;

  GrabaTempo = 0;

  |ABRE #99;
  |BUCLE agim0008;
  |BUCLE agim0009;
  |BUCLE agim0013;
  |BUCLE agim0012;
  |CIERRA #99;

   |SI GrabaTempo = 0;  |ACABA;  |FINSI;

  |PUSHV 0501 2380;
  |PINPA #0#99;
  |ENTLINEAL #1#99, -3, 4, 21, 1, ClaveBaja99, ClaveAlta99;
  |POPV;

  |DELINDEX #99;
|FPRC;

|| *************************************************************************
||                    DATOS GENERALES
|| *************************************************************************

|| ****************** Datos Clientes

|PROCESO MenuGnr1;
  |ABRE #10;
  #10#0 = #0#0;
  |LEE 040#10=;
  |SI FSalida ! 0;  |DDEFECTO #10;  |FINSI;
  |CIERRA #10;

  |ET Panta1;
      |PINPA #0#10;
      |PINDA #0#10;
      |ATRI R; |PINTA 0543, "                              "; |ATRI 0;
      |PAUSA;
      |SI FSalida = 2;  |O FSalida = 4;  |VETE Panta1;  |FINSI;
      |SI FSalida = 1;  |O FSalida = 7;  |ACABA;  |FINSI;

  |ET Panta2;
      |PINPA #1#10;
      |PINDA #1#10;
      |PAUSA;
      |SI FSalida = 2;  |O FSalida = 4;  |VETE Panta1;  |FINSI;
      |SI FSalida = 1;  |O FSalida = 7;  |ACABA;        |FINSI;

  |ET Panta3;
      |PINPA #2#10;
      |PINDA #2#10;
      |PAUSA;
      |SI FSalida = 2;  |O FSalida = 4;  |VETE Panta2;  |FINSI;
      |SI FSalida = 1;  |O FSalida = 7;  |ACABA;        |FINSI;
|FPRC;

|| ****************** Actividades

|RUTINA ClaveBaja11;
  #11#0 = #0#0;
  #11#1 = 01;
|FRUT;

|RUTINA ClaveAlta11;
  #11#0 = #0#0;
  #11#1 = 99;
|FRUT;

|PROCESO MenuGnr2;
  |ENTLINEAL #1#11, -2, 4, 22, 1, ClaveBaja11, ClaveAlta11;
|FPRC;

|| ****************** Representantes

|RUTINA ClaveBaja12;
  #12#0 = #0#0;
  #12#1 = 01;
|FRUT;

|RUTINA ClaveAlta12;
  #12#0 = #0#0;
  #12#1 = 99;
|FRUT;

|PROCESO MenuGnr3;
  |ENTLINEAL #1#12, -2, 4, 21, 1, ClaveBaja12, ClaveAlta12;
|FPRC;

|| ****************** Conceptos Contratados

|RUTINA ClaveBaja14;
  #14#0 = #0#0;
  #14#1 = 01;
|FRUT;

|RUTINA ClaveAlta14;
  #14#0 = #0#0;
  #14#1 = 99;
|FRUT;

|PROCESO MenuGnr4;
  |PUSHV 0501 0780;
  |PINPA #0#13;
  |POPV;
  |D_CUADRO 0802, 2179;

  |ABRE #10;
  #10#0 = #0#0;
  |LEE 040#10=;
  |SI FSalida ! 0;  |DDEFECTO #10;  |FINSI;
  |CIERRA #10;

  #13#4 = #10#79;  |PINTA #13#4;
  #13#5 = #10#80;  |PINTA #13#5;
  #13#6 = #10#81;  |PINTA #13#6;
  #13#7 = #10#82;  |PINTA #13#7;
  #13#8 = #10#83;  |PINTA #13#8;
  #13#9 = #10#84;  |PINTA #13#9;

  |ENTLINEAL #1#14, -2, 4, 20, 1, ClaveBaja14, ClaveAlta14;
|FPRC;

|| ****************** Rendimientos

|PROCESO defectos;
|FPRC;

|PROCESO totales;
|FPRC;

|PROCESO MenuGnr;
  |PUSHV 0501 2380;
  |PARA;  |SI;  |HACIENDO;
          |MENUG MenuGnr;
          MenuGnr2 = FSalida;
          |SI MenuGnr2 ] 1; |Y MenuGnr2 [ 5;
              |PUSHV 0501 2380;
              |SI MenuGnr2 = 1; |HAZ MenuGnr1;      |FINSI;
              |SI MenuGnr2 = 2; |HAZ MenuGnr2;      |FINSI;
              |SI MenuGnr2 = 3; |HAZ MenuGnr3;      |FINSI;
              |SI MenuGnr2 = 4; |HAZ MenuGnr4;      |FINSI;
              |SI MenuGnr2 = 5;
                  enEmpresa   = #0#0;
                  enActividad = 0;
                  |SUB_EJECUTA_NP "agim0002";
              |FINSI;
              |POPV;
          |SINO;
              |SAL;
          |FINSI;
  |SIGUE;
  |POPV;
|FPRC;

|| *************************************************************************
||                    EMPIEZA EL MEOLLO
|| *************************************************************************

|PROCESO Tipo3;  |TIPO 3;
  |PARA;  |SI;  |HACIENDO;
          Cliente = #0#0;
          |MENUG Menu;
          Menu2 = FSalida;
          |SI Menu2 ] 1; |Y Menu2 [ 3;
              |SI Menu2 = 1; |HAZ MenuGnr;             |FINSI;
              |SI Menu2 = 2; |SUB_EJECUTA_NP "agiw0025";  |FINSI;
              |SI Menu2 = 3; |HAZ Historico;           |FINSI;
          |SINO;
              |SAL;
          |FINSI;
  |SIGUE;
|FPRC;
