|FICHEROS;
   agiz0022 #0;
   agifigen #1;
   dsinform #100,MANTE;

   agiw0067 #998;
   canempr@ #330;
|FIN;

|VARIABLES;
 fLaFecha   = @;
 aAlfa1     = "";
 aAlfa2     = "";
 nNume1     = 0;
 nNume2     = 0;
 &enEmpresa = 0;
 &enSwalgun = 0;
 nCampo     = 0;
 aBlanc     = "";
 aZetes     = "";
 aMas       = "";
 aDef       = "";
 aFichero   = "";
 aParam     = "";
 nPara1     = 0;
 nGTempo    = 0;
 nDEmpresa  = 0;
 nHEmpresa  = 0;
 nSPin1     = 0;
 enSwSelEmp = 0;
 nEmpresa   = 0;
 aEjecutable = "";
 aDef330     = "";
 nPerConta   = 0;
 aDirConta   = "";
 &eaPathContagen = "";
 &eaParam2   = "";
|FIN;

|INCLUYE i_consul;
|INCLUYE i_masivo;  || Variables Utilizadas en Procesos Masivos

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\

|PROCESO Alta100;
 #100#6 = aBlanc;
 #100#7 = 0;
 #100#8 = aParam;
|FPRC;

|PROCESO Baja100;
 #100#6 = aZetes;
 #100#7 = 99;
 #100#8 = aParam;
|FPRC;

|CALCULO Eano; |TIPO 7;
 fLaFecha = ~;
 aAlfa1 = fLaFecha; aAlfa1 = aAlfa1 % -104;
 #0#0  = aAlfa1; |PINTA #0#0;
 |HAZ LosDefectos;

 |ENTLINEAL #2#100, -2, 7, 22, 0, Alta100, Baja100;
|FCAL;

|CALCULO LosDefectos; |TIPO 0;
 aAlfa1 = #0#0;
 aAlfa2 = "01.01." + aAlfa1; #0#31 = aAlfa2; |PINTA #0#31;
 aAlfa2 = "31.12." + aAlfa1; #0#32 = aAlfa2; |PINTA #0#32;
|FCAL;

|CALCULO Tipo7;  |TIPO 7;
     |SI enEmpresa ! 0;
         #0#1  = enEmpresa;  |PINTA #0#1;
         #0#2  = enEmpresa;  |PINTA #0#2;

         |C_M #0#1, 1, "N";
         |C_M #0#2, 1, "N";
     |FINSI;
|FCAL;

|CALCULO Parrilla; |TIPO 0;
     |SI #0#1 = 0;
         #0#1 = 0;  |PINTA #0#1;
         #0#2 = 0;  |PINTA #0#2;  |C_M #0#2, 1, "N";
         |PARA nCampo = 3;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#1, 1, "S";
             |C_M #0#2, 1, "S";
         |FINSI;

         |PARA nCampo = 3;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas; |TIPO 0;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO YaSeCual; |TIPO 7;
 |SI enEmpresa ! 0;
     #0#17 = "S"; |PINTA #0#17;
     #0#18 = "S"; |PINTA #0#18;
     #0#19 = 00;  |PINTA #0#19;
     #0#20 = 99;  |PINTA #0#20;
     |C_M #0#17, 1, "N";
     |C_M #0#18, 1, "N";
     |C_M #0#19, 1, "N";
     |C_M #0#20, 1, "N";
 |FINSI;
|FCAL;

|CALCULO SelTipo0; |TIPO 0;
 |SI #0#18 = "N";
     #0#19 = 0;   |PINTA #0#19;  |C_M #0#19, 1, "N";
     #0#20 = 99;  |PINTA #0#20;  |C_M #0#20, 1, "N";
     |PARA nCampo = 21;  |SI nCampo [ 30;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |SINO;
     |SI enEmpresa = 0;
         |C_M #0#19, 1, "S";
         |C_M #0#20, 1, "S";
     |FINSI;
     |PARA nCampo = 21;  |SI nCampo [ 30;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "N";
           #0nCampo = 0;  |PINTA #0nCampo;
     |SIGUE;
 |FINSI;
|FCAL;

|CALCULO SelTipo1;
 |SI #0Campo = 0;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 30;  |HACIENDO nCampo = nCampo + 1;
           #0nCampo = 0;  |C_M #0nCampo, 1, "N";
     |SIGUE;
 |SINO;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 30;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |FINSI;
|FCAL;

|| *********************************************************
|PROCESO MTipo5;  |TIPO 5;
       |LEE 040#100p;
       #100#4 = "S";
       |LINEAS_BI_ATRI #100#4, D, S, -1;
|FPRC;

|PROCESO Tipo11;
|FPRC;

|PROCESO Tipo11P;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;
     |SELECT #2#100;
     |LEE 101#100=;
     |SI #100#4 = "N";
         #100#4 = "S";
     |SINO;
         #100#4 = "N";
     |FINSI;

     |PINTA #100#4;

     |GRABA 020#100a;
     |LIBERA #100;
|FPRC;
|| ------------------------------------------------------------------------

|PROCESO Ejecutable;

  |SI #100#0 = "gen";
      |SI #998#4 < 0;  || no hay contabilidad
          |ACABA;
      |FINSI;
      aAlfa1 = #998#5; |QBF aAlfa1;
      aAlfa1 = aAlfa1 % -107;
      aAlfa1 = aAlfa1 %  106;
      |SI #100#1 ! "cay3ivas";
          aEjecutable = aEjecutable + " canempr1 " + aAlfa1 + "; N";
      |SINO;
          aEjecutable = aEjecutable + " canempr1 " + aAlfa1;
          |SI #100#9 = 1;
              aEjecutable = aEjecutable + ";D";
          |FINSI;
      |FINSI;
  |FINSI;

  nNume2 = 1; |SI #100#1 = "cay3ivas"; nNume2 = 2; |FINSI;

  |PARA nPara1 = 1; |SI nPara1 [ nNume2; |HACIENDO nPara1 = nPara1 + 1;
        |SI #100#1 = "cay3ivas";
            |SI nPara1 = 1; eaParam2 = "R"; |FINSI;
            |SI nPara1 = 2; eaParam2 = "S"; |FINSI;
        |FINSI;

        enEmprM = nEmpresa;
        enEjerM = #0#0;
        enDActM = #0#15;
        enHActM = #0#16;
        efDFecM = #0#31;
        efHFecM = #0#32;
        enPConM = #998#4;
        eaCDirM = #998#5; |QBF eaCDirM;
        enSwMvo = 1;
        enNuMvo = #100#9;

        |PINTA 2103, #100#6;
        |PINTA 2213, #998#1;
        |PINTA 2219, #998#2;
        |SUB_EJECUTA_NP aEjecutable;
        enSwMvo = 0;
   |SIGUE;
|FPRC;

|PROCESO PorEmpresa2;
 nEmpresa = #998#1;
 |HAZ Ejecutable;
|FPRC;

|DEFBUCLE PorEmpresa2;
 #998#1;
 ;
 000001;
 999999;
 ;
 NULL, PorEmpresa2;
|FIN;


|PROCESO PorInforme;
 aEjecutable = #100#5; |QBF aEjecutable;
 |SI aEjecutable = "";
     aAlfa1 = #100#3; |QBF aAlfa1;
     aAlfa2 = #100#1; |QBF aAlfa2;
     aEjecutable = ":"+aAlfa1+"/"+aAlfa2;
 |FINSI;

 |SI #0#33 = "P";
     |BUCLE PorEmpresa2;
 |SINO;
     |HAZ Ejecutable;
 |FINSI;
|FPRC;

|DEFBUCLE PorInforme;
 #100#2;
 4;
 aParam, 01, "S";
 aParam, 99, "S";
 ;
 NULL, PorInforme;
|FIN;

|PROCESO PorEmpresa;
 nEmpresa = #998#1;
 |BUCLE PorInforme;
|FPRC;

|DEFBUCLE PorEmpresa;
 #998#1;
 ;
 000001;
 999999;
 ;
 NULL, PorEmpresa;
|FIN;

|| //////////////////////////////////////////////////////////////////////////
|PROCESO LEmpresa;

 |SI #agifigen#94 > "01.01.0000";
     |SI #0#17 = "N"; |ACABA; |FINSI;
 |FINSI;

|| ... Seleccion de Tipo de Empresa
  nSPin1 = #agifigen#87;
  enSwSelEmp = 1;
  |SI #0#18 = "S";
      |SI nSPin1 ] #0#19; |Y nSPin1 [ #0#20;
          enSwSelEmp = 0;
      |FINSI;
  |SINO;
      |SI nSPin1 = #0#21;
          enSwSelEmp = 0;
      |SINO;
          |PARA nCampo = 22;  |SI nCampo [ 30;  |HACIENDO nCampo = nCampo + 1;
                |SI nSPin1 = #0nCampo; |Y nSPin1 ! 0;
                    enSwSelEmp = 0;
                    |SAL;
                |FINSI;
          |SIGUE;
      |FINSI;
  |FINSI;
  |SI enSwSelEmp = 1; |ACABA; |FINSI;

|| ------------------------------------------------------------------------
   nPerConta = -1;
   aDirConta = "";
   |SI aDef330 ! "";
       aAlfa1 = #0#0;
       aAlfa1 = aAlfa1 % -102;
       nNume1 = aAlfa1;
       |ABRE #330;
       |SELECT #4#330;
       #330#2  = #agifigen#0;   || Empresa
       #330#26 = nNume1;
       |LEE 041#330=;
       |SI FSalida = 0;
           aAlfa1 = #330#2; aAlfa1 = ("00000" + aAlfa1) % -105;
           aAlfa2 = #330#3; aAlfa2 = ("0" + aAlfa2) % -101;
           nPerConta = #330#3;
           aDirConta = eaPathContagen + "fich/" + aAlfa1 + aAlfa2 + "/";
       |FINSI;
       |SELECT #1#330;
       |CIERRA #330;
   |FINSI;
   nGTempo = nGTempo + 1;
   #998#0 = nGTempo;       || Guia
   #998#1 = #agifigen#0;   || Empresa
   #998#2 = #agifigen#1;   || Nombre
   #998#3 = #0#0;          || A¤o
   #998#4 = nPerConta;
   #998#5 = aDirConta;
   |GRABA 040#998n;
|FPRC;

|DEFBUCLE agifigen;
 #agifigen#1;
 ;
 nDEmpresa;
 nHEmpresa;
 e;
 NULL, LEmpresa;
|FIN;

|| //////////////////////////////////////////////////////////////////////////
|PROCESO CreaAuxiliar;

     |SI eaPathContagen ! "";
         aDef330 = eaPathContagen + "def/canempre";
         |CARGA_DEF aDef330, canempr@;
         |SI FSalida ! 0;
             aDef330 = "";
         |FINSI;
     |FINSI;

     aFichero = "y" + Usuario;
     |NOME_DAT #998 aFichero;
     |DELINDEX #998;

     |ABRE #998;
     nGTempo = 0;
     |SI #0#1 ! 0;
         nDEmpresa = #0#1;
         nHEmpresa = #0#2;
         |BUCLE agifigen;
     |SINO;
         |PARA nCampo = 3;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               |SI #0nCampo ! 0;
                   nDEmpresa = #0nCampo;
                   nHEmpresa = #0nCampo;
                   |BUCLE agifigen;
               |FINSI;
         |SIGUE;
     |FINSI;
     |CIERRA #998;

     |SI aDef330 ! ""; |DESCARGA_DEF #330; |FINSI;
|FPRC;

|CALCULO Tipo3; |TIPO 12;
  |ENTLINEAL #2#100, -2, 4, 22, 0, Alta100, Baja100;

  aAlfa1 = "    SLanzar Informes seleccionados";
  |SI aParam = "P";
      aAlfa1 = "    SLanzar Procesos seleccionados";
  |FINSI;

  |CONFI aAlfa1;
  |SI FSalida = 0;
      |HAZ CreaAuxiliar;
      |SI nGTempo = 0;
          |MENAV "    Seleccion vacia";
      |SINO;
          |IMPRE 0;
          |SI FSalida ! 0; |ACABA; |FINSI;
          eaImMvo = Impresora;

          |BLIN 20;
          |BLIN 21;
          |BLIN 22;
          |CUADRO 2001 2380;
          |ATRI S;
          |PINTA 2203, "Empresa: ";
          |ATRI 0;

          |SI #0#33 = "P";
              |BUCLE PorInforme;
          |SINO;
              |BUCLE PorEmpresa;
          |FINSI;
          |FINIMP;
      |FINSI;
  |FINSI;
|FCAL;

|| ----------------------------------------------------------------------

|PROGRAMA;

  aBlanc = " " * 80;
  aZetes = "z" * 80;

  aParam = PARAMETRO; |QBF aParam;

  |SI aParam ! "I"; |Y aParam ! "P";  aParam = "I"; |FINSI;

  |CLS;
  |SI aParam = "I";
      |CABEZA "Informes Masivos";
      |C_V #0#33, 1, "S";
  |SINO;
      |CABEZA "Procesos Masivos";
      |C_V #0#33, 1, "N";
  |FINSI;

  |SUB_EJECUTA_NP "agiz0023";
  |SI enSwalgun = 0;
      |MENAV "    Error en instalacion de Informes Masivos.";
      |ACABA;
  |FINSI;

  |C_V #100#0,1,"N"; |C_V #100#1,1,"N";
  |C_V #100#2,1,"N"; |C_V #100#3,1,"N";
  |C_V #100#4,1,"S"; |C_P #100#4,1,1578;
  |C_V #100#5,1,"N";

  |C_V #100#6,1,"S"; |C_L #100#6,1,"S";
  |C_P #100#6, 1, 1505; |C_A #100#6,1,72;
  |C_M #100#6,1,"S";

  |C_V #100#7,1,"S"; |C_P #100#7, 1, 1502;
  |C_V #100#8,1,"N";

  |HAZ BusquedaApli;

  |PINPA #0#0;
  |ATRI R;
  |SI aParam = "I";
      |PINTA 1414, "Informes"; |ATRI 0;
      |PINTA 1425,     "Orden por Programa o por Empresa:  ";
      |ATRI I; |PINTA 1435, "P"; |PINTA 1450, "E";
  |SINO;
      |PINTA 1414, "Procesos";
  |FINSI;
  |ATRI 0;

  |ABRE #0;
  |LEE 140#0p;
  |SI FSalida ! 0;
      |DDEFECTO #0;
      |GRABA 020#0c;
  |FINSI;

  |PINDA #0#0;
  |ENDATOS #1#0;
  |GRABA 020#0a;
  |CIERRA #0;
|FPRO;
