|FICHEROS;
   tadz0001 #0;

   tadm0001 #10;
   tadw0001 #12,MANTE;

   Referen@ #100;
|FIN;

|VARIABLES;
   &eaPathExplorador = "";
   &eaForzExplorador = "";

   nCliente = 0;
   aContrat = "";
   aNIF     = "";
   aNombre  = "";

   aTitulo = "";
   aAlfa   = "";
   aAlfa1  = "";
   aBase   = "";

   nCalc  = 0;
   nRango = 0;
   nPos1  = 0;
   nPos2  = 0;
   nSwCod = 0;
   Cmp    = 0;
   nCanal = 0;
   nSw    = 0;
   nHandPrueba = 0;

   aEsc1   = "";
   aEsc2   = "";
   aRetu   = "";
   aTecla  = "";

   aNuevo       = "";
   aNombreNuevo = "";
   aNifNuevo    = "";
   aBaseRemoto  = "";
   aIP          = "";
   aOrigen      = "";
   aDestino     = "";

   nPant0 = 0;
   nPant1 = 0;
   nGrid0 = 0;
   nVent  = 0;
   nVent0 = 0;
   nVentEsp = 0;
   nModo    = 0;

   nBotonNuevo = 0;
   nBotonModif = 0;
   nBotonBorra = 0;
   nBotonConsC = 0;
   nBotonCExpl = 0;

   || NO METER VARIABLES ENTRE ESTE BLOQUE
 {1}aContiene  = "";
{-1}aVent      = "";
    aVent1     = 0;
    aVent2     = 0;
    aVent3     = 0;
    aVent4     = 0;
    aVent5     = "";
   || NO METER VARIABLES ENTRE ESTE BLOQUE
|FIN;

|PROCESO PonVentEspera;
   |VENTANA 1203, 1482, -1, 17, ""; nVentEsp = FSalida;
   aVent1 = -1;
   aVent2 = -1;
   aVent3 = 0;
   aVent4 = nVentEsp;
   aVent5 = "MODAL";
   |ESPECIFICA "ESTADO_VENTANA", aVent;

   |CONTROL_SIMPLE 0, "LABEL,{{5}}Accediendo, espere por favor", 1205, 1480, NULL;
   |PTEC 802; |PAUSA;
|FPRC;

|PROCESO QuitaVentEspera;
   aVent4 = nVentEsp;
   aVent5 = "MODELESS";
   |ESPECIFICA "ESTADO_VENTANA", aVent;
   |FINVENTANA nVentEsp;
|FPRC;

|PROCESO GestCmp;
   |SI #tadw0001#2 = "N";
      #tadw0001#3 = "01.01.0000"; |PINTA #tadw0001#3; |C_M #tadw0001#3, 1, "N";
      #tadw0001#4 = "";           |PINTA #tadw0001#4; |C_M #tadw0001#4, 1, "N";
   |SINO;
      |C_M #tadw0001#3, 1, "S"; |C_M #tadw0001#4, 1, "S";
   |FINSI;
|FPRC;

|PROCESO GestVnt;
   |SI #tadw0001#5 = "N";
      #tadw0001#6 = "01.01.0000"; |PINTA #tadw0001#6; |C_M #tadw0001#6, 1, "N";
      #tadw0001#7 = "";           |PINTA #tadw0001#7; |C_M #tadw0001#7, 1, "N";
   |SINO;
      |C_M #tadw0001#6, 1, "S"; |C_M #tadw0001#7, 1, "S";
   |FINSI;
|FPRC;

|PROCESO MiraNif;
   nModo = (FEntrada / 100) ! 0;
   |SI nModo ! 1; |ACABA; |FINSI;

   #tadm0001#0 = nCliente;
   #tadm0001#1 = #tadw0001#0;
   |LEE 000#tadm0001.=;
   |SI FSalida = 0;
      |MENAV "    El contratista ya existe en esta ficha de cliente";
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO EvtoGrid0;
   |SI FSalida = 1; |O FSalida = 2;
      |ESTADO_CONTROL nBotonModif, "ENABLE";
      |ESTADO_CONTROL nBotonBorra, "ENABLE";

      |DDEFECTO #tadz0001;
      #tadz0001#0 = #tadm0001#3; |PINTA #tadz0001#0;
      #tadz0001#1 = #tadm0001#4; |PINTA #tadz0001#1;
      #tadz0001#2 = #tadm0001#5; |PINTA #tadz0001#2;
      #tadz0001#3 = #tadm0001#6; |PINTA #tadz0001#3;
      #tadz0001#4 = #tadm0001#7; |PINTA #tadz0001#4;
      #tadz0001#5 = #tadm0001#8; |PINTA #tadz0001#5;
   |FINSI;
|FPRC;

|PROCESO TamPantalla; |TIPO 80;
   FSalida = 2485;
|FPRC;

|PROCESO T66RelNifs; |TIPO 66;
   Cmp = Campo + 1;
   |DBASS aAlfa; |QBF aAlfa;
   |SI nSwCod = 1;
      aAlfa1 = aAlfa + "integral/def/dsam0103.mas";
   |SINO;
      aAlfa1 = aAlfa + "basica/def/agim0003.mas";
   |FINSI;
   |CARGA_DEF aAlfa1, Referen@;
   |SI FSalida = 0;
      |SI nSwCod = 1;
         aAlfa1 = aAlfa + "integral/fich/";
      |SINO;
         aAlfa1 = aAlfa + "basica/fich/";
      |FINSI;
      |PATH_DAT #Referen@#aAlfa1#;
      |ABRE #Referen@;
      #Referen@#0 = #Fichero@#Campo#;
      |LEE 000#Referen@.];
      |CONSULTA_CLAVE #1#Referen@;
      |SI FSalida = 0;
         #Fichero@#Campo# = #Referen@#0; |PINTA #Fichero@#Campo#;
         #Fichero@#Cmp# = #Referen@#1; |PINTA #Fichero@#Cmp#;
      |FINSI;
      |ERROR;

      |CIERRA #Referen@; |DESCARGA_DEF #Referen@;
   |FINSI;
|FPRC;

|PROCESO T00RelNifs;
   Cmp = Campo + 1;
   |DBASS aAlfa; |QBF aAlfa;
   |SI nSwCod = 1;
      aAlfa1 = aAlfa + "integral/def/dsam0103.mas";
   |SINO;
      aAlfa1 = aAlfa + "basica/def/agim0003.mas";
   |FINSI;
   |CARGA_DEF aAlfa1, Referen@;
   |SI FSalida = 0;
      |SI nSwCod = 1;
         aAlfa1 = aAlfa + "integral/fich/";
      |SINO;
         aAlfa1 = aAlfa + "basica/fich/";
      |FINSI;
      |PATH_DAT #Referen@#aAlfa1#;
      |ABRE #Referen@;
      #Referen@#0 = #Fichero@#Campo#;
      |LEE 000#Referen@.=;
      |SI FSalida = 0;
         #Fichero@#Cmp# = #Referen@#1; |PINTA #Fichero@#Cmp#;
      |SINO;
         |MENAV "    Nif inexistente"; |ERROR;
      |FINSI;
      |CIERRA #Referen@; |DESCARGA_DEF #Referen@;
   |FINSI;
|FPRC;

|PROCESO VentanaCertif;
   |VENTANA 0903, 1781, -1, 17, aTitulo; nVent0 = FSalida;
   aVent1 = -1;
   aVent2 = -1;
   aVent3 = 0;
   aVent4 = nVent0;
   aVent5 = "MODAL";
   |ESPECIFICA "ESTADO_VENTANA", aVent;

   |PINPA #0#tadw0001; nPant1 = FSalida;
   |PINDA #0#tadw0001;

   |ENDATOS #1#tadw0001;
   |SI FSalida = 0;
      |SI nSw = 1;
         #tadm0001#0 = nCliente;
         #tadm0001#1 = #tadw0001#0;
      |FINSI;
      #tadm0001#2 = #tadw0001#1;
      #tadm0001#3 = #tadw0001#2;
      #tadm0001#4 = #tadw0001#3;
      #tadm0001#5 = #tadw0001#4;
      #tadm0001#6 = #tadw0001#5;
      #tadm0001#7 = #tadw0001#6;
      #tadm0001#8 = #tadw0001#7;
      |SI nSw = 1; |GRABA 020#tadm0001.n; |FINSI;
      |SI nSw = 2; |GRABA 020#tadm0001.a; |FINSI;
   |FINSI;

   aVent4 = nVent0;
   aVent5 = "MODELESS";
   |ESPECIFICA "ESTADO_VENTANA", aVent;
   |FINVENTANA nVent0;

   |REFRESCACONTROL nGrid0;
|FPRC;

|PROCESO NuevoCert;
   aTitulo = "Nuevo certificado";
   |DDEFECTO #tadw0001; nSw = 1;

   |C_M #tadw0001#0, 1, "S";

   |HAZ VentanaCertif;
|FPRC;

|PROCESO ModifCert;
   aTitulo = "Modificar certificado";
   #tadw0001#0 = #tadm0001#1;
   #tadw0001#1 = #tadm0001#2;
   #tadw0001#2 = #tadm0001#3;
   #tadw0001#3 = #tadm0001#4;
   #tadw0001#4 = #tadm0001#5;
   #tadw0001#5 = #tadm0001#6;
   #tadw0001#6 = #tadm0001#7;
   #tadw0001#7 = #tadm0001#8;
   nSw = 2;

   |C_M #tadw0001#0, 1, "N";

   |HAZ VentanaCertif;
|FPRC;

|PROCESO BorraCert;
   aAlfa = "    N" + &191 + " Esta seguro de eliminar el certificado seleccionado ? ";
   |CONFI aAlfa;
   |SI FSalida = 0;
      |BORRA 020#tadm0001.a;

      #tadm0001#0 = nCliente;
      #tadm0001#1 = "            ";
      |LEE 000#tadm0001.];
      |SI FSalida ! 0; |Y #tadm0001#0 = nCliente;
         |ESTADO_CONTROL nBotonModif, "DISABLE";
         |ESTADO_CONTROL nBotonBorra, "DISABLE";

         |DDEFECTO #tadz0001;
      |FINSI;

      |REFRESCACONTROL nGrid0;
   |FINSI;
|FPRC;

|PROCESO ConsCert;
   |HAZ PonVentEspera;

   eaPathExplorador = ""; eaForzExplorador = "N";
   |HAZ Explorador;

   aAlfa = "cmd " + &34 + " /c START /WAIT " + eaPathExplorador + " https://aeat.es/tlceccem.htm " + &34;
   |RSYSTEM aAlfa;
   |HAZ QuitaVentEspera;
|FPRC;

|PROCESO Infm01;
   #tadm0001#0 = nCliente;
   #tadm0001#1 = "            ";
|FPRC;

|PROCESO Supm01;
   #tadm0001#0 = nCliente;
   #tadm0001#1 = "zzzzzzzzzzzz";
|FPRC;

|PROCESO CambiaExpl;
   eaPathExplorador = ""; eaForzExplorador = "S";
   |HAZ Explorador;
|FPRC;

|PROGRAMA;
   aAlfa = PARAMETRO;

   aAlfa = aAlfa - "@NUEVO";
   |SI FSalida ! 0;
      nCalc = FSalida + 93;
      aAlfa1 = aAlfa % nCalc; aNuevo = aAlfa1;
      nCalc = ((FSalida / 100) ! 0) + 99;
      aAlfa = aAlfa % nCalc;
   |SINO;
      aNuevo = "";
   |FINSI;

   aAlfa = aAlfa - "|";
   |SI FSalida ! 0;
      nCalc = FSalida + 98;
      aAlfa1 = aAlfa % nCalc; aContrat = aAlfa1;
      nCalc = ((FSalida / 100) ! 0) + 99;
      aAlfa = aAlfa % nCalc;
   |FINSI;

   aAlfa = aAlfa - ",";
   nCalc = ((FSalida / 100) ! 0) + 99;
   aAlfa1 = aAlfa % nCalc; nCliente = aAlfa1;
   nCalc = FSalida + 98; aAlfa = aAlfa % nCalc;

   aAlfa = aAlfa - ",";
   nCalc = ((FSalida / 100) ! 0) + 99;
   aAlfa1 = aAlfa % nCalc; aNIF = aAlfa1;
   nCalc = FSalida + 98; aNombre = aAlfa % nCalc;

   aAlfa = "Certificados de contratistas y subcontratistas      [";
   aAlfa = aAlfa + (("00000" + nCliente) % -105) + " / " + aNIF + "] " + aNombre;
   |VENTANA 0501, 2485, -1, 16, aAlfa;
   nVent = FSalida;

   |SI aNuevo ! "";
      aAlfa = aNuevo - ",";
      nCalc = FSalida + 98;
      aNombreNuevo = aAlfa % nCalc;
      nCalc = ((FSalida / 100) ! 0) + 99;
      aNifNuevo = aAlfa % nCalc;
   |FINSI;

   |DBASS aBase;   |QBT aBase;
   aAlfa = aBase + "integral/def/dsam0000.mas";
   |XABRE "E", aAlfa, nCanal;
   |SI FSalida ] 0;
      nSwCod = 1;
   |SINO;
      nSwCod = 2;
   |FINSI;

   |ABRE #tadm0001;
   #tadm0001#0 = nCliente;
   #tadm0001#1 = aContrat;
   |LEE 000#tadm0001.];

   |PINPA #0#tadz0001; nPant0 = FSalida;
   |PINDA #0#tadz0001;

   nPos1 = 2403; nPos2 = 2423;
   |CONTROL_SIMPLE nPant0, "BOTON,{{197}}Consulta certificado", nPos1, nPos2, ConsCert;
   nBotonConsC = FSalida;

   nPos1 = 2425; nPos2 = 2442;
   |CONTROL_SIMPLE nPant0, "BOTON,{{197}}Cambiar explorador", nPos1, nPos2, CambiaExpl;
   nBotonCExpl = FSalida;

   nPos1 = 2449; nPos2 = 2459;
   |CONTROL_SIMPLE nPant0, "BOTON,{{137}}Nuevo", nPos1, nPos2, NuevoCert;
   nBotonNuevo = FSalida;

   nPos1 = 2461; nPos2 = 2471;
   |CONTROL_SIMPLE nPant0, "BOTON,{{135}}Modificar", nPos1, nPos2, ModifCert;
   nBotonModif = FSalida; |ESTADO_CONTROL nBotonModif, "DISABLE";

   nPos1 = 2473; nPos2 = 2483;
   |CONTROL_SIMPLE nPant0, "BOTON,{{199}}Borrar", nPos1, nPos2, BorraCert;
   nBotonBorra = FSalida; |ESTADO_CONTROL nBotonBorra, "DISABLE";

   nRango = 4 + 8 + 16 + 32;
   nPos1 = 0603; nPos2 = 1882;
   |LINEAL_SIMPLE #1#tadm0001, nRango, nPos1, nPos2, Infm01, Supm01, EvtoGrid0;
   nGrid0 = FSalida;
   |PULSATECLA;

   |SI aNuevo ! "";
      |PTEC 802; |PAUSA;
      aTitulo = "Nuevo certificado";
      |DDEFECTO #tadw0001; nSw = 1;
      #tadw0001#0 = aNifNuevo;
      #tadw0001#1 = aNombreNuevo;
      |C_M #tadw0001#0, 1, "S";
      |HAZ VentanaCertif;
   |FINSI;

   aEsc1  = &255 + "806";
   aEsc2  = &255 + "807";
   aRetu  = &255 + "802";
   |PARA; |SI;  |HACIENDO;
      FSalida = "::{{001}}Salir," + nGrid0;
      |LEETECLA aTecla;
      |SI aTecla = aEsc1; |SAL; |FINSI;
      |SI aTecla = aEsc2; |SAL; |FINSI;
      |SI aTecla = aRetu; |SAL; |FINSI;
   |SIGUE;

   |CIERRA #tadm0001;
   |DESTRUYECONTROL nGrid0;

   |FIN_CONTROL_WINDOWS nBotonNuevo;
   |FIN_CONTROL_WINDOWS nBotonModif;
   |FIN_CONTROL_WINDOWS nBotonBorra;
   |FIN_CONTROL_WINDOWS nBotonCExpl;
   |FIN_CONTROL_WINDOWS nBotonConsC;

   |FINVENTANA nVent;
|FPRO;
