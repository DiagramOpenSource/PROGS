|FICHEROS;
     dsinfcoc;
     dsinfcol;

     dsinfcoy;

     agifigen;
     agiw0067;

     canempr@;
|FIN;

|VARIABLES;
   nSwExiste   = 0;
   aFecha      = "";
   fFecha      = @;
   aAlfaX      = "";
   aAlfa1      = "";
   aAlfa2      = "";
   aAlfa3      = "";
   aAlfa4      = "";
   aAlfa5      = "";
   nNume1      = 0;
   nPara1      = 0;
   aParam      = "";
   aAntes      = "";
   nCampo      = 0;
   aEjecuta    = "";
   aEjecutable = "";
   aFichero    = "";
   nDEmpresa   = 0;
   nHEmpresa   = 0;
   nEmpresa    = 0;
   nGTempo     = 0;
   nSPin1      = 0;
   enSwSelEmp  = 0;
   aDef330     = "";
   nPerConta   = 0;
   aDirConta   = "";
   &eaPathContagen = "";
   &eaParam2   = "";
   &dirfich0   = "";
   &eaUnidadBasico = "";

   aAlfa           = "";
   &enHayInforme   = 0;
   aOrigen         = "";
   aPathBase       = "";
   aGrupo          = "";
   aUsuario        = "";
   aEmpresa        = "";
   nInkey          = 0;
   &efFecLeg = @;
   nInstala  = 0;
   nHandle   = 0;
{1}aContiene = "";
   aDocRemoto   = "";
   aDocServidor = "";
   aPathTemporalM = "";
   aGhostScript = "";
   nSwError     = 0;
   aPdfs        = "";
   aIP          = "";
   aQueSistema  = "";
   &eaOrigen    = "";
   &eaDestino   = "";
   nPausa       = 0;
   aAbre        = "";
   aAux         = "";
   aImpre       = "";
   aExtension   = "";
   aFicFin      = "";
   aErr         = "";
   aLog         = "";
   aPathPrint   = "";
   aNomResImp   = "";
   aHora        = "";
   SwPrim1      = 0;
   SwPrim2      = 0;
   SwPrim3      = 0;
|FIN;

|INCLUYE i_masivo;  || Variables Utilizadas en Procesos Masivos

|PROCESO Tipo7Grupo; |TIPO 7;
  aParam = PARAMETRO; |QBF aParam;

  |SI aParam ! "";
      |C_M #dsinfcoy#0, 1, "N";
      #dsinfcoy#0 = aParam;
      |PINTA #dsinfcoy#0;
  |FINSI;

  fFecha = ~;
  aAlfa1 = fFecha % -104;
  #dsinfcoy#2 = aAlfa1;
  |PINTA #dsinfcoy#2;
|FPRC;

|PROCESO ElGrupo; |TIPO 6;
  nSwExiste = 0;
  |ABRE #dsinfcoc;
  |DDEFECTO #dsinfcoc;
  #dsinfcoc#0 = #dsinfcoy#0;
  |LEE 041#dsinfcoc.=;
  |SI FSalida ! 0;
      nSwExiste = 1;
      |MENAV "0000No Existe Grupo de Informes";
      |SI aParam = "";
          |ERROR;
      |SINO;
          |PTEC 807;
      |FINSI;
  |SINO;
      #dsinfcoy#1 = #dsinfcoc#1;
      |PINTA #dsinfcoy#1;
  |FINSI;
  |CIERRA #dsinfcoc;
  |HAZ LasFechas;
|FPRC;

|PROCESO Fecha;
   |SI aFecha = "29.02"; |O aFecha = "28.02";
       |SI #dsinfcoy#2 = 2008; |O #dsinfcoy#2 = 2012; |O #dsinfcoy#2 = 2016;
           |O #dsinfcoy#2 = 2020; |O #dsinfcoy#2 = 2024; |O #dsinfcoy#2 = 2028;
         aFecha = "29.02";
       |SINO;
         aFecha = "28.02";
       |FINSI;
   |FINSI;
   aFecha = aFecha + "." + #dsinfcoy#2;
|FPRC;

|PROCESO LasFechas;
   aFecha = #dsinfcoc#30; |HAZ Fecha; #dsinfcoy#3 = aFecha;
   aFecha = #dsinfcoc#31; |HAZ Fecha; #dsinfcoy#4 = aFecha;
   |SI #dsinfcoy#3 > #dsinfcoy#4;
       fFecha = #dsinfcoy#4;
       fFecha = fFecha + "0.0000001";
       #dsinfcoy#4 = fFecha;
   |FINSI;
   |PINTA #dsinfcoy#3;
   |PINTA #dsinfcoy#4;
|FPRC;

|| ------------------------------------------------------------------------
|PROCESO TraeteDSPrint;
     |DBASS aPathPrint;
     |QBF aPathPrint;
     aPathPrint = aPathPrint + "modelos/plugins/";

     nSwError = 0;
     eaOrigen  = aPathPrint     + "dsprint.exe";
     eaDestino = eaPathTemporal + "dsprint.exe";
     |HAZ EnviaFicheroConMD5;
     |XABRE "EC", eaDestino, 0;
     |SI FSalida < 0;
         |MENAV "0000 No puedo instalar el dsprint.exe";
         nSwError = 1;
         |ACABA;
     |FINSI;

     |DBASE aPathBase;
     |QBF aPathBase;
     nSwError = 0;
     eaOrigen  = aPathBase      + "pdf/wait.exe";
     eaDestino = eaPathTemporal + "wait.exe";
     |HAZ EnviaFicheroConMD5;
     |XABRE "EC", eaDestino, 0;
     |SI FSalida < 0;
         |MENAV "0000 No puedo instalar el wait.exe";
         nSwError = 1;
     |FINSI;
|FPRC;

|PROCESO ImprimeDocumento;
     || aImpre = "HP LaserJet Professional CP1520 Series PCL 6";
     || |QBT aImpre;

     nSwError = 0;
     |SI SwPrim3 = 0;
         |HAZ TraeteDSPrint;
     |FINSI;
     |SI nSwError ! 0; |ACABA; |FINSI;
     SwPrim3 = 1;

     aAbre = eaPathTemporal + "\dsprint.ini";
     |RFBORRA aAbre;

     eaDestino = eaPathTemporal + aFichero;

     aAbre = eaPathTemporal + "imprime.bat";
     |XABRE "CBW", aAbre, nHandle;

     aAlfa = eaUnidadBasico + &13 + &10;
     |XGRABA nHandle, aAlfa;

     aAlfa = "CD " + eaPathTemporal + &13 + &10;
     |XGRABA nHandle, aAlfa;

     aAlfa = "wait dsprint " + aFichero + &13 + &10;
     |XGRABA nHandle, aAlfa;

     |XCIERRA nHandle;

     |RSYSTEM aAbre;
     |RFBORRA aAbre;

     aExtension = (eaDestino % -104) > "";
     aAlfa1 = aExtension % 101;
     |SI aAlfa1 ! ".";
         aExtension = (eaDestino % -105) > "";
     |FINSI;
     |SI aExtension ! ".DOC";      ||Cuando dsprinto documentos Word, no me crea el "fin.txt"
          ||TODO CCC.
          ||Aqui falta el control del "Fin.txt"

          aFicFin = eaUnidadBasico + "\ds\dspdf\documentos\fin.txt";
          |PARA;  |SI;  |HACIENDO;
               |XABRE "EC", aFicFin, nHandle;
               |SI FSalida ] 0;
                    |SAL;
               |FINSI;
               |SLEEP 1;
               |PULSATECLA;
          |SIGUE;
     |SINO;
          |SLEEP 1;
          |SLEEP 1;
          |SLEEP 1;
          |SLEEP 1;
          |SLEEP 1;
     |FINSI;
     |RFBORRA aFicFin;

     |SLEEP 1;
|FPRC;


|PROCESO PresentaDocumento;

     nSwError = 0;
     aAlfa = eaPathTemporal + aFichero;
     |XABRE "CE", aAlfa, 0;
     |SI FSalida < 0;
         |MENAV "0000El documento no existe";
         |ACABA;
     |FINSI;

     |DBASE aPathBase;
     |QBF aPathBase;

     eaOrigen  = aPathBase      + "plantillas/dsabreextension.exe";
     eaDestino = eaPathTemporal + "dsabreextension.exe";
     |SI SwPrim2 = 0;
         |HAZ EnviaFicheroConMD5;
         SwPrim2 = 1;
     |FINSI;
     |XABRE "EC", eaDestino, 0;
     |SI FSalida < 0;
         nSwError = 1;
         |ACABA;
     |FINSI;

     aEjecuta = "START " + eaPathTemporal + "dsabreextension ";
     aEjecuta = aEjecuta + eaPathTemporal + aFichero;
     aEjecuta = aEjecuta + " " + eaPathTemporal + "fin" + Usuario + ".txt";

     aAlfa    = eaPathTemporal + "fin" + Usuario + ".txt";
     |RFBORRA aAlfa;

     |RSYSTEM aEjecuta;
     aAlfa  = eaPathTemporal + "fin" + Usuario + ".txt";
     |PARA;  |SI;  |HACIENDO;
             |XABRE "EC", aAlfa, nHandle;
             |SI FSalida ] 0;
                 |SAL;
             |FINSI;

             |PARA nPausa = 1; |SI nPausa < 10000; |HACIENDO nPausa = nPausa + 1;
             |SIGUE;
             |PULSATECLA;
     |SIGUE;
     |PULSATECLA;

     aAlfa   = eaPathTemporal + "fin" + Usuario + ".txt";
     |RFBORRA aAlfa;

||     aAlfa   = eaPathTemporal + aFichero;
||     |RFBORRA aAlfa;
|FPRC;

|PROCESO MiraSiImpreDsPDF;
     nSwError = 0;
     nInstala = 0;
     aAlfa = eaUnidadBasico + "\ds\dspdf\bin\puerto.exe";
     |XABRE "EC", aAlfa, nHandle;
     |SI FSalida < 0;  nInstala = 1;  |FINSI;

     |DBASE aPathBase;
     |QBF aPathBase;

     eaOrigen  = aPathBase      + "patrones/dspdf.tgz";
     eaDestino = eaPathTemporal + "dspdf.tgz";
     |HAZ EnviaFicheroConMD5;
     |XABRE "EC", eaDestino, 0;
     |SI FSalida < 0;
         nSwError = 1;
         |ACABA;
     |FINSI;

     |SI nInstala = 1;
         Pos   = -1;
         |RDESCOMPRIME eaDestino;
         aAlfa = eaUnidadBasico + "\ds\";
         |RDETAR eaDestino, aAlfa;
         eaDestino = aAlfa + "dspdf.tar"; |RFBORRA eaDestino;
         aAlfa = eaUnidadBasico + "\ds\dspdf\bin\puerto.exe";
         |XABRE "EC", aAlfa, nHandle;
         |SI FSalida < 0;
             |MENAV "0000 No puedo instalar la impresora en PDF.";
             nSwError = 1;
             |ACABA;
         |FINSI;
         aAlfa  = "cmd " + &34 + "/c START /WAIT "+ eaUnidadBasico + "\DS\DSPDF\BIN\PUERTO.EXE" + &34;
         |RSYSTEM aAlfa;
     |FINSI;
     SwPrim1 = 1;
|FPRC;

|PROCESO UnirInformes;
      aFichero = "infagrupado.pdf";

      aIP = ""; |IP_REMOTO aIP;

      aAlfa = eaPathTemporal;
      |SI aIP ! "";
         |RMKDIR aAlfa;

         aGhostScript = "gs8.51";
         aAlfa2 = eaUnidadBasico + "\DS\DSPDF\gs\*";
         |R_PDIR aAlfa2;
         |PARA; |SI FSalida = 0; |HACIENDO;
            aAlfa4 = eaUnidadBasico + "\DS\DSPDF\gs";
            aAlfa4 = aAlfa2 - aAlfa4; aAlfa4 = aAlfa4 % 0299;
            aAlfa4 = aAlfa4 % 0102;
            |SI aAlfa4 = "gs";
               aAlfa4 = eaUnidadBasico + "\DS\DSPDF\gs";
               aAlfa4 = aAlfa2 - aAlfa4; aAlfa4 = aAlfa4 % 0299;
               aAlfa4 = aAlfa4 % 0401;
               |SI aAlfa4 = ".";
                  aAlfa4 = aAlfa2 + "\BIN\gswin32c.exe";
                  |XABRE "CE", aAlfa4, nHandle;
                  |SI FSalida ] 0;
                     aAlfa4 = eaUnidadBasico + "\DS\DSPDF\gs";
                     aGhostScript = aAlfa2 - aAlfa4;
                     aGhostScript = aGhostScript % 0299;
                  |FINSI;
               |FINSI;
            |FINSI;
            |R_SDIR aAlfa2;
         |SIGUE;

         aAlfa  = eaUnidadBasico + "\DOCUMENTOS\PDF\" + aUsuario + "\";
         aAlfa2 = aAlfa + "une.bat";  |RFBORRA aAlfa2;

         aAlfa2 = aAlfa + "une.bat";
         |XABRE "BCW", aAlfa2, nHandle;

         |SI FSalida ] 0;
            aAlfa2 = eaUnidadBasico + &13 + &10;          |XGRABA nHandle, aAlfa2;
            aAlfa2 = "cd " + aPathTemporalM + &13 + &10;  |XGRABA nHandle, aAlfa2;
            aAlfa2 = "del " + aFichero +  &13 + &10;      |XGRABA nHandle, aAlfa2;
            aAlfa2 = eaUnidadBasico + "\DS\DSPDF\gs\" + aGhostScript + "\bin\gswin32c.exe ";     |XGRABA nHandle, aAlfa2;
            aAlfa2 = "@" + eaUnidadBasico + "\DS\DSPDF\bin\pdfwrite.txt -sOutputFile="; |XGRABA nHandle, aAlfa2;
            aAlfa2 = aFichero + " -q -dBATCH ";           |XGRABA nHandle, aAlfa2;

            aAlfa  = eaPathTemporal;
            aAlfa2 = aAlfa + "*.pdf";
            |R_PDIR aAlfa2;
            |PARA; |SI FSalida = 0; |HACIENDO;
               aAlfa1 = (aAlfa2 - aAlfa) + " "; |XGRABA nHandle, aAlfa1;
               |R_SDIR aAlfa2;
               |QBF aAlfa1;
               |SI aAlfa1 ! aFichero;
                  |SI aPdfs ! ""; |Y aAlfa1 ! ""; aPdfs = aPdfs + ", "; |FINSI;
                  aPdfs = aPdfs + aAlfa1; |QBF aPdfs;
               |FINSI;
            |SIGUE;
            aAlfa2 = &13 + &10; |XGRABA nHandle, aAlfa2;

            aAlfa  = eaPathTemporal;
            aAlfa2 = aAlfa + "0*.pdf";
            |R_PDIR aAlfa2;
            |PARA; |SI FSalida = 0; |HACIENDO;
               aAlfa1 = "DEL " + (aAlfa2 - aAlfa) + &13 + &10;  |XGRABA nHandle, aAlfa1;
               |R_SDIR aAlfa2;
            |SIGUE;

            aAlfa  = eaPathTemporal;
            aAlfa2 = aAlfa + "1*.pdf";
            |R_PDIR aAlfa2;
            |PARA; |SI FSalida = 0; |HACIENDO;
               aAlfa1 = "DEL " + (aAlfa2 - aAlfa) + &13 + &10;  |XGRABA nHandle, aAlfa1;
               |R_SDIR aAlfa2;
            |SIGUE;

            |XCIERRA nHandle;

            aAlfa2 = aAlfa + "une.bat";
            |RSYSTEM aAlfa2;
         |FINSI;
      |SINO;
         |MKDIR aAlfa;

         aGhostScript = "gs8.51";
         aAlfa2 = eaUnidadBasico + "\DS\DSPDF\gs\*";
         |R_PDIR aAlfa2;
         |PARA; |SI FSalida = 0; |HACIENDO;
            aAlfa4 = eaUnidadBasico + "\DS\DSPDF\gs";
            aAlfa4 = aAlfa2 - aAlfa4; aAlfa4 = aAlfa4 % 0299;
            aAlfa4 = aAlfa4 % 0102;
            |SI aAlfa4 = "gs";
               aAlfa4 = eaUnidadBasico + "\DS\DSPDF\gs";
               aAlfa4 = aAlfa2 - aAlfa4; aAlfa4 = aAlfa4 % 0299;
               aAlfa4 = aAlfa4 % 0401;
               |SI aAlfa4 = ".";
                  aAlfa4 = aAlfa2 + "\BIN\gswin32c.exe";
                  |XABRE "CE", aAlfa4, nHandle;
                  |SI FSalida ] 0;
                     aAlfa4 = eaUnidadBasico + "\DS\DSPDF\gs";
                     aGhostScript = aAlfa2 - aAlfa4;
                     aGhostScript = aGhostScript % 0299;
                  |FINSI;
               |FINSI;
            |FINSI;
            |R_SDIR aAlfa2;
         |SIGUE;

         aAlfa  = eaUnidadBasico + "\DOCUMENTOS\PDF\" + aUsuario + "\";
         aAlfa2 = aAlfa + "une.bat";  |RFBORRA aAlfa2;

         aAlfa2 = aAlfa + "une.bat";
         |XABRE "BW", aAlfa2, nHandle;
         |SI FSalida ] 0;
            aAlfa2 = eaUnidadBasico + &13 + &10;         |XGRABA nHandle, aAlfa2;
            aAlfa2 = "cd " + aPathTemporalM + &13 + &10; |XGRABA nHandle, aAlfa2;
            aAlfa2 = "del " + aFichero + &13 + &10; |XGRABA nHandle, aAlfa2;
            aAlfa2 = eaUnidadBasico + "\DS\DSPDF\gs\" + aGhostScript + "\bin\gswin32c.exe ";     |XGRABA nHandle, aAlfa2;
            aAlfa2 = "@" + eaUnidadBasico + "\DS\DSPDF\bin\pdfwrite.txt -sOutputFile="; |XGRABA nHandle, aAlfa2;
            aAlfa2 = aFichero + " -q -dBATCH "; |XGRABA nHandle, aAlfa2;

            aAlfa  = eaPathTemporal;
            aAlfa2 = aAlfa + "*.pdf";
            |_PDIR aAlfa2;
            |PARA; |SI FSalida = 0; |HACIENDO;
               aAlfa1 = (aAlfa2 - aAlfa) + " "; |XGRABA nHandle, aAlfa1;
               |_SDIR aAlfa2;
               |QBF aAlfa1;
               |SI aAlfa1 ! aFichero;
                  |SI aPdfs ! ""; |Y aAlfa1 ! ""; aPdfs = aPdfs + ", "; |FINSI;
                  aPdfs = aPdfs + aAlfa1; |QBF aPdfs;
               |FINSI;
            |SIGUE;
            aAlfa2 = &13 + &10; |XGRABA nHandle, aAlfa2;

            aAlfa  = eaPathTemporal;
            aAlfa2 = aAlfa + "0*.pdf";
            |_PDIR aAlfa2;
            |PARA; |SI FSalida = 0; |HACIENDO;
               aAlfa1 = "DEL " + (aAlfa2 - aAlfa) + &13 + &10;  |XGRABA nHandle, aAlfa1;
               |_SDIR aAlfa2;
            |SIGUE;

            aAlfa  = eaPathTemporal;
            aAlfa2 = aAlfa + "1*.pdf";
            |R_PDIR aAlfa2;
            |PARA; |SI FSalida = 0; |HACIENDO;
               aAlfa1 = "DEL " + (aAlfa2 - aAlfa) + &13 + &10;  |XGRABA nHandle, aAlfa1;
               |R_SDIR aAlfa2;
            |SIGUE;

            |XCIERRA nHandle;

            aAlfa2 = aAlfa + "une.bat";
            |SYSTEM aAlfa2;
      |FINSI;

   |FINSI;
|FPRC;

|PROCESO PreparaInf;
   |SI SwPrim1 = 0;
       aOrigen = eaPathTemporal + "*.*";
   |SINO;
       aOrigen = eaPathTemporal + "*.pdf";
   |FINSI;
   |SI aIP ! "";
       |R_PDIR aOrigen;
       |PARA; |SI FSalida = 0; |HACIENDO;
              |RFBORRA aOrigen;
              |R_SDIR aOrigen;
       |SIGUE;
   |SINO;
       |_PDIR aOrigen;
       |PARA; |SI FSalida = 0; |HACIENDO;
              |FBORRA aOrigen;
              |_SDIR aOrigen;
       |SIGUE;
   |FINSI;
|FPRC;

|| ///////////////////////////////////////////////////////////////////////
|PROCESO PorInforme;
  aParam      = "";

  aEjecutable = #dsinfcol#7; |QBF aEjecutable;
  |SI aEjecutable = "";
      aAlfa1 = #dsinfcol#5; |QBF aAlfa1;
      aAlfa2 = #dsinfcol#3; |QBF aAlfa2;
      aEjecutable = ":"+aAlfa1+"/"+aAlfa2;
  |FINSI;

  aAlfa3   = #dsinfcol#3 % 104;

  |SI #dsinfcol#2 = "gen";
      |SI #agiw0067#4 < 0;  || no hay contabilidad
          |ACABA;
      |FINSI;
      |SI aAlfa3 = "ca8y"; |O aAlfa3 = "cayc";
          aParam = #dsinfcol#3;
      |SINO;
          aParam = " N";
      |FINSI;
      aAlfa1   = #agiw0067#5; |QBF aAlfa1;
      dirfich0 = aAlfa1;
      aAlfa1   = aAlfa1 % -107;
      aAlfa1   = aAlfa1 %  106;
      aEjecutable = aEjecutable + " canempr1 " + aAlfa1 + ";" + aParam;
  |FINSI;

   enEmprM = nEmpresa;
   enEjerM = #dsinfcoy#2;

   efDFecM = #dsinfcoy#3;
   efHFecM = #dsinfcoy#4;
   enPConM = #agiw0067#4;
   eaCDirM = #agiw0067#5; |QBF eaCDirM;
   enSwMvo = 2;
   enNuMvo = #dsinfcol#4;
   enCoMvo = #dsinfcoy#0;
   enOrMvo = #dsinfcol#9;
   efFecLeg = #dsinfcoy#5;

   |PINTA 2103, #dsinfcol#8;
   |PINTA 2213, #agiw0067#1;
   |PINTA 2219, #agiw0067#2;
   |SUB_EJECUTA_NP aEjecutable;
   enCoMvo = 0;
   enOrMvo = 0;
   enSwMvo = 0;
|FPRC;

|DEFBUCLE PorInforme;
   #dsinfcol#2;
   6;
   #dsinfcoy#0, 01, "S";
   #dsinfcoy#0, 99, "S";
   ;
   NULL, PorInforme;
|FIN;

|PROCESO PorEmpresa;
  nEmpresa = #agiw0067#1;

  |HAZ PreparaInf;
  |SI SwPrim1 = 0;
      |HAZ MiraSiImpreDsPDF;
      |SI nSwError ! 0;
          |ERROR10;
          |ACABA;
      |FINSI;
  |FINSI;

  enHayInforme = 0;
  |BUCLE PorInforme;

  |SI enHayInforme ! 0;
      |HAZ UnirInformes;
      |SI #dsinfcoy#6 = "S";
          |HAZ PresentaDocumento;
      |SINO;
          |HAZ ImprimeDocumento;
      |FINSI;
      |SI nSwError ! 0;
          |ERROR10;
      |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE PorEmpresa;
  #agiw0067#1;
  ;
  000001;
  999999;
  ;
  NULL, PorEmpresa;
|FIN;

|| //////////////////////////////////////////////////////////////////////////
|PROCESO LEmpresa;

 |SI #agifigen#94 > "01.01.0000";
     |SI #dsinfcoc#16 = "N"; |ACABA; |FINSI;
 |FINSI;

|| ... Seleccion de Tipo de Empresa
  nSPin1 = #agifigen#87;
  enSwSelEmp = 1;
  |SI #dsinfcoc#17 = "S";
      |SI nSPin1 ] #dsinfcoc#18; |Y nSPin1 [ #dsinfcoc#19;
          enSwSelEmp = 0;
      |FINSI;
  |SINO;
      |SI nSPin1 = #dsinfcoc#20;
          enSwSelEmp = 0;
      |SINO;
          |PARA nCampo = 22;  |SI nCampo [ 29;  |HACIENDO nCampo = nCampo + 1;
                |SI nSPin1 = #dsinfcoc#nCampo#; |Y nSPin1 ! 0;
                    enSwSelEmp = 0;
                    |SAL;
                |FINSI;
          |SIGUE;
      |FINSI;
  |FINSI;
  |SI enSwSelEmp = 1; |ACABA; |FINSI;

|| ------------------------------------------------------------------------
   nPerConta = -1;
   aDirConta = "";
   |SI aDef330 ! "";
       aAlfa1 = #dsinfcoy#2;
       aAlfa1 = aAlfa1 % -102;
       nNume1 = aAlfa1;
       |ABRE #canempr@;
       |SELECT #4#canempr@;
       #canempr@#2  = #agifigen#0;   || Empresa
       #canempr@#26 = nNume1;
       |LEE 041#canempr@.=;
       |SI FSalida = 0;
           aAlfa1 = #canempr@#2; aAlfa1 = ("00000" + aAlfa1) % -105;
           aAlfa2 = #canempr@#3; aAlfa2 = ("0" + aAlfa2) % -101;
           nPerConta = #canempr@#3;
           aDirConta = eaPathContagen + "fich/" + aAlfa1 + aAlfa2 + "/";
       |FINSI;
       |SELECT #1#canempr@;
       |CIERRA #canempr@;
   |FINSI;
   nGTempo = nGTempo + 1;
   #agiw0067#0 = nGTempo;       || Guia
   #agiw0067#1 = #agifigen#0;   || Empresa
   #agiw0067#2 = #agifigen#1;   || Nombre
   #agiw0067#3 = #dsinfcoy#2;   || A¤o
   #agiw0067#4 = nPerConta;
   #agiw0067#5 = aDirConta;

   |GRABA 040#agiw0067.n;
|FPRC;

|DEFBUCLE agifigen;
  #agifigen#1;
  ;
  nDEmpresa;
  nHEmpresa;
  e;
  NULL, LEmpresa;
|FIN;

|| //////////////////////////////////////////////////////////////////////////
|PROCESO CreaAuxiliar;

     |SI eaPathContagen ! "";
         aDef330 = eaPathContagen + "def/canempre";
         |CARGA_DEF aDef330, canempr@;
         |SI FSalida ! 0;
             aDef330 = "";
         |FINSI;
     |FINSI;

     aFichero = "y" + Usuario;
     |NOME_DAT #agiw0067#aFichero#;
     |DELINDEX #agiw0067;

     |ABRE #agiw0067;
     nGTempo = 0;
     |SI #dsinfcoc#2 ! 0;
         nDEmpresa = #dsinfcoc#2;
         nHEmpresa = #dsinfcoc#3;
         |BUCLE agifigen;
     |SINO;
         |PARA nCampo = 4;  |SI nCampo [ 15;  |HACIENDO nCampo = nCampo + 1;
               |SI #dsinfcoc#nCampo# ! 0;
                   nDEmpresa = #dsinfcoc#nCampo#;
                   nHEmpresa = #dsinfcoc#nCampo#;
                   |BUCLE agifigen;
               |FINSI;
         |SIGUE;
     |FINSI;
     |CIERRA #agiw0067;

     |SI aDef330 ! ""; |DESCARGA_DEF #canempr@; |FINSI;
|FPRC;

|PROCESO Tipo3; |TIPO 3;

     |HAZ BusquedaApli;
     |HAZ LeeUnidadBasico;
     |HAZ CreaAuxiliar;

     |C_A #dsinfcol#8, 1, 76;
     |SI nGTempo = 0;
         |MENAV "    Seleccion vacia";
     |SINO;
          aIP = ""; |IP_REMOTO aIP;
          aUsuario = Usuario % 108; |QBF aUsuario;
          aGrupo   = ("00000" + #dsinfcoy#0) % -105;
          aAlfa    = eaUnidadBasico + "/DOCUMENTOS";
          |SI aIP ! "";  |RMKDIR aAlfa; |SINO; |MKDIR aAlfa; |FINSI;
          aAlfa    = aAlfa + "/PDF";
          |SI aIP ! "";  |RMKDIR aAlfa; |SINO; |MKDIR aAlfa; |FINSI;
          aAlfa    = aAlfa + "/" + aUsuario;
          |SI aIP ! "";  |RMKDIR aAlfa; |SINO; |MKDIR aAlfa; |FINSI;
          aPathTemporalM = aAlfa < "";
          eaPathTemporal = aAlfa + "/";

          SwPrim1 = 0;
          SwPrim2 = 0;
          SwPrim3 = 0;
          |BLIN 20;
          |BLIN 21;
          |BLIN 22;
          |CUADRO 2001 2380;
          |ATRI S;
          |PINTA 2203, "Empresa: ";
          |ATRI 0;
          |BUCLE PorEmpresa;

          |SI SwPrim1 ! 0;
              SwPrim1 = 0;
              |HAZ PreparaInf;
          |FINSI;
      |FINSI;
|FPRC;
