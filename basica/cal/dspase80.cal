|FICHEROS;
     dspase00 #0;
     dspase80 #80;
     :/modelos/def/dsmom014 #14;

     refer00@ #900;
     refer01@ #901;
|FIN;

|VARIABLES;
     aDef0           = "";
     aDef1           = "";
     aPathDefFiscal  = "";
     aPathFichFiscal = "";
     aPathAnterior   = "";
     aBase           = "";
     aPathPases      = "";
     aOrigen         = "";
     aDestino        = "";

     aError          = "";
     nError          = 0;
     nModo           = 0;
     nAny            = 0;
     aAlfa           = "";
     aAlfa1          = "";
     aAlfa2          = "";
     nNume1          = 0;
     nPara1          = 0;
     nPlan           = 0;
     nInkey          = 0;
|FIN;

|PROCESO Tipo8_80; |TIPO 8;
     |ABRE #0;
     |LEE 040#0p;
     |SI FSalida ! 0;
         |MENAV "     Pase de Ficheros Generales no Realizado";
         |CIERRA #0;
         |PTEC 807;
         |ACABA;
     |FINSI;
     |CIERRA #0;

     aAlfa2 = "S";
     aPathAnterior   = #0#0;  |QBT aPathAnterior;
     aPathDefFiscal  = aPathAnterior + "agieos/def/";
     aPathFichFiscal = aPathAnterior + "agieos/fich/";

     aDef0 = aPathDefFiscal + "eoscopl1.mas";
     |CARGA_DEF aDef0, refer00@;
     |SI FSalida ! 0;
         aAlfa1 = "    SError al carga def " + aDef0 + ". Continuar ";
         aDef0 = "";
         |CONFI aAlfa1;
         |SI FSalida ! 0;
              |PTEC 807;
              |ACABA;
         |FINSI;
     |FINSI;

     |SI aDef0 ! "";
         aDef1 = aPathDefFiscal + "eosconpl.mas";
         |CARGA_DEF aDef1, refer01@;
         |SI FSalida ! 0;
             aAlfa1 = "     Error al carga def " + aDef1;
             |MENAV aAlfa1;
             aDef1 = "";
             |DESCARGA_DEF #refer00@; aDef0 = "";
             |PTEC 807;
             |ACABA;
         |FINSI;

         |PATH_DAT #900 aPathFichFiscal;
         |PATH_DAT #901 aPathFichFiscal;
         aAlfa2 = "N";
     |FINSI;

     |DBASE aBase;  |QBF aBase;
     aPathPases = aBase + "pases";  |MKDIR aPathPases;
     aPathPases = aPathPases + "/";

     |PARA nPara1 = 1; |SI nPara1 [48; |HACIENDO nPara1 = nPara1 + 1;
           |C_M #80nPara1,1,aAlfa2;
     |SIGUE;
|FPRC;

|PROCESO Tipo9_80; |TIPO 9;
  |SI aDef1 ! "";  |DESCARGA_DEF #refer01@; aDef1 = ""; |FINSI;
  |SI aDef0 ! "";  |DESCARGA_DEF #refer00@; aDef0 = ""; |FINSI;
|FPRC;

|PROCESO CargoFichero;
  aAlfa1 = #80#0;
  aAlfa1 = aAlfa1 % -102;
  nAny   = aAlfa1;
  |SI aDef0 ! "";
      |ABRE #900;
      |DDEFECTO #900;
      |PARA nPara1 = 4; |SI nPara1 ] 1; |HACIENDO nPara1 = nPara1 - 1;
            #900#0 = nPara1;
            #900#1 = nAny;
            |LEE 041#900=;
            |SI FSalida = 0; |SAL; |FINSI;
      |SIGUE;
      |CIERRA #900;

      |ABRE #901;
      |DDEFECTO #901;
      #901#0 = nAny;
      |LEE 041#901=;
      |CIERRA #901;
     || ... Carga Control de IVA en Repercutido
      #80#1 = #900#2;
      #80#2 = #900#3;
      #80#3 = #900#4;
      #80#4 = #900#5;
      #80#5 = #900#6;
      #80#6 = #900#7;
      #80#7 = #900#8;
      #80#8 = #900#13;

      |PARA nPara1 = 1; |SI nPara1 [ 40; |HACIENDO nPara1 = nPara1 + 1;
            nNume1 = nPara1 + 8;
            #80nNume1 = #901nPara1;
      |SIGUE;
  |FINSI;
|FPRC;

|PROCESO Tipo0_80; |TIPO 0;
 nModo = (FEntrada / 100) ! 0;
 |SI nModo = 1;
     |HAZ CargoFichero;
     |PINDA #0#80;
 |FINSI;
|FPRC;


|PROCESO inf14;
 #14#0 = "    ";
 #14#10 = nPlan;
|FPRC;

|PROCESO sup14;
 #14#0 = "zzzz";
 #14#10 = nPlan;
|FPRC;

|PROCESO cuenta; |TIPO 0;
 nInkey = FSalida;

 nPlan = 1990; |SI #80#0 ] 2008; nPlan = 2008; |FINSI;
 nNume1 = Campo + 55;

 |ABRE #14;
 |SI nInkey = 10;
     #14#0 = #80Campo;
     |SI #14#0 = "    ";
         |SI Campo ] 49; |Y Campo [ 60;  #14#0 = "7000"; |FINSI;
         |SI Campo = 61;                 #14#0 = "3000"; |FINSI;
         |SI Campo ] 62; |Y Campo [ 103; #14#0 = "6000"; |FINSI;
     |FINSI;
     |CONSULTA_F_CLAVE #1#14, inf14, sup14;
     #80Campo = #14#0;
     |PINTA #80Campo;
 |FINSI;

 aAlfa1 = #80Campo; |QBF aAlfa1;
 |SI aAlfa1 = "";
     #80nNume1 = ""; |PINTA #80nNume1;
     |CIERRA #14;
     |ACABA;
 |FINSI;

 nError = 0;
 #14#0  = #80Campo;
 #14#10 = nPlan;
 |LEE 001#14=;
 |SI FSalida ! 0;
     aError = "    No existe cuenta en la Planilla";
     nError = 1;
 |SINO;
     |SI Campo ] 49; |Y Campo [ 60;
         |SI #14#2 ! "I";
             aError = "    Error. Esta Cuenta no es de Ingresos";
             nError = 1;
         |FINSI;
     |FINSI;
     |SI Campo = 61;
         |SI #14#2 ! "E";
             aError = "    Error. Esta Cuenta no es de Existencias";
             nError = 1;
         |FINSI;
     |FINSI;
     |SI Campo ] 62; |Y Campo [ 103;
         |SI #14#2 ! "C"; |Y #14#2 ! "G";
             aError = "    Error. Esta Cuenta no es de Compras o Gastos";
             nError = 1;
         |FINSI;
     |FINSI;
 |FINSI;

 |CIERRA #14;
 |SI nError = 1;
     |MENAV aError;
     #14#2 = "";
     |ERROR;
 |FINSI;
 #80nNume1 = #14#2; |PINTA #80nNume1;
|FPRC;
