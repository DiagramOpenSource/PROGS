|FICHEROS;
     socm0001;
     socm0002;

     socw0001,MANTE;
     socw0002,MANTE;

     agifigen;
     agim0019;
     agim0024;

     :/contagen/def/cacuenta;
|FIN;

|VARIABLES;
     aAlfa         = "";
     aQueQuiero    = "";
     aNumCampos    = "";
     aNumPanta     = "";

     aEsc1         = "";
     aEsc2         = "";
     aRetu         = "";
     aTecla        = "";
     aID           = "";

     nCampo        = 0;
     nRango        = 0;
     nGrid1        = 0;
     nGrid2        = 0;
     nBtnAltG1     = 0;
     nBtnModG1     = 0;
     nBtnBajG1     = 0;
     nBtnAltG2     = 0;
     nBtnModG2     = 0;
     nBtnBajG2     = 0;
     nBtnRetG2     = 0;
     nBtnCtbG2     = 0;
     nBtnRetG1     = 0;
     nBtnInfG1     = 0;
     nPanta        = 0;
     nID           = 0;
     nVent         = 0;
     nLabel        = -1;
     nVentTrab     = 0;

     nCampoCambia1 = 0;
     nNumCampos    = 0;
     nNumPanta     = 0;
     nCampos       = 0;
     nModoPag      = 0;

     {-1}aVent     = "";
         aVent1    = 0;
         aVent2    = 0;
         aVent3    = 0;
         aVent4    = 0;
         aVent5    = "";

     &enEmpresa     = 0;
     &enReg         = 0;
     &enModo        = 0;
     &enModoReg     = 0;
     &PRMNT_enError = 0;

     &enSoCtb    = 0;
     &enSoCtb0   = 0;
     &eaSoCtb1   = "";
     &efSoCtb2   = @;
     &enSoCtbE   = 0;

     &enTpRent   = 0;
     &enBaRent   = 0;
     &enCuRent   = 0;
     &enTotalF   = 0;
|FIN;

|INCLUYE i_agicon;

|| RETRIBUCIONES -----------------------------------------------------------

|PROCESO EntraDatosSocw0002;
     |ESTADO_CONTROL nBtnAltG2, "DISABLE";
     |ESTADO_CONTROL nBtnModG2, "DISABLE";
     |ESTADO_CONTROL nBtnBajG2, "DISABLE";
     |ESTADO_CONTROL nBtnRetG2, "DISABLE";
     |ESTADO_CONTROL nBtnCtbG2, "DISABLE";

     |HAZ QuitaBtnSalir;

     |ESTADO_CONTROL nGrid2, "DISABLE";

     |PULSATECLA;

     |DDEFECTO #socw0002;

     |SI nModoPag = 1;
         |C_M #socw0002#0, 1, "S";

         |DDEFECTO #socm0002;
         #socm0002#0 = #socm0001#0;
         #socm0002#1 = #socm0001#1;
     |SINO;
         |C_M #socw0002#0, 1, "N";

         |LEE 101#socm0002.=;
         |SI FSalida ! 0;  |ACABA;  |FINSI;
     |FINSI;

     #socw0002#0 = #socm0002#2;
     #socw0002#1 = #socm0002#3;

     |VENTANA 0501, 0940, -1, 16, "Datos de la retribuci¢n";
     nVentTrab = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentTrab;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802; |PAUSA;

     |PINPA #0#socw0002;
     |PINDA #0#socw0002;
     |ENDATOS #1#socw0002;
     |SI FSalida = 0;
         #socm0002#2 = #socw0002#0;
         #socm0002#3 = #socw0002#1;

         |SI nModoPag = 1;
             |GRABA 020#socm0002.n;
         |SINO;
             |GRABA 020#socm0002.a;
         |FINSI;
     |FINSI;
     |LIBERA #socm0002;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentTrab;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;
     |FINVENTANA nVentTrab;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |SI enModo ! 4;
         |ESTADO_CONTROL nBtnAltG2, "ENABLE";
     |FINSI;

     |HAZ PonBtnSalir;

     |ESTADO_CONTROL nGrid2, "ENABLE";
     |REFRESCACONTROL nGrid2;

     FSalida = 1;  |HAZ EventoGrid2;

     |FOCOTECLADO nGrid2;
|FPRC;

|PROCESO AltG2;
     nModoPag = 1;
     |HAZ EntraDatosSocw0002;
|FPRC;

|PROCESO ModG2;
     |LEE 000#socm0002.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI #socm0002#4 = "S";
         |CONFI "0000 El registro est  contabilizado, quiere dar de baja el asiento y modificar el registro";
         |SI FSalida ! 0;
             |FOCOTECLADO nGrid2;
             |ACABA;
         |FINSI;

         |HAZ BorraAsientoM2;
         |SI PRMNT_enError < 0;
             aAlfa = " el Ejercicio Contable est  bloqueado.";
             |SI PRMNT_enError = -4;
                 aAlfa = " el Ejercicio Contable est  Cerrado";
             |FINSI;
             aAlfa = "0000No se puede modificar el registro," + aAlfa;
             |MENAV aAlfa;
             |FOCOTECLADO nGrid2;
             |ACABA;
         |FINSI;

         |LEE 101#socm0002.=;
         #socm0002#4 = "N";
         #socm0002#5 = 0;
         #socm0002#6 = "01.01.0000";
         #socm0002#7 = 0;
         #socm0002#8 = 0;

         |GRABA 020#socm0002.a;
         |LIBERA #socm0002;
     |FINSI;

     nModoPag = 2;
     |HAZ EntraDatosSocw0002;
|FPRC;

|PROCESO BajG2;
     |LEE 000#socm0002.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI #socm0002#4 = "S";
         |CONFI "0000 El registro est  contabilizado, quiere dar de baja el asiento y borrar el registro";
         |SI FSalida ! 0;
             |FOCOTECLADO nGrid2;
             |ACABA;
         |FINSI;

         |HAZ BorraAsientoM2;
         |SI PRMNT_enError < 0;
             aAlfa = " el Ejercicio Contable est  bloqueado.";
             |SI PRMNT_enError = -4;
                 aAlfa = " el Ejercicio Contable est  Cerrado";
             |FINSI;
             aAlfa = "0000No se puede dar de baja el registro," + aAlfa;
             |MENAV aAlfa;
             |FOCOTECLADO nGrid2;
             |ACABA;
         |FINSI;
     |SINO;
         |CONFI "0000 Confirma la baja del registro seleccionado";
         |SI FSalida ! 0;
             |FOCOTECLADO nGrid2;
             |ACABA;
         |FINSI;
     |FINSI;

     |LEE 101#socm0002.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |BORRA 020#socm0002.a;
     |LIBERA #socm0002;

     |ESTADO_CONTROL nBtnModG2,  "DISABLE";
     |ESTADO_CONTROL nBtnBajG2,  "DISABLE";
     |ESTADO_CONTROL nBtnRetG2,  "DISABLE";

     |REFRESCACONTROL nGrid2;
     |FOCOTECLADO nGrid2;
|FPRC;

|PROCESO RetCarta;
     |ABRE #agifigen;
     #agifigen#0 = #socm0001#0;
     |LEE 000#agifigen.=;
     |SI FSalida ! 0;  |DDEFECTO #agifigen;  |FINSI;
     |CIERRA #agifigen;

     |LEE 000#socm0002.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |HAZ LeeAsientoM2;

     Impresora = "CrystalReport";
     |IMPRE -1;
     |SI FSalida ! 0;
         |MENAV "    Error en Impresora Crystal Report";
         |ACABA;
     |FINSI;

     |INFOR "socm0002";
     |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

     |VENTANA 0501, 0540, -1, 16, "Procesando informe";
     nVentTrab = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentTrab;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802; |PAUSA;

     |PRINT;

     |FININF;
     |FINIMP;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentTrab;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;
     |FINVENTANA nVentTrab;

     |ESTADO_CONTROL nBtnModG2,  "DISABLE";
     |ESTADO_CONTROL nBtnBajG2,  "DISABLE";
     |ESTADO_CONTROL nBtnRetG2,  "DISABLE";
     |ESTADO_CONTROL nBtnCtbG2,  "DISABLE";

     |REFRESCACONTROL nGrid2;
     |FOCOTECLADO nGrid2;
|FPRC;

|PROCESO ConPago;
     |LEE 000#socm0002.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |VENTANA 0501, 1860, -1, 16, "Procesando contabilizacion";
     nVentTrab = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentTrab;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802; |PAUSA;

     enSoCtb  = 1;
     enSoCtb0 = #socm0002#0;
     eaSoCtb1 = #socm0002#1;
     efSoCtb2 = #socm0002#2;
     |HAZ ContaRetPre;
     |SI enSoCtbE = 0;
         |MENAV "0000Retribucion Contabilizada";
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentTrab;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;
     |FINVENTANA nVentTrab;

     |ESTADO_CONTROL nBtnModG2,  "DISABLE";
     |ESTADO_CONTROL nBtnBajG2,  "DISABLE";
     |ESTADO_CONTROL nBtnRetG2,  "DISABLE";
     |ESTADO_CONTROL nBtnCtbG2,  "DISABLE";

     |REFRESCACONTROL nGrid2;
     |FOCOTECLADO nGrid2;
|FPRC;

|PROCESO EventoGrid2;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#socm0002.=;

         |SI enModo = 4;
             |ESTADO_CONTROL nBtnModG2, "DISABLE";
             |ESTADO_CONTROL nBtnBajG2, "DISABLE";
         |SINO;
             |ESTADO_CONTROL nBtnModG2, "ENABLE";
             |ESTADO_CONTROL nBtnBajG2, "ENABLE";
         |FINSI;
         |SI #socm0002#4 = "S";
             |ESTADO_CONTROL nBtnRetG2, "ENABLE";
             |ESTADO_CONTROL nBtnCtbG2, "DISABLE";
         |SINO;
             |ESTADO_CONTROL nBtnRetG2, "DISABLE";
             |ESTADO_CONTROL nBtnCtbG2, "ENABLE";
         |FINSI;

         |PULSATECLA;
     |FINSI;

     |SI FSalida = 4;
         |SI enModo = 4;  |ACABA;  |FINSI;

         |HAZ ModG2;
     |FINSI;
|FPRC;

|PROCESO BajaGrid2;
     #socm0002#0 = #socm0001#0;
     #socm0002#1 = #socm0001#1;
     #socm0002#2 = "31.12.2999";
|FPRC;

|PROCESO AltaGrid2;
     #socm0002#0 = #socm0001#0;
     #socm0002#1 = #socm0001#1;
     #socm0002#2 = "00.00.0000";
|FPRC;

|PROCESO BtnsGrid2;
     |CONTROL_SIMPLE 0, "BOTON,{{193}}Alta ", 2539, 2549, AltG2;
     nBtnAltG2 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Modificar ", 2551, 2561, ModG2;
     nBtnModG2 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{199}}Borrar", 2563, 2573, BajG2;
     nBtnBajG2 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{205}}Justificante Pago", 2503, 2519, RetCarta;
     nBtnRetG2 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Contabilizar Pago", 2521, 2537, ConPago;
     nBtnCtbG2 = FSalida;

     |ESTADO_CONTROL nBtnModG2,  "DISABLE";
     |ESTADO_CONTROL nBtnBajG2,  "DISABLE";
     |ESTADO_CONTROL nBtnRetG2,  "DISABLE";
     |ESTADO_CONTROL nBtnCtbG2,  "DISABLE";

     |SI enModo = 4;
         |ESTADO_CONTROL nBtnAltG2, "DISABLE";
     |FINSI;
|FPRC;

|PROCESO Retribuciones;
     aAlfa = "Retribuciones de " + #socm0001#2;  |QBF aAlfa;

     |VENTANA 0501, 2874, -1, 16, aAlfa;
     nVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802; |PAUSA;

     |HAZ BtnsGrid2;

     nRango = 4 + 8 + 16 + 32 + 128;

     |LINEAL_SIMPLE #1#socm0002, nRango, 0602, 2473, BajaGrid2, AltaGrid2, EventoGrid2;
     nGrid2 = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::{{001}}Salir," + nGrid2;
          |LEETECLA aTecla;
          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
          |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;
     |FINVENTANA nVent;

     |FOCOTECLADO nGrid1;
|FPRC;

|| -------------------------------------------------------------------------

|PROCESO PintaLabel;
     |SI nLabel ! -1;
         |FIN_CONTROL_WINDOWS nLabel;
         nLabel = -1;
     |FINSI;

     |SI #socw0001#15 = "00.00.0000";
         |CONTROL_SIMPLE nPanta, "LABEL,{{13}}INDEFINIDO", 3538, 3552, NULL;
         nLabel = FSalida;
     |FINSI;
|FPRC;

|PROCESO PintaDatosPantalla;
     |DDEFECTO #socw0001;
     |PARA nCampo = 0;  |SI nCampo [ nNumCampos;  |HACIENDO nCampo = nCampo + 1;
           #socw0001#nCampo# = #socm0001#nCampo#;

           |C_V #socw0001#nCampo#, 0, aAlfa;
           |SI aAlfa = "S";  |PINTA #socw0001#nCampo#;  |FINSI;

           |C_M #socw0001#nCampo#, 1, "N";
     |SIGUE;

     |HAZ PintaLabel;
|FPRC;

|PROCESO CargaFantaSocw0001;
     |C_M #socw0001#17, 1, "S";
     |C_M #socw0001#18, 1, "S";

     |PARA nCampo = 0;  |SI nCampo [ nNumCampos;  |HACIENDO nCampo = nCampo + 1;
           #socw0001#nCampo# = #socm0001#nCampo#;

           |C_V #socw0001#nCampo#, 0, aAlfa;
           |SI aAlfa = "S";  |PINTA #socw0001#nCampo#;  |FINSI;
     |SIGUE;

     |HAZ PintaLabel;
|FPRC;

|PROCESO DescargaFantaSocw0001;
     |PARA nCampo = 0;  |SI nCampo [ nNumCampos;  |HACIENDO nCampo = nCampo + 1;
           #socm0001#nCampo# = #socw0001#nCampo#;
     |SIGUE;

     |HAZ PintaDatosPantalla;
|FPRC;

|PROCESO agim0024_socm1;
     |SI enModoReg = 3;
         #agim0024#19 = "N";
     |SINO;
         #agim0024#20 = #socm0001#17;
     |FINSI;

     |GRABA 020#agim0024.a;
     |LIBERA #agim0024;

     |ERROR10;
|FPRC;

|DEFBUCLE agim0024_socm1;
     #agim0024#1;
     3;
     #socm0001#0, enReg, 01, #socm0001#1;
     #socm0001#0, enReg, 99, #socm0001#1;
     be;
     NULL, agim0024_socm1;
|FIN;

|PROCESO AltG1;
     |SI enReg = 0;
         aAlfa = "0000Los administradores se recogen de la ficha de sociedades ";
         aAlfa = aAlfa + "y no est  creada en esta empresa.";
         |MENAV aAlfa;

         |ACABA;
     |FINSI;

     |ESTADO_CONTROL nBtnAltG1, "DISABLE";
     |ESTADO_CONTROL nBtnModG1, "DISABLE";
     |ESTADO_CONTROL nBtnBajG1, "DISABLE";
     |ESTADO_CONTROL nBtnRetG1, "DISABLE";
     |ESTADO_CONTROL nBtnInfG1, "DISABLE";

     |HAZ QuitaBtnSalir;

     |ESTADO_CONTROL nGrid1, "DISABLE";

     |PULSATECLA;

     |DDEFECTO #socw0001;
     |DDEFECTO #socm0001;

     #socw0001#0 = enEmpresa;
     #socm0001#0 = #socw0001#0;

     enModoReg = 1;
     |HAZ CargaFantaSocw0001;

     |C_M #socw0001#1, 1, "S";
     |ENDATOS #1#socw0001;
     |SI FSalida = 0;
         |HAZ DescargaFantaSocw0001;

         |GRABA 020#socm0001.n;

         |BUCLE agim0024_socm1;
     |FINSI;
     |LIBERA #socm0001;

     |SI enModo ! 4;
         |ESTADO_CONTROL nBtnAltG1, "ENABLE";
     |FINSI;

     |HAZ PonBtnSalir;

     |ESTADO_CONTROL nGrid1, "ENABLE";
     |REFRESCACONTROL nGrid1;

     FSalida = 1;  |HAZ EventoGrid1;

     |FOCOTECLADO nGrid1;
|FPRC;

|PROCESO ModG1;
     |LEE 101#socm0001.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |ESTADO_CONTROL nBtnAltG1,  "DISABLE";
     |ESTADO_CONTROL nBtnModG1,  "DISABLE";
     |ESTADO_CONTROL nBtnBajG1,  "DISABLE";
     |ESTADO_CONTROL nBtnRetG1,  "DISABLE";
     |ESTADO_CONTROL nBtnInfG1,  "DISABLE";

     |HAZ QuitaBtnSalir;

     |ESTADO_CONTROL nGrid1, "DISABLE";

     enModoReg = 2;
     |C_M #socw0001#1, 1, "N";

     |HAZ CargaFantaSocw0001;
     |ENDATOS #2#socw0001;
     |SI FSalida = 0;
         |HAZ DescargaFantaSocw0001;

         |GRABA 020#socm0001.a;

         |BUCLE agim0024_socm1;
     |FINSI;
     |LIBERA #socm0001;

     |SI enModo ! 4;
         |ESTADO_CONTROL nBtnAltG1, "ENABLE";
         |ESTADO_CONTROL nBtnModG1, "ENABLE";
         |ESTADO_CONTROL nBtnBajG1, "ENABLE";
         |ESTADO_CONTROL nBtnRetG1, "ENABLE";
         |ESTADO_CONTROL nBtnInfG1, "ENABLE";
     |FINSI;

     |HAZ PonBtnSalir;

     |ESTADO_CONTROL nGrid1, "ENABLE";
     |REFRESCACONTROL nGrid1;
     |FOCOTECLADO nGrid1;
|FPRC;

|PROCESO socm0002B;
     |SI #socm0002#4 = "S";
         |HAZ BorraAsientoM2;
         |SI PRMNT_enError < 0;
             aAlfa = " hay asientos en Periodo Contable bloqueado.";
             |SI PRMNT_enError = -4;
                 aAlfa = " hay asientos en Periodo Contable cerrado";
             |FINSI;
             aAlfa = "0000No se puede dar de baja el Administrador, " + aAlfa;
             |MENAV aAlfa;
             |ERROR10;
             |ACABA;
         |FINSI;
     |FINSI;

     |BORRA 020#socm0002.a;
     |LIBERA #socm0002;
|FPRC;

|DEFBUCLE socm0002B;
     #socm0002#2;
     ;
     #socm0001#0, #socm0001#1, "01.01.0000";
     #socm0001#0, #socm0001#1, "31.12.2999";
     ;
     NULL, socm0002B;
|FIN;

|PROCESO BajG1;
     |CONFI "0000 Confirma la baja del registro seleccionado, borrar  los asientos de las retribuciones contabilizadas";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |LEE 101#socm0001.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |BUCLE socm0002B;
     |SI PRMNT_enError = 0;
         |BORRA 020#socm0001.a;
         |LIBERA #socm0001;

         enModoReg = 3;
         |BUCLE agim0024_socm1;
     |FINSI;

     |ESTADO_CONTROL nBtnModG1,  "DISABLE";
     |ESTADO_CONTROL nBtnBajG1,  "DISABLE";
     |ESTADO_CONTROL nBtnRetG1,  "DISABLE";
     |ESTADO_CONTROL nBtnInfG1,  "DISABLE";

     |DDEFECTO #socm0001;
     |HAZ PintaDatosPantalla;

     |REFRESCACONTROL nGrid1;
     |FOCOTECLADO nGrid1;
|FPRC;

|PROCESO EventoGrid1;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#socm0001.=;

         |SI enModo = 4;
             |ESTADO_CONTROL nBtnModG1, "DISABLE";
             |ESTADO_CONTROL nBtnBajG1, "DISABLE";
             |ESTADO_CONTROL nBtnRetG1, "DISABLE";
             |ESTADO_CONTROL nBtnInfG1, "DISABLE";
         |SINO;
             |ESTADO_CONTROL nBtnModG1, "ENABLE";
             |ESTADO_CONTROL nBtnBajG1, "ENABLE";
             |ESTADO_CONTROL nBtnRetG1, "ENABLE";
             |ESTADO_CONTROL nBtnInfG1, "ENABLE";
         |FINSI;

         |PULSATECLA;

         enModoReg = 4;
         |HAZ PintaDatosPantalla;
     |FINSI;

     |SI FSalida = 4;
         |SI enModo = 4;  |ACABA;  |FINSI;

         |HAZ ModG1;
     |FINSI;
|FPRC;

|PROCESO Informe;
     aAlfa = "socz0001;" + #socm0001#0;
     |SUB_EJECUTA aAlfa;

     |FOCOTECLADO nGrid1;
|FPRC;

|PROCESO BtnsGrid1;
     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}Alta ", 1764, 1774, AltG1;
     nBtnAltG1 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Modificar ", 1776, 1786, ModG1;
     nBtnModG1 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{199}}Borrar", 1788, 1798, BajG1;
     nBtnBajG1 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Retribuciones", 1748, 1762, Retribuciones;
     nBtnRetG1 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{205}}Informe", 1732, 1746, Informe;
     nBtnInfG1 = FSalida;

     |ESTADO_CONTROL nBtnModG1,  "DISABLE";
     |ESTADO_CONTROL nBtnBajG1,  "DISABLE";
     |ESTADO_CONTROL nBtnRetG1,  "DISABLE";
     |ESTADO_CONTROL nBtnInfG1,  "DISABLE";

     |SI enModo = 4;
         |ESTADO_CONTROL nBtnAltG1, "DISABLE";
     |FINSI;
|FPRC;

|PROCESO BajaGrid1;
     #socm0001#0 = enEmpresa;
     #socm0001#1 = "";
|FPRC;

|PROCESO AltaGrid1;
     #socm0001#0 = enEmpresa;
     #socm0001#1 = "z" * 12;
|FPRC;


|PROGRAMA;

     |HAZ BusquedaApli;
     |SI eaPathContagen ! "";
         |HAZ SituaMaestra;
         |SI swerror = 1;
             |MENAV "    Atencion, no existe Contabilidad ni de esta Empresa ni de Empresa Maestra";
             |ACABA;
         |FINSI;
         |PATH_DAT #cacuenta#aPathConta#;
     |FINSI;

     aQueQuiero = "|NC";
     aNumCampos = #socw0001#aQueQuiero#;
     nNumCampos = aNumCampos;
     nNumCampos = nNumCampos - 1;

     |PINPA #0#socw0001;  nPanta = FSalida;

     |HAZ BtnsGrid1;

     |DDEFECTO #socm0001;
     |HAZ PintaDatosPantalla;

     nRango = 4 + 8 + 16 + 32 + 128;

     |LINEAL_SIMPLE #1#socm0001, nRango, 0602, 1698, BajaGrid1, AltaGrid1, EventoGrid1;
     nGrid1 = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::{{001}}Salir," + nGrid1;
          |LEETECLA aTecla;
          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
          |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;
|FPRO;

|PROCESO LeeFicha;
     enReg = 0;

     |ABRE #agim0019;
     #agim0019#0 = enEmpresa;
     #agim0019#1 = 100;
     |LEE 000#agim0019.];
     |SI FSalida = 0;
         |LEE 000#agim0019.a;
     |SINO;
         |LEE 000#agim0019.u;
     |FINSI;

     |SI FSalida = 0;  |Y #agim0019#0 = enEmpresa;
         enReg = #agim0019#1;
     |FINSI;

     |CIERRA #agim0019;

|FPRC;

|PROCESO Tipo80Fsocm1;  |TIPO 80;
     |HAZ LeeUnidadBasico;

     aAlfa = PARAMETRO % 105;   enEmpresa = aAlfa;
     aAlfa = PARAMETRO % 601;   enModo    = aAlfa;
     |HAZ LeeFicha;

     |ABRET #socm0001;
     |ABRET #socm0002;

     FSalida = 3799;
|FPRC;

|PROCESO Tipo90Fsocm1;  |TIPO 90;
     |CIERRAT #socm0001;
     |CIERRAT #socm0002;
|FPRC;
