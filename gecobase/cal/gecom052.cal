|FICHEROS;
     gecom052;

     gecow002,MANTE;
|FIN;

|VARIABLES;
     n0Vent              = 0;
     n1Vent              = 0;
     nGrid52             = -1;
     nBtnAlt             = 0;
     nBtnCon             = 0;
     nBtnMod             = 0;
     nBtnBor             = 0;
     nRango              = 0;
     nHandle             = 0;
     nLong               = 0;
     nCampo              = 0;
     nCaracter           = 0;
     nPos                = 0;
     nTree               = 0;
     nCmp                = 0;
     nTCmp               = 0;

     aEsc1               = "";
     aEsc2               = "";
     aRetu               = "";
     aTecla              = "";
     aAlfa               = "";
     aPathLocal          = "";
     aFicTmp             = "";
     aDEnt               = "";
     aHEnt               = "";
     aBase               = "";
     aAbre               = "";
     aQQ                 = "";

     {-1}aVent           = "";
         aVent1          = 0;
         aVent2          = 0;
         aVent3          = 0;
         aVent4          = 0;
         aVent5          = "";

     &eaUnidadBasico     = "";
     &eaIde              = "";
|FIN;

|PROCESO Alta;
     |VENTANA 0501, 3599, -1, 16, "Nuevo centro";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;
     |PTEC 802; |PAUSA;

     |C_M #gecow002#1, 1, "S";

     |DDEFECTO #gecow002;

     #gecow002#0 = eaIde;

     |PINPA #0#gecow002;
     |PINDA #0#gecow002;
     |ENDATOS #1#gecow002;
     |SI FSalida = 0;
         |PARA nCmp = 0;  |SI nCmp [ nTCmp;  |HACIENDO nCmp = nCmp + 1;
               #gecom052#nCmp#  = #gecow002#nCmp#;
         |SIGUE;

         |GRABA 020#gecom052.n;
         |LIBERA #gecom052;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;

     |REFRESCACONTROL nGrid52;
     |FOCOTECLADO nGrid52;
|FPRC;

|PROCESO Consulta;
     |VENTANA 0501, 3599, -1, 16, "Consulta centro";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;
     |PTEC 802; |PAUSA;

     |DDEFECTO #gecow002;

     |PARA nCmp = 0;  |SI nCmp [ nTCmp;  |HACIENDO nCmp = nCmp + 1;
           #gecow002#nCmp#  = #gecom052#nCmp#;
     |SIGUE;

     |PINPA #0#gecow002;
     |PINDA #0#gecow002;
     |LEETECLA aTecla;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;
|FPRC;

|PROCESO Modificar;
     |VENTANA 0501, 3599, -1, 16, "Modificar centro";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;
     |PTEC 802; |PAUSA;

     |LEE 101#gecom052.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |C_M #gecow002#1, 1, "N";

     |PARA nCmp = 0;  |SI nCmp [ nTCmp;  |HACIENDO nCmp = nCmp + 1;
           #gecow002#nCmp#  = #gecom052#nCmp#;
     |SIGUE;

     |PINPA #0#gecow002;
     |PINDA #0#gecow002;
     |ENDATOS #2#gecow002;
     |SI FSalida = 0;
         |PARA nCmp = 0;  |SI nCmp [ nTCmp;  |HACIENDO nCmp = nCmp + 1;
               #gecom052#nCmp#  = #gecow002#nCmp#;
         |SIGUE;

         |GRABA 020#gecom052.a;
     |FINSI;
     |LIBERA #gecom052;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;

     |REFRESCACONTROL nGrid52;
     |FOCOTECLADO nGrid52;
|FPRC;

|PROCESO Borrar;
     |LEE 101#gecom052.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |BORRA 020#gecom052.a;
     |LIBERA #gecom052;

     |ESTADO_CONTROL nBtnCon, "DISABLE";
     |ESTADO_CONTROL nBtnMod, "DISABLE";
     |ESTADO_CONTROL nBtnBor, "DISABLE";

     |REFRESCACONTROL nGrid52;
     |FOCOTECLADO nGrid52;
|FPRC;

|PROCESO Baja52;
     #gecom052#0 = eaIde;
     #gecom052#1 = "";
|FPRC;

|PROCESO Alta52;
     #gecom052#0 = eaIde;
     #gecom052#1 = "z" * 30;
|FPRC;


|PROCESO Evnt52;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#gecom052.=;

         |ESTADO_CONTROL nBtnCon, "ENABLE";
         |ESTADO_CONTROL nBtnMod, "ENABLE";
         |ESTADO_CONTROL nBtnBor, "ENABLE";
     |FINSI;

     |SI FSalida = 4;
         |HAZ Consulta;
     |FINSI;
|FPRC;

|PROGRAMA;
     |VENTANA 0501, 3599, -1, 16, "Centros";
     n0Vent = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{193}}Alta", 3288, 3298, Alta;
     nBtnAlt  = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Consulta", 3274, 3286, Consulta;
     nBtnCon  = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Modificar", 3262, 3272, Modificar;
     nBtnMod = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{199}}Borrar", 3250, 3260, Borrar;
     nBtnBor = FSalida;

     |ESTADO_CONTROL nBtnCon, "DISABLE";
     |ESTADO_CONTROL nBtnMod, "DISABLE";
     |ESTADO_CONTROL nBtnBor, "DISABLE";

     nRango   = 2 + 4 + 8 + 16 + 32 + 128;
     |LINEAL_SIMPLE #1#gecom052, nRango, 0602, 3198, Baja52, Alta52, Evnt52;
     nGrid52 = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::{{001}}Salir," + nGrid52;
          |LEETECLA aTecla;
          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
          |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FINVENTANA n0Vent;

     |PULSATECLA;
|FPRO;

|PROCESO T80;  |TIPO 80;
     aQQ   = "|NC";
     aAlfa = #gecom052#aQQ#;
     nTCmp = aAlfa;
     nTCmp = nTCmp - 1;

     |ABRET #gecom052;
|FPRC;

|PROCESO T90;  |TIPO 90;
     |CIERRAT #gecom052;
|FPRC;
