|FICHEROS;
     gecom051;
     gecom052;

     gecow001,MANTE;
|FIN;

|VARIABLES;
     n0Vent              = 0;
     n1Vent              = 0;
     n2Vent              = 0;
     nGrid51            = -1;
     nBtnAlt             = 0;
     nBtnCon             = 0;
     nBtnMod             = 0;
     nBtnBor             = 0;
     nBtnCen             = 0;
     nRango              = 0;
     nHandle             = 0;
     nLong               = 0;
     nCampo              = 0;
     nCaracter           = 0;
     nPos                = 0;
     nTree               = 0;
     nCmp                = 0;
     nTCmp               = 0;

     aEsc1               = "";
     aEsc2               = "";
     aRetu               = "";
     aTecla              = "";
     aAlfa               = "";
     aPathLocal          = "";
     aFicTmp             = "";
     aDEnt               = "";
     aHEnt               = "";
     aBase               = "";
     aAbre               = "";
     aQQ                 = "";

     {-1}aVent           = "";
         aVent1          = 0;
         aVent2          = 0;
         aVent3          = 0;
         aVent4          = 0;
         aVent5          = "";

     &eaUnidadBasico     = "";
     &eaIde              = "";
|FIN;

|PROCESO Centros;
     aAlfa = #gecow001#0;  |QBF aAlfa;
     |SI aAlfa = "";
         |MENAV "0000Introduzca primero la identificaci¢n fiscal  ";
         |ACABA;
     |FINSI;

     |VENTANA 0501, 3599, -1, 0, "";
     n2Vent = FSalida;

     |PTEC 802;  |PAUSA;

     eaIde = #gecow001#0;
     |SUB_EJECUTA "gecom052";

     |FINVENTANA n2Vent;
|FPRC;

|PROCESO Alta;
     |VENTANA 0501, 3599, -1, 16, "Nueva identificaci¢n";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;
     |PTEC 802; |PAUSA;

     |C_M #gecow001#0, 1, "S";
     |C_M #gecow001#1, 1, "S";

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Centros administrativos", 3502, 3523, Centros;
     nBtnCen = FSalida;

     |DDEFECTO #gecow001;

     |PINPA #0#gecow001;
     |PINDA #0#gecow001;
     |ENDATOS #1#gecow001;
     |SI FSalida = 0;
         |PARA nCmp = 0;  |SI nCmp [ nTCmp;  |HACIENDO nCmp = nCmp + 1;
               #gecom051#nCmp#  = #gecow001#nCmp#;
         |SIGUE;

         |GRABA 020#gecom051.n;
         |LIBERA #gecom051;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;

     |REFRESCACONTROL nGrid51;
     |FOCOTECLADO nGrid51;
|FPRC;

|PROCESO Consulta;
     |VENTANA 0501, 3599, -1, 16, "Consulta identificaci¢n";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;
     |PTEC 802; |PAUSA;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Centros administrativos", 3502, 3520, Centros;
     nBtnCen = FSalida;

     |DDEFECTO #gecow001;

     |PARA nCmp = 0;  |SI nCmp [ nTCmp;  |HACIENDO nCmp = nCmp + 1;
           #gecow001#nCmp#  = #gecom051#nCmp#;
     |SIGUE;

     |PINPA #0#gecow001;
     |PINDA #0#gecow001;
     |LEETECLA aTecla;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;
|FPRC;

|PROCESO Modificar;
     |VENTANA 0501, 3599, -1, 16, "Modificar identificaci¢n";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;
     |PTEC 802; |PAUSA;

     |LEE 101#gecom051.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Centros administrativos", 3502, 3520, Centros;
     nBtnCen = FSalida;

     |C_M #gecow001#0, 1, "N";
     |C_M #gecow001#1, 1, "N";

     |PARA nCmp = 0;  |SI nCmp [ nTCmp;  |HACIENDO nCmp = nCmp + 1;
           #gecow001#nCmp#  = #gecom051#nCmp#;
     |SIGUE;

     |PINPA #0#gecow001;
     |PINDA #0#gecow001;
     |ENDATOS #2#gecow001;
     |SI FSalida = 0;
         |PARA nCmp = 0;  |SI nCmp [ nTCmp;  |HACIENDO nCmp = nCmp + 1;
               #gecom051#nCmp#  = #gecow001#nCmp#;
         |SIGUE;

         |GRABA 020#gecom051.a;
     |FINSI;
     |LIBERA #gecom051;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;

     |REFRESCACONTROL nGrid51;
     |FOCOTECLADO nGrid51;
|FPRC;

|PROCESO gecom052B;
     |BORRA 020#gecom052.a;
     |LIBERA #gecom052;
|FPRC;

|DEFBUCLE gecom052B;
     #gecom052#1;
     ;
     #gecom051#0, "";
     #gecom051#0, "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz";
     be;
     NULL, gecom052B;
|FIN;

|PROCESO Borrar;
     |LEE 101#gecom051.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |BUCLE gecom052B;

     |BORRA 020#gecom051.a;
     |LIBERA #gecom051;

     |ESTADO_CONTROL nBtnCon, "DISABLE";
     |ESTADO_CONTROL nBtnMod, "DISABLE";
     |ESTADO_CONTROL nBtnBor, "DISABLE";

     |REFRESCACONTROL nGrid51;
     |FOCOTECLADO nGrid51;
|FPRC;

|PROCESO Evnt51;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#gecom051.=;

         |ESTADO_CONTROL nBtnCon, "ENABLE";
         |ESTADO_CONTROL nBtnMod, "ENABLE";
         |ESTADO_CONTROL nBtnBor, "ENABLE";
     |FINSI;

     |SI FSalida = 4;
         |HAZ Consulta;
     |FINSI;
|FPRC;

|PROGRAMA;
     |VENTANA 0501, 3599, -1, 16, "Emisores/Receptores";
     n0Vent = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{193}}Alta", 3288, 3298, Alta;
     nBtnAlt  = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Consulta", 3274, 3286, Consulta;
     nBtnCon  = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Modificar", 3262, 3272, Modificar;
     nBtnMod = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{199}}Borrar", 3250, 3260, Borrar;
     nBtnBor = FSalida;

     |ESTADO_CONTROL nBtnCon, "DISABLE";
     |ESTADO_CONTROL nBtnMod, "DISABLE";
     |ESTADO_CONTROL nBtnBor, "DISABLE";

     nRango   = 2 + 4 + 8 + 16 + 32 + 128;
     |LINEAL_SIMPLE #1#gecom051, nRango, 0602, 3198, NULL, NULL, Evnt51;
     nGrid51 = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::{{001}}Salir," + nGrid51;
          |LEETECLA aTecla;
          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
          |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FINVENTANA n0Vent;

     |PULSATECLA;
|FPRO;

|PROCESO T80;  |TIPO 80;
     aQQ   = "|NC";
     aAlfa = #gecom051#aQQ#;
     nTCmp = aAlfa;
     nTCmp = nTCmp - 1;

     |ABRET #gecom051;
|FPRC;

|PROCESO T90;  |TIPO 90;
     |CIERRAT #gecom051;
|FPRC;
