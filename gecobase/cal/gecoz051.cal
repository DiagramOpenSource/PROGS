|FICHEROS;
|FIN;

|VARIABLES;
     nCampo              = 0;
     i                   = 0;
     nPos                = 0;
     nHandle             = 0;
     nOk                 = 0;

     aLon                = "";
     aDato               = "";
     aReg                = "";
     aLetra              = "";
     aTab                = "";
     aNomFic             = "";
     aPath               = "";
     aDes                = "";
     aOk                 = "";
     aBase               = "";
     aMas                = "";

     {10}aValor          = "";

     &enCodCli           = 0;
|FIN;

|PROCESO Dato;
     nCampo  = 1;
     IaValor = aValor1<-;
     aLon    = aDato % 0;
     aReg    = "";
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos   = i * 100 + 1;
          aLetra = aDato % nPos;
          |SI aLetra = aTab;
              aValor = aReg;
              aReg = "";
              nCampo = nCampo + 1;
              IaValor = IaValor + 1;
          |SINO;
               aReg = aReg + aLetra;
          |FINSI;

          |SI nCampo > 90; |SAL; |FINSI;     || Por si.... :)
     |SIGUE;

     aValor = aReg;
|FPRC;

|PROCESO LeeCSV;
     aNomFic = PARAMETRO;  |QBF aNomFic;
     |DFICE aPath;
     aDes = aPath + aNomFic + ".ent";
     |XABRE "", aDes, nHandle;
     |SI FSalida ] 0;
          |XLEE nHandle, 250, aDato;    || los nombres
          |XLEE nHandle, 250, aDato;
          |HAZ Dato;
          |XCIERRA nHandle;

          enCodCli = aValor1;
     |FINSI;
|FPRC;

|PROCESO GrabaOK;
     aDes = aPath + aNomFic + ".sal";
     |XABRE "W", aDes, nHandle;
     |SI FSalida ] 0;
          aDato = "resultado" + aTab;  |XGRABA nHandle, aDato;
          aDato = aOk + aTab;          |XGRABA nHandle, aDato;
     |FINSI;
     |XCIERRA nHandle;
|FPRC;

|PROGRAMA;
     |HAZ LeeCSV;

     |SI enCodCli = 0;
         aOk = "CSV sin c¢digo de empresa";
         |HAZ GrabaOK;
         |ACABA;
     |FINSI;

     |DBASS aBase;
     aMas = aBase + "integral/def/dsaz0003.mas";
     |XABRE "E", aMas, 0;
     |SI FSalida < 0;
         aOk = "No est  la integral instalada";
         |HAZ GrabaOK;
         |ACABA;
     |FINSI;

     |SUB_EJECUTA ":integral/dsaz0003;SOLOUNCLIENTE";

     |SUB_EJECUTA "gecoz001;ALTA";

     aOk = "OK";
     |HAZ GrabaOK;
|FPRO;
