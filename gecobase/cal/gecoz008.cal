|FICHEROS;
     gecom000;
     gecom001;
     gecom002;
     gecom014;
     gecom066;
|FIN;

|VARIABLES;
     nCampo              = 0;
     i                   = 0;
     nPos                = 0;
     nHandle             = 0;
     n1Vent              = 0;
     nPausa              = 0;
     nConsola            = 0;
     nOk                 = 0;
     nModoWeb            = 0;
     nLon                = 0;
     nCar                = 0;
     nLoc                = 0;
     nEnc                = 0;

     aAlfa               = "";
     aAlfa1              = "";
     aBase               = "";
     aHora               = "";
     aSh                 = "";
     aEjec               = "";
     aFicPet             = "";
     aFicRes             = "";
     aFicXml             = "";
     aLee                = "";
     aAbre               = "";
     aLon                = "";
     aLetra              = "";
     aReg                = "";
     aMensaje            = "";
     aTab                = "";
     aPath               = "";
     aDes                = "";
     aDato               = "";
     aCar                = "";
     aIde                = "";
     aPal                = "";

     fFechaSys           = @;

     {15}aParam          = "";
     {99}aValor          = "";

     {-1}aVent           = "";
         aVent1          = 0;
         aVent2          = 0;
         aVent3          = 0;
         aVent4          = 0;
         aVent5          = "";

     &eaError            = "";
     &eaPathPlu          = "";
     &eaPathXfa          = "";
     &eaPathXre          = "";
     &eaPathEnv          = "";
     &eaFicXml           = "";
     &eaAlfa             = "";
     &eaNomFic           = "";
     &enEmp              = 0;
     &eaIde              = "";
     &eaPet              = "";
     &eaSer              = "";
     &eaUsu              = "";
     &eaCmp0             = "";
     &eaCmp1             = "";
     &eaCmp4             = "";
     &eaCmp5             = "";
     &eaCmp7             = "";
     &eaCmp8             = "";
     &eaCmp9             = "";
     &eaCmp10            = "";
|FIN;

|PROCESO RecogeIde;
     |QBF eaAlfa;
     aLon = eaAlfa % 0;
     nLon = aLon;
     |PARA nCar = 1;  |SI nCar [ nLon;  |HACIENDO nCar = nCar + 1;
           nPos = (nCar * 100) + 1
           aCar = eaAlfa % nPos;

           |SI aCar = " ";   |O aCar = "|";
               |SI nLoc = 1;  |Y nEnc = 1;
                   nLoc = 0;
                   |SAL;
               |FINSI;
           |FINSI;

           |SI nLoc = 1;
               |SI aCar ! " ";
                   aIde = aIde + aCar;
                   nEnc  = 1;
               |FINSI;
           |FINSI;

           aPal = aPal + aCar;
           |SI aPal = "Motivo:";
               |SI nEnc = 0;
                   nLoc = 1;
               |FINSI;
           |FINSI;

           |SI aCar = " ";  |O aCar = ",";
               aPal = "";
           |FINSI;
     |SIGUE;
|FPRC;

|PROCESO LeeRespuesta;
     aFicRes = eaPathXre + aFicPet + ".txt";

     |PARA;  |SI;  |HACIENDO;
          |PARA nPausa = 1; |SI nPausa [ 10000; |HACIENDO nPausa = nPausa + 1;
          |SIGUE;

          |XABRE "E", aFicRes, nHandle;
          |SI FSalida ] 0;
              |SAL;
          |FINSI;

          |PULSATECLA;
     |SIGUE;

     |XABRE "U", aFicRes, nHandle;
     |SI FSalida ] 0;
         |XLEE nHandle, 250, aLee;  |QBF aLee;
     |FINSI;
     |XCIERRA nHandle;

     eaAlfa = "Motivo: Sin respuesta Resultado: Error";

     |SI aLee ! "";
         eaAlfa = aLee;  |HAZ QuitaRarosTF8;
     |FINSI;

     aIde  = "";
     nOk   = 0;

     aAlfa1 = eaAlfa > "";  |QBT aAlfa1;
     aAlfa  = aAlfa1 - "RESULTADO:OK";
     |SI FSalida ! 0;
         |HAZ RecogeIde;
     |FINSI;

     |ABRE #gecom066;
     #gecom066#0 = aIde;
     |LEE 000#gecom066.=;
     |SI FSalida ! 0;  |DDEFECTO #gecom066;  |FINSI;
     |CIERRA #gecom066;

     eaCmp0  = #gecom002#0;
     eaCmp1  = #gecom002#1;
     eaCmp4  = "Petici¢n de estado";
     eaCmp5  = #gecom002#7;
     eaCmp7  = eaAlfa;
     eaCmp8  = #gecom066#0;
     eaCmp9  = #gecom066#1;
     eaCmp10 = eaUsu;
     |HAZ GrabaLog;

     |FBORRA aFicRes;
|FPRC;

|PROCESO ConsultaFactura;
     |HORA aHora;
     aFicPet = (fFechaSys % -104) + (fFechaSys % 402) + (fFechaSys % 102);
     aFicPet = aFicPet + (aHora % 102) + (aHora % 402) + (aHora % 702);

     aSh = "peticion.sh";
     |SI eaSer = "PRO";
         aSh = "peticionpro.sh";
     |FINSI;
     aSh     = "bash " + eaPathPlu + aSh;

     aParam1  = ("00000" + #gecom001#0) % -105;
     aParam2  = aFicPet;
     aParam3  = #gecom000#2;   |QBF aParam3;
     aParam4  = #gecom000#3;   |QBF aParam4;
     aParam5  = "consultar_factura";
     aParam6  = #gecom002#17;  |QBF aParam6;
     aParam7  = #gecom002#8;   |QBF aParam7;
     aParam8  = &34 + "-" + &34;
     aParam9  = &34 + "-" + &34;
     aParam10 = &34 + "-" + &34;

     aEjec   = aSh   + " " + aParam1  + " " + aParam2;
     aEjec   = aEjec + " " + aParam3  + " " + aParam4;
     aEjec   = aEjec + " " + aParam5  + " " + aParam6;
     aEjec   = aEjec + " " + aParam7  + " " + aParam8;
     aEjec   = aEjec + " " + aParam9  + " " + aParam10;

     |SYSTEM aEjec;

     aSh     = "bash " + eaPathPlu + "recoger.sh";
     aParam1 = ("00000" + #gecom001#0) % -105;
     aParam2 = aFicPet + ".txt";
     aParam3 = eaPathXre;

     aEjec   = aSh   + " " + aParam1 + " " + aParam2;
     aEjec   = aEjec + " " + aParam3;
     |SYSTEM aEjec;

     |HAZ LeeRespuesta;
|FPRC;

|PROCESO GrabaErrorResultado;
     |DFICE aPath;
     aDes = aPath + eaNomFic + ".sal";
     |XABRE "W", aDes, nHandle;
     |SI FSalida ] 0;
          aDato = "error" + aTab;
          |XGRABA nHandle, aDato;
          aDato = "";
          aAlfa = eaError; |QBF aAlfa;
          aDato = aDato + aAlfa + aTab;
          |XGRABA nHandle, aDato;
          |XCIERRA nHandle;
     |FINSI;
|FPRC;

|PROGRAMA;
     |SI eaError ! "";
         |HAZ GrabaErrorResultado;
         |ACABA;
     |FINSI;

     |SI eaPet = "ConsultarFactura";
         |HAZ ConsultaFactura;
         |SI eaError ! "";
             |HAZ GrabaErrorResultado;
         |SINO;
             |HAZ GrabaResultado;
         |FINSI;
     |FINSI;
|FPRO;

|PROCESO Dato;
     nCampo  = 1;
     IaValor = aValor1<-;
     aLon    = aDato % 0;
     aReg    = "";
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos   = i * 100 + 1;
          aLetra = aDato % nPos;
          |SI aLetra = aTab;
              aValor  = aReg;
              aReg    = "";
              nCampo  = nCampo + 1;
              IaValor = IaValor + 1;
          |SINO;
              aReg    = aReg + aLetra;
          |FINSI;

          |SI nCampo > 90; |SAL; |FINSI;     || Por si.... :)
     |SIGUE;

     aValor = aReg;
|FPRC;

|PROCESO LeeCSV;
     eaSer   = "";
     eaPet   = "";
     enEmp   = 0;
     eaIde   = "";

     |DFICE aPath;
     aDes = aPath + eaNomFic + ".ent";
     |XABRE "", aDes, nHandle;
     |SI FSalida ] 0;
          |XLEE nHandle, 250, aDato;    || los nombres
          |XLEE nHandle, 250, aDato;
          |HAZ Dato;
          |XCIERRA nHandle;

          enEmp = aValor1;
          eaIde = aValor2;
          eaSer = aValor3;
          eaUsu = aValor4;
          eaPet = aValor5;
     |FINSI;
|FPRC;

|PROCESO T80;  |TIPO 80;
     eaError = "";

     aTab     = &9;
     eaNomFic = PARAMETRO;
     |HAZ LeeCSV;

     eaError = "";
     |SI eaSer ! "PRO";  |Y eaSer ! "PRU";
         eaError = "0000Falta introducir un servidor";
         |ACABA;
     |FINSI;

     |SI enEmp = 0;
         eaError = "0000Falta introducir una empresa";
         |ACABA;
     |FINSI;

     |SI eaIde = "";
         eaError = "0000Falta introducir un Id de factura";
         |ACABA;
     |FINSI;

     |SI eaPet ! "ConsultarFactura";
         eaError = "0000Petici¢n err¢nea";
         |ACABA;
     |FINSI;

     |ABRE #gecom001;
     #gecom001#0 = enEmp;
     |LEE 000#gecom001.=;
     |SI FSalida ! 0;  |DDEFECTO #gecom001;  |FINSI;
     |CIERRA #gecom001;

     |ABRET #gecom002;
     #gecom002#0 = #gecom001#0;
     #gecom002#1 = eaIde;
     |LEE 101#gecom002.=;
     |SI FSalida ! 0;
         eaError = "0000No existe la factura solicitada";
         |ACABA;
     |FINSI;

     aAlfa = #gecom002#17;  |QBF aAlfa;
     |SI aAlfa = "";
         eaError = "0000La factura no tiene c¢digo de env¡o";
         |ACABA;
     |FINSI;

     nOk = 0;
     |SI #gecom002#7 = "E ";   nOk = 1;  |FINSI;
     |SI #gecom002#7 = "RE";   nOk = 1;  |FINSI;
     |SI #gecom002#7 = "A ";   nOk = 1;  |FINSI;
     |SI #gecom002#7 = "RA";   nOk = 1;  |FINSI;

     |SI nOk = 0;
         eaError = "0000La factura no est  enviada.";
         |ACABA;
     |FINSI;

     |SI eaError ! "";
         |ACABA;
     |FINSI;

     |ABRE #gecom000;
     |LEE 000#gecom000.p;
     |SI FSalida ! 0;  |DDEFECTO #gecom000;  |FINSI;
     |CIERRA #gecom000;

     aAlfa = #gecom000#2 + #gecom000#3;   |QBF aAlfa;
     |SI aAlfa = "";
         eaError = "0000Usuario y password no definidos.";
         |ACABA;
     |FINSI;

     |DBASE aBase;
     eaPathPlu = aBase + "plugins/";

     eaPathXfa = aBase + "xface";                 |MKDIR eaPathXfa;
     eaPathXfa = aBase + "xface/";

     eaPathXre = eaPathXfa + "respuestas";        |MKDIR eaPathXre;
     eaPathXre = eaPathXre + "/" + "ds";          |MKDIR eaPathXre;
     eaPathXre = eaPathXre + "/";

     fFechaSys = ~;
     |HORA aHora;

     aFicPet = (fFechaSys % -104) + (fFechaSys % 402) + (fFechaSys % 102);
     aFicPet = aFicPet + (aHora % 102) + (aHora % 402) + (aHora % 702);

     aSh = "peticion.sh";
     |SI eaPet = "PRO";
         aSh = "peticionpro.sh";
     |FINSI;

     aSh     = "bash " + eaPathPlu + aSh;
     aParam1 = "ds";
     aParam2 = aFicPet;
     aParam3 = #gecom000#2;   |QBF aParam3;
     aParam4 = #gecom000#3;   |QBF aParam4;
     aParam5 = "haz_test";

     aEjec   = aSh   + " " + aParam1 + " " + aParam2;
     aEjec   = aEjec + " " + aParam3 + " " + aParam4;
     aEjec   = aEjec + " " + aParam5;
     |SYSTEM aEjec;

     aSh     = "bash " + eaPathPlu + "recoger.sh";
     aParam1 = "ds";
     aParam2 = aFicPet + ".txt";
     aParam3 = eaPathXre;

     aEjec   = aSh   + " " + aParam1 + " " + aParam2;
     aEjec   = aEjec + " " + aParam3;
     |SYSTEM aEjec;

     aFicRes = eaPathXre + aFicPet + ".txt";

     |XABRE "U", aFicRes, nHandle;
     |SI FSalida ] 0;
         |XLEE nHandle, 250, aLee;
     |FINSI;
     |XCIERRA nHandle;

     |QBF aLee;
     |SI aLee ! "0k";
         eaError = "0000Servicio xface no operativo. Vuelva a intentarlo mas tarde";
     |FINSI;

     |FBORRA aFicRes;
|FPRC;

|PROCESO T90;  |TIPO 90;
     |CIERRAT #gecom002;
|FPRC;
