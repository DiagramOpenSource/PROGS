|FICHEROS;
     gecom000;
     gecom001;
     gecom002;
|FIN;

|VARIABLES;
     nCampo              = 0;
     i                   = 0;
     nPos                = 0;
     nHandle             = 0;
     n1Vent              = 0;
     nPausa              = 0;
     nOk                 = 0;
     nLon                = 0;
     nCar                = 0;
     nLoc                = 0;
     nEnc                = 0;

     aAlfa               = "";
     aAlfa1              = "";
     aBase               = "";
     aHora               = "";
     aSh                 = "";
     aEjec               = "";
     aFicPet             = "";
     aFicRes             = "";
     aFicXml             = "";
     aLee                = "";
     aAbre               = "";
     aLon                = "";
     aLetra              = "";
     aReg                = "";
     aParametro          = "";
     aMensaje            = "";
     aTab                = "";
     aPath               = "";
     aDes                = "";
     aDato               = "";
     aIde                = "";
     aCar                = "";
     aPal                = "";
     aApi                = "";

     fFechaSys           = @;

     {15}aParam          = "";
     {99}aValor          = "";

     {-1}aVent           = "";
         aVent1          = 0;
         aVent2          = 0;
         aVent3          = 0;
         aVent4          = 0;
         aVent5          = "";

     &eaError            = "";
     &eaPathPlu          = "";
     &eaPathXfa          = "";
     &eaPathXre          = "";
     &eaPathEnv          = "";
     &eaFicXml           = "";
     &eaAlfa             = "";
     &eaNomFic           = "";
     &enEmp              = 0;
     &eaIde              = "";
     &eaSer              = "";
     &eaUsu              = "";
     &eaCer              = "";
     &eaPas              = "";
     &eaPython           = "";
     &eaCmp0             = "";
     &eaCmp1             = "";
     &eaCmp4             = "";
     &eaCmp5             = "";
     &eaCmp7             = "";
     &eaCmp8             = "";
     &eaCmp9             = "";
     &eaCmp10            = "";
|FIN;

|PROCESO RecogeIde;
     |QBF eaAlfa;
     aLon = eaAlfa % 0;
     nLon = aLon;

     |PARA nCar = 1;  |SI nCar [ nLon;  |HACIENDO nCar = nCar + 1;
           nPos = (nCar * 100) + 1
           aCar = eaAlfa % nPos;

           |SI nLoc = 1;
               |SI aCar ! " ";
                   aIde = aIde + aCar;
                   nEnc = 1;
               |FINSI;
           |FINSI;

           |SI aCar = " ";
               |SI nEnc = 1;
                   nLoc = 0;
                   |SAL;
               |FINSI;
           |FINSI;

           aPal = aPal + aCar;
           aAlfa1 = aPal > "";  |QBT aAlfa1;
           aAlfa  = aAlfa1 - "MOTIVO:";
           |SI FSalida ! 0;
               |SI nEnc = 0;
                   nLoc = 1;
               |FINSI;
               aPal = "";
           |FINSI;

           |SI aCar = " ";  |O aCar = ",";
               aPal = "";
           |FINSI;
     |SIGUE;
|FPRC;

|PROCESO LeeRespuesta;
     aFicRes = eaPathXre + aFicPet + ".txt";

     |PARA;  |SI;  |HACIENDO;
          |PARA nPausa = 1; |SI nPausa [ 10000; |HACIENDO nPausa = nPausa + 1;
          |SIGUE;

          |XABRE "E", aFicRes, nHandle;
          |SI FSalida ] 0;
              |SAL;
          |FINSI;

          |PULSATECLA;
     |SIGUE;

     |XABRE "U", aFicRes, nHandle;
     |SI FSalida ] 0;
         |XLEE nHandle, 250, aLee;  |QBF aLee;
     |FINSI;
     |XCIERRA nHandle;

     eaAlfa = "Motivo: Sin respuesta Resultado: Error";

     |SI aLee ! "";
         eaAlfa = aLee;  |HAZ QuitaRarosTF8;
     |FINSI;

     #gecom002#9  = eaAlfa;
     #gecom002#17 = "";

     aIde  = "";
     nOk   = 0;

     aAlfa1 = eaAlfa > "";  |QBT aAlfa1;
     aAlfa  = aAlfa1 - "RESULTADO:OK";
     |SI FSalida ! 0;
         |HAZ RecogeIde;
     |FINSI;

     |SI aIde ! ""; nOk = 1;  |FINSI;

     |SI nOk = 0;
         |SI #gecom002#7 = "P ";
          |O #gecom002#7 = "X ";
             #gecom002#7 = "X";
         |FINSI;

         |SI #gecom002#7 = "RP";
          |O #gecom002#7 = "RX";
             #gecom002#7 = "RX";
         |FINSI;
     |SINO;
         |SI #gecom002#7 = "P ";
          |O #gecom002#7 = "X ";
             #gecom002#7 = "E";
         |FINSI;

         |SI #gecom002#7 = "RP";
          |O #gecom002#7 = "RX";
             #gecom002#7 = "RE";
         |FINSI;

         #gecom002#17 = aIde;
     |FINSI;

     |HORA aHora;

     #gecom002#10 = fFechaSys;
     #gecom002#11 = aHora;
     #gecom002#12 = Usuario % 810;
     #gecom002#16 = eaSer;
     #gecom002#18 = eaFicXml;

     |GRABA 020#gecom002.a;
     |LIBERA #gecom002;

     eaCmp0  = #gecom002#0;
     eaCmp1  = #gecom002#1;
     eaCmp4  = "Env¡o de factura";
     eaCmp5  = #gecom002#7;
     eaCmp7  = #gecom002#9;
     eaCmp8  = "";
     eaCmp9  = "";
     eaCmp10 = eaUsu;
     |HAZ GrabaLog;

     |FBORRA aFicRes;
|FPRC;

|PROCESO FirmaXml;
     aApi    = eaPathPlu + "firma.py";
     aParam1 = "-o " + aFicXml;
     aParam2 = "-c " + eaCer;
     aParam3 = "-p " + eaPas;

     aEjec = eaPython + " " + aApi + " " + aParam1 + " " + aParam2;
     aEjec = aEjec    + " " + aParam3;
     |SYSTEM aEjec;
|FPRC;

|PROCESO Envio;
     eaPathEnv = eaPathXfa + "envios";
     eaPathEnv = eaPathEnv + "/" + (("00000" + #gecom001#0) % -105);  |MKDIR eaPathEnv;
     eaPathEnv = eaPathEnv + "/";

     eaPathXre = eaPathXfa + "respuestas";
     eaPathXre = eaPathXre + "/" + (("00000" + #gecom001#0) % -105);  |MKDIR eaPathXre;
     eaPathXre = eaPathXre + "/";

     aFicXml = eaPathEnv + eaFicXml;
     |XABRE "E", aFicXml, 0;
     |SI FSalida < 0;
         eaCmp0  = #gecom002#0;
         eaCmp1  = #gecom002#1;
         eaCmp4  = "Env¡o de factura";
         eaCmp5  = #gecom002#7;
         eaCmp7  = "Problemas en la preparaci¢n de la XML";
         eaCmp8  = "";
         eaCmp9  = "";
         eaCmp10 = eaUsu;
         |HAZ GrabaLog;

         |ACABA;
     |FINSI;

     |SI eaCer ! "";
         |HAZ FirmaXml;
     |FINSI;

     aSh     = "bash " + eaPathPlu + "subirxface.sh";
     aParam1 = ("00000" + #gecom001#0) % -105;
     aParam2 = eaFicXml;
     aParam3 = eaPathEnv + eaFicXml;

     aEjec   = aSh   + " " + aParam1 + " " + aParam2;
     aEjec   = aEjec + " " + aParam3;
     |SYSTEM aEjec;

     |HORA aHora;
     aFicPet = (fFechaSys % -104) + (fFechaSys % 402) + (fFechaSys % 102);
     aFicPet = aFicPet + (aHora % 102) + (aHora % 402) + (aHora % 702);

     aSh = "peticion.sh";
     |SI eaSer = "PRO";
         aSh = "peticionpro.sh";
     |FINSI;
     aSh     = "bash " + eaPathPlu + aSh;

     aParam1 = ("00000" + #gecom001#0) % -105;
     aParam2 = aFicPet;
     aParam3 = #gecom000#2;   |QBF aParam3;
     aParam4 = #gecom000#3;   |QBF aParam4;
     aParam5 = "enviar_factura";

     aParam6  = "/home/zerocoma/" + aParam1 + "/facturas/" + eaFicXml;
     aParam7  = "/home/zerocoma/" + aParam1 + "/facturas/";
     aParam8  = #gecom000#4;   |QBF aParam8;
     aParam9  = &34 + "-" + &34;

     aParam10 = #gecom002#8;   |QBF aParam10;
     |SI aParam10 = "";
         aParam10 = "-";
     |FINSI;
     aParam10 = &34 + aParam10 + &34;

     aParam11 = &34 + "-" + &34;
     aParam12 = &34 + "-" + &34;
     aParam13 = &34 + "-" + &34;

     aEjec   = aSh   + " " + aParam1  + " " + aParam2;
     aEjec   = aEjec + " " + aParam3  + " " + aParam4;
     aEjec   = aEjec + " " + aParam5  + " " + aParam6;
     aEjec   = aEjec + " " + aParam7  + " " + aParam8;
     aEjec   = aEjec + " " + aParam9  + " " + aParam10;
     aEjec   = aEjec + " " + aParam11 + " " + aParam12;
     aEjec   = aEjec + " " + aParam13;

     |SYSTEM aEjec;

     aSh     = "bash " + eaPathPlu + "recoger.sh";
     aParam1 = ("00000" + #gecom001#0) % -105;
     aParam2 = aFicPet + ".txt";
     aParam3 = eaPathXre;

     aEjec   = aSh   + " " + aParam1 + " " + aParam2;
     aEjec   = aEjec + " " + aParam3;
     |SYSTEM aEjec;

     |HAZ LeeRespuesta;

     aFicXml = eaPathEnv + eaFicXml;
|FPRC;

|PROCESO EnvioUnico;
     |SUB_EJECUTA "gecoz003;ENVIO";

     |HAZ Envio;
|FPRC;

|PROCESO GrabaErrorResultado;
     |DFICE aPath;
     aDes = aPath + eaNomFic + ".sal";
     |XABRE "W", aDes, nHandle;
     |SI FSalida ] 0;
          aDato = "error" + aTab;
          |XGRABA nHandle, aDato;
          aDato = "";
          aAlfa = eaError; |QBF aAlfa;
          aDato = aDato + aAlfa + aTab;
          |XGRABA nHandle, aDato;
          |XCIERRA nHandle;
     |FINSI;
|FPRC;

|PROCESO EnvioPeticion;
     |DFICE aPath;
     aAbre = aPath + eaNomFic + ".ent";
     |XABRE "WB", aAbre, nHandle;

     aTab = &9;

     aAlfa = "Empresa" + aTab;
     aAlfa = aAlfa  + "Identificador" + aTab;
     aAlfa = aAlfa  + "Servidor" + aTab;
     aAlfa = aAlfa  + "Usuario" + aTab;
     aAlfa = aAlfa  + "Peticion" + &13 + &10;
     |XGRABA nHandle, aAlfa;

     aAlfa = #gecom002#0;                     |QBF aAlfa;
     aAlfa = aAlfa + aTab + #gecom002#1;      |QBF aAlfa;
     aAlfa = aAlfa + aTab + eaSer;            |QBF aAlfa;
     aAlfa = aAlfa + aTab + eaUsu;            |QBF aAlfa;
     aAlfa = aAlfa + aTab + "ConsultarFactura";
     aAlfa = aAlfa + &13 + &10;
     |XGRABA nHandle, aAlfa;

     |XCIERRA nHandle;

     aAlfa = "gecoz008;" + eaNomFic;
     |SUB_EJECUTA aAlfa;
|FPRC;

|PROGRAMA;
     |SI eaError ! "";
         |HAZ GrabaErrorResultado;
         |ACABA;
     |FINSI;

     |HAZ EnvioUnico;

     |SI #gecom002#7 = "E ";  |O #gecom002#7 = "EA";
         |HAZ EnvioPeticion;
     |SINO;
         |HAZ GrabaResultado;
     |FINSI;
|FPRO;

|PROCESO Dato;
     nCampo  = 1;
     IaValor = aValor1<-;
     aLon    = aDato % 0;
     aReg    = "";
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos   = i * 100 + 1;
          aLetra = aDato % nPos;
          |SI aLetra = aTab;
              aValor = aReg;
              aReg = "";
              nCampo = nCampo + 1;
              IaValor = IaValor + 1;
          |SINO;
               aReg = aReg + aLetra;
          |FINSI;

          |SI nCampo > 90; |SAL; |FINSI;     || Por si.... :)
     |SIGUE;

     aValor = aReg;
|FPRC;

|PROCESO LeeCSV;
     enEmp = 0;
     eaIde = "";
     eaSer = "";
     eaUsu = "";
     eaPas = "";
     eaCer = "";

     |DFICE aPath;
     aDes = aPath + eaNomFic + ".ent";
     |XABRE "", aDes, nHandle;
     |SI FSalida ] 0;
          |XLEE nHandle, 250, aDato;    || los nombres
          |XLEE nHandle, 250, aDato;
          |HAZ Dato;
          |XCIERRA nHandle;

          enEmp = aValor1;
          eaIde = aValor2;
          eaSer = aValor3;
          eaUsu = aValor4;
          eaPas = aValor5;  |QBF eaPas;
          eaCer = aValor6;  |QBF eaCer;
     |FINSI;
|FPRC;

|PROCESO T80;  |TIPO 80;
     aTab     = &9;
     eaNomFic = PARAMETRO;   |QBF eaNomFic;
     |HAZ LeeCSV;

     eaError = "";
     |SI eaSer ! "PRO";  |Y eaSer ! "PRU";
         eaError = "0000Falta introducir un servidor";
     |FINSI;

     |SI enEmp = 0;
         eaError = "0000Falta introducir una empresa";
     |FINSI;

     |SI eaIde = "";
         eaError = "0000Falta introducir un Id de factura";
     |FINSI;

     |SI eaUsu = "";
         eaError = "0000Falta introducir un usuario";
     |FINSI;

     |SI eaError ! "";
         |ACABA;
     |FINSI;

     eaError = "";

     |ABRE #gecom001;
     #gecom001#0 = enEmp;
     |LEE 000#gecom001.=;
     |SI FSalida ! 0;  |DDEFECTO #gecom001;  |FINSI;
     |CIERRA #gecom001;

     |ABRET #gecom002;
     #gecom002#0 = #gecom001#0;
     #gecom002#1 = eaIde;
     |LEE 101#gecom002.=;
     |SI FSalida ! 0;
         eaError = "0000No existe la factura en la base de datos";
         |ACABA;
     |FINSI;

     nOk = 0;
     |SI #gecom002#7 = "P ";   nOk = 1;  |FINSI;
     |SI #gecom002#7 = "RP";   nOk = 1;  |FINSI;
     |SI #gecom002#7 = "X ";   nOk = 1;  |FINSI;
     |SI #gecom002#7 = "RX";   nOk = 1;  |FINSI;

     |SI nOk = 0;
         eaError = "0000La factura ya est  enviada.";
         |ACABA;
     |FINSI;

     |ABRE #gecom000;
     |LEE 000#gecom000.p;
     |SI FSalida ! 0;  |DDEFECTO #gecom000;  |FINSI;
     |CIERRA #gecom000;

     aAlfa = #gecom000#2 + #gecom000#3;   |QBF aAlfa;
     |SI aAlfa = "";
         eaError = "0000Usuario y password no definidos.";
         |ACABA;
     |FINSI;

     |DBASE aBase;
     eaPathPlu = aBase + "plugins/";

     eaPathXfa = aBase + "xface";                 |MKDIR eaPathXfa;
     eaPathXfa = aBase + "xface/";

     eaPathXre = eaPathXfa + "respuestas";        |MKDIR eaPathXre;
     eaPathXre = eaPathXre + "/" + "ds";          |MKDIR eaPathXre;
     eaPathXre = eaPathXre + "/";

     fFechaSys = ~;
     |HORA aHora;

     aFicPet = (fFechaSys % -104) + (fFechaSys % 402) + (fFechaSys % 102);
     aFicPet = aFicPet + (aHora % 102) + (aHora % 402) + (aHora % 702);

     aSh = "peticion.sh";
     |SI eaSer = "PRO";
         aSh = "peticionpro.sh";
     |FINSI;
     aSh     = "bash " + eaPathPlu + aSh;

     aParam1 = "ds";
     aParam2 = aFicPet;
     aParam3 = #gecom000#2;   |QBF aParam3;
     aParam4 = #gecom000#3;   |QBF aParam4;
     aParam5 = "haz_test";

     aEjec   = aSh   + " " + aParam1 + " " + aParam2;
     aEjec   = aEjec + " " + aParam3 + " " + aParam4;
     aEjec   = aEjec + " " + aParam5;
     |SYSTEM aEjec;

     aSh     = "bash " + eaPathPlu + "recoger.sh";
     aParam1 = "ds";
     aParam2 = aFicPet + ".txt";
     aParam3 = eaPathXre;

     aEjec   = aSh   + " " + aParam1 + " " + aParam2;
     aEjec   = aEjec + " " + aParam3;
     |SYSTEM aEjec;

     aFicRes = eaPathXre + aFicPet + ".txt";

     |XABRE "U", aFicRes, nHandle;
     |SI FSalida ] 0;
         |XLEE nHandle, 250, aLee;
     |FINSI;
     |XCIERRA nHandle;

     |QBF aLee;
     |SI aLee ! "0k";
         eaError = "0000Servicio xface no operativo. Vuelva a intentarlo mas tarde";
     |FINSI;

     |FBORRA aFicRes;

     |HAZ VersionPython;
|FPRC;

|PROCESO T90;  |TIPO 90;
     |CIERRAT #gecom002;

     |SI eaCer ! "";
         |FBORRA eaCer;
     |FINSI;
|FPRC;
