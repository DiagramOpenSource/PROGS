|FICHEROS;
     gecom053;
|FIN;

|VARIABLES;
     nHandle             = 0;
     nLong               = 0;
     nCampo              = 0;
     nCaracter           = 0;
     nPos                = 0;

     aAlfa               = "";
     aBase               = "";
     aAbre               = "";
     aCadena             = "";
     aLong               = "";
     aTab                = "";
     aCarac1             = "";
     aCampoA             = "";
     aCampoB             = "";
     aCampoC             = "";
     aCampoD             = "";
|FIN;

|PROGRAMA;
     |CONSULTA_CLAVE #1#gecom053;
|FPRO;

|PROCESO GrabaReg;
     |QBF aCampoA;
     |QBF aCampoB;
     |QBF aCampoC;
     |QBF aCampoD;

     |SI aCampoA = "";  |ACABA;  |FINSI;

     #gecom053#0 = aCampoA;
     |LEE 000#gecom053.=;
     |SI FSalida ! 0;
         |DDEFECTO #gecom053;
         #gecom053#0 = aCampoA;
         |GRABA 020#gecom053.b;
     |FINSI;

     #gecom053#1 = aCampoB;
     #gecom053#2 = aCampoC;
     #gecom053#3 = aCampoD;

     |GRABA 020#gecom053.a;
     |LIBERA #gecom053;
|FPRC;

|PROCESO RecogeCSV;
     |DBASE aBase;

     aAbre = aBase + "patrones/paises.csv";
     |XABRE "", aAbre, nHandle;
     |PARA;  |SI FSalida ] 0;  |HACIENDO;
          |XLEE nHandle, 250, aAlfa;
          |SI FSalida < 0;  |SAL;  |FINSI;

          aCampoA  = "";
          aCampoB  = "";
          aCampoC  = "";
          aCampoD  = "";

          aCadena = aAlfa;  |QBF aCadena;
          aLong   = aCadena % 0;
          nLong   = aLong;
          nCampo  = 1;
          aTab    = ";";

          |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
                nPos    = (100 * nCaracter) + 1;
                aCarac1 = aCadena % nPos;
                |SI aCarac1 = aTab;
                    nCampo = nCampo + 1;
                |SINO;
                    |SI nCampo = 1;  aCampoA  = aCampoA  + aCarac1;  |FINSI;
                    |SI nCampo = 2;  aCampoB  = aCampoB  + aCarac1;  |FINSI;
                    |SI nCampo = 3;  aCampoC  = aCampoC  + aCarac1;  |FINSI;
                    |SI nCampo = 4;  aCampoD  = aCampoD  + aCarac1;  |FINSI;

                    |SI nCampo > 4;  |SAL;  |FINSI;
                |FINSI;
          |SIGUE;

          |HAZ GrabaReg;
     |SIGUE;
     |XCIERRA nHandle;
|FPRC;

|PROCESO T80;  |TIPO 80;
     |ABRET #gecom053;

     |LEE 000#gecom053.p;
     |SI FSalida ! 0;
         |HAZ RecogeCSV;
     |FINSI;
|FPRC;

|PROCESO T90;  |TIPO 90;
     |CIERRAT #gecom053;
|FPRC;
