|FICHEROS;
     gecom001;
     gecom050;

     gecow000;
|FIN;

|VARIABLES;
     n0Vent              = 0;
     n1Vent              = 0;
     n2Vent              = 0;
     nBtnNue             = 0;
     nBtnQui             = 0;
     nBtnMod             = 0;
     nBtnRee             = 0;
     nRango              = 0;
     nIde                = 1;
     nHandle             = 0;

     aEsc1               = "";
     aEsc2               = "";
     aRetu               = "";
     aTecla              = "";
     aAlfa               = "";
     aHora               = "";
     aFecha              = "";
     aConten             = "";
     aTo                 = "";
     aBase               = "";
     aBaseMensaje        = "";
     aFrom               = "";
     aAsunto             = "";
     aAbreTXT            = "";
     aIP                 = "";
     aSalida             = "";
     aMensaje            = "";

     fFecha              = @;

 {-1}aVent               = "";
     aVent1              = 0;
     aVent2              = 0;
     aVent3              = 0;
     aVent4              = 0;
     aVent5              = "";

     &enEmp              = 0;
     &enModo             = 0;
     &enGrid50           = 0;
|FIN;

|PROCESO QuitarAcceso;
     |SI #gecom050#1 = 1;
         |MENAV "0000Acceso principal, no se puede quitar el acceso, puede cambiar el e_mail";
         |ACABA;
     |FINSI;

     |CONFI "0000NSeguro que quiere quitar el acceso";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |LEE 101#gecom050.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |BORRA 020#gecom050.a;
     |LIBERA #gecom050;

     |REFRESCACONTROL enGrid50;
     |FOCOTECLADO enGrid50;
|FPRC;

|PROCESO NuevoAcceso;
     |VENTANA 0501, 1680, -1, 16, "Nuevo Acceso";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     |DDEFECTO #gecow000;

     enModo = 1;
     |PINPA #0#gecow000;
     |PINDA #0#gecow000;
     |ENDATOS #2#gecow000;
     |SI FSalida = 0;
         #gecom050#0 = #gecom001#0;
         #gecom050#1 = 999;
         |LEE 000#gecom050.];
         |SI FSalida = 0;
             |LEE 000#gecom050.a;
         |SINO;
             |LEE 000#gecom050.u;
         |FINSI;

         nIde = 1;
         |SI FSalida = 0;  |Y #gecom050#0 = #gecom001#0;
             nIde = #gecom050#1 + 1;
         |FINSI;

         #gecom050#0 = #gecom001#0;
         #gecom050#1 = nIde;
         #gecom050#2 = #gecow000#0;
         #gecom050#3 = #gecow000#1;
         #gecom050#4 = #gecow000#2;

         |GRABA 020#gecom050.n;
         |LIBERA #gecom050;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;

     |REFRESCACONTROL enGrid50;
     |FOCOTECLADO enGrid50;
|FPRC;

|PROCESO ModificarDatos;
     |VENTANA 0501, 1680, -1, 16, "Modificar datos";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     |DDEFECTO #gecow000;
     #gecow000#0 = #gecom050#2;
     #gecow000#1 = #gecom050#3;
     #gecow000#2 = #gecom050#4;

     enModo = 2;
     |PINPA #0#gecow000;
     |PINDA #0#gecow000;
     |ENDATOS #2#gecow000;
     |SI FSalida = 0;
         |LEE 101#gecom050.=;
         |SI FSalida = 0;
             #gecom050#2 = #gecow000#0;
             #gecom050#3 = #gecow000#1;
             #gecom050#4 = #gecow000#2;

             |GRABA 020#gecom050.a;
             |LIBERA #gecom050;
         |FINSI;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;

     |REFRESCACONTROL enGrid50;
     |FOCOTECLADO enGrid50;
|FPRC;

|PROCESO Evnt50;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#gecom050.=;
         |SI FSalida = 0;
             |ESTADO_CONTROL nBtnQui, "ENABLE";
             |ESTADO_CONTROL nBtnMod, "ENABLE";
             |ESTADO_CONTROL nBtnRee, "ENABLE";
         |FINSI;
     |FINSI;

     |SI FSalida = 4;
         |HAZ ModificarDatos;
     |FINSI;
|FPRC;

|PROCESO Baja50;
     #gecom050#0 = #gecom001#0;
     #gecom050#1 = 001;
|FPRC;

|PROCESO Alta50;
     #gecom050#0 = #gecom001#0;
     #gecom050#1 = 999;
|FPRC;

|PROGRAMA;
     |CLS;
     |CABEZA "Accesos a la empresa DESPABOX";

     |VENTANA 0501, 3599, -1, 16, "Accesos SIIBOX";
     n0Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n0Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |CONTROL_SIMPLE 0, "BOTON,Nuevo acceso", 3302, 3314, NuevoAcceso;
     nBtnNue = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,Modificar datos", 3316, 3328, ModificarDatos;
     nBtnMod = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,Quitar acceso", 3330, 3342, QuitarAcceso;
     nBtnQui = FSalida;

     |ESTADO_CONTROL nBtnQui, "DISABLE";
     |ESTADO_CONTROL nBtnMod, "DISABLE";
     |ESTADO_CONTROL nBtnRee, "DISABLE";

     nRango = 4 + 8 + 16 + 32 + 128;

     |LINEAL_SIMPLE #1#gecom050, nRango, 0502, 3298, Baja50, Alta50, Evnt50;
     enGrid50 = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}Salir," + enGrid50;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |DESTRUYECONTROL enGrid50;

     |FIN_CONTROL_WINDOWS nBtnNue;
     |FIN_CONTROL_WINDOWS nBtnQui;
     |FIN_CONTROL_WINDOWS nBtnMod;
     |FIN_CONTROL_WINDOWS nBtnRee;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n0Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n0Vent;
|FPRO;

|PROCESO T80;  |TIPO 80;
     |ABRET #gecom001;
     |ABRET #gecom050;

     |SELECT #1#gecom001;
     #gecom001#0 = enEmp;
     |LEE 000#gecom001.=;
|FPRC;

|PROCESO T90;  |TIPO 90;
     |CIERRAT #gecom001;
     |CIERRAT #gecom050;
|FPRC;
