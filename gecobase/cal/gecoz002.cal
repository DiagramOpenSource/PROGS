|FICHEROS;
     gecom001;
     gecom002;
     gecom003;
     gecom004;
     gecom005;
     gecom006;
     gecom007;
     gecom008;
     gecom009;
     gecom010;
     gecom011;
     gecom012;
     gecom013;
     gecom014;
     gecom051;

     gecow001;
     gecow003;
     gecow015,MANTE;
|FIN;

|VARIABLES;
     n0Vent              = 0;
     n1Vent              = 0;
     nTree               = -1;
     nHandle             = 0;
     nLong               = 0;
     nPosi               = 0;
     nPos                = 0;
     nLabel              = 0;
     nRango              = 0;
     nCmp                = 0;

     aAlfa               = "";
     aEsc1               = "";
     aEsc2               = "";
     aRetu               = "";
     aTecla              = "";
     aFicTxt             = "";
     aFicCSV             = "";
     aAbre               = "";
     aBuscar             = "";
     aRefresca           = "";
     aLong               = "";
     aCarac              = "";
     aTexto              = "";
     aQQ                 = "";
     aBase               = "";
     aHora               = "";
     aSer                = "";
     aPath               = "";
     aTab                = "";
     aMot                = "";
     aFic                = "";

     fFechaSys           = @;

     {10} aPopup         = "";

     {-1}aVent           = "";
         aVent1          = 0;
         aVent2          = 0;
         aVent3          = 0;
         aVent4          = 0;
         aVent5          = "";

     {-1}sTree           = 0;
         sT_nID          = 0;
         sT_nOper        = 0;
         sT_aData        = "";

     &enGrid2            = 0;
     &enBtnMod3          = -1;
     &enBtnCon3          = -1;
     &enBtnBor3          = -1;
     &enBtnOpc3          = -1;
     &enBtnAnu3          = -1;
     &enBtnRec3          = -1;
     &enEmp              = 0;
     &enTCmp3            = 0;
     &enTCmp5            = 0;
     &enTCmp11           = 0;
     &enTCmp12           = 0;
     &enTCmp51           = 0;
     &enMeteCentro       = 0;
     &eaIde              = "";
     &eaTip              = "";
     &eaSit              = "";
     &eaUnidadBasico     = "";
     &eaPathTmpLocal     = "";
     &eaPathAdj          = "";
     &eaPathPlu          = "";
     &eaIP               = "";
     &eaError            = "";
     &eaTFac             = "";
|FIN;

|PROCESO PonLabel;
     aAlfa = "";
     aLong = Contenido % 0;
     nLong = aLong;
     |PARA nPosi = 1;  |SI nPosi [ nLong;  |HACIENDO nPosi = nPosi + 1;
           nPos = (nPosi * 100) + 1;
           aCarac = Contenido % nPos;
           |SI aCarac = ":";  |SAL;  |FINSI;
           aAlfa  = aAlfa + aCarac;
     |SIGUE;

     |QBT aAlfa;

     sT_nID   = nTree;
     sT_nOper = 4;
     sT_aData = ":" + aAlfa;

     |ESPECIFICA "COMMANDCONTROL", sTree;
     aTexto = Contenido;

     aAlfa = "LABEL,{{10}}" + aTexto;
     |CONTROL_SIMPLE 0, aAlfa, 1602, 1680, NULL;
     nLabel = FSalida;
|FPRC;


|PROCESO BtnAnu3;
     |CONFI "0000NAnulamos la factura seleccionada.";
     |SI FSalida ! 0;
         |FOCOTECLADO enGrid2;
         |ACABA;
     |FINSI;

     |VENTANA 0501, 2499, -1, 16, "Motivo de la anulaci¢n";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;
     |PTEC 802; |PAUSA;

     |DDEFECTO #gecow015;

     |PINPA #0#gecow015;
     |PINDA #0#gecow015;
     |ENDATOS #1#gecow015;
     |SI FSalida ! 0;
         #gecow015#0 = "";
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;

     aMot = #gecow015#0;   |QBF aMot;
     |SI aMot = "";
         |MENAV "0000Operaci¢n cancelada";
         |ACABA;
     |FINSI;

     eaIde = #gecom002#1;
     aSer  = #gecom002#16;  |QBF aSer;

     |SI aSer = "";
         aPopup1  = enBtnOpc3;
         aPopup2  = 2;
         aPopup3  = "Servidor real";
         aPopup4  = "Servidor pruebas";

         IaPopup  = aPopup1  <-;
         |ESPECIFICA "TRACKPOPUP", aPopup;
         |SI FSalida ! 0;
             |SI FSalida = 1;  aSer = "PRO";  |FINSI;
             |SI FSalida = 2;  aSer = "PRU";  |FINSI;
         |FINSI;
     |FINSI;

     |SI aSer = "";
         |ACABA;
     |FINSI;

     |HORA aHora;

     aFicCSV = (fFechaSys % -104) + (fFechaSys % 402) + (fFechaSys % 102);
     aFicCSV = aFicCSV + (aHora % 102) + (aHora % 402) + (aHora % 702);

     |DFICE aPath;
     aAbre = aPath + aFicCSV + ".ent";
     |XABRE "WB", aAbre, nHandle;

     aTab = &9;

     aAlfa = "Empresa" + aTab;
     aAlfa = aAlfa  + "Identificador" + aTab;
     aAlfa = aAlfa  + "Servidor" + aTab;
     aAlfa = aAlfa  + "Usuario" + aTab;
     aAlfa = aAlfa  + "Motivo" + &13 + &10;
     |XGRABA nHandle, aAlfa;

     aAlfa = #gecom002#0;                     |QBF aAlfa;
     aAlfa = aAlfa + aTab + #gecom002#1;      |QBF aAlfa;
     aAlfa = aAlfa + aTab + aSer;             |QBF aAlfa;
     aAlfa = aAlfa + aTab + (Usuario % 810);  |QBF aAlfa;
     aAlfa = aAlfa + aTab + aMot;             |QBF aAlfa;
     aAlfa = aAlfa + &13 + &10;
     |XGRABA nHandle, aAlfa;

     |XCIERRA nHandle;

     aAlfa = "gecoz005;" + aFicCSV;
     |SUB_EJECUTA aAlfa;

     |SI eaError ! "";
         |MENAV eaError;
     |FINSI;

     |DFICE aPath;
     aFic = aPath + aFicCSV + ".sal";
     |FBORRA aFic;

     aFic = aPath + aFicCSV + ".ent";
     |FBORRA aFic;

     |REFRESCACONTROL enGrid2;
     |FOCOTECLADO enGrid2;
|FPRC;

|PROCESO BtnRec3;
     |CONFI "0000NRectificamos la factura seleccionada.";
     |SI FSalida ! 0;
         |FOCOTECLADO enGrid2;
         |ACABA;
     |FINSI;

     eaIde = #gecom002#1;
     |SUB_EJECUTA "gecoz006";

     |REFRESCACONTROL enGrid2;
     |FOCOTECLADO enGrid2;
|FPRC;

|PROCESO VerXML;
     eaIde = #gecom002#1;
     |SUB_EJECUTA "gecoz003";

     |FOCOTECLADO enGrid2;
|FPRC;

|PROCESO RecXML;
                         eaTFac = "REC";
     |SI aAlfa = "Imp";  eaTFac = "EMI";  |FINSI;

     aPopup1  = enBtnOpc3;
     aPopup2  = 2;
     aPopup3  = "XML en el PC";
     aPopup4  = "Lista pendiente";

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
         |SI FSalida = 1;  |SUB_EJECUTA "gecoz007;BROWSE";  |FINSI;
         |SI FSalida = 2;  |SUB_EJECUTA "gecoz007;LISTA";   |FINSI;
     |FINSI;

     |REFRESCACONTROL enGrid2;
     |FOCOTECLADO enGrid2;
|FPRC;

|PROCESO EnvXML;
     |CONFI "0000SEnviamos la factura seleccionada.";
     |SI FSalida ! 0;
         |FOCOTECLADO enGrid2;
         |ACABA;
     |FINSI;

     eaIde = #gecom002#1;
     aSer  = "";

     aPopup1  = enBtnOpc3;
     aPopup2  = 2;
     aPopup3  = "Servidor real";
     aPopup4  = "Servidor pruebas";

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
         |SI FSalida = 1;  aSer = "PRO";  |FINSI;
         |SI FSalida = 2;  aSer = "PRU";  |FINSI;
     |FINSI;

     |SI aSer = "";
         |ACABA;
     |FINSI;

     |HORA aHora;

     aFicCSV = (fFechaSys % -104) + (fFechaSys % 402) + (fFechaSys % 102);
     aFicCSV = aFicCSV + (aHora % 102) + (aHora % 402) + (aHora % 702);

     |DFICE aPath;
     aAbre = aPath + aFicCSV + ".ent";
     |XABRE "WB", aAbre, nHandle;

     aTab = &9;

     aAlfa = "Empresa" + aTab;
     aAlfa = aAlfa  + "Identificador" + aTab;
     aAlfa = aAlfa  + "Servidor" + aTab;
     aAlfa = aAlfa  + "Usuario" + &13 + &10;
     |XGRABA nHandle, aAlfa;

     aAlfa = #gecom002#0;                     |QBF aAlfa;
     aAlfa = aAlfa + aTab + #gecom002#1;      |QBF aAlfa;
     aAlfa = aAlfa + aTab + aSer;             |QBF aAlfa;
     aAlfa = aAlfa + aTab + (Usuario % 810);  |QBF aAlfa;
     aAlfa = aAlfa + &13 + &10;
     |XGRABA nHandle, aAlfa;

     |XCIERRA nHandle;

     aAlfa = "gecoz004;" + aFicCSV;
     |SUB_EJECUTA aAlfa;

     |SI eaError ! "";
         |MENAV eaError;
     |FINSI;

     |DFICE aPath;
     aFic = aPath + aFicCSV + ".sal";
     |FBORRA aFic;

     aFic = aPath + aFicCSV + ".ent";
     |FBORRA aFic;

     |REFRESCACONTROL enGrid2;
     |FOCOTECLADO enGrid2;
|FPRC;

|PROCESO PideEstado;
     eaIde    = #gecom002#1;
     aSer     = #gecom002#16;  |QBF aSer;

     |SI aSer = "";
         aPopup1  = enBtnOpc3;
         aPopup2  = 2;
         aPopup3  = "Servidor real";
         aPopup4  = "Servidor pruebas";

         IaPopup  = aPopup1  <-;
         |ESPECIFICA "TRACKPOPUP", aPopup;
         |SI FSalida ! 0;
             |SI FSalida = 1;  aSer = "PRO";  |FINSI;
             |SI FSalida = 2;  aSer = "PRU";  |FINSI;
         |FINSI;

         |SI aSer = "";
             |MENAV "0000Seleccione un servidor";
             |ACABA;
         |FINSI;
     |FINSI;

     |HORA aHora;

     aFicCSV = (fFechaSys % -104) + (fFechaSys % 402) + (fFechaSys % 102);
     aFicCSV = aFicCSV + (aHora % 102) + (aHora % 402) + (aHora % 702);

     |DFICE aPath;
     aAbre = aPath + aFicCSV + ".ent";
     |XABRE "WB", aAbre, nHandle;

     aTab = &9;

     aAlfa = "Empresa" + aTab;
     aAlfa = aAlfa  + "Identificador" + aTab;
     aAlfa = aAlfa  + "Servidor" + aTab;
     aAlfa = aAlfa  + "Usuario" + aTab;
     aAlfa = aAlfa  + "Peticion" + &13 + &10;
     |XGRABA nHandle, aAlfa;

     aAlfa = #gecom002#0;                     |QBF aAlfa;
     aAlfa = aAlfa + aTab + #gecom002#1;      |QBF aAlfa;
     aAlfa = aAlfa + aTab + aSer;             |QBF aAlfa;
     aAlfa = aAlfa + aTab + (Usuario % 810);  |QBF aAlfa;
     aAlfa = aAlfa + aTab + "ConsultarFactura";
     aAlfa = aAlfa + &13 + &10;
     |XGRABA nHandle, aAlfa;

     |XCIERRA nHandle;

     aAlfa = "gecoz008;" + aFicCSV;
     |SUB_EJECUTA aAlfa;

     |SI eaError ! "";
         |MENAV eaError;
     |FINSI;

     |DFICE aPath;
     aFic = aPath + aFicCSV + ".sal";
     |FBORRA aFic;

     aFic = aPath + aFicCSV + ".ent";
     |FBORRA aFic;
|FPRC;

|PROCESO PetXML;
     |CONFI "0000SConsultamos el estado de la factura seleccionada.";
     |SI FSalida ! 0;
         |FOCOTECLADO enGrid2;
         |ACABA;
     |FINSI;

     |HAZ PideEstado;

     |SI eaError ! "";
         |MENAV eaError;
     |FINSI;
|FPRC;

|PROCESO Baja14;
     #gecom014#0  = #gecom002#0;
     #gecom014#1  = #gecom002#1;
     #gecom014#2  = "31.12.9999";
     #gecom014#3  = "zzzzzzzz";
|FPRC;

|PROCESO Alta14;
     #gecom014#0  = #gecom002#0;
     #gecom014#1  = #gecom002#1;
     #gecom014#2  = "";
     #gecom014#3  = "";
|FPRC;

|PROCESO Seguim;
     |ABRE #gecom014;
     |CONSULTA_F_CLAVE #2#gecom014, Baja14, Alta14;
     |CIERRA #gecom014;
|FPRC;

|PROCESO BtnOpc3;
     eaIde    = #gecom002#1;

     aPopup1  = enBtnOpc3;

     |SI eaSit = "P";  |O eaSit = "RP";
         aPopup2  = 4;
         aPopup3  = "Ver XML";
         aPopup4  = "Importar XML";
         aPopup5  = "Enviar XML";
         aPopup6  = "Segumiento factura";
     |FINSI;

     |SI eaSit = "X";  |O eaSit = "RX";
         aPopup2  = 3;
         aPopup3  = "Ver XML";
         aPopup4  = "Enviar XML";
         aPopup5  = "Segumiento factura";
     |FINSI;

     |SI eaSit = "E";  |O eaSit = "RE";
         aPopup2  = 3;
         aPopup3  = "Ver XML";
         aPopup4  = "Peticion de estado ";
         aPopup5  = "Segumiento factura";
     |FINSI;

     |SI eaSit = "R";  |O eaSit = "RR";
         aPopup2  = 3;
         aPopup3  = "Ver XML";
         aPopup4  = "Recibir XML";
         aPopup5  = "Segumiento factura";
     |FINSI;

     |SI eaSit = "A";  |O eaSit = "RA";
         aPopup2  = 2;
         aPopup3  = "Ver XML";
         aPopup4  = "Segumiento factura";
     |FINSI;

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
         IaPopup  = aPopup3  <-;
         IaPopup  = IaPopup + (FSalida - 1);

         aAlfa = aPopup % 103;

         |SI aAlfa = "Ver";  |HAZ VerXML;  |FINSI;
         |SI aAlfa = "Env";  |HAZ EnvXML;  |FINSI;
         |SI aAlfa = "Imp";  |HAZ RecXML;  |FINSI;
         |SI aAlfa = "Rec";  |HAZ RecXML;  |FINSI;
         |SI aAlfa = "Pet";  |HAZ PetXML;  |FINSI;
         |SI aAlfa = "Seg";  |HAZ Seguim;  |FINSI;
     |FINSI;

     |REFRESCACONTROL enGrid2;
     |FOCOTECLADO enGrid2;
|FPRC;

|PROCESO DibujaPanta;
     |HAZ PonLabel;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Consulta", 3586, 3598, BtnCon3;
     enBtnCon3  = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Modificar", 3574, 3584, BtnMod3;
     enBtnMod3 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{199}}Borrar", 3562, 3572, BtnBor3;
     enBtnBor3 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Opciones", 3502, 3515, BtnOpc3;
     enBtnOpc3 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Anular", 3532, 3542, BtnAnu3;
     enBtnAnu3 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Rectificar", 3544, 3556, BtnRec3;
     enBtnRec3 = FSalida;

     |ESTADO_CONTROL enBtnCon3, "DISABLE";
     |ESTADO_CONTROL enBtnMod3, "DISABLE";
     |ESTADO_CONTROL enBtnBor3, "DISABLE";
     |ESTADO_CONTROL enBtnAnu3, "DISABLE";
     |ESTADO_CONTROL enBtnRec3, "DISABLE";

     nRango   = 2 + 4 + 8 + 16 + 32 + 128;
     |LINEAL_SIMPLE #2#gecom002, nRango, 1702, 3498, Baja2, Alta2, Evnt2;
     enGrid2    = FSalida;
|FPRC;

|PROCESO EvntTree;
     |SI FEntrada ! 0;  |ACABA;  |FINSI;

     eaSit = Contenido % -102;  |QBF eaSit;

     |SI enGrid2 ! -1;
         |DESTRUYECONTROL enGrid2;
         |FIN_CONTROL_WINDOWS nLabel;
         |FIN_CONTROL_WINDOWS enBtnMod3;
         |FIN_CONTROL_WINDOWS enBtnCon3;
         |FIN_CONTROL_WINDOWS enBtnBor3;
         |FIN_CONTROL_WINDOWS enBtnOpc3;
         |FIN_CONTROL_WINDOWS enBtnAnu3;
         |FIN_CONTROL_WINDOWS enBtnRec3;

         |PULSATECLA;
     |FINSI;

     |SI eaSit = "";    |ACABA;  |FINSI;
     |SI eaSit = "0:";  |ACABA;  |FINSI;

     |HAZ DibujaPanta;
|FPRC;

|PROCESO GrabaLinea;
     aAlfa = aAlfa + &13 + &10;
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO SacaTree;
     |PTEC 802;  |PAUSA;

     aFicTxt  = "gecoz00201" + Usuario + ".txt";
     aAbre    = eaPathTmpLocal + aFicTxt;

     sT_nID   = nTree;
     sT_nOper = 0;

     |XABRE "WBC", aAbre, nHandle;

     aAlfa   = "1:Pendientes {{P }}";     |HAZ GrabaLinea;
     aAlfa   = "2:Rechazadas {{X }}";     |HAZ GrabaLinea;
     aAlfa   = "3:Enviadas {{E }}";       |HAZ GrabaLinea;
     aAlfa   = "4:Recibidas {{R }}";      |HAZ GrabaLinea;
     aAlfa   = "5:Anuladas {{A }}";       |HAZ GrabaLinea;

     aAlfa   = "6:Rectificativas {{  }}";               |HAZ GrabaLinea;
     aAlfa   = "6,1:Rectificativas Pendientes {{RP}}";  |HAZ GrabaLinea;
     aAlfa   = "6,2:Rectificativas Rechazadas {{RX}}";  |HAZ GrabaLinea;
     aAlfa   = "6,3:Rectificativas Enviadas {{RE}}";    |HAZ GrabaLinea;
     aAlfa   = "6,4:Rectificativas Recibidas {{RR}}";   |HAZ GrabaLinea;
     aAlfa   = "6,5:Rectificativas Anuladas {{RA}}";    |HAZ GrabaLinea;

     |XCIERRA nHandle;

     aAlfa = "TREECTRL,Arbol de facturas {{" + aAbre + "}}";
     |CONTROL_SIMPLE 0, aAlfa, 0502, 1540, EvntTree;
     nTree = FSalida;

     aFicTxt   = "gecoz00201" + Usuario + ".txt";
     aAlfa     = eaPathTmpLocal + aFicTxt;
     |RFBORRA aAlfa;

     |SI aBuscar ! "";
         sT_nID   = nTree;
         sT_nOper = 3;
         sT_aData = "[" + aBuscar;
         |ESPECIFICA "COMMANDCONTROL", sTree;
         |SI FSalida ] 0;
             aRefresca = ":" + FSalida;
         |SINO;
             aRefresca = "1:";
         |FINSI;
     |FINSI;

     sT_nID   = nTree;
     sT_nOper = 2;
     |SI aRefresca = "";
         sT_aData = "1:";
     |SINO;
         sT_aData = aRefresca;
     |FINSI;
     |ESPECIFICA "COMMANDCONTROL", sTree;

     |FOCOTECLADO nTree;
|FPRC;

|PROCESO Recalculo;
     #gecom002#0 = enEmp;
     #gecom002#1 = eaIde;
     |LEE 000#gecom002.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     #gecom003#0 = enEmp;
     #gecom003#1 = eaIde;
     |LEE 000#gecom003.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |DDEFECTO #gecow003;

     |PARA nCmp = 0;  |SI nCmp [ enTCmp3;  |HACIENDO nCmp = nCmp + 1;
           #gecow003#nCmp#  = #gecom003#nCmp#;
     |SIGUE;

     |HAZ Totalw003;

     |PARA nCmp = 0;  |SI nCmp [ enTCmp3;  |HACIENDO nCmp = nCmp + 1;
           #gecom003#nCmp#  = #gecow003#nCmp#;
     |SIGUE;

     |GRABA 020#gecom003.a;
     |LIBERA #gecom003;
|FPRC;

|PROGRAMA;
     |SI PARAMETRO = "BORRARESTOS";
         |HAZ BorraRestos;
         |ACABA;
     |FINSI;

     |SI PARAMETRO = "RECALCULO";
         |HAZ Recalculo;
         |ACABA;
     |FINSI;

     aAlfa = "FacturaeB2B (Empresa " + (("00000" + #gecom001#0) % -105);
     aAlfa = aAlfa  + " " + #gecom001#1;  |QBF aAlfa;
     aAlfa = aAlfa  + ")";
     |VENTANA 0501, 3799, -1, 16, aAlfa;
     n0Vent = FSalida;

     |HAZ SacaTree;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Generar nueva factura", 0675, 0798, NuevaFactura;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::{{001}}Salir," + nTree;
          |LEETECLA aTecla;
          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
          |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FINVENTANA n0Vent;

     |PULSATECLA;
|FPRO;

|PROCESO T80;  |TIPO 80;
     |HAZ LeeUnidadBasico;

     eaPathTmpLocal = eaUnidadBasico + "\documentos";   |RMKDIR eaPathTmpLocal;
     eaPathTmpLocal = eaPathTmpLocal + "\gecobase";     |RMKDIR eaPathTmpLocal;
     eaPathTmpLocal = eaPathTmpLocal + "\";

     |DBASE aBase;
     eaPathAdj = aBase + "documentos";                         |MKDIR eaPathAdj;
     eaPathAdj = eaPathAdj + "/adjuntos";                      |MKDIR eaPathAdj;
     eaPathAdj = eaPathAdj + "/" + (("00000" + enEmp) % -105); |MKDIR eaPathAdj;
     eaPathAdj = eaPathAdj + "/";

     eaPathPlu = aBase + "plugins/";
     fFechaSys = ~;

     eaIP      = "";
     |IP_REMOTO eaIP;

     aQQ      = "|NC";
     aAlfa    = #gecom051#aQQ#;
     enTCmp51 = aAlfa;
     enTCmp51 = enTCmp51 - 1;

     aQQ      = "|NC";
     aAlfa    = #gecom003#aQQ#;
     enTCmp3  = aAlfa;
     enTCmp3  = enTCmp3 - 1;

     aQQ      = "|NC";
     aAlfa    = #gecom005#aQQ#;
     enTCmp5  = aAlfa;
     enTCmp5  = enTCmp5 - 1;

     aQQ      = "|NC";
     aAlfa    = #gecom011#aQQ#;
     enTCmp11 = aAlfa;
     enTCmp11 = enTCmp11 - 1;

     aQQ      = "|NC";
     aAlfa    = #gecom012#aQQ#;
     enTCmp12 = aAlfa;
     enTCmp12 = enTCmp12 - 1;

     |ABRE #gecom001;
     |SELECT #1#gecom001;
     #gecom001#0 = enEmp;
     |LEE 000#gecom001.=;
     |CIERRA #gecom001;

     |ABRET #gecom002;
     |ABRET #gecom003;
     |ABRET #gecom004;
     |ABRET #gecom005;
     |ABRET #gecom006;
     |ABRET #gecom007;
     |ABRET #gecom008;
     |ABRET #gecom009;
     |ABRET #gecom010;
     |ABRET #gecom011;
     |ABRET #gecom012;
     |ABRET #gecom013;
     |ABRET #gecom051;
|FPRC;

|PROCESO T90;  |TIPO 90;
     |CIERRAT #gecom002;
     |CIERRAT #gecom003;
     |CIERRAT #gecom004;
     |CIERRAT #gecom005;
     |CIERRAT #gecom006;
     |CIERRAT #gecom007;
     |CIERRAT #gecom008;
     |CIERRAT #gecom009;
     |CIERRAT #gecom010;
     |CIERRAT #gecom011;
     |CIERRAT #gecom012;
     |CIERRAT #gecom013;
     |CIERRAT #gecom051;
|FPRC;
