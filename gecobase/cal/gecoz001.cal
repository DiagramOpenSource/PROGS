|FICHEROS;
     gecom000;
     gecom001;

     agifigen@;
|FIN;

|VARIABLES;
     n0Vent              = 0;
     n1Vent              = 0;
     n2Vent              = 0;
     nGrid1              = -1;
     nGrid2              = -1;
     nTree               = -1;

     nBtnBox             = -1;
     nBtnUtil            = -1;
     nBtnEmp             = -1;
     nBtnDesa            = -1;
     nBtnExc             = -1;
     nBtnCon             = -1;
     nBtnEnv             = -1;

     nBtnAcce            = 0;
     nBtnNLot            = 0;
     nBtnBLot            = 0;
     nBtnFact            = 0;
     nBtnEstd            = 0;
     nRango              = 0;
     nEje                = 0;
     nPer                = 0;
     nLot                = 0;
     nOk                 = 0;
     nLong               = 0;
     nCarac              = 0;
     nPos                = 0;
     nHandle             = 0;
     nPtec               = 1;
     nSeg                = 0;
     nOpcion             = 0;
     nFSalida            = 0;
     nCmp                = 0;

     aEsc1               = "";
     aEsc2               = "";
     aRetu               = "";
     aTecla              = "";
     aAlfa               = "";
     aEjecuta            = "";
     aOri                = "";
     aDes                = "";
     aBaseGeco           = "";
     aBaseAplis          = "";
     aBase               = "";
     aPath               = "";
     aVer1               = "";
     aVer2               = "";
     aLong               = "";
     aCaracter           = "";
     aLee                = "";
     aMas                = "";
     aTipoLbl            = "";
     aConten             = "";
     aTo                 = "";
     aBaseMensaje        = "";
     aFrom               = "";
     aAsunto             = "";
     aAbreTXT            = "";
     aIP                 = "";
     aSalida             = "";
     aMensaje            = "";
     aHora               = "";
     aFecha              = "";
     aFicTmp             = "";
     aContenido          = "";
     aAbre               = "";
     aOpcion             = "";

     fFecha              = @;

  {1}aConte              = "";

     {-1}sTree           = 0;
         sT_nID          = 0;
         sT_nOper        = 0;
         sT_aData        = "";

     {-1}aVent           = "";
         aVent1          = 0;
         aVent2          = 0;
         aVent3          = 0;
         aVent4          = 0;
         aVent5          = "";

     &enEmp              = 0;
     &enEje              = 0;
     &enPer              = 0;
     &enLot              = 0;
     &enReg              = 0;
     &eaUnidadBasico     = "";
     &eaUrlSB            = "";
     &eaUrlAP            = "";
     &eaIni              = "";
     &eaPython           = "";

     &eaModoNet          = "";
     &eaUsuNet           = "";
     &eaBaseNet          = "";
     &eaFechaNet         = "";
     &eaEmpNet           = "";
     &enCodCli           = 0;
|FIN;

|PROCESO GrabaCuerpo;
     aConten = "USUARIO REALIZACION: " + eaUsuNet + &13 + &10;
     |XGRABA nHandle, aConten;

     aConten = "CARPETA: " + eaBaseNet + &13 + &10;
     |XGRABA nHandle, aConten;

     aConten = "EMPRESA: " + eaEmpNet + &13 + &10;
     |XGRABA nHandle, aConten;
|FPRC;

|PROCESO EnviaNotificacion;
     aTo = #gecom000#4;
     |QBF aTo;

     |SI aTo = "";
         |ACABA;
     |FINSI;

     |VENTANA 1610, 2080, -1, 16, "Por favor Espere";
     n2Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n2Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802; |PAUSA;

     |DBASS eaBaseNet;

     |DBASE aBaseMensaje;  |QBF aBase;
     aBaseMensaje = aBaseMensaje + "tmp";   |MKDIR aBaseMensaje;
     aBaseMensaje = aBaseMensaje + "/";
     aFrom   = "no_reply@diagram.es";  |QBF aFrom;
     aAsunto = "GECOBOX: " + eaModoNet + " (" + eaBaseNet + " - " + eaFechaNet + ")";

     aAbreTXT = aBaseMensaje + "adjunto.txt";
     |XABRE "WB", aAbreTXT, nHandle;
     |HAZ GrabaCuerpo;
     |XCIERRA nHandle;

     aIP      = "xdscorreo.dv.diagram.net";
     aSalida  = "asmtp.diagram.es";

     aMensaje = "$$$$" + aAbreTXT;

     |DSCORREO_ENVIA aIP, aSalida, aFrom, aTo, aAsunto, aMensaje, aAbreTXT;
     |SI FSalida < 0;
         |MENAV "0000 Ha habido un error durante el envio.";
     |FINSI;

     |FBORRA aAbreTXT;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n2Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n2Vent;
|FPRC;

|PROCESO Baja1;
     #gecom001#4 = "S";
     #gecom001#0 = 00001;
|FPRC;

|PROCESO Alta1;
     #gecom001#4 = "S";
     #gecom001#0 = 99999;
|FPRC;

|PROCESO Evnt1;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#gecom001.=;

         |ESTADO_CONTROL nBtnDesa, "ENABLE";
     |FINSI;

     |SI FSalida = 4;
         |VENTANA 0501, 3699, -1, 0, "";
         n2Vent = FSalida;

         |PTEC 802;  |PAUSA;

         enEmp = #gecom001#0;
         |SUB_EJECUTA "gecoz002";

         |FINVENTANA n2Vent;
     |FINSI;
|FPRC;

|PROCESO AccesoGecoBOX;
     enEmp = #gecom001#0;
     |SUB_EJECUTA "gecoz050";

     |FOCOTECLADO nGrid1;
|FPRC;

|PROCESO ChequeaSel;
     #gecom001#0 = #agifigen@#0;
     |LEE 000#gecom001.=;
     |SI FSalida = 0;
         |SI #gecom001#4 = "S";
             |MENAV "0000La empresa ya est  en la bandeja";
             |ACABA;
         |FINSI;

         |CONFI "0000SLa empresa est  desactivada, quiere activarla de nuevo";
         |SI FSalida ! 0;
             |ACABA;
         |FINSI;

         #gecom001#4 = "S";
         |GRABA 020#gecom001.a;
         |LIBERA #gecom001;

         |HORA aHora;

         eaModoNet     = "Habilitado nuevo acceso GECOBOX";
         eaUsuNet      = Usuario % 810; |QBF eaUsuNet;
         fFecha        = ~;
         aFecha        = fFecha;
         eaFechaNet    = aFecha + " " + aHora;
         eaEmpNet      = ("00000" + #gecom001#0) % -105;
         eaEmpNet      = eaEmpNet + " / " + #gecom001#1;   |QBF eaEmpNet;
         eaEmpNet      = eaEmpNet + " / " + #gecom001#2;   |QBF eaEmpNet;

         |HAZ EnviaNotificacion;

         |ACABA;
     |FINSI;

     |CONFI "0000SQuiere agregar la empresa a la bandeja";
     |SI FSalida = 0;
         |DDEFECTO #gecom001;
         #gecom001#0 = #agifigen@#0;
         #gecom001#1 = #agifigen@#1;
         #gecom001#2 = #agifigen@#13;
         #gecom001#4 = "S";
         |GRABA 020#gecom001.n;
         |LIBERA #gecom001;

         |HORA aHora;

         eaModoNet     = "Habilitado nuevo acceso GECOBOX";
         eaUsuNet      = Usuario % 810; |QBF eaUsuNet;
         fFecha        = ~;
         aFecha        = fFecha;
         eaFechaNet    = aFecha + " " + aHora;
         eaEmpNet      = ("00000" + #gecom001#0) % -105;
         eaEmpNet      = eaEmpNet + " / " + #gecom001#1;   |QBF eaEmpNet;
         eaEmpNet      = eaEmpNet + " / " + #gecom001#2;   |QBF eaEmpNet;

         |HAZ EnviaNotificacion;
     |FINSI;
|FPRC;

|PROCESO NuevaEmp;
     aMas = aBaseAplis + "basica/def/agifigen.mas";
     |CARGA_DEF aMas, agifigen@;

     |ABRE #agifigen@;
     |CONSULTA_CLAVE #1#agifigen@;
     |SI FSalida = 0;
         |SELECT #1#gecom001;
         |HAZ ChequeaSel;
         |SELECT #3#gecom001;

         |LEE 000#gecom001.=;
     |FINSI;
     |CIERRA #agifigen@;

     |DESCARGA_DEF #agifigen@;

     |REFRESCACONTROL nGrid1;
     |FOCOTECLADO nGrid1;
|FPRC;

|PROCESO DesactivarEmp;
     |CONFI "0000NEst  seguro que quiere desactivar la empresa";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SELECT #1#gecom001;
     |LEE 101#gecom001.=;
     |SI FSalida = 0;
         #gecom001#4 = "N";
         |GRABA 020#gecom001.a;
         |LIBERA #gecom001;

         eaModoNet     = "Acceso GECOBOX quitado ";
         eaUsuNet      = Usuario % 810; |QBF eaUsuNet;
         fFecha        = ~;
         aFecha        = fFecha;
         eaFechaNet    = aFecha + " " + aHora;
         eaEmpNet      = ("00000" + #gecom001#0) % -105;
         eaEmpNet      = eaEmpNet + " / " + #gecom001#1;   |QBF eaEmpNet;
         eaEmpNet      = eaEmpNet + " / " + #gecom001#2;   |QBF eaEmpNet;
         |HAZ EnviaNotificacion;
     |FINSI;

     |SELECT #3#gecom001;
     |LEE 000#gecom001.=;

     |REFRESCACONTROL nGrid1;
     |FOCOTECLADO nGrid1;
|FPRC;

|PROCESO Alta;
     #agifigen@#0 = enCodCli;
     |LEE 000#agifigen@.=;
     |SI FSalida ! 0;
         |ACABA;
     |FINSI;

     #gecom001#0 = #agifigen@#0;
     |LEE 000#gecom001.=;
     |SI FSalida = 0;
         |SI #gecom001#4 = "S";
             |ACABA;
         |FINSI;

         #gecom001#4 = "S";
         |GRABA 020#gecom001.a;
         |LIBERA #gecom001;
     |SINO;
         |DDEFECTO #gecom001;
         #gecom001#0 = #agifigen@#0;
         #gecom001#1 = #agifigen@#1;
         #gecom001#2 = #agifigen@#13;
         #gecom001#4 = "S";
         |GRABA 020#gecom001.n;
         |LIBERA #gecom001;
     |FINSI;
|FPRC;

|PROGRAMA;
     |SI PARAMETRO = "ALTA";
         aMas = aBaseAplis + "basica/def/agifigen.mas";

         |CARGA_DEF aMas, agifigen@;
         |ABRE #agifigen@;

         |HAZ Alta;

         |CIERRA #agifigen@;
         |DESCARGA_DEF #agifigen@;

         |ACABA;
     |FINSI;

     |VENTANA 0501, 3599, -1, 16, "Bandeja control facturaeB2B";
     n0Vent = FSalida;

     |CONTROL_SIMPLE 0, "LABEL,{{15}}Empresas", 0502, 0520, NULL;
     |CONTROL_SIMPLE 0, "LABEL,{{11}}Doble click para entrar en la gesti¢n facturae de la empresa", 0547, 0598, NULL;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Definir accesos GECOBOX", 3302, 3323, AccesoGecoBOX;
     nBtnBox  = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Nueva empresa", 3380, 3398, NuevaEmp;
     nBtnEmp  = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Desactivar empresa", 3360, 3377, DesactivarEmp;
     nBtnDesa = FSalida;

     |ESTADO_CONTROL nBtnDesa, "DISABLE";

     nRango   = 2 + 4 + 8 + 16 + 32 + 128;
     |LINEAL_SIMPLE #3#gecom001, nRango, 0602, 3298, Baja1, Alta1, Evnt1;
     nGrid1 = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::{{001}}Salir," + nGrid1;
          |LEETECLA aTecla;
          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
          |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FINVENTANA n0Vent;

     |PULSATECLA;
|FPRO;

|PROCESO T80;  |TIPO 80;
     |DBASS aBaseAplis;  |QBF aBaseAplis;
     |DBASE aBaseGeco;

     |ABRE #gecom000;
     |LEE 000#gecom000.p;
     |CIERRA #gecom000;

     |ABRET #gecom001;

     |HAZ LeeUnidadBasico;

     aIP        = "";
     |IP_REMOTO aIP;
|FPRC;

|PROCESO T90;  |TIPO 90;
     |CIERRAT #gecom001;
|FPRC;
