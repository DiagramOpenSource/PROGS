|FICHEROS;
     gecow003;
     gecow006,MANTE;

     gecom012;
|FIN;

|VARIABLES;
     n0Vent              = 0;
     nPan6               = 0;
     nId                 = 0;
     nCmp                = 0;
     nOk                 = 0;
     nHandle             = 0;
     nLong               = 0;
     nCarac              = 0;
     nPos                = 0;
     nPausa              = 0;

     aAlfa               = "";
     aAlfa1              = "";
     aAlfa2              = "";
     aAlfa3              = "";
     aVer1               = "";
     aVer2               = "";
     aOri                = "";
     aDes                = "";
     aProg               = "";
     aAbre               = "";
     aLinea              = "";
     aRuta               = "";
     aFicPDF             = "";
     aFicB64             = "";
     aFicBrowse          = "";
     aFicLCK             = "";
     aLong               = "";
     aCaracter           = "";
     aApi                = "";
     aParam1             = "";
     aParam2             = "";
     aEjec               = "";

     {1}aMd5             = "";

     {-1}aVent           = "";
         aVent1          = 0;
         aVent2          = 0;
         aVent3          = 0;
         aVent4          = 0;
         aVent5          = "";

     &enGrid12           = 0;
     &enTCmp12           = 0;
     &eaPathTmpLocal     = "";
     &eaPathAdj          = "";
     &eaPathPlu          = "";
     &eaIP               = "";
     &eaPython           = "";
     &enErrorPy          = 0;
|FIN;

|PROCESO QuitaBarras;
     |PARA; |SI;  |HACIENDO;
          aAlfa = aAlfa - "/";
          |SI FSalida = 0;  |SAL;  |FINSI;
     |SIGUE;

     |PARA; |SI;  |HACIENDO;
          aAlfa = aAlfa - "\";
          |SI FSalida = 0;  |SAL;  |FINSI;
     |SIGUE;
|FPRC;

|PROCESO BorraAdjuntos;
     aFicPDF  = (("00000" + #gecom012#0) % -105);
     aFicPDF  = aFicPDF + #gecom012#1;  |QBF aFicPDF;
     aFicPDF  = aFicPDF + (("00" + #gecom012#2) % -102);
     aFicPDF  = aFicPDF + "." + #gecom012#3;   |QBF aFicPDF;

     aAlfa = aFicPDF;  |HAZ QuitaBarras;

     aFicPDF  = eaPathAdj + aAlfa
     |FBORRA aFicPDF;

     aFicB64  = (("00000" + #gecom012#0) % -105);
     aFicB64  = aFicB64 + #gecom012#1;  |QBF aFicB64;
     aFicB64  = aFicB64 + (("00" + #gecom012#2) % -102);
     aFicB64  = aFicB64 + ".b64";

     aAlfa = aFicB64;  |HAZ QuitaBarras;

     aFicB64  = eaPathAdj + aAlfa;
     |FBORRA aFicB64;
|FPRC;

|PROCESO PonDsabreextension;
     aVer1 = "";
     aVer2 = "";
     aMd5  = eaPathPlu + "dsabreextension.exe";
     |ESPECIFICA "MD5", aMd5;
     aVer1 = FSalida;

     aMd5 = eaPathTmpLocal + "dsabreextension.exe";
     |ESPECIFICA "REMOTOMD5", aMd5;
     aVer2 = FSalida;

     |SI aVer1 ! aVer2;
         aOri = eaPathPlu + "dsabreextension.exe";
         aDes = eaPathTmpLocal + "dsabreextension.exe";

         |SI eaIP = "";
             |COPIA_FICHERO aOri, aDes;
         |SINO;
             |ENVIA_FICHERO aOri, aDes;
         |FINSI;

         |XABRE "EC", aDes, 0;
         |SI FSalida < 0;
             |MENAV "0000No encuentro el programa para abrir documentos, por favor consulte con su administrador informatico.";
             |ACABA;
         |FINSI;
     |FINSI;

     nOk = 1;
|FPRC;

|PROCESO PonDsselect;
     aVer1 = "";
     aVer2 = "";
     aMd5  = eaPathPlu + "dsselect.exe";
     |ESPECIFICA "MD5", aMd5;
     aVer1 = FSalida;

     aMd5 = eaPathTmpLocal + "dsselect.exe";
     |ESPECIFICA "REMOTOMD5", aMd5;
     aVer2 = FSalida;

     |SI aVer1 ! aVer2;
         aOri = eaPathPlu + "dsselect.exe";
         aDes = eaPathTmpLocal + "dsselect.exe";

         |SI eaIP = "";
             |COPIA_FICHERO aOri, aDes;
         |SINO;
             |ENVIA_FICHERO aOri, aDes;
         |FINSI;

         |XABRE "EC", aDes, 0;
         |SI FSalida < 0;
             |MENAV "0000No encuentro el explorador de ficheros, por favor consulte con su administrador informatico.";
             |ACABA;
         |FINSI;
     |FINSI;

     nOk = 1;
|FPRC;

|PROCESO TraeDoc;
     |MENAV "0000A continuaci¢n busque y seleccione el fichero a importar";

     nOk = 0;
     |HAZ PonDsselect;
     |SI nOk = 0;  |ACABA;  |FINSI;

     aProg = eaPathTmpLocal + "dsselect.exe " + eaPathTmpLocal + "dsselect.ini";

     aAlfa = eaPathTmpLocal + "dsselect.txt";  |RFBORRA aAlfa;
     aAlfa = eaPathTmpLocal + "dsselect.ini";  |RFBORRA aAlfa;

     aAbre = eaPathTmpLocal + "dsselect.ini";
     |XABRE "CW", aAbre, nHandle;
     |SI FSalida ] 0;
          aLinea = &34 + eaPathTmpLocal + &34 + " " + &34 + eaPathTmpLocal + "dsselect.txt" + &34;
          |XGRABA nHandle, aLinea;
     |FINSI;
     |XCIERRA nHandle;

     |RSYSTEM aProg;
     |PULSATECLA;

     aRuta = "";
     aAbre = eaPathTmpLocal + "dsselect.txt";
     |XABRE "CU", aAbre, nHandle;
     |SI FSalida ] 0;
         |XLEE nHandle, 250, aRuta;
     |FINSI;
     |XCIERRA nHandle;

     aAlfa = eaPathTmpLocal + "dsselect.txt";
     |RFBORRA aAlfa;

     |PULSATECLA;

     |QBF aRuta;
     |SI aRuta = "";
         nOk = 0;
         |ACABA;
     |FINSI;

     aAlfa      = aRuta;
     aFicBrowse = "";
     aFicPDF    = "";
     aLong      = aAlfa % 0;
     nLong      = aLong;
     |PARA nCarac = 1;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos       = (nCarac * 100) + 1;
           aCaracter  = aAlfa % nPos;
           aFicBrowse = aFicBrowse + aCaracter;
           aFicPDF    = aFicPDF + aCaracter;

           |SI aCaracter = "/";  |O aCaracter = "\";
               aFicPDF = "";
           |FINSI;
     |SIGUE;

     aDes  = (("00000" + #gecow006#0) % -105);
     aDes  = aDes + #gecow006#1;  |QBF aDes;
     aDes  = aDes + (("00" + #gecow006#2) % -102);
     aDes  = aDes + ".b64";   |QBF aDes;

     aAlfa = aDes;  |HAZ QuitaBarras;
     aDes  = eaPathAdj + aAlfa;
     |FBORRA aDes;

     aDes  = (("00000" + #gecow006#0) % -105);
     aDes  = aDes + #gecow006#1;  |QBF aDes;
     aDes  = aDes + (("00" + #gecow006#2) % -102);
     aDes  = aDes + "." + #gecow006#3;   |QBF aDes;

     aAlfa = aDes;  |HAZ QuitaBarras;
     aDes  = eaPathAdj + aAlfa;

     |FBORRA aDes;

     aFicPDF = ("." + #gecow006#3) > "";  |QBF aFicPDF;
     aAlfa = aFicBrowse > "";
     aAlfa = aAlfa - aFicPDF;
     |SI FSalida = 0;
         aAlfa = "0000El fichero deber  tener extesi¢n " + aFicPDF;
         |MENAV aAlfa;

         |ACABA;
     |FINSI;

     aOri  = aFicBrowse;

     |SI eaIP = "";
         |COPIA_FICHERO aOri, aDes;
     |SINO;
         |RECIBE_FICHERO aOri, aDes;
     |FINSI;

     |HAZ VersionPython;
     |SI enErrorPy = 1;
         |ACABA;
     |FINSI;

     aApi    = eaPathPlu + "file2base64.py";
     aParam1 = aDes;
     aParam2 = (("00000" + #gecow006#0) % -105);
     aParam2 = aParam2 + #gecow006#1;  |QBF aParam2;
     aParam2 = aParam2 + (("00" + #gecow006#2) % -102);
     aParam2 = aParam2 + ".b64";

     aAlfa = aParam2;  |HAZ QuitaBarras;
     aParam2 = eaPathAdj + aAlfa;

     aEjec = eaPython + " " + aApi + " " + aParam1 + " " + aParam2;
     |SYSTEM aEjec;

     |XABRE "E", aParam2, 0;
     |SI FSalida < 0;
         |MENAV "0000No se ha podido convertir el PDF a B64, consulte con su administrador";

         |FBORRA aParam1;
     |FINSI;
|FPRC;

|PROCESO VerDoc;
     nOk = 0;
     |HAZ PonDsabreextension;
     |SI nOk = 0;  |ACABA;  |FINSI;

     aFicPDF  = (("00000" + #gecow006#0) % -105);
     aFicPDF  = aFicPDF + #gecow006#1;  |QBF aFicPDF;
     aFicPDF  = aFicPDF + (("00" + #gecow006#2) % -102);
     aFicPDF  = aFicPDF + "." + #gecow006#3;   |QBF aFicPDF;

     aAlfa = aFicPDF;  |HAZ QuitaBarras;  aFicPDF = aAlfa;

     aOri  = eaPathAdj + aFicPDF;
     aDes  = eaPathTmpLocal + aFicPDF;
     |SI eaIP = "";
         |COPIA_FICHERO aOri, aDes;
     |SINO;
         |ENVIA_FICHERO aOri, aDes;
     |FINSI;

     aFicLCK = eaPathTmpLocal + Usuario + ".txt";
     aProg   = eaPathTmpLocal + "dsabreextension.exe";

     aAlfa  = "START " + aProg + " " + aDes + " " + aFicLCK;
     |RSYSTEM aAlfa;

     |PARA;  |SI;  |HACIENDO;
          |PARA nPausa = 1; |SI nPausa [ 10000; |HACIENDO nPausa = nPausa + 1;
          |SIGUE;

          |XABRE "EC", aFicLCK, nHandle;
          |SI FSalida ] 0;
              |SAL;
          |FINSI;

          |PULSATECLA;
     |SIGUE;

     |RFBORRA aFicLCK;
     |RFBORRA aDes;
|FPRC;

|PROCESO T12w006;  |TIPO 12;
     |SI FSalida = 7;
         |ACABA;
     |FINSI;

     aAlfa = #gecow006#4;  |QBF aAlfa;
     |SI aAlfa = "";
         |MENAV "0000Campo descripci¢n es obligatorio";
         |POSICIONATE #gecow006#4;
         |ERROR;
         |ACABA;
     |FINSI;

     aDes  = (("00000" + #gecow006#0) % -105);
     aDes  = aDes + #gecow006#1;  |QBF aDes;
     aDes  = aDes + (("00" + #gecow006#2) % -102);
     aDes  = aDes + "." + #gecow006#3;   |QBF aDes;

     aAlfa = aDes;  |HAZ QuitaBarras;
     aDes  = eaPathAdj + aAlfa;

     |XABRE "E", aDes, 0;
     |SI FSalida < 0;
         |MENAV "0000Adjunte un documento";
         |POSICIONATE #gecow006#4;
         |ERROR;
         |ACABA;
     |FINSI;
|FPRC;

|PROCESO AltAdjunto;
     |VENTANA 0501, 1299, -1, 16, "Nuevo adjunto";
     n0Vent = FSalida;

     |PTEC 802; |PAUSA;

     |PINPA #0#gecow006;  nPan6 = FSalida;

     |CONTROL_SIMPLE nPan6, "BOTON,{{136}}", 1003, 1209, TraeDoc;

     |DDEFECTO #gecow006;

     #gecom012#0 = #gecow003#0;
     #gecom012#1 = #gecow003#1;
     #gecom012#2 = 99;
     |LEE 000#gecom012.];
     |SI FSalida = 0;
         |LEE 000#gecom012.a;
     |SINO;
         |LEE 000#gecom012.u;
     |FINSI;

     nId = 1;
     |SI FSalida = 0;  |Y #gecom012#0 = #gecow003#0;
                       |Y #gecom012#1 = #gecow003#1;
         nId = #gecom012#2 + 1;
     |FINSI;

     #gecow006#0 = #gecow003#0;
     #gecow006#1 = #gecow003#1;
     #gecow006#2 = nId;

     |PINDA #0#gecow006;
     |ENDATOS #1#gecow006;
     |SI FSalida = 0;
         |PARA nCmp = 0;  |SI nCmp [ enTCmp12;  |HACIENDO nCmp = nCmp + 1;
               #gecom012#nCmp#  = #gecow006#nCmp#;
         |SIGUE;

         |GRABA 020#gecom012.n;
         |LIBERA #gecom012;
     |SINO;
         #gecom012#0 = #gecow006#0;
         #gecom012#1 = #gecow006#1;
         #gecom012#2 = #gecow006#2;
         #gecom012#3 = #gecow006#3;

         |HAZ BorraAdjuntos;
     |FINSI;

     |FINVENTANA n0Vent;

     |REFRESCACONTROL enGrid12;
     |FOCOTECLADO enGrid12;
|FPRC;

|PROCESO ModAdjunto;
     |LEE 101#gecom012.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |VENTANA 0501, 1299, -1, 16, "Modificaci¢n adjunto";
     n0Vent = FSalida;

     |PTEC 802; |PAUSA;

     |PINPA #0#gecow006;  nPan6 = FSalida;

     |CONTROL_SIMPLE nPan6, "BOTON,{{135}}", 1003, 1209, VerDoc;

     |PARA nCmp = 0;  |SI nCmp [ enTCmp12;  |HACIENDO nCmp = nCmp + 1;
           #gecow006#nCmp# = #gecom012#nCmp#;
     |SIGUE;

     |PINDA #0#gecow006;
     |ENDATOS #1#gecow006;
     |SI FSalida = 0;
         |PARA nCmp = 0;  |SI nCmp [ enTCmp12;  |HACIENDO nCmp = nCmp + 1;
               #gecom012#nCmp#  = #gecow006#nCmp#;
         |SIGUE;

         |GRABA 020#gecom012.a;
     |FINSI;
     |LIBERA #gecom012;

     |FINVENTANA n0Vent;

     |REFRESCACONTROL enGrid12;
     |FOCOTECLADO enGrid12;
|FPRC;

|PROCESO ConAdjunto;
     |LEE 000#gecom012.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |VENTANA 0501, 1299, -1, 16, "Consulta adjunto";
     n0Vent = FSalida;

     |PTEC 802; |PAUSA;

     |PINPA #0#gecow006;  nPan6 = FSalida;

     |CONTROL_SIMPLE nPan6, "BOTON,{{135}}", 1003, 1209, VerDoc;

     |PARA nCmp = 0;  |SI nCmp [ enTCmp12;  |HACIENDO nCmp = nCmp + 1;
           #gecow006#nCmp# = #gecom012#nCmp#;
     |SIGUE;

     |PINDA #0#gecow006;
     |PAUSA;

     |FINVENTANA n0Vent;

     |REFRESCACONTROL enGrid12;
     |FOCOTECLADO enGrid12;
|FPRC;

|PROCESO BorAdjunto;
     |CONFI "0000NEst  seguro que quiere borrar el adjunto seleccionado";
     |SI FSalida ! 0;
         |FOCOTECLADO enGrid12;
         |ACABA;
     |FINSI;

     |LEE 101#gecom012.=;
     |SI FSalida ! 0;
         |FOCOTECLADO enGrid12;
         |ACABA;
     |FINSI;

     |HAZ BorraAdjuntos;

     |BORRA 020#gecom012.a;
     |LIBERA #gecom012;

     |REFRESCACONTROL enGrid12;
     |FOCOTECLADO enGrid12;
|FPRC;
