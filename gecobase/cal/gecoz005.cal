|FICHEROS;
     gecoz005;

     gecom000;
     gecom001;
     gecom002;
|FIN;

|VARIABLES;
     nHandle             = 0;
     n1Vent              = 0;
     nPausa              = 0;
     nOk                 = 0;
     nCampo              = 0;
     nPos                = 0;
     i                   = 0;

     aAlfa               = "";
     aAlfa1              = "";
     aBase               = "";
     aHora               = "";
     aSh                 = "";
     aEjec               = "";
     aFicPet             = "";
     aFicRes             = "";
     aFicXml             = "";
     aLee                = "";
     aAbre               = "";
     aPath               = "";
     aDes                = "";
     aDato               = "";
     aTab                = "";
     aLon                = "";
     aReg                = "";
     aLetra              = "";

     fFechaSys           = @;

     {15}aParam          = "";
     {99}aValor          = "";

     {-1}aVent           = "";
         aVent1          = 0;
         aVent2          = 0;
         aVent3          = 0;
         aVent4          = 0;
         aVent5          = "";

     &eaError            = "";
     &eaPathPlu          = "";
     &eaPathXfa          = "";
     &eaPathXre          = "";
     &eaPathAnu          = "";
     &eaFicXml           = "";
     &eaAlfa             = "";
     &eaNomFic           = "";
     &eaSer              = "";
     &eaMot              = "";
     &eaUsu              = "";

     &enEmp              = 0;
     &eaIde              = "";
     &eaCmp0             = "";
     &eaCmp1             = "";
     &eaCmp4             = "";
     &eaCmp5             = "";
     &eaCmp7             = "";
     &eaCmp8             = "";
     &eaCmp9             = "";
     &eaCmp10            = "";
|FIN;

|PROCESO AbreVtn;
     |VENTANA 0501, 0540, -1, 16, "Enviando ....";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;
     |PTEC 802;  |PAUSA;
     |PTEC 802;  |PAUSA;
|FPRC;

|PROCESO CierraVtn;
     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;
|FPRC;

|PROCESO LeeRespuesta;
     aFicRes = eaPathXre + aFicPet + ".txt";

     |PARA;  |SI;  |HACIENDO;
          |PARA nPausa = 1; |SI nPausa [ 10000; |HACIENDO nPausa = nPausa + 1;
          |SIGUE;

          |XABRE "E", aFicRes, nHandle;
          |SI FSalida ] 0;
              |SAL;
          |FINSI;

          |PULSATECLA;
     |SIGUE;

     |XABRE "U", aFicRes, nHandle;
     |SI FSalida ] 0;
         |XLEE nHandle, 250, aLee;  |QBF aLee;
     |FINSI;
     |XCIERRA nHandle;

     eaAlfa = "Motivo: Sin respuesta Resultado: Error";

     |SI aLee ! "";
         eaAlfa = aLee;  |HAZ QuitaRarosTF8;
     |FINSI;

     #gecom002#9 = eaAlfa;

     aAlfa1 = eaAlfa > "";  |QBT aAlfa1;
     aAlfa  = aAlfa1 - "RESULTADO:ERROR";
     |SI FSalida = 0;
         |SI #gecom002#7 = "E ";  #gecom002#7 = "A";   |FINSI;
         |SI #gecom002#7 = "RE";  #gecom002#7 = "RA";  |FINSI;

         |HORA aHora;

         #gecom002#10 = fFechaSys;
         #gecom002#11 = aHora;
         #gecom002#12 = Usuario % 810;
     |FINSI;

     eaCmp0  = #gecom002#0;
     eaCmp1  = #gecom002#1;
     eaCmp4  = "Anular factura";
     eaCmp5  = #gecom002#7;
     eaCmp7  = #gecom002#9;
     eaCmp8  = "";
     eaCmp9  = "";
     eaCmp10 = eaUsu;
     |HAZ GrabaLog;

     |FBORRA aFicRes;
|FPRC;

|PROCESO EnvioPeticion;
     |DFICE aPath;
     aAbre = aPath + eaNomFic + ".ent";
     |XABRE "WB", aAbre, nHandle;

     aTab = &9;

     aAlfa = "Empresa" + aTab;
     aAlfa = aAlfa  + "Identificador" + aTab;
     aAlfa = aAlfa  + "Servidor" + aTab;
     aAlfa = aAlfa  + "Usuario" + aTab;
     aAlfa = aAlfa  + "Peticion" + &13 + &10;
     |XGRABA nHandle, aAlfa;

     aAlfa = #gecom002#0;                     |QBF aAlfa;
     aAlfa = aAlfa + aTab + #gecom002#1;      |QBF aAlfa;
     aAlfa = aAlfa + aTab + eaSer;            |QBF aAlfa;
     aAlfa = aAlfa + aTab + eaUsu;            |QBF aAlfa;
     aAlfa = aAlfa + aTab + "ConsultarFactura";
     aAlfa = aAlfa + &13 + &10;
     |XGRABA nHandle, aAlfa;

     |XCIERRA nHandle;

     aAlfa = "gecoz008;" + eaNomFic;
     |SUB_EJECUTA aAlfa;
|FPRC;

|PROGRAMA;
     |SI eaError ! "";
         |ACABA;
     |FINSI;

     |ABRE #gecom001;
     #gecom001#0 = enEmp;
     |LEE 000#gecom001.=;
     |SI FSalida ! 0;
         |CIERRA #gecom001;
         |ACABA;
     |FINSI;
     |CIERRA #gecom001;

     |ABRE #gecom002;
     #gecom002#0 = #gecom001#0;
     #gecom002#1 = eaIde;
     |LEE 101#gecom002.=;
     |SI FSalida ! 0;
         |CIERRA #gecom002;
         |ACABA;
     |FINSI;

     eaPathAnu = eaPathXfa + "anulaciones";
     eaPathAnu = eaPathAnu + "/" + (("00000" + #gecom001#0) % -105);  |MKDIR eaPathAnu;
     eaPathAnu = eaPathAnu + "/";

     eaPathXre = eaPathXfa + "respuestas";
     eaPathXre = eaPathXre + "/" + (("00000" + #gecom001#0) % -105);  |MKDIR eaPathXre;
     eaPathXre = eaPathXre + "/";

     |HORA aHora;
     aFicPet = (fFechaSys % -104) + (fFechaSys % 402) + (fFechaSys % 102);
     aFicPet = aFicPet + (aHora % 102) + (aHora % 402) + (aHora % 702);

     aSh = "peticion.sh";
     |SI eaSer = "PRO";
         aSh = "peticionpro.sh";
     |FINSI;

     aSh     = "bash " + eaPathPlu + aSh;
     aParam1 = ("00000" + #gecom001#0) % -105;
     aParam2 = aFicPet;
     aParam3 = #gecom000#2;   |QBF aParam3;
     aParam4 = #gecom000#3;   |QBF aParam4;
     aParam5 = "anular_factura";

     aParam6  = #gecom002#17;  |QBT aParam6;
     |SI aParam6 = "";
         aParam6  = #gecom002#9;  |QBT aParam6;
     |FINSI;

     aParam7  = eaMot;         |QBT aParam7;
     aParam8  = #gecom002#8;   |QBF aParam8;
     aParam9  = &34 + "-" + &34;

     aEjec   = aSh   + " " + aParam1  + " " + aParam2;
     aEjec   = aEjec + " " + aParam3  + " " + aParam4;
     aEjec   = aEjec + " " + aParam5  + " " + aParam6;
     aEjec   = aEjec + " " + aParam7  + " " + aParam8;
     aEjec   = aEjec + " " + aParam9;
     |SYSTEM aEjec;

     aSh     = "bash " + eaPathPlu + "recoger.sh";
     aParam1 = ("00000" + #gecom001#0) % -105;
     aParam2 = aFicPet + ".txt";
     aParam3 = eaPathXre;

     aEjec   = aSh   + " " + aParam1 + " " + aParam2;
     aEjec   = aEjec + " " + aParam3;
     |SYSTEM aEjec;

     |HAZ LeeRespuesta;

     aFicXml = eaPathAnu + eaFicXml;
     |FBORRA aFicXml;

     |GRABA 020#gecom002.a;
     |LIBERA #gecom002;
     |CIERRA #gecom002;

     |SI #gecom002#7 = "A ";  |O #gecom002#7 = "RA";
         |HAZ EnvioPeticion;
     |SINO;
         |HAZ GrabaResultado;
     |FINSI;
|FPRO;

|PROCESO Dato;
     nCampo  = 1;
     IaValor = aValor1<-;
     aLon    = aDato % 0;
     aReg    = "";
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos   = i * 100 + 1;
          aLetra = aDato % nPos;
          |SI aLetra = aTab;
              aValor = aReg;
              aReg = "";
              nCampo = nCampo + 1;
              IaValor = IaValor + 1;
          |SINO;
               aReg = aReg + aLetra;
          |FINSI;

          |SI nCampo > 90; |SAL; |FINSI;     || Por si.... :)
     |SIGUE;

     aValor = aReg;
|FPRC;

|PROCESO LeeCSV;
     |DFICE aPath;
     aDes = aPath + eaNomFic + ".ent";
     |XABRE "", aDes, nHandle;
     |SI FSalida ] 0;
          |XLEE nHandle, 250, aDato;    || los nombres
          |XLEE nHandle, 250, aDato;
          |HAZ Dato;
          |XCIERRA nHandle;

          enEmp = aValor1;
          eaIde = aValor2;
          eaSer = aValor3;
          eaUsu = aValor4;
          eaMot = aValor5;
     |FINSI;
|FPRC;

|PROCESO T80;  |TIPO 80;
     aTab     = &9;
     eaNomFic = PARAMETRO;   |QBF eaNomFic;
     |HAZ LeeCSV;

     eaError = "";
     |SI eaSer ! "PRO";  |Y eaSer ! "PRU";
         eaError = "0000Falta introducir un servidor";
     |FINSI;

     |SI enEmp = 0;
         eaError = "0000Falta introducir una empresa";
     |FINSI;

     |SI eaIde = "";
         eaError = "0000Falta introducir un Id de factura";
     |FINSI;

     |SI eaMot = "";
         eaError = "0000Falta introducir un motivo";
     |FINSI;

     |SI eaError ! "";
         |ACABA;
     |FINSI;

     eaError = "";

     |ABRE #gecom000;
     |LEE 000#gecom000.p;
     |SI FSalida ! 0;  |DDEFECTO #gecom000;  |FINSI;
     |CIERRA #gecom000;

     aAlfa = #gecom000#2 + #gecom000#3;   |QBF aAlfa;
     |SI aAlfa = "";
         eaError = "0000Usuario y password no definidos.";
         |ACABA;
     |FINSI;

     |DBASE aBase;
     eaPathPlu = aBase + "plugins/";

     eaPathXfa = aBase + "xface";                 |MKDIR eaPathXfa;
     eaPathXfa = aBase + "xface/";

     eaPathXre = eaPathXfa + "respuestas";        |MKDIR eaPathXre;
     eaPathXre = eaPathXre + "/" + "ds";          |MKDIR eaPathXre;
     eaPathXre = eaPathXre + "/";

     fFechaSys = ~;
     |HORA aHora;

     aFicPet = (fFechaSys % -104) + (fFechaSys % 402) + (fFechaSys % 102);
     aFicPet = aFicPet + (aHora % 102) + (aHora % 402) + (aHora % 702);

     aSh = "peticion.sh";
     |SI eaSer = "PRO";
         aSh = "peticionpro.sh";
     |FINSI;

     aSh     = "bash " + eaPathPlu + aSh;
     aParam1 = "ds";
     aParam2 = aFicPet;
     aParam3 = #gecom000#2;   |QBF aParam3;
     aParam4 = #gecom000#3;   |QBF aParam4;
     aParam5 = "haz_test";

     aEjec   = aSh   + " " + aParam1 + " " + aParam2;
     aEjec   = aEjec + " " + aParam3 + " " + aParam4;
     aEjec   = aEjec + " " + aParam5;
     |SYSTEM aEjec;

     aSh     = "bash " + eaPathPlu + "recoger.sh";
     aParam1 = "ds";
     aParam2 = aFicPet + ".txt";
     aParam3 = eaPathXre;

     aEjec   = aSh   + " " + aParam1 + " " + aParam2;
     aEjec   = aEjec + " " + aParam3;
     |SYSTEM aEjec;

     aFicRes = eaPathXre + aFicPet + ".txt";

     |XABRE "U", aFicRes, nHandle;
     |SI FSalida ] 0;
         |XLEE nHandle, 250, aLee;
     |FINSI;
     |XCIERRA nHandle;

     |QBF aLee;
     |SI aLee ! "0k";
         eaError = "0000Servicio xface no operativo. Vuelva a intentarlo mas tarde";
     |FINSI;

     |FBORRA aFicRes;
|FPRC;

|PROCESO T90;  |TIPO 90;
|FPRC;
