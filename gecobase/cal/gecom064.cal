|FICHEROS;
     gecom064;
|FIN;

|VARIABLES;
     nHandle             = 0;
     nLong               = 0;
     nCampo              = 0;
     nCaracter           = 0;
     nPos                = 0;

     aAlfa               = "";
     aBase               = "";
     aAbre               = "";
     aCadena             = "";
     aLong               = "";
     aTab                = "";
     aCarac1             = "";
     aCampoA             = "";
     aCampoB             = "";
     aCampoC             = "";
|FIN;

|PROGRAMA;
     |CONSULTA_CLAVE #1#gecom064;
|FPRO;

|PROCESO GrabaReg;
     |QBF aCampoA;
     |QBF aCampoB;

     |SI aCampoA = "";  |ACABA;  |FINSI;

     #gecom064#0 = aCampoA;
     |LEE 000#gecom064.=;
     |SI FSalida ! 0;
         |DDEFECTO #gecom064;
         #gecom064#0 = aCampoA;
         |GRABA 020#gecom064.b;
     |FINSI;

     #gecom064#1 = aCampoB;

     |GRABA 020#gecom064.a;
     |LIBERA #gecom064;
|FPRC;

|PROCESO RecogeCSV;
     |DBASE aBase;

     aAbre = aBase + "patrones/metodosrectifica.csv";
     |XABRE "", aAbre, nHandle;
     |PARA;  |SI FSalida ] 0;  |HACIENDO;
          |XLEE nHandle, 255, aAlfa;
          |SI FSalida < 0;  |SAL;  |FINSI;

          aCampoA  = "";
          aCampoB  = "";

          aCadena = aAlfa;  |QBF aCadena;
          aLong   = aCadena % 0;
          nLong   = aLong;
          nCampo  = 1;
          aTab    = ";";

          |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
                nPos    = (100 * nCaracter) + 1;
                aCarac1 = aCadena % nPos;
                |SI aCarac1 = aTab;
                    nCampo = nCampo + 1;
                |SINO;
                    |SI nCampo = 1;  aCampoA  = aCampoA  + aCarac1;  |FINSI;
                    |SI nCampo = 2;  aCampoB  = aCampoB  + aCarac1;  |FINSI;

                    |SI nCampo > 2;  |SAL;  |FINSI;
                |FINSI;
          |SIGUE;

          |HAZ GrabaReg;
     |SIGUE;
     |XCIERRA nHandle;
|FPRC;

|PROCESO T80;  |TIPO 80;
     |ABRET #gecom064;

     |LEE 000#gecom064.p;
     |SI FSalida ! 0;
         |HAZ RecogeCSV;
     |FINSI;
|FPRC;

|PROCESO T90;  |TIPO 90;
     |CIERRAT #gecom064;
|FPRC;
