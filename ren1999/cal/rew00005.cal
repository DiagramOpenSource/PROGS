|FICHEROS;
     rew00005 #500,MANTE;
|FIN;

|VARIABLES;
     aFichOrigen  = "";
     aFichDestin  = "";
     aDirectorio  = "";
     aDirectorioD = "";
     aFichero     = "";
     aAlfa        = "";
     aAlfa1       = "";
     aAlfan       = "";
     aSesion      = "";
     aRaiz        = "";
     aPathBass    = "";
     aHandle      = "";
     aVariable    = "";
     aCaract      = "";
     aVersion     = "";
     aLongi       = "";
     aPath        = "";
     aAlfaa       = "";

     nEsBasico    = 0;
     nPosic       = 0;
     nVersion     = 0;
     nPara        = 0;
     nLongi       = 0;
     nCanal       = 0;
     nSwDirecta   = 0;

     &eaTipoSec   = "";

{200}&NEmpresas   = "";
|FIN;

|PROCESO CopiaFichero;
     FSalida = 0;
     aFichOrigen  = aDirectorio  + aFichero;
     aFichDestin  = aDirectorioD + aFichero;

     |COPIA_FICHERO aFichOrigen, aFichDestin;
     |SI FSalida ! 0;
         aAlfa = "Error [" + FSalida + "] en copia de ficheros ...";
         |MENAV aAlfa;
     |FINSI;

     |SI nEsBasico = 0;
         aAlfa = aDirectorio + aFichero;
         |FBORRA aAlfa;
     |FINSI;
|FPRC;

|PROCESO Sesion; |TIPO 0;
     aRaiz       = "";  |DIRECTORIO aRaiz;
     aDirectorio = "";  |DBASE aDirectorio;
     aDirectorio = aDirectorio + "hacienda/";
     aSesion     = #500#0; |QBF aSesion;
     aDirectorio = aDirectorio + aSesion + "/" + "imprime.bat";

     |SI nSwDirecta = 0;
         |XABRE "E", aDirectorio, nCanal;
         |SI FSalida ] 0;
             |CONFI "2400SLa Sesion ya existe, desea otra sesion : "
             |SI FSalida = 0;  |ERROR;  |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO QueVersion;
     nSwDirecta = 0;

     |VERSIONRTME aVersion;
     |SI aVersion ] "9.09Q";  nSwDirecta = 1;  |ACABA;  |FINSI;

     aLongi = aVersion % 0;
     nLongi = aLongi;
     aAlfan = "";
     |PARA nPara = 1;  |SI nPara [ nLongi;  |HACIENDO nPara = nPara + 1;
           nPosic  = (100 * nPara) + 1;
           aCaract = aAlfan % nPosic;
           |SI aCaract = ".";  |SAL;  |FINSI;

           aVersion = aVersion + aCaract;
     |SIGUE;

     nVersion = aVersion;

     |SI nVersion ] 10;  nSwDirecta = 1; |FINSI;
|FPRC;

|PROCESO EnviaFichero;
     |IP_REMOTO aAlfa1; |QBT aAlfa1;
     |SI aAlfa1 ! "";
         |ENVIA_FICHERO aFichOrigen, aFichDestin;
     |SINO;
         |COPIA_FICHERO aFichOrigen, aFichDestin;
     |FINSI;
|FPRC;

|PROCESO Directa;
     aAlfa = "/MODELOS";                      |RMKDIR aAlfa;
     aAlfa = "/MODELOS/RENTA";                |RMKDIR aAlfa;
     aAlfa = "/MODELOS/RENTA/" + eaTipoSec;   |RMKDIR aAlfa;

     aAlfa = "/MODELOS/RENTA/" + eaTipoSec + "/";
     |R_PDIR aAlfa;
     |PARA; |SI FSalida = 0; |HACIENDO;
            ||RFBORRA aAlfa;
            |R_SDIR aAlfa;
     |SIGUE;

     aDirectorio  = "";  |DBASE aDirectorio;
     aFichOrigen = aDirectorio + "hacienda/DIRECTA/ir99lase.exe";
     aFichDestin = "/MODELOS/RENTA/" + eaTipoSec + "/IR99LASE.EXE";
     |HAZ EnviaFichero;

     INEmpresas = NEmpresas1 <-;
     |PARA nPara = 1; |SI nPara [ 500; |HACIENDO nPara = nPara + 1;
           |SI NEmpresas = ""; |SAL; |FINSI;

           aFichOrigen = aDirectorio + "hacienda/DIRECTA/" + (NEmpresas % -108);
           aFichDestin = "/MODELOS/RENTA/" + eaTipoSec + "/" + (NEmpresas % -108);
           |HAZ EnviaFichero;

           INEmpresas = INEmpresas + 1;
     |SIGUE;

     aFichOrigen = aDirectorio + "hacienda/DIRECTA/imprime.bat";
     aFichDestin = "/MODELOS/RENTA/" + eaTipoSec + "/imprime.bat";
     |HAZ EnviaFichero;

     aAlfa1 = "/MODELOS/RENTA/" + eaTipoSec + "/imprime";
     |RSYSTEM aAlfa1;

     aAlfa1 = "/MODELOS/RENTA/" + eaTipoSec + "/imprime.bat";
     ||RFBORRA aAlfa1;

     aAlfa1 = "/MODELOS/RENTA/" + eaTipoSec + "/IR99LASE.EXE";
     ||RFBORRA aAlfa1;

     INEmpresas = NEmpresas1 <-;
     |PARA nPara = 1; |SI nPara [ 500; |HACIENDO nPara = nPara + 1;
           |SI NEmpresas = ""; |SAL; |FINSI;

           aAlfa1 = "/MODELOS/RENTA/" + eaTipoSec + "/" + (NEmpresas % -108);
           ||RFBORRA aAlfa1;

           INEmpresas = INEmpresas + 1;
     |SIGUE;
|FPRC;

|PROCESO CopiaBasico;
     nEsBasico = 1;
     aFichero  = "ir99lase.exe"; |HAZ CopiaFichero;
|FPRC;

|PROCESO CopiaBueno;
     Pos = -1;
     INEmpresas = NEmpresas1 <-;

     aPathBass = "";
     |DBASE aPathBass;
     aPath   = "wt  " + aPathBass + "hacienda/imprime.bat";

     Pos = -1;
     |FABRE aPath;
     aHandle = FSalida;
     |SI FSalida ] 0;
         |SI nSwDirecta = 1;
             aVariable = aHandle + " CD \";
             |FGRABA aVariable;

             aVariable = aHandle + " CD MODELOS";
             |FGRABA aVariable;

             aVariable = aHandle + " CD RENTA";
             |FGRABA aVariable;

             aVariable = aHandle + " CD " + eaTipoSec;
             |FGRABA aVariable;
         |FINSI;

         aVariable = aHandle + " ir99lase -y";
         |FGRABA aVariable;

         |PARA nPara = 1; |SI nPara [ 500; |HACIENDO nPara = nPara + 1;
             |SI NEmpresas = ""; |SAL; |FINSI;

             nEsBasico = 0;
             aFichero  = NEmpresas % -108;  |HAZ CopiaFichero;

             aVariable = aHandle + " wait r99pdfw " + aFichero + " " + aFichero + ".txt";

             |SI #500#1 = "S"; aVariable = aVariable + " -A"; |FINSI;

             |SI #500#2 = "N";
                 aVariable = aVariable + " -P0";
             |FINSI;

             |FGRABA aVariable;

             INEmpresas = INEmpresas + 1;
         |SIGUE;

         FSalida = aHandle;
         |FCIERRA FSalida;

         nEsBasico = 0;
         aFichero = "imprime.bat"; |HAZ CopiaFichero;
     |FINSI;

     |SI nSwDirecta = 0;
         |MENAV " FICHEROS COPIADOS !!!. SESION COMPLETADA CON EXITO";
     |FINSI;

     |POPV;
|FPRC;

|PROCESO CopiaDisco;
     |HAZ QueVersion;

     |DDEFECTO #500;

     |SI nSwDirecta = 1;
         #500#0 = "DIRECTA";
         |C_M #500#0, 1, "N";
     |FINSI;

     |PUSHV 0501 2380;

     Pos = -1;
     |PINPA #0#500;
     |PINDA #0#500;
     |ENDATOS #2#500;
     |SI FSalida ! 0;  |POPV;  |ACABA;  |FINSI;

     aRaiz       = "";  |DIRECTORIO aRaiz;
     aDirectorio = "";  |DBASE aDirectorio;
     aDirectorio = aDirectorio + "hacienda/";

     aSesion = #500#0; |QBF aSesion;
     aDirectorioD = aDirectorio + aSesion;
     |MKDIR aDirectorioD;
     aDirectorioD = aDirectorioD + "/";
     aAlfa = aRaiz + "bin/agichmod 777 " + aDirectorioD;  |SYSTEM aAlfa;

     |HAZ CopiaBasico;
     |HAZ CopiaBueno;

     |POPV;

     |SI nSwDirecta = 1;  |HAZ Directa;  |FINSI;
|FPRC;
