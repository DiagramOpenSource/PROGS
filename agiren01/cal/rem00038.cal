|FICHEROS;
     rem00038 #20;
     espejo@ #99;
|FIN;

|VARIABLES;
     aAlfa         = "";
     aAlfa1        = "";
     aLong         = "";
     aQueQuiero    = "";
     aNumCampos    = "";
     aNumPanta     = "";
     aPathRenta    = "";
     aDef          = "";

     nLong         = 0;
     nCaracter     = 0;
     nPosi         = 0;
     nPanta        = 0;
     nNumCampos    = 0;
     nNumPanta     = 0;
     nCampo        = 0;
     nModo         = 0;
     nEntrada      = 0;
     nValor        = 0;
     nDias         = 0;

     &enEmpresa    = 0;
     &enModo       = 0;
     &eaDeclarante = "";
     &enComunes    = 0;
     &enMinusvalia = 0;
     &eaMinuAyuda  = "";
     &eaFallece    = "";
     &eaSwVarios   = "";
     &eaVarDni     = "";
     &enSwLiquida  = 0;
     &enSwRetorno  = 0;
     &eaPrograma   = "";
     &eaPunto      = "";
|FIN;

|| ************************************************************************
|| PROCESOS DE CONSULTA DE PANTALLAS NORMALES
|| ************************************************************************

|PROCESO Totalm20;
     nModo  = (FEntrada / 100) ! 0;
     nValor = #20#15 - #20#16;
     |SI nValor < 0;
         #20#17 = -nValor;
         |SI #20#18 > #20#17;
             #20#18 = #20#17;
         |FINSI;
         |C_M #20#18, 1, "S";
         |PARA nCampo = 19;  |SI nCampo [ 26;  |HACIENDO nCampo = nCampo + 1;
               #20nCampo = 0; |C_M #20nCampo, 1, "N";
         |SIGUE;
     |SINO;
         #20#19 = nValor;

         |SI #20#12 = "N";
             |C_M #20#20, 1, "S";
             |C_M #20#22, 1, "S";

             #20#21 = #20#19 - #20#20;
             |SI #20#22 > #20#21;
                 #20#22 = #20#21;
             |FINSI;
             |PARA nCampo = 23;  |SI nCampo [ 26;  |HACIENDO nCampo = nCampo + 1;
                   #20nCampo = 0; |C_M #20nCampo, 1, "N";
             |SIGUE;
         |SINO;
             |C_M #20#23, 1, "S";
             |C_M #20#25, 1, "S";
             |C_M #20#26, 1, "S";

             #20#24 = #20#19 - #20#23;
             |SI #20#25 > #20#24;
                 #20#25 = #20#24;
             |FINSI;

             nValor = #20#24 - #20#25;
             |SI #20#26 > nValor;
                 #20#26 = nValor;
             |FINSI;

             |PARA nCampo = 20;  |SI nCampo [ 22;  |HACIENDO nCampo = nCampo + 1;
                   #20nCampo = 0; |C_M #20nCampo, 1, "N";
             |SIGUE;
         |FINSI;

         #20#17 = 0;
         #20#18 = 0;  |C_M #20#18, 1, "N";
     |FINSI;

     |PARA nCampo = 17;  |SI nCampo [ 26;  |HACIENDO nCampo = nCampo + 1;
           |PINTA #20nCampo;
     |SIGUE;
|FPRC;

|PROCESO Varios;  |TIPO 7;
     |C_M #20#5,  1, "N";
     |C_M #20#6,  1, "N";
     |C_M #20#7,  1, "N";
     |C_M #20#8,  1, "N";
     |C_M #20#9,  1, "N";
     |C_M #20#10, 1, "N";

     aAlfa = eaSwVarios;
     aLong = aAlfa % 0;
     nLong = aLong;
     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPosi = (100 * nCaracter) + 1;
           aAlfa1 = aAlfa % nPosi;
           |SI aAlfa1 = "T";  |C_M #20#5,  1, "S";  |FINSI;
           |SI aAlfa1 = "C";  |C_M #20#6,  1, "S";  |FINSI;
           |SI aAlfa1 = "1";  |C_M #20#7,  1, "S";  |FINSI;
           |SI aAlfa1 = "2";  |C_M #20#8,  1, "S";  |FINSI;
           |SI aAlfa1 = "3";  |C_M #20#9,  1, "S";  |FINSI;
           |SI aAlfa1 = "4";  |C_M #20#10, 1, "S";  |FINSI;
     |SIGUE;

     |SI #20#2 ! "VARIOS    ";
         |C_M #20#5,  1, "N";  #20#5  = 0;  |PINTA #20#5;
         |C_M #20#6,  1, "N";  #20#6  = 0;  |PINTA #20#6;
         |C_M #20#7,  1, "N";  #20#7  = 0;  |PINTA #20#7;
         |C_M #20#8,  1, "N";  #20#8  = 0;  |PINTA #20#8;
         |C_M #20#9,  1, "N";  #20#9  = 0;  |PINTA #20#9;
         |C_M #20#10, 1, "N";  #20#10 = 0;  |PINTA #20#10;
     |FINSI;
|FPRC;

|RUTINA ClaveBaja20;
     #20#0 = enEmpresa;
     #20#1 = 1;
|FRUT;

|RUTINA ClaveAlta20;
     #20#0 = enEmpresa;
     #20#1 = 99;
|FRUT;

|PROCESO PonDeclarante;  |TIPO 7;
     eaDeclarante = #20#2;
     enComunes    = 1;
     |HAZ SacaDeclarantes;
     #20#2 = eaDeclarante;
     |PINTA #20#2;
|FPRC;

|PROCESO Fallece;  |TIPO 7;
     |SI eaFallece = "S";
         |C_M #20#4, 1, "S";
     |SINO;
         |C_M #20#4, 1, "N";  #20#4 = 0;  |PINTA #20#4;
     |FINSI;
|FPRC;

|PROCESO Tipo2m20;  |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;  |O nModo = 3;
         enSwRetorno  = 1;
         |ACABA;
     |FINSI;

     nEntrada = 0 +. 1;
     |SI nEntrada ! 1;
         |PARA nCampo = 0;  |SI nCampo [ nNumCampos;  |HACIENDO nCampo = nCampo + 1;
               #99nCampo = #20nCampo;
         |SIGUE;
     |SINO;
         |PARA nCampo = 0;  |SI nCampo [ nNumCampos;  |HACIENDO nCampo = nCampo + 1;
               |SI #99nCampo ! #20nCampo;
                   enSwRetorno  = 1;
               |FINSI;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO Tipo3;  |TIPO 3;
     |SI #20#17 ! 0;
         |SI #20#18 = 0;
             |MENAV "0000 La Cantidad que tenemos en el Imputable esta a cero. Verifique el dato.";
         |FINSI;
     |FINSI;

     |SI #20#20 ! 0;  |O #20#21 ! 0;
         |SI #20#22 = 0;
             |MENAV "0000 La Cantidad que tenemos en el Imputable esta a cero. Verifique el dato.";
         |FINSI;
     |FINSI;

     |SI #20#23 ! 0;  |O #20#24 ! 0;  |O #20#25 ! 0;
         |SI #20#26 = 0;
             |MENAV "0000 La Cantidad que tenemos en el Imputable esta a cero. Verifique el dato.";
         |FINSI;
     |FINSI;
|FPRC;

|PROGRAMA;
     aQueQuiero = "|NP";
     aNumPanta  = #20#aQueQuiero#;
     nNumPanta  = aNumPanta;
     nNumPanta  = nNumPanta - 1;

     aQueQuiero = "|NC";
     aNumCampos = #20#aQueQuiero#;
     nNumCampos = aNumCampos;
     nNumCampos = nNumCampos - 1;

     |DBASE aPathRenta;  |QBT aPathRenta;
     aDef = aPathRenta + "def/rem00038.mas";
     |CARGA_DEF aDef, espejo@;
     |SI FSalida ! 0;
         aAlfa = "     Error en cargar el Def rem00038.mas";
         |MENAV aAlfa;
         |ERROR;
     |FINSI;

     |ENTLINEAL #1#20, -2, enModo, 22, 1, ClaveBaja20, ClaveAlta20;

     |DESCARGA_DEF #espejo@;

     eaPrograma   = "rem00038";
     eaPunto      = "  ";
     |HAZ ProgramaActivo;
|FPRO;
