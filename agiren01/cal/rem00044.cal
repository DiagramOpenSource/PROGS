|FICHEROS;
     rem00044 #20;
     rem00021 #6;
     rem00007 #7,MANTE;
     rem00008 #8;
     rem00100 #100;
     rem00101 #101;
     retabla1 #401;
     espejo@ #99;
|FIN;

|VARIABLES;
     aAlfa         = "";
     aAlfa1        = "";
     aLong         = "";
     aQueQuiero    = "";
     aNumCampos    = "";
     aNumPanta     = "";
     aPathRenta    = "";
     aDef          = "";
     aPerceptor    = "";
     aEdad         = "";
     aEdadPer      = "";

     nLong         = 0;
     nCaracter     = 0;
     nPosi         = 0;
     nPanta        = 0;
     nNumCampos    = 0;
     nNumPanta     = 0;
     nCampo        = 0;
     nModo         = 0;
     nEntrada      = 0;
     nComunidad    = 0;
     nTipo         = 0;
     nLinea        = 0;
     nNumero       = 0;
     nInversion    = 0;
     nDeduccion    = 0;
     nHijosAnt     = 0;
     nHijosDed     = 0;
     nAscend       = 0;
     nMinusv       = 0;
     nMinusD       = 0;
     nEdadMayor    = 0;
     nTotalHijos   = 0;
     nHijos        = 0;
     nCantidad     = 0;
     nPorce        = 0;

     &enEmpresa    = 0;
     &enModo       = 0;
     &eaDeclarante = "";
     &enComunes    = 0;
     &enMinusvalia = 0;
     &eaMinuAyuda  = "";
     &eaFallece    = "";
     &eaSwVarios   = "";
     &eaVarDni     = "";
     &enSwLiquida  = 0;
     &enSwRetorno  = 0;
     &eaPrograma   = "";
     &eaPunto      = "";
     &enAutonomia  = 0;
|FIN;

|| ************************************************************************
|| PROCESOS DE CONSULTA DE PANTALLAS NORMALES
|| ************************************************************************

|PROCESO GrabaLinea;
     |ABRE #7;
     #7#0 = nComunidad;
     |LEE 040#7=;
     |SI FSalida ! 0;  |CIERRA #7;  |ACABA;  |FINSI;
     |CIERRA #7;

     |ABRE #8;
     #8#0 = nComunidad;
     #8#1 = nTipo;
     |LEE 040#8=;
     |SI FSalida ! 0;  |CIERRA #8;  |ACABA;  |FINSI;
     |CIERRA #8;

     |DDEFECTO #20;
     nLinea = nLinea + 1;
     #20#0   = enEmpresa;
     #20#1   = nLinea;
     #20#2   = aPerceptor;
     #20#3   = "A";
     #20#11  = #7#0;
     #20#12  = #7#1;
     #20#13  = #8#1;
     #20#14  = #8#2;
     #20#15  = #8#3;
     #20#16  = #8#4;
     #20#17  = nNumero;
     #20#18  = nInversion;
     #20#19  = nDeduccion;

     |GRABA 020#20n;
     |LIBERA #20;
|FPRC;

|PROCESO CalcuHijos;
     |SI #101#4 = "DESCENDIENTE";
         |SI #101#7 = " ";
             nTotalHijos = nTotalHijos + 1;
         |FINSI;

         |SI #101#7 = "1";  |Y aPerceptor = "TITULAR";
             nTotalHijos = nTotalHijos + 1;
         |FINSI;

         |SI #101#7 = "2";  |Y aPerceptor = "CONYUGE";
             nTotalHijos = nTotalHijos + 1;
         |FINSI;

         |SI #101#5 < "2001"; |Y #101#4 ! "    ";
             |SI #101#7 = " ";
                 nHijosAnt = nHijosAnt + 1;
             |FINSI;
             |SI #101#7 = "1";  |Y aPerceptor = "TITULAR";
                 nHijosAnt = nHijosAnt + 1;
             |FINSI;
             |SI #101#7 = "2";  |Y aPerceptor = "CONYUGE";
                 nHijosAnt = nHijosAnt + 1;
             |FINSI;
         |FINSI;

         |SI #101#5 = "2001";
             |SI #101#7 = " ";
                 nHijosDed = nHijosDed + 1;
             |FINSI;
             |SI #101#7 = "1";  |Y aPerceptor = "TITULAR";
                 nHijosDed = nHijosDed + 1;
             |FINSI;
             |SI #101#7 = "2";  |Y aPerceptor = "CONYUGE";
                 nHijosDed = nHijosDed + 1;
             |FINSI;
         |FINSI;
     |FINSI;

     |SI #101#4 = "ASCENDIENTE ";
         |SI #101#5 [ aEdad;  |Y #101#5 ! "    ";
             |SI #101#7 = " ";
                 nAscend = nAscend + 1;
             |FINSI;
             |SI #101#7 = "1";  |Y aPerceptor = "TITULAR";
                 nAscend = nAscend + 1;
             |FINSI;
             |SI #101#7 = "2";  |Y aPerceptor = "CONYUGE";
                 nAscend = nAscend + 1;
             |FINSI;
         |FINSI;
     |FINSI;

     |SI #101#6 ] 33;
         |SI #101#7 = " ";
             nMinusv = nMinusv + 1;
         |FINSI;
         |SI #101#7 = "1";  |Y aPerceptor = "TITULAR";
             nMinusv = nMinusv + 1;
         |FINSI;
         |SI #101#7 = "2";  |Y aPerceptor = "CONYUGE";
             nMinusv = nMinusv + 1;
         |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE rem00101;
     #101#1;
     ;
     #20#0, 01;
     #20#0, 99;
     ;
     NULL, CalcuHijos;
|FIN;

|PROCESO LeeFamiliar;
     nTotalHijos = 0;
     nHijosAnt   = 0;   || Numero de Hijos nacidos antes de 1998
     nHijosDed   = 0;   || Numero de Hijos nacidos en 1998
     nEdadMayor  = 0;   || Sujetos Pasivos de ....A¤os o mas
     nAscend     = 0;   || Ascendientes de ... A¤os  o mas
     nMinusv     = 0;   || Minusvalidos
     nMinusD     = 0;   || Minusvalidos

     |BUCLE rem00101;

     |SI aPerceptor = "TITULAR";
         |SI #100#8 ] 33;                           nMinusv    = nMinusv + 1; |FINSI;
         |SI #100#8 ] 33;                           nMinusD    = 1;           |FINSI;
         |SI #100#7 [ aEdad;  |Y #100#7  ! "    ";  nEdadMayor = 1;           |FINSI;
     |FINSI;

     |SI aPerceptor = "CONYUGE";
         |SI #100#41 ] 33;                           nMinusv = nMinusv + 1; |FINSI;
         |SI #100#41 ] 33;                           nMinusD    = 1;        |FINSI;
         |SI #100#40 [ aEdad;  |Y #100#40 ! "    ";  nEdadMayor = 1;        |FINSI;
     |FINSI;
|FPRC;

|PROCESO Baleares;
     aEdad = "1936";
     |HAZ LeeFamiliar;

     |SI nEdadMayor > 0;
         nTipo       = 5;
         nNumero     = nEdadMayor;
         nInversion  = 0;
         nDeduccion  = nNumero * #401#39;
         |HAZ GrabaLinea;
     |FINSI;

     |SI nMinusv > 0;
         nTipo       = 12;
         nNumero     = nMinusv;
         nDeduccion  = nNumero * #401#40;
         nInversion  = 0;
         |HAZ GrabaLinea;
     |FINSI;
|FPRC;

|PROCESO CastillaLeon;
     aEdad = "1936";
     |HAZ LeeFamiliar;

     |SI nTotalHijos ] 3;
         nTipo       = 1;
         nNumero     = 0;
         |SI nTotalHijos ] 4;
             nNumero     = nTotalHijos - 3;
         |FINSI;
         nInversion  = 0;
         nDeduccion  = #401#41 + (nNumero * #401#42);
         |HAZ GrabaLinea;
     |FINSI;

     |SI nHijosDed > 0;
         nTipo       = 28;
         nNumero     = nHijosDed;
         nInversion  = 0;
         nDeduccion  = 0;
         |SI nNumero = 1;  nDeduccion = #401#43;             |FINSI;
         |SI nNumero = 2;  nDeduccion = #401#43 + #401#44;   |FINSI;
         |SI nNumero ] 3;
             nDeduccion = (#401#43 + #401#44) + ((nNumero - 2) * #401#45);
         |FINSI;
         |HAZ GrabaLinea;
     |FINSI;
|FPRC;

|PROCESO Catalunya;
     aEdad = "1936";
     |HAZ LeeFamiliar;

     |SI nHijosDed ! 0;  |Y nTotalHijos > 1;
         nHijos  = nTotalHijos - 1;
         |SI nHijosDed > nHijos;  nHijosDed = nHijos;    |FINSI;
         nNumero     = nHijosDed;

         nTipo       = 2;
         nDeduccion  = nNumero * #401#46;
         nInversion  = 0;
         |HAZ GrabaLinea;
     |FINSI;
|FPRC;

|PROCESO Galicia;
     aEdad = "1936";
     |HAZ LeeFamiliar;

     |SI nHijosDed > 0;
         nTipo       = 4;
         nNumero     = nHijosDed;
         nInversion  = 0;
         nDeduccion  = 0;
         |SI nNumero = 1;  nDeduccion = #401#47;                                 |FINSI;
         |SI nNumero = 2;  nDeduccion = #401#47 + #401#48;                       |FINSI;
         |SI nNumero = 3;  nDeduccion = #401#47 + #401#48 + #401#49;             |FINSI;
         |SI nNumero = 4;  nDeduccion = #401#47 + #401#48 + #401#49 + #401#50;   |FINSI;
         |SI nNumero ] 5;
             nDeduccion = #401#47 + #401#48 + #401#49 + #401#50;
             nDeduccion = nDeduccion + ((nNumero - 5) * #401#51);
         |FINSI;

         |HAZ GrabaLinea;
     |FINSI;
|FPRC;

|PROCESO Madrid;
     aEdad = "1936";
     |HAZ LeeFamiliar;

     |SI nHijosDed > 0;
         nTipo       = 23;
         nNumero     = nHijosDed;
         nInversion  = 0;
         nDeduccion  = nHijosDed * #401#52;
         |HAZ GrabaLinea;
     |FINSI;
|FPRC;

|PROCESO MiraVivienda;
     |SI #6#2 = "TITULAR   ";  |Y aPerceptor = "CONYUGE";  |ACABA;  |FINSI;
     |SI #6#2 = "CONYUGE   ";  |Y aPerceptor = "TITULAR";  |ACABA;  |FINSI;

     |SI #100#11 = 13;
         nCantidad    = #6#23 + #6#29;   || Murcia
     |SINO;
         nCantidad    = #6#23 - #6#22;   || Valencia
     |FINSI;

     |SI #6#2 = "TITULAR   ";  nPorce = 100;  |FINSI;
     |SI #6#2 = "CONYUGE   ";  nPorce = 100;  |FINSI;
     |SI #6#2 = "GANANCIAL ";  nPorce = 50;   |FINSI;
     |SI #6#2 = "VARIOS    ";
         |SI aPerceptor = "TITULAR";  nPorce = #6#5;  |FINSI;
         |SI aPerceptor = "CONYUGE";  nPorce = #6#6;  |FINSI;
     |FINSI;
     |ERROR10;
|FPRC;

|DEFBUCLE rem00021;
     #6#1;
     ;
     enEmpresa, 01;
     enEmpresa, 99;
     ;
     NULL, MiraVivienda;
|FIN;

|PROCESO Valencia;
     aEdad = "1936";
     |HAZ LeeFamiliar;

     |SI nHijosDed ! 0;  |Y nTotalHijos > 2;
         nHijos  = nTotalHijos - 2;
         |SI nHijosDed > nHijos;  nHijosDed = nHijos;    |FINSI;
         nNumero     = nHijosDed;
         nTipo       = 3;
         nDeduccion  = nNumero * #401#54;
         nInversion  = 0;
         |HAZ GrabaLinea;
     |FINSI;

     |SI nEdadMayor > 0; |Y nMinusD > 0;
         nTipo       = 6;
         nNumero     = nEdadMayor;
         nInversion  = 0;
         nDeduccion  = nNumero * #401#55;
         |HAZ GrabaLinea;
     |FINSI;

     nCantidad = 0;
     nPorce    = 100;
     |BUCLE rem00021;
     |SI nCantidad ! 0;  |Y aEdadPer ] "1966";
         nTipo       = 21;
         nNumero     = 0;
         nInversion  = nCantidad % nPorce;
         nDeduccion  = nInversion % 3;
         |HAZ GrabaLinea;
     |FINSI;
|FPRC;

|PROCESO Murcia;
     aEdad = "1936";
     |HAZ LeeFamiliar;

     nCantidad = 0;
     nPorce    = 100;
     |BUCLE rem00021;
     |SI nCantidad ! 0;  |Y aEdadPer ] "1971";
         nTipo       = 19;
         nNumero     = 0;
         nInversion  = nCantidad % nPorce;
         nDeduccion  = nInversion % 3;
         |HAZ GrabaLinea;
     |FINSI;
|FPRC;

|PROCESO Tipo11;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     #20#0 = enEmpresa;
     #20#1 = 1;
     |LEE 040#20];
     |SI FSalida = 0;  |Y #20#0 = enEmpresa;
         |MENAV "      Ya existen deducciones en esta empresa, debe estar sin deducciones.";
         |ERROR;
         |ACABA;
     |FINSI;

     nLinea = 0;

     #20#0 = enEmpresa;

     |ABRE #100;
     #100#0 = enEmpresa;
     |LEE 040#100=;
     |SI FSalida ! 0;  |DDEFECTO #100;  |FINSI;
     |CIERRA #100;

     aPerceptor = "TITULAR";
     nComunidad = #100#11;
     aEdadPer   = #100#7;
     |SI #100#11 = 4;   |HAZ Baleares;      |FINSI;
     |SI #100#11 = 8;   |HAZ CastillaLeon;  |FINSI;
     |SI #100#11 = 9;   |HAZ Catalunya;     |FINSI;
     |SI #100#11 = 11;  |HAZ Galicia;       |FINSI;
     |SI #100#11 = 12;  |HAZ Madrid;        |FINSI;
     |SI #100#11 = 13;  |HAZ Murcia;        |FINSI;
     |SI #100#11 = 17;  |HAZ Valencia;      |FINSI;

     aPerceptor = "CONYUGE";
     nComunidad = #100#43;
     aEdadPer   = #100#40;
     |SI #100#43 = 4;   |HAZ Baleares;      |FINSI;
     |SI #100#43 = 8;   |HAZ CastillaLeon;  |FINSI;
     |SI #100#43 = 9;   |HAZ Catalunya;     |FINSI;
     |SI #100#43 = 11;  |HAZ Galicia;       |FINSI;
     |SI #100#43 = 12;  |HAZ Madrid;        |FINSI;
     |SI #100#43 = 13;  |HAZ Murcia;        |FINSI;
     |SI #100#43 = 17;  |HAZ Valencia;      |FINSI;

     |ERROR;
     |PTEC 802;
     |PTEC 807;
|FPRC;

|RUTINA ClaveBaja8;
     |SI FSalida = "POSICION";  |Y #20#13 ! 99;
         #8#1 = #20#13;
         |ACABA;
     |FINSI;
     #8#0 = #20#11;
     #8#1 = 1;
|FRUT;

|RUTINA ClaveAlta8;
     #8#0 = #20#11;
     #8#1 = 99;
|FRUT;

|PROCESO Consulta;
     |SI FSalida = 10;
         |PUSHV 0501 2380;
         |PINPA #0#7;
         |PINDA #0#7;
         |ENTLINEAL #1#8, 2, 4, 16, 0, ClaveBaja8, ClaveAlta8;
         #20#13 = #8#1;
         |POPV;
     |FINSI;

     |ABRE #8;
     #8#0 = #20#11;
     #8#1 = #20#13;
     |LEE 040#8=;
     |SI FSalida ! 0;
         |MENAV "    No existe Deduccion.";
         |CIERRA #8;
         |ERROR;
         |ACABA;
     |FINSI;
     |CIERRA #8;

     |C_M #20#17, 1, "S";
     |C_M #20#18, 1, "S";
     |C_M #20#19, 1, "S";
     |C_M #20#20, 1, "S";

     |SI #8#5 = "S";
         |C_M #20#18, 1, "N";   #20#18 = 0;
         |C_M #20#20, 1, "N";   #20#20 = 0;
     |FINSI;

     |SI #8#5 = "N";
         |C_M #20#17, 1, "N";   #20#17 = 0;
         |C_M #20#18, 1, "N";   #20#17 = 0;
         |C_M #20#20, 1, "N";   #20#20 = 0;
     |FINSI;

     |SI #8#5 = "I";
         |C_M #20#17, 1, "N";   #20#17 = 0;
         #20#20 = #8#6;
         |SI #20#20 = 0;
             |C_M #20#20, 1, "N";  #20#20 = 0;
             |C_M #20#19, 1, "N";  #20#19 = 0;
         |FINSI;
     |FINSI;

     #20#14 = #8#2;
     #20#15 = #8#3;
     #20#16 = #8#4;

     |PINTA #20#13;
     |PINTA #20#14;
     |PINTA #20#15;
     |PINTA #20#16;
     |PINTA #20#17;
     |PINTA #20#18;
     |PINTA #20#19;
     |PINTA #20#20;
|FPRC;

|PROCESO Totalm020;
     |SI #20#20 ! 0;
         #20#19 = #20#18 % #20#20;
         |SI #8#7 ! 0;
             |SI #20#19 > #8#7;
                 #20#19 = #8#7;
             |FINSI;
        |FINSI;
     |FINSI;
     |PINTA #20#19;
|FPRC;

|PROCESO Varios;  |TIPO 7;
     |C_M #20#5,  1, "N";
     |C_M #20#6,  1, "N";
     |C_M #20#7,  1, "N";
     |C_M #20#8,  1, "N";
     |C_M #20#9,  1, "N";
     |C_M #20#10, 1, "N";

     aAlfa = eaSwVarios;
     aLong = aAlfa % 0;
     nLong = aLong;
     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPosi = (100 * nCaracter) + 1;
           aAlfa1 = aAlfa % nPosi;
           |SI aAlfa1 = "T";  |C_M #20#5,  1, "S";  |FINSI;
           |SI aAlfa1 = "C";  |C_M #20#6,  1, "S";  |FINSI;
           |SI aAlfa1 = "1";  |C_M #20#7,  1, "S";  |FINSI;
           |SI aAlfa1 = "2";  |C_M #20#8,  1, "S";  |FINSI;
           |SI aAlfa1 = "3";  |C_M #20#9,  1, "S";  |FINSI;
           |SI aAlfa1 = "4";  |C_M #20#10, 1, "S";  |FINSI;
     |SIGUE;

     |SI #20#2 ! "VARIOS    ";
         |C_M #20#5,  1, "N";  #20#5  = 0;  |PINTA #20#5;
         |C_M #20#6,  1, "N";  #20#6  = 0;  |PINTA #20#6;
         |C_M #20#7,  1, "N";  #20#7  = 0;  |PINTA #20#7;
         |C_M #20#8,  1, "N";  #20#8  = 0;  |PINTA #20#8;
         |C_M #20#9,  1, "N";  #20#9  = 0;  |PINTA #20#9;
         |C_M #20#10, 1, "N";  #20#10 = 0;  |PINTA #20#10;
     |FINSI;
|FPRC;

|PROCESO Totalw20;
     #20#14 = #20#11 + #20#12 + #20#13;

     |PINTA #20#14;
|FPRC;

|RUTINA ClaveBaja20;
     #20#0 = enEmpresa;
     #20#1 = 1;
|FRUT;

|RUTINA ClaveAlta20;
     #20#0 = enEmpresa;
     #20#1 = 99;
|FRUT;

|PROCESO PonDeclarante;  |TIPO 7;
     eaDeclarante = #20#2;
     enComunes    = 1;
     |HAZ SacaDeclarantes;
     #20#2  = eaDeclarante;
     #20#11 = enAutonomia;
     |PINTA #20#2;

     |ABRE #8;
     #8#0 = #20#11;
     #8#1 = 1;
     |LEE 040#8];
     |SI FSalida ! 0;
         |CIERRA #8;
         |MENAV "     No existe Autonomia en el Declarante.";
         |PTEC 807;
         |ACABA;
     |FINSI;
     |SI #8#1 = 99;
         |CIERRA #8;
         |MENAV "     No hay deducciones Autonomicas en la Comunidad";
         |PTEC 807;
         |ACABA;
     |FINSI;
     |CIERRA #8;

     |ABRE #7;
     #7#0 = #20#11;
     |LEE 040#7=;
     |SI FSalida ! 0;  |DDEFECTO #7;  |FINSI;
     |CIERRA #7;

     #20#12 = #7#1;

     |PINTA #20#11;
     |PINTA #20#12;
|FPRC;

|PROCESO Fallece;  |TIPO 7;
     |SI eaFallece = "S";
         |C_M #20#4, 1, "S";
     |SINO;
         |C_M #20#4, 1, "N";  #20#4 = 0;  |PINTA #20#4;
     |FINSI;
|FPRC;

|PROCESO Tipo2m20;  |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;  |O nModo = 3;
         enSwRetorno  = 1;
         |ACABA;
     |FINSI;

     nEntrada = 0 +. 1;
     |SI nEntrada ! 1;
         |PARA nCampo = 0;  |SI nCampo [ nNumCampos;  |HACIENDO nCampo = nCampo + 1;
               #99nCampo = #20nCampo;
         |SIGUE;
     |SINO;
         |PARA nCampo = 0;  |SI nCampo [ nNumCampos;  |HACIENDO nCampo = nCampo + 1;
               |SI #99nCampo ! #20nCampo;
                   enSwRetorno  = 1;
               |FINSI;
         |SIGUE;
     |FINSI;
|FPRC;

|PROGRAMA;
     |ABRE #401;
     |LEE 040#401p;
     |SI FSalida ! 0;  |DDEFECTO #401;  |FINSI;
     |CIERRA #401;

     aQueQuiero = "|NP";
     aNumPanta  = #20#aQueQuiero#;
     nNumPanta  = aNumPanta;
     nNumPanta  = nNumPanta - 1;

     aQueQuiero = "|NC";
     aNumCampos = #20#aQueQuiero#;
     nNumCampos = aNumCampos;
     nNumCampos = nNumCampos - 1;

     |DBASE aPathRenta;  |QBT aPathRenta;
     aDef = aPathRenta + "def/rem00044.mas";
     |CARGA_DEF aDef, espejo@;
     |SI FSalida ! 0;
         aAlfa = "     Error en cargar el Def rem00044.mas";
         |MENAV aAlfa;
         |ERROR;
     |FINSI;

     |ENTLINEAL #1#20, -2, enModo, 22, 1, ClaveBaja20, ClaveAlta20;

     |DESCARGA_DEF #espejo@;

     eaPrograma   = "rem00044";
     eaPunto      = "  ";
     |HAZ ProgramaActivo;
|FPRO;
