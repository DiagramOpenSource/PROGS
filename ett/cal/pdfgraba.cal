|FICHEROS;
     pdfimtmp #100;                || CUIDADO: LINKADO CON LABORAL
     pdfcontr;
     labcontr;
     labtraba;
|FIN;

|VARIABLES;
     aAlfa = "";
     aAlfa1 = "";
     aAlfa2 = "";
     nNume = 0;
     nNume1 = 0;
     swEsImpar = 0;
     aLinea = "";
     nLinea = 0;
     aValor = "";
     aCampo = "";
     &aArchivo = "";
     aArchivoPDF = "";
     aArchivoFDF = "";
     aPathLocPDF = "";
     aPathSerPDF = "";
     aPathLocFDF = "";
     aPathSerFDF = "";
     aPathReader = "";
     aPathDestino = "";
     aPathDestino2 = "";
     &aID = "";
     aOrigen = "";
     aDestino = "";
     aDestino2 = "";
     nCanal = 0;
     nCanalIdi = 0;
     aSystem = "";
     aDirBase = "";
     aLongitud = "";
     nLongitud = "";
     aCaracter = "";
     aCaracterSal = "";
     aCadena = "";
     aCadenaSal = "";
     nPos = 0;
     nBusca = 0;
     nAscii = 0;

     &enWeb = 0;
     &eaWebArchivo = "";
     &eaWebDirDes = "";
     &eaWebDirPDF = "";
     nTipo = 0;
     aIP = "";
     &swCopia = 0;
     &swAnexoClaus = 0;
     &aProcedencia = "";
     &aListaFich = "";
     &aListaFichNor = "";
     &aListaFichCop = "";
     &aListaImpares = "";
     &swDondeEstoy = 0;
     &swYaHeImpreso = 0;
     {18}&aClausula = "";
     nNroLinea = 0;
     aNroLinea = "";
     &nTipoSalida = 0;
     &nContador = 0;
     aContador = "";
     nCaracter = 0;
     nPorDonde = 0;
     aEjecutable = "";
     aMiCaracter = "";
     &aVengoDe = "";
     &swDuplex = 0;
     aOrigenD = "";
     aDestinoD = "";
     &nVoypor = 0;
     &nNCA = 0;
     aDestinoSpool = "";
     aOrigenSpool = "";
     &nNroCopias = 0;
     &eaDSMAC    = "";
     &nTotalHojas = 0;
     &nHoja = 0;
     &nTotalLineas = 0;
     &swAvisado = 0;
     {1}aContiene = "";
     aBusca = "";
     aCambia = "";
     aEspacio = "";
     &swImpMasiva = 0;
     aFich = "";
     &swETT = 0;
     aOrigenMulti = "";
     &eaIdioma = "";
|FIN;

|PROCESO BuscaRuta;
     |HAZ CogeMac;

     aIP = "";  |IP_REMOTO aIP;  |QBF aIP;

     |ABRE #pdfcontr;
     #pdfcontr#0 = eaDSMAC;
     |LEE 000#pdfcontr.=;
     |SI FSalida ! 0;
         #pdfcontr#0 = aIP;
         |LEE 000#pdfcontr.=;
         |SI FSalida = 0;
             #pdfcontr#0 = eaDSMAC;
             |GRABA 020#pdfcontr.n;
             |LIBERA #pdfcontr;
         |SINO;
             |DDEFECTO #pdfcontr;
         |FINSI;
     |FINSI;
     |CIERRA #pdfcontr;

     aPathReader = #pdfcontr#1;   |QBF aPathReader;
|FPRC;

|PROCESO BuscaCaracter;
     aLongitud = aCadena % 0;
     nLongitud = aLongitud;
     |PARA nPos = 1; |SI nPos [ nLongitud; |HACIENDO nPos = nPos + 1;
          nCaracter = ((nPos * 100) + 1) * nPorDonde;
          aCaracter = aCadena % nCaracter;
          |SI aCaracter = aMiCaracter;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO sustituye4;
     nAscii = & aCaracter;
     aCaracterSal = aCaracter;
     |SI nAscii = 160;                  || la "a" acentuada
          aCaracterSal = "\341";
     |FINSI;
     |SI nAscii = 130;                  || la "e" acentuada
          aCaracterSal = "\351";
     |FINSI;
     |SI nAscii = 161;                  || la "i" acentuada
          aCaracterSal = "\355";
     |FINSI;
     |SI nAscii = 162;                  || la "o" acentuada
          aCaracterSal = "\363";
     |FINSI;
     |SI nAscii = 163;                  || la "u" acentuada
          aCaracterSal = "\372";
     |FINSI;
     |SI nAscii = 164;                  || la "¤"
          aCaracterSal = "\361";
     |FINSI;
     |SI nAscii = 165;                  || la ¥
          aCaracterSal = "\321";
     |FINSI;
     |SI nAscii = 166;
          aCaracterSal = "\252";        || el caracter ¦
     |FINSI;
     |SI nAscii = 167;
          aCaracterSal = "\272";        || el caracter §
     |FINSI;
     |SI aCaracter = "(";
          aCaracterSal = "\(";
     |FINSI;
     |SI aCaracter = ")";
          aCaracterSal = "\)";
     |FINSI;
|FPRC;

|PROCESO sustituye3;
     aCadenaSal = "";
     aLongitud = aCadena % 0;
     nLongitud = aLongitud;
     |PARA nPos = 1; |SI nPos [ nLongitud; |HACIENDO nPos = nPos + 1;
          nBusca = (nPos * 100) + 1;
          aCaracter = aCadena % nBusca;
          |HAZ sustituye4;
          aCadenaSal = aCadenaSal + aCaracterSal;
     |SIGUE;
|FPRC;

|PROCESO sustituye;
     || aCadena es la cadena a buscar un caracter
     || aBusca es el caracter a buscar
     || aCambia es por el que lo queremos cambiar

     aCadenaSal = "";
     aLongitud = aCadena % 0;
     nLongitud = aLongitud;
     |PARA nPos = 1; |SI nPos [ nLongitud; |HACIENDO nPos = nPos + 1;
          nBusca = (nPos * 100) + 1;
          aCaracter = aCadena % nBusca;
          |SI aCaracter = aBusca;
               aCadenaSal = aCadenaSal + aCambia;
          |SINO;
               aCadenaSal = aCadenaSal + aCaracter;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO CargaInicial;
     |DBASE aDirBase;

     aPathLocPDF = "/C/ds/spoolpdf/";  || para la plantilla PDF (si, este formato)
     aPathLocFDF = "C:\ds\spoolpdf\";  || aqui va la plantilla y el FDF local
     aPathSerPDF = aDirBase + "pdf/";  || aqui van todas las plantillas PDF
     aPathSerFDF = aDirBase + "fich/"; || aqui va el FDF en el servidor

     aContiene = "";
     |ESPECIFICA "RBASS", aContiene;
     aPathDestino = aContiene1;
     |QBF aPathDestino;
     aPathDestino = aPathDestino + "spoolpdf/";
     |RMKDIR aPathDestino;       || lo creo primero de nada, por si no hubiera

     aPathLocFDF = aPathDestino;  || aqui va la plantilla y el FDF local

     aCadena = aPathDestino;
     aBusca = "\";
     aCambia = "/";
     |HAZ sustituye;
     aCadena = aCadenaSal;
     aBusca = ":";
     aCambia = "";
     |HAZ sustituye;
     aPathLocPDF = aCadenaSal;  || para la plantilla PDF (si, este formato)
     aPathLocPDF = "/" + aPathLocPDF;

     |SI #labcontr#65 = 0;    || como siempre
          aPathLocPDF = "/C/ds/spoolpdf/";  || para la plantilla PDF (si, este formato)
          aPathLocFDF = "C:\ds\spoolpdf\";  || aqui va la plantilla y el FDF local
     |FINSI;
|FPRC;

|PROCESO control;
     |ABRE #labcontr;
     |LEE 000#labcontr.p;
     |CIERRA #labcontr;

     |SI swImpMasiva = 1;
          #labcontr#79 = 1;
     |FINSI;
|FPRC;

|PROCESO Cabecera;
     aLinea = "%FDF-1.2";
     |XGRABA nCanal, aLinea;
     aLinea = "%âãÏÓ";
     |XGRABA nCanal, aLinea;
|FPRC;

|PROCESO ArchivoLlama;
     |SI aProcedencia = "labllaca";     || para el llamamiento
          |SI nTotalHojas > 1;
               aAlfa = nHoja;
               aAlfa = ("00" + aAlfa) % -102;
               aLinea = aLinea - ".pdf)";
               aLinea = aLinea + "_" + aAlfa + ".pdf)";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Objeto1;
     aLinea = "1 0 obj";
     |XGRABA nCanal, aLinea;
     aLinea = "<< /FDF ";
     |XGRABA nCanal, aLinea;
     aLinea = "<< /JavaScript ";
     |XGRABA nCanal, aLinea;
     aLinea = "   << /Doc 3 0 R";
     |XGRABA nCanal, aLinea;
     aLinea = "      /Before (protege();)";
     |XGRABA nCanal, aLinea;
     aLinea = "   >>";
     |XGRABA nCanal, aLinea;
     aLinea = "   /Fields 2 0 R ";
     |XGRABA nCanal, aLinea;
     |SI enWeb = 0;
          aLinea = "/F (" + aPathLocPDF + aArchivoPDF + ")";
          |HAZ ArchivoLlama;
     |SINO;
          aLinea = "/F (" + eaWebDirPDF + (eaWebArchivo - ".txt" + "_" + aContador) + ".pdf)";
     |FINSI;
     |HAZ c3;
     |XGRABA nCanal, aLinea;
     |HAZ c4;
     aLinea = "  >>";
     |XGRABA nCanal, aLinea;
     aLinea = ">>";
     |XGRABA nCanal, aLinea;
     aLinea = "endobj";
     |XGRABA nCanal, aLinea;
|FPRC;

|PROCESO Objeto2;
     aLinea = "2 0 obj";
     |XGRABA nCanal, aLinea;
     aLinea = "[";
     |XGRABA nCanal, aLinea;
     |HAZ Lineas;
     aLinea = "]";
     |XGRABA nCanal, aLinea;
     aLinea = "endobj";
     |XGRABA nCanal, aLinea;
|FPRC;

|PROCESO Objeto3;
     aLinea = "3 0 obj";
     |XGRABA nCanal, aLinea;
     aLinea = "[ (protege) (function protege() {";
     |XGRABA nCanal, aLinea;
     aLinea = "  for(var i = 0; i < this.numFields; i++) {";
     |XGRABA nCanal, aLinea;
     aLinea = "  s = this.getNthFieldName(i);";
     |XGRABA nCanal, aLinea;
     aLinea = "  f = this.getField(s);";
     |XGRABA nCanal, aLinea;
     |SI #labcontr#54 = "N";
          aLinea = "  f.readonly = true;}";
     |SINO;
          aLinea = "  f.readonly = false;}";
     |FINSI;
     |XGRABA nCanal, aLinea;
     aLinea = "  })";
     |XGRABA nCanal, aLinea;
     aLinea = "]";
     |XGRABA nCanal, aLinea;
     aLinea = "endobj";
     |XGRABA nCanal, aLinea;
|FPRC;

|PROCESO Lineas;
     |LEE 000#pdfimtmp.p;
     |SI FSalida = 0;
          aCampo = #pdfimtmp#0;    || nombre del campo
          aValor = #pdfimtmp#1;    || valor
          |QBF aCampo;
          |QBF aValor;
          /*
          aCadena = aValor;
          |HAZ sustituye3;
          aValor = aCadenaSal;
          */
          aLinea = "<< /V (" + aValor + ")/T (" + aCampo + ")>> ";
          |XGRABA nCanal, aLinea;
     |FINSI;
     |PARA nLinea = 2; |SI nLinea [ 250; |HACIENDO nLinea = nLinea + 1;
          |LEE 000#pdfimtmp.s;
          |SI FSalida ! 0;
               |SAL;
          |FINSI;
          aCampo = #pdfimtmp#0;    || nombre del campo
          aValor = #pdfimtmp#1;    || valor

          nTipo  = #pdfimtmp#2;    || TIpo > 90 sin parentesis
          |QBF aCampo;
          |QBF aValor;

          aAlfa = aCampo % 109;
          ||SI aAlfa = "clausadic"; |O aAlfa = "clausulas";
          |SI nTipo >0; |Y nTipo < 90;
               |SI aArchivo = "sol_mat_pat.pdf"
                    |HAZ Texto;
               |SINO;
                    |HAZ Clausulas;
               |FINSI;
          |SINO;
               |SI nTipo = 00;
                    aLinea = "<< /V (" + aValor + ")/T (" + aCampo + ")>> ";
               |FINSI;
               |SI nTipo ] 90;     || campo Radio: sin parentesis
                    aLinea = "<< /V " + aValor + "/T (" + aCampo + ")>> ";
               |FINSI;
               |SI aArchivo = "cpd.pdf";
               |Y swETT = 0;
                    |SI aCampo = "supuesto";
                         aLinea = "<< /T (" + aCampo + ") /V (" + aValor + " \";
                    |FINSI;
                    |SI aCampo = "supuesto1"; aLinea = aValor + " \"; |FINSI;
                    |SI aCampo = "supuesto2"; aLinea = aValor + " \"; |FINSI;
                    |SI aCampo = "supuesto3"; aLinea = aValor + " \"; |FINSI;
                    |SI aCampo = "supuesto4"; aLinea = aValor + " \"; |FINSI;
                    |SI aCampo = "supuesto5";
                         aLinea = aValor + ")>> ";
                    |FINSI;
               |FINSI;
          |FINSI;
          |XGRABA nCanal, aLinea;
     |SIGUE;
|FPRC;

|PROCESO Texto;
     aNroLinea = aCampo % -102;
     nNroLinea = aNroLinea;

     |SI nNroLinea = 1;
          aLinea = "<< /V (" + aValor + "\r";
     |SINO;
          |SI nNroLinea = nTotalLineas;
               aAlfa = aCampo - aNroLinea;
               aLinea = aValor + "\r) /T (" + aAlfa + ")>> "; || ULTIMA LINEA
          |SINO;
               aLinea = aValor + "\r";   || NI LA PRIMERA NI LA ULTIMA LINEA
          |FINSI;
     |FINSI;
     |SI nTotalLineas = 1;
          aAlfa = aCampo - aNroLinea;
          aLinea = aLinea + ") /T (" + aAlfa + ")>> "; || ULTIMA LINEA
     |FINSI;
|FPRC;

|PROCESO Clausulas;
     aNroLinea = aCampo % -102;
     nNroLinea = aNroLinea;

     |SI nNroLinea = 1;
          aLinea = "<< /V (" + aValor + "\r";
     |SINO;
          ||SI aCampo = "clausadici17"; |O aCampo = "clausulas17";
          |SI nNroLinea = nTipo;
               || aAlfa = aCampo - "17";
               aAlfa = aCampo - aNroLinea;
               aLinea = aValor + "\r) /T (" + aAlfa + ")>> "; || ULTIMA LINEA
          |SINO;
               aLinea = aValor + "\r";   || NI LA PRIMERA NI LA ULTIMA LINEA
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Pie;
     ||QBF aID;
     ||aLinea = "/ID [ " + aID + "] >>";
     ||XGRABA nCanal, aLinea;
     ||aLinea = ">> ";
     ||XGRABA nCanal, aLinea;
     ||aLinea = "endobj";
     ||XGRABA nCanal, aLinea;
     aLinea = "trailer";
     |XGRABA nCanal, aLinea;
     aLinea = "<</Root 1 0 R >>";
     |XGRABA nCanal, aLinea;
     aLinea = "%%EOF";
     |XGRABA nCanal, aLinea;
|FPRC;

|PROCESO Duplex;
     |SI nContador = 1;
          ||SI swDuplex = 1;
               aIP = "";
               |IP_REMOTO aIP;
               aOrigenD = aPathSerPDF + "enblanco.pdf";
               aDestinoD = aPathDestino + "no.pdf";
               |QBF aOrigen;
               |QBF aDestino;
               |SI aIP ! "";
                    |ENVIA_FICHERO aOrigenD, aDestinoD;
               |SINO;
                    |COPIA_FICHERO aOrigenD, aDestinoD;
               |FINSI;
          ||FINSI;
     |FINSI;
|FPRC;

|PROCESO c1;
     |SI swCopia = 2;                   || estoy en copia basica, tengo que
          aAlfa = "cb";                 || cambiar al fichero de nombre
          aDestino = aDestino - ".pdf"; || copio el pdf a¤adiendole al
          aDestino = aDestino + aAlfa;  || nombre lo de "cb"
          aDestino = aDestino + ".pdf";
     |FINSI;
     |SI nVoypor > 0;                   || para cuando haya mas de una
          aAlfa = nVoypor;              || hoja de claus. adici.
          aDestino = aDestino - ".pdf"; || copio el pdf a¤adiendole al
          aDestino = aDestino + aAlfa;  || nombre el numero de hoja
          aDestino = aDestino + ".pdf";
     |FINSI;
|FPRC;

|PROCESO c2;
     |SI swCopia = 2;                   || lo dejo todo como estaba
          aAlfa = "cb";
          aDestino = aDestino - ".pdf";
          aDestino = aDestino - aAlfa;
          aDestino = aDestino + ".pdf";
     |FINSI;
     |SI nVoypor > 0;
          aAlfa = nVoypor;
          aDestino = aDestino - ".pdf";
          aDestino = aDestino - aAlfa;
          aDestino = aDestino + ".pdf";
     |FINSI;
|FPRC;

|PROCESO c3;
     |SI swCopia = 2;                   || estoy en copia basica, tengo que
          aAlfa = "cb";                 || cambiar al fichero de nombre
          aLinea = aLinea - ".pdf)"; || copio el pdf a¤adiendole al
          aLinea = aLinea + aAlfa;  || nombre lo de "cb"
          aLinea = aLinea + ".pdf)";
     |FINSI;
     |SI nVoypor > 0;                   || para cuando haya mas de una hoja de
          aAlfa = nVoypor;              || claus. adici.
          aLinea = aLinea - ".pdf)";    || copio el pdf a¤adiendole al
          aLinea = aLinea + aAlfa;      || nombre el numero de hoja
          aLinea = aLinea + ".pdf)";
     |FINSI;
|FPRC;

|PROCESO c4;
     |SI swCopia = 2;                   || lo dejo todo como estaba
          aAlfa = "cb";
          aLinea = aLinea - ".pdf)";
          aLinea = aLinea - aAlfa;
          aLinea = aLinea + ".pdf)";
     |FINSI;
     |SI nVoypor > 0;                   || aqui lo dejo como estaba
          aAlfa = nVoypor;
          aLinea = aLinea - ".pdf)";
          aLinea = aLinea - aAlfa;
          aLinea = aLinea + ".pdf)";
     |FINSI;
|FPRC;

|PROCESO contador;
     nContador = nContador + 1;
     aContador = nContador;
     aContador = "000" + aContador;
     aContador = aContador % -103;
     aContador = "a" + aContador;
|FPRC;

|PROCESO nomrecib;
     |SI nContador = 1;       || nomina.pdf siempre el mismo
          |SI enWeb = 0;
               |SI aIP ! "";
                    |ENVIA_FICHERO aOrigen, aDestino;
               |SINO;
                    |COPIA_FICHERO aOrigen, aDestino;
               |FINSI;
          |SINO;
               |COPIA_FICHERO aOrigen, aDestino;
          |FINSI;
          |SI FSalida ! 0;
               aDestinoSpool = aOrigen;
               aOrigenSpool = aDirBase + "plantillas/" + "nomina.P00";
               |COPIA_FICHERO aOrigenSpool, aDestinoSpool;
               |SI enWeb = 0;
                    |SI aIP ! "";
                         |ENVIA_FICHERO aOrigen, aDestino;
                    |SINO;
                         |COPIA_FICHERO aOrigen, aDestino;
                    |FINSI;
               |SINO;
                    |COPIA_FICHERO aOrigen, aDestino;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO borra_fdf;
     |SI aProcedencia = "nomrecib";
          aAlfa = Usuario;
          || aAlfa = "C:\ds\spoolpdf\" + aAlfa + "\";
          aAlfa = aPathLocFDF + aAlfa + "\";
          |RMKDIR aAlfa;
          aAlfa = aAlfa + "*.fdf";
     |SINO;
          aAlfa = aPathDestino + "*.fdf";
     |FINSI;
     |R_PDIR aAlfa;
     |SI FSalida = 0;
          |PARA; |SI FSalida = 0; |HACIENDO;
               aAlfa1 = aAlfa;
               |RFBORRA aAlfa1;
               |R_SDIR aAlfa;
          |SIGUE;
     |FINSI;
|FPRC;

|PROCESO copia_fichero;
     |SI enWeb = 0;
          aIP = "";
          |IP_REMOTO aIP;
          aOrigen = aPathSerPDF + aArchivoPDF;
          aDestino = aPathLocFDF + aArchivoPDF;
          |RMKDIR aPathLocFDF;
          |SI FSalida ! 0;         || O ya existia o no ha podido: lo compruebo
               aAlfa = aPathLocFDF + "prueba.txt";|| grabo un ficherito chorra
               |XABRE "CW", aAlfa, nCanal;        || para comprobar que ha ido
               |XCIERRA nCanal;                   || bien la creacion del
               |XABRE "CE", aAlfa, nCanal;        || spoolpdf
                    |SI FSalida ] 0;
                         |RFBORRA aAlfa;
                    |SINO;
                         |MENAV "    Error en la creacion de la carpeta SPOOLPDF.";
                    |FINSI;
               |XCIERRA nCanal;
          |FINSI;
          |QBF aOrigen;
          |QBF aDestino;
          |SI eaIdioma ! "CAS";
               |SI eaIdioma = "CAT";
                    eaIdioma = "cat";
               |FINSI;
               |SI eaIdioma = "VAL";
                    eaIdioma = "val";
               |FINSI;
               |SI eaIdioma = "GAL";
                    eaIdioma = "gal";
               |FINSI;
          |FINSI;

          |SI eaIdioma ! "CAS";
               aOrigenMulti = aOrigen;
               |QBF aOrigenMulti;
               aOrigenMulti = aOrigenMulti - ".pdf";
               aOrigenMulti = aOrigenMulti + "_" + eaIdioma + ".pdf";

               |XABRE "", aOrigenMulti, nCanalIdi;
               |SI FSalida = 0;
                    aOrigen = aOrigenMulti;
               |FINSI;
               |XCIERRA nCanalIdi;
          |FINSI;

          |HAZ c1;
          |SI aProcedencia = "nomrecib";     || para el nomrecib
               |HAZ nomrecib;
          |SINO;
               |SI aProcedencia = "labllaca";     || para el llamamiento
                    |SI nTotalHojas > 1;
                         aAlfa = nHoja;
                         aAlfa = ("00" + aAlfa) % -102;
                         aDestino = aDestino - ".pdf";
                         aDestino = aDestino + "_" + aAlfa + ".pdf";
                    |FINSI;
               |FINSI;
               |SI aIP ! "";
                    |ENVIA_FICHERO aOrigen, aDestino;
               |SINO;
                    |COPIA_FICHERO aOrigen, aDestino;
               |FINSI;
          |FINSI;
          |HAZ c2;

          aOrigen = aPathSerFDF + aArchivoFDF;
          aDestino = aPathLocFDF + aArchivoFDF;

          aCadena = aPathReader;
          aMiCaracter = "\";
          nPorDonde = -1;
          |HAZ BuscaCaracter;
          nPos = nPos -1;
          nPos = nPos + 100;
          nPos = (-1) * nPos;
          aEjecutable = aPathReader % nPos;
          aPathReader = aPathReader - aEjecutable;
          |SI #labcontr#65 = 0;
               aPathDestino2 = aPathReader;
          |FINSI;
          |SI aProcedencia = "nomrecib";
               aAlfa = Usuario;
               || aDestino = "C:\ds\spoolpdf\" + aAlfa + "\" + aContador + ".fdf";
               aDestino = aPathLocFDF + aAlfa + "\" + aContador + ".fdf";
          |SINO;
               aDestino = aPathDestino + aContador + ".fdf";
               aDestino2 = aPathDestino2 + aContador + ".fdf";
          |FINSI;
          |SI nContador = 1;       || LA PRIMERA VEZ BORRA LOS FDF QUE HAYA
               |HAZ borra_fdf;
          |FINSI;
          |HAZ Duplex;

          |QBF aOrigen;
          |QBF aDestino;
          |SI aIP ! "";
               |ENVIA_FICHERO aOrigen, aDestino;
          |SINO;
               |COPIA_FICHERO aOrigen, aDestino;
          |FINSI;

          |SI #labcontr#65 = 0;
               |SI aIP ! "";
                    |ENVIA_FICHERO aOrigen, aDestino2;
               |SINO;
                    |COPIA_FICHERO aOrigen, aDestino2;
               |FINSI;
          |FINSI;
     |SINO;
          aArchivo = eaWebArchivo - ".txt" + "_" + aContador;
          aArchivo = aArchivo + ".pdf";

          aOrigen = aPathSerPDF + aArchivoPDF;
          |QBF aOrigen;

          aDestino = eaWebDirDes;  |QBF aDestino;
          aDestino = aDestino + aArchivo;
          |QBF aDestino;

          |HAZ c1;
          |COPIA_FICHERO aOrigen, aDestino;
          |HAZ c2;

          aArchivo = eaWebArchivo - ".txt";
          aArchivo = aArchivo + ".fdf";
          aOrigen = aPathSerFDF + aArchivoFDF;
          |QBF aOrigen;

          |SI nContador = 1;       || LA PRIMERA VEZ BORRA LOS FDF QUE HAYA
               |PARA nNume = 0; |SI nNume < 50; |HACIENDO nNume = nNume + 1;
                    aAlfa2 = nNume;
                    aAlfa2 = "a" + nNume + ".fdf";
                    aAlfa2 = eaWebDirDes + eaWebArchivo + "_" + aAlfa2;
                    |XABRE "CE", aAlfa2, nCanal;
                    |SI FSalida ] 0;
                         |FBORRA aAlfa2;
                    |FINSI;
               |SIGUE;
          |FINSI;
          aDestino = eaWebDirDes;  |QBF aDestino;
          aDestino = aDestino + eaWebArchivo + "_" + aContador + ".fdf";
          |QBF aDestino;

          |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;
|FPRC;

|PROCESO ListaImpares;
     swEsImpar = 0;
     |SI aArchivoPDF = "pe-229.pdf"; |O aArchivoPDF = "pe-230.pdf";
     |O aArchivoPDF = "pe-197.pdf"; |O aArchivoPDF = "pe-200.pdf";
     |O aArchivoPDF = "pe-191.pdf"; |O aArchivoPDF = "clauconf.pdf";
          swEsImpar = 1;
     |FINSI;

     nNume = nNCA / 2;
     nNume1 = nNume ! 0;
     |SI nNume ! nNume1;
          || el numero de hojas de clausulas es impar;
          |SI nVoypor = nNCA;
               || ademas, voy por la ultima hoja de clausulas,
               || tendre que a¤adir una en blanco despues
               swEsImpar = 1;
          |FINSI;
     |FINSI;

     |SI swEsImpar = 1;
          |SI aListaImpares = "";
               aEspacio = "";
          |SINO;
               aEspacio = " ";
          |FINSI;
          aListaImpares = aListaImpares + aEspacio + &34 + aContador + ".fdf" + &34;
     |FINSI;
|FPRC;

|PROCESO Rutas;
     || el programa esta linkado a la ett, pongo esto, para hacer los PATH_DAT
     |DBASS aFich;
     |QBF aFich;
     aFich = aFich + "laboral/fich/";
     |PATH_DAT #pdfcontr#aFich#;
     |PATH_DAT #labcontr#aFich#;
|FPRC;

|PROGRAMA;
     |HAZ Rutas;
     |HAZ control;
     |HAZ CargaInicial;
     |HAZ BuscaRuta;

     |SI aPathReader = "";
          aAlfa = "    ATENCION: No estan definidos los parametros de impresion de Adobe Acrobat";
          |MENAV aAlfa;
          aAlfa = "    Se va a proceder a su instalacion.";
          |MENAV aAlfa;
          |MODULO aVengoDe;
          |SUB_EJECUTA "pdfcontr";
     |FINSI;
     |HAZ BuscaRuta;
     |XABRE "CE", aPathReader, nCanal;
     |SI aPathReader = ""; |O FSalida < 0;
          |CIERRA #pdfcontr;
          aAlfa = "    No se ha encontrado el directorio donde esta instalado ";
          aAlfa = aAlfa + "Adobe Acrobat Reader. ";
          |MENAV aAlfa;
          aAlfa = "    Se va a proceder a su instalacion.";
          |MENAV aAlfa;
          |MODULO aVengoDe;
          |SUB_EJECUTA "pdfcontr";
     |FINSI;

     aAlfa = Usuario; |QBT aAlfa;    aAlfa = ("c" + aAlfa) % 108;
     |NOME_DAT #100 aAlfa;
     |ABRE #pdfimtmp;

     |SI swCopia = 1;
          aArchivoPDF = aArchivo;
          aArchivoFDF = aArchivo - ".pdf";
          aArchivoFDF = aArchivoFDF + "_" + Usuario + ".fdf";
     |SINO;
          aArchivoPDF = aArchivo;
          aArchivoFDF = aArchivoPDF - ".pdf";
          aArchivoFDF = aArchivoFDF + "cb_" + Usuario + ".fdf";
     |FINSI;

     aAlfa = aPathSerFDF + aArchivoFDF;
     |SI aIP = "";
          |RFBORRA aAlfa;
     |SINO;
          |FBORRA aAlfa;
     |FINSI;

     |XABRE "A", aAlfa, nCanal;
     |HAZ contador;
     |HAZ Cabecera;
     |HAZ Objeto1;
     |HAZ Objeto2;
     |HAZ Objeto3;
     |HAZ Pie;
     |XCIERRA nCanal;
     |CIERRA #pdfimtmp;
     |HAZ BuscaRuta;
     |HAZ copia_fichero;
     |SI nTipoSalida = 0;
          aListaFich = " " + &34 + aContador + ".fdf" + &34 + aListaFich;
          |SI swCopia = 0; |O swCopia = 1;
               |SI aListaFichNor = "";
                    aEspacio = "";
               |SINO;
                    aEspacio = " ";
               |FINSI;
               aListaFichNor = aListaFichNor + aEspacio + &34 + aContador + ".fdf" + &34;
          |FINSI;
          |SI swCopia = 2;
               |SI aListaFichCop = "";
                    aEspacio = "";
               |SINO;
                    aEspacio = " ";
               |FINSI;
               aListaFichCop = aListaFichCop + aEspacio + &34 + aContador + ".fdf" + &34;
          |FINSI;
          |SI #labcontr#79 = 1; |Y #labcontr#80 = "S";
               |HAZ ListaImpares;
          |FINSI;
     |SINO;
          aListaFich = aListaFich + " " + &34 + aContador + ".fdf" + &34;
     |FINSI;
|FPRO;
