|FICHEROS;
     ettperso #0;    || personal
     ettpers0 #1;    || experiencia
     ettpers1 #2;    || formacion
     ettpers2 #3;    || puestos trabajo
     ettpers3 #4;    || observaciones
     ettcontr #100;  || control de la aplicacion
     :/integral/def/dsam0101 #711;
     :/integral/def/dsam0103 #712;
     :/basica/def/agidanif #713;
     :/laboral/def/spaisxtc #714;
     :/laboral/def/tbonvfor #715;
     :/laboral/def/taiclaoc #716;
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    x = "";
    final = "";
    letra = "";
    lugar = 0;
    resto = 0;
    nifnum = 0;

    corta = 0;
    jg = 0;
    sitio = 0;
    nafss = "";

{-1}menu   = "";
    menu1  = "1754";
    menu2  = "1";
    menu3  = "5";
    menu4  = "Experiencia.";
    menu5  = "Formacion.";
    menu6  = "Puestos solicitados.";
    menu7  = "Observaciones.";
    menu8  = "Fotografia.";
    menu9  = "Datos Complementarios.";
    &modoex = 0;
    &person = 0;
    sw_repinta = 0;

     aNifQbf1   = "";
     aNifQbf2   = "";

     &eaVarDni  = "";
     &enCalcCif = 0;


     nModo = 0;
     aAlfa1 = "";
|FIN;
____________________________________/
|PROCESO mayus; |TIPO 0;
    #0Campo = #0Campo > #0Campo;
|PINTA #0Campo;
|FPRC;

|PROCESO alta; |TIPO 7;
    modo = (FEntrada / 100) ! 0;
|SI modo = 1;
    |LEE 000#0u;
    |SI FSalida = 0;
            nume1 = #0#0 + 1;
        |DDEFECTO #0;
            #0#0 = nume1;
        |PINTA #0#0;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO nif; |TIPO 6;
     eaVarDni  = #0Campo;
     enCalcCif = 0;
     |HAZ CalculoDNI;

     aNifQbf1 = #0Campo;   |QBF aNifQbf1;
     aNifQbf2 = eaVarDni;  |QBF aNifQbf2;

     |SI aNifQbf1 ! aNifQbf2; |Y aNifQbf1 ! "";
         |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
         |SI FSalida = 0;
             #0Campo = eaVarDni;  |PINTA #0Campo;
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO nafss; |TIPO 0;
    alfa1 = #0Campo;
|QBF alfa1;
    alfa2 = alfa1 % 0;    nume1 = alfa2;
|SI nume1 ! 0;
    |SI nume1 > 10;     alfa1 = alfa1 % 110;     |FINSI;
        nafss = alfa1;

        alfa2 = alfa1 % 102; alfa3 = alfa1 % -108;
        alfa4 = alfa3 % 101;
    |SI alfa4 = "0";
            alfa3 = alfa3 % 207;
            alfa1 = alfa2 + alfa3;
    |FINSI;
        nume1 = alfa1;
        nume2 = nume1 / 97;
    |HAZ trunca2;
    |SI nume1 ] 0; |Y nume1 [ 99;
            alfa1 = nafss;
        |QBF alfa1;
            alfa2 = nume1;
            alfa2 = "00" + alfa2;
            alfa2 = alfa2 % -102;
            alfa1 = alfa1 + alfa2;
            #0Campo = alfa1;
        |PINTA #0Campo;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO trunca2;
    corta = 0;
    alfa1 = nume2;  alfa2 = alfa1 % 0;  nume3 = alfa2;
|PARA jg = nume3; |SI jg ] 1;|Y corta = 0; |HACIENDO jg = jg - 1;
        sitio = (jg * 100) + 1;
        alfa2 = alfa1 % sitio;
    |SI alfa2 = ".";  corta = jg;  |FINSI;
|SIGUE;
|SI corta ! 0;
        sitio = 100 + corta - 1;
        alfa1 = alfa1 % sitio;
|FINSI;
    nume3 = alfa1;
    nume1 = nume1 - (nume3 * 97);
|FPRC;

|PROCESO borra_3;
|ABRE #4;
    #4#0 = #0#0;
|LEE 000#4=;
|SI FSalida = 0;
    |BORRA 020#4a;
|FINSI;
|CIERRA #4;
|FPRC;

|PROCESO borra_21;
|BORRA 020#3a;
|FPRC;

|DEFBUCLE borra_2;
 #3#1;
 ;
 #0#0,    1;
 #0#0, 9999;
 ;
 NULL, borra_21;
|FIN;

|PROCESO borra_11;
|BORRA 020#2a;
|FPRC;

|DEFBUCLE borra_1;
 #2#1;
 ;
 #0#0,    1;
 #0#0, 9999;
 ;
 NULL, borra_11;
|FIN;

|PROCESO borra_01;
|BORRA 020#1a;
|FPRC;

|DEFBUCLE borra_0;
 #1#1;
 ;
 #0#0,    1;
 #0#0, 9999;
 ;
 NULL, borra_01;
|FIN;

|PROCESO tipo2; |TIPO 2;
    modo = (FEntrada / 100) ! 0;
|SI modo = 3;
    |ATRI P;   |MENSA "    Borrando relaciones ...";   |ATRI 0;
    |BUCLE borra_0;      || experiencia
    |BUCLE borra_1;      || formacion
    |BUCLE borra_2;      || puestos trabajo
    |HAZ borra_3;        || observaciones
    |ATRI R;   |BLIN 4;  |ATRI 0;
|FINSI;
|FPRC;

|PROCESO tipo13; |TIPO 13;
     |SI sw_repinta = 0;
          |SI Campo > 25; |O modo = 4;
               |HAZ el_menu;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO tipo30; |TIPO 30;
|HAZ el_menu;
|FPRC;

|PROCESO repinta;
    sw_repinta = 1;
|PINPA #0#0;
|PINDA #0#0;
    sw_repinta = 0;
|FPRC;

|PROCESO borra_win;
|PARA para1 = 10; |SI para1 [ 23; |HACIENDO para1 = para1 + 1;
    |BLIN para1;
|SIGUE;
|FPRC;

|PROCESO Complementarios;
     |PINTA #0#1;
     |PINDA #0#1;
|FPRC;

|PROCESO el_menu;
    modo = (FEntrada / 100) ! 0;
|SI modo ! 3;|Y modo ! 0;
        person = #0#0;
        modoex = modo;
    |HAZ repinta;
    |PARA; |SI; |HACIENDO;
        |MENUG menu;
        |SI FSalida > 0;
                menu2 = FSalida;
            |SI FSalida ! 5;
                |HAZ borra_win;
            |FINSI;
            |SI FSalida = 1;  |SUB_EJECUTA "ettpers0"; |FINSI;
            |SI FSalida = 2;  |SUB_EJECUTA "ettpers1"; |FINSI;
            |SI FSalida = 3;  |SUB_EJECUTA "ettpers2"; |FINSI;
            |SI FSalida = 4;  |SUB_EJECUTA "ettpers3"; |FINSI;
            |SI FSalida = 5;  |HAZ VerFoto;            |FINSI;
            |SI FSalida = 6;
                ||HAZ Complementarios;
            |FINSI;
        |SINO;
            |SAL;
        |FINSI;
        |HAZ repinta;
    |SIGUE;
    |HAZ repinta;
|FINSI;
|FPRC;

____________________ PROCESOS PARA EL TRATAMIENTO DE IMAGENES ________________
|VARIABLES;
     &pasa     = "";
     &orden    = "";
     nSw       = 0;
     i         = 0;
     aIP       = "";
     aAlfa     = "";
     nBoton1   = 0;
     aPath     = "";
     aRuta     = "";
     aRuta0    = "";
     aFic      = "";
     aLon      = "";
     nPos      = 0;
     aDes      = "";
     &nCandidato = 0;
|FIN;

|PROCESO VerFoto; |TIPO 7;
     |ABRE #100; |LEE 000#100p; |CIERRA #100;
     aPath = #100#36; |QBF aPath;
     |MKDIR aPath;

     aFic = #0#25; |QBF aFic;
     |SI aFic ! "";
          aFic = aPath + aFic;
          |XABRE "E", aFic, 0;
          |SI FSalida = 0;
               |IP_REMOTO aIP;
               |SI aIP ! "";
                    aAlfa = "c:\tmp\"; |RMKDIR aAlfa;
                    aDes = "c:\tmp\" + #0#1; |QBF aDes;
                    |ENVIA_FICHERO aFic, aDes;
               |SINO;
                    aDes = aFic;
               |FINSI;
               aFic = "IMAGEN," + aDes;
               |CONTROL_SIMPLE 0, aFic , 1057, 2279, NULL;
               nBoton1 = FSalida;
               |ESTADO_CONTROL nBoton1, "ENABLE";
               |PAUSA;
               |FIN_CONTROL_WINDOWS nBoton1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Asigna;
     aRuta = "F";
     aFic = "";
     |BROWSE aRuta;

     |SI aRuta ! "F";
          nSw = 0;
          aRuta0 = "";
          aLon = aRuta % A0;
          |PARA i = 1; |SI i < aLon; |HACIENDO i = i + 1;
               nPos = -1 * (i * 100 + 1);
               aAlfa = aRuta % nPos;
               |SI aAlfa = "/"; |O aAlfa = "\"; |O nSw = 1;
                    nSw = 1;
                    aRuta0 = aAlfa + aRuta0;
               |SINO;
                    aRuta0 = aAlfa + aRuta0;
                    aFic = aAlfa + aFic;
               |FINSI;
          |SIGUE;
          |QBT aFic;
          aFic = aFic < "";
          aDes = aPath + aFic;
          aDes = aDes < "";
          aIP = "";
          |IP_REMOTO aIP;
          |XABRE "E", aDes, 0;
          |SI FSalida = 0;
               ||aAlfa = "0000Existe el fichero en el directorio" + &13 +"NO SE COPIA";
               ||MENAV aAlfa;
          |SINO;
               |SI aIP = "";
                    |COPIA_FICHERO aRuta0, aDes;
               |SINO;
                    |RECIBE_FICHERO aRuta0, aDes;
               |FINSI;
          |FINSI;
          #0#25 = aFic; |PINTA #0#25;
          |HAZ VerFoto;
     |FINSI;
|FPRC;

|PROCESO Foto;
     ||SI FSalida = 10;
     |SI FSalida = 15;
          |HAZ PathFotos;
          |HAZ Asigna
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO PathFotos;
     |ABRE #100; |LEE 000#100p; |CIERRA #100;
     aPath = #100#36; |QBF aPath;
     |MKDIR aPath;
     |SI FSalida = 0;
          |SI aPath = "";
               aAlfa = "0000No esta definido el directorio de imagenes";
          |SINO;
               |RMDIR aPath;
               aAlfa = "0000No existe el directorio";
               aAlfa = aAlfa + &13 + "[" + aPath + "]";
          |FINSI;
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO CambiaDNI; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     aAlfa1 = Contenido;
     |SI nModo = 1; |O #0Campo ! aAlfa1;
          ||eaCodNif = #0#7;
          |ABRE #712;
          #712#0 = #0#3;
          |LEE 000#712=;
          |SI FSalida = 0;
               ||HAZ LeeNifs;
               #0#2 = #712#1; |PINTA #0#2;   || Apellidos y nombre
               aAlfa1 = #712#9 + " " + #712#2; aAlfa1 = aAlfa1 % 130; |QBF aAlfa1;
               #0#5 = aAlfa1; |PINTA #0#5;   || domicilio
               #0#6 = #712#3; |PINTA #0#6;   || numero
               #0#46 = #712#16; |PINTA #0#46;  || escalera
               #0#7 = #712#10; |PINTA #0#7;  || piso
               #0#45 = #712#11; |PINTA #0#45;  || puerta
               #0#9 = #712#4; |PINTA #0#9;   || CP Provincia
               #0#10 = #712#5; |PINTA #0#10;   || CP Poblacion
               #0#8 = #712#6; |PINTA #0#8;   || Poblacion
               #0#47 = #712#7; |PINTA #0#47;  || Provincia
               #0#11 = #712#8; |PINTA #0#11;   || Telefono

               |ABRE #713;
               #713#0 = #0#3;   || DNI
               #713#1 = 01;     || TITULAR
               |LEE 000#713=;
               |SI FSalida = 0;
                    #0#14 = #713#13; |PINTA #0#14; || Fecha nacimiento
               |FINSI;
               |CIERRA #713;
          |FINSI;
          |CIERRA #712;
     |FINSI;
|FPRC;

|PROCESO ConsultaLoc; |TIPO 66;
     |DBASS aAlfa;
     |QBF aAlfa;
     aAlfa = aAlfa + "integral/fich/";
     |PATH_DAT #711 aAlfa;
     |ABRE #711;
     |CONSULTA_CLAVE #1#711;
     |SI FSalida = 0;
          #0#9 = #711#0; |PINTA #0#9;
          #0#10 = #711#1; |PINTA #0#10;
          #0#8 = #711#3; |PINTA #0#8;
          #0#47 = #711#2; |PINTA #0#47;
     |FINSI;
     |CIERRA #711;
|FPRC;

|PROCESO PintaLocalidad;
     |DBASS aAlfa;
     |QBF aAlfa;
     aAlfa = aAlfa + "integral/fich/";
     |PATH_DAT #711 aAlfa;
     |ABRE #711;
     #711#0 = #0#9;
     #711#1 = #0#10;
     |LEE 000#711=;
     |SI FSalida = 0;
          #0#8 = #711#3; |PINTA #0#8;
          #0#47 = #711#2; |PINTA #0#47;
     |SINO;
          |SI Campo = 10;
               |MENAV "    Código Postal no encontrado. Pulse F11 para consultar";
               |ERROR;
          |FINSI;
     |FINSI;
     |CIERRA #711;
|FPRC;

|PROCESO ConsultaPais; |TIPO 66;
     |DBASS aAlfa;
     |QBF aAlfa;
     aAlfa = aAlfa + "laboral/fich/";
     |PATH_DAT #714 aAlfa;
     |ABRE #714;
     |CONSULTA_CLAVE #1#714;
     |SI FSalida = 0;
          aAlfa = #714#0;
          |QBF aAlfa;
          aAlfa = aAlfa + " " + #714#1;
          #0#13 = aAlfa;
          |PINTA #0#13;
     |FINSI;
     |CIERRA #714;
|FPRC;


|PROCESO ConsultaNivel; |TIPO 66;
     |DBASS aAlfa;
     |QBF aAlfa;
     aAlfa = aAlfa + "laboral/fich/";
     |PATH_DAT #715 aAlfa;
     |ABRE #715;
     |CONSULTA_CLAVE #1#715;
     |SI FSalida = 0;
          #0#48 = #715#0; |PINTA #0#48;
          #0#22 = #715#1; |PINTA #0#22;
     |FINSI;
     |CIERRA #715;
|FPRC;

|PROCESO PintaNivel;
     |DBASS aAlfa;
     |QBF aAlfa;
     aAlfa = aAlfa + "laboral/fich/";
     |PATH_DAT #715 aAlfa;
     |ABRE #715;
     #715#0 = #0#48;
     |LEE 000#715.=;
     |SI FSalida = 0;
          #0#22 = #715#1; |PINTA #0#22;
     |FINSI;
     |CIERRA #715;
|FPRC;

|PROCESO ConsultaProf; |TIPO 66;
     |DBASS aAlfa;
     |QBF aAlfa;
     aAlfa = aAlfa + "laboral/fich/";
     |PATH_DAT #716 aAlfa;
     |ABRE #716;
     |CONSULTA_CLAVE #1#716;
     |SI FSalida = 0;
          #0#49 = #716#0; |PINTA #0#49;
          #0#50 = #716#1; |PINTA #0#50;
     |FINSI;
     |CIERRA #716;
|FPRC;

|PROCESO PintaProf;
     |DBASS aAlfa;
     |QBF aAlfa;
     aAlfa = aAlfa + "laboral/fich/";
     |PATH_DAT #716 aAlfa;
     |ABRE #716;
     #716#0 = #0#49;
     |LEE 000#716=;
     |SI FSalida = 0;
          #0#50 = #716#1; |PINTA #0#50;
     |FINSI;
     |CIERRA #716;
|FPRC;
