|FICHEROS;
    ettempar #0;    || emision partes
    ettpedi1 #1;    || pedidos cabs.
    ettpedi2 #2;    || pedidos lins.
    ettparte #3;    || partes de horas cabs.
    ettparli #4;    || partes de horas lins.
    ettocuca #5;    || ocupacion cabs.
    ettoculi #6;    || ocupacion lins.

    labempre #10;   || empresas AGINOM
    labtraba #12;   || trabajadores AGINOM
    ettcontr #13;   || control AGIETT
    ettclien #14;   || clientes AGIETT
    ettprovi #15;   || provincias AGIETT
    ettperso #16;   || personal AGIETT
    ettpuest #17;   || puestos AGIETT
    etttarca #18;   || tarifas cabs. AGIETT
    labregis #19;   || registro contratos AGINOM

     ettemitm #100;
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    nume5 = 0;
    nume6 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    &hpedido = 0;
    &hperson = 0;
    &hcopias = 0;
    &hfirmas = @;
    &hfallos = 0;
    informe = "";
    dia_semana = 0;
    fecha = @;
    ultimo = 0;
    des = 0;
    has = 0;
    topes = 0;
    mes_ant = 0;
    mes_act = 0;
    fec_act = @;
    apunta1 = 0;
    apunta2 = 0;
    apunta3 = 0;
    apunta4 = 0;
    ulti_fec = @;
    despa = @;
    haspa = @;
    sw_repite = 0;
    nro_parte = 0;
    sw_ultimo = 0;

     &enEmpLab = 0;
     &enTraLab = 0;
     &enMes = 0;
     &enAny = 0;
     aAlfa = "";
     aAlfa1 = "";
     aAlfa2 = "";
     nNume = 0;
     fFechaAlta = @;
     fFechaBaja = @;
     fFechaIniMes = @;
     fFechaFinMes = @;
|FIN;
____________________________________/
|PROCESO def_hasta; |TIPO 7;
|SI #2#30 = "N";
        #0#7 = #0#6 + 0.001;       || fecha inicial mas 1 mes
|SINO;
        #0#7 = #2#33;              || fecha ultimo parte
|FINSI;
|PINTA #0#7;
|FPRC;

|PROCESO fec_hasta; |TIPO 0;
|SI #0#7 < #0#6;
    |MENAV "    La fecha final PROVISIONAL debera ser mayor o igual que la inicial ...";
    |ERROR;
|FINSI;
|SI #0#7 < #2#33;
    |MENAV "    La fecha final PROVISIONAL debera ser mayor o igual que la anterior ...";
        #0#7 = #2#33;
    |PINTA #0#7;
    |ERROR;
|FINSI;
|FPRC;
____________________________________/
|PROCESO informes;
|SI hfallos = 0;         || si da error algun informe no continua
    |SI FSalida = 0;
            #2#30 = "S";           || impreso SI
        |SI ulti_fec ! "00.00.0000";
                #2#33 = ulti_fec;  || fecha ultimo parte
        |FINSI;
        |GRABA 020#2a;
        |PRINT;
        |PIEINF;
    |SINO;
            alfa1 = "    AVISO: Problemas con el informe [" + informe + "] ...";
        |MENAV alfa1;
            hfallos = 1;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO imprime_parte;
    seleccio = 1;
    hfallos = 0;
|PARA para1 = 1; |SI para1 [ hcopias; |HACIENDO para1 = para1 + 1;
        informe = "ett01par"; |HAZ informes;
|SIGUE;
|FPRC;

|DEFBUCLE reimprime;
 #3#2;
 ;
 #2#0, #2#2, nro_parte;
 #2#0, #2#2, 999999999999;
 ;
 NULL, imprime_parte;
|FIN;

|PROCESO busca_mes;
    nume6 = -1;
|PARA para1 = 8; |SI para1 [ 14; |HACIENDO para1 = para1 + 1;
    |SI #3para1 ! "01.01.0000";
            alfa1 = #3para1;
            alfa2 = alfa1 % 402;
            nume5 = alfa2;
        |SI nume6 = -1;  nume6 = nume5; |FINSI;
        |SI nume5 = nume6;
                #3#55 = nume5;          || mes AGINOM
                alfa2 = alfa1 % -104;
                #3#56 = alfa2;          || anyo AGINOM
        |SINO;
            |MENAV "    ATENCION! Parte conteniendo meses distintos ...";
        |FINSI;
    |FINSI;
|SIGUE;
|FPRC;

|PROCESO grabalo;
    #3#47 = #3#22+#3#23+#3#24+#3#25+#3#26+#3#27+#3#28; || tot.horas
|HAZ busca_mes;
|GRABA 020#3a;
|SI FSalida = 0;
    |SI #3#8  ! "01.01.0000"; ulti_fec = #3#8;    |FINSI;
    |SI #3#9  ! "01.01.0000"; ulti_fec = #3#9;    |FINSI;
    |SI #3#10 ! "01.01.0000"; ulti_fec = #3#10;   |FINSI;
    |SI #3#11 ! "01.01.0000"; ulti_fec = #3#11;   |FINSI;
    |SI #3#12 ! "01.01.0000"; ulti_fec = #3#12;   |FINSI;
    |SI #3#13 ! "01.01.0000"; ulti_fec = #3#13;   |FINSI;
    |SI #3#14 ! "01.01.0000"; ulti_fec = #3#14;   |FINSI;
        #4#0 = #3#0;     || nro.parte
        #4#1 = 1;        || linea
        #4#2 = "L";      || dia semana
        #4#3 = 101;      || cod.concepto
        #4#4 = 0;        || importe
        #4#5 = "";       || descripcion
    |GRABA 020#4n;
|FINSI;
|LIBERA #3;
|FPRC;

|PROCESO ruptura_mes;
|SI mes_ant = -1;
        alfa1 = fec_act; alfa1 = alfa1 % 402;     mes_ant = alfa1;
|FINSI;
    alfa1 = fec_act;     alfa1 = alfa1 % 402;     mes_act = alfa1;
|SI mes_ant ! mes_act;
    |HAZ grabalo;
    |HAZ ultimo;
|FINSI;
|FPRC;

|PROCESO ultimo;
|LEE 000#3u;
|SI FSalida = 0;
        ultimo = #3#0 + 1;
|SINO;
        ultimo = 1;
|FINSI;
    mes_ant = -1;
|DDEFECTO #3;
    #3#0 = ultimo;        || numero parte
|GRABA 020#3b;
    #3#1 = #2#2;         || codigo trabajador
    #3#2 = #1#2;         || codigo cliente
    #3#3 = #2#0;         || codigo pedido
    #3#4 = #2#24;        || codigo AGINOM
    #3#7 = #1#1;         || fecha pedido
    #3#43 = #1#7;        || desde hora 1
    #3#44 = #1#8;        || hasta hora 1
    #3#45 = #1#9;        || desde hora 2
    #3#46 = #1#10;       || hasta hora 2
    #3#48 = #2#1;        || linea del pedido
    #3#51 = #2#16;       || codigo tarifa
    #3#52 = #2#17;       || nivel tarifa
    #3#53 = #2#18 + #2#22 + #2#23; || ~precio hora nomina
    #3#54 = #2#21;                 || ~precio hora fra.
|GRABA 020#3a;

|SI sw_ultimo = 0;
        nro_parte = #3#0;
        sw_ultimo = 1;
|FINSI;
|FPRC;

|PROCESO calcula_parte;
|HAZ ultimo;

    nume2 = dia_semana - 1;
    topes = dia_semana + 7;
|PARA apunta1 = 8; |SI apunta1 [ topes; |HACIENDO apunta1 = apunta1 + 1;
        nume1 = fecha;
        nume1 = nume1 - nume2;
    |SI nume1 ] des;|Y nume1 [ has;

            fec_act = nume1;
        |HAZ ruptura_mes;               || cambio de mes en un mismo parte

            #3apunta1 = nume1;          || dia
        |SI nume1 = 729390;   #3apunta1 = "01.01.1997";     |FINSI;
        |SI nume1 = 730851;   #3apunta1 = "01.01.2001";     |FINSI;
            apunta4 = apunta1 + 55;     || puntero trabaja S/N
        |SI #1apunta4 = "S";
                apunta2 = apunta1 + 7;  || puntero marca
                #3apunta2 = "*";        || marca
                apunta3 = apunta1 + 14; || puntero horas dia
                #3apunta3 = #2#26;      || horas dia
        |FINSI;

    |FINSI;
        nume2 = nume2 - 1;
|SIGUE;

|HAZ grabalo;
|FPRC;

|PROCESO bucle_partes;
    fech1 = despa % 1;
    alfa1 = fech1;
    alfa1 = alfa1 % 11;
|QBF alfa1;
|SI alfa1 = "Lunes";     dia_semana = 1;     |FINSI;
|SI alfa1 = "Martes";    dia_semana = 2;     |FINSI;
|SI alfa1 = "Miercoles"; dia_semana = 3;     |FINSI;
|SI alfa1 = "Jueves";    dia_semana = 4;     |FINSI;
|SI alfa1 = "Viernes";   dia_semana = 5;     |FINSI;
|SI alfa1 = "Sabado";    dia_semana = 6;     |FINSI;
|SI alfa1 = "Domingo";   dia_semana = 7;     |FINSI;
|PARA fecha = despa; |SI fecha < haspa; |HACIENDO fecha = fecha + 1;
    |SI dia_semana = 7;
        |HAZ calcula_parte;
            dia_semana = 0;
    |FINSI;
        dia_semana = dia_semana + 1;
|SIGUE;
|HAZ calcula_parte;
|FPRC;

|PROCESO lectura_varios;
|DDEFECTO #10;
|ABRE #10;
    #10#0 = #13#4;
|LEE 000#10=;            || lee empresas AGINOM
|CIERRA #10;

|DDEFECTO #12;
|ABRE #12;
    #12#0 = #13#4;
    #12#1 = #2#24;
|LEE 000#12=;            || lee trabajadores AGINOM
|CIERRA #12;

|DDEFECTO #14;
|ABRE #14;
    #14#0 = #1#2;
|LEE 000#14=;            || lee cliente AGIETT
|CIERRA #14;

|DDEFECTO #15;
|ABRE #15;
    #15#0 = #14#5;
|LEE 000#15=;            || lee provincia AGIETT
|CIERRA #15;

|DDEFECTO #16;
|ABRE #16;
    #16#0 = #2#2;
|LEE 000#16=;            || lee personal AGIETT
|CIERRA #16;

|DDEFECTO #17;
|ABRE #17;
    #17#0 = #1#59;
|LEE 000#17=;            || lee puestos AGIETT
|CIERRA #17;

|DDEFECTO #18;
|ABRE #18;
    #18#0 = #2#16;
    #18#1 = #2#17;
|LEE 000#18=;            || lee tarifa cabs. AGIETT
|CIERRA #18;
|FPRC;

|PROCESO tipo8;
|ABRE #13;
|LEE 000#13p;
|SI FSalida = 0;
        alfa1 = #13#3;    |QBF alfa1;
        alfa1 = alfa1 + "/fich/";
    |PATH_DAT #10 alfa1;
    |PATH_DAT #12 alfa1;
    |PATH_DAT #19 alfa1;
|SINO;
    |MENAV "    Control AGIETT sin definir ...";
|FINSI;
|CIERRA #13;
|FPRC;

|PROCESO ocupacion;
|ABRE #6;
|SELECT #2#6;
    #6#0 = #2#2;    || cod.personal
    #6#9 = #2#24;   || cod.AGINOM
|LEE 101#6=;
|SI FSalida = 0;
        #6#3 = #0#7;     || hasta fecha de ocupacion
    |GRABA 020#6a;
|FINSI;
|LIBERA #6;
|CIERRA #6;
|FPRC;

|PROCESO graba_registro;
|ABRE #19;
|SELECT #2#19;
    #19#1 = #13#4;  || empresa
    #19#2 = #2#24;  || trabajador
|LEE 101#19=;
|SI FSalida = 0;
        #19#8 = #0#7;         || fecha fin contrato provisional
        #19#24 = #19#8;       || (campo fecha)
        nume1 = #19#23;
        nume2 = #19#24;
        #19#10 = #19#10 + (nume2 - nume1) + 1;    || dias duracion
        #19#22 = #19#22 + (nume2 - nume1) + 1;    || acumulado dias
    |GRABA 020#19a;
|FINSI;
|LIBERA #19;
|CIERRA #19;
|FPRC;

|PROCESO Fechas;
     |ABRE #labtraba;
     #labtraba#0 = enEmpLab;
     #labtraba#1 = enTraLab;
     |LEE 000#labtraba.=;
     |SI FSalida = 0;
          aAlfa = #labtraba#36;
          fFechaAlta = aAlfa;
          aAlfa = #labtraba#37;
          fFechaBaja = aAlfa;
          aAlfa1 = enMes;
          |QBF aAlfa1;
          aAlfa1 = ("00" + aAlfa1) % -102;
          aAlfa2 = enAny;
          aAlfa = "01" + "." + aAlfa1 + "." + aAlfa2;
          fFechaIniMes = aAlfa;
          aAlfa = "31";
          |SI enMes = 4;  |O enMes = 6; |O enMes = 9; |O enMes = 11;
               aAlfa = "30";
          |FINSI;
          |SI enMes = 2;
               nNume = enAny $ 4;
               |SI nNume = 0;
                    aAlfa = "29";
               |SINO;
                    aAlfa = "28";
               |FINSI;
          |FINSI;
          aAlfa = aAlfa + "." + aAlfa1 + "." + aAlfa2;
          fFechaFinMes = aAlfa;
     |FINSI;
     |CIERRA #labtraba;

     #0#6 = fFechaIniMes;
     #0#7 = fFechaFinMes;
     |SI fFechaAlta ] fFechaIniMes; |Y fFechaAlta [ fFechaFinMes;
          #0#6 = fFechaAlta;
     |FINSI;
     |SI fFechaBaja ] fFechaIniMes; |Y fFechaBaja [ fFechaFinMes;
          #0#7 = fFechaBaja;
     |FINSI;
|FPRC;

|PROCESO Partes;
     hpedido = #100#1;
     enTraLab = #100#5;
     hperson = #100#8;

     #1#0 = hpedido;
     |LEE 020#1=;
     |SI FSalida = 0;
          #2#0 = hpedido;
          #2#1 = hperson;
          |LEE 121#2=;
          |SI FSalida = 0;
               |HAZ lectura_varios;
               |DDEFECTO #0;
               #0#0 = #1#0;      || codigo pedido
               #0#1 = #1#1;      || fecha pedido
               #0#2 = #16#0;     || codigo trabajador
               #0#3 = #16#2;     || nombre trabajador
               #0#4 = #14#0;     || codigo cliente
               #0#5 = #14#1;     || nombre cliente
               #0#6 = #1#5;      || desde contrato
               |SI #2#30 = "S";
                    #0#7 = #2#33; || hasta contrato (repitiendo)
               |SINO;
                    #0#7 = #1#6;  || hasta contrato (primera vez)
               |FINSI;
               #0#8 = #17#0;     || codigo puesto
               #0#9 = #17#1;     || nombre puesto

               /*
               |PUSHV 0501 2380;
               |PINPA #0#0;
               |PINDA #0#0;
               sw_repite = 0;
               |SI #0#7 = "01.01.1900";|Y #2#30 = "N";
                    |ENDATOS #1#0;
                    FEstado = FSalida;
                    |SI #0#7 = "01.01.1900";   FEstado = -1;  |FINSI;
               |SINO;
                    |CONFI "    SConfirme la impresion (S/N): ";
                    FEstado = FSalida;
               |FINSI;
               */
               |HAZ Fechas;
               despa = #0#6;
               haspa = #0#7;
               |SI despa [ haspa;|Y sw_repite = 0;
                    des = despa;         || desde contrato (numerico)
                    has = haspa;         || hasta contrato (numerico)
                    |HAZ bucle_partes;       || graba partes nuevos
                    |HAZ ocupacion;          || actualiza ocupacion
               |FINSI;
               seleccio = 0;
               |BUCLE reimprime;
          |FINSI;
          |LIBERA #2;
     |FINSI;
|FPRC;

|DEFBUCLE impresion;
     #100#2;
     ;
     "***", 00001;
     "***", 99999;
     ;
     NULL, Partes;
|FIN;

|PROGRAMA;
     hfallos = 1;
     nro_parte = 1;
     ulti_fec = "00.00.0000";
     |ABRE #1;
     |ABRE #2;
     |ABRE #3;
     |ABRE #4;
     alfa1 = Usuario;     |QBT alfa1;
     |NOME_DAT #100 alfa1;
     |ABRE #100;

     |HAZ tipo8;
     |IMPRE 0;
     |INFOR "ett01par";
     |SI FSalida = 0;
          |BUCLE impresion
     |FINSI;
     |FININF;
     |FINIMP;

     |CIERRA #100;
     |CIERRA #1;
     |CIERRA #2;
     |CIERRA #3;
     |CIERRA #4;
|FPRO;
