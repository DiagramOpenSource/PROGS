|FICHEROS;
    ettpedi6 #0;
    ettpedi5 #1;
    nomcateg #2;
    ettcontr;
    labcentr;
    nomtrato;
    tfggrcot;
|FIN;

|VARIABLES;
    alfa1 = "";
    aAlfa = "";
|FIN;

|PROCESO ayuda1; |TIPO 7;
|PUSHV 07102151;
|CUADRO 07 10 21 51;
|ATRI R;
||            1234567890123456789012345678901234567890
|PINTA 0811, " ----- CLAVES DE SALARIO POSIBLES ----- ";
|PINTA 0911, " (M) cotizacion mensual                 ";
|PINTA 1011, " (D) cotizacion diaria                  ";
|PINTA 1111, " (I) cotizacion irregular por dias      ";
|PINTA 1211, " (P) cotizacion irregular por horas     ";
|PINTA 1311, " (T) tiempo parcial cotizacion diaria   ";
|PINTA 1411, " (X) tiempo parcial cotizacion mensual  ";
|PINTA 1511, " (A) r.agrario, fijos con desempleo     ";
|PINTA 1611, " (B) r.agrario, fijos sin desempleo     ";
|PINTA 1711, " (E) r.agrario, eventuales              ";
|PINTA 1811, " (C) r.citricos, lunes/sabado   (1.33)  ";
|PINTA 1911, " (F) r.citricos, lunes/viernes  (1.61)  ";
|PINTA 2011, " (H) gerentes/cooperativistas autonomos ";
|ATRI 0;
|FPRC;

|PROCESO ayuda3; |TIPO 7;
|PUSHV 07101651;
|CUADRO 07 10 16 51;
|ATRI R;
||            9012345678901234567890123456789012345678
|PINTA 0811, " -----  TIPOS DE AJUSTE POSIBLES ------ ";
|PINTA 0911, " (0) Sin ajuste                         ";
|PINTA 1011, " (1) A brutos   (no cotiza -407-)       ";
|PINTA 1111, " (2) A brutos   (cotiza -120-)          ";
|PINTA 1211, " (3) A liquidos (no cotiza -407-)       ";
|PINTA 1311, " (4) A liquidos (cotiza -120-)          ";
|PINTA 1411, " (5) A brutos   (no cot./no IRPF -411-) ";
|PINTA 1511, " (6) A liquidos (no cot./no IRPF -411-) ";
|ATRI 0;
|FPRC;

|PROCESO ayuda2; |TIPO 0;
|POPV;
|FPRC;

|PROCESO t_parcial; |TIPO 7;
|SI #0#3 = "X";|O #0#3 = "T";|O #0#3 = "P";|O #0#3 = "I";
        alfa1 = "S";
|SINO;
        alfa1 = "N";
|FINSI;
|C_M #0#6, 1, alfa1;
|C_M #0#12, 1, alfa1;
|FPRC;

|PROCESO no_ajuste; |TIPO 7;
|SI #0#9 = 0;
        alfa1 = "N";
|SINO;
        alfa1 = "S";
|FINSI;
|C_M #0#10, 1, alfa1;
|FPRC;

|PROCESO def_categ; |TIPO 7;
|SI #1#24 = 0;
        #0#13 = #2#2;         || descripcion categoria
    |PINTA #0#13;
|FINSI;
|FPRC;

|PROCESO MinCen;
     #labcentr#0 = #ettcontr#4;
     #labcentr#1 = 001;
|FPRC;

|PROCESO MaxCen;
     #labcentr#0 = #ettcontr#4;
     #labcentr#1 = 999;
|FPRC;

|PROCESO ConsultaCentro; |TIPO 66;
     |ABRE #ettcontr; |LEE 000#ettcontr.p; |CIERRA #ettcontr;

     |ABRE #labcentr;
     |CONSULTA_F_CLAVE #1#labcentr, MinCen, MaxCen;
     |SI FSalida = 0;
          #0#14 = #labcentr#1; |PINTA #0#14;
          #0#15 = #labcentr#2; |PINTA #0#15;
     |FINSI;
     |CIERRA #labcentr;
|FPRC;

|PROCESO PintaCentro;
     |ABRE #ettcontr; |LEE 000#ettcontr.p; |CIERRA #ettcontr;

     |ABRE #labcentr;
     #labcentr#0 = #ettcontr#4;
     #labcentr#1 = #0#14;
     |LEE 000#labcentr.=;

     |SI FSalida = 0;
          #0#14 = #labcentr#1; |PINTA #0#14;
          #0#15 = #labcentr#2; |PINTA #0#15;

          #0#0 = #labcentr#16;
          |PINTA #0#0;
     |SINO;
          aAlfa = #ettcontr#4; |QBF aAlfa;
          aAlfa = ("00000" + aAlfa) % -105;
          aAlfa = "    Centro no encontrado en la empresa " + aAlfa + " (laboral)";
          |MENAV aAlfa;
          |ERROR;
     |FINSI;
     |CIERRA #labcentr;
|FPRC;

|PROCESO MinCat;
     #nomcateg#0 = #0#0;
     #nomcateg#1 = 001;
|FPRC;

|PROCESO MaxCat;
     #nomcateg#0 = #0#0;
     #nomcateg#1 = 999;
|FPRC;

|PROCESO ConsultaCat; |TIPO 66;
     |ABRE #nomcateg;
     |CONSULTA_F_CLAVE #1#nomcateg, MinCat, MaxCat;
     |SI FSalida = 0;
          #0#1 = #nomcateg#1; |PINTA #0#1;
          #0#13 = #nomcateg#2; |PINTA #0#13;
     |FINSI;
     |CIERRA #nomcateg;
|FPRC;

|PROCESO PintaCat;
     |ABRE #nomcateg;
     #nomcateg#0 = #0#0;
     #nomcateg#1 = #0#1;
     |LEE 000#nomcateg.=;

     |SI FSalida = 0;
          #0#1 = #nomcateg#1; |PINTA #0#1;
          #0#13 = #nomcateg#2; |PINTA #0#13;
     |SINO;
          aAlfa = #0#0;
          aAlfa = ("000" + aAlfa) % -103;
          aAlfa = "    Categoría no encontrada en el convenio " + aAlfa + " (laboral)";
          |MENAV aAlfa;
          |ERROR;
     |FINSI;
     |CIERRA #nomcateg;
|FPRC;

|PROCESO MinCon;
     #nomtrato#0 = #0#16;
     #nomtrato#1 = 001;
|FPRC;

|PROCESO MaxCon;
     #nomtrato#0 = #0#16;
     #nomtrato#1 = 999;
|FPRC;

|PROCESO ConsultaCon; |TIPO 66;
     |ABRE #nomtrato;
     |CONSULTA_F_CLAVE #1#nomtrato, MinCon, MaxCon;
     |SI FSalida = 0;
          #0#4 = #nomtrato#1; |PINTA #0#4;
     |FINSI;
     |CIERRA #nomtrato;
|FPRC;

|PROCESO PintaCon;
     |ABRE #nomtrato;
     #nomtrato#0 = #0#16;
     #nomtrato#1 = #0#4;
     |LEE 000#nomtrato.=;

     |SI FSalida = 0;
          #0#4 = #nomtrato#1; |PINTA #0#4;
     |SINO;
          aAlfa = #0#0;
          aAlfa = ("000" + aAlfa) % -103;
          aAlfa = "    Tipo de contrato no encontrado. Pulse < F5 > para consultar lista";
          |MENAV aAlfa;
          |ERROR;
     |FINSI;
     |CIERRA #nomtrato;
|FPRC;

|PROCESO ConsultaGru; |TIPO 66;
     |ABRE #tfggrcot;
     |CONSULTA_CLAVE #1#tfggrcot;
     |SI FSalida = 0;
          #0#11 = #tfggrcot#0; |PINTA #0#11;
          #0#18 = #tfggrcot#1; |PINTA #0#18;
     |FINSI;
     |CIERRA #tfggrcot;
|FPRC;

|PROCESO PintaGru;
     |ABRE #tfggrcot;
     aAlfa = #0#11;
     |QBF aAlfa;
     aAlfa = ("00" + aAlfa) % -102;
     aAlfa = aAlfa + ("          ") % 110;
     #tfggrcot#0 = aAlfa;
     |LEE 000#tfggrcot.=;

     |SI FSalida = 0;
          #0#11 = #tfggrcot#0; |PINTA #0#11;
          #0#18 = #tfggrcot#1; |PINTA #0#18;
     |SINO;
          aAlfa = "    Grupo de cotización no encontrado ";
          |MENAV aAlfa;
          |ERROR;
     |FINSI;
     |CIERRA #tfggrcot;
|FPRC;
