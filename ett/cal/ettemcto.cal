|FICHEROS;
    ettemcto #0;    || emision contrato laboral
    ettpedi1 #1;    || pedidos cabs.
    ettpedi2 #2;    || pedidos lins.
    ettemct1 #3;    || completado de datos

    labempre #10;   || empresas AGINOM
    labtraba #12;   || trabajadores AGINOM
    ettcontr #13;   || control AGIETT
    ettclien #14;   || clientes AGIETT
    ettprovi #15;   || provincias AGIETT
    ettperso #16;   || personal AGIETT
    ettpuest #17;   || puestos AGIETT
    etttarca #18;   || tarifas cabs. AGIETT
    labregis #20;   || registro contratos AGINOM
    labcon00 #21;   || definicion decretos
|FIN;

|VARIABLES;
    Comodin = "";
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    &hpedido = 0;
    &hperson = 0;
    &hcopias = 0;
    &hfirmas = @;
    &hfallos = 0;
    informe = "";
    ultimo = 0;
    &tp = 0;
    &cbasica = 0;
    &tiporet  = "";
    &tiporet1 = "";
    &finaliza = "";
    aAlfa1 = "";
    &aConvenio1 = "";
    &aConvenio2 = "";
    &aFechaFin = "";
    &aPerPrueba = "";
    &aTipoIndem = "";
    &aClaus31 = "";
    &aClaus32 = "";
|FIN;
____________________________________/
|PROCESO ultimo_labregis;
|LEE 000#20u;
|SI FSalida = 0;
        ultimo = #20#0 + 1;
|SINO;
        ultimo = 1;
|FINSI;
|DDEFECTO #20;
    #20#0 = ultimo;
|GRABA 020#20b;
|FPRC;

|PROCESO graba_labregis;
|ABRE #20;
|HAZ ultimo_labregis;

    #20#1 = #13#4;       || empresa
    #20#2 = #2#24;       || trabajador
    #20#3 = #12#7;       || dni
    #20#4 = #12#45;      || contrato
    #20#5 = #21#1;       || definicion
    #20#6 = #21#2;       || R.D.
    #20#7 = #1#5;        || desde contrato
|SI #1#6 = "01.01.1900";
        #20#8 = "";      || hasta contrato (vacio)
|SINO;
        #20#8 = #1#6;    || hasta contrato
|FINSI;
    #20#9 = #1#3;        || meses duracion
    #20#10= #1#4;        || dias duracion
    #20#12= #12#24;      || oficina empleo
    #20#18= #20#9;       || acumulado meses
    #20#21= ~;           || fecha alta
    #20#22= #20#10;      || acumulado dias
|SI #20#7 ! ".........";|Y #20#7 ! "          ";
        #20#23 = #20#7;       || desde contrato
|SINO;
        #20#23 = "00.00.0000";
|FINSI;
|SI #20#8 ! ".........";|Y #20#8 ! "          ";
        #20#24= #20#8;        || hasta contrato
|SINO;
        #20#24 = "00.00.0000";
|FINSI;
|SI #20#19 ! "..........";|Y #20#19 ! "          ";
        #20#25 = #20#19;      || desde prorroga
|SINO;
        #20#25 = "00.00.0000";
|FINSI;
|SI #20#20 ! "..........";|Y #20#20 ! "          ";
        #20#26 = #20#20;      || hasta prorroga
|SINO;
        #20#26 = "00.00.0000";
|FINSI;

|GRABA 020#20a;
|LIBERA #20;
|CIERRA #20;
|FPRC;

|PROCESO informes;
|SI hfallos = 0;         || si da error algun informe no continua
    |INFOR informe;
    |SI FSalida = 0;
        |PRINT;
        |PIEINF;
            #2#29 = "S";      || impreso SI
        |GRABA 020#2a;
    |SINO;
            alfa1 = "    AVISO: Problemas con el informe [" + informe + "] ...";
        |MENAV alfa1;
            hfallos = 1;
    |FINSI;
    |FININF;
|FINSI;
|FPRC;

|PROCESO pedidos_lins;
    aFechaFin = #0#7;
    aPerPrueba = #13#12;
    |SI #2#57 = "H";
        aTipoIndem = "Hora";
    |SINO;
        aTipoIndem = "Mes";
    |FINSI;
    Comodin = "ettemct1.tab;" + #13#3 + "," + #13#4 + "," + #2#24; |SUB_EJECUTA_NP Comodin;
    |ABRE #ettemct1;
    #ettemct1#5 = #13#4;
    #ettemct1#6 = #2#24;
    |LEE 000#ettemct1.=;
    hfallos = 0;
|SI #2#29 = "N";
    |HAZ graba_labregis;
|FINSI;
|PARA para1 = 1; |SI para1 [ hcopias; |HACIENDO para1 = para1 + 1;
    |SI tp = 0;
            cbasica = 0;
            informe = "ett01cto"; |HAZ informes;
            informe = "ett02cto"; |HAZ informes;
            informe = "ett03cto"; |HAZ informes;
            cbasica = 1;
            informe = "ett01cto"; |HAZ informes;
            informe = "ett02cto"; |HAZ informes;
            informe = "ett03cto"; |HAZ informes;
    |SINO;
            cbasica = 0;
            informe = "ett11cto"; |HAZ informes;
            informe = "ett12cto"; |HAZ informes;
            informe = "ett13cto"; |HAZ informes;
            cbasica = 1;
            informe = "ett11cto"; |HAZ informes;
            informe = "ett12cto"; |HAZ informes;
            informe = "ett13cto"; |HAZ informes;
    |FINSI;
|SIGUE;
    |CIERRA #ettemct1;
|FPRC;

|PROCESO lectura_varios;
|DDEFECTO #10;
|ABRE #10;
    #10#0 = #13#4;
|LEE 000#10=;            || lee empresas AGINOM
|CIERRA #10;

|DDEFECTO #12;
|ABRE #12;
    #12#0 = #13#4;
    #12#1 = #2#24;
|LEE 000#12=;            || lee trabajadores AGINOM
|CIERRA #12;
aAlfa1 = "000" + #12#45; aAlfa1 = aAlfa1 % -103;
|SI aAlfa1 = "004";|O aAlfa1 = "064";     tp = 1;   |FINSI;   || tiempo parcial
    finaliza = "el " + #1#6 + ".";
|SI aAlfa1 = "014";  finaliza = "al t‚rmino de la obra o servicio.";        |FINSI;
|SI aAlfa1 = "016";  finaliza = "al incorporarse el trabajador sustituido.";|FINSI;

|DDEFECTO #14;
|ABRE #14;
    #14#0 = #1#2;
|LEE 000#14=;            || lee cliente AGIETT
|CIERRA #14;

|DDEFECTO #15;
|ABRE #15;
    #15#0 = #14#5;
|LEE 000#15=;            || lee provincia AGIETT
|CIERRA #15;

|DDEFECTO #16;
|ABRE #16;
    #16#0 = #2#2;
|LEE 000#16=;            || lee personal AGIETT
|CIERRA #16;

|DDEFECTO #17;
|ABRE #17;
    #17#0 = #1#59;
|LEE 000#17=;            || lee puestos AGIETT
|CIERRA #17;

|DDEFECTO #18;
|ABRE #18;
    #18#0 = #2#16;
    #18#1 = #2#17;
|LEE 000#18=;            || lee tarifa cabs. AGIETT
|CIERRA #18;

|DDEFECTO #21;
|ABRE #21;
    #21#0 = #12#45;
|LEE 000#21=;
|CIERRA #21;

|SI #2#35 = "H";    tiporet = "por hora ";   tiporet1 = "hora";   |FINSI;
|SI #2#35 = "D";    tiporet = "diarias";     tiporet1 = "dia";    |FINSI;
|SI #2#35 = "S";    tiporet = "semanales";   tiporet1 = "semana"; |FINSI;
|SI #2#35 = "M";    tiporet = "mensuales";   tiporet1 = "mes";    |FINSI;
|FPRC;

|PROCESO tipo8;
|ABRE #13;
|LEE 000#13p;
|SI FSalida = 0;
        alfa1 = #13#3;    |QBF alfa1;
        alfa1 = alfa1 + "/fich/";
    |PATH_DAT #10 alfa1;
    |PATH_DAT #12 alfa1;
    |PATH_DAT #20 alfa1;
    |PATH_DAT #21 alfa1;
|SINO;
    |MENAV "    Control AGIETT sin definir ...";
|FINSI;
|CIERRA #13;
|FPRC;

|PROGRAMA;
    hfallos = 1;
|ABRE #1;
|ABRE #2;
    #1#0 = hpedido;
|LEE 020#1=;
|SI FSalida = 0;
        #2#0 = hpedido;
        #2#1 = hperson;
    |LEE 121#2=;
    |SI FSalida = 0;
        |HAZ tipo8;
        |HAZ lectura_varios;
        |DDEFECTO #0;
            #0#0 = #1#0;      || codigo pedido
            #0#1 = #1#1;      || fecha pedido
            #0#2 = #16#0;     || codigo trabajador
            #0#3 = #16#2;     || nombre trabajador
            #0#4 = #14#0;     || codigo cliente
            #0#5 = #14#1;     || nombre cliente
            #0#6 = #1#5;      || desde contrato
            #0#7 = #1#6;      || hasta contrato
            #0#8 = #17#0;     || codigo puesto
            #0#9 = #17#1;     || nombre puesto
            #0#10= #12#45;    || tipo contrato
        |PUSHV 0501 2380;
        |PINPA #0#0;
        |PINDA #0#0;
        |CONFI "    SConfirme la impresion (S/N): ";
        |PARA; |SI FSalida = 0; |HACIENDO;
            |IMPRE 0;
            |SI FSalida = 0;
                |ATRI P;
                |PINTA 2402, "Imprimiendo ...";
                |ATRI 0;
                |HAZ pedidos_lins;
            |SINO;
                |MENAV "    Proceso abortado ...";
            |FINSI;
            |FINIMP;
            |BLIN 24;
            |CONFI "    NRepetir la impresion (S/N): ";
        |SIGUE;
        |POPV;
    |FINSI;
    |LIBERA #2;
|FINSI;
|CIERRA #1;
|CIERRA #2;
|FPRO;
