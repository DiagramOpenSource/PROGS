|FICHEROS;
    ettrecal #0;    || recalculo importes
    etttarli #1;    || tarifas lins.
    ettpedi2 #2;    || pedidos lins.
    ettcontr #3;    || control AGIETT
    labtraba #4;    || trabajadores AGINOM
    ettpedi1 #5;    || pedidos cabs.
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    &total = 0;
|FIN;
____________________________________/
|PROCESO tipo8; |TIPO 8;
|ABRE #3;
|LEE 000#3p;
|SI FSalida = 0;
        alfa1 = #3#3;    |QBF alfa1;    alfa1 = alfa1 + "/fich/";
    |PATH_DAT #4 alfa1;
|SINO;
    |MENAV "    Control AGIETT sin definir ...";
    |PTEC 807;
|FINSI;
|CIERRA #3;
|FPRC;

|PROCESO mayus; |TIPO 0;
    #0Campo = #0Campo > #0Campo;
|PINTA #0Campo;
|FPRC;
____________________________________/
|PROCESO lee_pedidos1;
|ATRI R;
    alfa1 = "    Procesando Pedido: [" + #2#0 + "] ...";
|MENSA alfa1;
|ATRI 0;

    #4#0 = #3#4;    || empresa AGINOM
    #4#1 = #2#24;   || trabajador AGINOM
|LEE 000#4=;
|SI FSalida = 0;|Y #4#44 = "A";
        #5#0 = #2#0;
    |LEE 000#5=;
    |SI FSalida = 0;
            #1#0 = #2#16;      || tarifa
            #1#1 = #2#17;      || nivel
        |LEE 000#1=;
        |SI FSalida = 0;
                nume1 = (#1#2 - #2#18) + (#1#6 - #2#22) + (#1#7 - #2#23);
                total = #2#21 + nume1;
            |PRINT;
                #2#18 = #1#2;            || salario base
                #2#22 = #1#6;            || pagas
                #2#23 = #1#7;            || vacaciones
                #2#21 = #2#21 + nume1;   || total hora
            |GRABA 020#2a;
            |SI FSalida = 0; seleccio = 1;  |FINSI;
        |SINO;
                alfa1 = "    Nivel:["+#1#0+"]["+#1#1+"] inexistente ...";
            |MENAV alfa1;
        |FINSI;
    |SINO;
            alfa1 = "    Pedido:["+#5#0+"] sin cabecera y con lineas ...";
        |MENAV alfa1;
    |FINSI;
|SINO;
    |SI FSalida ! 0;
            alfa1 = "    Trabajador LABORAL:["+#4#0+"]["+#4#1+"] no encontrado ...";
        |MENAV alfa1;
    |FINSI;
|FINSI;
|LIBERA #2;
|FPRC;

|DEFBUCLE lee_pedidos;
 #2#1;
 16, 17, 24;
      1,     1, #0#0, #0#1,     1;
 999999, 99999, #0#0, #0#2, 99999;
 be;
 NULL, lee_pedidos1;
|FIN;

|PROCESO tipo3; |TIPO 3;
|SI #0#3 = "SI";
    |IMPRE 0;
    |SI FSalida = 0;
        |ABRE #1;
        |ABRE #4;
        |ABRE #5;
        |INFOR "ettrecal";
        |BUCLE lee_pedidos;
        |SI seleccio ! 0;
            |PIEINF;
        |SINO;
            |MENAV "    Seleccion vacia ...";
        |FINSI;
        |FININF;
        |CIERRA #1;
        |CIERRA #4;
        |CIERRA #5;
    |SINO;
        |MENAV "    Proceso abortado ...";
    |FINSI;
    |FINIMP;
|FINSI;
|FPRC;
