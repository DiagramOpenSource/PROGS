|FICHEROS;
    ettpedi1 #0;         || pedidos cabs.
    ettpedi2 #1;         || pedidos lins.
    ettpuest #2;         || puestos de trabajo
    ettforma #3;         || formacion
    ettexper #4;         || experiencia
    ettperso #5;         || personal
    ettpers2 #6;         || puestos de trabajo personal
    ettpers0 #7;         || experiencia personal
    ettpers1 #8;         || formacion personal
    ettclien #9;         || clientes
    ettpedi3 #10,MANTE;  || pantallas auxiliares
    ettocuca #11;        || ocupaciones cabs.
    ettoculi #12;        || ocupaciones lins.
    ettpedi9 #13,MANTE;  || parrilla de trabajadores

    asclient #20;        || clientes FACGES
    ettprovi #21;        || provinciero
    ettcontr #23;        || control
    asbancos #24;        || bancos remesa FACGES
    asbancli #25;        || bancos clientes FACGES
    asfpagos #26;        || formas de pago FACGES
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    alfa5 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    nume5 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    inkey = 0;
    modifica = "";
    sw_codpo = 0;
    sw_puest = 0;
    sw_exper = 0;
    sw_forma = 0;
    sw_ocupa = "";
    valoraci = 0;
    tot_regis = 0;
    tot_candi = 0;
    nedad = 0;
    npues1 = 0;
    npues2 = 0;
    linea = 0;
    nhor1 = 0;
    nhor2 = 0;
    nmin1 = 0;
    nmin2 = 0;
    hora1 = 0;
    hora2 = 0;
    minu1 = 0;
    minu2 = 0;
    horas_dia = 0;
    hora9 = 0;
    tope = 0;
    sw_defhora = 0;
    sw_sexo = 0;
    sw_jornada = 0;
|FIN;

----------------------------------
'parte1' -> procesos desde campos
'parte2' -> procesos de seleccion
----------------------------------

____________________________________/ parte1: CALCULOS DESDE CAMPOS
|PROCESO tipo8; |TIPO 8;
|ABRE #23;
|LEE 000#23p;
|SI FSalida = 0;
        alfa1 = #23#1;   |QBF alfa1;    alfa1 = alfa1 + "/fich/";
        alfa2 = #23#2;   |QBF alfa2;    alfa2 = ("00" + alfa2) % -102;
        alfa1 = alfa1 + alfa2 + "/";
    |PATH_DAT #20 alfa1;
    |PATH_DAT #24 alfa1;
    |PATH_DAT #25 alfa1;
    |PATH_DAT #26 alfa1;
        alfa1 = #23#1;   |QBF alfa1;    alfa1 = alfa1 + "/pan/";
    |PATH_PAN #24 alfa1;
    |PATH_PAN #25 alfa1;
    |PATH_PAN #26 alfa1;
|SINO;
    |MENAV "    Control AGIETT sin definir ...";
    |PTEC 807;
|FINSI;
|CIERRA #23;
|FPRC;

|PROCESO alta; |TIPO 7;
    modo = (FEntrada / 100) ! 0;
|SI modo = 1;       || alta
    |LEE 000#0u;
    |SI FSalida = 0;
            nume1 = #0#0 + 1;
        |DDEFECTO #0;
            #0#0 = nume1;
        |PINTA #0#0;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO selec_no1;
    #1#24 = 0;
    #1#27 = "N";
|GRABA 020#1a;
|LIBERA #1;
|FPRC;

|DEFBUCLE selec_no;
 #1#1;
 ;
 #0#0,     1;
 #0#0, 99999;
 be;
 NULL, selec_no1;
|FIN;

|PROCESO modo; |TIPO 0;
    modo = (FEntrada / 100) ! 0;
|SI modo = 2;
    |SI #0#72 > 0;|Y #0#73 [ 0;
        |AVISO;
        |CONFI "    NPedido SELECCIONADO. ¨Anular seleccionados (S/N)?: ";
        |SI FSalida = 0;
                #0#72 = 0;
            |GRABA 020#0a;
            |SI FSalida = 0;
                |PINTA #0#72;
                |BUCLE selec_no;
            |FINSI;
        |SINO;
            |SI Campo = 0;
                |ERROR;
            |SINO;
                |PTEC 807;
            |FINSI;
        |FINSI;
    |FINSI;
    |SI #0#73 > 0;
        |MENAV "    Pedido CON contratos realizados. No Modificable ...";
        |SI Campo = 0;
            ||ERROR;
        |SINO;
            |PTEC 807;
        |FINSI;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO modo2; |TIPO 7;
|HAZ modo;
|FPRC;

|PROCESO tipo13; |TIPO 13;
|HAZ lee_cliente;
|HAZ lee_puesto;
|FPRC;

|PROCESO no_fechas; |TIPO 0;
|SI #0#5 = "01.01.1900";
        alfa1 = "N";
|SINO;
        alfa1 = "S";
|FINSI;
|C_M #0#3, 1, alfa1;
|C_M #0#4, 1, alfa1;
|C_M #0#6, 1, alfa1;
|FPRC;

|PROCESO duracion; |TIPO 7;
    #0#6 = "01.01.1900";
    nume1 = #0#3 + #0#4;
|SI nume1 ! 0;|Y #0#5 ! "01.01.1900";
        nume1 = #0#3 / 1000;
        nume2 = (#0#4 - 1) + nume1;
        fech1 = #0#5;
        fech1 = fech1 + nume2;
        #0#6 = fech1;
|FINSI;
|PINTA #0#6;
|FPRC;

|PROCESO hora; |TIPO 0;
    nume1 = Contenido;
|SI nume1 ! #0Campo;
        alfa1 = #0Campo;
        alfa1 = alfa1 % -102;
        nume1 = alfa1;
    |SI nume1 > 59;
        |MENAV "    Formato horario: HHMM. Donde 'H' son horas y 'M' minutos ...";
        |ERROR;
    |SINO;
        |HAZ calcula_horas;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO modificable_f;
    nume1 = Campo + 16;
    nume2 = Campo + 24;
|CAMPO_MODIFICA #0nume1, 1, modifica;
|CAMPO_MODIFICA #0nume2, 1, modifica;
|FPRC;

|PROCESO formacion6; |TIPO 6;
|SI #0#76 = "T";    |ACABA;   |FINSI;

    modifica = "S";
|HAZ modificable_f;
|SI #0Campo = 0;
    |DDEFECTO #3;
    |ABRE #3;
        #3#0 = 0;
        #3#1 = "";
    |LEE 000#3=;
    |SI FSalida ! 0;
        |GRABA 020#3n;
    |FINSI;
    |CIERRA #3;
        modifica = "N";
    |HAZ modificable_f;
|FINSI;
|FPRC;

|PROCESO formacion0; |TIPO 0;
|SI #0#76 = "T";    |ACABA;   |FINSI;

    inkey = FSalida;
|ABRE #3;
    #3#0 = #0Campo;
|LEE 000#3=;
|SI FSalida ! 0;
    |MENAV "    No puede a¤adir una formacion no definida.  Valor cero = sin formacion.";
    |ERROR;
|FINSI;
|CIERRA #3;
|FPRC;

|PROCESO formacion7; |TIPO 7;
|SI #0#76 = "T";    |ACABA;   |FINSI;

|DDEFECTO #3;
|SI Campo ] 11;|Y Campo [ 18; nume1 = Campo;      |FINSI;
|SI Campo ] 27;|Y Campo [ 34; nume1 = Campo - 16; |FINSI;
|SI Campo ] 35;|Y Campo [ 42; nume1 = Campo - 24; |FINSI;
|SI #0nume1 ! 0;
    |ABRE #3;
        #3#0 = #0nume1;
    |LEE 000#3=;
    |CIERRA #3;
|FINSI;
|ATRI I;
|PINTA 2219, #3#1;
|ATRI 0;
|FPRC;

|PROCESO modificable_e;
    nume1 = Campo + 24;
    nume2 = Campo + 32;
|CAMPO_MODIFICA #0nume1, 1, modifica;
|CAMPO_MODIFICA #0nume2, 1, modifica;
|FPRC;

|PROCESO experien6; |TIPO 6;
|SI #0#76 = "T";    |ACABA;   |FINSI;

    modifica = "S";
|HAZ modificable_e;
|SI #0Campo = 0;
    |DDEFECTO #4;
    |ABRE #4;
        #4#0 = 0;
        #4#1 = "";
    |LEE 000#4=;
    |SI FSalida ! 0;
        |GRABA 020#4n;
    |FINSI;
    |CIERRA #4;
        modifica = "N";
    |HAZ modificable_e;
|FINSI;
|FPRC;

|PROCESO experien0; |TIPO 0;
|SI #0#76 = "T";    |ACABA;   |FINSI;

    inkey = FSalida;
|ABRE #4;
    #4#0 = #0Campo;
|LEE 000#4=;
|SI FSalida ! 0;
    |MENAV "    No puede a¤adir una experiencia no definida. Valor cero = sin experiencia.";
    |ERROR;
|FINSI;
|CIERRA #4;
|FPRC;

|PROCESO experien7; |TIPO 7;
|SI #0#76 = "T";    |ACABA;   |FINSI;

|DDEFECTO #4;
|SI Campo ] 19;|Y Campo [ 26; nume1 = Campo;      |FINSI;
|SI Campo ] 43;|Y Campo [ 50; nume1 = Campo - 24; |FINSI;
|SI Campo ] 51;|Y Campo [ 58; nume1 = Campo - 32; |FINSI;
|SI #0nume1 ! 0;
    |ABRE #4;
        #4#0 = #0nume1;
    |LEE 000#4=;
    |CIERRA #4;
|FINSI;
|ATRI I;
|PINTA 2219, #4#1;
|ATRI 0;
|FPRC;

|PROCESO no_puesto; |TIPO 7;
|SI #0#76 = "P";    alfa1 = "S";   |FINSI;
|SI #0#76 = "T";    alfa1 = "N";   |FINSI;

|PARA para1 = 11; |SI para1 [ 58; |HACIENDO para1 = para1 + 1;
    |C_M #0para1, 1, alfa1;
|SIGUE;
|C_M #0#59, 1, alfa1;
|C_M #0#61, 1, alfa1;
|C_M #0#62, 1, alfa1;
|FPRC;

|PROCESO lee_puesto; |TIPO 0;
|SI FSalida ! 2;|Y #0#76 = "P";
    |ABRE #2;
        #2#0 = #0#59;
    |LEE 000#2=;
    |SI FSalida ! 0;|Y #0#59 ! 0;
        |MENAV "    El puesto de trabajo NO existe ...";
        |ERROR;
    |FINSI;
    |CIERRA #2;
    |ATRI I;
    |PINTA 1228, #2#1;
    |ATRI 0;
|FINSI;
|FPRC;

|PROCESO mayus; |TIPO 0;
    #0Campo = #0Campo > #0Campo;
|PINTA #0Campo;
|FPRC;

|PROCESO mayus10; |TIPO 0;
    #10Campo = #10Campo > #10Campo;
|PINTA #10Campo;
|FPRC;

|PROCESO solo_alta; |TIPO 7;
    modo = (FEntrada / 100) ! 0;
|SI modo = 1;
    |C_M #1#2, 1, "S";
|SINO;
    |C_M #1#2, 1, "N";
|FINSI;
|FPRC;

|PROCESO personal; |TIPO 0;
|ABRE #5;
    #5#0 = #1#2;
|LEE 000#5=;
|SI FSalida = 0;
    |HAZ la_edad;
    |ABRE #6;  |SELECT #2#6;  |HAZ valoracion;    |CIERRA #6;
    |SI #0#5 ! "01.01.1900";  |HAZ ocupado;  |FINSI;
        #1#3 = #5#17;         || sexo
        #1#4 = nedad;         || edad
        #1#5 = #5#18;         || peso
        #1#6 = #5#19;         || estatura
        #1#7 = valoraci;      || valoracion
        #1#8 = #5#1;          || alta en base
        #1#9 = sw_ocupa;      || ocupado S/N
        #1#10 = #5#2;         || apellidos y nombre
        #1#11 = #5#8;         || localidad
        #1#12 = #5#9;         || codigo provincia
        #1#13 = #5#10;        || codigo poblacion
        #1#14 = #5#11;        || telefono 1
        #1#15 = #5#12;        || telefono 2
    |PINDA #0#1;
|FINSI;
|CIERRA #5;
|FPRC;

|PROCESO tipo2; |TIPO 2;
    modo = (FEntrada / 100) ! 0;
|SI modo = 3;
    |SI #0#72 > 0;|Y #0#73 [ 0;
        |MENAV "    Pedido CON seleccionados ...";
        |ERROR;
    |SINO;
        |SI #0#73 > 0;
            |MENAV "    Pedido CON contratos ...";
            |ERROR;
        |SINO;
            |HAZ borra_lineas;
        |FINSI;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO cambia_clave; |TIPO 6;
|SELECT #1#9;
|FPRC;

|PROCESO lee_cliente; |TIPO 0;
|DDEFECTO #9;
|ABRE #9;
    #9#0 = #0#2;
|LEE 000#9=;
|CIERRA #9;
|ATRI I;
|PINTA 0817, #9#1;
|ATRI 0;
|FPRC;

|PROCESO tipo11; |TIPO 11;
|SI FSalida = 9;
    |PUSHV 0501 2380;
    |PINPA #0#5;
    |ABRE #5;  #5#0 = #1#2;   |LEE 000#5=;   |CIERRA #5;
    |PINDA #0#5;
    |PAUSA;
    |POPV;
    |ERROR;
|FINSI;
|FPRC;

|PROCESO calcula_horas; |TIPO 7;
    hora9 = 0;
|PARA para1 = 63; |SI para1 [ 69; |HACIENDO para1 = para1 + 1;
    |SI #0para1 = "S";
        |SI para1 = 63;
                nume1 = 7;
                nume2 = 8;
                nume3 = 9;
                nume4 = 10;
        |SINO;
                nume1 = para1 + (14 + (3 * (para1 - 64)));
                nume2 = nume1 + 1;
                nume3 = nume1 + 2;
                nume4 = nume1 + 3;
        |FINSI;
            nhor1 = (#0nume1 - (#0nume1 $ 100)) / 100; nmin1 = #0nume1 $ 100;
            nhor2 = (#0nume2 - (#0nume2 $ 100)) / 100; nmin2 = #0nume2 $ 100;
            hora1 = nhor2 - nhor1;
            minu1 = nmin2 - nmin1;
        |SI minu1 < 0;   hora1 = hora1 - 1;  minu1 = 60 + minu1; |FINSI;

            nhor1 = (#0nume3 - (#0nume3 $ 100)) / 100; nmin1 = #0nume3 $ 100;
            nhor2 = (#0nume4 - (#0nume4 $ 100)) / 100; nmin2 = #0nume4 $ 100;
            hora2 = nhor2 - nhor1;
            minu2 = nmin2 - nmin1;
        |SI minu2 < 0;   hora2 = hora2 - 1;  minu2 = 60 + minu2; |FINSI;

        |SI hora1 < 0;|O hora2 < 0;
            ||MENAV "    Introduccion erronea del horario ...";
        |SINO;
                hora1 = hora1 + hora2;
                minu1 = minu1 + minu2;
            |SI minu1 ] 60;
                    hora1 = hora1 + 1;
                    minu1 = minu1 - 60;
            |FINSI;
                hora9 = hora9 + (hora1 + ((minu1 * 100) / 6000));
        |FINSI;
    |FINSI;
|SIGUE;
    nume1 = 0;
|PARA para1 = 63; |SI para1 [ 69; |HACIENDO para1 = para1 + 1;
    |SI #0para1 = "S";   nume1 = nume1 + 1;  |FINSI;
|SIGUE;
    horas_dia = hora9 / nume1;          || media arit. de horas diarias
    #0#70 = hora9;                      || total horas total semana
|PINTA #0#70;
|FPRC;

|PROCESO carga_par1;
    tope = tope + 1;

|DDEFECTO #5;
    #5#0 = #1#2;
|LEE 000#5=;

    #13#0 = #0#0;        || pedido
    #13#1 = #1#2;        || codigo
    #13#2 = #5#2;        || nombre
|GRABA 020#13n;
|FPRC;

|DEFBUCLE carga_par;
 #1#1;
 ;
 #0#0,     1;
 #0#0, 99999;
 ;
 NULL, carga_par1;
|FIN;

|PROCESO borra_parrilla1;
|BORRA 020#13a;
|LIBERA #13;
|FPRC;

|DEFBUCLE borra_parrilla;
 #13#1;
 ;
 #0#0,     1;
 #0#0, 99999;
 be;
 NULL, borra_parrilla1;
|FIN;

|PROCESO tiposel; |TIPO 0;
    inkey = FSalida;
|SI #0#76 = "P";    alfa1 = "S";   |FINSI;
|SI #0#76 = "T";    alfa1 = "N";   |FINSI;

|SI #0#76 = "T";|Y inkey ! 2;
        tope = 0;
    |BUCLE borra_parrilla;
    |ABRE #5;
    |ABRE #13;
    |BUCLE carga_par;
    |CIERRA #5;
    |CIERRA #13;
    |HAZ parrilla;
|FINSI;
|FPRC;

|PROCESO min13;
    #13#0 = #0#0;
    #13#1 = 1;
|FPRC;

|PROCESO max13;
    #13#0 = #0#0;
    #13#1 = 99999;
|FPRC;

|PROCESO BuscaDisp;
     |SI #5#24 = "N";
          |MENAV "    AVISO: El trabajador seleccionado no esta disponible";
     |FINSI;
|FPRC;

|PROCESO tipo2_parrilla; |TIPO 2;
    tope = tope +. 1;
|SI tope > #0#71;
    |MENAV "    Se excede el numero de trabajadores requeridos ...";
|FINSI;
|FPRC;

|PROCESO parrilla;
|PUSHV 1133 2380;
|ENTLINEAL #1#13, 2, 1, 22, 1, min13, max13;      || siempre en alta
|POPV;
|SI tope = 0;
    |ERROR;
|FINSI;
|FPRC;
**************************************************************************

____________________________________/ PINTA ESTADISTICA DE LECTURA
|PROCESO pinta_guia;
    #10#0 = tot_regis;
    #10#1 = tot_candi;
    ||#10#2 = sin_cuest;
    #10#3 = #10#3 + 1;
    #10#4 = (#10#3 * 100) / #10#0;      || procesados
    #10#5 = (#10#1 * 100) / #10#3;      || candidatos
    ||#10#6 = (#10#2 * 100) / #10#1;      || candidatos sin cuestionarios
|PINDA #0#10;
|FPRC;
____________________________________/ GRABA CANDIDATO
|PROCESO lite_horario;
    para2 = 0;
    alfa2 = "";
    alfa3 = "";
|PARA para1 = 63; |SI para1 [ 69; |HACIENDO para1 = para1 + 1;
    |SI #0para1 = "S";
        |SI para1 = 63;
                nume1 = 7;
                nume2 = 8;
                nume3 = 9;
                nume4 = 10;
        |SINO;
                nume1 = para1 + (14 + (3 * (para1 - 64)));
                nume2 = nume1 + 1;
                nume3 = nume1 + 2;
                nume4 = nume1 + 3;
        |FINSI;
        |SI para1 = 63;  alfa1 = "lunes";    |FINSI;
        |SI para1 = 64;  alfa1 = "martes";   |FINSI;
        |SI para1 = 65;  alfa1 = "miercoles";|FINSI;
        |SI para1 = 66;  alfa1 = "jueves";   |FINSI;
        |SI para1 = 67;  alfa1 = "viernes";  |FINSI;
        |SI para1 = 68;  alfa1 = "s bado";   |FINSI;
        |SI para1 = 69;  alfa1 = "domingo";  |FINSI;
            alfa1 = alfa1 + "(" + #0nume1 + "-" + #0nume2;
        |SI #0nume3 ! 0;
                alfa1 = alfa1 + "," + #0nume3 + "-" + #0nume4;
        |FINSI;
            alfa1 = alfa1 + ")";

            alfa4 = alfa1 % 0;     nume4 = alfa4;
            alfa5 = alfa2 % 0;     nume5 = alfa5; nume5 = 110 - nume5;
            alfa5 = "";
        |SI nume5 ] nume4;
            |SI alfa2 ! "";   alfa5 = &59;   |FINSI;
                alfa2 = alfa2 + alfa5 + alfa1;
        |SINO;
            |SI alfa3 ! "";   alfa5 = &59;   |FINSI;
                alfa3 = alfa3 + alfa5 + alfa1;
        |FINSI;
    |FINSI;
|SIGUE;
    #1#43 = alfa2;       || horario 1
    #1#45 = alfa3;       || horario 2
|FPRC;

|PROCESO la_edad;
    nedad = 0;
|SI #5#14 ! "01.01.1900";
        nume1 = #5#14;   || fecha nacimiento
        nume2 = #0#1;    || fecha pedido
        nedad = ((nume2 - nume1) / 365) ! 1;      || con un decimal
|FINSI;
|FPRC;

|PROCESO graba_candidato;
|HAZ la_edad;
|DDEFECTO #1;
    #1#0 = #0#0;         || numero de pedido
    #1#1 = linea;        || registro
    #1#2 = #5#0;         || codigo personal
    #1#3 = #5#17;        || sexo
    #1#4 = nedad;        || edad
    #1#5 = #5#18;        || peso
    #1#6 = #5#19;        || estatura
    #1#7 = valoraci;     || valoracion
    #1#8 = #5#1;         || alta en base
    #1#9 = sw_ocupa;     || ocupado S/N
    #1#10 = #5#2;        || apellidos y nombre
    #1#11 = #5#8;        || localidad
    #1#12 = #5#9;        || codigo provincia
    #1#13 = #5#10;       || codigo poblacion
    #1#14 = #5#11;       || telefono 1
    #1#15 = #5#12;       || telefono 2
    #1#16 = #9#18;       || tarifa
    #1#25 = #0#70;       || horas semanales
    #1#26 = horas_dia;   || horas diarias
|SI #0#76 = "T";    #1#27 = "S";   |FINSI;   || seleccionado SI
    #1#42 = #2#136;      || epigrafe accidentes
    #1#58 = #5#24;       || Disponible (S/N)
|HAZ lite_horario;
|GRABA 020#1n;
|SI FSalida = 0;
        tot_candi = tot_candi + 1;
        linea = linea + 1;
        #0#60 = #0#60 + 1;                             || candidatos
    |SI #1#27 = "S";     #0#72 = #0#72 + 1;  |FINSI;   || seleccionados
    |GRABA 020#0a;
    |SI FSalida = 0;
        |PINTA #0#60;
        |PINTA #0#72;
    |FINSI;
|FINSI;
|FPRC;
____________________________________/ VALORACION DEL PUESTO
|PROCESO valoracion;
    valoraci = 0;
    npues1 = 0;
    npues2 = 0;
|HAZ puesto;
|PARA para1 = 4; |SI para1 [ 133; |HACIENDO para1 = para1 + 1;
    |SI #6para1 ! 0;
            npues1 = npues1 + 1;
            npues2 = npues2 + #6para1;
    |FINSI;
|SIGUE;
    valoraci = (npues2 / npues1) ! 4;
|FPRC;
____________________________________/ SELECCION POR FECHAS OCUPADAS
|PROCESO ocupacion1;
|SI #0#5 ] #12#2;|Y #0#5 [ #12#3;
        sw_ocupa = "S";                 || inicio contrato ocupado
|FINSI;
|SI #0#6 ] #12#2;|Y #0#6 [ #12#3;
        sw_ocupa = "S";                 || final contrato ocupado
|FINSI;
|FPRC;

|DEFBUCLE ocupacion;
 #11#1;
 ;
 #5#0;
 #5#0;
 ;
 NULL, NULL, ocupacion1;
|FIN;

|PROCESO ocupado;
    sw_ocupa = "N";
|BUCLE ocupacion;
|FPRC;
____________________________________/ SELECCION POR FORMACION
|PROCESO formacion;
    sw_forma = 1;
    #8#0 = #5#0;
|PARA para1 = 11; |SI para1 [ 18; |HACIENDO para1 = para1 + 1;
    |SI #0para1 ! 0;
            para2 = para1 + 16;
            para3 = para1 + 24;
            #8#2 = #0para1;
        |LEE 000#8=;
        |SI FSalida ! 0;|O #8#0 ! #5#0;|O #8#7 < #0para2;|O #8#7 > #0para3;
                sw_forma = 0;
            |SAL;
        |FINSI;
    |FINSI;
|SIGUE;
|FPRC;
____________________________________/ SELECION POR SEXO
|PROCESO Sexo;
     sw_sexo = 1;
     |SI #0#102 = "A";   |ACABA;   |FINSI;
     |SI #0#102 ! #5#17;
          sw_sexo = 0;
     |FINSI;
|FPRC;
____________________________________/ SELECION POR JORNADA
|PROCESO Jornada;
     sw_jornada = 1;
     |SI #0#103 = "A";   |ACABA;   |FINSI;
     |SI #0#103 ! #5#44;
          sw_jornada = 0;
     |FINSI;
|FPRC;
____________________________________/ SELECCION POR EXPERIENCIAS
|PROCESO experiencia;
    sw_exper = 1;
    #7#0 = #5#0;
|PARA para1 = 19; |SI para1 [ 26; |HACIENDO para1 = para1 + 1;
    |SI #0para1 ! 0;
            para2 = para1 + 24;
            para3 = para1 + 32;
            #7#2 = #0para1;
        |LEE 000#7=;
        |SI FSalida ! 0;|O #7#0 ! #5#0;|O #7#7 < #0para2;|O #7#7 > #0para3;
                sw_exper = 0;
            |SAL;
        |FINSI;
    |FINSI;
|SIGUE;
|FPRC;
____________________________________/ SELECCION POR PUESTO DE TRABAJO
|PROCESO puesto;
    #6#0 = #5#0;
    #6#2 = #0#59;
|LEE 000#6=;
|SI FSalida = 0;
        sw_puest = 1;
|FINSI;
|FPRC;
____________________________________/ SELECCION POR CODIGO POSTAL
|PROCESO codigo;
    nume1 = (#5#9 * 1000) + #5#10;
|SI nume1 ] #0#61;|Y nume1 [ #0#62;
        sw_codpo = 1;
|FINSI;
|FPRC;
____________________________________/ LECTURA DEL PERSONAL
|PROCESO lee_personal1;
    sw_codpo = 0;
    sw_puest = 0;
    sw_exper = 0;
    sw_forma = 0;
|HAZ codigo;
|SI sw_codpo = 1;
    |HAZ puesto;
    |SI sw_puest = 1;
        |HAZ Jornada;                               || seleccion por jornada
        |SI sw_jornada = 1;
            |HAZ Sexo;                              || seleccion por sexo
            |SI sw_sexo = 1;
                |HAZ experiencia;                   || seleccion por experiencias
                |SI sw_exper ! 0;
                    |HAZ formacion;                 || seleccion por formacion
                    |SI sw_forma ! 0;
                        |SI #0#5 ! "01.01.1900";
                            |HAZ ocupado;           || ocupacion de la vigencia contrato
                        |FINSI;
                        |HAZ valoracion;            || valoracion del puesto de trabajo
                        |HAZ graba_candidato;
                    |FINSI;
                |FINSI;
            |FINSI;
        |FINSI;
    |FINSI;
|FINSI;
|HAZ pinta_guia;
|FPRC;

|DEFBUCLE lee_personal;
 #5#1;
 ;
     1;
 99999;
 ;
 NULL, lee_personal1;
|FIN;

|PROCESO lee_parrilla1;
    tope = tope + 1;
|SI tope [ #0#71;
        #5#0 = #13#1;
    |LEE 000#5=;
    |SI FSalida = 0;
        |HAZ ocupado;
            valoraci = 0;
        |HAZ graba_candidato;
    |FINSI;
|FINSI;
|FPRC;

|DEFBUCLE lee_parrilla;
 #13#1;
 ;
 #0#0,     1;
 #0#0, 99999;
 ;
 NULL, lee_parrilla1;
|FIN;

|PROCESO seleccion;
|ABRE #1;
|ABRE #6; |SELECT #2#6;
|ABRE #7; |SELECT #2#7;
|ABRE #8; |SELECT #2#8;
|SI #0#76 = "P";
    |BUCLE lee_personal;
|SINO;
        tope = 0;
    |ABRE #5;
    |BUCLE lee_parrilla;
    |CIERRA #5;
|FINSI;
|CIERRA #1;
|CIERRA #6;
|CIERRA #7;
|CIERRA #8;
|FPRC;

|PROCESO desde1; |TIPO 7;
|SI Campo = 7;      alfa1 = #0#63; |FINSI;
|SI Campo = 78;     alfa1 = #0#64; |FINSI;
|SI Campo = 82;     alfa1 = #0#65; |FINSI;
|SI Campo = 86;     alfa1 = #0#66; |FINSI;
|SI Campo = 90;     alfa1 = #0#67; |FINSI;
|SI Campo = 94;     alfa1 = #0#68; |FINSI;
|SI Campo = 98;     alfa1 = #0#69; |FINSI;
|SI alfa1 = "S";
    |SI #0Campo = 0;|Y sw_defhora = 1;  #0Campo = #0#7;     |FINSI;
|SINO;
        #0Campo = 0;
|FINSI;
|PINTA #0Campo;
|FPRC;

|PROCESO hasta1; |TIPO 7;
|SI Campo = 8;      alfa1 = #0#63; |FINSI;
|SI Campo = 79;     alfa1 = #0#64; |FINSI;
|SI Campo = 83;     alfa1 = #0#65; |FINSI;
|SI Campo = 87;     alfa1 = #0#66; |FINSI;
|SI Campo = 91;     alfa1 = #0#67; |FINSI;
|SI Campo = 95;     alfa1 = #0#68; |FINSI;
|SI Campo = 99;     alfa1 = #0#69; |FINSI;
|SI alfa1 = "S";
    |SI #0Campo = 0;|Y sw_defhora = 1;  #0Campo = #0#8;     |FINSI;
|SINO;
        #0Campo = 0;
|FINSI;
|PINTA #0Campo;
|FPRC;

|PROCESO desde2; |TIPO 7;
|SI Campo = 9;      alfa1 = #0#63; |FINSI;
|SI Campo = 80;     alfa1 = #0#64; |FINSI;
|SI Campo = 84;     alfa1 = #0#65; |FINSI;
|SI Campo = 88;     alfa1 = #0#66; |FINSI;
|SI Campo = 92;     alfa1 = #0#67; |FINSI;
|SI Campo = 96;     alfa1 = #0#68; |FINSI;
|SI Campo = 100;    alfa1 = #0#69; |FINSI;
|SI alfa1 = "S";
    |SI #0Campo = 0;|Y sw_defhora = 1;  #0Campo = #0#9;     |FINSI;
|SINO;
        #0Campo = 0;
|FINSI;
|PINTA #0Campo;
|FPRC;

|PROCESO hasta2; |TIPO 7;
|SI Campo = 10;     alfa1 = #0#63; |FINSI;
|SI Campo = 81;     alfa1 = #0#64; |FINSI;
|SI Campo = 85;     alfa1 = #0#65; |FINSI;
|SI Campo = 89;     alfa1 = #0#66; |FINSI;
|SI Campo = 93;     alfa1 = #0#67; |FINSI;
|SI Campo = 97;     alfa1 = #0#68; |FINSI;
|SI Campo = 101;    alfa1 = #0#69; |FINSI;
|SI alfa1 = "S";
    |SI #0Campo = 0;|Y sw_defhora = 1;  #0Campo = #0#10;    |FINSI;
|SINO;
        #0Campo = 0;
|FINSI;
|PINTA #0Campo;
|FPRC;

|PROCESO sihorario; |TIPO 0;
    alfa1 = Contenido;
|SI alfa1 ! #0Campo;
        sw_defhora = 1;
|SINO;
        sw_defhora = 0;       || para rellenar por defecto o no
|FINSI;

    alfa1 = #0Campo;
|SI Campo = 63;      || el lunes es especial por la estructura de campos
        nume1 = 7;
        nume2 = 8;
        nume3 = 9;
        nume4 = 10;
|SINO;
        nume1 = Campo + (14 + (3 * (Campo - 64)));
        nume2 = nume1 + 1;
        nume3 = nume1 + 2;
        nume4 = nume1 + 3;
|FINSI;
|C_M #0nume1, 1, alfa1;
|C_M #0nume2, 1, alfa1;
|C_M #0nume3, 1, alfa1;
|C_M #0nume4, 1, alfa1;
|FPRC;
____________________________________/ parte2: PROCESOS DE SELECCION
|PROCESO ordena_valor1;
    #1#1 = linea;
|GRABA 020#1n;
|SI FSalida = 0;
    |BORRA 020#1a;
    |LIBERA #1;
        linea = linea + 1;
|FINSI;
|FPRC;

|DEFBUCLE ordena_valor;
 #1#3;
 ;
 #0#0, 9, 100000;
 #0#0, 0, 999999;
 be;
 NULL, ordena_valor1;
|FIN;

|PROCESO borra_lin1;
|BORRA 020#1a;
|LIBERA #1;
|FPRC;

|DEFBUCLE borra_lin;
 #1#1;
 ;
 #0#0,     1;
 #0#0, 99999;
 be;
 NULL, borra_lin1;
|FIN;

|PROCESO borra_lineas;
|BUCLE borra_lin;
|FPRC;

|PROCESO tipo3; |TIPO 3;
    modo = (FEntrada / 100) ! 0;
|SI modo ] 1;|Y modo [ 2;|Y #0#72 = 0;|Y #0#73 = 0;
    |PUSHV 1848 2380;
        tot_candi = 0;
    |LEEDEDEF "ettperso", "";     tot_regis = (A\FSalida % 1108);
    |DDEFECTO #10;
    |SI #0#76 = "P";
        |PINPA #0#10;
        |PINDA #0#10;
    |FINSI;
    |HAZ borra_lineas;   || borra seleccion anterior (en su caso)
        #0#60 = 0;
        linea = 100000;
    |HAZ seleccion;      || proceso de seleccion
        linea = 1;
    |BUCLE ordena_valor;   || ordena por valoraciones
    |PINTA #0#60;          || numero de candidatos
    |SI #0#60 = 0;
        |MENAV "    Seleccion vacia ...";
    |SINO;
        |MENAV "    Seleccion realizada ...";
    |FINSI;
    |POPV;
|FINSI;
|FPRC;
