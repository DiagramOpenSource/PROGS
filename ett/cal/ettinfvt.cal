|FICHEROS;
    ettinfvt #0;
    labtraba #2;         || trabajadores AGINOM
    labregis #3;         || registro contratos AGINOM
    ettclien #4;         || clientes AGIETT
    ettpedi1 #5;         || creacion pedidos AGIETT
    ettpedi2 #6;         || candidatos AGIETT

    ettcontr #23;
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    &venci = "";
    alfa_orden = "";
|FIN;
____________________________________/
|PROCESO mayus; |TIPO 0;
    #0Campo = #0Campo > #0Campo;
|PINTA #0Campo;
|FPRC;

|PROCESO def_fechas; |TIPO 7;
    fech1 = ~;
    alfa1 = fech1;
    alfa2 = alfa1 % 402;      || recoge el mes actual
    alfa3 = alfa1 % -104;     || recoge el a¤o actual
    nume1 = alfa2;
    nume2 = alfa3;

|SI nume1 = 12;
        nume1 = 1;
        nume2 = nume2 + 1;
|SINO;
        nume1 = nume1 + 1;    || calcula el mes siguiente al actual
|FINSI;

    nume4 = nume2 $ 4;        || calcula el a¤o bisiesto
|SI nume4 = 0;
        nume4 = 1;
|SINO;
        nume4 = 0;
|FINSI;

|SI nume1 = 1;      nume3 = 31;         |FINSI;
|SI nume1 = 2;      nume3 = 28 + nume4; |FINSI;
|SI nume1 = 3;      nume3 = 31;         |FINSI;   || calcula el ultimo dia
|SI nume1 = 4;      nume3 = 30;         |FINSI;   ||/del mes siguiente al
|SI nume1 = 5;      nume3 = 31;         |FINSI;   ||/actual
|SI nume1 = 6;      nume3 = 30;         |FINSI;
|SI nume1 = 7;      nume3 = 31;         |FINSI;
|SI nume1 = 8;      nume3 = 31;         |FINSI;
|SI nume1 = 9;      nume3 = 30;         |FINSI;
|SI nume1 = 10;     nume3 = 31;         |FINSI;
|SI nume1 = 11;     nume3 = 30;         |FINSI;
|SI nume1 = 12;     nume3 = 31;         |FINSI;

    alfa4 = nume1;  alfa4 = ("00" + alfa4) % -102;     || mes alfanumerico

    alfa1 = "01." + alfa4 + "." + nume2;
    #0#4 = alfa1;                                      || desde fecha

    alfa1 = nume3;
    alfa1 = alfa1 + "." + alfa4 + "." + nume2;
    #0#5 = alfa1;                                      || hasta fecha

|PINTA #0#4;
|PINTA #0#5;
|FPRC;

|PROCESO def_empresa; |TIPO 7;
    #0#0 = #23#4;    |PINTA #0#0;
    #0#1 = #23#4;    |PINTA #0#1;
|FPRC;

|PROCESO tipo8; |TIPO 8;
|ABRE #23;
|LEE 000#23p;
|SI FSalida = 0;
        alfa1 = #23#3;   |QBF alfa1;    alfa1 = alfa1 + "/fich/";
    |PATH_DAT #2 alfa1;
    |PATH_DAT #3 alfa1;
|SINO;
    |MENAV "    Control AGIETT sin definir ...";
    |PTEC 807;
|FINSI;
|CIERRA #23;
|FPRC;
____________________________________/
|PROCESO printa;
|DDEFECTO #6;
    #6#24 = #3#2;
|LEE 000#6=;
|SI FSalida = 0;
        #5#0 = #6#0;     |LEE 000#5=;
        #4#0 = #5#2;     |LEE 000#4=;
|SINO;
        #4#0 = 0;
        #4#1 = "<TRABAJADOR SIN PEDIDO>";
|FINSI;
|PRINT;
    seleccio = 1;
|FPRC;

|PROCESO leyendo;
    #2#0 = #3#1;
    #2#1 = #3#2;
|LEE 000#2=;
|SI FSalida = 0;|Y #0#10 [ #2#62;|Y #0#11 ] #2#62;|Y #0#12 [ #2#63;|Y #0#13 ] #2#63;
    |SI #0#6 = "C";|O #0#6 = "A";
            venci = "CONTRATO";
        |SI #3#24 ] #0#4;|Y #3#24 [ #0#5;    |HAZ printa;   |FINSI;
    |FINSI;
    |SI #0#6 = "P";|O #0#6 = "A";
            venci = "PRORROGA";
        |SI #3#26 ] #0#4;|Y #3#26 [ #0#5;    |HAZ printa;   |FINSI;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO ordena;
    alfa_orden = "";
|DDEFECTO #2;
    #2#0 = #3#1;
    #2#1 = #3#2;
|LEE 000#2=;
|SI FSalida = 0;|Y #0#10 [ #2#62;|Y #0#11 ] #2#62;|Y #0#12 [ #2#63;|Y #0#13 ] #2#63;
    |SI #3#24 ] #0#4;|Y #3#24 [ #0#5;   alfa1 = #3#24; |FINSI;
    |SI #3#26 ] #0#4;|Y #3#26 [ #0#5;   alfa1 = #3#26; |FINSI;
        alfa_orden = (alfa1 % -104) + (alfa1 % 402) + (alfa1 % 102);
|FINSI;
    #0#9 = alfa_orden;
|FPRC;

|DEFBUCLE leyendo1;      || por empresa, trabajador
 #3#2;
 ;
 #0#0, #0#2;
 #0#1, #0#3;
 ;
 NULL, ordena, NULL, NULL, NULL, leyendo;
 #0#9, #3#1, #3#2;
|FIN;

|PROCESO tipo3; |TIPO 3;
|IMPRE 0;
|SI FSalida = 0;
    |INFOR "ettinfvt";
    |ABRE #2;
    |ABRE #4;
    |ABRE #5;
    |ABRE #6;
    |SELECT #5#6;
    |BUCLE leyendo1;
    |CIERRA #2;
    |CIERRA #4;
    |CIERRA #5;
    |CIERRA #6;
    |SI seleccio ! 0;
        |PIEINF;
    |SINO;
        |MENAV "    Seleccion vacia ...";
    |FINSI;
    |FININF;
|SINO;
    |MENAV "    Proceso abortado ...";
|FINSI;
|FINIMP;
|FPRC;
