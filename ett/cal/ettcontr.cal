|FICHEROS;
    ettcontr #0;
    facemp   #1;    || empresas FACGES
    labempre #2;    || empresas LABORAL
    nomconca #3;    || convenios cabs. LABORAL
    nomconli #4;    || convenios lins. LABORAL
    nomcateg #5;    || categorias LABORAL
    asconcep #6;    || conceptos FACTURACION
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    directo = "";
    fiche = "";
    buf_men = 0;
    ele_men = 0;
    aplic = "";

    path = "";
    facges_f = "";
    facges_p = "";
    aginom_f = "";
    aginom_p = "";
    sw_mensa = 0;
    secci = 0;
|FIN;
____________________________________/
|PROGRAMA;
|CLS;
|CABEZA "Modificacion -CONTROL PI ETT";
|PINPA #0#0;
|DDEFECTO #0;
|ABRE #0;
|LEE 101#0p;
|SI FSalida ! 0;
    |GRABA 020#0b;
|SINO;
        sw_mensa = 1;
    |HAZ el_path;
    |HAZ lee_facges;
    |HAZ lee_aginom;
    |HAZ lee_categoria;
        sw_mensa = 0;
|FINSI;
|PINDA #0#0;
|ENDATOS #1#0;
|SI FSalida = 0;
    |GRABA 020#0a;
    |HAZ cambia_menu;
|FINSI;
|LIBERA #0;
|CIERRA #0;
|FPRO;

|PROCESO defecto; |TIPO 7;
    alfa1 = #0Campo;
|QBF alfa1;
|SI alfa1 = "";
    |DIRECTORIO alfa1;
    |SI Campo = 1;  alfa1 = alfa1 + "facturar/";    |FINSI;
    |SI Campo = 3;  alfa1 = alfa1 + "laboral/";    |FINSI;
        #0Campo = alfa1;
    |PINTA #0Campo;
|FINSI;
|FPRC;

|PROCESO el_path; |TIPO 0;
    path = #0#1;    |QBF path;
    facges_f = path + "/fich/";    |PATH_DAT #1 facges_f;
    facges_p = path + "/pan/";     |PATH_PAN #1 facges_p;
                                   |PATH_PAN #6 facges_p;

    path = #0#3;    |QBF path;
    aginom_f = path + "/fich/";    |PATH_DAT #2 aginom_f;
                                   |PATH_DAT #3 aginom_f;
                                   |PATH_DAT #4 aginom_f;
                                   |PATH_DAT #5 aginom_f;
    aginom_p = path + "/pan/";     |PATH_PAN #2 aginom_p;
                                   |PATH_PAN #3 aginom_p;
                                   |PATH_PAN #4 aginom_p;
|FPRC;

|PROCESO lee_facges; |TIPO 6;
|SI FSalida ! 2;
    |DDEFECTO #1;
    |ABRE #1;
        #1#0 = #0#2;
    |LEE 000#1=;
    |SI FSalida ! 0;
        |MENAV "    Empresa INEXISTENTE ...";
        |ERROR;
    |FINSI;
    |PINTA 1227, #1#1;
    |CIERRA #1;
|FINSI;
|FPRC;

|PROCESO lee_aginom; |TIPO 6;
|SI FSalida ! 2;
    |DDEFECTO #2;
    |ABRE #2;
    |SELECT #1#2;
        #2#0 = #0#4;
    |LEE 000#2=;
    |SI FSalida ! 0;
        |MENAV "    Empresa INEXISTENTE en LABORAL ...";
        |ERROR;
    |FINSI;
    |PINTA 1527, #2#2;
    |CIERRA #2;
|FINSI;
|FPRC;

|PROCESO lee_categoria; |TIPO 0;
|SI FSalida ! 2;
    |DDEFECTO #5;
    |ABRE #5;
        #5#0 = #2#1;     || convenio
        #5#1 = #0#7;     || categoria
    |LEE 000#5=;
    |SI FSalida ! 0;
            alfa1 = "    Categoria [" + #5#0 + "-" + #5#1 + "] INEXISTENTE en LABORAL ...";
        |MENAV alfa1;
        |ERROR;
    |FINSI;
    |PINTA 1627, #5#2;
    |CIERRA #5;
|FINSI;
|FPRC;

|PROCESO lee_concepto; |TIPO 0;
|SI FSalida ! 2;
    |DDEFECTO #4;
    |ABRE #4;
        #4#0 = #2#1;     || convenio
        #4#2 = #0Campo;  || concepto
    |LEE 000#4=;
    |SI FSalida ! 0;|Y sw_mensa = 0;
            alfa1 = "    Concepto [" + #4#0 + "-" + #4#2 + "] INEXISTENTE en LABORAL ...";
        |MENAV alfa1;
        |ERROR;
    |FINSI;
    |SI Campo = 16; |PINTA 1530, #4#6;  |FINSI;
    |SI Campo = 17; |PINTA 1630, #4#6;  |FINSI;
    |SI Campo = 18; |PINTA 1730, #4#6;  |FINSI;
    |SI Campo = 14; |PINTA 1830, #4#6;  |FINSI;
    |SI Campo = 19; |PINTA 2030, #4#6;  |FINSI;
    |SI Campo = 13; |PINTA 2130, #4#6;  |FINSI;
    |CIERRA #4;
|FINSI;
|FPRC;

|PROCESO lee_seccion; |TIPO 0;
|SI FSalida ! 2;
        path = #0#1;    |QBF path;
        facges_f = path + "/fich/";
        alfa1 = #0#2;   |QBF alfa1;    alfa1 = ("00" + alfa1) % -102;
        facges_f = facges_f + alfa1 + "/";
    |PATH_DAT #6 facges_f;

        secci = Campo - 7;
    |DDEFECTO #6;
    |ABRE #6;
        #6#0 = #0secci;  || seccion
        #6#1 = #0Campo;  || concepto
    |LEE 000#6=;
    |SI FSalida ! 0;|Y sw_mensa = 0;
            alfa1 = "    Concepto [" + #6#0 + "-" + #6#1 + "] INEXISTENTE ...";
        |MENAV alfa1;
        |ERROR;
    |FINSI;
        alfa1 = #6#2 % 130;
    |SI Campo = 30; |PINTA 1650, alfa1; |FINSI;
    |SI Campo = 33; |PINTA 2050, alfa1; |FINSI;
    |SI Campo = 27; |PINTA 2150, alfa1; |FINSI;
    |SI Campo = 28; |PINTA 2250, alfa1; |FINSI;
    |CIERRA #6;
|FINSI;
|FPRC;

|PROCESO pinpa2; |TIPO 7;
    sw_mensa = 1;
    Campo = 16; |HAZ lee_concepto;
    Campo = 17; |HAZ lee_concepto;
    Campo = 18; |HAZ lee_concepto;
    Campo = 14; |HAZ lee_concepto;
    Campo = 19; |HAZ lee_concepto;
    Campo = 13; |HAZ lee_concepto;
    Campo = 14; |HAZ lee_concepto;
    Campo = 15; |HAZ lee_concepto;

    Campo = 30; |HAZ lee_seccion;
    Campo = 33; |HAZ lee_seccion;
    Campo = 27; |HAZ lee_seccion;
    Campo = 28; |HAZ lee_seccion;
    Campo = 29; |HAZ lee_seccion;
    sw_mensa = 0;
|FPRC;

|PROCESO ayuda1; |TIPO 7;
|PUSHV 07252166;
|CUADRO 07 25 21 66;
|ATRI R;
||            1234567890123456789012345678901234567890
|PINTA 0826, " ----- CLAVES DE SALARIO POSIBLES ----- ";
|PINTA 0926, " (M) cotizacion mensual                 ";
|PINTA 1026, " (D) cotizacion diaria                  ";
|PINTA 1126, " (I) cotizacion irregular por dias      ";
|PINTA 1226, " (P) cotizacion irregular por horas     ";
|PINTA 1326, " (T) tiempo parcial cotizacion diaria   ";
|PINTA 1426, " (X) tiempo parcial cotizacion mensual  ";
|PINTA 1526, " (A) r.agrario, fijos con desempleo     ";
|PINTA 1626, " (B) r.agrario, fijos sin desempleo     ";
|PINTA 1726, " (E) r.agrario, eventuales              ";
|PINTA 1826, " (C) r.citricos, lunes/sabado   (1.33)  ";
|PINTA 1926, " (F) r.citricos, lunes/viernes  (1.61)  ";
|PINTA 2026, " (H) gerentes/cooperativistas autonomos ";
|ATRI 0;
|FPRC;

|PROCESO ayuda2; |TIPO 0;
|POPV;
|FPRC;
____________________________________/
|PROCESO cambia_menu;
    directo = "";
|DIRECTORIO directo;
    fiche = directo + "ett/ett.men";
|LEESECUENCIAL fiche, buf_men, ele_men, 1, 5;
|SI ele_men > 0;
        ele_men = ele_men + 1;
    |HAZ si_esta;
    |HAZ gra_men;
|SINO;
        alfa1 = "    [" + fiche + "] NO accesible intentando lectura.";
    |MENAV alfa1;
|FINSI;
|FPRC;

|PROCESO si_esta;
|PARA para4 = 1; |SI para4 [ ele_men; |HACIENDO para4 = para4 + 1;
    |DEL_BUF buf_men, para4, aplic;
        alfa2 = aplic - " facemp ";       || si es entrada a menu
    |SI aplic ! alfa2;
            alfa1 = aplic % -105;
            alfa1 = aplic - alfa1;
            alfa2 = #0#2;     |QBF alfa2;    alfa2 = ("00" + alfa2) % -102;
            aplic = alfa1 + " " + alfa2 + "}}";
        |AL_BUF buf_men, para4, aplic;
    |FINSI;
|SIGUE;
|FPRC;

|PROCESO gra_men;        || regraba el menu
    fiche = directo + "ett/ett.men";
|GRABASECUENCIAL fiche, buf_men, 1, ele_men;
|SI FSalida < 0;
        alfa1 = "    [" + fiche + "] NO accesible intentando grabacion.";
    |MENAV alfa1;
|FINSI;
|FPRC;
