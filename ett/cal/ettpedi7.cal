|FICHEROS;
    ettpedi7 #0;         || seleccionar cabs.
    ettpedi2 #1,MANTE;   || seleccionar lins.
    ettpuest #2;         || puestos trabajo
    ettclien #3;         || clientes
    ettperso #5;         || personal
    ettpers2 #6;         || puestos de trabajo personal
    ettocuca #11;        || ocupaciones cabs.
    ettoculi #12;        || ocupaciones lins.
    ettpedi1 #13;        || creacion pedido
    ettpedi8 #14,MANTE;  || consulta pendientes
    ettcontr #100;       || control de la aplicacion
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    inkey = 0;
    valoraci = 0;
    nedad = 0;
    npues1 = 0;
    npues2 = 0;
    linea = 0;
    sw_ocupa = "";
    sw_puest = 0;
    sw_alta = 0;
    ultimo = 0;

{-1}menu   = "";
    menu1  = "1754";
    menu2  = "1";
    menu3  = "6";
    menu4  = "Experiencia.";
    menu5  = "Formacion.";
    menu6  = "Puestos solicitados.";
    menu7  = "Observaciones.";
    menu8  = "Fotografia.";
    menu9  = "Mas Datos.";
    &modoex = 0;
    &person = 0;
    sw_repinta = 0;
    FEntrada = 0;
    aPath = "";
    aFic = "";
    aDes = "";
    aAlfa = "";
    aIP = "";
    nBoton1 = 0;
|FIN;
____________________________________/ CALCULOS DESDE CAMPOS
|PROCESO pen_min;
    #14#0 = 1;
|FPRC;

|PROCESO pen_max;
    #14#0 = 99999;
|FPRC;

|PROCESO pedidos1;
|SI #13#71 ! #13#72;
    |DDEFECTO #2;  #2#0 = #13#59;  |LEE 000#2=;
    |DDEFECTO #3;  #3#0 = #13#2;   |LEE 000#3=;

    |DDEFECTO #14;
        #14#0 = #13#0;   || pedido
        #14#1 = #13#1;   || fecha
        #14#2 = #13#2;   || cliente
        #14#3 = #13#59;  || puesto
        #14#4 = #13#5;   || inicio contrato
        #14#5 = #13#60;  || candidatos
        #14#6 = #13#72;  || seleccionados
        #14#7 = #13#73;  || contratados
        #14#8 = #3#1;    || nombre cliente
        #14#9 = #2#1;    || nombre puesto
        #14#10 = #13#71; || requeridos
    |GRABA 020#14n;
        seleccio = 1;
|FINSI;
|FPRC;

|DEFBUCLE pedidos;
 #13#1;
 ;
     1;
 99999;
 ;
 NULL, pedidos1;
|FIN;

|PROCESO pendientes;
|CIERRAT #0;
    seleccio = 0;
    alfa1 = Usuario;     |QBT alfa1;
|NOME_DAT #14 alfa1;
|DELINDEX #14;
|ABRE #2;
|ABRE #3;
|ABRE #14;
|BUCLE pedidos;
|CIERRA #2;
|CIERRA #3;
|CIERRA #14;
|SI seleccio = 1;
    |PUSHV 0401 2380;
    |ATRI R;
    |MENSA "     < Pedidos con Selecciones Pendientes >";
    |ATRI 0;
    |ENTLINEAL #1#14, 1, 4, 16, 1, pen_min, pen_max;
        #0#0 = #14#0;
    |POPV;
|SINO;
    |MENAV "    No existen pedidos con selecciones pendientes ...";
|FINSI;
|DELINDEX #14;
|ABRET #0;
|FPRC;

|PROCESO lee_pedido; |TIPO 0;
    inkey = FSalida;
|SI inkey ! 1;                || si no pulsa ESC-ESC
    |SI inkey = 9;
        |HAZ pendientes;
        |SI seleccio = 1;
            |PINTA #0#0;
            |PTEC 802;
            |PTEC 802;
        |FINSI;
        |ERROR;
    |SINO;
        |ABRE #13; #13#0 = #0#0;  |LEE 000#13=;  |CIERRA #13;
        |ABRE #3;  #3#0  = #13#2; |LEE 000#3=;   |CIERRA #3;

        |ATRI I;
        |PINTA 0712, #13#1; || fecha
        |PINTA 0812, #13#2; || cliente
        |PINTA 0818, #3#1;  || nombre cliente
        |ATRI 0;

        |ATRI R;
        |PINTA 0675, #13#71;|| requeridos
        |PINTA 0775, #13#60;|| candidatos
        |PINTA 0875, #13#72;|| seleccionados
        |PINTA 0975, #13#73;|| contratados
        |ATRI 0;

        |SI #13#71 = #13#73;
            |MENAV "    AVISO: El personal requerido YA se ha contratado ...";
            |ERROR;
        |SINO;
            |SI #13#71 = #13#72;
                |MENAV "    AVISO: El personal requerido YA se ha seleccionado ...";
            |SINO;
                |SI #13#5 = "01.01.1900";|O #13#6 = "01.01.1900";
                    |MENAV "    AVISO: El periodo de vigencia del contrato esta SIN determinar ...";
                |FINSI;
                |SI #13#7 = 0;|O #13#8 = 0;
                    |MENAV "    AVISO: El horario esta SIN determinar ...";
                |FINSI;
            |FINSI;
        |FINSI;
    |FINSI;
|SINO;
    |PTEC 807;
|FINSI;
|FPRC;
____________________________________/
|PROCESO solo_alta; |TIPO 7;
    modo = (FEntrada / 100) ! 0;
|SI modo = 1;
    |C_M #1#2, 1, "S";
        sw_alta = 1;
|SINO;
    |C_M #1#2, 1, "N";
        sw_alta = 0;
|FINSI;
|FPRC;

|PROCESO personal; |TIPO 0;
|SI sw_alta = 1;
    |ABRE #5;
        #5#0 = #1#2;
    |LEE 000#5=;
    |SI FSalida = 0;
        |HAZ la_edad;
        |ABRE #6;  |SELECT #2#6;  |HAZ valoracion;    |CIERRA #6;
        |SI #13#5 ! "01.01.1900"; |HAZ ocupado;  |FINSI;
            #1#3 = #5#17;         || sexo
            #1#4 = nedad;         || edad
            #1#5 = #5#18;         || peso
            #1#6 = #5#19;         || estatura
            #1#7 = valoraci;      || valoracion
            #1#8 = #5#1;          || alta en base
            #1#9 = sw_ocupa;      || ocupado S/N
            #1#10 = #5#2;         || apellidos y nombre
            #1#11 = #5#8;         || localidad
            #1#12 = #5#9;         || codigo provincia
            #1#13 = #5#10;        || codigo poblacion
            #1#14 = #5#11;        || telefono 1
            #1#15 = #5#12;        || telefono 2
        |PINDA #0#1;
    |FINSI;
    |CIERRA #5;
|FINSI;
|FPRC;

|PROCESO VerFoto; |TIPO 7;
     |ABRE #100; |LEE 000#100p; |CIERRA #100;
     aPath = #100#36; |QBF aPath;
     |MKDIR aPath;

     aFic = #5#25; |QBF aFic;
     |SI aFic ! "";
          aFic = aPath + aFic;
          |XABRE "E", aFic, 0;
          |SI FSalida = 0;
               |IP_REMOTO aIP;
               |SI aIP ! "";
                    aAlfa = "c:\tmp\"; |RMKDIR aAlfa;
                    aDes = "c:\tmp\" + #5#1; |QBF aDes;
                    |ENVIA_FICHERO aFic, aDes;
               |SINO;
                    aDes = aFic;
               |FINSI;
               aFic = "IMAGEN," + aDes;
               |CONTROL_SIMPLE 0, aFic , 1057, 2279, NULL;
               nBoton1 = FSalida;
               |ESTADO_CONTROL nBoton1, "ENABLE";
               |PAUSA;
               |FIN_CONTROL_WINDOWS nBoton1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO repinta;
    sw_repinta = 1;
|PINPA #0#5;
|PINDA #0#5;
    sw_repinta = 0;
|FPRC;

|PROCESO Complementarios;
     |CLS;
     |PINPA #1#5;
     |PINDA #1#5;
     |PAUSA;
|FPRC;

|PROCESO el_menu;
    ||modo = (FEntrada / 100) ! 0;
    modo = 4;
|SI modo ! 3;|Y modo ! 0;
        person = #5#0;
        modoex = 4;      || SIEMPRE CONSULTA
    |HAZ repinta;
    |HAZ VerFoto;
    |SI FSalida ! 1;          || si no se pulsa escape
        |PARA; |SI; |HACIENDO;
            |MENUG menu;
            |SI FSalida > 0;
                    menu2 = FSalida;
                |SI FSalida ! 5;
                    ||HAZ borra_win;
                |FINSI;
                |SI FSalida = 1;  |SUB_EJECUTA "ettpers0"; |FINSI;
                |SI FSalida = 2;  |SUB_EJECUTA "ettpers1"; |FINSI;
                |SI FSalida = 3;  |SUB_EJECUTA "ettpers2"; |FINSI;
                |SI FSalida = 4;  |SUB_EJECUTA "ettpers3"; |FINSI;
                |SI FSalida = 5;  |HAZ VerFoto;            |FINSI;
                |SI FSalida = 6;
                    |HAZ Complementarios;
                |FINSI;
            |SINO;
                |SAL;
            |FINSI;
            |HAZ repinta;
        |SIGUE;
    |FINSI;
    |HAZ repinta;
|FINSI;
|FPRC;

|PROCESO tipo11; |TIPO 11;
|SI FSalida = 9;
    |PUSHV 0501 2380;
    |PINPA #0#5;
    |ABRE #5;  #5#0 = #1#2;   |LEE 000#5=;
    |PINDA #0#5;
    ||ENDATOS #4#5;
    |HAZ el_menu;
    |CIERRA #5;
    |POPV;
    |ERROR;
|FINSI;
|SI FSalida = 10;
    |SI #1#24 = 0;
        |SI #13#72 = #13#71;|Y #1#27 = "N";
            |MENAV "    AVISO: Seleccion YA completada ...";
        |SINO;
            |SI #1#27 = "S";
                    #1#27 = "N";
                    #13#72 = #13#72 - 1;
            |SINO;
                    #1#27 = "S";
                    #13#72 = #13#72 + 1;
            |FINSI;
            |GRABA 020#1a;
            |SI FSalida = 0;
                |PINTA #1#27;
                |GRABA 020#13a;
                |SI FSalida = 0;
                    |ATRI R;
                    |PINTA 0875, #13#72;
                    |ATRI 0;
                |FINSI;
            |FINSI;
        |FINSI;
    |SINO;
        |MENAV "    AVISO: Este trabajador YA esta contratado ...";
    |FINSI;
    |ERROR;
|FINSI;
|FPRC;

|PROCESO min;
    #1#0 = #13#0;
    #1#1 = 1;
    #1#57 = "S";
|FPRC;

|PROCESO max;
    #1#0 = #13#0;
    #1#1 = 99999;
    #1#57 = "S";
|FPRC;

|PROCESO tipo3; |TIPO 3;
|ABRET #13;
    #13#0 = #0#0;
|LEE 121#13=;
|SI FSalida = 0;
    |ENTLINEAL #6#1, -2, 3, 18, 0, min, max;
|FINSI;
|LIBERA #13;
|CIERRAT #13;
|FPRC;

**************************************************************************

____________________________________/ CALCULA LA EDAD
|PROCESO la_edad;
    nedad = 0;
|SI #5#14 ! "01.01.1900";
        nume1 = #5#14;   || fecha nacimiento
        nume2 = #13#1;   || fecha pedido
        nedad = ((nume2 - nume1) / 365) ! 1;      || con un decimal
|FINSI;
|FPRC;
____________________________________/ VALORACION DEL PUESTO
|PROCESO valoracion;
    valoraci = 0;
    npues1 = 0;
    npues2 = 0;
|HAZ puesto;
|PARA para1 = 4; |SI para1 [ 133; |HACIENDO para1 = para1 + 1;
    |SI #6para1 ! 0;
            npues1 = npues1 + 1;
            npues2 = npues2 + #6para1;
    |FINSI;
|SIGUE;
    valoraci = (npues2 / npues1) ! 4;
|FPRC;
____________________________________/ SELECCION POR FECHAS OCUPADAS
|PROCESO ocupacion1;
|SI #13#5 ] #12#2;|Y #13#5 [ #12#3;
        sw_ocupa = "S";                 || inicio contrato ocupado
|FINSI;
|SI #13#6 ] #12#2;|Y #13#6 [ #12#3;
        sw_ocupa = "S";                 || final contrato ocupado
|FINSI;
|FPRC;

|DEFBUCLE ocupacion;
 #11#1;
 ;
 #5#0;
 #5#0;
 ;
 NULL, NULL, ocupacion1;
|FIN;

|PROCESO ocupado;
    sw_ocupa = "N";
|BUCLE ocupacion;
|FPRC;
____________________________________/ SELECCION POR PUESTO DE TRABAJO
|PROCESO puesto;
    #6#0 = #5#0;
    #6#2 = #13#59;
|LEE 000#6=;
|SI FSalida = 0;
        sw_puest = 1;
|FINSI;
|FPRC;
