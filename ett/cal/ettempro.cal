|FICHEROS;
    ettempro #0;    || emision prorrogas
    ettpedi1 #1;    || pedidos cabs.
    ettpedi2 #2;    || pedidos lins.
    ettparte #3;    || partes de horas cabs.
    ettparli #4;    || partes de horas lins.
    ettocuca #5;    || ocupacion cabs.
    ettoculi #6;    || ocupacion lins.

    labempre #10;   || empresas AGINOM
    labtraba #12;   || trabajadores AGINOM
    ettcontr #13;   || control AGIETT
    ettclien #14;   || clientes AGIETT
    ettprovi #15;   || provincias AGIETT
    ettperso #16;   || personal AGIETT
    ettpuest #17;   || puestos AGIETT
    etttarca #18;   || tarifas cabs. AGIETT
    labregis #19;   || registro contratos AGINOM

    asfpagos #119;  || formas de pago FACGES
    asbancli #120;  || bancos clientes FACGES
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    nume5 = 0;
    nume6 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    &hpedido = 0;
    &hperson = 0;
    &hcopias = 0;
    &hfirmas = @;
    &hfallos = 0;
    informe = "";
    dia_semana = 0;
    fecha = @;
    ultimo = 0;
    des = 0;
    has = 0;
    topes = 0;
    mes_ant = 0;
    mes_act = 0;
    fec_act = @;
    apunta1 = 0;
    apunta2 = 0;
    apunta3 = 0;
    apunta4 = 0;
    ulti_fec = @;
    despa = @;
    haspa = @;
    contador = 0;
    prorroga = "";
    nro_pro = 0;

    &fpago = "";
    &banco = "";
    &empieza = "";
    &finaliza = "";
    &supuesto1 = "";
    &supuesto2 = "";
    &supuesto3 = "";
    &supuesto4 = "";
    &titulo1   = "";

    tecalf1 = "";
    tecalf2 = "";
    tecalf3 = "";
    tecalf4 = "";
    tecnum1 = 0;
    tecnum2 = 0;
    tecnum3 = 0;
    tecnum4 = 0;
    tecpar1 = 0;
    tecpar2 = 0;
    tecpar3 = 0;
    tecpar4 = 0;
    teclea = "";
    sw_repite = 0;
    nro_parte = 0;
    sw_ultimo = 0;
    &infor_pro = "labprott";
|FIN;
____________________________________/
|PROCESO def_hasta; |TIPO 7;
|SI #2#30 = "N";
        #0#7 = #0#6 + 0.001;       || fecha inicial mas 1 mes
|SINO;
        #0#7 = #2#33;              || fecha ultimo parte
|FINSI;
|PINTA #0#7;
|FPRC;

|PROCESO fec_hasta; |TIPO 0;
|SI #0#7 < #0#6;
    |MENAV "    La fecha final PROVISIONAL debera ser mayor o igual que la inicial ...";
    |ERROR;
|FINSI;
|SI #0#7 < #2#33;
    |MENAV "    La fecha final PROVISIONAL debera ser mayor o igual que la anterior ...";
        #0#7 = #2#33;
    |PINTA #0#7;
    |ERROR;
|FINSI;
|FPRC;
____________________________________/
|PROCESO informes;
|SI hfallos = 0;         || si da error algun informe no continua
    |INFOR informe;
    |SI FSalida = 0;
            #2#30 = "S";           || impreso SI
        |SI ulti_fec ! "00.00.0000";
                #2#33 = ulti_fec;  || fecha ultimo parte
        |FINSI;
        |GRABA 020#2a;
        |PRINT;
        |PIEINF;
    |SINO;
            alfa1 = "    AVISO: Problemas con el informe [" + informe + "] ...";
        |MENAV alfa1;
            hfallos = 1;
    |FINSI;
    |FININF;
|FINSI;
|FPRC;

|PROCESO pedidos_lins;
    #1#5 = #19#19;       || fecha inicio prorroga
    #1#6 = #19#20;       || fecha final prorroga
|HAZ fecha_fin;

    supuesto1 = #1#74 % 170;  supuesto2 = #1#74 % -150;
    supuesto3 = #1#75 % 170;  supuesto4 = #1#75 % -150;

    hfallos = 0;
|PARA para1 = 1; |SI para1 [ hcopias; |HACIENDO para1 = para1 + 1;
        informe = "ett01cpd"; |HAZ informes;
        informe = "ett02cpd"; |HAZ informes;
        informe = "ett03cpd"; |HAZ informes;
        informe = "ett04cpd"; |HAZ informes;
        informe = "ett05cpd"; |HAZ informes;
|SIGUE;
|FPRC;

|PROCESO imprime_parte;
    seleccio = 1;
    hfallos = 0;
|PARA para1 = 1; |SI para1 [ hcopias; |HACIENDO para1 = para1 + 1;
        informe = "ett01par"; |HAZ informes;
|SIGUE;
|FPRC;

|DEFBUCLE reimprime;
 #3#2;
 ;
 #2#0, #2#2, nro_parte;
 #2#0, #2#2, 999999999999;
 ;
 NULL, imprime_parte;
|FIN;

|PROCESO busca_mes;
    nume6 = -1;
|PARA para1 = 8; |SI para1 [ 14; |HACIENDO para1 = para1 + 1;
    |SI #3para1 ! "01.01.0000";
            alfa1 = #3para1;
            alfa2 = alfa1 % 402;
            nume5 = alfa2;
        |SI nume6 = -1;  nume6 = nume5; |FINSI;
        |SI nume5 = nume6;
                #3#55 = nume5;          || mes AGINOM
                alfa2 = alfa1 % -104;
                #3#56 = alfa2;          || anyo AGINOM
        |SINO;
            |MENAV "    ATENCION! Parte conteniendo meses distintos ...";
        |FINSI;
    |FINSI;
|SIGUE;
|FPRC;

|PROCESO grabalo;
    #3#47 = #3#22+#3#23+#3#24+#3#25+#3#26+#3#27+#3#28; || tot.horas
|HAZ busca_mes;
|GRABA 020#3a;
|SI FSalida = 0;
    |SI #3#8  ! "01.01.0000"; ulti_fec = #3#8;    |FINSI;
    |SI #3#9  ! "01.01.0000"; ulti_fec = #3#9;    |FINSI;
    |SI #3#10 ! "01.01.0000"; ulti_fec = #3#10;   |FINSI;
    |SI #3#11 ! "01.01.0000"; ulti_fec = #3#11;   |FINSI;
    |SI #3#12 ! "01.01.0000"; ulti_fec = #3#12;   |FINSI;
    |SI #3#13 ! "01.01.0000"; ulti_fec = #3#13;   |FINSI;
    |SI #3#14 ! "01.01.0000"; ulti_fec = #3#14;   |FINSI;
        #4#0 = #3#0;     || nro.parte
        #4#1 = 1;        || linea
        #4#2 = "L";      || dia semana
        #4#3 = 101;      || cod.concepto
        #4#4 = 0;        || importe
        #4#5 = "";       || descripcion
    |GRABA 020#4n;
|FINSI;
|LIBERA #3;
|FPRC;

|PROCESO ruptura_mes;
|SI mes_ant = -1;
        alfa1 = fec_act; alfa1 = alfa1 % 402;     mes_ant = alfa1;
|FINSI;
    alfa1 = fec_act;     alfa1 = alfa1 % 402;     mes_act = alfa1;
|SI mes_ant ! mes_act;
    |HAZ grabalo;
    |HAZ ultimo;
|FINSI;
|FPRC;

|PROCESO ultimo;
|LEE 000#3u;
|SI FSalida = 0;
        ultimo = #3#0 + 1;
|SINO;
        ultimo = 1;
|FINSI;
    mes_ant = -1;
|DDEFECTO #3;
    #3#0 = ultimo;        || numero parte
|GRABA 020#3b;
    #3#1 = #2#2;         || codigo trabajador
    #3#2 = #1#2;         || codigo cliente
    #3#3 = #2#0;         || codigo pedido
    #3#4 = #2#24;        || codigo AGINOM
    #3#7 = #1#1;         || fecha pedido
    #3#43 = #1#7;        || desde hora 1
    #3#44 = #1#8;        || hasta hora 1
    #3#45 = #1#9;        || desde hora 2
    #3#46 = #1#10;       || hasta hora 2
    #3#48 = #2#1;        || linea del pedido
    #3#51 = #2#16;       || codigo tarifa
    #3#52 = #2#17;       || nivel tarifa
    #3#53 = #2#18 + #2#22 + #2#23; || ~precio hora nomina
    #3#54 = #2#21;                 || ~precio hora fra.
|GRABA 020#3a;

|SI sw_ultimo = 0;
        nro_parte = #3#0;
        sw_ultimo = 1;
|FINSI;
|FPRC;

|PROCESO calcula_parte;
|HAZ ultimo;

    nume2 = dia_semana - 1;
    topes = dia_semana + 7;
|PARA apunta1 = 8; |SI apunta1 [ topes; |HACIENDO apunta1 = apunta1 + 1;
        nume1 = fecha;
        nume1 = nume1 - nume2;
    |SI nume1 ] des;|Y nume1 [ has;

            fec_act = nume1;
        |HAZ ruptura_mes;               || cambio de mes en un mismo parte

            #3apunta1 = nume1;          || dia
        |SI nume1 = 729390;   #3apunta1 = "01.01.1997";     |FINSI;
        |SI nume1 = 730851;   #3apunta1 = "01.01.2001";     |FINSI;
            apunta4 = apunta1 + 55;     || puntero trabaja S/N
        |SI #1apunta4 = "S";
                apunta2 = apunta1 + 7;  || puntero marca
                #3apunta2 = "*";        || marca
                apunta3 = apunta1 + 14; || puntero horas dia
                #3apunta3 = #2#26;      || horas dia
        |FINSI;

    |FINSI;
        nume2 = nume2 - 1;
|SIGUE;

|HAZ grabalo;
|FPRC;

|PROCESO bucle_partes;
    fech1 = despa % 1;
    alfa1 = fech1;
    alfa1 = alfa1 % 11;
|QBF alfa1;
|SI alfa1 = "Lunes";     dia_semana = 1;     |FINSI;
|SI alfa1 = "Martes";    dia_semana = 2;     |FINSI;
|SI alfa1 = "Miercoles"; dia_semana = 3;     |FINSI;
|SI alfa1 = "Jueves";    dia_semana = 4;     |FINSI;
|SI alfa1 = "Viernes";   dia_semana = 5;     |FINSI;
|SI alfa1 = "Sabado";    dia_semana = 6;     |FINSI;
|SI alfa1 = "Domingo";   dia_semana = 7;     |FINSI;
|PARA fecha = despa; |SI fecha < haspa; |HACIENDO fecha = fecha + 1;
    |SI dia_semana = 7;
        |HAZ calcula_parte;
            dia_semana = 0;
    |FINSI;
        dia_semana = dia_semana + 1;
|SIGUE;
|HAZ calcula_parte;
|FPRC;

|PROCESO fecha_fin;
|SI #1#6 = "00.00.0000"; #1#6 = "01.01.1900";     |FINSI;

    fech1 = #1#6 % 2;
    alfa1 = fech1;
    alfa1 = alfa1 % 11;
    finaliza = "hasta el " + alfa1;
|SI #12#45 = "14";  finaliza = "hasta el t‚rmino de la obra o servicio.";   |FINSI;
|SI #12#45 = "16";  finaliza = "hasta la incorporaci¢n del trabajador sustituido."; |FINSI;
|FPRC;

|PROCESO lectura_varios;
|SI #2#35 = "H"; titulo1 = "a la hora  "; |FINSI;
|SI #2#35 = "S"; titulo1 = "a la semana"; |FINSI;
|SI #2#35 = "M"; titulo1 = "al mes     "; |FINSI;

|DDEFECTO #10;
|ABRE #10;
    #10#0 = #13#4;
|LEE 000#10=;            || lee empresas AGINOM
|CIERRA #10;

|DDEFECTO #12;
|ABRE #12;
    #12#0 = #13#4;
    #12#1 = #2#24;
|LEE 000#12=;            || lee trabajadores AGINOM
|CIERRA #12;

|DDEFECTO #14;
|ABRE #14;
    #14#0 = #1#2;
|LEE 000#14=;            || lee cliente AGIETT
|CIERRA #14;

|DDEFECTO #15;
|ABRE #15;
    #15#0 = #14#5;
|LEE 000#15=;            || lee provincia AGIETT
|CIERRA #15;

|DDEFECTO #16;
|ABRE #16;
    #16#0 = #2#2;
|LEE 000#16=;            || lee personal AGIETT
|CIERRA #16;

|DDEFECTO #17;
|ABRE #17;
    #17#0 = #1#59;
|LEE 000#17=;            || lee puestos AGIETT
|CIERRA #17;

|DDEFECTO #18;
|ABRE #18;
    #18#0 = #2#16;
    #18#1 = #2#17;
|LEE 000#18=;            || lee tarifa cabs. AGIETT
|CIERRA #18;

|DDEFECTO #119;
|ABRE #119;
    #119#0 = #14#26;
|LEE 000#119=;           || lee formas de pago FACGES
    fpago = #119#1;
|CIERRA #119;

|DDEFECTO #120;
|ABRE #120;
    #120#0 = #14#24;
|LEE 000#120=;           || lee bancos cliente FACGES
    banco = #120#1;
|CIERRA #120;
|FPRC;

|PROCESO tipo8;
|ABRE #13;
|LEE 000#13p;
|SI FSalida = 0;
        alfa1 = #13#3;    |QBF alfa1;
        alfa1 = alfa1 + "/fich/";
    |PATH_DAT #10 alfa1;
    |PATH_DAT #12 alfa1;
    |PATH_DAT #19 alfa1;

        alfa1 = #13#1;    |QBF alfa1;
        alfa1 = alfa1 + "/fich/";
        alfa2 = #13#2;    |QBF alfa2;   alfa2 = ("00" + alfa2) % -102;
        alfa1 = alfa1 + alfa2 + "/";
    |PATH_DAT #119 alfa1;
    |PATH_DAT #120 alfa1;
|SINO;
    |MENAV "    Control AGIETT sin definir ...";
|FINSI;
|CIERRA #13;
|FPRC;

|PROCESO emite;
|ABRE #19;
|SELECT #1#19;
    #19#0 = contador;
|LEE 121#19=;
|SI FSalida = 0;
    |DDEFECTO #0;
        #0#0 = #1#0;      || codigo pedido
        #0#1 = #1#1;      || fecha pedido
        #0#2 = #16#0;     || codigo trabajador
        #0#3 = #16#2;     || nombre trabajador
        #0#4 = #14#0;     || codigo cliente
        #0#5 = #14#1;     || nombre cliente
        #0#6 = #19#19;    || desde contrato
        #0#7 = #19#20;    || hasta contrato
        #0#8 = #17#0;     || codigo puesto
        #0#9 = #17#1;     || nombre puesto
    |PUSHV 0501 2380;
    |PINPA #0#0;
    |PINDA #0#0;
        sw_repite = 0;
        FEstado = 0;
    |PARA; |SI FEstado = 0; |HACIENDO;
        |MENSA "    A imprimir PARTES DE HORAS correspondientes a la PRORROGA ...";
        |IMPRE 0;
        |SI FSalida = 0;
            |BLIN  4;
            |ATRI P;
            |PINTA 2402, "Imprimiendo partes de horas ...";
            |ATRI 0;
                despa = #0#6;      || desde prorroga
                haspa = #0#7;      || hasta prorroga
            |SI despa [ haspa;|Y sw_repite = 0;
                    des = despa;         || desde contrato (numerico)
                    has = haspa;         || hasta contrato (numerico)
                |HAZ bucle_partes;       || graba partes nuevos
            |FINSI;
                seleccio = 0;
            |BUCLE reimprime;      || imprime partes
            |FINIMP;
            |BLIN 24;
            |SI seleccio = 0;
                |MENAV "    AVISO: No se encuentran partes para imprimir ...";
            |FINSI;
        |FINSI;

        |MENSA "    A imprimir CPD correspondiente a la PRORROGA ...";
        |IMPRE 0;
        |SI FSalida = 0;
            |BLIN  4;
            |ATRI P;
            |PINTA 2402, "Imprimiendo CPD ...";
            |ATRI 0;
            |HAZ pedidos_lins;     || imprime CPD
            |FINIMP;
            |BLIN 24;
        |FINSI;

            sw_repite = 1;
        |CONFI "    NRepetir la impresion (S/N): ";
            FEstado = FSalida;
    |SIGUE;
    |POPV;
|FINSI;
|LIBERA #19;
|CIERRA #19;
|FPRC;

|PROCESO tecleando;
    tecalf1 = teclea;
    tecalf1 = tecalf1 + &13;
    tecalf2 = tecalf1 % 0;
    tecnum1 = tecalf2;
|PARA tecpar1 = tecnum1; |SI tecpar1 ] 1; |HACIENDO tecpar1 = tecpar1 - 1;
        tecnum2 = (tecpar1 * 100) + 1;
        tecalf2 = tecalf1 % tecnum2;
        tecnum3 = &tecalf2;
    |PTEC tecnum3;
|SIGUE;
|FPRC;

|PROCESO ejecuta;
    alfa1 = "";
|DIRECTORIO alfa1;
|SI alfa1 = "/u/agisys/";
        ||prorroga = ":agilab/labprorr.tab" + &59 + "labprott";
        prorroga = ":laboral/labprorr.tab";
|SINO;
        ||prorroga = ":aginom/labprorr.tab" + &59 + "labprott";
        prorroga = ":laboral/labprorr.tab";
|FINSI;
    teclea = &13;   |HAZ tecleando;          || nro. prorrogas
    teclea = &13;   |HAZ tecleando;          || centro de trabajo
    teclea = #12#1; |HAZ tecleando;          || trabajador
    teclea = #12#0; |HAZ tecleando;          || empresa
|SUB_EJECUTA prorroga;
|FPRC;

|PROCESO ocupacion;
|ABRE #6;
|SELECT #2#6;
    #6#0 = #2#2;    || cod.personal
    #6#9 = #2#24;   || cod.AGINOM
|LEE 101#6=;
|SI FSalida = 0;
        #6#3 = #19#20;        || hasta fecha de ocupacion
    |GRABA 020#6a;
|FINSI;
|LIBERA #6;
|CIERRA #6;
|FPRC;

|PROCESO lee_registro;
    contador = -1;
|ABRE #19;
|SELECT #2#19;
    #19#1 = #13#4;
    #19#2 = #2#24;
|LEE 000#19=;
|SI FSalida = 0;
    |SI #19#8 ! "..........";|Y #19#8 ! "          ";
            contador = #19#0;
    |SINO;
            contador = -2;
    |FINSI;
|FINSI;
|CIERRA #19;
|FPRC;

|PROGRAMA;
    hfallos = 1;
    nro_parte = 1;
    ulti_fec = "00.00.0000";
|ABRE #1;
|ABRE #2;
|ABRE #3;
|ABRE #4;
    #1#0 = hpedido;
|LEE 020#1=;                       || lee pedido cabs.
|SI FSalida = 0;
        #2#0 = hpedido;
        #2#1 = hperson;
    |LEE 121#2=;                   || lee pedido lins.
    |SI FSalida = 0;|Y #2#30 = "S";|Y #2#33 ! "01.01.1900";
        |HAZ tipo8;                || lee control y cambia path's
        |HAZ lectura_varios;       || lee resto ficheros
        |HAZ lee_registro;         || lee registro contratos
        |SI contador > -1;         || si existe contrato inicial
                nro_pro = #19#15;
            |HAZ ejecuta;          || emite prorroga
            |HAZ lee_registro;     || vuelve a leer registro contratos
            |SI nro_pro ! #19#15;  || si se ha emitido la prorroga:
                |HAZ ocupacion;    || 1)actualiza fecha hasta ocupacion
                |HAZ emite;        || 2)emite los partes de horas y el CPD
            |FINSI;
        |SINO;
            |SI contador = -1;
                    alfa1 = "    Trabajador:["+#12#0+"]-["+#12#1+"] sin contrato inicial registrado.";
            |FINSI;
            |SI contador = -2;
                    alfa1 = "    Trabajador:["+#12#0+"]-["+#12#1+"] sin fecha final en contrato inicial.";
            |FINSI;
            |MENAV alfa1;
        |FINSI;
    |SINO;
        |SI FSalida = 0;
            |MENAV "    Faltan emitir los partes de horas del contrato inicial ...";
        |FINSI;
    |FINSI;
    |LIBERA #2;
|FINSI;
|CIERRA #1;
|CIERRA #2;
|CIERRA #3;
|CIERRA #4;
|FPRO;
