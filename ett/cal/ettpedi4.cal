|FICHEROS;
    ettpedi4 #0;         || contratos cabs.
    ettpedi1 #1;         || pedidos cabs.
    ettpuest #2;         || puestos trabajo
    ettclien #3;         || clientes
    ettpedi5 #4,MANTE;   || contratos lins.
    etttarca #5;         || tarifas cabs.
    etttarli #6;         || tarifas lins.
    ettperso #7;         || personal
    ettcontr #8;         || control aplicacion
    ettprovi #9;         || provinciero
    ettocuca #10;        || ocupacion cabs.
    ettoculi #11;        || ocupacion lins.
    ettpedi6 #12,MANTE;  || contratos auxiliar (datos laborales)
    ettpedi8 #14,MANTE;  || consulta pendientes
    ettped10 #15;

    ettpedi2;

    labempre #20;        || empresas AGINOM
    labtraba #21;        || trabajadores AGINOM
    nomcateg #22;        || categorias
    labprovi #23;        || provinciero
    nomconca #24;        || convenios cabs.
    nomconli #25;        || convenios lins.
    nomepigr #26;        || epigrafes
    nomregim;            || epigrafes
    labcentr;
    nomtrato;
    tfggrcot;
|FIN;

|VARIABLES;
    Comodin = "";
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    Calc      = 0;
    nCont     = 0;
    sw_modifi = 0;
    ultimo = 0;
    conten = 0;
    inkey = 0;
    traba = 0;
    matricula = 0;
    error = "";
    pantalla = 1;
    ct_epig = 0;
     aAlfa = "";
     aPunto  = "";

     nPedido = 0;
     nRegActual = 0;
     nTrabCopia = 0;
     nRegistro = 0;
|FIN;
____________________________________/ CALCULOS DESDE CAMPOS
|PROCESO tipo8; |TIPO 8;
|ABRE #8;
|LEE 000#8p;
|SI FSalida = 0;
        alfa1 = #8#3;    |QBF alfa1;    alfa1 = alfa1 + "/fich/";
        alfa2 = #8#3;    |QBF alfa2;    alfa2 = alfa2 + "/pan/";
    |PATH_DAT #20 alfa1;
    |PATH_DAT #21 alfa1;
    |PATH_DAT #22 alfa1;
    |PATH_DAT #23 alfa1;
    |PATH_DAT #24 alfa1;
    |PATH_DAT #25 alfa1;
    |PATH_DAT #26 alfa1;
    |PATH_DAT #nomregim#alfa1#;
    |PATH_DAT #labcentr#alfa1#;
    |PATH_DAT #nomtrato#alfa1#;
    |PATH_DAT #tfggrcot#alfa1#;

    |PATH_PAN #21 alfa2;
    |PATH_PAN #22 alfa2;
    |PATH_PAN #24 alfa2;
    |PATH_PAN #25 alfa2;
    |PATH_PAN #26 alfa2;
    |HAZ lee_empresa;
|SINO;
    |MENAV "    Control aplicacion SIN definir ...";
    |PTEC 807;
|FINSI;
|CIERRA #8;
|FPRC;

|PROCESO lee_empresa;
|ABRE #20;
    #20#0 = #8#4;
|LEE 000#20=;
|SI FSalida ! 0;
    |MENAV "    Empresa no encontrada en LABORAL ";
    |PTEC 807;
|FINSI;
|CIERRA #20;
|FPRC;

|PROCESO pen_min;
    #14#0 = 1;
|FPRC;

|PROCESO pen_max;
    #14#0 = 99999;
|FPRC;

|PROCESO pedidos1;
|SI #1#72 ! #1#73;
    |DDEFECTO #2;   #2#0 = #1#59;  |LEE 000#2=;
    |DDEFECTO #3;   #3#0 = #1#2;   |LEE 000#3=;

    |DDEFECTO #14;
        #14#0 = #1#0;    || pedido
        #14#1 = #1#1;    || fecha
        #14#2 = #1#2;    || cliente
        #14#3 = #1#59;   || puesto
        #14#4 = #1#5;    || inicio contrato
        #14#5 = #1#60;   || candidatos
        #14#6 = #1#72;   || seleccionados
        #14#7 = #1#73;   || contratados
        #14#8 = #3#1;    || nombre cliente
        #14#9 = #2#1;    || nombre puesto
        #14#10 = #1#71;  || requeridos
    |GRABA 020#14n;
        seleccio = 1;
|FINSI;
|FPRC;

|DEFBUCLE pedidos;
 #1#1;
 ;
     1;
 99999;
 ;
 NULL, pedidos1;
|FIN;

|PROCESO pendientes;
|CIERRAT #0;
    seleccio = 0;
    alfa1 = Usuario;     |QBT alfa1;
|NOME_DAT #14 alfa1;
|DELINDEX #14;
|ABRE #2;
|ABRE #3;
|ABRE #14;
|BUCLE pedidos;
|CIERRA #2;
|CIERRA #3;
|CIERRA #14;
|SI seleccio = 1;
    |PUSHV 0401 2380;
    |ATRI R;
    |MENSA "     < Pedidos con Contrataciones Pendientes >";
    |ATRI 0;
    |ENTLINEAL #1#14, 1, 4, 16, 1, pen_min, pen_max;
        #0#0 = #14#0;
    |POPV;
|SINO;
    |MENAV "    No existen pedidos con contrataciones pendientes ...";
|FINSI;
|DELINDEX #14;
|ABRET #0;
|FPRC;

|PROCESO lee_pedido; |TIPO 0;
    inkey = FSalida;
|SI inkey ! 1;                || si no pulsa ESC-ESC
    |SI inkey = 9;
        |HAZ pendientes;
        |SI seleccio = 1;
            |PINTA #0#0;
            |PTEC 802;
            |PTEC 802;
        |FINSI;
        |ERROR;
    |SINO;
        |DDEFECTO #1; |ABRE #1; #1#0 = #0#0; |LEE 000#1=; |CIERRA #1;
        |DDEFECTO #3; |ABRE #3; #3#0 = #1#2; |LEE 000#3=; |CIERRA #3;

        |ATRI I;
        |PINTA 0712, #1#1;   || fecha
        |PINTA 0812, #1#2;   || cliente
        |PINTA 0818, #3#1;   || nombre cliente
        |ATRI 0;

        |ATRI R;
        |PINTA 0575, #1#71;  || requeridos
        |PINTA 0675, #1#60;  || candidatos
        |PINTA 0775, #1#72;  || seleccionados
        |PINTA 0875, #1#73;  || contratados
        |ATRI 0;

        |SI #1#72 = 0;
            |MENAV "    AVISO: No se puede contratar SIN seleccionar ...";
            |ERROR;
        |FINSI;
        |SI #1#71 = #1#73;
            |MENAV "    AVISO: El personal requerido YA se ha contratado ...";
        |FINSI;
    |FINSI;
|SINO;
    |PTEC 807;
|FINSI;
|FPRC;
____________________________________/
|PROCESO min;
    #4#27 = "S";
    #4#0 = #0#0;
    #4#1 = 1;
|FPRC;

|PROCESO max;
    #4#27 = "S";
    #4#0 = #0#0;
    #4#1 = ultimo;
|FPRC;

|PROCESO el_ultimo;
    ultimo = 0;
|ABRE #4;
    #4#0 = #0#0;
    #4#1 = 99999;
|LEE 000#4];
|SI FSalida = 0;
    |LEE 000#4a;
|SINO;
    |LEE 000#4u;
|FINSI;
|SI FSalida = 0;|Y #4#0 = #0#0;
        ultimo = #4#1;
|FINSI;
|CIERRA #4;
|FPRC;

|PROCESO CargaPrevia;
     |SI #ettpedi5#21 = 0;
          |ABRE #6;
          #6#0 = #ettpedi5#16;   || codigo tarifa
          #6#1 = #ettpedi5#17;   || nivel
          |LEE 000#6=;
          |SI FSalida = 0;
               #ettpedi5#18 = #6#2;     || s.base
               #ettpedi5#19 = #6#3;     || costes
               #ettpedi5#20 = #6#4;     || margen
               #ettpedi5#22 = #6#6;     || pagas
               #ettpedi5#23 = #6#7;     || p.p.vacaciones
               #ettpedi5#46 = #6#9;     || indemnizacion
               || ret.cto.
               #ettpedi5#34 = #ettpedi5#18 + #ettpedi5#22 + #ettpedi5#23 + #ettpedi5#46;
               |HAZ coste_epig;
               #ettpedi5#41 = ct_epig;  || costes epigr.
               #ettpedi5#21 = #6#5 + #ettpedi5#41;        || total hora
               aAlfa = #ettpedi5#21; |QBF aAlfa;
               aPunto = aAlfa % -201;
               |SI aPunto = "."; |O aPunto = ",";
                    aAlfa = aAlfa + "0";
               |FINSI;
               aPunto = aAlfa % -301;
               |SI aPunto ! "."; |Y aPunto ! ",";
                    aAlfa = aAlfa + ".00";
               |FINSI;

               #ettpedi5#40 = aAlfa + " EUROS+IVA POR HORA"; |PINTA #ettpedi5#40; || texto CPD
               #ettpedi5#35 = "H";      || tipo retrib.
               |GRABA 020#ettpedi5.a;
               |LIBERA #ettpedi5;
          |FINSI;
          |CIERRA #6;
     |FINSI;
|FPRC;

|DEFBUCLE CargaPrevia;
     #ettpedi5#4;
     ;
     "S", #0#0, 1;
     "S", #0#0, ultimo;
     be;
     NULL, CargaPrevia;
|FIN;

|PROCESO tipo3; |TIPO 3;
     |ABRE #1;
     #1#0 = #0#0;
     |LEE 121#1=;
     |SI FSalida = 0;
          |HAZ el_ultimo;
          |SI ultimo ! 0;
               |BUCLE CargaPrevia;
               |ENTLINEAL #4#4, 3, 3, 17, 0, min, max;
          |FINSI;
     |FINSI;
     |LIBERA #1;
     |CIERRA #1;
|FPRC;
____________________________________/ CALCULOS DESDE LINEAS
|PROCESO conten; |TIPO 7;
    conten = #4Campo;
|FPRC;

|PROCESO coste_epig;
    ct_epig = 0;
    |ABRE #26;|DDEFECTO #26;  #26#0= #4#42; |LEE 000#26=; |CIERRA #26;|| epigr.
    ct_epig = #4#34 % (#26#1 + #26#2);
|FPRC;

|PROCESO carga_nivel; |TIPO 0;
     |SI #4Campo ! conten;|O #4#21 = 0;
          |ABRE #6;
          #6#0 = #4#16;   || codigo tarifa
          #6#1 = #4#17;   || nivel
          |LEE 000#6=;
          |SI FSalida = 0;
               #4#18 = #6#2;                    |PINTA #4#18;  || s.base
               #4#19 = #6#3;                    |PINTA #4#19;  || costes
               #4#20 = #6#4;                    |PINTA #4#20;  || margen
               #4#22 = #6#6;                    |PINTA #4#22;  || pagas
               #4#23 = #6#7;                    |PINTA #4#23;  || p.p.vacaciones
               #4#46 = #6#9;                    |PINTA #4#46;  || indemnizacion
               #4#34 = #4#18 + #4#22 + #4#23 + #4#46; |PINTA #4#34;  || ret.cto.
               |HAZ coste_epig;
               #4#41 = ct_epig;                 |PINTA #4#41;  || costes epigr.
               #4#21 = #6#5 + #4#41;            |PINTA #4#21;  || total hora
               aAlfa = #4#21; |QBF aAlfa;
               aPunto = aAlfa % -201;
               |SI aPunto = "."; |O aPunto = ",";
                    aAlfa = aAlfa + "0";
               |FINSI;
               aPunto = aAlfa % -301;
               |SI aPunto ! "."; |Y aPunto ! ",";
                    aAlfa = aAlfa + ".00";
               |FINSI;

               #4#40 = aAlfa + " EUROS+IVA POR HORA"; |PINTA #4#40; || texto CPD
               #4#35 = "H";                     |PINTA #4#35;  || tipo retrib.
          |SINO;
               |MENAV "    Nivel NO definido ...";
               |ERROR;
          |FINSI;
          |CIERRA #6;
     |FINSI;
|FPRC;

|PROCESO def_epigrafe; |TIPO 7;
|SI #4#24 ! 0;|Y #4#42 = 0;
    |ABRE #21;
        #21#0 = #8#4;    || empresa
        #21#1 = #4#24;   || trabajador
    |LEE 000#21=;
    |SI FSalida = 0;
            #4#42 = #21#32;
        |GRABA 020#4a;
        |SI FSalida = 0;
            |HAZ carga_nivel;
            |PINTA #4#42;
        |FINSI;
    |FINSI;
    |CIERRA #21;
|FINSI;
|FPRC;

|PROCESO cambia_epigrafe; |TIPO 0;
|SI #4#24 ! 0;
    |ABRE #21;
        #21#0 = #8#4;    || empresa
        #21#1 = #4#24;   || trabajador
    |LEE 101#21=;
    |SI FSalida = 0;
        |SI #21#32 ! #4#42;
            |AVISO;
            |CONFI "    SEpigrafe DISTINTO al de Nominas, Actualizar S/N: ";
            |SI FSalida = 0;
                    #21#32 = #4#42;
                |GRABA 020#21a;
            |FINSI;
        |FINSI;
    |FINSI;
    |LIBERA #21;
    |CIERRA #21;
|FINSI;
|FPRC;

|PROCESO tipo2; |TIPO 2;
|SI FEntrada = 302;
        sw_modifi = 1;
|FINSI;
|SI FEntrada = 303;
    |SI sw_modifi = 0;
        |MENAV "    No se pueden borrar candidatos desde este punto ...";
        |ERROR;
    |FINSI;
        sw_modifi = 0;
|FINSI;
|FPRC;

|PROCESO total; |TIPO 0;
     #4#21 = #4#18 + #4#19 + #4#20 + #4#22 + #4#23 + #4#41 + #4#46;
     |SI #4#21 ! 0;
          #4#40 = #4#21 + " EUROS+IVA POR HORA";
     |SINO;
          #4#40 = "";
     |FINSI;
     |PINTA #4#21;
     |PINTA #4#40; || texto CPD
|FPRC;

|PROCESO mayus; |TIPO 0;
    #4Campo = #4Campo > #4Campo;
|PINTA #4Campo;
|FPRC;

|PROCESO no_pacto; |TIPO 7;
    alfa1 = #4#37;
|C_M #4#38, 1, alfa1;
|C_M #4#39, 1, alfa1;
|SI Campo = 39;
   |HAZ SegPantalla;
|FINSI;
|FPRC;

|PROCESO no_cero; |TIPO 0;
|SI #4#38 = 0;
        #4#37 = "N";
    |PINTA #4#37;
|FINSI;
|FPRC;

|PROCESO no_alta; |TIPO 7;
    modo = (FEntrada / 100) ! 0;
|SI modo = 1;
    |MENAV "    Alta manual NO permitida en este punto ...";
    |PTEC 806;
|FINSI;
|FPRC;

|PROCESO tipo11; |TIPO 11;
    inkey = FSalida;
|SI inkey = 9;
    |PUSHV 0501 2380;
    |PINPA #0#7;
    |ABRE #7;  #7#0 = #4#2;   |LEE 000#7=;   |CIERRA #7;
    |PINDA #0#7;
    |PAUSA;
    |POPV;
    |ERROR;
|FINSI;
|SI inkey = 10;
    |HAZ lee_empresa;
    |HAZ contratacion;
    |ERROR;
|FINSI;
|SI inkey = 11;
    |SI #4#24 = 0;
        |MENAV "    Trabajador sin contratar ...";
    |SINO;
        |HAZ consulta_aginom;
    |FINSI;
    |ERROR;
|FINSI;
|SI inkey = 12;
    |SI #4#24 = 0;
        |MENAV "    No se puede descontratar a un trabajador que aun no esta contratado ...";
    |SINO;
        |HAZ descontratacion;
    |FINSI;
    |ERROR;
|FINSI;
|FPRC;
____________________________________/ PROCESOS DE CONTRATACION
|PROCESO alta_ocupa;
|ABRE #10;
    #10#0 = #4#2;
|LEE 101#10=;
|SI FSalida ! 0;
    |GRABA 020#10b;
|FINSI;

|ABRE #11;
    nume1 = 1;
    #11#0 = #10#0;
    #11#1 = 999;
|LEE 000#11];
|SI FSalida = 0;
    |LEE 000#11a;
|SINO;
    |LEE 000#11u;
|FINSI;
|SI FSalida = 0;|Y #11#0 = #10#0;
        nume1 = #11#1 + 1;
|FINSI;

|DDEFECTO #11;
    #11#0 = #10#0;       || cod.personal
    #11#1 = nume1;       || linea
    #11#2 = #1#5;        || desde contrato
    #11#3 = #1#6;        || hasta contrato
    #11#8 = #1#2;        || cliente
    #11#9 = traba;       || codigo AGINOM
|GRABA 020#11n;
|CIERRA #11;

|LIBERA #10;
|CIERRA #10;
|FPRC;

|PROCESO nro_matricula;
|ABRE #8;
|LEE 101#8p;
|SI FSalida = 0;
        #8#11 = #8#11 + 1;
        matricula = #8#11;
    |GRABA 020#8a;
|FINSI;
|LIBERA #8;
|CIERRA #8;
|FPRC;

|PROCESO graba_aginom;
     |DDEFECTO #2;
     |DDEFECTO #7;
     |DDEFECTO #23;
     |DDEFECTO #24;
     |ABRE #2;  #2#0 = #1#59;                 |LEE 020#2=;  |CIERRA #2;  ||puesto
     |ABRE #7;  #7#0 = #4#2;                  |LEE 020#7=;  |CIERRA #7;  ||person.
     |ABRE #23; #23#0 = #7#9;                 |LEE 000#23=; |CIERRA #23; ||provin.
     |ABRE #24; #24#0 = #12#0;                |LEE 000#24=; |CIERRA #24; ||conven.

     |ABRE #labcentr;
     #labcentr#0 = #ettcontr#4;
     #labcentr#1 = #12#14;
     |LEE 000#labcentr.=;
     |CIERRA #labcentr;

     |DDEFECTO #21;
     #21#0  = #8#4;       || empresa
     #21#1  = traba;      || trabajador
     |LEE 101#21=;
     FEstado = FSalida;

     |SI FEstado ! 0;
          |HAZ nro_matricula;
          #21#2  = #7#2;        || nombre
          #21#3  = #7#5;        || domicilio
          #21#4  = #7#6;        || numero
          #21#5  = #7#7;        || piso
          #21#6  = #7#8;        || localidad
          #21#7  = #7#3;        || DNI
          #21#8  = #7#9;        || cp. 1
          #21#9  = #7#10;       || cp. 2
          #21#10 = #23#1;       || provincia
          #21#11 = #7#14;       || fecha nacimiento
          #21#12 = #7#22;       || nivel de estudios
          #21#24 = #7#15;       || oficina INEM
          #21#26 = #21#7;       || nro. demanda
          #21#27 = #2#1;        || profesion inscrita
          #21#28 = #7#16;       || fecha INEM
          || #21#31 = matricula;   || nro. matricula

          #21#34 = #1#5;        || fecha antig.
          #21#36 = #21#34;      || fecha alta
          #21#38 = #21#34;      || fecha recibo
          #21#253 = #21#34;      || fecha ingreso categoria

          #21#252 = #7#17;       || codigo nivel estudios
          #21#75 = #7#4;        || nro. afiliacion
          #21#76 = #7#13;       || localidad nacimiento
          #21#156= #20#34;      || calendario laboral
          #21#228= #20#37;      || clave perceptora 2
          #21#229= #20#38;      || clave perceptora 3
          #21#244= #7#11;       || telefono
          #21#238= #2#1;        || puesto de trabajo
     |FINSI;

     #21#31 = #7#0;        || nro referencia (matricula)
     #21#73 = #7#48;       || codigo nivel estudios
     #21#140 = #7#22;       || descripcion nivel estudios
     #21#185 = #7#49;       || codigo profesion
     #21#19 = #7#50;       || descripcion profesion
     #21#31 = #7#0;        || nro referencia (matricula)

     #21#32 = #4#42;      || epigrafe accidentes
     #21#87 = #12#0;      || codigo convenio
     #21#25 = #24#1;      || descripcion convenio
     #21#17 = #12#1;      || codigo categoria
     #21#18 = #12#13;     || descripcion categoria

     #21#247 = #12#16;       || Regimen

     #21#30 = #12#2;      || (%) IRPF 1
     #21#174 = #12#2;      || (%) IRPF 1
     #21#175 = #12#2;      || (%) IRPF 1
     #21#42 = #12#3;      || clave dias salario
     |SI #21#42 ! "M";|Y #21#42 ! "D";
     |Y #21#42 ! "X";|Y #21#42 ! "T";
     |Y #21#42 ! "I";|Y #21#42 ! "P";
          #21#64 = #21#42;
     |SINO;
          |SI #21#42 = "D";|O #21#42 = "T";
               #21#64 = "D";
          |SINO;
               #21#64 = "M";
          |FINSI;
     |FINSI;
     #21#45 = #12#4;      || tipo contrato
     #21#37 = #12#5;      || fecha baja
     #21#46 = #12#6;      || dias TC2
     #21#47 = #12#7;      || horas TC2
     #21#97 = #12#9;      || tipo ajuste
     #21#98 = #12#10;     || importe ajuste
     #21#33 = #12#11;     || grupo tarifa
     #21#234 = #12#12;     || horas tiempo parcial
     #21#156 = #labcentr#15;     || calendario
     #21#77 = #12#14;    || centro de trabajo

     |ABRE #nomtrato;
     #nomtrato#0 = #12#16;
     #nomtrato#1 = #12#4;
     |LEE 000#nomtrato.=;
     |SI FSalida = 0;
          #21#248 = #nomtrato#3;
     |FINSI;
     |CIERRA #nomtrato;


     |SI FEstado = 0;    |GRABA 020#21a;     |FINSI;
     |SI FEstado ! 0;    |GRABA 020#21n;     |FINSI;
     |SI FSalida = 0;|Y FEstado ! 0;
          |HAZ alta_ocupa;
          #4#24 = traba;
          |GRABA 020#4a;
          |SI FSalida = 0;
               #1#73 = #1#73 + 1;     || contratados pedido
               |GRABA 020#1a;
         |FINSI;
     |FINSI;
     |LIBERA #21;
|FPRC;

|PROCESO ulti_traba;
    traba = 1;
    #21#0 = #8#4;
    #21#1 = #8#35;

|LEE 000#21];
|SI FSalida = 0;
    |LEE 000#21a;
|SINO;
    |LEE 000#21u;
|FINSI;
|SI FSalida = 0;|Y #21#0 = #8#4;
        traba = #21#1 + 1;
|FINSI;
|FPRC;

|PROCESO alta_aginom;
|ABRET #21;
|SI #4#24 = 0;
    |HAZ ulti_traba;
|SINO;
        traba = #4#24;
|FINSI;
|HAZ graba_aginom;
|CIERRAT #21;
|FPRC;

|PROCESO TraeDatosLab;
     #12#0 = #21#87;   || codigo convenio
     #12#1 = #21#17;   || codigo categoria
     #12#2 = #21#30;   || (%)IRPF
     #12#3 = #21#42;   || clave dias salario
     #12#4 = #21#45;   || tipo contrato
     #12#5 = #21#37;   || fecha baja
     #12#6 = #21#46;   || dias TC2
     #12#7 = #21#47;   || horas TC2
     #12#8 = #21#19;   || departamento (profesion)
     #12#9 = #21#97;   || tipo ajuste
     #12#10 = #21#98;   || importe ajuste
     #12#11 = #21#33;   || grupo tarifa
     #12#12 = #21#234;  || horas tiempo parcial
     #12#13 = #21#18;   || codigo categoria

     #12#14 = #21#77;
     |ABRE #labcentr;
     #labcentr#0 = #ettcontr#4;
     #labcentr#1 = #12#14;
     |LEE 000#labcentr.=;
     |SI FSalida = 0;
          #12#15 = #labcentr#2;
     |FINSI;
     |CIERRA #labcentr;

     #12#16 = #21#247;
     |ABRE #nomregim;
     #nomregim#0 = #12#16;
     |LEE 000#nomregim.=;
     |SI FSalida = 0;
          #12#17 = #nomregim#1;
     |FINSI;
     |CIERRA #nomregim;

     |ABRE #tfggrcot;
     aAlfa = #12#11;
     |QBF aAlfa;
     aAlfa = ("00" + aAlfa) % -102;
     #tfggrcot#0 = aAlfa;
     |LEE 000#tfggrcot.=;
     |SI FSalida = 0;
          #12#18 = #tfggrcot#1;
     |FINSI;
     |CIERRA #tfggrcot;
|FPRC;

|PROCESO BuscaDatos;
     nPedido = #ettpedi2#0;
     nRegActual = #ettpedi2#1 - 1;
     |PARA nRegistro = nRegActual; |SI nRegistro ] 1;
     |HACIENDO nRegistro = nRegistro - 1;
          #ettpedi2#0 = nPedido;
          #ettpedi2#1 = nRegistro;
          |LEE 000#ettpedi2.=;
          |SI FSalida = 0;
               |SI #ettpedi2#24 ! 0;
                    || he encontrado un trabajador contratado en este pedido
                    || leo sus datos
                    nTrabCopia = #ettpedi2#24;
                    |ABRE #labtraba;
                    #labtraba#0 = #ettcontr#4;
                    #labtraba#1 = nTrabCopia;
                    |LEE 000#labtraba.=;
                    |SI FSalida = 0;
                         |HAZ TraeDatosLab;
                         nRegistro = 1; || para que se salga del bucle
                    |FINSI;
                    |CIERRA #labtraba;
               |FINSI;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO contratacion;
     |PUSHV 0401 2380;
     |PINPA #0#12;
     |DDEFECTO #12;
     |SI #4#24 ! 0;                     || si ya existe modifica trabajador
          |ABRE #21;
          #21#0 = #8#4;    || empresa
          #21#1 = #4#24;   || trabajador
          |LEE 000#21=;
          |SI FSalida = 0;
               |HAZ TraeDatosLab;
          |FINSI;
          |CIERRA #21;
     |SINO;
          || primero busco si hay algun trabajador con los datos ya grabados
          |SALVA_FICHA #ettpedi2;
          nTrabCopia = 0;
          |HAZ BuscaDatos;
          |REPON_FICHA #ettpedi2;
          |SI nTrabCopia ! 0;
               |HAZ TraeDatosLab;
          |SINO;
               #12#0 = #20#1;        || codigo convenio
               #12#1 = #8#7;         || codigo categoria
               #12#2 = #8#8;         || (%) IRPF
               #12#3 = #8#9;         || clave dias salario
               || #12#4 = #8#10;        || tipo contrato
               |SI #1#6 = "01.01.1900";
                    |SI #4#33 ! "01.01.1900";
                         #12#5 = #4#33;     || fecha baja (fecha ultimo parte)
                    |FINSI;
               |SINO;
                    #12#5 = #1#6;          || fecha baja (fecha final contrato)
               |FINSI;
               #12#7 = #4#26;        || horas TC2 (diarias)
               #12#11 = #4#17;       || grupo de tarifa (nivel)
               #12#12 = #1#70;       || horas semanales
               |SI #12#12 < 40;     || < de 40h. sem. (t.parcial 04)
                    #12#3 = "X";
               |FINSI;
          |FINSI;
     |FINSI;
     |PINDA #0#12;
     |SI #4#24 ! 0;
          |ATRI I;
          |PINTA 1064, ">MODIFICANDO<";
          |ATRI 0;
     |FINSI;
     |ENDATOS #1#12;
     |SI FSalida = 0;
          |MENSA "    Grabando en LABORAL ...";
          |HAZ alta_aginom;
          |ATRI R;   |BLIN 4;  |ATRI 0;
     |FINSI;
     |POPV;
     |ATRI R;
     |PINTA 0875, #1#73; || pinta numero contratados
     |ATRI 0;
     |PINTA #4#24;       || pinta codigo AGINOM
|FPRC;
____________________________________/ PROCESOS DE CONSULTA AGINOM
|PROCESO consulta_aginom;
|PUSHV 0501 2380;
|ABRE #21;
    #21#0  = #8#4;       || empresa
    #21#1  = #4#24;      || trabajador
|LEE 000#21=;
|SI FSalida = 0;
        pantalla = 1;
    |ET pantalla1;
        |PINPA #0#21;
        |PINDA #0#21;
        |VETE apausa;
    |ET pantalla2;
        |PINPA #1#21;
        |PINDA #1#21;
        |VETE apausa;
    |ET pantalla3;
        |PINPA #2#21;
        |PINDA #2#21;
        |VETE apausa;
    |ET pantalla4;
        |PINPA #3#21;
        |PINDA #3#21;
        |VETE apausa;
    |ET pantalla5;
        |PINPA #4#21;
        |PINDA #4#21;
        ||VETE apausa;
    ||ET pantalla6;
        ||PINPA #5#21;
        ||PINDA #5#21;
    |ET apausa;
        |PAUSA;
        |SI FSalida ! 0;
            |SI FSalida ! 7;|Y FSalida ! 1;                       || CTRL-C/ESC
                |SI FSalida = 4;  pantalla = pantalla - 1;  |FINSI;  || PgUp
                |SI FSalida = 5;  pantalla = pantalla + 1;  |FINSI;  || PgDown
                |SI pantalla = 0; pantalla = 6; |FINSI;
                |SI pantalla = 7; pantalla = 1; |FINSI;
                |SI pantalla = 1; |VETE pantalla1;  |FINSI;
                |SI pantalla = 2; |VETE pantalla2;  |FINSI;
                |SI pantalla = 3; |VETE pantalla3;  |FINSI;
                |SI pantalla = 4; |VETE pantalla4;  |FINSI;
                |SI pantalla = 5; |VETE pantalla5;  |FINSI;
                ||SI pantalla = 6; |VETE pantalla6;  |FINSI;
            |FINSI;
        |FINSI;
|FINSI;
|CIERRA #21;
|POPV;
|FPRC;
____________________________________/ PROCESOS DE DESCONTRATACION
|PROCESO baja_ocupa;
|ABRE #11;
|SELECT #2#11;
    #11#0 = #4#2;   || codigo personal
    #11#9 = #4#24;  || codigo AGINOM
|LEE 101#11=;
|SI FSalida = 0;
    |BORRA 020#11a;
|FINSI;
|LIBERA #11;
|CIERRA #11;
|FPRC;

|PROCESO descontratacion;
|AVISO;
|CONFI "    NEsta seguro de descontratar el trabajador (S/N): ";
|SI FSalida = 0;
    |ABRE #21;
        #21#0 = #8#4;   || empresa
        #21#1 = #4#24;  || trabajador
    |LEE 101#21=;
    |SI FSalida = 0;
        |AVISO;
        |CONFI "    NAdemas de descontratar, desea borrar el trabajador de LABORAL (S/N): ";
        |SI FSalida = 0;
            |BORRA 020#21a;
        |FINSI;
        |HAZ baja_ocupa;
            #4#24 = 0;        || codigo AGINOM
        |GRABA 020#4a;
        |SI FSalida = 0;
            |PINTA #4#24;
                #1#73 = #1#73 - 1;     || contratados pedido
            |GRABA 020#1a;
            |SI FSalida = 0;
                |ATRI R;
                |PINTA 0875, #1#73;
                |ATRI 0;
            |FINSI;
        |FINSI;
    |SINO;
        |MENAV "    Trabajador no encontrado en LABORAL ";
    |FINSI;
    |LIBERA #21;
    |CIERRA #21;
|FINSI;
|FPRC;

|PROCESO SegPantalla;
   |DELINDEX #ettped10; |ABRE #ettped10; |DDEFECTO #ettped10;
   |PARA nCont = 47; |SI nCont [ 56; |HACIENDO nCont = nCont + 1;
      Calc = nCont - 47; #ettped10#Calc# = #ettpedi5#nCont#;
   |SIGUE;
   |GRABA 020#ettped10.n;
   |CIERRA #ettped10;
   Comodin = "ettped10.tab;" + #labtraba#45;
   |SUB_EJECUTA_NP Comodin;
   |ABRE #ettped10; |LEE 000#ettped10.p;
   |PARA nCont = 47; |SI nCont [ 56; |HACIENDO nCont = nCont + 1;
      Calc = nCont - 47; #ettpedi5#nCont# = #ettped10#Calc#;
   |SIGUE;
   |CIERRA #ettped10;
|FPRC;
