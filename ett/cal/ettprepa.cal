|FICHEROS;
    ettprepa #0;
    ettparte #1;         || partes de horas cabs.
    ettpedi2 #2;         || pedidos lins.
    ettperso #3;         || personal
    ettparli #4;         || partes de horas lins.

    nomvarca #10;        || variaciones cabs. AGINOM
    nomvarli #11;        || variaciones lins. AGINOM
    ettcontr #13;        || control AGIETT
    etttarca #18;        || tarifas cabs. AGIETT
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    p_mes = "";
    sw_mes = 0;
    horas = 0;
    codcto = 0;
    unidad = 0;
    unidad2 = 0;
    import = 0;
    import2 = 0;
    ant_tra = -1;
    nominas = 0;
    prenom = 0;
|FIN;
____________________________________/
|PROCESO tipo8; |TIPO 8;
|ABRE #13;
|LEE 000#13p;
|SI FSalida = 0;
        alfa1 = #13#3;    |QBF alfa1;
        alfa1 = alfa1 + "/fich/";
    |PATH_DAT #10 alfa1;
    |PATH_DAT #11 alfa1;
|SINO;
    |MENAV "    Control AGIETT sin definir ...";
    |PTEC 807;
|FINSI;
|CIERRA #13;
|FPRC;

|PROCESO defecto; |TIPO 7;
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % -104;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO mirames; |TIPO 7;
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % 402;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO pintames; |TIPO 0;
|SI #0Campo =  1;  p_mes = "ENERO     ";  |FINSI;
|SI #0Campo =  2;  p_mes = "FEBRERO   ";  |FINSI;
|SI #0Campo =  3;  p_mes = "MARZO     ";  |FINSI;
|SI #0Campo =  4;  p_mes = "ABRIL     ";  |FINSI;
|SI #0Campo =  5;  p_mes = "MAYO      ";  |FINSI;
|SI #0Campo =  6;  p_mes = "JUNIO     ";  |FINSI;
|SI #0Campo =  7;  p_mes = "JULIO     ";  |FINSI;
|SI #0Campo =  8;  p_mes = "AGOSTO    ";  |FINSI;
|SI #0Campo =  9;  p_mes = "SEPTIEMBRE";  |FINSI;
|SI #0Campo = 10;  p_mes = "OCTUBRE   ";  |FINSI;
|SI #0Campo = 11;  p_mes = "NOVIEMBRE ";  |FINSI;
|SI #0Campo = 12;  p_mes = "DICIEMBRE ";  |FINSI;
|PINTA 1629, p_mes;
|FPRC;
____________________________________/
|PROCESO graba_funciones;
|SI unidad ! 0;
    |DDEFECTO #10;
        #10#0 = #13#4;  || empresa
        #10#1 = #1#4;   || trabajador
        #10#2 = #0#7;   || mes
        #10#3 = 0;      || tipo
    |GRABA 000#10n;
    |SI FSalida = 0;
            nominas = nominas + 1;
    |FINSI;

    |DDEFECTO #11;
        #11#0 = #10#0;        || empresa
        #11#1 = #10#1;        || trabajador
        #11#2 = #10#2;        || mes
        #11#3 = #10#3;        || tipo
        #11#4 = codcto;       || concepto
    |LEE 000#11=;
    |SI FSalida ! 0;
            #11#5 = unidad;       || unidades
        |SI #11#4 = 918;     #11#6 = "Horas en TC2";  |FINSI;

            alfa1 = #11#5; |QBF alfa1; alfa1 = ("          " + alfa1) % -110;
            #11#5 = alfa1;
            alfa1 = #11#6; |QBF alfa1; alfa1 = ("            " + alfa1) % -112;
            #11#6 = alfa1;
        |GRABA 020#11n;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO graba_varia_lins;
    nume1 = unidad * import;
|SI nume1 ! 0;
    |DDEFECTO #10;
        #10#0 = #13#4;  || empresa
        #10#1 = #1#4;   || trabajador
        #10#2 = #0#7;   || mes
        #10#3 = 0;      || tipo
    |GRABA 000#10n;
    |SI FSalida = 0;
            nominas = nominas + 1;
    |FINSI;

    |DDEFECTO #11;
        #11#0 = #10#0;        || empresa
        #11#1 = #10#1;        || trabajador
        #11#2 = #10#2;        || mes
        #11#3 = #10#3;        || tipo
        #11#4 = codcto;       || concepto
    |LEE 101#11=;
        FEstado = FSalida;

    |SI FEstado = 0;
            unidad2 = #11#5;
            import2 = #11#6;
        |SI import2 = import;
                unidad2 = unidad2 + unidad;
        |SINO;
                import2 = (unidad2 * import2) + (unidad * import);
                unidad2 = 1;
        |FINSI;
    |SINO;
            unidad2 = unidad;
            import2 = import;
    |FINSI;
        #11#5 = unidad2;      || unidades
        #11#6 = import2;      || importe
    |SI #11#4 = 918;     #11#6 = "Horas en TC2";  |FINSI;

        alfa1 = #11#5; |QBF alfa1; alfa1 = ("          " + alfa1) % -110;
        #11#5 = alfa1;
        alfa1 = #11#6; |QBF alfa1; alfa1 = ("            " + alfa1) % -112;
        #11#6 = alfa1;

    |SI FEstado = 0;     |GRABA 020#11a;     |FINSI;
    |SI FEstado ! 0;     |GRABA 020#11n;     |FINSI;
    |LIBERA #11;
|FINSI;
|FPRC;

|PROCESO horas_tc2;
    codcto = 918;        || codigo concepto (fijo)
    unidad = #2#26;      || unidades
|HAZ graba_funciones;
|FPRC;

|PROCESO otros;
|SI #4#2 = "L";     para4 = 8;     |FINSI;
|SI #4#2 = "M";     para4 = 9;     |FINSI;
|SI #4#2 = "X";     para4 = 10;    |FINSI;
|SI #4#2 = "J";     para4 = 11;    |FINSI;
|SI #4#2 = "V";     para4 = 12;    |FINSI;
|SI #4#2 = "S";     para4 = 13;    |FINSI;
|SI #4#2 = "D";     para4 = 14;    |FINSI;
|SI #1para4 ! "01.01.0000";
        alfa1 = #1para4;
        alfa2 = alfa1 % 402;   nume1 = alfa2;
        alfa2 = alfa1 % -104;  nume2 = alfa2;
    |SI nume1 = #0#7;|Y nume2 = #0#6;
            codcto = #4#3;       || codigo concepto
            unidad = 1;          || unidades (fijo a 1)
            import = #4#4;       || importe
        |HAZ graba_varia_lins;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO noche;
    codcto = #13#13;     || codigo concepto
    unidad = #1#57 + #1#58 + #1#59 + #1#60 + #1#61 + #1#62 + #1#63; || horas
    prenom = #2#18 + #2#22 + #2#23;
    import = (prenom > #18#6) ! 0;           || precio hora noche
    ||import = (#1#53 > #18#6) ! 0;
|HAZ graba_varia_lins;
|FPRC;

|PROCESO extra;
    codcto = #13#19;     || codigo concepto
    unidad = #1#29 + #1#30 + #1#31 + #1#32 + #1#33 + #1#34 + #1#35; || horas
    prenom = #2#18 + #2#22 + #2#23;
    import = (prenom > #18#5) ! 0;           || precio hora extra
    ||import = (#1#53 > #18#5) ! 0;
|HAZ graba_varia_lins;
|FPRC;

|PROCESO indem;
    codcto = #13#14;     || codigo concepto
    unidad = horas;      || numero de horas
    import = #2#46;      || precio hora indemnizacion
|HAZ graba_varia_lins;
|FPRC;

|PROCESO vacac;
    codcto = #13#18;     || codigo concepto
    unidad = horas;      || numero de horas
    import = #2#23;      || precio hora p.p. vacaciones
|HAZ graba_varia_lins;
|FPRC;

|PROCESO pagas;
    codcto = #13#17;     || codigo concepto
    unidad = horas;      || numero de horas
    import = #2#22;      || precio hora p.p. pagas
|HAZ graba_varia_lins;
|FPRC;

|PROCESO sbase;
    codcto = #13#16;     || codigo concepto
    unidad = horas;      || numero de horas
    import = #2#18;      || precio hora s.base
|HAZ graba_varia_lins;
|FPRC;

|PROCESO horas;
    horas = #1#22 + #1#23 + #1#24 + #1#25 + #1#26 + #1#27 + #1#28;
    nume1 = #1#36 + #1#37 + #1#38 + #1#39 + #1#40 + #1#41 + #1#42;
    horas = horas + nume1;
|FPRC;

|PROCESO graba_varia;
    #2#0 = #1#3;     || codigo pedido
    #2#1 = #1#48;    || linea pedido
|LEE 101#2=;
|SI FSalida = 0;
    |DDEFECTO #18;  #18#0 = #2#16; |LEE 000#18=; || lee tarifa cabs.
    |HAZ horas;          || calcula numero de horas
    |HAZ sbase;          || salario base
    |HAZ pagas;          || p.p. pagas
    |HAZ vacac;          || p.p. vacaciones
    |HAZ indem;          || indemnizacion
    |HAZ extra;          || horas extras
    |HAZ noche;          || horas nocturnidad
    |BUCLELIN 2otros#1;  || otros importes
    |HAZ horas_tc2;      || cto. 918 (horas TC2)
|FINSI;
|LIBERA #2;
|FPRC;

|PROCESO borra_varia;
    #10#0 = #13#4;  || empresa
    #10#1 = #1#4;   || trabajador
    #10#2 = #0#7;   || mes
    #10#3 = 0;      || tipo
|LEE 000#10=;
|SI FSalida = 0;
    |BUCLELIN 1NULL#10;
    |BORRA 020#10a;
|FINSI;
|FPRC;

|PROCESO busca_mes;
    sw_mes = 0;
|PARA para1 = 8; |SI para1 [ 14; |HACIENDO para1 = para1 + 1;
    |SI #1para1 ! "01.01.0000";
            alfa1 = #1para1;
            alfa2 = alfa1 % 402;   nume1 = alfa2;
            alfa2 = alfa1 % -104;  nume2 = alfa2;
        |SI nume1 ! #0#7;|O nume2 ! #0#6;
                sw_mes = 1;
        |FINSI;
    |FINSI;
|SIGUE;
|FPRC;

|PROCESO lee_partes1;
    #3#0 = #1#1;
|LEE 000#3=;        || lee fichero personal
|SI FSalida = 0;
    |HAZ busca_mes;
    |SI sw_mes = 0;
            seleccio = 1;
        |ATRI I;
        |PINTA 2125, #1#3;         || pedido
        |PINTA 2131, #1#1;         || trabajador
        |PINTA 2137, #3#2;         || nombre trabajador
        |ATRI 0;
        |SI ant_tra ! #1#4;
                ant_tra = #1#4;
            |HAZ borra_varia;
        |FINSI;
        |HAZ graba_varia;
    |FINSI;
|FINSI;
|FPRC;

|DEFBUCLE lee_partes;
 #1#2;
 2;
 #0#0, #0#4,            1, #0#2;
 #0#1, #0#5, 999999999999, #0#3;
 ;
 NULL, lee_partes1;
|FIN;

|PROCESO tipo3; |TIPO 3;
|ABRET #2;
|ABRET #3;
|ABRET #10;
|ABRET #11;
|ABRET #18;
|BUCLE lee_partes;
|SI seleccio = 0;
    |MENAV "    Seleccion vacia ...";
|SINO;
        alfa1 = "    Preparadas [" + nominas + "] nominas ...";
    |MENAV alfa1;
|FINSI;
|CIERRAT #2;
|CIERRAT #3;
|CIERRAT #10;
|CIERRAT #11;
|CIERRAT #18;
|FPRC;
