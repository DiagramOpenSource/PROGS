|FICHEROS;
    ettborra #0;
    ettpedi1 #1;         || pedidos cabs.
    ettpedi2 #2;         || pedidos lins.
    ettoculi #3;         || ocupacion lins.
    ettparte #4;         || partes cabs.
    ettparli #5;         || partes lins.
    ettcontr #6;         || control AGIETT
    labtraba #7;         || trabajadores AGINOM
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    &em_borr = 0;
    &tr_borr = 0;
    borranom1 = ":laboral/nomborr1";
    borranom2 = ":laboral/nomborr2";
|FIN;
____________________________________/
|PROCESO mayus; |TIPO 0;
    #0Campo = #0Campo > #0Campo;
|PINTA #0Campo;
|FPRC;
____________________________________/
|PROCESO nominas;
|SI #2#24 ! 0;
        em_borr = #6#4;       || empresa (control)
        tr_borr = #2#24;      || trabajador (AGINOM)
        #7#0 = em_borr;
        #7#1 = tr_borr;
    |LEE 101#7=;
    |SI FSalida = 0;
        |SUB_EJECUTA borranom1;
        |SUB_EJECUTA borranom2;
        |BORRA 020#7a;
        |SI FSalida = 0;
                #0#6 = #0#6 + 1; |PINTA #0#6;
        |FINSI;
    |FINSI;
    |LIBERA #7;
|FINSI;
|FPRC;

|PROCESO partes1;
|BUCLELIN 1NULL#4;
|BORRA 020#4a;
|SI FSalida = 0;
        #0#4 = #0#4 + 1; |PINTA #0#4;
|FINSI;
|LIBERA #4;
|FPRC;

|DEFBUCLE partes;
 #4#2;
 ;
 #0#1,     0,            0;
 #0#2, 99999, 999999999999;
 be;
 NULL, partes1;
|FIN;

|PROCESO ocupacion;
    #3#0 = #2#2;    || cod.personal
    #3#9 = #2#24;   || cod.AGINOM
|LEE 101#3=;
|SI FSalida = 0;
    |BORRA 020#3a;
    |SI FSalida = 0;
            #0#5 = #0#5 + 1; |PINTA #0#5;
    |FINSI;
|FINSI;
|LIBERA #3;
|FPRC;

|PROCESO pedidos2;
|HAZ ocupacion;
|BUCLE partes;
|HAZ nominas;

|BORRA 020#2a;
|SI FSalida = 0;
        #0#7 = #0#7 + 1; |PINTA #0#7;
|FINSI;
|LIBERA #2;
|FPRC;

|DEFBUCLE pedido_lins;
 #2#1;
 ;
 #1#0,     1;
 #1#0, 99999;
 be;
 NULL, pedidos2;
|FIN;

|PROCESO pedidos1;
|BUCLE pedido_lins;
|BORRA 020#1a;
|SI FSalida = 0;
        #0#3 = #0#3 + 1; |PINTA #0#3;
|FINSI;
|LIBERA #1;
|FPRC;

|DEFBUCLE pedidos;
 #1#1;
 ;
 #0#1;
 #0#2;
 be;
 NULL, pedidos1;
|FIN;

|PROCESO tipo3; |TIPO 3;
|SI #0#0 = "SI";
    |ABRE #6;
    |LEE 000#6p;
    |SI FSalida = 0;
            alfa1 = #6#3;    |QBF alfa1;    alfa1 = alfa1 + "/fich/";
        |PATH_DAT #7 alfa1;
        |ABRE #3;
        |ABRE #7;
        |SELECT #2#3;
        |BUCLE pedidos;
        |CIERRA #3;
        |CIERRA #7;
            nume1 = #0#3 + #0#4 + #0#5 + #0#6 + #0#7;
        |SI nume1 ! 0;
            |AVISO;
            |PAUSA;
        |SINO;
            |MENAV "    Seleccion vacia ...";
        |FINSI;
    |SINO;
        |MENAV "    Control AGIETT sin definir ...";
    |FINSI;
    |CIERRA #6;
|FINSI;
|FPRC;
