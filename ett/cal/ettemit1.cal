|FICHEROS;
     labempre #0;
     labcentr;
     nomconca;
     ettcontr #1;
     ettclien #2;
     ettprovi #3;
     labtraba #4;
     ettperso #5;
     ettpuest #6;
     ettpedi1 #7;
     ettpedi2 #8;

     ettpuest #17;   || puestos AGIETT
     etttarca #18;   || tarifas cabs. AGIETT
     asfpagos #19;   || formas de pago FACGES
     asbancli #20;   || bancos clientes FACGES

     ettemitm,MANTE;
     ettemite;

     pdfimtmp #100;
|FIN;

|VARIABLES;
     aCampo = "";
     aValor = "";
     aAlfa = "";
     aAlfa1 = "";
     aAlfa2 = "";
     &swCopia = 0;
     &aArchivo = "";
     &nCuantos = 0;

     nAscii = 0;
     aCaracter = "";
     aCaracterSal = "";
     aCadena = "";
     aCadenaSal = "";
     aLongitud = "";
     nLongitud = 0;
     nPos = 0;
     nBusca = 0;
     nTipo = 0;
     aFich = "";

     &aProcedencia = "";
     &aListaFich = "";
     &aListaFichNor = "";
     &aListaFichCop = "";
     &aListaImpares = "";
     &swDondeEstoy = 0;
     &nContador = 0;
     &swETT = 0;

     aDia = "";
     aMes = "";
     aAny = "";

     &enEmp = 0;
     &enTra = 0;
     &eaFechaAlta = "";
     &marcados = 0;
     &swImpMasiva = 0;

     &aRutaExt = "";
     &aPathDestino = "";
     &aFichExt = "";
     &eaDSMAC = "";
|FIN;

|PROCESO MesLetra;
     |SI aMes = "01"; aMes = "enero"; |FINSI;
     |SI aMes = "02"; aMes = "febrero"; |FINSI;
     |SI aMes = "03"; aMes = "marzo"; |FINSI;
     |SI aMes = "04"; aMes = "abril"; |FINSI;
     |SI aMes = "05"; aMes = "mayo"; |FINSI;
     |SI aMes = "06"; aMes = "junio"; |FINSI;
     |SI aMes = "07"; aMes = "julio"; |FINSI;
     |SI aMes = "08"; aMes = "agosto"; |FINSI;
     |SI aMes = "09"; aMes = "septiembre"; |FINSI;
     |SI aMes = "10"; aMes = "octubre"; |FINSI;
     |SI aMes = "11"; aMes = "noviembre"; |FINSI;
     |SI aMes = "12"; aMes = "diciembre"; |FINSI;
|FPRC;

|PROCESO sustituye2;
     nAscii = & aCaracter;
     aCaracterSal = aCaracter;
     |SI nAscii = 160;             || el caracter  
          aCaracterSal = "\341";
     |FINSI;
     |SI nAscii = 130;             || el caracter ‚
          aCaracterSal = "\351";
     |FINSI;
     |SI nAscii = 161;             || el caracter ¡
          aCaracterSal = "\355";
     |FINSI;
     |SI nAscii = 162; |O nAscii = 243;            || el caracter ¢
          aCaracterSal = "\363";
     |FINSI;
     |SI nAscii = 163;             || el caracter £
          aCaracterSal = "\372";
     |FINSI;
     |SI nAscii = 164;             || el caracter ¤
          aCaracterSal = "\361";
     |FINSI;
     |SI nAscii = 241;             || el caracter ¤ (otro codigo ¨?)
          aCaracterSal = "\361";
     |FINSI;
     |SI nAscii = 165;             || el caracter ¥
          aCaracterSal = "\321";
     |FINSI;
     |SI nAscii = 129;
          aCaracterSal = "\374";   || el caracter u con dieresis
     |FINSI;
     |SI nAscii = 154;
          aCaracterSal = "\334";   || el caracter U (mayuscula) con dieresis
     |FINSI;
     |SI nAscii = 167;
          aCaracterSal = "\272";   || el caracter §
     |FINSI;
     |SI nAscii = 166;
          aCaracterSal = "\252";   || el caracter ¦
     |FINSI;
     |SI aCaracter = "(";
          aCaracterSal = "\(";
     |FINSI;
     |SI aCaracter = ")";
          aCaracterSal = "\)";
     |FINSI;
     |SI aCaracter = "€";       || la C-edilla o C trencada mayuscula
          aCaracterSal = "Ç";
     |FINSI;
     |SI aCaracter = "‡";       || la c-edilla o c trencada minuscula
          aCaracterSal = "ç";
     |FINSI;
|FPRC;

|PROCESO sustituye;
     aCadenaSal = "";
     aLongitud = aCadena % 0;
     nLongitud = aLongitud;
     |PARA nPos = 1; |SI nPos [ nLongitud; |HACIENDO nPos = nPos + 1;
          nBusca = (nPos * 100) + 1;
          aCaracter = aCadena % nBusca;
          |HAZ sustituye2;
          aCadenaSal = aCadenaSal + aCaracterSal;
     |SIGUE;
|FPRC;

|PROCESO GrabaTemporal;
     |QBF aValor;
     aCadena = aValor;
     |HAZ sustituye;
     aValor = aCadenaSal;
     aAlfa1 = aValor % 101;
     |SI aAlfa1 = ".";
          aValor = "";
     |FINSI;
     #pdfimtmp#0 = aCampo;
     #pdfimtmp#1 = aValor;
     #pdfimtmp#2 = nTipo;
     |GRABA 020#pdfimtmp.n;
|FPRC;

|PROCESO CargaCamposCPD;
     || I - EMPRESA DE TRABAJO TEMPORAL

     aCampo = "ettrep"; aValor = #0#15; |HAZ GrabaTemporal;
     aCampo = "ettempresa"; aValor = #0#2; |HAZ GrabaTemporal;
     aCampo = "ettdomic";
     aValor = #0#3; |QBF aValor;
     aValor = aValor + " " + #0#4; |QBF aValor;
     aAlfa = #0#5; |QBF aAlfa;
     |SI aAlfa ! "";
          aValor = aValor + " - " + aAlfa; |QBF aValor;
     |FINSI;
     |HAZ GrabaTemporal;
     aCampo = "ettnumauto"; aValor = #1#5; |HAZ GrabaTemporal;
     aCampo = "ettdnirep"; aValor = #0#17; |HAZ GrabaTemporal;
     aCampo = "ettcif"; aValor = #0#10; |HAZ GrabaTemporal;
     aCampo = "ettlocali";
     aAlfa = #0#7; |QBF aAlfa; aAlfa = ("00" + aAlfa) % -102;
     aValor = aAlfa;
     aAlfa = #0#8; |QBF aAlfa; aAlfa = ("000" + aAlfa) % -103;
     aValor = aValor + aAlfa;

     aAlfa = #0#6; |QBF aAlfa; aValor = aValor + " - " + aAlfa;
     aAlfa = #0#9; |QBF aAlfa; aValor = aValor + " (" + aAlfa + ")";
     |HAZ GrabaTemporal;
     aCampo = "ettvigencia"; aValor = "INDEFINIDA"; |HAZ GrabaTemporal;
     aCampo = "ettenconc"; aValor = #0#18; |HAZ GrabaTemporal;
     aValor = #labcentr#10;
     |QBF aValor;
     |SI aValor = "";
          aValor = #0#13;
     |FINSI;
     aCampo = "ettctacot"; |HAZ GrabaTemporal;

     || II - EMPRESA USUARIA

     aCampo = "usurep"; aValor = #2#21; |HAZ GrabaTemporal;
     aCampo = "usuempresa"; aValor = #2#1; |HAZ GrabaTemporal;
     aCampo = "usudomic";
     aValor = #2#2; |QBF aValor;
     aValor = aValor + " " + #2#3; |QBF aValor;
     aAlfa = #2#4; |QBF aAlfa;
     |SI aAlfa ! "";
          aValor = aValor + " - " + aAlfa; |QBF aValor;
     |FINSI;
     |HAZ GrabaTemporal;
     aCampo = "usudomicen";
     aValor = #2#32; |QBF aValor;
     aValor = aValor + " " + #2#33; |QBF aValor;
     aAlfa = #2#34; |QBF aAlfa;
     |SI aAlfa ! "";
          aValor = aValor + " - " + aAlfa; |QBF aValor;
     |FINSI;
     |HAZ GrabaTemporal;

     aCampo = "usudnirep"; aValor = #2#22; |HAZ GrabaTemporal;
     aCampo = "usucif"; aValor = #2#8; |HAZ GrabaTemporal;
     aCampo = "usulocali";

     aAlfa = #2#5; |QBF aAlfa; aAlfa = ("00" + aAlfa) % -102;
     aValor = aAlfa;
     aAlfa = #2#6; |QBF aAlfa; aAlfa = ("000" + aAlfa) % -103;
     aValor = aValor + aAlfa;
     aAlfa = #2#7; |QBF aAlfa; aValor = aValor + " - " + aAlfa;
     aAlfa = #3#1; |QBF aAlfa; aValor = aValor + " (" + aAlfa + ")";
     |HAZ GrabaTemporal;
     aCampo = "usulocalicen";
     aAlfa = #2#35; |QBF aAlfa; aAlfa = ("00" + aAlfa) % -102;
     aValor = aAlfa;
     aAlfa = #2#36; |QBF aAlfa; aAlfa = ("000" + aAlfa) % -103;
     aValor = aValor + aAlfa;
     aAlfa = #2#37; |QBF aAlfa; aValor = aValor + " - " + aAlfa;
     aAlfa = #3#1; |QBF aAlfa; aValor = aValor + " (" + aAlfa + ")";
     |HAZ GrabaTemporal;
     aCampo = "usuenconc"; aValor = #2#23; |HAZ GrabaTemporal;
     aCampo = "usuctacot"; aValor = #2#20; |HAZ GrabaTemporal;

     || III - SUPUESTO DE CELEBRACION

     aCampo = "supuesto";
     aAlfa = #7#74; |QBF aAlfa; aValor = aAlfa + " ";
     aAlfa = #7#75; |QBF aAlfa; aValor = aValor + aAlfa;
     |HAZ GrabaTemporal;

     || IV - DATOS DEL TRABAJADOR

     aCampo = "nombretra"; aValor = #5#2; |HAZ GrabaTemporal;
     aCampo = "fecnactra"; aValor = #5#14; |HAZ GrabaTemporal;
     aCampo = "dnitra"; aValor = #5#3; |HAZ GrabaTemporal;
     aCampo = "estudiostra"; aValor = #5#22; |HAZ GrabaTemporal;
     aCampo = "codestudtra"; aValor = #5#48; |HAZ GrabaTemporal;

     aCampo = "domictra";
     aValor = #5#5; |QBF aValor;
     aValor = aValor + " " + #5#6; |QBF aValor;
     aAlfa = #5#7; |QBF aAlfa;
     |SI aAlfa ! "";
          aValor = aValor + " - " + aAlfa; |QBF aValor;
     |FINSI;
     aAlfa = #5#8; |QBF aAlfa; aValor = aValor + ", " + aAlfa;
     aAlfa = #4#10; |QBF aAlfa; aValor = aValor + " (" + aAlfa + ")";
     |HAZ GrabaTemporal;

     || V - CARACTERISTICAS DEL PUESTO DE TRABAJO

     aCampo = "cualifi"; aValor = #4#18; |HAZ GrabaTemporal;
     aCampo = "funciones"; aValor = "LAS PROPIAS DEL PUESTO DE TRABAJO";
     |HAZ GrabaTemporal;
     aCampo = "riesgos"; aValor = "LOS PROPIOS DEL PUESTO DE TRABAJO";
     |HAZ GrabaTemporal;
     aCampo = "servicios"; aValor = "No procede";
     |HAZ GrabaTemporal;
     aCampo = "retribucion";
     aAlfa = #8#34; |QBF aAlfa; aValor = aAlfa;
     aAlfa = "";
     |SI #8#35 = "H" ; aAlfa = " a la hora  "; |FINSI;
     |SI #8#35 = "S" ; aAlfa = " a la semana"; |FINSI;
     |SI #8#35 = "M" ; aAlfa = " al mes     "; |FINSI;
     aValor = aValor + aAlfa;
     |HAZ GrabaTemporal;
     aCampo = "horario";
     aAlfa = #7#105; |QBF aAlfa; aValor = aAlfa;
     aAlfa = #7#106; |QBF aAlfa; aValor = aValor + " " + aAlfa;
     aAlfa = #7#107; |QBF aAlfa; aValor = aValor + " " + aAlfa;
     aAlfa = "Horario: " + #8#43; |QBF aAlfa;
     aValor = aValor + ". " + aAlfa;
     |HAZ GrabaTemporal;
     aCampo = "convenio"; aValor = #nomconca#1;
     |HAZ GrabaTemporal;

     || VI - DURACION DEL CONTRATO

     aCampo = "duracion";
     aAlfa = #7#5;
     aAlfa = "Desde el " + aAlfa;
     aValor = aAlfa;
     aAlfa = #7#6;
     aAlfa = " hasta el " + aAlfa;
     aValor = aValor + aAlfa;
     |HAZ GrabaTemporal;

     || VII - CONDICIONES ECONOMICAS

     aCampo = "precio"; aValor = #8#40; |HAZ GrabaTemporal;
     aCampo = "formapago"; aValor = #19#1; |HAZ GrabaTemporal;
     aCampo = "banco"; aValor = #20#1; |HAZ GrabaTemporal;
     aCampo = "cuenta"; aValor = #2#25; |HAZ GrabaTemporal;
     aCampo = "otrascond1"; aValor = "SEGUN CLAUSULAS ADICIONALES"; |HAZ GrabaTemporal;

     || FECHA Y FIRMA

     aCampo = "locafir"; aValor = #0#6; |HAZ GrabaTemporal;
     aAlfa = #ettemite#0;
     aDia = aAlfa % 102;
     aAny = aAlfa % -104;
     aMes = aAlfa % 402;
     |HAZ MesLetra;

     aCampo = "diafir"; aValor = aDia; |HAZ GrabaTemporal;
     aCampo = "mesfir"; aValor = aMes; |HAZ GrabaTemporal;
     aCampo = "anyfir"; aValor = aAny; |HAZ GrabaTemporal;

     aCampo = "empusu"; aValor = "Empresa usuaria"; |HAZ GrabaTemporal;
     aCampo = "empett"; aValor = "Empresa de Trabajo Temporal"; |HAZ GrabaTemporal;

     aAlfa1 = #labtraba#0; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
     aAlfa2 = #labtraba#1; |QBF aAlfa2; aAlfa2 = ("00000" + aAlfa2) % -105;
     aAlfa = aAlfa1 + "." + aAlfa2;
     aCampo = "registro"; aValor = aAlfa; |HAZ GrabaTemporal;
|FPRC;

|PROCESO ImprePDF;
     aAlfa1 = Usuario; |QBT aAlfa1;    aAlfa1 = ("c" + aAlfa1) % 108;
     |NOME_DAT #100 aAlfa1;

     |DELINDEX #pdfimtmp;
     |ABRE #pdfimtmp;         || Contrato Puesta a Disposicion
     swCopia = 1;
     aArchivo = "cpd.pdf"
     |HAZ CargaCamposCPD;
     nCuantos = 1;
     |CIERRA #pdfimtmp;

     enEmp = #labtraba#0;
     enTra = #labtraba#1;

     swETT = 1;
     |SUB_EJECUTA "pdfgraba";
     enEmp = #labtraba#0;
     enTra = #labtraba#1;
     swETT = 1;
     eaFechaAlta = #ettemite#0;
     swImpMasiva = 0;
     |SI marcados > 1;
          swImpMasiva = 1;
     |FINSI;
     |SUB_EJECUTA "pdfimpre";
|FPRC;

|PROCESO Rutas;
     || el programa esta linkado a la ett, pongo esto, para hacer los PATH_DAT
     |DBASS aFich;
     |QBF aFich;
     aFich = aFich + "laboral/fich/";
     |PATH_DAT #labempre#aFich#;
     |PATH_DAT #labtraba#aFich#;
     |PATH_DAT #labcentr#aFich#;
     |PATH_DAT #nomconca#aFich#;

     |DBASS aFich;
     |QBF aFich;
     aFich = aFich + "facturar/fich/";
     |PATH_DAT #asbancli#aFich#;
     |PATH_DAT #asfpagos#aFich#;
|FPRC;

|PROCESO lecturas;
     |ABRE #ettcontr; |LEE 000#ettcontr.p; |CIERRA #ettcontr;

     |DDEFECTO #labempre;
     |ABRE #labempre;
     #labempre#0 = #ettcontr#4;
     |LEE 000#labempre.=;
     |CIERRA #labempre;

     |DDEFECTO #labtraba;
     |ABRE #labtraba;
     #labtraba#0 = #ettcontr#4;
     #labtraba#1 = #ettemitm#5;
     |LEE 000#labtraba.=;
     |CIERRA #labtraba;

     |DDEFECTO #labcentr;
     |ABRE #labcentr;
     #labcentr#0 = #ettcontr#4;
     #labcentr#1 = #labtraba#77;
     |LEE 000#labcentr.=;
     |CIERRA #labcentr;

     |DDEFECTO #nomconca;
     |ABRE #nomconca;
     #nomconca#0 = #labtraba#87;
     |LEE 000#nomconca.=;
     |CIERRA #nomconca;

     |DDEFECTO #ettclien;
     |ABRE #ettclien;
     #ettclien#0 = #ettemitm#4;
     |LEE 000#ettclien.=;
     |CIERRA #ettclien;

     |DDEFECTO #ettprovi;
     |ABRE #ettprovi;
     #ettprovi#0 = #ettclien#5;
     |LEE 000#ettprovi.=;
     |CIERRA #ettprovi;

     |DDEFECTO #ettperso;
     |ABRE #ettperso;
     #ettperso#0 = #ettemitm#3;
     |LEE 000#ettperso.=;
     |CIERRA #ettperso;

     |DDEFECTO #ettpedi1;
     |ABRE #ettpedi1;
     #ettpedi1#0 = #ettemitm#1;
     |LEE 000#ettpedi1.=;
     |CIERRA #ettpedi1;

     |DDEFECTO #ettpuest;
     |ABRE #ettpuest;
     #ettpuest#0 = #ettpedi1#59;
     |LEE 000#ettpuest.=;
     |CIERRA #ettpuest;

     |DDEFECTO #ettpedi2;
     |ABRE #ettpedi2;
     #ettpedi2#0 = #ettemite#1;
     #ettpedi2#1 = #ettemite#8;
     |LEE 000#ettpedi2.=;
     |CIERRA #ettpedi2;

     |DDEFECTO #ettpuest
     |ABRE #ettpuest
     #ettpuest#0 = #ettpedi1#59;
     |LEE 000#ettpuest.=;            || lee puestos AGIETT
     |CIERRA #ettpuest;

     |DDEFECTO #etttarca;
     |ABRE #etttarca;
     #etttarca#0 = #ettpedi2#16;
     #etttarca#1 = #ettpedi2#17;
     |LEE 000#etttarca.=;            || lee tarifa cabs. AGIETT
     |CIERRA #etttarca;

     |DDEFECTO #19;
     |ABRE #19;
     #19#0 = #ettclien#26;
     |LEE 000#19=;            || lee formas de pago FACGES
     |CIERRA #19;

     |DDEFECTO #20;
     |ABRE #20;
     #20#0 = #ettclien#24;
     |LEE 000#20=;            || lee bancos cliente FACGES
     |CIERRA #20;
|FPRC;

|PROCESO Contrato;
     |HAZ Rutas;
     |HAZ lecturas;
     |HAZ ImprePDF;
|FPRC;
