|FICHEROS;
    ettparte #0;    || partes cabs.
    ettpedi1 #1;    || pedidos cabs.
    ettpedi2 #2;    || pedidos lins.
    ettparli #3;    || partes lins.
    ettctove #4;    || alta ctos. convenio

    labempre #10;   || empresas AGINOM
    labtraba #12;   || trabajadores AGINOM
    ettcontr #13;   || control AGIETT
    ettclien #14;   || clientes AGIETT
    ettprovi #15;   || provincias AGIETT
    ettperso #16;   || personal AGIETT
    ettpuest #17;   || puestos AGIETT
    nomconli #20;   || convenios lins.
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    sw_dia = 0;
     nModo = 0;
|FIN;
____________________________________/ CALCULOS DE PANTALLA CABECERAS
|PROCESO tipo8; |TIPO 8;
|ABRE #13;
|LEE 000#13p;
|SI FSalida = 0;
        alfa1 = #13#3;    |QBF alfa1;
        alfa1 = alfa1 + "/fich/";
    |PATH_DAT #10 alfa1;
    |PATH_DAT #12 alfa1;
    |PATH_DAT #20 alfa1;

        alfa1 = #13#3;    |QBF alfa1;
        alfa1 = alfa1 + "/pan/";
    |PATH_PAN #20 alfa1;

    |DDEFECTO #10;
    |ABRE #10;
        #10#0 = #13#4;       || empresa AGINOM
    |LEE 000#10=;
    |CIERRA #10;
|SINO;
    |MENAV "    Control AGIETT sin definir ...";
|FINSI;
|CIERRA #13;
|FPRC;

|PROCESO NoAlta; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          |PTEC 806;
     |FINSI;
|FPRC;

|PROCESO lectura_varios;
|DDEFECTO #10;
|ABRE #10;
    #10#0 = #13#4;
|LEE 000#10=;            || lee empresas AGINOM
|CIERRA #10;

|DDEFECTO #2;
|ABRE #2;
    #2#0 = #0#3;
    #2#1 = #0#48;
|LEE 000#2=;        || lee la linea del pedido
|CIERRA #2;

|DDEFECTO #12;
|ABRE #12;
    #12#0 = #13#4;
    #12#1 = #2#24;
|LEE 000#12=;            || lee trabajadores AGINOM
|CIERRA #12;

|DDEFECTO #14;
|ABRE #14;
    #14#0 = #1#2;
|LEE 000#14=;            || lee cliente AGIETT
|CIERRA #14;

|DDEFECTO #15;
|ABRE #15;
    #15#0 = #14#5;
|LEE 000#15=;            || lee provincia AGIETT
|CIERRA #15;

|DDEFECTO #16;
|ABRE #16;
    #16#0 = #2#2;
|LEE 000#16=;            || lee personal AGIETT
|CIERRA #16;

|DDEFECTO #17;
|ABRE #17;
    #17#0 = #1#59;
|LEE 000#17=;            || lee puestos AGIETT
|CIERRA #17;
|FPRC;

|PROCESO tipo4; |TIPO 4;
|IMPRE 0;
|SI FSalida = 0;
    |ABRE #1;
    |ABRE #2;
        #1#0 = #0#3;          || codigo pedido
    |LEE 020#1=;
    |SI FSalida = 0;
            #2#0 = #0#3;      || codigo pedido
            #2#1 = #0#48;     || linea pedido
        |LEE 020#2=;
        |SI FSalida = 0;
            |HAZ lectura_varios;
            |INFOR "ett01par";
            |PRINT;
            |PIEINF;
            |FININF;
        |FINSI;
    |FINSI;
    |CIERRA #1;
    |CIERRA #2;
|SINO;
    |MENAV "    Proceso abortado ...";
|FINSI;
|FINIMP;
|FPRC;

|PROCESO totales; |TIPO 0;
    nume1 = Contenido;
|SI nume1 ! #0Campo;
        #0#49 = #0#29 + #0#30 + #0#31 + #0#32 + #0#33 + #0#34 + #0#35;
        #0#50 = #0#36 + #0#37 + #0#38 + #0#39 + #0#40 + #0#41 + #0#42;

        #0#64 = #0#57 + #0#58 + #0#59 + #0#60 + #0#61 + #0#62 + #0#63;
    |PINTA #0#49;
    |PINTA #0#50;
    |PINTA #0#64;
|FINSI;
|FPRC;

|PROCESO consulta; |TIPO 7;
    modo = (FEntrada / 100) ! 0;
|SI modo = 4;
        alfa1 = "S";
|SINO;
        alfa1 = "N";
|FINSI;
|C_M #0Campo, 1, alfa1;
|FPRC;

|PROCESO noextras; |TIPO 7;
    nume1 = Campo - 21;
|SI #0nume1 = "01.01.0000";
        alfa1 = "N";
|SINO;
        alfa1 = "S";
|FINSI;
|C_M #0Campo, 1, alfa1;
|FPRC;

|PROCESO novaria; |TIPO 7;
    nume1 = Campo - 28;
|SI #0nume1 = "01.01.0000";
        alfa1 = "N";
|SINO;
        alfa1 = "S";
|FINSI;
|C_M #0Campo, 1, alfa1;
|FPRC;

|PROCESO nonoche; |TIPO 7;
    nume1 = Campo - 49;
|SI #0nume1 = "01.01.0000";
        alfa1 = "N";
|SINO;
        alfa1 = "S";
|FINSI;
|C_M #0Campo, 1, alfa1;
|FPRC;
____________________________________/ CALCULOS DE PANTALLA LINEAS
|PROCESO mayus1; |TIPO 0;
    #3Campo = #3Campo > #3Campo;
|PINTA #3Campo;
|FPRC;

|PROCESO cheq_dia; |TIPO 0;
    sw_dia = 0;
|SI #3#2 = "L";|Y #0#8 = "01.01.0000";  sw_dia = 1;    |FINSI;
|SI #3#2 = "M";|Y #0#9 = "01.01.0000";  sw_dia = 1;    |FINSI;
|SI #3#2 = "X";|Y #0#10= "01.01.0000";  sw_dia = 1;    |FINSI;
|SI #3#2 = "J";|Y #0#11= "01.01.0000";  sw_dia = 1;    |FINSI;
|SI #3#2 = "V";|Y #0#12= "01.01.0000";  sw_dia = 1;    |FINSI;
|SI #3#2 = "S";|Y #0#13= "01.01.0000";  sw_dia = 1;    |FINSI;
|SI #3#2 = "D";|Y #0#14= "01.01.0000";  sw_dia = 1;    |FINSI;
|SI sw_dia = 1;
    |MENAV "    Dia de la semana fuera de contrato ...";
    |ERROR;
|FINSI;
|FPRC;

|PROCESO min;
    #20#0 = #10#1;
    #20#2 = 101;
|FPRC;

|PROCESO max;
    #20#0 = #10#1;
    #20#2 = 420;
|FPRC;

|PROCESO consulta_cto;
|PUSHV 0501 2380;
|ENTLINEAL #1#20, 2, 4, 21, 1, min, max;
|POPV;
|FPRC;

|PROCESO cheq_cto; |TIPO 0;
|SI FSalida = 9;
    |HAZ consulta_cto;
    |ERROR;
|SINO;
    |ABRE #20;
        #20#0 = #10#1;       || convenio
        #20#2 = #3#3;        || codigo concepto
    |LEE 000#20=;
    |SI FSalida ! 0;
        |AVISO;
        |CONFI "    SConcepto INEXISTENTE en Convenio. ¨Confirma Alta? (S/N): ";
        |SI FSalida = 0;
            |PUSHV 0501 2380;
            |PINPA #0#4;
            |PINDA #0#4;
            |ENDATOS #1#4;
            |SI FSalida = 0;
                |DDEFECTO #20;
                    #20#0 = #10#1;          || codigo convenio
                    #20#2 = #3#3;           || codigo concepto
                    #20#3 = #4#0;           || calculo ALFA
                    #20#4 = #4#1;           || calculo NUME
                    #20#5 = #4#2;           || situacion
                    #20#6 = #4#3;           || descripcion
                |GRABA 020#20n;
                |POPV;
                    #3#5 = #20#6;
                |PINTA #3#5;
            |SINO;
                |POPV;
                |ERROR;
            |FINSI;
        |SINO;
            |ERROR;
        |FINSI;
    |SINO;
            #3#5 = #20#6;    |PINTA #3#5;
    |FINSI;
    |CIERRA #20;
|FINSI;
|FPRC;
