|FICHEROS;
    repasdec #0;   ||
    redatcon #1;   ||  Datos del Contribuyente 1998.
    repasde1 #3;   ||  Temporal pase deducciones.

    rededu01 #4;   ||  Deducciones Pendientes ejercicios anteriores 1998.
    rededu02 #5;   ||  Deducciones Ejercicio actual 1998.
    rededu03 #6;   ||  Deducciones Canarias 1998.
    rededu04 #7;   ||  Deducciones Expo, etc..., 1998.
    rededu05 #8;   ||  Deducciones Creacion de empleo 1998.
    reprogra #9;

    m2totrea #2;   ||  retotreb 1997.
    m2totreb #22;  ||  retotreb 1997.
    m2datcon #10;  ||  Datos del Contribuyente 1997.
    m2dedu01 #14;  ||  Deducciones Pendientes ejercicios anteriores 1997.
    m2dedu02 #15;  ||  Deducciones Ejercicio actual 1997.
    m2dedu03 #16;  ||  Deducciones Canarias 1997.
    m2dedu04 #17;  ||  Deducciones Expo, etc..., 1997.
    m2dedu05 #18;  ||  Deducciones Creacion de empleo 1997.
|FIN;

|VARIABLES;
    pen_1        = 0;
    pen_2        = 0;
    pen_3        = 0;
    pen_4        = 0;
    pen_5        = 0;
    apl_1        = 0;
    apl_2        = 0;
    apl_3        = 0;
    apl_4        = 0;
    apl_5        = 0;
    nSwFich03    = 0;
    nSwFich04    = 0;
    fichero      = "";
    path         = "";
    path97       = "";
    path98       = "";
    tipdec_ini   = "";
    tipdec_fin   = "";
    tipo_ini     = "";
    tipo_fin     = "";
    any_min      = 93;
    any_max      = 97;
    orden_rededu = 0;
    camtmp       = 0;
    camred       = 0;
    campte       = 0;
    camapl       = 0;
    existe_emp   = 0;
    existe_red   = 0;
    linea_red1   = 0;
    linea_red2   = 0;
    linea_red3   = 0;
    linea_red4   = 0;
    linea_red5   = 0;

    nCampos1     = 0;
    nCampos2     = 0;
    nCampoApli   = 0;
    nPuntero     = 0;
    aAlfa        = "";


    {50}pDedu    = 0;
|FIN;













|PROCESO cambia_paths;
|PATH_DAT #2  path97;  |NOME_DAT #2  "retotrea";
|PATH_DAT #22 path97;  |NOME_DAT #22 "retotreb";
|PATH_DAT #10 path97;  |NOME_DAT #10 "redatcon";
|PATH_DAT #14 path97;  |NOME_DAT #14 "rededu01";
|PATH_DAT #15 path97;  |NOME_DAT #15 "rededu02";
|PATH_DAT #16 path97;  |NOME_DAT #16 "rededu03";
|PATH_DAT #17 path97;  |NOME_DAT #17 "rededu04";
|PATH_DAT #18 path97;  |NOME_DAT #18 "rededu05";
|FPRC;

|PROCESO fichero;
fichero = Usuario; |QBT fichero;
fichero = (fichero + "zzzzzzzz") % 106;
fichero = "pd" + fichero;
|FPRC;

|PROCESO inicio; |TIPO 10;
|HAZ renta97;
|HAZ renta98;
|FPRC;


|PROCESO renta97; |TIPO 7;
|DBASS path97;
#0#4 = path97 + "ren1997/fich/";
|PINTA #0#4;
path97 = #0#4;  |QBF path97;
|FPRC;

|PROCESO path97; |TIPO 0;
path97 = #0#4;  |QBF path97;
path   = path97;
|HAZ directorio;
|FPRC;

|PROCESO renta98; |TIPO 7;
|DBASS path98;
#0#5   = path98 + "ren1998/fich/";  |PINTA #0#5;
path98 = #0#5;  |QBF path98;
|FPRC;

|PROCESO path98; |TIPO 0;
path98 = #0#5;  |QBF path98;
path   = path98;
|HAZ directorio;
|FPRC;

|PROCESO directorio;
|SI path97 = path98;
    |MENAV "0000No pueden ser el mismo directorio.";
    |ERROR;
    |ACABA;
|FINSI;

|SI path = path97;
    |PATH_DAT #10 path97;
    |NOME_DAT #10 "redatcon";
    |ABRE #10;
    existe_emp = FSalida;
    |CIERRA #10;
|SINO;
    |ABRE #1;
    existe_emp = FSalida;
    |CIERRA #1;
|FINSI;

|SI existe_emp ! 0;
    |PTEC 18;
    |MENAV "0000Existe algun error en la entrada del directorio. Revise el texto.";
    |ERROR;
|FINSI;
|FPRC;

|PROCESO Graba01;
     |SI #0#2 = "N";
         |SI linea_red1 = 0;
             FSalida = 0;
             #4#0 = #3#0;      ||  Codigo de empresa.
             #4#1 = 0;
             |PARA; |SI FSalida = 0; |Y #4#0 = #3#0; |HACIENDO;
                    linea_red1 = #4#1;
                    |LEE 001#4>;
              |SIGUE;
         |FINSI;
         existe_red = 1;
     |SINO;
         #4#0 = #3#0;  ||  Codigo de empresa.
         #4#1 = #3#1;  ||  Numero de linea.
         |LEE 101#4=;
         existe_red = FSalida;
     |FINSI;
     #3#9 = #3#10;              ||  Saldo = Aplicado.
     #4#0 = #3#0;               ||  Codigo de empresa.
     #4#1 = #3#1 + linea_red1;  ||  Numero de linea.
     #4#2 = #3#2;               ||  Tipo de declaracion.
     #4#3 = #3#3;               ||  Perceptor.
     |PARA camtmp = 5; |SI camtmp [ 20; |HACIENDO camtmp = camtmp + 1;
           camred = camtmp - 1;
           #4camred = #3camtmp;
     |SIGUE;
     |SI existe_red = 0;
         |GRABA 020#4a;
     |SINO;
         |GRABA 020#4b;
     |FINSI;
     |LIBERA #4;
     #9#20 = 1;
|FPRC;

|PROCESO Trata01;
                            ||  A partir del a¤o calculo el campo pendiente.

     aAlfa = #3#7 % 101;
     |SI #3#5 = 93;                  nPuntero = 2;  nCampoApli = 5; |FINSI;
     |SI #3#5 = 94;                  nPuntero = 2;  nCampoApli = 5; |FINSI;
     |SI #3#5 = 95;                  nPuntero = 2;  nCampoApli = 5; |FINSI;
     |SI #3#5 = 96; |Y aAlfa = "*";  nPuntero = 3;  nCampoApli = 7; |FINSI;
     |SI #3#5 = 96; |Y aAlfa ! "*";  nPuntero = 4;  nCampoApli = 9; |FINSI;
     nPuntero = nPuntero - 1;

     IpDedu = pDedu1 <-;
     IpDedu = IpDedu + nPuntero;
     |SI pDedu ! 0;
         |SI #22nCampoApli > 0;
             |SI #3#10 > #22nCampoApli;
                 #3#10 = #3#10 - #22nCampoApli;
                 #22nCampoApli = 0;
                 |HAZ Graba01;
             |SINO;
                  #22nCampoApli = #22nCampoApli - #3#10;
             |FINSI;
         |SINO;
             |HAZ Graba01;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Graba02;  ||  ATENCION!  No graba en rededu02 sino en rededu01.
     |SI linea_red2 = 0;
         FSalida = 0;
         #4#0 = #3#0;      ||  Codigo de empresa.
         #4#1 = 0;
         |PARA; |SI FSalida = 0; |Y #4#0 = #3#0; |HACIENDO;
                linea_red2 = #4#1;
                |LEE 001#4>;
         |SIGUE;
     |FINSI;

     #3#9 = #3#10;              ||  Saldo = Aplicado.
     #4#0 = #3#0;               ||  Codigo de empresa.
     #4#1 = #3#1 + linea_red2;  ||  Numero de linea.
     #4#2 = #3#2;               ||  Tipo de declaracion.
     #4#3 = #3#3;               ||  Perceptor.
     |PARA camtmp = 5; |SI camtmp [ 20; |HACIENDO camtmp = camtmp + 1;
           camred = camtmp - 1;
           #4camred = #3camtmp;
     |SIGUE;

     |GRABA 020#4b;
     |LIBERA #4;
     #9#20 = 1;
|FPRC;

|PROCESO Trata02;
     |SI #3#4 = "MA"; nPuntero = 5;  nCampoApli = 11;  |FINSI;
     |SI #3#4 = "EL"; nPuntero = 6;  nCampoApli = 13;  |FINSI;
     |SI #3#4 = "PC"; nPuntero = 7;  nCampoApli = 15;  |FINSI;
     |SI #3#4 = "GD"; nPuntero = 8;  nCampoApli = 17;  |FINSI;
     |SI #3#4 = "BI"; nPuntero = 9;  nCampoApli = 19;  |FINSI;
     |SI #3#4 = "AE"; nPuntero = 10; nCampoApli = 21;  |FINSI;
     |SI #3#4 = "FP"; nPuntero = 11; nCampoApli = 23;  |FINSI;
     |SI #3#4 = "CE"; nPuntero = 12; nCampoApli = 24;  |FINSI;

     nPuntero = nPuntero - 1;

     IpDedu = pDedu1 <-;
     IpDedu = IpDedu + nPuntero;
     |SI pDedu ! 0;
         |SI #22nCampoApli > 0;
             |SI #3#10 > #22nCampoApli;
                 #3#10 = #3#10 - #22nCampoApli;
                  #22nCampoApli = 0;
                  |HAZ Graba02;
             |SINO;
                  #22nCampoApli = #22nCampoApli - #3#10;
             |FINSI;
         |SINO;
             |HAZ Graba02;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Graba03;
     |SI #0#2 = "N";
         |SI linea_red3 = 0;
             FSalida = 0;
             #6#0 = #3#0;      ||  Codigo de empresa.
             #6#1 = 0;
             |PARA; |SI FSalida = 0; |Y #6#0 = #3#0; |HACIENDO;
                    linea_red3 = #6#1;
                    |LEE 001#6>;
             |SIGUE;
         |FINSI;
         existe_red = 1;
     |SINO;
         #6#0 = #3#0;  ||  Codigo de empresa.
         #6#1 = #3#1;  ||  Numero de linea.
         |LEE 101#6=;
         existe_red = FSalida;
     |FINSI;

     #3#9 = (#3#10 * 100) / #3#8;         ||  Saldo = Aplicado % Deduccion.
     #6#0 = #3#0;               ||  Codigo de empresa.
     #6#1 = #3#1 + linea_red3;  ||  Numero de linea.
     #6#2 = #3#2;               ||  Tipo de declaracion.
     #6#3 = #3#3;               ||  Perceptor.
     #6#4 = #3#4;               ||  Tipo de deduccion;
     |PARA camtmp = 6; |SI camtmp [ 21; |HACIENDO camtmp = camtmp + 1;
           camred = camtmp - 1;
           #6camred = #3camtmp;
     |SIGUE;
     #6#20 = #3#5;
     |SI existe_red = 0;
         |GRABA 020#6a;
     |SINO;
         |GRABA 020#6b;
     |FINSI;
     |LIBERA #6;

     #9#22 = 1;
|FPRC;

|PROCESO Trata03;
     |SI nSwFich03 = 0;
         nSwFich03 = 1;
         apl_4 = #22#27 + #22#29 + #22#31 + #22#33;
         pen_4 = pDedu13 + pDedu14 + pDedu15 + pDedu16;
         apl_5 = #22#35 + #22#37 + #22#39 + #22#41 + #22#43 + #22#45 + #22#47 + #22#49;
         pen_5 = pDedu17 + pDedu18 + pDedu19 + pDedu20 + pDedu21 + pDedu22 + pDedu23 + pDedu24;
     |FINSI;

     |SI #3#5 [ 96;
         |SI pen_4 > 0;
             |SI apl_4 > 0;
                 |SI #3#10 > apl_4;
                     #3#10 = #3#10 - apl_4;
                     apl_4 = 0;
                     |HAZ Graba03;
                 |SINO;
                     apl_4 = apl_4 - #3#10;
                 |FINSI;
             |SINO;
                 |HAZ Graba03;
             |FINSI;
         |FINSI;
     |SINO;
         |SI pen_5 > 0;
             |SI apl_5 > 0;
                 |SI #3#10 > apl_5;
                     #3#10 = #3#10 - apl_5;
                     apl_5 = 0;
                     |HAZ Graba03;
                 |SINO;
                      apl_5 = apl_5 - #3#10;
                 |FINSI;
             |SINO;
                 |HAZ Graba03;
             |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Graba04;
     |SI #0#2 = "N";
         |SI linea_red4 = 0;
             FSalida = 0;
             #7#0 = #3#0;      ||  Codigo de empresa.
             #7#1 = 0;
             |PARA; |SI FSalida = 0; |Y #7#0 = #3#0; |HACIENDO;
                    linea_red4 = #7#1;
                    |LEE 001#7>;
             |SIGUE;
         |FINSI;
         existe_red = 1;
     |SINO;
         #7#0 = #3#0;  ||  Codigo de empresa.
         #7#1 = #3#1;  ||  Numero de linea.
         |LEE 101#7=;
         existe_red = FSalida;
     |FINSI;

     #3#9 = (#3#10 * 100) / #3#8;         ||  Saldo = Aplicado % Deduccion.
     #7#0 = #3#0;               ||  Codigo de empresa.
     #7#1 = #3#1 + linea_red4;  ||  Numero de linea.
     |PARA camtmp = 2; |SI camtmp [ 20; |HACIENDO camtmp = camtmp + 1;
           #7camtmp = #3camtmp;
     |SIGUE;
     |SI existe_red = 0;  |GRABA 020#7a;  |SINO;  |GRABA 020#7b;  |FINSI;
     |LIBERA #7;
     #9#23 = 1;
|FPRC;

|PROCESO Trata04;
     |SI nSwFich04 = 0;
         nSwFich04 = 1;
         pen_1 = pDedu25;
         pen_2 = pDedu26 + pDedu27 + pDedu28;
         pen_3 = pDedu29 + pDedu30 + pDedu31 + pDedu32 + pDedu33;

         apl_1 = #22#51;
         apl_2 = #22#53 + #22#55 + #22#57;
         apl_3 = #22#59 + #22#61 + #22#63 + #22#65 + #22#67;
     |FINSI;

     |SI #3#21 = 0;
         |SI pen_1 > 0;
             |SI apl_1 > 0;
                 |SI #3#10 > apl_1;
                     #3#10 = #3#10 - apl_1;
                     apl_1 = 0;
                     |HAZ Graba04;
                 |SINO;
                     apl_1 = apl_1 - #3#10;
                 |FINSI;
             |SINO;
                 |HAZ Graba04;
             |FINSI;
         |FINSI;
     |FINSI;

     |SI #3#21 = 4;
         |SI pen_2 > 0;
             |SI apl_2 > 0;
                 |SI #3#10 > apl_2;
                     #3#10 = #3#10 - apl_2;
                     apl_2 = 0;
                     |HAZ Graba04;
                 |SINO;
                     apl_2 = apl_2 - #3#10;
                 |FINSI;
             |SINO;
                 |HAZ Graba04;
             |FINSI;
         |FINSI;
     |FINSI;

     |SI #3#21 = 3;  |O #3#21 = 5; |O #3#21 = 6; |O #3#21 = 7;
         |SI pen_3 > 0;
             |SI apl_3 > 0;
                 |SI #3#10 > apl_3;
                     #3#10 = #3#10 - apl_3;
                     apl_3 = 0;
                     |HAZ Graba04;
                 |SINO;
                     apl_3 = apl_3 - #3#10;
                 |FINSI;
             |SINO;
                 |HAZ Graba04;
             |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Graba05;
     |SI #0#2 = "N";
         |SI linea_red5 ! 0; |ACABA; |FINSI;

         FSalida = 0;
         #8#0 = #3#0;      ||  Codigo de empresa.
         #8#1 = 0;
         |PARA; |SI FSalida = 0; |Y #8#0 = #3#0; |HACIENDO;
                linea_red5 = #8#1;
                |LEE 001#8>;
         |SIGUE;
         existe_red = 1;
     |SINO;
         #8#0 = #3#0;  ||  Codigo de empresa.
         #8#1 = #3#1;  ||  Numero de linea.
         |LEE 101#8=;
         existe_red = FSalida;
     |FINSI;

     #3#9 = #3#10;              ||  Saldo = Aplicado.
     #8#0 = #3#0;               ||  Codigo de empresa.
     #8#1 = #3#1 + linea_red5;  ||  Numero de linea.
     #8#2 = #3#2;               ||  Tipo de declaracion.
     #8#3 = #3#3;               ||  Perceptor.
     #8#4 = #3#5;               ||  A¤o.
     #8#5 = #3#7;               ||  Descripcion.
     |PARA camtmp = 9; |SI camtmp < 21; |HACIENDO camtmp = camtmp + 1;
           camred = camtmp - 3;
           #8camred = #3camtmp;
     |SIGUE;

     |SI existe_red = 0; |GRABA 020#8a; |SINO; |GRABA 020#8b; |FINSI;
     |LIBERA #8;
     #9#24 = 1;
|FPRC;

|PROCESO Trata05;
     |SI #3#5 < 92; |ACABA; |FINSI;
     |SI #3#5 > 95; |ACABA; |FINSI;

     |SI #3#5 = 92;  nPuntero = 34;  nCampoApli = 69; |FINSI;
     |SI #3#5 = 93;  nPuntero = 35;  nCampoApli = 71; |FINSI;
     |SI #3#5 = 94;  nPuntero = 35;  nCampoApli = 71; |FINSI;
     |SI #3#5 = 95;  nPuntero = 35;  nCampoApli = 71; |FINSI;

     nPuntero = nPuntero - 1;

     IpDedu = pDedu1 <-;
     IpDedu = IpDedu + nPuntero;
     |SI pDedu ! 0;
         |SI #22nCampoApli > 0;
             |SI #3#10 > #22nCampoApli;
                 #3#10 = #3#10 - #22nCampoApli;
                 #22nCampoApli = 0;
                 |HAZ Graba05;
              |SINO;
                 #22nCampoApli = #22nCampoApli - #3#10;
              |FINSI;
         |SINO;
              |HAZ Graba05;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO ControlTmp;
     |SI #3#21 = 1;  |HAZ Trata01;  |FINSI;
     |SI #3#21 = 2;  |HAZ Trata03;  |FINSI;
     |SI #3#21 = 8;  |HAZ Trata02;  |FINSI;
     |SI #3#21 = 9;  |HAZ Trata05;  |FINSI;
     |SI #3#21 = 0;  |HAZ Trata04;  |FINSI;
     |SI #3#21 = 3;  |HAZ Trata04;  |FINSI;
     |SI #3#21 = 4;  |HAZ Trata04;  |FINSI;
     |SI #3#21 = 5;  |HAZ Trata04;  |FINSI;
     |SI #3#21 = 6;  |HAZ Trata04;  |FINSI;
     |SI #3#21 = 7;  |HAZ Trata04;  |FINSI;

     |BORRA 120#3a;
     |LIBERA #3;
|FPRC;

|DEFBUCLE repasde1;
     #3#1;
     ;
     #1#0, any_min, 0, "N", 01;
     #1#0, any_max, 9, "S", 99;
     be;
     NULL, ControlTmp;
|FIN;

|PROCESO BorraVar;
     nSwFich03  = 0;
     nSwFich04  = 0;
     linea_red1 = 0;
     linea_red2 = 0;
     linea_red3 = 0;
     linea_red4 = 0;
     linea_red5 = 0;
     pen_1      = 0;
     pen_2      = 0;
     pen_3      = 0;
     pen_4      = 0;
     pen_5      = 0;
     apl_1      = 0;
     apl_2      = 0;
     apl_3      = 0;
     apl_4      = 0;
     apl_5      = 0;
|FPRC;

|PROCESO Carga01;  ||  Carga en temporal Deducciones pendientes (rededu01).
     #3#0 = #14#0;  ||  Codigo de empresa.
     #3#1 = #14#1;  ||  Numero de linea.
     #3#2 = #14#2;  ||  Tipo de declaracion.
     #3#3 = #14#3;  ||  Perceptor.
     #3#4 = "";     ||  Tipo de deduccion.
     |PARA camtmp = 5; |SI camtmp [ 20; |HACIENDO camtmp = camtmp + 1;
           camred = camtmp - 1;
           #3camtmp = #14camred;
     |SIGUE;
     #3#21 = orden_rededu;
|FPRC;

|PROCESO Carga02;  ||  Carga en temporal Deducciones ejercicio actual (rededu02).
     #3#0 = #15#0;  ||  Codigo de empresa.
     #3#1 = #15#1;  ||  Numero de linea.
     #3#2 = #15#2;  ||  Tipo de declaracion.
     #3#3 = #15#3;  ||  Perceptor.
     #3#4 = #15#4;  ||  Tipo de deduccion.
     #3#5 = 97;     ||  A¤o.
     |PARA camtmp = 6; |SI camtmp [ 20; |HACIENDO camtmp = camtmp + 1;
           camred = camtmp - 1;
           #3camtmp = #15camred;
     |SIGUE;
     #3#21 = orden_rededu;
|FPRC;

|PROCESO Carga03;  ||  Carga en temporal Deducciones Canarias (rededu03).
     #3#0 = #16#0;  ||  Codigo de empresa.
     #3#1 = #16#1;  ||  Numero de linea.
     #3#2 = #16#2;  ||  Tipo de declaracion.
     #3#3 = #16#3;  ||  Perceptor.
     #3#4 = #16#4;  ||  Tipo de deduccion.
     #3#5 = #16#20; ||  A¤o.
     |PARA camtmp = 6; |SI camtmp [ 20; |HACIENDO camtmp = camtmp + 1;
           camred = camtmp - 1;
           #3camtmp = #16camred;
     |SIGUE;
     #3#21 = orden_rededu;
|FPRC;

|PROCESO Carga04;  ||  Carga en temporal Deducciones Expo, etc. (rededu04)
     |PARA camtmp = 0; |SI camtmp [ 20; |HACIENDO camtmp = camtmp + 1;
           #3camtmp = #17camtmp;
     |SIGUE;

     |SI #17#4 = "1"; #3#21 = 3; |FINSI;
     |SI #17#4 = "2"; #3#21 = 4; |FINSI;
     |SI #17#4 = "3"; #3#21 = 5; |FINSI;
     |SI #17#4 = "4"; #3#21 = 6; |FINSI;
     |SI #17#4 = "5"; #3#21 = 7; |FINSI;
     |SI #17#4 = "6"; #3#21 = 0; |FINSI;
|FPRC;

|PROCESO Carga05;  ||  Carga en temporal Deducciones Creacion empleo.
     #3#0 = #18#0;  ||  Codigo de empresa.
     #3#1 = #18#1;  ||  Numero de linea.
     #3#2 = #18#2;  ||  Tipo de declaracion.
     #3#3 = #18#3;  ||  Perceptor.
     #3#4 = "";     ||  Tipo de deduccion.
     #3#5 = #18#4;  ||  A¤o.
     #3#6 = 0;      ||  % Limite;
     #3#7 = #18#5;  ||  Descripcion.
     #3#8 = 0;      ||  % Deduccion.
     |PARA camtmp = 9; |SI camtmp [ 20; |HACIENDO camtmp = camtmp + 1;
           camred   = camtmp - 3;
           #3camtmp = #18camred;
     |SIGUE;
     #3#21 = orden_rededu;
|FPRC;

|PROCESO GrabaTempo;
     |DDEFECTO #3;
     |SI orden_rededu = 1; |HAZ Carga01; |FINSI;
     |SI orden_rededu = 2; |HAZ Carga03; |FINSI;
     |SI orden_rededu = 8; |HAZ Carga02; |FINSI;
     |SI orden_rededu = 9; |HAZ Carga05; |FINSI;
     |SI orden_rededu = 3; |HAZ Carga04; |FINSI;
     |GRABA 020#3n;

     |SI #3#20 = "N"; |Y #3#11 > 0;
         #3#10 = #3#11;
         #3#11 = 0;
         #3#20 = "S";
         |GRABA 020#3n;
     |FINSI;
|FPRC;

|DEFBUCLE rededu05;
     #18#1;
     2, 4;
     #1#0, 01, tipdec_ini, any_min;
     #1#0, 99, tipdec_fin, any_max;
     ;
     NULL, GrabaTempo;
|FIN;

|DEFBUCLE rededu04;
     #17#1;
     2, 5;
     #1#0, 01, tipdec_ini, any_min;
     #1#0, 99, tipdec_fin, any_max;
     ;
     NULL, GrabaTempo;
|FIN;

|DEFBUCLE rededu03;
     #16#1;
     2;
     #1#0, 01, tipdec_ini;
     #1#0, 99, tipdec_fin;
     ;
     NULL, GrabaTempo;
|FIN;

|DEFBUCLE rededu02;
     #15#1;
     2;
     #1#0, 01, tipdec_ini;
     #1#0, 99, tipdec_fin;
     ;
     NULL, GrabaTempo;
|FIN;

|DEFBUCLE rededu01;
     #14#1;
     2,4;
     #1#0, 01, tipdec_ini, any_min;
     #1#0, 99, tipdec_fin, any_max;
     ;
     NULL, GrabaTempo;
|FIN;

|PROCESO Calcula;
     IpDedu = pDedu1 <-;
     |PARA nCampos1 = 2;  |SI nCampos1 [ 70;  |HACIENDO nCampos1 = nCampos1 + 2;
           nCampos2 = nCampos1 + 1;
           pDedu    = #22nCampos1 - #22nCampos2;
           |SI pDedu < 0;  pDedu = 0;  |FINSI;
           IpDedu = IpDedu + 1;
     |SIGUE;
|FPRC;

|PROCESO Deduce;
     |SI #22#1 ! "A"; |O #1#35 = "X";
         |HAZ Calcula;
         |ABRE #3;
         |SI #1#35 = " ";
             tipdec_ini = #22#1;
             tipdec_fin = #22#1;
         |FINSI;
         orden_rededu = 1; |BUCLE rededu01;
         orden_rededu = 2; |BUCLE rededu03;
         orden_rededu = 8; |BUCLE rededu02;
         orden_rededu = 9; |BUCLE rededu05;
         orden_rededu = 3; |BUCLE rededu04;
         |CIERRA #3;

         |ABRE #4;
         |ABRE #6;
         |ABRE #7;
         |ABRE #8;
         |HAZ BorraVar;
         |BUCLE repasde1;
         |CIERRA #4;
         |CIERRA #6;
         |CIERRA #7;
         |CIERRA #8;
     |FINSI;
|FPRC;

|DEFBUCLE retotreb;
     #22#1;
     ;
     #1#0, tipo_ini;
     #1#0, tipo_fin;
     ;
     NULL, Deduce;
|FIN;

|PROCESO TipoDecl;
     #1#0 = #10#0;
     |LEE 040#1=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     tipo_ini = "1";
     tipo_fin = "T";

     |SI #1#35 = "X";
         tipo_ini   = "A";
         tipo_fin   = "A";
         tipdec_ini = "1";
         tipdec_fin = "T";
     |FINSI;

     #9#0 = #1#0;
     |LIBERA #9;
     |LEE 101#9=;
     |SI FSalida ! 0;
         |DDEFECTO #9;
         #9#0 = #1#0;
         |GRABA 020#9b;
     |FINSI;

     |BUCLE retotreb;

     |GRABA 020#9a;
|FPRC;

|DEFBUCLE redatcon;
     #10#1;
     ;
     #0#0;
     #0#1;
     ;
     NULL, TipoDecl;
|FIN;

|PROCESO final; |TIPO 3;
     |SI #0#3 = "N"; |ACABA; |FINSI;

     |HAZ cambia_paths;
     |HAZ fichero;
     |NOME_DAT #3 fichero;
     |ABRE #3; |CIERRA #3; |DELINDEX #3;

     |ABRE #9;
     |ABRE #1;
     |BUCLE redatcon;
     |CIERRA #1;
     |CIERRA #9;
|FPRC;
