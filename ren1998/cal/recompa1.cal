|FICHEROS;
   repatrim #2;
   repoblac #6;
   rew00001 #99;
|FIN;

|INCLUYE i_varcpa;

|| **************************************************************************
||                           PROCESOS IMPRESION
|| **************************************************************************

|PROCESO impre_01;
|SI swporta = 0; |HAZ cabecera; |FINSI;
|CIMPR 1;  |IMPRIME varalf;  |CIMPR 2;
linea = linea + 1; |HAZ linea;
|FPRC;

|| *************************************************************************
|| *********************** Del Capital inmobiliario ************************
|| *************************************************************************

|PROCESO carga_01_1;
conta = conta + 1;
|SI swrrr = 0;
    varalf = ""; |HAZ impre_01;
    varalf = "A) BIENES INMUEBLES DE NATURALEZA URBANA"; |HAZ impre_01;
    varalf = "=" * 122; |HAZ impre_01;
    varalf = "Clave    Tipo Referencia catastral                       Situacion ";
    varalf = varalf + "del inmueble                                            ";
    varalf = varalf + "Valor";
    |HAZ impre_01;
    varalf = "=" * 122; |HAZ impre_01;
    swrrr = 1;
|FINSI;
varalf = alfa1;
varalf = varalf + "        " + alfa4;
varalf = varalf + "    " + alfa2;
varalf = varalf + "                  " + alfa3;
varalf = varalf + "                       ";
canti   = canti % coefi;
importe = canti; |HAZ importe;
varalf = varalf + "     " + importe;
|HAZ impre_01;
total = total + canti;
|FPRC;

|PROCESO carga_01_2;
conta = conta + 1;

|SI swrrr = 0;
    varalf = ""; |HAZ impre_01;
    varalf = ""; |HAZ impre_01;
    varalf = "B) BIENES INMUEBLES DE NATURALEZA RUSTICA"; |HAZ impre_01;
    varalf = "=" * 122; |HAZ impre_01;
    varalf = "Clave   Referencia catastral      Municipio              ";
    varalf = varalf + "        Provincia           H.Regadio      H.Secano";
    varalf = varalf + "         Valor";
    |HAZ impre_01;
    varalf = "=" * 122; |HAZ impre_01;
    swrrr = 1;
|FINSI;
varalf = alfa1;
varalf = varalf + "       " + alfa4;
varalf = varalf + " " + alfa2;
varalf = varalf + "      " + alfa3;
importe = canti1; |HAZ importe;
varalf = varalf + " " + importe;
importe = canti2; |HAZ importe;
varalf = varalf + " " + importe;
canti   = canti % coefi;
importe = canti;                     |HAZ importe;
varalf = varalf + " " + importe;
|HAZ impre_01;
total = total + canti;
|FPRC;

|| *********************** De afectos a patrimonio inmuebles urbana *******

|PROCESO inmueblea;
#6#0 = #2#7;
#6#1 = #2#8;
|LEE 040#6=;
|SI FSalida ! 0; |DDEFECTO #6; |FINSI;

coefi = 100;
alfa1 = #2#15;
alfa4 = #2#18;
alfa2 = #2#16;
alfa3 = #2#5 + "  ";
canti = #2#9;

|SI #2#3 = "G";
    |SI #99#1 = "T"; coefi = 50; |HAZ carga_01_1; |ACABA; |FINSI;
    |SI #99#1 = "C"; coefi = 50; |HAZ carga_01_1; |ACABA; |FINSI;
|SINO;
    |SI #99#1 = #2#3; |HAZ carga_01_1; |FINSI;
|FINSI;
|FPRC;

|DEFBUCLE repatria;
#2#1;
;
#99#0, "A1", 001;
#99#0, "A1", 999;
e;
NULL, inmueblea;
|FIN;

|| *********************** De afectos a patrimonio inmuebles rustico *******

|PROCESO inmuebleb;
#6#0 = #2#7;
#6#1 = #2#8;
|LEE 040#6=;
|SI FSalida ! 0; |DDEFECTO #6; |FINSI;

alfa1  = #2#15;
alfa2  = #6#3;
alfa3  = #6#2;
alfa4  = #2#16;
canti1 = #2#11;
canti2 = #2#10;
canti  = #2#9;
coefi  = 100;

|SI #2#3 = "G";
    |SI #99#1 = "T"; coefi = 50; |HAZ carga_01_2; |ACABA; |FINSI;
    |SI #99#1 = "C"; coefi = 50; |HAZ carga_01_2; |ACABA; |FINSI;
|SINO;
    |SI #99#1 = #2#3; |HAZ carga_01_2; |FINSI;
|FINSI;
|FPRC;

|DEFBUCLE repatrib;
#2#1;
;
#99#0, "B1", 001;
#99#0, "B1", 999;
e;
NULL, inmuebleb;
|FIN;

|PROCESO recompa1;
|ABRE #6;
total = 0;
swrrr = 0;
conta = 0; |BUCLE repatria;

|SI swrrr = 1;
    varalf = " " * 108;
    varalf = varalf + "--------------"; |HAZ impre_01;
    importe = total;                     |HAZ importe;
    varalf = " " * 108;
    varalf = varalf + " " + importe;    |HAZ impre_01;
|FINSI;

total = 0;
swrrr = 0;
conta = 0; |BUCLE repatrib;

|SI swrrr = 1;
    varalf = " " * 108;
    varalf = varalf + "--------------"; |HAZ impre_01;
    importe = total;                     |HAZ importe;
    varalf = " " * 108;
    varalf = varalf + " " + importe;    |HAZ impre_01;
|FINSI;

|CIERRA #6;
|FPRC;
