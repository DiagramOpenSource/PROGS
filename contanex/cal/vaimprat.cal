|INCLUYE dscnex_v;
|FICHEROS;
   vaimprat #0;
   varatemp #1;
   varatios #2;
   varatem4 #4;

|FIN;

|VARIABLES;
   sw       = 0;
   swb      = 0;

   alfa1    = "";
   nume1    = 0;
   para1    = 0;
   para2    = 0;
   {13}mes  = "";

   informe = "";
   inform1 = "vaimpra1";
   inform2 = "vaimpra2";

   x = 0;
   y = 0;
   leidos = 0;

   any    = 0;
   fecinf = @;
   salta  = "";
   des_emp = 0;
   has_emp = 0;
   des_rat = 0;
   has_rat = 0;
|FIN;

|| ************************************************************************
||                        CALCULOS DE PANTALLA
|| *************************************************************************

|PROCESO mayor;
  alfa1 = #0Campo > " ";
  #0Campo = alfa1;
  |PINTA #0Campo;
|FPRC;

|PROCESO mayor1;
  |HAZ mayor;
  |SI #0Campo = "N";
      |PARA x = 6;  |SI x [ 15;  |HACIENDO x = x + 1;
            |CAMPO_MODIFICA #0x,1,"S";
      |SIGUE;
      |CAMPO_MODIFICA #0#4,1,"N";
      |CAMPO_MODIFICA #0#5,1,"N";
      #0#4 = 0; |PINTA #0#4;
      #0#5 = 0; |PINTA #0#5;
  |SINO;
      |PARA x = 6;  |SI x [ 15;  |HACIENDO x = x + 1;
            #0x = 0;
            |CAMPO_MODIFICA #0x,1,"N";
      |SIGUE;
      |CAMPO_MODIFICA #0#4,1,"S";
      |CAMPO_MODIFICA #0#5,1,"S";
  |FINSI;
|FPRC;

|PROCESO mayor2;
  |HAZ mayor;
  |SI #0Campo = "N";
      |PARA x = 29;  |SI x [ 38;  |HACIENDO x = x + 1;
            |CAMPO_MODIFICA #0x,1,"S";
      |SIGUE;
      |CAMPO_MODIFICA #0#27,1,"N";
      |CAMPO_MODIFICA #0#28,1,"N";
      #0#27 = 0; |PINTA #0#27;
      #0#28 = 0; |PINTA #0#28;
  |SINO;
      |PARA x = 29;  |SI x [ 38;  |HACIENDO x = x + 1;
            #0x = 0;
            |CAMPO_MODIFICA #0x,1,"N";
      |SIGUE;
      |CAMPO_MODIFICA #0#27,1,"S";
      |CAMPO_MODIFICA #0#28,1,"S";
  |FINSI;
|FPRC;

|CALCULO consulta; |TIPO 0;
  |SI FSalida ! 9;  |ACABA;  |FINSI;
  |SI dirfich0 = "*"; |ACABA; |FINSI;

  |SUB_EJECUTA_NP "vaconemp";
  |ERROR;
  |SI r_emp = 0; |PTEC 808; |ACABA; |FINSI;

  #0Campo = r_emp; |PINTA #0Campo;
|FCAL;

|| --------------- comprueba desde-hasta empresas ---------------------------
|PROCESO buscaem1;
  leidos = 1;
  |ERROR10;
|FPRC;

|DEFBUCLE buscaempre;
 #1#1;
 ;
 #0#4, #0#1, 0001;
 #0#5, #0#1, 9999;
 e;
 NULL,buscaem1;
|FIN;

|PROCESO deshas;
  |SI #0#5 < #0#4;
      |MENAV "      Error en Introduccion";
      |ERROR;
  |FINSI;

  |SI #0#4 = 0;  |Y #0#5 = 0;  |ACABA;  |FINSI;
  leidos = 0;
  |BUCLE buscaempre;
  |SI leidos = 0;
      |MENAV "    ATENCION SELECCION VACIA";
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO deshasr;
  |SI #0#28 < #0#27;
      |MENAV "      Error en Introduccion";
      |ERROR;
  |FINSI;
|FPRC;

|| ------------------------------------------------------------------------
|PROCESO nomodi;
 |CAMPO_MODIFICA #0Campo, 0,alfa1;
 |SI alfa1 = "N"; |ACABA; |FINSI;

 alfa1 = "S"; |SI #0Campo = 0; alfa1 = "N"; |FINSI;
 |PARA x = Campo + 1;  |SI x [ 15;  |HACIENDO x = x + 1;
       |CAMPO_MODIFICA #0x,1,alfa1;
       |SI alfa1 = "N"; y = x + 10;
           #0x = 0;  |PINTA #0x;
           #0y = ""; |PINTA #0y;
       |FINSI;
 |SIGUE;
|FPRC;

|PROCESO esbuena; || desde campo de empresas seleccion
  |SI FSalida = 2; |ACABA; |FINSI;
  |HAZ nomodi;
  |SI #0Campo = 0; |ACABA; |FINSI;
  y = Campo + 10;
  |SI dirfich0 ! "*";
    |ABRE #30;
    #30#2 = #0Campo;
    #30#3 = 0;
    |LEE 011#30];
    |SI FSalida ! 0;  |O #30#2 ! #0Campo;
        |MENAV "     NO Existe la empresa Contable ";
        |ERROR;
        |CIERRA #30;
        |ACABA;
    |FINSI;
    #0y = #30#1; |PINTA #0y;
    |CIERRA #30;
  |FINSI;

  |ABRE #1;
  #1#0 = #0Campo;
  #1#1 = #0#1;
  |LEE 011#1];
  |SI FSalida ! 0;  |O #1#0 ! #0Campo; |O #1#1 ! #0#1;
      |MENAV "     NO Existe calculado Ningun Ratio de esta Empresa Contable ";
      |ERROR;
  |SINO;
      #0y = #1#58; |PINTA #0y;
  |FINSI;
  |CIERRA #1;
|FPRC;

|PROCESO nomodir;
 |CAMPO_MODIFICA #0Campo, 0,alfa1;
 |SI alfa1 = "N"; |ACABA; |FINSI;

 alfa1 = "S"; |SI #0Campo = 0; alfa1 = "N"; |FINSI;
 |PARA x = Campo + 1;  |SI x [ 38;  |HACIENDO x = x + 1;
       |CAMPO_MODIFICA #0x,1,alfa1;
       |SI alfa1 = "N"; y = x + 10;
           #0x = 0;  |PINTA #0x;
           #0y = ""; |PINTA #0y;
       |FINSI;
 |SIGUE;
|FPRC;

|PROCESO esbueno; || desde campo de Ratio seleccion
  |SI FSalida = 2; |ACABA; |FINSI;
  |HAZ nomodir;
  |SI #0Campo = 0; |ACABA; |FINSI;
  y = Campo + 10;

  |ABRE #2;
  #2#0 = #0Campo;
  |LEE 011#2=;
  |SI FSalida ! 0;
      |MENAV "     NO Existe Ratio ";
      |ERROR;
  |SINO;
      #0y = #2#1;
      |PINTA #0y;
  |FINSI;
  |CIERRA #2;
|FPRC;

|| **********************************************************************
||                       CALCULOS DE IMPRESION
|| **********************************************************************
|PROCESO sel_emp;
 swb = 0;
 |PARA para1 = 6; |SI para1 [ 15; |HACIENDO para1 = para1 + 1;
       |SI #0para1 = #1#0;
           swb = 1;
           |SAL;
       |FINSI;
 |SIGUE;
|FPRC;

|PROCESO sel_rat;
 swb = 0;
 |PARA para1 = 29; |SI para1 [ 38; |HACIENDO para1 = para1 + 1;
       |SI #0para1 = #1#2;
           swb = 1;
           |SAL;
       |FINSI;
 |SIGUE;
|FPRC;

|PROCESO haztodo; || Proceso donde empieza a imprimir cada empresa
 swb = 1;
 |SI #0#3 = "N"; |HAZ sel_emp; |FINSI;
 |SI swb = 0; |ACABA; |FINSI;

 swb = 1;
 |SI #0#26 = "N"; |HAZ sel_rat; |FINSI;
 |SI swb = 0; |ACABA; |FINSI;

 |SI sw = 0;
     sw = 1;
     empresa = #1#0;
 |FINSI;

 |DDEFECTO #4;
 |HAZ meses;

 |SI empresa ! #1#0;
     |SI salta = "N"; |PIEINF; |FINSI;
     empresa = #1#0;
 |FINSI;

 |PRINT;
 |SI salta = "S"; |PIEINF; |FINSI;
|FPRC;

|DEFBUCLE varatimp0;
 #1#1;
 ;
 des_emp, any, des_rat;
 has_emp, any, has_rat;
 e;
 NULL, haztodo;
|FIN;

|| *********************************************************************
||                    COMIENZO con TIPO 3
|| *********************************************************************

|CALCULO imprime; |TIPO 3;

  any    = #0#1;
  fecinf = #0#0;
  salta  = #0#2;
  |IMPRE 0;
  |SI FSalida ! 0; |ACABA; |FINSI;

  |SI salta = "S"; informe = inform1; |FINSI;
  |SI salta = "N"; informe = inform2; |FINSI;
  |INFOR informe;
  |SI FSalida ! 0; |ACABA; |FINSI;

  des_emp = #0#4; has_emp = #0#5;
  |SI #0#3 = "N"; des_emp = 0; has_emp = 99999; |FINSI;

  des_rat = #0#27; has_rat = #0#28;
  |SI #0#26 = "N"; des_rat = 0; has_rat = 9999; |FINSI;

  |BUCLE varatimp0;
  |SI sw = 1; |Y salta = "N"; |PIEINF; |FINSI;

  |FININF;
  |FINIMP;
|FCAL;

|| *************************************************************************
|PROCESO meses;
 |DDEFECTO #4;
 |SI #1#57 = 1; |ACABA; |FINSI;

 Imes = mes1 <-;
 |PARA para1 = 1; |SI para1 [ 12; |HACIENDO para1 = para1 + 1;
       mes = #4para1;
       Imes = Imes + 1;
 |SIGUE;

 Imes = mes1 <-;
 para2 = 1 + (13 - #1#57);
 |PARA para1 = 1; |SI para1 [ 12; |HACIENDO para1 = para1 + 1;
       |SI para2 = 12; para2 = 1; |FINSI;
       #4para2 = mes;
       Imes = Imes + 1;
       para2 = para2 + 1;
 |SIGUE;
|FPRC;
