|INCLUYE dscnex_v;
|FICHEROS;
 vaesta02 #0;
 vagrempc #1;
 vagrempl #2;
 vamanpax #3;


 vadeparc #5;
 vadeparl #6;

 vatr0006 #11;  || Trabajo Acumulados Departamentos
 vatr0003 #12;  || Trabajo Total Pax

 vadetota #198;  || Grupos Totalizadores
 vatr0007 #199;  || Trabajo Grupos Totalizadores
|FIN;

|VARIABLES;
  nempresa = 0;
  informe  = "vaes0202";
  alfa1    = "";
  alfa2    = "";
  alfa3    = "";
  alfa4    = "";
  alfa5    = "";
  nume1    = 0;
  nume2    = 0;
  nume3    = 0;
  nume4    = 0;
  para1    = 0;
  para2    = 0;
  para3    = 0;
  para4    = 0;
  fech1    = @;

  mes      = 0;
  letrames = "";

  agrup  = 0;
  any   = 0;
  &any1  = 0;
  &any2  = 0;
  d_dep  = 0;
  h_dep  = 0;
  t_anu  = "";
  s      = 0;

  d_digi = 0;
  h_digi = 0;
  d_cuen = "";
  h_cuen = "";

  sw        = 0;
  &swalgun  = 0;
  swAgrup   = 0;
  swEmpre   = 0;

  saldo     = 0;
  &mpax1    = 0;
  &mpax2    = 0;
  &pordif   = 0;
  &tantpor  = 0;
  &t1       = 0;
  &t2       = 0;
  &t3       = 0;
  &t4       = 0;
  &t5       = 0;
  &t6       = 0;
  &fecha    = "";
  &titulo   = "";
  &elmesh   = "";
  &swinf    = 0;
|FIN;

|INCLUYE i_ficher;
|INCLUYE litmes_v;

|| *************************************************************************
||                         CALCULOS DE PANTALLA
|| *************************************************************************
|CALCULO mayor; |TIPO 0;
 alfa1 = #0Campo > " ";
 #0Campo = alfa1;
 |PINTA #0Campo;
|FCAL;

|PROCESO tipo8; |TIPO 8;
 |HAZ vbase;
 |SI dirfich0 = "*";
     |MENAV "    NO EXISTE PROGRAMA DE CONTABILIDAD";
     |PTEC 802;
     |PTEC 815;
 |FINSI;
|FPRC;

|CALCULO defec; |TIPO 7;
 |SI dirfich0 = "*"; |ACABA; |FINSI;
 fech1 = ~;
 alfa1 = fech1 % -104;
 #0#1 = alfa1;    |PINTA #0#1;
 #0#4 = #0#1 - 1; |PINTA #0#4;
 alfa2 = fech1 % 402;
 #0#5  = alfa2;   |PINTA #0#5;
 mes = #0#5;
 |HAZ mesletra;
 #0#11 = letrames; |PINTA #0#11;
|FCAL;

|PROCESO launo;
 #30#2 = #2#2;
 #30#3 = 0;
 |LEE 011#30];
 |SI FSalida = 0;  |Y #30#2 = #2#2;
     elinim = #30#11;
     |ERROR10;
 |FINSI;
|FPRC;

|CALCULO leeagrup; |TIPO 6;
 |HAZ lect_agrup;
 |ATRI I; |PINTA 0826, #1#1; |ATRI 0;
 |SI swAgrup = 1;
     |MENAV "    Error. NO EXISTE AGRUPACION !";
     |ERROR;
     |ACABA;
 |FINSI;
 |CIERRA #1;
 elinim = 1;
 |ABRE #30;
 |BUCLELIN 2launo#1;
 |CIERRA #30;
|FCAL;

|PROCESO lect_agrup;    || Lee la Agrupacion
 swAgrup = 0;
 |ABRE #1;
 |DDEFECTO #1;
 #1#0 = #0#0;
 |LEE 001#1=;
 |SI FSalida ! 0;
     swAgrup = 1;
 |FINSI;
 |CIERRA #1;
|FPRC;

|CALCULO pintames; |TIPO 0;
  |SI dirfich0 = "*"; |ACABA; |FINSI;
  |SI #0Campo ! 0; |Y #0Campo ! 13;
      mes = elinim + (#0Campo - 1);   || Operacion para saber que mes es
  |SINO;
      mes = #0Campo;
  |FINSI;
  |HAZ mesletra;
  nume1 = Campo + 6;
  #0nume1 = letrames;
  |PINTA #0nume1;
|FCAL;

|CALCULO mesletra;
  |SI mes = 0;  letrames = "Apertura  "; |FINSI;
  |SI mes = 1;  letrames = "Enero     "; |FINSI;
  |SI mes = 2;  letrames = "Febrero   "; |FINSI;
  |SI mes = 3;  letrames = "Marzo     "; |FINSI;
  |SI mes = 4;  letrames = "Abril     "; |FINSI;
  |SI mes = 5;  letrames = "Mayo      "; |FINSI;
  |SI mes = 6;  letrames = "Junio     "; |FINSI;
  |SI mes = 7;  letrames = "Julio     "; |FINSI;
  |SI mes = 8;  letrames = "Agosto    "; |FINSI;
  |SI mes = 9;  letrames = "Septiembre"; |FINSI;
  |SI mes = 10; letrames = "Octubre   "; |FINSI;
  |SI mes = 11; letrames = "Noviembre "; |FINSI;
  |SI mes = 12; letrames = "Diciembre "; |FINSI;
  |SI mes = 13; letrames = "Cierre    "; |FINSI;
|FCAL;

|CALCULO hasdep; |TIPO 0;
 |SI #0#3 < #0#2;
     |MENAV "    Error de Entrada ...";
     |ERROR;
 |FINSI;
|FCAL;

|PROCESO alfinal; |TIPO 7;
 alfa1 = "ESTADISTICA DEL A¥O ";
 alfa3 = #0#1; alfa3 = alfa3 % -102;
 #0#6 = alfa1 + "-" + alfa3;
 |PINTA #0#6;
 |PTEC 816;
|FPRC;

|| **************************************************************************
|| **************************************************************************
|| ********* CALCULOS PARA CREAR LOS FICHEROS AUXILIARES ********************

|| ............. Fichero de PAX
|PROCESO carga_pax;  || Lee las empresa de la Agrupacion
 swEmpre = 1;
 #3#0 = #2#2;
 #3#1 = any1;
 |LEE 001#3=;
 |SI FSalida = 0;
     any = any1;
     |HAZ graba_pax;
 |FINSI;
 #3#0 = #2#2;
 #3#1 = any2;
 |LEE 001#3=;
 |SI FSalida = 0;
     any = any2;
     |HAZ graba_pax;
 |FINSI;
|FPRC;

|PROCESO graba_pax;
 #12#0  = agrup;  || Agrupacion
 #12#26 = any;    || Any
 |LEE 101#12=;
 |SI FSalida ! 0;
     |DDEFECTO #12;
     #12#0  = agrup;  || Agrupacion
     #12#26 = any;    || Departamento
     |HAZ l_meses;    || Literal meses
     |GRABA 020#12b;
 |FINSI;
 |SI d_mes [  1;  |Y h_mes ]  1;  #12#13 = #12#13 + #3#2;  |FINSI;
 |SI d_mes [  2;  |Y h_mes ]  2;  #12#14 = #12#14 + #3#3;  |FINSI;
 |SI d_mes [  3;  |Y h_mes ]  3;  #12#15 = #12#15 + #3#4;  |FINSI;
 |SI d_mes [  4;  |Y h_mes ]  4;  #12#16 = #12#16 + #3#5;  |FINSI;
 |SI d_mes [  5;  |Y h_mes ]  5;  #12#17 = #12#17 + #3#6;  |FINSI;
 |SI d_mes [  6;  |Y h_mes ]  6;  #12#18 = #12#18 + #3#7;  |FINSI;
 |SI d_mes [  7;  |Y h_mes ]  7;  #12#19 = #12#19 + #3#8;  |FINSI;
 |SI d_mes [  8;  |Y h_mes ]  8;  #12#20 = #12#20 + #3#9;  |FINSI;
 |SI d_mes [  9;  |Y h_mes ]  9;  #12#21 = #12#21 + #3#10; |FINSI;
 |SI d_mes [ 10;  |Y h_mes ] 10;  #12#22 = #12#22 + #3#11; |FINSI;
 |SI d_mes [ 11;  |Y h_mes ] 11;  #12#23 = #12#23 + #3#12; |FINSI;
 |SI d_mes [ 12;  |Y h_mes ] 12;  #12#24 = #12#24 + #3#13; |FINSI;

 #12#25 = #12#13 + #12#14 + #12#15 + #12#16 + #12#17;
 #12#25 = #12#25 + #12#18 + #12#19 + #12#20 + #12#21;
 #12#25 = #12#25 + #12#22 + #12#23 + #12#24;

 |GRABA 020#12a;
 |LIBERA #12;
 sw = 1;
|FPRC;

|| ...............................................................
|PROCESO leedepar;
 |ABRE #5;
 |DDEFECTO #5;
 #5#0 = #6#0;
 |LEE 000#5=;
 |CIERRA #5;
|FPRC;

|| ............. Fichero de Departamentos / Cuentas
|PROCESO grabar;
 #11#0 = agrup;  || Agrupacion
 #11#1 = #6#0;   || Departamento
 |LEE 101#11=;
 |SI FSalida ! 0;
     |DDEFECTO #11;
     #11#0  = agrup;  || Agrupacion
     #11#1  = #6#0;   || Departamento
     |HAZ leedepar;
     #11#2  = #5#1;   || Descripcion
     #11#6  = any1;   || 1§ A¤o
     #11#10 = any2;   || 2§ A¤o
     |GRABA 020#11b;
 |FINSI;
 s = 1; |SI #6#5 = "H"; s = -1; |FINSI;
 saldo = 0;
 |SI d_mes [  1;  |Y h_mes ]  1;
     saldo = saldo + ((((s * #cacuenta#11) + (s * #cacuenta#14)) / enConversor) ! EURODBMOD);
 |FINSI;

 |SI d_mes [  2;  |Y h_mes ]  2;
     saldo = saldo + (((s * #cacuenta#17) / enConversor) ! EURODBMOD);
 |FINSI;

 |SI d_mes [  3;  |Y h_mes ]  3;
     saldo = saldo + (((s * #cacuenta#20) / enConversor) ! EURODBMOD);
 |FINSI;

 |SI d_mes [  4;  |Y h_mes ]  4;
     saldo = saldo + (((s * #cacuenta#23) / enConversor) ! EURODBMOD);
 |FINSI;

 |SI d_mes [  5;  |Y h_mes ]  5;
     saldo = saldo + (((s * #cacuenta#26) / enConversor) ! EURODBMOD);
 |FINSI;

 |SI d_mes [  6;  |Y h_mes ]  6;
     saldo = saldo + (((s * #cacuenta#29) / enConversor) ! EURODBMOD);
 |FINSI;

 |SI d_mes [  7;  |Y h_mes ]  7;
     saldo = saldo + (((s * #cacuenta#32) / enConversor) ! EURODBMOD);
 |FINSI;

 |SI d_mes [  8;  |Y h_mes ]  8;
     saldo = saldo + (((s * #cacuenta#35) / enConversor) ! EURODBMOD);
 |FINSI;

 |SI d_mes [  9;  |Y h_mes ]  9;
     saldo = saldo + (((s * #cacuenta#38) / enConversor) ! EURODBMOD);
 |FINSI;

 |SI d_mes [ 10;  |Y h_mes ] 10;
     saldo = saldo + (((s * #cacuenta#41) / enConversor) ! EURODBMOD);
 |FINSI;

 |SI d_mes [ 11;  |Y h_mes ] 11;
     saldo = saldo + (((s * #cacuenta#44) / enConversor) ! EURODBMOD);
 |FINSI;

 |SI d_mes [ 12;  |Y h_mes ] 12;
     saldo = saldo + (((s * #cacuenta#47) / enConversor) ! EURODBMOD);
 |FINSI;

 nume1 = 3; |SI any = any2; nume1 = 7; |FINSI;
 nume2 = nume1 + 1;
 nume3 = nume1 + 2;

 |SI #6#4 = "G"; #11nume1 = #11nume1 + saldo; |FINSI;
 |SI #6#4 = "I"; #11nume2 = #11nume2 + saldo; |FINSI;
 #11nume3 = #11nume2 - #11nume1;

 |GRABA 020#11a;
 |LIBERA #11;
 sw = 1;
|FPRC;

|DEFBUCLE vapascon0;
 #cacuenta#1;
 3;
 d_cuen, "S";
 h_cuen, "S";
 e;
 NULL, grabar;
|FIN;

|PROCESO lee_depar;
 d_cuen = #6#2; |QBF d_cuen;
 h_cuen = (d_cuen + "zzzzzzzzzzzz") % 112;
 d_digi = 0; h_digi = 9;
 |BUCLE vapascon0;
|FPRC;

|DEFBUCLE leedepar;
 #6#1;
 ;
 d_dep, 000;
 h_dep, 999;
 e;
 NULL, lee_depar;
|FIN;
|| ...............................................................
|PROCESO encuentra_emp;
 alfa1 = ("00" + #30#26) % -102;
 alfa2 = any1; alfa2 = alfa2 % -102;
 alfa5 = any2; alfa5 = alfa5 % -102;

 |SI alfa1 = alfa2; |O alfa1 = alfa5;

     alfa3 = #30#2; alfa3 = "00000" + alfa3; alfa3 = alfa3 % -105;
     alfa4 = #30#3; alfa4 = "0"     + alfa4; alfa4 = alfa4 % -101;
     dirfich = dirfich0 + alfa3 + alfa4 + "/";

     |SI alfa1 = alfa2; any = any1; |FINSI;
     |SI alfa1 = alfa5; any = any2; |FINSI;

     enEmpresa  = #30#2;
     enPerConta = #30#3;
     |HAZ EnConversor;

     |PATH_DAT #20 dirfich;

     |BUCLE leedepar;
     swEmpre = 1;
 |FINSI;
|FPRC;

|DEFBUCLE verempresa;
 #30#1;
 ;
 nempresa, 0;
 nempresa, 9;
 e;
 NULL, encuentra_emp;
|FIN;

|PROCESO carga_cta;  || Lee las empresa de la Agrupacion
 swEmpre = 0;
 nempresa = #2#2;
 |BUCLE verempresa;
|FPRC;
|| ...............................................................
|| ...............................................................

|| ********* CALCULOS PARA LANZAR LISTADO ***********************************
|PROCESO totgrupo1;
 pordif  = #199#4 - #199#7; || Diferencia saldos totales 1§a¤o-2§a¤o
 tantpor = 0;
 |SI #199#7 ! 0;
     tantpor = (pordif / #199#7) * 100;
 |FINSI;
 swinf = 3;
 |PRINT;
|FPRC;

|DEFBUCLE totgrupo;
 #199#1;
 ;
  0, ".";
 99, ".";
 ;
 NULL, totgrupo1;
|FIN;

|PROCESO grupo;
|DDEFECTO #5;
    #5#0 = #11#1;
|LEE 000#5=;

|DDEFECTO #198;
    #198#0 = #5#3;
|LEE 000#198=;

|DDEFECTO #199;
    #199#0 = #5#3;
    #199#16 = ".";
|LEE 101#199=;
|SI FSalida ! 0;
        #199#1 = #198#1; || descripcion
    |GRABA 020#199b;
|FINSI;
    #199#2 = #199#2 + #11#3;       || gastos   1 a¤o
    #199#3 = #199#3 + #11#4;       || ingresos 1 a¤o
    #199#4 = #199#4 + #11#5;       || saldo    1 a¤o
    #199#5 = #199#5 + #11#7;       || gastos   2 a¤o
    #199#6 = #199#6 + #11#8;       || ingresos 2 a¤o
    #199#7 = #199#7 + #11#9;       || saldo    2 a¤o
|GRABA 020#199a;
|LIBERA #199;
|FPRC;

|PROCESO total;   || General
 pordif  = t3 - t6; || Diferencia saldos totales 1§a¤o-2§a¤o
 tantpor = 0;
 |SI t6 ! 0;
     tantpor = (pordif / t6) * 100;
 |FINSI;
 |PIEINF;
|FPRC;

|PROCESO imprime;
 sw = 1;
 pordif  = #11#5 - #11#9; || Diferencia saldos 1§a¤o-2§a¤o
 tantpor = 0;
 |SI #11#9 ! 0;
     tantpor = (pordif / #11#9) * 100;
 |FINSI;
 swinf = 0;
 |PRINT;
 t1 = t1 + #11#3; || Total gastos    1§ a¤o.
 t2 = t2 + #11#4; || Total ingresos  1§ a¤o.
 t3 = t3 + #11#5; || Total saldo     1§ a¤o.
 t4 = t4 + #11#7; || Total gastos    2§ a¤o.
 t5 = t5 + #11#8; || Total ingresos  2§ a¤o.
 t6 = t6 + #11#9; || Total saldo     2§ a¤o.
 swalgun = 1;
 |HAZ grupo;
|FPRC;

|DEFBUCLE lregistros;
 #11#1;
 ;
 agrup, d_dep;
 agrup, h_dep;
 e;
 NULL, imprime;
|FIN;

|| **************************************************************************
|| ********* PROCESO PARA CREAR INFORME *************************************
|| **************************************************************************
|PROCESO elproceso; |TIPO 3
  |SI dirfich0 = "*"; |ACABA; |FINSI;

  |IMPRE 0;
  |SI FSalida ! 0; |ACABA; |FINSI;

  |INFOR informe;
  |SI FSalida ! 0;
      |FINIMP;
      |ACABA;
  |FINSI;

  |HAZ lect_agrup;
  |SI swAgrup = 1;
      |MENAV "    ERROR. NO EXISTE AGRUPACION! ";
  |FINSI;

  titulo = #0#6;
  alfa1  = #0#7;
  fecha  = (alfa1 % 102) + #1#2 + (alfa1 % 402) + #1#3 + (alfa1 % -104);

  agrup  = #0#0;
  any1   = #0#1;
  any2   = #0#4;
  d_dep  = #0#2;
  h_dep  = #0#3;
  d_mes  = 1;
  h_mes  = #0#5;
  elmesh = #0#11;

  || ........................... Preparando Ficheros Auxiliares
  |D_CUADRO 2134, 2355;
  |ATRI P;
  |PINTA 2235, "     <TEMPORAL>     ";
  |ATRI 0;

  |HAZ fichero;
  alfa1 = ("c" + fichero) % 108;
  alfa2 = ("d" + fichero) % 108;
  alfa3 = ("e" + fichero) % 108;
  |NOME_DAT #11 alfa1;
  |NOME_DAT #12 alfa2;
  |NOME_DAT #199 alfa3;

  |DELINDEX #11;
  |DELINDEX #12;
  |DELINDEX #199;

  sw = 0;
  |ABRE #11;
  |BUCLELIN 2carga_cta#1;
  |CIERRA #11;
  |SI sw = 0;
      |MENAV "    NO EXISTEN DATOS EN LAS EMPRESAS DE LA AGRUPACION ";
      |DELINDEX #11;
      |DELINDEX #12;
      |DELINDEX #199;
      |ACABA;
  |FINSI;
  |ABRE #3;
  |ABRE #12;
  |BUCLELIN 2carga_pax#1;
  |CIERRA #3;
  |CIERRA #12;
|| *******************************************************************
  |ATRI P;
  |PINTA 2235, " <LANZANDO INFORME> ";
  |ATRI 0;

 |ABRE #12;       || Lee la agrupacion/a¤o para Pax
 |DDEFECTO #12;
 #12#0  = agrup;  || Agrupacion
 #12#26 = any2;    || Any
 |LEE 000#12=;
 mpax2 = #12#25;

 |DDEFECTO #12;
 #12#0  = agrup;  || Agrupacion
 #12#26 = any1;    || Any
 |LEE 000#12=;
 mpax1 = #12#25;
 |CIERRA #12;


  d_cuen = "            ";
  h_cuen = "zzzzzzzzzzzz";
  sw = 0;
  |ABRE #5;
  |ABRE #198;
  |ABRE #199;
  |BUCLE lregistros;
  |CIERRA #5;
  |CIERRA #198;
  |CIERRA #199;
  |SI sw = 1;
          swinf = 2;     |PRINT;
      |BUCLE totgrupo;
          swinf = 2;     |PRINT;
      |HAZ total;
  |FINSI;

  |FININF;
  |FINIMP;
  |DELINDEX #11;
  |DELINDEX #12;
  |DELINDEX #199;
|FPRC;
