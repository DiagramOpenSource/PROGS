|FICHEROS;
  vapascon #0;
  vadacoca #1;
  vadacoli #2;

  vaplanca #5;
  vaplanli #6;
  vaaccbal #7;
  va8planc #15;
  va8planl #16;
  va8accba #17;

  vawb0003 #499;
|FIN;

|VARIABLES;
  alfa1    = "";
  alfa2    = "";

  blancos  = "                                        ";
  nume1    = 0;
  altamod  = 0;
  mes      = 0;
  letrames = "";


  cue_perdi = "";
  dig_perdi = 0;
  sw_error = 0;

  &act_ant_con = "";

  fondo = " ";
  linea = 0;
  d_digi = 0;
  h_digi = 0;
  d_cuen = "";
  h_cuen = "";
  eldesde = 0;
  elhasta = 0;
  des_exi = 0;
  has_exi = 0;
  sw_exp = 0;

  activo    = 0;
  pasivo    = 0;
  beneficio = 0;
  f_saldo   = 0;
  i_saldo   = 0;
  debe      = 0;
  haber     = 0;
  saldo     = 0;

  descar = 0;
  act_cuent = "";
  act_nivel = 0;
  x = 0;
  y = 0;

  aAlfa1   = "";
  aCuenta  = "";
  aNombre  = "";
  nImporte = 0;
  aFichero = "";
  aSigno   = "";
|FIN;

|INCLUYE dscnex_v;

|| *************************************************************************
||                         CALCULOS DE PANTALLA
|| *************************************************************************

|PROCESO tipo8; |TIPO 8;
 |HAZ vbase;
 |SI dirfich0 = "*";
     |MENAV "    NO EXISTE PROGRAMA DE CONTABILIDAD";
     |PTEC 802;
     |PTEC 815;
 |FINSI;
|FPRC;

|PROCESO mayor; |TIPO 0;
 alfa1 = #0Campo > " ";
 #0Campo = alfa1;
 |PINTA #0Campo;
|FPRC;

|CALCULO con_empr; |TIPO 0;
  |SI FSalida ! 9;  |ACABA;  |FINSI;

  |SUB_EJECUTA_NP "vaconemp";
  |ERROR;
  |SI r_emp = 0; |PTEC 808; |ACABA; |FINSI;

  #0#0 = r_emp; |PINTA #0#0;
  #0#1 = r_per; |PINTA #0#1;
  #0#2 = r_eje; |PINTA #0#2;
||  #0#3 = r_nom; |PINTA #0#3;
  |PTEC 802;
|FCAL;

|PROCESO leeregi; |TIPO 0;
 |SI dirfich0 = "*"; |ACABA; |FINSI;
 |SI FSalida = 2; |ACABA; |FINSI;
 cod1 = #0#0;
 cod2 = #0#1;
 |HAZ leelo;
 |SI swerror = 1;
     |MENAV "     NO Existe la Empresa Contable";
     |ERROR;
     |ACABA;
 |FINSI;
  |SI #30#26 < 80;
      #0#2 = 2000 + #30#26;
  |SINO;
      #0#2 = 1900 + #30#26;
  |FINSI;
 |PINTA #0#2;
 #0#3 = #30#1; |PINTA #0#3;
|FPRC;

|PROCESO def_reg; |TIPO 7;
 |SI dirfich0 = "*"; |ACABA; |FINSI;
 |SI FSalida = 2; |ACABA; |FINSI;

  nume1 = 0;
  |SI #0#16 = 2;
      |ABRE #7;
      #7#0 = #0#0;
      #7#1 = #0#2;
      #7#2 = 99;
      |LEE 040#7];
      |SI FSalida = 0;
          |LEE 040#7a;
      |SINO;
          |LEE 040#7u;
      |FINSI;
      |SI FSalida = 0; |Y #7#0 = #0#0; |Y #7#1 = #0#2;
          nume1 = #7#2;
      |FINSI;
      |CIERRA #7;
  |SINO;
      |ABRE #17;
      #17#0 = #0#0;
      #17#1 = #0#2;
      #17#2 = 99;
      |LEE 040#17];
      |SI FSalida = 0;
          |LEE 040#17a;
      |SINO;
          |LEE 040#17u;
      |FINSI;
      |SI FSalida = 0; |Y #17#0 = #0#0; |Y #17#1 = #0#2;
          nume1 = #17#2;
      |FINSI;
      |CIERRA #17;
  |FINSI;
  nume1 = nume1 + 1;

/*
  |LEE 001#7=;
  |PARA nume1 = 99;  |SI nume1 ] 1;  |HACIENDO nume1 = nume1 - 1;
        #7#0 = #0#0;
        #7#1 = #0#2;
        #7#2 = nume1;
        |LIBERA #7;
        |LEE 001#7=;
        |SI FSalida = 0; |SAL; |FINSI;
  |SIGUE;
*/

  #0#4 = nume1;
  |PINTA #0#4;
|FPRC;

|PROCESO lee_reg; |TIPO 0;
  |SI dirfich0 = "*"; |ACABA; |FINSI;
  |SI FSalida = 2; |ACABA; |FINSI;

  altamod = 0;
  |SI #0#16 = 2;
    |ABRE #7;
    |DDEFECTO #7;
    #7#0 = #0#0;
    #7#1 = #0#2;
    #7#2 = #0#4;
    |LEE 001#7=;
    |SI FSalida = 0;
        altamod = 1;
        #0#5 = #7#3; |PINTA #0#5; |PTEC 802;
    |FINSI;
    |CIERRA #7;
  |SINO;
    |ABRE #17;
    |DDEFECTO #17;
    #17#0 = #0#0;
    #17#1 = #0#2;
    #17#2 = #0#4;
    |LEE 001#17=;
    |SI FSalida = 0;
        altamod = 1;
        #0#5 = #17#3; |PINTA #0#5; |PTEC 802;
    |FINSI;
    |CIERRA #17;
  |FINSI;
|FPRC;

|PROCESO cuadro; |TIPO 7;
 |SI dirfich0 = "*"; |ACABA; |FINSI;
 |PUSHV 1401 2380;
 |SI altamod = 1;
     |PINTA 1448, " MODIFICACION ";
     |CUADRO 1424 2079;
     |ATRI R;
     |PINTA 1525, " (S) Deja Datos ACTUALES igual a Datos CONTABILIDAD   ";
     |PINTA 1625, " (N) Pone a Cero Datos ACTUALES                       ";
     |PINTA 1725, " (D) NO modifica Datos ACTUALES                       ";
     |PINTA 1825, "------------------------------------------------------";
     |PINTA 1925, " (S) o (N) Pasa Datos ACTUALES validados a ANTERIORES ";
     |ATRI r;
  |SINO;
     |PINTA 1448, " ALTA ";
     |CUADRO 1426 1779;
     |ATRI R;
     |PINTA 1527, " (S) Deja Datos ACTUALES igual a Datos CONTABILIDAD ";
     |PINTA 1627, " (N) o (D) Deja Datos ACTUALES a Cero               ";
     |ATRI r;
  |FINSI;
|FPRC;

|PROCESO repon; |TIPO 0;
 |PINTA 1448, "컴컴컴컴컴컴컴컴컴컴";
 |POPV;
|FPRC;

|PROCESO forzar;
 |SI #0#14 = "A";
     #0#15 = "A";
     |CAMPO_MODIFICA #0#15, 1, "N";
 |SINO;
     |CAMPO_MODIFICA #0#15, 1, "S";
 |FINSI;
|PINTA #0#15;
|FPRC;

|PROCESO cuadr; |TIPO 7;
 |SI #0#14 = "A"; |ACABA; |FINSI;
 |PUSHV 0501 2380;
 |CUADRO 1426 1979;
 |ATRI R;
 |PINTA 1527, " <A> Forzar a Abreviado                             ";
 |PINTA 1627, " <E> Forzar a Extendido                             ";
 |PINTA 1727, " <B> Balance Abreviado, Perd. y Ganancias Extendido ";
 |PINTA 1827, " <P> Balance Extendido, Perd. y Ganancias Abreviado ";
 |ATRI r;
|FPRC;


|PROCESO planilla;
 |SI dirfich0 = "*"; |ACABA; |FINSI;
 |SI FSalida = 2; |ACABA; |FINSI;

 |SI #0#16 = 2;
    |ABRE #5;
    #5#0 = #0Campo;
    |LEE 040#5=;
    |SI FSalida ! 0;
        |PINTA 1531, blancos;
        |MENAV "      No existe Planilla.";
        |ERROR;
    |SINO;
        |ATRI I; |PINTA 1531, #5#1; |ATRI 0;
    |FINSI;
    |CIERRA #5;
 |SINO;
    |ABRE #15;
    #15#0 = #0Campo;
    |LEE 040#15=;
    |SI FSalida ! 0;
        |PINTA 1531, blancos;
        |MENAV "      No existe Planilla.";
        |ERROR;
    |SINO;
        |ATRI I; |PINTA 1531, #15#1; |ATRI 0;
    |FINSI;
    |CIERRA #15;
 |FINSI;
|FPRC;

|CALCULO pintames; |TIPO 0;
 |SI dirfich0 = "*"; |ACABA; |FINSI;
  |SI #0Campo ! 0; |Y #0Campo ! 13;
      mes = #30#11 + (#0Campo - 1);   || Operacion para saber que mes es
  |SINO;
      mes = #0Campo;
  |FINSI;
  |HAZ mesletra;
  |ATRI I;
  |SI Campo =  9;  |PINTA 1825, letrames;  |FINSI;
  |SI Campo = 10;  |PINTA 1856, letrames;  |FINSI;
  |SI Campo = 12;  |PINTA 2025, letrames;  |FINSI;
  |SI Campo = 13;  |PINTA 2056, letrames;  |FINSI;
  |ATRI 0;
  |SI Campo = 10;  |Y #0#9  > #0#10; |MENAV "    Error de Entrada ..."; |ERROR; |FINSI;
  |SI Campo = 13;  |Y #0#12 > #0#13; |MENAV "    Error de Entrada ..."; |ERROR; |FINSI;
|FCAL;

|CALCULO mesletra;
  |SI mes = 0;  letrames = "Apertura  "; |FINSI;
  |SI mes = 1;  letrames = "Enero     "; |FINSI;
  |SI mes = 2;  letrames = "Febrero   "; |FINSI;
  |SI mes = 3;  letrames = "Marzo     "; |FINSI;
  |SI mes = 4;  letrames = "Abril     "; |FINSI;
  |SI mes = 5;  letrames = "Mayo      "; |FINSI;
  |SI mes = 6;  letrames = "Junio     "; |FINSI;
  |SI mes = 7;  letrames = "Julio     "; |FINSI;
  |SI mes = 8;  letrames = "Agosto    "; |FINSI;
  |SI mes = 9;  letrames = "Septiembre"; |FINSI;
  |SI mes = 10; letrames = "Octubre   "; |FINSI;
  |SI mes = 11; letrames = "Noviembre "; |FINSI;
  |SI mes = 12; letrames = "Diciembre "; |FINSI;
  |SI mes = 13; letrames = "Cierre    "; |FINSI;
|FCAL;

|CALCULO estruct; |TIPO 0;
  |SI dirfich0 = "*"; |ACABA; |FINSI;
  |SI FSalida = 2; |ACABA; |FINSI;
  |HAZ lee_estr;
  |SI sw_error  ! 0;
      |MENAV "    No existe la estructura de descarga.";
      |ERROR;
      |ACABA;
  |FINSI;
  |ATRI I; |PINTA 1934, #caexistc#1; |ATRI 0;
|FCAL;

|CALCULO lee_estr;
  sw_error = 0;
  alfa1 = #0#0; alfa1 = "00000" + alfa1; alfa1 = alfa1 % -105;
  alfa2 = #0#1; alfa2 = "0"     + alfa2; alfa2 = alfa2 % -101;
  dirfich = dirfich0 + alfa1 + alfa2 + "/";
  |PATH_DAT #21 dirfich;
  |ABRE #caexistc;
  #caexistc#0 = #0#11;
  |LEE 040#caexistc.=;
  |SI FSalida ! 0
      sw_error = 1;
      |CIERRA #caexistc;
      |ACABA;
  |FINSI;
  |CIERRA #caexistc;
  cue_perdi = #caexistc#2;
  dig_perdi = #caexistc#3;
|FCAL;

|CALCULO conexi;
  d_mes = #0#12;
  h_mes = #0#13;
  estru = #0#11;
  r_emp = #0#0;
  r_per = #0#1;
  r_eje = #0#2;
  r_nom = #0#3;
  |SUB_EJECUTA_NP "vaexicon";
|FCAL;

|| ********* CALCULOS PARA CREAR EL FICHERO AUXILIAR (SUMAS Y SALDOS) *******
|PROCESO borralin;
 |BORRA 020#2c;
|FPRC;

|PROCESO cab_grabar;
 #1#0 = empresa;
 #1#1 = anyo;
 |LIBERA #1;
 |LEE 101#1=;
 |SI FSalida ! 0;
     |DDEFECTO #1;
     #1#0 = empresa;
     #1#1 = anyo;
     #1#2 = #30#1;
     #1#3 = 0;
     #1#4 = 0;
     #1#5 = 0;
     #1#6 = 0;
     #1#7 = 0;
     |GRABA 020#1c;
     |LIBERA #1;
 |FINSI;
|FPRC;

|PROCESO lin_grabar;
  |PINTA 2207, " PASANDO CUENTA: ";
  |ATRI I;
  |PINTA 2224, #cacuenta#0;
  |ATRI 0;
  |DDEFECTO #2;

  linea = linea + 1;
  #2#0 = #1#0;
  #2#1 = anyo;
  #2#2 = linea;
  #2#3 = #cacuenta#0;
  #2#4 = #cacuenta#2;
  #2#9 = f_saldo;
  #2#5 = i_saldo;
  #2#6 = debe;
  #2#7 = haber;
  #2#8 = saldo;
  |GRABA 020#2n;

  #1#3 = #1#3 + #2#5;
  #1#4 = #1#4 + #2#6;
  #1#5 = #1#5 + #2#7;
  #1#6 = #1#6 + #2#8;
  #1#7 = #1#7 + #2#9;
 |GRABA 020#1a;
 |LIBERA #1;
|FPRC;

|CALCULO exis_expl;
  descar = #caexistl#des_exi# - #caexistl#has_exi#; || iniciales - finales
  descar = (descar / enConversor) ! EURODBMOD;

  |SI descar = 0; |ACABA; |FINSI;
  |SI descar ] 0;
      act_cuent = #caexistl#5;     || cuenta descarga D
      act_nivel = #caexistl#6;     || digito descarga D
  |SINO;
      act_cuent = #caexistl#8;     || cuenta descarga H
      act_nivel = #caexistl#9;     || digito descarga H
  |FINSI;

  |SI act_cuent = #cacuenta#0; |Y act_nivel = #cacuenta#1;
      sw_exp = 1;
      |SI descar ] 0;
          debe = debe + descar;
      |SINO;
          haber = haber - descar;
      |FINSI;
       saldo = debe - haber;
  |FINSI;
|FCAL;

|DEFBUCLE exis2;
 #caexistl#1;
 ;
 #0#11, 01;
 #0#11, 99;
 e;
 NULL, exis_expl;
|FIN;

|CALCULO exis_3000;
 sw_exp  = 1;
 f_saldo = (#caexistl#11 / enConversor) ! EURODBMOD;
 i_saldo = (#caexistl#des_exi# / enConversor) ! EURODBMOD;
 saldo   = (#caexistl#has_exi# / enConversor) ! EURODBMOD;
 haber   = 0;
 debe    = saldo - i_saldo;
 |SI debe < 0; haber = - debe; debe = 0; |FINSI;
|FCAL;

|DEFBUCLE exis1;
 #caexistl#2;
 ;
 #0#11, #cacuenta#0;
 #0#11, #cacuenta#0;
 e;
 NULL,exis_3000;
|FIN;

|CALCULO exis_8y9;
 debe  = debe  + #499#1;
 haber = haber + #499#2;
|FCAL;

|DEFBUCLE exis8;
 #499#1;
 ;
 #cacuenta#0;
 #cacuenta#0;
 e;
 NULL,exis_8y9;
|FIN;

|CALCULO calcular;
 f_saldo = 0;
 i_saldo = 0;
 saldo   = 0;
 debe    = 0;
 haber   = 0;
 sw_exp  = 0;
 alfa1   = #cacuenta#0 % 101;
 |SI alfa1 = "1";                 |BUCLE exis8; |FINSI;
 |SI alfa1 = "3";                 |BUCLE exis1; |FINSI;
 |SI alfa1 = "6"; |O alfa1 = "7"; |BUCLE exis2; |FINSI;

 |SI sw_exp = 1; |ACABA; |FINSI;

 |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
       y = x + 1;
       debe  = debe  + ((#cacuenta#x# / enConversor) ! EURODBMOD);
       haber = haber + ((#cacuenta#y# / enConversor) ! EURODBMOD);
       saldo = debe - haber;
 |SIGUE;
 f_saldo = (#cacuenta#11 / enConversor) ! EURODBMOD;
 |SI #0#9 = 0;
     i_saldo = (#cacuenta#11 / enConversor) ! EURODBMOD;
     debe    = debe  - ((#cacuenta#9  / enConversor) ! EURODBMOD);
     haber   = haber - ((#cacuenta#10 / enConversor) ! EURODBMOD);
 |FINSI;
|FCAL;

|PROCESO grabar;
 |HAZ cab_grabar;
 #2#0 = #1#0;
 #2#1 = anyo;
 #2#3 = #cacuenta#0;
 |LIBERA #2;
 |LEE 110#2=;
 |SI FSalida ! 0;
     |SI #cacuenta#0 ! cue_perdi; || no es perdidas y ganancias
         |HAZ calcular;
         |SI debe ! 0; |O haber ! 0; |O saldo ! 0;
             |HAZ lin_grabar;
             |SI #cacuenta#4 = 0; |Y #cacuenta#3 = "S";
                 |SI #cacuenta#5 = "A";
                     activo = activo + #2#8;
                 |SINO;
                     pasivo = pasivo + #2#8;
                 |FINSI;
             |FINSI;
         |FINSI;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE vapascon0;
 #cacuenta#1;
 3;
 d_cuen, "S";
 h_cuen, "S";
 e;
 NULL,grabar;
|FIN;

|PROCESO beneficio; || lee la cuenta de descarga y graba el beneficio
 |HAZ cab_grabar;
 |DDEFECTO #cacuenta;
 #cacuenta#0 = cue_perdi;
 #cacuenta#1 = dig_perdi;
 |LEE 101#cacuenta.=;
 |SI FSalida ! 0; #cacuenta#0 = cue_perdi; #cacuenta#1 = dig_perdi; |FINSI;

 #2#0 = #1#0;
 #2#1 = anyo;
 #2#3 = #cacuenta#0;
 |LIBERA #2;
 |LEE 110#2=;
 |SI FSalida ! 0;
     i_saldo = 0; debe = 0; haber = 0; saldo = 0; f_saldo = 0;
     |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
           y = x + 1;
           debe  = debe  + ((#cacuenta#x# / enConversor) ! EURODBMOD);
           haber = haber + ((#cacuenta#y# / enConversor) ! EURODBMOD);
     |SIGUE;
     f_saldo = (#cacuenta#11 / enConversor) ! EURODBMOD;
     |SI #0#9 = 0;
         i_saldo = (#cacuenta#11 / enConversor) ! EURODBMOD;
         debe    = debe  - ((#cacuenta#9  / enConversor) ! EURODBMOD);
         haber   = haber - ((#cacuenta#10 / enConversor) ! EURODBMOD);
     |FINSI;
     beneficio = activo + pasivo;
     |SI beneficio > 0;
         haber = haber + beneficio;
     |SINO;
         debe  = debe - beneficio;
     |FINSI;
     saldo = i_saldo + debe - haber
     |HAZ lin_grabar;
 |FINSI;
|FPRC;

|PROCESO calculit1;
 |HAZ cab_grabar;
 #2#0 = #1#0;
 #2#1 = anyo;
 #2#3 = #499#0;
 |LIBERA #2;
 |LEE 110#2=;
 |SI FSalida ! 0;
     #cacuenta#0 = #499#0;
     #cacuenta#2 = #499#5;
     f_saldo = 0;
     i_saldo = 0;
     debe    = #499#01;
     haber   = #499#2;
     saldo   = (debe - haber) ! 2;
     |SI debe ! 0; |O haber ! 0; |O saldo ! 0;
         |HAZ lin_grabar;
         activo = activo + #499#1;
         pasivo = pasivo + #499#2;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE elauxiliar;
 #499#1;
 ;
 "             ";
 "zzzzzzzzzzzzz";
 e;
 NULL,calculit1;
|FIN;

|| **************************************************************************
|| ********* CALCULOS PARA CREAR LOS FICHEROS DE BALANCE ********************

|PROCESO pase; |TIPO 3
  |SI dirfich0 = "*"; |ACABA; |FINSI;
  |HAZ lee_estr; |SI sw_error = 1; |ACABA; |FINSI; || Estructura descarga

  enEmpresa   = #0#0;
  enPerConta  = #0#1;
  |HAZ conexi;                                     || Existencias

  aFichero = "n" + Usuario;
  |NOME_DAT #499 aFichero;
  |DELINDEX #499;
  |HAZ LasCtas8;

  linea = 0;
  |ABRE #1;
  empAlta   = #0#0;
  empresa   = #0#0;
  periodo   = #0#1;
  anyo      = #0#2;
  nombre    = #0#3;
  registr   = #0#4;
  descrip   = #0#5;
  fec_con   = #0#6;
  tipo      = #0#14;
  fz        = #0#15;

  act_ant_con = #0#7;
  Planilla = #0#8;

  |HAZ EnConversor;

   #1#0 = empresa;
   #1#1 = anyo;
   |LEE 040#1=;
   |SI FSalida = 0;
      |BUCLELIN 0borralin#1;
      |BORRA 020#1c;
   |FINSI;
   |CIERRA #1;

   linea = 0;
   |ABRE #1;
   |ABRE #2;
   |SELECT #2#2;

   |PATH_DAT #20 dirfich;
   |PATH_DAT #22 dirfich;
   |ABRE #cacuenta;
   |ABRE #caexistl;

    eldesde = ( #0#9 * 3) + 9;
    elhasta = (#0#10 * 3) + 9;
    des_exi =  #0#12 + 11;
    has_exi =  #0#13 + 11;

    d_cuen = "            "; d_digi = 0;
    h_cuen = "799999999999"; h_digi = 9;
    activo = 0;
    pasivo = 0;
    beneficio = 0;

   |BUCLE vapascon0;
   |BUCLE elauxiliar;
   |CIERRA #caexistl;
   |HAZ beneficio;
   |CIERRA #cacuenta;
   |CIERRA #1;
   |CIERRA #2;

   fondo = fondo * 30;
   |PINTA 2207, fondo;
   |DELINDEX #499;

   || Segunda parte del Pase.

   |SI #0#16 = 2; |SUB_EJECUTA_NP "vabala00"; |FINSI;
   |SI #0#16 = 1; |SUB_EJECUTA_NP "va8bal00"; |FINSI;
|FPRC;


|PROCESO Grabaw3;
 |DDEFECTO #499;
 #499#0 = aCuenta;
 |LEE 001#499=;
 |SI FSalida ! 0;
     #499#0 = aCuenta;
     #499#5 = aNombre;
     |GRABA 020#499c;
 |FINSI;
 |SI aSigno = "D";
     #499#1 = #499#1 + nImporte;
 |SINO;
     #499#2 = #499#2 + nImporte;
 |FINSI;
 |GRABA 020#499a;
|FPRC;

|PROCESO reparto_lin;
 aAlfa1 = #432#2; |QBF aAlfa1;
 |SI aAlfa1 ! "";
     aCuenta  = #432#2;
     aNombre  = #432#3;
     aSigno   = #431#8;
     nImporte = #432#4;
     |HAZ Grabaw3;
 |FINSI;
|FPRC;

|DEFBUCLE reparto_lin;
 #432#1;
 ;
 #431#0, 01;
 #431#0, 99;
 e;
 NULL, reparto_lin;
|FIN;

|PROCESO reparto;
 aAlfa1 = #431#2; |QBF aAlfa1;
 |SI aAlfa1 ! "";
     aCuenta  = #431#2;
     aNombre  = #431#3;
     nImporte = #431#4;
     aSigno   = #431#8;
     |HAZ Grabaw3;
 |SINO;
     |BUCLE reparto_lin;
 |FINSI;
|FPRC;

|DEFBUCLE reparto;
 #431#1;
 ;
 "            ";
 "zzzzzzzzzzzz";
 e;
 NULL, reparto;
|FIN;

|PROCESO LasCtas8;
   |PATH_DAT #431 dirfich;
   |PATH_DAT #432 dirfich;

   |ABRE #499;
   |BUCLE reparto;
   |CIERRA #499;
|FPRC;
