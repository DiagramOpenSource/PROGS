|FICHEROS;
   vaaccbal #0;
   vabala02 #2;
   vabala03 #3;
   vabala04 #4;
   vabala05 #5;
|FIN;

|VARIABLES;
   sw       = 0;
   fondo    = " ";
   x        = 0;
   posici   = 0;
   horizo   = "";

   alfa1 = "";

{-1} menu   = "";
   menu1    = "1216";
   menu2    = "1";
   menu3    = "9";
   menu4    = "Entrada del Activo                 ";
   menu5    = "Entrada del Pasivo                 ";
   menu6    = "Entrada de Debe                    ";
   menu7    = "Entrada de Haber                   ";
   menu8    = "Calculo Debe - Haber               ";
   menu9    = "Pantalla de Totales                ";
   menu10    = "Impresion Registro                 ";
   menu11   = "Pase Datos Actuales a Anteriores   ";
   menu12   = "Datos Contables desde Entrada Gral ";
|FIN;

|INCLUYE dscnex_v;
|| =======================================================================
||                  PROCESOS POR PANTALLA
|| =======================================================================

|PROCESO mayor; |TIPO 0;
 alfa1 = #0Campo > " ";
 #0Campo = alfa1;
 |PINTA #0Campo;
|FPRC;

|PROCESO forzar;
 |SI #0#7 = "A";
     #0#8 = "A";
     |CAMPO_MODIFICA #0#8, 1, "N";
 |SINO;
     |CAMPO_MODIFICA #0#8, 1, "S";
 |FINSI;
 |PINTA #0#8;
|FPRC;

|PROCESO cuadro; |TIPO 7;
 |SI #0#7 = "A"; |ACABA; |FINSI;
 |PUSHV 0501 2380;
 |CUADRO 0926 1479;
 |ATRI R;
 |PINTA 1027, " <A> Forzar a Abreviado                             ";
 |PINTA 1127, " <E> Forzar a Extendido                             ";
 |PINTA 1227, " <B> Balance Abreviado, Perd. y Ganancias Extendido ";
 |PINTA 1327, " <P> Balance Extendido, Perd. y Ganancias Abreviado ";
 |ATRI r;
|FPRC;

|PROCESO repon; |TIPO 0;
 |POPV;
 |PINTA #0#8;
|FPRC;

|PROCESO leeregi; |TIPO 0;
  modo = (FEntrada / 100) ! 0;
  |SI dirfich0 = "*";
      |CAMPO_MODIFICA #0#10, 1, "S";
  |SINO;
      |CAMPO_MODIFICA #0#10, 1, "N";
  |FINSI;

  |SI modo = 1;
    |SI dirfich0 ! "*";
        |ABRE #canempre;
        #canempre#2 = #0Campo;
        #canempre#3 = 0;
        |LEE 011#canempre.];
        |SI FSalida ! 0;  |O #canempre#2 ! #0Campo;
            |MENAV "     NO Existe la Empresa Contable";
            |ERROR;
            |CIERRA #canempre;
            |ACABA;
        |FINSI;
        |CIERRA #canempre;
        #0#10 = #canempre#1;
        |PINTA #0#10;
    |FINSI;
    #0#4 = "00.00.0000"; |PINTA #0#4;
    #0#5 = "00.00.0000"; |PINTA #0#5;
    #0#6 = "00.00.0000"; |PINTA #0#6;
 |FINSI;
|FPRC;

|CALCULO con_empr; |TIPO 0;
  |SI FSalida ! 9;  |ACABA;  |FINSI;
  |SI dirfich0 = "*"; |ACABA; |FINSI;

  |CIERRAT #canempre;
  |SUB_EJECUTA_NP "vaconemp";
  |ABRET #canempre;
  |ERROR;
  |SI r_emp = 0; |PTEC 808; |ACABA; |FINSI;

  #0#0 = r_emp; |PINTA #0#0;
  #0#1 = r_eje; |PINTA #0#1;
  |SI dirfich0 = "*"; #0#10 = r_nom; |PINTA #0#10; |FINSI;
  |PTEC 802;
|FCAL;

|PROCESO cambioti; |TIPO 0;
 modo = (FEntrada / 100) ! 0;
 |SI modo ! 1;
     |CAMPO_MODIFICA #0#7, 1, "N";
     |CAMPO_MODIFICA #0#8, 1, "N";
 |SINO;
     |CAMPO_MODIFICA #0#7, 1, "S";
 |FINSI;
|FPRC;


|| ===================== Procesos de Actualizacion

|PROCESO borrado; |TIPO 2;
 modo = (FEntrada / 100) ! 0;
 |SI modo = 3;
     |PINTA 2430,"Borrando Registros ...";
     |ABRE #vabala02; #vabala02#0 = #0#0;
                      #vabala02#1 = #0#1;
                      #vabala02#2 = #0#2;
                      |BORRA 101#vabala02.c; |LIBERA #vabala02;
     |CIERRA #vabala02;

     |ABRE #vabala03; #vabala03#0 = #0#0;
                      #vabala03#1 = #0#1;
                      #vabala03#2 = #0#2;
                      |BORRA 101#vabala03.c; |LIBERA #vabala03;
     |CIERRA #vabala03;

     |ABRE #vabala04; #vabala04#0 = #0#0;
                      #vabala04#1 = #0#1;
                      #vabala04#2 = #0#2;
                      |BORRA 101#vabala04.c; |LIBERA #vabala04;
     |CIERRA #vabala04;

     |ABRE #vabala05; #vabala05#0 = #0#0;
                      #vabala05#1 = #0#1;
                      #vabala05#2 = #0#2;
                      |BORRA 101#vabala05.c; |LIBERA #vabala05;
     |CIERRA #vabala05;
     |PINTA 2430,"                     ";
 |FINSI;
|FPRC;

|PROCESO acceso;|TIPO 30;
 modo = (FEntrada / 100) ! 0;
 |SI modo = 1; |O modo = 2;
     |CIERRAT #0;
     |HAZ pantalla;
     |ABRET #0;
 |FINSI;
|FPRC;

|PROCESO consulta; |TIPO 13;
 modo = (FEntrada / 100) ! 0;
 |SI modo = 4;
     |PINTA #0#0;
     |PINTA #0#1;
     |PINTA #0#2;
     |PINTA #0#3;
     |PINTA #0#4;
     |PINTA #0#5;
     |PINTA #0#6;
     |PINTA #0#7;
     |PINTA #0#8;
     |PINTA #0#10;
     |HAZ pantalla;
 |FINSI;
|FPRC;

|PROCESO pantalla;
 opcion = 1;
 sw = 0;
 empresa = #0#0;
 anyo    = #0#1;
 registr = #0#2;
 descrip = #0#3;
 nombre  = #0#10;
 tipo    = #0#7;
 fz      = #0#8;
 fec_con = #0#4;
 fec_ant = #0#5;
 fec_act = #0#6;
 periodo = #0#9;

|PARA; |SI sw = 0; |HACIENDO;
   |PARA x =  11; |SI x [ 22; |HACIENDO x = x + 1;
       posici = (x * 100) + 57;
       |PINTA posici,fondo;
       posici = (x * 100) + 58;
       |PINTA posici,fondo;
   |SIGUE;
   horizo = "";
   |PARA x = 9; |SI x [ 57; |HACIENDO x = x + 1;
       horizo = horizo + fondo;
   |SIGUE;
   |PINTA 2310,horizo;
   menu2  = opcion;
   |MENUG menu;
   opcion = FSalida;
   |SI opcion ] 1; |Y opcion [ 9;

       |SI opcion = 9; |Y modo ! 4;  || pasar datos Contabilidad
           sw_act   = 0;
           Planilla = 0;
           |SUB_EJECUTA_NP "vabala00";
           |SI sw_act = 1
               #0#4 = fec_con;      |PINTA #0#4;
               #0#6 = fec_act;      |PINTA #0#6;
           |FINSI;
       |FINSI;

       |SI opcion = 8; |Y modo ! 4;  || pasar datos Actuales a Anteriores
           sw_act = 0;
           |SI #0#6 = "00.00.0000";
               |AVISO;
               |CONFI "2400NLos Datos Actuales NO han sido validados. ¨Desea continuar? ";
               |SI FSalida = 0;
                   |SUB_EJECUTA_NP "vabala01";
               |FINSI;
           |SINO;
               |SUB_EJECUTA_NP "vabala01";
           |FINSI;
           |SI sw_act = 1
               #0#5 = fec_ant;      |PINTA #0#5;
               #0#6 = fec_act;      |PINTA #0#6;
           |FINSI;
       |FINSI;

       |SI opcion = 1;  |SUB_EJECUTA_NP "vabala02"; |FINSI;    || ... activo
       |SI opcion = 2;  |SUB_EJECUTA_NP "vabala03"; |FINSI;    || ... pasivo
       |SI opcion = 3;  |SUB_EJECUTA_NP "vabala04"; |FINSI;    || ... debe
       |SI opcion = 4;  |SUB_EJECUTA_NP "vabala05"; |FINSI;    || ... haber
       |SI opcion = 5; |Y modo ! 4; |SUB_EJECUTA_NP "vabala06"; |FINSI; || Calculo

       |SI opcion = 6;  |SUB_EJECUTA_NP "vabala07"; |FINSI;    || ... Totales
       |SI opcion = 7;  |SUB_EJECUTA_NP "vabala08"; |FINSI;    || Imprimir reg.
   |SINO;
       sw = 1;
   |FINSI;

|SIGUE;
|PINPA #0#0;
|FPRC;
