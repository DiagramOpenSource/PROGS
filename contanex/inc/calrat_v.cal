|VARIABLES;

pregun1 = "2400NDesea calcular el Ratio ? ";

mensaje1="PROGRAMADOR:No alteres el orden de estos mensajes ni intercales nada";
mensaje01="0000*ATENCION !!!! La formula es demasiado grande ! ";
mensaje02="0000*ATENCION !!!! La formula contiene algun parentesis ) de mas !";
mensaje03="0000*ATENCION !!!! La formula contiene algun parentesis ( de mas !";
mensaje04="0000*ATENCION !!!! Formula Incoherente !!";

mensaje2 = "0401**** Acumulando Cuenta :";
mensaje3 = "2400**** CALCULANDO ****";

   error = 0;
{300}simple = "";
{-1}sim = 0;
   nada = "";
   trabajo = "";
   r_alfa1 = "";
   r_alfa2 = "";
   r_alfa3 = "";
   r_alfa4 = "";
   formu = "";
   M = "*";
   D = "/";
   S = "+";
   R = "-";
   dolar = "$";
   punte = "&";

   i = 0;
   n = 0;
   j = 0;
   r_modo = 0;
   poper = 0;
   nivel = 0;
   buceo = 0;
   paren = 0;
   sfinal = 0;

   alt = 0;
   aln = 0;
   al1 = "";
   al2 = "";
   all = 0;
{-1}alp = 0;
   total1 = 0;
   total2 = 0;
   total3 = 0;
   total4 = 0;
   total5 = 0;
   total6 = 0;
   total7 = 0;
   total8 = 0;
   total9 = 0;
   total10= 0;
   total11= 0;
   total12= 0;
   espacios = "                ";
   anadido = "+$0";
   pare1 = "(";
   pare2 = ")";
|FIN;

|CALCULO calculo_ratio;
 || |CONFI pregun1;
 || |SI FSalida ! 0;  |ACABA;  |FINSI;
  |HAZ calcula;
  |SI error ! 0;
      Isim = mensaje1 <-;
      Isim = Isim + error;
      mensaje1 = sim;
      |MENAV mensaje1;
  |FINSI;
|FCAL;

|CALCULO calcula;
|BLIN 24;
|ATRI P;
|| .... |MENSA mensaje3;
|ATRI 0;
error  = 0;
|| Borrar Importes de la ficha;
 |PDEFECTO #103,16,28;

|| Primer paso: componer las formula definitiva leyendo fichero de formulas
 |ABRE #104;
 formu = #103#4;
 #104#0 = formu;
 |LEE 020#104=;
 |SI FSalida = 0;
     trabajo = #104#2;
     |PARA buceo = 0;|SI buceo = 0;|Y error = 0;|HACIENDO;
           |QBT trabajo;
           buceo = 1;
           r_alfa1 = trabajo % 0;n = r_alfa1 * 100;n = n + 1;
           |PARA i = 101;|SI i [ n;|HACIENDO i = i + 100;
             r_alfa1 = trabajo % i;
             |SI r_alfa1 = AR;
                 i = i + 103;
                 formu = trabajo % i;
                 #104#0 = formu;
                 |LEE 020#104=;
                 formu = nada;
                 |SI FSalida = 0;
                    formu = pare1 + #104#2;
                    formu = formu + anadido + pare2;
                    |QBT formu;
                 |FINSI;
                 i = i / 100;i = i ! 0;i = i - 2;
                 r_alfa1 = nada;
                 aln = 100;
                 alt = i;
                 |PARA;|SI alt > 99;|HACIENDO alt = alt - 80;
                     all = aln + 80;
                     al1 = trabajo % all;
                     r_alfa1 = r_alfa1 + al1;
                     aln = aln + 8000;
                 |SIGUE;
                 all = aln + alt;
                 al1 = trabajo % all;
                 r_alfa1 = r_alfa1 + al1;
                 r_alfa1 = r_alfa1 + formu;
                 i = i + 6;
                 al1 = trabajo % 0;all = al1 - i;all = all + 1;
                 al1 = nada;
                 i = i * 100;
                 |PARA;|SI all > 99;|HACIENDO all = all - 80;
                    aln = i + 80;
                    al2 = trabajo % aln;
                    al1 = al1 + al2;
                    i = i + 8000;
                 |SIGUE;
                 i = i + all;
                 al2 = trabajo % i;
                 trabajo = al1 + al2;
                 trabajo = r_alfa1 + trabajo;
                 r_alfa1 = trabajo % 0;
                 |SI r_alfa1 > 240;
                        ||***** Error 1 exceso
                        error = 1;
                 |FINSI;
                 buceo = 0; || Continuar buscando formulas relacionadas
             |SINO;
         |SIGUE;
             |FINSI;
    |SIGUE;
|FINSI;
|CIERRA #104;

al1 = trabajo % 0;
|SI al1 > 0; || Si hay formula se calcula

  trabajo = trabajo + anadido; || Esto es por si acaso no hay operaciones

  || Segundo paso: desglosar en orden de ejecucion en subformulas los parentesis

  Isimple = simple1 <-; || A partir del 1 desglose de parentesis
  |PARA paren = 0;|SI paren = 0;|Y error = 0;|HACIENDO;
      |HAZ parentes;
  |SIGUE;
  simple = trabajo; || Formula resultante
  Isimple = Isimple + 1;
  sfinal = Isimple

  || Tercer paso : resolver las formulas sencillas
  |PARA Isimple=simple1<-;|SI Isimple < sfinal;|Y error = 0;
                                          |HACIENDO Isimple=Isimple + 1;
    Isim = simple200<-; || Pila de resultados intermedios
    trabajo = simple;
    |HAZ formula; || Resuelve una formula sencilla
    simple = trabajo;
  |SIGUE;

  |SI error = 0;
     r_alfa1 = nada;
     Isimple = sfinal - 1;
     trabajo = simple;
     || Poner resultado en la ficha
     j = 115;
     |PARA i = 16; |SI i < 28;|HACIENDO i = i + 1;
        r_alfa1 = trabajo % j;
        #103i = r_alfa1;
        j = j + 1500;
     |SIGUE;
  |FINSI;

|FINSI;

|BLIN 24;
|FCAL;


|CALCULO parentes;

r_alfa1 = trabajo % 0;n = r_alfa1;n = n * 100;n = n + 1;
nivel = 0;
paren = 1;
|PARA i = 101;|SI i [ n;|Y error = 0;|HACIENDO i = i + 100;
   r_alfa1 = trabajo % i;
   |SI r_alfa1 = pare1;
      nivel = i + 100;
   |FINSI;
   |SI r_alfa1 = pare2;
      |SI nivel = 0;
                     ||***** Error 2 parentesis )
          error = 2;
      |SINO;
        || Sustituir
        i = i - nivel;i = i / 100;i = i ! 0;i = i - 1;
        nivel = nivel + i;
        simple = trabajo % nivel;
        i = nivel / 100;i = i ! 0;i = i - 2;
        r_alfa1 = nada;
        aln = 100;
        |PARA;|SI i > 99;|HACIENDO i = i - 80;
              all = aln + 80;
              al1 = trabajo % all;
              r_alfa1 = r_alfa1 + al1;
              aln = aln + 8000;
        |SIGUE;
        all = aln + i;
        al1 = trabajo % all;
        r_alfa1 = r_alfa1 + al1;
        i = nivel / 100;i = i ! 0;nivel = nivel $ 100;
        i = i + nivel;i = i + 1;
        al1 = trabajo % 0;all = al1 - i;all = all + 1;
        i = i * 100;
        al1 = nada;
        |PARA;|SI all > 99;|HACIENDO all = all - 80;
               aln = i + 80;
               al2 = trabajo % aln;
               al1 = al1 + al2;
               i = i + 8000;
        |SIGUE;
        i = i + all;
        al2 = trabajo % i;
        trabajo = al1 + al2;
        r_alfa1 = r_alfa1 + punte;
        r_alfa2 = A0000 + Isimple;
        r_alfa2 = r_alfa2 % -104;
        r_alfa1 = r_alfa1 + r_alfa2;
        trabajo = r_alfa1 + trabajo;
        Isimple = Isimple + 1;
        paren = 0;
      |FINSI;
   |SINO;
|SIGUE;
      |SI nivel ! 0;
          || Error 3 parantesis (
          error = 3;
      |FINSI;
   |FINSI;

|FCAL;


|CALCULO formula;

   || Primero sustituir las multiplicaciones y divisiones buceo = 0
   || luego sumas y restas buceo = 1

|PARA buceo = 0;|SI buceo < 2;|Y error = 0;|HACIENDO buceo = buceo + 1;

   r_alfa1 = trabajo % 0;
   n = r_alfa1 * 100;n = n + 1;
   nivel = 0;
   poper = 0;
   r_modo = 0;
   |PARA i = 101;|SI i [ n;|HACIENDO i = i + 100;
        r_alfa1 = trabajo % i;
        |SI r_alfa1 = M;|O r_alfa1 = D;|O r_alfa1 = S;|O r_alfa1 = R;|O i = n;
            |SI r_modo = 0;
                |SI r_alfa1 = M;|O r_alfa1 = D;|O buceo = 1;
                   r_alfa2 = r_alfa1;
                   poper = i / 100;poper = poper ! 0;
                   r_modo = 1;
                |SINO;
                   nivel = i / 100;nivel = nivel ! 0;
                |FINSI;
            |SINO;
                |SI i = n;
                   i = i + 100;
                |FINSI;
                nivel = nivel + 1;
                j = poper - nivel;
                |SI j [ 0;
                   ||**** Error 4 operando erroneo
                   error = 4;
                |FINSI;
                nivel = nivel * 100;
                nivel = nivel + j;
                formu = trabajo % nivel;
                |HAZ valor; || Obtenido valor 1
                r_alfa3 = formu;
                poper = poper + 1;
                j = i / 100;j = j ! 0;
                j = j - poper;
                |SI j [ 0;
                   ||**** Error 4 operando erroneo
                   error = 4;
                |FINSI;
                poper = poper * 100;
                poper = poper + j;
                formu = trabajo % poper;
                |HAZ valor; || Obtenido valor 2
                r_alfa4 = formu;
                |SI r_alfa2 = S; || En formu vuelve el resultado
                   |HAZ r_suma;
                |FINSI;
                |SI r_alfa2 = R;
                   |HAZ resta;
                |FINSI;
                |SI r_alfa2 = M;
                   |HAZ multi;
                |FINSI;
                |SI r_alfa2 = D;
                   |HAZ divi;
                |FINSI;
                || Sustituir operacion por su resultado en la pila
                j = nivel $ 100; || longitud op 1
                poper = poper $ 100; || longitud op 2
                poper = j + poper;poper = poper + 1; || + 1 de la operacion
                poper = 5 - poper;
                poper = poper * 100; || poper es ahora la diferecia

                j = nivel / 100;j = j ! 0; || total hasta el cambio
                j = j - 1;
                r_alfa2 = nada;
                aln = 100;
                |PARA;|SI j > 99;|HACIENDO j = j - 80;
                     all = aln + 80;
                     al1 = trabajo % all;
                     r_alfa2 = r_alfa2 + al1;
                     aln = aln + 8000;
                |SIGUE;
                all = aln + j;
                al1 = trabajo % all;
                r_alfa2 = r_alfa2 + al1;
                j = i / 100;j = j ! 0;
                al1 = trabajo % 0;all = al1 - j;all = all + 1;
                j = j * 100;
                al1 = nada;
                |PARA;|SI all > 99;|HACIENDO all = all - 80;
                     aln = j + 80;
                     al2 = trabajo % aln;
                     al1 = al1 + al2;
                     j = j + 8000;
                |SIGUE;
                j = j + all;
                al2 = trabajo % j;
                trabajo = al1 + al2;

                r_alfa2 = r_alfa2 + punte;
                r_alfa4 = A0000 + Isim;
                r_alfa4 = r_alfa4 % -104;
                r_alfa2 = r_alfa2 + r_alfa4;

                trabajo = r_alfa2 + trabajo;
                n = n + poper; || nuevo total string
                i = i + poper; || Reposiciono puntero al string

                sim = formu; || guardar en pila
                Isim = Isim + 1; || Incrementar pila
                nivel = i / 100;nivel = nivel ! 0;
                r_modo = 0;
                |SI r_alfa1 = M;|O r_alfa1 = D;|O buceo = 1;
                   poper = nivel;
                   nivel = nivel - 6;
                   r_alfa2 = r_alfa1;
                   r_modo = 1;
                |FINSI;
            |FINSI;
        |FINSI;
   |SIGUE;

|SIGUE;

|| Tiene que haber quedado en trabajo un puntero a la pila
r_alfa1 = trabajo % 204;
Isim = r_alfa1;
trabajo = sim; || sustituirlo por el resultado

|FCAL;


|CALCULO valor;
|| entra formu , y sale formu con el valor en formato 15 * 12

|| Tres tipos :
|| 1 = $numero : se pone el numero en formato 15 * 12
|| 2 = numero : siendo numero una cuenta se hace su acumulado
|| 3 = &puntero : se sustituye el valor
|| Si todo va bien el string debe venir `limpio`

al1 = nada;
al1 = formu % 101;
|SI al1 = punte; || caso puntero
   al1 = formu % 0;all = al1;all = all - 1;all = all + 200;
   al1 = formu % all;
   Ialp = al1;
   formu = alp; || Todos los `apuntados` estan en 15 * 12
|SINO;
   |SI al1 = dolar; || caso numero directo
      al1 = formu % 0;all = al1;all = all - 1;all = all + 200;
      al1 = formu % all;
      al1 = al1 + espacios;
      al1 = al1 % 115;
      formu = nada;
      |PARA aln = 0;|SI aln < 12;|HACIENDO aln = aln + 1;
          formu = formu + al1;
      |SIGUE;
   |SINO;  || caso cuenta
      al1 = formu % 0;aln = al1;aln = aln + 100;
      total1 = 0;
      total2 = 0;
      total3 = 0;
      total4 = 0;
      total5 = 0;
      total6 = 0;
      total7 = 0;
      total8 = 0;
      total9 = 0;
      total10= 0;
      total11= 0;
      total12= 0;
      |BLIN 4;
      |MENSA mensaje2;
      |ABRE #1;
      #1#0 = formu;
      #1#1 = 1;
      |LEE 000#1];
      al1 = #1#0 % aln;
      |PARA;|SI FSalida = 0;|Y al1 = formu;|HACIENDO;
             || ... |PINTA 2226,#1#0;
            |PARA alt = 2;|SI alt < 15; |HACIENDO alt = alt + 1;
                  |SI alt < 4; total1 = total1 + #1alt;|FINSI;
                  |SI alt < 5; total2 = total2 + #1alt;|FINSI;
                  |SI alt < 6; total3 = total3 + #1alt;|FINSI;
                  |SI alt < 7; total4 = total4 + #1alt;|FINSI;
                  |SI alt < 8; total5 = total5 + #1alt;|FINSI;
                  |SI alt < 9; total6 = total6 + #1alt;|FINSI;
                  |SI alt < 10;total7 = total7 + #1alt;|FINSI;
                  |SI alt < 11;total8 = total8 + #1alt;|FINSI;
                  |SI alt < 12;total9 = total9 + #1alt;|FINSI;
                  |SI alt < 13;total10 = total10 + #1alt;|FINSI;
                  |SI alt < 14;total11 = total11 + #1alt;|FINSI;
                  total12 = total12 + #1alt;
            |SIGUE;
            |LEE 000#1s;
            al1 = #1#0 % aln;
      |SIGUE;
      |CIERRA #1;
      |BLIN 4;
      Ialp = total1 <-;
      formu = nada;
      |PARA alt = 0;|SI alt < 12;|HACIENDO alt = alt + 1;
         al1 = alp + espacios;
         al1 = al1 % 115;
         formu = formu + al1;
         Ialp = Ialp + 1;
      |SIGUE;
   |FINSI;
|FINSI;

|FCAL;

|| OPeraciones sencillas : entran r_alfa3 y r_alfa4 como operandos, sale formu
||  como resultado (todo va en 15 * 12)

|CALCULO r_suma;
formu = nada;
|PARA aln = 115;|SI aln < 18115;|HACIENDO aln = aln + 1500;
   al1 = r_alfa3 % aln;
   al2 = r_alfa4 % aln;

   alt = al1 + al2;

   al1 = alt + espacios;
   al1 = al1 % 115;
   formu = formu + al1;
|SIGUE;
|FCAL;

|CALCULO resta;
formu = nada;
|PARA aln = 115;|SI aln < 18115;|HACIENDO aln = aln + 1500;
   al1 = r_alfa3 % aln;
   al2 = r_alfa4 % aln;

   alt = al1 - al2;

   al1 = alt + espacios;
   al1 = al1 % 115;
   formu = formu + al1;
|SIGUE;
|FCAL;

|CALCULO multi;
formu = nada;
|PARA aln = 115;|SI aln < 18115;|HACIENDO aln = aln + 1500;
   al1 = r_alfa3 % aln;
   al2 = r_alfa4 % aln;

   alt = al1 * al2;

   al1 = alt + espacios;
   al1 = al1 % 115;
   formu = formu + al1;
|SIGUE;
|FCAL;


|CALCULO divi;
formu = nada;
|PARA aln = 115;|SI aln < 18115;|HACIENDO aln = aln + 1500;
   al1 = r_alfa3 % aln;
   al2 = r_alfa4 % aln;

   alt = al1 / al2;

   al1 = alt + espacios;
   al1 = al1 % 115;
   formu = formu + al1;
|SIGUE;
|FCAL;
