|FICHEROS;
     rem00100 #0;
     rem00101 #3;
     reparame #1;
     repant17 #917;

     :/integral/def/dsam0004 #790;
     &clientes@ #715;
     &haciend@ #706;
|FIN;

|VARIABLES;
     &enEmpresa  = 0;
     &eaNombre   = "";
     &eaNif      = "";
     &enModo     = 0;
     &eaVarDni   = "";
     &eaPrograma = "";
     &eaPunto    = "";

     nCampo    = 0;
     nCompara  = 0;
     nRegraba  = 0;
     nError    = 0;

     aTecla    = "";
     aAvance   = "";
     aAtras    = "";
     aNombre   = "";
     aAlfa1    = "";
     aAlfa2    = "";
     aAlfa     = "";

     &enCodProv = 0;
     &enCodPobl = 0;
     &enCodCli  = 0;
     &enSwDeDonde = 0;
|FIN;

|INCLUYE i_sepnom;

|PROCESO GrabaFamiliar;
     |ABRE #790;
     #790#0 = #3#0;
     #790#1 = #3#1;
     |LEE 101#790=;
     |SI FSalida ! 0;
         |DDEFECTO #790;
         #790#0 = #3#0;
         #790#1 = #3#1;
         |GRABA 020#790b;
     |FINSI;

     |PARA nCampo = 2;  |SI nCampo [ 15;  |HACIENDO nCampo = nCampo + 1;
           |SI nCompara = 0;
               #790nCampo = #3nCampo;
           |SINO;
               |SI #3nCampo ! #790nCampo;  nRegraba = 1;  |FINSI;
           |FINSI;
     |SIGUE;

     |SI nCompara = 0;
         #790#16 = #3#17;
         #790#17 = #3#18;
         #790#18 = #3#19;
         #790#19 = #3#20;
     |SINO;
         |SI #3#17 ! #790#16;  nRegraba = 1;  |FINSI;
         |SI #3#18 ! #790#17;  nRegraba = 1;  |FINSI;
         |SI #3#19 ! #790#18;  nRegraba = 1;  |FINSI;
         |SI #3#20 ! #790#19;  nRegraba = 1;  |FINSI;
     |FINSI;

     |SI nRegraba = 0;
         |GRABA 020#790a;
     |FINSI;
     |LIBERA #790;
     |CIERRA #790;
|FPRC;

|DEFBUCLE rem00101;
     #3#1;
     ;
     #0#0, 00;
     #0#0, 99;
     ;
     NULL, GrabaFamiliar;
|FIN;

|PROCESO BorraFamiliar;
     |SI nCompara = 0;
         |BORRA 020#790a;
         |LIBERA #790;
     |SINO;
         |ABRE #3;
         #3#0 = #790#0;
         #3#1 = #790#1;
         |LEE 040#3=;
         |SI FSalida ! 0;
             |CIERRA #3;
             nRegraba = 1;
             |ACABA;
         |FINSI;
         |CIERRA #3;

         |PARA nCampo = 2;  |SI nCampo [ 15;  |HACIENDO nCampo = nCampo + 1;
               |SI #3nCampo ! #790nCampo;  nRegraba = 1;  |FINSI;
         |SIGUE;

         |SI #3#17 ! #790#16;  nRegraba = 1;  |FINSI;
         |SI #3#18 ! #790#17;  nRegraba = 1;  |FINSI;
         |SI #3#19 ! #790#18;  nRegraba = 1;  |FINSI;
         |SI #3#20 ! #790#19;  nRegraba = 1;  |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE dsam0004;
     #790#1;
     ;
     #0#0, 00;
     #0#0, 99;
     ;
     NULL, BorraFamiliar;
|FIN;

|PROCESO CargaCampos;
     |SI nCompara = 0;
         |ABRE #715;
         #715#0 = #0#0;
         |LEE 101#715=;
         |SI FSalida ! 0;
             |DDEFECTO #715;
             #715#0 = #0#0;
             |GRABA 020#715b;
         |FINSI;
     |FINSI;

     |PARA nCampo = 0;  |SI nCampo [ 60;  |HACIENDO nCampo = nCampo + 1;
           |SI nCompara = 0;
               #715nCampo = #0nCampo;
           |SINO;
               |SI #0nCampo ! #715nCampo;  nRegraba = 1;  |FINSI;
           |FINSI;
     |SIGUE;

     |SI nCompara = 0;
         #715#63 = #0#93;
         #715#64 = #0#94;
         #715#65 = #0#95;
         #715#67 = #0#103;
         #715#68 = #0#104;
         #715#69 = #0#105;
         #715#70 = #0#106;
         |BUCLE rem00101;
     |SINO;
         |SI #0#94  ! #715#64;  nRegraba = 1;  |FINSI;
         |SI #0#103 ! #715#67;  nRegraba = 1;  |FINSI;
         |SI #0#104 ! #715#68;  nRegraba = 1;  |FINSI;
         |SI #0#105 ! #715#69;  nRegraba = 1;  |FINSI;
         |SI #0#106 ! #715#70;  nRegraba = 1;  |FINSI;
     |FINSI;

     |SI nRegraba = 0;  |BUCLE dsam0004;  |FINSI;
     |SI nRegraba = 0;  |BUCLE rem00101;  |FINSI;

     |SI nCompara = 0;
         |GRABA 020#715a;
         |LIBERA #715;
         |CIERRA #715;
     |FINSI;
|FPRC;

|PROCESO DatosFamiliar;
     nCompara = 1;
     nRegraba = 0;

     |HAZ CargaCampos;

     |SI nRegraba = 1;
         |CONFI "0000SDatos Personales del Cliente distintos a los de Renta, Desea Actualizarlos : ";
         |SI FSalida = 0;
             nCompara = 0;
             nRegraba = 0;
             |HAZ CargaCampos;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO DatosContribu;
     |PUSHV 0501 2380;
     |PARA nCampo = 0;  |SI nCampo [ 55;  |HACIENDO nCampo = nCampo + 1;
           #917nCampo = #0nCampo;
     |SIGUE;
     #917#57 = #0#88;
     #917#58 = #0#89;
     #917#59 = #0#94;
     #917#60 = #0#95;
     #917#61 = #0#99;
     #917#62 = #0#100;
     #917#63 = #0#101;
     #917#64 = #0#102;
     #917#65 = #0#103;
     #917#66 = #0#104;
     #917#67 = #0#105;
     #917#68 = #0#106;
     #917#69 = #0#107;
     #917#70 = #0#108;

     |PINPA #0#917;
     |PINDA #0#917;
     |ENDATOS #1#917;

     |SI FSalida = 0;
         |PARA nCampo = 0;  |SI nCampo [ 55;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = #917nCampo;
         |SIGUE;
         #0#88  = #917#57;
         #0#89  = #917#58;
         #0#94  = #917#59;
         #0#95  = #917#60;
         #0#99  = #917#61;
         #0#100 = #917#62;
         #0#101 = #917#63;
         #0#102 = #917#64;
         #0#103 = #917#65;
         #0#104 = #917#66;
         #0#105 = #917#67;
         #0#106 = #917#68;
         #0#107 = #917#69;
         #0#108 = #917#70;
         #0#7   = #0#103 % -104;
         #0#40  = #0#104 % -104;
     |FINSI;
     |POPV;
     |PINDA #0#0;
|FPRC;

|PROCESO DatosContribu7;  |TIPO 7;
     |SI #0#7 = "    ";  |O #0#7 = "0000";
         |HAZ DatosContribu;
     |FINSI;
|FPRC;

|PROCESO DatosContribu0;  |TIPO 0;
     |SI FSalida = 10;
         |HAZ DatosContribu;
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Mayus;
     #0Campo = #0Campo > #0Campo;
     |PINTA #0Campo;
|FPRC;

|PROCESO Hacienda;
     enCodProv = #0#69;
     enCodPobl = #0#70;
     |HAZ LeeHacienda;

     #0#71 = #706#2; |PINTA #0#71;
     #0#72 = #706#3; |PINTA #0#72;
|FPRC;

|PROCESO Parametro;
     |ABRE #1;
     |LEE 040#1=;
     |SI FSalida ! 0;  |DDEFECTO #1;  |FINSI;
     |CIERRA #1;

     #0#69 = #1#4;
     #0#70 = #1#5;
     #0#59 = #1#1;
     #0#60 = #1#1;
|FPRC;

|PROCESO Validaciones;
     nError = 0;
     |SI #0#100 = "D";
         |SI #0#12 = "            ";
             |MENAV "      Obligado a introducir numero de Telefono al ser Primera Declaracion Titular";
             nError = 1;
             |ACABA;
         |FINSI;
     |FINSI;

     |SI #0#14 = "  ";
         |MENAV "      Sigla de la Calle del Titular sin Contenido.";
         nError = 1;
         |ACABA;
     |FINSI;

     aAlfa = #0#15;  |QBF aAlfa;
     |SI aAlfa = "";
         |MENAV "      Calle del Titular sin Contenido.";
         nError = 1;
         |ACABA;
     |FINSI;

     |SI #0#16 = "    ";
         |MENAV "      Numero de la Calle del Titular sin Contenido.";
         nError = 1;
         |ACABA;
     |FINSI;

     aAlfa = #0#22;  |QBF aAlfa;
     |SI aAlfa = "";
         |MENAV "      Poblacion del Titular sin Contenido.";
         nError = 1;
         |ACABA;
     |FINSI;

     |SI #0#103 < "31.12.1882";
         |MENAV "      Fecha de Nacimiento del Titular incorrecta.";
         nError = 1;
         |ACABA;
     |FINSI;

     |SI #0#6 ! "C";  |ACABA;  |FINSI;

     |SI #0#101 = "D";
         |SI #0#13 = "            ";
             |MENAV "      Obligado a introducir numero de Telefono al ser Primera Declaracion Conyuge";
             nError = 1;
             |ACABA;
         |FINSI;
     |FINSI;

     aAlfa = #0#35;  |QBF aAlfa;
     |SI aAlfa = "";
         |MENAV "      No esta informado el NIF del Conyuge";
         nError = 1;
         |ACABA;
     |FINSI;

     aAlfa = #0#39;  |QBF aAlfa;
     |SI aAlfa = "";
         |MENAV "      Nombre del Conyuge sin Contenido.";
         nError = 1;
         |ACABA;
     |FINSI;

     |SI #0#44 = "  ";
         |MENAV "      Sigla de la Calle del Conyuge sin Contenido.";
         nError = 1;
         |ACABA;
     |FINSI;

     aAlfa = #0#45;  |QBF aAlfa;
     |SI aAlfa = "";
         |MENAV "      Calle del Conyuge sin Contenido.";
         nError = 1;
         |ACABA;
     |FINSI;

     |SI #0#46 = "    ";
         |MENAV "      Numero de la Calle del Conyuge sin Contenido.";
         nError = 1;
         |ACABA;
     |FINSI;

     aAlfa = #0#52;  |QBF aAlfa;
     |SI aAlfa = "";
         |MENAV "      Poblacion del Conyuge sin Contenido.";
         nError = 1;
         |ACABA;
     |FINSI;

     |SI #0#104 < "31.12.1882";
         |MENAV "      Fecha de Nacimiento del Titular incorrecta.";
         nError = 1;
         |ACABA;
     |FINSI;
|FPRC;

|PROCESO Pidedato;
     #0#0 = enEmpresa;
     |LEE 110#0=;
     |SI FSalida ! 0;
         |DDEFECTO #0;
         #0#0 = enEmpresa;
         #0#5 = eaNombre;
         Cadena = #0#5;
         |SI Cadena ! "";
             |HAZ SeparaNombre;
             #0#2 = Cadena1;
             #0#3 = Cadena2;
             #0#4 = Cadena3;
         |FINSI;
         |GRABA 020#0b;
     |FINSI;

     aNombre = #0#5; |QBF aNombre;
                     |QBF eaNombre;
     |SI aNombre ! eaNombre;
         #0#5 = eaNombre;
         Cadena = #0#5;
         |SI Cadena ! "";
             |HAZ SeparaNombre;
             #0#2 = Cadena1;
             #0#3 = Cadena2;
             #0#4 = Cadena3;
         |FINSI;
     |FINSI;

     #0#1   = eaNif;

     |ET OtraVez;
         |PINDA #0#0;
         |SI enModo = 1;
             |HAZ Parametro;
             |ENDATOS #1#0;
         |FINSI;
         |SI enModo = 2;
             |ENDATOS #2#0;
         |FINSI;
         |SI FSalida ! 0; |ACABA; |FINSI;

         |HAZ Validaciones;

         |SI nError = 1;  |VETE OtraVez;  |FINSI;

     #0#0   = enEmpresa;
     #0#1   = eaNif;
     |GRABA 020#0a;
     |LIBERA #0;

     enCodCli = #0#0;
     |HAZ LeePersona;

     |SI enSwDeDonde = 1;
         |HAZ DatosFamiliar;
     |FINSI;
|FPRC;

|PROCESO MuerteTipo7;  |TIPO 7;
     |ACABA;
     aAlfa1 = #0#5;   |QBF aAlfa1;
     aAlfa2 = #0#39;  |QBF aAlfa2;
     |SI aAlfa1 ! "";  |Y aAlfa2 ! "";
         |C_M #0#90, 1, "S";
     |SINO;
         |C_M #0#90, 1, "N";
         #0#90 = "N";  |PINTA #0#90;
     |FINSI;
|FPRC;

|PROCESO MuerteTipo0;  |TIPO 0;
     |C_M #0#91, 1, "S";
     |C_M #0#92, 1, "S";
     |SI #0#90 = "N";
         |C_M #0#91, 1, "N"; #0#91 = " ";           |PINTA #0#91;
         |C_M #0#92, 1, "N"; #0#92 = "01.01.0000";  |PINTA #0#92;
                             #0#98 = 0;             |PINTA #0#98;
     |FINSI;
|FPRC;

|PROCESO TipoCalculo;  |TIPO 0;
     |SI #0#92 > "01.01.0000";
         #0#98 = 2;
     |FINSI;
     |PINTA #0#98;
|FPRC;

|PROCESO FiltraCero;
     |SI #0#92 > "01.01.0000";
         #0#98 = 2;  |PINTA #0#98;
     |FINSI;
|FPRC;

|PROCESO RegEconomico;  |TIPO 7;
     |C_M #0#93, 1, "S";
     |SI #0#6 = "S"; |O #0#6 = "V";  |O #0#6 = "L";
         |C_M #0#93, 1, "N";
         #0#93 = "";  |PINTA #0#93;
     |FINSI;
|FPRC;

|PROCESO Pantalla1;
     |PINPA #0#0;
     |PINDA #0#0;
     |MENSA "0411  Av.Pag.= Siguiente Pantalla  Re.Pag. = Pantalla Anterior";
     |LEETECLA aTecla;
     |SI aTecla = aAvance;  |HAZ Pantalla2;  |ACABA;  |FINSI;
|FPRC;

|PROCESO Pantalla2;
     |PINPA #1#0;
     |PINDA #1#0;
     |MENSA "0411  Av.Pag.= Siguiente Pantalla  Re.Pag. = Pantalla Anterior";
     |LEETECLA aTecla;
     |SI aTecla = aAtras;  |HAZ Pantalla1;  |ACABA;  |FINSI;
|FPRC;

|PROCESO Consulta;
     aAvance = &255 + aAvance;
     aAtras  = &255 + aAtras;
     #0#0   = enEmpresa;
     |LEE 040#0=;
     |SI FSalida = 0; |HAZ Pantalla1; |FINSI;
|FPRC;

|PROCESO Tipo12m100;  |TIPO 12;
     || Este proceso sirve para que el endatos que se hace en el
     || calculo dsam0000 no pregunte Correcto S/N
     || POR LO TANTO NO LO QUITEIS !!!!!!
|FPRC;

|PROGRAMA;
     |ABRE #0;
     |PINPA #0#0;
     |SI enModo ! 4; |HAZ Pidedato; |FINSI;
     |SI enModo = 4; |HAZ Consulta; |FINSI;

     eaPrograma = "rem00100";
     eaPunto    = "";
     |HAZ ProgramaActivo;

     eaNombre = #0#5;
|FPRO;
