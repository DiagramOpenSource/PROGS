|FICHEROS;
     dslpm000 #0;
     dslpm001 #1;
     dslpm011 #11,MANTE;
     dslpm521 #521;
     dslpm522 #522;
     dslpw042 #942;

     dslpm011@ #999;
|FIN;

|VARIABLES;
     aAlfa       = "";
     aMas        = "";

     nPos1       = 0;
     nPos2       = 0;
     nRango      = 0;
     nVentana    = 0;
     nGrid       = 0;
     nPanta      = 0;
     nCampoD     = 0;
     nCampoH     = 0;
     nLinea      = 0;
     nModo       = 0;
     nHay        = 0;

     &enCliente  = 0;
     &enEstado   = 0;
     &enError    = 0;
     &eaEntidad  = "";
     &eaOficina  = "";
     &eaCuenta   = "";
     &eaDC       = "";
|FIN;

|PROCESO Tipo7C17Fw42;  |TIPO 7;
     eaEntidad = #942#14;  |QBT eaEntidad;
     eaEntidad = ("0000" + eaEntidad) % -104;
     eaOficina = #942#15;  |QBT eaOficina;
     eaOficina = ("0000" + eaOficina) % -104;
     eaCuenta  = #942#16;  |QBT eaCuenta;
     eaCuenta  = ("0000000000" + eaCuenta) % -110;
     eaDC = "";
     |HAZ CalculoDC;

     #942#17 = eaDC;  |PINTA #942#17;
|FPRC;

|PROCESO UltimaLinea;
     |DBASE aAlfa; |QBF aAlfa;

     aMas = aAlfa + "def/dslpm011.mas";
     |CARGA_DEF aMas, dslpm011@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |ABRE #999;

     #999#0 = #11#0;
     #999#1 = 999;
     |LEE 000#999];
     |SI FSalida = 0;
         |LEE 000#999a;
     |SINO;
         |LEE 000#999u;
     |FINSI;

     nLinea = 1;
     |SI FSalida = 0; |Y #999#0 = #11#0;
          nLinea = #999#1 + 1;
     |FINSI;

     |CIERRA #999;

     |DESCARGA_DEF #dslpm011@;
|FPRC;

|PROCESO Tipo7m11; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          nLinea = 0;
          |HAZ UltimaLinea;
          #11#1 = nLinea;
          |PINTA #11#1;
     |FINSI;
|FPRC;

|PROCESO Tipo2m11;  |TIPO 2;
     nHay = 1;

     |ABRE #522;
     #522#0 = #11#4;
     |LEE 000#522=;
     |SI FSalida ! 0;  |DDEFECTO #522;  |FINSI;
     |CIERRA #522;

     |SI #11#4 = 0;
         #942#12 = #942#12 +. #11#8;
     |FINSI;

     |SI #11#4 = 1;
         #942#0 = #942#0 +. #11#8;
         #942#1 = #522#2;
         #942#2 = #942#0 % #942#1;
         #942#3 = #942#0 + #942#2;
     |FINSI;

     |SI #11#4 = 2;
         #942#4 = #942#4 +. #11#8;
         #942#5 = #522#2;
         #942#6 = #942#4 % #942#5;
         #942#7 = #942#4 + #942#6;
     |FINSI;

     |SI #11#4 = 3;
         #942#8  = #942#8 +. #11#8;
         #942#9  = #522#2;
         #942#10 = #942#8 % #942#9;
         #942#11 = #942#8 + #942#10;
     |FINSI;

     #942#13 = #942#3 + #942#7 + #942#11 + #942#12;

     |PINTA #942#0;
     |PINTA #942#1;
     |PINTA #942#2;
     |PINTA #942#3;
     |PINTA #942#4;
     |PINTA #942#5;
     |PINTA #942#6;
     |PINTA #942#7;
     |PINTA #942#8;
     |PINTA #942#9;
     |PINTA #942#10;
     |PINTA #942#11;
     |PINTA #942#12;
     |PINTA #942#13;
|FPRC;

|PROCESO Importe;
     #11#8 = #11#6 * #11#7;
     |PINTA #11#8;
|FPRC;

|PROCESO Minimo;
     |ABRE #521;
     #521#0 = #11#2;
     |LEE 000#521=;
     |SI FSalida ! 0;  |DDEFECTO #521;  |FINSI;
     |CIERRA #521;

     |SI #11#7 < #521#5;
         |MENAV "0000 El Precio introducido es menor al precio minimo del Articulo";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Baja11;
     #11#0 = #1#1;
     #11#1 = 1;
|FPRC;

|PROCESO Alta11;
     #11#0 = #1#1;
     #11#1 = 99;
|FPRC;

|PROCESO Evento11;
     |SI FSalida = 1;
         |LEE 000#11c;
     |FINSI;
|FPRC;

|DEFBUCLE dslpm011;
     #11#1;
     ;
     #1#1, 01;
     #1#1, 99;
     ;
     NULL, Tipo2m11;
|FIN;

|PROCESO GrabaDefecto;
     aAlfa  = #0#24;  |QBF aAlfa;
     |SI aAlfa = "";  |ACABA;  |FINSI;

     |ABRE #521;
     #521#0 = #0#24;
     |LEE 000#521=;
     |SI FSalida ! 0;  |DDEFECTO #521;  |FINSI;
     |CIERRA #521;

     #11#0  = #1#1;
     #11#1  = 1;
     #11#2  = #521#0;
     #11#3  = #521#1;
     #11#4  = #521#2;
     #11#5  = #521#3;
     #11#6  = 1;
     #11#7  = #521#4;
     #11#8  = #521#4;
     |GRABA 020#11n;
     |LIBERA #11;

     |BUCLE dslpm011;
|FPRC;

|PROCESO Tipo12Fw42;  |TIPO 12;
|FPRC;

|PROCESO Tipo80Fw42;
     FSalida = 2899;
|FPRC;

|PROGRAMA;
     |ABRE #0;
     |LEE 000#0p;
     |SI FSalida ! 0;  |DDEFECTO #0;  |FINSI;
     |CIERRA #0;

     |ABRE #1;
     #1#0 = enEstado;
     #1#1 = enCliente;
     |LEE 101#1=;
     |SI FSalida ! 0;  |CIERRA #1;  |ACABA;  |FINSI;

     |VENTANA 0501, 2899, -1, 1, "";
     nVentana = FSalida;

     |ABRET #11;

     |PINPA #0#942;  nPanta = FSalida;

     |PARA nCampoD = 0;  |SI nCampoD [ 13;  |HACIENDO nCampoD = nCampoD + 1;
           #942nCampoD = 0;
     |SIGUE;

     #942#14  = #1#40;
     #942#15  = #1#41;
     #942#16  = #1#42;
     #942#17  = #1#43;

     nHay = 0;
     |BUCLE dslpm011;
     |SI nHay = 0;
         |HAZ GrabaDefecto;
     |FINSI;

     |PINDA #0#942;

     nPos1       = 0902;
     nPos2       = 1798;

     |SI enEstado [ 33;
         nRango = 4 + 8 + 16 + 32;
         |LINEAL_SIMPLE #1#11, nRango, nPos1, nPos2, Baja11, Alta11, Evento11;
         nGrid = FSalida;

         |ENDATOS #1#942;

         |DESTRUYECONTROL nGrid;
     |FINSI;

     |SI enEstado [ 33;
         nRango = 1 + 4 + 8 + 16;
     |SINO;
         nRango = 4 + 8 + 16;
     |FINSI;

     |LINEAL_SIMPLE #1#11, nRango, nPos1, nPos2, Baja11, Alta11, Evento11;
     nGrid = FSalida;
     |CIERRAT #11;

     |SI enEstado [ 33;
         |CONFI "0000SValoracion correcta? ";
         enError = FSalida;
     |SINO;
         enError = 0;
     |FINSI;

     |SI enError = 0;
         #1#36 = #942#13;
         #1#40 = #942#14;
         #1#41 = #942#15;
         #1#42 = #942#16;
         #1#43 = #942#17;
     |FINSI;

     |GRABA 020#1a;
     |LIBERA #1;
     |CIERRA #1;
     |FINVENTANA nVentana;
|FPRO;
