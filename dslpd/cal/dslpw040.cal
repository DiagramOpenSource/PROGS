|FICHEROS;
     dslpm001 #1;
     dslpm008 #8;
     dslpm505 #505;

     dslpw040 #940;
     dslpw041 #941;
|FIN;

|VARIABLES;
     aCampo0        = "";
     aCampo1        = "";
     aCampo2        = "";
     aCampo3        = "";
     aCampo4        = "";
     aCampo5        = "";
     aCampo6        = "";
     aCampo7        = "";
     aCampo8        = "";
     aCampo9        = "";
     aCampo10       = "";
     aCampo11       = "";
     aCampo12       = "";
     aCampo13       = "";
     aCampo14       = "";
     aCampo15       = "";
     aAbre          = "";
     aDirServ       = "";
     aDirFtp        = "";
     aDirRecoge     = "";
     aTXT           = "";
     aBase          = "";
     aBaseDoc       = "";
     aPDF           = "";
     aAlfa          = "";
     aCliente       = "";
     aHora          = "";
     aOrigen        = "";
     aDestino       = "";
     aIPRemoto      = "";
     aPos           = "";
     aCadena        = "";
     aFichero       = "";
     aTecla         = "";
     aCarpeta       = "";
     aQueBoton      = "";
     aID            = "";

     nBotonAceptar  = 0;
     nBotonCancelar = 0;
     nHandle        = 0;
     nDocu          = 0;
     nVentana       = 0;
     nPanta         = 0;
     nCampoD        = 0;
     nCampoH        = 0;
     nPos           = 0;
     nValidar       = 0;
     nConta         = 0;
     nPos1          = 0;
     nPos2          = 0;
     nRango         = 0;
     nCampo         = 0;
     nCarpeta       = 0;

     nBoton1        = 0;
     nBoton2        = 0;
     nBoton3        = 0;
     nBoton4        = 0;
     nBoton5        = 0;
     nBoton6        = 0;
     nBoton7        = 0;

     &enCliente     = 0;
     &enEstado      = 0;
     &enError       = 0;
     &eaAlfa        = "";
|FIN;

|PROCESO Tipo12;  |TIPO 12;
     #1#18 = #940#0;
     |PARA nCampoD = 1;  |SI nCampoD [ 15;  |HACIENDO nCampoD = nCampoD + 1;
           nCampoH     = nCampoD + 20;
           #1nCampoH   = #940nCampoD;
     |SIGUE;
|FPRC;

|PROCESO Tipo7Fw40;  |TIPO 7;
     aQueBoton     = "|X000";
     aID           = #940#aQueBoton#;
     nBotonAceptar = aID;
     |ESTADO_CONTROL nBotonAceptar, "HIDE";

     aQueBoton      = "|X001";
     aID            = #940#aQueBoton#;
     nBotonCancelar = aID;
     |ESTADO_CONTROL nBotonCancelar, "HIDE";
|FPRC;

|PROCESO Web;
     |C_M #940#7, 1, "S";
     |SI #940#6 = "N";
         |C_M #940#7, 1, "N";  #940#7 = "";  |PINTA #940#7;
     |FINSI;
|FPRC;

|PROCESO Otros;
     |C_M #940#15, 1, "S";
     |SI #940#14 = "N";
         |C_M #940#15, 1, "N";  #940#15 = "";  |PINTA #940#15;
     |FINSI;
|FPRC;

|PROCESO Boton1;
     nValidar = 1;
     enError  = 1;
     |PTEC 807;
|FPRC;

|PROCESO Boton2;
     nValidar = 1;
     enError  = 0;
     |PTEC 806;
|FPRC;

|PROCESO Evento505;
     |SI FSalida = 1;
         |LEE 000#505c;
     |FINSI;

     |SI FSalida = 4;
         |PTEC 806;
     |FINSI;
|FPRC;

|PROCESO Boton3;
     nRango = 4 + 8 + 16;
     nPos1  = 0902;
     nPos2  = 2398;

     |ABRE #505;
     |SELECT #2#505;
     #505#1 = #940#0;
     |LEE 000#505=;

     |LINEAL_SIMPLE #2#505, nRango, nPos1, nPos2, NULL, NULL, Evento505;

     #940#0 = #505#1;
     |PINTA #940#0;
     |CIERRA #505;
|FPRC;

|PROCESO Boton4;
     nRango = 4 + 8 + 16;
     nPos1  = 0902;
     nPos2  = 2398;

     |ABRE #505;
     |SELECT #2#505;
     #505#1 = #940#1;
     |LEE 000#505=;

     |LINEAL_SIMPLE #2#505, nRango, nPos1, nPos2, NULL, NULL, Evento505;

     #940#1 = #505#1;
     |PINTA #940#1;
     |CIERRA #505;
|FPRC;

|PROCESO Evento941;
     |SI FSalida = 1;
         |LEE 000#941c;
     |FINSI;

     |SI FSalida = 4;
         |PTEC 806;
     |FINSI;
|FPRC;

|PROCESO Boton5;
     aFichero = ("41" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #941 aFichero;

     |ABRE #941;  |CIERRA #941;  |DELINDEX #941;

     |ABRE #941;

     #941#0 = "";             |GRABA 020#941n;  |LIBERA #941;
     #941#0 = " 1";           |GRABA 020#941n;  |LIBERA #941;
     #941#0 = " 2";           |GRABA 020#941n;  |LIBERA #941;
     #941#0 = " 3";           |GRABA 020#941n;  |LIBERA #941;
     #941#0 = " 4";           |GRABA 020#941n;  |LIBERA #941;
     #941#0 = " 5";           |GRABA 020#941n;  |LIBERA #941;
     #941#0 = " 6";           |GRABA 020#941n;  |LIBERA #941;
     #941#0 = " 7";           |GRABA 020#941n;  |LIBERA #941;
     #941#0 = " 8";           |GRABA 020#941n;  |LIBERA #941;
     #941#0 = " 9";           |GRABA 020#941n;  |LIBERA #941;
     #941#0 = "10";           |GRABA 020#941n;  |LIBERA #941;
     #941#0 = "M s de 10";    |GRABA 020#941n;  |LIBERA #941;

     nRango = 4 + 8 + 16;
     nPos1  = 1075;
     nPos2  = 2198;

     #941#0 = #940#2;
     |LEE 000#941=;

     |LINEAL_SIMPLE #1#941, nRango, nPos1, nPos2, NULL, NULL, Evento941;

     #940#2 = #941#0;
     |PINTA #940#2;

     |DELINDEX #941;
|FPRC;

|PROCESO Boton6;
     aFichero = ("41" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #941 aFichero;

     |ABRE #941;  |CIERRA #941;  |DELINDEX #941;

     |ABRE #941;

     #941#0 = "";             |GRABA 020#941n;  |LIBERA #941;
     #941#0 = "De 1 a 5";     |GRABA 020#941n;  |LIBERA #941;
     #941#0 = "De 6 a 15";    |GRABA 020#941n;  |LIBERA #941;
     #941#0 = "M s de 15";    |GRABA 020#941n;  |LIBERA #941;

     nRango = 4 + 8 + 16;
     nPos1  = 1075;
     nPos2  = 2198;

     #941#0 = #940#3;
     |LEE 000#941=;

     |LINEAL_SIMPLE #1#941, nRango, nPos1, nPos2, NULL, NULL, Evento941;

     #940#3 = #941#0;
     |PINTA #940#3;

     |DELINDEX #941;
|FPRC;

|PROCESO Boton7;
     aFichero = ("41" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #941 aFichero;

     |ABRE #941;  |CIERRA #941;  |DELINDEX #941;

     |ABRE #941;

     #941#0 = "";             |GRABA 020#941n;  |LIBERA #941;

     |PARA nCampo = 1;  |SI nCampo [ 100;  |HACIENDO nCampo = nCampo + 1;
           #941#0 = ("   " + nCampo) % -103;
           |GRABA 020#941n;
           |LIBERA #941;
     |SIGUE;

     #941#0 = "M s de 100";  |GRABA 020#941n;  |LIBERA #941;

     nRango = 4 + 8 + 16;
     nPos1  = 1075;
     nPos2  = 2198;

     #941#0 = #940#4;
     |LEE 000#941=;

     |LINEAL_SIMPLE #1#941, nRango, nPos1, nPos2, NULL, NULL, Evento941;

     #940#4 = #941#0;
     |PINTA #940#4;

     |DELINDEX #941;
|FPRC;

|PROCESO RecogeDatosTxt;
     aCampo0  = "";
     aCampo1  = "";
     aCampo2  = "";
     aCampo3  = "";
     aCampo4  = "";
     aCampo5  = "";
     aCampo6  = "";
     aCampo7  = "";
     aCampo8  = "";
     aCampo9  = "";
     aCampo10 = "";
     aCampo11 = "";
     aCampo12 = "";
     aCampo13 = "";
     aCampo14 = "";
     aCampo15 = "";

     aAbre = aDirServ + "/txt" + (("00000" + #8#1) % -105) + ".txt";
     |XABRE "U", aAbre, nHandle;
     |SI FSalida ] 0;
         |PARA;  |SI FSalida ] 0;  |HACIENDO;
              |XLEE nHandle, 250, aAlfa;
              |SI FSalida < 0;  |SAL;  |FINSI;

              aCadena = aAlfa - "Actividad/Objeto Social: ";
              |SI FSalida ! 0;
                  aCadena = aCadena - "/";
                  aPos  = FSalida;
                  aPos  = aPos % 101;
                  nPos  = aPos;
                  nPos  = (nPos * 100) + 90;

                  aCampo0 = aCadena % nPos;
              |FINSI;

              aCadena = aAlfa - "Otras Actividades Secundarias: ";
              |SI FSalida ! 0;
                  aCadena = aCadena - "/";
                  aPos  = FSalida;
                  aPos  = aPos % 101;
                  nPos  = aPos;
                  nPos  = (nPos * 100) + 90;

                  aCampo1 = aCadena % nPos;
              |FINSI;

              aCadena = aAlfa - "Centros Administrativos: ";
              |SI FSalida ! 0;
                  aCampo2 = aCadena;
              |FINSI;

              aCadena = aAlfa - "Personas que manejan Información: ";
              |SI FSalida ! 0;
                  aCampo3 = aCadena;
              |FINSI;

              aCadena = aAlfa - "Ordenadores: ";
              |SI FSalida ! 0;
                  aCampo4 = aCadena;
              |FINSI;

              aCadena = aAlfa - "Conexión a Internet: ";
              |SI FSalida ! 0;
                  aCampo5 = aCadena;
              |FINSI;

              aCadena = aAlfa - "Página Web: ";
              |SI FSalida ! 0;
                  aCadena = aAlfa - "Dirección";
                  |SI FSalida = 0;
                      aCadena = aAlfa - "Página Web: ";
                      aCampo6 = aCadena;
                  |FINSI;
              |FINSI;

              aCadena = aAlfa - "Dirección Página Web: ";
              |SI FSalida ! 0;
                  aCampo7 = aCadena;
              |FINSI;

              aCadena = aAlfa - "Asesoría: ";
              |SI FSalida ! 0;
                  aCampo8 = aCadena;
              |FINSI;

              aCadena = aAlfa - "Mantenimiento Informático: ";
              |SI FSalida ! 0;
                  aCampo9 = aCadena;
              |FINSI;

              aCadena = aAlfa - "Limpieza: ";
              |SI FSalida ! 0;
                  aCampo10 = aCadena;
              |FINSI;

              aCadena = aAlfa - "Servicios Bancarios: ";
              |SI FSalida ! 0;
                  aCampo11 = aCadena;
              |FINSI;

              aCadena = aAlfa - "Prevención Riesgos Laborales: ";
              |SI FSalida ! 0;
                  aCampo12 = aCadena;
              |FINSI;

              aCadena = aAlfa - "Mutua de Trabajadores: ";
              |SI FSalida ! 0;
                  aCampo13 = aCadena;
              |FINSI;

              aCadena = aAlfa - "Otros: ";
              |SI FSalida ! 0;
                  aCadena = aAlfa - "Descripción ";
                  |SI FSalida = 0;
                      aCadena  = aAlfa - "Otros: ";
                      aCampo14 = aCadena;
                  |FINSI;
              |FINSI;

              aCadena = aAlfa - "Descripción Otros: ";
              |SI FSalida ! 0;
                  aCampo15 = aCadena;
              |FINSI;
          |SIGUE;
     |FINSI;
     |XCIERRA nHandle;

     eaAlfa = aCampo0;  |HAZ PonRaros;  #1#18    = eaAlfa;
     eaAlfa = aCampo1;  |HAZ PonRaros;  #1#21    = eaAlfa;
     eaAlfa = aCampo2;  |HAZ PonRaros;  #1#22    = eaAlfa;
     eaAlfa = aCampo3;  |HAZ PonRaros;  #1#23    = eaAlfa;
     eaAlfa = aCampo4;  |HAZ PonRaros;  #1#24    = eaAlfa;
     eaAlfa = aCampo5;  |HAZ PonRaros;  aCampo5  = eaAlfa;
     eaAlfa = aCampo6;  |HAZ PonRaros;  aCampo6  = eaAlfa;
     eaAlfa = aCampo7;  |HAZ PonRaros;  #1#27    = eaAlfa;
     eaAlfa = aCampo8;  |HAZ PonRaros;  aCampo8  = eaAlfa;
     eaAlfa = aCampo9;  |HAZ PonRaros;  aCampo9  = eaAlfa;
     eaAlfa = aCampo10; |HAZ PonRaros;  aCampo10 = eaAlfa;
     eaAlfa = aCampo11; |HAZ PonRaros;  aCampo11 = eaAlfa;
     eaAlfa = aCampo12; |HAZ PonRaros;  aCampo12 = eaAlfa;
     eaAlfa = aCampo13; |HAZ PonRaros;  aCampo13 = eaAlfa;
     eaAlfa = aCampo14; |HAZ PonRaros;  aCampo14 = eaAlfa;
     eaAlfa = aCampo15; |HAZ PonRaros;  #1#35    = eaAlfa;

     aAlfa  = #1#22;  |QBF aAlfa;
     aAlfa  = aAlfa - "10";
     |SI FSalida = 0;
         |QBT aAlfa;
         #1#22 = ("  " + aAlfa) % -102;
     |FINSI;

     aAlfa  = #1#24;  |QBF aAlfa;
     aAlfa  = aAlfa - "100";
     |SI FSalida = 0;
         |QBT aAlfa;
         #1#24 = ("   " + aAlfa) % -103;
     |FINSI;

     #1#25  = "N";  |SI aCampo5  = "S¡";  #1#25 = "S";  |FINSI;
     #1#26  = "N";  |SI aCampo6  = "S¡";  #1#26 = "S";  |FINSI;
     #1#28  = "N";  |SI aCampo8  = "S¡";  #1#28 = "S";  |FINSI;
     #1#29  = "N";  |SI aCampo9  = "S¡";  #1#29 = "S";  |FINSI;
     #1#30  = "N";  |SI aCampo10 = "S¡";  #1#30 = "S";  |FINSI;
     #1#31  = "N";  |SI aCampo11 = "S¡";  #1#31 = "S";  |FINSI;
     #1#32  = "N";  |SI aCampo12 = "S¡";  #1#32 = "S";  |FINSI;
     #1#33  = "N";  |SI aCampo13 = "S¡";  #1#33 = "S";  |FINSI;
     #1#34  = "N";  |SI aCampo14 = "S¡";  #1#34 = "S";  |FINSI;
|FPRC;

|PROCESO RecogeFormulario;
     |DBASS aBase;  |QBF aBase;

     aAlfa      = "C:\DOCUMENTOS";                     |RMKDIR aAlfa;
     aAlfa      = "C:\DOCUMENTOS\DSLPD";               |RMKDIR aAlfa;
     aAlfa      = "C:\DOCUMENTOS\DSLPD\RECOGE";        |RMKDIR aAlfa;
     aDirFtp    = aAlfa;
     aDirRecoge = aAlfa + "\";
     aCarpeta   = aBase % -204;
     nCarpeta   = aCarpeta;

     |SI nCarpeta = 0;  aCarpeta = "9999";  |FINSI;

     aTXT       = aCarpeta + (("00000" + #1#1) % -105) + "_info.txt";
     aPDF       = aCarpeta + (("00000" + #1#1) % -105) + "_info.pdf";

     aAbre = aDirRecoge + "dslpd.ftp";
     |XABRE "CWB", aAbre, nHandle;
     |SI FSalida ] 0;
         aAlfa = "open www.geconet.net"                  + &13 + &10;  |XGRABA nHandle, aAlfa;
         aAlfa = "user efetepe jordiet"                  + &13 + &10;  |XGRABA nHandle, aAlfa;
         aAlfa = "bin"                                   + &13 + &10;  |XGRABA nHandle, aAlfa;
         aAlfa = "cd /usr/local/www/data/dslpd/ficheros" + &13 + &10;  |XGRABA nHandle, aAlfa;
         aAlfa = "lcd " + aDirFtp                        + &13 + &10;  |XGRABA nHandle, aAlfa;
         aAlfa = "mget " + aTXT                          + &13 + &10;  |XGRABA nHandle, aAlfa;
         aAlfa = "mget " + aPDF                          + &13 + &10;  |XGRABA nHandle, aAlfa;
         aAlfa = "by"                                    + &13 + &10;  |XGRABA nHandle, aAlfa;
     |FINSI;
     |XCIERRA nHandle;

     nConta = 1;
     |PARA;  |SI;  |HACIENDO;
          |SI nConta > 3;  |SAL;  |FINSI;

          aAlfa = "ftp -i -n -s:" + aDirRecoge + "dslpd.ftp";
          |RSYSTEM aAlfa;

          aAlfa = aDirRecoge + aPDF;
          |XABRE "EC", aAlfa, nHandle;
          |SI FSalida ] 0;
              enError = 0;
              |SAL;
          |SINO;
              nConta = nConta + 1;
              |SLEEP 2;
          |FINSI;
     |SIGUE;

     |RFBORRA aAbre;

     |SI enError = 1;  |ACABA;  |FINSI;

     aCliente = ("00000" + #1#1) % -105;

     |DBASE aBaseDoc;  |QBF aBaseDoc;
     aDirServ = aBaseDoc + "documentos";                  |MKDIR aDirServ;
     aDirServ = aDirServ + "/clientes";                   |MKDIR aDirServ;
     aDirServ = aDirServ + "/" + aCliente;                |MKDIR aDirServ;

     |HORA aHora;

     |ABRE #8;
     #8#0 = #1#1;
     #8#1 = 99999;
     |LEE 040#8];
     |SI FSalida = 0;
         |LEE 040#8a;
     |SINO;
         |LEE 040#8u;
     |FINSI;

     nDocu = 1;
     |SI FSalida = 0;  |Y  #8#0 = #1#1;
         nDocu = #8#1 + 1;
     |FINSI;

     |DDEFECTO #8;
     #8#0  = #1#1;
     #8#1  = nDocu;
     #8#2  = "Formulario";

     #8#3  = 0;
     #8#4  = "";
     #8#5  = 0;
     #8#6  = "";
     #8#7  = "";
     #8#8  = ~;
     #8#9  = aHora;
     #8#10 = Usuario % 810;
     #8#11 = #8#8;
     #8#12 = #8#9;
     #8#13 = #8#10;
     #8#14 = 0;
     #8#15 = 0;
     #8#16 = "PDF";

     |GRABA 020#8n;
     |LIBERA #8;

     aOrigen  = aDirRecoge + aTXT;
     aDestino = aDirServ + "/txt" + (("00000" + #8#1) % -105) + ".txt";

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |SI aIPRemoto ! "";
         |RECIBE_FICHERO aOrigen, aDestino;
     |SINO;
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;

     |RFBORRA aOrigen;

     aOrigen  = aDirRecoge + aPDF;
     aDestino = aDirServ + "/pdf" + (("00000" + #8#1) % -105) + ".pdf";

     |SI aIPRemoto ! "";
         |RECIBE_FICHERO aOrigen, aDestino;
     |SINO;
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;

     |RFBORRA aOrigen;

     |HAZ RecogeDatosTxt;
|FPRC;

|PROCESO Modificacion;
     enError = 1;
     |HAZ RecogeFormulario;

     |SI enError = 1;
         |CONFI "0000NNo esta el formulario, desea crearlo manualmente";
         |SI FSalida ! 0;
             |LIBERA #1;
             |CIERRA #1;
             |ACABA;
         |FINSI;
     |FINSI;

     enError = 1;
     |VENTANA 0501, 2899, -1, 1, "";
     nVentana = FSalida;

     |PINPA #0#940;  nPanta = FSalida;

     #940#0 = #1#18;
     |PARA nCampoD = 1;  |SI nCampoD [ 15;  |HACIENDO nCampoD = nCampoD + 1;
           nCampoH     = nCampoD + 20;
           #940nCampoD = #1nCampoH;
     |SIGUE;

     |PINDA #0#940;

     |CONTROL_SIMPLE nPanta, "BOTON,{{207}} Cancelar", 2788, 2798, Boton1;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{206}} Validar Datos", 2773, 2786, Boton2;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{155}}", 1096, 1098, Boton3;
     nBoton3 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{155}}", 1196, 1198, Boton4;
     nBoton4 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{155}}", 1375, 1377, Boton5;
     nBoton5 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{155}}", 1475, 1477, Boton6;
     nBoton6 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{155}}", 1575, 1577, Boton7;
     nBoton7 = FSalida;

     |PARA;  |SI;  |HACIENDO;
          |ENDATOS #1#940;
          |SI nValidar = 1;  |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;
     |FIN_CONTROL_WINDOWS nBoton4;
     |FIN_CONTROL_WINDOWS nBoton5;
     |FIN_CONTROL_WINDOWS nBoton6;
     |FIN_CONTROL_WINDOWS nBoton7;

     |SI enError = 0;
         |GRABA 020#1a;
     |FINSI;

     |LIBERA #1;
     |CIERRA #1;
     |FINVENTANA nVentana;
|FPRC;

|PROCESO Consulta;
     |VENTANA 0501, 2899, -1, 1, "";
     nVentana = FSalida;

     |PINPA #0#940;  nPanta = FSalida;

     #940#0 = #1#18;
     |PARA nCampoD = 1;  |SI nCampoD [ 15;  |HACIENDO nCampoD = nCampoD + 1;
           nCampoH     = nCampoD + 20;
           #940nCampoD = #1nCampoH;
     |SIGUE;

     |PINDA #0#940;

     FSalida = "::Salir";
     |LEETECLA aTecla;

     |LIBERA #1;
     |CIERRA #1;
     |FINVENTANA nVentana;
|FPRC;

|PROCESO Tipo80Fw40;
     FSalida = 2899;
|FPRC;

|PROGRAMA;
     |ABRE #1;
     #1#0 = enEstado;
     #1#1 = enCliente;
     |LEE 101#1=;
     |SI FSalida ! 0;  |CIERRA #1;  |ACABA;  |FINSI;

     |SI #1#0 = 31;
         |HAZ Modificacion;
     |SINO;
         |HAZ Consulta;
     |FINSI;
|FPRO;
