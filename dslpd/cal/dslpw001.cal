|FICHEROS;
     dslpm001 #1;
     dslpm002 #2;
     dslpm004 #4;
     dslpm005 #5;
     dslpm006 #6;
     dslpm007 #7;
     dslpm012 #12;
     dslpm100 #100;
     dslpm505 #505;
     dslpw001 #901;

     :/gecochek/def/gecom017 #117;
|FIN;

|VARIABLES;
     aEsc1          = "";
     aEsc2          = "";
     aRetu          = "";
     aTecla         = "";
     aTeclaSub      = "";
     aAlfa          = "";

     nBoton1_1      = 0;
     nBoton2_1      = 0;
     nBoton2_2      = 0;
     nBoton2_3      = 0;
     nBoton2_4      = 0;
     nBoton2_5      = 0;
     nBoton4_1      = 0;
     nBoton4_2      = 0;
     nBoton4_3      = 0;

     nPanta0        = 0;
     nPanta1        = 0;
     nPanta2        = 0;
     nPanta3        = 0;
     nCampoD        = 0;
     nCampoH        = 0;
     nRango         = 0;
     nGrid1         = 0;
     nGrid2         = 0;
     nPos1          = 0;
     nPos2          = 0;
     nVentana       = 0;

     &enCliente     = 0;
     &enModelo      = 0;
     &eaNIF         = "";
     &enModo        = 1;
     &eaTabla       = "";
     &eaDestino     = "";
     &enPosicion    = 0;
     &enEstado      = 0;
     &enVarios      = 0;
     &enFichero = 0;
|FIN;

|PROCESO BajaPanta2;
     #2#0  = #901#0;
     #2#1  = "";
|FPRC;

|PROCESO AltaPanta2;
     #2#0  = #901#0;
     #2#1  = "zzzzzzzzzzzz";
|FPRC;

|PROCESO BajaPanta3;
     #4#0  = #901#0;
     #4#1  = 1;
|FPRC;

|PROCESO AltaPanta3;
     #4#0  = #901#0;
     #4#1  = 9999;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 1_1
|| ------------------------------------------------------

|PROCESO Evento505;
     |SI FSalida = 1;
         |LEE 000#505c;
     |FINSI;

     |SI FSalida = 4;
         |PTEC 806;
     |FINSI;
|FPRC;

|PROCESO Boton1_1;
     |ESTADO_CONTROL nBoton1_1, "DISABLE";

     nRango = 4 + 8 + 16;
     nPos1  = 0602;
     nPos2  = 2098;

     |ABRE #505;
     |SELECT #2#505;
     #505#1 = #901#17;
     |LEE 000#505=;

     |LINEAL_SIMPLE #2#505, nRango, nPos1, nPos2, NULL, NULL, Evento505;

     #901#17 = #505#1;
     |PINTA #901#17;
     |CIERRA #505;

     |ESTADO_CONTROL nBoton1_1, "ENABLE";
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 2_1
|| ------------------------------------------------------

|PROCESO Boton2_1;
     |VENTANA 0501, 2799, -1, 1, "";
     nVentana = FSalida;

     enCliente = #901#0;
     eaNIF     = "";
     enModo    = 1;
     |SUB_EJECUTA "dslpw002";

     |FINVENTANA nVentana;

     #2#0 = enCliente;
     #2#1 = eaNIF;
     |LEE 000#2=;

     |REFRESCACONTROL nGrid1;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 2_2
|| ------------------------------------------------------

|PROCESO Boton2_2;
     |VENTANA 0501, 2799, -1, 1, "";
     nVentana = FSalida;

     enCliente = #901#0;
     eaNIF     = #2#1;
     enModo    = 2;
     |SUB_EJECUTA "dslpw002";

     |FINVENTANA nVentana;

     |REFRESCACONTROL nGrid1;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 2_3
|| ------------------------------------------------------

|PROCESO Boton2_3;
     aAlfa = "0000N" + &191 + " Esta vd.seguro de dar de baja a " + #2#2 +" ?";
     |CONFI aAlfa;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |LEE 101#2=;
     |SI FSalida = 0;
         |BORRA 020#2a;
         |LIBERA #2;
     |FINSI;

     |REFRESCACONTROL nGrid1;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 2_4
|| ------------------------------------------------------

|PROCESO RegrabaNif;
     #100#0 = #2#1;
     |LEE 101#100=;
     |SI FSalida ! 0;
         |DDEFECTO #100;
         #100#0 = #2#1;
         |GRABA 020#100b;
     |FINSI;

     #100#1  = #2#2;
     #100#2  = #2#3;
     #100#3  = #2#4;
     #100#4  = #2#5;
     #100#5  = #2#6;
     #100#6  = #2#7;
     #100#7  = #2#8;
     #100#8  = #2#9;
     #100#9  = #2#10;
     #100#10 = #2#11;
     #100#11 = #2#12;
     #100#12 = #2#13;
     #100#13 = #2#14;
     #100#14 = #2#15;
     #100#15 = #2#28;
     |GRABA 020#100a;
     |LIBERA #100;
|FPRC;

|PROCESO gecom017;
     #2#0  = #117#0;
     #2#1  = #117#1;
     |LEE 101#2=;
     |SI FSalida = 0;
         #2#28 = #117#18;

         |SI #117#2 = "C";  #2#34 = "S";  |FINSI;
         |SI #117#2 = "P";  #2#34 = "S";  |FINSI;
         |SI #117#2 = "T";  #2#32 = "S";  |FINSI;
         |GRABA 020#2a;
         |LIBERA #2;

         |HAZ RegrabaNif;
         |ACABA;
     |FINSI;

     |DDEFECTO #2;
     #2#0  = #117#0;
     #2#1  = #117#1;
     #2#2  = #117#3;
     #2#3  = #117#4;
     #2#4  = #117#5;
     #2#5  = #117#6;
     #2#6  = #117#7;
     #2#7  = #117#8;
     #2#8  = #117#9;
     #2#9  = #117#10;
     #2#10 = #117#11;
     #2#11 = #117#12;
     #2#12 = #117#13;
     #2#13 = #117#15;
     #2#14 = #117#16;
     #2#15 = #117#17;
     #2#28 = #117#18;

     |SI #117#2 = "C";  #2#34 = "S";  |FINSI;
     |SI #117#2 = "P";  #2#34 = "S";  |FINSI;
     |SI #117#2 = "T";  #2#32 = "S";  |FINSI;
     |GRABA 020#2n;
     |LIBERA #2;

     |HAZ RegrabaNif;
|FPRC;

|DEFBUCLE gecom017;
     #117#1;
     ;
     #1#1, "            ", " ";
     #1#1, "zzzzzzzzzzzz", "z";
     ;
     NULL, gecom017;
|FIN;

|PROCESO Boton2_4;
     ||CONFI "0000SSeguro de Importar Registros Inexistentes de AdmonNet. ?";
     aAlfa = "0000S" + &191 + " Esta vd.seguro de importar registros inexistentes de admonNet. ?";
     |CONFI aAlfa;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |ABRE #100;
     |BUCLE gecom017;
     |CIERRA #100;

     |REFRESCACONTROL nGrid1;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 2_5
|| ------------------------------------------------------

|PROCESO Boton2_5;
     |VENTANA 0501, 2799, -1, 1, "";
     nVentana = FSalida;

     |ESTADO_CONTROL nBoton2_5, "DISABLE";
     enCliente = #901#0;
     enVarios  = 1;
     |SUB_EJECUTA "dslpz001";
     enVarios  = 0;
     |ESTADO_CONTROL nBoton2_5, "ENABLE";

     |FINVENTANA nVentana;

     |REFRESCACONTROL nGrid1;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 4_1
|| ------------------------------------------------------

|PROCESO Boton4_1;
     |VENTANA 0501, 2799, -1, 1, "";
     nVentana = FSalida;

     enCliente = #901#0;
     enModelo  = 0;
     enModo    = 1;
     |SUB_EJECUTA "dslpw003";

     |FINVENTANA nVentana;

     |REFRESCACONTROL nGrid2;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 4_2
|| ------------------------------------------------------

|PROCESO Boton4_2;
     |VENTANA 0501, 2799, -1, 1, "";
     nVentana = FSalida;

     enCliente = #901#0;
     enModelo  = #4#1;
     enModo    = 2;
     |SUB_EJECUTA "dslpw003";

     |FINVENTANA nVentana;

     |REFRESCACONTROL nGrid2;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 4_3
|| ------------------------------------------------------

|PROCESO dslpm005;
     |BORRA 020#5a;
     |LIBERA #5;
|FPRC;

|DEFBUCLE dslpm005;
     #5#1;
     ;
     #4#0, #4#1, 00001;
     #4#0, #4#1, 99999;
     be;
     NULL, dslpm005;
|FIN;

|PROCESO dslpm012;
     |BORRA 020#12a;
     |LIBERA #12;
|FPRC;

|DEFBUCLE dslpm012;
     #12#1;
     ;
     #4#0, #4#1, 00001;
     #4#0, #4#1, 99999;
     be;
     NULL, dslpm012;
|FIN;

|PROCESO dslpm006;
     |BORRA 020#6a;
     |LIBERA #6;
|FPRC;

|DEFBUCLE dslpm006;
     #6#1;
     ;
     #4#0, #4#1, 00001, "   ";
     #4#0, #4#1, 99999, "zzz";
     be;
     NULL, dslpm006;
|FIN;

|PROCESO dslpm007;
     |BORRA 020#7a;
     |LIBERA #7;
|FPRC;

|DEFBUCLE dslpm007;
     #7#1;
     ;
     #4#0, #4#1, 00001, "        ";
     #4#0, #4#1, 99999, "zzzzzzzz";
     be;
     NULL, dslpm007;
|FIN;

|PROCESO Boton4_3;
     aAlfa = "0000N" + &191 + " Esta vd.seguro de dar de baja a " + #4#2 + " ?";
     |CONFI aAlfa;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |LEE 101#4=;
     |SI FSalida = 0;
         |BUCLE dslpm005;
         |BUCLE dslpm012;
         |BUCLE dslpm006;
         |BUCLE dslpm007;

         |BORRA 020#4a;
         |LIBERA #4;
     |FINSI;

     |REFRESCACONTROL nGrid2;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Incio del Programa
|| ------------------------------------------------------

|PROCESO EventoFoco2;
     |FOCOTECLADO nGrid1;
|FPRC;

|PROCESO EventoFoco3;
     |FOCOTECLADO nGrid2;
|FPRC;

|PROCESO EventoPanta2;
     |SI FSalida = 1;  |O FSalida = 2;
         |ESTADO_CONTROL nBoton2_1, "ENABLE";

         |LEE 000#2=;
         |SI FSalida = 0;
             |ESTADO_CONTROL nBoton2_2, "ENABLE";
             |ESTADO_CONTROL nBoton2_3, "ENABLE";
             |ESTADO_CONTROL nBoton2_4, "ENABLE";
             |ESTADO_CONTROL nBoton2_5, "ENABLE";
         |SINO;
             |ESTADO_CONTROL nBoton2_2, "DISABLE";
             |ESTADO_CONTROL nBoton2_3, "DISABLE";
             |ESTADO_CONTROL nBoton2_4, "DISABLE";
             |ESTADO_CONTROL nBoton2_5, "DISABLE";
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO EventoPanta3;
     |SI FSalida = 1;  |O FSalida = 2;
         |ESTADO_CONTROL nBoton4_1, "ENABLE";

         |LEE 000#4=;
         |SI FSalida = 0;
             |ESTADO_CONTROL nBoton4_2, "ENABLE";
             |ESTADO_CONTROL nBoton4_3, "ENABLE";
         |SINO;
             |ESTADO_CONTROL nBoton4_2, "DISABLE";
             |ESTADO_CONTROL nBoton4_3, "DISABLE";
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO BotonesPanta1;
     |CONTROL_SIMPLE nPanta1, "BOTON,{{155}}", 1892, 1894, Boton1_1;
     nBoton1_1 = FSalida;
|FPRC;

|PROCESO BotonesPanta2;
     |CONTROL_SIMPLE nPanta2, "BOTON,{{193}} Alta", 0777, 0898, Boton2_1;
     nBoton2_1 = FSalida;

     |CONTROL_SIMPLE nPanta2, "BOTON,{{197}} Modificar", 1077, 1198, Boton2_2;
     nBoton2_2 = FSalida;

     |CONTROL_SIMPLE nPanta2, "BOTON,{{199}} Baja", 1377, 1498, Boton2_3;
     nBoton2_3 = FSalida;

     |CONTROL_SIMPLE nPanta2, "BOTON,{{135}} Importacion de AdmonNet", 1677, 1798, Boton2_4;
     nBoton2_4 = FSalida;

     |CONTROL_SIMPLE nPanta2, "BOTON,{{135}} Emitir Documentos", 1977, 2098, Boton2_5;
     nBoton2_5 = FSalida;

     |CONTROL_SIMPLE nPanta2, "EVENTOPAN", 0000, 0000, EventoFoco2;

     |ESTADO_CONTROL nBoton2_1, "DISABLE";
     |ESTADO_CONTROL nBoton2_2, "DISABLE";
     |ESTADO_CONTROL nBoton2_3, "DISABLE";
     |ESTADO_CONTROL nBoton2_4, "DISABLE";
     |ESTADO_CONTROL nBoton2_5, "DISABLE";
|FPRC;

|PROCESO BotonesPanta3;
     |CONTROL_SIMPLE nPanta3, "BOTON,{{193}} Alta", 0777, 0898, Boton4_1;
     nBoton4_1 = FSalida;

     |CONTROL_SIMPLE nPanta3, "BOTON,{{197}} Modificar", 1077, 1198, Boton4_2;
     nBoton4_2 = FSalida;

     |CONTROL_SIMPLE nPanta3, "BOTON,{{199}} Baja", 1377, 1498, Boton4_3;
     nBoton4_3 = FSalida;

     |CONTROL_SIMPLE nPanta3, "EVENTOPAN", 0000, 0000, EventoFoco3;

     |ESTADO_CONTROL nBoton4_1, "DISABLE";
     |ESTADO_CONTROL nBoton4_2, "DISABLE";
     |ESTADO_CONTROL nBoton4_3, "DISABLE";
|FPRC;

|PROCESO GrabaCif;  |TIPO 14;
     #2#0  = #901#0;
     #2#1  = #901#2;
     |LEE 101#2=;
     |SI FSalida ! 0;
         |DDEFECTO #2;
         #2#0  = #901#0;
         #2#1  = #901#2;
         |GRABA 020#2b;

         #2#16 = "S";
         #2#17 = "N";
         #2#18 = "N";
         #2#19 = "N";
         #2#20 = "N";
         #2#21 = "N";
     |FINSI;

     #2#2  = #901#1;
     #2#3  = #901#3;
     #2#4  = #901#4;
     #2#5  = #901#5;
     #2#6  = #901#6;
     #2#7  = #901#7;
     #2#8  = #901#8;
     #2#9  = #901#9;
     #2#10 = #901#10;
     #2#11 = #901#11;
     #2#12 = #901#12;
     #2#13 = #901#14;
     #2#14 = #901#15;
     #2#15 = #901#16;
     #2#29 = #901#17;

     |GRABA 020#2a;
     |LIBERA #2;

     |REFRESCACONTROL nGrid1;

     #2#0  = #901#0;
     #2#1  = "";
     |LEE 000#2];
|FPRC;

|PROCESO Tipo12;  |TIPO 12;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     |ABRET #2;
     |ABRET #4;

     FSalida = 2899;
|FPRC;

|PROCESO Tipo90;  |TIPO 90;
     |CIERRAT #2;
     |CIERRAT #4;
|FPRC;

|PROGRAMA;
     |ABRE #1;
     #1#0 = 40;
     #1#1 = enCliente;
     |LEE 101#1=;
     |SI FSalida ! 0;  |CIERRA #1;  |ACABA;  |FINSI;

     |PARA nCampoD = 1;  |SI nCampoD [ 18;  |HACIENDO nCampoD = nCampoD + 1;
           nCampoH = nCampoD - 1;
           #901nCampoH = #1nCampoD;
     |SIGUE;

     #901#0 = #1#1;
     #901#1 = #1#2;

     |CABEZA "Toma de Datos";

     |PINPA #0#901;  nPanta0 = FSalida;
     |PINDA #0#901;

     |PINPA #1#901;  nPanta1 = FSalida;
     |PINDA #1#901;

     |PINPA #2#901;  nPanta2 = FSalida;

     nRango = 4 + 8 + 16 + 32;
     nPos1  = 0602;
     nPos2  = 2060;

     #2#0   = #1#1;
     #2#1   = "";
     |LEE 000#2];

     |LINEAL_SIMPLE #1#2, nRango, nPos1, nPos2, BajaPanta2, AltaPanta2, EventoPanta2;
     nGrid1 = FSalida;

     |PINPA #3#901;  nPanta3 = FSalida;
     nRango = 1 + 4 + 8 + 16 + 32;
     nPos1  = 0602;
     nPos2  = 2060;

     |LINEAL_SIMPLE #1#4, nRango, nPos1, nPos2, BajaPanta3, AltaPanta3, EventoPanta3;
     nGrid2 = FSalida;

     |HAZ BotonesPanta1;
     |HAZ BotonesPanta2;
     |HAZ BotonesPanta3;

     |ENDATOS #1#901;
     |SI FSalida = 0;
         #1#17 = #901#16;
         #1#18 = #901#17;
         |GRABA 020#1a;
     |FINSI;

     |LIBERA #1;
     |CIERRA #1;

     |FIN_CONTROL_WINDOWS nBoton1_1;
     |FIN_CONTROL_WINDOWS nBoton2_1;
     |FIN_CONTROL_WINDOWS nBoton2_2;
     |FIN_CONTROL_WINDOWS nBoton2_3;
     |FIN_CONTROL_WINDOWS nBoton2_4;
     |FIN_CONTROL_WINDOWS nBoton2_5;
     |FIN_CONTROL_WINDOWS nBoton4_1;
     |FIN_CONTROL_WINDOWS nBoton4_2;
     |FIN_CONTROL_WINDOWS nBoton4_3;

     |DESTRUYECONTROL nGrid1;
     |DESTRUYECONTROL nGrid2;

     |PULSATECLA;
|FPRO;
