|FICHEROS;
     dslpm511 #511;
     dslpm512 #512;
     dslpm513 #513;

     dslpm512@ #612;
     dslpm513@ #613;
|FIN;

|VARIABLES;
     aAlfa            = "";
     aBase            = "";
     aMas             = "";
     aPathPatron      = "";
     aPathPlantilla   = "";
     aEsc1            = "";
     aEsc2            = "";
     aRetu            = "";
     aTecla           = "";
     aUsuario         = "";
     aFichero         = "";
     aOrigen          = "";
     aDestino         = "";

     nPanta1          = 0;
     nGrid1           = 0;
     nRango           = 0;
     nPos1            = 0;
     nPos2            = 0;
     nPermiso         = 0;
     nVentana         = 0;
     nTodo            = 0;
     nSwTar           = 0;

     nBoton1_1        = 0;
     nBoton1_2        = 0;
     nBoton1_3        = 0;
     nBoton1_4        = 0;
     nBoton1_5        = 0;


     &enAlta          = 0;
     &enPlantilla     = 0;
     &enModo          = 0;
|FIN;

|PROCESO BajaPanta1;
     #512#0  = 01;
|FPRC;

|PROCESO AltaPanta1;
     #512#0  = 99;
|FPRC;

|| ------------------------------------------------------
|| Procesos desde Patrones
|| ------------------------------------------------------

|PROCESO dslpm512P;
     |SI nTodo = 0;
         #512#0 = #612#0;
         |LEE 000#512=;
         |SI FSalida = 0;  |ACABA;  |FINSI;
     |FINSI;

     #512#0 = #612#0;
     |LEE 101#512=;
     |SI FSalida ! 0;
         |DDEFECTO #512;
         #512#0 = #612#0;
         |GRABA 020#512b;
     |FINSI;

     #512#1 = #612#1;
     |GRABA 020#512a;
     |LIBERA #512;
|FPRC;

|DEFBUCLE dslpm512P;
     #612#1001;
     ;
     00;
     99;
     ;
     NULL, dslpm512P;
|FIN;

|PROCESO Descomprime;
     aFichero   = aPathPatron + "zdocumen.tgz";
     |DESCOMPRIME aFichero;

     aFichero   = aPathPatron + "zdocumen.tar";
     |DETAR aFichero, aPathPatron;

     |FBORRA aFichero;
|FPRC;

|PROCESO dslpm513P;
     |SI nSwTar = 0;  |HAZ Descomprime;  |FINSI;

     nSwTar   = 1;
     aOrigen  = aPathPatron + "doc" + (("00" + #613#0) % -102) + (("000" + #613#1) % -103) + ".xgz";
     aDestino = aPathPlantilla + "doc" + (("00" + #613#0) % -102) + (("000" + #613#1) % -103) + ".xgz";

     |SI nTodo = 0;
         #513#0 = #613#0;
         #513#1 = #613#1;
         |LEE 000#513=;
         |SI FSalida = 0;
             |FBORRA aOrigen;
             |ACABA;
         |FINSI;
     |FINSI;

     #513#0 = #613#0;
     #513#1 = #613#1;
     |LEE 101#513=;
     |SI FSalida ! 0;
         |DDEFECTO #513;
         #513#0 = #613#0;
         #513#1 = #613#1;
         |GRABA 020#513b;
     |FINSI;

     #513#2 = #613#2;
     |GRABA 020#513a;
     |LIBERA #513;

     |COPIA_FICHERO aOrigen, aDestino;
     |FBORRA aOrigen;
|FPRC;

|DEFBUCLE dslpm513P;
     #613#1002;
     ;
     00, 000;
     99, 999;
     ;
     NULL, dslpm513P;
|FIN;

|PROCESO DesdePatron;
     |DBASE aBase;  |QBF aBase;

     aMas = aBase + "def/dslpm512.mas";
     |CARGA_DEF aMas, dslpm512@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aMas = aBase + "def/dslpm513.mas";
     |CARGA_DEF aMas, dslpm513@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #dslpm512@;
         |ACABA;
     |FINSI;

     aPathPatron    = aBase + "patrones/";
     aPathPlantilla = aBase + "documentos/plantillas/";

     |PATH_DAT #612 aPathPatron;
     |PATH_DAT #613 aPathPatron;

     |BUCLE dslpm512P;
     |BUCLE dslpm513P;

     |DESCARGA_DEF #dslpm513@;
     |DESCARGA_DEF #dslpm512@;

     aAlfa = aPathPatron + "dslpm512.ddx";  |FBORRA aAlfa;
     aAlfa = aPathPatron + "dslpm512.ixx";  |FBORRA aAlfa;
     aAlfa = aPathPatron + "dslpm513.ddx";  |FBORRA aAlfa;
     aAlfa = aPathPatron + "dslpm513.ixx";  |FBORRA aAlfa;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 1_1
|| ------------------------------------------------------

|PROCESO Boton1_1;
     |VENTANA 0501, 2799, -1, 1, "";
     nVentana = FSalida;

     enPlantilla = 0;
     enModo      = 1;
     |SUB_EJECUTA "dslpw037";

     |FINVENTANA nVentana;

     |REFRESCACONTROL nGrid1;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 1_2
|| ------------------------------------------------------

|PROCESO Boton1_2;
     |VENTANA 0501, 2799, -1, 1, "";
     nVentana = FSalida;

     enPlantilla = #512#0;
     enModo      = 2;
     |SUB_EJECUTA "dslpw037";

     |FINVENTANA nVentana;

     |REFRESCACONTROL nGrid1;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 1_3
|| ------------------------------------------------------

|PROCESO dslpm513B;
     |DBASE aBase;  |QBF aBase;
     aPathPlantilla = aBase + "documentos/plantillas/";
     aAlfa          = aPathPlantilla + "doc" + (("00" + #513#0) % -102) + (("000" + #513#1) % -103) + ".xgz";
     |FBORRA aAlfa;

     |BORRA 020#513a;
     |LIBERA #513;
|FPRC;

|DEFBUCLE dslpm513B;
     #513#1;
     ;
     #512#0, 001;
     #512#0, 999;
     be;
     NULL, dslpm513B;
|FIN;

|PROCESO Boton1_3;
     aAlfa = "0000N" + &191 + " Esta vd.seguro de dar de baja a " + #512#1 + " ?";
     |CONFI aAlfa;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |LEE 101#512=;
     |SI FSalida = 0;
         |BUCLE dslpm513B;

         |BORRA 020#512a;
         |LIBERA #512;
     |FINSI;

     |REFRESCACONTROL nGrid1;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 1_4
|| ------------------------------------------------------


|PROCESO Boton1_4;
     aAlfa = "0000N" + &191 + " Esta vd.seguro de importar TODO de los patrones ?";
     |CONFI aAlfa;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     nSwTar = 0;
     nTodo  = 1;  |HAZ DesdePatron;

     |REFRESCACONTROL nGrid1;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 1_5
|| ------------------------------------------------------

|PROCESO Boton1_5;
     aAlfa = "0000N" + &191 + " Esta vd.seguro de importar lo NUEVO de los patrones ?";
     |CONFI aAlfa;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     nSwTar = 0;
     nTodo  = 0;  |HAZ DesdePatron;

     |REFRESCACONTROL nGrid1;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Incio del Programa
|| ------------------------------------------------------

|PROCESO EventoPanta1;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#512=;
         |SI FSalida = 0;
             |ESTADO_CONTROL nBoton1_2, "ENABLE";
             |ESTADO_CONTROL nBoton1_3, "ENABLE";
             |ESTADO_CONTROL nBoton1_4, "ENABLE";
         |SINO;
             |ESTADO_CONTROL nBoton1_2, "DISABLE";
             |ESTADO_CONTROL nBoton1_3, "DISABLE";
             |ESTADO_CONTROL nBoton1_4, "DISABLE";
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO BotonesPanta1;
     |CONTROL_SIMPLE nPanta1, "BOTON,{{193}} Alta", 1070, 1198, Boton1_1;
     nBoton1_1 = FSalida;

     |CONTROL_SIMPLE nPanta1, "BOTON,{{197}} Modificar", 1370, 1498, Boton1_2;
     nBoton1_2 = FSalida;

     |CONTROL_SIMPLE nPanta1, "BOTON,{{199}} Baja ", 1670, 1798, Boton1_3;
     nBoton1_3 = FSalida;

     |CONTROL_SIMPLE nPanta1, "BOTON,{{217}} Importar TODO de Patrones", 2270, 2398, Boton1_4;
     nBoton1_4 = FSalida;

     |CONTROL_SIMPLE nPanta1, "BOTON,{{218}} Importar NUEVO de Patrones", 2570, 2698, Boton1_5;
     nBoton1_5 = FSalida;

     |ESTADO_CONTROL nBoton1_1, "ENABLE";
     |ESTADO_CONTROL nBoton1_2, "DISABLE";
     |ESTADO_CONTROL nBoton1_3, "DISABLE";
     |ESTADO_CONTROL nBoton1_4, "ENABLE";
     |ESTADO_CONTROL nBoton1_5, "ENABLE";

     |SI enAlta = 0;
         |ESTADO_CONTROL nBoton1_1, "HIDE";
         |ESTADO_CONTROL nBoton1_3, "HIDE";
     |FINSI;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     |ABRET #512;
     |ABRET #513;

     nPermiso = 0;
     aUsuario = Usuario % 810;

     |ABRE #511;
     #511#0 = aUsuario;
     |LEE 000#511=;
     |SI #511#2 = "N";
         || |MENAV "0000 Usted no tiene permiso para entrar en esta opcion";
         || nPermiso = 1;
     |FINSI;
     |CIERRA #511;

     FSalida = 2899;
|FPRC;

|PROCESO Tipo90;  |TIPO 90;
     |CIERRAT #512;
     |CIERRAT #513;
|FPRC;

|PROGRAMA;
     |SI nPermiso = 1;  |ACABA;  |FINSI;

     enAlta = 1;

     |PINPA #0#512;  nPanta1 = FSalida;

     nRango = 4 + 8 + 16 + 32;
     nPos1  = 0902;
     nPos2  = 2665;

     |LINEAL_SIMPLE #1#512, nRango, nPos1, nPos2, BajaPanta1, AltaPanta1, EventoPanta1;
     nGrid1 = FSalida;

     |HAZ BotonesPanta1;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::Salir";
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nBoton1_1;
     |FIN_CONTROL_WINDOWS nBoton1_2;
     |FIN_CONTROL_WINDOWS nBoton1_3;
     |FIN_CONTROL_WINDOWS nBoton1_4;
     |FIN_CONTROL_WINDOWS nBoton1_5;

     |DESTRUYECONTROL nGrid1;
     |PULSATECLA;
|FPRO;
t
