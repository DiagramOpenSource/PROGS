|FICHEROS;
     dslpm512 #512;
     dslpm513 #513;
     dslpw038 #938;
     dslpw996 #996;
     dslpw998 #998;
     dslpw999 #999;
|FIN;

|VARIABLES;
     aOrigen         = "";
     aDestino        = "";
     aDestinoPlan    = "";
     aDocRemoto      = "";
     aDocServidor    = "";
     aIPRemoto       = "";
     aBase           = "";
     aAlfa           = "";
     aNomFichero     = "";
     aDocumento      = "";
     aPathPatron     = "";
     aPathPlantilla  = "";
     aFichero        = "";
     aDir            = "";
     aRaiz           = "";

     nWord           = 0;
     nHandle         = 0;
     nPanta1         = 0;
     nBoton1_1       = 0;
     nBoton1_2       = 0;

     {1}aContiene    = "";

     &enPlantilla     = 0;
     &enDocumento     = 0;
     &enModo          = 0;
     &enAlta          = 0;

     &enModoExterno   = 1;
     &eaDirPlantilla  = "";
     &eaDirTrabajo    = "";
     &eaDirTrabServi  = "";
     &eaDirSerGuarda  = "";
     &eaDirSerPlan    = "";
     &eaDirSerPat     = "";
     &eaDirSerFich    = "";
     &eaOrigen        = "";
     &eaDestino       = "";
     &eaDestinoTra    = "";
     &eaFicheroTra    = "";
     &eaFicheroDef    = "";
     &eaFicheroXgz    = "";
     &eaFicheroTgz    = "";
     &eaFicheroTar    = "";
     &eaFicheroTxt    = "";
     &eaFicheroPlaGz  = "";
     &eaFicheroPlaMl  = "";
     &eaModoPresenta  = "";
|FIN;

|PROCESO Documento;  |TIPO 7;
     |SI enModo ! 1;  |ACABA;  |FINSI;

     #513#0 = #938#0;
     #513#1 = 999;
     |LEE 000#513];
     |SI FSalida = 0;
         |LEE 000#513a;
     |SINO;
         |LEE 000#513u;
     |FINSI;

     #938#2 = 1;
     |SI FSalida = 0;  |Y  #513#0 = #938#0;
         #938#2 = #513#1 + 1;
     |FINSI;
     |PINTA #938#2;
|FPRC;

|PROCESO EnviaFichero;
     aContiene1 = aDestinoPlan;
     |ESPECIFICA "REMOTOMD5", aContiene;
     aDocRemoto = FSalida;  |QBF aDocRemoto;

     aContiene1 = aOrigen;
     |ESPECIFICA "MD5", aContiene;
     aDocServidor = FSalida;  |QBF aDocServidor;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;
     |SI aDocRemoto ! aDocServidor;  |O aDocRemoto = "";
         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aOrigen, aDestinoPlan;
         |SINO;
             |COPIA_FICHERO aOrigen, aDestinoPlan;
         |FINSI;
     |FINSI;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 1_1
|| ------------------------------------------------------

|PROCESO Boton1_1;
     |DBASE aBase;   |QBF aBase;

     aRaiz          = "doc" + (("00" + #938#0) % -102) + (("000" + #938#2) % -103);
     eaDirSerPat    = aBase + "patrones/";
     eaDirSerPlan   = aBase + "documentos/plantillas/";
     eaDirSerGuarda = "";
     eaDirSerFich   = aBase + "fich/";
     eaDirPlantilla = "C:\DOCUMENTOS\DSLPD\PLANTILLAS\";
     eaDirTrabajo   = "C:\DOCUMENTOS\DSLPD\PLANTILLAS\";
     eaDirSerGuarda = aBase + "documentos/plantillas/";
     eaDirTrabServi = aBase  + "documentos/tmp/";
     eaFicheroTra   = aRaiz + ".doc";
     eaFicheroDef   = aRaiz + ".doc";
     eaFicheroTxt   = aRaiz + ".txt";
     eaFicheroXgz   = aRaiz + ".xgz";
     eaFicheroTgz   = aRaiz + ".tgz";
     eaFicheroTar   = aRaiz + ".tar";
     eaFicheroPlaGz = aRaiz + ".xgz";
     eaFicheroPlaMl = aRaiz + ".doc";
     eaModoPresenta = "P";

     |MKDIR eaDirTrabServi;

     eaOrigen       = eaDirSerGuarda + eaFicheroXgz;
     eaDestino      = eaDirTrabajo   + eaFicheroTgz;
     eaDestinoTra   = "";

     |HAZ MiraDirectorios;
     |HAZ EnviaFicheroConMD5;
     |HAZ DescomprimeFichero;

     eaDestino = eaDirTrabajo + eaFicheroXgz;
     |RFBORRA eaDestino;

     eaDestino = eaDirTrabajo + eaFicheroTra;

     |XABRE "EC", eaDestino, nHandle;
     |SI FSalida < 0;
         aAlfa   = "0000 El Documento " + eaFicheroTra  + " no lo encuentro. Consulte con su distribuidor";
         |MENAV aAlfa;
         |ACABA;
     |FINSI;

     |HAZ EditaModoExterno;
     |HAZ ComprimeFichero;

     eaOrigen  = eaDirTrabajo   + eaFicheroTgz;
     eaDestino = eaDirTrabajo   + eaFicheroXgz;
     |RRENOMBRA_FICHERO eaOrigen, eaDestino;

     eaOrigen  = eaDirSerGuarda + eaFicheroXgz;
     eaDestino = eaDirTrabajo   + eaFicheroXgz;

     |HAZ RecibeFichero;

     |DBASE aBase;   |QBF aBase;

     aAlfa = "C:/DOCUMENTOS";                     |RMKDIR aAlfa;
     aAlfa = "C:/DOCUMENTOS/DSLPD";               |RMKDIR aAlfa;
     aAlfa = "C:/DOCUMENTOS/DSLPD/PLANTILLAS";    |RMKDIR aAlfa;
     nWord = 1;

     aNomFichero  = "doc" + (("00" + #938#0) % -102) + (("000" + #938#2) % -103) + ".xgz"
     aDestinoPlan = "C:/DOCUMENTOS/DSLPD/PLANTILLAS/" + aNomFichero;
     aDestino     = aDestinoPlan;
     aOrigen      = aBase + "documentos/plantillas/" + aNomFichero;
     |HAZ EnviaFichero;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 1_2
|| ------------------------------------------------------

|PROCESO Descomprime;
     aFichero   = aPathPatron + "zdocumen.tgz";
     |DESCOMPRIME aFichero;

     aFichero   = aPathPatron + "zdocumen.tar";
     |DETAR aFichero, aPathPatron;

     |FBORRA aFichero;

|FPRC;

|PROCESO Boton1_2;
     aAlfa = "0000N" + &191 + " Esta vd.seguro de importar el documento de los PATRONES ?";
     |CONFI aAlfa;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |DBASE aBase;   |QBF aBase;

     aNomFichero    = "doc" + (("00" + #938#0) % -102) + (("000" + #938#2) % -103) + ".xgz"
     aPathPatron    = aBase + "patrones/";
     aPathPlantilla = aBase + "documentos/plantillas/";

     |HAZ Descomprime;

     aOrigen  = aPathPatron + aNomFichero;
     |XABRE "E", aOrigen, nHandle;
     |SI FSalida ] 0;
         aDestino = aPathPlantilla + aNomFichero;
         |COPIA_FICHERO aOrigen, aDestino;
         |MENAV "0000 Instalado.";

         |ESTADO_CONTROL nBoton1_1, "ENABLE";
     |SINO;
         |ESTADO_CONTROL nBoton1_1, "DISABLE";

         |MENAV "0000 El Documento no esta como Patron. Consulte a su Distribudor.";
     |FINSI;

     aDir = aPathPatron + "*.xgz";
     |_PDIR aDir;
     |PARA;  |SI FSalida = 0;  |HACIENDO;
          |FBORRA aDir;

          |_SDIR aDir;
     |SIGUE;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Incio del Programa
|| ------------------------------------------------------

|PROCESO BotonesPanta1;
     |CONTROL_SIMPLE nPanta1, "BOTON,{{137}} Editar Documento", 1525, 1775, Boton1_1;
     nBoton1_1 = FSalida;

     |CONTROL_SIMPLE nPanta1, "BOTON,{{217}} Importar desde Patrones", 2025, 2275, Boton1_2;
     nBoton1_2 = FSalida;

     |DBASE aBase;   |QBF aBase;

     aNomFichero  = "doc" + (("00" + #938#0) % -102) + (("000" + #938#2) % -103) + ".xgz"
     aOrigen      = aBase + "documentos/plantillas/" + aNomFichero;

     |SI enModo = 1;
         |ESTADO_CONTROL nBoton1_1, "DISABLE";
         |ESTADO_CONTROL nBoton1_2, "DISABLE";
         |ACABA;
     |FINSI;

     |XABRE "E", aOrigen, nHandle;
     |SI FSalida ] 0;
         |ESTADO_CONTROL nBoton1_1, "ENABLE";
     |SINO;
         |ESTADO_CONTROL nBoton1_1, "DISABLE";
     |FINSI;

     |ESTADO_CONTROL nBoton1_2, "ENABLE";
|FPRC;

|PROCESO Tipo12;  |TIPO 12;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     FSalida = 2899;
|FPRC;

|PROCESO Tipo90;  |TIPO 90;
|FPRC;

|PROGRAMA;
     |ABRE #512;
     #512#0 = enPlantilla;
     |LEE 000#512=;
     |SI FSalida ! 0;  |DDEFECTO #512;  |FINSI;
     |CIERRA #512;

     #938#0 = #512#0;
     #938#1 = #512#1;

     |ABRE #513;
     |SI enModo ! 1;
         #513#0 = #512#0;
         #513#1 = enDocumento;
         |LEE 000#513=;
         |SI FSalida ! 0;  |DDEFECTO #513; |FINSI;

         #938#2 = #513#1;
         #938#3 = #513#2;
     |FINSI;

     |CABEZA "Datos Documentos";

     |PINPA #0#938;  nPanta1 = FSalida;
     |PINDA #0#938;

     |HAZ BotonesPanta1;

     |SI enModo = 1;  |ENDATOS #1#938;  |FINSI;
     |SI enModo = 2;  |ENDATOS #2#938;  |FINSI;

     |SI FSalida = 0;
         #513#0 = #938#0;
         #513#1 = #938#2;
         |LEE 101#513=;
         |SI FSalida ! 0;
             |DDEFECTO #513;
             #513#0 = #938#0;
             #513#1 = #938#2;
             |GRABA 020#513b;
         |FINSI;
         #513#2 = #938#3;
         |GRABA 020#513a;
         |LIBERA #513;
     |FINSI;
     |CIERRA #513;

     |PULSATECLA;
|FPRO;
