|FICHEROS;
     dslpz002 #0;
     dslpm999 #999;
|FIN;

|VARIABLES;
     nFecha       = 0;
     nNume        = 0;
     nSwHay       = 0;

     aAlfa        = "";
     aBaseCorreo  = "";
     aImpresora   = "";

     fFecha       = @;
     fFechaLetra  = @;

     &eaBase      = "";
     &eaDiaSemana = "";
     &eaGuardian  = "";
     &efFecha     = @;
|FIN;

|PROCESO MandaCorreos;
     efFecha = #0#0;
     |SUB_EJECUTA "dslpw995";
|FPRC;

|PROCESO dslpm999;
     nSwHay = 1;
     |PRINT;
|FPRC;

|DEFBUCLE dslpm999;
     #999#4;
     ;
     #0#0, 00, "        ", 0000000001;
     #0#0, 99, "zzzzzzzz", 9999999999;
     ;
     NULL, dslpm999;
|FIN;

|PROCESO Tipo3;  |TIPO 3;
     fFechaLetra = #0#0 % 1;
     eaDiaSemana = (fFechaLetra % 1109) > " ";   |QBF eaDiaSemana;

     |SI #0#1 = "C";
         eaGuardian = (#0#0 % -104) + (#0#0 % 402) + (#0#0 % 102) + ".txt";
         aImpresora = aBaseCorreo + "/"+ eaGuardian;
         eaGuardian = aImpresora;

/*
         aAlfa = aImpresora % 103;
         |SI aAlfa = "/v/";  |O aAlfa = "/w/";
             aImpresora = "/u" + aImpresora;
         |FINSI;
*/

         Impresora = aImpresora;
         |IMPRE -77;
         |SI FSalida ! 0;  |ACABA;  |FINSI;
     |SINO;
         |IMPRE 0;
         |SI FSalida ! 0;  |ACABA;  |FINSI;
     |FINSI;

     |INFOR "dslpz002";
     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

     nSwHay = 0;
     |BUCLE dslpm999;

     |SI nSwHay = 1;  |PIEINF;  |FINSI;

     |FININF;
     |FINIMP;

     |SI #0#1 = "C";
         |SI nSwHay = 1;  |HAZ MandaCorreos;  |FINSI;
         |FBORRA eaGuardian;
     |FINSI;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     FSalida = 2499;
|FPRC;

|PROGRAMA;
     |DBASE eaBase;   |QBF eaBase;
     aBaseCorreo = eaBase + "correos";
     |MKDIR aBaseCorreo;

     |SI PARAMETRO ! "";
         |DDEFECTO #0;
         #0#1 = "C";

         fFecha      = ~;
         nFecha      = fFecha;

         fFechaLetra = fFecha % 1;
         eaDiaSemana = (fFechaLetra % 1109) > " ";   |QBF eaDiaSemana;
         nFecha      = fFecha;

         |SI eaDiaSemana = "LUNES";
             nNume  = nFecha - 3;   #0#0 = nNume;  |HAZ Tipo3;
             nNume  = nFecha - 2;   #0#0 = nNume;  |HAZ Tipo3;
             nNume  = nFecha - 1;   #0#0 = nNume;  |HAZ Tipo3;
         |SINO;
             nNume  = nFecha - 1;   #0#0 = nNume;  |HAZ Tipo3;
         |FINSI;
     |SINO;
         |CLS;
         |CABEZA "Informe situaciones";
         |DDEFECTO #0;
         |PINPA #0#0;
         |PINDA #0#0;
         |ENDATOS #1#0;
     |FINSI;
|FPRO;
