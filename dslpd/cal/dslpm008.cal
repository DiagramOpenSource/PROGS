|FICHEROS;
     dslpm001 #1;
     dslpm005 #5;
     dslpm008 #8;
     dslpm511 #511;
     dslpm512 #512;
     dslpm514 #514;
     dslpm999 #999;

     dslpw994 #994,MANTE;
     dslpw996 #996;
     dslpw997 #997;
     dslpw998 #998;
     dslpw999 #999;
     dslpw030 #930;
|FIN;

|VARIABLES;
     nCampo     = 0;
     nHandle    = 0;
     nRango     = 0;
     nPos1      = 0;
     nPos2      = 0;
     nGrid1     = 0;
     nPanta0    = 0;
     nBoton0_1  = 0;
     nBoton0_2  = 0;
     nBoton0_3  = 0;
     nBoton0_4  = 0;
     nBoton0_5  = 0;
     nCaracter  = 0;
     nError     = 0;
     nVentana   = 0;
     nSitAnt    = 0;
     nSitAct    = 0;
     nReg       = 0;
     nLongitud  = 0;
     nPos       = 0;
     nPorDonde  = 0;
     nDLinea    = 0;
     nHLinea    = 0;
     nDocu      = 0;

     aBase       = "";
     aAlfa       = "";
     aAlfa1      = "";
     aCliente    = "";
     aNomFichero = "";
     aIPRemoto   = "";
     aDocumento  = "";
     aHora       = "";
     aEsc1       = "";
     aEsc2       = "";
     aRetu       = "";
     aTecla      = "";
     aDirLocal   = "";
     aWait       = "";
     aAbre       = "";
     aDocPdf     = "";
     aSitAnt     = "";
     aSitAct     = "";
     aUsuario    = "";
     aOperacion  = "";
     aLongitud   = "";
     aCadena     = "";
     aCaracter   = "";
     aMiCaracter = "";
     aPathReader = "";
     aEjecutable = "";
     aUnidad     = "";
     aPdf        = "";
     aRaiz       = "";

     &enCliente     = 0;
     &enEstado      = 0;
     &enDocumento   = 0;
     &enIncidencias = 0;
     &enFormulario  = 0;
     &eaClave       = "";
     &enErrorDoc    = 0;
     &enErrorCorreo = 0;
     &eaPathAcrobat = "";
     &enDocu        = 0;
     &eaTipoEnvio   = "";

     &enModoExterno   = 1;
     &eaDirPlantilla  = "";
     &eaDirTrabajo    = "";
     &eaDirTrabServi  = "";
     &eaDirSerGuarda  = "";
     &eaDirSerPlan    = "";
     &eaDirSerPat     = "";
     &eaDirSerFich    = "";
     &eaOrigen        = "";
     &eaDestino       = "";
     &eaDestinoTra    = "";
     &eaFicheroTra    = "";
     &eaFicheroDef    = "";
     &eaFicheroXgz    = "";
     &eaFicheroTgz    = "";
     &eaFicheroTar    = "";
     &eaFicheroTxt    = "";
     &eaFicheroPlaGz  = "";
     &eaFicheroPlaMl  = "";
     &eaModoPresenta  = "";
     &eaParametro1    = "";
     &eaParametro2    = "";
     &eaParametro3    = "";
     &eaParametro4    = "";
     &eaParametro5    = "";
|FIN;

|PROCESO GrabaLogs;
     aSitAnt  = "";
     aSitAct  = "";
     aUsuario = Usuario % 810;

     |ABRE #514;

     #514#0 = nSitAnt;
     |LEE 000#514=;
     |SI FSalida = 0;  aSitAnt = #514#1;  |FINSI;

     #514#0 = nSitAct;
     |LEE 000#514=;
     |SI FSalida = 0;  aSitAct = #514#1;  |FINSI;

     |CIERRA #514;

     |HORA aHora;

     |ABRE #999;
     nReg = 1;
     |LEE 000#999u;
     |SI FSalida = 0;
         nReg = #999#0 + 1;
     |FINSI;

     #999#0  = nReg;
     |GRABA 020#999b;

     #999#1  = aUsuario;
     #999#2  = ~;
     #999#3  = aHora;
     #999#4  = #1#1;
     #999#5  = #1#2;
     #999#6  = aOperacion;
     #999#7  = nSitAnt;
     #999#8  = aSitAnt;
     #999#9  = nSitAct;
     #999#10 = aSitAct;

     |GRABA 020#999a;
     |LIBERA #999;
|FPRC;

|PROCESO BajaPanta0;
     #8#0 = enCliente;
     #8#1 = nDLinea;
|FPRC;

|PROCESO AltaPanta0;
     #8#0 = enCliente;
     #8#1 = nHLinea;
|FPRC;

|PROCESO Tipo12;  |TIPO 12;
|FPRC;

|PROCESO ConsultaUsuario;
     nError = 0;
     |BORRA_HIJOS_PAN #0#8;
     |SUB_PINPA #0#994, nPanta0;
     #994#0 = "";
     |PINDA #0#994;
     |ENDATOS #1#994;
     |SI FSalida = 0;
         |BORRA_HIJOS_PAN #0#8;

         |ABRE #511;
         #511#0 = #8#10;
         |LEE 000#511=;
         |SI FSalida ! 0;  |DDEFECTO #511;  |FINSI;
         |CIERRA #511;

         eaClave = #994#0;
         |HAZ Encripta;

         |SI eaClave ! #511#1;
             |MENAV "0000 Password erroneo.";
             nError = 1;
         |FINSI;
     |SINO;
         |BORRA_HIJOS_PAN #0#8;
     |FINSI;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 0_1
|| ------------------------------------------------------

|PROCESO Boton0_1;
     |BORRA_HIJOS_PAN #0#8;
     |SUB_PINPA #0#930, nPanta0;

     |PARA nCampo = 0;  |SI nCampo [ 15;  |HACIENDO nCampo = nCampo + 1;
           #930nCampo = #8nCampo;
     |SIGUE;

     |ABRE #512;
     #512#0 = #930#14;
     |LEE 000#512=;
     |SI FSalida ! 0;  |DDEFECTO #512;  |FINSI;
     |CIERRA #512;

     #930#16 = #512#1;

     |PINDA #0#930;

     |PAUSA;
     |BORRA_HIJOS_PAN #0#8;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 0_2
|| ------------------------------------------------------

|PROCESO EdicionDoc;
     aNomFichero  = ("doc" + (("00000" + #8#1) % -105) + ".doc") < "";
     eaDestino    = "C:/DOCUMENTOS/DSLPD/DOCDSLPD/" + aNomFichero;
     eaOrigen     = aBase + "documentos/clientes/" + aCliente + "/" + aNomFichero;

     |HAZ EnviaFicheroSinMD5;

     |XABRE "EC", eaDestino, nHandle;
     |SI FSalida < 0;
         aAlfa   = "0000 El Documento " + aNomFichero  + " no lo encuentro. Consulte con su distribuidor";
         |MENAV aAlfa;
         |ACABA;
     |FINSI;

     aDocumento = eaDestino;

     eaDirSerPlan   = aBase + "documentos/plantillas/";

     |BORRA_HIJOS_PAN #0#8;
     |SUB_PINPA #0#996, nPanta0;

     aRaiz          = ("doc" + (("00000" + #8#1) % -105)) < "";
     eaDirPlantilla = "";
     eaDirTrabajo   = "C:\DOCUMENTOS\DSLPD\DOCDSLPD\";
     eaDirTrabServi = aBase + "documentos/tmp/";
     eaDirSerGuarda = aBase + "documentos/clientes/" + aCliente + "/";
     eaDirSerPlan   = aBase + "documentos/plantillas/";
     eaFicheroTra   = aRaiz + ".doc";
     eaFicheroDef   = aRaiz + ".doc";
     eaFicheroTxt   = aRaiz + ".txt";
     eaFicheroXgz   = aRaiz + ".xgz";
     eaFicheroTgz   = aRaiz + ".tgz";
     eaFicheroTar   = aRaiz + ".tar";
     eaOrigen       = eaDirSerGuarda + eaFicheroXgz;
     eaDestino      = eaDirTrabajo   + eaFicheroTgz;
     eaModoPresenta = "P";

     |MKDIR eaDirTrabServi;

     |HAZ MiraDirectorios;
     |HAZ EditaModoExterno;

     |HAZ ComprimeFichero;

     eaOrigen  = eaDirSerGuarda + eaFicheroXgz;
     eaDestino = eaDirTrabajo   + eaFicheroTgz;

     |HAZ RecibeFichero;

     |RFBORRA eaDestino;

     aAlfa = aBase + "documentos/clientes/" + aCliente + "/" + aNomFichero;
     |FBORRA aAlfa;

     |LEE 101#8=;
     |SI FSalida = 0;
         |HORA aHora;
         #8#11 = ~;
         #8#12 = aHora;
         #8#13 = Usuario % 810;
         #8#16 = "XGZ";
         |GRABA 020#8a;
         |LIBERA #8;
     |FINSI;

     |BORRA_HIJOS_PAN #0#8;

     |REFRESCACONTROL nGrid1;
|FPRC;

|PROCESO EdicionXml;
     aNomFichero  = ("doc" + (("00000" + #8#1) % -105) + ".xgz") < "";
     eaOrigen     = aBase + "documentos/clientes/" + aCliente + "/" + aNomFichero;

     |XABRE "E", eaOrigen, nHandle;
     |SI FSalida < 0;
         aAlfa   = "0000 El Documento " + aNomFichero  + " no lo encuentro. Consulte con su distribuidor";
         |MENAV aAlfa;
         |ACABA;
     |FINSI;

     aDocumento = eaDestino;

     eaDirSerPlan   = aBase + "documentos/plantillas/";

     |BORRA_HIJOS_PAN #0#8;
     |SUB_PINPA #0#996, nPanta0;

     aRaiz          = ("doc" + (("00000" + #8#1) % -105)) < "";
     eaDirPlantilla = "";
     eaDirTrabajo   = "C:\DOCUMENTOS\DSLPD\DOCDSLPD\";
     eaDirTrabServi = aBase + "documentos/tmp/";
     eaDirSerGuarda = aBase + "documentos/clientes/" + aCliente + "/";
     eaDirSerPlan   = aBase + "documentos/plantillas/";
     eaFicheroTra   = aRaiz + ".doc";
     eaFicheroDef   = aRaiz + ".doc";
     eaFicheroTxt   = aRaiz + ".txt";
     eaFicheroXgz   = aRaiz + ".xgz";
     eaFicheroTgz   = aRaiz + ".tgz";
     eaFicheroTar   = aRaiz + ".tar";
     eaOrigen       = eaDirSerGuarda + eaFicheroXgz;
     eaDestino      = eaDirTrabajo   + eaFicheroTgz;
     eaModoPresenta = "P";

     |MKDIR eaDirTrabServi;

     |HAZ MiraDirectorios;
     |HAZ EnviaFicheroSinMD5;
     |HAZ DescomprimeFichero;

     eaOrigen  = eaDirTrabajo +  ("doc" + (("00000" + #8#1) % -105) + ".xml") < "";
     eaDestino = eaDirTrabajo +  ("doc" + (("00000" + #8#1) % -105) + ".doc") < "";
     |RRENOMBRA_FICHERO eaOrigen, eaDestino;

     eaDestino = eaDirTrabajo + eaFicheroTra;

     |HAZ EditaModoExterno;
     |HAZ ComprimeFichero;

     eaOrigen  = eaDirSerGuarda + eaFicheroXgz;
     eaDestino = eaDirTrabajo   + eaFicheroTgz;

     |HAZ RecibeFichero;

     eaDestino = eaDirTrabajo   + eaFicheroTgz;  |RFBORRA eaDestino;
     eaDestino = eaDirTrabajo   + eaFicheroXgz;  |RFBORRA eaDestino;

     |LEE 101#8=;
     |SI FSalida = 0;
         |HORA aHora;
         #8#11 = ~;
         #8#12 = aHora;
         #8#13 = Usuario % 810;
         #8#16 = "XGZ";
         |GRABA 020#8a;
         |LIBERA #8;
     |FINSI;

     |BORRA_HIJOS_PAN #0#8;

     |REFRESCACONTROL nGrid1;
|FPRC;

|PROCESO EdicionPdf;
     aNomFichero  = (#8#16 + (("00000" + #8#1) % -105) + "." + #8#16) < "";
     eaDestino    = "C:/DOCUMENTOS/DSLPD/DOCDSLPD/" + aNomFichero;
     eaOrigen     = aBase + "documentos/clientes/" + aCliente + "/" + aNomFichero;

     |HAZ EnviaFicheroSinMD5;

     |XABRE "EC", eaDestino, nHandle;
     |SI FSalida < 0;
         aAlfa   = "0000 El Documento " + aNomFichero  + " no lo encuentro. Consulte con su distribuidor";
         |MENAV aAlfa;
         |ACABA;
     |FINSI;

     aDocumento = eaDestino;

     eaDirSerPlan   = aBase + "documentos/plantillas/";

     |BORRA_HIJOS_PAN #0#8;
     |SUB_PINPA #0#998, nPanta0;

     eaParametro1 = "C:\DOCUMENTOS\DSLPD\DOCDSLPD\dsabreextension";
     eaParametro2 = "C:\DOCUMENTOS\DSLPD\DOCDSLPD\" + aNomFichero;
     eaParametro3 = eaDirSerPlan;
     eaParametro4 = eaDirPlantilla;
     eaParametro5 = "C:\DOCUMENTOS\DSLPD\DOCDSLPD\";

     |HAZ EjecutaDSAbreExtension;

     |RFBORRA eaParametro2;

     |BORRA_HIJOS_PAN #0#8;
|FPRC;

|PROCESO EdicionXgz;
     aNomFichero  = ("doc" + (("00000" + #8#1) % -105) + ".xgz") < "";
     eaOrigen     = aBase + "documentos/clientes/" + aCliente + "/" + aNomFichero;

     |XABRE "E", eaOrigen, nHandle;
     |SI FSalida < 0;
         aAlfa   = "0000 El Documento " + aNomFichero  + " no lo encuentro. Consulte con su distribuidor";
         |MENAV aAlfa;
         |ACABA;
     |FINSI;

     aDocumento = eaDestino;

     eaDirSerPlan   = aBase + "documentos/plantillas/";

     |BORRA_HIJOS_PAN #0#8;
     |SUB_PINPA #0#996, nPanta0;

     aRaiz          = ("doc" + (("00000" + #8#1) % -105)) < "";
     eaDirPlantilla = "";
     eaDirTrabajo   = "C:\DOCUMENTOS\DSLPD\DOCDSLPD\";
     eaDirTrabServi = aBase + "documentos/tmp/";
     eaDirSerGuarda = aBase + "documentos/clientes/" + aCliente + "/";
     eaDirSerPlan   = aBase + "documentos/plantillas/";
     eaFicheroTra   = aRaiz + ".doc";
     eaFicheroDef   = aRaiz + ".doc";
     eaFicheroTxt   = aRaiz + ".txt";
     eaFicheroXgz   = aRaiz + ".xgz";
     eaFicheroTgz   = aRaiz + ".tgz";
     eaFicheroTar   = aRaiz + ".tar";
     eaOrigen       = eaDirSerGuarda + eaFicheroXgz;
     eaDestino      = eaDirTrabajo   + eaFicheroTgz;
     eaModoPresenta = "P";

     |MKDIR eaDirTrabServi;

     |HAZ MiraDirectorios;
     |HAZ EnviaFicheroSinMD5;
     |HAZ DescomprimeFichero;

     eaDestino = eaDirTrabajo + eaFicheroTra;

     |HAZ EditaModoExterno;
     |HAZ ComprimeFichero;

     eaOrigen  = eaDirSerGuarda + eaFicheroXgz;
     eaDestino = eaDirTrabajo   + eaFicheroTgz;

     |HAZ RecibeFichero;

     eaDestino = eaDirTrabajo   + eaFicheroTgz;  |RFBORRA eaDestino;
     eaDestino = eaDirTrabajo   + eaFicheroXgz;  |RFBORRA eaDestino;

     |LEE 101#8=;
     |SI FSalida = 0;
         |HORA aHora;
         #8#11 = ~;
         #8#12 = aHora;
         #8#13 = Usuario % 810;
         |GRABA 020#8a;
         |LIBERA #8;
     |FINSI;

     |BORRA_HIJOS_PAN #0#8;
|FPRC;

|PROCESO BuscaCaracter;
     aLongitud = aCadena % 0;
     nLongitud = aLongitud;
     |PARA nPos = 1; |SI nPos [ nLongitud; |HACIENDO nPos = nPos + 1;
          nCaracter = ((nPos * 100) + 1) * nPorDonde;
          aCaracter = aCadena % nCaracter;
          |SI aCaracter = aMiCaracter;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO EdicionFdf;
     aNomFichero  = (#8#16 + (("00000" + #8#1) % -105) + "." + #8#16) < "";
     eaDestino    = "C:/DOCUMENTOS/DSLPD/DOCDSLPD/" + aNomFichero;
     eaOrigen     = aBase + "documentos/clientes/" + aCliente + "/" + aNomFichero;

     |HAZ EnviaFicheroSinMD5;

     |XABRE "EC", eaDestino, nHandle;
     |SI FSalida < 0;
         aAlfa   = "0000 El Documento " + aNomFichero  + " no lo encuentro. Consulte con su distribuidor";
         |MENAV aAlfa;
         |ACABA;
     |FINSI;

     aDocumento = eaDestino;

     eaDirSerPlan   = aBase + "documentos/plantillas/";

     aDocPdf  = "C:\DOCUMENTOS\DSLPD\DOCDSLPD\" + aNomFichero;

     eaOrigen  = aBase + "documentos/plantillas/wait.exe";
     eaDestino = "C:/DOCUMENTOS/DSLPD/DOCDSLPD/WAIT.EXE";
     aWait     = eaDestino;
     |HAZ EnviaFicheroSinMD5;

     eaOrigen  = aBase + "documentos/plantillas/encuesta.pdf";
     eaDestino = "C:/DOCUMENTOS/DSLPD/DOCDSLPD/encuesta.pdf";
     aPdf      = eaDestino;
     |HAZ EnviaFicheroSinMD5;

     |HAZ LocalizaAcrobat;
     |SI enErrorDoc ! 0;  |ACABA;  |FINSI;

     aPathReader = eaPathAcrobat;  |QBF aPathReader;
     aCadena     = aPathReader;
     aMiCaracter = "\";
     nPorDonde   = -1;
     |HAZ BuscaCaracter;
     nPos        = nPos -1;
     nPos        = nPos + 100;
     nPos        = (-1) * nPos;
     aEjecutable = aPathReader % nPos;
     aPathReader = aPathReader - aEjecutable;

     aAbre = "C:\DOCUMENTOS\DSLPD\DOCDSLPD\imprime.bat";
     |XABRE "CBW", aAbre, nHandle;
     |SI FSalida = 0;
         aAlfa   = "@echo off" + &13 + &10;  |XGRABA nHandle, aAlfa;
         aUnidad = aPathReader % 102;
         aAlfa   = aUnidad + &13 + &10;      |XGRABA nHandle, aAlfa;
         aAlfa   = "cd " + &34 + aPathReader + &34 + &13 + &10;
         aAlfa   = aAlfa - aUnidad;          |XGRABA nHandle, aAlfa;
         aAlfa   = aWait + " " + aEjecutable;
         aAlfa   = aAlfa + " " + aDocPdf + &13 + &10;
                                             |XGRABA nHandle, aAlfa;

         |XCIERRA nHandle;

         |BORRA_HIJOS_PAN #0#8;
         |SUB_PINPA #0#998, nPanta0;
         |RSYSTEM aAbre;
         |BORRA_HIJOS_PAN #0#8;
     |FINSI;

     |RFBORRA aAbre;
     |RFBORRA aDocumento;
     |RFBORRA aWait;
     |RFBORRA aPdf;
|FPRC;

|PROCESO Boton0_2;
     |ABRE #5;
     #5#0 = #8#0;
     #5#1 = #8#3;
     #5#2 = #8#5;
     |LEE 000#5=;
     |SI FSalida ! 0;  |DDEFECTO #5;  |FINSI;
     |CIERRA #5;

     |SI #8#16 = "DOC";  |O  #8#16 = "XML";  |O  #8#16 = "XGZ";
         |SI #5#241 ! "01.01.0000";
             |HAZ ConsultaUsuario;
             |SI nError = 1;  |ACABA;  |FINSI;
         |FINSI;
     |FINSI;

     aCliente     = (("00000" + #8#0) % -105);

     |SI #8#16 = "DOC";  |HAZ EdicionDoc;  |ACABA;  |FINSI;
     |SI #8#16 = "XML";  |HAZ EdicionXml;  |ACABA;  |FINSI;
     |SI #8#16 = "PDF";  |HAZ EdicionPdf;  |ACABA;  |FINSI;
     |SI #8#16 = "FDF";  |HAZ EdicionFdf;  |ACABA;  |FINSI;
     |SI #8#16 = "XGZ";  |HAZ EdicionXgz;  |ACABA;  |FINSI;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 0_3
|| ------------------------------------------------------

|PROCESO Boton0_3;
     |SI eaTipoEnvio = "";
         aAlfa = "0000NSe va proceder a dar de baja el Documento. " + &191 + " Esta seguro ?";
         |CONFI aAlfa;
         |SI FSalida ! 0;  |ACABA;  |FINSI;
     |FINSI;

     |ABRE #5;
     #5#0 = #8#0;
     #5#1 = #8#3;
     #5#2 = #8#5;
     |LEE 000#5=;
     |SI FSalida ! 0;  |DDEFECTO #5;  |FINSI;
     |CIERRA #5;

     |SI #8#16 = "DOC";  |O  #8#16 = "XML";  |O  #8#16 = "XGZ";
         |SI #5#241 ! "01.01.0000";
             |HAZ ConsultaUsuario;
             |SI nError = 1;  |ACABA;  |FINSI;
         |FINSI;
     |FINSI;

     |LEE 101#8=;
     |SI FSalida = 0;
         |BORRA 020#8a;
         |LIBERA #8;

         |SI #8#16 = "XML";  |O #8#16 = "XGZ";
             aCliente   = (("00000" + #8#0) % -105);
             aNomFichero = ("doc" + (("00000" + #8#1) % -105) + ".xgz") < "";
             eaOrigen    = aBase + "documentos/clientes/" + aCliente + "/" + aNomFichero;
             |FBORRA eaOrigen;
         |SINO;
             aCliente    = (("00000" + #8#0) % -105);
             aNomFichero = (#8#16 + (("00000" + #8#1) % -105) + "." + #8#16) < "";
             eaOrigen    = aBase + "documentos/clientes/" + aCliente + "/" + aNomFichero;
             |FBORRA eaOrigen;
         |FINSI;

         |SI #8#16 = "PDF";
             aNomFichero = "txt" + (("00000" + #8#1) % -105) + ".txt";
             eaOrigen    = aBase + "documentos/clientes/" + aCliente + "/" + aNomFichero;
             |FBORRA eaOrigen;
         |FINSI;

         |REFRESCACONTROL nGrid1;
    |FINSI;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 0_4
|| ------------------------------------------------------

|PROCESO Boton0_4;
     |SI #8#16 ! "DOC";  |Y #8#16 ! "XML";  |Y #8#16 ! "XGZ";
         |ACABA;
     |FINSI;

     aCliente     = (("00000" + #8#0) % -105);

     |SI #8#16 = "XML";  |O #8#16 = "XGZ";
         aNomFichero  = ("doc" + (("00000" + #8#1) % -105) + ".xgz") < "";
     |SINO;
         aNomFichero  = (#8#16 + (("00000" + #8#1) % -105) + "." + #8#16) < "";
     |FINSI;
     eaDestino     = "C:/DOCUMENTOS/DSLPD/DOCDSLPD/" + aNomFichero;
     eaOrigen      = aBase + "documentos/clientes/" + aCliente + "/" + aNomFichero;

     |HAZ EnviaFicheroSinMD5;

     |XABRE "EC", eaDestino, nHandle;
     |SI FSalida < 0;
         aAlfa   = "0000 El Documento " + aNomFichero  + " no lo encuentro. Consulte con su distribuidor";
         |MENAV aAlfa;
         |ACABA;
     |FINSI;

     |SI #8#16 = "DOC";
         aRaiz          = ("doc" + (("00000" + #8#1) % -105)) < "";
         eaDirPlantilla = "";
         eaDirTrabajo   = "C:\DOCUMENTOS\DSLPD\DOCDSLPD\";
         eaDirTrabServi = aBase + "documentos/tmp/";
         eaDirSerGuarda = aBase + "documentos/clientes/" + aCliente + "/";
         eaDirSerPlan   = aBase + "documentos/plantillas/";
         eaFicheroTra   = aRaiz + ".doc";
         eaFicheroTxt   = aRaiz + ".txt";
         eaModoPresenta = "I";

         |HAZ MiraDirectorios;
         |HAZ EditaModoExterno;

         eaDestino = eaDirTrabajo + eaFicheroTra;
         |RFBORRA eaDestino;
     |FINSI;

     |SI #8#16 = "XML";
         aRaiz          = ("doc" + (("00000" + #8#1) % -105)) < "";
         eaDirPlantilla = "";
         eaDirTrabajo   = "C:\DOCUMENTOS\DSLPD\DOCDSLPD\";
         eaDirTrabServi = aBase + "documentos/tmp/";
         eaDirSerGuarda = aBase + "documentos/clientes/" + aCliente + "/";
         eaDirSerPlan   = aBase + "documentos/plantillas/";
         eaFicheroTra   = aRaiz + ".xml";
         eaFicheroTxt   = aRaiz + ".txt";
         eaFicheroXgz   = aRaiz + ".xgz";
         eaFicheroTgz   = aRaiz + ".tgz";
         eaFicheroTar   = aRaiz + ".tar";
         eaOrigen       = eaDirSerGuarda + eaFicheroXgz;
         eaDestino      = eaDirTrabajo   + eaFicheroTgz;
         eaModoPresenta = "I";
         |MKDIR eaDirTrabServi;

         |HAZ MiraDirectorios;
         |HAZ EnviaFicheroSinMD5;
         |HAZ DescomprimeFichero;

         eaDestino = eaDirTrabajo + eaFicheroTra;

         |HAZ EditaModoExterno;

         eaDestino = eaDirTrabajo + eaFicheroTra;
         |RFBORRA eaDestino;
     |FINSI;

     |SI #8#16 = "XGZ";
         aRaiz          = ("doc" + (("00000" + #8#1) % -105)) < "";
         eaDirPlantilla = "";
         eaDirTrabajo   = "C:\DOCUMENTOS\DSLPD\DOCDSLPD\";
         eaDirTrabServi = aBase + "documentos/tmp/";
         eaDirSerGuarda = aBase + "documentos/clientes/" + aCliente + "/";
         eaDirSerPlan   = aBase + "documentos/plantillas/";
         eaFicheroTra   = aRaiz + ".doc";
         eaFicheroTxt   = aRaiz + ".txt";
         eaFicheroXgz   = aRaiz + ".xgz";
         eaFicheroTgz   = aRaiz + ".tgz";
         eaFicheroTar   = aRaiz + ".tar";
         eaOrigen       = eaDirSerGuarda + eaFicheroXgz;
         eaDestino      = eaDirTrabajo   + eaFicheroTgz;
         eaModoPresenta = "I";
         |MKDIR eaDirTrabServi;

         |HAZ MiraDirectorios;
         |HAZ EnviaFicheroSinMD5;
         |HAZ DescomprimeFichero;

         eaDestino = eaDirTrabajo + eaFicheroTra;

         |HAZ EditaModoExterno;

         eaDestino = eaDirTrabajo + eaFicheroTra;
         |RFBORRA eaDestino;
     |FINSI;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 0_5
|| ------------------------------------------------------

|PROCESO Boton0_5;
     |VENTANA 0501, 2799, -1, 1, "";
     nVentana = FSalida;

     enCliente     = #1#1;
     enDocumento   = #8#1;
     enIncidencias = 0;
     enFormulario  = 0;
     enErrorCorreo = 1;
     |SUB_EJECUTA "dslpw995";

     |FINVENTANA nVentana;

     |SI enErrorCorreo = 1;  |ACABA;  |FINSI;

     nSitAnt    = enEstado;
     aOperacion = "Envio Correo";
     nSitAct    = enEstado;
     |HAZ GrabaLogs;
|FPRC;

|PROCESO EventoPanta0;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#8=;
         |SI FSalida = 0;
             |ESTADO_CONTROL nBoton0_1, "ENABLE";
             |ESTADO_CONTROL nBoton0_2, "ENABLE";
             |ESTADO_CONTROL nBoton0_3, "ENABLE";
             |ESTADO_CONTROL nBoton0_4, "ENABLE";
             |ESTADO_CONTROL nBoton0_5, "ENABLE";
         |SINO;
             |ESTADO_CONTROL nBoton0_1, "DISABLE";
             |ESTADO_CONTROL nBoton0_2, "DISABLE";
             |ESTADO_CONTROL nBoton0_3, "DISABLE";
             |ESTADO_CONTROL nBoton0_4, "DISABLE";
             |ESTADO_CONTROL nBoton0_5, "DISABLE";
         |FINSI;

         |SI #8#16 ! "DOC";  |Y #8#16 ! "XML";  |Y #8#16 ! "XGZ";
             |ESTADO_CONTROL nBoton0_4, "DISABLE";
         |FINSI;

         |SI #8#14 ! 10;  |Y #8#14 ! 60;  |Y #8#14 ! 61;  |Y #8#14 ! 20;  |Y #8#14 ! 90;
             |ESTADO_CONTROL nBoton0_5, "DISABLE";
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO BotonesPanta0;
     |CONTROL_SIMPLE nPanta0, "BOTON, Datos del Documento", 1277, 1398, Boton0_1;
     nBoton0_1 = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON, Editar el Documento", 1577, 1698, Boton0_2;
     nBoton0_2 = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON, Baja del Documento", 2477, 2598, Boton0_3;
     nBoton0_3 = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON, Imprimir el Documento", 1877, 1998, Boton0_4;
     nBoton0_4 = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON, Correo", 2177, 2298, Boton0_5;
     nBoton0_5 = FSalida;

     |ESTADO_CONTROL nBoton0_1, "DISABLE";
     |ESTADO_CONTROL nBoton0_2, "DISABLE";
     |ESTADO_CONTROL nBoton0_3, "DISABLE";
     |ESTADO_CONTROL nBoton0_4, "DISABLE";
     |ESTADO_CONTROL nBoton0_5, "DISABLE";
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     |ABRET #8;

     FSalida = 2899;
|FPRC;

|PROCESO Tipo90;  |TIPO 90;
     |CIERRAT #8;
|FPRC;

|PROGRAMA;
     |CABEZA "Gestion Documental";

     |DBASE aBase;   |QBF aBase;

     aDirLocal = "C:\DOCUMENTOS";                     |RMKDIR aDirLocal;
     aDirLocal = "C:\DOCUMENTOS\DSLPD";               |RMKDIR aDirLocal;
     aDirLocal = "C:\DOCUMENTOS\DSLPD\PLANTILLAS";    |RMKDIR aDirLocal;
     aDirLocal = "C:\DOCUMENTOS\DSLPD\DOCDSLPD";      |RMKDIR aDirLocal;
     aDirLocal = "C:\DOCUMENTOS\DSLPD\DOCDSLPD\";

     |PINPA #0#8;  nPanta0 = FSalida;
     |PINDA #0#8;

     |ABRE #1;
     |SELECT #2#1;
     #1#1 = enCliente;
     |LEE 000#1=;
     |SI FSalida ! 0;  |DDEFECTO #1;  |FINSI;
     |CIERRA #1;

     |SI eaTipoEnvio = "I";  |O eaTipoEnvio = "C";
         #8#0 = enCliente;
         #8#1 = 99999;
         |LEE 000#8];
         |SI FSalida = 0;
             |LEE 000#8a;
         |SINO;
             |LEE 000#8u;
         |FINSI;
         |SI FSalida ! 0;  |O #8#0 ! enCliente;
             |DDEFECTO #8;
         |FINSI;

         nDocu  = #8#1;

         |SI eaTipoEnvio = "I";    |HAZ Boton0_4;  |FINSI;
         |SI eaTipoEnvio = "C";
             |HAZ Boton0_5;
             |SI enErrorCorreo = 1;
                 |LEE 101#8=;
                 |SI FSalida = 0;
                     |HAZ Boton0_3;
                 |FINSI;
             |FINSI;
         |FINSI;

         |ACABA;
     |FINSI;

     |PINTA 911, #1#1;
     |PINTA 920, #1#2;

     |HAZ BotonesPanta0;

     nRango = 4 + 8 + 16 + 32;
     nPos1  = 1102;
     nPos2  = 2675;
     nDocu  = 1;

     |SI enEstado = 20;  |O enEstado = 34;
         #8#0 = enCliente;
         #8#1 = 99999;
         |LEE 000#8];
         |SI FSalida = 0;
             |LEE 000#8a;
         |SINO;
             |LEE 000#8u;
         |FINSI;
         |SI FSalida ! 0;  |O #8#0 ! enCliente;
             |DDEFECTO #8;
         |FINSI;

         nDocu  = #8#1;
     |FINSI;

     nDLinea = 1;
     nHLinea = 99999;

     |SI enDocu ! 0;
         nDLinea = enDocu;
         nHLinea = enDocu;
     |FINSI;

     |LINEAL_SIMPLE #1#8, nRango, nPos1, nPos2, BajaPanta0, AltaPanta0, EventoPanta0;
     nGrid1 = FSalida;

     |FOCOTECLADO nGrid1;

     #8#1 = nDocu;
     FSalida = 1;
     |HAZ EventoPanta0;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::Salir," + nGrid1;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nBoton0_1;
     |FIN_CONTROL_WINDOWS nBoton0_2;
     |FIN_CONTROL_WINDOWS nBoton0_3;
     |FIN_CONTROL_WINDOWS nBoton0_4;
     |FIN_CONTROL_WINDOWS nBoton0_5;

     |DESTRUYECONTROL nGrid1;

     |PULSATECLA;
|FPRO;
