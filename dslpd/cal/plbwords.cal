|FICHEROS;
     dslpm000 #100;
     dslpm001 #1;
|FIN;

|VARIABLES;
     aLong            = "";
     aCadena          = "";
     aIPRemoto        = "";
     aAlfa            = "";
     aCaracter        = "";
     aLen             = "";
     aDecima          = "";
     aEntero          = "";
     aImpor1          = "";
     aImpor2          = "";
     aDocRemoto       = "";
     aDocServidor     = "";
     aBase            = "";
     aDirec           = "";
     aLogo            = "";
     aGuarda          = "";
     aAbreId1         = "";
     aAbreId2         = "";
     aAbre            = "";
     aPath            = "";
     aLongitud        = "";
     aSit             = "";
     aNomLogo         = "";
     aLongAlfa        = "";
     aParaQuitar      = "";
     aFicheroLCK      = "";
     aFicheroIMA      = "";

     nLen             = 0;
     nLongitud        = 0;
     nSer             = 0;
     nLong            = 0;
     nBarra           = 0;
     nWord            = 0;
     nCar             = 0;
     nPos             = 0;
     nCampo           = 0;
     nDecimal         = 0;
     nCaracter        = 0;
     nHandle          = 0;
     nHandleYa        = 0;
     nTope            = 0;
     nPuntero         = 0;
     nIdSalir         = 0;
     nIdSalir1        = 0;
     nIdSalir2        = 0;
     nIdSalir3        = 0;
     nLinea           = 0;
     nHay             = 0;
     nBlanco1         = 0;
     nBlanco2         = 0;
     nComa            = 0;
     nPara            = 0;
     nPosi            = 0;
     nLongNume        = 0;
     nPausa           = 0;
     nVeces           = 0;

     &eaAlfa          = "";
     &eaImporte       = "";
     &eaPathLogo      = "";
     &eaAsigna        = "";
     &eaConten        = "";
     &eaMes           = "";
     &eaModoPresenta  = "";
     &enWord1         = 0;
     &enWord2         = 0;
     &enCamWord       = 0;
     &enModoExterno   = 0;
     &enHandleTxt     = 0;
     &enHandleTxt2    = 0;
     &enCargaPuntero  = 0;
     &efFecha         = @;
     &eaFecha         = "";

     &eaDirPlantilla  = "";
     &eaDirTrabajo    = "";
     &eaDirTrabServi  = "";
     &eaDirDocId2     = "";
     &eaDirTraId2     = "";
     &eaDirSerGuarda  = "";
     &eaDirSerPlan    = "";
     &eaOrigen        = "";
     &eaDestino       = "";
     &eaDestinoTra    = "";
     &eaFicheroTra    = "";
     &eaFicheroDef    = "";
     &eaFicheroXgz    = "";
     &eaFicheroTgz    = "";
     &eaFicheroTar    = "";
     &eaFicheroTxt    = "";
     &eaFicheroPlaGz  = "";
     &eaFicheroPlaMl  = "";
     &eaEtiqueta      = "";
     &eaPlanilla      = "";
     &enTipoDoc       = 0;
     &eaCadena1       = "";
     &eaCadena2       = "";
     &eaCadena3       = "";
     &eaCadena        = "";
     &eaParametro1    = "";
     &eaParametro2    = "";
     &eaParametro3    = "";
     &eaParametro4    = "";
     &eaParametro5    = "";

     {1}aContiene     = "";
     {1}aVersion      = "";
     {2}nNivel        = 0;
     {18}aCarac       = "";

        &enError      = 0;
|FIN;

|PROCESO EjecutaDSXML;
     |SI eaParametro4 = "";
         eaParametro4 = "C:\DOCUMENTOS\DSLPD\PLANTILLAS";  |RMKDIR eaParametro4;
         eaParametro4 = "C:\DOCUMENTOS\DSLPD\PLANTILLAS\";
     |FINSI;

     || eaParametro1 = Path y fichero exe
     || eaParametro2 = Path y fichero txt
     || eaParametro3 = Origen  del dsxml
     || eaParametro4 = Destino 1 del dsxml
     || eaParametro5 = Destino 2 del dsxml

     |XABRE "EC", eaParametro2, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     aFicheroLCK  = eaParametro2 < "";
     aFicheroLCK  = aFicheroLCK - ".txt" + ".lck";
     aFicheroIMA  = eaParametro5 < "";
     aFicheroIMA  = aFicheroIMA + "~$" + (eaFicheroTra % 315);  |QBF aFicheroIMA;

     |SI eaParametro3 ! "";
         eaOrigen       = eaParametro3 + "dsxml.exe";
         eaDestino      = eaParametro4 + "dsxml.exe";
         eaDestinoTra   = eaParametro5 + "dsxml.exe";
         |HAZ EnviaFicheroConMD5;
     |FINSI;

     |RFBORRA aFicheroLCK;

     aAlfa  = "START " + eaParametro1 + " " + eaParametro2;
     |RSYSTEM aAlfa;

     |PARA;  |SI;  |HACIENDO;
             |PARA nPausa = 1; |SI nPausa [ 10000; |HACIENDO nPausa = nPausa + 1;
             |SIGUE;

             |XABRE "EC", aFicheroLCK, nHandle;
             |SI FSalida ] 0;
                 |SAL;
             |FINSI;

             |PULSATECLA;
     |SIGUE;

     |PARA nVeces = 0;  |SI nVeces [ 20;  |HACIENDO nVeces = nVeces + 1;
             |XABRE "EC", aFicheroIMA, nHandle;
             |SI FSalida < 0;  |SAL;  |FINSI;

             |MENSA "0000 Esperando cierre del documento.";
             |SLEEP 1;
             |PULSATECLA;
     |SIGUE;

     |RFBORRA eaParametro2;
     |RFBORRA aFicheroLCK;

     |PULSATECLA;
     |MENSA "0000  ";
|FPRC;

|PROCESO EjecutaDSAbreExtension;
     || eaParametro1 = Path y fichero exe
     || eaParametro2 = Path y fichero a editar
     || eaParametro3 = Origen  del dsabreextension
     || eaParametro4 = Destino 1 del dsabreextension
     || eaParametro5 = Destino 2 del dsabreextension

     |XABRE "EC", eaParametro2, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     aFicheroLCK  = (eaParametro1 - "dsabreextension") + "fin" + Usuario + ".txt";

     |SI eaParametro3 ! "";
         eaOrigen       = eaParametro3 + "dsabreextension.exe";
         eaDestino      = eaParametro4 + "dsabreextension.exe";
         eaDestinoTra   = eaParametro5 + "dsabreextension.exe";
         |HAZ EnviaFicheroConMD5;
     |FINSI;

     |RFBORRA aFicheroLCK;

     aAlfa  = "START " + eaParametro1 + " " + eaParametro2 + " " + aFicheroLCK;
     |RSYSTEM aAlfa;

     |PARA;  |SI;  |HACIENDO;
             |PARA nPausa = 1; |SI nPausa [ 10000; |HACIENDO nPausa = nPausa + 1;
             |SIGUE;

             |XABRE "EC", aFicheroLCK, nHandle;
             |SI FSalida ] 0;
                 |SAL;
             |FINSI;

             |PULSATECLA;
     |SIGUE;

     |RFBORRA aFicheroLCK;

     |PULSATECLA;
|FPRC;

|PROCESO QuitaRaros;
     |QBF eaAlfa;

     aCarac1   = "¥";
     aCarac2   = "¤";
     aCarac3   = "á";
     aCarac4   = " ";
     aCarac5   = "‚";
     aCarac6   = "¡";
     aCarac7   = "¢";
     aCarac8   = "£";
     aCarac9   = "§";
     aCarac10  = "¦";
     aCarac11  = "¨";
     aCarac12  = "€";
     aCarac13  = "‡";
     aCarac14  = "…";
     aCarac15  = "Š";
     aCarac16  = "";
     aCarac17  = "•";
     aCarac18  = "—";

     IaCarac = aCarac1 <-;
     nHay    = 0;
     |PARA nCampo = 1;  |SI nCampo [ 18;  |HACIENDO nCampo = nCampo + 1;
           aCaracter = eaAlfa - aCarac;
           |SI FSalida ! 0;  nHay = 1;  |SAL;  |FINSI;

           IaCarac = IaCarac + 1;
     |SIGUE;

     |SI nHay = 0;  |ACABA;  |FINSI;

     aCadena = "";
     aLen    = eaAlfa % 0;
     nLen    = aLen;
     |PARA nCampo = 1;  |SI nCampo [ nLen;  |HACIENDO nCampo = nCampo + 1;
           nPos = (100 * nCampo) + 1;
           aCaracter = eaAlfa % nPos;

           |SI aCaracter = aCarac1;   aCaracter = &0209; |FINSI;
           |SI aCaracter = aCarac2;   aCaracter = &0241;   |FINSI;
           |SI aCaracter = aCarac3;   aCaracter = &0225;   |FINSI;
           |SI aCaracter = aCarac4;   aCaracter = &0225;   |FINSI;
           |SI aCaracter = aCarac5;   aCaracter = &0233;   |FINSI;
           |SI aCaracter = aCarac6;   aCaracter = &0237;   |FINSI;
           |SI aCaracter = aCarac7;   aCaracter = &0243;   |FINSI;
           |SI aCaracter = aCarac8;   aCaracter = &0250;   |FINSI;
           |SI aCaracter = aCarac9;   aCaracter = &0176;   |FINSI;
           |SI aCaracter = aCarac10;  aCaracter = &0170;   |FINSI;
           |SI aCaracter = aCarac11;  aCaracter = &0191;   |FINSI;
           |SI aCaracter = aCarac12;  aCaracter = &0199;   |FINSI;
           |SI aCaracter = aCarac13;  aCaracter = &0231;   |FINSI;
           |SI aCaracter = aCarac14;  aCaracter = &0224;   |FINSI;
           |SI aCaracter = aCarac15;  aCaracter = &0232;   |FINSI;
           |SI aCaracter = aCarac16;  aCaracter = &0236;   |FINSI;
           |SI aCaracter = aCarac17;  aCaracter = &0242;   |FINSI;
           |SI aCaracter = aCarac18;  aCaracter = &0249;   |FINSI;

           aCadena = aCadena + aCaracter;
     |SIGUE;
     eaAlfa = aCadena;
|FPRC;

|PROCESO QuitaBlanco;
     |PARA;  |SI;  |HACIENDO;
         |QBF aParaQuitar;
         |SI aParaQuitar ! "";
             aCaracter = aParaQuitar % 101;
             |SI aCaracter = " ";
                 aParaQuitar = aParaQuitar % 240;
             |SINO;
                 |SAL;
             |FINSI;
         |SINO;
             |SAL;
         |FINSI;
     |SIGUE;
|FPRC;

|PROCESO SeparaNombre;
     || Busca una coma en la eaCadena

     eaCadena1 = "";
     eaCadena2 = "";
     eaCadena3 = "";

     nBlanco1 = 0;
     nBlanco2 = 0;
     nComa    = 0;

     |PARA nPara = 1; |SI nPara [ 40; |HACIENDO nPara = nPara + 1;
           nPosi     = nPara * 100 + 1;
           aCaracter = eaCadena % nPosi;

           |SI aCaracter = ","; nComa = nPara; |SAL; |FINSI;
     |SIGUE;

     |SI nComa = 0;
         || Si no hay coma busca el primer blanco empezando por detras

         aLongAlfa = eaCadena % 0;
         nLongNume = aLongAlfa;

         |PARA nPara = nLongNume; |SI nPara ] 1; |HACIENDO nPara = nPara - 1;
               nPosi = nPara * 100 + 1;
               aCaracter = eaCadena % nPosi;
               |SI aCaracter = " "; nComa = nPara; |SAL; |FINSI;
         |SIGUE;
     |FINSI;

     || Coge los dos primeros aCaracteres del segundo apellido

     nBlanco1  = nComa - 1;
     nPosi     = nBlanco1 + 100;
     eaCadena2 = eaCadena % nPosi;
     aLongAlfa = eaCadena2 % 0;
     nLongNume = aLongAlfa;

     |PARA nPara = nLongNume; |SI nPara ] 1; |HACIENDO nPara = nPara - 1;
           nPosi     = nPara * 100 + 1;
           aCaracter = eaCadena2 % nPosi;
           |SI aCaracter = " "; nBlanco1 = nPara; |SAL; |FINSI;
     |SIGUE;

     nBlanco1  = nBlanco1 + 1;
     nPosi     = nBlanco1 * 100 + nLongNume;
     eaCadena2 = eaCadena2 % nPosi;

     || Coge las dos primeros caracteres del primer apellido

     nBlanco1  = nBlanco1 - 1;
     nPosi     = 100 + nBlanco1;
     eaCadena1 = eaCadena % nPosi;

     || Recoge los dos primero aCaracteres de Nombre

     nPosi     = ((nComa + 1) * 100) + 40;
     eaCadena3 = eaCadena % nPosi;

     aParaQuitar = eaCadena1; |HAZ QuitaBlanco; eaCadena1 = aParaQuitar;
     aParaQuitar = eaCadena2; |HAZ QuitaBlanco; eaCadena2 = aParaQuitar;
     aParaQuitar = eaCadena3; |HAZ QuitaBlanco; eaCadena3 = aParaQuitar;

     |QBF eaCadena1;
     |QBF eaCadena2;
     |QBF eaCadena3;
|FPRC;

|PROCESO SeparaImporte;    || Calculo para poner las puntos y comas
                           || en el importe
     aDecima   = "";
     aEntero   = "";
     aLong     = eaImporte % 0;
     nLong     = aLong;
     nDecimal  = 0;
     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPos      = (100 * nCaracter) + 1;
           aCaracter = eaImporte % nPos;
           |SI aCaracter = ".";
               nDecimal = 1;
           |SINO;
               |SI nDecimal = 0;
                   aEntero = aEntero + aCaracter;
               |SINO;
                   aDecima = aDecima + aCaracter;
               |FINSI;
           |FINSI;
     |SIGUE;

     aDecima   = (aDecima + "00") % 102;
     eaImporte = aEntero;

     aLong = eaImporte % 0;
     nLong = aLong;
     eaImporte = ("      " + eaImporte) % -106;
     aImpor1   = eaImporte % -103;
     aImpor2   = eaImporte % -403;

     |SI aImpor2 ! "   ";  |Y aImpor2 ! "  -";
         eaImporte = aImpor2 + "." + aImpor1;
     |SINO;
         eaImporte = aImpor1;
     |FINSI;

     eaImporte = ("          " + eaImporte + "," + aDecima) % -110;
|FPRC;

|PROCESO SacaFecha;
     aAlfa = efFecha % 402;
     |SI aAlfa = "01";  eaMes = "Enero";      |FINSI;
     |SI aAlfa = "02";  eaMes = "Febrero";    |FINSI;
     |SI aAlfa = "03";  eaMes = "Marzo";      |FINSI;
     |SI aAlfa = "04";  eaMes = "Abril";      |FINSI;
     |SI aAlfa = "05";  eaMes = "Mayo";       |FINSI;
     |SI aAlfa = "06";  eaMes = "Junio";      |FINSI;
     |SI aAlfa = "07";  eaMes = "Julio";      |FINSI;
     |SI aAlfa = "08";  eaMes = "Agosto";     |FINSI;
     |SI aAlfa = "09";  eaMes = "Septiembre"; |FINSI;
     |SI aAlfa = "10";  eaMes = "Octubre";    |FINSI;
     |SI aAlfa = "11";  eaMes = "Noviembre";  |FINSI;
     |SI aAlfa = "12";  eaMes = "Diciembre";  |FINSI;

     eaAlfa  = (efFecha % 102) + " de " + eaMes + " de " + (efFecha % -104);
     eaFecha = eaAlfa;
|FPRC;

|PROCESO EnviaFicheroConMD5;
     aContiene1 = eaDestino;
     |ESPECIFICA "REMOTOMD5", aContiene;
     aDocRemoto = FSalida;  |QBF aDocRemoto;

     aContiene1 = eaOrigen;
     |ESPECIFICA "MD5", aContiene;
     aDocServidor = FSalida;  |QBF aDocServidor;

     |SI aDocRemoto ! aDocServidor;
         aIPRemoto = "";
         |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

         |SI aIPRemoto ! "";
             |ENVIA_FICHERO eaOrigen, eaDestino;
         |SINO;
             |COPIA_FICHERO eaOrigen, eaDestino;
         |FINSI;
     |FINSI;

     |SI eaDestinoTra ! "";  |Y eaDestinoTra ! eaDestino;
         |RCOPIA_FICHERO eaDestino, eaDestinoTra;
     |FINSI;
|FPRC;

|PROCESO EnviaFicheroSinMD5;
     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |SI aIPRemoto ! "";
         |ENVIA_FICHERO eaOrigen, eaDestino;
     |SINO;
         |COPIA_FICHERO eaOrigen, eaDestino;
     |FINSI;
|FPRC;

|PROCESO RecibeFichero;
     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |SI aIPRemoto ! "";
         |RECIBE_FICHERO eaDestino, eaOrigen;
     |SINO;
         |COPIA_FICHERO eaDestino, eaOrigen;
     |FINSI;
|FPRC;

|PROCESO CopiaRTF;
     |DBASE aBase;   |QBF aBase;

     eaDestinoTra  = eaDirTrabajo   + "planilla.rtf";
     eaDestino     = eaDirPlantilla + "planilla.rtf";
     eaOrigen      = eaDirSerPlan   + "planilla.rtf";
     |HAZ EnviaFicheroConMD5;
|FPRC;

|PROCESO DescomprimeFichero;
     |SI eaFicheroPlaGz ! eaFicheroTgz;  |Y eaFicheroPlaGz ! "";
         eaOrigen  = eaDirTrabajo + eaFicheroPlaGz;
         eaDestino = eaDirTrabajo + eaFicheroTgz;
         |RRENOMBRA_FICHERO eaOrigen, eaDestino;
     |FINSI;

     aAlfa = eaDirTrabajo + eaFicheroTgz;
     |RDESCOMPRIME aAlfa;
     |RDETAR aAlfa, eaDirTrabajo;

     aAlfa = eaDirTrabajo + eaFicheroTgz;  |RFBORRA aAlfa;
     aAlfa = eaDirTrabajo + eaFicheroTar;  |RFBORRA aAlfa;

     |SI eaFicheroPlaGz ! eaFicheroTgz;  |Y eaFicheroPlaGz ! "";
         eaOrigen  = eaDirTrabajo + eaFicheroPlaMl;
         eaDestino = eaDirTrabajo + eaFicheroTra;
         |RRENOMBRA_FICHERO eaOrigen, eaDestino;
     |FINSI;
|FPRC;

|PROCESO ComprimeFichero;
     |SI eaFicheroTra ! eaFicheroDef;
         eaOrigen  = eaDirTrabajo + eaFicheroTra;
         eaDestino = eaDirTrabajo + eaFicheroDef;
         |RRENOMBRA_FICHERO eaOrigen, eaDestino;
     |FINSI;

     aAlfa = eaDirTrabajo + eaFicheroTar + " " + eaFicheroDef;
     |RATAR aAlfa, eaDirTrabajo;

     aAlfa = eaDirTrabajo + eaFicheroTar;
     |RCOMPRIME aAlfa;

     aAlfa = eaDirTrabajo + eaFicheroTar;  |RFBORRA aAlfa;
     aAlfa = eaDirTrabajo + eaFicheroDef;  |RFBORRA aAlfa;
|FPRC;

|PROCESO CreaDirectorios;
     aLong   = aAlfa % 0;
     nLong   = aLong;
     nBarra  = 0;
     aCadena = "";
     |PARA nCar = 1;  |SI nCar [ nLong;  |HACIENDO nCar = nCar + 1;
           nPos      = (nCar * 100) + 1;
           aCaracter = aAlfa % nPos;
           |SI aCaracter = "/";  |O aCaracter = "\";
               nBarra = nBarra + 1;
               |SI nBarra ! 1;
                   |SI nSer = 0;  |RMKDIR aCadena;  |FINSI;
                   |SI nSer = 1;  |MKDIR aCadena;   |FINSI;
               |FINSI;
           |FINSI;
           aCadena = aCadena + aCaracter;
     |SIGUE;
|FPRC;

|PROCESO MiraDirectorios;
     aAlfa = eaDirPlantilla;  |QBF aAlfa;
     |SI aAlfa ! "";  nSer = 0;  |HAZ CreaDirectorios;  |FINSI;

     aAlfa = eaDirTrabajo;  |QBF aAlfa;
     |SI aAlfa ! "";  nSer = 0;  |HAZ CreaDirectorios;  |FINSI;

     aAlfa = eaDirDocId2;  |QBF aAlfa;
     |SI aAlfa ! "";  nSer = 0;  |HAZ CreaDirectorios;  |FINSI;

     aAlfa = eaDirSerGuarda;  |QBF aAlfa;
     |SI aAlfa ! "";  nSer = 1;  |HAZ CreaDirectorios;  |FINSI;
|FPRC;


||  WORD ------------------------------------------------------------------

|PROCESO GrabaFicheroExterno;
     aAlfa = eaAsigna % 101;
     |SI aAlfa ! "V";  |Y aAlfa ! "M";
      |Y aAlfa ! "I";  |Y aAlfa ! "T";
      |Y aAlfa ! "X";
         |ACABA;
     |FINSI;

     aAlfa = (eaAsigna % 101) + &13 + &10;  |XGRABA enHandleTxt, aAlfa;
     aAlfa = (eaAsigna % 240) + &13 + &10;  |XGRABA enHandleTxt, aAlfa;
     aAlfa = eaConten + &13 + &10;          |XGRABA enHandleTxt, aAlfa;
|FPRC;

|PROCESO PegarWordId1;
     aAbre = eaDirTrabServi + "asignaya.txt";
     |XABRE "WB", aAbre, nHandleYa;

     aAlfa = "[FICHERO]#" + eaDirTrabajo + eaFicheroTra + &13 + &10;
     |XGRABA nHandleYa, aAlfa;

     aAlfa = "M" + &13 + &10;                  |XGRABA nHandleYa, aAlfa;
     aAlfa = eaAsigna + &13 + &10;             |XGRABA nHandleYa, aAlfa;
     aAlfa = eaConten + &13 + &10;             |XGRABA nHandleYa, aAlfa;
     |XCIERRA nHandleYa;

     eaOrigen       = eaDirTrabServi + "asignaya.txt";
     eaDestino      = eaDirTrabajo   + "asignaya.txt";
     |HAZ EnviaFicheroSinMD5;

     |FBORRA eaOrigen;

     eaParametro1 = eaDirTrabajo + "dsxml";
     eaParametro2 = eaDirTrabajo + "asignaya.txt";
     eaParametro3 = eaDirSerPlan;
     eaParametro4 = eaDirPlantilla;
     eaParametro5 = eaDirTrabajo;

     |HAZ EjecutaDSXML;
|FPRC;

|PROCESO AbreWordExterno;
     eaParametro1 = eaDirTrabajo + "dsxml";
     eaParametro2 = eaDirTrabajo + eaFicheroTxt;
     eaParametro3 = eaDirSerPlan;
     eaParametro4 = eaDirPlantilla;
     eaParametro5 = eaDirTrabajo;

     |HAZ EjecutaDSXML;
|FPRC;

|PROCESO EditaModoExterno;
     aAbre = eaDirTrabServi + eaFicheroTxt;
     |XABRE "WB", aAbre, enHandleTxt;

     aAlfa = "[FICHERO]#" + eaDirTrabajo + eaFicheroTra + &13 + &10;
     |XGRABA enHandleTxt, aAlfa;

     aAlfa = "[IMPRESORA]#" + eaModoPresenta + &13 + &10;
     |XGRABA enHandleTxt, aAlfa;

     |XCIERRA enHandleTxt;

     eaOrigen       = eaDirTrabServi + eaFicheroTxt;
     eaDestino      = eaDirTrabajo   + eaFicheroTxt;
     |HAZ EnviaFicheroSinMD5;

     |FBORRA eaOrigen;

     |HAZ AbreWordExterno;
|FPRC;

|PROCESO TraeLogo;
     aDirec = aNomLogo;  |QBF aDirec;
     aLong  = aDirec % 0;
     nLong  = aLong;
     aLogo  = "";
     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPos      = (100 * nCaracter) + 1;
           aCaracter = aDirec % nPos;
           |SI aCaracter = "/";  |O aCaracter = "\";
               aLogo = "";
           |SINO;
               aLogo = aLogo + aCaracter;
           |FINSI;
     |SIGUE;

     eaOrigen      = aNomLogo;   |QBF eaOrigen;
     eaDestino     = "C:\DOCUMENTOS\DSLPD\LOGOS";   |RMKDIR eaDestino;
     eaDestino     = "C:\DOCUMENTOS\DSLPD\LOGOS\" + aLogo;
     eaDestinoTra  = eaDestino;
     |HAZ EnviaFicheroConMD5;

     |XABRE "EC", eaDestino, nHandleYa;
     |SI FSalida < 0;
         eaDestino = "";
     |FINSI;
|FPRC;

|PROCESO MiraLogos;
     |ABRE #100;
     |LEE 000#100p;
     |SI FSalida ! 0;  |DDEFECTO #100;  |FINSI;
     |CIERRA #100;

     aNomLogo = #100#16;

     |SI enTipoDoc = 20;
        aNomLogo = #100#22;
     |FINSI;

     |SI enTipoDoc = 90; |O enTipoDoc = 91; |O enTipoDoc = 92; |O enTipoDoc = 93;
        aNomLogo = #100#23;
     |FINSI;

     |HAZ TraeLogo;

     |SI eaDestino ! "";
         aAlfa = "I" + &13 + &10;
         |XGRABA enHandleTxt, aAlfa;

         aAlfa = "<<MEMBRETE>>" + &13 + &10;
         |XGRABA enHandleTxt, aAlfa;

         aAlfa = eaDestino + &13 + &10;
         |XGRABA enHandleTxt, aAlfa;
     |SINO;
         aAlfa = "V" + &13 + &10;
         |XGRABA enHandleTxt, aAlfa;

         aAlfa = "<<MEMBRETE>>" + &13 + &10;
         |XGRABA enHandleTxt, aAlfa;

         aAlfa = " " +  &13 + &10;
         |XGRABA enHandleTxt, aAlfa;
     |FINSI;

     aNomLogo = #1#38;
     |HAZ TraeLogo;

     |SI eaDestino ! "";
         aAlfa = "I" + &13 + &10;
         |XGRABA enHandleTxt, aAlfa;

         aAlfa = "<<MEMBRETECLI>>" + &13 + &10;
         |XGRABA enHandleTxt, aAlfa;

         aAlfa = eaDestino + &13 + &10;
         |XGRABA enHandleTxt, aAlfa;
     |SINO;
         aAlfa = "V" + &13 + &10;
         |XGRABA enHandleTxt, aAlfa;

         aAlfa = "<<MEMBRETECLI>>" + &13 + &10;
         |XGRABA enHandleTxt, aAlfa;

         aAlfa = " " +  &13 + &10;
         |XGRABA enHandleTxt, aAlfa;
     |FINSI;

     aNomLogo = #100#18;
     |HAZ TraeLogo;
     |SI eaDestino ! "";
         aAlfa = "I" + &13 + &10;
         |XGRABA enHandleTxt, aAlfa;

         aAlfa = "<<FIRMA>>" + &13 + &10;
         |XGRABA enHandleTxt, aAlfa;

         aAlfa = eaDestino + &13 + &10;
        |XGRABA enHandleTxt, aAlfa;
     |SINO;
         aAlfa = "V" + &13 + &10;
         |XGRABA enHandleTxt, aAlfa;

         aAlfa = "<<FIRMA>>" + &13 + &10;
         |XGRABA enHandleTxt, aAlfa;

         aAlfa = " " +  &13 + &10;
         |XGRABA enHandleTxt, aAlfa;
     |FINSI;
|FPRC;
