|FICHEROS;
     dslpm511 #511;
     dslpw035 #935,MANTE;
     dslpw994 #994,MANTE;
|FIN;

|VARIABLES;
     {2}nNivel = 0;
     nIdSalir = 0;

     aBase         = "";
     aAlfa         = "";
     aAlfa1        = "";
     aAlfa2        = "";
     aReg          = "";
     aEsc1         = "";
     aEsc2         = "";
     aRetu         = "";
     aTecla        = "";
     aUsuario      = "";
     aPassword     = "";

     nBuffer       = 0;
     nNumUsuarios  = 0;
     nRegistro     = 0;
     nPanta0       = 0;
     nRango        = 0;
     nPos1         = 0;
     nPos2         = 0;
     nCampoD       = 0;
     nCampoH       = 0;
     nBoton1_1     = 0;
     nBoton1_2     = 0;
     nBoton1_3     = 0;
     nBoton1_4     = 0;
     nDatos        = 0;
     nGrid         = 0;
     nPermiso      = 0;
     nExiste       = 0;
|FIN;

|PROCESO Tipo12;  |TIPO 12;
|FPRC;

|PROCESO PintaSubPanta;
     |PARA nCampoD = 0;  |SI nCampoD [ 52;  |HACIENDO nCampoD = nCampoD + 1;
           nCampoH = nCampoD + 2;
           #935nCampoD = #511nCampoH;
     |SIGUE;
     |PINDA #0#935;
|FPRC;

|PROCESO ObtenIdSalir;
     InNivel = nNivel1<-;
     nNivel1 = 0;
     |ESPECIFICA "IDBOTONTECLA", nNivel;
     nIdSalir = nNivel2;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 1_1
|| ------------------------------------------------------

|PROCESO Boton1_1;
     nIdSalir = 0;
     |HAZ ObtenIdSalir;
     |ESTADO_CONTROL nIdSalir, "HIDE";

     nDatos   = 1;
     aUsuario = #511#0;

     |ESTADO_CONTROL nBoton1_1, "DISABLE";
     |ESTADO_CONTROL nBoton1_3, "DISABLE";
     |ESTADO_CONTROL nBoton1_4, "DISABLE";
     |ESTADO_CONTROL nBoton1_2, "ENABLE";

     |ENDATOS #1#935;

     nDatos = 0;

     |ESTADO_CONTROL nBoton1_1, "ENABLE";
     |ESTADO_CONTROL nBoton1_3, "ENABLE";
     |ESTADO_CONTROL nBoton1_4, "ENABLE";
     |ESTADO_CONTROL nBoton1_2, "DISABLE";

     #511#0 = aUsuario;
     |LEE 101#511=;
     |SI FSalida = 0;
         |PARA nCampoD = 0;  |SI nCampoD [ 52;  |HACIENDO nCampoD = nCampoD + 1;
               nCampoH = nCampoD + 2;
               #511nCampoH = #935nCampoD;
         |SIGUE;
         |GRABA 020#511a;
         |LIBERA #511;
     |FINSI;
     |ESTADO_CONTROL nIdSalir, "SHOW";
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 1_2
|| ------------------------------------------------------

|PROCESO Boton1_2;
     |PTEC 806;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 1_3
|| ------------------------------------------------------

|PROCESO Boton1_3;
     |LEE 101#511=;
     |SI FSalida = 0;
         |PARA nCampoD = 2;  |SI nCampoD [ 38;  |HACIENDO nCampoD = nCampoD + 1;
               #511nCampoD = "N";
         |SIGUE;
          #511#54 = "N";
         |GRABA 020#511a;
         |LIBERA #511;

         |HAZ PintaSubPanta;
     |FINSI;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 1_3
|| ------------------------------------------------------

|PROCESO Boton1_4;
     |LEE 101#511=;
     |SI FSalida = 0;
         |PARA nCampoD = 2;  |SI nCampoD [ 38;  |HACIENDO nCampoD = nCampoD + 1;
               #511nCampoD = "S";
         |SIGUE;
         #511#54 = "S";
         |GRABA 020#511a;
         |LIBERA #511;

         |HAZ PintaSubPanta;
     |FINSI;
|FPRC;

|| ------------------------------------------------------
|| Procesos del Incio del Programa
|| ------------------------------------------------------

|PROCESO Evento511;
     |SI FSalida = 1;  |O FSalida = 2;
         |SI nDatos = 1;
             #511#0 = aUsuario;
             |LEE 000#511=;
             |REFRESCACONTROL nGrid;
             FSalida = "SKIPDEFAULT";
             |ACABA;
         |FINSI;

         |LEE 000#511=;
         |SI FSalida ! 0;  |ACABA;  |FINSI;

         |HAZ PintaSubPanta;
     |FINSI;
|FPRC;

|PROCESO dslpm511B;
     aAlfa1  = #511#0;  |QBF aAlfa1;
     nExiste = 0;
     |PARA nRegistro = 0; |SI nRegistro < nNumUsuarios; |HACIENDO nRegistro = nRegistro + 1;
           |DEL_BUF nBuffer, nRegistro, aAlfa;

           aAlfa2 = aAlfa % 410;  |QBF aAlfa2;
           |SI aAlfa1 = aAlfa2;  nExiste = 1;  |SAL;  |FINSI;
     |SIGUE;

     |SI nExiste = 0;
         |BORRA 020#511a;
     |FINSI;
     |LIBERA #511;
|FPRC;

|DEFBUCLE dslpm511B;
     #511#1;
     ;
     "          ";
     "zzzzzzzzzz";
     be;
     NULL, dslpm511B;
|FIN;

|PROCESO BuscaUsuarios;
     |DIMENSIONA 1000, nBuffer, 0;

     aBase = "";
     |DBASS aBase;  |QBF aBase;
     aAlfa  = aBase + "dev/usr/ds_sys.psw"
     |LEESECUENCIAL aAlfa, nBuffer, nNumUsuarios, 0, 0;
     |PARA nRegistro = 0; |SI nRegistro < nNumUsuarios; |HACIENDO nRegistro = nRegistro + 1;
         |DEL_BUF nBuffer, nRegistro, aAlfa;

         aReg = aAlfa % 410;  #511#0 = aReg;

         |LEE 101#511=;
         |SI FSalida ! 0;
             |DDEFECTO #511;
             #511#0 = aReg;
             |QBF aReg;

             |SI aUsuario = aReg;
                 |PARA nCampoD = 2;  |SI nCampoD [ 38;  |HACIENDO nCampoD = nCampoD + 1;
                       #511nCampoD = "S";
                 |SIGUE;
             |FINSI;

             |GRABA 020#511b;
         |FINSI;

         aReg = aAlfa % 1420;  #511#1 = aReg;

         |GRABA 020#511a;
         |LIBERA #511;
    |SIGUE;

    |BUCLE dslpm511B;

    |FINDIM nBuffer;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     |ABRET #511;

     aUsuario = Usuario % 810;
     |HAZ BuscaUsuarios;

     nPermiso = 0;
     aUsuario = Usuario % 810;
     #511#0 = aUsuario;
     |LEE 000#511=;
     |SI #511#2 = "N";
         || |MENAV "0000 Usted no tiene permiso para entrar en esta opcion";
         || nPermiso = 1;
     |FINSI;

     FSalida = 3099;
|FPRC;

|PROCESO Tipo90;  |TIPO 90;
     |CIERRAT #511;
|FPRC;

|PROCESO PidePassword;
     nPermiso = 0;
     #994#0 = "";

     |PINPA #0#994;
     |PINDA #0#994;
     |ENDATOS #1#994;
     |SI FSalida = 0;
         |SI #994#0 ! "solochim0 ";
             nPermiso = 1;
         |FINSI;
     |FINSI;
|FPRC;

|PROGRAMA;

     |SI nPermiso = 1;  |ACABA;  |FINSI;

     |HAZ PidePassword;

     |CABEZA "Permisos Usuarios";

     |PINPA #0#511;  nPanta0 = FSalida;

     |SI nPermiso = 1;
         |MENAV "0000 Password erroneo.";
         |ACABA;
     |FINSI;

     nRango = 4 + 8 + 16 + 32;
     nPos1  = 0802;
     nPos2  = 2914;

     |LEE 000#511p;

     |LINEAL_SIMPLE #1#511, nRango, nPos1, nPos2, NULL, NULL, Evento511;
     nGrid = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON,{{193}} Entrar Datos ", 2920, 2935, Boton1_1;
     nBoton1_1 = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON,{{197}} Validar Datos ", 2940, 2955, Boton1_2;
     nBoton1_2 = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON,{{199}} Quitar TODO", 2960, 2975, Boton1_3;
     nBoton1_3 = FSalida;

     |CONTROL_SIMPLE nPanta0, "BOTON,{{199}} Poner TODO", 2980, 2995, Boton1_4;
     nBoton1_4 = FSalida;

     |BORRA_HIJOS_PAN #0#511;
     |SUB_PINPA #0#935, nPanta0;

     |HAZ PintaSubPanta;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::Salir";
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nBoton1_1;
     |FIN_CONTROL_WINDOWS nBoton1_2;
     |FIN_CONTROL_WINDOWS nBoton1_3;
     |FIN_CONTROL_WINDOWS nBoton1_4;

     |DESTRUYECONTROL nGrid;

     |PULSATECLA;
|FPRO;
