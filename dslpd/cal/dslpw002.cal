|FICHEROS;
     dslpm001 #1;
     dslpm002 #2;
     dslpm100 #100;
     dslpm500 #500;
     dslpm501 #501;
     dslpm505 #505;
     dslpm506 #506;
     dslpw002 #902;

     dslpm002@ #999;
|FIN;

|VARIABLES;
     aAlfa          = "";
     aBase          = "";
     aMas           = "";

     nBoton1_1      = 0;
     nBoton1_2      = 0;

     nPanta0        = 0;
     nPanta1        = 0;
     nPanta2        = 0;
     nCampoD        = 0;
     nCampoH        = 0;
     nRango         = 0;
     nPos1          = 0;
     nPos2          = 0;
     nVentana       = 0;

     &enCliente     = 0;
     &eaNIF         = "";
     &enModo        = 1;
     &eaVarDni      = "";
     &enCalcCif     = 0;
|FIN;

|| ------------------------------------------------------
|| Procesos de la Pantalla
|| ------------------------------------------------------

|PROCESO MarcaAsesoria; |TIPO 0;
     |SI #902#36 = "N";
          #902#42 = "N"; |PINTA #902#42;
          #902#43 = "N"; |PINTA #902#43;
          #902#44 = "N"; |PINTA #902#44;
     |SINO;
          |SI #902#42 = "N"; |O #902#43 = "N"; |O #902#44 = "N";
               #902#42 = "S"; |PINTA #902#42;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO MarcaTipoAsesoria; |TIPO 0;
     |SI #902#42 = "S"; |O #902#43 = "S"; |O #902#44 = "S";
          #902#36 = "S"; |PINTA #902#36;
     |FINSI;
     |SI #902#42 = "S"; |Y Campo = 42;
          #902#43 = "N"; |PINTA #902#43;
          #902#44 = "N"; |PINTA #902#44;
     |FINSI;
     |SI #902#43 = "S"; |O #902#44 = "S";
          |SI Campo = 43; |O Campo = 44;
               #902#42 = "N"; |PINTA #902#42;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO RespFichero;
     |SI #902#15 = "S";
         |ABRE #999;
         |SELECT #3#999;
         #999#16 = "S";
         #999#0  = #2#0;
         |LEE 000#999=;
         |SI FSalida = 0;  |Y #2#1 ! #999#1;
             aAlfa = "0000 Ya hay un Responsable de Fichero asignado a la empresa " + #999#1;  |QBF aAlfa;
             aAlfa = aAlfa + " " + #999#2;  |QBF aAlfa;

             |MENAV aAlfa;

             #902#15 = "N";  |PINTA #902#15;
             |ERROR;
         |FINSI;
         |CIERRA #999;
     |FINSI;
|FPRC;

|PROCESO Gerente;
     |SI #902#32 = "S";
         |ABRE #999;
         |SELECT #2#999;
         #999#33 = "S";
         #999#0  = #2#0;
         |LEE 000#999=;
         |SI FSalida = 0;  |Y #2#1 ! #999#1;
             aAlfa = "0000 Ya hay un gerente asignado a la empresa " + #999#1;  |QBF aAlfa;
             aAlfa = aAlfa + " " + #999#2;  |QBF aAlfa;

             |MENAV aAlfa;

             #902#32 = "N";  |PINTA #902#32;
             |ERROR;
             |CIERRA #999;
             |ACABA;
         |FINSI;
         |CIERRA #999;

         #902#16 = "S";  |PINTA #902#16;
         #902#17 = "S";  |PINTA #902#17;
         #902#18 = "S";  |PINTA #902#18;
         #902#19 = "S";  |PINTA #902#19;
         #902#20 = "S";  |PINTA #902#20;
     |SINO;
         |SI enModo = 1;  |O Contenido ! #902Campo;
             #902#16 = "N";  |PINTA #902#16;
             #902#17 = "N";  |PINTA #902#17;
             #902#18 = "N";  |PINTA #902#18;
             #902#19 = "N";  |PINTA #902#19;
             #902#20 = "N";  |PINTA #902#20;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Otros;
     |C_M #902#40, 1, "S";
     |SI #902#39 = "N";  |Y #902#41 = "N";
         |C_M #902#40, 1, "N";  #902#40 = "";  |PINTA #902#40;
     |FINSI;
|FPRC;


|| ------------------------------------------------------
|| Procesos del Boton 1_1
|| ------------------------------------------------------

|PROCESO Evento506;
     |SI FSalida = 1;
         |LEE 000#506c;
     |FINSI;

     |SI FSalida = 4;
         |PTEC 806;
     |FINSI;
|FPRC;

|PROCESO Boton1_1;
     |ESTADO_CONTROL nBoton1_1, "DISABLE";
     |ESTADO_CONTROL nBoton1_2, "DISABLE";

     nRango = 4 + 8 + 16;
     nPos1  = 1130;
     nPos2  = 2450;

     |ABRE #506;
     #506#0 = #902#27;
     |LEE 000#506=;

     |VENTANA nPos1, nPos2, -1, 1, "";
     nVentana = FSalida;

     |LINEAL_SIMPLE #1#506, nRango, nPos1, nPos2, NULL, NULL, Evento506;

     |FINVENTANA nVentana;
     |PULSATECLA;

     #902#27 = #506#0;
     |PINTA #902#27;
     |CIERRA #506;

     |ESTADO_CONTROL nBoton1_1, "ENABLE";
     |ESTADO_CONTROL nBoton1_2, "ENABLE";
|FPRC;

|| ------------------------------------------------------
|| Procesos del Boton 1_2
|| ------------------------------------------------------

|PROCESO Evento505;
     |SI FSalida = 1;
         |LEE 000#505c;
     |FINSI;

     |SI FSalida = 4;
         |PTEC 806;
     |FINSI;
|FPRC;

|PROCESO Boton1_2;
     |ESTADO_CONTROL nBoton1_1, "DISABLE";
     |ESTADO_CONTROL nBoton1_2, "DISABLE";

     nRango = 4 + 8 + 16;
     nPos1  = 1102;
     nPos2  = 2498;

     |ABRE #505;
     |SELECT #2#505;
     #505#1 = #902#28;
     |LEE 000#505=;

     |VENTANA nPos1, nPos2, -1, 1, "";
     nVentana = FSalida;

     |LINEAL_SIMPLE #2#505, nRango, nPos1, nPos2, NULL, NULL, Evento505;

     |FINVENTANA nVentana;
     |PULSATECLA;

     #902#28 = #505#1;
     |PINTA #902#28;
     |CIERRA #505;

     |ESTADO_CONTROL nBoton1_1, "ENABLE";
     |ESTADO_CONTROL nBoton1_2, "ENABLE";
|FPRC;

|| ------------------------------------------------------
|| Procesos del Incio del Programa
|| ------------------------------------------------------

|PROCESO LetraDni;  |TIPO 6;
     |SI enModo ! 1;  |ACABA;  |FINSI;

     |SI FSalida = 10;
         eaVarDni   = #902Campo;
         enCalcCif  = 1;
         |HAZ CalculoDNI;
         #902Campo = eaVarDni;
         |PINTA #902Campo;
         |ERROR;
         |ACABA;
     |FINSI;

     eaVarDni  = #902Campo;
     enCalcCif = 0;
     |HAZ CalculoDNI;
     eaVarDni = (eaVarDni + "            ") % 112;
     |SI #902Campo ! eaVarDni;
         |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
         |SI FSalida = 0;
             #902Campo = eaVarDni;  |PINTA #902Campo;
             |ERROR;
         |FINSI;
     |FINSI;

     |ABRE #2;
     #2#0 = enCliente;
     #2#1 = #902#0;
     |LEE 000#2=;
     |SI FSalida = 0;
         |MENAV "0000 El DNI ya existe.";
         |ERROR;
     |FINSI;
     |CIERRA #2;
|FPRC;

|PROCESO BotonesPanta1;
     |CONTROL_SIMPLE nPanta1, "BOTON,{{155}}", 1126, 1128, Boton1_1;
     nBoton1_1 = FSalida;

     |CONTROL_SIMPLE nPanta1, "BOTON,{{155}}", 2089, 2091, Boton1_2;
     nBoton1_2 = FSalida;
|FPRC;

|PROCESO Tipo12;  |TIPO 12;
|FPRC;

|PROCESO RegrabaNif;
     #100#0 = #2#1;
     |LEE 101#100=;
     |SI FSalida ! 0;
         |DDEFECTO #100;
         #100#0 = #2#1;
         |GRABA 020#100b;
     |FINSI;

     #100#1  = #2#2;
     #100#2  = #2#3;
     #100#3  = #2#4;
     #100#4  = #2#5;
     #100#5  = #2#6;
     #100#6  = #2#7;
     #100#7  = #2#8;
     #100#8  = #2#9;
     #100#9  = #2#10;
     #100#10 = #2#11;
     #100#11 = #2#12;
     #100#12 = #2#13;
     #100#13 = #2#14;
     #100#14 = #2#15;
     #100#15 = #2#28;
     |GRABA 020#100a;
     |LIBERA #100;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     |DBASE aBase;  |QBF aBase;
     aMas = aBase + "def/dslpm002.mas";
     |CARGA_DEF aMas, dslpm002@;

     FSalida = 2899;
|FPRC;

|PROCESO Tipo90;  |TIPO 90;
     |DESCARGA_DEF #dslpm002@;
|FPRC;

|PROGRAMA;
     |ABRE #2;
     |SI enModo ! 1;
         #2#0 = enCliente;
         #2#1 = eaNIF;
         |LEE 101#2=;
         |SI FSalida ! 0;  |DDEFECTO #2; |FINSI;

         |PARA nCampoD = 1;  |SI nCampoD [ 45;  |HACIENDO nCampoD = nCampoD + 1;
               nCampoH = nCampoD - 1;
               #902nCampoH = #2nCampoD;
         |SIGUE;
     |FINSI;

     |CABEZA "Datos Personales";

     |PINPA #0#902;  nPanta0 = FSalida;
     |PINDA #0#902;

     |PINPA #1#902;  nPanta1 = FSalida;
     |PINDA #1#902;

     |PINPA #2#902;  nPanta2 = FSalida;
     |PINDA #2#902;

     |HAZ BotonesPanta1;

     |C_M #902#0, 1, "S";
     |SI enModo ! 1;
         |C_M #902#0, 1, "N";
     |FINSI;

     |SI enModo = 1;  |ENDATOS #1#902;  |FINSI;
     |SI enModo = 2;  |ENDATOS #2#902;  |FINSI;

     |SI FSalida = 0;
         #2#0 = enCliente;
         #2#1 = #902#0;
         |LEE 101#2=;
         |SI FSalida ! 0;
             |DDEFECTO #2;
             #2#0 = enCliente;
             #2#1 = #902#0;
             |GRABA 020#2b;
         |FINSI;

         |PARA nCampoD = 1;  |SI nCampoD [ 45;  |HACIENDO nCampoD = nCampoD + 1;
               nCampoH = nCampoD - 1;
               #2nCampoD = #902nCampoH;
         |SIGUE;
         |GRABA 020#2a;
         |LIBERA #2;

         |ABRE #100;
         |HAZ RegrabaNif;
         |CIERRA #100;
     |FINSI;
     |CIERRA #2;

     |FIN_CONTROL_WINDOWS nBoton1_1;
     |FIN_CONTROL_WINDOWS nBoton1_2;

     |PULSATECLA;

     eaNIF = #2#1;
|FPRO;
