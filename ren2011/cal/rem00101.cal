|FICHEROS;
     rem00101 #1;

     rew00203 #203,MANTE;
|FIN;

|VARIABLES;
      aAlfa         = "";
      aTecla        = "";
      aEsc1         = "";
      aEsc2         = "";
      aRetu         = "";

     nRango         = 0;
     nGrid          = 0;
     nCampo         = 0;
     nVentana       = 0;

     nBotonAlta     = 0;
     nBotonModi     = 0;
     nBotonBaja     = 0;
     nEBotonAlta    = 0;
     nEBotonModi    = 0;
     nEBotonBaja    = 0;

 {-1}aVent          = "";
     aVent1         = 0;
     aVent2         = 0;
     aVent3         = 0;
     aVent4         = 0;
     aVent5         = "";

 {-1} aMenuFami     = "";
      aMenuFami1    = "1232";
      aMenuFami2    = "1";
      aMenuFami3    = "2";
      aMenuFami4    = "DESCENDIENTE";
      aMenuFami5    = "ASCENDIENTE ";

     &enEmpresa     = 0;
     &enModo        = 0;
     &eaVarDni      = "";
     &enCalcCif     = 0;
     &eaConvive     = "";
     &eaEstado      = "";
     &enPanta       = 0;
|FIN;

|PROCESO PintaDatosPantalla;
     |DDEFECTO #203;
     |PARA nCampo = 0;  |SI nCampo [ 22;  |HACIENDO nCampo = nCampo + 1;
           #203nCampo = #1nCampo;

           |C_V #203nCampo, 0, aAlfa;
           |SI aAlfa = "S";  |PINTA #203nCampo;  |FINSI;

           |C_M #203nCampo, 1, "N";
     |SIGUE;

     |SI #203#17 = "00.00.0000";  #203#17 = "01.01.0000";  |FINSI;
     |SI #203#18 = "00.00.0000";  #203#18 = "01.01.0000";  |FINSI;
     |SI #203#21 = "00.00.0000";  #203#21 = "01.01.0000";  |FINSI;

     #203#23 = #203#10 % 104;
     #203#24 = #203#10 % 504;
     #203#25 = #203#10 % 902;
     #203#26 = #203#10 % 1110;
     #203#27 = #1#23;
     #203#28 = #1#24;

     |PINTA #203#17;
     |PINTA #203#18;
     |PINTA #203#21;
     |PINTA #203#23;
     |PINTA #203#24;
     |PINTA #203#25;
     |PINTA #203#26;
     |PINTA #203#27;
     |PINTA #203#28;
|FPRC;

|PROCESO CargaFantasma;
     |C_M #203#2,  1, "S";
     |C_M #203#3,  1, "S";
     |C_M #203#6,  1, "S";
     |C_M #203#7,  1, "S";
     |C_M #203#8,  1, "S";
     |C_M #203#9,  1, "S";
     |C_M #203#11, 1, "S";
     |C_M #203#12, 1, "S";
     |C_M #203#13, 1, "S";
     |C_M #203#14, 1, "S";
     |C_M #203#15, 1, "S";
     |C_M #203#16, 1, "S";
     |C_M #203#17, 1, "S";
     |C_M #203#18, 1, "S";
     |C_M #203#19, 1, "S";
     |C_M #203#20, 1, "S";
     |C_M #203#21, 1, "S";
     |C_M #203#22, 1, "S";
     |C_M #203#23, 1, "S";
     |C_M #203#24, 1, "S";
     |C_M #203#25, 1, "S";
     |C_M #203#26, 1, "S";

     |PARA nCampo = 0;  |SI nCampo [ 22;  |HACIENDO nCampo = nCampo + 1;
           #203nCampo = #1nCampo;

           |C_V #203nCampo, 0, aAlfa;
           |SI aAlfa = "S";  |PINTA #203nCampo;  |FINSI;
     |SIGUE;

     #203#23 = #203#10 % 104;     |PINTA #203#23;
     #203#24 = #203#10 % 504;     |PINTA #203#24;
     #203#25 = #203#10 % 902;     |PINTA #203#25;
     #203#26 = #203#10 % 1110;    |PINTA #203#26;
     #203#27 = #1#23;
     #203#28 = #1#24;
|FPRC;

|PROCESO DescargaFantasma;
     |PARA nCampo = 0;  |SI nCampo [ 22;  |HACIENDO nCampo = nCampo + 1;
           #1nCampo = #203nCampo;
     |SIGUE;

     #1#5  = #203#17 % -104;
     #1#23 = #203#27;
     #1#24 = #203#28;
|FPRC;

|PROCESO Alta;
     |SI nEBotonAlta = 0;  |ACABA;  |FINSI;

     |ESTADO_CONTROL nBotonAlta, "DISABLE";  nEBotonAlta = 0;
     |ESTADO_CONTROL nBotonModi, "DISABLE";  nEBotonModi = 0;
     |ESTADO_CONTROL nBotonBaja, "DISABLE";  nEBotonBaja = 0;

     |HAZ QuitaBotonSalir;

     |ESTADO_CONTROL nGrid, "DISABLE";

     |VENTANA 1030, 1450, -1, 1, "";
     nVentana = FSalida;

     |PTEC 802;  |PAUSA;

     |PARA; |SI; |HACIENDO;
            |MENUG aMenuFami;
            aMenuFami2 = FSalida;

            |SI aMenuFami2 = "1";  |SAL;  |FINSI;
            |SI aMenuFami2 = "2";  |SAL;  |FINSI;
     |SIGUE;

     |FINVENTANA nVentana;

     |PULSATECLA;

     #1#0 = enEmpresa;
     #1#1 = 99;
     |LEE 000#1];
     |SI FSalida = 0;
         |LEE 000#1a;
     |SINO;
         |LEE 000#1u;
     |FINSI;

     |DDEFECTO #203;
     #203#0 = enEmpresa;
     #203#1 = 1;
     |SI FSalida = 0;  |Y  #1#0 = enEmpresa;
         #203#1 = #1#1 + 1;
     |FINSI;

     |DDEFECTO #1;

     #1#0 = #203#0;
     #1#1 = #203#1;

     |SI aMenuFami2 = "1";   #1#4 = aMenuFami4;    |FINSI;
     |SI aMenuFami2 = "2";   #1#4 = aMenuFami5;    |FINSI;

     |HAZ CargaFantasma;

     |C_M #203#1, 1, "S";
     |ENDATOS #1#203;
     |SI FSalida = 0;
         |HAZ DescargaFantasma;

         |GRABA 020#1n;
     |FINSI;
     |LIBERA #1;

     |SI enModo ! 4;
         |ESTADO_CONTROL nBotonAlta, "ENABLE";  nEBotonAlta = 1;
     |FINSI;

     |HAZ PonBotonSalir;

     |ESTADO_CONTROL nGrid, "ENABLE";
     |REFRESCACONTROL nGrid;

     FSalida = 1;  |HAZ Evento1;

     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO Modificar;
     |SI nEBotonModi = 0;  |ACABA;  |FINSI;

     |LEE 101#1=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |ESTADO_CONTROL nBotonAlta, "DISABLE";  nEBotonAlta = 0;
     |ESTADO_CONTROL nBotonModi, "DISABLE";  nEBotonModi = 0;
     |ESTADO_CONTROL nBotonBaja, "DISABLE";  nEBotonBaja = 0;

     |HAZ QuitaBotonSalir;

     |ESTADO_CONTROL nGrid, "DISABLE";

     |C_M #203#1, 1, "N";

     |HAZ CargaFantasma;
     |ENDATOS #2#203;
     |SI FSalida = 0;
         |HAZ DescargaFantasma;

         |GRABA 020#1a;
     |FINSI;
     |LIBERA #1;

     |HAZ PintaDatosPantalla;

     |SI enModo ! 4;
         |ESTADO_CONTROL nBotonAlta, "ENABLE";  nEBotonAlta = 1;
         |ESTADO_CONTROL nBotonModi, "ENABLE";  nEBotonModi = 1;
         |ESTADO_CONTROL nBotonBaja, "ENABLE";  nEBotonBaja = 1;
     |FINSI;

     |HAZ PonBotonSalir;

     |ESTADO_CONTROL nGrid, "ENABLE";
     |REFRESCACONTROL nGrid;
     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO Baja;
     |SI nEBotonBaja = 0;  |ACABA;  |FINSI;

     |CONFI "0000 Confirma la baja del registro seleccionado";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |LEE 101#1=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |BORRA 020#1a;
     |LIBERA #1;

     |REFRESCACONTROL nGrid;
     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO Evento1;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#1=;

         |SI enModo = 4;
             |ESTADO_CONTROL nBotonAlta, "DISABLE";  nEBotonAlta = 0;
             |ESTADO_CONTROL nBotonModi, "DISABLE";  nEBotonModi = 0;
             |ESTADO_CONTROL nBotonBaja, "DISABLE";  nEBotonBaja = 0;
         |SINO;
             |ESTADO_CONTROL nBotonModi, "ENABLE";   nEBotonModi = 1;
             |ESTADO_CONTROL nBotonBaja, "ENABLE";   nEBotonBaja = 1;
         |FINSI;

         |PULSATECLA;

         |HAZ PintaDatosPantalla;
     |FINSI;

     |SI FSalida = 4;
         |SI enModo = 4;  |ACABA;  |FINSI;

         |HAZ Modificar;
     |FINSI;
|FPRC;

|PROCESO BotonesPanta;
     nEBotonAlta = 1;
     nEBotonModi = 1;
     nEBotonBaja = 1;

     |CONTROL_SIMPLE enPanta, "BOTON,{{193}}&Alta ", 1664, 1674, Alta;
     nBotonAlta = FSalida;

     |CONTROL_SIMPLE enPanta, "BOTON,{{197}}&Modificar ", 1676, 1686, Modificar;
     nBotonModi = FSalida;

     |CONTROL_SIMPLE enPanta, "BOTON,{{199}}&Borrar", 1688, 1698, Baja;
     nBotonBaja = FSalida;

     |ESTADO_CONTROL nBotonModi, "DISABLE";  nEBotonModi = 0;
     |ESTADO_CONTROL nBotonBaja, "DISABLE";  nEBotonBaja = 0;

     |SI enModo = 4;
         |ESTADO_CONTROL nBotonAlta, "DISABLE";  nEBotonAlta = 0;
     |FINSI;
|FPRC;

|PROCESO Baja1;
     #1#0 = enEmpresa;
     #1#1 = 1;
|FPRC;

|PROCESO Alta1;
     #1#0 = enEmpresa;
     #1#1 = 99;
|FPRC;

|PROCESO Tipo80F101;  |TIPO 80;
     FSalida = 3499;
|FPRC;

|PROGRAMA;
     |HAZ CargaColores;

     |PINPA #0#203;  enPanta = FSalida;

     |HAZ BotonesPanta;

     |DDEFECTO #1;
     |HAZ PintaDatosPantalla;

     |ABRE #1;
     nRango = 4 + 8 + 16 + 32 + 128;

     |LINEAL_SIMPLE #1#1, nRango, 0602, 1598, Baja1, Alta1, Evento1;
     nGrid = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::{{001}}sa&Lir," + nGrid;
          |LEETECLA aTecla;
          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
          |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |CIERRA #1;
|FPRO;
