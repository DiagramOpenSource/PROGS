|FICHEROS;
   rem01002;
   rem01043;

   rem00099;

   Referen@;
|FIN;

|VARIABLES;
   &eaModelo   = "";
   &enEmpr     = 0;
   &eaTipo     = "";
   &enConDatos = 0;
   &enNumPags  = 0;
   &eaFicheros = "";
   &eaCampos   = "";
   &eaCasillas = "";
   &enPagina   = 0;

   Bloque      = 0;
   SwError     = 0;
   nPYG        = 0;
   nCalc       = 0;
   nContCas    = 0;
   nContRegs   = 0;
   nNumPags    = 0;
   nApartados  = 0;
   nDCampo     = 0;
   nHCampo     = 0;
   nVuelta     = 0;

   aTipo       = "";
   Casilla     = "";
   aAlfa       = "";
   aAlfa1      = "";
   aAlfa2      = "";
   aAlfaC1     = "";
   aAlfaC2     = "";
   aAlfaX1     = "";

   aCapitulo   = "";
   aCapit      = "";
   nCantPP     = 0;

   aDCasilla   = "";
   aHCasilla   = "";

   nTotalReg = 0;
   nCalc1    = 0;
   nCalc2    = 0;
   nCalc3    = 0;
   nCalc4    = 0;
   nCalc5    = 0;
   nComunidad = 0;
|FIN;

|PROCESO BuscaDato;
   #rem01002#0 = enEmpr;
   #rem01002#1 = 0;
   #rem01002#2 = eaTipo;
   #rem01002#3 = Bloque;
   #rem01002#5 = Casilla;
   |LEE 010#rem01002.=;
   |SI FSalida = 0;
       Casilla = #rem01002#7;
       SwError = 0;
   |SINO;
       Casilla = 0; SwError = 1;
   |FINSI;
|FPRC;

|PROCESO RevisaCampos;
   aAlfaC2 = aAlfaC1 - "-";
   |SI FSalida ! 0;
      nCalc = FSalida + 98; aAlfaC2 = aAlfaC2 % nCalc;
      nHCampo = aAlfaC2; aAlfaC2 = "-" + aAlfaC2;
      aAlfaC1 = aAlfaC1 - aAlfaC2;
      nDCampo = aAlfaC1;
   |SINO;
      nDCampo = aAlfaC2; nHCampo = aAlfaC2;
   |FINSI;
   |PARA nContCas = nDCampo; |SI nContCas [ nHCampo; |HACIENDO nContCas = nContCas + 1;
      aAlfaC2 = #Referen@#nContCas#; |QBF aAlfaC2;
      |SI aAlfaC2 ! ""; |Y aAlfaC2 ! "0"; |Y aAlfaC2 ! "0.00";
                        |Y aAlfaC2 ! "01.01.0000";
         nContRegs = nContRegs + 1; enConDatos = 1;
         |SI nContRegs > nApartados;
            nNumPags = nNumPags + 1; nContRegs = 1;
         |FINSI;
         |SAL;
      |FINSI;
   |SIGUE;
|FPRC;

|PROCESO MiraSiEntra;
   enConDatos = 0; enNumPags = 1; nPYG = 0;

   |SI eaFicheros = "";
      Bloque = 0;
      aAlfa = eaCasillas; |QBF aAlfa;
      |PARA; |SI aAlfa ! ""; |HACIENDO;
         aAlfa2 = aAlfa - ",";
         |SI FSalida ! 0;
            nCalc = ((FSalida / 100) ! 0) + 99;
            aAlfa1 = aAlfa2 % nCalc; aAlfa2 = aAlfa1 + ","; aAlfa = aAlfa - aAlfa2;
         |SINO;
            aAlfa1 = aAlfa; aAlfa = "";
         |FINSI;

         |QBT aAlfa1; aAlfa2 = aAlfa1;
         aAlfa2 = aAlfa2 - "B";                || Miro si selecciono bloque
         |SI FSalida ! 0;
            nCalc = FSalida + 4; aAlfa2 = aAlfa1 % nCalc;
            aAlfa1 = aAlfa1 - aAlfa2;
            aAlfa2 = aAlfa2 - ":"; aAlfa2 = aAlfa2 - "B"; Bloque = aAlfa2;
            aAlfa2 = aAlfa1;
         |FINSI;

         aAlfa2 = aAlfa2 - "-";                || Miro si hay intervalo
         |SI FSalida ! 0;
            nCalc = FSalida + 98; aAlfa2 = aAlfa2 % nCalc; aHCasilla = aAlfa2;
            aAlfa2 = "-" + aAlfa2; aAlfa1 = aAlfa1 - aAlfa2;
            aDCasilla = aAlfa1;
         |SINO;
            aDCasilla = aAlfa1; aHCasilla = aAlfa1;
         |FINSI;

         |SI aDCasilla = aHCasilla;
            Casilla = aDCasilla; |QBF Casilla; Casilla = ("000" + Casilla) % -103;
            |HAZ BuscaDato;
            |SI SwError = 0;
                |QBF Casilla;
                |SI Casilla ! ""; |Y Casilla ! "0"; |Y Casilla ! "0.00";
                    enConDatos = 1; |SAL;
                |FINSI;
            |FINSI;
         |SINO;
            |PARA nContCas = aDCasilla; |SI nContCas [ aHCasilla; |HACIENDO nContCas = nContCas + 1;
               Casilla = nContCas; |QBF Casilla; Casilla = ("000" + Casilla) % -103;
               |HAZ BuscaDato;
               |SI SwError = 0;
                   |QBF Casilla;
                   |SI Casilla ! ""; |Y Casilla ! "0"; |Y Casilla ! "0.00";
                       enConDatos = 1; |SAL;
                   |FINSI;
               |FINSI;
            |SIGUE;
            |SI enConDatos ! 0; |SAL; |FINSI;
         |FINSI;
      |SIGUE;
   |SINO;
      enNumPags = 0;

      aAlfa = eaFicheros; |QBF aAlfa;
      |PARA; |SI aAlfa ! ""; |HACIENDO;
         aAlfa2 = aAlfa - ",";
         |SI FSalida ! 0;
            nCalc = ((FSalida / 100) ! 0) + 99;
            aAlfa1 = aAlfa2 % nCalc; aAlfa2 = aAlfa1 + ","; aAlfa = aAlfa - aAlfa2;
         |SINO;
            aAlfa1 = aAlfa; aAlfa = "";
         |FINSI;

         nApartados = 1;
         |QBT aAlfa1;
         aAlfa2 = aAlfa1 - "(";
         |SI FSalida ! 0;
            nCalc = FSalida + 100; aAlfa2 = aAlfa1 % nCalc;
            nApartados = aAlfa2; aAlfa2 = "(" + aAlfa2 + ")";
            aAlfa1 = aAlfa1 - aAlfa2;
         |FINSI;

         |SI aAlfa1 = "rem01032"; nPYG = 1; |FINSI;

         |DDEF aAlfa2; |QBF aAlfa2;
         aAlfa2 = aAlfa2 + aAlfa1 + ".mas";
         |CARGA_DEF aAlfa2, Referen@;
         |SI FSalida = 0;
            nContRegs = nApartados; nNumPags = 0;
            |ABRE #Referen@;
            #Referen@#0 = enEmpr;
            #Referen@#1 = 0;
            #Referen@#2 = eaTipo; aTipo = #Referen@#2;
            #Referen@#3 = 0;
            |LEE 000#Referen@.];
            |PARA; |SI FSalida = 0; |Y #Referen@#0 = enEmpr; |Y #Referen@#1 = 0; |Y #Referen@#2 = aTipo; |HACIENDO;
               |SI nPYG = 0;
                  |SI eaCampos = "";
                     nContRegs = nContRegs + 1; enConDatos = 1;
                     |SI nContRegs > nApartados;
                        nNumPags = nNumPags + 1; nContRegs = 1;
                     |FINSI;
                  |SINO;
                     aAlfaX1 = eaCampos; |QBF aAlfaX1;
                     aAlfaX1 = aAlfaX1 - ",";
                     |SI FSalida = 0;
                        aAlfaC1 = eaCampos; |QBF aAlfaC1;
                        |HAZ RevisaCampos;
                     |SINO;
                        |PARA; |SI FSalida ! 0; |HACIENDO;
                           nCalc  = ((FSalida / 100) ! 0) + 99;
                           nCalc1 = FSalida + 98;
                           aAlfaC1 = aAlfaX1 % nCalc; |QBF aAlfaC1;
                           aAlfaX1 = aAlfaX1 % nCalc1;
                           |HAZ RevisaCampos;
                           |SI enConDatos = 1; |SAL; |FINSI;
                           aAlfaX1 = aAlfaX1 - ",";
                        |SIGUE;
                        |SI enConDatos = 0;
                           aAlfaC1 = aAlfaX1; |QBF aAlfaC1;
                           |SI aAlfaC1 ! ""; |HAZ RevisaCampos; |FINSI;
                        |FINSI;
                     |FINSI;
                  |FINSI;
               |SINO;
                  || Ganancias
                  |SI nVuelta = 1; aAlfaC2 = #Referen@#6; |FINSI;
                  || Perdidas
                  |SI nVuelta = 2; aAlfaC2 = #Referen@#8; |FINSI;
                  |QBF aAlfaC2;

                  |SI aAlfaC2 ! ""; |Y aAlfaC2 ! "0"; |Y aAlfaC2 ! "0.00";
                     nContRegs = nContRegs + 1; enConDatos = 1;
                     |SI nContRegs > nApartados;
                        nNumPags = nNumPags + 1; nContRegs = 1;
                     |FINSI;
                  |FINSI;
               |FINSI;
               |LEE 000#Referen@.s;
            |SIGUE;
            |SI nNumPags > enNumPags; enNumPags = nNumPags; |FINSI;
            |CIERRA #Referen@; |DESCARGA_DEF #Referen@;
         |FINSI;
         |SI nPYG = 1; nVuelta = nVuelta + 1; |FINSI;
      |SIGUE;
   |FINSI;
|FPRC;

|PROCESO AfectoPat;
   |SI aCapit = "A1"; |O aCapit = "A2";
      |SI aCapit = "A1"; |Y #rem00099#22 = "N"; |ACABA; |FINSI;
      |SI aCapit = "A2"; |Y #rem00099#22 = "S"; |ACABA; |FINSI;
   |FINSI;

   aAlfa = eaTipo; |QBF aAlfa;
   |SI aAlfa = "ZPATRITIT";
      |SI #rem00099#3 = "CONYUGE   "; |ACABA;        |FINSI;
   |FINSI;
   |SI aAlfa = "ZPATRICON";
      |SI #rem00099#3 = "TITULAR   "; |ACABA;        |FINSI;
   |FINSI;

   enConDatos = 1;
   nTotalReg = nTotalReg + 1;
|FPRC;

|DEFBUCLE AfectoPat;
#rem00099#1;
;
enEmpr, aCapitulo, 001;
enEmpr, aCapitulo, 999;
;
NULL, AfectoPat;
|FIN;

|PROCESO SacaPags;
     nTotalReg = 0; |BUCLE AfectoPat;
     enNumPags = ((nTotalReg / nCantPP) + 0.49) ! 0;
|FPRC;

|PROCESO LeeTotalComunidad;
     |ABRE #rem01043;
     #rem01043#0 = enEmpr;
     #rem01043#1 = 0;
     #rem01043#2 = eaTipo;
     #rem01043#3 = nComunidad;
     #rem01043#4 = 999;
     |LEE 000#rem01043.=;
     |SI FSalida ! 0; |DDEFECTO #rem01043; |FINSI;
     |CIERRA #rem01043;

     |SI #rem01043#6 ! 0;  enConDatos = 1;  |FINSI;
|FPRC;

|PROCESO Obligadas100;
     enNumPags = 1;

     || Pagina 1  (OBLIGATORIA)
     |SI enPagina = 1; enConDatos = 1; |FINSI;

     || Pagina 2  (OBLIGATORIA)
     |SI enPagina = 2; enConDatos = 1; |FINSI;

     || Pagina 3
     |SI enPagina = 3;
         eaCasillas = "B001:1-22,B002:23-37,B003:38-47";
         |HAZ MiraSiEntra;
     |FINSI;

     || Pagina 4  (Datos en rem01011, rem01013, y rem01015)
     |SI enPagina = 4;
         eaFicheros = "rem01011(3),rem01013(3),rem01015(3)";
         |HAZ MiraSiEntra;
     |FINSI;

     || Pagina 5  (Datos en rem01014 o casillas 121 a 125)
     |SI enPagina = 5;
        eaFicheros = "rem01014(3)";
        |HAZ MiraSiEntra;
        |SI enConDatos = 0;
           eaCasillas = "B005:121-125"; eaFicheros = "";
           |HAZ MiraSiEntra;
        |FINSI;
     |FINSI;

     || Pagina 6  (Datos en rem01019 o casillas 148 a 150)
     |SI enPagina = 6;
        eaFicheros = "rem01019(2)";
        |HAZ MiraSiEntra;
        |SI enConDatos = 0;
           eaCasillas = "B006:148-150"; eaFicheros = "";
           |HAZ MiraSiEntra;
        |FINSI;
     |FINSI;

     || Pagina 7  (Datos en rem01021 o casillas 178 o 180)
     |SI enPagina = 7;
        eaFicheros = "rem01021(2)";
        |HAZ MiraSiEntra;
        |SI enConDatos = 0;
           eaCasillas = "B007:178-180"; eaFicheros = "";
           |HAZ MiraSiEntra;
        |FINSI;
     |FINSI;

     || Pagina 8  (Datos en rem01022, remn01023 o casillas 220 a 227, 746, 245, 747, 255, 265 o 275)
     |SI enPagina = 8;
        eaFicheros = "rem01022(2),rem01023(2)";
        |HAZ MiraSiEntra;
        |SI enConDatos = 0;
           eaCasillas = "B008:212-220,537,B009:235,538"; eaFicheros = "";
           |HAZ MiraSiEntra;
        |FINSI;
     |FINSI;

     || Pagina 9
     |SI enPagina = 9;
        eaFicheros = "rem01024(2),rem01025(2),rem01027(3)";
        |HAZ MiraSiEntra;
        |SI enConDatos = 0;
           eaCasillas = "B013:251-257,B014:258-262,B015:263-267,B016:281-282"; eaFicheros = "";
           |HAZ MiraSiEntra;
        |FINSI;
     |FINSI;

     || Pagina 10
     |SI enPagina = 10;
        nVuelta = 1;
        eaFicheros = "rem01039(3),rem01031(2),rem01032(3)";
        |HAZ MiraSiEntra;
        |SI enConDatos = 0;
           eaCasillas = "B017:296-297,B018:326-328,B019:331-334"; eaFicheros = "";
           |HAZ MiraSiEntra;
        |FINSI;
     |FINSI;

     || Pagina 11
     |SI enPagina = 11;
        eaFicheros = "rem01030(3),rem01034(3)";
        |HAZ MiraSiEntra;
        |SI enConDatos = 0;
           eaCasillas = "B020:337,B021:348,B022:349-391,B023:354-359,B024:364-367,B025:368-371"; eaFicheros = "";
           |HAZ MiraSiEntra;
        |FINSI;
     |FINSI;

     || Pagina 12  (OBLIGATORIA)
     |SI enPagina = 12; enConDatos = 1; |FINSI;

     || Pagina 13  (OBLIGATORIA)
     |SI enPagina = 13; enConDatos = 1; |FINSI;

     || Pagina 14  (OBLIGATORIA)
     |SI enPagina = 14; enConDatos = 1; |FINSI;

     || Pagina 15  (OBLIGATORIA)
     |SI enPagina = 15; enConDatos = 1; |FINSI;

     || Pagina 16  (OBLIGATORIA)
     |SI enPagina = 16;
         eaFicheros = "rem01048(1),rem01049(1),rem01050(1),rem01051(1)";
         |HAZ MiraSiEntra;
         enConDatos = 1;
     |FINSI;

     || Pagina 17  (OBLIGATORIA)
     |SI enPagina = 17; enConDatos = 1; |FINSI;

     || Pagina 18 : ANEXO A.1  (Datos en el rem01041)
     |SI enPagina = 18;
        eaFicheros = "rem01041(1)"; eaCampos = "3-45"; |HAZ MiraSiEntra;
     |FINSI;

     || Pagina 19 : ANEXO A.2  (Datos en el rem01041 y rem01042)
     |SI enPagina = 19;
        eaFicheros = "rem01041(1)"; eaCampos = "46-63";  |HAZ MiraSiEntra;
        |SI enConDatos = 1; |ACABA;  |FINSI;

        eaFicheros = "rem01042(1)"; eaCampos = "145-165";  |HAZ MiraSiEntra;
     |FINSI;

     || Pagina 20 : ANEXO A.3  (Datos en el rem01042)
     |SI enPagina = 20;
        eaFicheros = "rem01042(1)"; eaCampos = "3-144"; |HAZ MiraSiEntra;
     |FINSI;

     || Pagina 21 : ANEXO B.1
     |SI enPagina = 21;
         enConDatos = 0;
         nComunidad = 1;  |HAZ LeeTotalComunidad;  |SI enConDatos = 1; |ACABA;  |FINSI;
         nComunidad = 2;  |HAZ LeeTotalComunidad;  |SI enConDatos = 1; |ACABA;  |FINSI;
         nComunidad = 3;  |HAZ LeeTotalComunidad;  |SI enConDatos = 1; |ACABA;  |FINSI;
     |FINSI;

     || Pagina 22 : ANEXO B.2
     |SI enPagina = 22;
         enConDatos = 0;
         nComunidad = 4;  |HAZ LeeTotalComunidad;  |SI enConDatos = 1; |ACABA;  |FINSI;
         nComunidad = 5;  |HAZ LeeTotalComunidad;  |SI enConDatos = 1; |ACABA;  |FINSI;
         nComunidad = 6;  |HAZ LeeTotalComunidad;  |SI enConDatos = 1; |ACABA;  |FINSI;
     |FINSI;

     || Pagina 23 : ANEXO B.3
     |SI enPagina = 23;
         enConDatos = 0;
         nComunidad = 7;  |HAZ LeeTotalComunidad;  |SI enConDatos = 1; |ACABA;  |FINSI;
         nComunidad = 8;  |HAZ LeeTotalComunidad;  |SI enConDatos = 1; |ACABA;  |FINSI;
     |FINSI;

     || Pagina 24 : ANEXO B.4
     |SI enPagina = 24;
         enConDatos = 0;
         nComunidad = 9;   |HAZ LeeTotalComunidad;  |SI enConDatos = 1; |ACABA;  |FINSI;
         nComunidad = 10;  |HAZ LeeTotalComunidad;  |SI enConDatos = 1; |ACABA;  |FINSI;
         nComunidad = 11;  |HAZ LeeTotalComunidad;  |SI enConDatos = 1; |ACABA;  |FINSI;
         nComunidad = 12;  |HAZ LeeTotalComunidad;  |SI enConDatos = 1; |ACABA;  |FINSI;
     |FINSI;

     || Pagina 25 : ANEXO B.5
     |SI enPagina = 25;
         enConDatos = 0;
         nComunidad = 13;  |HAZ LeeTotalComunidad;  |SI enConDatos = 1; |ACABA;  |FINSI;
         nComunidad = 16;  |HAZ LeeTotalComunidad;  |SI enConDatos = 1; |ACABA;  |FINSI;
         nComunidad = 17;  |HAZ LeeTotalComunidad;  |SI enConDatos = 1; |ACABA;  |FINSI;
     |FINSI;

     || Pagina 26 : I+D (OBLIGATORIA)
     |SI enPagina = 26; enConDatos = 1; |FINSI;
|FPRC

|PROCESO Obligadas714;
     || Pagina 1  (OBLIGATORIA)
     |SI enPagina = 1; enConDatos = 1; enNumPags = 1; |FINSI;

     || Pagina 2
     |SI enPagina = 2;
        enConDatos = 0;
        aCapitulo = "A1"; nCantPP = 2;  aCapit = "A1"; |HAZ SacaPags; nCalc1 = enNumPags;
        aCapitulo = "A1"; nCantPP = 8;  aCapit = "A2"; |HAZ SacaPags; nCalc2 = enNumPags;
        aCapit = "";
        aCapitulo = "B1"; nCantPP = 10; |HAZ SacaPags; nCalc3 = enNumPags;
        enNumPags = 0;
        |SI enNumPags < nCalc1; enNumPags = nCalc1; |FINSI;
        |SI enNumPags < nCalc2; enNumPags = nCalc2; |FINSI;
        |SI enNumPags < nCalc3; enNumPags = nCalc3; |FINSI;
     |FINSI;

     || Pagina 3
     |SI enPagina = 3;
        enConDatos = 0;
        aCapitulo = "C1"; nCantPP = 7;  |HAZ SacaPags; nCalc1 = enNumPags;
        aCapitulo = "C2"; nCantPP = 5;  |HAZ SacaPags; nCalc2 = enNumPags;
        aCapitulo = "D1"; nCantPP = 12; |HAZ SacaPags; nCalc3 = enNumPags;
        enNumPags = 0;
        |SI enNumPags < nCalc1; enNumPags = nCalc1; |FINSI;
        |SI enNumPags < nCalc2; enNumPags = nCalc2; |FINSI;
        |SI enNumPags < nCalc3; enNumPags = nCalc3; |FINSI;
     |FINSI;

     || Pagina 4
     |SI enPagina = 4;
        enConDatos = 0;
        aCapitulo = "E1"; nCantPP = 12; |HAZ SacaPags; nCalc1 = enNumPags;
        aCapitulo = "F1"; nCantPP = 10; |HAZ SacaPags; nCalc2 = enNumPags;
        aCapitulo = "F2"; nCantPP = 6;  |HAZ SacaPags; nCalc3 = enNumPags;
        enNumPags = 0;
        |SI enNumPags < nCalc1; enNumPags = nCalc1; |FINSI;
        |SI enNumPags < nCalc2; enNumPags = nCalc2; |FINSI;
        |SI enNumPags < nCalc3; enNumPags = nCalc3; |FINSI;
     |FINSI;

     || Pagina 5
     |SI enPagina = 5;
        enConDatos = 0;
        aCapitulo = "G1"; nCantPP = 10; |HAZ SacaPags; nCalc1 = enNumPags;
        aCapitulo = "G2"; nCantPP = 13; |HAZ SacaPags; nCalc2 = enNumPags;
        aCapitulo = "G3"; nCantPP = 4;  |HAZ SacaPags; nCalc3 = enNumPags;
        enNumPags = 0;
        |SI enNumPags < nCalc1; enNumPags = nCalc1; |FINSI;
        |SI enNumPags < nCalc2; enNumPags = nCalc2; |FINSI;
        |SI enNumPags < nCalc3; enNumPags = nCalc3; |FINSI;
     |FINSI;

     || Pagina 6
     |SI enPagina = 6;
        enConDatos = 0;
        aCapitulo = "G4"; nCantPP = 4; |HAZ SacaPags; nCalc1 = enNumPags;
        aCapitulo = "H1"; nCantPP = 3; |HAZ SacaPags; nCalc2 = enNumPags;
        aCapitulo = "H2"; nCantPP = 3; |HAZ SacaPags; nCalc3 = enNumPags;
        aCapitulo = "I1"; nCantPP = 3; |HAZ SacaPags; nCalc4 = enNumPags;
        aCapitulo = "J1"; nCantPP = 3; |HAZ SacaPags; nCalc5 = enNumPags;
        enNumPags = 0;
        |SI enNumPags < nCalc1; enNumPags = nCalc1; |FINSI;
        |SI enNumPags < nCalc2; enNumPags = nCalc2; |FINSI;
        |SI enNumPags < nCalc3; enNumPags = nCalc3; |FINSI;
        |SI enNumPags < nCalc4; enNumPags = nCalc4; |FINSI;
        |SI enNumPags < nCalc5; enNumPags = nCalc5; |FINSI;
     |FINSI;

     || Pagina 7
     |SI enPagina = 7;
        enConDatos = 0;
        aCapitulo = "K1"; nCantPP = 8; |HAZ SacaPags; nCalc1 = enNumPags;
        aCapitulo = "L1"; nCantPP = 6; |HAZ SacaPags; nCalc2 = enNumPags;
        aCapitulo = "M1"; nCantPP = 6; |HAZ SacaPags; nCalc3 = enNumPags;
        aCapitulo = "N1"; nCantPP = 3; |HAZ SacaPags; nCalc4 = enNumPags;
        enNumPags = 0;
        |SI enNumPags < nCalc1; enNumPags = nCalc1; |FINSI;
        |SI enNumPags < nCalc2; enNumPags = nCalc2; |FINSI;
        |SI enNumPags < nCalc3; enNumPags = nCalc3; |FINSI;
        |SI enNumPags < nCalc4; enNumPags = nCalc4; |FINSI;
     |FINSI;

     || Pagina 8
     |SI enPagina = 8;
        enConDatos = 0;
        aCapitulo = "O1"; nCantPP = 3;  |HAZ SacaPags; nCalc1 = enNumPags;
        aCapitulo = "P1"; nCantPP = 3;  |HAZ SacaPags; nCalc2 = enNumPags;
        aCapitulo = "Q1"; nCantPP = 6;  |HAZ SacaPags; nCalc3 = enNumPags;
        aCapitulo = "U1"; nCantPP = 11; |HAZ SacaPags; nCalc4 = enNumPags;
        enNumPags = 0;
        |SI enNumPags < nCalc1; enNumPags = nCalc1; |FINSI;
        |SI enNumPags < nCalc2; enNumPags = nCalc2; |FINSI;
        |SI enNumPags < nCalc3; enNumPags = nCalc3; |FINSI;
        |SI enNumPags < nCalc4; enNumPags = nCalc4; |FINSI;
     |FINSI;

     || Pagina 9  (OBLIGATORIA)
     |SI enPagina = 9; enConDatos = 1; enNumPags = 1; |FINSI;

     || Pagina 10 (OBLIGATORIA)
     |SI enPagina = 10; enConDatos = 1; enNumPags = 1; |FINSI;

     || Pagina 11 (OBLIGATORIA)
     |SI enPagina = 11; enConDatos = 1; enNumPags = 1; |FINSI;
|FPRC;

|PROCESO MiraObligatoria;
     |ABRE #rem01002;
     |SELECT #2#rem01002;

     |SI eaModelo = "100";
         |HAZ Obligadas100;
     |FINSI;

     |SI eaModelo = "714";
         |HAZ Obligadas714;
     |FINSI;

     eaCampos = ""; eaFicheros = "";

     |CIERRA #rem01002;
|FPRC;
