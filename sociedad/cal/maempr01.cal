|FICHEROS;
   maempr01 #0;
|FIN;

|VARIABLES;
   &empresa = 0;
   &anyo    = 0;
   &nombre  = "";
   &nif     = "";
   &modo    = 0;

   FEstado  = 0;
   alfa1    = "";
   alfa2    = "";
   tecla    = "";
   avance   = "815";
   atras    = "814";

     aNifQbf1   = "";
     aNifQbf2   = "";

     &eaVarDni  = "";
     &enCalcCif = 0;
|FIN;

|INCLUYE verifech;

|PROGRAMA;
|ABRE #0;
|PINPA #0#0;
|SI modo = 1; |O modo = 2; |HAZ pidedato; |FINSI;
|SI modo = 4; |HAZ consulta; |FINSI;
|CIERRA #0;
|FPRO;

|PROCESO letranif;
     |SI FSalida = 9;
         eaVarDni   = #0Campo;
         enCalcCif  = 1;
         |HAZ CalculoDNI;
         #0Campo = eaVarDni;
         |PINTA #0Campo;
         |ERROR;
         |ACABA;
     |FINSI;

     eaVarDni  = #0Campo;
     enCalcCif = 0;
     |HAZ CalculoDNI;

     aNifQbf1 = #0Campo;   |QBF aNifQbf1;
     aNifQbf2 = eaVarDni;  |QBF aNifQbf2;

     |SI aNifQbf1 ! aNifQbf2; |Y aNifQbf1 ! "";
         |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
         |SI FSalida = 0;
             #0Campo = eaVarDni;  |PINTA #0Campo;
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO pidedato;
#0#0 = empresa;
#0#1 = anyo;
|LEE 110#0=;
FEstado = FSalida;
|PINDA #0#0;
|SI modo = 1; |ENDATOS #1#0;  |FINSI;
|SI modo = 2; |ENDATOS #2#0;  |FINSI;
|SI FSalida = 0;
    #0#0 = empresa;
    #0#1 = anyo;
    #0#2 = nombre;
    #0#3 = nif;
    |SI FEstado = 0; |GRABA 020#0a; |SINO; |GRABA 020#0n; |FINSI;
    |LIBERA #0;
|FINSI;
|FPRC;

|PROCESO consulta;
avance = &255 + avance;
atras = &255 + atras;
#0#0 = empresa;
#0#1 = anyo;
|LEE 040#0=;
|SI FSalida = 0; |HAZ pantalla1; |FINSI;
|FPRC;

|PROCESO pantalla1;
|PINPA #0#0;
|PINDA #0#0;
|MENSA "0411  Av.Pag.= Siguiente Pantalla  Re.Pag. = Pantalla Anterior";
|LEETECLA tecla;
|SI tecla = avance; |HAZ pantalla2; |ACABA; |FINSI;
|FPRC;

|PROCESO pantalla2;
|PINPA #1#0;
|PINDA #1#0;
|MENSA "0411  Av.Pag.= Siguiente Pantalla  Re.Pag. = Pantalla Anterior";
|LEETECLA tecla;
|SI tecla = atras;  |HAZ pantalla1; |ACABA; |FINSI;
|SI tecla = avance; |HAZ pantalla3; |ACABA; |FINSI;
|FPRC;

|PROCESO pantalla3;
|PINPA #2#0;
|PINDA #2#0;
|MENSA "0411  Av.Pag.= Siguiente Pantalla  Re.Pag. = Pantalla Anterior";
|LEETECLA tecla;
|SI tecla = atras;  |HAZ pantalla2; |ACABA; |FINSI;
|SI tecla = avance; |HAZ pantalla4; |ACABA; |FINSI;
|FPRC;

|PROCESO pantalla4;
|PINPA #3#0;
|PINDA #3#0;
|MENSA "0411  Av.Pag.= Siguiente Pantalla  Re.Pag. = Pantalla Anterior";
|LEETECLA tecla;
|SI tecla = atras;  |HAZ pantalla3; |ACABA; |FINSI;
|FPRC;

|PROCESO copostal; |TIPO 0;
  alfa1 = ("00"  + #0#10) % -102;
  alfa2 = ("000" + #0#11) % -103;
  #0#12 = alfa1 + alfa2;
|FPRC;

|PROCESO libros; |TIPO 0;
|SI #0#53 = "M";
    |SI #0Campo = "M"; |O #0Campo = "C"; |O #0Campo = "O";
        |MENAV "    Solo puede ser <L> o <F>";
        |ERROR;
    |FINSI;
|SINO;
    |SI #0Campo = "L"; |O #0Campo = "F";
        |MENAV "    Solo puede ser <M> o <C> o <O>";
        |ERROR;
    |FINSI;
|FINSI;
|FPRC;
