|FICHEROS;
   instalar #0;
   metextos #1;
   metexpat #2;
   soplanca #3;
   soplanli #4;
   soplanpc #5;
   soplanpl #6;

   meestrca #102;
   meestrli #103;
   imphacie #104;
   sodacuen #105;
   isyycasi #106;
   ctacuadc #107;
   ctacuadl #108;
   medescri #109;
   meempmem #110;

   referen@ #998;
   refere1@ #999;
|FIN;

|VARIABLES;
   XPara     = 0;
   ficheros  = "";
   traba1    = "";
   temporal1 = "";
   temporal2 = "";
   nDCod     = 0;
   nHCod     = 0;
   aParametro = "";
   &enPlan    = 0;
   aDef       = "";
   aPathDef   = "";
   nPara1     = 0;
   nPara2     = 0;
   alfa1      = "";
   nNumCampos = 0;
   nEstado    = 0;
   nDPlan     = 0;
   nHPlan     = 0;
|FIN;

|PROCESO mayor;
  #0Campo = #0Campo > " ";
  |PINTA #0Campo;

  |SI Campo = 1;
      |C_M #0#9, 1, "S";
      |SI #0Campo = "NO";  |C_M #0#9, 1, "N";  |FINSI;
  |FINSI;
  |SI Campo = 4;
      |C_M #0#10,1,"S"; |C_M #0#12,1,"S";
      |SI #0Campo = "NO";  |C_M #0#10, 1, "N"; |C_M #0#12,1,"N"; |FINSI;
  |FINSI;
  |SI Campo = 15;
      |C_M #0#16,1,"S";
      |SI #0Campo = "NO";  |C_M #0#16, 1, "N"; |FINSI;
  |FINSI;
|FPRC;

|PROCESO GrabaAl01;
     |PARA XPara = 0; |SI XPara [ 34;  |HACIENDO XPara = XPara + 1;
           #1XPara = #2XPara;
     |SIGUE;
     |SI #1#17 = 99994;  #1#17 = 99998;  |FINSI;
     |SI #1#17 = 99996;  #1#17 = 99998;  |FINSI;
     |SI #1#17 = 99995;  #1#17 = 99999;  |FINSI;
     |SI #1#17 = 99997;  #1#17 = 99999;  |FINSI;

     |GRABA 140#1n;
     |LIBERA #1;

     #109#0 = #1#0;
     |LEE 140#109=;
     |SI FSalida ! 0;
         #109#0 = #1#0;
         #109#1 = #1#34;
         |GRABA 020#109n;
     |FINSI;
     |LIBERA #109;
|FPRC;

|DEFBUCLE metexpat;
  #2#1;
  ;
  000000, 00, nDCod;
  999999, 99, nHCod;
  ;
  NULL, GrabaAl01;
|FIN;

|PROCESO GrabaEstruc;
     |SI #0#10 = 1;
         #110#4 = 4;
     |SINO;
         #110#4 = 10;
     |FINSI;

     |GRABA 020#110a;
     |LIBERA #110;
|FPRC;

|DEFBUCLE meempmem;
    #110#1;
    ;
    00001;
    99999;
    be;
    NULL, GrabaEstruc;
|FIN;

|PROCESO BorraEl01
  |BORRA 140#1a;
  |LIBERA #1;
|FPRC;

|DEFBUCLE metextos;
  #1#1;
  ;
  000000, 00, 99998;
  999999, 99, 99999;
  ;
  NULL, BorraEl01;
|FIN;

|PROCESO Graba03;
  #3#0 = #5#0;
  |LIBERA #3;
  |LEE 140#3=;
  |SI FSalida ! 0;
      #3#0 = #5#0;
      #3#1 = #5#1;
      |GRABA 140#3n;
  |FINSI;
|FPRC;

|DEFBUCLE soplanpc;
  #5#1;
  ;
  00;
  99;
  ;
  NULL, Graba03;
|FIN;

|PROCESO Graba04;
  #4#0 = #6#0;
  #4#1 = #6#1;
  |LIBERA #4;
  |LEE 140#4=;
  |SI FSalida ! 0;
      #4#0 = #6#0;
      #4#1 = #6#1;
      #4#2 = #6#2;
      #4#3 = #6#3;
      #4#4 = #6#4;
      #4#5 = #6#5;
      #4#6 = #6#6;
      #4#7 = #6#7;
      #4#8 = #6#8;
      #4#9 = #6#9;
      |GRABA 140#4n;
  |SINO;
      |SI #4#6 = "214000";  |Y #4#0 ! 16;
          #4#6 = "214070";
          |GRABA 020#4a;
          |LIBERA #4;
      |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE soplanpl;
  #6#1;
  ;
  00, 000;
  99, 999;
  ;
  NULL, Graba04;
|FIN;

|PROCESO instala; |TIPO 3;
  |SI #0#0 ! "SI";  |ACABA;  |FINSI;
  |DFICE ficheros;
  |SI #0#1 = "SI";
      |ATRI I; |PINTA 2320, "Instalando Planillas Contabilidad";
      traba1 = "soplanpc";
      |HAZ copiando;
      traba1 = "soplanpl";
      |HAZ copiando;

      |ABRE #5; |CIERRA #5;
      |ABRE #6; |CIERRA #6;

      |SI #0#9 = "E";
          |ABRE #3;  |CIERRA #3;  |DELINDEX #3;
          |ABRE #4;  |CIERRA #4;  |DELINDEX #4;
      |FINSI;

      |ABRE #3;  |BUCLE soplanpc;  |CIERRA #3;
      |ABRE #4;  |BUCLE soplanpl;  |CIERRA #4;

      |DELINDEX #5;
      |DELINDEX #6;
  |FINSI;

  |SI #0#2 = "SI";
      |ATRI I; |PINTA 2320, "Instalando Estructura Memorias   ";
      traba1 = "meestrca";
      |HAZ copiando;
      traba1 = "meestrli";
      |HAZ copiando;
  |FINSI;

  |SI #0#5 = "SI";
      |ATRI I; |PINTA 2320, "Instalando Datos Hacienda        ";
      traba1 = "imphacie";
      |HAZ copiando;
  |FINSI;

  |SI #0#6 = "SI";
      |ATRI I; |PINTA 2320, "Instalando Descrip. de Cuentas   ";
      traba1 = "sodacuen";
      |HAZ copiando;
  |FINSI;

  |SI #0#7 = "SI";
      |ATRI I; |PINTA 2320, "Instalando Casilla del 201.      ";
      traba1 = "isyycasi";
      |HAZ copiando;
  |FINSI;

  |SI #0#8 = "SI";
      |ATRI I; |PINTA 2320, "Instalando Definiciones Memorias con Cuadros";
      traba1 = "ctacuadc";
      |HAZ copiando;
      traba1 = "ctacuadl";
      |HAZ copiando;
  |FINSI;

  |SI #0#14 = "SI";
      |ATRI I; |PINTA 2320, "Instalando Descripcion Apartados Cuadro de Financiacion";
      traba1 = "mem00002";
      |HAZ copiando;
  |FINSI;

  |SI #0#15 = "SI";
      |SI #0#16 = 1;
          |ATRI I; |PINTA 2320, "Instalando Estructura Estado de Cambios del Patr. Neto";
          traba1 = "soda8m02";
          |HAZ copiando;
          traba1 = "soda8m03";
          |HAZ copiando;
          traba1 = "soda8m22";
          |HAZ copiando;
          traba1 = "soda8m23";
          |HAZ copiando;
          traba1 = "soda8tco";
          |HAZ copiando;
          traba1 = "soda8tli";
          |HAZ copiando;
     |SINO;
          |HAZ instala2;
     |FINSI;
  |FINSI;

  |SI #0#4 = "SI";
      traba1 = "medescri";
      |HAZ copiando;

      traba1 = "metexpat";
      |HAZ copiando;

      |ABRE #109;  |CIERRA #109;  |DELINDEX #109;
      |ABRE #2;    |CIERRA #2;

      |SI #0#10 = 1;
          nDCod = 99998;
          nHCod = 99999;
      |FINSI;
      |SI #0#10 = 2;
          nDCod = 99996;
          nHCod = 99997;
      |FINSI;
      |SI #0#10 = 3;
          nDCod = 99994;
          nHCod = 99995;
      |FINSI;

      |BUCLE metextos;

      |ABRE #1;
      |ABRE #109;
      |BUCLE metexpat;
      |CIERRA #109;
      |CIERRA #1;

      |SI #0#12 = "S";
          |BUCLE meempmem;
      |FINSI;

      |DELINDEX #2;
  |FINSI;

  |SI #0#13 = "SI";
      |ATRI I; |PINTA 2320, "Instalando F. Ctrl Comprobacion Impuesto ";
      traba1 = "ism00057";
      |HAZ copiando;
      traba1 = "ism00058";
      |HAZ copiando;
      traba1 = "ism00059";
      |HAZ copiando;
  |FINSI;

  |ATRI 0;

  |ABRE #102;  |CIERRA #102;
  |ABRE #103;  |CIERRA #103;
  |ABRE #104;  |CIERRA #104;
  |ABRE #105;  |CIERRA #105;
  |ABRE #106;  |CIERRA #106;
  |ABRE #107;  |CIERRA #107;
  |ABRE #108;  |CIERRA #108;
  |ABRE #109;  |CIERRA #109;
|FPRC;

|PROCESO Grabasodacuen;
     #105#2 = #998#2;
     #105#0 = #998#0;
     |LEE 101#105=;
     |SI FSalida ! 0;
         #105#0  = #998#0;
         #105#1  = #998#1;
         #105#2  = #998#2;
         |GRABA 020#105n;
     |FINSI;
     |LIBERA #105;
|FPRC;

|DEFBUCLE sodacuen;
     #998#1002;
     ;
     enPlan, "            ";
     enPlan, "zzzzzzzzzzzz";
     ;
     NULL, Grabasodacuen;
|FIN;

|PROCESO instala1;
  |SI #0#0 ! "SI";  |ACABA;  |FINSI;
  |DFICE ficheros;

  |SI #0#6 = "SI";
      |ATRI I; |PINTA 2320, "Instalando Descrip. de Cuentas   ";
      traba1 = "sodacue1";
      |HAZ copiando;

      |DDEF aPathDef;
      aDef = aPathDef + "sodacuen.mas";  |QBT aDef;
      |CARGA_DEF aDef, referen@;
      |SI FSalida = 0;
          |NOME_DAT #998 "soda2008";

          |ABRE #105;
          |BUCLE sodacuen;
          |CIERRA #105;

          |DELINDEX #998;
          |DESCARGA_DEF #referen@;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO instala2;
  |ATRI I; |PINTA 2320, "Instalando Estructuras de ECPN";
  |DFICE ficheros;
  traba1 = "soda8m02"; |HAZ instala2_soda;
  traba1 = "soda8m03"; |HAZ instala2_soda;
  traba1 = "soda8m22"; |HAZ instala2_soda;
  traba1 = "soda8m23"; |HAZ instala2_soda;
  traba1 = "soda8tco"; |HAZ instala2_soda;
  traba1 = "soda8tli"; |HAZ instala2_soda;
|FPRC;

|PROCESO b_oficial;
     |BORRA 020#999a;
     |LIBERA #999;
|FPRC;


|DEFBUCLE b_soda8xxx;
     #999#1003;
     ;
     nDPlan, nPara1, 001;
     nHPlan, nPara1, 999;
     be;
     NULL, b_oficial;
|FIN;

|DEFBUCLE b_soda8m03;
     #999#1005;
     ;
     nDPlan, nPara1, 001, 01, 001;
     nHPlan, nPara1, 999, 99, 999;
     be;
     NULL, b_oficial;
|FIN;

|DEFBUCLE b_soda8tco;
     #999#1003;
     ;
     nDPlan, nPara1, 01;
     nHPlan, nPara1, 99;
     be;
     NULL, b_oficial;
|FIN;

|PROCESO Grabasoda8tco;
     #999#5  = #998#5;
     #999#0  = #998#0;
     #999#1  = #998#1;
     |LEE 101#999=;
     nEstado = FSalida;
     #999#5  = #998#5;
     #999#0  = #998#0;
     #999#1  = #998#1;
     #999#2  = #998#2;
     #999#3  = #998#3;
     #999#4  = #998#4;
     |SI nEstado ! 0; |GRABA 020#999n; |FINSI;
     |SI nEstado = 0; |GRABA 020#999a; |FINSI;
     |LIBERA #999;
|FPRC;

|DEFBUCLE soda8tco;
     #998#1003;
     ;
     nDPlan, nPara1, 01;
     nHPlan, nPara1, 99;
     ;
     NULL, Grabasoda8tco;
|FIN;

|PROCESO Grabasoda8m03;
     #999#7  = #998#7;
     #999#0  = #998#0;
     #999#1  = #998#1;
     #999#2  = #998#2;
     #999#3  = #998#3;
     |LEE 101#999=;
     nEstado = FSalida;
     #999#7  = #998#7;
     #999#0  = #998#0;
     #999#1  = #998#1;
     #999#2  = #998#2;
     #999#3  = #998#3;
     #999#4  = #998#4;
     #999#5  = #998#5;
     #999#6  = #998#6;
     |SI nEstado ! 0; |GRABA 020#999n; |FINSI;
     |SI nEstado = 0; |GRABA 020#999a; |FINSI;
     |LIBERA #999;
|FPRC;

|DEFBUCLE soda8m03;
     #998#1005;
     ;
     nDPlan, nPara1, 001, 01, 001;
     nHPlan, nPara1, 999, 99, 999;
     ;
     NULL, Grabasoda8m03;
|FIN;

|PROCESO Grabasoda8xxx;
     #999nNumCampos = #998nNumCampos;
     #999#0  = #998#0;
     #999#1  = #998#1;
     |LEE 101#999=;
     nEstado = FSalida;
     #999nNumCampos = #998nNumCampos;
     #999#0  = #998#0;
     #999#1  = #998#1;
     |PARA nPara2 = 2; |SI nPara2 [ nNumCampos; |HACIENDO nPara2 = nPara2 + 1;
           #999nPara2  = #998nPara2;
     |SIGUE;
     |SI nEstado ! 0; |GRABA 020#999n; |FINSI;
     |SI nEstado = 0; |GRABA 020#999a; |FINSI;
     |LIBERA #999;
|FPRC;

|DEFBUCLE soda8xxx;
     #998#1003;
     ;
     nDPlan, nPara1, 001;
     nHPlan, nPara1, 999;
     ;
     NULL, Grabasoda8xxx;
|FIN;

|PROCESO instala2_soda;
      nDPlan = enPlan;
      nHPlan = enPlan;
      |SI enPlan = 0; |O enPlan = 2999;
          nDPlan = 2008;
          nHPlan = 2018;
      |FINSI;

      |HAZ copiando;
      |DDEF aPathDef;
      aDef = aPathDef + traba1 + ".mas";  |QBT aDef;

      |CARGA_DEF aDef, refere1@;
      |SI FSalida = 0;
          |CARGA_DEF aDef, referen@;
          |SI FSalida = 0;
              |NOME_DAT #998 "soda2008";

              |PARA nPara1 = 0; |SI nPara1 [ 42; |HACIENDO nPara1 = nPara1 + 1;
                   |SI traba1 = "soda8m02"; |O traba1 = "soda8m22"; |BUCLE b_soda8xxx; |FINSI;
                   |SI traba1 = "soda8m03"; |O traba1 = "soda8m23"; |BUCLE b_soda8m03; |FINSI;
                   |SI traba1 = "soda8tco"; |BUCLE b_soda8tco; |FINSI;
                   |SI traba1 = "soda8tli"; |BUCLE b_soda8xxx; |FINSI;

                   |ABRE #999;
                   |SI traba1 = "soda8m02"; |O traba1 = "soda8m22"; nNumCampos = 34; |BUCLE soda8xxx; |FINSI;
                   |SI traba1 = "soda8m03"; |O traba1 = "soda8m23"; |BUCLE soda8m03; |FINSI;
                   |SI traba1 = "soda8tco"; |BUCLE soda8tco; |FINSI;
                   |SI traba1 = "soda8tli"; nNumCampos = 8;  |BUCLE soda8xxx; |FINSI;
                   |CIERRA #999;

                   |SI nPara1 = 1;  nPara1 =  4; |FINSI;
                   |SI nPara1 = 7;  nPara1 =  9; |FINSI;
                   |SI nPara1 = 10; nPara1 = 19; |FINSI;
                   |SI nPara1 = 22; nPara1 = 29; |FINSI;
                   |SI nPara1 = 32; nPara1 = 39; |FINSI;
              |SIGUE;
              |DELINDEX #998;
              |DESCARGA_DEF #referen@;
          |FINSI;
          |DESCARGA_DEF #refere1@;
      |FINSI;
|FPRC;


|PROCESO copiando;
  alfa1 = traba1 % 105;
  |SI alfa1 = "soda8"; |Y #0#16 = 2;
       temporal1 = ficheros + traba1 + ".ddd";
       temporal2 = ficheros + "soda2008.dat";
       |COPIA_FICHERO temporal1,temporal2;
       |ACABA;
  |FINSI;

  |SI traba1 = "medescri"; |Y #0#10 = 3;
       temporal1 = ficheros + traba1 + ".nnn";
       temporal2 = ficheros + traba1 + ".dat";
       |COPIA_FICHERO temporal1,temporal2;
  |SINO;
       |SI traba1 = "sodacue1";
           temporal1 = ficheros + "sodacuen.ddd";
           temporal2 = ficheros + "soda2008.dat";
           |COPIA_FICHERO temporal1,temporal2;
       |SINO;
           temporal1 = ficheros + traba1 + ".ddd";
           temporal2 = ficheros + traba1 + ".dat";
           |COPIA_FICHERO temporal1,temporal2;
       |FINSI;
  |FINSI;

  temporal1 = ficheros + traba1 + ".ddx";
  |FBORRA temporal1;

  temporal1 = ficheros + traba1 + ".ixx";
  |FBORRA temporal1;
|FPRC;

|PROGRAMA;
     aParametro = PARAMETRO;   |QBF aParametro;
     |SI aParametro ! "";
         |DDEFECTO #0;
         #0#0  = "SI";
         |SI aParametro = "sodacuen";
            #0#6  = "SI";
            |HAZ instala1;
         |SINO;
            |SI aParametro = "soda8";
                #0#15 = "SI";
                #0#16 = "2";
            |SINO;
                #0#1  = "SI";
                #0#9  = "I";
                |SI aParametro ! "soplanca";
                    #0#7  = "SI";
                    #0#11 = "SI";
                    |PINPA #0#0;
                    |PINDA #0#0;
               |FINSI;
            |FINSI;
            |HAZ instala;
        |FINSI;
     |SINO;
         |MANTE #0;
     |FINSI;
|FPRO;
