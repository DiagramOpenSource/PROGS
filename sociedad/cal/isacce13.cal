|FICHEROS;
     isacce13;
     isom0001;
     zusuario;

     :/agicli/def/agifigen;
     isow0900,MANTE;

     maacceso;
|FIN;

|VARIABLES;
   nVentana      = 0;
   nVent         = 0;
   nError        = 0;
   nValor        = 0;
   nHandle       = 0;
   nLabel        = 0;
   nBoton1       = -1;
   nBoton2       = -1;
   nBoton3       = -1;
   nBoton4       = -1;
   nGrid         = -1;
   nTree         = -1;
   nBotonSal     = -1;
   nEBoton2      = 0;
   nEBoton4      = 0;
   nPanta        = 0;
   nRango        = 0;
   nHandleTgz    = 0;
   nIdSalir      = 0;
   nPos1         = 0;
   nPos2         = 0;
   nPunto        = 0;

   aPathSociedad = "";
   aDef          = "";
   aAlfa         = "";
   aTecla        = "";
   aError        = "";
   aF2           = "";
   aOrigen       = "";
   aDestino      = "";
   aIPRemoto     = "";
   aDocRemoto    = "";
   aDocServidor  = "";
   aAbre         = "";
   aBase         = "";
   aContenido    = "";
   aTexto        = "";
   aEntidad      = "";
   aSucursal     = "";
   aDC           = "";
   aCuenta       = "";
   aPais         = "";
   aIBAN         = "";

   aEsc1         = "";
   aEsc2         = "";
   aRetu         = "";

     {-1}sTree      = 0;
     sT_nID         = 0;
     sT_nOper       = 0;
     sT_aData       = "";

     {-1}aVent      = "";
         aVent1     = 0;
         aVent2     = 0;
         aVent3     = 0;
         aVent4     = 0;
         aVent5     = "";

     {2}nNivel      = 0;

     &empresa     = 0;
     &anyo        = 0;
     &nombre      = "";
     &nif         = "";
     &tipo        = "";
     &modo        = 0;
     &sw_mod      = 0;
     &SwAdic      = 0;
     &eParametro  = "FUERA08";
     &enCodCli    = 0;
     &enSwDeDonde = 0;
     &eaEntidad   = "";
     &eaOficina   = "";
     &eaDC        = "";
     &eaCuenta    = "";
     &enModelo    = 0;
     &enBanco     = 0;
     &enForzar    = 0;
     &eaPathAcrobat = "";
     &eaModelo      = "";
     &enAnyo        = 0;
     &enEmpresa     = 0;
     &fFechaChequeo = @;
     &enError       = 0;
     &fFechaDisol   = @;
     &eaDirTrabajo   = "";
     &eaRutaOrigen   = "";
     &eaRutaDestino  = "";
     &eaFicheroTra   = "";
     &eaFicheroTxt   = "";
     &eaFicheroTgz   = "";
     &eaFicheroTar   = "";
     &eaOrigen       = "";
     &eaDestino      = "";
     &eaAlfa         = "";
     &eaPathExplorador = "";
     &eaForzExplorador = "";
     &efFechaImpre     = @;
     &eaImpre          = "";
     &eaJusti          = "";
     &enEjercicio      = 0;
     &enModoConsulta   = 0;
     &eaUnidadBasico   = "";

     &eaAzul           = "";
     &eaVerde          = "";
     &eaRojo           = "";

     &eaTNegro         = "";
     &eaTAzul          = "";
     &eaTRojo          = "";
     &eaTVerde         = "";
     &eaTTodos         = "";
|FIN;

|PROCESO BotonSalir;
     |PTEC 806;
|FPRC;

|PROCESO PonBotonSalir;
     |SI nBotonSal ! -1;  |FIN_CONTROL_WINDOWS nBotonSal;  |FINSI;

     |CONTROL_SIMPLE 0, "BOTON,{{197}} sa&Lir", 3485, 3498, BotonSalir;
     nBotonSal = FSalida;
|FPRC;

|PROCESO QuitaBotonSalir;
     |SI nIdSalir  = -1;  |ACABA;  |FINSI;

     nIdSalir  = 0;
     InNivel   = nNivel1<-;
     nNivel1   = 0;
     |ESPECIFICA "IDBOTONTECLA", nNivel;

     nIdSalir = nNivel2;
     |FIN_CONTROL_WINDOWS nIdSalir;
     nIdSalir  = 1;

    |HAZ PonBotonSalir;
|FPRC;

|PROCESO GrabaBanco;
     |ABRE #isow0900;

     #isow0900#0 = #isacce13#0;
     #isow0900#1 = #isacce13#1;
     |LEE 000#isow0900.=;
     |SI FSalida = 0;  |CIERRA #isow0900;  |ACABA;  |FINSI;

     |ABRE #agifigen;

     aEntidad  = "";
     aSucursal = "";
     aDC       = "";
     aCuenta   = "";
     aPais     = "";
     aIBAN     = "";

     #isow0900#0 = #isacce13#0;
     #isow0900#1 = #isacce13#1 - 1;
     |LEE 000#isow0900.=;
     |SI FSalida = 0;
         aEntidad  = #isow0900#2;
         aSucursal = #isow0900#3;
         aDC       = #isow0900#4;
         aCuenta   = #isow0900#5;
         aPais     = #isow0900#6;
         aIBAN     = #isow0900#7;
     |FINSI;

     #isow0900#0 = #isacce13#0;
     #isow0900#1 = #isacce13#1;
     |LEE 101#isow0900.=;
     |SI FSalida ! 0;
         |DDEFECTO #isow0900;
         #isow0900#0 = #isacce13#0;
         #isow0900#1 = #isacce13#1;

         |SI aEntidad ! "";
             #isow0900#2 = aEntidad;
             #isow0900#3 = aSucursal;
             #isow0900#4 = aDC;
             #isow0900#5 = aCuenta;
             #isow0900#6 = aPais;
             #isow0900#7 = aIBAN;
         |SINO;
             |ABRE #agifigen;
             #agifigen#0 = #isacce13#0;
             |LEE 000#agifigen.=;
             |SI FSalida ! 0;  |DDEFECTO #agifigen;  |FINSI;
             |CIERRA #agifigen;

             aAlfa   = #agifigen#81;        |QBF aAlfa; #isow0900#2 = ("0000" + aAlfa) % -104;
             aAlfa   = #agifigen#82;        |QBF aAlfa; #isow0900#3 = ("0000" + aAlfa) % -104;
             aAlfa   = #agifigen#83;        |QBF aAlfa; #isow0900#4 = ("00"   + aAlfa) % -102;
             aAlfa   = #agifigen#84 % 110;  |QBF aAlfa; #isow0900#5 =  ("0000000000" + aAlfa) % -110;

             #isow0900#6 = #agifigen#100;
             #isow0900#7 = #agifigen#94;
         |FINSI;

         |GRABA 020#isow0900.n;
         |LIBERA #isow0900;
     |FINSI;

     |CIERRA #agifigen;
     |CIERRA #isow0900;
|FPRC;

|PROCESO EventoGrid;
     |SI FSalida = 1;  |O FSalida = 2;
         |HAZ QuitaBotonSalir;

         |ESTADO_CONTROL nBoton2, "DISABLE";  nEBoton2 = 0;
         |ESTADO_CONTROL nBoton4, "DISABLE";  nEBoton4 = 0;
         |ESTADO_CONTROL nTree, "ENABLE";
         |LEE 000#isacce13.=;
         |SI FSalida = 0;
             enEmpresa = #isacce13#0;
             enAnyo    = #isacce13#1;
             |ESTADO_CONTROL nBoton2, "ENABLE";  nEBoton2 = 1;
             |ESTADO_CONTROL nBoton4, "ENABLE";  nEBoton4 = 1;

             |HAZ GrabaBanco;
         |FINSI;

         |SI nLabel ! -1;
              |FIN_CONTROL_WINDOWS nLabel;
         |FINSI;

         aAlfa = "LABEL,{{2}}" + (("00000" + #isacce13#0) % -105) + " - " + #isacce13#2;  |QBF aAlfa;
         |SI #isacce13#6 = "N";
             aAlfa = aAlfa + " - NO IMPRESA";
         |SINO;
             aAlfa = aAlfa + " - IMPRESA";
         |FINSI;

         nPos1 = ((#zusuario#2 + 2) * 100) + 02;
         nPos2 = ((#zusuario#2 + 2) * 100) + 65;
         |CONTROL_SIMPLE nPanta, aAlfa, nPos1, nPos2, NULL;
         nLabel = FSalida;
     |FINSI;
|FPRC;

|PROCESO Boton2;
     |SI nEBoton2 = 0;   |ACABA;  |FINSI;

     |CONFI "0000NSeguro que quiere borrar el registro del impuesto de sociedades";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     empresa = #isacce13#0;
     anyo    = #isacce13#1;

     |LEE 101#isacce13.=;
     |SI FSalida = 0;
         |SUB_EJECUTA "z0000001;IMPUESTO";

         |BORRA 020#isacce13.a;
         |LIBERA #isacce13;
     |FINSI;

     |LEE 000#isacce13.];

     |ESTADO_CONTROL nTree, "DISABLE";

     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO EmisionImpuesto;
     |SI nEBoton4 = 0;   |ACABA;  |FINSI;

     |SI #isacce13#6 = "S";
         |CONFI "0000NEl impuesto est  impreso. Quiere dejarlo como no impreso y volverlo a emitir";
         |SI FSalida ! 0; |ACABA;  |FINSI;
     |FINSI;

     efFechaImpre = "01.01.0000";
     eaImpre   = "N";
     eaJusti   = "";
     enEmpresa = #isacce13#0;
     enAnyo    = #isacce13#1;
     |SUB_EJECUTA "isoy2013";

     |LEE 101#isacce13.=;
     |SI FSalida = 0;
         |HAZ CargaColores;

         #isacce13#6 = eaImpre;
         #isacce13#7 = eaJusti;
         #isacce13#9 = "";
         #isacce13#10 = efFechaImpre;
         |SI #isacce13#6 = "S";
             #isacce13#9 = eaVerde;
         |FINSI;

         |GRABA 020#isacce13.a;
         |LIBERA #isacce13;

         |REFRESCACONTROL nGrid;
     |FINSI;
|FPRC;

|PROCESO Justificante;
     |SI #isacce13#6 ! "S";
         |MENAV "0000No est  emitido el modelo 200";
         |ACABA;
     |FINSI;

     |LEE 101#isacce13.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |VENTANA 0501, 1350, -1, 16, "N£mero de justificante obtenido";
     nVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     aAlfa = "LABEL,{{2}}N£mero de justificante ";
     |CONTROL_SIMPLE 0, aAlfa, 0902, 0920, NULL;

     eaJusti = #isacce13#7;
     E_inf   = "";
     E_sup   = "16";

     |PARA;  |SI;  |HACIENDO;
          eaJusti = 0922 ? 1;
          |CONFI "0000SEl n£mero introducido es correcto";
          |SI FSalida = 0;  |SAL;  |FINSI;
     |SIGUE;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;
     |FINVENTANA nVent;

     |PULSATECLA;

     #isacce13#7 = eaJusti;
     |GRABA 020#isacce13.a;
     |LIBERA #isacce13;

     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO Banco200;
     |VENTANA 0501, 1455, -1, 16, "Banco impuesto";
     nVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |ABRE #isow0900;
     #isow0900#0 = #isacce13#0;
     #isow0900#1 = #isacce13#1;
     |LEE 101#isow0900.=;
     |SI FSalida ! 0;
         |DDEFECTO #isow0900;
         #isow0900#0 = #isacce13#0;
         #isow0900#1 = #isacce13#1;
         |GRABA 020#isow0900.b;
     |FINSI;

     |PINPA #0#isow0900;
     |PINDA #0#isow0900;
     |ENDATOS #1#isow0900;
     |SI FSalida = 0;
         |GRABA 020#isow0900.a;
     |FINSI;
     |LIBERA #isow0900;
     |CIERRA #isow0900;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;
     |FINVENTANA nVent;

     |PULSATECLA;
|FPRC;

|PROCESO Boton3;
     eaForzExplorador = "S";
     enEjercicio      = #isacce13#1;
     |HAZ Explorador;
|FPRC;

|PROCESO Baja100;
     #maacceso#0 = 1;
     #maacceso#1 = 2013;
|FPRC;

|PROCESO Alta100;
     #maacceso#0 = 99999;
     #maacceso#1 = 2013;
|FPRC;

|PROCESO Boton1;
     |ABRE #maacceso;
     #maacceso#0 = #isacce13#0;
     #maacceso#1 = #isacce13#1;
     |LEE 000#maacceso.=;
     |CONSULTA_F_CLAVE #5#maacceso, Baja100, Alta100;
     |SI FSalida ! 0;  |CIERRA #maacceso; |ACABA;  |FINSI;
     |CIERRA #maacceso;

     #isacce13#0   = #maacceso#0;
     #isacce13#1   = #maacceso#1;
     |LEE 000#isacce13.=;
     |SI FSalida = 0;
         |MENAV  "0000La empresa ya ha sido abierta.";
         |ACABA;
     |FINSI;

     enEmpresa = #isacce13#0;
     aAlfa         = "01.01." + #isacce13#1;
     fFechaChequeo = aAlfa;
     |HAZ ChequeaEmpresa;
     |SI enError = 2;
         aAlfa = "0000NEmpresa con fecha disolucion " + fFechaDisol + ". Continuar.";
         |CONFI aAlfa;
         |SI FSalida ! 0;
             |ERROR;
             |ACABA;
         |FINSI;
      |FINSI;


     |DDEFECTO #isacce13;
     #isacce13#0 = #maacceso#0;
     #isacce13#1 = #maacceso#1;
     #isacce13#2 = #maacceso#2;
     #isacce13#3 = #maacceso#3;
     |GRABA 020#isacce13.n;
     |LIBERA #isacce13;

     enEmpresa = #isacce13#0;
     enAnyo    = #isacce13#1;

     |SUB_EJECUTA "isoz0001";

     |LEE 000#isacce13.=;

     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO BotonMas;
     |ABRE #zusuario;
     #zusuario#0 = Usuario % 810;
     |LEE 101#zusuario.=;
     |SI FSalida = 0;
         #zusuario#2 = #zusuario#2 + 1;
         |SI #zusuario#2 > 29;
             #zusuario#2 = 29;
         |FINSI;

         |GRABA 020#zusuario.a;
         |LIBERA #zusuario;
     |FINSI;
     |CIERRA #zusuario;

     |PTEC 802;
|FPRC;

|PROCESO BotonMenos;
     |ABRE #zusuario;
     #zusuario#0 = Usuario % 810;
     |LEE 101#zusuario.=;
     |SI FSalida = 0;
         #zusuario#2 = #zusuario#2 - 1;
         |SI #zusuario#2 < 10;
             #zusuario#2 = 10;
         |FINSI;

         |GRABA 020#zusuario.a;
         |LIBERA #zusuario;
     |FINSI;
     |CIERRA #zusuario;

     |PTEC 802;
|FPRC;

|PROCESO isom0001;
     aAlfa = "";
     |SI enModoConsulta = 1;
         aAlfa = "(MODO CONSULTA)   ";
     |FINSI;

     aAlfa =  aAlfa + "Liquidaci¢n (" + (("00000" + #isacce13#0) % -105) + " / " + #isacce13#1 + " " + #isacce13#2;
     |QBF aAlfa;
     aAlfa = aAlfa + ")";
     |VENTANA 0501, 3399, -1, 16, aAlfa;
     nVentana = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |SUB_EJECUTA "isom0001";

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PULSATECLA;

     |FINVENTANA nVentana;

     |HAZ PonBotonSalir;
|FPRC;

|PROCESO Ejecucion;
     enEmpresa = #isacce13#0;
     enAnyo    = #isacce13#1;

     |ESTADO_CONTROL nGrid, "DISABLE";
     |ESTADO_CONTROL nTree, "DISABLE";

     |SI aContenido = "isoy0001";  |SUB_EJECUTA "isoy0001";           aContenido = "";  |FINSI;
     |SI aContenido = "isoy0002";  |SUB_EJECUTA "isoy0002";           aContenido = "";  |FINSI;
     |SI aContenido = "borrador";  |SUB_EJECUTA "isoy2013;BORRADOR";  aContenido = "";  |FINSI;
     |SI aContenido = "isoy2013";  |HAZ EmisionImpuesto;              aContenido = "";  |FINSI;
     |SI aContenido = "justific";  |HAZ Justificante;                 aContenido = "";  |FINSI;
     |SI aContenido = "Banco200";  |HAZ Banco200;                     aContenido = "";  |FINSI;

     |SI #isacce13#6 = "S";  |Y aContenido ! "";
         |MENAV "0000El impuesto est  impreso. Se entrar  en MODO CONSULTA";

         enModoConsulta = 1;
     |FINSI;

     |SI aContenido = "isom0001";  |HAZ isom0001;            |FINSI;

     |SI aContenido = "isom0016";  |SUB_EJECUTA "isom0016";  |FINSI;
     |SI aContenido = "isow0013";  |SUB_EJECUTA "isow0013";  |FINSI;
     |SI aContenido = "isow0006";  |SUB_EJECUTA "isow0006";  |FINSI;
     |SI aContenido = "isow0007";  |SUB_EJECUTA "isow0007";  |FINSI;
     |SI aContenido = "isow0008";  |SUB_EJECUTA "isow0008";  |FINSI;
     |SI aContenido = "isow0009";  |SUB_EJECUTA "isow0009";  |FINSI;
     |SI aContenido = "isoz0001";  |SUB_EJECUTA "isoz0001";  |FINSI;
     |SI aContenido = "isoz0002";  |SUB_EJECUTA "isoz0002;PANTALLA";  |FINSI;

     |SI aContenido = "isom0017";  |SUB_EJECUTA "isom0017";  |FINSI;
     |SI aContenido = "isom0018";  |SUB_EJECUTA "isom0018";  |FINSI;
     |SI aContenido = "isom0019";  |SUB_EJECUTA "isom0019";  |FINSI;
     |SI aContenido = "isom0020";  |SUB_EJECUTA "isom0020";  |FINSI;
     |SI aContenido = "isom0021";  |SUB_EJECUTA "isom0021";  |FINSI;

     |ESTADO_CONTROL nGrid, "ENABLE";
     |ESTADO_CONTROL nTree, "ENABLE";

     |LEE 000#isacce13.=;
|FPRC;

|PROCESO EventoTree;
     aContenido = Contenido % -108;

     |SI FEntrada = 0;
         aTexto  = Contenido;
         |ACABA;
     |FINSI;

     enModoConsulta = 0;

     |LEE 000#isacce13.=;

     |HAZ Ejecucion;

     |REFRESCACONTROL nGrid;  |PULSATECLA;

     |FOCOTECLADO nTree;
|FPRC;

|PROCESO Baja0;
     #isacce13#0 = 1;
     #isacce13#1 = 2013;
|FPRC;

|PROCESO Alta0;
     #isacce13#0 = 99999;
     #isacce13#1 = 2013;
|FPRC;

|PROCESO CargaArbol;
     eaAlfa = eaAlfa + &13 + &10;
     |XGRABA nHandle, eaAlfa;
|FPRC;

|PROCESO ControlPantalla;
     |SI nTree   ! -1;  |FIN_CONTROL_WINDOWS nTree;    nTree   = -1;  |FINSI;
     |SI nGrid   ! -1;  |FIN_CONTROL_WINDOWS nGrid;    nGrid   = -1;  |FINSI;
     |SI nLabel  ! -1;  |FIN_CONTROL_WINDOWS nLabel;   nLabel  = -1;  |FINSI;
     |SI nBoton1 ! -1;  |FIN_CONTROL_WINDOWS nBoton1;  nBoton1 = -1;  |FINSI;
     |SI nBoton2 ! -1;  |FIN_CONTROL_WINDOWS nBoton2;  nBoton2 = -1;  |FINSI;
     |SI nBoton3 ! -1;  |FIN_CONTROL_WINDOWS nBoton3;  nBoton3 = -1;  |FINSI;
     |SI nBoton4 ! -1;  |FIN_CONTROL_WINDOWS nBoton4;  nBoton4 = -1;  |FINSI;

     |PULSATECLA;
     |PTEC 802;  |PAUSA;
     |PULSATECLA;

     |ABRE #zusuario;
     #zusuario#0 = Usuario % 810;
     |LEE 000#zusuario.=;
     |CIERRA #zusuario;

     |DBASE aBase;  |QBF aBase;
     aAlfa = aBase + "tmp";      |MKDIR aAlfa;

     eaRutaOrigen  = aBase + "tmp/";
     eaRutaDestino = eaUnidadBasico + "\MODELOS";            |RMKDIR eaRutaDestino;
     eaRutaDestino = eaUnidadBasico + "\MODELOS\TMP";        |RMKDIR eaRutaDestino;
     eaRutaDestino = eaUnidadBasico + "\MODELOS\TMP\";
     eaFicheroTxt  = "isacce13" + Usuario + ".txt";
     eaFicheroTgz  = "isacce13" + Usuario + ".tgz";
     eaFicheroTar  = "isacce13" + Usuario + ".tar";

     sT_nID   = nTree;
     sT_nOper = 0;

     aAbre = eaRutaOrigen + eaFicheroTxt;
     |XABRE "WB", aAbre, nHandle;

     nPunto = 1;
     eaAlfa = nPunto + ":Liquidaci¢n {{isom0001}}";
     |HAZ CargaArbol;

     nPunto = nPunto + 1;
     eaAlfa = nPunto + ":Apartados no contemplados en la liquidaci¢n {{}}";
     |HAZ CargaArbol;

     eaAlfa = nPunto + ",1:Presentaci¢n de documentaci¢n previa por Registro electr¢nico {{isom0016}}";
     |HAZ CargaArbol;

     eaAlfa = nPunto + ",2:R‚gimen especial de la reserva para inversiones en Canarias {{isow0013}}";
     |HAZ CargaArbol;

     eaAlfa = nPunto + ",3:Comunicaci¢n del importe neto de la cifra de negocio {{}}";
     |HAZ CargaArbol;

     eaAlfa = nPunto + ",3,1:Grupos de sociedades {{isow0006}}";
     |HAZ CargaArbol;

     eaAlfa = nPunto + ",3,2:No residentes de un establecimiento permanente {{isow0007}}";
     |HAZ CargaArbol;

     eaAlfa = nPunto + ",3,3:Entidades de cr‚dito, aseguradoras e instituciones inversi¢n colectiva {{isow0008}}";
     |HAZ CargaArbol;

     eaAlfa = nPunto + ",4:Operaciones de fusi¢n, escisi¢n y canje de valores {{isow0009}}";
     |HAZ CargaArbol;

     nPunto = nPunto + 1;
     eaAlfa = nPunto + ":Informaci¢n adicional de ajustes y deducciones {{}}";
     |HAZ CargaArbol;

     eaAlfa = nPunto + ",1:Otras correcciones a la cuenta de PyG {{isom0017}}";
     |HAZ CargaArbol;

     eaAlfa = nPunto + ",2:Deducci¢n art¡culo 42 LIS {{isom0018}}";
     |HAZ CargaArbol;

     eaAlfa = nPunto + ",3:Deducci¢n por inversiones para protecci¢n medio ambiente {{isom0019}}";
     |HAZ CargaArbol;

     eaAlfa = nPunto + ",4:Deducci¢n por gastos en investigaci¢n y desarrollo {{isom0020}}";
     |HAZ CargaArbol;

     eaAlfa = nPunto + ",5:Deducci¢n por innovaci¢n tecnol¢gica {{isom0021}}";
     |HAZ CargaArbol;

     nPunto = nPunto + 1;
     eaAlfa = nPunto + ":Utilidades {{}}";
     |HAZ CargaArbol;

     eaAlfa = nPunto + ",1:Carga de datos de contabilidad y del impuesto del ejercicio anterior {{isoz0001}}";
     |HAZ CargaArbol;

     eaAlfa = nPunto + ",2:Chequeo de integridad de datos {{isoz0002}}";
     |HAZ CargaArbol;

     nPunto = nPunto + 1;
     eaAlfa = nPunto + ":Informes y emisiones {{}}";
     |HAZ CargaArbol;

     eaAlfa = nPunto + ",1:Informe de comprobaci¢n  {{isoy0001}}";
     |HAZ CargaArbol;

     eaAlfa = nPunto + ",2:Emisi¢n del formulario de la Informaci¢n adicional de ajustes y deducciones {{isoy0002}}";
     |HAZ CargaArbol;

     eaAlfa = nPunto + ",3:Borrador del modelo 200 sin validaci¢n {{borrador}}";
     |HAZ CargaArbol;

     eaAlfa = nPunto + ",4:Emisi¢n modelo 200 {{isoy2013}}";
     |HAZ CargaArbol;

     eaAlfa = nPunto + ",5:Justificante de la declaraci¢n {{justific}}";
     |HAZ CargaArbol;

     eaAlfa = nPunto + ",6:Banco del modelo 200 {{Banco200}}";
     |HAZ CargaArbol;

     |XCIERRA nHandle;

     |HAZ TraeTxtComprimido;
     aAlfa = eaRutaDestino + eaFicheroTxt;

     nPos1 = ((#zusuario#2 + 3) * 100) + 02;
     nPos2 = 3598;

     eaAlfa = "TREECTRL, Tabla de contenidos de la empresa seleccionada {{" + aAlfa + "}}";
     |CONTROL_SIMPLE nPanta, eaAlfa, nPos1, nPos2, EventoTree;
     nTree = FSalida;

     |ESTADO_CONTROL nTree, "DISABLE";

     eaFicheroTxt  = "isacce13" + Usuario + ".txt";
     aAlfa         = eaRutaDestino + eaFicheroTxt;
     |RFBORRA aAlfa;

     sT_nID   = nTree;
     sT_nOper = 2;
     sT_aData = "1:";

     |ESPECIFICA "COMMANDCONTROL", sTree;

     |FOCOTECLADO nTree;

     nPos1 = ((#zusuario#2 + 1) * 100) + 67;
     nPos2 = ((#zusuario#2 + 1) * 100) + 81;
     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}nuevo &Registro", nPos1, nPos2, Boton1;
     nBoton1 = FSalida;

     nPos1 = ((#zusuario#2 + 1) * 100) + 84;
     nPos2 = ((#zusuario#2 + 1) * 100) + 98;
     |CONTROL_SIMPLE nPanta, "BOTON,{{207}}&Borrar registro", nPos1, nPos2, Boton2;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}explorador &Predeterminado", 3455, 3480, Boton3;
     nBoton3 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{205}}&Emisi¢n modelo 200", 3402, 3425, EmisionImpuesto;
     nBoton4 = FSalida;


     |ESTADO_CONTROL nBoton2, "DISABLE";  nEBoton2 = 0;
     |ESTADO_CONTROL nBoton4, "DISABLE";  nEBoton4 = 0;

     nRango = 4 + 8 + 16 + 32 + 128;
     nPos1  = 0602;
     nPos2  = (#zusuario#2 * 100) + 98;

     |LINEAL_SIMPLE #1#isacce13, nRango, nPos1, nPos2, Baja0, Alta0, EventoGrid;
     nGrid = FSalida;

     |PULSATECLA;
|FPRC;

|PROCESO AyudaColores;

     |HAZ ContarImpuestos;

     |VENTANA 0502, 1246, -1, 16, "Ayuda colores GRID / Contadores";
     nVentana = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |CONTROL_SIMPLE 0, "LABEL,{{16}}Negro", 0603, 0610, NULL;
     |CONTROL_SIMPLE 0, "LABEL,{{16}}Impuestos sin emitir", 0612, 0649, NULL;
     aAlfa = "LABEL,{{16}}" + eaTNegro; |CONTROL_SIMPLE 0, aAlfa, 0641, 0649, NULL;

     |CONTROL_SIMPLE 0, "LABEL,{{16}}Azul", 0703, 0710, NULL;
     |CONTROL_SIMPLE 0, "LABEL,{{16}}Impuestos con errores leves", 0712, 0749, NULL;
     aAlfa = "LABEL,{{16}}" + eaTAzul;  |CONTROL_SIMPLE 0, aAlfa, 0741, 0749, NULL;

     |CONTROL_SIMPLE 0, "LABEL,{{16}}Rojo", 0803, 0810, NULL;
     |CONTROL_SIMPLE 0, "LABEL,{{16}}Impuestos con errores graves", 0812, 0849, NULL;
     aAlfa = "LABEL,{{16}}" + eaTRojo;  |CONTROL_SIMPLE 0, aAlfa, 0841, 0849, NULL;

     |CONTROL_SIMPLE 0, "LABEL,{{16}}Verde", 903, 910, NULL;
     |CONTROL_SIMPLE 0, "LABEL,{{16}}Impuestos emitidos", 912, 949, NULL;
     aAlfa = "LABEL,{{16}}" + eaTVerde;  |CONTROL_SIMPLE 0, aAlfa, 0941, 0949, NULL;

     |CONTROL_SIMPLE 0, "LABEL,{{15}}Total Impuestos introducidos", 1012, 1049, NULL;
     aAlfa = "LABEL,{{15}}" + eaTTodos; |CONTROL_SIMPLE 0, aAlfa, 1041, 1049, NULL;

     |PAUSA;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVentana;
|FPRC;

|PROGRAMA;
     |HAZ LeeUnidadBasico;

     |PINPA #0#isacce13;
     nPanta = FSalida;

     |ABRE #zusuario;
     #zusuario#0 = Usuario % 810;
     |LEE 101#zusuario.=;
     |SI FSalida ! 0;
         |DDEFECTO #zusuario;
         #zusuario#0 = Usuario % 810;
         |GRABA 020#zusuario.n;
     |FINSI;
     |LIBERA #zusuario;
     |CIERRA #zusuario;

     |CONTROL_SIMPLE nPanta, "BOTON, + ", 0595, 0598, BotonMas;
     |CONTROL_SIMPLE nPanta, "BOTON, - ", 0590, 0593, BotonMenos;
     |CONTROL_SIMPLE nPanta, "BOTON,Ayuda colores/contadores", 0565, 0587, AyudaColores;

     aTecla = "";
     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         |HAZ ControlPantalla;
         nIdSalir  = 0;

         FSalida = "::{{001}}sa&Lir," + nGrid;

         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu;        |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;
     |FIN_CONTROL_WINDOWS nBoton4;
|FPRO;

|PROCESO Tipo80;   |TIPO 80;
     |ABRET #isacce13;

     FSalida = 3499;
|FPRC;

|PROCESO Tipo90;   |TIPO 90;
     |CIERRAT #isacce13;
|FPRC;
