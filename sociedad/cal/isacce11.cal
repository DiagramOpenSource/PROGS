|FICHEROS;
     isacce11 #0;
     maempr10 #200;
     isom0001 #101;

     :/agicli/def/agifigen #1000;    || Bancos
     isow0900 #1001,MANTE;

     maacceso #100;
|FIN;

|VARIABLES;
   nVentana    = 0;
   nVent       = 0;
   nError      = 0;
   nValor      = 0;
   nHandle     = 0;
   nLabel      = 0;

   aPathSociedad = "";
   aDef          = "";
   aAlfa         = "";
   aTecla        = "";
   aError        = "";
   aF2           = "";
   aOrigen       = "";
   aDestino      = "";
   aIPRemoto     = "";
   aDocRemoto    = "";
   aDocServidor  = "";
   aAbre         = "";
   aBase         = "";
   aContenido    = "";
   aTexto        = "";
   aEntidad      = "";
   aSucursal     = "";
   aDC           = "";
   aCuenta       = "";

   nBoton1       = 0;
   nBoton2       = 0;
   nBoton3       = 0;
   nBoton4       = 0;
   nEBoton2      = 0;
   nEBoton4      = 0;
   nGrid         = 0;
   nPanta        = 0;
   nRango        = 0;
   nHandleTgz    = 0;
   nTree         = 0;
   nIdSalir      = 0;
   nBotonSal     = -1;

   aEsc1         = "";
   aEsc2         = "";
   aRetu         = "";

     {-1}sTree      = 0;
     sT_nID         = 0;
     sT_nOper       = 0;
     sT_aData       = "";

     {-1}aVent      = "";
         aVent1     = 0;
         aVent2     = 0;
         aVent3     = 0;
         aVent4     = 0;
         aVent5     = "";

     {2}nNivel      = 0;

     &empresa     = 0;
     &anyo        = 0;
     &nombre      = "";
     &nif         = "";
     &tipo        = "";
     &modo        = 0;
     &sw_mod      = 0;
     &SwAdic      = 0;
     &eParametro  = "FUERA08";
     &enCodCli    = 0;
     &enSwDeDonde = 0;
     &eaEntidad   = "";
     &eaOficina   = "";
     &eaDC        = "";
     &eaCuenta    = "";
     &enModelo    = 0;
     &enBanco     = 0;
     &enForzar    = 0;
     &eaPathAcrobat = "";
     &eaModelo      = "";
     &enAnyo        = 0;
     &enEmpresa     = 0;
     &fFechaChequeo = @;
     &enError       = 0;
     &fFechaDisol   = @;
     &eaDirTrabajo   = "";
     &eaRutaOrigen   = "";
     &eaRutaDestino  = "";
     &eaFicheroTra   = "";
     &eaFicheroTxt   = "";
     &eaFicheroTgz   = "";
     &eaFicheroTar   = "";
     &eaOrigen       = "";
     &eaDestino      = "";
     &eaAlfa         = "";
     &eaPathExplorador = "";
     &eaForzExplorador = "";
     &eaImpre          = "";
     &eaJusti          = "";
     &enEjercicio      = 0;
     &enModoConsulta   = 0;
|FIN;

|PROCESO BotonSalir;
     |PTEC 806;
|FPRC;

|PROCESO PonBotonSalir;
     |SI nBotonSal ! -1;  |FIN_CONTROL_WINDOWS nBotonSal;  |FINSI;

     |CONTROL_SIMPLE 0, "BOTON,{{197}} sa&Lir", 3485, 3498, BotonSalir;
     nBotonSal = FSalida;
|FPRC;

|PROCESO QuitaBotonSalir;
     |SI nIdSalir  = -1;  |ACABA;  |FINSI;

     nIdSalir  = 0;
     InNivel   = nNivel1<-;
     nNivel1   = 0;
     |ESPECIFICA "IDBOTONTECLA", nNivel;

     nIdSalir = nNivel2;
     |FIN_CONTROL_WINDOWS nIdSalir;
     nIdSalir  = 1;

    |HAZ PonBotonSalir;
|FPRC;

|PROCESO GrabaBanco;
     |ABRE #1001;

     #1001#0 = #0#0;
     #1001#1 = #0#1;
     |LEE 000#1001=;
     |SI FSalida = 0;  |CIERRA #1001;  |ACABA;  |FINSI;

     |ABRE #1000;

     aEntidad  = "";
     aSucursal = "";
     aDC       = "";
     aCuenta   = "";

     #1001#0 = #0#0;
     #1001#1 = #0#1 - 1;
     |LEE 000#1001=;
     |SI FSalida = 0;
         aEntidad  = #1001#2;
         aSucursal = #1001#3;
         aDC       = #1001#4;
         aCuenta   = #1001#5;
     |FINSI;

     #1001#0 = #0#0;
     #1001#1 = #0#1;
     |LEE 101#1001=;
     |SI FSalida ! 0;
         |DDEFECTO #1001;
         #1001#0 = #0#0;
         #1001#1 = #0#1;

         |SI aEntidad ! "";
             #1001#2 = aEntidad;
             #1001#3 = aSucursal;
             #1001#4 = aDC;
             #1001#5 = aCuenta;
         |SINO;
             |ABRE #1000;
             #1000#0 = #0#0;
             |LEE 000#1000=;
             |SI FSalida ! 0;  |DDEFECTO #1000;  |FINSI;
             |CIERRA #1000;

             aAlfa   = #1000#81;        |QBF aAlfa; #1001#2 = ("0000" + aAlfa) % -104;
             aAlfa   = #1000#82;        |QBF aAlfa; #1001#3 = ("0000" + aAlfa) % -104;
             aAlfa   = #1000#83;        |QBF aAlfa; #1001#4 = ("00"   + aAlfa) % -102;
             aAlfa   = #1000#84 % 110;  |QBF aAlfa; #1001#5 =  ("0000000000" + aAlfa) % -110;
         |FINSI;

         |GRABA 020#1001n;
         |LIBERA #1001;
     |FINSI;

     |CIERRA #1000;
     |CIERRA #1001;
|FPRC;

|PROCESO EventoGrid;
     |SI FSalida = 1;  |O FSalida = 2;
         |HAZ QuitaBotonSalir;

         |ESTADO_CONTROL nBoton2, "DISABLE";  nEBoton2 = 0;
         |ESTADO_CONTROL nBoton4, "DISABLE";  nEBoton4 = 0;
         |ESTADO_CONTROL nTree, "ENABLE";
         |LEE 000#0=;
         |SI FSalida = 0;
             enEmpresa = #0#0;
             enAnyo    = #0#1;
             |ESTADO_CONTROL nBoton2, "ENABLE";  nEBoton2 = 1;
             |ESTADO_CONTROL nBoton4, "ENABLE";  nEBoton4 = 1;

             |HAZ GrabaBanco;
         |FINSI;

         |SI nLabel ! -1;
              |FIN_CONTROL_WINDOWS nLabel;
         |FINSI;

         aAlfa = "LABEL,{{2}}" + (("00000" + #0#0) % -105) + " - " + #0#2;  |QBF aAlfa;
         |SI #0#6 = "N";
             aAlfa = aAlfa + " - NO IMPRESA";
         |SINO;
             aAlfa = aAlfa + " - IMPRESA";
         |FINSI;

         |CONTROL_SIMPLE nPanta, aAlfa, 2702, 2765, NULL;
         nLabel = FSalida;
     |FINSI;
|FPRC;

|PROCESO Boton2;
     |SI nEBoton2 = 0;   |ACABA;  |FINSI;

     |CONFI "0000NSeguro que quiere borrar el registro del impuesto de sociedades";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     empresa = #0#0;
     anyo    = #0#1;

     |LEE 101#0=;
     |SI FSalida = 0;
         |SUB_EJECUTA "z0000001;IMPUESTO";

         |BORRA 020#0a;
         |LIBERA #0;
     |FINSI;

     |LEE 000#0];

     |ESTADO_CONTROL nTree, "DISABLE";

     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO EmisionImpuesto;
     |SI nEBoton4 = 0;   |ACABA;  |FINSI;

     |SI #0#6 = "S";
         |CONFI "0000NEl impuesto est  impreso. Quiere dejarlo como no impreso y volverlo a emitir";
         |SI FSalida ! 0; |ACABA;  |FINSI;
     |FINSI;

     eaImpre   = "N";
     eaJusti   = "";
     enEmpresa = #0#0;
     enAnyo    = #0#1;
     |SUB_EJECUTA "isoy2011";

     |LEE 101#0=;
     |SI FSalida = 0;
         #0#6 = eaImpre;
         #0#7 = eaJusti;
         |GRABA 020#0a;
         |LIBERA #0;

         |REFRESCACONTROL nGrid;
     |FINSI;
|FPRC;

|PROCESO Justificante;
     |SI #0#6 ! "S";
         |MENAV "0000No est  emitido el modelo 200";
         |ACABA;
     |FINSI;

     |LEE 101#0=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |VENTANA 0501, 1350, -1, 16, "N£mero de justificante obtenido";
     nVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     aAlfa = "LABEL,{{2}}N£mero de justificante ";
     |CONTROL_SIMPLE 0, aAlfa, 0902, 0920, NULL;

     eaJusti = #0#7;
     E_inf   = "";
     E_sup   = "16";

     |PARA;  |SI;  |HACIENDO;
          eaJusti = 0922 ? 1;
          |CONFI "0000SEl n£mero introducido es correcto";
          |SI FSalida = 0;  |SAL;  |FINSI;
     |SIGUE;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;
     |FINVENTANA nVent;

     |PULSATECLA;

     #0#7 = eaJusti;
     |GRABA 020#0a;
     |LIBERA #0;

     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO Bancos;
     |VENTANA 0501, 1455, -1, 16, "Banco impuesto";
     nVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |ABRE #1001;
     #1001#0 = #0#0;
     #1001#1 = #0#1;
     |LEE 101#1001=;
     |SI FSalida ! 0;
         |DDEFECTO #1001;
         #1001#0 = #0#0;
         #1001#1 = #0#1;
         |GRABA 020#1001b;
     |FINSI;

     |PINPA #0#1001;
     |PINDA #0#1001;
     |ENDATOS #1#1001;
     |SI FSalida = 0;
         |GRABA 020#1001a;
     |FINSI;
     |LIBERA #1001;
     |CIERRA #1001;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;
     |FINVENTANA nVent;

     |PULSATECLA;
|FPRC;

|PROCESO Boton3;
     |ABRE #200;
     #200#0 = #0#0;
     #200#1 = #0#1;
     |LEE 000#200=;
     |SI FSalida ! 0;  |DDEFECTO #200;  |FINSI;
     |CIERRA #200;

     eaForzExplorador = "S";
     enEjercicio      = #0#1;
     |HAZ Explorador;
|FPRC;

|PROCESO Baja100;
     #100#0 = 1;
     #100#1 = 2011;
|FPRC;

|PROCESO Alta100;
     #100#0 = 99999;
     #100#1 = 2011;
|FPRC;

|PROCESO Boton1;
     |ABRE #100;
     #100#0 = #0#0;
     #100#1 = #0#1;
     |LEE 000#100=;
     |CONSULTA_F_CLAVE #5#100, Baja100, Alta100;
     |SI FSalida ! 0;  |CIERRA #100; |ACABA;  |FINSI;
     |CIERRA #100;

     #0#0   = #100#0;
     #0#1   = #100#1;
     |LEE 000#0=;
     |SI FSalida = 0;
         |MENAV  "0000La empresa ya ha sido abierta.";
         |ACABA;
     |FINSI;

     enEmpresa = #0#0;
     aAlfa         = "01.01." + #0#1;
     fFechaChequeo = aAlfa;
     |HAZ ChequeaEmpresa;
     |SI enError = 2;
         aAlfa = "0000NEmpresa con fecha disolucion " + fFechaDisol + ". Continuar.";
         |CONFI aAlfa;
         |SI FSalida ! 0;
             |ERROR;
             |ACABA;
         |FINSI;
      |FINSI;


     |DDEFECTO #0;
     #0#0 = #100#0;
     #0#1 = #100#1;
     #0#2 = #100#2;
     #0#3 = #100#3;
     |GRABA 020#0n;
     |LIBERA #0;

     enEmpresa = #0#0;
     enAnyo    = #0#1;

     |SUB_EJECUTA "isoz0001";

     |LEE 000#0=;

     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO isom0001;
     aAlfa = "";
     |SI enModoConsulta = 1;
         aAlfa = "(MODO CONSULTA)   ";
     |FINSI;

     aAlfa =  aAlfa + "Liquidaci¢n (" + (("00000" + #0#0) % -105) + " / " + #0#1 + " " + #0#2;
     |QBF aAlfa;
     aAlfa = aAlfa + ")";
     |VENTANA 0501, 3399, -1, 16, aAlfa;
     nVentana = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |SUB_EJECUTA "isom0001";

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PULSATECLA;

     |FINVENTANA nVentana;

     |HAZ PonBotonSalir;
|FPRC;

|PROCESO Ejecucion;
     enEmpresa = #0#0;
     enAnyo    = #0#1;

     |ESTADO_CONTROL nGrid, "DISABLE";
     |ESTADO_CONTROL nTree, "DISABLE";

     |SI aContenido = "isoy0001";  |SUB_EJECUTA "isoy0001";           aContenido = "";  |FINSI;
     |SI aContenido = "isoy0002";  |SUB_EJECUTA "isoy0002";           aContenido = "";  |FINSI;
     |SI aContenido = "borrador";  |SUB_EJECUTA "isoy2011;BORRADOR";  aContenido = "";  |FINSI;
     |SI aContenido = "isoy2011";  |HAZ EmisionImpuesto;              aContenido = "";  |FINSI;
     |SI aContenido = "justific";  |HAZ Justificante;                 aContenido = "";  |FINSI;
     |SI aContenido = "bancos";    |HAZ Bancos;                       aContenido = "";  |FINSI;

     |SI #0#6 = "S";  |Y aContenido ! "";
         |MENAV "0000El impuesto est  impreso. Se entrar  en MODO CONSULTA";

         enModoConsulta = 1;
     |FINSI;

     |SI aContenido = "isom0001";  |HAZ isom0001;            |FINSI;

     |SI aContenido = "isom0016";  |SUB_EJECUTA "isom0016";  |FINSI;
     |SI aContenido = "isow0013";  |SUB_EJECUTA "isow0013";  |FINSI;
     |SI aContenido = "isow0006";  |SUB_EJECUTA "isow0006";  |FINSI;
     |SI aContenido = "isow0007";  |SUB_EJECUTA "isow0007";  |FINSI;
     |SI aContenido = "isow0008";  |SUB_EJECUTA "isow0008";  |FINSI;
     |SI aContenido = "isow0009";  |SUB_EJECUTA "isow0009";  |FINSI;
     |SI aContenido = "isoz0001";  |SUB_EJECUTA "isoz0001";  |FINSI;
     |SI aContenido = "isoz0002";  |SUB_EJECUTA "isoz0002;PANTALLA";  |FINSI;

     |SI aContenido = "isom0017";  |SUB_EJECUTA "isom0017";  |FINSI;
     |SI aContenido = "isom0018";  |SUB_EJECUTA "isom0018";  |FINSI;
     |SI aContenido = "isom0019";  |SUB_EJECUTA "isom0019";  |FINSI;
     |SI aContenido = "isom0020";  |SUB_EJECUTA "isom0020";  |FINSI;

     |ESTADO_CONTROL nGrid, "ENABLE";
     |ESTADO_CONTROL nTree, "ENABLE";

     |LEE 000#isacce11.=;
|FPRC;

|PROCESO EventoTree;
     aContenido = Contenido % -108;

     |SI FEntrada = 0;
         aTexto  = Contenido;
         |ACABA;
     |FINSI;

     enModoConsulta = 0;

     |LEE 000#isacce11.=;

     |HAZ Ejecucion;

     |REFRESCACONTROL nGrid;  |PULSATECLA;

     |FOCOTECLADO nTree;
|FPRC;

|PROCESO CargaArbol;
     eaAlfa = eaAlfa + &13 + &10;
     |XGRABA nHandle, eaAlfa;
|FPRC;

|PROCESO MontaArbol;
     |PTEC 802;  |PAUSA;

     |DBASE aBase;  |QBF aBase;
     aAlfa = aBase + "tmp";      |MKDIR aAlfa;

     eaRutaOrigen  = aBase + "tmp/";
     eaRutaDestino = "C:\MODELOS";            |RMKDIR eaRutaDestino;
     eaRutaDestino = "C:\MODELOS\TMP";        |RMKDIR eaRutaDestino;
     eaRutaDestino = "C:\MODELOS\TMP\";
     eaFicheroTxt  = "isacce11" + Usuario + ".txt";
     eaFicheroTgz  = "isacce11" + Usuario + ".tgz";
     eaFicheroTar  = "isacce11" + Usuario + ".tar";

     sT_nID   = nTree;
     sT_nOper = 0;

     aAbre = eaRutaOrigen + eaFicheroTxt;
     |XABRE "WB", aAbre, nHandle;

     eaAlfa = "1:Liquidaci¢n {{isom0001}}";
     |HAZ CargaArbol;

     eaAlfa = "2:Apartados no contemplados en la liquidaci¢n {{}}";
     |HAZ CargaArbol;

     eaAlfa = "2,1:Presentaci¢n de documentaci¢n previa por Registro electr¢nico {{isom0016}}";
     |HAZ CargaArbol;

     eaAlfa = "2,2:R‚gimen especial de la reserva para inversiones en Canarias {{isow0013}}";
     |HAZ CargaArbol;

     eaAlfa = "2,3:Comunicaci¢n del importe neto de la cifra de negocio {{}}";
     |HAZ CargaArbol;

     eaAlfa = "2,3,1:Grupos de sociedades {{isow0006}}";
     |HAZ CargaArbol;

     eaAlfa = "2,3,2:No residentes de un establecimiento permanente {{isow0007}}";
     |HAZ CargaArbol;

     eaAlfa = "2,3,3:Entidades de cr‚dito, aseguradoras e instituciones inversi¢n colectiva {{isow0008}}";
     |HAZ CargaArbol;

     eaAlfa = "2,4:Operaciones de fusi¢n, escisi¢n y canje de valores {{isow0009}}";
     |HAZ CargaArbol;

     eaAlfa = "3:Informaci¢n adicional de ajustes y deducciones {{}}";
     |HAZ CargaArbol;

     eaAlfa = "3,1:Otras correcciones a la cuenta de PyG {{isom0017}}";
     |HAZ CargaArbol;

     eaAlfa = "3,2:Deducci¢n art¡culo 42 LIS {{isom0018}}";
     |HAZ CargaArbol;

     eaAlfa = "3,3:Deducci¢n por inversiones para protecci¢n medio ambiente {{isom0019}}";
     |HAZ CargaArbol;

     eaAlfa = "3,4:Deducci¢n por gastos en investigaci¢n y desarrollo e innovaci¢n tecnol¢gica {{isom0020}}";
     |HAZ CargaArbol;

     eaAlfa = "4:Utilidades {{}}";
     |HAZ CargaArbol;

     eaAlfa = "4,1:Carga de datos de contabilidad y del impuesto del ejercicio anterior {{isoz0001}}";
     |HAZ CargaArbol;

     eaAlfa = "4,2:Chequeo de totales {{isoz0002}}";
     |HAZ CargaArbol;

     eaAlfa = "5:Informes y emisiones {{}}";
     |HAZ CargaArbol;

     eaAlfa = "5,1:Informe de comprobaci¢n  {{isoy0001}}";
     |HAZ CargaArbol;

     eaAlfa = "5,2:Emisi¢n del formulario de la Informaci¢n adicional de ajustes y deducciones {{isoy0002}}";
     |HAZ CargaArbol;

     eaAlfa = "5,3:Borrador del modelo 200 sin validaci¢n {{borrador}}";
     |HAZ CargaArbol;

     eaAlfa = "5,4:Emisi¢n modelo 200 {{isoy2011}}";
     |HAZ CargaArbol;

     eaAlfa = "5,5:Justificante de la declaraci¢n {{justific}}";
     |HAZ CargaArbol;

     eaAlfa = "5,6:Banco del modelo 200 {{bancos}}";
     |HAZ CargaArbol;

     |XCIERRA nHandle;

     |HAZ TraeTxtComprimido;
     aAlfa = eaRutaDestino + eaFicheroTxt;

     eaAlfa = "TREECTRL, Tabla de contenidos {{" + aAlfa + "}}";
     |CONTROL_SIMPLE nPanta, eaAlfa, 2802, 3598, EventoTree;
     nTree = FSalida;

     |ESTADO_CONTROL nTree, "DISABLE";

     eaFicheroTxt  = "isacce11" + Usuario + ".txt";
     aAlfa         = eaRutaDestino + eaFicheroTxt;
     |RFBORRA aAlfa;

     sT_nID   = nTree;
     sT_nOper = 2;
     sT_aData = "1:";

     |ESPECIFICA "COMMANDCONTROL", sTree;

     |FOCOTECLADO nTree;
|FPRC;

|PROCESO Baja0;
     #0#0 = 1;
     #0#1 = 2011;
|FPRC;

|PROCESO Alta0;
     #0#0 = 99999;
     #0#1 = 2011;
|FPRC;

|PROCESO Tipo80;   |TIPO 80;
     |ABRET #0;

     FSalida = 3499;
|FPRC;

|PROCESO Tipo90;   |TIPO 90;
     |CIERRAT #0;
|FPRC;

|PROGRAMA;
     |PINPA #0#0;
     nPanta = FSalida;

     |HAZ MontaArbol;

     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}&Nuevo registro", 2667, 2681, Boton1;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{207}}&Borrar registro", 2684, 2698, Boton2;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}explorador &Predeterminado", 3455, 3480, Boton3;
     nBoton3 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{205}}&Emisi¢n modelo 200", 3402, 3425, EmisionImpuesto;
     nBoton4 = FSalida;

     |ESTADO_CONTROL nBoton2, "DISABLE";  nEBoton2 = 0;
     |ESTADO_CONTROL nBoton4, "DISABLE";  nEBoton4 = 0;

     nRango = 4 + 8 + 16 + 32 + 128;

     |LINEAL_SIMPLE #1#0, nRango, 0602, 2598, Baja0, Alta0, EventoGrid;
     nGrid = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}sa&Lir," + nGrid;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;
     |FIN_CONTROL_WINDOWS nBoton4;
|FPRO;
