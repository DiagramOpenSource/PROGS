|FICHEROS;
     refer00@ #0;

     :/agicli/def/agim0019 #19;
|FIN;

|VARIABLES;
     aPrograma    = "";
     aDef         = "";
     aQueQuiero   = "";
     aNombreCampo = "";
     aFichero     = "";
     aDecima      = "";
     aEntero      = "";
     aLong        = "";
     aCaracter    = "";
     aImpor1      = "";
     aImpor2      = "";
     aImpor3      = "";
     aImpor4      = "";
     aBase        = "";
     aIPRemoto    = "";
     aUsuario     = "";

     nCampo       = 0;
     nTCampo      = 0;
     nSwHay       = 0;
     nLong        = 0;
     nDecimal     = 0;
     nCaracter    = 0;
     nPos         = 0;
     nPorDeci     = 0;

     &eaMoneda    = "";
     &enCanti     = 0;
     &enSwTrata   = 0;
     &eaImporte   = "";
     &eaPrograma  = "";

     &fFechaChequeo  = @;
     &enError        = 0;
     &enSaltaError   = 0;
     &fFechaDisol    = @;
     &enEmpresa      = 0;

     &eaAzul         = "";
     &eaVerde        = "";
     &eaRojo         = "";
     &eaGris         = "";

     &EURODB      = 0;

     {-1}aMoneda   = "";
         aMoneda1  = "LEER";
         aMoneda2  = "";
|FIN;

|PROCESO PintaMoneda;  |TIPO 7;
     aPrograma = Control;  |QBF aPrograma;
     |SI eaPrograma ! "";
         aPrograma = eaPrograma;
     |FINSI;

     |DBASE aDef;  |QBF aDef;
     aDef   = aDef + "def/" + aPrograma + ".mas";
     |CARGA_DEF aDef, refer00@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aQueQuiero = "|NC";
     aFichero   = #0#aQueQuiero#;
     nTCampo    = aFichero;
     nTCampo    = nTCampo - 1;
     nSwHay     = 0;
     |PARA nCampo = 0;  |SI nCampo [ nTCampo;  |HACIENDO nCampo = nCampo + 1;
           aQueQuiero   = "|C" + (("000" + nCampo) % -103) + "NOMBRE";
           aNombreCampo = (#0#aQueQuiero# + (" " * 20)) % 120;
           |QBF aNombreCampo;
           |SI aNombreCampo = "***MONEDA***";
               nSwHay = 1;
               |SAL;
           |FINSI;
     |SIGUE;

     |SI nSwHay = 0;
         |DESCARGA_DEF #refer00@;
         |ACABA;
     |FINSI;

     IaMoneda = aMoneda1<-;
     |ESPECIFICA "MODO_VER_EURO", aMoneda;

     |SI EURODB ! 0;
         |SI aMoneda2 = "0";
             #0nCampo = "EUROS";
         |SINO;
             #0nCampo = "PESETAS";
         |FINSI;
     |SINO;
         |SI aMoneda2 = "0";
             #0nCampo = "PESETAS";
         |SINO;
             #0nCampo = "EUROS";
         |FINSI;
     |FINSI;

     |PINTA #0nCampo;
     eaMoneda = #0nCampo;

     |DESCARGA_DEF #refer00@;
|FPRC;

|PROCESO ConverMoneda;
     |SI eaMoneda = "";
         eaMoneda = "PESETAS";
         |SI EURODB = 2;
             eaMoneda = "EUROS  ";
         |FINSI;
     |FINSI;

     enCanti  = enCanti ! EURODB;
     nPorDeci = 1;
     |SI enSwTrata = 0;  nPorDeci = 100;  |FINSI;

     |SI EURODB ! 0;
         |SI eaMoneda = "PESETAS";
             enCanti = ((enCanti * 166.386) ! 0);
         |SINO;
             |SI nPorDeci = 100;
                 enCanti = (enCanti * 100) ! 0;
             |SINO;
                 enCanti = enCanti;
             |FINSI;
         |FINSI;
     |SINO;
         |SI eaMoneda = "EUROS  ";
             enCanti = ((enCanti / 166.386) ! 2) * nPorDeci;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO SeparaImporte;    || Calculo para poner las puntos y comas
                           || en el importe

     |HAZ ConverMoneda;
     eaImporte = enCanti;

     aDecima   = "";
     aEntero   = "";
     |SI eaMoneda ! "PESETAS";
         aLong    = eaImporte % 0;
         nLong    = aLong;
         nDecimal = 0;
         |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
               nPos      = (100 * nCaracter) + 1;
               aCaracter = eaImporte % nPos;
               |SI aCaracter = ".";
                   nDecimal = 1;
               |SINO;
                   |SI nDecimal = 0;
                       aEntero = aEntero + aCaracter;
                   |SINO;
                       aDecima = aDecima + aCaracter;
                   |FINSI;
               |FINSI;
         |SIGUE;
         aDecima   = (aDecima + "00") % 102;
         eaImporte = aEntero;
     |FINSI;

     aLong = eaImporte % 0;
     nLong = aLong;
     eaImporte = "             " + eaImporte;
     eaImporte = eaImporte % -113;
     aImpor1   = eaImporte % -103;
     aImpor2   = eaImporte % -403;
     aImpor3   = eaImporte % -703;
     aImpor4   = eaImporte % -1004;
     |SI nLong ] 10;
         |SI aImpor4 ! "  -";
             eaImporte = aImpor4 + "." + aImpor3  + "." + aImpor2 + "." + aImpor1;
         |SINO;
             eaImporte = aImpor4 + aImpor3  + "." + aImpor2 + "." + aImpor1;
      |FINSI;
    |SINO;
        |SI nLong ] 7;
            |SI aImpor3 ! "  -";
                eaImporte = aImpor3  + "." + aImpor2 + "." + aImpor1;
            |SINO;
                eaImporte = aImpor3 + aImpor2 + "." + aImpor1;
            |FINSI;
        |SINO;
            |SI nLong ] 4;
                |SI aImpor2 ! "  -";
                    eaImporte = aImpor2 + "." + aImpor1;
                |SINO;
                    eaImporte = aImpor2 + aImpor1;
                |FINSI;
            |SINO;
                eaImporte = aImpor1;
            |FINSI;
        |FINSI;
     |FINSI;

     |SI eaMoneda = "PESETAS";
         eaImporte = ("                " + eaImporte) % -116;
     |SINO;
         eaImporte = ("                " + eaImporte + "," + aDecima) % -116;
     |FINSI;

     |SI eaImporte = "               0";  eaImporte = "";  |FINSI;
     |SI eaImporte = "            0,00";  eaImporte = "";  |FINSI;

     |QBT eaImporte;
     |SI eaImporte = ""; eaImporte = " ";  |FINSI;
|FPRC;

|PROCESO agim0019;
     |SI #19#3 > fFechaChequeo;
         |ACABA;
     |FINSI;

     enError = 0;

     |SI #19#54 = "I";
         |SI #19#55 < fFechaChequeo;
             enError = 1;
         |FINSI;
     |FINSI;

     |SI #19#12 > "01.01.0000";  |Y #19#12 < fFechaChequeo;
         enError     = 2;
         fFechaDisol = #19#12;
     |FINSI;
|FPRC;

|DEFBUCLE agim0019;
     #19#1;
     ;
     enEmpresa, 01;
     enEmpresa, 99;
     ;
     NULL, agim0019;
|FIN;

|PROCESO ChequeaEmpresa;
     |DBASS aBase;
     aBase = aBase + "agicli/fich/";
     |PATH_DAT #19 aBase;

     enError = 0;
     |BUCLE agim0019;
|FPRC;

|PROCESO SaltaError;
     enSaltaError = 0;

     |IP_REMOTO aIPRemoto;
     aUsuario = Usuario % 810;  |QBF aUsuario;
     |SI aIPRemoto = "192.168.10.42";  |Y aUsuario = "JUANMA";
         enSaltaError = 1;
     |FINSI;
|FPRC;

|PROCESO CargaColores;
     eaAzul  = "18,28,245;255,255,255";
     eaVerde = "0,185,0;255,255,255";
     eaRojo  = "255,80,80;255,255,255";
     eaGris  = "90,90,90;255,255,255";
|FPRC;
