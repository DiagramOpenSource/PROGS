|FICHEROS;
   cubal807 #0;
   cubal801 #1;
   cubal802 #2;
   cubal803 #3;
   cubal804 #4;
   cubal805 #5;

   cuacceso #10;
   maempr01 #6;
   maempr03 #7;
   :/sociedad/def/soda8tco #12;
   :/sociedad/def/soda8m02 #421;  || Def Balance Estado

   soda8w07 #890;
|FIN;

|VARIABLES;
  &empresa   = 0;
  &anyo      = 0;
  &nombre    = "";
  &nif       = "";
  &hoja1     = 0;
  &direccion = "";
  &ejer1     = "";
  &ejer2     = "";
  &adm1      = "";
  &adm2      = "";
  &adm3      = "";
  &adm4      = "";
  &adm5      = "";
  numero     = 0;
  inform1    = "";
  inform2    = "";

  {-1}menu   = "";
      menu1  = "2400";
      menu2  = "1";
      menu3  = " Elija formato: ";
      menu4  = "CS";
      menu5  = " Con Datos Empresa ";
      menu6  = " Sin Datos Empresa ";

 aAlfa1    = "";
 aAlfa2    = "";
 aAlfa3    = "";
 sw        = 0;
 tipo      = "";
 nImprimir = 0;

 &doh      = "";
 &eaTitol  = "";
 &sw_inf  = 0;
 &swImp     = 0;
 aCasilla = "";
 nCol       = 0;
 nCol1      = 0;
 nCol2      = 0;
 nCol3      = 0;
 nNume1     = 0;
 nNume2     = 0;
 nNume3     = 0;
 nNume4     = 0;
 &swImp1    = 0;
 &swImp2    = 0;
 &swImp3    = 0;
 &eaRaya    = "";

 aParam     = "";
 &eaImp1    = "";
 &eaImp2    = "";
 &eaImp3    = "";
 &eaCeros   = "";
 &eaFich1   = "";
 &eaFich2   = "";
 &eaFich3   = "";
 &eaICtas   = "";
 nNumero    = 0;
 swCta      = 0;
|FIN;

|INCLUYE varbal_i;

|PROCESO nomodi;
|SI #0#0 = "S";
    |CAMPO_MODIFICA #0#1, 1, "S";
|SINO;
    |CAMPO_MODIFICA #0#1, 1, "N";
|FINSI;
|FPRC;

|PROCESO ImpTodos;
 aAlfa1 = "S";
 |SI #0#4 = "S";
     aAlfa1 = "N";
     #0#5 = "S"; |PINTA #0#5;
     #0#6 = "S"; |PINTA #0#6;
     #0#7 = "S"; |PINTA #0#7;
     #0#8 = "S"; |PINTA #0#8;
     #0#9 = "S"; |PINTA #0#9;
 |FINSI;
 |C_M #0#5, 1, aAlfa1;
 |C_M #0#6, 1, aAlfa1;
 |C_M #0#7, 1, aAlfa1;
 |C_M #0#8, 1, aAlfa1;
 |C_M #0#9, 1, aAlfa1;
|FPRC;

|PROGRAMA;
 |DDEFECTO #0;
 aParam = PARAMETRO; |QBF aParam;

 |SI aParam ! "BORRADOR";
     eaICtas = "N";
     |PINPA #0#0;
     |SI enTBal ! 1; #0#9 = "N"; |FINSI;
     |SI enTBal = 3; #0#7 = "N"; |FINSI;

     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
         |MENU menu;
         menu2 = FSalida;
         |SI menu2 = 1; |O menu2 = 2;
             |HAZ impresio;
         |FINSI;
     |FINSI;
 |SINO;
     |NOME_DAT #1   eaFich1;
     |NOME_DAT #2   eaFich2;
     |NOME_DAT #890 eaFich3;
     #0#1 = hoja1;
     #0#2 = eaCeros;
     #0#5 = eaImp2;
     #0#6 = eaImp3;
     #0#7 = "N";
     #0#8 = "N";
     #0#9 = "N";
     menu2 = 2;
     |HAZ impresio;
 |FINSI;
|FPRO;

|| ******************************************
|PROCESO Leem805;

  |PDEFECTO #0,10,21;
  aCasilla = "";
  |SI #5#16 ! 0;
      aCasilla = #5#16;
      aCasilla = ("000000" + aCasilla) % -106;
  |FINSI;
  #0#16 = #5#3;
  #0#17 = aCasilla;
  #0#18 = #5#7;
  #0#19 = #5#6;
  #0#24 = #5#5;
  |SI #5#8 ! "A"; nImprimir = 0; |FINSI;

  |SI #5#7 = 0; |Y #5#6 = 0; |Y #5#8 ! "A";
      |SI #0#2 = "N"; |ACABA; |FINSI;
  |FINSI;

  |SI sw = 0;
      sw = 1;
  |FINSI;

  sw_inf = 0;
  |SI #5#4 = "G"; sw_inf = 1; |FINSI;
  |SI #5#8 = "A"; sw_inf = 7; |FINSI;
  |SI #5#8 ! "A"; |O nImprimir = 1;
      |PRINT;
      nImprimir = 1;
  |FINSI;
|FPRC;

|DEFBUCLE Leem805;
 #5#1;
 ;
 empresa, anyo, 0001;
 empresa, anyo, 9999;
 e;
 NULL,Leem805;
|FIN;

|PROCESO Imprimir_5;
 doh = "";
 nBalance = 4;
 eaTitol = "         F L U J O S   D E   E F E C T I V O";
 sw      = 0;
 |BUCLE Leem805;
 |SI sw = 1;
     |PIEINF;
 |FINSI;
|FPRC;

|PROCESO Leem804;

  |PDEFECTO #0,16,21;
  aCasilla = "";
  |SI #4#4 ! 0;
      aCasilla = #4#4;
      aCasilla = ("000" + aCasilla) % -103;
  |FINSI;
  #0#16  = #4#5;
  #0#17 = aCasilla;

  |SI sw = 0;
      tipo = #4#3;
      sw = 1;
      sw_inf = 1;
  |SINO;
      |SI tipo ! #4#3;
          sw_inf = 5; |PRINT;
          tipo = #4#3;
          sw_inf = 1;
      |FINSI;
  |FINSI;

  |SI nCol < 13;
      nCol1 = nCol;     nNume1 = nCol1 + 5;
      nCol2 = nCol + 1; nNume2 = nCol2 + 5;
      nCol3 = nCol + 2; nNume3 = nCol3 + 5;
      #0#18 = #4nNume1;
      #0#19 = #4nNume2;
      #0#20 = #4nNume3;
  |SINO;
      nCol1 = nCol;     nNume1 = nCol1 + 5;
      nCol2 = 0;
      nCol3 = 0;
      #0#18 = #4nNume1;
      #0#19 = 0;
      #0#20 = 0;
  |FINSI;

  swImp1 = 1; swImp2 = 1; swImp3 = 1;
  #421#34= enEjerBal;
  #421#0 = nBalNum;
  #421#1 = #4#2;
  |LEE 001#421=;
  |SI nCol1 < 13;
      nNume1 = nCol1 + 6; |SI #421nNume1 ! "X";  swImp1 = 0; |FINSI;
      nNume1 = nCol2 + 6; |SI #421nNume1 ! "X";  swImp2 = 0; |FINSI;
      nNume1 = nCol3 + 6; |SI #421nNume1 ! "X";  swImp3 = 0; |FINSI;
  |SINO;
      swImp1 = 0;
  |FINSI;

  |PRINT;
  sw_inf = 0;
  sw = 1;
|FPRC;

|DEFBUCLE Leem804;
 #4#1;
 ;
 empresa, anyo, 001;
 empresa, anyo, 999;
 e;
 NULL,Leem804;
|FIN;

|PROCESO Recorta;
 |QBF aAlfa1;
 aAlfa2 = aAlfa1 % 0;
 nNume4 = aAlfa2;
 nNume4 = ((15 - nNume4) / 2) ! 0;
 aAlfa1 =(( " " * nNume4) + aAlfa1) % 115;
|FPRC;


|PROCESO Imprimir_4;
 nBalance = 4;
 eaTitol = "        ESTADO DE CAMBIOS DEL PATRIMONIO NETO";

 |ABRE #12;
 |ABRE #421;
 |PARA nCol = 1; |SI nCol [ 13; |HACIENDO nCol = nCol + 3;
       sw      = 0;
       |PDEFECTO #0,10,16;
       #0#21 = ""; #0#22 = ""; #0#23 = "";
       |SI nCol < 13;
          eaRaya = "Ä" * 52;
          |DDEFECTO #12;
          #12#5 = enEjerBal;
          #12#0 = nBalNum;
          #12#1 = nCol;
          |LEE 001#12=;
          #0#10 = #12#3; #0#11 = #12#4;
          #0#21 = ("00" + #12#1) % -102;

          |DDEFECTO #12;
          #12#5 = enEjerBal;
          #12#0 = nBalNum;
          #12#1 = nCol + 1;
          |LEE 001#12=;
          #0#12 = #12#3; #0#13 = #12#4;
          #0#22 = ("00" + #12#1) % -102;

          |DDEFECTO #12;
          #12#5 = enEjerBal;
          #12#0 = nBalNum;
          #12#1 = nCol + 2;
          |LEE 001#12=;
          #0#14 = #12#3; #0#15 = #12#4;
          #0#23 = ("00" + #12#1) % -102;
      |SINO;
          #0#10 = ""; #0#11 = "   T O T A L   ";
          #0#21 = "13";
      |FINSI;
      aAlfa1 = #0#10; |HAZ Recorta; #0#10 = aAlfa1;
      aAlfa1 = #0#11; |HAZ Recorta; #0#11 = aAlfa1;
      aAlfa1 = #0#12; |HAZ Recorta; #0#12 = aAlfa1;
      aAlfa1 = #0#13; |HAZ Recorta; #0#13 = aAlfa1;
      aAlfa1 = #0#14; |HAZ Recorta; #0#14 = aAlfa1;
      aAlfa1 = #0#15; |HAZ Recorta; #0#15 = aAlfa1;
      |BUCLE Leem804;
      |SI sw = 1; |PIEINF; |FINSI;
 |SIGUE;
 |CIERRA #421;
 |CIERRA #12;
|FPRC;

|PROCESO Leem803;

  |PDEFECTO #0,10,21;
  aCasilla = "";
  |SI #3#16 ! 0;
      aCasilla = #3#16;
      aCasilla = ("000000" + aCasilla) % -106;
  |FINSI;
  #0#16 = #3#3;
  #0#17 = aCasilla;
  #0#18 = #3#7;
  #0#19 = #3#6;
  #0#24 = #3#5;
  |SI #3#8 ! "A"; nImprimir = 0; |FINSI;

  |SI #3#7 = 0; |Y #3#6 = 0; |Y #3#8 ! "A";
      |SI #0#2 = "N"; |ACABA; |FINSI;
  |FINSI;

  |SI sw = 0;
      sw = 1;
  |FINSI;

  sw_inf = 0;
  |SI #3#4 = "G"; sw_inf = 1; |FINSI;
  |SI #3#8 = "A"; sw_inf = 7; |FINSI;
  |SI #3#8 ! "A"; |O nImprimir = 1;
      |PRINT;
      nImprimir = 1;
  |FINSI;
|FPRC;

|DEFBUCLE Leem803;
 #3#1;
 ;
 empresa, anyo, 0001;
 empresa, anyo, 9999;
 e;
 NULL,Leem803;
|FIN;

|PROCESO Imprimir_3;
 doh = "";
 nBalance = 2;
 eaTitol = "          INGRESOS  Y  GASTOS  RECONOCIDOS";
 sw      = 0;
 |BUCLE Leem803;
 |SI sw = 1;
     |PIEINF;
 |FINSI;
|FPRC;

|PROCESO LeoSoda8w07;
  swCta  = 1;
  sw_inf = 11;
  |PRINT;
|FPRC;

|DEFBUCLE LeoSoda8w07;
  #890#1;
  ;
  empresa, anyo, nBalance, nNumero, "            ";
  empresa, anyo, nBalance, nNumero, "zzzzzzzzzzzz";
  ;
  NULL, LeoSoda8w07;
|FIN;

|PROCESO Leem802;

  swCta = 0;
  |PDEFECTO #0,10,21;
  aCasilla = "";
  |SI #2#16 ! 0;
      aCasilla = #2#16;
      aCasilla = ("000000" + aCasilla) % -106;
  |FINSI;
  #0#16 = #2#3;
  #0#17 = aCasilla;
  #0#18 = #2#7;
  #0#19 = #2#6;
  #0#24 = #2#5;
  |SI #2#8 ! "A"; nImprimir = 0; |FINSI;

  |SI #2#7 = 0; |Y #2#6 = 0; |Y #2#8 ! "A";
      |SI #0#2 = "N"; |ACABA; |FINSI;
  |FINSI;

  |SI sw = 0;
      sw = 1;
  |FINSI;

  sw_inf = 0;
  |SI #2#4 = "G"; sw_inf = 1; |FINSI;
  |SI #2#8 = "A"; sw_inf = 7; |FINSI;
  |SI #2#8 ! "A"; |O nImprimir = 1;
      |PRINT;
      nImprimir = 1;
      |SI eaICtas = "S";
          nNumero = #2#2;
          |BUCLE LeoSoda8w07;
      |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE Leem802;
 #2#1;
 ;
 empresa, anyo, 0001;
 empresa, anyo, 9999;
 e;
 NULL,Leem802;
|FIN;

|PROCESO Imprimir_2;
 doh = "(DEBE)/HABER";
 nBalance = 1;
 eaTitol = "       C U E N T A   D E   R E S U L T A D O S";
 sw      = 0;
 |BUCLE Leem802;
 |SI sw = 1;
     |PIEINF;
 |FINSI;
|FPRC;

|PROCESO Leem801;
  swCta = 0;
  |PDEFECTO #0,10,21;
  aCasilla = "";
  |SI #1#16 ! 0;
      aCasilla = #1#16;
      aCasilla = ("000000" + aCasilla) % -106;
  |FINSI;
  #0#16 = #1#3;
  #0#17 = aCasilla;
  #0#18 = #1#7;
  #0#19 = #1#6;
  #0#24 = #1#5;

  |SI #1#8 ! "A"; nImprimir = 0; |FINSI;

  |SI #1#7 = 0; |Y #1#6 = 0; |Y #1#8 ! "A";
      |SI #0#2 = "N"; |ACABA; |FINSI;
  |FINSI;

  |SI sw = 0;
      tipo     = #1#10;  ||Activo o Pasivo
      |SI #1#10 = "A"; doh = "ACTIVO"; |FINSI;
      |SI #1#10 = "P"; doh = "PASIVO"; |FINSI;
      sw = 1;
  |FINSI;

  |SI #1#11 = "99";
      sw_inf = 5;  |PRINT;  |PRINT;
      sw_inf = 1;
      |PRINT;
      |PIEINF;
      doh  = "PASIVO";
      tipo  = #1#10;
      |ACABA;
  |FINSI;

  sw_inf = 0;
  |SI #1#4 = "G"; sw_inf = 1; |FINSI;
  |SI #1#8 = "A"; sw_inf = 7; |FINSI;
  |SI #1#8 ! "A"; |O nImprimir = 1;
      |PRINT;
      nImprimir = 1;
      |SI eaICtas = "S";
          nNumero = #1#2;
          |BUCLE LeoSoda8w07;
      |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE Leem801;
 #1#1;
 ;
 empresa, anyo, 0001;
 empresa, anyo, 9999;
 e;
 NULL,Leem801;
|FIN;

|PROCESO Imprimir_1;
 nBalance = 0;
 eaTitol = "       B A L A N C E   D E   S I T U A C I O N";
 sw      = 0;
 |BUCLE Leem801;
|FPRC;

|PROCESO impresio;
  hoja1 = #0#1;

  |SI aParam ! "BORRADOR"; |IMPRE 0; |SINO; FSalida = 0; |FINSI;
  |SI FSalida = 0;
      |HAZ lectura;
  |SINO;
      |MENAV "      Proceso Interrumpido";
  |FINSI;
  |SI aParam ! "BORRADOR"; |FINIMP; |FINSI;
|FPRC;

|PROCESO lectura;
 |ABRE #6; #6#0 = empresa; #6#1 = anyo; |LEE 040#6=; |CIERRA #6;
 |SI FSalida ! 0; |DDEFECTO #6; |FINSI;

 |ABRE #7;
 adm1 = ""; adm2 = ""; adm3 = ""; adm4 = ""; adm5 = "";
 #7#0 = empresa; #7#1 = anyo; #7#2 = 1; |LEE 040#7=; |SI FSalida = 0; adm1 = #7#5; |FINSI;
 #7#0 = empresa; #7#1 = anyo; #7#2 = 2; |LEE 040#7=; |SI FSalida = 0; adm2 = #7#5; |FINSI;
 #7#0 = empresa; #7#1 = anyo; #7#2 = 3; |LEE 040#7=; |SI FSalida = 0; adm3 = #7#5; |FINSI;
 #7#0 = empresa; #7#1 = anyo; #7#2 = 4; |LEE 040#7=; |SI FSalida = 0; adm4 = #7#5; |FINSI;
 #7#0 = empresa; #7#1 = anyo; #7#2 = 5; |LEE 040#7=; |SI FSalida = 0; adm5 = #7#5; |FINSI;
 |CIERRA #7;

 direccion = #6#5; |QBF direccion;
 direccion = direccion + ", " + #6#6 + " " + #6#7 + " " + #6#8 + " " + #6#9;
 numero    = anyo - 1;
 ejer1     = anyo;
 ejer2      = numero;

 |SI menu2 = 1;
     inform1 = "cubal807";
     inform2 = "cuba4807";
     |SI aParam = "BORRADOR";
         inform1 = "crba1807";
         |SI eaICtas = "S"; inform1 = "crba2807"; |FINSI;
     |FINSI;
 |SINO;
     inform1 = "crbal807";
     inform2 = "crba4807";
     |SI aParam = "BORRADOR";
         inform1 = "crba1807";
         |SI eaICtas = "S"; inform1 = "crba2807"; |FINSI;
     |FINSI;
 |FINSI;

 |ABRE #10; #10#0 = empresa; #10#1 = anyo; |LEE 040#10=; |CIERRA #10;
 |SI FSalida ! 0; |DDEFECTO #10; |FINSI;

 |SI #0#5 = "S"; |O #0#6 = "S";
     |INFOR inform1;
     |SI FSalida ! 0;
         |MENAV "    No existe Informe";
        |ACABA;
     |FINSI;

     |SI #0#5 = "S";  |HAZ Imprimir_1;  |FINSI;
     |SI #0#6 = "S";  |HAZ Imprimir_2;  |FINSI;
     |FININF;
 |FINSI;

 |SI #0#7 = "S"; |Y enTBal ! 3;
     |INFOR inform1;
     |SI FSalida ! 0;
         |MENAV "    No existe Informe";
        |ACABA;
     |FINSI;
     |HAZ Imprimir_3;
     |FININF;
 |FINSI;

 |SI #0#8 = "S";
     |INFOR inform2;
     |SI FSalida ! 0;
         |MENAV "    No existe Informe";
         |ACABA;
     |FINSI;
     |HAZ Imprimir_4;
     |FININF;
 |FINSI;

 |SI #0#9 = "S"; |Y enTBal = 1;
     |INFOR inform1;
     |SI FSalida ! 0;
         |MENAV "    No existe Informe";
         |ACABA;
     |FINSI;
     |HAZ Imprimir_5;
     |FININF;
 |FINSI;
|FPRC;
