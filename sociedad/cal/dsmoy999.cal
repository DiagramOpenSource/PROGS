|FICHEROS;
     isy00000;
|FIN;

|VARIABLES;
     aAlfa           = "";
     aAbre           = "";
     aModelos        = "";
     aVersionJava    = "";
     aEjecutaJava    = "";
     aPathModelo     = "";
     aPathLocal      = "";
     aPathServidor   = "";
     aOrigen         = "";
     aDestino        = "";
     aDocRemoto      = "";
     aDocServidor    = "";
     aIPRemoto       = "";
     aPathPdf        = "";
     aLongitud       = "";
     aIP             = "";
     aCaracter       = "";
     aCadena         = "";
     aVersion        = "";

     nHandleEjec     = 0;
     nHandError      = 0;
     nHandle1        = 0;
     nCopia          = 0;
     nLongitud       = 0;
     nCaracter       = 0;
     nCanal          = 0;
     nPos            = 0;

     {1}aContiene    = "";
     {1}version      = "";

     &enAnyo         = 0;

     &eaFOrigen      = "";
     &eaFDestino     = "";
     &eaDirJava      = "";
     &eaModelo       = "";
     &enBasico       = 0;
     &eaTipoP        = "";
     &eaTipoE        = "";
     &eaTipoR        = "";
     &eaTipoC        = "";
     &eaTipoA        = "";
     &eaTipoV        = "";
     &eaModoImpre    = "";
     &eaPathAcrobat  = "";
     &enEmpresa      = 0;
     &enHandle       = 0;
     &enError        = 0;
|FIN;

|PROCESO Acrobat;  |TIPO 0;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |ABRE #isy00000;
     #isy00000#0 = aIPRemoto;
     |LEE 101#isy00000.=;
     |SI FSalida ! 0;
         |DDEFECTO #isy00000;
         #isy00000#0 = aIPRemoto;
         |GRABA 020#isy00000.b;
     |FINSI;

     eaPathAcrobat = #isy00000#1;  |QBF eaPathAcrobat;
     |SI eaPathAcrobat = "";
         eaPathAcrobat = "AcroRd32.exe";
     |FINSI;

     eaPathAcrobat = "F" + eaPathAcrobat;
     |BROWSE eaPathAcrobat;
     eaPathAcrobat = eaPathAcrobat % 290;  |QBF eaPathAcrobat;

     |SI eaPathAcrobat ! "";
         #isy00000#1 = eaPathAcrobat;
     |FINSI;

     |GRABA 020#isy00000.a;
     |LIBERA #isy00000;
     |CIERRA #isy00000;
|FPRC;

|PROCESO ExtraePath;
     eaPathAcrobat = version1;
     aLongitud     = eaPathAcrobat % 0;
     nLongitud     = aLongitud;
     |PARA nPos = 1; |SI nPos [ nLongitud; |HACIENDO nPos = nPos + 1;
          nCaracter = (nPos * 100) + 1;
          aCaracter = eaPathAcrobat % nCaracter;
          |SI aCaracter = ",";
               nPos          = nLongitud - nPos;
               nPos          = nPos + 100;
               nPos          = (-1) * nPos;
               eaPathAcrobat = eaPathAcrobat % nPos;
               |SAL;
          |FINSI;
     |SIGUE;

     eaPathAcrobat = eaPathAcrobat - "[";
     eaPathAcrobat = eaPathAcrobat - "]";
|FPRC;

|PROCESO CompruebaRuta;
     enError = 0;
     |QBF eaPathAcrobat;
     |XABRE "CE", eaPathAcrobat, nCanal;
     |SI eaPathAcrobat = ""; |O FSalida < 0;
          |XCIERRA nCanal;
          aAlfa = "    No se ha encontrado el directorio donde esta instalado ";
          aAlfa = aAlfa + "Adobe Acrobat Reader. ";
          |MENAV aAlfa;
          aAlfa = "    Debera instalarselo.";
          |MENAV aAlfa;

          enError = 1;
     |FINSI;
|FPRC;

|PROCESO LocalizaAcrobat;
     |IP_REMOTO aIP; |QBF aIP;

     |ABRE #isy00000;
     #isy00000#0 = aIP;
     |LEE 000#isy00000.=;
     |SI FSalida ! 0;  |DDEFECTO #isy00000;  |FINSI;
     |CIERRA #isy00000;

     eaPathAcrobat = #isy00000#1;  |QBF eaPathAcrobat;

     enError  = 0;

     |SI eaPathAcrobat ! "";  |ACABA;  |FINSI;

     version1 = "AcroExch.Document";
     |ESPECIFICA "VERSION_PROGRAMA",version;
     |HAZ ExtraePath;
     |SI eaPathAcrobat = "NO LOCALIZADO";
          version1 = "AcroExch.App";
          |ESPECIFICA "VERSION_PROGRAMA",version;
          |HAZ ExtraePath;
     |FINSI;

     |SI eaPathAcrobat ! "NO LOCALIZADO";
          |HAZ CompruebaRuta;
          |SI enError = 1;          || todo ok
               |ACABA;              || hay Acrobat pero no lo encuentra
          |FINSI;
     |SINO;
          enError = 1;
     |FINSI;

     |SI enError = 0;
         |ABRE #isy00000;
         #isy00000#0 = aIP;
         |LEE 101#isy00000.=;
         |SI FSalida ! 0;
             |DDEFECTO #isy00000;
             #isy00000#0 = aIP;
             |GRABA 020#isy00000.b;
         |FINSI;
         #isy00000#1 = eaPathAcrobat;
         |GRABA 020#isy00000.a;
         |LIBERA #isy00000;
         |CIERRA #isy00000;
    |FINSI;
|FPRC;

|PROCESO TraeWait;
     aPathLocal  = "\MODELOS\SOCIEDAD\" + eaModelo + "\" + enAnyo + "\";

     |DBASE aAlfa;  |QBT aAlfa;
     aAlfa    = aAlfa + "planilla";
     aOrigen  = aAlfa + "/wait.exe";
     aDestino = aPathLocal + "wait.exe";

     aContiene1 = aDestino;
     |ESPECIFICA "REMOTOMD5", aContiene;
     aDocRemoto = FSalida;  |QBF aDocRemoto;

     aContiene1 = aOrigen;
     |ESPECIFICA "MD5", aContiene;
     aDocServidor = FSalida;  |QBF aDocServidor;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;
     |SI aDocRemoto ! aDocServidor;  |O aDocRemoto = "";
         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aOrigen, aDestino;
         |SINO;
             |COPIA_FICHERO aOrigen, aDestino;
         |FINSI;
     |FINSI;

     |DBASE aAlfa;  |QBT aAlfa;
     aOrigen  = aAlfa + "planilla/dsprint.exe";
     aDestino = aPathLocal + "dsprint.exe";

     aContiene1 = aDestino;
     |ESPECIFICA "REMOTOMD5", aContiene;
     aDocRemoto = FSalida;  |QBF aDocRemoto;

     aContiene1 = aOrigen;
     |ESPECIFICA "MD5", aContiene;
     aDocServidor = FSalida;  |QBF aDocServidor;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;
     |SI aDocRemoto ! aDocServidor;  |O aDocRemoto = "";
         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aOrigen, aDestino;
         |SINO;
             |COPIA_FICHERO aOrigen, aDestino;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO CopiaFichero999;
     aAlfa = "";
     |IP_REMOTO aAlfa;  |QBT aAlfa;
     |SI aAlfa ! "";
         |ENVIA_FICHERO eaFOrigen, eaFDestino;
     |SINO;
         |COPIA_FICHERO eaFOrigen, eaFDestino;
     |FINSI;
|FPRC;

|PROCESO LeeJava;
     enError = 0;
     aAbre   = "\Archivos de programa\Java\" + aVersionJava + "\bin\java.exe";

     |XABRE "EC", aAbre, nHandleEjec;
     |SI FSalida < 0;  enError = 1;  |ACABA;  |FINSI;
|FPRC;

|PROCESO BuscaJava;
     aVersionJava   = "jre1.6.0_03";
     aEjecutaJava   = "zjava_1_6_0_03.exe";
     |HAZ LeeJava;
     |SI enError = 1;
         aAlfa = "0000 No tiene instalada la Maquina Virtual para Java Version " + aVersionJava + ". Se va a proceder a instalar";
         |MENAV aAlfa;

         aPathModelo = "\MODELOS\SOCIEDAD\" + eaModelo + "\" + enAnyo + "\";
         aPathLocal  = "\MODELOS\SOCIEDAD\" + eaModelo + "\" + enAnyo + "\";
         |HAZ TraeWait;

         |DBASE aModelos;  |QBT aModelos;
         eaFOrigen  = aModelos + "hacienda/" + aEjecutaJava;
         eaFDestino = aPathModelo +  aEjecutaJava;
         |HAZ CopiaFichero999;

         aAbre = aPathModelo + "Ejecuta.bat";
         |XABRE "CBW", aAbre, nHandle1;

         aAlfa = "CD " + aPathModelo + &13 + &10;
         |XGRABA nHandle1, aAlfa;

         aAlfa = "wait " + aEjecutaJava + &13 + &10;
         |XGRABA nHandle1, aAlfa;

         |XCIERRA nHandle1;

         |RSYSTEM aAbre;

         |RFBORRA aAbre;
         |RFBORRA eaFDestino;

         |HAZ LeeJava;
         |SI enError = 1;
              aAlfa = "0000 No tiene instalada la Maquina Virtual para Java Version " + aVersionJava + ". Proceso Interrumpido";
              |MENAV aAlfa;
              |ACABA;
         |FINSI;
     |FINSI;

     aAlfa  = "\Archivos de programa\Java\" + aVersionJava;
     eaDirJava = "SET JAVA_HOME=" + aAlfa;  |QBF eaDirJava;
|FPRC;

|PROCESO TraeBasico;
     aPathLocal = "\MODELOS";                                                     |RMKDIR aPathLocal;
     aPathLocal = "\MODELOS\JAVA";                                                |RMKDIR aPathLocal;
     aPathLocal = "\MODELOS\JAVA\SOCIEDADES";                                       |RMKDIR aPathLocal;
     aPathLocal = "\MODELOS\JAVA\SOCIEDADES" + "\" + enAnyo;                        |RMKDIR aPathLocal;
     aPathLocal = "\MODELOS\JAVA\SOCIEDADES" + "\" + enAnyo + "\" + aVersion;       |RMKDIR aPathLocal;
     aPathLocal = "\MODELOS\JAVA\SOCIEDADES" + "\" + enAnyo + "\" + aVersion + "\";

     |DBASE aPathServidor;   |QBF aPathServidor;
     aPathServidor = aPathServidor + "hacienda/";

     aAlfa   = enAnyo;
     aAlfa   = aAlfa % -102;

     aOrigen    = aPathServidor + "i" + eaModelo + "ls" + aAlfa + ".tgz";
     aDestino   = aPathLocal    + "i" + eaModelo + "ls" + aAlfa + ".tgz";

     aContiene1 = aDestino;
     |ESPECIFICA "REMOTOMD5", aContiene;
     aDocRemoto = FSalida;  |QBF aDocRemoto;

     aContiene1 = aOrigen;
     |ESPECIFICA "MD5", aContiene;
     aDocServidor = FSalida;  |QBF aDocServidor;

     nCopia    = 0;
     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;
     |SI aDocRemoto ! aDocServidor;  |O aDocRemoto = "";
         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aOrigen, aDestino;
         |SINO;
             |COPIA_FICHERO aOrigen, aDestino;
         |FINSI;
         nCopia = 1;
     |FINSI;

     enBasico = 0;
     |XABRE "EC", aDestino, nHandleEjec;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     |SI nCopia = 1;
         |RDESCOMPRIME aDestino;

         aDestino = (aDestino - ".tgz") + ".tar";
         |RDETAR aDestino, aPathLocal;

         |RFBORRA aDestino;
     |FINSI;

     enBasico = 1;
|FPRC;

|PROCESO PresentaJava;
     aPathLocal = "\MODELOS\SOCIEDAD\" + eaModelo + "\" + enAnyo + "\";

     aAbre = aPathLocal + eaTipoP;
     |XABRE "EC", aAbre, nHandle1;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     |HAZ LocalizaAcrobat;
     |SI enError ! 0;  |ACABA;  |FINSI;

     |HAZ TraeWait;

     |SI eaModoImpre = "N";
         aAlfa = "cmd " + &34 + "/c START /WAIT \MODELOS\SOCIEDAD\201\" + enAnyo;
         aAlfa = aAlfa + "\dsprint \MODELOS\SOCIEDAD\201\" + enAnyo + "\" + eaTipoP + &34;
         |RSYSTEM aAlfa;
     |FINSI;

     |SI eaModoImpre = "S";
         aAbre = aPathLocal + "imprime.bat";
         |XABRE "CBW", aAbre, nHandle1;

         aAlfa = "CD " + aPathLocal + &13 + &10;
         |XGRABA nHandle1, aAlfa;

         aAlfa = "wait " + eaPathAcrobat + " " + eaTipoP + &13 + &10;
         |XGRABA nHandle1, aAlfa;

         |XCIERRA nHandle1;

         |RSYSTEM aAbre;
         |RFBORRA aAbre;
     |FINSI;
|FPRC;

|PROCESO LineaShellJava;
     aVersion = "V100";
     enBasico = 0;
     |HAZ BuscaJava;
     |SI enError = 1;  |ACABA;  |FINSI;

     aAbre = "\MODELOS\SOCIEDAD\" + eaModelo + "\" + enAnyo + "\imprime.bat";
     |XABRE "CBW", aAbre, enHandle;

     aAlfa = "CD \" + &13 + &10;                 |XGRABA enHandle, aAlfa;
     aAlfa = "CD MODELOS" + &13 + &10;           |XGRABA enHandle, aAlfa;
     aAlfa = "CD JAVA" + &13 + &10;              |XGRABA enHandle, aAlfa;
     aAlfa = "CD " + eaModelo + &13 + &10;       |XGRABA enHandle, aAlfa;
     aAlfa = "CD " + enAnyo + &13 + &10;         |XGRABA enHandle, aAlfa;

     |SI eaTipoR ! "";
         aAlfa = "DEL \MODELOS\SOCIEDAD\" + eaModelo + "\" + enAnyo + "\" + eaTipoR + &13 + &10;
         |XGRABA enHandle, aAlfa;
     |FINSI;

     |SI eaTipoP ! "";
         aAlfa = "DEL \MODELOS\SOCIEDAD\" + eaModelo + "\" + enAnyo + "\" + eaTipoP + &13 + &10;
         |XGRABA enHandle, aAlfa;
     |FINSI;

     aAlfa = eaDirJava + &13 + &10;
     |XGRABA enHandle, aAlfa;

     aAlfa = &34 + "%JAVA_HOME%\bin\java" + &34 + " -jar \MODELOS\JAVA\SOCIEDADES\" + enAnyo + "\" + aVersion + "\20107pdf.jar ";

     |SI eaTipoV ! "S";
         aAlfa = aAlfa + "/E:\MODELOS\SOCIEDAD\" + eaModelo + "\" + enAnyo + "\" + eaTipoE + " ";
         aAlfa = aAlfa + "/R:\MODELOS\SOCIEDAD\" + eaModelo + "\" + enAnyo + "\" + eaTipoR + " ";
         aAlfa = aAlfa + "/P:\MODELOS\SOCIEDAD\" + eaModelo + "\" + enAnyo + "\" + eaTipoP + " ";
         aAlfa = aAlfa + "/C:" + eaTipoC + " ";
     |SINO;
         aAlfa = aAlfa + "/E:\MODELOS\SOCIEDAD\" + eaModelo + "\" + enAnyo + "\" + eaTipoE + " ";
         aAlfa = aAlfa + "/V:S ";
         aAlfa = aAlfa + "/R:\MODELOS\SOCIEDAD\" + eaModelo + "\" + enAnyo + "\" + eaTipoR + " ";
     |FINSI;
     aAlfa = aAlfa + "/M:S";
     aAlfa = aAlfa + &13 + &10;
     |XGRABA enHandle, aAlfa;

     |XCIERRA enHandle;

     |HAZ TraeBasico;

     |SI enBasico = 0;  |ACABA;  |FINSI;

     aAlfa = "\MODELOS\SOCIEDAD\" + eaModelo + "\" + enAnyo + "\imprime.bat";
     |RSYSTEM aAlfa;

     |SI eaTipoV ! "S";
         |HAZ PresentaJava;
     |FINSI;
|FPRC;
