|FICHEROS;
    plutraba #0;
    sodacoca #1;
    sodacoli #2;
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    FEstado = 0;
    linea = 0;
    grupo = "";
    &param = 0;
    &empre = 0;
    &ejerc = 0;
    festa = 0;
    sw_ultima = 0;
    ultima = 0;
    saldillo = 0;
|FIN;

|CALCULO borra;
  |ATRI P;
  |MENSA "    Borrando ...";
  |ATRI 0;
  #1#0 = empre;    || empresa
  #1#1 = ejerc;    || a¤o
  |LEE 001#1=;
  |SI FSalida = 0;
      |BORRA 020#1a;
      |BUCLELIN 1NULL#1;
  |FINSI;
  |BLIN 4;
|FCAL;

|CALCULO cabs;
|DDEFECTO #1;
    #1#0 = #0#0;    || empresa
    #1#1 = #0#1;    || a¤o
|LEE 101#1=;
    FEstado = FSalida;
|SI param ! 2;
    |SI festa ! 0;
        #1#3 = #1#3 + #2#5;       || total apertura
    |FINSI;
        #1#4 = #1#4 + #2#6;       || total debe
        #1#5 = #1#5 + #2#7;       || total haber
        #1#6 = #1#6 + #2#8;       || saldo
    |SI festa ! 0;
        #1#7 = #1#7 + #2#9;       || saldo a¤o anterior
    |FINSI;
|FINSI;
|SI param = 2;
        #1#7 = #1#7 + (#0#5 - #0#6);    || saldo a¤o anterior
    |SI grupo ! "6";|Y grupo ! "7";
            #1#3 = #1#3 + (#0#5 - #0#6);    || total apertura
    |FINSI;
|FINSI;
|SI FEstado = 0;   |GRABA 020#1a;  |FINSI;
|SI FEstado ! 0;   |GRABA 020#1n;  |FINSI;
|LIBERA #1;
|FCAL;

|CALCULO si_dos;
  |SELECT #2#2;
  #2#0 = #0#0;         || empresa
  #2#1 = #0#1;         || a¤o
  #2#3 = #0#2;         || cuenta
  |LEE 101#2=;
  FEstado = FSalida;
  |SI FEstado = 0;
      #2#9 = #0#5 - #0#6;  || saldo a¤o anterior
      #2#5 = 0;
      |SI grupo ! "6";|Y grupo ! "7";
          #2#5 = #2#9;         || apertura
      |FINSI;
      |SI #2#5 ! 0;
          |SI #2#5 > 0;
              #2#6 = #2#6 - #2#5;
        |SINO;
                #2#7 = #2#7 + #2#5;
        |FINSI;
    |FINSI;
    |GRABA 020#2a;
        FEstado = FSalida;
|FINSI;
|LIBERA #2;
|FCAL;

|CALCULO no_dos;
  |SELECT #2#2;
  #2#0 = #0#0;         || empresa
  #2#1 = #0#1;         || a¤o
  #2#3 = #0#2;         || cuenta
  |LEE 000#2=;
  festa = FSalida;

  |SELECT #1#2;
  |SI festa = 0;
      |LEE 121#2=;
      linea = #2#2;
  |SINO;
      ultima = ultima + 1;
      linea = ultima;
  |FINSI;

  #2#0 = #0#0;         || empresa
  #2#1 = #0#1;         || a¤o
  #2#2 = linea;        || registro
  #2#3 = #0#2;         || cuenta
  #2#4 = #0#3;         || titulo
  |SI grupo ! "6";|Y grupo ! "7";|Y festa ! 0;
      #2#5 = #0#4;     || apertura
  |FINSI;
  |SI param ! 4;
      |SI #0#5 ! 0;  #0#5 = #0#5 - #2#5;      |FINSI;  || le resta la apertura
      |SI #0#6 ! 0;  #0#6 = -#0#6 - #2#5;     #0#6 = -#0#6;  |FINSI;
      |SI #0#5 < 0;
          #0#6 = -#0#5;  #0#5 = 0;
      |SINO;
          |SI #0#6 < 0;
              #0#5 = -#0#6;  #0#6 = 0;
          |FINSI;
      |FINSI;
      #2#6 = #0#5;         || debe
      #2#7 = #0#6;         || haber
      #2#8 = #2#5 + #2#6 - #2#7;  || saldo
  |SINO;
      |SI #0#5 ! 0;
          #2#8 = #0#5;
      |FINSI;
      |SI #0#6 ! 0;
          #2#8 = -#0#6;
      |FINSI;
      saldillo = #2#5- #2#8;
      |SI saldillo < 0;
          #2#6 = - saldillo;
      |SINO;
          #2#7 = saldillo;
      |FINSI;
  |FINSI;
  |SI festa ! 0;
      #2#9 = #0#7;     || saldo a¤o anterior
  |FINSI;
  |SI festa = 0; |GRABA 020#2a; |FINSI;
  |SI festa ! 0; |GRABA 020#2n; |FINSI;
  FEstado = FSalida;
  |LIBERA #2;
|FCAL;

|CALCULO ultima;
  #2#0 = #0#0;
  #2#1 = #0#1;
  #2#2 = 9999;
  |LEE 000#2];
  |SI FSalida = 0;
      |LEE 000#2a;
  |SINO;
       |LEE 000#2u;
  |FINSI;
  |SI FSalida ! 0;|O #2#0 ! #0#0;|O #2#1 ! #0#1;
      ultima = 0;
  |SINO;
      ultima = #2#2;
  |FINSI;
|FCAL;

|CALCULO lee_traba1;
  |SI param = 3; |Y sw_ultima = 0;
      sw_ultima = 1;
      |HAZ ultima;
  |FINSI;

  grupo = #0#2 % 101;       || carga el grupo contable
  |DDEFECTO #2;
  |SI param ! 2;
      |HAZ no_dos;
  |SINO;
      |HAZ si_dos;
  |FINSI;
  |SI FEstado = 0;  |HAZ cabs;  |FINSI;
  |ATRI I;
  |PINTA 2221, #2#3;
  |PINTA 2226, #2#4;
  |ATRI 0;
|FCAL;

|DEFBUCLE lee_traba;
 #0#1;
 ;
  00000,    0, "            ";
  99999, 9999, "zzzzzzzzzzzz";
 e;
 NULL, lee_traba1;
|FIN;

|PROGRAMA;
  |ABRET #1;
  |ABRET #2;
  |SI param ! 2;|Y param ! 3; |HAZ borra;  |FINSI;
  |BUCLE lee_traba;
  |CIERRAT #1;
  |CIERRAT #2;
|FPRO;
