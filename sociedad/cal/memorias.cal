|FICHEROS;
    memorias #0;            || Fichero emision
    meempmem #1;            || Fichero empresas memoria
    metextos #3;            || Fichero de Textos
    meestrca #4;            || Cabecera de Estructuras
    meestrli #5;            || Lineas de Estructuras
    sodacoli #6;            || Lineas Entrada de Contabilidad
    soplanli #7;            || Lineas Planillas
    sodacuen #8;
    medescri #10;           || Descripciones claves

    mem5inma #11;
    mem5mate #12;

    mem5fina #13;
    mem8cabe #14;
    mem8line #15;
    mem9gtos #16;
    mem8ltmp #17;
    mem10amb #110;
    maempr01 #18;

     isw00016 #100;
     agicontr #33;  || Control
     agifigen #44;  || Ficha de Clientes
     agicen01 #45;  || Censo
     agim0028 #28;  || Ficha de Sociedades Cabeceras
     agim0019 #19;  || Ficha de Sociedades Lineas
     agim0024 #24;  || Representantes/Administradores
     agim0025 #25;  || Socios
     agim0026 #26;  || Cargos
|FIN;

|VARIABLES;
     &enPlan        = 0;
     &eaPlanillaS   = "";
     &enEjerS       = 0;

     nSwMedAmb      = 0;
     PathCliente    = "";
     PathSociedad   = "";
     PathFichAgicli = "";
     Valfa          = "";
     Contador = 0;

     &blanco40 = "                                        ";
     &blanco30 = "                              ";
     &variable = "";
     &alfa     = "";
     &alfa1    = "";
     &hoja     = 0;
     &sw_print = 0;
     &acabalo  = 0;
     &eImpresora = "";

     XPara    = 0;
     XPara1   = 0;
     XPara2   = 0;
     informe  = "";
     regi     = 0;
     fichita  = 0;
     x        = 0;
     sw_pan   = 0;
     sw_emp   = 0;
     empresa  = 0;
     sw       = 0;
     pepe     = 0;
     y        = 0;
     swmovi   = 0;
     swcuenta = 0;
     mas      = 0;
     menos    = 0;
     ini      = 0;
     total    = 0;
     swimpre  = 0;
     z        = 0;
     descri   = "";
     direct   = "";
     vari1    = "";
     vari2    = "";
     cuenta   = "";
     importe  = "";
     vari     = "";
     desde    = "";
     hasta    = "";
     numero   = "";
     asiento  = "";
     aste     = "";
     pantalla = "";
     pantalla1 = "";
     sw_infor = 0;
     linea = 0;
     campo = 0;
     tip1 = "";
     fichero = "";
     ultlin = 0;
     codtexto = 0;
     existemp = 0;
     desde_l  = 0;
     sw_final = 0;
     importe1 = "";
     importe2 = "";
     importe3 = "";
     importe4 = "";
     len      = "";
     leng     = 0;
     nSuma    = 0;
     aAlfa    = "";

     &eaMoneda    = "";
     &enCanti     = 0;
     &enSwTrata   = 0;
     &eaImporte   = "";
     &eaDesMoneda = "";
     &EURODB      = 0;
|FIN;

|PROCESO nomodi; |TIPO 7;
  |CAMPO_MODIFICA #0#11, 1, "S";
  |SI #0#10 = "N";
      |CAMPO_MODIFICA #0#11, 1, "N";
  |FINSI;
|FPRC;

|PROCESO mayor; |TIPO 0;
  #0Campo = #0Campo > #0Campo;
  |PINTA #0Campo;
|FPRC;

|PROCESO MiraVarMoneda;  |TIPO 7;
     |HAZ PintaMoneda;
     #0#17 = eaMoneda;

     |PINTA #0#17;

     eaDesMoneda = "Euros";
     |SI #0#17 = "PESETAS";  eaDesMoneda = "Pesetas";  |FINSI;
|FPRC;

|| *************************************************************************
||                         IMPRESION FICHA DE SOCIEDADES
|| *************************************************************************

|PROCESO Actividades;
  Contador = Contador + 1;
  |SI Contador = 1;
      #100#9  = #45#5;                                   || Fecha  Actividad
      #100#10 = #45#4;                                   || Nombre Actividad
      #100#11 = #45#2 + " " + #45#3;                     || Epigrafe
  |FINSI;

  |SI Contador = 2;
      #100#12 = #45#5;                                   || Fecha  Actividad
      #100#13 = #45#4;                                   || Nombre Actividad
      #100#14 = #45#2 + " " + #45#3;                     || Epigrafe
  |FINSI;
|FPRC;

|DEFBUCLE agicen01;
  #45#1;
  ;
  #19#0, 00;
  #19#0, 99;
  ;
  NULL, Actividades;
|FIN;

|PROCESO Socios;
  Contador = Contador + 1;  #100Contador = #25#4;        || Nombre
  Contador = Contador + 1;  #100Contador = #25#3;        || NIF
  Contador = Contador + 1;  #100Contador = #25#14;       || Numero
  Contador = Contador + 1;  #100Contador = #25#13;       || % Participacion

  |SI Contador = 59;  |ERROR10;  |FINSI;
|FPRC;

|DEFBUCLE agim0025;
  #25#1;
  ;
  #19#0, #19#1, 00;
  #19#0, #19#1, 99;
  ;
  NULL, Socios;
|FIN;

|PROCESO Cargos;
  |SI #26#16 = 1;
      #100#87 = #26#4;
      #100#88 = (#26#7 * 1000) + #26#8;
      #100#89 = #26#6;
      #100#90 = #26#5;
      #100#91 = #26#3;
      #100#92 = #26#12;
      #100#93 = #26#14;
      #100#94 = #26#15
  |FINSI;

  |SI #26#16 = 2;
      #100#95  = #26#4;
      #100#96  = #26#3;
      #100#97  = #26#12;
      #100#98  = #26#5;
      #100#99  = #26#6;
      #100#100 = (#26#7 * 1000) + #26#8;
      #100#101 = #26#14;
      #100#102 = #26#15
  |FINSI;

  |SI #26#16 = 3;
      #100#103 = #26#4;
      #100#104 = #26#3;
      #100#105 = #26#12;
      #100#106 = #26#5;
      #100#107 = #26#6;
      #100#108 = (#26#7 * 1000) + #26#8;
      #100#109 = #26#14;
      #100#110 = #26#15
  |FINSI;

  |SI #26#16 = 4;
      #100#111 = #26#4;
      #100#112 = #26#3;
      #100#113 = #26#12;
      #100#114 = #26#5;
      #100#115 = #26#6;
      #100#116 = (#26#7 * 1000) + #26#8;
      #100#117 = #26#14;
      #100#118 = #26#15
  |FINSI;

  |SI #26#16 = 5;
      #100#119 = #26#4;
      #100#120 = #26#3;
      #100#121 = #26#12;
      #100#122 = #26#5;
      #100#123 = #26#6;
      #100#124 = (#26#7 * 1000) + #26#8;
      #100#125 = #26#14;
      #100#126 = #26#15
  |FINSI;

  |SI #26#16 = 6;
      #100#127 = #26#4;
      #100#128 = #26#3;
      #100#129 = #26#12;
      #100#130 = #26#5;
      #100#131 = #26#6;
      #100#132 = (#26#7 * 1000) + #26#8;
      #100#133 = #26#14;
      #100#134 = #26#15
  |FINSI;

  |SI #26#16 = 7;
      Valfa = #100#60;  |QBT Valfa;
      |SI Valfa = "";
          #100#60 = #26#4;
          #100#61 = #26#13;
          #100#62 = #26#14;
          #100#63 = #26#15;
          |ACABA;
      |FINSI;

      Valfa = #100#64;  |QBT Valfa;
      |SI Valfa = "";
          #100#64 = #26#4;
          #100#65 = #26#13;
          #100#66 = #26#14;
          #100#67 = #26#15;
          |ACABA;
      |FINSI;

      Valfa = #100#68;  |QBT Valfa;
      |SI Valfa = "";
          #100#68 = #26#4;
          #100#69 = #26#13;
          #100#70 = #26#14;
          #100#71 = #26#15;
          |ACABA;
      |FINSI;
  |FINSI;

  |SI #26#16 = 8;
      #100#72 = #26#4;
      #100#73 = #26#14;
      #100#74 = #26#15;
      |ACABA;
  |FINSI;

  |SI #26#16 = 9;
      Valfa = #100#75;  |QBT Valfa;
      |SI Valfa = "";
          #100#75 = #26#4;
          #100#76 = #26#13;
          #100#77 = #26#14;
          #100#78 = #26#15;
          |ACABA;
      |FINSI;

      Valfa = #100#79;  |QBT Valfa;
      |SI Valfa = "";
          #100#79 = #26#4;
          #100#80 = #26#13;
          #100#81 = #26#14;
          #100#82 = #26#15;
          |ACABA;
      |FINSI;

      Valfa = #100#83;  |QBT Valfa;
      |SI Valfa = "";
          #100#83 = #26#4;
          #100#84 = #26#13;
          #100#85 = #26#14;
          #100#86 = #26#15;
          |ACABA;
      |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE agim0026;
  #26#1;
  ;
  #19#0, #19#1, 00;
  #19#0, #19#1, 99;
  ;
  NULL, Cargos;
|FIN;

|PROCESO CargaFicha;
  |ABRE #44;
  #44#0 = #0x;
  |LEE 040#44=;
  |SI FSalida ! 0;  |DDEFECTO #44;  |FINSI;
  |CIERRA #44;

  |ABRE #28;
  #28#0 = #0x;
  |LEE 040#28=;
  |SI FSalida ! 0;  |DDEFECTO #28;  |FINSI;
  |CIERRA #28;

  |ABRE #19;
  #19#0 = #0x;
  #19#1 = 99;
  |LEE 040#19];
  |SI FSalida = 0;
      |LEE 040#19a;
  |SINO;
      |LEE 040#19u;
  |FINSI;
  |SI FSalida ! 0;  |O #19#0 ! #0x;
      |DDEFECTO #19;
  |FINSI;
  |CIERRA #19;

  #100#0   = #19#0;                                     || Empresa
  #100#1   = #19#1;                                     || Registro
  #100#2   = #28#2;                                     || Nombre Empresa
  #100#3   = #28#3;                                     || CIF

  aAlfa  = #44#3;  |QBF aAlfa;
  |SI aAlfa ! "";
      aAlfa  = aAlfa + " " + #44#4;
  |SINO;
      aAlfa  = #44#4;
  |FINSI;
  |QBF aAlfa;

  |SI #44#5 ! "    ";
      aAlfa = aAlfa + " Num: " + #44#5;
  |FINSI;
  |QBF aAlfa;

  |SI #44#6 ! "   ";
      aAlfa = aAlfa + " Esc: " + #44#6;
  |FINSI;
  |QBF aAlfa;

  |SI #44#7 ! "  ";
      aAlfa = aAlfa + " Piso: " + #44#7;
  |FINSI;
  |QBF aAlfa;

  |SI #44#8 ! "  ";
      aAlfa = aAlfa + " Pta: " + #44#8;
  |FINSI;
  |QBF aAlfa;

  #100#4   = aAlfa;                                     || Domicilio

  #100#5   = #44#11;                                    || Poblacion
  #100#6   = #28#5;                                     || Telefono
  #100#7   = #19#11;                                    || Constitucion
  #100#8   = #19#8;                                     || Notaria
  #100#15  = #19#29;                                    || Tomo
  #100#16  = #19#30;                                    || Libro
  #100#17  = #19#31;                                    || Seccion
  #100#18  = #19#32;                                    || Folio
  #100#19  = #19#33;                                    || Hoja
  #100#20  = #19#34;                                    || Inscripcion

  #100#21  = #19#14;                                    || Capital Social
  #100#23  = #19#16;
  #100#27  = #19#20;
  #100#31  = #19#24;

  |SI EURODB = 0;  |Y #19#51 = "E";
      #100#21  = (#19#14 * 166.386) ! EURODB;           || Capital Social
      #100#23  = (#19#16 * 166.386) ! EURODB;
      #100#27  = (#19#20 * 166.386) ! EURODB;
      #100#31  = (#19#24 * 166.386) ! EURODB;
  |FINSI;

  |SI EURODB ! 0;  |Y #19#51 = "P";
      #100#21  = (#19#14 / 166.386) ! EURODB;           || Capital Social
      #100#23  = (#19#16 / 166.386) ! EURODB;
      #100#27  = (#19#20 / 166.386) ! EURODB;
      #100#31  = (#19#24 / 166.386) ! EURODB;
  |FINSI;

  #100#22  = #19#15;
  #100#24  = #19#17;
  #100#25  = #19#18;
  #100#26  = #19#19;
  #100#28  = #19#21;
  #100#29  = #19#22;
  #100#30  = #19#23;
  #100#153 = (#44#9  * 1000) + #44#10;                  || Codigo Postal
  #100#154 = (#44#63 * 1000) + #44#64;                  || Codigo Hacienda
  #100#159 = #19#28;                                    || Fecha Inscrip.
  #100#160 = #19#27;                                    || Localidad Registr.

  Contador = 0;  |BUCLE agicen01;
  |SI Contador = 0;
      #100#9  = #19#7;                                   || Fecha  Actividad
      #100#10 = #19#48;                                  || Nombre Actividad
      #100#11 = #19#46 + " " + #19#47;                   || Epigrafe
      #100#12 = "";                                      || Fecha  Actividad
      #100#13 = "";                                      || Nombre Actividad
      #100#14 = "";                                      || Epigrafe
  |FINSI;

  Contador = 31; |BUCLE agim0025;
  Contador = 0;  |BUCLE agim0026;
|FPRC;

|PROCESO impreficha;
  |SI #0#13 = "S";  |INFOR "memori01";  |FINSI;
  |SI #0#13 = "N";  |INFOR "memori81";  |FINSI;

  |SI FSalida = 0;
      |HAZ CargaFicha;
      |PRINT;
      |PIEINF;
      |FININF;
  |FINSI;
|FPRC;

|| *************************************************************************
||                         IMPRESION DE CUENTAS
|| *************************************************************************

|PROCESO impremovi;
  |SI swcuenta = 0;
      |SI sw_final = 1;
          |ABRE #8;
          #8#0 = numero;
          #8#2 = enPlan;
          |LEE 040#8=;
          |SI FSalida ! 0; |DDEFECTO #8; |FINSI;
          |CIERRA #8;
          cuenta   = numero;
          descri   = #8#1;
      |SINO;
          cuenta   = #6#3;
          descri   = #6#4;
      |FINSI;

      swimpre  = 1;
      swcuenta = 1;
      |SI swmovi = 0;
          variable = ""; |PRINT; |PRINT;
          variable = "                         SITUACIONES DE LAS CUENTAS ";
          |PRINT;
          variable = "                         -------------------------- ";
          |PRINT;
          variable = ""; |PRINT;
          swmovi   = 1;
      |FINSI;
  |FINSI;

  |SI cuenta ! #6#3; |Y sw_final = 0;
      variable = "       " + cuenta + "  " + descri;
      |PRINT;
      variable = "              S.Inicial       Aumentos  Disminuciones        S.Final";
      |PRINT;
      variable = "           --------------------------------------------------------";
      |PRINT;
      importe = mas;   |HAZ importe; importe1 = importe;
      importe = menos; |HAZ importe; importe2 = importe;
      importe = ini;   |HAZ importe; importe3 = importe;
      total = (mas + ini) - menos;
      importe = total; |HAZ importe; importe4 = importe;
      variable = "          " + importe3 + "  " + importe1 + "  " + importe2 + "  " + importe4;
      |PRINT;
      mas = 0;  menos = 0;  ini = 0;  total = 0;
      variable = ""; |PRINT; |PRINT;
      cuenta   = #6#3;
      descri   = #6#4;
  |FINSI;

  |SI #7#8 = "D";
      mas   = mas   + #6#6;
      menos = menos + #6#7;
      ini   = ini   + #6#5;
  |SINO;
      mas   = mas   + #6#7;
      menos = menos + #6#6;
      ini   = ini   + -#6#5;
  |FINSI;
|FPRC;

|DEFBUCLE cuentas;
  #6#2;                    || Bucle de lineas de movimientos
  ;
  #1#0, #1#5, desde;
  #1#0, #1#5, hasta;
  be;
  NULL, impremovi;
|FIN;

|PROCESO cuenlin;
  swcuenta = 0;
  vari   = #7#2; |QBF vari;
  aste   = vari % -101;
  len    = vari % 0;
  leng   = len - 1;
  leng   = 100 + leng;
  numero = vari % leng;
  sw_final = 0;

  |SI aste = "*"; |O aste = ".";
      |SI aste = "."; sw_final = 1; |FINSI;
      desde = numero + "            ";
      desde = desde % 112;
      hasta = numero + "zzzzzzzzzzzz";
      hasta = hasta % 112;
      |BUCLE cuentas;
  |SINO;
      desde = #7#2;
      hasta = #7#2;
      |BUCLE cuentas;
  |FINSI;

  |SI swimpre = 1;
      variable = "       " + cuenta + "  " + descri;
      |PRINT;
      variable = "              S.Inicial       Aumentos  Disminuciones        S.Final";
      |PRINT;
      variable = "           --------------------------------------------------------";
      |PRINT;
      importe = mas;   |HAZ importe; importe1 = importe;
      importe = menos; |HAZ importe; importe2 = importe;
      importe = ini;   |HAZ importe; importe3 = importe;
      total = (mas + ini) - menos;
      importe = total; |HAZ importe; importe4 = importe;
      variable = "          " + importe3 + "  " + importe1 + "  " + importe2 + "  " + importe4;
      |PRINT;
      mas = 0;  menos = 0;  ini = 0;  total = 0;
      swimpre = 0;
      variable = ""; |PRINT; |PRINT;
  |FINSI;
|FPRC;

|DEFBUCLE planilla;
  #7#2;
  ;
  #1#4, pepe;
  #1#4, pepe;
  e;
  NULL, cuenlin;
|FIN;

|PROCESO movimi;
  |BUCLE planilla;
  swmovi = 0;
|FPRC;

|| *************************************************************************
||                         IMPRESION DE TEXTOS
|| *************************************************************************

|PROCESO print1;
  |SI nSwMedAmb = 0; |Y #3#0 = 100100; |ACABA; |FINSI;
  |SI nSwMedAmb = 1; |Y #3#0 = 100000; |ACABA; |FINSI;

  |SI sw_pan = 0;
      sw_pan = 1;
      pantalla = #3#0 + #3#1;
  |FINSI;

  |ABRE #10;
  #10#0 = #3#0;
  |LEE 040#10=;
  |SI FSalida ! 0; |DDEFECTO #10; |FINSI;

  pantalla1 = #3#0 + #3#1;
  |SI pantalla ! pantalla1;
      sw_emp = 0;
      pantalla = pantalla1;
  |FINSI;

  |SI #3#17 = 99999;
          |SI #3#33 ! "        ";
              informe = #3#33;
              |SI informe ! "memori07";
                  |HAZ inf_normal;
              |SINO;
                  |HAZ empregrup;
              |FINSI;
          |FINSI;

      |ACABA;
  |FINSI;

  |SI #3#17 = empresa; sw_emp = 1; |VETE impresio; |FINSI;
  |SI #3#17 = 0;                   |VETE impresio; |FINSI;

  |SI #3#17 = 99998; |Y sw_emp = 0;
      |VETE impresio;
  |SINO;
      |ACABA;

  |FINSI;

  |ET impresio;

  |INFOR "memori02";

  sw_infor = 1;

  |SI sw = 0;
     alfa = #3#0;             || Recoge los dos primeros digitos del primer
     alfa = "000000" + #3#0;  || registro para compararlos con sucesivos
     alfa = alfa % -502;      || registros
     sw = 1;
     pepe =  #3#0;
     variable = "                    " + #10#1; |QBF variable;
     |SI variable ! "";
         sw_print = 1;  |PRINT;
         sw_print = 0;
     |FINSI;
  |FINSI;

  alfa1 = #3#0;            || Recoge los dos primeros digitos de la clave;
  alfa1 = "000000" + #3#0; || de la clave actual
  alfa1 = alfa1 % -502;

  |SI #3#33 = "aumeydis";  |Y #1#4 = 10;
      alfa1 = "";
  |FINSI;

  |SI alfa ! alfa1;
      |HAZ movimi;          || Rutina para comprobar si hay o no movimientos

      FSalida = "PAGINA";      || salto de pagina
      pepe = #3#0;

     variable = "                    " + #10#1; |QBF variable;
     |SI variable ! "";
         sw_print = 1;  |PRINT;
         sw_print = 0;
     |FINSI;
  |SINO;
      |SI pepe ! #3#0;
          |HAZ movimi;          || Rutina para comprobar si hay o no movimientos
          variable = ""; |PRINT; |PRINT; |PRINT;
          pepe = #3#0;

          variable = "                    " + #10#1; |QBF variable;
          |SI variable ! "";
              sw_print = 1;  |PRINT;
              sw_print = 0;
          |FINSI;
      |FINSI;
  |FINSI;

  |PARA y = 2; |SI y < 17; |HACIENDO y = y + 1;
        variable = #3y;
        |QBF variable;
        |SI variable ! "";
            |SI variable = "."; variable = ""; |FINSI;
            |PRINT;
        |FINSI;
        alfa = alfa1;            || Si los registros son diferentes
  |SIGUE;

  |SI #3#0 = 060000;  |Y #3#1 = 0;
      |HAZ cero6;             || Imprime datos pantalla 07.00.00
  |FINSI;

  |SI #3#33 = "aumeydis";
      |HAZ aumeydis;
  |FINSI;

  |SI #3#33 = "otrainfo";
      |HAZ otrainfo;
  |FINSI;
|FPRC;

|PROCESO imprelin;
  |PARA campo = 0; |SI campo < 12; |HACIENDO campo = campo + 1;
        #15campo = #17campo;
  |SIGUE;
  |SI #17#3 = "X"; #15#3 = " "; |FINSI;
  |SI informe = "memori07";
      |PRINT;
  |SINO;
      |SI #17#3 = "M"; |DDEFECTO #15; |FINSI;
      |PRINT;
  |FINSI;
|FPRC;

|DEFBUCLE temporal;
  #17#1;
  ;
  #14#0, #14#1, "A",   0;
  #14#0, #14#1, "X", 999;
  e;
  NULL,imprelin;
|FIN;

|PROCESO inforgrup;
      informe = "memori07";

  |INFOR informe;
  |SI FSalida ! 0; |ACABA; |FINSI;

  |BUCLE temporal;
  |PIEINF;
  |FININF;

  |SI tip1 = "M"; |ACABA; |FINSI;

  informe = "memori08";

  |INFOR informe;
  |SI FSalida ! 0; |ACABA; |FINSI;
  |BUCLE temporal;
  |PIEINF;
  |FININF;

  linea = 0;
  |DELINDEX #17;
|FPRC;

|PROCESO crea_tmp;
  |DDEFECTO #17;
  |PARA campo = 0; |SI campo < 12; |HACIENDO campo = campo + 1;
        #17campo = #15campo;
  |SIGUE;
  |GRABA 020#17n;
  ultlin = #17#2;
  linea = linea + 1;
  |SI linea = 1; tip1 = #17#3; |FINSI;
  |SI linea = 14; |HAZ inforgrup; |FINSI;
|FPRC;

|DEFBUCLE emprgras;
  #15#2;
  ;
  #14#0, #14#1, "A",   1;
  #14#0, #14#1, "M", 999;
  e;
  NULL,crea_tmp;
|FIN;

|PROCESO empregrup;
  |SI sw_infor = 1;
      sw_infor = 0;
      |PIEINF;
      |FININF;
  |FINSI;

  |ABRE #17;
  |BUCLE emprgras;
  |SI linea = 0; |CIERRA #17; |ACABA; |FINSI;

  desde_l = 14;

  |PARA campo = linea; |SI campo < desde_l; |HACIENDO campo = campo + 1;
        ultlin = ultlin + 1;
        |DDEFECTO #17;
        #17#0 = #14#0;
        #17#1 = #14#1;
        #17#2 = ultlin;
        #17#3 = "X";
        |GRABA 020#17n;
  |SIGUE;

  |CIERRA #17;
  |HAZ inforgrup;
|FPRC;

|DEFBUCLE pantatext;
  #3#1;
  ;
  #5#2, #5#3, 00000;               ||  Bucle de pantalla de textos
  #5#4, #5#5, 99999;
  be;
  NULL, print1;
|FIN;

|PROCESO texto;
  |BUCLE pantatext;                  || Bucle de pantallas de textos
|FPRC;

|PROCESO inf_normal;
  |SI sw_infor = 1;
      sw_infor = 0;
      |PIEINF;
      |FININF;
  |FINSI;
  |INFOR informe;
  |SI FSalida = 0;
      |PRINT;
      |PIEINF;
      |FININF;
  |FINSI;
|FPRC;

|PROCESO impretexto;
  sw_infor = 0;

  |ABRE #1;
  |ABRE #4;

  #1#0 = #0x;
  |LEE 040#1=;           || Lee empresa memoria para saber la estructura
  |SI FSalida = 0;
      |HAZ lee_memabr;
  |FINSI;

  enPlan = 2008;
  |SI #1#5 < 2008; enPlan = 1990; |FINSI;

  #4#0 = #1#2;
  |LEE 040#4=;           || Lee la cabecera de estructuras

  |BUCLELIN 0texto#4;    || Bucle de lineas de estructuras
  |SI sw_infor = 1;
      |PIEINF;
      |FININF;
  |FINSI;
  |CIERRA #1;
  |CIERRA #4;

  |CIERRA #11;
  |CIERRA #12;
  |CIERRA #13;
  |CIERRA #14;
  |CIERRA #16;
  |CIERRA #110;
|FPRC;

|PROCESO lee_memabr;
  nSwMedAmb = 0;
  |ABRE #3;
  |ABRE #11;
  |ABRE #12;
  |ABRE #13;
  |ABRE #14;
  |ABRE #16;
  |ABRE #110;

  |ABRE #18;
  #18#0 = #1#0;
  #18#1 = #1#5;
  |LEE 040#18=;
  |SI FSalida ! 0;
      |DDEFECTO #18;
      #18#0 = #1#0;
      #18#1 = #1#5;
  |FINSI;
  existemp = FSalida;
  |CIERRA #18;

  #11#0 = #1#0;
  #11#1 = #1#5;
  |LEE 040#11=;
  |SI FSalida ! 0;
      |DDEFECTO #11;
      #11#0 = #1#0;
      #11#1 = #1#5;
      #11#20 = -1;
  |SINO;
      |SI existemp ! 0;
          #18#0 = #11#0;
          #18#1 = #11#1;
          #18#2 = #11#2;
          #18#3 = #11#3;
      |FINSI;
      codtexto = #11#20;
      |HAZ graba_texto;
  |FINSI;

  #12#0 = #1#0;
  #12#1 = #1#5;
  |LEE 040#12=;
  |SI FSalida ! 0;
      |DDEFECTO #12;
      #12#0 = #1#0;
      #12#1 = #1#5;
      #12#16 = -1;
  |SINO;
      |SI existemp ! 0;
          #18#0 = #12#0;
          #18#1 = #12#1;
          #18#2 = #12#2;
          #18#3 = #12#3;
      |FINSI;
      codtexto = #12#16;
      |HAZ graba_texto;
  |FINSI;

  #13#0 = #1#0;
  #13#1 = #1#5;
  |LEE 040#13=;
  |SI FSalida ! 0;
      |DDEFECTO #13;
      #13#0 = #1#0;
      #13#1 = #1#5;
      #13#16 = -1;
  |SINO;
      |SI existemp ! 0;
          #18#0 = #13#0;
          #18#1 = #13#1;
          #18#2 = #13#2;
          #18#3 = #13#3;
      |FINSI;
      codtexto = #13#16;
      |HAZ graba_texto;
  |FINSI;

  #14#0 = #1#0;
  #14#1 = #1#5;
  |LEE 040#14=;
  |SI FSalida ! 0;
      |DDEFECTO #14;
      #14#0 = #1#0;
      #14#1 = #1#5;
      #14#4  = -1;
  |SINO;
      |SI existemp ! 0;
          #18#0 = #14#0;
          #18#1 = #14#1;
          #18#2 = #14#2;
          #18#3 = #14#3;
      |FINSI;
      codtexto = #14#4;
      |HAZ graba_texto;
  |FINSI;

  #16#0 = #1#0;
  #16#1 = #1#5;
  |LEE 040#16=;
  |SI FSalida ! 0;
      |DDEFECTO #16;
      #16#0 = #1#0;
      #16#1 = #1#5;
      #16#22 = -1;
  |SINO;
      |SI existemp ! 0;
          #18#0 = #16#0;
          #18#1 = #16#1;
          #18#2 = #16#2;
          #18#3 = #16#3;
      |FINSI;
      codtexto = #16#22;
      |HAZ graba_texto;
  |FINSI;

  #110#0 = #1#0;
  #110#1 = #1#5;
  |LEE 040#110=;
  |SI FSalida ! 0;
      |DDEFECTO #110;
      #110#0 = #1#0;
      #110#1 = #1#5;
      #110#28 = -1;
      nSwMedAmb = 0;
  |SINO;
      nSwMedAmb = 1;
      |SI existemp ! 0;
          #18#0 = #110#0;
          #18#1 = #110#1;
          #18#2 = #110#2;
          #18#3 = #110#3;
      |FINSI;
      codtexto = #110#28;
      |HAZ graba_texto;
  |FINSI;
  |CIERRA #3;
|FPRC;

|PROCESO graba_texto;
  #3#0  = codtexto;
  #3#1  = 00;
  #3#17 = 99999;
  |LEE 040#3=;
  |SI FSalida ! 0; |GRABA 040#3n; |FINSI;

  #3#0  = codtexto;
  #3#1  = 00;
  #3#17 = 99999;
  |LEE 040#3=;
  |SI FSalida ! 0; |GRABA 040#3n; |FINSI;
|FPRC;

|| *************************************************************************
||                         IMPRESION PANTALLA DE MOMORIA
|| *************************************************************************

|PROCESO cero6;
  |PARA z = 20; |SI z [ 34; |HACIENDO z = z + 1;
        variable = #1z; |QBF variable;
        |SI variable ! "";
           |PRINT;
        |FINSI;
  |SIGUE;
  variable = ""; |PRINT; |PRINT; |PRINT;
|FPRC;

|PROCESO aumeydis;
  nSuma    = 0;
  Contador = 0;
  |PARA XPara = 35; |SI XPara [ 59; |HACIENDO XPara = XPara + 1;
        |SI #1XPara ! "                                     ";
            XPara1   = XPara  + 25 + Contador;
            XPara2   = XPara1 + 1;
            nSuma    = nSuma + #1XPara1 + #1XPara2;
        |FINSI;
        Contador = Contador + 1;
  |SIGUE;

  Contador = 0;
  |PARA XPara = 153; |SI XPara [ 167; |HACIENDO XPara = XPara + 1;
        |SI #1XPara ! "                                     ";
            XPara1   = XPara  + 15 + Contador;
            XPara2   = XPara1 + 1;
            nSuma    = nSuma + #1XPara1 + #1XPara2;
        |FINSI;
        Contador = Contador + 1;
  |SIGUE;

  |SI nSuma = 0;  |ACABA;  |FINSI;

  variable = ""; |PRINT;

  variable = "                                              AUMENTOS  DISMINUCIONES";
  |PRINT;

  variable = "                                        -------------- --------------";
  |PRINT;

  variable = "";
  |PRINT;

  Contador = 0;
  |PARA XPara = 35; |SI XPara [ 59; |HACIENDO XPara = XPara + 1;
        |SI #1XPara ! "                                     ";
            XPara1   = XPara  + 25 + Contador;
            XPara2   = XPara1 + 1;
            importe  = #1XPara1; |HAZ importe; importe1 = importe;
            importe  = #1XPara2; |HAZ importe; importe2 = importe;
            |SI #1XPara1 = 0; importe1 = "             ";  |FINSI;
            |SI #1XPara2 = 0; importe2 = "             ";  |FINSI;
            variable = #1XPara + "    " + importe1 + "  " + importe2;
            |PRINT;
        |FINSI;

        Contador = Contador + 1;
   |SIGUE;

  Contador = 0;
  |PARA XPara = 153; |SI XPara [ 167; |HACIENDO XPara = XPara + 1;
        |SI #1XPara ! "                                     ";
            XPara1   = XPara  + 15 + Contador;
            XPara2   = XPara1 + 1;
            importe  = #1XPara1; |HAZ importe; importe1 = importe;
            importe  = #1XPara2; |HAZ importe; importe2 = importe;
            |SI #1XPara1 = 0; importe1 = "             ";  |FINSI;
            |SI #1XPara2 = 0; importe2 = "             ";  |FINSI;
            variable = #1XPara + "    " + importe1 + "  " + importe2;
            |PRINT;
        |FINSI;

        Contador = Contador + 1;
   |SIGUE;

  variable = ""; |PRINT; |PRINT; |PRINT;
|FPRC;

|PROCESO otrainfo;
  variable = ""; |PRINT; |PRINT; |PRINT;

  |PARA z = 110; |SI z [ 115; |HACIENDO z = z + 1;
        variable = #1z; |QBF variable;
        |SI variable ! "";
           |PRINT;
        |FINSI;
  |SIGUE;
  variable = ""; |PRINT; |PRINT; |PRINT;

  variable = "                      CONCEPTOS RETRIBUTIVOS     IMPORTE BRUTO";
  |PRINT;
  variable = "                      ----------------------------------------";
  |PRINT;

  importe  = #1#119; |HAZ importe;
  variable = "                      " + #1#116 + "  " + importe;  |PRINT;
  variable = "";                      |PRINT;

  importe  = #1#120; |HAZ importe;
  variable = "                      " + #1#117 + "  " + importe;  |PRINT;
  variable = "";                      |PRINT;

  importe  = #1#121; |HAZ importe;
  variable = "                      " + #1#118 + "  " + importe;  |PRINT;
  variable = "";                      |PRINT;

  importe  = #1#122; |HAZ importe;
  variable = "                      TOTAL .................... " + importe;  |PRINT;
  variable = "";                      |PRINT;

  variable = ""; |PRINT; |PRINT; |PRINT;
|FPRC;

|PROCESO importe;    || Calculo para poner las comas en el importe
     eaMoneda  = #0#17;
     enCanti   = importe;
     enSwTrata = 1;
     |HAZ SeparaImporte;
     importe   = eaImporte;
|FPRC;

|| *************************************************************************
||                         EMPIEZA EL PROGRAMA
|| *************************************************************************

|PROCESO impre;|TIPO 3;
  enEjerS = 2008; eaPlanillaS = "sodacuen";  |HAZ PlanDePlanillasS;

  |DBASS PathCliente;  |QBF PathCliente;
  |PATH_DAT #33PathCliente;

  |ABRE #33;
  |LEE 040#33p;
  |SI FSalida ! 0; aste = "*"; |CIERRA #33; |ACABA; |FINSI;
  |CIERRA #33;

  PathSociedad = #33#4;
  |QBF PathSociedad;
  |SI PathSociedad = "*";  |ACABA;  |FINSI;

  PathFichAgicli = #33#8; |QBF PathFichAgicli;

  |PATH_DAT #44 PathFichAgicli;
  |PATH_DAT #45 PathFichAgicli;

  |PATH_DAT #19 PathFichAgicli;
  |PATH_DAT #24 PathFichAgicli;
  |PATH_DAT #25 PathFichAgicli;
  |PATH_DAT #26 PathFichAgicli;
  |PATH_DAT #28 PathFichAgicli;

  |ABRE #3;
  codtexto = 030000;  |HAZ graba_texto;

  |DDEFECTO #3;
  |LEE 040#3=;
  |SI FSalida ! 0;
      #3#0 = 140000; #3#1 = 00; #3#17 = 0;
      |GRABA 040#3n;
  |FINSI;

  |DDEFECTO #3;
  #3#0 = 060000; #3#1 = 00; #3#17 = 99998;
  |LEE 040#3=;
  |SI FSalida ! 0; |GRABA 040#3n; |FINSI;

  |DDEFECTO #3;
  #3#0 = 070000; #3#1 = 00; #3#17 = 99998;
  |LEE 040#3=;
  |SI FSalida ! 0; |GRABA 040#3n; |FINSI;
  |CIERRA #3;

  |SI acabalo = 1;
      Impresora = eImpresora;
      |IMPRE -1;
  |SINO;
      |IMPRE 0;
  |FINSI;

  |SI FSalida = 0;
     |HAZ fichero;
     |NOME_DAT #17 fichero;
     |ABRE #17; |CIERRA #17; |DELINDEX #17;
     |PARA x = 0; |SI x [ 9; |HACIENDO x = x + 1;
          |SI #0x ! 0;
              empresa = #0x;
              sw_pan = 0;
              sw_emp = 0;
              hoja = #0#11;
              |DDEFECTO #100;
              |HAZ impreficha;             || Rutina para imprimir la ficha
              |HAZ impretexto;             || Rutina para imprimir texto
          |FINSI;
     |SIGUE;
     |FINIMP;
  |FINSI;

  |ABRE #3; #3#0 = 140000; #3#1 = 00; #3#17 = 0; |BORRA 140#3c; |LIBERA #3; |CIERRA #3;
|FPRC;

|PROCESO fichero;
  fichero = Usuario; |QBT fichero;
  fichero = (fichero + "zzzzzzzz") % 106;
  fichero = "me" + fichero;
|FPRC;
