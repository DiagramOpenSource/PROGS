|FICHEROS;
     isz00009 #0;
     sodacuen #1;
     sodirect #9;

     cacuenta #303;
     canempre #330;
|FIN;

|VARIABLES;
     aPath         = "";
     aPathImpuesto = "";
     nInkey        = 0;
     aBase         = "";
     aLosDefs      = "";
     aElProgr      = "";

     nDEmpresa     = 0;
     nHEmpresa     = 0;
     aEmpresa      = "";
     aAlfa1        = "";
     aAlfa2        = "";
     nCanal        = 0;

     enEmpresa     = 0;
     enPeriodo     = 0;
     aPathConta    = "";
     nEjer         = 0;
|FIN;

|| /////////////////////// PROCESOS ENTRADA POR PANTALLA \\\\\\\\\\\\\\\\\\\\
|| ****************************************************************
|RUTINA infA2;
   #330#2 = nDEmpresa;
   #330#3 = 0;
|FRUT;

|RUTINA supA2;
   #330#2 = nHEmpresa;
   #330#3 = 9;
|FRUT;

|CALCULO LeeEmpresa; |TIPO 0;
  nInkey = FSalida;

  |SI nInkey = 10;
     nDEmpresa = 0;
     nHEmpresa = 99999;
     |ABRE #330;
     |CONSULTA_F_CLAVE #1#330,infA2,supA2;
     #0#1 = #330#2; |PINTA #0#1;
     #0#2 = #330#3; |PINTA #0#2;
     |CIERRA #330;
     |PTEC 802;
     |ACABA;
  |FINSI;

  |ABRE #330;
  #330#2 = #0#1;
  #330#3 = 0;
  |LEE 011#330];
  |SI FSalida ! 0;  |O #330#2 ! #0#1;
      |MENAV "     NO Existe la empresa Contable";
      |ERROR;
  |SINO;
      #0#3 = #330#1; |PINTA #0#3;
  |FINSI;
  |CIERRA #330;
|FCAL;

|CALCULO LeeEmpresaP; |TIPO 0;
  nInkey = FSalida;
  |SI nInkey = 2; |ACABA; |FINSI;

  |SI nInkey = 10;
     nDEmpresa = #0#1;
     nHEmpresa = #0#1;
     |ABRE #330;
     |CONSULTA_F_CLAVE #1#330,infA2,supA2;
     #0#2 = #330#3; |PINTA #0#2;
     |CIERRA #330;
  |FINSI;

  |ABRE #330;
  #330#2 = #0#1;
  #330#3 = #0#2;
  |LEE 011#330=;
  |SI FSalida ! 0;
      |MENAV "     NO Existe Periodo en la Empresa Contable";
      |ERROR;
  |SINO;
      #0#3 = #330#1; |PINTA #0#3;
      |SI #330#26 > 80;
          nEjer = #330#26 + 1900;
      |SINO;
          nEjer = #330#26 + 2000;
      |FINSI;
      |SI nEjer < 2008; #0#6 = 1990; |FINSI;
      |SI nEjer ] 2008; #0#6 = 2008; |FINSI;
      |PINTA #0#6;
  |FINSI;
  |CIERRA #330;
|FCAL;

|PROCESO Actsodacuen;
 #1#0 = #303#0;
 #1#2 = #0#6;
 |LEE 001#1=;
 |SI FSalida = 0;
     #1#1 = #303#2;
     |GRABA 020#1a;
 |SINO;
     #1#0 = #303#0;
     #1#1 = #303#2;
     #1#2 = #0#6;
     |GRABA 020#1n;
 |FINSI;

 |PINTA 2009, "Cuenta: ";
 |PINTA 2017, #1#0;
 |PINTA 2030, #1#1;
|FPRC;

|DEFBUCLE Actsodacuen;
 #303#1;
 ;
 "            ", #0#4;
 "zzzzzzzzzzzz", #0#5;
 ;
 NULL, Actsodacuen;
|FIN;

|| .................................... Imprimir
|PROCESO Tipo3;  |TIPO 3;

     |CUADRO 1907 2170;

     |PATH_DAT #1 aPathImpuesto;

     enEmpresa = #0#1;
     enPeriodo = #0#2;
     |HAZ CreaPath;
     |PATH_DAT #303 aPath;

     |ABRE #1;
     |BUCLE Actsodacuen;
     |CIERRA #1;
|FPRC;

|PROCESO CreaPath;
  aAlfa1 = enEmpresa; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
  aAlfa2 = enPeriodo; |QBF aAlfa2; aAlfa2 = ("0"     + aAlfa2) % -101;
  aEmpresa = aAlfa1 + aAlfa2;
  aPath = aPathConta + aEmpresa + "/";
|FPRC;

|PROGRAMA;

  |HAZ BuscaAplicacion;
  |SI aPathConta = "";
      |ACABA;
  |FINSI;

  |CLS;
  |CABEZA "Actualizar Titulo de Cuentas en Impuesto";

  |PINPA #0#0;
  |PINDA #0#0;
  |ENDATOS #1#0;
|FPRO;

|PROCESO BuscaAplicacion;

 |ABRE #9;
 |LEE 040#9p;
 |SI FSalida ! 0;
     |CIERRA #9;
     |MENAV "    No existe directorio de Contabilidad";
     |ACABA;
 |FINSI;
 |CIERRA #9;

 aPathConta = #9#2;  |QBF aPathConta;
 |SI aPathConta ! "";
     |PATH_DAT #330 aPathConta;
 |FINSI;
|FPRC;
