|FICHEROS;
     maempx02;
     maempr02,MANTE;
     maempr10;
|FIN;

|VARIABLES;
     n0Vent         = 0;
     nRango         = 0;
     nGrid          = 0;
     nSuma          = 0;

     aAlfa          = "";
     aEsc1          = "";
     aEsc2          = "";
     aRetu          = "";
     aTecla         = "";

     {-1}aVent      = "";
         aVent1     = 0;
         aVent2     = 0;
         aVent3     = 0;
         aVent4     = 0;
         aVent5     = "";

     &enEmpresa = 0;
     &enAnyo    = 0;
     &enModoConsulta = 0;
|FIN;

|PROCESO Baja02;
     #maempr02#0  = enEmpresa;
     #maempr02#1  = enAnyo;
     #maempr02#2  = 1;
|FPRC;

|PROCESO Alta02;
     #maempr02#0  = enEmpresa;
     #maempr02#1  = enAnyo;
     #maempr02#2  = 99;
|FPRC;

|PROCESO SumaParticipacion;
     #maempr10#73 = (#maempr10#73 + #maempr02#8) ! 2;
     nSuma        = (nSuma  + #maempr02#8) ! 2;
|FPRC;

|DEFBUCLE SumaParticipacion;
     #maempr02#1;
     ;
     enEmpresa, enAnyo, 001;
     enEmpresa, enAnyo, 999;
     ;
     NULL, SumaParticipacion;
|FIN;

|PROGRAMA;
     aAlfa = "";
     |SI enModoConsulta = 1;
         aAlfa = "(MODO CONSULTA)   ";
     |FINSI;

     aAlfa = aAlfa + "Relaci¢n de socios";
     |VENTANA 0501, 3399, -1, 16, aAlfa;
     n0Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n0Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PINPA #0#maempx02;

     #maempr10#0 = enEmpresa;
     #maempr10#1 = enAnyo;
     |LEE 101#maempr10.=;
     |SI FSalida ! 0;
         |DDEFECTO #maempr10;
         #maempr10#0 = enEmpresa;
         #maempr10#1 = enAnyo;
         |GRABA 020#maempr10.b;
     |FINSI;

     |PARA;  |SI;  |HACIENDO;
          |PINTA 3385, #maempr10#73;
          |PINTA 3485, #maempr10#74;

          |SI enModoConsulta = 0;
              nRango = 1 + 2 + 4 + 8 + 16 + 32;
          |SINO;
              nRango = 4 + 8 + 16 + 32;
          |FINSI;

          |LINEAL_SIMPLE #1#maempr02, nRango, 0502, 3099, Baja02, Alta02, NULL;
          nGrid = FSalida;

          |PULSATECLA;
          aEsc1  = &255 + "806";
          aEsc2  = &255 + "807";
          aRetu  = &255 + "802";
          |PARA; |SI;  |HACIENDO;
               FSalida = "::{{001}}Salir," + nGrid;
               |LEETECLA aTecla;
               |SI aTecla = aEsc1; |SAL;  |FINSI;
               |SI aTecla = aEsc2; |SAL;  |FINSI;
               |SI aTecla = aRetu; |SAL;  |FINSI;
          |SIGUE;

          #maempr10#73 = 0; nSuma = 0;
          |BUCLE SumaParticipacion;
          |SI nSuma [ 100;
              #maempr10#73 = (100 - #maempr10#73 - #maempr10#74) ! 2; |SI #maempr10#73 < 0; #maempr10#73 = 0; |FINSI;

              |C_P #maempr10#73, 1, 3385;
              |C_P #maempr10#74, 1, 3485;

              |ENCAMPO #73#maempr10;
              |ENCAMPO #74#maempr10;
              nSuma = (nSuma + #maempr10#73 + #maempr10#74) ! 2;
          |FINSI;

          |DESTRUYECONTROL nGrid;

          |SI nSuma = 100;  |SAL;  |FINSI;

          |SI nSuma > 100;
              |MENAV "    Error. La suma de todos los porcentajes de participacion es MAYOR al 100%";
          |FINSI;

          |SI nSuma < 100;
              |CONFI "0000SLa suma de todos los porcentajes de participacion debe ser un 100%. Corregir ?";
              |SI FSalida ! 0;  |SAL;  |FINSI;
          |FINSI;
     |SIGUE;

     |GRABA 020#maempr10.a;
     |LIBERA #maempr10;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n0Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n0Vent;

     |PULSATECLA;
|FPRO;

|PROCESO Tipo80;  |TIPO 80;
     aAlfa  = PARAMETRO % 104;  enAnyo    = aAlfa;
     aAlfa  = PARAMETRO % 505;  enEmpresa = aAlfa;

     |ABRET #maempr02;
     |ABRET #maempr10;
|FPRC;

|PROCESO Tipo90;  |TIPO 90;
     |CIERRAT #maempr02;
     |CIERRAT #maempr10;
|FPRC;
