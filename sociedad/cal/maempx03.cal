|FICHEROS;
     maempr03,MANTE;
     maempr10;
|FIN;

|VARIABLES;
|FIN;

|VARIABLES;
     n0Vent         = 0;
     nRango         = 0;
     nGrid          = 0;

     aAlfa          = "";
     aEsc1          = "";
     aEsc2          = "";
     aRetu          = "";
     aTecla         = "";

     {-1}aVent      = "";
         aVent1     = 0;
         aVent2     = 0;
         aVent3     = 0;
         aVent4     = 0;
         aVent5     = "";

     &enEmpresa = 0;
     &enAnyo    = 0;
     &enModoConsulta = 0;
|FIN;

|PROCESO Baja03;
     #maempr03#0  = enEmpresa;
     #maempr03#1  = enAnyo;
     #maempr03#2  = 1;
|FPRC;

|PROCESO Alta03;
     #maempr03#0  = enEmpresa;
     #maempr03#1  = enAnyo;
     #maempr03#2  = 99;
|FPRC;

|PROGRAMA;
     aAlfa = "";
     |SI enModoConsulta = 1;
         aAlfa = "(MODO CONSULTA)   ";
     |FINSI;

     aAlfa = aAlfa + "Relaci¢n de administradores";
     |VENTANA 0501, 3399, -1, 16, aAlfa;
     n0Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n0Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |SI enModoConsulta = 0;
         nRango = 1 + 2 + 4 + 8 + 16 + 32;
     |SINO;
         nRango = 4 + 8 + 16 + 32;
     |FINSI;

     |LINEAL_SIMPLE #1#maempr03, nRango, 0502, 3199, Baja03, Alta03, NULL;
     nGrid = FSalida;

     |PULSATECLA;
     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}Salir," + nGrid;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |DESTRUYECONTROL nGrid;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n0Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PULSATECLA;

     |FINVENTANA n0Vent;
|FPRO;

|PROCESO Tipo80;  |TIPO 80;
     aAlfa  = PARAMETRO % 104;  enAnyo    = aAlfa;
     aAlfa  = PARAMETRO % 505;  enEmpresa = aAlfa;

     |ABRE #maempr10;
     #maempr10#0 = enEmpresa;
     #maempr10#1 = enAnyo;
     |LEE 000#maempr10.=;
     |SI FSalida ! 0;  |DDEFECTO #maempr10;  |FINSI;
     |CIERRA #maempr10;

     |ABRET #maempr03;
|FPRC;

|PROCESO Tipo90;  |TIPO 90;
     |CIERRAT #maempr03;
|FPRC;
