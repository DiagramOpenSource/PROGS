|FICHEROS;
     isom0001;
     isom0033;
     isom0520;
     maempr10;
|FIN;

|VARIABLES;
      nRango         = 0;
      nBotonSal      = 0;
      nBotonAnota    = -1;
      nBotonPorce    = -1;
      nTree          = 0;
      nHandle        = 0;
      nPanta         = 0;
      nBotonLabel    = 0;
      nVentana       = 0;
      nVent          = 0;
      nGrid          = 0;
      nModo          = 0;
      nLinea         = 0;
      nBase          = 0;
      nBase1         = 0;
      nCasilla552    = 0;
      nPorce         = 0;
      nDLinea        = 0;
      nHLinea        = 0;
      nTope          = 0;
      nValor1170     = 0;
      nValor505      = 0;
      nVal1          = 0;
      nVal2          = 0;
      nOk            = 0;

      aContenido     = "";
      aTexto         = "";
      aCasilla       = "";
      aCas           = "";
      aBase          = "";
      aAlfa          = "";
      aAbre          = "";
      aEsc1          = "";
      aEsc2          = "";
      aRetu          = "";
      aTecla         = "";

     {-1}sTree       = 0;
     sT_nID          = 0;
     sT_nOper        = 0;
     sT_aData        = "";

     {-1}aVent       = "";
         aVent1      = 0;
         aVent2      = 0;
         aVent3      = 0;
         aVent4      = 0;
         aVent5      = "";

     &enEmpresa      = 0;
     &enAnyo         = 0;
     &eaCasilla      = "";
     &eaTitCasilla   = "";
     &eaRutaOrigen   = "";
     &eaRutaDestino  = "";
     &eaFicheroTra   = "";
     &eaFicheroTxt   = "";
     &eaFicheroTgz   = "";
     &eaFicheroTar   = "";
     &eaOrigen       = "";
     &eaDestino      = "";
     &eaAlfa         = "";
     &eaTipoColor    = "";
     &eaColor        = "";
     &enModoConsulta = 0;
|FIN;

|PROCESO T12;  |TIPO 12;
|FPRC;

|PROCESO T1;  |TIPO 1;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;  |O nModo = 3;
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Totalm33;
     #isom0033#11 = #isom0033#5 - #isom0033#7 - #isom0033#9;

     |PINTA #isom0033#11;
|FPRC;

|PROCESO T2;  |TIPO 2;
     |HAZ T1;
|FPRC;

|PROCESO T3;  |TIPO 3;
     #isom0520#0 = #isom0033#1;
     #isom0520#1 = #isom0033#2;
     |LEE 000#isom0520.=;
     |SI FSalida ! 0;  |DDEFECTO #isom0520;  |FINSI;

     |SI #isom0520#7 ! "I";  |ACABA;  |FINSI;

     |GRABA 020#isom0033.a;
     |LIBERA #isom0033;

     nLinea = #isom0033#2;

     |HAZ TotalFich133;

     #isom0033#0 = #maempr10#0;
     #isom0033#1 = #maempr10#1;
     #isom0033#2 = nLinea;
     |LEE 101#isom0033.=;

     aAlfa = "|IDGRID";
     aAlfa = #isom0033#aAlfa#;
     nGrid = aAlfa;
     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO T0;  |TIPO 0;
     |C_M #isom0033#5,  1, "N";
     |C_M #isom0033#7,  1, "N";
     |C_M #isom0033#9,  1, "N";

     #isom0520#0 = #isom0033#1;
     #isom0520#1 = #isom0033#2;
     |LEE 000#isom0520.=;
     |SI FSalida ! 0;  |DDEFECTO #isom0520;  |FINSI;

     |SI #isom0520#7 ! "I";  |ACABA;  |FINSI;

     |SI #isom0033#4  ! "     ";  |C_M #isom0033#5,  1, "S";  |FINSI;
     |SI #isom0033#6  ! "     ";  |C_M #isom0033#7,  1, "S";  |FINSI;
     |SI #isom0033#8  ! "     ";  |C_M #isom0033#9,  1, "S";  |FINSI;
|FPRC;

|PROCESO Evento133;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#isom0033.=;
     |FINSI;
|FPRC;

|PROCESO isom0520;
     #isom0033#0  = enEmpresa;
     #isom0033#1  = #isom0520#0;
     #isom0033#2  = #isom0520#1;
     |LEE 101#isom0033.=;
     |SI FSalida ! 0;
         |DDEFECTO #isom0033;
         #isom0033#0  = enEmpresa;
         #isom0033#1  = #isom0520#0;
         #isom0033#2  = #isom0520#1;
         |GRABA 020#isom0033.b;
     |FINSI;

     #isom0033#3  = #isom0520#2;
     #isom0033#4  = #isom0520#3;
     #isom0033#6  = #isom0520#4;
     #isom0033#8  = #isom0520#5;
     #isom0033#10 = #isom0520#6;

     eaTipoColor  = #isom0520#8;
     |HAZ CogeColor;
     #isom0033#12 = eaColor;

     |GRABA 020#isom0033.a;
     |LIBERA #isom0033;
|FPRC;

|DEFBUCLE isom0520;
     #isom0520#1;
     ;
     enAnyo, 0001;
     enAnyo, 9999;
     ;
     NULL, isom0520;
|FIN;

|PROCESO MontaEstructura520;
     |BUCLE isom0520;
|FPRC;

|PROCESO BotonSalir;
     |PTEC 807;
|FPRC;

|PROCESO BotonAnota;
     eaCasilla    = #isom0033#4;     |QBF eaCasilla;
     eaTitCasilla = #isom0033#3;     |QBF eaTitCasilla;

     |SUB_EJECUTA "isom0499";

     aAlfa = "|IDGRID";
     aAlfa = #isom0033#aAlfa#;
     nGrid = aAlfa;
     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO Baja133;
     #isom0033#0  = enEmpresa;
     #isom0033#1  = enAnyo;
     #isom0033#2  = 1;
|FPRC;

|PROCESO Alta133;
     #isom0033#0  = enEmpresa;
     #isom0033#1  = enAnyo;
     #isom0033#2  = 9999;
|FPRC;

|PROCESO isom0033Cheq;
     nVal1 = (#isom0033#7 + #isom0033#9) ! 2;
     nVal2 = #isom0033#5 ! 2;
     |SI nVal1 > nVal2;
         aAlfa = "0000La casillas " + #isom0033#6 + " + " + #isom0033#8;
         aAlfa = aAlfa + " no puede ser superior a la " + #isom0033#4;
         |MENAV aAlfa

         nOk = 0;  |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE isom0033Cheq;
     #isom0033#1;
     ;
     enEmpresa, enAnyo, nDLinea;
     enEmpresa, enAnyo, nHLinea;
     be;
     NULL, isom0033Cheq;
|FIN;

|PROCESO isom0033;
     |SI #isom0033#5  ! 0;  |ACABA;  |FINSI;
     |SI #isom0033#7  ! 0;  |ACABA;  |FINSI;
     |SI #isom0033#9  ! 0;  |ACABA;  |FINSI;
     |SI #isom0033#11 ! 0;  |ACABA;  |FINSI;

     |BORRA 020#isom0033.a;
     |LIBERA #isom0033;
|FPRC;

|DEFBUCLE isom0033;
     #isom0033#1;
     ;
     enEmpresa, enAnyo, nDLinea;
     enEmpresa, enAnyo, nHLinea;
     be;
     NULL, isom0033;
|FIN;

|PROGRAMA;
     |ABRE #maempr10;
     #maempr10#0 = enEmpresa;
     #maempr10#1 = enAnyo;
     |LEE 000#maempr10.=;
     |SI FSalida ! 0;  |DDEFECTO #maempr10;  |FINSI;
     |CIERRA #maempr10;

     |HAZ MontaEstructura520;

     #isom0033#0 = enEmpresa;
     #isom0033#1 = enAnyo;
     #isom0033#2 = 1;
     |LEE 000#isom0033.];

     |PINPA #0#isom0033;

     |CONTROL_SIMPLE 0, "BOTON,{{197}} sali&R", 3488, 3498, BotonSalir;
     nBotonSal = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}} a&Notaciones de la compesaci¢n", 3302, 3425, BotonAnota;
     nBotonAnota = FSalida;

     |SI enModoConsulta = 0;
         nRango = 1 + 2 + 4 + 8 + 16 + 128 + 256;
     |SINO;
         nRango = 4 + 8 + 16 + 128 + 256;
     |FINSI;

     |PARA;  |SI;  |HACIENDO;
          |LINEAL_SIMPLE #1#isom0033, nRango, 0502, 3498, Baja133, Alta133, Evento133;

          nOk = 1;
          |BUCLE isom0033Cheq;
          |SI nOk = 1;  |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nBotonSal;
     |FIN_CONTROL_WINDOWS nBotonAnota;

     |BUCLE isom0033;

     |PULSATECLA;
|FPRO;

|PROCESO T80m33;  |TIPO 80;
     |ABRET #isom0033;
     |ABRET #isom0520;

     nDLinea = 0;
     nHLinea = 9999;

     FSalida = 3499;
|FPRC;

|PROCESO T90m33;  |TIPO 90;
     |CIERRAT #isom0033;
     |CIERRAT #isom0520;

     |SELECT #1#isom0001;
|FPRC;
