|FICHEROS;
   cubal805 #0;
   imagen@  #900;
   :/agicont/def/ca8m0001 #421;  || Def Balance Textos
|FIN;

|VARIABLES;
   &empresa = 0;
   &anyo    = 0;
   &nombre  = "";
   &nif     = "";
   &tipo    = "";
   &unid    = "";
   &modo    = 0;
   &sw_mod  = 0;
   anyo1    = 0;
   anyo2    = 0;
   x        = 0;
   FEstado  = 0;
   nSeguir  = 0;

   nLinea  = 0;
   activo  = 0;
   activ1  = 0;
   pasivo  = 0;
   pasiv1  = 0;
   saldo   = 0;
   aMas    = "";
   aDef    = "";

   nImporte  = 0;
   nImporte0 = 0;
   nImporte1 = 0;
   nImporte2 = 0;
   nImporte3 = 0;
   nImporte4 = 0;
   nImporte5 = 0;
   nImporte11= 0;
   nImporte12= 0;
   nImporte13= 0;
   nImporte14= 0;
   nImporte15= 0;
   nCampo2 = 0;
   aCampo3 = "";
   nCampo4 = 0;
   nCampo5 = 0;
   nCampo6 = 0;
   nCampo7 = 0;
   nCampo8 = 0;

   nDCampo4 = 0;
   nDCampo5 = 0;
   nDCampo6 = 0;
   nDCampo7 = 0;
   nDCampo8 = 0;
   nHCampo4 = 0;
   nHCampo5 = 0;
   nHCampo6 = 0;
   nHCampo7 = 0;
   nHCampo8 = 0;
   aAlfa1   = "";
   nInkey   = 0;
   nSwDescuadre     = 0;
   nValor           = 0;
   nValor1          = 0;
   nValor2          = 0;
   nCasilla         = 0;
|FIN;

|INCLUYE varbal_i;

|PROCESO LeoNota;
 |ABRE #421;
 |DDEFECTO #421;
 #421#14 = enEjerBal;
 #421#0  = nBalNum;
 #421#1  = nBalance;
 #421#2  = #0#9;
 #421#3  = #0#10;
 #421#4  = #0#11;
 #421#5  = #0#12;
 #421#6  = #0#13;
 #421#7  = #0#14;
 #421#10 = #0#15;
 |LEE 040#421=;
 |CIERRA #421;
 #0#5 = #421#13; |PINTA #0#5;
|FPRC;

|PROCESO Nota5; TIPO 0;
 |SI FSalida = 10;
     |HAZ LeoNota;
 |FINSI;
 aAlfa1 = #0#5 % 105;
 #0#5 = aAlfa1;
|FPRC;


|PROCESO Tipo7_5;  |TIPO 7;
    aAlfa1 = "     Pulse <F2> para Introducir Nota, <F3> Recoger Nota Memoria";
    |SI #0#8 = "C";
        aAlfa1 = "     Pulse <F2> para Introducir Importe y Nota, <F3> Recoger Nota";
    |FINSI;
    |SI modo = 4; aAlfa1 = "     "; |FINSI;
    |MENSA aAlfa1;
|FPRC;

|PROCESO Tipo11_5;  |TIPO 11;
    nInkey = FSalida;
    |SI nInkey ! 10;  |Y nInkey ! 11; |ACABA; |FINSI;
    |SI modo     = 4;   |ACABA; |FINSI;

    |LEE 101#0=;

    |SI nInkey = 10;
        |ENCAMPO #5#0;

        |SI #0#8 = "C";
            |ENCAMPO #7#0;
            |SI sw_mod = 0; |ENCAMPO #6#0; |FINSI;
        |FINSI;
    |FINSI;
    |SI nInkey = 11;
        |HAZ LeoNota;
    |FINSI;

    |GRABA 020#0a;
    |LIBERA #0;

    |SALVA_DATOS #0;

    |SI #0#8 = "C"; |Y nInkey = 10;
        |HAZ Totales_5;
    |FINSI;

    |REPON_DATOS #0;

    |PONCONTROL 23, "SI";
|FPRC;

|PROCESO Inferior0;
 #0#0 = empresa;
 #0#1 = anyo;
 #0#2 = 0001;
|FPRC;

|PROCESO Superior0;
 #0#0 = empresa;
 #0#1 = anyo;
 #0#2 = 9999;
|FPRC;

|PROGRAMA;
 |SI enTBal ! 1;
     |MENAV "    Informe solo para Balance NORMAL";
     |ACABA;
 |FINSI;

 |DBASS aMas;
 aDef = aMas + "sociedad/def/cubal805";
 |CARGA_DEF aDef, imagen@;
 |SI FSalida ! 0; |ACABA; |FINSI;

 nBalance = 4; ||Flujos de estado
 nSwDescuadre = 1;
 |PARA; |SI nSwDescuadre = 1; |HACIENDO;
        |PINPA #0#0;
        |HAZ pintaany;
        |HAZ pidedato;
        |ENTLINEAL #1#0, -3, 4, 21, 0, Inferior0, Superior0;
        |HAZ MiroDescuadre;
        |SI nSwDescuadre = 1;
            |ATRI S;
            |PINTA 1115, "                                                       ";
            |PINTA 1215, " Atencion!. No cuadra la variacion neta del ejercicio  ";
            |PINTA 1315, " con la diferencia entre saldo inicial y final de las  ";
            |PINTA 1415, " cuentas de tesoreria. Revise los datos introducidos.  ";
            |PINTA 1515, " CONTINUAR S/N:                                        ";
            |ATRI 0;
            |CUADRO 1114 1568;
            |ATRI P;
            aAlfa1 = "N";
            E_sup  = 1;
            E_inf  = "SNsn";
            aAlfa1 = 1531 ? 0; aAlfa1 = aAlfa1 > ""; |PINTA 1531, aAlfa1;
            |SI aAlfa1 = "S";
                nSwDescuadre = 0;
            |FINSI;
        |FINSI;
 |SIGUE;
 |DESCARGA_DEF #900;
|FPRO;

|PROCESO pidedato;
 |ABRE #0;
 #0#0 = empresa;
 #0#1 = anyo;
 #0#2 = 0000;
 |LEE 001#0];
 |SI FSalida ! 0; |O #0#0 ! empresa; |O #0#1 ! anyo;
     |CIERRA #0;
     |HAZ eCrearVacio;
     |CIERRA #0;
 |FINSI;
 |CIERRA #0;
|FPRC;

|PROCESO pintaany;
 anyo2 = anyo - 1;
 anyo1 = anyo;
 |ATRI P; |PINTA 962, anyo1;
 |PINTA 975, anyo2; |ATRI 0;
|FPRC;

|PROCESO Tipo5_5;  |TIPO 5;
   ||    |LEE 040#0p;
   ||    #0#4 = "N";
   ||    |LINEAS_BI_ATRI #0#4, D, S, -1;
|FPRC;

|PROCESO LeeCas805;  ||Carga Trabajo de Cuentas
  #0#0  = empresa;
  #0#1  = anyo;
  #0#9  = nCampo2;
  #0#10 = aCampo3;
  #0#11 = nCampo4;
  #0#12 = nCampo5;
  #0#13 = nCampo6;
  #0#14 = nCampo7;
  #0#15 = nCampo8;
  |LEE 101#0=;
  |SI FSalida = 0; |Y #0#8 ! "A";
      #0#7 = #0#7 + nImporte;
      |SI sw_mod = 0; #0#6 = #0#6 + nImporte0; |FINSI;
      |GRABA 020#0a;
  |FINSI;
  |LIBERA #0;
|FPRC;

|PROCESO ASumar805;
  nImporte  = #900#7;
  nImporte0 = #900#6;

  nCampo2 = #900#9;
  aCampo3 = #900#10;
  nCampo4 = #900#11;
  nCampo5 = #900#12;
  nCampo6 = #900#13;
  nCampo7 = #900#14;
  nCampo8 = #900#15;

  |SI nCampo8 ! 0;    ||Subpartida (A/P = 0)
      nCampo8 = 0;
      |HAZ LeeCas805;
  |FINSI;

  |SI nCampo7 ! 0;   ||Partida
      nCampo8 = 0;
      nCampo7 = 0;
      |HAZ LeeCas805;
  |FINSI;
|FPRC;

|PROCESO LeoTotales805;
 |SI #900#8 = "C";
     nImporte  = nImporte  + #900#7;
     nImporte0 = nImporte0 + #900#6;
 |FINSI;
|FPRC;

|DEFBUCLE LeoTotales805;
 #900#2009;
 ;
 empresa,anyo, nCampo2, aCampo3, nDCampo4, 0, 00, 00,0;
 empresa,anyo, nCampo2, aCampo3, nHCampo4, 9, 99, 99,9;
 ;
 NULL, LeoTotales805;
|FIN;

|PROCESO elTotal805;
  |SI #421#8 = "A"; |ACABA; |FINSI;

  nImporte  = 0;
  nImporte0 = 0;
  nCampo2 = #421#2;  ||Bloque
  aCampo3 = #421#3;  ||Tipo
  nCampo4 = #421#4;  ||Agrupacion
  nCampo5 = #421#5;  ||Subagrupacion

  nDCampo4 = 00; nHCampo4 = 99;
  |BUCLE LeoTotales805;

  |SI nCampo5 = 5;
      nImporte  = nImporte1  + nImporte2  + nImporte3  + nImporte4;
      nImporte0 = nImporte11 + nImporte12 + nImporte13 + nImporte14;
  |FINSI;

  |SI nCampo5 ! 4;
      nCampo6 = #421#6;
      nCampo7 = #421#7;
      nCampo8 = #421#10;
      |HAZ LeeCas805;
  |FINSI;

  |SI nCampo5 = 1; nImporte1 = nImporte; nImporte11 = nImporte0; |FINSI;
  |SI nCampo5 = 2; nImporte2 = nImporte; nImporte12 = nImporte0; |FINSI;
  |SI nCampo5 = 3; nImporte3 = nImporte; nImporte13 = nImporte0; |FINSI;
  |SI nCampo5 = 4; nImporte4 = nImporte; nImporte14 = nImporte0; |FINSI;
  |SI nCampo5 = 5; nImporte5 = nImporte; nImporte15 = nImporte0; |FINSI;
|FPRC;

|DEFBUCLE elTotal805;
 #421#1;
 ;
 enEjerBal, nBalNum, nBalance, 7, " ", 00, 1, 00, 00, 0;
 enEjerBal, nBalNum, nBalance, 9, "Z", 99, 9, 99, 99, 9;
 e;
 NULL, elTotal805;
|FIN;

|PROCESO Final5;
    nImporte1 = 0; nImporte11 = 0;
    nImporte2 = 0; nImporte12 = 0;
    nImporte3 = 0; nImporte13 = 0;
    nImporte4 = 0; nImporte14 = 0;
    nImporte5 = 0; nImporte15 = 0;
    |BUCLE elTotal805;
|FPRC;

|DEFBUCLE ASumar805;
 #900#1003;
 8;
 empresa, anyo, 0001, "C";
 empresa, anyo, 9999, "C";
 be;
 NULL, ASumar805;
|FIN;

|PROCESO ACero805;
  |SI #900#8 ! "C";
      #900#7 = 0;
      |SI sw_mod = 0; #900#6 = 0; |FINSI;
      |GRABA 020#900a;
  |FINSI;
  |LIBERA #900;
|FPRC;

|DEFBUCLE ACero805;
 #900#1003;
 ;
 empresa, anyo, 0001;
 empresa, anyo, 9999;
 be;
 NULL, ACero805;
|FIN;

|PROCESO Totales_5;

  |BUCLE ACero805;

  |SELECT #2#0;
  |BUCLE ASumar805;

  |HAZ Final5;
  |SELECT #1#0;
|FPRC;
|| ****************************************************************
|PROCESO LeoCasilla;
 nValor = 0;
 |DDEFECTO #0;
 #0#0  = empresa;
 #0#1  = anyo;
 #0#16 = nCasilla;
 |LEE 041#0=;
 |SI FSalida = 0;
     nValor = #0#7;
 |FINSI;
|FPRC;

|PROCESO MiroDescuadre;
 nSwDescuadre = 0;
 nValor1 = 0;
 nValor2 = 0;

 |ABRE #0;
 |SELECT #3#0;
 nCasilla = 65000;  |HAZ LeoCasilla;  nValor1 = nValor;
 nCasilla = 65200;  |HAZ LeoCasilla;  nValor2 = nValor;
 nCasilla = 65100;  |HAZ LeoCasilla;  nValor2 = (nValor2 - nValor) ! 2;
 |SI nValor1 ! nValor2;
     nSwDescuadre = 1;
 |FINSI;
 |SELECT #1#0;
 |CIERRA #0;
|FPRC;
