|FICHEROS;
     zregcu16 #1016;
     zw000012 #912,MANTE;
|FIN;

|VARIABLES;
     nEstado        = 0;
     nPan           = 0;
     nBoton1        = 0;
     nBoton2        = 0;
     nBoton3        = 0;
     nGrid          = 0;
     nRango         = 0;
     nIdSalir       = 0;
     nCampo         = 0;
     nModo          = 0;
     nVentana       = 0;

     aEsc1          = "";
     aEsc2          = "";
     aRetu          = "";
     aTecla         = "";

     {-1}aVent      = "";
         aVent1     = 0;
         aVent2     = 0;
         aVent3     = 0;
         aVent4     = 0;
         aVent5     = "";

     {2}nNivel      = 0;

     &enEmpresa     = 0;
     &enEjerCuadro  = 0;
|FIN;

|PROCESO Tipo12Cuadros;  |TIPO 12;
|FPRC;

|PROCESO Baja1016;
     #1016#0 = enEmpresa;
     #1016#1 = enEjerCuadro;
     #1016#2 = 001;
|FPRC;

|PROCESO Alta1016;
     #1016#0 = enEmpresa;
     #1016#1 = enEjerCuadro;
     #1016#2 = 999;
|FPRC;

|PROCESO Evento1016;
     |SI nModo ! 0;  |ACABA;  |FINSI;

     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#1016=;
         nEstado = FSalida;
         |SI FSalida ! 0;  |O #1016#0 ! enEmpresa;  |O #1016#1 ! enEjerCuadro;
             |DDEFECTO #1016;
             nEstado = 1;
         |FINSI;

         |PARA nCampo = 0;  |SI nCampo [ 11;  |HACIENDO nCampo = nCampo + 1;
               #912nCampo = #1016nCampo;

               |C_M #912nCampo, 1, "N";   |PINTA #912nCampo;
         |SIGUE;

         FSalida = 0;

         |SI nEstado = 0;
             |ESTADO_CONTROL nBoton2, "ENABLE";
             |ESTADO_CONTROL nBoton3, "ENABLE";
         |SINO;
             |ESTADO_CONTROL nBoton2, "DISABLE";
             |ESTADO_CONTROL nBoton3, "DISABLE";
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Alta;
     nModo     = 1;
     nIdSalir  = 0;
     InNivel   = nNivel1<-;
     nNivel1   = 0;
     |ESPECIFICA "IDBOTONTECLA", nNivel;

     nIdSalir = nNivel2;
     |ESTADO_CONTROL nIdSalir, "HIDE";

     |ESTADO_CONTROL nBoton1,   "DISABLE";
     |ESTADO_CONTROL nBoton2,   "DISABLE";
     |ESTADO_CONTROL nBoton3,   "DISABLE";
     |ESTADO_CONTROL nGrid,     "DISABLE";

     |DDEFECTO #912;

     #1016#0 = enEmpresa;
     #1016#1 = enEjerCuadro;
     #1016#2 = 999;
     |LEE 000#1016];
     |SI FSalida = 0;
         |LEE 000#1016a;
     |SINO;
         |LEE 000#1016u;
     |FINSI;

     #912#0 = enEmpresa;
     #912#1 = enEjerCuadro;
     #912#2 = 1;
     |SI FSalida = 0;  |Y  #1016#0 = enEmpresa;  |Y  #1016#1 = enEjerCuadro;
          #912#2 = #1016#2 + 1;
     |FINSI;

     |PARA nCampo = 0;  |SI nCampo [ 11;  |HACIENDO nCampo = nCampo + 1;
           |C_M #912nCampo, 1, "S";  |PINTA #912nCampo;
     |SIGUE;

     |C_M #912#0, 1, "N";
     |C_M #912#1, 1, "N";
     |C_M #912#2, 1, "N";  |PINTA #912#2;

     |ENDATOS #1#912;
     |SI FSalida = 0;
         |PARA nCampo = 0;  |SI nCampo [ 11;  |HACIENDO nCampo = nCampo + 1;
               #1016nCampo = #912nCampo;
         |SIGUE;
         |GRABA 020#1016n;
         |LIBERA #1016;
     |FINSI;

     |ESTADO_CONTROL nBoton1,   "ENABLE";
     |ESTADO_CONTROL nBoton2,   "ENABLE";
     |ESTADO_CONTROL nBoton3,   "ENABLE";
     |ESTADO_CONTROL nGrid,     "ENABLE";

     |REFRESCACONTROL nGrid;  |PULSATECLA;

     nModo     = 0;

     |FOCOTECLADO nGrid;

     |ESTADO_CONTROL nIdSalir, "SHOW";

     |PULSATECLA;
|FPRC;

|PROCESO Modificar;
     |LEE 101#1016=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     nModo     = 1;
     nIdSalir  = 0;
     InNivel   = nNivel1<-;
     nNivel1   = 0;
     |ESPECIFICA "IDBOTONTECLA", nNivel;

     nIdSalir = nNivel2;
     |ESTADO_CONTROL nIdSalir, "HIDE";

     |ESTADO_CONTROL nBoton1,   "DISABLE";
     |ESTADO_CONTROL nBoton2,   "DISABLE";
     |ESTADO_CONTROL nBoton3,   "DISABLE";
     |ESTADO_CONTROL nGrid,     "DISABLE";

     |DDEFECTO #912;

     |PARA nCampo = 0;  |SI nCampo [ 11;  |HACIENDO nCampo = nCampo + 1;
           #912nCampo = #1016nCampo;

           |C_M #912nCampo, 1, "S";  |PINTA #912nCampo;
     |SIGUE;

     |C_M #912#0, 1, "N";
     |C_M #912#1, 1, "N";
     |C_M #912#2, 1, "N";  |PINTA #912#2;

     |ENDATOS #1#912;
     |SI FSalida = 0;
         |PARA nCampo = 0;  |SI nCampo [ 11;  |HACIENDO nCampo = nCampo + 1;
               #1016nCampo = #912nCampo;
         |SIGUE;
         |GRABA 020#1016a;
         |LIBERA #1016;
     |FINSI;

     |ESTADO_CONTROL nBoton1,   "ENABLE";
     |ESTADO_CONTROL nBoton2,   "ENABLE";
     |ESTADO_CONTROL nBoton3,   "ENABLE";
     |ESTADO_CONTROL nGrid,     "ENABLE";

     |REFRESCACONTROL nGrid;  |PULSATECLA;

     nModo     = 0;

     |FOCOTECLADO nGrid;

     |ESTADO_CONTROL nIdSalir, "SHOW";

     |PULSATECLA;
|FPRC;

|PROCESO Baja;
     |LEE 101#1016=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |CONFI "0000NSeguro que quiere BORRAR el apunte";
     |SI FSalida ! 0;  |LIBERA #1016;  |ACABA;  |FINSI;

     |BORRA 020#1016a;
     |LIBERA #1016;

     |REFRESCACONTROL nGrid;  |PULSATECLA;

     FSalida = "SKIPDEFAULT";

     FSalida = 1;  |HAZ Evento1016;
     |PULSATECLA;
     |FOCOTECLADO nGrid;
|FPRC;

|PROGRAMA;
     |VENTANA 0501, 2799, -1, 16, "Arrendamientos financieros";
     nVentana = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |ABRET #1016;

     |PINPA #0#912;
     nPan = FSalida;

     #1016#0 = enEmpresa;
     #1016#1 = enEjerCuadro;
     #1016#2 = 1;
     |LEE 000#1016=;

     nRango = 4 + 8 + 16 + 32 + 128;

     |LINEAL_SIMPLE #1#1016, nRango, 0502, 1598, Baja1016, Alta1016, Evento1016;
     nGrid = FSalida;

     FSalida = 1;  |HAZ Evento1016;

     |CONTROL_SIMPLE nPan, "BOTON,{{193}}Alta ", 1664, 1674, Alta;
     nBoton1  = FSalida;

     |CONTROL_SIMPLE nPan, "BOTON,{{197}}Modificar ", 1676, 1686, Modificar;
     nBoton2  = FSalida;

     |CONTROL_SIMPLE nPan, "BOTON,{{199}}Borrar", 1688, 1698, Baja;
     nBoton3  = FSalida;

     |ESTADO_CONTROL nBoton2, "DISABLE";
     |ESTADO_CONTROL nBoton3, "DISABLE";

     |PINDA #0#912;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::{{001}}Salir," + nGrid;
          |LEETECLA aTecla;
          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
          |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;
     |DESTRUYECONTROL nGrid;

     |PULSATECLA;

     |CIERRAT #1016;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PULSATECLA;

     |FINVENTANA nVentana;
|FPRO;
