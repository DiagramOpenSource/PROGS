|FICHEROS;
     megwordc #0;
     megwordl #1,MANTE;
     meempmem #2;
     maempr10 #3;
|FIN;

|VARIABLES;
     aAlfa     = "";
     aAlfa1    = "";
     aAlfa2    = "";
     aAlfa3    = "";
     aAlfa4    = "";
     aAlfa5    = "";
     aAlfa6    = "";
     aAlfa7    = "";
     aAlfa8    = "";
     aHoja1    = "";
     aHoja2    = "";
     aLen      = "";
     aCaracter = "";
     aBase     = "";
     aFichero  = "";


     nSw       = 0;
     nLen      = 0;
     nCampo    = 0;
     nPos      = 0;
     nSwAct    = 0;
     nNume     = 0;

     &eaPagina = "";
|FIN;

|PROCESO Borral;
     |DBASE aBase;   |QBT aBase;
     aBase    = aBase + "emisiones/" + (("00000" + #1#0) % -105) + "/" + #1#1;
     aFichero = aBase + "/" + #1#2;  |QBT aFichero;
     |FBORRA aFichero;

     |BORRA 020#1a;
     |LIBERA #1;
|FPRC;

|DEFBUCLE megwordl;
     #1#1;
     ;
     #0#0, #0#1, "           ";
     #0#0, #0#1, "zzzzzzzzzzz";
     be;
     NULL, Borral;
|FIN;

|PROCESO ActualizaCab;
     |LEE 101#0=;

     aHoja1 = "";
     aHoja2 = "";
     aLen   = eaPagina % 0;
     nLen   = aLen;
     nSw    = 0;
     |PARA nCampo = 1;  |SI nCampo [ nLen;  |HACIENDO nCampo = nCampo + 1;
           nPos      = (nCampo * 100) + 1;
           aCaracter = eaPagina % nPos;
           |SI aCaracter = ":";  nSw = 1;  |FINSI;
           |SI aCaracter = ",";  nSw = 2;  |FINSI;

           |SI nSw = 1;  |Y aCaracter ! ":";
               aHoja1 = aHoja1 + aCaracter;
           |FINSI;

           |SI nSw = 2;  |Y aCaracter ! ",";
               aHoja2 = aHoja2 + aCaracter;
           |FINSI;
     |SIGUE;

     |SI nSwAct = 1;
         #0#3  = aHoja1;
         nNume = aHoja2;
         nNume = (#0#3 + nNume) - 1;
         #0#4  = nNume;

         |GRABA 020#0a;
         |LIBERA #0;

         |PINTA #0#3;
         |PINTA #0#4;
         |ACABA;
     |FINSI;

     |SI nSwAct = 2;
         #0#5  = aHoja1;
         nNume = aHoja2;
         nNume = (#0#5 + nNume) - 1;
         #0#6  = nNume;

         |GRABA 020#0a;
         |LIBERA #0;

         |PINTA #0#5;
         |PINTA #0#6;
         |ACABA;
     |FINSI;

     |SI nSwAct = 3;
         |LIBERA #0;
         |ABRE #2;
         #2#0 = #0#0;
         |LEE 101#2=;
         |SI FSalida = 0;  |Y #2#5 = #0#1;
             #2#134 = #0#4;
             |SI #0#7 = "S";  #2#134 = #0#6;  |FINSI;
             |GRABA 020#2a;
         |FINSI;
         |LIBERA #2;
         |CIERRA #2;

         |ABRE #3;
         #3#0 = #0#0;
         #3#1 = #0#1;
         |LEE 101#3=;
         |SI FSalida = 0;
             #3#90 = #0#4;
             |SI #0#7 = "S";  #3#90 = #0#6;  |FINSI;
             |GRABA 020#3a;
         |FINSI;
         |LIBERA #3;
         |CIERRA #3;

         |PUSHV 0501 2380;
         |CUADRO 0630 0862;
         |ATRI P;  |PINTA 0731, "EL NUMERO DE PAGINAS HA VARIADO"; |ATRI p;

         |CONFI "2400SDesea crear el Certificado y las Cuentas Anuales con los datos actuales";
         |SI FSalida = 0;
             aAlfa1 = "zmenumem;";
             aAlfa2 = "000000";
             aAlfa3 = (("00000" + #0#0) % -105);
             aAlfa4 =  "9";
             aAlfa5 = "0";
             aAlfa6 = "0";
             aAlfa7 = "    ";
             aAlfa  = aAlfa1 + aAlfa2 + aAlfa3 + aAlfa4 + aAlfa5 + aAlfa6 + aAlfa7;
             |LIBERA #0;
             |SUB_EJECUTA aAlfa;

             aAlfa1  = "zmenumem;";
             aAlfa2  = "000000";
             aAlfa3  = (("00000" + #0#0) % -105);
             aAlfa4  =  "4";
             aAlfa5  = "0";
             aAlfa6  = "0";
             aAlfa7  = #0#1;
             aAlfa   = aAlfa1 + aAlfa2 + aAlfa3 + aAlfa4 + aAlfa5 + aAlfa6 + aAlfa7;
             |SUB_EJECUTA aAlfa;
         |FINSI;
         |POPV;
     |FINSI;
|FPRC;

|PROCESO Tipo11m1;  |TIPO 11;
     |SI FSalida = 10;
         aAlfa = #1#2 % 206;
         |SI aAlfa = "memori";
             aAlfa = "zmenumem;" + (#1#2 % 206) + (("00000" + #1#0) % -105) + "6" + "1" + "1" + #1#1;
             |SUB_EJECUTA aAlfa;
             nNume = eaPagina;
             nNume = nNume - 1;
             |SI nNume ! #0#4;
                 nSwAct = 1;  |HAZ ActualizaCab;

                 |SI #0#7 = "S";
                     nNume = #0#4 + 1;
                     aAlfa = "zmenumem;" + "gestio" + (("00000" + #1#0) % -105) + "6" + "1" + "1" + #1#1 + "=" + (("000" + nNume) % -103);
                     |SUB_EJECUTA aAlfa;
                     nSwAct = 2;  |HAZ ActualizaCab;
                 |FINSI;
                 nSwAct = 3;  |HAZ ActualizaCab;
             |FINSI;
             |ACABA;
         |FINSI;

         |SI aAlfa = "gestio";
             aAlfa = "zmenumem;" + (#1#2 % 206) + (("00000" + #1#0) % -105) + "6" + "1" + "1" + #1#1 + "";
             |SUB_EJECUTA aAlfa;
             nNume = eaPagina;
             nNume = nNume - 1;
             |SI nNume ! #0#6;
                 nSwAct = 2;   |HAZ ActualizaCab;
                 |SI #0#7 = "S";  nSwAct = 3;   |HAZ ActualizaCab;  |FINSI;
             |FINSI;
             |ACABA;
         |FINSI;

         aAlfa = "zmenumem;" + (#1#2 % 206) + (("00000" + #1#0) % -105) + "6" + "1" + "1" + #1#1;
         |SUB_EJECUTA aAlfa;
     |FINSI;

     |SI FSalida = 11;
         |SI #1#7 = "S";
             |CONFI "2400SEl Documento ya esta impreso, Desea repetir impresion ";
             |SI FSalida ! 0;  |ACABA;  |FINSI;
         |FINSI;

         |LEE 101#1=;

         aAlfa = "zmenumem;" + (#1#2 % 206) + (("00000" + #1#0) % -105) + "6" + "2" + "1" + #1#1;
         |SUB_EJECUTA aAlfa;

         #1#7 = "S";
         |GRABA 020#1a;
         |LIBERA #1;
     |FINSI;
|FPRC;

|RUTINA ClaveBaja1;
     #1#0 = #0#0;
     #1#1 = #0#1;
     #1#2 = "           ";
|FRUT;

|RUTINA ClaveAlta1;
     #1#0 = #0#0;
     #1#1 = #0#1;
     #1#2 = "zzzzzzzzzzz";
|FRUT;

|PROCESO Tipo7m0;  |TIPO 7;
     |ENTLINEAL #1#1, -3, 7, 22, 0, ClaveBaja1, ClaveAlta1;
|FPRC;

|PROCESO Tipo11m0;  |TIPO 11;
     |SI FSalida = 10;
         |ENTLINEAL #1#1, -3, 4, 22, 0, ClaveBaja1, ClaveAlta1;
     |FINSI;

     |SI FSalida = 11;
         |CONFI "2400NEsta seguro de dar de Baja los Documentos";
         |SI FSalida = 0;
             |BUCLE megwordl;

             |LEE 101#0=;
             |SI FSalida = 0;
                 |BORRA 020#0a;
             |FINSI;
             |LIBERA #0;

             |PTEC 812;
         |FINSI;
     |FINSI;

     |SI FSalida = 13;
         |CONSULTA_CLAVE #1#0;
     |FINSI;
|FPRC;

|RUTINA ClaveBaja0;
     #0#0 = 1;
     #0#1 = 1900;
|FRUT;

|RUTINA ClaveAlta0;
     #0#0 = 99999;
     #0#1 = 2999;
|FRUT;

|PROGRAMA;
     |CLS;
     |CABEZA "Gestion Documentos";
     |ENTLINEAL #1#0, -1, 4, 22, 1, ClaveBaja0, ClaveAlta0;
|FPRO;
