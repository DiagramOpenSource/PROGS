|FICHEROS;
   sodacoca #0;
   maacceso #1;
   sodacoli #2;
   agifigen #3;     || clientes general (agicli)
   agiconpa #4;     || control pases (agicli)
   canempre #5;     || empresas (agicont)
   agicontr #99;
   sodacotm #90;

   soda8w06 #500,MANTE;
|FIN;

|VARIABLES;
   &enPlan  = 0;
   &empAlta = 0;
   &empre = 0;
   &perio = 0;
   &plani = 0;
   &cuent = "";
   &siono = "";
   dire = "";
   nume1 = 0;
   nume2 = 0;
   nume3 = 0;
   nume4 = 0;
   alfa1 = "";
   alfa2 = "";
   alfa3 = "";
   alfa4 = "";
   inkey = 0;
   f1    = "823";
   tecla = "";
   opcion = 0;
    linord = 0;
    graord = 0;
    borord = 0;

  fFecha = @;

   &empresa  = 0;
   &anyo     = 0;
   &nombre   = "";

   &eaImp1 = "";
   &eaImp2 = "";
   &eaImp3 = "";
   &eaCeros = "";
   &eaICtas = "";
   nSwPtec806 = 0;
   sw = 0;
   aCuenta = "";
|FIN;

|PROCESO tipo4; |TIPO 4;
   |PUSHV 0548 1178;

   |PINPA #0#500;

   |SI #0#1 < 2008;
       |POPV;
       |PUSHV 0548 0778;
       |CUADRO 0548 0778;
       |ATRI R;
       |PINTA 0549, "Imprimir borrador de Balances";
       |ATRI I;
       |PINTA 0649 ," Entrada Gerenal de Datos:   ";
       #500#1 = "N"; |C_M #500#1, 1, "N"; |C_V #500#1, 1, "N";
       #500#2 = "N"; |C_M #500#2, 1, "N"; |C_V #500#2, 1, "N";
       #500#3 = "N"; |C_M #500#3, 1, "N"; |C_V #500#3, 1, "N";
       #500#4 = "N"; |C_M #500#4, 1, "N"; |C_V #500#4, 1, "N";
   |FINSI;

   |PINDA #0#500;
   |ENDATOS #2#500;
   |CONFI "    SCONFORME S/N";
   |SI FSalida ! 0;
       |POPV;
   |SINO;
       |POPV;

       empresa = #0#0;
       anyo    = #0#1;
       eaImp1  = #500#0;
       eaImp2  = #500#1;
       eaImp3  = #500#2;
       eaCeros = #500#3;
       eaICtas = #500#4;
       |SUB_EJECUTA_NP "soda8y03;BORRADOR";
   |FINSI;
|FPRC;

|CALCULO defecto; |TIPO 7;
   fFecha = ~;
   alfa1 = fFecha;
   alfa1 = alfa1 % -104;
   nume1 = alfa1;
   #0Campo = nume1;
|FCAL;

|CALCULO pintanom;
  |ABRE #1;
  |SELECT #1#1;
  #1#0 = #0#0;
  #1#1 = #0#1;
  |LEE 040#1=;
  |SI FSalida = 0;
      #0#2 = #1#2;
      |PINTA #0#2;
  |SINO;
      |MENAV "     No existe Empresa.";
      |ERROR;
  |FINSI;
  |CIERRA #1;
|FCAL;

|CALCULO lineasli;
  alfa1 = #2#3; alfa1 = alfa1 % 103;
  #0#4 = #0#4 + #2#6;
  #0#5 = #0#5 + #2#7;
  #0#6 = #0#6 + #2#8;
  |SI alfa1 ! "129";
      #0#3 = #0#3 + #2#5;
      #0#7 = #0#7 + #2#9;
  |FINSI;
  |PINTA #0#3;
  |PINTA #0#4;
  |PINTA #0#5;
  |PINTA #0#6;
  |PINTA #0#7;
|FCAL;

|CALCULO recalcul; |TIPO 3;
   nSwPtec806 = 0;
  |CONFI "2400NRecalcular importes Cabecera : ";
  |SI FSalida = 0;
      #0#3 = 0; #0#4 = 0; #0#5 = 0; #0#6 = 0; #0#7 = 0;
      |BUCLELIN 0lineasli#0;
  |FINSI;
  |HAZ ordenacion;
|FCAL;

|CALCULO tipo30; |TIPO 30;
  |CONFI "2400NImprimir borrador de Balances: ";
  |SI FSalida = 0;
      |HAZ tipo4;
  |FINSI;
  |SI nSwPtec806 = 1;
      |PTEC 806;
  |FINSI;
   nSwPtec806 = 0;
|FCAL;
____________________________________/ TRASPASO DESDE AGICONT
|PROCESO lee_empresas1;
    nume1 = #0#1 $ 100;
|SI nume1 = #5#26;
        perio = #5#3;
    |ERROR10;
|FINSI;
|FPRC;

|DEFBUCLE lee_empresas;
 #5#1;
 ;
 nume2, 0;
 nume2, 9;
 ;
 NULL, lee_empresas1;
|FIN;

|PROCESO busca_perio;
    perio = -1;
    nume2 = #4#5;
|BUCLE lee_empresas;
|FPRC;

|PROCESO agiconpa1;
|SI #4#2 = "I1";
    |CONFI "    NConfirme el Traspaso desde AGICONT (S/N): ";
    |SI FSalida = 0;
        |HAZ busca_perio;
        |SI perio ! -1;
            empAlta = #3#0;
            empre = #4#5;
            plani = #4#17;
            cuent = #4#18;
            siono = "SI";
            |CIERRAT #0;
            |SUB_EJECUTA "soagico1.tab";
            |ABRET #0;
            |PTEC 802;
        |SINO;
            alfa1 = "    A¤o " + #0#1 + " SIN definir en AGICONT ...";
            |MENAV alfa1;
        |FINSI;
    |FINSI;
|FINSI;
|FPRC;

|DEFBUCLE agiconpa;
 #4#1;
 ;
 #3#0,  1;
 #3#0, 99;
 ;
 NULL, agiconpa1;
|FIN;

|CALCULO traspaso; |TIPO 0;
opcion = (FEntrada / 100) ! 0;
enPlan = 2008; |SI #0#1 < 2008; enPlan = 1990; |FINSI;
|SI opcion ! 1; |ACABA; |FINSI;

|MENSA "0415 Pulse <F1> para traspaso desde Agicont";
|LEETECLA tecla;
f1 = &255 + "823";
|BLIN 04;

    inkey = FSalida;
|SI tecla = f1;
    dire = "";
    |DBASS dire;  |QBF dire;
    |PATH_DAT #99 dire;
    |ABRE #99;
    |LEE 040#99p;
    |SI FSalida ! 0; |DDEFECTO #99; |FINSI;
    |CIERRA #99;

    alfa1 = #99#8; |QBF alfa1;
    |PATH_DAT #3 alfa1;
    |PATH_DAT #4 alfa1;

    alfa1 = #99#7;  |QBT alfa1;
    |PATH_DAT #5 alfa1;

    |ABRE #3;
        #3#0 = #0#0;
    |LEE 000#3=;
    |SI FSalida = 0;
        |BUCLE agiconpa;
    |SINO;
        |MENAV "    Empresa inexistente en Clientes General ...";
    |FINSI;
    |CIERRA #3;
|FINSI;
|FCAL;

____________________________________/ PROCESOS DE ORDENACION POR CUENTA
|PROCESO lee_tempo1;
    linord = linord + 1;

    #2#0 = #90#0;
    #2#1 = #90#1;
    #2#2 = linord;
    #2#3 = #90#3;
    #2#4 = #90#4;
    #2#5 = #90#5;
    #2#6 = #90#6;
    #2#7 = #90#7;
    #2#8 = #90#8;
    #2#9 = #90#9;
    |GRABA 020#2n;
    |SI FSalida = 0;
        graord = graord + 1;
    |FINSI;
    |SI aCuenta = #2#3; |Y aCuenta ! "";
        sw = 1;
    |FINSI;
    aCuenta = #2#3;
|FPRC;

|DEFBUCLE lee_tempo;
 #90#1;
 ;
 #0#0, #0#1, "            ", 0000;
 #0#0, #0#1, "zzzzzzzzzzzz", 9999;
 ;
 NULL, lee_tempo1;
|FIN;

|PROCESO gra_tempo;
    |DDEFECTO #90;
    #90#0 = #2#0;
    #90#1 = #2#1;
    #90#2 = #2#2;
    #90#3 = #2#3;
    #90#4 = #2#4;
    #90#5 = #2#5;
    #90#6 = #2#6;
    #90#7 = #2#7;
    #90#8 = #2#8;
    #90#9 = #2#9;
    |GRABA 020#90n;
    |SI FSalida = 0;
        |BORRA 020#2a;
        |SI FSalida = 0;
            borord = borord + 1;
        |FINSI;
    |FINSI;
|FPRC;

|PROCESO ordenacion; |TIPO 0;
    opcion = (FEntrada / 100) ! 0;
    |SI opcion ! 2; |ACABA;  |FINSI;        || solo en modificacion

    |AVISO;
    |CONFI "    NOrdenar las lineas por Cuentas Contables: ";
    |SI FSalida ! 0;    |ACABA;   |FINSI;

    |ATRI P;  |MENSA "    Ordenando ...";   |ATRI 0;
    linord = 0;
    graord = 0;
    borord = 0;
    |LEE 121#0=;                  || bloquea la cabecera
    alfa1 = Usuario;
    |QBF alfa1;
    |NOME_DAT #90 alfa1;
    |DELINDEX #90;
    |ABRE #90;
    |CIERRAT #2;
    |BUCLELIN 0gra_tempo#0;       || graba en temporal y borra en sodacoli
    |ABRET #2;
    |CIERRA #90;
    sw = 0;
    aCuenta = "";
    |BUCLE lee_tempo;             || lee de temporal y graba en sodacoli
    |DELINDEX #90;
    |SI linord = 0;
        |MENAV "    Asegurese que existe la empresa y a¤o informados ...";
    |SINO;
        |SI borord ! graord;
            alfa1 = "    Problemas: borrados [" + borord + "], grabados [" + graord + "] ...";
            |MENAV alfa1;
        |FINSI;
        |SI sw ! 0;
            alfa1 = "    Problemas: Existen Cuentas repetidas, repase sus datos";
            |MENAV alfa1;
        |FINSI;
    |FINSI;
    |ATRI R;  |BLIN 4;   |ATRI 0;
    |LIBERA #0;
    nSwPtec806 = 1;
|FPRC;
