|FICHEROS;
     zregcu22 #1022;
     zw000015 #915,MANTE;
|FIN;

|VARIABLES;
     nEstado        = 0;
     nPan           = 0;
     nBoton1        = 0;
     nBoton2        = 0;
     nBoton3        = 0;
     nGrid          = 0;
     nRango         = 0;
     nIdSalir       = 0;
     nCampo         = 0;
     nModo          = 0;
     nVentana       = 0;
     nID            = 0;

     aID            = "";
     aAlfa          = "";
     aEsc1          = "";
     aEsc2          = "";
     aRetu          = "";
     aTecla         = "";

     {-1}aVent      = "";
         aVent1     = 0;
         aVent2     = 0;
         aVent3     = 0;
         aVent4     = 0;
         aVent5     = "";

     {2}nNivel      = 0;

     {15} aPopup    = "";

     &enEmpresa     = 0;
     &enEjerCuadro  = 0;
     &enCalcCif     = 0;
     &eaVarDni      = "";
|FIN;

|PROCESO Tipo66C4w15;  |TIPO 66;
     aAlfa = "|C004WID";
     aID   = #915#aAlfa#;
     nID   = aID;

     aPopup1  = nID;  || Si fuera -1 en la posicion
     aPopup2  = 7;
     aPopup3  = "Entidad dominante";
     aPopup4  = "Empresa del grupo";
     aPopup5  = "Negocios conjuntos";
     aPopup6  = "Empresas asociadas";
     aPopup7  = "Empresas con control conjunto";
     aPopup8  = "Personal clave de la direcci¢n de la empresa";
     aPopup9  = "Otra parte vinculada";

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
         IaPopup  = aPopup3  <-;
         IaPopup  = IaPopup + (FSalida - 1);

         aAlfa = aPopup % 150;
         #915#4 = aAlfa;  |PINTA #915#4;
     |FINSI;
|FPRC;

|PROCESO Tipo0C4w15;   |TIPO 0;
     aAlfa = #915#4;  |QBF aAlfa;

     aPopup3  = "Entidad dominante";
     aPopup4  = "Empresa del grupo";
     aPopup5  = "Negocios conjuntos";
     aPopup6  = "Empresas asociadas";
     aPopup7  = "Empresas con control conjunto";
     aPopup8  = "Personal clave de la direcci¢n de la empresa";
     aPopup9  = "Otra parte vinculada";

     |SI aAlfa ! aPopup3;  |Y  aAlfa ! aPopup4;  |Y  aAlfa ! aPopup5;
     |Y  aAlfa ! aPopup6;  |Y  aAlfa ! aPopup7;  |Y  aAlfa ! aPopup8;
     |Y  aAlfa ! aPopup9;
         |MENAV "0000Contenido err¢neo, consulte los posibles valores";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo0C10;  |TIPO 0;
     aAlfa = #915#10;  |QBF aAlfa;

     |C_M #915#11, 1, "S";
     |C_M #915#12, 1, "S";

     |SI aAlfa = "";
         |C_M #915#11, 1, "N";  #915#11 = 0;
         |C_M #915#12, 1, "N";
     |FINSI;

     |PINTA #915#11;
     |PINTA #915#12;
|FPRC;

|PROCESO Tipo12Cuadros;  |TIPO 12;
|FPRC;

|PROCESO Baja1022;
     #1022#0 = enEmpresa;
     #1022#1 = enEjerCuadro;
     #1022#2 = 001;
|FPRC;

|PROCESO Alta1022;
     #1022#0 = enEmpresa;
     #1022#1 = enEjerCuadro;
     #1022#2 = 999;
|FPRC;

|PROCESO Evento1022;
     |SI nModo ! 0;  |ACABA;  |FINSI;

     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#1022=;
         nEstado = FSalida;
         |SI FSalida ! 0;  |O #1022#0 ! enEmpresa;  |O #1022#1 ! enEjerCuadro;
             |DDEFECTO #1022;
             nEstado = 1;
         |FINSI;

         |PARA nCampo = 0;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               #915nCampo = #1022nCampo;

               |C_M #915nCampo, 1, "N";   |PINTA #915nCampo;
         |SIGUE;

         FSalida = 0;

         |SI nEstado = 0;
             |ESTADO_CONTROL nBoton2, "ENABLE";
             |ESTADO_CONTROL nBoton3, "ENABLE";
         |SINO;
             |ESTADO_CONTROL nBoton2, "DISABLE";
             |ESTADO_CONTROL nBoton3, "DISABLE";
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Alta;
     nModo     = 1;
     nIdSalir  = 0;
     InNivel   = nNivel1<-;
     nNivel1   = 0;
     |ESPECIFICA "IDBOTONTECLA", nNivel;

     nIdSalir = nNivel2;
     |ESTADO_CONTROL nIdSalir, "HIDE";

     |ESTADO_CONTROL nBoton1,   "DISABLE";
     |ESTADO_CONTROL nBoton2,   "DISABLE";
     |ESTADO_CONTROL nBoton3,   "DISABLE";
     |ESTADO_CONTROL nGrid,     "DISABLE";

     |DDEFECTO #915;

     #1022#0 = enEmpresa;
     #1022#1 = enEjerCuadro;
     #1022#2 = 999;
     |LEE 000#1022];
     |SI FSalida = 0;
         |LEE 000#1022a;
     |SINO;
         |LEE 000#1022u;
     |FINSI;

     #915#0 = enEmpresa;
     #915#1 = enEjerCuadro;
     #915#2 = 1;
     |SI FSalida = 0;  |Y  #1022#0 = enEmpresa;  |Y  #1022#1 = enEjerCuadro;
          #915#2 = #1022#2 + 1;
     |FINSI;

     |PARA nCampo = 0;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
           |C_M #915nCampo, 1, "S";  |PINTA #915nCampo;
     |SIGUE;

     |C_M #915#0, 1, "N";
     |C_M #915#1, 1, "N";
     |C_M #915#2, 1, "N";  |PINTA #915#2;

     |ENDATOS #1#915;
     |SI FSalida = 0;
         |PARA nCampo = 0;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               #1022nCampo = #915nCampo;
         |SIGUE;
         |GRABA 020#1022n;
         |LIBERA #1022;
     |FINSI;

     |ESTADO_CONTROL nBoton1,   "ENABLE";
     |ESTADO_CONTROL nBoton2,   "ENABLE";
     |ESTADO_CONTROL nBoton3,   "ENABLE";
     |ESTADO_CONTROL nGrid,     "ENABLE";

     |REFRESCACONTROL nGrid;  |PULSATECLA;

     nModo     = 0;

     |FOCOTECLADO nGrid;

     |ESTADO_CONTROL nIdSalir, "SHOW";

     |PULSATECLA;
|FPRC;

|PROCESO Modificar;
     |LEE 101#1022=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     nModo     = 1;
     nIdSalir  = 0;
     InNivel   = nNivel1<-;
     nNivel1   = 0;
     |ESPECIFICA "IDBOTONTECLA", nNivel;

     nIdSalir = nNivel2;
     |ESTADO_CONTROL nIdSalir, "HIDE";

     |ESTADO_CONTROL nBoton1,   "DISABLE";
     |ESTADO_CONTROL nBoton2,   "DISABLE";
     |ESTADO_CONTROL nBoton3,   "DISABLE";
     |ESTADO_CONTROL nGrid,     "DISABLE";

     |DDEFECTO #915;

     |PARA nCampo = 0;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
           #915nCampo = #1022nCampo;

           |C_M #915nCampo, 1, "S";  |PINTA #915nCampo;
     |SIGUE;

     |C_M #915#0, 1, "N";
     |C_M #915#1, 1, "N";
     |C_M #915#2, 1, "N";  |PINTA #915#2;

     |ENDATOS #1#915;
     |SI FSalida = 0;
         |PARA nCampo = 0;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               #1022nCampo = #915nCampo;
         |SIGUE;
         |GRABA 020#1022a;
         |LIBERA #1022;
     |FINSI;

     |ESTADO_CONTROL nBoton1,   "ENABLE";
     |ESTADO_CONTROL nBoton2,   "ENABLE";
     |ESTADO_CONTROL nBoton3,   "ENABLE";
     |ESTADO_CONTROL nGrid,     "ENABLE";

     |REFRESCACONTROL nGrid;  |PULSATECLA;

     nModo     = 0;

     |FOCOTECLADO nGrid;

     |ESTADO_CONTROL nIdSalir, "SHOW";

     |PULSATECLA;
|FPRC;

|PROCESO Baja;
     |LEE 101#1022=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |CONFI "0000NSeguro que quiere BORRAR el apunte";
     |SI FSalida ! 0;  |LIBERA #1022;  |ACABA;  |FINSI;

     |BORRA 020#1022a;
     |LIBERA #1022;

     |REFRESCACONTROL nGrid;  |PULSATECLA;

     FSalida = "SKIPDEFAULT";

     FSalida = 1;  |HAZ Evento1022;
     |PULSATECLA;
     |FOCOTECLADO nGrid;
|FPRC;

|PROGRAMA;
     |VENTANA 0501, 2799, -1, 16, "Empresas del grupo, multigrupo y asociadas";
     nVentana = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |ABRET #1022;

     |PINPA #0#915;
     nPan = FSalida;

     #1022#0 = enEmpresa;
     #1022#1 = enEjerCuadro;
     #1022#2 = 1;
     |LEE 000#1022=;

     nRango = 4 + 8 + 16 + 32 + 128;

     |LINEAL_SIMPLE #1#1022, nRango, 0502, 1598, Baja1022, Alta1022, Evento1022;
     nGrid = FSalida;

     FSalida = 1;  |HAZ Evento1022;

     |CONTROL_SIMPLE nPan, "BOTON,{{193}}Alta ", 1664, 1674, Alta;
     nBoton1  = FSalida;

     |CONTROL_SIMPLE nPan, "BOTON,{{197}}Modificar ", 1676, 1686, Modificar;
     nBoton2  = FSalida;

     |CONTROL_SIMPLE nPan, "BOTON,{{199}}Borrar", 1688, 1698, Baja;
     nBoton3  = FSalida;

     |ESTADO_CONTROL nBoton2, "DISABLE";
     |ESTADO_CONTROL nBoton3, "DISABLE";

     |PINDA #0#915;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::{{001}}Salir," + nGrid;
          |LEETECLA aTecla;
          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
          |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;
     |DESTRUYECONTROL nGrid;

     |PULSATECLA;

     |CIERRAT #1022;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PULSATECLA;

     |FINVENTANA nVentana;
|FPRO;
