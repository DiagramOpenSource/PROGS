|FICHEROS;
    meimpfin #0;
    maempr01 #1;            || Fichero empresas memoria

    mem11fil #11;
    mem11fic #12;
    mem00002 #202;
    mem00004 #204;

    mew00012 #999;
|FIN;

|VARIABLES;
 &enEmpresa = 0;
 &enEjer    = 0;
 &eaMoneda  = "";

 informe    = "";
 fFecha     = @;
 aAlfa1     = "";
 aFichero   = "";

 x       = 0;
 sw      = 0;
 nDApar  = 0;
 nHApar  = 0;
 nSwOp   = 0;
 nContar = 0;
 nLinea  = 0;
 nEstado = 0;
 &nSwLinea = 0;
|FIN;

|PROCESO mayor;
     #0Campo = #0Campo > #0Campo;
     |PINTA #0Campo;
|FPRC;

|| /////////////////////////////////////////////////////////////////////
|PROCESO Auxiliar;
 nSwLinea = 0;
 |SI #999#2 > 90000;
 nSwLinea = 2;
 |FINSI;
 |PRINT;
 sw = 1;
|FPRC;

|DEFBUCLE Auxiliar;
 #999#1;
 ;
 enEmpresa, #0#12, 00001;
 enEmpresa, #0#12, 99999;
 ;
 NULL, Auxiliar;
|FIN;


|PROCESO LeeDatos;
 |SI nSwOp = 1;
     nSwLinea = 0;
     |SI #11#3=0; |Y #11#4=0; |Y #11#5=0;
         nSwLinea = 1;
     |FINSI;
     |SI #11#3 ] 90;
         nSwLinea = 2;
     |FINSI;
     |PRINT;
     sw = 1;
     |ACABA;
 |FINSI;

 |SI #11#3 = 0; |Y #11#4 = 0; |Y #11#5 = 0;
     |ACABA;
 |FINSI;

 |SI #11#3 ] 90;
     nLinea = #11#3 + 90000;
 |SINO;
     nContar = nContar + 1;
     nLinea = nContar;
 |FINSI;

 |DDEFECTO #999;
 #999#0 = enEmpresa;
 #999#1 = #0#12;
 #999#2 = nLinea;
 |LEE 001#999=;
 nEstado = FSalida;
 #999#0 = enEmpresa;
 #999#1 = #0#12;
 #999#2 = nLinea;

 |SI #11#2 = 1;
     #999#3 = 1;
     #999#4 = #11#6;
     #999#5 = #11#7;
     #999#6 = #11#8;
     #999#7 = #11#9;
     #999#8 = #11#10;
     #999#9 = #11#11;
 |SINO;
     #999#10 = 1;
     #999#11 = #11#6;
     #999#12 = #11#7;
     #999#13 = #11#8;
     #999#14 = #11#9;
     #999#15 = #11#10;
     #999#16 = #11#11;
 |FINSI;
 |SI nEstado = 0; |GRABA 020#999a; |FINSI;
 |SI nEstado ! 0; |GRABA 020#999n; |FINSI;
|FPRC;

|DEFBUCLE LeeDatos;
 #11#1;
 ;
 #12#0, #12#1, nDApar, 00, 00, 00;
 #12#0, #12#1, nHApar, 99, 99, 99;
 ;
 NULL, LeeDatos;
|FIN;


|PROCESO impretexto;

 #12#0 = enEmpresa;
 #12#1 = #0#12;
 |LEE 001#12=;
 |SI FSalida ! 0;
     |ACABA;
 |FINSI;

 |DDEFECTO #1;
 #1#0 = enEmpresa;
 #1#1 = #0#12;
 |LEE 001#1=;
 |SI FSalida ! 0;
     #1#0 = enEmpresa;
     #1#1 = #0#12;
     #1#2 = #12#2;
     #1#3 = #12#3;
     |ACABA;
 |FINSI;

 |SI #0#10 = "N";
     informe = "meimpfin";
 |SINO;
     informe = "meimpfip";
 |FINSI;
 |INFOR informe;
 |SI FSalida ! 0; |ACABA; |FINSI;

 sw = 0;
 |SI #0#10 = "N";
     nSwOp  = 1; || Imprimir
     nDApar = 1;
     nHApar = 1;
     |BUCLE LeeDatos;
     |PIEINF;
     nDApar = 2;
     nHApar = 2;
     |BUCLE LeeDatos;
 |SINO;
     |DELINDEX #999;
     |ABRE #999;
     nContar = 0;
     nSwOp  = 0; || Crear Auxiliar
     nDApar = 1;
     nHApar = 1;
     |BUCLE LeeDatos;
     nContar = 0;
     nDApar = 2;
     nHApar = 2;
     |BUCLE LeeDatos;
     |CIERRA #999;
     |BUCLE Auxiliar;
 |FINSI;

 |SI sw = 1; |PIEINF; |FINSI;
 |FININF;

 sw = 0;
 informe = "meimpfi3";
 |INFOR informe;
 |SI FSalida ! 0; |ACABA; |FINSI;

 nSwOp  = 1; || Imprimir
 nDApar = 3;
 nHApar = 3;
 |BUCLE LeeDatos;
 |SI sw = 1; |PIEINF; |FINSI;
 |FININF;

|FPRC;

|PROCESO impre;|TIPO 3;

     aFichero = "12w" + Usuario;
     |NOME_DAT #999 aFichero;

     |IMPRE 0;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |ABRE #1;
     |ABRE #12;
     |PARA x = 0; |SI x < 10; |HACIENDO x = x + 1;
          |SI #0x ! 0;
              enEmpresa = #0x;
              |HAZ impretexto;
          |FINSI;
     |SIGUE;
     |CIERRA #12;
     |CIERRA #1;
     |FINIMP;

     |DELINDEX #999;
|FPRC;

|PROGRAMA;
    |CLS;

    fFecha = ~;
    aAlfa1 = fFecha;
    aAlfa1 = aAlfa1 % -104;
    x      = aAlfa1;
    #0#12  = x;

    |SI enEmpresa ! 0;
         |DDEFECTO #0;
         #0#0    = enEmpresa
         #0#12   = enEjer;
         #0#11   = eaMoneda;
         |HAZ impre;
         |ACABA;
    |FINSI;

    |PINPA #0#0;
    |PINDA #0#0;
    |ENDATOS #1#0;
|FPRO;
