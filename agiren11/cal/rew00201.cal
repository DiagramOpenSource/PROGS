|FICHEROS;
     rew00201 #201;
     rem00101 #101;

     recofisc #2;
|FIN;

|VARIABLES;
     nCampo1      = 0;

     aAlfa        = "";
     aAlfa1       = "";
     aAlfa2       = "";
     aAlfa3       = "";
     aComa        = "";

     &enEmpresa   = 0;

     nCampo      = 0;
     nLCod       = 0;
     nLDes       = 0;
     nCalc       = 0;


     &eaTabla    = "";
     &eaDestino  = "";
     &eaAlfa     = "";
     &enPosicion = 0;
     &enComp     = 0;
     &enError    = 0;

     fHoy = @;
|FIN;

|PROCESO Tipo12Fw201;  |TIPO 12;
     |HAZ Tipo7C7Fw201;

     |SI #201#18 = "N";
         aAlfa = #201#20;
     |SINO;
         aAlfa = #201#35;
     |FINSI;

     |QBF aAlfa;

     |SI aAlfa = "";
         |MENAV "0000 Introduzca datos del domicilio habitual";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo7C7Fw201;  |TIPO 7;
     aAlfa1 = #201#2;  |QBF aAlfa1;
     aAlfa2 = #201#3;  |QBF aAlfa2;
     aAlfa3 = #201#4;  |QBF aAlfa3;
     |SI aAlfa1 ! "";  |O aAlfa2 ! "";  |O aAlfa3 ! "";
         aComa = ",";
     |SINO;
         aComa = "";
     |FINSI;

     aAlfa  = aAlfa1 + " " + aAlfa2 + aComa + " " + aAlfa3;
     #201#1 = aAlfa;
     |PINTA #201#1;
|FPRC;

|PROCESO Tipo0C6Fw201;
     |C_M #201#10, 1, "S";
     |SI #201#6 = "C";
         #201#10 = " ";  |PINTA #201#10;
         |C_M #201#10, 1, "N";
     |FINSI;
|FPRC;

|PROCESO PonSiglo20_201;
     aAlfa1    = (#201Campo % 106) + "19" + (#201Campo % -102);
     #201Campo = aAlfa1;
     |PINTA #201Campo;
|FPRC;

|PROCESO Tipo0C7Fw201;
     |SI #201Campo = "01.01.0000";  |ACABA;  |FINSI;

     fHoy = ~;

     |SI #201Campo > fHoy;    || nacimientos futuros como que no
          |HAZ PonSiglo20_201;    ||/se cambia sin preguntar
     |FINSI;

     |SI #201Campo ] "01.01.2000"; |Y #201Campo [ fHoy;     || presuntos ni¤os,
                                                            ||/se pregunta
         aAlfa = "0000SLa fecha puede ser incorrecta. Ha introducido un contribuyente menor de edad. Corregir siglo?";
         |CONFI aAlfa;
         |SI FSalida = 0;
              |HAZ PonSiglo20_201;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo0C8Fw201;
     nCampo1 = Campo + 1;
     |C_M #201nCampo1, 1, "S";
     |SI #201Campo = 0;
         |C_M #201nCampo1, 1, "N";
         #201nCampo1 = "N";  |PINTA #201nCampo1;
     |FINSI;
|FPRC;

|PROCESO Tipo0C15Fw201;
     |C_M #201#17, 1, "S";

     aAlfa = #201#15;  |QBF aAlfa;
     |SI aAlfa = "";
         |C_M #201#17, 1, "N";  #201#17 = "N";  |PINTA #201#17;
     |FINSI;
|FPRC;

|PROCESO rem00101Conv;
     aAlfa = "S";
|FPRC;

|DEFBUCLE rem00101Conv;
     #101#1;
     4, 7, 8;
     enEmpresa, 00, "DESCENDIENTE", "3", "3";
     enEmpresa, 99, "DESCENDIENTE", "3", "3";
     ;
     NULL, rem00101Conv;
|FIN;

|PROCESO Tipo7C10;  |TIPO 7;
     |C_M #201#10, 0, aAlfa;
     |SI aAlfa = "N";  |ACABA;  |FINSI;

     aAlfa = "N";
     |BUCLE rem00101Conv;

     |C_M #201#10, 1, "S";
     |SI aAlfa = "S";
         |C_M #201#10, 1, "N";
         #201#10 = "S";  |PINTA #201#10;
     |FINSI;
|FPRC;

|PROCESO CampoObligatorio; |TIPO 6;
     |C_M #201Campo, 0, aAlfa;
     |SI aAlfa = "N";  |ACABA;  |FINSI;

     aAlfa = #201Campo;  |QBF aAlfa;

     |SI aAlfa = "";
         |MENAV "0000Contenido obligatorio. ";
         |ERROR;
         |ACABA;
     |FINSI;
|FPRC;

|PROCESO Tipo0C21Fw201; |TIPO 0;
     |C_M #201#21, 0, aAlfa;
     |SI aAlfa = "N";  |ACABA;  |FINSI;

     |SI #201#21 ! "NUM";  |Y #201#21 ! "KM.";  |Y #201#21 ! "S/N";
      |Y #201#21 ! "OTR";  |Y #201#21 ! "   ";
         |MENAV "0000Contenido incorrecto.";
         |ERROR;
         |ACABA;
     |FINSI;

     |C_M #201#22, 1, "S";
     |SI #201#21 = "S/N";
         |C_M #201#22, 1, "N";   #201#22 = "0000";  |PINTA #201#22;
     |FINSI;
|FPRC;

|PROCESO Tipo7C19Fw201; |TIPO 7;
     |C_M #201#19, 0, aAlfa;
     |SI aAlfa = "N";  |ACABA;  |FINSI;

     eaTabla = "zsiglas.txt";
     nLCod   = 5;
     nLDes   = 15;
     |HAZ CargaTabla;
|FPRC;

|PROCESO Tipo66C19Fw201;  |TIPO 66;
     nCampo   = Campo + 1;

     |HAZ SacaTabla;
     nCalc = 100 + nLCod;
     aAlfa1   = eaDestino % nCalc;
     #201Campo  = aAlfa1;
     |PINTA #201Campo;
|FPRC;

|PROCESO Tipo0C19Fw201;  |TIPO 0;
     |C_M #201#19, 0, aAlfa;
     |SI aAlfa = "N";  |ACABA;  |FINSI;

     nCampo   = Campo + 1;

     |SI #201Campo = "     ";
         |MENAV "0000 Contenido Incorrecto. Consulta la Tabla con F2";
         |ERROR; |ACABA;
     |FINSI;

     enComp = nLCod;
     aAlfa  = "|C" + (("000" + Campo) % -103) + "TIPO";
     aAlfa1 = #201#aAlfa#;
     aAlfa  = #201Campo;   |QBF aAlfa;

     |SI aAlfa1 = "N";
         nCalc = - (100 + nLCod);
         aAlfa = (("0" * enComp) + aAlfa) % nCalc;
     |FINSI;

     |SI aAlfa1 ! "N";
         nCalc = 100 + nLCod;
         aAlfa = (aAlfa + (" " * enComp)) % nCalc;
     |FINSI;
     eaAlfa = aAlfa;
     |HAZ CompruebaTabla;

     |SI enError = 1;
         |MENAV "0000 Contenido Incorrecto. Consulta la Tabla con F2";
         |ERROR; |ACABA;
     |FINSI;

     nCalc      = 100 + nLCod;
     aAlfa1     = eaDestino % nCalc;
     nCalc      = ((nLCod + 2) * 100) + nLDes;
     aAlfa2     = eaDestino % nCalc;
     #201Campo  = aAlfa1;

     |PINTA #201Campo;

     |HAZ FinTabla;
|FPRC;

|PROCESO Tipo0C18Fw201;  |TIPO 0;
     |PARA nCampo = 19;  |SI nCampo [ 45;  |HACIENDO nCampo = nCampo + 1;
           |C_M #201nCampo, 1, "S";
     |SIGUE;

     |C_M #201#34, 1, "N";
     |C_M #201#44, 1, "N";

     |SI #201#18 = "N";
         |PARA nCampo = 35;  |SI nCampo [ 42;  |HACIENDO nCampo = nCampo + 1;
               |C_M #201nCampo, 1, "N";  #201nCampo = "";  |PINTA #201nCampo;
         |SIGUE;
     |SINO;
         aAlfa1 = #201#20;   |QBF aAlfa1;
         aAlfa2 = #201#35;   |QBF aAlfa2;

         |SI aAlfa2 = "";
             |SI aAlfa1 ! "";
                 aAlfa = #201#19;  |QBF aAlfa;
                 |SI aAlfa ! "";  aAlfa = aAlfa + " ";  |FINSI;
                 aAlfa = aAlfa + #201#20;   |QBF aAlfa;
                 |SI #201#22 ! "    ";  aAlfa = aAlfa + " " + #201#22;   |FINSI;
                 |SI #201#26 ! "   ";   aAlfa = aAlfa + " " + #201#26;   |FINSI;
                 |SI #201#27 ! "  ";    aAlfa = aAlfa + " " + #201#27;   |FINSI;
                 |SI #201#28 ! "  ";    aAlfa = aAlfa + " " + #201#28;   |FINSI;

                 #201#35 = aAlfa;    |PINTA #201#35;
                 #201#36 = #201#29;  |PINTA #201#36;
                 #201#37 = #201#33;  |PINTA #201#37;
                 #201#39 = (("00" + #201#31) % -102) + (("000" + #201#32) % -103);  |PINTA #201#39;
                 #201#40 = #201#34;  |PINTA #201#40;
             |FINSI;
         |FINSI;

         |PARA nCampo = 19;  |SI nCampo [ 34;  |HACIENDO nCampo = nCampo + 1;
               |C_M #201nCampo, 1, "N";  #201nCampo = "";  |PINTA #201nCampo;
         |SIGUE;

         |C_M #201#45, 1, "N";  #201#45 = "";  |PINTA #201#45;
     |FINSI;
|FPRC;

|PROCESO Tipo0C31Fw201;
     |ABRE #2;
     #2#0 = #201#31;
     |LEE 040#2=;
     |SI FSalida ! 0;  |DDEFECTO #2;  |FINSI;
     |CIERRA #2;

     #201#43 = #2#3;
     #201#44 = #2#4;

     |PINTA #201#43;
     |PINTA #201#44;
|FPRC;
