|FICHEROS;
   rem01002 #10;

   Referen@ #1000;
|FIN;

|VARIABLES;
   &enEmpr     = 0;
   &eaTipo     = "";
   &enConDatos = 0;
   &enNumPags  = 0;
   &eaFicheros = "";
   &eaCampos   = "";
   &eaCasillas = "";
   &enPagina   = 0;

   Bloque      = 0;
   SwError     = 0;
   nPYG        = 0;
   nCalc       = 0;
   nContCas    = 0;
   nContRegs   = 0;
   nNumPags    = 0;
   nApartados  = 0;
   nDCampo     = 0;
   nHCampo     = 0;
   nVuelta     = 0;

   aTipo       = "";
   Casilla     = "";
   aAlfa       = "";
   aAlfa1      = "";
   aAlfa2      = "";
   aAlfaC1     = "";
   aAlfaC2     = "";

   aDCasilla   = "";
   aHCasilla   = "";
|FIN;

|PROCESO BuscaDato;
   #rem01002#0 = enEmpr;
   #rem01002#1 = " ";
   #rem01002#2 = eaTipo;
   #rem01002#3 = Bloque;
   #rem01002#4 = Casilla;
   |LEE 010#rem01002.=;
   |SI FSalida = 0;
       Casilla = #rem01002#6;
       SwError = 0;
   |SINO;
       Casilla = 0; SwError = 1;
   |FINSI;
|FPRC;

|PROCESO MiraSiEntra;
   enConDatos = 0; enNumPags = 1; nPYG = 0;

   |SI eaFicheros = "";
      Bloque = 0;
      aAlfa = eaCasillas; |QBF aAlfa;
      |PARA; |SI aAlfa ! ""; |HACIENDO;
         aAlfa2 = aAlfa - ",";
         |SI FSalida ! 0;
            nCalc = ((FSalida / 100) ! 0) + 99;
            aAlfa1 = aAlfa2 % nCalc; aAlfa2 = aAlfa1 + ","; aAlfa = aAlfa - aAlfa2;
         |SINO;
            aAlfa1 = aAlfa; aAlfa = "";
         |FINSI;

         |QBT aAlfa1; aAlfa2 = aAlfa1;
         aAlfa2 = aAlfa2 - "B";                || Miro si selecciono bloque
         |SI FSalida ! 0;
            nCalc = FSalida + 4; aAlfa2 = aAlfa1 % nCalc;
            aAlfa1 = aAlfa1 - aAlfa2;
            aAlfa2 = aAlfa2 - ":"; aAlfa2 = aAlfa2 - "B"; Bloque = aAlfa2;
            aAlfa2 = aAlfa1;
         |FINSI;

         aAlfa2 = aAlfa2 - "-";                || Miro si hay intervalo
         |SI FSalida ! 0;
            nCalc = FSalida + 98; aAlfa2 = aAlfa2 % nCalc; aHCasilla = aAlfa2;
            aAlfa2 = "-" + aAlfa2; aAlfa1 = aAlfa1 - aAlfa2;
            aDCasilla = aAlfa1;
         |SINO;
            aDCasilla = aAlfa1; aHCasilla = aAlfa1;
         |FINSI;

         |SI aDCasilla = aHCasilla;
            Casilla = aDCasilla; |QBF Casilla; Casilla = ("000" + Casilla) % -103;
            |HAZ BuscaDato;
            |SI SwError = 0;
               |QBF Casilla;
               |SI Casilla ! ""; enConDatos = 1; |SAL; |FINSI;
            |FINSI;
         |SINO;
            |PARA nContCas = aDCasilla; |SI nContCas [ aHCasilla; |HACIENDO nContCas = nContCas + 1;
               Casilla = nContCas; |QBF Casilla; Casilla = ("000" + Casilla) % -103;
               |HAZ BuscaDato;
               |SI SwError = 0;
                  |QBF Casilla;
                  |SI Casilla ! ""; |Y Casilla ! "0"; |Y Casilla ! "0.00"; enConDatos = 1; |SAL; |FINSI;
               |FINSI;
            |SIGUE;
            |SI enConDatos ! 0; |SAL; |FINSI;
         |FINSI;
      |SIGUE;
   |SINO;
      enNumPags = 0;

      aAlfa = eaFicheros; |QBF aAlfa;
      |PARA; |SI aAlfa ! ""; |HACIENDO;
         aAlfa2 = aAlfa - ",";
         |SI FSalida ! 0;
            nCalc = ((FSalida / 100) ! 0) + 99;
            aAlfa1 = aAlfa2 % nCalc; aAlfa2 = aAlfa1 + ","; aAlfa = aAlfa - aAlfa2;
         |SINO;
            aAlfa1 = aAlfa; aAlfa = "";
         |FINSI;

         nApartados = 1;
         |QBT aAlfa1;
         aAlfa2 = aAlfa1 - "(";
         |SI FSalida ! 0;
            nCalc = FSalida + 100; aAlfa2 = aAlfa1 % nCalc;
            nApartados = aAlfa2; aAlfa2 = "(" + aAlfa2 + ")";
            aAlfa1 = aAlfa1 - aAlfa2;
         |FINSI;

         |SI aAlfa1 = "rem01032"; nPYG = 1; |FINSI;

         |DDEF aAlfa2; |QBF aAlfa2;
         aAlfa2 = aAlfa2 + aAlfa1 + ".mas";
         |CARGA_DEF aAlfa2, Referen@;
         |SI FSalida = 0;
            nContRegs = nApartados; nNumPags = 0;
            |ABRE #Referen@;
            #Referen@#0 = enEmpr;
            #Referen@#1 = 0;
            #Referen@#2 = eaTipo; aTipo = #Referen@#2;
            #Referen@#3 = 0;
            |LEE 000#Referen@.];
            |PARA; |SI FSalida = 0; |Y #Referen@#0 = enEmpr; |Y #Referen@#1 = 0; |Y #Referen@#2 = aTipo; |HACIENDO;
               |SI nPYG = 0;
                  |SI eaCampos = "";
                     nContRegs = nContRegs + 1; enConDatos = 1;
                     |SI nContRegs > nApartados;
                        nNumPags = nNumPags + 1; nContRegs = 1;
                     |FINSI;
                  |SINO;
                     aAlfaC1 = eaCampos; |QBF aAlfaC1;
                     aAlfaC2 = aAlfaC1 - "-";
                     |SI FSalida ! 0;
                        nCalc = FSalida + 98; aAlfaC2 = aAlfaC2 % nCalc;
                        nHCampo = aAlfaC2; aAlfaC2 = "-" + aAlfaC2;
                        aAlfaC1 = aAlfaC1 - aAlfaC2;
                        nDCampo = aAlfaC1;
                     |SINO;
                        nDCampo = aAlfaC2; nHCampo = aAlfaC2;
                     |FINSI;
                     |PARA nContCas = nDCampo; |SI nContCas [ nHCampo; |HACIENDO nContCas = nContCas + 1;
                        aAlfaC2 = #Referen@#nContCas#; |QBF aAlfaC2;
                        |SI aAlfaC2 ! ""; |Y aAlfaC2 ! "0"; |Y aAlfaC2 ! "0.00";
                           nContRegs = nContRegs + 1; enConDatos = 1;
                           |SI nContRegs > nApartados;
                              nNumPags = nNumPags + 1; nContRegs = 1;
                           |FINSI;
                           |SAL;
                        |FINSI;
                     |SIGUE;
                  |FINSI;
               |SINO;
                  || Ganancias
                  |SI nVuelta = 1; aAlfaC2 = #Referen@#6; |FINSI;
                  || Perdidas
                  |SI nVuelta = 2; aAlfaC2 = #Referen@#8; |FINSI;
                  |QBF aAlfaC2;

                  |SI aAlfaC2 ! ""; |Y aAlfaC2 ! "0"; |Y aAlfaC2 ! "0.00";
                     nContRegs = nContRegs + 1; enConDatos = 1;
                     |SI nContRegs > nApartados;
                        nNumPags = nNumPags + 1; nContRegs = 1;
                     |FINSI;
                  |FINSI;
               |FINSI;
               |LEE 000#Referen@.s;
            |SIGUE;
            |SI nNumPags > enNumPags; enNumPags = nNumPags; |FINSI;
            |CIERRA #Referen@; |DESCARGA_DEF #Referen@;
         |FINSI;
         |SI nPYG = 1; nVuelta = nVuelta + 1; |FINSI;
      |SIGUE;
   |FINSI;
|FPRC;

|PROCESO MiraObligatoria;
   |ABRE #rem01002;

   enNumPags = 1;
   || Pagina 1  (OBLIGATORIA)
   |SI enPagina = 1; enConDatos = 1; |FINSI;

   || Pagina 2  (OBLIGATORIA)
   |SI enPagina = 2; enConDatos = 1; |FINSI;

   || Pagina 3  (Casillas 1 a 8, 14, 16 a 20, 29, 30, 32, 45, 46, 48)
   |SI enPagina = 3;
      eaCasillas = "B001:1-8,14,16-20,B002:29,30,32,B003:45,46,48";
      |HAZ MiraSiEntra;
   |FINSI;

   || Pagina 4  (Datos en rem01011, rem01013, y rem01015)
   |SI enPagina = 4;
      eaFicheros = "rem01011(3),rem01013(3),rem01015(3)";
      |HAZ MiraSiEntra;
   |FINSI;

   || Pagina 5  (Datos en rem01014 o casillas 136 a 138)
   |SI enPagina = 5;
      eaFicheros = "rem01014(3)";
      |HAZ MiraSiEntra;
      |SI enConDatos = 0;
         eaCasillas = "B005:136-138"; eaFicheros = "";
         |HAZ MiraSiEntra;
      |FINSI;
   |FINSI;

   || Pagina 6  (Datos en rem01019 o casillas 168 a 170)
   |SI enPagina = 6;
      eaFicheros = "rem01019(2)";
      |HAZ MiraSiEntra;
      |SI enConDatos = 0;
         eaCasillas = "B006:168-170"; eaFicheros = "";
         |HAZ MiraSiEntra;
      |FINSI;
   |FINSI;

   || Pagina 7  (Datos en rem01021 o casillas 195 o 196)
   |SI enPagina = 7;
      eaFicheros = "rem01021(2)";
      |HAZ MiraSiEntra;
      |SI enConDatos = 0;
         eaCasillas = "B007:195-196"; eaFicheros = "";
         |HAZ MiraSiEntra;
      |FINSI;
   |FINSI;

   || Pagina 8  (Datos en rem01022, remn01023 o rem01024 o casillas 220 a 227, 746, 245, 747, 255, 265 o 275)
   |SI enPagina = 8;
      eaFicheros = "rem01022(3),rem01023(3),rem01024(3),rem01025(3)";
      |HAZ MiraSiEntra;
      |SI enConDatos = 0;
         eaCasillas = "B008:220-227,746,B009:245,747,B010:255,B011:265,B012:275"; eaFicheros = "";
         |HAZ MiraSiEntra;
      |FINSI;
   |FINSI;

   || Pagina 9  (Datos en rem01027, rem01039 o rem01031 o casillas 300 a 303 o 310 a 313)
   |SI enPagina = 9;
      eaFicheros = "rem01027(3),rem01039(3),rem01031(2)";
      |HAZ MiraSiEntra;
      |SI enConDatos = 0;
         eaCasillas = "B013:300-303,B014:310-313"; eaFicheros = "";
         |HAZ MiraSiEntra;
      |FINSI;
   |FINSI;

   || Pagina 10  (OBLIGATORIA)
   |SI enPagina = 10;
      || Es obligatoria, pero ejecuto el proceso MiraSiEntra para devolver las
      ||     hojas adicionales
      nVuelta = 1;
      eaFicheros = "rem01030(3),rem01032(3),rem01032(3)";
      |HAZ MiraSiEntra;
      nVuelta = 0; enConDatos = 1;
      |SI enNumPags = 0; enNumPags = 1; |FINSI;
   |FINSI;

   || Pagina 11  (OBLIGATORIA)
   |SI enPagina = 11;
      || Es obligatoria, pero ejecuto el proceso MiraSiEntra para devolver las
      ||     hojas adicionales
      nVuelta = 1;
      eaFicheros = "rem01037(2),rem01045(2),rem01035(2),rem01046(2),rem01047(2)";
      |HAZ MiraSiEntra;
      enConDatos = 1;
      |SI enNumPags = 0; enNumPags = 1; |FINSI;
   |FINSI;

   || Pagina 12  (OBLIGATORIA)
   |SI enPagina = 12; enConDatos = 1; |FINSI;

   || Pagina 13  (OBLIGATORIA)
   |SI enPagina = 13; enConDatos = 1; |FINSI;

   || Pagina 14  (OBLIGATORIA)
   |SI enPagina = 14; enConDatos = 1; |FINSI;

   || Pagina 15 : ANEXO A.1  (Datos en el rem01041)
   |SI enPagina = 15;
      eaFicheros = "rem01041(1)"; eaCampos = "3-43, 100-104"; |HAZ MiraSiEntra;
   |FINSI;

   || Pagina 16 : ANEXO A.2  (Datos en el rem01041 y rem01042)
   |SI enPagina = 16;
      eaFicheros = "rem01041(1)"; eaCampos = "47-62, 149-154, 126-140, 100-103"; |HAZ MiraSiEntra;
      |SI enConDatos = 0;
         eaFicheros = "rem01042(1)"; eaCampos = "3-89"; |HAZ MiraSiEntra;
      |FINSI;
   |FINSI;

   || Pagina 17 : ANEXO A.3  (Datos en el rem01042)
   |SI enPagina = 17;
      eaFicheros = "rem01042(1)"; eaCampos = "90-146"; |HAZ MiraSiEntra;
   |FINSI;

   || Pagina 18 : ANEXO B.1  (Datos en el rem01043, Campos del 3 al 45)
   |SI enPagina = 18;
      eaFicheros = "rem01043(1)"; eaCampos = "3-45"; |HAZ MiraSiEntra;
   |FINSI;

   || Pagina 19 : ANEXO B.2  (Datos en el rem01043, Campos del 46 al 86 y el 194)
   |SI enPagina = 19;
      eaFicheros = "rem01043(1)"; eaCampos = "46-86,194,206-212,218-220,231"; |HAZ MiraSiEntra;
   |FINSI;

   || Pagina 20 : ANEXO B.3  (Datos en el rem01043, Campos del 87 al 121 y del 195 al 200)
   |SI enPagina = 20;
      eaFicheros = "rem01043(1)"; eaCampos = "87-119,215-226"; |HAZ MiraSiEntra;
   |FINSI;

   || Pagina 21 : ANEXO B.4  (Datos en el rem01043, Campos del 122 al 156, 200 y 201)
   |SI enPagina = 21;
      eaFicheros = "rem01043(1)"; eaCampos = "122-156,200-201"; |HAZ MiraSiEntra;
   |FINSI;

   || Pagina 22 : I+D (OBLIGATORIA)
   |SI enPagina = 22; enConDatos = 1; |FINSI;

   eaCampos = ""; eaFicheros = "";

   |CIERRA #rem01002;
|FPRC;
