|FICHEROS;
     recapcso #0;
     rem00099 #1;
     rem00022 #2;
     rem00100 #11;

     agicontr #20;
     agim0033 #33;
     agim0034 #34;
|FIN;

|VARIABLES;
     aNombre     = "";
     aAlfa       = "";
     aAgicli     = "";
     aTipoDecl   = "";

     nLinea      = 0;
     nValor      = 0;
     nImporte    = 0;
     nSwBueno    = 0;
     nCampo      = 0;

     &eaPrograma = "";
     &eaPunto    = "";
     &enEmpresa  = 0;
     &eaLetra    = "";
|FIN;

|PROCESO Parrilla;  |TIPO 0;
     #0Campo = #0Campo > #0Campo;  |PINTA #0Campo;

     |SI #0#0 = "N";
                              |PINTA #0#1;  |C_M #0#1, 1, "S";
                              |PINTA #0#2;  |C_M #0#2, 1, "S";
         |PARA nCampo = 3;  |SI nCampo [ 26;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |PINTA #0nCampo;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         #0#1 = 1;      |PINTA #0#1;  |C_M #0#1, 1, "N";
         #0#2 = 99999;  |PINTA #0#2;  |C_M #0#2, 1, "N";
         |PARA nCampo = 3;  |SI nCampo [ 26;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO GrabaPat;
     nSwBueno = 0;
     |SI #0#0 = "S";
         |PARA nCampo = 3;  |SI nCampo [ 26;  |HACIENDO nCampo = nCampo + 1;
               |SI #0nCampo = #11#0;  nSwBueno = 1;  |SAL;  |FINSI;
         |SIGUE;
     |SINO;
         |SI #11#0 ] #0#1;  |Y #11#0 [ #0#2;  nSwBueno = 1;  |FINSI;
     |FINSI;

     |SI nSwBueno = 0;  |ACABA;  |FINSI;

     #33#0 = #34#0;
     #33#1 = #34#1;
     |LEE 040#33=;
     |SI FSalida ! 0;  |DDEFECTO #33;  |FINSI;

                             nImporte = #34#9;   nValor = #34#7;
     |SI #34#14 ] nImporte;  nImporte = #34#14;  nValor = #34#12;  |FINSI;
     |SI #34#20 ] nImporte;  nImporte = #34#20;  nValor = #34#17;  |FINSI;

     enEmpresa = #11#0;

     #2#0 = #11#0;
     #2#1 = 99;
     |LEE 040#2];
     |SI FSalida = 0;
         |LEE 040#2a;
     |SINO;
         |LEE 040#2u;
     |FINSI;

     |SI FSalida = 0; |Y #2#0 = #11#0;
         nLinea = #2#1 + 1;
     |SINO;
         nLinea = 1;
     |FINSI;

     |DDEFECTO #2;
     #2#0  = #11#0;
     #2#1  = nLinea;
     #2#2  = aTipoDecl;
     #2#3  = "P";
     #2#11 = "G4";
     #2#15 = nImporte;
     #2#17 = #33#2;
     #2#16 = nValor;

     |GRABA 020#2n;
     |LIBERA #2;

     eaPrograma   = "rem00022";
     eaPunto      = "";
     |HAZ ProgramaActivo;

     #1#0 = #11#0;
     #1#1 = "G4";
     #1#2 = 99;
     |LEE 040#1];
     |SI FSalida = 0;
         |LEE 040#1a;
     |SINO;
         |LEE 040#1u;
     |FINSI;

     |SI FSalida = 0; |Y #1#0 = #11#0;  |Y #1#1 = "G4";
         nLinea = #1#2 + 1;
     |SINO;
         nLinea = 1;
     |FINSI;

     #1#0  = #11#0;
     #1#1  = "G4";
     #1#2  = nLinea;
     #1#3  = aTipoDecl;
     #1#4  = "C0";
     #1#5  = #2#1;
     #1#6  = #33#2;
     #1#10 = nImporte;
     |GRABA 020#1n;
     |LIBERA #1;

     eaLetra      = "G4";
     eaPrograma   = "rem00099";
     eaPunto      = "G4";
     |HAZ ProgramaActivo;
|FPRC;

|PROCESO Miralos;
     |SELECT #2#11;
     #11#1 = #34#4;
     |LEE 040#11=;
     |SI FSalida = 0;
         aTipoDecl = "TITULAR";
         aNombre   = #11#5;
         |HAZ GrabaPat;
         |ACABA;
     |FINSI;

     |SELECT #3#11;
     #11#35 = #34#4;
     |LEE 040#11=;
     |SI FSalida = 0;
         aTipoDecl = "CONYUGE";
         aNombre   = #11#39;
         |HAZ GrabaPat;
         |ACABA;
     |FINSI;
|FPRC;

|DEFBUCLE agim0034;
     #34#1;
     ;
     00000, 2011,  1;
     99999, 2011, 99;
     ;
     NULL, Miralos;
|FIN;

|PROCESO Path;
     |DBASS aAlfa;  |QBF aAlfa;
     |PATH_DAT #20 aAlfa;
     |ABRE #20;
     |LEE 040#20p;
     |SI FSalida ! 0; |DDEFECTO #20; |FINSI;
     |CIERRA #20;

     aAgicli = #20#8; |QBF aAgicli;

     |PATH_DAT #33 aAgicli;
     |PATH_DAT #34 aAgicli;
|FPRC;

|PROCESO Tipo3;  |TIPO 3;
     |HAZ Path;

     |ABRE #1;
     |ABRE #2;
     |ABRE #11;
     |ABRE #33;

     |BUCLE agim0034;

     |CIERRA #1;
     |CIERRA #2;
     |CIERRA #11;
     |CIERRA #33;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     FSalida = 2499;
|FPRC;
