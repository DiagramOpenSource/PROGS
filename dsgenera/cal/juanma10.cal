|FICHEROS;
     juanma10 #0;
     juanma11 #1,MANTE;
     juanma12 #2,MANTE;
     juanma13 #3;

     referen@ #100;
|FIN;

|VARIABLES;
     aBase        = "";
     aAbre        = "";
     aDef         = "";
     aCarga       = "";
     aMensa       = "";
     aRelacion    = "";
     aQueQuiero   = "";
     aFichero     = "";
     aNombreDef   = "";
     aNumeroCampo = "";
     aNombreCampo = "";
     aTipoCampo   = "";
     aLongCampo   = "";
     aMinCampo    = "";
     aMaxCampo    = "";
     aAlfa        = "";
     aAlfa1       = "";
     aAlfa2       = "";
     aAlfa3       = "";
     aAlfa4       = "";
     aCaracter    = "";
     aLong        = "";
     aDirInfor    = "";
     aInformes    = "";
     aBuscar      = "";
     aCanal       = "";
     aVariable    = "";
     aCampo       = "";
     aEuro        = "";
     aDecimal     = "";
     aLongitud    = "";
     aOrden       = "";
     aRuta        = "";
     aCadena      = "";
     aSustituto   = "";

     nHandle      = 0;
     nContador    = 0;
     nSwPrint     = 0;
     nTCampo      = 0;
     nCampo       = 0;
     nLong        = 0;
     nPosi        = 0;
     nCaracter    = 0;
     nDecimales   = 0;
     nNume        = 0;
     nBoton1      = 0;
     nBoton2      = 0;
     nBoton3      = 0;
     nBoton4      = 0;
     nBoton5      = 0;
     nBoton6      = 0;
     nBoton7      = 0;
     nBoton8      = 0;
     nTecla       = 0;
     nSwHay       = 0;
     nSwHay2      = 0;
     nFic         = 0;
     nCanal       = 0;
     nFichero     = 0;
     nDecimal     = 0;
     nChequeo     = 0;
     nLongitud    = 0;
     nSustitu     = 0;
     nAsterisco   = 0;

     {99}iFichero = "";
|FIN;

|RUTINA ClaveBaja2;
     #2#0 = #1#0;
     #2#1 = 1;
|FRUT;

|RUTINA ClaveAlta2;
     #2#0 = #1#0;
     #2#1 = 999999;
|FRUT;

|PROCESO Base;  |TIPO 7;
     aBase = #0#0;               |QBF aBase;
     |SI aBase = "";
         |DBASS aBase;               |QBF aBase;
         #0#0 = aBase;
         |PINTA #0#0;
     |FINSI;
|FPRC;

|PROCESO Browse;  |TIPO 0;
     |SI FSalida = 10;
         aRuta = "D" + #0Campo;
         |BROWSE aRuta;
         #0Campo = aRuta % 280;
         |PINTA #0Campo;
         |ERROR;
         |ACABA;
     |FINSI;

     aAlfa  = #0Campo > "";   |QBF aAlfa;
     aAlfa1 = #0#1;           |QBF aAlfa1;
     aAlfa1 = aAlfa1 + "\DEF";
     aAlfa1 = aAlfa1 > "";   |QBF aAlfa1;
     aAlfa1 = aAlfa - aAlfa1;
     |SI aAlfa = aAlfa1;
         |MENAV "     Ubicacion Erronea";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO PonleAsterisco;
     aLong      = aAlfa % 0;
     nLong      = aLong;
     aCadena    = "";
     aSustituto = "";
     nSustitu   = 0;
     nDecimal   = 0;
     |PARA nCampo = 1;  |SI nCampo [ nLong;  |HACIENDO nCampo = nCampo + 1;
           nPosi     = (nCampo * 100) + 1;
           aCaracter = aAlfa % nPosi;

           |SI aCaracter = "%";  |Y nDecimal = 0;
               nDecimal = 1;
               aCadena = aCadena + aCaracter;
           |FINSI;

           |SI nDecimal = 0;
               aCadena = aCadena + aCaracter;
           |FINSI;

           |SI nDecimal = 1;
               |SI aCaracter ] "0";  |Y aCaracter [ "9";  |Y nSustitu = 0;
                   aSustituto = aSustituto + aCaracter;
                   aCadena = aCadena + aCaracter;
               |FINSI;
               |SI aCaracter = ".";  nSustitu = 1;  |FINSI;
               |SI aCaracter = ",";  nDecimal = 2;  |FINSI;
           |FINSI;

           |SI nDecimal = 2;
               aCadena  = aCadena + "." + aSustituto + "s";
               nDecimal = 3;
           |FINSI;

           |SI nDecimal = 3;
               aCadena = aCadena + aCaracter;
           |FINSI;
     |SIGUE;
     aAlfa = aCadena;
     #3#2  = aAlfa;
|FPRC;

|PROCESO Graba2;
     aLong     = aAlfa % 0;
     nLong     = aLong;
     aVariable = "";
     |PARA nCampo = 1;  |SI nCampo [ nLong;  |HACIENDO nCampo = nCampo + 1;
           nPosi     = (nCampo * 100) + 1;
           aCaracter = aAlfa % nPosi;
           |SI aCaracter = ",";  |SAL;  |FINSI;
           aVariable  = aVariable + aCaracter;
     |SIGUE;

     aAlfa1 = aVariable - "#";
     |SI aAlfa1 ! aVariable;
         aLong    = aAlfa % 0;
         nLong    = aLong;
         nFichero = 0;
         aCanal   = "";
         aCampo   = "";
         |PARA nCampo = 1;  |SI nCampo [ nLong;  |HACIENDO nCampo = nCampo + 1;
               nPosi     = (nCampo * 100) + 1;
               aCaracter = aVariable % nPosi;
               |SI aCaracter = "#";
                   |SI aCanal ! "";  nFichero = 1;  |FINSI;
               |SINO;
                   |SI nFichero = 0;
                       aCanal   = aCanal + aCaracter;
                   |SINO;
                       aCampo   = aCampo + aCaracter;
                   |FINSI;
               |FINSI;
         |SIGUE;

         nCanal    = aCanal;
         nCampo    = aCampo;
         IiFichero = iFichero1 <-;
         IiFichero = IiFichero + nCanal;
         aFichero  = iFichero;

         aCampo = "";
         aDef   = aDirInfor + aFichero + ".mas";
         |CARGA_DEF aDef, referen@;
         |SI FSalida = 0;
             aQueQuiero   = "|C" + (("000" + nCampo) % -103) + "NOMBRE";
             aCampo       = (#100#aQueQuiero# + (" " * 20)) % 120;

             aQueQuiero   = "|C" + (("000" + nCampo) % -103) + "MINIMO";
             aMinCampo    = (#100#aQueQuiero# + (" " * 20)) % 120;

             |DESCARGA_DEF #referen@;
         |SINO;
             |ACABA;
         |FINSI;

         aAlfa1 = aMinCampo - ".?EURO";
         |SI aAlfa1 = aMinCampo;  |ACABA;  |FINSI;

         aAlfa2  = aAlfa - "lf,";
         |SI aAlfa2 ! aAlfa;
             |HAZ PonleAsterisco;   || Variable Campo en EURO pero Decimales
                                    || sin asterisco.
             nAsterisco = 1;
         |SINO;
             |SI #0#3   = "I";  |ACABA;  |FINSI;
         |FINSI;

         aEuro   = "E";
         |QBF aAlfa1;
         aDecimal  = "0";
         aLongitud = aAlfa1 % 0;
     |SINO;
         |SI #0#3 = "F";  |ACABA;  |FINSI;
         aFichero = "";
         aCampo   = "VARIABLE INFORME";
         aEuro    = "N";
         aAlfa1   = aAlfa - "lf,";
         |SI aAlfa1 = aAlfa;  |ACABA;  |FINSI;

         aLong     = aAlfa % 0;
         nLong     = aLong;
         aDecimal  = "";
         nDecimal  = 0;
         nLongitud = 0;
         aLongitud = "";
         |PARA nCampo = 1;  |SI nCampo [ nLong;  |HACIENDO nCampo = nCampo + 1;
               nPosi     = (nCampo * 100) + 1;
               aCaracter = aAlfa % nPosi;

               |SI aCaracter = ".";
                   nDecimal = 1;
               |SINO;
                   |SI nDecimal = 1;
                       |SI aCaracter = "l";  |SAL;  |FINSI;
                       aDecimal = aDecimal + aCaracter;
                   |FINSI;
               |FINSI;

               |SI aCaracter = "%";
                   nLongitud = 1;
               |SINO;
                   |SI nLongitud = 1; |Y nDecimal = 0;
                       aLongitud = aLongitud + aCaracter;
                   |FINSI;
               |FINSI;
         |SIGUE;

         |SI aDecimal = "8";
             aDecimal = "0";
             aEuro    = "E";
         |FINSI;
     |FINSI;

     |DDEFECTO #2;
     #2#0 = #3#0;
     #2#1 = #3#1;
     #2#2 = aFichero;
     #2#3 = aCampo;
     #2#4 = aVariable;
     #2#5 = aEuro;
     #2#6 = aDecimal;
     #2#7 = aLongitud;
     |GRABA 020#2n;
     |LIBERA #2;
     nSwHay  = 1;
     nSwHay2 = 1;
|FPRC;

|PROCESO Trata02;
     aAlfa = #3#2;  |QBF aAlfa;
     |SI aAlfa = "FIC";  nFic = 1;  |ACABA;  |FINSI;
     |SI aAlfa = "VAR";  nFic = 0;  |ACABA;  |FINSI;

     |SI nFic = 1;
         aLong    = aAlfa % 0;
         nLong    = aLong;
         nFichero = 0;
         aFichero = "";
         aCanal   = "";
         |PARA nCampo = 1;  |SI nCampo [ nLong;  |HACIENDO nCampo = nCampo + 1;
               nPosi     = (nCampo * 100) + 1;
               aCaracter = aAlfa % nPosi;
               |SI aCaracter = "#";
                    nFichero = 1;
               |SINO;
                    |SI nFichero = 0;
                        aFichero = aFichero + aCaracter;
                    |SINO;
                        aCanal   = aCanal + aCaracter;
                    |FINSI;
               |FINSI;
         |SIGUE;
         |SI aCanal = "";  |ACABA;  |FINSI;

         nCanal    = aCanal;
         IiFichero = iFichero1 <-;
         IiFichero = IiFichero + nCanal;
         iFichero  = aFichero;
     |FINSI;

     aAlfa1 = aAlfa - ",,%";
     |SI aAlfa ! aAlfa1;  |ACABA;  |FINSI;

     aAlfa1 = aAlfa - ",%";
     |SI aAlfa ! aAlfa1;
         |HAZ Graba2;
     |FINSI;

     |GRABA 020#3a;
     |LIBERA #3;
|FPRC;

|DEFBUCLE juanma13A;
     #3#1;
     ;
     aInformes, 000001;
     aInformes, 999999;
     be;
     NULL, Trata02;
|FIN;

|PROCESO Borra3;
     |BORRA 020#3a;
     |LIBERA #3;
|FPRC;

|DEFBUCLE juanma13B;
     #3#1;
     ;
     aInformes, 000001;
     aInformes, 999999;
     be;
     NULL, Borra3;
|FIN;

|PROCESO GrabaInforme;
     aAlfa = #3#2;
     |XGRABA nHandle, aAlfa;
|FPRC;

|DEFBUCLE juanma13C;
     #3#1;
     ;
     #1#0, 000001;
     #1#0, 999999;
     ;
     NULL, GrabaInforme;
|FIN;

|PROCESO Borra2;
     |BORRA 020#2a;
     |LIBERA #2;
|FPRC;

|DEFBUCLE juanma12B;
     #2#1;
     ;
     aInformes, 000001;
     aInformes, 999999;
     be;
     NULL, Borra2;
|FIN;

|PROCESO CreaInforme;
     nContador = 1;
     aLong     = aInformes % 0;
     nLong     = aLong;
     |SI nLong > 12;  |ACABA;  |FINSI;

     |ABRE #3;
     |XABRE "U", aBuscar, nHandle;
     |PARA;  |SI  FSalida ] 0;  |HACIENDO;
             |XLEE nHandle, 250, aAlfa;
             |SI FSalida < 0;  |SAL;  |FINSI;

             |SI nContador = 1;
                 #1#0 = aInformes;
                 |GRABA 000#1n;
                 |LIBERA #1;
             |FINSI;

             #3#0 = aInformes;
             #3#1 = nContador;
             #3#2 = aAlfa;
             |GRABA 020#3n;

             nContador = nContador + 1;
     |SIGUE;
     |XCIERRA  nHandle;

     |CIERRA #3;

     nFic      = 0;
     IiFichero = iFichero1 <-;
     |PARA nCampo = 1;  |SI nCampo [ 99;  |HACIENDO nCampo = nCampo + 1;
           iFichero = "";
           IiFichero = IiFichero + 1;
     |SIGUE;

     nAsterisco = 0;
     nSwHay2    = 0;
     |ABRE #2;
     |BUCLE juanma13A;
     |CIERRA #2;

     |SI nAsterisco = 1;
         |XABRE "W", aBuscar, nHandle;
         |BUCLE juanma13C;
         |XCIERRA nHandle;
     |FINSI;

     |SI nSwHay2 = 0;
         |BUCLE juanma13B;
         #1#0 = aInformes;
         |LEE 101#1=;
         |SI FSalida = 0;
             |BORRA 020#1a;
             |LIBERA #1;
         |FINSI;
     |FINSI;
|FPRC;

|RUTINA ClaveBaja1;
     #1#0 = "        ";
|FRUT;

|RUTINA ClaveAlta1;
     #1#0 = "zzzzzzzz";
|FRUT;

|PROCESO Tipo3;  |TIPO 3;
     aDirInfor = #0#0;   |QBF aDirInfor;
     aDirInfor = aDirInfor + #0#1;  |QBF aDirInfor;
     aDirInfor = aDirInfor + "/def/";
     aBuscar   = aDirInfor + "*.inf";
     nSwHay    = 0;

     |ABRE #1;
     |_PDIR aBuscar;
     |PARA; |SI; |HACIENDO;
         aInformes = aBuscar   - aDirInfor;
         aInformes = aInformes - ".inf";
         #0#2      = aInformes;
         |PINTA #0#2;

         |HAZ CreaInforme;

         |_SDIR aBuscar;
         |SI FSalida ! 0; |SAL; |FINSI;
     |SIGUE;
     |CIERRA #1;

     |SI nSwHay = 0;
         |MENAV  "      No hay informes en la Aplicacion ";
         |ACABA;
     |FINSI;

     |ENTLINEAL #1#1, -1, 4, 22, 1, ClaveBaja1, ClaveAlta1;
|FPRC;

|PROGRAMA;
     aFichero = ("11" + Usuario + "zzzzzzzz") % 108;  |NOME_DAT #1 aFichero;
     aFichero = ("12" + Usuario + "zzzzzzzz") % 108;  |NOME_DAT #2 aFichero;
     aFichero = ("13" + Usuario + "zzzzzzzz") % 108;  |NOME_DAT #3 aFichero;

     |ABRE #1;  |CIERRA #1;  |DELINDEX #1;
     |ABRE #2;  |CIERRA #2;  |DELINDEX #2;
     |ABRE #3;  |CIERRA #3;  |DELINDEX #3;

     |ABRE #0;
     #0#6 = Usuario;
     |LEE 040#0=;
     |SI FSalida ! 0;
         |DDEFECTO #0;
         #0#6 = Usuario;
         |GRABA 000#0b;
     |FINSI;

     |CLS;
     |CABEZA "Cambio a EURO Informes";
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
         |GRABA 000#0a;
         |HAZ Tipo3;
     |FINSI;
     |LIBERA #0;
     |CIERRA #0;

     |ABRE #1;  |CIERRA #1;  |DELINDEX #1;
     |ABRE #2;  |CIERRA #2;  |DELINDEX #2;
     |ABRE #3;  |CIERRA #3;  |DELINDEX #3;
|FPRO;

|| Calculo Programa juanma11

|PROCESO Tipo7m11;  |TIPO 7;
     |ENTLINEAL #1#2, -2, 7, 22, 0, ClaveBaja2, ClaveAlta2;
|FPRC;

|PROCESO Tipo11m11;  |TIPO 11;
     |SI FSalida = 10;
         nChequeo = 0;
         |ENTLINEAL #1#2, -2, 4, 22, 0, ClaveBaja2, ClaveAlta2;
         |SI nChequeo = 0;  |ACABA;  |FINSI;

         aBuscar   = aDirInfor + #1#0;  |QBF aBuscar;
         aBuscar   = aBuscar + ".inf";
         |XABRE "W", aBuscar, nHandle;
         |BUCLE juanma13C;
         |XCIERRA nHandle;
     |FINSI;

     |SI FSalida = 11;
         |SI #0#4 = "        ";
             #0#4 = "NULL";
         |FINSI;

         aOrden  = "../ut/aginfont " + #0#5;         |QBF aOrden;
         aOrden  = aOrden + "/ "  + #0#4;            |QBF aOrden;
         aOrden  = aOrden + " "   + #1#0;            |QBF aOrden;
         |RSYSTEM aOrden;

         aInformes = #1#0;
         aBuscar   = aDirInfor + #1#0;  |QBF aBuscar;
         aBuscar   = aBuscar + ".inf";

         |BUCLE juanma12B;
         |BUCLE juanma13B;
         |HAZ CreaInforme;
         |PONCONTROL 23, "SI";
     |FINSI;
|FPRC;

|| Calculo Programa juanma12

|PROCESO Tipo5m12;  |TIPO 5;
     #2#5     = "N";
     |LINEAS_BI_ATRI #2#5, D, S, -1;
|FPRC;

|PROCESO Tipo11m12;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     |LEE 101#2=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI #2#2 ! "        ";
         |MENAV "     Variable de Def, use la opcion Cambiar Importes de Pesetas a Euros de un .mas";
         |ACABA;
     |FINSI;

     |ABRE #3;
     #3#0 = #2#0;
     #3#1 = #2#1;
     |LEE 101#3=;
     |SI FSalida ! 0;  |CIERRA #3;  |ACABA;  |FINSI;

     aAlfa1 = #3#2;   |QBF aAlfa1;
     aLong    = aAlfa1 % 0;
     nLong    = aLong;
     aAlfa    = "";
     nFichero = 0;
     |PARA nCampo = 1;  |SI nCampo [ nLong;  |HACIENDO nCampo = nCampo + 1;
           nPosi     = (nCampo * 100) + 1;
           aCaracter = aAlfa1 % nPosi;

           |SI aCaracter = ".";  |Y nFichero = 0;  nFichero = 1;  |FINSI;

           |SI nFichero = 1;
               |SI #2#5 = "E";
                   |SI aCaracter = "8";
                       aCaracter = #2#6;
                       nFichero  = -1;
                   |FINSI;
               |SINO;
                   |SI aCaracter ] "0";  |Y aCaracter [ "9";
                       aCaracter = "8";
                       nFichero  = -1;
                   |FINSI;
               |FINSI;
            |FINSI;

            aAlfa = aAlfa + aCaracter;
     |SIGUE;

     #3#2 = aAlfa;

     |GRABA 020#3a;
     |LIBERA #3;
     |CIERRA #3;

     |SI #2#5 = "E";
         #2#5 = "N";
     |SINO;
         #2#5 = "E";
     |FINSI;
     |GRABA 020#2a;
     |LIBERA #2;

     |PINTA #2#5;
     nChequeo = 1;
|FPRC;
