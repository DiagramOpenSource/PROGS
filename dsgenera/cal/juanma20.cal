|FICHEROS;
     juanma20;
|FIN;

|VARIABLES;
     aLong         = "";
     aAlfa         = "";
     aAlfa1        = "";
     aAlfa2        = "";
     aPass         = "";
     aPassEnc      = "";

     nLong         = 0;
     nPos          = 0;
     nCamuf        = 0;
     nCamuf1       = 0;
     nCamuf2       = 0;
     nNum          = 0;
     nCaracter     = 0;
|FIN;

|PROCESO T12;  |TIPO 12;
     |SI FSalida = 0;
         |ERROR;
         |POSICIONATE #juanma20#0;
     |FINSI;
|FPRC;

|PROCESO Desencripta;
     aLong    = aPass % 0;
     nLong    = aLong;
     aPassEnc = "";
     |PARA nCaracter = 1; |SI nCaracter [ nLong; |HACIENDO nCaracter = nCaracter + 2;
          nPos   = (nCaracter * 100) + 1;       aAlfa1 = aPass % nPos;
          nPos   = ((nCaracter + 1) * 100) + 1; aAlfa2 = aPass % nPos;

          nCamuf2 = &aAlfa2;
          nCamuf2 = nCamuf2 - 65;

          nCamuf1 = &aAlfa1;
          nCamuf1 = nCamuf1 - 65;
          nCamuf1 = ((nCamuf1 * 26) + nCamuf2) / 2;

          aPassEnc = aPassEnc + &nCamuf1;
     |SIGUE;
|FPRC;

|PROCESO Encripta;
     aLong   = aPass % 0;
     nLong   = aLong;
     aPassEnc = "";
     |PARA nCaracter = 1; |SI nCaracter [ nLong; |HACIENDO nCaracter = nCaracter + 1;
           nPos   = (nCaracter * 100) + 1;
           aAlfa1 = aPass % nPos;

           nCamuf = &aAlfa1;
           nCamuf = nCamuf * 2;

           nNum      = ((nCamuf - (nCamuf $ 26)) / 26 ) + 65;
           aPassEnc = aPassEnc + &nNum;

           nNum      = (nCamuf $ 26) + 65;
           aPassEnc = aPassEnc + &nNum;
     |SIGUE;
|FPRC;

|PROCESO T0C1;  |TIPO 0;
     aPass = #juanma20#0;  |QBF aPass;

     |SI #juanma20#1 = "1";  |HAZ Encripta;     |FINSI;
     |SI #juanma20#1 = "2";  |HAZ Desencripta;  |FINSI;

     #juanma20#2 = aPassEnc;

     |PINTA #juanma20#2;
|FPRC;
