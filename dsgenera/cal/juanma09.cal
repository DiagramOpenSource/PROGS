|FICHEROS;
     juanma09 #0;
     juanmw03 #98,MANTE;
     juanmw04 #99,MANTE;
     juanmw05 #97,MANTE;

     referen@ #100;
|FIN;

|VARIABLES;
     aBase        = "";
     aAbre        = "";
     aDef         = "";
     aCarga       = "";
     aMensa       = "";
     aRelacion    = "";
     aTitulo      = "";
     aQueQuiero   = "";
     aFichero     = "";
     aNombreDef   = "";
     aNumeroCampo = "";
     aNombreCampo = "";
     aTipoCampo   = "";
     aLongCampo   = "";
     aMinCampo    = "";
     aMaxCampo    = "";
     aAlfa        = "";
     aAlfa1       = "";
     aAlfa2       = "";
     aAlfa3       = "";
     aAlfa4       = "";
     aCaracter    = "";
     aLong        = "";
     aCadena      = "";

     nPon         = 0;
     nHandle      = 0;
     nContador    = 0;
     nSwPrint     = 0;
     nTCampo      = 0;
     nCampo       = 0;
     nLong        = 0;
     nPosi        = 0;
     nCaracter    = 0;
     nDecimal     = 0;
     nDecimales   = 0;
     nNume        = 0;
     nBoton1      = 0;
     nBoton2      = 0;
     nBoton3      = 0;
     nBoton4      = 0;
     nBoton5      = 0;
     nBoton6      = 0;
     nBoton7      = 0;
     nBoton8      = 0;
     nBoton9      = 0;
     nBoton10     = 0;
     nBoton11     = 0;
     nBoton12     = 0;
     nTecla       = 0;
     nContaCambio = 0;
     nSwHay       = 0;
     nCambia      = 0;
|FIN;

|| _______________________________________________________________________
|| Procesos del juanmw003
|| _______________________________________________________________________

|RUTINA ClaveBaja99;
     #99#0 = #98#0;
     #99#1 = 0;
|FRUT;

|RUTINA ClaveAlta99;
     #99#0 = #98#0;
     #99#1 = 999;
|FRUT;

|PROCESO Tipo5w3;  |TIPO 5;

     #98#2 = " ";
     |LINEAS_BI_ATRI #98#2, D, S, -1;
|FPRC;

|PROCESO Boton1;
     |PTEC 824;
|FPRC;

|PROCESO Boton2;
     |PTEC 825;
|FPRC;

|PROCESO Boton3;
     |PTEC 826;
|FPRC;

|PROCESO Boton4;
     |PTEC 827;
|FPRC;

|PROCESO Boton5;
     |PTEC 828;
|FPRC;

|PROCESO Boton6;
     |PTEC 829;
|FPRC;

|PROCESO Boton7;
     |PTEC 808;
|FPRC;

|PROCESO Boton8;
     |PTEC 809;
|FPRC;

|PROCESO Boton9;
     |PTEC 830;
|FPRC;

|PROCESO Boton10;
     |PTEC 831;
|FPRC;

|PROCESO Boton11;
     |PTEC 832;
|FPRC;

|PROCESO Boton12;
     |PTEC 841;
|FPRC;

|PROCESO Tipo7w3;  |TIPO 7;
     nPon = 1;
     |CONTROL_SIMPLE 0, "BOTON,F2 Entrar en Campos", 0568, 0680, Boton1;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,F3 Cambio de Divisas (.?xxxx)", 0768, 0880, Boton2;
     nBoton2 = FSalida;

     |SI #0#13 = "N";
         |CONTROL_SIMPLE 0, "BOTON,F4 Grabar los Def", 0968, 1080, Boton3;
         nBoton3 = FSalida;
     |FINSI;

     |CONTROL_SIMPLE 0, "BOTON,F5 Comparador / Cambiador", 1168, 1280, Boton4;
     nBoton4 = FSalida;

     |ENTLINEAL #1#99, -2, 7, 22, 0, ClaveBaja99, ClaveAlta99;
|FPRC;

|PROCESO Tipo0w3;  |TIPO 0;
     |SI nPon = 0;  |ACABA;  |FINSI;

     nPon = 0;
                       |FIN_CONTROL_WINDOWS nBoton1;
                       |FIN_CONTROL_WINDOWS nBoton2;
     |SI #0#13 = "N";  |FIN_CONTROL_WINDOWS nBoton3;  |FINSI;
                       |FIN_CONTROL_WINDOWS nBoton4;
|FPRC;

|| _______________________________________________________________________
|| Entrada al Lineal de Campos <F2>
|| _______________________________________________________________________

|PROCESO EntraLineal;
     |LEE 101#98=;

     |CONTROL_SIMPLE 0, "BOTON,F2 Automatico", 0568, 0680, Boton1;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F3 EURO", 0868, 0880, Boton2;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F4 EURO1", 0968, 0980, Boton3;
     nBoton3 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F5 EURO2", 1068, 1080, Boton4;
     nBoton4 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F6 EURO3", 1168, 1180, Boton5;
     nBoton5 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F7 EURO4", 1268, 1280, Boton6;
     nBoton6 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,Subir", 1373, 1380, Boton7;
     nBoton7 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,Bajar", 2273, 2280, Boton8;
     nBoton8 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,F8 Quita Entero", 1573, 1680, Boton9;
     nBoton9 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,F9 Pone Entero", 1773, 1880, Boton10;
     nBoton10 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,F10Quita + EURO", 1973, 2080, Boton11;
     nBoton11 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,Ctrl-F9 EURO*", 0768, 0780, Boton12;
     nBoton12 = FSalida;

     |ENTLINEAL #1#99, -2, 4, 22, 0, ClaveBaja99, ClaveAlta99;

     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;
     |FIN_CONTROL_WINDOWS nBoton4;
     |FIN_CONTROL_WINDOWS nBoton5;
     |FIN_CONTROL_WINDOWS nBoton6;
     |FIN_CONTROL_WINDOWS nBoton7;
     |FIN_CONTROL_WINDOWS nBoton8;
     |FIN_CONTROL_WINDOWS nBoton9;
     |FIN_CONTROL_WINDOWS nBoton10;
     |FIN_CONTROL_WINDOWS nBoton11;
     |FIN_CONTROL_WINDOWS nBoton12;

     #98#2 = "*";
     |PINTA #98#2;
     |PINTA #98#3;

     |GRABA 020#98a;
     |LIBERA #98;

     |PONCONTROL 23, "SI";
|FPRC;

|| _______________________________________________________________________
|| Masivo de Divisas <F3>
|| _______________________________________________________________________

|PROCESO ConvertirMasivo;
     aAlfa1 = #99#3;  |QBF aAlfa1;
     aAlfa2 = aAlfa1 - ".?";
     |SI aAlfa2 ! aAlfa1;
         aAlfa2 = aAlfa1 - ".?EURO";
         |SI aAlfa2 = aAlfa1;
             aLong      = aAlfa1 % 0;
             nLong      = aLong;
             nDecimal   = 0;
             aAlfa2     = "";
             |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
                   nPosi = (100 * nCaracter) + 1;
                   aCaracter = aAlfa1 % nPosi;
                   |SI aCaracter = ".";
                       |SAL;
                   |SINO;
                       aAlfa2 = aAlfa2 + aCaracter;
                   |FINSI;
             |SIGUE;

             aAlfa3   = aAlfa2 + ".?" + #0#12;  |QBF aAlfa3;

             aAlfa1   = #99#0;  |QBF aAlfa1;
             aAlfa    = ":" + aAlfa1 + &13 +  &10;         |XGRABA nHandle, aAlfa;
             aAlfa1   = "#" + #99#1 + ",VALOR_MINIMO=" + aAlfa3;  |QBF aAlfa1;
             aAlfa    = aAlfa1 + &13 + &10;                |XGRABA nHandle, aAlfa;

             nContaCambio = nContaCambio + 1;
         |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE juanmw04;
     #99#1;
     ;
     "        ", 000;
     "zzzzzzzz", 999;
     ;
     NULL, ConvertirMasivo;
|FIN;

|PROCESO EntraMasivo;
     |PUSHV 0501 2380;
     |CUADRO 1110 1562;
     |BLANCO 1211 1461;
     |PINTA 1311, "Valor EURO a Grabar ...: ";
     |C_P #0#12, 1, 1335;
     |C_V #0#12, 1, "S";  |C_M #0#12, 1, "S";
     |PINTA #0#12;

     |ET Campo12;
         |ENCAMPO #12#0;
         |SI FSalida = 7;  |POPV; |ACABA;  |FINSI;
         |SI FSalida = 2;  |VETE Campo12;  |FINSI;

     |POPV;

     |CONFI "0000NDesea hacer el Cambio al EURO.";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     nContaCambio = 0;

     aAbre = #0#7;  |QBF aAbre;
     |XABRE "BW", aAbre, nHandle;
     |BUCLE juanmw04;
     |XCIERRA nHandle;

     |SI nContaCambio ! 0;
         |MENSA "     Convirtiendo Campos, Espere por favor ...";
         aBase  = #0#0;  |QBF aBase;
         aAlfa1 = #0#5;  |QBF aAlfa1;
         aAlfa2 = #0#6;  |QBF aAlfa2;
         aAlfa3 = #0#7;  |QBF aAlfa3;
         aAlfa4 = #0#1;  |QBF aAlfa4;
         aAlfa  = aBase + "ut/" + aAlfa1 + " @" + aBase + " " + aAlfa4 + " @:" + aAlfa2 + " -batch:" + aAlfa3;
         |SYSTEM aAlfa;

         |FBORRA aAlfa3;

         aAlfa = "       Convertidos " + nContaCambio + " Campos con Divisas.";
         |MENAV aAlfa;

         |PTEC 807;
     |FINSI;
|FPRC;

|| _______________________________________________________________________
|| Grabacion de los Def <F4>
|| _______________________________________________________________________

|PROCESO ConvertirManual;
     aAlfa1   = #99#0;  |QBF aAlfa1;
     aAlfa    = ":" + aAlfa1 + &13 +  &10;         |XGRABA nHandle, aAlfa;
     aAlfa1   = "#" + #99#1 + ",VALOR_MINIMO=" + #99#3;  |QBF aAlfa1;
     aAlfa    = aAlfa1 + &13 + &10;                |XGRABA nHandle, aAlfa;

     nContaCambio = nContaCambio + 1;

     #99#6 = " ";
     |GRABA 020#99a;
     |LIBERA #99;
|FPRC;

|DEFBUCLE juanmw04T;
     #99#1;
     6;
     "        ", 000, "*";
     "zzzzzzzz", 999, "*";
     be;
     NULL, ConvertirManual;
|FIN;

|PROCESO GrabaDefs;
     aAbre = #0#7;  |QBF aAbre;
     |XABRE "BW", aAbre, nHandle;
     |BUCLE juanmw04T;
     |XCIERRA nHandle;

     |SI nContaCambio ! 0;
         aBase  = #0#0;  |QBF aBase;
         aAlfa1 = #0#5;  |QBF aAlfa1;
         aAlfa2 = #0#6;  |QBF aAlfa2;
         aAlfa3 = #0#7;  |QBF aAlfa3;
         aAlfa4 = #0#1;  |QBF aAlfa4;
         aAlfa  = aBase + "ut/" + aAlfa1 + " @" + aBase + " " + aAlfa4 + " @:" + aAlfa2 + " -batch:" + aAlfa3;
         |SYSTEM aAlfa;

         |FBORRA aAlfa3;

         aAlfa = "       Grabados " + nContaCambio + " Campos Manuales.";
         |MENAV aAlfa;

         |PONCONTROL 23, "SI";

         nContaCambio = 0;
     |FINSI;
|FPRC;

|| _______________________________________________________________________
|| Comparador y Cambiador  <F5>
|| _______________________________________________________________________

|PROCESO MiraCampos;
     #97#3 = #97#3 + 1;  |PINTA #97#3;

     nCampo       = #99#1;
     aQueQuiero   = "|C" + (("000" + nCampo) % -103) + "NOMBRE";
     aNombreCampo = (#100#aQueQuiero# + (" " * 20)) % 120;
     aQueQuiero   = "|C" + (("000" + nCampo) % -103) + "TIPO";
     aTipoCampo   = #100#aQueQuiero#;
     aQueQuiero   = "|C" + (("000" + nCampo) % -103) + "MINIMO";
     aMinCampo    = (#100#aQueQuiero# + (" " * 20)) % 120;
     aAlfa        = aMinCampo - "EURO";

     |SI #99#3 = aMinCampo;     |ACABA;  |FINSI;
     |SI aAlfa = aMinCampo;     |ACABA;  |FINSI;
     |SI aNombreCampo ! #99#2;  |ACABA;  |FINSI;
     |SI aTipoCampo   ! "N";    |ACABA;  |FINSI;

     aAlfa1 = #99#0;  |QBF aAlfa1;
     aAlfa  = ":" + aAlfa1 + &13 +  &10;         |XGRABA nHandle, aAlfa;
     aAlfa1 = "#" + #99#1 + ",VALOR_MINIMO=" + aMinCampo;  |QBF aAlfa1;
     aAlfa  = aAlfa1 + &13 + &10;                |XGRABA nHandle, aAlfa;

     #97#5 = #97#5 + 1;  |PINTA #97#5;

     nSwHay = 1;
|FPRC;

|DEFBUCLE juanmw04C;
     #99#1;
     ;
     #98#0, 000;
     #98#0, 999;
     ;
     NULL, MiraCampos;
|FIN;

|PROCESO CargaDef;
     aBase = #97#0;   |QBF aBase;
     aBase = aBase + #97#1;  |QBF aBase;
     aDef  = aBase + "/def/" + #98#0;  |QBF aDef;
     aDef  = aDef + ".mas";

     |CARGA_DEF aDef, referen@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     nSwHay = 0;
     |BUCLE juanmw04C;

     |DESCARGA_DEF #referen@;

     #97#2 = #97#2 + 1;  |PINTA #97#2;

     |SI nSwHay = 1;
         #97#4 = #97#4 + 1;  |PINTA #97#4;
     |FINSI;
|FPRC;

|DEFBUCLE juanmw03;
     #98#1;
     ;
     "        ";
     "zzzzzzzz";
     ;
     NULL, CargaDef;
|FIN;

|PROCESO Comparador;
     |SALVA_DATOS #98;

     |PUSHV 0501 2380;
     |DDEFECTO #97;
     |PINPA #0#97;
     |PINDA #0#97;
     |ENDATOS #1#97;

     aAbre = #0#7;  |QBF aAbre;
     |XABRE "BW", aAbre, nHandle;
     |BUCLE juanmw03;
     |XCIERRA nHandle;

     nContaCambio = 1;

     |SI #97#5 ! 0;
         aAlfa = "S";
         E_inf = "SN";
         E_sup = "1";
         aAlfa = 2059 ? 1;
         |SI aAlfa = "S";
             |MENSA "     Convirtiendo Campos, Espere por favor ...";
             nContaCambio = #97#5;
             aBase  = #0#0;  |QBF aBase;
             aAlfa1 = #0#5;  |QBF aAlfa1;
             aAlfa2 = #0#6;  |QBF aAlfa2;
             aAlfa3 = #0#7;  |QBF aAlfa3;
             aAlfa4 = #0#1;  |QBF aAlfa4;
             aAlfa  = aBase + "ut/" + aAlfa1 + " @" + aBase + " " + aAlfa4 + " @:" + aAlfa2 + " -batch:" + aAlfa3;
             |SYSTEM aAlfa;

             aAlfa = "       Convertidos " + nContaCambio + " Campos";
             |MENAV aAlfa;
         |SINO;
             aAlfa3 = #0#7;  |QBF aAlfa3;
         |FINSI;
     |SINO;
         |MENAV "      No se ha encontrado campos EUROS a cambiar.";
     |FINSI;

     |POPV;

     aAlfa3 = #0#7;  |QBF aAlfa3;
     |FBORRA aAlfa3;

     |REPON_DATOS #98;
     |PTEC 807;
|FPRC;

|PROCESO Tipo11w3;  |TIPO 11;
     nTecla = FSalida;
     |SI nPon = 1;
         |FIN_CONTROL_WINDOWS nBoton1;
         |FIN_CONTROL_WINDOWS nBoton2;
         |SI #0#13 = "N";  |FIN_CONTROL_WINDOWS nBoton3;  |FINSI;
         |FIN_CONTROL_WINDOWS nBoton4;
         nPon = 0;
     |FINSI;

     |SI nTecla = 10;  |HAZ EntraLineal;  |FINSI;
     |SI nTecla = 11;  |HAZ EntraMasivo;  |FINSI;
     |SI nTecla = 12;  |HAZ GrabaDefs;    |FINSI;
     |SI nTecla = 13;  |HAZ Comparador;   |FINSI;

     FSalida = 0;
|FPRC;

|| Procesos del juanmw004

|PROCESO Tipo5w4;  |TIPO 5;
     #99#5 = " ";
     |LINEAS_BI_ATRI #99#5, D, S, -1;
|FPRC;

|PROCESO ProcesoNormal;
     |SI #99#5 = " ";
         aAlfa1 = #99#3;  |QBF aAlfa1;
         aAlfa2 = aAlfa1 - "?EURO";
         aAlfa3 = aAlfa2 % -101;
         |SI aAlfa3 ! ".";
             nDecimales = aAlfa3;
             aAlfa2     = aAlfa2 - aAlfa3;
             nNume      = aAlfa2;
             |SI aAlfa3 = "*";
                 aAlfa2 = aAlfa2 + "*";
             |SINO;
                 |SI nNume ] 0;
                     aAlfa2 = aAlfa2 + ("0" * nDecimales);
                 |SINO;
                     aAlfa2 = aAlfa2 + ("9" * nDecimales);
                 |FINSI;
             |FINSI;
         |SINO;
             aAlfa2 = aAlfa1 - ".?EURO";
             |SI aAlfa2 = "";  aAlfa2 = "0";  |FINSI;
         |FINSI;
     |SINO;
         nDecimales = 0;
         aAlfa1 = #99#3;  |QBF aAlfa1;
         aAlfa2 = aAlfa1 - ".";
         |SI aAlfa1 ! aAlfa2;
             aLong      = aAlfa1 % 0;
             nLong      = aLong;
             nDecimal   = 0;
             aAlfa2     = "";
             |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
                   nPosi = (100 * nCaracter) + 1;
                   aCaracter = aAlfa1 % nPosi;
                   |SI aCaracter = ".";
                       nDecimal = 1;
                   |SINO;
                       |SI nDecimal = 1;
                           |SI aCaracter = "*";
                               nDecimales = -1;
                               |SAL;
                           |FINSI;
                           nDecimales = nDecimales + 1;
                       |SINO;
                           aAlfa2 = aAlfa2 + aCaracter;
                       |FINSI;
                   |FINSI;
             |SIGUE;
         |FINSI;

         |SI nDecimales ! 0;
             |SI aAlfa2 = "0";  aAlfa2 = "";  |FINSI;
             |SI nDecimales = -1;
                 aAlfa2 = aAlfa2 + ".?EURO*";
             |SINO;
                 aAlfa2 = aAlfa2 + ".?EURO" + nDecimales;
             |FINSI;
         |SINO;
             |SI aAlfa2 = "0";  aAlfa2 = "";  |FINSI;
             aAlfa2 = aAlfa2 + ".?EURO";
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO ProcesoForzado;
     aAlfa1 = #99#3;  |QBF aAlfa1;
     aAlfa2 = aAlfa1 - ".";
     |SI aAlfa1 ! aAlfa2;
         aLong      = aAlfa1 % 0;
         nLong      = aLong;
         nDecimal   = 0;
         aAlfa2     = "";
         |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
               nPosi = (100 * nCaracter) + 1;
               aCaracter = aAlfa1 % nPosi;
               |SI aCaracter = ".";
                   nDecimal = 1;
               |SINO;
                   |SI nDecimal = 0;
                       aAlfa2 = aAlfa2 + aCaracter;
                   |FINSI;
               |FINSI;
         |SIGUE;
     |FINSI;

     |SI aAlfa2 = "0";  aAlfa2 = "";  |FINSI;

     |SI nTecla = 11;  aAlfa2 = aAlfa2 + ".?EURO";   |FINSI;
     |SI nTecla = 12;  aAlfa2 = aAlfa2 + ".?EURO1";  |FINSI;
     |SI nTecla = 13;  aAlfa2 = aAlfa2 + ".?EURO2";  |FINSI;
     |SI nTecla = 14;  aAlfa2 = aAlfa2 + ".?EURO3";  |FINSI;
     |SI nTecla = 15;  aAlfa2 = aAlfa2 + ".?EURO4";  |FINSI;
     |SI nTecla = 27;  aAlfa2 = aAlfa2 + ".?EURO*";  |FINSI;
|FPRC;

|PROCESO ChequeaEuro;
     |SI nTecla = 10;
         |HAZ ProcesoNormal;
     |SINO;
         |HAZ ProcesoForzado;
     |FINSI;
     #99#3 = aAlfa2;
|FPRC;

|PROCESO GrabaYa;
     |SI #0#13 = "N";  |ACABA;  |FINSI;

     aAbre = #0#7;  |QBF aAbre;
     |XABRE "BW", aAbre, nHandle;

     aAlfa1 = #99#0;  |QBF aAlfa1;
     aAlfa  = ":" + aAlfa1 + &13 +  &10;         |XGRABA nHandle, aAlfa;
     aAlfa1 = "#" + #99#1 + ",VALOR_MINIMO=" + #99#3;  |QBF aAlfa1;
     aAlfa  = aAlfa1 + &13 + &10;                |XGRABA nHandle, aAlfa;
     |XCIERRA nHandle;

     aBase  = #0#0;  |QBF aBase;
     aAlfa1 = #0#5;  |QBF aAlfa1;
     aAlfa2 = #0#6;  |QBF aAlfa2;
     aAlfa3 = #0#7;  |QBF aAlfa3;
     aAlfa4 = #0#1;  |QBF aAlfa4;
     aAlfa  = aBase + "ut/" + aAlfa1 + " @" + aBase + " " + aAlfa4 + " @:" + aAlfa2 + " -batch:" + aAlfa3;
     |SYSTEM aAlfa;

     |FBORRA aAlfa3;
|FPRC;

|PROCESO QuitaCaracter;
     aMinCampo  = #99#3;  |QBF aMinCampo;
     aLong      = aMinCampo % 0;
     nLong      = aLong;
     aCadena    = "";
     nCambia    = 0;
     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPosi = (100 * nCaracter) + 1;
           aCaracter = aMinCampo % nPosi;
           |SI aCaracter ] "0"; |Y aCaracter [ "9";  |Y nCambia = 0;
               nCambia = 1;
           |SINO;
               aCadena = aCadena + aCaracter;
           |FINSI;
     |SIGUE;
     #99#3 = aCadena;  |PINTA #99#3;
|FPRC;

|PROCESO QuitaEntero;
     |HAZ QuitaCaracter;

     #99#6 = "*";
     |GRABA 020#99a;
     |LIBERA #99;

     |HAZ GrabaYa;

     |ESTADO_CONTROL nBoton1, "ENABLE";
     |ESTADO_CONTROL nBoton2, "ENABLE";
     |ESTADO_CONTROL nBoton3, "ENABLE";
     |ESTADO_CONTROL nBoton4, "ENABLE";
     |ESTADO_CONTROL nBoton5, "ENABLE";
     |ESTADO_CONTROL nBoton6, "ENABLE";

     |PONCONTROL 23, "SI";

     |PULSATECLA;
     |PTEC 809;
|FPRC;

|PROCESO PonerEntero;
     aMinCampo  = #99#3;  |QBF aMinCampo;
     aLong      = aMinCampo % 0;
     nLong      = aLong;
     aCadena    = "";
     nCambia    = 0;
     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPosi = (100 * nCaracter) + 1;
           aCaracter = aMinCampo % nPosi;
           |SI aCaracter ] "0"; |Y aCaracter [ "9";  |Y nCambia = 0;
               aCadena = aCadena + aCaracter;
               aCadena = aCadena + aCaracter;
               nCambia = 1;
           |SINO;
               aCadena = aCadena + aCaracter;
           |FINSI;
     |SIGUE;

     |SI aCadena = "";  aCadena   = "0";  |FINSI;

     #99#3 = aCadena;  |PINTA #99#3;
     #99#6 = "*";
     |GRABA 020#99a;
     |LIBERA #99;

     |HAZ GrabaYa;

     |ESTADO_CONTROL nBoton1, "ENABLE";
     |ESTADO_CONTROL nBoton2, "ENABLE";
     |ESTADO_CONTROL nBoton3, "ENABLE";
     |ESTADO_CONTROL nBoton4, "ENABLE";
     |ESTADO_CONTROL nBoton5, "ENABLE";
     |ESTADO_CONTROL nBoton6, "ENABLE";

     |PONCONTROL 23, "SI";

     |PULSATECLA;
     |PTEC 809;
|FPRC;

|PROCESO Tipo11w4;  |TIPO 11;
     nTecla = FSalida;
     |SI nTecla ! 27;
         |SI FSalida > 18;  |ACABA;  |FINSI;
         |SI FSalida < 10;  |ACABA;  |FINSI;
     |FINSI;

     |ESTADO_CONTROL nBoton1, "DISABLE";
     |ESTADO_CONTROL nBoton2, "DISABLE";
     |ESTADO_CONTROL nBoton3, "DISABLE";
     |ESTADO_CONTROL nBoton4, "DISABLE";
     |ESTADO_CONTROL nBoton5, "DISABLE";
     |ESTADO_CONTROL nBoton6, "DISABLE";

     |LEE 101#99=;

     |SI nTecla = 16;
         |HAZ QuitaEntero;
         |ACABA;
     |FINSI;

     |SI nTecla = 17;
         |HAZ PonerEntero;
         |ACABA;
     |FINSI;

     |SI nTecla = 18;
         |HAZ QuitaCaracter;
         nTecla = 10;
     |FINSI;

     |SI nTecla = 10;
         |SI #99#5 = "*";
             #99#5 = " ";
             #99#6 = "*";
             #98#3 = #98#3 - 1;
             |HAZ ChequeaEuro;
         |SINO;
             #99#5 = "*";
             #99#6 = "*";
             #98#3 = #98#3 + 1;
             |HAZ ChequeaEuro;
         |FINSI;
     |SINO;
         |HAZ ChequeaEuro;
         #99#6 = "*";
         |SI #99#5 = " ";
             #99#5 = "*";
             #98#3 = #98#3 + 1;
         |FINSI;
     |FINSI;

     |PINTA #99#5;
     |PINTA #99#3;

     |GRABA 020#99a;
     |LIBERA #99;

     |HAZ GrabaYa;

     |ESTADO_CONTROL nBoton1, "ENABLE";
     |ESTADO_CONTROL nBoton2, "ENABLE";
     |ESTADO_CONTROL nBoton3, "ENABLE";
     |ESTADO_CONTROL nBoton4, "ENABLE";
     |ESTADO_CONTROL nBoton5, "ENABLE";
     |ESTADO_CONTROL nBoton6, "ENABLE";

     |PONCONTROL 23, "SI";

     |PULSATECLA;
     |PTEC 809;
|FPRC;

|| Procesos del juanma009

|PROCESO Base;  |TIPO 7;
     aBase = #0#0;               |QBF aBase;
     |SI aBase = "";
         |DBASS aBase;               |QBF aBase;
         #0#0 = aBase;
         |PINTA #0#0;
     |FINSI;
|FPRC;

|PROCESO Fichero;  |TIPO 7;
     aBase = #0#0;         |QBF aBase;
     #0#7 = aBase + "ut/batch.txt";
     |PINTA #0#7;
|FPRC;

|PROCESO PonPuntoDir;  |TIPO 7;
     #0#2 = #0#1;
     |PINTA #0#2;
|FPRC;

|PROCESO BuscarPor;
     |SI #0#9 = "E";  |O #0#9 = "?";
         |C_M #0#10, 1, "N";  #0#10 = "";  |PINTA #0#10;
         |C_M #0#11, 1, "N";  #0#11 = "";  |PINTA #0#11;
         |C_M #0#3,  1, "N";  #0#3  = "";  |PINTA #0#3;
     |FINSI;

     |SI #0#9 = "L";  |O #0#9 = "P";
         |C_M #0#10, 1, "N";  #0#10 = "";  |PINTA #0#10;
         |C_M #0#11, 1, "N";  #0#11 = "";  |PINTA #0#11;
         |C_M #0#3,  1, "S";
     |FINSI;

     |SI #0#9 = "M";
         |C_M #0#3,  1, "N";  #0#3  = 0;   |PINTA #0#3;
         |C_M #0#11, 1, "N";  #0#11 = "";  |PINTA #0#11;
         |C_M #0#10, 1, "S";
     |FINSI;

     |SI #0#9 = "X";
         |C_M #0#3,  1, "N";  #0#3  = 0;   |PINTA #0#3;
         |C_M #0#10, 1, "N";  #0#10 = "";  |PINTA #0#10;
         |C_M #0#11, 1, "S";
     |FINSI;
|FPRC;

|PROCESO GrabaTempo;
     |SI aTipoCampo ! "N";  |ACABA;  |FINSI;

     |SI #0#9 = "L";  |O #0#9 = "P";
         nLong = aLongCampo;
         |SI nLong < #0#3;  |ACABA;  |FINSI;
     |FINSI;

     |SI #0#9 = "M";
         |SI #0#10 ! aMinCampo;  |ACABA;  |FINSI;
     |FINSI;

     |SI #0#9 = "X";
         |SI #0#11 ! aMaxCampo;  |ACABA;  |FINSI;
     |FINSI;

     |SI #0#9 = "E";
         aAlfa1 = aMinCampo - "EURO";
         |SI aAlfa1 = aMinCampo;  |ACABA;  |FINSI;
     |FINSI;

     |SI #0#9 = "P";
         aAlfa1 = aMinCampo - "EURO";
         |SI aAlfa1 ! aMinCampo;  |ACABA;  |FINSI;
     |FINSI;

     |SI #0#9 = "?";
         aAlfa1 = aMinCampo - ".?";
         |SI aAlfa1 = aMinCampo;  |ACABA;  |FINSI;
     |FINSI;

     #98#0 = aNombreDef;
     |LEE 101#98=;
     |SI FSalida ! 0;
         |DDEFECTO #98;
         #98#0 = aNombreDef;
         #98#1 = aTitulo;
         |GRABA 020#98b;
     |FINSI;

     aAlfa1 = aMinCampo - "EURO";
     #99#0 = aNombreDef;
     #99#1 = aNumeroCampo;
     #99#2 = aNombreCampo;
     #99#3 = aMinCampo;
     #99#4 = aMaxCampo;
     #99#5 = "";
     #99#6 = "";
     |SI aAlfa1 ! aMinCampo;
         #99#5 = "*";
         #98#2 = "*";
         #98#3 = #98#3 + 1;
     |FINSI;
     #98#4 = #98#4 + 1;
     |GRABA 020#99n;
     |LIBERA #99;

     |GRABA 020#98a;
     |LIBERA #98;

     nSwPrint = 1;
|FPRC;

|PROCESO MiraDef;
     aCarga = aDef + aNombreDef + ".mas";  |QBT aCarga;

     aMensa = "      Cargando Def " + aCarga;
     |MENSA aMensa;

     |CARGA_DEF aCarga, referen@;
     |SI FSalida = 0;
         aQueQuiero = "|TITULO";
         aTitulo    = #100#aQueQuiero#;

         aQueQuiero  = "|NC";
         aFichero    = #100#aQueQuiero#;
         nTCampo     = aFichero;
         nTCampo     = nTCampo - 1;
         |PARA nCampo = 0;  |SI nCampo [ nTCampo;  |HACIENDO nCampo = nCampo + 1;
               aNumeroCampo = (("000" + nCampo) % -103);

               aQueQuiero   = "|C" + (("000" + nCampo) % -103) + "NOMBRE";
               aNombreCampo = (#100#aQueQuiero# + (" " * 20)) % 120;

               aQueQuiero   = "|C" + (("000" + nCampo) % -103) + "TIPO";
               aTipoCampo   = #100#aQueQuiero#;

               aQueQuiero   = "|C" + (("000" + nCampo) % -103) + "LONGITUD";
               aLongCampo   = (((" " * 8) + #100#aQueQuiero#) % -108);

               aQueQuiero   = "|C" + (("000" + nCampo) % -103) + "MINIMO";
               aMinCampo    = (#100#aQueQuiero# + (" " * 20)) % 120;

               aAlfa        = aMinCampo - ",";
               |SI aMinCampo ! aAlfa;
                   aLong      = aMinCampo % 0;
                   nLong      = aLong;
                   aCadena    = "";
                   |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
                         nPosi = (100 * nCaracter) + 1;
                         aCaracter = aMinCampo % nPosi;
                         |SI aCaracter = ",";
                             aCadena = aCadena + ".";
                         |SINO;
                             aCadena = aCadena + aCaracter;
                        |FINSI;
                   |SIGUE;
                   aMinCampo = aCadena;
               |FINSI;

               aQueQuiero   = "|C" + (("000" + nCampo) % -103) + "MAXIMO";
               aMaxCampo    = (#100#aQueQuiero# + (" " * 20)) % 120;

               |HAZ GrabaTempo;
         |SIGUE;
     |SINO;
         aMensa = "     No puedo cargar el Def : " + aNombreDef;
         |MENAV aMensa;
     |FINSI;

     |DESCARGA_DEF #referen@;

     aMensa = "               ";
     |MENSA aMensa;
|FPRC;

|PROCESO MiraSiHay;
     nSwHay = 1;
     |ERROR10;
|FPRC;

|DEFBUCLE juanmw04S;
     #99#2;
     ;
     "*", "        ", 000;
     "*", "zzzzzzzz", 999;
     ;
     NULL, MiraSiHay;
|FIN;

|RUTINA ClaveBaja98;
     #98#0 = "        ";
|FRUT;

|RUTINA ClaveAlta98;
     #98#0 = "zzzzzzzz";
|FRUT;

|PROCESO Busqueda;
     |ET Arriba;
         |ABRE #98;  |CIERRA #98;  |DELINDEX #98;
         |ABRE #99;  |CIERRA #99;  |DELINDEX #99;

         aBase = #0#0;               |QBF aBase;
         aBase = aBase + #0#1;       |QBF aBase;
         aDef  = aBase + "/def/";
         aBase = aBase + "/fich/";   |QBF aBase;

         aAbre = aBase + #0#2;       |QBF aAbre;
         aAbre = aAbre + ".dir";

         |ABRE #98;
         |ABRE #99;
         nContador = 0;
         |XABRE "U", aAbre, nHandle;
         |SI FSalida ] 0;
             |PARA;  |SI FSalida ] 0;  |HACIENDO;
                  nContador = nContador + 1;
                  |XLEE nHandle, 255, aNombreDef;
                  |QBT aNombreDef;
                  |SI nContador = 1;  |Y aNombreDef ! "";
                      |HAZ MiraDef;
                  |FINSI;
                  |SI nContador = 5;  nContador = 0;  |FINSI;
             |SIGUE;
         |FINSI;
         |XCIERRA nHandle;
         |CIERRA #98;
         |CIERRA #99;

         |SI nSwPrint ! 0;
             |PARA;  |SI;  |HACIENDO;
                  |ENTLINEAL #1#98, -1, 4, 11, 1, ClaveBaja98, ClaveAlta98;
                  |SI nContaCambio ! 0;
                      |SAL;
                  |FINSI;

                  |SI #0#13 = "N";
                      nSwHay = 0;
                      |BUCLE juanmw04S;
                      |SI nSwHay = 1;
                          |CONFI "0000NHay campos cambiados y no se ha hecho la grabacion. Desea salir sin Grabar ";
                          |SI FSalida = 0;  |SAL;  |FINSI;
                      |SINO;
                          |CONFI "0000SDesea salir realmente : ";
                          |SI FSalida = 0;  |SAL;  |FINSI;
                      |FINSI;
                  |SINO;
                      |CONFI "0000SDesea salir realmente : ";
                      |SI FSalida = 0; |SAL;  |FINSI;
                  |FINSI;
             |SIGUE;
         |SINO;
             |MENAV "     No encotrados Campos  en la Aplicacion ";
         |FINSI;

         |SI nContaCambio ! 0;
             nContaCambio = 0;
             |VETE Arriba;
         |FINSI;
|FPRC;

|PROGRAMA;
     |ABRE #0;
     #0#8 = Usuario;
     |LEE 040#0=;
     |SI FSalida ! 0;
         |DDEFECTO #0;
         #0#8 = Usuario;
         |GRABA 000#0b;
     |FINSI;

     aFichero = ("w3" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #98 aFichero;
     aFichero = ("w4" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #99 aFichero;

     |CLS;
     |CABEZA "Cambio a EURO";
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
         |GRABA 000#0a;
         |HAZ Busqueda;
     |FINSI;

     |MENAV "     Recuerde de consultar las pantallas el cambio puede haber trastocado las posiciones de los campos.";

     |LIBERA #0;
     |CIERRA #0;
     |DELINDEX #98;
     |DELINDEX #99;
|FPRO;
