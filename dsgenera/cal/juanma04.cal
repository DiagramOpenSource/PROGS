|FICHEROS;
     juanma04 #0;
     juanmw01 #99;

     referen@ #100;
|FIN;

|VARIABLES;
     aBase      = "";
     aAbre      = "";
     aAlfa      = "";
     aAlfa1     = "";
     aCarga     = "";
     aDef       = "";
     aDef1      = "";
     aMensa     = "";
     aRelacion  = "";
     aTitulo    = "";
     aQueQuiero = "";
     aDescri    = "";
     aClave     = "";
     aFichero   = "";
     aFich      = "";

     nHandle    = 0;
     nHandle1   = 0;
     nContador  = 0;
     nSwPrint   = 0;
     nSwAplica  = 0;
     nError     = 0;
     nCampo     = 0;
     nTCampo    = 0;
     nCompara   = 0;
     nCompo     = 0;
     nTCompo    = 0;
     nPosi      = 0;
     nNume      = 0;
     nFichero   = 0;

     &eaVar1    = "SAL1                                                                  ";
     &eaVar2    = "SAL2                                                                  ";
|FIN;

|PROCESO Base;  |TIPO 7;
     aAlfa = #0#0;
     |QBF aAlfa;
     |SI aAlfa = "";
          |DBASS aBase;               |QBF aBase;
          #0#0 = aBase;
          |PINTA #0#0;
     |FINSI;
|FPRC;

|PROCESO Imprimir;
     |SI #99#3 ! #99#4;
         |PRINT;
         nSwPrint = 1;
     |FINSI;
|FPRC;

|DEFBUCLE juanma99;
     #99#1;
     ;
     "        ", "    ";
     "zzzzzzzz", "zzzz";
     ;
     NULL, Imprimir;
|FIN;

|PROCESO Grabalo;
     #99#0 = #0#2;
     #99#1 = aClave;
     |LEE 101#99=;
     |SI FSalida ! 0;
         |DDEFECTO #99;
         #99#0 =  #0#2;
         #99#1 =  aClave;
         |GRABA 020#99b;
     |FINSI;

     #99#2 = aDescri;
     |SI nSwAplica = 1;  #99#3 = aFichero;  |FINSI;
     |SI nSwAplica = 2;  #99#4 = aFichero;  |FINSI;

     aAlfa1 = #99#3;  |QBT aAlfa1;  |SI aAlfa1 = "";  #99#3 = "INEXISTENTE";  |FINSI;
     aAlfa1 = #99#4;  |QBT aAlfa1;  |SI aAlfa1 = "";  #99#4 = "INEXISTENTE";  |FINSI;

     |GRABA 020#99a;
     |LIBERA #99;
|FPRC;

|PROCESO GrabaTemporal;
     |SI nSwAplica = 1;  aAlfa = #0#2;   |QBT aAlfa;  |FINSI;
     |SI nSwAplica = 2;  aAlfa = #0#13;  |QBT aAlfa;  |FINSI;
     aCarga = aDef + aAlfa + ".mas";  |QBT aCarga;

     aMensa = "      Cargando Def " + aCarga;
     |MENSA aMensa;

     |CARGA_DEF aCarga, referen@;
     |SI FSalida = 0;
         aQueQuiero = "|FICHERO";
         aFichero   = #100#aQueQuiero#;
         aClave     = "0002";
         aDescri    = "NOMBRE FICHERO";
         aFichero   = aFichero - aFich;
         |HAZ Grabalo;

         aQueQuiero = "|NP";
         aFichero   = #100#aQueQuiero#;
         aClave     = "0003";
         aDescri    = "NUMERO DE PANTALLAS";
         |HAZ Grabalo;

         aQueQuiero = "|NR";
         aFichero   = #100#aQueQuiero#;
         aClave     = "0004";
         aDescri    = "NUMERO DE RELACIONES";
         |HAZ Grabalo;

         aQueQuiero = "|NC";
         aFichero   = #100#aQueQuiero#;
         nTCampo    = aFichero;
         nTCampo    = nTCampo - 1;
         |PARA nCampo = 0;  |SI nCampo [ nTCampo;  |HACIENDO nCampo = nCampo + 1;
               aClave     = "C" + (("000" + nCampo) % -103);
               aDescri    = "CAMPO : " + (("000" + nCampo) % -103);
               aQueQuiero = "|C" + (("000" + nCampo) % -103) + "NOMBRE";
               aFichero   = (#100#aQueQuiero# + (" " * 20)) % 120;

               aQueQuiero = "|C" + (("000" + nCampo) % -103) + "TIPO";
               aFichero   = aFichero + " TIPO : " + #100#aQueQuiero#;

               aQueQuiero = "|C" + (("000" + nCampo) % -103) + "LONGITUD";
               aFichero   = aFichero + " LONG : " + ((#100#aQueQuiero# + (" " * 3)) % 103);

               aQueQuiero = "|C" + (("000" + nCampo) % -103) + "MINIMO";
               aFichero   = aFichero + " MINIMO : " + ((#100#aQueQuiero# + (" " * 10)) % 110);

               aQueQuiero = "|C" + (("000" + nCampo) % -103) + "MAXIMO";
               aFichero   = aFichero + " MAXIMO : " + ((#100#aQueQuiero# + (" " * 10)) % 110);

               |HAZ Grabalo;
         |SIGUE;

         aQueQuiero = "|NK";
         aFichero   = #100#aQueQuiero#;
         nTCampo    = aFichero;
         nTCampo    = nTCampo - 1;
         |PARA nCampo = 0;  |SI nCampo [ nTCampo;  |HACIENDO nCampo = nCampo + 1;
               aClave     = "K" + (("000" + nCampo) % -103);
               aDescri    = "CLAVE : " + (("000" + nCampo) % -103);
               aQueQuiero = "|K" + (("000" + nCampo) % -103);
               aFichero   = "N.COMPONENTES : " + (#100#aQueQuiero# % 103) + " CLAVES : ";
               aAlfa1     = #100#aQueQuiero# % 103;
               nTCompo    = aAlfa1;
               |PARA nCompo = 1;  |SI nCompo [ nTCompo;  |HACIENDO nCompo = nCompo + 1;
                     nPosi    = (nCompo * 300) + 103;
                     aAlfa1   = #100#aQueQuiero# % nPosi;
                     nNume    = aAlfa1;
                     nNume    = nNume + 1;
                     aAlfa1   = ("000" + nNume) % -103;
                     aFichero = aFichero + " " + aAlfa1;
               |SIGUE;
               |HAZ Grabalo;
         |SIGUE;
         |DESCARGA_DEF #referen@;
     |SINO;
         aMensa = "     No puedo cargar el Def : " + aAlfa;
         |MENAV aMensa;
     |FINSI;
|FPRC;

|PROCESO Diferencias;
     aFichero = ("99" + Usuario) % 108;
     |NOME_DAT #99 aFichero;
     |ABRE #99;  |CIERRA #99;  |DELINDEX #99;

     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |INFOR "juanma04";
     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

     |DBASE aBase;                  |QBF aBase;
     aFich = aBase + "fich/";


     aBase = #0#0;               |QBF aBase;
     aBase = aBase + #0#1;       |QBF aBase;
     aDef  = aBase + "/def/";
     aBase = aBase + "/fich/";   |QBF aBase;

     |PARA nFichero = 3;  |SI nFichero [ 12;  |HACIENDO nFichero = nFichero + 1;
           |SI #0nFichero ! "        ";

               #0#13 = #0nFichero;

               |ABRE #99;
               nSwAplica = 1;   |HAZ GrabaTemporal;
               nSwAplica = 2;   |HAZ GrabaTemporal;
               |CIERRA #99;

               |BUCLE juanma99;

               |SI nSwPrint ! 0;  |PIEINF;  |FINSI;

               |DELINDEX #99;
           |FINSI;
     |SIGUE;

     |FININF;
     |FINIMP;
     |DELINDEX #99;
|FPRC;

|PROGRAMA;
     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;

     |CLS;
     |CABEZA "Diferencias";
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
         |HAZ Diferencias;
         |GRABA 020#0a;
     |FINSI;

     |DELINDEX #99;

     |CIERRA #0;
|FPRO;
