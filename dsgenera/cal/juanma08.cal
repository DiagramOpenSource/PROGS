|FICHEROS;
     juanma08 #0;

     referen@ #100;
|FIN;

|VARIABLES;
     aBase        = "";
     aAbre        = "";
     aAlfa        = "";
     aCarga       = "";
     aDef         = "";
     aMensa       = "";
     aRelacion    = "";
     aTitulo      = "";
     aQueQuiero   = "";
     aFichero     = "";
     aNumeroCampo = "";
     aNombreCampo = "";
     aTipoCampo   = "";
     aLongCampo   = "";
     aMinCampo    = "";
     aMaxCampo    = "";
     aAlfa1       = "";

     nHandle      = 0;
     nContador    = 0;
     nSwPrint     = 0;
     nTitulo      = 0;
     nTCampo      = 0;
     nCampo       = 0;
     nLong        = 0;
|FIN;

|PROCESO Base;  |TIPO 7;
     |DBASS aBase;               |QBF aBase;
     #0#0 = aBase;
     |PINTA #0#0;
|FPRC;

|PROCESO PonPuntoDir;  |TIPO 7;
     #0#4 = #0#1;
     |PINTA #0#4;
|FPRC;

|PROCESO Modifica;  |TIPO 0;
     |C_M #0#5,  1, "S";
     |C_M #0#6,  1, "S";
     |C_M #0#7,  1, "S";
     |C_M #0#8,  1, "S";
     |C_M #0#9,  1, "S";
     |C_M #0#10, 1, "S";

     |SI #0#2 = "F";
         #0#5  = 0;    |PINTA #0#6;   |C_M #0#5,  1, "N";
         #0#6  = 0;    |PINTA #0#6;   |C_M #0#6,  1, "N";
         #0#7  = 0;    |PINTA #0#7;   |C_M #0#7,  1, "N";
         #0#8  = 0;    |PINTA #0#8;   |C_M #0#8,  1, "N";
         #0#9  = 0;    |PINTA #0#9;   |C_M #0#9,  1, "N";
         #0#10 = "A";  |PINTA #0#10;  |C_M #0#10, 1, "N";
     |FINSI;

     |SI #0#2 = "A";
         #0#10 = "A";  |PINTA #0#10;  |C_M #0#10, 1, "N";
     |FINSI;
|FPRC;

|PROCESO ChequeaLong;
     |SI #0#5 ! 0;
         #0#6 = 0;   |PINTA #0#6;
         #0#7 = 0;   |PINTA #0#7;
         #0#8 = 0;   |PINTA #0#8;
         #0#9 = 0;   |PINTA #0#9;
     |FINSI;

     |SI #0#6 ! 0;
         #0#5 = 0;   |PINTA #0#5;
         #0#7 = 0;   |PINTA #0#7;
         #0#8 = 0;   |PINTA #0#8;
         #0#9 = 0;   |PINTA #0#9;
     |FINSI;

     |SI #0#7 ! 0;
         #0#5 = 0;   |PINTA #0#5;
         #0#6 = 0;   |PINTA #0#6;
         #0#8 = 0;   |PINTA #0#8;
         #0#9 = 0;   |PINTA #0#9;
     |FINSI;

     |SI #0#8 ! 0;
         #0#5 = 0;   |PINTA #0#5;
         #0#6 = 0;   |PINTA #0#6;
         #0#7 = 0;   |PINTA #0#7;
         #0#9 = 0;   |PINTA #0#9;
     |FINSI;

     |SI #0#9 ! 0;
         #0#5 = 0;   |PINTA #0#5;
         #0#6 = 0;   |PINTA #0#6;
         #0#7 = 0;   |PINTA #0#7;
         #0#8 = 0;   |PINTA #0#8;
     |FINSI;
|FPRC;

|PROCESO Imprime;
     |SI aTipoCampo ! #0#2;  |ACABA;  |FINSI;


     |SI aTipoCampo ! "F";
         nLong = aLongCampo;
         |SI #0#5 ! 0;  |Y nLong ! #0#5;  |ACABA;  |FINSI;
         |SI #0#6 ! 0;  |Y nLong [ #0#6;  |ACABA;  |FINSI;
         |SI #0#7 ! 0;  |Y nLong ] #0#7;  |ACABA;  |FINSI;
         |SI #0#8 ! 0;  |Y nLong < #0#8;  |ACABA;  |FINSI;
         |SI #0#9 ! 0;  |Y nLong > #0#9;  |ACABA;  |FINSI;
     |FINSI;

     |SI aTipoCampo = "N";
         aAlfa1 = aMinCampo - "EURO";
         |SI #0#10 = "E";
             |SI aAlfa1 = aMinCampo;  |ACABA;  |FINSI;
         |FINSI;

         |SI #0#10 = "P";
             |SI aAlfa1 ! aMinCampo;  |ACABA;  |FINSI;
         |FINSI;
     |FINSI;

     |SI nTitulo = 0;
         #0#3 = "";
         |PRINT;

         #0#3 = aAlfa + ".mas " + aTitulo;
         |PRINT;

         nTitulo = 1;
     |FINSI;

     #0#3 = aNumeroCampo + "   " + aNombreCampo + " " + aLongCampo + " " + aMinCampo + " " + aMaxCampo;
     |PRINT;
     nSwPrint = 1;
|FPRC;

|PROCESO MiraDef;
     aCarga = aDef + aAlfa + ".mas";  |QBT aCarga;

     aMensa = "      Cargando Def " + aCarga;
     |MENSA aMensa;

     |CARGA_DEF aCarga, referen@;
     |SI FSalida = 0;
         nTitulo    = 0;
         aQueQuiero = "|TITULO";
         aTitulo    = #100#aQueQuiero#;

         aQueQuiero  = "|NC";
         aFichero    = #100#aQueQuiero#;
         nTCampo     = aFichero;
         nTCampo     = nTCampo - 1;
         |PARA nCampo = 0;  |SI nCampo [ nTCampo;  |HACIENDO nCampo = nCampo + 1;
               aNumeroCampo = (("000" + nCampo) % -103);

               aQueQuiero   = "|C" + (("000" + nCampo) % -103) + "NOMBRE";
               aNombreCampo = (#100#aQueQuiero# + (" " * 20)) % 120;

               aQueQuiero   = "|C" + (("000" + nCampo) % -103) + "TIPO";
               aTipoCampo   = #100#aQueQuiero#;

               aQueQuiero   = "|C" + (("000" + nCampo) % -103) + "LONGITUD";
               aLongCampo   = (((" " * 8) + #100#aQueQuiero#) % -108);

               aQueQuiero   = "|C" + (("000" + nCampo) % -103) + "MINIMO";
               aMinCampo    = (((" " * 20) + #100#aQueQuiero#) % -120);

               aQueQuiero   = "|C" + (("000" + nCampo) % -103) + "MAXIMO";
               aMaxCampo    = (((" " * 20) + #100#aQueQuiero#) % -120);

               |HAZ Imprime;
         |SIGUE;
     |SINO;
         aMensa = "     No puedo cargar el Def : " + aAlfa;
         |MENAV aMensa;
     |FINSI;

     |DESCARGA_DEF #referen@;
|FPRC;

|PROCESO Busqueda;
     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |INFOR "juanma08";
     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

     aBase = #0#0;               |QBF aBase;
     aBase = aBase + #0#1;       |QBF aBase;
     aDef  = aBase + "/def/";
     aBase = aBase + "/fich/";   |QBF aBase;

     aAbre = aBase + #0#4;       |QBF aAbre;
     aAbre = aAbre + ".dir";

     nContador = 0;
     |XABRE "U", aAbre, nHandle;
     |SI FSalida ] 0;
         |PARA;  |SI FSalida ] 0;  |HACIENDO;
              nContador = nContador + 1;
              |XLEE nHandle, 255, aAlfa;
              |QBT aAlfa;
              |SI nContador = 1;  |Y aAlfa ! "";
                  |HAZ MiraDef;
              |FINSI;
              |SI nContador = 5;  nContador = 0;  |FINSI;
         |SIGUE;
     |FINSI;
     |XCIERRA nHandle;

     |SI nSwPrint ! 0;
         |PIEINF;
     |SINO;
         |MENAV "     No encotrados Campos  en la Aplicacion ";
     |FINSI;

     |FININF;
     |FINIMP;
|FPRC;

|PROGRAMA;
     |CLS;
     |CABEZA "Busqueda de Campos";
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
         |HAZ Busqueda;
     |FINSI;
|FPRO;
