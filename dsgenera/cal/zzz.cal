|FICHEROS;
     juanma09 #0;
     juanmw03 #98,MANTE;
     juanmw04 #99,MANTE;

     referen@ #100;
|FIN;

|VARIABLES;
     aBase        = "";
     aAbre        = "";
     aDef         = "";
     aCarga       = "";
     aMensa       = "";
     aRelacion    = "";
     aTitulo      = "";
     aQueQuiero   = "";
     aFichero     = "";
     aNombreDef   = "";
     aNumeroCampo = "";
     aNombreCampo = "";
     aTipoCampo   = "";
     aLongCampo   = "";
     aMinCampo    = "";
     aMaxCampo    = "";
     aAlfa        = "";
     aAlfa1       = "";
     aAlfa2       = "";
     aAlfa3       = "";
     aAlfa4       = "";
     aCaracter    = "";
     aLong        = "";

     nHandle      = 0;
     nContador    = 0;
     nSwPrint     = 0;
     nTCampo      = 0;
     nCampo       = 0;
     nLong        = 0;
     nPosi        = 0;
     nCaracter    = 0;
     nDecimal     = 0;
     nDecimales   = 0;
     nNume        = 0;
     nBoton1      = 0;
     nBoton2      = 0;
     nBoton3      = 0;
     nBoton4      = 0;
     nBoton5      = 0;
     nBoton6      = 0;
     nBoton7      = 0;
     nBoton8      = 0;
     nTecla       = 0;
|FIN;

|| Procesos del juanmw003

|RUTINA ClaveBaja99;
     #99#0 = #98#0;
     #99#1 = 0;
|FRUT;

|RUTINA ClaveAlta99;
     #99#0 = #98#0;
     #99#1 = 999;
|FRUT;

|PROCESO Tipo5w3;  |TIPO 5;
     #98#2 = " ";
     |LINEAS_BI_ATRI #98#2, D, S, -1;
|FPRC;

|PROCESO Tipo7w3;  |TIPO 7;
     |ENTLINEAL #1#99, -2, 7, 22, 0, ClaveBaja99, ClaveAlta99;
|FPRC;

|PROCESO Boton1;
     |PTEC 824;
|FPRC;

|PROCESO Boton2;
     |PTEC 825;
|FPRC;

|PROCESO Boton3;
     |PTEC 826;
|FPRC;

|PROCESO Boton4;
     |PTEC 827;
|FPRC;

|PROCESO Boton5;
     |PTEC 828;
|FPRC;

|PROCESO Boton6;
     |PTEC 829;
|FPRC;

|PROCESO Boton7;
     |PTEC 808;
|FPRC;

|PROCESO Boton8;
     |PTEC 809;
|FPRC;

|PROCESO Tipo11w3;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     |LEE 101#98=;

     |CONTROL_SIMPLE 0, "BOTON,F2 Automatico", 0568, 0780, Boton1;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F3 EURO", 0868, 0880, Boton2;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F4 EURO1", 0968, 0980, Boton3;
     nBoton3 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F5 EURO2", 1068, 1080, Boton4;
     nBoton4 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F6 EURO3", 1168, 1180, Boton5;
     nBoton5 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F7 EURO4", 1268, 1280, Boton6;
     nBoton6 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,Subir", 1573, 1580, Boton7;
     nBoton7 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,Bajar", 2273, 2280, Boton8;
     nBoton8 = FSalida;

     |ENTLINEAL #1#99, -2, 4, 22, 0, ClaveBaja99, ClaveAlta99;

     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;
     |FIN_CONTROL_WINDOWS nBoton4;
     |FIN_CONTROL_WINDOWS nBoton5;
     |FIN_CONTROL_WINDOWS nBoton6;
     |FIN_CONTROL_WINDOWS nBoton7;
     |FIN_CONTROL_WINDOWS nBoton8;

     #98#2 = "*";
     |PINTA #98#2;
     |PINTA #98#3;

     |GRABA 020#98a;
     |LIBERA #98;

     |PONCONTROL 23, "SI";
|FPRC;

|| Procesos del juanmw004

|PROCESO Tipo5w4;  |TIPO 5;
     #99#5 = " ";
     |LINEAS_BI_ATRI #99#5, D, S, -1;
|FPRC;

|PROCESO ProcesoNormal;
     |SI #99#5 = " ";
         aAlfa1 = #99#3;  |QBF aAlfa1;
         aAlfa2 = aAlfa1 - "?EURO";
         aAlfa3 = aAlfa2 % -101;
         |SI aAlfa3 ! ".";
             nDecimales = aAlfa3;
             aAlfa2     = aAlfa2 - aAlfa3;
             nNume      = aAlfa2;
             |SI nDecimales = 5;
                 aAlfa2 = aAlfa2 + "*";
             |SINO;
                 |SI nNume ] 0;
                     aAlfa2 = aAlfa2 + ("0" * nDecimales);
                 |SINO;
                     aAlfa2 = aAlfa2 + ("9" * nDecimales);
                 |FINSI;
             |FINSI;
         |SINO;
             aAlfa2 = aAlfa1 - ".?EURO";
             |SI aAlfa2 = "";  aAlfa2 = "0";  |FINSI;
         |FINSI;
     |SINO;
         nDecimales = 0;
         aAlfa1 = #99#3;  |QBF aAlfa1;
         aAlfa2 = aAlfa1 - ".";
         |SI aAlfa1 ! aAlfa2;
             aLong      = aAlfa1 % 0;
             nLong      = aLong;
             nDecimal   = 0;
             aAlfa2     = "";
             |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
                   nPosi = (100 * nCaracter) + 1;
                   aCaracter = aAlfa1 % nPosi;
                   |SI aCaracter = ".";
                       nDecimal = 1;
                   |SINO;
                       |SI nDecimal = 1;
                           |SI aCaracter = "*";
                               nDecimales = 5;
                               |SAL;
                           |FINSI;
                           nDecimales = nDecimales + 1;
                       |SINO;
                           aAlfa2 = aAlfa2 + aCaracter;
                       |FINSI;
                   |FINSI;
             |SIGUE;
         |FINSI;

         |SI nDecimales ! 0;
             |SI aAlfa2 = "0";  aAlfa2 = "";  |FINSI;
             aAlfa2 = aAlfa2 + ".?EURO" + nDecimales;
         |SINO;
             |SI aAlfa2 = "0";  aAlfa2 = "";  |FINSI;
             aAlfa2 = aAlfa2 + ".?EURO";
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO ProcesoForzado;
     aAlfa1 = #99#3;  |QBF aAlfa1;
     aAlfa2 = aAlfa1 - ".";
     |SI aAlfa1 ! aAlfa2;
         aLong      = aAlfa1 % 0;
         nLong      = aLong;
         nDecimal   = 0;
         aAlfa2     = "";
         |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
               nPosi = (100 * nCaracter) + 1;
               aCaracter = aAlfa1 % nPosi;
               |SI aCaracter = ".";
                   nDecimal = 1;
               |SINO;
                   |SI nDecimal = 0;
                       aAlfa2 = aAlfa2 + aCaracter;
                   |FINSI;
               |FINSI;
         |SIGUE;
     |FINSI;

     |SI aAlfa2 = "0";  aAlfa2 = "";  |FINSI;

     |SI nTecla = 11;  aAlfa2 = aAlfa2 + ".?EURO";   |FINSI;
     |SI nTecla = 12;  aAlfa2 = aAlfa2 + ".?EURO1";  |FINSI;
     |SI nTecla = 13;  aAlfa2 = aAlfa2 + ".?EURO2";  |FINSI;
     |SI nTecla = 14;  aAlfa2 = aAlfa2 + ".?EURO3";  |FINSI;
     |SI nTecla = 15;  aAlfa2 = aAlfa2 + ".?EURO4";  |FINSI;
|FPRC;

|PROCESO ChequeaEuro;
     |SI nTecla = 10;
         |HAZ ProcesoNormal;
     |SINO;
         |HAZ ProcesoForzado;
     |FINSI;
     #99#3 = aAlfa2;
|FPRC;

|PROCESO Tipo11w4;  |TIPO 11;
     nTecla = FSalida;
     |SI FSalida > 15;  |ACABA;  |FINSI;
     |SI FSalida < 10;  |ACABA;  |FINSI;

     |ESTADO_CONTROL nBoton1, "DISABLE";
     |ESTADO_CONTROL nBoton2, "DISABLE";
     |ESTADO_CONTROL nBoton3, "DISABLE";
     |ESTADO_CONTROL nBoton4, "DISABLE";
     |ESTADO_CONTROL nBoton5, "DISABLE";
     |ESTADO_CONTROL nBoton6, "DISABLE";

     |LEE 101#99=;

     |SI nTecla = 10;
         |SI #99#5 = "*";
             #99#5 = " ";
             #98#3 = #98#3 - 1;
             |HAZ ChequeaEuro;
         |SINO;
             #99#5 = "*";
             #98#3 = #98#3 + 1;
             |HAZ ChequeaEuro;
         |FINSI;
     |SINO;
         |HAZ ChequeaEuro;

         |SI #99#5 = " ";
             #99#5 = "*";
             #98#3 = #98#3 + 1;
         |FINSI;
     |FINSI;

     |PINTA #99#5;
     |PINTA #99#3;

     |GRABA 020#99a;
     |LIBERA #99;

     aAbre = #0#7;  |QBF aAbre;
     |XABRE "BW", aAbre, nHandle;

     aAlfa1 = #99#0;  |QBF aAlfa1;
     aAlfa  = ":" + aAlfa1 + &13 +  &10;         |XGRABA nHandle, aAlfa;
     aAlfa1 = "#" + #99#1 + ",VALOR_MINIMO=" + #99#3;  |QBF aAlfa1;
     aAlfa  = aAlfa1 + &13 + &10;                |XGRABA nHandle, aAlfa;
     |XCIERRA nHandle;

     aBase  = #0#0;  |QBF aBase;
     aAlfa1 = #0#5;  |QBF aAlfa1;
     aAlfa2 = #0#6;  |QBF aAlfa2;
     aAlfa3 = #0#7;  |QBF aAlfa3;
     aAlfa4 = #0#1;  |QBF aAlfa4;
     aAlfa  = aBase + "ut/" + aAlfa1 + " @" + aBase + " " + aAlfa4 + " @:" + aAlfa2 + " -batch:" + aAlfa3;
     |SYSTEM aAlfa;

     |FBORRA aAlfa3;

     |ESTADO_CONTROL nBoton1, "ENABLE";
     |ESTADO_CONTROL nBoton2, "ENABLE";
     |ESTADO_CONTROL nBoton3, "ENABLE";
     |ESTADO_CONTROL nBoton4, "ENABLE";
     |ESTADO_CONTROL nBoton5, "ENABLE";
     |ESTADO_CONTROL nBoton6, "ENABLE";

     |PONCONTROL 23, "SI";

     |PULSATECLA;
     |PTEC 809;
|FPRC;

|| Procesos del juanma009

|PROCESO Base;  |TIPO 7;
     aBase = #0#0;               |QBF aBase;
     |SI aBase = "";
         |DBASS aBase;               |QBF aBase;
         #0#0 = aBase;
         |PINTA #0#0;
     |FINSI;
|FPRC;

|PROCESO Fichero;  |TIPO 7;
     aBase = #0#0;         |QBF aBase;
     #0#7 = aBase + "ut/batch.txt";
     |PINTA #0#7;
|FPRC;

|PROCESO PonPuntoDir;  |TIPO 7;
     #0#2 = #0#1;
     |PINTA #0#2;
|FPRC;

|PROCESO BuscarPor;
     |SI #0#9 = "E";
         |C_M #0#10, 1, "N";  #0#10 = "";  |PINTA #0#10;
         |C_M #0#11, 1, "N";  #0#11 = "";  |PINTA #0#11;
         |C_M #0#3,  1, "N";  #0#3  = "";  |PINTA #0#3;
     |FINSI;

     |SI #0#9 = "L";
         |C_M #0#10, 1, "N";  #0#10 = "";  |PINTA #0#10;
         |C_M #0#11, 1, "N";  #0#11 = "";  |PINTA #0#11;
         |C_M #0#3,  1, "S";
     |FINSI;

     |SI #0#9 = "M";
         |C_M #0#3,  1, "N";  #0#3  = 0;   |PINTA #0#3;
         |C_M #0#11, 1, "N";  #0#11 = "";  |PINTA #0#11;
         |C_M #0#10, 1, "S";
     |FINSI;

     |SI #0#9 = "X";
         |C_M #0#3,  1, "N";  #0#3  = 0;   |PINTA #0#3;
         |C_M #0#10, 1, "N";  #0#10 = "";  |PINTA #0#10;
         |C_M #0#11, 1, "S";
     |FINSI;
|FPRC;

|PROCESO GrabaTempo;
     |SI aTipoCampo ! "N";  |ACABA;  |FINSI;

     |SI #0#9 = "L";
         nLong = aLongCampo;
         |SI nLong < #0#3;  |ACABA;  |FINSI;
     |FINSI;

     |SI #0#9 = "M";
         |SI #0#10 ! aMinCampo;  |ACABA;  |FINSI;
     |FINSI;

     |SI #0#9 = "X";
         |SI #0#11 ! aMaxCampo;  |ACABA;  |FINSI;
     |FINSI;

     |SI #0#9 = "E";
         aAlfa1 = aMinCampo - "EURO";
         |SI aAlfa1 = aMinCampo;  |ACABA;  |FINSI;
     |FINSI;

     #98#0 = aNombreDef;
     |LEE 101#98=;
     |SI FSalida ! 0;
         |DDEFECTO #98;
         #98#0 = aNombreDef;
         #98#1 = aTitulo;
         |GRABA 020#98b;
     |FINSI;

     aAlfa1 = aMinCampo - "EURO";
     #99#0 = aNombreDef;
     #99#1 = aNumeroCampo;
     #99#2 = aNombreCampo;
     #99#3 = aMinCampo;
     #99#4 = aMaxCampo;
     #99#5 = "";
     |SI aAlfa1 ! aMinCampo;
         #99#5 = "*";
         #98#2 = "*";
         #98#3 = #98#3 + 1;
     |FINSI;
     #98#4 = #98#4 + 1;
     |GRABA 020#99n;
     |LIBERA #99;

     |GRABA 020#98a;
     |LIBERA #98;

     nSwPrint = 1;
|FPRC;

|PROCESO MiraDef;
     aCarga = aDef + aNombreDef + ".mas";  |QBT aCarga;

     aMensa = "      Cargando Def " + aCarga;
     |MENSA aMensa;

     |CARGA_DEF aCarga, referen@;
     |SI FSalida = 0;
         aQueQuiero = "|TITULO";
         aTitulo    = #100#aQueQuiero#;

         aQueQuiero  = "|NC";
         aFichero    = #100#aQueQuiero#;
         nTCampo     = aFichero;
         nTCampo     = nTCampo - 1;
         |PARA nCampo = 0;  |SI nCampo [ nTCampo;  |HACIENDO nCampo = nCampo + 1;
               aNumeroCampo = (("000" + nCampo) % -103);

               aQueQuiero   = "|C" + (("000" + nCampo) % -103) + "NOMBRE";
               aNombreCampo = (#100#aQueQuiero# + (" " * 20)) % 120;

               aQueQuiero   = "|C" + (("000" + nCampo) % -103) + "TIPO";
               aTipoCampo   = #100#aQueQuiero#;

               aQueQuiero   = "|C" + (("000" + nCampo) % -103) + "LONGITUD";
               aLongCampo   = (((" " * 8) + #100#aQueQuiero#) % -108);

               aQueQuiero   = "|C" + (("000" + nCampo) % -103) + "MINIMO";
               aMinCampo    = (#100#aQueQuiero# + (" " * 20)) % 120;

               aQueQuiero   = "|C" + (("000" + nCampo) % -103) + "MAXIMO";
               aMaxCampo    = (#100#aQueQuiero# + (" " * 20)) % 120;

               |HAZ GrabaTempo;
         |SIGUE;
     |SINO;
         aMensa = "     No puedo cargar el Def : " + aNombreDef;
         |MENAV aMensa;
     |FINSI;

     |DESCARGA_DEF #referen@;
|FPRC;

|RUTINA ClaveBaja98;
     #98#0 = "        ";
|FRUT;

|RUTINA ClaveAlta98;
     #98#0 = "zzzzzzzz";
|FRUT;

|PROCESO Busqueda;
     aBase = #0#0;               |QBF aBase;
     aBase = aBase + #0#1;       |QBF aBase;
     aDef  = aBase + "/def/";
     aBase = aBase + "/fich/";   |QBF aBase;

     aAbre = aBase + #0#2;       |QBF aAbre;
     aAbre = aAbre + ".dir";

     |ABRE #98;
     |ABRE #99;
     nContador = 0;
     |XABRE "U", aAbre, nHandle;
     |SI FSalida ] 0;
         |PARA;  |SI FSalida ] 0;  |HACIENDO;
              nContador = nContador + 1;
              |XLEE nHandle, 255, aNombreDef;
              |QBT aNombreDef;
              |SI nContador = 1;  |Y aNombreDef ! "";
                  |HAZ MiraDef;
              |FINSI;
              |SI nContador = 5;  nContador = 0;  |FINSI;
         |SIGUE;
     |FINSI;
     |XCIERRA nHandle;
     |CIERRA #98;
     |CIERRA #99;

     |SI nSwPrint ! 0;
         |PARA;  |SI;  |HACIENDO;
              |ENTLINEAL #1#98, -1, 4, 11, 1, ClaveBaja98, ClaveAlta98;
              |CONFI "0000SDesea salir realmente : ";
              |SI FSalida = 0;  |SAL;  |FINSI;
         |SIGUE;
     |SINO;
         |MENAV "     No encotrados Campos  en la Aplicacion ";
     |FINSI;
|FPRC;

|PROGRAMA;
     |ABRE #0;
     |LEE 040#0p;
     |SI FSalida ! 0;
         |DDEFECTO #0;
         |GRABA 000#0b;
     |FINSI;

     aFichero = ("w03" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #98 aFichero;
     aFichero = ("w04" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #99 aFichero;

     |ABRE #98;  |CIERRA #98;  |DELINDEX #98;
     |ABRE #99;  |CIERRA #99;  |DELINDEX #99;

     |CLS;
     |CABEZA "Cambio a EURO";
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
         |GRABA 000#0a;
         |HAZ Busqueda;
     |FINSI;

     |CIERRA #0;
     |DELINDEX #98;
     |DELINDEX #99;
|FPRO;
