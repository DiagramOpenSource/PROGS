|FICHEROS;
     juanma19 #0;
|FIN;

|VARIABLES;
     aBase      = "";
     aAbre      = "";
     aAbre2     = "";
     aAlfa      = "";
     aAlfa1     = "";
     aAlfa2     = "";
     aNumero    = "";
     aCaracter  = "";

     nHandle    = 0;
     nHandle2   = 0;
     nSwLectura = 0;
     nCaracter  = 0;
     nNumero    = 0;
     nPos       = 0;
|FIN;

|PROCESO Base;  |TIPO 7;
     |DBASS aBase;               |QBF aBase;
     #0#0 = aBase;
     |PINTA #0#0;
|FPRC;

|PROCESO PonMenu;  |TIPO 7;
     #0#2 = #0#1;
     |PINTA #0#2;
|FPRC;

|PROCESO BuscaContador;
     aAlfa1 = aAlfa;  |QBF aAlfa1;
     |SI aAlfa1 = "";  |ACABA;  |FINSI;

     aAlfa1 = aAlfa % 101;
     |SI aAlfa1 = "#";  |ACABA;  |FINSI;

     aNumero = "";
     |SI aAlfa1 = "\";
         |PARA nCaracter = 2; |SI;  |HACIENDO nCaracter = nCaracter + 1;
               nPos      = (nCaracter * 100) + 1;
               aCaracter = aAlfa % nPos;
               |SI aCaracter ! "\";
                   aNumero = aNumero + aCaracter;
               |SINO;
                   |SAL;
               |FINSI;
         |SIGUE;
     |SINO;
         #0#4 = #0#4 + 1;
     |FINSI;

     nNumero = aNumero;
     |SI #0#3 < nNumero;  #0#3 = nNumero;  |FINSI;
|FPRC;

|PROCESO GrabaContador;
     aAlfa1 = aAlfa;  |QBF aAlfa1;
     |SI aAlfa1 = "";  |ACABA;  |FINSI;

     aAlfa1 = aAlfa % 101;
     aAlfa2 = aAlfa;

     |SI aAlfa1 ! "\";  |Y aAlfa1 ! "#";
         #0#3    = #0#3 + 1;
         aAlfa2  = "\" + (("00000" + #0#3) % -105) + "\" + aAlfa;
     |FINSI;

     |XGRABA nHandle2, aAlfa2;
|FPRC;

|PROCESO LeeMenu;
     |XABRE "U", aAbre, nHandle;
     |SI FSalida ] 0;
         |PARA;  |SI;  |HACIENDO;
              |XLEE nHandle, 255, aAlfa;
              |SI FSalida < 0;  |SAL;  |FINSI;

              |SI nSwLectura = 0;  |HAZ BuscaContador;  |FINSI;
              |SI nSwLectura = 1;  |HAZ GrabaContador;  |FINSI;
         |SIGUE;
     |FINSI;
     |XCIERRA nHandle;
|FPRC;

|PROCESO UltimoNumero;
     aBase = #0#0;                 |QBF aBase;
     aBase = aBase + #0#1;         |QBF aBase;

     aAbre = aBase + "/" + #0#2;   |QBF aAbre;
     aAbre = aAbre + ".men";

     #0#3       = 0;
     #0#4       = 0;
     nSwLectura = 0;  |HAZ LeeMenu;

     |PINTA #0#3;
     |PINTA #0#4;

     |LEETECLA aAlfa;
|FPRC;

|PROCESO Tipo12;  |TIPO 12;
     |SI #0#4 = 0;  |ACABA;  |FINSI;

     |CONFI "0000SDesea Numerar el Menu en estos momentos.";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aAbre2 = aBase + "/" + #0#2;   |QBF aAbre;
     aAbre2 = aAbre2 + ".bue";
     |XABRE "W", aAbre2, nHandle2;

     nSwLectura = 1;  |HAZ LeeMenu;

     |XCIERRA nHandle2;

     |RENOMBRA_FICHERO aAbre2, aAbre;
     |SI FSalida = 0;
         |MENAV "0000 El Menu ha sido numerado correctamente.";
     |SINO;
         |MENAV "0000 Revise el Menu, puede no haber quedado bien.";
     |FINSI;
|FPRC;
