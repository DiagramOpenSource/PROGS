|FICHEROS;
     nifz0001;

     nifm0001;
     nifm0002;
     dsam0103;
|FIN;

|VARIABLES;
     nBtnSal             = -1;
     nBtnCon             = -1;
     nBtnMas             = -1;
     nGrid1              = -1;
     nGrid2              = -1;
     nSalir              = -1;
     nID                 = 0;
     n1Vent              = 0;
     nHandle             = 0;
     nVeces              = 0;
     nOk                 = 0;
     nLong               = 0;
     nCarac              = 0;
     nPos                = 0;
     nFSalida            = 0;
     nRango              = 0;
     nMasivo             = 0;

     aAlfa               = "";
     aEsc1               = "";
     aEsc2               = "";
     aRetu               = "";
     aTecla              = "";
     aQB                 = "";
     aID                 = "";
     aLong               = "";
     aLee                = "";
     aBaseNIF            = "";
     aFicXml             = "";
     aFicRes             = "";
     aAbreXml            = "";
     aHora               = "";
     aAbre               = "";
     aExt                = "";
     aVer1               = "";
     aVer2               = "";
     aOri                = "";
     aDes                = "";
     aLinea              = "";
     aEnlace             = "";
     aCadena             = "";
     aCaracter           = "";
     aNom1               = "";
     aNom2               = "";


  {1}aConte              = "";

 {-1}aVent               = "";
     aVent1              = 0;
     aVent2              = 0;
     aVent3              = 0;
     aVent4              = 0;
     aVent5              = "";

     &eaNIF              = "";
     &eaAzul             = "";
     &eaVerde            = "";
     &eaRojo             = "";
     &eaRosa             = "";
     &eaMarron           = "";
     &eaUnidadBasico     = "";
     &eaIP               = "";
     &eaPathTmp          = "";
     &eaPathNIF          = "";
     &eaPathPlu          = "";
     &efFechaSys         = @;
     &eaAlfa             = "";

     &enPanta            = 0;
     &enLblRes1          = -1;
     &enLblRes2          = -1;
     &enLblRes3          = -1;
     &eaNomNIF           = "";
     &eaResNIF           = "";
|FIN;

|PROCESO Masivo;
     |VENTANA 0501, 3599, -1, 1, "";
     n1Vent = FSalida;

     |PTEC 802;  |PAUSA;
     |SUB_EJECUTA "nifz0002";

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;

     |POSICIONATE #nifz0001#0;
|FPRC;

|PROCESO Baja2;
     #nifm0002#0 = #nifm0001#0;
     #nifm0002#1 = "31.12.2999";
     #nifm0002#2 = "zzzzzzzz";
|FPRC;

|PROCESO Alta2;
     #nifm0002#0 = #nifm0001#0;
     #nifm0002#1 = "01.01.0000";
     #nifm0002#2 = "        ";
|FPRC;

|PROCESO Evnt2;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#nifm0002.=;
     |FINSI;
|FPRC;

|PROCESO Evnt1;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#nifm0001.=;

         |REFRESCACONTROL nGrid2;
     |FINSI;
|FPRC;

|PROCESO Logger;
     |VENTANA 0501, 3599, -1, 16, "NIF/CIF consultado a la AEAT";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     nRango   = 2 + 4 + 8 + 16 + 32 + 128;
     |LINEAL_SIMPLE #1#nifm0001, nRango, 0602, 2598, NULL, NULL, Evnt1;
     nGrid1 = FSalida;

     |LINEAL_SIMPLE #2#nifm0002, nRango, 2702, 3498, Baja2, Alta2, Evnt2;
     nGrid2 = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::{{001}}Salir," + nGrid1;
          |LEETECLA aTecla;
          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
          |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;
|FPRC;

|PROCESO T12;  |TIPO 12;
     |SI FSalida = 7;
         |ACABA;
     |FINSI;

     |SI nSalir = 0;
         |POSICIONATE #nifz0001#0;
         |ERROR;
         |ACABA;
     |FINSI;

     |SI nMasivo = 0;
         eaNIF = #nifz0001#0;  |QBF eaNIF;
         |SI eaNIF ! "";
             |HAZ Consulta;
         |FINSI;
     |FINSI;

     |SI nMasivo = 1;
         |HAZ Masivo;
     |FINSI;

     |SI PARAMETRO = "";
         |POSICIONATE #nifz0001#0;
         |ERROR;
     |FINSI;

     nSalir  = 0;
     nMasivo = 0;
|FPRC;

|PROCESO Ptec807;
     |PTEC 807;
|FPRC;

|PROCESO Ptec806;
     nMasivo = 0;
     |SI FSalida = nBtnMas;
         nMasivo = 1;
     |FINSI;

     |PTEC 806;
     nSalir = 1;
|FPRC;

|PROCESO T7C1;  |TIPO 7;
     |C_M #nifz0001#1, 1, "N";

     #dsam0103#0 = #nifz0001#0;
     |LEE 000#dsam0103.=;
     |SI FSalida ! 0;
         |C_M #nifz0001#1, 1, "S";
     |FINSI;

     |PINTA #nifz0001#1;
|FPRC;

|PROCESO T7C0;  |TIPO 7;
     |SI nBtnSal ! -1;  |ACABA;  |FINSI;

     aQB = "|X000";
     aID = #nifz0001#aQB#;
     nID = aID;
     |FIN_CONTROL_WINDOWS nID;

     aQB = "|X001";
     aID = #nifz0001#aQB#;
     nID = aID;
     |FIN_CONTROL_WINDOWS nID;

     |CONTROL_SIMPLE 0, "BOTON,{{001}}Salir", 2485, 2495, Ptec807;
     nBtnSal = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}NIF consultados", 2402, 2420, Logger;

     |CONTROL_SIMPLE enPanta, "{{197}}BOTON,Consultar", 0902, 0913, Ptec806;
     nBtnCon = FSalida;

     |CONTROL_SIMPLE enPanta, "{{197}}BOTON,Consulta masiva", 0985, 0998, Ptec806;
     nBtnMas = FSalida;
|FPRC;


|PROGRAMA;
     |PINPA #0#nifz0001;
     enPanta = FSalida;
     nSalir = 0;

     |SI PARAMETRO ! "";
         #nifz0001#0 = PARAMETRO;
     |FINSI;

     |PINDA #0#nifz0001;
     |ENDATOS #1#nifz0001;
|FPRO;

|PROCESO CargaUrl;
     aAbre = eaPathNIF + "sii_urls.txt";
     |XABRE "WBC", aAbre, nHandle;

     eaAlfa = "Identificacion NIF#https://www1.agenciatributaria.gob.es/wlpl/BURT-JDIT/ws/VNifV2SOAP";
     |HAZ GrabaFic;

     |XCIERRA nHandle;
|FPRC;

|PROCESO CargaColores;
     eaAzul   = "18,28,245;255,255,255";
     eaVerde  = "0,185,0;255,255,255";
     eaRojo   = "0,0,0;255,80,80";
     eaRosa   = "215,120,210;255,255,255";
     eaMarron = "200,150,0;255,255,255";
|FPRC;

|PROCESO T80;  |TIPO 80;
     efFechaSys = ~;

     |HAZ LeeUnidadBasico;
     |HAZ CargaColores;

     eaIP  = "";
     |IP_REMOTO eaIP;

     |DBASE aBaseNIF;  |QBF aBaseNIF;
     eaPathTmp = aBaseNIF + "tmp";                    |MKDIR eaPathTmp;
     eaPathTmp = aBaseNIF + "tmp/" + Usuario;         |MKDIR eaPathTmp;
     eaPathTmp = aBaseNIF + "tmp/" + Usuario + "/";

     eaPathPlu = aBaseNIF + "plantillas/";

     eaPathNIF = eaUnidadBasico + "\documentos";   |RMKDIR eaPathNIF;
     eaPathNIF = eaPathNIF + "\NIF";                |RMKDIR eaPathNIF;
     eaPathNIF = eaPathNIF + "\";

     aAlfa = eaPathTmp + "*.xml";
     |_PDIR aAlfa;
     |PARA;  |SI FSalida = 0;  |HACIENDO;
          |FBORRA aAlfa;

          |_SDIR aAlfa;
     |SIGUE;

     aAlfa = eaPathNIF + "*.*";
     |R_PDIR aAlfa;
     |PARA;  |SI FSalida = 0;  |HACIENDO;
          aExt = aAlfa % -103;
          |SI aExt = "res";  |O aExt = "xml";  |O aExt = "bat";
              |RFBORRA aAlfa;
          |FINSI;

          |R_SDIR aAlfa;
     |SIGUE;

     |HAZ CargaUrl;

     |ABRET #dsam0103;
     |ABRET #nifm0001;
     |ABRET #nifm0002;

     FSalida   = 2499;
|FPRC;

|PROCESO T90;  |TIPO 90;
     |CIERRAT #dsam0103;
     |CIERRAT #nifm0001;
     |CIERRAT #nifm0002;
|FPRC;
