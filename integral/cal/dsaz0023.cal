|FICHEROS;
     dsaz0023 #0;

     :/integral/def/dsam0000 #1;
     :/integral/def/dsam0103 #103;
     :/integral/def/dsam0017 #17,MANTE;
     :/integral/def/dsam0018 #18,MANTE;
     :/integral/def/dsam0019 #19,MANTE;

     :/laboral/def/labtraba #200;
     :/laboral/def/nomembca #201;
     :/laboral/def/nomembli #202;
     :/laboral/def/nomcontr #203;
     :/laboral/def/nomembcf #204;
|FIN;

|VARIABLES;
     aParam       = "";
     aBase        = "";
     aFichero     = "";
     aAlfa        = "";
     aAlfa1       = "";
     aAlfa2       = "";
     fFecha       = @;
     nDCliente    = 0;
     nHCliente    = 0;
     aDNif        = "";
     aHNif        = "";
     nDReg        = 0;
     nHReg        = 0;
     nDTEmbargo   = 0;
     nHTEmbargo   = 0;
     fDFecEmb     = @;
     fHFecEmb     = @;
     nError       = 0;
     esalta       = 0;
     esalta17     = 0;
     esalta18     = 0;
     nOpera       = 0;
     nUltimo      = 0;

     &eaPath      = "";
     &enEmpresa   = 0;
     &enDCliente  = 0;
     &enHCliente  = 0;
     &enDTEmbargo = 0;
     &enHTEmbargo = 0;
     &efDFecEmb   = @;
     &efHFecEmb   = @;
     &eaDNif      = "";
     &eaHNif      = "";

     &eaVarDni  = "";
     &enCalcCif = 0;

     nNume1      = 0;
     nModo       = 0;
     nUltimoAny  = 0;
     nEmpresa    = 0;
     nTrabajador = 0;
     nAny        = 0;

     nCanal          = 0;
     &eaPathLaboral  = "";
     &eaPathContagen = "";
     aPathLaboral    = "";
     swBorraLaboral  = 0;
     nTipo = 0;
     swHayConc = 0;
     swBusca = 0;
     swBorra = 0;
     nCampo = 0;
     nNume = 0;
     FEstado = 0;

     nNoNomEmb = 0;
     nNoNomRet = 0;
     nNoNomRes = 0;
     swAlgunSiNom = 0;
     nAnyEntrada = 0;

     swNoSePuede = 0;
|FIN;

|| ////////////////////////////////////////////////////////////////////
|PROCESO LeeEmpresa;
     #1#0 = #0#0;
     |LEE 001#1=;
     |SI FSalida = 0;
         #0#2 = #1#1;
         #0#3 = #1#3;
     |FINSI;
|FPRC;

|PROCESO LeeNif;
     #103#0 = #0#1;
     |LEE 001#103=;
     |SI FSalida = 0;
         #0#4 = #103#1;
     |FINSI;
|FPRC;

|PROCESO Graba0;
     |SI #17#7 < nDTEmbargo; |O #17#7 > nHTEmbargo;
         |ACABA;
     |FINSI;

     #0#0 = #17#0;
     #0#1 = #17#1;
     |LEE 001#0=;
     |SI FSalida ! 0;
         |DDEFECTO #0;
         #0#0 = #17#0;
         #0#1 = #17#1;
         |HAZ LeeEmpresa;
         |HAZ LeeNif;
         |GRABA 020#0b;
     |FINSI;

     #0#5 = #0#5 + #17#8;
     #0#6 = #0#6 + #17#9;
     #0#7 = #0#5 - #0#6;
     |SI #17#4 > "01.01.1900";
         #0#8 = #0#8 + #17#10;
     |FINSI;
     |GRABA 020#0a;
     |LIBERA #0;
|FPRC;

|DEFBUCLE dsam0017;
     #17#1;
     ;
     nDCliente, aDNif, 01;
     nHCliente, aHNif, 99;
     ;
     NULL, Graba0;
|FIN;

|| ////////////////////////////////////////////////////////////////////
|PROCESO Tipo2_18; |TIPO 2;
    esalta18 = (FEntrada / 100) ! 0;
    |SI esalta18 = 1; #18#12 = "M"; |FINSI;

    nNume1 = 0 +.1;

 |SI nNume1 < 1;

    |SI #18#12 ! "M"; |Y #18#12 ! "C";
        aAlfa1 = "";
        aAlfa  = "    Linea de Dto. vinculada a ";
        |SI #17#13 = "S";
            aAlfa1 = "una NOMINA";
            aAlfa2 = "la Nomina.";
        |SINO;
            aAlfa2 = "el Asiento.";
            |SI #18#9 ! 0; aAlfa1 = "un ASIENTO CONTABLE"; |FINSI;
        |FINSI;
        |SI aAlfa1 ! "";
            |SI esalta18 = 3; aAlfa = aAlfa + aAlfa1 + " No se puede dar de Baja"; |FINSI;
            |SI esalta18 = 2; aAlfa = aAlfa + aAlfa1 + " Modifique "+ aAlfa2; |FINSI;
            |MENAV aAlfa;
            swNoSePuede = 1;
            |ERROR;
            |ACABA;
        |FINSI;
    |FINSI;
    |SI #18#12 = "C";
        aAlfa  = "    Linea de la Cancelacion del Embargo. ";
        |SI esalta18 = 3; aAlfa = aAlfa + " No se puede dar de Baja"; |FINSI;
        |SI esalta18 = 2; aAlfa = aAlfa + " No se puede modificar";   |FINSI;
        |MENAV aAlfa;
        swNoSePuede = 1;
        |ERROR;
        |ACABA;
    |FINSI;
 |FINSI;

|SI swNoSePuede = 1;
     || para que no descuente el importe de la cabecera y por tanto del
     || mantenimiento de embargos de la nomina si no se ha modificado la linea
     swNoSePuede = 0;
     |ACABA;
|FINSI;

    #17#9  = #17#9 +. #18#6;  |PINTA #17#9;
    #17#10 = #17#8 - #17#9;   |PINTA #17#10;
    #0#6   = #0#6  +. #18#6;  |PINTA #0#6;
    #0#7   = #0#5  -  #0#6;   |PINTA #0#7;
|FPRC;

|PROCESO DespuesLinea; |TIPO 0;
   esalta18 = ( FEntrada/100) ! 0;
   |SI esalta18 = 1; |Y #17#4 > "01.01.1900";
       |MENAV "    Embargo Cancelado, no puede dar de alta lineas de Descuento";
       |ERROR;
   |FINSI;
|FPRC;

|PROCESO DespuesFecha; |TIPO 0;
     |SI #18#4 < #17#3;
         |CONFI "    NFecha Operacion menor que la Fecha de Notificacion.Es correcto?";
         |SI FSalida ! 0;
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;
     |SI #18#4 > #17#4; |Y #17#4 > "01.01.1900";
         |MENAV "    Fecha Operacion mayor que la fecha de Cancelacion.";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO DespuesImporte; |TIPO 0;

     |SI #18#6 > #17#10;
         |MENAV "    Error! El Importe es superior al importe pendiente de Embargo";
         |ERROR;
     |FINSI;
|FPRC;

|| ////////////////////////////////////////////////////////////////////
|PROCESO dsam0018;
     |SI nOpera = 4;
         #17#9 = #17#9 + #18#6;
     |SINO;
         |SI #18#4 > #17#4;
             nError = 1;
             |ERROR10;
         |FINSI;
     |FINSI;
     |LIBERA #18;
|FPRC;

|DEFBUCLE dsam0018;
     #18#1;
     ;
     #17#0, #17#1, #17#2, 01;
     #17#0, #17#1, #17#2, 99;
     e;
     NULL, dsam0018;
|FIN;

|PROCESO borrardsam0018;
     |BORRA 020#18a;
     |LIBERA #18;
|FPRC;

|DEFBUCLE borrardsam0018;
     #18#1;
     ;
     #0#0, aDNif, nDReg, 01;
     #0#0, aHNif, nHReg, 99;
     be;
     NULL, borrardsam0018;
|FIN;

|PROCESO borrardsam0017;
     |BORRA 020#17a;
     |LIBERA #17;
|FPRC;

|DEFBUCLE borrardsam0017;
     #17#1;
     ;
     #0#0, aDNif, nDReg;
     #0#0, aHNif, nHReg;
     be;
     NULL, borrardsam0017;
|FIN;
|| ////////////////////////////////////////////////////////////////////

|PROCESO ClaveBaja18;
     #18#0 = #17#0;
     #18#1 = #17#1;
     #18#2 = #17#2;
     #18#3 = 01;
|FPRC;

|PROCESO ClaveAlta18;
     #18#0 = #17#0;
     #18#1 = #17#1;
     #18#2 = #17#2;
     #18#3 = 99;
|FPRC;

|PROCESO Tipo7_17;  |TIPO 7;

     |ENTLINEAL #1#18, -4, 7, 22, 0, ClaveBaja18, ClaveAlta18;
     |ABRE #18;
     #18#0 = #17#0;
     #18#1 = #17#1;
     #18#2 = #17#2;
     #18#3 = 01;
     |LEE 041#18];
     |SI FSalida = 0; |Y #18#0 = #17#0; |Y #18#1 = #17#1; |Y #18#2 = #17#2;
         |PINDA #0#18;
     |FINSI;
     |CIERRA #18;

     |HAZ Activa12;
|FPRC;

|PROCESO LineaCancela;
     |ABRE #18;
     #18#0 = #17#0;
     #18#1 = #17#1;
     #18#2 = #17#2;
     #18#3 = 99;
     |LEE 041#18];
     |SI FSalida = 0;
         |LEE 041#18a;
     |SINO;
         |LEE 041#18u;
     |FINSI;
     nUltimo = 0;
     |SI FSalida = 0; |Y #18#0 = #17#0; |Y #18#1 = #17#1; |Y #18#2 = #17#2;
         nUltimo = #18#3;
     |FINSI;

     |SI nOpera = 1; || Alta
         nUltimo = nUltimo + 1;
         |DDEFECTO #18;
         #18#0  = #17#0;
         #18#1  = #17#1;
         #18#2  = #17#2;
         #18#3  = nUltimo;
         #18#4  = #17#4;
         #18#5  = "CANCELACION";
         #18#6  = #17#10;
         #18#12 = "C";
         |GRABA 020#18n;
         #17#9  = #17#9 + #18#6;  |PINTA #17#9;
         #17#10 = #17#8 - #17#9;  |PINTA #17#10;
         || ESTAS DOS LINEAS HACIAN QUE SE RESTARA DOS VECES LO QUE QUEDABA
         || DE EMBARGO, las quito. Migue 02.10.2014
         || #0#6   = #0#6  + #18#6;  |PINTA #0#6;
         || #0#7   = #0#5  -  #0#6;  |PINTA #0#7;
     |FINSI;
     |SI nOpera = 2; |Y #18#12 = "C";  || Modificacion
         #18#4 = #17#4;
         |GRABA 020#18a;
     |FINSI;
     |SI nOpera = 3;  |Y #18#12 = "C"; || Baja
         |BORRA 020#18a;
         #17#9  = #17#9 - #18#6;  |PINTA #17#9;
         #17#10 = #17#8 - #17#9;  |PINTA #17#10;
         || ESTAS DOS LINEAS HACIAN QUE SE RESTARA DOS VECES LO QUE QUEDABA
         || DE EMBARGO, las quito. Migue 02.10.2014
         || #0#6 = #0#6 - #18#6;  |PINTA #0#6;
         || #0#7 = #0#5 + #0#6;  |PINTA #0#7;
     |FINSI;

     |CIERRA #18;
|FPRC;

|PROCESO FCancelacion; |TIPO 0;
     |SI FSalida ] 1; |Y FSalida [ 5; |ACABA; |FINSI;

     aAlfa = Contenido; fFecha = Contenido;

     |SI #17#4 > "01.01.0000";
         |SI #17#3 > "01.01.0000"; |Y #17#4 < #17#3;
             |MENAV "    Error! Fecha Cancelacion anterior a la Fecha Notificacion";
             #17#4 = fFecha; |PINTA #17#4;
             |ERROR;
             |ACABA;
         |SINO;
              esalta17 = (FEntrada / 100) ! 0;
              |SI esalta17 = 2;
                  nError = 0;
                  |BUCLE dsam0018;
                  |SI nError = 1;
                     |MENAV "    Error! Fecha Cancelacion posterior al ultimo movimiento";
                     #17#4 = fFecha; |PINTA #17#4;
                     |ERROR;
                     |ACABA;
                  |FINSI;
              |FINSI;
         |FINSI;
     |FINSI;

     |SI #17#4 ! fFecha;
          nOpera = 0;
          |SI fFecha [ "01.01.0000"; |Y #17#4 > "01.01.0000";
               nOpera = 1;   || alta
         |FINSI;
         |SI fFecha > "01.01.0000"; |Y #17#4 [ "01.01.0000";
             nOpera = 3;
    ||         |MENAV "    Recuerde: si tiene informada la Linea de Cancelacion, se borrara";
         |FINSI;
         |SI fFecha > "01.01.0000"; |Y #17#4 > "01.01.0000";
             nOpera = 2;
    ||       |MENAV "    Recuerde: si tiene informada la Linea de Cancelacion, se modificara la fecha";
         |FINSI;
         |SI nOpera ! 0;
             aAlfa = "    SGrabar la Linea de Cancelacion. Continuar";
             |SI nOpera = 2;
                 aAlfa = "    SEl Mov. de Cancelacion se Modifica. Continuar";
             |FINSI;
             |SI nOpera = 3;
                 aAlfa = "    SEl Mov. de Cancelacion se Borra. Continuar";
             |FINSI;
             |CONFI aAlfa;
             |SI FSalida = 0;
                 |HAZ LineaCancela;
             |SINO;
                 #17#4 = fFecha; |PINTA #17#4;
                 |ERROR;
             |FINSI;
         |FINSI;
     |FINSI;
|FPRC;


|PROCESO Tipo1_17;  |TIPO 1;
     esalta17 = (FEntrada / 100) ! 0;
     |SI esalta17 = 3;
         |CONFI "    NEsta Seguro de Borrar este Embargo ";
         |SI FSalida ! 0;
             |ERROR;
             |ACABA;
         |FINSI;
         aDNif = #17#1; aHNif = #17#1;
         nDReg = #17#2; nHReg = #17#2;
         |BUCLE borrardsam0018;
         |HAZ EmbargoLaboral;
     |FINSI;
|FPRC;

|PROCESO Tipo2_17; |TIPO 2;
     #0#5 = #0#5 +. #17#8;       |PINTA #0#5;
     #0#6 = #0#6 +. #17#9;       |PINTA #0#6;
     #0#7 = #0#5 -  #0#6;        |PINTA #0#7;
     |SI #17#4 > "01.01.1900";
         #0#8 = #0#8 +. #17#10;
     |FINSI;
     |HAZ EmbargoLaboral;
|FPRC;

/***************** PROCESOS PARA ACTUALIZAR O DAR DE ALTA *******************/
/************************* EL EMBARGO EN LABORAL ****************************/

|PROCESO BorraNomembli;
     |BORRA 020#nomembli.a;
|FPRC;

|DEFBUCLE BorraNombemli;
     #nomembli#1;
     ;
     nEmpresa, nTrabajador, 2000;
     nEmpresa, nTrabajador, 2999;
     ;
     NULL, BorraNomembli;
|FIN;

|PROCESO BorraEmbargoLaboral;
     #nomembca#0 = nEmpresa;
     #nomembca#1 = nTrabajador;
     |LEE 000#nomembca.=;
     |SI FSalida = 0;
          aAlfa =  "    S¿Desea borrar el embargo del trabajador en la ";
          aAlfa = aAlfa + "aplicación laboral?";
          |CONFI aAlfa;
          |SI FSalida = 0;
               |BORRA 020#nomembca.a;
               |BUCLE BorraNombemli;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO ActNomembli;
     |DDEFECTO #nomembli;

     #nomembli#0 = #nomembca#0;
     #nomembli#1 = #nomembca#1;

     |SI nUltimoAny ! 0;
          aAlfa = #dsam0017#3;
          aAlfa = aAlfa % -104;
          nAnyEntrada = aAlfa;

          |SI nAnyEntrada > nUltimoAny;
               nNume = nAnyEntrada;
          |SINO;
               nNume = nUltimoAny;
          |FINSI;
     |SINO;
          || aAlfa = #dsam0017#3;
          fFecha = ~;
          aAlfa = fFecha;
          aAlfa = aAlfa % -104;
          nNume = aAlfa;
     |FINSI;

     #nomembli#2 = nNume;
     |LEE 101#nomembli.=;
     FEstado = FSalida;

     |PARA nCampo = 26; |SI nCampo > 4; |HACIENDO nCampo = nCampo - 2;
          |SI #nomembli#nCampo# ! 0; |Y #dsaz0023#7 ! 0;
               #nomembli#nCampo# = #dsaz0023#7;
               |SAL;
          |FINSI;
     |SIGUE;

     |HAZ EmbNoNomina;

     #nomembli#27 = #dsaz0023#5 - nNoNomEmb;
     #nomembli#28 = #dsaz0023#6 - nNoNomRet;
     #nomembli#29 = #dsaz0023#7 - nNoNomRes;

     |SI FEstado = 0;
          |GRABA 020#nomembli.a;
     |SINO;
          |GRABA 020#nomembli.n;
     |FINSI;
     |LIBERA #nomembli;
|FPRC;

|PROCESO LeeNomembli;
     nUltimoAny = #nomembli#2;
|FPRC;

|DEFBUCLE LeeNomembli;
     #nomembli#1;
     ;
     #nomembca#0, #nomembca#1, 2000;
     #nomembca#0, #nomembca#1, 2999;
     ;
     NULL, LeeNomembli;
|FIN;

|PROCESO CompEmbargo;
     #nomembca#4 = #dsam0017#7;
     #nomembca#5 = #dsam0017#11;

     |HAZ EmbNoNomina;

     #nomembca#6 = #dsaz0023#5 - nNoNomEmb;
     || este del dsaz, pero es queriendo

     #nomembca#7 = #dsam0017#12;
     #nomembca#9 = #dsam0017#14;
     #nomembca#10 = #dsam0017#15;
     |GRABA 020#nomembca.a;

     nUltimoAny = 0;
     |BUCLE LeeNomembli;

     |HAZ ActNomembli;
|FPRC;

|PROCESO nomembcf;
     |BORRA 020#nomembcf.a;
|FPRC;

|DEFBUCLE nomembcf;
     #nomembcf#1;
     ;
     nEmpresa, nTrabajador, 101;
     nEmpresa, nTrabajador, 999;
     ;
     NULL, nomembcf;
|FIN;

|PROCESO dsam0019;
     swHayConc = 1;
     |SI swBusca = 1;
          |ACABA;
     |FINSI;
     |SI swBusca = 0;
          #nomembcf#0 = nEmpresa;
          #nomembcf#1 = nTrabajador;
          #nomembcf#2 = #dsam0019#3;
          #nomembcf#3 = #dsam0019#4;
          #nomembcf#4 = #dsam0019#5;
          |GRABA 020#nomembcf.n;
          |LIBERA #nomembcf;
     |FINSI;
|FPRC;

|DEFBUCLE dsam0019;
     #dsam0019#1;
     ;
     #17#0, #17#1, #17#2, 101;
     #17#0, #17#1, #17#2, 999;
     ;
     NULL, dsam0019;
|FIN;

|PROCESO conceptos;
     |ABRE #nomembcf;
     swHayConc = 0;
     swBusca = 1;
     |BUCLE dsam0019;

     |SI swHayConc = 0;
     /*
          || arriesgado
          swBorra = 1;
          |BUCLE nomembcf;
     */
     |SINO;
          swBorra = 1;
          |BUCLE nomembcf;
          swBusca = 0;
          |BUCLE dsam0019;
     |FINSI;
     swBusca = 0;

     |CIERRA #nomembcf;
|FPRC;

|PROCESO Busca0017;
     |SI #dsam0017#13 = "N";
          nNoNomEmb = nNoNomEmb + #dsam0017#8;
          nNoNomRet = nNoNomRet + #dsam0017#9;
          nNoNomRes = nNoNomRes + #dsam0017#10;
     |FINSI;
|FPRC;

|DEFBUCLE Busca0017;
     #dsam0017#1;
     ;
     #dsaz0023#0, #dsaz0023#1, 01;
     #dsaz0023#0, #dsaz0023#1, 99;
     ;
     NULL, Busca0017;
|FIN;

|PROCESO EmbNoNomina;
     nNoNomEmb = 0;
     nNoNomRet = 0;
     nNoNomRes = 0;
     |SALVA_FICHA #dsam0017;

     |BUCLE Busca0017;

     |REPON_FICHA #dsam0017;
|FPRC;

|PROCESO NuevoEmbargo;
     #nomembca#0 = nEmpresa;
     #nomembca#1 = nTrabajador;
     #nomembca#2 = #1#1;
     #nomembca#3 = #0#4;
     #nomembca#4 = #dsam0017#7;
     #nomembca#5 = #dsam0017#11;
     #nomembca#6 = #dsam0017#8;
     #nomembca#7 = #dsam0017#12;
     #nomembca#9 = #dsam0017#14;
     #nomembca#10 = #dsam0017#15;
     |GRABA 020#nomembca.n;

     || fFecha = ~;           || se crea en nomina en el a¤o de la fecha que se
     || aAlfa = fFecha;       || crea aqui, lo dejamos asi definitivamente
     aAlfa = #dsam0017#3;     || la nomina se encargara de crear la linea del a¤o
     aAlfa = aAlfa % -104;    || si despues se empezara a aplicar en un a¤o posterior
     nAny = aAlfa;

     #nomembli#0 = nEmpresa;
     #nomembli#1 = nTrabajador;
     #nomembli#2 = nAny;

     |HAZ EmbNoNomina;

     #nomembli#27 = #dsaz0023#5 - nNoNomEmb;
     #nomembli#28 = #dsaz0023#6 - nNoNomRet;
     #nomembli#29 = #dsaz0023#7 - nNoNomRes;

     |GRABA 020#nomembli.n;
     |LIBERA #nomembli;

     |HAZ conceptos;
|FPRC;

|PROCESO ActualizaEmbargo;
     |DDEFECTO #nomembca;
     |DDEFECTO #nomembli;

     #nomembca#0 = nEmpresa;
     #nomembca#1 = nTrabajador;
     |LEE 101#nomembca.=;
     |SI FSalida ! 0;
          |HAZ NuevoEmbargo;
     |SINO;
          |HAZ CompEmbargo;
     |FINSI;
     |LIBERA #nomembca;
     |HAZ conceptos;
|FPRC;

|PROCESO labtraba;
     nEmpresa = #labtraba#0;
     nTrabajador = #labtraba#1;
     |SI swBorraLaboral = 1;
          |HAZ BorraEmbargoLaboral;
     |SINO;
          |SI nModo = 1; |O nModo = 2;
               |HAZ ActualizaEmbargo;
          |FINSI;
          |SI nModo = 3;
               |SI esalta17 = 3;
                    |HAZ ActualizaEmbargo;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE labtraba;
     #labtraba#5;
     44;
     #0#0, #0#1, "A";
     #0#0, #0#1, "A";
     ;
     NULL, labtraba;
|FIN;

|PROCESO AlgunSiNom;
     |SI #dsam0017#13 = "S";
          swAlgunSiNom = 1;
     |FINSI;
|FPRC;

|DEFBUCLE AlgunSiNom;
     #dsam0017#1;
     ;
     #dsaz0023#0, #dsaz0023#1, 01;
     #dsaz0023#0, #dsaz0023#1, 99;
     ;
     NULL, AlgunSiNom;
|FIN;

|PROCESO EmbargoLaboral;
     |SI eaPathLaboral = ""; |ACABA; |FINSI;  || No esta instalada Laboral

     swAlgunSiNom = 0;
     |SALVA_FICHA #dsam0017;
     |BUCLE AlgunSiNom;
     |REPON_FICHA #dsam0017;
     |SI swAlgunSiNom = 0; |ACABA; |FINSI;

     |DBASS aAlfa;
     |QBF aAlfa;
     aPathLaboral = eaPathLaboral + "fich/";
     |PATH_DAT #200 aPathLaboral;
     |PATH_DAT #201 aPathLaboral;
     |PATH_DAT #202 aPathLaboral;
     |PATH_DAT #203 aPathLaboral;
     |PATH_DAT #204 aPathLaboral;

     |ABRE #labtraba;
     |ABRE #nomembca;
     |ABRE #nomembli;
     |ABRE #nomcontr;

     |LEE 000#nomcontr.p;
     |SI #nomcontr#65 ! "S";
          |CIERRA #labtraba;
          |CIERRA #nomcontr;
          |CIERRA #nomembli;
          |CIERRA #nomembca;
          |ACABA;
     |FINSI;

     nModo = (FEntrada / 100) ! 0;
     nEmpresa = 00000;
     nTrabajador = 00000;

     |BUCLE labtraba;

     |SI nEmpresa ! 0; |Y nTrabajador ! 0;
          || ha entrado en el bucle
     |SINO;
          aAlfa = "    ATENCION: No se ha encontrado un trabajador de alta ";
          aAlfa = aAlfa + "en laboral con este NIF";
          |MENAV aAlfa;
     |FINSI;

     |CIERRA #labtraba;
     |CIERRA #nomcontr;
     |CIERRA #nomembli;
     |CIERRA #nomembca;
|FPRC;

/************************** FIN PROCESOS LABORAL ****************************/

|PROCESO EmbargoIntegral;
    #17#9  = 0;
    nOpera = 4;
    |BUCLE dsam0018;
    nOpera = 0;
                            |PINTA #17#9;
    #17#10 = #17#8 - #17#9; |PINTA #17#10;
|FPRC;

|PROCESO Tipo3_17;  |TIPO 3;
     |ENTLINEAL #1#18, -4, 1, 22, 0, ClaveBaja18, ClaveAlta18;
     |HAZ EmbargoIntegral;
     |HAZ EmbargoLaboral;
|FPRC;

|PROCESO Ayuda7; |TIPO 7;
     |PUSHV 09012345;

     |C_M #17Campo, 0, aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     |ATRI R;
               ||  0        1         2         3         4
               ||  2345678901234567890123456789012345678901234
     |PINTA 0902, "      - Tipos de c lculo de embargo -      ";
     |PINTA 1002, " 1. Cantidad fija todos los meses.         ";
     |PINTA 1102, " 2. Porcentaje sobre el l¡quido mensual    ";
     |PINTA 1202, " 3. C lculo autom tico seg£n normativa le- ";
     |PINTA 1302, "    gal. Se calcula seg£n c lculo anterior ";
     |PINTA 1402, "    de la AEAT, siendo inembargable un SMI ";
     |PINTA 1502, "    por paga extra,incluyendo prorrateadas ";
     |PINTA 1602, " 4. Igual que el punto 3 para embargos so- ";
     |PINTA 1702, "    bre deudores hipotecarios que han per- ";
     |PINTA 1802, "    dido su vivienda habitual.             ";
     |PINTA 1902, " 5. C lculo autom tico seg£n normativa le- ";
     |PINTA 2002, "    gal, seg£n c lculo ACTUAL de la  AEAT, ";
     |PINTA 2102, "    siendo inembargable UN SMI por mes in- ";
     |PINTA 2202, "    dependientemente de las  pagas  extras ";
     |PINTA 2302, "    que tenga el trabajador.               ";
     |ATRI 0;
|FPRC;

|PROCESO Restaura;
     |POPV;
     |SI #17#7 = 0; |Y #17#13 = "S";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO min_con;
     #19#0 = #17#0;
     #19#1 = #17#1;
     #19#2 = #17#2;
     #19#3 = 101;
|FPRC;

|PROCESO max_con;
     #19#0 = #17#0;
     #19#1 = #17#1;
     #19#2 = #17#2;
     #19#3 = 999;
|FPRC;

|PROCESO Repon;
     || dos veces pon
     |POPV;
     |POPV;
|FPRC;

|PROCESO PintaConceptos; |TIPO 7;
     |SI Campo = 7;
          nTipo = #17#7;
     |FINSI;
     |PUSHV 09491779;
     |ENTLINEAL #1#dsam0019, -4, 7, 15, 1, min_con, max_con;
|FPRC;

|PROCESO IntroConceptos;
     |SI FSalida = 13; |Y #17#7 ! 1;
          |ENTLINEAL #1#dsam0019, -4, 1, 15, 1, min_con, max_con;
     |FINSI;
|FPRC;

|PROCESO Importe;
     #17#10 = #17#8 - #17#9; |PINTA #17#10;
     |SI #17#10 < 0;
         |MENAV "    Error. Importe Descontado mayor al Importe del Embargo";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Dto1; |TIPO 7;
     |C_M #17#11, 1, "S";
     |SI #17#7 = 0; |O #17#7 ] 3;
          #17#11 = 0;
          |PINTA #17#11;
          |C_M #17#11, 1, "N";
     |FINSI;
|FPRC;

|PROCESO Dto2;
     |SI #17#7 = 2; |Y #17#11 > 100;
          |MENAV "    ATENCION: El porcentaje de descuento nunca puede superar el 100%";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Activa12; |TIPO 7;
     |SI #17#7 = 4;
          |ATRI 0;
          |C_M #17#12, 1, "S";
          |C_V #17#12, 1, "S";
          |PINTA 1457, "-  ";
          |PINTA #17#12;
          |ATRI R;
     |SINO;
          |ATRI 0;
          |C_M #17#12, 1, "N";
          |C_V #17#12, 1, "N";
          #17#12 = 0;
          |PINTA 1457, "   ";
          |ATRI I;
     |FINSI;
|FPRC;

|PROCESO DeNomina7; |TIPO 7;
    esalta17 = (FEntrada / 100) ! 0;
    |SI esalta17 = 1;
        |C_M #17#13,1, "S";
    |SINO;
        |C_M #17#13,1, "N";
    |FINSI;
|FPRC;

|PROCESO DeNomina;
     |C_M #17#7,1, "S";
     |SI #17#13 = "N";
         |C_M #17#7, 1, "N";
         |C_M #17#11,1, "N";
         #17#7  = 0; |PINTA #17#7;
         #17#11 = 0; |PINTA #17#11;
         #17#14 = "N"; |PINTA #17#14;
         #17#15 = "T"; |PINTA #17#15;
    |FINSI;
|FPRC;

|PROCESO Activa15;
     |C_M #17#15, 1, "S";
     |SI #17#14 = "N";
         |C_M #17#15, 1, "N";
         #17#15 = "T"; |PINTA #17#15;
    |FINSI;
|FPRC;

|| ////////////////////////////////////////////////////////////////////
|| ////////////////////////////////////////////////////////////////////
|PROCESO ClaveBaja17;
     #17#0 = #0#0;
     #17#1 = #0#1;
     #17#2 = 01;
|FPRC;

|PROCESO ClaveAlta17;
     #17#0 = #0#0;
     #17#1 = #0#1;
     #17#2 = 99;
|FPRC;

|PROCESO Tipo7_0;  |TIPO 7;

     |ENTLINEAL #1#17, -3, 7, 22, 0, ClaveBaja17, ClaveAlta17;
     |ABRE #17;
     #17#0 = #0#0;
     #17#1 = #0#1;
     #17#2 = 01;
     |LEE 041#17];
     |SI FSalida = 0; |Y #17#0 = #0#0; |Y #17#1# = #0#1;
         |PINDA #0#17;
         |ENTLINEAL #1#18, -4, 7, 22, 0, ClaveBaja18, ClaveAlta18;
     |FINSI;
     |CIERRA #17;

|FPRC;

|PROCESO Tipo1_0;  |TIPO 1;
     esalta = (FEntrada / 100) ! 0;
||     |SI esalta = 2;
||         |ERROR;
||         |PTEC 825;
||     |FINSI;
     |SI esalta = 3;
         |CONFI "    NEsta Seguro de Borrar todos los Embargos de Este Nif";
         |SI FSalida ! 0;
             |ERROR;
             |ACABA;
         |FINSI;
         aDNif = #0#1; aHNif = #0#1;
         nDReg = 01;   nHReg = 99;
         |BUCLE borrardsam0017;
         |BUCLE borrardsam0018;

         swBorraLaboral = 1;
         |HAZ EmbargoLaboral;
         swBorraLaboral = 0;
     |FINSI;
|FPRC;

|PROCESO Tipo11_0;  |TIPO 11;
     |SI FSalida = 10;
         eaVarDni   = #0Campo;
         enCalcCif  = 1;
         |HAZ CalculoDNI;
         #0Campo = eaVarDni;
         |PINTA #0Campo;
         |ERROR;
         |ACABA;
     |FINSI;
     |SI FSalida = 11;  |Y aParam = "ConsultaNif";
         |ENTLINEAL #1#17, -3, 4, 22, 0, ClaveBaja17, ClaveAlta17;
         |PTEC 806;
     |FINSI;
|FPRC;

|PROCESO Tipo6_0;  |TIPO 6;
     eaVarDni  = #0Campo;
     enCalcCif = 0;
     |HAZ CalculoDNI;
     eaVarDni = (eaVarDni + "            ") % 112;
     |SI #0Campo ! eaVarDni;
         |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
         |SI FSalida = 0;
             #0Campo = eaVarDni;  |PINTA #0Campo;
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo3_0;  |TIPO 3;
     |ENTLINEAL #1#17, -3, 2, 22, 0, ClaveBaja17, ClaveAlta17;
|FPRC;

|| **********************************************************************
|| **********************************************************************

|RUTINA ClaveBaja0;
     #0#0 = nDCliente;
     #0#1 = "            ";
|FRUT;

|RUTINA ClaveAlta0;
     #0#0 = nHCliente;
     #0#1 = "zzzzzzzzzzzz";
|FRUT;

|PROCESO BusApli;
     eaPathContagen  = "";
     eaPathLaboral   = "";

     |DBASS aBase;   |QBF aBase;
     aAlfa1 = aBase + "contagen/def/canempre.mas";
     |XABRE "E", aAlfa1, nCanal;
     |SI FSalida ] 0;
         eaPathContagen = aBase + "contagen/";
     |FINSI;

     aAlfa1 = aBase + "laboral/def/labempre.mas";
     |XABRE "E", aAlfa1, nCanal;
     |SI FSalida ] 0;
         eaPathLaboral = aBase + "laboral/";
     |FINSI;
|FPRC;

|PROGRAMA;
      aParam = PARAMETRO; |QBF aParam;

      |HAZ  BusApli;

      aFichero = ("23" + Usuario + "zzzzzzzz") % 108;
      |NOME_DAT #0 aFichero;

      |ABRE #0;  |CIERRA #0;  |DELINDEX #0;

      |SI aParam = "";

          |PINPA #0#0;
          |MENSA "0000 Configurando Datos, Espere un Momento";
          nDCliente  = enEmpresa;
          nHCliente  = enEmpresa;
          nDTEmbargo = 0;
          nHTEmbargo = 9;
          fDFecEmb   = "01.01.0000";
          fHFecEmb   = "31.12.2999";
          aDNif      = "            ";
          aHNif      = "zzzzzzzzzzzz";

      |SINO;

          nDCliente  = enDCliente;
          nHCliente  = enHCliente;
          nDTEmbargo = enDTEmbargo;
          nHTEmbargo = enHTEmbargo;
          fDFecEmb   = efDFecEmb;
          fHFecEmb   = efHFecEmb;
          aDNif      = eaDNif;
          aHNif      = eaHNif;

      |FINSI;

      |ABRE #103;
      |ABRE #1;
      |ABRE #0;
      |BUCLE dsam0017;
      |CIERRA #0;
      |CIERRA #1;
      |CIERRA #103;

      |BLIN 4;
      |SI aParam = "";
          |ENTLINEAL #1#0, -2, 2, 22, 0, ClaveBaja0, ClaveAlta0;
      |FINSI;
      |SI aParam = "LABORAL";
          |PINPA #0#17;
          |ENTLINEAL #1#0, -2, 2, 22, 0, ClaveBaja0, ClaveAlta0;
      |FINSI;
      |SI aParam = "ConsultaNif";
          |CLS;
          |PINTA 0502, #0#0;
          |PINTA 0509, #0#3;
          |PINTA 0522, #0#4;
          |ATRI I; |PINTA 0702, "- Gestion de Embargos"; |ATRI 0;
          |PINPA #0#17;
          |PTEC 825;
          |ENTLINEAL #1#0, -2, 4, 22, 0, ClaveBaja0, ClaveAlta0;
      |FINSI;
      |DELINDEX #0;
|FPRO;
