|FICHEROS;
     pntz0001;

     dsam0001;

     pntm0001;
     pntm0002;
     pntm0003;

     pntw0001;

     :/dsoporte/def/dsorm002;
     :/dsoporte/def/dsprem05;
     :/dsoporte/def/dsorz999;
|FIN;

|VARIABLES;
     nVentPNT         = 0;
     n0Vent           = 0;
     nCampo           = 0;
     nPanta           = 0;
     nHandle          = 0;
     nLinea           = 0;
     nGrid            = 0;
     nGrid3           = 0;
     nBtnMost         = 0;
     nBtnHabi         = 0;
     nBtn1            = 0;
     nBtn2            = 0;
     nNume            = 0;
     nRango           = 0;
     nError           = 0;

     aAlfa            = "";
     aTexto           = "";
     aContenido       = "";
     aBuscar          = "";
     aBase            = "";
     aAbre            = "";
     aRefresca        = "";
     aEsc1            = "";
     aEsc2            = "";
     aRetu            = "";
     aTecla           = "";
     aUsuario         = "";
     aMas             = "";

     {-1}sTree        = 0;
     sT_nID           = 0;
     sT_nOper         = 0;
     sT_aData         = "";

     {1}aContiene     = "";

     {-1}aVent        = "";
         aVent1       = 0;
         aVent2       = 0;
         aVent3       = 0;
         aVent4       = 0;
         aVent5       = "";

     &enTree          = 0;
     &enVentana       = 0;
     &enCreaUsuarios  = 0;
     &eaTipoDSOPORTE  = "";
     &eaAlfa          = "";
     &eaRutaOrigen    = "";
     &eaRutaDestino   = "";
     &eaUnidadBasico  = "";
     &eaFicheroTxt    = "";
     &eaFicheroTgz    = "";
     &eaFicheroTar    = "";
     &eaPathDSoporte  = "";
     &eaPathGecochek  = "";
|FIN;

|PROCESO DUsuario;
     |LEE 101#pntm0001.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |VENTANA 0501, 2699, -1, 16, "Datos empresa";
     nVentPNT = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentPNT;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PARA nCampo = 0;  |SI nCampo [ 10;  |HACIENDO nCampo = nCampo + 1;
           #pntw0001#nCampo# = #pntm0001#nCampo#;
     |SIGUE;

     |C_M #pntw0001#0, 1, "N";
     |C_M #pntw0001#6, 1, "N";

     |PINPA #0#pntw0001;  nPanta = FSalida;

     |SI eaTipoDSOPORTE = "G";
         |CONTROL_SIMPLE nPanta, "LABEL,{{0}}            ", 1201, 1230, NULL;
         |CONTROL_SIMPLE nPanta, "LABEL,{{0}}            ", 1301, 1330, NULL;

         |C_V #pntw0001#4, 1, "N";   #pntw0001#4 = 0;
         |C_V #pntw0001#5, 1, "N";   #pntw0001#5 = "";
         |C_V #pntw0001#6, 1, "N";   #pntw0001#6 = 0;
         |C_M #pntw0001#6, 1, "N";
         |C_V #pntw0001#7, 1, "N";   #pntw0001#7 = "";
     |FINSI;

     |PINDA #0#pntw0001;

     |ENDATOS #1#pntw0001;
     |SI FSalida = 0;
         |PARA nCampo = 0;  |SI nCampo [ 10;  |HACIENDO nCampo = nCampo + 1;
               #pntm0001#nCampo#  = #pntw0001#nCampo#;
         |SIGUE;

         |GRABA 020#pntm0001.a;
     |FINSI;
     |LIBERA #pntm0001;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentPNT;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PULSATECLA;
     |FINVENTANA nVentPNT;
     |PULSATECLA;
|FPRC;

|PROCESO BajaM0003;
     #pntm0003#0 = #pntm0001#0;
     #pntm0003#1 = "";
|FPRC;

|PROCESO AltaM0003;
     #pntm0003#0 = #pntm0001#0;
     #pntm0003#1 = "zzzzzzzz";
|FPRC;

|PROCESO EvntM0003;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#pntm0003.=;

         |ESTADO_CONTROL nBtn2, "ENABLE";
     |FINSI;
|FPRC;

|PROCESO BtnAltM003;
     |ABRE #dsorm002;
     |CONSULTA_CLAVE #1#dsorm002;
     |SI FSalida = 0;
         #pntm0003#0 = #pntm0001#0;
         #pntm0003#1 = #dsorm002#0;
         |LEE 101#pntm0003.=;
         |SI FSalida = 0;
             |MENAV "0000La aplicaci¢n ya est  en la bandeja";
         |SINO;
             |DDEFECTO #pntm0003;
             #pntm0003#0 = #pntm0001#0;
             #pntm0003#1 = #dsorm002#0;
             #pntm0003#2 = #dsorm002#2;
             |GRABA 020#pntm0003.n;
         |FINSI;
         |LIBERA #pntm0003;
     |FINSI;
     |CIERRA #dsorm002;

     |REFRESCACONTROL nGrid3;
|FPRC;

|PROCESO BtnBajM003;
     |CONFI "0000NSeguro que quiere quitar la aplicaci¢n";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |LEE 101#pntm0003.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |BORRA 020#pntm0003.a;
     |LIBERA #pntm0003;

     |REFRESCACONTROL nGrid3;
|FPRC;

|PROCESO Aplicaciones;
     aAlfa = "Aplicaciones no utilizadas por el usuario " + #pntm0001#2;  |QBF aAlfa;

     |VENTANA 0501, 3399, -1, 16, aAlfa;
     n0Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n0Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Alta", 3188, 3198, BtnAltM003;
     nBtn1  = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Baja", 3176, 3186, BtnBajM003;
     nBtn2  = FSalida;

     |ESTADO_CONTROL nBtn2, "DISABLE";

     nRango = 4 + 8 + 16 + 32 + 128;

     |LINEAL_SIMPLE #1#pntm0003, nRango, 0502, 3098, BajaM0003, AltaM0003, EvntM0003;
     nGrid3 = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::{{001}}Salir," + nGrid3;
          |LEETECLA aTecla;
          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
          |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nBtn1;
     |FIN_CONTROL_WINDOWS nBtn2;

     |PULSATECLA;

     |DESTRUYECONTROL nGrid3;
     |PULSATECLA;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n0Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n0Vent;
     |PULSATECLA;
|FPRC;

|PROCESO Deshabilitar;
     |LEE 101#pntm0001.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |CONFI "0000NQuiere deshabilitar la empresa";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |HAZ Desactiva;
|FPRC;

|PROCESO Salir;
     |PTEC 806;
|FPRC;

|PROCESO EventoTree;
     |SI FEntrada = 0;
         aTexto = Contenido;  |QBF aTexto;
         |ACABA;
     |FINSI;

     aContenido = Contenido;  |QBF aContenido;
     |SI aContenido = "";  |ACABA;   |FINSI;

     |HAZ QuitaBotonSalir;

     aBuscar = aContenido;

     |ESTADO_CONTROL enTree, "DISABLE";

     |SI aContenido = "DUsuario";      |HAZ DUsuario;      |FINSI;
     |SI aContenido = "Empresas";      |HAZ Empresas;      |FINSI;
     |SI aContenido = "Aplicaciones";  |HAZ Aplicaciones;  |FINSI;
     |SI aContenido = "Deshabilitar";  |HAZ Deshabilitar;  |FINSI;

     |ESTADO_CONTROL enTree, "ENABLE";

     |HAZ PonBotonSalir;
     |PULSATECLA;

     |FOCOTECLADO enTree;

     |SI aContenido = "Deshabilitar";  |PTEC 806;  |FINSI;
|FPRC;

|PROCESO CargaArbol;
     eaAlfa = eaAlfa + &13 + &10;
     |XGRABA nHandle, eaAlfa;
|FPRC;

|PROCESO MontaArbol;
     |PTEC 802;  |PAUSA;  |PULSATECLA;

     |DBASE aBase;  |QBF aBase;
     eaRutaOrigen  = aBase + "/tmp";   |MKDIR eaRutaOrigen;
     eaRutaOrigen  = aBase + "/tmp/";
     eaRutaDestino = eaUnidadBasico + "\documentos";      |RMKDIR eaRutaDestino;
     eaRutaDestino = eaUnidadBasico + "\documentos\tmp";   |RMKDIR eaRutaDestino;
     eaRutaDestino = eaUnidadBasico + "\documentos\tmp\";
     eaFicheroTxt  = "pntz000101" + Usuario + ".txt";
     eaFicheroTgz  = "pntz000101" + Usuario + ".tgz";
     eaFicheroTar  = "pntz000101" + Usuario + ".tar";

     aAbre = eaRutaOrigen + eaFicheroTxt;

     sT_nID   = 0;
     sT_nOper = 0;

     |XABRE "WB", aAbre, nHandle;

     nLinea  = 1;

     eaAlfa = nLinea + ":Gesti¢n de Datos";                              |HAZ CargaArbol;
     eaAlfa = nLinea + ",1:Datos usuario {{DUsuario}}";                  |HAZ CargaArbol;
     eaAlfa = nLinea + ",2:Gestor de empresas {{Empresas}}";             |HAZ CargaArbol;
     eaAlfa = nLinea + ",3:Aplicaciones no utilizadas {{Aplicaciones}}"; |HAZ CargaArbol;

     nLinea = nLinea + 1;
     eaAlfa = nLinea + ":Deshabilitar usuario {{Deshabilitar}}";
     |HAZ CargaArbol;

     |XCIERRA nHandle;

     |HAZ TraeTxtComprimido;
     aAlfa = eaRutaDestino + eaFicheroTxt;

     eaAlfa = "TREECTRL,Opciones del asistente {{" + aAlfa + "}}";
     |CONTROL_SIMPLE 0, eaAlfa, 0603, 2858, EventoTree;
     enTree = FSalida;

     eaFicheroTxt  = "pntz000101" + Usuario + ".txt";
      aAlfa         = eaRutaDestino + eaFicheroTxt;
     |RFBORRA aAlfa;

     |SI aBuscar ! "";
         sT_nID   = enTree;
         sT_nOper = 3;
         sT_aData = "[" + aBuscar;
         |ESPECIFICA "COMMANDCONTROL", sTree;
         |SI FSalida ] 0;
             aRefresca = ":" + FSalida;
         |SINO;
             aRefresca = "1:";
         |FINSI;
     |FINSI;

     sT_nID   = enTree;
     sT_nOper = 2;
     |SI aRefresca = "";
         sT_aData = "1:";
     |SINO;
         sT_aData = aRefresca;
     |FINSI;
     |ESPECIFICA "COMMANDCONTROL", sTree;

     |FOCOTECLADO enTree;
|FPRC;

|PROCESO MostrarArbol;
     aAlfa = #pntm0001#2;   |QBF aAlfa;
     |VENTANA 0502, 3060, -1, 16, aAlfa;
     enVentana = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = enVentana;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |HAZ MontaArbol;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}Salir," + enTree;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = enVentana;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA enVentana;
     |PULSATECLA;

     |REFRESCACONTROL nGrid;  |PULSATECLA;
|FPRC;

|PROCESO Evento1;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#pntm0001.=;

         |ESTADO_CONTROL nBtnMost, "ENABLE";
     |FINSI;

     |SI FSalida = 4;
         |HAZ MostrarArbol;
     |FINSI;
|FPRC;

|PROCESO NuevoAcceso;
     |ABRE #dsprem05;
     #dsprem05#0 = 99;
     |LEE 000#dsprem05.=;
     |SI FSalida ! 0;
         |MENAV "0000No existe el Area 99, por favor pongase en contacto con su distribuidor";
         |CIERRA #dsprem05;
         |ACABA;
     |FINSI;
     |CIERRA #dsprem05;

     |VENTANA 0501, 2699, -1, 16, "Habilitar nuevo acceso";
     nVentPNT = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentPNT;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PATH_DAT #dsorm002#eaPathDSoporte#;
     |PATH_DAT #dsprem05#eaPathDSoporte#;

     |DDEFECTO #pntw0001;

     |LEE 000#pntm0001.u;
     |SI FSalida ! 0;  |DDEFECTO #pntm0001;  |FINSI;

     #pntw0001#0 = #pntm0001#0 + 1;

     |DDEFECTO #pntm0001;

     |C_M #pntw0001#0, 1, "S";
     |C_M #pntw0001#6, 1, "S";

     #pntw0001#3  = "";
     #pntw0001#4  = 99;
     #pntw0001#5  = #dsprem05#1;
     #pntw0001#6  = 01;
     #pntw0001#7  = "PINET";

     |PINPA #0#pntw0001;  nPanta = FSalida;

     |SI eaTipoDSOPORTE = "G";
         |CONTROL_SIMPLE nPanta, "LABEL,{{0}}            ", 1201, 1230, NULL;
         |CONTROL_SIMPLE nPanta, "LABEL,{{0}}            ", 1301, 1330, NULL;

         |C_V #pntw0001#4, 1, "N";   #pntw0001#4 = 0;
         |C_V #pntw0001#5, 1, "N";   #pntw0001#5 = "";
         |C_V #pntw0001#6, 1, "N";   #pntw0001#6 = 0;
         |C_M #pntw0001#6, 1, "N";
         |C_V #pntw0001#7, 1, "N";   #pntw0001#7 = "";
     |FINSI;

     |PINDA #0#pntw0001;

     |ENDATOS #1#pntw0001;
     |SI FSalida = 0;
         |PARA nCampo = 0;  |SI nCampo [ 10;  |HACIENDO nCampo = nCampo + 1;
               #pntm0001#nCampo#  = #pntw0001#nCampo#;
         |SIGUE;

         #pntm0001#1  = "PNT" + (("00000" + #pntm0001#0) % -105);

         |GRABA 020#pntm0001.n;   |LIBERA #pntm0001;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentPNT;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PULSATECLA;
     |FINVENTANA nVentPNT;
     |PULSATECLA;

     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO CreaUsuarios;
     |VENTANA 1610, 2080, -1, 16, "Realizando trabajo";
     nVentPNT = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentPNT;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802; |PAUSA;

     |CONFI "0000SHan habido cambios que ser  necesario guardar perfiles de usuarios. Desea hacerlo ahora";

     |SI FSalida = 0;
         |PTEC 802; |PAUSA;

         |CONTROL_SIMPLE 0, "LABEL,{{14}}Realizando trabajo", 1720, 1879, NULL;

         |PTEC 802; |PAUSA;

         |SI eaTipoDSOPORTE = "S";
             |SUB_EJECUTA_NP ":dsoporte/dsprez02;AUTO";
         |FINSI;

         |SI eaTipoDSOPORTE = "G";
             |SUB_EJECUTA_NP ":dsoporte/dsorz020;9000";
         |FINSI;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentPNT;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVentPNT;
|FPRC;

|PROCESO Requisitos;
     nError         = 0;
     eaTipoDSOPORTE = "";

     |DBASS aBase;  |QBF aBase;
     eaPathDSoporte = aBase + "dsoporte/fich/";
     eaPathGecochek = aBase + "gecochek/fich/";

     |PATH_DAT #dsorz999#eaPathDSoporte#;

     |ABRE #dsorz999;
     |LEE 000#dsorz999.p;
     |SI FSalida = 0;
         |SI #dsorz999#1 = "A";  eaTipoDSOPORTE = "S";  |FINSI;
         |SI #dsorz999#1 = "G";  eaTipoDSOPORTE = "G";  |FINSI;
     |FINSI;
     |CIERRA #dsorz999;

     |SI eaTipoDSOPORTE = "S";
         |DBASS aBase;
         aMas = aBase + "gecochek/def/gecom001.mas";
         |XABRE "E", aMas, nHandle;
         |SI FSalida < 0;  eaTipoDSOPORTE = "";  |FINSI;
     |FINSI;

     |SI eaTipoDSOPORTE = "";
         |MENAV "0000ADMONNET sin configurar.";
         nError = 1;
         |ACABA;
     |FINSI;

     |PATH_DAT #dsorm002#eaPathDSoporte#;
     |PATH_DAT #dsprem05#eaPathDSoporte#;

     |DBASS aBase;
     aAlfa = aBase - "ase";
     |SI FSalida = 0;
         |MENAV "0000Para usar el PINET debe estar en hosting.";
         nError = 1;
         |ACABA;
     |FINSI;

     |ABRE #dsam0001;
     |LEE 000#dsam0001.p;
     |CIERRA #dsam0001;

     aAlfa = #dsam0001#1;  |QBF aAlfa;
     |SI aAlfa = "";
         |MENAV "0000Introduzca un nombre de empresa en el mantenimiento Datos generales del despacho";
         nError = 1;
         |ACABA;
     |FINSI;

     aAlfa = #dsam0001#8;  |QBF aAlfa;
     |SI aAlfa = "";
         |MENAV "0000Introduzca un e_mail en el mantenimiento Datos generales del despacho";
         nError = 1;
         |ACABA;
     |FINSI;

     aAbre = "/usr/bin/smtp-cli";
     |XABRE "E", aAbre, nHandle;
     |SI FSalida < 0;
         |MENAV "0000Debe estar instalado el smtp-cli en el servidor. Pongase en contacto con su distribuidor";
         nError = 1;
         |ACABA;
     |FINSI;
|FPRC;

|PROCESO Acotar;
     |SUB_EJECUTA "pntm0004";
     |FOCOTECLADO nGrid;
|FPRC;

|PROGRAMA;
     |HAZ Requisitos;
     |SI nError = 1;  |ACABA;  |FINSI;

     aAlfa = aBase % -204;
     nNume = aAlfa;
     |SI nNume = 0;
         |MENAV "0000Para usar el PINET debe estar en hosting.";
         |ACABA;
     |FINSI;

     |SI eaUnidadBasico = "";  |HAZ LeeUnidadBasico;  |FINSI;

     enCreaUsuarios = 0;

     |CLS;
     |CABEZA "Asistente accesos PINET";

     |PINPA #0#pntz0001;

     |CONTROL_SIMPLE 0, "BOTON,Mostrar Arbol", 3202, 3315, MostrarArbol;
     nBtnMost = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,Habilitar nuevo acceso", 3285, 3398, NuevoAcceso;
     nBtnHabi = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,Tablas acotadas en los perdiles", 3502, 3527, Acotar;

     |ESTADO_CONTROL nBtnMost, "DISABLE";

     aUsuario = Usuario % 810;

     nRango = 4 + 8 + 16 + 32 + 128;

     |LINEAL_SIMPLE #1#pntm0001, nRango, 0602, 3398, NULL, NULL, Evento1;
     nGrid = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}Salir," + nGrid;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |DESTRUYECONTROL nGrid;
     |PULSATECLA;

     |SI enCreaUsuarios = 1;
         |HAZ CreaUsuarios;
     |FINSI;
|FPRO;

|PROCESO Tipo80;  |TIPO 80;
     |ABRET #pntm0001;
     |ABRET #pntm0002;
     |ABRET #pntm0003;

     FSalida = 3599;
|FPRC;

|PROCESO Tipo90;  |TIPO 90;
     |CIERRAT #pntm0001;
     |CIERRAT #pntm0002;
     |CIERRAT #pntm0003;
|FPRC;
