|FICHEROS;
     dsaw0202 #202;
     dsam0103 #103;
|FIN;

|VARIABLES;
     nCampo1      = 0;

     aAlfa        = "";
     aAlfa1       = "";
     aAlfa2       = "";
     aAlfa3       = "";
     aComa        = "";
     aApellido1   = "";
     aApellido2   = "";
     aNombre      = "";
     aNombreC     = "";

     fHoy         = @;
     nDia1        = 0;
     nDia2        = 0;
     nMes1        = 0;
     nMes2        = 0;
     nAny1        = 0;
     nAny2        = 0;
     nEdad        = 0;

     &eaVarDni    = "";
     &eaCadena    = "";
     &eaCadena1   = "";
     &eaCadena2   = "";
     &eaCadena3   = "";
|FIN;

|PROCESO Tipo12Fw202;  |TIPO 12;
     |HAZ Tipo7C5Fw202;

     |SI #202#14 = "N";
         aAlfa = #202#16;
     |SINO;
         aAlfa = #202#31;
     |FINSI;

     |QBF aAlfa;

     |SI aAlfa = "";
         |MENAV "0000 Introduzca datos del domicilio habitual";
         |ERROR;
         |ACABA;
     |FINSI;
|FPRC;

|PROCESO NombreEntero;  |TIPO 6;
     |SI #202#1 ! Contenido;
         #202#2 = "";
         #202#3 = "";
         #202#4 = "";
     |FINSI;

     |ABRE #103;
     #103#0 = #202#0;
     |LEE 000#103=;
     |SI FSalida = 0;
         #202#1 = #103#1;
     |FINSI;
     |CIERRA #103;

     aAlfa = #202#1;  |QBF aAlfa;
     |SI aAlfa = "";  |ACABA;  |FINSI;

     aApellido1 = #202#2;  |QBT aApellido1;
     aApellido2 = #202#3;  |QBT aApellido2;
     aNombre    = #202#4;  |QBT aNombre;
     aNombreC   = #202#1;  |QBT aNombreC;

     |SI aApellido1 = "";  |Y  aApellido2 = "";  |Y  aNombre    = ""; |Y  aNombreC   ! "";
         eaCadena = #202#1;
         |HAZ SeparaNombreDer;
         #202#2 = eaCadena1;  |PINTA #202#2;
         #202#3 = eaCadena2;  |PINTA #202#3;
         #202#4 = eaCadena3;  |PINTA #202#4;
         |ACABA;
     |FINSI;

     aAlfa     = "";
     nCampo1   = Campo + 1;  aAlfa1 = #202nCampo1;  |QBF aAlfa1;
     nCampo1   = Campo + 2;  aAlfa2 = #202nCampo1;  |QBF aAlfa2;
     nCampo1   = Campo + 3;  aAlfa3 = #202nCampo1;  |QBF aAlfa3;

     aAlfa  = aAlfa1;
     |SI aAlfa2 ! "";
         |SI aAlfa1 ! "";
             aAlfa = aAlfa + " " + aAlfa2;
         |SINO;
             aAlfa = aAlfa2;
         |FINSI;
     |FINSI;

     aComa = "";
     |SI aAlfa1 ! "";  |O aAlfa2 ! "";
         |SI aAlfa3 ! "";
             aComa = ", ";
         |FINSI;
     |FINSI;

     aAlfa  = aAlfa + aComa + aAlfa3;
     #202Campo = aAlfa;
     |PINTA #202Campo;
|FPRC;

|PROCESO Tipo6C0Fw202;  |TIPO 6;
     |SI FSalida ! 10;
         |ACABA;
     |FINSI;

     eaVarDni = #202Campo;
     |HAZ CalculoDNI;
     #202Campo = eaVarDni;
     |PINTA #202Campo;
     |ERROR;
|FPRC;

|PROCESO Tipo7C5Fw202;  |TIPO 7;
     aAlfa     = "";
     aAlfa1 = #202#2;  |QBF aAlfa1;
     aAlfa2 = #202#3;  |QBF aAlfa2;
     aAlfa3 = #202#4;  |QBF aAlfa3;

     aAlfa  = aAlfa1;
     |SI aAlfa2 ! "";
         |SI aAlfa1 ! "";
             aAlfa = aAlfa + " " + aAlfa2;
         |SINO;
             aAlfa = aAlfa2;
         |FINSI;
     |FINSI;

     aComa = "";
     |SI aAlfa1 ! "";  |O aAlfa2 ! "";
         |SI aAlfa3 ! "";
             aComa = ", ";
         |FINSI;
     |FINSI;

     aAlfa  = aAlfa + aComa + aAlfa3;
     #202#1 = aAlfa;
     |PINTA #202#1;
|FPRC;

|PROCESO Tipo0C6Fw202;
     |C_M #202#10, 1, "S";
     |SI #202#6 ! "L"; |Y #202#6 ! "S";
         #202#10 = " ";  |PINTA #202#10;
         |C_M #202#10, 1, "N";
     |FINSI;
|FPRC;

|PROCESO PonSiglo20_202;
     aAlfa1    = (#202Campo % 106) + "19" + (#202Campo % -102);
     #202Campo = aAlfa1;
     |PINTA #202Campo;
|FPRC;

|PROCESO CumpleAnyos_202;
     aAlfa1 = fHoy;       aAlfa1 = aAlfa1 % -104; nAny1 = aAlfa1;
     aAlfa1 = #202Campo;  aAlfa1 = aAlfa1 % -104; nAny2 = aAlfa1;
     nEdad  = nAny1 - nAny2;

     |SI nEdad ! 18; |ACABA; |FINSI;

     aAlfa1 = fHoy;       aAlfa1 = aAlfa1 % 402; nMes1 = aAlfa1;
     aAlfa1 = #202Campo;  aAlfa1 = aAlfa1 % 402; nMes2 = aAlfa1;

     |SI nMes2 > nMes1;  nEdad = nEdad - 1; |FINSI;
     |SI nMes1 ! nMes2;  |ACABA;            |FINSI;

     aAlfa1 = fHoy;       aAlfa1 = aAlfa1 % 102; nDia1 = aAlfa1;
     aAlfa1 = #202Campo;  aAlfa1 = aAlfa1 % 102; nDia2 = aAlfa1;

     |SI nDia2 > nDia1;  nEdad = nEdad - 1; |FINSI;
|FPRC;

|PROCESO Tipo0C7Fw202;
     |SI #202Campo = "01.01.0000";  |ACABA;  |FINSI;

     fHoy = ~;

     |SI #202Campo > fHoy;        || nacimientos futuros como que no
          |HAZ PonSiglo20_202;    ||/se cambia sin preguntar
     |FINSI;

     |SI #202Campo ] "01.01.2000"; |Y #202Campo [ fHoy;     || presuntos ni¤os,
                                                            ||/se pregunta
         |HAZ CumpleAnyos_202;
         |SI nEdad < 18;
             aAlfa = "0000SLa fecha puede ser incorrecta. Ha introducido un contribuyente menor de edad. Corregir fecha?";
             |CONFI aAlfa;
             |SI FSalida = 0;
                 |ERROR;
                 || |HAZ PonSiglo20;
             |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo0C8Fw202;
     nCampo1 = Campo + 1;
     |C_M #202nCampo1, 1, "S";
     |SI #202Campo = 0;
         |C_M #202nCampo1, 1, "N";
         #202nCampo1 = "N";  |PINTA #202nCampo1;
     |FINSI;
|FPRC;
