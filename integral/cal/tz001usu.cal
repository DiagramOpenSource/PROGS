|FICHEROS;
     pntm0001;
     pntm0002;

     dsam0000;
     dsam0001;

     pntw0001,MANTE;
     pntw0002,MANTE;

     :/gecochek/def/gecom001;

     :/dsoporte/def/dsprem01;
     :/dsoporte/def/dsprem07;
     :/dsoporte/def/dsprem99;
     :/dsoporte/def/dsorm001;
     :/dsoporte/def/dsorm015;

     :/dsarchi/def/dpntm001;
     :/dsarchi/def/dpntm005;

     refermas@;
|FIN;

|VARIABLES;
     nBtn1            = 0;
     nBtn2            = 0;
     nBtn3            = 0;
     nBtn4            = 0;
     nGrid            = 0;
     nVentTrab        = 0;
     nPanta           = 0;
     nRango           = 0;
     nReg             = 0;
     nGrupo           = 0;
     nLong            = 0;
     nCaracter        = 0;
     nPos             = 0;
     nCamuf           = 0;
     nNum             = 0;
     nCod             = 0;
     nIdSalir         = 0;
     nIdSalir1        = 0;
     nPosi            = 0;
     nHandle          = 0;
     nHandle1         = 0;
     nHandle2         = 0;
     nRegilla         = 0;
     nCodInterno      = 0;
     nModo            = 0;
     nCampo           = 0;

     aBase            = "";
     aTecla           = "";
     aRetu            = "";
     aEsc1            = "";
     aEsc2            = "";
     aAlfa            = "";
     aAlfa1           = "";
     aAlfa2           = "";
     aAlfa3           = "";
     aID              = "";
     aHora            = "";
     aDOperacion      = "";
     aOperacion       = "";
     aNGrupo          = "";
     aLong            = "";
     aServidor        = "";
     aPathGecochek    = "";
     aTo              = "";
     aFrase           = "";
     aDirec           = "";
     aCarac           = "";
     aAbre            = "";
     aAbreTXT         = "";
     aAbreTXTO        = "";
     aAbreTXTD        = "";
     aAbreHTMD        = "";
     aAbreHTMO        = "";
     aAbreFIN         = "";
     aBaseMensaje     = "";
     aNomDes          = "";
     aCarac10         = "";
     aFrom            = "";
     aAsunto          = "";
     aConten          = "";
     aEjecuta         = "";
     aUsuario         = "";
     aPassword        = "";
     aNombreUsuario   = "";
     aMailUsuario     = "";
     aFecha           = "";
     aBass            = "";
     aMas             = "";
     aPath            = "";
     aFic             = "";

     nID              = 0;

     fFecha           = @;

     {-1}aVent        = "";
         aVent1       = 0;
         aVent2       = 0;
         aVent3       = 0;
         aVent4       = 0;
         aVent5       = "";

     {2}nNivel        = 0;

     &enCreaUsuarios  = 0;
     &eaUsuPerfil     = "";
     &eaFrom          = "";
     &enEsOTP         = 0;
     &enAprobacion    = 0;
     &eaPrograma      = "";
     &eaSPA           = "";
     &eaProd          = "";
     &enErrorCorreo   = 0;
     &eaPassEnc       = "";
     &eaPass          = "";
     &eaTipoDSOPORTE  = "";
     &eaPathDSoporte  = "";
     &eaPathGecochek  = "";
     &eaBaseNet       = "";
     &eaModoNet       = "";
     &eaUsuNet        = "";
     &eaFechaNet      = "";
     &eaUsuAfectado   = "";
     &enCodCli        = 0;
     &eaEMail         = "";
|FIN;

|| --------------------------------------------------------------------------

|PROCESO PonDirecciones;
     aLong  = aTo % 0;
     nLong  = aLong;
     aFrase = "";
     aDirec = "";
     |PARA nPosi = 1;  |SI nPosi [ nLong;  |HACIENDO nPosi = nPosi + 1;
           nPos   = (nPosi * 100) + 1;
           aCarac = aTo % nPos;

           |SI aCarac = ";";  |O aCarac = ",";
               aFrase = aFrase + "--to=" + &34 + aDirec + &34 + " ";
               aDirec = "";
           |SINO;
               aDirec = aDirec + aCarac;
           |FINSI;
     |SIGUE;

     |SI aDirec ! "";
         aFrase = aFrase + "--to=" + &34 + aDirec + &34;
     |FINSI;

     aConten = aFrase + " \" + &10;
|FPRC;

|PROCESO EjecutaShellCorreo;
     aAbreTXT  = aAbre;
     aAbreHTMO = aBaseMensaje + "dsoporte.htm";
     aAbreHTMD = aBaseMensaje + aUsuario + ".htm";  |QBT aAbreHTMD;
     |FBORRA aAbreHTMD;

     |XABRE "WB", aAbreHTMD, nHandle1;
     |XABRE "AB", aAbreHTMO, nHandle2;

     nRegilla = 0;
     |XLEE nHandle2, 1, aAlfa;
     |PARA; |SI FSalida > 0; |HACIENDO;
            |SI nRegilla = 0;  |Y aAlfa ! "{";
                |XGRABA nHandle1, aAlfa;
            |FINSI;

            |SI aAlfa = "}";
                aConten  = aConten + aAlfa;

                |SI aConten = "{NOMBREDESPA}";
                    aAlfa = aNomDes;
                |FINSI;

                |SI aConten = "{USUARIO}";
                    aAlfa = aUsuario;
                |FINSI;

                |SI aConten = "{PASSWORD}";
                    aAlfa = aPassword;
                |FINSI;

                |SI aConten = "{ACCESO}";
                    aAlfa = "http://www.geconet.net/portal/";
                    aAlfa = aAlfa + "crearAcceso.php?claveGeconet=" + eaPassEnc;

                    eaPass = aMailUsuario;  |HAZ EncriptaMAC;
                    aAlfa  = aAlfa + "&email=" + eaPassEnc;  |QBF aAlfa;
                    eaPass    = "";
                    eaPassEnc = "";
                |FINSI;

                |XGRABA nHandle1, aAlfa;

                nRegilla = 0;
                aConten  = "";
            |SINO;
                |SI aAlfa = "{";  |O nRegilla = 1;
                    aConten  = aConten + aAlfa;
                    nRegilla = 1;
                |FINSI;
            |FINSI;

            |XLEE nHandle2, 1, aAlfa;
     |SIGUE;
     |XCIERRA nHandle1;
     |XCIERRA nHandle2;

     aAbreTXTO = aBaseMensaje + "dscorreo.sh";
     aAbreTXTD = aBaseMensaje + aUsuario + ".sh";  |QBT aAbreTXTD;
     aAbreFIN  = aBaseMensaje + aUsuario + ".txt"; |QBT aAbreFIN;
     |FBORRA aAbreTXTD;
     |FBORRA aAbreFIN;

     |XABRE "WB", aAbreTXTD, nHandle1;
     |XABRE "AB", aAbreTXTO, nHandle2;

     aCarac10 = &10;
     |XLEE nHandle2, 1, aAlfa;
     |PARA; |SI FSalida > 0; |HACIENDO;
            aConten = aConten + aAlfa;

            |SI aAlfa = aCarac10;
                aAlfa1 = aConten - "DE=$1";
                |SI FSalida ! 0;
                    aConten = "DE=" + &34 + aFrom + &34 + &10;
                |FINSI;

                aAlfa1 = aConten - "$PARA";
                |SI FSalida ! 0;
                    |HAZ PonDirecciones;
                |FINSI;

                aAlfa1 = aConten - "ASUNTO=$3";
                |SI FSalida ! 0;
                    aConten = "ASUNTO=" + &34 + aAsunto + &34 + &10;
                |FINSI;

                aAlfa1 = aConten - "FICHTM=$4";
                |SI FSalida ! 0;
                    aConten = "FICHTM=" + &34 + aAbreHTMD + &34 + &10;
                |FINSI;

                aAlfa1 = aConten - "FICFIN=$6";
                |SI FSalida ! 0;
                    aConten = "FICFIN=" + &34 + aAbreFIN + &34 + &10;  |QBT aConten;
                |FINSI;

                aAlfa1 = aConten - "$ADJUNTO";
                |SI FSalida ! 0;
                    aConten = "";
                |FINSI;

                |XGRABA nHandle1, aConten;

                aConten = "";
            |FINSI;

            |XLEE nHandle2, 1, aAlfa;
     |SIGUE;
     |XCIERRA nHandle1;
     |XCIERRA nHandle2;

     aEjecuta = "/bin/sh " + aAbreTXTD;

     |SYSTEM aEjecuta;

     |PARA;  |SI;  |HACIENDO;
          |XABRE "E", aAbreFIN, nHandle;
          |SI FSalida ] 0;  |SAL;  |FINSI;

          |SLEEP 1;
          |PULSATECLA;
     |SIGUE;

     |FBORRA aAbreTXTD;
     |FBORRA aAbreHTMD;
     |FBORRA aAbreFIN;
|FPRC;

|PROCESO CorreoNotifica;
     |ABRE #dsam0001;
     |LEE 000#dsam0001.p;
     |CIERRA #dsam0001;

     |VENTANA 1610, 2080, -1, 16, "Enviando correo de notificaci¢n";
     nVentTrab = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentTrab;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802; |PAUSA;

     |DBASE aBaseMensaje;  |QBF aBase;
     aBaseMensaje = aBaseMensaje + "dscorreo/";

     aNomDes  = #dsam0001#1;  |QBF aNomDes;
     aFrom    = #dsam0001#8;  |QBF aFrom;
     aAsunto  = "Acceso para el uso de las aplicaciones PINET";

     |HAZ EjecutaShellCorreo;

     |FBORRA aAbreTXT;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentTrab;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVentTrab;
|FPRC;

|PROCESO EjecutaShellCorreoDS;
     aAbreTXT  = aBaseMensaje + "ToNotifi.txt";
     |XABRE "E", aAbreTXT, nHandle;
     |SI FSalida < 0;
         |XABRE "WB", aAbreTXT, nHandle;
         aAlfa = "joaquin.molina@diagram.es" + &13 + &10;
         |XGRABA nHandle, aAlfa;
         |XCIERRA nHandle;
     |FINSI;

     |XABRE "", aAbreTXT, nHandle;
     |XLEE nHandle, 250, aTo;
     |XCIERRA nHandle;

     |QBF aTo;
     |SI aTo = "";  |ACABA;  |FINSI;

     aAbreTXTO = aBaseMensaje + "dsnotifi.sh";
     aAbreTXTD = aBaseMensaje + "notifica" + eaUsuNet + ".sh";  |QBT aAbreTXTD;
     aAbreFIN  = aBaseMensaje + "notifica" + eaUsuNet + ".txt"; |QBT aAbreFIN;
     |FBORRA aAbreTXTD;
     |FBORRA aAbreFIN;

     |XABRE "WB", aAbreTXTD, nHandle1;
     |XABRE "AB", aAbreTXTO, nHandle2;

     aCarac10 = &10;
     |XLEE nHandle2, 1, aAlfa;
     |PARA; |SI FSalida > 0; |HACIENDO;
            aConten = aConten + aAlfa;

            |SI aAlfa = aCarac10;
                aAlfa1 = aConten - "DE=$1";
                |SI FSalida ! 0;
                    aConten = "DE=" + &34 + aFrom + &34 + &10;
                |FINSI;

                aAlfa1 = aConten - "$PARA";
                |SI FSalida ! 0;
                    |HAZ PonDirecciones;
                |FINSI;

                aAlfa1 = aConten - "ASUNTO=$3";
                |SI FSalida ! 0;
                    aConten = "ASUNTO=" + &34 + aAsunto + &34 + &10;
                |FINSI;

                aAlfa1 = aConten - "FICFIN=$6";
                |SI FSalida ! 0;
                    aConten = "FICFIN=" + &34 + aAbreFIN + &34 + &10;  |QBT aConten;
                |FINSI;

                |XGRABA nHandle1, aConten;

                aConten = "";
            |FINSI;

            |XLEE nHandle2, 1, aAlfa;
     |SIGUE;
     |XCIERRA nHandle1;
     |XCIERRA nHandle2;

     aEjecuta = "/bin/sh " + aAbreTXTD;

     |SYSTEM aEjecuta;

     |PARA;  |SI;  |HACIENDO;
          |XABRE "E", aAbreFIN, nHandle;
          |SI FSalida ] 0;  |SAL;  |FINSI;

          |SLEEP 1;
          |PULSATECLA;
     |SIGUE;

     |FBORRA aAbreTXTD;
     |FBORRA aAbreHTMD;
     |FBORRA aAbreFIN;
|FPRC;

|PROCESO CorreoNotificaDS;
     |ABRE #dsam0001;
     |LEE 000#dsam0001.p;
     |CIERRA #dsam0001;

     |VENTANA 1610, 2080, -1, 16, "Enviando correo de notificaci¢n";
     nVentTrab = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentTrab;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802; |PAUSA;

     |DBASE aBaseMensaje;  |QBF aBase;
     aBaseMensaje = aBaseMensaje + "dscorreo/";

     |DBASS eaBaseNet;

     aFrom        = "no_reply@diagram.es";  |QBF aFrom;
     aAsunto      = "PINET: " + eaModoNet + " " + eaUsuAfectado + " " + eaBaseNet + " " + eaFechaNet + " " + eaUsuNet;

     |HAZ EjecutaShellCorreoDS;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentTrab;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVentTrab;
|FPRC;

|| --------------------------------------------------------------------------

|PROCESO BorraGecochek;
     |PATH_DAT #dsorm001#eaPathDSoporte#;
     |PATH_DAT #dsprem01#eaPathDSoporte#;
     |PATH_DAT #dsprem99#eaPathDSoporte#;
     |PATH_DAT #gecom001#eaPathGecochek#;

     |ABRE #gecom001;
     #gecom001#0 = nCodInterno;
     |LEE 101#gecom001.=;
     |SI FSalida = 0;
         |BORRA 020#gecom001.a;
         |LIBERA #gecom001;
     |FINSI;
     |CIERRA #gecom001;

     aOperacion  = "B";
     aDOperacion = "BAJA DEL USUARIO";
     |HAZ GrabaHistorico;

     enCreaUsuarios = 1;

     eaModoNet = "Deshabilitar";
     eaUsuNet  = Usuario % 810; |QBF eaUsuNet;
     fFecha    = ~;
     aFecha    = fFecha;
     |HORA aHora;
     eaFechaNet = aFecha + " " + aHora;
     eaUsuAfectado = aUsuario; |QBF eaUsuAfectado;
     |HAZ CorreoNotificaDS;
|FPRC;

|PROCESO BorraDsoporte;
     |PATH_DAT #dsorm001#eaPathDSoporte#;
     |PATH_DAT #dsprem01#eaPathDSoporte#;
     |PATH_DAT #dsorm015#eaPathDSoporte#;
     |PATH_DAT #dsprem99#eaPathDSoporte#;

     nModo = 1;

     |ABRE #dsorm015;
     #dsorm015#0 = 9000;
     #dsorm015#1 = aUsuario;
     |LEE 101#dsorm015.=;
     |SI FSalida = 0;
         |BORRA 020#dsorm015.a;
         |LIBERA #dsorm015;
     |FINSI;
     |CIERRA #dsorm015;

     aOperacion  = "B";
     aDOperacion = "BAJA DEL USUARIO";
     |HAZ GrabaHistorico;

     enCreaUsuarios = 1;

     eaModoNet = "Deshabilitar";
     eaUsuNet  = Usuario % 810; |QBF eaUsuNet;
     fFecha    = ~;
     aFecha    = fFecha;
     |HORA aHora;
     eaFechaNet = aFecha + " " + aHora;
     eaUsuAfectado = aUsuario; |QBF eaUsuAfectado;
     |HAZ CorreoNotificaDS;
|FPRC;

|PROCESO GrabaGecochek;
     |PATH_DAT #dsorm001#eaPathDSoporte#;
     |PATH_DAT #dsprem01#eaPathDSoporte#;
     |PATH_DAT #dsprem99#eaPathDSoporte#;
     |PATH_DAT #gecom001#eaPathGecochek#;

     nCod = nCodInterno;

     |ABRE #gecom001;
     |SI nCodInterno = 0;
         |PARA nCod = 1;  |SI nCod [ 99999;  |HACIENDO nCod = nCod + 1;
               #gecom001#0 = nCod;
               |LEE 000#gecom001.=;
               |SI FSalida ! 0;  |SAL;  |FINSI;
         |SIGUE;

         nCodInterno = nCod;
     |FINSI;

     nModo       = 2;
     #gecom001#0 = nCod;
     |LEE 101#gecom001.=;
     |SI FSalida ! 0;
         |DDEFECTO #gecom001;
         #gecom001#0 = nCod;
         |GRABA 020#gecom001.b;

         nModo       = 1;
     |FINSI;

     #gecom001#1  = aNombreUsuario;
     #gecom001#2  = aMailUsuario;
     #gecom001#4  = aUsuario;
     #gecom001#5  = aPassword;
     #gecom001#7  = #pntw0001#4;
     #gecom001#8  = #pntw0001#5;
     #gecom001#9  = #pntw0001#6;
     #gecom001#10 = #pntw0001#7;
     #gecom001#16 = #dsam0000#54;

     |HAZ CargaMac;
     #gecom001#6 = eaPassEnc;

     |SI nModo = 1;
          aOperacion  = "A";
          aDOperacion = "ALTA DEL USUARIO";
     |SINO;
          aOperacion  = "M";
          aDOperacion = "MODIFICACION DEL USUARIO";
     |FINSI;
     |HAZ GrabaHistorico;

     |GRABA 020#gecom001.a;
     |LIBERA #gecom001;
     |CIERRA #gecom001;

     enCreaUsuarios = 1;

     |SI nModo ! 1;  |ACABA;  |FINSI;

     eaModoNet = "Habilitar";
     eaUsuNet  = Usuario % 810; |QBF eaUsuNet;
     fFecha    = ~;
     aFecha    = fFecha;
     |HORA aHora;
     eaFechaNet = aFecha + " " + aHora;
     eaUsuAfectado = aUsuario; |QBF eaUsuAfectado;
     |HAZ CorreoNotificaDS;
|FPRC;

|PROCESO GrabaDsoporte;
     |PATH_DAT #dsorm001#eaPathDSoporte#;
     |PATH_DAT #dsprem01#eaPathDSoporte#;
     |PATH_DAT #dsorm015#eaPathDSoporte#;
     |PATH_DAT #dsprem99#eaPathDSoporte#;

     nModo = 1;

     |ABRE #dsorm015;
     #dsorm015#0 = 9000;
     #dsorm015#1 = aUsuario;
     |LEE 101#dsorm015.=;
     |SI FSalida ! 0;
         |DDEFECTO #dsorm015;
         #dsorm015#0 = 9000;
         #dsorm015#1 = aUsuario;
         |GRABA 020#dsorm015.b;

         nModo = 2;
     |FINSI;

     #dsorm015#2  = aNombreUsuario;
     #dsorm015#3  = aPassword;
     #dsorm015#4  = 0;

     |HAZ CargaMac;

     #dsorm015#5 = eaPassEnc;
     #dsorm015#6  = aMailUsuario;

     |SI nModo = 1;
         aOperacion  = "A";
         aDOperacion = "ALTA DEL USUARIO";
     |SINO;
         aOperacion  = "M";
         aDOperacion = "MODIFICACION DEL USUARIO";
     |FINSI;

     |HAZ GrabaHistorico;

     |GRABA 020#dsorm015.a;
     |LIBERA #dsorm015;
     |CIERRA #dsorm015;

     enCreaUsuarios = 1;

     |SI nModo ! 1;  |ACABA;  |FINSI;

     eaModoNet = "Habilitar";
     eaUsuNet  = Usuario % 810; |QBF eaUsuNet;
     fFecha    = ~;
     aFecha    = fFecha;
     |HORA aHora;
     eaFechaNet = aFecha + " " + aHora;
     eaUsuAfectado = aUsuario; |QBF eaUsuAfectado;
     |HAZ CorreoNotificaDS;
|FPRC;

|| --------------------------------------------------------------------------

|PROCESO QuitaBotonSalir;
     nIdSalir = 0;
     InNivel   = nNivel1<-;
     nNivel1   = 0;
     |ESPECIFICA "IDBOTONTECLA", nNivel;

     nIdSalir = nNivel2;
     |ESTADO_CONTROL nIdSalir, "HIDE";
|FPRC;

|PROCESO PonBotonSalir;
     |ESTADO_CONTROL nIdSalir, "SHOW";
|FPRC;

|PROCESO QuitaBotonSalir1;
     nIdSalir1 = 0;
     InNivel   = nNivel1<-;
     nNivel1   = 0;
     |ESPECIFICA "IDBOTONTECLA", nNivel;

     nIdSalir1 = nNivel2;
     |ESTADO_CONTROL nIdSalir1, "HIDE";
|FPRC;

|PROCESO PonBotonSalir1;
     |ESTADO_CONTROL nIdSalir1, "SHOW";
|FPRC;

|PROCESO GrabaHistorico;
     |HORA aHora;

     |ABRE #dsprem99;
     nReg = 1;
     |LEE 000#dsprem99.u;
     |SI FSalida = 0;
         nReg = #dsprem99#0 + 1;
     |FINSI;

     |DBASS aBase;  |QBF aBase;
     aAlfa = aBase % -201;

     |ABRE #dsprem01;
     #dsprem01#0 = aAlfa;
     |LEE 000#dsprem01.=;
     |SI FSalida ! 0;  |DDEFECTO #dsprem01;  |FINSI;
     |CIERRA #dsprem01;

     #dsprem99#0  = nReg;
     |GRABA 020#dsprem99.b;

     #dsprem99#1  = #pntw0001#1;
     #dsprem99#2  = ~;
     #dsprem99#3  = aHora;
     #dsprem99#4  = aOperacion;
     #dsprem99#5  = aDOperacion;
     #dsprem99#6  = aAlfa + "0000";
     #dsprem99#7  = #pntw0001#4;
     #dsprem99#8  = #pntw0001#6;
     #dsprem99#9  = aUsuario;
     #dsprem99#10 = "PINET";
     #dsprem99#11 = "PINET";
     #dsprem99#12 = #pntw0001#5;
     #dsprem99#13 = #pntw0001#7;
     aAlfa        = #dsprem99#6 + " " + #pntw0001#5;  |QBF aAlfa;
     aAlfa        = aAlfa + #pntw0001#7;
     #dsprem99#14 = aAlfa;
     #dsprem99#15 = "N";
     #dsprem99#16 = "N";
     #dsprem99#17 = eaPassEnc;
     #dsprem99#18 = 9999;
     #dsprem99#19 = 0;
     #dsprem99#20 = Usuario % 810;

     |GRABA 020#dsprem99.a;
     |LIBERA #dsprem99;
     |CIERRA #dsprem99;

     |ABRE #dsorm001;
     |SI eaTipoDSOPORTE = "S";
         |SI #gecom001#12 = 0;
             #dsorm001#0 = 9999;
             |LEE 000#dsorm001.];
             |SI FSalida = 0;
                 |LEE 000#dsorm001.a;
             |SINO;
                 |LEE 000#dsorm001.u;
             |FINSI;

             #gecom001#12 = 9000;
             |SI FSalida = 0; |Y  #dsorm001#0 ] 9000;
                 #gecom001#12 = #dsorm001#0 + 1;
             |FINSI;
         |FINSI;

         nGrupo  = #gecom001#12;
         aNGrupo = #gecom001#2;
     |SINO;
         nGrupo = 9000;
         aNGrupo = "PINET";
     |FINSI;

     #dsorm001#0 = nGrupo;
     |LEE 101#dsorm001.=;
     |SI FSalida ! 0;
         |DDEFECTO #dsorm001;
         #dsorm001#0 = nGrupo;
         |GRABA 020#dsorm001.b;
     |FINSI;
     #dsorm001#1 = aNGrupo;
     |GRABA 020#dsorm001.a;
     |LIBERA #dsorm001;
     |CIERRA #dsorm001;
|FPRC;

|PROCESO EncriptaMAC;
     |QBF eaPass;
     aLong   = eaPass % 0;
     nLong   = aLong;
     eaPassEnc = "";
     |PARA nCaracter = 1; |SI nCaracter [ nLong; |HACIENDO nCaracter = nCaracter + 1;
           nPos   = (nCaracter * 100) + 1;
           aAlfa1 = eaPass % nPos;

           nCamuf = &aAlfa1;
           nCamuf = nCamuf * 2;

           nNum      = ((nCamuf - (nCamuf $ 26)) / 26 ) + 65;
           eaPassEnc = eaPassEnc + &nNum;

           nNum      = (nCamuf $ 26) + 65;
           eaPassEnc = eaPassEnc + &nNum;
     |SIGUE;
|FPRC;

|PROCESO CargaMac;
     |DBASS aBase;  |QBF aBase;

     aAlfa     = aBase % -201;
     aServidor = aBase % -303;  |QBF aServidor;
     nNum      = aServidor;

     |SI nNum      = 0;   aServidor = "001";  |FINSI;
     |SI aServidor = "";  aServidor = "001";  |FINSI;

     eaPass = aAlfa + "0000" + aServidor + aUsuario;
     |HAZ EncriptaMAC;
|FPRC;

|| --------------------------------------------------------------------------

|PROCESO Baja_dsprem07;
     #dsprem07#0 = #pntm0001#4;
     #dsprem07#1 = 1;
|FPRC;

|PROCESO Alta_dsprem07;
     #dsprem07#0 = #pntm0001#4;
     #dsprem07#1 = 99;
|FPRC;

|PROCESO Tipo66C6Fw1;  |TIPO 66;
     |PATH_DAT #dsprem07#eaPathDSoporte#;

     |ABRE #dsprem07;
     #dsprem07#0 = #pntw0001#4;
     #dsprem07#1 = #pntw0001#6;
     |LEE 000#dsprem07.=;

     |CONSULTA_F_CLAVE #1#dsprem07, Baja_dsprem07, Alta_dsprem07;
     |SI FSalida = 0;
         #pntw0001#6 = #dsprem07#1;  |PINTA #pntw0001#6;
         #pntw0001#7 = #dsprem07#3;  |PINTA #pntw0001#7;
     |FINSI;
     |CIERRA #dsprem07;
|FPRC;

|PROCESO Tipo0C6Fw1;  |TIPO 0;
     |C_M #pntw0001#6, 0, aAlfa;
     |SI aAlfa = "N";  |ACABA;  |FINSI;

     |PATH_DAT #dsprem07#eaPathDSoporte#;

     |ABRE #dsprem07;
     #dsprem07#0 = #pntw0001#4;
     #dsprem07#1 = #pntw0001#6;
     |LEE 000#dsprem07.=;
     |SI FSalida ! 0;
         |MENAV "0000No existe el Area-Nivel. Por favor introduzca uno correcto o consulte con su Administrador";
         |ERROR;
     |FINSI;

     #pntw0001#7 = #dsprem07#3;  |PINTA #pntw0001#7;
     |CIERRA #dsprem07;
|FPRC;

|PROCESO Tipo0C0Fw1;
     |C_M #pntw0001#0, 0, aAlfa;
     |SI aAlfa = "N";  |ACABA;  |FINSI;

     #pntm0001#0 = #pntw0001#0;
     |LEE 000#pntm0001.=;
     |SI FSalida = 0;
         |MENAV "0000El usuario ya existe.";
         |ERROR;
         |ACABA;
     |FINSI;

     #pntw0001#1 = "PNT" + (("00000" + #pntw0001#0) % -105);

     |PINTA #pntw0001#1;
     |PINTA #pntw0001#2;
     |PINTA #pntw0001#9;
     |PINTA #pntw0001#10;
|FPRC;

|| --------------------------------------------------------------------------

|PROCESO Tipo12Fw2;  |TIPO 12;
     |SI FSalida = 7;  |ACABA;  |FINSI;
|FPRC;

|PROCESO Tipo0C6Fw2;  |TIPO 0;
     |SI FSalida ! 1;  |Y FSalida ! 7;
         |ERROR;
         |POSICIONATE #pntw0002#3;
     |FINSI;
|FPRC;

|PROCESO Tipo0C1Fw2;  |TIPO 0;
     |C_M #pntw0002#1, 0, aAlfa;
     |SI aAlfa = "N";  |ACABA;  |FINSI;

     #pntm0002#0 = #pntm0001#0;
     #pntm0002#1 = #pntw0002#1;
     |LEE 000#pntm0002.=;
     |SI FSalida = 0;
         |MENAV "0000La empresa ya est  seleccionada.";
         |ERROR;
     |FINSI;
|FPRC;

|| --------------------------------------------------------------------------

|PROCESO EnablaGecobox;
     |ESTADO_CONTROL nBtn4, "DISABLE";

     aMas = aBass + "dscomer9/def/agifa041.mas";
     |CARGA_DEF aMas, refermas@;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     aPath = aBass + "dscomer9/fich/";
     |PATH_DAT #refermas@#aPath#;

     |ABRE #refermas@;
     #refermas@#0 = #pntm0002#1;
     |LEE 000#refermas@.=;
     |SI FSalida ! 0;
         |ESTADO_CONTROL nBtn4, "ENABLE";
     |FINSI;
     |CIERRA #refermas@;

     |DESCARGA_DEF #refermas@;
|FPRC;

|PROCESO Evento;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#pntm0002.=;
         |SI FSalida ! 0;  |DDEFECTO #pntm0002;  |FINSI;

         #pntw0002#1 = #pntm0002#1;
         #pntw0002#2 = #pntm0002#2;
         #pntw0002#3 = #pntm0002#3;
         #pntw0002#4 = #pntm0002#4;
         #pntw0002#5 = #pntm0002#5;
         #pntw0002#6 = #pntm0002#6;

         |PINTA #pntw0002#1;
         |PINTA #pntw0002#2;
         |PINTA #pntw0002#3;
         |PINTA #pntw0002#4;
         |PINTA #pntw0002#5;
         |PINTA #pntw0002#6;

         |ESTADO_CONTROL nBtn2, "ENABLE";
         |ESTADO_CONTROL nBtn3, "ENABLE";

         |HAZ EnablaGecobox;
     |FINSI;
|FPRC;

|PROCESO Alta;
     |HAZ QuitaBotonSalir1;

     |ESTADO_CONTROL nBtn1, "DISABLE";
     |ESTADO_CONTROL nBtn2, "DISABLE";
     |ESTADO_CONTROL nBtn3, "DISABLE";
     |ESTADO_CONTROL nGrid, "DISABLE";

     |DDEFECTO #pntw0002;
     |DDEFECTO #pntm0002;

     |C_M #pntw0002#1, 1, "S";

     #pntw0002#0 = #pntm0001#0;

     |PINTA #pntw0002#1;
     |PINTA #pntw0002#2;
     |PINTA #pntw0002#3;
     |PINTA #pntw0002#4;
     |PINTA #pntw0002#5;
     |PINTA #pntw0002#6;

     |ENDATOS #1#pntw0002;
     |SI FSalida = 0;
         #pntm0002#0 = #pntw0002#0;
         #pntm0002#1 = #pntw0002#1;
         #pntm0002#2 = #pntw0002#2;
         #pntm0002#3 = #pntw0002#3;
         #pntm0002#4 = #pntw0002#4;
         #pntm0002#5 = #pntw0002#5;
         #pntm0002#6 = #pntw0002#6;

         |GRABA 020#pntm0002.n;
         |LIBERA #pntm0002;

         enCreaUsuarios = 1;
     |FINSI;

     |REFRESCACONTROL nGrid;  |PULSATECLA;

     |ESTADO_CONTROL nBtn1,   "ENABLE";
     |ESTADO_CONTROL nBtn2,   "ENABLE";
     |ESTADO_CONTROL nBtn3,   "ENABLE";
     |ESTADO_CONTROL nGrid,   "ENABLE";

     |FOCOTECLADO nGrid;

     |PULSATECLA;

     |HAZ PonBotonSalir1;
|FPRC;

|PROCESO Modificar;
     |LEE 101#pntm0002.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |HAZ QuitaBotonSalir1;

     |ESTADO_CONTROL nBtn1, "DISABLE";
     |ESTADO_CONTROL nBtn2, "DISABLE";
     |ESTADO_CONTROL nBtn3, "DISABLE";
     |ESTADO_CONTROL nGrid, "DISABLE";

     #pntw0002#0 = #pntm0002#0;
     #pntw0002#1 = #pntm0002#1;
     #pntw0002#2 = #pntm0002#2;
     #pntw0002#3 = #pntm0002#3;
     #pntw0002#4 = #pntm0002#4;
     #pntw0002#5 = #pntm0002#5;
     #pntw0002#6 = #pntm0002#6;

     |C_M #pntw0002#1, 1, "N";

     |PINTA #pntw0002#1;
     |PINTA #pntw0002#2;
     |PINTA #pntw0002#3;
     |PINTA #pntw0002#4;
     |PINTA #pntw0002#5;
     |PINTA #pntw0002#6;

     |ENDATOS #1#pntw0002;
     |SI FSalida = 0;
         #pntm0002#2 = #pntw0002#2;
         #pntm0002#3 = #pntw0002#3;
         #pntm0002#4 = #pntw0002#4;
         #pntm0002#5 = #pntw0002#5;
         #pntm0002#6 = #pntw0002#6;

         |GRABA 020#pntm0002.a;

         enCreaUsuarios = 1;
     |FINSI;
     |LIBERA #pntm0002;

     |REFRESCACONTROL nGrid;  |PULSATECLA;

     |ESTADO_CONTROL nBtn1, "ENABLE";
     |ESTADO_CONTROL nBtn2, "ENABLE";
     |ESTADO_CONTROL nBtn3, "ENABLE";
     |ESTADO_CONTROL nGrid, "ENABLE";

     |FOCOTECLADO nGrid;

     |HAZ PonBotonSalir1;

     |PULSATECLA;
|FPRC;

|PROCESO Baja;
     |LEE 101#pntm0002.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |CONFI "0000NSeguro que quiere BORRAR la empresa";
     |SI FSalida ! 0;  |LIBERA #pntm0002;  |ACABA;  |FINSI;

     |BORRA 020#pntm0002.a;
     |LIBERA #pntm0002;

     |REFRESCACONTROL nGrid;  |PULSATECLA;

     |FOCOTECLADO nGrid;

     enCreaUsuarios = 1;
|FPRC;

|PROCESO Gecobox;
     |CONFI "0000SSeguro que quiere crear GECOBOX en la empresa seleccionada";
     |SI FSalida ! 0;
         |FOCOTECLADO nGrid;
         |ACABA;
     |FINSI;

     enCodCli = #pntm0002#1;
     eaEMail  = #pntm0001#9;
     |SUB_EJECUTA "dsaz9996;GECOBOX";

     |REFRESCACONTROL nGrid;  |PULSATECLA;

     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO BajaEmp;
     #pntm0002#0 = #pntm0001#0;
     #pntm0002#1 = 1;
|FPRC;

|PROCESO AltaEmp;
     #pntm0002#0 = #pntm0001#0;
     #pntm0002#1 = 99999;
|FPRC;

|PROCESO Empresas;
     |DBASS aBass;

     aAlfa = "Empresas del usuario " + #pntm0001#2;  |QBF aAlfa;
     aAlfa = aAlfa + " adem s del principal";
     |VENTANA 0501, 3399, -1, 16, aAlfa;
     nVentTrab = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentTrab;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PINPA #0#pntw0002;
     nPanta = FSalida;

     nRango = 4 + 8 + 16 + 32 + 128;

     |LINEAL_SIMPLE #1#pntm0002, nRango, 0502, 1998, BajaEmp, AltaEmp, Evento;
     nGrid = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}Alta ", 2064, 2074, Alta;
     nBtn1  = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Modificar ", 2076, 2086, Modificar;
     nBtn2  = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{199}}Borrar", 2088, 2098, Baja;
     nBtn3  = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Generar GECOBOX", 2002, 2020, Gecobox;
     nBtn4  = FSalida;

     |ESTADO_CONTROL nBtn2, "DISABLE";
     |ESTADO_CONTROL nBtn3, "DISABLE";
     |ESTADO_CONTROL nBtn4, "DISABLE";

     |PINDA #0#pntw0002;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::{{001}}Salir," + nGrid;
          |LEETECLA aTecla;
          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
          |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nBtn1;
     |FIN_CONTROL_WINDOWS nBtn2;
     |FIN_CONTROL_WINDOWS nBtn3;
     |FIN_CONTROL_WINDOWS nBtn4;

     |PULSATECLA;

     |DESTRUYECONTROL nGrid;
     |PULSATECLA;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentTrab;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVentTrab;
     |PULSATECLA;
|FPRC;

|| --------------------------------------------------------------------------

|PROCESO GrabaUsuario;
     |ABRE #dsam0000;
     #dsam0000#0 = #pntw0001#0;
     |LEE 000#dsam0000.=;
     |SI FSalida ! 0;  |DDEFECTO #dsam0000;  |FINSI;
     |CIERRA #dsam0000;

     aUsuario       = #pntw0001#1;  |QBF aUsuario;
     aPassword      = #pntw0001#8;  |QBF aPassword;
     aNombreUsuario = #pntw0001#2;
     aMailUsuario   = #pntw0001#9;
     nCodInterno    = #pntw0001#10;

     |SI eaTipoDSOPORTE = "S";
         |HAZ GrabaGecochek;
     |FINSI;

     |SI eaTipoDSOPORTE = "G";
         |HAZ GrabaDsoporte;
     |FINSI;

     #pntm0001#10 = nCodInterno;
     #pntw0001#10 = nCodInterno;

     aTo = #pntw0001#9;  |QBF aTo;
     |SI aTo = "";  |ACABA;  |FINSI;

     |SI nModo = 2;
         |CONFI "0000NQuiere reenviar el correo de acceso al usuario modificado.";
         |SI FSalida ! 0;  |ACABA;  |FINSI;
     |FINSI;

     |HAZ CorreoNotifica;
|FPRC;

|PROCESO Tipo12Fw1;  |TIPO 12;
     |SI FSalida = 7;  |ACABA;  |FINSI;

     |SI eaTipoDSOPORTE = "S";
         |SI #pntw0001#6 = 0;
             |MENAV "0000Por favor introduzca NIVEL";
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;

     aAlfa = #pntw0001#9;  |QBF aAlfa;
     |SI aAlfa = "";
         |MENAV "0000Por favor introduzca E_MAIL";
         |ERROR;
         |ACABA;
     |FINSI;

     aAlfa = #pntw0001#8;  |QBF aAlfa;
     |SI aAlfa = "";
         |MENAV "0000Por favor introduzca PASSWORD";
         |ERROR;
         |ACABA;
     |FINSI;

     |CONFI "0000SSon correctos los datos introducidos";
     |SI FSalida ! 0;
         |ERROR;
         |ACABA;
     |FINSI;

     |HAZ GrabaUsuario;
|FPRC;

|PROCESO dpntm005;
     |BORRA 020#dpntm005.a;
     |LIBERA #dpntm005;
|FPRC;

|DEFBUCLE dpntm005;
     #dpntm005#2;
     ;
     #pntm0002#1, #pntw0001#9;
     #pntm0002#1, #pntw0001#9;
     be;
     NULL, dpntm005;
|FIN;

|PROCESO BorraDsarchi;

     |DBASS aBass;
     aFic = aBass + "dsarchi/fich/";

     |PATH_DAT #dpntm005#aFic#;

     |BUCLE dpntm005;

     |ABRE #dpntm005;
     |SELECT #1#dpntm005;
     #dpntm005#0 = #pntm0002#1;
     #dpntm005#1 = 1;
     |LEE 000#dpntm005.];
     |SI FSalida ! 0;  |O #dpntm005#0 ! #pntm0002#1;
         |PATH_DAT #dpntm001#aFic#;
         |ABRE #dpntm001;
         #dpntm001#0 = #pntm0002#1;
         |LEE 101#dpntm001.=;
         |SI FSalida = 0;
             |BORRA 020#dpntm001.a;
             |LIBERA #dpntm001;
         |FINSI;
         |CIERRA #dpntm001;
     |FINSI;
     |CIERRA #dpntm005;
|FPRC;

|PROCESO BorraUsuario;
     |ABRE #dsam0000;
     #dsam0000#0 = #pntw0001#0;
     |LEE 000#dsam0000.=;
     |SI FSalida ! 0;  |DDEFECTO #dsam0000;  |FINSI;
     |CIERRA #dsam0000;

     |LEE 101#pntm0001.=;
     |SI FSalida = 0;
         |BORRA 020#pntm0001.a;
         |LIBERA #pntm0001;

         enCreaUsuarios = 1;
         aUsuario       = #pntm0001#1;  |QBF aUsuario;
         aPassword      = #pntm0001#8;  |QBF aPassword;
         aNombreUsuario = #pntm0001#2;
         aMailUsuario   = #pntm0001#9;
         nCodInterno    = #pntm0001#10;

         |SI eaTipoDSOPORTE = "S";  |HAZ BorraGecochek;  |FINSI;
         |SI eaTipoDSOPORTE = "G";  |HAZ BorraDsoporte;  |FINSI;

         |HAZ BorraDsarchi;
     |FINSI;
|FPRC;

|PROCESO pntm0002;
     |BORRA 020#pntm0002.a;
     |LIBERA #pntm0002;
|FPRC;

|DEFBUCLE pntm0002;
     #pntm0002#1;
     ;
     #pntm0001#0, 00001;
     #pntm0001#0, 99999;
     be;
     NULL, pntm0002;
|FIN;

|PROCESO Desactiva;
     |HAZ BorraUsuario;

     |BUCLE pntm0002;
|FPRC;
