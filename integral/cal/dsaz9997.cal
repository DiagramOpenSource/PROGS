|FICHEROS;
     dsam9998;
     dsam9999;
|FIN;

|VARIABLES;
     aUsuario         = "";
     aBass            = "";
     aBase            = "";
     aAbre            = "";
     aAbreTmp         = "";
     aLee             = "";
     aAlfa            = "";
     aCarp            = "";
     aPathUsu         = "";
     aOrigen          = "";
     aDestino         = "";
     aBusco1          = "";
     aBusco2          = "";
     aMenuCambia      = "";

     nHay             = 0;
     nHandle          = 0;
     nHandleTmp       = 0;

     &enErrorMac      = 0;
     &eaDSMAC         = "";
     &eaModoMac       = "";
|FIN;

|PROCESO CambiaOpcion;
     aAbre = aPathUsu + aMenuCambia;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     aAbre = aPathUsu + aMenuCambia;
     |XABRE "E", aAbre, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     aBusco1  = "{{dsam0000}}";
     aBusco2  = "{{|dsxz0001}}";

     nHay = 0;
     |XABRE " ", aAbre, nHandle;
     |PARA;  |SI;  |HACIENDO;
          |XLEE nHandle, 254, aLee;
          |SI FSalida [ 0;  |SAL;  |FINSI;

          aAlfa = aLee - aBusco1;
          |SI FSalida ! 0;  nHay = 1;  |SAL;  |FINSI;

          aAlfa = aLee - aBusco2;
          |SI FSalida ! 0;  nHay = 1;  |SAL;  |FINSI;
     |SIGUE;
     |XCIERRA nHandle;

     |SI nHay = 0;  |ACABA;  |FINSI;

     aAbreTmp = aPathUsu + "dsmenutmp.men";

     |XABRE " ", aAbre, nHandle;
     |XABRE "W ", aAbreTmp, nHandleTmp;
     |PARA;  |SI;  |HACIENDO;
          |XLEE nHandle, 254, aLee;
          |SI FSalida [ 0;  |SAL;  |FINSI;

          aAlfa = aLee - aBusco1;
          |SI FSalida ! 0;
              aAlfa = aAlfa + " {{[:integral/dsaz9997]dscliente.men}}";
          |FINSI;

          aAlfa = aLee - aBusco2;
          |SI FSalida ! 0;
              aAlfa = aAlfa + " {{[:integral/dsaz9997]dscliente.men}}";
          |FINSI;

          |XGRABA nHandleTmp, aAlfa;
     |SIGUE;
     |XCIERRA nHandle;
     |XCIERRA nHandleTmp;

     aOrigen    = aPathUsu + "dsmenutmp.men";
     aDestino   = aPathUsu + aMenuCambia;
     |COPIA_FICHERO aOrigen, aDestino;
     |SI FSalida = 0;
         |FBORRA aOrigen;
     |FINSI;
|FPRC;

|PROCESO CreaMenu;
     aAbreTmp = aPathUsu + "dscliente.men";

     |XABRE "W ", aAbreTmp, nHandleTmp;

     aAlfa = "#10,!:Acceso cliente" + &13 + &10;
     |XGRABA nHandleTmp, aAlfa;

     |SI PARAMETRO ! "SINOPCION";
         |SI #dsam9999#1 = "9";
             aAlfa = "10:Acceso cliente {{dsam0000}}" + &13 + &10;
         |SINO;
             aAlfa = "10:Acceso cliente {{|dsxz0001}}" + &13 + &10;
         |FINSI;
         |XGRABA nHandleTmp, aAlfa;
     |FINSI;

     |XCIERRA nHandle;
|FPRC;

|PROCESO ProcesaMenu;
     aUsuario = Usuario % 810;  |QBF aUsuario;

     aAbre = aBass + "dev/usr/ds_sys.psw";

     aCarp = "";
     |XABRE " ", aAbre, nHandle;
     |PARA;  |SI;  |HACIENDO;
          |XLEE nHandle, 200, aLee;
          |SI FSalida [ 0;  |SAL;  |FINSI;

          aAlfa = aLee % 410;  |QBF aAlfa;
          |SI aAlfa = aUsuario;
              aCarp = aLee % 103;
              |SAL;
          |FINSI;
     |SIGUE;
     |XCIERRA nHandle;

     |SI aCarp = "";  |ACABA;  |FINSI;

     aPathUsu = aBass + "dev/usr/" + aCarp;  |MKDIR aPathUsu;
     aPathUsu = aBass + "dev/usr/" + aCarp + "/";

     |HAZ CreaMenu;

     aMenuCambia = "dsmenu02.men";  |HAZ CambiaOpcion;
     aMenuCambia = "dsmenu20.men";  |HAZ CambiaOpcion;
|FPRC;

|PROCESO Menu;
     enErrorMac = 1;

     #dsam9999#0 = eaDSMAC;
     |LEE 101#dsam9999.=;
     |SI FSalida ! 0;
         |DDEFECTO #dsam9999;
         #dsam9999#0 = eaDSMAC;

         |SI #dsam9998#1 = "X";
             #dsam9999#1 = "X";
         |FINSI;

         |SI #dsam9998#1 = "9";
             #dsam9999#1 = "9";
         |FINSI;

         |SI #dsam9998#1 = "A";
             #dsam9999#1 = "9";

             |PUSHV 0501 2380;
             |BLANCO 0501 2380;
             |CUADRO 0501 2380;

             |ATRI S;
             |PINTA 0710, "NUEVA ENTRADA AL MANTENIMIENTO DE CLIENTES DISPONIBLE MODO X";
             |ATRI 0;

             aAlfa = "- En estos momentos disponemos de dos entradas al mantenimiento de clientes:";
             |PINTA 0902, aAlfa;

             aAlfa = "     - Modo 9: Versi¢n anterior.";
             |PINTA 1002, aAlfa;

             aAlfa = "     - Modo X: Nueva versi¢n, acceso por lineal y  rbol, como en la Renta.";
             |PINTA 1102, aAlfa;

             aAlfa = "- Existe un bot¢n en las dos pantallas para poder activar una u otra.";
             |PINTA 1302, aAlfa;

             aAlfa = "- La versi¢n elegida se guardar  por PC, as¡ cada usuario tendr  su forma ";
             |PINTA 1502, aAlfa;

             aAlfa = "  de trabajar favorita.";
             |PINTA 1602, aAlfa;

             aAlfa = "- ¨Desea activar en estos momentos la versi¢n Modo X? ";
             |PINTA 1902, aAlfa;

             E_inf = "SNsn";
             E_sup = "1";

             aAlfa = "N";
             aAlfa = 1956 ? 1;
             aAlfa = aAlfa > "";
             |POPV;

             |SI aAlfa = "S";
                 #dsam9999#1 = "X";
             |FINSI;
         |FINSI;

         |GRABA 020#dsam9999.n;
         |LIBERA #dsam9999;
     |SINO;
         |SI PARAMETRO = "";
             enErrorMac = 0;
             |LIBERA #dsam9999;
             |ACABA;
         |FINSI;

         |SI #dsam9998#1 = "X";
             #dsam9999#1 = "X";
         |FINSI;

         |SI #dsam9998#1 = "9";
             #dsam9999#1 = "9";
         |FINSI;

         |SI #dsam9998#1 = "A";
             |SI PARAMETRO = "MODO9";  #dsam9999#1 = "9";  |FINSI;
             |SI PARAMETRO = "MODOX";  #dsam9999#1 = "X";  |FINSI;
         |FINSI;

         |GRABA 020#dsam9999.a;
         |LIBERA #dsam9999;

         |SI #dsam9998#1 = "A";
             |SI PARAMETRO = "MODO9";  |O PARAMETRO = "MODOX";
                 |MENAV "0000Nuevo modo activado, vuelva a entrar en la opcion.";
             |FINSI;
         |FINSI;
     |FINSI;

     |HAZ ProcesaMenu;
|FPRC;

|PROGRAMA;
     |HAZ CogeMac;
     |HAZ Menu;

     eaModoMac = #dsam9999#1;
|FPRO;

|PROCESO Tipo80;  |TIPO 80;
     |DBASS aBass;

     |ABRE #dsam9998;
     |LEE 000#dsam9998.p;
     |SI FSalida ! 0;  |DDEFECTO #dsam9998;  |FINSI;
     |CIERRA #dsam9998;

     |ABRE #dsam9999;
|FPRC;

|PROCESO Tipo90;  |TIPO 90;
     |CIERRA #dsam9999;
|FPRC;
