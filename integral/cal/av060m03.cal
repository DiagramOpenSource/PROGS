|FICHEROS;
     dsam0001;
     dsam0016;

     av060m01;
     av060m02;
     av060m03;

     ficgrid@ #0;

     :modelos/dsmom108;
|FIN;

|VARIABLES;
     n0Vent           = -1;
     nRango           = 0;
     nGrid2          = 0;
     nLabel           = -1;
     nDEmp            = 0;
     nHEmp            = 0;
     nHandle          = 0;
     nTree            = 0;
     nLong            = 0;
     nPos             = 0;
     nPosi            = 0;
     nCampo           = 0;
     nTCampos         = 0;
     nDias            = 0;
     nAviso           = 0;
     nBtnLeer         = -1;

     aDEst            = "";
     aHEst            = "";
     aDCer            = "";
     aHCer            = "";
     aDBuz            = "";
     aHBuz            = "";
     aAlfa            = "";
     aAlfa1           = "";
     aEsc1            = "";
     aEsc2            = "";
     aRetu            = "";
     aTecla           = "";
     aDColor          = "";
     aHColor          = "";
     aBase            = "";
     aPathLocal       = "";
     aFicTree         = "";
     aAbre            = "";
     aBuscar          = "";
     aRefresca        = "";
     aLong            = "";
     aCarac           = "";
     aTexto           = "";
     aDef             = "";
     aMas             = "";
     aFicTmp          = "";
     aUsuario         = "";
     aUsu1            = "";
     aUsu2            = "";
     aUsu3            = "";
     aQueQuiero       = "";
     aProc            = "";
     aHora            = "";

     aRojo            = "255,80,80;255,255,255";
     aAzul            = "18,28,245;255,255,255";
     aVerde           = "0,185,0;255,255,255";

     fFechaHoy        = @;

     {-1}aVent        = "";
         aVent1       = 0;
         aVent2       = 0;
         aVent3       = 0;
         aVent4       = 0;
         aVent5       = "";

     {-1}sTree        = 0;
         sT_nID       = 0;
         sT_nOper     = 0;
         sT_aData     = "";

     {-1}aAviso       = "";
         aAviso1      = "";
         aAviso2      = "";
         aAviso3      = "";
         aAviso4      = "";

     &eaCer            = "";
     &eaBuz            = "";
     &eaUnidadBasico   = "";
     &eaDSMAC          = "";
     &eaPathExplorador = "";
     &eaForzExplorador = "";
|FIN;

|PROCESO PonLeido;
     |HORA aHora;

     #av060m03#0 = #ficgrid@#0;
     #av060m03#1 = #ficgrid@#1;
     #av060m03#2 = #ficgrid@#2;
     |LEE 101#av060m03.=;
     |SI FSalida = 0;
         #av060m03#8  = "S";
         #av060m03#9  = ~;
         #av060m03#10 = aHora;
         #av060m03#11 = aUsuario;
         #av060m03#12 = "S";

         aAlfa = #av060m03#4 - "caducado";
         |SI FSalida ! 0;
             #av060m03#7 = "ACEPTADA";
         |FINSI;

         |GRABA 020#av060m03.a;
         |LIBERA #av060m03;
     |FINSI;

     #ficgrid@#8  = "S";
     #ficgrid@#9  = ~;
     #ficgrid@#10 = aHora;
     #ficgrid@#11 = aUsuario;
     #ficgrid@#12 = "S";
     |GRABA 020#ficgrid@.a;
     |LIBERA #ficgrid@;
|FPRC;

|DEFBUCLE ficgrid;
     #ficgrid@#2003;
     ;
     "N", "            ", "            ";
     "N", "zzzzzzzzzzzz", "zzzzzzzzzzzz";
     be;
     NULL, PonLeido;
|FIN;

|PROCESO TodosLeidos;
     |CONFI "0000NSeguro que quiere dejar todos los registros como leidos";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |BUCLE ficgrid;
     |REFRESCACONTROL nGrid2;
|FPRC;

|PROCESO BajaC2;
     #ficgrid@#8 = aDEst;
     #ficgrid@#0 = aDCer;
     #ficgrid@#1 = aDBuz;
|FPRC;

|PROCESO AltaC2;
     #ficgrid@#8 = aHEst;
     #ficgrid@#0 = aHCer;
     #ficgrid@#1 = aHBuz;
|FPRC;

|PROCESO Evento2;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#ficgrid@.c;
     |FINSI;

     |SI FSalida = 4;
         |SI eaCer        ! "";  |ACABA;  |FINSI;
         |SI #ficgrid@#8 ! "N";  |ACABA;  |FINSI;

         |CONFI "0000SEst  seguro de dejar el registro como leido";
         |SI FSalida ! 0;  |ACABA;  |FINSI;

         |SELECT #1#ficgrid@;
         |LEE 101#ficgrid@.=;

         |HAZ PonLeido;

         |SELECT #2#ficgrid@;
         |REFRESCACONTROL nGrid2;
    |FINSI;
|FPRC;

|PROCESO Pendientes;
     aDEst   = "N";
     aHEst   = "N";
     aDCer   = "";
     aHCer   = "zzzzzzzzzzzz";
     aDBuz   = "";
     aHBuz   = "zzzzzzzzzzzz";

     |SI eaCer ! "";
         aDCer = eaCer;
         aHCer = eaCer;
         aDBuz = eaBuz;
         aHBuz = eaBuz;
     |FINSI;

     |DESTRUYECONTROL nGrid2;

     |LINEAL_SIMPLE #2#ficgrid@, nRango, 1102, 3298, BajaC2, AltaC2, Evento2;
     nGrid2 = FSalida;

     |FOCOTECLADO nGrid2;
|FPRC;

|PROCESO Leidos;
     aDEst   = "S";
     aHEst   = "S";
     aDCer   = "";
     aHCer   = "zzzzzzzzzzzz";
     aDBuz   = "";
     aHBuz   = "zzzzzzzzzzzz";

     |SI eaCer ! "";
         aDCer = eaCer;
         aHCer = eaCer;
         aDBuz = eaBuz;
         aHBuz = eaBuz;
     |FINSI;

     |DESTRUYECONTROL nGrid2;

     |LINEAL_SIMPLE #2#ficgrid@, nRango, 1102, 3298, BajaC2, AltaC2, Evento2;
     nGrid2 = FSalida;

     |FOCOTECLADO nGrid2;
|FPRC;

|PROCESO Todas;
     aDEst   = " ";
     aHEst   = "z";
     aDCer   = "";
     aHCer   = "zzzzzzzzzzzz";
     aDBuz   = "";
     aHBuz   = "zzzzzzzzzzzz";

     |SI eaCer ! "";
         aDCer = eaCer;
         aHCer = eaCer;
         aDBuz = eaBuz;
         aHBuz = eaBuz;
     |FINSI;

     |DESTRUYECONTROL nGrid2;

     |LINEAL_SIMPLE #2#ficgrid@, nRango, 1102, 3298, BajaC2, AltaC2, Evento2;
     nGrid2 = FSalida;

     |FOCOTECLADO nGrid2;
|FPRC;


|PROCESO PonLabel;
     |SI nLabel ! -1;
         |FIN_CONTROL_WINDOWS nLabel;
     |FINSI;

     |SI nBtnLeer ! -1;
         |FIN_CONTROL_WINDOWS nBtnLeer;
     |FINSI;

     aAlfa = "";
     aLong = Contenido % 0;
     nLong = aLong;
     |PARA nPosi = 1;  |SI nPosi [ nLong;  |HACIENDO nPosi = nPosi + 1;
           nPos = (nPosi * 100) + 1;
           aCarac = Contenido % nPos;
           |SI aCarac = ":";  |SAL;  |FINSI;
           aAlfa  = aAlfa + aCarac;
     |SIGUE;

     |QBT aAlfa;

     sT_nID   = nTree;
     sT_nOper = 4;
     sT_aData = ":" + aAlfa;

     |ESPECIFICA "COMMANDCONTROL", sTree;
     aTexto = Contenido;  |QBF aTexto;

     aAlfa = "LABEL,{{10}}" + aTexto;
     |SI aProc = "GRID0";
         |SI eaCer = "";
             aAlfa = aAlfa + "    (doble click en el registro para dejarlo como leido)";

             |CONTROL_SIMPLE 0, "BOTON,{{197}}Dejar todos como leidos", 3302, 3325, TodosLeidos;
             nBtnLeer = FSalida;

         |FINSI;
     |FINSI;

     |CONTROL_SIMPLE 0, aAlfa, 1002, 1098, NULL;
     nLabel = FSalida;
|FPRC;

|PROCESO EventoTree;
     |SI FEntrada ! 0;  |ACABA;  |FINSI;

     aAlfa1 = Contenido;  |QBF aAlfa1;
     aAlfa1 = aAlfa1 % -101;
     |SI aAlfa1 = ":";
         |ACABA;
     |FINSI;

     aProc = Contenido % -105;

     |SI aProc = "     ";  |O aProc = "";
         |ACABA;
     |FINSI;

     |SI aProc = "GRID0";  |HAZ Pendientes;  |FINSI;
     |SI aProc = "GRID1";  |HAZ Leidos;      |FINSI;

     |HAZ PonLabel;
|FPRC;

|PROCESO PintaArbol;
     aAbre = aPathLocal + aFicTree;
     aAlfa = "TREECTRL,Avisos {{" + aAbre + "}}";
     |CONTROL_SIMPLE 0, aAlfa, 0602, 0898, EventoTree;
     nTree = FSalida;

     |SI aBuscar ! "";
         sT_nID   = nTree;
         sT_nOper = 3;
         sT_aData = "[" + aBuscar;
         |ESPECIFICA "COMMANDCONTROL", sTree;
         |SI FSalida ] 0;
             aRefresca = ":" + FSalida;
         |SINO;
             aRefresca = "1:";
         |FINSI;
     |FINSI;

     sT_nID   = nTree;
     sT_nOper = 2;
     |SI aRefresca = "";
         sT_aData = "1:";
     |SINO;
         sT_aData = aRefresca;
     |FINSI;
     |ESPECIFICA "COMMANDCONTROL", sTree;
|FPRC;

|PROCESO Explorador;
     |HAZ CogeMac;

     |ABRE #dsmom108;
     #dsmom108#0 = eaDSMAC;
     |LEE 101#dsmom108.=;
     |SI FSalida ! 0;
         |DDEFECTO #dsmom108;
         #dsmom108#0 = eaDSMAC;
         |GRABA 020#dsmom108.b;
     |FINSI;

     eaPathExplorador = #dsmom108#3;  |QBF eaPathExplorador;

     |SI eaPathExplorador = "";  |O eaForzExplorador = "S";
         |MENAV "0000Busque y pinche en el explorador con el que quiera trabajar";

         eaPathExplorador = "F" + eaPathExplorador;
         |BROWSE eaPathExplorador;
         eaPathExplorador = eaPathExplorador % 290;  |QBF eaPathExplorador;
     |FINSI;

     |SI eaPathExplorador ! "";
         #dsmom108#3 = eaPathExplorador;
     |FINSI;

     |GRABA 020#dsmom108.a;
     |LIBERA #dsmom108;
     |CIERRA #dsmom108;
|FPRC;

|PROCESO BtnPagi;
     |ABRE #dsam0001;
     |LEE 000#dsam0001.=;
     |SI FSalida ! 0;  |DDEFECTO #dsam0001;  |FINSI;
     |CIERRA #dsam0001;

     eaForzExplorador = "N";
     |HAZ Explorador;

     |SI eaPathExplorador ! "";
         aAlfa = eaPathExplorador + " " + #dsam0001#11;  |QBF aAlfa;
         aAlfa = "cmd " + &34 + "/c START /WAIT " + aAlfa + &34;
     |SINO;
         aAlfa  = "cmd " + &34 + "/c START /WAIT " + #dsam0001#11;  |QBF aAlfa;
         aAlfa  = aAlfa + &34;
     |FINSI;

     |RSYSTEM aAlfa;

     |SUB_EJECUTA ":dsarchi/dsarz065";

     |FOCOTECLADO nGrid2;
|FPRC;

|PROGRAMA;
     |SI eaCer = "";
         |VENTANA 0501, 3599, -1, 16, "Mis avisos";
     |SINO;
         |VENTANA 0501, 3599, -1, 16, "Avisos por certificado/buz¢n";
     |FINSI;
     n0Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n0Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     |CONTROL_SIMPLE 0, "LABEL,{{15}}AVISOS", 0502, 0532, NULL;

     aDEst   = "N";
     aHEst   = "N";
     aDCer   = "";
     aHCer   = "zzzzzzzzzzzz";
     aDBuz   = "";
     aHBuz   = "zzzzzzzzzzzz";

     |SI eaCer ! "";
         aDCer = eaCer;
         aHCer = eaCer;
         aDBuz = eaBuz;
         aHBuz = eaBuz;
     |FINSI;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Ir a la URL", 3502, 3520, BtnPagi;

     nRango = 2 + 4 + 8 + 16 + 32 + 128;

     |LINEAL_SIMPLE #2#ficgrid@, nRango, 1002, 3298, BajaC2, AltaC2, Evento2;
     nGrid2 = FSalida;

     |HAZ PintaArbol;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}Salir," + nGrid2;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |DESTRUYECONTROL nGrid2;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n0Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n0Vent;
|FPRO;

|PROCESO CargaArbol;
     aAlfa = aAlfa + &13 + &10;
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO GrabaArbol;
     |HAZ LeeUnidadBasico;

     |DBASE aBase;  |QBF aBase;
     aPathLocal = eaUnidadBasico + "\documentos";   |RMKDIR aPathLocal;
     aPathLocal = aPathLocal + "\tmp";              |RMKDIR aPathLocal;
     aPathLocal = aPathLocal + "\";

     aFicTree = "av060m0301" + Usuario + ".txt";

     aAbre = aPathLocal + aFicTree;
     |XABRE "WBC", aAbre, nHandle;

     aAlfa   = "1:Pendientes de lectura {{GRID0}}";  |HAZ CargaArbol;
     aAlfa   = "2:Leidos {{GRID1}}";  |HAZ CargaArbol;

     |XCIERRA nHandle;
|FPRC;

|PROCESO GrabaTmp;
     |PARA nCampo = 0;  |SI nCampo [ nTCampos;  |HACIENDO nCampo = nCampo + 1;
           #ficgrid@#nCampo# = #av060m03#nCampo#
     |SIGUE;

     |GRABA 020#ficgrid@.n;
     |LIBERA #ficgrid@;
|FPRC;

|PROCESO av060m03;
     #av060m02#0 = #av060m03#0;
     #av060m02#1 = #av060m03#1;
     |LEE 000#av060m02.=;
     |SI FSalida ! 0;  |DDEFECTO #av060m02;  |FINSI;

     aUsu1 = #av060m02#4;  |QBF aUsu1;
     aUsu2 = "";

     |SI #av060m03#8 = "N";
         nDias = fFechaHoy - #av060m03#5;
         |SI nDias ] #av060m02#6;
             aUsu2 = #av060m02#5;  |QBF aUsu2;
         |FINSI;
     |SINO;
         aUsu2 = #av060m02#5;  |QBF aUsu2;
     |FINSI;

     |SI aUsu1 = "";  |Y aUsu2 = "";
         |SI aUsu3 = aUsuario;
             |HAZ GrabaTmp;
             |ACABA;
         |FINSI;
     |FINSI;

     |SI aUsu1 = aUsuario;
         |HAZ GrabaTmp;
         |ACABA;
     |FINSI;

     |SI aUsu2 = aUsuario;
         |HAZ GrabaTmp;
     |FINSI;
|FPRC;

|DEFBUCLE av060m03;
     #av060m03#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, av060m03;
|FIN;

|PROCESO CompruebaAviso;
     |ABRE #ficgrid@;
     |SELECT #2#ficgrid@;
     #ficgrid@#8 = "N";
     #ficgrid@#0 = 0;
     |LEE 000#ficgrid@.];
     |SI FSalida = 0;  |Y #ficgrid@#8 = "N";
         aAviso1 = "0";
         aAviso2 = "2";
         aAviso3 = "Plataforma Integrada DS";
         aAviso4 = "Tiene avisos DEH, recuerde comprobarlos en su mantenimiento";
         |ESPECIFICA "MENSAJETRAY", aAviso;
     |FINSI;
     |CIERRA #ficgrid@;

     |SI nAviso = 1;
         aAviso1 = "1";
         aAviso2 = "1";
         aAviso3 = "Plataforma Integrada DS";
         aAviso4 = "DEH revisando avisos a día de hoy";
         |ESPECIFICA "MENSAJETRAY", aAviso;
     |FINSI;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     aUsuario  = Usuario % 810;  |QBF aUsuario;
     aUsu3     = "";
     fFechaHoy = ~;

     |DDEF aDef;
     aMas = aDef + "av060m03.mas";
     |CARGA_DEF aMas, ficgrid@;

     aQueQuiero = "|NC";
     aAlfa      = #ficgrid@#aQueQuiero#;
     nTCampos   = aAlfa;
     nTCampos   = nTCampos - 1;

     |SI eaCer = "";
         aFicTmp = "av060m03" + Usuario;
         |NOME_DAT #ficgrid@#aFicTmp#;

         |VENTANA 0501, 0540, -1, 16, "Buscando avisos";
         n0Vent = FSalida;

         aVent1 = -1;
         aVent2 = -1;
         aVent3 = 0;
         aVent4 = n0Vent;
         aVent5 = "MODAL";
         |ESPECIFICA "ESTADO_VENTANA", aVent;

         |PTEC 802;  |PAUSA;

         |ABRE #dsam0016;
         #dsam0016#0 = aUsuario;
         |LEE 000#dsam0016.=;
         |SI FSalida = 0;
             aUsu3 = #dsam0016#0;  |QBF aUsu3;
         |FINSI;
         |CIERRA #dsam0016;

         |ABRE #ficgrid@;
         |ABRE #av060m01;
         |ABRE #av060m02;
         |BUCLE av060m03;
         |CIERRA #av060m01;
         |CIERRA #av060m02;
         |CIERRA #ficgrid@;

         aVent1 = -1;
         aVent2 = -1;
         aVent3 = 0;
         aVent4 = n0Vent;
         aVent5 = "MODELESS";
         |ESPECIFICA "ESTADO_VENTANA", aVent;

         |FINVENTANA n0Vent;

         nAviso = 1;  |HAZ CompruebaAviso;
     |FINSI;

     |HAZ GrabaArbol;

     |ABRET #ficgrid@;
     |ABRET #av060m03;
|FPRC;

|PROCESO Tipo90;  |TIPO 90;
     aAbre = aPathLocal + aFicTree;  |RFBORRA aAbre;

     |CIERRAT #av060m03;
     |CIERRAT #ficgrid@;

     |SI eaCer = "";
         nAviso = 2;  |HAZ CompruebaAviso;

         |DELINDEX #ficgrid@;
     |FINSI;

     |DESCARGA_DEF #ficgrid@;

     |SI eaCer = "";
         |SUB_EJECUTA "av060z02;ENVIO";
     |FINSI;
|FPRC;
