|FICHEROS;
     av060m01;

     av060w00,MANTE;
     av060w02,MANTE;
|FIN;

|VARIABLES;
     aAlfa            = "";
     aBase            = "";
     aFicOrigen       = "";
     aFicDestino      = "";
     aDocRemoto       = "";
     aDocServidor     = "";
     aIPRemoto        = "";
     aDirLocal        = "";
     aPrograma        = "";
     aBorra           = "";
     aAbre            = "";
     aLinea           = "";
     aLong            = "";
     aCarac           = "";
     aContenido       = "";

     n2Vent           = 0;
     nLong            = 0;
     nBtnPass         = -1;
     nBtnCert         = -1;
     nCarac           = 0;
     nPos             = 0;
     nHandle          = 0;

     {1}aContiene     = "";

     {-1}aVent        = "";
         aVent1       = 0;
         aVent2       = 0;
         aVent3       = 0;
         aVent4       = 0;
         aVent5       = "";

     &enPanta43       = 0;
     &eaPass          = "";
     &eaPassEnc       = "";
     &eaUnidadBasico  = "";
     &eaFicCert       = "";
     &enCorreo        = 0;
|FIN;

|PROCESO Tipo7Campo;  |TIPO 7;
     aContenido = #av060w00#Campo#;
|FPRC;

|PROCESO Tipo0Campo;  |TIPO 0;
     aAlfa = #av060w00#Campo#;

     |SI aContenido ! aAlfa;
         enCorreo = 1;
     |FINSI;

     |SI Campo = 8;
         |POSICIONATE #av060w00#1;
     |FINSI;
|FPRC;

|PROCESO Pondsselect;
     |HAZ LeeUnidadBasico;

     |DBASS aBase;
     |QBF aBase;
     aFicOrigen = aBase + "dsarchi/dlls/dsselect.exe";
     |XABRE "E", aFicOrigen, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     aDirLocal    = eaUnidadBasico + "\DOCUMENTOS";          |RMKDIR aDirLocal;
     aDirLocal    = eaUnidadBasico + "\DOCUMENTOS\DSARCHI";  |RMKDIR aDirLocal;
     aDirLocal    = eaUnidadBasico + "\DOCUMENTOS\DSARCHI\";
     aFicDestino  = aDirLocal + "dsselect.exe";

     aContiene1 = aFicDestino;
     |ESPECIFICA "REMOTOMD5", aContiene;
     aDocRemoto = FSalida;  |QBF aDocRemoto;

     aContiene1 = aFicOrigen;
     |ESPECIFICA "MD5", aContiene;
     aDocServidor = FSalida;  |QBF aDocServidor;

     |SI aDocRemoto ! aDocServidor;  |O aDocRemoto = "";
         aIPRemoto = "";
         |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aFicOrigen, aFicDestino;
         |SINO;
             |COPIA_FICHERO aFicOrigen, aFicDestino;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO FinVentana2;
     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n2Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n2Vent;
|FPRC;

|PROCESO RecogeCertificado;
     |VENTANA 0501, 0540, -1, 16, "Recogiendo certificado";
     n2Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n2Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     |HAZ Pondsselect;
     aPrograma = aDirLocal + "dsselect.exe";

     aBorra = aDirLocal + "dsselect.txt";  |RFBORRA aBorra;
     aBorra = aDirLocal + "dsselect.ini";  |RFBORRA aBorra;

     aAbre = aDirLocal + "dsselect.ini";
     |XABRE "CW", aAbre, nHandle;
     |SI FSalida ] 0;
          aLinea = &34 + eaUnidadBasico + &34 + " " + &34 + aDirLocal + "dsselect.txt" + &34;
          |XGRABA nHandle, aLinea;
          |XCIERRA nHandle;
     |FINSI;

     |RSYSTEM aPrograma;
     |PULSATECLA;

     aAbre = aDirLocal + "dsselect.txt";
     |XABRE "CU", aAbre, nHandle;
     |SI FSalida < 0;
          eaFicCert = "";
          |HAZ FinVentana2;
          |ACABA;
     |SINO;
          |XLEE nHandle, 250, eaFicCert;
          |SI FSalida < 0
               eaFicCert = "";
               |HAZ FinVentana2;
               |ACABA;
          |FINSI;
     |FINSI;

     |XCIERRA nHandle;
     aBorra = aDirLocal + "dsselect.txt";
     |RFBORRA aBorra;

     |PULSATECLA;

     |QBF eaFicCert;
     |SI eaFicCert = "";
         |HAZ FinVentana2;
         |ACABA;
     |FINSI;

     aAlfa  = "";
     aLong  = eaFicCert % 0;
     nLong  = aLong;
     |PARA nCarac = 1;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos   = (nCarac * 100) + 1;
           aCarac =  eaFicCert % nPos;
           |SI aCarac = "/";  |O aCarac = "\";
               aAlfa = "";
           |SINO;
               |SI aCarac = " ";
                   aCarac = "_";
               |FINSI;

               aAlfa = aAlfa + aCarac;
           |FINSI;
     |SIGUE;

     #av060w00#2 = aAlfa;

     |HAZ FinVentana2;
     |PULSATECLA;
|FPRC;

|PROCESO PintaPass;
     aAlfa = #av060w00#6;  |QBF aAlfa;
     aAlfa = aAlfa % 0;
     nLong = aAlfa;

     #av060w00#9 = "*" * nLong;
     |PINTA #av060w00#9;
|FPRC;

|PROCESO BtnPass;
     |VENTANA 0501, 1430, -1, 16, "Contrase¤a";
     n2Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n2Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     |C_M #av060w02#0, 1, "S";

     aAlfa = #av060w00#6;  |QBF aAlfa;
     |SI aAlfa = "";
         |C_M #av060w02#0, 1, "N";
     |FINSI;

     #av060w02#0 = "";
     #av060w02#1 = "";
     #av060w02#2 = "";

     |PINPA #0#av060w02;
     |PINDA #0#av060w02;
     |ENDATOS #1#av060w02;
     |SI FSalida = 0;
         |SI #av060w00#6 ! #av060w02#1;
             enCorreo = 1;
         |FINSI;

         #av060w00#6 = #av060w02#1;
     |FINSI;

     |HAZ FinVentana2;

     |HAZ PintaPass;
|FPRC;

|PROCESO BtnCert;
     |CONFI "0000NSeguro que quiere cambiar el certificado";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |HAZ RecogeCertificado;

     |PINTA #av060w00#6;

     #av060w00#6  = "";
     #av060w00#9  = "";  |PINTA #av060w00#9;
|FPRC;

|PROCESO Tipo12Fw00;  |TIPO 12;
     |SI FSalida = 7;
         nBtnPass = -1;
         nBtnCert = -1;
         |ACABA;
     |FINSI;

     |SI #av060w00#3 [ "01.01.0000";
         |MENAV "0000Fecha alta incorrecta.";
         |POSICIONATE #av060w00#3;
         |ERROR;
         |ACABA;
     |FINSI;

     |SI #av060w00#5 [ #av060w00#3;
         |MENAV "0000Fecha caducidad incorrecta.";
         |POSICIONATE #av060w00#5;
         |ERROR;
         |ACABA;
     |FINSI;

     aAlfa = #av060w00#6;  |QBF aAlfa;
     |SI aAlfa = "";
         |MENAV "0000No se ha introducido una contrase¤a para el certificado";
         |POSICIONATE #av060w00#5;
         |ERROR;
         |ACABA;
     |FINSI;

     nBtnPass = -1;
     nBtnCert = -1;
|FPRC;

|PROCESO Tipo7C9Fw00;  |TIPO 7;
     aAlfa = #av060w00#6;  |QBF aAlfa;
     |SI aAlfa = "";
         |HAZ BtnPass;
     |FINSI;
|FPRC;

|PROCESO Tipo0C0Fw00;  |TIPO 0;
     |C_M #av060w00#0, 0, aAlfa;
     |SI aAlfa = "N";  |ACABA;  |FINSI;

     aAlfa = #av060w00#0;  |QBF aAlfa;
     |SI aAlfa = "";
         |MENAV "0000Introduzca el CIF/NIF del certificado.";
         |ERROR;
         |ACABA;
     |FINSI;

     #av060m01#0 = #av060w00#0;
     |LEE 000#av060m01.=;
     |SI FSalida = 0;
         |MENAV "0000El certificado ya est  dado de alta.";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo7C0Fw00;  |TIPO 7;
     |SI nBtnPass ! -1;
         |ACABA;
     |FINSI;

     |CONTROL_SIMPLE enPanta43, "BOTON,{{197}}", 2485, 2487, BtnCert;
     nBtnCert = FSalida;

     |CONTROL_SIMPLE enPanta43, "BOTON,{{197}}", 1544, 1546, BtnPass;
     nBtnPass = FSalida;
|FPRC;
