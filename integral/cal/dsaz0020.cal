|FICHEROS;
     dsaz0020;
     dsam0119;
     dsam0120;
     dsaw0036,MANTE;
|FIN;

|VARIABLES;
     aAbre          = "";
     aBase          = "";
     aAlfa          = "";
     aAlfa1         = "";
     aAlfa2         = "";
     aEsc1          = "";
     aEsc2          = "";
     aRetu          = "";
     aTecla         = "";
     aFichero       = "";
     aCarga         = "";
     aQueQuiero     = "";
     aTexto         = "";
     aLong          = "";
     aCarac         = "";
     aRefresca      = "";
     aBusca         = "";
     aIPRemoto      = "";
     aNombreIP      = "";
     aRuta          = "";

     nBotonLabel    = 0;
     nHandle        = 0;
     nTree          = -1;
     nPanta         = 0;
     nSubPanta      = 0;
     nLong          = 0;
     nCargar        = 0;
     nPos           = 0;
     nPosi          = 0;
     nBoton1        = 0;
     nBoton2        = 0;
     nBoton3        = 0;
     nSubVent       = 0;
     nCampo         = 0;
     nTCampo        = 0;
     nCarpeta1      = 0;
     nCarpeta2      = 0;
     FEstado        = 0;
     nSubBoton1     = 0;
     nSubBoton2     = 0;

     {-1}sTree      = 0;
     sT_nID         = 0;
     sT_nOper       = 0;
     sT_aData       = "";

     {-1}aVent      = "";
         aVent1     = 0;
         aVent2     = 0;
         aVent3     = 0;
         aVent4     = 0;
         aVent5     = "";

     &eaRutaOrigen  = "";
     &eaRutaDestino = "";
     &eaFicheroTxt  = "";
     &eaFicheroTgz  = "";
     &eaFicheroTar  = "";
     &eaAlfa        = "";
     &eaUnidadBasico = "";
     &eaDSMAC        = "";
     &enCodigo       = 0;
|FIN;

|PROCESO Tipo12Fw36;  |TIPO 12;
|FPRC;

|PROCESO Tipo0C0Fw36;  |TIPO 0;
     |C_M #dsaw0036#0, 0, aAlfa;
     |SI aAlfa = "N";  |ACABA;  |FINSI;

     #dsam0119#0 = #dsaw0036#0;
     |LEE 000#dsam0119.=;
     |SI FSalida = 0;
         |MENAV "0000El c¢digo de programa ya existe.";
         |ERROR;
         |ACABA;
     |FINSI;
|FPRC;

|PROCESO Tipo0C2Fw36;  |TIPO 0;
     |C_M #dsaw0036#3, 1, "S";

     |SI #dsaw0036#2 = "1";   #dsaw0036#3 = "DOC";   |C_M #dsaw0036#3, 1, "N";   |FINSI;
     |SI #dsaw0036#2 = "2";   #dsaw0036#3 = "XLS";   |C_M #dsaw0036#3, 1, "N";   |FINSI;
     |SI #dsaw0036#2 = "3";   #dsaw0036#3 = "TXT";   |C_M #dsaw0036#3, 1, "N";   |FINSI;
     |SI #dsaw0036#2 = "4";
         |SI #dsaw0036#3 ! "TIF";  |Y #dsaw0036#3 ! "PDF";
             #dsaw0036#3 = "TIF";
         |FINSI;
     |FINSI;

     |ESTADO_CONTROL nSubBoton1, "ENABLE";
     |C_M #dsaw0036#7, 1, "S";
     |SI #dsaw0036#2 = "1";
         |C_M #dsaw0036#7, 1, "N";
         #dsaw0036#5 = "LLAMADA INTERNA DESDE PROGRAMA";
         #dsaw0036#7 = "S";
         |ESTADO_CONTROL nSubBoton1, "DISABLE";
     |FINSI;

     |SI #dsaw0036#7 = "S";
         |ESTADO_CONTROL nSubBoton1, "DISABLE";
     |FINSI;

     |C_M #dsaw0036#6, 1, "S";
     |ESTADO_CONTROL nSubBoton2, "ENABLE";

     |PINTA #dsaw0036#3;
     |PINTA #dsaw0036#5;
     |PINTA #dsaw0036#6;
|FPRC;

|PROCESO Tipo0C7Fw36;  |TIPO 0;
     |ESTADO_CONTROL nSubBoton1, "ENABLE";
     |SI #dsaw0036#7 = "S";
         |ESTADO_CONTROL nSubBoton1, "DISABLE";
     |FINSI;

     |PINTA #dsaw0036#5;
|FPRC;

|PROCESO Browse1;
     aRuta = "F" + #dsaw0036#5;
     |BROWSE aRuta;
     aAlfa = aRuta % 280;  |QBF aRuta;
     |SI aAlfa ! "";
         #dsaw0036#5 = aRuta % 280;
     |FINSI;
     |PINTA #dsaw0036#5;
     |ERROR;
|FPRC;

|PROCESO Browse2;
     aRuta = "F" + #dsaw0036#6;
     |BROWSE aRuta;
     aAlfa = aRuta % 280;  |QBF aRuta;
     |SI aAlfa ! "";
         #dsaw0036#6 = aRuta % 280;
     |FINSI;
     |PINTA #dsaw0036#6;
     |ERROR;
|FPRC;

|PROCESO NuevoPrograma;
     |DDEFECTO #dsaw0036;

     |LEE 000#dsam0119.u;
     |SI FSalida ! 0;  |DDEFECTO #dsam0119;  |FINSI;

     |SI #dsam0119#0 > 99;
         |PARA nCampo = 1;  |SI nCampo [ 100;  |HACIENDO nCampo = nCampo + 1;
               #dsam0119#0 = nCampo;
               |LEE 000#dsam0119.=;
               |SI FSalida ! 0;
                   |SAL;
               |FINSI;
         |SIGUE;
     |FINSI;

     #dsam0119#0 = #dsam0119#0 + 1;

     |SI #dsam0119#0 > 99;
         |MENAV "0000 Ya tenemos 99 programas creados, borre alguno que no use.";
         |ACABA;
     |FINSI;

     #dsaw0036#0 = #dsam0119#0;
     #dsaw0036#4 = aNombreIP;

     |VENTANA 0701, 2599, nPanta, 17, "Nuevo Programa";
     nSubVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PINPA #0#dsaw0036;
     nSubPanta = FSalida;

     |CONTROL_SIMPLE nSubPanta, "BOTON,{{136}}", 2092, 2095, Browse1;
     nSubBoton1 = FSalida;

     |CONTROL_SIMPLE nSubPanta, "BOTON,{{136}}", 2392, 2395, Browse2;
     nSubBoton2 = FSalida;

     |C_M #dsaw0036#0, 1, "S";

     |PINDA #0#dsaw0036;

     |ENDATOS #1#dsaw0036;
     FEstado = FSalida;
     |SI FSalida = 0;
         #dsam0119#0 = #dsaw0036#0;
         #dsam0119#1 = #dsaw0036#1;
         #dsam0119#2 = #dsaw0036#2;
         #dsam0119#3 = #dsaw0036#3;
         #dsam0119#4 = #dsaw0036#7;

         |GRABA 020#dsam0119.n;
         |LIBERA #dsam0119;

         enCodigo = #dsam0119#0;
         |HAZ CambiaAMac120;

         #dsam0120#0 = #dsam0119#0;
         #dsam0120#1 = eaDSMAC;
         |LEE 101#dsam0120.=;
         |SI FSalida ! 0;
             #dsam0120#0 = #dsam0119#0;
             #dsam0120#1 = eaDSMAC;
             |GRABA 020#dsam0120.b;
         |FINSI;
         #dsam0120#2 = #dsaw0036#4;
         #dsam0120#3 = #dsaw0036#5;
         #dsam0120#4 = #dsaw0036#6;
         |GRABA 020#dsam0120.a;
         |LIBERA #dsam0120;
     |FINSI;

     |FIN_CONTROL_WINDOWS nSubBoton1;
     |FIN_CONTROL_WINDOWS nSubBoton2;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nSubVent;

     |SI FEstado = 0;
         aBusca = #dsaw0036#0;
         |HAZ MontaArbol;
     |FINSI;
|FPRC;

|PROCESO ModificarPrograma;
     #dsam0119#0 = aTexto;
     |LEE 101#dsam0119.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |DDEFECTO #dsaw0036;

     #dsaw0036#0  = #dsam0119#0;
     #dsaw0036#1  = #dsam0119#1;
     #dsaw0036#2  = #dsam0119#2;
     #dsaw0036#3  = #dsam0119#3;
     #dsaw0036#7  = #dsam0119#4;

     enCodigo = #dsam0119#0;
     |HAZ CambiaAMac120;

     #dsam0120#0  = #dsam0119#0;
     #dsam0120#1  = eaDSMAC;
     |LEE 000#dsam0120.=;
     |SI FSalida ! 0;  |DDEFECTO #dsam0120;  |FINSI;

     #dsaw0036#4  = #dsam0120#2;
     #dsaw0036#5  = #dsam0120#3;
     #dsaw0036#6  = #dsam0120#4;

     |VENTANA 0701, 2599, nPanta, 17, "Modificar programa";
     nSubVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PINPA #0#dsaw0036;
     nSubPanta = FSalida;

     |CONTROL_SIMPLE nSubPanta, "BOTON,{{136}}", 2092, 2095, Browse1;
     nSubBoton1 = FSalida;

     |CONTROL_SIMPLE nSubPanta, "BOTON,{{136}}", 2392, 2395, Browse2;
     nSubBoton2 = FSalida;

     |HAZ Tipo0C2Fw36;

     |C_M #dsaw0036#0, 1, "N";

     |PINDA #0#dsaw0036;
     |ENDATOS #1#dsaw0036;
     FEstado = FSalida;
     |SI FSalida = 0;
         #dsam0119#1 = #dsaw0036#1;
         #dsam0119#2 = #dsaw0036#2;
         #dsam0119#3 = #dsaw0036#3;
         #dsam0119#4 = #dsaw0036#7;

         |GRABA 020#dsam0119.a;
         |LIBERA #dsam0119;

         enCodigo = #dsam0119#0;
         |HAZ CambiaAMac120;
         #dsam0120#0 = #dsam0119#0;
         #dsam0120#1  = eaDSMAC;
         |LEE 101#dsam0120.=;
         |SI FSalida ! 0;
             #dsam0120#0 = #dsam0119#0;
             #dsam0120#1  = eaDSMAC;
             |GRABA 020#dsam0120.b;
         |FINSI;
         #dsam0120#2 = #dsaw0036#4;
         #dsam0120#3 = #dsaw0036#5;
         #dsam0120#4 = #dsaw0036#6;
         |GRABA 020#dsam0120.a;
         |LIBERA #dsam0120;
     |FINSI;

     |FIN_CONTROL_WINDOWS nSubBoton1;
     |FIN_CONTROL_WINDOWS nSubBoton2;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nSubVent;

     |SI FEstado = 0;
         aBusca = #dsaw0036#0;
         |HAZ MontaArbol;
     |FINSI;
|FPRC;

|PROCESO dsam0120B;
     |BORRA 020#dsam0120.a;
     |LIBERA #dsam0120;
|FPRC;

|DEFBUCLE dsam0120B;
     #dsam0120#1;
     ;
     #dsam0119#0, "";
     #dsam0119#0, "zzzzzzzzzzzzzzzzzzzz";
     be;
     NULL, dsam0120B;
|FIN;

|PROCESO BorrarPrograma;
     #dsam0119#0 = aTexto;
     |LEE 101#dsam0119.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |MENAV "0000Est  usted a punto de borrar un programa, puede perder la presentaci¢n de ciertos documentos.";
     |CONFI "0000NEst  seguro";
     |SI FSalida ! 0;
         |LIBERA #dsam0119;
         |ACABA;
     |FINSI;

     |BUCLE dsam0120B;

     |BORRA 020#dsam0119.a;
     |LIBERA #dsam0119;

     |LEE 000#dsam0119.p;
     aBusca = #dsam0119#0;
     |HAZ MontaArbol;
|FPRC;

|PROCESO EventoTree;
     |SI FEntrada ! 0;  |ACABA;  |FINSI;

     aAlfa       = Contenido;  |QBF aAlfa;
     aTexto      = "";
     aLong       = aAlfa % 0;
     nLong       = aLong;
     nCargar     = 0;
     |PARA nPosi = 1;  |SI nPosi [ nLong;  |HACIENDO nPosi = nPosi + 1;
           nPos = (nPosi * 100) + 1;
           aCarac = aAlfa % nPos;

           |SI nCargar = 1;
               aTexto = aTexto + aCarac;
           |FINSI;

           |SI aCarac = ":";  nCargar = 1; |FINSI;
     |SIGUE;

     |ESTADO_CONTROL nBoton2, "ENABLE";
     |ESTADO_CONTROL nBoton3, "ENABLE";

     aAlfa = aTexto % 102;
     |SI aTexto = "";
         |ESTADO_CONTROL nBoton2, "DISABLE";
         |ESTADO_CONTROL nBoton3, "DISABLE";
     |FINSI;
|FPRC;

|PROCESO CargaArbol;
     |QBF aAlfa;
     aAlfa = aAlfa + &13 + &10;
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO dsam0120;
     aAlfa     = #dsam0120#1 - ".";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     nCarpeta2 = nCarpeta2  + 1;
     aAlfa1    = #dsam0120#1;  |QBF aAlfa1;
     aAlfa2    = #dsam0120#2;  |QBF aAlfa2;

     |SI aAlfa1 = eaDSMAC;
         aAlfa1 = "---> " + aAlfa1;
     |FINSI;

     aAlfa = nCarpeta1 + "," + nCarpeta2 + ":" + aAlfa1 + " - " + aAlfa2;
     |HAZ CargaArbol;
|FPRC;

|DEFBUCLE dsam0120;
     #dsam0120#1;
     ;
     #dsam0119#0, "";
     #dsam0119#0, "zzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, dsam0120;
|FIN;

|PROCESO dsam0119;
     nCarpeta1 = nCarpeta1  + 1;

     aAlfa = nCarpeta1 + ":" + (("00" + #dsam0119#0) % -102) + " - " + #dsam0119#1;  |QBF aAlfa;
     aAlfa = aAlfa + " {{" + #dsam0119#0 + "}}";
     |HAZ CargaArbol;

     nCarpeta2 = 0;
     |BUCLE dsam0120;
|FPRC;

|DEFBUCLE dsam0119;
     #dsam0119#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dsam0119;
|FIN;

|PROCESO MontaArbol;
     |SI nTree ! -1;
         |FIN_CONTROL_WINDOWS nTree;
         nTree = -1;
     |FINSI;

     |CONTROL_SIMPLE 0, "LABEL,{{5}}Cargando informaci¢n ...", 1315, 1680, NULL;
     nBotonLabel = FSalida;

     |PTEC 802;  |PAUSA;

     |DBASE aBase;  |QBF aBase;
     aAlfa = aBase + "basura";          |MKDIR aAlfa;

     eaRutaOrigen  = aBase + "basura/";
     eaRutaDestino = eaUnidadBasico + "\documentos";           |RMKDIR eaRutaDestino;
     eaRutaDestino = eaUnidadBasico + "\documentos\tmp";       |RMKDIR eaRutaDestino;
     eaRutaDestino = eaUnidadBasico + "\documentos\tmp\";
     eaFicheroTxt  = "dsaz002001" + Usuario + ".txt";
     eaFicheroTgz  = "dsaz002001" + Usuario + ".tgz";
     eaFicheroTar  = "dsaz002001" + Usuario + ".tar";

     aAbre = eaRutaOrigen + eaFicheroTxt;

     sT_nID   = nTree;
     sT_nOper = 0;

     |XABRE "WB", aAbre, nHandle;

     nCarpeta1 = 0;

     |BUCLE dsam0119;

     |XCIERRA nHandle;

     |HAZ TraeTxtComprimido;
     aAlfa = eaRutaDestino + eaFicheroTxt;

     eaAlfa = "TREECTRL,Programas Externos {{" + aAlfa + "}}";
     |CONTROL_SIMPLE nPanta, eaAlfa, 0702, 2598, EventoTree;
     nTree = FSalida;

     eaFicheroTxt  = "dsaz002001" + Usuario + ".txt";
     aAlfa         = eaRutaDestino + eaFicheroTxt;
     |RFBORRA aAlfa;

     |FIN_CONTROL_WINDOWS nBotonLabel;

     |SI aBusca ! "";
         sT_nID   = nTree;
         sT_nOper = 3;
         sT_aData = "[" + aBusca;
         |ESPECIFICA "COMMANDCONTROL", sTree;
         aRefresca = ":" + FSalida;
     |FINSI;

     sT_nID   = nTree;
     sT_nOper = 2;
     |SI aRefresca = "";
         sT_aData = "1:";
     |SINO;
         sT_aData = aRefresca;
     |FINSI;

     |ESPECIFICA "COMMANDCONTROL", sTree;
     |PULSATECLA;

     |FOCOTECLADO nTree;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     FSalida = 2799;
|FPRC;

|PROCESO dsam0119Chequeo;
     enCodigo = #dsam0119#0;
     |HAZ CambiaAMac120;

     #dsam0120#0 = #dsam0119#0;
     #dsam0120#1 = eaDSMAC;
     |LEE 101#dsam0120.=;
     |SI FSalida ! 0;
         |DDEFECTO #dsam0120;
         #dsam0120#0 = #dsam0119#0;
         #dsam0120#1 = eaDSMAC;
         #dsam0120#2 = aNombreIP;
         |SI #dsam0119#2 = 1;  #dsam0120#3 = "LLAMADA INTERNA DESDE PROGRAMA";  |FINSI;
         |GRABA 020#dsam0120.n;
     |SINO;
         #dsam0120#2 = aNombreIP;
         |GRABA 020#dsam0120.a;
     |FINSI;
     |LIBERA #dsam0120;
|FPRC;

|DEFBUCLE dsam0119Chequeo;
     #dsam0119#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dsam0119Chequeo;
|FIN;

|PROGRAMA;
     |HAZ LeeUnidadBasico;
     |HAZ CogeMac;

     |ABRET #dsam0119;
     |ABRET #dsam0120;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;   |QBT aIPRemoto;
     |SI aIPRemoto = "";
         aIPRemoto = "LOCAL";
         aNombreIP = "MAQUINA LOCAL";
     |SINO;
         |NOMBRE_IP aIPRemoto, aNombreIP;
     |FINSI;

     |BUCLE dsam0119Chequeo;

     |CLS;
     |CABEZA "Programa Externos";

     |PINPA #0#dsaz0020;
     nPanta = FSalida;

     aAlfa = "LABEL,{{0}} Configuraci¢n de la MAC " + eaDSMAC;
     |CONTROL_SIMPLE nPanta, aAlfa, 0650, 0698, NULL;

     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}Nuevo Programa", 2634, 2654, NuevoPrograma;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Modificar Programa", 2656, 2676, ModificarPrograma;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{199}}Borrar Programa", 2678, 2698, BorrarPrograma;
     nBoton3 = FSalida;

     |HAZ MontaArbol;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}Salir," + nTree;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nTree;
     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;

     |CIERRAT #dsam0119;
     |CIERRAT #dsam0120;
|FPRO;
