|FICHEROS;
    dsay0024;

    dsam0000;
    dsam0026;
    dsam0105;
    dsam0127;

    dsat0008;
    dsat0009;
|FIN;

|VARIABLES;
     aAlfa     = "";
     aFichero  = "";
     aParam    = "";
     aAlfa1    = "";
     aAlfa2    = "";
     nPara1    = 0;

     fFecha    = @;
     fDFecha   = @;
     fHFecha   = @;
     aDEstado  = "";
     aHEstado  = "";
     nDEjer    = 0;
     nHEjer    = 0;
     nDEmpresa = 0;
     nHEmpresa = 0;
     nDAgente  = 0;
     nHAgente  = 0;
     nDMes     = 0;
     nHMes     = 0;
     nEmpresa  = 0;
     nEjer     = 0;
     nMes      = 0;
     nAlgun    = 0;
     nHay      = 0;
     aBlancs   = "";
     aZetas    = "";

     nTipoInf  = 0;
     informe     = "";
     inform1     = "dsay0024";
     inform2     = "dsay1024";
     inform3     = "dsay2024";

     &efFecha1 = @;
     &efFecha2 = @;
     &eSwLinea = 0;

     &enMes      = 0;
     &eaMes      = "";
     &eaSubTit   = "";
     &eaRaya     = "";
     &eaRaya1    = "";
     &eaMail     = "";
     &eaFecha    = "";
     &eaFechaA   = "";
     &enPrint    = 0;
|FIN;

|INCLUYE i_varfun;

|CALCULO HTarjeta; |TIPO 0;
    |SI #dsay0024#4 < #dsay0024#3;
        |MENAV "0000Error. Hasta Empresa inferior al Desde";
        |ERROR;
    |FINSI;
|FCAL;

|CALCULO HCliente; |TIPO 0;
    |SI #dsay0024#6 < #dsay0024#5;
        |MENAV "0000Error. Hasta Cliente inferior al Desde";
        |ERROR;
    |FINSI;
|FCAL;

|CALCULO HAgente; |TIPO 0;
    |SI #dsay0024#8 < #dsay0024#7;
        |MENAV "0000Error. Hasta Agente inferior al Desde";
        |ERROR;
    |FINSI;
|FCAL;

|| *******************************************************************
|PROCESO CampoFecha;
   eaFecha = "";
   eaFechaA = "";
   |SI #dsat0009#9 > "01.01.0000";
       eaFechaA = #dsat0009#9;
   |FINSI;
   |SI #dsat0009#8 = "B"; |Y #dsat0009#10 > "01.01.0000";
       eaFecha = #dsat0009#10;
   |FINSI;
|FPRC;

|PROCESO LeoDsam127;
  |DDEFECTO #dsam0127;
  #dsam0127#0 = eaTTarTrans;
  |LEE 041#dsam0127.=;
|FPRC;

|PROCESO LeeDsat0009FM;
   |HAZ CampoFecha;

   |DDEFECTO #dsat0008;
   #dsat0008#0  = #dsat0009#0;
   #dsat0008#20 = 01;
   #dsat0008#21 = 2000;
   |LEE 041#dsat0008.=;

   |SI #dsay0024#10 = "F";
       |SI enEjer ! #dsat0009#6; |O enMes ! #dsat0009#5;
           |SI nHay = 1;
               eSwLinea = 2; |PRINT;
           |FINSI;
           enEjer = #dsat0009#6;
           enMes  = #dsat0009#5;
           eaMes  = ("00" + enMes) % -102;
           eSwLinea = 0;
           enPrint  = 0;
       |FINSI;
   |FINSI;
   |SI #dsay0024#10 = "T";
       |SI eaTTarTrans ! #dsat0009#3;
           |SI nHay = 1;
               eSwLinea = 2; |PRINT;
           |FINSI;
           eaTTarTrans = #dsat0009#3;
           eSwLinea = 0;
           enPrint  = 0;
           |HAZ LeoDsam127;
       |FINSI;
   |FINSI;
   enPrint = 0;
   |SI enEmpresa = #dsat0009#0; enPrint = 1; |FINSI;

   |PRINT;
   eSwLinea  = 1;
   nHay      = 1;
   enEmpresa = #dsat0009#0;
|FPRC;

|DEFBUCLE LeeDsat0009_T;
   #dsat0009#4;
   ;
   #dsay0024#3, nDEmpresa, "          ";
   #dsay0024#4, nHEmpresa, "zzzzzzzzzz";
   e;
   NULL, LeeDsat0009FM;
|FIN;

|DEFBUCLE LeeDsat0009_F;
   #dsat0009#1;
   ;
   nDEjer, 01, nDEmpresa, "          ";
   nHEjer, 12, nHEmpresa, "zzzzzzzzzz";
   e;
   NULL, LeeDsat0009FM;
|FIN;

|PROCESO LeeDsat0009;
   |HAZ CampoFecha;
   |PRINT;
   eSwLinea = 1;
   nHay     = 1;
|FPRC;

|DEFBUCLE LeeDsat0009_C;
   #dsat0009#3;
   ;
   #dsat0008#0, "          ", 2000, 00;
   #dsat0008#0, "zzzzzzzzzz", 2999, 99;
   e;
   NULL, LeeDsat0009;
|FIN;

|PROCESO ImpDsat0008;
   eaMail = #dsat0008#5; |QBF eaMail;
   eSwLinea = 0;
   |BUCLE LeeDsat0009_C;
   |SI eSwLinea = 1;
       eSwLinea = 2;
       |PRINT;
   |FINSI;
|FPRC;

|DEFBUCLE LeeDsat0008_C;
   #dsat0008#1;
   ;
   2000, 00, nDEmpresa;
   2999, 99, nHEmpresa;
   ;
   NULL, ImpDsat0008;
|FIN;

|DEFBUCLE LeeDsat0008_N;
   #dsat0008#3;
   ;
   aBlancs, nDEmpresa;
   aZetas,  nHEmpresa;
   ;
   NULL, ImpDsat0008;
|FIN;

|PROCESO ImprimirDatos;
    aBlancs = " " * 40;
    aZetas  = "z" * 40;
    informe = inform1;
    |SI #dsay0024#10 = "F"; informe = inform2; |FINSI;
    |SI #dsay0024#10 = "T"; informe = inform3; |FINSI;

    |SI #dsay0024#9 ! "A";
        eaSubTit = "Fecha Baja  Causa de la Baja             ";
        eaRaya   = "-----------------------------------------";
        eaRaya1  = "=========================================";
    |FINSI;

    |IMPRE 0;
    |SI FSalida ! 0;
        |MENAV "0000Proceso Interrumpido";
        |ACABA;
    |FINSI;
    |INFOR informe;
    |SI FSalida ! 0;
        |MENAV "0000No existe informe. Proceso Interrumpido";
        |FINIMP;
        |ACABA;
    |FINSI;

    eaTTarTrans = "";
    enEmpresa   = 0;
    enEjer      = 0;
    enMes       = 0;
    nHay        = 0;
    eSwLinea    = 0;
    |ABRE #dsam0127;
    |SI #dsay0024#10 = "C"; |O #dsay0024#10 = "N";
        |SI #dsay0024#10 = "C"; |BUCLE LeeDsat0008_C; |FINSI;
        |SI #dsay0024#10 = "N"; |BUCLE LeeDsat0008_N; |FINSI;
    |SINO;
         |ABRE #dsat0008;
         |SI #dsay0024#10 = "F"; |BUCLE LeeDsat0009_F; |FINSI;
         |SI #dsay0024#10 = "T"; |BUCLE LeeDsat0009_T; |FINSI;
         |CIERRA #dsat0008;
    |FINSI;
    |CIERRA #dsam0127;
    |SI nHay ! 0;
        |PIEINF;
    |FINSI;
    |FININF;
    |FINIMP;
|FPRC;

|| *******************************************************************
|PROCESO GrabaAuxiliar8;
   #dsat0008#0  = nEmpresa;
   #dsat0008#20 = 01;
   #dsat0008#21 = 2000;
   |LEE 041#dsat0008.=;
   |SI FSalida ! 0;
       |DDEFECTO #dsat0008;
       #dsat0008#0  = nEmpresa;
       #dsat0008#1  = #dsam0000#1;
       #dsat0008#2  = #dsam0000#3;
       #dsat0008#3  = #dsam0000#54;
       #dsat0008#4  = #dsam0000#55;
       #dsat0008#5  = #dsam0000#71;
       #dsat0008#6  = #dsam0000#72;
       #dsat0008#7  = #dsam0000#52;
       #dsat0008#20 = 01;
       #dsat0008#21 = 2000;
       |GRABA 020#dsat0008.b;
       nAlgun = 1;
   |FINSI;
|FPRC;

|PROCESO LeeDatosM026;
    nHay = 1;
    #dsat0009#0 = nEmpresa;
    #dsat0009#1 = #dsam0026#1;
    #dsat0009#5 = #dsam0026#5;
    #dsat0009#6 = #dsam0026#6;
    |LEE 041#dsat0009.=;
    |SI FSalida ! 0;
        |DDEFECTO #dsat0009;
        #dsat0009#0 = nEmpresa;
        #dsat0009#1 = #dsam0026#1;
        |PARA nPara1 = 2; |SI nPara1 [ 11; |HACIENDO nPara1 = nPara1 + 1;
              #dsat0009#nPara1# = #dsam0026#nPara1#;
        |SIGUE;
        |GRABA 020#dsat0009.n;
    |FINSI;
|FPRC;

|DEFBUCLE dsam0026;
   #dsam0026#1;
   3,6,8;
   nEmpresa, "          ", #dsay0024#3, nDEjer, aDEstado;
   nEmpresa, "zzzzzzzzzz", #dsay0024#4, nHEjer, aHEstado;
   e;
   NULL, LeeDatosM026;
|FIN;

|PROCESO LeoDsam0000;
   nHay     = 0;
   nEmpresa = #dsam0000#0;

   |BUCLE dsam0026;
   |SI nHay = 1;
       |HAZ GrabaAuxiliar8;
   |FINSI;
|FPRC;

|DEFBUCLE dsam0000;
   #dsam0000#1;
   72;
   nDEmpresa, nDAgente;
   nHEmpresa, nHAgente;
   e;
   NULL, LeoDsam0000;
|FIN;

|PROCESO ProcesoY024;
     aFichero = ("4b" + Usuario) % 108;
     |NOME_DAT #dsat0008#aFichero#;
     |DELINDEX #dsat0008;
     aFichero = ("4c" + Usuario) % 108;
     |NOME_DAT #dsat0009#aFichero#;
     |DELINDEX #dsat0009;

     aAlfa1 = #dsay0024#0;
     aAlfa2 = "01.01." + aAlfa1; fDFecha = aAlfa2;
     aAlfa1 = #dsay0024#1;
     aAlfa2 = "31.12." + aAlfa1; fHFecha = aAlfa2;

     nDEmpresa = #dsay0024#5;
     nHEmpresa = #dsay0024#6;
     nDAgente  = #dsay0024#7;
     nHAgente  = #dsay0024#8;
     nDMes     = 01;
     nHMes     = 12;
     nDEjer    = #dsay0024#0;
     nHEjer    = #dsay0024#1;
     aDEstado  = #dsay0024#9;
     aHEstado  = #dsay0024#9;
     |SI #dsay0024#9 = "T";
         aDEstado  = " ";
         aHEstado  = "z";
     |FINSI;

     |ABRE #dsat0008;
     |ABRE #dsat0009;
     nAlgun = 0;
     |BUCLE dsam0000;
     |CIERRA #dsat0009;
     |CIERRA #dsat0008;

     |SI nAlgun = 1;
/*
        |ABRE #dsat0009;
        |CONSULTA_CLAVE #1#dsat0009;
        |CIERRA  #dsat0009;
*/
         |HAZ ImprimirDatos;
     |SINO;
         |MENAV "0000Seleccion vacia";
     |FINSI;

     |DELINDEX #dsat0008;
     |DELINDEX #dsat0009;
|FPRC;

|PROGRAMA;

    aParam = PARAMETRO; |QBF aParam;

    |SI aParam = "";
        |CLS;
        |CABEZA "Inf. de Tarjetas de Transportes";
    |FINSI;

    fFecha = ~;
    |ABRE #dsay0024;
    #dsay0024#12 = "A";
    |LEE 041#dsay0024.=;
    |SI FSalida ! 0;
        #dsay0024#12 = "A";
        aAlfa  = fFecha;
        aAlfa  = aAlfa % -104;
        #dsay0024#0  = aAlfa;
        #dsay0024#1  = aAlfa;
        |GRABA 020#dsay0024.b;
    |FINSI;

    #dsay0024#13 = fFecha;
    |SI aParam = "";
        |PINPA #0#dsay0024;
        |PINDA #0#dsay0024;
        |ENDATOS #2#dsay0024;
        |SI FSalida = 0;
            |GRABA 020#dsay0024.a;
            |LIBERA #dsay0024;
            |CIERRA #dsay0024;
            |HAZ ProcesoY024;
        |SINO;
            |LIBERA #dsay0024;
            |CIERRA #dsay0024;
        |FINSI;
    |SINO;
        |LIBERA #dsay0024;
        |CIERRA #dsay0024;
        |DDEFECTO #dsay0024;
        #dsay0024#0 = 0000;
        #dsay0024#1 = 2099;
        #dsay0024#5 = enEmpresa;
        #dsay0024#6 = enEmpresa;
        #dsay0024#9 = aParam;
        |HAZ ProcesoY024;
    |FINSI;
|FPRO;
