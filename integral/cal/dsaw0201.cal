|FICHEROS;
     dsaw0201 #201;
|FIN;

|VARIABLES;
     nCampo1      = 0;

     aAlfa        = "";
     aAlfa1       = "";
     aAlfa2       = "";
     aAlfa3       = "";
     aComa        = "";
     aConte       = "";

     fHoy         = @;
     nDia1        = 0;
     nDia2        = 0;
     nMes1        = 0;
     nMes2        = 0;
     nAny1        = 0;
     nAny2        = 0;
     nEdad        = 0;
|FIN;

|PROCESO Tipo12Fw201;  |TIPO 12;
     |HAZ Tipo7C5Fw201;

     |SI #201#18 = "N";
         aAlfa = #201#20;
     |SINO;
         aAlfa = #201#35;
     |FINSI;

     |QBF aAlfa;

     |SI aAlfa = "";
         |MENAV "0000 Introduzca datos del domicilio habitual";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO T0C10F201;  |TIPO 0;
     |SI aConte ! #dsaw0201#10;
         aAlfa = "0000Ha cambiado el valor de este campo, ";
         aAlfa = aAlfa + "revise la vinculaci¢n de los descencientes. ";
         |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO T7C10F201;  |TIPO 7;
     aConte = #dsaw0201#10;
|FPRC;

|PROCESO Tipo7C5Fw201;  |TIPO 7;
     aAlfa  = "";
     aAlfa1 = #201#2;  |QBF aAlfa1;
     aAlfa2 = #201#3;  |QBF aAlfa2;
     aAlfa3 = #201#4;  |QBF aAlfa3;

     aAlfa  = aAlfa1;
     |SI aAlfa2 ! "";
         |SI aAlfa1 ! "";
             aAlfa = aAlfa + " " + aAlfa2;
         |SINO;
             aAlfa = aAlfa2;
         |FINSI;
     |FINSI;

     aComa = "";
     |SI aAlfa1 ! "";  |O aAlfa2 ! "";
         |SI aAlfa3 ! "";
             aComa = ", ";
         |FINSI;
     |FINSI;

     aAlfa  = aAlfa + aComa + aAlfa3;
     #201#1 = aAlfa;
     |PINTA #201#1;
|FPRC;

|PROCESO Tipo0C6Fw201;
     |C_M #201#10, 1, "S";
     |SI #201#6 ! "L"; |Y #201#6 ! "S";
         #201#10 = " ";  |PINTA #201#10;
         |C_M #201#10, 1, "N";
     |FINSI;
|FPRC;

|PROCESO PonSiglo20_201;
     aAlfa1    = (#201Campo % 106) + "19" + (#201Campo % -102);
     #201Campo = aAlfa1;
     |PINTA #201Campo;
|FPRC;

|PROCESO CumpleAnyos_201;
     aAlfa1 = fHoy;       aAlfa1 = aAlfa1 % -104; nAny1 = aAlfa1;
     aAlfa1 = #201Campo;  aAlfa1 = aAlfa1 % -104; nAny2 = aAlfa1;
     nEdad  = nAny1 - nAny2;

     |SI nEdad ! 18; |ACABA; |FINSI;

     aAlfa1 = fHoy;       aAlfa1 = aAlfa1 % 402; nMes1 = aAlfa1;
     aAlfa1 = #201Campo;  aAlfa1 = aAlfa1 % 402; nMes2 = aAlfa1;

     |SI nMes2 > nMes1;  nEdad = nEdad - 1; |FINSI;
     |SI nMes1 ! nMes2;  |ACABA;            |FINSI;

     aAlfa1 = fHoy;       aAlfa1 = aAlfa1 % 102; nDia1 = aAlfa1;
     aAlfa1 = #201Campo;  aAlfa1 = aAlfa1 % 102; nDia2 = aAlfa1;

     |SI nDia2 > nDia1;  nEdad = nEdad - 1; |FINSI;
|FPRC;

|PROCESO Tipo0C7Fw201;
     |SI #201Campo = "01.01.0000";  |ACABA;  |FINSI;

     fHoy = ~;

     |SI #201Campo > fHoy;    || nacimientos futuros como que no
          |HAZ PonSiglo20_201;    ||/se cambia sin preguntar
     |FINSI;

     |SI #201Campo ] "01.01.2000"; |Y #201Campo [ fHoy;     || presuntos ni¤os,
                                                            ||/se pregunta
         |HAZ CumpleAnyos_201;
         |SI nEdad < 18;
             aAlfa = "0000SLa fecha puede ser incorrecta. Ha introducido un contribuyente menor de edad. Corregir fecha?";
             |CONFI aAlfa;
             |SI FSalida = 0;
                 |ERROR;
                 || |HAZ PonSiglo20;
             |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo0C8Fw201;
     nCampo1 = Campo + 1;
     |C_M #201nCampo1, 1, "S";
     |SI #201Campo = 0;
         |C_M #201nCampo1, 1, "N";
         #201nCampo1 = "N";  |PINTA #201nCampo1;
     |FINSI;
|FPRC;
