7|FICHEROS;
    dsay0025;

    dsam0000;
    dsam0026;
    dsam0027;
    dsam0028;

    dsat0010;
|FIN;

|VARIABLES;
     aAlfa     = "";
     aFichero  = "";
     aParam    = "";
     aAlfa1    = "";
     aAlfa2    = "";
     aAlfa3    = "";
     nNume1    = 0;
     nPara1    = 0;
     nOk       = 0;
     aFecha    = "";
     aDFecha   = "";
     aHFecha   = "";
     nDia      = 0;
     &nMes      = 0;
     nAny      = 0;
     nCampo    = 0;
     nCamp1    = 0;

     nSw       = 0;
     fFecha    = @;
     fDFecha   = @;
     fHFecha   = @;
     aDEstado  = "";
     aHEstado  = "";
     nDEjer    = 0;
     nHEjer    = 0;
     nDEmpresa = 0;
     nHEmpresa = 0;
     nDExpedie = 0;
     nHExpedie = 0;
     nDMes     = 0;
     nHMes     = 0;
     nEmpresa  = 0;
     &nEjer     = 0;
     nAlgun    = 0;
     nHay      = 0;
     aBlancs   = "";
     aZetas    = "";
     aEmpresa  = "";

     nTipoInf  = 0;
     informe     = "";
     inform1     = "dsay0025";
     inform2     = "dsay1025";
     inform3     = "dsay2025";

     &efFecha1 = @;
     &efFecha2 = @;
     &eSwLinea = 0;

     &enMes      = 0;
     &eaMes      = "";
     &eaSubTit   = "";
     &eaRaya     = "";
     &eaRaya1    = "";
     &eaMail     = "";
     &eaFecha    = "";
     &eaFechaA   = "";
     &enPrint    = 0;
     &eaCliente  = "";
     &eaNom      = "";
     &eaNif      = "";
     &eaTel      = "";
     &eaFec1     = "";
     &eaFec2     = "";
     &eaFec3     = "";
     &eaFec4     = "";
     &eaFec5     = "";
     &eaFec6     = "";
     &enTotal    = 0;
|FIN;

|INCLUYE i_varfun;

|CALCULO HCliente; |TIPO 0;
    |SI #dsay0025#6 < #dsay0025#5;
        |MENAV "0000Error. Hasta Cliente inferior al Desde";
        |ERROR;
    |FINSI;
|FCAL;

|CALCULO HExpe; |TIPO 0;
    |SI #dsay0025#8 < #dsay0025#7;
        |MENAV "0000Error. Hasta Expediente inferior al Desde";
        |ERROR;
    |FINSI;
|FCAL;

|CALCULO LasFechas; |TIPO 0;
    nCampo = Campo + 1;
    aAlfa = #dsay0025#Campo#;
    |SI aAlfa = "N";
        #dsay0025#nCampo# = "";
        |PINTA #dsay0025#nCampo#;
    |FINSI;
    |C_M #dsay0025#nCampo#,1, aAlfa;
|FCAL;

|CALCULO ForFecha; |TIPO 0;
    |C_M #dsay0025#Campo#,0, aAlfa;
    |SI aAlfa = "N"; |ACABA; |FINSI;

    |SI #dsay0025#Campo# = "01.01.0000"; |O #dsay0025#Campo# = "          ";
        |ACABA;
    |FINSI;

    aFecha = #dsay0025#Campo#;
    |HAZ FechaCorrecta;
    |SI nOk = 1;
        |MENAV "0000Formato de Fecha incorrecta";
        |ERROR;
    |FINSI;
|FCAL;

|PROCESO FechaCorrecta;
    nOk = 0;
    fDFecha = "01.01.0000";
    fHFecha = "31.12.2999";

    aAlfa1 = aFecha % 0;
    nNume1 = aAlfa1;
    |SI nNume1 ! 10;  nOk = 1; |ACABA;  |FINSI;

    aAlfa1 = aFecha % 301;
    aAlfa2 = aFecha % 601;
    |SI aAlfa1 ! "."; |O aAlfa2 ! ".";
        nOk = 1;
        |ACABA;
    |FINSI;

    aAlfa1 = aFecha % 102; nDia = aAlfa1;
    aAlfa2 = aFecha % 402; nMes = aAlfa2;
    aAlfa3 = aFecha % 704; nAny = aAlfa3;
    |SI nAny < 2000; |O nAny  > 2099;
        nOk = 1;
        |ACABA;
    |FINSI;

    |SI aAlfa2 = "XX"; |Y aAlfa1 ! "XX";
        nOk = 1;
        |ACABA;
    |FINSI;
    |SI aAlfa2 ! "XX";
        |SI nMes < 1; |O nMes > 12;
            nOk = 1;
            |ACABA;
        |FINSI;
    |FINSI;

    |SI aAlfa1 ! "XX";
        |SI nDia <  1; nOk = 1; |ACABA; |FINSI;
        |SI nDia > 31; nOk = 1; |ACABA; |FINSI;
        |SI nDia = 31;
            |SI nMes = 2; |O nMes = 4; |Y nMes = 6;
               |O nMes = 9; |O nMes = 11;
                  nOk = 1; |ACABA;
            |FINSI;
        |FINSI;
        |SI nDia = 30; |Y nMes = 2;
            nOk = 1; |ACABA;
        |FINSI;
        |SI nDia = 29; |Y nMes = 2;
            |SI nAny ! 2000; |Y nAny ! 2004; |Y nAny ! 2008;
              |Y nAny ! 2012; |Y nAny ! 2016; |Y nAny ! 2020;
                |Y nAny ! 2024; |Y nAny ! 2028; |Y nAny ! 2032;
                 nOk = 1;
                 |ACABA;
             |FINSI;
        |FINSI;
    |FINSI;

    aDFecha = "." + nAny; aHFecha = "." + nAny;
    |SI aAlfa2 = "XX";
        aDFecha = ".01" + aDFecha;
        aHFecha = ".12" + aHFecha;
    |SINO;
        aDFecha = "." + (("00" + nMes) % -102) + aDFecha;
        aHFecha = "." + (("00" + nMes) % -102) + aHFecha;
    |FINSI;

    |SI aAlfa1 ! "XX";
        aDFecha = (("00" + nDia) % -102) + aDFecha;
        aHFecha = (("00" + nDia) % -102) + aHFecha;
    |SINO;
        aDFecha = "01" + aDFecha;
        |SI nMes = 0; |O nMes = 1; |O nMes = 3; |O nMes = 5; |O nMes = 7; |O nMes = 8; |O nMes = 10; |O nMes = 12;
            aHFecha = "31" + aHFecha;
        |SINO;
            |SI nMes ! 2;
                aHFecha = "30" + aHFecha;
            |SINO;
                |SI nAny = 2000; |O nAny = 2004; |O nAny = 2008; |O nAny = 2012;
                   |O nAny = 2016; |O nAny = 2020; |O nAny = 2024; |O nAny = 2028;
                     aHFecha = "29" + aHFecha;
                |SINO;
                    aHFecha = "28" + aHFecha;
                |FINSI;
             |FINSI;
         |FINSI;
     |FINSI;
     fDFecha = aDFecha;
     fHFecha = aHFecha;
|FPRC;

|PROCESO LaFecha;
   nOk = 0;
   |SI #dsam0027#nCamp1# [ "01.01.0000";
       nOk = 1;
       |ACABA;
   |FINSI;
   |SI #dsay0025#nCampo# = "01.01.0000"; |O #dsay0025#nCampo# = "          ";
       nOk = 0;
      |ACABA;
   |FINSI;

   aFecha = #dsay0025#nCampo#;
   |HAZ FechaCorrecta;
   |SI nOk = 0;
       |SI #dsam0027#nCamp1# < fDFecha; |O #dsam0027#nCamp1# > fHFecha;
           nOk = 1;
       |FINSI;
   |FINSI;
|FPRC;

|CALCULO Resumen; TIPO 7;
   |C_M #dsay0025#21,1,"S";
   |SI #dsay0025#10 = "E";
       |C_M #dsay0025#21,1,"N";
       #dsay0025#21 = "D";
       |PINTA #dsay0025#21;
   |FINSI;
|FCAL;
|| *******************************************************************
|PROCESO elMes;
    |SI nMes = 1;  eaMes = "Enero     "; |FINSI;
    |SI nMes = 2;  eaMes = "Febrero   "; |FINSI;
    |SI nMes = 3;  eaMes = "Marzo     "; |FINSI;
    |SI nMes = 4;  eaMes = "Abril     "; |FINSI;
    |SI nMes = 5;  eaMes = "Mayo      "; |FINSI;
    |SI nMes = 6;  eaMes = "Junio     "; |FINSI;
    |SI nMes = 7;  eaMes = "Julio     "; |FINSI;
    |SI nMes = 8;  eaMes = "Agosto    "; |FINSI;
    |SI nMes = 9;  eaMes = "Septiembre"; |FINSI;
    |SI nMes = 10; eaMes = "Octubre   "; |FINSI;
    |SI nMes = 11; eaMes = "Noviembre "; |FINSI;
    |SI nMes = 12; eaMes = "Diciembre "; |FINSI;
|FPRC;

|| *******************************************************************
|PROCESO LeeDsam0028_C;
   |SI #dsay0025#21 = "R";
        enTotal = enTotal + 1;
   |SINO;
        |PRINT;
        eSwLinea = 1;
   |FINSI;
   nHay = 1;
|FPRC;

|DEFBUCLE LeeDsam0028_C;
   #dsam0028#1;
   ;
   #dsam0027#0, #dsam0027#1, "          ";
   #dsam0027#0, #dsam0027#1, "zzzzzzzzzz";
   e;
   NULL, LeeDsam0028_C;
|FIN;

|PROCESO Impdsat0010;
   #dsam0027#0 = eaFun07;
   #dsam0027#1 = #dsat0010#0;
   |LEE 041#dsam0027.=;
   |SI FSalida ! 0;
       |ACABA;
   |FINSI;

   nMes = #dsam0027#3; |HAZ elMes;
   eaFec1 = ""; |SI #dsam0027#9  > "01.01.0000"; eaFec1 = #dsam0027#9;  |FINSI;
   eaFec2 = ""; |SI #dsam0027#10 > "01.01.0000"; eaFec2 = #dsam0027#10; |FINSI;
   eaFec3 = ""; |SI #dsam0027#11 > "01.01.0000"; eaFec3 = #dsam0027#11; |FINSI;
   eaFec4 = ""; |SI #dsam0027#12 > "01.01.0000"; eaFec4 = #dsam0027#12; |FINSI;
   eaFec5 = ""; |SI #dsam0027#8  > "01.01.0000"; eaFec5 = #dsam0027#8;  |FINSI;
   eaFec6 = ""; |SI #dsam0027#27 > "01.01.0000"; eaFec6 = #dsam0027#27; |FINSI;

   eSwLinea = 0;
   |SI nSw = 0;
       nEmpresa = #dsat0010#3;
       aEmpresa = #dsat0010#4;
       nEjer    = #dsat0010#2;
       nMes     = #dsat0010#1;
       nSw      = 1;
       |SI #dsay0025#10 ! "E"; eSwLinea = 3; |FINSI;
   |FINSI;

   |SI #dsay0025#10 = "C"; |Y nEmpresa ! #dsat0010#3;
       eSwLinea = 3
       nEmpresa = #dsat0010#3;
   |FINSI;
   |SI #dsay0025#10 = "N"; |Y aEmpresa ! #dsat0010#4;
       eSwLinea = 3
       aEmpresa = #dsat0010#4;
   |FINSI;
   |SI #dsay0025#10 = "F";
       |SI nEjer ! #dsat0010#2; |O nMes ! #dsat0010#1;
           eSwLinea = 3
           nEjer    = #dsat0010#2;
           nMes     = #dsat0010#1;
       |FINSI;
   |FINSI;
   eaCliente = ("00000" + #dsam0027#5) % -105;
   eaNom     = #dsam0027#6;
   eaNif     = #dsam0027#7;
   eaTel     = #dsam0027#30;
   eaMail    = #dsam0027#31; |QBF eaMail;

   enTotal = 0;
   |BUCLE LeeDsam0028_C;
   |SI #dsay0025#21 = "R";
       |SI enTotal ! 0; |PRINT; |FINSI;
   |SINO;
       |SI eSwLinea = 1;
           |SI #dsay0025#10 ! "E";
               eSwLinea = 2;
               |PRINT;
           |SINO;
               |PIEINF;
           |FINSI;
       |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE Leedsat0010_C;
   #dsat0010#3;
   ;
   nDEmpresa, nDEjer, nDMes, nDExpedie;
   nHEmpresa, nHEjer, nHMes, nHExpedie;
   ;
   NULL, Impdsat0010;
|FIN;

|DEFBUCLE Leedsat0010_N;
   #dsat0010#4;
   ;
   aBlancs, nDEjer, nDMes, nDExpedie;
   aZetas,  nHEjer, nHMes, nHExpedie;
   ;
   NULL, Impdsat0010;
|FIN;

|DEFBUCLE LeeDsat0010_E;
   #dsat0010#1;
   ;
   nDExpedie;
   nHExpedie;
   e;
   NULL, Impdsat0010;
|FIN;

|DEFBUCLE LeeDsat0010_F;
   #dsat0010#2;
   ;
   nDEjer, nDMes, nDExpedie;
   nHEjer, nHMes, nHExpedie;
   e;
   NULL, Impdsat0010;
|FIN;

|PROCESO ImprimirDatos;
    aBlancs = " " * 40;
    aZetas  = "z" * 40;
    informe = inform1;
    |SI #dsay0025#10 = "F"; informe = inform2; |FINSI;
    |SI #dsay0025#10 = "E"; informe = inform3; |FINSI;
    |SI #dsay0025#21 = "R";
        informe = (informe % 105) + "r" + (informe % -102);
    |FINSI;

    |SI #dsay0025#9 ! "A";
        eaSubTit = "Fecha Baja  Causa de la Baja             ";
        eaRaya   = "-----------------------------------------";
        eaRaya1  = "=========================================";
    |FINSI;

    |IMPRE 0;
    |SI FSalida ! 0;
        |MENAV "0000Proceso Interrumpido";
        |ACABA;
    |FINSI;
    |INFOR informe;
    |SI FSalida ! 0;
        |MENAV "0000No existe informe. Proceso Interrumpido";
        |FINIMP;
        |ACABA;
    |FINSI;

    nSw         = 0;
    eaTTarTrans = "";
    nEmpresa    = 0;
    nEjer       = 0;
    nMes        = 0;
    nHay        = 0;
    eSwLinea    = 0;
    |ABRE #dsam0027;
    |SI #dsay0025#10 = "C"; |BUCLE Leedsat0010_C; |FINSI;
    |SI #dsay0025#10 = "N"; |BUCLE Leedsat0010_N; |FINSI;
    |SI #dsay0025#10 = "F"; |BUCLE LeeDsat0010_F; |FINSI;
    |SI #dsay0025#10 = "E"; |BUCLE LeeDsat0010_E; |FINSI;
    |CIERRA #dsam0027;
    |SI nHay ! 0; |Y #dsay0025#10 ! "E";
        |PIEINF;
    |FINSI;
    |FININF;
    |FINIMP;
|FPRC;

|| *******************************************************************
|PROCESO GrabaAuxiliar10;
   #dsat0010#0  = #dsam0027#1;
   |LEE 041#dsat0010.=;
   |SI FSalida ! 0;
       |DDEFECTO #dsat0010;
       #dsat0010#0  = #dsam0027#1;
       #dsat0010#1  = #dsam0027#3;
       #dsat0010#2  = #dsam0027#4;
       #dsat0010#3  = #dsam0027#5;
       #dsat0010#4  = #dsam0027#6;
       |GRABA 020#dsat0010.b;
   |FINSI;
    nAlgun = 1;
|FPRC;

|PROCESO LeoDsam0027;
   nHay     = 0;
   nEmpresa = #dsam0027#5;

   |SI #dsay0025#13 = "S";
       nCampo = 14; nCamp1 = 9; |HAZ LaFecha;
       |SI nOk = 1; |ACABA; |FINSI;
   |FINSI;
   |SI #dsay0025#15 = "S";
       nCampo = 16; nCamp1 = 10; |HAZ LaFecha;
       |SI nOk = 1; |ACABA; |FINSI;
   |FINSI;
   |SI #dsay0025#17 = "S";
       nCampo = 18; nCamp1 = 11; |HAZ LaFecha;
       |SI nOk = 1; |ACABA; |FINSI;
   |FINSI;
   |SI #dsay0025#19 = "S";
       nCampo = 20; nCamp1 = 12; |HAZ LaFecha;
       |SI nOk = 1; |ACABA; |FINSI;
   |FINSI;

   |HAZ GrabaAuxiliar10;
|FPRC;

|DEFBUCLE dsam0027;
   #dsam0027#1;
   5,4,3,24;
   eaFun07, nDExpedie, nDEmpresa, nDEjer, nDMes, aDEstado;
   eaFun07, nHExpedie, nHEmpresa, nHEjer, nHMes, aHEstado;
   e;
   NULL, LeoDsam0027;
|FIN;

|PROCESO ProcesoY025;

     aFichero = ("4b" + Usuario) % 108;
     |NOME_DAT #dsat0010#aFichero#;
     |DELINDEX #dsat0010;

     aAlfa1 = #dsay0025#0;
     aAlfa2 = "01.01." + aAlfa1; fDFecha = aAlfa2;
     aAlfa1 = #dsay0025#1;
     aAlfa2 = "31.12." + aAlfa1; fHFecha = aAlfa2;

     nDEmpresa = #dsay0025#5;
     nHEmpresa = #dsay0025#6;
     nDExpedie = #dsay0025#7;
     nHExpedie = #dsay0025#8;
     nDMes     = #dsay0025#2;
     nHMes     = #dsay0025#3;
     nDEjer    = #dsay0025#0;
     nHEjer    = #dsay0025#1;
     aDEstado  = #dsay0025#9;
     aHEstado  = #dsay0025#9;
     |SI #dsay0025#9 = "T";
         aDEstado  = " ";
         aHEstado  = "z";
     |FINSI;

     |ABRE #dsat0010;
     |ABRE #dsam0000;
     nAlgun = 0;
     |BUCLE dsam0027;
     |CIERRA #dsam0000;
     |CIERRA #dsat0010;

     |SI nAlgun = 1;
      ||   |ABRE #dsat0010;
      ||   |CONSULTA_CLAVE #1#dsat0010;
      ||   |CIERRA  #dsat0010;
         |HAZ ImprimirDatos;
     |SINO;
         |SI aParam = ""; |MENAV "0000Seleccion vacia"; |FINSI;
     |FINSI;

     |DELINDEX #dsat0010;
|FPRC;

|PROGRAMA;

    eaFun07 = "A";
    aParam = PARAMETRO; |QBF aParam;

    |SI aParam = "";
        |CLS;
        |CABEZA "Inf. de Tarjetas de Transportes";

        fFecha = ~;
        |ABRE #dsay0025;
        #dsay0025#12 = "A";
        |LEE 041#dsay0025.=;
        |SI FSalida ! 0;
            #dsay0025#12 = "A";
            aAlfa  = fFecha;
            aAlfa  = aAlfa % -104;
            #dsay0025#0 = aAlfa;
            #dsay0025#1 = aAlfa;
            |GRABA 020#dsay0025.b;
        |FINSI;
    |FINSI;

    #dsay0025#22 = fFecha;
    |SI aParam = "";
        |PINPA #0#dsay0025;
        |PINDA #0#dsay0025;
        |ENDATOS #2#dsay0025;
        |SI FSalida = 0;
            |GRABA 020#dsay0025.a;
            |LIBERA #dsay0025;
            |CIERRA #dsay0025;
            |HAZ ProcesoY025;
        |SINO;
            |LIBERA #dsay0025;
            |CIERRA #dsay0025;
        |FINSI;
    |SINO;
        |DDEFECTO #dsay0025;
        #dsay0025#0 = 2000;
        #dsay0025#1 = 2099;
        #dsay0025#7 = enNExpediente;
        #dsay0025#8 = enNExpediente;
        #dsay0025#21= "D";
        |HAZ ProcesoY025;
    |FINSI;
|FPRO;
