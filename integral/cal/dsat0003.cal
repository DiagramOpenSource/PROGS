|FICHEROS;
    dsam0000;
    dsam0023;
    dsam0024;
    dsam0105;
    dsam0109;
    dsam0110;

    dsaz0031;
    dsat0003,MANTE;
    dsat003@;
|FIN;

|VARIABLES;
     nBoton2    = -1;
     nBoton3    = -1;
     nBoton4    = -1;
     nBoton5    = -1;
     nBoton6    = -1;
     nBoton8    = -1;
     nNoGraba   = 0;
     nTecla     = 0;
     nImport1   = 0;
     nImport2   = 0;
     nImport3   = 0;
     nImport4   = 0;
     aAlfa      = "";
     aBase      = "";
     aDef       = "";
     informe    = "";
     inform1    = "dsat0003";
     inform11   = "dsat0b03";
     inform2    = "dsat1003";
     inform21   = "dsat1b03";
     nAlgun     = 0;
     nOp        = 0;
     nSwError   = 0;
     nPara1     = 0;
     &enEmpresa = 0;
     &enEmpPD   = 0;
     &enEjer    = 0;
     &aFuncion  = "";

     &eSwImp     = 0;
     &eaFichero  = "";
     &eaDLiqu    = "";
     &eaHLiqu    = "";
     &enDEmpresa = 0;
     &enHEmpresa = 0;
     &enDAgente  = 0;
     &enHAgente  = 0;
     &enDMes     = 0;
     &enHMes     = 0;

     &eaTitul1  = "";
     &eaTitul2  = "";
|FIN;

|| ///////////////////////////////
|PROCESO Tipo12; |TIPO 12;
  ||
|FPRC;

|PROCESO MTipo5;  |TIPO 5;
     |LEE 040#dsat0003.p;
     #dsat0003#9 = "N";
     |LINEAS_BI_ATRI #dsat0003#9, D, S, -1;
|FPRC;

|PROCESO PintaTotal;
   #dsaz0031#12 = #dsaz0031#12 - nImport1 + nImport3;
   #dsaz0031#13 = #dsaz0031#13 - nImport2 + nImport4;
   |PINTA #dsaz0031#12;
   |PINTA #dsaz0031#13;
|FPRC;

|PROCESO MarcaDatos;
   |LEE 141#dsat0003.=;

   nImport1 = 0; nImport2 = 0;
   |SI #dsat0003#9 = "S";
       nImport1 = #dsat0003#10;
       nImport2 = #dsat0003#14;
   |FINSI;

   |SI #dsat0003#9 = "S";
       #dsat0003#9 = "N";
   |SINO;
       #dsat0003#9 = "S";
   |FINSI;

   |PINTA #dsat0003#9;
   |GRABA 020#dsat0003.a;
   |LIBERA #dsat0003;

   nImport3 = 0; nImport4 = 0;
   |SI #dsat0003#9 = "S";
       nImport3 = #dsat0003#10;
       nImport4 = #dsat0003#14;
   |FINSI;

   |HAZ PintaTotal;
|FPRC;

|PROCESO InforDsat0003;
   |PARA nPara1 = 0; |SI nPara1 [ 15; |HACIENDO nPara1 = nPara1 + 1;
         #dsat0003#nPara1# = #dsat003@#nPara1#;
   |SIGUE;
   |PRINT;
   nAlgun = 1;
|FPRC;

|PROCESO LeeDsat0003;
   |SI nOp = 0;
       nImport1 = 0; nImport2 = 0;
       |SI #dsat003@#9 = "S";
           nImport1 = #dsat003@#10;
           nImport2 = #dsat003@#14;
       |FINSI;

       #dsat003@#9 = aAlfa;
       nImport3 = 0; nImport4 = 0;
       |SI #dsat003@#9 = "S";
           nImport3 = #dsat003@#10;
           nImport4 = #dsat003@#14;
       |FINSI;
       |GRABA 020#dsat003@.a;
       |HAZ PintaTotal;
   |FINSI;

   |SI nOp = 1;
      |SI #dsat003@#9 = "S"; |O eSwImp = 0;
          |HAZ InforDsat0003;
      |FINSI;
   |FINSI;
   |LIBERA #dsat003@;
|FPRC;

|DEFBUCLE LeeDsat0003;
   #dsat003@#1005;
   ;
   aFuncion, enEmpPD, enEjer, enDMes, enDEmpresa;
   aFuncion, enEmpPD, enEjer, enHMes, enHEmpresa;
   ;
   NULL, LeeDsat0003;
|FIN;
|| //////////////////////////////////////////////////////////////

|PROCESO LosInformes;
   |HAZ CargaFichero;
   |SI nSwError = 1;  |ACABA;  |FINSI;

   informe = inform1;
   |SI eSwImp = 1; informe = inform2; |FINSI;
   |SI aFuncion = "B";
       informe = inform11;
       |SI eSwImp = 1; informe = inform21; |FINSI;
   |FINSI;

   nAlgun  = 0;
   |IMPRE 0;
   |SI FSalida ! 0;
       |ACABA;
   |FINSI;
   eaTitul1 = "PROTECCION DE DATOS";
   eaTitul2 = "Proteccion Datos";
   |SI aFuncion = "B";
       eaTitul1 = "PREVENCION DE RIESGOS LABORALES";
       eaTitul2 = "Preven. Riesgos";
   |FINSI;

   |INFOR informe;
   |SI FSalida ! 0;
       |MENAV "0000No existe informe";
       |FINIMP;
       |ACABA;
   |FINSI;

   |SALVA_DATOS #dsat0003;
   nOp = 1; |BUCLE LeeDsat0003;
   |SI nAlgun = 1;
       |PIEINF;
   |FINSI;
   |FININF;
   |FINIMP;

   |HAZ DesCargaFichero;
   |REPON_DATOS #dsat0003;
|FPRC;

|PROCESO LosMarcados;
   |HAZ CargaFichero;
   |SI nSwError = 1;  |ACABA;  |FINSI;

   nOp = 0; |BUCLE LeeDsat0003;
   |HAZ DesCargaFichero;
|FPRC;

|PROCESO CargaFichero;
   |DBASE aBase;
   aDef = aBase + "def/dsat0003";
   |CARGA_DEF aDef, dsat003@;
   |SI FSalida ! 0;
       |MENAV "0000Error cargado def, proceso interrumpido";
       nSwError = 1;
   |SINO;
       |NOME_DAT #dsat003@#eaFichero#;
   |FINSI;
|FPRC;

|PROCESO DesCargaFichero;
   |DESCARGA_DEF #dsat003@;
|FPRC;

|| /////////////////////////////////////////////////////////
|PROCESO Boton2m001;
     |PTEC 824;
|FPRC;

|PROCESO Boton3m001;
     |PTEC 825;
|FPRC;

|PROCESO Boton5m001;
     |PTEC 827;
|FPRC;

|PROCESO Boton6m001;
     |PTEC 828;
|FPRC;

|PROCESO Boton8m001;
     |PTEC 830;
|FPRC;

|PROCESO FinBotont003;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;
     |FIN_CONTROL_WINDOWS nBoton5;
     |FIN_CONTROL_WINDOWS nBoton6;

     |FIN_CONTROL_WINDOWS nBoton8;
     |PULSATECLA;
|FPRC;

|PROCESO Tipo0t003;  |TIPO 0;
     |HAZ FinBotont003;
|FPRC;

|PROCESO Tipo7t003;  |TIPO 7;
     |BLIN 04;

     |CONTROL_SIMPLE 0, "LBOTON,F2 Liquidar/No Liquidar", 1405, 1421, Boton2m001;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F3 Informe", 1423, 1435, Boton3m001;
     nBoton3 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F5 Liquidar Todos", 1437, 1449, Boton5m001;
     nBoton5 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F6 No Liquidar Todos", 1451, 1465, Boton6m001;
     nBoton6 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F8 Dar de Bajar", 1467, 1479, Boton8m001;
     nBoton8 = FSalida;
|FPRC;

|PROCESO Tipo11t003;  |TIPO 11;
     nTecla = FSalida;

     |HAZ FinBotont003;

     enEmpresa     = #dsat0003#4;
     enEjer        = #dsat0003#2;

     |SI nTecla = 10;
         |HAZ MarcaDatos;
         |PONCONTROL  23, "SI";
         |ACABA;
     |FINSI;

     |SI nTecla = 11;
         eSwImp = 0;
         |HAZ LosInformes;
     |FINSI;

     |SI nTecla = 13; |O nTecla = 14;
         aAlfa = "S";
         |SI nTecla = 14; aAlfa = "N"; |FINSI;
         |HAZ LosMarcados;
         |PONCONTROL  23, "SI";
         |ACABA;
     |FINSI;

     |SI nTecla = 16;
         |CONFI "0000SDesea borrar el registro (No actualiza en Cliente)";
         |SI FSalida = 0;

             |LEE 141#dsat0003.=;
             |BORRA 020#dsat0003.a;
             |LIBERA #dsat0003;

             #dsaz0031#10 = #dsaz0031#10 - #dsat0003#10;
             #dsaz0031#11 = #dsaz0031#11 - #dsat0003#14;
             |SI #dsat0003#9 = "S";
                 #dsaz0031#12 = #dsaz0031#12 - #dsat0003#10;
                 #dsaz0031#13 = #dsaz0031#13 - #dsat0003#14;
             |FINSI;
             |PINTA #dsaz0031#10;
             |PINTA #dsaz0031#11;
             |PINTA #dsaz0031#12;
             |PINTA #dsaz0031#13;
         |FINSI;
         |PONCONTROL  23, "SI";
     |FINSI;
|FPRC;
