|FICHEROS;
     dsam0111 #111;
     dsam0112 #112;
     dsaw0022 #98,MANTE;
|FIN;

|VARIABLES;
     nCanal          = 0;
     nModo           = 0;
     nContador       = 0;
     nCampo          = 0;
     nImprimir       = 0;
     nSwAborto       = 0;
     nSwGraba        = 0;
     nDCodigo        = 0;
     nHCodigo        = 0;

     aAlfa           = "";
     aClave          = "";
     aClave1         = "";
     aClave2         = "";
     aClave3         = "";
     aRutaFichero    = "";
     aFicheroActu    = "";
     aRegilla        = "";
     aDirOrigen      = "";
     aDirDestino     = "";
     aOrigen         = "";
     aDestino        = "";
     aEjecuta        = "";
     aTemporal       = "";
     aPathPlan       = "";
     aCadena1        = "";
     aCadena2        = "";
     aExtension      = "";
     aNombreFich     = "";

     &enMailing      = 0;
     &eaCadenaImpre  = "";
     &eaRuta           = "";

     &eaFicheroRecog = "";
     &enFSalida        = 0;
     &enCodUbica       = 0;
     &enTipoDocumento  = 0;
     &eaRutaDocumento  = "";
     &eaNombrePrograma = "";
     &eaNombreRuta     = "";
     &eaRutaPlanilla   = "";
     &eaExtension      = "";
     &eaFicheroEdita   = "";
     &eaFicheroEditaO  = "";
     &enSwPlanilla     = 0;
     &enModelo         = 0;
|FIN;

|PROCESO Imprime;
     aRegilla = "#";
     |SI nContador = 0;  aRegilla = " ";  |FINSI;
     aAlfa = ("00000" + #112#1) % -105;

     nContador     = nContador + 1;
     |SI #112#5 = "dsam0000";  |O #112#5 = "dsay0004";
         eaCadenaImpre = "#" + (aAlfa % 102) + "#" + "0" + (aAlfa % 303) + "   " + #112#2 + "   " + #112#3;
         |PRINT;
     |SINO;
         eaCadenaImpre = "#" + (aAlfa % 102) + "#" + nCampo + (aAlfa % 303) + "   " + #112#2 + "   " + #112#3 + "   L" + nCampo;
         |PRINT;
     |FINSI;
|FPRC;

|DEFBUCLE dsam0112C;
     #112#1;
     ;
     #111#0, nDCodigo;
     #111#0, nHCodigo;
     be;
     NULL, Imprime;
|FIN;

|RUTINA ClaveBaja112;
     #112#0 = #111#0;
     #112#1 = 00000;
|FRUT;

|RUTINA ClaveAlta112;
     #112#0 = #111#0;
     #112#1 = 99999;
|FRUT;

|RUTINA ClaveBaja98;
     #98#0 = " " * 30;
|FRUT;

|RUTINA ClaveAlta98;
     #98#0 = "z" * 30;
|FRUT;

|PROCESO RecogePlanillas;
     nSwGraba     = 0;
     enSwPlanilla = 1;

     aTemporal = (Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #98 aTemporal;
     |ABRE #98;  |CIERRA #98;  |DELINDEX #98;

     |ABRE #98;

     aPathPlan  = eaRutaPlanilla;   |QBF aPathPlan;
     aPathPlan  = aPathPlan - "\*" + "/";
     aDirOrigen = aPathPlan;

     |R_PDIR aDirOrigen;
     |PARA; |SI FSalida = 0; |HACIENDO;
            aCadena1    = aDirOrigen  > " ";
            aCadena2    = eaExtension > " ";
            aCadena2    = "." + aCadena2;
            aDirDestino = aCadena1 - aCadena2;
            |SI aDirDestino ! aCadena1;
                aCadena1    = aDirOrigen > " ";
                aCadena2    = aPathPlan  > " ";  |QBF aCadena2
                aNombreFich = aCadena1 - aCadena2;

                #98#0 = aNombreFich;
                #98#1 = aNombreFich;
                |GRABA 020#98n;
                |LIBERA #98;
                nSwGraba = 1;
            |FINSI;

            |R_SDIR aDirOrigen;
     |SIGUE;
     |CIERRA #98;

     nSwAborto = 1;
     |SI nSwGraba = 1;
         |PUSHV 0501 2380;
         eaFicheroRecog = "";
         |ENTLINEAL #1#98, -1, 4, 22, 1, ClaveBaja98, ClaveAlta98;
         |POPV;

         |SI eaFicheroRecog ! "";
             aOrigen = aPathPlan + eaFicheroRecog;
         |FINSI;
     |SINO;
         |MENAV "      Seleccion Vacia.";
     |FINSI;

     |DELINDEX #98;
|FPRC;

|PROCESO Tipo3m0111;  |TIPO 3;
     enMailing = #111#0;
     enModelo  = #111#6;
     |SUB_EJECUTA_NP "dsaw0023";

     |HAZ Tipo7m0111;

     |IMPRE 0;
     |SI FSalida = 0;
         |INFOR "dsam0111";
         |SI FSalida = 0;
             nContador = 0;
             nDCodigo  = 01000;  nHCodigo  = 01999;  |BUCLE dsam0112C;
             nDCodigo  = 07000;  nHCodigo  = 07999;  |BUCLE dsam0112C;
             nDCodigo  = 08000;  nHCodigo  = 08999;  |BUCLE dsam0112C;

             |PARA nCampo = 1;  |SI nCampo [ 5;  |HACIENDO nCampo = nCampo + 1;
                   nDCodigo  = 02000;  nHCodigo  = 02999;  |BUCLE dsam0112C;
             |SIGUE;

             |PARA nCampo = 1;  |SI nCampo [ 5;  |HACIENDO nCampo = nCampo + 1;
                   nDCodigo  = 03000;  nHCodigo  = 03999;  |BUCLE dsam0112C;
             |SIGUE;

             |PARA nCampo = 1;  |SI nCampo [ 5;  |HACIENDO nCampo = nCampo + 1;
                   nDCodigo  = 04000;  nHCodigo  = 04999;  |BUCLE dsam0112C;
             |SIGUE;

             |PARA nCampo = 1;  |SI nCampo [ 5;  |HACIENDO nCampo = nCampo + 1;
                   nDCodigo  = 05000;  nHCodigo  = 05999;  |BUCLE dsam0112C;
             |SIGUE;

             |PARA nCampo = 1;  |SI nCampo [ 5;  |HACIENDO nCampo = nCampo + 1;
                   nDCodigo  = 06000;  nHCodigo  = 06999;  |BUCLE dsam0112C;
             |SIGUE;

             |SI nContador ! 0;  |PIEINF;  |FINSI;
             |FININF;
         |FINSI;
         |FINIMP;
     |FINSI;

     aOrigen  = eaRutaPlanilla;                                |QBF aOrigen;
     aDestino = eaRutaDocumento + "/" + #111#4 + "." + eaExtension;  |QBT aDestino;

     |XABRE "EC", aDestino, nCanal;
     |SI FSalida > 0;
         aOrigen  = "";
     |FINSI;
     |XCIERRA nCanal;

     |SI aOrigen ! "";
         aAlfa = aOrigen % -101;
         |SI aAlfa = "*";
             |HAZ RecogePlanillas;
         |FINSI;

         |SI aOrigen ! "";
             |RCOPIA_FICHERO aOrigen, aDestino;

             |SI FSalida ! 0;
                 |MENAV "         Ha habido un error al copiar la Plantilla.";
                 |ACABA;
             |FINSI;
         |FINSI;
     |FINSI;

     eaFicheroEditaO = #111#4;                      |QBF eaFicheroEditaO;
     eaFicheroEditaO = eaFicheroEditaO + "." + eaExtension;  |QBF eaFicheroEditaO;
     eaFicheroEdita  = eaFicheroEditaO;             |QBT eaFicheroEdita;
     |HAZ AbreDocumento;
|FPRC;

|PROCESO Borra112;
     |BORRA 020#112a;
     |LIBERA #112;
|FPRC;

|DEFBUCLE dsam0112B;
     #112#1;
     ;
     #111#0, 00000;
     #111#0, 99999;
     ;
     NULL, Borra112;
|FIN;

|PROCESO Tipo2m0111;  |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo ! 3;  |ACABA;  |FINSI;

     |BUCLE dsam0112B;

     enCodUbica = #111#2;  |HAZ LeeUbicaciones;

     aFicheroActu = #111#4 + "." + eaExtension;   |QBT aFicheroActu;
     aAlfa        = eaRutaDocumento + "\" + aFicheroActu;
     |RFBORRA aAlfa;
|FPRC;

|PROCESO Tipo7m0111;  |TIPO 7;
     |ENTLINEAL #1#112, -2, 7, 22, 1, ClaveBaja112, ClaveAlta112;
|FPRC;

|PROCESO ChequeaTipo;
     enCodUbica = #111#2;  |HAZ LeeUbicaciones;
     eaRuta     = eaRutaDocumento;

     |SI enTipoDocumento ! 1;
         |MENAV "     El programa externo debe ser Tipo WORD.";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO ChequeaPrograma;
     #111#4 = #111#4 > " ";   |PINTA #111#4;

     aAlfa = #111#4;  |QBF aAlfa;
     |SI aAlfa = "";
         |MENAV "      Introduzca el Nombre del Documento.";
         |ERROR;
     |FINSI;
|FPRC;

|RUTINA ClaveBaja111;
     #111#0 = 1;
|FRUT;

|RUTINA ClaveAlta111;
     #111#0 = 999;
|FRUT;

|PROGRAMA;
     |CLS;
     |CABEZA "Creacion de Mailing";
     |ENTLINEAL #1#111, -1, 2, 22, 1, ClaveBaja111, ClaveAlta111;
|FPRO;
