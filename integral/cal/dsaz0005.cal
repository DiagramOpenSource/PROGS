|FICHEROS;
     dsaz0005 #0;
     dsam0000 #1;
     dsaw0011 #100,MANTE;
     dsaw0012 #101,MANTE;

     :/facturar/def/facemp   #200;
     :/facturar/def/asclient #201;
     :/facturar/def/ascuoca1 #202;
|FIN;

|VARIABLES;
     aBase       = "";
     aFichero    = "";
     aAlfa       = "";
     aTodos      = "";
     nDEjercicio = 0;
     nCliente    = 0;

     nSwNO       = 0;
     aPassword   = "";
     aClave      = "";

     &eaPath     = "";
     &enTipoFac  = 0;
     &enEmpresa  = 0;
     &enEmpreFac = 0;
|FIN;

|PROCESO Graba101;
     |SI aTodos = "N";
         #202#0 = #201#0;
         #202#8 = 0;
         |LEE 040#202];
         |SI FSalida ! 0;  |O  #202#0 ! #201#0;
             |ACABA;
         |FINSI;
     |FINSI;

     #101#0 = #200#0;
     #101#1 = #201#0;
     #101#2 = #201#1;
     aAlfa  = #101#2;  |QBF aAlfa;
     |SI aAlfa = "";
         #101#2 = #201#2;
     |FINSI;

     |GRABA 020#101n;
     |LIBERA #101;
     nCliente = nCliente + 1;
|FPRC;

|DEFBUCLE asclient;
     #201#1;
     ;
     00001;
     99999;
     ;
     NULL, Graba101;
|FIN;

|PROCESO Graba100;
     eaPath  = aBase + "facturar/fich/" + (("00" + #200#0) % -102) + "/";
     |PATH_DAT #201 eaPath;
     |PATH_DAT #202 eaPath;

     nCliente = 0;
     |ABRE #202;
     |BUCLE asclient;
     |CIERRA #202;

     |SI nCliente ! 0;
         #100#0 = #200#0;
         #100#1 = #200#1;
         |GRABA 020#100n;
         |LIBERA #100;
     |FINSI;
|FPRC;

|DEFBUCLE facemp;
     #200#1;
     ;
     00;
     99;
     ;
     NULL, Graba100;
|FIN;

|RUTINA ClaveBaja100;
     #100#0 = 00;
|FRUT;

|RUTINA ClaveAlta100;
     #100#0 = 99;
|FRUT;

|RUTINA ClaveBaja101;
     #101#0 = #100#0;
     #101#1 = 1;
|FRUT;

|RUTINA ClaveAlta101;
     #101#0 = #100#0;
     #101#1 = 99999;
|FRUT;

|RUTINA ClaveBaja202;
     #202#0 = #101#1;
     #202#8 = 2099;
|FRUT;

|RUTINA ClaveAlta202;
     #202#0 = #101#1;
     #202#8 = 1900;
|FRUT;

|PROCESO Tipo7_100;  |TIPO 7;
     |ENTLINEAL #1#101, -2, 7, 22, 0, ClaveBaja101, ClaveAlta101;

     |ABRE #101;
     #101#0 = #100#0;
     #101#1 = 0;
     |LEE 041#101];
     |HAZ Tipo7_101;
     |CIERRA #101;
|FPRC;

|PROCESO MiraClave;
     |ABRE #200;
     |DDEFECTO #200;
     #200#0 = #100#0;
     |LEE 041#200=;
     |CIERRA #200;

     nSwNO = 0;
     aPassword = "";
     |PUSHV 2102 2379;
     |BLANCO 2126 2355;
     |CUADRO 2126 2355;
     |ATRI S; |PINTA 2227, "Entre la CLAVE:            "; |ATRI 0;

     E_inf = "";
     E_sup = "10";
     aPassword = 2243 ? -1;
     |QBF aPassword;
     aClave = #200#2; |QBF aClave;
     |SI aPassword ! aClave;
         |MENAV "     Clave incorrecta.";
         nSwNO = 1;
     |FINSI;
     |POPV;
|FPRC;

|PROCESO Tipo11_100;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     |HAZ  MiraClave;

     |SI nSwNO = 0;
         |ENTLINEAL #1#101, -2, 4, 22, 0, ClaveBaja101, ClaveAlta101;
     |FINSI;
|FPRC;

|PROCESO Tipo7_101;  |TIPO 7;

     |CIERRAT #202;
     eaPath  = aBase + "facturar/fich/" + (("00" + #101#0) % -102) + "/";
     |PATH_DAT #202 eaPath;

     |ENTLINEAL #2#202, -2, 7, 22, 0, ClaveBaja202, ClaveAlta202;
|FPRC;

|PROCESO Tipo11_101;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     eaPath     = aBase + "facturar/fich/" + (("00" + #101#0) % -102) + "/";
     enTipoFac  = 12;
     enEmpresa  = #101#1;
     enEmpreFac = #101#0;
     |SUB_EJECUTA_NP ":facturar/facemp2";
|FPRC;

|PROCESO Tipo66_101;  |TIPO 66;
     |ABRE #1;
     #1#0 = #101#1;
     |LEE 040#1=;

     |CONSULTA_CLAVE #1#1;
     |SI FSalida = 0;
         #101#1 = #1#0;
         |PINTA #101#1;
     |FINSI;

     |CIERRA #1;

     |ERROR;
|FPRC;

|PROCESO PreguntaDesde;
     |CLS;
     |CUADRO 1217 1652;
     |ATRI S;
     |PINTA 1319, "CUOTAS DESDE EL EJERCICIO:      ";
     |PINTA 1419, "       HASTA EL EJERCICIO ACTUAL";
     |ATRI 0;
     nDEjercicio = 2000;
     E_inf = 2000;
     E_sup = 2099;
     nDEjercicio = 1346 ? 1;
|FPRC;

|PROGRAMA;
      |DBASS aBase;  |QBF aBase;

      aFichero = ("11" + Usuario + "zzzzzzzz") % 108;
      |NOME_DAT #100 aFichero;

      aFichero = ("12" + Usuario + "zzzzzzzz") % 108;
      |NOME_DAT #101 aFichero;

      |ABRE #100;  |CIERRA #100;  |DELINDEX #100;
      |ABRE #101;  |CIERRA #101;  |DELINDEX #101;

    ||  |HAZ PreguntaDesde;

      aTodos = "N";
      |CONFI "0000SSolo Clientes con Cuotas ";
      |SI FSalida ! 0;  aTodos = "S";  |FINSI;

      |PINPA #0#0;
      |MENSA "0000 Configurando Datos, Espere un Momento";

      |ABRE #100;
      |ABRE #101;
      |BUCLE facemp;
      |CIERRA #100;
      |CIERRA #101;

      |ENTLINEAL #1#100, -1, 4, 22, 0, ClaveBaja100, ClaveAlta100;

      |DELINDEX #100;
      |DELINDEX #101;
|FPRO;
