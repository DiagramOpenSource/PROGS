|FICHEROS;
     dsam0000;
     dsam0101;
     dsam0103;

     :basica/agipatro;

     ficher1@;
     ficher2@;
|FIN;

|VARIABLES;
     nCampo              = 0;
     i                   = 0;
     nPos                = 0;
     nHandle             = 0;
     nOk                 = 0;
     nError              = 0;
     nNume               = 0;
     nCon                = 0;
     nGecobox            = 0;

     aLon                = "";
     aDato               = "";
     aReg                = "";
     aLetra              = "";
     aTab                = "";
     aNomFic             = "";
     aPath               = "";
     aDes                = "";
     aOk                 = "";
     aMas                = "";
     aBasApli            = "";
     aFic                = "";
     aPer                = "";
     aEjer               = "";
     aAlfa               = "";
     aOr                 = "";
     aDe                 = "";
     aPathOr             = "";
     aPathDe             = "";
     aAbre               = "";
     aLee                = "";
     aInd                = "";
     aApli               = "";
     aAlf1               = "";
     aAlf2               = "";

     fFecha              = @;

     {10}aValor          = "";

     &enCodCli           = 0;
     &enEmpresa          = 0;
     &enPeriodo          = 0;
     &enEmpreOri         = 0;
     &enPerioOri         = 0;
     &swOperacion        = 0;
     &any_pos            = 0;
     &eaMoneda           = "";
     &fich_alta          = "";
     &eaExterno          = "";
     &eaCopiar           = "";
     &eaCeros            = "";
     &eaOtras            = "";
     &eaEmpresa          = "";
     &eaEMail            = "";
     &eaCIF              = "";
     &eaNomEmp           = "";
     &eaNomCom           = "";
     &eaCodPos           = "";
|FIN;

|PROCESO GrabaOK;
     aDes = aPath + aNomFic + ".sal";
     |XABRE "W", aDes, nHandle;
     |SI FSalida ] 0;
          aDato = "resultado" + aTab;  |XGRABA nHandle, aDato;
          aDato = aOk + aTab;          |XGRABA nHandle, aDato;
     |FINSI;
     |XCIERRA nHandle;
|FPRC;

|PROCESO Contagen;
     aMas = aBasApli + "contagen/def/canempre.mas";
     aFic = aBasApli + "contagen/fich/";
     |CARGA_DEF aMas, ficher1@;

     |PATH_DAT #ficher1@#aFic#;

     nOk = 1;

     |ABRE #ficher1@;
     #ficher1@#2 = enCodCli;
     #ficher1@#3 = aPer;
     |LEE 000#ficher1@.=;
     |SI FSalida = 0;
         nOk = 0;
     |FINSI;
     |CIERRA #ficher1@;

     |DESCARGA_DEF #ficher1@;

     |SI nOk = 0;
         |ACABA;
     |FINSI;

     |CARGA_DEF aMas, ficher1@;
     |CARGA_DEF aMas, ficher2@;

     |PATH_DAT #ficher1@#aFic#;
     |PATH_DAT #ficher2@#aFic#;

     |ABRE #ficher1@;
     |ABRE #ficher2@;

     |ABRE #agipatro;
     |LEE 000#agipatro.p;
     |SI FSalida = 0;
         #ficher2@#2 = #agipatro#1;
         #ficher2@#3 = #agipatro#2;
     |SINO;
         #ficher2@#2 = 99999;
         #ficher2@#3 = 9;
     |FINSI;
     |CIERRA #agipatro;

     |LEE 000#ficher2@.=;
     |SI FSalida ! 0;  |DDEFECTO #ficher2@;  |FINSI;

     |PARA nCampo = 0;  |SI nCampo [ 35;  |HACIENDO nCampo = nCampo + 1;
           #ficher1@#nCampo# = #ficher2@#nCampo#;
     |SIGUE;

     #ficher1@#2  = enCodCli;
     #ficher1@#3  = aPer;
     aAlfa        = (("00000" + #ficher1@#2) % -105) + #ficher1@#3;
     #ficher1@#0  = aAlfa;
     #ficher1@#1  = #dsam0000#1;
     #ficher1@#4  = #dsam0000#2;
     #ficher1@#5  = "";
     #ficher1@#6  = "";
     #ficher1@#7  = "";
     #ficher1@#8  = "";
     #ficher1@#10 = "";
     #ficher1@#11 = 01;
     #ficher1@#12 = 12;
     #ficher1@#22 = "O";       || subdepartamento OTROS
     #ficher1@#24 = "S";       || departamento SI
     #ficher1@#26 = aEjer;
     #ficher1@#28 = "E";

     |SI #ficher1@#26 ] 80;
         nNume = ficher1@ + #ficher1@#26;
     |SINO;
        nNume = 2000 + #ficher1@#26;
     |FINSI;
     aAlfa   =  "[" + (("00000" + #ficher1@#2) % -105) + "/" + nNume + "] " + #ficher1@#1;

     #ficher1@#29 = aAlfa;
     |GRABA 020#ficher1@.n;
     |LIBERA #ficher1@;
     |CIERRA #ficher1@;

     aAlfa = aBasApli + "contagen/fich/" + (("000000" + #ficher1@#0) % -106);
     |MKDIR aAlfa;

     enEmpresa   = #ficher1@#2;
     enPeriodo   = #ficher1@#3;
     eaMoneda    = #ficher1@#28;
     swOperacion = 3;
     |SUB_EJECUTA_NP ":contagen/camoneda";
     swOperacion = 0;

     fich_alta  = aAlfa + "/";
     any_pos    = #ficher1@#26;
     eaExterno  = fich_alta;
     enEmpreOri = #ficher2@#2;
     enPerioOri = #ficher2@#3;
     eaCopiar   = "T";
     eaCeros    = "S";
     eaOtras    = "N";
     |SUB_EJECUTA ":contagen/cacrebal";

     |DESCARGA_DEF #ficher2@;
     |DESCARGA_DEF #ficher1@;
|FPRC;

|PROCESO CopFic;
     |QBF aMas;

     aOr = aPathOr + aMas + ".dat";
     aDe = aPathDe + aMas + ".dat";
     |COPIA_FICHERO aOr, aDe;

     aOr = aPathOr + aMas + ".ddx";
     aDe = aPathDe + aMas + ".ddx";
     |COPIA_FICHERO aOr, aDe;

     aOr = aPathOr + aMas + ".ixx";
     aDe = aPathDe + aMas + ".ixx";
     |COPIA_FICHERO aOr, aDe;
|FPRC;

|PROCESO CopiaFic;
     aAbre = aBasApli + aApli + "/fich/" + aApli + ".dir";
     |XABRE "U", aAbre, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     nCon = 0;
     |PARA;  |SI;  |HACIENDO;
          |XLEE nHandle, 250, aLee;
          |SI FSalida < 0;  |SAL;  |FINSI;

          nCon = nCon + 1;

          |SI nCon = 1;  aMas = aLee;  |FINSI;

          |SI nCon = 4;
              aInd = aLee % 101;

              |SI aInd = "1";  |O aInd = 2;
                  |HAZ CopFic;
              |FINSI;
          |FINSI;

          |SI nCon = 5;  nCon = 0;  |FINSI;
     |SIGUE;
|FPRC;

|PROCESO agifa041;
     |CARGA_DEF aMas, ficher1@;
     |PATH_DAT #ficher1@#aFic#;

     |ABRE #ficher1@;
     |DDEFECTO #ficher1@;

     #ficher1@#0  = enCodCli;
     #ficher1@#1  = #dsam0000#1;
     #ficher1@#5  = #dsam0000#3;
     #ficher1@#2  = #dsam0000#20;
     #ficher1@#3  = #dsam0000#27;
     #ficher1@#4  = #dsam0000#28;
     #ficher1@#6  = #dsam0000#54;
     #ficher1@#15 = #dsam0000#55;
     #ficher1@#20 = #dsam0000#70;
     #ficher1@#21 = #dsam0000#71;
     #ficher1@#8  = "~~~~~~~~~~";
     |GRABA 020#ficher1@.n;
     |LIBERA #ficher1@;
     |CIERRA #ficher1@;
     |DESCARGA_DEF #ficher1@;

     aPathOr = aBasApli + "dscomer9/fich/99999/";
     aPathDe = aBasApli + "dscomer9/fich/" + (("00000" + enCodCli) % -105);
     |MKDIR aPathDe;
     aPathDe = aPathDe + "/";

     aApli = "dscomer9";
     |HAZ CopiaFic;

     eaEmpresa = ("00000" + enCodCli) % -105;
     |SUB_EJECUTA_NP ":dscomer9/agifa041;PATRONES";
|FPRC;

|PROCESO agifa142;
     aMas = aBasApli + "dscomer9/def/agifa142.mas";
     aFic = aBasApli + "dscomer9/fich/" + (("00000" + enCodCli) % -105) + "/";

     |CARGA_DEF aMas, ficher1@;
     |PATH_DAT #ficher1@#aFic#;

     |ABRE #ficher1@;
     |LEE 101#ficher1@.p;
     |SI FSalida ! 0;
         |DDEFECTO #ficher1@;
         |GRABA 020#ficher1@.b;
     |FINSI;

     aAlfa         = aBasApli + "contagen/";
     #ficher1@#151 = aAlfa;
     aAlfa         = (("00000" + enCodCli) % -105) + aPer;
     #ficher1@#152 = aAlfa;

     |GRABA 020#ficher1@.a;
     |LIBERA #ficher1@;
     |CIERRA #ficher1@;
     |DESCARGA_DEF #ficher1@;
|FPRC;

|PROCESO agifa141;
     aMas = aBasApli + "dscomer9/def/agifa141.mas";
     aFic = aBasApli + "dscomer9/fich/" + (("00000" + enCodCli) % -105) + "/";

     |CARGA_DEF aMas, ficher1@;
     |PATH_DAT #ficher1@#aFic#;

     |ABRE #ficher1@;
     |LEE 101#ficher1@.p;
     |SI FSalida ! 0;
         |DDEFECTO #ficher1@;
         |GRABA 020#ficher1@.b;
     |FINSI;

     aAlfa        = aBasApli + "contagen/";
     #ficher1@#11 = aAlfa;
     #ficher1@#0  = enCodCli;
     #ficher1@#8  = aPer;

     |GRABA 020#ficher1@.a;
     |LIBERA #ficher1@;
     |CIERRA #ficher1@;
     |DESCARGA_DEF #ficher1@;
|FPRC;

|PROCESO agifa704Bu;
     aAlfa  = aBasApli + "contagen/";

     #ficher1@#6 = aAlfa;
     #ficher1@#4 = enCodCli;
     #ficher1@#5 = aPer;

     |GRABA 020#ficher1@.a;
     |LIBERA #ficher1@;
|FPRC;

|DEFBUCLE agifa704;
     #ficher1@#1001;
     ;
     "    ";
     "zzzz";
     be;
     NULL, agifa704Bu;
|FIN;

|PROCESO agifa704;
     aMas = aBasApli + "dscomer9/def/agifa704.mas";
     aFic = aBasApli + "dscomer9/fich/" + (("00000" + enCodCli) % -105) + "/";

     |CARGA_DEF aMas, ficher1@;
     |PATH_DAT #ficher1@#aFic#;
     |BUCLE agifa704;
     |DESCARGA_DEF #ficher1@;
|FPRC;

|PROCESO malpe115Bu;
     aAlfa  = ("00000" + enCodCli) % -105;

     #ficher1@#2 = aAlfa;
     |GRABA 020#ficher1@.a;
     |LIBERA #ficher1@;
|FPRC;

|DEFBUCLE malpe115;
     #ficher1@#1003;
     ;
     "     ", " ", " ";
     "zzzzz", "z", "z";
     be;
     NULL, malpe115Bu;
|FIN;

|PROCESO malpe115;
     aMas = aBasApli + "dscomer9/def/malpe115.mas";
     aFic = aBasApli + "dscomer9/fich/" + (("00000" + enCodCli) % -105) + "/";

     |CARGA_DEF aMas, ficher1@;
     |PATH_DAT #ficher1@#aFic#;
     |BUCLE malpe115;
     |DESCARGA_DEF #ficher1@;
|FPRC;

|PROCESO Dscomer9;
     aMas = aBasApli + "dscomer9/def/agifa041.mas";
     aFic = aBasApli + "dscomer9/fich/";

     |CARGA_DEF aMas, ficher1@;
     |PATH_DAT #ficher1@#aFic#;

     nOk = 1;

     |ABRE #ficher1@;
     #ficher1@#0 = enCodCli;
     |LEE 000#ficher1@.=;
     |SI FSalida = 0;
         nOk = 0;
     |FINSI;
     |CIERRA #ficher1@;

     |DESCARGA_DEF #ficher1@;

     |SI nOk = 0;
         |ACABA;
     |FINSI;

     |HAZ agifa041;
     |HAZ agifa141;
     |HAZ agifa142;
     |HAZ agifa704;
     |HAZ malpe115;
|FPRC;

|PROCESO dscam000;
     |CARGA_DEF aMas, ficher1@;
     |PATH_DAT #ficher1@#aFic#;

     |ABRE #ficher1@;
     #ficher1@#0  = enCodCli;
     #ficher1@#1  = #dsam0000#1;
     #ficher1@#14 = enCodCli;
     #ficher1@#15 = enCodCli;

     aAlfa        = aBasApli + "contagen/";
     #ficher1@#16 = aAlfa;
     aAlfa        = aBasApli + "dscomer9/";
     #ficher1@#17 = aAlfa;
     #ficher1@#18 = "~~~~~~~~~~";
     #ficher1@#19 = aPer;
     |GRABA 020#ficher1@.n;
     |LIBERA #ficher1@;
     |CIERRA #ficher1@;
     |DESCARGA_DEF #ficher1@;

     aPathOr = aBasApli + "dscarter/fich/99999/";
     aPathDe = aBasApli + "dscarter/fich/" + (("00000" + enCodCli) % -105);
     |MKDIR aPathDe;
     aPathDe = aPathDe + "/";

     aApli = "dscarter";
     |HAZ CopiaFic;
|FPRC;

|PROCESO dscam044;
     aMas = aBasApli + "dscarter/def/dscam044.mas";
     aFic = aBasApli + "dscarter/fich/";

     |CARGA_DEF aMas, ficher1@;
     |PATH_DAT #ficher1@#aFic#;

     |ABRE #ficher1@;
     #ficher1@#0 = enCodCli;
     |LEE 101#ficher1@.=;
     |SI FSalida ! 0;
         |DDEFECTO #ficher1@;
         #ficher1@#0 = enCodCli;
         |GRABA 020#ficher1@.b;
     |FINSI;

     aAlfa       = aBasApli + "dscomer9/";
     #ficher1@#1 = aAlfa;
     |GRABA 020#ficher1@.a;
     |LIBERA #ficher1@;
     |CIERRA #ficher1@;
     |DESCARGA_DEF #ficher1@;
|FPRC;

|PROCESO dscam045;
     aMas = aBasApli + "dscarter/def/dscam045.mas";
     aFic = aBasApli + "dscarter/fich/";

     |CARGA_DEF aMas, ficher1@;
     |PATH_DAT #ficher1@#aFic#;

     |ABRE #ficher1@;
     #ficher1@#0 = enCodCli;
     #ficher1@#1 = 1;
     |LEE 101#ficher1@.=;
     |SI FSalida ! 0;
         #ficher1@#0 = enCodCli;
         #ficher1@#1 = 1;
         #ficher1@#2 = "V";
         #ficher1@#3 = "VENTAS";
         #ficher1@#4 = "";
         #ficher1@#5 = "zzzzz";
         |GRABA 020#ficher1@.b;
     |FINSI;

     #ficher1@#6 = enCodCli;
     |GRABA 020#ficher1@.a;
     |LIBERA #ficher1@;

     #ficher1@#0 = enCodCli;
     #ficher1@#1 = 2;
     |LEE 101#ficher1@.=;
     |SI FSalida ! 0;
         #ficher1@#0 = enCodCli;
         #ficher1@#1 = 2;
         #ficher1@#2 = "C";
         #ficher1@#3 = "COMPRAS";
         #ficher1@#4 = "";
         #ficher1@#5 = "zzzzz";
         #ficher1@#6 = enCodCli;
         |GRABA 020#ficher1@.b;
     |FINSI;

     #ficher1@#6 = enCodCli;
     |GRABA 020#ficher1@.a;
     |LIBERA #ficher1@;
     |CIERRA #ficher1@;
     |DESCARGA_DEF #ficher1@;
|FPRC;

|PROCESO dscam037;
     aMas = aBasApli + "dscarter/def/dscam037.mas";
     aFic = aBasApli + "dscarter/fich/";

     |CARGA_DEF aMas, ficher1@;
     |SI FSalida = 0;
         |PATH_DAT #ficher1@#aFic#;

         |ABRE #ficher1@;
         #ficher1@#0 = enCodCli;
         |LEE 101#ficher1@.=;
         |SI FSalida ! 0;
             |DDEFECTO #ficher1@;
             #ficher1@#0 = enCodCli;
             |GRABA 020#ficher1@.b;
         |FINSI;

         aAlfa       = aBasApli + "dscomer9/";
         #ficher1@#2 = aAlfa;
         |GRABA 020#ficher1@.a;
         |LIBERA #ficher1@;
         |CIERRA #ficher1@;

         |DESCARGA_DEF #ficher1@;
     |FINSI;
|FPRC;

|PROCESO dscam038;
     aMas = aBasApli + "dscarter/def/dscam038.mas";
     aFic = aBasApli + "dscarter/fich/";

     |CARGA_DEF aMas, ficher1@;
     |PATH_DAT #ficher1@#aFic#;
     |ABRE #ficher1@;

     #ficher1@#0 = enCodCli;
     #ficher1@#1 = 1;
     |LEE 101#ficher1@.=;
     |SI FSalida ! 0;
         #ficher1@#0 = enCodCli;
         #ficher1@#1 = 1;
         #ficher1@#2 = "F";
         #ficher1@#7 = "Facturas";
         #ficher1@#3 = "";
         #ficher1@#4 = "zzzzz";
         #ficher1@#6 = 01;
         #ficher1@#8 = "R";
         |GRABA 020#ficher1@.b;
     |FINSI;

     #ficher1@#5 = enCodCli;
     |GRABA 020#ficher1@.a;
     |LIBERA #ficher1@;

     #ficher1@#0 = enCodCli;
     #ficher1@#1 = 2;
     |LEE 101#ficher1@.=;
     |SI FSalida ! 0;
         #ficher1@#0 = enCodCli;
         #ficher1@#1 = 2;
         #ficher1@#2 = "A";
         #ficher1@#7 = "Albaranes";
         #ficher1@#3 = "";
         #ficher1@#4 = "zzzzz";
         #ficher1@#6 = 01;
         #ficher1@#8 = "R";
         |GRABA 020#ficher1@.b;
     |FINSI;

     #ficher1@#5 = enCodCli;
     |GRABA 020#ficher1@.a;
     |LIBERA #ficher1@;

     #ficher1@#0 = enCodCli;
     #ficher1@#1 = 3;
     |LEE 101#ficher1@.=;
     |SI FSalida ! 0;
         #ficher1@#0 = enCodCli;
         #ficher1@#1 = 3;
         #ficher1@#2 = "P";
         #ficher1@#7 = "Pedido";
         #ficher1@#3 = "";
         #ficher1@#4 = "zzzzz";
         #ficher1@#6 = 01;
         #ficher1@#8 = "R";
         |GRABA 020#ficher1@.b;
     |FINSI;

     #ficher1@#5 = enCodCli;
     |GRABA 020#ficher1@.a;
     |LIBERA #ficher1@;

     |DESCARGA_DEF #ficher1@;
|FPRC;

|PROCESO dscam042;
     aMas = aBasApli + "dscarter/def/dscam042.mas";
     aFic = aBasApli + "dscarter/fich/";
     |CARGA_DEF aMas, ficher1@;
     |PATH_DAT #ficher1@#aFic#;

     |ABRE #ficher1@;
     #ficher1@#0 = enCodCli;
     #ficher1@#1 = aPer;
     |LEE 101#ficher1@.=;
     |SI FSalida ! 0;
         |DDEFECTO #ficher1@;
         #ficher1@#0 = enCodCli;
         #ficher1@#1 = aPer;
         |GRABA 020#ficher1@.b;
     |FINSI;

     aAlfa  = aBasApli + "contagen/";
     #ficher1@#2 = aAlfa;

     |GRABA 020#ficher1@.a;
     |LIBERA #ficher1@;
     |CIERRA #ficher1@;
     |DESCARGA_DEF #ficher1@;
|FPRC;

|PROCESO dscam043;
     aMas = aBasApli + "dscarter/def/dscam043.mas";
     aFic = aBasApli + "dscarter/fich/";

     |CARGA_DEF aMas, ficher1@;
     |PATH_DAT #ficher1@#aFic#;
     |ABRE #ficher1@;

     #ficher1@#0 = enCodCli;
     #ficher1@#1 = aPer;
     #ficher1@#2 = 1;
     |LEE 101#ficher1@.=;
     |SI FSalida ! 0;
         #ficher1@#0 = enCodCli;
         #ficher1@#1 = aPer;
         #ficher1@#2 = 1;
         #ficher1@#3 = "V";
         #ficher1@#4 = "VENTAS";
         #ficher1@#5 = 0;
         #ficher1@#6 = 99;
         #ficher1@#7 = "";
         #ficher1@#8 = "zzzzz";
         |GRABA 020#ficher1@.b;
     |FINSI;

     #ficher1@#9 = enCodCli;
     |GRABA 020#ficher1@.a;
     |LIBERA #ficher1@;

     #ficher1@#0 = enCodCli;
     #ficher1@#1 = aPer;
     #ficher1@#2 = 2;
     |LEE 101#ficher1@.=;
     |SI FSalida ! 0;
         #ficher1@#0 = enCodCli;
         #ficher1@#1 = aPer;
         #ficher1@#2 = 2;
         #ficher1@#3 = "C";
         #ficher1@#4 = "COMPRAS";
         #ficher1@#5 = 0;
         #ficher1@#6 = 99;
         #ficher1@#7 = "";
         #ficher1@#8 = "zzzzz";
         |GRABA 020#ficher1@.b;
     |FINSI;

     #ficher1@#9 = enCodCli;
     |GRABA 020#ficher1@.a;
     |LIBERA #ficher1@;
     |CIERRA #ficher1@;
     |DESCARGA_DEF #ficher1@;
|FPRC;

|PROCESO dscam030Bu;
     aAlfa  = aBasApli + "contagen/";

     #ficher1@#6 = aAlfa;
     #ficher1@#4 = enCodCli;
     #ficher1@#5 = aPer;

     |GRABA 020#ficher1@.a;
     |LIBERA #ficher1@;
|FPRC;

|DEFBUCLE dscam030;
     #ficher1@#1001;
     ;
     "    ";
     "zzzz";
     be;
     NULL, dscam030Bu;
|FIN;

|PROCESO dscam030;
     aMas = aBasApli + "dscarter/def/dscam030.mas";
     aFic = aBasApli + "dscarter/fich/" + (("00000" + enCodCli) % -105) + "/";

     |CARGA_DEF aMas, ficher1@;
     |PATH_DAT #ficher1@#aFic#;

     |BUCLE dscam030;

     |DESCARGA_DEF #ficher1@;
|FPRC;

|PROCESO Dscarter;
     aMas = aBasApli + "dscarter/def/dscam000.mas";
     aFic = aBasApli + "dscarter/fich/";

     |CARGA_DEF aMas, ficher1@;
     |PATH_DAT #ficher1@#aFic#;

     nOk = 1;

     |ABRE #ficher1@;
     #ficher1@#0 = enCodCli;
     |LEE 000#ficher1@.=;
     |SI FSalida = 0;
         nOk = 0;
     |FINSI;
     |CIERRA #ficher1@;
     |DESCARGA_DEF #ficher1@;

     |SI nOk = 0;
         |ACABA;
     |FINSI;

     |HAZ dscam000;
     |HAZ dscam044;
     |HAZ dscam045;
     |HAZ dscam037;
     |HAZ dscam038;
     |HAZ dscam042;
     |HAZ dscam043;
     |HAZ dscam030;
|FPRC;

|PROCESO dpntm001;
     aMas = aBasApli + "dsarchi/def/dpntm001.mas";
     aFic = aBasApli + "dsarchi/fich/";

     |CARGA_DEF aMas, ficher1@;
     |PATH_DAT #ficher1@#aFic#;

     nOk = 1;

     |ABRE #ficher1@;
     #ficher1@#0 = enCodCli;
     |LEE 000#ficher1@.=;
     |SI FSalida ! 0;
         |DDEFECTO #ficher1@;
         #ficher1@#0 = enCodCli;
         |GRABA 020#ficher1@.b;
     |FINSI;

     aAlfa        = ("00000" + enCodCli) % -105;
     #ficher1@#1  = aAlfa;
     #ficher1@#2  = #dsam0000#2;
     #ficher1@#9  = eaEMail;
     #ficher1@#10 = #dsam0000#3;

     |GRABA 020#ficher1@.a;
     |LIBERA #ficher1@;
     |CIERRA #ficher1@;
     |DESCARGA_DEF #ficher1@;
|FPRC;

|PROCESO dpntm005;
     aMas = aBasApli + "dsarchi/def/dpntm005.mas";
     aFic = aBasApli + "dsarchi/fich/";

     |CARGA_DEF aMas, ficher1@;
     |PATH_DAT #ficher1@#aFic#;

     nOk = 1;

     |ABRE #ficher1@;
     #ficher1@#0 = enCodCli;
     #ficher1@#1 = 1;
     |LEE 000#ficher1@.=;
     |SI FSalida ! 0;
         |DDEFECTO #ficher1@;
         #ficher1@#0 = enCodCli;
         #ficher1@#1 = 1;
         |GRABA 020#ficher1@.b;
     |FINSI;

     #ficher1@#2 = eaEMail;
     |GRABA 020#ficher1@.a;
     |LIBERA #ficher1@;
     |CIERRA #ficher1@;
     |DESCARGA_DEF #ficher1@;
|FPRC;

|PROCESO Dsarchi;
     |HAZ dpntm001;
     |HAZ dpntm005;
|FPRC;

|| --------------------------------------------------------------------------

|PROCESO Dato;
     nCampo  = 1;
     IaValor = aValor1<-;
     aLon    = aDato % 0;
     aReg    = "";
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos   = i * 100 + 1;
          aLetra = aDato % nPos;
          |SI aLetra = aTab;
              aValor = aReg;
              aReg = "";
              nCampo = nCampo + 1;
              IaValor = IaValor + 1;
          |SINO;
               aReg = aReg + aLetra;
          |FINSI;

          |SI nCampo > 90; |SAL; |FINSI;     || Por si.... :)
     |SIGUE;

     aValor = aReg;
|FPRC;

|PROCESO LeeCSV;
     aTab = &9;

     aNomFic = PARAMETRO;  |QBF aNomFic;
     |DFICE aPath;
     aDes = aPath + aNomFic + ".ent";
     |XABRE "", aDes, nHandle;
     |SI FSalida ] 0;
          |XLEE nHandle, 250, aDato;    || los nombres
          |XLEE nHandle, 250, aDato;
          |HAZ Dato;
          |XCIERRA nHandle;

          enCodCli = aValor1;
          eaEMail  = aValor2;   |QBF eaEMail;
          eaCIF    = aValor3;   |QBF eaCIF;
          eaNomEmp = aValor4;   |QBF eaNomEmp;
          eaNomCom = aValor5;   |QBF eaNomCom;
          eaCodPos = aValor6;   |QBF eaCodPos;
     |FINSI;
|FPRC;

|PROCESO Compatibilidad;
     nError = 0;
     aOk    = "";

     |SI enCodCli = 0;
         aOk = "CSV sin c¢digo de empresa";
         |HAZ GrabaOK;

         nError = 1;
         |ACABA;
     |FINSI;

     |SI eaEMail = "";
         aOk = "El campo email no tiene contenido";
         |HAZ GrabaOK;

         nError = 1;
         |ACABA;
     |FINSI;

     #dsam0000#0 = enCodCli;
     |LEE 000#dsam0000.=;
     |SI FSalida ! 0;
         aOk = "No existe los datos del cliente en la base da datos (dsam0000)";
         |HAZ GrabaOK;

         nError = 1;
         |ACABA;
     |FINSI;

     aMas = aBasApli + "contagen/def/canempre.mas";
     |XABRE "E", aMas, 0;
     |SI FSalida < 0;
         aOk = "Contabilidad no instalada.";
         |HAZ GrabaOK;

         nError = 1;
         |ACABA;
     |FINSI;

     aMas = aBasApli + "dscomer9/def/agifa041.mas";
     |XABRE "E", aMas, 0;
     |SI FSalida < 0;
         aOk = "Gesti¢n comercial no instalada.";
         |HAZ GrabaOK;

         nError = 1;
         |ACABA;
     |FINSI;

     aMas = aBasApli + "dscarter/def/dscam000.mas";
     |XABRE "E", aMas, 0;
     |SI FSalida < 0;
         aOk = "Gesti¢n de cobros y pagos no instalada.";
         |HAZ GrabaOK;

         nError = 1;
         |ACABA;
     |FINSI;

     aMas = aBasApli + "dsarchi/def/dpntm005.mas";
     |XABRE "E", aMas, 0;
     |SI FSalida < 0;
         aOk = "Gesti¢n documental no instalada.";
         |HAZ GrabaOK;

         nError = 1;
         |ACABA;
     |FINSI;

     aMas = aBasApli + "contagen/def/canempre.mas";
     aFic = aBasApli + "contagen/fich/";
     |CARGA_DEF aMas, ficher1@;
     |PATH_DAT #ficher1@#aFic#;

     |ABRE #agipatro;
     |LEE 000#agipatro.p;
     |SI FSalida = 0;
         #ficher1@#2 = #agipatro#1;
         #ficher1@#3 = #agipatro#2;
     |SINO;
         #ficher1@#2 = 99999;
         #ficher1@#3 = 9;
     |FINSI;
     |CIERRA #agipatro;

     |ABRE #ficher1@;
     |LEE 000#ficher1@.=;
     |SI FSalida ! 0;
         aOk = "Empresa patr¢n el contabilidad inaccesible.";
         |HAZ GrabaOK;
     |FINSI;
     |CIERRA #ficher1@;
     |DESCARGA_DEF #ficher1@;

     |SI aOk ! "";
         nError = 1;
         |ACABA;
     |FINSI;

     aMas = aBasApli + "dscomer9/def/agifa041.mas";
     aFic = aBasApli + "dscomer9/fich/";
     |CARGA_DEF aMas, ficher1@;
     |PATH_DAT #ficher1@#aFic#;

     |ABRE #ficher1@;
     #ficher1@#0 = 99999;
     |LEE 000#ficher1@.=;
     |SI FSalida ! 0;
         aOk = "Empresa patr¢n en gesti¢n comercial inaccesible.";
         |HAZ GrabaOK;
     |FINSI;
     |CIERRA #ficher1@;
     |DESCARGA_DEF #ficher1@;

     |SI aOk ! "";
         nError = 1;
         |ACABA;
     |FINSI;
|FPRC;

|PROCESO GrabaNuevo;
     |SI eaCIF = "";
         aOk = "El campo NIF/CIF no tiene contenido";
         |HAZ GrabaOK;

         nError = 1;
         |ACABA;
     |FINSI;

     |SI eaNomEmp = "";
         aOk = "El nombre empresa no tiene contenido";
         |HAZ GrabaOK;

         nError = 1;
         |ACABA;
     |FINSI;

     |SI eaCodPos = "";
         aOk = "El codigo postal no tiene contenido";
         |HAZ GrabaOK;

         nError = 1;
         |ACABA;
     |FINSI;

     |SELECT #3#dsam0000;
     #dsam0000#3 = eaCIF;
     |LEE 000#dsam0000.=;
     |SI FSalida = 0;
         enCodCli = #dsam0000#0;
         |ACABA;
     |FINSI;

     nOk = 0;
     |SELECT #1#dsam0000;
     |PARA enCodCli = 99999;  |SI enCodCli ] 1;  |HACIENDO enCodCli = enCodCli - 1;
           #dsam0000#0 = enCodCli;
           |LEE 000#dsam0000.=;
           |SI FSalida ! 0;
               nOk = 1;
               |SAL;
           |FINSI;
     |SIGUE;

     |SI nOk = 0;
         aOk = "Hemos superado el n£mero de accesos";
         |HAZ GrabaOK;

         nError = 1;
         |ACABA;
     |FINSI;

     aAlf1 = eaCodPos % 102;
     aAlf2 = eaCodPos % 303;

     #dsam0101#0 = aAlf1;
     #dsam0101#1 = aAlf2;
     |LEE 000#dsam0101.=;
     |SI FSalida ! 0;  |DDEFECTO #dsam0101;  |FINSI;

     #dsam0103#0 = eaCIF;
     |LEE 101#dsam0103.=;
     |SI FSalida ! 0;
         |DDEFECTO #dsam0103;
         #dsam0103#0  = eaCIF;
         #dsam0103#1  = eaNomEmp;
         #dsam0103#4  = aAlf1;
         #dsam0103#5  = aAlf2;
         #dsam0103#6  = #dsam0101#3;
         #dsam0103#7  = #dsam0101#2;
         #dsam0103#12 = #dsam0101#6;
         #dsam0103#13 = #dsam0101#7;
         #dsam0103#14 = #dsam0101#4;
         #dsam0103#15 = #dsam0101#5;

         |GRABA 020#dsam0103.n;
         |LIBERA #dsam0103;
     |FINSI;

     #dsam0000#0 = enCodCli;
     |LEE 101#dsam0000.=;
     |SI FSalida ! 0;
         |DDEFECTO #dsam0000;
         #dsam0000#0  = enCodCli;
         #dsam0000#1  = #dsam0103#1;
         #dsam0000#2  = eaNomCom;
         #dsam0000#3  = eaCIF;
         #dsam0000#19 = #dsam0103#9;
         #dsam0000#20 = #dsam0103#2;
         #dsam0000#21 = #dsam0103#3;
         #dsam0000#22 = #dsam0103#16;
         #dsam0000#23 = #dsam0103#10;
         #dsam0000#24 = #dsam0103#11;
         #dsam0000#25 = #dsam0103#4;
         #dsam0000#26 = #dsam0103#5;
         #dsam0000#27 = #dsam0103#6;
         #dsam0000#28 = #dsam0103#7;
         #dsam0000#29 = #dsam0103#9;
         #dsam0000#30 = #dsam0103#2;
         #dsam0000#31 = #dsam0103#3;
         #dsam0000#32 = #dsam0103#16;
         #dsam0000#33 = #dsam0103#10;
         #dsam0000#34 = #dsam0103#11;
         #dsam0000#35 = #dsam0103#4;
         #dsam0000#36 = #dsam0103#5;
         #dsam0000#37 = #dsam0103#6;
         #dsam0000#38 = #dsam0103#7;
         #dsam0000#39 = #dsam0103#9;
         #dsam0000#40 = #dsam0103#2;
         #dsam0000#41 = #dsam0103#3;
         #dsam0000#42 = #dsam0103#16;
         #dsam0000#43 = #dsam0103#10;
         #dsam0000#44 = #dsam0103#11;
         #dsam0000#45 = #dsam0103#4;
         #dsam0000#46 = #dsam0103#5;
         #dsam0000#47 = #dsam0103#6;
         #dsam0000#48 = #dsam0103#7;
         #dsam0000#54 = #dsam0103#8;
         #dsam0000#86 = #dsam0103#14;
         #dsam0000#87 = #dsam0103#15;
         #dsam0000#98 = #dsam0103#12;
         #dsam0000#98 = #dsam0103#12;

         #dsam0000#85 = "A";
         #dsam0000#5  = "ACTIVA";
         #dsam0000#71 = eaEMail;

         |GRABA 020#dsam0000.n;
         |LIBERA #dsam0000;
     |FINSI;
|FPRC;

|PROGRAMA;
     |DBASS aBasApli;  |QBF aBasApli;

     |SI PARAMETRO = "GECOBOX";
         #dsam0000#0 = enCodCli;
         |LEE 000#dsam0000.=;

         fFecha = ~;
         aPer   = fFecha % -101;
         aEjer  = fFecha % -102;

         |HAZ Contagen;
         |HAZ Dscomer9;
         |HAZ Dscarter;
         |HAZ Dsarchi;

         |ACABA;
     |FINSI;

     |HAZ LeeCSV;

     nGecobox = 0;
     |SI enCodCli = 0;
         nGecobox = 1;

         |HAZ GrabaNuevo;
         |SI nError = 1;
             |ACABA;
         |FINSI;
     |FINSI;

     |HAZ Compatibilidad;

     |SI nError = 1;
         |ACABA;
     |FINSI;

     |SUB_EJECUTA "dsaz0003;SOLOUNCLIENTE";

     #dsam0000#0 = enCodCli;
     |LEE 000#dsam0000.=;

     fFecha = ~;
     aPer   = fFecha % -101;
     aEjer  = fFecha % -102;

     |SI nGecobox = 1;
         |HAZ Contagen;
         |HAZ Dscomer9;
         |HAZ Dscarter;
         |HAZ Dsarchi;

         #dsam0000#0 = enCodCli;
         |LEE 101#dsam0000.=;
         |SI FSalida = 0;
             #dsam0000#65 = "S";

             |GRABA 020#dsam0000.a;
         |FINSI;
     |FINSI;

     aOk = "OK";
     |HAZ GrabaOK;
|FPRO;

|PROCESO T80;  |TIPO 80;
     |ABRET #dsam0000;
     |ABRET #dsam0101;
     |ABRET #dsam0103;
|FPRC;

|PROCESO T90;  |TIPO 90;
     |CIERRAT #dsam0000;
     |CIERRAT #dsam0101;
     |CIERRAT #dsam0103;
|FPRC;
