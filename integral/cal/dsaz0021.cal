|FICHEROS;
     dsaz0021;
     dsam0119;
     dsam0120;
     dsam0121;
     dsam0122;
     dsaw0037,MANTE;
|FIN;

|VARIABLES;
     aAbre          = "";
     aBase          = "";
     aAlfa          = "";
     aAlfa1         = "";
     aAlfa2         = "";
     aEsc1          = "";
     aEsc2          = "";
     aRetu          = "";
     aTecla         = "";
     aFichero       = "";
     aCarga         = "";
     aQueQuiero     = "";
     aTexto         = "";
     aLong          = "";
     aCarac         = "";
     aRefresca      = "";
     aBusca         = "";
     aIPRemoto      = "";
     aNombreIP      = "";
     aRuta          = "";

     nBotonLabel    = 0;
     nHandle        = 0;
     nTree          = -1;
     nPanta         = 0;
     nSubPanta      = 0;
     nLong          = 0;
     nCargar        = 0;
     nPos           = 0;
     nPosi          = 0;
     nBoton1        = 0;
     nBoton2        = 0;
     nBoton3        = 0;
     nSubVent       = 0;
     nCampo         = 0;
     nTCampo        = 0;
     nCarpeta1      = 0;
     nCarpeta2      = 0;
     FEstado        = 0;
     nSubBoton1     = 0;

     {-1}sTree      = 0;
     sT_nID         = 0;
     sT_nOper       = 0;
     sT_aData       = "";

     {-1}aVent      = "";
         aVent1     = 0;
         aVent2     = 0;
         aVent3     = 0;
         aVent4     = 0;
         aVent5     = "";

     &eaRutaOrigen  = "";
     &eaRutaDestino = "";
     &eaFicheroTxt  = "";
     &eaFicheroTgz  = "";
     &eaFicheroTar  = "";
     &eaAlfa        = "";
     &eaUnidadBasico = "";
     &eaDSMAC        = "";
     &enCodigo       = 0;
|FIN;

|PROCESO Tipo12Fw37;  |TIPO 12;
|FPRC;

|PROCESO Tipo0C0Fw37;  |TIPO 0;
     |C_M #dsaw0037#0, 0, aAlfa;
     |SI aAlfa = "N";  |ACABA;  |FINSI;

     #dsam0121#0 = #dsaw0037#0;
     |LEE 000#dsam0121.=;
     |SI FSalida = 0;
         |MENAV "0000El c¢digo de ubicaci¢n ya existe.";
         |ERROR;
         |ACABA;
     |FINSI;
|FPRC;

|PROCESO Browse;
     aRuta = "D" + #dsaw0037#5;
     |BROWSE aRuta;
     aAlfa = aRuta % 280;  |QBF aRuta;
     |SI aAlfa ! "";
         #dsaw0037#5 = aRuta % 280;
     |FINSI;
     |PINTA #dsaw0037#5;
     |ERROR;
|FPRC;

|PROCESO Tipo0C7Fw37;  |TIPO 0;
     aAlfa = #dsaw0037#5;  |QBF aAlfa;
     |RMKDIR aAlfa;
|FPRC;

|PROCESO NuevoTipo;
     |DDEFECTO #dsaw0037;

     |LEE 000#dsam0121.u;
     |SI FSalida ! 0;  |DDEFECTO #dsam0119;  |FINSI;

     |SI #dsam0121#0 > 9999;
         |PARA nCampo = 1;  |SI nCampo [ 10000;  |HACIENDO nCampo = nCampo + 1;
               #dsam0121#0 = nCampo;
               |LEE 000#dsam0121.=;
               |SI FSalida ! 0;
                   |SAL;
               |FINSI;
         |SIGUE;
     |FINSI;

     #dsam0121#0 = #dsam0121#0 + 1;

     |SI #dsam0121#0 > 9999;
         |MENAV "0000 Ya tenemos 9999 tipos creados, borre alguno que no use.";
         |ACABA;
     |FINSI;

     #dsaw0037#0 = #dsam0121#0;

     |VENTANA 0701, 2599, nPanta, 17, "Nuevo Tipo";
     nSubVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PINPA #0#dsaw0037;
     nSubPanta = FSalida;

     |C_M #dsaw0037#0, 1, "S";

     |CONTROL_SIMPLE nSubPanta, "BOTON,{{136}}", 1692, 1695, Browse;
     nSubBoton1 = FSalida;

     |PINDA #0#dsaw0037;

     |ENDATOS #1#dsaw0037;
     FEstado = FSalida;
     |SI FSalida = 0;
         #dsam0121#0 = #dsaw0037#0;
         #dsam0121#1 = #dsaw0037#1;
         #dsam0121#2 = #dsaw0037#2;
         #dsam0121#3 = #dsaw0037#3;

         |GRABA 020#dsam0121.n;
         |LIBERA #dsam0121;

         enCodigo = #dsam0121#0;
         |HAZ CambiaAMac122;

         #dsam0122#0 = #dsam0121#0;
         #dsam0122#1 = eaDSMAC;
         |LEE 101#dsam0122.=;
         |SI FSalida ! 0;
             #dsam0122#0 = #dsam0121#0;
             #dsam0122#1 = eaDSMAC;
             |GRABA 020#dsam0122.b;
         |FINSI;
         #dsam0122#2 = #dsaw0037#4
         #dsam0122#3 = #dsaw0037#5
         |GRABA 020#dsam0122.a;
         |LIBERA #dsam0122;
     |FINSI;

     |FIN_CONTROL_WINDOWS nSubBoton1;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nSubVent;

     |SI FEstado = 0;
         aBusca = #dsaw0037#0;
         |HAZ MontaArbol;
     |FINSI;
|FPRC;

|PROCESO ModificarTipo;
     #dsam0121#0 = aTexto;
     |LEE 101#dsam0121.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |DDEFECTO #dsaw0037;

     #dsaw0037#0  = #dsam0121#0;
     #dsaw0037#1  = #dsam0121#1;
     #dsaw0037#2  = #dsam0121#2;
     #dsaw0037#3  = #dsam0121#3;
     #dsaw0037#4  = aNombreIP;

     enCodigo = #dsam0121#0;
     |HAZ CambiaAMac122;

     #dsam0122#0  = #dsam0121#0;
     #dsam0122#1  = eaDSMAC;
     |LEE 000#dsam0122.=;
     |SI FSalida ! 0;  |DDEFECTO #dsam0122;  |FINSI;

     #dsaw0037#5  = #dsam0122#3;

     |VENTANA 0701, 2599, nPanta, 17, "Modificar Tipo";
     nSubVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |C_M #dsaw0037#0, 1, "N";

     |PINPA #0#dsaw0037;
     nSubPanta = FSalida;

     |CONTROL_SIMPLE nSubPanta, "BOTON,{{136}}", 1692, 1695, Browse;
     nSubBoton1 = FSalida;

     |PINDA #0#dsaw0037;

     |ENDATOS #1#dsaw0037;
     FEstado = FSalida;
     |SI FSalida = 0;
         #dsam0121#1 = #dsaw0037#1;
         #dsam0121#2 = #dsaw0037#2;
         #dsam0121#3 = #dsaw0037#3;

         |GRABA 020#dsam0121.a;
         |LIBERA #dsam0121;

         enCodigo = #dsam0121#0;
         |HAZ CambiaAMac122;

         #dsam0122#0 = #dsam0121#0;
         #dsam0122#1  = eaDSMAC;
         |LEE 101#dsam0122.=;
         |SI FSalida ! 0;
             #dsam0122#0 = #dsam0121#0;
             #dsam0122#1  = eaDSMAC;
             |GRABA 020#dsam0122.b;
         |FINSI;
         #dsam0122#2 = #dsaw0037#4;
         #dsam0122#3 = #dsaw0037#5;
         |GRABA 020#dsam0122.a;
         |LIBERA #dsam0122;
     |FINSI;

     |FIN_CONTROL_WINDOWS nSubBoton1;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nSubVent;

     |SI FEstado = 0;
         aBusca = #dsaw0037#0;
         |HAZ MontaArbol;
     |FINSI;
|FPRC;

|PROCESO dsam0122B;
     |BORRA 020#dsam0122.a;
     |LIBERA #dsam0122;
|FPRC;

|DEFBUCLE dsam0122B;
     #dsam0122#1;
     ;
     #dsam0121#0, "";
     #dsam0121#0, "zzzzzzzzzzzzzzzzzzzz";
     be;
     NULL, dsam0122B;
|FIN;

|PROCESO BorrarTipo;
     #dsam0121#0 = aTexto;
     |LEE 101#dsam0121.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |MENAV "0000Est  usted a punto de borrar una ubicaci¢n, puede perder la ubicaci¢n de ciertos documentos.";
     |CONFI "0000NEst  seguro";
     |SI FSalida ! 0;
         |LIBERA #dsam0121;
         |ACABA;
     |FINSI;

     |BUCLE dsam0122B;

     |BORRA 020#dsam0121.a;
     |LIBERA #dsam0121;

     |LEE 000#dsam0121.p;
     aBusca = #dsam0121#0;
     |HAZ MontaArbol;
|FPRC;

|PROCESO EventoTree;
     |SI FEntrada ! 0;  |ACABA;  |FINSI;

     aAlfa       = Contenido;  |QBF aAlfa;
     aTexto      = "";
     aLong       = aAlfa % 0;
     nLong       = aLong;
     nCargar     = 0;
     |PARA nPosi = 1;  |SI nPosi [ nLong;  |HACIENDO nPosi = nPosi + 1;
           nPos = (nPosi * 100) + 1;
           aCarac = aAlfa % nPos;

           |SI nCargar = 1;
               aTexto = aTexto + aCarac;
           |FINSI;

           |SI aCarac = ":";  nCargar = 1; |FINSI;
     |SIGUE;

     |ESTADO_CONTROL nBoton2, "ENABLE";
     |ESTADO_CONTROL nBoton3, "ENABLE";

     aAlfa = aTexto % 102;
     |SI aTexto = "";
         |ESTADO_CONTROL nBoton2, "DISABLE";
         |ESTADO_CONTROL nBoton3, "DISABLE";
     |FINSI;
|FPRC;

|PROCESO CargaArbol;
     |QBF aAlfa;
     aAlfa = aAlfa + &13 + &10;
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO dsam0122;
     aAlfa     = #dsam0122#1 - ".";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     nCarpeta2 = nCarpeta2  + 1;
     aAlfa1    = #dsam0122#1;  |QBF aAlfa1;
     aAlfa2    = #dsam0122#2;  |QBF aAlfa2;

     |SI aAlfa1 = eaDSMAC;
         aAlfa1 = "---> " + aAlfa1;
     |FINSI;

     aAlfa = nCarpeta1 + "," + nCarpeta2 + ":" + aAlfa1 + " - " + aAlfa2;
     |HAZ CargaArbol;
|FPRC;

|DEFBUCLE dsam0122;
     #dsam0122#1;
     ;
     #dsam0121#0, "";
     #dsam0121#0, "zzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, dsam0122;
|FIN;

|PROCESO dsam0121;
     nCarpeta1 = nCarpeta1  + 1;

     aAlfa = nCarpeta1 + ":" + (("0000" + #dsam0121#0) % -104) + " - " + #dsam0121#1;  |QBF aAlfa;
     aAlfa = aAlfa + " {{" + #dsam0121#0 + "}}";
     |HAZ CargaArbol;

     nCarpeta2 = 0;
     |BUCLE dsam0122;
|FPRC;

|DEFBUCLE dsam0121;
     #dsam0121#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dsam0121;
|FIN;

|PROCESO MontaArbol;
     |SI nTree ! -1;
         |FIN_CONTROL_WINDOWS nTree;
         nTree = -1;
     |FINSI;

     |CONTROL_SIMPLE 0, "LABEL,{{5}}Cargando informaci¢n ...", 1315, 1680, NULL;
     nBotonLabel = FSalida;

     |PTEC 802;  |PAUSA;

     |DBASE aBase;  |QBF aBase;
     aAlfa = aBase + "basura";          |MKDIR aAlfa;

     eaRutaOrigen  = aBase + "basura/";
     eaRutaDestino = eaUnidadBasico + "\documentos";           |RMKDIR eaRutaDestino;
     eaRutaDestino = eaUnidadBasico + "\documentos\tmp";       |RMKDIR eaRutaDestino;
     eaRutaDestino = eaUnidadBasico + "\documentos\tmp\";
     eaFicheroTxt  = "dsaz002101" + Usuario + ".txt";
     eaFicheroTgz  = "dsaz002101" + Usuario + ".tgz";
     eaFicheroTar  = "dsaz002101" + Usuario + ".tar";

     aAbre = eaRutaOrigen + eaFicheroTxt;

     sT_nID   = nTree;
     sT_nOper = 0;

     |XABRE "WB", aAbre, nHandle;

     nCarpeta1 = 0;

     |BUCLE dsam0121;

     |XCIERRA nHandle;

     |HAZ TraeTxtComprimido;
     aAlfa = eaRutaDestino + eaFicheroTxt;

     eaAlfa = "TREECTRL,Tipos de Documentos {{" + aAlfa + "}}";
     |CONTROL_SIMPLE nPanta, eaAlfa, 0702, 2598, EventoTree;
     nTree = FSalida;

     eaFicheroTxt  = "dsaz002101" + Usuario + ".txt";
     aAlfa         = eaRutaDestino + eaFicheroTxt;
     |RFBORRA aAlfa;

     |FIN_CONTROL_WINDOWS nBotonLabel;

     |SI aBusca ! "";
         sT_nID   = nTree;
         sT_nOper = 3;
         sT_aData = "[" + aBusca;
         |ESPECIFICA "COMMANDCONTROL", sTree;
         aRefresca = ":" + FSalida;
     |FINSI;

     sT_nID   = nTree;
     sT_nOper = 2;
     |SI aRefresca = "";
         sT_aData = "1:";
     |SINO;
         sT_aData = aRefresca;
     |FINSI;

     |ESPECIFICA "COMMANDCONTROL", sTree;
     |PULSATECLA;

     |FOCOTECLADO nTree;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     FSalida = 2799;
|FPRC;

|PROCESO dsam0121Chequeo;
     enCodigo = #dsam0121#0;
     |HAZ CambiaAMac122;

     #dsam0122#0 = #dsam0121#0;
     #dsam0122#1  = eaDSMAC;
     |LEE 101#dsam0122.=;
     |SI FSalida ! 0;
         |DDEFECTO #dsam0122;
         #dsam0122#0 = #dsam0121#0;
         #dsam0122#1  = eaDSMAC;
         #dsam0122#2 = aNombreIP;
         |GRABA 020#dsam0122.n;
     |SINO;
         #dsam0122#2 = aNombreIP;
         |GRABA 020#dsam0122.a;
     |FINSI;
     |LIBERA #dsam0122;
|FPRC;

|DEFBUCLE dsam0121Chequeo;
     #dsam0121#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dsam0121Chequeo;
|FIN;

|PROGRAMA;
     |HAZ LeeUnidadBasico;
     |HAZ CogeMac;

     |ABRET #dsam0119;
     |ABRET #dsam0120;
     |ABRET #dsam0121;
     |ABRET #dsam0122;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;   |QBT aIPRemoto;
     |SI aIPRemoto = "";
         aIPRemoto = "LOCAL";
         aNombreIP = "MAQUINA LOCAL";
     |SINO;
         |NOMBRE_IP aIPRemoto, aNombreIP;
     |FINSI;

     |BUCLE dsam0121Chequeo;

     |CLS;
     |CABEZA "Tipos de Documentos";

     |PINPA #0#dsaz0021;
     nPanta = FSalida;

     aAlfa = "LABEL,{{0}} Configuraci¢n de la MAC " + eaDSMAC;
     |CONTROL_SIMPLE nPanta, aAlfa, 0650, 0698, NULL;

     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}Nuevo Tipo", 2634, 2654, NuevoTipo;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Modificar Tipo", 2656, 2676, ModificarTipo;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{199}}Borrar Tipo", 2678, 2698, BorrarTipo;
     nBoton3 = FSalida;

     |HAZ MontaArbol;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}Salir," + nTree;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nTree;
     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;

     |CIERRAT #dsam0119;
     |CIERRAT #dsam0120;
     |CIERRAT #dsam0121;
     |CIERRAT #dsam0122;
|FPRO;
