|FICHEROS;
     dsay0020 #0;
     dsam0017 #17;
     dsaw0042 #100;
     dsam0000;

     :laboral/labempre #200;
     :laboral/labtraba #201;
     :laboral/nomembca #202;
     :laboral/nomembli #203;
|FIN;

|VARIABLES;
     nCampo = 0;
     nAntEmp = 0;
     aAntNif = "";
     nTotal = 0;
     nEmbargado = 0;
     nPendiente = 0;
     nTotalLaboral = 0;
     nEmbargadoLaboral = 0;
     nPendienteLaboral = 0;
     nUltimoAny = 0;
     nEmpresa = 0;
     nTrabajador = 0;
     nError = 0;
     nUltima = 0;
     nDesdeEmpresa = 0;
     nHastaEmpresa = 0;
     aNif = "";
     aAlfa = "";
     aAlfa1 = "";
     swHayAlguno = 0;
     aPathLaboral = "";
|FIN;

/*********************** PROCESOS DE LA PANTALLA ****************************/

|PROCESO ActivaParrilla;
     |SI #0#2 = 0;
          |PARA nCampo = 3; |SI nCampo [ 13; |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;
               |CAMPO_MODIFICA #0nCampo, 1, "N";
               |PINTA #0nCampo;
          |SIGUE;
          |PARA nCampo = 0; |SI nCampo [ 1; |HACIENDO nCampo = nCampo + 1;
               |CAMPO_MODIFICA #0nCampo, 1, "S";
          |SIGUE;
     |SINO;
          |PARA nCampo = 0; |SI nCampo [ 1; |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;
               |CAMPO_MODIFICA #0nCampo, 1, "N";
               |PINTA #0nCampo;
          |SIGUE;
          |PARA nCampo = 2; |SI nCampo [ 13; |HACIENDO nCampo = nCampo + 1;
               |CAMPO_MODIFICA #0nCampo, 1, "S";
          |SIGUE;
     |FINSI;
|FPRC;


/*********************** FIN PROCESOS DE LA PANTALLA ************************/

|PROCESO informe;
     |PRINT;
|FPRC;

|DEFBUCLE informe;
     #100#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, informe;
|FIN;

|PROCESO dsaw0042;
     swHayAlguno = 1;
|FPRC;

|DEFBUCLE dsaw0042;
     #100#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dsaw0042;
|FIN;

|PROCESO GrabaError;
     #labempre#0 = nEmpresa;
     |LEE 000#labempre.=;

     #labtraba#0 = nEmpresa;
     #labtraba#1 = nTrabajador;
     |LEE 000#labtraba.=;

     #100#0 = nEmpresa;
     #100#1 = nTrabajador;
     #100#2 = #labtraba#7;
     #100#3 = #labempre#2;
     aAlfa = #labtraba#2;
     |QBF aAlfa;
     |SI #labtraba#44 = "A";
          #100#4 = aAlfa;
     |SINO;
          #100#4 = aAlfa + " (B)";
     |FINSI;
     |SI nEmpresa = 0;
          |ACABA;
          || salen mogollon de trabajadores por culpa de que se
          || crearon embargos de trabajadores que ya no estaban de alta
          || quito el mensaje ya que depsista mas que otra cosa

          #100#3 = "NIFs con embargo sin trabajador de alta en laboral";

          #dsam0000#0 = #17#0;
          |LEE 000#dsam0000.=;
          |SI FSalida = 0;
               #100#0 = #dsam0000#0;
               #100#3 = #dsam0000#1;
          |FINSI;
     |FINSI;
     |SI nTrabajador = 0;
          #100#4 = "No existe trabajador de alta en laboral con este NIF";
          #100#2 = aAntNif;
     |FINSI;
     |SI nError = 2; |Y #17#10 = 0;
          |ACABA;
          || por lo mismo de antes
     |FINSI;
     #100#5 = nError;
     |GRABA 020#100n;
     |LIBERA #100;
|FPRC;

|PROCESO BuscaEmbIntegral;
     nUltima = #17#2;
|FPRC;

|DEFBUCLE BuscaEmbIntegral;
     #17#1;
     13;
     #nomembca#0, #labtraba#7, 01, "S";
     #nomembca#0, #labtraba#7, 99, "S";
     ;
     NULL, BuscaEmbIntegral;
|FIN;

|PROCESO BuscaEmbLaboral;
     #labtraba#0 = #nomembca#0;
     #labtraba#1 = #nomembca#1;
     |LEE 000#labtraba.=;
     |SI FSalida = 0;
          aNif = #labtraba#7;
          |QBF aNif;
          aNif = (aNif + "            ") % 112
     |SINO;
          |ACABA;
     |FINSI;

     nUltima = 0;

     |BUCLE BuscaEmbIntegral;

     nEmpresa = #labtraba#0;
     nTrabajador = #labtraba#1;

     |SI nUltima = 0;
          || no existe el embargo en la gestion de embargos de la integral
          || pero si en la de laboral
          nError = 2;
          |HAZ GrabaError;
     |FINSI;
|FPRC;

|DEFBUCLE BuscaEmbLaboral;
     #nomembca#1;
     ;
     nDesdeEmpresa, 00001;
     nHastaEmpresa, 99999;
     ;
     NULL, BuscaEmbLaboral;
|FIN;

|PROCESO ActNomembli;
     nTotalLaboral = #nomembli#27;
     nEmbargadoLaboral = #nomembli#28;
     nPendienteLaboral = #nomembli#29;
|FPRC;

|DEFBUCLE ActNomembli;
     #nomembli#1;
     ;
     #nomembca#0, #nomembca#1, nUltimoAny;
     #nomembca#0, #nomembca#1, nUltimoAny;
     ;
     NULL, ActNomembli;
|FIN;

|PROCESO LeeNomembli;
     nUltimoAny = #nomembli#2;
|FPRC;

|DEFBUCLE LeeNomembli;
     #nomembli#1;
     ;
     #nomembca#0, #nomembca#1, 2000;
     #nomembca#0, #nomembca#1, 2999;
     ;
     NULL, LeeNomembli;
|FIN;

|PROCESO CompNomembli;
     nUltimoAny = 0;
     |BUCLE LeeNomembli;

     nTotalLaboral = 0;
     nEmbargadoLaboral = 0;
     nPendienteLaboral = 0;
     |BUCLE ActNomembli;
|FPRC;

|PROCESO CompruebaEmbargo;
     |DDEFECTO #nomembca;
     |DDEFECTO #nomembli;

     #nomembca#0 = nEmpresa;
     #nomembca#1 = nTrabajador;

     |LEE 000#nomembca.=;
     |SI FSalida ! 0;
          nError = 1;              || el embargo no existe en laboral
          |HAZ GrabaError;
     |SINO;
          |HAZ CompNomembli;
          nTotal = nTotal ! 2;
          nTotalLaboral = nTotalLaboral ! 2;
          |SI nTotal ! nTotalLaboral;
               nError = 11;
               |HAZ GrabaError;
          |FINSI;
          nEmbargado = nEmbargado ! 2;
          nEmbargadoLaboral = nEmbargadoLaboral ! 2;
          |SI nEmbargado ! nEmbargadoLaboral;
               nError = 12;
               |HAZ GrabaError;
          |FINSI;
          nPendiente = nPendiente ! 2;
          nPendienteLaboral = nPendienteLaboral ! 2;
          |SI nPendiente ! nPendienteLaboral;
               nError = 13;
               |HAZ GrabaError;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO labtraba;
     nEmpresa = #labtraba#0;
     nTrabajador = #labtraba#1;
|FPRC;

|DEFBUCLE labtraba;
     #labtraba#5;
     44;
     nAntEmp, aAntNif, "A";
     nAntEmp, aAntNif, "A";
     ;
     NULL, labtraba;
|FIN;

|PROCESO BuscaErrores
     nEmpresa = 0;
     nTrabajador = 0;

     |BUCLE labtraba;

     |SI nEmpresa ! 0; |Y nTrabajador ! 0;
          |HAZ CompruebaEmbargo;
     |SINO;
          nError = 3;         || no existe trabajador de alta en laboral
          |HAZ GrabaError;    || evidentemente no encuentra el embargo
     |FINSI;
     nTotal = 0;
     nEmbargado = 0;
     nPendiente = 0;
|FPRC;

|PROCESO dsam0017;
     |SI aAntNif ! ""; |Y aAntNif ! #17#1;
          |HAZ BuscaErrores;
     |FINSI;

     nTotal = nTotal + #17#8;
     nEmbargado = nEmbargado + #17#9;
     nPendiente = nPendiente + #17#10;
     nUltima = #17#2;

     nAntEmp = #17#0;
     aAntNif = #17#1;
     |QBF aAntNif;
     aAntNif = (aAntNif + "            ") % 112;
|FPRC;

|| pongo el campo 10 en el bucle para que no me busque los que tengan embargo cero
|| estos ya me dan igual

|| lo quito, si lo pongo asi no acumulara bien el total de los que
|| tienen embargos ya terminados

|DEFBUCLE dsam0017;
     #dsam0017#1;
     3, 13;
     nDesdeEmpresa, "            ", 01, #0#14, "S";
     nHastaEmpresa, "zzzzzzzzzzzz", 99, #0#15, "S";
     ;
     NULL, dsam0017;
|FIN;

/*
|DEFBUCLE dsam0017;
     #dsam0017#1;
     3, 13, 10;
     nDesdeEmpresa, "            ", 01, #0#14, "S", 0.01;
     nHastaEmpresa, "zzzzzzzzzzzz", 99, #0#15, "S", 99999999.99;
     ;
     NULL, dsam0017;
|FIN;
*/

|PROCESO DesdeHasta;
     nDesdeEmpresa = #0#0;
     nHastaEmpresa = #0#1;
     |BUCLE dsam0017;

     |HAZ BuscaErrores;  || para el ultimo

     |BUCLE BuscaEmbLaboral;
|FPRC;

|PROCESO Parrilla;
     |PARA nCampo = 2; |SI nCampo [ 13; |HACIENDO nCampo = nCampo + 1;
          nDesdeEmpresa = #0nCampo;
          nHastaEmpresa = #0nCampo;
          |BUCLE dsam0017;
     |SIGUE;
|FPRC;

|PROGRAMA;
     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |DBASS aAlfa;
          |QBF aAlfa;
          aPathLaboral = aAlfa + "laboral/fich/";
          |PATH_DAT #200 aPathLaboral;
          |PATH_DAT #201 aPathLaboral;
          |PATH_DAT #202 aPathLaboral;
          |PATH_DAT #203 aPathLaboral;

          |ABRE #dsam0000;
          |ABRE #dsam0017;
          |ABRE #nomembca;
          |ABRE #nomembli;
          |ABRE #labtraba;
          |ABRE #labempre;
          aAlfa1 = Usuario; |QBT aAlfa1;    aAlfa1 = ("c" + aAlfa1) % 108;
          |NOME_DAT #100 aAlfa1;
          |DELINDEX #100;
          |ABRE #100;
          |SI #0#2 ! 00000;
               |HAZ Parrilla;
          |SINO;
               |HAZ DesdeHasta;
          |FINSI;

          swHayAlguno = 0;
          |BUCLE dsaw0042;
          |SI swHayAlguno ! 0;
               Impresora = "CrystalReport";
               |IMPRE -1;
               |INFOR "dsay0020";

               |BUCLE informe;

               |PIEINF;
               |FININF;
               |FINIMP;
          |SINO;
               aAlfa1 = "    No se han encontrado diferencias entre la gestión ";
               aAlfa1 = aAlfa1 + "de embargos integral y laboral";
               |MENAV aAlfa1;
          |FINSI;

          |CIERRA #100;
          |DELINDEX #100;
          |CIERRA #labempre;
          |CIERRA #labtraba;
          |CIERRA #nomembli;
          |CIERRA #nomembca;
          |CIERRA #dsam0017;
          |CIERRA #dsam0000;
     |FINSI;
|FPRO;
