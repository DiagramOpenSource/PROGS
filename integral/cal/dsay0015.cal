|FICHEROS;
     dsay0015 #0;
     dsam0000 #1;
     :/basica/def/agitab04 #2;
     :/basica/def/agitab03 #3;
     dsam0008 #8;

     :/contagen/def/canempre  #800;      || Empresa Contabilidad
     :/contagen/def/camaasic  #801;      || Empresa Contabilidad

     :/laboral/def/labempre   #500;      || Empresa Laboral
     :/laboral/def/labtraba   #501;      || trabajadores
     :/laboral/def/nomhicca   #511;      || historico cabs, mensuales y atrasos
|FIN;

|VARIABLES;
     nCampo = 0;
     nSwHay = 0;
     nSwSi  = 0;

     aDSit = "";
     aHSit = "";
     nDTCl = 0;
     nHTCl = 0;
     nDTEm = 0;
     nHTEm = 0;

     nPara1   = 0;
     nNume1   = 0;
     aAlfa    = "";
     aAlfa1   = "";
     aAlfa2   = "";
     aAlfa3   = "";
     nAny     = 0;
     nEjer    = 0;
     nDMes    = 0;
     nHMes    = 0;
     fFecha   = @;
     fFecin1  = @;
     fFecini  = @;
     fFecfin  = @;
     nMulti   = 0;
     nImporte = 0;

     aBase    = "";
     aPath    = "";
|FIN;

|PROCESO Seleccion;
     |SI #0#2 = "D";
         |C_M #0#3, 1, "S";
         |C_M #0#4, 1, "S";
         |PARA nCampo = 5;  |SI nCampo [ 16;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";  #0nCampo = -1;  |PINTA #0nCampo;
         |SIGUE;
     |SINO;
         |C_M #0#3, 1, "N";  #0#3 = 0;      |PINTA #0#3;
         |C_M #0#4, 1, "N";  #0#4 = 99;     |PINTA #0#4;
         |PARA nCampo = 5;  |SI nCampo [ 16;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = -1;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 16;  |HACIENDO nCampo = nCampo + 1;
               #0Campo = -1;  |C_M #0nCampo, 1, "N";  |PINTA #0Campo;
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 16;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S"; |PINTA #0Campo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO Leoagitab03; |TIPO 6;
 |SI #0Campo ! 0;
     |ABRE #3;
     #3#0 = #0Campo;
     |LEE 001#3=;
     |SI FSalida ! 0;
         |MENAV "    Error, no existe Concepto";
         |ERROR;
     |FINSI;
     |CIERRA #3;
 |FINSI;
|FPRC;

|PROCESO elmes; |TIPO 0;
     nCampo = Campo + 2;

     |SI Campo = 25; |Y #0#24 > #0#25;
         |MENAV "    Mes Incorrecto";
         |ERROR;
     |FINSI;

     |SI #0Campo =  1; #0nCampo = "ENERO     "; |FINSI;
     |SI #0Campo =  2; #0nCampo = "FEBRERO   "; |FINSI;
     |SI #0Campo =  3; #0nCampo = "MARZO     "; |FINSI;
     |SI #0Campo =  4; #0nCampo = "ABRIL     "; |FINSI;
     |SI #0Campo =  5; #0nCampo = "MAYO      "; |FINSI;
     |SI #0Campo =  6; #0nCampo = "JUNIO     "; |FINSI;
     |SI #0Campo =  7; #0nCampo = "JULIO     "; |FINSI;
     |SI #0Campo =  8; #0nCampo = "AGOSTO    "; |FINSI;
     |SI #0Campo =  9; #0nCampo = "SEPTIEMBRE"; |FINSI;
     |SI #0Campo = 10; #0nCampo = "OCTUBRE   "; |FINSI;
     |SI #0Campo = 11; #0nCampo = "NOVIEMBRE "; |FINSI;
     |SI #0Campo = 12; #0nCampo = "DICIEMBRE "; |FINSI;

     |PINTA #0nCampo;
     |SI Campo = 24;
         #0#25 = #0#24; |PINTA #0#25;
         #0#27 = #0#26; |PINTA #0#27;
     |FINSI;
|FPRC;


|| ************************************************************************
|PROCESO LeoCuotas;

|SI #8#2 ! 0;
 |SI #8#2 = #0#28; |O #8#2 = #0#29; |O #8#2 = #0#30; |O #8#2 = #0#31;
    |O #8#2 = #0#44; |O #8#2 = #0#45; |O #8#2 = #0#46; |O #8#2 = #0#47;

     |SI #8#11 > "01.01.0000"; |Y #8#11 > fFecfin; |ACABA; |FINSI;
     |SI #8#12 > "01.01.0000"; |Y #8#12 < fFecfin; |ACABA; |FINSI;

     nMulti = 12;
     |SI #8#6 = 13; nMulti =  1;  |FINSI;
     |SI #8#6 = 14; nMulti =  2;  |FINSI;
     |SI #8#6 = 15; nMulti =  3;  |FINSI;
     |SI #8#6 = 16; nMulti =  4;  |FINSI;
     |SI #8#6 = 17; nMulti =  6;  |FINSI;
     nImporte = #8#5 / nMulti;

     |SI #8#2 = #0#28; |O #8#2 = #0#29; |O #8#2 = #0#44; |O #8#2 = #0#45;
         #0#33 = #0#33 + nImporte;
     |FINSI;
     |SI #8#2 = #0#30; |O #8#2 = #0#31; |O #8#2 = #0#46; |O #8#2 = #0#47;
         #0#35 = #0#35 + nImporte;
     |FINSI;
 |FINSI;
|FINSI;

|FPRC;

|DEFBUCLE LeoCuotas;
 #8#1;
 ;
 #1#0, 01;
 #1#0, 99;
 ;
 NULL, LeoCuotas;
|FIN;

|PROCESO LeoAstos;
 |SI #801#1 ] fFecini;
     #0#32 = #0#32 + 1;
 |FINSI;
  #0#39 = #0#39 + 1;
|FPRC;

|DEFBUCLE LeoAstos;
 #801#1;
 ;
 00, fFecin1, 000001;
 99, fFecfin, 999999;
 e;
 NULL, LeoAstos;
|FIN;

|PROCESO CuentoAstos;
  |DBASS aBase; |QBT aBase;
  aBase = aBase + "contagen/fich/";
  aAlfa1 = #800#2; aAlfa1 = ("0000" + aAlfa1) % -105;
  aAlfa2 = #800#3; aAlfa2 = ("0"    + aAlfa2) % -101;
  aPath = aBase + aAlfa1 + aAlfa2 + "/";

  |PATH_DAT #801 aPath;

  |BUCLE LeoAstos;

|FPRC;

|PROCESO LeoNominas;
 |SI #511#2 ] nDMes;
     #0#34 = #0#34 + 1;
 |FINSI;
 #0#40 = #0#40 + 1;
|FPRC;

|DEFBUCLE LeoNominas;
 #511#1;
 ;
 #1#0, 00001, nAny,     1, 0;
 #1#0, 99999, nAny, nHMes, 9;
 e;
 NULL, LeoNominas;
|FIN;

|PROCESO LeoTrabajadores;


 |SI #501#36 ! "         "; |Y #501#36 ! "..........";  ||Alta
     fFecha = #501#36;
     |SI fFecha ] fFecini; |Y fFecha [ fFecfin;
         #0#36 = #0#36 + 1;
     |FINSI;
     |SI fFecha ] fFecin1; |Y fFecha [ fFecfin;
         #0#41 = #0#41 + 1;
     |FINSI;
 |FINSI;

 |SI #501#37 ! "         "; |Y #501#37 ! "..........";  ||Baja
     fFecha = #501#37;
     |SI fFecha ] fFecini; |Y fFecha [ fFecfin;
         #0#37 = #0#37 + 1;
     |FINSI;
     |SI fFecha ] fFecin1; |Y fFecha [ fFecfin;
         #0#42 = #0#42 + 1;
     |FINSI;
 |FINSI;

 #0#38 = #0#36 + #0#37;
 #0#43 = #0#41 + #0#42;
|FPRC;

|DEFBUCLE LeoTrabajadores;
 #501#1;
 ;
 #1#0, 00001;
 #1#0, 99999;
 e;
 NULL, LeoTrabajadores;
|FIN;

|PROCESO CuentoNominas;
  |DBASS aBase; |QBT aBase;
  aPath = aBase + "laboral/fich/";

  |PATH_DAT #511 aPath;

  |BUCLE LeoTrabajadores;

  |BUCLE LeoNominas;
|FPRC;

|PROCESO Imprimelos;
     |SI #0#2 = "P";
         nSwSi = 0;
         |PARA nPara1 = 5; |SI nPara1 [ 16; |HACIENDO nPara1 = nPara1 + 1;
               |SI #1#9 = #0nPara1;
                   nSwSi = 1;
                   |SAL;
               |FINSI;
         |SIGUE;

         |SI nSwSi = 0; |ACABA; |FINSI;
     |FINSI;

     |PDEFECTO #0, 32, 44;

     |SELECT #4#800;
     #800#2  = #1#0;
     #800#26 = nAny;
     |LEE 001#800=;
     |SI FSalida = 0;
         |HAZ CuentoAstos;
     |FINSI;
     |SELECT #1#800;

     #500#0 = #1#0;
     |LEE 001#500=;
     |SI FSalida = 0;
         |HAZ CuentoNominas;
     |FINSI;

     |BUCLE LeoCuotas;

     |PRINT;
     nSwHay = 1;
|FPRC;

|DEFBUCLE porNombre;
     #1#2;
     0, 9, 50, 85;
     #0#17, #0#0, nDTEm, nDTCl, aDSit;
     #0#18, #0#1, nHTEm, nHTCl, aHSit;
     ;
     NULL, Imprimelos;
|FIN;

|DEFBUCLE porCodigo;
     #1#1;
     1, 9, 50, 85;
     #0#0, #0#17, nDTEm, nDTCl, aDSit;
     #0#1, #0#18, nHTEm, nHTCl, aHSit;
     ;
     NULL, Imprimelos;
|FIN;

|PROCESO Tipo3;  |TIPO 3;

     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |INFOR "dsay0015";
     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

 || ...  Situacion Activa/Inactiva
     |SI #0#20 = "X";
         aDSit = "A"; aHSit = "Z";
     |SINO;
          aDSit = #0#20; aHSit = #0#20;
     |FINSI;

 || ... Tipo Cliente
      |SI #0#19 = "X";
          nDTCl = 0; nHTCl = 2;
      |SINO;
          nDTCl = #0#19; nHTCl = #0#19;
      |FINSI;

 || ... Tipo Empresa
      |SI #0#2 = "D";
          nDTEm = #0#3; nHTEm = #0#4;
      |SINO;
          nDTEm = 0; nHTEm = 99;
      |FINSI;

      nEjer  = #0#23;
      nDMes  = #0#24;
      nHMes  = #0#25;

      aAlfa1  = #0#23;
      aAlfa2  = "01.01." + aAlfa1;
      fFecin1 = aAlfa2;

      aAlfa2 = #0#24; aAlfa2 = ("00" + aAlfa2) % -102;
      aAlfa2 = "01." + aAlfa2 + "." + aAlfa1; fFecini = aAlfa2;
      aAlfa2 = #0#25; aAlfa2 = ("00" + aAlfa2) % -102;
      aAlfa3 = "31";
      |SI #0#25 = 4; |O #0#25 = 6; |O #0#25 = 9; |O #0#25 = 11; aAlfa3 = 30; |FINSI;
      |SI #0#25 = 2;
          aAlfa3 = "28";
          |SI #0#23 = 2000; |O #0#23 = 2004; |O #0#23 = 2008; |O #0#23 = 2012;
               |O #0#23 = 2016; |O #0#23 = 2020;
                  aAlfa3 = "29";
          |FINSI;
      |FINSI;
      aAlfa2 = aAlfa3 + "." + aAlfa2 + "." + aAlfa1; fFecfin = aAlfa2;
      aAlfa1 = aAlfa1 % -102;
      nAny   = aAlfa1;

      nSwHay = 0;
      |ABRE #500;
      |ABRE #800;
      |SI #0#21 = 1;
          |BUCLE porCodigo;
      |SINO;
          |BUCLE porNombre;
      |FINSI;
      |CIERRA #800;
      |CIERRA #500;

      |SI  nSwHay = 1;
           |PIEINF;
      |FINSI;

      |FININF;
      |FINIMP;
|FPRC;

|PROGRAMA;
  |ABRE #0;
  |LEE 101#0p;
  |SI FSalida ! 0;
      |DDEFECTO #0;
      |GRABA 020#0b;
  |FINSI;
  |CLS;
  |PINPA #0#0;
  |PINDA #0#0;

  |ENDATOS #1#0;
  |SI FSalida = 0;
      |PDEFECTO #0, 32, 44;
      |GRABA 020#0a;
  |FINSI;
  |LIBERA #0;
  |CIERRA #0;
|FPRO;
