|FICHEROS;
    dsboez03 #0;

    dsboem03 #10;
    dsboem00 #11;
    dsboem01 #12;
    dsboem02 #13;
|FIN;

|VARIABLES;
   aAlfa       = "";
   aAlfa1      = "";
   aAlfa2      = "";
   aAlfa3      = "";
   aAlfaClv    = "";
   aClave      = "";
   aPath       = "";
   aIPDSCorreo = "";
   aServidor   = "";
   aUsuario    = "";
   aFicheros   = "";
   aFicOrigen  = "";
   aDirDestino = "";
   aDirOrigen  = "";
   aLink       = "";
   aAsunto     = "";
   FEstado     = "";
   aPuntos     = "";
   aIni        = "";
   aTexto      = "";
   aMensaje    = "";
   aReferencia = "";

{1}aContiene   = "";

   fFecha   = @;

   nNumChar = 0;
   nCont    = 0;
   nCalc    = 0;
   nCalc1   = 0;
   nHandle  = 0;
   nHandle1 = 0;
   nHanErr  = 0;
   nSwErr   = 0;
   nLong    = 0;
   nDato    = 0;

   aNumRegistro = "";
   nNumChars  = 0;
   nNumVacios = 0;
   nRetardo   = 0;

   aReg  = "";
   aReg1 = "";
   aTab  = "";
   aUltChar = "";

   nContVeces = 0;
|FIN;

|PROCESO Encriptado;
   aAlfaClv = "";
   aAlfa = aClave; |QBF aAlfa;
   aAlfa = aAlfa % 0; nNumChar = aAlfa;
   |PARA nCont = 1; |SI nCont [ nNumChar; |HACIENDO nCont = nCont + 1;
      nCalc = (nCont * 100) + 1; aAlfa = aClave % nCalc;
      nCalc = &aAlfa;
      |SI nCalc > 79;
         nCalc = 79 - (nCalc - 79);
      |SINO;
         nCalc = 79 + (79 - nCalc);
      |FINSI;
      aAlfaClv = aAlfaClv + &nCalc;
   |SIGUE;
   aClave = aAlfaClv;
|FPRC;

|PROCESO BuscaDireccion;
   aAlfa = aDirDestino; |QBF aAlfa;
   aAlfa = aAlfa + "_cuerpo_.txt";
   |XABRE "", aAlfa, nHandle;
   |SI FSalida = 0;
      FSalida = 1;
      |PARA; |SI FSalida ! 0; |HACIENDO;
         |XLEE nHandle, 250, aAlfa;

         aAlfa1 = aAlfa - "http://www.boe.es/boe";
         |SI FSalida ! 0; |SAL; |FINSI;
      |SIGUE;

      aLink  = "";
      aAlfa1 = aAlfa - "http://www.boe.es/boe";
      |SI FSalida ! 0;
         nCalc = FSalida;
         nCalc  = nCalc - 21;
         aAlfa1 = aAlfa - ".pdf";
         |SI FSalida ! 0;
            nCalc1 = FSalida; nCalc1 = ((nCalc1 / 100 ) ! 0) + 4;
            nCalc = nCalc + nCalc1;
            aLink = aAlfa % nCalc;
         |FINSI;
      |FINSI;

      |XCIERRA nHandle;
   |FINSI;
|FPRC;

|PROCESO RecogeCorreo;
   |SI PARAMETRO = "";
      |BLIN 23; |ATRI I; |PINTA 2302, "Chequeando correo ...."; |ATRI N;
      |PULSATECLA;
   |FINSI;

   ||SI es un windows, instalo el pdftotext y el wget
   |QUE_SISTEMA aAlfa; |QBF aAlfa;
   |SI aAlfa = "ESDOS";
      |DBASE aAlfa1; |QBF aAlfa1; aAlfa2 = aAlfa1;
      aContiene1 = aAlfa1 + "plantillas/pdftotext.tgz";
      |ESPECIFICA "MD5", aContiene; aAlfa1 = FSalida;

      aContiene1 = aAlfa2 + "boecom/tmp/pdftotext.tgz";
      |ESPECIFICA "MD5", aContiene; aAlfa2 = FSalida;

      |SI aAlfa1 ! aAlfa2;
         |DBASE aAlfa1; |QBF aAlfa1; aAlfa2 = aAlfa1;
         aAlfa1 = aAlfa1 + "plantillas/pdftotext.tgz";
         aAlfa2 = aAlfa2 + "boecom/tmp/"; |MKDIR aAlfa2;
         aAlfa2 = aAlfa2 + "pdftotext.tgz";
         |COPIA_FICHERO aAlfa1, aAlfa2;

         aAlfa = aAlfa2; aAlfa = aAlfa - ".tgz";
         |DESCOMPRIME aAlfa;
         |DBASE aAlfa1; |QBF aAlfa1;
         aAlfa1 = aAlfa1 + "boecom/tmp/"; aAlfa = aAlfa + ".tar";
         |DETAR aAlfa, aAlfa1;
         aAlfa2 = aAlfa2 - ".tgz"; aAlfa2 = aAlfa2 + ".tar"; |FBORRA aAlfa2;
      |FINSI;
   |SINO;
      |DBASE aAlfa1; |QBF aAlfa1; aAlfa2 = aAlfa1;
      aContiene1 = aAlfa1 + "plantillas/pdf2txt.sh";
      |ESPECIFICA "MD5", aContiene; aAlfa1 = FSalida;

      aContiene1 = aAlfa2 + "boecom/tmp/pdftotext.tgz";
      |ESPECIFICA "MD5", aContiene; aAlfa2 = FSalida;

      |SI aAlfa1 ! aAlfa2;
         |DBASE aAlfa1; |QBF aAlfa1; aAlfa2 = aAlfa1;
         aAlfa1 = aAlfa1 + "plantillas/pdf2txt.sh";
         aAlfa2 = aAlfa2 + "boecom/tmp/"; |MKDIR aAlfa2;
         aAlfa2 = aAlfa2 + "pdf2txt.sh";
         |COPIA_FICHERO aAlfa1, aAlfa2;
         aAlfa = "chmod 755 " + aAlfa2;
         |SYSTEM aAlfa;

         |DBASE aAlfa1; |QBF aAlfa1; aAlfa2 = aAlfa1;
         aAlfa1 = aAlfa1 + "plantillas/wget.sh";
         aAlfa2 = aAlfa2 + "boecom/tmp/"; |MKDIR aAlfa2;
         aAlfa2 = aAlfa2 + "wget.sh";
         |COPIA_FICHERO aAlfa1, aAlfa2;
         aAlfa = "chmod 755 " + aAlfa2;
         |SYSTEM aAlfa;
      |FINSI;
   |FINSI;

   aIPDSCorreo = #dsboem03#1 + "." + #dsboem03#2 + "." + #dsboem03#3 + "." + #dsboem03#4;

   || El asterisco le indica al DSCORREO que descargue todos los ficheros ...
   ||    que vengan adjuntos en los correos. Si no, solo descarga el ultimo
   aServidor = "*" + #dsboem03#8;     |QBF aServidor;
   aUsuario  = #dsboem03#6;           |QBF aUsuario;
   aClave    = #dsboem03#7; |HAZ Encriptado;

   fFecha = ~; |HORA aAlfa; |QBF aAlfa; FSalida = 1;
   |PARA; |SI FSalida ! 0; |HACIENDO;
      aAlfa = aAlfa - ":";
   |SIGUE;
   aAlfa1 = fFecha; |QBF aAlfa1;
   aAlfa1 = (aAlfa1 % 0704) + (aAlfa1 % 0402) + (aAlfa1 % 0102);

   |DBASE aPath; |QBF aPath; aPath = aPath + "boecom/correo/rec/"; |MKDIR aPath;
   aDirDestino = aPath + aAlfa1 + aAlfa + "/";

   |DSCORREO_RECIBE aIPDSCorreo, aServidor, aUsuario, aClave, aPath, aFicheros;
   FEstado = FSalida; |QBF FEstado; aAlfa = FEstado % 0102;
   FSalida = aAlfa;
|FPRC;

|PROCESO ReubicaContenido;
   aFicOrigen = aPath + "*";
   |_PDIR aFicOrigen;
   |PARA; |SI FSalida = 0; |HACIENDO;
      aAlfa = aFicOrigen; aAlfa = aAlfa - aPath;
      aAlfa2 = aAlfa - ".";
      |SI FSalida ! 0;
         |MKDIR aDirDestino;
         aAlfa2 = aDirDestino + aAlfa;
         |COPIA_FICHERO aFicOrigen, aAlfa2;
         |FBORRA aFicOrigen;
      |FINSI;
      |_SDIR aFicOrigen;
   |SIGUE;
|FPRC;

|PROCESO DescargaBOE;
   |DBASE aAlfa; |QBF aAlfa; aAlfa = aAlfa + "boecom/tmp/"; |MKDIR aAlfa;
   |QUE_SISTEMA aAlfa2; |QBF aAlfa2;
   |SI aAlfa2 = "ESDOS";
      || Descargamos el pdf del BOE
      |DBASE aAlfa1; |QBF aAlfa1; aAlfa1 = aAlfa1 + "boecom/tmp/"; |MKDIR aAlfa1;
      aAlfa1 = aAlfa1 + "wget -qbc " + aLink + " -P " + aAlfa;
      || Descargamos el pdf del BOE
      aAlfa = "cmd " + &34 + "/c START " + aAlfa1; |QBF aAlfa;
      |SYSTEM aAlfa;
      || Lo transformamos a txt y lo metemos con toda la info del correo
      |DBASE aAlfa; |QBF aAlfa; aAlfa = aAlfa + "boecom/"; |MKDIR aAlfa;
      |SLEEP 1;
      aAlfa1 = "dir " + aAlfa + "tmp/ > " + aAlfa + "pp.txt"; |SYSTEM aAlfa1;
      |PARA; |SI; |HACIENDO;
         |SI PARAMETRO = "";
            |BLIN 23;
            aAlfa1 = " Descargando " + aLink + aPuntos;
            |ATRI I; |PINTA 2302, aAlfa1; |ATRI N;
            |SI aPuntos = ".....";
               aPuntos = "";
            |SINO;
               aPuntos = aPuntos + ".";
            |FINSI;
            |PULSATECLA;
         |FINSI;
         |SLEEP 1;
         aAlfa1 = "dir " + aAlfa + "tmp/ > " + aAlfa + "pp1.txt"; |SYSTEM aAlfa1;
         aContiene = aAlfa + "pp.txt";
         |ESPECIFICA "MD5", aContiene;
         aAlfa1 = FSalida; |QBF aAlfa1;
         aContiene = aAlfa + "pp1.txt";
         |ESPECIFICA "MD5", aContiene;
         aAlfa2 = FSalida; |QBF aAlfa1;
         |SI aAlfa1 ! aAlfa2;
            aAlfa1 = aAlfa + "pp1.txt";
            aAlfa2 = aAlfa + "pp.txt";
            |COPIA_FICHERO aAlfa1, aAlfa2;
         |SINO;
            |SI nContVeces < 3;
               nContVeces = nContVeces + 1;
            |SINO;
               |SAL;
            |FINSI;
         |FINSI;
      |SIGUE;
   |SINO;
      || Descargamos el pdf del BOE
      |DBASE aAlfa1; |QBF aAlfa1; aAlfa1 = aAlfa1 + "boecom/tmp/"; |MKDIR aAlfa1;
      aAlfa1 = aAlfa1 + "wget.sh " + aLink + " " + aAlfa;
      |SYSTEM aAlfa1;
      || Lo transformamos a txt y lo metemos con toda la info del correo
      |DBASE aAlfa; |QBF aAlfa; aAlfa = aAlfa + "boecom/"; |MKDIR aAlfa;
      |SLEEP 1;
      aAlfa1 = "ls -l -a " + aAlfa + "tmp/ > " + aAlfa + "pp.txt"; |SYSTEM aAlfa1;
      |PARA; |SI; |HACIENDO;
         |SI PARAMETRO = "";
            |BLIN 23;
            aAlfa1 = " Descargando " + aLink + aPuntos;
            |ATRI I; |PINTA 2302, aAlfa1; |ATRI N;
            |SI aPuntos = ".....";
               aPuntos = "";
            |SINO;
               aPuntos = aPuntos + ".";
            |FINSI;
            |PULSATECLA;
         |FINSI;
         |SLEEP 1;
         aAlfa1 = "ls -l -a " + aAlfa + "tmp/ > " + aAlfa + "pp1.txt"; |SYSTEM aAlfa1;
         aContiene = aAlfa + "pp.txt";
         |ESPECIFICA "MD5", aContiene;
         aAlfa1 = FSalida; |QBF aAlfa1;
         aContiene = aAlfa + "pp1.txt";
         |ESPECIFICA "MD5", aContiene;
         aAlfa2 = FSalida; |QBF aAlfa1;
         |SI aAlfa1 ! aAlfa2;
            aAlfa1 = aAlfa + "pp1.txt";
            aAlfa2 = aAlfa + "pp.txt";
            |COPIA_FICHERO aAlfa1, aAlfa2;
         |SINO;
            |SI nContVeces < 3;
               nContVeces = nContVeces + 1;
            |SINO;
               |SAL;
            |FINSI;
         |FINSI;
      |SIGUE;
   |FINSI;
   aAlfa1 = aAlfa + "pp.txt";  |FBORRA aAlfa1;
   aAlfa1 = aAlfa + "pp1.txt"; |FBORRA aAlfa1;
|FPRC;

|PROCESO TransformaTxt;
   |SI PARAMETRO = "";
      |BLIN 23; |ATRI I; |PINTA 2302, "Transformando PDF a txt ...."; |ATRI N;
   |FINSI;

   |QUE_SISTEMA aAlfa2; |QBF aAlfa2;
   |SI aAlfa2 = "ESDOS";
      || Lo transformamos a txt y lo metemos con toda la info del correo
      aAlfa = aLink; aAlfa1 = "";
      aAlfa2 = aAlfa % -101; aAlfa  = aAlfa % -299;
      |PARA; |SI aAlfa2 ! "/"; |Y aAlfa2 ! "\"; |HACIENDO;
         aAlfa1 = aAlfa2 + aAlfa1;
         aAlfa2 = aAlfa % -101; aAlfa  = aAlfa % -299;
      |SIGUE;
      |DBASE aAlfa; |QBF aAlfa; aAlfa = aAlfa + "boecom/tmp/";
      aAlfa2 = aAlfa; aAlfa = aAlfa + aAlfa1;
      aAlfa = "cmd " + &34 + "/c START " + aAlfa2 + "pdftotext.exe -layout " + aAlfa; |QBF aAlfa;
      aAlfa = aAlfa + " " + aDirDestino; |QBF aAlfa;
      aAlfa = aAlfa + "pdf.txt" + &34;
      |SYSTEM aAlfa;
   |SINO;
      aAlfa = aLink; aAlfa1 = "";
      aAlfa2 = aAlfa % -101; aAlfa  = aAlfa % -299;
      |PARA; |SI aAlfa2 ! "/"; |Y aAlfa2 ! "\"; |HACIENDO;
         aAlfa1 = aAlfa2 + aAlfa1;
         aAlfa2 = aAlfa % -101; aAlfa  = aAlfa % -299;
      |SIGUE;
      |DBASE aAlfa2; |QBF aAlfa2; aAlfa2 = aAlfa2 + "boecom/tmp/";
      aAlfa = aAlfa2 + "pdf2txt.sh " + aAlfa2 + aAlfa1; |QBF aAlfa;
      aAlfa = aAlfa + " " + aDirDestino + "pdf.txt";
      |SYSTEM aAlfa;
   |FINSI;

   aAlfa = aDirDestino; |QBF aAlfa;
   |SLEEP 1;
   |DBASE aAlfa3; |QBF aAlfa3; aAlfa3 = aAlfa3 + "boecom/tmp/";

   |QUE_SISTEMA aAlfa2; |QBF aAlfa2;
   |SI aAlfa2 = "ESDOS";
      aAlfa1 = "dir " + aAlfa + " > " + aAlfa3 + "pp.txt";
   |SINO;
      aAlfa1 = "ls -l -a " + aAlfa + " > " + aAlfa3 + "pp.txt";
   |FINSI;
   |SYSTEM aAlfa1;

   nContVeces = 0;
   |PARA; |SI; |HACIENDO;
      |SI PARAMETRO = "";
         |BLIN 23;
         aAlfa1 = "Transformando PDF a txt " + aPuntos;
         |ATRI I; |PINTA 2302, aAlfa1; |ATRI N;
         |SI aPuntos = ".....";
            aPuntos = "";
         |SINO;
            aPuntos = aPuntos + ".";
         |FINSI;
         |PULSATECLA;
      |FINSI;
      |SLEEP 1;
      |QUE_SISTEMA aAlfa2; |QBF aAlfa2;
      |SI aAlfa2 = "ESDOS";
         aAlfa1 = "dir " + aAlfa + " > " + aAlfa3 + "pp1.txt";
      |SINO;
         aAlfa1 = "ls -l -a " + aAlfa + " > " + aAlfa3 + "pp1.txt";
      |FINSI;
      |SYSTEM aAlfa1;
      aContiene = aAlfa3 + "pp.txt";
      |ESPECIFICA "MD5", aContiene;
      aAlfa1 = FSalida; |QBF aAlfa1;
      aContiene = aAlfa3 + "pp1.txt";
      |ESPECIFICA "MD5", aContiene;
      aAlfa2 = FSalida; |QBF aAlfa1;
      |SI aAlfa1 ! aAlfa2;
         aAlfa1 = aAlfa3 + "pp1.txt";
         aAlfa2 = aAlfa3 + "pp.txt";
         |COPIA_FICHERO aAlfa1, aAlfa2;
      |SINO;
         |SI nContVeces < 3;
            nContVeces = nContVeces + 1;
         |SINO;
            |SAL;
         |FINSI;
      |FINSI;
   |SIGUE;
   aAlfa1 = aAlfa3 + "pp.txt";  |FBORRA aAlfa1;
   aAlfa1 = aAlfa3 + "pp1.txt"; |FBORRA aAlfa1;
|FPRC;

|PROCESO FiltraTxt;
   |SI PARAMETRO = "";
      |BLIN 23; |ATRI I; |PINTA 2302, "Transformando PDF a txt ...."; |ATRI N;
   |FINSI;

   aAlfa = aDirDestino + "pdf.txt";
   |XABRE "", aAlfa, nHandle;
   |SI FSalida ] 0;
      |LEE 000#dsboem02.u;
      |SI FSalida ! 0;
         |DDEFECTO #dsboem02;
      |SINO;
         #dsboem02#0 = #dsboem02#0 + 1;
      |FINSI;
      nCalc = #dsboem02#0; aAlfa = (("0" * 12) + nCalc); aAlfa = aAlfa % -112;
      aNumRegistro = aAlfa;

      aAlfa = aDirDestino + aNumRegistro; |QBF aAlfa;
      aAlfa = aAlfa + ".txt";
      |XABRE "W", aAlfa, nHandle1;
      |SI FSalida ] 0;
         nNumChars = 240;
         |PARA; |SI nNumChars ] 0; |Y nNumVacios < 80; |HACIENDO;
            |XLEE nHandle, 240, aReg;
            nNumChars = FSalida;
            |SI nNumChars > 0;
               nNumVacios = 0;
               aAlfa2 = ""; aReg1 = ""; aTab = &9;
               aAlfa = aReg; |QBF aAlfa; aAlfa1 = aAlfa % 0101;
               |PARA; |SI aAlfa1 = " "; |HACIENDO;
                  aAlfa3 = aAlfa % 20099;
                  aAlfa2 = aAlfa % 10199;
                  aAlfa = aAlfa % 0299; aAlfa = aAlfa + aAlfa2 + aAlfa3;
                  aAlfa1 = aAlfa % 0101;
               |SIGUE;
               aAlfa1 = aAlfa % 0101; aAlfa2 = "";
               |PARA; |SI aAlfa1 ! " "; |Y aAlfa ! ""; |HACIENDO;
                  aAlfa2 = aAlfa2 + aAlfa1;
                  aAlfa = aAlfa % 0299; aAlfa1 = aAlfa % 0101;
               |SIGUE;
               aAlfa = aAlfa2; aAlfa2 = "";

               aAlfa = aAlfa % 0207; nCalc = aAlfa;
               aAlfa1 = ("0000000" + nCalc) % -107;
               |SI aAlfa = aAlfa1;
                  |SI nCalc ] 0; |Y nCalc [ 9999999;
                     aAlfa = aReg % 0; nCalc = aAlfa;
                     |PARA nCont = 1; |SI nCont [ nCalc; |HACIENDO nCont = nCont + 1;
                        nCalc1 = (nCont * 100) + 1;
                        aAlfa1 = aReg % nCalc1;
                        |SI aAlfa1 = " ";
                           |SI aReg1 = "";
                              aAlfa1 = "";
                           |SINO;
                              aAlfa2 = aAlfa2 + aAlfa1;
                              |SI aAlfa2 = "   ";
                                 aAlfa1 = &9; aAlfa2 = "";
                                 |SI aUltChar = aAlfa1; aAlfa1 = ""; |FINSI;
                              |SINO;
                                 aAlfa1 = "";
                              |FINSI;
                           |FINSI;
                        |SINO;
                           |SI aUltChar = aTab; |QBF aAlfa2; |FINSI;
                           |SI aAlfa2 ! ""; aAlfa1 = aAlfa2 + aAlfa1; |FINSI;
                           aAlfa2 = "";
                        |FINSI;
                        aReg1 = aReg1 + aAlfa1;
                        |SI aAlfa1 ! ""; aUltChar = aReg1 % -101; |FINSI;
                     |SIGUE;
                     |XGRABA nHandle1, aReg1;
                  |FINSI;
               |FINSI;
            |SINO;
               nNumVacios = nNumVacios + 1;
            |FINSI;

            |SI nRetardo ] 100;
               nRetardo = 0;
               |SI aPuntos = "......"; aPuntos = ""; |FINSI;
               aPuntos = aPuntos + ".";
               |SI PARAMETRO = "";
                  |PINTA 2355, "       ";
                  aAlfa2 = " Desechando partes inservibles ..... espere por favor " + aPuntos;
                  |ATRI I; |PINTA 2302, aAlfa2; |ATRI N;
                  |PULSATECLA;
               |FINSI;
            |SINO;
               nRetardo = nRetardo + 1;
            |FINSI;
         |SIGUE;
         |XCIERRA nHandle1;
      |FINSI;
      |XCIERRA nHandle;
   |FINSI;

   aAlfa = aDirDestino + "pdf.txt"; |FBORRA aAlfa;
   |BLIN 23;
|FPRC;

|PROCESO PreparaRef;
   |SELECT #3#dsboem00;
   |DDEFECTO #dsboem00;
   |LEE 000#dsboem00.u;
   |SI FSalida ! 0; |DDEFECTO #dsboem00; |FINSI;
   nCalc = #dsboem00#6;
   |SELECT #1#dsboem00;

   nCalc = nCalc + 1; aAlfa = "00000000" + nCalc;
   aAlfa = aAlfa % -108;

   |HORA aAlfa1; |QBF aAlfa1; aAlfa1 = aAlfa1 % 0105;
   fFecha = ~; aAlfa2 = fFecha; |QBF aAlfa2;
   aAlfa2 = "REF" + (aAlfa2 % 0704) + (aAlfa2 % 0402) + (aAlfa2 % 0102);
   aReferencia = aAlfa2 + (aAlfa1 % 0102) + (aAlfa1 % 0402);

   |DDEFECTO #dsboem00;
   #dsboem00#0 = aReferencia;
   #dsboem00#1 = "INDEXADO AUTOMATICO (" + fFecha + " - " + aAlfa1 + ")";
   #dsboem00#2 = ~;
   #dsboem00#6 = aAlfa;
   #dsboem00#3 = "batch";
   #dsboem00#5 = aAlfa1;
   |GRABA 020#dsboem00.b;
|FPRC;

|PROCESO GrabaHistorico;

   |DBASE aAlfa; |QBF aAlfa; aAlfa = aAlfa + "boecom/correo/rec/";
   aAlfa = aDirDestino - aAlfa;
   aAlfa = aAlfa + aNumRegistro + ".txt";
   #dsboem00#4 = aAlfa;
   |GRABA 020#dsboem00.a;

   |LEE 000#dsboem02.u;
   |SI FSalida ! 0;
      |DDEFECTO #dsboem02;
   |SINO;
      #dsboem02#0 = #dsboem02#0 + 1;
   |FINSI;

   |GRABA 020#dsboem02.b;
   #dsboem02#1 = ~;
   |HORA aAlfa; |QBF aAlfa; aAlfa = aAlfa % 0105;
   #dsboem02#2 = aAlfa;
   #dsboem02#3 = "batch";
   #dsboem02#4 = #dsboem00#0;
   |GRABA 020#dsboem02.a; |LIBERA #dsboem02;
|FPRC;

|PROCESO Procesado;
   aAlfa1 = aReg % 0; nLong = aAlfa1; nLong = nLong + 1;

   |DDEFECTO #dsboem01;
   #dsboem01#0 = #dsboem00#0;
   nCont = 1; aAlfa1 = aReg;
   |PARA nDato = 1; |SI nDato [ 6; |HACIENDO nDato = nDato + 1;
|ET OtraVez;
      aAlfa1 = aAlfa1 - aTab;
      |SI FSalida ! 0;
         nCalc = ((FSalida / 100) ! 0) + 99; aAlfa2 = aAlfa1 % nCalc;
         aAlfa1 = aAlfa1 - aAlfa2;
      |SINO;
         aAlfa2 = aAlfa1;
      |FINSI;

      |SI nDato = 4;
         |QBF aAlfa2;
         |SI aAlfa2 = "-"; |O aAlfa2 = "";
            |VETE OtraVez;
         |FINSI;
      |FINSI;

      aAlfa3 = aAlfa2 % 0101;
      |PARA; |SI aAlfa3 = " "; |HACIENDO;
         aAlfa2 = aAlfa2 % 0299;
         aAlfa3 = aAlfa2 % 0101;
      |SIGUE;

      #dsboem01#nDato# = aAlfa2;
      |SI nDato = 2;
         |SI PARAMETRO = "";
            |SI nRetardo ] 150;
               nRetardo = 0;
               aMensaje = #dsboem01#1; |QBF aMensaje;
               aMensaje = aMensaje + " " + #dsboem01#2; |QBF aMensaje;
               |BLIN 23; |ATRI I; |PINTA 2302, aMensaje; |ATRI N;
               |PULSATECLA;
            |SINO;
               nRetardo = nRetardo + 1;
            |FINSI;
         |FINSI;
      |FINSI;
   |SIGUE;
   |GRABA 000#dsboem01.n;

   |SI FSalida ! 0; |Y nHanErr ] 0;
      |SI nSwErr = 0;
         aMensaje = " ************************************************************************* " + &13 + &10;
         |XGRABA nHanErr, aMensaje;
         aMensaje = " *****              LISTADO DE ERRORES DE PROCESADO                  ***** " + &13 + &10;
         |XGRABA nHanErr, aMensaje;
         aMensaje = " ************************************************************************* " + &13 + &10;
         |XGRABA nHanErr, aMensaje;
         aMensaje = " " + &13 + &10;
         |XGRABA nHanErr, aMensaje;
         nSwErr = 1;
      |FINSI;
      aMensaje = "Error al grabar registro NIF [" + #dsboem01#0 + "/" + #dsboem01#1 + "/" + #dsboem01#4 + "]" + &13 + &10;
      |XGRABA nHanErr, aMensaje;
   |FINSI;
|FPRC;

|PROCESO Indexado;

   |SI PARAMETRO = "";
      |BLIN 23; |ATRI I; |PINTA 2302, "Procesando ...."; |ATRI N;
      |PULSATECLA;
   |FINSI;

   nSwErr = 0;
   |DBASE aAlfa; |QBF aAlfa; aAlfa = aAlfa + "boecom/errores.txt";
   |XABRE "W", aAlfa, nHanErr;
   |SI FSalida < 0; nHanErr = -1; |FINSI;

   aAlfa = aDirDestino + aNumRegistro + ".txt";
   aTab = &9;
   |XABRE "", aAlfa, nHandle;
   |SI FSalida ] 0;
      nNumChars = 1;
      |PARA; |SI nNumChars > 0; |HACIENDO;
         |XLEE nHandle, 240, aReg;
         nNumChars = FSalida;
         |SI nNumChars > 0; |HAZ Procesado; |FINSI;
      |SIGUE;
   |FINSI;
   |XCIERRA nHandle;

   |SI nHanErr ] 0;
      |XCIERRA nHanErr;
      |SI nSwErr ! 0;
         aIPDSCorreo = #dsboem03#1 + "." + #dsboem03#2 + "." + #dsboem03#3 + "." + #dsboem03#4;
         aServidor   = #dsboem03#9;     |QBF aServidor;
         aDirOrigen  = #dsboem03#10;
         aDirDestino = #dsboem03#11;
         aAsunto     = " Errores en procesado de correo BoeCom";
         |DBASE aAlfa; |QBF aAlfa; aAlfa = aAlfa + "boecom/errores.txt";
         aTexto      = "$$$$" + aAlfa;
         |DSCORREO_ENVIA aIPDSCorreo, aServidor, aDirOrigen, aDirDestino, aAsunto, aTexto, aAlfa;
      |FINSI;
   |FINSI;

   |SI PARAMETRO = ""; |BLIN 23; |FINSI;
|FPRC;

|PROCESO EnviaCorreo;
   aIPDSCorreo = #dsboem03#1 + "." + #dsboem03#2 + "." + #dsboem03#3 + "." + #dsboem03#4;
   aServidor   = #dsboem03#9;     |QBF aServidor;
   |SI #dsboem03#5 = "S";
      aClave    = #dsboem03#7; |QBF aClave; |HAZ Encriptado;
      aDirOrigen  = aClave; |QBF aDirOrigen;
      aDirOrigen  = aDirOrigen + ":" + #dsboem03#6;  |QBF aDirOrigen;
      aDirOrigen  = aDirOrigen + ":" + #dsboem03#10; |QBF aDirOrigen;
   |SINO;
      aDirOrigen  = #dsboem03#10; |QBF aDirOrigen;
   |FINSI;
   aDirDestino = #dsboem03#11;
   aAsunto     = " Errores en procesado de correo BoeCom";
   |DBASE aAlfa; |QBF aAlfa; aAlfa = aAlfa + "boecom/informe.txt";
   aTexto      = "$$$$" + aAlfa;
   |DSCORREO_ENVIA aIPDSCorreo, aServidor, aDirOrigen, aDirDestino, aAsunto, aTexto, aAlfa;

   |FBORRA aAlfa;
|FPRC;

|PROCESO Proceso;
   |DFICB aAlfa; |QBF aAlfa; |PATH_DAT #dsboem00#aAlfa#; |PATH_DAT #dsboem01#aAlfa#;
   |ABRE #dsboem00; |ABRE #dsboem02;

   |HAZ RecogeCorreo;

   |SI FSalida = 0; || Hay correo
      |HAZ ReubicaContenido;

      aAlfa = FEstado - "]";
      nCalc = FSalida + 198;
      aAsunto = FEstado % nCalc;
      aAlfa = aAsunto - "Notificaciones BOECom";
      |SI FSalida ! 0;
         aLink = ""; |HAZ BuscaDireccion;

         |HAZ DescargaBOE;
         |HAZ TransformaTxt;
         |HAZ FiltraTxt;
         |HAZ PreparaRef;

         aAlfa = #dsboem00#6; |QBF aAlfa;
         |SI aAlfa ! ""; |NOME_DAT #dsboem01#aAlfa#; |FINSI;
         |ABRE #dsboem01;
         |HAZ GrabaHistorico;

         |HAZ Indexado;
         aAlfa = "dsboez02.tab;" + aReferencia;
         |SUB_EJECUTA aAlfa;

         |HAZ EnviaCorreo;
         |LIBERA #dsboem00; |CIERRA #dsboem01;
      |FINSI;
   |FINSI;

   |CIERRA #dsboem00; |CIERRA #dsboem02;
|FPRC;

|PROGRAMA;
   |ABRE #dsboem03;
   |LEE 000#dsboem03.p;
   |SI FSalida ! 0; |DDEFECTO #dsboem03; |FINSI;
   |SI PARAMETRO = "";
      |CLS;
      |CABEZA "Comprobacion descarga boecom";
      |PINPA #0#0; |PINDA #0#0;
      |ENDATOS #1#0;
      |SI #0#0 ! "S"; FSalida = -1; |FINSI;
   |SINO;
      FSalida = 0;
   |FINSI;
   |SI FSalida = 0; |HAZ Proceso; |FINSI;
   |CIERRA #dsboem03;
|FPRO;
