|FICHEROS;
     :integral/dsam0000;
     :integral/dsxw0002;
     :basica/agicen14;
     :basica/agicen17;

     :modelos/dsmom033;
|FIN;

|VARIABLES;
     aAlfa      = "";
     aAlfa1     = "";
     aAlfa2     = "";
     aAlfa3     = "";
     nNume      = 0;

     &eOpSub        = 0;
     &enConcepto    = 0;
     &eaCodSubdep   = "";
     &eswLect       = 0;
     &eaNomSubdep   = "";
     &enTipoTribut  = 0;
     &enCultivo     = 0;
     &eaTipoCultivo = "";
|FIN;

|PROCESO Tipo0C2W2; |TIPO 0;
     |SI #dsxw0002#2 = 1;
         #dsxw0002#3 = 12;
     |SINO;
         #dsxw0002#3 = #dsxw0002#2 - 1;
     |FINSI;
     |PINTA #dsxw0002#3;
|FPRC;

|PROCESO Tipo7Nivel;  |TIPO 7;
     |C_M #dsxw0002#5,  1, "S";
     |C_M #dsxw0002#6,  1, "S";
     |C_M #dsxw0002#7,  1, "S";
     |C_M #dsxw0002#8,  1, "S";
     |C_M #dsxw0002#9,  1, "S";
     |C_M #dsxw0002#10, 1, "S";

     |SI #dsxw0002#4 < 6;
         |C_M #dsxw0002#10, 1, "N";  #dsxw0002#10 = 0;
         |SI #dsxw0002#4 < 5;
             |C_M #dsxw0002#9, 1, "N";  #dsxw0002#9 = 0;
             |SI #dsxw0002#4 < 4;
                 |C_M #dsxw0002#8, 1, "N";  #dsxw0002#8 = 0;
                 |SI #dsxw0002#4 < 3;
                     |C_M #dsxw0002#7, 1, "N";  #dsxw0002#7 = 0;
                     |SI #dsxw0002#4 < 2;
                         |C_M #dsxw0002#6, 1, "N";  #dsxw0002#6 = 0;
                     |FINSI;
                 |FINSI;
             |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo0Nivel;  |TIPO 0;
  |SI Campo = 6;  |Y #dsxw0002#6 = 0; |Y #dsxw0002#4 ] 2;
      |MENAV  "0000Nivel repetido o valor mas bajo que los anteriores";
      |ERROR;
      |ACABA;
  |FINSI;

  |SI Campo = 7;  |Y #dsxw0002#7 = 0; |Y #dsxw0002#4 ] 3;
      |MENAV  "0000Nivel repetido o valor mas bajo que los anteriores";
      |ERROR;
      |ACABA;
  |FINSI;

  |SI Campo = 8;  |Y #dsxw0002#8 = 0; |Y #dsxw0002#4 ] 4;
      |MENAV  "0000Nivel repetido o valor mas bajo que los anteriores";
      |ERROR;
      |ACABA;
  |FINSI;

  |SI Campo = 9;  |Y #dsxw0002#9 = 0; |Y #dsxw0002#4 ] 5;
      |MENAV  "0000Nivel repetido o valor mas bajo que los anteriores";
      |ERROR;
      |ACABA;
  |FINSI;

  |SI Campo = 10;  |Y #dsxw0002#10 = 0; |Y #dsxw0002#4 ] 6;
      |MENAV  "0000Nivel repetido o valor mas bajo que los anteriores";
      |ERROR;
      |ACABA;
  |FINSI;

  |SI Campo = 6; |Y #dsxw0002#6 ! 0;    || Si hay dos niveles iguales o menor error
      |SI #dsxw0002#6 [ #dsxw0002#5;
          |MENAV  "0000Nivel repetido o valor mas bajo que los anteriores";
          |ERROR;
          |ACABA;
      |FINSI;
  |FINSI;

  |SI Campo = 7; |Y #dsxw0002#7 ! 0; || Si hay dos niveles iguales o menor error
      |SI #dsxw0002#7 [ #dsxw0002#5; |O #dsxw0002#7 [ #dsxw0002#6;
          |MENAV  "0000Nivel repetido o valor mas bajo que los anteriores";
          |ERROR;
          |ACABA;
      |FINSI;
  |FINSI;

  |SI Campo = 17; |Y #dsxw0002#8 ! 0; || Si hay dos niveles iguales o menor error
      |SI #dsxw0002#8 [ #dsxw0002#5; |O #dsxw0002#8 [ #dsxw0002#6; |O #dsxw0002#8 [ #dsxw0002#7;
         |MENAV  "0000Nivel repetido o valor mas bajo que los anteriores";
         |ERROR;
         |ACABA;
      |FINSI;
  |FINSI;

  |SI Campo = 18; |Y #dsxw0002#9 ! 0; || Si hay dos niveles iguales o menor error
      |SI #dsxw0002#9 [ #dsxw0002#5; |O #dsxw0002#9 [ #dsxw0002#6; |O #dsxw0002#9 [ #dsxw0002#7; |O #dsxw0002#9 [ #dsxw0002#8;
          |MENAV  "0000Nivel repetido o valor mas bajo que los anteriores";
          |ERROR;
          |ACABA;
      |FINSI;
  |FINSI;

  |SI Campo = 19; |Y #dsxw0002#10 ! 0;    || Si hay dos niveles iguales o menor error
      |SI #dsxw0002#10 [ #dsxw0002#5; |O #dsxw0002#10 [ #dsxw0002#6; |O #dsxw0002#10 [ #dsxw0002#7; |O #dsxw0002#10 [ #dsxw0002#8; |O #dsxw0002#10 [ #dsxw0002#9;
          |MENAV  "0000Nivel repetido o valor mas bajo que los anteriores";
          |ERROR;
          |ACABA;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO Tipo0C1W2;  |TIPO 0;
     |ABRE #agicen14;
     #agicen14#0 = #dsam0000#0;
     #agicen14#1 = 1;
     |LEE 040#agicen14.];
     |SI FSalida ! 0;  |O #agicen14#0 ! #dsam0000#0;
         |DDEFECTO #agicen14;
     |FINSI;
     |CIERRA #agicen14;

     aAlfa = #dsxw0002#16;  |QBF aAlfa;
     |SI aAlfa = "";
         |SI #dsxw0002#1 ] 80;
             nNume = 1900 + #dsxw0002#1;
         |SINO;
             nNume = 2000 + #dsxw0002#1;
         |FINSI;
         aAlfa1 = #dsam0000#1;     |QBF aAlfa1;
         aAlfa2 = #agicen14#8;   |QBF aAlfa2;
         aAlfa3 = #agicen14#10;  |QBF aAlfa3;
         aAlfa =  "[" + (("00000" + #dsam0000#0) % -105) + "/" + nNume + "] " + aAlfa1 + " " + aAlfa2 + " " + aAlfa3;
         #dsxw0002#16 = aAlfa;
         |PINTA #dsxw0002#16;
     |FINSI;
|FPRC;

|| ***********************************************************************
|| ..... Datos de Defecto Departamento y subdepartamento

|PROCESO Tipo0C13W2; |TIPO 0;
 aAlfa1 = #dsxw0002#13; |C_M #dsxw0002#18,1,aAlfa1;
|FPRC;

|PROCESO Inf2;
   #agicen14#0 = #dsam0000#0;
   #agicen14#1 = 01;
|FPRC;

|PROCESO Sup2;
   #agicen14#0 = #dsam0000#0;
   #agicen14#1 = 99;
|FPRC;

|PROCESO Tipo0C18W2; |TIPO 0;
   |C_M #dsxw0002#Campo#, 0, aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   |SI FSalida = 10;
       |ABRE #agicen14;
       #agicen14#1 = #dsxw0002#18;
       |CONSULTA_F_CLAVE #1#agicen14,Inf2, Sup2;
       #dsxw0002#18 = #agicen14#1;
       |CIERRA #agicen14;
   |FINSI;

   |ABRE #agicen14;
   |DDEFECTO #agicen14;
   #agicen14#0 = #dsam0000#0;
   #agicen14#1 = #dsxw0002#18;
   |LEE 001#agicen14.=;
   |SI FSalida ! 0;
       |CONFI "    NError.! No Existe Actividad / Departamento. Continuar S/N ?";
       |SI FSalida ! 0;
           |ERROR;
       |FINSI;
       #dsxw0002#20 = "";
   |SINO;
       enTipoTribut = #agicen14#9;
       aAlfa1 = #agicen14#1;
       aAlfa1 = ("00" + aAlfa1) % A02;
       #dsxw0002#18 = aAlfa1; |PINTA #dsxw0002#18;
       #dsxw0002#20 = #agicen14#4;
   |FINSI;
   |CIERRA #agicen14;
|FPRC;

|PROCESO Tipo0C14W2; |TIPO 0;
 aAlfa1 = "S";
 |SI #dsxw0002#14 = "N"; aAlfa1 = "N"; |FINSI;

 |C_M #dsxw0002#19,1,aAlfa1;
|FPRC;

|PROCESO Tipo0C19W2; |TIPO 0;
   |SI #dsxw0002#14 = "N"; |ACABA; |FINSI;

   eOpSub = 0; || 1 = Consulta pantalla
   |SI FSalida = 10; eOpSub = 1;  |FINSI;
   enConcepto  = 1;
   eaCodSubdep = #dsxw0002#19;
   |HAZ SubActiv;
   |SI eswLect = 1;
       |CONFI "    NError.! No Existe SubDepartamento. Continuar S/N ?";
       |SI FSalida ! 0;
           |ERROR;
       |FINSI;
       #dsxw0002#21 = "";
   |SINO;
       #dsxw0002#19 = eaCodSubdep; |PINTA #dsxw0002#19;
       #dsxw0002#21 = eaNomSubdep;
   |FINSI;
|FPRC;

|PROCESO Inf1;
   #agicen17#0 = #dsam0000#0;
   #agicen17#1 = #dsxw0002#18;
   #agicen17#2 = 001;
|FPRC;

|PROCESO Sup1;
   #agicen17#0 = #dsam0000#0;
   #agicen17#1 = #dsxw0002#18;
   #agicen17#2 = 999;
|FPRC;


|PROCESO SubActiv;
   eswLect = 0;
   eaNomSubdep = "";
   |SI #dsxw0002#14 = "O"; |ACABA; |FINSI;

   |SI enTipoTribut = 6;  |O enTipoTribut = 7;
    |O enTipoTribut = 11; |O enTipoTribut = 12;
    |O enTipoTribut = 16; |O enTipoTribut = 17;
       enCultivo     = eaCodSubdep;
       eaTipoCultivo = "A";
       |SI eOpSub = 1;
           |SUB_EJECUTA_NP ":modelos/dsmom033";
       |FINSI;

       |ABRE #dsmom033;
       #dsmom033#0 = enCultivo;
       |LEE 001#dsmom033.=;
       |SI FSalida ! 0;
           eswLect = 1;
       |FINSI;
       |CIERRA #dsmom033;
       eaCodSubdep = ("000" + enCultivo) % -103;
       eaNomSubdep = #dsmom033#1 % 140;
   |SINO;

       |ABRE #agicen17;
       |SI eOpSub = 1;
           |CONSULTA_F_CLAVE #1#agicen17, Inf1, Sup1;
           eaCodSubdep = #agicen17#2; |QBF eaCodSubdep;
           eaCodSubdep = ("000" + eaCodSubdep) % -103;
       |FINSI;

       #agicen17#0 = #dsam0000#0;
       #agicen17#1 = #dsxw0002#18;
       #agicen17#2 = eaCodSubdep;
       |LEE 040#agicen17.=;
       |SI FSalida ! 0; |Y eaCodSubdep ! "   ";
           eswLect = 1;
       |FINSI;
       eaNomSubdep = #agicen17#3;
       eaCodSubdep = #agicen17#2; |QBF eaCodSubdep;
       eaCodSubdep = ("000" + eaCodSubdep) % -103;
       |CIERRA #agicen17;
   |FINSI;
|FPRC;
