|FICHEROS;
     dsam0053 #53,MANTE;
     dsam0000 #0;
     dsam0013 #13;
|FIN;

|VARIABLES;
     aHora          = "";
     aAlfa          = "";

     nModo          = 0;
     nVariable      = 0;
     nForma         = 0;
     nDCliente      = 0;
     nHCliente      = 0;

     &enEmpresa     = 0;
     &enModo        = 0;
     &enDentro      = 0;
     &enCodExpe     = 0;
     &enCodPers     = 0;

     &eaNombreList  = "";
     &enCuadroIni   = 0;
     &enCuadroFin   = 0;
     &enCampoAncho  = 0;
     &enModoEdita   = 0;
     &enTotalVaria  = 0;

{100}&epPuntEdita   = "";
|FIN;

|PROCESO Cuadrom0053;  |TIPO 7;
     |PUSHV 0501 2380;

     |D_CUADRO 1320, 2072;
     |ATRI R;
     |PINTA 1421, "-0- Solo el dia de Aviso.  -X- Todos los Miercoles.";
     |PINTA 1521, "-T- Todos los Dias.        -J- Todos los Jueves.   ";
     |PINTA 1621, "-2- Dos Semanas Antes.     -V- Todos los Viernes.  ";
     |PINTA 1721, "-1- Una Semanas Antes.     -S- Todos los Sabados.  ";
     |PINTA 1821, "-L- Todos los Lunes.       -D- Todos los Domingos. ";
     |PINTA 1921, "-M- Todos los Martes.                              ";
     |ATRI 0;
|FPRC;

|PROCESO Mayusm0053;
     #53Campo = #53Campo > #53Campo;
     |PINTA #53Campo;

     |SI Campo = 5;
         |C_M #53#6,  1, "S";
         |C_M #53#7,  1, "S";
         |C_M #53#8,  1, "S";
         |C_M #53#18, 1, "S";
         |SI #53#5 = "N";
             |C_M #53#6,  1, "N";  #53#6  = "01.01.0000";  |PINTA #53#6;
             |C_M #53#7,  1, "N";  #53#7  = 0;             |PINTA #53#7;
             |C_M #53#8,  1, "N";  #53#8  = 0;             |PINTA #53#8;
             |C_M #53#18, 1, "N";  #53#18 = "";            |PINTA #53#18;
                                   #53#17 = "S";           |PINTA #53#17;
         |SINO;
                                   #53#17 = "N";           |PINTA #53#17;
         |FINSI;
     |FINSI;

     |SI Campo = 18;
         |POPV;
         |PINTA #53Campo;
     |FINSI;
|FPRC;

|PROCESO Tipo11m0053;  |TIPO 11;
     |SI FSalida = 10;   nForma = 1;  |ERROR;    |FINSI;
     |SI FSalida = 11;   nForma = 0;  |PTEC 807; |FINSI;
|FPRC;

|PROCESO Dentrom0053;  |TIPO 7;
     |SI enEmpresa ! 0;
         |SI Campo = 4;  enDentro = 1; |FINSI;
         |SI Campo = 1;  enDentro = 0; |FINSI;
     |SINO;
         |SI Campo = 4;  enDentro = 1;  |FINSI;
         |SI Campo = 1;  |Y nForma = 1;
             |PTEC 809;
             |PTEC 809;
             enDentro = 0;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO EditaAsuntom0053;  |TIPO 7;
     enTotalVaria  = 6;
     eaNombreList  = "";
     enCuadroIni   = 1730;
     enCuadroFin   = 2279;
     enCampoAncho  = 50;
     enModoEdita = 1100;

     IepPuntEdita  = epPuntEdita1 <-;
     |PARA nVariable = 11;  |SI nVariable [ 16;  |HACIENDO nVariable = nVariable + 1;
          epPuntEdita  = #53nVariable;
          IepPuntEdita = IepPuntEdita + 1;
     |SIGUE;

     |HAZ Edita;

     IepPuntEdita  = epPuntEdita1 <-;
     |PARA nVariable = 11;  |SI nVariable [ 16;  |HACIENDO nVariable = nVariable + 1;
          #53nVariable = epPuntEdita;             |PINTA #53nVariable
          IepPuntEdita = IepPuntEdita + 1;
     |SIGUE;
|FPRC;

|PROCESO PonHora;  |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo ! 1;  |ACABA;  |FINSI;

     |HORA aHora;
                           #53#1  = ~;      |PINTA #53#1;
     aAlfa = aHora % 102;  #53#2  = aAlfa;  |PINTA #53#2;
     aAlfa = aHora % 402;  #53#3  = aAlfa;  |PINTA #53#3;
     aAlfa = aHora % 702;  #53#4  = aAlfa;
|FPRC;

|PROCESO Tipo7C03m53;  |TIPO 7;
     aAlfa = "    ";
     |SI enEmpresa = 0;
         aAlfa = "    Pulse <F2> Modo Alta;  <F3> Modo Consulta o Modificacion.";
     |FINSI;
     |MENSA aAlfa;
|FPRC;

|PROCESO BuscaCliente;  |TIPO 0;
     |BLIN 4;
     |SI enEmpresa ! 0;
         #53#0  = #0#0;  |PINTA #53#0;
         |ACABA;
     |FINSI;

     nModo = (FEntrada / 100) ! 0;
     |SI nModo ! 1;  |ACABA;  |FINSI;

     |HORA aHora;
                           #53#1 = ~;      |PINTA #53#1;
     aAlfa = aHora % 102;  #53#2 = aAlfa;  |PINTA #53#2;
     aAlfa = aHora % 402;  #53#3 = aAlfa;  |PINTA #53#3;
     aAlfa = aHora % 702;  #53#4 = aAlfa;

     |CONFI "2400SCliente S/N : ";
     |SI FSalida ! 0;
         #53#0  = 0;  |PINTA #53#0;
         |ACABA;
     |FINSI;

     |PUSHV 0501 2380;
     |ABRE #0;
     |LEE 040#0p;
     |CONSULTA_CLAVE #1#0;
     |CIERRA #0;
     |POPV;

     #53#0  = #0#0;  |PINTA #53#0;
     #53#19 = #0#1;  |PINTA #53#19;
|FPRC;

|PROCESO Tipo6C20; |TIPO 6;
     |SI #53#20 = 0;
         |ABRE #13;
         #13#0 = #53#20;
         |LEE 001#13=;
         |SI FSalida ! 0;
             #13#0 = #53#20;
             #13#1 = "";
             |GRABA 020#13n;
         |FINSI;
         |CIERRA #13;
     |FINSI;
|FPRC;

|| ************************************************************************
||                  PROCESO PARA PONER EN COLORES LAS LINEAS
|| ************************************************************************

|PROCESO Tipo5m0053;  |TIPO 5;
     |LEE 040#53p;
     |SI FSalida = 0;
         |SI #53#17 = "N";
             |LINEAS_BI_ATRI #53#17, N, I, 179;
         |SINO;
             |LINEAS_BI_ATRI #53#17, I, N, 179;
         |FINSI;
     |SINO;
         |LINEAS_BI_ATRI #53#17, I, N, 179;
     |FINSI;
|FPRC;

|RUTINA ClaveBaja;
     #53#0  = nDCliente;
     #53#1  = "01.01.0000";
     #53#2  = 00;
     #53#3  = 00;
     #53#4  = 00;
|FRUT;

|RUTINA ClaveAlta;
     #53#0  = nHCliente;
     #53#1  = "31.12.2999";
     #53#2  = 23;
     #53#3  = 59;
     #53#4  = 99;
|FRUT;

|PROGRAMA;
     nDCliente = 0;
     nHCliente = 99999;
     |SI enEmpresa ! 0;
         nDCliente = enEmpresa;
         nHCliente = enEmpresa;
         |ABRE #0;
         #0#0 = enEmpresa;
         |LEE 040#0=;
         |CIERRA #0;
     |SINO;
         |CLS;
     |FINSI;

     nForma = 1;

     |CABEZA "Entrada Agenda";
     |SI enEmpresa = 0;
         |ENTLINEAL #2#53, -1, 1, 22, 1, ClaveBaja, ClaveAlta;
     |SINO;
         |ENTLINEAL #1#53, -2, enModo, 22, 1, ClaveBaja, ClaveAlta;
     |FINSI;
|FPRO;
