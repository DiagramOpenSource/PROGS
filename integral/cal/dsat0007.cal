|FICHEROS;
    dsam0000;
    dsam0025;
    dsam0105;
    dsam0125;

    dsaz0036;
    dsat0007,MANTE;
    dsat007@;
|FIN;

|VARIABLES;
     nBoton2     = -1;
     nBoton3     = -1;
     nBoton4     = -1;
     nBoton5     = -1;
     nBoton6     = -1;
     nBoton8     = -1;
     nNoGraba    = 0;
     nTecla      = 0;
     nImport1    = 0;
     nImport2    = 0;
     nImport3    = 0;
     nImport4    = 0;
     aAlfa       = "";
     aAlfa1      = "";
     aAlfa2      = "";
     aBase       = "";
     aDef        = "";
     informe     = "";
     inform1     = "dsat0007";
     inform11    = "dsat0u07";
     inform2     = "dsat1007";
     inform21    = "dsat1u07";
     nAlgun      = 0;
     nOp         = 0;
     nSwError    = 0;
     nPara1      = 0;
     nPrint      = 0;
     nPrint1     = 0;

     &enEmpresa  = 0;
     &enEmpPD    = 0;
     &enEjer     = 0;
     &aFuncion   = "";
     &eaTipoImp  = "";

     &eSwImp     = 0;
     &eaFichero  = "";
     &eaDLiqu    = "";
     &eaHLiqu    = "";
     &enDEmpresa = 0;
     &enHEmpresa = 0;
     &enDAgente  = 0;
     &enHAgente  = 0;
     &enDConve   = 0;
     &enHConve   = 0;

     &eaTitul1  = "";
     &eaTitul2  = "";
     &eaTitul3  = "";
     &eaTitul4  = "";
     &enImporte1 = 0;
     &enImporte2 = 0;
     &eaLiquidad = "";

     &enTotal1  = 0;
     &enTotal2  = 0;
     &enTotal3  = 0;
     &enTotal4  = 0;
     &eSwLinea  = 0;
|FIN;

|| ///////////////////////////////

|PROCESO Tipo12; |TIPO 12;
  ||
|FPRC;

|PROCESO PintaTotal;
   #dsaz0036#12 = #dsaz0036#12 - nImport1 + nImport3;
   #dsaz0036#13 = #dsaz0036#13 - nImport2 + nImport4;
   |PINTA #dsaz0036#12;
   |PINTA #dsaz0036#13;
|FPRC;

|PROCESO MarcaDatos;
   |LEE 141#dsat0007.=;

   nImport1  = 0; nImport2  = 0;
   |SI #dsat0007#9 = "S"; |Y #dsat0007#17 ! "S";
       nImport1 = #dsat0007#10;
   |FINSI;
   |SI #dsat0007#15 = "S"; |Y #dsat0007#18 ! "S";
       nImport2 = #dsat0007#14;
   |FINSI;

   |SI eaTipoImp ! "R"; |Y #dsat0007#17 ! "S";
       |SI #dsat0007#9 = "S";
           #dsat0007#9 = "N";
       |SINO;
           #dsat0007#9 = "S";
       |FINSI;
       |PINTA #dsat0007#9;
   |FINSI;
   |SI eaTipoImp ! "P"; |Y #dsat0007#18 ! "S";
       |SI #dsat0007#15 = "S";
           #dsat0007#15 = "N";
       |SINO;
           #dsat0007#15 = "S";
       |FINSI;
       |PINTA #dsat0007#15;
   |FINSI;

   |GRABA 020#dsat0007.a;
   |LIBERA #dsat0007;

   nImport3  = 0; nImport4  = 0;
   |SI #dsat0007#9 = "S"; |Y #dsat0007#17 ! "S";
       nImport3 = #dsat0007#10;
   |FINSI;
   |SI #dsat0007#15 = "S"; |Y #dsat0007#18 ! "S";
       nImport4 = #dsat0007#14;
   |FINSI;

   |HAZ PintaTotal;
|FPRC;

|PROCESO InforDsat0007;

   |PARA nPara1 = 0; |SI nPara1 [ 19; |HACIENDO nPara1 = nPara1 + 1;
         #dsat0007#nPara1# = #dsat007@#nPara1#;
   |SIGUE;

   enImporte1 = 0;
   enImporte2 = 0;
   eaLiquidad = "";
   eSwLinea   = 0;

   |SI eaTipoImp ! "A";
       |SI eaTipoImp = "P";
           enImporte1 = #dsat0007#7;
           enImporte2 = #dsat0007#10;
           eaLiquidad = #dsat0007#9;
       |FINSI;
       |SI eaTipoImp = "R";
           enImporte1 = #dsat0007#13;
           enImporte2 = #dsat0007#14;
           eaLiquidad = #dsat0007#15;
       |FINSI;
       |SI eaLiquidad = "S";
           eSwLinea = 1;
           enTotal1 = enTotal1 + enImporte2;
       |SINO;
           eSwLinea = 2;
           enTotal2 = enTotal2 + enImporte2;
       |FINSI;

   |SINO;

       |SI #dsat0007#17 ! "S";
           |SI #dsat0007#9 = "S";
               enTotal1 = enTotal1 + #dsat0007#10;
               eSwLinea = 1;
           |SINO;
               enTotal2 = enTotal2 + #dsat0007#10;
               eSwLinea = 2;
           |FINSI;
       |FINSI;
       |SI #dsat0007#18 ! "S";
           |SI #dsat0007#15 = "S";
               enTotal3 = enTotal3 + #dsat0007#14;
               eSwLinea = 1;
           |SINO;
               enTotal4 = enTotal4 + #dsat0007#14;
               eSwLinea = 2;
           |FINSI;
       |FINSI;
   |FINSI;

   |SI eSwLinea ! 0;
       |PRINT;
       nAlgun = 1;
   |FINSI;
|FPRC;

|PROCESO LeeDsat0007;
   |SI nOp = 0;
       nImport1 = 0; nImport2 = 0;
       |SI #dsat007@#9 = "S"; |Y #dsat007@#17 ! "S";
           nImport1 = #dsat007@#10;
       |FINSI;
       |SI #dsat007@#15 = "S"; |Y #dsat007@#18 ! "S";
           nImport2 = #dsat007@#14;
       |FINSI;
       |SI eaTipoImp ! "R"; |Y #dsat007@#17 ! "S";
           #dsat007@#9 = aAlfa;
       |FINSI;
       |SI eaTipoImp ! "P"; |Y #dsat007@#18 ! "S";
           #dsat007@#15 = aAlfa;
       |FINSI;

       nImport3 = 0; nImport4 = 0;
       |SI #dsat007@#9 = "S"; |Y #dsat007@#17 ! "S";
           nImport3 = #dsat007@#10;
       |FINSI;
       |SI #dsat007@#15 = "S"; |Y #dsat007@#18 ! "S";
           nImport4 = #dsat007@#14;
       |FINSI;
       |GRABA 020#dsat007@.a;
       |HAZ PintaTotal;
   |FINSI;

   |SI nOp = 1;
       nPrint = 0;
       |SI eSwImp = 0; nPrint = 1; |FINSI;
       |SI #dsat007@#9  = "S"; |Y #dsat007@#17 ! "S"; |Y eaTipoImp ! "R"; nPrint = 1; |FINSI;
       |SI #dsat007@#15 = "S"; |Y #dsat007@#18 ! "S"; |Y eaTipoImp ! "P"; nPrint = 1; |FINSI;
       |SI nPrint = 1;
          |HAZ InforDsat0007;
      |FINSI;
   |FINSI;
   |LIBERA #dsat007@;
|FPRC;

|DEFBUCLE LeeDsat0007;
   #dsat007@#1005;
   ;
   aFuncion, enEmpPD, enEjer, enDEmpresa, enDConve;
   aFuncion, enEmpPD, enEjer, enHEmpresa, enHConve;
   ;
   NULL, LeeDsat0007;
|FIN;
|| //////////////////////////////////////////////////////////////

|PROCESO LosInformes;
   |HAZ CargaFichero;
   |SI nSwError = 1;  |ACABA;  |FINSI;

   informe = inform1;
   |SI eSwImp = 1; informe = inform2; |FINSI;
   |SI eaTipoImp ! "A";
       informe = inform11;
       |SI eSwImp = 1; informe = inform21; |FINSI;
   |FINSI;
   nAlgun  = 0;
   |IMPRE 0;
   |SI FSalida ! 0;
       |ACABA;
   |FINSI;
   eaTitul1 = "SEGUROS DE CONVENIO";
   eaTitul2 = "Aseguradora";
   eaTitul3 = "Poliza y Regularizacion";
   |SI eaTipoImp = "P"; eaTitul3 = "      Poliza"; |FINSI;
   |SI eaTipoImp = "R"; eaTitul3 = "Regularizac."; |FINSI;
   eaTitul4 = eaTitul3 > "";
   eaTitul4 = eaTitul4 + "INFORMADAS PARA SER LIQUIDADAS";

   |INFOR informe;
   |SI FSalida ! 0;
       |MENAV "0000No existe informe";
       |FINIMP;
       |ACABA;
   |FINSI;

   |SALVA_DATOS #dsat0007;
   enTotal1 = 0;
   enTotal2 = 0;
   enTotal3 = 0;
   enTotal4 = 0;
   nOp = 1; |BUCLE LeeDsat0007;
   |SI nAlgun = 1;
       |PIEINF;
   |FINSI;
   |FININF;
   |FINIMP;

   |HAZ DesCargaFichero;
   |REPON_DATOS #dsat0007;
|FPRC;

|PROCESO LosMarcados;
   |HAZ CargaFichero;
   |SI nSwError = 1;  |ACABA;  |FINSI;

   nOp = 0; |BUCLE LeeDsat0007;
   |HAZ DesCargaFichero;
|FPRC;

|PROCESO CargaFichero;
   |DBASE aBase;
   aDef = aBase + "def/dsat0007";
   |CARGA_DEF aDef, dsat007@;
   |SI FSalida ! 0;
       |MENAV "0000Error cargado def, proceso interrumpido";
       nSwError = 1;
   |SINO;
       |NOME_DAT #dsat007@#eaFichero#;
   |FINSI;
|FPRC;

|PROCESO DesCargaFichero;
   |DESCARGA_DEF #dsat007@;
|FPRC;

|| /////////////////////////////////////////////////////////
|PROCESO Boton2m001;
     |PTEC 824;
|FPRC;

|PROCESO Boton3m001;
     |PTEC 825;
|FPRC;

|PROCESO Boton5m001;
     |PTEC 827;
|FPRC;

|PROCESO Boton6m001;
     |PTEC 828;
|FPRC;

|PROCESO Boton8m001;
     |PTEC 830;
|FPRC;

|PROCESO FinBotont007;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;
     |FIN_CONTROL_WINDOWS nBoton5;
     |FIN_CONTROL_WINDOWS nBoton6;

     |FIN_CONTROL_WINDOWS nBoton8;
     |PULSATECLA;
|FPRC;

|PROCESO Tipo0t007;  |TIPO 0;
     |HAZ FinBotont007;
|FPRC;

|PROCESO Tipo7t007;  |TIPO 7;
     |BLIN 04;

     |CONTROL_SIMPLE 0, "LBOTON,F2 Liquidar/No Liquidar", 1405, 1421, Boton2m001;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F3 Informe", 1423, 1435, Boton3m001;
     nBoton3 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F5 Liquidar Todos", 1437, 1449, Boton5m001;
     nBoton5 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F6 No Liquidar Todos", 1451, 1465, Boton6m001;
     nBoton6 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F8 Dar de Bajar", 1467, 1479, Boton8m001;
     nBoton8 = FSalida;
|FPRC;

|PROCESO Tipo11t007;  |TIPO 11;
     nTecla = FSalida;

     |HAZ FinBotont007;

     enEmpresa     = #dsat0007#4;
     enEjer        = #dsat0007#2;

     |SI nTecla = 10;
         |HAZ MarcaDatos;
         |PONCONTROL  23, "SI";
         |ACABA;
     |FINSI;

     |SI nTecla = 11;
         eSwImp = 0;
         |HAZ LosInformes;
     |FINSI;

     |SI nTecla = 13; |O nTecla = 14;
         aAlfa = "S";
         |SI nTecla = 14; aAlfa = "N"; |FINSI;
         |HAZ LosMarcados;
         |PONCONTROL  23, "SI";
         |ACABA;
     |FINSI;

     |SI nTecla = 16;
         |CONFI "0000SDesea borrar el registro (No actualiza en Cliente)";
         |SI FSalida = 0;

             |LEE 141#dsat0007.=;
             |BORRA 020#dsat0007.a;
             |LIBERA #dsat0007;

             |SI #dsat0007#17 ! "S";
                 #dsaz0036#10 = #dsaz0036#10 - #dsat0007#10;
             |FINSI;
             |SI #dsat0007#18 ! "S";
                 #dsaz0036#11 = #dsaz0036#11 - #dsat0007#14;
             |FINSI;
             |SI #dsat0007#9 = "S"; |Y #dsat0007#17 ! "S";
                 #dsaz0036#12 = #dsaz0036#12 - #dsat0007#10;
             |FINSI;
             |SI #dsat0007#15 = "S"; |Y #dsat0007#18 ! "S";
                 #dsaz0036#13 = #dsaz0036#13 - #dsat0007#14;
             |FINSI;
             |PINTA #dsaz0036#10;
             |PINTA #dsaz0036#11;
             |PINTA #dsaz0036#12;
             |PINTA #dsaz0036#13;
         |FINSI;
         |PONCONTROL  23, "SI";
     |FINSI;
|FPRC;
