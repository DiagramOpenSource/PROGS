|FICHEROS;
|FIN;

|VARIABLES;
     aAlfa          = "";
     aAlfa1         = "";
     aAlfa2         = "";
     aBase          = "";
     aAbre          = "";
     aFichero       = "";
     aCadena        = "";
     aCR            = "";
     aLF            = "";
     aLong          = "";

     nHandle        = 0;
     nLong          = 0;
     nPos           = 0;
     nCaracter      = 0;
     nCamuf         = 0;
     nCamuf1        = 0;
     nCamuf2        = 0;
     nNum           = 0;

     &enAprobacion  = 0;
     &eaPrograma    = "";
     &eaPass        = "";
     &eaPassEnc     = "";
     &enEmpresa     = 0;
     &eaBloqueo     = "";
|FIN;

|PROCESO EncriptaPass;
     |QBF eaPass;
     aLong   = eaPass % 0;
     nLong   = aLong;
     eaPassEnc = "";
     |PARA nCaracter = 1; |SI nCaracter [ nLong; |HACIENDO nCaracter = nCaracter + 1;
           nPos   = (nCaracter * 100) + 1;
           aAlfa1 = eaPass % nPos;

           nCamuf = &aAlfa1;
           nCamuf = nCamuf * 2;

           nNum      = ((nCamuf - (nCamuf $ 26)) / 26 ) + 65;
           eaPassEnc = eaPassEnc + &nNum;

           nNum      = (nCamuf $ 26) + 65;
           eaPassEnc = eaPassEnc + &nNum;
     |SIGUE;
|FPRC;

|PROCESO DesencriptaPass;
     |QBF eaPass;
     aLong = eaPass % 0;
     nLong = aLong;
     aAlfa = "";
     |PARA nCaracter = 1; |SI nCaracter [ nLong; |HACIENDO nCaracter = nCaracter + 2;
          nPos = (nCaracter * 100) + 1;         aAlfa1 = eaPass % nPos;
          nPos = ((nCaracter + 1) * 100) + 1;   aAlfa2 = eaPass % nPos;

          nCamuf2 = &aAlfa2;
          nCamuf2 = nCamuf2 - 65;

          nCamuf1 = &aAlfa1;
          nCamuf1 = nCamuf1 - 65;
          nCamuf1 = ((nCamuf1 * 26) + nCamuf2) / 2;

          aAlfa = aAlfa + &nCamuf1;
     |SIGUE;
     eaPass = aAlfa;
|FPRC;

|PROCESO DSARCHI_INI;
     enAprobacion = 0;

     |DBASS aBase;  |QBF aBase;
     aFichero = aBase + "dsarchi/dsarchi.ini";
     |XABRE "AB", aFichero, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     aCR        = &10;
     aLF        = &13;
     aCadena    = "";

     |XLEE nHandle, 1, aAlfa;
     |PARA; |SI FSalida > 0; |HACIENDO;
            |SI aAlfa = aCR;
                |SI aCadena = eaPrograma;
                    enAprobacion = 1;
                    |SAL;
                |FINSI;

                aCadena = "";
            |SINO;
                |SI aAlfa ! aLF;
                    aCadena = aCadena + aAlfa;
                |FINSI;
            |FINSI;
            |XLEE nHandle, 1, aAlfa;
     |SIGUE;

     |SI aCadena = eaPrograma;
          enAprobacion = 1;
     |FINSI;

     |XCIERRA nHandle;
|FPRC;

|PROCESO MiraBloqueo;
     |DBASS aBase;
     aAbre = aBase + "integral/def/dsam0021.mas";
     |XABRE "E", aAbre, 0;
     |SI FSalida ] 0;
         aAlfa = ":integral/dsam0021;" + (("00000" + enEmpresa) % -105);
         aAlfa = aAlfa + "CHEQUEO";
         |SUB_EJECUTA aAlfa;

         |SI eaBloqueo ! "1";
             aAlfa = "|:integral/dsam0021;" + (("00000" + enEmpresa) % -105);
             aAlfa = aAlfa + "PANTALLA";
             |SUB_EJECUTA aAlfa;
         |FINSI;
     |FINSI;
|FPRC;
