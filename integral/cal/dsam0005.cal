|FICHEROS;
     :/integral/def/dsam0000 #0;
     :/integral/def/dsam0005 #5;
     :/integral/def/dsaw0001 #901,MANTE;
     :/basica/def/agicen14   #114;
|FIN;

|VARIABLES;
     aAlfa      = "";
     aCarac     = "";
     aParametro = "";

     nPtec      = 0;
     nCampo     = 0;
     nModo      = 0;
     nLinea     = 0;
     nLineaAct  = 0;
     nError     = 0;
     nDActividad = 0;
     nHActividad = 0;

     &enEmpresa   = 0;
     &enActividad = 0;
     &enModo      = 0;
|FIN;

|PROCESO LeeEmpresa;
     |ABRE #0;
     #0#0 = enEmpresa;
     |LEE 000#0=;
     |SI FSalida ! 0;  |DDEFECTO #0;  |FINSI;
     |CIERRA #0;
|FPRC;

|PROCESO Tipo5m005; |TIPO 5;
     #5#0 = enEmpresa;
     #5#1 = 97;
     |LEE 040#5=;
     |SI FSalida ! 0;
         |HAZ LeeEmpresa;

         #5#0  = #0#0;
         #5#1  = 97;
         #5#2  = "DOMICILIO SOCIAL ";
         #5#3  = #0#19;
         #5#4  = #0#20;
         #5#5  = #0#21;
         #5#6  = #0#22;
         #5#7  = #0#23;
         #5#8  = #0#24;
         #5#9  = #0#25;
         #5#10 = #0#26;
         #5#11 = #0#27;
         #5#12 = #0#28;
         #5#17 = #0#98;
         #5#18 = #0#99;
         #5#15 = #0#86;
         #5#16 = #0#87;
         |GRABA 020#5n;

         #5#0 = #0#0;
         #5#1 = 98;
         #5#2 = "DOMICILIO FISCAL ";
         |GRABA 020#5n;

         #5#0 = #0#0;
         #5#1 = 99;
         #5#2 = "DOMICILIO ENVIO  ";
         |GRABA 020#5n;
     |FINSI;
|FPRC;

|PROCESO Tipo7m0005;  |TIPO 7;
     |PARA nCampo = 0;  |SI nCampo [ 18;  |HACIENDO nCampo = nCampo + 1;
           #901nCampo = #5nCampo;
     |SIGUE;

     |PINPA #0#901;
     |PINDA #0#901;
|FPRC;

|PROCESO MiraActividad;
     |SI #5#1 ] 97;                  |ACABA;  |FINSI;

     |ABRE #114;
     #114#0 = #5#0;
     #114#1 = #5#1;
     |LEE 040#114=;
     |SI FSalida ! 0;
         |CIERRA #114;
         |SI nDActividad = nHActividad;  |ACABA;  |FINSI;

         |CONFI "     No Existe la Actividad, Desea Crearla ";
         |SI FSalida = 0;
             enEmpresa = #5#0;
             |SUB_EJECUTA_NP ":basica/agicen14";
         |FINSI;
         nError = 1;
         |ERROR;
         |ACABA;
     |FINSI;
     |CIERRA #114;
|FPRC;

|PROCESO CogeNombre;  |TIPO 7;
     |SI #5#1 ] 97;  |ACABA;  |FINSI;

     #5#2   = #114#4;  |PINTA #5#2;
     #901#2 = #5#2;    |PINTA #901#2;
|FPRC;

|PROCESO Tipo0m0005;
     |ENDATOS #1#901;
     |SI FSalida = 0;
         |PARA nCampo = 3;  |SI nCampo [ 18;  |HACIENDO nCampo = nCampo + 1;
               #5nCampo = #901nCampo;
         |SIGUE;
         |PINTA #5#3;
         |PINTA #5#4;
         |PINTA #5#5;
         |PINTA #5#9;
         |PINTA #5#10;
         |PINTA #5#11;
     |FINSI;
|FPRC;

|PROCESO Tipo2m005;  |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 3;  |Y #5#1 ] 97;
         |MENAV "      Linea Fija, no se puede borrar.";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo11m005;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     nError = 0;
     |HAZ MiraActividad;
     |SI nError = 1;  |ACABA;  |FINSI;

     |PUSHV 0501 2380;

     |BLANCO 1327 1760;
     |PINTA 1528, "Introduzca la linea origen : ";

     E_inf = 1;
     E_sup = 99;
     nLinea = 1557 ? 1;

     |POPV;

     nLineaAct = #5#1;
     aAlfa     = #5#2;

     #5#0 = enEmpresa;
     #5#1 = nLinea;
     |LEE 040#5=;
     |SI FSalida ! 0;
         |MENAV "     No existe linea origen.";
         |ACABA;
     |FINSI;

     |SALVA_DATOS #5;

     #5#0 = enEmpresa;
     #5#1 = nLineaAct;
     |LEE 101#5=;
     |SI FSalida ! 0;
         #5#0 = enEmpresa;
         #5#1 = nLineaAct;
         |GRABA 020#5b;
     |FINSI;

     |REPON_DATOS #5;
     #5#0 = enEmpresa;
     #5#1 = nLineaAct;
     #5#2 = aAlfa;
     |GRABA 020#5a;
     |LIBERA #5;

     aCarac  = ("00" + #5#1) % -102

     |PINTA #5#3;   |PINTA #5#4;   |PINTA #5#5;
     |PINTA #5#9;   |PINTA #5#10;  |PINTA #5#11;

     |HAZ Tipo7m0005;

     |PTEC 802;

     aAlfa   = aCarac % -101;
     nPtec   = &aAlfa;
     |PTEC nPtec;

     aAlfa   = aCarac % -201;
     nPtec  = &aAlfa;
     |PTEC nPtec;
|FPRC;

|PROCESO ClaveBaja5;
     #5#0 = enEmpresa;
     #5#1 = nDActividad;
|FPRC;

|PROCESO ClaveAlta5;
     #5#0 = enEmpresa;
     #5#1 = nHActividad;
|FPRC;

|PROGRAMA;
     |SI PARAMETRO ! "";
         aParametro = PARAMETRO % 101;
         |SI aParametro ! "X";
             |ABRE #5;
             |HAZ Tipo5m005;
             |CIERRA #5;
             |ACABA;
         |FINSI;
     |FINSI;

     |SI aParametro = "X";
         aAlfa = PARAMETRO % 205;  enEmpresa = aAlfa;
         aAlfa = PARAMETRO % 701;  enModo    = aAlfa;
     |FINSI;

     nDActividad = 1;
     nHActividad = 99;

     |SI enActividad ! 0;
         nDActividad = enActividad;
         nHActividad = enActividad;
     |FINSI;

     |ENTLINEAL #1#5, -2, enModo, 14, 1, ClaveBaja5, ClaveAlta5;
|FPRO;
