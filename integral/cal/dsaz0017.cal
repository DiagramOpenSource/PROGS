|FICHEROS;
     dsaz0017 #17;
     :/integral/def/dsam0000 #0;         || Datos Generales
     :/integral/def/dsam0123 #123;       || Correos electronicos C
     :/integral/def/dsam0124 #124;       || Correos electronicos L
|FIN;


|VARIABLES;
     aAlfa1     = "";
     aCorreoM   = "";
     aCorreoC   = "";
     nSwGraba   = 0;
     nEstadoL   = 0;
     nEstadoC   = 0;
     nEstado    = 0;
|FIN;


|CALCULO HastaC; |TIPO 0;
  |SI #17#1 < #17#0;
      |MENAV "    Error en Seleccion Clientes";
      |ERROR;
  |FINSI;
|FCAL;

|| ------------------------------------------------------------------

|PROCESO dsam0000;

     |ATRI S; |PINTA 2203, "Cliente: "; |ATRI 0;
     |PINTA 2212, #0#0;
     |PINTA 2218, #0#1;
     |PINTA 2301, #0#71;
     aCorreoM = #0#71;|QBF aCorreoM;

     aAlfa1   = #0#1; |QBF aAlfa1;
     |SI aAlfa1 =  "";  |ACABA; |FINSI;

     |DDEFECTO #124;
     #124#0 = #0#1;
     #124#1 = 1;
     |LEE 101#124=;
     nEstadoL = FSalida;
     aCorreoC = #124#3;|QBF aCorreoC;

     |SI #17#3 = "M";       ||Campo correcto el del manteminiento

         |SI aCorreoM ! "";
             #124#3 = aCorreoM;
             |SI nEstadoL = 0;
                 |SI #17#2 = "S"; |O aCorreoC = "";
                     |GRABA 020#124a;
                 |FINSI;
             |FINSI;
             |SI nEstadoL ! 0;
                 #124#2 = "CORREO PRINCIPAL";
                 |GRABA 020#124n;
             |FINSI;
         |SINO;

             |SI nEstadoL = 0;
                 |SI #17#2 = "S";
                     |BORRA 020#124a;
                 |FINSI;
             |FINSI;

         |FINSI;
     |FINSI;

     |SI #17#3 = "C";       ||Campo correcto Correo Electronico
         |SI nEstadoL = 0;
             |SI aCorreoM = ""; |O #17#2 = "S";
                 #0#71 = #124#3;
                 |GRABA 020#0a;
             |FINSI;
         |FINSI;
     |FINSI;

     |LIBERA #124;
     |LIBERA #0;

     nSwGraba = 0;
     #123#0 = #0#1;
     |LEE 101#123=;
     nEstadoC = FSalida;

     #124#0 = #0#1;
     #124#1 = 1;
     |LEE 001#124];
     |SI FSalida = 0; |Y #124#0 = #0#1;
         nSwGraba = 1;
     |FINSI;

     |SI nSwGraba = 1;
         #123#0 = #0#1;
         #123#1 = #0#27;
         #123#2 = #0#28;
         |SI nEstadoC = 0; |GRABA 020#123a; |FINSI;
         |SI nEstadoC ! 0; |GRABA 020#123n; |FINSI;
     |SINO;
         |SI nEstadoC = 0; |BORRA 020#123a; |FINSI;
     |FINSI;
     |LIBERA #123;
|FPRC;

|DEFBUCLE dsam0000;
     #0#1;
     ;
     #17#0;
     #17#1;
     be;
     NULL, dsam0000;
|FIN;

|PROCESO Tipo3;  |TIPO 3;
         |SI #17#4 = "SI";
             |ABRE #123;
             |ABRE #124;
             |BUCLE dsam0000;
             |CIERRA #124;
             |CIERRA #123;
         |FINSI;
|FPRC;


|PROGRAMA;

        |CLS;
        |CABEZA "Carga Correos Electronicos";
        |PINPA #0#17;

        |ABRE #17;
        #17#5 = "S";
        |LEE 101#17=;
        nEstado = FSalida;
        |SI nEstado = 0;
            |CONFI "    NProceso ya ejecutado. Desea Continuar ?";
            |SI FSalida ! 0;
                |LIBERA #17;
                |CIERRA #17;
                |ACABA;
            |FINSI;
            |DDEFECTO #17;
        |SINO;
            |C_M #17#2, 1, "N";
            #17#2 = "N";
        |FINSI;

        |PINDA #0#17;
        |ENDATOS #2#17;
        |SI FSalida = 0; |Y #17#4 = "SI";
            |HAZ Tipo3;
            #17#5 = "S";
            |SI nEstado = 0; |GRABA 020#17a; |FINSI;
            |SI nEstado ! 0; |GRABA 020#17n; |FINSI;
        |FINSI;
        |LIBERA #17;
        |CIERRA #17;
|FPRO;
