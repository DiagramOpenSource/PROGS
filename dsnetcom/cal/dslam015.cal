|FICHEROS;
     dsnetm00 #0;
     dslam010 #10;
     dslam015 #15;

     nomvarca@ #901;
     nomvarli@ #902;
|FIN;

|VARIABLES;
     aDirOrigen = "";
     aUsuOrigen = "";
     aFichero = "";
     aFichOrigen = "";
     aFichDestino = "";
     aFichTrabajo = "";
     aDestino2 = "";
     aWaitD = "";
     aWaitO = "";
     aBase = "";
     nCanal = 0;
     aIPRemoto = "";
     aPath = "";
     nLongitud = 0;
     aLongitud = "";
     nPos = 0;
     {1}aVersion = "";
     nCaracter = 0;
     aCaracter = "";
     aAlfa = "";

     nEmp = 0;
     nTra = 0;
     nMes = 0;
     nConcepto = 0;
     nValor = 0;
     nVarAct = 0;
     nTraAct = 0;
     nAntTra = 0;
     aCol = "";
     nCol = 0;
     aFila = "";
     nFila = 0;
     aCelda = "";
     aValor = "";
     nHoja = 0;
     nFilaCon = 0;
     swAcaba = 0;
     aEmpresa = "";
     nEmpresa = 0;
     aConvenio = "";
     nConvenio = 0;
     aNombre = "";
     nAny = 0;
     nHandle = 0;
     aMas = "";
     aAlfa1 = "";
     aAlfa2 = "";
     nHastaCol = 0;
     aHoja = "";
     aFich = "";
     &aParametro = "";
     FEstado = 0;
|FIN;

|PROCESO Mensa_015; |TIPO 7;
     |SI aParametro = "N";
          |MENSA "    Opciones: < F3 > Editar, < F4 > Borrar, < F5 > Realizar el pase";
     |SINO;
          |MENSA "    Opciones: < F6 > Restaurar copia ya importada";
     |FINSI;
|FPRC;

|PROCESO ExtraePath;
     aPath     = aVersion1;
     aLongitud = aPath % 0;
     nLongitud = aLongitud;
     |PARA nPos = 1; |SI nPos [ nLongitud; |HACIENDO nPos = nPos + 1;
           nCaracter = (nPos * 100) + 1;
           aCaracter = aPath % nCaracter;
           |SI aCaracter = ",";
               nPos = nLongitud - nPos;
               nPos = nPos + 100;
               nPos = (-1) * nPos;
               aPath = aPath % nPos;
               |SAL;
          |FINSI;
     |SIGUE;

     aPath = aPath - "[";
     aPath = aPath - "]";
     |QBF aPath;
|FPRC;

|PROCESO EditaLibro_015;
     aFichOrigen = "C:\DOCUMENTOS\XLSTMP\" + aFichero - ".xls" + "_TRA.xls";
     aDestino2 = aFichOrigen - "_TRA.xls" + "_EDI.xls";
     |RFBORRA aDestino2;
     |RCOPIA_FICHERO aFichOrigen, aDestino2;

     |DBASE aBase;   |QBF aBase;
     aWaitD = "C:\DOCUMENTOS\XLSTMP\WAIT.EXE";
     aWaitO = aBase + "plantillas/wait.exe";

     |XABRE "EC", aWaitD, nCanal;
     |SI FSalida < 0;
          aIPRemoto = "";
          |IP_REMOTO aIPRemoto;  |QBF aIPRemoto;
          |SI aIPRemoto ! "";
               |ENVIA_FICHERO aWaitO, aWaitD;
          |SINO;
               |COPIA_FICHERO aWaitO, aWaitD;
          |FINSI;
     |FINSI;

     aVersion1 = "Excel.Sheet";
     |ESPECIFICA "VERSION_PROGRAMA", aVersion;
     |HAZ ExtraePath;
     |SI aPath = "NO LOCALIZADO";
         |MENAV "0000No se ha encontrado la aplicacion Microsoft Excel";
         |ACABA;
     |FINSI;

     |QBF aPath;
     aAlfa = "C:\DOCUMENTOS\XLSTMP\wait " + aPath + " " + aDestino2;
     |RSYSTEM aAlfa;

     |RFBORRA aFichDestino;
|FPRC;

|PROCESO Variacion;
     #901#0 = nEmpresa;
     #901#1 = nTra;
     #901#2 = nMes;
     #901#3 = 0;
     |LEE 101#901=;
     |SI FSalida ! 0;
          |GRABA 020#901n;
     |FINSI;

     #902#0 = nEmpresa;
     #902#1 = nTra;
     #902#2 = nMes;
     #902#3 = 0;
     #902#4 = nConcepto;
     |LEE 101#902=;
     FEstado = FSalida;

     #dslam010#1 = nEmpresa;
     #dslam010#2 = nConvenio;
     #dslam010#3 = nConcepto;
     |LEE 000#dslam010.=;
     |SI #dslam010#6 = "I"; |Y FSalida = 0;
          #902#5 = "1";
     |SINO;
          #902#5 = "";
     |FINSI;
     #902#6 = nValor;

     |SI FEstado ! 0;
          |GRABA 020#902n;
     |SINO;
          |GRABA 020#902a;
     |FINSI;

     |LIBERA #901;
     |LIBERA #902;

     nVarAct = nVarAct + 1;
     |SI nTra ! nAntTra;
          nTraAct = nTraAct + 1;
     |FINSI;
     nAntTra = nTra;
|FPRC;

|PROCESO LeeCeldaCon;
     aCol = &nCol;   |QBF aCol;
     aFila = nFilaCon; |QBF aFila;
     aCelda = aCol + aFila;
     |QBF aValor;
     aValor = aCelda;
     |ALFACALLDLL "agixl97.dll", "peek_dato", aValor;
     |QBF aValor;
|FPRC;

|PROCESO LeeCelda015;
     aCol = &nCol;   |QBF aCol;
     aFila = nFila; |QBF aFila;
     aCelda = aCol + aFila;
     |QBF aValor;
     aValor = aCelda;
     |ALFACALLDLL "agixl97.dll", "peek_dato", aValor;
     |QBF aValor;
|FPRC;

|PROCESO LeeFilas;
     nHastaCol = 0;
     |PARA nCol = 68; |SI; |HACIENDO nCol = nCol + 1;
          nFila = 4;
          |HAZ LeeCelda015;
          |SI aValor ! "";
               nHastaCol = nCol;
          |SINO;
               |SAL;
          |FINSI;
     |SIGUE;

     nFilaCon = 4;
     |PARA nFila = 7; |SI; |HACIENDO nFila = nFila + 1;
          nCol = 65;
          |HAZ LeeCelda015;
          |SI aValor ! "";
               nTra = aValor;
               |PARA nCol = 68; |SI nCol [ nHastaCol; |HACIENDO nCol = nCol + 1;
                    |HAZ LeeCeldaCon;
                    nConcepto = aValor;

                    |HAZ LeeCelda015;
                    |SI aValor ! "";
                         nValor = aValor;
                         |HAZ Variacion;
                    |FINSI;
               |SIGUE;
          |SINO;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO CargaHoja_015;
     aHoja = nHoja;
     |QBF aHoja;
     aAlfa = "Hoja" + aHoja;
     |ALFACALLDLL "agixl97.dll", "selecciona_hoja", aAlfa;

     nCol = 67;
     nFila = 1;
     |HAZ LeeCelda015;
     |QBF aValor;
     |SI aValor = "";
          swAcaba = 1;
          |ACABA;
     |FINSI;

     aEmpresa = aValor % 105;
     nEmpresa = aEmpresa;

     nCol = 69;
     nFila = 1;
     |HAZ LeeCelda015;
     |QBF aValor;
     nMes = aValor;

     nCol = 69;
     nFila = 2;
     |HAZ LeeCelda015;
     |QBF aValor;
     nAny = aValor;

     nCol = 67;
     nFila = 2;
     |HAZ LeeCelda015;
     |QBF aValor;

     aConvenio = aValor % 103;
     nConvenio = aConvenio;

     |HAZ LeeFilas;
|FPRC;

|PROCESO PasaLibro_015;
     aFichDestino = "C:\DOCUMENTOS\XLSTMP\" + aFichero - ".xls" + "_EDI.xls";
     |XABRE "EC", aFichDestino, nHandle;
     |SI FSalida < 0;
          aFichDestino = "C:\DOCUMENTOS\XLSTMP\" + aFichero - ".xls" + "_TRA.xls";
          |XABRE "EC", aFichDestino, nHandle;
          |SI FSalida < 0;
               aAlfa = "    No se ha encontrado el Libro Excel en la carpeta "
               aAlfa = aAlfa + "local C:\DOCUMENTOS";
               |MENAV aAlfa;
               |ACABA;
          |FINSI;
     |FINSI;

     |CARGADLL "agixl97.dll";
     aFichTrabajo = aFichDestino;

     |SI FSalida < 0;
          |MENAV "0000No se puede usar agixl97.dll";
          |ACABA;
     |FINSI;

     |ALFACALLDLL "agixl97.dll", "carga_book", aFichTrabajo;

     swAcaba = 0;
     nTraAct = 0;
     nVarAct = 0;
     |PARA nHoja = 1; |SI swAcaba ! 1; |HACIENDO nHoja = nHoja + 1;
          |HAZ CargaHoja_015;
     |SIGUE;

     |DESCARGADLL "agixl97.dll";
|FPRC;

|PROCESO Tipo8;
     |ABRE #dsnetm00;
     |LEE 000#dsnetm00.p;
     |CIERRA #dsnetm00;
|FPRC;

|PROCESO BorraLibro_015;
     aAlfa = "    SATENCION: Se eliminara el archivo del Servidor de recogida ";
     aAlfa = aAlfa + "de Datos. Desea Continuar?";
     |CONFI aAlfa;
     |SI FSalida = 0;
          aFichDestino = "C:\DOCUMENTOS\XLSTMP\" + aFichero - ".xls" + "_TRA.xls";
          |RFBORRA aFichDestino;
          aFichDestino = "C:\DOCUMENTOS\XLSTMP\" + aFichero - ".xls" + "_EDI.xls";
          |RFBORRA aFichDestino;
          aFichDestino = aDirOrigen + aUsuOrigen + "/CLI_CTOS/" + aFichero;
          |FBORRA aFichDestino;
          |BORRA 020#dslam015.a;
          ||PTEC 809;
          |PTEC 814;
          |PONCONTROL 23, "si";
     |FINSI;
|FPRC;

|PROCESO ActualizaPatio;
     |BORRA 020#dslam015.a;
     #dslam015#6 = "S";
     |LEE 101#dslam015.=;
     |SI FSalida = 0;
          |GRABA 020#dslam015.a;
     |SINO;
          |GRABA 020#dslam015.n;
     |FINSI;

     aFichero = #dslam015#5;
     |QBF aFichero;

     aFichDestino = "C:\DOCUMENTOS\XLSTMP\" + aFichero - ".xls" + "_TRA.xls";
     |RFBORRA aFichDestino;
     aFichDestino = "C:\DOCUMENTOS\XLSTMP\" + aFichero - ".xls" + "_EDI.xls";
     |RFBORRA aFichDestino;

     aFichOrigen = aDirOrigen + aUsuOrigen + "/CLI_CTOS/" + aFichero;
     aFichDestino = aDirOrigen + aUsuOrigen + "/ASE_HIS_CTOS/" + aFichero;
     |COPIA_FICHERO aFichOrigen, aFichDestino;
     |FBORRA aFichOrigen;
     ||PTEC 809;
     |PTEC 814;
     |PONCONTROL 23, "si";
|FPRC;

|PROCESO RecuperaCopia;
     aAlfa = "    SDesea que este fichero vuelva a estar disponible ";
     aAlfa = aAlfa + "para importar?";
     |CONFI aAlfa;
     |SI FSalida = 0;
          || BORRA 020#dslam015.a;
          || #dslam015#6 = "N";
          || GRABA 020#dslam015.n;
          || PONCONTROL 23, "si";

          aFichero = #dslam015#5;
          |QBF aFichero;

          aFichOrigen = aDirOrigen + aUsuOrigen + "/ASE_HIS_CTOS/" + aFichero;
          aFichDestino = aDirOrigen + aUsuOrigen + "/CLI_CTOS/" + aFichero;
          |COPIA_FICHERO aFichOrigen, aFichDestino;
          ||FBORRA aFichOrigen;
     |FINSI;
|FPRC;

|PROCESO Tipo0_015; |TIPO 0;
     aDirOrigen = #dsnetm00#7;
     |QBF aDirOrigen;
     aUsuOrigen = #15#0;
     |QBF aUsuOrigen;
     aFichero = #15#5;
     |QBF aFichero;
     |SI FSalida = 11;
          |HAZ EditaLibro_015;
     |FINSI;
     |SI FSalida = 12;
          |HAZ BorraLibro_015;
     |FINSI;
     |SI FSalida = 13;
          |SALVA_FICHA #dslam015;
          #dslam015#6 = "S";
          |LEE 000#dslam015.=;
          |SI FSalida = 0;
               aAlfa = "    SEl fichero ya ha sido importado. Desea volverlo ";
               aAlfa = aAlfa + "a generar?";
               |CONFI aAlfa;
               |SI FSalida ! 0;
                    |REPON_FICHA #dslam015;
                    |ACABA;
               |FINSI;
          |FINSI;
          |REPON_FICHA #dslam015;

          |DBASS aBase;  |QBF aBase;

          |SI #0#1 = "S";
              aMas  = aBase + "laboral/def/nomvarca.mas";
              aFich = aBase + "laboral/fich/";
          |SINO;
              aMas  = aBase + "aginom/def/nomvarca.mas";
              aFich = aBase + "aginom/fich/";
          |FINSI;

          |CARGA_DEF aMas, nomvarca@;
          |SI FSalida ! 0;
              aAlfa = "0000 No puedo cargar el Def " + aMas;
              |MENAV aAlfa;
              |ACABA;
          |FINSI;

          |SI #0#1 = "S";
              aMas  = aBase + "laboral/def/nomvarli.mas";
          |SINO;
              aMas  = aBase + "aginom/def/nomvarli.mas";
          |FINSI;

          |CARGA_DEF aMas, nomvarli@;
          |SI FSalida ! 0;
              aAlfa = "0000 No puedo cargar el Def " + aMas;
              |MENAV aAlfa;

              |DESCARGA_DEF #nomvarca@;
              |ACABA;
          |FINSI;

          |PATH_DAT #901 aFich;
          |PATH_DAT #902 aFich;

          |ABRE #901;
          |ABRE #902;
          |ABRE #dslam010;

          |HAZ Mensaje1_015;
          |HAZ PasaLibro_015;
          |HAZ Restaura;

          |CIERRA #dslam010;
          |CIERRA #902;
          |CIERRA #901;

          |DESCARGA_DEF #nomvarli@;
          |DESCARGA_DEF #nomvarca@;

          |SI nTraAct = 0;
               aAlfa = "    ATENCION: No se han encontrado variaciones a ";
               aAlfa = aAlfa + "actualizar en la hoja";
               |MENAV aAlfa;
          |SINO;
               aAlfa1 = nTraAct;
               aAlfa2 = nVarAct;
               aAlfa = "    Actualizadas " + aAlfa2 + " variaciones en "
               aAlfa = aAlfa + aAlfa1 + " trabajadores."
               |MENAV aAlfa;
          |FINSI;
          |ABRE #dslam015;
          |HAZ ActualizaPatio;
          |CIERRA #dslam015;
     |FINSI;
     |SI FSalida = 14; |Y aParametro = "S";
          |HAZ RecuperaCopia;
     |FINSI;
|FPRC;

|PROCESO Mensaje1_015;
     |PUSHV 13241650;
     |BLANCO 13241650;
     |CUADRO 13241650;
     |ATRI R;
     |PINTA 1425, " IMPORTANDO VARIACIONES ";
     |ATRI 0;
|FPRC;

|PROCESO Restaura;
     |POPV;
|FPRC;
