|FICHEROS;
     dsnetm00 #0;
     dslam102 #102;
     dsnetm07 #7;
     dslaw002 #202;
     dslamx01 #99;

     labtraba@ #900;

     nomvarca@ #901;
     nomvarli@ #902;
|FIN;

|VARIABLES;
     sw = 0;
     nCampo         = 0;
     nCampo1        = 0;
     nCampo2        = 0;
     nCampo3        = 0;
     nRegis         = 0;
     nAscci         = 0;
     nHandle        = 0;
     nError         = 0;

     aParametro     = "";
     aCampo         = "";
     aCampo1        = "";
     aCampo2        = "";
     aCampo3        = "";
     aCampo4        = "";
     aCampo5        = "";
     aCampo6        = "";
     aCampo7        = "";
     aCampo8        = "";
     aCampo9        = "";
     aCampo10       = "";
     aCampo11       = "";
     aCampo12       = "";
     aCampo15       = "";
     aCampo16       = "";
     aCaracter      = "";
     aWeb           = "";
     aAbre          = "";
     aAbreSmf       = "";
     aAlfa          = "";
     aBase          = "";
     aMas           = "";
     aFich          = "";
     aTecla         = "";
     aCopialo1      = "";
     aCopialo2      = "";
     &aTituloB      = "";
     nExiste        = 0;
|FIN;

|| Recogida de la Web ------------------------------------------------------

|PROCESO CargaCampos;
     |SI nCampo = 1;  aCampo1  = aCampo;  |FINSI;
     |SI nCampo = 2;  aCampo2  = aCampo;  |FINSI;
     |SI nCampo = 3;  aCampo3  = aCampo;  |FINSI;
     |SI nCampo = 4;  aCampo4  = aCampo;  |FINSI;
     |SI nCampo = 5;  aCampo5  = aCampo;  |FINSI;
     |SI nCampo = 6;  aCampo6  = aCampo;  |FINSI;
     |SI nCampo = 7;  aCampo7  = aCampo;  |FINSI;
     |SI nCampo = 8;  aCampo8  = aCampo;  |FINSI;
     |SI nCampo = 9;  aCampo9  = aCampo;  |FINSI;
     |SI nCampo = 10; aCampo10 = aCampo;  |FINSI;
     |SI nCampo = 11; aCampo11 = aCampo;  |FINSI;
     |SI nCampo = 12;
          aCampo12 = aCampo % 150;
          aCampo15 = aCampo % 5150;     ||Observaciones2
          aCampo16 = aCampo % 10150;    ||Observaciones3
     |FINSI;
|FPRC;

|PROCESO GrabaRegistros;
     #102#0  = "N";
     #102#1  = aCampo1;
     #102#2  = aCampo2;
     #102#3  = aCampo3;
     #102#4  = aCampo4;
     #102#5  = aCampo5;
     #102#6  = aCampo6;
     #102#7  = aCampo7;
     #102#8  = aCampo8;
     #102#9  = aCampo9;
     #102#10 = aCampo10;
     #102#11 = aCampo11;
     #102#12 = aCampo12;
     #102#15 = aCampo15;
     #102#16 = aCampo16;

     #7#1 = "LABORAL";
     #7#2 = #102#5;
     |LEE 040#7=;
     |SI FSalida ! 0;  |DDEFECTO #7;  |FINSI;

     #102#13 = #7#3;

     #900#0 = #102#5;
     #900#1 = #102#6;
     |LEE 040#900=;
     |SI FSalida ! 0;  |DDEFECTO #900;  |FINSI;

     #102#14 = #900#2;


     nExiste = 0;
     |SALVA_FICHA #102;
     #102#0 = "B";  |LEE 000#102=; |SI FSalida = 0;    nExiste = 1;   |FINSI;
     #102#0 = "S";  |LEE 000#102=; |SI FSalida = 0;    nExiste = 1;   |FINSI;
     |REPON_FICHA #102;

     |SI nExiste = 0;
          |GRABA 000#102n;
          |SI FSalida = 0; nRegis = nRegis + 1; |FINSI;
     |FINSI;

     nCampo   = 1;
     aCampo   = "";
     aCampo1  = "";
     aCampo2  = "";
     aCampo3  = "";
     aCampo4  = "";
     aCampo5  = "";
     aCampo6  = "";
     aCampo7  = "";
     aCampo8  = "";
     aCampo9  = "";
     aCampo10 = "";
     aCampo11 = "";
     aCampo12 = "";
     aCampo15 = "";
     aCampo16 = "";
|FPRC;

|PROCESO LeeRegistros;
     aCampo        = "";
     nCampo        = 1;
     nRegis        = 0;

     |PARA;  |SI  FSalida ] 0;  |HACIENDO;
             |XLEE nHandle, 1, aCaracter;
             |SI FSalida [ 0;  |SAL;  |FINSI;

             nAscci = &aCaracter;

             |SI nCampo = 13;  |Y nAscci = 10;
                 |HAZ CargaCampos;

                || nRegis = nRegis + 1;
                 |HAZ GrabaRegistros;
             |FINSI;

             |SI aCaracter = "|";
                 |HAZ CargaCampos;

                 nCampo = nCampo + 1;
                 aCampo = "";
             |FINSI;

             |SI nAscci ! 10;  |Y nAscci ! 13;  |Y aCaracter ! "|";
                 aCampo = aCampo + aCaracter;
             |FINSI;
     |SIGUE;
|FPRC;

|PROCESO RecogerDatos;
     |ABRE #0;
     |LEE 040#0p;
     |SI FSalida ! 0;  |CIERRA #0;  |ACABA;  |FINSI;
     |CIERRA #0;

     aWeb = #0#7;  |QBF aWeb;
     |SI aWeb = "";  |ACABA;  |FINSI;

     aAbreSmf = aWeb + "bajas.smf";
     |PARA;  |SI;  |HACIENDO;
          |XABRE "E", aAbreSmf, nHandle;
          |SI FSalida < 0;  |SAL;  |FINSI;
          |SLEEP 1;
          |PULSATECLA;
     |SIGUE;

     |XABRE "W", aAbreSmf, nHandle;
     |SI FSalida ] 0;
         aAlfa = "Grabando";
         |XGRABA nHandle, aAlfa;
     |FINSI;
     |XCIERRA nHandle;

     |ABRE #102;
     |ABRE #7;
     |ABRE #900;
     |SELECT #2#7;

     aCopialo1 = aWeb + "bajas.txt";
     aCopialo2 = aWeb + "bajastra.txt";
     |COPIA_FICHERO aCopialo1, aCopialo2;

     aAbre = aWeb + "bajastra.txt";
     |XABRE "AB", aAbre, nHandle;
     |SI FSalida ] 0;
         |HAZ LeeRegistros;
     |FINSI;
     |XCIERRA nHandle;

     |CIERRA #7;
     |CIERRA #102;
     |CIERRA #900;

     ||FBORRA aCopialo1;
     |FBORRA aCopialo2;
     |FBORRA aAbreSmf;
|FPRC;

|| Actualizar Datos ------------------------------------------------------

|PROCESO GrabaDatos;
     |ABRE #900;
     #900#0 = #102#5;
     #900#1 = #102#6;
     |LEE 101#900=;
     |SI FSalida ! 0;
         |CIERRA #900;
         |MENAV "0000 No puedo Actualizar el Registro NO existe el Trabajador.";
         |ACABA;
     |FINSI;

     |SI #900#44 = "B";
         |MENAV "0000 No puedo Actualizar el Registro el Trabajador esta de baja.";
         |LIBERA #900;
         |CIERRA #900;
         |ACABA;
     |FINSI;

     aAlfa   = (("00" + #102#8) % -102) + "." + (("00" + #102#9) % -102) + "." + #102#10;
     #900#37 = aAlfa;
     |GRABA 020#900a;
     |LIBERA #900;
     |CIERRA #900;

     #901#0 = #102#5;
     #901#1 = #102#6;
     #901#2 = #102#9;
     #901#3 = 0;
     |LEE 101#901=;
     |SI FSalida ! 0;
         |DDEFECTO #901;
         #901#0 = #102#5;
         #901#1 = #102#6;
         #901#2 = #102#9;
         #901#3 = 0;
         |GRABA 020#901b;
     |FINSI;

     aAlfa   = (("00" + #102#8) % -102) + "." + (("00" + #102#9) % -102) + "." + #102#10;
     #901#16 = aAlfa;

     |GRABA 020#901a;
     |LIBERA #901;

     #902#0  = #901#0;
     #902#1  = #901#1;
     #902#2  = #901#2;
     #902#3  = #901#3;
     #902#4  = 0;
     |LEE 040#902];
     |SI FSalida ! 0;  |O #902#0  ! #901#0;  |O #902#1  ! #901#1;
                       |O #902#2  ! #901#2;  |O #902#3  ! #901#3;
         |DDEFECTO #902;
         #902#0  = #901#0;
         #902#1  = #901#1;
         #902#2  = #901#2;
         #902#3  = #901#3;
         #902#4  = 999;
         #902#6  = "<INDEFINIDO>";
         |GRABA 020#902n;
         |LIBERA #902;
     |FINSI;

     |BORRA 020#102a;
     |LIBERA #102;

     #102#0 = "S";
     |GRABA 020#102n;
     |LIBERA #102;

     |PONCONTROL 23, "SI";
     |PTEC 808;
|FPRC;

|PROCESO Actualiza;
     |CONFI "0000SSeguro de Actualizar el Registro";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |LEE 101#102=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI #0#1 = "S";
         aMas  = aBase + "laboral/def/nomvarca.mas";
         aFich = aBase + "laboral/fich/";
     |SINO;
         aMas  = aBase + "aginom/def/nomvarca.mas";
         aFich = aBase + "aginom/fich/";
     |FINSI;

     |CARGA_DEF aMas, nomvarca@;
     |SI FSalida ! 0;
         aAlfa = "0000 No puedo cargar el Def " + aMas;
         |MENAV aAlfa;
         |ACABA;
     |FINSI;

     |SI #0#1 = "S";
         aMas  = aBase + "laboral/def/nomvarli.mas";
     |SINO;
         aMas  = aBase + "aginom/def/nomvarli.mas";
     |FINSI;

     |CARGA_DEF aMas, nomvarli@;
     |SI FSalida ! 0;
         aAlfa = "0000 No puedo cargar el Def " + aMas;
         |MENAV aAlfa;

         |DESCARGA_DEF #nomvarca@;
         |ACABA;
     |FINSI;

     |PATH_DAT #901 aFich;
     |PATH_DAT #902 aFich;

     |ABRE #901;
     |ABRE #902;

     |HAZ GrabaDatos;

     |CIERRA #901;
     |CIERRA #902;

     |DESCARGA_DEF #nomvarli@;
     |DESCARGA_DEF #nomvarca@;
|FPRC;

|| Impresion de datos ---------------------------------------------------------------------------------------

|PROCESO ImpeWebBa;
     |SI #102#0 = aParametro;
          |SI aParametro = "N" ;
               aTituloB = "    Informe de bajas de la web    ";
          |SINO;
               aTituloB = "Informe del hist¢rico web de bajas";
          |FINSI;
          |PRINT;
     |FINSI;
|FPRC;

|DEFBUCLE dslam102;
     #102#1;
     ;
     "0",#99#0,"000000000",#99#2,"00000000";
     "z",#99#1,"zzzzzzzzz",#99#3,"zzzzzzzz";
     ;
     NULL,ImpeWebBa;
|FIN;

|PROCESO ImprimirBa;
     |AVISO;
     |CONFI "0000NDesea imprimir informe?";
     |SI FSalida = 0;
          |PUSHV 0501 2380;
          |CLS;
          |CABEZA "Recogida de la Web";
          |PINPA #0#99;
          |PINDA #0#99;
          |ABRE #99;
          |ENDATOS #1#99;
          |SI FSalida = 0;
             |IMPRE 0;
             |SI FSalida = 0;
               |INFOR "dslam102";
               |SI FSalida = 0;
                    |BUCLE dslam102;
                    sw = 1;
                    |PTEC 807;
                    |PIEINF;
                    |FININF;
               |SINO;
                    |MENAV "0000 El informe no esta disponible.";
               |FINSI;
           |SINO;
               |MENAV "0000 La impresora selecionada no esta disponible";
           |FINSI;
           |FINIMP;
           |CIERRA #99;
      |FINSI;
|FINSI;
|POPV;
|FPRC;
|| Datos Pantalla --------------------------------------------------------

|PROCESO Mensa;  |TIPO 7;
     |SI #102#0 = "N";
         |MENSA "0000 <F2> Ver  Datos. <F3> Actualizar Baja. <F4> Borrar Registro. <F5> Imprimir";
     |SINO;
         |MENSA "0000 <F2> Pantalla todos los Datos. <F5> Imprimir";
     |FINSI;
|FPRC;

|PROCESO Tipo11;  |TIPO 11;
     |SI FSalida = 10;
         |PARA nCampo = 0;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               #202nCampo = #102nCampo;
         |SIGUE;

          #202#15 = #102#15;  ||Observaciones2
          #202#16 = #102#16;  ||Observaciones3

         |PUSHV 0501 2380;
         |PINPA #0#202;
         |PINDA #0#202;
         |LEETECLA aTecla;
         |POPV;

         |ERROR;
     |FINSI;

     |SI FSalida = 11;  |Y #102#0 = "N";
         |HAZ Actualiza;
     |FINSI;

     |SI FSalida = 12;  |Y #102#0 = "N";
         |CONFI "0000NSeguro de Borrar el Registro ";
         |SI FSalida ! 0;
             |ERROR;
             |ACABA;
         |FINSI;

         |LEE 101#102=;
         |BORRA 020#102a;
         |LIBERA #102;

          #102#0 = "B";
          |GRABA 020#102n;

         |PONCONTROL 23, "SI";
         |PTEC 808;
     |FINSI;
     |SI FSalida = 13;
          |HAZ ImprimirBa;
     |FINSI;
|FPRC;


|| Comienzo Programa -----------------------------------------------------


|RUTINA ClaveBaja102;
     #102#0 = aParametro;
     #102#1 = "";
     #102#2 = "";
     #102#3 = "01.01.0000";
     #102#4 = "";
|FRUT;

|RUTINA ClaveAlta102;
     #102#0 = aParametro;
     #102#1 = "zzzzzzzzzz";
     #102#2 = "zzzzzzzzzz";
     #102#3 = "31.12.9999";
     #102#4 = "zzzzzzzz";
|FRUT;

|PROGRAMA;
     |ABRE #0;
     |LEE 040#0p;
     |SI FSalida ! 0;  |DDEFECTO #0;  |FINSI;
     |CIERRA #0;

     |DBASS aBase;  |QBF aBase;

     |SI #0#1 = "S";
         aMas  = aBase + "laboral/def/labtraba.mas";
         aFich = aBase + "laboral/fich/";
     |SINO;
         aMas  = aBase + "aginom/def/labtraba.mas";
         aFich = aBase + "aginom/fich/";
     |FINSI;

     |CARGA_DEF aMas, labtraba@;
     |SI FSalida ! 0;
         aAlfa = "0000 No puedo cargar el Def " + aMas;
         |MENAV aAlfa;
         |ACABA;
     |FINSI;

     |PATH_DAT #900 aFich;

     |CLS;
     |CABEZA "Bajas";

     |PINPA #0#102;
     |MENSA "0000 Recogiendo Datos de la Web.";

     aParametro = PARAMETRO;  |QBF aParametro;

     |SI aParametro = "N";
         |HAZ RecogerDatos;

         aAlfa = "0000 Registros Recogidos " + nRegis;
         |MENAV aAlfa;

         |MENSA "0000                            ";
     |FINSI;

     sw = 1;
     |PARA; |SI sw ! 0; |HACIENDO;
          sw = 0;
          |ENTLINEAL #1#102, -2, 4, 22, 1, ClaveBaja102, ClaveAlta102;
     |SIGUE;

     |DESCARGA_DEF #labtraba@;
|FPRO;
