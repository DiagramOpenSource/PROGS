|FICHEROS;
     dsnetm00 #0;
     dslam104 #104;
     dsnetm07 #7;
     dslaw004 #204;
     dslam004 #4;
     dslam005 #5;
     dslam006 #6;
     dslamx01 #99;

     labtraba@ #900;
     labcentr@ #901;
     nomcateg@ #902;
|FIN;

|VARIABLES;
     sw = 0;
     nCampo         = 0;
     nRegis         = 0;
     nAscci         = 0;
     nHandle        = 0;
     nError         = 0;
     nLinea         = 0;

     aParametro     = "";
     aCampo         = "";
     aCaracter      = "";
     aWeb           = "";
     aAbre          = "";
     aAbreSmf       = "";
     aAlfa          = "";
     aBase          = "";
     aMas           = "";
     aFichLab       = "";
     aTecla         = "";
     aCopialo1      = "";
     aCopialo2      = "";
     &aTituloPa     = "";
     nNume = 0;
     aFich = "";
     nExiste   = 0;
|FIN;

|| Recogida de la Web ------------------------------------------------------

|PROCESO CargaCampos;
     #104nCampo = aCampo > " ";
     |SI nCampo = 3;
          #104nCampo = aCampo;
     |FINSI;
|FPRC;

|PROCESO GrabaRegistros;
     #104#0  = "N";

     #900#0 = #104#5;
     #900#1 = #104#6;
     |LEE 040#900=;
     |SI FSalida ! 0;  |DDEFECTO #900;  |FINSI;

     #7#1 = "LABORAL";
     #7#2 = #104#5;
     |LEE 040#7=;
     |SI FSalida ! 0;  |DDEFECTO #7;  |FINSI;

     #5#0 = #104#12;
     |LEE 040#5=;
     |SI FSalida ! 0;  |DDEFECTO #5;  |FINSI;

     #901#0 = #104#5;
     #901#1 = #104#13;
     |LEE 040#901=;
     |SI FSalida ! 0;  |DDEFECTO #901;  |FINSI;

     #4#0 = #104#8;
     |LEE 040#4=;
     |SI FSalida ! 0;  |DDEFECTO #4;  |FINSI;

     #6#0 = #104#8;
     #6#1 = #104#9;
     |LEE 040#6=;
     |SI FSalida ! 0;  |DDEFECTO #6;  |FINSI;

     #104#17 = #7#3;
     #104#18 = #900#2;
     #104#19 = #4#1;
     #104#20 = #6#2;
     #104#21 = #5#1;
     #104#22 = #901#2;



     nExiste = 0;
     |SALVA_FICHA #104;
     #104#0 = "B";  |LEE 000#104=; |SI FSalida = 0;    nExiste = 1;   |FINSI;
     #104#0 = "S";  |LEE 000#104=; |SI FSalida = 0;    nExiste = 1;   |FINSI;
     |REPON_FICHA #104;

     |SI nExiste = 0;
          |GRABA 000#104n;
          |SI FSalida = 0; nRegis = nRegis + 1; |FINSI;
     |FINSI;

     nCampo   = 1;
     aCampo   = "";
     |DDEFECTO #104;
|FPRC;

|PROCESO LeeRegistros;
     aCampo        = "";
     nCampo        = 1;
     nRegis        = 0;

     |PARA;  |SI  FSalida ] 0;  |HACIENDO;
             |XLEE nHandle, 1, aCaracter;
             |SI FSalida [ 0;  |SAL;  |FINSI;

             nAscci = &aCaracter;

             |SI nCampo = 17;  |Y nAscci = 10;
                 |HAZ CargaCampos;

                 ||nRegis = nRegis + 1;
                 |HAZ GrabaRegistros;
             |FINSI;

             |SI aCaracter = "|";
                 |HAZ CargaCampos;

                 nCampo = nCampo + 1;
                 aCampo = "";
             |FINSI;

             |SI nAscci ! 10;  |Y nAscci ! 13;  |Y aCaracter ! "|";
                 aCampo = aCampo + aCaracter;
             |FINSI;
     |SIGUE;
|FPRC;

|PROCESO RecogerDatos;
     |ABRE #0;
     |LEE 040#0p;
     |SI FSalida ! 0;  |CIERRA #0;  |ACABA;  |FINSI;
     |CIERRA #0;

     aWeb = #0#7;  |QBF aWeb;
     |SI aWeb = "";  |ACABA;  |FINSI;

     aAbreSmf = aWeb + "parte_variaciones.smf";
     |PARA;  |SI;  |HACIENDO;
          |XABRE "E", aAbreSmf, nHandle;
          |SI FSalida < 0;  |SAL;  |FINSI;
          |SLEEP 1;
          |PULSATECLA;
     |SIGUE;

     |XABRE "W", aAbreSmf, nHandle;
     |SI FSalida ] 0;
         aAlfa = "Grabando";
         |XGRABA nHandle, aAlfa;
     |FINSI;
     |XCIERRA nHandle;

     |ABRE #104;
     |ABRE #4;
     |ABRE #5;
     |ABRE #6;
     |ABRE #7;
     |ABRE #900;
     |ABRE #901;
     |SELECT #2#7;

     aCopialo1 = aWeb + "parte_variaciones.txt";
     aCopialo2 = aWeb + "parte_variacionestra.txt";
     |COPIA_FICHERO aCopialo1, aCopialo2;

     aAbre = aWeb + "parte_variacionestra.txt";
     |XABRE "AB", aAbre, nHandle;
     |SI FSalida ] 0;
         |HAZ LeeRegistros;
     |FINSI;
     |XCIERRA nHandle;

     |CIERRA #4;
     |CIERRA #5;
     |CIERRA #6;
     |CIERRA #7;
     |CIERRA #104;
     |CIERRA #900;
     |CIERRA #901;

     ||FBORRA aCopialo1;
     |FBORRA aCopialo2;
     |FBORRA aAbreSmf;
|FPRC;

|| Actualizar Datos ------------------------------------------------------

|PROCESO GrabaLabtraba;
     nError = 0;                   || No he hecho nada aun
     |SI #104#9 ! 0;               || Cambio de Categoria
          #902#0 = #104#8;
          #902#1 = #104#9;
          |LEE 000#902=;
          |SI FSalida ! 0;
               nError = 1;
               aAlfa = "0000 No puedo Actualizar la Categoria. ";
               aAlfa = aAlfa + "No esta definida la Categoria en el Convenio";
               |MENAV aAlfa;
          |SINO;
               #900#17 = #104#9;        || nuevo codigo de categoria
               #900#18 = #104#20;       || nueva descripcion de categoria
          |FINSI;
     |FINSI;
     nNume = #104#12;

/*   DESACTIVADO PORQUE NO PROCEDE HACERLO SIN CAMBIO DE CODIGO DE TRABAJADOR
     |SI nNume ! 0;                || Cambio de Contrato
          aAlfa = #104#12;
          aAlfa = "000" + aAlfa;
          aAlfa = aAlfa % -103;
          #900#45 = aAlfa;         || Nuevo Contrato
     |FINSI;
*/
     |SI #104#13 ! 0;               || Cambio de Centro de Trabajo
          #901#0 = #104#5;
          #901#1 = #104#13;
          |LEE 000#901=;
          |SI FSalida ! 0;
               nError = 1;
               |MENAV "0000 No puedo Actualizar el Centro. NO existe el Centro.";
          |SINO;
               #900#77 = #104#13;        || nuevo centro de Trabajo
          |FINSI;
     |FINSI;

/*   DESACTIVADO PORQUE NO PROCEDE HACERLO SIN CAMBIO DE CODIGO DE TRABAJADOR
     |SI #104#11 ! 0;              || Cambio Nro de Horas Semanales
          #900#234 = #104#11;      || nuevo nro de horas semanales
     |FINSI;
*/
     |GRABA 020#900.a;
     |LIBERA #900;
|FPRC;

|PROCESO GrabaDatos;
     |ABRE #900;
     #900#0 = #104#5;
     #900#1 = #104#6;
     |LEE 040#900=;
     |SI FSalida ! 0;
         |CIERRA #900;
         |MENAV "0000 No puedo Actualizar el Registro NO existe el Trabajador.";
         |ACABA;
     |FINSI;

     |SI #900#44 = "B";
         |CIERRA #900;
         |MENAV "0000 No puedo Actualizar el Registro el Trabajador esta de baja.";
         |ACABA;
     |FINSI;

     nError = 0;
     |HAZ GrabaLabtraba;
     |CIERRA #900;


     |SI nError = 1;
          aAlfa = "    Se han encontrado errores: la actualizacion no se ha ";
          aAlfa = aAlfa + "realizado completa"
          |MENAV aAlfa;
          aAlfa = "    Revise los datos a actualizar y vuelva a intentarlo";
          |MENAV aAlfa;
          |ACABA;
     |FINSI;

     |SI nError = 0;
          aAlfa = "    Actualizacion completa realizada con exito.";
          |MENAV aAlfa;
     |FINSI;

     |BORRA 020#104a;
     |LIBERA #104;

     #104#0 = "S";
     |GRABA 020#104n;
     |LIBERA #104;

     |PONCONTROL 23, "SI";
     |PTEC 808;
|FPRC;

|PROCESO Actualiza;
     |CONFI "0000SSeguro de Actualizar el Registro";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |LEE 101#104=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI #0#1 = "S";
         aMas  = aBase + "laboral/def/labtraba.mas";
         aFich = aBase + "laboral/fich/";
     |SINO;
         aMas  = aBase + "aginom/def/labtraba.mas";
         aFich = aBase + "aginom/fich/";
     |FINSI;

     |CARGA_DEF aMas, labtraba@;
     |SI FSalida ! 0;
         aAlfa = "0000 No puedo cargar el Def " + aMas;
         |MENAV aAlfa;
         |ACABA;
     |FINSI;

     |SI #0#1 = "S";
         aMas  = aBase + "laboral/def/labcentr.mas";
     |SINO;
         aMas  = aBase + "aginom/def/labcentr.mas";
     |FINSI;

     |CARGA_DEF aMas, labcentr@;
     |SI FSalida ! 0;
         aAlfa = "0000 No puedo cargar el Def " + aMas;
         |MENAV aAlfa;

         |DESCARGA_DEF #labtraba@;
         |ACABA;
     |FINSI;

     |SI #0#1 = "S";
         aMas  = aBase + "laboral/def/nomcateg.mas";
     |SINO;
         aMas  = aBase + "aginom/def/nomcateg.mas";
     |FINSI;

     |CARGA_DEF aMas, nomcateg@;
     |SI FSalida ! 0;
         aAlfa = "0000 No puedo cargar el Def " + aMas;
         |MENAV aAlfa;

         |DESCARGA_DEF #labtraba@;
         |DESCARGA_DEF #labcentr@;
         |ACABA;
     |FINSI;

     |PATH_DAT #900 aFich;
     |PATH_DAT #901 aFich;
     |PATH_DAT #902 aFich;

     |ABRE #900;
     |ABRE #901;
     |ABRE #902;

     |HAZ GrabaDatos;

     |CIERRA #900;
     |CIERRA #901;
     |CIERRA #902;

     |DESCARGA_DEF #labcentr@;
     |DESCARGA_DEF #labtraba@;
     |DESCARGA_DEF #nomcateg@;
|FPRC;

|| Impresion dedatos------------------------------------------------------------------------

|PROCESO ImpWebPa;
     |SI #104#0 = aParametro;
          |SI aParametro = "N";
               aTituloPa = "       Informe web de parte variaci¢n       ";
          |SINO;
               aTituloPa = "Informe del hist¢rico web de parte variaci¢n";
          |FINSI;
          |PRINT;
     |FINSI;
|FPRC;

|DEFBUCLE dslam104;
     #104#1;
     ;
     "0",#99#0,"000000000",#99#2,"00000000";
     "z",#99#1,"zzzzzzzzz",#99#3,"zzzzzzzz";
     ;
     NULL,ImpWebPa;
|FIN;

|PROCESO ImpreWebPa ;
     |AVISO;
     |CONFI "0000NDesea imprimir el informe?";
     |SI FSalida = 0;
          |PUSHV 0501 2380;
          |CLS;
          |CABEZA "Recogida de la Web";
          |PINPA #0#99;
          |PINDA #0#99;
          |ABRE #99;
          |ENDATOS #1#99;
          |SI FSalida = 0;
             |IMPRE 0;
             |SI FSalida = 0;
               |INFOR "dslam104";
               |SI FSalida = 0;
                    |BUCLE dslam104;
                    sw = 1;
                    |PTEC 807;
                    |PIEINF;
                    |FININF;
               |SINO;
                    |MENAV "0000 El informe no esta disponible";
               |FINSI;
          |SINO;
               |MENAV "0000 La impresora selecionada no esta acesible";
          |FINSI;
          |FINIMP;
          |CIERRA #99;
     |FINSI;
|FINSI;
|POPV;
|FPRC;

|| Datos Pantalla --------------------------------------------------------

|PROCESO Mensa;  |TIPO 7;
     |SI #104#0 = "N";
          |MENSA "0000 <F2> Ver Datos. <F3> Actualizar Trabajador. <F4> Borrar Registro. <F5> Imprimir";
     |SINO;
          |MENSA "0000 <F2> Pantalla todos los Datos. <F5> Imprimir";
     |FINSI;
|FPRC;



|PROCESO Tipo11;  |TIPO 11;
     |SI FSalida = 10;
         |PARA nCampo = 0;  |SI nCampo [ 22;  |HACIENDO nCampo = nCampo + 1;
               #204nCampo = #104nCampo;
         |SIGUE;

         |MENSA "    Pulse cualquier tecla para volver";
         |PUSHV 0501 2380;
         |PINPA #0#204;
         |PINDA #0#204;
         |LEETECLA aTecla;
         |POPV;
         |MENSA "0000 <F2> Ver Datos. <F3> Actualizar Trabajador. <F4> Borrar Registro. <F5> Imprimir";

         |ERROR;
     |FINSI;

     |SI FSalida = 11;  |Y #104#0 = "N";
         |HAZ Actualiza;
     |FINSI;

     |SI FSalida = 12;  |Y #104#0 = "N";
         |CONFI "0000NSeguro de Borrar el Registro ";
         |SI FSalida ! 0;
             |ERROR;
             |ACABA;
         |FINSI;

         |LEE 101#104=;
         |BORRA 020#104a;
         |LIBERA #104;

          #104#0 = "B"
          |GRABA 020#104n;

         |PONCONTROL 23, "SI";
         |PTEC 808;
     |FINSI;
     |SI FSalida = 13;
          |HAZ ImpreWebPa;
     |FINSI;
|FPRC;


|| Comienzo Programa -----------------------------------------------------

|RUTINA ClaveBaja101;
     #104#0 = aParametro;
     #104#1 = "";
     #104#2 = "";
     #104#3 = "01.01.0000";
     #104#4 = "";
|FRUT;

|RUTINA ClaveAlta101;
     #104#0 = aParametro;
     #104#1 = "zzzzzzzzzz";
     #104#2 = "zzzzzzzzzz";
     #104#3 = "31.12.9999";
     #104#4 = "zzzzzzzz";
|FRUT;

|PROGRAMA;
     |ABRE #0;
     |LEE 040#0p;
     |SI FSalida ! 0;  |DDEFECTO #0;  |FINSI;
     |CIERRA #0;

     |DBASS aBase;  |QBF aBase;

     |SI #0#1 = "S";
         aFichLab = aBase + "laboral/fich/";
         aMas     = aBase + "laboral/def/labtraba.mas";
     |SINO;
         aFichLab = aBase + "aginom/fich/";
         aMas     = aBase + "aginom/def/labtraba.mas";
     |FINSI;

     |CARGA_DEF aMas, labtraba@;
     |SI FSalida ! 0;
         aAlfa = "0000 No puedo cargar el Def " + aMas;
         |MENAV aAlfa;
         |ACABA;
     |FINSI;

     |SI #0#1 = "S";
         aMas  = aBase + "laboral/def/labcentr.mas";
     |SINO;
         aMas  = aBase + "aginom/def/labcentr.mas";
     |FINSI;

     |CARGA_DEF aMas, labcentr@;
     |SI FSalida ! 0;
         aAlfa = "0000 No puedo cargar el Def " + aMas;
         |MENAV aAlfa;
         |DESCARGA_DEF #labtraba@;

         |ACABA;
     |FINSI;

     |PATH_DAT #900 aFichLab;
     |PATH_DAT #901 aFichLab;

     |CLS;
     |CABEZA "Parte Variacion";

     |PINPA #0#104;
     |MENSA "0000 Recogiendo Datos de la Web.";

     aParametro = PARAMETRO;  |QBF aParametro;

     |SI aParametro = "N";
         |HAZ RecogerDatos;

         aAlfa = "0000 Registros Recogidos " + nRegis;
         |MENAV aAlfa;

         |MENSA "0000                            ";
     |FINSI;

     sw = 1;
     |PARA; |SI sw ! 0; |HACIENDO;
          sw = 0;
          |ENTLINEAL #1#104, -2, 4, 22, 1, ClaveBaja101, ClaveAlta101;
     |SIGUE;

     |DESCARGA_DEF #labcentr@;
     |DESCARGA_DEF #labtraba@;
|FPRO;
