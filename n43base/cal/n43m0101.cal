|FICHEROS;
     n43m0101;
|FIN;

|VARIABLES;
     nHandle             = 0;
     nLong               = 0;
     nCampo              = 0;
     nCaracter           = 0;
     nPos                = 0;

     aAlfa               = "";
     aBase               = "";
     aAbre               = "";
     aCadena             = "";
     aLong               = "";
     aTab                = "";
     aCarac1             = "";
     aCampoA             = "";
     aCampoB             = "";
|FIN;

|PROGRAMA;
     |CONSULTA_CLAVE #1#n43m0101;
|FPRO;

|PROCESO GrabaReg;
     |QBF aCampoA;
     |QBF aCampoB;

     |SI aCampoA = "";  |ACABA;  |FINSI;

     #n43m0101#0 = aCampoA;
     |LEE 000#n43m0101.=;
     |SI FSalida ! 0;
         |DDEFECTO #n43m0101;
         #n43m0101#0 = aCampoA;
         |GRABA 020#n43m0101.b;
     |FINSI;

     #n43m0101#1 = aCampoB;

     |GRABA 020#n43m0101.a;
     |LIBERA #n43m0101;
|FPRC;

|PROCESO RecogeCSV;
     |DBASE aBase;

     aAbre = aBase + "documentos/patrones/divisas.csv";
     |XABRE "", aAbre, nHandle;
     |PARA;  |SI FSalida ] 0;  |HACIENDO;
          |XLEE nHandle, 250, aAlfa;
          |SI FSalida < 0;  |SAL;  |FINSI;

          aCampoA  = "";
          aCampoB  = "";

          aCadena = aAlfa;  |QBF aCadena;
          aLong   = aCadena % 0;
          nLong   = aLong;
          nCampo  = 1;
          aTab    = ";";

          |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
                nPos    = (100 * nCaracter) + 1;
                aCarac1 = aCadena % nPos;
                |SI aCarac1 = aTab;
                    nCampo = nCampo + 1;
                |SINO;
                    |SI nCampo = 1;  aCampoA  = aCampoA  + aCarac1;  |FINSI;
                    |SI nCampo = 2;  aCampoB  = aCampoB  + aCarac1;  |FINSI;

                    |SI nCampo > 2;  |SAL;  |FINSI;
                |FINSI;
          |SIGUE;

          |HAZ GrabaReg;
     |SIGUE;
     |XCIERRA nHandle;
|FPRC;

|PROCESO T80;  |TIPO 80;
     |ABRET #n43m0101;

     |LEE 000#n43m0101.p;
     |SI FSalida ! 0;
         |HAZ RecogeCSV;
     |FINSI;
|FPRC;

|PROCESO T90;  |TIPO 90;
     |CIERRAT #n43m0101;
|FPRC;
