|FICHEROS;
     n43m0102;
     n43m0103;
     n43m0104;
     n43m0105;

     n43w0001,MANTE;
|FIN;

|VARIABLES;
     n0Vent              = 0;
     n1Vent              = 0;
     n2Vent              = 0;
     nGrid102            = -1;
     nBtnAlt             = 0;
     nBtnMod             = 0;
     nBtnBor             = 0;
     nRango              = 0;
     nHandle             = 0;
     nLong               = 0;
     nCampo              = 0;
     nCaracter           = 0;
     nPos                = 0;
     nTree102            = 0;

     aBase               = "";
     aAbre               = "";
     aCadena             = "";
     aLong               = "";
     aTab                = "";
     aCarac1             = "";
     aCampoA             = "";
     aCampoB             = "";
     aCampoC             = "";
     aEsc1               = "";
     aEsc2               = "";
     aRetu               = "";
     aTecla              = "";
     aAlfa               = "";
     aBaseN43            = "";
     aPathLocal          = "";
     aFicTmp             = "";
     aContenido          = "";
     aVer1               = "";
     aVer2               = "";
     aOri                = "";
     aDes                = "";
     aIP                 = "";

  {1}aConte              = "";

 {-1}aVent               = "";
     aVent1              = 0;
     aVent2              = 0;
     aVent3              = 0;
     aVent4              = 0;
     aVent5              = "";

     {-1}sTree           = 0;
         sT_nID          = 0;
         sT_nOper        = 0;
         sT_aData        = "";

     &eaUnidadBasico     = "";
     &eaEnt              = "";
|FIN;

|PROCESO EvntTree;
     aContenido = Contenido;

     |SI FEntrada = 0;
         |ACABA;
     |FINSI;

     eaEnt = #n43m0102#0;

     |VENTANA 0501, 0501, -1, 16, "";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;
     |PTEC 802; |PAUSA;

     |SI aContenido = "1";  |SUB_EJECUTA "n43m0104";  |FINSI;
     |SI aContenido = "2";  |SUB_EJECUTA "n43m0103";  |FINSI;
     |SI aContenido = "3";  |SUB_EJECUTA "n43m0105";  |FINSI;
     |SI aContenido = "4";  |SUB_EJECUTA "n43z0005";  |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;

     |REFRESCACONTROL nGrid102;  |PULSATECLA;

     |FOCOTECLADO nTree102;
|FPRC;

|PROCESO Evnt102;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#n43m0102.=;

         |ESTADO_CONTROL nBtnMod, "ENABLE";
         |ESTADO_CONTROL nBtnBor, "ENABLE";

         #n43m0104#0 = #n43m0102#0;
         #n43m0104#1 = 0;
         |LEE 000#n43m0104.];
         |SI FSalida = 0;  |Y  #n43m0104#0 = #n43m0102#0;
             |ESTADO_CONTROL nBtnBor, "DISABLE";
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Alta;
     |VENTANA 0501, 2399, -1, 16, "Nueva Entidad";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;
     |PTEC 802; |PAUSA;

     |C_M #n43w0001#0, 1, "S";

     |DDEFECTO #n43w0001;

     |PINPA #0#n43w0001;
     |PINDA #0#n43w0001;
     |ENDATOS #1#n43w0001;
     |SI FSalida = 0;
         #n43m0102#0 = #n43w0001#0;
         #n43m0102#1 = #n43w0001#1;

         |GRABA 020#n43m0102.n;
         |LIBERA #n43m0102;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;

     |REFRESCACONTROL nGrid102;
     |FOCOTECLADO nGrid102;
|FPRC;

|PROCESO n43m0105;
     #n43m0105#6 = #n43m0102#1;

     |GRABA 020#n43m0105.a;
     |LIBERA #n43m0105;
|FPRC;

|DEFBUCLE n43m0105;
     #n43m0105#1;
     ;
     #n43m0102#0, "    ", "          ";
     #n43m0102#0, "zzzz", "zzzzzzzzzz";
     be;
     NULL, n43m0105;
|FIN;

|PROCESO Modificar;
     |LEE 101#n43m0102.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |VENTANA 0501, 2399, -1, 16, "Modificaci¢n Entidad";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;
     |PTEC 802; |PAUSA;

     |DDEFECTO #n43w0001;
     #n43w0001#0 = #n43m0102#0;
     #n43w0001#1 = #n43m0102#1;

     |C_M #n43w0001#0, 1, "N";

     |PINPA #0#n43w0001;
     |PINDA #0#n43w0001;
     |ENDATOS #1#n43w0001;
     |SI FSalida = 0;
         |SI #n43m0102#1 ! #n43w0001#1;
             #n43m0102#1 = #n43w0001#1;
             |BUCLE n43m0105;

             |GRABA 020#n43m0102.a;
             |LIBERA #n43m0102;
         |FINSI;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;

     |REFRESCACONTROL nGrid102;
     |FOCOTECLADO nGrid102;
|FPRC;

|PROCESO n43m0103B;
     |BORRA 020#n43m0103.a;
     |LIBERA #n43m0103;
|FPRC;

|DEFBUCLE n43m0103B;
     #n43m0103#1;
     ;
     #n43m0102#0, "    ";
     #n43m0102#0, "zzzz";
     be;
     NULL, n43m0103B;
|FIN;

|PROCESO Borrar;
     |CONFI "0000NEsta seguro que quiere borrar la entidad seleccionada.";
     |SI FSalida ! 0;
         |FOCOTECLADO nGrid102;
         |ACABA;
     |FINSI;

     |LEE 101#n43m0102.=;
     |SI FSalida ! 0;
         |FOCOTECLADO nGrid102;
         |ACABA;
     |FINSI;

     |BUCLE n43m0103B;

     |BORRA 020#n43m0102.a;
     |LIBERA #n43m0102;

     |REFRESCACONTROL nGrid102;
     |FOCOTECLADO nGrid102;
|FPRC;

|PROCESO BtnExcel;
     |VENTANA 0501, 0501, -1, 0, "";
     n1Vent = FSalida;

     |SUB_EJECUTA "n43z0004";

     |FINVENTANA n1Vent;

     |FOCOTECLADO nGrid102;
|FPRC;

|PROCESO CreaTree;
     aPathLocal = eaUnidadBasico + "\documentos\n43base\tmp\";
     aFicTmp    = "n43m0102" + Usuario + ".txt";

     aAbre = aPathLocal + aFicTmp;

     |XABRE "WBC", aAbre, nHandle;

     aAlfa = "1:Conceptos propios {{1}}" + &13 + &10;
     |XGRABA nHandle, aAlfa;

     aAlfa = "2:Oficinas {{2}}" + &13 + &10;
     |XGRABA nHandle, aAlfa;

     aAlfa = "3:Cuentas corrientes {{3}}" + &13 + &10;
     |XGRABA nHandle, aAlfa;

     aAlfa = "4:Reglas {{4}}" + &13 + &10;
     |XGRABA nHandle, aAlfa;

     |XCIERRA nHandle;

     aAlfa = "TREECTRL,Opciones {{" + aAbre + "}}";

     |CONTROL_SIMPLE 0, aAlfa, 0765, 1399, EvntTree;
     nTree102 = FSalida;

     |RFBORRA aAbre;

     sT_nID   = nTree102;
     sT_nOper = 2;
     sT_aData = "1:";
     |ESPECIFICA "COMMANDCONTROL", sTree;
|FPRC;

|PROGRAMA;
     |VENTANA 0501, 3599, -1, 16, "Entidades bancarias";
     n0Vent = FSalida;

     |CONTROL_SIMPLE 0, "LABEL,{{15}}Entidades", 0502, 0520, NULL;

     |CONTROL_SIMPLE 0, "BOTON,{{193}}Alta", 3247, 3258, Alta;
     nBtnAlt  = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Modificar", 3235, 3245, Modificar;
     nBtnMod  = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{199}}Borrar", 3223, 3233, Borrar;
     nBtnBor = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Importaci¢n CSV de ctas.ctes.", 3502, 3530, BtnExcel;

     |ESTADO_CONTROL nBtnMod, "DISABLE";
     |ESTADO_CONTROL nBtnBor, "DISABLE";

     nRango   = 2 + 4 + 8 + 16 + 32 + 128;
     |LINEAL_SIMPLE #1#n43m0102, nRango, 0602, 3160, NULL, NULL, Evnt102;
     nGrid102 = FSalida;

     |HAZ CreaTree;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::{{001}}Salir," + nGrid102;
          |LEETECLA aTecla;
          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
          |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FINVENTANA n0Vent;

     |PULSATECLA;
|FPRO;

|PROCESO GrabaReg;
     |QBF aCampoA;
     |QBF aCampoB;
     |QBF aCampoC;

     |SI aCampoA = "";  |ACABA;  |FINSI;

     aCampoA = ("0000" + aCampoA) % -104;

     #n43m0102#0 = aCampoA;
     |LEE 000#n43m0102.=;
     |SI FSalida ! 0;
         |DDEFECTO #n43m0102;
         #n43m0102#0 = aCampoA;
         |GRABA 020#n43m0102.b;
     |FINSI;

     #n43m0102#1 = aCampoB;

     |SI aCampoC ! "";
         #n43m0102#2 = aCampoC;
     |FINSI;

     |GRABA 020#n43m0102.a;
     |LIBERA #n43m0102;
|FPRC;

|PROCESO RecogeCSV;
     |DBASE aBase;

     aAbre = aBase + "documentos/patrones/entidades.csv";
     |XABRE "", aAbre, nHandle;
     |PARA;  |SI FSalida ] 0;  |HACIENDO;
          |XLEE nHandle, 250, aAlfa;
          |SI FSalida < 0;  |SAL;  |FINSI;

          aCampoA  = "";
          aCampoB  = "";
          aCampoC  = "";

          aCadena = aAlfa;  |QBF aCadena;
          aLong   = aCadena % 0;
          nLong   = aLong;
          nCampo  = 1;
          aTab    = ";";

          |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
                nPos    = (100 * nCaracter) + 1;
                aCarac1 = aCadena % nPos;
                |SI aCarac1 = aTab;
                    nCampo = nCampo + 1;
                |SINO;
                    |SI nCampo = 1;  aCampoA  = aCampoA  + aCarac1;  |FINSI;
                    |SI nCampo = 2;  aCampoB  = aCampoB  + aCarac1;  |FINSI;
                    |SI nCampo = 3;  aCampoC  = aCampoC  + aCarac1;  |FINSI;

                    |SI nCampo > 3;  |SAL;  |FINSI;
                |FINSI;
          |SIGUE;

          |HAZ GrabaReg;
     |SIGUE;
     |XCIERRA nHandle;
|FPRC;

|PROCESO T80;  |TIPO 80;
     |ABRET #n43m0102;
     |ABRET #n43m0103;
     |ABRET #n43m0104;

     |HAZ LeeUnidadBasico;

     aIP        = "";
     |IP_REMOTO aIP;

     |DBASE aBaseN43;

     aPathLocal = eaUnidadBasico + "\documentos";   |RMKDIR aPathLocal;
     aPathLocal = aPathLocal + "\n43base";          |RMKDIR aPathLocal;
     aPathLocal = aPathLocal + "\tmp";              |RMKDIR aPathLocal;
     aPathLocal = aPathLocal + "\";

     |LEE 000#n43m0102.p;
     |SI FSalida ! 0;
         |HAZ RecogeCSV;
     |FINSI;
|FPRC;

|PROCESO T90;  |TIPO 90;
     |CIERRAT #n43m0102;
     |CIERRAT #n43m0103;
     |CIERRAT #n43m0104;
|FPRC;
