|FICHEROS;
     n43m0001;
     n43m0102;
     n43m0103;
     n43m0105;

     agifigen@;
|FIN;

|VARIABLES;
     n0Vent              = 0;
     n1Vent              = 0;
     n2Vent              = 0;
     nOk                 = 0;
     nLong               = 0;
     nCarac              = 0;
     nPos                = 0;
     nHandle             = 0;
     nReg                = 0;
     nCampo              = 0;
     nCmp                = 0;
     nCaracter           = 0;
     nVeces              = 0;
     nTreeXls            = 0;
     nOpcion             = 0;

     aAlfa               = "";
     aAlfa1              = "";
     aEsc1               = "";
     aEsc2               = "";
     aRetu               = "";
     aTecla              = "";
     aParam1             = "";
     aParam2             = "";
     aParam3             = "";
     aProg               = "";
     aFicBrowse          = "";
     aVer1               = "";
     aVer2               = "";
     aOri                = "";
     aDes                = "";
     aBaseN43            = "";
     aFicConta           = "";
     aPathConta          = "";
     aPathLocal          = "";
     aPathDoc            = "";
     aPathTmp            = "";
     aLong               = "";
     aIP                 = "";
     aAbre               = "";
     aLinea              = "";
     aRuta               = "";
     aFic                = "";
     aCaracter           = "";
     aCarac1             = "";
     aVar                = "";
     aLee                = "";
     aC10                = "";
     aC13                = "";
     aCarac              = "";
     aFicTmp             = "";
     aQQ                 = "";
     aTipo               = "";
     aCif                = "";
     aIdent              = "";
     aExt                = "";
     aError1             = "";
     aError2             = "";
     aHora               = "";
     aModoImp            = "";
     aMas                = "";
     aCampoA             = "";
     aCampoB             = "";
     aCampoC             = "";
     aCampoD             = "";
     aCampoE             = "";
     aCampoF             = "";
     aCampoG             = "";
     aCampoH             = "";
     aCampoI             = "";
     aCampoJ             = "";
     aCampoK             = "";
     aCampoL             = "";
     aCadena             = "";
     aTab                = "";
     aContenido          = "";
     aOpcion             = "";

     fFecha              = @;

     {1}aConte           = "";

     {-1}aVent           = "";
         aVent1          = 0;
         aVent2          = 0;
         aVent3          = 0;
         aVent4          = 0;
         aVent5          = "";

     {-1}sTree           = 0;
         sT_nID          = 0;
         sT_nOper        = 0;
         sT_aData        = "";

     &eaUnidadBasico     = "";
     &eaAlfa             = "";
     &eaBaseAplis        = "";
     &eaPaq              = "";
|FIN;

|| Lectura CSV --------------------------------------------------------------

|PROCESO GrabaReg;
     |QBF aCampoA;
     |QBF aCampoB;
     |QBF aCampoC;
     |QBF aCampoD;
     |QBF aCampoE;
     |QBF aCampoF;
     |QBF aCampoG;
     |QBF aCampoH;
     |QBF aCampoI;
     |QBF aCampoJ;
     |QBF aCampoK;
     |QBF aCampoL;

     |SI aCampoA = "";  |ACABA;  |FINSI;
     |SI aCampoB = "";  |ACABA;  |FINSI;
     |SI aCampoC = "";  |ACABA;  |FINSI;
     |SI aCampoK = "";  |ACABA;  |FINSI;

     #agifigen@#0 = aCampoA;
     |LEE 000#agifigen@.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aCampoB = ("0000"  + aCampoB) % -104;
     aCampoC = ("0000"  + aCampoC) % -104;
     aCampoF = ("00000" + aCampoF) % -105;

     #n43m0102#0 = aCampoB;
     |LEE 000#n43m0102.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     #n43m0103#0 = aCampoB;
     #n43m0103#1 = aCampoC;
     |LEE 000#n43m0103.=;
     |SI FSalida ! 0;
         |DDEFECTO #n43m0103;
         #n43m0103#0 = aCampoB;
         #n43m0103#1 = aCampoC;
         #n43m0103#2 = aCampoD;
         #n43m0103#3 = aCampoE;
         #n43m0103#4 = aCampoF;
         #n43m0103#5 = aCampoG;
         #n43m0103#6 = aCampoH;
         #n43m0103#7 = aCampoI;
         #n43m0103#8 = aCampoJ;
         #n43m0103#9 = aCampoK;

         |GRABA 020#n43m0103.n;
         |LIBERA #n43m0103;
     |FINSI;

     #n43m0001#0 = aCampoA;
     |LEE 000#n43m0001.=;
     |SI FSalida ! 0;
         |DDEFECTO #n43m0001;
         #n43m0001#0 = #agifigen@#0;
         #n43m0001#1 = #agifigen@#1;
         #n43m0001#2 = #agifigen@#13;

         |GRABA 020#n43m0001.n;
         |LIBERA #n43m0001;
     |FINSI;

     #n43m0105#0 = aCampoB;
     #n43m0105#1 = aCampoC;
     #n43m0105#2 = aCampoK;
     |LEE 000#n43m0105.=;
     |SI FSalida = 0;  |ACABA;  |FINSI;

     |DDEFECTO #n43m0105;
     #n43m0105#0 = aCampoB;
     #n43m0105#1 = aCampoC;
     #n43m0105#2 = aCampoK;
     #n43m0105#3 = aCampoA;
     #n43m0105#4 = #agifigen@#1;
     #n43m0105#5 = aCampoL;
     #n43m0105#6 = #n43m0102#1;
     #n43m0105#7 = #n43m0103#2;
     |GRABA 020#n43m0105.n;
     |LIBERA #n43m0105;

     nOk = 1;
|FPRC;

|PROCESO LeeFichero;
     nOk = 0;

     |XABRE "", aAbre, nHandle;
     |PARA;  |SI FSalida ] 0;  |HACIENDO;
          |XLEE nHandle, 250, aAlfa;
          |SI FSalida < 0;  |SAL;  |FINSI;

          aCampoA  = "";
          aCampoB  = "";
          aCampoC  = "";
          aCampoD  = "";
          aCampoE  = "";
          aCampoF  = "";
          aCampoG  = "";
          aCampoH  = "";
          aCampoI  = "";
          aCampoJ  = "";
          aCampoK  = "";
          aCampoL  = "";

          aCadena = aAlfa;  |QBF aCadena;
          aLong   = aCadena % 0;
          nLong   = aLong;
          nCampo  = 1;
          aTab    = ";";

          |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
                nPos    = (100 * nCaracter) + 1;
                aCarac1 = aCadena % nPos;
                |SI aCarac1 = aTab;
                    nCampo = nCampo + 1;
                |SINO;
                    |SI nCampo = 1;  aCampoA  = aCampoA  + aCarac1;  |FINSI;
                    |SI nCampo = 2;  aCampoB  = aCampoB  + aCarac1;  |FINSI;
                    |SI nCampo = 3;  aCampoC  = aCampoC  + aCarac1;  |FINSI;
                    |SI nCampo = 4;  aCampoD  = aCampoD  + aCarac1;  |FINSI;
                    |SI nCampo = 5;  aCampoE  = aCampoE  + aCarac1;  |FINSI;
                    |SI nCampo = 6;  aCampoF  = aCampoF  + aCarac1;  |FINSI;
                    |SI nCampo = 7;  aCampoG  = aCampoG  + aCarac1;  |FINSI;
                    |SI nCampo = 8;  aCampoH  = aCampoH  + aCarac1;  |FINSI;
                    |SI nCampo = 9;  aCampoI  = aCampoI  + aCarac1;  |FINSI;
                    |SI nCampo = 10; aCampoJ  = aCampoJ  + aCarac1;  |FINSI;
                    |SI nCampo = 11; aCampoK  = aCampoK  + aCarac1;  |FINSI;
                    |SI nCampo = 12; aCampoL  = aCampoL  + aCarac1;  |FINSI;

                    |SI nCampo > 12;  |SAL;  |FINSI;
                |FINSI;
          |SIGUE;

          |HAZ GrabaReg;
     |SIGUE;
|FPRC;

|PROCESO PonDsselect;
     aVer1   = "";
     aVer2   = "";
     aConte  = aBaseN43 + "plugins/dsselect.exe";
     |ESPECIFICA "MD5", aConte;
     aVer1 = FSalida;

     aConte  = aPathLocal + "dsselect.exe";
     |ESPECIFICA "REMOTOMD5", aConte;
     aVer2 = FSalida;

     |SI aVer1 ! aVer2;
         aOri = aBaseN43 + "plugins/dsselect.exe";
         aDes = aPathLocal + "dsselect.exe";

         |SI aIP = "";
             |COPIA_FICHERO aOri, aDes;
         |SINO;
             |ENVIA_FICHERO aOri, aDes;
         |FINSI;

         aAlfa = aPathLocal + "dsselect.exe";
         |XABRE "EC", aAlfa, 0;
         |SI FSalida < 0;
             |MENAV "0000No encuentro el explorador de ficheros, por favor consulte con su administrador informatico.";
             |ACABA;
         |FINSI;
     |FINSI;

     nOk = 1;
|FPRC;

|PROCESO Dsofficetxt;
     aVer1   = "";
     aVer2   = "";
     aConte  = aBaseN43 + "plugins/dsofficetxt.exe";
     |ESPECIFICA "MD5", aConte;
     aVer1 = FSalida;

     aConte  = aPathLocal + "dsofficetxt.exe";
     |ESPECIFICA "REMOTOMD5", aConte;
     aVer2 = FSalida;

     |SI aVer1 ! aVer2;
         aOri = aBaseN43 + "plugins/dsofficetxt.exe";
         aDes = aPathLocal + "dsofficetxt.exe";

         |SI aIP = "";
             |COPIA_FICHERO aOri, aDes;
         |SINO;
             |ENVIA_FICHERO aOri, aDes;
         |FINSI;

         aAlfa = aPathLocal + "dsofficetxt.exe";
         |XABRE "EC", aAlfa, 0;
         |SI FSalida < 0;
             |MENAV "0000No encuentro el conversor a CSV, por favor consulte con su administrador informatico.";
             |ACABA;
         |FINSI;
     |FINSI;

     nOk = 1;
|FPRC;

|PROCESO Conversion;
     aAlfa = aPathLocal + "*.csv";
     |R_PDIR aAlfa;
     |PARA;  |SI FSalida = 0;  |HACIENDO;
          |RFBORRA aAlfa;

          |R_SDIR aAlfa;
     |SIGUE;

     aExt = (aFic % -103) < "";
     |SI aExt = "csv";
         aOri    = aFicBrowse;
         aDes    = aPathLocal + "ficcsv.csv";
         aParam2 = aDes;
         |RCOPIA_FICHERO aOri, aDes;

         nOk = 1;
         |ACABA;
     |FINSI;

     nOk = 0;
     |HAZ Dsofficetxt;
     |SI nOk = 0;
         |MENAV "0000Falta programa de conversi¢n a CSV, pongase en contacto con su distribuidor";
         |ACABA;
     |FINSI;

     aParam2 = aPathLocal + "ficcsv.txt";  |RFBORRA aParam2;

     aProg   = aPathLocal + "dsofficetxt.exe";
     aParam1 = aFicBrowse;
     aParam2 = aPathLocal + "ficcsv.csv";
     aParam3 = "";

     aAbre = aPathLocal + "ejecuta.bat";
     |XABRE "WC", aAbre, nHandle;

     aAlfa  =  aProg + " " + &34 + aParam1 + &34 + " " + &34 + aParam2 + &34;
     |XGRABA nHandle, aAlfa;
     |XCIERRA nHandle;

     |RSYSTEM aAbre;

     nOk    = 1;
     nVeces = 0;
     |PARA;  |SI;  |HACIENDO;
          |XABRE "EC", aParam2, 0;
          |SI FSalida ] 0;  |SAL;  |FINSI;

          nVeces = nVeces + 1;
          |SLEEP 1;

          |SI nVeces > 15;
              |MENAV "0000No se ha podido convertir a CSV el fichero seleccionado.";
              nOk = 0;
              |SAL;
          |FINSI;
     |SIGUE;

     |RFBORRA aAbre;
|FPRC;

|PROCESO RecogeCSV;
     |MENAV "0000A continuaci¢n busque y seleccione el fichero a importar";

     nOk = 0;
     |HAZ PonDsselect;
     |SI nOk = 0;  |ACABA;  |FINSI;

     aProg = aPathLocal + "dsselect.exe " + aPathLocal + "dsselect.ini";

     aAlfa = aPathLocal + "dsselect.txt";  |RFBORRA aAlfa;
     aAlfa = aPathLocal + "dsselect.ini";  |RFBORRA aAlfa;

     aAbre = aPathLocal + "dsselect.ini";
     |XABRE "CW", aAbre, nHandle;
     |SI FSalida ] 0;
          aLinea = &34 + aPathLocal + &34 + " " + &34 + aPathLocal + "dsselect.txt" + &34;
          |XGRABA nHandle, aLinea;
     |FINSI;
     |XCIERRA nHandle;

     |RSYSTEM aProg;
     |PULSATECLA;

     aRuta = "";
     aAbre = aPathLocal + "dsselect.txt";
     |XABRE "CU", aAbre, nHandle;
     |SI FSalida ] 0;
         |XLEE nHandle, 250, aRuta;
     |FINSI;
     |XCIERRA nHandle;

     aAlfa = aPathLocal + "dsselect.txt";
     |RFBORRA aAlfa;

     |PULSATECLA;

     |QBF aRuta;
     |SI aRuta = "";
         nOk = 0;
         |ACABA;
     |FINSI;

     aAlfa      = aRuta;
     aFicBrowse = "";
     aFic       = "";
     aLong      = aAlfa % 0;
     nLong      = aLong;
     |PARA nCarac = 1;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos       = (nCarac * 100) + 1;
           aCaracter  = aAlfa % nPos;
           aFicBrowse = aFicBrowse + aCaracter;
           aFic       = aFic + aCaracter;

           |SI aCaracter = "/";  |O aCaracter = "\";
               aFic = "";
           |FINSI;
     |SIGUE;

     nOk = 0;
     |HAZ Conversion;
     |SI nOk = 0;  |ACABA;  |FINSI;

     aOri  = aParam2;
     aDes  = aPathTmp + "ficcsv.csv";
     |SI aIP = "";
         |COPIA_FICHERO aOri, aDes;
     |SINO;
         |RECIBE_FICHERO aOri, aDes;
     |FINSI;
|FPRC;

|| ------------------------------------------------------------------------

|PROCESO AbreVtn;
     |VENTANA 0501, 0540, -1, 16, "Realizando la importaci¢n";
     n2Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n2Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;
|FPRC;

|PROCESO CierraVtn;
     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n2Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n2Vent;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n0Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;
|FPRC;

|PROCESO Importacion;
     |HAZ RecogeCSV;
     |SI nOk = 0;  |ACABA;  |FINSI;

     |HAZ AbreVtn;
     aAbre = aPathTmp + "ficcsv.csv";
     |HAZ LeeFichero;
     |HAZ CierraVtn;

     |SI nOk = 0;
         |MENAV "0000No se ha encotrado nada a importar";
     |FINSI;

     |SI nOk = 1;
         |MENAV "0000Importaci¢n realizada";
     |FINSI;
|FPRC;

|PROCESO PonPlantilla;
     aPathLocal = eaUnidadBasico + "\documentos\n43base\";

     aVer1   = "";
     aVer2   = "";
     aConte  = aBaseN43 + "plugins/plantillactasctes.xlsx";
     |ESPECIFICA "MD5", aConte;
     aVer1 = FSalida;

     aConte  = aPathLocal + "plantillactasctes.xlsx";
     |ESPECIFICA "REMOTOMD5", aConte;
     aVer2 = FSalida;

     |SI aVer1 ! aVer2;
         aOri  = aBaseN43 + "plugins/plantillactasctes.xlsx";
         aDes  = aPathLocal + "plantillactasctes.xlsx";

         |SI aIP = "";
             |COPIA_FICHERO aOri, aDes;
         |SINO;
             |ENVIA_FICHERO aOri, aDes;
         |FINSI;

         aAlfa = aPathLocal + "plantillactasctes.xlsx";
         |XABRE "EC", aAlfa, 0;
         |SI FSalida < 0;
             |MENAV "0000No encuentro la plantilla, por favor consulte con su administrador informatico.";
             |ACABA;
         |FINSI;
     |FINSI;

     aAlfa = "0000El documento plantillactasctes.xlsx est  ubicado en " + aPathLocal + " de su PC";
     |MENAV aAlfa;
|FPRC;

|PROCESO EvntTreeImpor;
     |SI FEntrada = 0;
         |ACABA;
     |FINSI;

     aContenido = Contenido % 101;

     |VENTANA 0501, 0502, -1, 16, "";
     n1Vent = FSalida;
     |PTEC 802;  |PAUSA;

     |SI aContenido = "1";  |HAZ PonPlantilla;  |FINSI;
     |SI aContenido = "2";  |HAZ Importacion;   |FINSI;

     |FINVENTANA n1Vent;

     |FOCOTECLADO nTreeXls;
|FPRC;

|PROCESO CargaArbol;
     aOpcion = aOpcion + &13 + &10;
     |XGRABA nHandle, aOpcion;
|FPRC;

|PROGRAMA;
     |VENTANA 0501, 1540, -1, 16, "Importaci¢n Excel/CSV";
     n0Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n0Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     sT_nID   = nTreeXls;
     sT_nOper = 0;

     aAbre = aPathLocal + "tree" + Usuario + ".txt";
     |XABRE "WBC", aAbre, nHandle;

     nOpcion = 0;

     nOpcion  = nOpcion + 1;
     aOpcion  = nOpcion + ":Descargar plantilla {{1}}";
     |HAZ CargaArbol;

     nOpcion  = nOpcion + 1;
     aOpcion  = nOpcion + ":Importar cuentas corrientes {{2}}";
     |HAZ CargaArbol;

     |XCIERRA nHandle;

     aAlfa = "TREECTRL, Tabla de contenidos {{" + aAbre + "}}";
     |CONTROL_SIMPLE 0, aAlfa, 0602, 1338, EvntTreeImpor;
     nTreeXls = FSalida;

     |RFBORRA aAbre;

     sT_nID   = nTreeXls;
     sT_nOper = 2;
     sT_aData = "1:";

     |ESPECIFICA "COMMANDCONTROL", sTree;

     |PULSATECLA;
     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}sali&R," + nTreeXls;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n0Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n0Vent;
     |PULSATECLA;
|FPRO;

|PROCESO T80;  |TIPO 80;
     aModoImp = PARAMETRO;  |QBF aModoImp;

     aIP      = "";
     |IP_REMOTO aIP;

     |DBASE aBaseN43;

     |HAZ LeeUnidadBasico;

     aPathLocal = eaUnidadBasico + "\documentos";   |RMKDIR aPathLocal;
     aPathLocal = aPathLocal + "\tmp";              |RMKDIR aPathLocal;
     aPathLocal = aPathLocal + "\";

     aPathTmp = aBaseN43 + "tmp";                    |MKDIR aPathTmp;
     aPathTmp = aBaseN43 + "tmp/" + Usuario;         |MKDIR aPathTmp;
     aPathTmp = aBaseN43 + "tmp/" + Usuario + "/";

     |HAZ DimePaquete;

     |SI eaPaq = "BASICA";
         aMas = eaBaseAplis + "basica/def/agifigen.mas";
     |FINSI;

     |SI eaPaq = "AGICLI";
         aMas = eaBaseAplis + "agicli/def/agifigen.mas";
     |FINSI;

     |CARGA_DEF aMas, agifigen@;

     |ABRET #n43m0001;
     |ABRET #n43m0102;
     |ABRET #n43m0103;
     |ABRET #n43m0105;
     |ABRET #agifigen@;
|FPRC;

|PROCESO T90;  |TIPO 90;
     aAlfa = aPathLocal + "*.csv";
     |R_PDIR aAlfa;
     |PARA;  |SI FSalida = 0;  |HACIENDO;
          |RFBORRA aAlfa;

          |R_SDIR aAlfa;
     |SIGUE;

     |CIERRAT #agifigen@;
     |DESCARGA_DEF #agifigen@;

     |CIERRAT #n43m0001;
     |CIERRAT #n43m0102;
     |CIERRAT #n43m0103;
     |CIERRAT #n43m0105;
|FPRC;
