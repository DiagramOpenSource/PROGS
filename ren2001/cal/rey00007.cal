|FICHEROS;
     rey00007 #0;

     rem01000  #1003;

     fic0100@ #100;
     fic0101@ #101;
     fic1001@ #1001;
     fic1002@ #1002;

     rew00553 #553;
|FIN;

|VARIABLES;
     nCampo      = 0;
     nCampo1     = 0;
     nCampo2     = 0;
     nSwHay      = 0;

     aBase       = "";
     aFichero    = "";
     aDef        = "";

     {-1}aMoneda   = "";
         aMoneda1  = "LEER";
         aMoneda2  = "";

     &eaPath     = "";
     &enCapitulo = 0;
     &eaCasilla  = "";
     &EURODB     = 0;
|FIN;

|INCLUYE i_consul;

|PROCESO PintaMoneda;  |TIPO 7;
     IaMoneda = aMoneda1<-;
     |ESPECIFICA "MODO_VER_EURO", aMoneda;

     |SI EURODB ! 0;
         |SI aMoneda2 = "0";
             #0#36 = "EUROS";
         |SINO;
             #0#36 = "PESETAS";
         |FINSI;
     |SINO;
         |SI aMoneda2 = "0";
             #0#36 = "PESETAS";
         |SINO;
             #0#36 = "EUROS";
         |FINSI;
     |FINSI;

     |PINTA #0#36;
|FPRC;

|PROCESO DesdeHasta;
     |C_M #0#2, 1, "S";
     |PARA nCampo = 3;  |SI nCampo [ 27;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "N";
           #0nCampo = 0;  |PINTA #0nCampo;
     |SIGUE;

     |SI #0#1 = 0;
         |C_M #0#2,  1, "N";  #0#1  = 0;         |PINTA #0#2;

         |PARA nCampo = 3;  |SI nCampo [ 27;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO NoMasEmpresas;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 27;  |HACIENDO nCampo = nCampo + 1;
               #0Campo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 27;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO Ejercicio;
     |DBASS aBase;  |QBT aBase;
     |SI #0#0 = 2000;
         eaPath = aBase + "ren2000/fich/";
     |SINO;
         eaPath = aBase + "ren2001/fich/";
     |FINSI;

     |PATH_DAT #1003 eaPath;
|FPRC;

|PROCESO Casilla;  |TIPO 0;
     |SI FSalida = 10;
         nCampo1 = Campo;
         nCampo2 = Campo + 1;

         |SUB_EJECUTA_NP "rem01000";

         #0nCampo1 = enCapitulo;  |PINTA #0nCampo1;
         #0nCampo2 = eaCasilla;   |PINTA #0nCampo2;
     |FINSI;
|FPRC;

|PROCESO ChequeaCasilla;  |TIPO 0;
     nCampo1 = Campo - 1;
     nCampo2 = Campo;
     |SI #0nCampo1 = 0;  |Y #0nCampo2 = "   ";  |ACABA;  |FINSI;

     |ABRE #1003;
     #1003#0 = #0nCampo1;
     #1003#1 = #0nCampo2;
     |LEE 040#1003=;
     |SI FSalida ! 0;
         |MENAV "     La Casilla de Liquidacion no Existe.";
         |ERROR;
     |FINSI;
     |CIERRA #1003;
|FPRC;

|PROCESO BuscaFamiliar;
     |SI #1001#7 = #101#3;
         #553#2 = #101#2;
         |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE rem00101;
     #101#1002;
     ;
     #100#0, 01;
     #100#0, 99;
     ;
     NULL, BuscaFamiliar;
|FIN;

|PROCESO Lee1002;
     |ABRE #1002;
     #1002#0 = #1001#0;
     #1002#1 = #1001#1;
     #1002#2 = #1001#2;
     #1002#3 = enCapitulo;
     #1002#4 = eaCasilla;
     |LEE 040#1002=;
     |SI FSalida ! 0;  |DDEFECTO #1002;  |FINSI;
     |CIERRA #1002;
|FPRC;

|PROCESO GrabaTemporal;
     |SI #1001#2 = "ZPATRITIT ";  |O #1001#2 = "ZPATRICON ";
         |ACABA;
     |FINSI;

     |DDEFECTO #553;
     #553#0 = #1001#0;
     #553#1 = #1001#2;
     #553#3 = #1001#7;

     |ABRE #100;
     #100#0 = #1001#0;
     |LEE 040#100=;
     |SI FSalida ! 0;  |DDEFECTO #100;  |FINSI;
     |CIERRA #100;


     |SI #1001#2 = "TITULAR   ";  |O #1001#2 = "CONJUNTA  ";
      |O #1001#2 = "CONJUNTAE ";  |O #1001#2 = "ZPATRITIT ";
         #553#2 = #100#1;
     |FINSI;

     |SI #1001#2 = "CONYUGE   ";  |O #1001#2 = "ZPATRICON ";
         #553#2 = #100#35;
     |FINSI;

     |SI #1001#2 ! "TITULAR   ";  |Y #1001#2 ! "CONJUNTA  ";
      |Y #1001#2 ! "CONJUNTAE ";  |Y #1001#2 ! "ZPATRITIT ";
      |Y #1001#2 ! "CONYUGE   ";  |Y #1001#2 ! "ZPATRICON ";
         #553#2 = "";
         |BUCLE rem00101;
     |FINSI;

     |SI #0#28 ! 0;  |O #0#29 ! "   ";
         enCapitulo = #0#28;
         eaCasilla  = #0#29;
         |HAZ Lee1002;

         |SI #0#0 = 2000;
             #553#4 = (#1002#6 / 166.386) ! EURODB;
         |SINO;
             #553#4 = #1002#6;
         |FINSI;
     |FINSI;

     |SI #0#30 ! 0;  |O #0#31 ! "   ";
         enCapitulo = #0#30;
         eaCasilla  = #0#31;
         |HAZ Lee1002;
         |SI #0#0 = 2000;
             #553#5 = (#1002#6 / 166.386) ! EURODB;
         |SINO;
             #553#5 = #1002#6;
         |FINSI;
     |FINSI;

     |SI #0#32 ! 0;  |O #0#33 ! "   ";
         enCapitulo = #0#32;
         eaCasilla  = #0#33;
         |HAZ Lee1002;
         |SI #0#0 = 2000;
             #553#6 = (#1002#6 / 166.386) ! EURODB;
         |SINO;
             #553#6 = #1002#6;
         |FINSI;
     |FINSI;

     |SI #0#34 ! 0;  |O #0#35 ! "   ";
         enCapitulo = #0#34;
         eaCasilla  = #0#35;
         |HAZ Lee1002;
         |SI #0#0 = 2000;
             #553#7 = (#1002#6 / 166.386) ! EURODB;
         |SINO;
             #553#7 = #1002#6;
         |FINSI;
     |FINSI;

     |GRABA 020#553n;
     |LIBERA #553;

     nSwHay = 1;
|FPRC;

|DEFBUCLE rem01001;
     #1001#1003;
     5;
     #0#1, 00, "          ", "!  ";
     #0#2, 99, "zzzzzzzzzz", "zzz";
     ;
     NULL, GrabaTemporal;
|FIN;

|PROCESO ImprimeRentas;
     |PRINT;
|FPRC;

|DEFBUCLE rew00553;
     #553#1;
     ;
     00001, "          ";
     99999, "zzzzzzzzzz";
     ;
     NULL, ImprimeRentas;
|FIN;

|PROCESO Tipo3;  |TIPO 3;
     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |INFOR "rey00007";
     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

     |DBASS aBase;  |QBT aBase;
     |SI #0#0 = 2000;
         eaPath = aBase + "ren2000/def/";
     |SINO;
         eaPath = aBase + "ren2001/def/";
     |FINSI;

     aDef = eaPath + "rem00100.mas";
     |CARGA_DEF aDef, fic0100@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aDef = eaPath + "rem00101.mas";
     |CARGA_DEF aDef, fic0101@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #fic0100@;
         |ACABA;
     |FINSI;

     aDef = eaPath + "rem01001.mas";
     |CARGA_DEF aDef, fic1001@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #fic0101@;
         |DESCARGA_DEF #fic0100@;
         |ACABA;
     |FINSI;

     aDef = eaPath + "rem01002.mas";
     |CARGA_DEF aDef, fic1002@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #fic1001@;
         |DESCARGA_DEF #fic0101@;
         |DESCARGA_DEF #fic0100@;
         |ACABA;
     |FINSI;

     |DBASS aBase;  |QBT aBase;
     |SI #0#0 = 2000;
         eaPath = aBase + "ren2000/fich/";
     |SINO;
         eaPath = aBase + "ren2001/fich/";
     |FINSI;

     |PATH_DAT #100  eaPath;
     |PATH_DAT #101  eaPath;
     |PATH_DAT #1001 eaPath;
     |PATH_DAT #1002 eaPath;

     aFichero = Usuario;  |QBT aFichero;
     aFichero = (aFichero + "zzzzzzzz") % -108;
     |NOME_DAT #553 aFichero;
     |ABRE #553;  |CIERRA #553;  |DELINDEX #553;

     nSwHay = 0;
     |ABRE #553;
     |SI #0#1 ! 0;
         |BUCLE rem01001;
     |SINO;
         |PARA nCampo = 3;  |SI nCampo [ 27;  |HACIENDO nCampo = nCampo + 1;
               |SI #0nCampo ! 0;
                   #0#1 = #0nCampo;
                   #0#2 = #0nCampo;
                   |BUCLE rem01001;
               |FINSI;
         |SIGUE;
     |FINSI;
     |CIERRA #553;

     |DESCARGA_DEF #fic1002@;
     |DESCARGA_DEF #fic1001@;
     |DESCARGA_DEF #fic0101@;
     |DESCARGA_DEF #fic0100@;

     |SI nSwHay = 1;
         |BUCLE rew00553;
         |PIEINF;
     |FINSI;

     |FININF;
     |FINIMP;

     |DELINDEX #553;
|FPRC;
