|FICHEROS;
     rem00032 #20;
     espejo@ #99;
|FIN;

|VARIABLES;
     aAlfa         = "";
     aAlfa1        = "";
     aLong         = "";
     aQueQuiero    = "";
     aNumCampos    = "";
     aNumPanta     = "";
     aPathRenta    = "";
     aDef          = "";

     nLong         = 0;
     nCaracter     = 0;
     nPosi         = 0;
     nPanta        = 0;
     nNumCampos    = 0;
     nNumPanta     = 0;
     nCampo        = 0;
     nModo         = 0;
     nEntrada      = 0;

     &enEmpresa    = 0;
     &enModo       = 0;
     &eaDeclarante = "";
     &enComunes    = 0;
     &enMinusvalia = 0;
     &eaMinuAyuda  = "";
     &eaFallece    = "";
     &eaSwVarios   = "";
     &eaVarDni     = "";
     &enCalcCif    = 0;
     &enSwLiquida  = 0;
     &enSwRetorno  = 0;
     &eaPrograma   = "";
     &eaPunto      = "";
|FIN;

|| ************************************************************************
|| PROCESOS DE CONSULTA DE PANTALLAS NORMALES
|| ************************************************************************

|PROCESO LetraNif;  |TIPO 6;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     eaVarDni = #20Campo;
     enCalcCif = 1;
     |HAZ CalculoDNI;
     #20Campo = eaVarDni;
     |PINTA #20Campo;
     |ERROR;
|FPRC;

|PROCESO Totalm20;
     #20#22 = #20#20 + #20#21;  |PINTA #20#22;
|FPRC;

|PROCESO TipoEntidad;
     |C_M #20#23, 1, "S";
     |SI #20#12 = "1";
         |C_M #20#23, 1, "N";  #20#23 = "S";  |PINTA #20#23;
     |FINSI;

     |SI #20#12 = "4";
         |C_M #20#23, 1, "N";  #20#23 = "N";  |PINTA #20#23;
     |FINSI;
|FPRC;

|PROCESO AplicarMaximo;
     |C_M #20#24, 1, "S";
     |C_M #20#25, 1, "S";
     |SI #20#23 = "N";
         |C_M #20#24, 1, "N";  #20#24 = 0;  |PINTA #20#24;
         |C_M #20#25, 1, "N";  #20#25 = 0;  |PINTA #20#25;
     |FINSI;
|FPRC;

|PROCESO Cantidad;
     |SI #20#23 = "N";  |ACABA;  |FINSI;

     |SI #20#24 = 0;
         |MENAV "      Por Favor introduzca importe";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Varios;  |TIPO 7;
     |C_M #20#5,  1, "N";
     |C_M #20#6,  1, "N";
     |C_M #20#7,  1, "N";
     |C_M #20#8,  1, "N";
     |C_M #20#9,  1, "N";
     |C_M #20#10, 1, "N";

     aAlfa = eaSwVarios;
     aLong = aAlfa % 0;
     nLong = aLong;
     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPosi = (100 * nCaracter) + 1;
           aAlfa1 = aAlfa % nPosi;
           |SI aAlfa1 = "T";  |C_M #20#5,  1, "S";  |FINSI;
           |SI aAlfa1 = "C";  |C_M #20#6,  1, "S";  |FINSI;
           |SI aAlfa1 = "1";  |C_M #20#7,  1, "S";  |FINSI;
           |SI aAlfa1 = "2";  |C_M #20#8,  1, "S";  |FINSI;
           |SI aAlfa1 = "3";  |C_M #20#9,  1, "S";  |FINSI;
           |SI aAlfa1 = "4";  |C_M #20#10, 1, "S";  |FINSI;
     |SIGUE;

     |SI #20#2 ! "VARIOS    ";
         |C_M #20#5,  1, "N";  #20#5  = 0;  |PINTA #20#5;
         |C_M #20#6,  1, "N";  #20#6  = 0;  |PINTA #20#6;
         |C_M #20#7,  1, "N";  #20#7  = 0;  |PINTA #20#7;
         |C_M #20#8,  1, "N";  #20#8  = 0;  |PINTA #20#8;
         |C_M #20#9,  1, "N";  #20#9  = 0;  |PINTA #20#9;
         |C_M #20#10, 1, "N";  #20#10 = 0;  |PINTA #20#10;
     |FINSI;
|FPRC;

|RUTINA ClaveBaja20;
     #20#0 = enEmpresa;
     #20#1 = 1;
|FRUT;

|RUTINA ClaveAlta20;
     #20#0 = enEmpresa;
     #20#1 = 99;
|FRUT;

|PROCESO PonDeclarante;  |TIPO 7;
     eaDeclarante = #20#2;
     enComunes    = 1;
     |HAZ SacaDeclarantes;
     #20#2 = eaDeclarante;
     |PINTA #20#2;
|FPRC;

|PROCESO Fallece;  |TIPO 7;
     |SI eaFallece = "S";
         |C_M #20#4, 1, "S";
     |SINO;
         |C_M #20#4, 1, "N";  #20#4 = 0;  |PINTA #20#4;
     |FINSI;
|FPRC;

|PROCESO Tipo7m20;  |TIPO 7;
     |SI #20#26 = "S";
         |ATRI I;
         |PINTA 0702, "PROVIENE DEL PASE AUTOMATICO";
         |ATRI 0;
     |SINO;
         |PINTA 0702, "ÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÂÄÂÄÄÂÄÄÂÄÄÂÄ";
     |FINSI;
|FPRC;

|PROCESO Tipo2m20;  |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;  |O nModo = 3;
         enSwRetorno  = 1;
         |ACABA;
     |FINSI;

     nEntrada = 0 +. 1;
     |SI nEntrada ! 1;
         |PARA nCampo = 0;  |SI nCampo [ nNumCampos;  |HACIENDO nCampo = nCampo + 1;
               #99nCampo = #20nCampo;
         |SIGUE;
     |SINO;
         |PARA nCampo = 0;  |SI nCampo [ nNumCampos;  |HACIENDO nCampo = nCampo + 1;
               |SI #99nCampo ! #20nCampo;
                   enSwRetorno  = 1;
               |FINSI;
         |SIGUE;
     |FINSI;
|FPRC;

|PROGRAMA;
     aQueQuiero = "|NP";
     aNumPanta  = #20#aQueQuiero#;
     nNumPanta  = aNumPanta;
     nNumPanta  = nNumPanta - 1;

     aQueQuiero = "|NC";
     aNumCampos = #20#aQueQuiero#;
     nNumCampos = aNumCampos;
     nNumCampos = nNumCampos - 1;

     |DBASE aPathRenta;  |QBT aPathRenta;
     aDef = aPathRenta + "def/rem00032.mas";
     |CARGA_DEF aDef, espejo@;
     |SI FSalida ! 0;
         aAlfa = "     Error en cargar el Def rem00032.mas";
         |MENAV aAlfa;
         |ERROR;
     |FINSI;

     |ENTLINEAL #1#20, -2, enModo, 22, 1, ClaveBaja20, ClaveAlta20;

     |DESCARGA_DEF #espejo@;

     eaPrograma   = "rem00032";
     eaPunto      = "  ";
     |HAZ ProgramaActivo;
|FPRO;
