|FICHEROS;
     rem00025 #20;
     rem99999 #666;
     rew00605 #1003,MANTE;
|FIN;

|VARIABLES;
     aPathRenta    = "";
     aDef          = "";
     aAlfa         = "";
     aAlfa1        = "";
     aEscape       = "";
     aAvance       = "";
     aRetroceso    = "";
     aQueQuiero    = "";
     aNumCampos    = "";
     aNumPanta     = "";
     aLong         = "";

     nPanta        = 0;
     nNumCampos    = 0;
     nNumPanta     = 0;
     nCampos       = 0;
     nModo         = 0;
     nEntrada      = 0;
     nFactor       = 0;
     nLong         = 0;
     nCaracter     = 0;
     nPosi         = 0;
     nCampo        = 0;
     nDesde        = 0;
     nValor        = 0;

     &enEmpresa    = 0;
     &enModo       = 0;
     &eaDeclarante = "";
     &enComunes    = 0;
     &enMinusvalia = 0;
     &eaMinuAyuda  = "";
     &eaFallece    = "";
     &eaSwVarios   = "";
     &eaVarDni     = "";
     &enCalcCif    = 0;
     &enSwLiquida  = 0;
     &enSwRetorno  = 0;
     &eaPrograma   = "";
     &eaPunto      = "";

     &eaLetra      = "";
     &eaDeDonde    = "";
     &eaLinDonde   = "";
     &eaAnyoUsu    = "";
     &eaEdadDec    = "";
     &eaPatr06     = "";
     &eaPatr07     = "";
     &eaPatr08     = "";
     &eaPatr09     = "";
     &eaPatr10     = "";
     &eaPatr11     = "";
     &eaPatr12     = "";
     &eaPatr13     = "";
     &eaPatr14     = "";
     &eaPatr15     = "";
     &eaPatr16     = "";
     &eaPatr17     = "";
     &eaPatr18     = "";
     &eaPatr19     = "";
     &eaPatr20     = "";
     &eaPatr21     = "";
|FIN;

|| ************************************************************************
|| PROCESOS DE CONSULTA DE PANTALLAS NORMALES
|| ************************************************************************

|PROCESO PintaPantalla;
     |SI nPanta < 0;   nPanta = 0;                         |FINSI;
     |SI nPanta = 0;   |PINPA #00#1003;  |PINDA #00#1003;  |FINSI;
     |SI nPanta = 1;   |PINPA #01#1003;  |PINDA #01#1003;  |FINSI;
|FPRC;

|PROCESO Varios;  |TIPO 7;
     |C_M #1003#5,  1, "N";
     |C_M #1003#6,  1, "N";
     |C_M #1003#7,  1, "N";
     |C_M #1003#8,  1, "N";
     |C_M #1003#9,  1, "N";
     |C_M #1003#10, 1, "N";

     aAlfa = eaSwVarios;
     aLong = aAlfa % 0;
     nLong = aLong;
     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPosi = (100 * nCaracter) + 1;
           aAlfa1 = aAlfa % nPosi;
           |SI aAlfa1 = "T";  |C_M #1003#5,  1, "S";  |FINSI;
           |SI aAlfa1 = "C";  |C_M #1003#6,  1, "S";  |FINSI;
           |SI aAlfa1 = "1";  |C_M #1003#7,  1, "S";  |FINSI;
           |SI aAlfa1 = "2";  |C_M #1003#8,  1, "S";  |FINSI;
           |SI aAlfa1 = "3";  |C_M #1003#9,  1, "S";  |FINSI;
           |SI aAlfa1 = "4";  |C_M #1003#10, 1, "S";  |FINSI;
     |SIGUE;

     |SI #1003#2 ! "VARIOS    ";
         |C_M #1003#5,  1, "N";  #1003#5  = 0;  |PINTA #1003#5;
         |C_M #1003#6,  1, "N";  #1003#6  = 0;  |PINTA #1003#6;
         |C_M #1003#7,  1, "N";  #1003#7  = 0;  |PINTA #1003#7;
         |C_M #1003#8,  1, "N";  #1003#8  = 0;  |PINTA #1003#8;
         |C_M #1003#9,  1, "N";  #1003#9  = 0;  |PINTA #1003#9;
         |C_M #1003#10, 1, "N";  #1003#10 = 0;  |PINTA #1003#10;
     |FINSI;
|FPRC;

|PROCESO MensaPorce;  |TIPO 7;
     |SI #1003#14 = "N";
         #1003#15 = 100;   |PINTA #1003#15;
         #1003#21 = "";    |PINTA #1003#21;
         |C_M #1003#15, 1, "N";
         |C_M #1003#21, 1, "N";
         |ACABA;
     |FINSI;

     |C_M #1003#15, 1, "S";
     |C_M #1003#21, 1, "S";

     |PUSHV 0501 2380;
     |CUADRO 0937 1480;
     |BLANCO 1038 1379;
     |ATRI R;
     |PINTA 1038, "Las cantidades se introduciran al 100 %,  ";
     |PINTA 1138, "luego el calculo las computara al porcen- ";
     |PINTA 1238, "taje de comunero que le hayamos informado.";
     |ATRI r;
     |LEETECLA aAlfa;
     |POPV;
|FPRC;

|PROCESO LetraNif;  |TIPO 6;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     eaVarDni  = #1003Campo;
     enCalcCif = 1;
     |HAZ CalculoDNI;
     #1003Campo = eaVarDni;
     |PINTA #1003Campo;
     |ERROR;
|FPRC;

|PROCESO Totalw1003;
     #1003#29 = #1003#26 + #1003#27 + #1003#28;
     #1003#44 = 0;
     |PARA nCampo = 30;  |SI nCampo [ 43;  |HACIENDO nCampo = nCampo + 1;
           #1003#44 = #1003#44 + #1003nCampo;
     |SIGUE;

     #1003#45 = #1003#29 - #1003#44;
     #1003#47 = 0;
     |SI #1003#46 = "S";  #1003#47 = #1003#45 % 30;  |FINSI;
     |SI #1003#47 < 0;    #1003#47 = 0;              |FINSI;
     #1003#48 = #1003#45 - #1003#47;

     |PINTA #1003#29;
     |PINTA #1003#44;
     |PINTA #1003#45;
     |PINTA #1003#47;
     |PINTA #1003#48;
|FPRC;

|PROCESO Fantasma;
     |SI #20#3 = "X";  |Y nModo = 1;
         |ACABA;
     |FINSI;

     |PARA nCampos = nDesde;  |SI nCampos [ nNumCampos;  |HACIENDO nCampos = nCampos + 1;
           #1003nCampos = #20nCampos;
     |SIGUE;

     aEscape    = &255 + "806";
     aAvance    = &255 + "815";
     aRetroceso = &255 + "814";

     |SI nNumPanta ] 1;
         |MENSA "      Av.Pag Pantalla Siguiente. Re.Pag Pantalla Anterior. ESC Salir.";
     |SINO;
         |MENSA "      ESC Salir.";
     |FINSI;

     |PUSHV 0401 2380;
     |SI nModo ! 4;
         |PINPA #0#1003;
         |PINDA #0#1003;
         |ENDATOS #1#1003;
         |SI FSalida = 0;
             |PARA nCampos = nDesde;  |SI nCampos [ nNumCampos;  |HACIENDO nCampos = nCampos + 1;
                   |SI enSwLiquida = 1;
                       |SI #20nCampos ! #1003nCampos;
                           enSwRetorno = 1;
                       |FINSI;
                   |FINSI;
                   #20nCampos = #1003nCampos;
             |SIGUE;
         |FINSI;
     |SINO;
         |PINPA #0#1003;
         |PINDA #0#1003;
         nPanta = 0;
         |PARA;  |SI;  |HACIENDO;
              |LEETECLA aAlfa;
              |SI aAlfa = aEscape;  |SAL;  |FINSI;
              |SI aAlfa = aAvance;
                  |SI nPanta < nNumPanta;
                      nPanta = nPanta + 1;
                      |HAZ PintaPantalla;
                  |FINSI;
              |FINSI;

              |SI aAlfa = aRetroceso;
                  |SI nPanta [ nNumPanta;
                      nPanta = nPanta - 1;
                      |HAZ PintaPantalla;
                  |FINSI;
              |FINSI;
         |SIGUE;
     |FINSI;
     |POPV;
|FPRC;

|RUTINA ClaveBaja20;
     #20#0 = enEmpresa;
     #20#1 = 1;
|FRUT;

|RUTINA ClaveAlta20;
     #20#0 = enEmpresa;
     #20#1 = 99;
|FRUT;

|PROCESO PonDeclarante;  |TIPO 7;
     eaDeclarante = #20#2;
     enComunes    = 1;
     |HAZ SacaDeclarantes;
     #20#2 = eaDeclarante;
     |PINTA #20#2;
|FPRC;

|PROCESO Fallece;  |TIPO 7;
     |SI eaFallece = "S";
         |C_M #20#4, 1, "S";
     |SINO;
         |C_M #20#4, 1, "N";  #20#4 = 0;  |PINTA #20#4;
     |FINSI;
|FPRC;

|PROCESO Patrimonio;
     |SI #20#3  = "R";           |ACABA;  |FINSI;
     |SI #20#3  = "X";           |ACABA;  |FINSI;
     |SI #20#2  = "1 FAMILIAR";  |ACABA;  |FINSI;
     |SI #20#2  = "2 FAMILIAR";  |ACABA;  |FINSI;
     |SI #20#2  = "3 FAMILIAR";  |ACABA;  |FINSI;
     |SI #20#2  = "4 FAMILIAR";  |ACABA;  |FINSI;
     |SI #20#11 = 0;             |ACABA;  |FINSI;

     |HAZ LimpiaPatrim;

     eaLetra    = "C1";
     eaDeDonde  = "D0";
     eaLinDonde = #20#1;
     eaAnyoUsu  = 0;
     eaPatr06   = #20#25;
     eaPatr08   = #20#23;
     eaPatr09   = #20#24;
     eaPatr15   = #20#22;
     eaPatr18   = #20#19;
     nValor     = #20#11 % #20#15;
     eaPatr10   = nValor;
     eaPatr20   = #20#5;
     eaPatr21   = #20#6;

     |HAZ GrabaPatrimonio;
|FPRC;

|PROCESO Tipo2m20;  |TIPO 2;
     nModo = (FEntrada / 100) ! 0;

     nEntrada = 0 +. 1;
     |SI nEntrada ! 1;
         eaDeDonde  = "D0";
         eaLinDonde = #20#1;
         eaLetra    = "C1";
         |HAZ BorraPatrimonio;
     |FINSI;

     |SI nModo = 3;  enSwRetorno = 1;  |ACABA;  |FINSI;
     |SI nModo = 1;  enSwRetorno = 1;           |FINSI;

     |SI nEntrada = 1;
         nDesde = 5;
         |SI nModo = 1;  nDesde = 0;  |FINSI;
         nModo     = 1;

         |HAZ Fantasma;

         |SI enSwLiquida = 1;
             |PARA nCampos = 0;  |SI nCampos [ 4;  |HACIENDO nCampos = nCampos + 1;
                   |SI #20nCampos ! #1003nCampos;
                       enSwRetorno = 1;
                   |FINSI;
             |SIGUE;
         |FINSI;
         |HAZ Patrimonio;
     |SINO;
         |PARA nCampos = 0;  |SI nCampos [ 4;  |HACIENDO nCampos = nCampos + 1;
               #1003nCampos = #20nCampos;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO Tipo11m20;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     nDesde = 0;   nModo = 4;   |HAZ Fantasma;
     |ERROR;
|FPRC;

|PROGRAMA;
     aQueQuiero = "|NP";
     aNumPanta  = #1003#aQueQuiero#;
     nNumPanta  = aNumPanta;
     nNumPanta  = nNumPanta - 1;

     aQueQuiero = "|NC";
     aNumCampos = #1003#aQueQuiero#;
     nNumCampos = aNumCampos;
     nNumCampos = nNumCampos - 1;

     |ENTLINEAL #1#20, -2, enModo, 22, 1, ClaveBaja20, ClaveAlta20;

     eaPrograma   = "rem00025";
     eaPunto      = "  ";
     |HAZ ProgramaActivo;
|FPRO;
