|FICHEROS;
     rem00041 #20;
     retabla7 #407;
     espejo@ #99;
|FIN;

|VARIABLES;
     aAlfa         = "";
     aAlfa1        = "";
     aAlfa2        = "";
     aLong         = "";
     aQueQuiero    = "";
     aNumCampos    = "";
     aNumPanta     = "";
     aPathRenta    = "";
     aDef          = "";

     nLong         = 0;
     nCaracter     = 0;
     nPosi         = 0;
     nPanta        = 0;
     nNumCampos    = 0;
     nNumPanta     = 0;
     nCampo        = 0;
     nModo         = 0;
     nEntrada      = 0;
     nValor        = 0;
     nAnyoVenta    = 0;
     nAnyoCompra   = 0;
     nDiferencia   = 0;
     nCoefi        = 0;

     &enEmpresa    = 0;
     &enModo       = 0;
     &eaDeclarante = "";
     &enComunes    = 0;
     &enMinusvalia = 0;
     &eaMinuAyuda  = "";
     &eaFallece    = "";
     &eaSwVarios   = "";
     &enSwLiquida  = 0;
     &enSwRetorno  = 0;
     &eaPrograma   = "";
     &eaPunto      = "";
|FIN;

|| ************************************************************************
|| PROCESOS DE CONSULTA DE PANTALLAS NORMALES
|| ************************************************************************

|PROCESO Automatico;
     aAlfa1      = #20#14 % -104;
     aAlfa2      = #20#15 % -104;
     nAnyoCompra = aAlfa1;
     nAnyoVenta  = aAlfa2;
     nDiferencia = (#20#15 - #20#14) + 1;

     |SI #20#12 = "N";
         |SI #20#14      < "31.12.1994";    nAnyoCompra = 1994;  |FINSI;
         |SI #20#14      = "31.12.1994";    nAnyoCompra = 1995;  |FINSI;

         |ABRE #407;
         #407#0 = 2001;
         #407#1 = nAnyoCompra;
         |LEE 040#407=;
         |SI FSalida ! 0;  |DDEFECTO #407;  |FINSI;
         |CIERRA #407;

         #20#18 = #407#2;
         |SI nDiferencia [ 365;             #20#18 = 1;      |FINSI;
         |SI #20#13      ! "I";             #20#18 = 1;      |FINSI;
     |SINO;
         |SI #20#14      < "01.01.1984";    nAnyoCompra = 1983;  |FINSI;

         |ABRE #407;
         #407#0 = 2001;
         #407#1 = nAnyoCompra;
         |LEE 040#407=;
         |SI FSalida ! 0;  |DDEFECTO #407;  |FINSI;
         |CIERRA #407;

         #20#18 = #407#3;

         |SI nDiferencia < 365;             #20#18 = 1;      |FINSI;
         |SI #20#13      ! "I";             #20#18 = 1;      |FINSI;
     |FINSI;

     nAnyoCompra = aAlfa1;

     #20#19 = #20#17 * #20#18;
     #20#21 = #20#19 - #20#20;

     nValor = #20#16 - #20#21;
     |SI nValor < 0;
         #20#22 = -nValor;
         |SI #20#23 > #20#22;  |O nModo = 1;  |O #20#23 < 0;
             #20#23 = #20#22;
         |FINSI;

         |C_M #20#23, 1, "S";
         |PARA nCampo = 24;  |SI nCampo [ 33;  |HACIENDO nCampo = nCampo + 1;
               #20nCampo = 0; |C_M #20nCampo, 1, "N";
         |SIGUE;
     |SINO;
                               #20#22 = 0;
         |C_M #20#23, 1, "N";  #20#23 = 0;

         #20#24 = nValor;

         |SI #20#12 = "N";
             #20#25 = (1996 - nAnyoCompra) + 1;
             nCoefi = 0;
             |SI #20#25 ] 3;
                 |SI #20#13 = "I";  nCoefi = (#20#25 - 2) * 11.11;  |FINSI;
                 |SI #20#13 = "A";  nCoefi = (#20#25 - 2) * 25;     |FINSI;
                 |SI #20#13 = "O";  nCoefi = (#20#25 - 2) * 14.28;  |FINSI;
             |FINSI;
             |SI nCoefi > 99; nCoefi = 100; |FINSI;
             |SI nCoefi [ 100;
                 #20#26 = #20#24 % (100 - nCoefi);
             |FINSI;

             |SI #20#27 > #20#26;
                 #20#27 = #20#26;
             |FINSI;

             #20#28 = #20#26 - #20#27;
             |SI #20#29 > #20#28;  |O nModo = 1; |O #20#29 < 0;
                 #20#29 = #20#28;
             |FINSI;

             |C_M #20#27, 1, "S";
             |C_M #20#29, 1, "S";

             |PARA nCampo = 30;  |SI nCampo [ 33;  |HACIENDO nCampo = nCampo + 1;
                   #20nCampo = 0; |C_M #20nCampo, 1, "N";
             |SIGUE;
         |SINO;
             #20#31 = #20#24 - #20#30;

             |SI #20#32 > #20#31;  |O nModo = 1;
                 #20#32 = #20#31;
             |FINSI;

             #20#33 = #20#31 - #20#32;

             |C_M #20#30, 1, "S";
             |C_M #20#32, 1, "S";
             |C_M #20#33, 1, "S";

             |PARA nCampo = 25;  |SI nCampo [ 29;  |HACIENDO nCampo = nCampo + 1;
                   #20nCampo = 0; |C_M #20nCampo, 1, "N";
             |SIGUE;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Manual;
     |C_M #20#18, 1, "S";

     #20#19 = #20#17 * #20#18;
     #20#21 = #20#19 - #20#20;

     nValor = #20#16 - #20#21;
     |SI nValor < 0;
         #20#22 = -nValor;
         |SI #20#23 > #20#22;  |O nModo = 1;  |O #20#23 < 0;
             #20#23 = #20#22;
         |FINSI;

         |C_M #20#23, 1, "S";
         |PARA nCampo = 24;  |SI nCampo [ 33;  |HACIENDO nCampo = nCampo + 1;
               #20nCampo = 0; |C_M #20nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |C_M #20#25, 1, "S";
         |C_M #20#26, 1, "S";
         |C_M #20#28, 1, "S";
         |C_M #20#30, 1, "S";
         |C_M #20#31, 1, "S";
         |C_M #20#32, 1, "S";
         |C_M #20#33, 1, "S";

         #20#22 = 0;
         #20#23 = 0;
         #20#24 = nValor;

         |C_M #20#23, 1, "N";
     |FINSI;
|FPRC;

|PROCESO Totalm20;
     nModo  = (FEntrada / 100) ! 0;

     |SI #20#13 = "M";  |HAZ Manual;      |FINSI;
     |SI #20#13 ! "M";  |HAZ Automatico;  |FINSI;

     |PARA nCampo = 18;  |SI nCampo [ 33;  |HACIENDO nCampo = nCampo + 1;
           |PINTA #20nCampo;
     |SIGUE;
|FPRC;

|PROCESO Varios;  |TIPO 7;
     |C_M #20#5,  1, "N";
     |C_M #20#6,  1, "N";
     |C_M #20#7,  1, "N";
     |C_M #20#8,  1, "N";
     |C_M #20#9,  1, "N";
     |C_M #20#10, 1, "N";

     aAlfa = eaSwVarios;
     aLong = aAlfa % 0;
     nLong = aLong;
     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPosi = (100 * nCaracter) + 1;
           aAlfa1 = aAlfa % nPosi;
           |SI aAlfa1 = "T";  |C_M #20#5,  1, "S";  |FINSI;
           |SI aAlfa1 = "C";  |C_M #20#6,  1, "S";  |FINSI;
           |SI aAlfa1 = "1";  |C_M #20#7,  1, "S";  |FINSI;
           |SI aAlfa1 = "2";  |C_M #20#8,  1, "S";  |FINSI;
           |SI aAlfa1 = "3";  |C_M #20#9,  1, "S";  |FINSI;
           |SI aAlfa1 = "4";  |C_M #20#10, 1, "S";  |FINSI;
     |SIGUE;

     |SI #20#2 ! "VARIOS    ";
         |C_M #20#5,  1, "N";  #20#5  = 0;  |PINTA #20#5;
         |C_M #20#6,  1, "N";  #20#6  = 0;  |PINTA #20#6;
         |C_M #20#7,  1, "N";  #20#7  = 0;  |PINTA #20#7;
         |C_M #20#8,  1, "N";  #20#8  = 0;  |PINTA #20#8;
         |C_M #20#9,  1, "N";  #20#9  = 0;  |PINTA #20#9;
         |C_M #20#10, 1, "N";  #20#10 = 0;  |PINTA #20#10;
     |FINSI;
|FPRC;

|RUTINA ClaveBaja20;
     #20#0 = enEmpresa;
     #20#1 = 1;
|FRUT;

|RUTINA ClaveAlta20;
     #20#0 = enEmpresa;
     #20#1 = 99;
|FRUT;

|PROCESO PonDeclarante;  |TIPO 7;
     eaDeclarante = #20#2;
     enComunes    = 1;
     |HAZ SacaDeclarantes;
     #20#2 = eaDeclarante;
     |PINTA #20#2;
|FPRC;

|PROCESO Fallece;  |TIPO 7;
     |SI eaFallece = "S";
         |C_M #20#4, 1, "S";
     |SINO;
         |C_M #20#4, 1, "N";  #20#4 = 0;  |PINTA #20#4;
     |FINSI;
|FPRC;

|PROCESO Tipo2m20;  |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;  |O nModo = 3;
         enSwRetorno  = 1;
         |ACABA;
     |FINSI;

     nEntrada = 0 +. 1;
     |SI nEntrada ! 1;
         |PARA nCampo = 0;  |SI nCampo [ nNumCampos;  |HACIENDO nCampo = nCampo + 1;
               #99nCampo = #20nCampo;
         |SIGUE;
     |SINO;
         |PARA nCampo = 0;  |SI nCampo [ nNumCampos;  |HACIENDO nCampo = nCampo + 1;
               |SI #99nCampo ! #20nCampo;
                   enSwRetorno  = 1;
               |FINSI;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO Tipo3;  |TIPO 3;
     |SI #20#22 ! 0;
         |SI #20#23 = 0;
             |MENAV "0000 La Cantidad que tenemos en el Imputable esta a cero. Verifique el dato.";
         |FINSI;
     |FINSI;

     |SI #20#26 ! 0;  |O #20#27 ! 0;  |O #20#28 ! 0;
         |SI #20#29 = 0;
             |MENAV "0000 La Cantidad que tenemos en el Imputable esta a cero. Verifique el dato.";
         |FINSI;
     |FINSI;

     |SI #20#30 ! 0;  |O #20#31 ! 0;  |O #20#32 ! 0;
         |SI #20#33 = 0;
             |MENAV "0000 La Cantidad que tenemos en el Imputable esta a cero. Verifique el dato.";
         |FINSI;
     |FINSI;
|FPRC;

|PROGRAMA;
     aQueQuiero = "|NP";
     aNumPanta  = #20#aQueQuiero#;
     nNumPanta  = aNumPanta;
     nNumPanta  = nNumPanta - 1;

     aQueQuiero = "|NC";
     aNumCampos = #20#aQueQuiero#;
     nNumCampos = aNumCampos;
     nNumCampos = nNumCampos - 1;

     |DBASE aPathRenta;  |QBT aPathRenta;
     aDef = aPathRenta + "def/rem00041.mas";
     |CARGA_DEF aDef, espejo@;
     |SI FSalida ! 0;
         aAlfa = "     Error en cargar el Def rem00041.mas";
         |MENAV aAlfa;
         |ERROR;
     |FINSI;

     |ENTLINEAL #1#20, -2, enModo, 22, 1, ClaveBaja20, ClaveAlta20;

     |DESCARGA_DEF #espejo@;

     eaPrograma   = "rem00041";
     eaPunto      = "  ";
     |HAZ ProgramaActivo;
|FPRO;
