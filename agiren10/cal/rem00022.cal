|FICHEROS;
     reparame #1;
     rem00022 #20;
     rem00043 #43,MANTE;
     rem00041 #41;
     rem00059 #59;
     retabla7 #407;
     rew99998 #900,MANTE;
     rew99999 #999;
     rem99999 #666;
     espejo@ #99;
|FIN;

|VARIABLES;
     aAlfa         = "";
     aAlfa1        = "";
     aAlfa2        = "";
     aAlfa3        = "";
     aLong         = "";
     aQueQuiero    = "";
     aNumCampos    = "";
     aNumPanta     = "";
     aPathRenta    = "";
     aDef          = "";
     aDescri       = "";
     aTecla        = "";

     nLong         = 0;
     nCaracter     = 0;
     nPosi         = 0;
     nPanta        = 0;
     nNumCampos    = 0;
     nNumPanta     = 0;
     nCampo        = 0;
     nModo         = 0;
     nEntrada      = 0;
     nPtas         = 0;
     nRetencion    = 0;
     nCampo15      = 0;
     nCampo16      = 0;
     nCampo17      = 0;
     nCampo20      = 0;
     nCampo27      = 0;
     nValor        = 0;
     nValor1       = 0;
     nValorPat     = 0;
     nDias         = 0;
     nAnyoVenta    = 0;
     nAnyoCompra   = 0;
     nDiferencia   = 0;
     nCoefi        = 0;
     nLinea        = 0;
     nContenido    = 0;
     nCas732       = 0;
     nCas733       = 0;
     nCas735       = 0;
     nCas736       = 0;
     nCas737       = 0;
     nCas745       = 0;
     nCas746       = 0;
     nCas747       = 0;
     nCas748       = 0;
     nAnyo         = 0;
     nSwHay        = 0;
     nCampo1       = 0;
     nCampo2       = 0;
     nCampo3       = 0;
     nCampo4       = 0;
     nCampo5       = 0;
     nFallecido    = 0;

     nTodosDias    = 0;
     nParciDias    = 0;

     fFechaLim     = @;

     fFechaAdq     = @;
     fFechaTra     = @;
     fFecha1       = @;
     fFecha2       = @;

     &enEmpresa    = 0;
     &enModo       = 0;
     &eaDeclarante = "";
     &enComunes    = 0;
     &enMinusvalia = 0;
     &eaMinuAyuda  = "";
     &eaFallece    = "";
     &eaSwVarios   = "";
     &enSwLiquida  = 0;
     &enSwRetorno  = 0;
     &eaPrograma   = "";
     &eaPunto      = "";

     &eaLetra      = "";
     &eaDeDonde    = "";
     &eaLinDonde   = "";
     &eaAnyoUsu    = "";
     &eaEdadDec    = "";
     &eaPatr06     = "";
     &eaPatr07     = "";
     &eaPatr08     = "";
     &eaPatr09     = "";
     &eaPatr10     = "";
     &eaPatr11     = "";
     &eaPatr12     = "";
     &eaPatr13     = "";
     &eaPatr14     = "";
     &eaPatr15     = "";
     &eaPatr16     = "";
     &eaPatr17     = "";
     &eaPatr18     = "";
     &eaPatr19     = "";
     &eaPatr20     = "";
     &eaPatr21     = "";
|FIN;

|PROCESO Tipo12Fm43;  |TIPO 12;
     |SI #20#11 ! "F1";  |Y #20#11 ! "G1";  |Y #20#11 ! "H1";
      |Y #20#11 ! "G2";
         #43#15 = 0;
         |ACABA;
     |FINSI;

     |SI #43#6 ] "31.12.1994";
         #43#15 = 0;
         |ACABA;
     |FINSI;

     |SI #20#11 = "G2";
         |SI #43#2 < "20.01.2006";
             #43#15 = 0;
             |ACABA;
         |FINSI;
     |FINSI;

     |PUSHV 0501 2380;
     |BLANCO 1150 1780;
     |CUADRO 1150 1780;
     |ATRI R;
     |PINTA 1251, "      VALOR PATRIMONIAL      ";
     |PINTA 1351, "CALCULO GANANCIA SUSCEP.REDU.";
     |ATRI r;

     E_inf     = "1.?EURO";
     E_sup     = "9999999999";
     nValor    = #43#15;
     nValor    = 1560 ? 1;
     #43#15    = nValor;

     |POPV;
|FPRC;

|PROCESO Calculadora43;
     |SI FSalida ! 13;  |ACABA;  |FINSI;

     |PUSHV 0501 2380;
     |CUADRO 1030 1672;
     |BLANCO 1131 1571;

     |PINTA 1131, "Importe 1 .............:                ";
     |PINTA 1231, "Importe 2 .............:                ";
     |PINTA 1331, "Importe 3 .............:                ";
     |PINTA 1431, "Importe 4 .............:                ";
     |PINTA 1531, "Importe 5 .............:                ";

     E_inf = -999999999.99;
     E_sup = 9999999999.99;

     nCampo1 = #43Campo;
     nCampo2 = 0;
     nCampo3 = 0;
     nCampo4 = 0;
     nCampo5 = 0;

     |ET nCampo43_1;
         nCampo1 = 1156 ? 1;
         |SI FSalida = 1;  |VETE Abajo43;    |FINSI;
         |SI FSalida = 7;  |VETE Abajo43;    |FINSI;

     |ET nCampo43_2;
         nCampo2 = 1256 ? 1;
         |SI FSalida = 2;  |VETE nCampo43_1;  |FINSI;
         |SI FSalida = 1;  |VETE Abajo43;    |FINSI;
         |SI FSalida = 7;  |VETE Abajo43;    |FINSI;

     |ET nCampo43_3;
         nCampo3 = 1356 ? 1;
         |SI FSalida = 2;  |VETE nCampo43_2;  |FINSI;
         |SI FSalida = 1;  |VETE Abajo43;    |FINSI;
         |SI FSalida = 7;  |VETE Abajo43;    |FINSI;

     |ET nCampo43_4;
         nCampo4 = 1456 ? 1;
         |SI FSalida = 2;  |VETE nCampo43_3;  |FINSI;
         |SI FSalida = 1;  |VETE Abajo43;    |FINSI;
         |SI FSalida = 7;  |VETE Abajo43;    |FINSI;

     |ET nCampo43_5;
         nCampo5 = 1556 ? 1;
         |SI FSalida = 2;  |VETE nCampo43_4;  |FINSI;
         |SI FSalida = 1;  |VETE Abajo43;    |FINSI;
         |SI FSalida = 7;  |VETE Abajo43;    |FINSI;

     |ET Abajo43;
         #43Campo = nCampo1 + nCampo2 + nCampo3 + nCampo4 + nCampo5;

     |POPV;

     |PINTA #43Campo;
|FPRC;
|| ************************************************************************
|| PROCESOS DE CONSULTA DE PANTALLAS NORMALES
|| ************************************************************************

|PROCESO Calculadora;
     |SI FSalida ! 13;  |ACABA;  |FINSI;

     |PUSHV 0501 2380;
     |CUADRO 1030 1672;
     |BLANCO 1131 1571;

     |PINTA 1131, "Importe 1 .............:                ";
     |PINTA 1231, "Importe 2 .............:                ";
     |PINTA 1331, "Importe 3 .............:                ";
     |PINTA 1431, "Importe 4 .............:                ";
     |PINTA 1531, "Importe 5 .............:                ";

     E_inf = -999999999.99;
     E_sup = 9999999999.99;

     nCampo1 = #20Campo;
     nCampo2 = 0;
     nCampo3 = 0;
     nCampo4 = 0;
     nCampo5 = 0;

     |ET nCampo1;
         nCampo1 = 1156 ? 1;
         |SI FSalida = 1;  |VETE Abajo;    |FINSI;
         |SI FSalida = 7;  |VETE Abajo;    |FINSI;

     |ET nCampo2;
         nCampo2 = 1256 ? 1;
         |SI FSalida = 2;  |VETE nCampo1;  |FINSI;
         |SI FSalida = 1;  |VETE Abajo;    |FINSI;
         |SI FSalida = 7;  |VETE Abajo;    |FINSI;

     |ET nCampo3;
         nCampo3 = 1356 ? 1;
         |SI FSalida = 2;  |VETE nCampo2;  |FINSI;
         |SI FSalida = 1;  |VETE Abajo;    |FINSI;
         |SI FSalida = 7;  |VETE Abajo;    |FINSI;

     |ET nCampo4;
         nCampo4 = 1456 ? 1;
         |SI FSalida = 2;  |VETE nCampo3;  |FINSI;
         |SI FSalida = 1;  |VETE Abajo;    |FINSI;
         |SI FSalida = 7;  |VETE Abajo;    |FINSI;

     |ET nCampo5;
         nCampo5 = 1556 ? 1;
         |SI FSalida = 2;  |VETE nCampo4;  |FINSI;
         |SI FSalida = 1;  |VETE Abajo;    |FINSI;
         |SI FSalida = 7;  |VETE Abajo;    |FINSI;

     |ET Abajo;
         #20Campo = nCampo1 + nCampo2 + nCampo3 + nCampo4 + nCampo5;

     |POPV;

     |PINTA #20Campo;
|FPRC;

|PROCESO FechaTope;
     |SI #43#6 > #43#2;
         |MENAV "      Fecha Compra es Superior a la Fecha Operacion.";
         |ERROR;
     |FINSI;

     |SI #43#4 ! "V";           |ACABA;  |FINSI;
     |SI #43#6 ] "31.12.1994";  |ACABA;  |FINSI;

     |SI #20#2 = "GANANCIAL ";  |O #20#2 = "VARIOS    ";
         nFallecido = 0;

         |ABRE #999;
         |SELECT #2#999;
         #999#1 = "TITULAR   ";
         |LEE 000#999=;
         |SI FSalida ! 0;  |DDEFECTO #999;  |FINSI;
         |SI #999#5 = "S";  nFallecido = 1;  |FINSI;

         |SELECT #2#999;
         #999#1 = "CONYUGE   ";
         |LEE 000#999=;
         |SI FSalida ! 0;  |DDEFECTO #999;  |FINSI;
         |SI #999#5 = "S";  nFallecido = 1;  |FINSI;

         |SELECT #1#999;
         |CIERRA #999;

         |SI nFallecido = 1;
             |MENAV "0000 Venta con un fallecido con fecha de adquisicion inferior a 31.12.1994, por favor introduzcalos individuales";
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Operacion;
     |C_M #43#6,  1, "S";
     |C_M #43#7,  1, "S";
     |C_M #43#10, 1, "S";

     |SI #43#4 = "C";
         |C_M #43#6,  1, "N";
         |C_M #43#10, 1, "N";  #43#10 = 0;
         |C_M #43#11, 1, "N";  #43#11 = 0;

         #43#9 = "01.01.0000";
         #43#6 = #43#2;
     |FINSI;

     |SI #43#4 = "V";
         #43#9 = #43#2;
         |SI #43#9 < "01.01.2010";  |O #43#9 > "31.12.2010";
             |MENAV "    No puede ser una Venta. La Fecha no corresponde al Ejercicio";
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;

     |PINTA #43#6;
     |PINTA #43#9;
     |PINTA #43#10;
     |PINTA #43#11;
|FPRC;

|PROCESO Totalm043;
     #43#8  = #43#5 * #43#7;
     #43#11 = #43#5 * #43#10;

     |PINTA #43#8;
     |PINTA #43#11;
|FPRC;

|RUTINA ClaveBaja43;
     #43#0 = #20#0;
     #43#1 = #20#1;
     #43#2 = "01.01.1900";
     #43#3 = 1;
|FRUT;

|RUTINA ClaveAlta43;
     #43#0 = #20#0;
     #43#1 = #20#1;
     #43#2 = "31.12.2010";
     #43#3 = 99;
|FRUT;

|PROCESO Tipo7m20;  |TIPO 7;
     |ENTLINEAL #1#43, 3, 7, 22, 0, ClaveBaja43, ClaveAlta43;
|FPRC;

|PROCESO Lineal; |TIPO 0;
     |SI FSalida = 13;
         |HAZ Calculadora;
         |ERROR;
     |FINSI;

     |SI FSalida = 2;  |ACABA;  |FINSI;

     |ENTLINEAL #1#43, -3, 2, 22, 0, ClaveBaja43, ClaveAlta43;

     |HAZ Tipo7m20;
|FPRC;

|PROCESO Usufructuario;
     |C_M #20#14, 1, "S";
     |SI #20#13 ! "N";
         |C_M #20#14, 1, "N";  #20#14 = 0;  |PINTA #20#14;
     |FINSI;
|FPRC;

|PROCESO nContenido;
     nContenido = Contenido;
     |SI #20Campo ! nContenido;
         #20#21 = #20#19 % #20#20;   |PINTA #20#21;
         #20#24 = #20#19 % #20#23;   |PINTA #20#24;
     |FINSI;
|FPRC;

|PROCESO Incremento;
     |SI #20#20 ! 40;  |Y #20#20 ! 25;  |Y #20#20 ! 0;
         |MENAV  "     Porcentaje incremento incoherente, el 25% o 40%";
         |ERROR;
         |ACABA;
     |FINSI;

     nContenido = Contenido;
     |SI #20Campo ! nContenido; |O #20#21 = 0;
         #20#21 = #20#19 % #20#20;
     |FINSI;

     |ABRE #1;
     |LEE 040#1p;
     |SI FSalida ! 0;  |DDEFECTO #1;  |FINSI;
     |CIERRA #1;

     |SI #1#18 = "S";  |Y #20#25 ! -1;
         #20#25 = #20#20;           |PINTA #20#25;
         #20#26 = #20#19 % #20#25;  |PINTA #20#26;
     |FINSI;
|FPRC;

|PROCESO Tipo0C28;
     |C_M #20#20, 1, "S";
     |C_M #20#21, 1, "S";
     |C_M #20#25, 1, "S";
     |C_M #20#26, 1, "S";
     |SI #20#28 = "S";
         |C_M #20#20, 1, "N";  #20#20 = 0;  |PINTA #20#20;
         |C_M #20#21, 1, "N";  #20#21 = 0;  |PINTA #20#21;
         |C_M #20#25, 1, "N";  #20#25 = 0;  |PINTA #20#25;
         |C_M #20#26, 1, "N";  #20#26 = 0;  |PINTA #20#26;
     |FINSI;
|FPRC;

|PROCESO Retencion;
     nContenido = Contenido;
     |SI #20Campo = nContenido;  |Y #20#24 ! 0;  |ACABA;  |FINSI;

     #20#24 = #20#19 % #20#23;
|FPRC;

|PROCESO DobleImposicion;
     |SI #20#25 = 0;   #20#26 = 0;  |ACABA;  |FINSI;
     |SI #20#25 = -1;               |ACABA;  |FINSI;

     |SI #20#20 = 40;
         |SI #20#25 ! 40;
             |MENAV "     La Doble Imposicion debe ser 40%";
             #20#25 = 40;
         |FINSI;
     |FINSI;

     |SI #20#20 = 25;
         |SI #20#25 ! 25;
             |MENAV "     La Doble Imposicion debe ser 25%";
             #20#25 = 25;
         |FINSI;
     |FINSI;

     |SI #20#20 = 0;
         |SI #20#25 ! 10;  |Y #20#25 ! 5;
             |MENAV "     La Doble Imposicion debe ser 5% O 10%";
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;

     nContenido = Contenido;
     |SI #20Campo = nContenido;  |Y #20#26 ! 0;  |ACABA;  |FINSI;

     #20#26 = #20#19 % #20#25;
|FPRC;

|PROCESO Totalm020;
     #20#22 = #20#19 + #20#21;

     |PINTA #20#21;
     |PINTA #20#22;
     |PINTA #20#24;
     |PINTA #20#25;
     |PINTA #20#26;
|FPRC;

|PROCESO Varios;  |TIPO 0;
     |SI #20#3 = "X";  |ACABA;  |FINSI;

     |SI #20#2 ! "VARIOS    ";
         #20#5  = 0;
         #20#6  = 0;
         #20#7  = 0;
         #20#8  = 0;
         #20#9  = 0;
         #20#10 = 0;
         |ACABA;
     |FINSI;

     |C_M #900#0,  1, "N";
     |C_M #900#1,  1, "N";
     |C_M #900#2,  1, "N";
     |C_M #900#3,  1, "N";
     |C_M #900#4,  1, "N";
     |C_M #900#5,  1, "N";

     aAlfa = eaSwVarios;
     aLong = aAlfa % 0;
     nLong = aLong;
     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPosi = (100 * nCaracter) + 1;
           aAlfa1 = aAlfa % nPosi;
           |SI aAlfa1 = "T";  |C_M #900#0,  1, "S";  |FINSI;
           |SI aAlfa1 = "C";  |C_M #900#1,  1, "S";  |FINSI;
           |SI aAlfa1 = "1";  |C_M #900#2,  1, "S";  |FINSI;
           |SI aAlfa1 = "2";  |C_M #900#3,  1, "S";  |FINSI;
           |SI aAlfa1 = "3";  |C_M #900#4,  1, "S";  |FINSI;
           |SI aAlfa1 = "4";  |C_M #900#5,  1, "S";  |FINSI;
     |SIGUE;

     #900#0 = #20#5;
     #900#1 = #20#6;
     #900#2 = #20#7;
     #900#3 = #20#8;
     #900#4 = #20#9;
     #900#5 = #20#10;

     |PUSHV 0501 2380;

     |PINPA #0#900;
     |PINDA #0#900;
     |ENDATOS #1#900;
     |SI FSalida = 0;
         #20#5  = #900#0;
         #20#6  = #900#1;
         #20#7  = #900#2;
         #20#8  = #900#3;
         #20#9  = #900#4;
         #20#10 = #900#5;
     |FINSI;

     |POPV;
|FPRC;

|RUTINA ClaveBaja20;
     #20#0 = enEmpresa;
     #20#1 = 1;
|FRUT;

|RUTINA ClaveAlta20;
     #20#0 = enEmpresa;
     #20#1 = 99;
|FRUT;

|PROCESO PonDeclarante;  |TIPO 7;
     eaDeclarante = #20#2;
     enComunes    = 1;
     |HAZ SacaDeclarantes;
     #20#2 = eaDeclarante;
     |PINTA #20#2;
|FPRC;

|PROCESO Fallece;  |TIPO 7;
     |SI eaFallece = "S";
         |C_M #20#4, 1, "S";
     |SINO;
         |C_M #20#4, 1, "N";  #20#4 = 0;  |PINTA #20#4;
     |FINSI;
|FPRC;

|PROCESO TipoX;
     |SI #20Campo = "X";
         |PTEC 806;
     |FINSI;
|FPRC;

|PROCESO Calculo411;
     |SI #41#14 ] "31.12.1994";  |ACABA;  |FINSI;

     |ABRE #999;
     |SELECT #2#999;
     #999#1 = #41#2;
     |LEE 000#999=;
     |SI FSalida ! 0;  |DDEFECTO #999;  |FINSI;

     |SELECT #1#999;
     |CIERRA #999;

     |SI #999#5 = "S";
         #41#39 = #41#24;
         |ACABA;
     |FINSI;

     |SI #41#41 < #41#16;
         #41#39 = #41#41 - #41#21;
     |SINO;
         #41#39 = #41#24;
     |FINSI;
|FPRC;

|PROCESO FechaLimite;
     fFechaLim  = "20.01.2006";

     |SI #41#15 [ fFechaLim;  |ACABA;  |FINSI;

     |ABRE #999;
     |SELECT #2#999;
     #999#1 = #41#2;
     |LEE 000#999=;
     |SI FSalida ! 0;  |DDEFECTO #999;  |FINSI;

     |SELECT #1#999;
     |CIERRA #999;

     |SI #999#5 = "S";
         #41#39 = #41#24;
         |ACABA;
     |FINSI;

     nTodosDias = (#41#15    - #41#14);
     nParciDias = (fFechaLim - #41#14);

     #41#39     = ((#41#24 / nTodosDias) * nParciDias) ! 2;
|FPRC;

|PROCESO Total41;
     aAlfa1      = #41#14 % -104;
     aAlfa3      = #41#14 % 0105;
     aAlfa2      = #41#15 % -104;
     nAnyoCompra = aAlfa1;
     nAnyoVenta  = aAlfa2;
     nDiferencia = #41#15 - #41#14

     |SI #41#12 = "1";  |O #41#12 = "2";
         |SI #41#14      < "01.01.1984";    nAnyoCompra = 1983;  |FINSI;

         |ABRE #407;
         #407#0 = 2010;
         #407#1 = nAnyoCompra;
         |LEE 040#407=;
         |SI FSalida ! 0;  |DDEFECTO #407;  |FINSI;
         |CIERRA #407;

         #41#18 = #407#3;

         |SI nDiferencia < 365;             #41#18 = 1;      |FINSI;
         |SI #41#13      ! "I";             #41#18 = 1;      |FINSI;
     |FINSI;

     |SI #41#12 = "3";
         |SI #41#14      < "31.12.1994";    nAnyoCompra = 1994;  |FINSI;
         |SI #41#14      = "31.12.1994";    nAnyoCompra = 1995;  |FINSI;

         |ABRE #407;
         #407#0 = 2010;
         #407#1 = nAnyoCompra;
         |LEE 040#407=;
         |SI FSalida ! 0;  |DDEFECTO #407;  |FINSI;
         |CIERRA #407;

         #41#18 = #407#2;
         |SI nDiferencia [ 365;             #41#18 = 1;      |FINSI;
         |SI #41#13      ! "I";             #41#18 = 1;      |FINSI;
     |FINSI;

     |SI #41#12 = "4";  |O #41#12 = "5";  |O #41#12 = "6";  |O #41#12 = "7";
         #41#18 = 1;
     |FINSI;

     nAnyoCompra = aAlfa1;
     |SI aAlfa3 = "31.12";  nAnyoCompra = nAnyoCompra + 1;  |FINSI;

     #41#19 = #41#17 * #41#18;
     #41#21 = #41#19 - #41#20;
     #41#40 = 0;

     nValor = #41#16 - #41#21;
     |SI nValor < 0;
         #41#22 = -nValor;
         |SI #43#14 = "S";  #41#23 = #41#22;  |FINSI;

         |PARA nCampo = 24;  |SI nCampo [ 33;  |HACIENDO nCampo = nCampo + 1;
               #41nCampo = 0;
         |SIGUE;

         #41#38 = "N";
         #41#39 = 0;
         #41#40 = 0;
     |SINO;
         #41#22 = 0;
         #41#23 = 0;

         #41#24 = nValor;

         |SI #41#12 ! "1";  |Y #41#12 ! "2";
             #41#25 = ((1996 - nAnyoCompra) + 1) - 2;
             nAnyo  = (1996 - nAnyoCompra) + 1;
             |SI #41#25 < 0;  #41#25 = 0;  |FINSI;

             nCoefi     = 0;
             |SI nAnyo ] 3;
                 |SI #41#13 = "I";  nCoefi = #41#25 * 11.11; |FINSI;
                 |SI #41#13 = "O";  nCoefi = #41#25 * 14.28;  |FINSI;

                 |SI #41#13 = "A";
                                      nCoefi = #41#25 * 25;
                     |SI #41#12 = 4;  nCoefi = #41#25 * 11.11;  |FINSI;
                 |FINSI;
             |FINSI;

             |SI nCoefi > 99; nCoefi = 100; |FINSI;

             #41#39     = #41#24;

             |SI #41#12 ! "6";
                 |SI #41#12 = "4";  |Y #41#13 = "A";
                     |HAZ Calculo411;
                 |SINO;
                     |HAZ FechaLimite;
                 |FINSI;
             |SINO;
                 |HAZ Calculo411;
             |FINSI;

             |SI #41#14 ] "31.12.1994";
                 #41#39 = 0;
             |FINSI;

             |SI #41#39 < 0;  #41#39 = 0;  |FINSI;

             #41#40 = #41#39 % nCoefi;
             #41#26 = #41#24 - #41#40;

             |SI #41#27 > #41#26;
                 #41#27 = #41#26;
             |FINSI;

             #41#28 = #41#26 - #41#27;
             |SI #43#14 = "S";  #41#29 = #41#28;  |FINSI;

             |PARA nCampo = 30;  |SI nCampo [ 33;  |HACIENDO nCampo = nCampo + 1;
                   #41nCampo = 0;
             |SIGUE;
         |SINO;
             #41#31 = #41#24 - #41#30;
             #41#32 = #41#31;
             #41#33 = #41#31 - #41#32;

             |PARA nCampo = 25;  |SI nCampo [ 29;  |HACIENDO nCampo = nCampo + 1;
                   #41nCampo = 0;
             |SIGUE;

             #41#38 = "N";
             #41#39 = 0;
             #41#40 = 0;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO El41;
     #41#0 = #20#0;
     #41#1 = 99;
     |LEE 040#41];
     |SI FSalida = 0;
         |LEE 040#41a;
     |SINO;
         |LEE 040#41u;
     |FINSI;

     |SI FSalida = 0; |Y #41#0 = #20#0;
         nLinea = #41#1 + 1;
     |SINO;
         nLinea = 1;
     |FINSI;

     |DDEFECTO #41;
     #41#0  = #20#0;
     #41#1  = nLinea;
     |PARA nCampo = 2;  |SI nCampo [ 10;  |HACIENDO nCampo = nCampo + 1;
           #41nCampo = #20nCampo;
     |SIGUE;
     #41#3  = "R";
     |SI #20#3 = "X";  #41#3 = "X";  |FINSI;
     #41#11 = aDescri;

                         #41#12 = "5";
     |SI #20#11 = "E1";  #41#12 = "5";  |FINSI;
     |SI #20#11 = "F2";  #41#12 = "5";  |FINSI;
     |SI #20#11 = "G1";  #41#12 = "5";  |FINSI;
     |SI #20#11 = "G3";  #41#12 = "5";  |FINSI;
     |SI #20#11 = "G4";  #41#12 = "5";  |FINSI;
     |SI #20#11 = "H2";  #41#12 = "5";  |FINSI;

     |SI #20#11 = "F1";  #41#12 = "6";  |FINSI;
     |SI #20#11 = "G2";  #41#12 = "6";  |FINSI;
     |SI #20#11 = "H1";  #41#12 = "6";  |FINSI;

     |SI #41#12 = "5";  #41#13 = "O";  |FINSI;
     |SI #41#12 = "6";  #41#13 = "A";  |FINSI;

     #41#14 = fFechaAdq;
     #41#15 = fFechaTra;
     #41#16 = nCampo15;
     #41#17 = nCampo16;
     #41#41 = #43#15;

     |HAZ Total41;

     #41#34 = "C1";
     #41#35 = #20#1;
     |GRABA 020#41n;
     |LIBERA #41;

     eaPrograma   = "rem00041";
     eaPunto      = "  ";
     |HAZ ProgramaActivo;
|FPRC;

|PROCESO Graba59;
     #59#0 = #20#0;
     #59#1 = 99;
     |LEE 040#59];
     |SI FSalida = 0;
         |LEE 040#59a;
     |SINO;
         |LEE 040#59u;
     |FINSI;

     |SI FSalida = 0; |Y #59#0 = #20#0;
         nLinea = #59#1 + 1;
     |SINO;
         nLinea = 1;
     |FINSI;

     |DDEFECTO #59;
     #59#0  = #20#0;
     #59#1  = nLinea;
     |PARA nCampo = 2;  |SI nCampo [ 10;  |HACIENDO nCampo = nCampo + 1;
           #59nCampo = #20nCampo;
     |SIGUE;
     #59#3  = "R";
     |SI #20#3 = "X";  #59#3 = "X";  |FINSI;

     #59#13 = #20#17;
     #59#14 = nCas732;
     #59#15 = nCas733;
     #59#16 = nCas735 + nCas745;
     #59#17 = nCas746;
     #59#18 = nCas736 + nCas747;
     #59#19 = nCas737 + nCas748;

     #59#11 = "C1";
     #59#12 = #20#1;
     |GRABA 020#59n;
     |LIBERA #59;

     eaPrograma   = "rem00059";
     eaPunto      = "  ";
     |HAZ ProgramaActivo;
|FPRC;

|PROCESO Calculo411_59;
     |ABRE #999;
     |SELECT #2#999;
     #999#1 = #20#2;
     |LEE 000#999=;
     |SI FSalida ! 0;  |DDEFECTO #999;  |FINSI;

     |SELECT #1#999;
     |CIERRA #999;

     |SI #999#5 = "S";
         nValor1 = nValor;
         |ACABA;
     |FINSI;

     |SI #43#2 < "20.01.2006";  |Y #43#15 = 0;
         nValor1 = nValor;
         |ACABA;
     |FINSI;

     |SI nValorPat < nCampo15;
         nValor1 = nValorPat - nCampo16;
     |SINO;
         nValor1 = nValor;
     |FINSI;
|FPRC;

|PROCESO Total59;
     aAlfa1      = #43#6 % -104;
     aAlfa3      = #43#6 %  105;
     aAlfa2      = #43#9 % -104;
     nAnyoCompra = aAlfa1;
     nAnyoVenta  = aAlfa2;
     nDiferencia = nAnyoVenta - nAnyoCompra;

     |SI aAlfa3 = "31.12";  nAnyoCompra = nAnyoCompra + 1;  |FINSI;

     nCas732 = nCas732 + nCampo15;
     nCas733 = nCas733 + nCampo16;

     nValor = nCampo15 - nCampo16;
     |SI nDias [ 365;
         |SI nValor ] 0;
             nCas735 = nCas735 + nValor;
         |SINO;
             nValor     = -nValor;
             nCas736    = nCas736 + nValor;
             |SI #43#14 = "S";  nCas737 = nCas737 + nValor;  |FINSI;
         |FINSI;
         nCas746 = nCas735 + nCas745;
     |SINO;
         |SI nValor ] 0;
             nCas745 = nCas745 + nValor;
             nAnyo   = (1996 - nAnyoCompra) + 1;
             nCoefi  = 0;
             |SI nAnyo ] 3;   nCoefi = (nAnyo - 2) * 25;  |FINSI;
             |SI nCoefi > 99; nCoefi = 100;               |FINSI;
             |SI nCoefi [ 100;
                 nValor1 = nValor;
                 |HAZ Calculo411_59;
                 |SI nValor1 < 0;  nValor1 = 0;  |FINSI;

                 |SI #43#6 ] "31.12.1994";
                     nValor1 = 0;
                 |FINSI;

                 nCas746 = nCas746 + (nValor - (nValor1 % nCoefi));
             |FINSI;
         |SINO;
             nValor     = -nValor;
             nCas747    = nCas747 + nValor;
             |SI #43#14 = "S";  nCas748 = nCas748 + nValor;  |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO PuntosG;
     |SI #43#11 = 0;  |ACABA;  |FINSI;

     nSwHay     = 1;
     nDias      = #43#9 - #43#6;
     aDescri    = #20#17;
     fFechaAdq  = #43#6;
     fFechaTra  = #43#9;
     nCampo15   = #43#11 - #43#13;
     nCampo16   = #43#8  + #43#12;
     nValorPat  = #43#15;

     |SI #20#11 ! "G2";
         |HAZ El41;
     |SINO;
         |HAZ Total59;
     |FINSI;
|FPRC;

|DEFBUCLE rem00043;
     #43#1;
     4;
     #20#0, #20#1, "01.01.1900", 01, "V";
     #20#0, #20#1, "31.12.2999", 99, "V";
     be;
     NULL, PuntosG;
|FIN;

|PROCESO Borra41;
     |BORRA 020#41a;
     |LIBERA #41;

     eaPrograma   = "rem00041";
     eaPunto      = "  ";
     |HAZ ProgramaActivo;
|FPRC;

|DEFBUCLE rem00041;
     #41#1;
     34, 35;
     #20#0, 01, "C1", #20#1;
     #20#0, 99, "C1", #20#1;
     be;
     NULL, Borra41;
|FIN;

|PROCESO Borra59;
     |BORRA 020#59a;
     |LIBERA #59;

     eaPrograma   = "rem00059";
     eaPunto      = "  ";
     |HAZ ProgramaActivo;
|FPRC;

|DEFBUCLE rem00059;
     #59#1;
     11, 12;
     #20#0, 01, "C1", #20#1;
     #20#0, 99, "C1", #20#1;
     be;
     NULL, Borra59;
|FIN;

|PROCESO Borra43;
     |BORRA 020#43a;
     |LIBERA #43;
|FPRC;

|DEFBUCLE rem00043B;
     #43#1;
     ;
     #20#0, #20#1, "01.01.1900", 01;
     #20#0, #20#1, "31.12.2999", 99;
     be;
     NULL, Borra43;
|FIN;

|PROCESO Patrimonio;
     |SI #20#3  = "R";           |ACABA;  |FINSI;
     |SI #20#3  = "X";           |ACABA;  |FINSI;
     |SI #20#2  = "1 FAMILIAR";  |ACABA;  |FINSI;
     |SI #20#2  = "2 FAMILIAR";  |ACABA;  |FINSI;
     |SI #20#2  = "3 FAMILIAR";  |ACABA;  |FINSI;
     |SI #20#2  = "4 FAMILIAR";  |ACABA;  |FINSI;
     |SI #20#15 = 0;             |ACABA;  |FINSI;

     |HAZ LimpiaPatrim;

     eaLetra    = #20#11;
     eaDeDonde  = "C0";
     eaLinDonde = #20#1;
     eaAnyoUsu  = #20#14;
     eaPatr06   = #20#17;
     eaPatr16   = #20#13;
     eaPatr10   = #20#15;
     |SI #20#11 = "E1";  eaPatr13 = #20#18;  |FINSI;
     eaPatr20   = #20#5;
     eaPatr21   = #20#6;

     |HAZ GrabaPatrimonio;
|FPRC;

|PROCESO Tipo2m20;  |TIPO 2;
     nModo = (FEntrada / 100) ! 0;

     nEntrada = 0 +. 1;
     |SI nEntrada ! 1;
         |BUCLE rem00041;
         |BUCLE rem00059;

         eaDeDonde  = "C0";
         eaLinDonde = #20#1;
         eaLetra    = #20#11;
         |HAZ BorraPatrimonio;
     |FINSI;

     |SI nModo = 1;  |O nModo = 3;
         |SI nModo = 3;
             |BUCLE rem00043B;
             enSwRetorno  = 1;
             |ACABA;
         |FINSI;
         enSwRetorno  = 1;
     |FINSI;

     |SI nEntrada ! 1;
         |PARA nCampo = 0;  |SI nCampo [ nNumCampos;  |HACIENDO nCampo = nCampo + 1;
               #99nCampo = #20nCampo;
         |SIGUE;
     |SINO;
         |PARA nCampo = 0;  |SI nCampo [ nNumCampos;  |HACIENDO nCampo = nCampo + 1;
               |SI #99nCampo ! #20nCampo;
                   enSwRetorno  = 1;
               |FINSI;
         |SIGUE;

         nCas732 = 0;
         nCas733 = 0;
         nCas735 = 0;
         nCas736 = 0;
         nCas737 = 0;
         nCas745 = 0;
         nCas746 = 0;
         nCas747 = 0;
         nCas748 = 0;
         nSwHay  = 0;

         |ABRE #41;
         |BUCLE rem00043;
         |CIERRA #41;

         |SI #20#11 = "G2";  |Y nSwHay = 1;
             |ABRE #59;
             |HAZ Graba59;
             |CIERRA #59;
         |FINSI;

         |HAZ Patrimonio;
     |FINSI;
|FPRC;

|PROCESO Tipo11m20;  |TIPO 11;
     |SI FSalida = 10;
         |SUB_EJECUTA_NP "rew00551";
         |ERROR;
     |FINSI;

     |SI FSalida = 11;
         |SI #20#2 ! "VARIOS    ";  |ACABA;  |FINSI;

         #900#0 = #20#5;
         #900#1 = #20#6;
         #900#2 = #20#7;
         #900#3 = #20#8;
         #900#4 = #20#9;
         #900#5 = #20#10;

         |PUSHV 0501 2380;
         |PINPA #0#900;
         |PINDA #0#900;
         |LEETECLA aTecla;
         |POPV;
         |ERROR;
     |FINSI;
|FPRC;

|PROGRAMA;
     aQueQuiero = "|NP";
     aNumPanta  = #20#aQueQuiero#;
     nNumPanta  = aNumPanta;
     nNumPanta  = nNumPanta - 1;

     aQueQuiero = "|NC";
     aNumCampos = #20#aQueQuiero#;
     nNumCampos = aNumCampos;
     nNumCampos = nNumCampos - 1;

     |DBASE aPathRenta;  |QBT aPathRenta;
     aDef = aPathRenta + "def/rem00022.mas";
     |CARGA_DEF aDef, espejo@;
     |SI FSalida ! 0;
         aAlfa = "     Error en cargar el Def rem00022.mas";
         |MENAV aAlfa;
         |ERROR;
     |FINSI;

     |ENTLINEAL #1#20, -2, enModo, 22, 1, ClaveBaja20, ClaveAlta20;

     |DESCARGA_DEF #espejo@;

     eaPrograma   = "rem00022";
     eaPunto      = "  ";
     |HAZ ProgramaActivo;
|FPRO;
