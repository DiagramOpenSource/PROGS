|FICHEROS;
     rem00028 #20;
     rem00029 #29,MANTE;
     rem00058 #58;
     rew99998 #900,MANTE;
     rew00608 #1003,MANTE;
     rem00500 #500;
     retabla1 #401;
|FIN;

|VARIABLES;
     aPathRenta    = "";
     aDef          = "";
     aAlfa         = "";
     aAlfa1        = "";
     aEscape       = "";
     aAvance       = "";
     aRetroceso    = "";
     aQueQuiero    = "";
     aNumCampos    = "";
     aNumPanta     = "";
     aLong         = "";
     aTecla        = "";

     nPanta        = 0;
     nNumCampos    = 0;
     nNumPanta     = 0;
     nCampos       = 0;
     nModo         = 0;
     nEntrada      = 0;
     nFactor       = 0;
     nLong         = 0;
     nCaracter     = 0;
     nPosi         = 0;
     nCampo        = 0;
     nPorce        = 0;
     nSwPanta      = 0;
     nCanti        = 0;
     nCanti1       = 0;
     nCanti2       = 0;
     nSwBorra      = 0;
     nDesde        = 0;
     nValor        = 0;
     nTipoLin      = 0;
     nImporte1     = 0;
     nImporte2     = 0;
     nTecla        = 0;
     nLinea        = 0;
     nContenido    = 0;

     &enEmpresa    = 0;
     &enModo       = 0;
     &eaDeclarante = "";
     &enComunes    = 0;
     &enMinusvalia = 0;
     &eaMinuAyuda  = "";
     &eaFallece    = "";
     &eaSwVarios   = "";
     &eaVarDni     = "";
     &enSwLiquida  = 0;
     &enSwRetorno  = 0;
     &eaPrograma   = "";
     &eaPunto      = "";
     &enNomeAgr    = 0;
     &enCultivo    = 0;
     &eaTipo       = "";
     &enTipo       = 0;

     &eaLetra      = "";
     &eaDeDonde    = "";
     &eaLinDonde   = "";
     &eaAnyoUsu    = "";
     &eaEdadDec    = "";
     &eaPatr06     = "";
     &eaPatr07     = "";
     &eaPatr08     = "";
     &eaPatr09     = "";
     &eaPatr10     = "";
     &eaPatr11     = "";
     &eaPatr12     = "";
     &eaPatr13     = "";
     &eaPatr14     = "";
     &eaPatr15     = "";
     &eaPatr16     = "";
     &eaPatr17     = "";
     &eaPatr18     = "";
     &eaPatr19     = "";
     &eaPatr20     = "";
     &eaPatr21     = "";
|FIN;

|| ************************************************************************
|| PROCESOS DE CONSULTA DE PANTALLAS NORMALES
|| ************************************************************************

|PROCESO Tipo0C65;  |TIPO 0;
     |C_M #1003#66, 1, "S";
     |SI #1003#65 ! "S";
         |C_M #1003#66, 1, "N";  #1003#66 = 0;
     |FINSI;

     |C_M #1003#67, 1, "N";
     |SI #1003#65 = "F";
         |C_M #1003#67, 1, "S";
     |FINSI;

     |PINTA #1003#66;
     |PINTA #1003#67;
|FPRC;

|PROCESO Totalw1003;
     #1003#30 = #1003#26 + #1003#27;
     #1003#34 = #1003#30 - #1003#58 - #1003#33;

     |SI #1003#34 < 0;
         #1003#35 = 0;   |PINTA #1003#35;
         #1003#36 = 0;   |PINTA #1003#36;
         #1003#37 = 0;   |PINTA #1003#37;
         #1003#38 = 0;   |PINTA #1003#38;
         #1003#54 = 0;   |PINTA #1003#54;
         #1003#55 = 0;   |PINTA #1003#55;
     |FINSI;

     nCanti1 = #1003#26 - #1003#59 - #1003#31;
     |SI #1003#35 ! 0;  nCanti1 = nCanti1 * #1003#35;  |FINSI;
     |SI #1003#36 ! 0;  nCanti1 = nCanti1 * #1003#36;  |FINSI;
     |SI #1003#38 ! 0;  nCanti1 = nCanti1 * #1003#38;  |FINSI;
     |SI #1003#54 ! 0;  nCanti1 = nCanti1 * #1003#54;  |FINSI;

     nCanti2 = #1003#27 - #1003#60 - #1003#32;
     |SI #1003#35 ! 0;  nCanti2 = nCanti2 * #1003#35;  |FINSI;
     |SI #1003#36 ! 0;  nCanti2 = nCanti2 * #1003#36;  |FINSI;
     |SI #1003#37 ! 0;  nCanti2 = nCanti2 * #1003#37;  |FINSI;
     |SI #1003#38 ! 0;  nCanti2 = nCanti2 * #1003#38;  |FINSI;
     |SI #1003#54 ! 0;  nCanti2 = nCanti2 * #1003#54;  |FINSI;

     #1003#39 = 0;
     |SI #1003#17 ! "2";  |Y  #1003#17 ! "6";  |Y  #1003#17 ! "8";
         |SI #1003#34 ] 0;
             |SI #1003#34 [ #401#38;
                 |SI #1003#47 = 0;
                     #1003#39 = 0.9;
                 |FINSI;
             |FINSI;
         |FINSI;
     |FINSI;

     |PINTA #1003#39;

     |SI #1003#39 ! 0;  nCanti1 = nCanti1 * #1003#39;  |FINSI;
     |SI #1003#55 ! 0;  nCanti1 = nCanti1 * #1003#55;  |FINSI;

     |SI #1003#39 ! 0;  nCanti2 = nCanti2 * #1003#39;  |FINSI;
     |SI #1003#55 ! 0;  nCanti2 = nCanti2 * #1003#55;  |FINSI;

     |SI nTipoLin = 0;
         |C_M #1003#36, 1, "S";
         |SI #1003#35 ! 0;  #1003#36 = 0;  |C_M #1003#36, 1, "N";  |PINTA #1003#36;  |FINSI;

         |C_M #1003#35, 1, "S";
         |SI #1003#36 ! 0;  #1003#35 = 0;  |C_M #1003#35, 1, "N";  |PINTA #1003#35;  |FINSI;

         |C_M #1003#37, 1, "S";
         |SI #1003#27 = 0;  #1003#37 = 0;  |C_M #1003#37, 1, "N";  |PINTA #1003#37;  |FINSI;

         |SI #1003#47 > 0;  |O #1003#47 = -1;
             #1003#39 = 0;   |PINTA #1003#39;
         |FINSI;
     |FINSI;

     #1003#40 = nCanti1 + nCanti2;
     #1003#57 = 0;

     |C_M #1003#64, 1, "N";

     |SI #1003#40 > 0;
         #1003#64 =  5;
         #1003#57 = #1003#40 % #1003#64;
     |SINO;
          #1003#64 = 0;
     |FINSI;

     #1003#44 = #1003#40 - #1003#57;
     #1003#48 = 0;

     |SI #1003#47 > 0;
         #1003#48 = #1003#44 % #1003#47;
     |FINSI;

     |SI #1003#48 < 0;  #1003#48 = -#1003#48;  |FINSI;

     #1003#49 = #1003#44 - #1003#48 - #1003#43;

     #1003#51 = 0;
     |SI #1003#50 = "S";
         #1003#51 = #1003#49 % 40;
     |FINSI;

     #1003#52 = #1003#49 - #1003#51;

     |SI #1003#65 = "N";
         #1003#67 = 0;
     |FINSI;

     |SI #1003#65 = "F";
         |SI #1003#67 = 0;
             |SI #1003#52 > 0;
                 #1003#67 = #1003#52 % 20;
             |FINSI;
         |FINSI;
     |FINSI;

     |SI #1003#65 = "S";
         nImporte1 = 0;
         |SI #1003#52 > 0;
             nImporte1 = (#1003#52 % 20) ! 2;
         |FINSI;
         nImporte2 = (#1003#66 / 2) ! 2;
         |SI nImporte1 > nImporte2;
             nImporte1 = nImporte2;
         |FINSI;

         #1003#67 = nImporte1;
     |FINSI;

     |SI nSwPanta = 1;
         |PINTA #1003#25;
         |PINTA #1003#30;
         |PINTA #1003#34;
         |PINTA #1003#40;
         |PINTA #1003#44;
         |PINTA #1003#49;
         |PINTA #1003#52;
     |SINO;
         |ATRI I;
         |PINTA 1040, #1003#30;
         |PINTA 1440, #1003#34;
         |PINTA 2240, #1003#40;
         |PINTA 0970, #1003#44;
         |PINTA 1370, #1003#49;
         |PINTA 1670, #1003#52;

         |PINTA #1003#51;
         |PINTA #1003#57;
         |PINTA #1003#48;
         |PINTA #1003#64;
         |PINTA #1003#67;

         |ATRI i;
     |FINSI;
|FPRC;

|PROCESO TotalLin;
     #29#7  = #29#6 * #29#5;
|FPRC;

|PROCESO Consulta;  |TIPO 0;
     nContenido = Contenido;
     |SI nContenido ! #29#3;
         #29#5 = 0;  |PINTA #20#5;
     |FINSI;

     nTecla = FSalida;
     eaTipo = "";

     |SI nTecla = 10;  |O nTecla = 11;
         |PUSHV 0501 2380;
         |BLANCO 1434 2155;
         |CUADRO 1434 2155;

         |ATRI S;
         |PINTA 1535, "   Tipo Producto    ";
         |ATRI s;

         |ATRI I;
         |PINTA 1635, "<A> Agrario         ";
         |PINTA 1735, "<G> Ganaderos       ";
         |PINTA 1835, "<F> Forestales      ";
         |ATRI i;

         E_inf  = "AGFagf";
         E_sup  = "1";
         eaTipo = 2045 ? 1;
         eaTipo = eaTipo > " ";

         |POPV;
     |FINSI;

     |SI nTecla = 10;
         enTipo    = 1;
         enCultivo = #29#3;
         |SUB_EJECUTA_NP "rem00500";

         #29#3 = enCultivo;
         |PINTA #29#3;

         |ERROR;
         |ACABA;
     |FINSI;

     |SI nTecla = 11;
         enTipo    = 2;
         enCultivo = #29#3;
         |SUB_EJECUTA_NP "rem00500";

         #29#3 = enCultivo;
         |PINTA #29#3;

         |ERROR;
         |ACABA;
     |FINSI;

     |SI nTecla = 12;
         eaTipo    = "";
         enTipo    = 3;
         enCultivo = #29#3;
         |SUB_EJECUTA_NP "rem00500";

         #29#3 = enCultivo;
         |PINTA #29#3;

         |ERROR;
         |ACABA;
     |FINSI;

     |ABRE #500;
     #500#0 = #29#3;
     |LEE 040#500=;
     |SI FSalida ! 0;
         |CIERRA #500;
         |MENAV "0000 No existe el Producto";
         |ERROR;
         |ACABA;
     |FINSI;
     |CIERRA #500;

     |SI #29#5 = 0;
         #29#5 = #500#5;
     |FINSI;

     #29#9 = #500#8;

     |C_M #29#8, 1, "S";
     |SI #500#6 ! "A";
         #29#8 = "N";  |PINTA #29#8;
         |C_M #29#8, 1, "N";
     |FINSI;
|FPRC;

|PROCESO Arrendamiento;
     |ABRE #500;
     #500#0 = #29#3;
     |LEE 040#500=;
     |SI FSalida ! 0;  |DDEFECTO #500;  |FINSI;
     |CIERRA #500;

     |SI #29#8 = "S";
         #29#9 = #500#8 + 1;
     |SINO;
         #29#9 = #500#8;
     |FINSI;
|FPRC;

|PROCESO Tipo2Lin;  |TIPO 2;
     #1003#25 = #1003#25 +. #29#6;

     |SI #1003#25 > 300000;
         |MENAV "0000Los ingresos superan los 300.0000 Euros. No puede estar en este regimen";
     |FINSI;

     |SI #29#8 = "N";
         #1003#26 = #1003#26 +. #29#7;
     |SINO;
         #1003#27 = #1003#27 +. #29#7;
     |FINSI;

     nTipoLin = 1;
     |HAZ Totalw1003;
     nTipoLin = 0;
|FPRC;

|PROCESO PonPorce;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     FSalida = 0;
     |SI Campo = 36;
         |PUSHV 0501 2380;
         |CUADRO 1241 1473;
         |BLANCO 1342 1372;
         |PINTA 1342, "Importe Asalariado :           ";
         |C_P #1003#53, 1, 1363;  |C_V #1003#53, 1, "S";  |C_M #1003#53, 1, "S";
         |ENCAMPO #53#1003;
                               |C_V #1003#53, 1, "N";  |C_M #1003#53, 1, "N";

         |POPV;

         nPorce = (#1003#53 / #1003#25) * 100;

         #1003#36 = 1;
         |SI nPorce ] 10; #1003#36 = 0.90; |FINSI;
         |SI nPorce > 20; #1003#36 = 0.85; |FINSI;
         |SI nPorce > 30; #1003#36 = 0.80; |FINSI;
         |SI nPorce > 40; #1003#36 = 0.75; |FINSI;

         |PINTA #1003#36;
     |FINSI;

     |SI Campo = 35;  #1003#35 = 0.75;  |PINTA #1003#35;  |FINSI;
     |SI Campo = 37;  #1003#37 = 0.90;  |PINTA #1003#37;  |FINSI;
     |SI Campo = 38;  #1003#38 = 0.65;  |PINTA #1003#38;  |FINSI;
     |SI Campo = 47;  #1003#47 = 25;    |PINTA #1003#47;  |FINSI;
     |SI Campo = 54;  #1003#54 = .95;   |PINTA #1003#54;  |FINSI;
     |SI Campo = 55;  #1003#55 = .80;   |PINTA #1003#55;  |FINSI;
|FPRC;

|PROCESO TotalAmort;
     |SI FSalida = 10;
         |PUSHV 0501 2380;
         |CUADRO 1241 1573;
         |BLANCO 1342 1472;
         |PINTA 1342, "Amort.T.Propias ...:           ";
         |PINTA 1442, "Amort.T.Arrendadas :           ";
         |C_P #1003#31, 1, 1363;  |C_V #1003#31, 1, "S";  |C_M #1003#31, 1, "S";
         |C_P #1003#32, 1, 1463;  |C_V #1003#32, 1, "S";  |C_M #1003#32, 1, "S";

         |PINTA #1003#31;
         |PINTA #1003#32;

         |ET Campo31;
         |ENCAMPO #31#1003;

         |ENCAMPO #32#1003;
         |SI FSalida = 2;  |VETE Campo31;  |FINSI;

                               |C_V #1003#31, 1, "N";  |C_M #1003#31, 1, "N";
                               |C_V #1003#32, 1, "N";  |C_M #1003#32, 1, "N";
         |POPV;
     |FINSI;

     #1003#33 = #1003#31 + #1003#32;
     |PINTA #1003#33;
|FPRC;

|PROCESO TotalGasoleo;
/*
     |SI FSalida = 10;
         |PUSHV 0501 2380;
         |CUADRO 1241 1573;
         |BLANCO 1342 1472;
         |PINTA 1342, "Gasol.T.Propias ...:           ";
         |PINTA 1442, "Gasol.T.Arrendadas :           ";
         |C_P #1003#59, 1, 1363;  |C_V #1003#59, 1, "S";  |C_M #1003#59, 1, "S";
         |C_P #1003#60, 1, 1463;  |C_V #1003#60, 1, "S";  |C_M #1003#60, 1, "S";

         |PINTA #1003#59;
         |PINTA #1003#60;

         |ET Campo59;
         |ENCAMPO #59#1003;

         |ENCAMPO #60#1003;
         |SI FSalida = 2;  |VETE Campo59;  |FINSI;

                               |C_V #1003#59, 1, "N";  |C_M #1003#59, 1, "N";
                               |C_V #1003#60, 1, "N";  |C_M #1003#60, 1, "N";
         |POPV;
     |FINSI;

     #1003#58 = #1003#59 + #1003#60;
     |PINTA #1003#58;
*/
|FPRC;

|PROCESO CogeLineal;
     |SI nSwBorra = 1;
         |BORRA 020#29a;
         |LIBERA #29;
         |ACABA;
     |FINSI;

     #1003#25 = #1003#25 + #29#6;

     |SI #29#8 = "N";
         #1003#26 = #1003#26 + #29#7;
     |SINO;
         #1003#27 = #1003#27 + #29#7;
     |FINSI;
|FPRC;

|DEFBUCLE rem00029;
     #29#1;
     ;
     #20#0, #20#1, 00;
     #20#0, #20#1, 99;
     be;
     NULL, CogeLineal;
|FIN;

|RUTINA ClaveBaja29;
     #29#0 = #20#0;
     #29#1 = #20#1;
     #29#2 = 1;
|FRUT;

|RUTINA ClaveAlta29;
     #29#0 = #20#0;
     #29#1 = #20#1;
     #29#2 = 99;
|FRUT;

|PROCESO Tipo7m20;  |TIPO 7;
     nSwPanta = 1;
     |ENTLINEAL #1#29, 3, 7, 22, 1, ClaveBaja29, ClaveAlta29;
|FPRC;

|PROCESO Tipo14P2; |TIPO 14;
     nSwPanta = 2;
|FPRC;

|PROCESO Tipo7P1;  |TIPO 7;
     |PINPA #0#1003;
     |PINDA #0#1003;
     |HAZ Tipo7m20;
|FPRC;

|PROCESO Tipo0P1; |TIPO 0;
     |SI FSalida = 2;  |ACABA;  |FINSI;

     |ENTLINEAL #1#29, -3, 2, 22, 1, ClaveBaja29, ClaveAlta29;

     |SI #1003#25 > 300000;
         |MENAV "0000Los ingresos superan los 300.0000 Euros. No puede estar en este regimen";
     |FINSI;

     #1003#25 = 0;
     #1003#26 = 0;
     #1003#27 = 0;
     nSwBorra = 0;
     |BUCLE rem00029;

     |HAZ Tipo7m20;
|FPRC;

|PROCESO PintaPantalla;
     |SI nPanta < 0;   nPanta = 0;                         |FINSI;
     |SI nPanta = 0;   |PINPA #00#1003;  |PINDA #00#1003;  |FINSI;
     |SI nPanta = 1;   |PINPA #01#1003;  |PINDA #01#1003;  |FINSI;
|FPRC;

|PROCESO MensaPorce;  |TIPO 7;
     |SI #1003#14 = "N";
         #1003#15 = 100;   |PINTA #1003#15;
         #1003#20 = "";    |PINTA #1003#20;
         |C_M #1003#15, 1, "N";
         |C_M #1003#20, 1, "N";
         |ACABA;
     |FINSI;

     |C_M #1003#15, 1, "S";
     |C_M #1003#20, 1, "S";

     |PUSHV 0501 2380;
     |CUADRO 0937 1480;
     |BLANCO 1038 1379;
     |ATRI R;
     |PINTA 1038, "Las cantidades se introduciran al 100 %,  ";
     |PINTA 1138, "luego el calculo las computara al porcen- ";
     |PINTA 1238, "taje de comunero que le hayamos informado.";
     |ATRI r;
     |LEETECLA aAlfa;
     |POPV;
|FPRC;

|PROCESO CuadroClave;  |TIPO 0;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     |PUSHV 0501 2380;
     |CUADRO 0819 2275;
     |BLANCO 0920 2174;

     |ATRI R;
     |PINTA 0920, "                CLAVES AGRICOLAS                  ";
     |ATRI r;

     |ATRI S;
     |PINTA 1120, "1.- Agricola o ganadera R.E. de Agr.Ganad.y Pesca ";
     |PINTA 1220, "2.- Forestal R.E.de Agricultura,Ganaderia y Pesca ";
     |PINTA 1320, "3.- Ganaderia independiente en la division 0 IAE  ";
     |PINTA 1420, "4.- Servicio de cria, guarda y engorde de ganado  ";
     |PINTA 1520, "5.- Otros trabajos o servicios excluido del 1.    ";
     |PINTA 1620, "6.- Otros trabajos o servicios excluido del 2.    ";
     |PINTA 1720, "7.- Aprovechamiento del cedente Actividad Agricola";
     |PINTA 1820, "8.- Aprovechamiento del cedente Actividad Forestal";
     |PINTA 1920, "9.- Procesos de Transformacion.                   ";
     |ATRI s;

     |LEETECLA aTecla;
     |POPV;

     |ERROR;
|FPRC;

|PROCESO Varios;  |TIPO 0;
     |SI #20#3 = "X";  |ACABA;  |FINSI;

     |SI #20#2 ! "VARIOS    ";
         #20#5  = 0;
         #20#6  = 0;
         #20#7  = 0;
         #20#8  = 0;
         #20#9  = 0;
         #20#10 = 0;
         |ACABA;
     |FINSI;

     |C_M #900#0,  1, "N";
     |C_M #900#1,  1, "N";
     |C_M #900#2,  1, "N";
     |C_M #900#3,  1, "N";
     |C_M #900#4,  1, "N";
     |C_M #900#5,  1, "N";

     aAlfa = eaSwVarios;
     aLong = aAlfa % 0;
     nLong = aLong;
     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPosi = (100 * nCaracter) + 1;
           aAlfa1 = aAlfa % nPosi;
           |SI aAlfa1 = "T";  |C_M #900#0,  1, "S";  |FINSI;
           |SI aAlfa1 = "C";  |C_M #900#1,  1, "S";  |FINSI;
           |SI aAlfa1 = "1";  |C_M #900#2,  1, "S";  |FINSI;
           |SI aAlfa1 = "2";  |C_M #900#3,  1, "S";  |FINSI;
           |SI aAlfa1 = "3";  |C_M #900#4,  1, "S";  |FINSI;
           |SI aAlfa1 = "4";  |C_M #900#5,  1, "S";  |FINSI;
     |SIGUE;

     #900#0 = #20#5;
     #900#1 = #20#6;
     #900#2 = #20#7;
     #900#3 = #20#8;
     #900#4 = #20#9;
     #900#5 = #20#10;

     |PUSHV 0501 2380;

     |PINPA #0#900;
     |PINDA #0#900;
     |ENDATOS #1#900;
     |SI FSalida = 0;
         #20#5  = #900#0;
         #20#6  = #900#1;
         #20#7  = #900#2;
         #20#8  = #900#3;
         #20#9  = #900#4;
         #20#10 = #900#5;
     |FINSI;

     |POPV;
|FPRC;

|PROCESO LetraNif;  |TIPO 6;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     eaVarDni = #1003Campo;
     |HAZ CalculoDNI;
     #1003Campo = eaVarDni;
     |PINTA #1003Campo;
     |ERROR;
|FPRC;

|PROCESO CampoNif;  |TIPO 0;
     |SI #1003#14 = "N";  |ACABA;  |FINSI;

     |SI #1003#20 = "            ";
         |MENAV "     Introduzca un Nif.";
         |ERROR;
         |ACABA;
     |FINSI;

     aLong = #1003#20;  |QBF aLong;
     aLong = aLong % 0;
     nLong = aLong;
     |SI nLong ! 9;
         |MENAV "     Longitud Erronea.";
         |ERROR;
         |ACABA;
     |FINSI;

     aAlfa = #1003#20 % 101;
     |SI aAlfa ! "E";  |Y aAlfa ! "H";  |Y aAlfa ! "J";
         |MENAV "     El Nif  debe empezar por E, H o J";
         |ERROR;
         |ACABA;
     |FINSI;
|FPRC;

|PROCESO Fantasma;
     |SI #20#3 = "X";  |Y nModo = 1;
         |ACABA;
     |FINSI;

     |PARA nCampos = nDesde;  |SI nCampos [ nNumCampos;  |HACIENDO nCampos = nCampos + 1;
           #1003nCampos = #20nCampos;
     |SIGUE;

     aEscape    = &255 + "806";
     aAvance    = &255 + "815";
     aRetroceso = &255 + "814";

     |SI nNumPanta ] 1;
         |MENSA "      Av.Pag Pantalla Siguiente. Re.Pag Pantalla Anterior. ESC Salir.";
     |SINO;
         |MENSA "      ESC Salir.";
     |FINSI;

     |PUSHV 0401 2380;
     |SI nModo ! 4;
         |PINPA #0#1003;
         |PINDA #0#1003;
         |ENDATOS #1#1003;
         |SI FSalida = 0;
             |PARA nCampos = nDesde;  |SI nCampos [ nNumCampos;  |HACIENDO nCampos = nCampos + 1;
                   |SI enSwLiquida = 1;
                       |SI #20nCampos ! #1003nCampos;
                           enSwRetorno = 1;
                       |FINSI;
                   |FINSI;
                   #20nCampos = #1003nCampos;
             |SIGUE;
         |FINSI;
     |SINO;
         |PINPA #0#1003;
         |PINDA #0#1003;
         nPanta = 0;
         |PARA;  |SI;  |HACIENDO;
              |LEETECLA aAlfa;
              |SI aAlfa = aEscape;  |SAL;  |FINSI;
              |SI aAlfa = aAvance;
                  |SI nPanta < nNumPanta;
                      nPanta = nPanta + 1;
                      |HAZ PintaPantalla;
                  |FINSI;
              |FINSI;

              |SI aAlfa = aRetroceso;
                  |SI nPanta [ nNumPanta;
                      nPanta = nPanta - 1;
                      |HAZ PintaPantalla;
                  |FINSI;
              |FINSI;
         |SIGUE;
     |FINSI;
     |POPV;
|FPRC;

|RUTINA ClaveBaja20;
     #20#0 = enEmpresa;
     #20#1 = 1;
|FRUT;

|RUTINA ClaveAlta20;
     #20#0 = enEmpresa;
     #20#1 = 99;
|FRUT;

|PROCESO PonDeclarante;  |TIPO 7;
     eaDeclarante = #20#2;
     enComunes    = 1;
     |HAZ SacaDeclarantes;
     #20#2 = eaDeclarante;
     |PINTA #20#2;
|FPRC;

|PROCESO Fallece;  |TIPO 7;
     |SI #20#3 = "X";
         |C_M #20#4, 1, "N";
         |ACABA;
     |FINSI;

     |SI eaFallece = "S";
         |C_M #20#4, 1, "S";
     |SINO;
         |C_M #20#4, 1, "N";  #20#4 = 0;  |PINTA #20#4;
     |FINSI;
|FPRC;

|PROCESO Patrimonio;
     |SI #20#3  = "R";           |ACABA;  |FINSI;
     |SI #20#3  = "X";           |ACABA;  |FINSI;
     |SI #20#2  = "1 FAMILIAR";  |ACABA;  |FINSI;
     |SI #20#2  = "2 FAMILIAR";  |ACABA;  |FINSI;
     |SI #20#2  = "3 FAMILIAR";  |ACABA;  |FINSI;
     |SI #20#2  = "4 FAMILIAR";  |ACABA;  |FINSI;
     |SI #20#11 = 0;             |ACABA;  |FINSI;

     |HAZ LimpiaPatrim;

     eaLetra    = "C1";
     eaDeDonde  = "D4";
     eaLinDonde = #20#1;
     eaAnyoUsu  = 0;
     eaPatr06   = #20#24;              || Domicilio
     eaPatr08   = #20#22;              || Codigo Postal 1
     eaPatr09   = #20#23;              || Codigo Postal 2
     eaPatr15   = #20#21;              || Actividad
     eaPatr18   = #20#19;              || Epigrafe
     nValor     = #20#11 % #20#15;     || Valor Patrimonial
     eaPatr10   = nValor;              || Valor Patrimonial
     eaPatr20   = #20#5;
     eaPatr21   = #20#6;

     |HAZ GrabaPatrimonio;
|FPRC;

|PROCESO Graba58;
     |ABRE #58;
     |SELECT #1#58;
     #58#0 = #20#0;
     #58#1 = 99;
     |LEE 040#58];
     |SI FSalida = 0;
         |LEE 040#58a;
     |SINO;
         |LEE 040#58u;
     |FINSI;
     nLinea = 1;

     |SI FSalida = 0;  |Y #58#0 = #20#0;
         nLinea = #58#1 + 1;
     |FINSI;

     |DDEFECTO #58;
     #58#0  = #20#0;
     #58#1  = nLinea;
     #58#2  = #20#2;
     #58#3  = "R";
     |SI #20#3 = "X";  #58#3 = "X";  |FINSI;
     #58#4  = #20#4;
     #58#5  = #20#5;
     #58#6  = #20#6;
     #58#7  = #20#7;
     #58#8  = #20#8;
     #58#9  = #20#9;
     #58#10 = #20#10;
     #58#11 = #20#15;
     #58#12 = #20#20;
     #58#13 = 3;
     #58#14 = "E4";
     #58#15 = #20#1;
     #58#22 = #20#49 % #58#11;
     #58#23 = #20#51 % #58#11;
     #58#24 = #58#22 - #58#23;
     |GRABA 020#58n;
     |LIBERA #58;

     |CIERRA #58;

     eaPrograma   = "rem00058";
     eaPunto      = "  ";
     |HAZ ProgramaActivo;
|FPRC;

|PROCESO Tipo2m20;  |TIPO 2;
     nModo    = (FEntrada / 100) ! 1;

     nEntrada = 0 +. 1;
     |SI nEntrada ! 1;
         eaDeDonde  = "D4";
         eaLinDonde = #20#1;
         eaLetra    = "C1";
         |HAZ BorraPatrimonio;

         |ABRE #58;
         |SELECT #2#58;
         #58#0  = #20#0;
         #58#14 = "E4";
         #58#15 = #20#1;
         |LEE 101#58=;
         |SI FSalida = 0;
             |BORRA 020#58a;
             |LIBERA #58;

             eaPrograma   = "rem00058";
             eaPunto      = "  ";
             |HAZ ProgramaActivo;
         |FINSI;
         |CIERRA #58;
     |FINSI;

     |SI nModo = 3;
         nSwBorra = 1;
         |BUCLE rem00029;
         enSwRetorno = 1;
         |ACABA;
     |FINSI;

     |SI nModo = 1;  enSwRetorno  = 1;           |FINSI;

     |SI nEntrada = 1;
         nDesde = 5;
         |SI nModo = 1;  nDesde = 0;  |FINSI;
         nModo     = 1;

         |HAZ Fantasma;

         |SI enSwLiquida = 1;
             |PARA nCampos = 0;  |SI nCampos [ 4;  |HACIENDO nCampos = nCampos + 1;
                   |SI #20nCampos ! #1003nCampos;
                       enSwRetorno = 1;
                   |FINSI;
             |SIGUE;
         |FINSI;

         |HAZ Patrimonio;

         |SI #20#14 = "S";  |HAZ Graba58;  |FINSI;
     |SINO;
         |PARA nCampos = 0;  |SI nCampos [ 4;  |HACIENDO nCampos = nCampos + 1;
               #1003nCampos = #20nCampos;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO Tipo11m20;  |TIPO 11;
     |SI FSalida = 10;
         nModo  = 4;  nDesde = 0;  |HAZ Fantasma;
         |ERROR;
     |FINSI;

     |SI FSalida = 11;
         |SI #20#2 ! "VARIOS    ";  |ACABA;  |FINSI;

         #900#0 = #20#5;
         #900#1 = #20#6;
         #900#2 = #20#7;
         #900#3 = #20#8;
         #900#4 = #20#9;
         #900#5 = #20#10;

         |PUSHV 0501 2380;
         |PINPA #0#900;
         |PINDA #0#900;
         |LEETECLA aTecla;
         |POPV;
         |ERROR;
     |FINSI;
|FPRC;

|PROGRAMA;
     |ABRE #401;
     |LEE 040#401p;
     |SI FSalida ! 0;  |DDEFECTO #401;  |FINSI;
     |CIERRA #401;

     |SI enNomeAgr = 1;
         |NOME_DAT #20 "zzm00028";
         |NOME_DAT #29 "zzm00029";
         |NOME_DAT #58 "zzm00058";
     |FINSI;

     aQueQuiero = "|NP";
     aNumPanta  = #1003#aQueQuiero#;
     nNumPanta  = aNumPanta;
     nNumPanta  = nNumPanta - 1;

     aQueQuiero = "|NC";
     aNumCampos = #1003#aQueQuiero#;
     nNumCampos = aNumCampos;
     nNumCampos = nNumCampos - 1;

     |ENTLINEAL #1#20, -2, enModo, 22, 1, ClaveBaja20, ClaveAlta20;

     eaPrograma   = "rem00028";
     eaPunto      = "  ";
     |HAZ ProgramaActivo;
|FPRO;
