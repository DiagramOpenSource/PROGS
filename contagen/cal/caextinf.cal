|FICHEROS;
   caextinf #0;

   caextras #1;
   caextral #3;

   caexttip #2;
   caextm01 #11;
   caextm02 #12;

   canempre #30;
   caselem1 #998;
|FIN;

|VARIABLES;
     ||DSARCHI
     &nArchi = 0;
 {-1}aMenu  = "";
     aMenu1 = "2400";
     aMenu2 = "1";
     aMenu3 = "Archivo documental activado. Elija opci¢n: ";
     aMenu4 = "IAB";
     aMenu5 = "Imprimir";
     aMenu6 = "Archivar";
     aMenu7 = "amBos";
     &nOpcion = 0;
     &aImpresora = "";
     &swPasada = 0;
     &swErrorDSArchi = 0;
     &eaInforme = "";
     &eaVengoDe = "";
     &enSwDesde9 = 1;
     &enSwMeteGrupoSubTipo = 0;
     &eaObservaciones = "";
     nOpSegur = 0;
     SwCierre = 0;
     SwApertura = 0;

   nSalir   = 0;
   &entrada  = 1;

   aAlfa     = "";
   aAlfa1    = "";
   aAlfa2    = "";

   nDTipo    = 0;
   nHTipo    = 0;
   aDEstado  = "";
   aHEstado  = "";
   seleccio  = 0;
   nSw       = 0;

   informe   = "caextm01";
   &swprim   = 0;
   nSwP      = 0;
   &nSwSalto = 0;
   nDiario   = 0;
   fFecha    = @;
   nDocum    = 0;
   aCuenta   = "";

   aNombrePDF    = "";
   nSwError      = 0;
   nCalc         = 0;
   aIP           = "";
   &enHayInforme = 0;
{1}aContiene     = "";
|FIN;

|INCLUYE dscont_i;
|INCLUYE masivo_i;
|INCLUYE promas_i;

|CALCULO fechas; |TIPO 7;
   #0#2  = fec1;         |PINTA #0#2;
   #0#3  = fec2;         |PINTA #0#3;
   #0#13 = fec1;         |PINTA #0#13;
   #0#14 = fec2;         |PINTA #0#14;
|FCAL;

|CALCULO ElTipo;
   aAlfa1 = "S";
   aAlfa2 = "S";
   |SI #0#15 = "G";
       aAlfa1 = "N";
       aAlfa2 = "N";
   |FINSI;
   |SI #0#15 = "C";
       aAlfa2 = "N";
   |FINSI;
   |SI #0#15 = "A";
       aAlfa1 = "N";
   |FINSI;
   |C_M #0#9,  1, aAlfa1;
   |C_M #0#10, 1, aAlfa1;
   |C_M #0#0,  1, aAlfa2;
   |C_M #0#1,  1, aAlfa2;
   |C_M #0#2,  1, aAlfa2;
   |C_M #0#3,  1, aAlfa2;
   |C_M #0#4,  1, aAlfa2;
   |C_M #0#5,  1, aAlfa2;
|FCAL;

|| ////////////////////////////////////////////////////////////////////
|PROCESO imprlin;
   |PRINT;
   swprim = 1;
|FPRC;

|DEFBUCLE LasLineas;
   #12#1;
   ;
   #11#13, #11#0, 0001;
   #11#13, #11#0, 9999;
   e;
   NULL, imprlin;
|FIN;

|PROCESO SelTipo;
   |DDEFECTO #2;
   #2#0 = #11#1;
   |LEE 001#2=;
   |SI FSalida ! 0;
       nSw = 1;
   |SINO;
       |SI #0#16 ! "T"; |Y #2#3 ! #0#16;
           nSw = 1;
       |FINSI;
   |FINSI;
|FPRC;

|PROCESO imprime;
   nSw = 0;
   |HAZ SelTipo;
   |SI nSw = 1; |ACABA; |FINSI;

   |SI #11#2 = 2;
       |SI #11#6 < #0#9;  nSw = 1; |FINSI;
       |SI #11#6 > #0#10; nSw = 1; |FINSI;
   |FINSI;
   |SI #11#2 = 3;
       |SI #11#3 < #0#0; |O #11#3 > #0#1;  nSw = 1; |FINSI;
       |SI #11#4 < #0#2; |O #11#4 > #0#3;  nSw = 1; |FINSI;
       |SI #11#5 < #0#4; |O #11#5 > #0#5;  nSw = 1; |FINSI;
   |FINSI;
   |SI nSw = 1; |ACABA; |FINSI;

   |SI #11#2 ! 1;
       |SI nSwP = 0;
           aCuenta = #11#6;
           nDiario = #11#3;
           fFecha  = #11#4;
           nDocum  = #11#5;
       |FINSI;
       nSwP = 1;
   |FINSI;
   |SI #11#2 = 2;
       |SI aCuenta ! #11#6; nSwSalto = 1; |FINSI;
   |FINSI;
   |SI #11#2 = 3;
       |SI nDiario ! #11#3; |O fFecha ! #11#4; |O nDocum ! #11#5;
           nSwSalto = 1;
        |FINSI;
   |FINSI;

   swprim = 0;
   |BUCLE LasLineas;
   seleccio = 1;
   aCuenta  = #11#6;
   nDiario  = #11#3;
   fFecha   = #11#4;
   nDocum   = #11#5;
   nSwSalto = 0;
   nDiario  = 0;
   fFecha   = @;
   nDocum   = 0;
   aCuenta  = "";
|FPRC;

|DEFBUCLE lectura3;
   #11#2;
   1,8;
   enEmpresa, nDTipo, #0#2, #0#0, #0#4, #0#13, #0#11, #0#7, aDEstado;
   enEmpresa, nHTipo, #0#3, #0#1, #0#5, #0#14, #0#12, #0#8, aHEstado;
   ;
   NULL, imprime;
|FIN;

|DEFBUCLE lectura2;
   #11#3;
   1,8;
   enEmpresa, nDTipo,  #0#9, #0#13, #0#11, #0#7, aDEstado;
   enEmpresa, nHTipo, #0#10, #0#14, #0#12, #0#8, aHEstado;
   ;
   NULL, imprime;
|FIN;

|DEFBUCLE lectura1;
   #11#4;
   1,2,8;
   enEmpresa, #0#13, #0#11, #0#7, nDTipo, aDEstado;
   enEmpresa, #0#14, #0#12, #0#8, nHTipo, aHEstado;
   ;
   NULL, imprime;
|FIN;

|PROCESO HazTodo;
|| ... Observaciones DsArchi
       aAlfa1 = #0#2; aAlfa1 = aAlfa1 % 0402; enM1 = aAlfa1;
       aAlfa1 = #0#3; aAlfa1 = aAlfa1 % 0402; enM2 = aAlfa1;
       eaObservaciones = "";
       |SI #0#0 = #0#1;
           |SI #0#0 = 00; eaObservaciones = "DE APERTURA"; |FINSI;
           |SI #0#0 = 99; eaObservaciones = "DE CIERRE";   |FINSI;
       |FINSI;
       |SI eaObservaciones = "";
           SwApertura = 0;
           SwCierre   = 0;
           NumMes = enM1; |HAZ NombreMes; NombreMes = " " + NombreMes; |QBF NombreMes;
           |SI #0#2 = fec1; |Y #0#0 = 00; NombreMes = " LA APERTURA"; SwApertura = 1; |FINSI;
           eaObservaciones = "DE" + NombreMes + " ";

           NumMes = enM2; |HAZ NombreMes; NombreMes = " " + NombreMes;
           |SI #0#3 = fec2; |Y #0#1 = 99; NombreMes = "L CIERRE"; SwCierre = 1; |FINSI;
           |SI enM1 ! enM2; |O SwApertura = 1; |O SwCierre = 1;
               eaObservaciones = eaObservaciones + "A" + NombreMes;
           |FINSI;
       |FINSI;
       eaObservaciones = eaObservaciones > " ";

   |SI nOpcion = 2;
       eaInforme = informe;
       |HAZ ElIMPREArchi;
       |SI swErrorDSArchi = 1;
           nSalir = 1;
           |ACABA;
        |FINSI;
   |FINSI;

   seleccio = 0;

   |ABRE #2;

   |SI #0#15 = "T";|O #0#15 = "G";
       nDTipo = 1; nHTipo = 1;
       |BUCLE lectura1;
   |FINSI;

   nSwSalto = 0;
   nSwP     = 0;
   |SI #0#15 = "T";|O #0#15 = "C";
       nDTipo = 2; nHTipo = 2;
       |BUCLE lectura2;
    |FINSI;

   nSwSalto = 0;
   nSwP     = 0;
   |SI #0#15 = "T";|O #0#15 = "A";
       nDTipo = 3; nHTipo = 3;
       |BUCLE lectura3;
   |FINSI;

   |CIERRA #2;

   |SI seleccio = 0;
       |SI #48#27 = "N"; |Y enSwMvo ! 2; |Y nOpcion = 1;
           |MENAV "    ATENCION!!! Seleccion Vacia ...";
       |FINSI;
   |SINO;
       |PIEINF;
   |FINSI;

   |SI nOpcion = 2;
       |FININF;
       |FINIMP;
       |SI swErrorDSArchi = 0; |Y seleccio ! 0;
           |HAZ Archivando;
       |FINSI;
       Impresora = "CrystalReport";
   |FINSI;

|FPRC;

|PROCESO InformePDF;
   nSwError = 0;

   aIP = ""; |IP_REMOTO aIP;
   aContiene = "";
   |ESPECIFICA "RBASS", aContiene;
   aAlfa = aContiene1;
   aAlfa = aAlfa - "\";
   |SI FSalida ! 0;
      nCalc = ((FSalida / 100) ! 0) + 99;
      aAlfa = aAlfa % nCalc;
   |SINO;
      aAlfa = aAlfa - "/";
      |SI FSalida ! 0;
         nCalc = ((FSalida / 100) ! 0) + 99;
         aAlfa = aAlfa % nCalc;
      |FINSI;
   |FINSI;

   aAlfa = eaPathTemporal;
   aNombrePDF = (("00" + enOrMvo) % - 102) + aNombrePDF;

   |SI aIP ! "";
       |RMKDIR aAlfa;
   |SINO;
       |MKDIR aAlfa;
   |FINSI;

   Impresora = "CrystalReport;" + aAlfa + aNombrePDF;
   |IMPRE -1;
   |SI FSalida = 0;
       |INFOR informe;
       |SI FSalida ! 0;
          || |SI nSwMen = 0; |MENAV "     Error en informe."; |FINSI;
          nSwError = 1;
       |FINSI;
   |FINSI;
   enHayInforme = 1;
|FPRC;

|PROCESO impresion; |TIPO 3;

    |SI nArchi = 1;
        |MENU aMenu;
        nOpcion = FSalida;
        |SI nOpcion = 0;
            |ACABA;
        |FINSI;
    |SINO;
         nOpcion = 1;
    |FINSI;
    nOpSegur = nOpcion

    |SI nOpSegur = 3;  |SALVA_DATOS #caextinf;  |FINSI;

    |SI nOpSegur = 1; |O nOpSegur = 3;
        nOpcion = 1;
        |HAZ impre_Uno;
        |SI nOpSegur = 3;  |REPON_DATOS #caextinf;  |FINSI;
        |SI nSalir = 1; |ACABA; |FINSI;
    |FINSI;

    |SI nOpSegur = 2; |O nOpSegur = 3;
        nOpcion = 2;
        |HAZ impre_Uno;
    |FINSI;
|FPRC;

|PROCESO impre_Uno;

   nDTipo = 1; nHTipo = 3;
   |SI #0#15 = "G"; nDTipo = 1; nHTipo = 1; |FINSI;
   |SI #0#15 = "C"; nDTipo = 2; nHTipo = 2; |FINSI;
   |SI #0#15 = "A"; nDTipo = 3; nHTipo = 3; |FINSI;

   aDEstado = " "; aHEstado = "z";
   |SI #0#17 ! "T";
       aDEstado = #0#17;
       aHEstado = #0#17;
   |FINSI;

   nSalir = 0;
   |SI nOpcion = 2;
        Impresora = "CrystalReport";
   |FINSI;
   |SI nOpcion = 1;
       |SI enSwMvo ! 2;
           Impresora = "CrystalReports";
           |IMPRE -1;
           |SI FSalida ! 0;
               |MENAV "0000Error en Impresora. ";
               nSalir = 1;
               |ACABA;
           |FINSI;

           |INFOR informe;
           |SI FSalida ! 0;
               |MENAV "     Error en informe.";
               |FINIMP;
               nSalir = 1;
               |ACABA;
           |FINSI;
       |SINO;
            aNombrePDF = "RevCon.pdf";
            |HAZ InformePDF;
            |SI nSwError = 1; nSalir = 1; |ACABA; |FINSI;
       |FINSI;
   |FINSI;

   |SI #48#27 = "S"; |Y enSwMvo ! 2;
       |HAZ MultiEmpresa;
   |SINO;
       |DFICE dirfich0; |QBF dirfich0;
       |HAZ HazTodo;
   |FINSI;

   |SI nOpcion = 1;
       |FININF;
       |FINIMP;
   |FINSI;
|FPRC;
|| ***********************************************************************

|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #1  dirfich0;
   |PATH_DAT #3  dirfich0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 000001;
 999999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SI nOpSegur ! 3; |O nOpcion = 1;
     |SUB_EJECUTA "caselemp";
     |NOME_DAT #998 eaFichsel;
  |FINSI;
  |BUCLE MultiEmp0;
  |SI nOpSegur ! 3; |O nOpcion = 2;
      |DELINDEX #998;
  |FINSI;
|FPRC;


|PROGRAMA;
   nArchi = 0;
   |SI enSwMvo ! 2;
       |HAZ ActivaPLB_arc1;
       eaVengoDe = "caextinf";
       |HAZ MiraSiArchiva;

       |MANTE #caextinf;
   |SINO;
       enEjercicio = enEjerM;
       enEmpresa   = enEmprM;
       enPeriodo   = enPConM;
       |HAZ inicio;

       |HAZ eLosInfBasica;
       |DDEFECTO #caextinf;
       #caextinf#0 = #dsinfcoc#110;
       #caextinf#1 = #dsinfcoc#111;
       #caextinf#2 = efDFecM;
       #caextinf#3 = efHFecM;
       #caextinf#4 = #dsinfcoc#112;
       #caextinf#5 = #dsinfcoc#113;
       #caextinf#6 = #dsinfcoc#114;
       #caextinf#9 = #dsinfcoc#115;
       #caextinf#10 = #dsinfcoc#116;
       #caextinf#15 = #dsinfcoc#117;
       #caextinf#16 = #dsinfcoc#118;
       #caextinf#17 = #dsinfcoc#119;
       #caextinf#18 = #dsinfcoc#120;

       |HAZ impresion;
   |FINSI;
|FPRO;
