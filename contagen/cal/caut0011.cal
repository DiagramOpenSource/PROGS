|FICHEROS;
     caut0011 #0;

     casieaca #1;
     casieali #2;
     camautoc #3;
     camautol #4;

     casiegru #5;  || grupos predef.
     casiesub #6;  || subgrupos predef.
     camautti #7;

     imsieac@ #11;
     imsieal@ #12;
     imautoc@ #13;
     imautol@ #14;

     canempre #30;
     caselem1 #998;
     caw00026 #999;

     predecab #101;
     predelin #102;
     prmautoc #103;
     prmautol #104;

     predegru #105;
     predesub #106;
     prmautti #107;

     camaasic #10;
|FIN;

|VARIABLES;

     aAlfa0   = "";
     aAlfa1   = "";
     aAlfa2   = "";
     aAlfa3   = "";
     aAlfa4   = "";
     aAlfa5   = "";
     nPara1   = 0;
     nPara2   = 0;
     nNume1   = 0;
     nNume2   = 0;
     nNume3   = 0;
     nNume4   = 0;
     nNume5   = 0;
     aTitulo  = "";

     aFichero   = "";
     aBass      = "";
     aDef       = "";

     nSwCambio  = 0;
     nSwAlgun   = 0;
     nSwAlguP   = 0;
     nSwAlguF   = 0;
     nLiteral   = 0;
     nTantoPor  = 0;
     nTantoPorR = 0;
     nTantoPorP = 0;
     nCalculo   = 0;

     &enSwLinea = 0;
     &eaCambio  = "";
     &eaTitulo  = "";

     aQueQuiero = "";
     aNumCampos = "";
     nNumCampos = 0;
     nNumCampo1 = 0;

     informe    = "caut0011";

     &enCampo40 = 0;
     &enCampo41 = 0;
     &enCampo42 = 0;
     &enCampo43 = 0;

     &eaCampo42 = "";
     &eaCampo43 = "";
     &eaCampo44 = "";
     &eaCampo45 = "";

     nNuevo     = 0;
     nOpera     = 0;
     nError     = 0;
     nSwNuevo   = 0;
     aTipo0     = "";
     nBueno     = 0;
     nEstado    = 0;
     aParApte   = "";
     nAnyo      = 0;
|FIN;

|INCLUYE dscont_i;

|| *************************************************************************
||                        CALCULOS DE PANTALLA
|| *************************************************************************
|CALCULO SoloBasicos;
 |SI #0#18 = "S"; aAlfa1 = "N"; |FINSI;
 |SI #0#18 = "N"; aAlfa1 = "S"; |FINSI;
 |PARA nPara1 = 1; |SI nPara1 [ 17; |HACIENDO nPara1 = nPara1 + 1;
       |C_M #0nPara1, 1, aAlfa1;
 |SIGUE;
 |SI enEjercicio = 2012;|C_M #0#19, 1, aAlfa1; |FINSI;

 |SI #0#18 = "S";
     #0#1  = "N"; |PINTA #0#1;
     #0#8  = "N"; |PINTA #0#8;
     #0#13 = "N"; |PINTA #0#13;
     #0#16 = "N"; |PINTA #0#16;
     #0#17 = "S"; |PINTA #0#17;
 |FINSI;
|FCAL;

|CALCULO CambioPred; |TIPO 0;
 aAlfa1 = #0#1;
 |C_M #0#2, 1, aAlfa1;
 |C_M #0#3, 1, aAlfa1;
 |C_M #0#4, 1, aAlfa1;
 |C_M #0#5, 1, aAlfa1;
 |C_M #0#6, 1, aAlfa1;
 |C_M #0#7, 1, aAlfa1;
 |C_M #0#14, 1, aAlfa1;
 |SI #0#1 = "N";
     |PDEFECTO #0, 2, 8;
     |PINTA #0#2; |PINTA #0#3;
     |PINTA #0#4; |PINTA #0#5;
     |PINTA #0#6; |PINTA #0#7;
 |FINSI;
|FCAL;

|CALCULO CambioFijos; |TIPO 0;
 aAlfa1 = #0#8;
 |C_M #0#9,  1, aAlfa1;
 |C_M #0#10, 1, aAlfa1;
 |C_M #0#11, 1, aAlfa1;
 |C_M #0#12, 1, aAlfa1;
 |C_M #0#15, 1, aAlfa1;
 |SI #0#8 = "N";
     |PDEFECTO #0, 9, 13;
     |PINTA #0#9;  |PINTA #0#10;
     |PINTA #0#11; |PINTA #0#12;
 |FINSI;
|FCAL;

|CALCULO SiConservo; |TIPO 0;
 aAlfa1 = #0#13;
 |SI #0#1 = "S"; |C_M #0#14, 1, aAlfa1; |FINSI;
 |SI #0#8 = "S"; |C_M #0#15, 1, aAlfa1; |FINSI;
 |SI #0#13 = "N";
     #0#14 = 1000;  |PINTA #0#14;
     #0#15 = 10000; |PINTA #0#15;
 |FINSI;
|FCAL;

|| **********************************************************************
||///////////////////// Asientos Fijos
|PROCESO mautotipo;
   #7#0 = #107#0;
   |LEE 101#7=;
   nEstado = FSalida;
   #7#0 = #107#0;
   #7#1 = #107#1;

   |SI nEstado = 0; |GRABA 020#7a; |FINSI;
   |SI nEstado ! 0; |GRABA 020#7n; |FINSI;
   |LIBERA #7;

   aAlfa1 = #7#0 + " " + #7#1;;
   |ATRI R; |PINTA 1412, aAlfa1; |ATRI 0;
|FPRC;

|DEFBUCLE mautotipo;
   #107#1;
   ;
   01;
   99;
   e;
   NULL, mautotipo;
|FIN;

|PROCESO mautol_Borrar;
 |BORRA 020#4a;
 |LIBERA #4;
|FPRC;

|DEFBUCLE mautol_Borra;
   #4#1;
   ;
   #103#0, 0001;
   #103#0, 9999;
   be;
   NULL, mautol_Borrar;
|FIN;

|PROCESO mautol_lin2;
   #4#0 = #104#0;
   #4#1 = #104#1;
   |LEE 101#4=;
   nEstado = FSalida;

   |PARA nPara1 = 0; |SI nPara1 [ nNumCampo1; |HACIENDO nPara1 = nPara1 + 1;
         #4nPara1 = #104nPara1;
   |SIGUE;

   |SI nEstado = 0; |GRABA 020#4a; |FINSI;
   |SI nEstado ! 0; |GRABA 020#4n; |FINSI;
   |LIBERA #4;
|FPRC;

|DEFBUCLE mautol_lin1;
   #104#1;
   ;
   #103#0, 0001;
   #103#0, 9999;
   e;
   NULL, mautol_lin2;
|FIN;

||....... Fijos Cabecera
|PROCESO mautoc_cab2;

   #3#0 = #103#0;
   |LEE 101#3=;
   nEstado = FSalida;
   |PARA nPara1 = 0; |SI nPara1 [ nNumCampos; |HACIENDO nPara1 = nPara1 + 1;
         #3nPara1 = #103nPara1;
   |SIGUE;

   |SI nEstado = 0; |GRABA 020#3a; |FINSI;
   |SI nEstado ! 0; |GRABA 020#3n; |FINSI;
   |LIBERA #3;

   aAlfa1 = #3#0 + " " + #3#1;
   |ATRI R; |PINTA 1412, aAlfa1; |ATRI 0;

   |BUCLE mautol_Borra;

   |ABRE #4;
   |BUCLE mautol_lin1;
   |CIERRA #4;
|FPRC;

|DEFBUCLE mautoc_cab1;
   #103#1;
   ;
   0;
   99999;
   e;
   NULL, mautoc_cab2;
|FIN;

||//////////////////////////////// Asientos Predefinidos
|PROCESO prede_gru2;
   #5#0 = #105#0;   || grupo
   |LEE 101#5=;
   nEstado = FSalida;
   #5#0 = #105#0;
   #5#1 = #105#1;
   |SI nEstado = 0;  |GRABA 020#5a;  |FINSI;
   |SI nEstado ! 0;  |GRABA 020#5n;  |FINSI;
   |LIBERA #5;
   aAlfa1 = #casiegru#0 + " " + #casiegru#1;
   |ATRI R; |PINTA 1412, aAlfa1; |ATRI 0;
|FPRC;

|DEFBUCLE prede_gru1;
   #105#1;
   ;
   "  ";
   "zz";
   e;
   NULL, prede_gru2;
|FIN;

||....... Subgrupos
|PROCESO prede_sub2;
   #6#0 = #106#0;   || grupo
   |LEE 101#6=;
   nEstado = FSalida;
   #6#0 = #106#0;
   #6#1 = #106#1;
   |SI nEstado = 0;  |GRABA 020#6a;  |FINSI;
   |SI nEstado ! 0;  |GRABA 020#6n;  |FINSI;
   |LIBERA #6;

   aAlfa1 = #casiesub#0 + " " + #casiesub#1;
   |ATRI R; |PINTA 0412, aAlfa1; |ATRI 0;
|FPRC;

|DEFBUCLE prede_sub1;
   #106#1;
   ;
   "    ";
   "zzzz";
   e;
   NULL, prede_sub2;
|FIN;

||....... Predefinidos Lins.
|PROCESO prede_lin2_B;
  |BORRA 020#2a;
  |LIBERA #2;
|FPRC;

|DEFBUCLE prede_lin1_B;
   #2#1;
   ;
   #101#0,  0;
   #101#0, 99;
   be;
   NULL, prede_lin2_B;
|FIN;

|PROCESO prede_lin2;
   #2#0 = #102#0;   || nro. asiento predefinido
   #2#1 = #102#1;   || linea
   |LEE 101#2=;
   nEstado = FSalida;

   |PARA nPara1 = 0; |SI nPara1 [ nNumCampo1; |HACIENDO nPara1 = nPara1 + 1;
         #2nPara1 = #102nPara1;
   |SIGUE;

   |SI nEstado = 0;  |GRABA 020#2a;  |FINSI;
   |SI nEstado ! 0;  |GRABA 020#2n;  |FINSI;
   |LIBERA #2;
|FPRC;

|DEFBUCLE prede_lin1;
   #102#1;
   ;
   #101#0,  0;
   #101#0, 99;
   e;
   NULL, prede_lin2;
|FIN;

||....... Predefinidos Cabs.
|PROCESO prede_cab2;
   #1#0 = #101#0;
   |LEE 101#1=;
   nEstado = FSalida;

   |PARA nPara1 = 0; |SI nPara1 [ nNumCampos; |HACIENDO nPara1 = nPara1 + 1;
         #1nPara1 = #101nPara1;
   |SIGUE;

   |SI nEstado = 0;
      |GRABA 020#1a;
   |SINO;
      |GRABA 020#1n;
   |FINSI;
   |LIBERA #1;

   aAlfa1 = (#1#0 + " " + #1#1) % 135;
   |ATRI R; |PINTA 1412, aAlfa1; |ATRI 0;

   |BUCLE prede_lin1_B;

   |ABRE #2;
   |BUCLE prede_lin1;
   |CIERRA #2;
|FPRC;

|DEFBUCLE prede_cab1;
   #101#1;
   ;
   0;
   9999;
   e;
   NULL, prede_cab2;
|FIN;

|PROCESO CopiaBasicos;
    |ATRI R; |PINTA 1209, "                                                             ";
             |PINTA 1309, "                                                             ";
             |PINTA 1409, "                                                             ";
    |ATRI 0; |PINTA 1509, "                                                             ";
             |CUADRO 1209 1570;

    |DBASE dir_fich;
    dir_fich =dir_fich + "fich/";

    |PATH_DAT #predecab #dir_fich#;
    |PATH_DAT #predelin #dir_fich#;
    |PATH_DAT #predegru #dir_fich#;
    |PATH_DAT #predesub #dir_fich#;
    |PATH_DAT #prmautoc #dir_fich#;
    |PATH_DAT #prmautol #dir_fich#;
    |PATH_DAT #prmautti #dir_fich#;

 || ... Predefinidos
    |SI enEjercicio < 2008;
        aAlfa1 = "predec07"; |NOME_DAT #predecab #aAlfa1#;
        aAlfa1 = "predel07"; |NOME_DAT #predelin #aAlfa1#;
    |FINSI;

    aQueQuiero = "|NC";
    aNumCampos = #1#aQueQuiero#;
    nNumCampos = aNumCampos;
    nNumCampos = nNumCampos - 1;

    aQueQuiero = "|NC";
    aNumCampos = #2#aQueQuiero#;
    nNumCampo1 = aNumCampos;
    nNumCampo1 = nNumCampo1 - 1;

    |ATRI R; |PINTA 1311, "Asientos Predefinidos                                      ";
             |PINTA 1410, "                                                           "; |ATRI 0;
    |ABRE #1;
    |BUCLE prede_cab1;
    |CIERRA #1;

 || ... Grupos de Predefinidos
    |ATRI R; |PINTA 1311, "Grupo de Predefinidos                                      ";
             |PINTA 1410, "                                                           "; |ATRI 0;
    |ABRE #5;
    |BUCLE prede_gru1;
    |CIERRA #5;

 || ... Subgrupos de Predefinidos
    |ATRI R; |PINTA 1311, "Subgrupos de Predefinidos                                  ";
             |PINTA 1410, "                                                           "; |ATRI 0;
    |ABRE #6;
    |BUCLE prede_sub1;
    |CIERRA #6;

 || ... Asientos Fijos

    aAlfa1 = "prmautol";
    |SI enEjercicio < 2008;
        aAlfa1 = "prmau07c"; |NOME_DAT #prmautoc #aAlfa1#;
        aAlfa1 = "prmau07l"; |NOME_DAT #prmautol #aAlfa1#;
    |FINSI;

    aQueQuiero = "|NC";
    aNumCampos = #camautoc#aQueQuiero#;
    nNumCampos = aNumCampos;
    nNumCampos = nNumCampos - 1;

    aQueQuiero = "|NC";
    aNumCampos = #camautol#aQueQuiero#;
    nNumCampo1 = aNumCampos;
    nNumCampo1 = nNumCampo1 - 1;

    |ATRI R; |PINTA 1311, "Asientos Fijos                                             ";
             |PINTA 1410, "                                                           "; |ATRI 0;
    |ABRE #3;
    |BUCLE mautoc_cab1;
    |CIERRA #3;

 || ... Tipos Astos Fijos
    |ATRI R; |PINTA 1311, "Tipos Asientos Fijos                                       ";
             |PINTA 1410, "                                                           "; |ATRI 0;
    |ABRE #7;
    |BUCLE mautotipo;
    |CIERRA #7;
|FPRC;

|| **********************************************************************
|PROCESO LeeAsientos;
  #10#7 = #999#2;
  |GRABA 020#10a;
  |LIBERA #10;
|FPRC;

|DEFBUCLE LeeAsientos;
  #10#1;
  7;
  00, "01.01.0000", 000001, #999#1;
  99, "31.12.2999", 999999, #999#1;
  be;
  NULL, LeeAsientos;
|FIN;

|| **********************************************************************
|PROCESO GrabaNuevo;
 #999#0 = aTipo0;
 #999#1 = nBueno;
 #999#2 = nNuevo;
 |GRABA 040#999n;

 |SI aTipo0 = "P"; |Y #1#17 = "X";
     |BUCLE LeeAsientos;
 |FINSI;
|FPRC;

|PROCESO  LecturaNuevos;
 nSwNuevo = 0;
 |SELECT #2#999;
 |DDEFECTO #999;
 #999#0 = aTipo0;
 #999#2 = nBueno;
 |LEE 041#999=;
 |SI FSalida = 0;
     nSwNuevo = 1;
 |FINSI;
 |SELECT #1#999;
|FPRC;

|| **********************************************************************
|PROCESO CambioNumero;  || Numero de IVA
|SI nAnyo = 2010;
    |SI nNume1 = 16;   nNume1 = 18;   |FINSI;
    |SI nNume1 = 1.16; nNume1 = 1.18; |FINSI;
    |SI nNume1 = 0.16; nNume1 = 0.18; |FINSI;
    |SI nNume1 = 7;    nNume1 = 8;    |FINSI;
    |SI nNume1 = 1.07; nNume1 = 1.08; |FINSI;
    |SI nNume1 = 0.07; nNume1 = 0.08; |FINSI;
    |SI nNume1 = 9;    nNume1 = 10;   |FINSI;
    |SI nNume1 = 1.09; nNume1 = 1.10; |FINSI;
    |SI nNume1 = 0.09; nNume1 = 0.10; |FINSI;
    |SI nNume1 = 7.50; nNume1 = 8.50; |FINSI;
    |SI nNume1 = 0.75; nNume1 = 0.85; |FINSI;

    |SI nNume1 = 1.20; nNume1 = 1.22; |FINSI;
    |SI nNume1 = 0.20; nNume1 = 0.22; |FINSI;
|FINSI;

|SI nAnyo = 2012;
    |SI nNume1 = 18;   nNume1 = 21;   |FINSI;
    |SI nNume1 = 1.18; nNume1 = 1.21; |FINSI;
    |SI nNume1 = 0.18; nNume1 = 0.21; |FINSI;

    |SI nNume1 = 10;   nNume1 = 12;   |FINSI;
    |SI nNume1 = 1.10; nNume1 = 1.12; |FINSI;
    |SI nNume1 = 0.10; nNume1 = 0.12; |FINSI;

    |SI nNume1 = 8;    nNume1 = 10;   |FINSI;
    |SI nNume1 = 1.08; nNume1 = 1.10; |FINSI;
    |SI nNume1 = 0.08; nNume1 = 0.10; |FINSI;

    |SI nNume1 = 8.50; nNume1 = 10.50; |FINSI;
    |SI nNume1 = 0.85; nNume1 = 1.05;  |FINSI;

    |SI nNume1 = 1.22; nNume1 = 1.262; |FINSI;
    |SI nNume1 = 0.22; nNume1 = 0.262; |FINSI;
|FINSI;
|FPRC;

|PROCESO CambioNumeroR;  || Numero de Recargo
|SI nAnyo = 2012;
    |SI nNume1 = 4;    nNume1 = 5.20;  |FINSI;
    |SI nNume1 = 1.04; nNume1 = 1.052; |FINSI;
    |SI nNume1 = 0.04; nNume1 = 0.052; |FINSI;

    |SI nNume1 = 1;    nNume1 = 1.40;  |FINSI;
    |SI nNume1 = 1.01; nNume1 = 1.014; |FINSI;
    |SI nNume1 = 0.01; nNume1 = 0.014; |FINSI;
|FINSI;
|FPRC;

|PROCESO CambioNumeroP;  || Numero de Retencion
|SI nAnyo = 2012;
    |SI nNume1 = 15;   nNume1 = 21;   |FINSI;
    |SI nNume1 = 1.15; nNume1 = 1.21; |FINSI;
    |SI nNume1 = 0.15; nNume1 = 0.21; |FINSI;
|FINSI;
|FPRC;

|PROCESO CambioLit;
    aAlfa3 = aAlfa0 - aAlfa1;
    nNume4 = FSalida;

    |SI aAlfa3 ! aAlfa0;
        nNume4 = (nNume4 / 100) ! 0;
        nNume3 = nNume4 - 1;
        |SI nNume3 ] 1;
            nNume3 = 100 + nNume3;
            aAlfa3 = aAlfa0 % nNume3;
        |SINO;
            aAlfa3 = "";
        |FINSI;
        aAlfa3 = aAlfa3 + aAlfa2;

        nNume4 = nNume4 + nNume2;
        nNume3 = nNume4 * 100 + 60;

        aAlfa0 = aAlfa3 + (aAlfa0 % nNume3);
    |FINSI;
|FPRC;

|PROCESO CambioLiteral;
    aAlfa4 = aAlfa0; |QBF aAlfa4;

|SI nAnyo = 2010;
    |SI aAlfa4 ! "";
        nNume2 = 3; aAlfa1 = "7.5"; aAlfa2 = "8.5"; |HAZ CambioLit;
        nNume2 = 2; aAlfa1 = "16";  aAlfa2 = "18";  |HAZ CambioLit;
        nNume2 = 1; aAlfa1 = "7";   aAlfa2 = "8";   |HAZ CambioLit;
        nNume2 = 1; aAlfa1 = "9";   aAlfa2 = "10";  |HAZ CambioLit;
    |FINSI;
|FINSI;
|SI nAnyo = 2012;
    |SI aAlfa4 ! "";
        nNume2 = 3; aAlfa1 = "8.5"; aAlfa2 = "10.5"; |HAZ CambioLit;
        nNume2 = 2; aAlfa1 = "18";  aAlfa2 = "21";   |HAZ CambioLit;
        nNume2 = 2; aAlfa1 = "10";  aAlfa2 = "12";   |HAZ CambioLit;
        nNume2 = 1; aAlfa1 = "8";   aAlfa2 = "10";   |HAZ CambioLit;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO CambioLiteralR;
    aAlfa4 = aAlfa0; |QBF aAlfa4;
|SI nAnyo = 2012;
    |SI aAlfa4 ! "";
        nNume2 = 1; aAlfa1 = "4";  aAlfa2 = "5.20"; |HAZ CambioLit;
        nNume2 = 1; aAlfa1 = "1";  aAlfa2 = "1.40"; |HAZ CambioLit;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO CambioLiteralP;
    aAlfa4 = aAlfa0; |QBF aAlfa4;
|SI nAnyo = 2012;
    |SI aAlfa4 ! "";
        nNume2 = 2; aAlfa1 = "15";  aAlfa2 = "21"; |HAZ CambioLit;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO CambiosFijoLineas;

  |SI nOpera = 1;
      |SI #0#13 = "S"; |Y nNuevo ! -1; |Y #0#16 = "S";
          aQueQuiero = "|NC";
          aNumCampos = #4#aQueQuiero#;
          nNumCampos = aNumCampos;
          nNumCampos = nNumCampos - 1;
          |PARA nNume1 = 0; |SI nNume1 [ nNumCampos; |HACIENDO nNume1 = nNume1 + 1;
               #14nNume1 = #4nNume1;
          |SIGUE;
          #14#0 = nNuevo;
          |GRABA 020#14n;
      |FINSI;
  |FINSI;

  nTantoPor  = 0;
  nTantoPorR = 0;
  nTantoPorP = 0;

  aAlfa5 = #4#11; aAlfa5 = aAlfa5 > " ";
  |SI #4#13 ! 0; |Y #4#11 = "I";
      nNume1 = #4#13;
      |HAZ CambioNumero;
      |SI nNume1 ! #4#13;
          nTantoPor = 1;
          #4#13 = nNume1;
      |FINSI;
  |FINSI;
  |SI #4#13 ! 0; |Y #4#11 = "R";
      nNume1 = #4#13;
      |HAZ CambioNumeroR;
      |SI nNume1 ! #4#13;
          nTantoPorR = 1;
          #4#13 = nNume1;
      |FINSI;
  |FINSI;
  |SI #4#13 ! 0; |Y #4#11 = "P"; |Y #0#19 = "S";
      nNume1 = #4#13;
      |HAZ CambioNumeroP;
      |SI nNume1 ! #4#13;
          nTantoPorP = 1;
          #4#13 = nNume1;
      |FINSI;
  |FINSI;

  |SI nTantoPor ! 0; |O nTantoPorR ! 0; |O nTantoPorP ! 0;
      nSwCambio = 1;
  |FINSI;

  |SI nOpera = 1;
      |SI #0#16 = "S";
          |GRABA 020#4a;
      |FINSI;
      |SI nTantoPor ! 0; |O nTantoPorR ! 0; |O nTantoPorP ! 0;
          eaCambio = "Cambio en: ";
          |SI nTantoPor  ! 0; eaCambio = eaCambio + "- Tipo IVA       "; |FINSI;
          |SI nTantoPorR ! 0; eaCambio = eaCambio + "- Tipo Recargo   "; |FINSI;
          |SI nTantoPorP ! 0; eaCambio = eaCambio + "- Tipo Retencion "; |FINSI;
          |PRINT;
          enSwLinea = 5;
      |FINSI;
  |FINSI;
  |LIBERA #2;

  |SI nOpera = 0; |Y nSwCambio = 1;
      |ERROR10;
  |FINSI;
|FPRC;

|DEFBUCLE LasLineasF;
  #4#1;
  ;
  #3#0, 0001;
  #3#0, 9999;
  be;
  NULL, CambiosFijoLineas;
|FIN;

|PROCESO CambiosFijos;

  |SI #3#16 = "N"; |ACABA; |FINSI;

  aTipo0 = "F";
  nBueno = #3#0;
  |HAZ LecturaNuevos;
  |SI nSwNuevo = 1; |ACABA; |FINSI;

  nNuevo = -1;
  |SI #0#13 = "S";
      nNuevo = #3#0 + #0#15;  ||Mas el Incremento
      |SI nNuevo [ 99999;
          #13#0 = nNuevo;
          |LEE 041#13=;
          |SI FSalida = 0; nNuevo = -1; |FINSI;
      |SINO;
          nNuevo = -1;
      |FINSI;
  |FINSI;

  enSwLinea = 4;
  aTitulo   = #3#1;

  aAlfa0 = #3#1;
  |HAZ CambioLiteral;
  |SI #0#19 = "S"; |Y nAnyo = 2012;  |HAZ CambioLiteralP;  |FINSI;
  |SI aAlfa0 ! #3#1;
      #3#1 = aAlfa0;
  |FINSI;

  nSwCambio = 0;
  nOpera    = 0;
  |BUCLE LasLineasF;
  |SI nSwCambio = 1;
      nOpera = 1;
      |BUCLE LasLineasF;

      |SI #0#16 = "S";
          |GRABA 020#3a;

          |SI #0#13 = "S"; |Y nNuevo ! -1;
              aQueQuiero = "|NC";
              aNumCampos = #3#aQueQuiero#;
              nNumCampos = aNumCampos;
              nNumCampos = nNumCampos - 1;
              |PARA nNume1 = 0; |SI nNume1 [ nNumCampos; |HACIENDO nNume1 = nNume1 + 1;
                    #13nNume1 = #3nNume1;
              |SIGUE;
              #13#0 = nNuevo;
              #13#1 = aTitulo;
              |GRABA 020#13n;
              |HAZ GrabaNuevo;
          |FINSI;
      |FINSI;
      |SI aTitulo ! #3#1;
          eaCambio = "Modificada la descripcion del Asiento Predefinido";
          |SI enSwLinea = 4; enSwLinea = 6; |PRINT; |FINSI;
          enSwLinea = 2; |PRINT;
      |FINSI;
      |SI #0#13 = "S";
          |SI nNuevo ! -1;
              aAlfa1 = nNuevo; aAlfa1 = ("00000" + aAlfa1) % -105;
              eaCambio = "el Asiento Original pasa al Codigo " + aAlfa1;
          |SINO;
              nNume1 = #3#0 + #0#15;
              |SI nNume1 [ 99999;
                  aAlfa1 = nNume1; aAlfa1 = ("00000" + aAlfa1) % -105;
                  eaCambio = "No se puede conservar, el Codigo " + aAlfa1 + " ya existe";
              |SINO;
                  aAlfa1 = nNume1; aAlfa1 = ("000000" + aAlfa1) % -106;
                  eaCambio = "No se puede conservar, el Nuevo Codigo " + aAlfa1 + " es erroneo";
              |FINSI;
          |FINSI;
          |SI enSwLinea = 4; enSwLinea = 6; |PRINT; |FINSI;
          enSwLinea = 2; |PRINT;
      |FINSI;
      nSwAlguF = 1;
  |FINSI;
  |LIBERA #3;
|FPRC;

|DEFBUCLE CambiosFijos;
   #3#1;
   13,11;
   #0#9,  "S", #0#11;
   #0#10, "S", #0#12;
   be;
   NULL, CambiosFijos;
|FIN;

|PROCESO CambiosPredLineas;

  |SI nOpera = 1;
      |SI #0#13 = "S"; |Y nNuevo ! -1; |Y #0#16 = "S";
          aQueQuiero = "|NC";
          aNumCampos = #2#aQueQuiero#;
          nNumCampos = aNumCampos;
          nNumCampos = nNumCampos - 1;
          |PARA nNume1 = 0; |SI nNume1 [ nNumCampos; |HACIENDO nNume1 = nNume1 + 1;
               #12nNume1 = #2nNume1;
          |SIGUE;
          #12#0 = nNuevo;
          |GRABA 020#12n;
      |FINSI;
  |FINSI;

  nLiteral  = 0;
  nTantoPor = 0;
  nTantoPorR= 0;
  nTantoPorP= 0;
  nCalculo  = 0;

  aParApte = #2#56; aParApte = aParApte > " ";

  |SI aParApte ! "R"; |Y aParApte ! "P"; |Y aParApte ! "D"; |Y aParApte ! "X";

      aAlfa0 = #2#59;
      |HAZ CambioLiteral;
      |SI aAlfa0 ! #2#59;
          #2#59    = aAlfa0;
          nLiteral = 1;
      |FINSI;
  |FINSI;

  |SI nAnyo = 2012;
      |SI aParApte = "R";                   || Recargo
          aAlfa0 = #2#59;
          |HAZ CambioLiteralR;
          |SI aAlfa0 ! #2#59;
              #2#59    = aAlfa0;
              nLiteral = 1;
          |FINSI;
      |FINSI;
      |SI aParApte = "P"; |Y #0#19 = "S";  || Retencion
          aAlfa0 = #2#59;
          |HAZ CambioLiteralP;
          |SI aAlfa0 ! #2#59;
              #2#59    = aAlfa0;
              nLiteral = 1;
          |FINSI;
      |FINSI;
  |FINSI;

  |SI #2#60 ! 0; |Y aParApte = "I";
      nNume1 = #2#60;
      |HAZ CambioNumero;
      |SI nNume1 ! #2#60;
          nTantoPor = 1;
          #2#60 = nNume1;
      |FINSI;
  |FINSI;

  |SI nAnyo = 2012;
      |SI #2#60 ! 0; |Y aParApte = "R";
          nNume1 = #2#60;
          |HAZ CambioNumeroR;
          |SI nNume1 ! #2#60;
              nTantoPorR = 1;
              #2#60 = nNume1;
          |FINSI;
      |FINSI;
      |SI #2#60 ! 0; |Y aParApte = "P"; |Y #0#19 = "S";
          nNume1 = #2#60;
          |HAZ CambioNumeroP;
          |SI nNume1 ! #2#60;
              nTantoPorP = 1;
              #2#60 = nNume1;
          |FINSI;
      |FINSI;
  |FINSI;

  |SI aParApte ! "R"; |Y aParApte ! "P"; |Y aParApte ! "D"; |Y aParApte ! "X";
    |SI #2#7 = "A";
        |PARA nPara1 = 48; |SI nPara1 [ 55; |HACIENDO nPara1 = nPara1 + 1;
              nPara2 = nPara1 - 8;
              |SI #2nPara2 ! " "; |Y #2nPara2 ! "+"; |Y #2nPara2 ! "-";
                   nNume1 = #2nPara1;
                   |HAZ CambioNumero;
                   |SI nNume1 ! #2nPara1;
                       #2nPara1 = nNume1;
                       nCalculo = 1;
                   |FINSI;
               |FINSI;
         |SIGUE;
    |FINSI;
  |FINSI;

  |SI nAnyo = 2012;
      |SI aParApte = "R"; |Y #2#7 = "A";
        |PARA nPara1 = 48; |SI nPara1 [ 55; |HACIENDO nPara1 = nPara1 + 1;
              nPara2 = nPara1 - 8;
              |SI #2nPara2 ! " "; |Y #2nPara2 ! "+"; |Y #2nPara2 ! "-";
                   nNume1 = #2nPara1;
                   |HAZ CambioNumeroR;
                   |SI nNume1 ! #2nPara1;
                       #2nPara1 = nNume1;
                       nCalculo = 1;
                   |FINSI;
              |FINSI;
         |SIGUE;
      |FINSI;
      |SI aParApte = "P"; |Y #2#7 = "A"; |Y #0#19 = "S";
        |PARA nPara1 = 48; |SI nPara1 [ 55; |HACIENDO nPara1 = nPara1 + 1;
              nPara2 = nPara1 - 8;
              |SI #2nPara2 ! " "; |Y #2nPara2 ! "+"; |Y #2nPara2 ! "-";
                   nNume1 = #2nPara1;
                   |HAZ CambioNumeroP;
                   |SI nNume1 ! #2nPara1;
                       #2nPara1 = nNume1;
                       nCalculo = 1;
                   |FINSI;
              |FINSI;
         |SIGUE;
      |FINSI;
  |FINSI;

  |SI nLiteral ! 0; |O nTantoPor ! 0; |O nTantoPorR ! 0;
         |O nTantoPorP ! 0; |O nCalculo ! 0;
      nSwCambio = 1;
  |FINSI;

  |SI nOpera = 1;
      |SI #0#16 = "S";
          |GRABA 020#2a;
      |FINSI;
      |SI nLiteral ! 0; |O nTantoPor ! 0; |O nTantoPorR ! 0;
           |O nTantoPorP ! 0; |O nCalculo ! 0;
          eaCambio = "Cambio en: ";
          |SI nLiteral   ! 0; eaCambio = eaCambio + "- Texto Ayuda  "; |FINSI;
          |SI nTantoPor  ! 0; eaCambio = eaCambio + "- Tipo IVA     "; |FINSI;
          |SI nTantoPorR ! 0; eaCambio = eaCambio + "- Tipo Recargo "; |FINSI;
          |SI nTantoPorP ! 0; eaCambio = eaCambio + "- Tipo Retencion "; |FINSI;
          |SI nCalculo   ! 0; eaCambio = eaCambio + "- Operaciones "; |FINSI;
          |PRINT;
          enSwLinea = 1;
      |FINSI;
  |FINSI;
  |LIBERA #2;

  |SI nOpera = 0; |Y nSwCambio = 1;
      |ERROR10;
  |FINSI;
|FPRC;

|DEFBUCLE LasLineasP;
  #2#1;
  ;
  #1#0, 00;
  #1#0, 99;
  be;
  NULL, CambiosPredLineas;
|FIN;

|PROCESO CambiosPred;
  aTipo0 = "P";

  nBueno = #1#0;
  |HAZ LecturaNuevos;
  |SI nSwNuevo = 1; |ACABA; |FINSI;

  nNuevo = -1;
  |SI #0#13 = "S";
      nNuevo = #1#0 + #0#14;  ||Mas el Incremento
      |SI nNuevo [ 9999;
          #11#0 = nNuevo;
          |LEE 041#11=;
          |SI FSalida = 0; nNuevo = -1; |FINSI;
      |SINO;
          nNuevo = -1;
      |FINSI;
  |FINSI;

  enSwLinea = 0;
  aTitulo   = #1#1;

  aAlfa0 = #1#1;
  |HAZ CambioLiteral;
  |SI #0#19 = "S"; |Y nAnyo = 2012;  |HAZ CambioLiteralP; |FINSI;

  |SI aAlfa0 ! #1#1;
      #1#1 = aAlfa0;
  |FINSI;

  nSwCambio = 0;
  nOpera    = 0;
  |BUCLE LasLineasP;
  |SI nSwCambio = 1;
      nOpera = 1;
      |BUCLE LasLineasP;

      |SI #0#16 = "S";
          |GRABA 020#1a;

          |SI #0#13 = "S"; |Y nNuevo ! -1;
              aQueQuiero = "|NC";
              aNumCampos = #1#aQueQuiero#;
              nNumCampos = aNumCampos;
              nNumCampos = nNumCampos - 1;
              |PARA nNume1 = 0; |SI nNume1 [ nNumCampos; |HACIENDO nNume1 = nNume1 + 1;
                    #11nNume1 = #1nNume1;
              |SIGUE;
              #11#0 = nNuevo;
              #11#1 = aTitulo;
              |GRABA 020#11n;
              |HAZ GrabaNuevo;
          |FINSI;
      |FINSI;
      |SI aTitulo ! #1#1;
          eaCambio = "Modificada la descripcion del Asiento Predefinido";
          |SI enSwLinea = 0; enSwLinea = 3; |PRINT; |FINSI;
          enSwLinea = 2; |PRINT;
      |FINSI;
      |SI #0#13 = "S";
          |SI nNuevo ! -1;
              aAlfa1 = nNuevo; aAlfa1 = ("0000" + aAlfa1) % -104;
              eaCambio = "el Asiento Original pasa al Codigo " + aAlfa1;
          |SINO;
              nNume1 = #1#0 + #0#14;
              |SI nNume1 [ 9999;
                  aAlfa1 = nNume1; aAlfa1 = ("0000" + aAlfa1) % -104;
                  eaCambio = "No se puede conservar, el Codigo " + aAlfa1 + " ya existe";
              |SINO;
                  aAlfa1 = nNume1; aAlfa1 = ("00000" + aAlfa1) % -105;
                  eaCambio = "No se puede conservar, el Nuevo Codigo " + aAlfa1 + " es erroneo";
              |FINSI;
          |FINSI;
          |SI enSwLinea = 0; enSwLinea = 3; |PRINT; |FINSI;
          enSwLinea = 2; |PRINT;
      |FINSI;
      nSwAlguP = 1;
  |FINSI;
  |LIBERA #1;
|FPRC;

|DEFBUCLE CambiosPred;
   #1#1;
   2,13,14;
   #0#2, "S", #0#4, #0#6;
   #0#3, "S", #0#5, #0#7;
   be;
   NULL, CambiosPred;
|FIN;

|PROCESO HazTodo;

       |SI #0#18 = "S";
           |HAZ CopiaBasicos;
           |ACABA;
       |FINSI;

       |DELINDEX #999;

       nError = 0;
       |DBASE aBass;  |QBT aBass;
       aDef = aBass + "def/casieaca.mas";
       |CARGA_DEF aDef, imsieac@;
       |SI FSalida ! 0;
           |MENAV "    Error en def casieaca. Proceso interrumpido";
           nError = 1;
           |ACABA;
       |FINSI;

       aDef = aBass + "def/casieali.mas";
       |CARGA_DEF aDef, imsieal@;
       |SI FSalida ! 0;
           |MENAV "    Error en def casieali. Proceso interrumpido";
           |DESCARGA_DEF #11;
           nError = 1;
           |ACABA;
       |FINSI;

       aDef = aBass + "def/camautoc.mas";
       |CARGA_DEF aDef, imautoc@;
       |SI FSalida ! 0;
           |MENAV "    Error en def camautoc. Proceso interrumpido";
           |DESCARGA_DEF #12;
           |DESCARGA_DEF #11;
           nError = 1;
           |ACABA;
       |FINSI;

       aDef = aBass + "def/camautol.mas";
       |CARGA_DEF aDef, imautol@;
       |SI FSalida ! 0;
           |MENAV "    Error en def camautol. Proceso interrumpido";
           |DESCARGA_DEF #13;
           |DESCARGA_DEF #12;
           |DESCARGA_DEF #11;
           nError = 1;
           |ACABA;
       |FINSI;

       |PATH_DAT #11 dirfich0;
       |PATH_DAT #12 dirfich0;
       |PATH_DAT #13 dirfich0;
       |PATH_DAT #14 dirfich0;

       |ABRE #999;
       nSwAlgun = 0;

       aTipo0 = "P";
       nSwAlguP = 0;
       |SI #0#1 = "S";
          |ABRE #11;
          |ABRE #12;
          |BUCLE CambiosPred;
          |CIERRA #12;
          |CIERRA #11;
       |FINSI;
       |SI nSwAlguP = 1;  nSwAlgun = 1; |FINSI;

       aTipo0 = "F";
       nSwAlguF = 0;
       |SI #0#8 = "S";
           |ABRE #13;
           |ABRE #14;
           |BUCLE CambiosFijos;
           |CIERRA #13;
           |CIERRA #14;
      |FINSI;

      |CIERRA #999;
      |SI nSwAlguF = 1;  nSwAlgun = 1;  |FINSI;

      |SI nSwAlgun = 1; |PIEINF; |FINSI;

      |DESCARGA_DEF #imautol@;
      |DESCARGA_DEF #imautoc@;
      |DESCARGA_DEF #imsieal@;
      |DESCARGA_DEF #imsieac@;
|FPRC;

|CALCULO Tipo3; |TIPO 3;

  |SI #0#0 = "SI";

      |SI #0#18 = "N";
              |SI #0#16 = "S";
                  eaTitulo = "RELACION DE CAMBIOS REALIZADOS EN ";
              |SINO;
                  eaTitulo = "RELACION DE CAMBIOS A REALIZAR EN ";
              |FINSI;
              |SI #0#1 = "S"; |Y #0#8 = "S";
                  eaTitulo = eaTitulo + "ASIENTOS PREDEFINIDOS Y ASIENTOS FIJOS";
              |SINO;
                  |SI #0#1 = "S";
                      eaTitulo = eaTitulo + "ASIENTOS PREDEFINIDOS";
                  |SINO;
                      eaTitulo = eaTitulo + "ASIENTOS FIJOS";
                  |FINSI;
              |FINSI;

              aFichero = "1n" + Usuario;
              |NOME_DAT #999 aFichero;

              |IMPRE 0;
              |SI FSalida ! 0; |ACABA; |FINSI;

              |INFOR informe;
              |SI FSalida ! 0;
                 |MENAV "    No existe Informe. Proceso Interrumpido";
                 |FINIMP;
                 |ACABA;
              |FINSI;
      |FINSI;

      |SI #0#17 = "S";
          |HAZ MultiEmpresa;
      |SINO;
          |DFICE dirfich0; |QBF dirfich0;
          |HAZ HazTodo;
      |FINSI;

      |SI #0#18 = "N";
              |FININF;
              |FINIMP;

              |SI #0#17 = "N";
                  |SI nSwAlgun = 1;
                      |SI #0#16 = "S";
                          |CONFI "    SDesea Copiar Ahora los Nuevos Asientos a Otras Empresas";
                          |SI FSalida = 0;
                              |SI #0#1 = "S"; |Y nSwAlguP = 1;
                                  enCampo40 = #0#2; enCampo41 = #0#3;
                                  eaCampo42 = #0#4; eaCampo43 = #0#5;
                                  eaCampo44 = #0#5; eaCampo45 = #0#6;
                                  |SI #0#13 = "S";  enCampo41 = #0#3 + #0#14;  |FINSI;
                                  |SI enCampo41 > 9999;  enCampo41 = 9999;  |FINSI;
                                  enEjercicio = nAnyo;
                                  |SUB_EJECUTA "caut0006;E2010";
                              |FINSI;
                              |SI #0#8 = "S"; |Y nSwAlguF = 1;
                                  enCampo40 = #0#9;  enCampo41 = #0#10;
                                  enCampo42 = #0#11; enCampo43 = #0#12;
                                  |SI #0#13 = "S";  enCampo41 = #0#10 + #0#15; |FINSI;
                                  |SI enCampo41 > 99999;  enCampo41 = 99999;  |FINSI;
                                  enEjercicio = nAnyo;
                                  |SUB_EJECUTA "caut0004;E2010";
                              |FINSI;
                          |FINSI;
                      |FINSI;
                  |SINO;
                      |MENAV "    No hay cambios a Realizar";
                  |FINSI;
              |FINSI;
              |DELINDEX #999;
      |FINSI;

  |FINSI;
|FCAL;

|PROGRAMA;

 |HAZ inicio;
 |SI enEjercicio ! 2010; |Y enEjercicio ! 2012;
     |PINTA 1008, "Utilidad creada por los Cambios de IVA en los Ejercicios 2010 y 2012 ";
     |PINTA 1108, "Esta entrado en un periodo que no corresponde a estos ejercicios.    ";
     |PAUSA;
     |ACABA;
 |FINSI;

 nAnyo = enEjercicio;

 |CLS;
 |CABEZA "Adapta IVA Pred/Fijo";

 |PINPA #0#0;
 |SI enEjercicio = 2012;
     |ATRI R; |PINTA 0576, "2012"; |ATRI 0;

     |PINTA 0960, "del  18% al   21%";
     |PINTA 1060, "del   8% al   10%";
     |PINTA 1160, "del 8.5% al 10.5%";
     |PINTA 1260, "del  10% al   12%";
     |PINTA 1359, "ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´"
     |ATRI S; |PINTA 1360,"Recargo"; |ATRI 0;
     |PINTA 1459, "³del   1% al 1.40% ³";
     |PINTA 1559, "³del   4% al 5.20% ³";
     |PINTA 1659, "ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ"
 |SINO;
     |C_V #0#19,1,"N";
     |C_M #0#19,1,"N"; #0#19 = "N";
     |PINTA 1535, "                  ";
 |FINSI;

 |PINDA #0#0;
 |ENDATOS #1#0;

|FPRO;

|| *********************************************************************
|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   |SI enEjercicio < nAnyo; |ACABA; |FINSI;

   |SI enEjercicio ] 2012; |Y nAnyo = 2010; |ACABA; |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |ATRI P; |PINTA 1710," Empresa:                                                    "; |ATRI 0;
   |CUADRO 1609 1870;
   |PINTA 1720, #30#2;
   |PINTA 1726, #30#3;
   |PINTA 1728, #30#1;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #2 dirfich0;
   |PATH_DAT #3 dirfich0;
   |PATH_DAT #4 dirfich0;
   |PATH_DAT #10 dirfich0;
   |HAZ HazTodo;
   |SI nError = 1; |ERROR10;|FINSI;
|FPRC;

|DEFBUCLE MultiEmp0;
   #caselem1#1;
   ;
   000001;
   999999;
   ;
   NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;
