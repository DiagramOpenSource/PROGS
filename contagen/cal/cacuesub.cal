|FICHEROS;
  cacuesub #0;
  cacuenta #1;
  cafantas #2;
  camaasil #3;

  :/contagen/def/cacuebal #5;    || Cuentas de balance
  :/contagen/def/caparbal #6;    || partidas balances
  :/contagen/def/caparexp #7;    || partidas explotacion

  ca8cumay #410;
  ca8m0002 #422;
|FIN;

|VARIABLES;
  &entrada   = 1;
  &eSwSelec  = 0;
  &eaDDepar  = "";
  &eaHDepar  = "";
  &eaDSubde  = "";
  &eaHSubde  = "";
  &enCNivel  = 0;   || 0 = dep y subd, 1 = un solo dep, 2 = un total por cta
  &efDFecha  = @;
  &efHFecha  = @;
  &eaNomFSub = "";  ||Nombre del Fichero Creado

  &eaTex1 = "";
  &eaTex2 = "";
  &eaTex3 = "";
  aFichero = "";

  nDebe    = 0;
  nHaber   = 0;

  afFecha  = "";
  aMes     = "";
  nMes     = 0;
  nMesAc   = 0;
  nNume1   = 0;
  nNume2   = 0;
  nNume3   = 0;

  cta      = "";

  aElDepar = "";
  aElSubde = "";
|FIN;

|INCLUYE  dscont_i;
|INCLUYE  defec1_i;

|| =======================================================================
|PROCESO MiraCuenta;
  |ABRE #1;
  #1#0 = #0#0;
  |LEE 001#1=;
  |SI FSalida ! 0;
      |DDEFECTO #1;
      #1#0 = #0#0;
      |HAZ defect2;
  |FINSI;
  #0#2  = #1#2;  #0#3 = #1#3;
  #0#4  = #1#4;  #0#5 = #1#5;
  #0#6  = #1#6;  #0#7 = #1#7;
  #0#8  = #1#8;
  #0#54 = #1#54; #0#55 = #1#55;
  |CIERRA #1;
|FPRC;

|PROCESO defect2;
  #1#2 = "Cuenta NO Creada ";
  #1#3 = "S";
  nMensa  = 1;
  nMensa9 = 1;
  |HAZ defect1;
|FPRC;

|| ........................................................................

|| ========================================================================
|PROCESO Acumular;
 aElDepar = #3#21;
 aElSubde = #3#28;
 |SI eaSubde ! "N";
    |SI aElSubde = "   "; |ACABA; |FINSI;
 |FINSI;

 |SI enCNivel ! 0; aElDepar = "  ";  |FINSI;

 |SI Haybasica = 1;
     |QBF aElDepar;
     aElDepar = ("00" + aElDepar) % -102
     |QBF aElSubde;
     aElSubde = ("000" + aElSubde) % -103
 |FINSI;

 |SI aElDepar < eaDDepar; |O aElDepar > eaHDepar;  |ACABA; |FINSI;
 |SI aElSubde < eaDSubde; |O aElSubde > eaHSubde;  |ACABA; |FINSI;

 |SI Haybasica = 1; |Y enCNivel = 0;
     |Y aElSubde = "000"; |Y eSwSelec = 0;
        |ACABA;
 |FINSI;

 #0#0  = #3#4;        || Cuenta
 eaCuenta = #0#0; |HAZ SacaNivel; #3#5 = enNivel;
 #0#1  = #3#5;        || Digito
 #0#57 = aElDepar;    || Departamento
 #0#72 = aElSubde;    || Subdepartamento
 |LEE 101#0=;
 |SI FSalida ! 0;
     |DDEFECTO #0;
     #0#0  = #3#4;        || Cuenta
     #0#1  = #3#5;        || Digito
     #0#57 = aElDepar;    || Departamento
     #0#72 = aElSubde;    || Subdepartamento
     |HAZ MiraCuenta;
     |GRABA 020#0b;
 |FINSI;

 |SI #3#1 > #0#56;  #0#56 = #3#1; |FINSI;   ||Fecha Ultima Operacion


|| .... La Suma
   |SI #3#8 = "D";
       nDebe  = #3#9;
       nHaber = 0;
   |SINO;
       nHaber = #3#9;
       nDebe  = 0;
   |FINSI;

   |SI #3#0 = 0;                      || Diario 00
       #0#9  = #0#9  + nDebe;
       #0#10 = #0#10 + nHaber;
       #0#11 = #0#9 - #0#10;
   |SINO;
       |SI #3#0 = 99;                 || Diario de Cierre
           #0#48 = #0#48 + nDebe;
           #0#49 = #0#49 + nHaber;
           #0#50 = #0#48 - #0#49;
       |SINO;
           afFecha = #3#1;
           aMes    = afFecha % 402;
           nMes    = aMes;
           nMesAc  = (nMes - dig1) + 1;
           |SI nMesAc [ 0;
               nMesAc = (13 - dig1) + nMes;
           |FINSI;
           nNume1 = (nMesAc * 3) + 9;   ||debe
           nNume2 = nNume1 + 1;         ||haber
           nNume3 = nNume1 + 2;         ||saldo

           #0nNume1 = #0nNume1 + nDebe;
           #0nNume2 = #0nNume2 + nHaber;
           #0nNume3 = #0nNume1 - #0nNume2;
       |FINSI;
   |FINSI;
   #0#51 = #0#51 + nDebe;
   #0#52 = #0#52 + nHaber;
   #0#53 = #0#51 - #0#52;

   #0#58 = #0#11 + #0#14;
   #0#59 = #0#58 + #0#17;
   #0#60 = #0#59 + #0#20;
   #0#61 = #0#60 + #0#23;
   #0#62 = #0#61 + #0#26;
   #0#63 = #0#62 + #0#29;
   #0#64 = #0#63 + #0#32;
   #0#65 = #0#64 + #0#35;
   #0#66 = #0#65 + #0#38;
   #0#67 = #0#66 + #0#41;
   #0#68 = #0#67 + #0#44;
   #0#69 = #0#68 + #0#47;
   #0#70 = #0#69 + #0#50;
   #0#71 = #0#70;

   |GRABA 020#0a;
   |LIBERA #0;
|FPRC;

|DEFBUCLE LeeAsientos;
 #3#1;
 21;
 00, efDFecha, 000001, 0001, "  ";
 99, efHFecha, 999999, 9999, "zz";
 e;
 NULL, Acumular;
|FIN;

|RUTINA inf0;
  #0#0  = "            ";
  #0#57 = "  ";
  #0#72 = "   ";
|FRUT;

|RUTINA sup0;
  #0#0  = "zzzzzzzzzzzz";
  #0#57 = "zz";
  #0#72 = "zzz";
|FRUT;

|PROGRAMA;

  |SI eSwSelec = 2;
      |PATH_DAT #0 dirfich0;
      |PATH_DAT #1 dirfich0;
      |PATH_DAT #3 dirfich0;
      |PATH_DAT #5 dirfich0;
      |PATH_DAT #6 dirfich0;
      |PATH_DAT #7 dirfich0;
  |SINO;
      |HAZ inicio;
  |FINSI;

  |SI eaSubde = "N";
      |SI eSwSelec = 0;
          |MENAV "    Empresa sin Subdepartamentos ";
          |ACABA;
      |FINSI;
  |FINSI;

  |HAZ DirAplicSt;

  |SI eSwSelec = 0;
      efDFecha = fec1;
      efHFecha = fec2;
      eaDDepar = "  ";
      eaHDepar = "zz";
      eaDSubde = "   ";
      eaHSubde = "zzz";
      enCNivel = 0; |SI eaDepar = "N"; enCNivel = 1; |FINSI;
  |FINSI;

  eaNomFSub = ("zsub" + Usuario) % 108;
  |NOME_DAT #0 eaNomFSub;

  |ABRE #0;  |CIERRA #0;  |DELINDEX #0;

  |ABRE #0;
  |BUCLE LeeAsientos;
  |CIERRA #0;

  |SI eSwSelec = 0;
      |MANTE #0;
  |FINSI;
|FPRO;

|PROCESO Tipo1;  |TIPO 1;
   |MENAV "      De de Alta, Modifique o de de Baja por el Plan Contable.";
   |ERROR;
|FPRC;


|PROCESO Nivel; |TIPO 0;
     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;

   cta = #0#0;  |QBF cta;
   |SI cta = ""; |ACABA; |FINSI;
   eaCuenta = #0#0;
   |HAZ SacaNivel;
   #0#1 = enNivel;
|FPRC;

|CALCULO AntesDep; |TIPO 7;
   |SI eaDepar = "S";
      |SI Haybasica = 1;
          |MENSA "    <F2> Consultar Actividades.";
      |SINO;
          |MENSA "    <F2> Consultar Departamentos.";
      |FINSI;
   |FINSI;
|FCAL;

|CALCULO TrasDep; |TIPO 0;
   |SI eaDepar = "N"; |ACABA; |FINSI;

   #2#8  = "";
   #2#10 = "01.01.0000";
   #2#11 = "01.01.0000";
   eOpDep = 0; || 1 = Consulta pantalla
   |SI FSalida = 10; eOpDep = 1;  |FINSI;
   enActividad = #0#57;
   eaCodDep    = #0#57;
   |HAZ Actividad;
   |SI eswLect = 0;
       #2#8  = eaNombreAct;
       #2#10 = efActIni;
       #2#11 = efActFin;
   |FINSI;
   #0#57 = eaCodDep; |PINTA #0#57;
   |ATRI P;
   |PINTA 0513, #2#8;
   |PINTA 0551, #2#10;
   |PINTA 0569, #2#11;
   |ATRI 0;
|FCAL;

|CALCULO AntesSubdep; |TIPO 7;
  |SI Haybasica = 1;
      |SI enArrend ! 2;
          |MENSA "    Subdepartamento. <F2> Consultar Referencias Catastrales";
      |SINO;
          |MENSA "    Subdepartamento. <F2> Consultar Cultivos";
      |FINSI;
  |FINSI;
|FCAL;

|CALCULO TrasSubdep; |TIPO 0;
   |SI eaSubde = "N"; |ACABA; |FINSI;

   eOpSub = 0; || 1 = Consulta pantalla
   |SI FSalida = 10; eOpSub = 1;  |FINSI;

   #2#9 = "";
   #2#5 = "";
   enConcepto = 1;
   eaCodSubdep = #0#72;
   enQue = 2;
   |HAZ SubActividad;
   enQue = 0;
   |SI eswLect = 0;
       #2#9 = eaNomSubdep;
   |FINSI;
   #0#72 = eaCodSubdep; |PINTA #0#72;
   |ATRI P;
   |PINTA 0623, #2#9;
   |ATRI 0;
|FCAL;

|| **************************************************************
|PROCESO Tipo13; |TIPO 13;
  |HAZ TrasDep;
  |HAZ TrasSubdep;
|FPRC;

|PROCESO Tipo4; |TIPO 4;
   |IMPRE 0;
   |SI FSalida = 0;
      |HAZ Lecturas;

      |INFOR "cacuedep";
      |PRINT;
      |PIEINF;
      |FININF;
   |SINO;
      |MENAV "    Proceso abortado ...";
   |FINSI;
   |FINIMP;
|FPRC;

|PROCESO Lecturas;
 eaTex1 = "";
 eaTex2 = "";
 eaTex3 = "";
 |SI #0#4 = 0;
    |ABRE #6;
    #caparbal#0 = #0#5;     || tipo
    #caparbal#1 = #0#6;     || agrupacion
    #caparbal#3 = #0#7;     || epigrafe
    #caparbal#5 = #0#8;     || partida
    |LEE 001#6=;
    |SI FSalida ! 0;  |DDEFECTO #6;  |FINSI;
    eaTex1 = #6#2;
    eaTex2 = #6#4;
    eaTex3 = #6#6;
    |CIERRA #6;
  |SINO;
     |ABRE #7;
     #caparexp#0 = #0#4;     || bloque
     #caparexp#1 = #0#5;     || tipo
     #caparexp#2 = #0#6;     || agrupacion
     #caparexp#4 = #0#7;     || epigrafe
     #caparexp#6 = #0#8;     || partida
     |LEE 001#7=;
     |SI FSalida ! 0;  |DDEFECTO #7;  |FINSI;
     eaTex1 = #7#3;
     eaTex2 = #7#5;
     eaTex3 = #7#7;
     |CIERRA #7;
  |FINSI;
|FPRC;
