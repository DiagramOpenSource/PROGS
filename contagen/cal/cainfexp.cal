|FICHEROS;
   cainfexp #0;
   caparexp #1;
   cainfex1 #2;
   canempre #30;
|FIN;

|VARIABLES;
   &entrada = 1;
   error       = 0;
   lin         = 0;
   agru        = "";
   epig        = "";
   part        = "";
   pepe        = "";

   informe = "cainfexp";
   men1 = "    NO EXISTE INFORME";
   men2 = "    NO EXISTE IMPRESORA";
   wagrupacion = 0;
   wepigrafe   = 0;
   sw          = 0;
   &agrupacion = 0
   &epigrafe   = 0;
   &nada       = "                                       ";
   &linea      = 0;
   {-1}menu  = "";
   menu1 = "2400";
   menu2 = "1";
   menu3 = " Elija opcion: ";
   menu4 = "PI";
   menu5 = " Pantalla ";
   menu6 = " Impresora ";
|FIN;

|INCLUYE dscont_i;

|PROCESO mayus;
   #0Campo = #0Campo > " ";
   |PINTA #0Campo;
|FPRC;

|PROCESO porimpre;
   agrupacion = 0;
   epigrafe   = 0;
   |SI sw = 0;
      wagrupacion = #caparexp#2;
      wepigrafe   = #caparexp#4;
      agrupacion = 1;
      |SI #caparexp#4 ! 0; epigrafe   = 1; |FINSI;
      |PRINT;
      sw = 1;
   |FINSI;

   |SI wagrupacion ! #caparexp#2;
      linea      = 1;
      agrupacion = 1;
      epigrafe   = 1;
      |PRINT;
      wagrupacion = #caparexp#2;
      wepigrafe   = #caparexp#4;
      linea       = 0;
   |SINO;
      |SI wepigrafe ! #caparexp#4;
         linea    = 1;
         epigrafe = 1;
         |PRINT;
         wepigrafe = #caparexp#4;
         linea     = 0;
         |SI #caparexp#6 ! 0;
            epigrafe = 0;
         |FINSI;
      |FINSI;
   |FINSI;

   |SI epigrafe = 0; |Y agrupacion = 0;
      |PRINT;
   |FINSI;
|FPRC;

|PROCESO porpanta;
   agrupacion = 0;
   epigrafe   = 0;
   |SI sw = 0;
      wagrupacion = #caparexp#2;
      wepigrafe   = #caparexp#4;
      agrupacion = 1;
      |SI #caparexp#4 ! 0;   epigrafe = 1;   |FINSI;
      |HAZ pintapan;
      sw = 1;
   |FINSI;

   |SI wagrupacion ! #caparexp#2;
      linea      = 1;
      agrupacion = 1;
      epigrafe   = 1;
      |HAZ pintapan;
      wagrupacion = #caparexp#2;
      wepigrafe   = #caparexp#4;
      linea       = 0;
   |SINO;
      |SI wepigrafe ! #caparexp#4;
         linea      = 1;
         epigrafe   = 1;
         |HAZ pintapan;
         wepigrafe = #caparexp#4;
         linea       = 0;
      |FINSI;
   |FINSI;

   |SI epigrafe = 0; |Y agrupacion = 0;
      |HAZ pintapan;
   |FINSI;
|FPRC;

|PROCESO pintapan;
   agru = #caparexp#2;
   agru = ("00" + agru) % -102;
   epig = #caparexp#4;
   epig = ("00" + epig) % -102;
   part = #caparexp#6;
   part = ("00" + part) % -102;

   |SI linea = 1;
      #cainfex1#3 = "";
      #cainfex1#4 = "";
      |HAZ enpanta;
   |FINSI;

   |SI agrupacion = 1;
      #cainfex1#3 = #caparexp#0 + #caparexp#1 + agru + "0000";
      #cainfex1#4 = #caparexp#3;
      |HAZ enpanta;
   |FINSI;

   |SI epigrafe = 1;
      #cainfex1#3 = #caparexp#0 + #caparexp#1 + agru + epig + "00";
      #cainfex1#4 = "     " + #caparexp#5;
      |HAZ enpanta;
      |SI #caparexp#6 ! 0;
         #cainfex1#3 = #caparexp#0 + #caparexp#1 + agru + epig + part;
         #cainfex1#4 = "          " + #caparexp#7;
         |HAZ enpanta;
      |FINSI;
   |FINSI;

   |SI agrupacion = 0; |Y epigrafe = 0;
      #cainfex1#3 = #caparexp#0 + #caparexp#1 + agru + epig + part;
      #cainfex1#4 = "          " + #caparexp#7;
      |HAZ enpanta;
   |FINSI;
|FPRC;

|PROCESO enpanta;
   |PINTA #cainfex1#0;
   |PINTA #cainfex1#1;
   |PINTA #cainfex1#2;

   pepe = #cainfex1#4;
   |QBF pepe;
   |SI pepe = "";  #cainfex1#3 = "";  |FINSI;

   Pos = lin;
   |PINDA #0#2;
   Pos = -1;
   lin = lin + 100;
   |SI lin > 1200;  |HAZ cambiapa;  |FINSI;
|FPRC;

|PROCESO cambiapa;
   error = 0;
   lin = 100;
   |PAUSA;
   |SI FSalida = 1; |O FSalida = 7;
      error = 1;
      |ERROR10;
   |SINO;
      |PINPA #0#2;
   |FINSI;
|FPRC;

|DEFBUCLE cainfexp0;
   #1#1;
   ;
   #0#0, #0#2, #0#3, #0#5, #0#7;
   #0#1, #0#2, #0#4, #0#6, #0#8;
   e;
   NULL, porimpre;
|FIN;

|DEFBUCLE cainfexp1;
   #1#1;
   ;
   #0#0, #0#2, #0#3, #0#5, #0#7;
   #0#1, #0#2, #0#4, #0#6, #0#8;
   e;
   NULL, porpanta;
|FIN;

|PROCESO impre; |TIPO 3;
   |MENU menu;
   menu2 = FSalida;
   |SI FSalida ! 1; |Y FSalida ! 2;  |ACABA; |FINSI;
   |HAZ inicio;
   |SI menu2 = 1; |HAZ pantalla;  |FINSI;
   |SI menu2 = 2; |HAZ impresora; |FINSI;
|FPRC;

|PROCESO impresora;
   |IMPRE 0;
   |SI FSalida = 0;
      |INFOR informe;
      |BUCLE cainfexp0;
      |PIEINF;
      |FININF;
   |SINO;
      |MENAV men2;
   |FINSI;
   |FINIMP;
|FPRC;

|PROCESO pantalla;
   #cainfex1#0 = "EXPLOTACION";
   #cainfex1#1 = #canempre#1;
   #cainfex1#2 = #canempre#26;
   error = 0;
   lin = 100;
   |PINPA #0#2;
   |BUCLE cainfexp1;
   |SI error = 0;  |PAUSA;  |FINSI;
|FPRC;
