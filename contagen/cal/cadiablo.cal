|FICHEROS;
   cadiablo #0;
   caentasc #1;
   caentasl #2;
   caditra1 #3;
   cacuenta #4;
   caivaasi #200;
|FIN;

|VARIABLES;
   &entrada = 1;
   bloque   = "";
   fichero  = "";

   lin     = 0;
   sw_desc = 0;
   opcion  = 0;
   fecalf  = "";

   diario  = 0;
   fecha   = @;
   docume  = 0;

   men1    = "    NO EXISTE EMPRESA";
   mensa1  = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
   mensa3  = "    NO EXISTE BLOQUE DE APUNTES ";
   traba1  = "";

   &swprim  = 0;
   &a_debe  = 0;
   &a_haber = 0;
   &t_debe  = 0;
   &t_haber = 0;

   inform1 = "cadiablo";
   inform2 = "cadiabl1";
   informe = "";

   {-1}menu = "";
   menu1 = "2420";
   menu2 = "1";
   menu3 = " Impresion por : ";
   menu4 = "PI";
   menu5 = "  Pantalla  ";
   menu6 = "  Impresora  ";
|FIN;

|INCLUYE dscont_i;

|| *******************************************************************
|| **************** Campos Pantalla **********************************
|| *******************************************************************

|PROCESO leebloq; |TIPO 6;
   |ABRE #1;
   #caentasc#0 = #cadiablo#0;
   |LEE 001#1=;
   |SI FSalida ! 0;
      |MENAV mensa3;
      |ERROR;
   |SINO;
      #cadiablo#10 = #caentasc#4; |PINTA #cadiablo#10;
      #cadiablo#11 = #caentasc#5; |PINTA #cadiablo#11;
   |FINSI;
   |CIERRA #1;
|FPRC;

|PROCESO diarh; | TIPO 0;   ||  hasta diario
   |SI #cadiablo#8 < #cadiablo#7;  |ERROR;  |FINSI;
|FPRC;

|PROCESO fecha; | TIPO 0;   || Desde/hasta fecha
   |SI #cadiablo#Campo# < fec1; |O #cadiablo#Campo# > fec2;
      |MENAV mensa1;
      |ERROR;
   |FINSI;
   |SI Campo = 5;
      |SI #cadiablo#5 < #cadiablo#4; |ERROR; |FINSI;
   |FINSI;
|FPRC;

|PROCESO documh; | TIPO 0;   ||  hasta documento
   |SI #cadiablo#3 < #cadiablo#2;  |ERROR;  |FINSI;
|FPRC;

|| *************************************************************************
||                          MEOLLO
|| *************************************************************************

|PROCESO buclepan;
   |SI swprim = 0;
      diario = #caentasl#2;
      fecha  = #caentasl#3;
      docume = #caentasl#21;
      #caditra1#10  = 0;
      #caditra1#11  = 0;
      #caditra1#12  = 0;
      swprim = 1;
      |HAZ pantalla;
   |FINSI;
   |SI #cadiablo#9 = 1;
      |SI diario ! #caentasl#2; |O fecha ! #caentasl#3; |O docume ! #caentasl#21;
         sw_desc = 0;
         |SI #caditra1#12 ! 0; sw_desc = 1; |FINSI;
         |HAZ cambiapa;
         diario = #caentasl#2;
         fecha  = #caentasl#3;
         docume = #caentasl#21;
         #caditra1#10  = 0;
         #caditra1#11  = 0;
         #caditra1#12  = 0;
         |HAZ pantalla;
      |FINSI;
   |FINSI;

   |SI lin > 1000;
      sw_desc = 0;
      |HAZ cambiapa;
      |HAZ pantalla;
   |FINSI;

   #caditra1#0 = #caentasl#3;
   #caditra1#1 = #caentasl#2;
   #caditra1#3 = #caentasl#21;
   #caditra1#4 = #caentasl#4;
   #caditra1#5 = #caentasl#6;
   #caditra1#6 = #caentasl#7;
   #caditra1#7 = #caentasl#8;
   |SI #caditra1#7 = "D";
      #caditra1#8 = #caentasl#9;          || debe
      #caditra1#16 = 0;            || haber
      #caditra1#10 = #caditra1#10 + #caentasl#9;
      #caditra1#13 = #caditra1#13 + #caentasl#9;
   |SINO;
      #caditra1#8 = 0;
      #caditra1#16 = #caentasl#9;
      #caditra1#11 = #caditra1#11 + #caentasl#9;
      #caditra1#14 = #caditra1#14 + #caentasl#9;
   |FINSI;
   #caditra1#12 = #caditra1#10 - #caditra1#11;
   #caditra1#15 = #caditra1#13 - #caditra1#14;
   |PINTA #caditra1#0;
   |PINTA #caditra1#1;
   |PINTA #caditra1#3;
   Pos = lin;
   |PINDA #0#3;
   Pos = -1;
   |PINTA #caditra1#10;
   |PINTA #caditra1#11;
   |PINTA #caditra1#12;
   |PINTA #caditra1#13;
   |PINTA #caditra1#14;
   |PINTA #caditra1#15;
   lin = lin + 100;
|FPRC;

|PROCESO cambiapa;
   |SI sw_desc = 1;
      |AVISO;
      |AVISO;
      |ATRI P;
      |PINTA 0430,"===  DESCUADRE ===";
      |ATRI 0;
   |FINSI;
   |PAUSA;
   |SI FSalida = 1; |O FSalida = 7;
      |ERROR10;
   |FINSI;
   |ATRI R;
   |BLIN 04;
   |ATRI 0;
|FPRC;

|PROCESO pantalla;
   lin = 100;
   |C_V #caditra1#2,1,"N";
   |PINPA #0#3;
   |PINTA 0659, "Bloque .: ";
   |ATRI I;
   |PINTA 0669, #cadiablo#0;
   |ATRI 0;
|FPRC;

|PROCESO finpan;
   sw_desc = 0;
   |SI #caditra1#12 ! 0; sw_desc = 1; |FINSI;
   |HAZ cambiapa;
|FPRC;

|DEFBUCLE cadiablo3;
   #2#4;
   ;
   #0#7, #0#4, #0#2, 00001;
   #0#8, #0#5, #0#3, 99999;
   e;
   NULL, buclepan, NULL, NULL, finpan;
|FIN;

|DEFBUCLE cadiablo4;
   #2#1;
   2,3,21;
   #0#0, 00001, #0#7, #0#4, #0#2;
   #0#0, 99999, #0#8, #0#5, #0#3;
   e;
   NULL, buclepan, NULL, NULL, finpan;
|FIN;

|| *************************************************************************
|PROCESO bucleimp;
   |DDEFECTO #4;
   #cacuenta#0 = #caentasl#4;
   #cacuenta#1 = #caentasl#5;
   |LEE 040#4=;

   |SI #caentasl#8 = "D";
      a_debe = #caentasl#9;
      a_haber = 0;
   |SINO;
      a_debe = 0;
      a_haber = #caentasl#9;
   |FINSI;
   |PRINT;
   swprim = 1;
   t_debe  = t_debe  + a_debe;
   t_haber = t_haber + a_haber;
|FPRC;

|DEFBUCLE cadiablo1;
   #2#4;
   ;
   #0#7, #0#4, #0#2, 00001;
   #0#8, #0#5, #0#3, 99999;
   e;
   NULL, bucleimp;
|FIN;

|DEFBUCLE cadiablo2;
   #2#1;
   2,3,21;
   #0#0, 00001, #0#7, #0#4, #0#2;
   #0#0, 99999, #0#8, #0#5, #0#3;
   e;
   NULL, bucleimp;
|FIN;

|PROCESO impresora;
   |IMPRE 0;
   |SI FSalida ! 0;  |ACABA;  |FINSI;
   
   |SI #cadiablo#9 = 1; informe = inform1; |FINSI;
   |SI #cadiablo#9 = 2; informe = inform2; |FINSI;
   
   |INFOR informe;
   |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;
   
   |SI #cadiablo#9 = 1; |BUCLE cadiablo1; |FINSI;
   |SI #cadiablo#9 = 2; |BUCLE cadiablo2; |FINSI;
   |SI swprim = 1;
      |PIEINF;
   |FINSI;
   |FININF;
   |FINIMP;
|FPRC;
|| ----------------------------------------------------------------------

|PROCESO impre;
   bloque = #cadiablo#0;
   fichero = bloque;
   |QBF fichero;
   fichero = fichero + "zzzzzzzz";
   fichero = fichero % 107;
   fichero = fichero + "l";
   
   |NOME_DAT #caentasl #fichero#;
   
   |ABRET #2;
   |ABRE #4;
   
   |MENU menu;
   opcion = FSalida;
   
   |SI opcion = 1;
      |SI #cadiablo#9 = 1;
         |C_V #caditra1#17,1,"N";
         |BUCLE cadiablo3;
      |FINSI;
      |SI #cadiablo#9 = 2; |BUCLE cadiablo4; |FINSI;
   |FINSI;
   |SI opcion = 2;  |HAZ impresora;   |FINSI;
   
   |CIERRAT #2;
   |CIERRA #4;
|FPRC;

|| *********************************************************************
||                             COMIENZO
|| *********************************************************************

|PROGRAMA;
   |CLS;
   |CABEZA "nDIARIO Bloque";
   |HAZ inicio;

   |ABRE #200;
   |DDEFECTO #200;
   |LEE 001#200p;
   |CIERRA #200;

   #cadiablo#4 = fec1;
   #cadiablo#5 = fec2;
   FSalida = 1;
   |PARA; |SI FSalida = 1; |HACIENDO;
      |PINPA #0#0;
      |PINDA #0#0;                    || Pinta los datos por defecto
      |ENDATOS #1#0;                  || Seleccion informe
      |SI FSalida ! 0;  |SAL;  |FINSI;

      |DDEFECTO #3;
      swprim = 0;
      t_debe = 0;
      t_haber = 0;
      |HAZ impre;                     || Calculo impresion
      FSalida = 1;                || Vuelve al bucle
   |SIGUE;
|FPRO;
