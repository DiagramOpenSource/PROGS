|FICHEROS;
   caz00036 #0;

   camaasil #1;    || apuntes lins
   camaasic #2;    || apuntes cabeceras
   calibros #5;    || Libros de IVA
   cacuenta #6;    || Cuentas
   camaniva #10;
   caivalin #11;
   caivaasi #200;
|FIN;

|VARIABLES;
   aParam   = "";
   aAlfa1   = "";
   mensa1   = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";

   enQSerie = 0;
   aDtipoRS = "";
   aHtipoRS = "";

   sw         = 0;
   swc        = 0;
   nSwError   = 0;
   &aError    = "";
   &enSwLinea = 0;
   nPrimL     = 0;
   nPrimLR    = 0;
   nPrimA     = 0;
   nPrimAR    = 0;
   nSwGrabaC  = 0;
   nSwGrabaL  = 0;
   nSwGrabaA  = 0;
   nSwOp      = 0;
   aSigno     = "";

   nCalc      = 0;
|FIN;

|INCLUYE dscont_i;

____________________________________/ CALCULOS DESDE PANTALLA

|PROCESO def_fec; |TIPO 7;
   |SI sw = 0;
      #0#2 = fec1;    |PINTA #0#2;
      #0#3 = fec2;    |PINTA #0#3;
      sw = 1;
   |FINSI;
|FPRC;

|PROCESO fecha; | TIPO 0;   || Desde/hasta fecha
   |SI #0Campo < fec1; |O #0Campo > fec2;
      |MENAV mensa1;
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO diarioh; | TIPO 0;   ||  hasta diario
   |SI #0#1 < #0#0;  |ERROR;  |FINSI;
|FPRC;

|PROCESO fechah; | TIPO 0;   ||  hasta fecha
   |SI #0#3 < #0#2;  |ERROR;  |FINSI;
|FPRC;

|PROCESO documh; | TIPO 0;   ||  hasta documento
   |SI #0#5 < #0#4;  |ERROR;  |FINSI;
|FPRC;


|CALCULO Ehque; |TIPO 7;
 |SI enQSerie = 0;
     |C_M #0#13,1,"N"; #0#13 = "";      |PINTA #0#13;
     |C_M #0#14,1,"N"; #0#14 = "zzzzz"; |PINTA #0#14;
 |SINO;
     |C_M #0#13,1,"S";
     |C_M #0#14,1,"S";
 |FINSI;
|FCAL;

|| *************************************************************************
|| ***********************************************************************
|PROCESO Actualizar;
   MasMenos     = #0#18;
   DebeHaber    = #camaasil#8;
   aCUENTA      = #camaasil#4;
   nNIVELCUENTA = #camaasil#5;
   nREGISTRO    = #camaasil#24;
   nDIARIO      = #camaasil#0;
   aFECHA       = #camaasil#1;
   nDOCUMENTO   = #camaasil#2;

   |HAZ ActDiario;
   |HAZ ActCuenta;
   |HAZ ActTesteo;
|FPRC;

|PROCESO LeoAstos;
 |SI #1#13 ! #10#0; |O #1#15 ! #10#2; |O #1#14 ! #10#3; |ACABA; |FINSI;

 |SI #1#19 = "I"; |O #1#19 = "i";
     |SI nSwOp = 0;
         |SI #1#19 = "I";
             #0#17 = #0#17 + #1#9;
         |SINO;
             #0#17 = #0#17 - #1#9;
         |FINSI;
         |SI #1#20 = 0; |O #1#20 = nPrimL;
             nPrimA = #1#3;
         |FINSI;
     |FINSI;
     |SI nSwOp = 1;
         |SI #1#3   = nPrimA;
             aSigno = #1#8;
             #1#9   = #1#9 + #0#18;
             |HAZ Actualizar;
             |GRABA 020#1a;
         |FINSI;
     |FINSI;
 |FINSI;

 |SI #1#19 = "R"; |O #1#19 = "r";
     |SI nSwOp = 0;
         |SI #1#19 = "R";
             #0#20 = #0#20 + #1#9;
         |SINO;
             #0#20 = #0#20 - #1#9;
         |FINSI;
         |SI #1#20 = 0; |O #1#20 = nPrimLR;
             nPrimAR = #1#3;
         |FINSI;
     |FINSI;
     |SI nSwOp = 1;
         |SI #1#3   = nPrimAR;
             aSigno = #1#8;
             #1#9   = #1#9 + #0#21;
             |HAZ Actualizar;
             |GRABA 020#1a;
         |FINSI;
     |FINSI;
 |FINSI;
 |LIBERA #1;
|FPRC;

|PROCESO LeoLineas;

 |SI #11#2 = 0;
     |BORRA 020#11a;
     |LIBERA #11;
     |ACABA;
 |FINSI;

 |SI nSwOp = 0;
     #0#16 = #0#16 + #11#8;
     |SI #11#8 ! 0; |Y nPrimL= 0;
         nPrimL = #11#2;  || Primera linea con IVA
     |FINSI;
     #0#19 = #0#19 + #11#10;
     |SI #11#10 ! 0; |Y nPrimLR= 0;
         nPrimLR = #11#2;  || Primera linea con Recargo
     |FINSI;
 |FINSI;
 |SI nSwOp = 1;
     |SI nPrimL = #11#2; |O nPrimLR = #11#2;
         |SI nPrimL  = #11#2; #11#8  = #11#8  + #0#18;  |FINSI;
         |SI nPrimLR = #11#2; #11#10 = #11#10 + #0#21;  |FINSI;
         |GRABA 020#11a;
     |FINSI;
 |FINSI;
 |LIBERA #11;
|FPRC;

|DEFBUCLE LeoLineas;
  #11#1;
  ;
  #10#0, #10#1, 00;
  #10#0, #10#1, 99;
  be;
  NULL, LeoLineas;
|FIN;

|PROCESO Imprimir;
 |SI #0#11 = "S"; |PRINT; swc = 1; |FINSI;
 enSwLinea = 1;
|FPRC;

|PROCESO LeeFacturas;
 enSwLinea = 0;
 aSigno    = "";
 nSwGrabaC = 0;
 nSwGrabaL = 0;
 nSwGrabaA = 0;
 nPrimL    = 0;       || Primera linea con IVA
 nPrimA    = 0;       || Linea Apunte del IVA
 #0#15     = 0;
 #0#16     = 0;
 #0#17     = 0;
 #0#18     = 0;
 #0#19     = 0;||  Tiquet 3218:2582
 #0#20     = 0;||     ""
 #0#21     = 0;

 |DDEFECTO #2;
 #2#0 = #10#7;
 #2#1 = #10#8;
 #2#2 = #10#9;
 |LEE 141#2=;
 |SI FSalida ! 0;
     aError   = "ERROR. No existe asiento de esta Factura. No corregido";
     |HAZ Imprimir;
     |LIBERA #10;
     |LIBERA #2;
     |ACABA;
 |FINSI;

 nSwOp     = 0;
 #0#15     = #10#23;  || Iva de Cabecera
 |BUCLE LeoLineas;
 |BUCLELIN 0LeoAstos#2;

 |SI #2#6 = 0;     || Si el asiento esta cuadrado
     |SI #0#17 ! #0#16;
         aError    = "Diferencia entre el IVA de la Linea y el Asiento";
         nSwGrabaL = 1;
         #0#18     = #0#17 - #0#16;
         |HAZ Imprimir;
     |FINSI;
     |SI #0#20 ! #0#19;
         aError    = "Diferencia entre el REC de la Linea y el Asiento";
         nSwGrabaL = 1;
         #0#21     = #0#20 - #0#19;
         |HAZ Imprimir;
     |FINSI;
     nCalc = #0#17 + #0#20;
     |SI #0#15 ! nCalc;
         #0#15 = nCalc;
         aError    = "Diferencia entre el IVA o REC de la Cabecera y el Asiento";
         nSwGrabaC = 1;
         |HAZ Imprimir;
     |FINSI;
 |SINO;
      |SI #10#36 = "S";
         #0#18 = - #2#6;
||         #0#21 = - #2#6;
      |SINO;
          #0#18 =  #2#6;
||          #0#21 =  #2#6;
      |FINSI;
      #0#15 = (#0#16 + #0#18) + (#0#19 + #0#21);
      nSwGrabaA = 1;
      nSwGrabaL = 1;
      nSwGrabaC = 1;
      aError    = "Asiento descuadrado ";
      |HAZ Imprimir;
 |FINSI;

 |SI nSwGrabaL = 1;
     nSwOp = 1;
     |BUCLE LeoLineas;
 |FINSI;

 |SI nSwGrabaA = 1;
     nSwOp = 1;
     |BUCLELIN 0LeoAstos#2;
     |SI aSigno = "D";
         #2#4 = #2#4 + #0#18;
     |SINO;
         #2#5 = #2#5 + #0#18;
     |FINSI;
     #2#6 = #2#4 - #2#5;
     |GRABA 020#2a;
 |FINSI;

 |SI nSwGrabaC = 1;
     #10#23 = #0#15;
     |GRABA 020#10a;
 |FINSI;
 |LIBERA #2;
 |LIBERA #10;
|FPRC;

|DEFBUCLE LeeFacturas;
 #10#2;
 36, 0, 2, 3;
 #0#0, #0#2, #0#4, aDtipoRS, #0#7, #0#13, #0#9;
 #0#1, #0#3, #0#5, aHtipoRS, #0#8, #0#14, #0#10;
 be;
 NULL, LeeFacturas;
|FIN;

|| ***********************************************************************
|| *********************** RECUPERACION CON TIPO 3 ***********************
|| ***********************************************************************

|CALCULO recupera; |TIPO 3;
   |SI #0#12 ! "SI"; |ACABA; |FINSI;

   aDtipoRS= #0#6;
   aHtipoRS= #0#6;
   |SI #0#6 = "A"; aDtipoRS = "R"; aHtipoRS = "Z"; |FINSI;

   |SI #0#11 = "S";
       |IMPRE 0;
       |SI FSalida ! 0; |ACABA; |FINSI;

       |INFOR "caz00036";
       |SI FSalida ! 0;
           |MENAV "    Error no existe informe";
           |FINIMP;
           |ACABA;
       |FINSI;
   |FINSI;

   swc = 0;
   |ABRE #2;
   |BUCLE LeeFacturas;
   |CIERRA #2;
   |SI swc = 0;
       |SI aParam = "";
           |MENAV "    No existe ninguna factura con Error";
       |FINSI;
   |FINSI;

   |SI #0#11 = "S";
       |SI swc ! 0; |PIEINF; |FINSI;
       |FININF;
       |FINIMP;
   |FINSI;
|FCAL;


|PROGRAMA;

  aParam = PARAMETRO; |QBF aParam;
  |SI aParam = "";
      |HAZ inicio;
  |SINO;
      |PATH_DAT #1 dirfich0;
      |PATH_DAT #2 dirfich0;
      |PATH_DAT #5 dirfich0;
      |PATH_DAT #6 dirfich0;
      |PATH_DAT #10 dirfich0;
      |PATH_DAT #11 dirfich0;
      |PATH_DAT #200 dirfich0;
  |FINSI;

  |ABRE #200;
  |DDEFECTO #200;
  |LEE 010#200p;
  enQSerie = 0;
  |SI #200#44 = "S"; enQSerie = 1;|FINSI;
  |CIERRA #200;

  |SI aParam = "";
      |MANTE #0;
  |SINO;
      |DDEFECTO #0;
      aAlfa1 = aParam % 0102; #0#0 = aAlfa1;
      aAlfa1 = aParam % 0310; #0#2 = aAlfa1;
      aAlfa1 = aParam % 1306; #0#4 = aAlfa1;
      aAlfa1 = aParam % 1902; #0#1 = aAlfa1;
      aAlfa1 = aParam % 2110; #0#3 = aAlfa1;
      aAlfa1 = aParam % 3106; #0#5 = aAlfa1;

      #0#6  = "A";
      #0#11 = "N";
      #0#12 = "SI";
      |HAZ recupera;
  |FINSI;
|FPRO;
