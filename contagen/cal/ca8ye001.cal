|FICHEROS;
    ca8ye001 #0;

    cacuenta #1;         || cuentas
    ca8wb001 #3;         || temporal Totales Texto
    cabaexp1 #4;         || temporal existencias
    ca8wb002 #5;         || temporal Cuentas

    caexistc #6;         || descarga cabs.
    caexistl #7;         || descarga lins.

    canempre #30;        || empresas
    caconimp #8;
    cawdatos #900;
    cuentas@ #901;

    ca8comay #401;  || Control de Balances
    ca8accaa #409;
    ca8cumay #410;  || Control de Balances
    ca8m0000 #420;  || Descripcion Numero de Balance
    ca8m0001 #421;  || Def Balance Textos
    ca8m0002 #422;  || Def Balance Linea Cuentas
    ca8m0021 #451;  || Desglose
    ca8m0022 #452;  || Desglose

    caselem1 #998;
|FIN;

|VARIABLES;
     ||DSARCHI
     &nArchi = 0;
 {-1}aMenu  = "";
     aMenu1 = "2400";
     aMenu2 = "1";
     aMenu3 = "Archivo documental activado. Elija opci¢n: ";
     aMenu4 = "IAB";
     aMenu5 = "Imprimir";
     aMenu6 = "Archivar";
     aMenu7 = "amBos";
     &nOpcion = 0;
     &aImpresora = "";
     &swPasada = 0;
     &swErrorDSArchi = 0;
     &eaInforme = "";
     &eaVengoDe = "";
     &enSwDesde9 = 1;
     &enSwMeteGrupoSubTipo = 0;
     &eaObservaciones = "";
     nOpSegur = 0;

   &aparam =  "";
   &enSwComp = 0;
   aFichero = "";
   nBalance = 0;
   nBalNum  = 0;
   aLucrat  = "";
   nImpoOri = 0;
   nImporte = 0;
   nImporte1= 0;
   nImporte2= 0;
   nImporte3= 0;
   nImporte4= 0;
   nImporte5= 0;
   nImporte6= 0;
   nImporte7= 0;
   nImporte8= 0;
   nImporte9= 0;
   nImpB1   = 0;
   nImpB2   = 0;
   nImpB3   = 0;
   nImpB4   = 0;
   nImpB5   = 0;
   nImpB6   = 0;
   nImpB7   = 0;
   nImpB8   = 0;
   nImpB9   = 0;
   nLong    = 0;
   nLongT   = 0;
   nLongE   = 0;
   aTitulo  = "";
   aMas     = "";
   aDef     = "";
   aAPR     = "";
   aCtaExis  = "";
   aCuenta  = "";
   aAlfa    = "";
   aAlfa1   = "";
   acumula = 0;
   e1 = 0;
   e2 = 0;
   e3 = 0;
   e4 = 0;
   nCampo  = 0;
   nCampo2 = 0;
   aCampo3 = "";
   nCampo4 = 0;
   nCampo5 = 0;
   nCampo6 = 0;
   nCampo7 = 0;
   nCampo8 = 0;
   nSI     = 0;
   aDCta   = "";
   aHCta   = "";
   aCtaM   = "";
   nNivel  = 0;
   nCam1  = 0;
   nCam2  = 0;
   nCam3  = 0;

 ||  v_nombre = "cabaexp2";
   &pathbal = "";
   &balance = 1;
   &entrada = 1;
    nNume1  = 0;
    mes     = 0;
    nivel   = 0;
    linea   = 0;
    FEstado = 0;
    activo  = 0;
    pasivo  = 0;
    activ1  = 0;
    pasiv1  = 0;
    explo   = 0;
    expl1   = 0;
    debe1   = 0;
    haber1  = 0;
    saldo   = 0;

    x = 0;
    y = 0;
    &eldesde = 0;
    &elhasta = 0;
    sw = 0;
    agrupaci = 0;
    subagrup = 0;
    epigrafe = 0;
    sw_epi = 0;
    d = "D";
    h = "H";
    tipo = "";
    tipo1 = "";
    cue_perdi = "";
    dig_perdi = 0;
    &sw_inf = 0;
    &cantidad = 0;
    &variable = "";
    &canti = 0;
    &doh = "";
    mes_fin = 0;
    descar = 0;
    traraf = 0;
    informe = "";
    alfa1 = "";
   &r_emp = 0;
   &r_per = 0;
   &anu1 = 0;
   &anu  =0;
   dos = 0;
   &cta = "";
   &nom = "";
   &canti2 = 0;
   &cantidad2 = 0;
   act_cuent = "";
   alfa2 = "";
   alfa3 = "";
   tracan5 = 0;
   &d_mes = 0;
   &h_mes = 0;
   &estru = 0;
   &r_eje = 0;
   &r_nom = "";
   nom1 = "";
   sw_exp = 0;
   descn = 0;
   swexis = 0;

   &eaNomFEst  = "";  ||Nombre del Fichero Creado
   &eaEstruc   = "";
   &eaEstrucN  = "";
   &enCam1     = 0;
   &enCam2     = 0;

   aNomCta     = "";
   aNomCta1    = "";

   &nPagDup = 0;
   &nSwPagDup = 0;
   &pagina = 0;
   sw_pagina = 0;
   nImprimir = 0;

   CodigoEmpresa    = 0;
   EjercicioEmpresa = 0;
   NombreEmpresa    = "";
   nEmpTodo         = 0;
   nPerTodo         = 0;
   nSwMen           = 0;
   &enNumPag        = 0;
   nSwLInv          = 0;
   Comodin          = "";
   Comodin1         = "";
   swNoExiste       = 0;
   Calc             = 0;
   Compara          = 0;
   Per1             = 0;
   Per2             = 0;

   nHay             = 0;
   nDCampo5 = 0;
   nDCampo6 = 0;
   nDCampo7 = 0;
   nDCampo8 = 0;
   nHCampo5 = 0;
   nHCampo6 = 0;
   nHCampo7 = 0;
   nHCampo8 = 0;

   &eaExtension = "";
   &enPlan      = 0;
   nSwErrorBal  = 0;

   &espe17     = "";
   &eSwSuma    = 0;
   dirfichOri  = "";
   nSw      = 0;
   &eaHora    = "";
   aHora      = "";

   &enErChile = 0;
   &eaC1Chile = "";
   &eaC2Chile = "";
   nErrorI    = 0;
   nYaInforme = 0;

   swImp = 0;
   nImpC5 = 0;
   nImpC6 = 0;
   nImpC7 = 0;
   nImpC8 = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoto_i;
|INCLUYE lospat_i;
|INCLUYE digito_i;
|INCLUYE masivo_i;
|INCLUYE dslegv_i;

|| **********************************************************
|PROCESO &c_pagina;
   |SI sw_pagina = 0;
      pagina = #0#30;
      sw_pagina = 1;

      nPagDup = #0#30;
      nSwPagDup = 1;
   |SINO;
      pagina = pagina + 1;

      |HAZ PDuplex;
   |FINSI;
|FPRC;

|PROCESO antesque;
   aparam = PARAMETRO;
   |QBF aparam; |SI aparam = " N"; aparam = ""; |FINSI;
   aNomCta = "cacuenta";

   |SI aparam = "fech"; |O aparam = "Situacionfech";
       aNomCta = "cabalano";
   |FINSI;
   |SI aparam = "Analitica";
       aNomCta =  eaUsuAna;
   |FINSI;
   |SI aparam = "otp";
       aNomCta =  eaNomFEst;
       #0#9 = 0;
       #0#10 = 0;
       #0#11 = "";
       d_desde = 0;
       d_hasta = 0;
       d_masca = "";
   |FINSI;
   |NOME_DAT #1 aNomCta;
   aNomCta1 = aNomCta;
|FPRC;

|CALCULO AnaT13; |TIPO 13;
 aparam = PARAMETRO; |QBF aparam;
 |SI aparam = " N"; aparam = ""; |FINSI;
 |SI aparam = "Analitica";
     |ATRI P;
     |PINTA 0572, "ANALITICO";
     |ATRI 0;
     #30#1 = eaNomAna;
 |FINSI;
  |SI aparam = "otp";
      #0#9  = enCam1; |PINTA #0#9;
      #0#10 = enCam2; |PINTA #0#10;
      #0#11 = eaEstruc; |PINTA #0#11;
      |C_M #0#9,1,"N";
      |C_M #0#10,1,"N";
      |C_M #0#11,1,"N";
      |ATRI P;
      |PINTA 0570, "ESTRUCTURA";
      |ATRI 0;
  |FINSI;
|FCAL;

|| ========================================================================
||                        CALCULOS PANTALLA
|| ========================================================================
|PROCESO deentrada;  |TIPO 7;
  |SI #0#14 = 0;  |Y #0#15 = 0;
      #0#14 = empre;
      #0#15 = ejerc - 1;
      |SI #0#15 < 0; #0#15 = 9; |FINSI;
      |PINTA #0#14;
      |PINTA #0#15;
  |FINSI;

  #0#6 = 1;
  |SI Haybasica = 1; |Y eaDepar = "S";
      #0#6 = 0;
  |FINSI;
  |PINTA #0#6;
  |HAZ DatosEmp;
|FPRC;

|PROCESO mayor1;
  |SI #0Campo = "N";
      #0#14 = empre; |C_M #0#14,1,"N"; |PINTA #0#14;
      #0#15 = ejerc; |C_M #0#15,1,"N"; |PINTA #0#15;
  |SINO;
      |C_M #0#14,1,"S";
      |C_M #0#15,1,"S";
  |FINSI;
|FPRC;

|PROCESO esbuena;
  |SI FSalida = 9;  |ACABA;  |FINSI;
  |ABRE #30;
  #30#2 = #0#14;
  #30#3 = 0;
  |LEE 011#30];
  |SI FSalida ! 0;  |O #30#2 ! #0#14;
      |SI nSwMen = 0;
          |MENAV "     NO Existe la empresa Contable";
          |ERROR;
      |FINSI;
  |FINSI;
  |CIERRA #30;
|FPRC;

|PROCESO consulta;
  |SI FSalida ! 9;  |ACABA;  |FINSI;
  |CIERRAT #30;
  r_emp = 0;
  |SUB_EJECUTA "caconemp.run";
  |ABRET #30;
  |ERROR;
      #0#14 = r_emp;
      #0#15 = r_per;
      |PTEC 802;
      |PTEC 802;
|FPRC;

|PROCESO esbuena1;

  |SI FSalida = 2; |ACABA; |FINSI;
  swNoExiste = 0;
  |ABRE #30;
  #30#2 = #0#14;
  #30#3 = #0#15;
  |LEE 011#30=;
  |SI FSalida ! 0;
      |SI nSwMen = 0;
          |MENAV "     NO Existe Periodo en la empresa Contable";
          |ERROR;
      |FINSI;
      swNoExiste = 1;
      |VETE LeoActual;
  |FINSI;
  |SI #30#13 = 1; nNume1 = #30#14; |FINSI;
  |SI #30#13 = 2; nNume1 = #30#15; |FINSI;
  |SI #30#13 = 3; nNume1 = #30#16; |FINSI;
  |SI #30#13 = 4; nNume1 = #30#17; |FINSI;
  |SI #30#13 = 5; nNume1 = #30#18; |FINSI;
  |SI #30#13 = 6; nNume1 = #30#19; |FINSI;
  |SI MaximoDigitos ! nNume1;
      |SI nSwMen = 0;
          |MENAV "     NO CORRESPONDEN LOS NIVELES";
          |ERROR;
      |FINSI;
      swNoExiste = 1;
      |VETE LeoActual;
  |FINSI;

  #0#16 = #30#1; |PINTA #0#16;
  anu1 = 1900 + #30#26; |SI anu1 [ 1980; anu1 = anu1 + 100; |FINSI;
  nom1 = #30#1;
  |SI anu1 < 2008;
      |SI nSwMen = 0;
          |MENAV "    Periodo Seleccionado anterior al Ejercicio 2008.   NO COMPARABLE";
          |ERROR;
      |FINSI;
      swNoExiste = 1;
  |FINSI;

|ET LeoActual;
  #30#2 = empre;
  #30#3 = ejerc;
  |LEE 011#30=;
  anu = 1900 + #30#26; |SI anu [ 1980; anu = anu + 100; |FINSI;
  |CIERRA #30;
|FPRC;


|PROCESO pintames; |TIPO 0;
  nNume1 = Campo + 2;
  |SI #0Campo ! 0; |Y #0Campo ! 13;
      NumMes = dig1 + (#0Campo - 1);
      |SI NumMes > 12;
          NumMes = NumMes - 12;
      |FINSI;
  |SINO;
      NumMes = #0Campo;
  |FINSI;
  |HAZ NombreMes;
  #0nNume1 = NombreMes; |PINTA #0nNume1;

  |SI Campo = 1; |Y  #0#0 > #0#1;
      |SI nSwMen = 0;
          |MENAV "    Error de Entrada ...";
          |ERROR;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO pintames2; |TIPO 0;
  |SI #0Campo ! 0; |Y #0Campo ! 13;
      NumMes = dig1 + (#0Campo - 1);
      |SI NumMes > 12;
          NumMes = NumMes - 12;
      |FINSI;
  |SINO;
      NumMes = #0Campo;
  |FINSI;
  |HAZ NombreMes;

  |ATRI I;
  |SI Campo = 7;  |PINTA 1519, NombreMes;  |FINSI;
  |SI Campo = 8;  |PINTA 1554, NombreMes;  |FINSI;
  |ATRI 0;
  |SI Campo = 8;  |Y #0#7 > #0#8;
      |SI nSwMen = 0;
          |MENAV "    Error de Entrada ...";
          |ERROR;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO nivel; |TIPO 7;
 |SI aparam ! "chile";
     aAlfa1 = #0#4;
     |C_M #0#5, 1,aAlfa1;
     |SI aAlfa1 = "N";
         #0#5  = MaximoDigitos; |PINTA #0#5;
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO miraniv; |TIPO 0;
  nLongT = #0#5;
  |SI nLongT > 4;
      nivel = 0;
      |SI nLongT = dig4; nivel = 1;  |FINSI;
      |SI nLongT = dig5; nivel = 2;  |FINSI;
      |SI nLongT = dig6; nivel = 3;  |FINSI;
      |SI nLongT = dig7; nivel = 4;  |FINSI;
      |SI nLongT = dig8; nivel = 5;  |FINSI;
      |SI nLongT = dig9; nivel = 6;  |FINSI;
      |SI nivel = 0;
          |MENAV "    Error. Longitud incorrecta";
          |ERROR;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO estruct; |TIPO 0;
  |SI #0#6 = 0;  |ACABA;  |FINSI;

  |ABRE #6;
  #6#0 = #0#6;
  |LEE 040#6=;
  |SI FSalida ! 0;
      |CIERRA #6;
      |SI nSwMen = 0;
          |MENAV "    No existe la estructura de descarga.";
          |ERROR;
      |FINSI;
      |ACABA;
  |FINSI;
  |HAZ cue_per;
  |CIERRA #6;
|FPRC;

|PROCESO cue_per; || calcula la cuenta de perdidas y ganancias
  cue_perdi = #6#2;
  dig_perdi = #6#3;
  |SI #0#4 = "N";
      aAlfa1 = #6#2; |QBF aAlfa1;
      aAlfa1 = aAlfa1 % 0;
      nLong  = aAlfa1;
  |SINO;
      traraf = 100 + #0#5;
      cue_perdi = #6#2 % traraf;
      dig_perdi = nivel;
  |FINSI;
  cue_perdi = cue_perdi + "            ";
  cue_perdi = cue_perdi % 112;
|FPRC;

|PROCESO DatosEmp; |TIPO 7;
 |ABRE #8;
 |DDEFECTO #8;
 #8#0 = 1;
 |LEE 011#8=;
 |CIERRA #8;
 #0#18 = #8#7; |PINTA #0#18;
 alfa1 = #8#8; |C_M #0#18,1,alfa1;

 aAlfa1 = "S";
 |SI #8#18 = "N"; |O #8#19 = "N"; aAlfa1 = "N"; |FINSI;
 |C_M #0#31, 1, aAlfa1;
 |C_M #0#32, 1, aAlfa1;
 |SI #8#18 = "N";
     |C_V #0#31, 1, aAlfa1;
     |C_V #0#32, 1, aAlfa1;
     |PINTA 1832, "            ";
 |SINO;
     |HAZ PonHora;
 |FINSI;
|FPRC;

|PROCESO PonHora; |TIPO 7;
     |C_M #0#31, 0 , aAlfa1;
     |SI aHora = ""; |O aAlfa1 = "N";
         |HORA aHora;
         aAlfa = aHora % 102;  #0#31 = aAlfa;  |PINTA #0#31;
         aAlfa = aHora % 402;  #0#32 = aAlfa;  |PINTA #0#32;
     |FINSI;
|FPRC;

|PROCESO descri; |TIPO 6;
 nSwErrorBal = 0;
 |ABRE #420;
 |DDEFECTO #420;
 #420#0 = #0#26;
 |LEE 001#420=;
 |SI FSalida ! 0;
     |SI nSwMen = 0;
         |MENAV "   Error. No existe Numero de Balance";
         |ERROR;
     |FINSI;
     nSwErrorBal = 1;
 |FINSI;
 |CIERRA #420;
 #0#27 = #420#1; |SI nSwMen = 0; |PINTA #0#27; |FINSI;

 eaNomIBal = "";
 |ABRE #8;
 |DDEFECTO #8;
 |LEE 011#8=;
 |CIERRA #8;
 |SI #8#16 = "S";
     |SI #420#2 = 1; eaNomIBal = "(NORMAL)   "; |FINSI;
     |SI #420#2 = 2; eaNomIBal = "(ABREVIADO)"; |FINSI;
     |SI #420#2 = 3; eaNomIBal = "(PYME)     "; |FINSI;
 |FINSI;
 aLucrat = #420#3;
|FPRC;
|| ****************************************************************
|| ****************************************************************

|PROCESO conexi;
  enTExi = 1;
  nCam1 = empre;
  nCam2 = ejerc;
  nCam3 = enEjercicio;
  d_mes = #0#7;
  h_mes = #0#8;
  estru = #0#6;
  r_emp = empre;
  r_per = ejerc;
  r_eje = #30#26;
  r_nom = #30#1;
  |SUB_EJECUTA "caexicon.run";
  |SI #0#13 = "N"; |ACABA; |FINSI;

  r_emp = #0#14;
  r_per = #0#15;
  r_eje = anu1 - 2000; |SI r_eje < 0; r_eje = anu1 - 1900; |FINSI;
  r_nom = nom1;
  |SUB_EJECUTA "caexicon.run";

  empre       = nCam1;
  ejerc       = nCam2;
  enEjercicio = nCam3;
|FPRC;

|| ***************************************************************************
|| *********************  GRABACION DE TEMPORALES Y CALCULOS *****************
|| ***************************************************************************
|PROCESO MSigno_eLeo;
  #410#12 = enEjerBal;
  #410#0  = aCtaM;
  #410#10 = eNum2008;
  |LEE 001#410=;
  |SI FSalida = 0;
      nSI  = 1;
      aAPR = #410#3;
  |FINSI;
|FPRC;

|PROCESO MSigno08_e;
  nSI   = 0;
  aCtaM = aCuenta % 105;
  |HAZ MSigno_eLeo;
  |SI nSI = 1; |ACABA; |FINSI;

  aCtaM = aCuenta % 104;
  |HAZ MSigno_eLeo;
  |SI nSI = 1; |ACABA; |FINSI;

  aCtaM = aCuenta % 103;
  |HAZ MSigno_eLeo;
  |SI nSI = 1; |ACABA; |FINSI;

  aCtaM = aCuenta % 102;
  |HAZ MSigno_eLeo;
|FPRC;


|PROCESO BuscaCuenta;
 nNume1  = 100 + nLongT;
 aCuenta = aCuenta % nNume1;

 #901#0 = aCuenta;
 #901#1 = nivel;
 |LEE 001#901=;
 |SI FSalida = 0;
     aTitulo = #901#2;
     aAPR    = #901#5; |HAZ MSigno08_e;
 |SINO;
     |SI nLongT [ 4;
         #410#12 = enEjerBal;
         #410#0  = aCuenta;
         #410#10 = eNum2008;
         |LEE 001#410=;
         |SI FSalida = 0;
             aTitulo = #410#1;
             aAPR    = #410#3;
         |FINSI;
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO CambioCta;
  aAlfa1 = aCuenta; |QBF aAlfa1;
  aAlfa1 = aAlfa1 % 0;
  nLong  = aAlfa1;
  |SI nLong ! nLongT;
      |HAZ BuscaCuenta;
  |FINSI;
|FPRC;
|| .............................................................

|PROCESO cargatra_3;  ||Carga Trabajo de Cuentas
  #3#0 = nBalNum;
  #3#1 = nBalance;
  #3#2 = nCampo2;
  #3#3 = aCampo3;
  #3#4 = nCampo4;
  #3#5 = nCampo5;
  #3#6 = nCampo6;
  #3#7 = nCampo7;
  #3#8 = nCampo8;
  |LEE 101#3=;
  |SI FSalida = 0;
      |SI nCampo5 ! 0;
          #3acumula = nImporte;
      |SINO;
          #3acumula = #3acumula + nImporte;
      |FINSI;
      |GRABA 020#3a;
  |FINSI;
  |LIBERA #3;
|FPRC;

|PROCESO LosTotales;
  nCampo2 = #5#2;  ||Bloque
  aCampo3 = #5#3;  ||Tipo
  nCampo4 = #5#4;  ||Agrupacion
  nCampo5 = #5#5;  ||Subagrupacion
  nCampo6 = #5#6;  ||Epigrafe
  nCampo7 = #5#7;  ||Partida
  nCampo8 = #5#8;  ||subpartida

  |HAZ cargatra_3;

  |SI nCampo8 ! 0;    ||Subpartida (A/P = 0)
      nCampo8 = 0;
      |HAZ cargatra_3;
  |FINSI;

  |SI nCampo7 ! 0;   ||Partida
      nCampo8 = 0;
      nCampo7 = 0;
      |HAZ cargatra_3;
  |FINSI;

  saldo = (saldo + nImporte) ! EURODB;

|FPRC;

|PROCESO LeoTotales;
 |SI #3#9 = "C";
     nImporte = nImporte + #3acumula;
 |FINSI;
|FPRC;

|DEFBUCLE LeoTotales;
 #3#1;
 ;
 nBalNum, nBalance, nCampo2, aCampo3, nCampo4, 0, 00, 00,0;
 nBalNum, nBalance, nCampo2, aCampo3, nCampo4, 0, 99, 99,9;
 ;
 NULL, LeoTotales;
|FIN;

|PROCESO elTotal;
  nImporte = 0;
  nCampo2 = #421#2;  ||Bloque
  aCampo3 = #421#3;  ||Tipo
  nCampo4 = #421#4;  ||Agrupacion
  nCampo5 = #421#5;  ||Subagrupacion
  |SI nCampo2 = 5; |Y nCampo5 = 1;
      nImpB1 = nImporte1; nImporte1 = 0;
      nImpB2 = nImporte2; nImporte2 = 0;
      nImpB3 = nImporte3; nImporte3 = 0;
      nImpB4 = nImporte4; nImporte4 = 0;
      nImpB5 = nImporte5; nImporte5 = 0;
      nImpB6 = nImporte6; nImporte6 = 0;
      nImpB7 = nImporte7; nImporte7 = 0;
      nImpB8 = nImporte8; nImporte8 = 0;
      nImpB9 = nImporte9; nImporte9 = 0;
 |FINSI;

  |SI nCampo5 ! 3;
      |BUCLE LeoTotales;
  |FINSI;
  |SI nCampo5 = 3;
      nImporte = nImporte1 + nImporte2;
  |FINSI;
  |SI nCampo5 = 4;
      nImporte = nImporte + nImporte3;
      |SI nCampo2 = 5;  nImporte = nImporte + nImpB4; |FINSI;
  |FINSI;
  |SI nCampo5 = 5;
      nImporte = nImporte + nImporte4;
  |FINSI;
  |SI nCampo5 = 6;
      nImporte = nImporte + nImporte5;
  |FINSI;
  |SI nCampo5 = 7;
      nImporte = nImporte + nImporte6;
  |FINSI;
  |SI nCampo5 = 8;
      nImporte = nImporte + nImporte7;
  |FINSI;
  |SI nCampo5 = 9;
      nImporte = nImporte + nImporte8;
  |FINSI;

  |ABRE #3;
  nCampo6 = 0;
  nCampo7 = 0;
  nCampo8 = 0;
  |HAZ cargatra_3;
  |CIERRA #3;
  |SI nCampo5 = 1; nImporte1 = nImporte; |FINSI;
  |SI nCampo5 = 2; nImporte2 = nImporte; |FINSI;
  |SI nCampo5 = 3; nImporte3 = nImporte; |FINSI;
  |SI nCampo5 = 4; nImporte4 = nImporte; |FINSI;
  |SI nCampo5 = 5; nImporte5 = nImporte; |FINSI;
  |SI nCampo5 = 6; nImporte6 = nImporte; |FINSI;
  |SI nCampo5 = 7; nImporte7 = nImporte; |FINSI;
  |SI nCampo5 = 8; nImporte8 = nImporte; |FINSI;
  |SI nCampo5 = 9; nImporte9 = nImporte; |FINSI;
|FPRC;

|DEFBUCLE elTotal;
 #421#1;
 ;
 enEjerBal, nBalNum, nBalance, 1, "R", 00, 1, 00, 00, 0;
 enEjerBal, nBalNum, nBalance, 5, "R", 99, 9, 00, 00, 0;
 e;
 NULL, elTotal;
|FIN;

|PROCESO ElCalculoFinal;
 nImpB1 = 0; nImporte1 = 0;
 nImpB2 = 0; nImporte2 = 0;
 nImpB3 = 0; nImporte3 = 0;
 nImpB4 = 0; nImporte4 = 0;
 nImpB5 = 0; nImporte5 = 0;
 nImpB6 = 0; nImporte6 = 0;
 nImpB7 = 0; nImporte7 = 0;
 nImpB8 = 0; nImporte8 = 0;
 nImpB9 = 0; nImporte9 = 0;
 |BUCLE elTotal;
|FPRC;

|| ***********************************************
|PROCESO cargatra_5;  ||Carga Trabajo de Cuentas
  #5#0 = nBalNum;
  #5#1 = nBalance;
  #5#2 = #422#2;
  #5#3 = #422#3;
  #5#4 = #422#4;
  #5#5 = #422#5;
  #5#6 = #422#6;
  #5#7 = #422#7;
  #5#8 = #422#12;
  #5#9 = #422#8;
  #5#10 = aCuenta;
  |LEE 001#5=;
  |SI FSalida ! 0;
      |DDEFECTO #5;
      #5#0 = nBalNum;
      #5#1 = nBalance;
      #5#2 = #422#2;
      #5#3 = #422#3;
      #5#4 = #422#4;
      #5#5 = #422#5;
      #5#6 = #422#6;
      #5#7 = #422#7;
      #5#8 = #422#12;
      #5#9 = #422#8;
      #5#10 = aCuenta;
      |GRABA 020#5b;
  |FINSI;

  #5#11 = aTitulo;
  #5#12 = nLong;
  #5acumula = #5acumula + nImporte;
  |GRABA 020#5a;
  |LIBERA #5;

  |HAZ LosTotales;
|FPRC;

|PROCESO Busco;
  nImpoOri = nImporte;
  nImporte = - nImporte; || (H-D)

  |SI aAPR = "D"; |Y #422#10 = "-"; nImporte = - nImporte; |FINSI;
  |SI aAPR = "G"; |Y #422#10 = "-"; nImporte = - nImporte; |FINSI;
  |SI aAPR = "H"; |Y #422#10 = "+"; nImporte = - nImporte; |FINSI;
  |SI aAPR = "I"; |Y #422#10 = "+"; nImporte = - nImporte; |FINSI;

  |SI #422#11 ! " ";
      |SI #422#11 = "P"; |Y nImporte < 0; |ACABA; |FINSI;
      |SI #422#11 = "N"; |Y nImporte > 0; |ACABA; |FINSI;
  |FINSI;
  nSI = 1;
  |HAZ cargatra_5;
  nImporte = nImpoOri;
|FPRC;

|DEFBUCLE Busco;
 #422#2;
 ;
 enEjerBal, nBalNum, nBalance, aDCta;
 enEjerBal, nBalNum, nBalance, aHCta;
 ;
 NULL, Busco;
|FIN;

|PROCESO CargaVarios;
  aAlfa1 = aCtaExis; |QBF aAlfa1;
  aAlfa1 = aAlfa1 % 0;
  nLongE = aAlfa1;

  nSI = 0;
  |SI nLongE ] 5;
      aDCta = aCtaExis % 105;
      aHCta = aCtaExis % 105;
      |BUCLE Busco;
||  |SI nSI = 1; |ACABA; |FINSI;
  |FINSI;

  |SI nLongE ] 4;
      aDCta = aCtaExis % 104;
      aHCta = aCtaExis % 104;
      |BUCLE Busco;
||  |SI nSI = 1; |ACABA; |FINSI;
  |FINSI;

   aDCta = aCtaExis % 103;
   aHCta = aCtaExis % 103;
   |BUCLE Busco;
||  |SI nSI = 1; |ACABA; |FINSI;

   aDCta = aCtaExis % 102;
   aHCta = aCtaExis % 102;
   |BUCLE Busco;
|FPRC;

||......................................................
|PROCESO descarga;
  |HAZ digitos2;
  |SI d_error ! 0;  |ACABA;  |FINSI;

  linea = linea + 1;
  #4#0 = linea;
  #4#8 = #0#6;
  |LEE 001#4=;
  |SI FSalida = 0;  |BORRA 020#4a;  |FINSI;


  |DDEFECTO #4;
  #4#0 = linea;    || linea
  #4#8 = #0#6;     || estructura
  #4#1 = #1#0;     || cuenta
  #4#2 = #1#1;     || digito
  #4#3 = #1#2;     || descripcion

  mes_fin  = #0#7 + 11;  || mes iniciales
  #4e1     = #7mes_fin;    || exist. iniciales
  mes_fin  = #0#8 + 11;  || mes finales
  #4e2     = #7mes_fin;    || exist. finales
  descar = (#4e1 - #4e2) ! EURODB; || ... iniciales - finales
  tracan5 = descar;
  |SI descar ] 0;
      #4e3 = #7#5;     || cuenta descarga D
      #4e4 = #7#6;     || digito descarga D
  |SINO;
      #4e3 = #7#8;     || cuenta descarga H
      #4e4 = #7#9;     || digito descarga H
  |FINSI;
  |GRABA 020#4n;

  #1#0 = #4e3;
  #1#1 = #4e4;
  |LEE 000#1=;
  |SI FSalida = 0;
      nImporte = tracan5;
      aCtaExis = #1#0;
      aCuenta  = #1#0;
      nNivel   = #1#1;
      aTitulo  = #1#2;
      aAPR     = #1#5;  |HAZ MSigno08_e;
      |SI #0#4 = "S";   |HAZ CambioCta; |FINSI;
      |SI nImporte ! 0; |HAZ CargaVarios; |FINSI;
  |FINSI;
|FPRC;

|PROCESO lineades;
  #1#0 = #7#2;
  #1#1 = #7#3;
  |LEE 000#1=;
  |SI FSalida = 0; |Y #1#0 > doce;
      |HAZ descarga;
  |FINSI;
|FPRC;

|PROCESO pideexis;
  |ABRE #6;
  #6#0 = #0#6;
  |LEE 001#6=;
  |HAZ cue_per;
  |BUCLELIN 2lineades#6;
  |CIERRA #6;
|FPRC;

|| Explotacion -- NO
|PROCESO ver_exp;
 |SI #4e3 = #1#0; |Y #4e4 = #1#1;
     sw_exp = 1;
     descar = (#4e1-#4e2) ! EURODB;
     descn  = (descn + descar) ! EURODB;
 |FINSI;
|FPRC;

|DEFBUCLE cabasitu9;
 #4#1;
 ;
 #0#6, 001;
 #0#6, 999;
 e;
 NULL, ver_exp;
|FIN;

|PROCESO grabatra; || grabo todas
  |SI #0#4 = "S"; |HAZ CambioCta; |FINSI;
  |SI nImporte ! 0;
    nImporte = - nImporte; || (H-D)  IMPORTANTE
    |SI aAPR = "D"; |Y #422#10 = "-"; nImporte = - nImporte; |FINSI;
    |SI aAPR = "G"; |Y #422#10 = "-"; nImporte = - nImporte; |FINSI;
    |SI aAPR = "H"; |Y #422#10 = "+"; nImporte = - nImporte; |FINSI;
    |SI aAPR = "I"; |Y #422#10 = "+"; nImporte = - nImporte; |FINSI;
    |SI #422#11 ! " ";
        |SI #422#11 = "P"; |Y nImporte < 0; |ACABA; |FINSI;
        |SI #422#11 = "N"; |Y nImporte > 0; |ACABA; |FINSI;
    |FINSI;
    |HAZ cargatra_5;
  |FINSI;
|FPRC;

|PROCESO calculit;

  aCuenta = #1#0;
  nNivel  = #1#1;
  aTitulo = #1#2;
  aAPR    = #1#5; |HAZ MSigno08_e;
  |SI aLucrat ! "S";
      |SI aAPR  ! "D"; |Y aAPR ! "H";
        |Y aAPR  ! "G"; |Y aAPR ! "I";
          |ACABA;
      |FINSI;
  |FINSI;

  |HAZ digitos2;
  |SI d_error ! 0;  |ACABA;  |FINSI;

  aAlfa1 = #1#0; |QBF aAlfa1;
  aAlfa1 = aAlfa1 % 0;
  nLong  = aAlfa1;
  aCuenta = #1#0;
  nNivel  = #1#1;
  aTitulo = #1#2;
  nImporte = 0;

  sw_exp = 0;
  descn  = 0;
  |BUCLE cabasitu9;
  |SI sw_exp = 1; |ACABA;  |FINSI;

  |SI aLucrat ! "S";
      |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
            nImporte = nImporte + #1x;
      |SIGUE;
  |SINO;
      nImpC5 = 0; nImpC6 = 0; nImpC7 = 0; nImpC8 = 0;
      |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
            |SI x = 11;
                nImpC5 = (nImpC5 + #1x) ! EURODB;
            |SINO;
                y = x - 2; nImpC6 = (nImpC6 + #1y) ! EURODB;
                y = x - 1; nImpC7 = (nImpC7 + #1y) ! EURODB;
            |FINSI;
            nImpC8 = (nImpC8 + #1x) ! EURODB;
      |SIGUE;
      |SI swImp = 1; nImporte = nImpC8; |FINSI;
      |SI swImp = 2; nImporte = nImpC6 - nImpC7; |FINSI;
      |SI swImp ] 3;
          |SI swImp = 3; nImpC7 = 0; |FINSI;  ||solo debe
          |SI swImp = 4; nImpC6 = 0; |FINSI;  ||solo haber
          nImporte = nImpC6 - nImpC7;
      |FINSI;
  |FINSI;

  |HAZ grabatra;
|FPRC;

|DEFBUCLE cabasitu0;
 #1#1;
 3;
 aDCta, "S";
 aHCta, "S";
 e;
 NULL,calculit;
|FIN;

|PROCESO calculit_4;
 aAlfa1 = #422#9; |QBF aAlfa1;
 aDCta = (aAlfa1 + "            ") % 112;
 aHCta = (aAlfa1 + "zzzzzzzzzzzz") % 112;
 swImp = #422#13;
 |BUCLE cabasitu0;
|FPRC;

|DEFBUCLE cabasitu4;
 #422#1;
 ;
 enEjerBal, nBalNum, nBalance, 1, "R", 00, 0, 00, 00, 0, 001;
 enEjerBal, nBalNum, nBalance, 5, "R", 99, 9, 99, 99, 9, 999;
 e;
 NULL,calculit_4;
|FIN;

|| ***************************************************************************
|| ***************************************************************************
|| ***************************************************************************
|| *********************  CALCULO DE IMPRESION *******************************
|| ***************************************************************************
|PROCESO HaySaldoSI;
  |SI #5#13 ! 0; |O #5#14 ! 0;
      nHay = 1;
      |ERROR10;
  |FINSI;
|FPRC;

|DEFBUCLE HaySaldoSI;
 #5#1;
 ;
 #3#0, #3#1, #3#2, #3#3, #3#4, nDCampo5, nDCampo6, nDCampo7, nDCampo8, "            ";
 #3#0, #3#1, #3#2, #3#3, #3#4, nHCampo5, nHCampo6, nHCampo7, nHCampo8, "zzzzzzzzzzzz";
 e;
 NULL, HaySaldoSI;
|FIN;

|PROCESO HaySaldo;
   nDCampo5 = #3#5; nHCampo5 = #3#5;
   |SI #3#5 = 0;
       nDCampo5 = 0;
       nHCampo5 = 9;
   |FINSI;
   nDCampo6 = #3#6; nHCampo6 = #3#6;
   |SI #3#6 = 0;
       nDCampo6 = 00;
       nHCampo6 = 99;
   |FINSI;
   nDCampo7 = #3#7; nHCampo7 = #3#7;
   |SI #3#7 = 0;
       nDCampo7 = 00;
       nHCampo7 = 99;
   |FINSI;
   nDCampo8 = #3#8; nHCampo8 = #3#8;
   |SI #3#8 = 0;
       nDCampo8 = 0;
       nHCampo8 = 9;
   |FINSI;
   nHay = 0;
   |BUCLE HaySaldoSI;
|FPRC;

|PROCESO cuentas;
  eaCuenta = #5#10;
  |SI aparam = "chile";
      |HAZ eParamChile;
      |SI enErChile = 0;
          #5#10 = eaC1Chile;
          #5#11 = eaC2Chile;
      |SINO;
          #5#11 = "Error cuenta sin Conversion ";
      |FINSI;
  |FINSI;
  cantidad  = #5#13;
  cantidad2 = #5#14;
  |SI cantidad ! 0; |O cantidad2 ! 0; |O eaValeCero ! "N";
       |PRINT;
  |FINSI;
  sw_inf = 4;
  #5#10 = eaCuenta;
|FPRC;

|DEFBUCLE cabasitu2;
 #5#1;
 ;
 #3#0, #3#1, #3#2, #3#3, #3#4, #3#5, #3#6, #3#7, #3#8, "            ";
 #3#0, #3#1, #3#2, #3#3, #3#4, #3#5, #3#6, #3#7, #3#8, "zzzzzzzzzzzz";
 e;
 NULL, cuentas;
|FIN;

|PROCESO agrupacion;
  sw_epi = 1;                || fuerza la impresion del epigrafe
|FPRC;

|PROCESO impresio;

  |SI #3#9 ! "A"; nImprimir = 0; |FINSI;

  |SI #3#13 = 0; |Y #3#14 = 0; |Y #3#6 ! 0; |Y #3#9 ! "A";
      |SI eaValeCero = "N";
          nHay = 0;
          |SI #0#4 = "S"; |HAZ HaySaldo; |FINSI;
          |SI nHay = 0; |ACABA; |FINSI;
      |FINSI;
  |FINSI;

  |SI sw = 0;
      tipo     = #3#3;  ||Activo o Pasivo
      agrupaci = #3#4;
      subagrup = #3#5;
      |HAZ agrupacion;
      sw = 1;
  |FINSI;

  |SI agrupaci ! #3#4; |O subagrup ! #3#5;
      sw_inf = 5;  |PRINT;       || linea en blanco
      |HAZ agrupacion;
      agrupaci = #3#4;
      subagrup = #3#5;
  |FINSI;

  |SI #3#9 = "T";
      sw_inf = 0;
      |SI #3#5 = 0; |Y #3#6 = 0; sw_inf = 6; |FINSI;
  |FINSI;
  |SI #3#9 = "S"; sw_inf = 0; |FINSI;
  |SI #3#9 = "C";
      sw_inf = 2;
      |SI #3#5 = 0; |Y #3#7 = 0; sw_inf = 0; |FINSI;  ||es un total con Cuentas
  |FINSI;
  |SI #3#9 = "A"; sw_inf = 7; |FINSI;

  |SI #3#9 ! "A"; |O nImprimir = 1;
      |PRINT;
      nImprimir = 1;
  |FINSI;

  |SI #3#9 = "C"; |Y #0#4 = "S";
      sw_inf=3;
      |BUCLE cabasitu2;
   |FINSI;
|FPRC;


|DEFBUCLE cabasitu1;
 #3#1;
 ;
 nBalNum, nBalance, 1, "R", 00, 0, 00, 00, 0;
 nBalNum, nBalance, 5, "R", 99, 9, 99, 99, 9;
 e;
 NULL,impresio;
|FIN;

|| ---------------------------------------------
|PROCESO GrabaAuxiliar;

     |DDEFECTO #3;
     #3#0  = #421#0;
     #3#1  = #421#1;
     #3#2  = #421#2;
     #3#3  = #421#3;
     #3#4  = #421#4;
     #3#5  = #421#5;
     #3#6  = #421#6;
     #3#7  = #421#7;
     #3#8  = #421#10;
     #3#9  = #421#8;
     #3#10 = #421#9;
     |GRABA 000#3n;

 |LIBERA #421;
|FPRC;

|DEFBUCLE GrabaAuxiliar;
 #421#1;
 ;
 enEjerBal, nBalNum, nBalance, 1, "R", 00, 0, 00, 00, 0;
 enEjerBal, nBalNum, nBalance, 5, "R", 99, 9, 99, 99, 9;
 e;
 NULL, GrabaAuxiliar;
|FIN;

|| *****************************************************************
|PROCESO PongoCeros;
    activo = 0;
    pasivo = 0;
    activ1 = 0;
    pasiv1 = 0;
    explo = 0;
    expl1 = 0;
|FPRC;

|PROCESO HazTodo;
|SI nOpSegur = 3; |Y nOpcion = 2;
   |PATH_DAT #1  dirfich0;
   |PATH_DAT #6  dirfich0;
   |PATH_DAT #7  dirfich0;
   |PATH_DAT #8  dirfich0;
|FINSI;

|SI aparam ! "PDF";

    |SI nOpcion = 1;
        |SI nYaInforme = 0;
            |INFOR informe;
            |SI FSalida ! 0;
                |SI nSwMen = 0;  |MENAV "     Error en informe."; |FINSI;
                |SI nSwLInv = 0;
                    |SI enSwMvo = 0;
                        |FINIMP;
                    |FINSI;
                |FINSI;
                nErrorI = 1;
                |ACABA;
            |FINSI;
            nYaInforme = 1;
        |FINSI;
    |SINO;
        |SI nYaInforme = 0;
            eaInforme = informe;
            |HAZ ElIMPREArchi;
            |SI swErrorDSArchi = 1;
                |ACABA;
            |FINSI;
            nYaInforme = 1;
         |FINSI;
    |FINSI;

|| ////////////////////////////////////////////////////////////////////////
    eaIa = #canempre#21;
    enM1 = #0#0;
    enM2 = #0#1;
    |HAZ LeeDatosEmpresa;
    |SI aparam = "fech"; |O aparam = "Situacionfech";
        |SI enM1 ! enM1f;
            NumMes = enM1f; |HAZ NombreMes; #0#2 = NombreMes;
        |FINSI;
        |SI enM2 ! enM2f;
            NumMes = enM2f; |HAZ NombreMes; #0#3 = NombreMes;
        |FINSI;
    |FINSI;

    |SI enMAper ! dig1;
        |SI #0#1 < enMAper; |Y #0#1 ! 0;
            |ACABA;
        |FINSI;
    |FINSI;
|FINSI;

|| ... Observaciones DsArchi
       eaObservaciones = "";
       |SI #0#0 = #0#1;
           eaObservaciones = "DE " + #0#2;
       |SINO;
           NombreMes = #0#2; |QBF NombreMes;
           eaObservaciones = "DE " + NombreMes + " A ";
           NombreMes = #0#3; |QBF NombreMes;
           eaObservaciones = eaObservaciones + NombreMes;
       |FINSI;
       eaObservaciones = eaObservaciones > " ";

  |SI nOpSegur ! 3; |O nOpcion = 1;
      |SI #0#17 = "S"; |HAZ conexi; |FINSI;
  |FINSI;

  nLongT = #0#5;
  |SI #0#4 = "N";
      |SI dig3 = 1; nLongT = dig4; nivel = 1; |FINSI;
      |SI dig3 = 2; nLongT = dig5; nivel = 2; |FINSI;
      |SI dig3 = 3; nLongT = dig6; nivel = 3; |FINSI;
      |SI dig3 = 4; nLongT = dig7; nivel = 4; |FINSI;
      |SI dig3 = 5; nLongT = dig8; nivel = 5; |FINSI;
      |SI dig3 = 6; nLongT = dig9; nivel = 6; |FINSI;
  |SINO;
      nivel = 0;
      |SI nLongT = dig4; nivel = 1; |FINSI;
      |SI nLongT = dig5; nivel = 2; |FINSI;
      |SI nLongT = dig6; nivel = 3; |FINSI;
      |SI nLongT = dig7; nivel = 4; |FINSI;
      |SI nLongT = dig8; nivel = 5; |FINSI;
      |SI nLongT = dig9; nivel = 6; |FINSI;
  |FINSI;

  |DELINDEX #4;
  |HAZ PongoCeros;

  nBalNum = #0#26;
  |SI espe17 ! "S"; |O nSw = 0;
      |DELINDEX #3;
      |DELINDEX #5;
      |ABRE #3;
      |BUCLE GrabaAuxiliar;
      |CIERRA #3;
  |FINSI;

  nSw = 1;
  |DBASE aMas;
  aDef = aMas + "def/cacuenta";
  |CARGA_DEF aDef, cuentas@;
  |SI FSalida ! 0;
      |MENAV "    Error al Carga fichero de la Tabla Equivalencia";
      |ACABA;
  |FINSI;
  |PATH_DAT #901 dirfich0;

  eldesde = (#0#0 * 3) + 11;
  elhasta = (#0#1 * 3) + 11;

  |PARA dos = 1; |SI dos [ 2; |HACIENDO dos = dos + 1;

       |NOME_DAT #1   aNomCta;
       |NOME_DAT #901 aNomCta;
       acumula = 13;
       e1 = 4;
       e2 = 5;
       e3 = 6;
       e4 = 7;

       |SI dos = 2; |Y #0#13 = "N";
           activ1 = activo;
           pasiv1 = pasivo;
           expl1 = explo;
           |SAL;
       |FINSI;

       |SI dos = 2;
           activ1 = activo;
           pasiv1 = pasivo;
           expl1 = explo;
           activo = 0;
           pasivo = 0;
           explo = 0;

           debe1 = 0;
           haber1 = 0;
           acumula = 14;
           e1 =  9;
           e2 = 10;
           e3 = 11;
           e4 = 12;

          |DBASE dir_fich;
          dir_fich = dir_fich + "fich/";
          alfa1 = #0#14;   alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
          alfa2 = #0#15;   alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
          alfa3 = dir_fich + alfa1 + alfa2 + "/";

          |PATH_DAT #1   alfa3;
          |PATH_DAT #901 alfa3;
          |PATH_DAT #6 alfa3;
          |PATH_DAT #7 alfa3;
          |PATH_DAT #8 alfa3;

          |ABRE #30;
          #30#2 = #0#14;
          #30#3 = #0#15;
          |LEE 011#30=;
          |CIERRA #30;
          dig3 = #30#13; dig4 = #30#14; dig5 = #30#15;
          dig6 = #30#16; dig7 = #30#17; dig8 = #30#18; dig9 = #30#19;

          pathbal = dirfich0;
          dirfich0 = alfa3;
          |HAZ FecApeCie;
          dirfich0 = pathbal;
          |SI enMAper ! #30#11;
              |SI #0#1 < enMAper; |Y #0#1 ! 0; |SAL; |FINSI;
          |FINSI;
  |FINSI;

  |SI dos = 1;
      r_emp = empre;
      r_per = ejerc;
  |SINO;
      r_emp = #0#14;
      r_per = #0#15;
  |FINSI;

  |ABRE #8;
  |DDEFECTO #8;
  #8#0 = 1;
  |LEE 000#8=;
  |CIERRA #8;
  |SI #8#2 = "S";  |Y #8#3 = "N";  |Y aparam = "";
      |SI dos = 2;
          pathbal = alfa3;
      |SINO;
          |DFICE pathbal;
      |FINSI;
      balance = 1;
      |SI #caconimp#10 = "S";
           enSwOper = 2;
           |SI dos = 1;
               r_emp = empre;
               r_per = ejerc;
           |SINO;
               r_emp = #0#14;
               r_per = #0#15;
           |FINSI;
           |SUB_EJECUTA "carecsig";
           enSwOper = 0;
           |SI enECuadre = 1; |ACABA; |FINSI;
           |NOME_DAT #1 eaNomFCta;
           |SI dos = 1; aNomCta1 = eaNomFCta; |FINSI;
      |SINO;
           |SUB_EJECUTA "careccue";
      |FINSI;
 |FINSI;

|| ---------------------------------------------------------------------
  saldo = 0;
  |ABRE #1;
  |ABRE #901;
  |ABRE #410;
  |ABRE #3;
  |ABRE #4;
  |ABRE #5;
  |HAZ pideexis;        || calcula existencias y graba
  |CIERRA #1;
  |CIERRA #4;

  |BUCLE cabasitu4;     || calcula resto de cuentas
  |CIERRA #5;
  |CIERRA #3;
  |HAZ ElCalculoFinal;

  |CIERRA #410;
  |CIERRA #901;
|SIGUE; || ==================================================================
   |DESCARGA_DEF #901;

   |SI #0#13 = "S";
       |ABRE #30;
       #30#2 = empre;
       #30#3 = ejerc;
       |LEE 011#30=;
       |CIERRA #30;
       dig3 = #30#13; dig4 = #30#14; dig5 = #30#15;
       dig6 = #30#16; dig7 = #30#17; dig8 = #30#18; dig9 = #30#19;
   |FINSI;

   |SI espe17 ! "S";
       |SI aparam ! "PDF";
           sw = 0;
           sw_pagina = 0;
           |BUCLE cabasitu1;
           |HAZ final;
           |PIEINF;
           |FININF;
           nYaInforme = 0;
           |SI nOpcion = 2;
               |FINIMP;
               |SI swErrorDSArchi = 0;
                   |HAZ Archivando;
               |FINSI;
               Impresora = "CrystalReport";
           |FINSI;
       |FINSI;
   |FINSI;
|FPRC;

|PROCESO impre; |TIPO 3;
  eaHora = (("00" + #0#31) % -102) + ":" + (("00" + #0#32) % -102):
  eaHora = "Hora: " + eaHora;
  |SI #8#18 = "N"; eaHora = ""; |FINSI;

  |HAZ antesque;

  aFichero = ("8t" + Usuario) % 108;
  |NOME_DAT #3 aFichero;|DELINDEX #3;

  aFichero = ("8e" + Usuario) % 108;
  |NOME_DAT #4 aFichero;|DELINDEX #4;

  aFichero = ("8c" + Usuario) % 108;
  |NOME_DAT #5 aFichero;|DELINDEX #5;

  |SI nArchi = 1;
      |MENU aMenu;
      nOpcion = FSalida;
      |SI nOpcion = 0;
          |ACABA;
      |FINSI;
  |SINO;
       nOpcion = 1;
  |FINSI;

  |SI nOpcion = 3;  |SALVA_DATOS #0;  |FINSI;

   nOpSegur = nOpcion;
   |SI nOpSegur = 1; |O nOpSegur = 3;
       nOpcion = 1;
       |HAZ impre_Uno;
       |SI nOpSegur = 3;  |REPON_DATOS #0;  |FINSI;
   |FINSI;

   |SI nOpSegur = 2; |O nOpSegur = 3;
       nOpcion = 2;
       |HAZ impre_Uno;
   |FINSI;
   eSwSuma = 0;
   espe17  = "";
|FPRC;

|PROCESO impre_Uno;
|SI aparam ! "PDF";

   ||Archivar
   |SI nOpcion = 2;
       Impresora = "CrystalReport";
   |FINSI;

  |SI nOpcion = 1;

      |SI aparam ! ""; |Y aparam ! "fech"; |Y aparam ! "Analitica";
         |Y aparam ! "opt"; |Y aparam ! "chile";
            |Y aparam ! "Impuesto"; |Y aparam ! "Situacion";
            |Y aparam ! "Situacionfech";
          |SI nSwLInv = 0;
              Impresora = eaNomImp + "?" + eaUnidadBTmp + "\tmp1,,,?";
              |SI Compara = 1;
                  Impresora = Impresora + "Bal.Res.Comp";
              |SINO;
                  Impresora = Impresora + "Bal.Res.";
              |FINSI;
              |SI enBasImp ! 0;
                  |IMPRE -1;
              |SINO;
                  |IMPRE 0;
              |FINSI;
              |SI FSalida ! 0;  |ACABA;  |FINSI;
          |FINSI;
      |SINO;
          |SI enSwMvo = 0;
              Impresora = "Crystal Reports";
              |IMPRE 0;
              |SI FSalida ! 0;  |ACABA;  |FINSI;
          |SINO;
              Impresora = eaImMvo;
          |FINSI;
      |FINSI;
  |FINSI;

   |HAZ los_informes;
   |SI #0#13 = "N";
       informe = que_i1;
   |SINO;
       informe = que_i2;
   |FINSI;

   enDatos = 0; |SI #0#18 = "S"; enDatos = 1; |FINSI;
   aAlfa = Impresora;
   aAlfa = aAlfa % 113;
   |SI aAlfa = "CrystalReport";
       enSwComp = 0;
       informe = "cc" + (informe % 0306);
       aAlfa = informe % -301;
       |SI aAlfa = "c";
          enSwComp = 1;
          informe = (informe % 0105) + "0" + (informe % 702);
       |FINSI;
       |SI enSwMvo = 0; |Y nArchi = 0;
           |CONFI "0000NOrientacion horizontal";
           |SI FSalida = 0;
               informe = (informe % 0103) + "x" + (informe % 504);
           |FINSI;
       |FINSI;
   |FINSI;
|FINSI;

  |DFICE dirfichOri;
   nSw = 0;
   CodigoEmpresa    = enEmpresa;   nEmpTodo = enEmpresa;
   EjercicioEmpresa = enEjercicio;
   NombreEmpresa    = eaNombreEmp; nPerTodo = enPeriodo;

   aAlfa1 = #0#29; |QBF aAlfa1;
   aAlfa1 = aAlfa1 + " " + eaNomIBal;
   #0#29 = aAlfa1;

   |SI aparam = ""; |O aparam = "fech";
       |O aparam = "Analitica"; |O aparam = "chile";

       |SI #48#27= "S"; |Y aparam ! "Analitica"; |Y enSwMvo = 0;
           |HAZ MultiEmpresa;
       |SINO;
           aMonedaA = eaMoneda;
           |DFICE dirfich0;
           |HAZ HazTodo;
       |FINSI;
   |SINO;
       aMonedaA = eaMoneda;
       |HAZ HazTodo;
   |FINSI;
   eSwSuma = 0;
   espe17  = "";

|SI aparam ! "PDF";
   || ... |FININF;
   |SI nOpcion = 1;
       |SI nSwLInv = 0;
           |SI enSwMvo = 0;
               |FINIMP;
           |FINSI;
       |FINSI;
   |FINSI;

   |DELINDEX #3;
   |DELINDEX #4;
   |DELINDEX #5;
|FINSI;

|FPRC;

|PROCESO final;
  sw_inf = 5;
  sw_inf = 1;
|FPRC;

|PROGRAMA;

   nSwMen = 1;
   aparam = PARAMETRO; |QBF aparam;
   |SI aparam = ""; |O aparam = "Analitica"; |O aparam = "fech";
     |O aparam = "otp"; |O aparam = " N"; |O aparam = "chile";
       |HAZ inicial;
       |SI swerror = 2; |PTEC 807; |ACABA; |FINSI;
       |SI swerror = 3; |PTEC 831; |PTEC 807; |ACABA; |FINSI;
       nSwMen = 0;
   |FINSI;
   |SI enEjercicio < 2008;
       |SI nSwMen = 0; |MENAV "    Informe para la Estructuras del 2008, no factible en este Ejercicio"; |FINSI;
       |ACABA;
   |FINSI;
   |HAZ DirAplicSt;

   |HAZ LosPatrones;

   nArchi = 0;
   anu = enEjercicio;
   nBalance = 1; || Cuenta de Perdidas y Ganancias
                 || eNum2008 = Codigo de las Cuentas de Mayor

   |SI aparam = "PDF";
       |DDEFECTO #0;
       #0#0  = nCTodo0;
       #0#1  = nCTodo1;
       #0#2  = aCTodo2;
       #0#3  = aCTodo3;
       #0#4  = aCTodo4;
       #0#5  = nCTodo5;
       #0#6  = nCTodo6;
       #0#7  = nCTodo7;
       #0#8  = nCTodo8;
       #0#12 = fCTodo12;
       #0#13 = aCTodo13;
       #0#14 = nCTodo14;
       #0#15 = nCTodo15;
       #0#17 = "N";
       #0#18 = aCTodo18;
       #0#26 = nCTodo32;
       |HAZ descri;
       |SI nSwErrorBal = 1; |ACABA; |FINSI;
       |PATH_DAT #1  dirfich0;
       |PATH_DAT #6  dirfich0;
       |PATH_DAT #7  dirfich0;
       |PATH_DAT #8  dirfich0;
       #8#18  = "N";
       |HAZ impre;
       |ACABA;
   |FINSI;

   |SI aparam = "Impuesto";
       |O aparam = "Situacion";
         |O aparam = "Situacionfech";
       enSwMvo = 1;
       |PATH_DAT #0  dirfich0;
       |PATH_DAT #1  dirfich0;
       |PATH_DAT #6  dirfich0;
       |PATH_DAT #7  dirfich0;
       |PATH_DAT #8  dirfich0;
   |FINSI;

   |SI aparam ! ""; |Y aparam ! "Analitica"; |Y aparam ! "fech";
        |Y aparam ! "otp"; |Y aparam ! " N"; |Y aparam ! "chile";
           |Y aparam ! "Impuesto"; |Y aparam ! "Situacion";
           |Y aparam ! "Situacionfech";
       |HAZ Legalia;
   |SINO;
       |SI enSwMvo = 0;
           |CLS;
           |CABEZA "Cuenta de Perdidas y Ganancias";
       |FINSI;

       |ABRE #0;
       #0#28 = "A";
       |LEE 041#0=;
       |SI FSalida ! 0;
           |DDEFECTO #0;
           #0#6 = 1;
           |SI eNum2008 = 1;  #0#26 = 1;  |FINSI;
           |SI eNum2008 = 20; #0#26 = 20; |FINSI;
           |SI eNum2008 = 21; #0#26 = 21; |FINSI;
           |SI Haybasica = 1; |Y eaDepar = "S";  #0#6 = 0; |FINSI;
           |HAZ DatosEmp;
           |GRABA 001#0c;
       |SINO;
          |SI enSwMvo ! 0;
              |PDEFECTO #0, 0, 18;
          |FINSI;
       |FINSI;

       #0#12 = ~;
       |SI aparam = "Impuesto";
            #0#4 = eaCtasLeg;
            #0#5 = enCtasLeg;
       |FINSI;

       |SI aparam = "Situacion"; |O aparam = "Situacionfech";
           |DDEFECTO #0;
           #0#0  = nCTodo0;
           #0#1  = nCTodo1;
           #0#2  = aCTodo2;
           #0#3  = aCTodo3;
           #0#4  = aCTodo4;
           #0#5  = nCTodo5;
           #0#6  = nCTodo6;
           #0#7  = nCTodo7;
           #0#8  = nCTodo8;
           #0#9  = nCTodo9;
           #0#10 = nCTodo10;
           #0#11 = aCTodo11;
           #0#12 = fCTodo12;
           #0#13 = aCTodo13;
           #0#14 = nCTodo14;
           #0#15 = nCTodo15;
           #0#17 = "N";
           #0#18 = aCTodo18;
           #0#26 = nCTodo32;
           |HAZ descri;
           |SI nSwErrorBal = 1; |ACABA; |FINSI;
       |FINSI;

       |SI enSwMvo = 0;
           |PINPA #0#0;
           |SI aparam = "chile";
               #0#4 = "S";
               #0#5 = 4;
               |C_M #0#5,1, "N";
               |ATRI R; |PINTA 0534, "ESTRUCTURA CONTABILIDAD DE CHILE"; |ATRI 0;
           |FINSI;
           |SI aparam = ""; |O aparam = " N";
               |HAZ ActivaPLB_arc1;
               eaVengoDe = "ca8ye001";
               |HAZ MiraSiArchiva;
           |FINSI;

           |PINDA #0#0;

           |ENDATOS #1#0;
           |SI FSalida = 0;
              #0#29 = #0#29 - eaNomIBal;
              |GRABA 001#0a;
           |FINSI;
       |SINO;
           |HAZ impre;
       |FINSI;
       |CIERRA #0;
   |FINSI;
|FPRO;

|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   |SI enEjercicio < 2008; |ACABA; |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |SI #0#13 = "S"; #0#14 = #caselem1#1; |FINSI;

   CodigoEmpresa    = enEmpresa;
   EjercicioEmpresa = enEjercicio;
   NombreEmpresa    = eaNombreEmp;

   nYaLoHeDicho = 0;

   |PATH_DAT #1  dirfich0;
   |PATH_DAT #6  dirfich0;
   |PATH_DAT #7  dirfich0;
   |PATH_DAT #8  dirfich0;

   aMonedaA = eaMoneda;
   |SI #0#13 = "N"; aMonedaC = aMonedaA; |FINSI;
   |HAZ HazTodo;
   |SI nErrorI = 1; |ERROR10; |FINSI;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 000001;
 999999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SI nOpSegur ! 3; |O nOpcion = 1;
      eSwSuma = 0; espe17  = "";
      |SI #8#17 = "S"; |Y aparam = ""; eSwSuma = 1; |FINSI;

      |SUB_EJECUTA "caselemp";
      |NOME_DAT #998 eaFichsel;
  |FINSI;

  |BUCLE MultiEmp0;
  |SI nOpSegur ! 3; |O nOpcion = 2;
      |DELINDEX #998;
  |FINSI;

  |SI espe17 = "S"; |Y nSw ! 0;
      cod1 = nEmpTodo;
      cod2 = nPerTodo;
      |HAZ leelo;

      dirfich0 = dirfichOri;
      eaIa = #canempre#21;
      enM1 = #0#0;
      enM2 = #0#1;
      |HAZ LeeDatosEmpresa;

      aAlfa1 = #cawdatos#2; |QBF aAlfa1;
      aAlfa1 = aAlfa1 + " (Consolidada)";
      #cawdatos#2 = aAlfa1;

      sw = 0;
      sw_pagina = 0;
      |BUCLE cabasitu1;
      |HAZ final;
      |PIEINF;
      |FININF;
      nYaInforme = 0;
      |SI nOpcion = 2;
          |FINIMP;
          |SI swErrorDSArchi = 0;
              |HAZ Archivando;
          |FINSI;
          Impresora = "CrystalReport";
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO Legalia;
    nSwMen = 1;
    Comodin = PARAMETRO;
    Comodin = Comodin - ",C";
    |SI FSalida ! 0;
        Compara = 1;
    |SINO;
        Compara = 0;
    |FINSI;
    Comodin = Comodin - ",L";
    |SI FSalida ! 0;
        nSwLInv = 1;
    |SINO;
        nSwLInv = 0;
    |FINSI;
    Comodin = Comodin - ",";
    |SI FSalida ! 0;
        Calc = ((FSalida / 100) ! 0) + 99;
        Comodin1 = Comodin % 0101;
        Per1 = Comodin1; Comodin = Comodin - Comodin1;
        Per2 = Comodin;
    |FINSI;

    cod1 = eaLegEmp;
    cod2 = eaLegPer;
    emEmp = 1;  || Sin mensaje
    |HAZ leelo;
    |SI swerror ! 0;  |ACABA;   |FINSI;
    empre = eaLegEmp;
    ejerc = eaLegPer;

    |DBASE fich_alta; |QBF fich_alta;
    dirfich0 = fich_alta + "fich/" + eaLegEmp + eaLegPer + "/";

    |PATH_DAT #0  dirfich0;
    |PATH_DAT #1  dirfich0;
    |PATH_DAT #6  dirfich0;
    |PATH_DAT #7  dirfich0;
    |PATH_DAT #8  dirfich0;

    |ABRE #8;
    |DDEFECTO #8;
    #8#0 = 1;
    |LEE 000#8=;
    |CIERRA #8;

    |ABRE #0;
    |DDEFECTO #0;
    #0#28 = "A";
    |LEE 041#0=;
    |SI FSalida ! 0;
        #0#6 = 1;
        |SI Haybasica = 1; |Y eaDepar = "S";  #0#6 = 0; |FINSI;
        |SI eNum2008 = 1;  #0#26 = 1;  |FINSI;
        |SI eNum2008 = 20; #0#26 = 20; |FINSI;
        |SI eNum2008 = 21; #0#26 = 21; |FINSI;
        |HAZ DatosEmp;
        |GRABA 001#0n;
    |FINSI;
    |CIERRA #0;

    |HAZ descri;
    |SI nSwErrorBal = 1; |ACABA; |FINSI;

    |SI Compara = 1;
         #0#13 = "S";
         #0#14 = eaLegEmp;
         |SI eaLegPer > 0;
             #0#15 = eaLegPer - 1;
         |SINO;
             #0#15 = 9;
         |FINSI;
         |HAZ esbuena1;
         |SI swNoExiste = 1;
             #0#13 = "N";
             Compara = 0;
         |FINSI;
     |SINO;
         #0#13 = "N";
     |FINSI;
     #0#0  = Per1; #0#7 = Per1;
     #0#1  = Per2; #0#8 = Per2;
     Campo = 0; |HAZ pintames;
     Campo = 1; |HAZ pintames;
     #0#4  = eaCtasLeg;
     |SI eaCtasLeg ! "S";
         #0#5 = MaximoDigitos;
     |SINO;
         |SI enCtasLeg = 0;
             |SI dig4 [ 4; |Y dig4 ! 0; #0#5   = 1; |FINSI;
             |SI dig5 [ 4; |Y dig5 ! 0; #0#5   = 2; |FINSI;
             |SI dig6 [ 4; |Y dig6 ! 0; #0#5   = 3; |FINSI;
             |SI dig7 [ 4; |Y dig7 ! 0; #0#5   = 4; |FINSI;
             |SI dig8 [ 4; |Y dig8 ! 0; #0#5   = 5; |FINSI;
             |SI dig9 [ 4; |Y dig9 ! 0; #0#5   = 6; |FINSI;
         |SINO;
              #0#5 = enCtasLeg;
         |FINSI;
     |FINSI;
     #0#6   = enDcarLeg;
     #0#17  = "N";
     #0#12  = efFecLeg;
     #0#18  = eaDatLeg;
     aMonedaA = eaMoneda;
     |SI #0#13 = "N"; aMonedaC = aMonedaA; |FINSI;
     #8#18  = "N";
     |HAZ impre;
|FPRC;
