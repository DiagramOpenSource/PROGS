|FICHEROS;
  calis500 #0;
  camaasil #1;
  cacuenta #2;
  caclipro #3;
  catmp500 #5;
  camaasic #10;

  canempre #30;
  caselem1 #998;
|FIN;


|VARIABLES;
  &entrada = 1;
  men1 = "     NO EXISTE IMPRESORA";
  men2 = "     NO EXISTE INFORME";
  informe = "";
  inform1 = "calis500";
  inform2 = "calis501";

  dfecha = @01.01.1900;
  hfecha = @01.01.2999;

  alfa1 = "";
  alfa2 = "";
  num = 0;
  swencontra = 0;

  &cabecera = "";
  &codcue = "";
  &nomcue = "";
  &cifcue = "";
  &dircue = "";
  &pobcue = "";
  &cp = "";
  &importe  = 0;

  cta = "";
  niv = 0;
  swcta = 0;
  sw = 0;
  swalgun = 0;

  p_error = 0;
  p_alfa1 = "";
  aFichero = "";

   Comodin = "";
   Cont    = 0;
   SwCont  = 0;
   SwError = 0;
   Dobles  = 0;
   Path = "";
   Comodin1 = "";
   ComodNum = 0;
   Empresa = 0;
   Periodo = 0;
   Dia = "";
   Mes1 = "";
   Mes2 = "";
   Year = "";
   FechaIni  = @;
   FechaFin  = @;
   aAlfa1    = "";
   nNume1    = 0;
   nCampo    = 0;
|FIN;

|INCLUYE dscont_i;

|PROCESO eldefecto; |TIPO 7;
 |SI eaMoneda = "E";
     #0#25 = 3005.06;
     #0#26 = "INFORME CTAS SALDO MAYOR A 3.005,06 EUROS / APUNTES";
 |SINO;
     #0#25 = 500000;
     #0#26 = "INFORME CTAS SALDO MAYOR A 500.000 PTAS / APUNTES";
 |FINSI;
 |PINTA #0#25;
 |PINTA #0#26;
|FPRC;

|PROCESO cuenta; |TIPO 0;
     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;
|FPRC;

|PROCESO nomodif;|TIPO 7;
  |SI #0#3 = "N";
      |C_M #0Campo,1,"N";
  |SINO;
      |C_M #0Campo,1,"S";
  |FINSI;
|FPRC;

|PROCESO simodifi;|TIPO 7;
  |SI #0#3 = "S";
      |C_M #0Campo,1,"N";
  |SINO;
      |C_M #0Campo,1,"S";
  |FINSI;
|FPRC;

|PROCESO LeeCaclipro;
   |DDEFECTO #3;
   #3#23 = "C";
   #3#10 = #1#4;
   |LEE 010#3=;
   |SI FSalida ! 0;
       |DDEFECTO #3;
       #3#23 = "P";
       #3#10 = #1#4;
       |LEE 010#3=;
   |FINSI;
|FPRC;

|PROCESO Asientos;

   |SI #0#3 = "S";
       |SI #1#6 < #0#4; |O #1#6 > #0#5;
           |ACABA;
       |FINSI;
   |SINO;
       SwError = 1;
       |PARA Cont = 6; |SI Cont [ 40; |HACIENDO Cont = Cont + 1;
             |SI #1#6 = #0Cont; SwError = 0; |SAL; |FINSI;
       |SIGUE;
       |SI SwError = 1; |ACABA; |FINSI;
   |FINSI;

   SwCont = 1;

   #5#0 = #1#4;
   |LEE 010#5=;
   |SI FSalida ! 0;
      |DDEFECTO #5;
      #5#0 = #1#4;
      |HAZ LeeCaclipro;
      #5#1 = #3#9;
      #5#3 = #3#23;
      #5#4 = #1#5;
      |GRABA 020#5n;
      |LEE 010#5=;
   |FINSI;

   aAlfa1 = #1#1;
   aAlfa1 = aAlfa1 % 402;
   nNume1 = aAlfa1;
   |SI nNume1 [ 3;                nCampo = 5; |FINSI;
   |SI nNume1 > 3; |Y nNume1 [ 6; nCampo = 6; |FINSI;
   |SI nNume1 > 6; |Y nNume1 [ 9; nCampo = 7; |FINSI;
   |SI nNume1 > 9;                nCampo = 8; |FINSI;

   |SI #0#24 = "D";
       |SI #1#8 = "D";
           #5#2 = #5#2 + #1#9;
           #5nCampo = #5nCampo + #1#9;
       |SINO;
           #5#2 = #5#2 - #1#9;
           #5nCampo = #5nCampo - #1#9;
      |FINSI;
   |SINO;
      |SI #1#8 = "D";
          #5#2 = #5#2 - #1#9;
          #5nCampo = #5nCampo - #1#9;
      |SINO;
          #5#2 = #5#2 + #1#9;
          #5nCampo = #5nCampo + #1#9;
      |FINSI;
   |FINSI;

   |GRABA 020#5a;
|FPRC;

|DEFBUCLE Asientos;
   #1#1;
   ;
   #0#41, dfecha, 000000, 0001;
   #0#42, hfecha, 999999, 9999;
   ;
   NULL,Asientos;
|FINSI;


|PROCESO Imprime;

   |SI #5#3 = "C"; |Y #5#2 < #0#25; |ACABA; |FINSI;
   |SI #5#3 = "P"; |Y #5#2 < #0#25; |ACABA; |FINSI;

   |DDEFECTO #3;
   #3#23 = #5#3;
   #3#10 = #5#0;
   #3#11 = #5#4;
   |LEE 001#3=;
   |SI FSalida ! 0;
       #2#0 = #5#0;
       |LEE 001#2=;
       |SI FSalida = 0;
           #3#1 = #2#2;
       |FINSI;
   |FINSI;
   codcue = #5#0;
   dircue = #3#2;
   pobcue = #3#6;
   nomcue = #3#1;
   cifcue = #3#9;
   cp     = (("00" + #3#3) % -102) + (("000" + #3#4) % -103);
   importe = #5#2;
   |SI cp = "00000"; cp = ""; |FINSI;

   |PRINT;
   SwCont = 1;
|FPRC;

|DEFBUCLE ImprimeCta;
    #5#1;
    ;
    #0#1;
    #0#2;
    ;
    NULL,Imprime;
|FIN;

|PROCESO HazTodo;

   aFichero = "xz" + Usuario;
   |NOME_DAT #5 aFichero;
   |DELINDEX #5;

   FechaIni = fec1;
   FechaFin = fec2;

   |ABRE #5;
   |ABRE #3;
   |ABRE #2;
   SwCont = 0;
   |BUCLE Asientos;
   |CIERRA #5;
   |SI SwCont ! 0;
       SwCont = 0;
       |BUCLE ImprimeCta;
       |SI SwCont = 1;
           |PIEINF;
       |FINSI;
   |FINSI;

   |CIERRA #2;
   |CIERRA #3;
|FPRC;

|CALCULO periodo;|TIPO 3;
   cabecera = #0#26;

   |IMPRE 0;
   |SI FSalida ! 0;
       |MENAV "    Error en Impresora";
       |ACABA;
   |FINSI;

   informe = inform1;
   |SI #0#43 = "S";
       informe = inform2;
   |FINSI;

   |INFOR informe;
   |SI FSalida ! 0;
       |MENAV "    Error en Impresora";
       |FINIMP;
       |ACABA;
   |FINSI;

   |SI #48#27 = "S";
       |HAZ MultiEmpresa;
   |SINO;
       |DFICE dirfich0; |QBF dirfich0;
       |HAZ HazTodo;
   |FINSI;

   |FININF;
   |FINIMP;
|FCAL;

|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #2 dirfich0;
   |PATH_DAT #3 dirfich0;
   |PATH_DAT #10 dirfich0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 001;
 999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;
