|FICHEROS;
   cactemod #00;
   camaasic #02;
   camaasil #03;
   cacuenta #05;
   camautoc #06;
   camautol #07;
   catrasi1 #09;
   camandia #10;
   caconasi #20;
   canempre #30;
   caselem1 #998;

   :/basica/def/agitab99 #99;
   modelo@  #912;
   espmod@  #913;
   caw00098 #901;
|FIN;

|VARIABLES;
   &entrada = 1;
   aParam = "";
   nParam = 0;
   aAlfa1 = "";
   aAlfa2 = "";
   aAlfa3 = "";
   aAlfa4 = "";
   nNume1 = 0;
   nNume2 = 0;
   nNume3 = 0;
   nNume4 = 0;
   fFech1 = @;
   fFech2 = @;
   fFechaAsto = @;
   nPara1 = 0;
   nModelo   = 0;
   nNumCampo = 0;
   swModelo = 0;

   diar = 0;
   fech = @;
   asie = 0;
   apun = 0;
   cuen = "";
   digi = 0;
   sign = "";
   ptas = 0;
   conc = 0;
   desc = "";

   sw = 0;
   estado = 0;

   dpar = "";
   subd = "";
   swpr = 0;
   documen = 0;
   &eaTabMod = "";
   nPerio    = 0;

   &enwModAu = 0;
   &enQueSoc = 0;
   &antdia  = 0;  ||Variables para el Informe de posibles errores
   &antfec  = @;
   &antdoc  = 0;
   &antapu  = 0;
   &lerror  = 0;
   &men     = "";
   informe = "cacheapu";
   swImpresora = 0;
   aQueConver  = "";
   nIncrApte   = 0;
   aElSigno    = "";
   nImporte    = 0;
   nEjer       = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoan_i;

|| ************************************************************************
|| *********** CALCULOS PANTALLA ******************************************
|| ************************************************************************
|CALCULO Mtipo8; |TIPO 8;
  aParam = PARAMETRO; |QBT aParam;
  |SI aParam ! "";
      aAlfa1 = aParam % 0105; nParam = aAlfa1;
      aAlfa1 = aParam % 0604; nEjer  = aAlfa1;
  |SINO;
      nParam = 0;
      nEjer  = 0;
  |FINSI;

  |HAZ inicio;
  |HAZ DirAplicSt;
  |SI nEjer = 0; nEjer = enEjercicio; |FINSI;

  enQueSoc = nEjer - 1;
  |SI Haybasica = 0;
      |MENAV "    ERROR, NO EXISTE APLICACION DE MODELOS ";
      |PTEC 807;
  |SINO;
      enwModAu = 0;
      |HAZ CargaAuxModelo;
      |NOME_DAT #901 eaTabMod;
      |HAZ LeeConversor;
  |FINSI;
|FCAL;

|CALCULO AntesAsiento; |TIPO 7;
 |SI nParam ! 0;
     |ABRE #6;
     #6#0 = nParam;
     |LEE 001#6=;
     |SI FSalida = 0; |Y #6#21 ! 0;
         #0#3 = #6#0; |PINTA #0#3;
         |C_M #0#3, 1, "N";
     |SINO;
         |MENAV "    Error en el Parametro, Asiento Fijo no correcto.";
         |MENAV "    Introduzca otro codigo";
         |C_M #0#3, 1, "S";
     |FINSI;
     |CIERRA #6;
 |FINSI;
|FCAL;

|CALCULO LeeAsiento; |TIPO 0;
 |ABRE #6;
 #6#0 = #0#3;
 |LEE 001#6=;
 |SI FSalida ! 0;
     |CIERRA #6;
     |MENAV "    Error, Asiento Fijo no existe ";
     |ERROR;
     |ACABA;
 |FINSI;
 |CIERRA #6;
 |SI #6#21 = 0;
     |C_M #0#3,1,"S";
     |MENAV "    Error, este Asiento Fijo no es para Modelo Oficial ";
     |ERROR;
     |ACABA;
 |FINSI;
 #0#0 = #6#21; |PINTA #0#0;
 |HAZ LeeModelo;
 |SI swModelo = 1;
     |C_M #0#3,1,"S";
     |MENAV "    Error en Asiento Fijo, No Existe MODELO";
     |ERROR;
     |ACABA;
 |FINSI;

 |SI #6#21 = 303;
     sw = 0;
     |SI nEjer ] 2014; |Y #6#3 = 1; sw = 1; |FINSI;
     |SI nEjer < 2014; |Y #6#3 ! 1; sw = 1; |FINSI;
     |SI sw = 1;
         |MENAV "2400El Formato del Modelo 303 no correspondiente a este Ejercicio. ";
         |SI nParam ! 0;
             |PTEC 807;
         |SINO;
             |C_M #0#3,1,"S";
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;
 |FINSI;

 #0#15 = #99#1; |PINTA #0#15;
 |SI #99#5 = "A";
     #0#1 = 99;
     #0#6 = enEjercicio - 1;
 |SINO;
     #0#6 = enEjercicio;
     |SI #0#0 = 202; #0#1 = 99; |FINSI;
 |FINSI;
 |PINTA #0#1;
 |PINTA #0#6;
|FCAL;

|PROCESO LeeModelo;
 swModelo = 0;
 |ABRE #99;
 #99#0 = #6#21;
 |SI #99#0 = 111; #99#0 = 110; |FINSI;
 |LEE 001#99=;
 |SI FSalida ! 0;
     swModelo = 1;
     #99#1 = "NO EXISTE MODELO";
 |FINSI;
 |CIERRA #99;
|FPRC;

|PROCESO VeoTabla1;
  swModelo = 0;
  |ERROR10;
|FPRC;

|DEFBUCLE VeoTabla0;
 #901#1;
 ;
 #0#0, 001;
 #0#0, 999;
 e;
 NULL, VeoTabla1;
|FIN;

|CALCULO VeoTabla;
 |C_M #0#1,  1, "S"; |C_V #0#1, 1, "S"; |PINTA #0#1;
 |C_M #0#16, 1, "N"; |C_V #0#16,1, "N";
 |SI #0#0 = 202;
     |SI #0#6 ] 2010;
         |C_V #0#16, 1, "S";
         |C_M #0#16, 1, "S"; |PINTA #0#16;
         |C_M #0#1,  1, "N"; |C_V #0#1, 1, "N"; #0#1 = 99;
     |FINSI;
 |FINSI;
 swModelo = 1;
 |BUCLE VeoTabla0;
 |SI swModelo = 1;
     |MENAV "    CONTABILIZACION DEL MODELO PENDIENTE !!! ";
     |ERROR;
 |FINSI;
 |SI #0#0 = 303;
     |SI nEjer ! 0;
         |SI nEjer < 2014;
             |SI #0#6 ] 2014;
                 |MENAV "1234Ejercicio del Modelo no correcto";
                 |ERROR;
             |FINSI;
         |SINO;
             |SI #0#6 < 2014;
                 |MENAV "1234Ejercicio del Modelo no correcto";
                 |ERROR;
             |FINSI;
         |FINSI;
     |FINSI;
 |FINSI;
|FCAL;

|CALCULO limite; |TIPO 0;
   |SI #0Campo < fec1; |O #0Campo > fec2;
      |MENAV "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
      |ERROR;
   |SINO;
      |SI #0Campo [ fec3;
          |MENAV "    ERROR FECHA INFERIOR O IGUAL AL BLOQUEO DE DATOS";
          |ERROR;
      |FINSI;
   |FINSI;
|FCAL;

|PROCESO Periodo;
     |C_M #0Campo, 0, aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     |SI Campo = 1;
         #0#2 = "          ";
         |SI #0#1 = 1;   #0#2 = "ENERO     ";  |FINSI;
         |SI #0#1 = 2;   #0#2 = "FEBRERO   ";  |FINSI;
         |SI #0#1 = 3;   #0#2 = "MARZO     ";  |FINSI;
         |SI #0#1 = 4;   #0#2 = "ABRIL     ";  |FINSI;
         |SI #0#1 = 5;   #0#2 = "MAYO      ";  |FINSI;
         |SI #0#1 = 6;   #0#2 = "JUNIO     ";  |FINSI;
         |SI #0#1 = 7;   #0#2 = "JULIO     ";  |FINSI;
         |SI #0#1 = 8;   #0#2 = "AGOSTO    ";  |FINSI;
         |SI #0#1 = 9;   #0#2 = "SEPTIEMBRE";  |FINSI;
         |SI #0#1 = 10;  #0#2 = "OCTUBRE   ";  |FINSI;
         |SI #0#1 = 11;  #0#2 = "NOVIEMBRE ";  |FINSI;
         |SI #0#1 = 12;  #0#2 = "DICIEMBRE ";  |FINSI;
         |SI #0#1 = 99;  #0#2 = "ANUAL     ";  |FINSI;

         |SI #0#2 = "          ";
             |MENAV "     Periodo Incorrecto.";
             |ERROR;
         |FINSI;
     |FINSI;

     |SI Campo = 16;
         #0#2 = "          ";
         |SI #0#16 = "1P";   #0#2 = "1§ PERIODO";  |FINSI;
         |SI #0#16 = "2P";   #0#2 = "2§ PERIODO";  |FINSI;
         |SI #0#16 = "3P";   #0#2 = "3§ PERIODO";  |FINSI;

         |SI #0#2 = "          ";
             |MENAV "     Periodo Incorrecto.";
             |ERROR;
         |FINSI;
     |FINSI;

     |PINTA #0#2;
|FPRC;
|| ///////////////////////////////////////////////////////////////////////
|| ///////////////////////////////////////////////////////////////////////
|PROCESO CalImporte;
  ptas = 0;

  nModelo   = #6#21;
  nNumCampo = #7#10;

  |ABRE #901;
  #901#0 = nModelo;    || Numero modelo
  #901#1 = nNumCampo;  || Campo
  |LEE 001#901=;
  |SI FSalida ! 0;
      |CIERRA #901;
      |ACABA;       || NO PASA LINEA
  |FINSI;
  |CIERRA #901;

  aAlfa1 = #901#4; |QBF aAlfa1;   || Fichero
  |SI aAlfa1 = "";     || No PASA
      |ACABA;
  |FINSI;
  aAlfa2 = #901#5; |QBF aAlfa2;   || Aplicacion
  |SI aAlfa2 = "";     || No PASA
      |ACABA;
  |FINSI;

  aQueConver = aAlfa2;  ||Para saber que moneda tiene la aplicacion

  |DBASS aAlfa4;
  aAlfa3 = aAlfa4 + aAlfa2 + "/def/" +  aAlfa1;
  aAlfa4 = aAlfa4 + aAlfa2 + "/fich/";

  |CARGA_DEF aAlfa3, modelo@;
  |SI FSalida = 0;
      |PATH_DAT #912 aAlfa4;
      |CARGA_DEF aAlfa3, espmod@;
      |SI FSalida = 0;
          |PATH_DAT #913 aAlfa4;
          |HAZ LeePorModelo;
          |DESCARGA_DEF #913;
      |FINSI;
      |DESCARGA_DEF #912;
  |FINSI;
|FPRC;

|| ///////////////////////////////////////////////////////////////////////
|| ///////////////////////////////////////////////////////////////////////
|PROCESO leecta;
   sign = "D";
   ptas = 0;
   |ABRE #5;
   #5#0 = cuen;
   |LEE 001#5=;
   |SI FSalida ! 0;
       |DDEFECTO #5;
   |FINSI;
   |SI #5#53 < 0; sign = "D"; ptas = - #5#53; |FINSI;
   |SI #5#53 ] 0; sign = "H"; ptas =   #5#53; |FINSI;
   |CIERRA #5;
|FPRC;

|PROCESO leelineas;
  ptas  = 0;
  cuen  = #7#2;
  eaCtaAna = "";

  eaCuenta = cuen; |HAZ SacaNivel;
  digi     = enNivel;
  |SI enSwAna = 1;
      aAlfa1 = #7#14; |QBF aAlfa1;
      |SI aAlfa1 = "*TABLA";
          |HAZ LeeTablaAna;
      |SINO;
          |SI aAlfa1 ! "*REGIS";
              eaCtaAna = #7#14;
          |FINSI;
      |FINSI;
  |FINSI;

  conc     = #7#4;
  aAlfa2   = #7#5; |QBF aAlfa2;
  aAlfa1   = #0#8; aAlfa1 = " " + aAlfa1;
  desc     = aAlfa2 + aAlfa1;

  |SI #camautoc#22 = "N";
      dpar = #7#6;
  |SINO;
      dpar = eaDfDepC;
  |FINSI;
  |QBF dpar; dpar = ( "00" + dpar) % -102;

  |SI #camautoc#23 = "N";
      subd = #7#9;
  |SINO;
      subd = eaDfSubC;
  |FINSI;

  aElSigno  = #7#15; ||signo del importe

  |SI #7#7 = "S";    ||saldo de la cuenta
      |HAZ leecta;
  |SINO;
      |SI #7#7 = "C";  || Cuadre de Asiento
          |SI #0#14 ] 0; sign = "H"; ptas = #0#14;   |FINSI;
          |SI #0#14 < 0; sign = "D"; ptas = - #0#14; |FINSI;
      |SINO;
          sign = #7#7;     ||debe o haber
          |SI #7#10 = 0;   ||Importe del asiento Fijo
              ptas = #7#8;
          |SINO;
             |HAZ CalImporte;  || Calcula importe Modelo
          |FINSI;
      |FINSI;
  |FINSI;

  |SI #7#7 = "S"; |O #7#7 = "C";
      |SI aElSigno = "+"; |Y sign = "H"; ptas = 0; |FINSI;
      |SI aElSigno = "-"; |Y sign = "D"; ptas = 0; |FINSI;
  |FINSI;

  |SI ptas ! 0;
      apun  = apun + nIncrApte;
      |HAZ lineas;
      sw   = 1;

      |SI sign = "D"; #0#12 = #0#12 + ptas; |FINSI;
      |SI sign = "H"; #0#13 = #0#13 + ptas; |FINSI;
      #0#14 = #0#12 - #0#13;
  |FINSI;
|FPRC;

|| **************************************************************************
|PROCESO HazTodo;
   |HAZ Analitica;
   |ABRE #9;  |CIERRA #9;  |DELINDEX #9;

   |HAZ eLeeParametro;
/*
   |ABRE #20;
   |LEE 101#20p;
   |SI FSalida ! 0;
      |DDEFECTO #20;
      |GRABA 020#20b;
   |FINSI;
   |LIBERA #20;
   |CIERRA #20;
   documen     = #caconasi#1 + 1;
*/

   |HAZ contador;
   documen = nDOCUMENTO;

 || ////  Lee el Asiento Fijo
   |ABRE #6;
   #6#0 = #0#3;  || Asiento Fijo
   |LEE 001#6=;
   |SI FSalida ! 0;
       |CIERRA #6;  ||En esta empresa no esta el asiento elegido
       |ACABA;
   |FINSI;
   |SI #6#21 = 0; |O #6#21 ! #0#0;
       |CIERRA #6;  || NO es de Modelo
       |ACABA;
   |FINSI;
   nIncrApte = #6#26; |SI nIncrApte = 0; nIncrApte = 10; |FINSI;

|| /////// Proceso contab0;
   #0#10 = fFechaAsto;
   #0#11 = documen;
   #0#12 = 0;  #0#13 = 0; #0#14 = 0;

   lerror = 0;
   |ABRE #9;
   |DDEFECTO #9;
   apun = 1 - nIncrApte;
   sw = 0;
   |BUCLELIN 2leelineas#6;  || Lee las Lineas del asiento fijo
   |CIERRA #9;
   |SI sw = 1;  |HAZ pasa_asto; |FINSI;
   |DELINDEX #9;

   |SI lerror ! 0;
       |SI swImpresora = 1;
           |PIEINF;
       |SINO;
           aAlfa1 = enEmpresa; aAlfa1 = ("00000" + aAlfa1) % -105;
           aAlfa1 = "    Errores en Asientos de la Empresa " + aAlfa1;
           |MENAV aAlfa1;
       |FINSI;
   |FINSI;
|FPRC;

||////////////////////////////////////////////////////////////////////////
|CALCULO proceso; |TIPO 3;
   |HAZ nom_usu;
   aAlfa1 = "m" + nom_tmp;
   |NOME_DAT #catrasi1 #aAlfa1#;

   |SI #0#0 = 200; |Y enQueSoc ! #0#6;
       enQueSoc = #0#6;
       enwModAu = 0;
       |HAZ CargaAuxModelo;
       |NOME_DAT #901 eaTabMod;
   |SINO;
       |SI #0#0 = 202; |Y enEjercicio ! #0#6;
           enQueSoc = #0#6 - 1;
           enwModAu = 0;
           |HAZ CargaAuxModelo;
           |NOME_DAT #901 eaTabMod;
      |FINSI;
   |FINSI;

   fFechaAsto = #0#7;  || Fecha del Asiento en Pantalla

   |SI #48#27 = "S";
       |HAZ MultiEmpresa;
   |SINO;
       |DFICE dirfich0; |QBF dirfich0;
       |HAZ HazTodo;
   |FINSI;
   |DELINDEX #901;
   |SI swImpresora = 1;
       |FININF;
       |FINIMP;
   |FINSI;
|FCAL;

|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   |SI fFechaAsto [ fec3; |ACABA; |FINSI; || fecha bloqueo
   |SI fFechaAsto < fec1; |O fFechaAsto > fec2;
       |ACABA;    || no contabiliza en esta Empresa
   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #2 dirfich0;
   |PATH_DAT #3 dirfich0;
   |PATH_DAT #5 dirfich0;
   |PATH_DAT #6 dirfich0;
   |PATH_DAT #7 dirfich0;
   |PATH_DAT #10 dirfich0;
   |PATH_DAT #20 dirfich0;

   |ATRI N;
   |PINTA 2014, "EMPRESA: ";
   |ATRI P; |PINTA 2023, #30#2;
   |PINTA 2029, #30#1; |ATRI 0;

   |CUADRO 2113 2370;
   |PINTA 2214, "                                                       ";
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
   #caselem1#1;
   ;
   000001;
   999999;
   ;
   NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;

|| ************************************************************************
|PROCESO lineas;    || graba apunte
   |DDEFECTO #9;
   #catrasi1#00 = #0#9;
   #catrasi1#01 = #0#10;
   #catrasi1#02 = #0#11;

   #catrasi1#03 = apun;   || apunte
   #catrasi1#04 = cuen;   || cuenta
   #catrasi1#05 = digi;   || digito
   #catrasi1#06 = conc;   || concepto
   #catrasi1#07 = desc;   || descripcion
   #catrasi1#08 = sign;   || signo
   #catrasi1#09 = ptas;   || importe
   |SI #catrasi1#08 = "D";
      #catrasi1#16 = #catrasi1#4;
      #catrasi1#17 = #catrasi1#5;
      #catrasi1#10 = "";
      #catrasi1#11 = 0;
   |SINO;
      #catrasi1#16 = "";
      #catrasi1#17 = 0;
      #catrasi1#10 = #catrasi1#4;
      #catrasi1#11 = #catrasi1#5;
   |FINSI;
   #catrasi1#21 = dpar;
   #catrasi1#28 = subd;
   #catrasi1#29 = eaCtaAna;
   |GRABA 020#9n;
|FPRC;

|PROCESO pasa_asto1;    || graba apunte
   |DDEFECTO #3;
   |PARA nPara1 = 0; |SI nPara1 [ 30; |HACIENDO nPara1 = nPara1 + 1;
         #camaasil#nPara1# = #catrasi1#nPara1#;
   |SIGUE;
   #camaasil#2  = nDOCUMENTO;
   #camaasil#24 = nREGISTRO;
   |GRABA 020#3n;
   |HAZ cabecera;           || cabecera apunte
   MasMenos     = #camaasil#9;
   DebeHaber    = #camaasil#8;
   aCUENTA      = #camaasil#4;
   nREGISTRO    = #camaasil#24;
   nDIARIO      = #camaasil#0;
   aFECHA       = #camaasil#1;
   nDOCUMENTO   = #camaasil#2;
   nNIVELCUENTA = #camaasil#5;
   |HAZ ActDiario;          || diario
   |HAZ ActCuenta;          || cuenta
   |HAZ ActTesteo;          || cuenta

   |HAZ HazElChequeo;
|FPRC;

|DEFBUCLE pasa_asto0;
   #9#1;
   ;
   00, "01.01.1900", 000001, 0001;
   99, "31.12.2090", 999999, 9999;
   e;
   NULL, pasa_asto1;
|FIN;

|PROCESO pasa_asto;
   |HAZ Multi_contador;
   |ABRE #2;
   |ABRE #3;
   |BUCLE pasa_asto0;
   |CIERRA #2;
   |CIERRA #3;

   documen = #camaasil#2 + 1;
   aAlfa1 = #camaasil#0; aAlfa1 = ("00" + aAlfa1) % -102;
   #camautoc#7  = aAlfa1;
   #camautoc#8  = #camaasil#1;
   aAlfa1  = #camaasil#2; aAlfa1 = ("000000" + aAlfa1) % -106;
   #camautoc#9  = aAlfa1;
   |GRABA 020#6a;
|FPRC;

|PROCESO cabecera;       || actualiza cabeceras
   |DDEFECTO #2;
   #camaasic#00 = #camaasil#0; ||  diario
   #camaasic#01 = #camaasil#1; ||  fecha
   #camaasic#02 = #camaasil#2; ||  documento
   #camaasic#03 = #camaasil#2; ||  asiento
   |LEE 101#2=;
   |SI FSalida ! 0;
       |DDEFECTO #2;
       #camaasic#00 = #camaasil#0; ||  diario
       #camaasic#01 = #camaasil#1; ||  fecha
       #camaasic#02 = #camaasil#2; ||  documento
       #camaasic#03 = #camaasil#2; ||  asiento
       |GRABA 020#2b;
   |FINSI;
   |SI #camaasil#08 = "D";
       #camaasic#04 = #camaasic#04 + #camaasil#09;
       |SI #camaasic#09 = 0; #camaasic#08 = #camaasil#4; #camaasic#09 = #camaasil#5; |FINSI;  || contrp.HABER
   |SINO;
       #camaasic#05 = #camaasic#05 + #camaasil#09;
       |SI #camaasic#11 = 0; #camaasic#10 = #camaasil#4; #camaasic#11 = #camaasil#5; |FINSI;  || contrp.DEBE
   |FINSI;
   #camaasic#6  = #camaasic#4 - #camaasic#5;      || saldo
   #camaasic#12 = #camaasil#24;
   |SI #camaasil#12 = "R"; #camaasic#13 = 1; |FINSI;
   |SI #camaasil#12 = "S"; #camaasic#13 = 2; |FINSI;
   |SI #camaasil#12 = "N"; #camaasic#13 = 3; |FINSI;
   |GRABA 020#2a;
   |LIBERA #2;
|FPRC;


|| //////////////////// Proceso Para Montar Claves Segun Modelo
|PROCESO SImpModelo;
 |SI nModelo ! 202; |Y nModelo ! 200; |Y nModelo ! 225;
     #913#0 = #912#0;
     #913#1 = #912#1;
     #913#2 = #912#2;
     #913#3 = #912#3;
     #913#4 = #912#4 + 1;
     #913#5 = #912#5;
     |LEE 040#913=;
     |SI FSalida = 0; |ACABA; |FINSI;
 |FINSI;
 |SI nModelo =  202; |Y #0#6 ] 2010;
     #913#0 = #912#0;
     #913#1 = #912#1;
     #913#2 = #912#2;
     #913#3 = #912#3 + 1;
     |LEE 040#913=;
     |SI FSalida = 0; |ACABA; |FINSI;
 |FINSI;

 nNume1 = nNumCampo;
 |SI nModelo = 200;
     |SI #0#6 < 2008;
         |SI nNumCampo > 100; nNume1 = nNumCampo - 100; |FINSI;
     |FINSI;
 |FINSI;
 |SI nModelo = 225; |Y nNumCampo > 100; nNume1 = nNumCampo - 100; |FINSI;

 |SI aQueConver = "contasoc";
     |SI enConverContasoc ! 0;
         #912nNume1 = (#912nNume1 * enConverContasoc) ! enRedonContasoc;
     |FINSI;
 |FINSI;

 |SI aQueConver = "modelos";
     |SI enConverModelo ! 0;
         #912nNume1 = (#912nNume1 * enConverModelo) ! enRedonModelo;
     |FINSI;
 |FINSI;

 nImporte = #912nNume1;
 |SI aElSigno = "+";
      |SI nImporte < 0; nImporte = 0; |FINSI;
 |FINSI;
 |SI aElSigno = "-";
     |SI nImporte > 0; nImporte = 0; |FINSI;
     nImporte = - nImporte;  ||cambiar signo para que suma en positivo
 |FINSI;

  ptas = ptas + nImporte;
|FPRC;

|DEFBUCLE Modelo110;
 #912#1006;
 ;
 enEmpresa, #0#6, #0#1, nModelo, 00, 00;
 enEmpresa, #0#6, #0#1, nModelo, 99, 99;
 e;
 NULL, SImpModelo;
|FIN;

|DEFBUCLE Modelo202;
 #912#1002;
 ;
 enEmpresa, #0#6;
 enEmpresa, #0#6;
 e;
 NULL, SImpModelo;
|FIN;

|DEFBUCLE Modelo202_2010;
 #912#1004;
 ;
 enEmpresa, #0#6, #0#16, 00;
 enEmpresa, #0#6, #0#16, 99;
 e;
 NULL, SImpModelo;
|FIN;

|PROCESO LeePorModelo; || Pregunta para cuando cambie  las claves en algun modelo
  |ABRE #913;
  |SI nModelo = 110;  |BUCLE Modelo110;  |FINSI;
  |SI nModelo = 115;  |BUCLE Modelo110;  |FINSI;
  |SI nModelo = 123;  |BUCLE Modelo110;  |FINSI;
  |SI nModelo = 130;  |BUCLE Modelo110;  |FINSI;
  |SI nModelo = 131;  |BUCLE Modelo110;  |FINSI;
  |SI nModelo = 202;
      |SI #0#6 < 2010; |BUCLE Modelo202;      |FINSI;
      |SI #0#6 ] 2010; |BUCLE Modelo202_2010; |FINSI;
  |FINSI;

  |SI nModelo = 200;  |BUCLE Modelo202;  |FINSI;
  |SI nModelo = 225;  |BUCLE Modelo202;  |FINSI;

  |SI nModelo = 300;  |BUCLE Modelo110;  |FINSI;
  |SI nModelo = 303;  |BUCLE Modelo110;  |FINSI;
  |SI nModelo = 310;  |BUCLE Modelo110;  |FINSI;
  |SI nModelo = 320;  |BUCLE Modelo110;  |FINSI;
  |SI nModelo = 330;  |BUCLE Modelo110;  |FINSI;
  |SI nModelo = 332;  |BUCLE Modelo110;  |FINSI;
  |SI nModelo = 370;  |BUCLE Modelo110;  |FINSI;
  |CIERRA #913;
|FPRC;

|| ************************* Proceso para sacar posibles errores

|PROCESO HazElChequeo;
  eaCuenta = #camaasil#4; |QBF eaCuenta;

  |SI eaCuenta = "";
      men = "CUENTA CONTABLE EN BLANCO";
      |HAZ HazPrint;
  |SINO;
      |HAZ SacaNivel;
      |SI enNivel = 0;
          men = "CUENTA CONTABLE FUERA DE NIVEL";
          |HAZ HazPrint;
      |FINSI;
  |FINSI;
|ABRE #cacuenta;
  #cacuenta#0 = #camaasil#4;
  #cacuenta#1 = #camaasil#5;
  |LEE 001#5=;
  |SI FSalida ! 0;
      men = "NO EXISTE CUENTA CONTABLE";
      |HAZ HazPrint;
  |SINO;
      |SI #cacuenta#3 = "N";
         men = "CUENTA CONTABLE SIN ASIENTO DIRECTO";
         |HAZ HazPrint;
      |FINSI;
  |FINSI;
|CIERRA #cacuenta;

  |SI #camaasil#9 = 0;                   || Importe Cero
      men = "IMPORTE IGUAL A CERO";
      |HAZ HazPrint;
  |FINSI;

  |SI #camaasil#6 = 0;                   || Concepto
      men = "CONCEPTO AUTOMATICO IGUAL A CERO";
      |HAZ HazPrint;
  |FINSI;
|FPRC;

|PROCESO HazPrint;
  |SI swImpresora = 0;
      swImpresora = 1;
      |IMPRE 0;
      |SI FSalida ! 0;  swImpresora = 2; |ACABA; |FINSI;
      |INFOR informe;
      |SI FSalida ! 0;  swImpresora = 3; |FINIMP;  |ACABA;  |FINSI;
 |FINSI;
 |SI swImpresora = 1;
     antdia  = #camaasil#0;
     antfec  = #camaasil#1;
     antdoc  = #camaasil#2;
     antapu  = #camaasil#3;
     |PRINT;
 |FINSI;
 lerror = lerror + 1;
|FPRC;
