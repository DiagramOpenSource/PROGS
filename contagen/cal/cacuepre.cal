|FICHEROS;
   cacuepre #0;
   cacuenta #05;   || cuentas
   canempre #30;
|FIN;

|VARIABLES;
   &entrada = 1;
   sw = 0;
   long = 0;
   sw_yaesta = 0;
   mesalf = "";
   alfa1 = "";
   alfa2 = "";
   alfa3 = "";
   alfa4 = "";
   aAlfa = "";
   nume1 = 0;
   nume2 = 0;
   nume3 = 0;
   nume4 = 0;
   nume5 = 0;
   &aparam = "";

   nTotal = 0;
   nCont  = 0;
   nOpc   = 0;

{-1}aMenu = "";
    aMenu1 = "2400";
    aMenu2 = "1";
    aMenu3 = "";
    aMenu4 = "CS";
    aMenu5 = "Comprobacion Con niveles";
    aMenu6 = "Comprobacion Sin niveles";
|FIN;

|INCLUYE dscont_i;

|PROCESO antesque;  |TIPO 8;
   aparam = PARAMETRO;
   |QBF aparam;
   |SI aparam = "fech";
      |CIERRAT #0;
      |NOME_DAT #0 "cabalpre";
      |ABRET #0;
      |NOME_DAT #5 "cabalano";
   |FINSI;
   |HAZ inicio;
|FPRC;

|PROCESO alsalir;  |TIPO 9;
   |CIERRAT #0;
|FPRC;

|PROCESO deshas; |TIPO 7;
    nume5 = #canempre#11;  |HAZ mes;  #cacuepre#17 = mesalf;
    nume5 = #canempre#12;  |HAZ mes;  #cacuepre#18 = mesalf;
    |PINTA #cacuepre#17;
    |PINTA #cacuepre#18;
|FPRC;

|PROCESO mes;
   |SI nume5 =  1;  mesalf = "ENERO     ";  |FINSI;
   |SI nume5 =  2;  mesalf = "FEBRERO   ";  |FINSI;
   |SI nume5 =  3;  mesalf = "MARZO     ";  |FINSI;
   |SI nume5 =  4;  mesalf = "ABRIL     ";  |FINSI;
   |SI nume5 =  5;  mesalf = "MAYO      ";  |FINSI;
   |SI nume5 =  6;  mesalf = "JUNIO     ";  |FINSI;
   |SI nume5 =  7;  mesalf = "JULIO     ";  |FINSI;
   |SI nume5 =  8;  mesalf = "AGOSTO    ";  |FINSI;
   |SI nume5 =  9;  mesalf = "SEPTIEMBRE";  |FINSI;
   |SI nume5 = 10;  mesalf = "OCTUBRE   ";  |FINSI;
   |SI nume5 = 11;  mesalf = "NOVIEMBRE ";  |FINSI;
   |SI nume5 = 12;  mesalf = "DICIEMBRE ";  |FINSI;
|FPRC;

|PROCESO tipopre; |TIPO 0;

   |SI #cacuepre#Campo# = "M";
       alfa1 = "S";
       |C_M #cacuepre#16, 1, "N";
   |SINO;
       alfa1 = "N";
       |C_M #cacuepre#16, 1, "S";
   |FINSI;
|FPRC;

|PROCESO divide; |TIPO 0;
   |SI #cacuepre#3 = "A";
       nume2 = #cacuepre#16 / 12;
       nume4 = 0;
       |PARA nume3 = 4; |SI nume3 [ 14; |HACIENDO nume3 = nume3 + 1;
             #cacuepre#nume3# = nume2;
             nume4 = nume4 + #cacuepre#nume3#;
       |SIGUE;
       #cacuepre#15 = #cacuepre#16 - nume4;
   |FINSI;
   |PINDA #0#0;
|FPRC;

|PROCESO cuenta; |TIPO 6;
     eaCuenta = #cacuepre#Campo#;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #cacuepre#Campo# = eaCuenta;
     |PINTA #cacuepre#Campo#;

   nume1 = Campo + 1;
   alfa1 = #cacuepre#Campo#;
   |QBF alfa1;
   alfa1 = alfa1 % 0;
   long = alfa1;
   sw   = 0;
   #cacuepre#nume1# = 0;  || inicializa el digito
   |SI long = dig4; |Y dig4 ! 0; sw = 1; #cacuepre#nume1# = 1; |FINSI;
   |SI long = dig5; |Y dig5 ! 0; sw = 1; #cacuepre#nume1# = 2; |FINSI;
   |SI long = dig6; |Y dig6 ! 0; sw = 1; #cacuepre#nume1# = 3; |FINSI;
   |SI long = dig7; |Y dig7 ! 0; sw = 1; #cacuepre#nume1# = 4; |FINSI;
   |SI long = dig8; |Y dig8 ! 0; sw = 1; #cacuepre#nume1# = 5; |FINSI;
   |SI long = dig9; |Y dig9 ! 0; sw = 1; #cacuepre#nume1# = 6; |FINSI;
   |SI sw = 0;|Y long ! 0;
      |MENAV "    Longitud de Cuenta Fuera de Nivel !";
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO cuefin; |TIPO 0;
   |SI #cacuepre#Campo# ! doce;
      |ABRE #5;
      #cacuenta#0 = #cacuepre#0;
      |LEE 001#5=;
      |SI FSalida ! 0;
         |MENAV "    Cuenta Inexistente ...";
         |ERROR;
      |FINSI;
      |CIERRA #5;
   |FINSI;
|FPRC;

|PROCESO acumula; |TIPO 0;
   #cacuepre#16 = 0;
   |PARA nume1 = 4; |SI nume1 [ 15; |HACIENDO nume1 = nume1 + 1;
         #cacuepre#16 = #cacuepre#16 + #cacuepre#nume1#;
   |SIGUE;
   |PINTA #cacuepre#16;
|FPRC;

|PROCESO Controla;
   nTotal = 0;
   |PARA nCont = 4; |SI nCont [ 15; |HACIENDO nCont = nCont + 1;
      nTotal = nTotal + #cacuepre#nCont#; nTotal = nTotal ! 2;
   |SIGUE;
   |SI #cacuepre#3 = "A";
      |SI nTotal > #cacuepre#16;
         |MENAV "    Total anual sobrepasado";
         |ERROR;
      |FINSI;
   |SINO;
      #cacuepre#16 = nTotal; |PINTA #cacuepre#16;
   |FINSI;
|FPRC;

|PROCESO Tipo20; |TIPO 20;
   |SI FSalida = "OPCION";
      FSalida = "Informes";
   |SINO;
      |MENU aMenu;
      nOpc = FSalida;
      |SI nOpc < 1; |O nOpc > 2; |ACABA; |FINSI;
      aAlfa = "FICHA:" + #cacuepre#19 + "," + #cacuepre#0;
      |SI nOpc = 1; aAlfa = "caysalpr.tab;" + aAlfa; |SUB_EJECUTA aAlfa; |FINSI;
      |SI nOpc = 2; aAlfa = "cazsalpr.tab;" + aAlfa; |SUB_EJECUTA aAlfa; |FINSI;
   |FINSI;
|FPRC;

|PROCESO Ajusta; |TIPO 7;
   |SI #cacuepre#3 ! "A"; |ACABA; |FINSI;
   nTotal = 0;
   |PARA nCont = 4; |SI nCont [ 14; |HACIENDO nCont = nCont + 1;
      nTotal = nTotal + #cacuepre#nCont#; nTotal = nTotal ! 2;
   |SIGUE;
   nTotal = #cacuepre#16 - nTotal;
   #cacuepre#15 = nTotal;
|FPRC;

|PROCESO Prueba;
   |SI FSalida = 2; |ACABA; |FINSI;
   |SI #cacuepre#3 = "A";
      nTotal = 0;
      |PARA nCont = 4; |SI nCont [ 15; |HACIENDO nCont = nCont + 1;
         nTotal = nTotal + #cacuepre#nCont#; nTotal = nTotal ! 2;
      |SIGUE;
      |SI nTotal ! #cacuepre#16;
         |CONFI "    NEl importe total no coincide con el de desglose. Ajustar ? ";
         |SI FSalida = 0; |HAZ Ajusta; |FINSI;
         |ERROR;
      |FINSI;
   |FINSI;
|FPRC;
