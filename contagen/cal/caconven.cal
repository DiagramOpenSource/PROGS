|FICHEROS;
   caconvec #0;
   caconvel #1;

   cafantas #2;

   cacalend #13;
   calendar #3;
   cavenaut #4;
   caconven #5;

   cacuenta #10;
   caivaasi #200;
|FIN;

|VARIABLES;
   &enCalen = 0;
   &eSwNoCrea = 0;

   &entrada    = 1;
   &DVentas    = "";
   aAlfa1      = "";
   nSaldo      = 0;
   nVentas     = 0;
   nNume1      = 0;
   nNume2      = 0;
   nSwSal      = 0;
   swNo        = 0;
   nInkey      = 0;
   nT1         = 0;
   nT2         = 0;
   swGrab      = 0;
   aMesPeso  = "";

   i = 0;
   repa1 = 0;
   procesa = "     Estoy procesando espere un poco Por Favor ";
   modifi = "    S Contabilizar los Datos  S/N : ";
   grabador = " Estoy grabando ";
   reparto = " Estoy repartiendo ";
   repart1 = " Estoy repartiendo el resto ";
   repart2 = "";
   dd = 0;
   x = 0;
   y = 0;
   cero = 0;
   suma = 0;
   swclave = 1;
   p1 = "";
   cantidad = 0;
   canti  = 0;
   reparto1 = 0;
   rdon = 0;
   veces = 0;
   desv = 0;
   peso = 0;
   sumar = 0;
   total = 0;
   sumar1 = 0;
   sumar2 = 0;
   sumar3 = 0;
   sumar5 = 0;
   suma4 = 0;
   resto = 0;
   repa2 = 0;
   tpor = 0;
   rnd = 0.000000;
   pesoalfa = "";
   did = 0;
   num = 0;
   producto = 0.000000;
   producto1 = 0.000000;
   variable = 0;
   mod = "";
   mod1 = "";
   numero = 0;
   longi = "";
   longi1 = "";
   modnum = 0;
   alfapunto = "";
   nueve = 9;
   {366}cant = 0;
   {367}pesodia = 0;

   aParam = "";
   &snFilBa = "";
|FIN;

|INCLUYE dscont_i;

|| *************************************************************************
|| ******** Calculos Pantalla
|| *************************************************************************
|CALCULO elTipo8; |TIPO 8;

 |HAZ inicial;
 |SI swerror = 2;
     |PTEC 807;
     |ACABA;
 |FINSI;

 |HAZ DirAplicSt;
 |ABRE #200;
 |DDEFECTO #200;
 |LEE 001#200p;
 snFilBa = #200#119;
 |CIERRA #200;
|FCAL;

|CALCULO AntesDep; |TIPO 7;

   |SI eaDepar = "S";
      |SI Haybasica = 1;
          |MENSA "    <F2> Consultar Actividades.";
      |SINO;
          |MENSA "    <F2> Consultar Departamentos";
      |FINSI;
   |FINSI;

|FCAL;

|CALCULO TrasDep; |TIPO 0;

   |SI eaDepar = "S";
       eOpDep = 0; || 1 = Consulta pantalla
       |SI FSalida = 10; eOpDep = 1;  |FINSI;
       enActividad = #5#8;
       eaCodDep    = #5#8;
       |HAZ Actividad;
       |SI eswLect = 1;
           |MENAV "    Error.! No Existe Actividad / Departamento";
           |ERROR;
       |SINO;
           #5#8 = eaCodDep; |PINTA #5#8;
           #5#9 = eaNombreAct; |PINTA #5#9;
           |HAZ PintaFecha;
           |SI efActFin > "01.01.1900"; |Y snFilBa = "S";
               aAlfa1 = "    NAtencion!!!. Actividad con Fecha de BAJA " + efActFin + ", Desea introducir Facturas ? ";
               |CONFI aAlfa1;
               |SI FSalida ! 0;
                   |ERROR;
                   |ACABA;
               |FINSI;
           |FINSI;
           |SI eaEsCom = "S";
               |CONFI "    NAtencion!!!. Actividad Comunera, ¨ Desea introducir Movimientos ? ";
               |SI FSalida ! 0;
                   |ERROR;
                   |ACABA;
               |FINSI;
           |FINSI;
       |FINSI;
   |SINO;
       |C_M #5#9,1,"S";
   |FINSI;
   |HAZ LeeVentas;
|FCAL;

|PROCESO PintaFecha;
    aAlfa1 = " "; aAlfa1 = aAlfa1 * 42;
    |PINTA 0926, aAlfa1;
    #cafantas#10 = efActIni;
    #cafantas#11 = efActFin;
    |SI efActIni > "01.01.1900";
        |ATRI S; |PINTA 0926, "F.Inicio: "; |ATRI 0;
        |PINTA 0935, #cafantas#10;
    |FINSI;
    |SI efActFin > "01.01.1900";
        |ATRI S; |PINTA 0946, "F.Fin: "; |ATRI 0;
                 |PINTA 0952, #cafantas#11;
    |FINSI;
|FPRC;

|| ------- Cuenta
|CALCULO Cuenta; |TIPO 6;
     eaCuenta = #caconven#Campo#;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #caconven#Campo# = eaCuenta;
     |PINTA #caconven#Campo#;
|FCAL;

|CALCULO TrasCuenta; |TIPO 0;
   emCta    = 1;
   eaCuenta = #5Campo;
   |HAZ SacaNivel;
   |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea
   |HAZ LeeCuenta;
   |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea

   #5#11 = #10#2;  |PINTA #5#11;
   nSaldo = 0;
   |PARA nNume1 = 11; |SI nNume1 [ 47; |HACIENDO nNume1 = nNume1 + 3;
         nSaldo = nSaldo + #10nNume1;
   |SIGUE;
   #5#12 = nSaldo; |PINTA #5#12;
|FCAL;

|CALCULO TrasCalendario; |TIPO 0;
  |ABRE #13;
  #13#0 = #5Campo;
  |LEE 101#13=;
  |SI FSalida ! 0;
      |MENAV "    Atencion. No existe Calendario ";
      |ERROR;
  |SINO;
      |SI #13#1 ! enEjercicio;
          #13#1 = enEjercicio;
          |GRABA 020#13a;
      |FINSI;
  |FINSI;
  |LIBERA #13;
  |CIERRA #13;
|FCAL;

|CALCULO noSuper; |TIPO 0;
  |SI #5#3 > #5#2;
      |ERROR;
  |FINSI;
|FCAL;

|CALCULO ehnocero; |TIPO 0;
  |SI #5Campo = 0;
      |SI Campo = 4; |Y EURODB = 2;
          #5Campo = 1 / 100;
      |SINO;
          #5Campo = 1;
      |FINSI;
      |PINTA #5Campo;
  |FINSI;
|FCAL;


|PROCESO LeeVentas;
 |ABRE #0;
 #0#0 = #5#8;
 |LEE 001#0=;
 |SI FSalida = 0;
     #5#10 = #0#2; |PINTA #5#10;
     #5#11 = #0#3; |PINTA #5#11;
     #5#12 = #0#5; |PINTA #5#12;
     #5#1  = #0#4; |PINTA #5#1;
     #5#2  = #0#6; |PINTA #5#2;
   ||  |C_M #5#13,1,"S";
 |SINO;
   ||  |C_M #5#13,1,"N";
   ||  #5#13 = "N"; |PINTA #5#13;
 |FINSI;
 |CIERRA #0;
|FPRC;

|| *******************************************************************
|| ****** Procesos de Reparto
|PROCESO cargapesos;
   peso = 0;
   Ipesodia = pesodia1 <-;                             ||PROCESO
   |PARA x = 1;  |SI x [ 12;  |HACIENDO x = x + 1;
      num = 0;
      |PARA y = 1;  |SI y [ 31;  |HACIENDO y = y + 1;
         |SI y = 31;
            |SI x = 4;  |O x = 6;  |O x = 9;  |O x = 11;
               |SAL;
            |FINSI;
         |FINSI;
         |SI y = 30;  |Y x = 2;
            |SAL;
         |FINSI;
         num = num + 1;
         pesoalfa = num + "01";
         p1 = #calendar#x# % pesoalfa;
         peso = peso + p1;
         pesodia = p1;
         Ipesodia = Ipesodia + 1;
      |SIGUE;
   |SIGUE;
|FPRC;

|PROCESO asigna1;
   Ipesodia = pesodia1 <-;
   Icant = cant1 <-;
   |PARA x = cero;|SI x < 366;|HACIENDO x = x + 1;
      cant = sumar * pesodia;
      total = total + cant;
      Icant = Icant + 1;
      Ipesodia = Ipesodia + 1;
   |SIGUE;
|FPRC;

|PROCESO asigna2;
   dd = 1;
   |PARA dd = 1;  |SI dd [ veces;  |HACIENDO dd = dd + 1;
      resto = canti - total;
      did = veces + 1 - dd;
      |SI resto = 0;  |SAL;  |FINSI;
      repa2 = resto / did;
      repa2 = repa2 - 0.5;
      repa2 = repa2 ! 0;
      sumar1 = repa2 / peso;
      sumar1 = sumar1 - 0.5;
      sumar1 = sumar1 ! 0;

      ||Distribucion aleatoria;
      Ipesodia = pesodia1 <-;
      Icant = cant1 <-;
      |BLIN 23;
      |ATRI R;
      repart2 = reparto + dd + "a vez. ";
      |PINTA 2350,repart2;
      |ATRI 0;
      |PARA x = cero;|SI x < 366;|HACIENDO x = x + 1;
         |HAZ randon;
         tpor = rnd * (desv * 2) - desv;
         tpor = tpor ! 0;
         sumar5 = (sumar1 * tpor) / 100
         sumar5 = sumar5 ! 0;
         sumar2 = sumar1 + sumar5;
         sumar3 = sumar2 * pesodia;
         cant = cant + sumar3;
         total = total + sumar3;
         Ipesodia = Ipesodia + 1;
         Icant = Icant + 1;
      |SIGUE;
   |SIGUE;
|FPRC;

|PROCESO resto1;
   Ipesodia = pesodia1 <-;
   Icant = cant1 <-;
   |PARA x = cero;|SI x < 366;|HACIENDO x = x + 1;
      sumar3 = sumar * pesodia;
      cant = cant + sumar3;
      total = total + sumar3;
      Ipesodia = Ipesodia + 1;
      Icant = Icant + 1;
   |SIGUE;
|FPRC;

|| *********************************************************************
|CALCULO automat;|TIPO 3;
   |ATRI P;
   |PINTA 2125,procesa;
   |ATRI 0;

   |ABRE #13;
   #cacalend#0 = #caconven#1;
   |LEE 001#13=;
   |SI FSalida ! 0; |CIERRA #13; |ACABA; |FINSI;
   |CIERRA #13;

    enCalen = #cacalend#0;
    eSwNoCrea = 1;
    |HAZ CreaCalend;

    |HAZ random1;

    |HAZ cargapesos;

    |SI peso = 0; |ACABA; |FINSI;
    num = 0;
    cantidad = #caconven#2;
    reparto1 = #caconven#3;
    rdon     = #caconven#4;
    veces    = #caconven#5;
    desv     = #caconven#6;
    canti    = cantidad / rdon;
    repa1    = reparto1 / rdon;
    repa1    = repa1 ! 0;
    sumar    = repa1 / peso;
    sumar    = sumar ! 0;

   |HAZ asigna1;      || asigna cantidad fija
   |HAZ asigna2;      || asigna en dd veces;

   resto = canti - total;
   |BLIN 23;
   |ATRI R;
   |PINTA 2350,repart1;
   |ATRI 0;
   |SI resto ! 0;
      sumar = resto / peso;
      sumar = sumar - 0.5;
      sumar = sumar ! 0;
      |SI sumar ! 0;
         |HAZ resto1;                          ||asigna resto entero;
         resto = canti -total;
      |FINSI;
   |FINSI;
   Ipesodia = pesodia1 <-;
   Icant = cant1 <-;
   |SI resto < 0;
      resto = -resto;
   |FINSI;
   |PARA x = cero;|SI x < 366;|HACIENDO x = x + 1;
      |SI resto = 0;  |SAL;  |FINSI;
      |SI resto < pesodia;
         sumar = resto;
      |SINO;
         sumar = pesodia;
      |FINSI;
      |SI resto < 0;
         cant = cant - sumar;
      |SINO;
         cant = cant + sumar;
      |FINSI;
      resto = resto - sumar;
      Ipesodia = Ipesodia + 1;
      Icant = Icant + 1;
   |SIGUE;

   total = total - resto;

   |BLIN 23;
   |ATRI R;
   |PINTA 2350,grabador;
   |ATRI 0;
   Icant = cant1 <-;
   |PARA x = cero;|SI x < 366;|HACIENDO x = x + 1;
      cant = cant * rdon;
      Icant = Icant + 1;
   |SIGUE;


   ||Graba fichero de ventas automaticas;
   |ABRE #4;
   Icant = cant1 <-;
   |PARA y = 1;  |SI y [ 4;  |HACIENDO y = y + 1;
      swclave = 0;
      #cavenaut#0 = #caconven#0;
      #cavenaut#1 = y;
      #cavenaut#95 = #caconven#8;
      |LEE 110#4=;
      |SI FSalida = 0;  swclave = 1;  |FINSI;
      #cavenaut#0 = #caconven#0;
      #cavenaut#1 = y;
      #cavenaut#95 = #caconven#8;
      num = 1;
      #cavenaut#94 = #calendar#13;
      |PARA x = 1;|SI x [ 92;|HACIENDO x = x + 1;
         num = num + 1;
         #cavenaut#num# = cant;
         Icant = Icant + 1;
         |SI x = 91;  |Y y [ 2;
            |SAL;
         |FINSI;
      |SIGUE;
      |SI swclave = 1; |GRABA 020#4a; |SINO; |GRABA 020#4n; |FINSI;
      |LIBERA #4;
   |SIGUE;


   |HAZ CreaVentas;
   |CIERRA #4;

   |BLIN 21;
   |BLIN 23;
   |CONFI modifi;

   |SI FSalida = 0;
       DVentas = #5#8;
       |SUB_EJECUTA_NP "capasven";
   |FINSI;
|FCAL;

||*****************
|PROCESO CreaVentas;
 |ABRE #0;
 #0#0 = #5#8;
 |LEE 101#0=;
 |SI FSalida ! 0;
     |DDEFECTO #0;
     #0#0 = #5#8;
     |GRABA 020#0b;
 |FINSI;

 #0#1 = #5#9;
 #0#2 = #5#10;
 #0#3 = #5#11;
 #0#4 = #5#1;
 #0#5 = #5#12;
 #0#6 = #5#2;
 #0#8 = #3#13;
 |GRABA 020#0a;
 |LIBERA #0;
 |CIERRA #0;

 |ABRE #10;
 #10#0 = #5#10;
 |LEE 001#10=;
 |SI FSalida ! 0;
     |DDEFECTO #10;
 |FINSI;
 |CIERRA #10;

 nSaldo = #10#11;
 |ABRE #1;
 |PARA NumMes = 1; |SI NumMes [ 12; |HACIENDO NumMes = NumMes  + 1;
        #1#0 = #0#0;  || departamento
        #1#1 = NumMes;
        |LEE 101#1=;
        |SI FSalida ! 0;
            |DDEFECTO #1;
            #1#0 = #0#0;
            #1#1 = NumMes;
            |HAZ Nuevo;
            |GRABA 020#1b;
        |FINSI;
        nNume1 = 11 + (NumMes * 3)
        nSaldo = nSaldo + #10nNume1;
        #1#3 = nSaldo;
        nVentas = 0;
        |HAZ VentasMes;
        #1#4 = nVentas;
        #1#5 = #5#1;
        #1#7 = #5#3;
        |SI #1#4 < #1#7;
            #1#7 = 0;
        |FINSI;
        #1#8 = #5#4;
        #1#9 = #5#5;
        #1#10 = #5#6;
        #1#11 = "N";
        #1#13 = "*";
        |GRABA 020#1a;
        |LIBERA #1;
  |SIGUE;
  |CIERRA #1;
|FPRC;


|| *********************************************************************
|PROCESO randon;
   producto = producto / 3.141592;
   mod = producto;
   numero = 0;
   |ET punto;
   numero = numero + 1;
   alfapunto = numero + "01";
   mod1 = mod % alfapunto;
   |SI mod1 ! ".";
      |VETE punto;
   |FINSI;
   longi = mod % A0;
   alfapunto = numero;
   longi1 = longi % A0;
   |SI longi1 = "1";
      longi = "0" + longi;
   |FINSI;
   mod1 = alfapunto + longi;
   mod = mod % mod1;
   mod = "0" + mod;
   modnum = mod;
   rnd = modnum / 3.141592;
   |SI producto < 1;
      producto = producto * 1000000;
   |FINSI;
   |SI producto < 1;
      producto = producto1;
   |FINSI;
|FPRC;

|PROCESO random1;
   suma4 = 0;
   Ipesodia = pesodia1 <-;
   |PARA x = cero;|SI x < 183;|HACIENDO x = x + 1;
      |SI pesodia = 0;
         variable = 1;
      |SINO;
         variable = pesodia;
      |FINSI;
      suma4 = suma4 + variable;
      Ipesodia = Ipesodia + 1;
   |SIGUE;
   producto1 = suma4 * #caconven#1 * #caconven#0;
   producto = producto1;
|FPRC;

|PROCESO Nuevo;
  NumMes = #1#1;
  |HAZ NombreMes;
  #1#2 = NombreMes;
  #1#6 = NumMes;
  |SI #200#46 = "T";
      #1#6 = 1;
      |SI NumMes ] 4;  #1#6 = 2; |FINSI;
      |SI NumMes ] 7;  #1#6 = 3; |FINSI;
      |SI NumMes ] 10; #1#6 = 4; |FINSI;
  |SINO;
      |SI #200#46 = "A";
          #1#6 = 0;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO VentasMes;
 nVentas = 0;
 |SI NumMes [ 3;                  y = 1; |FINSI;
 |SI NumMes ] 4; |Y NumMes [ 6;   y = 2; |FINSI;
 |SI NumMes ] 7; |Y NumMes [ 9;   y = 3; |FINSI;
 |SI NumMes ] 10;                 y = 4; |FINSI;

 #4#0 = #5#0;
 #4#1 = y;
 #4#95 = #5#8;
 |LEE 110#4=;
 |SI FSalida ! 0;
     |ACABA;
 |FINSI;

 |SI NumMes = 1;  nT1 = 2;  nT2 = 32; |FINSI;
 |SI NumMes = 2;  nT1 = 33; nT2 = 61; |FINSI;
 |SI NumMes = 3;  nT1 = 62; nT2 = 92; |FINSI;

 |SI NumMes = 4;  nT1 = 2;  nT2 = 31; |FINSI;
 |SI NumMes = 5;  nT1 = 32; nT2 = 62; |FINSI;
 |SI NumMes = 6;  nT1 = 63; nT2 = 92; |FINSI;

 |SI NumMes = 7;  nT1 = 2;  nT2 = 32; |FINSI;
 |SI NumMes = 8;  nT1 = 33; nT2 = 63; |FINSI;
 |SI NumMes = 9;  nT1 = 64; nT2 = 93; |FINSI;

 |SI NumMes = 10;  nT1 = 2;  nT2 = 32; |FINSI;
 |SI NumMes = 11;  nT1 = 33; nT2 = 62; |FINSI;
 |SI NumMes = 12;  nT1 = 63; nT2 = 93; |FINSI;

 |PARA nNume2 = nT1; |SI nNume2 [ nT2; |HACIENDO nNume2 = nNume2 + 1;
       nVentas = nVentas + #4nNume2;
 |SIGUE;
|FPRC;


|PROGRAMA;
 aParam = PARAMETRO; |QBF aParam;
 |HAZ inicial;
 |SI swerror = 2;
     |ACABA;
 |FINSI;

 |HAZ DirAplicSt;
 |ABRE #200;
 |DDEFECTO #200;
 |LEE 001#200p;
 |CIERRA #200;

 aParam = aParam % 101;
 |SI aParam = "m";
     #5#8 = enActividad;
     |C_M #5#8, 1, "N";
 |SINO;
     #5#8 = #200#19;
     |C_M #5#8, 1, "S";
 |FINSI;
 |CLS;
 |PINPA #0#5;
 |PINDA #0#5;
 |ENDATOS #1#5;
|FPRO;
