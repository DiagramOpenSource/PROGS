|FICHEROS;
   luxeg020 #0;
   luxeg021 #10;
   luxeg022 #11;
   luxeg023 #12;
   luxeg024 #13;
   luxeg025 #14;
   luxeg026 #15;
   luxeg027 #16;
   luxeg028 #17;
   luxeg029 #18;
   luxeg02a #19;
   luxeg02b #20;

   canempre #100;
   cacuenta #101;
   camaasic #102;
   camaasil #103;
   camandia #104;

   :/dscarter/def/dscam003 #200;
   :/dscarter/def/dscam004 #201;
   :/dscarter/def/dscam008 #202;
   :/dscarter/def/dscam009 #203;
   :/dscarter/def/dscam012 #204;
   :/dscarter/def/dscam021 #205;
   :/dscarter/def/dscam046 #206;
   :/dscarter/def/dscam095 #207;
   :/dscarter/def/dscam000 #208;
   :/dscarter/def/dscam014 #209;
   :/dscarter/def/dscam067 #210;
|FIN;

|VARIABLES;
   &ext1         = "";
   &runnom       = "";
   &efDFecha     = @;
   &efHFecha     = @;

   aAlfa    = "";
   aAlfa1   = "";
   aAlfa2   = "";
   aAlfa3   = "";
   aEsc1    = "";
   aEsc2    = "";
   aRetu    = "";
   aTecla   = "";
   aTecla1  = "";
   aTecla2  = "";
   aDCuenta = "";
   aHCuenta = "";

   aEmpreCont  = "";
   aPerioCont  = "";
   aEmpCartera = "";
   aBaseRemoto = "";

   {1}aContiene = "";

   fFecha = @;
   fFechaHoy  = @;
   fFechaAyer = @;

   nSw    = 0;
   nSwImp = 0;
   nSwSel = 0;
   nSwOp  = 0;
   nRango = 0;
   nPos1  = 0;
   nPos2  = 0;
   nCalc  = 0;
   nCalc1 = 0;
   nCalc2 = 0;
   nBanco = 0;
   nCargos   = 0;
   nOpPdtes  = 0;
   nRemPdtes = 0;
   nHandle   = 0;

   nSaldo      = 0;
   nSaldoD     = 0;
   nSaldoH     = 0;
   nSaldoDAyer = 0;
   nSaldoHAyer = 0;
   nSaldoDHoy  = 0;
   nSaldoHHoy  = 0;

   nCobPdtAyer = 0;
   nCobPdtHoy  = 0;
   nPagPdtAyer = 0;
   nPagPdtHoy  = 0;

   nSaldoAnt = 0;
   nSaldoHoy = 0;

   nSaldoRealAyer = 0;
   nSaldoRealHoy  = 0;

   nAyerInic = 0;
   nEntCAyer = 0;
   nEntDAyer = 0;
   nEntrAyer = 0;
   nSalCAyer = 0;
   nSalDAyer = 0;
   nSaliAyer = 0;
   nCbPdAyer = 0;
   nPgPdAyer = 0;
   nAyerFin  = 0;
   nHoyInic  = 0;
   nEntCHoy  = 0;
   nEntDHoy  = 0;
   nEntrHoy  = 0;
   nSalCHoy  = 0;
   nSalDHoy  = 0;
   nSaliHoy  = 0;
   nCbPdHoy  = 0;
   nPgPdHoy  = 0;
   nHoyFin   = 0;

   nSldRAyer = 0;
   nSldEAyer = 0;
   nSldRHoy  = 0;
   nSldEHoy  = 0;

   nPant0 = 0;
   nPant1 = 0;
   nVent  = 0;
   nVent1 = 0;

   nLabel1 = -1;
   nLabel2 = -1;
   nBoton1a = 0;
   nBoton2a = 0;
   nBoton1 = 0;
   nBoton2 = 0;
   nBoton3 = 0;
   nBoton4 = 0;
   nGrid1  = 0;
   nGrid2  = 0;
   nGrid3  = 0;
   || NO INCLUIR LINEAS EN ESTE BLOQUE
   {-1}aVent        = "";
       aVent1       = 0;
       aVent2       = 0;
       aVent3       = 0;
       aVent4       = 0;
       aVent5       = "";
   || NO INCLUIR LINEAS EN ESTE BLOQUE

   fDFecha   = @;
   fHFecha   = @;
|FIN;

|PROCESO Tipo80; |TIPO 80;
   FSalida = 2499;
|FPRC;

|PROCESO TmpAstosCartera;
   #luxeg02a#0 = #dscam067#4;
   #luxeg02a#1 = #dscam067#5;
   #luxeg02a#2 = #dscam067#6;
   |LEE 000#luxeg02a.=;
   |SI FSalida ! 0;
      |DDEFECTO #luxeg02a;
      #luxeg02a#0 = #dscam067#4;
      #luxeg02a#1 = #dscam067#5;
      #luxeg02a#2 = #dscam067#6;
      |GRABA 020#luxeg02a.n;
   |FINSI;
|FPRC;

|DEFBUCLE TmpAstosCartera;
#dscam067#2;
;
#canempre#2, #canempre#3, 00, "01.01.0000", 000000, 0000;
#canempre#2, #canempre#3, 99, "31.12.9999", 999999, 9999;
;
NULL, TmpAstosCartera;
|FIN;

|PROCESO TmpCobrosPdtes;
   |SI #dscam003#31 = 0; |ACABA; |FINSI;
   |SI #dscam003#14 = "A"; |O #dscam003#14 = "D"; |ACABA; |FINSI;

   #dscam014#0 = #dscam003#51;
   |LEE 000#dscam014.=;
   |SI FSalida ! 0; |DDEFECTO #dscam014; |FINSI;

   |DDEFECTO #luxeg02b;
   #luxeg02b#0 = #dscam014#10;
   #luxeg02b#1 = #dscam000#0;
   #luxeg02b#2 = "C";
   #luxeg02b#3 = #dscam003#40;
   #luxeg02b#6 = #dscam003#4;
   #luxeg02b#7 = #dscam003#3;
   |SI #dscam003#17 ! 0;
      #luxeg02b#4 = "R";
      #luxeg02b#5 = #dscam003#17;

      #dscam012#0 = #dscam003#17;
      |LEE 000#dscam012.=;
      |SI FSalida = 0;
         #luxeg02b#9 = #dscam012#1;
         #luxeg02b#10 = #dscam012#23;
         #luxeg02b#11 = #dscam012#8;
         #luxeg02b#12 = #dscam012#22;
      |FINSI;
   |FINSI;
   |SI #dscam003#70 ! "          "; |O #dscam003#72 ! "    ";
      #luxeg02b#4 = "p";
      #luxeg02b#8 = #dscam003#71;
   |FINSI;

   #luxeg02b#13 = #dscam003#31;
   |GRABA 020#luxeg02b.n;
|FPRC;

|DEFBUCLE TmpCobrosPdtes;
#dscam003#1;
;
000000001;
999999999;
;
NULL, TmpCobrosPdtes;
|FIN;

|PROCESO TmpPagosPdtes;
   |SI #dscam008#31 = 0; |ACABA; |FINSI;
   |SI #dscam008#14 = "A"; |O #dscam008#14 = "D"; |ACABA; |FINSI;

   #dscam014#0 = #dscam008#16;
   |LEE 000#dscam014.=;
   |SI FSalida ! 0; |DDEFECTO #dscam014; |FINSI;

   |DDEFECTO #luxeg02b;
   #luxeg02b#0 = #dscam014#10;
   #luxeg02b#1 = #dscam000#0;
   #luxeg02b#2 = "P";
   #luxeg02b#3 = #dscam008#40;
   #luxeg02b#6 = #dscam008#4;
   #luxeg02b#7 = #dscam008#3;
   |SI #dscam008#39 = "P"; |O #dscam008#39 = "T"; |O #dscam008#39 = "D";
      |SI #dscam008#39 = "P";
         #luxeg02b#4 = "P";
         #luxeg02b#5 = #dscam008#17;

         #dscam021#0 = #dscam008#17;
         |LEE 000#dscam021.=;
         |SI FSalida = 0;
            #luxeg02b#9  = #dscam021#2;
            #luxeg02b#10 = #dscam021#7;
         |FINSI;
      |FINSI;

      |SI #dscam008#39 = "T";
         #luxeg02b#4 = "T";
         #luxeg02b#5 = #dscam008#17;

         #dscam046#0 = #dscam008#17;
         |LEE 000#dscam046.=;
         |SI FSalida = 0;
            #luxeg02b#9  = #dscam046#1;
            #luxeg02b#10 = #dscam046#7;
         |FINSI;
      |FINSI;

      |SI #dscam008#39 = "D";
         #luxeg02b#4 = "D";
         #luxeg02b#5 = #dscam008#17;

         #dscam095#0 = #dscam008#17;
         |LEE 000#dscam095.=;
         |SI FSalida = 0;
            #luxeg02b#9  = #dscam095#1;
            #luxeg02b#10 = #dscam095#8;
         |FINSI;
      |FINSI;
   |FINSI;

   #luxeg02b#13 = #dscam008#31;
   |GRABA 020#luxeg02b.n;
|FPRC;

|DEFBUCLE TmpPagosPdtes;
#dscam008#1;
;
000000001;
999999999;
;
NULL, TmpPagosPdtes;
|FIN;

|PROCESO TomaDatosCartera;
   |DBASS aAlfa; |QBF aAlfa;
   aAlfa = aAlfa + "dscarter/fich/"; |PATH_DAT #dscam000#aAlfa#;
   |ABRE #dscam000;
   |LEE 000#dscam000.p;
   |PARA; |SI FSalida = 0; |HACIENDO;
      aEmpCartera = ("00000" + #dscam000#0) % -105;

      |SI nLabel1 ] 0; |FIN_CONTROL_WINDOWS nLabel1; |FINSI;
      aAlfa = "LABEL,{{1}}Tomando datos de la empresa " + aEmpCartera; |QBF aAlfa;
      aAlfa = aAlfa + " [" + #dscam000#2; |QBF aAlfa;
      aAlfa = aAlfa + "] " + #dscam000#1; |QBF aAlfa;
      nPos1 = 2202; nPos2 = 2298;
      |CONTROL_SIMPLE nPant0, aAlfa, nPos1, nPos2, NULL;
      nLabel1 = FSalida;
      |PULSATECLA;

      |DBASS aAlfa; |QBF aAlfa;
      aAlfa = aAlfa + "dscarter/fich/" + aEmpCartera + "/";
      |PATH_DAT #dscam008#aAlfa#; |PATH_DAT #dscam009#aAlfa#;
      |PATH_DAT #dscam003#aAlfa#; |PATH_DAT #dscam004#aAlfa#;
      |PATH_DAT #dscam067#aAlfa#; |PATH_DAT #dscam014#aAlfa#;
      |PATH_DAT #dscam012#aAlfa#; |PATH_DAT #dscam021#aAlfa#;
      |PATH_DAT #dscam046#aAlfa#; |PATH_DAT #dscam095#aAlfa#;

      |SI nLabel2 ] 0; |FIN_CONTROL_WINDOWS nLabel2; |FINSI;
      nPos1 = 2302; nPos2 = 2398;
      |CONTROL_SIMPLE nPant0, "LABEL,{{1}}Recogiendo asientos de la empresa", nPos1, nPos2, NULL;
      nLabel2 = FSalida;
      |PULSATECLA;

      |BUCLE TmpAstosCartera;

      |SI nLabel2 ] 0; |FIN_CONTROL_WINDOWS nLabel2; |FINSI;
      nPos1 = 2302; nPos2 = 2398;
      |CONTROL_SIMPLE nPant0, "LABEL,{{1}}Recogiendo cobros pendientes de la empresa", nPos1, nPos2, NULL;
      nLabel2 = FSalida;
      |PULSATECLA;
      |ABRE #dscam014;
      |ABRE #dscam012; |BUCLE TmpCobrosPdtes; |CIERRA #dscam012;

      |SI nLabel2 ] 0; |FIN_CONTROL_WINDOWS nLabel2; |FINSI;
      nPos1 = 2302; nPos2 = 2398;
      |CONTROL_SIMPLE nPant0, "LABEL,{{1}}Recogiendo pagos pendientes de la empresa", nPos1, nPos2, NULL;
      nLabel2 = FSalida;
      |PULSATECLA;
      |ABRE #dscam021; |ABRE #dscam046; |ABRE #dscam095;
      |BUCLE TmpPagosPdtes;
      |CIERRA #dscam021; |CIERRA #dscam046; |CIERRA #dscam095;
      |CIERRA #dscam014;

      |LEE 000#dscam000.s;
   |SIGUE;
   |SI nLabel2 ] 0; |FIN_CONTROL_WINDOWS nLabel2; nLabel2 = -1; |FINSI;
   |SI nLabel1 ] 0; |FIN_CONTROL_WINDOWS nLabel1; nLabel1 = -1; |FINSI;
|FPRC;

|PROCESO Asientos;
   |SI #camaasil#4 = #luxeg021#0;
      |SI #camaasil#8 = "H";
         nSaldo = nSaldo - #camaasil#9;
         nSaldoH = nSaldoH + #camaasil#9;
      |SINO;
         nSaldo = nSaldo + #camaasil#9;
         nSaldoD = nSaldoD + #camaasil#9;
      |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE Asientos;
#camaasil#1;
;
01, fDFecha, 000000, 0000;
98, fHFecha, 999999, 9999;
;
NULL, Asientos;
|FIN;

|PROCESO BuscaExterno;
   #luxeg027#0 = #cacuenta#0;
   #luxeg027#1 = fFecha;
   |LEE 000#luxeg027.];
   |SI FSalida = 0;
      |SI #luxeg027#0 = #cacuenta#0;
         |SI #luxeg027#1 > fFecha;
            |LEE 000#luxeg027.a;
            |SI FSalida ! 0; |O #luxeg027#0 ! #cacuenta#0;
               |DDEFECTO #luxeg027; #luxeg027#0 = #cacuenta#0;
            |FINSI;
         |FINSI;
      |SINO;
         |LEE 000#luxeg027.a;
         |SI FSalida ! 0; |O #luxeg027#0 ! #cacuenta#0;
            |DDEFECTO #luxeg027; #luxeg027#0 = #cacuenta#0;
         |FINSI;
      |FINSI;
   |SINO;
      |LEE 000#luxeg027.u;
      |SI FSalida = 0;
         |SI #luxeg027#0 = #cacuenta#0;
            |SI #luxeg027#1 > fFecha;
               |DDEFECTO #luxeg027; #luxeg027#0 = #cacuenta#0;
            |FINSI;
         |SINO;
            |DDEFECTO #luxeg027; #luxeg027#0 = #cacuenta#0;
         |FINSI;
      |SINO;
         |DDEFECTO #luxeg027; #luxeg027#0 = #cacuenta#0;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO AstosCartera;
   |DDEFECTO #camaasil;
   #camaasil#0 = #luxeg02a#0;
   #camaasil#1 = #luxeg02a#1;
   #camaasil#2 = #luxeg02a#2;
   |LEE 000#camaasil.];
   |PARA; |SI FSalida = 0; |Y #camaasil#0 = #luxeg02a#0; |Y #camaasil#1 = #luxeg02a#2;
    |Y #camaasil#2 = #luxeg02a#2; |HACIENDO;
      |SI #camaasil#4 = #luxeg021#0;
         |SI #camaasil#1 = fDFecha;
            |SI #camaasil#8 = "D"; nSaldoDAyer = nSaldoDAyer + #camaasil#9; |FINSI;
            |SI #camaasil#8 = "H"; nSaldoHAyer = nSaldoHAyer + #camaasil#9; |FINSI;
         |FINSI;
         |SI #camaasil#1 = fHFecha;
            |SI #camaasil#8 = "D"; nSaldoDHoy = nSaldoDHoy + #camaasil#9; |FINSI;
            |SI #camaasil#8 = "H"; nSaldoHHoy = nSaldoHHoy + #camaasil#9; |FINSI;
         |FINSI;
      |FINSI;
      |LEE 000#camaasil.s;
   |SIGUE;
|FPRC;

|DEFBUCLE AstosCartera;
#luxeg02a#1;
;
00, fDFecha, 000000;
99, fHFecha, 999999;
;
NULL, AstosCartera;
|FIN;

|PROCESO Pendientes;
   |SI #luxeg02b#2 = "C";
      |SI #luxeg02b#4 ! " ";
         |SI #luxeg02b#4 = "R";
            |SI #luxeg02b#12 = 1; |O #luxeg02b#12 = 3;
               |SI #luxeg02b#10 = "N";
                  |SI #luxeg02b#9 [ fDFecha; nCobPdtAyer = nCobPdtAyer + #luxeg02b#13; |ACABA; |FINSI;
                  |SI #luxeg02b#9 = fHFecha; nCobPdtHoy  = nCobPdtHoy  + #luxeg02b#13; |ACABA; |FINSI;
               |FINSI;
            |FINSI;
            |SI #luxeg02b#12 = 2; |O #luxeg02b#12 = 4; |O #luxeg02b#12 = 5;
               |SI #luxeg02b#10 = "N"; |O #luxeg02b#11 = "N";
                  |SI #luxeg02b#9 [ fDFecha; nCobPdtAyer = nCobPdtAyer + #luxeg02b#13; |ACABA; |FINSI;
                  |SI #luxeg02b#9 = fHFecha; nCobPdtHoy  = nCobPdtHoy  + #luxeg02b#13; |ACABA; |FINSI;
               |FINSI;
            |FINSI;
            |ACABA;
         |FINSI;

         |SI #luxeg02b#4 = "p";
            |SI #luxeg02b#14 ! "R"; |Y #luxeg02b#14 ! "L";
               |SI #luxeg02b#8 [ fDFecha; nCobPdtAyer = nCobPdtAyer + #luxeg02b#13; |ACABA; |FINSI;
               |SI #luxeg02b#8 = fHFecha; nCobPdtHoy  = nCobPdtHoy  + #luxeg02b#13; |ACABA; |FINSI;
            |FINSI;
            |ACABA;
         |FINSI;
      |SINO;
         |SI #luxeg02b#0 = #luxeg021#0;
            |SI #luxeg02b#7 [ fDFecha; nCobPdtAyer = nCobPdtAyer + #luxeg02b#13; |ACABA; |FINSI;
            |SI #luxeg02b#7 = fHFecha; nCobPdtHoy  = nCobPdtHoy  + #luxeg02b#13; |ACABA; |FINSI;
         |FINSI;
      |FINSI;
   |FINSI;
   |SI #luxeg02b#2 = "P";
      |SI #luxeg02b#4 ! " ";
         |SI #luxeg02b#4 = "P"; |O #luxeg02b#4 = "T";
            |SI #luxeg02b#10 = "S";
               |SI #luxeg02b#9 [ fDFecha; nPagPdtAyer = nPagPdtAyer + #luxeg02b#13; |ACABA; |FINSI;
               |SI #luxeg02b#9 = fHFecha; nPagPdtHoy  = nPagPdtHoy  + #luxeg02b#13; |ACABA; |FINSI;
            |FINSI;
            |ACABA;
         |FINSI;
         |SI #luxeg02b#4 = "D";
            |SI #luxeg02b#10 = "N";
               |SI #luxeg02b#9 [ fDFecha; nPagPdtAyer = nPagPdtAyer + #luxeg02b#13; |ACABA; |FINSI;
               |SI #luxeg02b#9 = fHFecha; nPagPdtHoy  = nPagPdtHoy  + #luxeg02b#13; |ACABA; |FINSI;
            |FINSI;
            |ACABA;
         |FINSI;
      |SINO;
         |SI #luxeg02b#0 = #luxeg021#0;
            |SI #luxeg02b#7 [ fDFecha; nPagPdtAyer = nPagPdtAyer + #luxeg02b#13; |ACABA; |FINSI;
            |SI #luxeg02b#7 = fHFecha; nPagPdtHoy  = nPagPdtHoy  + #luxeg02b#13; |ACABA; |FINSI;
         |FINSI;
      |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE Pendientes;
#luxeg02b#1;
;
#luxeg021#0, 00000, " ", 000000000;
#luxeg021#0, 99999, "z", 999999999;
;
NULL, Pendientes;
|FIN;

|PROCESO DatosCartera;
   nSaldoDAyer = 0; nSaldoHAyer = 0;
   nSaldoDHoy  = 0; nSaldoHHoy = 0;
   nCobPdtAyer = 0; nCobPdtHoy = 0;
   nPagPdtAyer = 0; nPagPdtHoy = 0;

   |BUCLE AstosCartera; |BUCLE Pendientes;

   #luxeg021#9  = nCobPdtAyer;
   #luxeg021#10 = nPagPdtAyer;
   #luxeg021#19 = nCobPdtHoy;
   #luxeg021#20 = nPagPdtHoy;
|FPRC;

|PROCESO RellenaTempo;
   #luxeg021#0 = #cacuenta#0;
   |LEE 000#luxeg021.=;
   |SI FSalida ! 0;
      |DDEFECTO #luxeg021;
      #luxeg021#0 = #cacuenta#0;
      |GRABA 020#luxeg021.b;
      #luxeg021#1 = #cacuenta#2;

      |SI nLabel1 ] 0; |FIN_CONTROL_WINDOWS nLabel1; |FINSI;
      aAlfa = "LABEL,{{1}}Recogiendo datos de los asiento de la cuenta " + #cacuenta#0; |QBF aAlfa;
      aAlfa = aAlfa + " " + #cacuenta#2; |QBF aAlfa;
      nPos1 = 2202; nPos2 = 2298;
      |CONTROL_SIMPLE nPant0, aAlfa, nPos1, nPos2, NULL;
      nLabel1 = FSalida;

      |SI nLabel2 ] 0; |FIN_CONTROL_WINDOWS nLabel2; |FINSI;
      nPos1 = 2302; nPos2 = 2398;
      |CONTROL_SIMPLE nPant0, "LABEL,{{1}}Obteniendo datos de los asientos", nPos1, nPos2, NULL;
      nLabel2 = FSalida;
      |PULSATECLA;

      nSaldo = #cacuenta#11; nCalc = #luxeg020#4; nCalc = nCalc - 2;
      aAlfa = #luxeg020#3; |QBF aAlfa; aAlfa = aAlfa % -104;
      aAlfa = "01." + (("00" + #canempre#11) % -102) + "." + aAlfa;
      fDFecha = aAlfa; fHFecha = nCalc; |BUCLE Asientos;
      #luxeg021#2 = nSaldo;

      nSaldoD = 0; nSaldoH = 0; nSaldo = 0;
      nCalc = #luxeg020#4; nCalc = nCalc - 1;
      fDFecha = nCalc; fHFecha = nCalc; |BUCLE Asientos;
      #luxeg021#5 = nSaldoD; #luxeg021#8 = nSaldoH;
      nSaldoRealAyer = nSaldo;

      fFecha = fDFecha; |HAZ BuscaExterno;
      #luxeg021#23 = #luxeg027#3;

      |SI nLabel2 ] 0; |FIN_CONTROL_WINDOWS nLabel2; |FINSI;
      nPos1 = 2302; nPos2 = 2398;
      |CONTROL_SIMPLE nPant0, "LABEL,{{1}}Grabando datos de las empresas de cartera", nPos1, nPos2, NULL;
      nLabel2 = FSalida;
      |PULSATECLA;

      fHFecha = #luxeg020#4; |HAZ DatosCartera;
      #luxeg021#3  = nSaldoDAyer; #luxeg021#6  = nSaldoHAyer;
      #luxeg021#4  = #luxeg021#5 - #luxeg021#3; || Las entradas directas son
                                                || las totales menos las de
                                                || cartera.
      #luxeg021#7  = #luxeg021#8 - #luxeg021#6; || Las salidas directas son
                                                || las totales menos las de
                                                || cartera.
      #luxeg021#13 = nSaldoDHoy;  #luxeg021#16 = nSaldoHHoy;

      nSaldoD = 0; nSaldoH = 0; nSaldo = 0;
      fDFecha = #luxeg020#4; fHFecha = #luxeg020#4; |BUCLE Asientos;
      #luxeg021#14 = nSaldoD; #luxeg021#17 = nSaldoH;
      nSaldoRealHoy = nSaldo;

      fFecha = fDFecha; |HAZ BuscaExterno;
      #luxeg021#25 = #luxeg027#3;

      nSw = 1; |HAZ Calculos;
      |GRABA 020#luxeg021.a; |LIBERA #luxeg021;
   |FINSI;
   |SI nLabel1 ] 0; |FIN_CONTROL_WINDOWS nLabel2; nLabel1 = -1; |FINSI;
   |SI nLabel2 ] 0; |FIN_CONTROL_WINDOWS nLabel2; nLabel2 = -1; |FINSI;
|FPRC;

|DEFBUCLE RellenaTempo;
#cacuenta#1;
3;
#luxeg020#1, "S";
#luxeg020#2, "S";
;
NULL, RellenaTempo;
|FIN;

|PROCESO Inf902;
   #luxeg023#0 = #luxeg024#18;
   #luxeg023#1 = #luxeg024#8;
   #luxeg023#2 = #luxeg024#9;
   #luxeg023#3 = 1;
|FPRC;

|PROCESO Sup902;
   #luxeg023#0 = #luxeg024#18;
   #luxeg023#1 = #luxeg024#8;
   #luxeg023#2 = #luxeg024#9;
   #luxeg023#3 = 9999;
|FPRC;

|PROCESO Calculos;
   |SI nSw [ 2;
      #luxeg021#11 = #luxeg021#2 + (#luxeg021#5 - #luxeg021#8) + (#luxeg021#9 - #luxeg021#10);
      #luxeg021#12 = #luxeg021#2 + (#luxeg021#5 - #luxeg021#8);
      #luxeg021#15 = #luxeg021#13 + #luxeg021#14;
      #luxeg021#18 = #luxeg021#16 + #luxeg021#17;
      #luxeg021#21 = #luxeg021#12 + (#luxeg021#15 - #luxeg021#18) + (#luxeg021#19 - #luxeg021#20);
      |SI nSw [ 1;
         #luxeg021#22 = nSaldoRealAyer + #luxeg021#2;
         #luxeg021#24 = nSaldoRealHoy  + #luxeg021#12;
      |FINSI;
   |FINSI;
   #luxeg021#26 = #luxeg021#11 - #luxeg021#22;
   #luxeg021#27 = #luxeg021#22 - #luxeg021#23;
   #luxeg021#28 = #luxeg021#21 - #luxeg021#24;
   #luxeg021#29 = #luxeg021#24 - #luxeg021#25;
   #luxeg021#30 = #luxeg021#26 + #luxeg021#27;
   #luxeg021#31 = #luxeg021#28 + #luxeg021#29;
   #luxeg021#32 = #luxeg021#30 + #luxeg021#31;
|FPRC;

|PROCESO EvtoAsiento;
   |SI FSalida = 1; |O FSalida = 2;
      |ABRE #camandia;
      #camandia#0 = #luxeg023#0;
      |LEE 000#camandia.=;
      |SI FSalida ! 0; |DDEFECTO #camandia; |FINSI;

      #luxeg026#0 = #luxeg023#0; |PINTA #luxeg026#0;
      #luxeg026#1 = #camandia#1; |PINTA #luxeg026#1;
      #luxeg026#2 = #luxeg023#1; |PINTA #luxeg026#2;
      #luxeg026#3 = #luxeg023#2; |PINTA #luxeg026#3;
      #luxeg026#4 = #camandia#2; |PINTA #luxeg026#4;
      #luxeg026#5 = #camandia#3; |PINTA #luxeg026#5;
      #luxeg026#6 = #luxeg026#5 - #luxeg026#4;  |PINTA #luxeg026#6;
      #luxeg026#7 = #luxeg023#11; |PINTA #luxeg026#7;
      #luxeg026#8 = #luxeg023#12; |PINTA #luxeg026#8;
      #luxeg026#9 = #luxeg023#13; |PINTA #luxeg026#9;
   |FINSI;
|FPRC;

|PROCESO SacaAsiento;
   |ABRE #luxeg023; |ABRE #camandia;
   #luxeg023#0 = #luxeg024#18;
   #luxeg023#1 = #luxeg024#8;
   #luxeg023#2 = #luxeg024#9;
   #luxeg023#3 = 1;
   |LEE 000#luxeg023.];
   |SI FSalida = 0; |Y #luxeg023#0 = #luxeg024#18;
    |Y #luxeg023#1 = #luxeg024#8; |Y #luxeg023#2 = #luxeg024#9;
      |VENTANA 0805, 2196, -1, 17, "Asiento contable";
      nVent1 = FSalida;

      aVent1 = -1;
      aVent2 = -1;
      aVent3 = 0;
      aVent4 = nVent1;
      aVent5 = "MODAL";
      |ESPECIFICA "ESTADO_VENTANA", aVent;

      |PINPA #0#luxeg026; nPant1 = FSalida;
      |PINDA #0#luxeg026;

      nRango = 4 + 8 + 16 + 32;
      nPos1 = 1003; nPos2 = 1790;
      |LINEAL_SIMPLE #1#luxeg023, nRango, nPos1, nPos2, Inf902, Sup902, EvtoAsiento;
      nGrid3 = FSalida;
      |PULSATECLA;

      |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}Salir," + nGrid3;
         |LEETECLA aTecla2;
         |SI aTecla2 = aEsc1; |SAL;  |FINSI;
         |SI aTecla2 = aEsc2; |SAL;  |FINSI;
         |SI aTecla2 = aRetu; |SAL;  |FINSI;
      |SIGUE;

      |DESTRUYECONTROL nGrid3;
      |PULSATECLA;

      aVent4 = nVent1;
      aVent5 = "MODELESS";
      |ESPECIFICA "ESTADO_VENTANA", aVent;
      |FINVENTANA nVent1;
   |FINSI;
   |CIERRA #luxeg023; |CIERRA #camandia;
|FPRC;

|PROCESO EvtoExtracto;
   |SI FSalida = 1; |O FSalida = 2;
      #luxeg025#0  = #luxeg024#1;  |PINTA #luxeg025#0;
      #luxeg025#1  = #luxeg024#2;  |PINTA #luxeg025#1;
      #luxeg025#2  = #luxeg024#3;  |PINTA #luxeg025#2;
      #luxeg025#3  = #luxeg024#4;  |PINTA #luxeg025#3;
      #luxeg025#4  = #luxeg024#47; |PINTA #luxeg025#4;
      #luxeg025#5  = #luxeg024#5;  |PINTA #luxeg025#5;
      #luxeg025#6  = #luxeg024#6;  |PINTA #luxeg025#6;
      #luxeg025#7  = #luxeg024#7;  |PINTA #luxeg025#7;
      #luxeg025#8  = #luxeg024#48;
      #luxeg025#9  = #luxeg024#14; |PINTA #luxeg025#9;
      #luxeg025#10 = #luxeg024#15; |PINTA #luxeg025#10;
      #luxeg025#11 = #luxeg024#16; |PINTA #luxeg025#11;
   |FINSI;

   |SI FSalida = 4; |HAZ SacaAsiento; |FINSI;
|FPRC;

|PROCESO Inf903;
   #luxeg024#8  = "01.01.0000";
   #luxeg024#18 = 00;
   #luxeg024#9  = 000001;
   #luxeg024#19 = 0001;
   #luxeg024#23 = 00001;
   #luxeg024#0  = 000001;
|FPRC;

|PROCESO Sup903;
   #luxeg024#8  = "31.12.9999";
   #luxeg024#18 = 99;
   #luxeg024#9  = 999999;
   #luxeg024#19 = 9999;
   #luxeg024#23 = 99999;
   #luxeg024#0 = 999999;
|FPRC;

|PROCESO SacaExtracto;
   |ESTADO_CONTROL nGrid1, "DISABLE";
/*
   |ESTADO_CONTROL nGrid1, "HIDE";
   |ESTADO_CONTROL nBoton1, "HIDE";
   |ESTADO_CONTROL nBoton2, "HIDE";
   |ESTADO_CONTROL nBoton3, "HIDE";
   |ESTADO_CONTROL nBoton4, "HIDE";
   |ESTADO_CONTROL nLabel1, "HIDE";
   |ESTADO_CONTROL nLabel2, "HIDE";
*/
   runnom = "dspreven";
   ext1  = #luxeg021#0;
   efDFecha = #luxeg020#3; efHFecha = #luxeg020#4;
   |SUB_EJECUTA "caextrap.tab;NOMEQUITESQUETECORTOLASMANOS";

   aAlfa = "5o" + Usuario; |NOME_DAT #luxeg024#aAlfa#;
   |ABRE #luxeg024;
   |LEE 000#luxeg024.u;

   |VENTANA 0510, 2390, -1, 17, "Extracto especial (Haga doble click en la linea para ver el asiento)";
   nVent = FSalida;

   aVent1 = -1;
   aVent2 = -1;
   aVent3 = 0;
   aVent4 = nVent;
   aVent5 = "MODAL";
   |ESPECIFICA "ESTADO_VENTANA", aVent;

   |LEE 000#luxeg024.u;
   |SI FSalida = 0;
      |PINPA #0#luxeg025; nPant1 = FSalida;
      |PINDA #0#luxeg025;

      nRango = 4 + 8 + 16 + 32;
      nPos1 = 1102; nPos2 = 2079;
      |LINEAL_SIMPLE #2#luxeg024, nRango, nPos1, nPos2, Inf903, Sup903, EvtoExtracto;
      nGrid2 = FSalida;
      |PULSATECLA;

      aEsc1  = &255 + "806";
      aEsc2  = &255 + "807";
      aRetu  = &255 + "802";
      |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}Salir," + nGrid2;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
      |SIGUE;

      |DESTRUYECONTROL nGrid2;
      |PULSATECLA;
   |SINO;
      |MENAV "    Sin movimientos para esta cuenta contable";
   |FINSI;
   |CIERRA #luxeg024; |DELINDEX #luxeg024;

   aVent4 = nVent;
   aVent5 = "MODELESS";
   |ESPECIFICA "ESTADO_VENTANA", aVent;
   |FINVENTANA nVent;

   |ESTADO_CONTROL nGrid1, "ENABLE";
/*
   |ESTADO_CONTROL nGrid1, "SHOW";
   |ESTADO_CONTROL nBoton1, "SHOW";
   |ESTADO_CONTROL nBoton2, "SHOW";
   |ESTADO_CONTROL nBoton3, "SHOW";
   |ESTADO_CONTROL nBoton4, "SHOW";
   |ESTADO_CONTROL nLabel1, "SHOW";
   |ESTADO_CONTROL nLabel2, "SHOW";
*/
   |FOCOTECLADO nGrid1;
|FPRC;

|PROCESO EvtoGrid1;
   |SI FSalida = 1; |O FSalida = 2;
      |SI #luxeg021#0 ! "            ";
         |ESTADO_CONTROL nLabel1, "SHOW";
      |SINO;
         |ESTADO_CONTROL nLabel1, "HIDE";
      |FINSI;
      |LEE 000#luxeg021.c;
   |FINSI;

   |SI FSalida = 4;
      |SI #luxeg021#0 ! "            ";
         |HAZ SacaExtracto;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO Puntos; |TIPO 6;
   aAlfa = #luxeg020#Campo#; |QBF aAlfa;
   aAlfa = aAlfa - ".";
   |SI FSalida ! 0;
      nCalc = 0;
      |SI #canempre#14 ] nCalc; nCalc = #canempre#14; |FINSI;
      |SI #canempre#15 ] nCalc; nCalc = #canempre#15; |FINSI;
      |SI #canempre#16 ] nCalc; nCalc = #canempre#16; |FINSI;
      |SI #canempre#17 ] nCalc; nCalc = #canempre#17; |FINSI;
      |SI #canempre#18 ] nCalc; nCalc = #canempre#18; |FINSI;
      |SI #canempre#19 ] nCalc; nCalc = #canempre#19; |FINSI;

      aAlfa = #luxeg020#Campo#; |QBF aAlfa;
      aAlfa = aAlfa - ".";
      |PARA; |SI FSalida ! 0; |HACIENDO;
         nCalc1 = ((FSalida / 100) ! 0) + 99; aAlfa1 = aAlfa % nCalc1;
         nCalc2 = FSalida + 98;               aAlfa2 = aAlfa % nCalc2;
         aAlfa = aAlfa1 + aAlfa2; |QBF aAlfa;
         aAlfa = aAlfa % 0; nCalc1 = aAlfa; nCalc1 = nCalc - nCalc1;
         aAlfa = aAlfa1 + ("0" * nCalc1) + aAlfa2;

         aAlfa = aAlfa - ".";
      |SIGUE;
      #luxeg020#Campo# = aAlfa; |PINTA #luxeg020#Campo#;
   |FINSI;
|FPRC;

|PROCESO GrabaLinAstos;
   |SI nSwOp = 1;                      || Comprueba si aparece alguna cuenta
      aAlfa = #camaasil#4; |QBF aAlfa; ||    de la acotacion en el asiento
                                       ||    corriente
      |SI aAlfa ] aDCuenta; |Y aAlfa [ aHCuenta; nSwOp = 0; |ERROR10; |FINSI;
   |FINSI;
   |SI nSwOp = 2;  || Graba el asiento en el temporal
      #camandia#0 = #camaasil#0;
      |LEE 000#camandia.=;
      |SI FSalida ! 0; |DDEFECTO #camandia; |FINSI;

      #luxeg023#0 = #camaasil#0;
      #luxeg023#1 = #camaasil#1;
      #luxeg023#2 = #camaasil#2;
      #luxeg023#3 = #camaasil#3;
      |LEE 101#luxeg023.=;
      |SI FSalida ! 0;
         |DDEFECTO #luxeg023;
         #luxeg023#0 = #camaasil#0;
         #luxeg023#1 = #camaasil#1;
         #luxeg023#2 = #camaasil#2;
         #luxeg023#3 = #camaasil#3;
         |GRABA 020#luxeg023.b;
      |FINSI;
      #luxeg023#4 = #camaasil#4;
      #luxeg023#5 = #camaasil#6;
      #luxeg023#6 = #camaasil#7;
      #luxeg023#7 = #camaasil#8;
      #luxeg023#8 = #camaasil#9;
      |SI #luxeg023#7 = "D";
         #luxeg023#9 = #camaasil#4;
         #luxeg023#10 = "";
      |SINO;
         #luxeg023#9 = "";
         #luxeg023#10 = #camaasil#4;
      |FINSI;
      #luxeg023#11 = #camaasic#4;
      #luxeg023#12 = #camaasic#5;
      #luxeg023#13 = #camaasic#6;
      |GRABA 020#luxeg023.a; |LIBERA #luxeg023;
   |FINSI;
|FPRC;

|DEFBUCLE GrabaLinAstos;
#camaasil#1;
;
#camaasic#0, #camaasic#1, #camaasic#2, 0000;
#camaasic#0, #camaasic#1, #camaasic#2, 9999;
;
NULL, GrabaLinAstos;
|FIN;

|PROCESO GrabaAsientos;
   nSwOp = 1; |BUCLE GrabaLinAstos;
   |SI nSwOp = 0;
      nSwOp = 2; |BUCLE GrabaLinAstos;
   |FINSI;
|FPRC;

|DEFBUCLE GrabaAsientos;
#camaasic#1;
;
00, #luxeg020#3, 000000;
99, #luxeg020#4, 999999;
;
NULL, GrabaAsientos;
|FIN;

|PROCESO luxeg021;
   |SI #luxeg021#0 = "            "; |ACABA; |FINSI;

   #luxeg023#4 = #luxeg021#0;
   |LEE 000#luxeg023.];
   |SI FSalida ! 0; |O #luxeg023#4 ! #luxeg021#0;
      nAyerInic = #luxeg021#2;
      nEntCAyer = #luxeg021#3; nEntDAyer = #luxeg021#4; nEntrAyer = #luxeg021#5;
      nSalCAyer = #luxeg021#6; nSalDAyer = #luxeg021#7; nSaliAyer = #luxeg021#8;
      nCbPdAyer = #luxeg021#9; nPgPdAyer = #luxeg021#10;
      nAyerFin  = #luxeg021#11;

      nHoyInic  = #luxeg021#12;
      nEntCHoy = #luxeg021#13; nEntDHoy = #luxeg021#14; nEntrHoy = #luxeg021#15;
      nSalCHoy = #luxeg021#16; nSalDHoy = #luxeg021#17; nSaliHoy = #luxeg021#18;
      nCbPdHoy = #luxeg021#19; nPgPdHoy = #luxeg021#20;
      nHoyFin  = #luxeg021#21;

      nSldRAyer = #luxeg021#22; nSldEAyer = #luxeg021#23;
      nSldRHoy  = #luxeg021#22; nSldEHoy  = #luxeg021#23;

      |SI #luxeg020#6 = "S";
         |SALVA_FICHA #luxeg021;
         #luxeg021#0 = "";
         |LEE 101#luxeg021.=;
         |SI FSalida ! 0;
            |DDEFECTO #luxeg021;
            #luxeg021#0 = "";
            #luxeg021#1 = "CUENTAS SIN MOVIMIENTOS";
            |GRABA 020#luxeg021.b;
         |FINSI;

         #luxeg021#2  = #luxeg021#2  + nAyerInic;
         #luxeg021#3  = #luxeg021#3  + nEntCAyer;
         #luxeg021#4  = #luxeg021#4  + nEntDAyer;
         #luxeg021#5  = #luxeg021#5  + nEntrAyer;
         #luxeg021#6  = #luxeg021#6  + nSalCAyer;
         #luxeg021#7  = #luxeg021#7  + nSalDAyer;
         #luxeg021#8  = #luxeg021#8  + nSaliAyer;
         #luxeg021#9  = #luxeg021#9  + nCbPdAyer;
         #luxeg021#10 = #luxeg021#10 + nPgPdAyer;
         #luxeg021#11 = #luxeg021#11 + nAyerFin;
         #luxeg021#12 = #luxeg021#12 + nHoyInic;
         #luxeg021#13 = #luxeg021#13 + nEntCHoy;
         #luxeg021#14 = #luxeg021#14 + nEntDHoy;
         #luxeg021#15 = #luxeg021#15 + nEntrHoy;
         #luxeg021#16 = #luxeg021#16 + nSalCHoy;
         #luxeg021#17 = #luxeg021#17 + nSalDHoy;
         #luxeg021#18 = #luxeg021#18 + nSaliHoy;
         #luxeg021#19 = #luxeg021#19 + nCbPdHoy;
         #luxeg021#20 = #luxeg021#20 + nPgPdHoy;
         #luxeg021#21 = #luxeg021#21 + nHoyFin;
         #luxeg021#22 = #luxeg021#22 + nSldRAyer;
         #luxeg021#23 = #luxeg021#23 + nSldEAyer;
         #luxeg021#24 = #luxeg021#24 + nSldRHoy;
         #luxeg021#25 = #luxeg021#25 + nSldEHoy;
         nSw = 3; |HAZ Calculos;

         |GRABA 020#luxeg021.a; |LIBERA #luxeg021;
         |REPON_FICHA #luxeg021;

         |BORRA 020#luxeg021.a;
      |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE luxeg021;
#luxeg021#1;
;
INICIO;
FINAL;
;
NULL, luxeg021;
|FIN;

|PROCESO AgrupaSinMovtos;
   || Leo todo el luxeg022 y compruebo si hay movimientos. Si no es asi,
   ||    borro esta cuenta y la acumulo a la cuenta vacia con descripcion
   ||    'CUENTAS SIN MOVIMIENTOS'. Evidentemente si hago doble click en esta
   ||    linea no aparece el extracto.

   ||ABRE #luxeg023;
   |SELECT #2#luxeg023;
   |ABRE #luxeg027;
   |BUCLE luxeg021;
   |CIERRA #luxeg027;
   |SELECT #1#luxeg023;
   ||CIERRA #luxeg023;

|FPRC;

|RUTINA inf;
   #luxeg027#0 = #luxeg021#0;
   #luxeg027#1 = "01.01.0000";
|FRUT;

|RUTINA sup;
   #luxeg027#0 = #luxeg021#0;
   #luxeg027#1 = "31.12.9999";
|FRUT;

|PROCESO Boton1;
   |VENTANA 1120, 1780, -1, 17, "Indicar saldo externo";
   nVent = FSalida;

   aVent1 = -1;
   aVent2 = -1;
   aVent3 = 0;
   aVent4 = nVent;
   aVent5 = "MODAL";
   |ESPECIFICA "ESTADO_VENTANA", aVent;

   fFecha = ~;
   |ABRE #luxeg027;
   #luxeg027#0 = #luxeg021#0;
   #luxeg027#1 = fFecha;
   |LEE 101#luxeg027.=;
   |SI FSalida ! 0;
      |DDEFECTO #luxeg027;
      #luxeg027#0 = #luxeg021#0;
      #luxeg027#1 = fFecha;
      #luxeg027#2 = #luxeg021#1;
      |GRABA 020#luxeg027.b;
   |FINSI;
   |PINPA #0#luxeg027; nPant1 = FSalida;
   |PINDA #0#luxeg027;

   |ENCAMPO #1#luxeg027;
   |SI FSalida = 10;
      |CONSULTA_F_CLAVE #1#luxeg027, inf, sup;
      |SI FSalida = 0;
         |LEE 000#luxeg027.=;
         |PINDA #0#luxeg027; FSalida = 0;
      |FINSI;
   |FINSI;
   |SI FSalida = 0;
      |LEE 101#luxeg027.=;
      |SI FSalida ! 0; |GRABA 020#luxeg027.b; |FINSI;

      |ENDATOS #1#luxeg027;
      |SI FSalida = 0;
         |GRABA 020#luxeg027.a; |LIBERA #luxeg027;

         fFechaHoy = #luxeg020#4; nCalc = fFechaHoy; nCalc = nCalc - 1;
         fFechaAyer = nCalc;
         |SI #luxeg027#1 = fFechaHoy;
            #luxeg021#25 = #luxeg027#3;
            nSw = 2; |HAZ Calculos;
            |GRABA 020#luxeg021.a;
         |FINSI;
         |SI #luxeg027#1 = fFechaAyer;
            #luxeg021#23 = #luxeg027#3;
            nSw = 2; |HAZ Calculos;
            |GRABA 020#luxeg021.a;
         |FINSI;
      |FINSI;
      |LIBERA #luxeg021;
      |REFRESCACONTROL nGrid1;
   |FINSI;

   |CIERRA #luxeg027;

   aVent4 = nVent;
   aVent5 = "MODELESS";
   |ESPECIFICA "ESTADO_VENTANA", aVent;

   |FINVENTANA nVent;
|FPRC;

|PROCESO EliminaSaldExt;
   |BORRA 020#luxeg021.a;
|FPRC;

|DEFBUCLE EliminaSaldExt;
#luxeg027#1;
;
#luxeg021#0, "01.01.0000";
#luxeg021#0, "31.12.9999";
be;
NULL, EliminaSaldExt;
|FIN;

|PROCESO Boton2;
   aAlfa = "    NSe eliminaran los saldos externos guardados del banco seleccionado. " + &191 + " Continuar ?";
   |CONFI aAlfa;
   |SI FSalida = 0;
      |BUCLE EliminaSaldExt;
   |FINSI;
|FPRC;

|PROCESO NoSelecciona;
   nSwSel = 0; |PTEC 807;
|FPRC;

|PROCESO Selecciona;
   nSwSel = 1; |PTEC 807;
|FPRC;

|PROCESO Evento;
   |SI FSalida = 1; |O FSalida = 2; |LEE 000#luxeg028.c; |FINSI;
   |SI FSalida = 4;
      |HAZ Selecciona;
   |FINSI;
|FPRC;

|PROCESO Boton3;
   |CIERRA #luxeg028;
   aAlfa = "fm" + Usuario; |NOME_DAT #luxeg028#aAlfa#;
   |DELINDEX #luxeg028; |ABRE #luxeg028;

   |DDEFECTO #luxeg028;
   #luxeg028#0 = "luxeg020";
   |GRABA 020#luxeg028.n;

   |DDEF aAlfa; |QBF aAlfa;
   aAlfa = aAlfa + "*.rpt";
   |_PDIR aAlfa;
   |PARA; |SI FSalida = 0; |HACIENDO;
      |DDEF aAlfa1; |QBF aAlfa1;
      aAlfa1 = aAlfa - aAlfa1;
      aAlfa2 = aAlfa1 - "luxe20";
      |SI FSalida ! 0;
         #luxeg028#0 = aAlfa1;
         |LEE 000#luxeg028.=;
         |SI FSalida ! 0;
            |DDEFECTO #luxeg028;
            #luxeg028#0 = aAlfa1;
            |GRABA 020#luxeg028.n;
         |FINSI;
      |FINSI;
      |_SDIR aAlfa;
   |SIGUE;

   |VENTANA 1138, 1862, -1, 17, "Indicar formato";
   nVent1 = FSalida;

   aVent1 = -1;
   aVent2 = -1;
   aVent3 = 0;
   aVent4 = nVent1;
   aVent5 = "MODAL";
   |ESPECIFICA "ESTADO_VENTANA", aVent;

   |ABRE #luxeg028;
   #luxeg028#0 = #luxeg020#5;
   |LEE 000#luxeg028.];

   |PINPA #0#luxeg028; nPant1 = FSalida;
   nPos1 = 1302; nPos2 = 1312;
   |CONTROL_SIMPLE nPant1, "BOTON,{{206}}Ok", nPos1, nPos2, Selecciona;
   nBoton1a = FSalida;

   nPos1 = 1314; nPos2 = 1324;
   |CONTROL_SIMPLE nPant1, "BOTON,{{207}}Cancela", nPos1, nPos2, NoSelecciona;
   nBoton2a = FSalida;

   nSwSel = 0;
   nRango = 2 + 4 + 8 + 16;
   nPos1 = 0602; nPos2 = 1124;
   |LINEAL_SIMPLE #1#luxeg028, nRango, nPos1, nPos2, NULL, NULL, Evento;
   |SI nSwSel = 1;
      #luxeg020#5 = #luxeg028#0;
      |GRABA 020#luxeg020.a;
   |FINSI;
   |CIERRA #luxeg028;

   |FIN_CONTROL_WINDOWS nBoton1a;
   |FIN_CONTROL_WINDOWS nBoton2a;

   aVent4 = nVent1;
   aVent5 = "MODELESS";
   |ESPECIFICA "ESTADO_VENTANA", aVent;

   |FINVENTANA nVent1;
   |PINTA 2411, #luxeg020#5;
|FPRC;

|PROCESO ImprimeLinAsto;
   |PRINT;
   nSwImp = 1;
|FPRC;

|DEFBUCLE ImprimeLinAsto;
#luxeg023#1;
;
#luxeg029#18, #luxeg029#8, #luxeg029#9, 0000;
#luxeg029#18, #luxeg029#8, #luxeg029#9, 9999;
;
NULL, ImprimeLinAsto;
|FIN;

|PROCESO LinsExtracto;
   nSwImp = 0;
   |BUCLE ImprimeLinAsto;
   |SI nSwImp = 0; |PRINT; |FINSI;
|FPRC;

|DEFBUCLE LinsExtracto;
#luxeg024#1;
;
INICIO;
FINAL;
;
NULL, LinsExtracto;
|FIN;

|PROCESO Imprime;
   runnom = "dspreven";
   ext1  = #luxeg021#0;
   efDFecha = #luxeg020#3; efHFecha = #luxeg020#4;
   |SUB_EJECUTA "caextrap.tab;NOMEQUITESQUETECORTOLASMANOS";

   aAlfa = "5o" + Usuario; |NOME_DAT #luxeg024#aAlfa#;
   |BUCLE LinsExtracto;
   |DELINDEX #luxeg024;
|FPRC;

|DEFBUCLE Imprime;
#luxeg021#1;
;
INICIO;
FINAL;
;
NULL, Imprime;
|FIN;

|PROCESO Boton4;
   |SI #luxeg020#5 = "        ";
      |MENAV "    Formato de informe no indicado. Proceso cancelado.";
      |ACABA;
   |FINSI;

   |DDEF aAlfa; |QBF aAlfa; aAlfa = aAlfa + #luxeg020#5; |QBF aAlfa;
   aAlfa = aAlfa + ".rpt";
   |XABRE "E", aAlfa, nHandle;
   |SI FSalida < 0;
      |MENAV "    El formato de informe indicado no existe. Por favor, cambielo.";
      |ACABA;
   |FINSI;

   Impresora = "CrystalReport";
   |IMPRE -1;
   |SI FSalida = 0;
      |SI #luxeg020#5 ! "luxeg020";
         |DDEF aAlfa; |QBF aAlfa; aAlfa1 = aAlfa;
         aAlfa = aAlfa + "luxeg020.inf";
         aAlfa1 = aAlfa1 + #luxeg020#5; |QBF aAlfa1;
         aAlfa1 = aAlfa1 + ".inf";
         |XABRE "E", aAlfa1, nHandle;
         |SI FSalida < 0;
            |COPIA_FICHERO aAlfa, aAlfa1;
         |FINSI;
      |FINSI;

      aAlfa = #luxeg020#5; |QBF aAlfa;
      |INFOR aAlfa;
      |SI FSalida = 0;
         |BUCLE Imprime;
         |PIEINF; |FININF;
      |FINSI;
      |FINIMP;
   |FINSI;
|FPRC;

|PROGRAMA;
   |ABRE #luxeg020;
   |LEE 000#luxeg020.p;
   |SI FSalida ! 0;
      |DDEFECTO #luxeg020;
      |GRABA 020#luxeg020.c;
   |FINSI;

   aContiene = "";
   |ESPECIFICA "RBASS", aContiene;
   aBaseRemoto = aContiene1;

   |DFICE aAlfa; |QBF aAlfa;
   aAlfa = aAlfa % -107; aAlfa = aAlfa % 0106;
   aEmpreCont = aAlfa % 0105; aPerioCont = aAlfa % 0601;

   |ABRE #canempre;
   #canempre#2 = aEmpreCont;
   #canempre#3 = aPerioCont;
   |LEE 000#canempre.=;
   |SI FSalida ! 0; |DDEFECTO #canempre; |FINSI;

   aAlfa = "ct" + Usuario; |NOME_DAT #luxeg021#aAlfa#;
   aAlfa = "bn" + Usuario; |NOME_DAT #luxeg022#aAlfa#;
   aAlfa = "as" + Usuario; |NOME_DAT #luxeg023#aAlfa#;
   aAlfa = "fm" + Usuario; |NOME_DAT #luxeg028#aAlfa#;
   aAlfa = "ex" + Usuario; |NOME_DAT #luxeg029#aAlfa#;
   aAlfa = "T1" + Usuario; |NOME_DAT #luxeg02a#aAlfa#;
   aAlfa = "T2" + Usuario; |NOME_DAT #luxeg02b#aAlfa#;
   |DELINDEX #luxeg021; |ABRE #luxeg021;
   |DELINDEX #luxeg022; |ABRE #luxeg022;
   |DELINDEX #luxeg023; |ABRE #luxeg023;
   |DELINDEX #luxeg028; |ABRE #luxeg028;
   |DELINDEX #luxeg029; |ABRE #luxeg029;
   |DELINDEX #luxeg02a; |ABRE #luxeg02a;
   |DELINDEX #luxeg02b; |ABRE #luxeg02b;

   |CLS;
   |CABEZA "Informe de estado contable y prevision Cobros/Pagos";
   |PINPA #0#0; |PINDA #0#0;
   |ENDATOS #1#0;
   |SI FSalida = 0;
      |GRABA 020#luxeg020.a;

      |HAZ TomaDatosCartera;

      |ABRE #luxeg027; |BUCLE RellenaTempo; |CIERRA #luxeg027;

      |SI nLabel1 ] 0; |FIN_CONTROL_WINDOWS nLabel1; nLabel1 = -1; |FINSI;
      |SI nLabel2 ] 0; |FIN_CONTROL_WINDOWS nLabel2; nLabel2 = -1; |FINSI;
      aDCuenta = #luxeg020#1; |QBF aDCuenta;
      aHCuenta = #luxeg020#2; |QBF aHCuenta;
      |ABRE #camandia; |BUCLE GrabaAsientos; |CIERRA #camandia;
      |HAZ AgrupaSinMovtos;

      |CLS;
      |PINPA #0#luxeg021; nPant0 = FSalida;

      nPos1 = 2480; nPos2 = 2498;
      |CONTROL_SIMPLE nPant0, "BOTON,{{135}}Cambiar saldo externo", nPos1, nPos2, Boton1;
      nBoton1 = FSalida;

      nPos1 = 2460; nPos2 = 2478;
      |CONTROL_SIMPLE nPant0, "BOTON,{{197}}Vaciar saldos externos", nPos1, nPos2, Boton2;
      nBoton2 = FSalida;

      nPos1 = 2421; nPos2 = 2432;
      |CONTROL_SIMPLE nPant0, "BOTON,{{197}}Cambiar", nPos1, nPos2, Boton3;
      nBoton3 = FSalida;

      nPos1 = 2440; nPos2 = 2458;
      |CONTROL_SIMPLE nPant0, "BOTON,{{205}}Emitir informe", nPos1, nPos2, Boton4;
      nBoton4 = FSalida;

      nPos1 = 0602; nPos2 = 0698;
      |CONTROL_SIMPLE nPant0, "LABEL,{{1}}Doble click para obtener extracto", nPos1, nPos2, NULL;
      nLabel1 = FSalida;

      nPos1 = 2402; nPos2 = 2411;
      |CONTROL_SIMPLE nPant0, "LABEL,{{1}}Formato ", nPos1, nPos2, NULL;
      nLabel2 = FSalida;
      |PINTA 2411, #luxeg020#5;

      nRango = 1 + 4 + 8 + 16 + 32;
      nPos1 = 0702; nPos2 = 2398;
      |LINEAL_SIMPLE #1#luxeg021, nRango, nPos1, nPos2, NULL, NULL, EvtoGrid1;

      nGrid1 = FSalida; |PULSATECLA;

      aEsc1  = &255 + "806";
      aEsc2  = &255 + "807";
      aRetu  = &255 + "802";
      |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}Salir," + nGrid1;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL; |FINSI;
         |SI aTecla = aEsc2; |SAL; |FINSI;
         |SI aTecla = aRetu; |SAL; |FINSI;
      |SIGUE;

      |FIN_CONTROL_WINDOWS nBoton1;
      |FIN_CONTROL_WINDOWS nBoton2;
      |FIN_CONTROL_WINDOWS nBoton3;
      |FIN_CONTROL_WINDOWS nBoton4;
      |FIN_CONTROL_WINDOWS nLabel1;
      |FIN_CONTROL_WINDOWS nLabel2;
      |DESTRUYECONTROL nGrid1;
   |FINSI;

   |CIERRA #canempre;

   |CIERRA #luxeg02b; |DELINDEX #luxeg02b;
   |CIERRA #luxeg02a; |DELINDEX #luxeg02a;
   |CIERRA #luxeg029; |DELINDEX #luxeg029;
   |CIERRA #luxeg028; |DELINDEX #luxeg028;
   |CIERRA #luxeg023; |DELINDEX #luxeg023;
   |CIERRA #luxeg022; |DELINDEX #luxeg022;
   |CIERRA #luxeg021; |DELINDEX #luxeg021;
   |CIERRA #luxeg020;
|FPRO;



|| *************************************************************************
|| *************************************************************************
|| *************************************************************************

|PROCESO Tipo003_15;
|FPRC;

|PROCESO Tipo003_16;
|FPRC;

|PROCESO Tipo003_17;
|FPRC;
