|FICHEROS;
     dscsvz91;
     caxsecfi;

     canempre;
     cacuenta;
     caclipro;
     caivaasi;
     calibros;
     caivases;
     camaniva;
     calinpag;
     calincob;
     caenlace;

     :modelos/dsmom030;

     dscsvw93;
     dscsvw94;

     &regisnif@;
     &sgpaises@;
     &Puente@;
     &provinc@;
|FIN;

|VARIABLES;
     aLong           = "";
     nLong           = 0;
     aLongVar        = "";
     nLongVar        = 0;
     nCarVar         = 0;
     nSw             = 0;
     nSwSal          = 0;
     nSwAlguno       = 0;
     nSwPaso         = 0;
     nPosi           = 0;
     nHandle         = 0;
     nDife           = 0;
     nDesdeCar       = 0;
     nPos1           = 0;
     nPos2           = 0;
     aTextoVentana   = "";


     aModAsiento     = "";
     aPathContagen   = "";
     n0Vent          = -1;
     n1Vent          = -1;
     nBtnPat1        = -1;
     nBtnUni         = -1;
     nBtnCan         = -1;

     nPos            = 0;
     nCarac          = 0;
     aOrigen         = "";
     aDestino        = "";
     aPathLocal      = "";
     aPathTmp        = "";
     aTipo           = "";
     aFicTmp         = "";
     aTecla          = "";
     nFSalida        = 0;
     nChequeo        = 0;
     aFicBrowse      = "";
     aEsc1           = "";
     aEsc2           = "";
     aRetu           = "";
     nPanta          = 0;

     {1}aConte       = "";

     {-1}sTree       = 0;
         sT_nID      = 0;
         sT_nOper    = 0;
         sT_aData    = "";

     {-1}aVent       = "";
         aVent1      = 0;
         aVent2      = 0;
         aVent3      = 0;
         aVent4      = 0;
         aVent5      = "";

     &eaUnidadBasico = "";
     &eaIP           = "";
     &eaModoImpo     = "";
     &eaRepeSopo     = "";
     &eaFicheroCSV   = "";
     &eaTipoCSV      = "";
     &eaFicheroIVA   = "";
     &eaFicheroDatos = "";
     &eaNombreDatos  = "";
     &eaBinarioConve = "";
     &eaNombrePDF    = "";
     &HayBasica      = 0;
     &enDnDIARIO     = 0;
     &enHnDIARIO     = 0;
     &efDFechas      = @;
     &efHFechas      = @;
     &enDDocume      = 0;
     &enHDocume      = 0;
     &enBorraAu      = 0;
     &enNoModif      = 0;
     &eaPath         = "";
     &eaRetorno      = "";
     &enNoEndatos    = 0;

     nDiario         = 0;
     nDocum          = 0;
     aSerie          = "";
     nLibro          = 0;
     nApunte         = 0;
     aNumFactura     = "";
     aPasar          = "";
     nMes            = 0;
     nUltDoc         = 0;
     nDocumSec       = 0;
     nPara1          = 0;
     nNumDoc         = 0;
     nNume1          = 0;
     nNume2          = 0;
     nNume3          = 0;
     aAlfa           = "";
     aAlfa1          = "";
     aAlfa2          = "";
     aAlfa3          = "";
     aAlfa4          = "";
     nConcepto       = 0;

     fFecha          = @;
     nContador       = 0;
     aLee1           = "";
     aLee2           = "";
     nLee            = 0;
     nTotLee         = 0;
     nSumaLee        = 0;
     nLeeNume        = 0;
     aQueQuiero      = "";
     aNumCampos      = "";
     nNumCampos      = 0;
     aDescripcion    = "";
     aDescr1         = "";
     aDescr2         = "";
     aCuenta         = "";
     aCamp19         = "";
     nCamp20         = 0;
     aSigno          = "";
     aCP             = "";
     aDirec          = "";
     aDirec0         = "";
     aDirec1         = "";
     aDirec2         = "";
     aMes            = "";
     mesdes          = "";
     meshas          = "";
     nDCon0          = 0;
     nDCon1          = 0;
     aDCon1          = "";
     aDCon2          = "";
     aSepSer         = "";
     aNumIzq         = "";

     aDirectorio     = "";
     nHay            = 0;
     nPrimero        = 0;
     aSwCS           = "";
|FIN;

|INCLUYE dscont_i;

|PROCESO CalculaCuenta;
     |QBF eaCuenta;
     |SI eaCuenta = "";  |ACABA;  |FINSI;

     aLongVar = eaCuenta % 0;
     nLongVar = aLongVar;

     |SI nLongVar ! MaximoDigitos;
         nDife = MaximoDigitos - nLongVar;
         |SI nDife < 0;  |ACABA;  |FINSI;

         nDesdeCar = 4;

         nPos1    = 100 + nDesdeCar;
         nPos2    = (100 * (nDesdeCar + 1)) + 99;
         aAlfa    = (eaCuenta % nPos1) + ("0" * nDife) + (eaCuenta % nPos2);
         eaCuenta = aAlfa;
     |FINSI;

     |HAZ SacaNivel;
|FPRC;

|PROCESO LaDivision;
     aDirec0 = "";
     aDirec1 = aDirec;
     aDirec2 = "";

     nSw    = 0;
     aAlfa1 = aDirec % 103;
     |SI aAlfa1 = "CL ";  nSw = 1; |FINSI;
     |SI aAlfa1 = "AV ";  nSw = 1; |FINSI;
     |SI aAlfa1 = "PA ";  nSw = 1; |FINSI;
     |SI aAlfa1 = "PZ ";  nSw = 1; |FINSI;
     |SI aAlfa1 = "PL ";  nSw = 1; |FINSI;
     |SI aAlfa1 = "CR ";  nSw = 1; |FINSI;
     |SI nSw = 1;
         aDirec0 = aAlfa1;
         aDirec1 = aDirec % 440;
     |FINSI;
|FPRC;

|PROCESO GrabaCaclipro;

     aCP = "C";
     |SI aTipoIVA = "S";
         aCP = "P";
     |FINSI;

     #caclipro#23 = aCP;
     #caclipro#10 = #dscsvw94#5;
     |LEE 101#caclipro.=;
     |SI FSalida ! 0;
         |DDEFECTO #caclipro;
         #caclipro#23 = aCP;
         #caclipro#10 = #dscsvw94#5;
         |GRABA 020#caclipro.b;
     |SINO;
         |ACABA;
     |FINSI;

     #caclipro#0  = #dscsvw94#5;
     #caclipro#1  = #dscsvw94#6;
     #caclipro#2  = #dscsvw94#8;

     aAlfa1 = #dscsvw94#10; |QBF aAlfa1;
     aAlfa1 = ("00000" + aAlfa1) % -105;
     #caclipro#25 = aAlfa1;
     aAlfa2 = aAlfa1 % 102; #caclipro#3  = aAlfa2;
     aAlfa2 = aAlfa1 % 303; #caclipro#4  = aAlfa2;

     |ABRE #provinc@;
     |DDEFECTO #provinc@;
     #provinc@#0 = #caclipro#3;
     |LEE 040#provinc@.=;
     #caclipro#5  = #provinc@#1;
     |CIERRA #provinc@;

     #caclipro#6  = #dscsvw94#9;
     #caclipro#7  = #dscsvw94#11;
     #caclipro#9  = #dscsvw94#7;

     |GRABA 020#caclipro.a;
     |LIBERA #caclipro;

     #cacuenta#0 = #caclipro#10;
     |LEE 141#cacuenta.=;
     |SI FSalida ! 0;
         |DDEFECTO #cacuenta;
         #cacuenta#0  = #caclipro#10;
         #cacuenta#1  = dig3;
         #cacuenta#2  = #caclipro#1;
         #cacuenta#3  = "S";
         aAlfa1       = #cacuenta#0; |QBF aAlfa1;
         #cacuenta#54 = (aAlfa1 + "zzzzzzzzzzzz") % 112;
         #cacuenta#55 = dig3;
         #cacuenta#56 = mesdes;
         #cacuenta#57 = meshas;
         |GRABA 020#cacuenta.n;
     |FINSI;

     eaCodNif = #caclipro#9; |QBF eaCodNif;
     |SI eaCodNif ! "";

         #regisnif@#0 = #caclipro#9;
         |LEE 000#regisnif@.=;
         |SI FSalida ! 0;
             |DDEFECTO #regisnif@;
             #regisnif@#0 = #caclipro#9;
             #regisnif@#1 = #caclipro#1;

             aDirec  = #caclipro#2;
             |HAZ LaDivision;
             #regisnif@#2  = aDirec1;    || Direccion
             #regisnif@#3  = aDirec2;    || No tengo el numero
             #regisnif@#4  = #caclipro#3;       || Codigo Provincia
             #regisnif@#5  = #caclipro#4;       || Codigo Poblacion
             #regisnif@#6  = #caclipro#6;       || Poblacion
             #regisnif@#7  = #caclipro#5;       || Provincia
             #regisnif@#8  = #caclipro#7;       || Telefono
             #regisnif@#17 = #caclipro#8;       || Fax
             #regisnif@#9  = aDirec0;    || SG

             |SI #caclipro#24 = "   "; #caclipro#24 = "ES "; |FINSI;
             #regisnif@#12 = #caclipro#24;      || Codigo Pais

             |SI HayBasica = 1;
                  #sgpaises@#0 = #regisnif@#12;
                  |LEE 000#sgpaises@.=;
                  |SI FSalida ! 0; |DDEFECTO #sgpaises@; |FINSI;
                  #regisnif@#13 = #sgpaises@#1;
             |FINSI;

             |SI HayBasica = 0;
                 #sgpaises@#0 = #regisnif@#4;
                 #sgpaises@#1 = #regisnif@#5;
                 |LEE 000#sgpaises@.=;
                 |SI FSalida ! 0; |DDEFECTO #sgpaises@; |FINSI;
                 |SI #regisnif@#12 ! #sgpaises@#6;
                     #regisnif@#12 = #sgpaises@#6;
                 |FINSI;
                 #regisnif@#13 = #sgpaises@#7;
                 #regisnif@#14 = #sgpaises@#4;
                 #regisnif@#15 = #sgpaises@#5;
             |FINSI;

             |GRABA 020#regisnif@.n;
             |LIBERA #regisnif@;
        |FINSI;
     |FINSI;
|FPRC;

|| /////////////////////////////////////////////////////////////////////
|PROCESO AdaptaC;
     |QBF aAlfa2;  || lo que a¤ade
     aAlfa3 = aAlfa2 % 0;
     nDCon0 = aAlfa3;
     |PARA nDCon1 = 1; |SI nDCon1 [ nDCon0; |HACIENDO nDCon1 = nDCon1 + 1;
           aAlfa3 = nDCon1;
           aAlfa3 = aAlfa3 + "01";
           aDCon2 = aAlfa2 % aAlfa3;
           |HAZ adapta2_c;
           aAlfa4 = aAlfa4 + aDCon1;
     |SIGUE;
     aAlfa4 = aAlfa1 + aAlfa4;
|FPRC;

|PROCESO adapta2_c;  ||El concepto
     aDCon1 = "";
     |SI aDCon2 = "S";
         aDCon1 = aSerie; |QBF aDCon1;
         |SI aDCon1 ! ""; aDCon1 = aDCon1 + aSepSer; |QBF aDCon1; |FINSI;
     |FINSI;                                         || Serie
     |SI aDCon2 = "F";
         aDCon1 = aNumFactura; |QBF aDCon1;
         |SI aNumIzq = "S";
             aDCon1 = ("000000" + aDCon1) % -106;
         |FINSI;
     |FINSI;                                         || Factura
     |SI aDCon2 = "C";
         aDCon1 = #dscsvw94#5; |QBF aDCon1;
     |FINSI;                                         || Cuenta Cliente
     |SI aDCon2 = "N";
         aDCon1 = #dscsvw94#6; |QBF aDCon1;
     |FINSI;                                         || Nombre Cliente
     |SI aDCon2 = "R";
         |SI #dscsvw94#15 ! "                    ";
             aDCon1 = #dscsvw94#15; |QBF aDCon1;
         |FINSI;
     |FINSI;

     |SI aDCon1 ! "";
         aDCon1 = " " + aDCon1;
     |FINSI;
|FPRC;

|PROCESO CambioConcepto;
     |DDEFECTO #dsmom030;
     #dsmom030#0 = nConcepto;
     |LEE 001#dsmom030.=;

     aAlfa4 = "";
     aAlfa1 = #dsmom030#1; |QBF aAlfa1;
     aAlfa2 = #caivaasi#45;
     |HAZ AdaptaC;
     aDescr1 = aAlfa4;
     aDescr2 = aAlfa4;

     |SI #caivaasi#130 ! "=  ";
         aAlfa4 = "";
         aAlfa1 = #dsmom030#1; |QBF aAlfa1;
         aAlfa2 = #caivaasi#130;
         |HAZ AdaptaC;
         aDescr2 = aAlfa4;
     |FINSI;
|FPRC;

|PROCESO BorroAntes;
     enDnDIARIO = #camaniva#7;     || diario
     enHnDIARIO = #camaniva#7;
     efDFechas  = #camaniva#8;     || fecha
     efHFechas  = #camaniva#8;
     enDDocume  = #camaniva#9;     || documento
     enHDocume  = #camaniva#9;     || documento
     enBorraAu  = 3;

     |SUB_EJECUTA_NP "caborasi;NOMEQUITESQUETECORTOLOSGUEVOS";
|FPRC;

|PROCESO VerFactura;
     nSwPaso = 0;
     |SELECT #3#camaniva;
     #camaniva#0 = nLibro;
     #camaniva#2 = aSerie;
     #camaniva#3 = aNumFactura;
     |LEE 041#camaniva.=;
     |SI FSalida = 0;
         aAlfa  = ("00" + #camaniva#0) % -102;
         aAlfa1 = #camaniva#2; |QBF aAlfa1;
         |SI aAlfa1 ! "";
             aAlfa1 = aAlfa1 + "/";
         |FINSI;

         aAlfa2 = ("000000" + #camaniva#3) % -106;
         aAlfa1 = aAlfa + "-" + aAlfa1 + aAlfa2;
         aAlfa  = "    "+ aPasar  + "La Factura " + aAlfa1 + " ya existe. (S) Pasar otra vez o (N) ignorar";
         aPasar = "N";
         |CONFI aAlfa;
         |SI FSalida = 0;
             aPasar = "S";
             nDocumSec = #camaniva#9;
         |FINSI;

         |SI aPasar = "N";
             nSwPaso = 1;
         |SINO;
             |HAZ BorroAntes;
         |FINSI;
         |SELECT #1#camaniva;
     |FINSI;
|FPRC;

|PROCESO GrabaCaenlace;
     #caenlace#0 = nDiario;   || Diaro
     #caenlace#1 = fFecha;    || Fecha
     #caenlace#2 = nDocum;    || Documento
     #caenlace#3 = nApunte;
     #caenlace#37 = enEmpresa;
     #caenlace#38 = enPeriodo;
     |LEE 001#caenlace.=;
     |SI FSalida ! 0;
         |DDEFECTO #caenlace;
         #caenlace#0 = nDiario;   || Diaro
         #caenlace#1 = fFecha;    || Fecha
         #caenlace#2 = nDocum;    || Documento
         #caenlace#3 = nApunte;
         #caenlace#37 = enEmpresa;
         #caenlace#38 = enPeriodo;
         |GRABA 020#caenlace.c;
     |FINSI;
|FPRC;

|PROCESO GraboLinea;
     nApunte = nApunte + 10;
     |HAZ GrabaCaenlace;

     eaCuenta   = aCuenta;
     #caenlace#4 = eaCuenta;
     |HAZ SacaNivel;
     #caenlace#5 = enNivel;
     #caenlace#6 = nConcepto;
     #caenlace#7 = aDescripcion;
     #caenlace#8 = aSigno;
     #caenlace#9 = #dscsvw94#13;     || Importe

     #caenlace#10 = aTipoIVA;        || Soportado/Repercutido

     #caenlace#11 = nLibro;
     #caenlace#12 = aSerie;
     #caenlace#13 = aNumFactura;
     #caenlace#14 = 0;               || Recibo 0

     #caenlace#15 = eaDfDepC;        || Departamento
     #caenlace#19 = Usuario;         || Usuario

     #caenlace#26 = aCamp19;         || Tipo Acumulador
     #caenlace#27 = nCamp20;         || Numero

     |SI #caenlace#10 ! "R"; |Y #caenlace#10 ! "S";
         #caenlace#11 = 0;
         #caenlace#12 = "";
         #caenlace#13 = 0;
         #caenlace#26 = "";
         #caenlace#27 = 0;
     |FINSI;

     #caenlace#29 = fFecha;
     #caenlace#31 = "N";  || Deducible
     #caenlace#32 = "N";  || Inversion

     |SI aCamp19 = "F";
         #caenlace#43 = #dscsvw94#6; || Nombre
         #caenlace#44 = #dscsvw94#7; || NIF
     |FINSI;

     #caenlace#45 = #dscsvw94#15;

     |GRABA 020#caenlace.a;

     |SI nUltDoc < nDocum; nUltDoc = nDocum; |FINSI;
|FPRC;

|PROCESO dscsvw94;

    nLibro     = #dscsvw94#1;
    aTipoIVA   = "";
    |SI nLibro = #caivaasi#3;  aTipoIVA = "R"; nConcepto = #caivaasi#5;  |FINSI;
    |SI nLibro = #caivaasi#29; aTipoIVA = "S"; nConcepto = #caivaasi#31; |FINSI;

    aSerie = (#dscsvw94#2 + "     ") % 105;
    aAlfa  = #dscsvw94#3; |QBF aAlfa;
    aAlfa  = aAlfa % -106;
    aAlfa1 = "";
    |PARA nPara1 = 6; |SI nPara1 > 0; |HACIENDO nPara1 = nPara1 - 1;
           nNume2 = (nPara1 * 100) + 1;
           aAlfa2 = aAlfa % nNume2;
           |SI aAlfa2 ]  "0"; |Y aAlfa2 [ "9";
               aAlfa1 = aAlfa2 + aAlfa1;
           |SINO;
               |SAL;
           |FINSI;
    |SIGUE;
    aNumFactura = aAlfa1;
    #dscsvw94#3 = aNumFactura;

    fFecha    = #dscsvw94#4;

    nDiario   = 01;
    nDocumSec = 0;

    nSwPaso   = 0;
    |SI aTipoIVA ! "";
        |HAZ VerFactura;
        |SI nSwPaso = 1; |ACABA; |FINSI;
    |FINSI;

    nApunte    = -9;
    |SI nDocumSec ! 0;
        nDocum = nDocumSec;
    |SINO;
        nDocum = nDOCUMENTO + 1;
        nDOCUMENTO = nDocum;
    |FINSI;

    |HAZ CambioConcepto;
    aDescripcion = aDescr1;
    aCuenta = #dscsvw94#5;
    aCamp19 = "F"; |SI aTipoIVA = ""; aCamp19 = ""; |FINSI;
    nCamp20 = 0;
    aSigno  = "D";
    |SI aTipoIVA = "S"; aSigno = "H"; |FINSI;
    |HAZ GraboLinea;

    aDescripcion = aDescr2;
    aCuenta = #dscsvw94#12;
    aCamp19 = "B"; |SI aTipoIVA = ""; aCamp19 = ""; |FINSI;
    nCamp20 = 1;
    aSigno  = "H";
    |SI aTipoIVA = "S"; aSigno = "D"; |FINSI;
    |HAZ GraboLinea;

    #dscsvw94#16 = "S";
    |SI aTipoIVA = "R"; #dscsvw94#17 = "C"; |FINSI;
    |SI aTipoIVA = "S"; #dscsvw94#17 = "P"; |FINSI;
    |GRABA 040#dscsvw94.a;

    |SI aTipoIVA ! "";
        |HAZ GrabaCaclipro;
    |FINSI;

    |PULSATECLA;
|FPRC;

|DEFBUCLE dscsvw94;
     #dscsvw94#1;
     ;
     0000000001;
     9999999999;
     ;
     NULL, dscsvw94;
|FIN;

|| /////////////////////////////////////////////////////////////////////////
|PROCESO IgualoCampo;
     |SI nNume1 = 4;
         aAlfa1 = (aAlfa1 % -102) + "." + (aAlfa1 % 502) + "." + (aAlfa1 % 104);
     |FINSI;
     |SI nNume1 = 13;
         aAlfa1 = aAlfa1 - ".";
         |PARA; |SI FSalida ! 0; |HACIENDO;
                aAlfa1 = aAlfa1 - ".";
         |SIGUE;
     |FINSI;

     #dscsvw94#nNume1# = aAlfa1;
     aAlfa1 = "";
     nNume1 = nNume1 + 1;
|FPRC;

|PROCESO Facturas;

     aAlfa2 = aLee1 % 105;
     |SI nPrimero = 0;
         nPrimero = 1;
         |SI aAlfa2 ! "LIBRO";
             || ... Es el primer registro y no es el formato esperado
             || ... este ".csv" NO vale
             nPrimero = 2;
             |ACABA;
         |FINSI;
     |FINSI;
     |SI aAlfa2 = "LIBRO"; |ACABA; |FINSI;

     |DDEFECTO #dscsvw94;
     aAlfa1 = "";
     nNume1 = 1;
     |PARA nPara1 = 1; |SI nPara1 [ nLee; |HACIENDO nPara1 = nPara1 + 1;
           nNume2 = (nPara1 * 100) + 1;
           aAlfa2 = aLee1 % nNume2;
           |SI aAlfa2 = ";";
               |HAZ IgualoCampo;
           |SINO;
               aAlfa1 = aAlfa1 + aAlfa2;
           |FINSI;
     |SIGUE;

     nLee = nTotLee - nLee;
     |PARA nPara1 = 1; |SI nPara1 [ nLee; |HACIENDO nPara1 = nPara1 + 1;
           nNume2 = (nPara1 * 100) + 1;
           aAlfa2 = aLee2 % nNume2;
           |SI aAlfa2 = ";";
               |HAZ IgualoCampo;
               |SI nNume1 > nNumCampos; |SAL;|FINSI;
           |SINO;
                aAlfa1 = aAlfa1 + aAlfa2;
           |FINSI;
     |SIGUE;

     |SI aAlfa1 ! ""; |Y nNume1 [ nNumCampos;
         #dscsvw94#nNume1# = aAlfa1;
         aAlfa1 = "";
     |FINSI;

     aAlfa1   = #dscsvw94#14; |QBF aAlfa1; #dscsvw94#14 = aAlfa1 % -102;
     eaCuenta = #dscsvw94#5;  |HAZ CalculaCuenta; #dscsvw94#5 = eaCuenta;
     eaCuenta = #dscsvw94#12; |HAZ CalculaCuenta; #dscsvw94#12 = eaCuenta;

     |SI #dscsvw94#4 < fec1; |O #dscsvw94#4 > fec2;
         |SI nSwSal = 0;
             nSwSal = 2;
             |CONFI "0000NHay Registros con Fecha fuera de Periodo. Seguir con el Pase";
             |SI FSalida ! 0;
                  nSwSal = 1;
             |FINSI;
         |FINSI;
     |SINO;
         nContador   = nContador + 1;
         #dscsvw94#0 = nContador;
         |GRABA 020#dscsvw94.n;
         nSwAlguno = 1;
     |FINSI;

     |LIBERA #dscsvw94;

     |PULSATECLA;
|FPRC;
|| /////////////////////////////////////////////////////////////////////////

|PROCESO ImportaFacturas;
     aAlfa = "Procesando fichero de facturas";
     |VENTANA 0501, 3599, -1, 16, aAlfa;
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     aAlfa = "dscsvw94" + Usuario;
     |NOME_DAT #dscsvw94#aAlfa#;
     |ABRE #dscsvw94;   |CIERRA #dscsvw94;   |DELINDEX #dscsvw94;

     nPrimero  = 0;
     nSwAlguno = 0;
     nContador = 0;
     nLee      = 0;
     aLee1     = "";
     aLee2     = "";
     nSumaLee  = 0;
     aAlfa1    = "";

     |ABRE #dscsvw94;
     |XABRE "B", aFicTmp, nHandle;
     |PARA; |SI; |HACIENDO;

            |XLEE nHandle, 1, aAlfa;
            |SI FSalida ! 1;   |SAL;   |FINSI;

            nLeeNume = &aAlfa;
            |SI nLeeNume = 10;

                nNume1 = nTotLee + nSumaLee;
                |SI nNume1 < 215;
                    aLee1 = aLee1 + aAlfa1;
                    nLee  = nNume1;
                |SINO;
                    aLee2 = aLee2 + aAlfa1;
                |FINSI;
                nTotLee  = nTotLee + nSumaLee;
                nSumaLee = 0;
                aAlfa1   = "";

                |HAZ Facturas;
                |SI nSwSal = 1; |O nPrimero = 2;
                    nSwAlguno = 0;
                    |SAL;
                |FINSI;
                aLee1    = "";
                aLee2    = "";
                nLee     = 0;
                nTotLee  = 0;
                nSumaLee = 0;
                aAlfa1   = "";
            |SINO;
                |ATRI P; |PINTA 1010, nLeeNume; |ATRI 0;
                |SI nLeeNume > 31;
                    aAlfa1 = aAlfa1 + aAlfa;
                    nSumaLee = nSumaLee + 1;
                    |SI aAlfa = ";";
                        nNume1 = nTotLee + nSumaLee;
                        |SI nNume1 < 215;
                            aLee1 = aLee1 + aAlfa1;
                            nLee  = nNume1;
                        |SINO;
                            aLee2 = aLee2 + aAlfa1;
                        |FINSI;
                        nTotLee  = nTotLee + nSumaLee;
                        nSumaLee = 0;
                        aAlfa1   = "";
                    |FINSI;
                |FINSI;
            |FINSI;
     |SIGUE;
     |XCIERRA nHandle;

     |CIERRA #dscsvw94;

     |SI nSwAlguno = 1;
         aAlfa = "caenlace" + Usuario;
         |NOME_DAT #caenlace#aAlfa#;
         |DELINDEX #caenlace;

         |HAZ contador;

         nSwPaso = 0;
         aPasar  = "N";
         |ABRE #caenlace;
         |ABRE #regisnif@;
         |BUCLE dscsvw94;
         |CIERRA #regisnif@;
         |CIERRA #caenlace;

         nDOCUMENTO = nUltDoc;
         |HAZ ActContador;
         enNoModif = 0;
         |SI aModAsiento = "N";
             enNoModif = 100;  || para que no se modifiquen los apuntes
         |FINSI;

         |DFICE aAlfa1; |QBF aAlfa1;
         enNoEndatos = 1;
         aAlfa  = "caenlace" + Usuario;
         aAlfa1 = "dsapunte.tab;" + aAlfa1 + "," + aAlfa;
         |SUB_EJECUTA_NP aAlfa1;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;
|FPRC;
|| ////////////////////////////////////////////////////////////////

|PROCESO Cobros;
     #calincob#0  = #dscsvw94#1;
     #calincob#1  = #dscsvw94#3;
     #calincob#2  = #dscsvw94#2;
     #calincob#3  = 1;
     |LEE 000#calincob.=;
     |SI FSalida = 0;
         aAlfa = "Cobro del recibo ";
         aAlfa = aAlfa + #calincob#2 + "  / " + #calincob#1 + " / " + #calincob#3;
         aAlfa = aAlfa + " ya existe";

         #dscsvw93#0 = aAlfa;
         |GRABA 000#dscsvw93.n;
         |LIBERA #dscsvw93;

         |ACABA;
     |FINSI;

     |DDEFECTO #calincob;
     #calincob#0  = #dscsvw94#1;
     #calincob#1  = #dscsvw94#3;
     #calincob#2  = #dscsvw94#2;
     #calincob#3  = 1;
     #calincob#4  = #dscsvw94#5;
     #calincob#6  = #dscsvw94#4;
     #calincob#7  = #dscsvw94#4;
     #calincob#8  = #dscsvw94#13;

     #calincob#14 = #calincob#7;
     #calincob#28 = #dscsvw94#14;
     |GRABA 020#calincob.n;
     |LIBERA #calincob;

     nNumDoc = nNumDoc + 1;

     #caclipro#23 = "C";
     #caclipro#10 = #calincob#4;
     |LEE 000#caclipro.=;

     |DDEFECTO #Puente@;
     #Puente@#0  = #calincob#0;            || Libro
     #Puente@#1  = #calincob#2;            || Serie
     #Puente@#2  = #calincob#1;            || Factura
     #Puente@#3  = #calincob#3;            || Numero recibo
     #Puente@#4  = #calincob#7;            || Fecha vto.
     #Puente@#5  = #calincob#6;            || Fecha emision
     #Puente@#6  = #calincob#4;            || Cuenta Cliente
     #Puente@#7  = #dscsvw94#6;            || Nombre Cliente
     #Puente@#8  = #calincob#15;           || Nombre Banco
     #Puente@#9  = #caclipro#29;           || Entidad
     #Puente@#10 = #caclipro#30;           || Sucursal
     #Puente@#11 = #caclipro#31;           || DC
     #Puente@#12 = #caclipro#32;           || N§ Cuenta
     #Puente@#13 = #calincob#11;           || Tipo de recibo
     #Puente@#14 = "";                     || Departamento
     #Puente@#15 = #calincob#8;            || Importe Recibo
     #Puente@#16 = "";                     || A Aceptar
     #Puente@#17 = "";                     || Aceptado
     #Puente@#18 = "";                     || Codigo Cliente (GESTION)
     #Puente@#19 = #calincob#28;           || Representante
     #Puente@#21 = "";                     || Zona
     #Puente@#22 = nNumDoc;                || N§ Documento
     #Puente@#23 = enEmpresa;              || Codigo Empresa
     #Puente@#24 = enPeriodo;              || Periodo Empresa
     #Puente@#25 = "";                     || Empresa gestion
     #Puente@#26 = "N";                    || Recibo a cuenta
     #Puente@#27 = "V";                    || Tipo de recibo
     #Puente@#28 = "A";
     #Puente@#29 = "EUR";               || Divisa
     #Puente@#30 = "EURO";              || Nombre Divisa
     #Puente@#31 = 1;                   || Cambio Divisa en EUROS
     #Puente@#32 = 1;                   || Cambio M.Base en EUROS
     #Puente@#33 = #caclipro#28;
     #Puente@#40 = #caclipro#20;
     #Puente@#48 = #caclipro#40;
     #Puente@#49 = #caclipro#41;

     |GRABA 020#Puente@.n;

     |PULSATECLA;
|FPRC;

|PROCESO Pagos;
     #calinpag#0  = #dscsvw94#1;
     #calinpag#1  = #dscsvw94#3;
     #calinpag#2  = #dscsvw94#2;
     #calinpag#3  = 1;
     |LEE 000#calinpag.=;
     |SI FSalida = 0;
         aAlfa = "Pago del recibo ";
         aAlfa = aAlfa + #calinpag#2 + "  / " + #calinpag#1 + " / " + #calinpag#3;
         aAlfa = aAlfa + " ya existe";

         #dscsvw93#0 = aAlfa;
         |GRABA 000#dscsvw93.n;
         |LIBERA #dscsvw93;
         |ACABA;
     |FINSI;

     |DDEFECTO #calinpag;

     #calinpag#0  = #dscsvw94#1;
     #calinpag#1  = #dscsvw94#3;
     #calinpag#2  = #dscsvw94#2;
     #calinpag#3  = 1;
     #calinpag#4  = #dscsvw94#5;
     eaCuenta = #calinpag#4;
     |HAZ SacaNivel;
     #calinpag#5  = enNivel;
     #calinpag#6  = #dscsvw94#4;
     #calinpag#7  = #dscsvw94#4;
     #calinpag#8  = #dscsvw94#13;

     |GRABA 020#calinpag.n;
     |LIBERA #calinpag;

     nNumDoc = nNumDoc + 1;

     #caclipro#23 = "P";
     #caclipro#10 = #calincob#4;
     |LEE 000#caclipro.=;

     |DDEFECTO #Puente@;
     #Puente@#0  = #calinpag#0;            || Libro
     #Puente@#1  = #calinpag#2;            || Serie
     #Puente@#2  = #calinpag#1;            || Factura
     #Puente@#3  = #calinpag#3;            || Numero recibo
     #Puente@#4  = #calinpag#7;            || Fecha vto.
     #Puente@#5  = #calinpag#6;            || Fecha emision
     #Puente@#6  = #calinpag#4;            || Cuenta Cliente
     #Puente@#7  = #dscsvw94#6;            || Nombre Cliente
     #Puente@#8  = #calincob#15;           || Nombre Banco
     #Puente@#9  = #caclipro#29;           || Entidad
     #Puente@#10 = #caclipro#30;           || Sucursal
     #Puente@#11 = #caclipro#31;           || DC
     #Puente@#12 = #caclipro#32;           || N§ Cuenta
     #Puente@#13 = #calinpag#11;           || Tipo de recibo
     #Puente@#14 = "";                     || Departamento
     #Puente@#15 = #calinpag#8;            || Importe Recibo
     #Puente@#16 = "";                     || A Aceptar
     #Puente@#17 = "";                     || Aceptado
     #Puente@#18 = "";                     || Codigo Cliente (GESTION)
     #Puente@#21 = "";                     || Zona
     #Puente@#22 = nNumDoc;                || N§ Documento
     #Puente@#23 = enEmpresa;              || Codigo Empresa
     #Puente@#24 = enPeriodo;              || Periodo Empresa
     #Puente@#25 = "";                     || Empresa gestion
     #Puente@#26 = "N";                    || Recibo a cuenta
     #Puente@#27 = "C";                    || Tipo de recibo
     #Puente@#28 = "A";
     #Puente@#29 = "EUR";               || Divisa
     #Puente@#30 = "EURO";              || Nombre Divisa
     #Puente@#31 = 1;                   || Cambio Divisa en EUROS
     #Puente@#32 = 1;                   || Cambio M.Base en EUROS
     #Puente@#33 = #caclipro#28;
     #Puente@#40 = #caclipro#20;
     #Puente@#48 = #caclipro#40;
     #Puente@#49 = #caclipro#41;

     |GRABA 020#Puente@.n;
     |PULSATECLA;
|FPRC;

|PROCESO Vencimientos;
     aTipo = #dscsvw94#17;
     |SI aTipo = "C";  |HAZ Cobros;  |FINSI;
     |SI aTipo = "P";  |HAZ Pagos;   |FINSI;
|FPRC;

|DEFBUCLE Vencimientos;
     #dscsvw94#1;
     16;
     0000000001, "S";
     9999999999, "S";
     ;
     NULL, Vencimientos;
|FIN;

|PROCESO ImportaVencimientos;
     eaPath = #caivaasi#56; |QBF eaPath;
     |SI eaPath = "";
         |MENAV "0000Configure la aplicaci¢n de cartera en Control Defecto Entrada de IVA, no puedo importar vencimientos.";
         |ACABA;
     |FINSI;

     |HAZ CreaPuente;

     |SI eaRetorno = "ERROR";
         |MENAV "0000No est  la cartera instalada, no puedo importar vencimientos";
         |ACABA;
     |FINSI;

     aAlfa = "dscsvw93" + Usuario;
     |NOME_DAT #dscsvw93#aAlfa#;
     |ABRE #dscsvw93;   |CIERRA #dscsvw93;   |DELINDEX #dscsvw93;

     aAlfa = "Procesando fichero de Vencimientos";
     |VENTANA 0501, 3599, -1, 16, aAlfa;
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     |ABRE #dscsvw93;
     |BUCLE Vencimientos;

     |SI nNumDoc ! 0;
         |HAZ CierraPuente;
     |FINSI;

     |SI nNumDoc = 0;
         |HAZ CiePuente2;
     |FINSI;

     |LEE 000#dscsvw93.p;
     |SI FSalida = 0;
         |MENAV "0000Ha habido errores en la importaci¢n de recibos. A continuaci¢n informamos de los recibos afectados";

         |PARA;  |SI;  |HACIENDO;
              |CONSULTA_CLAVE #1#dscsvw93;
              |CONFI "0000SQuiere salir de la consulta";
              |SI FSalida = 0;  |SAL;  |FINSI;
         |SIGUE;
     |FINSI;

     |CIERRA #dscsvw93;
     |DELINDEX #dscsvw93;

     |XCIERRA nHandle;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;
|FPRC;

|PROCESO pinta;
   |SI nMes =  1; aMes = "Enero";     |ACABA;  |FINSI;
   |SI nMes = 12; aMes = "Diciembre"; |ACABA;  |FINSI;
   |SI nMes =  2; aMes = "Febrero";   |ACABA;  |FINSI;
   |SI nMes =  3; aMes = "Marzo";     |ACABA;  |FINSI;
   |SI nMes =  4; aMes = "Abril";     |ACABA;  |FINSI;
   |SI nMes =  5; aMes = "Mayo";      |ACABA;  |FINSI;
   |SI nMes =  6; aMes = "Junio";     |ACABA;  |FINSI;
   |SI nMes =  7; aMes = "Julio";     |ACABA;  |FINSI;
   |SI nMes =  8; aMes = "Agosto";    |ACABA;  |FINSI;
   |SI nMes =  9; aMes = "Septiembre";|ACABA;  |FINSI;
   |SI nMes = 10; aMes = "Octubre";   |ACABA;  |FINSI;
   |SI nMes = 11; aMes = "Noviembre"; |ACABA;  |FINSI;
|FPRC;

|PROCESO Importacion;
     aAlfa = aPathTmp + "facturas.txt";        |FBORRA aAlfa;

     aOrigen    = aFicBrowse;
     aDestino   = aPathTmp + "facturas.txt";
     |HAZ CopiaFichero;

     aFicTmp = aDestino;  |QBF aFicTmp;
     |SI aFicTmp ! "";
         |HAZ ImportaFacturas;
     |FINSI;

     |SI enNoEndatos = 100;  |ACABA; |FINSI;

     |SI nSwAlguno = 1;
         nHay = 1;
         |HAZ ImportaVencimientos;
     |FINSI;
|FPRC;

|PROCESO TrataDirectorio;

     nMes = dig1; |HAZ pinta; mesdes = aMes;
     nMes = dig1 + 11;
     |SI nMes > 12; nMes = nMes - 12; |FINSI;
     |HAZ pinta; meshas = aMes;

     aQueQuiero = "|NC";
     aNumCampos = #dscsvw94#aQueQuiero#;
     nNumCampos = aNumCampos;
     nNumCampos = nNumCampos - 1;

     nHay   = 0;
     nSwSal = 0;
     aSwCS  = "";
     aAlfa  = #dscsvz91#1; |QBF aAlfa;
     aDirectorio = aAlfa + "*";
     |_PDIR aDirectorio;
     |PARA; |SI FSalida = 0; |HACIENDO;
            aFicBrowse = aDirectorio; |QBF aFicBrowse;
            aAlfa1 = aFicBrowse % -104;
            |SI aAlfa1 = ".csv";
                aSwCS = "S";
                |HAZ Importacion;
                |SI enNoEndatos = 100; |O nSwSal = 1;
                    |SAL;
                |SINO;
                    |FBORRA aFicBrowse;
                |FINSI;
            |FINSI;
            |_SDIR aDirectorio;
     |SIGUE;

     |SI aSwCS = "";
         aAlfa = #dscsvz91#1;
         |QBF aAlfa;
         aDirectorio = aAlfa + "*";
         |R_PDIR aDirectorio;
         |PARA; |SI FSalida = 0; |HACIENDO;
                aFicBrowse = aDirectorio; |QBF aFicBrowse;
                aAlfa1 = aFicBrowse % -104;
                |SI aAlfa1 = ".csv";
                    aSwCS = "C";
                    |HAZ Importacion;
                    |SI enNoEndatos = 100; |O nSwSal = 1;
                        |SAL;
                    |SINO;
                        |RFBORRA aFicBrowse;
                    |FINSI;
                |FINSI;
                |R_SDIR aDirectorio;
         |SIGUE;
     |FINSI;

     |SI aSwCS = "";
         |MENAV "0000No hay ficheros .cvs para Importar";
     |SINO;
         |SI nHay = 0;
             |MENAV "0000Seleccion vacia";
         |FINSI;
     |FINSI;
|FPRC;

|| ///////////////////////////////////////////////////////////////////

|PROCESO CopiaFichero;
     |SI eaIP   = "";
         |COPIA_FICHERO aOrigen, aDestino;
     |SINO;
         |RECIBE_FICHERO aOrigen, aDestino;
     |FINSI;
|FPRC;

|PROCESO RecogePath;
     nFSalida = FSalida;

     |ESTADO_CONTROL nBtnPat1, "DISABLE";

     aFicBrowse = "D" + #dscsvz91#1;  |QBF aFicBrowse;
     |BROWSE aFicBrowse;

     aAlfa      = aFicBrowse;   |QBF aAlfa;
     aFicBrowse = "";
     aLong      = aAlfa % 0;
     nLong      = aLong;
     |PARA nCarac = 2;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos  = (nCarac * 100) + 1;
           aFicBrowse = aFicBrowse + (aAlfa % nPos);
     |SIGUE;

     |ESTADO_CONTROL nBtnPat1, "ENABLE";

     |SI aFicBrowse = "";  |ACABA;  |FINSI;

     #dscsvz91#1 = aFicBrowse + "\";  |PINTA #dscsvz91#1;
|FPRC;

|PROCESO Cancelar;
     |PTEC 807;
|FPRC;

|PROCESO ModAsiento;
     |FIN_CONTROL_WINDOWS nBtnUni;

     |SI aModAsiento = "SI";
         aModAsiento = "NO";
     |SINO;
         aModAsiento = "SI";
     |FINSI;

     aAlfa = "BOTON," + aModAsiento;
     |CONTROL_SIMPLE nPanta, aAlfa, 2116, 2319, ModAsiento; nBtnUni = FSalida;
|FPRC;

|PROGRAMA;
     aTextoVentana = "Importaci¢n MANTAS MORA";
     |VENTANA 0501, 3599, -1, 16, aTextoVentana;
     n0Vent = FSalida;

     |PINPA #0#dscsvz91;  nPanta = FSalida;
     |PINDA #0#dscsvz91;

     aModAsiento = "NO";

     |CONTROL_SIMPLE nPanta, "LABEL,{{15}}Asientos e IVA", 2202, 2215, NULL;
     |CONTROL_SIMPLE nPanta, "LABEL,{{15}}se dejan modificables", 2221, 2240, NULL;

     aAlfa = "BOTON," + aModAsiento;
     |CONTROL_SIMPLE nPanta, aAlfa, 2116, 2319, ModAsiento; nBtnUni = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}", 0642, 0648, RecogePath; nBtnPat1 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{207}}Cancelar", 3570, 3582, Cancelar;
     nBtnCan = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::{{230}}Continuar";
          |LEETECLA aTecla;
          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
          |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nBtnPat1;
     |FIN_CONTROL_WINDOWS nBtnCan;

     |SI aTecla = aEsc2;
         |MENAV "0000Proceso cancelado.";

         |FINVENTANA n0Vent;

         |PULSATECLA;
         |PULSATECLA;
         |PULSATECLA;
         |ACABA;
     |FINSI;

     |HAZ TrataDirectorio;

     |FINVENTANA n0Vent;

     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
|FPRO;

|PROCESO Tipo80;  |TIPO 80;

     |ABRE #dscsvz91;
     |LEE 101#dscsvz91.p;
     |SI FSalida ! 0;
         |DDEFECTO #dscsvz91;
         |GRABA 020#dscsvz91.b;
     |FINSI;

     nChequeo = 0;
     eaIP     = "";  |IP_REMOTO eaIP;

     |HAZ LeeUnidadBasico;

     aPathLocal = eaUnidadBasico + "\documentos";   |RMKDIR aPathLocal;
     aPathLocal = aPathLocal + "\tmp";              |RMKDIR aPathLocal;
     aPathLocal = aPathLocal + "\";

     |DBASE aBase;
     aPathTmp   = aBase     + "tmp";           |MKDIR aPathTmp;
     aPathTmp   = aPathTmp  + "/" + Usuario;   |MKDIR aPathTmp;
     aPathTmp   = aPathTmp  + "/";

     aPathContagen = eaUnidadBasico + "\documentos";   |RMKDIR aPathContagen;
     aPathContagen = aPathContagen + "\contagen\";     |RMKDIR aPathContagen;

     aAlfa = aPathTmp + "facturas.txt";        |FBORRA aAlfa;

     |ABRET #cacuenta;

     |HAZ inicio;
     |HAZ DirAplicSt;
     |HAZ Referencias;

     |ABRET #caclipro;
     |ABRET #camaniva;
     |ABRET #calincob;
     |ABRET #calinpag;
     |ABRET #calibros;
     |ABRET #caivases;
     |ABRET #dsmom030;

     |ABRE #caivaasi;
     |LEE 000#caivaasi.p;
     |SI FSalida ! 0;  |DDEFECTO #caivaasi;  |FINSI;
     |CIERRA #caivaasi;

     aSepSer = #caivaasi#97; |QBF aSepSer;
     aNumIzq = #caivaasi#98;

     |PULSATECLA;
     |PULSATECLA;
|FPRC;

|PROCESO Tipo90;  |TIPO 90;
     aAlfa = aPathTmp + "facturas.txt";        |FBORRA aAlfa;

     |CIERRAT #cacuenta;
     |CIERRAT #caclipro;
     |CIERRAT #camaniva;
     |CIERRAT #calincob;
     |CIERRAT #calinpag;
     |CIERRAT #calibros;
     |CIERRAT #caivases;
     |CIERRAT #dsmom030;

     #dscsvz91#2 = "";

     |GRABA 020#dscsvz91.a;
     |LIBERA #dscsvz91;
     |CIERRA #dscsvz91;

     |PULSATECLA;
     |PULSATECLA;
|FPRC;
