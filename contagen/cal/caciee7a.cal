|FICHEROS;
     cacieex7 #0,MANTE;   || Auxiliar Apuntes Lineas
     cacieexa #11;  || def de proceso Cabecera

     cacuenta #1;   || cuentas
     camaasic #2;   || apuntes cabs.
     camaasil #3;   || apuntes lins.
     :/modelos/def/dsmom030 #4;   || conceptos automaticos
     camandia #5;   || diarios
     caconasi #20;  || Contador Asientos
     caconi15 #100; || Control de Asientos

     catw0011 #900,MANTE;
     catw0012 #901,MANTE;
     catw0014 #903,MANTE;
|FIN;

|VARIABLES;
    nBuena     = 0;
    nDocumento = 0;
    &entrada = 1;
    &modcon  = 0;
    &swpanta = 0;
    &swcalcu = 0;
    &algun   = 0;
    nDesglose = 0;
    aBaseApli   = "";
    p_alfa1 = "";
    p_error = 0;
    mierda = 0;
    nNume    = 0;
    nNume1   = 0;
    nNume2   = 0;
    nNume3   = 0;
    nDiDeAnt = 0;
    nDiHaAnt = 0;
    nCoAnt   = 0;
    nImpAnt  = 0;
    nCampo   = 0;
    nCampo1  = 0;
    nCont    = 0;
    nSw      = 0;
    nLineal  = 0;
    nConcep  = 0;
    nImport  = 0;
    nCampos  = 0;
    nDigito  = 0;
    nRetencion = 0;
    nBeneficio = 0;

    aLong    = "";
    aAvisos  = "";
    aCtDeAnt = "";
    aCtHaAnt = "";
    aDeAnt   = "";
    aDhAnt   =  "";
    aAlfa1   = "";
    aAlfa2   = "";
    aAlfa3   = "";
    aAlfa    = "";
    aConcep  = "";
    aCuenta  = "";
    nSwError = 0;

    Comodin = "";
    nModoEntrada = 0;
    nOp     = 0;
    nSegDoc = 0;
    aSigno  = "";
    nImporteL = 0;
    nDAny   = 0;
    nHAny   = 0;
|FIN;

|INCLUYE dscont_i;

|PROCESO Aviso5;         || desde calculo 'c_importe' (camaasil)
     |PUSHV 09101171;

     |HAZ ColorF;
     |CUADRO 09 10 11 71;
     |HAZ ColorC;
     |QBF aAvisos;

     aAlfa1 = aAvisos % 0;  nNume1 = aAlfa1;
     nNume2 = ((60 - nNume1) / 2) ! 0;
     nNume1 = 60 - nNume1 - nNume2;
     aAvisos = (" " * nNume1) + aAvisos + (" " * nNume2);        || lo centra

     |PINTA 1011, aAvisos;
     |ATRI 0;
     |AVISO;
|FPRC;

|PROCESO Aviso9;         || desde calculo 'descuadre', 'modbaj'
     |ATRI 0;
     |POPV;
|FPRC;

|| ***********************************************************
|PROCESO CuenFin; |TIPO 0;
     |HAZ Signo;
     |ABRE #1;
     nCampo  = Campo;
     nCampo1 = Campo + 1;
     #1#0 = #0nCampo;
     #1#1 = #0nCampo1;
     |LEE 001#1=;
     |SI FSalida ! 0;  |ERROR;  |ACABA;  |FINSI;
     |SI #1#3 = "N";
         |MENAV "     La Cuenta NO tiene PASE DIRECTO";
         |ERROR;
     |FINSI;
     |CIERRA #1;
|FPRC;

|PROCESO CuentaTl;
     aAlfa1 = #0nCampo;    |QBF aAlfa1;
     aAlfa2 = aAlfa1 % 0;
     aLong  = aAlfa2;
     nCont  = 0;
     nSw    = 0;

     |SI aLong = dig4;  nSw = 1;  #0nCampo1 = 1;  |FINSI;
     |SI aLong = dig5;  nSw = 1;  #0nCampo1 = 2;  |FINSI;
     |SI aLong = dig6;  nSw = 1;  #0nCampo1 = 3;  |FINSI;
     |SI aLong = dig7;  nSw = 1;  #0nCampo1 = 4;  |FINSI;
     |SI aLong = dig8;  nSw = 1;  #0nCampo1 = 5;  |FINSI;
     |SI aLong = dig9;  nSw = 1;  #0nCampo1 = 6;  |FINSI;
     |SI aLong = 0;     nSw = 1;  #0nCampo1 = 0;  |FINSI;

     |SI nSw = 0;
         #0nCampo1 = 0;
         |MENAV "      Longitud de Cuenta Fuera de Nivel ";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Cuenta; |TIPO 6;                    || desde campo 'cuenta'
     salidas = FSalida;
     |SI salidas = 15;
         |SI Campo = 8;
             #0#8 = aCtHaAnt;
             #0#9 = nDiHaAnt;
         |FINSI;
         |SI Campo = 10;
             #0#10 = aCtDeAnt;
             #0#11 = nDiDeAnt;
         |FINSI;
         |PINTA #0Campo;
         salidas = 0;
     |FINSI;

     mierda  = 0;

     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;

     nCampo  = Campo;
     nCampo1 = Campo + 1;
     |HAZ CuentaTl;
|FPRC;

|PROCESO CCuenta; |TIPO 7;
     nNume = (FEntrada / 100) ! 0;
     |C_M #0#8,  1, "S";
     |C_M #0#10, 1, "S";

     |SI #0#8 ! "            "; |O #0#10 ! "            ";
         |SI Campo = 8;  |Y #0#10 ! "            "; |C_M #0#8, 1,"N"; |FINSI;
         |SI Campo = 10; |Y #0#8  ! "            "; |C_M #0#10,1,"N"; |FINSI;
     |FINSI;
|FPRC;

|PROCESO NoCero; |TIPO 0;
     salidas = FSalida;
     |HAZ Aviso9;
     |SI #0Campo = 0; |Y salidas ! 11;
         |ERROR;
         |ACABA;
     |SINO;
         |SI salidas = 11;   || <F3>
             |SI #0#6 = "D";  #0#7 = #11#21 * -1;  |FINSI;
             |SI #0#6 = "H";  #0#7 = #11#21;       |FINSI;
             |PINTA #0#7;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Signo;
     |SI #0#10 ! "            ";
         #0#6 = "D";
         #0#2 = #0#10;
         #0#3 = #0#11;
         #0#8 = "";
         #0#9 = 0;
     |FINSI;

     |SI #0#8  ! "            ";
         #0#6 = "H";
         #0#2 = #0#8;
         #0#3 = #0#9;
         #0#10 = "";
         #0#11 = 0;
     |FINSI;
|FPRC;

|PROCESO CImporte; |TIPO 7;
     |HAZ Signo;
     nNume = (FEntrada / 100) ! 0;
     |SI nNume = 1;
         #0#7 = nImpAnt;
         |PINTA #0#7;
     |FINSI;
|FPRC;

|PROCESO CDescrip; |TIPO 7;
       |SI eaSobreEs ! "S";
           |PTEC 816;
       |FINSI;
|FPRC;

|PROCESO Descrip; |TIPO 0;
     salidas = FSalida;
     |SI FSalida = 15;
         #0#5 = aDeAnt;
         |PINTA #0#5;
     |FINSI;
|FPRC;

|PROCESO Concepto; |TIPO 0;             || desde campo 'concepto'
     salidas = FSalida;
     |ABRE #4;
     #4#0 = #0#4;
     |LEE 040#4=;
   |SI #dsmom030#8 ! "S";
       |MENAV "    El concepto no corresponde al Tipo de Apunte";
       |ERROR;
   |FINSI;
     |SI FEntrada = 101;
         |SI #0#4 = nCoAnt; |Y #11#19 ! #11#20;
             #0#5 = aDeAnt;
         |SINO;
             #0#5 = #4#1;
         |FINSI;
     |SINO;
         |SI #0#4 = nCoAnt;
             #0#5 = aDeAnt;
         |SINO;
             #0#5 = #4#1;
         |FINSI;
     |FINSI;
     |PINTA #0#5;
     |CIERRA #4;
|FPRC;

|PROCESO ColorC;        || activa el color caracter en Avisos
     |COLOR 14, 0;
     |ATRI 0;
|FPRC;

|PROCESO ColorF;        || activa el color graficos en Avisos
     |COLOR 4, 0;
     |ATRI 0;
|FPRC;


|PROCESO Tipo3; |TIPO 3;          || desde campo 'linea'

     aAlfa1 = #0#10;   |QBF aAlfa1;
     aAlfa2 = #0#8;    |QBF aAlfa2;
     |SI aAlfa1 = "";  |Y aAlfa2 = "";    ||al menos una cuenta cargada;
         aAvisos = "ATENCION!!! Apunte sin cuentas ...";
         |HAZ Aviso5;
         |PAUSA;
         |HAZ Aviso9;
         |HAZ DejoBien;
         |ERROR;
         |ACABA;
     |FINSI;

     |SI #0#7 = 0;                       || importe distinto de cero
         aAvisos = "ATENCION!!! Importe cero ...";
         |HAZ Aviso5;
         |PAUSA;
         |HAZ Aviso9;
         |ERROR;
         |ACABA;
     |FINSI;

     |SI aAlfa1 ! "";
         aCtDeAnt = #0#10;
         nDiDeAnt = #0#11;
     |FINSI;

     |SI aAlfa2 ! "";
         aCtHaAnt = #0#8;
         nDiHaAnt = #0#9;
     |FINSI;

     nCoAnt  = #0#4;
     aDeAnt  = #0#5;
     aDhAnt  = #0#6;
     nImpAnt = #0#7;
|FPRC;

|PROCESO Salta; |TIPO 0;
     nModoEntrada = (FEntrada / 100) ! 0;
     salidas = FSalida;
     |SI nModoEntrada = 1; |Y #0#1 ! 1;
         |SI nCoAnt ! 0;
             #0#4 = nCoAnt;
             #0#5 = aDeAnt;
             #0#6 = aDhAnt;
         |FINSI;
         |PINTA #0#4;
         |PINTA #0#5;
         #0#0  = #11#22;
         #0#12 = #11#2;  ||un solo documento
         |SI nSegDoc ! 0; |Y #0#1 > nSegDoc;
             #0#12 = #11#31;
         |FINSI;
     |FINSI;
|FPRC;


|PROCESO Tipo2; |TIPO 2;

     |HAZ Signo;

     |SI #0#6 = "D"; #11#19 = #11#19 +. #0#7; |FINSI;
     |SI #0#6 = "H"; #11#20 = #11#20 +. #0#7; |FINSI;

     #11#21 = #11#19 - #11#20;

     |PINTA #11#19;  |PINTA #11#20;  |PINTA #11#21;
|FPRC;

|PROCESO DejoBien;
     |SI nModoEntrada ! 1; |ACABA; |FINSI;

     |SI #0#6 = "D"; #11#19 = #11#19 - #0#7; |FINSI;
     |SI #0#6 = "H"; #11#20 = #11#20 - #0#7; |FINSI;
     #11#21 = #11#19 - #11#20;
     |PINTA #11#19;  |PINTA #11#20;  |PINTA #11#21;
|FPRC;

|PROCESO Tipo11;  |TIPO 11;
    salidas = FSalida;
|FPRC;

|| **********************************************************************
|| PORCESO DE CREACION DE LAS LINEAS
|| **********************************************************************

|PROCESO PonSaldo;

     aAlfa3 = #0#2; |QBF aAlfa3;
     |SI aAlfa3 = ""; |Y nSwError = 0;
         |MENAV "    ATENCION!!!. APUNTES CON CUENTAS EN BLANCO. REPASE EL ASIENTO";
         nSwError = 1;
     |FINSI;

     |SI #0#6 = "D";
         #11#19 = #11#19 + #0#7;
     |FINSI;
     |SI #0#6 = "H";
         #11#20 = #11#20 + #0#7;
     |FINSI;
     #11#21 = #11#19 - #11#20;

     |PINTA #11#19;
     |PINTA #11#20;
     |PINTA #11#21;

     |SI #0#2 = #100#7; |Y nDesglose = 0;
         |SI #0#6 = "D";  nBeneficio = nBeneficio - #0#7;  |FINSI;
         |SI #0#6 = "H";  nBeneficio = nBeneficio + #0#7;  |FINSI;
     |FINSI;
|FPRC;

|| ***********************************************************
|PROCESO CuentaDirectoNueva;
     |SI #901#6 = #11#26;
         |SI #901#10 ! doce;
             #0#2 = #901#10;
         |SINO;
             eaCuenta = #0#2 - "NUEVA "
             #0#2 = eaCuenta;
         |FINSI;
     |FINSI;
     |HAZ CuentaDirecto;
     |SI #901#6 = #11#26;
         #901#10 = #0#2;
         |GRABA 020#901a;

         |SI #901#9 = 35; #11#53 = #0#2; |FINSI;
         |SI #901#9 = 44; #11#54 = #0#2; |FINSI;
         |SI #901#9 = 48; #11#55 = #0#2; |FINSI;
     |FINSI;
|FPRC;

|PROCESO CuentaDirecto;
     eaCuenta = #0#2; |QBF eaCuenta;
     nBuena = 0;
     aAlfa1 = eaCuenta % -101;
     |SI aAlfa1 = "*";
         eaCuenta = eaCuenta - "*";
         nBuena = 1;
     |SINO;
         |SI aAlfa1 = "a"; |O aAlfa1 = "u";
             nBuena = 2;
         |FINSI;
     |FINSI;
     |SI nBuena = 0;
         aAlfa1 = eaCuenta - ".";
         |SI aAlfa1 ! eaCuenta; nBuena = 2; |FINSI;
     |FINSI;

     |SI nBuena = 0;
         |ABRE #1;
         |DDEFECTO #1;
         #1#0 = eaCuenta;
         |LEE 001#1=;
         |SI FSalida ! 0; |O #1#3 = "N";
             nBuena = 1;
         |FINSI;
         |CIERRA #1;
     |FINSI;
     |SI nBuena = 1;
         nNume1 = 100 + MaximoDigitos;
         aAlfa1 = (eaCuenta + "000000000000") % nNume1;
         eaCuenta = aAlfa1;
         nBuena = 2;
     |FINSI;
     |SI nBuena = 2;
         salidas  = FSalida;
         Campo    = 2;
         mierda   = 1;
         |HAZ FiltraPuntos;
         #0#2     = eaCuenta;
     |FINSI;
|FPRC;

|| ***********************************************************
|PROCESO Perdida;
     nLineal = nLineal + 3;

     |DDEFECTO #0;
     #0#0  = #11#22;
     #0#1  = nLineal;
     #0#2  = #100#31;
     #0#3  = #100#32;
     |HAZ CuentaDirecto;
     #0#2  = eaCuenta;
     #0#3  = enNivel;

     nCampo  = 2;
     nCampo1 = 3;
     |HAZ CuentaTl;

     #0#4  = #100#57;
     #0#5  = #100#58;
     #0#6  = "D";
     #0#7  = nImport;
     |HAZ adapta;
     #0#13 = 31;
     |GRABA 020#0n;

     |HAZ PonSaldo;
|FPRC;

|PROCESO Beneficio;

     |SI nImport = 0; |ACABA; |FINSI;
     nLineal = nLineal + 3;

     |DDEFECTO #0;
     #0#0  = #11#22;
     #0#1  = nLineal;
     #0#2  = #100#28;
     #0#3  = #100#29;
     |HAZ CuentaDirecto;
     #0#2 = eaCuenta;
     #0#3 = enNivel;

     #0#4  = #100#55;
     #0#5  = #100#56;
     #0#6  = "H";
     #0#7  = nImport;
     |HAZ adapta;
     #0#13 = 28;
     |GRABA 020#0n;

     |HAZ PonSaldo;
|FPRC;

|PROCESO Asto2Perdida;
     nLineal = nLineal + 3;

     |DDEFECTO #0;
     #0#0  = #11#22;
     #0#1  = nLineal;
     #0#2  = #100#7;
     #0#3  = #100#8;
     |HAZ CuentaDirecto;
     #0#2  = eaCuenta;
     #0#3  = enNivel;

     nCampo  = 2;
     nCampo1 = 3;
     |HAZ CuentaTl;

     #0#4  = #100#41;
     #0#5  = #100#42;
     #0#6  = "D";
     #0#7  = nImport;
     |HAZ adapta;
     #0#13 = 7;
     |GRABA 020#0n;

     |HAZ PonSaldo;
|FPRC;

|PROCESO Asiento1;   || Perdidas a la 6301
  nImport = - #11#12;
  |HAZ Asto2Perdida;
  nImport =  #11#12;
  |SI nDesglose = 0; |HAZ Asiento00; |FINSI;
|FPRC;

|PROCESO Asiento0;   ||Asiento cuenta 6300

     |SI nImport = 0; |ACABA; |FINSI; || no hay impuesto

     nLineal = nLineal + 3;

     |DDEFECTO #0;
     #0#0  = #11#22;
     #0#1  = nLineal;
     #0#2  = #100#1;
     #0#3  = #100#2;
     |HAZ CuentaDirecto;

     #0#4  = #100#37;
     #0#5  = #100#38;
     |SI nImport ] 0;
         #0#6  = "D";
         #0#7  = nImport;
     |SINO;
         #0#6  = "H";
         #0#7  = -nImport;
     |FINSI;
     |HAZ adapta;
     #0#13 = 1;
     |GRABA 020#0n;

     |HAZ PonSaldo;
|FPRC;

|PROCESO Asiento00;   ||Asiento cuenta 6301

     |SI nImport = 0; |ACABA; |FINSI; || no hay impuesto

     nLineal = nLineal + 3;

     |DDEFECTO #0;
     #0#0  = #11#22;
     #0#1  = nLineal;
     #0#2  = #100#4;
     #0#3  = #100#5;
     |HAZ CuentaDirecto;

     #0#4  = #100#39;
     #0#5  = #100#40;
     |SI nImport < 0;
         #0#6  = "H";
         #0#7  = - nImport;
     |SINO;
         #0#6  = "D";
         #0#7  = nImport;
     |FINSI;
     |HAZ adapta;
     #0#13 = 4;
     |GRABA 020#0n;

     |HAZ PonSaldo;
|FPRC;

||------------------------------------------------------------
|PROCESO PonAsiento3;
     |SI #903#4 = 0;  |ACABA;  |FINSI;
     |SI nOp = 1;
         nImport = (nImport + #903#4) ! EURODB;
         |ACABA;
     |FINSI;

     nLineal = nLineal + 3;
     |DDEFECTO #0;
     #0#0  = #11#22;
     #0#1  = nLineal;
     #0#2  = #903#0;
     #0#3  = #903#5;
     #0#4  = nConcep;
     #0#5  = aConcep;
     |SI #903#9 = 8;
         #0#6  = "H";
         #0#7  = #903#4;
     |SINO;
         #0#6  = "D";
         #0#7  = -#903#4;
     |FINSI;
     |SI #0#7 < 0;
         |SI #0#6 = "D";
              #0#6 = "H";
         |SINO;
              #0#6 = "D";
         |FINSI;
         #0#7 = - #0#7;
     |FINSI;
     |HAZ adapta;
     |GRABA 020#0n;

     |HAZ PonSaldo;
|FPRC;

|DEFBUCLE caw000081;
     #903#1;
     ;
     #11#22, nCampos, 0, "            ";
     #11#22, nCampos, 1, "zzzzzzzzzzzz";
     ;
     NULL, PonAsiento3;
|FIN;

|PROCESO Asiento3;  || asiento diferencias temporales
     nImport = 0;
     |SI  nCampos = 8;
          nConcep = #100#45;
          aConcep = #100#46;
          nOp = 1; |BUCLE caw000081;
          |SI nDesglose = 0; |HAZ Asiento00; |FINSI;
          nOp = 0; |BUCLE caw000081;
     |FINSI;
     |SI  nCampos = 9;
         nConcep = #100#47;
         aConcep = #100#48;
         nOp = 1; |BUCLE caw000081;
         |SI nDesglose = 0; |HAZ Asiento00; |FINSI;
         nOp = 0; |BUCLE caw000081;
     |FINSI;
|FPRC;

|PROCESO PonAsiento5_6;
     |SI #900#3 = 0;  |ACABA;  |FINSI;  || bonificaciones y retenciones
     |SI nOp = 1;
         nImport = (#900#3 + nImport) ! EURODB;
         |ACABA;
     |FINSI;

     |SI nCampos = 13; nImport = (nImport + #900#3) ! EURODB; |FINSI;

     nLineal = nLineal + 3;
     |DDEFECTO #0;
     #0#0  = #11#22;
     #0#1  = nLineal;
     #0#2  = #900#1;
     #0#3  = #900#4;
     #0#4  = nConcep;
     #0#5  = aConcep;

     #0#6  = "H";
     #0#7  = #900#3;
     |HAZ adapta;
     |GRABA 020#0n;

     |HAZ PonSaldo;
|FPRC;

|DEFBUCLE caw000051;
     #900#1;
     ;
     #11#22, nCampos, "            ";
     #11#22, nCampos, "zzzzzzzzzzzz";
     ;
     NULL, PonAsiento5_6;
|FIN;

|PROCESO Asiento6;    ||asiento bonificaciones y deducciones
     nImport = 0;
     nCampos = 13;
     nConcep = #100#51;
     aConcep = #100#52;
     nOp = 0; |BUCLE caw000051;
     |SI nDesglose = 0; |HAZ Asiento00; |FINSI;
|FPRC;

|PROCESO PonAsiento4;  || Ejercicios anteriores

     |SI #901#4 = 0; |ACABA; |FINSI;

     nImport = (nImport + #901#4) ! EURODB;

     nLineal = nLineal + 3;
     |DDEFECTO #0;
     #0#0  = #11#22;
     #0#1  = nLineal;
     #0#2  = #901#0;
     #0#3  = #901#5;
     #0#4  = nConcep;
     #0#5  = aConcep;
     #0#6  = "H";
     #0#7  = #901#4;
     |HAZ adapta;
     |GRABA 020#0n;

     |HAZ PonSaldo;
|FPRC;

|DEFBUCLE caw000061;
     #901#1;
     ;
     #11#22, nCampos, "            ";
     #11#22, nCampos, "zzzzzzzzzzzz";
     ;
     NULL, PonAsiento4;
|FIN;

|PROCESO Asiento4;  || asiento diferencias temporales
     nImport = 0;
     nCampos = 10;
     nConcep = #100#49;
     aConcep = #100#50;
     |BUCLE caw000061;
     |SI nDesglose = 0; |HAZ Asiento00; |FINSI;
|FPRC;

|PROCESO PonAsiento35_44;  || Provision

     |SI #901#6 ! #11#26;
         nImporteL = #901#4;
         aSigno    = "H";
     |SINO;
         nImporteL = #901#2 - #901#4;
         aSigno    = "D";
     |FINSI;
     |SI nImporteL = 0; |ACABA; |FINSI;

     |SI aSigno = "D";
         nImport = (nImport - nImporteL) ! EURODB;
     |SINO;
         nImport = (nImport + nImporteL) ! EURODB;
     |FINSI;

     nLineal = nLineal + 3;
     |DDEFECTO #0;
     #0#0  = #11#22;
     #0#1  = nLineal;
     #0#2  = #901#0;
     #0#3  = #901#5;
     |HAZ CuentaDirectoNueva;
     #0#4  = nConcep;
     #0#5  = aConcep;
     #0#6  = aSigno;
     #0#7  = nImporteL;
     |HAZ adapta;
     |GRABA 020#0n;

     |HAZ PonSaldo;
|FPRC;

|DEFBUCLE caw000061A;
     #901#2;
     ;
     #11#22, nCampos, #11#26, "            ";
     #11#22, nCampos, 2000,   "zzzzzzzzzzzz";
     ;
     NULL, PonAsiento35_44;
|FIN;

|PROCESO PonAsiento48;  || Provision

     |SI #901#6 ! #11#26;
         aSigno    = "D";
         nImporteL = #901#4;
     |SINO;
         aSigno    = "H";
         nImporteL = #901#2;
     |FINSI;
     |SI nImporteL = 0; |ACABA; |FINSI;

     |SI aSigno = "D";
         nImport = (nImport - nImporteL) ! EURODB;
     |SINO;
         nImport = (nImport + nImporteL) ! EURODB;
     |FINSI;

     nLineal = nLineal + 3;
     |DDEFECTO #0;
     #0#0  = #11#22;
     #0#1  = nLineal;
     #0#2  = #901#0;
     #0#3  = #901#5;
     |HAZ CuentaDirectoNueva;
     #0#4  = nConcep;
     #0#5  = aConcep;
     #0#6  = aSigno;
     #0#7  = nImporteL;
     |HAZ adapta;
     |GRABA 020#0n;

     |HAZ PonSaldo;
|FPRC;

|DEFBUCLE caw000061B;
     #901#3;
     ;
     #11#22, nCampos, nDAny, "            ";
     #11#22, nCampos, nHAny, "zzzzzzzzzzzz";
     ;
     NULL, PonAsiento48;
|FIN;

|PROCESO Asiento5;   || Retenciones y pagos a Cuenta
     nImport = 0;
     |SI #11#14 ! 0;
         nCampos = 14;
         nConcep = #100#53;
         aConcep = #100#54;
         nOp = 1; |BUCLE caw000051;
         |SI nDesglose = 0; |HAZ Asiento0; |FINSI;
         nOp = 0; |BUCLE caw000051;
     |FINSI;
|FPRC;

|PROCESO Asiento35;  || Provisiones deterioro creditos comerciales
     nImport = 0;
     nCampos = 35;
     nConcep = #100#89;
     aConcep = #100#90;
     |BUCLE caw000061A;
     |SI nDesglose = 0; |HAZ Asiento00; |FINSI;
|FPRC;

|PROCESO Asiento44;  || Provisiones deterioro creditos comerciales
     nImport = 0;
     nCampos = 44;
     nConcep = #100#91;
     aConcep = #100#92;
     |BUCLE caw000061A;
     |SI nDesglose = 0; |HAZ Asiento00; |FINSI;
|FPRC;

|PROCESO Asiento48;  || Nivelacion bases negativas

     |SI #11#49 > 0;
         nDAny = #11#26;
         nHAny = #11#26;
     |SINO;
         nDAny = 2000;
         nHAny = #11#26 - 1;
     |FINSI;

     nImport = 0;
     nCampos = 48;
     nConcep = #100#93;
     aConcep = #100#94;
     |BUCLE caw000061B;
     |SI nDesglose = 0; |HAZ Asiento00; |FINSI;
|FPRC;

|PROCESO adapta;
     #0#12 = nDocumento;
     |SI #0#6 = "D";
         #0#10 = #0#2;
         #0#11 = #0#3;
         #0#8  = "";
         #0#9  = 0;
         aCtDeAnt = #0#10;
         nDiDeAnt = #0#11;
     |SINO;
         #0#8  = #0#2;
         #0#9  = #0#3;
         #0#10 = "";
         #0#11 = 0;
         aCtHaAnt = #0#8;
         nDiHaAnt = #0#9;
     |FINSI;
     #0#13 = 0;

     nCoAnt  = #0#4;
     aDeAnt  = #0#5;
     aDhAnt  = #0#6;
     nImpAnt = #0#7;

     |SI nSegDoc = 0; |Y nDocumento = #11#31; |Y #11#31 ! 0;
         nSegDoc = #0#1;
     |FINSI;
|FPRC;

|PROCESO Borrarx7_l;
 |SI #0#13 ! 0;
     nNume1 = #0#13; nNume2 = nNume1 + 1;
     aAlfa2 = #100nNume1;         || Reservo la que tengo ahora
     |PDEFECTO #100, nNume1, nNume2;
     aAlfa1 = #0#2 % 104;         || Cuenta del Asiento Anterior
     aAlfa3 = #100nNume1 % 104;   || Cuenta defecto
     |SI aAlfa1 = aAlfa3;
         #100nNume1 = #0#2;
     |SINO;
         #100nNume1 = aAlfa2;
     |FINSI;
 |FINSI;
 |BORRA 020#0a;
 |LIBERA #0;
|FPRC;

|DEFBUCLE Borrarx7_l;
 #0#1;
 ;
 #11#22, 001;
 #11#22, 999;
 be;
 NULL, Borrarx7_l;
|FIN;

|PROCESO ClaveB7_7;
 #0#0 = #11#22;
 #0#1 = 001;
|FPRC;

|PROCESO ClaveA7_7;
 #0#0 = #11#22;
 #0#1 = 999;
|FPRC;

|PROCESO Tipo5;

  |BUCLE Borrarx7_l;

  |ENTLINEAL #1#0, -2, 7, 22, 0, ClaveB7_7, ClaveA7_7;

  #11#19 = 0;
  #11#20 = 0;
  #11#21 = 0;

  |ABRE #0;

     nSwError   = 0;
     nBeneficio = 0;

     nDesglose = 0; |SI #100#61 = "N"; nDesglose = 1; |FINSI;

     nDocumento = #11#2;
     nLineal = -2;

|| ... Primer Asiento
    nBeneficio = 0;
    |SI nDesglose = 1;
        nImport = #11#16 + #11#14;  ||Cuota mas retenciones
        |HAZ Asiento0;
    |FINSI;
    |HAZ Asiento5;  ||las retenciones.

    |SI nDesglose = 0;
        nImport = #11#16;
        |HAZ Asiento0;
    |FINSI;

    |SI #11#16 < 0;                  ||Perdida o ganancia
        nImport = -#11#16;
        |HAZ Perdida;
    |SINO;
        nImport = #11#16;
        |HAZ Beneficio;
    |FINSI;

|| ..........................................
|| ... Segundo Asiento
     |SI #100#62 = "S";  nDocumento = #11#31; |FINSI;

     |SI  #11#8  ! 0;
          nCampos = 8; |HAZ Asiento3;
     |FINSI;   || Activos temporales

     |HAZ Asiento35;
     |HAZ Asiento44;

     |SI #11#10 ! 0;  |Y #11#19 ] 0;  ||  no compensaria si el impuesto es negativo
         |HAZ Asiento4;
     |FINSI;

     |HAZ Asiento48;

     |SI #11#13 ! 0;  |HAZ Asiento6; |FINSI;
     |SI #11#9  ! 0;  nCampos = 9; |HAZ Asiento3; |FINSI;
     |SI #11#12 < 0;  || Cuota integra en Negativo;
         |HAZ Asiento1;
     |FINSI;
     |SI nDesglose = 1;
         nImport = #11#20 - #11#19;
         |HAZ Asiento00;
     |FINSI;
 |CIERRA #0;
|FPRC;
