|FICHEROS;
   caybalca #0;
   cacuenta #1;
   caconimp #8;
   caycosy1 #99;

   canempre #30;
   caselem1 #998;
   cawdatos #900;
|FIN;

|VARIABLES;
   aFichero  = "";
   &aparam    = "";
   &balance  = 1;
   &pathbal  = "";
   &entrada  = 1;
   {-1}menu  = "";
   menu1 = "2400";
   menu2 = "2";
   menu3 = "Elija opcion: ";
   menu4 = "PI";
   menu5 = " Pantalla ";
   menu6 = " Impresora ";

   aAlfa1   = "";
   aAlfa2   = "";
   aAlfa3   = "";
   nNume1   = 0;
   nNume2   = 0;
   nNume3   = 0;
   nPara1   = 0;
   informe  = "caybalca";

   swSaldo  = 0;
   sw       = 0;
   &nDebe   = 0;
   &nHaber  = 0;
   &nSaldo  = 0;
   &nTdebe  = 0;
   &nThaber = 0;
   &nTsaldo = 0;
   lin      = 0;
   Linea    = 0;
|FIN;

|INCLUYE dscont_i;

|PROCESO inicio_y; |TIPO 8;
  |HAZ inicial;
  |SI swerror = 2; |PTEC 807; |ACABA; |FINSI;
  |SI swerror = 3; |PTEC 831; |PTEC 807; |ACABA; |FINSI;
|FPRC;

|PROCESO antesque;
   aparam = PARAMETRO;
   |QBF aparam; |SI aparam = " N"; aparam = ""; |FINSI;
   |SI aparam = "fech";
      |NOME_DAT #1 "cabalano";
   |FINSI;
|FPRC;

|| ***********************************************************************
||                       CALCULOS DE PANTALLA
|| ***********************************************************************

|PROCESO mesconta; |TIPO 0;

   nNume1 = Campo + 1; NumMes = #0Campo;

   |SI #0Campo ! 0;|Y #0Campo ! 13;
       NumMes = #canempre#11 + (#0Campo - 1);
       |SI NumMes > 12; NumMes = NumMes - 12; |FINSI;
   |FINSI;
   |HAZ NombreMes;
   #0nNume1 = NombreMes; |PINTA #0nNume1;

   |SI Campo = 4;
      |SI #0#2 > #0#4;
         |MENAV "    ATENCION!!! Error de Entrada ...";
         |ERROR;
      |FINSI;
   |FINSI;
|FPRC;

|CALCULO cuenta; |TIPO 6;
     eaCuenta = #caybalca#Campo#;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #caybalca#Campo# = eaCuenta;
     |PINTA #caybalca#Campo#;
|FCAL;

|CALCULO nosaldo0; |TIPO 7;
   |SI #0#9 > 0; |O #0#10 < 0;
       |C_M #0#6, 1,"N"; #0#6  = "N"; |PINTA #0#6;
       |C_M #0#11,1,"N"; #0#11 = "N"; |PINTA #0#11;
   |SINO;
       |C_M #0#6, 1,"S";
       |C_M #0#11,1,"S";
   |FINSI;
|FCAL;

|CALCULO nosaldo; |TIPO 7;
   |C_M #0#6,0,aAlfa1; |SI aAlfa1 = "N"; |ACABA; |FINSI;

   |SI #0#6 = "N";  |C_M #0#11, 1, "S"; |FINSI;
   |SI #0#6 = "S";  |C_M #0#11, 1, "N";  #0#11 = "S";  |FINSI;
   |SI #0#9 = 0; |Y #0#10 = 0;
       |C_M #0#11,1,"N"; #0#11 = "S"; |PINTA #0#11;
   |FINSI;
   |PINTA #0#11;
|FCAL;

|| **********************************************************************
||                       CALCULOS DE IMPRESION
|| **********************************************************************
|PROCESO operapanta;   || Operaciones por pantalla;
   #99#0 = #cacuenta#0;     || Cuenta
   #99#1 = #cacuenta#2;     || Descripcion

   #99#2 = nDebe;
   #99#3 = nHaber;
   #99#4 = nSaldo;

   FSalida = 0;
   #99#5 = #99#5 + #99#2;
   #99#6 = #99#6 + #99#3;
   #99#7 = #99#7 + #99#4;
   #99#11 = Linea;

   Linea = Linea + 1;
   |GRABA 020#99n;
   sw = 1;

   |SI 0 [ FSalida;    || Pinta linea por pantalla
         |PINTA #99#5;
         |PINTA #99#6;
         |PINTA #99#7;
         Pos = lin;
         Pos = -1;
         lin = lin + 100;     || Posicionar una linea mas abajo
   |FINSI;
|FPRC;

|PROCESO operaimpre;     || impresora
   sw = 1;
   |PRINT;

   nTdebe  = nTdebe  + nDebe;
   nThaber = nThaber + nHaber;
   nTsaldo = nTsaldo + nSaldo;
|FPRC;

|RUTINA inf;
   #99#11 = 1;
|FRUT;

|RUTINA sup;
   #99#11 = 99999999999;
|FRUT;

|RUTINA evento;
|FRUT;


|PROCESO lasCuentas0;
   swSaldo = 0;
   nDebe  = 0;
   nHaber = 0;
   nNume1  = (#0#2 * 3) + 9;
   |PARA nPara1 = #0#2; |SI nPara1 [ #0#4; |HACIENDO nPara1 = nPara1 + 1;
         |SI #1nNume1 ! 0; swSaldo = 1; |FINSI;
         nDebe  = nDebe +  #1nNume1; nNume1 = nNume1 + 1;

         |SI #1nNume1 ! 0; swSaldo = 1; |FINSI;
         nHaber = nHaber + #1nNume1; nNume1 = nNume1 + 2;
   |SIGUE;

   |SI #0#6  = "N"; |Y swSaldo = 0; |ACABA; |FINSI;

   nSaldo = nDebe - nHaber;

   aAlfa1 = nSaldo; |QBF aAlfa1;
   |SI aAlfa1 = "-0"; |O aAlfa1 = "0";
       nSaldo = 0;
   |FINSI;

   |SI nSaldo < #0#9; |O nSaldo > #0#10;
       |ACABA;
   |FINSI;

   |SI #0#11 = "N"; |Y nSaldo  = 0; |ACABA; |FINSI;

   |SI menu2 = 1; |HAZ operapanta; |FINSI;
   |SI menu2 = 2; |HAZ operaimpre; |FINSI;
|FPRC;

|DEFBUCLE lasCuentas;
   #1#1;
   3;
   #0#0,"S";
   #0#1,"S";
   e;
   NULL, lasCuentas0;
|FIN;

|PROCESO HazTodo;
   sw         = 0;
   nTdebe     = 0;
   nThaber    = 0;
   nTsaldo    = 0;

   |SI menu2 = 2;
       eaIa = #canempre#21;
       enM1 = #0#2;
       enM2 = #0#4;
       |HAZ LeeDatosEmpresa;
       |SI aparam = "fech";
           |SI enM1 ! enM1f;
               NumMes = enM1f; |HAZ NombreMes; #0#3 = NombreMes;
           |FINSI;
           |SI enM2 ! enM2f;
               NumMes = enM2f; |HAZ NombreMes; #0#5 = NombreMes;
           |FINSI;
       |FINSI;
   |SINO;
       |HAZ FecApeCie;
   |FINSI;

   |SI enMAper ! dig1;
       |SI #0#4 < enMAper; |Y #0#4 ! 0;
           |ACABA;
       |FINSI;
   |FINSI;

   |ABRE #8;
   |DDEFECTO #8;
   #8#0 = 1;
   |LEE 000#8=;
   |SI #caconimp#2 = "S";  |Y #caconimp#3 = "N";  |Y aparam = "";
       pathbal = dirfich0;
       balance = 1;
       |CIERRA #8;
       |SUB_EJECUTA "careccue";
   |FINSI;
   |CIERRA #8;

   |SI menu2  = 1;
       lin = 100;
       |DELINDEX #99;

       |PINTA 0401,#canempre#2;
       |PINTA 0407,#canempre#1;
       |PINPA #0#99;
       |ABRE #99;
   |FINSI;

   |BUCLE lasCuentas;

   |SI menu2 = 1;
      |SI sw ! 0;
          |C_V #99#5,1,"N"; |C_V #99#6,1,"N";
          |C_V #99#7,1,"N"; |C_V #99#8,1,"N";
          |C_V #99#9,1,"N"; |C_V #99#10,1,"N";
          |LINEAL_SIMPLE #2#99,28,0502,2079,inf,sup,evento;
          |C_V #99#5,1,"S"; |C_V #99#6,1,"S";
          |C_V #99#7,1,"S"; |C_V #99#8,1,"S";
          |C_V #99#9,1,"S"; |C_V #99#10,1,"S";
      |FINSI;
      |CIERRA #99;
  |SINO;
      |SI sw = 1; |PIEINF; |FINSI;
  |FINSI;
|FPRC;

||*************************************************************************
|CALCULO ElImprimir; |TIPO 3;
   |CIERRAT #1;
   |HAZ antesque;

   |MENU menu;
   |SI FSalida < 1; |ACABA; |FINSI;
   menu2 = FSalida;

   |SI menu2 = 1;
       aFichero = ("bal" + Usuario) % 108;
       |NOME_DAT #99 aFichero;
   |SINO;
       |IMPRE 0;
       |SI FSalida ! 0; |ACABA; |FINSI;

       |INFOR informe;
       |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;
   |FINSI;

|| ... titulos del Informe
   aAlfa1 = #0#8; |QBF aAlfa1;
   aAlfa2 = aAlfa1 % 0;
   nNume1 = aAlfa2;
   nNume1 = ((50 - nNume1) / 2) ! 0;
   aAlfa2 = " " * nNume1;
   #0#8 = aAlfa2 + aAlfa1;

                     aAlfa1 = "SALDO CONTABLE MAYOR QUE ";
   |SI #0#9 = #0#10; aAlfa1 = "SALDO CONTABLE IGUAL A "; |FINSI;
   aAlfa2 = #0#9; |QBF aAlfa2;
   aAlfa1 = aAlfa1 + aAlfa2;
   |SI #0#9 ! #0#10;
         aAlfa1 = aAlfa1 + " Y MENOR QUE ";
         aAlfa2 = #0#10; |QBF aAlfa2;
         aAlfa1 = aAlfa1 + aAlfa2;
   |FINSI;
   aAlfa2 = aAlfa1 % 0;
   nNume1 = aAlfa2;
   nNume1 = ((62 - nNume1) / 2) ! 0;
   aAlfa2 = " " * nNume1;
   #0#12 = aAlfa2 + aAlfa1;


   |SI #48#27 = "S";
       |HAZ MultiEmpresa;
   |SINO;
       |DFICE dirfich0; |QBF dirfich0;
       |HAZ HazTodo;
   |FINSI;

   |SI menu2 = 1
        |DELINDEX #99;
   |SINO;
        |FININF;
        |FINIMP;
   |FINSI;
|FCAL;
||********************************************************************

|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #8 dirfich0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 001;
 999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;
