|FICHEROS;
    pacontap #0;
    camaasic #2;    || apuntes cabs.
    camaasil #3;    || apuntes lins.
    cacuenta #4;    || plan contable
    cacuebal #15;   || cuentas de balance
    caclipro #16;   || clientes/proveedores
    ca8cumay #410;
    ca8m0002 #422;
|FIN;

|VARIABLES;
    &eaOrigen  = "";
    &eaDestino = "";
    &enErrPas  = 0;
    nCopio     = 0;
    aFichero   = "";

    &entrada = 1;
    &enDiario   = 0;
    &enContador = 0;
    fiche   = "";
    handle  = 0;
    varalfa = "";
    varalfa2 = "";
    varalfa3 = "";
    varalfa4 = "";
    varalfa5 = "";
    varalfa6 = "";
    varalfa7 = "";
    varalfa8 = "";

    trabajo = "";
    trabaj1 = "";
    traba = "";
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    sw = 0;
    &param = 0;

    ult_apunt = 0;
    docum = 0;
    impor = 0;
    conte = "";
    digcu = 0;
    conce = 0;
    fecha = @;
    descr = "";
    signe = "";
    FEstado = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    nada = "";
    debe = 0;
    haber = 0;
    nRegi = 0;

    aTipo  = "";
    top    = 0;
    nNume1 = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE defec1_i;

____________________________________/ CALCULOS DE PANTALLA
|CALCULO Solo8; |TIPO 0;
 |C_M #0#10,1,"N";
 |SI #0#9 = 8;
     |C_M #0#10,1,"S";
 |FINSI;
|FCAL;


|CALCULO AntesCta; |TIPO 7;
  |PTEC 816;
|FCAL;

|CALCULO Cuenta; |TIPO 6;
   eaCuenta = #0Campo;
   salidas  = FSalida;
   |HAZ FiltraPuntos;
   #0Campo = eaCuenta;
   |PINTA #0Campo;
|FCAL;

|CALCULO TrasCuenta; |TIPO 0;
   emCta    = 1;
   eaCuenta = #0Campo;
   |HAZ SacaNivel;
   |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea

   |HAZ LeeCuenta;
   |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea
|FCAL;
|| .................................................................


____________________________________/ CALCULOS DEL PASE
|PROCESO la_129;
    |SI debe ! haber;
        #4#0 = #0#8;
        eaCuenta = #4#0; |HAZ SacaNivel;
        conte = #0#8;
        digcu = enNivel;
        impor = debe - haber;
        |SI impor [ 0;
            signe = "D";
            impor = -impor;
        |SINO;
            signe = "H";
        |FINSI;
        |HAZ apuntes;
   |FINSI;
|FPRC;

|| ***************************************************************************
|PROCESO cli_prov_07;
  #16#1 = #4#2;              || nombre
  #16#2 = varalfa % 5835;    || direccion
  #16#6 = varalfa % 9325;    || poblacion
  #16#9 = varalfa % 4315;    || cif
|FPRC;

|PROCESO cli_prov_02;
  #16#1 = #4#2;               || nombre
  #16#2 = varalfa %  6835;    || direccion
  #16#6 = varalfa % 10325;    || poblacion
  #16#9 = varalfa %  5315;    || cif
|FPRC;

|PROCESO cli_prov_01;
  #16#1 = #4#2;              || nombre
  #16#2 = varalfa % 5340;    || direccion
  #16#6 = varalfa % 9325;    || poblacion
  #16#9 = varalfa % 4310;    || cif
|FPRC;


|PROCESO Cli_Prov;

   alfa1 = #4#0 % 103;
   |SI alfa1 ! "430";  |Y alfa1 ! "440";  |Y alfa1 ! "400";  |Y alfa1 ! "410";
       |ACABA;
   |FINSI;

   |DDEFECTO #16;
   aTipo = "P";
   |SI alfa1 = "430";|O alfa1 = "440";
       aTipo =  "C";
   |FINSI;
   #16#23 = aTipo;
   #16#10 = #4#0;             || cuenta contable
   #16#11 = #4#1;             || digito
   |LEE 101#16=;
   FEstado = FSalida;

   #16#23 = aTipo;
   #16#10 = #4#0;
   #16#11 = #4#1;

   |SI #0#9 = 1;              |HAZ cli_prov_01;   |FINSI;
   |SI #0#9 ] 2;|Y #0#9 [ 6;  |HAZ cli_prov_02;   |FINSI;
   |SI #0#9 = 7;              |HAZ cli_prov_07;   |FINSI;

   |SI FEstado ! 0;  |GRABA 020#16n;     |FINSI;
   |SI FEstado = 0;  |GRABA 020#16a;     |FINSI;
   |LIBERA #16;
|FPRC;


|PROCESO cuentas;

    |DDEFECTO #4;
    #4#0     = varalfa % 112;     || cuenta
    eaCuenta = #4#0;
    |HAZ SacaNivel;
    |SI enNivel ! 0;
        #4#1 = enNivel;           || digito
        #4#2 = varalfa % 1330;    || Titulo
        #4#3 = "S";               || pase directo

        |ATRI I;
        |PINTA 2221, #4#0;
        |PINTA 2234, #4#2;
        |ATRI 0;

        mascara = #4#0;
        |QBF mascara;
        mascara = mascara +"zzzzzzzzzzzz";
        mascara = mascara % 112;

        #4#54 = mascara;
        #4#55 = #4#1;

        NumMes = dig1; |HAZ NombreMes;
        #4#56 = NombreMes;
        NumMes = dig1 + 11; |SI NumMes > 12; NumMes = NumMes - 12; |FINSI;
        |HAZ NombreMes;
        #4#57 = NombreMes;

        |HAZ defect1;

        |GRABA 000#4n;
        |SI FSalida = 0;
            |HAZ Cli_Prov;

            |SI #0#9 = 8;
               alfa1 = varalfa7 % 0416;   nume1 = alfa1;
               alfa2 = varalfa7 % 2016;   nume2 = alfa2;
            |SINO;
               alfa1 = varalfa % 11811;   nume1 = alfa1;
               alfa2 = varalfa % 12911;   nume2 = alfa2;
            |FINSI;
            |SI nume1 ! nume2;
                conte = #4#0;
                digcu = #4#1;
                impor = nume1 - nume2;
                |SI impor [ 0;
                    signe = "H";
                    impor = -impor;
                |SINO;
                    signe = "D";
                |FINSI;
                alfa1 = #4#0 % 101;
                |SI alfa1 ! "6";|Y alfa1 ! "7";
                    |HAZ apuntes;
                |FINSI;
            |FINSI;
        |FINSI;
   |SINO;
        |MENAV "    No corresponden los Niveles; proceso interrumpido ...";
        sw = 1;
   |FINSI;
|FPRC;

|PROCESO nombre;  || nombre del fichero
    Pos   = -1;
    alfa1 = #0#1;
    |QBF alfa1;

    alfa1 = alfa1 + "subcta"; |QBF alfa1;
    traba = trabaj1;          |QBF traba;
    alfa1 = alfa1 + traba + ".dbf";
    aFichero = "subcta" + traba + ".dbf";
    fiche = alfa1;

    nCopio = 0;
    |XABRE "UB", fiche, handle;
    |SI FSalida < 0;
        |XABRE "CUB", fiche, handle;
        |SI FSalida = 0; nCopio = 1; |FINSI;
    |FINSI;
    |XCIERRA handle;

|SI nCopio = 1;
    |DFICE aBase;
    eaOrigen  = fiche;
    eaDestino = aBase + aFichero;
    |HAZ eReciboFich;
    |SI enErrPas = 0; fiche = eaDestino; |FINSI;
|FINSI;
|FPRC;


|PROCESO principio;
   Pos = -1;
   |PARA para1 = top;  |SI para1 > 0;  |HACIENDO para1 = para1 - 250;
         |SI para1 ] 250;
             |XLEE handle, 250, varalfa;
         |SINO;
             |XLEE handle, para1, varalfa;
         |FINSI;
   |SIGUE;
|FPRC;


|PROCESO subcta_08;

    |XABRE "UB", fiche, handle;
    |SI FSalida < 0;
        |XABRE "CUB", fiche, handle;
    |FINSI;
    |SI FSalida ] 0;
        top = 4099; |SI #0#10 = 1; top = top + 32; |FINSI;
        |HAZ principio;

        sw = 0;
        |PARA; |SI sw = 0; |HACIENDO;
               sw = 1;
               |XLEE handle, 110, varalfa;
               |SI FSalida = 110;                                            ||
                   |XLEE handle, 250, varalfa2;
                   |SI FSalida = 250;                                       ||
                       |XLEE handle, 250, varalfa2;
                       |SI FSalida = 250;                                  ||
                           |XLEE handle, 250, varalfa2;
                           |SI FSalida = 250;                             ||
                               |XLEE handle, 250, varalfa3;
                               |SI FSalida = 250;                        ||
                                   |XLEE handle, 250, varalfa4;
                                   |SI FSalida = 250;                   ||
                                       |XLEE handle, 250, varalfa5;
                                       |SI FSalida = 250;               ||
                                           |XLEE handle, 250, varalfa6;
                                           |SI FSalida = 250;
                                               |SI #0#10 = 1; |XLEE handle, 233, varalfa7; nNume1 = 233; |FINSI; ||8
                                               |SI #0#10 = 2; |XLEE handle, 232, varalfa7; nNume1 = 232; |FINSI; ||8
                                               |SI FSalida = nNume1;
                                                   sw = 0;
                                                   |HAZ cuentas;
                                               |FINSI;
                                           |FINSI;
                                       |FINSI;
                                   |FINSI;
                               |FINSI;
                           |FINSI;
                       |FINSI;
                   |FINSI;
               |FINSI;
        |SIGUE;
        |XCIERRA handle;
        |HAZ la_129;
    |FINSI;
    Pos = -1;
|FPRC;


|PROCESO subcta_04;

    |XABRE "UB", fiche, handle;
    |SI FSalida < 0;
        |XABRE "CUB", fiche, handle;
    |FINSI;
    |SI FSalida ] 0;
        top = 2307;
        |HAZ principio;
        sw = 0;
        |PARA; |SI sw = 0; |HACIENDO;
               sw = 1;
               |XLEE handle, 110, varalfa;
               |SI FSalida = 110;
                   |XLEE handle, 250, varalfa2;
                   |SI FSalida = 250;
                       |XLEE handle, 250, varalfa2;
                       |SI FSalida = 250;
                           |XLEE handle, 250, varalfa2;
                           |SI FSalida = 250;
                                sw = 0;
                                |HAZ cuentas;
                           |FINSI;
                       |FINSI;
                   |FINSI;
               |FINSI;
        |SIGUE;
        |XCIERRA handle;
        |HAZ la_129;
    |FINSI;
    Pos = -1;
|FPRC;

|PROCESO subcta_03;

    |XABRE "UB", fiche, handle;
    |SI FSalida < 0;
        |XABRE "CUB", fiche, handle;
    |FINSI;
    |SI FSalida ] 0;
        top = 3971;
        |HAZ principio;
        sw = 0;
        |PARA; |SI sw = 0; |HACIENDO;
               sw = 1;
               |XLEE handle, 212, varalfa;
               |SI FSalida = 212;
                   |XLEE handle, 250, varalfa2;
                   |SI FSalida = 250;
                       |XLEE handle, 250, varalfa2;
                       |SI FSalida = 250;
                           |XLEE handle, 250, varalfa2;
                           |SI FSalida = 250;
                               |XLEE handle, 250, varalfa2;
                               |SI FSalida = 250;
                                   |XLEE handle, 250, varalfa2;
                                   |SI FSalida = 250;
                                       |XLEE handle, 250, varalfa2;
                                       |SI FSalida = 250;
                                           |XLEE handle, 200, varalfa2;
                                           |SI FSalida = 200;
                                               sw = 0;
                                               |HAZ cuentas;
                                           |FINSI;
                                       |FINSI;
                                   |FINSI;
                               |FINSI;
                            |FINSI;
                       |FINSI;
                   |FINSI;
               |FINSI;
         |SIGUE;
         |XCIERRA handle;
         |HAZ la_129;
    |FINSI;
    Pos = -1;
|FPRC;


|PROCESO subcta_02;
    |XABRE "UB", fiche, handle;
    |SI FSalida < 0;
        |XABRE "CUB", fiche, handle;
    |FINSI;
    |SI FSalida ] 0;
        top = 2115;
        |HAZ principio;
        sw = 0;
        |PARA; |SI sw = 0; |HACIENDO;
               sw = 1;
               |XLEE handle, 214, varalfa;
               |SI FSalida = 214;
                   |XLEE handle, 200, varalfa2;
                   |SI FSalida = 200;
                       |XLEE handle, 200, varalfa2;
                       |SI FSalida = 200;
                           |XLEE handle, 200, varalfa2;
                           |SI FSalida = 200;
                               sw = 0;
                               |HAZ cuentas;
                           |FINSI;
                       |FINSI;
                   |FINSI;
               |FINSI;
        |SIGUE;
        |XCIERRA handle;
        |HAZ la_129;
    |FINSI;
    Pos = -1;
|FPRC;

|PROCESO subcta_01;
    |XABRE "UB", fiche, handle;
    |SI FSalida < 0;
        |XABRE "CUB", fiche, handle;
    |FINSI;
    |SI FSalida ] 0;
        top = 547;
        |HAZ principio;
        sw = 0;
        |PARA; |SI sw = 0; |HACIENDO;
               |XLEE handle, 211, varalfa;
               |SI FSalida = 211;
                   |HAZ cuentas;
               |SINO;
                    sw = 1;
               |FINSI;
        |SIGUE;
        |XCIERRA handle;
        |HAZ la_129;
    |FINSI;
    Pos = -1;
|FPRC;


|PROCESO pasa;  |TIPO 3;

    |SI #0#0 ! "S"; |ACABA; |FINSI;
    |HAZ eLeeParametro;

    entrada = 0;

    ult_apunt = 1;
    |HAZ contador;
    docum = nDOCUMENTO;
    nRegi = nREGISTRO;
    fecha = fec1;

    |ABRET #2;
    |ABRET #3;
    |ABRET #4;
    |ABRET #16;

    trabaj1 = #0#2;
    |HAZ nombre;

    |SI #0#9 = 1;|O #0#9 = 4;  |HAZ subcta_01;  |FINSI;
    |SI #0#9 = 2;|O #0#9 = 3;  |HAZ subcta_02;  |FINSI;
    |SI #0#9 = 5;|O #0#9 = 7;  |HAZ subcta_03;  |FINSI;
    |SI #0#9 = 6;              |HAZ subcta_04;  |FINSI;
    |SI #0#9 = 8;              |HAZ subcta_08;  |FINSI;

    |CIERRAT #2;
    |CIERRAT #3;
    |CIERRAT #4;
    |CIERRAT #16;

|FPRC;

____________________________________/ GRABACION DEL ASIENTO
|PROCESO apuntes;
    |SI impor ! 0;|Y conte ! doce;
        |DDEFECTO #3;
        #3#00 = 0;          || diario
        #3#01 = fecha;      || fecha
        #3#02 = docum;      || documento
        #3#03 = ult_apunt;  || apunte
        #3#04 = conte;      || cuenta
        #3#05 = enNivel;
        #3#06 = #0#6;       || concepto
        #3#07 = #0#7;       || descripcion
        #3#08 = signe;      || signo
        #3#09 = impor;      || importe
        |SI #3#08 = "D";
            #3#16 = #3#4;
            #3#17 = #3#5;
        |SINO;
            #3#10 = #3#4;
            #3#11 = #3#5;
        |FINSI;
        #3#12 = "";           || cobros/pagos
        #3#13 = 0;            || libro
        #3#14 = 0;            || orden
        #3#15 = "";           || serie
        #3#22 = 0;            || nro.recibo
        #3#19 = "";           || tipo acumulador
        #3#20 = 0;            || numero acumulador
        #3#21 = "";           || departamento
        #3#24 = nRegi;
        |GRABA 020#3n;
        |SI FSalida = 0;
            |SI #3#8 = "D";
                debe = debe + #3#9;
            |SINO;
                haber = haber + #3#9;
            |FINSI;

            |HAZ Actualiza;
            ult_apunt = ult_apunt + 3;
        |FINSI;
   |FINSI;
|FPRC;

|PROCESO cabecera;

    |DDEFECTO #2;
    #2#0 = #3#0;
    #2#1 = #3#1;
    #2#2 = #3#2;
    |LEE 101#2=;
    FEstado = FSalida;
    #2#0 = #3#0;         || diario
    #2#1 = #3#1;         || fecha
    #2#2 = #3#2;         || documento
    #2#3 = #2#2;         || asiento
    |SI #3#8 = "D";
        #2#4 = #2#4 + #3#9;
        |SI #2#09 = 0; #2#08 = #3#4; #2#09 = #3#5; |FINSI;      || contrp.HABER
    |FINSI;
    |SI #3#8 = "H";
        #2#5 = #2#5 + #3#9;
       |SI #2#11 = 0; #2#10 = #3#4; #2#11 = #3#5; |FINSI;      || contrp.DEBE
    |FINSI;
    #2#6 = #2#4 - #2#5;
    #2#12 = #3#24;
    |SI FEstado = 0;  |GRABA 020#2a;  |FINSI;
    |SI FEstado ! 0;  |GRABA 020#2n;  |FINSI;
    |LIBERA #2;
|FPRC;

|PROCESO Actualiza;

     |HAZ cabecera;

      MasMenos     = #3#9;
      DebeHaber    = #3#8;
      aCUENTA      = #3#4;
      nREGISTRO    = #3#24;
      nDIARIO      = #3#0;
      aFECHA       = #3#1;
      nDOCUMENTO   = #3#2;
      nNIVELCUENTA = #3#5;

      |HAZ ActDiario;          || diario
      |HAZ ActCuenta;          || cuenta
      |HAZ ActTesteo;          || cuenta
|FPRC;
