|FICHEROS;
   caxcopia #0;
   caxcontr #1;
|FIN;

|VARIABLES;
   direc  = "";
   fiche  = "";

   nTipo     = 0;
   nUnidad   = 0;
   unida     = "";
   aOrigen   = "";
   aDestino  = "";
   aPath     = "";
   aCopia    = "";
   aTecla    = "";
   aIPRemoto = "";
   ip        = "";
   aAlfa     = "";
   &eaUnidadBasico = "";
|FIN;

|INCLUYE dscont_i;

____________________________________/ CALCULOS DE PANTALLA

|PROCESO importa;
   aAlfa  = eaUnidadBasico + "/DS/DISKETTE";  |MKDIR aAlfa;
   aAlfa  = eaUnidadBasico + "/DS/DISKETTE/"
   |R_PDIR aAlfa;
   |PARA; |SI FSalida = 0; |HACIENDO;
           |RFBORRA aAlfa;
           |R_SDIR aAlfa;
   |SIGUE;

   aOrigen  = direc + fiche;
   |SI nUnidad = 1;
       aAlfa  = eaUnidadBasico + "/DS/DISKETTE/";
   |SINO;
       aAlfa  = aPath;
   |FINSI;
   aDestino = aAlfa + fiche; |QBT aDestino;
   |SI nUnidad = 1;
       aCopia   = "COPY " +  unida + ":" + fiche + " " + aDestino;
       |RSYSTEM aCopia;
   |FINSI;

   aIPRemoto = "";
   |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;
   |SI aIPRemoto ! "";
       |RECIBE_FICHERO aDestino, aOrigen;
   |SINO;
       |COPIA_FICHERO aDestino, aOrigen;
   |FINSI;
|FPRC;

|PROCESO exporta;
   aAlfa  = eaUnidadBasico + "/DS/DISKETTE";  |MKDIR aAlfa;
   aAlfa  = eaUnidadBasico + "/DS/DISKETTE/"
   |R_PDIR aAlfa;
   |PARA; |SI FSalida = 0; |HACIENDO;
           |RFBORRA aAlfa;
           |R_SDIR aAlfa;
   |SIGUE;

   |SI nUnidad = 1;
       aAlfa    = eaUnidadBasico + "/DS/DISKETTE/"
   |SINO;
       aAlfa    = aPath;
   |FINSI;

   aOrigen  = direc + fiche;
   aDestino = aAlfa + fiche; |QBT aDestino;

   aIPRemoto = "";
   |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;
   |SI aIPRemoto ! "";
       |ENVIA_FICHERO aOrigen, aDestino;
   |SINO;
       |COPIA_FICHERO aOrigen, aDestino;
   |FINSI;

   |SI nUnidad = 1;
       aCopia   = "COPY " + aDestino + " " + unida + ":" + fiche;
       |RSYSTEM aCopia;
   |FINSI;
|FPRC;

|PROCESO ejecuta2;
   |SI #1#1 = "I";   |HAZ importa;  |FINSI;
   |SI #1#1 = "E";   |HAZ exporta;  |FINSI;
|FPRC;

|PROCESO ejecuta;

   |HAZ LeeUnidadBasico;
   |ATRI P;
   |PINTA 2305, "Ejecutando proceso de copia ...";
   |ATRI 0;

   |DFICE direc;            || directorio

   nUnidad = 0;
   aPath = #1#14; |QBF aPath;
   |SI aPath = "A"; |O aPath = "A:"; |O aPath = "A:/"; |O aPath = "A:\";
       nUnidad = 1;
       unida   = "A";
   |FINSI;

   |SI #1#5 = "S";
       fiche = "excuenta.asc"; |HAZ ejecuta2;
       fiche = "exclipro.asc"; |HAZ ejecuta2;
   |FINSI;

   |SI #1#2 = "S"; fiche = "exmaasil.asc"; |HAZ ejecuta2; |FINSI;

   |SI #1#3 = "S";
       |SI #1#1 = "I"; nTipo = #1#9;  |FINSI;
       |SI #1#1 = "E"; nTipo = #1#10; |FINSI;
       |SI nTipo = 1;
           fiche = "exivasop.asc"; |HAZ ejecuta2;
           fiche = "exivarep.asc"; |HAZ ejecuta2;
       |SINO;
           fiche = "exmaniva.asc";   |HAZ ejecuta2;
           fiche = "exivalin.asc";   |HAZ ejecuta2;
       |FINSI;
   |FINSI;
   |SI #1#13 = "S";
       fiche = "exsiim01.asc"; |HAZ ejecuta2;
       fiche = "exsiim02.asc"; |HAZ ejecuta2;
   |FINSI;

   |BLIN 23;
|FPRC;

|PROCESO copiando; |TIPO 12;
   |SI #0#0 = "SI";
       |ABRE #1;
       |LEE 001#1p;
       |SI FSalida = 0;
           |HAZ ejecuta;
       |SINO;
           |MENAV "    Debera definir el Control de Importaciones/Exportaciones ...";
      |FINSI;
      |CIERRA #1;
   |FINSI;
|FPRC;
