|FICHEROS;
   cacherep #0;
   catraivb #4;    || iva temporal
   camaniva #5;    || iva

   canempre #30;
   caselem0 #997;
   caselem1 #998;

   caivaasi #200;
|FIN;

|VARIABLES;
   &entrada = 1;

   nSalir = 0;
   mensa1 = "    ERROR!. FECHA INCORRECTA ";
   mensa2 = "    ERROR!. LIBRO INCORRECTO ";
   mensa3 = "    ERROR!. DEPARTAMENTO INCORRECTO ";
   mensa4 = "    ERROR!. CONCEPTO INCORRECTO ";

   informe   = "cacherep";
   aFichero  = "";
   nCampo    = 0;
   aTip     = "";
   &aDescrip = "";
   fDFecha   = @;
   nDImporte = 0;
   nHImporte = 0;
   nImporte  = 0;
   swAlgun   = 0;
   swSelec   = 0;
   swPrime   = "";
   swFinal   = 0;
   &swImprime = 0;
   nAntLin0  = 0;
   nAntLin   = 0;
   nAntImp   = 0;
   nLinea    = 0;
   aAlfa1    = "";
   aAlfa2    = "";
   nNume1    = 0;
   aCampo0   = "";
   fCampo1   = @;
   nCampo2   = 0;
   aCampo3   = "";
   aCampo4   = "";
   aCampo5   = "";
   aCampo9   = "";
   aCampo11  = "";

   nLibro     = 0;
   nDocumento = 0;
   aDCam0     = "";
   aHCam0     = "";
   aDCam11    = "";
   aHCam11    = "";
   fDCam1     = @;
   fHCam1     = @;
   nDCam2     = 0;
   nHCam2     = 0;
   aDCam3     = "";
   aHCam3     = "";
   aDCam4     = "";
   aHCam4     = "";
   aDCam5     = "";
   aHCam5     = "";
   aDCam9     = "";
   aHCam9     = "";

   aParam     = "";

  nEmpMulti = 0;
  &eSwMul   = 0;
  &espe15   = "";
  &espe16   = 0;
  nSwOpera  = 0;
  nSw       = 0;

  CodigoEmpresa  = 0;
  PeriodoEmpresa = 0;
  dirfichOri     = "";
  nSitua         = 0;
|FIN;

|INCLUYE dscont_i;

*****************************************************************************
CALCULOS DESDE CAMPOS
*****************************************************************************
|CALCULO defectos; |TIPO 0;
  |SI #0#32 = "N";
      #0#3 = fec1;
  |SINO;
      #0#3 = "01.01.2000";
  |FINSI;
  |PINTA #0#3;
  #0#4 = fec2; |PINTA #0#4;
|FCAL;

|CALCULO elTitul; |TIPO 0;
   aTip = #0#0;
   |SI aTip = "R"; aDescrip = "REPERCUTIDO"; |FINSI;
   |SI aTip = "S"; aDescrip = "SOPORTADO  "; |FINSI;
   |SI aTip = "N"; aDescrip = "NO IVA     "; |FINSI;
   aAlfa1 = #0#31  - "REPERCUTIDO";
   aAlfa1 = aAlfa1 - "SOPORTADO";
   aAlfa1 = aAlfa1 - "NO IVA";
   |QBF aAlfa1;
   aAlfa1 = aAlfa1 + " " +  aDescrip;
   #0#31 = aAlfa1;
   |SI aParam = "";
       |PINTA #0#31;
   |FINSI;
|FCAL;

|CALCULO libroh; |TIPO 0;
   |SI #0#2 < #0#1;
      |MENAV mensa2;
      |ERROR;
   |FINSI;
|FCAL;

|CALCULO fechad;
   |SI #0#32 = "N";
       fDFecha = fec1;
       |SI enSwSelFc = 1; fDFecha = "01.01.0000"; |FINSI;
       |SI #0Campo < fDFecha; |O #0Campo > fec2;
           |MENAV mensa1;
           |ERROR;
           |ACABA;
       |FINSI;
   |FINSI;
   |SI Campo = 4; |Y  #0Campo < #0#3;
       |MENAV mensa1;
       |ERROR;
   |FINSI;
|FCAL;

|CALCULO nodepar; |TIPO 7;
   |CAMPO_MODIFICA #0#5,1,eaDepar;
   |CAMPO_MODIFICA #0#6,1,eaDepar;
|FCAL;

|CALCULO deparh; |TIPO 0;
   |C_M #0Campo,0,aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;
   |SI #0#6 < #0#5;
       |MENAV mensa3;
       |ERROR;
   |FINSI;
|FCAL;

|CALCULO conceph; |TIPO 0;
   |SI #0#8 < #0#7;
       |MENAV mensa4;
       |ERROR;
   |FINSI;
|FCAL;

|CALCULO MiraSelec; |TIPO 0;
  |SI #0#11 = "S";
      |C_M #0#12,1,"S";
      |C_M #0#13,1,"S";
      |PARA nCampo = 14; |SI nCampo [ 25; |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo,1,"N";
      |SIGUE;
   |SINO;
      |C_M #0#12,1,"N";
      |C_M #0#13,1,"N";
      |PARA nCampo = 14; |SI nCampo [ 25; |HACIENDO nCampo = nCampo + 1;
            |C_M #0nCampo,1,"S";
      |SIGUE;
   |FINSI;
|FCAL;

|CALCULO NoMasCuentas; |TIPO 0;
   aAlfa1 = #0Campo; |QBF aAlfa1;
   |SI aAlfa1 = "";
      |PARA nCampo = Campo + 1;  |SI nCampo [ 25;  |HACIENDO nCampo = nCampo + 1;
            #0nCampo = "";  |C_M #0nCampo, 1, "N";
      |SIGUE;
   |SINO;
      |PARA nCampo = Campo + 1;  |SI nCampo [ 25;  |HACIENDO nCampo = nCampo + 1;
            |C_M #0nCampo, 1, "S";
      |SIGUE;
  |FINSI;
|FCAL;

|CALCULO BuscaCuenta; |TIPO 6;
     |C_M #0Campo,0,aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;
|FCAL;

|CALCULO SinoUno; |TIPO 0;
  |SI FSalida = 2; |ACABA; |FINSI;
  |SI #0#26 = "N"; |Y #0#27 = "N"; |Y #0#28 = "N"; |Y #0#29 = "N"; |Y #0#30 = "N";
      |MENAV "    Error. Introduzca al menos una Igualdad";
      |ERROR;
  |FINSI;
|FCAL;

|CALCULO ladesv;
  aAlfa1 = #0Campo;
  |C_M #0#33,1,aAlfa1;
|FCAL;

|| *****************************************************************************
|| CALCULOS DE IMPRESION
|| *****************************************************************************
|PROCESO VerDesviacion;
   |SI #0#33 ! 0; |Y #0#27 = "S";
       aAlfa1 = #4#4 + #4#5;
       nImporte  = aAlfa1;
       nDImporte = nAntImp - #0#33;
       nHImporte = nAntImp + #0#33;
       |SI nImporte ] nDImporte; |Y nImporte [ nHImporte;
           swImprime = 1;
       |FINSI;
   |FINSI;
|FPRC;

|PROCESO Imprimir1;
 swImprime = 0;

 |SI swFinal = 1;
     |SI nAntLin ! 1;
         swImprime = 1;
     |SINO;
         |SI #0#27 = "S"; |Y #0#33 ! 0;
             |SI nAntLin0 ! 0;
                 swImprime = 1;
             |FINSI;
         |FINSI;
     |FINSI;
 |SINO;
     |SI #4#6 ! 1;
         swImprime = 1;
     |SINO;
        |SI #0#27 = "N"; |O #0#33 = 0;
            |SI aCampo0 ! #4#0; |O fCampo1 ! #4#1; |O nCampo2 ! #4#2; |O aCampo3 ! #4#3;
                |O aCampo4 ! #4#4; |O aCampo5 ! #4#5; |O aCampo9 ! #4#9; |O aCampo11 ! #4#11;
                    |SI nAntLin ! 1;
                        swImprime = 2;
                    |FINSI;
            |FINSI;

        |SINO;
            |SI aCampo0 ! #4#0; |O fCampo1 ! #4#1; |O nCampo2 ! #4#2; |O aCampo3 ! #4#3;
                 |O aCampo9 ! #4#9; |O aCampo11 ! #4#11;
                    |SI nAntLin0 ! 0;
                        swImprime = 2;
                    |FINSI;
                    nAntLin0 = 0;
            |SINO;
                 |HAZ VerDesviacion;
                 |SI swImprime = 0;
                     |SI nAntLin0 ! 0;
                         swImprime = 2;
                     |FINSI;
                     nAntLin0  = 0;
                 |FINSI;
            |FINSI;
        |FINSI;
     |FINSI;
 |FINSI;

 |SI swImprime ! 0;
     |SI eSwMul ! 0;  |ABRE #5;  |FINSI;
     #5#0 = nLibro;
     #5#1 = nDocumento;
     |LEE 001#5=;
     |SI FSalida = 0;
         |PRINT;
         |SI swImprime = 1;
             nAntLin0 = nAntLin0 + 1;
         |FINSI;
     |FINSI;
     |SI eSwMul ! 0;  |CIERRA #5;  |FINSI;
 |FINSI;
|FPRC;

|PROCESO Imprimir0;
 |SI swPrime = 1;
     |HAZ Imprimir1;
 |FINSI;

 nLibro     = #4#7;
 nDocumento = #4#8;
 swPrime    = 1;

 aCampo0  = #4#0;
 fCampo1  = #4#1;
 nCampo2  = #4#2;
 aCampo3  = #4#3;
 aCampo4  = #4#4;
 aCampo5  = #4#5;
 aCampo9  = #4#9;
 aCampo11 = #4#11;
 nAntLin  = #4#6;
 aAlfa1   = #4#4 + #4#5;
 nAntImp  = aAlfa1;

 |SI eSwMul ! 0;
     dirfich0 = #4#10; |QBF dirfich0;
     |PATH_DAT #5 dirfich0;
 |FINSI;

|FPRC;

|DEFBUCLE Imprimir;
  #4#1;
  ;
  aDCam0, aDCam11, fDCam1, nDCam2, aDCam3, aDCam9, aDCam4, aDCam5, 00001;
  aHCam0, aHCam11, fHCam1, nHCam2, aHCam3, aHCam9, aHCam4, aHCam5, 99999;
  e;
  NULL, Imprimir0;
|FIN;

|| -------------------------------------------------------------
|PROCESO GrabaTmp0;

  |SI Haybasica = 0
      |SI #5#10 < #0#5; |O #5#10 > #0#6;
          |ACABA;
      |FINSI;
  |SINO;
      nDepar = #5#10; |SI nDepar = 0;  nDepar = 1;   |FINSI;
      |SI nDepar < #0#5; |O nDepar > #0#6;  ||desde/hasta actividad
          |ACABA;
      |FINSI;
  |FINSI;
  |SI #0#11 = "S";
      |SI #5#12 < #0#12; |O #5#12 > #0#13;
          |ACABA;
      |FINSI;
  |SINO;
       swSelec = 0;
      |PARA nCampo = 14; |SI nCampo [ 25; |HACIENDO nCampo = nCampo + 1;
            |SI #5#12 = #0nCampo;
                swSelec = 1; |SAL;
            |FINSI;
      |SIGUE;
      |SI swSelec = 0; |ACABA; |FINSI;
  |FINSI;

  aCampo0 = "            ";
  aCampo11= "               ";
  fCampo1 = "01.01.0000";
  nCampo2 = 0;
  aCampo3 = " " * 30;
  aCampo4 = "+";
  aCampo5 = "00000000000.00"
  aCampo9 = " " * 60;

  |SI #0#26 = "S"; |O #0#26 = "A";
      aCampo0 = #5#12;    ||Cuenta Cliente/Proveedor
  |FINSI;
  |SI #0#26 = "C"; |O #0#26 = "A";
      aCampo11 = #5#14;    ||NIF Cliente/Proveedor
  |FINSI;

  |SI #0#28 = "S";
      fCampo1 = #5#4;     ||Fecha Factura
  |FINSI;

  |SI #0#29 = "S";
      nCampo2 = #5#27;
      aCampo3 = #5#28;    ||Concepto Contable
  |FINSI;

  |SI #0#30 = "S";
      aCampo9 = #5#22;    ||Referencia
  |FINSI;

  |SI #0#27 = "S";
      |SI #5#21 < 0; aCampo4 = "-"; #5#21 = - #5#21; |FINSI;

      nImporte = (#5#21 * 100) ! 0;
      aAlfa1   = nImporte;
      aAlfa1   = ("0000000000000" + aAlfa1) % -113;
      aAlfa2   = "." + aAlfa1 % -102;
      aAlfa1   = aAlfa1 % 0111;

      aCampo5 = aAlfa1 + aAlfa2;
  |FINSI;

  nLinea = 0;
  #4#0  = aCampo0;
  #4#11 = aCampo11;
  #4#1  = fCampo1;
  #4#2  = nCampo2;
  #4#3  = aCampo3;
  #4#4  = aCampo4;
  #4#5  = aCampo5;
  #4#9  = aCampo9;
  #4#6  = 99999;
  |LEE 041#4];
  |SI FSalida = 0;
       |LEE 040#4a;
  |SINO;
       |LEE 040#4u;
  |FINSI;
  |SI FSalida = 0; |Y #4#0 = aCampo0; |Y #4#1 = fCampo1; |Y #4#2 = nCampo2;
      |Y #4#3 = aCampo3; |Y #4#4 = aCampo4; |Y #4#5 = aCampo5;
        |Y #4#9 = aCampo9; |Y #4#11 = aCampo11;
         nLinea = #4#6;
  |FINSI;
  nLinea = nLinea + 1;

  #4#0  = aCampo0;
  #4#11 = aCampo11;
  #4#1  = fCampo1;
  #4#2  = nCampo2;
  #4#3  = aCampo3;
  #4#4  = aCampo4;
  #4#5  = aCampo5;
  #4#9  = aCampo9;
  #4#6  = nLinea;
  #4#7  = #5#0;
  #4#8  = #5#1;
  #4#10 = dirfich0;
  |GRABA 020#4n;

  |SI nLinea > 1;
      swAlgun = 1;
  |FINSI;
  |SI swAlgun = 0;
      |SI #0#27 = "S"; |Y #0#33 ! 0;
          #4#0  = aCampo0;
          #4#11 = aCampo11;
          #4#1  = fCampo1;
          #4#2  = nCampo2;
          #4#3  = aCampo3;
          #4#9  = aCampo9;
          #4#4  = "-";
          #4#5  = 99999999999.99;
          #4#6  = 99999;
          |LEE 041#4];
          |SI FSalida = 0;
              |LEE 040#4a;
          |SINO;
              |LEE 040#4u;
          |FINSI;
          |SI FSalida = 0; |Y #4#0 = aCampo0; |Y #4#1 = fCampo1; |Y #4#2 = nCampo2;
               |Y #4#3 = aCampo3; |Y #4#9 = aCampo9; |Y #4#11 = aCampo11;
                  swAlgun = 1;
          |FINSI;
      |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE GrabaTmp;
   #5#5;
   36,27,21;                 || R/S, Departamento, Concepto, Importe;
   #0#3, #0#1, 0000000001, aTip, #0#7, #0#9;
   #0#4, #0#2, 9999999999, aTip, #0#8, #0#10;
   e;
   NULL,GrabaTmp0;
|FIN;

|| ---------------------------------------------------------
|PROCESO EmpiezaEmpresa;
  |DELINDEX #4;
  swAlgun = 0;
|FPRC;

|PROCESO HazTodo;

   |SI nEmpMulti = -1; |O eSwMul = 0;

       |HAZ EmpiezaEmpresa;
       |SI swAlgun = -1; |ACABA; |FINSI;

       nEmpMulti = #30#2;
   |FINSI;

   |ABRE #4;
   |BUCLE GrabaTmp;
   |CIERRA #4;

   |SI eSwMul = 0;
       |HAZ TerminaEmpresa;
   |FINSI;
|FPRC;

|PROCESO TerminaEmpresa;

   |SI swAlgun = 1;

      nAntLin0 = 0;
      swPrime = 0;
      swFinal = 0;
      aDCam0  = "            ";      aHCam0   = "zzzzzzzzzzzz";
      aDCam11 = "               ";   aHCam11  = "zzzzzzzzzzzzzzz";
      fDCam1  = "01.01.0000";        fHCam1   = "31.12.2900";
      nDCam2  = 0;                   nHCam2   = 999;
      aDCam3  = " " * 30;            aHCam3   = "z" * 30;
      aDCam4  = " ";                 aHCam4   = "z";
      aDCam5  = "00000000000.00"   ; aHCam5   = "99999999999.99";
      aDCam9  = " " * 20;            aHCam9   = "z" * 20;
      |SI eSwMul = 0;  |ABRE #5;  |FINSI;

      |BUCLE Imprimir;
      |SI swPrime = 1;
          swFinal = 1;
          |HAZ Imprimir1;
      |FINSI;

      |PIEINF;
      |SI eSwMul = 0;  |CIERRA #5;  |FINSI;
  |FINSI;
|FPRC;

|| ************************************************************************
|| ****************** programa por tipo 3 *********************************
|| ************************************************************************
|CALCULO chequea; |TIPO 3;

   aFichero = ("zz" + Usuario) % 108;
   |NOME_DAT #4 aFichero;

   |IMPRE 0;
   |SI FSalida ! 0;  |ACABA;  |FINSI;

   |INFOR informe;
   |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

   aDescrip = #0#31;

   |SI Haybasica = 1;
       |SI #0#6 = "zz"; #0#6 = "99"; |FINSI;
   |FINSI;

   nSitua = 0;
   eSwMul = 0;
   |SI #0#32 = "N";
       |SI #48#27 = "S"; |Y aParam = "";
           |HAZ MultiEmpresa;
       |SINO;
           |DFICE dirfich0; |QBF dirfich0;
           |HAZ HazTodo;
       |FINSI;
   |SINO;
       eSwMul = 1;
       |SI #48#27 = "S";  eSwMul = 2; |FINSI;
       |HAZ MultiEjercicio;
   |FINSI;
   |FININF;
   |FINIMP;
   |DELINDEX #4;

  |SI nSitua = 1;
      cod1 = CodigoEmpresa;
      cod2 = PeriodoEmpresa;
      emEmp = 1;  || Sin mensaje
      |HAZ leelo;
      |PATH_DAT #5 dirfichOri;
 |FINSI;
|FCAL;

|| **********************************************************************

|PROCESO MultiEmp1;
   nSitua = 1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;
   |SI enSwSelFc = 0;
       |SI #0#3 > #caselem1#5; |O #0#4 < #caselem1#4;
           |ACABA;  || Fuera de la seleccion
       |FINSI;
   |FINSI;

   |PATH_DAT #5 dirfich0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 000001;
 999999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;

|DEFBUCLE MultiEmpEjer;
  #caselem1#2;
  ;
  #997#0, "01.01.0000";
  #997#0, "31.12.2999";
  ;
  NULL,MultiEmp1;
|FIN;

|PROCESO MultiEjer1;
  nEmpMulti = -1;
  |BUCLE MultiEmpEjer;
  |SI nEmpMulti > 0;
      |HAZ TerminaEmpresa;
  |FINSI;
|FPRC;

|DEFBUCLE MultiEjer0;
   #caselem0#1;
   ;
   00001;
   99999;
   ;
   NULL,MultiEjer1;
|FIN;

|PROCESO MultiEjercicio;
   espe15 = "S";

   |SUB_EJECUTA "caselemp";
   |NOME_DAT #998 eaFichsel;
   |NOME_DAT #997 eaFichres;

   nEmpMulti = -1;
   |BUCLE MultiEjer0;
   |DELINDEX #998;
   |DELINDEX #997;
|FPRC;

|PROGRAMA;

 |ABRE #200;
 |LEE 001#200p;
 |SI FSalida ! 0;
     |DDEFECTO #200;
     |GRABA 020#200n;
 |FINSI;
 |CIERRA #200;

 aParam = PARAMETRO; |QBF aParam;
 |SI aParam = "";

     |CLS;
     |CABEZA "Chequeo Fras. Duplicadas";
     |HAZ inicio;
     |HAZ eParaModelos;

     |DDEFECTO #0;

     #0#3 = fec1;
     #0#4 = fec2;
     #0#26 = #200#121; #0#27 = #200#122;
     #0#28 = #200#123; #0#29 = #200#124;
     #0#30 = #200#125; #0#33 = #200#183;

     CodigoEmpresa    = enEmpresa;
     PeriodoEmpresa   = enPeriodo;
     |DFICE dirfichOri;
     nSalir = 0;
     |PARA; |SI nSalir = 0; |HACIENDO;
            |PINPA #0#0;
            |PINDA #0#0;                    || Pinta los datos por defecto
            |ENDATOS #1#0;                  || Seleccion informe
            |SI FSalida ! 0;
                nSalir = 1;
            |FINSI;
     |SIGUE;
 |SINO;
     |ABRE #30;
     #30#2 = enEmpresa;
     #30#3 = enPeriodo;
     |LEE 000#30=;
     |CIERRA #30;
     aAlfa1 = aParam % 101;
     aAlfa2 = aParam % 202;

     |DDEFECTO #0;
     #0#0 = aAlfa1 > " ";
     #0#1 = aAlfa2;
     #0#2 = #0#1;
     #0#3 = fec1;
     #0#4 = fec2;
     #0#13 = "999999999999";
     #0#26 = #200#121; #0#27 = #200#122;
     #0#28 = #200#123; #0#29 = #200#124;
     #0#30 = #200#125; #0#33 = #200#183;
     |HAZ elTitul;
     |HAZ chequea;
 |FINSI;
|FPRO;
