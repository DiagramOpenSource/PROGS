|FICHEROS;
  cacuiv2c #0,MANTE;
  cacuiv2l #1,MANTE;
  cacuenta #2;
  cacuiv2z #50,MANTE;   ||Fichero para copiar F2
  caw00010 #10;
|FIN;

|VARIABLES;
  &entrada  = 1;
  &nEsteAny = 0;
  inkey     = 0;
  esalta    = 0;
  esaltaL   = 0;
  FaltaCtaIVA = 0;
  FaltaCtaRec = 0;
  nSwSal      = 0;

  alfa1       = "";
  alfa2       = "";
  nume1       = 0;
  nume2       = 0;
  fFecha      = @;
  nEjercicio  = 0;
|FIN;

|INCLUYE dscont_i;

**************************************************************************
|RUTINA ClaveBajaL;
     #1#0  = aTipoIVA;
     #1#12 = nEjercicio;
     #1#13 = fFecha;
     #1#1  = 01;
|FRUT;

|RUTINA ClaveAltaL;
     #1#0  = aTipoIVA;
     #1#12 = nEjercicio;
     #1#13 = fFecha;
     #1#1  = 99;
|FRUT;

**************************************************************************
LLAMADAS DESDE Cuentas IVA Cabecera
**************************************************************************
                            || .... Desde Campo A¤o
|CALCULO descri1; |TIPO 0;
  esalta = (FEntrada / 100) ! 0;
||  |SI #0#20 < nEsteAny;     || Tique 70000_6433
||      |SI esalta = 1; |O esalta = 2;
||          |MENAV "    Aviso. No puede modificar un a¤o Inferior al Actual ";
||          |ERROR;
||      |FINSI;
||  |FINSI;
  |SI aTipoIVA = "S"; #0#1 = "Soportado"; |FINSI;
  |SI aTipoIVA = "R"; #0#1 = "Repercutido"; |FINSI;
  |SI aTipoIVA = "N";
      #0#1 = "Reg. sin IVA";
      |C_M #0#2, 1, "N"; #0#2 = "S";  |PINTA #0#2;
      |C_M #0#6, 1, "N"; #0#6 = "";   |PINTA #0#6;
      |C_M #0#7, 1, "N"; #0#7 = "";   |PINTA #0#7;
      |C_M #0#17,1, "N"; #0#17 = "S"; |PINTA #0#17;
      |C_M #0#18,1, "N"; #0#18 = "";  |PINTA #0#18;
  |SINO;
      |C_M #0#2, 1, "S";
      |C_M #0#6, 1, "S";
      |C_M #0#7, 1, "S";
      |C_M #0#17,1, "S";
      |C_M #0#18,1, "S";
  |FINSI;
  |PINTA #0#0;
  |PINTA #10#1;
  nEjercicio = #0#20;
  fFecha     = #0#21;
  |HAZ PresentaLinea;
|FCAL;

|PROCESO BorraLineas;
     |BORRA 140#1a;
     |LIBERA #1;
|FPRC;

|DEFBUCLE cacuiv2l;
     #1#1;
     ;
     #0#0, #0#20, #0#21, 00;
     #0#0, #0#20, #0#21, 99;
     be;
     NULL, BorraLineas;
|FIN;

|CALCULO Tipo2_1;  |TIPO 2;
     esalta  = (FEntrada / 100) ! 0;
     |SI esalta ! 3;  |ACABA;  |FINSI;
     |BUCLE cacuiv2l;
|FCAL;

||************************************************************************
|PROCESO RevisaIVA;
   |SI #1#3 = "            "; |Y #1#2 ! 0;
        FaltaCtaIVA = 1;
        |ERROR10;
   |FINSI;
   |SI #1#5 = "            ";  |Y #1#4 ! 0;
       FaltaCtaRec = 1;
       |ERROR10;
   |FINSI;
|FPRC;

|DEFBUCLE RevisaIVA;
  #1#1;
  ;
  #0#0, #0#20, #0#21, 01;
  #0#0, #0#20, #0#21, 99;
  e;
  NULL, RevisaIVA;
|FIN;

|CALCULO elTipo3; |TIPO 3;
    esalta  = (FEntrada / 100) ! 0;

    nEjercicio = #0#20;
    fFecha     = #0#21;
    nSwSal = 0;
    |ENTLINEAL #1#1, -4, 2, 22, 0, ClaveBajaL, ClaveAltaL;

    FaltaCtaIVA = 0;
    FaltaCtaRec = 0;

    |SI aTipoIVA ! "N";
        |SI #0#2 = "S";  || Busca una cuenta de IVA y si esta vacia , revisa que todas las lineas de iva tienen una cuenta de IVA y de recargo
            |BUCLE RevisaIVA;
            |SI FaltaCtaIVA = 1;
                |MENAV "0400Introduzca una cuenta de IVA";
                |ERROR;
                |ACABA;
            |FINSI;
            |SI FaltaCtaRec = 1;
                |MENAV "0400Introduzca una cuenta de Recargo";
                |ERROR;
                |ACABA;
            |FINSI;
       |FINSI;

   |FINSI;
|FCAL;

|CALCULO PresentaLinea;  |TIPO 7;
    |PINTA #0#0;
    |PINTA #10#1;
    nEjercicio = #0#20;
    fFecha     = #0#21;
    nSwSal = 1;
    |ENTLINEAL #1#1, -4, 7, 22, 0, ClaveBajaL, ClaveAltaL;
|FCAL;

|| ************************************************************************
|CALCULO CompruebaIVA; |TIPO 0;
 |SI aTipoIVA = "N"; |ACABA; |FINSI;
 alfa1 = "S";
 |SI #0#2 = "S";
     alfa1 = "N";
     #0#6 = ""; |PINTA #0#6;
     #0#7 = ""; |PINTA #0#7;
 |FINSI;
 |CAMPO_MODIFICA #0#6,1,alfa1;
 |CAMPO_MODIFICA #0#7,1,alfa1;
|FCAL;

|CALCULO CompruebaDto; |TIPO 0;
 |SI aTipoIVA = "N"; |ACABA; |FINSI;
 alfa1 = "S";
 |SI #0#17 = "S";
     alfa1 = "N";
     #0#18 = ""; |PINTA #0#18;
 |FINSI;
 |C_M #0#18,1,alfa1;
|FCAL;

                        || Desde Campo Cuenta
|CALCULO AntesCuenta; |TIPO 7;
  esalta = (FEntrada / 100) ! 0;
  |SI esalta = 1;
      #0#7 = #0#6;
      |PINTA #0#7;
  |FINSI;
|FCAL;

|CALCULO cuenta1; |TIPO 6;
   emCta = 1;        || Dar mensajes de error

   eaCuenta = #0Campo;
   salidas  = FSalida;
   |HAZ FiltraPuntos;
   #0Campo = eaCuenta;
   |PINTA #0Campo;

   |HAZ SacaNivel;
   |SI Campo ! 18;
       nume1 = Campo + 5;
       #0nume1 = enNivel;
  |FINSI;
|FCAL;

|CALCULO refresca1; |TIPO 0;
   |C_M #0Campo,0,alfa1;
   |SI alfa1 = "N";
       #0Campo = ""; |PINTA #0Campo;
       |ACABA;
   |FINSI;
   |SI Campo ! 18;    || Cuenta de Descuento puede ser blanca
       |SI #0Campo = doce;  || No blanca
           |MENSA "    Introduzca cuenta";
           |AVISO;
           |ERROR;
           |ACABA;
       |FINSI;
   |FINSI;
   emCta = 1;        || Dar mensajes de error
   eaCuenta = #0Campo;
   |HAZ LeeCuenta;
|FCAL;

|| ***************************************************************************
|| ***************************************************************************
||   Calculos de las Lineas de Tipo IVA
|| ***************************************************************************
|| ***************************************************************************
|CALCULO SalgoYa;  |TIPO 7;
     |SI nSwSal = 1;  |PTEC 807;  |FINSI;
|FCAL;

|CALCULO Comprueba; |TIPO 0;
  alfa1 = #0#2;
  |SI #0#2 = "N"
      #1#3 = "            "; |PINTA #1#3;
      #1#5 = "            "; |PINTA #1#5;
  |FINSI;
  |C_M #1#3,1,alfa1;
  |C_M #1#5,1,alfa1;

  alfa1 = #0#17;
  |SI #0#17 = "N"
      #1#7 = "            "; |PINTA #1#7;
  |FINSI;
  |C_M #1#7,1,alfa1;
|FCAL;

|CALCULO cuenta_L; |TIPO 6;
   |C_M #1Campo,0,alfa1;
   |SI alfa1 = "N"; |ACABA; |FINSI;

   emCta    = 1;        || Dar mensajes de error
   eaCuenta = #1Campo;
   salidas  = FSalida;
   |HAZ FiltraPuntos;
   #1Campo = eaCuenta;
   |PINTA #1Campo;

   |HAZ SacaNivel;
|FCAL;

|CALCULO cuefin_L;  |TIPO 0;
   |C_M #1Campo,0,alfa1;
   |SI alfa1 = "N";
       #1Campo = ""; |PINTA #1Campo;
       |ACABA;
   |FINSI;

   |SI Campo ! 7;
       nume1 = Campo - 1;
       |SI #1nume1 = 0;
           #1Campo = ""; |PINTA #1Campo;
           |ACABA;
       |FINSI;
   |FINSI;

   |SI Campo ! 7; |Y #1Campo = doce;  || No blanca
       |MENSA "    Introduzca cuenta";
       |ERROR;
       |ACABA;
   |FINSI;

   emCta = 1;        || Dar mensajes de error
   eaCuenta = #1Campo;
   |HAZ LeeCuenta;
|FCAL;

|CALCULO MiraIva;|TIPO 7;
  |C_M #1Campo,0,alfa1;
  |SI alfa1 = "N"; |ACABA; |FINSI;
  |SI #1#2 = 0;
      #1#3 = "";
      |PTEC 802;
  |FINSI;
|FCAL;

|CALCULO MiraRecargo;|TIPO 7;
  |C_M #1Campo,0,alfa1;
  |SI alfa1 = "N"; |ACABA; |FINSI;
  |SI #1#4 = 0;
      #1#5 = "";
      |PTEC 802;
  |SINO;
      esaltaL = (FEntrada / 100) ! 0;
      |SI esaltaL = 1;
          #1#5 = #1#3;
          |PINTA #1#5;
      |FINSI;
  |FINSI;
|FCAL;

|CALCULO Tipo3_L; |TIPO 3;  ||Comprobar la correccion de la Linea

  |SI #0#2  = "N"; #1#3 = ""; #1#5 = ""; |FINSI;
  |SI #0#17 = "N"; #1#7 = ""; |FINSI;

  |SI #1#2 = 0; #1#3 = ""; |FINSI;
  |SI #1#4 = 0; #1#5 = ""; |FINSI;

  |SI #1#2 ! 0; |Y #0#2 = "S";
      |SI #1#3 = doce;
          |MENAV "    ERROR. Introduzca Cuenta de IVA";
          |ERROR;
          |ACABA;
      |FINSI;
  |FINSI;
  |SI #1#4 ! 0; |Y #0#2 = "S";
      |SI #1#5 = doce;
          |MENAV "    ERROR. Introduzca Cuenta de Recargo";
          |ERROR;
          |ACABA;
      |FINSI;
  |FINSI;
|FCAL;

|| ****************** PROCESO para copiar a¤os
|PROCESO CopiarLin;
     |SALVA_FICHA #1;
     #1#0  = #50#2;
     #1#12 = #50#1;
     #1#13 = #50#4;
     |GRABA 020#1n;
     |REPON_FICHA #1;
     |LIBERA #1;
|FPRC;

|DEFBUCLE CopiaLineas;
     #1#1;
     ;
     #50#2, #50#0, #50#3, 00;
     #50#2, #50#0, #50#3, 99;
     be;
     NULL, CopiarLin;
|FIN;

|CALCULO noigual;
 |SI #50#1 = #50#0;  |Y #50#4 = #50#3; |AVISO; |ERROR;  |FINSI;
|FCAL;

|CALCULO copiarAny; |TIPO 11;
 inkey = FSalida;
 |SI FSalida ! 10; |ACABA; |FINSI;
 |ERROR;

 |PUSHV 0708 1537;
 |PINPA #0#50;
 #50#1 = #0#20;
 #50#0 = #50#1 - 1;
 #50#2 = aTipoIVA;
 #50#3 = #0#21;
 #50#4 = #0#21;
 |PINDA #0#50;
 |ENDATOS #1#50;
 |SI FSalida ! 0;
     |POPV;
     |ACABA;
 |FINSI;

 |SI #50#0 = #50#1; |Y #50#3 = #50#4;
     |MENAV "    Error El mismo A¤o y fecha en vigor ";
     |POPV;
     |ACABA;
 |FINSI;

 #0#0  = #50#2;
 #0#20 = #50#0;  ||A¤o Origen
 #0#21 = #50#3;
 |LEE 001#0=;
 |SI FSalida ! 0;
     |MENAV "    Copia cancelada. Tabla A¤o Origen inexistente ...";
     |POPV;
     |ACABA;
 |FINSI;
 |SALVA_DATOS #0;

 #0#0  = #50#2;
 #0#20 = #50#1;  ||A¤o Destino
 #0#21 = #50#4;
 |LEE 101#0=;
 |SI FSalida = 0;
     |AVISO;
     |CONFI "    NRegrabar Tabla del A¤o Destino YA EXISTENTE (S/N): ";
     |SI FSalida ! 0;
         |MENAV "    Copia cancelada ...";
         |POPV;
         |REPON_DATOS #0;    ||para que no se queda el salvadatos suelto
         |ACABA;
     |SINO;
         |BORRA 020#0a;
         |BUCLE cacuiv2l;        || Borra Lineas
     |FINSI;
 |FINSI;

 |REPON_DATOS #0;
 #0#0  = #50#2;
 #0#20 = #50#1;  ||A¤o Destino
 #0#21 = #50#4;
 |GRABA 020#0n;

 |BUCLE CopiaLineas;
 |POPV;

 |PTEC 809;
 |PTEC 808;
|FCAL;
