|FICHEROS;
     cacieex9 #0,MANTE;   || Auxiliar Apuntes Lineas
     cacieex8 #11;  || def de proceso Cabecera

     cacuenta #1;   || cuentas
     camaasic #2;   || apuntes cabs.
     camaasil #3;   || apuntes lins.
     camandia #5;   || diarios
     caconimp #8;
     caconasi #20;  || Contador Asientos
     canempre #30;
     caconi08 #100; || Control del Asiento

     :/modelos/def/dsmom030 #4;   || conceptos automaticos
     :/modelos/def/dsmom042 #50;  || Amortizaciones
     :/modelos/def/dsmom041 #51;  || Amortizaciones

     &regisnif@ #709;

     catw0011 #900,MANTE;
     catw0012 #901,MANTE;
     catw0013 #902,MANTE;
     catw0014 #903,MANTE;
     catw0015 #904,MANTE;
     catw0016 #905,MANTE;

     ca8cumay #410;
     ca8m0002 #422;
     cacuebal #4;

     :/contasoc/def/isocie59 #800;
     :/contasoc/def/maempr11 #801;
     :/contasoc/def/isocie62 #806;

     :/contasoc/def/isom0001 #802;
     :/contasoc/def/isom0009 #809;
     :/contasoc/def/isom0010 #810;

     cawdatos #1900;
|FIN;

|VARIABLES;
   nCooperativa  = 0;
   &enDnDIARIO   = 0;
   &enHnDIARIO   = 0;
   &efDFechas    = @;
   &efHFechas    = @;
   &enDDocume    = 0;
   &enHDocume    = 0;
   &enBorraAu    = 0;
   &eaVarDni     = "";
   &enCalcCif    = 0;
    nInkey       = 0;
   &aDni         = "";
   &nSuma6300    = 0;
   aNombre       = "";

     aF2        = "";
     aF3        = "";
     aF4        = "";
     aF5        = "";
     aF6        = "";
     aF7        = "";
     aF8        = "";
     aF12       = "";
     aF13       = "";
     aF14       = "";
     nBoton1    = 0;
     nBoton2    = 0;
     nBoton3    = 0;
     nBoton4    = 0;
     nBoton5    = 0;
     nBoton6    = 0;
     nBoton12   = 0;
     nBoton13   = 0;
     nBoton14   = 0;
     nBoton15   = 0;
     nDesPan    = 0;
     nEstado    = 0;
     nEstado_d  = 0;
     nEstado_b  = 0;
     aTecla     = "";
     aTecla _d  = "";
     aTecla _b  = "";
     nImprim_d  = 0;
     nError     = 0;
     nError_d   = 0;
     nError_b   = 0;
     nOpera     = 0;
     nOpera_d   = 0;
     nSumo      = 0;
     mesdes     = "";
     meshas     = "";
     nSalir = 0;
     nModo  = 0;
     aMensa = "";
     aDirInicio = "";
     aMas0      = "";
     aMas1      = "";
     aMas2      = "";
     aMas3      = "";
     aDef902 = "";
     aElPath = "";
     &entrada   = 1;
     &modcon    = 0;
     &swpanta   = 0;
     &swcalcu   = 0;
     &algun     = 0;
     &anyo      = 0;
     &modo      = 0;
     &nPuntoC   = 0;
     &nPuntoT   = 0;
     &enPorce   = 0;
     &enPorce2  = 0;

     aMascara  = "";
     aFichero1 = "";
     aFichero2 = "";
     aFichero3 = "";
     nNivel    = 0;

     nDiario = 0;
     fFecha  = @;
     nDocum  = 0;
     nLinea  = 0;
     nRegis  = 0;
     nAntes  = 0;

     estado  = 0;
     importe = 0;
     ini     = 0;
     fecalf  = @;
     mes     = "";
     mesfec  = 0;
     mesac   = 0;
     a       = 0;
     b       = 0;
     c       = 0;
     d       = 0;
     x       = 0;
     y       = 0;

     nCampo    = 0;
     nCampo1   = 0;
     nCampos   = 0;
     nNume     = 0;
     nPasado   = 0;
     aAlfa1    = "";
     aAlfa2    = "";
     aAlfa3    = "";
     aLong     = 0;
     nCont     = 0;
     nSw       = 0;
     nSaldo    = 0;
     nPosi     = 0;
     nAsiento  = 0;
     nTecla    = 0;
     nAnyo     = 0;

     aConcepto = "";
     aDCuenta  = "";
     aHCuenta  = "";
     aCuenta   = "";
     aAlfa     = "";
     aCaracter = "";

     am = 0;
     bm = 0;
     cm = 0;
     swalta = 0;
     pasado = 0;
     avisos = "";

     nNume7 = 0;
     nNume1 = 0;
     nNume2 = 0;
     nNume3 = 0;
     nNume4 = 0;
     xx = 0;
     ctdeant = "";
     dideant = 0;
     cthaant = "";
     dihaant = 0;
     cuant = "";
     diant = 0;
     coant = 0;
     deant = "";
     dhant = "";
     imant = 0;
     trant = "";
     drant = 0;

     SwForales       = 0;
     SwCoopera       = 0;
     SwTranspa       = 0;
     SwTranspa1      = 0;
     SwTranspa2      = 0;
     SwTranspa3      = 0;
     SwPeqDimen      = 0;

     Tecla = 0;
     XPara = 0;
     Empre = 0;
     Comodin = "";

     nLaBaseImp = 0;
     nImporte = 0;
     aDepar   = "";
     aElSub   = "";
     aElDep   = "";
     &eaMonConta = "";
     nSwError = 0;
     &enTipSoc = 0;
     &eaTipSoc = "";

     nCanti1 = 0;
     nCanti2 = 0;
     nCanti3 = 0;
     nCanti4 = 0;
     nCantAmor = 0;

     nCam12p   = 0;
     n35Base   = 0;
     nSwMasB   = 0;

    nSwSoc = 0;
    nBusAny = 0;
    nAny10 = 0;
    nAny9  = 0;
    nAny8  = 0;
    nAny7  = 0;
    nAny6  = 0;
    nAny5  = 0;
    nAny4  = 0;
    nAny3  = 0;
    nAny2  = 0;
    nAny1  = 0;

    nCuota = 0;
    nCompensa = 0;
    nComp10 = 0;
    nComp9  = 0;
    nComp8  = 0;
    nComp7  = 0;
    nComp6  = 0;
    nComp5  = 0;
    nComp4  = 0;
    nComp3  = 0;
    nComp2  = 0;
    nComp1  = 0;
   nTtip1 = 0;
   nTtip2 = 0;

   nImprimir = 0;
   &enLinea = 0;
   &nImpCampo = 0;
   inform1  = "cacieex8";
   inform2  = "cacieex9";
   inform3  = "catw0015";
   &aTexto  = "";
   nSigno   = "";
   nNume51  = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE defec1_i;

|| ************************************************************
|| PROCESOS DE LA PANTALLA
|| ************************************************************
|PROCESO ClaveA709;
 #709#0 = "               ";
|FPRC;

|PROCESO ClaveB709;
 #709#0 = "zzzzzzzzzzzzzzz";
|FPRC;

|CALCULO elDNI; |TIPO 6;
  nInkey = FSalida;
  |SI nInkey = 1; |O nInkey = 7; nSalir = 1; |FINSI;

  |SI nInkey = 10;
      enCalcCif = 1;
      eaVarDni  = #11#22;
      |HAZ CalculoDNI;
      #11#22 = eaVarDni;
      |PINTA #11#22;
      |ERROR;
      |ACABA;
  |FINSI;

 |SI nInkey = 17;
     |ABRE #709;
     #709#0 = #11#22;
     |CONSULTA_F_CLAVE #1#709, ClaveA709, ClaveB709;
     #11#22 = #709#0; |PINTA #11#22;
     #11#23 = #709#1; |PINTA #11#23;
     |CIERRA #709;
 |FINSI;

  eaVarDni  = #11Campo;
  enCalcCif = 0;
  |HAZ CalculoDNI;
  eaVarDni = (eaVarDni + "                ") % 115;
  |SI #11Campo ! eaVarDni;
      |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
       |SI FSalida = 0;
           #11Campo = eaVarDni;  |PINTA #11Campo;
           |ERROR;
           |ACABA;
       |FINSI;
    |FINSI;
|FCAL;

|CALCULO elDNI0; |TIPO 0;
  eaCodNif = #11#22; |HAZ LeeNifs;
  |SI ewNif = 0;
      #11#23 = #709#1; |PINTA #11#23;
  |FINSI;
  |SI #11#22 ! #30#8;
      #11#24 = 0; |PINTA #11#24;
  |FINSI;
|FCAL;

|PROCESO Documento;
     |ABRE #20;
     |LEE 001#20p;
     |SI FSalida ! 0;
         |DDEFECTO #20;
         |GRABA 020#20n;
     |FINSI;
     nDocum  = #20#1 + 1;
     nRegis  = #20#2 + 1;
     |CIERRA #20;
|FPRC;

|PROCESO Defectos; |TIPO 7;
     |SI nPasado = 0; |Y #11#27 = "N";
         #11#0 = fec2;
         #11#1 = 1;
         |HAZ Documento;
         #11#2 = nDocum;
         nPasado = 1;
         |PINTA #11#1;  |PINTA #11#0;  |PINTA #11#2;
         #11#31 = 0; |C_V #11#31,1,"N"; |C_M #11#31,1,"N";
         |SI #100#62 = "S";
             #11#31 = #11#2 + 1;
             |C_V #11#31,1,"S";
             |C_M #11#31,1,"S";
             |PINTA #11#31;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO CalFecha; |TIPO 0;
    aAlfa1 = "31.12." + #11#26; #11#0 = aAlfa1;
    |PINTA #11#0;
|FPRC;

|PROCESO Topes; |TIPO 0;
     salidas = FSalida;

     |SI #11Campo < fec1; |O #11Campo > fec2;
         |MENAV "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
         |ERROR;
     |SINO;
          |SI #11Campo [ fec3;
              |MENAV "    ERROR FECHA INFERIOR O IGUAL AL BLOQUEO DE DATOS";
              |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO no_depar; |TIPO 7;
 |C_M #11#18,1,eaDepar;
|FPRC;

|PROCESO LeeDocum; |TIPO 0;
     salidas = FSalida;
     |SI salidas = 2; |ACABA; |FINSI;

     |C_M #11Campo,0,aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     |ABRE #2;
     #2#0 = #11#1;
     #2#1 = #11#0;
     #2#2 = #11Campo;
     |LEE 001#2=;
     |SI FSalida = 0;
         |MENAV "    ERROR!. El Asiento Contable YA existe ... ";
         |ERROR;
         |ACABA;
     |FINSI;
     |CIERRA #2;
     #11#18 = aElDep; |PINTA #11#18;
|FPRC;

|PROCESO SiModi7; |TIPO 7;
 |SI enTipSoc > 1;
     |C_M #11#5,1,"N";
 |SINO;
     |C_M #11#5,1,"S";
 |FINSI;
|FPRC;

|PROCESO ElCampoBase;
  |SI #11#26 [ 2010;
      nLaBaseImp = 20000000; || Preparado para comparacion Euro
      |SI eaMoneda = "E";
          nLaBaseImp = (nLaBaseImp / EURO) ! EURODB;
          nLaBaseImp = nLaBaseImp - 0.01;
      |FINSI;
  |SINO;
      nLaBaseImp = 300000; || Solo euros
      |SI eaMoneda ! "E";
          nLaBaseImp = (nLaBaseImp * EURO) ! 0;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO GrTipo2; |TIPO 7;

     |C_M #11#6,1,"S";

     || #11#11 = #11#3 + #11#7 - (#11#8 + #11#9) - #11#10;
     || Base Imponible : A + C + D + E + F.
     || donde D y F son negativos.

     #11#11 = #11#3 + #11#7 + #11#8 + #11#9 + #11#10;

     |HAZ ElCampoBase;

     |SI #11#11 [ nLaBaseImp; |Y enTipSoc [ 1;
         |C_M #11#6,1,"N";
         #11#6 = nTtip2; |PINTA #11#6;
     |FINSI;
|FPRC;

|PROCESO VerSiBien;
  aAlfa3 = #0#2;
  |QBF aAlfa3;
  |SI aAlfa3 = "";
      nSwError = 1;
      |ERROR10;
  |FINSI;
|FPRC;

|DEFBUCLE VerSiBien;
     #0#1;
     ;
     #11#22, 001;
     #11#22, 999;
     ;
     NULL, VerSiBien;
|FIN;

|PROCESO Tipo12C11; |TIPO 12;

 |CONFI "    SCORRECTO S/N";
 |SI FSalida ! 0;
     #11#30 = "N";
     |ACABA;
 |FINSI;


 #11#30 = "S";
 |HAZ Tipo5;

 nSalir = 1;
 |PARA; |SI nSalir = 1; |HACIENDO;
        |ENTLINEAL #1#0, -2, 2, 22, 0, ClaveB7, ClaveA7;
        nSalir = 1;
        |HAZ ConGruEnl;
        |SI #11#30 = "S"; nSalir = 0; |FINSI;
 |SIGUE;

|FPRC;

|PROCESO ConGruEnl;
     #11#30 = "N";
     |SI #11#21 ! 0;
         |CONFI "    NASIENTO DESCUADRADO. Desea Continuar?";
         |SI FSalida = 0;
             #11#30 = "S";
         |FINSI;
     |SINO;
         #11#30 = "S";
     |FINSI;
     |SI #11#30 = "S";
         nSwError = 0;
         |BUCLE VerSiBien;
         |SI nSwError = 1;
             |CONFI "    NHay Cuentas mal Informadas, Salir";
             |SI FSalida ! 0;  #11#30 = "N"; |FINSI;
         |FINSI;
     |FINSI;

     nNume = #11#19;
     |SI #11#15 [ 0; nNume = -nNume; |FINSI;
     |SI #11#15 ! nNume;
         ||MENAV "    EL ASIENTO NO COINCIDE CON EL IMPUESTO A DEVENGAR";
     |FINSI;
|FPRC;
|| /////////////////////////////////////////////////////////
|| /////////////////////////////////////////////////////////
|PROCESO ModiCampo13;
     nNume1 = (nSaldo + #900#3) ! EURODB;
     |SI nNume1 > #11#13;
         nNume2 = (#11#13 - nSaldo) ! EURODB;
         |SI nNume2 < 0; nNume2 = 0; |FINSI;
         #900#3 = nNume2;
         |GRABA 020#900a;
     |FINSI;
     nSaldo = nSaldo + #900#3;
     |LIBERA #900;
|FPRC;

|DEFBUCLE ModiCampo13;
     #900#1;
     ;
     #11#22, 13, "            ";
     #11#22, 13, "zzzzzzzzzzzz";
     be;
     NULL, ModiCampo13;
|FIN;

|CALCULO AvisoNegativo; |TIPO 0;
 |SI FSalida = 2; |ACABA; |FINSI;
 |SI #11#11 < 0;
     |CONFI "    SBASE IMPONIBLE NEGATIVA. CONTINUAR ";
     |SI FSalida ! 0;
         |ERROR;
     |FINSI;
 |FINSI;
|FCAL;

|PROCESO caw00008;  ||aqui - 8 y 9
     |SI nSumo = 1;
         nSaldo = nSaldo - #903#7;
     |SINO;
         #11nCampos = #11nCampos - #903#7;
     |FINSI;
|FPRC;

|DEFBUCLE caw00008;
     #903#1;
     ;
     #11#22, nCampos, 0, "            ";
     #11#22, nCampos, 1, "zzzzzzzzzzzz";
     ;
     NULL, caw00008;
|FIN;

|PROCESO CantiAmort;
     nCantAmor = nCantAmor - #902#5;  ||Tique de 1243_1672
|FPRC;

|DEFBUCLE caw00007;
     #902#1;
     ;
     #11#22, nCampos, "        ", 00, 000;
     #11#22, nCampos, "zzzzzzzz", 99, 999;
     ;
     NULL, CantiAmort;
|FIN;

|PROCESO CantiComp;
     |SI nSumo = 1;
         nSaldo = nSaldo - #901#7;
     |SINO;
         #11#28= #11#28 - #901#4;
         #11#10= #11#10 - #901#7;
     |FINSI;
|FPRC;

|DEFBUCLE caw00006;
     #901#1;
     ;
     #11#22, "            ";
     #11#22, "zzzzzzzzzzzz";
     ;
     NULL, CantiComp;
|FIN;

|PROCESO CantiDemas;
     |SI nSumo = 1;
         nSaldo = nSaldo + #900#3;
     |SINO;
         #11nCampos = #11nCampos + #900#3;
     |FINSI;
|FPRC;

|DEFBUCLE caw00005;
     #900#1;
     ;
     #11#22, nCampos, "            ";
     #11#22, nCampos, "zzzzzzzzzzzz";
     ;
     NULL, CantiDemas;
|FIN;

|PROCESO CantiEspecial;
     #1#0 = #900#1;
     |LEE 041#1=;
     |SI FSalida = 0;
         |SI #1#53 > #900#3; nSwMasB = 1; |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE caw00005s;
     #900#1;
     ;
     #11#22, 13, "            ";
     #11#22, 13, "zzzzzzzzzzzz";
     ;
     NULL, CantiEspecial;
|FIN;

|PROCESO MiraSaldo;
    |SI nTecla = 13;
        |SI nCampos = 8; |Y #1#0 = #100#34; |ACABA; |FINSI;
        |SI nCampos = 9; |Y #1#0 = #100#63; |ACABA; |FINSI;
    |FINSI;

    nSaldo = nSaldo + #1#53;

    |SI nTecla = 10;
         #900#0 = nCampos;
         #900#1 = #1#0;
         #900#5 = #11#22;
         |LIBERA #900;
         |LEE 101#900=;
         |SI FSalida = 0; |ACABA; |FINSI;

         #900#0 = nCampos;
         #900#1 = #1#0;
         #900#5 = #11#22;
         #900#2 = #1#2;
         #900#3 = #1#53;
         #900#4 = #1#1;
         |GRABA 020#900n;
     |FINSI;

     |SI nTecla = 11;
         |DDEFECTO #901;
         #901#8 = #11#22;
         #901#0 = #1#0;
         |LEE 101#901=;
         |SI FSalida = 0;
             #901#2 = #1#53;
             nCuota = #1#53; |HAZ CalCuota2;
             #901#3 = nCompensa;
             |GRABA 020#901a;
             |LIBERA #901;
         |SINO;
             #901#8 = #11#22;
             #901#0 = #1#0;
             #901#1 = #1#2;
             #901#2 = #1#53;
             nCuota = #1#53; |HAZ CalCuota2;
             #901#3 = nCompensa;
             #901#4 = 0;
             #901#5 = #1#1;
             #901#7 = 0;
             |GRABA 020#901n;
             |LIBERA #901;
         |FINSI;
     |FINSI;

     |SI nTecla = 12; |O nTecla = 13;

         |DDEFECTO #903;
         #903#8 = #11#22;
         #903#9 = nCampos;
         #903#6 = 0;     || Linea de Amortizacion
         |SI nTecla = 13; #903#6 = 1; |FINSI;
         #903#0 = #1#0;
         |LEE 101#903=;
         |SI FSalida = 0;
             #903#2 = #1#53;
             nCuota = #1#53; |HAZ CalCuota2;
             #903#3 = nCompensa;
             |SI nTecla = 12;
                 #903#7 = nCantAmor;
                 nCompensa = #903#7; |HAZ CalCuotaCom;
                 #903#4 = nCuota;
             |FINSI;
             |GRABA 020#903a;
             |LIBERA #903;
         |SINO;
             #903#8 = #11#22;
             #903#9 = nCampos;
             #903#6 = 0;     || Linea de Amortizacion
             |SI nTecla = 13; #903#6 = 1; |FINSI;
             #903#0 = #1#0;
             #903#1 = #1#2;
             #903#5 = #1#1;
             #903#2 = #1#53;
             nCuota = #1#53; |HAZ CalCuota2;
             #903#3 = nCompensa;
             #903#4 = 0;
             #903#7 = 0;
             |SI nTecla = 12;
                 #903#7 = nCantAmor;
                 nCompensa = #903#7; |HAZ  CalCuotaCom;
                 #903#4 = nCuota;
             |FINSI;
             |GRABA 020#903n;
             |LIBERA #903;
         |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE cacuenta_1;
    #1#1;
    3;
    aDCuenta, "S";
    aHCuenta, "S";
    ;
    NULL, MiraSaldo;
|FIN;

|PROCESO MontaCuenta;

     nSaldo = 0;
     |QBT aCuenta;
     |SI aCuenta ! "";
         aAlfa   = aCuenta % -101;
         |SI aAlfa ! "*";
             aDCuenta = aCuenta;
             aHCuenta = aCuenta;
         |SINO;
             aCuenta = (aCuenta + "            ") % 112;
             aAlfa   = "";
            |PARA nCampo = 1;  |SI nCampo [ 12;  |HACIENDO nCampo = nCampo + 1;
                  nPosi = (nCampo * 100) + 1;
                  aCaracter = aCuenta % nPosi;
                  |SI aCaracter ! "*";  |Y aCaracter ! " ";
                      aAlfa = aAlfa + aCaracter;
                  |FINSI;
            |SIGUE;

            aDCuenta = (aAlfa + "            ") % 112;
            aHCuenta = (aAlfa + "999999999999") % 112;
        |FINSI;
        |BUCLE cacuenta_1;
     |FINSI;
     nSaldo = 0;
     nSumo  = 1;
     |SI nTecla = 10; |BUCLE caw00005; |FINSI;
     |SI nTecla = 11; |BUCLE caw00006; |FINSI;
     |SI nTecla = 12; |O nTecla = 13; |BUCLE caw00008; |FINSI;
     nSumo = 0;
|FPRC;

|| ........ Auxiliar 900

|RUTINA ClaveBaja900;
     #900#0 = nCampos;
     #900#1 = "            ";
     #900#5 = #11#22;
|FRUT;

|RUTINA ClaveAlta900;
     #900#0 = nCampos;
     #900#1 = "zzzzzzzzzzzz";
     #900#5 = #11#22;
|FRUT;

|CALCULO AntesC13; |TIPO 7;

     n35Base = (#11#12 % 35) ! EURODB;
     |SI n35Base < 0; n35Base = 0; |FINSI;

     |SI n35Base > #11#13; |Y #11#12 ! nCam12p;
         nSwMasB = 0;
         |ABRE #1;
         |BUCLE caw00005s;
         |CIERRA #1;
         |SI nSwMasB = 1;
             |MENAV "    Puede Informar mas Bonificaciones y Deducciones";
             |PTEC 824;
         |FINSI;
     |FINSI;
|FCAL;

|PROCESO MiraCuentas;
  nTecla = FSalida;

  |SI nTecla ! 10;
      nCampos = Campo;
      #11nCampos = 0;
      |BUCLE caw00005;
      |HAZ TopeCampo13;
      |PINTA #11nCampos;
      |ACABA;
  |FINSI;

  nCampos = Campo;
  |SI nOpera = 0;        ||Recoge datos Contables siempre
      |ABRE #900;
      |SI nCampos = 7;  aCuenta = #100#10;  |HAZ MontaCuenta;  |FINSI;
      |SI nCampos = 13; aCuenta = #100#22;  |HAZ MontaCuenta;  |FINSI;
      |SI nCampos = 14; aCuenta = #100#25;  |HAZ MontaCuenta;  |FINSI;
      |CIERRA #900;
  |FINSI;

  |PUSHV 0501 2380;
  |PINPA #0#900;
  |SI nCampos = 7;  aAlfa = " C) DIFERENCIAS PERMANENTES                                     "; |FINSI;
  |SI nCampos = 13; aAlfa = " G) BONIFICACIONES Y DEDUCCIONES                                "; |FINSI;
  |SI nCampos = 14; aAlfa = " H) RETENCIONES                                                 "; |FINSI;
  |ATRI R; |PINTA 1117, aAlfa; |ATRI 0;

  |ENTLINEAL #1#900, -3, 2, 22, 0, ClaveBaja900, ClaveAlta900;
  |POPV;

  #11nCampos = 0;
  |BUCLE caw00005;
  |HAZ TopeCampo13;
  |PINTA #11nCampos;
|FPRC;

|PROCESO TopeCampo13;
     |SI nCampos = 13;
         #11#32 = #11#13;
         |SI #11#12 [ 0;
             #11#13 = 0;
         |SINO;
             nNume1 = #11#12 % 35;
             |SI #11#13 > nNume1;
                 #11#13 = nNume1;
             |FINSI;
         |FINSI;
         |SI #11#32 ! #11#13;
              nSaldo = 0;
              |BUCLE ModiCampo13;
         |FINSI;
         #11#32 = #11#13;
     |FINSI;
|FPRC;
|| ...................................

|RUTINA ClaveBaja901;
     #901#0 = "            ";
     #901#8 = #11#22;
|FRUT;

|RUTINA ClaveAlta901;
     #901#0 = "zzzzzzzzzzzz";
     #901#8 = #11#22;
|FRUT;

|PROCESO  MiraCompensa;  |TIPO 7; ||solo campo numero 10 (Perdidas Ejercicios Anteriores)
     nTecla = 11;

     nCampos = Campo;
     |SI nOpera = 0;        ||Recoge datos Contables siempre
         |ABRE #901;
         |SI nCampos = 10; aCuenta = #100#19;  |HAZ MontaCuenta;  |FINSI;
         |CIERRA #901;
     |FINSI;

     |PUSHV 0501 2380;
     |PINPA #0#901;
     |SI nCampos = 10;  aAlfa = " F) PERDIDAS EJERCICIOS ANTERIORES (Saldo Deudor = importe en positivo)"; |FINSI;
     |ATRI R; |PINTA 1002, aAlfa; |ATRI 0;
     |ENTLINEAL #1#901, -2, 2, 22, 0, ClaveBaja901, ClaveAlta901;
     |POPV;

     nCampos = Campo;
     #11#10 = 0;
     #11#28 = 0;
     |BUCLE caw00006;
     |PINTA #11#10;
|FPRC;

|PROCESO GrabaW0007;
     #51#0  = #50#0;
     #51#2  = #50#2;
     #51#3  = #50#3;
     |LEE 040#51=;
     |SI FSalida ! 0;  |DDEFECTO #51;  |FINSI;

     |DDEFECTO #902;
     #902#0 = #50#2;  ||Elemento
     #902#1 = #50#3;  ||Numero
     #902#2 = #50#4;  ||Linea
     #902#6 = #11#22;
     #902#7 = nCampos; || Numero de Campo, activos o pasivos
     |LEE 101#902=;
     estado = FSalida;

     #902#0 = #50#2;  ||Elemento
     #902#1 = #50#3;  ||Numero
     #902#2 = #50#4;  ||Linea
     #902#6 = #11#22;
     #902#7 = nCampos;        || Numero de Campo, activos o pasivos
     #902#3 = #50#5;          ||A¤o
     #902#4 = #51#4;          ||Descripcion

     |SI #50#13  = 0;  ||Diferencia
         |SI estado ! 0; |ACABA; |FINSI;
         #902#5 = 0;
         |GRABA 020#902a;
         |LIBERA #902;
         |ACABA;
     |FINSI;

     nNume51 = #51#20 - #51#14;  ||% contable - % fiscal

     |SI nNume51 ] 0;  || ... #50#13 > 0;       ||diferencia Positiva entre Contable - Fiscal
         |SI nCampos = 8;
             #902#5 = #50#13;
         |SINO;
             |SI estado ! 0; |ACABA; |FINSI;
             #902#5 = 0;
         |FINSI;
     |SINO;
         |SI nCampos = 9;
             #902#5 = #50#13;
         |SINO;
             |SI estado ! 0; |ACABA; |FINSI;
             #902#5 = 0;
         |FINSI;
     |FINSI;

     |SI estado ! 0; |GRABA 020#902n; |FINSI;
     |SI estado = 0; |GRABA 020#902a; |FINSI;
     |LIBERA #902;
|FPRC;

|DEFBUCLE dsmom042;
     #50#2;
     5;
     enEmpresa,"        ", 00, "A", "01.01.1900", 001, nAnyo;
     enEmpresa,"zzzzzzzz", 99, "A", "31.12.2999", 999, nAnyo;
     ;
     NULL, GrabaW0007;
|FIN;


|RUTINA ClaveBaja902;
     #902#0 = "        ";
     #902#1 = 0;
     #902#2 = 0;
     #902#6 = #11#22;
     #902#7 = nCampos;
|FRUT;

|RUTINA ClaveAlta902;
     #902#0 = "zzzzzzzz";
     #902#1 = 99;
     #902#2 = 999;
     #902#6 = #11#22;
     #902#7 = nCampos;
|FRUT;

|RUTINA ClaveBaja903;
     #903#8 = #11#22;
     #903#9 = nCampos;
     #903#6 = 0;
     #903#0 = "            ";
|FRUT;

|RUTINA ClaveAlta903;
     #903#8 = #11#22;
     #903#9 = nCampos;
     #903#6 = 1;
     #903#0 = "zzzzzzzzzzzz";
|FRUT;

|PROCESO ActPasivos; |TIPO 7;
     nTecla  = 10;
     nCampos = Campo;
     |HAZ AmortizaC;
|FPRC;

|PROCESO CtaActPas;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     eaCuenta = eaCuenta;
     emCta = 0; |HAZ SacaNivel;
     |HAZ LeeCuenta;
     |SI eswCta = 1;
         |HAZ AltaCta;
     |FINSI;  ||Cuenta erronea
|FPRC;

|PROCESO AmortizaC;  |TIPO 0;

     nAnyo   =  #11#26;
     |SI nOpera = 0;
         |ABRE #51;
         |ABRE #902;
         |BUCLE dsmom042;
         |CIERRA #51;
         |CIERRA #902;
     |FINSI;

     nAntes = nTecla;
     nCantAmor = 0;
     |BUCLE caw00007;

     nTecla = 12;
     |ABRE #903;
     |SI nCampos = 8;
         eaCuenta = #100#34; |HAZ CtaActPas; #100#34 = eaCuenta;
         aCuenta = #100#34;  |HAZ MontaCuenta;
     |FINSI;
     |SI nCampos = 9;
         eaCuenta = #100#63; |HAZ CtaActPas; #100#63 = eaCuenta;
         aCuenta = #100#63; |HAZ MontaCuenta;
     |FINSI;
     |CIERRA #903;

     #903#6 = 1;  || sin amortizaciones
     |SI nOpera = 0;
         nTecla = 13;
         |ABRE #903;
         |SI nCampos = 8; aCuenta = #100#13;  |HAZ MontaCuenta;  |FINSI;
         |SI nCampos = 9;
             aCuenta = #100#16;  |HAZ MontaCuenta;
             aCuenta = #100#66;  |HAZ MontaCuenta;
         |FINSI;
         |CIERRA #903;
     |FINSI;

     nTecla = nAntes;
     |SI nTecla = 10;
         |PUSHV 0501 2380;
         |PINPA #0#903;
         |SI nCampos = 8; aAlfa = " D) ACTIVOS TEMPORALES (Saldo Deudor = importe en positivo)"; |FINSI;
         |SI nCampos = 9; aAlfa = " E) PASIVOS TEMPORALES (Saldo Acreedor = importe en negativo)"; |FINSI;
         |ATRI R; |PINTA 0607, aAlfa; |ATRI 0;

         |ENTLINEAL #1#903, -4, 2, 14, 0, ClaveBaja903, ClaveAlta903;
         |POPV;
     |FINSI;

     #11nCampos = 0;
     |BUCLE caw00008;
     |PINTA #11nCampos;
|FPRC;

|| ::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
|PROCESO Impuesto;
     modo    = 1;
     enEmpresa = #30#2;
     enPeriodo = #30#3;
     enPorce   = #11#5;
     enPorce2  = #11#6;

     |SI #30#26 > 80;
         anyo = 1900 + #30#26;
     |SINO;
         anyo = 2000 + #30#26;
     |FINSI;

     nPuntoC = #11#12;
     nPuntoT = #11#14; || falta restarle los pagos a Cuenta

     |SI enConverContasoc ! 0;
         nPuntoC = (nPuntoC / enConverContasoc) ! enRedonPaYaContasoc;
         nPuntoT = (nPuntoT / enConverContasoc) ! enRedonPaYaContasoc;
     |FINSI;

  ||    |SUB_EJECUTA ":contasoc/isacceco"; no hacemos el pase
|FPRC;

|PROCESO BaseImponible; |TIPO 7;
 || |HAZ DatosBase;

  |PUSHV 0601 2380;
  |PINPA #0#905;
  |PINDA #0#905;
  |ENDATOS #2#905;
  |POPV;
|FPRC;


|| //////////////////////////////////////////////////////////////////////////
||... ... ... ... PROCESOS Para las Cuentas de los Lineales
|CALCULO CtaL900; |TIPO 6;
 nModo = (FEntrada / 100) ! 0;

 |SI nModo = 1;
     eaCuenta = #900Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #900Campo = eaCuenta;
     |PINTA #900Campo;

     aAlfa1 = #900Campo; |QBF aAlfa1;
     |SI aAlfa1 = "";
         |MENAV "0000Introduzca una cuenta";
         |ERROR;
         |ACABA;
     |FINSI;

     emCta = 1;
     |HAZ SacaNivel;
     |SI eswCta = 1; |ACABA; |FINSI;

     emCta = 0;
     |HAZ LeeCuenta;
     |SI eswCta = 1;
         |CONFI "    SNo Existe Cuenta. Dar de Alta";
         |SI FSalida ! 0;
             |ACABA;
         |SINO;
             |HAZ AltaCta;
         |FINSI;
     |FINSI;  ||Cuenta erronea

     #900#2 = #1#2;  |PINTA #900#2;
     #900#3 = #1#53; |PINTA #900#3;
     #900#4 = #1#1;
  |FINSI;
|FCAL;

|CALCULO Tipo2_900; |TIPO 2;
 nCampos = #900#0; #11nCampos = #11nCampos +. #900#3;
 |SI nCampos = 13;
     #11#32 = #11#32 +. #900#3;
 |FINSI;
|FCAL;

|CALCULO Ver35_900; |TIPO 0;

  nInkey = FSalida;
  |SI nInkey = 10;
      emCta = 0;
      eaCuenta = #900#1;
      |HAZ LeeCuenta;
      |SI eswCta = 0;
          #900#3 = #1#53; |PINTA #900#3;
      |FINSI;
  |FINSI;

  |SI nCampos = 13;

      |SI #11#12 [ 0;
           nNume1 = 0;
      |SINO;
           nNume1 = #11#12 % 35;
      |FINSI;
      nNume2 = #11#32 + #900#3;
      |SI nNume1 < nNume2;
          |MENAV "    Ha superado el 35% de la Cuota Integra. ";
          #900#3 = nNume1 - #11#32;
          |SI #900#3 < 0; #900#3 = 0; |FINSI;
          |PINTA #900#3;
          |ERROR;
      |FINSI;
  |FINSI;
|FCAL;

|CALCULO CtaL901; |TIPO 0;
 nModo = (FEntrada / 100) ! 0;

 |SI nModo = 1;
     eaCuenta = #901Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #901Campo = eaCuenta;
     |PINTA #901Campo;

     aAlfa1 = #901Campo; |QBF aAlfa1;
     |SI aAlfa1 = "";
         |MENAV "0000Introduzca una cuenta";
         |ERROR;
         |ACABA;
     |FINSI;

     emCta = 1;
     |HAZ SacaNivel;
     |SI eswCta = 1; |ACABA; |FINSI;

     emCta = 0;
     |HAZ LeeCuenta;
     |SI eswCta = 1;
         |CONFI "    SNo Existe Cuenta. Dar de Alta";
         |SI FSalida ! 0;
             |ACABA;
         |SINO;
             |HAZ AltaCta;
         |FINSI;
     |FINSI;  ||Cuenta erronea

     #901#8 = #11#22;
     #901#1 = #1#2;      |PINTA #901#1;
     #901#2 = #1#53;     |PINTA #901#2;
     nCuota = #901#2;    |HAZ CalCuota2;
     #901#3 = nCompensa; |PINTA #901#3;
     #901#4 = 0;         |PINTA #901#4;
     #901#5 = #1#1;
     #901#7 = 0;         |PINTA #901#7;
  |FINSI;
|FCAL;

|CALCULO MiraAny;
 nSw = 0; nCompensa = 0;
 |SI #901#6 = nAny10; nSw = 1; nCompensa = nComp10; |FINSI;
 |SI #901#6 = nAny9;  nSw = 1; nCompensa = nComp9;  |FINSI;
 |SI #901#6 = nAny8;  nSw = 1; nCompensa = nComp8;  |FINSI;
 |SI #901#6 = nAny7;  nSw = 1; nCompensa = nComp7;  |FINSI;
 |SI #901#6 = nAny6;  nSw = 1; nCompensa = nComp6;  |FINSI;
 |SI #901#6 = nAny5;  nSw = 1; nCompensa = nComp5;  |FINSI;
 |SI #901#6 = nAny4;  nSw = 1; nCompensa = nComp4;  |FINSI;
 |SI #901#6 = nAny3;  nSw = 1; nCompensa = nComp3;  |FINSI;
 |SI #901#6 = nAny2;  nSw = 1; nCompensa = nComp2;  |FINSI;
 |SI #901#6 = nAny1;  nSw = 1; nCompensa = nComp1;  |FINSI;
 |SI nSw = 0;
     |CONFI "    NAviso. El A¤o introducido NO es Correcto. ¨Continuar S/N?";
     |SI FSalida ! 0;
         |ERROR;
     |FINSI;
 |FINSI;
 |SI nSwSoc = 1;  #901#3 = nCompensa; |PINTA #901#3; |FINSI;
|FCAL;

|CALCULO AntesCompensa; |TIPO 7;
     nImporte = (#11#3 + #11#7 + #11#8 + #11#9 + #11#10) ! EURODB;

     |SI #901#3 [ nImporte; |Y nImporte > 0;
         #901#7 = #901#3; |PINTA #901#7;
     |FINSI;
     |SI #901#3 > nImporte; |Y nImporte > 0;
         #901#7 = nImporte; |PINTA #901#7;
     |FINSI;
|FCAL;

|CALCULO Tipo2_901; |TIPO 2;
 #11#10 = #11#10 -. #901#7;
|FCAL;

|CALCULO CalBase901; |TIPO 0;
     nImporte = (#11#3 + #11#7 + #11#8 + #11#9 + #11#10) ! EURODB;
||     |SI nImporte [ 0;
||         #901#7 = 0; |PINTA #901#7;
||         #901#4 = 0; |PINTA #901#4;
||         |ACABA;
||     |FINSI;
     |SI nImporte < #901#7; |Y nImporte > 0;
         |MENAV "    Base a Compensar mayor que la Base Imponible";
         #901#7 = nImporte; |PINTA #901#7;
         |ERROR;
         |ACABA;
     |FINSI;

     nCompensa = #901#7;
     |HAZ CalCuotaCom;
     #901#4 = nCuota; |PINTA #901#4;
|FCAL;

|CALCULO CalCuota2_901;
   nCuota = #901#2;
   |HAZ CalCuota;
   #901#3 = nCompensa; |PINTA #901#3;
|FCAL;

|CALCULO CalCuota_901;
   nNume1 = Contenido;

   |SI #901#4 > #901#2; |Y nOpera = 0;
       |CONFI "    NError, Cuota superior al Saldo de la Cuenta. Continuar";
        |SI FSalida ! 0;
            |ERROR;
            |ACABA;
        |FINSI;
   |FINSI;

   |SI #901#4 = nNume1; |ACABA; |FINSI;
   nCuota = #901#4;
   |HAZ CalCuota;
   #901#7 = nCompensa; |PINTA #901#7;
|FCAL;

|CALCULO CtaL903; |TIPO 0;
 nModo = (FEntrada / 100) ! 0;

 |SI nModo = 1;
     eaCuenta = #903Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #903Campo = eaCuenta;
     |PINTA #903Campo;

     |SI nCampos = 8; |O nCampos = 9;
         |SI #903Campo = #100#34; |O #903Campo = #100#63;
             |MENAV "    Cuenta para Ajustes de Amortizaciones";
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;

     aAlfa1 = #903Campo; |QBF aAlfa1;
     |SI aAlfa1 = "";
         |MENAV "0000Introduzca una cuenta";
         |ERROR;
         |ACABA;
     |FINSI;

     emCta = 1;
     |HAZ SacaNivel;
     |SI eswCta = 1; |ACABA; |FINSI;

     emCta = 0;
     |HAZ LeeCuenta;
     |SI eswCta = 1;
         |CONFI "    SNo Existe Cuenta. Dar de Alta";
         |SI FSalida ! 0;
             |ACABA;
         |SINO;
             |HAZ AltaCta;
         |FINSI;
     |FINSI;  ||Cuenta erronea

     #903#8 = #11#22;
     #903#1 = #1#2;      |PINTA #903#1;
     #903#2 = #1#53;     |PINTA #903#2;
     nCuota = #903#2;    |HAZ CalCuota2;
     #903#3 = nCompensa; |PINTA #903#3;
     #903#4 = 0;         |PINTA #903#4;
     #903#5 = #1#1;
     #903#7 = 0;         |PINTA #903#7;
  |FINSI;
|FCAL;

|PROCESO Alta903; |TIPO 0;
 nModo = (FEntrada / 100) ! 0;
 |SI nModo = 1; #903#6 = 1; |FINSI;
|FPRC;

|PROCESO PintaAjuste;
  |ATRI S;
  |PINTA 1609, "Cuenta ";
  |PINTA 1709, "para   ";
  |PINTA 1809, "Ajustes";
  |PINTA 1909, "por    ";
  |PINTA 2009, "Amorti-";
  |PINTA 2109, "zacion.";
  |ATRI 0;
|FPRC;

|PROCESO NoPintaAjuste;
  |ATRI 0;
  |PINTA 1609, "       ";
  |PINTA 1709, "       ";
  |PINTA 1809, "       ";
  |PINTA 1909, "       ";
  |PINTA 2009, "       ";
  |PINTA 2109, "       ";
|FPRC;

|PROCESO Tipo5_903;  |TIPO 5;
       |LEE 040#903p;
       #903#6 = "1";
       |LINEAS_BI_ATRI #903#6, D, S, -1;
|FPRC;

|PROCESO Tipo7_903;  |TIPO 7;
    |SI #903#6 = 0;
        |HAZ PintaAjuste;
        |ENTLINEAL #1#902, -4, 7, 22, 1, ClaveBaja902, ClaveAlta902;
     |SINO;
        |HAZ NoPintaAjuste;
     |FINSI;
|FPRC;

|PROCESO LaMod903; |TIPO 7;
 nModo = (FEntrada / 100) ! 0;

 |SI #903#6 = 0;
     |ENTLINEAL #1#902, -4, 2, 22, 1, ClaveBaja902, ClaveAlta902;

     nCantAmor = 0;
     |BUCLE caw00007;
     #903#7 = nCantAmor;
     nCompensa = #903#7; |HAZ  CalCuotaCom;
     #903#4 = nCuota;
     |PINTA #903#4;
     |PINTA #903#7;
 |FINSI;

 aAlfa1 = "S"; |SI #903#6 = 0; aAlfa1 = "N"; |FINSI;
 |C_M #903#2, 1, aAlfa1;
 |C_M #903#3, 1, aAlfa1;
 |C_M #903#4, 1, aAlfa1;
 |C_M #903#7, 1, aAlfa1;
|FPRC;

|CALCULO AntesCompensa3; |TIPO 7;
     |SI #903#6 = 1; |Y nCampos = 8;  || (saldo deudor)
          nImporte = (#11#3 + #11#7 + #11#8 + #11#9) ! EURODB;

          |SI #903#3 [ nImporte; |Y nImporte > 0;
              #903#7 = #903#3; |PINTA #903#7;
          |FINSI;
          |SI #903#3 > nImporte; |Y nImporte > 0;
              #903#7 = nImporte; |PINTA #903#7;
          |FINSI;
     |FINSI;
     |SI #903#6 = 1; |Y nCampos = 9;
          nImporte = (#11#3 + #11#7 + #11#8 + #11#9) ! EURODB;

          nNume1 = - #903#3;
          |SI nNume1 [ nImporte; |Y nImporte > 0;
              #903#7 = #903#3; |PINTA #903#7;
          |FINSI;
          |SI nNume1 > nImporte; |Y nImporte > 0;
              #903#7 = nImporte; |PINTA #903#7;
          |FINSI;
     |FINSI;
|FCAL;

|CALCULO Tipo2_903; |TIPO 2;
 |SI #903#9 = 8; #11#8 = #11#8 -. #903#7; |FINSI;
 |SI #903#9 = 9; #11#9 = #11#9 -. #903#7; |FINSI;
|FCAL;

|CALCULO CalBase903; |TIPO 0;
     nImporte = (#11#3 + #11#7 + #11#8 + #11#9) ! EURODB;
||     |SI nImporte [ 0;
||         #903#7 = 0; |PINTA #903#7;
||         #903#4 = 0; |PINTA #903#4;
||         |ACABA;
||     |FINSI;

     nCompensa = #903#7;
     |HAZ CalCuotaCom;
     #903#4 = nCuota; |PINTA #903#4;
|FCAL;

|CALCULO CalCuota2_903;
   nCuota = #903#2;
   |HAZ CalCuota;
   #903#3 = nCompensa; |PINTA #903#3;
|FCAL;

|CALCULO CalCuota_903;
   nNume1 = Contenido;

   |SI nCampos = 8;  ||saldo deudor
      |SI #903#4 > #903#2; |Y nOpera = 0;
          |CONFI "    NError, Cuota superior al Saldo de la Cuenta. Continuar";
           |SI FSalida ! 0;
               |ERROR;
               |ACABA;
           |FINSI;
      |FINSI;
   |SINO;
      nNume2 = - #903#2; ||haber - debe
      nNume3 = - #903#4;
      |SI nNume3 > nNume2; |Y nOpera = 0;
          |CONFI "    NError, Cuota superior al Saldo de la Cuenta. Continuar";
           |SI FSalida ! 0;
               |ERROR;
               |ACABA;
           |FINSI;
      |FINSI;
   |FINSI;
   |SI #903#4 = nNume1; |ACABA; |FINSI;
   nCuota = #903#4;
   |HAZ CalCuota;
   #903#7 = nCompensa; |PINTA #903#7;
|FCAL;

|| //////////////////////////////////////////////////////////////////////////

|| //////////////////////// Calculos Contables
|PROCESO CalCuota;

     |HAZ ElCampoBase;

     |SI enTipSoc [ 1;
         nNume1 = (#11#11 - nLaBaseImp) ! EURODB;  || el resto de base

         |SI #11#11 [ nLaBaseImp;
             nCompensa = ((nCuota * 100) / #11#5) ! EURODB;
         |SINO;
             nNume2 = ((nNume1 * #11#6) / 100) ! EURODB;
             |SI nCuota < nNume2;
                 nCompensa = ((nCuota * 100) / #11#6) ! EURODB;
             |SINO;
                 |SI nCuota = nNume2;
                     nCompensa = nNume1;
                 |SINO;
                     nNume3 = nCuota - nNume2;
                     nNume4 = ((nNume3 * 100) / #11#5) ! EURODB;
                     nCompensa = nNume1 + nNume4;
                 |FINSI;
             |FINSI;
         |FINSI;
    |SINO;
        nCompensa = ((nCuota * 100) / #11#6) ! EURODB;
    |FINSI;
|FPRC;

|PROCESO CalCuotaCom;

     |HAZ ElCampoBase;

     |SI enTipSoc [ 1;
         nNume1 = (#11#11 - nLaBaseImp) ! EURODB;  || el resto de base

         |SI #11#11 [ nLaBaseImp;
             nCuota = (nCompensa % #11#5) ! EURODB;
         |SINO;
             nCuota = (nCompensa % #11#6) ! EURODB;
         |FINSI;
    |SINO;
        nCuota = (nCompensa % #11#6) ! EURODB;
    |FINSI;
|FPRC;

|PROCESO CalCuota2;

     |HAZ ElCampoBase;

     |SI enTipSoc [ 1;
        nCompensa = ((nCuota * 100) / #11#5) ! EURODB;
     |SINO;
        nCompensa = ((nCuota * 100) / #11#6) ! EURODB;
    |FINSI;
|FPRC;

|PROCESO CalcuPorce; |TIPO 0;
     salidas = FSalida;

     |HAZ ElCampoBase;

     #11#11 = #11#3 + #11#7 + #11#8 + #11#9 + #11#10;

     |SI enTipSoc [ 1;
         |SI #11#11 < nLaBaseImp;
             #11#12 = ((#11#11 * #11#5) / 100) ! EURODB;
         |SINO;
             #11#12 = ((nLaBaseImp * #11#5) / 100) ! EURODB;
             nNume1 = #11#11 - nLaBaseImp;
             nNume2 = ((nNume1 * #11#6) / 100) ! EURODB;
             #11#12 = #11#12 + nNume2;
         |FINSI;
     |SINO;
         #11#12 = ((#11#11 * #11#6) / 100) ! EURODB;
     |FINSI;

     #11#32 = #11#13;
     |SI #11#12 [ 0;
         #11#13 = 0;
     |SINO;
        nNume1 = #11#12 % 35;
        |SI #11#13 > nNume1; #11#13 = nNume1; |FINSI;
     |FINSI;
     |SI #11#32 ! #11#13;
         nSaldo = 0;
         |BUCLE ModiCampo13;
     |FINSI;
     #11#32 = #11#13;

||     #11#15 = #11#12 - #11#13 - #11#14;  antes
||     nNume7 = #11#12 - #11#13 - #11#14;
|| cambio calculo segun Carlos Clemente
|| Cuota Liquida       = cuota Integra - bonificaciones y Deducciones
|| cuota del ejercicio = cuota liquida - retenciones

      #11#15 = #11#12 - #11#13;
      nNume7 = (#11#15 - #11#14) ! EURODB;

     |SI nNume7 > 0;
         #11#16 = nNume7; #11#17 = "A INGRESAR";
     |SINO;
         nNume1 = - nNume7;
         |SI nNume1 > #11#14;
             #11#16 = - #11#14;
         |SINO;
             #11#16 = nNume7;
         |FINSI;
         #11#17 = "A DEVOLVER";
     |FINSI;
     |SI #11#16 = 0; #11#17 = "RESULTADO 0"; |FINSI;

     |PINTA #11#11;
     |PINTA #11#12;
     |PINTA #11#13;
     |PINTA #11#15;
     |PINTA #11#16;
     |PINTA #11#17;
|FPRC;


|PROCESO RellenaPantalla;
     |ABRE #900;
     nTecla  = 10;
     nCampos = 7;  aCuenta = #100#10;  |HAZ MontaCuenta;  #11#7  = nSaldo;
     nCampos = 13; aCuenta = #100#22;  |HAZ MontaCuenta;  #11#13 = nSaldo; #11#32 = nSaldo;
     nCampos = 14; aCuenta = #100#25;  |HAZ MontaCuenta;  #11#14 = nSaldo;
     |CIERRA #900;
     nTecla  = 0;
     nCampos = 8;  |HAZ AmortizaC; #11#8 = nSaldo;
     nCampos = 9;  |HAZ AmortizaC; #11#9 = nSaldo;

     #11#11 = #11#3 + #11#7 + #11#8 + #11#9 + #11#10;
     FSalida = 0;
|FPRC;

|PROCESO CalcuBene;
     aAlfa = #1#0;
     aAlfa  = aAlfa % 103;
     |SI aAlfa = "630"; |O aAlfa = "633"; |O aAlfa = "638";
         || no sumo, resultado antes de impuesto
     |SINO;
         #11#3 = #11#3 + #1#53;
     |FINSI;
|FPRC;

|DEFBUCLE cacuenta;
     #1#1;
     3;
     "6           ", "S";
     "7zzzzzzzzzzz", "S";
     ;
     NULL, CalcuBene;
|FIN;

|PROCESO CalCampo3;
    #11#3 = 0;
    |BUCLE cacuenta;

    |ATRI 0;  |PINTA 2410,"                                      ";

    |SI #11#3 [ 0;
        #11#4 = "BENEFICIO";
    |SINO;
        #11#4 = "PERDIDAS";
    |FINSI;
    #11#3 = -#11#3;

    || Saldo Acreedor = Haber - Debe  positivo
|FPRC;

|CALCULO CalCamp3_0; |TIPO 0;
  nInkey = FSalida;
  |SI nInkey = 10;
      |HAZ CalCampo3;
      |PINTA #11#3;
      |PINTA #11#4;
      |ACABA;
  |FINSI;
  |SI #11#3 ] 0;
      #11#4 = "BENEFICIO";
  |SINO;
      #11#4 = "PERDIDAS";
  |FINSI;
  |PINTA #11#4;
|FCAL;
|| =========================================================================
|| ///////// Proceso para borrar datos pantalla
|PROCESO Borrar905;
 #905#33 = #11#22;
 |LEE 141#905=;
 |SI FSalida = 0;
     |BORRA 020#905a;
 |FINSI;
 |LIBERA #905;
|FPRC;

|PROCESO Borrar903;
 |BORRA 020#903a;
 |LIBERA #903;
|FPRC;

|DEFBUCLE Borra903;
 #903#1;
 ;
 #11#22, 01, 0, "            ";
 #11#22, 99, 9, "zzzzzzzzzzzz";
 be;
 NULL, Borrar903;
|FIN;

|PROCESO Borrar902;
 |BORRA 020#902a;
 |LIBERA #902;
|FPRC;

|DEFBUCLE Borra902;
 #902#1;
 ;
 #11#22, 00, "       ", 00, 001;
 #11#22, 99, "zzzzzzz", 99, 999;
 be;
 NULL, Borrar902;
|FIN;

|PROCESO Borrar901;
 |BORRA 020#901a;
 |LIBERA #901;
|FPRC;

|DEFBUCLE Borra901;
 #901#1;
 ;
 #11#22, "            ";
 #11#22, "zzzzzzzzzzzz";
 be;
 NULL, Borrar901;
|FIN;

|PROCESO Borrar900;
 |BORRA 020#900a;
 |LIBERA #900;
|FPRC;

|DEFBUCLE Borra900;
 #900#1;
 ;
 #11#22, 01, "            ";
 #11#22, 99, "zzzzzzzzzzzz";
 be;
 NULL, Borrar900;
|FIN;

|PROCESO Borrarx7;
 |SI #0#13 ! 0;
     nNume1 = #0#13;
     #100nNume1 = #0#2;
 |FINSI;
 |BORRA 020#0a;
 |LIBERA #0;
|FPRC;

|DEFBUCLE Borrarx7;
 #0#1;
 ;
 #11#22, 001;
 #11#22, 999;
 be;
 NULL, Borrarx7;
|FIN;

|PROCESO BorrarDatos;
 |PDEFECTO #11, 3,18;
 #11#5 = nTtip1;
 #11#6 = nTtip2;
 #11#19 = 0;
 #11#20 = 0;
 #11#21 = 0;
 #11#27 = "N";
 #11#32 = 0;

 |BUCLE Borrarx7;
 |BUCLE Borra900;
 |BUCLE Borra901;
 |BUCLE Borra902;
 |BUCLE Borra903;
 |HAZ  Borrar905;

 |ENTLINEAL #1#0, -2, 7, 22, 0, ClaveB7, ClaveA7;
 |PINDA #0#11;
|FPRC;

|PROCESO BajaAsto;
  enDnDIARIO = #11#1;            || diario
  enHnDIARIO = #11#1;
  efDFechas  = #11#0;            || fecha
  efHFechas  = #11#0;            || fecha
  enDDocume  = #11#2;            || documento
  enHDocume  = #11#2;            || documento
  enBorraAu  = 2;
  |SUB_EJECUTA_NP "caborasi;NOMEQUITESQUETECORTOLOSGUEVOS";

  |SI #100#62 = "S"; |Y #11#31 ! 0;
      enDDocume  = #11#31;            || documento
      enHDocume  = #11#31;            || documento
      enBorraAu  = 2;
      |SUB_EJECUTA_NP "caborasi;NOMEQUITESQUETECORTOLOSGUEVOS";
  |FINSI;
  #11#27 = "N"; |PINTA #11#27;
  nPasado = 1;
|FPRC;

|| PROCESOS GRABACION DE LOS ASIENTOS
|| ************************************************************
|PROCESO GrabaDocum;

     |ABRE #20;
     |LEE 101#20p;
     |SI FSalida ! 0;
         |DDEFECTO #20;
         |GRABA 020#20b;
     |FINSI;
     #20#2 = nRegis;
     nDocum = #20#1 + 1;
     |SI nDocum = #11#2;
         #20#1 = nDocum;
     |FINSI;
     |GRABA 020#20a;
     |LIBERA #20;
     |CIERRA #20;
|FPRC;

|PROCESO Cabecera;
     #2#00 = #3#0; ||  diario
     #2#01 = #3#1; ||  Fecha
     #2#02 = #3#2; ||  documento
     |LEE 101#2=;
     |SI FSalida ! 0;
         |DDEFECTO #2;
         #2#00 = #3#0; ||  diario
         #2#01 = #3#1; ||  Fecha
         #2#02 = #3#2; ||  documento
         #2#03 = #3#2; ||  asiento
         |GRABA 020#2b;
     |FINSI;
     |SI #3#08 = "D";
         #2#04 = #2#04 + #3#09;
         |SI #2#09 = 0; #2#08 = #3#4; #2#09 = #3#5; |FINSI;  || contrp.HABER
     |SINO;
         #2#05 = #2#05 + #3#09;
         |SI #2#11 = 0; #2#10 = #3#4; #2#11 = #3#5; |FINSI;  || contrp.DEBE
     |FINSI;
     #2#6 = #2#4 - #2#5;
     #2#12 = nRegis;
     |GRABA 020#2a;
     |LIBERA #2;
|FPRC;

|PROCESO GrabaPuntes;
     |SI #0#7 = 0; |ACABA; |FINSI;

     |SI nDocum = -1; |O nDocum ! #0#12;
         nDocum = #0#12;
         nLinea = -9;
         |HAZ LeeCab;
     |FINSI;

     nLinea = nLinea + 10;
     |DDEFECTO #3;
     #3#0 = nDiario;
     #3#1 = fFecha;
     #3#2 = nDocum;
     #3#3 = nLinea;
     #3#4 = #0#2;
     eaCuenta = #3#4; |HAZ SacaNivel;
     #3#5 = enNivel;
     #3#6 = #0#4;
     #3#7 = #0#5;
     #3#8 = #0#6;
     #3#9 = #0#7;
     #3#21 = aDepar;
     #3#28 = aElSub;
     |SI #3#8 = "D";
         #3#10 = "";
         #3#11 = 0;
         #3#16 = #3#4;
         #3#17 = #3#5;
     |SINO;
         #3#10 = #3#4;
         #3#11 = #3#5;
         #3#16 = "";
         #3#17 = 0;
     |FINSI;

     #3#24 = nRegis;  || Registro
     |GRABA 020#3n;

     |HAZ Cabecera;   || Cabecera Asiento
     MasMenos     = #camaasil#9;
     DebeHaber    = #camaasil#8;
     aCUENTA      = #camaasil#4;
     nREGISTRO    = #camaasil#24;
     nDIARIO      = #camaasil#0;
     aFECHA       = #camaasil#1;
     nDOCUMENTO   = #camaasil#2;
     nNIVELCUENTA = #camaasil#5;
     |HAZ ActDiario;          || diario
     |HAZ ActCuenta;          || cuenta
     |HAZ ActTesteo;          || cuenta
     modcon = 1;
|FPRC;

|DEFBUCLE cacieex9;
     #0#1;
     ;
     #11#22, 001;
     #11#22, 999;
     ;
     NULL, GrabaPuntes;
|FIN;

|PROCESO LeeCab;
     nLinea = 0;
     #2#0 = nDiario;
     #2#1 = fFecha;
     #2#2 = nDocum;
     |LEE 001#2=;
     |SI FSalida = 0;
         #3#0 = #2#0;
         #3#1 = #2#1;
         #3#2 = #2#2;
         #3#3 = 9999;
         |LEE 040#3];
         |SI FSalida = 0;
             |LEE 040#3a;
         |SINO;
             |LEE 040#3u;
         |FINSI;
         |SI FSalida = 0; |Y #3#0 = #2#0; |Y #3#1 = #2#1; |Y #3#2 = #2#2;
             nLinea = #3#3;
         |FINSI;
         nRegis = #2#12;
     |FINSI;
|FPRC;

|PROCESO Apuntes;
     |SI #11#0 < fec1; |O #11#0 > fec2;
         |MENAV "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO. NO CONTABILIZA";
         |ACABA;
     |SINO;
          |SI #11#0 [ fec3;
              |MENAV "    ERROR FECHA INFERIOR O IGUAL AL BLOQUEO DE DATOS. NO CONTABILIZA";
              |ACABA;
          |FINSI;
     |FINSI;

     nDocum  = -1;
     nDiario = #11#1;
     fFecha  = #11#0;
     aDepar  = #11#18;
     |ABRE #2;
     |ABRE #3;
     |BUCLE cacieex9;
     |CIERRA #3;
     |CIERRA #2;

     #11#27 = "S"; |PINTA #11#27;
|FPRC;


|PROCESO ParaGrabar;

     nSwError = 0;
     |BUCLE VerSiBien;
     |SI nSwError = 1;
         |MENAV "    ASIENTO INCORRECTO. NO SE CONTABILIZA";
         |SI nEstado = 0; |GRABA 020#11a; |FINSI;
         |SI nEstado ! 0; |GRABA 020#11n; |FINSI;
         |LIBERA #11;
         |LEE 101#11=;
         nEstado = FSalida;
         |ACABA;
     |FINSI;

     |CONFI "2455SContabilizar Asiento (S/N)";
     |SI FSalida = 0;
         |HAZ Apuntes;
         |SI modcon ! 0;
             |HAZ ActContador;  || GrabaDocum;
         |FINSI;

       ||   |SI HaySociedad = 1; |Y #11#22 = #30#8; |Y enEjercicio ] 2009;
       ||      |CONFI "0000SDesea hacer el Impuesto de Sociedades";
       ||      |SI FSalida = 0;
       ||          |HAZ Impuesto;
       ||      |FINSI;
       || |FINSI;
     |FINSI;

     |SI nEstado = 0; |GRABA 020#11a; |FINSI;
     |SI nEstado ! 0; |GRABA 020#11n; |FINSI;
     |LIBERA #11;
     |LEE 101#11=;
     nEstado = FSalida;

     |SI nEstado_b = 0; |GRABA 020#905a; |FINSI;
     |SI nEstado_b ! 0; |GRABA 020#905n; |FINSI;
     |LIBERA #905;
     |LEE 101#905=;
     nEstado_b = FSalida;

     nDesPan = 1;
     aAlfa1 = "Dar de Alta ";
     |SI nEstado_d = 0; aAlfa1 = "Modificar "; |FINSI;
     aAlfa1 = "    SDesea " + aAlfa1 + "la Distribucion de Resultados";
     |CONFI aAlfa1;
     |SI FSalida = 0;
         |HAZ Distribucion;
     |SINO;
         |HAZ DatosDistribucion;
         #904#22 = #11#22;
         |LEE 141#904=;
         |SI FSalida = 0;
             #904#23 = #11#3;
             #904#24 = #11#4;
             #904#25 = nSuma6300;
             |GRABA 020#904a;
         |FINSI;
         |LIBERA #904;
     |FINSI;

     |CONFI "    SImprimir Asiento (S/N)";
     |SI FSalida = 0;
         nImprimir = 1;
     |FINSI;
|FPRC;

|PROCESO MiraAsto;
 nError = 0;
 |ABRE #2;
 #2#0 = #11#1;
 #2#1 = #11#0;
 #2#2 = #11#2;
 |LEE 001#2=;
 |SI FSalida = 0;
     nError = 1;
 |FINSI;
 |CIERRA #2;
|FPRC;

|PROCESO Avisos;
  nError = 0;
  |SI #11#27 = "S";
      |HAZ MiraAsto;

      |SI nError = 1;
          |CONFI "    NAsiento Contabilizado. Borrarlo";
          |SI FSalida = 0;
              nError = 0;
              |HAZ BajaAsto;
          |FINSI;
      |FINSI;
 |FINSI;
 |SI nError = 0;
     |SI nEstado_d = 0; |Y nOpera = 2;
         |CONFI "    SDesea Borrar la Distribucion de Resultados";
         |SI FSalida = 0;
             |BORRA 020#904a;
             |LIBERA #904;
             nEstado_d = 1;
         |FINSI;
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO Calcular;

    |HAZ BorrarDatos;

    |ATRI I;
    |PINTA 2410,"ESTOY CALCULANDO EL RESULTADO CONTABLE";

   || ...  |C_M #11#3, 1, "N"; Siempre modificable
    #11#3 = 0;
    |BUCLE cacuenta;

    |ATRI 0;  |PINTA 2410,"                                      ";

    |HAZ CalCampo3;

    |HAZ RellenaPantalla;
    |HAZ CalcuPorce;
    |HAZ LeeFichSocie;
|FPRC;

|PROCESO Recoger;

  nImprim_d = 0;
  nImprimir = 0;
  nOpera    = 0;       || recoger datos desde contabilidad
  |HAZ NoPulso;
  |SI nEstado = 0;
      |HAZ Avisos;
      |SI nError = 1;
          |HAZ SiPulso;
          |ACABA;
      |FINSI;
  |FINSI;
  |HAZ Calcular;

  nCam12p = #11#12;

  |PINDA #0#11;
  |ENDATOS #2#11;
  |SI #11#30 = "S";
      |HAZ ParaGrabar;
  |SINO;
      |MENAV "    Proceso abortado ... ";
      |PTEC 802;
      |PTEC 806;
  |FINSI;
  |HAZ SiPulso;
|FPRC;

|PROCESO Modificar;
  nImprim_d = 0;
  nImprimir = 0;
  nOpera    = 1;       || Modificar sin recoger Datos
  |HAZ NoPulso;
  |SI nEstado = 0;
      |HAZ Avisos;
      |SI nError = 1;
          |HAZ SiPulso;
          |ACABA;
      |FINSI;
  |FINSI;

  |HAZ LeeFichSocie;
||  |SI #11#22 ! #30#8
||      |C_M #11#3, 1, "S";
||  |SINO;
||      |C_M #11#3, 1, "N";
||  |FINSI;
  nCam12p = #11#12;

  |ENDATOS #2#11;
  |SI #11#30 = "S";
      |HAZ ParaGrabar;
  |SINO;
      |MENAV "    Proceso abortado ... ";
      |PTEC 802;
      |PTEC 806;
  |FINSI;
  |HAZ SiPulso;
|FPRC;

|PROCESO Borrar;
  nOpera = 2;       || Borrar
  |HAZ NoPulso;
  |SI nEstado = 0;
      |CONFI "    SConfirmacion Borrar";
      |SI FSalida ! 0;
          |HAZ SiPulso;
          |ACABA;
      |FINSI;
  |FINSI;
  |SI nEstado = 0;
      |HAZ Avisos;
      |SI nError = 1;
          |HAZ SiPulso;
          |ACABA;
      |FINSI;
  |FINSI;
  |HAZ BorrarDatos;
  |SI nEstado = 0;
      |BORRA 040#11a;
      |LIBERA #11;
  |FINSI;

  |HAZ SiPulso;
|FPRC;

|| ---------------------------- Distribucion de Resultados
|PROCESO cuenta63;

     aAlfa = #1#0;
     aAlfa  = aAlfa % 103;
     |SI aAlfa ="630"; |O aAlfa = "633"; |O aAlfa = "638";
         nSuma6300 = nSuma6300 + (#1#53 - #1#50);
     |FINSI;

|FPRC;

|DEFBUCLE cuenta63;
     #1#1;
     3;
     "63          ", "S";
     "638zzzzzzzzz", "S";
     ;
     NULL, cuenta63;
|FIN;

|PROCESO Las6300;
  aAlfa = #0#2 % 103;
  |SI aAlfa = "630"; |O aAlfa = "633"; |O aAlfa = "638";
      |SI #0#6 = "D";
          nSuma6300 = (nSuma6300 + #0#7) ! 2;
      |SINO;
          nSuma6300 = (nSuma6300 - #0#7) ! 2;
      |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE Las6300;
 #0#1;
 ;
 aDni, 001;
 aDni, 999;
 e;
 NULL, Las6300;
|FIN;

|PROCESO DatosDistribucion;
  aDni      = #11#22;
  nSuma6300 = 0;
  |SI #11#27 = "N"; |BUCLE Las6300; |FINSI;

  |BUCLE cuenta63;
|FPRC;

|PROCESO Lectura_d;

  nError_d = 0;
  |DDEFECTO #904;

  #904#22 = #11#22;
  |LEE 101#904=;
  nEstado_d = FSalida;
  #904#23 = #11#3;
  #904#24 = #11#4;
  #904#25 = nSuma6300;

  |SI nEstado_d ! 0; |Y  nOpera_d ! 2;    || NO es baja
      |SI nDesPan = 0;
         |CONFI "    SNo Existe. Desea Dar de Alta la Distribucion";
         |SI FSalida ! 0;
             nError_d = 1;
              |ACABA;
          |FINSI;
      |FINSI;

      |DDEFECTO #904;
      #904#22 = #11#22;
      #904#0  = 0;
      #904#23 = #11#3;
      #904#24 = #11#4;
      #904#25 = nSuma6300;
      |SI #11#3 > 0;
          #904#0 = #904#23 - #904#25;
          |SI #904#0 < 0; #904#0 = 0; |FINSI;
      |FINSI;
      #904#4 = #904#0 + #904#1 + #904#2 + #904#3;
      |GRABA 020#904b;

      nEstado_d = 0;
  |SINO;
      |SI nDesPan = 1;
          #904#0 = 0;
          |SI #11#3 > 0;
              #904#0 = #904#23 - #904#25;
              |SI #904#0 < 0; #904#0 = 0; |FINSI;
          |FINSI;
          #904#4 = #904#0 + #904#1 + #904#2 + #904#3;
          |GRABA 020#904a;

          #904#22 = #11#22;
          |LEE 141#904=;
          nEstado_d = FSalida;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO Modificar_d;
  |HAZ NoPulso_d;

  nOpera_d = 1;
  |HAZ Lectura_d;
  |SI nError_d = 0;
     |ENDATOS #2#904;
     |SI FSalida = 0;
         |GRABA 020#904a;
         nEstado_d = 0;
     |FINSI;
  |FINSI;
  |HAZ SiPulso_d;
|FPRC;

|PROCESO Borrar_d;
  |HAZ NoPulso_d;
  nOpera_d = 2;
  |HAZ Lectura_d;
  |SI nEstado_d = 0;
      |CONFI "    SConfirmacion Borrar";
      |SI FSalida ! 0;
          |HAZ SiPulso_d;
          |ACABA;
      |FINSI;
      |BORRA 040#904a;
      |LIBERA #904;

      |DDEFECTO #904;
      #904#22 = #11#22;
      |LEE 141#904=;
      nEstado_d = FSalida;
      |PINDA #0#904;
  |FINSI;

  |HAZ SiPulso_d;
|FPRC;

|PROCESO Distribucion;

  nImprimir = 0;
  nOpera_d  = 0;

  |HAZ DatosDistribucion;
  |SI nDesPan = 1;
      #904#0 = 0;
  |FINSI;

  |HAZ Lectura_d;
  |SI nError_d = 1; |ACABA; |FINSI;

  |PUSHV 0601 2380;
  |PINPA #0#904;
  |PINDA #0#904;

  |SI nDesPan = 0;
      |CONTROL_SIMPLE 0, "LBOTON,F7 Alta/Modificacion ", 1903, 1921, Boton12;
      nBoton12 = FSalida;

      |CONTROL_SIMPLE 0, "LBOTON,F8 Baja Datos ", 2003, 2021, Boton13;
      nBoton13 = FSalida;

      |CONTROL_SIMPLE 0, "LBOTON,F9 Imprimir ", 2103, 2121, Boton14;
      nBoton14 = FSalida;

      |CONTROL_SIMPLE 0, "LBOTON, ESC Salir", 1922, 2137, Boton15;
      nBoton15 = FSalida;

      aF12 = &255 + "829";
      aF13 = &255 + "830";
      aF14 = &255 + "831";
      aF7  = &255 + "807";
      aF8  = &255 + "806";
      |PARA; |SI;  |HACIENDO;
             |LEETECLA aTecla_d;
             |SI aTecla_d = aF12;  |HAZ Modificar_d;  |FINSI;
             |SI aTecla_d = aF13;  |HAZ Borrar_d;     |FINSI;
             |SI aTecla_d = aF14;  nImprim_d = 1; |HAZ Imprimir; |FINSI;
             |SI aTecla_d = aF7;  |O aTecla_d = aF8; |SAL; |FINSI;
      |SIGUE;

     |FIN_CONTROL_WINDOWS nBoton15;
     |FIN_CONTROL_WINDOWS nBoton14;
     |FIN_CONTROL_WINDOWS nBoton13;
     |FIN_CONTROL_WINDOWS nBoton12;
     |PULSATECLA;
  |SINO;
      |HAZ Modificar_d;
  |FINSI;
  |LIBERA #904;
  |POPV;
|FPRC;

|PROCESO Imp902;
 |PRINT;
 enLinea = 9;
|FPRC;

|DEFBUCLE Imp902;
 #902#1;
 ;
 #11#22, nCampos, "        ", 00, 000;
 #11#22, nCampos, "zzzzzzzz", 99, 999;
 ;
 NULL, Imp902;
|FIN;


|PROCESO Imp903;
 nImpCampo = nCampos;
 |SI nCampos = 8; aTexto = "ACTIVOS TEMPORALES"; |FINSI;
 |SI nCampos = 9; aTexto = "PASIVOS TEMPORALES"; |FINSI;
 |PRINT;
 |SI #903#6 = 0;
     enLinea = 8;
     |BUCLE Imp902;
 |FINSI;
 enLinea = 5;
|FPRC;

|DEFBUCLE Imp903;
 #903#1;
 ;
 #11#22, nCampos, 0, "            ";
 #11#22, nCampos, 1, "zzzzzzzzzzzz";
 ;
 NULL, Imp903;
|FIN;

|PROCESO Imp901;
 nImpCampo = nCampos;
 aTexto = "PERDIDAS EJERCICIOS ANTERIORES";
 |PRINT;
 enLinea = 7;
|FPRC;

|DEFBUCLE Imp901;
 #901#1;
 ;
 #11#22, "            ";
 #11#22, "zzzzzzzzzzzz";
 ;
 NULL, Imp901;
|FIN;

|PROCESO Imp900;
 nImpCampo = nCampos;
 |SI nCampos = 7;  aTexto = "DIFERENCIAS PERMANENTES"; |FINSI;
 |SI nCampos = 13; aTexto = "BONIFICACIONES Y DEDUCCIONES"; |FINSI;
 |SI nCampos = 14; aTexto = "RETENCIONES"; |FINSI;
 |PRINT;
 enLinea = 3;
|FPRC;

|DEFBUCLE Imp900;
 #900#1;
 ;
 #11#22, nCampos, "            ";
 #11#22, nCampos, "zzzzzzzzzzzz";
 ;
 NULL, Imp900;
|FIN;


|PROCESO ImpCierre7;
 |SI nDocum = -1; |O nDocum ! #0#12;
     enLinea = 0;
     nDocum = #0#12;
 |FINSI;

 |DDEFECTO #1;
 #1#0 = #0#2;
 |LEE 001#1=;

 |PRINT;
 enLinea = 1;
|FPRC;

|DEFBUCLE ImpCierre7;
 #0#1;
 ;
 #11#22, 001;
 #11#22, 999;
 ;
 NULL, ImpCierre7;
|FIN;

|PROCESO Imprimir;
  #1900#1  = #11#22;  || nif
  #1900#2  = #11#23;  || Nombre
  #1900#12 = #11#26;  || Ejercicio

 |IMPRE 0;
 |SI FSalida ! 0; |ACABA; |FINSI;

 |SI nImprimir = 1;
    |INFOR inform1;
    |SI FSalida ! 0;
        |MENAV "    Error en el Informe";
        |FINIMP;
        |ACABA;
    |FINSI;

    enLinea = 0;
    |PRINT;
    enLinea = 2; nCampos = 7;  |BUCLE Imp900;
    enLinea = 4; nCampos = 8;  |BUCLE Imp903;
    enLinea = 4; nCampos = 9;  |BUCLE Imp903;
    enLinea = 6; nCampos = 10; |BUCLE Imp901;
    enLinea = 2; nCampos = 13; |BUCLE Imp900;
    enLinea = 2; nCampos = 14; |BUCLE Imp900;

    |PIEINF;
    |FININF;

    |INFOR inform2;
    |SI FSalida ! 0;
        |MENAV "    Error en el Informe";
        |FINIMP;
        |ACABA;
    |FINSI;

    nDocum = -1;
    |ABRE #1;
    |BUCLE ImpCierre7;
    |CIERRA #1;
    |PIEINF;
    |FININF;
 |FINSI;

 |SI nImprim_d = 0; |Y nImprimir = 1;
     |SI nEstado_d = 0;
         |CONFI "    SImprimir Distribucion de Resultados (S/N)";
         |SI FSalida = 0;
             nImprim_d = 1;
          |FINSI;
      |FINSI;
 |FINSI;

 |SI nImprim_d = 1;
    |INFOR inform3;
    |SI FSalida ! 0;
        |MENAV "    Error en el Informe";
        |FINIMP;
        |ACABA;
    |FINSI;

    |PRINT;
    |PIEINF;
    |FININF;
 |FINSI;

 |SI nImprimir = 1;
     |CONFI "    SImprimir Balance de Cuentas Contable (S/N)";
     |SI FSalida = 0;
         |DFICE dirfich0;
         |SUB_EJECUTA_NP "cabaresu;Impuesto";
         |PULSATECLA;
     |FINSI;
 |FINSI;

 |FINIMP;
|FPRC;

|| =========================================================================
|PROCESO NoPulso;
     |ESTADO_CONTROL nBoton1,  "DISABLE";
     |ESTADO_CONTROL nBoton2,  "DISABLE";
     |ESTADO_CONTROL nBoton3,  "DISABLE";
     |ESTADO_CONTROL nBoton4,  "DISABLE";
     |ESTADO_CONTROL nBoton5,  "DISABLE";
     |ESTADO_CONTROL nBoton6,  "DISABLE";
|FPRC;

|PROCESO SiPulso;
     |ESTADO_CONTROL nBoton1,  "ENABLE";
     |ESTADO_CONTROL nBoton2,  "ENABLE";
     |ESTADO_CONTROL nBoton3,  "ENABLE";
     |ESTADO_CONTROL nBoton4,  "ENABLE";
     |ESTADO_CONTROL nBoton5,  "ENABLE";
     |ESTADO_CONTROL nBoton6,  "ENABLE";
|FPRC;

|PROCESO NoPulso_d;
     |SI nDesPan = 0;
         |ESTADO_CONTROL nBoton12,  "DISABLE";
         |ESTADO_CONTROL nBoton13,  "DISABLE";
         |ESTADO_CONTROL nBoton14,  "DISABLE";
         |ESTADO_CONTROL nBoton15,  "DISABLE";
     |FINSI;
|FPRC;

|PROCESO SiPulso_d;
     |SI nDesPan = 0;
         |ESTADO_CONTROL nBoton12,  "ENABLE";
         |ESTADO_CONTROL nBoton13,  "ENABLE";
         |ESTADO_CONTROL nBoton14,  "ENABLE";
         |ESTADO_CONTROL nBoton15,  "ENABLE";
     |FINSI;
|FPRC;


|PROCESO Boton1;
     |PTEC 824;
|FPRC;

|PROCESO Boton2;
     |PTEC 825;
|FPRC;

|PROCESO Boton3;
     |PTEC 826;
|FPRC;

|PROCESO Boton4;
     |PTEC 827;
|FPRC;

|PROCESO Boton6;
     |PTEC 828;
|FPRC;

|PROCESO Boton5;
     |PTEC 806;
|FPRC;

|PROCESO Boton12;
     |PTEC 829;
|FPRC;

|PROCESO Boton13;
     |PTEC 830;
|FPRC;

|PROCESO Boton14;
     |PTEC 831;
|FPRC;

|PROCESO Boton15;
     |PTEC 806;
|FPRC;

|PROCESO ClaveB7;
 #0#0 = #11#22;
 #0#1 = 001;
|FPRC;

|PROCESO ClaveA7;
 #0#0 = #11#22;
 #0#1 = 999;
|FPRC;

|PROCESO BotonDatos;
||aqui
     nImprimir = 0;
     nImprim_d = 0;
     |DDEFECTO #904;
     #904#22 = #11#22;
     |LEE 041#904=
     nEstado_d = FSalida;

     |DDEFECTO #905;
     #905#33 = #11#22;
     |LEE 041#905=
     nEstado_b = FSalida;

     |ENTLINEAL #1#0, -2, 7, 22, 0, ClaveB7, ClaveA7;

     |CONTROL_SIMPLE 0, "LBOTON,F2 Recoger Contabilidad ", 0653, 0670, Boton1;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F3 Modificar Datos ", 0753, 0770, Boton2;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F4 Borrar Datos ", 0853, 0870, Boton3;
     nBoton3 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F5 Imprimir ", 0953, 0970, Boton4;
     nBoton4 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON,F6 Distribucion Resultados ", 1053, 1070, Boton6;
     nBoton6 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON, ESC Salir", 0772, 1078, Boton5;
     nBoton5 = FSalida;

     aF2 = &255 + "824";
     aF3 = &255 + "825";
     aF4 = &255 + "826";
     aF5 = &255 + "827";
     aF6 = &255 + "828";
     aF7 = &255 + "807";
     aF8 = &255 + "806";
     |PARA; |SI;  |HACIENDO;
         |LEETECLA aTecla;
         |SI aTecla = aF2;
             |HAZ Recoger;
             |SI nImprimir = 1; |HAZ Imprimir; |FINSI;
         |FINSI;
         |SI aTecla = aF3;
             |HAZ Modificar;
             |SI nImprimir = 1; |HAZ Imprimir; |FINSI;
         |FINSI;
         |SI aTecla = aF4;  |HAZ Borrar;     |SAL; |FINSI;
         |SI aTecla = aF5;  nDesPan = 0;     nImprimir = 1; |HAZ Imprimir;     |FINSI;
         |SI aTecla = aF6;  nDesPan = 0;     |HAZ Distribucion; |FINSI;
         |SI aTecla = aF7;  |O aTecla = aF8; |SAL; |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nBoton5;
     |FIN_CONTROL_WINDOWS nBoton6;
     |FIN_CONTROL_WINDOWS nBoton4;
     |FIN_CONTROL_WINDOWS nBoton3;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton1;

     |PULSATECLA;
|FPRC;

|PROCESO PrimDefecto;
    |DDEFECTO #11;
    |SI enEjercicio [ 2010;
        aAlfa1 = "120.202,41 Euros"; |SI eaMoneda ! "E"; aAlfa1 = "20.000.000 Ptas "; |FINSI;
    |SINO;
        aAlfa1 = "300.000,00 Euros";
    |FINSI;

    nTtip1 = #11#5;
    nTtip2 = #11#6;
    |PINTA 0814, aAlfa1;
    |PINTA 0914, aAlfa1;

    |SI enTipSoc ! 0; |ATRI P; |PINTA 0662, eaTipSoc; |ATRI 0; |FINSI;
    #11#22 = aDni;
    #11#23 = aNombre;
    #11#24 = #30#2;
    #11#25 = #30#3;
    #11#26 = enEjercicio;
    aAlfa1 = "31.12." + #11#26; #11#0 = aAlfa1;
    |PINPA #0#0;
    |PINDA #0#11;
|FPRC;

|PROGRAMA;

    |HAZ inicio;

    |HAZ DirAplicSt;
    |SI Haybasica = 1;
         eaIa = #canempre#21;
         enM1 = #canempre#11;
         enM2 = #canempre#12;
         |HAZ LeeDatosEmpresa;
         |SI enCooperativa = 0;
             |ABRE #11;
             |LEE 001#11p;
             |SI FSalida ! 0;
                 |CONFI "    SLa empresa NO es una Cooperativa. Seguir ";
                 |SI FSalida ! 0;
                     |CIERRA #11;
                     |ACABA;
                 |FINSI;
             |FINSI;
             |CIERRA #11;
         |FINSI;
    |FINSI;

    |HAZ eLeeParametro;
    aElDep = eaDfDepC; |SI eaDepar = "N"; aElDep = "01"; |FINSI;
    aElSub = eaDfSubC; |SI eaSubde = "N"; aElSub = "";   |FINSI;
    |ABRE #8;
    |DDEFECTO #8;
    |LEE 001#8=;
    |CIERRA #8;
    enDatos = 0; |SI #8#7 = "S"; enDatos=1; |FINSI;

    |ABRE #100;
    |LEE 040#100p;
    |SI FSalida ! 0;  |DDEFECTO #100;  |FINSI;
    |CIERRA #100;

    |CLS;
    |CABEZA "Asto.Imp. SOCIEDADES para Cooperativas";

    |ABRE #11;
    |ABRE #904;
    |ABRE #905;

    |PINPA #0#11;
    aDni    = #30#8;
    aNombre = #30#1;
    |HAZ PrimDefecto;

    |PARA; |SI nSalir = 0; |HACIENDO;
           |ENCAMPO #22#11;
           |SI FSalida = 1; |O FSalida = 7; |SAL; |FINSI;

           |LEE 101#11=;
           nEstado = FSalida;
           |PINDA #0#11;
           |HAZ BotonDatos;
           aDni    = #11#22;
           aNombre = #11#23;
           |HAZ PrimDefecto;
    |SIGUE;
    |CIERRA #905;
    |CIERRA #904;
    |CIERRA #11;
|FPRO;

|| Detalles del Impuesto
|PROCESO BusAny;
 nCompensa = 0;
 |SI nBusAny ] 2000;
     nBusAny = nBusAny - 2000;
 |SINO;
     nBusAny = nBusAny - 1900;
 |FINSI;
 |SI nBusAny = #806#4; |Y #806#4 ! "  "; nCompensa = #806#21; |FINSI;
 |SI nBusAny = #806#5; |Y #806#5 ! "  "; nCompensa = #806#22; |FINSI;
 |SI nBusAny = #806#6; |Y #806#6 ! "  "; nCompensa = #806#23; |FINSI;
 |SI nBusAny = #806#7; |Y #806#7 ! "  "; nCompensa = #806#24; |FINSI;
 |SI nBusAny = #806#8; |Y #806#8 ! "  "; nCompensa = #806#25; |FINSI;
 |SI nBusAny = #806#27; |Y #806#27 ! "  "; nCompensa = #806#30; |FINSI;
 |SI nBusAny = #806#31; |Y #806#31 ! "  "; nCompensa = #806#34; |FINSI;
 |SI nBusAny = #806#35; |Y #806#35 ! "  "; nCompensa = #806#38; |FINSI;
 |SI nBusAny = #806#39; |Y #806#39 ! "  "; nCompensa = #806#42; |FINSI;
 |SI nBusAny = #806#43; |Y #806#43 ! "  "; nCompensa = #806#46; |FINSI;
|FPRC;

|PROCESO BusAny9;
 nCompensa = 0;
 #809#0 = enEmpresa;
 #809#1 = nAny10;
 #809#4 = nBusAny;
 |LEE 041#809=;
 |SI FSalida = 0;
     nCompensa = #809#10;
     nSwSoc = 1;
 |FINSI;
|FPRC;

|PROCESO LeeFichSocie;

  nSwSoc = 0;
  nAny1  = enEjercicio - 10; nComp1  = 0;
  nAny2  = enEjercicio - 9;  nComp2  = 0;
  nAny3  = enEjercicio - 8;  nComp3  = 0;
  nAny4  = enEjercicio - 7;  nComp4  = 0;
  nAny5  = enEjercicio - 6;  nComp5  = 0;
  nAny6  = enEjercicio - 5;  nComp6  = 0;
  nAny7  = enEjercicio - 4;  nComp7  = 0;
  nAny8  = enEjercicio - 3;  nComp8  = 0;
  nAny9  = enEjercicio - 2;  nComp9  = 0;
  nAny10 = enEjercicio - 1;  nComp10 = 0;

  |SI HaySociedad = 0; |ACABA; |FINSI;
  |SI #11#22 ! #30#8; |ACABA; |FINSI; ||no estamos en la de la Empresa

  Comodin = aPContas + "fich/";
  |PATH_DAT #800 Comodin;
  |PATH_DAT #801 Comodin;
  |PATH_DAT #806 Comodin;

  |PATH_DAT #802 Comodin;
  |PATH_DAT #809 Comodin;
  |PATH_DAT #810 Comodin;

  nCooperativa = 0;

  |ABRE #801;
  #801#0 = enEmpresa;
  #801#1 = nAny10;
  #801#2 = 17;
  |LEE 000#801=;
  |SI FSalida = 0;  nCooperativa = 17;  |FINSI;

  #801#0 = enEmpresa;
  #801#1 = nAny10;
  #801#2 = 18;
  |LEE 000#801=;
  |SI FSalida = 0;  nCooperativa = 18;  |FINSI;

  #801#0 = enEmpresa;
  #801#1 = nAny10;
  #801#2 = 19;
  |LEE 000#801=;
  |SI FSalida = 0;  nCooperativa = 19;  |FINSI;

  |CIERRA #801;

  |SI nAny10 < 2008;
      |ABRE #800;
      #800#0 = enEmpresa;
      #800#1 = nAny10;
      |LEE 040#800=;
      |SI FSalida = 0;
          nComp10 = #800#41;
          nSwSoc = 1;
      |FINSI;
      |CIERRA #800;

      |ABRE #806;
      |DDEFECTO #806;
      #806#0 = Empre;
      #806#1 = nAny10;
      #806#2 = 547;
      |LEE 040#806=;
      |SI FSalida = 0;
          nSwSoc = 1;
          nBusAny = nAny1; |HAZ BusAny;  nComp1  = nCompensa;
          nBusAny = nAny2; |HAZ BusAny;  nComp2  = nCompensa;
          nBusAny = nAny3; |HAZ BusAny;  nComp3  = nCompensa;
          nBusAny = nAny4; |HAZ BusAny;  nComp4  = nCompensa;
          nBusAny = nAny5; |HAZ BusAny;  nComp5  = nCompensa;
          nBusAny = nAny6; |HAZ BusAny;  nComp6  = nCompensa;
          nBusAny = nAny7; |HAZ BusAny;  nComp7  = nCompensa;
          nBusAny = nAny8; |HAZ BusAny;  nComp8  = nCompensa;
          nBusAny = nAny9; |HAZ BusAny;  nComp9  = nCompensa;
       |FINSI;
       |CIERRA #806;

   |SINO;
        |ABRE #802;
        |SELECT #2#802;
        #802#0 = enEmpresa;
        #802#1 = nAny10;
        #802#3 = 552;
        |LEE 040#802=;
        |SI FSalida = 0;
            nComp10 = #802#5;
            nSwSoc = 1;
        |FINSI;
        |SELECT #1#802;
        |CIERRA #802;
        |SI nCooperativa = 0;
            |ABRE #809;
            |SELECT #3#809;
            nBusAny = nAny1; |HAZ BusAny9;  nComp1  = nCompensa;
            nBusAny = nAny2; |HAZ BusAny9;  nComp2  = nCompensa;
            nBusAny = nAny3; |HAZ BusAny9;  nComp3  = nCompensa;
            nBusAny = nAny4; |HAZ BusAny9;  nComp4  = nCompensa;
            nBusAny = nAny5; |HAZ BusAny9;  nComp5  = nCompensa;
            nBusAny = nAny6; |HAZ BusAny9;  nComp6  = nCompensa;
            nBusAny = nAny7; |HAZ BusAny9;  nComp7  = nCompensa;
            nBusAny = nAny8; |HAZ BusAny9;  nComp8  = nCompensa;
            nBusAny = nAny9; |HAZ BusAny9;  nComp9  = nCompensa;
            |SELECT #1#809;
            |CIERRA #809;
        |FINSI;
   |FINSI;
|FPRC;

|PROCESO AltaCta;
   |ABRE #1;
   #1#0 = eaCuenta;
   #1#1 = enNivel;
   |LEE 001#1=;
   |SI FSalida ! 0;
       NumMes = dig1; |HAZ NombreMes; mesdes = NombreMes;
       NumMes = dig1 + 11; |SI NumMes > 12; NumMes = NumMes - 12; |FINSI;
       |HAZ NombreMes; meshas = NombreMes;
       |DDEFECTO #1;
       #1#0 = eaCuenta;
       #1#1 = enNivel;
       |HAZ LeeCtaSup;
       #1#2 = eaNomCtaSup;
       #1#3 = "N"; |SI enNivel = dig3; #1#3 = "S"; |FINSI;
       nMensa   = 1;
       nMensa9  = 1;
       |HAZ defect1;
       #1#56   = mesdes;
       #1#57   = meshas;

       |PUSHV 05012380;
       |PINPA #0#1;
       |PINDA #0#1;
       |ENCAMPO #2#1;
       |POPV;

       |GRABA 020#1n;
  |FINSI;
  |LIBERA #1;
  |CIERRA #1;
|FPRC;
