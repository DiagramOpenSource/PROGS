|FICHEROS;
   otp00000 #0;
|FIN;

|VARIABLES;
 aAlfa1 = "";
 aAlfa2 = "";
 nNume1 = 0;
 nNume2 = 0;
 nNume3 = 0;
 nSwPaso = 0;

 nC1 = 0;
 nC2 = 0;
 aC3 = "";

 &eaNomFEst  = "";
 &eaEstruc   = "";
 &eaEstrucN  = "";
 &enCam1     = 0;
 &enCam2     = 0;

 {-1} aMenu       = "";
      aMenu1      = "1611";
      aMenu2      = "1";
      aMenu3      = "4";
      aMenu4      = "Crear Fichero de Datos.                             ";
      aMenu5      = "Balance de Comprobacion de Sumas y Saldos.          ";
      aMenu6      = "Balance de Situacion I.                             ";
      aMenu7      = "Cuenta de Perdidas y Ganancias I.                   ";
|FIN;

|INCLUYE dscont_i;

|PROGRAMA;

   |HAZ inicio;
   |CLS;
   |CABEZA "Sectores de Produccion";
   |PINPA #0#0;
   |ABRE #0;
   #0#0 = enEmpresa;
   |LEE 101#0=;
   |SI FSalida ! 0;
       |DDEFECTO #0;
       #0#0 = enEmpresa;
       #0#1 = eaNombreEmp;
       |GRABA 020#0b;
   |FINSI;
   #0#7 = enEjercicio;

   |DFICE dirfich0; |QBF dirfich0;
   fich_alta = dirfich0;

  |PARA;  |SI;  |HACIENDO;
          |PINPA #0#0;
          |PINDA #0#0;
          |ENDATOS #1#0;
          |SI FSalida ! 0;  |SAL;  |FINSI;
          nC1 = #0#2; nC2 = #0#3; aC3 = #0#4;
  |SIGUE;

  |GRABA 020#0a;
  |LIBERA #0;
  |CIERRA #0;
|FPRO;

|| ********************************************************************

|CALCULO ElNumero; |TIPO 0;
  |SI FSalida = 2; |ACABA; |FINSI;
  nNume1 = #0#2 + #0#3 - 1;
  |SI nNume1 > 12;
      |MENAV "    Error. Numero de Digitos Incorrecto";
      |ERROR;
      #0#3 = 13 - #0#2; |PINTA #0#3;
  |FINSI;
|FCAL;

|CALCULO LaMascara; |TIPO 0;
   nNume1 = #0#3;
   nNume1 = 100 + nNume1;
   aAlfa1 = nNume1;
   aAlfa2 = #0#4 % aAlfa1;
   #0#4 = aAlfa2;
   |PINTA #0#4;
|FCAL;

|| ********************************************************************
|CALCULO Salir;  |TIPO 0;
     |SI FSalida = 1;
         |PTEC 807;
     |FINSI;
|FCAL;

|CALCULO Tipo12;  |TIPO 12;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     enEmpresa = #0#0;

     |GRABA 020#0a;
     |LIBERA #0;

     eaEstruc  = #0#4;
     eaEstrucN = #0#5;
     enCam1    = #0#2;
     enCam2    = #0#3;

     |SI nC1 ! #0#2; |O nC2 ! #0#3; |O aC3 ! #0#4;
         nSwPaso = 0;
     |FINSI;

     |PUSHV 0501 2380;
     |PARA; |SI; |HACIENDO;
            |MENUG aMenu;
            aMenu2 = FSalida;
            |SI aMenu2 < "1";  |O aMenu2 > "4";  |SAL;  |FINSI;

            |SI aMenu2 = "1";
                |SUB_EJECUTA_NP "otp00003";
                nSwPaso = 1;
            |FINSI;

            |SI aMenu2 = "2";
                |SI nSwPaso = 1;
                    |SUB_EJECUTA_NP "caycosy0;otp";
                |SINO;
                    |MENAV "    Debe crear antes el Fichero";
                |FINSI;
            |FINSI;

            |SI aMenu2 = "3";
                |SI nSwPaso = 1;
                    |SUB_EJECUTA_NP "cabasitu;otp";
                |SINO;
                    |MENAV "    Debe crear antes el Fichero";
                |FINSI;
            |FINSI;

            |SI aMenu2 = "4";
                |SI nSwPaso = 1;
                    |SUB_EJECUTA_NP "cabaexpl;otp";
                |SINO;
                    |MENAV "    Debe crear antes el Fichero";
                |FINSI;
            |FINSI;

            dirfich0 = fich_alta;
     |SIGUE;
     |POPV;

     #0#0 = enEmpresa;
     |LEE 101#0=;
|FCAL;
