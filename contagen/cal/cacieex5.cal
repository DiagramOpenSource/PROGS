|FICHEROS;
   cacieex5 #0;
   caexistc #1;
   caexistl #2;

   existca@ #101;
   existli@ #102;

   canempre #30;
   caselem1 #998;
|FIN;

|VARIABLES;
  &enSoloF = 0;
  &d_mes = 0;
  &h_mes = 0;
  &r_emp = 0;
  &r_per = 0;
  &r_eje = 0;
  &r_nom ="";
  &estru = 0;
  &swUno = 0;
  &eaSiModi = "";

  aPath    = "";
  aMas     = "";
  nCampo   = 0;
  aAlfa    = "";
  aAlfa1   = "";
  aAlfa2   = "";
  nDescar  = 0;
  nNume1   = 0;
  nImporte = 0;
  nAlgun   = 0;

  nPeriodoO   = 0;
  &nEmpresaD  = 0;
  &nPeriodoD  = 0;
  nEjercicioO = 0;
  nEjercicioD = 0;
  nEjerO      = 0;
  nEjerD      = 0;
  nEstado     = 0;
  nSwBorro    = 0;
|FIN;

|INCLUYE dscont_i;

|| *****************************************************************
|PROCESO pintames2; |TIPO 0;
  nCampo = Campo + 1;
  |SI #0Campo ! 0; |Y #0Campo ! 13;
      NumMes = dig1 + (#0Campo - 1);   || Operacion para saber que mes es
      |SI NumMes > 12; NumMes = NumMes - 12; |FINSI;
  |SINO;
      NumMes = #0Campo;
  |FINSI;
  |HAZ NombreMes;
  #0nCampo = NombreMes;
  |PINTA #0nCampo;
|FPRC;

|| **********************************************************************
|PROCESO conexi;
   enTExi = 0;
   d_mes = 0;
   h_mes = #0#3;
   estru = nDescar;
   r_emp = enEmpresa;
   r_per = nPeriodoO;
   r_eje = nEjerO;
   r_nom = eaNombreEmp;
   |SUB_EJECUTA_NP "caexicon";
|FPRC;

|| **********************************************************************
|PROCESO CreoLineas;

   |DDEFECTO #102;
   #102#0 = #2#0;
   #102#1 = #2#1;
   |LEE 001#102=;
   nEstado = FSalida;

   |DDEFECTO #102;
   #102#0 = #2#0; #102#1 = #2#1;
   #102#2 = #2#2; #102#3 = #2#3;
   #102#4 = #2#4; #102#5 = #2#5;
   #102#6 = #2#6; #102#7 = #2#7;
   #102#8 = #2#8; #102#9 = #2#9;
   #102#10 = #2#10;

   nNume1   = #0#3 + 11;
   nImporte = #2nNume1;
   |SI aMonedaC ! aMonedaA;
       impMoneda = nImporte; |HAZ ElCambioMoneda;nImporte = impMoneda;
   |FINSI;
   nNume1     = #0#5 + 11;
   #102nNume1 = nImporte;
   |SI nEstado = 0; |GRABA 020#102a; |FINSI;
   |SI nEstado ! 0; |GRABA 020#102n; |FINSI;
   |LIBERA #102;
|FPRC;

|DEFBUCLE CreoLineas;
 #2#1;
 ;
 #1#0, 00;
 #1#0, 99;
 e;
 NULL, CreoLineas;
|FIN;


|PROCESO LeeCabeceras;
   nDescar = #1#0;
   |SI #0#2 = "S"; |HAZ conexi; |FINSI;

   |ABRE #101;
   #101#0 = #1#0;
   |LEE 101#101=;
   nEstado = FSalida;
   #101#0 = #1#0; #101#1 = #1#1;
   #101#2 = #1#2; #101#3 = #1#3;
   |SI nEstado = 0; |GRABA 020#101a; |FINSI;
   |SI nEstado ! 0; |GRABA 020#101n; |FINSI;
   |LIBERA #101;
   |CIERRA #101;

   |ABRE #102;
   |BUCLE CreoLineas;
   |CIERRA #102;
   nAlgun = 1;
|FPRC;

|DEFBUCLE LeeCabeceras;
 #1#1;
 ;
 #0#0;
 #0#1;
 e;
 NULL, LeeCabeceras;
|FIN;

|PROCESO BorroLineas;
 |BORRA 020#102a;
 |LIBERA #102;
|FPRC;

|DEFBUCLE BorroLineas;
 #102#1002;
 ;
 #101#0, 00;
 #101#0, 99;
 be;
 NULL, BorroLineas;
|FIN;

|PROCESO BorroPrimero;
   nSwBorro = 0;
   |SI #101#0 = 0; nSwBorro = 1; |FINSI;
   |SI #101#0 ] #0#0; |Y #101#0 [ #0#0; nSwBorro = 1; |FINSI;
   |SI nSwBorro = 1;
       |BUCLE BorroLineas;
   |FINSI;
   |LIBERA #101;
|FPRC;

|DEFBUCLE BorroPrimero;
 #101#1001;
 ;
 0;
 #0#1;
 be;
 NULL, BorroPrimero;
|FIN;


|PROCESO HazTodo;

   nPeriodoO   = enPeriodo;
   aMonedaC    = eaMoneda;
   nEjercicioO = enEjercicio;
   nEjerO      = #30#26;


|| ... Lee Ejercicio Posterior
   |SI swUno = 0; |O nEmpresaD = 0;
       nEmpresaD = enEmpresa;
       nEjercicioD = nEjercicioO + 1;
       aAlfa  = nEjercicioD; |QBF aAlfa;
       aAlfa  = aAlfa % -102;
       nEjerD = aAlfa;
       nPeriodoD = -1;
       |ABRE #30;
       |SELECT #4#30;
       #30#2  = enEmpresa;
       #30#26 = nEjerD;
       |LEE 001#30=;
       |SI FSalida = 0;
           nPeriodoD = #30#3;
           aMonedaA  = #30#28;
       |FINSI;
       |CIERRA #30;
   |FINSI;

   |SI nPeriodoD = -1;
       |ACABA;
   |FINSI;

   |DFICB aBase; |QBF aBase;
   aAlfa1 = nEmpresaD; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
   aAlfa2 = nPeriodoD; |QBF aAlfa2; aAlfa2 = ("0"     + aAlfa2) % -101;
   aPath  = aBase + aAlfa1 + aAlfa2 + "/";

   |PATH_DAT #101 aPath;
   |PATH_DAT #102 aPath;

   |BUCLE BorroPrimero;

   nAlgun = 0;
   |ABRE #101;
   |BUCLE LeeCabeceras;
   |CIERRA #101;

   |SI nAlgun = 1;
       r_emp = nEmpresaD;
       r_per = nPeriodoD;
       |HAZ ExisGeneral;
   |FINSI;
|FPRC;

|CALCULO pasalo; |TIPO 3;

    |DBASE aBase; |QBF aBase;
    aMas = aBase + "/def/caexistc";
    |CARGA_DEF aMas, existca@;
    |SI FSalida ! 0;
        |MENAV "    Error en Instalacion de Contabilidad";
        |ACABA;
    |FINSI;

    |DBASE aBase; |QBF aBase;
    aMas = aBase + "/def/caexistl";
    |CARGA_DEF aMas, existli@;
    |SI FSalida ! 0;
        |MENAV "    Error en Instalacion de Contabilidad";
        |DESCARGA_DEF #101;
        |ACABA;
    |FINSI;

   |SI #48#27 = "S"; |Y swUno = 0;
       |HAZ MultiEmpresa;
   |SINO;
       |DFICE dirfich0; |QBF dirfich0;
       |HAZ HazTodo;
   |FINSI;

   |DESCARGA_DEF #102;
   |DESCARGA_DEF #101;
|FCAL;

|PROGRAMA;
 |HAZ inicio;

 |SI swUno = 0;
     |CLS;
     |MANTE #0;
 |SINO;
     |DDEFECTO #0;
     #0#2 = eaSiModi;
     |HAZ pasalo;
 |FINSI;
|FPRO;

|| ..........................................................
|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #2 dirfich0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 000001;
 999999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;
