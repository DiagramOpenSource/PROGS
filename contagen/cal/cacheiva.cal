|FICHEROS;
    cacheiva #0;
    cacuenta #1;    || cuentas
    camaasic #2;    || apuntes cabs.
    camaasil #3;    || apuntes lins.
    caivatmp #4;    || iva temporal
    camaniva #5;
    caivalin #6;
    calibros #7;    || libros

    caivaasi #200;
|FIN;

|VARIABLES;
    TotBase = 0; TotCIva = 0; TotCRec = 0; TotDesc = 0; TotRete = 0;
    Base1   = 0; Base2   = 0; Base3   = 0; Base4   = 0; Base5   = 0;
    CIva1   = 0; CIva2   = 0; CIva3   = 0; CIva4   = 0; CIva5   = 0;
    CRec1   = 0; CRec2   = 0; CRec3   = 0; CRec4   = 0; CRec5   = 0;
    Desc1   = 0; Desc2   = 0; Desc3   = 0; Desc4   = 0; Desc5   = 0;
    Rete1   = 0; Rete2   = 0; Rete3   = 0; Rete4   = 0; Rete5   = 0;
    SwHay   = 0; nCont   = 0; NumRegist = 0;
    p_alfa1 = ""; p_error = 0;
    DLibro = 0;  HLibro = 0;
    DRegist = 0; HRegist = 0;
    fDFecha = @;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    elsw1 = 0;
    FEstado = 0;
    long = 0;
    sw = 0;
    &tipo = "";
    &fec_ant = @;
    &difa = 0;
    &acua = 0;
    &acui = 0;
    &libi = 0;
    &seri = "";
    &frai = 0;
    &desi = "";
    &impi = 0;
    &sw_lin = 0;
    &serii = "";
    sw_iva = 0;
    sw_nod = 0;
    repetir = 0;
    sw_eof = 0;
    des_signo = "";
    has_signo = "";
    sw_asegura = 0;
    &sw_ruptura = 0;
    &dia = @;
    d_cuen = "";
    h_cuen = "";
    d_digi = 0;
    h_digi = 0;
    primero = 0;
    mayor1 = "";
    ord = 0;

    Cont = 0; Signo = 0;
    ExistLibro = 0;

    Opcion = 0;

{-1}Menu  = "";
    Menu1 = "2400";
    Menu2 = "1";
    Menu3 = "Elija formato: ";
    Menu4 = "AE";
    Menu5 = " < Automatico > ";
    Menu6 = " < Extensivo > ";
|FIN;

|INCLUYE dscont_i;

*****************************************************************************
                       CALCULOS DESDE CAMPOS
*****************************************************************************
|PROCESO Tipo8; |TIPO 8;
  |HAZ inicio;
  |HAZ eParaModelos;
|FPRC;

|PROCESO Defectos;
 |ABRE #200;
 |DDEFECTO #200;
 |LEE 041#200p;
 |CIERRA #200;

 |SI #0Campo = "R"; #0#1 = "477*"; #0#5 = #200#3;  |FINSI;
 |SI #0Campo = "S"; #0#1 = "472*"; #0#5 = #200#29; |FINSI;
 |PINTA #0#1;
 |PINTA #0#5;

 #0#12 = fec1; |PINTA #0#12;
 #0#13 = fec2; |PINTA #0#13;
|FPRC;

|PROCESO asterisco;
  elsw1 = 0;
  |PARA para1 = 1; |SI para1 [ 12; |HACIENDO para1 = para1 + 1;
        nume1 = (para1 * 100) + 1;
        alfa1 = #0#1 % nume1;
        |SI alfa1 = "*";
            elsw1 = para1;
        |FINSI;
  |SIGUE;
|FPRC;

|PROCESO cuenta; |TIPO 6;
  |HAZ asterisco;
  |SI elsw1 ! 0;  |ACABA;  |FINSI;
  p_alfa1 = #0Campo;

  eaCuenta = #0Campo;
  salidas  = FSalida;
  |HAZ FiltraPuntos;
  #0Campo = eaCuenta;
  |PINTA #0Campo;

  |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
  alfa1 = #0#1;
  |QBF alfa1;
  alfa1 = alfa1 % 0;
  long = alfa1;
  sw   = 0;
  #0#2 = 0;  || inicializa el digito
  |SI long = dig4; |Y dig4 ! 0; sw = 1; #0#2 = 1; |FINSI;
  |SI long = dig5; |Y dig5 ! 0; sw = 1; #0#2 = 2; |FINSI;
  |SI long = dig6; |Y dig6 ! 0; sw = 1; #0#2 = 3; |FINSI;
  |SI long = dig7; |Y dig7 ! 0; sw = 1; #0#2 = 4; |FINSI;
  |SI long = dig8; |Y dig8 ! 0; sw = 1; #0#2 = 5; |FINSI;
  |SI long = dig9; |Y dig9 ! 0; sw = 1; #0#2 = 6; |FINSI;
  |SI sw = 0;|Y long ! 0;
      |MENAV "    Longitud de Cuenta Fuera de Nivel !";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO cuefin; |TIPO 0;
  |SI #0#1 = "            ";
      |MENAV "    Error de Entrada ...";
      |ERROR;
      |ACABA;
  |FINSI;
  |HAZ asterisco;
  |SI elsw1 = 0;
      |ABRE #1;
      |SELECT #1#1;
      #1#0 = #0#1;
      |LEE 001#1=;
      FEstado = FSalida;
      |SI FEstado ! 0;
          |DDEFECTO #1;
      |FINSI;
      |ATRI I;
      |PINTA 1438, #1#2;
      |ATRI 0;
      |SI FEstado = 0;|Y #1#3 = "N";
          |MENAV "    Cuenta sin pase directo ...";
          |ERROR;
          |DDEFECTO #1;
      |FINSI;
      |CIERRA #1;
      |HAZ asegura;
  |FINSI;
  |SI #0#11 = "R";
      #1#5 = "P";
  |FINSI;
|FPRC;

|PROCESO favor; |TIPO 7;
  |SI #0#11 = "R";  #0#1 = "477*";  |FINSI;
  |SI #0#11 = "S";  #0#1 = "472*";  |FINSI;
  |PINTA #0#1;
  |PTEC 816;
|FPRC;

|PROCESO asegura; |TIPO 0;
  sw_asegura = 0;
  alfa1 = #0#1 % 103;
  |SI alfa1 = "472";|Y #0#11 = "R";  sw_asegura = 1;  |FINSI;
  |SI alfa1 = "477";|Y #0#11 = "S";  sw_asegura = 1;  |FINSI;
  |SI sw_asegura = 1;
      |MENAV "    Esta Seguro ? ";
  |FINSI;
|FPRC;

|PROCESO fech_bien; |TIPO 0;
  fDFecha = fec1;
  |SI enSwSelFc = 1; fDFecha = "01.01.0000"; |FINSI;
  |SI #0Campo < fDFecha; |O #0Campo > fec2;
      |MENAV "    Fecha fuera de Periodo ...";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO libro_bien; |TIPO 0;
  |SI #0Campo ! 0;|Y FSalida ! 2;
      |SI #7#2 = "R";|Y #0#11 = "S";
          |MENAV "    ATENCION!!! Libro de I.V.A. Repercutido ...";
          |ERROR;
      |FINSI;
      |SI #7#2 = "S";|Y #0#11 = "R";
          |MENAV "    ATENCION!!! Libro de I.V.A. Soportado ...";
          |ERROR;
      |FINSI;
  |FINSI;
|FPRC;

*****************************************************************************
                       CALCULOS DE IMPRESION
*****************************************************************************

|PROCESO SumaLineas;
   |SI #cacheiva#14 = 2; |Y #caivalin#5 = "N"; |ACABA; |FINSI;  || no deduc. no incluido
   |SI #cacheiva#14 = 3; |Y #caivalin#5 = "S"; |ACABA; |FINSI;  || no deduc. no incluido

   |SI Base1 = 0; |Y Desc1 = 0; |Y Rete1 = 0;
      Base1 = Base1 + #caivalin#6;  CIva1 = CIva1 + #caivalin#8;
      CRec1 = CRec1 + #caivalin#10; Desc1 = Desc1 + #caivalin#11;
      Rete1 = Rete1 + #caivalin#22;
   |SINO;
      |SI Base2 = 0; |Y Desc2 = 0; |Y Rete2 = 0;
         Base2 = Base2 + #caivalin#6;  CIva2 = CIva2 + #caivalin#8;
         CRec2 = CRec2 + #caivalin#10; Desc2 = Desc2 + #caivalin#11;
         Rete2 = Rete2 + #caivalin#22;
      |SINO;
         |SI Base3 = 0; |Y Desc3 = 0; |Y Rete3 = 0;
            Base3 = Base3 + #caivalin#6;  CIva3 = CIva3 + #caivalin#8;
            CRec3 = CRec3 + #caivalin#10; Desc3 = Desc3 + #caivalin#11;
            Rete3 = Rete3 + #caivalin#22;
         |SINO;
            |SI Base4 = 0; |Y Desc4 = 0; |Y Rete4 = 0;
               Base4 = Base4 + #caivalin#6;  CIva4 = CIva4 + #caivalin#8;
               CRec4 = CRec4 + #caivalin#10; Desc4 = Desc4 + #caivalin#11;
               Rete4 = Rete4 + #caivalin#22;
            |SINO;
               |SI Base5 = 0; |Y Desc5 = 0; |Y Rete5 = 0;
                  Base5 = Base5 + #caivalin#6;  CIva5 = CIva5 + #caivalin#8;
                  CRec5 = CRec5 + #caivalin#10; Desc5 = Desc5 + #caivalin#11;
                  Rete5 = Rete5 + #caivalin#22;
               |FINSI;
            |FINSI;
         |FINSI;
      |FINSI;
   |FINSI;
   TotBase = TotBase + #caivalin#6;  TotCIva = TotCIva + #caivalin#8;
   TotCRec = TotCRec + #caivalin#10; TotDesc = TotDesc + #caivalin#11;
   TotRete = TotRete + #caivalin#22;
   SwHay = 1;
|FPRC;

|DEFBUCLE SumaLineas;
#caivalin#1;
;
DLibro,DRegist,01;
HLibro,HRegist,99;
;
NULL,SumaLineas;
|FIN;

|PROCESO ivas2;
  TotBase = 0; TotCIva = 0; TotCRec = 0; TotDesc = 0; TotRete = 0; SwHay = 0;
  Base1   = 0; Base2   = 0; Base3   = 0; Base4   = 0; Base5   = 0;
  CIva1   = 0; CIva2   = 0; CIva3   = 0; CIva4   = 0; CIva5   = 0;
  CRec1   = 0; CRec2   = 0; CRec3   = 0; CRec4   = 0; CRec5   = 0;
  Desc1   = 0; Desc2   = 0; Desc3   = 0; Desc4   = 0; Desc5   = 0;
  Rete1   = 0; Rete2   = 0; Rete3   = 0; Rete4   = 0; Rete5   = 0;
  DLibro  = #caivatmp#0; HLibro  = #caivatmp#0;
  DRegist = #caivatmp#1; HRegist = #caivatmp#1;
  |BUCLE SumaLineas;
  |SI SwHay = 0; |ACABA; |FINSI;

  nume1 = TotCIva;     || cuotas iva
  nume2 = TotCRec;     || cuotas recargo
  nume3 = 0;
  |SI #0#4 = 1;  nume3 = nume1 + nume2;   |FINSI;   || incluido recargo
  |SI #0#4 = 2;  nume3 = nume1;           |FINSI;   || no incluido
  |SI #0#4 = 3;  nume3 = nume2;           |FINSI;   || cta. recargo
  acui = acui + nume3;
  impi = nume3;

  libi = #caivatmp#0;
  frai = #caivatmp#3;
  desi = #caivatmp#28;
  serii = #caivatmp#2;
  sw_lin = 0;
  |SI #0#0 = "R";  |PRINT;  |FINSI;
  libi = 0;
  frai = 0;
  impi = 0;
  desi = "";
  serii = "";
|FPRC;


|PROCESO ivas1;
  sw_iva = 0;
  sw_nod = 0;
  |SI primero = 0;
      |LEE 001#caivatmp.=;
  |SINO;
      |LEE 001#caivatmp.s;
  |FINSI;
  FEstado = FSalida;
  |SI FEstado = 0;
      |SI ord = 1;
          |SI #caivatmp#4 = dia;
              |HAZ ivas2;
              sw_iva = 1;
              primero = 1;
          |FINSI;
      |SINO;
          |SI #caivatmp#8 = dia;
              |HAZ ivas2;
              sw_iva = 1;
              primero = 1;
          |FINSI;
      |FINSI;
  |FINSI;

  |SI sw_iva = 0; |Y sw_eof = 0;          || imprime apunte aunque no hay iva
      |SI #0#0 = "R";  |PRINT;  |FINSI;
  |FINSI;
|FPRC;

|PROCESO ivas0;
  acua = 0;
  acui = 0;
  |DDEFECTO #caivatmp;
  |SI ord = 1; #caivatmp#4 = dia;  |FINSI;         || fecha factura
  |SI ord = 2; #caivatmp#8 = dia;  |FINSI;         || fecha factura
  #caivatmp#0 = 0;            || libro
  #caivatmp#2 = "";           || Serie
  #caivatmp#3 = 0;            || fra. PEPE
  |LEE 000#caivatmp.];
  |SI FSalida = 0;|Y primero = 1;  |LEE 001#caivatmp.a;   |FINSI;        || se situa
|FPRC;

|PROCESO tot_fecha;
  difa = acua - acui;
  sw_lin = 1;
  |PRINT;                   || imprime total fecha
  sw_lin = 0;
|FPRC;

|PROCESO apuntes2;
  |SI #1#5 = "A";
      |SI #3#8 = "D";  acua = acua + #3#9;  |FINSI;
      |SI #3#8 = "H";  acua = acua - #3#9;  |FINSI;
  |FINSI;
  |SI #1#5 = "P";
      |SI #3#8 = "D";  acua = acua - #3#9;  |FINSI;
      |SI #3#8 = "H";  acua = acua + #3#9;  |FINSI;
  |FINSI;
|FPRC;

|PROCESO apuntes1;

  ExistLibro = 0;
  |PARA Cont = 5; |SI Cont [ 21; |HACIENDO Cont = Cont + 1;
     |SI #camaasil#13 = #cacheiva#Cont#;
        ExistLibro = 1;
        |SAL;
     |FINSI;
     |SI Cont = 10; Cont = 15; |FINSI;
  |SIGUE;

  |SI ExistLibro = 0; |ACABA; |FINSI;

  #2#0 = #3#0;
  #2#1 = #3#1;
  #2#2 = #3#2;
  |LEE 001#2=;        || sigue si la cabecera existe
  |SI FSalida ! 0;  |ACABA;  |FINSI;
  |SI fec_ant = "01.01.0001";
      fec_ant = #3#1;
      |HAZ ivas0;
  |FINSI;
  |HAZ apuntes2;
  |HAZ ivas1;
|FPRC;

|DEFBUCLE apuntes;
 #3#5;
 1, 4, 5, 8;
 00, "     ", 000000, dia, d_cuen, d_digi, des_signo;
 99, "zzzzz", 999999, dia, h_cuen, h_digi, has_signo;
 e;
 NULL, apuntes1;
|FIN;

|| -----------------------------------------------------------------------
|PROCESO tmp_iva;

  ExistLibro = 0;
  |PARA Cont = 5; |SI Cont [ 21; |HACIENDO Cont = Cont + 1;
     |SI #camaniva#0 = #cacheiva#Cont#;
        ExistLibro = 1;
        |SAL;
     |FINSI;
     |SI Cont = 10; Cont = 15; |FINSI;
  |SIGUE;

  |SI ExistLibro = 0; |ACABA; |FINSI;

  |PARA nCont = 0; |SI nCont [ 47; |HACIENDO nCont = nCont + 1;
     #caivatmp#nCont# = #camaniva#nCont#;
  |SIGUE;

  |GRABA 020#caivatmp.n;
|FPRC;

|DEFBUCLE tmp_iva;
 #5#1;
 36;
 00, 000000001, #0#11;
 99, 999999999, #0#11;
 e;
 NULL, tmp_iva;
|FIN;

|| ------------------------------------------------------------------------

|PROCESO desde_hasta;
  d_cuen = #0#1;  h_cuen = #0#1;      || desde/hasta cuenta
  d_digi = #0#2;  h_digi = #0#2;      || desde/hasta digito
  |HAZ asterisco;
  |SI elsw1 = 0;  |ACABA;  |FINSI;
  nume1 = 99 + elsw1;
  alfa1 = #0#1 % nume1;
  d_cuen = alfa1 + "            ";     d_cuen = d_cuen % 112;
  h_cuen = alfa1 + "999999999999";     h_cuen = h_cuen % 112;
  d_digi = 0;
  h_digi = 9;
|FPRC;

|PROCESO Chequea2;
  ord = #0#15;
  |IMPRE 0;
  |SI FSalida ! 0;  |ACABA;  |FINSI;
  |INFOR "cacheiva";
  |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;
  tipo = #0#1;
  |ABRE #caivatmp;
  |CIERRA #caivatmp;
  |DELINDEX #caivatmp;
  |ABRE #camaasic; |ABRE #camaniva;
  |ABRE #caivatmp;
  |SI ord = 2; |SELECT #2#caivatmp; |FINSI;  || .... Fecha Contable
  |ATRI P;  |PINTA 2138, "<TEMPORAL>";  |ATRI 0;
  |HAZ desde_hasta;
  |BUCLE tmp_iva;
  |SI #0#3 = "D";  des_signo = "D";  has_signo = "D";  |FINSI;
  |SI #0#3 = "H";  des_signo = "H";  has_signo = "H";  |FINSI;
  |SI #0#3 = "A";  des_signo = " ";  has_signo = "z";  |FINSI;
  primero = 0;
  |PARA dia = #0#12; |SI dia [ #0#13; |HACIENDO dia = dia + 1;
        |ATRI I;  |PINTA 2138, dia;  |ATRI 0;
        fec_ant = "01.01.0001";
        sw_eof = 0;
        |BUCLE apuntes;
        sw_eof = 1;
        sw_ruptura = 1;        || no imprime datos del apunte en informe
        |HAZ ruptura;
        sw_ruptura = 0;
  |SIGUE;
  |PIEINF;
  |FININF;
  |CIERRA #camaasic; |CIERRA #camaniva;
  |CIERRA #caivatmp;
  |DELINDEX #caivatmp;
  |FINIMP;
|FPRC;

|PROCESO ruptura;
  |SI fec_ant = "01.01.0001";   || si no hay apuntes
      |HAZ ivas0;               || se situa en el iva
  |FINSI;

  FEstado = 0;
  |SI ord = 1; #caivatmp#4 = dia; |FINSI;
  |SI ord = 2; #caivatmp#8 = dia; |FINSI;
  |SI ord = 1;
     |PARA; |SI FEstado = 0; |Y #caivatmp#4 [ dia;  |HACIENDO;
            |HAZ ivas1;               || sigue imprimiendo ivas
     |SIGUE;
  |SINO;
     |PARA; |SI FEstado = 0; |Y #caivatmp#8 [ dia;  |HACIENDO;
            |HAZ ivas1;               || sigue imprimiendo ivas
     |SIGUE;
  |FINSI;
  |SI acui ! 0; |O acua ! 0;
      |HAZ tot_fecha;           || totaliza
  |FINSI;
|FPRC;

||****************************************************************************
||****************************************************************************
||****************************************************************************
||****************************************************************************
||****************************************************************************
||****************************************************************************

|FICHEROS;
   catmpapu #100;
   catmpiva #101;
|FIN;

|VARIABLES;
   Cont1 = 0;
   SwLibro = 0;
   SwFecha = 0;

   First = 0;

   Comodin  = "";
   Comodin1 = "";

   Calc = 0;

   CuotaIva = 0;
   CuotaRec = 0;

   &SubTotAp = 0;
   &SubTotLi = 0;

   &FechaAnt = @;
   &SwApunte = 0;
   &SwLinea = 0;
   &nImporte = 0;
   &MarcaDif = "  ";

   DSigno = "";
   HSigno = "";
|FIN;

|PROCESO CreaTempo2;
   |SI #0#15 = 1;
      |SI #camaniva#4 < #0#12; |O #camaniva#4 > #0#13; |ACABA; |FINSI;
   |FINSI;
   |SI #0#15 = 2;
      |SI #camaniva#8 < #0#12; |O #camaniva#8 > #0#13; |ACABA; |FINSI;
   |FINSI;

   TotBase = 0; TotCIva = 0; TotCRec = 0; TotDesc = 0; TotRete = 0; SwHay = 0;
   Base1   = 0; Base2   = 0; Base3   = 0; Base4   = 0; Base5   = 0;
   CIva1   = 0; CIva2   = 0; CIva3   = 0; CIva4   = 0; CIva5   = 0;
   CRec1   = 0; CRec2   = 0; CRec3   = 0; CRec4   = 0; CRec5   = 0;
   Desc1   = 0; Desc2   = 0; Desc3   = 0; Desc4   = 0; Desc5   = 0;
   Rete1   = 0; Rete2   = 0; Rete3   = 0; Rete4   = 0; Rete5   = 0;
   DLibro  = #camaniva#0; HLibro  = #camaniva#0;
   DRegist = #camaniva#1; HRegist = #camaniva#1;
   |BUCLE SumaLineas;
   |SI SwHay = 0; |ACABA; |FINSI;

   CuotaIva = TotCIva; CuotaRec = TotCRec;

   #catmpapu#0 = #camaniva#8;
   #catmpapu#1 = #camaniva#7;
   #catmpapu#2 = #camaniva#9;
   #catmpapu#3 = 1;
   |LEE 010#catmpapu.];
   |SI FSalida = 0;|Y #catmpapu#0 = #camaniva#8;
   |Y #catmpapu#1 = #camaniva#7; |Y  #catmpapu#2 = #camaniva#9;
      |SI #0#4 = "1"; #catmpapu#11 = #catmpapu#11 + CuotaIva + CuotaRec; |FINSI;
      |SI #0#4 = "2"; #catmpapu#11 = #catmpapu#11 + CuotaIva;            |FINSI;
      |SI #0#4 = "3"; #catmpapu#11 = #catmpapu#11 + CuotaRec;            |FINSI;
      |GRABA 020#catmpapu.a;
   |SINO;
      |DDEFECTO #catmpapu;
      #catmpapu#0 = #camaniva#8;
      #catmpapu#1 = #camaniva#7;
      #catmpapu#2 = #camaniva#9;
      #catmpapu#3 = 1;
      #catmpapu#4 = #camaniva#0;
      #catmpapu#5 = #camaniva#2;
      #catmpapu#6 = #camaniva#3;
      |SI #0#4 = "1"; #catmpapu#11 = CuotaIva + CuotaRec; |FINSI;
      |SI #0#4 = "2"; #catmpapu#11 = CuotaIva;            |FINSI;
      |SI #0#4 = "3"; #catmpapu#11 = CuotaRec;            |FINSI;
      |GRABA 020#catmpapu.n;
   |FINSI;

   |DDEFECTO #catmpiva;
   #catmpiva#0 = #camaniva#4;
   #catmpiva#1 = #camaniva#0;
   #catmpiva#2 = #camaniva#2;
   #catmpiva#3 = #camaniva#3;
   #catmpiva#4 = #camaniva#7;
   #catmpiva#5 = #camaniva#8;
   #catmpiva#6 = #camaniva#9;
   #catmpiva#7 = #camaniva#28;
   |SI #0#4 = "1"; #catmpiva#8 = CuotaIva + CuotaRec; |FINSI;
   |SI #0#4 = "2"; #catmpiva#8 = CuotaIva;            |FINSI;
   |SI #0#4 = "3"; #catmpiva#8 = CuotaRec;            |FINSI;

   #catmpiva#9  = Base1;
   #catmpiva#10 = Base2;
   #catmpiva#11 = Base3;
   #catmpiva#12 = Base4;
   #catmpiva#13 = Base5;
   #catmpiva#14 = CIva1;
   #catmpiva#15 = CIva2;
   #catmpiva#16 = CIva3;
   #catmpiva#17 = CIva4;
   #catmpiva#18 = CIva5;
   #catmpiva#19 = CRec1;
   #catmpiva#20 = CRec2;
   #catmpiva#21 = CRec3;
   #catmpiva#22 = CRec4;
   #catmpiva#23 = CRec5;
   |GRABA 020#catmpiva.n;
|FPRC;

|DEFBUCLE CreaTempo2;
 #camaniva#1;
 36;
 00,000000001,#0#11;
 99,999999999,#0#11;
 ;
 NULL,CreaTempo2;
|FIN;

|PROCESO CreaTempo1;

   |SI #camaasil#13 = 0; |ACABA; |FINSI;

   NumRegist = 0;
   |SELECT #3#camaniva;
   #camaniva#0 = #camaasil#13;
   #camaniva#2 = #camaasil#15;
   #camaniva#3 = #camaasil#14;
   |LEE 000#camaniva.=;
   |SI FSalida = 0;
      NumRegist = #camaniva#1;
   |FINSI;
   |SELECT #1#camaniva;

   |SI NumRegist ! 0;
       #camaniva#0 = #camaasil#13;
       #camaniva#1 = NumRegist;
       |LEE 001#camaniva.=;
       |SI FSalida ! 0;
           #catmpiva#1 = #camaasil#13;
           #catmpiva#2 = #camaasil#15;
           #catmpiva#3 = #camaasil#14;
           |LEE 000#catmpiva.=;
           |SI FSalida ! 0;
               |DDEFECTO #catmpiva;
               #catmpiva#0 = #camaasil#1;
               #catmpiva#1 = #camaasil#13;
               #catmpiva#2 = #camaasil#15;
               #catmpiva#3 = #camaasil#14;
               #catmpiva#4 = #camaasil#0;
               #catmpiva#5 = #camaasil#1;
               #catmpiva#6 = #camaasil#2;
               |GRABA 020#catmpiva.n;
           |FINSI;
        |FINSI;
   |FINSI;

   |SI #camaasil#8 = "D";
      Comodin1 = #camaasil#16;
   |SINO;
      Comodin1 = #camaasil#10;
   |FINSI;
   |QBF Comodin1;
   |SI Calc ! 0; Comodin1 = Comodin1 % Calc; |FINSI;

   |SI Comodin1 ! Comodin; |ACABA; |FINSI;

   #camaasic#0 = #camaasil#0;
   #camaasic#1 = #camaasil#1;
   #camaasic#2 = #camaasil#2;
   |LEE 010#camaasic.=;
   |SI FSalida = 0;
      |DDEFECTO #catmpapu;
      #catmpapu#0 = #camaasil#1;
      #catmpapu#1 = #camaasil#0;
      #catmpapu#2 = #camaasil#2;
      #catmpapu#3 = #camaasil#3;
      #catmpapu#4 = #camaasil#13;
      #catmpapu#5 = #camaasil#15;
      #catmpapu#6 = #camaasil#14;
      #catmpapu#7 = #camaasil#7;
      #catmpapu#8 = #camaasil#8;
      #catmpapu#9 = #camaasil#9;
      #catmpapu#10 = #camaasil#12;
      #catmpapu#12 = #camaasil#19;
      #catmpapu#13 = #camaasil#20;
      |GRABA 020#catmpapu.n;
   |FINSI;
   |LIBERA #camaasil;
   |LIBERA #camaniva;

|FPRC;

|DEFBUCLE CreaTempo1;
  #camaasil#1;
  12,8;
  01,#0#12,000001,0001,#cacheiva#11,DSigno;
  99,#0#13,999999,9999,#cacheiva#11,HSigno;
  e;
  NULL,CreaTempo1;
|FIN;

|PROCESO Imprime;
   SwApunte = 1;
   |SI #cacheiva#11 = "R";
      |SI #catmpapu#8 = "H";
         SubTotAp = SubTotAp + #catmpapu#9;
      |SINO;
         SubTotAp = SubTotAp - #catmpapu#9;
      |FINSI;
   |SINO;
      |SI #catmpapu#8 = "D";
         SubTotAp = SubTotAp + #catmpapu#9;
      |SINO;
         SubTotAp = SubTotAp - #catmpapu#9;
      |FINSI;
   |FINSI;
   SubTotLi = SubTotLi + #catmpapu#11;
   |SI #catmpapu#12 = "B";
      |SI #catmpapu#13 = 0;
         |SI #catmpapu#9 = #catmpiva#9;  nImporte = #catmpiva#9;  |FINSI;
         |SI #catmpapu#9 = #catmpiva#10; nImporte = #catmpiva#10; |FINSI;
         |SI #catmpapu#9 = #catmpiva#11; nImporte = #catmpiva#11; |FINSI;
         |SI #catmpapu#9 = #catmpiva#12; nImporte = #catmpiva#12; |FINSI;
         |SI #catmpapu#9 = #catmpiva#13; nImporte = #catmpiva#13; |FINSI;
      |FINSI;
      |SI #catmpapu#13 = 1; nImporte = #catmpiva#9;  |FINSI;
      |SI #catmpapu#13 = 2; nImporte = #catmpiva#10; |FINSI;
      |SI #catmpapu#13 = 3; nImporte = #catmpiva#11; |FINSI;
      |SI #catmpapu#13 = 4; nImporte = #catmpiva#12; |FINSI;
      |SI #catmpapu#13 = 5; nImporte = #catmpiva#13; |FINSI;
   |FINSI;
   |SI #catmpapu#12 = "I";
      |SI #catmpapu#13 = 0;
         |SI #catmpapu#9 = #catmpiva#14; nImporte = #catmpiva#14; |FINSI;
         |SI #catmpapu#9 = #catmpiva#15; nImporte = #catmpiva#15; |FINSI;
         |SI #catmpapu#9 = #catmpiva#16; nImporte = #catmpiva#16; |FINSI;
         |SI #catmpapu#9 = #catmpiva#17; nImporte = #catmpiva#17; |FINSI;
         |SI #catmpapu#9 = #catmpiva#18; nImporte = #catmpiva#18; |FINSI;
      |FINSI;
      |SI #catmpapu#13 = 1; nImporte = #catmpiva#14; |FINSI;
      |SI #catmpapu#13 = 2; nImporte = #catmpiva#15; |FINSI;
      |SI #catmpapu#13 = 3; nImporte = #catmpiva#16; |FINSI;
      |SI #catmpapu#13 = 4; nImporte = #catmpiva#17; |FINSI;
      |SI #catmpapu#13 = 5; nImporte = #catmpiva#18; |FINSI;
   |FINSI;
   |SI #catmpapu#12 = "R";
      |SI #catmpapu#13 = 0;
         |SI #catmpapu#9 = #catmpiva#19; nImporte = #catmpiva#19; |FINSI;
         |SI #catmpapu#9 = #catmpiva#20; nImporte = #catmpiva#20; |FINSI;
         |SI #catmpapu#9 = #catmpiva#21; nImporte = #catmpiva#21; |FINSI;
         |SI #catmpapu#9 = #catmpiva#22; nImporte = #catmpiva#22; |FINSI;
         |SI #catmpapu#9 = #catmpiva#23; nImporte = #catmpiva#23; |FINSI;
      |FINSI;
      |SI #catmpapu#13 = 1; nImporte = #catmpiva#19;  |FINSI;
      |SI #catmpapu#13 = 2; nImporte = #catmpiva#20; |FINSI;
      |SI #catmpapu#13 = 3; nImporte = #catmpiva#21; |FINSI;
      |SI #catmpapu#13 = 4; nImporte = #catmpiva#22; |FINSI;
      |SI #catmpapu#13 = 5; nImporte = #catmpiva#23; |FINSI;
   |FINSI;

   Signo = 1;
   |SI #catmpapu#12 = "F"; |O #catmpapu#12 = " "; nImporte = #catmpiva#8; |FINSI;
   nImporte = nImporte * Signo;

   nImporte = nImporte ! EURODB;

   |SI nImporte ! #catmpapu#9;
      MarcaDif = "->";

   |SINO;
      MarcaDif = "  ";
   |FINSI;

   |SI #cacheiva#0 = "R"; |PRINT; |FINSI;
|FPRC;

|DEFBUCLE Imprime
#catmpiva#2;
;
#catmpapu#1,#catmpapu#0,#catmpapu#2;
#catmpapu#1,#catmpapu#0,#catmpapu#2;
;
NULL,Imprime;
|FIN;

|PROCESO Presenta;

   |ATRI I;
   |PINTA 2138, #catmpapu#0;
   |ATRI N;

   SwLibro = 0;
   |PARA Cont1 = 5; |SI Cont1 [ 21; |HACIENDO Cont1 = Cont1 + 1;
      |SI #0#Cont1# = #catmpapu#4; SwLibro = 1; |SAL; |FINSI;
      |SI Cont1 = 10; Cont1 = 15; |FINSI;
   |SIGUE;

   |SI SwLibro = 0; |O #catmpapu#4 = 0; |ACABA; |FINSI;

   |SI FechaAnt = "01.01.1900";
      FechaAnt = #catmpapu#0;
   |SINO;
      |SI FechaAnt ! #catmpapu#0;
         SwLinea = 1;
         |SI SubTotAp ! 0; |O SubTotLi ! 0;
            |PRINT;
         |FINSI;
         SwLinea = 0;
         FechaAnt = #catmpapu#0;
         SubTotAp = 0;
         SubTotLi = 0;
      |FINSI;
   |FINSI;

   ||SI #catmpapu#9 ! #catmpapu#11; First = 1; |BUCLE Imprime; |FINSI;
   |SI #catmpapu#9 ! #catmpapu#11; First = 1; |FINSI;
   |BUCLE Imprime;

|FPRC;

|DEFBUCLE Presenta;
#catmpapu#1;
;
INICIO;
FINAL;
;
NULL,Presenta;
|FIN;

|PROCESO Chequea1;

   Comodin = #0#1;
   Comodin = Comodin - "*";
   |SI FSalida ! 0;
      Calc = ((FSalida / 100) ! 0) + 99;
      Comodin = Comodin % Calc;
   |SINO;
      Calc = 0;
   |FINSI;
   |QBF Comodin;

   |DELINDEX #catmpapu;
   |DELINDEX #catmpiva;

   |ABRE #catmpapu;
   |ABRE #catmpiva;
   |ABRE #camaasic;

   |ABRE #camaniva; |ABRE #caivalin;

   |IMPRE 0;
   |SI FSalida < 0; |ACABA; |FINSI;

   |INFOR "cacheiv1";

   tipo = #0#1;
   |ATRI I;
   |PINTA 2138, "<TEMPORALES>";
   |ATRI N;

   |SI #0#3 = "D"; DSigno = "D"; HSigno = "D"; |FINSI;
   |SI #0#3 = "H"; DSigno = "H"; HSigno = "H"; |FINSI;
   |SI #0#3 = "A"; DSigno = "D"; HSigno = "H"; |FINSI;

   |BUCLE CreaTempo1;     || Creo los temporales
   |BUCLE CreaTempo2;

          || Ahora hago el bucle de los asientos

   |ATRI I;
   |PINTA 2138, "            ";
   |ATRI N;
   FechaAnt = "01.01.1900";
   |BUCLE Presenta;

   SwLinea = 1;
   |PRINT;

   |PIEINF;
   |FININF;
   |FINIMP;

   |CIERRA #caivalin; |CIERRA #camaniva;

   |CIERRA #camaasic;
   |CIERRA #catmpapu;
   |CIERRA #catmpiva;
|FPRC;


|PROCESO Chequea; |TIPO 3;
   |MENU Menu;
   Opcion = FSalida;
   |SI Opcion < 1; |O Opcion > 2; |ACABA; |FINSI;
   |SI Opcion = 1; |HAZ Chequea1; |FINSI;
   |SI Opcion = 2; |HAZ Chequea2; |FINSI;
|FPRC;

|PROGRAMA;
     |SI PARAMETRO = "";
         |MANTE #cacheiva;
     |SINO;
         |HAZ inicio;

         |MENAV "0000Chequeando cuentas tipo de IVA repercutido";
         #cacheiva#11 = "R";
         #cacheiva#15 = 1;
         #cacheiva#12 = fec1;
         #cacheiva#13 = fec2;
         #cacheiva#5  = 1;
         #cacheiva#0  = "R";
         #cacheiva#1  = "477*";
         #cacheiva#3  = "A";
         #cacheiva#4  = 1;
         #cacheiva#14 = 1;
         |HAZ Chequea1;

         |MENAV "0000Chequeando cuentas tipo de IVA soportado";
         #cacheiva#11 = "S";
         #cacheiva#15 = 2;
         #cacheiva#12 = fec1;
         #cacheiva#13 = fec2;
         #cacheiva#5  = 2;
         #cacheiva#0  = "R";
         #cacheiva#1  = "472*";
         #cacheiva#3  = "A";
         #cacheiva#4  = 1;
         #cacheiva#14 = 1;
         |HAZ Chequea1;
     |FINSI;
|FPRO;
