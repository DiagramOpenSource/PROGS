|FICHEROS;
   caconext #0;
   caconcil #1;
   camaasil #2;
   caconex1 #3;
   caconcic #4;
   cacuenta #10;

   canempre #30;
   caselem1 #998;
   caw00039 #999;
|FIN;

|VARIABLES;
    &entrada  = 1;
    sw        = 0;
    error     = 0;
    lin       = 0;
    fDFecha   = @;
    fHFecha   = @;
    aFichero  = "";

    &eSwMul   = 0;
    &espe15   = "";
    &espe16   = 0;
    nSwOpera  = 0;
    nSw       = 0;
    CodigoEmpresa  = 0;
    PeriodoEmpresa = 0;
    dirfichOri     = "";
    nSitua         = 0;
    nPerAct        = 0;

    nDDiario = 0;
    nHDiario = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE dsco08_i;

|| *************************************************************************
||                  PROCESOS PARA SACAR LA CUENTA
|| *************************************************************************
|CALCULO Tipo7C0; |TIPO 7;
   |HAZ defectos;
|FCAL;

|CALCULO defectos; |TIPO 0;
  |SI #0#6 = "N";
      #0#4  = fec1;
  |SINO;
      #0#4 = "01.01.2000";
  |FINSI;
  |PINTA #0#4;
  #0#5 = fec2; |PINTA #0#5;
|FCAL;

|CALCULO fechas; |TIPO 0;
  |SI #0#6 = "N";
      |SI #0Campo > fec2;|O #0Campo < fec1;
          |MENAV "    Fecha fuera de Periodo ...";
          |ERROR;
      |FINSI;
  |FINSI;
|FCAL;

|PROCESO cuenta; |TIPO 6;
     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;

     emCta = 1;
     |HAZ SacaNivel;
     #0#1 = enNivel;
|FPRC;


|PROCESO cuefin; |TIPO 0;
   error = 0;

   |ABRE #10;
   #10#0 = #0#0;
   |LEE 040#10=;
   |SI FSalida ! 0;
       error = 1;
       |CIERRA #10;
       |ERROR;
       |ACABA;
   |FINSI;
   |CIERRA #10;

   |SI #10#3 = "N";
       error = 1;
       |MENAV "      La cuenta no tiene pase DIRECTO !";
       |ERROR;
       |ACABA;
   |FINSI;

   #0#2 = #10#2;
   |PINTA #0#2;
|FPRC;

|| *************************************************************************
||                       PROCESOS DEL EXTRACTO POR PANTALLA
|| *************************************************************************
|PROCESO cambipag;
   error = 0;
   |PAUSA;
   |SI FSalida = 1; |O FSalida = 7;
      error = 1;
      |ERROR10;
      |ACABA;
   |FINSI;

   |PINPA #0#3;

   |PINTA #3#0;
   |PINTA #3#1;
   |PINTA #3#2;
   |PINTA #3#3;
   |PINTA #3#4;
   |PINTA #3#5;
   |PINTA #3#27;
   |PINTA #3#28;

   lin = 100;
|FPRC;

|PROCESO diferenp;
   error = 0;
   |SI #1#3  = 1;
      #3#6  = #1#2;
      #3#7  = #1#6;
      #3#8  = #1#7;
      #3#9  = #1#4;
      #3#10 = "";
      #3#11 = 0;
      #3#12 = 0;
      #3#13 = 0;
   |SINO;
      #3#6  = #1#2;
      #3#7  = 0;
      #3#8  = 0;
      #3#9  = #1#4;
      #3#10 = "";
      #3#11 = 0;
      #3#12 = #1#6;
      #3#13 = #1#7;
   |FINSI;

   #3#14 = #3#14 + #3#7;
   #3#15 = #3#15 + #3#8;
   #3#16 = #3#16 + #3#12;
   #3#17 = #3#17 + #3#13;

   Pos = lin;
   |PINDA #0#3;
   Pos = -1;

   |PINTA #3#14;
   |PINTA #3#15;
   |PINTA #3#16;
   |PINTA #3#17;

   lin = lin + 100;
   |SI lin > 800; |HAZ cambipag; |FINSI;
|FPRC;

|DEFBUCLE caconcilp;
   #1#1;
   4,8;
   #0#0, 0001, fDFecha, " ";
   #0#0, 9999, fHFecha, " ";
   e;
   NULL, diferenp;
|FIN;

/*
|PROCESO asientosp;
   error = 0;
   #caconex1#6  = #camaasil#3;
   #caconex1#7  = 0;
   #caconex1#8  = 0;
   #caconex1#9  = #camaasil#1;
   #caconex1#10 = "";
   #caconex1#11 = #camaasil#2;
   |SI #camaasil#8 = "D";
      #caconex1#12 = #camaasil#9;
      #caconex1#13 = 0;
   |SINO;
      #caconex1#13 = #camaasil#9;
      #caconex1#12 = 0;
   |FINSI;
   #caconex1#14 = #caconex1#14 + #caconex1#7;
   #caconex1#15 = #caconex1#15 + #caconex1#8;
   #caconex1#16 = #caconex1#16 + #caconex1#12;
   #caconex1#17 = #caconex1#17 + #caconex1#13;

   Pos = lin;
   |PINDA #0#3;
   Pos = -1;

   |PINTA #caconex1#14;
   |PINTA #caconex1#15;
   |PINTA #caconex1#16;
   |PINTA #caconex1#17;

   lin = lin + 100;
   |SI lin > 800; |HAZ cambipag; |FINSI;
|FPRC;


|DEFBUCLE camaasilp;
   #2#3;
   18, 0;
   #0#0, fec1, 000000, 0001, " ", nDDiario;
   #0#0, fec2, 999999, 9999, " ", nHDiario;
   e;
   NULL, asientosp;
|FIN;
*/

|PROCESO asientosp;
   error = 0;
   #3#6  = #999#3;
   #3#7  = 0;
   #3#8  = 0;
   #3#9  = #999#1;
   #3#10 = #999#10;
   #3#11 = #999#2;
   |SI #999#8 = "D";
       #3#12 = #999#9;
       #3#13 = 0;
   |SINO;
       #3#13 = #999#9;
       #3#12 = 0;
   |FINSI;
   #3#14 = #3#14 + #3#7;
   #3#15 = #3#15 + #3#8;
   #3#16 = #3#16 + #3#12;
   #3#17 = #3#17 + #3#13;

   Pos = lin;
   |PINDA #0#3;
   Pos = -1;

   |PINTA #caconex1#14;
   |PINTA #caconex1#15;
   |PINTA #caconex1#16;
   |PINTA #caconex1#17;

   lin = lin + 100;
   |SI lin > 800; |HAZ cambipag; |FINSI;
|FPRC;

|DEFBUCLE camaasilp;
   #999#2;
   ;
   #0#0, fDFecha, 000000, 0001;
   #0#0, fHFecha, 999999, 9999;
   e;
   NULL, asientosp;
|FIN;

|PROCESO pantalla;

   error = 0;
   lin   = 100;
   |PINPA #0#3;

   #3#0 = #0#0;
   #3#1 = #0#2;
   #3#2 = #0#3;
   #3#27 = #0#4;
   #3#28 = #0#5;

   |PINTA #3#0;
   |PINTA #3#1;
   |PINTA #3#2;
   |PINTA #3#3;
   |PINTA #3#4;
   |PINTA #3#5;
   |PINTA #3#27;
   |PINTA #3#28;

   |BUCLE caconcilp;
   |SI error = 0; |BUCLE camaasilp; |FINSI;

   |SI error = 0;
      |PAUSA;
      |PINPA #1#3;
      #3#18 = #3#3;
      #3#19 = #3#5;
      #3#20 = #3#14;
      #3#21 = #3#16;
      #3#22 = #3#15;
      #3#23 = #3#17;

      |SI #3#4 = "H"; #3#3 = 0 - #3#3; |FINSI;

      #3#24 = #3#18 - #3#20 + #3#22;
      #3#25 = #3#19 - #3#21 + #3#23;

      |PINDA #1#3;
      |PAUSA;
   |FINSI;
|FPRC;

|PROCESO saldo;

   |SI #camaasil#8 = "D";
       #3#5 = #3#5 + #2#9;
   |SINO;
       #3#5 = #3#5 - #2#9;
   |FINSI;

   |SI #2#18 = " ";
       #999#0 = #2#0;
       #999#1 = #2#1;
       #999#2 = #2#2;
       #999#3 = #2#3;
       #999#4 = #0#0;  || la cuenta de pantalla
       #999#5 = enPeriodo;
       #999#6 = dirfich0;
       #999#7 = enEjercicio;
       #999#8 = #2#8;
       #999#9 = #2#9;
       #999#10 = #2#7;
       |GRABA 020#999n;
   |FINSI;
|FPRC;

|DEFBUCLE camaasil;
   #2#3;
   0;
   aCtaSe, fDFecha, 000000, 0001, nDDiario;
   aCtaSe, fHFecha, 999999, 9999, nHDiario;
   e;
   NULL, saldo;
|FIN;

|PROCESO HazTodo;
   aCtaSe = #0#0;
   |SI #0#6 = "S";
       aCtaSe = eaCta08;
       |SI enEjercicio < 2008;
           aCtaSe = eaCta90;
       |FINSI;
   |FINSI;

   |ABRE #999;
   |BUCLE camaasil;
   |CIERRA #999;
|FPRC;

|PROCESO impre;|TIPO 3;
    CodigoEmpresa    = enEmpresa;
    PeriodoEmpresa   = enPeriodo;
    |DFICE dirfichOri;
    dirfich0 = dirfichOri;
    |HAZ eCam_Primero;

   aFichero = ("1z" + Usuario) % 108;
   |NOME_DAT #999 aFichero;
   |DELINDEX #999;

    nDDiario = 00;
    nHDiario = 98;
    |SI #0#6 = "S"; nDDiario = 01; nHDiario = 98; |FINSI;

   |ABRE #4;
   #4#0 = #0#0;
   #4#1 = #0#1;
   |LEE 040#4=;
   |SI FSalida ! 0; |DDEFECTO #4; |FINSI;
   |CIERRA #4;

   |DDEFECTO #3;
   #3#3 = #4#4;
   #3#4 = #4#5;

   fDFecha  = #0#4; fHFecha  = #0#5;

   eaCuenta = #0#0;
   eaCta08  = #0#0;
   eaCta90  = #0#0;
   enBEjer  = enEjercicio;
   |SI #0#6 = "S";
       |HAZ eCam_Segundo;
   |FINSI;

   nSitua = 0;
   eSwMul = 0;
   |SI #0#6 = "N";
       dirfich0 = dirfichOri;
       |HAZ HazTodo;
   |SINO;
       eSwMul = 1;
       |HAZ MultiEjercicio;
   |FINSI;

   |HAZ pantalla;

   |SI nSitua = 1;
       cod1 = CodigoEmpresa;
       cod2 = PeriodoEmpresa;
       emEmp = 1;  || Sin mensaje
       |HAZ leelo;
       dirfich0 = dirfichOri;
   |FINSI;
|FPRC;

|PROCESO MultiEmp1;
   nSitua = 1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;
   |SI #0#4 > #caselem1#5; |O #0#5 < #caselem1#4;
       |ACABA;  || Fuera de la seleccion
   |FINSI;

   |PATH_DAT #2  dirfich0;

   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmpEjer;
   #caselem1#2;
   ;
   enEmpresa, "01.01.0000";
   enEmpresa, "31.12.2999";
   ;
   NULL, MultiEmp1;
|FIN;

|PROCESO MultiEjercicio;
   espe15 = "S";

   |SUB_EJECUTA "caselemp";
   |NOME_DAT #998 eaFichsel;

   |BUCLE MultiEmpEjer;

   |DELINDEX #998;
|FPRC;
