|FICHEROS;
  otp00006 #1;
  otp00007 #2;

  caw00011 #65;

 :/modelos/def/dsmom040 #140;
 :/modelos/def/dsmom041 #141;
 :/modelos/def/dsmom042 #142;

  smom040@ #240;
  smom041@ #241;
  smom042@ #242;

  caw00040 #999;
|FIN;

|VARIABLES;
   aFichero  = "";
   nEstado   = 0;
   nNumGrupo = 0;
   aElemento = "";
   nNumero   = 0;
   nLinea    = 0;
   swPrimero = 0;

 &enAseDest = 0;
 &enEmpOrig = 0;
 &enAseOrig = 0;
 &nMaxDes = 0;
 &eaDirDestino = "";
 &eaDirOrigen  = "";

 swOpera  = 0;
 nCalC    = 0;
 nNume1   = 0;
 nNume2   = 0;
 aAlfa1   = "";
 aAlfa2   = "";
 aAlfa3   = "";
 aDeparta = "";

 &nLong1   = 0;
 &nLong2   = 0;
 &nLong3   = 0;
 &nLong4   = 0;
 &nLong5   = 0;
 &nLong6   = 0;
 &aDigDif  = "";
 &LineaErrores = 0;
 &UltNivel = 0;

 mascara = "";
 Comodin = "";
 Tipo = 0;
 Nivel = 0;
 LongAlfa = "";

 Long = 0;
 ComodNum = 0;
 ComodNum2 = 0;
 Comodin1 = "";
 Comodin2 = "";

 nSwBueno = 0;

 cfecha = @;
 importe = 0;
 afiscal = 0;
 acontab = 0;
 vfecha = @;
 sw = 0;
 any = 0;

|FIN;

|INCLUYE dscont_i;

|| /////////////////////// PROCESOS DE TRABAJO \\\\\\\\\\\\\\\\\\\\
|PROGRAMA;
 |HAZ DirAplicSt;
 |SI Haymodelo = 0;  |ACABA;  |FINSI;    ||no hay amortizaciones

 |DBASS aAlfa; |QBF aAlfa;
 aAlfa = aAlfa % -299; aAlfa2 = ""; aAlfa1 = "";
 |PARA; |SI aAlfa1 ! "\"; |Y aAlfa1 ! "/"; |HACIENDO;
    aAlfa2 = aAlfa1 + aAlfa2;
    aAlfa1 = aAlfa % -101; aAlfa = aAlfa % -299;
 |SIGUE;
 aAseActual = aAlfa2 - "ase";
 aBaseAse  = aAlfa + "/";

 |PATH_DAT #otp00006#aBaseAse#; |PATH_DAT #otp00007#aBaseAse#;

 aAlfa2 = aPModelo + "def/dsmom040.mas";
 |CARGA_DEF aAlfa2, smom040@;
 |SI FSalida = 0;
     aAlfa2 = aPModelo + "def/dsmom041.mas";
     |CARGA_DEF aAlfa2, smom041@;
     |SI FSalida = 0;
         aAlfa2 = aPModelo + "def/dsmom042.mas";
         |CARGA_DEF aAlfa2, smom042@;
         |SI FSalida = 0;
             aAlfa1 = aPModelo + "fich/";
             |PATH_DAT #140 aAlfa1; |PATH_DAT #240 aAlfa1;
             |PATH_DAT #141 aAlfa1; |PATH_DAT #241 aAlfa1;
             |PATH_DAT #142 aAlfa1; |PATH_DAT #242 aAlfa1;
             |HAZ HazTodo;
             |DESCARGA_DEF #smom042@;
         |FINSI;
         |DESCARGA_DEF #smom041@;
     |FINSI;
     |DESCARGA_DEF #smom040@;
 |FINSI;
|FPRO;

|| **************************************************************************
|| /////////////////// Pase Lineas
|PROCESO GrabaLinea;
  |DDEFECTO #dsmom042;
  #142#0 = enEmpresa;
  #142#1 = 00;
  #142#2 = aElemento;
  #142#3 = nNumero;

  aAlfa1 = "|NC";
  nCalC  = #dsmom042#aAlfa1#;
  |PARA nNume1 = 4; |SI nNume1 < nCalC; |HACIENDO nNume1 = nNume1 + 1;
        #142nNume1 = #242nNume1;
  |SIGUE;
  |SI enTP100 ! 100;
      impMoneda = #142#7;  |HAZ ElCambio100; #142#7  = impMoneda;
      impMoneda = #142#11; |HAZ ElCambio100; #142#11 = impMoneda;
      #142#13 = #142#11 - #142#7;
  |FINSI;

  |SI nEstado ! 0; |GRABA 020#142n; |FINSI;
  |SI nEstado = 0; |GRABA 020#142a; |FINSI;
  |LIBERA #142;
|FPRC;

|PROCESO LasLineas1;
  #142#0 = enEmpresa;
  #142#2 = aElemento;
  #142#3 = nNumero;
  #142#4 = #242#4;
  |LEE 101#142=;
  |SI FSalida ! 0;
      nEstado = 1;
      |HAZ GrabaLinea;
  |FINSI;
  |LIBERA #242;
|FPRC;

|DEFBUCLE LasLineas;
 #242#1004;
 ;
 #241#0, #241#2, #241#3, 000;
 #241#0, #241#2, #241#3, 999;
 e;
 NULL, LasLineas1;
|FIN;

|| /////////////////// Pase Cabeceras
|PROCESO GrabaCabecera;
  |DDEFECTO #141;
  #141#0 = enEmpresa;
  #141#1 = 00;
  #141#2 = aElemento;
  #141#3 = nNumero;

  aAlfa1 = "|NC";
  nCalC  = #dsmom041#aAlfa1#;
  |PARA nNume1 = 4; |SI nNume1 < nCalC; |HACIENDO nNume1 = nNume1 + 1;
        #141nNume1 = #241nNume1;
  |SIGUE;

  eaCuenta = #141#5; |HAZ FiltroCuenta; #141#5 = eaCuenta; #141#6 = enNivel;

  #141#32 = eaNombreEmp;

  #999#0 = #141#18;  || Grupo
  |LEE 001#999=;
  |SI FSalida = 0;
      #141#18 = #999#1;   || Cambia el Numero del Grupo
  |FINSI;

  |SI nEstado ! 0; |GRABA 020#141n; |FINSI;
  |SI nEstado = 0; |GRABA 020#141a; |FINSI;
  |LIBERA #141;

  |PINTA 2202, "Elementos: ";
  |PINTA 2213, #141#2;
  |PINTA 2222, #141#4;
|FPRC;

|PROCESO Cabeceras1;
  aElemento = #241#2;
  nNumero   = #241#3;
  |HAZ FiltroElemento;

  #141#0 = enEmpresa;
  #141#2 = aElemento;
  #141#3 = nNumero;
  |LEE 101#141=;
  |SI FSalida ! 0;
      nEstado = 1;
      |HAZ GrabaCabecera;
  |SINO;
      LineaErrores = LineaErrores + 1;
      #caw00011#0  = LineaErrores;
      #caw00011#1  = "Problemas escribiendo cabecera elementos amortizables [" + aElemento + "] (" + FSalida + ")";
      |GRABA 020#caw00011.n;
  |FINSI;
  |BUCLE LasLineas;
  |LIBERA #241;
|FPRC;

|DEFBUCLE LasCabeceras;
 #241#1003;
 ;
 enEmpOrig, "        ", 00;
 enEmpOrig, "zzzzzzzz", 99;
 e;
 NULL, Cabeceras1;
|FIN;

|| *********************************************************************
|PROCESO Busca1400;
 nNumGrupo = #140#2;
 |SI #140#3 = #240#3; |Y #140#5 = #240#5;
     swPrimero = 1;
     |ERROR10;
 |FINSI;
|FPRC;

|DEFBUCLE Busca140;
 #140#1;
 ;
 enEmpresa, 0001;
 enEmpresa, 9999;
 e;
 NULL, Busca1400;
|FIN;

|PROCESO LosGrupos1;

  eaCuenta = #240#3; |HAZ FiltroCuenta; #240#3 = eaCuenta; #240#4 = enNivel;
  eaCuenta = #240#5; |HAZ FiltroCuenta; #240#5 = eaCuenta; #240#6 = enNivel;

  nNumGrupo = #240#2;
  nEstado = 0;
  #140#0 = enEmpresa;
  #140#2 = nNumGrupo;
  |LEE 101#140=;
  |SI FSalida ! 0;
      nEstado = 1;
      |HAZ GrabaGrupo;    || No existe Ese Grupo
  |SINO;
      |SI #140#3 ! #240#3; |O #140#5 ! #240#5;
          nNumGrupo = -1
          swPrimero = 0;
          |CIERRA #140;
          |BUCLE Busca140;
          |ABRE #140;
          |SI swPrimero = 0;
              nNumGrupo = nNumGrupo + 1;
          |FINSI;
          #140#0 = enEmpresa;
          #140#2 = nNumGrupo;
          |LEE 001#140=;
          nEstado = FSalida;
          |HAZ GrabaGrupo;    || No existe Ese Grupo
      |FINSI;
      |LIBERA #140;
  |FINSI;
  |LIBERA #240;
|FPRC;

|DEFBUCLE LosGrupos;
 #240#1002;
 ;
 enEmpOrig, 0001;
 enEmpOrig, 9999;
 e;
 NULL, LosGrupos1;
|FIN;

|| -------------------------------------------------------------------
|PROCESO HazTodo;
 |ABRE #1;
 #1#0 = enEmpresa;
 |LEE 101#1=;
 |SI FSalida ! 0;
     |MENAV "    ERROR EN LA CONSOLIDACION ";
     |CIERRA #1;
     |ACABA;
 |FINSI;
 |CIERRA #1;

 |ABRE #2;
 #2#19 = enAseDest;
 #2#0 = enEmpresa;
 #2#20 = enAseOrig;
 #2#1 = enEmpOrig;
 |LEE 040#2=;
 |SI FSalida ! 0;
     |MENAV "    ERROR EN LA CONSOLIDACION ";
     |CIERRA #2;
     |ACABA;
 |FINSI;
 |CIERRA #2;

 aFichero = ("40w" + Usuario) % 108;
 |NOME_DAT #999 aFichero;
 |ABRE #999;  |CIERRA #999;  |DELINDEX #999;

 |ABRE #caw00011;

|| ....      Proceso Los Grupos
 |ABRE #999;

 |ABRE #140;
 |BUCLE LosGrupos;
 |CIERRA #140;

 |ABRE #141;
 |ABRE #142;
 |BUCLE LasCabeceras;
 |CIERRA #142;
 |CIERRA #141;
 |CIERRA #999;
 |CIERRA #caw00011;
 |DELINDEX #999;
 |HAZ ElRecalculo;
|FPRC;

|| *********************************************************************
|| --------------------------------------------- Rutinas Varias

|PROCESO FiltroCuenta;
   enNivel = 0;
   |HAZ SacaNivel;
   |SI enNivel = 0; |ACABA; |FINSI;

   |SI #1#3 = 0; |ACABA; |FINSI;

      || Tomar la primera parte segun la posicion indicada
      ComodNum  = 100 + (#1#3 - 1);
      Comodin  = eaCuenta % ComodNum;

      || Sumar el digito diferenciador
      |QBF Comodin;
      Comodin = Comodin + aDigDif;
      |QBF Comodin;
      LongAlfa = Comodin % 0;
      Long = LongAlfa;

      || Tomar la segunda parte segun la posicion indicada
      ComodNum2 = ((#1#3 + 1) * 100) + 99;
      Comodin1 = eaCuenta % ComodNum2;
      |QBF Comodin1;
      LongAlfa = Comodin1 % 0;
      Long = Long + LongAlfa;

      || Obtengo la diferencia entre la longitud del ultimo nivel y la
      ||    longitud de la cadena que ya tengo calculada para rellenar
      ||    la cuenta con ceros (si procede)

      |SI UltNivel = 1; Long = dig4 - Long; |FINSI;
      |SI UltNivel = 2; Long = dig5 - Long; |FINSI;
      |SI UltNivel = 3; Long = dig6 - Long; |FINSI;
      |SI UltNivel = 4; Long = dig7 - Long; |FINSI;
      |SI UltNivel = 5; Long = dig8 - Long; |FINSI;
      |SI UltNivel = 6; Long = dig9 - Long; |FINSI;

      Comodin2 = "0" * Long;
      |QBF Comodin2;
      Comodin = Comodin + Comodin2 + Comodin1;
      |QBF Comodin;

      eaCuenta = Comodin;
      |HAZ SacaNivel;
|FPRC;

|PROCESO GrabaGrupo;
  |DDEFECTO #140;
  #140#0 = enEmpresa;
  #140#1 = 00;
  #140#2 = nNumGrupo;
  #140#3 = #240#3;
  #140#4 = #240#4;
  #140#5 = #240#5;
  #140#6 = #240#6;
  #140#7 = #240#7;
  #140#8 = eaNombreEmp;

  |SI nEstado ! 0; |GRABA 020#140n; |FINSI;
  |SI nEstado = 0; |GRABA 020#140a; |FINSI;
  |LIBERA #140;

  |SI #240#2 ! nNumGrupo;
      |DDEFECTO #999;
      #999#0 = #240#2;
      #999#1 = nNumGrupo;
      |GRABA 020#999n;
  |FINSI;

  |PINTA 2202, "Grupos: ";
  |PINTA 2210, #140#2;
  |PINTA 2216, #140#7;
|FPRC;

|PROCESO FiltroElemento;
   |SI #1#4 = 0; |ACABA; |FINSI;  ||No Hay Cambio

   nNume1 = 100 + (#1#4 - 1);
   nNume2 = ((#1#4 + 1) * 100) + 99;
   aAlfa1    = aElemento % nNume1;
   aAlfa2    = aElemento % nNume2;
   aElemento = aAlfa1 + aDigDif + aAlfa2;
|FPRC;

|| ********************************************************************
|| /////////////////////// Proceso El Recalculo
|PROCESO L_Amortiza;
   |SI #dsmom042#6 = "C";
      importe = importe + #dsmom042#7;
      |SI sw = 0;
         cfecha = #dsmom042#9;
         sw = 1;
      |FINSI;
   |FINSI;
   |SI #dsmom042#6 = "A";
      afiscal = afiscal + #dsmom042#7;
      acontab = acontab + #dsmom042#11;
   |FINSI;
   |SI #dsmom042#6 = "V";
      vfecha = #dsmom042#9;
   |FINSI;
|FPRC;

|DEFBUCLE lineas;
   #dsmom042#1;
   ;
   #141#0,#141#2,#141#3, 000;
   #141#0,#141#2,#141#3, 999;
   e;
   NULL, L_Amortiza;
|FIN;

|PROCESO C_Amortiza;
   cfecha = "00.00.0000";
   importe = 0;
   afiscal = 0;
   acontab = 0;
   vfecha = "00.00.0000";
   sw = 0;
   |BUCLE lineas;
   |SI sw = 1;
      #141#9  = cfecha;
      #141#26 = vfecha;
      #141#10 = importe;
      #141#15 = afiscal;
      #141#16 = importe - afiscal;
      #141#21 = acontab;
      #141#22 = importe - acontab;
      |GRABA 020#141.a;
      |LIBERA #141;
   |SINO;
                             || Graba Linea de Compra
      |ABRE #dsmom042;
      #dsmom042#0 = #141#0;
      #dsmom042#1 = #141#1;
      #dsmom042#2 = #141#2;
      #dsmom042#3 = #141#3;
      #dsmom042#4 = 1;
      any = #141#27 % 704;
      #dsmom042#5 = any;
      #dsmom042#6 = "C";
      #dsmom042#7 = #141#10;
      #dsmom042#8 = "S";
      #dsmom042#9 = #141#27;
      #dsmom042#10 = "COMPRA ";
      #dsmom042#11 = 0;
      #dsmom042#12 = 1;
      |GRABA 021#dsmom042.n;
      |CIERRA #dsmom042;
   |FINSI;
|FPRC;

|DEFBUCLE ElRecalculo;
   #141#1;
   ;
   enEmpresa,"        ",00;
   enEmpresa,"zzzzzzzz",99;
   e;
   NULL, C_Amortiza;
|FIN;

|PROCESO ElRecalculo;
  |BUCLE ElRecalculo;
|FPRC;
