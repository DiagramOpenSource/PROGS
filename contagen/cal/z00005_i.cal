|FICHEROS;
   caextm01;
   caextm02;
   caexttip;

   camaasil;
   caextras;
   caextral;

   caextmw1;

   canempre@ #900;

   caextm02@ #902;
   caexttip@ #903;
|FIN;

|VARIABLES;
   nExiste = 0;
   nExisAn = 0;
   nExisTi = 0;
   nNumero = 0;
   aMas    = "";
   aDef    = "";
   aQueQuiero = "";
   aNumCampos = "";
   nNumCampoC = 0;
   nNumCampoL = 0;
   nCampo     = 0;
   aPath      = "";
   aPathFich  = "";
|FIN;

|INCLUYE dscont_i;

|| //////////////////////////////////////////////////////////////////////
|PROCESO BuscoElNum;
    nNumero = 0;
    #caextm01#13 = enEmpresa;
    #caextm01#0  = 9999;
    |LEE 041#caextm01.];
    |SI FSalida = 0;
        |LEE 041#caextm01.a;
    |SINO;
        |LEE 041#caextm01.u;
    |FINSI;
    |SI FSalida = 0; |Y #caextm01#13 = enEmpresa;
        nNumero = #caextm01#0;
    |FINSI;
|FPRC;

|| ////////// Pasa de una ejercicio al 00000
|| //////////

|PROCESO CierreExtrasL;
  |PARA nCampo = 0; |SI nCampo [ nNumCampoL; |HACIENDO nCampo = nCampo + 1;
        #caextm02#nCampo# = #caextm02@#nCampo#;
  |SIGUE;
  #caextm02#3 = #caextm01#13;
  #caextm02#0 = nNumero;
  |GRABA 020#caextm02.n

  |BORRA 020#caextm02@.a;
  |LIBERA #caextm02@;
|FPRC;

|DEFBUCLE CierreExtrasL;
  #caextm02@#1003;
  ;
  00000, #caextmw1#0, 0001;
  99999, #caextmw1#0, 9999;
  e;
  NULL, CierreExtrasL;
|FIN;

|PROCESO DeLaCarpeta;

  |SI #caextmw1#8 ! "X";

      |DDEFECTO #caextm01;
      nNumero = nNumero + 1;
      |PARA nCampo = 0; |SI nCampo [ nNumCampoC; |HACIENDO nCampo = nCampo + 1;
            #caextm01#nCampo# = #caextmw1#nCampo#;
      |SIGUE;

      #caextm01#0  = nNumero;
      #caextm01#13 = #canempre@#2;
      |SI #caextm01#12 = -1;
          #caextm01#12 = #canempre@#3;
      |FINSI;
      |GRABA 041#caextm01.n;

      |BUCLE CierreExtrasL;
  |FINSI;

  |BORRA 041#caextmw1.a;
  |LIBERA #caextmw1;
|FPRC;

|DEFBUCLE DeLaCarpeta;
   #caextmw1#1;
   ;
   00000, 0001;
   99999, 9999;
   be;
   NULL, DeLaCarpeta;
|FIN;

|PROCESO PasarExtras;

    |DBASE aMas;
    aDef  = aMas + "def/caextm02";
    |CARGA_DEF aDef, caextm02@;
    |SI FSalida ! 0;
        |MENAV "    Error Cargado Fichero de Contagen. Proceso Interrumpido";
        |ACABA;
    |FINSI;
    |PATH_DAT #902 aPathFich;

    |ABRE #caextm02;
    |ABRE #caextm01;
    |HAZ BuscoElNum;

    |BUCLE DeLaCarpeta;
    |CIERRA #caextm01;
    |CIERRA #caextm02;

    |DESCARGA_DEF #caextm02@;
|FPRC;

|| //////////////////////////////////
|PROCESO DeLaCarpetaTipos;
  #caexttip#0 = #caexttip@#0;
  |LEE 041#caexttip.=;
  |SI FSalida ! 0;
      #caexttip#0 = #caexttip@#0;
      #caexttip#1 = #caexttip@#1;
      #caexttip#2 = #caexttip@#2;
      #caexttip#3 = #caexttip@#3;
      |GRABA 020#caexttip.n;
  |FINSI;

  |BORRA 041#caexttip@.a;
  |LIBERA #caexttip@;
|FPRC;

|DEFBUCLE DeLaCarpetaTipos;
   #caexttip@#1001;
   ;
   001;
   999;
   e;
   NULL, DeLaCarpetaTipos;
|FIN;

|PROCESO PasarExtTipos;

    |DBASE aMas;
    aDef  = aMas + "def/caexttip";
    |CARGA_DEF aDef, caexttip@;
    |SI FSalida ! 0;
        |ACABA;
    |FINSI;
    |PATH_DAT #903 aPathFich;

    |ABRE #caexttip;
    |BUCLE DeLaCarpetaTipos;
    |CIERRA #caexttip;

    |DESCARGA_DEF #caexttip@;
|FPRC;

|| //////////////////////////////////
|| ///// Pasa las Anotaciones Extracontables anteriores (caextras)
||       al nuevo formato.
|PROCESO MiraCamaasil;
       #camaasil#0 = #caextm01#3;
       #camaasil#1 = #caextm01#4;
       #camaasil#2 = #caextm01#5;
       #camaasil#3 = 1;
       |LEE 041#camaasil.];
       |SI FSalida = 0; |Y #camaasil#0 = #caextm01#3;
                          |Y #camaasil#1 = #caextm01#4;
                            |Y #camaasil#2 = #caextm01#5;
           #caextm01#10 = #camaasil#6;
           #caextm01#11 = #camaasil#7;
       |FINSI;
|FPRC;

|PROCESO PasoExtrasL;
  |DDEFECTO #caextm02;
  #caextm02#0 = nNumero;
  #caextm02#1 = #caextral#3;
  #caextm02#2 = #caextral#4;
  #caextm02#3 = enEmpresa;
  |GRABA 040#caextm02.n;

  |BORRA 020#caextral.a;
  |LIBERA #caextral;
|FPRC;

|DEFBUCLE PasoExtrasL;
  #caextral#1;
  ;
  #caextras#0, #caextras#1, #caextras#2, 0001;
  #caextras#0, #caextras#1, #caextras#2, 9999;
  be;
  NULL, PasoExtrasL;
|FIN;

|PROCESO PasoExtras;
  nNumero = nNumero + 1;
  |DDEFECTO #caextm01;
  #caextm01#0 = nNumero;
  #caextm01#1 = #caextras#3;
  #caextm01#2 = 3;
  #caextm01#3 = #caextras#0;
  #caextm01#4 = #caextras#1;
  #caextm01#5 = #caextras#2;
  |HAZ MiraCamaasil;
  #caextm01#12 = enPeriodo;
  #caextm01#13 = enEmpresa;
  |GRABA 040#caextm01.n;

  |BUCLE PasoExtrasL;

  |BORRA 020#caextras.a;
  |LIBERA #caextras;
|FPRC;

|DEFBUCLE PasoExtras;
  #caextras#1;
  ;
  00, "01.01.0000", 000001;
  99, "31.12.2099", 999999;
  be;
  NULL, PasoExtras;
|FIN;

|PROCESO PasarCaExtras;
    nNumero = 0;
    |ABRE #caextm01;
    |HAZ BuscoElNum;

    |ABRE #camaasil;
    |ABRE #caextm02;
    |BUCLE PasoExtras;
    |CIERRA #caextm02;
    |CIERRA #camaasil;

    |CIERRA #caextm01;
|FPRC;

|PROCESO LosPeriodos;

   aPathFich = ("000000" + #canempre@#0) % -106;
   aPathFich = aPath + aPathFich + "/";

   |PATH_DAT #caextras#aPathFich#;
   |PATH_DAT #caextral#aPathFich#;
   |PATH_DAT #caextmw1#aPathFich#;

   nExiste = 0;
   nExisAn = 0;
   |ABRE #caextras;
   |DDEFECTO #caextras;
   |LEE 041#caextras.p;
   |SI FSalida = 0;
       nExiste = 1;
   |FINSI;
   |CIERRA #caextras;

   |ABRE #caextmw1;
   |DDEFECTO #caextmw1;
   |LEE 041#caextmw1.p;
   |SI FSalida = 0;
       nExisAn = 1;
   |FINSI;
   |CIERRA #caextmw1;

   |SI nExiste = 1;
       |HAZ PasarCaExtras;
   |FINSI;

   |SI nExisAn = 1;
       |HAZ PasarExtras;
   |FINSI;

   |SI nExisAn = 1; |O nExiste = 1;
       |HAZ PasarExtTipos;
   |FINSI;
|FPRC;

|DEFBUCLE LosPeriodos;
   #canempre@#4002;
   ;
   enEmpresa, 00;
   enEmpresa, 99;
   ;
   NULL, LosPeriodos;
|FIN;

|PROCESO eVerNuevaAnotacion;

    aQueQuiero = "|NC";
    aNumCampos = #caextm02#aQueQuiero#;
    nNumCampoL = aNumCampos;
    nNumCampoL = nNumCampoL - 1;
    aNumCampos = #caextm01#aQueQuiero#;
    nNumCampoC = aNumCampos;
    nNumCampoC = nNumCampoC - 1;

    |DFICB aPath;

    |DBASE aMas;
    aDef  = aMas + "def/canempre";
    |CARGA_DEF aDef, canempre@;
    |SI FSalida = 0;
        |BUCLE LosPeriodos;
    |FINSI;
    |DESCARGA_DEF #canempre@;
|FPRC;

|| //////////////////////////////////////////////////////////////////////
|| //////////////////////////////////////////////////////////////////////
|PROCESO eBorraTex;
   |BORRA 041#caextm02.a;
   |LIBERA #caextm02;
|FPRC;

|DEFBUCLE eBorraTex;
   #caextm02#1;
   ;
   #caextm01#13, #caextm01#0, 0001;
   #caextm01#13, #caextm01#0, 9999;
   be;
   NULL, eBorraTex;
|FIN;

|PROCESO eBorraEx;
   |BUCLE eBorraTex;
   |BORRA 020#caextm01.a;
   |LIBERA #caextm01;
|FPRC;

|DEFBUCLE eBorraEx1;
   #caextm01#1;
   2;
   enEmpresa, 0001, enTipoEx;
   enEmpresa, 9999, enTipoEx;
   be;
   NULL, eBorraEx;
|FIN;

|DEFBUCLE eBorraEx2;
   #caextm01#3;
   ;
   enEmpresa, enTipoEx, eaLaCtaEx, "01.01.0000", 0001;
   enEmpresa, enTipoEx, eaLaCtaEx, "31.12.9999", 9999;
   be;
   NULL, eBorraEx;
|FIN;

|DEFBUCLE eBorraEx3;
   #caextm01#2;
   12;
   enEmpresa, enTipoEx, efFechaEx, enDiarioEx, enDocumEx, "01.01.0000", 0001, enPeriodo;
   enEmpresa, enTipoEx, efFechaEx, enDiarioEx, enDocumEx, "31.12.2099", 9999, enPeriodo;
   be;
   NULL, eBorraEx;
|FIN;

|| //////////////// Borra Anotaciones vinculadas a Asto o Cuenta dada de baja
|PROCESO eBorraExtras;
  |SI enTipoEx = 1; |BUCLE eBorraEx1; |FINSI;
  |SI enTipoEx = 2; |BUCLE eBorraEx2; |FINSI;
  |SI enTipoEx = 3; |BUCLE eBorraEx3; |FINSI;
|FPRC;
|| /////////////////////////////////////////////////////////////////////////
