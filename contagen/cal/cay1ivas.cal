|FICHEROS;
   cay1ivas #0;
   cainliva #1;
   cacuenta #3;
   calibros #10;
   caw00017 #17;
   caivaasi #200;

   canempre #30;
   caselem1 #998;
|FIN;

|VARIABLES;
    &entrada = 1;
    aAlfa1   = "";
    nSw      = 0;
    nLibro   = 0;
    nSwPie   = 0;
    informe  = "";
    inform1  = "cay1ivas";
    inform2  = "cay1ivap";
    &nSwLin  = 0;
    fechaW17 = @;
    nDiario = 0;
    nDocum  = 0;

    &eaFactu = "";
    &nPBase = 0;
    &nPIVA  = 0;
    &nPRec  = 0;
    &nPTot  = 0;
    aSerie  = "";
    nFactu  = 0;
    nSwPr   = 0;
    &nLineas = 0;
    fDFecha = @;
    aDIntra = "";
    aHIntra = "";
|FIN;

|INCLUYE dscont_i;

|| ***************** Calculos de Pantalla

|CALCULO defectos; |TIPO 7;
  fDFecha = fec1;
  |SI enSwSelFc = 1; fDFecha = "01.01.2000"; |FINSI;
  #0#4 = fDFecha; |PINTA #0#4;
  #0#5 = fec2;    |PINTA #0#5;

  |ABRE #caivaasi;
  |LEE 000#caivaasi.p;
  |SI FSalida ! 0;
      |DDEFECTO #caivaasi;
      |GRABA 040#caivaasi.n;
  |FINSI;
  |CIERRA #caivaasi;
  aAlfa1 = #caivaasi#44;
  |C_M #0#7,1,aAlfa1;
  |C_M #0#8,1,aAlfa1;
|FCAL;

|CALCULO fecha; |TIPO 0;
  |SI #0Campo < fDFecha; |O #0Campo > fec2;
      |MENAV "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
      |ERROR;
      |ACABA;
  |FINSI;
  |SI Campo = 5; |Y #0#5 < #0#4;
      |MENAV "    FECHA ERRONEA ";
      |ERROR;
  |FINSI;
|FCAL;


|| *************************************************************************
|| **************************   MEOLLO   ***********************************
|| *************************************************************************
|PROCESO Recojo17;
  |ABRE #3;
  |DDEFECTO #3;
  #3#0 = #17#4;
  |LEE 001#3=;
       eaNomCta= #3#2;
  |CIERRA #3;
  |SI nSwLin = 0;
      nSwLin = 1;
  |SINO;
      nSwLin = 2;
  |FINSI;
  |PRINT;
|FPRC;

|DEFBUCLE LeeW17;
 #17#1;
 ;
 nDiario, fechaW17, nDocum, 0001;
 nDiario, fechaW17, nDocum, 9999;
 ;
 NULL, Recojo17;
|FIN;

|PROCESO leecab;
 |ABRE #10;
 |DDEFECTO #10;
 #10#0 = nLibro;
 |LEE 001#10=;
 |CIERRA #10;
 |HAZ PongoaCero;
 nSwPr   = 0;
|FPRC;

|PROCESO PongoaCero;
    nPBase = 0;
    nPIVA  = 0;
    nPRec  = 0;
    nPTot  = 0;
    aSerie  = "";
    nFactu  = 0;
    nLineas = 0;
|FPRC;

|PROCESO ElTotal;

  |SI #0#9 = "N";  |ACABA; |FINSI;
  |SI nLineas [ 1; |ACABA; |FINSI;

  nSwLin = 3;
  eaFactu = nFactu; eaFactu = ("000000" + eaFactu) % -106
  aAlfa1 = aSerie; |QBF aAlfa1;
  |SI aAlfa1 ! "";
      eaFactu = aAlfa1 + "/" + eaFactu;
  |FINSI;
  |PRINT;
  nSwLin = 0;
|FPRC;

|PROCESO imprimir;
 |SI nSw = 0;
     nLibro = #1#0;
     |HAZ leecab;
     nSw = 1;
 |FINSI;

 |SI nLibro ! #1#0;
     |HAZ ElTotal;
     nSwPie = 1;
     |PIEINF;
     nLibro = #1#0;
     |HAZ leecab;
 |FINSI;

 |SI nSwPr = 0;
     |HAZ PongoaCero;
     nSwPr   = 1;
     aSerie  = #1#26;
     nFactu  = #1#2;
 |FINSI;

 |SI aSerie ! #1#26; |O nFactu ! #1#2;
     |HAZ ElTotal;
     |HAZ PongoaCero;
     aSerie  = #1#26;
     nFactu  = #1#2;
 |FINSI;

 nSwPie = 0;
 nSwLin = 0;
 |PRINT;
 nDiario = #1#0;
 nDocum  = #1#1;
 |BUCLE LeeW17;

 nLineas = nLineas + 1;
 nPBase = nPBase + #1#12;
 nPIVA  = nPIVA  + #1#14;
 nPRec  = nPRec  + #1#16;
 nPTot  = nPTot  + #1#29;
|FPRC;

|DEFBUCLE cay1ivac0;
 #1#2;
 4,54;
 #0#0, #0#7, #0#2, 01, #0#4, aDIntra;
 #0#1, #0#8, #0#3, 99, #0#5, aHIntra;
 e;
 NULL,imprimir;
|FIN;

|PROCESO HazTodo;

  nSwPie = 0;
  nSw    = 0;
  nSwPr  = 0;
  |BUCLE cay1ivac0;
  |SI nSwPie = 0; |Y nSw = 1;
      |HAZ ElTotal;
      |PIEINF;
  |FINSI;
|FPRC;


|| ********************************************************************
|CALCULO impre; |TIPO 3;


  |SI #0#10 = "T";
      aDIntra = "N";
      aHIntra = "S";
  |SINO;
      aDIntra = #0#10;
      aHIntra = #0#10;
  |FINSI;

  |IMPRE 0;
  |SI FSalida ! 0;  |ACABA;  |FINSI;

  informe = inform1;
  |SI #0#9 = "S";
      informe = inform2;
  |FINSI;
  |INFOR informe;
  |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

  fechaW17 = "01.01.0000";

  |SI #48#27 = "S";
      |HAZ MultiEmpresa;
  |SINO;
      |DFICE dirfich0; |QBF dirfich0;
      |HAZ HazTodo;
  |FINSI;

  |FININF;
  |FINIMP;
|FCAL;

|| **********************************************************************
|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;
   |PATH_DAT #1 dirfich0;
   |PATH_DAT #3 dirfich0;
   |PATH_DAT #10 dirfich0;
   |PATH_DAT #17 dirfich0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 00001;
 99999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;
