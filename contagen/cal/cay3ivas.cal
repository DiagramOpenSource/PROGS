|FICHEROS;
   cay3ivas #0;
   caivalin #1;
   camaniva #2;
   calibros #3;
   caivaasi #200;
   :/modelos/def/dsmom030 #300;
   caconimp #8;

   canempre #30;
   caselem1 #998;
   caselem0 #997;
   caw00031 #901;
   caw00032 #902;
   caw00045 #903;
   cawdatos #900;
|FIN;

|VARIABLES;
  nSwSel    = 0;
  nCampo    = 0;
  aAlfa1    = "";
  aFichero  = "";
  swAlgun   = 0;
  informe   = "";
  inform1   = "cay3ivas";
  inform2   = "cay3ivan";
  infor11   = "cay3ivds";
  infor12   = "cay3ivdi";
  inform3   = "cay3ivay";
  inform4   = "cay3ivax";
  nDdepar   = 0;
  nHdepar   = 0;
  aParam    = "";
  aParam1   = "";
        nNo = 0;
  nResum    = 0;
  &nSwLinea = 0;
  nDCIrpf   = 0;
  nHCIrpf   = 0;
  nSiIrpf   = 0;

  aMulDep    = "";
  nEmpMulti  = 0;
  &eSwMul    = 0;
  &espe15    = "";
  &espe16    = 0;
  fDFecha    = @;
  &eaParam2  = "";
|FIN;

|INCLUYE dscont_i;
|INCLUYE masivo_i;

|| *****************************************************************
|CALCULO defectos; |TIPO 7;
  |C_M #0#16,1,"N";
  |C_V #0#16,1,"N";
  |SI nResum = 1;
      |ATRI R;
      |PINTA 0505, "Informe de Comprobacion de Facturas por Conceptos Contables";
      |ATRI 0;
      #0#14 = "COMPROBACION DE FACTURAS POR CONCEPTOS CONTABLES"; |PINTA #0#14;
      |PINTA 2225, "Resumen Total Concepto:   ";
      |C_V #0#16,1,"S";
      |C_M #0#16,1,"S";
      |PINTA #0#16;
      #0#35 = "N"; |PINTA #0#35;
  |FINSI;

  |ABRE #200;
  |LEE 001#200p;
  |SI FSalida ! 0;
      |DDEFECTO #200;
  |FINSI;
  |CIERRA #200;

  aAlfa1 = #200#44;
  |C_M #0#3,1,aAlfa1;
  |C_M #0#4,1,aAlfa1;
  |SI aParam ! "";
      |SI nNo = 0;
          |PTEC 802;
          |PTEC 802;
          nNo = 1;
      |FINSI;
  |FINSI;
  |HAZ DatosEmp;

  |SI enSwAna = 1; |Y aParam = "";
      |C_V #0#36,1,"S"; |C_M #0#36,1,"S";
      |C_V #0#37,1,"S"; |C_M #0#37,1,"S";
      |PINTA 1404, "³ De la Cta Analitica:             ³ A la Cta Analitica:             ³";
      |PINTA 1539, "³";
      |PINTA 1639, "³";
      |PINTA 1739, "Á";
      || NTA 1504, "ÃÄDe la Fecha .......:ÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄA la Fecha .......:ÄÄÄÄÄÄÄÄÄÄÄÄÄ´";
      |PINTA #0#36;
      |PINTA #0#37;
  |FINSI;
|FCAL;

|CALCULO defectos0; |TIPO 0;
  fDFecha = fec1;
  |SI enSwSelFc = 1; |Y #0#0 ! "N"; fDFecha = "01.01.2000"; |FINSI;
  |SI #0#34 = "N";
      #0#9  = fDFecha;
  |SINO;
      #0#9  = "01.01.2000";
  |FINSI;
  |PINTA #0#9;
  #0#10 = fec2; |PINTA #0#10;
|FCAL;

|CALCULO fechad; |TIPO 0;
  |SI #0#34 = "N";
      fDFecha = fec1;
      |SI enSwSelFc = 1; |Y #0#0 ! "N"; fDFecha = "01.01.2000"; |FINSI;
      |SI #0#9 < fDFecha; |O #0#9 > fec2;
          |MENAV "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
          |ERROR;
      |FINSI;
  |FINSI;
|FCAL;

|CALCULO fechah; |TIPO 0;
  |SI #0#34 = "N";
      fDFecha = fec1;
      |SI enSwSelFc = 1;  |Y #0#0 ! "N"; fDFecha = "01.01.2000"; |FINSI;
      |SI #0#10 < fDFecha; |O #0#10 > fec2;
           |MENAV "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
           |ERROR;
           |ACABA;
      |FINSI;
   |FINSI;
   |SI #0#10 < #0#9;
       |MENAV "    Fecha Erronea ! ";
   |FINSI;
|FCAL;

|CALCULO nodepar; |TIPO 7;
   |CAMPO_MODIFICA #0#7, 1, eaDepar;
   |CAMPO_MODIFICA #0#8, 1, eaDepar;
|FCAL;

|CALCULO nosubde; |TIPO 7;
   |CAMPO_MODIFICA #0#38, 1, eaSubde;
   |CAMPO_MODIFICA #0#39, 1, eaSubde;
|FCAL;

|CALCULO Eltope7; |TIPO 7;
  aAlfa1 = "    00 = No liquidados, 1-12 Mes de Liquidacion ";
  |SI #200#46 = "T";
      |SI enEjercicio [ 2009;
          aAlfa1 = "    00 = No liquidados, 1-4 Trimestre ";
          |SI #0Campo > 4; #0Campo = 4; |PINTA #0Campo; |FINSI;
      |FINSI;
  |FINSI;
  |MENSA aAlfa1;
|FCAL;

|CALCULO Eltope; |TIPO 0;

  |SI #200#46 = "T";
      |SI enEjercicio [ 2009; |Y  #0Campo > 4;
          |ERROR;
          |ACABA;
      |FINSI;
  |FINSI;

  |SI Campo = 12; |Y #0#12 < #0#11;
      |ERROR;
  |FINSI;
|FCAL;

|PROCESO DatosEmp; |TIPO 7;
 |ABRE #8;
 |DDEFECTO #8;
 #8#0 = 1;
 |LEE 011#8=;
 |CIERRA #8;
 #0#15 = #8#7; |PINTA #0#15;
 aAlfa1 = #8#8; |C_M #0#15,1,aAlfa1;
|FPRC;

|CALCULO DesHasC; |TIPO 0;
     |SI #0#17 = "N";
         #0#18 = 001;  |PINTA #0#18;  |C_M #0#18, 1, "N";
         #0#19 = 999;  |PINTA #0#19;  |C_M #0#19, 1, "N";
         |PARA nCampo = 20;  |SI nCampo [ 31;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |C_M #0#18, 1, "S";
         |C_M #0#19, 1, "S";
         |PARA nCampo = 20;  |SI nCampo [ 31;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasCon; |TIPO 0;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 31;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 31;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO Antes35; |TIPO 7;
     |C_M #0#35,1,"S";
     |SI nResum = 1; |Y #0#16 = "N";
         |C_M #0#35,1,"N";
     |FINSI;
|FCAL;

|| ******************************************************************

|CALCULO ElTipo3; |TIPO 3;
   aFichero = ("31cw" + Usuario) % 108;
   |NOME_DAT #901 aFichero; |DELINDEX #901;
   aFichero = ("32cw" + Usuario) % 108;
   |NOME_DAT #902 aFichero; |DELINDEX #902;
   aFichero = ("4c" + Usuario) % 108;
   |NOME_DAT #903 aFichero; |DELINDEX #903;

   |HAZ DirAplicSt;
   |SI Haybasica = 1; |Y #0#8 = "zz"; #0#8 = "99"; |FINSI;

   nDCIrpf = #0#32;
   nHCIrpf = #0#33;

   |SI aParam1 = "";
       |IMPRE 0;
       |SI FSalida < 0; |ACABA; |FINSI;
   |FINSI;

   enDatos = 0; |SI #0#15 = "S"; enDatos = 1; |FINSI;

   |SI #0#17 = "N";
       #0#18 = 001;
       #0#19 = 999;
   |FINSI;

   |SI aParam = "";
       |SI #0#34 = "N";
           |SI #48#27 = "S"; |Y enSwMvo = 0;
               |HAZ MultiEmpresa;
           |SINO;
               |DFICE dirfich0; |QBF dirfich0;
               |HAZ HazTodo;
           |FINSI;
       |SINO;
           eSwMul = 1;
           |SI #48#27 = "S";  eSwMul = 2; |FINSI;
           |HAZ MultiEjercicio;
       |FINSI;
   |SINO;
       |HAZ HazTodo;
   |FINSI;

   |SI aParam1 = ""; |FINIMP; |FINSI;
   |DELINDEX #901;
   |DELINDEX #902;
   |DELINDEX #903;
|FCAL;

|| ------------------------------------------------------------------------
|PROCESO caw00045;
  |PRINT;
  nSwLinea = 1;
|FPRC;

|DEFBUCLE caw00045;
   #903#1;
   ;
   #901#5, #901#0,   0,   0, #901#9;
   #901#5, #901#0, 999, 999, #901#9;
   ;
   NULL, caw00045;
|FIN;

|PROCESO ElCon;
 |ABRE #300;
 #300#0 = #901#0;
 |LEE 001#300=;
 |SI FSalida ! 0;
     |DDEFECTO #300;
 |FINSI;
 #901#6 = #300#1;
 #901#7 = #300#2;
 |CIERRA #300;
|FPRC;

|PROCESO caw00032;
  |PRINT;
  nSwLinea = 1;
|FPRC;

|DEFBUCLE caw00032;
 #902#2;
 ;
 #901#5, #901#0, "01.01.0000", "     ", 000000;
 #901#5, #901#0, "31.12.2999", "zzzzz", 999999;
 ;
 NULL, caw00032;
|FIN;

|PROCESO caw00031;
 nSwLinea = 0;
 |SI nResum = 0;
    |SI #0#35 = "S"; |BUCLE caw00045; nSwLinea = 2; |FINSI;
    |PRINT;
 |SINO;
    |BUCLE caw00032;
    |SI nSwLinea = 1;
        nSwLinea = 2;
        |PRINT;
    |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE caw00031;
 #901#1;
 ;
 #0#0, 001, "       ";
 #0#0, 999, "zzzzzzz";
 ;
 NULL, caw00031;
|FIN;

|| ---------------------------
|PROCESO LinLibro0;
  aAlfa1 = #1#30; |QBF aAlfa1;
  |SI aAlfa1 = "";
      #1#30 = #2#10;
      aAlfa1 = #1#31; |QBF aAlfa1; |SI aAlfa1 = ""; #1#31 = #2#11; |FINSI;
  |FINSI;
  |SI aMulDep = "S";
     |SI Haybasica = 1;
         nDepar  = #1#30; |SI nDepar = 0;  nDepar = 1;   |FINSI;
         nDdepar = #0#7;
         nHdepar = #0#8;
         |SI nDepar < nDdepar; |O nDepar > nHdepar; |ACABA; |FINSI;
     |SINO;
         |SI #1#30 < #0#7;  |O #1#30 > #0#8;  |ACABA;  |FINSI;
     |FINSI;
     |SI #1#31 < #0#38;  |O #1#31 > #0#39;  |ACABA;  |FINSI;
 |FINSI;

 |SI #0#17 = "N";
     nSwSel = 0;
     |PARA nCampo = 20; |SI nCampo [ 31; |HACIENDO nCampo = nCampo + 1;
           |SI #0nCampo ! 0;
               |SI #0nCampo = #1#3;
                   nSwSel = 1;
                   |SAL;
               |FINSI;
           |FINSI;
     |SIGUE;
     |SI nSwSel = 0; |ACABA; |FINSI;
 |FINSI;

 |SI #1#18 < nDCIrpf; |O #1#18 > nHCIrpf;
     |ACABA;
 |FINSI;

 |SI enSwAna = 1;
     |SI #1#28 < #0#36; |O #1#28 > #0#37;
         |ACABA;
     |FINSI;
 |FINSI;

|| ... Graba Resumen  por Conceptos
 #901#0 = #1#3;
 #901#5 = #2#36;
 #901#9 = "";
 |LEE 101#901=;
 |SI FSalida ! 0;
     |DDEFECTO #901;
     #901#0 = #1#3;
     #901#5 = #2#36;
     |HAZ ElCon;
     #901#9 = "";
     |GRABA 020#901b;
 |FINSI;
 #901#1 = #901#1 + #1#6;
 #901#2 = #901#2 + #1#8;
 #901#3 = #901#3 + #1#10;
 #901#4 = #901#4 + (#1#6 + #1#8 + #1#10 - #1#22);
 #901#8 = #901#8 + #1#22;
 #901#5 = #2#36;
 |GRABA 020#901a;
 |LIBERA #901;
 swAlgun = 1;
 |SI #1#22 ! 0; nSiIrpf = 1; |FINSI;

 |SI nResum = 1;
     |ABRE #902;
     #902#0  = #2#36;  || S/R/N
     #902#1  = #1#3;   || Concepto Linea
     #902#2  = #2#0;   || Libro
     #902#3  = #2#1;   || Registro
     #902#19 = enPeriodo;
     |LEE 101#902=;
     |SI FSalida ! 0;
         |DDEFECTO #902;
         #902#0  = #2#36;  || S/R/N
         #902#1  = #1#3;   || Concepto Linea
         #902#2  = #2#0;   || Libro
         #902#3  = #2#1;   || Registro
         #902#4  = #2#2;   || Serie
         #902#5  = #2#3;   || Num.Factura
         #902#6  = #2#4;   || Fecha
         #902#7  = #2#10;  || Departamento
         #902#8  = #2#11;  || Subdepartamento
         #902#9  = #2#12;  || Cliente/Prov.
         #902#10 = #2#13;  || Nombre Cliente/Prov.
         #902#11 = #2#14;  || Cif Cliente/Prov.
         #902#18 = #2#22;  || Referencia
         #902#19 = enPeriodo;
         |GRABA 020#902b;
     |FINSI;
     #902#12 = #902#12 + #1#6;    || Base
     #902#13 = #902#13 + #1#8;    || IVA
     #902#14 = #902#14 + #1#10;   || Rec
     #902#15 = #902#15 + #1#22;   || IRPF
     #902#16 = #902#16 + #1#11;   || Descuento
     #902#17 = #902#12 + #902#13 + #902#14 - #902#15;
     |GRABA 020#902a;
     |LIBERA #902;
     |CIERRA #902;
 |FINSI;

 |SI #0#35 = "S";
     #903#0 = #1#3;
     #903#5 = #2#36;
     #903#6 = #1#7;
     #903#7 = #1#9;
     |LEE 101#903=;
     |SI FSalida ! 0;
         |DDEFECTO #903;
         #903#0 = #1#3;
         #903#5 = #2#36;
         #903#6 = #1#7;
         #903#7 = #1#9;
         |GRABA 020#903b;
     |FINSI;
     #903#1 = #903#1 + #1#6;
     #903#2 = #903#2 + #1#8;
     #903#3 = #903#3 + #1#10;
     #903#4 = #903#4 + (#1#6 + #1#8 + #1#10 - #1#22);
     #903#8 = #903#8 + #1#22;
     #903#5 = #2#36;
     |GRABA 020#903a;
     |LIBERA #903;
 |FINSI;
|FPRC;

|DEFBUCLE LinLibro;
 #1#1;
 3;
 #2#0, #2#1, 01, #0#18;
 #2#0, #2#1, 99, #0#19;
 ;
 NULL, LinLibro0;
|FIN;

|PROCESO Facturas;
 |SI aMulDep ! "S";
     |SI Haybasica = 1;
         nDepar  = #2#10; |SI nDepar = 0;  nDepar = 1;   |FINSI;
         nDdepar = #0#7;
         nHdepar = #0#8;
         |SI nDepar < nDdepar; |O nDepar > nHdepar; |ACABA; |FINSI;
     |SINO;
         |SI #2#10 < #0#7;  |O #2#10 > #0#8;  |ACABA;  |FINSI;
     |FINSI;
     |SI #2#11 < #0#38;  |O #2#11 > #0#39;  |ACABA;  |FINSI;
 |FINSI;

 |BUCLE LinLibro;
|FPRC;

|DEFBUCLE Facturas;
 #2#3;
 4, 36, 40;
 #0#1, #0#3, #0#5, #0#9,  #0#0, #0#11;
 #0#2, #0#4, #0#6, #0#10, #0#0, #0#12;
 ;
 NULL, Facturas;
|FIN;

|| ************************************************
|PROCESO EmpiezaEmpresa;
   |ABRE #200;
   |DDEFECTO #200;
   |LEE 000#200p;
   |CIERRA #200;
   aMulDep = #200#132;
   |SI Haybasica = 0;  || si selecciona todos los departamentos no es necesario ver lineas
       |SI #0#7 = "  "; |Y #0#8 = "zz";
          |Y #0#38 = "   "; |Y #0#39 = "zzz";
           aMulDep = "N";
       |FINSI;
   |SINO;
       |SI #0#7 [ 1;    |Y #0#8 = 99;
          |Y #0#38 = "   "; |Y #0#39 = "zzz";
           aMulDep = "N";
       |FINSI;
   |FINSI;

   eaIa = #30#21;
   enM1 = 1 + (#0#11 -1) * 3;
   enM2 = 3 + (#0#12 -1) * 3;
   |HAZ LeeDatosEmpresa;
   |SI eSwMul ! 0; #900#12 = #caselem0#4; |FINSI;
   #900#13 = #0#9;
   #900#14 = #0#10;

   |DELINDEX #901;
   |DELINDEX #902;
   |DELINDEX #903;

   nSiIrpf = 0;
   swAlgun = 0;
|FPRC;

|PROCESO HazTodo;

   |SI nEmpMulti = -1; |O eSwMul = 0;

       |HAZ EmpiezaEmpresa;
       |SI swAlgun = -1; |ACABA; |FINSI;

       nEmpMulti = #30#2;
   |FINSI;

   |SI eSwMul ! 0; |Y nEmpMulti ! #30#2;
       |HAZ TerminaEmpresa;

       nEmpMulti = #30#2;
       |HAZ EmpiezaEmpresa;
       |SI swAlgun = -1; |ACABA; |FINSI;
   |FINSI;

   |ABRE #901;
   |ABRE #903;
   |BUCLE Facturas;
   |CIERRA #903;
   |CIERRA #901;

   |SI eSwMul = 0;
       |HAZ TerminaEmpresa;
   |FINSI;
|FPRC;

|PROCESO TerminaEmpresa;
   |SI swAlgun = 1;

       |HAZ ParaImpAux;
       |SI nResum = 1; |Y #0#16 = "S";  || Imprimo otra vez
           nResum = 0;
           aAlfa1 = #0#14;
           #0#14 = "RESUMEN FACTURAS POR CONCEPTOS CONTABLES";
           |HAZ ParaImpAux;
           #0#14 = aAlfa1;
           nResum = 1;
       |FINSI;
   |FINSI;
|FPRC;

|PROCESO ParaImpAux;
       |SI nResum = 0;
           informe = inform1;
           |SI #0#35 = "S"; informe = inform3; |FINSI;
           |SI nSiIrpf = 1;
               informe = inform2;
               |SI #0#35 = "S"; informe = inform4; |FINSI;
           |FINSI;
       |SINO;
           informe = infor11;
           |SI nSiIrpf = 1; informe = infor12; |FINSI;
       |FINSI;

       |INFOR informe;
       |SI FSalida ! 0; |ACABA;  |FINSI;

       |BUCLE caw00031;
       |PIEINF;
       |FININF;
|FPRC;

|| ////////////////////////////////////
|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   |HAZ Analitica;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;
   |SI #0#9 > #caselem1#5; |O #0#10 < #caselem1#4;
       |ACABA;  || Fuera de la seleccion
   |FINSI;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #2 dirfich0;
   |PATH_DAT #3 dirfich0;
   |PATH_DAT #200 dirfich0;

   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 000001;
 999999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;

|DEFBUCLE MultiEmpEjer;
  #caselem1#2;
  ;
  #997#0, "01.01.0000";
  #997#0, "31.12.2999";
  ;
  NULL,MultiEmp1;
|FIN;

|PROCESO MultiEjer1;
  |BUCLE MultiEmpEjer;
|FPRC;

|DEFBUCLE MultiEjer0;
   #caselem0#1;
   ;
   00001;
   99999;
   ;
   NULL,MultiEjer1;
|FIN;

|PROCESO MultiEjercicio;
   espe15 = "S";

   |SUB_EJECUTA "caselemp";
   |NOME_DAT #998 eaFichsel;
   |NOME_DAT #997 eaFichres;
   nEmpMulti = -1;
   |BUCLE MultiEjer0;
   |SI nEmpMulti ! 0;
       |HAZ TerminaEmpresa;
   |FINSI;
   |DELINDEX #998;
   |DELINDEX #997;
|FPRC;

|PROGRAMA;

  aParam1= "";
  aParam = PARAMETRO; |QBF aParam;
  |SI aParam = "D";
      nResum = 1;
      aParam = "";
  |FINSI;

  |HAZ eParaModelos;
  |SI aParam = "";
      |HAZ inicio;
      |HAZ Analitica;
  |SINO;
      aParam1 = aParam - "modelos"; |QBF aParam1;
      cod1 = enEmpresa;
      cod2 = enPeriodo;
      |HAZ leelo;
      |DBASS dirfich0; |QBT dirfich0;
      dirfich0 = dirfich0 + "contagen/fich/" + (("00000" + enEmpresa) % -105) + enPeriodo + "/";
      |PATH_DAT #1 dirfich0;
      |PATH_DAT #2 dirfich0;
      |PATH_DAT #3 dirfich0;
      |PATH_DAT #8 dirfich0;
      |PATH_DAT #200 dirfich0;
  |FINSI;

  |SI aParam1 = "";
      |SI enSwMvo = 0;
          |CLS;
          |MANTE #0;
      |SINO;
          aParam1 = eaParam2;
          |DDEFECTO #0;
          #0#0  = eaParam2;
          #0#9  = efDFecM;
          #0#10 = efHFecM;
          #0#7  = ("00" + enDActM) % -102;
          #0#8  = ("00" + enHActM) % -102;
          |ABRE #200;
          |LEE 001#200p;
          |SI FSalida ! 0;
              |DDEFECTO #200;
          |FINSI;
          |CIERRA #200;
          |ABRE #8;
          |DDEFECTO #8;
          #8#0 = 1;
          |LEE 011#8=;
          |CIERRA #8;
          #0#15 = #8#7;
          |HAZ ElTipo3;
      |FINSI;
  |SINO;
      |DDEFECTO #0;
      #0#0  = aParam1;
      #0#9  = "01.01.2000";
      #0#10 = fec2;

      |ABRE #200;
      |LEE 001#200p;
      |SI FSalida ! 0;
          |DDEFECTO #200;
      |FINSI;
      |CIERRA #200;
      |ABRE #8;
      |DDEFECTO #8;
      #8#0 = 1;
      |LEE 011#8=;
      |CIERRA #8;
      #0#15 = #8#7;

      |HAZ ElTipo3;
  |FINSI;
|FPRO;
