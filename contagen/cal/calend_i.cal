|FICHEROS;
  cacalend;
  calendar #401;
|FIN;

|VARIABLES;
    &enCalen       = 0;
    &eSwNoCrea     = 0;

    aEjer          = "";
    nCalc          = 0;
    DiasEnero      = 31;
    DiasFebrero    = 28;
    DiasMarzo      = 31;
    DiasAbril      = 30;
    DiasMayo       = 31;
    DiasJunio      = 30;
    DiasJulio      = 31;
    DiasAgosto     = 31;
    DiasSeptiembre = 30;
    DiasOctubre    = 31;
    DiasNoviembre  = 30;
    DiasDiciembre  = 31;
    aAlfa1         = "";
    aAlfa2         = "";
    aAlfa21        = "";
    fFech1         = @;
    fFech2         = @;
    sdia           = 0;
    nCoMes         = 0;
    NumDias        = 0;
    Cont           = 0;
    ContDias       = 0;
    Desde          = "";
    Hasta          = "";
    Peso           = 0;
    PesoAnt        = 0;
    DDia           = 0;
    DMes           = 0;
    HDia           = 0;
    HMes           = 0;
|FIN;

|INCLUYE dscont_i;


|PROCESO CreaCalend;

  |SI dirfich0 = "";
      |DFICE dirfich0; |QBF dirfich0;
  |FINSI;
  |PATH_DAT #calendar #dirfich0#;

  |PINTA 2330, "Creando Calendario ";

  aEjer = enEjercicio; |QBF aEjer;

  |ABRE #calendar;
  #calendar#0  = enCalen;
  #calendar#13 = aEjer;
  |LEE 110#calendar.=;
  |SI FSalida ! 0;
      |DDEFECTO #calendar;
      #calendar#0 = enCalen;
      #calendar#13 = aEjer;
      |GRABA 020#calendar.b;
  |SINO;
      |SI eSwNoCrea = 1;
          |CIERRA #calendar;
          |PINTA 2330, "                   ";
          |ACABA;
      |FINSI;
  |FINSI;

|| ... Bisiesto
  DiasFebrero = 28;
  nCalc = enEjercicio $ 4;
  |SI nCalc = 0;
      nCalc = enEjercicio $ 100;
      |SI nCalc = 0;
          nCalc = enEjercicio $ 400;
          |SI nCalc = 0;   DiasFebrero = 29;  |FINSI;
      |SINO;
          DiasFebrero = 29;
      |FINSI;
  |FINSI;
||...........................

|| ...  MiraDiaUno;
    aAlfa1 = enEjercicio;
    aAlfa1 = "01.01." + aAlfa1;
    fFech1 = aAlfa1;            || primero saca el dia de la semana
    fFech2 = fFech1 % 1;        || que fue el uno de enero
    aAlfa1 = fFech2 % 1199;

    |SI aAlfa1 = "Lunes";     sdia=1; |FINSI;
    |SI aAlfa1 = "Martes";    sdia=2; |FINSI;
    |SI aAlfa1 = "Miercoles"; sdia=3; |FINSI;
    |SI aAlfa1 = "Jueves";    sdia=4; |FINSI;
    |SI aAlfa1 = "Viernes";   sdia=5; |FINSI;
    |SI aAlfa1 = "Sabado";    sdia=6; |FINSI;
    |SI aAlfa1 = "Domingo";   sdia=7; |FINSI;
||...........................

|| .................... Rellena el a¤o entero
  nCalc = sdia + 1;
  |PARA nCoMes = 1; |SI nCoMes [ 12; |HACIENDO nCoMes = nCoMes + 1;
        |HAZ LosDiasMes;

        aAlfa2 = "";
        |PARA ContDias = 1; |SI ContDias [ 31; |HACIENDO ContDias = ContDias + 1;
              |SI ContDias [ NumDias;
                  aAlfa2 = aAlfa2 + #cacalend#nCalc#;
                  nCalc = nCalc + 1; |SI nCalc > 8; nCalc = nCalc - 7; |FINSI;
             |SINO;
                 aAlfa2 = aAlfa2 + "0";
            |FINSI;
        |SIGUE;
        #calendar#nCoMes# = aAlfa2;
  |SIGUE;


|| .............. relleno fecha

   || Primero los intervalos entre fechas .......

   |PARA Cont = 9; |SI Cont [ 96; |HACIENDO Cont = Cont + 3;

      Desde = #cacalend#Cont#;  nCalc = Cont + 1;
      Hasta = #cacalend#nCalc#; nCalc = Cont + 2;
      Peso  = #cacalend#nCalc#;

      |SI Desde ! "    "; |Y Hasta ! "    ";
          aAlfa2 = Desde % 0102; DDia = aAlfa2;
          aAlfa2 = Desde % 0302; DMes = aAlfa2;
          aAlfa2 = Hasta % 0102; HDia = aAlfa2;
          aAlfa2 = Hasta % 0302; HMes = aAlfa2;

          |PARA nCoMes = DMes; |SI nCoMes [ HMes; |HACIENDO nCoMes = nCoMes + 1;

                |HAZ LosDiasMes;
                aAlfa2 = #calendar#nCoMes#;

                |SI DMes = HMes;
                    |PARA ContDias = DDia; |SI ContDias [ HDia; |HACIENDO ContDias = ContDias + 1;
                          |HAZ CambiaValor;
                    |SIGUE;
                |SINO;
                    |SI nCoMes = DMes;
                        |PARA ContDias = DDia; |SI ContDias [ 31; |HACIENDO ContDias = ContDias + 1;
                              |SI ContDias [ NumDias;
                                  |HAZ CambiaValor;
                              |SINO;
                                  PesoAnt = Peso;
                                  Peso = 0;
                                  |HAZ CambiaValor;
                                  Peso = PesoAnt;
                              |FINSI;
                        |SIGUE;
                    |SINO;
                        |SI nCoMes = HMes;
                            |PARA ContDias = 1; |SI ContDias [ HDia; |HACIENDO ContDias = ContDias + 1;
                                  |SI ContDias [ NumDias;
                                       |HAZ CambiaValor;
                                  |SINO;
                                        PesoAnt = Peso;
                                        Peso = 0;
                                        |HAZ CambiaValor;
                                        Peso = PesoAnt;
                                  |FINSI;
                            |SIGUE;
                        |SINO;
                            |PARA ContDias = 1; |SI ContDias [ 31; |HACIENDO ContDias = ContDias + 1;
                                  |SI ContDias [ NumDias;
                                      |HAZ CambiaValor;
                                  |SINO;
                                       PesoAnt = Peso;
                                       Peso = 0;
                                       |HAZ CambiaValor;
                                       Peso = PesoAnt;
                                  |FINSI;
                            |SIGUE;
                        |FINSI;
                     |FINSI;
               |FINSI;
               #calendar#nCoMes# = aAlfa2;
         |SIGUE;
      |FINSI;
   |SIGUE;

   |PARA Cont = 99; |SI Cont [ 157; |HACIENDO Cont = Cont + 2;

         |SI #cacalend#Cont# = "    "; |SAL; |FINSI;

         aAlfa1 = #cacalend#Cont# % 102; DDia = aAlfa1;
         aAlfa1 = #cacalend#Cont# % 302; DMes = aAlfa1;
         nCalc  = Cont + 1;
         Peso   = #cacalend#nCalc#;

         aAlfa2    = #calendar#DMes#;
         ContDias = DDia;
         |HAZ CambiaValor;
         #calendar#DMes# = aAlfa2;
   |SIGUE;
|| ...........................................

  |PINTA 2330, "                   ";

  |GRABA 020#calendar.a;
  |LIBERA #calendar;
  |CIERRA #calendar;

|FPRC;

|PROCESO CambiaValor;
   nCalc = 100 + (ContDias - 1);
   aAlfa21 = aAlfa2 % nCalc;
   aAlfa21 = aAlfa21 + Peso;
   nCalc = ((ContDias + 1) * 100) + 31;
   aAlfa21 = aAlfa21 + (aAlfa2 % nCalc);
   aAlfa2 = aAlfa21;
|FPRC;

|PROCESO LosDiasMes;
             |SI nCoMes = 1;  NumDias = DiasEnero;      |FINSI;
             |SI nCoMes = 2;  NumDias = DiasFebrero;    |FINSI;
             |SI nCoMes = 3;  NumDias = DiasMarzo;      |FINSI;
             |SI nCoMes = 4;  NumDias = DiasAbril;      |FINSI;
             |SI nCoMes = 5;  NumDias = DiasMayo;       |FINSI;
             |SI nCoMes = 6;  NumDias = DiasJunio;      |FINSI;
             |SI nCoMes = 7;  NumDias = DiasJulio;      |FINSI;
             |SI nCoMes = 8;  NumDias = DiasAgosto;     |FINSI;
             |SI nCoMes = 9;  NumDias = DiasSeptiembre; |FINSI;
             |SI nCoMes = 10; NumDias = DiasOctubre;    |FINSI;
             |SI nCoMes = 11; NumDias = DiasNoviembre;  |FINSI;
             |SI nCoMes = 12; NumDias = DiasDiciembre;  |FINSI;
|FPRC;
