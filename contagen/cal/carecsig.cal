|FICHEROS;
   cacuenta #10;
   cacuesig #1;
   caexistl #2; || existencias para calcular el saldo Real de Estas cuentas

   camandia #11;
|FIN;

|VARIABLES;
   aAlfa1 = "";
   &eldesde = 0;
   &elhasta = 0;
   &pathbal = "";
   &r_emp = 0;
   &r_per = 0;
   &d_mes = 0;
   &h_mes = 0;
   &estru = 0;
   &aparam = "";

   dirfich9 = "";
   alfa1 = "";
   alfa2 = "";
   alfa3 = "";
   alfa4 = "";
   alfa5 = "";
   uno = "";
   dos = " ";
   tres = "";
   saldo = 0;
   x = 0;
   y = 0;
   mes_fin = 0;
   sw_algun = 0;
   descn = 0;
   descar = 0;
   act_cuent = "";
   act_nivel = 0;
   traraf = 0;
   &eaNomFEst = "";
   aFich_or = "";
   fich = "";
   temporal1 = "";
   temporal2 = "";
   &balance = 0;
|FIN;

|INCLUYE dscont_i;

|PROCESO antesque;
   |SI aparam = "fech";
       |NOME_DAT #10 "cabalano";
   |FINSI;
   |SI aparam = "Analitica";
       |NOME_DAT #10 eaUsuAna;
   |FINSI;
   |SI aparam = "otp";
       |NOME_DAT #10 eaNomFEst;
   |FINSI;
   |SI aparam = ""; |Y eaNomFCta ! "";
       |NOME_DAT #10 eaNomFCta;
   |FINSI;
|FPRC;

|PROCESO dirfich9; || calcular el directorio base de los ficheros

   |DBASE dir_fich;
   dirfich9 = dir_fich + "fich/";
   alfa1 = r_emp;
   alfa2 = r_per;
   alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
   alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
   dirfich9 = dirfich9 + alfa1 + alfa2 + "/";


   |PATH_DAT #11 dirfich9;
   |PATH_DAT #10 dirfich9;
   |PATH_DAT #1 dirfich9;
   |PATH_DAT #2 dirfich9;
|FPRC;

|| ********************************************************************
|PROCESO LaClave30;
    alfa1 = #10#0;
    |QBF alfa1;
    alfa1 = alfa1 + "zzzzzzzzzzzz";
    alfa1 = alfa1 % 112;
    #10#54 = alfa1;   || cuenta tercera clave
    eaCuenta = #10#0;
    |HAZ SacaNivel;
    #10#1  = enNivel;
    #10#55 = enNivel;
    |GRABA 020#10a;
    |LIBERA #10;
|FPRC;

|DEFBUCLE LaClave3;
 #10#1;
 ;
 "            ";
 "zzzzzzzzzzzz";
 be;
 NULL, LaClave30;
|FIN;
|| ********************************

|PROCESO actua; || calcula el saldo y coloca en balance segun este
   saldo = 0;

   tres = #cacuenta#0 % A101;
   |SI tres = "3"; || si es existencias, calcula saldo segun descarga exist.
      |HAZ exist1;
   |SINO;
      |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
         saldo = saldo + #cacuenta#x#;
      |SIGUE;
   |FINSI;

   |SI tres = "6"; |O tres = "7";
      |HAZ exist2;
   |FINSI;

   |SI saldo = 0; |ACABA; |FINSI;

   |SI saldo ] 0;
      #cacuenta#4 = #cacuesig#1;    || bloque
      #cacuenta#5 = #cacuesig#2;    || tipo
      #cacuenta#6 = #cacuesig#3;    || agrupacion
      #cacuenta#7 = #cacuesig#4;    || epigrafe
      #cacuenta#8 = #cacuesig#5;    || partida
   |SINO;
      #cacuenta#4 = #cacuesig#6;    || bloque
      #cacuenta#5 = #cacuesig#7;    || tipo
      #cacuenta#6 = #cacuesig#8;    || agrupacion
      #cacuenta#7 = #cacuesig#9;    || epigrafe
      #cacuenta#8 = #cacuesig#10;   || partida
   |FINSI;

   |GRABA 020#10a;
   |LIBERA #10;
|FPRC;

|DEFBUCLE lee_ctas;
   #10#1;
   ;
   #1#0;
   dos;
   be;
   NULL, actua;
|FIN;

|PROCESO cuenta;
   uno = #cacuesig#0;
   |QBF uno;
   |SI uno = ""; |ACABA; |FINSI;
   dos = uno + "999999999999";
   dos = dos % 112;
   |BUCLE lee_ctas;
|FPRC;

|DEFBUCLE cambia;
   #1#1;
   ;
   "            ";
   "zzzzzzzzzzzz";
   e;
   NULL, cuenta;
|FIN;

|PROCESO BloqDiario;
  |LEE 101#11p;
  |PARA; |SI FSalida = 0; |HACIENDO;
         |SI #11#2 ! #11#3;
              |PUSHV 1020 1367;
              |CUADRO 1020 1367;
              |PINTA 1121," AVISO!!!. SE ESTA TRABAJANDO EN EL DIARIO.   ";
              |PINTA 1221," NO PUEDE LANZAR EL BALANCE, ESTA DESCUADRADO ";
              |PAUSA;
              |POPV;
              enECuadre = 1;
              |SAL;
          |FINSI;
          |LEE 101#11s;
  |SIGUE;
|FPRC;

|PROCESO DesbDiario;
   |LIBERA #11;
|FPRC;

|DEFBUCLE DesbDiario;
 #11#1;
 ;
 00;
 99;
 ;
 NULL, DesbDiario;
|FIN;



|PROCESO copiando;
   || |DFICE fich;
   fich = pathbal;
   temporal1 = fich + aFich_or + ".ixx";
   temporal2 = fich + eaNomFCta + ".ixx";
   |COPIA_FICHERO temporal1,temporal2;
   |SI FSalida ! 0;  |ACABA;  |FINSI;
   temporal1 = fich + aFich_or + ".dat";
   temporal2 = fich + eaNomFCta + ".dat";
   |COPIA_FICHERO temporal1,temporal2;
   |SI FSalida ! 0;
      temporal2 = fich + eaNomFCta + ".ixx";
      |FBORRA temporal2;
      |ACABA;
   |FINSI;
   temporal1 = fich + aFich_or + ".ddx";
   temporal2 = fich + eaNomFCta + ".ddx";
   |COPIA_FICHERO temporal1,temporal2;
|FPRC;

|| calculos de existencias y variacion de las mismas
|| saldos reales en las cuentas 3, y las de variacion

|| ...................................... Calcula las cuentas 3 EXISTENCIAS.
|PROCESO ver_exis;
   mes_fin = h_mes + 11; || mes finales
   saldo = saldo + #caexistl#mes_fin#; || exist. finales
   sw_algun = 1;
|FPRC;

|DEFBUCLE exis3;
   #2#2;
   ;
   estru, #10#0;
   estru, alfa4;
   be;
   NULL, ver_exis;
|FIN;

|PROCESO exist1;
   sw_algun = 0;
   alfa3 = #cacuenta#0;
   |QBF alfa3;
   alfa4 = alfa3 + "999999999999";
   alfa4 = alfa4 % 112;
   |BUCLE exis3;
   |SI sw_algun = 0;
      saldo = 0;
      |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
            saldo = saldo + #cacuenta#x#;
      |SIGUE;
   |FINSI;
|FPRC;

|| ----------------------------------------------- Fin c lculo existencias

|| ................................... Calcula las cuentas 6 y 7 VARIACION

|PROCESO ver_desc;
   mes_fin = d_mes + 11;  || mes iniciales
   descar = #caexistl#mes_fin#;
   mes_fin = h_mes + 11;  || mes finales
   descar = descar - #caexistl#mes_fin#; || iniciales - finales

   |SI descar ] 0;
      act_cuent = #caexistl#5;     || cuenta descarga D
      act_nivel = #caexistl#6;     || digito descarga D
   |SINO;
      act_cuent = #caexistl#8;     || cuenta descarga H
      act_nivel = #caexistl#9;     || digito descarga H
   |FINSI;

   |SI descar = 0; |ACABA; |FINSI;

   || .. acortar cuenta
   traraf    = 100 + y;
   act_cuent = act_cuent % traraf;
   act_cuent = act_cuent + "              ";
   act_cuent = act_cuent % 112

   |SI act_cuent = #cacuenta#0;
      sw_algun = 1;
      descn = descn + descar;
   |FINSI;
|FPRC;

|DEFBUCLE exis4;
   #2#1;
   ;
   estru, 00;
   estru, 99;
   be;
   NULL, ver_desc;
|FIN;

|PROCESO exist2;
   alfa3 = #cacuenta#0;
   |QBF alfa3;
   alfa5 = alfa3 % 0;
   y = alfa5;
   |SI y = 1; |ACABA; |FINSI;
   sw_algun = 0;
   descn = 0;
   |BUCLE exis4;
   |SI sw_algun = 1;
       saldo = descn;
   |FINSI;
|FPRC;

|PROGRAMA;
  |SI enSwOper ! 2;
      |HAZ antesque;
  |FINSI;

  |HAZ dirfich9;

  |SI enSwOper = 0; |BUCLE cambia; |FINSI;

  |SI enSwOper = 1; |BUCLE LaClave3; |FINSI;

  |SI enSwOper = 2;  || ... Nuevo Bloqueo para cuadrar

      |ABRET #11;
      enECuadre = 0;
      |HAZ BloqDiario;

      |SI enECuadre = 0;
          balance = 2;
          |SUB_EJECUTA "careccue";
          eaNomFCta = ("0w" + Usuario) % 108;
          aFich_or  = "cacuenta";
          |HAZ copiando;
      |FINSI;

      |BUCLE DesbDiario

      |CIERRAT #11;
 |FINSI;

 |PULSATECLA;
|FPRO;
