|FICHEROS;
   cagecz01;
   cageco01,MANTE;
   dsapum03;

   canempre;
   camandia;
   cacuenta;

   caconasi;
   calibros;
   caivases;
   caclipro;
   caivaasi;
   caforpag;

   caintiva;
   cainliva;
   caivam03;

   cientasc;
   cientasl;
   cisalant;

   caw00011,MANTE;
   caw00036,MANTE;
   :/modelos/def/dsmom030;

   ca8m0002;
   ca8cumay;
   cacuebal;
|FIN;

|VARIABLES;
   &entrada       = 1;
   nError         = 0;
   nSwError       = 0;
   aError         = "";
   nCampo         = 0;
   aFichero       = "";
   aAlfa          = "";
   nNumero        = 0;
   aUsuario       = "";
   aUsuari1       = "";
   aHora          = "";
   fFechaP        = @;
   fFechaU        = @;
   nAsientos      = 0;
   nFacturas      = 0;
   nInkey         = 0;
   nOpera         = 0;
   aDTipo         = "";
   aHTipo         = "";
   nSwGastos      = 0;
   nLinea         = 0;
   nOk            = 0;
   NumFactura     = 0;

   alfa1   = "";
   alfa2   = "";
   alfa3   = "";
   alfa4   = "";
   alfa5   = "";
   alfa6   = "";
   muestra = "";
   asteris = 0;
   nNume1  = 0;
   nNume2  = 0;
   nNume3  = 0;
   nNume4  = 0;
   nNume5  = 0;
   nNume6  = 0;
   nPara1  = 0;
   longi   = 0;
   nSwLaC  = 0;

   &nSwOpera = 0;
   &enQSerie = 0;

   &eaFichero1 = "";
   &eaFichero2 = "";
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoiv_i;
|INCLUYE dscosi_i;
|INCLUYE defec1_i;

|| *************************************************************************
|PROCESO Max965;
 #caw00036#0 = 1;
 #caw00036#2 = nError;
|FPRC;

|PROCESO Min965;
 #caw00036#0 = 999999;
 #caw00036#2 = nError;
|FPRC;

|PROCESO Tipo11_w011; |TIPO 11;
 nInkey = FSalida;
 |BLIN 4;
 |SI nInkey = 10;
     |PUSHV 0501 2380;
     nError = #caw00011#0;
     |PINPA #0#caw00036;
     |ATRI S; |PINTA 0603, #caw00011#1; |ATRI 0;
     |ENTLINEAL #2#caw00036, -2, 5, 22, 0, Max965, Min965;
     |POPV;
 |FINSI;
 nInkey = 0;
|FPRC;

|PROCESO Tipo7_w011; |TIPO 7;
 |MENSA "    <F2> Ver Detalle Error";
|FPRC;

|PROCESO Max65;
 #caw00011#0 = 1;
|FPRC;

|PROCESO Min65;
 #caw00011#0 = 999999;
|FPRC;

|PROCESO VerErrores;
      |C_P #caw00011#0,1,0603;
      |C_P #caw00011#1,1,0610;
      |CLS;
      |ATRI S;
      aAlfa = "ATENCION: ERRORES EN EL FICHERO DE COMPRAS/GASTOS";
      |PINTA 0503,aAlfa;|ATRI 0;
      |ATRI 0;
      |CUADRO 0602 2180;
      |ENTLINEAL #1#caw00011, -1, 5, 20, 0, Max65, Min65;
|FPRC;
|| *************************************************************************
|PROCESO GrabaRegistro;
   aUsuario = Usuario % 108;
   aUsuari1 = Usuario % 840;

   |ABRE #dsapum03;
   #dsapum03#0 = #cagecz01#0;
   #dsapum03#1 = 999999;
   |LEE 010#dsapum03.];
   |SI FSalida = 0;
       |LEE 040#dsapum03.a;
   |SINO;
       |LEE 040#dsapum03.u;
   |FINSI;

   nNumero = 1;
   |SI FSalida = 0; |Y #dsapum03#0 = #cagecz01#0;
       nNumero = #dsapum03#1 + 1;
   |FINSI;

   |DDEFECTO #dsapum03;
   #dsapum03#0 = #cagecz01#0;
   #dsapum03#1 = nNumero;
   #dsapum03#2 = #cagecz01#1;
   #dsapum03#3 = #cagecz01#2;
   #dsapum03#4 = #cagecz01#3;
   #dsapum03#5 = aUsuario;
   #dsapum03#6 = aUsuari1;

   #dsapum03#9  = fFechaP;
   #dsapum03#10 = fFechaU;
   #dsapum03#11 = nAsientos;
   #dsapum03#12 = nFacturas;

   |HORA aHora;
   #dsapum03#7 = ~;
   #dsapum03#8 = aHora;
   |GRABA 020#dsapum03.n;

   |CIERRA #dsapum03;
|FPRC;

|| **************************************************************************
|PROCESO AltaCuenta;
     |DDEFECTO #cacuenta;
     #cacuenta#0 = eaCuenta;
     #cacuenta#1 = enNivel;
     #cacuenta#3 = "S";
     mascara = #cacuenta#0;
     |QBF mascara;
     mascara = mascara +"zzzzzzzzzzzz";
     mascara = mascara % 112;
     #cacuenta#54 = mascara;
     #cacuenta#55 = enNivel;
     |HAZ defect1;
     |GRABA 020#cacuenta.n;
     |LIBERA #cacuenta;
|FPRC;

|| **************************************************************************
|PROCESO GrabaError;

  nNumero = nNumero + 1;
  #caw00036#0 = nNumero;
  #caw00036#1 = aError;
  #caw00036#2 = nSwError;
  |GRABA 020#caw00036.n;

  aAlfa = "";
  #caw00011#0 = nSwError;
  |LEE 040#caw00011.=;
  |SI FSalida ! 0;
    || ... Errores de los asientos
      |SI nSwError = 1;  aAlfa = "TIPO DE MOVIMIENTO EN BLANCO ";          |FINSI;
      |SI nSwError = 2;  aAlfa = "CODIGO DEL CONCEPTO CON VALOR 000 ";     |FINSI;
      |SI nSwError = 3;  aAlfa = "FECHAS FUERA DEL EJERCICIO CONTABLE ";   |FINSI;
      |SI nSwError = 4;  aAlfa = "CUENTA PARTIDA DE PAGO EN BLANCO ";      |FINSI;
      |SI nSwError = 14; aAlfa = "CUENTA CONTRAPARTIDA EN BLANCO ";        |FINSI;
      |SI nSwError = 5;  aAlfa = "CUENTA PARTIDA DE PAGO FUERA DE NIVEL "; |FINSI;
      |SI nSwError = 15; aAlfa = "CUENTA CONTRAPARTIDA FUERA DE NIVEL ";   |FINSI;
      #caw00011#1 = aAlfa;
      |GRABA 020#caw00011.n;
 |FINSI;
 nError = 1;
|FPRC;

|PROCESO ComCuentas;
     eaCuenta = #cageco01#nCampo#; |QBF eaCuenta;
     |SI eaCuenta = "";
         nSwError   = 4; |SI nCampo = 13; nSwError = 14; |FINSI;
         |HAZ GrabaError;
         |ACABA;
     |FINSI;
     |HAZ SacaNivel;
     |SI enNivel = 0;
         nSwError   = 5; |SI nCampo = 13; nSwError = 15; |FINSI;
         |HAZ GrabaError;
         |ACABA;
     |FINSI;

     #cacuenta#0 = eaCuenta;
     |LEE 041#cacuenta.=;
     |SI FSalida ! 0;
         |HAZ AltaCuenta;
     |FINSI;

     aAlfa = eaCuenta % 102;
     |SI aAlfa = "40"; |O aAlfa = "41"; |O aAlfa = "43"; |O aAlfa = "44";
         |DDEFECTO #caclipro;
         #caclipro#23 = "P";
         |SI aAlfa = "43"; |O aAlfa = "44"; #caclipro#23 = "C"; |FINSI;
         #caclipro#10 = eaCuenta;
         |LEE 041#caclipro.=;
     |FINSI;
|FPRC;

|PROCESO Comprueba;
     aError = "Apunte: " + #cageco01#0;
     aError = aError + " Fecha: " + #cageco01#5;
     aError = aError + " Partida Pago: " + #cageco01#1;

     |SI #cageco01#2 ! "C"; |Y #cageco01#2 ! "G";
         nSwError = 1; |HAZ GrabaError;
     |FINSI;
     |SI #cageco01#3 = 0;
         nSwError = 2; |HAZ GrabaError;
     |FINSI;
     |SI #cageco01#5 < fec1; |O #cageco01#5 > fec2;
         nSwError = 3; |HAZ GrabaError;
     |FINSI;
     nCampo = 1;  |HAZ ComCuentas;
     nCampo = 13; |HAZ ComCuentas;
|FPRC;

|| **************************************************************************
|PROCESO BuscaNumero;
   |SI enQSerie = 0;
       |ABRE #calibros;
       #calibros#0 = #caintiva#0;
       |LEE 101#calibros.=;
       |SI FSalida = 0;
           NumFactura  = #calibros#4;
           #calibros#4 = #calibros#4 + #calibros#3;
           |GRABA 020#calibros.a;
       |FINSI;
       |LIBERA #calibros;
       |CIERRA #calibros;
   |SINO;
       |ABRE #caivases;
       #caivases#0 = #cainliva#26;
       #caivases#1 = aTipoIVA;
       |LEE 101#caivases.=;
       |SI FSalida = 0;
           NumFactura = #caivases#3;
           #caivases#3 = #caivases#3 + #caivases#4;
           |GRABA 020#caivases.a;
      |FINSI;
      |LIBERA #caivases;
      |CIERRA #caivases;
      || SerieAnt = #cainliva#26;
   |FINSI;
   #cainliva#2 = NumFactura;
|FPRC;

|PROCESO  ProcesoTipo12;
     nOk = 0;
     |SI enTipoIva = 11;  nOk = 1;  |FINSI;
     |SI enTipoIva = 12;  nOk = 1;  |FINSI;
     |SI enTipoIva = 13;  nOk = 1;  |FINSI;
     |SI enTipoIva = 15;  nOk = 1;  |FINSI;
     |SI enTipoIva = 16;  nOk = 1;  |FINSI;
     |SI enTipoIva = 17;  nOk = 1;  |FINSI;

     |SI #caivaasi#188 = "S";  nOk = 1; |FINSI;

     |SI nOk = 0;
         |ACABA;
     |FINSI;

     eaSiiPro  = "cainliva";
     enSiiLib  = #cainliva#0;
     enSiiDoc  = #cainliva#1;
     enSiiOpe  = 1;
     enSiiLin  = 0;
     eaSiiSer  = #cainliva#26;
     eaSiiCpt  = #cainliva#7;
     eaSiiCta  = #cainliva#5;
     eaSiiCIF  = #cainliva#30;  |QBF eaSiiCIF;
     eaSiiDes  = #cainliva#8;
     eaSiiUlt  = #cainliva#64;  |QBF eaSiiUlt;
     eaSii340  = #cainliva#62;  |QBF eaSii340;
     eaSiiEje  = #cainliva#4 % -104;
     enSiiPer  = #cainliva#31;
     enSiiRet  = #cainliva#35;
     eaSiiDep  = #cainliva#45;
     eaSiiSub  = #cainliva#47;
     eaSiiCtr  = #cainliva#28;

     |SUB_EJECUTA "casiim01;DEFECTOSGECONET";

     enSiiDoc = #cainliva#1;
     eaSiiPro  = "cainliva";
     enSiiLib  = #cainliva#0;
     enSiiOpe  = (FEntrada / 100) ! 0;
     enSiiLin  = #cainliva#3;
     enSiiIva  = #cainliva#13;
     eaSiiSer  = #cainliva#26;
     eaSiiCpt  = #cainliva#7;
     eaSiiCta  = #cainliva#5;
     eaSiiCIF  = #cainliva#30;  |QBF eaSiiCIF;
     eaSiiDes  = #cainliva#8;
     eaSiiUlt  = #cainliva#64;  |QBF eaSiiUlt;
     eaSii340  = #cainliva#62;  |QBF eaSii340;
     eaSiiEje  = #cainliva#4 % -104;
     enSiiPer  = #cainliva#31;
     enSiiRet  = #cainliva#35;
     eaSiiDep  = #cainliva#45;
     eaSiiSub  = #cainliva#47;
     eaSiiCtr  = #cainliva#28;
     |SUB_EJECUTA "casiim01";
|FPRC;

|CALCULO TrasDepar; |TIPO 0;
   |SI eaDepar = "N"; |ACABA; |FINSI;
   enActividad = #cainliva#45;
   eaCodDep    = #cainliva#45;
   |HAZ Actividad;
   |SI eswLect = 0;
       #cainliva#45 = eaCodDep;
       #cainliva#46 = eaNombreAct;
   |FINSI;
|FCAL;

|CALCULO TrasSubde; |TIPO 0;
   |SI eaSubde = "N"; |ACABA; |FINSI;
   enConcepto  = #cainliva#7;
   eaCodSubdep = #cainliva#47;
   enQue = 0;
   |HAZ SubActividad;
   |SI eswLect = 0;
       #cainliva#47 = eaCodSubdep;
       #cainliva#48 = eaNomSubdep;
   |FINSI;
|FCAL;

|PROCESO LeoLibro;
   |ABRE #calibros;
   #calibros#0 = #caintiva#0;
   |LEE 010#calibros.=;
   |SI FSalida ! 0;
       |DDEFECTO #calibros;
       #calibros#0 = #caintiva#0;
       #calibros#1 = "LIBRO DE COMPRAS";
       #calibros#2 = aTipoIVA;
       |GRABA 020#calibros.n;
   |FINSI;
   |CIERRA #calibros;
|FPRC;

|CALCULO CuentaCobro;
    |SI #cainliva#49 ! "N";
        #cainliva#41 = #cacuenta#1;
        #cainliva#42 = #cacuenta#2;
        |SI #cainliva#49 = "T";
            #cainliva#43 = dConCEf;
            #cainliva#44 = dDesCEf;
        |SINO;
            #cainliva#43 = dConCar;
            #cainliva#44 = dDesCar;
       |FINSI;
   |FINSI;
|FCAL;

|PROCESO CalCuenta;

  asteris = 0;
  muestra = dCtaCEf; |QBF muestra;
  alfa1   = dCtaCEf; |QBF alfa1;
  alfa2   = alfa1 % 0;
  longi   = alfa2;            || 'longi' sirve despues (no tocar)
  nNume3  = (longi * 100) + 1;
  alfa2   = alfa1 % nNume3;
  |SI alfa2 = "*";
      nNume3    = 99 + longi;
      muestra   = alfa1 % nNume3;
      asteris   = 1;
  |FINSI;

  nSwLaC = 0;
  |PARA nNume4 = 1; |SI nNume4 [ longi; |HACIENDO nNume4 = nNume4 + 1;
        nNume1 = (nNume4 * 100) + 1;
        alfa3  = alfa1 % nNume1;
        |SI alfa3 = "C";  nSwLaC = 1; |FINSI;
  |SIGUE;

  |SI nSwLaC = 1;
      alfa5 = muestra % 0;
      nNume5 = alfa5;
      alfa5 = "";
      |PARA nPara1 = 1; |SI nPara1 [ nNume5; |HACIENDO nPara1 = nPara1 + 1;
            nNume6 = (nPara1 * 100) + 1;
            alfa6  = muestra % nNume6;
            |SI alfa6 = "C";
                alfa6 = #cainliva#5 % nNume6;
            |FINSI;
            alfa5 = alfa5 + alfa6;
     |SIGUE;

     muestra = alfa5;
 |FINSI;

  nNume2 = MaximoDigitos - (longi - 1);
  |SI asteris = 1;
      nNume2 = (100 * longi) + nNume2;
      alfa1 = #cainliva#5 % nNume2;
      muestra = muestra + alfa1;             || si es "*"
  |FINSI;

  #cainliva#40 = muestra;
|FPRC;
|| //////////////////////////////////////
|PROCESO Actual_Compra;
    enLibIVA = dLibro;
    #caintiva#0  = enLibIVA;
    |LEE 041#caintiva.=;
    |SI FSalida ! 0;
        |DDEFECTO #caintiva;
        #caintiva#0 = dLibro;
        |HAZ LeoLibro;
        #caintiva#1 = #calibros#2;
        #caintiva#7 = aTipoIVA;
        |GRABA 020#caintiva.b;
    |FINSI;

    nLinea = 0;
    #cainliva#0 = #caintiva#0;
    #cainliva#1 = 999;
    |LEE 041#cainliva.];
    |SI FSalida = 0;
        |LEE 041#cainliva.a;
    |SINO;
        |LEE 041#cainliva.u;
    |FINSI;
    |SI FSalida = 0; |Y #cainliva#0 = #caintiva#0;
        nLinea = #cainliva#1;
    |FINSI;
    nLinea = nLinea + 1;

    |DDEFECTO #cainliva;
    #cainliva#0  = #caintiva#0;
    #cainliva#1  = nLinea;
    #cainliva#26 = dSerie;
    |HAZ BuscaNumero;
    #cainliva#2  = NumFactura;
    #cainliva#26 = dSerie;
    #cainliva#4  = #cageco01#5;
    aAlfa        = #cainliva#4 % 0402;
    #cainliva#31 = aAlfa;
    #cainliva#5  = #cageco01#1;
    emCta = 0;
    eaCuenta     = #cainliva#5; |HAZ SacaNivel;
    #cainliva#6  = enNivel;
    #cainliva#7  = #cageco01#3;
    #cainliva#8  = #cageco01#4;
    #cainliva#10 = #cageco01#7;
    #cainliva#12 = #cageco01#6;
    #cainliva#13 = #cageco01#8;
    #cainliva#14 = #cageco01#10;
    #cainliva#15 = #cageco01#9;
    #cainliva#16 = #cageco01#11;
    #cainliva#17 = #cageco01#13;
    eaCuenta     = #cainliva#17; |HAZ SacaNivel;
    #cainliva#18 = enNivel;
    #cainliva#19 = dDeduc;

    efFechaIVA = #cainliva#4; enLineaIVA = #cainliva#10;
    |HAZ SacaIVA;
    #cainliva#20 = eaCtaIVA;
    #cainliva#22 = eaCtaREC;
    #cainliva#24 = eaCtaDTO;
    |SI #cainliva#19 = "N";
        |SI dCtaNoD ! "";
            #cainliva#20 = dCtaNoD;
            #cainliva#22 = dCtaNoD;
        |SINO;
            #cainliva#20 = #cainliva#17;
            |SI #cainliva#15 ! 0; #cainliva#22 = #cainliva#17; |FINSI;
        |FINSI;
    |FINSI;
    |SI #cainliva#19 = "I";
        #cainliva#20 = "";
        #cainliva#22 = 0;
    |FINSI;

    emCta = 0;
    eaCuenta = #cainliva#20; |HAZ SacaNivel; #cainliva#21 = enNivel;
    eaCuenta = #cainliva#22; |HAZ SacaNivel; #cainliva#23 = enNivel;
    eaCuenta = #cainliva#24; |HAZ SacaNivel; #cainliva#25 = enNivel;

    emCta = 0;
    eaCuenta = #cainliva#5;  |HAZ LeeCuenta; #cainliva#27 = #cacuenta#2;
    eaCuenta = #cainliva#17; |HAZ LeeCuenta; #cainliva#28 = #cacuenta#2;

    #cainliva#29 = #cageco01#12;
    #cainliva#30 = #caclipro#9;

    #cainliva#32 = dInver;
    #cainliva#38 = aTipoIVA;
    #cainliva#45 = #caivaasi#19; |HAZ TrasDepar;
    #cainliva#47 = #caivaasi#67; |HAZ TrasSubde;
    #cainliva#49 = "N";

    #cainliva#53 = #cageco01#18;
    eaSiiRef = #cainliva#53; |QBF eaSiiRef;
    eaSiiSer = #cainliva#26; |QBF eaSiiSer;
    enSiiFra = #cainliva#2;
    |HAZ NReferencia;
    #cainliva#53 = eaSiiRef;

    #cainliva#65 = #cainliva#5;
    #cainliva#66 = #cainliva#17;
    #cainliva#67 = #cainliva#29;

    #cainliva#49 = "N";
    #cainliva#74 = #cageco01#19;
    |SI #cageco01#19 ! 0;
        #caforpag#0 = #cageco01#19;
        |LEE 041#caforpag.=;
        |SI FSalida = 0;
            #cainliva#49 = #caforpag#6;
        |FINSI;
    |FINSI;

    |SI #cainliva#49 = "N";
        #cainliva#40 = ""; #cainliva#41 = 0;
        #cainliva#42 = ""; #cainliva#43 = 1; #cainliva#44 = "";
        #cainliva#73 = "01.01.0000";
    |SINO;
        |SI #cainliva#49 = "P";
            #cainliva#40 = dCtaCar
        |SINO;
            |HAZ CalCuenta;
        |FINSI;
        emCta = 0;
        eaCuenta = #cainliva#40; |HAZ SacaNivel; #cainliva#41 = enNivel;
        emCta = 0;
        eaCuenta = #cainliva#40; |HAZ LeeCuenta; #cainliva#42 = #cacuenta#2;
        |SI #cainliva#49 = "T";
            #cainliva#43 = dConCEf;
            #cainliva#44 = dDesCEf;
        |SINO;
            #cainliva#43 = dConCar;
            #cainliva#44 = dDesCar;
       |FINSI;
    |FINSI;

    |GRABA 020#cainliva.b;
    |LIBERA #cainliva;
    |LIBERA #caintiva;

    |HAZ ProcesoTipo12;

    nFacturas = nFacturas + 1;

    |SI fFechaP = "01.01.0000"; fFechaP = #cainliva#4; |FINSI;
    |SI #cainliva#4 < fFechaP;  fFechaP = #cainliva#4; |FINSI;
    |SI fFechaU = "01.01.0000"; fFechaU = #cainliva#4; |FINSI;
    |SI #cainliva#4 > fFechaU;  fFechaU = #cainliva#4; |FINSI;

    |LIBERA #caintiva;
    |LIBERA #cainliva;
|FPRC;

|PROCESO Actual_Gasto;

     |SI nSwGastos = 0;
         nSwOpera = 0;
         |SUB_EJECUTA_NP "caz00017";
         aAlfa = "cactaesp"; |NOME_DAT #cacuenta#aAlfa#;
         aAlfa = "cadiaesp"; |NOME_DAT #camandia#aAlfa#;

         nSwGastos = 1;
     |FINSI;

     emCta    = 0;
     eaCuenta = #cageco01#1;
     |HAZ SacaNivel;

     nSwOpera = 1;
     |SUB_EJECUTA_NP "caz00017";
     |HAZ LeeCuenta;

     #cisalant#0 = eaCuenta;
     |LEE 141#cisalant.=;
     |SI FSalida ! 0;
         |DDEFECTO #cisalant;
         #cisalant#0 = eaCuenta;
         #cisalant#1 = enNivel;
         #cisalant#2 = #cacuenta#2;
         |GRABA 020#cisalant.b;
     |FINSI;

     #cientasc#0 = eaCuenta;
     #cientasc#1 = enNivel;
     |LEE 141#cientasc.=;
     |SI FSalida ! 0;
         |DDEFECTO #cientasc;
         #cientasc#0 = eaCuenta;
         #cientasc#1 = enNivel;
         #cientasc#2 = #cisalant#2;
         |GRABA 020#cientasc.b;
     |FINSI;

     nLinea = 0;
     #cientasl#0 = #cientasc#0;
     #cientasl#1 = #cientasc#1;
     #cientasl#2 = 99999;
     |LEE 041#cientasl.];
     |SI FSalida = 0;
        |LEE 041#cientasl.a;
     |SINO;
        |LEE 041#cientasl.u;
     |FINSI;
     |SI FSalida = 0; |Y #cientasl#0 = #cientasc#0;
         nLinea = #cientasl#2;
     |FINSI;
     nLinea = nLinea + 1;

     |DDEFECTO #cientasl;
     #cientasl#0 = #cientasc#0;
     #cientasl#1 = #cientasc#1;
     #cientasl#2 = nLinea;

     #cientasl#3 = #cageco01#5;
     #cientasl#4 = #cageco01#3;
     #cientasl#5 = #cageco01#4;
     #cientasl#6 = "H";
     #cientasl#7 = #cageco01#12;
     #cientasl#8 = #cageco01#13;
     #cientasl#9 = enNivel;
     #cientasl#10 = #cageco01#3;
     #cientasl#11 = #cageco01#4;

     #cientasl#12 = eaDfDepC;
     #cientasl#13 = eaDfSubC;

     |GRABA 020#cientasl.n;

     #cientasc#6 = #cientasc#6 + #cientasl#7;
     #cientasc#7 = #cientasc#4 + #cientasc#5 - #cientasc#6;

     MasMenos     = #cientasl#7;
     DebeHaber    = #cientasl#6;
     aCUENTA      = #cientasl#0;
     nNIVELCUENTA = #cientasl#1;
     nREGISTRO    = -1;
     nDIARIO      = 01;     ||No tiene diario asignado
     aFECHA       = #cientasl#3;
     nDOCUMENTO   = -1;
     |HAZ ActCuentM;          || cuenta de pase Directo S

     MasMenos     = #cientasl#7;
     DebeHaber    = "D";
     aCUENTA      = #cientasl#8;
     nNIVELCUENTA = #cientasl#9;
     nREGISTRO    = -1;
     nDIARIO      = 01;     ||No tiene diario asignado
     aFECHA       = #cientasl#3;
     nDOCUMENTO   = -1;
     |HAZ ActCuentM;          || cuenta de pase Directo S

     |GRABA 020#cientasc.a;
     |LIBERA #cientasc;
     |LIBERA #cisalant;

     nAsientos = nAsientos + 1;

     |SI fFechaP = "01.01.0000"; fFechaP = #cientasl#3; |FINSI;
     |SI #cientasl#3 < fFechaP;  fFechaP = #cientasl#3; |FINSI;
     |SI fFechaU = "01.01.0000"; fFechaU = #cientasl#3; |FINSI;
     |SI #cientasl#3 > fFechaU;  fFechaU = #cientasl#3; |FINSI;
|FPRC;

|PROCESO LeoCageco01;

   |SI nOpera = 0;
       nError = 0;
       |ERROR10;
   |FINSI;

   |SI nOpera = 1;

       |SI #cageco01#2 = "G"; |HAZ Actual_Gasto;  |FINSI;
       |SI #cageco01#2 = "C"; |HAZ Actual_Compra; |FINSI;

       |BORRA 020#cageco01.a;
   |FINSI;

   |SI nOpera = 2;
       |HAZ Comprueba;
   |FINSI;

   |LIBERA #cageco01;
|FPRC;

|DEFBUCLE LeoCageco01;
     #cageco01#2;
     ;
     aDTipo, 0000001;
     aHTipo, 9999999;
     be;
     NULL, LeoCageco01;
|FIN;

|CALCULO wTipo12; |TIPO 12;

  |SI #cagecz01#5 = "NO";
      |ACABA;
  |FINSI;

  fFechaP     = "01.01.0000";
  fFechaU     = "01.01.0000";
  nAsientos   = 0;
  nFacturas   = 0;

  |DFICE dirfich0; |QBF dirfich0;

  |SI PARAMETRO ! "CRON";
      |ATRI P; |PINTA 1710, "Verificando datos Importados .........."; |ATRI 0;
  |FINSI;

  aFichero = ("e" + Usuario) % 108;
  |NOME_DAT #caw00011#aFichero#;
  |DELINDEX #caw00011;
  aFichero = ("w" + Usuario) % 108;
  |NOME_DAT #caw00036#aFichero#;
  |DELINDEX #caw00036;

  nNumero = 0;
  nError  = 0;
  aDTipo  = " ";
  aHTipo  = "z";
  |ABRE #caw00011;
  |ABRE #caw00036;
  |ABRE #cacuenta;
  |ABRE #caclipro;
  |ABRE #caforpag;
  nOpera = 2; |BUCLE LeoCageco01;
  |CIERRA #caforpag;
  |CIERRA #caclipro;
  |CIERRA #cacuenta;
  |CIERRA #caw00036;
  |CIERRA #caw00011;

  |SI nError ! 0;

      |MENAV "0000Hay errores en el fichero. Proceso abortado";
      |SI PARAMETRO ! "CRON";
         |CONFI "0000SDesea ver los errores";
         |SI FSalida = 0;
             |HAZ VerErrores;
         |FINSI;
      |FINSI;

      |DELINDEX #caw00011;
      |DELINDEX #caw00036;
      |ACABA;
  |FINSI;

|| .... Actualizar
  |SI PARAMETRO ! "CRON";
      |ATRI S; |PINTA 1710, "Recogiendo datos Contables ............"; |ATRI 0;
  |FINSI;

|| ... Actualiza Compras
  |ABRE #caintiva;
  |ABRE #cainliva;

  aTipoIVA = "S";
  |HAZ LeeDefIVA;

  aDTipo = "C";
  aHTipo = "C";
  nOpera = 1; |BUCLE LeoCageco01;

  |CIERRA #cainliva;
  |CIERRA #caintiva;

|| ... Actualiza Gastos  No hay gastos Tique 700000_ 10432
/*
  |ABRE #cientasc;
  |ABRE #cientasl;
  |ABRE #cisalant;

  aDTipo = "G";
  aHTipo = "G";
  nSwGastos = 0;
  nOpera = 1; |BUCLE LeoCageco01;

  |CIERRA #cisalant;
  |CIERRA #cientasl;
  |CIERRA #cientasc;
*/
  |SI PARAMETRO ! "CRON";
      |MENAV "0000Proceso Importacion Completado ";
  |FINSI;

  |HAZ GrabaRegistro;
|FCAL;


|PROCESO MiroFichero;
/*
     |ABRE #cageco01;
     |CONSULTA_CLAVE #1#cageco01;
     |CIERRA #cageco01;
*/
     aDTipo = " ";
     aHTipo = "z";
     nError = 1;
     nOpera = 0;  |BUCLE LeoCageco01;

     |SI nError ! 0;
         |SI PARAMETRO ! "CRON";
             |MENAV "0000No hay registros para Importar";
         |FINSI;
     |FINSI;
|FPRC;

|| /////////////////////////////////////////////////////////////////////////

|PROCESO AltaClave;
   #cageco01#0 = 000001;
|FPRC;

|PROCESO BajaClave;
   #cageco01#0 = 999999;
|FPRC;

|PROGRAMA;
     |HAZ inicio;
     |HAZ eLeeParametro;
/*
     eaFichero1 = "camaniva";
     eaFichero2 = "caivalin";
     |HAZ eImprimirDef;
*/
     #cagecz01#0 = enEmpresa;
     #cagecz01#1 = enPeriodo;
     #cagecz01#2 = eaNombreEmp;
     #cagecz01#3 = enEjercicio;

     |SI PARAMETRO = "CRON";
         #cagecz01#5  = "SI";

         |HAZ wTipo12;
         |ACABA;
     |FINSI;

     aTipoIVA = "S";
     |CLS;
     |ENTLINEAL #1#cageco01, -1, 2, 22, 1, AltaClave, BajaClave;

     |CLS;
     |CABEZA "Importar Compras Geconet";
     |PINPA #0#cagecz01;
     |PINDA #0#cagecz01;
     |HAZ MiroFichero;
     |SI nError = 0;
         |ENDATOS #1#cagecz01;
     |FINSI;
|FPRO;
