|FICHEROS;
   anz00006 #0;
   cacuenta #1;
   camaasil #3;
   cacuebal #5;
   :/modelos/def/dsmom035 #6;
   :/modelos/def/dsmom036 #7;
   canempre #30;

   anm00013 #13;
   anm00003 #17;
   anm0003@ #117;
   ca8m0002 #422;
   ca8cumay #410;
|FIN;

|VARIABLES;
   aAlfa1 = "";
   alfa1 = "";
   fich = "";
   temporal1 = "";
   temporal2 = "";
   juan = 0;
   mensa = "";
   lon = 0;
   prim = 0;
   men2   = "     NO existe el Grupo de Conceptos";
   a = 0;
   b = 0;
   c = 0;
   mesac = 0;
   mesfec = 0;
   mes = "";
   fecalf = "";
   ini = 0;
   errant = "aaaaaaaaaaaa";
   cuant = "";
   iniemp = 0;
   traba = "";
   aar = "";
   aa = 0;
   sw_con = 0;
   x = 0;
   y = 0;
   incta = "";
   ficta = "";
    swsel = 0;
    alfa3 = "";

     nSwGApte = 0;
     aMas     = "";
     aCta2Gru = "";
     dn_error2 = 0;

|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoan_i;
|INCLUYE digana_i;
|INCLUYE defec1_i;

||****************************************************************

|CALCULO anacta; |TIPO 6;
  salidas = FSalida;
  |HAZ FPuntosAna;
  |SI Campo = 20;
      |SI #0#19 > #0#20;
          |MENAV "     ERROR EN CUENTA ANALITICA ";
          |ERROR;
      |FINSI;
  |FINSI;
|FCAL;

|CALCULO recoge; |TIPO 7;
   #0#2 = fec1;
   #0#3 = fec2;
   |PINTA #0#2;
   |PINTA #0#3;
|FCAL;

|CALCULO fechas;
   |SI #0Campo > fec2;|O #0Campo < fec1;
      |MENAV "    Fecha fuera de Periodo ...";
      |ERROR;
   |FINSI;
|FCAL;

|CALCULO no_dep; |TIPO 7;
   |CAMPO_MODIFICA #0#8, 1, eaDepar;
   |CAMPO_MODIFICA #0#9, 1, eaDepar;
|FCAL;

|CALCULO nosubde; |TIPO 7;
   |CAMPO_MODIFICA #0#15, 1, eaSubde;
   |CAMPO_MODIFICA #0#16, 1, eaSubde;
|FCAL;

|CALCULO noreferencia; |TIPO 7;
   |CAMPO_MODIFICA #0#17, 1, eaRefer;
   |CAMPO_MODIFICA #0#18, 1, eaRefer;
|FCAL;


|CALCULO selecci2; |TIPO 0;
   |SI #0#10 = "N";
      |CAMPO_MODIFICA #0#06, 1, "S";
      |CAMPO_MODIFICA #0#07, 1, "S";

      |CAMPO_MODIFICA #0#11, 1, "N";
      |CAMPO_MODIFICA #0#12, 1, "N";
      |CAMPO_MODIFICA #0#13, 1, "N";
      |CAMPO_MODIFICA #0#14, 1, "N";
      #0#11 = 0;      |PINTA #0#11;
      #0#12 = 0;      |PINTA #0#12;
      #0#13 = 0;      |PINTA #0#13;
      #0#14 = 0;      |PINTA #0#14;
   |SINO;
      |CAMPO_MODIFICA #0#06, 1, "N";
      |CAMPO_MODIFICA #0#07, 1, "N";
      #0#06 = 1;      |PINTA #0#06;
      #0#07 = 999;    |PINTA #0#07;

      |CAMPO_MODIFICA #0#11, 1, "S";
      |CAMPO_MODIFICA #0#12, 1, "S";
      |CAMPO_MODIFICA #0#13, 1, "S";
      |CAMPO_MODIFICA #0#14, 1, "S";
   |FINSI;
|FCAL;

|CALCULO leegrupo; |TIPO 0;
   |SI FSalida = 2; |ACABA; |FINSI;
   |SI #0Campo = 0; |ACABA; |FINSI;
   |ABRE #6;
   #dsmom035#0 = #0Campo;
   |LEE 001#6=;
   |SI FSalida ! 0;
      |MENAV men2;
      |ERROR;
   |FINSI;
   |CIERRA #6;
|FCAL;

|CALCULO alentrar;  |TIPO 8;
   |HAZ inicio;
   |HAZ Analitica; |SI enSwAna = 0; |ACABA; |FINSI;

   |HAZ DirAplicSt;
   |SI Haymodelo = 0; |ACABA; |FINSI;
   aAlfa1 = aPModelo + "/fich/";
   |PATH_DAT #6 aAlfa1;
|FCAL;

|| *********************************************************************

|CALCULO acumulados;
   |PDEFECTO #1,9,54;
   |GRABA 020#1a;
   |LIBERA #1;
|FCAL;

|DEFBUCLE cacreano0;     || acera las cuentas
   #1#1;
   ;
   "            ";
   "999999999999";
   be;
   NULL, acumulados;
|FIN;

|CALCULO ver_grupo2;
 |SI #117#2 ] incta; |Y #117#2 [ ficta;
     dn_cuent = #117#2;
     |HAZ dig_ana2;
     |SI dn_error  = 0;
         dn_error2 = 0;
     |FINSI;
 |FINSI;
|FCAL;

|DEFBUCLE selegrupo2;
 #117#1002;
 ;
 aCta2Gru, 000;
 aCta2Gru, 999;
 e;
 NULL, ver_grupo2;
|FIN;

|PROCESO ver_grupo;
 |SI #17#2 ] incta; |Y #17#2 [ ficta;
     dn_cuent = #17#2;
     |HAZ dig_ana2;
     |SI dn_error = 0;
         swsel = 1;
        |ERROR10;
     |FINSI;
 |SINO;
     alfa3 = #17#2 % 103;
     |SI alfa3 = "GRU";
         dn_error2 = 1;
         aCta2Gru  = #17#2;
         |BUCLE selegrupo2;
         |SI dn_error2 = 0;
             swsel = 1;
             |ERROR10;
         |FINSI;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE selegrupo;
 #17#1;
 ;
 #3#29, 000;
 #3#29, 999;
 e;
 NULL, ver_grupo;
|FIN;

|PROCESO ver_grupo_apte;
 nSwGApte = 1;
 |SI #13#6 ] incta; |Y #13#6 [ ficta;
     dn_cuent = #13#6;
     |HAZ dig_ana2;
     |SI dn_error = 0;
         swsel = 1;
        |ERROR10;
     |FINSI;
 |SINO;
      alfa3 = #13#6 % 103;
      |SI alfa3 = "GRU";
         dn_error2 = 1;
         aCta2Gru  = #13#6;
         |BUCLE selegrupo2;
         |SI dn_error2 = 0;
             swsel = 1;
             |ERROR10;
         |FINSI;
      |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE selgruApte;
 #13#1;
 ;
 #3#0, #3#1, #3#2, #3#3, #3#29, 000;
 #3#0, #3#1, #3#2, #3#3, #3#29, 999;
 e;
 NULL, ver_grupo_apte;
|FIN;

|PROCESO cuenta_ana;
 swsel = 0;
 |SI #3#29 ] incta; |Y #3#29 [ ficta;
     dn_cuent = #3#29;
     |HAZ dig_ana2;
     |SI dn_error = 0; swsel = 1; |FINSI;
 |SINO;
     alfa3 = #3#29 % 103;
     |SI alfa3 = "GRU";
         nSwGApte = 0;
         |BUCLE selgruApte;
         |SI nSwGApte = 0;
             |BUCLE selegrupo;
         |FINSI;
     |FINSI;
 |FINSI;
|FPRC;


|CALCULO conceptos;
   |SI #0#10 = "N"; |ACABA; |FINSI;
   sw_con = 0;
   |PARA y = 11; |SI y [ 14; |HACIENDO y = y + 1;
      |SI #0y ! 0;
          #dsmom036#0 = #0y;
          #dsmom036#1 = #camaasil#6;
          |LEE 001#7=;
          |SI FSalida = 0;
              sw_con = 1;
          |FINSI;
      |FINSI;
      |SI sw_con = 1; |SAL; |FINSI;
   |SIGUE;
|FCAL;

|CALCULO actuali;
   |SI #3#26 < #0#17; |O #3#26 > #0#18;
       |ACABA;
   |FINSI;

   sw_con = 1;
   |HAZ conceptos;
   |SI sw_con = 0; |ACABA; |FINSI;

   |HAZ cuenta_ana;
   |SI swsel = 0; |ACABA; |FINSI;

   aa = 100 + lon;
   aar = aa;
   traba = #camaasil#4 % aar;            || coge cuenta
   iniemp = 0;
   |PARA; |SI iniemp = 0; |HACIENDO;
      iniemp = 1;
      |SI prim = 0;
         cuant = traba;
         prim = 1;
         #cacuenta#0 = cuant;
         #cacuenta#1 = juan;
         |SI #cacuenta#0 = errant;
            prim = 0;
            |ACABA;
         |FINSI;
         |LIBERA #1;
         |LEE 101#1=;
         |SI FSalida ! 0;
            |LIBERA #1;
            |DDEFECTO #1;
            #cacuenta#0 = cuant;
            #cacuenta#1 = juan;
            |HAZ defect1;
            |PUSHV 0501 1980;
            |PINPA #0#1;
            |PINDA #0#1;
            |ENDATOS #1#1;
            |SI FSalida ! 0;
               |POPV;
               mensa = "Recuperando nivel: " + juan + "               ";
               |PINTA 2410,mensa;
               errant = #cacuenta#0;
               prim = 0;
               |ACABA;
            |FINSI;
            |GRABA 020#1b;
            |POPV;
            mensa = "Recuperando nivel: " + juan + "               ";
            |PINTA 2410,mensa;
         |FINSI;
      |FINSI;
      |SI cuant ! traba;
         |GRABA 020#1a;
         prim = 0;
         iniemp = 0;
      |FINSI;
   |SIGUE;
   |HAZ suma;
|FCAL;

|DEFBUCLE cacreano1;
   #3#3;
   0,6,21,28,5;
   "            ", #0#2, #0#4, 0001, #0#0, #0#6, #0#8, #0#15, juan;
   "999999999999", #0#3, #0#5, 9999, #0#1, #0#7, #0#9, #0#16,    9;
   be;
   NULL, actuali;
|FIN;

|CALCULO alsalir;  |TIPO 3;

  |DBASE aMas;
  aMas  = aMas + "def/anm00003";
  |CARGA_DEF aMas, anm0003@;
  |SI FSalida ! 0;
      |MENAV "    Error Cargado Fichero de Grupos Analiticos. Proceso Interrumpido";
      |ACABA;
  |FINSI;

  |HAZ copiando;
  |NOME_DAT #1 "cabalano";
  |PINTA 2410,"Poniendo a cero el Plan Contable";
  |BUCLE cacreano0;

   incta = #0#19; ficta = #0#20;
   |ABRE #1;
   |ABRE #7;
   |PARA juan = 1; |SI juan [ #canempre#13; |HACIENDO juan = juan + 1;
      prim = 0;
      |SI juan = 1; lon = #canempre#14; |FINSI;
      |SI juan = 2; lon = #canempre#15; |FINSI;
      |SI juan = 3; lon = #canempre#16; |FINSI;
      |SI juan = 4; lon = #canempre#17; |FINSI;
      |SI juan = 5; lon = #canempre#18; |FINSI;
      |SI juan = 6; lon = #canempre#19; |FINSI;
      mensa = "Recuperando nivel: " + juan + "              ";
      |PINTA 2410,mensa;
      prim = 0;
      |BUCLE cacreano1;
      |SI prim = 1;
         |GRABA 020#1a;
      |FINSI;
   |SIGUE;
   |CIERRA #1;
   |CIERRA #7;

   |DESCARGA_DEF #117;
|FCAL;

|CALCULO copiando;
   |DFICE fich;
   temporal1 = fich + "cacuenta.ixx";
   temporal2 = fich + "cabalano.ixx";
   |COPIA_FICHERO temporal1,temporal2;
   |SI FSalida ! 0;  |ACABA;  |FINSI;
   temporal1 = fich + "cacuenta.dat";
   temporal2 = fich + "cabalano.dat";
   |COPIA_FICHERO temporal1,temporal2;
   |SI FSalida ! 0;
      temporal2 = fich + "cabalano.ixx";
      |FBORRA temporal2;
      |ACABA;
   |FINSI;
   temporal1 = fich + "cacuenta.ddx";
   temporal2 = fich + "cabalano.ddx";
   |COPIA_FICHERO temporal1,temporal2;
|FCAL;

|CALCULO suma;
   |SI #camaasil#1 > #cacuenta#58;
      #cacuenta#58 = #camaasil#1;
   |FINSI;
   |SI #camaasil#0 ! 0;|Y #camaasil#0 ! 99;
      ini = #canempre#11;
      fecalf = #camaasil#1;
      mes = fecalf % 402;
      mesfec = mes;
      mesac = (mesfec - ini) + 1;
      |SI mesac [ 0;
         mesac = (13 - ini) + mesfec;
      |FINSI;
      a = (mesac * 3) + 9;
      b = a + 1;
      c = a + 2;
      |SI #camaasil#8 = "D";
         #cacuenta#a# = #cacuenta#a# + #camaasil#9;
         #cacuenta#51 = #cacuenta#51 + #camaasil#9;
         #cacuenta#c# = #cacuenta#a# - #cacuenta#b#;
         #cacuenta#53 = #cacuenta#51 - #cacuenta#52;
         |ACABA;
      |FINSI;
      #cacuenta#b# = #cacuenta#b# + #camaasil#9;
      #cacuenta#52 = #cacuenta#52 + #camaasil#9;
      #cacuenta#c# = #cacuenta#a# - #cacuenta#b#;
      #cacuenta#53 = #cacuenta#51 - #cacuenta#52;
      |ACABA;
   |FINSI;
   |SI #camaasil#0 = 0;
      |SI #camaasil#8 = "D";
         #cacuenta#9 = #cacuenta#9 + #camaasil#9;
         #cacuenta#51 = #cacuenta#51 + #camaasil#9;
         #cacuenta#11 = #cacuenta#9 - #cacuenta#10;
         #cacuenta#53 = #cacuenta#51 - #cacuenta#52;
         |ACABA;
      |FINSI;
      #cacuenta#10 = #cacuenta#10 + #camaasil#9;
      #cacuenta#52 = #cacuenta#52 + #camaasil#9;
      #cacuenta#11 = #cacuenta#9 - #cacuenta#10;
      #cacuenta#53 = #cacuenta#51 - #cacuenta#52;
      |ACABA;
   |FINSI;
   |SI #camaasil#8 = "D";
      #cacuenta#48 = #cacuenta#48 + #camaasil#9;
      #cacuenta#51 = #cacuenta#51 + #camaasil#9;
      #cacuenta#50 = #cacuenta#48 - #cacuenta#49;
      #cacuenta#53 = #cacuenta#51 - #cacuenta#52;
      |ACABA;
   |FINSI;
   #cacuenta#49 = #cacuenta#49 + #camaasil#9;
   #cacuenta#52 = #cacuenta#52 + #camaasil#9;
   #cacuenta#50 = #cacuenta#48 - #cacuenta#49;
   #cacuenta#53 = #cacuenta#51 - #cacuenta#52;
|FCAL;
