|FICHEROS;
   caconvec #0;
   caconvel #1,MANTE;
   cafantas #2;
   calendar #3;
   cavenaut #4;

   cacuenta #10;
   cacalend #13;

   caivaasi;
|FIN;

|VARIABLES;
   &eSwNoCrea = 0;
   &enCalen = 0;
   &entrada    = 1;
   &DVentas    = "";
   modoEntrada = 0;
   modoLinea   = 0;
   aAlfa1      = "";
   nSaldo      = 0;
   nNume1      = 0;
   nSwSal      = 0;
   swNo        = 0;
   nInkey      = 0;
   nT1         = 0;
   nT2         = 0;
   nElTrimes   = 0;
   swOperar    = 0;
  || i = 0;
   repa1 = 0;
   procesa  = "Estoy procesando espere un poco Por Favor ";
   blanco   = "                                          ";
   grabador = " Estoy grabando                           ";
   reparto  = " Estoy repartiendo  ";
   repart1  = " Estoy repartiendo el resto               ";
   repart2  = "";
   dd = 0;
   x = 0;
   y = 0;
   cero = 0;
   suma = 0;
   swclave = 1;
   p1 = "";
   cantidad = 0;
   canti  = 0;
   reparto1 = 0;
   rdon = 0;
   veces = 0;
   desv = 0;
   peso = 0;
   sumar = 0;
   total = 0;
   sumar1 = 0;
   sumar2 = 0;
   sumar3 = 0;
   sumar5 = 0;
   suma4 = 0;
   resto = 0;
   repa2 = 0;
   tpor = 0;
   rnd = 0.000000;
   pesoalfa = "";
   did = 0;
   num = 0;
   producto = 0.000000;
   producto1 = 0.000000;
   variable = 0;
   mod = "";
   mod1 = "";
   numero = 0;
   longi = "";
   longi1 = "";
   modnum = 0;
   alfapunto = "";
   nueve = 9;
   {366}cant = 0;
   {367}pesodia = 0;
   aMesPeso  = "";
   &snFiltr = "";
   &snFilAc = "";
   &snFilBa = "";
   &enAParam = 0;
   nsw = 0;
|FIN;

|INCLUYE dscont_i;

|| ************************************************************************
|| PROCESOS DE PANTALLA
|| ************************************************************************
|CALCULO no12; |TIPO 12;
|FCAL;

|CALCULO elTipo8; |TIPO 8;
 |HAZ inicial;
 |SI swerror = 2;
     |PTEC 807;
     |ACABA;
 |FINSI;
 |HAZ DirAplicSt;

 |ABRE #caivaasi;
 |DDEFECTO #caivaasi;
 |LEE 001#caivaasi.p;
 |CIERRA #caivaasi;
 snFilBa = #caivaasi#119;
|FCAL;

|RUTINA ClaveBaja1;
 #1#0 = #0#0;
 #1#1 = 1;
|FRUT;

|RUTINA ClaveAlta1;
 #1#0 = #0#0;
 #1#1 = 12;
|FRUT;

|PROCESO  PreLin; |TIPO 13;
     |SI swNo = 0;  swNo = 1;  |ACABA;  |FINSI;
     |BLIN 4;
     aAlfa1 = " "; aAlfa1 = aAlfa1 * 50;
     |PINTA 0522, aAlfa1;
     modoEntrada = (FEntrada / 100) ! 0;
     |SI modoEntrada  [ 1; |ACABA; |FINSI;
     |SI eaDepar = "S";
         eOpDep = 0;
         enActividad = #0#0;
         eaCodDep    = #0#0;
         |HAZ Actividad;
         |HAZ PintaFecha;
     |FINSI;

     nSwSal = 1;
     |ENTLINEAL #1#1, -2, 7, 21, 0, ClaveBaja1, ClaveAlta1;
     swNo = 1;
|FPRC;

|CALCULO AntesDep; |TIPO 7;
   |SI eaDepar = "S";
      |SI Haybasica = 1;
          |MENSA "    <F2> Consultar Actividades.";
      |SINO;
          |MENSA "    <F2> Consultar Departamentos";
      |FINSI;
   |FINSI;
   modoEntrada = (FEntrada / 100) ! 0;
   |SI modoEntrada = 1;
       #0#0 = #caivaasi#19; |PINTA #0#0;
   |FINSI;
|FCAL;

|CALCULO TrasDep; |TIPO 0;
   |SI eaDepar = "S";
       eOpDep = 0; || 1 = Consulta pantalla
       |SI FSalida = 10; eOpDep = 1;  |FINSI;
       enActividad = #0#0;
       eaCodDep    = #0#0;
       |HAZ Actividad;
       |SI eswLect = 1;
           |MENAV "    Error.! No Existe Actividad / Departamento";
           |ERROR;
       |SINO;
           #0#0 = eaCodDep;    |PINTA #0#0;
           #0#1 = eaNombreAct; |PINTA #0#1;
           |HAZ PintaFecha;
           |SI efActFin > "01.01.1900"; |Y snFilBa = "S";
               aAlfa1 = "    NAtencion!!!. Actividad con Fecha de BAJA " + efActFin + ", Desea introducir Facturas ? ";
               |CONFI aAlfa1;
               |SI FSalida ! 0;
                   |ERROR;
                   |ACABA;
               |FINSI;
           |FINSI;
           |SI eaEsCom = "S";
               |CONFI "    NAtencion!!!. Actividad Comunera, ¨ Desea introducir Movimientos ? ";
               |SI FSalida ! 0;
                   |ERROR;
                   |ACABA;
               |FINSI;
           |FINSI;
       |FINSI;
   |SINO;
       |C_M #0#1,1,"S";
   |FINSI;
   modoEntrada = (FEntrada / 100) ! 0;
   |SI modoEntrada = 1;
       |HAZ CalculoMes;
       |HAZ PreLin;
   |FINSI;
   enAParam = #0#0;
   |HAZ eActivParam;
|FCAL;

|PROCESO PintaFecha;
    aAlfa1 = " "; aAlfa1 = aAlfa1 * 50;
    |PINTA 0522, aAlfa1;
    #cafantas#10 = efActIni;
    #cafantas#11 = efActFin;
    |SI efActIni > "01.01.1900";
        |ATRI S; |PINTA 0522, "F.Inicio: "; |ATRI 0;
        |PINTA 0532, #cafantas#10;
    |FINSI;
    |SI efActFin > "01.01.1900";
        |ATRI S; |PINTA 0544, "F.Fin: "; |ATRI 0;
                 |PINTA 0551, #cafantas#11;
    |FINSI;
|FPRC;


|CALCULO LaDescripcion; |TIPO 7;
 |BLIN 4;
 modoEntrada = (FEntrada / 100) ! 0;
 |SI modoEntrada = 2;
     |HAZ CalculoCuenta;
     |HAZ PreLin;
 |FINSI;
|FCAL;

|| ------- Cuenta

|CALCULO Cuenta; |TIPO 6;
     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;
|FCAL;

|CALCULO TrasCuenta; |TIPO 0;
   emCta    = 1;
   eaCuenta = #0Campo;
   |HAZ SacaNivel;
   |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea
   |HAZ LeeCuenta;
   |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea
   |HAZ CalculoCuenta; |PINTA #0#5;
   |HAZ PreLin;
|FCAL;

|CALCULO TrasCalendario; |TIPO 0;

  |ABRE #13;
  #13#0 = #0Campo;
  |LEE 001#13=;
  |SI FSalida ! 0;
      |MENAV "    No existe Calendario";
      |ERROR;
      |CIERRA #13;
      |ACABA;
  |FINSI;
  #cacalend#1 = enEjercicio;
  |GRABA 020#13a;
  |CIERRA #13;
  |PINTA 2345, #cacalend#159;

  |PUSHV 2301 2380;
  enCalen = #13#0;
  eSwNoCrea = 1;
  |HAZ CreaCalend;
  |POPV;

  #0#8  = #calendar#13; |PINTA #0#8;

  |ABRE #1;
  |PARA NumMes = 1; |SI NumMes [ 12; |HACIENDO NumMes = NumMes  + 1;
        #1#0 = #0#0;  || departamento
        #1#1 = NumMes;
        |LEE 101#1=;
        |SI FSalida ! 0;
            |DDEFECTO #1;
            #1#0 = #0#0;
            #1#1 = NumMes;
            |HAZ Nuevo;
            |GRABA 020#1b;
        |FINSI;
        |SI #1#4 = 0;
            #1#5 = #0Campo;
        |FINSI;
        |GRABA 020#1a;
        |LIBERA #1;
  |SIGUE;
  |CIERRA #1;
  |HAZ PreLin;
|FCAL;

|PROCESO BorraLineas0;
     |SI #1#13 = "*";
         nsw = 1;
     |FINSI;
     |BORRA 140#1a;
     |LIBERA #1;
|FPRC;

|DEFBUCLE BorraLineas;
     #1#1;
     ;
     #0#0, 01;
     #0#0, 99;
     be;
     NULL, BorraLineas0;
|FIN;

|PROCESO BorraVentas0;
     |BORRA 140#4a;
     |LIBERA #4;
|FPRC;

|DEFBUCLE BorraVentas;
     #4#1;
     ;
     1, 1, #0#0;
     1, 4, #0#0;
     be;
     NULL, BorraVentas0;
|FIN;

|CALCULO Tipo2_c;  |TIPO 2;
     modoEntrada = (FEntrada / 100) ! 0;
     |SI modoEntrada ! 3;  |ACABA;  |FINSI;
     nsw = 0;
     |BUCLE BorraLineas;
     |SI nsw = 1;
         |CONFI "    SDesea Borrar el Reparto de Ventas que existe";
         |SI FSalida = 0;
             |BUCLE BorraVentas;
         |FINSI;
     |FINSI;
|FCAL;


|CALCULO Tipo3_c; |TIPO 3;
     modoEntrada = (FEntrada / 100) ! 0;
     nSwSal = 0;
     |ENTLINEAL #1#1, -2, 2, 21, 0, ClaveBaja1, ClaveAlta1;
     |LIBERA #0;
     |CONFI "    SDesea Repartir los Periodo Seleccionados";
     |SI FSalida = 0;
         |HAZ Reparto;
         |PINTA 2425, blanco;
         |HAZ PreLin;
     |FINSI;
     swOperar  = 1;
     DVentas = #0#0;
|FCAL;

|CALCULO Tipo9; |TIPO 30;
     |SI swOperar = 0; |ACABA; |FINSI;
     |CONFI "    SDesea Realizar La Contabilizacion Ahora ";
     |SI FSalida = 0;
         |CIERRAT #0;
         |CIERRAT #1;
         |SUB_EJECUTA_NP "capasven";
         |ABRET #1;
         |ABRET #0;
         |HAZ PreLin;
     |FINSI;
     swOperar = 0;
|FCAL;

||********************************
|PROCESO buenredon; |TIPO 0;
 |SI #1#8 = 0;
     #1#8 = 1;
     |SI EURODB = 2; #1#8 = 0.01; |FINSI;
     |PINTA #1#8;
 |FINSI;
|FPRC;

|PROCESO CalculoMes;
  |ABRE #1;
  |PARA NumMes = 1; |SI NumMes [ 12; |HACIENDO NumMes = NumMes  + 1;
        #1#0 = #0#0;  || departamento
        #1#1 = NumMes;
        |LEE 101#1=;
        |SI FSalida ! 0;
            |DDEFECTO #1;
            #1#0 = #0#0;
            #1#1 = NumMes;
            |GRABA 020#1b;
        |FINSI;
        |HAZ Nuevo;
        |GRABA 020#1a;
        |LIBERA #1;
  |SIGUE;
  |CIERRA #1;
|FPRC;

|PROCESO Nuevo;
  NumMes = #1#1;
  |HAZ NombreMes;
  #1#2 = NombreMes;
  #1#6 = NumMes;
  |SI #caivaasi#46 = "T";
      #1#6 = 1;
      |SI NumMes ] 4;  #1#6 = 2; |FINSI;
      |SI NumMes ] 7;  #1#6 = 3; |FINSI;
      |SI NumMes ] 10; #1#6 = 4; |FINSI;
  |SINO;
      |SI #caivaasi#46 = "A";
          #1#6 = 0;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO CalculoCuenta;
  |ABRE #10;
  #10#0 = #0#2;
  |LEE 001#10=;
  |SI FSalida ! 0;
      |DDEFECTO #10;
  |FINSI;
  |CIERRA #10;

  #0#3 = #10#2;
  |ABRE #1;
  nSaldo = #10#11;
  |PARA NumMes = 1; |SI NumMes [ 12; |HACIENDO NumMes = NumMes  + 1;
        #1#0 = #0#0;  || departamento
        #1#1 = NumMes;
        |LEE 101#1=;
        |SI FSalida ! 0;
            |DDEFECTO #1;
            #1#0 = #0#0;
            #1#1 = NumMes;
            |HAZ Nuevo;
            |GRABA 020#1b;
        |FINSI;
        nNume1 = 11 + (NumMes * 3)
        nSaldo = nSaldo + #10nNume1;
        #1#3 = nSaldo;
        |GRABA 020#1a;
        |LIBERA #1;
  |SIGUE;
  #0#5 = nSaldo;
  |CIERRA #1;
|FPRC;

||**********************************************************************
|| ****** Calculos de Lineas
||**********************************************************************

|CALCULO soloModi; |TIPO 11;
  nInkey = FSalida;
  |SI nInkey = 5;                |ERROR; |FINSI;
  |SI nInkey = 3; |Y #1#1 = 12;  |ERROR; |FINSI;
  |SI nInkey = 10;
      |SI #1#11 = "S";
          #1#11 = "N";
      |SINO;
          #1#11 = "S";
      |FINSI;
      |GRABA 020#1a;
      |PINTA #1#11;
      |PTEC 806;
  |FINSI;
  nInkey = 0;
|FCAL;

|CALCULO Tipo1_l; |TIPO 1;
  modoLinea = (FEntrada / 100) ! 0;
  |SI modoLinea = 3;
      |ERROR;
  |FINSI;
|FCAL;

|CALCULO Tipo2_l; |TIPO 2;
  |PINTA #0#5;
  #0#6 = #0#6 +. #1#4; |PINTA #0#6;
|FCAL;

|CALCULO AntesPer; |TIPO 7;
 |SI #caivaasi#46 = "A";
     #1#6 = 0; |PINTA #1#6;
     |C_M #1#6, 1, "N";
 |SINO;
     |C_M #1#6, 1, "S";
 |FINSI;
|FCAL;

|CALCULO EsBueno; |TIPO 0;
  nNume1 = #1#6;
  |SI #caivaasi#46 = "A"; |ACABA; |FINSI;
  |SI #caivaasi#46 = "T";
      |SI nNume1 > 4; |ERROR; |FINSI;
  |FINSI;
|FCAL;

|CALCULO noSuper; |TIPO 0;
  |SI #1#7 > #1#4;
      |ERROR;
  |FINSI;
|FCAL;

|CALCULO Cuadro; |TIPO 7;
 |PUSHV  1433 1874;
 |CUADRO 1433 1774;
 |ATRI R;
 |SI #1#13 = "*";
     |PINTA 1534, " Existen Ventas Contado en Este Periodo.";
 |SINO;
     |PINTA 1534, " NO hay Ventas Contado en Este Periodo. ";
 |FINSI;
 |SI #1#12 = "S";
     |PINTA 1634, " Ya se ha contabilizado Ventas Contado. ";
 |SINO;
     |PINTA 1634, " Aun no se han Contabilizado Ventas.    ";
 |FINSI;
 |ATRI r;
|FCAL;

|CALCULO repon; |TIPO 0;
   |POPV;
|FCAL;

|| ********************************* Reparto

|PROCESO elReparto0;
   |ATRI P;
   |PINTA 2425,procesa;
   |ATRI 0;

   #13#0  = #1#5;
   |LEE 011#13=;
   |SI FSalida ! 0;
       |MENAV "    Error. No Existe Calendario, no se puede realizar el Proceso";
       |PINTA 2425, blanco;
       |ACABA;
   |FINSI;

   |PUSHV 2301 2380;
   enCalen = #13#0;
   eSwNoCrea = 1;
   |HAZ CreaCalend;
   |POPV;

   NumMes = #1#1;
   |HAZ ModCalenda;

   |HAZ random1;

   |HAZ cargapesos;

   |SI peso = 0; |VETE NoSalto; |FINSI;

   num      = 0;
   cantidad = #1#4;
||   |SI cantidad = 0; |VETE NoSalto; |FINSI;
   reparto1 = #1#7;
   rdon     = #1#8;
   veces    = #1#9;
   desv     = #1#10;
   canti = cantidad / rdon;
   repa1 = reparto1 / rdon;
   repa1 = repa1 ! EURODB;
   sumar = repa1 / peso;
   sumar = sumar ! EURODB;

   |HAZ asigna1;      || asigna cantidad fija
   |HAZ asigna2;      || asigna en dd veces;

   resto = canti - total;
   |BLIN 24;
   |ATRI R;
   |PINTA 2425,repart1;
   |ATRI 0;
   |SI resto ! 0;
      sumar = resto / peso;
      sumar = sumar - 0.5;
      sumar = sumar ! EURODB;
      |SI sumar ! 0;
          |HAZ resto1;                          ||asigna resto entero;
          resto = canti -total;
      |FINSI;
   |FINSI;
   Ipesodia = pesodia1 <-;
   Icant = cant1 <-;
   |SI resto < 0;
       resto = -resto;
   |FINSI;
   |PARA x = cero;|SI x < 366;|HACIENDO x = x + 1;
         |SI resto = 0;  |SAL;  |FINSI;
         |SI resto < pesodia;
             sumar = resto;
         |SINO;
             sumar = pesodia;
         |FINSI;
         |SI resto < 0;
             cant = cant - sumar;
         |SINO;
             cant = cant + sumar;
         |FINSI;
         resto = resto - sumar;
         Ipesodia = Ipesodia + 1;
         Icant = Icant + 1;
   |SIGUE;

   total = total - resto;

   |BLIN 24;
   |ATRI R;
   |PINTA 2425,grabador;
   |ATRI 0;
   Icant = cant1 <-;
   |PARA x = cero;|SI x < 366;|HACIENDO x = x + 1;
         cant = cant * rdon;
         Icant = Icant + 1;
   |SIGUE;

   || ................................... Graba fichero de ventas automaticas;

   Icant = cant1 <-;

   |PARA y = 1;  |SI y [ 4;  |HACIENDO y = y + 1;
         swclave = 1;
         #cavenaut#0  = 1;
         #cavenaut#1  = y;
         #cavenaut#95 = #0#0;
         |LEE 110#4=;
         |SI FSalida ! 0;
             |DDEFECTO #4;
             swclave = 0;
        |FINSI;

         #cavenaut#0  = 1;
         #cavenaut#1  = y;
         #cavenaut#95 = #0#0;
         num = 1;
         #cavenaut#94 = #calendar#13;
         |SI #cavenaut#94 = 0; #cavenaut#94 = enEjercicio; |FINSI;
         |PARA x = 1;|SI x [ 92;|HACIENDO x = x + 1;

               num = num + 1;
               |SI num ] nT1; |Y num [ nT2; |Y y = nElTrimes;
                   #cavenaut#num# = cant;
               |FINSI;
         Icant = Icant + 1;
         |SI x = 91;  |Y y [ 2;
            |SAL;
         |FINSI;
      |SIGUE;

      |SI y = nElTrimes;
          |SI swclave = 1;
             |GRABA 020#4a;
          |SINO;
             |GRABA 020#4n;
          |FINSI;
      |FINSI;
      |LIBERA #4;
   |SIGUE;
   #1#13 = "*";

|ET NoSalto;
   #1#11 = "N";
   #1#12 = " ";  ||ya no esta contabilizado, pues es otro reparto
   |GRABA 020#1a;
   |LIBERA #1;
|FPRC;

|DEFBUCLE elReparto;
 #1#1;
 11;
 #0#0, 1, "S";
 #0#0,12, "S";
 be;
 NULL, elReparto0;
|FIN;

|PROCESO Reparto;
  |ABRE #13;
  |ABRE #4;
  |BUCLE elReparto;
  |CIERRA #4;
  |CIERRA #13;
|FPRC;

|PROCESO cargapesos;
   peso = 0;
   Ipesodia = pesodia1 <-;                             ||PROCESO
   |PARA x = 1;  |SI x [ 12;  |HACIENDO x = x + 1;
      num = 0;
      |PARA y = 1;  |SI y [ 31;  |HACIENDO y = y + 1;
         |SI y = 31;
            |SI x = 4;  |O x = 6;  |O x = 9;  |O x = 11;
               |SAL;
            |FINSI;
         |FINSI;
         |SI y = 30;  |Y x = 2;
            |SAL;
         |FINSI;
         num = num + 1;
         pesoalfa = num + "01";
         p1 = #calendar#x# % pesoalfa;
         peso = peso + p1;
         pesodia = p1;
         Ipesodia = Ipesodia + 1;
      |SIGUE;
   |SIGUE;
|FPRC;

|PROCESO asigna1;
   Ipesodia = pesodia1 <-;
   Icant = cant1 <-;
   |PARA x = cero;|SI x < 366;|HACIENDO x = x + 1;
      cant = sumar * pesodia;
      total = total + cant;
      Icant = Icant + 1;
      Ipesodia = Ipesodia + 1;
   |SIGUE;
|FPRC;

|PROCESO asigna2;
   dd = 1;
   |PARA dd = 1;  |SI dd [ veces;  |HACIENDO dd = dd + 1;
      resto = canti - total;
      did = veces + 1 - dd;
      |SI resto = 0;  |SAL;  |FINSI;
      repa2 = resto / did;
      repa2 = repa2 - 0.5;
      repa2 = repa2 ! 0;
      sumar1 = repa2 / peso;
      sumar1 = sumar1 - 0.5;
      sumar1 = sumar1 ! 0;

      ||Distribucion aleatoria;
      Ipesodia = pesodia1 <-;
      Icant = cant1 <-;
      |BLIN 24;
      |ATRI R;
      repart2 = reparto + dd + "a vez. ";
      |PINTA 2425,repart2;
      |ATRI 0;
      |PARA x = cero;|SI x < 366;|HACIENDO x = x + 1;
         |HAZ randon;
         tpor = rnd * (desv * 2) - desv;
         tpor = tpor ! 0;
         sumar5 = (sumar1 * tpor) / 100
         sumar5 = sumar5 ! 0;
         sumar2 = sumar1 + sumar5;
         sumar3 = sumar2 * pesodia;
         cant = cant + sumar3;
         total = total + sumar3;
         Ipesodia = Ipesodia + 1;
         Icant = Icant + 1;
      |SIGUE;
   |SIGUE;
|FPRC;

|PROCESO resto1;
   Ipesodia = pesodia1 <-;
   Icant = cant1 <-;
   |PARA x = cero;|SI x < 366;|HACIENDO x = x + 1;
      sumar3 = sumar * pesodia;
      cant = cant + sumar3;
      total = total + sumar3;
      Ipesodia = Ipesodia + 1;
      Icant = Icant + 1;
   |SIGUE;
|FPRC;


|PROCESO randon;
   producto = producto / 3.141592;
   mod = producto;
   numero = 0;
   |ET punto;
   numero = numero + 1;
   alfapunto = numero + "01";
   mod1 = mod % alfapunto;
   |SI mod1 ! ".";
      |VETE punto;
   |FINSI;
   longi = mod % A0;
   alfapunto = numero;
   longi1 = longi % A0;
   |SI longi1 = "1";
      longi = "0" + longi;
   |FINSI;
   mod1 = alfapunto + longi;
   mod = mod % mod1;
   mod = "0" + mod;
   modnum = mod;
   rnd = modnum / 3.141592;
   |SI producto < 1;
      producto = producto * 1000000;
   |FINSI;
   |SI producto < 1;
      producto = producto1;
   |FINSI;
|FPRC;

|PROCESO random1;
   suma4 = 0;
   Ipesodia = pesodia1 <-;
   |PARA x = cero;|SI x < 183;|HACIENDO x = x + 1;
      |SI pesodia = 0;
         variable = 1;
      |SINO;
         variable = pesodia;
      |FINSI;
      suma4 = suma4 + variable;
      Ipesodia = Ipesodia + 1;
   |SIGUE;
   producto1 = suma4 * #0#4 * 1;
   producto = producto1;
|FPRC;

|PROCESO ModCalenda;  || Borro todos los pesos
 aMesPeso = #3NumMes;
 |PDEFECTO #3,1,13;   ||el resto en Blanco
 #3NumMes = aMesPeso;

 |SI NumMes = 1;  nElTrimes = 1; nT1 = 2;  nT2 = 32; |FINSI;
 |SI NumMes = 2;  nElTrimes = 1; nT1 = 33; nT2 = 61; |FINSI;
 |SI NumMes = 3;  nElTrimes = 1; nT1 = 62; nT2 = 92; |FINSI;

 |SI NumMes = 4;  nElTrimes = 2; nT1 = 2;  nT2 = 31; |FINSI;
 |SI NumMes = 5;  nElTrimes = 2; nT1 = 32; nT2 = 62; |FINSI;
 |SI NumMes = 6;  nElTrimes = 2; nT1 = 63; nT2 = 92; |FINSI;

 |SI NumMes = 7;  nElTrimes = 3; nT1 = 2;  nT2 = 32; |FINSI;
 |SI NumMes = 8;  nElTrimes = 3; nT1 = 33; nT2 = 63; |FINSI;
 |SI NumMes = 9;  nElTrimes = 3; nT1 = 64; nT2 = 93; |FINSI;

 |SI NumMes = 10;  nElTrimes = 4; nT1 = 2;  nT2 = 32; |FINSI;
 |SI NumMes = 11;  nElTrimes = 4; nT1 = 33; nT2 = 62; |FINSI;
 |SI NumMes = 12;  nElTrimes = 4; nT1 = 63; nT2 = 93; |FINSI;

 repa1    = 0;
 suma     = 0;
 cantidad = 0;
 canti    = 0;
 reparto1 = 0;
 rdon     = 0;
 veces    = 0;
 desv     = 0;
 peso     = 0;
 sumar    = 0;
 total    = 0;
 suma4    = 0;
 resto    = 0;
 repa2    = 0;
 tpor     = 0;
 rnd = 0.000000;
 producto = 0.000000;
 producto1 = 0.000000;

   Icant = cant1 <-;
   |PARA x = cero;|SI x < 366;|HACIENDO x = x + 1;
         cant = 0;
         Icant = Icant + 1;
   |SIGUE;
|FPRC;
