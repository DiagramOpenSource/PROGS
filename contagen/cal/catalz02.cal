|FICHEROS;
    catalz02 #0;
    catalm01 #1;

    canempre #30;
    caselem1 #998;

    catalw02 #999,MANTE;
|FIN;

|VARIABLES;
    &entrada  = 1;
    sw        = 0;
    sw_act    = 0;
    aFichero  = "";
    nOpera    = 0;
    nContador = 0;
    nSwHay    = 0;
    aAlfa1    = "";
    nSwClave  = 0;
    nRango    = 0;
|FIN;

|INCLUYE dscont_i;

|CALCULO cuenta; |TIPO 0;
     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;
|FCAL;

|CALCULO juntos; |TIPO 0;
 |SI sw_act = 0;
     #0#10 = #0#14;
     |PINTA #0#10;
 |FINSI;
|FCAL;

|CALCULO nodepar; |TIPO 7;
   |C_M #0#17, 1, eaDepar;
   |C_M #0#18, 1, eaDepar;
|FCAL;

|CALCULO Miro;
   aAlfa1 = #0#19;
   |C_M #0#20, 1, aAlfa1;
   |SI aAlfa1 = "N";
       #0#20 = "S"; |PINTA #0#20;
   |FINSI;
|FCAL;

|| ***************************************************************
|PROCESO ElAuxiliar;
   #1#18 = enEmpresa;
   #1#0  = #999#0;
   |LEE 141#1=;
   |SI FSalida = 0;
        |BORRA 020#1a;
   |FINSI;
   |LIBERA #1;
|FPRC;

|DEFBUCLE ElAuxiliar;
  #999#1;
  17;
  000001, "S";
  999999, "S";
  ;
  NULL, ElAuxiliar;
|FIN;

|PROCESO AltaAuxiliar;
    nContador = nContador + 1;

    #999#0  = #1#0;       || Numero Talon
    #999#1  = #1#1;       || Cuenta Cargo
    #999#2  = #1#14;      || Titulo Cargo
    #999#3  = #1#15;      || Nif
    #999#4  = #1#3;       || Cuenta Abono
    #999#5  = #1#22;      || Titulo Abono
    #999#6  = #1#5;       || Fecha Emision
    #999#7  = #1#6;       || Fecha Vto
    #999#8  = #1#9;       || Importe
    #999#9  = #1#10;      || Impresa Carta
    #999#10 = #1#12;      || Impreso Pagare
    #999#11 = #1#7;       || Contabilizado Emision
    #999#12 = #1#8;       || Contabilizado Vto
    #999#13 = #1#16;      || Tipo Recibo
    #999#14 = #1#17;      || Observaciones
    #999#15 = #1#20;      || Departamento
    #999#16 = #1#21;      || Descripcion Departamento

    #999#17 = #0#20;
    |GRABA 020#999n;
|FPRC;

|PROCESO impresion;
  |SI #0#10 ! "A";
      |SI #0#10 ! #1#10;  |ACABA;  |FINSI;  || impresa Carta
  |FINSI;
  |SI #0#14 ! "A";
      |SI #0#14 ! #1#12;  |ACABA;  |FINSI;  || impreso talon
  |FINSI;
  |SI #0#11 ! "A";
      |SI #0#11 ! #1#07;  |ACABA;  |FINSI;  || emision
  |FINSI;
  |SI #0#12 ! "A";
      |SI #0#12 ! #1#08;  |ACABA;  |FINSI;  || vencimiento
  |FINSI;

  |SI nOpera = 1;
      |HAZ AltaAuxiliar;
  |SINO;
      |BORRA 020#1a;
  |FINSI;
  |LIBERA #1;
  sw = 1;
|FPRC;

|DEFBUCLE cabajtal0;
    #1#1;
    5,6,1,3,16,20;
    enEmpresa, #0#0,#0#2,#0#4,#0#6,#0#8,#0#15, #0#17; || talon,F.emi,F.vto,Prove,Banco.
    enEmpresa, #0#1,#0#3,#0#5,#0#7,#0#9,#0#16, #0#18;
    be;
    NULL,impresion;
|FIN;

|PROCESO ClaveAlta999;
     #999#0 = 000001;
|FPRC;

|PROCESO ClaveBaja999;
     #999#0 = 999999;
|FPRC;

|PROCESO inf;
     #999#0 = 000001;
     #999#17 = "S";
|FPRC;

|PROCESO sup;
     #999#0 = 999999;
     #999#17 = "S";
|FPRC;

|PROCESO evento;
      |LEE 041#999=;
      |SI #999#17 = "S";
          #999#17 = "N";
      |SINO;
          #999#17 = "S";
      |FINSI;
      |GRABA 020#999a;
|FPRC;

|PROCESO VerEnPantalla;
/*
     |ATRI S; |PINTA 0706, "Empresa: "; |ATRI 0;
     |PINTA 0715, #canempre#2;
     |PINTA 0721, #canempre#1;
     |ENTLINEAL #1#999, -1, 4, 22, 0, ClaveAlta999, ClaveBaja999;
     |CONFI "2400SDesea borrar los Asientos Seleccionados ";
     |SI FSalida = 0;
         |ABRE #1;
         |BUCLE ElAuxiliar;
         |CIERRA #1;
     |FINSI;
*/
   nSwClave = 0;
   |ABRE #999;
   |PINPA #0#999;

   |PARA; |SI nSwClave ! -1; |HACIENDO;

          |ATRI I;  |PINTA 0401, "Empresa:";
                    |PINTA 0410, #canempre#2;
                    |PINTA 0416, #canempre#1; |ATRI 0;
              |ATRI P;
              |PINTA 0461,"<Enter>=Cambiar S/N";
              |ATRI 0;
          nRango = 2 + 4 + 8;
          |LINEAL_SIMPLE #1#999,nRango,0601,2280,inf,sup,NULL;
          nSwClave = FSalida;
          |SI nSwClave ! -1;
              |HAZ evento;
          |FINSI;
   |SIGUE;
   |CIERRA #999;
   |ENTLINEAL #2#999, -1, 7, 22, 0, ClaveAlta999, ClaveBaja999;
   |CONFI "2400SDesea borrar los Vencimientos Seleccionados ";
   |SI FSalida = 0;
       |ABRE #1;
       |BUCLE ElAuxiliar;
       |CIERRA #1;
   |FINSI;
|FPRC;

|PROCESO HazTodo;
    |SI #0#19 = "S";
        nContador = 0;
        |DELINDEX #999;

        nOpera = 1;
        |ABRE #999;
        |BUCLE cabajtal0;
        |CIERRA #999;
        |SI nContador ! 0;
            |HAZ VerEnPantalla;
        |FINSI;
    |SINO;
        nOpera = 0;
        |BUCLE cabajtal0;
    |FINSI;
|FPRC;

|CALCULO baja; |TIPO 3;

   |HAZ DirAplicSt;
   |SI Haybasica = 1; |Y #0#18 = "zz"; #0#18 = "99"; |FINSI;

   aFichero = ("1b" + Usuario) % 108;
   |NOME_DAT #999 aFichero;

   sw = 0;
   |SI #48#27 = "S";
       |HAZ MultiEmpresa;
   |SINO;
       |DFICE dirfich0; |QBF dirfich0;
       |HAZ HazTodo;
   |FINSI;
   |SI sw = 0;
       |MENAV "    Seleccion vacia.";
   |FINSI;
|FCAL;

|| *****************************

|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
   #caselem1#1;
   ;
   000001;
   999999;
   ;
   NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;
