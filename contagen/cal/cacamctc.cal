|FICHEROS;
    cacamctc #0;         || pantalla
    cacamctl #11,MANTE;  || Lineas

   :/modelos/def/dsmom030 #330;

    cacamcts #1,MANTE;
    cacamctx #10,MANTE;
    cacuenta #69;    || Cuentas

    canempre #30;
|FIN;

|VARIABLES;
    &entrada    = 1;
    &swTabla    = 0;
    &aCampo0    = "";
    &nCampo1    = 0;
    &aCampo2    = "";
    &aCampo3    = "";
    &nCampo4    = 0;
    &aCampo5    = "";
    &aCampo6    = "";
    &aCampo9    = "";
    &nCampo10   = 0;
    &nCampo11   = 0;
    &fCampo12   = @;
    &fCampo13   = @;
    &nCampo14   = 0;
    &nCampo15   = 0;
    &nCampo16   = 0;
    &nCampo17   = 0;
    &pathbal    = "";
    &balance    = 1;
    aAlfa1      = "";
    aMultiEmp   = "";
    &nLaEmpresa = 0;
    &nSwTipo    = 0;

    mens      = "";
    x         = 0;
    sw_ya     = 0;
    sw_ya0    = 0;
    aAlfa     = "";
    nPara1    = 0;
    nNume1    = 0;
    nInkey    = 0;
    nAny      = 0;
    aAny      = "";
    aMes      = "";
    aDia      = "";
    fFechaBlq = @;
    fFechaFin = @;

|FIN;

|INCLUYE dscont_i;
____________________________________/ CALCULOS DE PANTALLA

|PROCESO cuenta; |TIPO 6;
     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;
|FPRC;

|PROCESO cuefin; |TIPO 0;
     |SI #0Campo = doce; |ERROR; |ACABA; |FINSI;

     emCta    = 1;
     eaCuenta = #0#0;
     |HAZ SacaNivel;
     |SI eswCta ! 0;  |ERROR; |ACABA; |FINSI;  ||Cuenta erronea
     #0#1 = enNivel;

     |HAZ LeeCuenta;
     |SI eswCta ! 0;  |ERROR; |ACABA; |FINSI;  ||Cuenta erronea
     #0#2 = eaNomCta; |PINTA #0#2;
|FPRC;


|| //////////////////////////////////////////////////
|PROCESO cuenta11; |TIPO 6;
     eaCuenta = #11Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #11Campo = eaCuenta;
     |PINTA #11Campo;
|FPRC;

|PROCESO cuefin11; |TIPO 0;
     |SI #11#4 = doce;
         #11#6 = ""; |PINTA #11#6;
         |ACABA;
     |FINSI;
     emCta    = 1;
     eaCuenta = #11#4;
     |HAZ SacaNivel;
     |SI eswCta ! 0;  |ERROR; |ACABA; |FINSI;  ||Cuenta erronea
     #11#5 = enNivel;

     |HAZ LeeCuenta;
     |SI eswCta ! 0;  |ERROR; |ACABA; |FINSI;  ||Cuenta erronea
     #11#6 = eaNomCta; |PINTA #11#6;
|FPRC;

|PROCESO Tipo0ctl; |TIPO 0;
     |ABRE #330;
     #330#0 = #11#2;
     |LEE 040#330=;
     |SI FSalida ! 0;
         |MENAV "    Error, no existe concepto";
         |ERROR;
     |SINO;
         #11#3 = #330#1; |PINTA #11#3;
     |FINSI;
     |CIERRA #330;
|FPRC;

|PROCESO Tipo11ctl; |TIPO 11;
     nInkey = FSalida;

     |SI nInkey = 10; |O nInkey = 11; |O nInkey = 12;

         |SI nInkey = 10; |HAZ Limites; |FINSI;
         |SI nInkey = 11; |Y aMultiEmp = "S";
             |HAZ EntraMultiEmp;
         |FINSI;
         |SI nInkey = 12; |HAZ Copiar; |FINSI;

         |ERROR;

     |FINSI;
|FPRC;

|PROCESO Copiar;
     |CONFI "    SDesea copiar la tabla a otras Empresa";
     |SI FSalida = 0;

         aAlfa1 = "caut0016;" + aMultiEmp;
         |SUB_EJECUTA_NP aAlfa1;
     |FINSI;
|FPRC;

|PROCESO Selec12; |TIPO 12;
|FPRC;

||**************************************************************************
|PROCESO CalFechas;
   nAny = #canempre#26;
   |SI #canempre#11 > #canempre#12;
       nAny = nAny + 1;
       |SI nAny > 99; nAny = nAny - 100; |FINSI;
   |FINSI;
   |SI nAny > 80;
       nAny = nAny + 1900;
   |SINO;
       nAny = nAny + 2000;
   |FINSI;
   aAny = nAny;
   aMes = ("00" + #canempre#12) % -102;
   aDia = "31";
   |SI #canempre#12 = 4; |O #canempre#12 = 6; |O #canempre#12 = 9; |O #canempre#12 = 11;
       aDia = "30";
   |FINSI;
   |SI #canempre#12 = 2;
       aDia = "28";
      |SI nAny = 2000; |O nAny = 2004; |O nAny = 2008; |O nAny = 2012;
          |O nAny = 2016; |O nAny = 2020; |O nAny = 2024; |O nAny = 2028;
         aDia = "29";
      |FINSI;
   |FINSI;
   aAlfa = aDia + "." + aMes + "." + aAny;
   fFechaFin = aAlfa;
|FPRC;

|PROCESO ElBloqueo;
  nSwTipo = 0;
  fFechaBlq = fec3;
  fFechaFin = fec2;
  aAlfa1    = ("00000" + #canempre#2) % -105 + " " + #canempre#1; |QBF aAlfa1;

  |SI aMultiEmp = "S";
      fFechaBlq = #canempre#34;
      |HAZ CalFechas;
  |FINSI;

  |SI fFechaBlq > "01.01.0000";
      |SI fFechaBlq ] fFechaFin;  nSwTipo = 1;  |FINSI;
      |SI fFechaBlq < fFechaFin;
          |SI #10#1 = "N";
              nSwTipo = 2;
          |SINO;
              |SI fFechaBlq ] #10#4; nSwTipo = 2; |FINSI;
          |FINSI;
      |FINSI;
  |FINSI;

  |SI nSwTipo ! 0; |Y aMultiEmp = "S";
      aAlfa  = "0000Empresa: " + aAlfa1;
      |MENAV aAlfa;
  |FINSI;

  |SI nSwTipo = 1;
      aAlfa  = "0000Empresa bloqueada a fecha Fin de Ejercicio. Proceso no permitido";
      |MENAV aAlfa;
  |FINSI;

  |SI nSwTipo = 2;
      nSwTipo = 1;

      aAlfa = "0000NEmpresa bloqueada a " + fFechaBlq + ". Desea continuar con el cambio";
      |CONFI aAlfa;
      |SI FSalida = 0; nSwTipo = 0; |FINSI;

  |FINSI;
|FPRC;

||**************************************************************************
|PROCESO camcta;
     aAlfa = #11#4; |QBF aAlfa;
     |SI aAlfa = ""; |ACABA; |FINSI;

     swTabla = 1;
     aCampo0 = #11#0;
     nCampo1 = #11#1;
     aCampo2 = #0#2;    ||Descripcion
     aCampo3 = #11#4;
     nCampo4 = #11#5;
     aCampo5 = #11#6;
     aCampo6 = #11#7;
     aCampo9  = "S";
     |SI #10#1 = "N";
          |DDEFECTO #10;
          #10#4 = fec1;
          #10#5 = fec2;
     |FINSI;
     nCampo10 = #10#2;
     nCampo11 = #10#3;
     fCampo12 = #10#4;
     fCampo13 = #10#5;
     nCampo14 = #10#6;
     nCampo15 = #10#7;

     nCampo16 = #11#2;  ||Desde Concepto
     nCampo17 = #11#2;  ||Hasta Concepto
     aCampo6 = "N";
     |SI aMultiEmp = "S";
         aAlfa =  ":contagen/cacamcta canempr1 " + (("00000" + #30#2) % -105) + #30#3;
         |SUB_EJECUTA_NP aAlfa;
     |SINO;
         |SUB_EJECUTA_NP "cacamcta";
     |FINSI;
|FPRC;

|DEFBUCLE leeTemporal;
     #11#1;
     ;
     #0#0, #10#8;
     #0#0, #10#9;
     ;
     NULL, camcta;
|FIN;

|PROCESO canempre;

      |SI #1#0 = "S";
          |SI #1#79 ! "T";
              |SI #30#21 ! #1#79; |ACABA; |FINSI;
          |FINSI;

          nLaEmpresa = #30#2;
          |HAZ ElTipoEmpresa;
          |SI nSwTipo = 1; |ACABA; |FINSI;
      |FINSI;

      |HAZ ElBloqueo;
      |SI nSwTipo = 1; |ACABA; |FINSI;

      |BUCLE leeTemporal;

      |SI #1#65 = "S";
          |DFICB pathbal; |QBF pathbal;
          pathbal = pathbal + (("00000" + #30#2) % -105) + #30#3 + "/";
          balance = 1;
          |SUB_EJECUTA "careccue";
      |FINSI;
|FPRC;

|DEFBUCLE canempre;
     #canempre#1;
     ;
     #1#1, #1#2;
     #1#3, #1#4;
     ;
     NULL, canempre;
|FIN;


|PROCESO elCambio;

     |ABRE #10;
     |DDEFECTO #10;
     |LEE 001#10p;
     |CIERRA #10;

     |SI aMultiEmp = "S";
         |SI #1#0 = "S";
             |BUCLE canempre;
         |SINO;
             |ABRE #30;
             |PARA nPara1 = 5; |SI nPara1 [ 62; |HACIENDO nPara1 = nPara1 + 3;
                   nNume1 = nPara1 + 1;
                   |SI #1nPara1 ! 0;
                       #canempre#2 = #1nPara1;
                       #canempre#3 = #1nNume1;
                       |LEE 000#canempre.=;
                       |SI FSalida = 0;
                           |HAZ canempre;
                       |FINSI;
                   |FINSI;
            |SIGUE;
            |CIERRA #30;
         |FINSI;

||         |CONFI "2410NDesea Borrar la Tabla de Cambio de Cuentas S/N ";
||         |SI FSalida = 0;
||             |DELINDEX #11;
||         |FINSI;

         |ABRE #30;
         #canempre#2 = #48#2;
         #canempre#3 = #48#3;
         |LEE 000#canempre.=;
         |SI FSalida = 0;
             |PTEC 807;
             aAlfa =  ":contagen/cacuenta canempr1 " + (("00000" + #30#2) % -105) + #30#3;
             |SUB_EJECUTA_NP aAlfa;
         |FINSI;
         |CIERRA #30;
     |SINO;

         |HAZ ElBloqueo;
         |SI nSwTipo = 1; |ACABA; |FINSI;

         |BUCLE leeTemporal;

||         |CONFI "2410NDesea Borrar la Tabla de Cambio de Cuentas S/N ";
||         |SI FSalida = 0;
||             |DELINDEX #11;
||         |FINSI;

         |DFICE pathbal;
         |CONFI "2410SDesea Realizar el Recalculo de Saldos: ";
         |SI FSalida = 0;
             balance = 1;
             |SUB_EJECUTA "careccue";
         |FINSI;
     |FINSI;
|FPRC;

|| ********************************************************************
|PROCESO EntraMultiEmp;
     |PUSHV 0501 2380;
     |PINPA #0#1;
     |PINDA #0#1;
     |ENDATOS #1#1;
     |POPV;
|FPRC;

|PROCESO ElFGral;
  |DFICB aAlfa1; |QBF aAlfa1;
  aAlfa1 = aAlfa1 + "000000/";
  |PATH_DAT #11 aAlfa1;
|FPRC;

|PROCESO Seleccion;
  aAlfa1 = #10#1;
  |C_M #10#2, 1, aAlfa1; |C_M #10#3, 1, aAlfa1;
  |C_M #10#4, 1, aAlfa1; |C_M #10#5, 1, aAlfa1;
  |C_M #10#6, 1, aAlfa1; |C_M #10#7, 1, aAlfa1;
  |C_M #10#8, 1, aAlfa1; |C_M #10#9, 1, aAlfa1;
  |SI aAlfa1 = "N";
      #10#2 = 00;     |PINTA #10#2;
      #10#3 = 99;     |PINTA #10#3;
      #10#4 = fec1;   |PINTA #10#4;
      #10#5 = fec2;   |PINTA #10#5;
      #10#6 = 000001; |PINTA #10#6;
      #10#7 = 999999; |PINTA #10#7;
      #10#8 = 001;    |PINTA #10#8;
      #10#9 = 999;    |PINTA #10#9;
 |FINSI;
|FPRC;

|PROCESO Limites;
 |ABRE #10;
 |LEE 101#10p;
 |SI FSalida ! 0;
     |DDEFECTO #10;
     #10#4 = fec1; #10#5 = fec2;
     |GRABA 020#10b;
 |FINSI;
 |PINDA #0#10;

 |ENDATOS #1#10;
 |GRABA 020#10a;
 |LIBERA #10;
 |CIERRA #10;
|FPRC;
|| ********************************************************************
|RUTINA mix;
 #11#0 = #0#0;
 #11#1 = #0#1;
 #11#2 = 1;
|FRUT;

|RUTINA max;
 #11#0 = #0#0;
 #11#1 = #0#1;
 #11#2 = 999;
|FRUT;

|PROGRAMA;
     |HAZ inicio;
     |CLS;
     |CABEZA "Aplicacion P.Pendientes";
     |PINPA #0#0;

     |PUSHV 0501 0738;
     |ATRI P;
     |PINTA 0601, " Trabajar con Multiempresa S/N ";
     E_sup     = 1;
     E_inf     = "SNsn";
     aMultiEmp = "N";
     aMultiEmp = 0635 ? 0;
     aMultiEmp = aMultiEmp  > " "; |PINTA 0635, aMultiEmp;
     |ATRI 0;
     |POPV;

     |SI aMultiEmp = "S";
         |ATRI P;
         |PINTA 0643, "TABLA STANDAR (Empresa 000000)";
         |PINTA 0602, "<F3> Ver seleccion de empresas";
         |ATRI 0;
         |HAZ ElFGral;
         |HAZ EntraMultiEmp;
     |FINSI;

     |ABRE #11;
     |DDEFECTO #11;
     |LEE 040#11p;
     #0#0 = #11#0; |PINTA #0#0;
     emCta    = 0;
     eaCuenta = #0#0;
     |HAZ LeeCuenta;
     #0#2 = eaNomCta; |PINTA #0#2;
     |CIERRA #11;

|ET PideCuenta;
     |ENDATOS #1#0;

     |ENTLINEAL #1#11, -2, 7, 17, 0, mix, max;

     |HAZ Limites;

     |ENTLINEAL #1#11, -2, 2, 17, 0, mix, max;
     |SI FSalida = 0;
         |CONFI "1234SRealizar ahora el cambio S/N";
         |SI FSalida = 0;
             |HAZ elCambio;
         |FINSI;
     |FINSI;
|FPRO;
