|FICHEROS;
   dsapuz01;

   caenlace;
   caconasi;
   canempre;

   camaasic;
   camaasil;
   camandia;
   cacuenta;
   calibros;
   caivases;
   camaniva;
   caivalin;
   caivam03;

   dsapum01;
|FIN;

|VARIABLES;
   nError         = 0;
   aFichero       = "";
   aAlfa          = "";
   &enNoModif     = 0;
   &PRMNT_enError = 0;
   &enCaenlace    = 0;
   nNumero        = 0;
   aUsuario       = "";
   aUsuari1       = "";
   aHora          = "";
   fFechaP        = @;
   fFechaU        = @;
   nAsientos      = 0;
   nFacturas      = 0;
   aRegistros     = "";
   nRegistros     = 0;
   primero        = "";
   temporal       = "";
   nOpera         = 0;

   &eSwFicNif     = 0;
   &eaAsi         = "";
   &enSwConfi     = 0;
   &enSwMensajes  = 0;
|FIN;

|INCLUYE dscont_i;

|| *************************************************************************
|PROCESO BorraSerie0;
   #caivases#3 = 1;
   |GRABA 020#caivases.a;
   |LIBERA #caivases;
|FPRC;

|DEFBUCLE BorraSerie;
   #caivases#1;
   ;
   "     ", " ";
   "zzzzz", "z";
   be;
   NULL, BorraSerie0;
|FIN;

|PROCESO BorraLibro0;
   #calibros#4 = 1;
   |GRABA 020#calibros.a;
   |LIBERA #calibros;
|FPRC;

|DEFBUCLE BorraLibro;
   #calibros#1;
   ;
   00;
   99;
   be;
   NULL, BorraLibro0;
|FIN;

|PROCESO BorraCuenta0;
   |PDEFECTO #cacuenta,9,54;
   #cacuenta#58 ="01.01.1900";
   |GRABA 020#cacuenta.a;
   |LIBERA #cacuenta;
|FPRC;

|DEFBUCLE BorraCuenta;
   #cacuenta#1;
   ;
   "            ";
   "zzzzzzzzzzzz";
   be;
   NULL, BorraCuenta0;
|FIN;

|PROCESO BorraDiario0;
   #camandia#2 = 0;
   #camandia#3 = 0;
   |GRABA 020#camandia.a;
   |LIBERA #camandia;
|FPRC;

|DEFBUCLE BorraDiario;
   #camandia#1;
   ;
   00;
   99;
   be;
   NULL, BorraDiario0;
|FIN;

|PROCESO Acerar;
   |SI PARAMETRO ! "CRON";
       |PINTA 1706, "Borrando datos de la Empresa Actual ...";
   |FINSI;

   |DELINDEX #caconasi;
   |ABRET #caconasi;
   |CIERRAT #caconasi;

   |DELINDEX #camaasic;
   |ABRET #camaasic;
   |CIERRAT #camaasic;

   |DELINDEX #camaasil;
   |ABRET #camaasil;
   |CIERRAT #camaasil;

   |BUCLE BorraDiario;
   |BUCLE BorraCuenta;

   |DELINDEX #camaniva;
   |ABRET #camaniva;
   |CIERRAT #camaniva;

   |DELINDEX #caivalin;
   |ABRET #caivalin;
   |CIERRAT #caivalin;

   |DELINDEX #caivam03;
   |ABRET #caivam03;
   |CIERRAT #caivam03;

   |BUCLE BorraLibro;
   |BUCLE BorraSerie;
|FPRC;

|| *************************************************************************
|PROCESO VQuants;
  |DFICE dirfich0;
  nRegistros = 0;
  temporal   = dirfich0;
  |LEEDEDEF primero, temporal;
  |SI FSalida ] 0;
      temporal   = FSalida % 375;
      aRegistros = (A\temporal % 1615);
      nRegistros = aRegistros;
  |FINSI;
|FPRC;

|PROCESO CabAsientos;
   primero   = "camaasic";
   |HAZ VQuants;
   nAsientos = nRegistros;

   |ABRE #camaasic;
   |SELECT #6#camaasic;
   |DDEFECTO #camaasic;
   |LEE 041#camaasic.p;
   |SI FSalida = 0;
       fFechaP = #camaasic#1;
   |FINSI;

   |DDEFECTO #camaasic;
   |LEE 041#camaasic.u;
   |SI FSalida = 0;
       fFechaU = #camaasic#1;
   |FINSI;
   |SELECT #1#camaasic;
   |CIERRA #camaasic;
|FPRC;

|PROCESO GrabaRegistro;
   aUsuario = Usuario % 108;
   aUsuari1 = Usuario % 840;

   |ABRE #dsapum01;
   #dsapum01#0 = #dsapuz01#0;
   #dsapum01#1 = 999999;
   |LEE 010#dsapum01.];
   |SI FSalida = 0;
       |LEE 040#dsapum01.a;
   |SINO;
       |LEE 040#dsapum01.u;
   |FINSI;

   nNumero = 1;
   |SI FSalida = 0; |Y #dsapum01#0 = #dsapuz01#0;
       nNumero = #dsapum01#1 + 1;
   |FINSI;

   |DDEFECTO #dsapum01;
   #dsapum01#0 = #dsapuz01#0;
   #dsapum01#1 = nNumero;
   #dsapum01#2 = #dsapuz01#1;
   #dsapum01#3 = #dsapuz01#2;
   #dsapum01#4 = #dsapuz01#3;
   #dsapum01#5 = aUsuario;
   #dsapum01#6 = aUsuari1;

   fFechaP     = "01.01.0000";
   fFechaU     = "01.01.0000";
   nAsientos   = 0;
   nFacturas   = 0;

   |HAZ CabAsientos;
   primero   = "camaniva";
   |HAZ VQuants;
   nFacturas = nRegistros;

   #dsapum01#9  = fFechaP;
   #dsapum01#10 = fFechaU;
   #dsapum01#11 = nAsientos;
   #dsapum01#12 = nFacturas;

   |HORA aHora;
   #dsapum01#7 = ~;
   #dsapum01#8 = aHora;
   |GRABA 020#dsapum01.n;

   |CIERRA #dsapum01;
|FPRC;

|PROCESO LeoCaenlace;
   |SI nOpera = 0;
       nError = 0;
       |ERROR10;
   |FINSI;

   |SI nOpera = 1;
       |BORRA 020#caenlace.a;
   |FINSI;
   |LIBERA #caenlace;
|FPRC;

|DEFBUCLE LeoCaenlace;
     #caenlace#1;
     20;
     00, fec1, 000001, 0001, enEmpresa, enPeriodo, "N";
     99, fec2, 999999, 9999, enEmpresa, enPeriodo, "N";
     be;
     NULL, LeoCaenlace;
|FIN;

|CALCULO wTipo12; |TIPO 12;
  |SI #dsapuz01#5 = "NO";
      |ACABA;
  |FINSI;

  enNoModif  = 0;
  enCaenlace = 1;
  |DFICE dirfich0; |QBF dirfich0;

  aFichero = "caenlace";

  |SI PARAMETRO ! "CRON";
      |ATRI P; |PINTA 1710, "Verificando datos Importados .........."; |ATRI 0;
  |FINSI;

  aAlfa = "dsapunte;" + dirfich0 + "," + aFichero;
  |SUB_EJECUTA_NP aAlfa;

  |SI PRMNT_enError = 0;
||      |HAZ Acerar;  || Quitado pro que ahora se puede importar parcialmente

      enNoModif = 0;
      |SI #dsapuz01#6 = "N";
          enNoModif = 100;  || para que no se modifiquen los apuntes
      |FINSI;
      enCaenlace = 2;
      |DFICE dirfich0; |QBF dirfich0;
      aFichero = "caenlace";

      |SI PARAMETRO ! "CRON";
         |ATRI S; |PINTA 1710, "Recogiendo datos Contables ............"; |ATRI 0;
      |FINSI;

      aAlfa = "dsapunte;" + dirfich0 + "," + aFichero;
      |SUB_EJECUTA_NP aAlfa;

      |SI eSwFicNif = 1;
          aAlfa = "capasnif;ALTA";
          |SUB_EJECUTA_NP aAlfa;
      |FINSI;

      nOpera = 1; |BUCLE LeoCaenlace;

      |SI PARAMETRO ! "CRON";
          |MENAV "0000Proceso Importacion Completado ";
      |FINSI;

      |HAZ GrabaRegistro;
  |SINO;
      |SI PARAMETRO ! "CRON";
          |MENAV "0000Proceso Abortado";
      |FINSI;
  |FINSI;

|FCAL;


|PROCESO MiroFichero;
/*
     |ABRE #caenlace;
     |CONSULTA_CLAVE #1#caenlace;
     |CIERRA #caenlace;
*/

     nError = 1;
     nOpera = 0;  |BUCLE LeoCaenlace;

     |SI nError ! 0;
         |SI PARAMETRO ! "CRON";
             |MENAV "0000No hay registros para Importar";
         |FINSI;
     |FINSI;
|FPRC;

|| /////////////////////////////////////////////////////////////////////////

|PROGRAMA;
     |HAZ inicio;
     |HAZ eLeeParametro;

     #dsapuz01#0 = enEmpresa;
     #dsapuz01#1 = enPeriodo;
     #dsapuz01#2 = eaNombreEmp;
     #dsapuz01#3 = enEjercicio;

     |SI PARAMETRO = "CRON";
         #dsapuz01#5  = "SI";
         #dsapuz01#6  = eaAsi;
         enSwConfi    = 2;
         enSwMensajes = 1;

         |HAZ wTipo12;
         |ACABA;
     |FINSI;

     |CLS;
     |CABEZA "Importar Caenlace";
     |PINPA #0#dsapuz01;
     |PINDA #0#dsapuz01;
     |HAZ MiroFichero;
     |SI nError = 0;
         |ENDATOS #1#dsapuz01;
     |FINSI;
|FPRO;
