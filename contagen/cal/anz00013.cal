|FICHEROS;
  anz00013 #0;
  anm00001 #1;
  anm00002 #2;
  anm00003 #3;

  anw00008 #908,MANTE;

  camaasil #11;
  anm00012 #12;
  anm00013 #13;
  anm00014 #14;
  anm00015 #15;

  :/modelos/def/dsmom041 #41;
|FIN;

|VARIABLES;
  &entrada = 1;
  aAlfa1   = "";
  nNume1   = 0;

  nImporte = 0;
  enImAna  = 0;
  nUltLin  = 0;
  nDif     = 0;

  nSwGrupo = 0;
  nEstado  = 0;
  aFichero = "";
  nInkey   = 0;
  nSwOp    = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoan_i;
|INCLUYE numamo_i;

|| ******************************************************************
|| ************ Proceso Campos en Pantalla
|| ******************************************************************
|PROCESO Atipo8; |TIPO 8;
  |HAZ inicio;
  |HAZ Analitica;
  |SI  enSwAna = 0;
       aAlfa1 = "    CONTABILIDAD NO ANALITICA ";
       |SI eaAnNiv = "E";
           aAlfa1 = "    EMPRESA NO ANALITICA ";
       |FINSI;
       |MENAV aAlfa1;
       |ACABA;
  |FINSI;

  |HAZ DirAplicSt;
  |SI Haymodelo = 0;  |C_M #0#12,1,"N"; |FINSI;

  aFichero = ("wa" + Usuario) % 108;
  |NOME_DAT #908 aFichero;
  |ABRE #908;|CIERRA #908;|DELINDEX #908;
|FPRC;

|PROCESO Atipo9; |TIPO 9;
 |DELINDEX #908;
|FPRC;

|CALCULO defectos; |TIPO 7;
   #0#6 = fec1; |PINTA #0#6;
   #0#7 = fec2; |PINTA #0#7;
|FCAL;

|CALCULO fecha; |TIPO 0;
  |SI #0Campo < fec1; |O #0Campo > fec2;
      |MENAV "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
      |ERROR;
  |SINO;
      |SI Campo = 7; |Y #0#6 > #0#7;
          |MENAV "    ERROR EN FECHA CONTABLE";
          |ERROR;
      |FINSI;
  |FINSI;
|FCAL;

|CALCULO cta7; |TIPO 7;
 aAlfa1 = #0#3; |QBF aAlfa1;
 |SI aAlfa1 = "";
     #0#3 = #0#2; |PINTA #0#3;
 |FINSI;
|FCAL;

|CALCULO cuenta; |TIPO 6;
   eaCuenta = #0Campo;
   salidas  = FSalida;
   |HAZ FiltraPuntos;
   #0Campo = eaCuenta;
   |PINTA #0Campo;
|FCAL;

|CALCULO LosElem; |TIPO 0;
 aAlfa1 = #0#12;
 |C_M #0#13,1,aAlfa1;
 |C_M #0#14,1,aAlfa1;
 |C_M #0#15,1,aAlfa1;
 |C_M #0#16,1,aAlfa1;

 |SI aAlfa1 = "N";
     #0#13 = "";         |PINTA #0#13;
     #0#14 = "zzzzzzzz"; |PINTA #0#14;
     #0#15 = 0;          |PINTA #0#15;
     #0#16 = 99;         |PINTA #0#16;
 |FINSI;
|FCAL;

|CALCULO ElElem6; |TIPO 6;
  nInkey = FSalida;
  |C_M #0Campo,0,aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

  x_entra = entrada;
  x_alfa1 = #0Campo;
  |HAZ x_punto;
  #0Campo = x_alfa1; |PINTA #0Campo;
|FCAL;

|RUTINA infA2;
   #41#0 = enEmpresa;
   #41#2 = "        ";
   #41#3 = 00;
|FRUT;

|RUTINA supA2;
   #41#0 = enEmpresa;
   #41#2 = "zzzzzzzz";
   #41#3 = 99;
|FRUT;

|CALCULO ElElem0; |TIPO 0;
  |C_M #0Campo,0,aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

  |SI nInkey  = 10;
      nNume1 = Campo + 2;
      |ABRE #41;
      #41#2 = #0Campo;
      #41#3 = #0nNume1;
      |CONSULTA_F_CLAVE #1#41,infA2,supA2;
      #0Campo = #41#2; |PINTA #0Campo;
      #0nNume1= #41#3; |PINTA #0nNume1;
      |CIERRA #41;
  |FINSI;
|FCAL;
|| -----------------------------------------------

|PROCESO LeeGrupo;
  nSwGrupo = 0;
  |ABRE #2;
  #2#0 = #0#0;
  |LEE 001#2=;
  |SI FSalida ! 0;
      nSwGrupo = 1;
  |FINSI;
  |CIERRA #2;
|FPRC;

|RUTINA inf2;
 #2#0 = "";
|FRUT;

|RUTINA sup2;
 #2#0 = "zzzzzzz";
|FRUT;

|CALCULO anagru; |TIPO 6;
  nInkey  = FSalida;
  salidas = FSalida;
  |HAZ FPuntosAna;

  |SI nInkey = 10;
      |ABRE #2;
      #2#0 = #0#0;
      |CONSULTA_F_CLAVE #1#2, inf2, sup2;
      |CIERRA #2;
      #0#0 = #2#0;
      |PINTA #0#0;
  |FINSI;

  |HAZ LeeGrupo;
  |SI nSwGrupo = 1;
      |MENAV "    Error No existe Grupo de Analitica";
      |ERROR;
  |FINSI;
|FCAL;


|PROCESO Leeanw908;
 |SI nSwOp = 0;
     #0#11 = #0#11 + #908#4;
 |SINO;
     #3#0 = #908#0
     #3#1 = #908#1;
     |LEE 101#3=;
     nEstado = FSalida;
     #3#0 = #908#0;
     #3#1 = #908#1;
     #3#2 = #908#2;
     #3#3 = #908#3;
     #3#4 = #908#4;
     |SI nEstado = 0; |GRABA 020#3a; |FINSI;
     |SI nEstado ! 0; |GRABA 020#3b; |FINSI;
     |LIBERA #3;
 |FINSI;
|FPRC;

|DEFBUCLE Leeanw908;
 #908#1;
 ;
 #0#0,001;
 #0#0,999;
 e;
 NULL, Leeanw908;
|FIN;

|| .................................................
|PROCESO Leeanm003;
 |SI nSwOp = 0;
     #908#0 = #3#0;
     #908#1 = #3#1;
     |LEE 001#908=;
     nEstado = FSalida;
     #908#0 = #3#0;
     #908#1 = #3#1;
     #908#2 = #3#2;
     #908#3 = #3#3;
     #908#4 = #3#4;
     |SI nEstado = 0; |GRABA 020#908a; |FINSI;
     |SI nEstado ! 0; |GRABA 020#908b; |FINSI;
     |LIBERA #908;
 |SINO;
     |BORRA 020#3a;
 |FINSI;
 |LIBERA #3;
|FPRC;

|DEFBUCLE Leeanm003;
 #3#1;
 ;
 #0#0,001;
 #0#0,999;
 be;
 NULL, Leeanm003;
|FIN;
|| //////////////////////////////////////////////////////

|RUTINA inf908;
 #908#0 = #0#0;
 #908#1 = 001;
|FRUT;

|RUTINA sup908;
 #908#0 = #0#0;
 #908#1 = 999;
|FRUT;

|CALCULO grupo0; |TIPO 0;
  |HAZ LeeGrupo;
  #0#1  = #2#1; |PINTA #0#1;
  #0#11 = #2#2; |PINTA #0#11;

  nSwOp = 0;
  |ABRE #908;
  #908#0 = #0#0;
  #908#1 = 0;
  |LEE 001#908];
  |SI FSalida ! 0; |O #908#0 ! #0#0;
      |BUCLE Leeanm003;
  |SINO;
      |CIERRA #908;
      #0#11 = 0;
      |BUCLE Leeanw908;
      |PINTA #0#11;
      |ABRE #908;
  |FINSI;
  |CIERRA #908;

  |ET pregunto;

  |ENTLINEAL #1#908, -2, 2, 21, 0, inf908, sup908;
  |SI #0#11 ! 100;
      |CONFI "    NATENCION! Reparto incorrecto. Continuar ? ";
      |SI FSalida ! 0;
          |ERROR;
          |PTEC 807;
      |SINO;
          |VETE pregunto;
      |FINSI;
  |SINO;
      |CONFI "    SReparto Correcto S/N: ";
      |SI FSalida ! 0;
          |ERROR;
      |FINSI;
  |FINSI;
|FCAL;


|| ------------------------- Proceso de las lineas
|RUTINA inf1;
 #1#0 = "";
|FRUT;

|RUTINA sup1;
 #1#0 = "zzzzzzz";
|FRUT;

|CALCULO anacta; |TIPO 6;
  nInkey  = FSalida;
  salidas = FSalida;
  |HAZ FPuntosAna;

  |SI nInkey = 10;
      |ABRE #1;
      #1#0 = #908Campo;
      |CONSULTA_F_CLAVE #1#1, inf1, sup1;
      |CIERRA #2;
      #908Campo = #1#0; |PINTA #908Campo;
  |FINSI;
|FCAL;

|CALCULO sumar13; |TIPO 2;
  #0#11 = #0#11 +. #908#4; |PINTA #0#11;
|FCAL;

|CALCULO Completa; |TIPO 0;
 nInkey = FSalida;
 |SI nInkey = 10;
     #908#4 = 100 - #0#11; |PINTA #908#4;
 |FINSI;
|FCAL;

|| **************************************************************************
|PROCESO CManGrupo;

  |ABRE #2;
  #2#0 = #0#0;
  |LEE 101#2=;
  nEstado = FSalida;
  #2#0 = #0#0;
  #2#1 = #0#1;
  #2#2 = #0#11;
  |SI nEstado = 0; |GRABA 020#2a; |FINSI;
  |SI nEstado ! 0; |GRABA 020#2b; |FINSI;
  |LIBERA #2;
  |CIERRA #2;

  nSwOp = 1;
  |BUCLE Leeanm003;
  |ABRE #3;
  |BUCLE Leeanw908;
  |CIERRA #3;
|FPRC;
|| ..........................................
|PROCESO Reparto015;
 #15#0 = #14#0;
 #15#1 = #14#1;
 #15#2 = #14#2;
 #15#3 = #908#1;
 #15#4 = #908#2;
 #15#5 = #908#3;
 #15#6 = #908#4;
 |GRABA 020#15n;
|FPRC;

|DEFBUCLE Reparto15;
 #908#1;
 ;
 #0#0, 001;
 #0#0, 999;
 e;
 NULL, Reparto015;
|FIN;

|PROCESO sus_elementos;

 |BUCLELIN 1NULL#14;

 |PINTA 2244, #14#1;
 |PINTA 2253, #14#2;

 |ABRE #15;
 |BUCLE Reparto15;
 |CIERRA #15;

 |LIBERA #14;
|FPRC;

|DEFBUCLE LeeElementos;
 #14#1;
 3;
 enEmpresa, #0#13, #0#15, #0#0;
 enEmpresa, #0#14, #0#16, #0#0;
 be;
 NULL, sus_elementos;
|FIN;

|| ..........................................
|PROCESO Reparto0;

 #13#0 = #12#0;
 #13#1 = #12#1;
 #13#2 = #12#2;
 #13#3 = #12#3;
 #13#4 = #12#4;
 #13#5 = #908#1;
 #13#6 = #908#2;
 #13#7 = #908#3;
 #13#8 = #908#4;
 #13#9 = (enImAna % #13#8) ! EURODB;
 |GRABA 020#13n;

 nImporte = nImporte + #13#9;
 #12#6    = #12#6 + #13#8;
 nUltLin  = #13#5;

 |PINTA 2260, #13#6;
|FPRC;

|DEFBUCLE Reparto;
 #908#1;
 ;
 #0#0, 001;
 #0#0, 999;
 e;
 NULL, Reparto0;
|FIN;

|PROCESO ElBorrado13;
 |BORRA 020#13a;
 |LIBERA #13;
|FPRC;

|DEFBUCLE ElBorrado13;
 #13#1;
 ;
 #11#0, #11#1, #11#2, #11#3, "       ", 001;
 #11#0, #11#1, #11#2, #11#3, "zzzzzzz", 999;
 e;
 NULL, ElBorrado13;
|FIN;

|PROCESO ElBorrado;
 |BORRA 020#12a;
 |LIBERA #12;
|FPRC;

|DEFBUCLE ElBorrado;
 #12#1;
 ;
 #11#0, #11#1, #11#2, #11#3, "       ";
 #11#0, #11#1, #11#2, #11#3, "zzzzzzz";
 e;
 NULL, ElBorrado;
|FIN;

|PROCESO sus_apuntes;

 |BUCLE ElBorrado;
 |BUCLE ElBorrado13;

 |PINTA 2229, #11#1;
 |PINTA 2240, #11#2;
 |PINTA 2247, #11#4;

 nImporte = 0;
 enImAna  = #11#9;

 |ABRE #12;
 |DDEFECTO #12;
 #12#0 = #11#0;
 #12#1 = #11#1;
 #12#2 = #11#2;
 #12#3 = #11#3;
 #12#4 = #0#0;
 #12#5 = #0#1;
 #12#6 = 0;
 #12#7 = enImAna;
 #12#8 = 0;
 |GRABA 020#12b;

 nUltLin  = 0;
 |ABRE #13;
 |BUCLE Reparto;
 |CIERRA #13;
 nDif = (enImAna - nImporte) ! EURODB;
 |SI nDif ! 0;
     |ABRE #13;
     #13#0 = #12#0;
     #13#1 = #12#1;
     #13#2 = #12#2;
     #13#3 = #12#3;
     #13#4 = #12#4;
     #13#5 = nUltLin;
     |LEE 101#13=;
     |SI FSalida = 0;
         #13#9 = #13#9 + nDif;
         nImporte = nImporte + nDif;
         |GRABA 020#13a;
     |FINSI;
     |LIBERA #13;
     |CIERRA #13;
 |FINSI;

 #12#8 = nImporte;
 |GRABA 020#12a;
 |LIBERA #12;
 |CIERRA #12;
|FPRC;

|DEFBUCLE LeeApuntes;
 #11#1;
 4,29;
 #0#4, #0#6, #0#8, 0001, #0#2, #0#0;
 #0#5, #0#7, #0#9, 9999, #0#3, #0#0;
 be;
 NULL, sus_apuntes;
|FIN;

|| **********************************************************************
|CALCULO modificar; |TIPO 3;

  |SI #0#10 = "S";
      |HAZ CManGrupo;
  |FINSI;

  |D_CUADRO 2104, 2369;
  |PINTA 2205," En Proceso:                                                    ";

  |ATRI I; |PINTA 2218,"Apuntes ->                                         "; |ATRI 0;
  |BUCLE LeeApuntes;

  |SI #0#12 = "S";
      |ATRI I; |PINTA 2218,"Elementos Amortizables ->                          "; |ATRI 0;
      |BUCLE LeeElementos;
  |FINSI;
|FCAL;
