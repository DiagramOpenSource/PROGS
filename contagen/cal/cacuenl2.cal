|FICHEROS;
   cacuent2 #0;
   :/contagen/def/cacuenta #1;
   cacuenl2 #2;
   :/contagen/def/cacuebal #5;    || Cuentas de balance
   :/contagen/def/caparbal #6;    || partidas balances
   :/contagen/def/caparexp #7;    || partidas explotacion

   canempre #30;
   caselem1 #998;
|FIN;

|VARIABLES;
   &entrada = 1;
   &eaTex1 = "";
   &eaTex2 = "";
   &eaTex3 = "";
   aDTit   = "";
   aHTit   = "";
   aDCod   = "";
   aHCod   = "";
   nCampo  = 0;
   nSwSaldo = 0;
|FIN;

|INCLUYE dscont_i;

|| **************************** PROCESOS PANTALLA
|CALCULO cuenta; |TIPO 0;
     eaCuenta = #cacuenl2#Campo#;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #cacuenl2#Campo# = eaCuenta;
     |PINTA #cacuenl2#Campo#;
|FCAL;

|| ........................
|PROCESO ImprimeTempo;
   |DDEFECTO #0;
   nSwSaldo = 0;
   |PARA nCampo = 0;  |SI nCampo [ 60;  |HACIENDO nCampo = nCampo + 1;
      #0nCampo = #1nCampo;
      |SI nCampo ] 9; |Y nCampo [ 53;
          |SI #1nCampo ! 0;
              nSwSaldo = 1;
          |FINSI;
      |FINSI;
   |SIGUE;
   |SI #2#4 = "N"; |Y nSwSaldo = 0;
       |ACABA;
   |FINSI;
   |SI #1#58 [ "01.01.1900"; #0#58 = "01.01.0000"; |FINSI;
   #0#61 = #1#11 + #1#14;
   #0#62 = #0#61 + #1#17;
   #0#63 = #0#62 + #1#20;
   #0#64 = #0#63 + #1#23;
   #0#65 = #0#64 + #1#26;
   #0#66 = #0#65 + #1#29;
   #0#67 = #0#66 + #1#32;
   #0#68 = #0#67 + #1#35;
   #0#69 = #0#68 + #1#38;
   #0#70 = #0#69 + #1#41;
   #0#71 = #0#70 + #1#44;
   #0#72 = #0#71 + #1#47;
   #0#73 = #0#72 + #1#50;
   |HAZ Lecturas;
   |PRINT;
   |PIEINF;
|FPRC;

|DEFBUCLE cacuenta_D;
   #1#2;
   0,1;
   aDTit, aDCod, #2#2;
   aHTit, aHCod, #2#2;
   ;
   NULL, ImprimeTempo;
|FIN;

|DEFBUCLE cacuenta_C;
   #1#1;
   1;
   aDCod, #2#2;
   aHCod, #2#2;
   ;
   NULL, ImprimeTempo;
|FIN;

|PROCESO HazTodo;
 |SI #2#3 = 1; |BUCLE cacuenta_C; |FINSI;
 |SI #2#3 = 2; |BUCLE cacuenta_D; |FINSI;
|FPRC;
||*************************************************************************
|CALCULO impre;|TIPO 3;

   aDCod = #2#0;
   aHCod = #2#1;
   aDTit = " " * 36; aHTit = "z" * 36;

   |IMPRE 0;
   |SI FSalida ! 0;  |ACABA;  |FINSI;

   |INFOR "cacuenta";
   |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;


   |SI #48#27 = "S";
       |HAZ MultiEmpresa;
   |SINO;
       |DFICE dirfich0; |QBF dirfich0;
       |HAZ HazTodo;
   |FINSI;
   |FININF;
   |FINIMP;
|FCAL;
||*************************************************************************

|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #5 dirfich0;
   |PATH_DAT #6 dirfich0;
   |PATH_DAT #7 dirfich0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 000001;
 999999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;

||*************************************************************************
|PROCESO Lecturas;
 eaTex1 = "";  eaTex2 = "";  eaTex3 = "";
 |SI #0#4 = 0;
     |ABRE #6;
     #caparbal#0 = #0#5;     || tipo
     #caparbal#1 = #0#6;     || agrupacion
     #caparbal#3 = #0#7;     || epigrafe
     #caparbal#5 = #0#8;     || partida
     |LEE 001#6=;
     |SI FSalida ! 0;  |DDEFECTO #6;  |FINSI;
     eaTex1 = #6#2;
     eaTex2 = #6#4;
     eaTex3 = #6#6;
     |CIERRA #6;
  |SINO;
     |ABRE #7;
     #caparexp#0 = #0#4;     || bloque
     #caparexp#1 = #0#5;     || tipo
     #caparexp#2 = #0#6;     || agrupacion
     #caparexp#4 = #0#7;     || epigrafe
     #caparexp#6 = #0#8;     || partida
     |LEE 001#7=;
     |SI FSalida ! 0;  |DDEFECTO #7;  |FINSI;
     eaTex1 = #7#3;
     eaTex2 = #7#5;
     eaTex3 = #7#7;
     |CIERRA #7;
  |FINSI;
|FPRC;
