|FICHEROS;
   anz00007 #0;
   camaasil #1;
   caconcil #3;
   anw00006 #6;
|FIN;

|VARIABLES;
    mensa1 = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
    men2  = "     Longitud de Cuenta Fuera de Nivel !";
    alfa1 = "";
    nume1 = 0;
    paso = 0;
    incta = "";
    ficta = "";
    indig = 0;
    fidig = 0;
    desfec = @;
    hasfec = @;
    swalgun = 0;
    debe = 0;
    haber = 0;
    inref = "";
    firef = "";
    referencia = "";
    cuenta = "";
    d_diario = 0;
    h_diario = 98;
    digito = 0;
    aFichero = "";
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoan_i;

|| *********************************************************************
||                             COMIENZO
|| *********************************************************************
|CALCULO seleccio; |TIPO 0;
 |SI #0Campo = "N";
     |PARA nume1 = 7; |SI nume1 [ 22; |HACIENDO nume1 = nume1 + 1;
           |CAMPO_MODIFICA #0nume1, 1, "S";
     |SIGUE;
     |CAMPO_MODIFICA #0#2,1,"N";
     |CAMPO_MODIFICA #0#3,1,"N";
     #0#2 = doce;           #0#4 = 0; |PINTA #0#2;
     #0#3 = "zzzzzzzzzzzz"; #0#5 = 9; |PINTA #0#3;
 |SINO;
     |CAMPO_MODIFICA #0#2,1,"S";
     |CAMPO_MODIFICA #0#3,1,"S";
     |PARA nume1 = 7; |SI nume1 [ 22; |HACIENDO nume1 = nume1 + 1;
           |CAMPO_MODIFICA #0nume1, 1, "N";
           #0nume1 = doce; |PINTA #0nume1;
     |SIGUE;
 |FINSI;
|FCAL;

|CALCULO empieza; |TIPO 7;
 |SI paso = 0;
    #0#0 = fec1;
    #0#1 = fec2;
    |PINTA #0#0;
    |PINTA #0#1;
    paso = 1;
 |FINSI;
|FCAL;

|CALCULO cuenta; |TIPO 6;
     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;


     |SI #0Campo = doce; |ACABA; |FINSI;
     |SI Campo [ 3;
         digito = Campo + 2;
     |SINO;
         digito = Campo + 16;
     |FINSI;
     eaCuenta = #0Campo; |HAZ SacaNivel;
     #0digito = enNivel;
|FCAL;

|CALCULO fechad;
  |SI #0#0 < fec1; |O #0#0 > fec2;
      |MENAV mensa1;
      |ERROR;
  |FINSI;
|FCAL;

|CALCULO fechah;
  |SI #0#1 < fec1; |O #0#1 > fec2; |O #0#1 < #0#0;
      |MENAV mensa1;
      |ERROR;
  |FINSI;
|FCAL;

|| *************************************************************************

|| .................... Diferencias
|PROCESO grabatra_d;
 |ATRI I;
 |PINTA 2229, #3#0;
 |PINTA 2242, #3#1;
 |PINTA 2253, #3#9;
 |ATRI 0;
 || ... Graba temporales
 |SI #3#6 = 0; |Y #3#7 = 0; |ACABA; |FINSI;
 debe  = #3#6;
 haber = #3#7;

 || ... Graba temporal de Referencia - Cuenta

 #6#0 = #3#9;
 #6#1 = #3#0;
 #6#2 = #3#1;
 |LEE 101#6=;
 |SI FSalida ! 0;
     |DDEFECTO #6;
     #6#0 = #3#9;
     #6#1 = #3#0;
     #6#2 = #3#1;
     |GRABA 020#6c;
 |FINSI;

 #6#3 = #6#3 + debe;
 #6#4 = #6#4 + haber;
 #6#5 = #6#3 - #6#4;
 |GRABA 020#6a;
 |LIBERA #6;
 swalgun = 1;
|FPRC;

|DEFBUCLE cargatra_d;
 #3#1;
 4,3,8;
 incta, 001, desfec, 2, " ";
 ficta, 999, hasfec, 2, " ";
 e;
 NULL, grabatra_d;
|FIN;

|PROCESO grabatra;
 |ATRI I;
 |PINTA 2229, #1#4;
 |PINTA 2242, #1#1;
 |PINTA 2253, #1#26;
 |ATRI 0;
 || ... Graba temporales
 |SI #1#9 = 0; |ACABA; |FINSI;
 |SI #1#8 = "D";
     debe = #1#9;
     haber = 0;
 |SINO;
     debe = 0;
     haber = #1#9;
 |FINSI;

 || ... Graba temporal de Referencia - Cuenta

 #6#0 = #1#26;
 #6#1 = #1#4;
 #6#2 = #1#5;
 |LEE 101#6=;
 |SI FSalida ! 0;
     |DDEFECTO #6;
     #6#0 = #1#26;
     #6#1 = #1#4;
     #6#2 = #1#5;
     |GRABA 020#6c;
 |FINSI;

 #6#3 = #6#3 + debe;
 #6#4 = #6#4 + haber;
 #6#5 = #6#3 - #6#4;
 |GRABA 020#6a;
 |LIBERA #6;
 swalgun = 1;
|FPRC;

|DEFBUCLE cargatra1;
 #1#3;
 0,18;
 incta, desfec, 000001, 0001, d_diario, " ";
 ficta, hasfec, 999999, 9999, h_diario, " ";
 e;
 NULL, grabatra;
|FIN;

|PROCESO cargatra2; || Ha elegido una parrilla de cuentas
 |PARA nume1 = 7; |SI nume1 [ 22; |HACIENDO nume1 = nume1 + 1;
       |SI #0nume1 ! doce;
           digito = nume1 + 16;
           incta = #0nume1; indig = #0digito;
           ficta = #0nume1; fidig = #0digito;
           |BUCLE cargatra1;
           |SI #0#40 = "S"; |BUCLE cargatra_d; |FINSI; || SI busca en Diferencias
       |FINSI;
 |SIGUE;
|FPRC;

|| ---------------------------------
|PROCESO grabadif;
   #3#8 = "û";
   |GRABA 020#3a;
   |LIBERA #3;
|FPRC;

|DEFBUCLE marcar_d;
 #3#1;
 4,3,8,9;
 cuenta, 001, desfec, 2, " ", referencia;
 cuenta, 999, hasfec, 2, " ", referencia;
 e;
 NULL, grabadif;
|FIN;

|PROCESO grabaapun;
   #1#18 = "û";
   |GRABA 020#1a;
   |LIBERA #1;
|FPRC;

|DEFBUCLE marcar;
 #1#3;
 0,18,26;
 cuenta, desfec, 000001, 0001, d_diario, " ", referencia;
 cuenta, hasfec, 999999, 9999, h_diario, " ", referencia;
 e;
 NULL, grabaapun;
|FIN;

|PROCESO puntear;
 |SI #6#5  = 0;
     referencia = #6#0;
     cuenta = #6#1;
     digito = #6#2;
     |PINTA 2229, #6#1;
     |PINTA 2242, #6#0;
     |BUCLE marcar;
     |SI #0#40 = "S"; |BUCLE marcar_d;|FINSI;  || Puntear Diferencias
 |FINSI;
|FPRC;

|DEFBUCLE punteo;
 #6#1;
 ;
 inref, incta;
 firef, ficta;
 e;
 NULL, puntear;
|FIN;

|| *************************************************************************
|PROCESO tipo3; |TIPO 3;

 |SI #0#39 = "N"; |ACABA; |FINSI;

 aFichero = ("s" + Usuario) % 108;
 |NOME_DAT #6 aFichero;

 |DELINDEX #6;

 desfec = #0#0;
 hasfec = #0#1;

 || .... Crear los ficheros auxiliares

 swalgun = 0;
 |ABRE #6;

 |ATRI N;
 |D_CUADRO 2108, 2374;
 |PINTA 2209, " Grabando temporal:                                              ";
 |ATRI 0;

 |SI #0#40 = "S"; d_diario = 1; |FINSI;  || Si Diferencias = Apertura.

 |SI #0#6 = "S";
     incta = #0#2; indig = 0;
     ficta = #0#3; fidig = 9;
     |BUCLE cargatra1;
     |SI #0#40 = "S"; |BUCLE cargatra_d; |FINSI; || SI busca en Diferencias
 |SINO;
     |HAZ cargatra2;
 |FINSI;

 |CIERRA #6;
 |SI swalgun = 0;
     |MENAV "    Seleccion Vacia ...";
 |SINO;
      |PINTA 2209, " Punteando .......:                                              ";
      |ABRE #1;
      |ABRE #3;
      incta = doce;           indig = 0; inref = "                    ";
      ficta = "zzzzzzzzzzzz"; fidig = 9; firef = "zzzzzzzzzzzzzzzzzzzz";
      |BUCLE punteo;
      |CIERRA #1;
      |CIERRA #3;
 |FINSI;
 |DELINDEX #6;
|FPRC;
