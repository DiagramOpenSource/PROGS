|FICHEROS;
     caut0004 #0;

     camautoc #1;
     camautol #2;
     camauto0 #3;
     camauto1 #4;

     imautoc@ #11;
     imautol@ #12;
     imauto0@ #13;
     imauto1@ #14;

     canempre #30;
     caselemz #911;
     caselem1 #998;
     caw00026 #999;

     :/basica/def/agifigen #100;
|FIN;

|VARIABLES;

     &enNFijo = 0;
     &enNFijE = 0;

     nCampo   = 0;
     aAlfa1   = "";
     aAlfa2   = "";
     aAlfa3   = "";
     nPara1   = 0;
     nPara2   = 0;
     nNume1   = 0;
     nNume2   = 0;

     &r_emp   = 0;
     &r_per   = 0;

     aFichero   = "";
     Linea      = 0;
     aBass      = "";
     aDef       = "";
     nSwNoDir   = 0;
     nSwTipo    = 0;
     nSwEjer    = 0;
     nLaEmpresa = 0;
     nPin1      = 0;
     nEjer      = 0;

     &eaOrigen  = "";
     &eaDestino = "";

     aEstEmp    = "";
     aSelTipo   = "";
     nDTip      = 0;
     nHTip      = 0;
     nTip1      = 0;
     nTip2      = 0;
     nTip3      = 0;
     nTip4      = 0;
     nTip5      = 0;
     nTip6      = 0;
     nTip7      = 0;
     nTip8      = 0;
     nTip9      = 0;
     nTip0      = 0;
     aNumCampos = "";
     aQueQuiero = "";
     nNumCampo1 = 0;
     nNumCampo2 = 0;
     nNumCampo3 = 0;
     nNumCampo4 = 0;

     aParam     = "";
     &enCampo40 = 0;
     &enCampo41 = 0;
     &enCampo42 = 0;
     &enCampo43 = 0;
|FIN;

|INCLUYE dscont_i;

|| *************************************************************************
||                        CALCULOS DE PANTALLA
|| *************************************************************************
|CALCULO Consulta; |TIPO 0;
     |C_M #0Campo,0,aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     |SI FSalida ! 9;  |ACABA;  |FINSI;

     |SUB_EJECUTA_NP "caconemp";
     |ERROR;
     |SI Campo = 3;
         #0#3 = r_emp;
         #0#5 = r_per;
         |PTEC 802;
         |PTEC 802;
     |FINSI;

     |SI Campo = 4;
         #0#4 = r_emp;
         #0#6 = r_per;
         |PTEC 802;
     |FINSI;

     |SI Campo ] 7;  |Y Campo [  16;
         #0Campo  = r_emp;
         nCampo   = Campo + 10;
         #0nCampo = r_per;
         |PTEC 802;
         |PTEC 802;
     |FINSI;

     |SI Campo = 37;
         #0#37 = r_emp;
         #0#38 = r_per;
         |PTEC 802;
         |PTEC 802;
     |FINSI;
|FCAL;

|CALCULO LeePlanilla; |TIPO 6;
 |ABRE #911;
 #911#0 = #0#49;
 |LEE 001#911=;
 |SI FSalida ! 0;
     |SI #0#49 = 0;
         |DDEFECTO #911;
         #911#0 = 0;
         #911#1 = "Empresa Actual";
         |GRABA 020#911n;
     |FINSI;
 |FINSI;
 |CIERRA #911;
 |SI #0#49 ! 0;
     |C_M #0#5,1,"S"; #0#5 = #0#38; |PINTA #0#5;
     |C_M #0#6,1,"S"; #0#6 = #0#38; |PINTA #0#6;
 |FINSI;
|FCAL;

|CALCULO AntesSel; |TIPO 7;
 aAlfa1 = "S";
 |SI #0#49 ! 0;
     aAlfa1 = "N";
     #0#2 = "S";
     #0#3 = 1;
     #0#4 = 99999;
     #0#5 = #0#38;
     #0#6 = #0#38;
     |PDEFECTO #0, 7, 37;
 |FINSI;
 |PARA nCampo = 2; |SI nCampo [ 36; |HACIENDO nCampo = nCampo + 1;
       |SI aAlfa1 = "N"; |PINTA #0nCampo; |FINSI;
       |SI nCampo < 27;
           |C_M #0nCampo,1,aAlfa1;
       |FINSI;
 |SIGUE;
 |SI #0#49 ! 0;
     |C_M #0#5,1,"S";
     |C_M #0#6,1,"S";
 |FINSI;
|FCAL;

|CALCULO Parrilla; |TIPO 0;
     |C_M #0#2, 0, aAlfa1;
     |SI aAlfa1 = "N";  |ACABA;  |FINSI;

     |SI #0#2 = "N";;
         #0#3 = 0;  |PINTA #0#3;  |C_M #0#3, 1, "N";
         #0#5 = 0;  |PINTA #0#5;  |C_M #0#5, 1, "N";
         #0#4 = 0;  |PINTA #0#4;  |C_M #0#4, 1, "N";
         #0#6 = 0;  |PINTA #0#6;  |C_M #0#6, 1, "N";
         |PARA nCampo = 7;  |SI nCampo [ 26;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |C_M #0#3, 1, "S"; |C_M #0#5, 1, "S";
         |C_M #0#4, 1, "S"; |C_M #0#6, 1, "S";
         |PDEFECTO #0,7,37;
         |PARA nCampo = 7;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas; |TIPO 0;
     |C_M #0Campo, 0, aAlfa1;
     |SI aAlfa1 = "N";  |ACABA;  |FINSI;
     |SI FSalida = 2;   |ACABA;  |FINSI;
     |SI FSalida = 9;   |HAZ Consulta;  |FINSI;

     |SI #0Campo = 0;
         nNume1 = Campo + 10;
         nNume2 = Campo + 20;
         #0nNume1 = 0; |C_M #0nNume1, 1, "N"; |PINTA #0nNume1;
         #0nNume2 = "";                       |PINTA #0nNume2;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 16;  |HACIENDO nCampo = nCampo + 1;
               nNume1 = nCampo + 10;
               nNume2 = nCampo + 20;
               #0nCampo = 0; |C_M #0nCampo, 1, "N"; |PINTA #0nCampo;
               #0nNume1 = 0; |C_M #0nNume1, 1, "N"; |PINTA #0nNume1;
               #0nNume2 = "";                       |PINTA #0nNume2;
         |SIGUE;
         |ACABA;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 26;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;

     |ABRE #30;
     #30#2 = #0Campo;
     #30#3 = 0;
     |LEE 011#30];
     |SI FSalida ! 0;  |O #30#2 ! #0Campo;
         |MENAV "     NO Existe la Empresa Contable";
         |ERROR;
     |FINSI;
     |CIERRA #30;
|FCAL;

|CALCULO EsBuena1;
     |C_M #0Campo,0,aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     |SI FSalida = 2; |ACABA; |FINSI;

     nCampo = Campo - 10;
     |SI #0nCampo = 0;  |ACABA;  |FINSI;

     |ABRE #30;
     #30#2 = #0nCampo;
     #30#3 = #0Campo;
     |LEE 011#30=;
     |SI FSalida ! 0;
         |MENAV "     NO Existe Periodo en la Empresa Contable";
         |CIERRA #30;
         |ERROR;
         |ACABA;
     |FINSI;
     |CIERRA #30;

     nCampo   = Campo + 10;
     #0nCampo = #30#1;
     |PINTA #0nCampo;
|FCAL;

|CALCULO EsBuena2;          || desde campo empresa Origen
     |C_M #0#37,0,aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     |C_M #0#38,1,aAlfa3;
     |SI #0#37 = 0;
         |CONFI "2400SCopiar desde Ficheros Standar.";
         |SI FSalida ! 0;
             |ERROR;
             |ACABA;
         |FINSI;
         #0#38 = 0; |PINTA #0#38;
         |C_M #0#38,1,"N";
     |FINSI;

     |SI #0#37 ! 0;
         |ABRE #30;
         #30#2 = #0#37;
         #30#3 = 0;
         |LEE 011#30];
         |SI FSalida ! 0;  |O #30#2 ! #0#37;
             |MENAV "     NO Existe la Empresa Contable";
             |ERROR;
         |FINSI;
         |CIERRA #30;
     |SINO;
         #0#39 = "FICHEROS STANDAR DE LA APLICACION"; |PINTA #0#39;
         #0#1  = enEjercicio;                         |PINTA #0#1;
     |FINSI;
|FCAL;

|CALCULO EsBuena3;        ||Desde campo Periodo Origen
     |C_M #0#38,0,aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     |ABRE #30;
     #30#2 = #0#37;
     #30#3 = #0#38;
     |LEE 011#30=;
     |SI FSalida ! 0;
         |MENAV "     NO Existe Periodo en la Empresa Contable";
         |CIERRA #30;
         |ERROR;
         |ACABA;
     |FINSI;
     |CIERRA #30;

     #0#39 = #30#1; |PINTA #0#39;
     |SI #30#26 > 80;
         nEjer = #30#26 + 1900;
     |SINO;
         nEjer = #30#26 + 2000;
     |FINSI;
     #0#1 = nEjer; |PINTA #0#1;
|FCAL;

|CALCULO fijo; |TIPO 0;
  |C_M #0#40,0,aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

  aAlfa1 = "S";
  |SI #0#40 = 0;
      aAlfa1 = "N";
      #0#41 = 0; |PINTA #0#41;
  |FINSI;

  |C_M #0#41,1, aAlfa1;
  |C_M #0#42,1, aAlfa1;
  |C_M #0#43,1, aAlfa1;
|FCAL;

|CALCULO fijoe; |TIPO 0;
  |C_M #0#44,0,aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

  aAlfa1 = "S";
  |SI #0#44 = 0;
      aAlfa1 = "N";
      #0#45 = 0; |PINTA #0#45;
  |FINSI;

  |C_M #0#45,1, aAlfa1;
  |C_M #0#46,1, aAlfa1;
  |C_M #0#47,1, aAlfa1;
|FCAL;

|| **********************************************************************
|| ********************* Procesos de seleccion

|PROCESO CalDirectorio;
  |DBASE dir_fich; |QBF dir_fich;
  dir_fich = dir_fich + "fich/";
  aAlfa1 = #998#1; |QBF aAlfa1;
  aAlfa2 = #998#2; |QBF aAlfa2;
  aAlfa1 = "00000" + aAlfa1;   aAlfa1 = aAlfa1 % -105;
  aAlfa2 = "0"     + aAlfa2;   aAlfa2 = aAlfa2 % -101;
  #998#3 = dir_fich + aAlfa1 + aAlfa2 + "/";

  nSwNoDir = 0;
  aAlfa1 = #998#3;   |QBF aAlfa1;
  |MKDIR aAlfa1;
  |SI FSalida = 0;
       nSwNoDir = 1;
       |RMDIR aAlfa1;
  |FINSI;
|FPRC;

|PROCESO ElTipoEmpresa;
    nSwTipo = 1;
    |SI Haybasica = 0; nSwTipo = 0; |ACABA; |FINSI;

    |ABRE #agifigen;
    #agifigen#0 = nLaEmpresa; || Empresa
    |LEE 041#agifigen.=;
    |SI FSalida ! 0;
        |DDEFECTO #agifigen;
        #agifigen#87 = 1;
    |FINSI;
    |CIERRA #agifigen;
    nPin1 = #agifigen#87;

    |SI aSelTipo = "S";
        |SI nPin1 ] nDTip; |Y nPin1 [ nHTip;
            nSwTipo = 0;
        |FINSI;
    |SINO;
        |SI nPin1 = nTip1; nSwTipo = 0; |FINSI;
        |SI nPin1 = nTip2; nSwTipo = 0; |FINSI;
        |SI nPin1 = nTip3; nSwTipo = 0; |FINSI;
        |SI nPin1 = nTip4; nSwTipo = 0; |FINSI;
        |SI nPin1 = nTip5; nSwTipo = 0; |FINSI;
        |SI nPin1 = nTip6; nSwTipo = 0; |FINSI;
        |SI nPin1 = nTip7; nSwTipo = 0; |FINSI;
        |SI nPin1 = nTip8; nSwTipo = 0; |FINSI;
        |SI nPin1 = nTip9; nSwTipo = 0; |FINSI;
        |SI nPin1 = nTip0; nSwTipo = 0; |FINSI;
    |FINSI;
|FPRC;

|PROCESO SelEjercicio;
  nSwEjer   = 0;

  |SI #0#49   = 0;   |ACABA; |FINSI;
  |SI #911#45 = "T"; |ACABA; |FINSI;

  |SI #30#26 > 80;
       nEjer = #30#26 + 1900;
  |SINO;
       nEjer = #30#26 + 2000;
  |FINSI;

  |SI #911#45 = "="; |Y nEjer = #0#1; |ACABA; |FINSI;
  |SI #911#45 = "<"; |Y nEjer < #0#1; |ACABA; |FINSI;
  |SI #911#45 = ">"; |Y nEjer > #0#1; |ACABA; |FINSI;
  |SI #911#45 = "["; |Y nEjer [ #0#1; |ACABA; |FINSI;
  |SI #911#45 = "]"; |Y nEjer ] #0#1; |ACABA; |FINSI;

  nSwEjer = 1;
|FPRC;

|PROCESO SelEjercicio2010;
  nSwEjer   = 1;
  |SI #30#26 > 80;
       nEjer = #30#26 + 1900;
  |SINO;
       nEjer = #30#26 + 2000;
  |FINSI;
  |SI nEjer < #0#1; |ACABA; |FINSI;
  |SI nEjer ] 2012; |Y #0#1 = 2010; |ACABA; |FINSI;

  nSwEjer = 0;
|FPRC;
|| ******************************************************************
|PROCESO Rellena;

   |SI aEstEmp ! "T";
       |SI #30#21 ! aEstEmp;  |ACABA; |FINSI;
   |FINSI;

   nLaEmpresa = #30#2;
   |HAZ ElTipoEmpresa;
   |SI nSwTipo = 1; |ACABA; |FINSI;

   |SI aParam = "E2010";
       |HAZ SelEjercicio;
       |SI nSwEjer = 1; |ACABA; |FINSI;
   |FINSI;

   |DDEFECTO #998;
   #998#0 = Linea;
   #998#1 = #30#2;
   #998#2 = #30#3;
   |HAZ CalDirectorio;
   |SI nSwNoDir = 0;
       |GRABA 020#998n;
       Linea = Linea + 1;
   |FINSI;
|FPRC;

|DEFBUCLE Rellena;
 #30#1;
 ;
 #0#3, #0#5;
 #0#4, #0#6;
 ;
 NULL, Rellena;
|FIN;


|PROCESO PorPlanilla;
 |ABRE #911;
 #911#0 = #0#49;
 |LEE 001#911=;
 |SI FSalida ! 0;
     |CIERRA #911;
     |ACABA;
 |FINSI;
 |CIERRA #911;

 aEstEmp  = #911#46;
 aSelTipo = #911#47;
 nDTip    = #911#48;
 nHTip    = #911#49;
 nTip1    = #911#50;
 nTip2    = #911#51;
 nTip3    = #911#52;
 nTip4    = #911#53;
 nTip5    = #911#54;
 nTip6    = #911#55;
 nTip7    = #911#56;
 nTip8    = #911#57;
 nTip9    = #911#58;
 nTip0    = #911#59;

 |SI #911#2 = "S";
     #0#3 = #911#3;     ||   #0#5 = 0;
     #0#4 = #911#4;     ||   #0#6 = 9;
     |BUCLE Rellena;
 |SINO;
     |PARA nPara1 = 5; |SI nPara1 [ 24; |HACIENDO nPara1 = nPara1 + 1;
           |SI #911nPara1 ! 0;
               #0#3 = #911nPara1;       ||   #0#5 = 0;
               #0#4 = #911nPara1;       ||   #0#6 = 9;
               |BUCLE Rellena;
           |FINSI;
     |SIGUE;
  |FINSI;
|FPRC;
|| *******************************************************************
|| ..... Copia del fichero en si
|PROCESO LosGraboB;
 |PARA nNume1 = 0; |SI nNume1 [ nNumCampo4; |HACIENDO nNume1 = nNume1 + 1;
       #14nNume1 = #4nNume1;
 |SIGUE;
 |GRABA 020#14n;
|FPRC;

|DEFBUCLE LosGraboB;
 #4#1;
 ;
 #3#0, 0001;
 #3#0, 9999;
 be;
 NULL, LosGraboB;
|FIN;

|PROCESO BorroB;
 |BORRA 020#14a;
 |LIBERA #14;
|FPRC;

|DEFBUCLE BorroB;
 #14#1002;
 ;
 #13#0, 0001;
 #13#0, 9999;
 be;
 NULL, BorroB;
|FIN;

|PROCESO AstosFijosE;
 #13#0 = #3#0;
 |LEE 001#13=;
 |SI FSalida = 0;
     |SI #0#48 = "N";
         |ACABA;
     |SINO;
         |BORRA 020#13a;
         |BUCLE BorroB;
     |FINSI;
 |FINSI;

 |ABRE #14;
 |BUCLE LosGraboB;
 |CIERRA #14;
 |PARA nNume1 = 0; |SI nNume1 [ nNumCampo3; |HACIENDO nNume1 = nNume1 + 1;
       #13nNume1 = #3nNume1;
 |SIGUE;
 |GRABA 020#13n;
|FPRC;

|DEFBUCLE AstosFijosE;
 #3#1;
 11;
 #0#44, #0#46;
 #0#45, #0#47;
 e;
 NULL, AstosFijosE;
|FIN;
|| -------------------------------------------------------------------

|PROCESO LosGraboA;
 |PARA nNume1 = 0; |SI nNume1 [ nNumCampo2; |HACIENDO nNume1 = nNume1 + 1;
       #12nNume1 = #2nNume1;
 |SIGUE;
 |GRABA 020#12n;
|FPRC;

|DEFBUCLE LosGraboA;
 #2#1;
 ;
 #1#0, 0001;
 #1#0, 9999;
 be;
 NULL, LosGraboA;
|FIN;

|PROCESO BorroA;
 |BORRA 020#12a;
 |LIBERA #12;
|FPRC;

|DEFBUCLE BorroA;
 #12#1002;
 ;
 #11#0, 0001;
 #11#0, 9999;
 be;
 NULL, BorroA;
|FIN;

|PROCESO AstosFijos;
 #11#0 = #1#0;
 |LEE 001#11=;
 |SI FSalida = 0;
     |SI #0#48 = "N";
         |ACABA;
     |SINO;
         |BORRA 020#11a;
         |BUCLE BorroA;
     |FINSI;
 |FINSI;

 |ABRE #12;
 |BUCLE LosGraboA;
 |CIERRA #12;
 |PARA nNume1 = 0; |SI nNume1 [ nNumCampo1; |HACIENDO nNume1 = nNume1 + 1;
       #11nNume1 = #1nNume1;
 |SIGUE;
 |GRABA 020#11n;
|FPRC;

|DEFBUCLE AstosFijos;
 #1#1;
 11;
 #0#40, #0#42;
 #0#41, #0#43;
 e;
 NULL, AstosFijos;
|FIN;

|PROCESO NuevoAuxiliar;
 #1#0 = #999#1;
 |LEE 041#1=;
 |SI FSalida = 0;
      |HAZ AstosFijos;
 |FINSI;
 |SI #999#2 > 0;
     #1#0 = #999#2;
     |LEE 041#1=;
     |SI FSalida = 0;
         |HAZ AstosFijos;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE NuevoAuxiliar;
 #999#1;
 ;
 "F", 00001;
 "F", 99999;
 e;
 NULL, NuevoAuxiliar;
|FIN;

|PROCESO CopioaEmp;

    |SI #998#1 = #0#37; |Y #998#2 = #0#38; |ACABA; |FINSI;

    eaDestino = #998#3;
    |QBF eaDestino;

    |PATH_DAT #11 eaDestino;
    |PATH_DAT #12 eaDestino;
    |PATH_DAT #13 eaDestino;
    |PATH_DAT #14 eaDestino;

    |SI aParam = "E2010";
        |ABRE #11;
        |ABRE #1;
        |BUCLE NuevoAuxiliar;
        |CIERRA #1;
        |CIERRA #11;
    |SINO;
       |SI #0#40 ! 0;
           |ABRE #11;
           |BUCLE AstosFijos;
           |CIERRA #11;
       |FINSI;
       |SI #0#44 ! 0;
           |ABRE #13;
           |BUCLE AstosFijosE;
           |CIERRA #13;
       |FINSI;
    |FINSI;
|FPRC;

|DEFBUCLE CopioaEmp;
 #998#1;
 ;
 000001;
 999999;
 ;
 NULL, CopioaEmp;
|FIN;


|PROCESO LaCopia;

  |PATH_DAT #1 eaOrigen;
  |PATH_DAT #2 eaOrigen;
  |PATH_DAT #3 eaOrigen;
  |PATH_DAT #4 eaOrigen;

  aQueQuiero = "|NC";

  aNumCampos = #1#aQueQuiero#; nNumCampo1 = aNumCampos; nNumCampo1 = nNumCampo1 - 1;
  aNumCampos = #2#aQueQuiero#; nNumCampo2 = aNumCampos; nNumCampo2 = nNumCampo2 - 1;
  aNumCampos = #3#aQueQuiero#; nNumCampo3 = aNumCampos; nNumCampo3 = nNumCampo3 - 1;
  aNumCampos = #4#aQueQuiero#; nNumCampo4 = aNumCampos; nNumCampo4 = nNumCampo4 - 1;

  |DBASE aBass;  |QBT aBass;

  aDef = aBass + "def/camautoc.mas";
  |CARGA_DEF aDef, imautoc@;
  |SI FSalida ! 0;
      |MENAV "    Error en def camautoc. Proceso interrumpido";
      |ACABA;
  |FINSI;

  aDef = aBass + "def/camautol.mas";
  |CARGA_DEF aDef, imautol@;
  |SI FSalida ! 0;
      |MENAV "    Error en def camautol. Proceso interrumpido";
      |DESCARGA_DEF #11;
      |ACABA;
  |FINSI;

  aDef = aBass + "def/camauto0.mas";
  |CARGA_DEF aDef, imauto0@;
  |SI FSalida ! 0;
      |MENAV "    Error en def camauto0. Proceso interrumpido";
      |DESCARGA_DEF #12;
      |DESCARGA_DEF #11;
      |ACABA;
  |FINSI;

  aDef = aBass + "def/camauto1.mas";
  |CARGA_DEF aDef, imauto1@;
  |SI FSalida ! 0;
      |MENAV "    Error en def camauto1. Proceso interrumpido";
      |DESCARGA_DEF #13;
      |DESCARGA_DEF #12;
      |DESCARGA_DEF #11;
      |ACABA;
  |FINSI;

  |BUCLE CopioaEmp;

  |DESCARGA_DEF #14;
  |DESCARGA_DEF #13;
  |DESCARGA_DEF #12;
  |DESCARGA_DEF #11;
|FPRC;

|| ************************************************************
|CALCULO Tipo3; |TIPO 3;
  aFichero = "1n" + Usuario;
  |NOME_DAT #999 aFichero;

  |DBASE aBass;  |QBT aBass;

  aBass  = aBass + "fich/";
  |SI #0#37 ! 0;
      aAlfa1 = #0#37; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
      aAlfa2 = #0#38; |QBF aAlfa2; aAlfa2 = ("0"     + aAlfa2) % -101;
      eaOrigen = aBass + aAlfa1 + aAlfa2 + "/";
  |SINO;
      eaOrigen = aBass;
      |NOME_DAT #1 "prmautoc";
      |NOME_DAT #2 "prmautol";
      |NOME_DAT #3 "prmauto0";
      |NOME_DAT #4 "prmauto1";
  |FINSI;

  |HAZ DirAplicSt;

  aFichero = ("u4" + Usuario) % 108;
  |NOME_DAT #998 aFichero;
  |DELINDEX #998;

  Linea = 1;
  |ABRE #998;
  |SI #0#49 ! 0;
      |HAZ PorPlanilla;
  |SINO;
       aEstEmp    = "T";
       aSelTipo   = "S"; nDTip = 0; nHTip = 99;
       |SI #0#2 = "S";
           |BUCLE Rellena;
       |SINO;
           |PARA nPara1 = 7; |SI nPara1 [ 16; |HACIENDO nPara1 = nPara1 + 1;
                 nPara2 = nPara1 + 10;
                 |SI #0nPara1 ! 0;
                    |DDEFECTO #998;
                    #998#0 = Linea;
                    #998#1 = #0nPara1;
                    #998#2 = #0nPara2;
                    |HAZ CalDirectorio;
                    |SI nSwNoDir = 0;
                        |GRABA 020#998n;
                        Linea  = Linea + 1;
                    |FINSI;
               |FINSI;
          |SIGUE;
      |FINSI;
  |FINSI;
  |CIERRA #998;

  |SI Linea = 1;
      |MENAV "    Seleccion Vacia";
  |SINO;
      |HAZ LaCopia;
  |FINSI;
|FCAL;


|PROGRAMA;

 aParam = PARAMETRO; |QBF aParam;
 |SI aParam ! "E2010"; |HAZ inicio; |FINSI;

 |DDEFECTO #0;
 #0#37 = enEmpresa;
 #0#38 = enPeriodo;
 #0#39 = eaNombreEmp;
 #0#1  = enEjercicio;

 |SI enNFijo ! 0; |O enNFijE ! 0; |O aParam = "E2010";
     |C_M #0#37,1,"N"; |C_M #0#38,1,"N";
     |C_M #0#40,1,"N"; |C_M #0#41,1,"N";
     |C_M #0#42,1,"N"; |C_M #0#43,1,"N";
     |C_M #0#44,1,"N"; |C_M #0#45,1,"N";
     |C_M #0#46,1,"N"; |C_M #0#47,1,"N";
     |SI enNFijo ! 0;
         #0#40 = enNFijo;
         #0#41 = enNFijo;
     |FINSI;
     |SI enNFijE ! 0;
         #0#44 = enNFijE;
         #0#45 = enNFijE;
     |FINSI;
     |SI aParam = "E2010";
         #0#40 = enCampo40;
         #0#41 = enCampo41;
         #0#42 = enCampo42;
         #0#43 = enCampo43;
         |C_M #0#48,1,"N"; #0#48 = "S";
     |FINSI;
 |FINSI;
 |C_M #0#38,0,aAlfa3;

 |CLS;
 |PINPA #0#0;
 |PINDA #0#0;
 |SI enNFijo ! 0; |O enNFijE ! 0;
     |POPV;
     |CUADRO 1001 2380;
     |ATRI R; |PINTA 1102," COPIAR ASIENTO FIJO A OTRAS EMPRESAS "; |ATRI 0;
     |PINTA 1140, "                                        ";
 |FINSI;
 |SI aParam = "E2010";
     |ATRI P; |PINTA 1403,"COPIAR ASIENTO MODIFICADOS POR CAMBIOS TIPO IVA ";  |ATRI 0;
 |FINSI;
 |ENDATOS #1#0;
|FPRO;
