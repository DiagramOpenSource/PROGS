|FICHEROS;
   cabusapu #0;
   cabusap1 #1;
   cacuenta #2;
   camaasic #3;
   camaasil #4;
   caextrp4 #6;         || display asientos completo

   canempre #30;
   caselem1 #998;
   cabusap2 #12,MANTE;
   espejo@  #901;
|FIN;

|VARIABLES;
   aRpt = ""; ||PROVISIONAL POR LOS RPT's SISCO
   nSalir = 0;
   aFichero  = "";
   &tipo     = "";
   &pag      = "";
   alfa      = "";
   alf1      = "";
   alf2      = "";
   sw        = 0;
   inform1   = "cabusapu";
   inform2   = "cabusap1";
   informe   = "";
   ficher1   = "";
   ficher2   = "";
   hay       = 0;
   sw_des    = 0;
   i         = 0;
   ds        = "";
   hs        = "";
   cuenta    = "";
   dfecha    = "31.12.9999";
   hfecha    = "01.01.0000";
   dig       = 0;
   debe      = 0;
   haber     = 0;
   saldo     = 0;
   SwSal     = 0;
   salida    = 0;

   {-1}menu      = "";
   menu1     = "2400",
   menu2     = "1";
   menu3     = "Tipo Informe: ";
   menu4     = "PI";
   menu5     = " Pantalla ";
   menu6     = " Impresora ";
   {-1}amenu      = "";
   amenu1     = "2400",
   amenu2     = "1";
   amenu3     = "Salto Pagina por Cuenta: ";
   amenu4     = "SN";
   amenu5     = " SI ";
   amenu6     = " NO ";

   nDebe   = 0;
   nHaber  = 0;
   nApunte = 0;

   &enDiario          = 0;
   &efFecha           = @;
   &enDocumento       = 0;
   &enDiarioDentro    = 0;
   &efFechaDentro     = @;
   &enDocumentoDentro = 0;
   aMas   = "";
   aDef   = "";
   &enDnDIARIO = 0;
   &enHnDIARIO  =0;
   &efDFechas  = @;
   &efHFechas  = @;
   &enDDocume  = 0;
   &enHDocume  = 0;
   &enBorraAu  = 0;
   nBaja       = 0;
   seleccio    = 0;
   lalinea     = 0;
   totallin    = 0;

   ultdirfich  = "";
   &eSwMul   = 0;
   &espe15   = "";
   CodigoEmpresa  = 0;
   PeriodoEmpresa = 0;
   dirfichOri     = "";
   nSitua         = 0;
   &swVarios      = 0;
|FIN;

|INCLUYE dscont_i;

|CALCULO cuenta; |TIPO 6;
     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;

     |HAZ SacaNivel;
|FCAL;

|CALCULO nodepar; |TIPO 7;
     |CAMPO_MODIFICA #0#15, 1, eaDepar;
     |CAMPO_MODIFICA #0#16, 1, eaDepar;
|FCAL;

|CALCULO nosubde; |TIPO 7;
     |CAMPO_MODIFICA #0#17, 1, eaSubde;
     |CAMPO_MODIFICA #0#18, 1, eaSubde;
|FCAL;

|CALCULO defectos; |TIPO 0;
  alf1 = Contenido;
  |SI alf1 ! #0#19;
      |SI #0#19 = "N";
          #0#2  = fec1;
      |SINO;
          #0#2  = "01.01.2000";
      |FINSI;
      |PINTA #0#2;
      #0#3 = fec2; |PINTA #0#3;
 |FINSI;
|FCAL;

-------------------------------------------------------------------------
|PROCESO camaasil;
   |SI sw_des = 1;
      |PARA i = 6; |SI i [ 11; |HACIENDO i = i + 1;
         alf1 = #4#7;  |QBF alf1; alf1 = alf1 > "";
         alf2 = #0i;   |QBF alf2; alf2 = alf2 > "";
         alfa = alf1 - alf2;
         |SI alf1 ! alfa; |SAL; |FINSI;
      |SIGUE;
      |SI i > 11; |ACABA; |FINSI;
   |FINSI;
   |PARA i = 0; |SI i [ 9; |HACIENDO i = i + 1;
      #1i = #4i;
   |SIGUE;
   #1#10 = enEmpresa;
   #1#11 = enPeriodo;
   #1#12 = dirfich0;
   #1#13 = fec3;
   |GRABA 021#1n;
   hay = hay + 1;
|FPRC;

|DEFBUCLE camaasil;
   #4#3;
   9,6,8,21,28;||Cta,Dig,Fec,Doc,Impo,Conce,Signo,Departamento, Subdepatamento
   #0#4, #0#2, 000001, 0001, #0#0, #0#12, ds, #0#15,#0#17;
   #0#5, #0#3, 999999, 9999, #0#1, #0#13, hs, #0#16,#0#18;
   ;
   NULL, camaasil;
|FIN;

|PROCESO HazTodo;
     |ABRE #1;
     |BUCLE camaasil;
     |CIERRA #1;
|FPRC;

|| ====================================================================
|PROCESO cabusapI;
   |SI cuenta ! ""; |Y cuenta ! #1#4; |Y pag ! "2";
       |PIEINF;
   |FINSI;
   |PRINT;
   cuenta = #1#4;
|FPRC;

|DEFBUCLE cabusapI;
   #1#2;
   ;
   "            ", "01.01.0000", 00;
   "zzzzzzzzzzzz", "99.99.9999", 99;
   ;
   NULL, cabusapI;
|FIN;

|PROCESO impresora;
   |IMPRE 0;
   |SI FSalida = 0;
      |SI pag ! "2";
         informe = inform1;
      |SINO;
         informe = inform2;
||-------------CODIGO PROVISIONAL SUBCABECERAS-------------SISCO
          aRpt = Impresora;
          |QBF aRpt;
          |SI aRpt = "";
               informe = "cabusap3";
               pag = 1;
          |FINSI;
||-------------CODIGO PROVISIONAL SUBCABECERAS-------------SISCO

      |FINSI;
      |INFOR informe;
      |SI FSalida = 0;
         |BUCLE cabusapI;
         |PIEINF;
      |FINSI;
      |FINIMP;
      |FININF;
   |FINSI;
|FPRC;

||=================================================================
|PROCESO VerApunte;
      |SI eSwMul ! 0;
          |CIERRA #4;
          dirfich0 = #12#15; |QBF dirfich0;
          |PATH_DAT #3 dirfich0;
          |PATH_DAT #4 dirfich0;
          |ABRE #4;
      |FINSI;

      |PTEC 802;
      |PTEC 802;
      |PTEC 802;
      |PUSHV 0501 2380;
      |PINPA #0#3;
      |ABRE #3;
      #3#0 = #12#0;
      #3#1 = #12#1;
      #3#2 = #12#2;
      |LEE 000#3=;
      |CONSULTA #0#3;
      |CIERRA #3;
      |POPV;
      |ERROR;

      |SI eSwMul ! 0;
          |CIERRA #4;
      |FINSI;
|FPRC;

|PROCESO CalculaTotales;
     |SI #12#8  = "D";
         #12#10 = #12#9;
         nDebe  = nDebe  + #12#9;
     |SINO;
         #12#11 = #12#9;
         nHaber = nHaber + #12#9;
     |FINSI;

     #12#12 = nDebe - nHaber;
     |GRABA 020#12a;
     |LIBERA #12;
|FPRC;

|DEFBUCLE cabusap2B;
   #12#1;
   ;
   "01.01.0000", 00, 000000, 0000;
   "31.12.2999", 99, 999999, 9999;
   ;
   NULL, CalculaTotales;
|FIN;

|PROCESO ModificarApunte;
   enDiario    = #12#0;         || diario
   efFecha     = #12#1;         || fecha
   enDocumento = #12#2;         || documento
   nApunte     = #12#3;

   |SI #12#1 [ #12#16;
       |MENAV "    ERROR FECHA INFERIOR O IGUAL AL BLOQUEO DE DATOS";
       |ACABA;
   |FINSI;

   |SI enDiarioDentro = enDiario;  |Y efFechaDentro = efFecha;
    |Y enDocumentoDentro = enDocumento;
       |MENAV "      Registro Bloqueado, usted a llamado a la busqueda desde este asiento.";
       |ERROR;
       |ACABA;
   |FINSI;

   |LEE 101#12=;
   |SI FSalida ! 0;  |ACABA;  |FINSI;

   |SI eSwMul ! 0;
      enEmpresa = #12#13;
      enPeriodo = #12#13;
      dirfich0 = #12#15; |QBF dirfich0;
      |CIERRA #4;
      |PATH_DAT #2 dirfich0;
      |PATH_DAT #4 dirfich0;
      |ABRE #4;
   |FINSI;

   #12#10 = 0;
   #12#11 = 0;
   #12#12 = 0;

   swVarios   = 0;
   |SI eSwMul ! 0; swVarios = 1; |FINSI;
   |SUB_EJECUTA "camaasic;NOMEQUITESQUETECORTOLOSGUEVOS";

   #4#0 = #12#0;       || diario
   #4#1 = #12#1;       || fecha
   #4#2 = #12#2;       || documento
   #4#3 = #12#3;       || linea apunte
   |LEE 040#4=;
   |SI FSalida ! 0;  |DDEFECTO #4;  |FINSI;

   #12#4 = #4#4;
   #12#5 = #4#5;
   #12#6 = #4#6;
   #12#7 = #4#7;
   #12#8 = #4#8;
   #12#9 = #4#9;

   nDebe   = 0;
   nHaber  = 0;
   |GRABA 020#12a;
   |LIBERA #12;

   |BUCLE cabusap2B;

   #12#0 = enDiario;
   #12#1 = efFecha;
   #12#2 = enDocumento;
   #12#3 = nApunte;
   |LEE 040#12=;

   |PONCONTROL 23, "SI";
   |PTEC 809;
|FPRC;

|| *************
|PROCESO Ncuenta;
   |ABRE #2;
   #2#0 = #4#4;         || cuenta
   |LEE 001#2=;
   |SI FSalida ! 0;
       |DDEFECTO #2;
       #2#2 = "<CUENTA INEXISTENTE>";
   |FINSI;
   |CIERRA #2;
|FPRC;

|PROCESO lee_asiento1;             || CARGA LA PANTALLA DEL ASIENTO COMPLETO
   seleccio = 1;
   |HAZ Ncuenta;
   |DDEFECTO #6;
   #6#00 = #4#3;        || apunte
   #6#01 = #4#16;       || cuenta DEBE
   #6#02 = #4#17;       || digito DEBE
   #6#03 = #4#10;       || cuenta HABER
   #6#04 = #4#11;       || digito HABER
   #6#05 = #4#6;        || concepto
   #6#06 = #4#7;        || descripcion
   #6#07 = #4#21;       || departamento
   #6#08 = #4#9;        || importe
   #6#09 = #2#2;        || descripcion
   #6#10 = #2#51;       || debe
   #6#11 = #2#52;       || haber
   #6#12 = #2#53;       || saldo
   #6#13 = #4#0;        || diario
   #6#14 = #4#1;        || fecha
   #6#15 = #4#2;        || documento
   #6#16 = #4#28;       || Subdepartamento
   |SI #6#0 = #4#19;
       lalinea = #6#0;
   |FINSI;
   |GRABA 020#6n;
   totallin = totallin + 1;
|FPRC;

|DEFBUCLE lee_asiento;
   #4#1;
   ;
   #12#0, #12#1, #12#2,    1;
   #12#0, #12#1, #12#2, 9999;
   e;
   NULL, lee_asiento1;
|FIN;


|PROCESO mini;
   |SI FSalida = "POSICION";  |Y totallin > 8;
       #caextrp4#0 = lalinea;
   |SINO;
       #caextrp4#0 = 1;
   |FINSI;
|FPRC;

|PROCESO maxi;
   #caextrp4#0 = 9999;
|FPRC;

|PROCESO BajaApunte;
   enDiario    = #12#0;         || diario
   efFecha     = #12#1;         || fecha
   enDocumento = #12#2;         || documento
   nApunte     = #12#3;

   |SI #12#1 [ #12#16;
       |MENAV "    ERROR FECHA INFERIOR O IGUAL AL BLOQUEO DE DATOS";
       |ACABA;
   |FINSI;

   |SI enDiarioDentro = enDiario;  |Y efFechaDentro = efFecha;
    |Y enDocumentoDentro = enDocumento;
       |MENAV "      Registro Bloqueado, usted a llamado a la busqueda desde este asiento.";
       |ERROR;
       |ACABA;
   |FINSI;

   |LEE 101#12=;
   |SI FSalida ! 0;  |ACABA;  |FINSI;

   #12#10 = 0;
   #12#11 = 0;
   #12#12 = 0;

   |SI eSwMul ! 0;
      dirfich0 = #12#15; |QBF dirfich0;
      |CIERRA #4;
      |PATH_DAT #2 dirfich0;
      |PATH_DAT #4 dirfich0;
      |ABRE #4;
   |FINSI;

   aFichero = ("q" + Usuario);
   aFichero = (aFichero + "zzzzzzzz") % 108;
   |NOME_DAT #6 aFichero;
   |DELINDEX #6;
   seleccio = 0;
   |ABRE #6;
   lalinea = 1;
   totallin = 0;
   |CIERRA #4;
   |BUCLE lee_asiento;
   |ABRE #4;
   |CIERRA #6;

   |SI seleccio ! 0;
      |PUSHV 07012380;

      |PINPA #0#6;
      |PINTA #6#13; |PINTA #6#14; |PINTA #6#15;
      |ATRI P; |PINTA 0861, "BAJA ASIENTO"; |ATRI 0;

      |ENTLINEAL #1#6, -1, 7, 20, 0, mini, maxi;
      |CONFI "    NBorrar Asiento S/N";
      |SI FSalida = 0;
          |HAZ BorroAsiento;
      |FINSI;
      |POPV;
   |FINSI;

   |SI eSwMul ! 0;
       |CIERRA #4;
   |FINSI;

   |DELINDEX #6;

   |PONCONTROL 23, "SI";
   |PTEC 809;


|FPRC;

|PROCESO BorroAsiento;

   enDnDIARIO = enDiario;            || diario
   enHnDIARIO = enDiario;
   efDFechas  = efFecha;             || fecha
   efHFechas  = efFecha;             || fecha
   enDDocume  = enDocumento;         || documento
   enHDocume  = enDocumento;         || documento

   enBorraAu  = 2;
   swVarios   = 0;
   |SI eSwMul ! 0; swVarios = 1; |FINSI;
   |SUB_EJECUTA_NP "caborasi;NOMEQUITESQUETECORTOLOSGUEVOS";

   #4#0 = #12#0;       || diario
   #4#1 = #12#1;       || fecha
   #4#2 = #12#2;       || documento
   #4#3 = #12#3;       || linea apunte
   |LEE 040#4=;
   |SI FSalida ! 0;
       |BORRA 020#12a;

       |BUCLE cabusap2B;
   |FINSI;
   |LIBERA #12;
|FPRC;

|PROCESO tecla; |TIPO 11;
     salida = FSalida;

     |SI salida = 10;  |HAZ VerApunte;        |FINSI;
     |SI salida = 11;  |HAZ BajaApunte;       |FINSI;
     |SI salida = 12;  |HAZ ModificarApunte;  |FINSI;
|FPRC;

|PROCESO inf;
   #cabusap2#0 = 00;
   #cabusap2#1 = "01.01.0000";
   #cabusap2#2 = 000001;
   #cabusap2#3 = 0001;
|FPRC;

|PROCESO sup;
   #cabusap2#0 = 99;
   #cabusap2#1 = "31.12.9999";
   #cabusap2#2 = 999999;
   #cabusap2#3 = 9999;
|FPRC;

|PROCESO Pantalla;
   SwSal = 0;

   |SI eSwMul ! 0;
       |PATH_DAT #2 ultdirfich;
   |FINSI;

   |ABRE #2;
   |DDEFECTO #2;
   #cacuenta#0 = cuenta;
   #cacuenta#1 = dig;
   |LEE 000#2=;
   |CIERRA #2;

   |ATRI I;
   |PINTA 0703, cuenta;
   |PINTA 0716, #cacuenta#2;
   |ATRI I;
   |PINTA 0756, dfecha;
   |PINTA 0769, hfecha;
   |ATRI 0;
   |MENSA "2400 [ESC] Cuenta siguiente     [CTRL+Q] Acabar";

   |CIERRAT #12;

   |ENTLINEAL #1#12, -2, 4, 21, 0, inf, sup;

   |DELINDEX #12;

   |ABRET #12;
   dfecha    = "31.12.9999";
   hfecha    = "01.01.0000";
   debe      = 0;
   haber     = 0;
   saldo     = 0;
   |BLIN 24;
|FPRC;

|PROCESO cabusap2;
   |SI eSwMul ! 0;
       |CIERRA #4;
       dirfich0 = #1#12; |QBF dirfich0;
       |PATH_DAT #4 dirfich0;
       |ABRE #4;
   |FINSI;

   #4#0 = #cabusap1#0;
   #4#1 = #cabusap1#1;
   #4#2 = #cabusap1#2;
   #4#3 = #cabusap1#3;
   |LEE 001#4=;
   |SI FSalida ! 0; |VETE ElAcaba; |FINSI;
   |SI #4#4 ! #1#4; |VETE ElAcaba; |FINSI;

   |SI cuenta ! ""; |Y cuenta ! #cabusap1#4;
       |HAZ Pantalla;
       |SI salida = 7; SwSal = 1; |ERROR10; |VETE ElAcaba; |FINSI;
   |FINSI;

   |PARA i = 0; |SI i [ 9; |HACIENDO i = i + 1;
         #cabusap2#i# = #cabusap1#i#;
   |SIGUE;

   cuenta = #cabusap1#4;
   dig    = #cabusap1#5;
   |SI #cabusap1#1 < dfecha; dfecha = #cabusap1#1; |FINSI;
   |SI #cabusap1#1 > hfecha; hfecha = #cabusap1#1; |FINSI;
   debe  = 0;
   haber = 0;
   |SI #cabusap1#8 = "D";
       debe = #cabusap1#9;
       saldo = saldo + #cabusap1#9;
   |SINO;
       haber = #cabusap1#9;
       saldo = saldo - #cabusap1#9;
   |FINSI;
   #cabusap2#10 = debe;
   #cabusap2#11 = haber;
   #cabusap2#12 = saldo;
   #cabusap2#13 = #1#10;    || empresa
   #cabusap2#14 = #1#11;    || periodo
   #cabusap2#15 = #1#12;    || directorio
   #cabusap2#16 = #1#13;    || fecha bloqueo

   |GRABA 021#12n;

   |SI eSwMul ! 0;
       ultdirfich = dirfich0;
   |FINSI;

|ET ElAcaba;
   |SI eSwMul ! 0;
       |CIERRA #4;
   |FINSI;
|FPRC;

|DEFBUCLE cabusap2;
   #1#2;
   ;
   "            ", "01.01.0000", 00;
   "zzzzzzzzzzzz", "99.99.9999", 99;
   ;
   NULL, cabusap2;
|FIN;

|PROCESO cabusap1;

      |HAZ nom_usu;
      ficher2 = "e" + nom_tmp;
      |NOME_DAT #12 ficher2;
      |DELINDEX #12;

      cuenta = "";
      |CLS;
      |PINPA #0#12;
      |CABEZA "Apuntes Encontrados";

      |SI eSwMul = 0;
          |ABRE #4;
      |FINSI;

      |ABRET #12;
      SwSal = 0;
      |BUCLE cabusap2;
      |SI SwSal = 0; |HAZ Pantalla; |FINSI;
      |CIERRAT #12;

      |SI eSwMul = 0;
          |CIERRA #4;
          |CIERRA #2;
      |FINSI;

      |DELINDEX #12;
|FPRC;


|CALCULO tipo3; |TIPO 3;
   ds = " "; hs = "z";
   |SI #0#14 ! "T";   ds = #0#14; hs = #0#14;  |FINSI;

   sw_des = 0;
   alfa = #0#6 + #0#7 + #0#8 + #0#9 + #0#10 + #0#11;
   |QBF alfa;
   |SI alfa ! ""; sw_des = 1; |FINSI;

   aFichero = ("9z" + Usuario) % 108;
   |NOME_DAT #1 aFichero;
   |DELINDEX #1;

   |DBASE aMas;
   aDef = aMas + "def/cabusap1";
   |CARGA_DEF aDef, espejo@;
   |SI FSalida ! 0;
       |MENAV "    Error Cargado " + aDef;
       |ACABA;
   |FINSI;
   |NOME_DAT #901 aFichero;
   hay = 0;

   nSitua = 0;
   eSwMul = 0;
   |SI #0#19 = "S";
       |HAZ MultiEjercicio;
   |SINO;
       dirfich0 = dirfichOri;
       |HAZ HazTodo;
   |FINSI;

   |SI hay = 0;
       |MENAV "0000Seleccion Vacia...";
   |SINO;
       |MENU menu;
       tipo = FSalida;
       |SI tipo = 2;
           |MENU amenu;
           pag = FSalida;
           |HAZ impresora;
       |SINO;
           |HAZ cabusap1;
       |FINSI;
   |FINSI;

   |DESCARGA_DEF #901;
   |DELINDEX #1;

   |SI nSitua = 1;
       cod1 = CodigoEmpresa;
       cod2 = PeriodoEmpresa;
       emEmp = 1;  || Sin mensaje
       |HAZ leelo;
       |CIERRA #2;
       |CIERRA #3;
       |CIERRA #4;
       |PATH_DAT #2 dirfichOri;
       |PATH_DAT #3 dirfichOri;
       |PATH_DAT #4 dirfichOri;
       dirfich0 = dirfichOri;
   |FINSI;
|FCAL;

|| ******************************************************************
|PROGRAMA;
 |CLS;
 |CABEZA "Busqueda de Apuntes";
 |HAZ inicio;

 CodigoEmpresa    = enEmpresa;
 PeriodoEmpresa   = enPeriodo;
 |DFICE dirfichOri;

 |DDEFECTO #0;
 |ABRE #2;
 |LEE 000#2p;  #0#4 = #2#0;
 |LEE 000#2u;  #0#5 = #2#0;
 |CIERRA #2;

 #0#2 = fec1;
 #0#3 = fec2;

 nSalir = 0;
 |PARA; |SI nSalir = 0; |HACIENDO;
        |PINPA #0#0;
        |PINDA #0#0;                    || Pinta los datos por defecto
        |ENDATOS #1#0;                  || Seleccion informe
        |SI FSalida ! 0;
            nSalir = 1;
        |FINSI;
 |SIGUE;
|FPRO;

|| ***********************************************************************
|PROCESO MultiEmp1;
   nSitua = 1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;
   |SI #0#2 > #caselem1#5; |O #0#3 < #caselem1#4;
       |ACABA;  || Fuera de la seleccion
   |FINSI;

   |PATH_DAT #3 dirfich0;
   |PATH_DAT #4 dirfich0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
   #caselem1#1;
   ;
   000001;
   999999;
   ;
   NULL,MultiEmp1;
|FIN;

|PROCESO MultiEjercicio;
   eSwMul = 1;
   espe15 = "S";

   |SUB_EJECUTA "caselemp";
   |NOME_DAT #998 eaFichsel;

   |BUCLE MultiEmp0;
   |DELINDEX #998;
|FPRC;
