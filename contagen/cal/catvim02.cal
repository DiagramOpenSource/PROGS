|FICHEROS;
     catvim02;
     cacuenta;
     dscsvw10;

     catviw03,MANTE;

     espejo@;

     cacuebal;
     ca8cumay #410;
     ca8m0002 #422;
|FIN;

|VARIABLES;
   aAlfa       = "";
   alfa1       = "";
   alfa2       = "";
   alfa3       = "";
   alfa4       = "";
   nNume1      = 0;
   nNume2      = 0;
   nSwNivel    = 0;
   nError      = 0;
   aFichero    = "";
   aFicBrowse  = "";
   aVer1       = "";
   aVer2       = "";
   {1}aConte   = "";
   aOrigen     = "";
   aDestino    = "";
   aPathLocal  = "";
   aPathTmp    = "";
   aPathContagen = "";
   aBaseConta  = "";
   aParam1     = "";
   aParam2     = "";
   aParam3     = "";
   aProg       = "";
   aDocRemoto  = "";
   nCarac     = 0;
   nPos       = 0;
   aCaracter  = "";
   aDocServidor = "";
   {1}aContiene  = "";

   nReg     = 0;
   nCmp     = 0;
   nTCampos = 0;
   nTotCmp  = 0;
   aC10     = "";
   aC13     = "";
   nTValor  = 0;
   nPorce   = 0;
   nCampo   = 0;

   aVar      = "";
   aCarac    = "";
   nHandle   = 0;
   aLong     = "";
   nLong     = 0;
   aLee      = "";
   aAbre     = "";
   aMas      = "";
   aDef      = "";
   aRuta     = "";
   &eaUnidadBasico = "";
   &eaIP           = "";
   &eaNomOri       = "";
|FIN;

|INCLUYE dscont_i;
|INCLUYE defec1_i;

|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\

|CALCULO FiltraReten; |TIPO 6;
     eaCuenta = #catvim02#Campo#;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #catvim02#Campo# = eaCuenta;
     |PINTA #catvim02#Campo#;
|FCAL;

|CALCULO inf;
   #cacuenta#0 = "6           ";
   #cacuenta#3 = "S";
|FCAL;

|CALCULO sup;
   #cacuenta#0 = "7zzzzzzzzzzz";
   #cacuenta#3 = "S";
|FCAL;

|CALCULO ConsultaRen; |TIPO 11;
   |SI FSalida = 10;
      |ABRE #cacuenta;
      |CONSULTA_F_CLAVE #6#cacuenta, inf, sup;
      #catvim02#0 = #cacuenta#0; |PINTA #catvim02#0;
      #catvim02#1 = #cacuenta#2; |PINTA #catvim02#1;
      |ERROR;
      |CIERRA #cacuenta;
   |FINSI;

   |SI FSalida = 11;
       |HAZ Recoger;
       |PONCONTROL 23, "SI";
   |FINSI;

   |SI FSalida = 20;
     eaCuenta = #catvim02#Campo#;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
  |FINSI;
|FCAL;

|| /////////////////////////////////////////////////////////////////////////
|PROCESO nuSacaNivel;

 |QBF eaCuenta;
 alfa1  = eaCuenta % 0;
 nNume1 = alfa1;

 |SI nNume1 < MaximoDigitos;
     nNume2 = MaximoDigitos - nNume1;
     alfa4 = "0" * nNume2;

     nSwNivel = 0;
     aAlfa = eaCuenta % 103;
     |SI aAlfa = "472"; |O aAlfa = "477";
        nSwNivel = 1;
     |SINO;
        |SI nNume1 [ 6;
            |SI aAlfa = "400"; |O aAlfa = "410"; |O aAlfa = "430";
                nSwNivel = 1;
            |FINSI;
        |FINSI;
     |FINSI;

     |SI nSwNivel = 0;
         alfa1 = eaCuenta % 104;
         alfa2 = eaCuenta % 508;
     |SINO;
         alfa1 = eaCuenta % 103;
         alfa2 = eaCuenta % 409;
     |FINSI;
     |QBF alfa2;

     alfa3 = alfa1 + alfa4 + alfa2;
     eaCuenta = alfa3;

 |FINSI;

 |HAZ SacaNivel;
|FPRC;
|| ////////////////////////////////////////

|PROCESO AltaCuenta;
    |DDEFECTO #cacuenta;
    #cacuenta#0 = eaCuenta;
    #cacuenta#1 = enNivel;
    #cacuenta#2 = #espejo@#1;
    #cacuenta#3 = "S";
    mascara = #cacuenta#0;
    |QBF mascara;
    mascara = mascara +"zzzzzzzzzzzz";
    mascara = mascara % 112;
    #cacuenta#54 = mascara;
    #cacuenta#55 = #cacuenta#1;
    NumMes = dig1; |HAZ NombreMes; #cacuenta#56 = NombreMes;
    NumMes = dig1 + 11; |SI NumMes > 12; NumMes = NumMes - 12; |FINSI;
    |HAZ NombreMes; #cacuenta#57 = NombreMes;
    |HAZ defect1;
    |GRABA 000#cacuenta.n;
|FPRC;

|PROCESO LaCuenta;
  eaCuenta = #espejo@#0;
  |HAZ nuSacaNivel;
  #espejo@#0 = eaCuenta;

  #cacuenta#0 = eaCuenta;
  |LEE 041#cacuenta.=;
  |SI FSalida ! 0;
      |HAZ AltaCuenta;
  |FINSI;
|FPRC;

|| /////////////////////////////////////////////////////////////////////////
|PROCESO dscsvw10Carga;
     nCampo = #dscsvw10#0 - 1;
     #espejo@#nCampo#= #dscsvw10#1;
|FPRC;

|DEFBUCLE dscsvw10Carga;
     #dscsvw10#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dscsvw10Carga;
|FIN;

|PROCESO dscsvw10Campos;
     nTCampos  = nTCampos + 1;
|FPRC;

|DEFBUCLE dscsvw10Campos;
     #dscsvw10#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dscsvw10Campos;
|FIN;

|PROCESO GrabaDatos;
     |CIERRA #dscsvw10;
     nTCampos = 0;
     |BUCLE dscsvw10Campos;

     nError   = 0;
     nTValor  = nTotCmp;
     |SI nTCampos < nTValor;
         |DELINDEX #dscsvw10;
         |ACABA;
     |FINSI;

     nPorce = ((nTValor * 100) / nTCampos) ! 0;
     |SI nPorce < 70;
         |DELINDEX #dscsvw10;
         |ACABA;
     |FINSI;

     nReg = nReg + 1;
     |SI nReg = 1;
         |DELINDEX #dscsvw10;
         |ACABA;
     |FINSI;

     |DDEFECTO #espejo@;
     |BUCLE dscsvw10Carga;

     |HAZ LaCuenta;
     |GRABA 001#espejo@.n;

     |DELINDEX #dscsvw10;
     |PULSATECLA;
|FPRC;

|PROCESO Grabaw10;
     #dscsvw10#0 = nCmp;
     #dscsvw10#1 = aVar;
     |GRABA 020#dscsvw10.n;
     |LIBERA #dscsvw10;
|FPRC;

|PROCESO SacaDatos;
     aLong  = aLee % 0;
     nLong  = aLong;
     aC10   = &10;
     aC13   = &13;
     |PARA nCarac = 1;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos   = (nCarac * 100) + 1;
           aCarac = aLee % nPos;

           |SI aCarac = aC13;
               aCarac = ";";
           |FINSI;

           |SI aCarac = ";";
               nCmp   = nCmp + 1;
               |HAZ Grabaw10;

               aVar   = "";
               aCarac = "";
           |FINSI;

           |SI aCarac = aC10;
               aCarac = "";
           |FINSI;

           |SI nCmp = nTotCmp;  |Y nCmp ! 0;
               |HAZ GrabaDatos;

               |ABRE #dscsvw10;

               nCmp = 0;
           |FINSI;

           aVar = aVar + aCarac;
      |SIGUE;
|FPRC;

|PROCESO LeeCSV;
     aAlfa = "Procesando registros del fichero de Contrapartidas";
     |PINTA 2205, aAlfa;

     |ABRE #dscsvw10;
     nCmp     = 0;
     nReg     = 0;
     |XABRE "B", aAbre, nHandle;
     |PARA;  |SI;  |HACIENDO;
          |XLEE nHandle, 255, aLee;
          |SI FSalida [ 0;  |SAL;  |FINSI;

          |HAZ SacaDatos;
     |SIGUE;
     |XCIERRA nHandle;

     |LEE 000#dscsvw10.p;
     |SI FSalida = 0;
         nCmp   = nCmp + 1;
         |HAZ Grabaw10;
         |HAZ GrabaDatos;
     |FINSI;
|FPRC;

|PROCESO Conversion;
     nError  = 0;
     aAlfa = aPathLocal + "*.csv";
     |R_PDIR aAlfa;
     |PARA;  |SI FSalida = 0;  |HACIENDO;
          |RFBORRA aAlfa;

          |R_SDIR aAlfa;
     |SIGUE;

     aParam2 = aPathLocal + "catvim02.txt";  |RFBORRA aParam2;

     aVer1   = "";
     aVer2   = "";
     aConte  = aBaseConta + "plugin/dsofficetxt.exe";
     |ESPECIFICA "MD5", aConte;
     aVer1 = FSalida;

     aConte  = aPathLocal + "dsofficetxt.exe";
     |ESPECIFICA "REMOTOMD5", aConte;
     aVer2 = FSalida;

     |SI aVer1 ! aVer2;
         aOrigen  = aBaseConta + "plugin/dsofficetxt.exe";
         aDestino = aPathLocal + "dsofficetxt.exe";

         |SI eaIP = "";
             |COPIA_FICHERO aOrigen, aDestino;
         |SINO;
             |ENVIA_FICHERO aOrigen, aDestino;
         |FINSI;

         aAlfa = aPathLocal + "dsofficetxt.exe";
         |XABRE "EC", aAlfa, 0;
         |SI FSalida < 0;
             |MENAV "0000No encuentro el conversor a CSV, por favor consulte con su administrador informatico.";
             nError     = 1;
             aFicBrowse = "";
             |ACABA;
         |FINSI;
     |FINSI;

     aProg    = aPathLocal + "dsofficetxt.exe";
     aParam1  = aFicBrowse;
     aParam2  = aPathLocal + "catvim02.csv";
     aParam3  = "";

     aAlfa  = "cmd " + &34 + "/c START /WAIT " + aProg + " " + aParam1 + " " + aParam2 + aParam3 + &34;
     |RSYSTEM aAlfa;

     |XABRE "EC", aParam2, 0;
     |SI FSalida < 0;
         nError     = 1;
         aFicBrowse = "";
         |MENAV "0000No se ha podido convertir a CSV el fichero seleccionado.";
         |ACABA;
     |FINSI;

     aFicBrowse = aParam2;
|FPRC;

|PROCESO Browse;
     aFicBrowse = "F*.*";
     |BROWSE aFicBrowse;

     aAlfa      = aFicBrowse;   |QBF aAlfa;
     aFicBrowse = "";
     aLong      = aAlfa % 0;
     nLong      = aLong;
     |PARA nCarac = 2;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos  = (nCarac * 100) + 1;
           aFicBrowse = aFicBrowse + (aAlfa % nPos);
     |SIGUE;

     aAlfa = aFicBrowse;
     aRuta = "";
     aLong = aAlfa % 0;
     nLong = aLong;
     |PARA nCarac = 1;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos       = (nCarac * 100) + 1;
           aCaracter  = aAlfa % nPos;
           aRuta      = aRuta    + aCaracter;
           eaNomOri   = eaNomOri + aCaracter;

           |SI aCaracter = "/";  |O aCaracter = "\";
               eaNomOri = "";
           |FINSI;
     |SIGUE;
|FPRC;

|CALCULO PonFichero; |TIPO 0;
     |SI FSalida = 10;
        |HAZ Browse;
        #catviw03#Campo# = aFicBrowse;
     |FINSI;
     |PINTA #catviw03#Campo#;

     aAlfa = #catviw03#Campo#; |QBF aAlfa;
     |SI aAlfa = "";
         |MENAV "0000Error. Introduzca Fichero a Importar";
         |ERROR;
     |FINSI;
|FCAL;

|PROCESO ElPase;
  aFicBrowse = #catviw03#1;
  |QBF aFicBrowse;

  |HAZ Conversion;

  aOrigen  = aFicBrowse;
  aDestino = aPathTmp + "catvim02.csv";
  |SI eaIP = "";
      |COPIA_FICHERO aOrigen, aDestino;
   |SINO;
      |RECIBE_FICHERO aOrigen, aDestino;
   |FINSI;
   aFicBrowse = aDestino;

   aAbre = aFicBrowse;  |QBF aAbre;
   |SI aAbre = ""; |ACABA;  |FINSI;

   aFichero = "dscsvw10" + Usuario;  |NOME_DAT #dscsvw10#aFichero#;

   nTotCmp = 3;
   |ABRE #cacuenta;
   |ABRE #espejo@;
   |HAZ LeeCSV;
   |CIERRA #espejo@;
   |CIERRA #cacuenta;

   |SI nReg = 0;
      |MENAV "0000No Hay datos para Pasar";
   |FINSI;
|FPRC;

|PROCESO Recoger;
   |PUSHV 0501 2380;
   |PINPA #0#catviw03;
   |PINDA #0#catviw03;
   |ENDATOS #1#catviw03;
   |SI FSalida = 0; |Y #catviw03#0 = "SI";
       |HAZ ElPase;
   |FINSI;
   |POPV;
|FPRC;

|| /////////////////////////////////////////////////////////////////////////

|CALCULO ClaveBaja22;
     #catvim02#0 = "            ";
|FCAL;

|CALCULO ClaveAlta22;
     #catvim02#0 = "zzzzzzzzzzzz";
|FCAL;

|PROGRAMA;
     eaIP = "";  |IP_REMOTO eaIP;

     |HAZ LeeUnidadBasico;
     aPathLocal = eaUnidadBasico + "\documentos";   |RMKDIR aPathLocal;
     aPathLocal = aPathLocal + "\tmp";              |RMKDIR aPathLocal;
     aPathLocal = aPathLocal + "\";
     |DBASE aBaseConta;
     aPathTmp   = aBaseConta + "tmp";           |MKDIR aPathTmp;
     aPathTmp   = aPathTmp   + "/" + Usuario;   |MKDIR aPathTmp;
     aPathTmp   = aPathTmp   + "/";

     aPathContagen = eaUnidadBasico + "\documentos";   |RMKDIR aPathContagen;
     aPathContagen = aPathContagen + "\contagen\";     |RMKDIR aPathContagen;

     aAlfa = aPathTmp + "catvim02.csv"; |FBORRA aAlfa;

     |DBASE aMas;
     aDef = aMas + "def/catvim02";
     |CARGA_DEF aDef, espejo@;
     |SI FSalida ! 0;
         |MENAV "    Error al Carga fichero de Contrapartidas";
         |ACABA;
     |FINSI;

     |CLS;
     |HAZ inicio;

     |ABRE #catvim02;
     |LEE 041#catvim02.p;
     |SI FSalida ! 0;
         |CIERRA #catvim02;
         |CONFI "0000SNo existe Fichero, quiere importarlo";
         |SI FSalida = 0;
             |HAZ Recoger;
         |FINSI;
     |SINO;
         |CIERRA #catvim02;
     |FINSI;

     |ENTLINEAL #1#catvim02, -1, 1, 22, 1, ClaveBaja22, ClaveAlta22;

     |DESCARGA_DEF # espejo@;

     aAlfa = aPathTmp + "catvim02.csv"; |FBORRA aAlfa;
|FPRO;
