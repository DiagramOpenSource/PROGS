|FICHEROS;
     caconi15 #0;

     cacuenta #4;
     caconi08 #8;
     caconi1l #10,MANTE;
     caivaasi #200;
     ca8cumay #410;

     :/modelos/def/dsmom030 #330;
|FIN;

|VARIABLES;
     aParam  = "";
     aAlfa   = "";
     aAlfa1  = "";
     aCuenta = "";
     aLong   = "";
     nLong   = 0;
     aCaracter = "";

     nLinea  = 0;
     nPara1  = 0;
     nSwPrim = 0;
     nSw     = 0;
     nClave  = 0;
     nCampo  = 0;
     nCampo1 = 0;
     nCampo2 = 0;
     nPosi   = 0;
     p_alfa1 = "";

     nSwBona = 0;
     nNume   = 0;
     nNume1  = 0;
     nNume2  = 0;
     nNume3  = 0;
     nNume4  = 0;
     nNume5  = 0;
     &aNombre = "";
     nInkey = 0;
|FIN;

|INCLUYE dscont_i;

|PROCESO Punto;
     aAlfa   = "";
     |PARA nCampo = 1;  |SI nCampo [ 12;  |HACIENDO nCampo = nCampo + 1;
           nPosi = (nCampo * 100) + 1;
           aCaracter = #0Campo % nPosi;
           |SI aCaracter = "0"; |O aCaracter = "1"; |O aCaracter = "2";
            |O aCaracter = "3"; |O aCaracter = "4"; |O aCaracter = "5";
            |O aCaracter = "6"; |O aCaracter = "7"; |O aCaracter = "8";
            |O aCaracter = "9";
                   aAlfa = aAlfa + aCaracter;
          |FINSI;
     |SIGUE;
     aAlfa = aAlfa + ".a";
     #0Campo = aAlfa;  |PINTA #0Campo;
|FPRC;

|PROCESO Cuenta;   |TIPO 6;

      aAlfa   = #0Campo;
      p_alfa1 = #0Campo;

      aCuenta = #0Campo; |QBT aCuenta;
      aCuenta = aCuenta > " ";
      |SI aCuenta = "TABLA";
          #0Campo = aCuenta; |PINTA #0Campo;
          aAlfa   = "T";
      |SINO;
          aCuenta = #0Campo; |QBT aCuenta;
          aAlfa   = aCuenta % -101;
      |FINSI;

      |SI aAlfa = "*"; |O aAlfa = "u"; |O aAlfa = "a"; |O aAlfa = "T";
          |SI Campo = 1; |O Campo = 4; |O Campo = 7; |O Campo = 28;
              |O Campo = 31; |O Campo = 34; |O Campo = 63;
              |SI aAlfa = "*"; |O aAlfa = "T";
                  |MENAV "    Introduzca una Cuenta, no grupo de Cuentas";

                  nNume1 = Campo + 1;
                  |PDEFECTO #0, Campo, nNume1;
                  |PINTA #0Campo;
                  |ERROR;
              |FINSI;
          |SINO;
              |SI aAlfa = "a";
                  |MENAV "    Cuenta no correcta";
                  nNume1 = Campo + 1;
                  |PDEFECTO #0, Campo, nNume1;
                  |PINTA #0Campo;
                  |ERROR;
              |FINSI;
          |FINSI;

          |SI Campo = 34; |O Campo = 63;
              |SI aAlfa = "u"; |O aAlfa = "a";
                  |MENAV "    Introduzca una Cuenta de pase directo Concreta";
                  |ERROR;
              |FINSI;
          |FINSI;
          |ACABA;
      |FINSI;

      aAlfa   = aCuenta % -102;
      |SI aAlfa = ".0";
          |ACABA;
      |FINSI;

      eaCuenta = #0Campo;
      salidas  = FSalida;
      |HAZ FiltraPuntos;
      #0Campo = eaCuenta;
      |PINTA #0Campo;

      |SI #0Campo = "            ";  |ACABA;  |FINSI;

      aCuenta = #0Campo;   |QBT aCuenta;
      aLong   = aCuenta % A0;
      nSw     = 0;
      nCampo1 = Campo + 1;

      aAlfa   = aCuenta % -101;
      |SI aAlfa = "*";  #0nCampo1 = 0;  |ACABA;  |FINSI;
      nLong = aLong;
      |SI nLong > 4;
          |SI aLong = dig4; nSw = 1; #0nCampo1 = 1; |FINSI;
          |SI aLong = dig5; nSw = 1; #0nCampo1 = 2; |FINSI;
          |SI aLong = dig6; nSw = 1; #0nCampo1 = 3; |FINSI;
          |SI aLong = dig7; nSw = 1; #0nCampo1 = 4; |FINSI;
          |SI aLong = dig8; nSw = 1; #0nCampo1 = 5; |FINSI;
          |SI aLong = dig9; nSw = 1; #0nCampo1 = 6; |FINSI;

          |SI nSw = 0;
              |MENAV  "     Atencion. Longitud de Cuenta Fuera de Nivel !";
          |FINSI;
      |FINSI;
|FPRC;

|PROCESO Min10;
  #10#0 = nClave;
  #10#1 = 001;
|FPRC;

|PROCESO Max10;
  #10#0 = nClave;
  #10#1 = 999;
|FPRC;

|PROCESO ModTabla;
      |PUSHV 0501 2380;

      |PINPA #0#10;
      nClave = Campo;
      |SI nClave = 10; aAlfa = "c1) Diferencias Permanentes";                   |FINSI;
      |SI nClave = 13; aAlfa = "d) Activos diferencias temporales";             |FINSI;
      |SI nClave = 16; aAlfa = "e) Pasivos diferencias temporales";             |FINSI;
      |SI nClave = 19; aAlfa = "i) Creditos. perdidas ejercicios anteriores";   |FINSI;
      |SI nClave = 22; aAlfa = "j) Deducciones y Bonificaciones pendientes";    |FINSI;
      |SI nClave = 25; aAlfa = "k) Retenciones y Pagos a Cuenta";               |FINSI;
      |SI nClave = 64; aAlfa = "e2) Imp. fiscal var. ejer.";                    |FINSI;
      |SI nClave = 73; aAlfa = "c2) Diferencias provisiones por deterioro";     |FINSI;
      |SI nClave = 77; aAlfa = "f) Ctas Gastos Provisiones deterioro crtos com.";|FINSI;
      |SI nClave = 80; aAlfa = "f) Provisiones deterioro creditos comerciales"; |FINSI;
      |SI nClave = 83; aAlfa = "g) Reservas de Capitalizacion"; |FINSI;
      |SI nClave = 86; aAlfa = "i) Reservas de Nivelacion"; |FINSI;

      |ATRI I; |PINTA 0836, aAlfa; |ATRI 0;
      |ENTLINEAL #1#10, -2, 2, 19, 0, Min10, Max10;

      |POPV;
|FPRC;


|PROCESO NombreCuenta;  |TIPO 0;
     |SI #0Campo = "            ";  |ACABA;  |FINSI;

     nInkey = FSalida;
     |SI Campo = 1; |Y FSalida = 10;
         |HAZ Defectos;
         |PINDA #0#0;
     |FINSI;

     nCampo1 = Campo + 1;
     nCampo2 = Campo + 2;

     aCuenta = #0Campo;   |QBT aCuenta;
     |SI aCuenta = "TABLA";
         aNombre = "Tabla de Cuentas";
         nLong   = 4;
     |SINO;
         aAlfa   = aCuenta % -101;
         |SI aAlfa = "*";
              aCuenta = aCuenta - aAlfa;
         |FINSI;
         |SI aAlfa = "u"; |O aAlfa = "a";
              aCuenta = aCuenta - aAlfa;
              aCuenta = aCuenta - ".";
          |FINSI;
         aAlfa   = aCuenta % -102;
         |SI aAlfa = ".0";
              aCuenta = aCuenta - aAlfa;
         |FINSI;

         aLong   = aCuenta % A0;
         nLong   = aLong;
         aNombre = "";
         |ABRE #4;
         |SELECT #1#4;
         #4#0 = #0Campo;
         |LEE 000#4=;
         |SI FSalida ! 0;
             |SI nLong [ 4;
                 eaCuenta = aCuenta;
                 |HAZ LeeMayor;
             |SINO;
                  |MENAV "      Atencion. No existe Cuenta ";
             |FINSI;
         |SINO;
             aNombre = #4#2;
         |FINSI;
     |FINSI;

     |SI aNombre ! "";
         #0nCampo2 = aNombre;
     |FINSI;
     |PINTA #nCampo2;

     |SI #4#3 = "N";
         |SI nLong > 4;
             |MENAV "      Esta Cuenta NO tiene Pase directo";
         |FINSI;
     |FINSI;
     |CIERRA #4;

     |SI aCuenta = "TABLA"; |Y nInkey = 0;
         |HAZ ModTabla;
     |FINSI;
|FPRC;

|PROCESO repetir;
 |SI FSalida = 15;
     nNume1 = Campo + 1; ||Descripcion
     |SI Campo ! 37; |Y Campo ! 89; |Y Campo ! 49;
         nNume2 = Campo - 2;
         nNume3 = Campo - 1;
     |FINSI;
     |SI Campo = 37;  nNume2 = 37; nNume3 = 38;  |FINSI;
     |SI Campo = 89;  nNume2 = 47; nNume3 = 48;  |FINSI;
     |SI Campo = 49;  nNume2 = 93; nNume3 = 94;  |FINSI;

     #0Campo  = #0nNume2; |PINTA #0Campo;
     #0nNume1 = #0nNume3; |PINTA #0nNume1;

     nNume2 = Campo + 2;
     |PARA nNume3 = nNume2; |SI nNume3 [ 47; |HACIENDO nNume3 = nNume3 + 2;
           nNume4 = nNume3 + 1;
           #0nNume3 = #0Campo; |PINTA #0nNume3;
           #0nNume4 = #0nNume1;|PINTA #0nNume4;
     |SIGUE;

     nNume5 = 95;
     |SI Campo [ 47; nNume5 = 89; |FINSI;
     |SI Campo ] 89; nNume5 = Campo + 2; |FINSI;

     |PARA nNume3 = nNume5; |SI nNume3 [ 93; |HACIENDO nNume3 = nNume3 + 2;
           nNume4 = nNume3 + 1;
           #0nNume3 = #0Campo; |PINTA #0nNume3;
           #0nNume4 = #0nNume1;|PINTA #0nNume4;
     |SIGUE;

     nNume5 = 49;
     |SI Campo ] 49; |Y Campo [ 57; nNume5 = Campo + 2; |FINSI;
     |PARA nNume3 = nNume5; |SI nNume3 [ 57; |HACIENDO nNume3 = nNume3 + 2;
           nNume4 = nNume3 + 1;
           #0nNume3 = #0Campo; |PINTA #0nNume3;
           #0nNume4 = #0nNume1;|PINTA #0nNume4;
     |SIGUE;

     |ACABA;
 |FINSI;

  nNume2 = Contenido;
  nNume1 = Campo + 1; ||Descripcion
  aAlfa = #0nNume1; |QBF aAlfa;

  |DDEFECTO #330;
  |ABRE #330;
  #330#0 = #0Campo;
  |LEE 001#330=;

  |SI #0Campo ! nNume2; |O aAlfa  = "";
      #0nNume1 = #330#1; |PINTA #0nNume1;
  |FINSI;
|FPRC;

|PROCESO ElPtec; |TIPO 7;
   |SI #200#155 ! "S";
       |PTEC 816;
   |FINSI;
|FPRC;

|PROCESO Tipo0C72; |TIPO 0;
         nNume   = #0Campo;

         nSwBona = 0;
         |SI nNume < 305; |O nNume > 416;
             nSwBona = 1;
             |SI nNume = 503; |O nNume = 504; |O nNume = 505;
                 |O nNume = 517; |O nNume = 250; |O nNume = 251;
                 nSwBona = 0;
             |FINSI;
         |SINO;
             |SI nNume = 340; |O nNume = 344; |O nNume = 351; |O nNume = 353;
                 |O nNume = 364; |O nNume = 366; |O nNume = 368; |O nNume = 371;
                    |O nNume = 395; |O nNume = 397; |O nNume = 399; |O nNume = 401;
                       |O nNume = 402; |O nNume = 408;
                 nSwBona = 1;
             |FINSI;
         |FINSI;
         |SI nSwBona = 1;
             aAlfa = "    La Casilla " + #0Campo + " NO es correcta";
             |MENAV aAlfa;
             |ERROR;
         |FINSI;
|FPRC;

|PROCESO Tipo0C76; |TIPO 0;
         nNume   = #0Campo;
         nSwBona = 1;
         |SI nNume = 331; |O nNume = 332; |O nNume = 325; |O nNume = 328;
             nSwBona = 0;
         |FINSI;
         |SI nSwBona = 1;
             aAlfa = "    La Casilla " + #0Campo + " NO es correcta";
             |MENAV aAlfa;
             |ERROR;
         |FINSI;
|FPRC;

|PROCESO Tipo0C95; |TIPO 0;
         nNume   = #0Campo;
         nSwBona = 1;
         |SI nNume = 415; |O nNume = 211; |O nNume = 416; |O nNume = 543;
             nSwBona = 0;
         |FINSI;
         |SI nSwBona = 1;
             aAlfa = "    La Casilla " + #0Campo + " NO es correcta";
             |MENAV aAlfa;
             |ERROR;
         |FINSI;
|FPRC;

|PROCESO Tipo3Coni15; |TIPO 3;
    |HAZ RepasoTablas;
|FPRC;


|PROCESO BorraTabla;
  |BORRA 020#10a;
  |LIBERA #10;
|FPRC;

|DEFBUCLE BorraTabla;
   #10#1;
   ;
   nClave, 001;
   nClave, 999;
   be;
   NULL, BorraTabla;
|FIN;

|PROCESO BajaTabla;
    aAlfa = #0nClave; |QBF aAlfa;

    |SI aAlfa ! "TABLA";
        |BUCLE BorraTabla;
    |FINSI;
|FPRC;

|PROCESO RepasoTablas;
    nClave = 10; |HAZ BajaTabla;
    nClave = 13; |HAZ BajaTabla;
    nClave = 16; |HAZ BajaTabla;
    nClave = 19; |HAZ BajaTabla;
    nClave = 22; |HAZ BajaTabla;
    nClave = 25; |HAZ BajaTabla;
    nClave = 64; |HAZ BajaTabla;
    nClave = 73; |HAZ BajaTabla;
    nClave = 77; |HAZ BajaTabla;
    nClave = 80; |HAZ BajaTabla;
    nClave = 83; |HAZ BajaTabla;
    nClave = 86; |HAZ BajaTabla;
|FPRC;

|PROCESO GrabaEsta;
     |ABRE #4;
     #4#0 = eaCuenta;
     |LEE 000#4=;
     |SI FSalida ! 0;
         |HAZ LeeMayor;
     |SINO;
          aNombre = #4#2;
     |FINSI;
     |CIERRA #4;

     nLinea = nLinea + 1;
     #10#0 = nClave;
     #10#1 = nLinea;
     #10#2 = eaCuenta;
     #10#3 = #4#1;
     #10#4 = aNombre;
     |GRABA 040#10n;
|FPRC;

|PROCESO CreaTablaDefecto;
    nClave = 73; |BUCLE BorraTabla;
    nLinea = 0;
    |ABRE #10;
    eaCuenta = "690*"; |HAZ GrabaEsta;
    eaCuenta = "691*"; |HAZ GrabaEsta;
    eaCuenta = "692*"; |HAZ GrabaEsta;
    eaCuenta = "695*"; |HAZ GrabaEsta;
    eaCuenta = "696*"; |HAZ GrabaEsta;
    eaCuenta = "697*"; |HAZ GrabaEsta;
    eaCuenta = "698*"; |HAZ GrabaEsta;
    eaCuenta = "699*"; |HAZ GrabaEsta;
    |CIERRA #10;

/*
    nClave = 77; |BUCLE BorraTabla;
    nLinea = 0;
    |ABRE #10;
    eaCuenta = "694*"; |HAZ GrabaEsta;
    eaCuenta = "794*"; |HAZ GrabaEsta;
    |CIERRA #10;
*/
|FPRC;

|PROCESO Defectos;
    |DDEFECTO #0;
    |HAZ RepasoTablas;
    |HAZ CreaTablaDefecto;

    |SI nSwPrim = 0;
        |ABRE #8;
        |DDEFECTO #8;
        |LEE 101#8p;
        |SI FSalida = 0;
            |PARA nPara1 = 0; |SI nPara1 [ 72; |HACIENDO nPara1 = nPara1 + 1;
                  #0nPara1 = #8nPara1;
            |SIGUE;
        |FINSI;
        |CIERRA #8;
    |FINSI;
|FPRC;

|PROGRAMA;
    |HAZ inicio;

    |ABRE #200;
    |DDEFECTO #200;
    |LEE 041#200p;
    |CIERRA #200;

    |CLS;
    |CABEZA "Asiento Impuesto Sociedades";
    |ABRET #0;
    |LEE 101#0p;
    |SI FSalida ! 0;
       |HAZ Defectos;
       |GRABA 020#0b;
    |FINSI;

    nSwPrim = 1;
    |PINPA #0#0;
    |PINDA #0#0;
    |ENDATOS #1#0;
    |SI FSalida = 0;  |GRABA 020#0a;  |FINSI;
    |LIBERA  #0;
    |CIERRAT #0;
|FPRO;
