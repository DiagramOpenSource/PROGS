|FICHEROS;
     caut0001 #0;
     canempre #30;

     cacuenta #1;
     imagen@  #2;
     caclipro #3;

    :/basica/def/agifigen #100;
|FIN;

|VARIABLES;
     leidos   = 0;
     emp1     = 0;
     per1     = 0;
     xx       = 0;
     yy       = 0;
     x        = 0;
     y        = 0;
     varcamp  = 0;
     &r_emp   = 0;
     &r_per   = 0;
     alfa2    = "";
     men9     = "     Empresa Contable INACTIVA ";
     alfa1    = "";
     aMas     = "";
     nCampo   = 0;

     &eaOrigen  = "";
     &eaDestino = "";

     nLaEmpresa = 0;
     nSwTipo    = 0;
     nPin1      = 0;
     aAlfa      = "";
|FIN;

|INCLUYE dscont_i;

|| *************************************************************************
||                        CALCULOS DE PANTALLA
|| *************************************************************************

|PROCESO Pongo0; |TIPO 0;
 |C_M #0#71,1,"S";
 |SI #0#70 = 0;
     |C_M #0#71,1,"N";
     #0#71 = 0; |PINTA #0#71;
 |FINSI;
|FPRC;

|PROCESO LaRelacion;  |TIPO 88;
     |HAZ DirAplicSt;
     |SI Haybasica = 1;         || Existe Acceso;
         || ... Activado Basica
         |MODO_RELACION #0, 0, "agitab04", "X", "X";
         |MODO_RELACION #0, 1, "agitab04", "N", "N";
     |SINO;
         || No existe Basica
         |MODO_RELACION #0, 1, "agitab04", "N", "N";
         |MODO_RELACION #0, 1, "agitab04", "X", "X";
         |C_M #0#74,1,"N"; |C_M #0#75,1,"N";
         |C_M #0#76,1,"N"; |C_M #0#77,1,"N";
         |C_M #0#78,1,"N"; |C_M #0#79,1,"N";
         |C_M #0#80,1,"N"; |C_M #0#81,1,"N";
         |C_M #0#82,1,"N"; |C_M #0#83,1,"N";
         |C_M #0#84,1,"N"; |C_M #0#85,1,"N";
         |C_M #0#86,1,"N";
    |FINSI;
|FPRC;

|PROCESO AlEntrar; |TIPO 7;
     #0#1 = #30#26; |PINTA #0#1;
     #0#67 = enEmpresa;   |PINTA #0#67;
     #0#68 = enPeriodo;   |PINTA #0#68;
     #0#69 = eaNombreEmp; |PINTA #0#69;
|FPRC;

|PROCESO Consulta;
     |SI FSalida ! 9;  |ACABA;  |FINSI;

     |SUB_EJECUTA "caconemp";
     |ERROR;
     |SI Campo = 3;
         #0#3 = r_emp;
         #0#5 = r_per;
         |PTEC 802;
         |PTEC 802;
     |FINSI;

     |SI Campo = 4;
         #0#4 = r_emp;
         #0#6 = r_per;
         |PTEC 802;
     |FINSI;

     |SI Campo ] 7;  |Y Campo [  66;
         #0Campo   = r_emp;
         varcamp   = Campo + 20;
         #0varcamp = r_per;
         |PTEC 802;
         |PTEC 802;
     |FINSI;

     |SI Campo = 67;
         #0#67 = r_emp;
         #0#68 = r_per;
         |PTEC 802;
         |PTEC 802;
     |FINSI;
|FPRC;

|PROCESO Mayor;
     |SI #0Campo = "N";
         |PARA x = 7;  |SI x [ 46;  |HACIENDO x = x + 1;
               |CAMPO_MODIFICA #0x,1,"S";
         |SIGUE;
         #0#3 = 0;
         #0#5 = 0;
         #0#4 = 0;
         #0#6 = 0;
         |CAMPO_MODIFICA #0#3,1,"N";
         |CAMPO_MODIFICA #0#5,1,"N";
         |CAMPO_MODIFICA #0#4,1,"N";
         |CAMPO_MODIFICA #0#6,1,"N";
         |PINTA #0#3;
         |PINTA #0#5;
         |PINTA #0#4;
         |PINTA #0#6;
     |SINO;
         |PARA x = 7;  |SI x [ 46;  |HACIENDO x = x + 1;
               #0x = 0;
               |CAMPO_MODIFICA #0x,1,"N";
         |SIGUE;
         |CAMPO_MODIFICA #0#3,1,"S";
         |CAMPO_MODIFICA #0#5,1,"S";
         |CAMPO_MODIFICA #0#4,1,"S";
         |CAMPO_MODIFICA #0#6,1,"S";
     |FINSI;
|FPRC;

|CALCULO SelTipo0;
 |SI Haybasica = 0; |ACABA; |FINSI;
     |SI #0#86 = "N";
         #0#74 = 0;   |PINTA #0#74;  |C_M #0#74, 1, "N";
         #0#75 = 99;  |PINTA #0#75;  |C_M #0#75, 1, "N";
         |PARA nCampo = 76;  |SI nCampo [ 85;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |C_M #0#74, 1, "S";
         |C_M #0#75, 1, "S";
         |PARA nCampo = 76;  |SI nCampo [ 85;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO SelTipo1;
 |SI Haybasica = 0; |ACABA; |FINSI;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 85;  |HACIENDO nCampo = nCampo + 1;
               #0Campo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 85;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|| --------------- comprueba desde-hasta empresas ---------------------------

|PROCESO BuscaEm1;
     |SI #30#26 ! #0#1; |ACABA; |FINSI;

     |SI #0#73 = "A"; |Y #30#21 = "I";  |ACABA;  |FINSI;
     |SI #0#73 = "I"; |Y #30#21 = "A";  |ACABA;  |FINSI;
     nLaEmpresa = #30#2; |HAZ ElTipoEmpresa;
     |SI nSwTipo = 1; |ACABA; |FINSI;

     leidos = 1;
     |ERROR10;
|FPRC;

|DEFBUCLE BuscaEmpre;
     #30#1;
     ;
     #0#3, #0#5;
     #0#4, #0#6;
     ;
     NULL,BuscaEm1;
|FIN;

|PROCESO BucleEmpre;
     |SI FSalida = 2; |ACABA; |FINSI;
     |SI #0#5 > #0#6;
         |MENAV "     ERROR EN INTRODUCCION";
         |ERROR;
         |ACABA;
     |FINSI;
     |SI #0#3 = 0;  |Y #0#4 = 0;  |ACABA;  |FINSI;
     |SI #0#67 ] #0#3;  |Y #0#67 [ #0#4;
         |MENAV  "     LA EMPRESA ORIGEN DEBE ESTAR FUERA DE LA SELECCION";
         |ERROR;
         |ACABA;
     |FINSI;
     leidos = 0;
     |BUCLE BuscaEmpre;
     |SI leidos = 0;
         |MENAV "    ATENCION SELECCION VACIA";
         |ERROR;
         |ACABA;
     |FINSI;
|FPRC;

|PROCESO DesHas;
     |SI #0#4 < #0#3;
         |MENAV "      Error en Introduccion";
         |ERROR;
     |FINSI;
|FPRC;

|| ------------------------------------------------------------------------

|PROCESO SiModi; |TIPO 7;
     |PARA x = Campo + 1;  |SI x [ 26;  |HACIENDO x = x + 1;
           |CAMPO_MODIFICA #0x,1,"S";
           y = x + 20;
           |CAMPO_MODIFICA #0y,1,"S";
     |SIGUE;
     y = Campo + 20;
     |CAMPO_MODIFICA #0y,1,"S";
|FPRC;

|PROCESO NoModi;
     |PARA x = Campo + 1;  |SI x [ 26;  |HACIENDO x = x + 1;
           #0x = 0;
           |CAMPO_MODIFICA #0x,1,"N";
           y = x + 20;
           #0y = 0;
           |CAMPO_MODIFICA #0y,1,"N";
     |SIGUE;

     y = Campo + 20;
     #0y = 0;
     |CAMPO_MODIFICA #0y,1,"N";
     |PINTA #0y;
|FPRC;

|PROCESO EsBuena; || desde campo de empresas seleccion
     |SI FSalida = 2; |ACABA; |FINSI;
     |SI #0Campo = 0; |HAZ NoModi;  |ACABA;  |FINSI;
     |SI #0Campo = #0#67;
         |MENAV "    LA EMPRESA ES IGUAL AL ORIGEN";
         |ERROR;
         |ACABA;
     |FINSI;

     |ABRE #30;
     #30#2 = #0Campo;
     #30#3 = 0;
     |LEE 011#30];
     |SI FSalida ! 0;  |O #30#2 ! #0Campo;
         |MENAV "     NO Existe la Empresa Contable";
         |ERROR;
     |FINSI;
     |CIERRA #30;
|FPRC;

|PROCESO EsBuena1;
     |SI FSalida = 2;|ACABA;|FINSI;
     x = Campo - 20;
     |SI #0x = 0;  |ACABA;  |FINSI;
     |ABRE #30;
     x = Campo - 20;
     #30#2 = #0x;
     #30#3 = #0Campo;
     |LEE 011#30=;
     |SI FSalida ! 0;
         |MENAV "     NO Existe Periodo en la Empresa Contable";
         |CIERRA #30;
         |ERROR;
         |ACABA;
     |FINSI;
     |CIERRA #30;

     |SI #0#73 = "A"; |Y #30#21 = "I"; |MENAV men9; |ERROR; |ACABA;  |FINSI;
     |SI #0#73 = "I"; |Y #30#21 = "A"; |MENAV men9; |ERROR; |ACABA;  |FINSI;

     |SI #30#26 ! #0#1;
         |MENAV "     El a¤o contable NO esta dentro de la seleccion ";
         |ERROR;
         |ACABA;
     |FINSI;

     nLaEmpresa = #30#2; |HAZ ElTipoEmpresa;
     |SI nSwTipo = 1;
         |MENAV "     Tipo Empresa No Seleccionado ";
         |ERROR;
         |ACABA;
     |FINSI;

     x = Campo + 20;
     #0x = #30#1;
     |PINTA #0x;
     x = Campo - 20;
     alfa1 = #0x;
     alfa2 = #0Campo;
|FPRC;

|PROCESO EsBuena2;          || desde campo empresa Origen
     |SI #0#67 = 0;  |Y #0#68 = 0;  |ACABA;  |FINSI;

     |ABRE #30;
     #30#2 = #0#67;
     #30#3 = 0;
     |LEE 011#30];
     |SI FSalida ! 0;  |O #30#2 ! #0#67;
         |MENAV "     NO Existe la Empresa Contable";
         |ERROR;
     |FINSI;
     |CIERRA #30;
|FPRC;

|PROCESO EsBuena3;        ||Desde campo Periodo Origen
     |SI #0#67 = 0;  |Y #0#68 = 0;  |ACABA;  |FINSI;

     |ABRE #30;
     #30#2 = #0#67;
     #30#3 = #0#68;
     |LEE 011#30=;
     |SI FSalida ! 0;
         |MENAV "     NO Existe Periodo en la Empresa Contable";
         |CIERRA #30;
         |ERROR;
         |ACABA;
     |FINSI;
     |CIERRA #30;
     #0#69 = #30#1;
     |PINTA #0#69;
|FPRC;

|| **********************************************************************
||                       CALCULOS DE IMPRESION
|| **********************************************************************

|| *************** propio del rec lculo

|PROCESO procal;
     |NOPARA;
     |SIPARA;
|FPRC;

|PROCESO GrabaAlli;
     |PINTA 2213, #1#0;
     |PINTA 2226, #1#1;

     #2#0 = #1#0;
     #2#1 = #1#1;
     |LEE 101#2=;
     |SI FSalida ! 0;
         |DDEFECTO #2;
         #2#0 = #1#0;
         #2#1 = #1#1;
         |GRABA 020#2b;
     |FINSI;

     |PARA nCampo = 0;   |SI nCampo [ 8;  |HACIENDO nCampo = nCampo + 1;
           #2nCampo = #1nCampo;
     |SIGUE;

     |PARA nCampo = 54;   |SI nCampo [ 57;  |HACIENDO nCampo = nCampo + 1;
           #2nCampo = #1nCampo;
     |SIGUE;

     #2#59 = #1#59;

     |GRABA 020#2a;
     |LIBERA #2;
|FPRC;

|DEFBUCLE cacuenta;
     #1#1;
     1;
     "            ", #0#70;
     "zzzzzzzzzzzz", #0#71;
     ;
     NULL, GrabaAlli;
|FIN;

|PROCESO cacuentaAqui;
     |PINTA 2213, #2#0;
     |PINTA 2226, #2#1;

     aAlfa = #2#2;  |QBF aAlfa;
     |SI aAlfa = "";
         #3#23 = "C";
         #3#10 = #2#0;
         |LEE 000#3=;
         |SI FSalida ! 0;  |DDEFECTO #3;  |FINSI;

          #2#2 = #3#1;
     |FINSI;

     aAlfa = #2#2;  |QBF aAlfa;
     |SI aAlfa = "";
         #3#23 = "P";
         #3#10 = #2#0;
         |LEE 000#3=;
         |SI FSalida ! 0;  |DDEFECTO #3;  |FINSI;

          #2#2 = #3#1;
     |FINSI;

     |GRABA 020#2a;
     |LIBERA #2;
|FPRC;

|DEFBUCLE cacuentaAqui;
     #2#1001;
     2;
     "            ", "                                    ";
     "zzzzzzzzzzzz", "                                    ";
     ;
     NULL, cacuentaAqui;
|FIN;

|PROCESO Traspasa;
     |PINTA 2113, #30#2;
     |PINTA 2119, #30#3;
     |PINTA 2121, #30#1;

     |PATH_DAT #2 dirfich0;
     |PATH_DAT #3 dirfich0;

     |ABRE #2;
     |BUCLE cacuenta;
     |CIERRA #2;

     |ABRE #3;
     |BUCLE cacuentaAqui;
     |CIERRA #3;
|FPRC;

|| ---------- Seleccion DESDE / HASTA Empresa.

|PROCESO esdesd1;
     |DBASE dir_fich;
     dirfich0  = dir_fich + "fich/";
     alfa1     = #30#2;
     alfa2     = #30#3;
     emp1      = #30#2;
     per1      = #30#3;
     alfa1     = "00000" + alfa1;   alfa1 = alfa1 % -105;
     alfa2     = "0"   + alfa2;   alfa2 = alfa2 % -101;
     dirfich0  = dirfich0 + alfa1 + alfa2 + "/";
     eaDestino = dirfich0;

     |SI #0#73 = "A"; |Y #30#21 = "I";  |ACABA;  |FINSI;
     |SI #0#73 = "I"; |Y #30#21 = "A";  |ACABA;  |FINSI;
     |SI #30#26 ! #0#1;                 |ACABA;  |FINSI;

     nLaEmpresa = #30#2;
     |HAZ ElTipoEmpresa;
     |SI nSwTipo = 1; |ACABA; |FINSI;

     |SI #0#70 ! 0; |Y #0#71 ! 0; |HAZ Traspasa; |FINSI;
     |SI #0#72 = "S"; |HAZ Auxiliar; |FINSI;
|FPRC;

|DEFBUCLE esdesde;
     #30#1;
     ;
     #0#3, #0#5;
     #0#4, #0#6;
     e;
     NULL, esdesd1;
|FIN;

|| ---------- Seleccion en parrilla.

|PROCESO esunaauna;
     |PARA xx = 7;  |SI xx [ 26;  |HACIENDO xx = xx + 1;
           |SI #0xx = 0;  |SAL;  |FINSI;

           |DBASE dir_fich;
           dirfich0  = dir_fich + "fich/";
           yy        = xx + 20;
           emp1      = #0xx;
           per1      = #0yy;
           alfa1     = #0xx;
           alfa2     = #0yy;
           alfa1     = "00000" + alfa1;   alfa1 = alfa1 % -105;
           alfa2     = "0"   + alfa2;   alfa2 = alfa2 % -101;
           dirfich0  = dirfich0 + alfa1 + alfa2 + "/";
           eaDestino = dirfich0;

           || lee empresa
           |ABRE #30;
           #30#2 = emp1;
           #30#3 = per1;
           |LEE 011#30=;
           |CIERRA #30;

           |SI #0#73 = "A"; |Y #30#21 = "I";  |VETE otramas;  |FINSI;
           |SI #0#73 = "I"; |Y #30#21 = "A";  |VETE otramas;  |FINSI;
           |SI #30#26 ! #0#1;                 |VETE otramas;  |FINSI;

           nLaEmpresa = #30#2;
           |HAZ ElTipoEmpresa;
           |SI nSwTipo = 1; |ACABA; |FINSI;

           |SI #0#70 ! 0; |Y #0#71 ! 0; |HAZ Traspasa; |FINSI;
           |SI #0#72 = "S"; |HAZ Auxiliar; |FINSI;

           |ET otramas;
     |SIGUE;
|FPRC;

|| *********************************************************************
||                    COMIENZO con TIPO 3
|| *********************************************************************

|PROCESO Tipo3; |TIPO 3;
     |SI #0#0 = "N"; |ACABA; |FINSI;

     |SI #0#72 = "S";   |HAZ CargaTempo;   |FINSI;

     |SI #0#72 ! "S";
         |Y #0#70 = 0; |Y #0#71 = 0;
            |MENAV "    No realizar ningun proceso ";
            |ACABA;
     |FINSI;

     |PUSHV 2002 2378;
     |ATRI I;
     |CUADRO 2002 2378
     |BLANCO 2103 2277
     |ATRI N;

     |ATRI R;
     |PINTA 2103," EMPRESA: ";
     |PINTA 2203," CUENTAS: ";
     |ATRI 0;

     |DBASE dir_fich;
     dirfich0 = dir_fich + "fich/";
     emp1     = #0#67;
     per1     = #0#68;
     alfa1    = ("00000" + #0#67) % -105;
     alfa2    = ("0"     + #0#68) % -101;
     dirfich0 = dirfich0 + alfa1 + alfa2 + "/";
     eaOrigen = dirfich0;
     |PATH_DAT #1 dirfich0;

     aMas  = dir_fich + "def/cacuenta.mas";
     |CARGA_DEF aMas, imagen@;
     |SI FSalida ! 0;
         |MENAV  "         Error en cargar el Def ... Proceso Abortado.";
         |ACABA;
     |FINSI;

     |SI #0#2 = "S";
         |BUCLE esdesde;
     |SINO;
         |HAZ esunaauna;
     |FINSI;
     |POPV;

     |DESCARGA_DEF #imagen@;

     |SI #0#72 = "S";   |HAZ BorraTempo;   |FINSI;
|FPRC;


|PROCESO ElTipoEmpresa;
    nSwTipo = 1;
    |SI Haybasica = 0; nSwTipo = 0; |ACABA; |FINSI;
    |ABRE #agifigen;
    #agifigen#0 = nLaEmpresa; || Empresa
    |LEE 041#agifigen.=;
    |SI FSalida ! 0;
        |DDEFECTO #agifigen;
        #agifigen#87 = 1;
    |FINSI;
    |CIERRA #agifigen;
    nPin1 = #agifigen#87;

    |SI #0#86 = "S";
        |SI nPin1 ] #0#74; |Y nPin1 [ #0#75;
            nSwTipo = 0;
        |FINSI;
    |SINO;
        |SI nPin1 = #0#76;
            nSwTipo = 0;
        |SINO;
            |PARA nCampo = 77;  |SI nCampo [ 85;  |HACIENDO nCampo = nCampo + 1;
                  |SI nPin1 = #0nCampo; |Y nPin1 ! 0;
                      nSwTipo = 0;
                      |SAL;
                  |FINSI;
            |SIGUE;
        |FINSI;
    |FINSI;
|FPRC;
