|FICHEROS;
    n43conso #0;  || Proceso Consolidacion
    n43consl #1;  || consolidados.
    n43datos #2;  || Datos Recepcionados.
    camaasil #3;  || Movimientos Contables.
    cacuenta #10;
|FIN;

|VARIABLES;
   &entrada = 1;
   cuenta1 = "            ";
   cuenta2 = "zzzzzzzzzzzz";
   swgraba = 0;
   contador = 0;
   nNorma = 0;
   nApunt = 0;
   nPasa  = 0;
   nPara1 = 0;
   aAlfa1 = "";

   fFecha    = @;
   fDFecha   = @;
   fHFecha   = @;
   fDesFecha = @;
   fHasFecha = @;

   nNume1  = 0;
   nNume2  = 0;
   nSwOp   = 0;
   nNumero = 0;
|FIN;

|INCLUYE dscont_i;

|CALCULO defecto; |TIPO 7;
 #0#0 = fec1; |PINTA #0#0;
 #0#1 = fec2; |PINTA #0#1;
|FCAL;

|CALCULO Parrilla;
 |SI #0#2 = "S";
     |C_M #0#3, 1, "S";
     |C_M #0#4, 1, "S";
     |PARA nPara1 = 5; |SI nPara1 [ 13; |HACIENDO nPara1 = nPara1 + 1;
           #0nPara1 = ""; |PINTA #0nPara1;
           |C_M #0nPara1, 1, "N";
     |SIGUE;
 |SINO;
     |C_M #0#3, 1, "N"; #0#3 = ""; |PINTA #0#3;
     |C_M #0#4, 1, "N"; #0#4 = "z" * 12; |PINTA #0#4
     |PARA nPara1 = 5; |SI nPara1 [ 13; |HACIENDO nPara1 = nPara1 + 1;
           |C_M #0nPara1, 1, "S";
     |SIGUE;
 |FINSI;
|FCAL;

|CALCULO cuefin; |TIPO 0;
  |C_M #0Campo, 0, aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

   eaCuenta = #0Campo;
   salidas  = FSalida;
   |HAZ FiltraPuntos;
   #0Campo = eaCuenta;
   |PINTA #0Campo;
|FCAL;

|CALCULO ElMargen;
   aAlfa1 = #0#15;
   |C_M #0#16,1, aAlfa1;
   |C_M #0#17,1, aAlfa1;
   |SI #0#15 = "N";
       #0#16 = 0;   |PINTA #0#16;
       #0#17 = "O"; |PINTA #0#17;
   |FINSI;
|FCAL;
|| **********************************************************************
|CALCULO cuentas;

   |PINTA 1924, #2#1; |PINTA 2024, #2#2; |PINTA 2124, #2#7; |PINTA 2224,#2#6;
   |PINTA 1943, #3#4; |PINTA 2043, #3#1; |PINTA 2143, #3#9; |PINTA 2243,#3#8;

   nPasa  = 1;
   nApunt = #3#9 ! EURODB;
   nNorma = #2#7 ! EURODB;

/*
 |SI #0#15 = "S";
     nPasa  = 0;
     fDesFecha = #3#1;
     fHasFecha = #3#1;
     |SI #0#16 ! 0;
         nNume1 = #3#1; nNume1 = nNume1 - #0#16; fDesFecha = nNume1;
         nNume1 = #3#1; nNume1 = nNume1 + #0#16; fHasFecha = nNume1;
     |FINSI;
     |SI #2#2 ] fDesFecha; |Y #2#2 [ fHasFecha; nPasa = 1; |FINSI;
     |SI #2#3 ] fDesFecha; |Y #2#3 [ fHasFecha; nPasa = 1; |FINSI;
 |FINSI;
*/

 #1#10  = #3#4; ||Cuenta Apunte;
 #1#11  = #3#5; ||Digito;
 #1#12  = #3#1; ||Fecha Apunte;
 #1#13  = #3#2; ||Documento Apunte;
 #1#18  = #3#0; ||Diario Apunte;
 #1#19  = #3#3; ||Apunte
 |LEE 040#1=;  ||Lee Fichero Consolidaciones;
 |SI FSalida ! 0;

     |SI #3#8 = #2#6; |Y nApunt = nNorma; |Y #3#18 = " ";

         |SI nSwOp = 0;
             contador = contador + 1;
             #1#0  = contador; #1#1  = #2#0; #1#2  = #2#1;  #1#3  = #2#2;
             #1#4  = #2#3;     #1#5  = #2#4; #1#6  = #2#5;  #1#7  = #2#6;
             #1#8  = #2#7;     #1#9  = #2#8;

             #1#10 = #3#4;     #1#11 = #3#5; #1#12 = #3#1;  #1#13 = #3#2;
             #1#14 = #3#6;     #1#15 = #3#7; #1#16 = #3#8;  #1#17 = #3#9;
             #1#18 = #3#0;     #1#19 = #3#3;

             || Consolidacion uno a uno
             #3#18 = "S";
             |GRABA 020#3a;

             |GRABA 040#1n;
             #2#9 = "S";

             |GRABA 020#2a;
             |ERROR10;
             swgraba = 1;
         |FINSI;
         |SI nSwOp = 1;
             nNumero = nNumero + 1;
             |SI nNumero ] 2;  |ERROR10;  |FINSI;
         |FINSI;
     |FINSI;

 |SINO;

     |SI #3#8 = #2#6; |Y nApunt = nNorma; |Y #3#18 = " "; |Y #1#19 ! #3#3;
         |SI nSwOp = 0;
             contador = contador + 1;
             #1#0  = contador; #1#1  = #2#0; #1#2  = #2#1;  #1#3  = #2#2;
             #1#4  = #2#3;     #1#5  = #2#4; #1#6  = #2#5;  #1#7  = #2#6;
             #1#8  = #2#7;     #1#9  = #2#8;

             #1#10 = #3#4;     #1#11 = #3#5; #1#12 = #3#1;  #1#13 = #3#2;
             #1#14 = #3#6;     #1#15 = #3#7; #1#16 = #3#8;  #1#17 = #3#9;
             #1#18 = #3#0;     #1#19 = #3#3;

             || Consolidacion uno a uno
             #3#18 = "S";
             |GRABA 020#3a;

             |GRABA 040#1n;
             #2#9 = "S";
             |GRABA 020#2a;
             |ERROR10;
              swgraba = 1;
         |FINSI;
         |SI nSwOp = 1;
             nNumero = nNumero + 1;
             |SI nNumero ] 2;  |ERROR10;  |FINSI;
         |FINSI;
      |FINSI;
  |FINSI;
  |LIBERA #3;
|FCAL;

|DEFBUCLE cplinmov;
   #3#3;
   8,18;
   #2#1, fDesFecha, 000001, 0001, #2#6, " ";
   #2#1, fHasFecha, 999999, 9999, #2#6, " ";
   be;
   NULL, cuentas;
|FIN;


|CALCULO entrada;

   fDesFecha = fDFecha;
   fHasFecha = fHFecha;

   |SI #0#15 = "S";
       fFecha = #2#2;
       |SI #0#17 = "V";  fFecha = #2#3;  |FINSI;

       fDesFecha = fFecha;
       fHasFecha = fFecha;
       |SI #0#16 ! 0;
           nNume1 = fFecha; nNume1 = nNume1 - #0#16; fDesFecha = nNume1;
           nNume1 = fFecha; nNume1 = nNume1 + #0#16; fHasFecha = nNume1;
       |FINSI;
    |FINSI;

    nNumero = 1;
    |SI #0#15 = "S"; |Y #0#16 ! 0;
        nNumero = 0;
        nSwOp = 1; |BUCLE cplinmov;
    |FINSI;

    |SI nNumero = 1;
        nSwOp = 0; |BUCLE cplinmov;
    |FINSI;
    |LIBERA #2;
|FCAL;

|DEFBUCLE cpentrad;
   #2#3;
   9;
   cuenta1, #0#0, "N";
   cuenta2, #0#1, "N";
   be;
   NULL, entrada;
|FIN;
|| --------------------------------------------------------

|PROCESO PongoIgual;
  #3#18 = " ";
  |GRABA 020#3a;
  |LIBERA #3;
|FPRC;

|DEFBUCLE PongoIgual;
  #3#1;
  18;
  00, fDFecha, 000001, 0001, "S";
  99, fHFecha, 999999, 9999, "S";
  be;
  NULL,PongoIgual;
|FIN;

|PROCESO HazTodo;
  |BUCLE cpentrad;           || Bucle entrada;
  |SI swgraba = 1; |BUCLE PongoIgual; |FINSI;
|FPRC;

|CALCULO pase;|TIPO 3;

   fDFecha = #0#0;
   fHFecha = #0#1;
   |SI #0#15 = "S"; |Y #0#16 ! 0;
       nNume1 = #0#0; nNume1 = nNume1 - #0#16; fDFecha = nNume1;
       nNume1 = #0#1; nNume1 = nNume1 + #0#16; fHFecha = nNume1;
  |FINSI;

  contador = 0;
  |ABRE #1;
  |LEE 040#1u;
  |SI FSalida = 0;
      contador = #1#0;
  |FINSI;
  |SELECT #3#1;

  |SI #0#2 = "S";
      cuenta1 = #0#3;
      cuenta2 = #0#4;
      |HAZ HazTodo;
  |SINO;
      |PARA nPara1 = 5; |SI nPara1 [ 13; |HACIENDO nPara1 = nPara1 + 1;
            aAlfa1 = #0nPara1; |QBF aAlfa1;
            |SI aAlfa1 ! "";
                cuenta1 = #0nPara1;
                cuenta2 = #0nPara1;
                |HAZ HazTodo;
            |FINSI;
      |SIGUE;
  |FINSI;
  |CIERRA #1;
|FCAL;
