|FICHEROS;
   caivahis #0;
   camaniva #1;
   caivalin #2;
   calibros #3;
   caconimp #8;
   caivaasi #200;

   canempre #30;
   caselem0 #997;
   caselem1 #998;
   cawdatos #900;
|FIN;

|VARIABLES;
   informe = "";
   inform1 = "caivahis";
   inform2 = "caivahi0";
   inform3 = "caivahi1";

   &SwParcial = 0;
   &Mensaje = "";
   &SwCabIva = 0;
   &SwLinIva = 0;
   &SwTotal = 0;

   &TotalBases  = 0;
   &TotalIvas   = 0;
   &TotalRecs   = 0;
   &TotalCuotas = 0;

   swAlgun  = 0;
   desinv   = "";
   hesinv   = "";
   &aDesTip = "";
   aAlfa    = "";
   aAlfa1   = "";
   aTitulo  = "";

   nEmpMulti = 0;
   &eSwMul   = 0;
   &espe15   = "";
   &espe16   = 0;
   aGuarda   = "";
   fDFecha    = @;
|FIN;

|INCLUYE dscont_i;
|INCLUYE caliva_i;

|| ====================================================================
|CALCULO Defectos; |TIPO 7;
     |HAZ eParaModelos;
     fDFecha = fec1;
     |SI enSwSelFc = 1; fDFecha = "01.01.2000"; |FINSI;
     |SI #0#14 = "N";
         #0#3 = fDFecha;
     |SINO;
         #0#3 = "01.01.2000";
     |FINSI;
     |PINTA #0#3;
     #0#4 = fec2; |PINTA #0#4;
     |HAZ DatosEmp;
|FCAL;

|PROCESO DatosEmp;
     |ABRE #8;
     |DDEFECTO #8;
     #8#0 = 1;
     |LEE 011#8=;
     |CIERRA #8;
     #0#12 = #8#7; |PINTA #0#12;
     aAlfa1 = #8#8; |C_M #0#12,1,aAlfa1;
|FPRC;

|CALCULO fecha; |TIPO 0;
    |SI FSalida = 2; |ACABA; |FINSI;
    |SI #0#14 = "N";
         fDFecha = fec1;
         |SI enSwSelFc = 1; fDFecha = "01.01.2000"; |FINSI;
         |SI #0Campo < fDFecha; |O #0Campo > fec2;
             |MENAV "0000Fecha fuera del ejercicio. ";
             |ERROR;
             |ACABA;
        |FINSI;
    |FINSI;
    |SI Campo = 4;
        |SI #0#4 < #0#3;
            |MENAV "    Fecha Erronea.! ";
            |ERROR;
            |ACABA;
       |FINSI;
    |FINSI;
    aAlfa = #0#4; aAlfa = aAlfa % 0402; #0#10 = aAlfa; |PINTA #0#10;
|FCAL;

|CALCULO HPeriodo; |TIPO 0;
    |SI #0#10 < #0#9;
        |MENAV "0000Periodo hasta inferior a Periodo desde";
        |ERROR;
    |FINSI;
|FCAL;
|| *********************************************************************

|PROCESO LineasFacturas;
   swAlgun = 1;
   |PRINT;
   SwCabIva = 0; SwTotal = 0;

   |HAZ sumaLiqIVA;

   TotalBases  = (TotalBases + #caivalin#6) ! EURODB;
   TotalIvas   = (TotalIvas  + #caivalin#8) ! EURODB;
   TotalRecs   = (TotalRecs  + #caivalin#10) ! EURODB;
   TotalCuotas = (TotalIvas + TotalRecs) ! EURODB;
|FPRC;

|DEFBUCLE LineasFacturas;
 #caivalin#1;
 17;
 #camaniva#0,#camaniva#1,01,desinv;
 #camaniva#0,#camaniva#1,99,hesinv;
 ;
 NULL,LineasFacturas;
|FIN;

|PROCESO Facturas;
   |SI #camaniva#36 = "R"; aDesTip = "REPERCUTIDO  "; |FINSI;
   |SI #camaniva#36 = "S"; aDesTip = "SOPORTADO    "; |FINSI;
   |SI #camaniva#36 = "N"; aDesTip = "REGISTRO IRPF"; |FINSI;
   SwCabIva = 1; SwTotal = 1;
   |BUCLE LineasFacturas;
|FPRC;

|DEFBUCLE Facturas;
 #camaniva#3;
 4,5;
 #0#0, #0#6, #0#1, #0#3, #0#9;
 #0#0, #0#7, #0#2, #0#4, #0#10;
 ;
 NULL,Facturas;
|FIN;

|DEFBUCLE Facturas1;
 #camaniva#5;
 2,3,5;
 #0#3, #0#0, 0000000001, #0#6, #0#1, #0#9;
 #0#4, #0#0, 9999999999, #0#7, #0#2, #0#10;
 ;
 NULL, Facturas;
|FIN;

|DEFBUCLE Facturas2;
 #camaniva#1;
 2,3,8,5;
 #0#0, 0000000001, #0#6, #0#1, #0#3, #0#9;
 #0#0, 9999999999, #0#7, #0#2, #0#4, #0#10;
 ;
 NULL,NULL,NULL,NULL,NULL,Facturas;
 #camaniva#8, #camaniva#0, #camaniva#1;
|FIN;

|| *********************************************************************
|CALCULO impre; |TIPO 3;
   desinv = #0#8;
   hesinv = #0#8;
   |SI #0#8 = "A"; desinv = "A"; hesinv = "Z"; |FINSI;

   enDatos = 0; |SI #0#12 = "S"; enDatos = 1; |FINSI;

   |IMPRE 0;
   |SI FSalida < 0; |ACABA; |FINSI;

   eSwMul = 0;
   |SI #0#14 = "N";
       |SI #48#27 = "S";
           |HAZ MultiEmpresa;
       |SINO;
           |DFICE dirfich0; |QBF dirfich0;
           |HAZ HazTodo1;
       |FINSI;
   |SINO;
       eSwMul = 1;
       |SI #48#27 = "S";  eSwMul = 2; |FINSI;
       |HAZ MultiEjercicio;
   |FINSI;

   |FINIMP;
|FCAL;

|PROCESO HazTodo1;
  |SI #0#8 ! "D";
      |HAZ HazTodo;
  |SINO;
      aTitulo = #0#13; |QBF aTitulo;
      desinv = "N";
      hesinv = "N";
      |HAZ HazTodo;
      #0#13 = aTitulo + " (Inversion)";
      desinv = "S";
      hesinv = "S";
      |HAZ HazTodo;
      #0#13 = aTitulo;
  |FINSI;
|FPRC;

|PROCESO EmpiezaEmpresa;
   eaIa   = #30#21;
   aAlfa1 = #0#3; aAlfa1 = aAlfa1 % 0402; enM1 = aAlfa1;
   aAlfa1 = #0#4; aAlfa1 = aAlfa1 % 0402; enM2 = aAlfa1;
   |HAZ LeeDatosEmpresa;
   #900#13 = #0#3; #900#14 = #0#4;
   #900#27 = #0#13;
   |SI eSwMul ! 0; #900#12 = #caselem0#4; |FINSI;

   SwParcial = 0;
   Mensaje = "";
   SwLinIva = 1;
   TotalBases  = 0;
   TotalIvas   = 0;
   TotalRecs   = 0;
   TotalCuotas = 0;
   |HAZ BorraAcum;

   |ABRE #3;
   #3#0 = #0#0;
   |LEE 041#3=;
   |SI FSalida ! 0;
       |DDEFECTO #3;
   |FINSI;
   |CIERRA #3;

   informe = inform1;
   |SI #3#2 ! "N";
       |ABRE #200;
       |LEE 041#200p;
       |SI FSalida ! 0;  |DDEFECTO #200; |FINSI;
       |CIERRA #200;
       |SI #200#69 = "N"; informe = inform2; |FINSI;
   |SINO;
       informe = inform3;
   |FINSI;

   aAlfa1 = Impresora;
   aAlfa1 = aAlfa1 % 113;
   |SI aAlfa1 = "CrystalReport";
       informe = "cc" + (informe % 306);
   |FINSI;

   swAlgun = 0;

   |INFOR informe;
   |SI FSalida < 0; swAlgun = -1; |FINSI;
|FPRC;

|PROCESO HazTodo;

   |SI nEmpMulti = -1; |O eSwMul = 0;

       |HAZ EmpiezaEmpresa;
       |SI swAlgun = -1; |ACABA; |FINSI;

       nEmpMulti = #30#2;
   |FINSI;

   |SI eSwMul ! 0; |Y nEmpMulti ! #30#2;
       |HAZ TerminaEmpresa;

       nEmpMulti = #30#2;
       |HAZ EmpiezaEmpresa;
       |SI swAlgun = -1; |ACABA; |FINSI;
   |FINSI;

   |SI #0#11 = 3; |BUCLE Facturas;  |FINSI;
   |SI #0#11 = 2; |BUCLE Facturas2; |FINSI;
   |SI #0#11 = 1; |BUCLE Facturas1; |FINSI;

   |SI eSwMul = 0;
       |HAZ TerminaEmpresa;
   |FINSI;
|FPRC;

|PROCESO TerminaEmpresa;
   |SI swAlgun = -1;          |ACABA; |FINSI;   || No hay ninguna informe
   |SI swAlgun =  0; |FININF; |ACABA; |FINSI;   || No hay ninguna factura sel.
   |PIEINF; |FININF;

   |SI #3#2 ! "N"; |HAZ imprimeLiqIVA; |FINSI;
|FPRC;

|| ////////////////////////////////////
|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;
   |SI enSwSelFc = 0;
       |SI #0#3 > #caselem1#5; |O #0#4 < #caselem1#4;
           |ACABA;  || Fuera de la seleccion
       |FINSI;
   |FINSI;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #2 dirfich0;
   |PATH_DAT #3 dirfich0;
   |PATH_DAT #200 dirfich0;
   |HAZ HazTodo1;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 000001;
 999999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  espe15 = "N";
  eSwMul = 0;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;

|DEFBUCLE MultiEmpEjer;
  #caselem1#2;
  ;
  #997#0, "01.01.0000";
  #997#0, "31.12.2999";
  ;
  NULL,MultiEmp1;
|FIN;

|PROCESO MultiEjer1;

  aGuarda = #0#8;
  |SI #0#8 ! "D";
      |BUCLE MultiEmpEjer;
      |HAZ TerminaEmpresa;
  |SINO;
      nEmpMulti = -1;
      #0#8 = "N";
      aTitulo = #0#13; |QBF aTitulo;
      desinv = "N";
      hesinv = "N";
      |BUCLE MultiEmpEjer;
      |HAZ TerminaEmpresa;
      nEmpMulti = -1;
      #0#8 = "S";
      #0#13 = aTitulo + " (Inversion)";
      desinv = "S";
      hesinv = "S";
      |BUCLE MultiEmpEjer;
      |HAZ TerminaEmpresa;
      #0#13 = aTitulo;
  |FINSI;
  #0#8 = aGuarda;
|FPRC;

|DEFBUCLE MultiEjer0;
   #caselem0#1;
   ;
   00001;
   99999;
   ;
   NULL,MultiEjer1;
|FIN;

|PROCESO MultiEjercicio;
   espe15 = "S";

   |SUB_EJECUTA "caselemp";
   |NOME_DAT #998 eaFichsel;
   |NOME_DAT #997 eaFichres;

   nEmpMulti = -1;
   |BUCLE MultiEjer0;
   |DELINDEX #998;
   |DELINDEX #997;
|FPRC;
