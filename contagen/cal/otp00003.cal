|FICHEROS;
  otp00003 #0;

  cacuenta #1;
  otp00002 #2;
  otp00001 #3;

  otp00000 #201;
  imagen@  #901;
|FIN;

|VARIABLES;
  &entrada    = 1;

  &eaNomFEst  = "";  ||Nombre del Fichero Creado
  &eaEstruc   = "";
  &eaEstrucN  = "";
  &enCam1     = 0;
  &enCam2     = 0;
  aDirInicio  = "";
  aMas        = "";
  nCanal      = 0;

    d_desde = 0;
    d_hasta = 0;
    d_trozo = "";
    d_masca = "";
    d_alfa1 = "";
    d_cuent = "";
    d_error = 0;

    nEstado = 0;
    nNume1 = 0;
    nPara1 = 0;
    nPara2 = 0;
    nPara3 = 0;
    nTp100 = 0;
    nSwBusco = 0;
    nSwAlgun = 0;
    aAlfa1   = "";
    nSaldo   = 0;
    nSwOper  = 0;
    nTSaldo  = 0;

   actcta_alfa1 = "";

   actcta_i1 = 0;
   actcta_i2 = 0;
   actcta_i3 = 0;
   actcta_i4 = 0;
   actcta_i5 = 0;
   actcta_alfa2 = "";
   actcta_alfa3 = "";
   actcta_alfa4 = "";
   actcta_alfa5 = "";
   {-1}actcta_punt1 = 0;
   nCalc  = 0;
|FIN;

|INCLUYE  dscont_i;

|| ========================================================================

|PROCESO SumoTP100;
 #901#0 = #2#2;
 |LEE 001#901=;
 |SI FSalida = 0;
     |SI #901#5 = "A"; |O #901#5 = "D";
         nSaldo = #901#51 - #901#52;
     |SINO;
         nSaldo = #901#52 - #901#51;
     |FINSI;
     |SI nSwOper = 0;
         nTSaldo = nTSaldo +  nSaldo;
     |SINO;
         #2#5 = ((nSaldo * 100) / nTSaldo) ! 2;
         #2#6 = nSaldo;
         |GRABA 020#2a;
     |FINSI;
 |FINSI;
 |LIBERA #2;
|FPRC;

|DEFBUCLE SumoTP100;
 #2#1;
 ;
 #3#0, 001;
 #3#0, 999;
 be;
 NULL, SumoTP100;
|FIN;

|PROCESO LosTP100;
 |ABRE #901;
 nTSaldo = 0;
 nSaldo  = 0;
 nSwOper = 0;
 |BUCLE SumoTP100;

 nSwOper = 1;
 |BUCLE SumoTP100;

 #3#3 = 100;
 #3#4 = nTSaldo;
 |GRABA 020#3a;
 |LIBERA #3;
 |CIERRA #901;
|FPRC;

|DEFBUCLE LosTP100;
 #3#1;
 ;
 "            ";
 "zzzzzzzzzzzz";
 be;
 NULL, LosTP100;
|FIN;

|| ========================================================================
|PROCESO GrabaCuenta;

 #1#0 = #901#0;
 eaCuenta = #1#0; |HAZ SacaNivel;
 #1#1 = enNivel;
 #1#2 = #901#2; #1#3 = #901#3;
 #1#4 = #901#4; #1#5 = #901#5;
 #1#6 = #901#6; #1#7 = #901#7;
 #1#8 = #901#8;

 aAlfa1 = #1#0; |QBF aAlfa1;
 aAlfa1 = aAlfa1 +"zzzzzzzzzzzz";
 aAlfa1 = aAlfa1 % 112;
 #1#54   = aAlfa1;
 #1#55   = enNivel;

 #1#56   = #901#56;
 #1#57   = #901#57;
 #1#58   = #901#58;
 #1#59   = #901#59;
 |SI nEstado ! 0; |GRABA 020#1n; |FINSI;
 |SI nEstado = 0; |GRABA 020#1a; |FINSI;
|FPRC;

|PROCESO SiVale;
  d_error = 0;
  d_cuent = eaCuenta;
  d_alfa1 = d_cuent % d_trozo;   || mascara
  |SI d_alfa1 ! d_masca;|Y d_desde ! 0;|Y d_hasta ! 0;
        d_error = 1;               || no es valida la cuenta
  |FINSI;
|FPRC;

|PROCESO GrabaDirecta;
     #1#0 = #901#0;
     |LEE 001#1=;
     nEstado = FSalida;

     |DDEFECTO #1;
     |PDEFECTO #0, 5, 52;

     #0#5 = #901#0;  #0#6 = #901#2;
     |PINTA #0#5;  |PINTA #0#6;

     eaCuenta = #901#0;
     |PARA nPara1 = 9; |SI nPara1 [ 48; |HACIENDO nPara1 = nPara1 + 3;
           nPara2 = nPara1 + 1;
           nPara3 = nPara1 + 2;
           |SI nTp100 = 100;
               #1nPara1 = #901nPara1;
               #1nPara2 = #901nPara2;
           |SINO;
               #1nPara1 = (#901nPara1 % nTp100) ! EURODB;
               #1nPara2 = (#901nPara2 % nTp100) ! EURODB;
           |FINSI;
           #1nPara3 = #1nPara1 - #1nPara2;
           #1#51 = #1#51 + #1nPara1;
           #1#52 = #1#52 + #1nPara2;
           #1#53 = #1#51 - #1#52;

           #0nPara1 = #1nPara1;
           #0nPara2 = #1nPara2;
           #0nPara3 = #1nPara3;
     |SIGUE;
     #0#7 = #1#51;           || .. |PINTA #0#7;
     #0#8 = #1#52;           || .. |PINTA #0#8;
     #0#51 = #0#7 - #0#8;    || .. |PINTA #0#51;

     |HAZ GrabaCuenta;
     nSwAlgun     = 1;
     aCUENTA      = #1#0;
     nNIVELCUENTA = #1#1;
     |HAZ ActCuenta_est;
|FPRC;

|PROCESO CtaEstructura;
 eaCuenta = #2#2;
 |HAZ SiVale;
 |SI d_error = 0;
     nSwBusco = 1;
     nTp100 = nTp100 + #2#5;
 |FINSI;
|FPRC;

|DEFBUCLE CtaEstructura;
 #2#1;
 ;
 #901#0, 001;
 #901#0, 999;
 ;
 NULL, CtaEstructura;
|FIN;

|PROCESO BuscoFich;
 nSwBusco = 0;
 nTp100   = 0;
 |BUCLE CtaEstructura;
 |SI nSwBusco = 1;
     |HAZ GrabaDirecta;
 |FINSI;
|FPRC;
||///////////////////////////////////////////////////
|| ... Cuentas con pase directo S

|PROCESO PasaImagen_S;

 eaCuenta = #901#0;
 |HAZ SiVale;
 |SI d_error = 0;
     nTp100 = 100;
     |HAZ GrabaDirecta;
 |SINO;
     |HAZ BuscoFich;
 |FINSI;
|FPRC;

|DEFBUCLE PasaImagen_S;
 #901#1001;
 3;
 "            ","S";
 "zzzzzzzzzzzz","S";
 e;
 NULL, PasaImagen_S;
|FIN;

|| //////////////////////////////////////////////////
|| ... Cuentas con pase directo N (todas las cuentas)

|PROCESO PasaImagen_N;

 #1#0 = #901#0;
 |LEE 001#1=;
 nEstado = FSalida;

 |DDEFECTO #1;
 |HAZ GrabaCuenta;

 #0#5 = #901#0;  #0#6 = #901#2;
 |PINTA #0#5;  |PINTA #0#6;
|FPRC;

|DEFBUCLE PasaImagen_N;
 #901#1001;
 3;
 "            ","N";
 "zzzzzzzzzzzz","N";
 e;
 NULL, PasaImagen_N;
|FIN;

|| *********************************************************

|PROGRAMA;

   |HAZ LeeControl;

   |DBASE aDirInicio;
   aMas  = aDirInicio + "def/cacuenta";
   |CARGA_DEF aMas, imagen@;
   |SI FSalida ! 0;
       |MENAV "    Error Cargado Fichero de Contabilidad. Proceso Interrumpido";
       |PTEC 807;
   |FINSI;

   eaNomFEst = ("3ow" + Usuario) % 108;
   |NOME_DAT #1 eaNomFEst;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #2 dirfich0;
   |PATH_DAT #901 dirfich0;

   |PINPA #0#0;

   #0#0 = enEmpresa;   ||  |PINTA #0#0;
   #0#1 = enEjercicio; ||  |PINTA #0#1;
   #0#2 = eaNombreEmp; ||  |PINTA #0#2;
   #0#3 = eaEstruc;    ||  |PINTA #0#3;
   #0#4 = eaEstrucN;   ||  |PINTA #0#4;

   |ABRE #1;  |CIERRA #1;  |DELINDEX #1;

   |BUCLE LosTP100;

   |ABRE #1;
   |BUCLE PasaImagen_N;

   |BUCLE PasaImagen_S;
   |CIERRA #1;

   |DESCARGA_DEF #901;
|FPRO;

|| **********************************************************************

|| ... Primeros Procesos
|PROCESO LeeControl;

   |ABRE #201;
   #201#0 = enEmpresa;
   |LEE 101#201=;
   |SI FSalida ! 0;
       |DDEFECTO #201;
       #201#0 = enEmpresa;
       #201#1 = eaNombreEmp;
       #201#7 = enEjercicio;
       |GRABA 020#201b;
   |FINSI;
   |CIERRA #201;

   d_desde = #201#2;     || a partir de ...
   d_hasta = #201#3;     || nro. digitos
   d_masca = #201#4;
   |QBF d_masca;         || mascara
   nNume1  = (d_desde * 100) + d_hasta;
   d_trozo = nNume1;

   eaEstruc  = #201#4;
   eaEstrucN = #201#5;
   enCam1    = #201#2;
   enCam2    = #201#3;
|FPRC;

**********************************************************************
ACTUALIZACION DE CUENTAS
**********************************************************************

|PROCESO suma_cuenta;
     #1#51 = 0; #1#52 = 0; #1#53 = 0;
     |PARA nPara1 = 9; |SI nPara1 [ 48; |HACIENDO nPara1 = nPara1 + 3;
           nPara2 = nPara1 + 1;
           nPara3 = nPara1 + 2;

           #1nPara1 = #1nPara1 + #0nPara1;
           #1nPara2 = #1nPara2 + #0nPara2;
           #1nPara3 = #1nPara1 - #1nPara2;
           #1#51 = #1#51 + #1nPara1;
           #1#52 = #1#52 + #1nPara2;
           #1#53 = #1#51 - #1#52;
     |SIGUE;
|FPRC;


|PROCESO ActCuenta_est;

   actcta_alfa1 = aCUENTA;
   |QBF actcta_alfa1;
   |SI actcta_alfa1 = "";
       |ACABA;
   |FINSI;

   Iactcta_punt1 = dig3<-;
   Iactcta_punt1 = Iactcta_punt1 + nNIVELCUENTA;
   nCalc = nNIVELCUENTA;
   nCalc = nCalc - 1;
   |PARA actcta_i5 = nCalc; |SI actcta_i5 > 0;|HACIENDO actcta_i5 = actcta_i5 - 1;
         Iactcta_punt1 = Iactcta_punt1 - 1;
         actcta_alfa1 = aCUENTA % (N\ 100 + actcta_punt1);
         |DDEFECTO #1;
         #1#0 = actcta_alfa1;
         #1#1 = actcta_i5;
         |LEE 101#1=;
         |SI FSalida = 0;
             |HAZ suma_cuenta;
             |GRABA 020#1a;
         |FINSI;
         |LIBERA #1; || importante
   |SIGUE;
|FPRC;
