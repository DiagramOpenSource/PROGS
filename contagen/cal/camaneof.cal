|FICHEROS;
   camaneof #0;
   camanent #1;

   &provinc@ #700;
   &poblaci@ #703;
   &regisnif@ #709;
   &clientes@ #715;
   &sgpaises@ #718;

   :/integral/def/dsam0116 #116;

   camaneo@ #999;
|FIN;

|INCLUYE dscont_i;

|VARIABLES;
   nume1  = 0;
   nModo  = 0;
   nInkey = 0;
   aMas   = "";
   aDef   = "";
   aAlfa  = "";
|FIN;

|CALCULO OfLocalidad;
  enCodProv = #0#6;
  enCodPobl = #0#7;
  |HAZ LeeLocalidad;
  #0#8 = eaProvincia; |PINTA #0#8;
  #0#5 = eaPoblacion; |PINTA #0#5;
|FCAL;

|PROCESO LaEntidad; |TIPO 0;
    |ABRE #1;
    #1#0 = #0#0;
    |LEE 041#1=;
    |SI FSalida = 0;
        #0#3 = #1#1;
        |PINTA #0#3;
    |FINSI;
    |CIERRA #1;
|FPRC;

|PROCESO Tipo7C0y1;  |TIPO 7;
  |SI HayAcceso = 1;
      aAlfa =  "0000Pulse <F2> Consultar Bancos de Clientes";
      |SI Campo = 0;
          aAlfa = "0000<F2> Consultar Bancos de Clientes, <F3> Recoger Bancos de Clientes";
      |FINSI;
      |MENSA aAlfa;
  |FINSI;
|FPRC;

|PROCESO Dsam0116;
  #999#0 = #116#2;
  #999#1 = #116#3;
  |LEE 041#999=;
  |SI FSalida ! 0;
      |DDEFECTO #999;
      #999#0 = #116#2;
      #999#1 = #116#3;
      #999#2 = #116#4;
      #999#3 = #116#5;
      #999#4 = #116#6;
      |GRABA 040#999n;
  |FINSI;
|FPRC;

|DEFBUCLE Dsam0116;
  #116#1;
  ;
  00001;
  99999;
  e;
  NULL, Dsam0116;
|FIN;

|PROCESO RecojoENT;
  |DBASE aMas;
  aDef = aMas + "def/camaneof";
  |CARGA_DEF aDef, camaneo@;
  |SI FSalida = 0;
      |ABRE #camaneo@;
      |BUCLE Dsam0116;
      |CIERRA #camaneo@;
      |DESCARGA_DEF #camaneo@;
      |MENAV "0000Datos Recogidos";
      |PTEC 806;
  |FINSI;
|FPRC;

|PROCESO ConsEntidadmEof;  |TIPO 0;
  nInkey = FSalida;

  |SI HayAcceso = 0; |ACABA; |FINSI;

  |BLIN 4;
  |SI nInkey = 10;
     |PUSHV 0501 2380;
     |ABRE #116;
     |SELECT #2#116;
     #116#1 = "";
     #116#2 = #0#0;
     |LEE 040#116];
     |SI FSalida ! 0;
         |LEE 040#116p;
     |FINSI;
     |CONSULTA_CLAVE #2#116;
     |CIERRA #116;
     |POPV;

     #0#0 = #116#2;   |PINTA #0#0; ||Entidad
     #0#1 = #116#3;   |PINTA #0#1; ||Oficina

     |PTEC 802;
  |FINSI;

  |SI nInkey = 11;
      |CONFI "0000SRecoger Datos de Bancos de Clientes";
      |SI FSalida = 0;
          |HAZ RecojoENT;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO ConsOficinamEof;  |TIPO 0;
  nInkey = FSalida;
  |SI HayAcceso = 0; |ACABA; |FINSI;

  |BLIN 4;
  |SI nInkey = 10;
     |PUSHV 0501 2380;
     |ABRE #116;
     |SELECT #2#116;
     #116#2 = #0#0;
     #116#3 = #0#1;
     |LEE 040#116];
     |SI FSalida ! 0;
         |LEE 040#116p;
     |FINSI;
     |CONSULTA_CLAVE #3#116;
     |CIERRA #116;
     |POPV;
     #0#0 = #116#2;   |PINTA #0#0; ||Entidad
     #0#1 = #116#3;   |PINTA #0#1; ||Oficina
  |FINSI;
|FPRC;

|PROCESO Tipo0;
     nModo = (FEntrada / 100) ! 0;
     |SI HayAcceso = 1;
         |SI nModo = 1; |O nModo = 2;
             |HAZ ActualizaDatos;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO ActualizaDatos;
     |ABRE #116;
     |SELECT #2#116;
     #116#2 = #0#0;
     #116#3 = #0#1;
     |LEE 040#116=;
     |SI FSalida = 0;
         #0#2 = #116#4;   |PINTA #0#2; ||D.C.
         #0#3 = #116#5;   |PINTA #0#3; ||Nombre Oficina
         #0#4 = #116#6;   |PINTA #0#4; ||Calle
     |FINSI;
|FPRC;

|PROCESO Tipo3Eof; |TIPO 3;
    |ABRE #1;
    #1#0 = #0#0;
    |LEE 041#1=;
    |SI FSalida ! 0;
        #1#0 = #0#0;
        |GRABA 020#1c;
    |FINSI;
    #1#1 = #0#3;
    |GRABA 020#1a;
    |CIERRA #1;
|FPRC;
