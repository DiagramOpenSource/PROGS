|FICHEROS;
   caz00029 #0;

   canempre #30;
   canempr@ #330;

   :/basica/def/agicen14 #114;

   camoneda #88;
   catesfin #170;

   ca8ctrle #400;  ||Control Empresa
   ca8comay #401;  ||Control Cuentas de mayor

   Referen@;
   ReferLi@;
|FIN;

|VARIABLES;
   swExisEmp = 0;
   nNume    = 0;
   nNume1   = 0;
   nNume2   = 0;
   nNume3   = 0;
   nNume4   = 0;
   nPer     = 0;
   nEmp     = 0;
   nCampo   = 0;
   nInkey   = 0;
   aAlfa    = "";
   aAlfa1   = "";
   aAlfa2   = "";
   aAlfa3   = "";
   aMensa   = "";
   fFecha   = @;
   nSelError  = 0;
   nLaEmpresa = 0;

   aPath    = "";
   aDef     = "";

   aQueQuiero = "";
   aNumCampos = "";
   nNumCampos = 0;
   nEstado    = 0;

   &any_pos = 0;
   &num_per = 0;
   nEjerActual = 0;
   &enPerioOri = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE copian_i;
|INCLUYE copia1_i;

|| *******************************************************************
|PROCESO DefectosA; |TIPO 7;
 #0#2  = enPeriodo;   |PINTA #0#2;
 #0#31 = enEjercicio; |PINTA #0#31;
 |HAZ DefectosN;
|FPRC;

|PROCESO DefectosN;
 #0#4  = #0#2 + 1; |SI #0#4 = 10; #0#4 = 0; |FINSI;
 |PINTA #0#4;
 #0#32 = #0#31 + 1; |PINTA #0#32;

 |C_M #0#34, 1, "N";
 |SI #0#32 = 2008;
     |C_M #0#34, 1, "S";
 |SINO;
     |C_M #0#34, 1, "N";
     #0#34 = 0;
     |PINTA #0#34;
 |FINSI;
|FPRC;

|PROCESO CodCta0; |TIPO 0;
  |C_M #0#34, 0, aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

  |ABRE #401;
  #401#0 = #0#34;
  |LEE 001#401=;
  |SI FSalida ! 0;
      |MENAV "    Error, no existe Codigo de Cuentas de Mayor";
      |ERROR;
  |SINO;
      |C_A #401#1,1,35; |PINTA 1040, #401#1;
  |FINSI;
  |CIERRA #401;
|FPRC;

|PROCESO Select; |TIPO 0;
  |SI #0#0 = "S";
      |C_M #0#1,1,"S";
      |C_M #0#3,1,"S";
      |PARA nPara1 = 5; |SI nPara1 [ 27; |HACIENDO nPara1 = nPara1 + 2;
            nNume1 = nPara1 + 1;
            |C_M #0nPara1,1,"N"; #0nPara1 = 0;  |PINTA #0nPara1;
                                 #0nNume1 = ""; |PINTA #0nNume1;
      |SIGUE;
   |SINO;
      |C_M #0#1,1,"N"; #0#1 = 1; |PINTA #0#1;
      |C_M #0#3,1,"N"; #0#3 = 99999; |PINTA #0#3;
      |PARA nPara1 = 5; |SI nPara1 [ 27; |HACIENDO nPara1 = nPara1 + 2;
            |C_M #0nPara1,1,"S";
      |SIGUE;
   |FINSI;
|FPRC;

|CALCULO MiraEmpresa; |TIPO 0;
   nInkey = FSalida;
   |SI nInkey = 2; |ACABA; |FINSI;

   |SI #0Campo = 0; |ACABA; |FINSI;

   |SI #0#0 = "N";
       nEmp = #0Campo;
       nPer = #0#2;
       |HAZ SeldeEmpresa;
       |SI nSelError ! 0;
           |MENAV aMensa;
           |ERROR;
           |ACABA;
       |FINSI;
       nCampo = Campo + 1;
       #0nCampo = #30#1;  |PINTA #0nCampo;
   |FINSI;
|FCAL;

|CALCULO NoMasEmp; |TIPO 0;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 2;  |SI nCampo [ 27;  |HACIENDO nCampo = nCampo + 2;
               nNume1 = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
               #0nNume1 = ""; |PINTA #0nNume1;
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 2;  |SI nCampo [ 27;  |HACIENDO nCampo = nCampo + 2;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|PROCESO SeldeEmpresa;
   |ABRE #30;
   aMensa    = "";
   nSelError = 0;
   #30#2 = nEmp;
   #30#3 = nPer;
   |LEE 010#30=;
   |SI FSalida = 0;
       |SI #0#29 ! "T";
           |SI #30#21 ! #0#29;
               |SI #30#21 = "I";
                   aMensa = "    Empresa Inactiva";
               |SINO;
                   aMensa = "    Empresa Activa";
               |FINSI;
               nSelError = 1;
               |VETE Cierra;
           |FINSI;
       |FINSI;
   |SINO;
       nSelError  = 1;
       aMensa = "    No Existe Empresa ";
   |FINSI;

|ET Cierra;
   |CIERRA #30;
|FPRC;

|| ::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
|PROCESO catesfin;
 #170#3 = 0;
 #170#4 = "01.01.1900";
 |GRABA 020#170a;
 |LIBERA #170;
|FPRC;

|DEFBUCLE catesfin;
  #170#1;
  ;
  "            ";
  "zzzzzzzzzzzz";
  be;
  NULL, catesfin;
|FIN;

|PROCESO pone0_tesfin;
   |PATH_DAT #170 fich_alta;
   |BUCLE catesfin;
|FPRC;

|PROCESO Cartera;
   || Compruebo si hay cartera y si la hay compruebo que el ejercicio
   ||    anterior estaba en el control de recibos de contabilidad. Si es
   ||    asi, creo el registro del nuevo periodo en el control

     |DBASS aAlfa; |QBF aAlfa;
     aAlfa = aAlfa + "dscarter/def/dscam042.mas";
     |CARGA_DEF aAlfa, Referen@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |DBASS aAlfa; |QBF aAlfa;
     aAlfa = aAlfa + "dscarter/def/dscam043.mas";
     |CARGA_DEF aAlfa, ReferLi@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #Referen@;
         |ACABA;
     |FINSI;

     |DBASS aAlfa; |QBF aAlfa;
     aAlfa = aAlfa + "dscarter/fich/";
     |PATH_DAT #Referen@#aAlfa#;
     |PATH_DAT #ReferLi@#aAlfa#;

     |ABRE #Referen@;
     |ABRE #ReferLi@;
     #Referen@#0 = #canempre#2;
     #Referen@#1 = #caz00029#4;
     |LEE 000#Referen@.=;
     |SI FSalida ! 0;
         #Referen@#0 = #canempre#2;
         #Referen@#1 = #caz00029#2;
         |LEE 000#Referen@.=;
         |SI FSalida = 0;
             #ReferLi@#0 = #Referen@#0;
             #ReferLi@#1 = #Referen@#1;
             #ReferLi@#2 = 1;
             |LEE 000#ReferLi@.];
             |PARA; |SI FSalida = 0; |Y #ReferLi@#0 = #Referen@#0; |Y #ReferLi@#1 = #Referen@#1; |HACIENDO;
                    |SALVA_FICHA #ReferLi@;
                    #ReferLi@#1 = #caz00029#4;
                    |GRABA 020#ReferLi@.n;
                    |LIBERA #ReferLi@;

                    |REPON_FICHA #ReferLi@;
                    |LEE 000#ReferLi@.s;
             |SIGUE;

             #Referen@#1 = #caz00029#4;
             |GRABA 020#Referen@.n;
             |LIBERA #Referen@;
         |FINSI;
     |FINSI;

     |CIERRA #ReferLi@;
     |CIERRA #Referen@;
     |DESCARGA_DEF #ReferLi@;
     |DESCARGA_DEF #Referen@;
|FPRC;

|PROCESO LasCreo;

   |SI #0#29 ! "T";
       |SI #330#21 ! #0#29;
           |ACABA;
       |FINSI;
   |FINSI;

   eNum2008 = #0#34;
   |ABRE #400;
   |DDEFECTO #400;
   #400#0 = #330#2;
   #400#1 = #330#3;
   |LEE 001#400=;
   |SI FSalida = 0;
      eNum2008 = #400#2;
   |FINSI;
   |CIERRA #400;

   |ATRI P;
   |PINTA 2203, "Creando Nuevo Periodo ... ";
   |ATRI 0;

   |PINTA 2312, #330#2;
   |PINTA 2319, #330#3;
   |PINTA 2322, #330#1;
   |PINTA 2375, #330#26;

   enEmpresa = #330#2;
   |HAZ Analitica;  || Comprobar para copiar ficheros Analitica

   |SI #330#26 < 80;
       enEjercicio = #330#26 + 2000;
   |SINO;
       enEjercicio = #330#26 + 1900;
   |FINSI;
   |SI enEjercicio ! #0#31; |ACABA; |FINSI;

   |ABRE #30;
   #30#2 = #330#2;
   #30#3 = #330#3;
   |LEE 011#30=;
   |SALVA_DATOS #30;
   num_por1 = #30#2;
   any_por1 = #30#26; || Datos para la Portada

|| --------------------- Empresa Destino por si Ya Existe
   swExisEmp = 0;
   #30#2 = #330#2;
   #30#3 = #0#4;
   |LEE 011#30=;
   nEstado = FSalida;
   |SI nEstado = 0;
       |SI #30#26 < 80;
           nEjerActual = #30#26 + 2000;
       |SINO;
           nEjerActual = #30#26 + 1900;
       |FINSI;
       |SI #0#33 = "N"; |O nEjerActual ! #0#32;
           |LIBERA #330;
           |CIERRA #30;
           |ACABA;
       |FINSI;
       swExisEmp = 1;
   |SINO;

      |REPON_DATOS #30;
      #30#3  = #0#4;
      nNume  = #30#2 * 10; nNume = nNume + #30#3;
      #30#0  = nNume;

      #30#26 = #30#26 + 1;
      |SI #30#26 > 99; #30#26 = 0; |FINSI;

      swOperacion = 3;  ||Para Pedir Moneda siempre
      eaMoneda  = #30#28;
      enEmpresa = #30#2;
      enPeriodo = #30#3;
      |SUB_EJECUTA_NP "camoneda";
      #30#28 = eaMoneda;

      |ABRE #114;
      #114#0 = #30#2;
      #114#1 = 1;
      |LEE 040#114];
      |SI FSalida ! 0;  |O #114#0 ! #30#2;
          |DDEFECTO #114;
      |FINSI;
      |CIERRA #114;

      |SI #30#26 ] 80;
          nNume = 1900 + #30#26;
      |SINO;
          nNume = 2000 + #30#26;
      |FINSI;

      aAlfa1 = #30#1;     |QBF aAlfa1;
      aAlfa2 = #114#8;   |QBF aAlfa2;
      aAlfa3 = #114#10;  |QBF aAlfa3;
      aAlfa =  "[" + (("00000" + #30#2) % -105) + "/" + nNume + "] " + aAlfa1 + " " + aAlfa2 + " " + aAlfa3;

      #30#29 = aAlfa;
      #30#34 = "01.01.0000";

      |GRABA 020#30n;
   |FINSI;
   nEjerActual = #0#32;  ||

   num_por = #30#2;
   num_per = #30#3;
   any_por = #30#26; || Datos para la Portada

   |ABRE #400;
   |DDEFECTO #400;
   #400#0 = #30#2;
   #400#1 = #30#3;
   |LEE 101#400=;
   |SI FSalida = 0;
       #400#2 = eNum2008;
       fFecha = ~;
       aAlfa1 = Usuario % 810;
       #400#6 = fFecha;
       #400#7 = aAlfa1;
       |GRABA 020#400a;
   |SINO;
       #400#0 = #30#2;
       #400#1 = #30#3;
       #400#2 = eNum2008;
       #400#3 = #330#2;
       #400#4 = #330#3;
       fFecha = ~;
       aAlfa1 = Usuario % 810;
       #400#6 = fFecha;
       #400#7 = aAlfa1;
       |GRABA 020#400n;
   |FINSI;
   |LIBERA #400;
   |CIERRA #400;
||  ------ CALCULOS DE CREACION DE LA EMPRESA

   |DFICB aPath;
   aAlfa2 = #30#0;  aAlfa2 = "000000" + aAlfa2;  aAlfa2 = aAlfa2 % -106;
   fich_alta = aPath + aAlfa2;
   |SI nEstado ! 0;    ||Si ya Existe, no crea directorio
       |MKDIR fich_alta;
   |FINSI;
   fich_alta = fich_alta + "/";

   aAlfa2 = #330#0;  aAlfa2 = "000000" + aAlfa2;  aAlfa2 = aAlfa2 % -106;
   fich_copi = aPath + aAlfa2 + "/";
   |PUSHV 2101 2380;
   |HAZ copia_maes;
   |SI eSwC2008 = 1;
       nCamp5 = 1; |SI #0#34 = 1; nCamp5 = 2; |FINSI;
       |HAZ creacion_m;   || .... Copia las estructuras
   |FINSI;

   |HAZ acerar4;
   |HAZ pone0_maes;
   |HAZ pone0_tesfin;

   |LIBERA #330;
   |POPV;
   |SI eSwC2008 = 1;
       eNum2208   = 0;
       eNum2008   = #0#34;
       enEmpreOri = #30#2;
       enPerioOri = #0#2;
       |PINTA 2211, " ADAPTANDO PLAN CONTABLE 2008. Cuenta:                 ";
       |SUB_EJECUTA_NP "ca8zcamb";
   |FINSI;

   |HAZ Cartera;
|FPRC;

|DEFBUCLE LasCreo;
 #330#1002;
 ;
 #0#1, #0#2;
 #0#3, #0#2;
 e;
 NULL, LasCreo;
|FIN;

|| ::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
|CALCULO VoyaAbrir; |TIPO 3;

  |SI #0#30 = "N";
      |ACABA;
  |FINSI;

  |CONFI "    SAbrir las Empresa Contables Seleccionadas";
  |SI FSalida ! 0;
      |ACABA;
  |FINSI;

  eSwC2008    = 0;
  nEjerActual = #0#32;
  |SI nEjerActual ] 2008; |Y #0#31 < 2008;  eSwC2008 = 1; |FINSI;

  |DBASE aBase;
  aDef = aBase + "def/canempre.mas";
  |CARGA_DEF aDef, canempr@;
  |SI FSalida ! 0;
      |MENAV "    Error cargando fichero de Empresas.";
      |ACABA;
  |FINSI;

  aQueQuiero = "|NC";
  aNumCampos = #30#aQueQuiero#;
  nNumCampos = aNumCampos;
  nNumCampos = nNumCampos - 1;

  |BLIN 21;
  |BLIN 22;
  |BLIN 23;
  |CUADRO 2101 2480;
  |ATRI P;
  |PINTA 2303, "Empresa: ";
  |PINTA 2364, "Ejercicio: ";
  |ATRI 0;

  |SI #0#0 = "S";
      |BUCLE LasCreo;
  |SINO;
      |ABRE #330;
      |PARA nPara1 = 5; |SI nPara1 [ 27; |HACIENDO nPara1 = nPara1 + 2;
            |SI #0nPara1 ! 0;
                #330#2 = #0nPara1;
                #330#3 = #0#2;
                |LEE 011#330=;
                |SI FSalida = 0;
                    |HAZ LasCreo;
                |FINSI;
            |FINSI;
      |SIGUE;
      |CIERRA #330;
   |FINSI;
   enEjercicio = #0#32;
   |SUB_EJECUTA "caw00010;crea";

   |DESCARGA_DEF #canempr@;
|FCAL;
||*********************************************************************
