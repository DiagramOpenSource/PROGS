|FICHEROS;
   caextgrt #0;
   camaasil #1;
   camaasic #2;
   cacuenta #3;
   :/modelos/def/dsmom035 #6;
   :/modelos/def/dsmom036 #7;
   caconimp #8;
   ca8m0004 #404;
   canempre #30;
|FIN;

|VARIABLES;
   &entrada = 1;
   men1   = "    NO existe Grupo de Conceptos!";
   men2  = "     Longitud de Cuenta Fuera de Nivel !";
   mensa1 = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
   x = 0;
   y = 0;
   informe = "caextgrt";
   long = "  ";
   cta  = 0;
   num   = 0;
   swdig = 0;
   alfa1 = "";
   nume1 = 0;
   nume2 = 0;

   &cuenta = "";
   &asiento = 0;  || datos del informe por linea
   &debe = 0;
   &haber = 0;
   &saldo = 0;
   &t_debe  = 0; || totales por cuenta
   &t_haber = 0;
   &t_saldo = 0;
   &t_degru = 0;
   &t_hagru = 0;
   &gdebe   = 0;
   &ghaber  = 0;
   &gsaldo  = 0;

   &grup1 = 0;
   &grup2 = "";

   sw_con = 0;          || 1 = EL CONCEPTO ES BUENO
   sw_print = 0;        || 1 = NO DEBO ESCRIBIR
   sw_prim = 0;         || 0 = PRIMERA VEZ
   sw_pagina = 0;

   &swant = 0;
   &sw_inf = 0;
   &sw_cab = 0;
   &pagina = 0;

   nDia8      = 0;
   fFec8      = @;
   nDoc8      = 0;
   nIgual     = 0;
   nAsto8     = 0;
   nDiaAper   = 0;
   contra_ad  = "";
   contra_tad = "";
   contra_ah  = "";
   contra_tah = "";
   nSwAper    = 0;
   contra_d   = "";
   contra_td  = "";
   contra_h   = "";
   contra_th  = "";
|FIN;

|INCLUYE dscont_i;

|| *********************************************************************
||                             COMIENZO
|| *********************************************************************
|PROCESO mayus;
   #caextgrt#Campo# = #caextgrt#Campo# > " ";
   |PINTA #caextgrt#Campo#;
   |CAMPO_MODIFICA #caextgrt#13,1,"S";
   |CAMPO_MODIFICA #caextgrt#14,1,"S";
   |PARA nume1 = 15; |SI nume1 [ 38; |HACIENDO nume1 = nume1 + 1;
      |CAMPO_MODIFICA #caextgrt#nume1#,1,"N";
   |SIGUE;
   |SI #caextgrt#Campo# = "N";
      |CAMPO_MODIFICA #caextgrt#13,1,"N"; #caextgrt#13 =  1; |PINTA #caextgrt#13;
      |CAMPO_MODIFICA #caextgrt#14,1,"N"; #caextgrt#14 = 99; |PINTA #caextgrt#14;
      |PARA nume1 = 15; |SI nume1 [ 38; |HACIENDO nume1 = nume1 + 1;
         |CAMPO_MODIFICA #caextgrt#nume1#,1,"S";
      |SIGUE;
   |FINSI;
|FPRC;

|PROCESO saltagrupo;
   |SI FSalida = 2; |ACABA; |FINSI;
   |CAMPO_MODIFICA #caextgrt#Campo#,0,alfa1;
   |SI alfa1 = "N"; |ACABA; |FINSI;
   |PARA nume1 = Campo + 1; |SI nume1 [ 38; |HACIENDO nume1 = nume1 + 1;
      |CAMPO_MODIFICA #caextgrt#nume1#,1,"S";
   |SIGUE;
   |SI #caextgrt#Campo# = 0;
      |PARA nume1 = Campo + 1; |SI nume1 [ 38; |HACIENDO nume1 = nume1 + 1;
         |CAMPO_MODIFICA #caextgrt#nume1#,1,"N";
         #caextgrt#nume1# = 0; |PINTA #caextgrt#nume1#;
      |SIGUE;
   |FINSI;
|FPRC;

|PROCESO empieza; |TIPO 7;
   #caextgrt#2 = fec1;
   #caextgrt#3 = fec2;
   |PINTA #caextgrt#2;
   |PINTA #caextgrt#3;
|FPRC;

|PROCESO cuenta; |TIPO 6;
     eaCuenta = #caextgrt#Campo#;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #caextgrt#Campo# = eaCuenta;
     |PINTA #caextgrt#Campo#;

   |SI #caextgrt#Campo# = "            ";  |ERROR;  |ACABA;  |FINSI;
   x = Campo + 4;
   cta  = #caextgrt#Campo#;
   long = cta % 0;
   swdig = 0;
   |SI long = #canempre#14; swdig = 1; #caextgrt#x# = 1; |FINSI;
   |SI long = #canempre#15; swdig = 1; #caextgrt#x# = 2; |FINSI;
   |SI long = #canempre#16; swdig = 1; #caextgrt#x# = 3; |FINSI;
   |SI long = #canempre#17; swdig = 1; #caextgrt#x# = 4; |FINSI;
   |SI long = #canempre#18; swdig = 1; #caextgrt#x# = 5; |FINSI;
   |SI long = #canempre#19; swdig = 1; #caextgrt#x# = 6; |FINSI;
   |SI swdig = 0;  |MENAV men2;  |ERROR;  |FINSI;
|FPRC;

|PROCESO fechad;
   |SI #caextgrt#2 < fec1; |O #caextgrt#2 > fec2;
      |MENAV mensa1;
      |ERROR;
      |ACABA;
   |FINSI;
|FPRC;

|PROCESO fechah;
   |SI #caextgrt#3 < fec1; |O #caextgrt#3 > fec2; |O #caextgrt#3 < #caextgrt#2;
      |MENAV mensa1;
      |ERROR;
      |ACABA;
   |FINSI;
|FPRC;

|PROCESO no_dep; |TIPO 7;
   |CAMPO_MODIFICA #caextgrt#6, 1, eaDepar;
   |CAMPO_MODIFICA #caextgrt#7, 1, eaDepar;
|FPRC;

|PROCESO nosubde; |TIPO 7;
   |CAMPO_MODIFICA #caextgrt#87, 1, eaSubde;
   |CAMPO_MODIFICA #caextgrt#88, 1, eaSubde;
|FPRC;

|| **********************************************************************
|PROCESO ParaApertura;
   nDia8  = -1;
   nAsto8 = 0;
   contra_ad  = ""; contra_tad = "";
   contra_ah  = ""; contra_tah = "";

   |SI enEjercicio = 2008;
       |ABRE #8;
       |DDEFECTO #8;
       #8#0 = 1;
       |LEE 001#8=;
       |CIERRA #8;

       |SI #8#11 = "S";
           |ABRE #404;
           |LEE 001#404p;
           |SI FSalida = 0;
               nDia8 = #404#10;
               fFec8 = #404#11;
               nDoc8 = #404#12;
           |FINSI;
           |CIERRA #404;
           |ABRE #2;
           #2#0 = nDia8;
           #2#1 = fFec8;
           #2#2 = nDoc8;
           |LEE 001#2=;
           |SI FSalida = 0;
               contra_ad  = #2#10; contra_tad = "";
               contra_ah  = #2#8;  contra_tah = "";
               |HAZ LeeContra;
           |FINSI;
           |CIERRA #2;
       |FINSI;
   |FINSI;
|FPRC;

|PROCESO LeeContra;
  |ABRE #3;
  |DDEFECTO #3;
  #3#0 = contra_ad;
  |LEE 000#3=;
  contra_tad = #3#2;

  |DDEFECTO #3;
  #3#0 = contra_ah;
  |LEE 000#3=;
  contra_tah = #3#2;

  |CIERRA #3;
|FPRC;
|| **********************************************************************

|PROGRAMA;
   |CLS;
   |CABEZA "Extracto Total Grupo ";
   |HAZ inicio;

   |HAZ ParaApertura;
   FSalida = 1;
   |PARA;|SI FSalida = 1; |HACIENDO;
      sw_prim = 0;
      sw_print = 0;
      |DDEFECTO #0;
      |PINPA #0#0;
      |PINDA #0#0;                    || Pinta los datos por defecto
      |ENDATOS #1#0;                  || Seleccion informe
      |SI FSalida = 0;                || Si el conforme es igua a S
         |HAZ impre;                 || Calculo impresion
         FSalida = 1;                || Vuelve al bucle
      |SINO;
         FSalida = 0;
      |FINSI;
   |SIGUE;
|FPRO;

|| *************************************************************************
||                               MEOLLO
|| *************************************************************************

|PROCESO cabecera;
   #camaasic#0 = #camaasil#0;
   #camaasic#1 = #camaasil#1;
   #camaasic#2 = #camaasil#2;
   |LEE 040#2=;
   |SI FSalida ! 0;
      |DDEFECTO #2;
   |FINSI;
   |SI nSwAper ! 0;
       |SI nSwAper = 1;
           nAsto8 = #2#3 - 1; |SI nAsto8 [ 0; nAsto8 = 1; |FINSI;
           #2#3 = nAsto8;
       |SINO;
           #2#3 = nAsto8 + 1;
       |FINSI;
   |FINSI;

   asiento = #camaasic#3;
|FPRC;

|PROCESO campogrupo;
   nume2 = nume2 + 1;
   |SI nume2 > 24; |ERROR10; |FINSI;
   y = nume2 + 14;
   #caextgrt#y# = #dsmom035#0;
|FPRC;

|DEFBUCLE vergrupo;
   #6#1;
   ;
   #0#13;
   #0#14;
   e;
   NULL, campogrupo;
|FIN;

|PROCESO congru;  || Para sumar en el lugar adecuado
 |PARA nume2 = 15; |SI nume2 [ 38; |HACIENDO nume2 = nume2 + 1;
       |SI #caextgrt#nume2# ! 0;
           #dsmom036#0 = #caextgrt#nume2#;
           #dsmom036#1 = #camaasil#6;  || Concepto apunte
           |LEE 001#7=;
           |SI FSalida = 0;
               sw_con = 1;
               |SI #camaasil#8 = "D";
                   y = nume2 + 24;
               |SINO;
                   y = nume2 + 48;
               |FINSI;
               #caextgrt#y# = #caextgrt#y# + #camaasil#9;
           |FINSI;
       |FINSI;
   |SIGUE;
|FPRC;

|PROCESO leecuen;
   #cacuenta#0 = #camaasil#4;
   #cacuenta#1 = #camaasil#5;
   |LEE 040#3=;
   |SI FSalida ! 0; |DDEFECTO #3; |FINSI;
   sw_pagina = 0;
   sw_print = 0;
   swant = 0;
   t_debe = 0;
   t_haber = 0;
   t_saldo = 0;
   gdebe   = 0;
   ghaber  = 0;
   gsaldo  = 0;
   |PDEFECTO #0,39,87;
|FPRC;

|PROCESO Apertura2008;
   |SALVA_DATOS #1;

   |ABRE #404;
   |DDEFECTO #404;
   #404#10 = nDia8;
   #404#11 = fFec8;
   #404#12 = nDoc8;
   #404#0  = #1#3;
   |LEE 041#404=;
   |SI FSalida ! 0; |O #404#8 ! #1#4; || no existe o no son iguales
       |CIERRA #404;
       |REPON_DATOS #1;
       |HAZ ParaImprimir;
       |ACABA;
   |FINSI;
   || .... Primer Asiento Apertura 2007
   nSwAper   = 1;
   contra_d  = contra_ad;
   contra_td = contra_tad;
   contra_h  = contra_ah;
   contra_th = contra_tah;
   #1#4 = #404#1;
   #1#5 = #404#7;
   |HAZ ParaImprimir;
   |SI #404#8 ! #404#1;
       nSwAper = 2;
       contra_d  = #404#8; contra_td = #404#2;
       contra_h  = #404#8; contra_th = #404#2;
       #1#4 = #404#1;   || 2§ asiento
       #1#5 = #404#7;
       #1#7 = #8#13;
       |SI #404#3 = "D";
           #1#8 = "H";
       |SINO;
           #1#8 = "D";
       |FINSI;
       #1#9 = #404#4 + #404#5;
       |HAZ ParaImprimir;

       contra_d  = #404#1; contra_td = #404#2;
       contra_h  = #404#1; contra_th = #404#2;
       #1#4 = #404#8;   || 2§ asiento
       #1#5 = #404#9;
       #1#7 = #8#13;
       #1#8 = #404#3;
       #1#9 = #404#4 + #404#5;
       |HAZ ParaImprimir;
   |FINSI;
   |CIERRA #404;
   |REPON_DATOS #1;
|FPRC;

|PROCESO buclepan;
   |SI sw_prim = 0;
      cuenta = #camaasil#4;
      sw_prim  = 1;
      |HAZ leecuen;
   |FINSI;

   |SI cuenta ! #camaasil#4;
      |SI sw_print = 1;
         |HAZ impr_totales;
         |PIEINF;
      |FINSI;
      cuenta = #camaasil#4;
      |HAZ leecuen;
   |FINSI;

   nSwAper = 0;
   |SI nDia8 ! -1;
       |SI #1#0 = nDia8; |Y #1#1 = fFec8; |Y #1#2 = nDoc8;
           |HAZ Apertura2008;
           nDiaAper = 1;
           |ACABA;           ||este asiento se trata de forma diferente
       |FINSI;
   |FINSI;

   |HAZ ParaImprimir;
|FPRC;

|PROCESO ParaImprimir;
   |SI #camaasil#8 = "D";
      debe = #camaasil#9;
      haber = 0;
   |SINO;
      debe = 0;
      haber = #camaasil#9;
   |FINSI;
   saldo = t_saldo + (debe - haber);

   |SI #camaasil#1 < #caextgrt#2;
      swant = 1;
      |VETE suma_totales;
   |FINSI;

   |SI #camaasil#1 ] #caextgrt#2; |Y #camaasil#1 [ #caextgrt#3;
      |HAZ cabecera;
      sw_inf = 1;
      |PRINT;
      sw_print = 1;
      swant = 1;
   |FINSI;

   |ET suma_totales;
   t_debe  = t_debe  + debe;
   t_haber = t_haber + haber;
   t_saldo = t_debe - t_haber;
   |HAZ congru; || Para sumarlo en la columna adecuada
|FPRC;

|DEFBUCLE caextrim0;
   #1#3;
   0, 21,28;
   #0#0,  fec1, 000000, 0001, #0#8, #0#6, #0#87;
   #0#1,  #0#3, 999999, 9999, #0#9, #0#7, #0#88;
   e;
   NULL, buclepan;
|FIN;

|PROCESO impre;
   |IMPRE 0;
   |SI FSalida ! 0; |ACABA; |FINSI;

   |INFOR "caextgrt";

   |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

   nume2 = 0;
   |BUCLE vergrupo; || Cargar grupos en Pantalla
   |ABRE #2;
   |ABRE #3;
   |ABRE #6;
   |ABRE #7;
   |BUCLE caextrim0;
   |CIERRA #2;
   |CIERRA #3;
   |SI sw_print = 1;
      |HAZ impr_totales;
      |PIEINF;
   |FINSI;
   |CIERRA #2;
   |CIERRA #3;
   |CIERRA #6;
   |CIERRA #7;
   |FININF;
   |FINIMP;
|FPRC;

|PROCESO &c_pagina;
   |SI sw_pagina = 0;
      pagina = 1;
      sw_pagina = 1;
   |SINO;
      pagina = pagina + 1;
   |FINSI;
|FPRC;

|PROCESO impr_totales;
   sw_inf = 2; || linea totales
   |PRINT;
   gdebe = 0; ghaber = 0;
   |PARA nume2 = 15; |SI nume2 [ 38; |HACIENDO nume2 = nume2 + 1;

      |SI #caextgrt#nume2# = 0; |VETE fintgru; |FINSI;
      #dsmom035#0 = #caextgrt#nume2#;
      |LEE 001#6=;
      |SI FSalida ! 0; |VETE fintgru; |FINSI;
      y = nume2 + 24;
      x = nume2 + 48;
      grup1 = #dsmom035#0;
      grup2 = #dsmom035#1;
      t_degru = #caextgrt#y#;
      t_hagru = #caextgrt#x#;
      |SI t_degru ! 0; |O t_hagru ! 0;
         sw_inf = 3;
         |PRINT;
         gdebe  = gdebe  + t_degru;
         ghaber = ghaber + t_hagru;
         gsaldo = gdebe - ghaber;
      |FINSI;
      |ET fintgru;
   |SIGUE;
|FPRC;
