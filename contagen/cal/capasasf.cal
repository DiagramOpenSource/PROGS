|FICHEROS;
     capasasf #0;
     camautoc #1;
     camautol #2;
     camautti #3;

     imautoc@ #11;
     imautol@ #12;
     imauttt@ #13;

     canempre #30;
     caw00026 #999;
|FIN;

|VARIABLES;
   &enEmpresaV = 0;
   &enPeriodoV = 0;

   nPara1       = 0;
   nCampo       = 0;
   aAlfa1       = "";
   aAlfa2       = "";
   aDef         = "";
   aAntPath     = "";
   aPathDestino = "";
   aPathOrigen  = "";
   nSwOp        = 0;
   nAnter       = 0;
   nNuevo       = 0;

   aFichero   = "";
   aNumCampos = "";
   aQueQuiero = "";
   nNumCampos = 0;
   aParam     = "";
   nEstado    = "";
   nTipo      = 0;
   nTipoA     = 0;
|FIN;

|INCLUYE dscont_i;

|PROCESO CambioTipo;
 #1#0 = #999#2;
 |LEE 141#1=;
 |SI FSalida = 0;
     #1#11 = nTipo;
     |GRABA 020#1a;
 |FINSI;
 |LIBERA #1;
|FPRC;

|DEFBUCLE CambioTipo;
   #999#1;
   3;
   "F", 00001, nTipoA;
   "F", 99999, nTipoA;
   ;
   NULL, CambioTipo;
|FIN;

|PROCESO LasLineas;
 #2#0  = nNuevo;
 #2#1  = #12#1;
 #2#2  = #12#2;
 #2#3  = #12#3;
 #2#4  = #12#4;
 #2#5  = #12#5;
 #2#6  = #12#6;
 #2#7  = #12#7;
 #2#8  = #12#8;
 |GRABA 020#2n;
|FPRC;

|DEFBUCLE LasLineas;
  #12#1002;
  ;
  nAnter, 0001;
  nAnter, 9999;
  e;
  NULL, LasLineas;
|FIN;

|PROCESO AltaAsiento;

  |DDEFECTO #1;
  #1#0 = nNuevo;
  |PARA nCampo = 1;  |SI nCampo [ 11;  |HACIENDO nCampo = nCampo + 1;
        #1nCampo = #11nCampo;
  |SIGUE;
  |SI nNumCampos = 26; #1#26 = #11#12; |FINSI; || Incremento

  |GRABA 020#1n;
  |LIBERA #1;

  |BUCLE LasLineas;

  #999#0 = "F";
  #999#1 = nAnter;
  #999#2 = nNuevo;
  #999#3 = #11#11;
  |GRABA 040#999n;
|FPRC;

|PROCESO BuscaNuevo;
  nNuevo = 0;
  #999#0 = "F";
  #999#1 = nAnter;
  |LEE 041#999=;
  |SI FSalida = 0;
      |ACABA;
  |FINSI;

  |LEE 041#1u;
  |SI FSalida = 0;
      nNuevo = #1#0;
  |FINSI;
  nNuevo = nNuevo + 1;
|FPRC;


|PROCESO LeeCamautoc;
  nNuevo = 0;
  nAnter = #11#0;

  #1#0 = #11#0;
  |LEE 141#1=;
  nEstado = FSalida;
  |SI nEstado ! 0;         ||No existe lo crea
      nNuevo = #11#0;
      |HAZ AltaAsiento;
  |FINSI;
  |SI nEstado = 0;         ||Si que existe, renumera en la segunda vuelta
      |SI nSwOp = 1;
          |HAZ BuscaNuevo;
          |SI nNuevo ! 0; |HAZ AltaAsiento; |FINSI;
      |FINSI;
  |FINSI;
  |LIBERA #11;
|FPRC;

|DEFBUCLE LeeCabsAsto;
     #11#1001;
     ;
     00001;
     99999;
     e;
     NULL, LeeCamautoc;
|FIN;

|PROCESO AltaTipo;
  |DDEFECTO #3;
  #3#0 = nTipo;
  #3#1 = #13#1;
  |GRABA 020#3n;
  |LIBERA #3;

  #999#0 = "T";
  #999#1 = nTipoA;
  #999#2 = nTipo;
  #999#3 = 0;
  |GRABA 040#999n;
|FPRC;

|PROCESO BuscaNuevoTipo;
  nTipo = 0;
  #999#0 = "T";
  #999#1 = nTipoA;
  |LEE 041#999=;
  |SI FSalida = 0;
      |ACABA;
  |FINSI;

  |LEE 041#3u;
  |SI FSalida = 0;
      nTipo = #3#0;
  |FINSI;
  nTipo = nTipo + 1;
|FPRC;

|PROCESO LeeCamautti;
  nTipoA = #13#0;
  nTipo  = 0;
  #3#0 = #13#0;
  |LEE 141#3=;
  |SI FSalida ! 0;         ||No existe lo crea
      nTipo = #13#0;
      |HAZ AltaTipo;
  |SINO;
      |SI nSwOp = 1;
          |HAZ BuscaNuevoTipo;
          |SI nTipo ! 0;
              |HAZ AltaTipo;
              |CIERRA #999;
              |BUCLE CambioTipo;
              |ABRE #999;
          |FINSI;
      |FINSI;
  |FINSI;
  |LIBERA #13;
|FPRC;

|DEFBUCLE LeeTipoAsto;
     #13#1001;
     ;
     01;
     99;
     e;
     NULL, LeeCamautti;
|FIN;

|PROCESO RecogeAstos;

  aFichero = "x" + Usuario;
  |NOME_DAT #999 aFichero;
  |DELINDEX #999;

  aQueQuiero = "|NC";
  aNumCampos = #camautoc#aQueQuiero#;
  nNumCampos = aNumCampos;
  nNumCampos = nNumCampos - 1;

  |ABRE #999;
  |ABRE #1;
  |ABRE #2;

  nSwOp = 0; |BUCLE LeeCabsAsto;
  nSwOp = 1; |BUCLE LeeCabsAsto;

  |CIERRA #2;

  |ABRE #3;
  nSwOp = 0; |BUCLE LeeTipoAsto;
  nSwOp = 1; |BUCLE LeeTipoAsto;
  |CIERRA #3;

  |CIERRA #1;

  |CIERRA #999;
|FPRC;

|PROGRAMA;

     |HAZ Monedas;
     aParam = PARAMETRO; |QBF aParam;
     |SI aParam ! "";
         aAlfa1 = aParam % -101;
         |SI aAlfa1 ! "/";
             aAntPath = aParam + "/agicont/";
         |SINO;
             aAntPath = aParam + "agicont/";
         |FINSI;
         #0#1 = enEmpresa;
         #0#2 = enPeriodo;

         |DBASE aBase; |QBF aBase;
         aAlfa1 = (("00000" + #0#1) % -105) + #0#2;
         aPathDestino = aBase + "fich/" + aAlfa1 + "/";

         |PATH_DAT #1  aPathDestino;
         |PATH_DAT #2  aPathDestino;
         |PATH_DAT #3  aPathDestino;

     |SINO;

         |CLS;

         |DBASS aBase; |QBF aBase;
         aAntPath = aBase + "agicont/";
         #0#0 = aAntPath;
         #0#1 = #48#2;
         #0#2 = #48#3;

         |PINPA #0#0;
         |PINDA #0#0;
         |ENDATOS #1#0;
         |SI FSalida ! 0; |ACABA; |FINSI;

         aAntPath = #0#0; |QBF aAlfa1;
         aAlfa1 = aAntPath % -101;
         |SI aAlfa1 ! "/";
             aAntPath = aAntPath + "/";
         |FINSI;
     |FINSI;

     |ABRE #30;
     #30#2 = #0#1;
     #30#3 = #0#2;
     |LEE 001#30=;
     |SI FSalida ! 0;
         |DDEFECTO #30;
     |FINSI;
     |CIERRA #30;
     |SI enEmpresaV = 0;
         enEmpresaV = #0#1;
         enPeriodoV = #0#2;
     |FINSI;

     |SI #30#26 < 80;
         enEjercicio = 2000 + #30#26;
     |SINO;
         enEjercicio = 1900 + #30#26;
     |FINSI;

     aDef= aAntPath + "def/camautoc.mas";
     |CARGA_DEF aDef, imautoc@;
     |SI FSalida = 0;
         aDef = aAntPath + "def/camautol.mas";
         |CARGA_DEF aDef, imautol@;
         |SI FSalida = 0;
             aDef = aAntPath + "def/camautti.mas";
             |CARGA_DEF aDef, imauttt@;
             |SI FSalida = 0;

                 aAlfa1 = (("00000" + enEmpresaV) % -105) + enPeriodoV;
                 aPathOrigen = aAntPath + "fich/" + aAlfa1 + "/";
                 |PATH_DAT #11 aPathOrigen;
                 |PATH_DAT #12 aPathOrigen;
                 |PATH_DAT #13 aPathOrigen;

                 |MENSA "    Copiando asientos fijos";

                 |HAZ RecogeAstos;

                 |DESCARGA_DEF #imauttt@;
             |FINSI;
             |DESCARGA_DEF #imautol@;
         |FINSI;
         |DESCARGA_DEF #imautoc@;
     |FINSI;
|FPRO;
