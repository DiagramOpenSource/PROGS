|FICHEROS;
   caz00033 #0;

   caclipro #1;
   cacuenta #2;

   espejo@  #901;

   canempre #30;
   caselem1 #998;
|FIN;

|VARIABLES;
   &fich_copi = "";
   aParam    = "";
   aAlfa1    = "";
   aAlfa2    = "";
   aAlfa3    = "";
   nNume1    = 0;

   desde = "";
   aDCuenta = "";
   aHCuenta = "";
   aDNombre = "";
   aHNombre = "";

   aDef = "";
   aMas = "";
   &r_emp = 0;
   &r_per = 0;
   swSelec = 0;

   aQueQuiero = "";
   aNumCampos = "";
   nNumCampos = 0;
   nPara1     = 0;
   nEstado    = 0;
   nMaxi      = 0;
|FIN;

|INCLUYE dscont_i;

|CALCULO LaSeleccion; |TIPO 0;
 |C_M #0#1, 1,"S"; |C_M #0#2, 1,"S";
 |C_M #0#3, 1,"S"; |C_M #0#4, 1,"S";
 |C_M #0#5, 1,"S"; |C_M #0#6, 1,"S";
 |C_M #0#7, 1,"S"; |C_M #0#8, 1,"S";
 |C_M #0#9, 1,"S"; |C_M #0#10,1,"S";
 |C_M #0#11,1,"S"; |C_M #0#12,1,"S";
 |C_M #0#13,1,"S"; |C_M #0#14,1,"S";
 |SI #0#0 = "C";
      |C_M #0#7, 1,"N"; |C_M #0#8, 1,"N";
      |C_M #0#9, 1,"N"; |C_M #0#10,1,"N";
      |C_M #0#11,1,"N"; |C_M #0#12,1,"N";
      |C_M #0#13,1,"N"; |C_M #0#14,1,"N";
 |FINSI;
 |SI #0#0 = "P";
      |C_M #0#1, 1,"N"; |C_M #0#2, 1,"N";
      |C_M #0#3, 1,"N"; |C_M #0#4, 1,"N";
      |C_M #0#5, 1,"N"; |C_M #0#6, 1,"N";
 |FINSI;
|FCAL;

|CALCULO ElPtec; |TIPO 7;
 |C_M #0Campo,0,aAlfa1;
 |SI aAlfa1 = "S";
     |PTEC 816;
 |FINSI;
|FCAL;

|CALCULO Hasta; |TIPO 0;
 |C_M #0Campo,0,aAlfa1;
 |SI aAlfa1 = "S";
     nNume1 = Campo - 1;
     |SI #0Campo < #0nNume1;
         |MENAV "    Error!. Cuenta Hasta Menor que la Cuenta Desde";
         |ERROR;
     |FINSI;
 |FINSI;
|FCAL;

|CALCULO cuenta;|TIPO 6;
     |C_M #0Campo, 0, aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta; |PINTA #0Campo;
|FCAL;

|PROCESO esbuena;
   |SI FSalida = 9;  |ACABA;  |FINSI;
   |ABRE #30;
   #canempre#2 = #0#15;
   #canempre#3 = 0;
   |LEE 011#30];
   |SI FSalida ! 0;  |O #canempre#2 ! #0#15;
      |MENAV "     NO Existe la empresa Contable";
      |ERROR;
   |FINSI;
   |CIERRA #30;
|FPRC;

|PROCESO consulta;
   |SI FSalida ! 9;  |ACABA;  |FINSI;

   r_emp = 0;
   |SUB_EJECUTA "caconemp";
   |ERROR;
   #0#15 = r_emp;
   #0#16 = r_per;
   |PTEC 802;
   |PTEC 802;
|FPRC;

|PROCESO CalMaxi;
   |SI #canempre#13 = 1; nMaxi = #canempre#14; |FINSI;
   |SI #canempre#13 = 2; nMaxi = #canempre#15; |FINSI;
   |SI #canempre#13 = 3; nMaxi = #canempre#16; |FINSI;
   |SI #canempre#13 = 4; nMaxi = #canempre#17; |FINSI;
   |SI #canempre#13 = 5; nMaxi = #canempre#18; |FINSI;
   |SI #canempre#13 = 6; nMaxi = #canempre#19; |FINSI;
|FPRC;

|PROCESO esbuena1;
   |SI FSalida = 2; |ACABA; |FINSI;
   |ABRE #30;
   #canempre#2 = #0#15;
   #canempre#3 = #0#16;
   |LEE 011#30=;
   |SI FSalida ! 0;
       |MENAV "     NO Existe Periodo en la empresa Contable";
       |CIERRA #30;
       |ERROR;
       |ACABA;
   |FINSI;

   |HAZ CalMaxi;
   |SI nMaxi ! MaximoDigitos;
       |MENAV "     NO CORRESPONDEN LOS NIVELES";
       |CIERRA #30;
       |ERROR;
       |ACABA;
   |FINSI;

   #0#17 = #canempre#1; |PINTA #0#17;
   #canempre#2 = empre;
   #canempre#3 = ejerc;
   |LEE 011#30=;
   |CIERRA #30;
|FPRC;

|| **********************************************************************
|PROCESO Actualizar;
   swSelec = 0;
   |SI desde = "C";
       |SI #0#2 ! doce;
           |SI #901#10 ] #0#1; |Y #901#10 [ #0#2; swSelec = 1; |FINSI;
       |FINSI;
       |SI #0#4 ! doce;
           |SI #901#10 ] #0#3; |Y #901#10 [ #0#4; swSelec = 1; |FINSI;
       |FINSI;
       |SI #0#6 ! doce;
           |SI #901#10 ] #0#5; |Y #901#10 [ #0#6; swSelec = 1; |FINSI;
       |FINSI;
   |FINSI;
   |SI desde = "P";
       |SI #0#8 ! doce;
           |SI #901#10 ] #0#7; |Y #901#10 [ #0#8; swSelec = 1; |FINSI;
       |FINSI;
       |SI #0#10 ! doce;
           |SI #901#10 ] #0#9; |Y #901#10 [ #0#10; swSelec = 1; |FINSI;
       |FINSI;
       |SI #0#12 ! doce;
           |SI #901#10 ] #0#11; |Y #901#10 [ #0#12; swSelec = 1; |FINSI;
       |FINSI;
       |SI #0#14 ! doce;
           |SI #901#10 ] #0#13; |Y #901#10 [ #0#14; swSelec = 1; |FINSI;
       |FINSI;
   |FINSI;
   |SI swSelec = 0; |ACABA; |FINSI;

   |DDEFECTO #1;
   #1#23 = #901#23;
   #1#10 = #901#10;
   |LEE 101#1=;
   nEstado = FSalida;
   |SI nEstado = 0; |Y #0#18 = "N";
       |LIBERA #1;
       |ACABA;
   |FINSI;
   aAlfa1 = #1#1;

   |PARA nPara1 = 0; |SI nPara1 [ nNumCampos; |HACIENDO nPara1 = nPara1 + 1;
         #1nPara1 = #901nPara1;
   |SIGUE;
   |SI nEstado = 0; |GRABA 020#1a; |FINSI;
   |SI nEstado ! 0; |GRABA 020#1c; |FINSI;
   |LIBERA #1;

   |SI aAlfa1 ! #1#1;
       #2#0 = #1#10;
       |LEE 101#2=;
       |SI FSalida = 0;
           #2#2 = #1#1;
           |GRABA 020#2a;
       |FINSI;
       |LIBERA #2;
   |FINSI;

   |PINTA 2202, "Cliente / Proveedor: ";
   |PINTA 2223, #1#10;
   |PINTA 2236, #1#1;
|FPRC;

|DEFBUCLE Lee_codigo;
   #901#1002;
   ;
   desde, aDCuenta;
   desde, aHCuenta;
   e;
   NULL, Actualizar;
|FIN;

|PROCESO Leeimprime;
 |ABRE #1;
 |ABRE #2;
 |BUCLE Lee_codigo;
 |CIERRA #2;
 |CIERRA #1;
|FPRC;

|PROCESO HazTodo;
   |SI #0#0 ! "P";
     desde = "C";

     aDCuenta = #0#1;
     |SI aDCuenta > #0#3; |Y #0#4 ! doce; aDCuenta = #0#3; |FINSI;
     |SI aDCuenta > #0#5; |Y #0#6 ! doce; aDCuenta = #0#5; |FINSI;
     aHCuenta = #0#2;
     |SI aHCuenta < #0#4;                 aHCuenta = #0#4; |FINSI;
     |SI aHCuenta < #0#6;                 aHCuenta = #0#6; |FINSI;

     |HAZ Leeimprime;
 |FINSI;

 |SI #0#0 ! "C";
     desde = "P";

     aDCuenta = #0#7;
     |SI aDCuenta > #0#9;  |Y #0#10 ! doce; aDCuenta = #0#9;  |FINSI;
     |SI aDCuenta > #0#11; |Y #0#12 ! doce; aDCuenta = #0#11; |FINSI;
     |SI aDCuenta > #0#13; |Y #0#14 ! doce; aDCuenta = #0#13; |FINSI;
     aHCuenta = #0#8;
     |SI aHCuenta < #0#10;                 aHCuenta = #0#10; |FINSI;
     |SI aHCuenta < #0#12;                 aHCuenta = #0#12; |FINSI;
     |SI aHCuenta < #0#14;                 aHCuenta = #0#14; |FINSI;

     |HAZ Leeimprime;
 |FINSI;

|FPRC;

|CALCULO impre;|TIPO 3;

   |SI aParam = "";
       |DFICB aAlfa1; |QBF aAlfa1;
       aAlfa2 = #0#15; aAlfa2 = ("00000" + aAlfa2) % -105;
       aAlfa3 = #0#16; aAlfa3 = ("0"     + aAlfa3) % -101;
       fich_copi = aAlfa1 + aAlfa2 + aAlfa3 + "/";
   |FINSI;

   |DBASE aMas;
   aDef = aMas + "def/caclipro";
   |CARGA_DEF aDef, espejo@;
   |SI FSalida !  0;
       |MENAV "    Error al Carga fichero de Cuentas";
       aDef = "";
       |ACABA;
   |FINSI;
   |PATH_DAT #901 fich_copi;

   aQueQuiero = "|NC";
   aNumCampos = #1#aQueQuiero#;
   nNumCampos = aNumCampos;
   nNumCampos = nNumCampos - 1;

   aDNombre = " " * 36; aHNombre = "z" * 36;

   |SI #48#27 = "S"; |Y aParam = "";
       |HAZ MultiEmpresa;
   |SINO;
       |HAZ HazTodo;
   |FINSI;

   |DESCARGA_DEF #901;
|FCAL;


|PROGRAMA;

  aParam = PARAMETRO; |QBF aParam;

  |SI aParam = "";
      |CLS;
      |CABEZA "Actualizar Clientes/Proveedores";

      |HAZ inicio;
      |DDEFECTO #0;
      #0#15 = enEmpresa;
      #0#16 = enPeriodo - 1; |SI #0#16 < 0; #0#16 = 9; |FINSI;
      #0#17 = eaNombreEmp;
      |DFICE fich_alta;
      |PINPA #0#0;
      |PINDA #0#0;
      |ENDATOS #1#0;

  |SINO;

      |DDEFECTO #0;
      aAlfa1 = fich_copi;
      aAlfa1 = aAlfa1 % -107;
      aAlfa2 = aAlfa1 % 105; #0#15 = aAlfa2;
      aAlfa2 = aAlfa1 % 601; #0#16 = aAlfa2;

      #0#18 = "S";
      |PATH_DAT #1 fich_alta;
      |PATH_DAT #2 fich_alta;
      |ABRE #30;
      #canempre#2 = #0#15;
      #canempre#3 = #0#16;
      |LEE 011#30=;
      |SI FSalida ! 0;
          |CIERRA #30;
          |ACABA;
      |FINSI;
      |HAZ CalMaxi;

      |HAZ impre;
  |FINSI;
|FPRO;

|| *************************************
|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   |SI nMaxi ! MaximoDigitos; |ACABA; |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;
   |PATH_DAT #1  dirfich0;
   |PATH_DAT #2  dirfich0;
   fich_alta = dirfich0;

   |ATRI N; |CUADRO 1508 2055;
   |ATRI R; |PINTA 1609, "Empresa: ";      |ATRI 0;
   |PINTA 1618, #30#2; |PINTA 1709, #30#1; |ATRI 0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 001;
 999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;
