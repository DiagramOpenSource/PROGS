|FICHEROS;
   cajainli #0;
   cajainca #1;
   cafantas #2;
   :/modelos/def/dsmom030 #3;
   cacuenta #4;
   cacuebal #5;
   cajasald #6;
   caivaasi #200;
   caclipro #18;

   dscam043@ #1000;
   dscam003@ #1001;
   dscam004@ #1002;
   dscam051@ #1003;
   dscamrie@ #1004;
   dscam008@ #1005;
   dscam009@ #1006;
   dscam070@ #1007;

   caw00042 #998,MANTE;
   espejo@  #999;
|FIN;

|VARIABLES;
   &enDiarioNivel = -1;
   &entrada = 1;
   &esalta = 0;
   &adocum = 0;
   &adiari = 0;
   &afecha = @;
   &dpta   = "";
   &sdta   = "";
   &cpartida = "";
   &dpartida = 0;
   &avisos = "";
   &aRPath = "";
   &aRNome = "";
   lcuenta = "";
   ldigito = 0;
   signo = "";
   xx = 0;

   nSwError = 0;
   nSwErr   = 0;

   nCalc    = 0;
   nSwLuxe  = 0;

   nModoEntrada = 0;
   nFlag        = 0;

   fFecha  = @;
   fFecha1 = @;
   nTotal1 = 0;

   men2  = "     Longitud de Cuenta Fuera de Nivel !";
   men3  = "      Esta Cuenta NO tiene Pase directo";
   long = "  ";
   sw = 0;
   ini = 0;
   fecalf = "";
   mes = "";
   mesfec = 0;
   mesac = 0;
   a = 0;
   b = 0;
   c = 0;

   nHandle = 0;

   &cuant = "";
   &diant = 0;
   &coant = 0;
   &deant = "";
   &dhant = "";
   &dpant = "";
   &sdant = "";
   &imant = 0;

   alfa1 = "";
   alfa2 = "";
   alfa3 = "";
   alfa4 = "";
   nume1 = 0;
   nume2 = 0;
   nume3 = 0;
   nume4 = 0;

   aAlfa  = "";
   aAlfa1 = "";
   aAlfa2 = "";

   &ext1 = "";
   &ext2 = "";
   &ext3 = "";
   &ext4 = "";
   &ext5 = "";
   &inkey = 0;
   &sit1 = 0;
   &sit2 = @;
   &sit3 = 0;
   &sit4 = 0;
   &sit5 = 0;
   &sit6 = 0;

   programa = "";
   importe = 0;
   a_debe = 0;
   a_haber = 0;
   SwSoloUno = 0;
   aBlancs = "";
   nNumRef = 0;

   &enDiarioDentro    = 0;
   &efFechaDentro     = @;
   &enDocumentoDentro = 0;
   &nSwOpera = 0;
   &snFiltr = "";
   &snFilAc = "";
   &snFilBa = "";
   &snFiltrA= "";

   nDImporte = 0;
   nHImporte = 0;
   aDef      = "";
   aMas      = "";
   nNumero   = 0;
   aFichero  = "";
   nConNum   = 0;
   nAlta     = 0;
|FIN;

|INCLUYE dscont_i;

***************************************************************************
LLAMADAS DESDE CAMPOS
***************************************************************************

|| ... Campo Cuenta
|PROCESO cuenta; |TIPO 6;                         || desde campo 'cuenta'
   salidas = FSalida;
   aAlfa1  = #cajainli#4; |QBF aAlfa1;
   |SI salidas = 15; |O aAlfa1 = "";
      #cajainli#4 = cuant;
      #cajainli#5 = diant;
      |PINTA #cajainli#4;
      salidas = 0;
   |FINSI;
   |SI salidas ] 10; |Y salidas [ 17; |ACABA; |FINSI;

   eaCuenta = #cajainli#Campo#;
   salidas  = FSalida;
   |HAZ FiltraPuntos;
   #cajainli#Campo# = eaCuenta;
   |PINTA #cajainli#Campo#;

   |SI #cajainli#Campo# ! doce;
       enDiarioNivel = #cajainli#0;
       eaCuenta = #cajainli#4;
       |HAZ SacaNivel;
       #cajainli#5 = enNivel;
   |SINO;
      |MENAV "    ATENCION!!! Entrada erronea ...";
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO cuefin; |TIPO 0;                    || desde campo 'cuenta'
   |SI salidas ] 9; |Y salidas [ 17;
      inkey = salidas;
      |HAZ funcion2;
      |ACABA;
   |FINSI;

   |SI #cacuenta#3 = "N";
       |MENAV men3;
       |ERROR;
       |ACABA;
   |FINSI;
   |ATRI I;
   |PINTA 0740, #cacuenta#51;
   |PINTA 0754, #cacuenta#52;
   |PINTA 0768, #cacuenta#53;
   |PINTA 0639, #cacuenta#2;
   |ATRI 0;

   nSwOpera = 1;
   eaCuenta = #cacuenta#0;
   |SUB_EJECUTA_NP "caz00017";
|FPRC;

|| ==========================================================================
|PROCESO c_concepto; |TIPO 7;                || desce campo 'concepto'
   #cajainli#Campo# = coant;
   |PINTA #cajainli#Campo#;
|FPRC;

|PROCESO concepto; |TIPO 0;                  || desde campo 'concepto'

   |SI FSalida = 10;
       enConcepto = #cajainli#6;
       |HAZ VerConcepto;
       #cajainli#6 = enConcepto; |PINTA #cajainli#6;
   |FINSI;
   enFilConc = 1;
   eaCuenta   = #cajainli#4;
   enConcepto = #cajainli#6;
   |HAZ FiltroConcepto;  |SI eswLect ! 0; |ACABA; |FINSI;

   xx = (FEntrada / 100) ! 0;
   |SI #cajainli#6 = coant; |Y xx ! 1;
      #cajainli#7 = deant;
   |SINO;
      #cajainli#7 = #dsmom030#1;
   |FINSI;
   |PINTA #cajainli#7;
|FPRC;

|PROCESO lee_clien;
     |ABRE #18;
     #18#23 = "C";
     #18#10 = eaCuenta;
     |LEE 041#18=;
     |SI FSalida = 0;
         nAlta = 1;
     |SINO;
         |DDEFECTO #18;
         #18#23 = "P";
         #18#10 = eaCuenta;
         |LEE 041#18=;
         |SI FSalida = 0;
             nAlta = 1;
         |FINSI;
     |FINSI;
     |CIERRA #18;
|FPRC;

|PROCESO AntesPara347; |TIPO 7;
     |C_V #0#19, 0, aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     nAlta = 0;
     |SI #dsmom030#7 = "S";
         eaCuenta = #cajainli#4;
         |HAZ lee_clien;
     |FINSI;

     |SI nAlta = 0;
         #cajainli#19 = 0;
         #cajainli#20 = 0;
         |C_M #cajainli#19,1, "N";
     |SINO;
         |C_M #0#19,1, "S";
         |SI #0#19 = 0;
             #0#19 = enEjercicio;
         |FINSI;
     |FINSI;
     |PINTA #0#19;
|FPRC;

|PROCESO TrasPara347; |TIPO 0;
     |C_M #0#19,0,aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     |SI #0#19 < 2000; |ERROR; |FINSI;
|FPRC;
|| ==========================================================================
|PROCESO final; |TIPO 7;                     || desde campo 'descripcion'
       |SI eaSobreEs ! "S";
           |PTEC 816;
       |FINSI;
|FPRC;

|PROCESO descripc; |TIPO 0;                  || desde campo 'descripcion'
   |SI FSalida = 15;
      #cajainli#7 = deant;
      |PINTA #cajainli#7;
   |FINSI;
|FPRC;

|| ==========================================================================
|PROCESO c_departa; |TIPO 7;
   |SI eaDepar  = "N";
      |CAMPO_MODIFICA #cajainli#12, 1, "N";
   |SINO;
      |CAMPO_MODIFICA #cajainli#12, 1, "S";
      #cajainli#12= dpant; |PINTA #cajainli#12;
      |SI Haybasica = 1;
          |MENSA "    <F2> Consulta de actividades";
      |SINO;
          |MENSA "    <F2> Consulta de departamentos";
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO c_Subdepar; |TIPO 7;
   |SI eaSubde  = "N";
      |CAMPO_MODIFICA #cajainli#18, 1, "N";
   |SINO;
      |CAMPO_MODIFICA #cajainli#18, 1, "S";
      #cajainli#18= sdant; |PINTA #cajainli#18;
      |SI Haybasica = 1;
         |SI enArrend ! 2;
            |MENSA "    Subdepartamento: Referencias Catastrales <F2> Consultar <F3> Modificar";
         |SINO;
            |MENSA "    Subdepartamento. <F2> Consulta de Cultivos";
         |FINSI;
      |FINSI;
   |FINSI;
|FPRC;

|| ================= desde  Aqui
|PROCESO cActividad; |TIPO 0;
   |SI eaDepar = "N"; |ACABA; |FINSI;

   aBlancs = " "* 78; |PINTA 2202, aBlancs;
   eOpDep = 0; || 1 = Consulta pantalla
   |SI FSalida = 10;  eOpDep = 1; |FINSI;
   enActividad = #cajainli#12;
   eaCodDep    = #cajainli#12;
   |HAZ Actividad;
   |SI eswLect = 1;
       |MENAV "    Error.! No Existe Actividad / Departamento";
       |ERROR;
   |SINO;
       #cajainli#12 = eaCodDep; |PINTA #cajainli#12;
       |HAZ pDepartamento;
       |SI efActFin > "01.01.1900"; |Y snFilBa = "S";
           alfa1 = "    NAtencion!!!. Actividad con Fecha de BAJA " + efActFin + ", Desea introducir Facturas ? ";
           |CONFI alfa1;
           |SI FSalida ! 0;
               |ERROR;
               |ACABA;
           |FINSI;
       |FINSI;
       |SI eaEsCom = "S";
           |CONFI "    NAtencion!!!. Actividad Comunera, ¨ Desea introducir Movimientos ? ";
           |SI FSalida ! 0;
               |ERROR;
               |ACABA;
           |FINSI;
       |FINSI;
   |FINSI;
|FPRC;

|PROCESO cSubActividad; |TIPO 0;
   |SI eaSubde = "N"; |ACABA; |FINSI;

   eOpSub = 0; || 1 = Consulta pantalla
   |SI FSalida = 10; eOpSub = 1; |FINSI;
   |SI FSalida = 11; eOpSub = 2; |FINSI;

   aBlancs = " " * 78; |PINTA 2302, aBlancs;
   enConcepto  = #cajainli#6;
   eaCodSubdep = #cajainli#18;
   enQue = 1;
   |HAZ SubActividad;
   enQue = 0;
   |SI eswLect = 1;
       |MENAV "    Error.! No Existe SubDepartamento";
       |ERROR;
   |SINO;
       #cajainli#18 = eaCodSubdep; |PINTA #cajainli#18;
       |HAZ pSubdepar;
   |FINSI;
|FPRC;
|| ================= Hasta aqui

|| ==========================================================================
|PROCESO c_signo; |TIPO 7;   || desde campo 'cobro/pago'
   #cajainli#Campo# = dhant;
   |PINTA #cajainli#Campo#;
|FPRC;

|| ==========================================================================
|PROCESO c_importe; |TIPO 7;   || desde campo 'importe'
   #cajainli#Campo# = imant;
   |PINTA #cajainli#Campo#;
|FPRC;

|PROCESO nocero; |TIPO 0;                    || desde campo 'importe'
   inkey = FSalida;
   |SI #cajainli#Campo# = 0;
      |ERROR;
      |ACABA;
   |FINSI;
   |SI inkey = 14;
      inkey = 0;
      |PTEC 802;
      |PTEC 802;
      |PTEC 802;
      |PTEC 802;
      |PTEC 806;
   |FINSI;
|FPRC;

|| ==========================================================================
|| ............. DESDE LINEA APUNTE
|PROCESO saalta; |TIPO 0;               || desde campo 'linea'
   |SI FEntrada = 101; |Y #cajainli#3 ! 1;
      |SI coant ! 0;
         #cajainli#6 = coant;
         #cajainli#7 = deant;
         #cajainli#8 = dhant;
      |FINSI;
      |PINTA #cajainli#6;
   |FINSI;
   |SI FEntrada = 101;
       #cajainli#12 = dpant; |PINTA #cajainli#12;
       #cajainli#18 = sdant; |PINTA #cajainli#18;
   |FINSI;
|FPRC;

|PROCESO IncorporaLog;
   |DBASS aAlfa1; |QBF aAlfa1;
   aAlfa1 = aAlfa1 + "contagen/log/"; |MKDIR aAlfa1;

   fFecha = ~; aAlfa2 = fFecha; |QBF aAlfa2;
   aAlfa2 = (aAlfa2 % 0102) + (aAlfa2 % 0402) + (aAlfa2 % 0704);
   |DBASE aAlfa1; |QBF aAlfa1;
   aAlfa1 = aAlfa1 + "log/" + aAlfa2 + ".log";
   |XABRE "A", aAlfa1, nHandle;
   |SI FSalida ] 0;
      aAlfa1 = "";
      aAlfa2 = Usuario % 0810; |QBF aAlfa2;
      aAlfa2 = (aAlfa2 + "          ") % 0110;
      aAlfa1 = aAlfa1 + aAlfa2;
      aAlfa2 = fFecha; |QBF aAlfa2; aAlfa1 = aAlfa1 + " " + aAlfa2;
      |HORA aAlfa2; |QBF aAlfa2; aAlfa1 = aAlfa1 + " " + aAlfa2;
      |MODULO aAlfa2; |QBF aAlfa2;
      aAlfa1 = aAlfa1 + " " + aAlfa2;
      aAlfa = aAlfa1 + " " + aAlfa;
      |XGRABA nHandle, aAlfa;
      |XCIERRA nHandle;
   |FINSI;
|FPRC;

|PROCESO refresca; |TIPO 2;
   nModoEntrada = (FEntrada / 100) ! 0;
   nFlag = 0 +. 1;

   |SI nFlag < 0;
      |SI nModoEntrada = 2;
         aAlfa = "Entrando a modificar la linea " + #cajainli#3 + " de la caja del dia " + #cajainli#1 + " [" + #cajainli#7;
         |QBF aAlfa; aAlfa = aAlfa + "]";
      |FINSI;
      |SI nModoEntrada = 3;
         aAlfa = "Entrando a dar de baja la linea " + #cajainli#3 + " de la caja del dia " + #cajainli#1 + " [" + #cajainli#7;
         |QBF aAlfa; aAlfa = aAlfa + "]";
      |FINSI;
   |SINO;
      |SI nModoEntrada = 1;
         aAlfa = "Dando de alta la linea " + #cajainli#3 + " de la caja del dia " + #cajainli#1 + " [" + #cajainli#7;
         |QBF aAlfa; aAlfa = aAlfa + "]";
      |FINSI;
      |SI nModoEntrada = 2;
         aAlfa = "Finalizando la modificacion de la linea " + #cajainli#3 + " de la caja del dia " + #cajainli#1 + " [" + #cajainli#7;
         |QBF aAlfa; aAlfa = aAlfa + "]";
      |FINSI;
   |FINSI;
   |HAZ IncorporaLog;

   |DDEF aAlfa; |QBF aAlfa;
   aAlfa = aAlfa + "luxeg011.mas";
   |XABRE "E", aAlfa, nHandle;
   |SI FSalida ] 0; |Y nFlag < 0;
      |DBASS aAlfa; |QBF aAlfa;
      aAlfa = aAlfa + "dscarter/def/dscam043.mas";
      |CARGA_DEF aAlfa, dscam043@;
      |SI FSalida = 0;
         |DBASS aAlfa; |QBF aAlfa;
         aAlfa = aAlfa + "dscarter/fich/";
         |PATH_DAT #dscam043@#aAlfa#;
         |ABRE #dscam043@;
         #dscam043@#0 = #48#2;
         #dscam043@#1 = #48#3;
         #dscam043@#2 = 1;
         |LEE 000#dscam043@.];
         |PARA; |SI FSalida = 0; |Y #dscam043@#0 = #48#2; |Y #dscam043@#1 = #48#3; |HACIENDO;
            |SI #cajainli#8 = "C";
               |SI #dscam043@#3 = "V"; |SAL; |FINSI;
            |FINSI;
            |SI #cajainli#8 = "P";
               |SI #dscam043@#3 = "C"; |SAL; |FINSI;
            |FINSI;
            |LEE 000#dscam043@.s;
         |SIGUE;

         nSwErr = 0;
         |SI FSalida = 0; |Y #dscam043@#0 = #48#2; |Y #dscam043@#1 = #48#3;
            |SI #cajainli#8 = "C";
               |DBASS aAlfa; |QBF aAlfa; aAlfa = aAlfa + "dscarter/def/dscam003.mas";
               |CARGA_DEF aAlfa, dscam003@;
               |SI FSalida = 0;
                  |DBASS aAlfa; |QBF aAlfa; aAlfa = aAlfa + "dscarter/def/dscam004.mas";
                  |CARGA_DEF aAlfa, dscam004@;
                  |SI FSalida = 0;
                     |DBASS aAlfa; |QBF aAlfa; aAlfa = aAlfa + "dscarter/def/dscam070.mas";
                     |CARGA_DEF aAlfa, dscam070@;
                     |SI FSalida = 0;
                        |DBASS aAlfa; |QBF aAlfa;
                        aAlfa = aAlfa + "dscarter/fich/" + (("00000" + #dscam043@#9) % -105) + "/";
                        |PATH_DAT #dscam003@#aAlfa#; |PATH_DAT #dscam004@#aAlfa#;
                        |PATH_DAT #dscam070@#aAlfa#;

                        |ABRE #dscam003@; |ABRE #dscam004@; |ABRE #dscam070@;
                        |SELECT #7#dscam004@;
                        aAlfa = "Buscando una linea de un recibo del dia " + #cajainli#1 + " con marca de no enlazable y marcado con la linea " + #cajainli#3; |HAZ IncorporaLog;
                        #dscam004@#6 = #cajainli#1;
                        #dscam004@#40 = " ";
                        |LEE 000#dscam004@.];
                        |PARA; |SI FSalida = 0; |Y #dscam004@#6 = #cajainli#1; |HACIENDO;
                           |SI #dscam004@#55 = "S"; |Y #dscam004@#56 = #cajainli#3; |SAL; |FINSI;
                           |LEE 000#dscam004@.s;
                        |SIGUE;
                        |SI FSalida = 0; |Y #dscam004@#6 = #cajainli#1; |Y #dscam004@#55 = "S"; |Y #dscam004@#56 = #cajainli#3;
                           aAlfa = "Encontrada una linea de un recibo del dia " + #cajainli#1 + " con marca de no enlazable y marcado con la linea " + #cajainli#3 + " [" + #dscam004@#17 + "/" + #dscam004@#3 + "]"; |HAZ IncorporaLog;
                           || Este es .... lo borro y actualizo el recibo
                           |SI nModoEntrada = 2;
                              aAlfa = "Intentando modificar una linea creada por Cartera [" + #dscam004@#17 + "/" + #dscam004@#3 + "]. Peticion cancelada."; |HAZ IncorporaLog;
                              |MENAV "    Asiento procedente de la aplicacion de cartera. No se puede modificar.";
                              nSwErr = 1;
                           |FINSI;
                           |SI nModoEntrada = 3;
                              aAlfa = "    NSi elimina esta linea se eliminará tambien el cobro de cartera." + &191 + " Continuar ?";
                              |CONFI aAlfa;
                              |SI FSalida = 0;
                                 aAlfa = "Eliminando la linea creada por Cartera [" + #dscam004@#17 + "/" + #dscam004@#3 + "][" + #cajainli#7;
                                 |QBF aAlfa; aAlfa = aAlfa + "]"; |HAZ IncorporaLog;
                                 |SI #dscam004@#8 ! #cajainli#9;
                                    aAlfa = "ATENCION !!! Mientras que la linea del recibo es de " + #dscam004@#8 + " la de caja es de " + #cajainli#9; |HAZ IncorporaLog;
                                    aAlfa = "Se esta eliminando la linea del recibo " + #dscam004@#17 + " de fecha " + #dscam004@#6 + " marcado con la linea " + #dscam004@#56; |HAZ IncorporaLog;
                                 |FINSI;
                                 |SELECT #1#dscam004@;
                                 |LEE 101#dscam004@.=;
                                 |SI FSalida = 0;
                                    #dscam003@#40 = #dscam004@#17;
                                    |LEE 101#dscam003@.=;
                                    |SI FSalida = 0;
                                       #dscam003@#56 = #dscam003@#56 - #dscam004@#24;
                                       #dscam003@#23 = #dscam003@#23 - #dscam004@#8; #dscam003@#61 = #dscam003@#61 - #dscam004@#26;
                                       |SI #dscam004@#22 = "D";
                                          #dscam003@#54 = #dscam003@#54 - #dscam004@#23; #dscam003@#67 = #dscam003@#67 - #dscam004@#32;
                                       |SINO;
                                          #dscam003@#55 = #dscam003@#55 - #dscam004@#23; #dscam003@#68 = #dscam003@#68 - #dscam004@#32;
                                       |FINSI;
                                       #dscam003@#24 = #dscam003@#24 - #dscam004@#12; #dscam003@#62 = #dscam003@#62 - #dscam004@#29;
                                       #dscam003@#25 = #dscam003@#25 - #dscam004@#13; #dscam003@#63 = #dscam003@#63 - #dscam004@#30;
                                       #dscam003@#38 = #dscam003@#38 - #dscam004@#14; #dscam003@#66 = #dscam003@#66 - #dscam004@#31;

                                       |SI #dscam004@#54 ! 0; #dscam003@#12 = #dscam004@#54; |FINSI;
                                       |SALVA_FICHA #dscam004@;
                                       |LEE 000#dscam004@.a;
                                       |SI FSalida = 0;
                                          |SI #dscam004@#17 = #dscam003@#40;
                                             #dscam003@#26 = #dscam004@#6;
                                          |SINO;
                                             #dscam003@#26 = "01.01.0000";
                                          |FINSI;
                                          |LEE 000#dscam004@.s;
                                       |SINO;
                                          #dscam003@#26 = "01.01.0000";
                                       |FINSI;
                                       |REPON_FICHA #dscam004@;
                                       #dscam003@#30 = #dscam003@#22 + #dscam003@#24 + #dscam003@#25 + #dscam003@#38;
                                       #dscam003@#30 = #dscam003@#30 - (#dscam003@#54 + #dscam003@#55);
                                       #dscam003@#64 = #dscam003@#60 + #dscam003@#62 + #dscam003@#63 + #dscam003@#66 + #dscam003@#56;
                                       #dscam003@#64 = #dscam003@#64 - (#dscam003@#67 + #dscam003@#68);

                                       #dscam003@#31 = #dscam003@#30 - #dscam003@#23;  #dscam003@#65 = #dscam003@#64 - #dscam003@#61;

                                       |SI #dscam003@#69 ! " "; |O #dscam003@#47 = "S";
                                          |SI #dscam003@#23 = 0;
                                             |SI #dscam003@#31 = 0;
                                                #dscam003@#14 = "L";
                                                #dscam003@#15 = "Recibo Compensado";
                                             |SINO;
                                                #dscam003@#14 = "p";
                                                #dscam003@#15 = "Entrega pdte.cobro";
                                             |FINSI;
                                          |SINO;
                                             |SI #dscam003@#23 = #dscam003@#22;
                                                #dscam003@#14 = "e";
                                                #dscam003@#15 = "Pendiente de compensar";
                                             |SINO;
                                                #dscam003@#14 = "E";
                                                #dscam003@#15 = "Compensado parcialmente";
                                             |FINSI;
                                          |FINSI;
                                       |SINO;
                                          |SI #dscam003@#31 = 0;
                                             #dscam003@#14 = "L";
                                             #dscam003@#15 = "Recibo Liquidado";
                                          |SINO;
                                             |SI #dscam003@#23 = 0;
                                                |SI #dscam003@#70 = "          ";
                                                   |SI #dscam003@#16 = 0; |Y #dscam004@#5 ! "IMP";
                                                      #dscam003@#14 = "P";
                                                      #dscam003@#15 = "Pendiente de cobro";
                                                   |SINO;
                                                      #dscam003@#14 = "I";
                                                      #dscam003@#15 = "Impagado";
                                                   |FINSI;
                                                |SINO;
                                                   |SI #dscam003@#16 = 0; |Y #dscam004@#5 ! "IMP";
                                                      #dscam003@#14 = "T";
                                                      #dscam003@#15 = "Pagare/Talon de cliente";
                                                   |SINO;
                                                      #dscam003@#14 = "I";
                                                      #dscam003@#15 = "Impagado";
                                                   |FINSI;
                                                |FINSI;
                                             |SINO;
                                                #dscam003@#14 = "C";
                                                #dscam003@#15 = "Parcialmente cobrado";
                                             |FINSI;
                                          |FINSI;
                                       |FINSI;
                                       |GRABA 020#dscam003@.a; |LIBERA #dscam003@;
                                    |FINSI;
                                    |BORRA 020#dscam004@.a; |LIBERA #dscam004@;

                                    || Control de riesgo
                                    |DBASS aAlfa; |QBF aAlfa; aAlfa = aAlfa + "dscarter/def/dscam051.mas";
                                    |CARGA_DEF aAlfa, dscam051@;
                                    |SI FSalida = 0;
                                       |DBASS aAlfa; |QBF aAlfa;
                                       aAlfa = aAlfa + "dscarter/fich/" + (("00000" + #dscam043@#9) % -105) + "/";
                                       |PATH_DAT #dscam051@#aAlfa#;
                                       |ABRE #dscam051@;
                                       |LEE 000#dscam051@.p;
                                       |SI FSalida ! 0; |DDEFECTO #dscam051@; |FINSI;
                                       |SI #dscam051@#3 = "S";
                                          || Descuento el riesgo
                                          |DBASS aAlfa; |QBF aAlfa; aAlfa = aAlfa + "dscarter/def/dscamrie.mas";
                                          |CARGA_DEF aAlfa, dscamrie@;
                                          |SI FSalida = 0;
                                             |DBASS aAlfa; |QBF aAlfa;
                                             aAlfa = aAlfa + "dscarter/fich/" + (("00000" + #dscam043@#9) % -105) + "/";
                                             |PATH_DAT #dscamrie@#aAlfa#;
                                             aAlfa = "ri" + Usuario; |NOME_DAT #dscamrie@#aAlfa#;
                                             |DELINDEX #dscamrie@; |ABRE #dscamrie@;
                                             #dscamrie@#0 = #dscam003@#46;
                                             #dscamrie@#1 = 1;
                                             #dscamrie@#2 = #dscam043@#9;
                                             #dscamrie@#3 = "F";
                                             #dscamrie@#4 = #dscam003@#0;
                                             #dscamrie@#5 = #dscam004@#8;
                                             #dscamrie@#6 = #dscam003@#32;
                                             #dscamrie@#9 = #dscam003@#6;
                                             |GRABA 020#dscamrie@.n;
                                             |CIERRA #dscamrie@; |DESCARGA_DEF #dscamrie@;

                                             |DBASS aAlfa; |QBF aAlfa;
                                             aRPath = aAlfa + "dscarter/fich/" + (("00000" + #dscam043@#9) % -105) + "/";
                                             aRNome = "ri" + Usuario;
                                             aAlfa = ":dscarter/dsriesgo.tab";
                                             |SUB_EJECUTA_NP aAlfa;
                                          |FINSI;
                                       |FINSI;
                                       |CIERRA #dscam051@; |DESCARGA_DEF #dscam051@;
                                    |FINSI;
                                 |FINSI;

                                 nNumRef = 1;
                                 |LEE 000#dscam070@.u;
                                 |SI FSalida = 0;
                                    nNumRef = #dscam070@#0 + 1;
                                 |FINSI;

                                 |DDEFECTO #dscam070@;
                                 #dscam070@#0 = nNumRef;
                                 #dscam070@#5 = "CB";
                                 aAlfa = ("000000000" + #dscam004@#17) % -109;
                                 #dscam070@#6 = aAlfa;
                                 #dscam070@#2 = ~;
                                 |GRABA 020#dscam070@.b;
                                 aAlfa = Usuario % 0810;
                                 #dscam070@#1 = aAlfa;
                                 |HORA aAlfa;
                                 #dscam070@#3 = aAlfa;
                                 aAlfa = "BAJA DE LA LINEA DE CAJA EFECTIVO DESDE CONTABILIDAD";
                                 #dscam070@#4 = aAlfa;
                                 #dscam070@#9 = "B";
                                 #dscam070@#10 = "dscam004";
                                 aAlfa = ("000000000" + #dscam004@#17) % -109;
                                 aAlfa = aAlfa + (("00" + #dscam004@#3) % -102);
                                 #dscam070@#11 = aAlfa;
                                 |GRABA 020#dscam070@.a; |LIBERA #dscam070@;
                              |SINO;
                                 nSwErr = 1;
                              |FINSI;
                           |FINSI;
                        |FINSI;
                        |DESCARGA_DEF #dscam070@;
                     |FINSI;
                     |DESCARGA_DEF #dscam004@;
                  |FINSI;
                  |DESCARGA_DEF #dscam003@;
               |FINSI;
            |FINSI;
            |SI #cajainli#8 = "P";
               |DBASS aAlfa; |QBF aAlfa; aAlfa = aAlfa + "dscarter/def/dscam008.mas";
               |CARGA_DEF aAlfa, dscam008@;
               |SI FSalida = 0;
                  |DBASS aAlfa; |QBF aAlfa; aAlfa = aAlfa + "dscarter/def/dscam009.mas";
                  |CARGA_DEF aAlfa, dscam009@;
                  |SI FSalida = 0;
                     |DBASS aAlfa; |QBF aAlfa;
                     aAlfa = aAlfa + "dscarter/fich/" + (("00000" + #dscam043@#9) % -105) + "/";
                     |PATH_DAT #dscam008@#aAlfa#; |PATH_DAT #dscam009@#aAlfa#;
                     |ABRE #dscam008@; |ABRE #dscam009@;
                     |SELECT #4#dscam009@;
                     #dscam009@#6 = #cajainli#1;
                     |LEE 000#dscam009@.];
                     |PARA; |SI FSalida = 0; |Y #dscam009@#6 = #cajainli#1; |HACIENDO;
                        |SI #dscam009@#37 = "S"; |Y #dscam009@#38 = #cajainli#3; |SAL; |FINSI;
                        |LEE 000#dscam009@.s;
                     |SIGUE;
                     |SI FSalida = 0; |Y #dscam009@#6 = #cajainli#1; |Y #dscam009@#37 = "S"; |Y #dscam009@#38 = #cajainli#3;
                        |SI nModoEntrada = 2;
                           |MENAV "    Asiento procedente de la aplicacion de cartera. No se puede modificar.";
                           nSwErr = 1;
                        |FINSI;
                        |SI nModoEntrada = 3;
                           aAlfa = "    NSi elimina esta linea se eliminará tambien el pago de cartera." + &191 + " Continuar ?";
                           |CONFI aAlfa;
                           |SI FSalida = 0;
                              #dscam008@#40 = #dscam009@#14;
                              |LEE 101#dscam008@.=;
                              |SI FSalida = 0;
                                 #dscam008@#62 = #dscam008@#62 - #dscam009@#26;
                                 #dscam008@#23 = #dscam008@#23 - #dscam009@#8;  #dscam008@#57 = #dscam008@#57 - #dscam009@#21;
                                 #dscam008@#24 = #dscam008@#24 - #dscam009@#11; #dscam008@#54 = #dscam008@#54 - #dscam009@#23;
                                 #dscam008@#38 = #dscam008@#38 - #dscam009@#12; #dscam008@#55 = #dscam008@#55 - #dscam009@#24;

                                 |SALVA_FICHA #dscam009@;
                                 |LEE 000#dscam009@.a;
                                 |SI FSalida = 0;
                                    |SI #dscam009@#14 = #dscam008@#40;
                                       #dscam008@#26 = #dscam009@#6;
                                    |SINO;
                                       #dscam008@#26 = "01.01.0000";
                                    |FINSI;
                                    |LEE 000#dscam009@.s;
                                 |SINO;
                                    #dscam008@#26 = "01.01.0000";
                                 |FINSI;
                                 |REPON_FICHA #dscam009@;
                                 #dscam008@#25 = #dscam008@#22 + #dscam008@#24 + #dscam008@#38;
                                 #dscam008@#56 = #dscam008@#53 + #dscam008@#54 + #dscam008@#55;

                                 #dscam008@#31 = #dscam008@#25 - #dscam008@#23;
                                 #dscam008@#58 = #dscam008@#56 - #dscam008@#57;

                                 |SI #dscam008@#31 = 0;
                                    #dscam008@#14 = "L";
                                    #dscam008@#15 = "Recibo pagado";
                                 |SINO;
                                    |SI #dscam008@#23 = 0;
                                       #dscam008@#14 = "P";
                                       #dscam008@#15 = "Pendiente de pago";
                                    |SINO;
                                       #dscam008@#14 = "C";
                                       #dscam008@#15 = "Parcialmente pagado";
                                    |FINSI;
                                 |FINSI;
                                 |GRABA 020#dscam008@.a; |LIBERA #dscam008@;
                              |FINSI;
                              |BORRA 020#dscam009@.a; |LIBERA #dscam009@;
                           |SINO;
                              nSwErr = 1;
                           |FINSI;
                        |FINSI;
                     |FINSI;
                     |DESCARGA_DEF #dscam009@;
                  |FINSI;
                  |DESCARGA_DEF #dscam008@;
               |FINSI;
            |FINSI;
         |FINSI;

         |CIERRA #dscam043@;
         |DESCARGA_DEF #dscam043@;
      |FINSI;
   |FINSI;

   |SI nSwErr = 1;
      |SI nModoEntrada = 2;
         |SI nFlag > 0;
            aAlfa = "No finalizada la modificacion de la linea por cancelacion en la entrada a la misma"; |HAZ IncorporaLog;
         |FINSI;
      |FINSI;
      |SI nModoEntrada = 3;
         aAlfa = "Cancelacion de la baja de la linea por no querer eliminar el contenido de cartera"; |HAZ IncorporaLog;
      |FINSI;
      |SI nFlag > 0; nSwErr = 0; |FINSI;
      |ERROR; |ACABA;
   |FINSI;

   nSwError = 0; |HAZ a_caja;
   |SI nSwError = 1;
      aAlfa = "Caja sin fondos para hacer la operacion. [" + nModoEntrada + "/" + nFlag + "]"; |HAZ IncorporaLog;
      |ERROR; |ACABA;
   |FINSI;

   aAlfa = "Operacion permitida. ";
   |SI nModoEntrada = 1;
      aAlfa = aAlfa + "Dando de alta la linea " + #cajainli#3 + " en la caja del dia " + #cajainli#1 + ". [";
      aAlfa = aAlfa + #cajainli#7; |QBF aAlfa;
      aAlfa = aAlfa + "]";
   |FINSI;
   |SI nModoEntrada = 2;
      |SI nFlag > 0;
         aAlfa = aAlfa + "Modificando la linea " + #cajainli#3 + " en la caja del dia " + #cajainli#1 + ". [";
         aAlfa = aAlfa + #cajainli#7; |QBF aAlfa;
         aAlfa = aAlfa + "]";
      |FINSI;
   |FINSI;
   |SI nModoEntrada = 3;
      aAlfa = aAlfa + "Eliminando la linea " + #cajainli#3 + " en la caja del dia " + #cajainli#1 + ". [";
      aAlfa = aAlfa + #cajainli#7; |QBF aAlfa;
      aAlfa = aAlfa + "]";
   |FINSI;
   |HAZ IncorporaLog;

   |SI #cajainli#8 = "P"; signo = "D"; |FINSI; || prov. o cli: si pago al debe
   |SI #cajainli#8 = "C"; signo = "H"; |FINSI; ||              si cobro al Haber

    MasMenos     = 0 +. #cajainli#9;
    DebeHaber    = signo;
    aCUENTA      = #cajainli#4;
    nREGISTRO    = -1;
    nDIARIO      = #cajainli#0;
    aFECHA       = #cajainli#1;
    nDOCUMENTO   = #cajainli#2;
    nNIVELCUENTA = #cajainli#5;
    |HAZ ActCuentM;          || cuenta de pase Directo S

    |ATRI I;
    |PINTA 0740, #cacuenta#51;
    |PINTA 0754, #cacuenta#52;
    |PINTA 0768, #cacuenta#53;
    |ATRI 0;

   |SI #cajainli#8 = "P"; signo = "H"; |FINSI;  || caja, si pago al Haber
   |SI #cajainli#8 = "C"; signo = "D"; |FINSI;  ||       si cobro al Debe
|FPRC;

|PROCESO entmod; |TIPO 1;
   coant = #cajainli#6;
   deant = #cajainli#7;
   dpant = #cajainli#12;
   sdant = #cajainli#18;
   dhant = #cajainli#8;
   imant = #cajainli#9;
|FPRC;


|PROCESO guarda; |TIPO 3;               || al grabar la ficha
   xx = (FEntrada / 100) ! 0;
   |SI #cajainli#4 = doce;
      avisos = "ATENCION!!! Apunte sin cuentas ...";
      |HAZ aviso5;
      |PAUSA;
      |HAZ aviso9;
      |ERROR;
      |ACABA;
   |FINSI;
   |SI #cajainli#9 = 0;                       || importe distinto de cero
      avisos = "ATENCION!!! Importe cero ...";
      |HAZ aviso5;
      |PAUSA;
      |HAZ aviso9;
      |ERROR;
      |ACABA;
   |FINSI;

   enFilConc = 1;
   eaCuenta   = #cajainli#4;
   enConcepto = #cajainli#6;
   |HAZ FiltroConcepto;  |SI eswLect ! 0; |ACABA; |FINSI;

   cuant = #cajainli#4;
   diant = #cajainli#5;
   coant = #cajainli#6;
   deant = #cajainli#7;
   dpant = #cajainli#12;
   sdant = #cajainli#18;
   dhant = #cajainli#8;
   imant = #cajainli#9;
|FPRC;

|PROCESO pinta_cta; |TIPO 13;      || desde 'refresca', 'cuefin_t', CONSULTA
   |SI SwSoloUno = 0; |CIERRAT #cacuenta; |NOME_DAT #cacuenta #cactaesp#; |ABRET #cacuenta; SwSoloUno = 1; |FINSI;

   |SI #cajainli#4 ! doce;
      #cacuenta#0 = #cajainli#4;
      #cacuenta#1 = #cajainli#5;
      |LEE 001#cacuenta.=;
      |SI FSalida ! 0;  |DDEFECTO #cacuenta;  |FINSI;
   |SINO;
      |DDEFECTO #cacuenta;
   |FINSI;
   |ATRI I;
   |PINTA 0740, #cacuenta#51;
   |PINTA 0754, #cacuenta#52;
   |PINTA 0768, #cacuenta#53;
   |PINTA 0639, #cacuenta#2;
   |ATRI 0;

   aBlancs = " " * 78;
   |PINTA 2202, aBlancs; |PINTA 2302, aBlancs;
   xx = (FEntrada / 100) ! 0; |SI xx = 1; |ACABA; |FINSI;
   |SI eaDepar = "S";
       eOpDep = 0;
       enActividad = #cajainli#12;
       eaCodDep    = #cajainli#12;
       |HAZ Actividad;
       |HAZ pDepartamento;
   |FINSI;
   |PINTA 2341, "                                        ";
   |SI eaSubde = "S";
       eOpSub = 0;
       enConcepto  = #cajainli#6;
       eaCodSubdep = #cajainli#18;
       |HAZ SubActividad;
       |HAZ pSubdepar;
   |FINSI;
|FPRC;
||*******************************************************
|PROCESO LeoEspejo;
  nNumero = #999#3;
  nConNum = nConNum + 1;
  #998#0  = #999#0;
  #998#1  = #999#1;
  #998#2  = #999#2;
  #998#3  = #999#3;
  #998#4  = #999#4;
  #998#5  = #999#5;
  #998#6  = #999#6;
  #998#7  = #999#7;
  #998#8  = #999#8;
  #998#9  = #999#9;
  #998#10 = #999#10;
  #998#11 = #999#11;
  #998#12 = #999#12;
  #998#13 = #999#13;
  #998#14 = #999#14;
  #998#15 = #999#15;
  #998#16 = #999#16;
  #998#17 = #999#17;
  #998#18 = #999#18;
  #998#19 = #999#19;
  #998#20 = #999#20;
  |GRABA 020#998n;
|FPRC;

|DEFBUCLE LeoEspejo;
 #999#1002;
 9;
 #0#1, 0001, nDImporte;
 #0#1, 9999, nHImporte;
 ;
 NULL, LeoEspejo;
|FIN;
||*******************************************************
|PROCESO Tipo11W42; |TIPO 11;
  nNumero = #998#3;
|FPRC;
||*******************************************************
|PROCESO Alta998;
 #998#1 = #0#1;
 #998#3 = 0001;
|FPRC;

|PROCESO Baja998;
 #998#1 = #0#1;
 #998#3 = 9999;
|FPRC;

|PROCESO LaBusqueda;

       |ERROR;

       |PUSHV 1022 2353;
       |PINTA 1023, "                              ";
       |PINTA 1123, " Del importe:                 ";
       |PINTA 1223, " Al importe :                 ";
       |PINTA 1323, "                              ";
       |CUADRO 1022 1352;
       |C_P #2#12, 1, 1137; |PINTA #2#12;
       |C_P #2#13, 1, 1237; |PINTA #2#13;

    |ET Camp10;
       |ENCAMPO #12#2;
       |SI FSalida = 1; |VETE  Campo0; |FINSI;
       #2#13 = #2#12; |PINTA #2#13;

    |ET Camp11;
       |ENCAMPO #13#2;
       |SI FSalida = 2; |VETE Camp10; |FINSI;
       |SI #2#13 < #2#12;
           |MENAV "    Error. Importe inferior al desde";
           |VETE Camp11;
       |FINSI;

    |ET Campo0;
       |POPV;
       nDImporte = #2#12;
       nHImporte = #2#13;

       nNumero = 0;
       nConNum = 0;
       |PUSHV 0801 2080;
       |DBASE aMas;
       aDef = aMas + "def/cajainli";
       |CARGA_DEF aDef, espejo@;
       |SI FSalida ! 0;
           |MENAV "    Error al Carga fichero de cajainli";
           |POPV;
           |ACABA;
       |FINSI;

       aFichero = "6x" + Usuario;
       |NOME_DAT #998 aFichero;
       |DELINDEX #998;

       |ABRE #998;
       |BUCLE LeoEspejo;
       |CIERRA #998;

       |SI nConNum = 0;
           |MENAV "    No Existe Apuntes dentro de la Seleccion";
       |SINO;
           |SI nConNum ! 1;
               |PINPA #0#998;
               |ATRI R; |PINTA 0943, #2#12;
                        |PINTA 0964, #2#13; |ATRI 0;

               |ENTLINEAL #1#998, -2, 4, 20, 0, Alta998, Baja998;
           |FINSI;
       |FINSI;

       |DESCARGA_DEF #espejo@;
       |POPV;

       |SI nNumero ! 0;
           #0#3 = nNumero;
           |PTEC 802;
           |PONCONTROL 23, "SI";
       |FINSI;
|FPRC;

|PROCESO funcion1; |TIPO 11;                  || desde campo 'linea'
   inkey = FSalida;
   xx = (FEntrada / 100) ! 0;

   |SI inkey = 11;
       |HAZ LaBusqueda;
       |ACABA;
   |FINSI;

   |SI xx ! 1;
      |HAZ funcion2;
   |SINO;
      |SI Campo = 3;  |Y inkey = 14;  |Y  #cajainli#3 ] 1;
         |PTEC 802;
         |PTEC 802;
         |PTEC 802;
         |PTEC 806;
         |PTEC 807;
      |SINO;
         |SI inkey ] 9;|Y inkey [ 20;
            |AVISO;
            |ERROR;
         |FINSI;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO funcion2;                           || desde calculo 'funcion1'
   |SI inkey ] 0;  |Y inkey [ 8;
       |ACABA;
   |FINSI;

   |SI inkey ! 9;  |Y inkey ! 10;
      |AVISO;
      |ERROR;
      |ACABA;
   |FINSI;


   |SI inkey = 9;            || extractos
      ext1 = #cajainli#4;          || cuenta
      ext2 = #cajainli#5;          || digito
      programa = "caextrap.run";
      |HAZ externos;
      |ERROR;
      |ACABA;
   |FINSI;

   |SI inkey = 10;             || diario de trabajo
      programa = "caditrab.run";
      |HAZ externos;
      |ERROR;
      |ACABA;
   |FINSI;
|FPRC;

|PROCESO externos;                      || desde calculo 'cuefin'
   enDiarioDentro    = #1#0;
   efFechaDentro     = #1#1;
   enDocumentoDentro = #1#2;

   |ATRI P;
   |MENSA "    Cargando Opcion ...";
   |ATRI 0;
   sit1 = #cajainli#0;         || diario
   sit2 = #cajainli#1;         || fecha
   sit3 = #cajainli#2;         || documento        / las guarda para la vuelta
   sit4 = #cajainli#3;         || apunte
   sit5 = #cajainca#2;         || debe asiento
   sit6 = #cajainca#3;         || haber asiento
   |LIBERA #0;
   |CIERRAT #0;        || cierra lins.
   |LIBERA #1;
   |CIERRAT #1;        || cierra cabs.

   |SUB_EJECUTA programa;

   |ABRET #0;          || abre lins.
   #cajainli#0 = sit1;         || diario
   #cajainli#1 = sit2;         || fecha
   #cajainli#2 = sit3;         || documento
   #cajainli#3 = sit4;         || apunte
   |LEE 101#0=;        || lee el apunte
   |ABRET #1;          || abre cabs.
   #cajainca#0 = sit1;         || diario
   #cajainca#1 = sit2;         || fecha
   #cajainca#2 = sit3;         || documento
   |LEE 101#1=;        || lee el asiento
   #cajainca#2 = sit5;         || debe asiento
   #cajainca#3 = sit6;         || haber asiento
   #cafantas#0 = #cajainca#3 - #cajainca#2;  || saldo asiento
|FPRC;

***************************************************************************
RUTINAS
***************************************************************************

|PROCESO CalcCaja1;
   nTotal1 = nTotal1 + (#cajainca#3 - #cajainca#2);
   nTotal1 = nTotal1 ! 2;
|FPRC;

|DEFBUCLE CalcCaja1;
#cajainca#1;
;
"01.01.0000";
fFecha1;
;
NULL, CalcCaja1;
|FIN;

|PROCESO a_caja;
   nTotal1 = 0; fFecha1 = #cajainca#1;
   |ABRE #cajasald;
   |LEE 000#cajasald.p;
   |SI FSalida ! 0; |DDEFECTO #cajasald; |FINSI;
   |CIERRA #cajasald;
   nTotal1 = #cajasald#1;
   |SALVA_FICHA #cajainca;
   |BUCLE CalcCaja1;
   |REPON_FICHA #cajainca;

   |SI nTotal1 < #0#9; |Y #cajainli#8 = "P"; |Y nModoEntrada = 1;
      |MENAV "    Caja sin fondos suficientes";
      nSwError = 1; |ACABA;
   |FINSI;

   |SI #cajainli#8 = "P";
      #cajainca#2 = #cajainca#2 +. #0#9;
      nTotal1 = nTotal1 -. #0#9;
   |SINO;
      #cajainca#3 = #cajainca#3 +. #0#9;
      nTotal1 = nTotal1 +. #0#9;
   |FINSI;
   |GRABA 020#cajainca.a;

   #cafantas#0 = #cajainca#3 - #cajainca#2;
   #cafantas#0 = nTotal1;
   |PINTA #cajainca#2;
   |PINTA #cajainca#3;
   |ATRI I;
   |PINTA 0967, #cafantas#0;
   |ATRI 0;
|FPRC;

|PROCESO CalcLineas;
   |SI #cajainli#8 = "P";
      #cajainca#2 = #cajainca#2 + #0#9;
   |SINO;
      #cajainca#3 = #cajainca#3 + #0#9;
   |FINSI;
|FPRC;

|DEFBUCLE CalcLineas;
#cajainli#1;
;
#cajainca#1, 0000;
#cajainca#1, 9999;
;
NULL, CalcLineas;
|FIN;

|PROCESO RepintaTodo; |TIPO 7;
   |SI nSwLuxe < 0; |ACABA; |FINSI;

   |SI nSwLuxe = 0;
      |DDEF aAlfa; |QBF aAlfa;
      aAlfa = aAlfa + "luxeg011.mas";
      |XABRE "E", aAlfa, nHandle;
      |SI FSalida < 0; nSwLuxe = -1; |SINO; nSwLuxe = 1; |FINSI;

      |SI nSwLuxe < 0; |ACABA; |FINSI;
   |FINSI;

   nTotal1 = 0; fFecha1 = #cajainca#1;
   nCalc = fFecha1; nCalc = nCalc - 1; fFecha1 = nCalc;

   |ABRE #cajasald;
   |LEE 000#cajasald.p;
   |SI FSalida ! 0; |DDEFECTO #cajasald; |FINSI;
   |CIERRA #cajasald;
   nTotal1 = #cajasald#1;
   |SALVA_FICHA #cajainca;
   |BUCLE CalcCaja1;
   |REPON_FICHA #cajainca;

   |SALVA_FICHA #cajainli;
   #cajainca#3 = 0; #cajainca#2 = 0;
   |BUCLE CalcLineas;
   |GRABA 020#cajainca.a;
   |REPON_FICHA #cajainli;

   |PINTA #cajainca#2; |PINTA #cajainca#3;
   #cafantas#0 = nTotal1 + (#cajainca#3 - #cajainca#2);
   |ATRI I;
   |PINTA 0967, #cafantas#0;
   |ATRI 0;
|FPRC;
