|FICHEROS;
   cay4ivas #0;

   camaniva #11;
   caivalin #12;
   caivaasi #200;

   caw00019 #900;
   canempre #30;
   caselem1 #998;
|FIN;

|VARIABLES;
    aAlfa1   = "";
    aAlfa2   = "";
    aAlfa3   = "";
    aAlfa4   = "";
    nNume1   = 0;
    nNume2   = 0;
    aFichero = "";

    informe  = "cay4ivas";

    nExis    = 0;
    nSwOp    = 0;

    nSwAlgun = 0;
    nSw      = 0;
    nImporte = 0;
    fFecha   = @;
    nLinea   = 0;
    nLineaS  = 0;
    nLineaR  = 0;
    aTipo    = "";
    nCampo   = 0;
    nEstado  = 0;
    nSwEncon = 0;

    &enSwLinea = 0;
    fDFecha    = @;
|FIN;

|INCLUYE dscont_i;

|| ***************** Calculos de Pantalla
|CALCULO Primero; |TIPO 8;
  |HAZ inicio;

  |ABRE #200;
  |LEE 000#200p;
  |SI FSalida ! 0;
      |DDEFECTO #200;
      |GRABA 040#200n;
  |FINSI;
  |CIERRA #200;
  aAlfa1 = #200#44;
  |C_M #0#7,1,aAlfa1;
  |C_M #0#8,1,aAlfa1;
  |HAZ eParaModelos;
|FCAL;

|CALCULO defectos; |TIPO 7;
  fDFecha = fec1;
  |SI enSwSelFc = 1; fDFecha = "01.01.2000"; |FINSI;
  #0#4 = fDFecha; |PINTA #0#4;
  #0#5 = fec2;    |PINTA #0#5;
|FCAL;

|CALCULO fecha; |TIPO 0;
  fDFecha = fec1;
  |SI enSwSelFc = 1; fDFecha = "01.01.0000"; |FINSI;
  |SI #0Campo < fDFecha; |O #0Campo > fec2;
      |MENAV "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
      |ERROR;
      |ACABA;
  |FINSI;
  |SI Campo = 5; |Y #0#5 < #0#4;
      |MENAV "    FECHA ERRONEA ";
      |ERROR;
  |FINSI;
|FCAL;

|| *************************************************************************
|| **************************   MEOLLO   ***********************************
|| *************************************************************************
|PROCESO LasLineas0;
     nExis = 1;
     |ERROR10;
|FPRC;

|DEFBUCLE LasLineas;
 #12#1;
 25;
 #11#0, #11#1, 01, "S";
 #11#0, #11#1, 99, "S";
 e;
 NULL, LasLineas0;
|FIN;

|PROCESO GrabaRegistro;
      |ABRE #900;
      |DDEFECTO #900;
      #900#0 = fFecha;          || Fecha
      #900#1 = nImporte;        || Importe
      #900#2 = nLinea;
      |LEE 000#900=;
      nEstado = FSalida;
      #900#0 = fFecha;
      #900#1 = nImporte;
      #900#2 = nLinea;

      nCampo = 3;
      |SI aTipo = "R"; nCampo = 18; |FINSI;

      aAlfa1 = #11#0;  aAlfa1 = ("00" + aAlfa1) % -102;
      aAlfa2 = #11#1;  aAlfa2 = ("0000000000" + aAlfa2) % -110;
      aAlfa3 = #11#3;  aAlfa3 = ("000000" + aAlfa3) % -106;
      aAlfa4 = #11#27; aAlfa4 = ("000" + aAlfa4) % -103;
      #900nCampo = 1;
      nNume1 = nCampo + 1;  #900nNume1 = aAlfa1;  ||Libro
      nNume1 = nCampo + 2;  #900nNume1 = aAlfa2;  ||Documento
      nNume1 = nCampo + 3;  #900nNume1 = #11#2;   ||Serie
      nNume1 = nCampo + 4;  #900nNume1 = aAlfa3;  ||Factura
      nNume1 = nCampo + 5;  #900nNume1 = #11#4;   ||Fecha
      nNume1 = nCampo + 6;  #900nNume1 = #11#10;  ||Departamento
      nNume1 = nCampo + 7;  #900nNume1 = #11#11;  ||Subdepartamento
      nNume1 = nCampo + 8;  #900nNume1 = #11#12;  ||Cuenta Prov/Cli
      nNume1 = nCampo + 9;  #900nNume1 = #11#13;  ||Nombre Cuenta Prov/Cli
      nNume1 = nCampo + 10; #900nNume1 = #11#14;  ||CIF Cuenta Prov/Cli
      nNume1 = nCampo + 11; #900nNume1 = #11#21;  ||Total Factura
      nNume1 = nCampo + 12; #900nNume1 = #11#22;  ||Referencia
      nNume1 = nCampo + 13; #900nNume1 = aAlfa4;  ||Concepto
      nNume1 = nCampo + 14; #900nNume1 = #11#28;  ||Descripcion

      |SI nEstado = 0; |GRABA 020#900a; |FINSI;
      |SI nEstado ! 0; |GRABA 020#900n; |FINSI;
      nSwAlgun = 1;
      |CIERRA #900;
|FPRC;

|PROCESO BuscaUno;
 nLineaR = #900#2;
 |SI #900#18 = 0;
     nSwEncon = 1;
     |ERROR10;
 |FINSI;
|FPRC;

|DEFBUCLE BuscaUno;
 #900#1;
 ;
 fFecha, nImporte, 000001;
 fFecha, nImporte, 999999;
 ;
 NULL, BuscaUno;
|FIN;

|PROCESO GrabaAuxiliar;
  nExis = 0;
  |BUCLE LasLineas;
  |SI nExis = 0; |ACABA; |FINSI;

|| ... .... ... Cabecera

  |SI nSw = 0;
      fFecha   = #11#4;
      nImporte = #11#21;
      nSw      = 1;
  |FINSI;

  |SI fFecha ! #11#4;
      nLineaS = 0;
      fFecha  = #11#4;
  |FINSI;

  nImporte = #11#21;
  |SI aTipo = "S";
      nLineaS = nLineaS + 1;
      nLinea  = nLineaS;
      |HAZ GrabaRegistro;
  |FINSI;

  |SI aTipo = "R";
      nSwEncon = 0;
      nLineaR  = 0;
      |BUCLE BuscaUno;
      |SI nSwEncon = 0;
          nLineaR = nLineaR + 1;
      |FINSI;
      nLinea = nLineaR;
      |HAZ GrabaRegistro;
  |FINSI;
|FPRC;

|DEFBUCLE caymaniva;
 #11#5;
 2,3,36;
 #0#4, #0#0, 0000000001, #0#7, #0#2, aTipo;
 #0#5, #0#1, 9999999999, #0#8, #0#3, aTipo;
 e;
 NULL,GrabaAuxiliar;
|FIN;

|| ******************************************************
|PROCESO ImprimeAux;
 enSwLinea = 0;
 |SI #900#3 = 1; |Y #900#18 = 1; enSwLinea = 1; |FINSI;
 |SI #900#3 = 1; |Y #900#18 = 0; enSwLinea = 2; |FINSI;
 |SI #900#3 = 0; |Y #900#18 = 1; enSwLinea = 3; |FINSI;
 |PRINT;
|FPRC;

|DEFBUCLE ImprimeAux;
 #900#1;
 ;
 INICIO;
 FINAL;
 ;
 NULL, ImprimeAux;
|FIN;

|PROCESO AuxMin;
 #900#0 = "01.01.0000";
 #900#1 = -9999999999;
 #900#2 = 1;
|FPRC;

|PROCESO AuxMax;
 #900#0 = "31.12.2999";
 #900#1 = 9999999999;
 #900#2 = 99999;
|FPRC;

|PROCESO HazTodo;

   nSwAlgun = 0;
   nSw      = 0;
   nImporte = 0;
   fFecha   = "01.01.0000";
   nLineaS  = 0;
   aTipo    = "S";
   |BUCLE caymaniva;

   nSw      = 0;
   nImporte = 0;
   fFecha   = "01.01.0000";
   nLineaR  = 0;
   aTipo    = "R";
   |BUCLE caymaniva;

    |SI nSwAlgun = 1;
        |SI #0#10 = "I";
            |BUCLE ImprimeAux;
            |PIEINF;
        |SINO;
            |PINPA #0#900;
            |ENTLINEAL #1#900, -1, 4, 22, 0, AuxMin, AuxMax;
        |FINSI;
    |FINSI;

   |DELINDEX #900;
|FPRC;

|| ********************************************************************
|CALCULO impre; |TIPO 3;

   aFichero = ("4i" + Usuario) % 108;
   |NOME_DAT #900 aFichero;
   |ABRE #900; |CIERRA #900; |DELINDEX #900;

   |IMPRE 0;
   |SI FSalida ! 0;  |ACABA;  |FINSI;

   |INFOR informe;
   |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

  |SI #48#27 = "S";
      |HAZ MultiEmpresa;
  |SINO;
      |DFICE dirfich0; |QBF dirfich0;
      |HAZ HazTodo;
  |FINSI;

  |FINIMP;
|FCAL;


|PROCESO impreComp;
  nExis = 0;
  |ABRE #11;
  |SELECT #3#11;
  #11#0 = #0#0;
  #11#2 = #0#7;
  #11#3 = #0#2;
  |LEE 040#11=;
  |SI FSalida = 0;
      nExis = 0;
      nSwOp = 1;
      |BUCLE LasLineas;
  |FINSI;
  |SELECT #1#11;
  |CIERRA #11;

  |SI nExis = 1;
      |HAZ impre;
  |FINSI;
|FPRC;

|| **********************************************************************
|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;
   |PATH_DAT #11  dirfich0;
   |PATH_DAT #12  dirfich0;

   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 000001;
 999999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;
