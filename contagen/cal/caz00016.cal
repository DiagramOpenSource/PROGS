|FICHEROS;
  caz00016 #0;

  camaasil #1;
  caivases #4;
  calibros #5;
  cacuenta #6;
  caclipro #7;
  camaniva #10;
  caivalin #11;
  :/modelos/def/dsmom030 #130;

  caivaasi #200;
|FIN;

|VARIABLES;
  &entrada = 1;
  sw       = 0;
  enQSerie = 0;
  nSwSel   = 0;

  aAlfa1   = "";
  aAlfa2   = "";
  aAlfa3   = "";
  nNume1   = 0;
  nNume2   = 0;
  nNume3   = 0;
  nNume4   = 0;
  nCampo   = 0;
  nPara1   = 0;

  aFichero   = "";
  nElDoc     = 0;
  swc        = 0;
  nDCta      = 0;
  nHCta      = 0;
  aDCuenta   = "";
  aHCuenta   = "";
  nSCon      = 0;
  nDConcepto = 0;
  nHConcepto = 0;
  nLibro     = 0;
  aSerie     = "";
  nFactu     = 0;
  nConcep    = 0;
  aDescri    = "";
  fFecha     = @;
  nPeriodo   = 0;
  aTitCuenta = "";
  nImporte   = 0;
  aCtaContra = "";
  nIncre     = 0;
  nParcial   = 0;
  aSigno     = "";
  aPassword  = "";
|FIN;

|INCLUYE dscont_i;

|| **************************************************************************
|CALCULO eInic8; |TIPO 8;
 |HAZ DirAplicSt;
 |HAZ inicio;
 |ABRE #200;
 |DDEFECTO #200;
 |LEE 010#200p;
 enQSerie = 0;
 |SI #200#44 = "S"; enQSerie = 1;|FINSI;
 eaSobreEs = #200#155;
 |CIERRA #200;
|FCAL;

|CALCULO def_fec; |TIPO 7;
   |SI sw = 0;
       sw = 1;
       #0#2  = fec1;    |PINTA #0#2;
       #0#3  = fec2;    |PINTA #0#3;
       #0#29 = #200#3;  |PINTA #0#29;
       #0#30 = #200#63; |PINTA #0#30;
       #0#32 = #200#5;  |PINTA #0#32;
       #0#33 = #200#7;  |PINTA #0#33;

       #0#56 = #200#29; |PINTA #0#56;
       #0#57 = #200#65; |PINTA #0#57;
       #0#59 = #200#31; |PINTA #0#59;
       #0#60 = #200#33; |PINTA #0#60;
   |FINSI;
|FCAL;

|CALCULO fecha; | TIPO 0;   || Desde/hasta fecha
   |SI Campo = 74;
       |C_M #0#74,0,aAlfa1;
       |SI aAlfa1 = "N"; |ACABA; |FINSI;
   |FINSI;
   |SI #0Campo < fec1; |O #0Campo > fec2;
      |MENAV "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
      |ERROR;
   |FINSI;
|FCAL;

|CALCULO diarioh; |TIPO 0;   ||  hasta diario
   |SI #0#1 < #0#0;  |ERROR;  |FINSI;
|FCAL;

|CALCULO fechah; |TIPO 0;   ||  hasta fecha
   |SI #0#3 < #0#2;  |ERROR;  |FINSI;
|FCAL;

|CALCULO documh; |TIPO 0;   ||  hasta documento
   |SI #0#5 < #0#4;  |ERROR;  |FINSI;
|FCAL;

|CALCULO pedirfecha; |TIPO 0;
   |SI #0#6 = "T";
       |C_M #0#74, 1,"S";
       #0#74 = #0#3; |PINTA #0#74;
   |SINO;
       |C_M #0#74, 1,"N";
       #0#74 = "01.01.0000"; |PINTA #0#74;
   |FINSI;
|FCAL;

|CALCULO paso; |TIPO 0;
 aAlfa1 = #0Campo;
 nNume2 = Campo + 1;
 nNume3 = Campo + 26;
 |SI aAlfa1 = "N";
     |SI Campo = 7;  |PDEFECTO #0,  8, 34; |FINSI;
     |SI Campo = 34; |PDEFECTO #0, 35, 61; |FINSI;
 |FINSI;
 |PARA nNume1 = nNume2; |SI nNume1 [ nNume3; |HACIENDO nNume1 = nNume1 + 1;
       |C_M #0nNume1,1,aAlfa1;
       |SI aAlfa1 = "N"; |PINTA #0nNume1; |FINSI;
 |SIGUE;
 |SI Campo = 7;  |C_M #0#79,1,aAlfa1; |FINSI;
 |SI Campo = 34; |C_M #0#80,1,aAlfa1; |FINSI;
|FCAL;

|CALCULO cuenta; |TIPO 0;
     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;
|FCAL;

|CALCULO cuentah; |TIPO 0;
 |C_M #0Campo,0,aAlfa1;
 |SI aAlfa1 = "N"; |ACABA; |FINSI;
 nNume1 = Campo - 1;
 |SI #0Campo < #0nNume1; |ERROR; |FINSI;
|FCAL;

|CALCULO Parrilla; |TIPO 0;
 |C_M #0Campo,0,aAlfa1;
 |SI aAlfa1 = "N"; |ACABA; |FINSI;
   |SI Campo = 10;
      |SI #0#10 = "N";
          #0#11 = 001;  |PINTA #0#11;  |C_M #0#11, 1, "N";
          #0#12 = 999;  |PINTA #0#12;  |C_M #0#12, 1, "N";
          |PARA nCampo = 13;  |SI nCampo [ 28;  |HACIENDO nCampo = nCampo + 1;
                |C_M #0nCampo, 1, "S";
          |SIGUE;
      |SINO;
          |C_M #0#11, 1, "S";
          |C_M #0#12, 1, "S";

          |PARA nCampo = 13;  |SI nCampo [ 28;  |HACIENDO nCampo = nCampo + 1;
                |C_M #0nCampo, 1, "N";
                #0nCampo = 0;  |PINTA #0nCampo;
          |SIGUE;
      |FINSI;
  |SINO;
      |SI #0#37 = "N";
          #0#38 = 001;  |PINTA #0#38;  |C_M #0#38, 1, "N";
          #0#39 = 999;  |PINTA #0#39;  |C_M #0#39, 1, "N";
          |PARA nCampo = 40;  |SI nCampo [ 55;  |HACIENDO nCampo = nCampo + 1;
                |C_M #0nCampo, 1, "S";
          |SIGUE;
      |SINO;
          |C_M #0#38, 1, "S";
          |C_M #0#39, 1, "S";

          |PARA nCampo = 40;  |SI nCampo [ 55;  |HACIENDO nCampo = nCampo + 1;
                |C_M #0nCampo, 1, "N";
                #0nCampo = 0;  |PINTA #0nCampo;
          |SIGUE;
      |FINSI;
  |FINSI;
|FCAL;

|CALCULO NoMasConcep; |TIPO 0;
    |C_M #0Campo,0,aAlfa1;
    |SI aAlfa1 = "N"; |ACABA; |FINSI;
    |SI Campo [ 28; nNume1 = 28; |SINO; nNume1 = 55; |FINSI;
    |SI #0Campo = 0;
          |PARA nCampo = Campo + 1;  |SI nCampo [ nNume1; |HACIENDO nCampo = nCampo + 1;
                #0nCampo = 0;  |C_M #0nCampo, 1, "N";
          |SIGUE;
    |SINO;
          |PARA nCampo = Campo + 1;  |SI nCampo [ nNume1; |HACIENDO nCampo = nCampo + 1;
                |C_M #0nCampo, 1, "S";
          |SIGUE;
    |FINSI;
|FCAL;

|CALCULO LeeLibro; |TIPO 0;
   |C_M #0Campo,0,aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   |DDEFECTO #5;
   |ABRE #5;
   #5#0 = #0Campo;
   |LEE 000#5=;
   |CIERRA #5;

   |SI Campo = 29;
       |SI #5#2 ! "R";
           |MENAV "    ATENCION!!! Libro de I.V.A. Soportado ...";
           |ERROR;
           |ACABA;
       |FINSI;
   |SINO;
       |SI #5#2 ! "S";
           |MENAV "    ATENCION!!! Libro de I.V.A. Repercutido ...";
           |ERROR;
           |ACABA;
       |FINSI;
   |FINSI;
   |ABRE #camaniva;
   #camaniva#0 = #0Campo;
   #camaniva#1 = 1;
   |LEE 041#camaniva.];
   |SI FSalida = 0; |Y #camaniva#0 = #0Campo;
       |CONFI "0000NLibro de IVA con Movimientos. Esta seguro de continuar";
       |SI FSalida ! 0;
           |ERROR;
       |FINSI;
   |FINSI;
   |CIERRA #camaniva;
|FCAL;

|CALCULO Ehque; |TIPO 7;
 |C_M #0Campo,0,aAlfa1;
 |SI aAlfa1 = "N"; |ACABA; |FINSI;

 |SI enQSerie = 0;
     |C_M #0#30,1,"N"; #0#30 = "";      |PINTA #0#30;
     |C_M #0#57,1,"N"; #0#57 = "";      |PINTA #0#57;
 |SINO;
     |C_M #0#30,1,"S";
     |C_M #0#57,1,"S";
 |FINSI;
|FCAL;

|CALCULO BuscaNum; |TIPO 7;
   |C_M #0Campo,0,aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   |SI Campo = 31
       aTipoIVA = "R"; nNume1 = 29; nNume2 = 30; nNume3 = 77;
   |SINO;
       aTipoIVA = "S"; nNume1 = 56; nNume2 = 57; nNume3 = 78;
   |FINSI;

   |SI #200#44 = "N";
       |ABRE #5;
       #5#0 = #0nNume1;
       |LEE 001#5=;
       |SI FSalida = 0;
           #0Campo = #5#4; |PINTA #0Campo;
           #0nNume3 = #5#3;
       |FINSI;
       |CIERRA #5;
   |SINO;
       |ABRE #4;
       #4#0 = #0nNume2;
       #caivases#1 = aTipoIVA;
       |LEE 001#4=;
       |SI FSalida = 0;
           #0Campo = #4#3; |PINTA #0Campo;
           #0nNume3 = #4#4;
       |FINSI;
       |CIERRA #4;
   |FINSI;
|FCAL;

|CALCULO TrasConcepto;  |TIPO 0;
   |C_M #0Campo,0,aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   nNume1 = Campo - 3;
   enLibIVA  = #0nNume1;
   enFilConc = 2;          || Concepto de IVA
   enConcepto = #0Campo;
   |HAZ FiltroConcepto;
   |SI eswLect ! 0; |ACABA; |FINSI;

   nNume1 = Campo + 1;
   #0nNume1 = #dsmom030#1; |PINTA #0nNume1;
   |SI Campo = 32; #0#75 = eaCtaIVA; |FINSI;
   |SI Campo = 59; #0#76 = eaCtaIVA; |FINSI;
|FCAL;

|CALCULO Alfinal; |TIPO 7;
   |C_M #0Campo,0,aAlfa1;
   |SI aAlfa1 = "S";  |Y eaSobreEs ! "S"; |PTEC 816; |FINSI;
|FCAL;

|| *************************************************************************
|| *************************************************************************
|| *************************************************************************
|PROCESO LaFactura;
 |ABRE #10;
 #10#0 = nLibro;
 #10#1 = nElDoc;
 |LEE 001#10=;
 |SI FSalida = 0;
     |MENAV "    Error, Registros Duplicados. Proceso Abortado.";
     |CIERRA #10;
     |ACABA;
 |FINSI;

 |DDEFECTO #10;
 #10#0   = nLibro;
 #10#1   = nElDoc;
 #10#2   = aSerie;
 #10#3   = nFactu;
 #10#4   = fFecha;
 |HAZ Periodo;
 #10#5  = nPeriodo;
 #10#7  = #1#0;
 #10#8  = fFecha;
 #10#9  = -1;          || Cuidado
 #10#10 = #200#19;
 #10#11 = #200#67;
 #10#12 = #0#61;
 |HAZ LeeCliPro;
 #10#13 = aTitCuenta;
 #10#14 = eaCodNif;
 #10#24 = eaNombreAct;
 #10#25 = eaNomSubdep;
 #10#27 = nConcep;
 #10#28 = aDescri;
 #10#29 = nConcep;
 #10#30 = aDescri;
 #10#36 = aTipoIVA;

 |GRABA 020#10b;

 |ABRE #11;
 |DDEFECTO #11;
 #11#0 = #10#0;
 #11#1 = #10#1;
 #11#2 = 1;
 #11#3 = #10#27;
 #11#4 = #10#28;
 #11#6 = nImporte;
 #11#12 = aCtaContra;
 #11#16 = 0;
 |HAZ  BuscaIVA;
 #11#30 = #10#10;
 #11#31 = #10#11;
 |GRABA 020#11n;

 #10#19 = #10#19 + #11#6;
 #10#15 = #10#15 + #11#6;
 #10#17 = #10#15 %  #10#16;
 #10#20 = #10#20 + #11#22;
 #10#23 = #10#23 + (#11#8 + #11#10);
 #10#26 = #10#26 + #11#11;
 #10#21 = #10#19 + #10#23 - #10#26 - #10#20;

 |CIERRA #11;

 |GRABA 020#10a;
 |LIBERA #10;
 |CIERRA #10;
 nElDoc = nElDoc + 1;
 nFactu = nFactu + nIncre;;
 swc = 1;
|FPRC;

|PROCESO GraboRegistros;
   |SI #0#6 = "T";
       fFecha   = #0#74;
       nImporte = #0#62 + #0#63 + #0#64 + #0#65 + #0#66 + #0#67 + #0#68;
       nImporte = nImporte + #0#69 + #0#70 + #0#71 + #0#72 + #0#73;
       |HAZ LaFactura;
   |SINO;
       |PARA nPara1 = 62; |SI nPara1 [ 73; |HACIENDO nPara1 = nPara1 + 1;
             |SI #0nPara1 ! 0;
                 nImporte = #0nPara1;
                 |HAZ LaFecha;
                 |HAZ LaFactura;
             |FINSI;
       |SIGUE;
   |FINSI;
|FPRC;

|PROCESO grabaiva;

   |SI aSigno ! "X";
       |SI aSigno ! #1#8; |ACABA; |FINSI;
   |FINSI;

   |SI #0nSCon = "N";
       nSwSel = 0;
       nNume2 = nSCon + 3; nNume3 = nSCon + 18;
       |PARA nNume1 = nNume2; |SI nNume1 [ nNume3; |HACIENDO nNume1 = nNume1 + 1;
             |SI #0nNume1 ! 0; |Y #0nNume1 = #1#6;
                  nSwSel = 1;
                  |SAL;
             |FINSI;
       |SIGUE;
       |SI nSwSel = 0; |ACABA; |FINSI;
   |FINSI;

   |SI sw = 0;
       #0#61 = #1#4;
       |PDEFECTO #0,62,74;
   |FINSI;

   |SI #0#61 ! #1#4;
       |HAZ GraboRegistros;
       #0#61 = #1#4;
       |PDEFECTO #0,62,74;
   |FINSI;

   aAlfa1 = #1#1;
   aAlfa1 = aAlfa1 % 402;  || Mes del asiento

   nNume4 = aAlfa1;
   nNume4 = nNume4 + 61;

   nParcial = #1#9 ! EURODB;

   |SI aTipoIVA = "R";
       |SI #1#8 = "H";    || ... Apunte Cliente al haber
           nParcial = - nParcial;
       |FINSI;
   |SINO;
       |SI #1#8 = "D";    || ... Apunte Proveedor al Debe
           nParcial = - nParcial;
       |FINSI;
   |FINSI;

   #0nNume4 = #0nNume4 + nParcial;
   sw = 1;
|FPRC;

|DEFBUCLE lee_diario;
   #1#3;
   0,6;
   aDCuenta, #0#2, #0#4, 0001, #0#0, nDConcepto;
   aHCuenta, #0#3, #0#5, 9999, #0#1, nHConcepto;
   e;
   NULL, grabaiva;
|FIN;

|PROCESO CrearFra;

   |HAZ BuscaNumero;
   |HAZ Departamento;

   aDCuenta = #0nDCta;
   aHCuenta = #0nHCta;
   |SI #0nSCon = "S";
       nNume1 = nSCon + 1; nNume2 = nSCon + 2;
       nDConcepto = #0nNume1;
       nHConcepto = #0nNume2;
   |SINO;
       nDConcepto = 001;
       nHConcepto = 999;
   |FINSI;
   sw       = 0;
   #0#61    = "";
   |BUCLE lee_diario;
   |SI sw = 1;
       |HAZ GraboRegistros;
   |FINSI;
|FPRC;

|| *************************************************************
|CALCULO recupera; |TIPO 3;

   |SI #0#81 ! "SI"; |ACABA; |FINSI;

   swc    = 0;
   |SI #0#7 = "S";
       || ... Proceso Iva Repercutido
       nDCta    = 8;  nHCta = 9;
       nSCon    = 10;
       aTipoIVA = "R";
       nLibro   = #0#29;
       aSerie   = #0#30;
       nFactu   = #0#31;
       nConcep  = #0#32;
       aDescri  = #0#33;
       aCtaContra = #0#75;
       nIncre     = #0#77;
       aSigno   = #0#79;
       |HAZ CrearFra;
       #0#31    = nFactu;
   |FINSI;
   |SI #0#34 = "S";
       || ... Proceso Iva Soportado
       nDCta    = 35;  nHCta = 36;
       nSCon    = 37;
       aTipoIVA = "S";
       nLibro   = #0#56;
       aSerie   = #0#57;
       nFactu   = #0#58;
       nConcep  = #0#59;
       aDescri  = #0#60;
       aCtaContra = #0#76;
       nIncre     = #0#78;
       aSigno   = #0#80;
       |HAZ CrearFra;
       #0#58    = nFactu;
   |FINSI;
   |SI swc ! 0;
       |HAZ ActIVA;
   |FINSI;
|FCAL;

|| *************************************************************************
|| *************************************************************************
|PROCESO BuscaNumero;
   |ABRE #10;
   #10#0 = nLibro;
   #10#1 = 9999999999;
   |LEE 010#10];
   |SI FSalida = 0;
       |LEE 040#10a;
   |SINO;
       |LEE 040#10u;
   |FINSI;
   nElDoc = 1;
   |SI FSalida = 0; |Y #10#0 = nLibro;
       nElDoc = #10#1 + 1;
   |FINSI;
   |CIERRA #10;
|FPRC;

|PROCESO Departamento;
   eOpDep = 0;
   enActividad = #200#19;
   eaCodDep    = #200#19;
   |HAZ Actividad;
   eOpSub = 0;
   enConcepto  = nConcep;
   eaCodSubdep = #200#67;
   |HAZ OtroSubActividad;
|FPRC;

|PROCESO Periodo;
  aAlfa1 = #10#4;
  aAlfa1 = aAlfa1 % 402;
  nNume1 = aAlfa1;
  nPeriodo = nNume1;
  |SI #200#46 = "A"; nPeriodo = 0; |FINSI;
  |SI #200#46 = "T"; |Y enEjercicio [ 2009;
      nPeriodo = 1;
      |SI nNume1 ] 4;  nPeriodo = 2; |FINSI;
      |SI nNume1 ] 7;  nPeriodo = 3; |FINSI;
      |SI nNume1 ] 10; nPeriodo = 4; |FINSI;
  |FINSI;
|FPRC;

|PROCESO LeeCliPro;
  |ABRE #7;
  aTitCuenta = "";
  eaCodNif   = "";
  #7#23 = "P";
  aAlfa1 = #10#12 % 0102; |QBF aAlfa1;
  |SI aAlfa1 = "43"; |O aAlfa1 = "44"; #7#23 = "C"; |FINSI;
  eaCuenta = #10#12; |HAZ SacaNivel;
  #7#10 = #10#12;
  #7#11 = enNivel;
  |LEE 010#7=;
  |SI FSalida = 0;
      aTitCuenta  = #7#1;
      eaCodNif    = #7#9;
  |SINO;
      |ABRE #6;
      #6#0 = #10#12;
      |LEE 001#6=;
      |SI FSalida = 0;
          aTitCuenta = #6#2;
      |FINSI;
      |CIERRA #6;
  |FINSI;
  |CIERRA #7;
|FPRC;

|PROCESO BuscaIVA;
  efFechaIVA = #camaniva#4; enLineaIVA = 0;
  enPorcIVA = #caivalin#7;  enPorcREC = #caivalin#9;
  eaCtaIVA  = #caivalin#13; eaCtaREC  = #caivalin#14;
  |HAZ BuscaTipoIVA;
  #11#16 = enLineaIVA;
  #11#5  = eaIVADeduc; #11#17 = eaIVAInver;
|FPRC;

|PROCESO LaFecha;
 aAlfa2 = enEjercicio;
 |SI nPara1 = 62; aAlfa3 = "31.01." + aAlfa2; |FINSI;
 |SI nPara1 = 63; aAlfa3 = "28.02." + aAlfa2; |FINSI;
 |SI nPara1 = 64; aAlfa3 = "31.03." + aAlfa2; |FINSI;
 |SI nPara1 = 65; aAlfa3 = "30.04." + aAlfa2; |FINSI;
 |SI nPara1 = 66; aAlfa3 = "31.05." + aAlfa2; |FINSI;
 |SI nPara1 = 67; aAlfa3 = "30.06." + aAlfa2; |FINSI;
 |SI nPara1 = 68; aAlfa3 = "31.07." + aAlfa2; |FINSI;
 |SI nPara1 = 69; aAlfa3 = "31.08." + aAlfa2; |FINSI;
 |SI nPara1 = 70; aAlfa3 = "30.09." + aAlfa2; |FINSI;
 |SI nPara1 = 71; aAlfa3 = "31.10." + aAlfa2; |FINSI;
 |SI nPara1 = 72; aAlfa3 = "30.11." + aAlfa2; |FINSI;
 |SI nPara1 = 73; aAlfa3 = "31.12." + aAlfa2; |FINSI;
 fFecha = aAlfa3;
|FPRC;
|| ////////////////////////////////////////////////////////////////////

|PROCESO ActIVA;
   aTipoIVA = "R"; nNume1 = 29; nNume2 = 30; nNume3 = 31;
   |HAZ ActIVA1;
   aTipoIVA = "S"; nNume1 = 56; nNume2 = 57; nNume3 = 58;
   |HAZ ActIVA1;
|FPRC;

|PROCESO ActIVA1;

   |SI #200#44 = "N";
       |ABRE #5;
       #5#0 = #0nNume1;
       |LEE 001#5=;
       |SI FSalida = 0;
           #5#4 = #0nNume3 + #5#3;
           |GRABA 020#5a;
           |LIBERA #5;
       |FINSI;
       |CIERRA #5;
   |SINO;
       |ABRE #4;
       #4#0 = #0nNume2;
       #caivases#1 = aTipoIVA;
       |LEE 001#4=;
       |SI FSalida = 0;
           #4#3 = #0nNume3 + #4#4;
           |GRABA 020#4a;
           |LIBERA #4;
       |FINSI;
       |CIERRA #4;
   |FINSI;
|FPRC;



|PROGRAMA;
 |CLS;
 |CUADRO 0501 2380;
 |BLANCO 0602 2279;
 |PINTA 1320, "Introduzca Password : ";
 E_inf = "";
 E_sup = "20";
 aPassword = 1342 ? -1;
 |SI aPassword ! "<MYPASWD>     ";
     |MENAV "     Permiso denegado.";
     |ACABA;
 |FINSI;
 |MANTE #0;
|FPRO;
