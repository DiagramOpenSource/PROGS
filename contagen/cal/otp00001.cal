|FICHEROS;
 otp00001 #0;
 otp00002 #1;
 cacuenta #2;

 imagen@  #901;
|FIN;

|VARIABLES;
 &entrada = 1;
 nModoEntradaC = 0;
 nModoEntradaL = 0;
 aAlfa1 = "";

 aDirInicio = "";
 aMas       = "";
 aDef901    = "";
 nSwExi     = 0;
 aLaCuenta  = "";
 nSaldo     = 0;
 nTSaldo    = 0;
 nSwOper    = 0;
|FIN;

|INCLUYE dscont_i;

|| *********************************************************************

|PROCESO ElTipo8; |TIPO 8;
  |HAZ inicio;

  |DBASE aDirInicio;
  aMas  = aDirInicio + "def/otp00002";
  aDef901 = aMas;
  |CARGA_DEF aMas, imagen@;
  |SI FSalida ! 0;
       aDef901 = "";
       |MENAV "    Error Cargado Fichero de Contabilidad. Proceso Interrumpido";
       |PTEC 807;
  |FINSI;

|FPRC;

|PROCESO ElTipo9; |TIPO 9;
 |SI aDef901 ! ""; |DESCARGA_DEF #901; aDef901 = ""; |FINSI;
|FPRC;

|CALCULO cuenta_c; |TIPO 6;
     salidas = FSalida;

     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;
|FCAL;

|CALCULO cuefin_c; |TIPO 0;

   nModoEntradaC = (FEntrada / 100) ! 0;
   |SI nModoEntradaC ] 1; |Y nModoEntradaC [ 3;
       aAlfa1 = #0Campo;
       |QBF aAlfa1;
       |SI aAlfa1 = "";
           |ERROR;
           |ACABA;
       |FINSI;
   |FINSI;

   emCta    = 1;
   eaCuenta = #0Campo;
   |HAZ SacaNivel;
   |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea
   #0#1 = enNivel;

   |HAZ LeeCuenta;
   |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea

   #0#2 = #cacuenta#2;  |PINTA #0#2;
   nSaldo = #cacuenta#53;
   |SI #cacuenta#5 = "P"; |O #cacuenta#5 = "H";
       nSaldo = - #cacuenta#53;
   |FINSI;
   #0#5 = nSaldo; |PINTA #0#5;
|FCAL;

|| *************************************

|PROCESO SumoTP100;
 #2#0 = #1#2;
 |LEE 001#2=;
 |SI FSalida = 0;
     nSaldo = #2#53;
     |SI #2#5 = "P"; |O #2#5 = "H";
         nSaldo = - #2#53;
     |FINSI;
     |SI nSwOper = 0;
         nTSaldo = nTSaldo +  nSaldo;
     |SINO;
         #1#5 = ((nSaldo * 100) / nTSaldo) ! 2;
         #1#6 = nSaldo;
         |GRABA 020#1a;
     |FINSI;
 |FINSI;
 |LIBERA #1;
|FPRC;

|DEFBUCLE SumoTP100;
 #1#1;
 ;
 #0#0, 001;
 #0#0, 999;
 be;
 NULL, SumoTP100;
|FIN;

|RUTINA ClaveA1;
 #1#0 = #0#0;
 #1#1 = 001;
|FRUT;
|RUTINA ClaveB1;
 #1#0 = #0#0;
 #1#1 = 999;
|FRUT;

|CALCULO grupo3; |TIPO 3;

 |ABRE #2;
 nTSaldo = 0;
 nSaldo  = 0;
 nSwOper = 0;
 |BUCLE SumoTP100;

 nSwOper = 1;
 |BUCLE SumoTP100;

 #0#3 = 100;
 #0#4 = nTSaldo;

 |DDEFECTO #2;
 #2#0 = #0#0;
 |LEE 001#2=;
 nSaldo = #2#53;
 |SI #2#5 = "P"; |O #2#5 = "H";
     nSaldo = - #2#53;
 |FINSI;

 #0#2 = #2#2;
 #0#5 = nSaldo;
 |CIERRA #2;

 |PINDA #0#0;
 |ENTLINEAL #1#1, -2, 7, 19, 0, ClaveA1, ClaveB1;
|FCAL;

|| **************************************************************************
|CALCULO cuenta_l; |TIPO 6;
     salidas = FSalida;

     eaCuenta = #1Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #1Campo = eaCuenta;
     |PINTA #1Campo;
|FCAL;

|PROCESO LaImagen;
 |SI #1#1 ! #901#1;
     nSwExi = 1;
 |FINSI;
|FPRC;

|DEFBUCLE LaImagen;
 #901#1002;
 2;
 #0#0, 001, aLaCuenta;
 #0#0, 999, aLaCuenta;
 ;
 NULL, LaImagen;
|FIN;

|CALCULO cuefin_l; |TIPO 0;

   |SI #1Campo = #0#0;
       |MENAV "    Error, la Cuenta introducida es la de la Cabecera";
       |ERROR;
       |ACABA;
   |FINSI;

   nSwExi = 0;
   aLaCuenta = #1Campo;
   |BUCLE LaImagen;
   |SI nSwExi = 1;
       |MENAV "    Error. Cuenta Ya Introducida ";
       |ERROR;
       |ACABA;
   |FINSI;

   emCta    = 1;
   eaCuenta = #1Campo;
   |HAZ SacaNivel;
   |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea
   #1#3 = enNivel;

   |HAZ LeeCuenta;
   |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea

   #1#4   = #cacuenta#2; |PINTA #1#4;
   nSaldo = #cacuenta#53;
   |SI #cacuenta#5 = "P"; |O #cacuenta#5 = "H";
       nSaldo = - #cacuenta#53;
   |FINSI;
   #1#6 = nSaldo; |PINTA #1#6;
|FCAL;

|CALCULO sumar; |TIPO 2;
  #0#3 = #0#3 +. #1#5;
  #0#4 = #0#4 +. #1#6; |PINTA #0#4;
|FCAL;
