|FICHEROS;
  ca8z0006 #10;

  cacuenta #1;
  canempre #30;

  cacambiw #90;   || def. trabajo
  cacambix #91;   || tmp. trabajo

  ca8ctrle #400;  ||Control Empresa
  ca8m0003 #403;  ||Tabla de Esta Empresa

  :/basica/def/dspase22 #22;
|FIN;

|VARIABLES;
 nEjer    = 0;
 nSwError = 0;
 error1   = "0000Nivel repetido o valor mas bajo que los anteriores";
 aAlfa1   = "";
 aAlfa2   = "";
 aAlfa3   = "";
 aAlfa4   = "";
 nPara1   = 0;
 nNume1   = 0;
 nNume2   = 0;
 nNume3   = 0;
 nNume4   = 0;

 &eaDirFich = "";
 nUlt1      = 0;
 nUlt2      = 0;
 nLongi     = 0;
 nListo     = 0;
 nA_nivels  = 0;
 nD_nivels  = 0;
 nA_cuentas = 0;
 nD_cuentas = 0;

 nAnterior  = 0;
 aCampo9    = "";
 &nCambio   = 0;
 eSwCambio  = 0;
 eaPreg22_6 = "";
 aPathPlan  = "";
 &eDNum2008 = 0;
 aParam     = "";
 &eaRevis   = "";
 nSwGrabar  = 0;
|FIN;

|INCLUYE dscont_i;

|| ************ Procesos pantalla
|PROCESO Antes8; |TIPO 7;
 |C_M #10#8,1, "S";
 |SI nAnterior = 1;
     |C_M #10#8,1, "N";
     #10#8 = 1; |PINTA #10#8;
 |FINSI;
|FPRC;

|PROCESO Despues8; |TIPO 0;
 nNume1   = 17 + #10#17;
 |SI #10#8 ! 2;
     #10nNume1 = #10#24;
 |SINO;
     #10nNume1 = #10#25;
 |FINSI;
 |PINTA #10nNume1;
|FPRC;

|CALCULO nivel;|TIPO 7;
   |C_M #10#18, 1,"S";  |C_M #10#19, 1,"S";
   |C_M #10#20, 1,"S";  |C_M #10#21, 1,"S";
   |C_M #10#22, 1,"S";  |C_M #10#23, 1,"S";

   |SI #10#17 < 6;
      |C_M #10#23, 1,"N"; #10#23 = 0; |PINTA #10#23;
      |SI #10#17 < 5;
          |C_M #10#22, 1,"N"; #10#22 = 0; |PINTA #10#22;
          |SI #10#17 < 4;
              |C_M #10#21, 1,"N"; #10#21 = 0; |PINTA #10#21;
              |SI #10#17 < 3;
                  |C_M #10#20, 1, "N"; #10#20 = 0; |PINTA #10#20;
                  |SI #10#17 < 2;
                      |C_M #10#19, 1,"N"; #10#19 = 0; |PINTA #10#19;
                  |FINSI;
              |FINSI;
           |FINSI;
      |FINSI;
   |FINSI;
|FCAL;

|CALCULO nivelrep; |TIPO 0;
   |SI Campo = 19;  |Y #10#19 = 0; |Y #10#17 ] 2;
       |MENAV error1;
       |ERROR;
       |ACABA;
   |FINSI;

   |SI Campo = 20;  |Y #10#20 = 0; |Y #10#17 ] 3;
       |MENAV error1;
       |ERROR;
       |ACABA;
   |FINSI;
   |SI Campo = 21;  |Y #10#21 = 0; |Y #10#17 ] 4;
      |MENAV error1;
      |ERROR;
      |ACABA;
   |FINSI;
   |SI Campo = 22;  |Y #10#22 = 0; |Y #10#17 ] 5;
      |MENAV error1;
      |ERROR;
      |ACABA;
   |FINSI;
   |SI Campo = 23;  |Y #10#23 = 0; |Y #10#17 ] 6;
      |MENAV error1;
      |ERROR;
      |ACABA;
   |FINSI;

   |SI Campo = 19; |Y #10#19 ! 0;    || Si hay dos niveles iguales o menor error
      |SI #10#19 [ #10#18;
         |MENAV error1;
         |ERROR;
         |ACABA;
      |FINSI;
   |FINSI;

   |SI Campo = 20; |Y #10#20 ! 0; || Si hay dos niveles iguales o menor error
      |SI #10#20 [ #10#18; |O #10#20 [ #10#19;
         |MENAV error1;
         |ERROR;
         |ACABA;
      |FINSI;
   |FINSI;

   |SI Campo = 21; |Y #10#21 ! 0; || Si hay dos niveles iguales o menor error
      |SI #10#21 [ #10#18; |O #10#21 [ #10#19; |O #10#21 [ #10#20;
         |MENAV error1;
         |ERROR;
         |ACABA;
      |FINSI;
   |FINSI;

   |SI Campo = 22; |Y #10#22 ! 0; || Si hay dos niveles iguales o menor error
      |SI #10#22 [ #10#18; |O #10#22 [ #10#19; |O #10#22 [ #10#20; |O #10#22 [ #10#21;
         |MENAV error1;
         |ERROR;
         |ACABA;
      |FINSI;
   |FINSI;

   |SI Campo = 23; |Y #10#23 ! 0;    || Si hay dos niveles iguales o menor error
      |SI #10#23 [ #10#18; |O #10#23 [ #10#19; |O #10#23 [ #10#20; |O #10#23 [ #10#21; |O #10#23 [ #10#22;
         |MENAV error1;
         |ERROR;
         |ACABA;
      |FINSI;
   |FINSI;
|FCAL;
||///////////////////////////////////////////////////////////////////////////

|PROCESO caso2;
  |SI nListo ! 0;  |ACABA;  |FINSI;
  nNume1 = enNivel;     || si es el ultimo nivel

  |SI enNivel = nA_nivels; |Y #90nNume1 < #90nD_nivels;  ||/y los digitos del
                                                       ||/ultimo nivel destino
                                                       ||/son mayores
      nNume2 = #90nD_nivels - #90nNume1;
      aAlfa1 = "0" * nNume2; ||digitos que hacen falta para rellenar
      aAlfa2 = eaCuenta;    |QBF aAlfa2;

      |DDEFECTO #91;
      #91#0 = eaCuenta;         || cuenta actual
      #91#1 = enNivel;         || nivel actual

      |SI eaPreg22_6 ! "N";
          nNume3 = nA_nivels - 1;        || el nivel anterior
          nNume4 = #90nNume3;            || el nivel anterior
          nNume4 = 100 + nNume4;
          aAlfa3 = nNume4; |QBF aAlfa3;  || digitos del principio

          nNume4 = #90nNume3;            || el nivel anterior
          nNume4 = #90nNume1 - nNume4;   ||Diferencia entre anterior y este
          nNume4 = 0 - (100 + nNume4);   || parte posterior
          aAlfa4 = nNume4;

          #91#2 = (aAlfa2 % aAlfa3) + aAlfa1 + (aAlfa2 % aAlfa4);
      |SINO;
          #91#2 = aAlfa2 + aAlfa1; || cuenta destino ||al final
      |FINSI;

      #91#3 = nD_nivels - 10; || nivel destino
      nD_cuentas  = nD_cuentas + 1;
      #91#4 = nD_cuentas + 10000;
      #91#5 = eaNomCta;         || nombre cuenta
      #91#6 = #91#2;
      |GRABA 020#91n;
      nListo = 1;
      eSwCambio = 0;
   |FINSI;
|FPRC;

|PROCESO caso1;
 |SI nListo ! 0;  |ACABA;  |FINSI;

 |PARA nPara1 = 11; |SI nPara1 [ 16; |HACIENDO nPara1 = nPara1 + 1;
       |SI nLongi = #90nPara1;
           nListo = 1;
           |SAL;
       |FINSI;
 |SIGUE;
|FPRC;

|PROCESO LeoTabla;
   |SI nCambio = 1;
       eaCuenta = #403#2;
       eaNomCta = #403#3;
   |SINO;
       eaCuenta = #403#0;
       eaNomCta = #403#1;
   |FINSI;

   aAlfa1 = eaCuenta; |QBF aAlfa1;
   aAlfa2 = aAlfa1 % 0;
   nLongi = aAlfa2;
   enNivel = 0;
   |SI nLongi = #10#18; enNivel = 1; |FINSI;
   |SI nLongi = #10#19; enNivel = 2; |FINSI;
   |SI nLongi = #10#20; enNivel = 3; |FINSI;
   |SI nLongi = #10#21; enNivel = 4; |FINSI;
   |SI nLongi = #10#22; enNivel = 5; |FINSI;
   |SI nLongi = #10#23; enNivel = 6; |FINSI;
   |SI enNivel = 0; |ACABA; |FINSI;

   nListo = 0;        || switch para saber si la cuenta esta cambiada

   |HAZ caso1;         || cambia nivel, no cambia Longitud
                       ||/no cambia nivel, no cambia longitud

   |HAZ caso2;         || cambia nivel, cambia longitud
                       ||/no cambia nivel, cambia longitud
|FPRC;


|DEFBUCLE LeoTabla;
 #403#1;
 ;
 "            ";
 "zzzzzzzzzzzz";
 e;
 NULL, LeoTabla;
|FIN;


|PROCESO PreparaCambio;

   |ATRI P; |PINTA 2301, " Preparando ... ";    |ATRI 0;
   |DELINDEX #91;

   #90#0  = #10#17;
   #90#1  = #10#18; #90#2  = #10#19;
   #90#3  = #10#20; #90#4  = #10#21;
   #90#5  = #10#22; #90#6  = #10#23;
   nA_nivels = #90#0;

   #90#10 = #10#10;
   #90#11 = #10#11; #90#12 = #10#12;
   #90#13 = #10#13; #90#14 = #10#14;
   #90#15 = #10#15; #90#16 = #10#16;
   nD_nivels = #90#10 + 10;

   |ABRE #91;
   |BUCLE LeoTabla;
   |CIERRA #91;

   |BLIN 23;
|FPRC;

|PROCESO ElCambio;
 eSwCambio = 0;
 aCampo9 = #10#9;
 |ABRE #91;
 |LEE 001#91p;
 |SI FSalida ! 0;
     |SI aParam = "";
         eSwCambio = 2;
         aAlfa1 = "    SNo existe Tabla Cambio Nivel en Empresa de " + enEjercicio + ". Crearla";
         |CONFI aAlfa1;
         |SI FSalida = 0;
             #10#9 = "S";
         |FINSI;
     |SINO;
         eSwCambio = 1;
     |FINSI;
 |FINSI;
 |CIERRA #91;

 |SI eSwCambio = 2; |ACABA; |FINSI;
 |SI eSwCambio = 1; |HAZ PreparaCambio; |FINSI;
 |SI #10#9 = "S"; |Y eSwCambio = 0;
     |SUB_EJECUTA_NP ":contagen/cacambix;PaseCa8";
 |FINSI;
 |SI eSwCambio = 0;
     |SUB_EJECUTA_NP ":contagen/cacambi2;PaseCa8";
 |SINO;
     |SI aParam = ""; |MENAV "    Las Cuentas en la Tabla, no necesitan cambio de Nivel"; |FINSI;
 |FINSI;
 #10#9 = aCampo9;
|FPRC;

|PROCESO LaActual;
 nCambio = 1;  ||columna actual
 |PATH_DAT #91 fich_alta;
 eaDirFich   = fich_alta;
 enEjercicio = #10#3;
 |HAZ ElCambio;
|FPRC;

|PROCESO LaAnterior;
 nCambio = 2;  ||columna actual
 |PATH_DAT #91 aPathPlan;
 eaDirFich = aPathPlan;
 enEjercicio = #10#7;
 |HAZ ElCambio;
|FPRC;

|PROCESO HagoTodo;
  nEjer = enEjercicio;
  |PATH_DAT #403 fich_alta;  ||la tabla siempre en la del 2008

  |SI #10#8 ! 2;
      |HAZ LaActual;
  |FINSI;

  |SI #10#8 ! 1;
      |HAZ LaAnterior;
  |FINSI;
  enEjercicio = nEjer;
|FPRC;

|PROCESO LaEmpresaAnterior;
  nAnterior = 1;
  |ABRE #30;
  #30#2  = #10#4;
  #30#3  = #10#5;
  |LEE 001#30=;
  |SI FSalida = 0; |Y #30#26 = 7;
      nAnterior = 0;
      aAlfa1 = #30#2; aAlfa1 = ("00000" + aAlfa1) % -105;
      aAlfa2 = #30#3; aAlfa2 = ("0"     + aAlfa2) % -101;
      |DFICB aPathPlan;
      aPathPlan = aPathPlan + aAlfa1 + aAlfa2 + "/";
  |SINO;
      |SELECT #4#30;
      #30#2   = #10#4;
      #30#26  = 7;
      |LEE 001#30=;
      |SI FSalida = 0;
          #10#5 = #30#3;
          nSwGrabar = 1;
          nAnterior = 0;
          aAlfa1 = #30#2; aAlfa1 = ("00000" + aAlfa1) % -105;
          aAlfa2 = #30#3; aAlfa2 = ("0"     + aAlfa2) % -101;
          |DFICB aPathPlan;
          aPathPlan = aPathPlan + aAlfa1 + aAlfa2 + "/";
     |FINSI;
     |SELECT #1#30;
  |FINSI;
  |CIERRA #30;
|FPRC;

|PROGRAMA;

   aParam = PARAMETRO; |QBF aParam;
   |SI aParam = "";
       |HAZ inicio;
       |SI enEjercicio ! 2008;
           aAlfa1 = "    Proceso para el Ejercicio 2008. No ejecutable en el " + enEjercicio;
           |MENAV aAlfa1;
           |ACABA;
       |FINSI;
   |FINSI;

   |SI aParam ! "Pase8";
       |QBF fich_alta;
       |SI fich_alta = ""; |DFICE fich_alta; |FINSI;  || fich actual
   |SINO;
       fich_alta = eaDirFich;
       |PATH_DAT #403 fich_alta;
   |FINSI;

   |DBASS aBase;
   aBase = aBase + "basica/fich/";
   |PATH_DAT #22 aBase;
   |ABRE #22;
   |DDEFECTO #22;
   #22#0 = "A";
   |LEE 001#22=;
   |CIERRA #22;
   eaPreg22_6 = #22#6;

   nSwGrabar = 0;
   |DDEFECTO #10;
   |ABRE #400;
   |DDEFECTO #400;
   #400#0 = enEmpresa;
   #400#1 = enPeriodo;
   |LEE 101#400=;
   |SI FSalida ! 0;
       |DDEFECTO #400;
       #400#0 = enEmpresa;
       #400#1 = enPeriodo;
       #400#2 = eDNum2008;
       #400#3 = enEmpresa;
       #400#4 = enPeriodo - 1; |SI #400#4 < 0; #400#4 = 9; |FINSI;
       nSwGrabar = 1;
   |FINSI;
   |LIBERA #400;
   |CIERRA #400;

   |ABRE #403;
   #403#0 = "";
   |LEE 040#403];
   |SI FSalida ! 0;
       |SI aParam = ""; |MENAV "    No existe Tabla de Equivalencia. "; |FINSI;
       |CIERRA #403;
       |ACABA;
   |FINSI;
   |CIERRA #403;

   #10#0 = enEmpresa;
   #10#1 = enPeriodo;
   #10#2 = eaNombreEmp;
   #10#3 = enEjercicio;
   #10#4 = #400#3;
   #10#5 = #400#4;
   |SI #400#3 ! 0;
       |HAZ LaEmpresaAnterior;
       |SI nAnterior = 1; #10#4 = 0; #10#5 = 0; |FINSI;
   |FINSI;
   |SI #10#4 ! 0;
        #10#6 = #30#1;
        |SI #30#26 < 80;
            #10#7 = #30#26 + 2000;
        |SINO;
             #10#7 = #30#26 + 1900;
        |FINSI;
   |SINO;
        #10#6 = "SI EMPRESA ANTERIOR";
        #10#7 = enEjercicio - 1;
   |FINSI;

   aAlfa1 = #403#0; |QBF aAlfa1;
   aAlfa1 = aAlfa1 % 0;
   #10#25 = aAlfa1;    ||Longitud anterior 2007

   aAlfa1 = #403#2; |QBF aAlfa1;
   aAlfa1 = aAlfa1 % 0;
   #10#24 = aAlfa1;    ||Longitud anterior 2008

   |CLS;
   |CABEZA "Cambiar Niveles en Tabla de Equivalencia";
   |PINPA #0#10;
   #10#10 = dig3;  #10#17 = dig3;
   #10#11 = dig4;  #10#18 = dig4;
   #10#12 = dig5;  #10#19 = dig5;
   #10#13 = dig6;  #10#20 = dig6;
   #10#14 = dig7;  #10#21 = dig7;
   #10#15 = dig8;  #10#22 = dig8;
   #10#16 = dig9;  #10#23 = dig9;
   nNume1   = 17 + #10#17;
   #10nNume1 = #10#24;
   |PINDA #0#10;

   |SI aParam = "";
       |ENDATOS #1#10;
   |SINO;
       #10#8   = 3;
       |SI nAnterior = 1; #10#8 = 1; |FINSI;
       |SI aParam = "Pase8";
           |SI nAnterior = 1;
               |ACABA;
           |SINO;
               #10#8 = 2;
           |FINSI;
       |FINSI;
       #10#9   = eaRevis; |PINTA #10#9;
       #10#26  = "SI";    |PINTA #10#26;
       FSalida = 0;
   |FINSI;
   |SI FSalida = 0; |Y #10#26 = "SI";
       |HAZ HagoTodo;
   |FINSI;

   |SI nSwGrabar = 1;
       |ABRE #400;
       #400#0 = #10#0;
       #400#1 = #10#1;
       |LEE 101#400=;
       |SI FSalida ! 0;
           |DDEFECTO #400;
           #400#0 = #10#0;
           #400#1 = #10#1;
           #400#2 = eDNum2008;
           |GRABA 020#400c;
       |FINSI;
       #400#3 = #10#4;
       #400#4 = #10#5;
       |GRABA 020#400a;
       |LIBERA #400;
       |CIERRA #400;
  |FINSI;
|FPRO;
