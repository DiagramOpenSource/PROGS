|FICHEROS;
     dscsvz15;
     dscsvm11;

     canempre;
     cacuenta;
     caclipro;
     camaasic;
     camaasil;
     camaniva;
     caivalin;
     caintiva;
     cainliva;
     cacuiv2c;
     cacuiv2l;

     :modelos/dsmom030;

     dscsvw06;
     dscsvw09;
     dscsvw20;
     dscsvw21;

     dscsvw10;
     dscsvw16,MANTE;

     &regisnif@;
|FIN;

|VARIABLES;
     nDigiOrigen     = 0;
     nDigiDestino    = 0;
     nOrden1         = 0;
     nOrden2         = 0;
     nPausa          = 0;
     nImpor          = 0;
     nOk             = 0;

     aNombre1        = "";
     aNombre2        = "";
     aNombre3        = "";
     aNombre4        = "";
     aModificaAsiIva = "";
     aFicheroLCK     = "";
     aPathContagen   = "";
     aBorra          = "";
     aEjecuta        = "";
     aMensaje        = "";
     aFicheroMDB     = "";
     aFicheroAux     = "";
     nBtnInfo        = -1;
     aPathDavid      = "";
     aTextoVentana   = "";
     aDRef           = "";
     aHRef           = "";
     aBaseConta      = "";
     aNumAsi         = "";
     aNomCta         = "";
     aNIFCta         = "";
     a17             = "";
     a25             = "";

     n0Vent          = -1;
     n1Vent          = -1;
     n2Vent          = -1;
     nBtnUni         = -1;
     nBtnEOS         = -1;
     nBtnCon         = -1;
     nBtnDef         = -1;
     nBtnCan         = -1;
     nBtnCar1        = -1;
     nBtnCar2        = -1;
     nBtnCar3        = -1;
     nBtnCar4        = -1;
     nLblReg1        = -1;
     nLblReg2        = -1;
     nLblReg3        = -1;
     nLblReg4        = -1;
     nLblReg5        = -1;
     nLblImp         = -1;
     nGrid11         = -1;

     nRango          = 0;
     nHandle         = 0;
     nIde            = 0;
     nDCampo         = 0;
     nCampo          = 0;
     nPanta          = 0;
     nLong           = 0;
     nCarac          = 0;
     nPos            = 0;
     nFSalida        = 0;
     nColum          = 0;
     nNume1          = 0;
     nNume2          = 0;
     nRam1           = 0;
     nRam2           = 0;
     nPide           = 0;
     nFila           = 0;
     nCargar         = 0;
     nPosi           = 0;
     nTotCmp         = 0;
     nTCampos        = 0;
     nTValor         = 0;
     nReg            = 0;
     nPorce          = 0;
     nError          = 0;
     nCmp            = 0;
     nLongVar        = 0;
     nCarVar         = 0;
     nPosVar         = 0;
     nDife           = 0;
     nDesdeCar       = 0;
     nPos1           = 0;
     nPos2           = 0;
     nChequeo        = 0;
     nDocu           = 0;
     nFact           = 0;
     nApun           = 0;
     nBarra          = 0;
     nDia            = 0;
     nMes            = 0;
     nAny            = 0;
     nLibro          = 0;
     nIBas           = 0;
     nIIva           = 0;
     nITot           = 0;
     nIRet           = 0;
     nConcep         = 0;
     nBuscoCli       = 0;
     nPrint          = 0;
     nDiario         = 0;
     nDocum          = 0;
     nIVA            = 0;
     nLin            = 0;
     nNume           = 0;
     nPasada         = 0;

     aCtaBas         = "";
     aCtaIva         = "";
     aCtaTot         = "";
     aCtaRec         = "";
     aCtaRet         = "";
     aConcep         = "";
     aAlfa           = "";
     aAlfa1          = "";
     aAlfa2          = "";
     aAlfa3          = "";
     aFecha          = "";
     aEsc1           = "";
     aEsc2           = "";
     aRetu           = "";
     aTecla          = "";
     aPathLocal      = "";
     aPathTmp        = "";
     aFicTree        = "";
     aTabla          = "";
     aTablaTree      = "";
     aFilaTree       = "";
     aDescri         = "";
     aRuta           = "";
     aLong           = "";
     aCadena         = "";
     aOrigen         = "";
     aDestino        = "";
     aDTabla         = "";
     aHTabla         = "";
     aBuscar         = "";
     aRefresca       = "";
     aCarac          = "";
     aContenido      = "";
     a100            = "";
     aVar            = "";
     aLee            = "";
     aC10            = "";
     aC13            = "";
     aFicTmp         = "";
     aFicCSV         = "";
     aLongVar        = "";
     aComilla        = "";
     aCarVar         = "";
     aTipo           = "";
     aAsiento        = "";
     aReferencia     = "";
     aLibro          = "";
     aParam1         = "";
     aParam2         = "";
     aParam3         = "";
     aProg           = "";
     aFicBrowse      = "";
     aVer1           = "";
     aVer2           = "";
     aModAsiento     = "";
     aNomNif         = "";

     fFecha          = @;
     fFechaFac       = @;

     {1}aConte       = "";

     {-1}sTree       = 0;
         sT_nID      = 0;
         sT_nOper    = 0;
         sT_aData    = "";

     {-1}aVent       = "";
         aVent1      = 0;
         aVent2      = 0;
         aVent3      = 0;
         aVent4      = 0;
         aVent5      = "";

     &eaUnidadBasico = "";
     &eaIP           = "";
     &eaModoImpo     = "";
     &eaRepeSopo     = "";
     &eaFicheroCSV   = "";
     &eaTipoCSV      = "";
     &eaFicheroIVA   = "";
     aAnyo           = "";
     &eaFicheroDatos = "";
     &eaNombreDatos  = "";
     &eaBinarioConve = "";
     &eaNombrePDF    = "";
|FIN;

|INCLUYE dscont_i;

|PROCESO CalculaCuenta;
     |QBF eaCuenta;
     |SI eaCuenta = "";  |ACABA;  |FINSI;

     aLongVar = eaCuenta % 0;
     nLongVar = aLongVar;

     |SI nLongVar ! MaximoDigitos;
         nDife = MaximoDigitos - nLongVar;
         |SI nDife < 0;  |ACABA;  |FINSI;

         nDesdeCar = 5;

         nPos1    = 100 + nDesdeCar;
         nPos2    = (100 * (nDesdeCar + 1)) + 99;
         aAlfa    = (eaCuenta % nPos1) + ("0" * nDife) + (eaCuenta % nPos2);
         eaCuenta = aAlfa;
     |FINSI;

     |HAZ SacaNivel;
|FPRC;

|PROCESO CalculaFecha;
     |QBT aFecha;
     aLongVar = aFecha % 0;
     nLongVar = aLongVar;
     nBarra   = 0;
     aAlfa1   = "";
     aAlfa2   = "";
     aAlfa3   = "";
     |PARA nCarVar = 1;  |SI nCarVar [ nLongVar;  |HACIENDO nCarVar = nCarVar + 1;
           nPosVar = (nCarVar * 100) + 1;
           aCarVar = aFecha % nPosVar;

           |SI  aCarVar = "0"; |O aCarVar = "1"; |O aCarVar = "2";
             |O aCarVar = "3"; |O aCarVar = "4"; |O aCarVar = "5";
             |O aCarVar = "6"; |O aCarVar = "7"; |O aCarVar = "8";
             |O aCarVar = "9";
                |SI nBarra = 0;  aAlfa1 = aAlfa1 + aCarVar;  |FINSI;
                |SI nBarra = 1;  aAlfa2 = aAlfa2 + aCarVar;  |FINSI;
                |SI nBarra = 2;  aAlfa3 = aAlfa3 + aCarVar;  |FINSI;
           |SINO;
                |SI aCarVar = ".";  |O aCarVar = "/";  |O aCarVar = "\";
                    nBarra = nBarra + 1;
               |FINSI;
           |FINSI;
      |SIGUE;

      |QBF aAlfa1;
      |QBF aAlfa2;
      |QBF aAlfa3;

      nDia = aAlfa1;
      nMes = aAlfa2;
      nAny = aAlfa3;

      |SI nAny < 1000;
          nAny = 2000 + nAny;
      |FINSI;

      |SI nDia = 0;  nDia = 1;                    |FINSI;
      |SI nMes = 0;  nMes = 1;                    |FINSI;
      |SI nAny = 0;  nAny = 2000 + #canempre#26;  |FINSI;

      aFecha = (("00" + nDia) % -102);
      aFecha = aFecha + "." + (("00" + nMes) % -102);
      aFecha = aFecha + "." + (("0000" + nAny) % -104);
|FPRC;

|PROCESO QuitaPuntos;
     |QBT aAlfa;
     aLongVar = aAlfa % 0;
     nLongVar = aLongVar;
     aAlfa1   = "";
     |PARA nCarVar = 1;  |SI nCarVar [ nLongVar;  |HACIENDO nCarVar = nCarVar + 1;
           nPosVar = (nCarVar * 100) + 1;
           aCarVar = aAlfa % nPosVar;

           |SI  aCarVar ! ".";
                aAlfa1 = aAlfa1 + aCarVar;
           |FINSI;
      |SIGUE;

      aAlfa = aAlfa1;
|FPRC;

|PROCESO AltaNIF;
     |HAZ LeeNifs;
     |SI ewNif = 0; |ACABA;  |FINSI;

     |ABRE #regisnif@;
     |DDEFECTO #regisnif@;

     #regisnif@#0  = eaCodNif;
     #regisnif@#1  = aNomNif;

     |GRABA 020#regisnif@.n;
     |LIBERA #regisnif@;
     |CIERRA #regisnif@;
|FPRC;

|PROCESO LeeConcep;
     #dsmom030#0 = nConcep;
     |LEE 000#dsmom030.=;
     |SI FSalida ! 0;  |DDEFECTO #dsmom030;  |FINSI;

     aDescri = #dsmom030#1;   |QBF aDescri;
|FPRC;

|PROCESO AltaCuenta;
     |HAZ CalculaCuenta;

     #cacuenta#0 = eaCuenta;
     |LEE 000#cacuenta.=;
     |SI FSalida ! 0;
         #cacuenta#0 = eaCuenta;
         #cacuenta#1 = dig3;
         #cacuenta#2 = aNomCta;
         #cacuenta#3 = "S";

         |GRABA 020#cacuenta.n;
         |LIBERA #cacuenta;
     |FINSI;

     aTipo = "";
     aAlfa = eaCuenta % 102;

     |SI aAlfa = "40";  |O aAlfa = "41";
         aTipo = "P";
     |FINSI;

     |SI aAlfa = "43";  |O aAlfa = "44";
         aTipo = "C";
     |FINSI;

     |SI aTipo = "";  |ACABA;  |FINSI;

     #caclipro#23 = aTipo;
     #caclipro#10 = eaCuenta;
     |LEE 101#caclipro.=;
     |SI FSalida ! 0;
         |DDEFECTO #caclipro;
         #caclipro#23 = aTipo;
         #caclipro#10 = eaCuenta;

         |GRABA 020#caclipro.b;
     |FINSI;

     |SI aNomCta ! "";  #caclipro#1  = aNomCta;  |FINSI;
     |SI aNIFCta ! "";  #caclipro#9  = aNIFCta;  |FINSI;

     |GRABA 020#caclipro.a;
     |LIBERA #caclipro;
|FPRC;

|PROCESO LeeCuenta;
     #cacuenta#0 = eaCuenta;
     |LEE 000#cacuenta.=;
     |SI FSalida ! 0;  |DDEFECTO #cacuenta;  |FINSI;
|FPRC;

|| --------------------------------------------------------------------------

|PROCESO dscsvw10Campos;
     nTCampos  = nTCampos + 1;
|FPRC;

|DEFBUCLE dscsvw10Campos;
     #dscsvw10#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dscsvw10Campos;
|FIN;

|PROCESO GrabaIva_EOS;
     #dscsvw20#0  = nReg;

     |SI nCampo = 1;
         aFecha      = #dscsvw10#1;  |QBF aFecha;
         |HAZ CalculaFecha;
         #dscsvw20#1 = aFecha;
     |FINSI;

     |SI nCampo = 2;
         aFecha = #dscsvw10#1;  |QBF aFecha;
         |HAZ CalculaFecha;
         #dscsvw20#2 = aFecha;
     |FINSI;

     |SI nCampo = 3;  #dscsvw20#3  = #dscsvw10#1;        |FINSI;
     |SI nCampo = 4;  #dscsvw20#4  = #dscsvw10#1;        |FINSI;
     |SI nCampo = 5;  #dscsvw20#5  = #dscsvw10#1;        |FINSI;
     |SI nCampo = 6;  #dscsvw20#13 = #dscsvw10#1 > "" ;  |FINSI;

     |SI nCampo = 7;
         aAlfa = #dscsvw10#1;
         |HAZ QuitaPuntos;
         #dscsvw20#6 = aAlfa;
     |FINSI;

     |SI nCampo = 8;  #dscsvw20#7 = #dscsvw10#1 > "";  |FINSI;

     |SI nCampo = 9;
         aAlfa = #dscsvw10#1;
         |HAZ QuitaPuntos;
         #dscsvw20#8 = aAlfa;
     |FINSI;

     |SI nCampo = 10;
         aAlfa = #dscsvw10#1;
         |HAZ QuitaPuntos;
         #dscsvw20#9 = aAlfa;
     |FINSI;

     |SI nCampo = 11;
         aAlfa = #dscsvw10#1;
         |HAZ QuitaPuntos;
         #dscsvw20#10 = aAlfa;
     |FINSI;

     |SI nCampo = 12;
         aAlfa = #dscsvw10#1;
         |HAZ QuitaPuntos;
         #dscsvw20#11 = aAlfa;
     |FINSI;

     |SI nCampo = 14;  #dscsvw20#12 = #dscsvw10#1 > "";  |FINSI;
|FPRC;

|PROCESO GrabaConceptos;
     |SI nCampo = 1;  #dscsvw21#0  = #dscsvw10#1;  |FINSI;
     |SI nCampo = 2;  #dscsvw21#1  = #dscsvw10#1;  |FINSI;
|FPRC;

|PROCESO GrabaAsiento;
     #dscsvw06#0  = nReg;

     |SI nCampo = 1;
         aFecha      = #dscsvw10#1;
         |HAZ CalculaFecha;
         #dscsvw06#2 = aFecha;
     |FINSI;

     |SI nCampo = 2;
         aAlfa = #dscsvw10#1 - "Asiento";  |QBT aAlfa;
         aAlfa = ("000000" + aAlfa) % -106;
         #dscsvw06#34 = aAlfa;
     |FINSI;

     |SI nCampo = 3;  #dscsvw06#27 = #dscsvw10#1;  |FINSI;
     |SI nCampo = 4;  #dscsvw06#35 = #dscsvw10#1;  |FINSI;
     |SI nCampo = 5;  #dscsvw06#5  = #dscsvw10#1;  |FINSI;
     |SI nCampo = 6;  #dscsvw06#36 = #dscsvw10#1;  |FINSI;
     |SI nCampo = 7;  #dscsvw06#7  = #dscsvw10#1;  |FINSI;

     |SI nCampo = 8;
         nNume = #dscsvw10#1;
         |SI nNume ! 0;
             aAlfa = #dscsvw10#1;
             |HAZ QuitaPuntos;
             #dscsvw06#10  = aAlfa;
             #dscsvw06#9   = "D";

             |SI #dscsvw06#10 < 0;
                 #dscsvw06#10  = -#dscsvw06#10;
                 #dscsvw06#9   = "H";
             |FINSI;
         |FINSI;
     |FINSI;

     |SI nCampo = 9;
         nNume = #dscsvw10#1;
         |SI nNume ! 0;
             aAlfa = #dscsvw10#1;
             |HAZ QuitaPuntos;
             #dscsvw06#10  = aAlfa;
             #dscsvw06#9   = "H";

             |SI #dscsvw06#10 < 0;
                 #dscsvw06#10  = -#dscsvw06#10;
                 #dscsvw06#9   = "D";
             |FINSI;
         |FINSI;
     |FINSI;

     |SI nCampo = 11;  #dscsvw06#33 = #dscsvw10#1;  |FINSI;
|FPRC;

|PROCESO GrabaIva_Conta;
     #dscsvw09#0  = nReg;
     #dscsvw09#4  = nReg;

     |SI nCampo = 2;
         aFecha      = #dscsvw10#1;  |QBF aFecha;
         |HAZ CalculaFecha;
         #dscsvw09#1  = aFecha;
         #dscsvw09#12 = aLibro;
     |FINSI;

     |SI nCampo = 1;  #dscsvw09#2  = #dscsvw10#1;  |FINSI;
     |SI nCampo = 3;  #dscsvw09#16 = #dscsvw10#1;  |FINSI;
     |SI nCampo = 6;  #dscsvw09#10 = #dscsvw10#1;  |FINSI;
     |SI nCampo = 8;  #dscsvw09#5  = #dscsvw10#1;  |FINSI;
     |SI nCampo = 10; #dscsvw09#17 = #dscsvw10#1;  |FINSI;
     |SI nCampo = 14; #dscsvw09#3  = #dscsvw10#1;  |FINSI;

     |SI nCampo = 4;
         eaCuenta = #dscsvw10#1;
         |HAZ CalculaCuenta;
         #dscsvw09#14 = eaCuenta;
     |FINSI;

     |SI nCampo = 5; #dscsvw09#11 = #dscsvw10#1;  |FINSI;
     |SI nCampo = 6; #dscsvw09#10 = #dscsvw10#1;  |FINSI;

     |SI nCampo = 13;
         aAlfa = #dscsvw10#1;
         |HAZ QuitaPuntos;
         #dscsvw09#6  = aAlfa;
     |FINSI;

     |SI nCampo = 9;
         aAlfa = #dscsvw10#1;
         |HAZ QuitaPuntos;
         #dscsvw09#7  = aAlfa;
     |FINSI;

     |SI nCampo = 7;
         aAlfa = #dscsvw10#1;
         |HAZ QuitaPuntos;
         #dscsvw09#8  = aAlfa;
     |FINSI;

     |SI nCampo = 11;
         aAlfa = #dscsvw10#1;
         |HAZ QuitaPuntos;
         #dscsvw09#18 = aAlfa;
     |FINSI;

     |SI nCampo = 12;
         aAlfa = #dscsvw10#1;
         |HAZ QuitaPuntos;
         #dscsvw09#19 = aAlfa;
     |FINSI;
|FPRC;

|PROCESO PonConcepDS;
     |SI #dscsvm11#0 = "IV01";  #dscsvm11#2 = 700; #dscsvm11#4 = 600;  |FINSI;
     |SI #dscsvm11#0 = "IV02";  #dscsvm11#2 = 000; #dscsvm11#4 = 607;  |FINSI;
     |SI #dscsvm11#0 = "IV03";  #dscsvm11#2 = 000; #dscsvm11#4 = 605;  |FINSI;
     |SI #dscsvm11#0 = "IV04";  #dscsvm11#2 = 713; #dscsvm11#4 = 000;  |FINSI;
     |SI #dscsvm11#0 = "IV05";  #dscsvm11#2 = 715; #dscsvm11#4 = 000;  |FINSI;
     |SI #dscsvm11#0 = "IV06";  #dscsvm11#2 = 738; #dscsvm11#4 = 000;  |FINSI;
     |SI #dscsvm11#0 = "IV07";  #dscsvm11#2 = 736; #dscsvm11#4 = 000;  |FINSI;
     |SI #dscsvm11#0 = "IV08";  #dscsvm11#2 = 736; #dscsvm11#4 = 000;  |FINSI;
     |SI #dscsvm11#0 = "IV09";  #dscsvm11#2 = 700; #dscsvm11#4 = 000;  |FINSI;
     |SI #dscsvm11#0 = "IV10";  #dscsvm11#2 = 000; #dscsvm11#4 = 604;  |FINSI;
     |SI #dscsvm11#0 = "IV11";  #dscsvm11#2 = 000; #dscsvm11#4 = 606;  |FINSI;
     |SI #dscsvm11#0 = "IV12";  #dscsvm11#2 = 000; #dscsvm11#4 = 608;  |FINSI;
     |SI #dscsvm11#0 = "IV13";  #dscsvm11#2 = 000; #dscsvm11#4 = 602;  |FINSI;
     |SI #dscsvm11#0 = "IV14";  #dscsvm11#2 = 000; #dscsvm11#4 = 600;  |FINSI;
     |SI #dscsvm11#0 = "IV15";  #dscsvm11#2 = 000; #dscsvm11#4 = 600;  |FINSI;
     |SI #dscsvm11#0 = "IV16";  #dscsvm11#2 = 000; #dscsvm11#4 = 000;  |FINSI;
     |SI #dscsvm11#0 = "IV17";  #dscsvm11#2 = 741; #dscsvm11#4 = 000;  |FINSI;
     |SI #dscsvm11#0 = "IV18";  #dscsvm11#2 = 712; #dscsvm11#4 = 000;  |FINSI;
     |SI #dscsvm11#0 = "IV19";  #dscsvm11#2 = 729; #dscsvm11#4 = 000;  |FINSI;
     |SI #dscsvm11#0 = "IV20";  #dscsvm11#2 = 000; #dscsvm11#4 = 628;  |FINSI;
     |SI #dscsvm11#0 = "IV21";  #dscsvm11#2 = 713; #dscsvm11#4 = 000;  |FINSI;
     |SI #dscsvm11#0 = "IV22";  #dscsvm11#2 = 715; #dscsvm11#4 = 000;  |FINSI;
     |SI #dscsvm11#0 = "IV23";  #dscsvm11#2 = 000; #dscsvm11#4 = 604;  |FINSI;
     |SI #dscsvm11#0 = "IV24";  #dscsvm11#2 = 740; #dscsvm11#4 = 000;  |FINSI;
     |SI #dscsvm11#0 = "IV25";  #dscsvm11#2 = 000; #dscsvm11#4 = 633;  |FINSI;

     nConcep = #dscsvm11#2;  |HAZ LeeConcep;
     #dscsvm11#3 = aDescri;

     nConcep = #dscsvm11#4;  |HAZ LeeConcep;
     #dscsvm11#5 = aDescri;
|FPRC;

|PROCESO dscsvw10Carga;
     nCampo = #dscsvw10#0;

     |SI aTabla = "operacion";
         |SI nCampo = 2;  #dscsvm11#0 = #dscsvw10#1;  |FINSI;
         |SI nCampo = 3;  #dscsvm11#1 = #dscsvw10#1;  |FINSI;

         |HAZ PonConcepDS;
     |FINSI;

     |SI aTabla = "venta_eos";
         |HAZ GrabaIva_EOS;
     |FINSI;

     |SI aTabla = "compra_eos";
         |HAZ GrabaIva_EOS;
     |FINSI;

     |SI aTabla = "conceptos";
         |HAZ GrabaConceptos;
     |FINSI;

     |SI aTabla = "asientos";
         |HAZ GrabaAsiento;
     |FINSI;

     |SI aTabla = "ivaventa";
         aLibro = "R";
         |HAZ GrabaIva_Conta;
     |FINSI;

     |SI aTabla = "ivacompra";
         aLibro = "S";
         |HAZ GrabaIva_Conta;
     |FINSI;
|FPRC;

|DEFBUCLE dscsvw10Carga;
     #dscsvw10#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dscsvw10Carga;
|FIN;

|PROCESO Print;
     |SI aTabla = "conceptos";
         |SI nLblReg1 ! -1;
             |FIN_CONTROL_WINDOWS nLblReg1;
         |FINSI;

         aAlfa =  "LABEL,{{15}}" + (("00000" + nReg) % -105);
         |CONTROL_SIMPLE nPanta, aAlfa, 1190, 1198, NULL;
         nLblReg1 = FSalida;
     |FINSI;

     |SI aTabla = "asientos";
         |SI nLblReg2 ! -1;
             |FIN_CONTROL_WINDOWS nLblReg2;
         |FINSI;

         aAlfa =  "LABEL,{{15}}" + (("00000" + nReg) % -105);
         |CONTROL_SIMPLE nPanta, aAlfa, 1490, 1498, NULL;
         nLblReg2 = FSalida;
     |FINSI;

     |SI aTabla = "venta_eos";  |O aTabla = "ivaventa";
         |SI nLblReg3 ! -1;
             |FIN_CONTROL_WINDOWS nLblReg3;
         |FINSI;

         aAlfa =  "LABEL,{{15}}" + (("00000" + nReg) % -105);
         |CONTROL_SIMPLE nPanta, aAlfa, 1790, 1798, NULL;
         nLblReg3 = FSalida;
     |FINSI;

     |SI aTabla = "compra_eos";  |O aTabla = "ivacompra";
         |SI nLblReg4 ! -1;
             |FIN_CONTROL_WINDOWS nLblReg4;
         |FINSI;

         aAlfa =  "LABEL,{{15}}" + (("00000" + nReg) % -105);
         |CONTROL_SIMPLE nPanta, aAlfa, 2090, 2098, NULL;
         nLblReg4 = FSalida;
     |FINSI;
|FPRC;

|PROCESO GrabaDatos;
     |CIERRA #dscsvw10;
     nTCampos = 0;
     |BUCLE dscsvw10Campos;

     nError   = 0;
     nTValor  = nTotCmp;
     |SI nTCampos < nTValor;
         |DELINDEX #dscsvw10;
         |ACABA;
     |FINSI;

     nPorce = ((nTValor * 100) / nTCampos) ! 0;
     |SI nPorce < 70;
         |DELINDEX #dscsvw10;
         |ACABA;
     |FINSI;

     nReg = nReg + 1;
     |SI nReg = 1;
         |DELINDEX #dscsvw10;
         |ACABA;
     |FINSI;

     |SI aTabla = "venta_eos";
         |DDEFECTO #dscsvw20;

         |BUCLE dscsvw10Carga;

         |GRABA 000#dscsvw20.n;
         |LIBERA #dscsvw20;
     |FINSI;

     |SI aTabla = "compra_eos";
         |DDEFECTO #dscsvw20;

         |BUCLE dscsvw10Carga;

         |GRABA 000#dscsvw20.n;
         |LIBERA #dscsvw20;
     |FINSI;

     |SI aTabla = "operacion";
         |BUCLE dscsvw10Carga;

         |SI #dscsvm11#0 ! "Clav";  |Y #dscsvm11#0 ! "    ";
             |GRABA 020#dscsvm11.c;
             |LIBERA #dscsvm11;
         |FINSI;
     |FINSI;

     |SI aTabla = "conceptos";
         |DDEFECTO #dscsvw21;

         |BUCLE dscsvw10Carga;

         |GRABA 020#dscsvw21.n;
         |LIBERA #dscsvw21;
     |FINSI;

     |SI aTabla = "asientos";
         |DDEFECTO #dscsvw06;

         |BUCLE dscsvw10Carga;

         |GRABA 020#dscsvw06.n;
         |LIBERA #dscsvw06;
     |FINSI;

     |SI aTabla = "ivaventa";
         |DDEFECTO #dscsvw09;

         |BUCLE dscsvw10Carga;

         |GRABA 000#dscsvw09.n;
         |LIBERA #dscsvw09;
     |FINSI;

     |SI aTabla = "ivacompra";
         |DDEFECTO #dscsvw09;

         |BUCLE dscsvw10Carga;

         |GRABA 000#dscsvw09.n;
         |LIBERA #dscsvw09;
     |FINSI;

     |DELINDEX #dscsvw10;
     |PULSATECLA;
|FPRC;

|PROCESO Grabaw10;
     #dscsvw10#0 = nCmp;
     #dscsvw10#1 = aVar;
     |GRABA 020#dscsvw10.n;
     |LIBERA #dscsvw10;
|FPRC;

|PROCESO SacaDatos;
     aLong  = aLee % 0;
     nLong  = aLong;
     aC10   = &10;
     aC13   = &13;
     |PARA nCarac = 1;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos   = (nCarac * 100) + 1;
           aCarac = aLee % nPos;

           |SI aCarac = aC13;
               aCarac = ";";
           |FINSI;

           |SI aCarac = ";";
               nCmp   = nCmp + 1;
               |HAZ Grabaw10;

               aVar   = "";
               aCarac = "";
           |FINSI;

           |SI aCarac = aC10;
               aCarac = "";
           |FINSI;

           |SI nCmp = nTotCmp;  |Y nCmp ! 0;
               |HAZ GrabaDatos;

               |ABRE #dscsvw10;

               nCmp = 0;
           |FINSI;

           aVar = aVar + aCarac;
      |SIGUE;
|FPRC;

|PROCESO LeeCSV;
     aAlfa = "Procesando registros del fichero " + aTabla;
     |VENTANA 0501, 0540, -1, 16, aAlfa;
     n2Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n2Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     |ABRE #dscsvw10;

     nCmp  = 0;
     nReg  = 0;
     |XABRE "B", aFicCSV, nHandle;
     |PARA;  |SI;  |HACIENDO;
          |XLEE nHandle, 255, aLee;
          |SI FSalida [ 0;  |SAL;  |FINSI;

          |HAZ SacaDatos;
     |SIGUE;
     |XCIERRA nHandle;

     |LEE 000#dscsvw10.p;
     |SI FSalida = 0;
         nCmp   = nCmp + 1;
         |HAZ Grabaw10;
         |HAZ GrabaDatos;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n2Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n2Vent;

     |HAZ Print;

     |ABRE #dscsvw10; |CIERRA #dscsvw10; |DELINDEX #dscsvw10;
|FPRC;

|| PASE EOS

|PROCESO cacuiv2l;
     |SI nIVA ! #cacuiv2l#2;
         |ACABA;
     |FINSI;

     eaCuenta = #cacuiv2l#3;
     |ERROR10;
|FPRC;

|DEFBUCLE cacuiv2l;
     #cacuiv2l#1;
     ;
     #cacuiv2c#0, #cacuiv2c#20, #cacuiv2c#21, 00;
     #cacuiv2c#0, #cacuiv2c#20, #cacuiv2c#21, 99;
     ;
     NULL, cacuiv2l;
|FIN;

|PROCESO CargaDatosLibro1;
     eaCuenta     = "430000000000";
     |HAZ CalculaCuenta;  #cainliva#5  = eaCuenta;

     eaCuenta     = "700000000000";
     |HAZ CalculaCuenta;  #cainliva#17 = eaCuenta;

     nIVA = #dscsvw20#7;

     |SI nIVA > 0;
         #cainliva#7 = 700;
     |SINO;
                                       #cainliva#7 = 738;
         |SI #dscsvw20#7 =  "EX    ";  #cainliva#7 = 715;  |FINSI;
         |SI #dscsvw20#7 =  "IT    ";
             #cainliva#7  = 713;
             #cainliva#54 = "S";
         |FINSI;

         |SI #dscsvw20#8 ! 0;
             nIVA = ((#dscsvw20#8 * 100) / #dscsvw20#6) ! 0;
         |FINSI;

         #dscsvw20#7 = "0";
     |FINSI;

     eaCuenta     = "477000000000";
     aAlfa        = #cainliva#4 % -104;
     #cacuiv2c#0  = "R";
     #cacuiv2c#20 = aAlfa;
     #cacuiv2c#21 = "01.01.0000";
     |LEE 000#cacuiv2c.];
     |SI FSalida = 0;  |Y #cacuiv2c#0 = "R";
         |SI #cacuiv2c#2 = "N";
             eaCuenta = #cacuiv2c#6;
         |SINO;
             |BUCLE cacuiv2l;
         |FINSI;
     |FINSI;

     |HAZ CalculaCuenta;  #cainliva#20 = eaCuenta;
|FPRC;

|PROCESO BuscaContra;
     aAlfa = #dscsvw20#13 - "AGUA";
     |SI FSalida ! 0;  eaCuenta = "628000000000";  |ACABA;  |FINSI;

     aAlfa = #dscsvw20#13 - "ENERGIA";
     |SI FSalida ! 0;  eaCuenta = "628000000000";  |ACABA;  |FINSI;

     aAlfa = #dscsvw20#13 - "GASOLINA";
     |SI FSalida ! 0;  eaCuenta = "628000000000";  |ACABA;  |FINSI;

     aAlfa = #dscsvw20#13 - "TELEFONO";
     |SI FSalida ! 0;  eaCuenta = "628000000000";  |ACABA;  |FINSI;

     aAlfa = #dscsvw20#13 - "COLEGIO";
     |SI FSalida ! 0;  eaCuenta = "629000000000";  |ACABA;  |FINSI;

     aAlfa = #dscsvw20#13 - "OTROS GASTOS";
     |SI FSalida ! 0;  eaCuenta = "629000000000";  |ACABA;  |FINSI;
|FPRC;

|PROCESO CargaDatosLibro2;
     eaCuenta    = "400000000000";
     |HAZ CalculaCuenta;  #cainliva#5 = eaCuenta;

     eaCuenta = "600000000000";
     |HAZ BuscaContra;
     |HAZ CalculaCuenta; #cainliva#17 = eaCuenta;

     nIVA = #dscsvw20#7;

     |SI nIVA > 0;
         #cainliva#7 = 600;
     |SINO;
                                       #cainliva#7 = 600;
         |SI #dscsvw20#7 =  "EX    ";  #cainliva#7 = 605;  |FINSI;
         |SI #dscsvw20#7 =  "IT    ";
             #cainliva#7  = 607;
             #cainliva#54 = "S";
         |FINSI;

         |SI #dscsvw20#8 ! 0;
             nIVA = ((#dscsvw20#8 * 100) / #dscsvw20#6) ! 0;
         |FINSI;

         #dscsvw20#7 = "0";
     |FINSI;

     eaCuenta     = "472000000000";
     aAlfa        = #cainliva#4 % -104;
     #cacuiv2c#0  = "S";
     #cacuiv2c#20 = aAlfa;
     #cacuiv2c#21 = "01.01.0000";
     |LEE 000#cacuiv2c.];
     |SI FSalida = 0;  |Y #cacuiv2c#0 = "S";
         |SI #cacuiv2c#2 = "N";
             eaCuenta = #cacuiv2c#6;
         |SINO;
             |BUCLE cacuiv2l;
         |FINSI;
     |FINSI;

     |HAZ CalculaCuenta;   #cainliva#20 = eaCuenta;
|FPRC;

|PROCESO ActuaIVA;
     #caintiva#0 = nLibro;
     |LEE 101#caintiva.=;
     |SI FSalida ! 0;
         |DDEFECTO #caintiva;
         #caintiva#0 = nLibro;
         |GRABA 020#caintiva.n;
         |LIBERA #caintiva;
     |FINSI;

     enLibro  = #caintiva#0;
     |SUB_EJECUTA "caivaact;CSV";
|FPRC;

|PROCESO dscsvw20Gra;
     nDocu = nDocu + 1;
     nFact = nFact + 1;

     |SI #dscsvw20#3 ! aAsiento;  |O #dscsvw20#1 ! fFecha;
         |SI nDocu > 900;
             |HAZ ActuaIVA;

             nDocu = 1;
         |FINSI;

         nLin = 0;
     |FINSI;

     nLin = nLin + 1;

     #cainliva#0  = nLibro;
     #cainliva#1  = nDocu;
     #cainliva#2  = nFact;
     #cainliva#3  = nLin;
     #cainliva#4  = #dscsvw20#1;
     aAlfa        = #cainliva#4 % 402;
     #cainliva#31 = aAlfa;

     |SI nLibro = 1;
         |HAZ CargaDatosLibro1;
     |FINSI;

     |SI nLibro = 2;
         |HAZ CargaDatosLibro2;
     |FINSI;

     nConcep = #cainliva#7;
     |HAZ LeeConcep;

     aAlfa = aDescri;
     aAlfa = aAlfa + " " + #dscsvw20#3;
     #cainliva#8  = aAlfa;
     #cainliva#53 = #dscsvw20#3;
     #cainliva#30 = #dscsvw20#5;
     #cainliva#27 = #dscsvw20#4;

     |SI #canempre#24 = "S";
         #cainliva#45 = #canempre#30;
     |FINSI;

     #cainliva#12 = #dscsvw20#6;
     #cainliva#13 = nIVA;
     #cainliva#14 = #dscsvw20#8;
     #cainliva#15 = #dscsvw20#9;
     #cainliva#16 = #dscsvw20#10;
     #cainliva#29 = #dscsvw20#11;
     #cainliva#49 = "N";

     |GRABA 020#cainliva.n;
     |LIBERA #cainliva;

     eaCodNif = #dscsvw20#5;
     aNomNif  = #dscsvw20#4;
     |HAZ AltaNIF;

     aNIFCta  = #dscsvw20#5;
     aNomCta  = #dscsvw20#4;

     eaCuenta = #cainliva#5;   |HAZ AltaCuenta;
     eaCuenta = #cainliva#17;  |HAZ AltaCuenta;
     eaCuenta = #cainliva#20;  |HAZ AltaCuenta;

     aAsiento    = #dscsvw20#3;
     fFecha      = #dscsvw20#1;
|FPRC;

|DEFBUCLE dscsvw20Gra;
     #dscsvw20#2;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dscsvw20Gra;
|FIN;

|PROCESO CreaEOS_Ventas;
     aFicCSV = #dscsvz15#3;  |QBF aFicCSV;
     |SI aFicCSV = "";  |ACABA;  |FINSI;

     aFicTmp = "dscsvw10" + Usuario;  |NOME_DAT #dscsvw10#aFicTmp#;
     aFicTmp = "dscsvw20" + Usuario;  |NOME_DAT #dscsvw20#aFicTmp#;

     |ABRE #dscsvw20;  |CIERRA #dscsvw20;  |DELINDEX #dscsvw20;

     nTotCmp  = 14;

     |ABRE #dscsvw20;
     aTabla = "venta_eos";
     |HAZ LeeCSV;
     |CIERRA #dscsvw20;

     |SI nChequeo = 1;
         |CONFI "0000SRegistros de IVA preparados para importar. Quiere consultarlos antes?";

         |SI FSalida = 0;
             |ABRE #dscsvw20;
             |CONSULTA_CLAVE #1#dscsvw20;
             |CIERRA #dscsvw20;
         |FINSI;
     |FINSI;

     nLibro = 1;
     nDocu  = 0;
     nFact  = 0;

     |BUCLE dscsvw20Gra;

     |HAZ ActuaIVA;

     |ABRE #dscsvw20;  |CIERRA #dscsvw20;  |DELINDEX #dscsvw20;
|FPRC;

|PROCESO CreaEOS_Compra;
     aFicCSV = #dscsvz15#4;  |QBF aFicCSV;
     |SI aFicCSV = "";  |ACABA;  |FINSI;

     aFicTmp = "dscsvw20" + Usuario;  |NOME_DAT #dscsvw20#aFicTmp#;
     aFicTmp = "dscsvw10" + Usuario;  |NOME_DAT #dscsvw10#aFicTmp#;

     |ABRE #dscsvw20;  |CIERRA #dscsvw20;  |DELINDEX #dscsvw20;

     nTotCmp  = 14;

     |ABRE #dscsvw20;
     aTabla = "compra_eos";
     |HAZ LeeCSV;
     |CIERRA #dscsvw20;

     |SI nChequeo = 1;
         |CONFI "0000SRegistros de IVA preparados para importar. Quiere consultarlos antes?";

         |SI FSalida = 0;
             |ABRE #dscsvw20;
             |CONSULTA_CLAVE #1#dscsvw20;
             |CIERRA #dscsvw20;
         |FINSI;
     |FINSI;

     nLibro = 2;
     nDocu  = 0;
     nFact  = 0;

     |BUCLE dscsvw20Gra;

     |HAZ ActuaIVA;

     |ABRE #dscsvw20;  |CIERRA #dscsvw20;  |DELINDEX #dscsvw20;
|FPRC;

|PROCESO ImportaEOS;
     aFicheroAux = #dscsvz15#3;   |QBF aFicheroAux;
     |SI aFicheroAux = "";
         aMensaje = "0000Fichero datos Facturas Expedidas vacio. Por favor introduzca dicha informaci¢n.";
         |MENAV aMensaje;
         |ERROR;
         |ACABA;
     |FINSI;

     aFicheroAux = #dscsvz15#4;  |QBF aFicheroAux;
     |SI aFicheroAux = "";
         aMensaje = "0000Fichero datos Facturas Recibidas vacio. Por favor introduzca dicha informaci¢n.";
         |MENAV aMensaje;
         |ERROR;
         |ACABA;
     |FINSI;

     |HAZ CreaEOS_Ventas;
     |HAZ CreaEOS_Compra;
|FPRC;

|| PASE CONTABILIDAD

|PROCESO CreaConceptos;
     aFicCSV = #dscsvz15#1;  |QBF aFicCSV;
     |SI aFicCSV = "";  |ACABA;  |FINSI;

     aFicTmp = "dscsvw21" + Usuario;  |NOME_DAT #dscsvw21#aFicTmp#;
     aFicTmp = "dscsvw10" + Usuario;  |NOME_DAT #dscsvw10#aFicTmp#;

     |ABRE #dscsvw21;  |CIERRA #dscsvw21;  |DELINDEX #dscsvw21;

     nTotCmp  = 3;

     |ABRE #dscsvw21;
     aTabla = "conceptos";
     |HAZ LeeCSV;
     |CIERRA #dscsvw21;

     |SI nChequeo = 1;
         |CONFI "0000SRegistros de asientos preparados para importar. Quiere consultarlos antes?";

         |SI FSalida = 0;
             |ABRE #dscsvw21;
             |CONSULTA_CLAVE #1#dscsvw21;
             |CIERRA #dscsvw21;
         |FINSI;
     |FINSI;

     nError = 1;
     |ABRE #dscsvw21;
     |LEE 000#dscsvw21.p;
     |SI FSalida = 0;  nError = 0;  |FINSI;
     |CIERRA #dscsvw21;

     |SI nError = 1;
         |MENAV "0000No se han importado los conceptos. Importaci¢n cancelada.";
     |FINSI;
|FPRC;

|PROCESO dscsvw06Cheq;
     |SI #dscsvw06#34 ! aAsiento;  |O #dscsvw06#27 ! aReferencia;
         nApun = 0;
         nDocu = nDocu + 1;
     |FINSI;

     #dscsvw21#0 = #dscsvw06#7;
     |LEE 000#dscsvw21.=;
     |SI FSalida ! 0;  |DDEFECTO #dscsvw21;  |FINSI;

     #dscsvw06#3  = nDocu;
     #dscsvw06#1  = 1;
     #dscsvw06#7  = 1;

     aAlfa = #dscsvw21#1 > "";

     aAlfa1 = aAlfa - "APERTURA";
     |SI FSalida ! 0;
         #dscsvw06#1 = 0;
         #dscsvw06#7 = 96;
     |FINSI;

     eaCuenta = #dscsvw06#5;
     |HAZ CalculaCuenta;
     #dscsvw06#5  = eaCuenta;
     #dscsvw06#6  = dig3;

     aAlfa = #dscsvw06#2;  |QBF aAlfa;
     |SI aAlfa = "";
         |MENAV "0000Registros con fecha de apunte sin contenido. Cancelamos la importaci¢n";

         nError = 1;
         |ERROR10;
         |ACABA;
     |FINSI;

     aAlfa = #dscsvw06#5;  |QBF aAlfa;
     |SI aAlfa = "";
         |MENAV "0000Registros con cuentas sin contenido. Cancelamos la importaci¢n";

         nError = 1;
         |ERROR10;
         |ACABA;
     |FINSI;

     |SI #dscsvw06#9 = " ";
         |MENAV "0000Registros sin signo en la cuenta. Cancelamos la importaci¢n";

         nError = 1;
         |ERROR10;
         |ACABA;
     |FINSI;

     |SI #dscsvw06#9 = "D";
         #dscsvw06#17 = #dscsvw06#5;
         #dscsvw06#18 = #dscsvw06#6;
     |FINSI;

     |SI #dscsvw06#9 = "H";
         #dscsvw06#11 = #dscsvw06#5;
         #dscsvw06#12 = #dscsvw06#6;
     |FINSI;

     |SI #dscsvw06#22 = "  ";
         #dscsvw06#22 = #canempre#30;
     |FINSI;

     aAlfa = #dscsvw06#35;  |QBF aAlfa;
     |SI aAlfa = "";
         aAlfa = #dscsvw06#33;  |QBF aAlfa;
         |SI aAlfa = "";
             aAlfa = #dscsvw21#1;  |QBF aAlfa;
         |FINSI;
     |FINSI;

     #dscsvw06#8 = aAlfa;

     nApun = nApun + 1;
     #dscsvw06#4 = nApun;

     |GRABA 020#dscsvw06.a;
     |LIBERA #dscsvw06;

     aAsiento    = #dscsvw06#34;
     aReferencia = #dscsvw06#27;
|FPRC;

|DEFBUCLE dscsvw06Cheq;
     #dscsvw06#5;
     ;
     INICIO;
     FINAL;
     be;
     NULL, dscsvw06Cheq;
|FIN;

|PROCESO dscsvw06Gra;
     |PARA nCampo = 1;  |SI nCampo [ 31;  |HACIENDO nCampo = nCampo + 1;
           nDCampo = nCampo - 1;
           #camaasil#nDCampo# = #dscsvw06#nCampo#
     |SIGUE;

     |GRABA 020#camaasil.n;
     |LIBERA #camaasil;

     eaCuenta = #dscsvw06#5;
     aNomCta  = #dscsvw06#36;
     aNIFCta  = "";
     |HAZ AltaCuenta;

     #camaasic#0 = #camaasil#0;
     #camaasic#1 = #camaasil#1;
     #camaasic#2 = #camaasil#2;
     |LEE 000#camaasic.=;
     |SI FSalida = 0;  |ACABA;  |FINSI;

     |DDEFECTO #camaasic;
     #camaasic#0  = #camaasil#0;
     #camaasic#1  = #camaasil#1;
     #camaasic#2  = #camaasil#2;

     |GRABA 020#camaasic.n;
     |LIBERA #camaasic;
|FPRC;

|DEFBUCLE dscsvw06Gra;
     #dscsvw06#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dscsvw06Gra;
|FIN;

|PROCESO CreaAsientos;
     aFicCSV = #dscsvz15#2;  |QBF aFicCSV;
     |SI aFicCSV = "";  |ACABA;  |FINSI;

     aFicTmp = "dscsvw06" + Usuario;  |NOME_DAT #dscsvw06#aFicTmp#;
     aFicTmp = "dscsvw10" + Usuario;  |NOME_DAT #dscsvw10#aFicTmp#;

     |ABRE #dscsvw06;  |CIERRA #dscsvw06;  |DELINDEX #dscsvw06;

     nTotCmp  = 11;

     |ABRE #dscsvw06;
     aTabla = "asientos";
     |HAZ LeeCSV;
     |CIERRA #dscsvw06;

     nDocu = 0;
     |SELECT #3#camaasic;
     |LEE 000#camaasic.u;
     |SI FSalida = 0;
         nDocu = #camaasic#2;
     |FINSI;

     nError = 0;
     |ABRE #dscsvw21;
     |BUCLE dscsvw06Cheq;
     |CIERRA #dscsvw21;
     |SI nChequeo = 1;
         |CONFI "0000SRegistros de asientos preparados para importar. Quiere consultarlos antes?";

         |SI FSalida = 0;
             |ABRE #dscsvw06;
             |CONSULTA_CLAVE #1#dscsvw06;
             |CIERRA #dscsvw06;
         |FINSI;
     |FINSI;

     |BUCLE dscsvw06Gra;

     nError = 1;
     |LEE 000#camaasic.p;
     |SI FSalida = 0;  nError = 0;  |FINSI;

     |SI nError = 1;
         |MENAV "0000No se han importado el diario de movimientos. Importaci¢n cancelada.";
     |FINSI;
|FPRC;

|PROCESO dscsvw09Cheq;
     nError = 1;
     |SELECT #5#dscsvw06;
     #dscsvw06#27 = #dscsvw09#16;
     #dscsvw06#35 = #dscsvw09#2;
     |LEE 000#dscsvw06.=;
     |SI FSalida = 0;
         |SELECT #1#camaasic;
         #camaasic#0 = #dscsvw06#1;
         #camaasic#1 = #dscsvw06#2;
         #camaasic#2 = #dscsvw06#3;
         |LEE 000#camaasic.=;
         |SI FSalida = 0;  nError = 0;  |FINSI;
     |FINSI;

     |SI nError = 1;
         |ERROR10;
         |ACABA;
     |FINSI;
|FPRC;

|DEFBUCLE dscsvw09Cheq;
     #dscsvw09#5;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dscsvw09Cheq;
|FIN;

|PROCESO RecogeConcepto;
     #dscsvm11#0 = #dscsvw09#3;
     |LEE 000#dscsvm11.=;
     |SI FSalida ! 0;  |DDEFECTO #dscsvm11;  |FINSI;

     a25 = "N";
     a17 = "N";

     |SI nLibro = 1;
         nConcep = #dscsvm11#2;

         |SI nConcep = 0;  nConcep = 700;  |FINSI;

         |SI nConcep = 713;  a25 = "S";  |FINSI;
         |SI nConcep = 729;  a25 = "S";  |FINSI;
     |FINSI;

     |SI nLibro = 2;
         nConcep = #dscsvm11#4;

         |SI nConcep = 0;  nConcep = 600;  |FINSI;

         |SI nConcep = 607;  a25 = "S";  |FINSI;
         |SI nConcep = 608;  a25 = "S";  |FINSI;
         |SI nConcep = 628;  a25 = "S";  |FINSI;

         |SI nConcep = 604;  a17 = "S";  |FINSI;
         |SI nConcep = 606;  a17 = "S";  |FINSI;
         |SI nConcep = 608;  a17 = "S";  |FINSI;
     |FINSI;

     |HAZ LeeConcep;

     aAlfa   = aDescri;
     aConcep = aAlfa + " " + #dscsvw09#2;  |QBF aConcep;
|FPRC;

|PROCESO camaasil;
     nOk = 0;
     |HAZ RecogeConcepto;

     #camaasil#12 = #camaniva#36;
     #camaasil#13 = #camaniva#0;
     #camaasil#14 = #camaniva#3;
     #camaasil#15 = #camaniva#2;
     #camaasil#20 = 1;

     |SI nPasada = 1;
         aAlfa = #camaasil#4 % 103;
         |SI aAlfa = "400";  |O aAlfa = "410";
          |O aAlfa = "430";  |O aAlfa = "440";
             aCtaTot      = #camaasil#4;
             #camaasil#19 = "F";

             #camaasil#6 = nConcep;
             #camaasil#7 = aConcep;

             |GRABA 020#camaasil.a;
         |FINSI;

         |ACABA;
     |FINSI;

     |SI #camaasil#9 = #dscsvw09#6;   || Total factura
         aAlfa = #camaasil#4 % 103;
         |SI aAlfa = "400";  |O aAlfa = "410";
          |O aAlfa = "430";  |O aAlfa = "440";
             aCtaTot      = #camaasil#4;
             #camaasil#19 = "F";
             nOk          = 1;
         |FINSI;
     |FINSI;

     |SI nOk = 0;
         |SI #camaasil#9 = #dscsvw09#8;  || Base imponible
             #camaasil#19 = "B";
             aCtaBas      = #camaasil#4;
             nOk          = 1;
         |FINSI;
     |FINSI;

     |SI nOk = 0;
         |SI #camaasil#9 = #dscsvw09#7;  || Cuota IVA
             #camaasil#19 = "I";
             aCtaIva      = #camaasil#4;
             nOk          = 1;
         |FINSI;
     |FINSI;

     |SI nOk = 0;
         |SI #camaasil#9 = #dscsvw09#18;  || Cuota Recargo
             #camaasil#19 = "I";
             aCtaRec      = #camaasil#4;
             nOk          = 1;
         |FINSI;
     |FINSI;

     |SI nOk = 0;
         |SI #camaasil#9 = #dscsvw09#19;  || Retencion
             |SI nLibro = 1;  nConcep = 500;  |FINSI;
             |SI nLibro = 2;  nConcep = 410;  |FINSI;

             |HAZ LeeConcep;
             aConcep      = aDescri;
             #camaasil#19 = "P";
             aCtaRet      = #camaasil#4;
             nOk          = 1;
         |FINSI;
     |FINSI;

     |SI nOk = 1;
         #camaasil#6 = nConcep;
         #camaasil#7 = aConcep;

         |GRABA 020#camaasil.a;
     |FINSI;

     |LIBERA #camaasil;
|FPRC;

|DEFBUCLE camaasil;
     #camaasil#1;
     ;
     #camaasic#0, #camaasic#1, #camaasic#2, 0001;
     #camaasic#0, #camaasic#1, #camaasic#2, 9999;
     be;
     NULL, camaasil;
|FIN;

|PROCESO dscsvw09Gra;
     |SELECT #5#dscsvw06;
     #dscsvw06#27 = #dscsvw09#16;
     #dscsvw06#35 = #dscsvw09#2;
     |LEE 000#dscsvw06.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SELECT #1#camaasic;
     #camaasic#0 = #dscsvw06#1;
     #camaasic#1 = #dscsvw06#2;
     #camaasic#2 = #dscsvw06#3;
     |LEE 000#camaasic.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |HAZ RecogeConcepto;

     |SI #dscsvw09#1 ! fFechaFac; |O  #dscsvw09#16 ! aNumAsi;
         #camaniva#0 = nLibro;
         #camaniva#1 = 9999999999;
         |LEE 000#camaniva.];
         |SI FSalida = 0;
             |LEE 000#camaniva.a;
         |SINO;
             |LEE 000#camaniva.u;
         |FINSI;

         nDocu = 1;
         |SI FSalida = 0;  |Y #camaniva#0 = nLibro;
             nDocu = #camaniva#1 + 1;
         |FINSI;

         |DDEFECTO #camaniva;
         #camaniva#0  = nLibro;
         #camaniva#1  = nDocu;
         #camaniva#2  = "";
         #camaniva#3  = nDocu;
         #camaniva#36 = "R";

         |SI #camaniva#0 = 2;
             #camaniva#36 = "S";
         |FINSI;

         #camaniva#4  = #dscsvw09#1;
         aAlfa        = #camaasic#1 % 402;  #camaniva#5 = aAlfa;
         #camaniva#6  = "N";
         #camaniva#7  = #camaasic#0;
         #camaniva#8  = #camaasic#1;
         #camaniva#9  = #camaasic#2;
         #camaniva#10 = #canempre#30;
         #camaniva#24 = #canempre#32;
         #camaniva#22 = #dscsvw09#2;
         #camaniva#14 = #dscsvw09#10;
         #camaniva#27 = nConcep;
         #camaniva#29 = nConcep;
         #camaniva#28 = aConcep;
         #camaniva#30 = aConcep;

         |GRABA 020#camaniva.b;
         |LIBERA #camaniva;

         eaCodNif = #dscsvw09#10;
         aNomNif  = #dscsvw09#11;
         |HAZ AltaNIF;

         nApun = 0;
     |FINSI;

     aCtaTot = "";
     aCtaBas = "";
     aCtaIva = "";
     aCtaRec = "";
     nPasada = 0;
     |BUCLE camaasil;

     |SI aCtaTot = "";
         nPasada = 1;
         |BUCLE camaasil;
     |FINSI;

     eaCuenta = #dscsvw09#14;
     aNomCta  = #dscsvw09#11;
     aNIFCta  = #dscsvw09#10;
     |HAZ AltaCuenta;

     eaCuenta = aCtaTot;
     |HAZ LeeCuenta;

     #camaniva#12 = eaCuenta;
     #camaniva#13 = #cacuenta#2;

     |GRABA 020#camaniva.a;
     |LIBERA #camaniva;

     nApun = nApun + 1;

     |DDEFECTO #caivalin;
     #caivalin#0  = #camaniva#0;
     #caivalin#1  = #camaniva#1;
     #caivalin#2  = nApun;
     #caivalin#3  = #camaniva#27;
     #caivalin#6  = #dscsvw09#8;
     #caivalin#7  = #dscsvw09#5;
     #caivalin#8  = #dscsvw09#7;
     #caivalin#9  = #dscsvw09#17;
     #caivalin#10 = #dscsvw09#18;
     #caivalin#12 = aCtaBas;
     #caivalin#13 = aCtaIva;
     #caivalin#14 = aCtaRec;
     #caivalin#30 = #camaniva#10;

     |SI #dscsvw09#19 ! 0;
         #caivalin#20 = #caivalin#6;
         #caivalin#22 = #dscsvw09#19;
         #caivalin#21 = ((#dscsvw09#19 * 100) / #caivalin#6) ! 2;
         #caivalin#23 = aCtaRet;

         |SI #camaniva#0 = 1;  #caivalin#18 = 500;  |FINSI;
         |SI #camaniva#0 = 2;  #caivalin#18 = 410;  |FINSI;
     |FINSI;

     #caivalin#4  = #camaniva#28;

     nConcep = #caivalin#18; |HAZ LeeConcep;  #caivalin#19 = aDescri;

     efFechaIVA = #camaniva#4;
     enLineaIVA = 0;
     enPorcIVA  = #caivalin#7;
     enPorcREC  = #caivalin#9;
     aTipoIVA   = "S";
     |HAZ BuscaTipoIVA;
     #caivalin#16 = enLineaIVA;
     #caivalin#17 = a17;
     #caivalin#25 = a25;

     |GRABA 020#caivalin.n;
     |LIBERA #caivalin;

     aNumAsi   = #dscsvw09#16;
     fFechaFac = #dscsvw09#1;
|FPRC;

|DEFBUCLE dscsvw09Gra;
     #dscsvw09#5;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dscsvw09Gra;
|FIN;

|PROCESO CreaIvaVenta;
     aFicCSV = #dscsvz15#3;  |QBF aFicCSV;
     |SI aFicCSV = "";  |ACABA;  |FINSI;

     aFicTmp = "dscsvw09" + Usuario;  |NOME_DAT #dscsvw09#aFicTmp#;
     aFicTmp = "dscsvw10" + Usuario;  |NOME_DAT #dscsvw10#aFicTmp#;

     |ABRE #dscsvw09;  |CIERRA #dscsvw09;  |DELINDEX #dscsvw09;

     nTotCmp  = 14;
     nLibro   = 1;

     |ABRE #dscsvw09;
     aTabla = "ivaventa";
     |HAZ LeeCSV;
     |CIERRA #dscsvw09;

     |ABRET #dscsvw06;
     |BUCLE dscsvw09Cheq;
     |CIERRAT #dscsvw06;

     |SI nError = 1;
         |MENAV "0000Hay registros de IVA VENTAS sin movimientos en el diario. Importaci¢n IVA cancelada.";
         |ACABA;
     |FINSI;

     |SI nChequeo = 1;
         |CONFI "0000SRegistros de IVA VENTAS preparados para importar. Quiere consultarlos antes?";

         |SI FSalida = 0;
             |ABRE #dscsvw09;
             |CONSULTA_CLAVE #1#dscsvw09;
             |CIERRA #dscsvw09;
         |FINSI;
     |FINSI;

     |ABRET #dscsvw06;
     |BUCLE dscsvw09Gra;
     |CIERRAT #dscsvw06;
|FPRC;

|PROCESO CreaIvaCompra;
     aFicCSV = #dscsvz15#4;  |QBF aFicCSV;
     |SI aFicCSV = "";  |ACABA;  |FINSI;

     aFicTmp = "dscsvw09" + Usuario;  |NOME_DAT #dscsvw09#aFicTmp#;
     aFicTmp = "dscsvw10" + Usuario;  |NOME_DAT #dscsvw10#aFicTmp#;

     |ABRE #dscsvw09;  |CIERRA #dscsvw09;  |DELINDEX #dscsvw09;

     nTotCmp  = 14;
     nLibro   = 2;

     |ABRE #dscsvw09;
     aTabla = "ivacompra";
     |HAZ LeeCSV;
     |CIERRA #dscsvw09;

     |ABRET #dscsvw06;
     |BUCLE dscsvw09Cheq;
     |CIERRAT #dscsvw06;

     |SI nError = 1;
         |MENAV "0000Hay registros de IVA COMPRAS sin movimientos en el diario. Importaci¢n IVA cancelada.";
         |ACABA;
     |FINSI;

     |SI nChequeo = 1;
         |CONFI "0000SRegistros de IVA COMPRAS preparados para importar. Quiere consultarlos antes?";

         |SI FSalida = 0;
             |ABRE #dscsvw09;
             |CONSULTA_CLAVE #1#dscsvw09;
             |CIERRA #dscsvw09;
         |FINSI;
     |FINSI;

     |ABRET #dscsvw06;
     |BUCLE dscsvw09Gra;
     |CIERRAT #dscsvw06;
|FPRC;

|PROCESO ImportaConta;
     |HAZ CreaConceptos;

     |SI nError = 0; |HAZ CreaAsientos;  |FINSI;
     |SI nError = 0; |HAZ CreaIvaVenta;  |FINSI;
     |SI nError = 0; |HAZ CreaIvaCompra; |FINSI;
|FPRC;

|| PASE

|PROCESO RecalculosFinales;
     |SI aModAsiento = "NO";
         aAlfa = "*:contagen/careccab canempr1 " + (("00000" + #48#2) % -105) + #48#3 + ";" + "100";
     |SINO;
         aAlfa = "*:contagen/careccab canempr1 " + (("00000" + #48#2) % -105) + #48#3 + ";" + "000";
     |FINSI;
     |SUB_EJECUTA aAlfa;

     aAlfa = "*:contagen/caz00023 canempr1 " + (("00000" + #48#2) % -105) + #48#3 + ";" + "CREA";
     |SUB_EJECUTA aAlfa;

     |MENAV "0000Importaci¢n realizada. A continuaci¢n se van a realizar una serie de chequeos";

     |MENAV "0000Chequeo general de asientos";
     aAlfa = "*:contagen/cacheapu canempr1 " + (("00000" + #48#2) % -105) + #48#3 + ";" + "CHEQUEO";
     |SUB_EJECUTA aAlfa;

     |MENAV "0000Chequeo general de facturas";
     aAlfa = "*:contagen/caconiva canempr1 " + (("00000" + #48#2) % -105) + #48#3 + ";" + "CHEQUEO";
     |SUB_EJECUTA aAlfa;

     |MENAV "0000Chequeo cuentas de IVA";
     aAlfa = "*:contagen/cacheiva canempr1 " + (("00000" + #48#2) % -105) + #48#3 + ";" + "CHEQUEO";
     |SUB_EJECUTA aAlfa;

     |MENAV "0000Asistente de balances";
     aAlfa = "*:contagen/caut0009 canempr1 " + (("00000" + #48#2) % -105) + #48#3 + ";" + "CHEQUEO";
     |SUB_EJECUTA aAlfa;

     |MENAV "0000Recuperaci¢n de saldos";
     aAlfa = "*:contagen/carecupe canempr1 " + (("00000" + #48#2) % -105) + #48#3 + ";" + "CHEQUEO";
     |SUB_EJECUTA aAlfa;

     |MENAV "0000Proceso terminado.";
|FPRC;

|PROCESO Importacion;
     |SI #dscsvz15#0 = "E";
         |HAZ ImportaEOS;
     |FINSI;

     |SI #dscsvz15#0 = "C";
         |HAZ ImportaConta;
     |FINSI;

     |HAZ RecalculosFinales;
|FPRC;

|| ------------------------------------------------------------------------

|PROCESO ConversionEOS;
     nError  = 0;

     aAlfa = aPathLocal + "*.csv";
     |R_PDIR aAlfa;
     |PARA;  |SI FSalida = 0;  |HACIENDO;
          |RFBORRA aAlfa;

          |R_SDIR aAlfa;
     |SIGUE;

     aParam2 = aPathLocal + "ficcsv.txt";  |RFBORRA aParam2;

     aVer1   = "";
     aVer2   = "";
     aConte  = aBaseConta + "plugin/dsncstods.exe";
     |ESPECIFICA "MD5", aConte;
     aVer1 = FSalida;

     aConte  = aPathLocal + "dsncstods.exe";
     |ESPECIFICA "REMOTOMD5", aConte;
     aVer2 = FSalida;

     |SI aVer1 ! aVer2;
         aOrigen  = aBaseConta + "plugin/dsncstods.exe";
         aDestino = aPathLocal + "dsncstods.exe";

         |SI eaIP = "";
             |COPIA_FICHERO aOrigen, aDestino;
         |SINO;
             |ENVIA_FICHERO aOrigen, aDestino;
         |FINSI;

         aAlfa = aPathLocal + "dsncstods.exe";
         |XABRE "EC", aAlfa, 0;
         |SI FSalida < 0;
             |MENAV "0000No encuentro el conversor a CSV de EOS, por favor consulte con su administrador informatico.";
             aFicBrowse = "";
             |ACABA;
         |FINSI;
     |FINSI;

     aProg   = aPathLocal + "dsncstods.exe";
     aParam1 = aFicBrowse;
     aParam2 = aPathLocal + "ficcsv.csv";

     |SI nFSalida = nBtnCar3;  aParam3 = "E";  |FINSI;
     |SI nFSalida = nBtnCar4;  aParam3 = "R";  |FINSI;

     aAlfa  = "cmd " + &34 + "/c START /WAIT " + aProg + " " + aParam1 + " " + aParam2 + " " + aParam3 + &34;
     |RSYSTEM aAlfa;

     |XABRE "EC", aParam2, 0;
     |SI FSalida < 0;
         aFicBrowse = "";
         |MENAV "0000No se ha podido convertir a CSV el fichero seleccionado.";
         |ACABA;
     |FINSI;

     aFicBrowse = aParam2;
|FPRC;

|PROCESO ConversionConta;
     nError  = 0;

     aAlfa = aPathLocal + "*.csv";
     |R_PDIR aAlfa;
     |PARA;  |SI FSalida = 0;  |HACIENDO;
          |RFBORRA aAlfa;

          |R_SDIR aAlfa;
     |SIGUE;

     aParam2 = aPathLocal + "ficcsv.txt";  |RFBORRA aParam2;

     aVer1   = "";
     aVer2   = "";
     aConte  = aBaseConta + "plugin/dsofficetxt.exe";
     |ESPECIFICA "MD5", aConte;
     aVer1 = FSalida;

     aConte  = aPathLocal + "dsofficetxt.exe";
     |ESPECIFICA "REMOTOMD5", aConte;
     aVer2 = FSalida;

     |SI aVer1 ! aVer2;
         aOrigen  = aBaseConta + "plugin/dsofficetxt.exe";
         aDestino = aPathLocal + "dsofficetxt.exe";

         |SI eaIP = "";
             |COPIA_FICHERO aOrigen, aDestino;
         |SINO;
             |ENVIA_FICHERO aOrigen, aDestino;
         |FINSI;

         aAlfa = aPathLocal + "dsofficetxt.exe";
         |XABRE "EC", aAlfa, 0;
         |SI FSalida < 0;
             |MENAV "0000No encuentro el conversor a CSV, por favor consulte con su administrador informatico.";
             aFicBrowse = "";
             |ACABA;
         |FINSI;
     |FINSI;

     aProg   = aPathLocal + "dsofficetxt.exe";
     aParam1 = aFicBrowse;
     aParam2 = aPathLocal + "ficcsv.csv";
     aParam3 = "";

     aAlfa  = "cmd " + &34 + "/c START /WAIT " + aProg + " " + aParam1 + " " + aParam2 + " " + aParam3 + &34;
     |RSYSTEM aAlfa;

     |XABRE "EC", aParam2, 0;
     |SI FSalida < 0;
         aFicBrowse = "";
         |MENAV "0000No se ha podido convertir a CSV el fichero seleccionado.";
         |ACABA;
     |FINSI;

     aFicBrowse = aParam2;
|FPRC;

|PROCESO Chequeo;
     nError = 0;

     |SI #dscsvz15#0 = "E";
         |PARA nCampo = 3;  |SI nCampo [ 4;  |HACIENDO nCampo = nCampo + 1;
               aAlfa = #dscsvz15#nCampo#;  |QBF aAlfa;
               |SI aAlfa = ""; nError = 1;  |SAL;  |FINSI;
         |SIGUE;
     |FINSI;

     |SI #dscsvz15#0 = "C";
         |LEE 000#dscsvm11.p;
         |SI FSalida ! 0;
             nError = 1;
             |MENAV "0000No se puede realizar la importaci¢n no est n los tipos de operaciones definidos.";
             |ACABA;
         |FINSI;

         |PARA nCampo = 1;  |SI nCampo [ 4;  |HACIENDO nCampo = nCampo + 1;
               aAlfa = #dscsvz15#nCampo#;  |QBF aAlfa;
               |SI aAlfa = ""; nError = 1;  |SAL;  |FINSI;
         |SIGUE;
     |FINSI;

     |SI nError = 1;
         |MENAV "0000Faltan ficheros a informar";
     |FINSI;
|FPRC;

|| ------------------------------------------------------------------------

|PROCESO CreaOperacion;
     aFicCSV = aPathTmp + "operacion.csv";

     nTotCmp  = 3;
     aTabla   = "operacion";
     |HAZ LeeCSV;
|FPRC;

|PROCESO PantaW11;
     |VENTANA 0501, 1460, -1, 16, "Concepto DIAGRAM";
     n2Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n2Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     |LEE 101#dscsvm11.=;

     |DDEFECTO #dscsvw16;

     #dscsvw16#0 = #dscsvm11#2;
     #dscsvw16#1 = #dscsvm11#3;
     #dscsvw16#2 = #dscsvm11#4;
     #dscsvw16#3 = #dscsvm11#5;

     |PINPA #0#dscsvw16;
     |PINDA #0#dscsvw16;

     |ENDATOS #1#dscsvw16;
     |SI FSalida = 0;
         #dscsvm11#2 = #dscsvw16#0;
         #dscsvm11#3 = #dscsvw16#1;
         #dscsvm11#4 = #dscsvw16#2;
         #dscsvm11#5 = #dscsvw16#3;

         |GRABA 020#dscsvm11.a;
     |FINSI;
     |LIBERA #dscsvm11;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n2Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n2Vent;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |REFRESCACONTROL nGrid11;
     |FOCOTECLADO nGrid11;

     |PULSATECLA;
|FPRC;

|PROCESO Evnt11;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#dscsvm11.=;
     |FINSI;

     |SI FSalida = 4;
         |HAZ PantaW11;
     |FINSI;
|FPRC;

|PROCESO ImportaConcep;
     aAlfa = "0000La tabla de tipo de operaci¢n est  vac¡a. ";
     aAlfa = aAlfa + "A continuaci¢n seleccione el fichero que contiene ";
     aAlfa = aAlfa + "los tipo de operaciones NCS";
     |MENAV aAlfa;

     |HAZ Browse;

     |SI aFicBrowse = "";
         |MENAV "0000Importaci¢n cancelada.";
         |ACABA;
     |FINSI;

     aOrigen  = aFicBrowse;
     aDestino = aPathTmp + "operacion.csv";

     |SI eaIP = "";
         |COPIA_FICHERO aOrigen, aDestino;
     |SINO;
         |RECIBE_FICHERO aOrigen, aDestino;
     |FINSI;

     |HAZ CreaOperacion;
|FPRC;

|PROCESO EntraM11;
     |LEE 000#dscsvm11.p;
     |SI FSalida ! 0;
         |HAZ ImportaConcep;
     |FINSI;

     |LEE 000#dscsvm11.p;
     |SI FSalida ! 0;
         |ACABA;
     |FINSI;

     |VENTANA 0501, 3599, -1, 16, "Operaci¢n a definir";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     |CONTROL_SIMPLE 0, "LABEL,{{11}}(Doble click para entrar el concepto DS)", 0502, 0598, NULL;

     nRango   = 2 + 4 + 8 + 16 + 32 + 128;
     |LINEAL_SIMPLE #1#dscsvm11, nRango, 601, 3298, NULL, NULL, Evnt11;
     nGrid11 = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::{{001}}Salir," + nGrid11;
          |LEETECLA aTecla;
          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
          |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;

     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
|FPRC;

|| ------------------------------------------------------------------------

|PROCESO Browse;
     aFicBrowse = "F*.*";
     |BROWSE aFicBrowse;

     aAlfa      = aFicBrowse;   |QBF aAlfa;
     aFicBrowse = "";
     aLong      = aAlfa % 0;
     nLong      = aLong;
     |PARA nCarac = 2;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos  = (nCarac * 100) + 1;
           aFicBrowse = aFicBrowse + (aAlfa % nPos);
     |SIGUE;

     |SI aFicBrowse = "";
         |ACABA;
     |FINSI;

     |SI #dscsvz15#0 = "E";
         |HAZ ConversionEOS;
     |FINSI;

     |SI #dscsvz15#0 = "C";
         |HAZ ConversionConta;
     |FINSI;
|FPRC;

|| ------------------------------------------------------------------------

|PROCESO Cancelar;
     |PTEC 807;
|FPRC;

|PROCESO ModAsiento;
     |FIN_CONTROL_WINDOWS nBtnUni;

     |SI aModAsiento = "SI";
         aModAsiento = "NO";
     |SINO;
         aModAsiento = "SI";
     |FINSI;

     aAlfa = "BOTON," + aModAsiento;
     |CONTROL_SIMPLE nPanta, aAlfa, 2416, 2619, ModAsiento; nBtnUni = FSalida;
|FPRC;

|PROCESO Instrucciones;
     |HAZ LeeUnidadBasico;

     eaNombrePDF = "dscsvz15";
     |HAZ MuestraPDF;
|FPRC;

|PROCESO Ubica;
     nFSalida = FSalida;

     |ESTADO_CONTROL nBtnCar1, "DISABLE";
     |ESTADO_CONTROL nBtnCar2, "DISABLE";
     |ESTADO_CONTROL nBtnCar3, "DISABLE";
     |ESTADO_CONTROL nBtnCar4, "DISABLE";
     |ESTADO_CONTROL nBtnDef,  "DISABLE";

     |HAZ Browse;

     |SI #dscsvz15#0 = "C";
         |ESTADO_CONTROL nBtnCar1, "ENABLE";
         |ESTADO_CONTROL nBtnCar2, "ENABLE";
         |ESTADO_CONTROL nBtnDef,  "ENABLE";
     |FINSI;

     |ESTADO_CONTROL nBtnCar3, "ENABLE";
     |ESTADO_CONTROL nBtnCar4, "ENABLE";

     |SI aFicBrowse = "";  |ACABA;  |FINSI;

     |SI nFSalida = nBtnCar1;
         aOrigen  = aFicBrowse;
         aDestino = aPathTmp + "conceptos.csv";

         |SI eaIP = "";
             |COPIA_FICHERO aOrigen, aDestino;
         |SINO;
             |RECIBE_FICHERO aOrigen, aDestino;
         |FINSI;

         #dscsvz15#1 = aDestino;  |PINTA #dscsvz15#1;
     |FINSI;

     |SI nFSalida = nBtnCar2;
         aOrigen  = aFicBrowse;
         aDestino = aPathTmp + "diario.csv";

         |SI eaIP = "";
             |COPIA_FICHERO aOrigen, aDestino;
         |SINO;
             |RECIBE_FICHERO aOrigen, aDestino;
         |FINSI;

         #dscsvz15#2 = aDestino;  |PINTA #dscsvz15#2;
     |FINSI;

     |SI nFSalida = nBtnCar3;
         aOrigen  = aFicBrowse;
         aDestino = aPathTmp + "ivaventa.csv";

         |SI eaIP = "";
             |COPIA_FICHERO aOrigen, aDestino;
         |SINO;
             |RECIBE_FICHERO aOrigen, aDestino;
         |FINSI;

         #dscsvz15#3 = aDestino;  |PINTA #dscsvz15#3;
     |FINSI;

     |SI nFSalida = nBtnCar4;
         aOrigen  = aFicBrowse;
         aDestino = aPathTmp + "ivacompra.csv";

         |SI eaIP = "";
             |COPIA_FICHERO aOrigen, aDestino;
         |SINO;
             |RECIBE_FICHERO aOrigen, aDestino;
         |FINSI;

         #dscsvz15#4 = aDestino;  |PINTA #dscsvz15#4;
     |FINSI;
|FPRC;

|PROCESO T0C0;  |TIPO 0;
     |SI #dscsvz15#0 = "E";
         |ESTADO_CONTROL nBtnCar1, "DISABLE";
         |ESTADO_CONTROL nBtnCar2, "DISABLE";
         |ESTADO_CONTROL nBtnDef,  "DISABLE";
     |SINO;
         |ESTADO_CONTROL nBtnCar1, "ENABLE";
         |ESTADO_CONTROL nBtnCar2, "ENABLE";
         |ESTADO_CONTROL nBtnDef,  "ENABLE";
     |FINSI;

     #dscsvz15#1 = "";   |PINTA #dscsvz15#1;
     #dscsvz15#2 = "";   |PINTA #dscsvz15#2;
     #dscsvz15#3 = "";   |PINTA #dscsvz15#3;
     #dscsvz15#4 = "";   |PINTA #dscsvz15#4;
|FPRC;

|PROCESO BtnEOS;
     #dscsvz15#0 = "E";
     |ESTADO_CONTROL nBtnEOS, "DISABLE";
     |ESTADO_CONTROL nBtnCon, "ENABLE";

     |FIN_CONTROL_WINDOWS nLblImp;

     |CONTROL_SIMPLE nPanta, "LABEL,{{10}}IMPORTACION EOS", 0835, 0898, NULL;
     nLblImp = FSalida;

     |HAZ T0C0;
|FPRC;

|PROCESO BtnCon;
     #dscsvz15#0 = "C";
     |ESTADO_CONTROL nBtnCon, "DISABLE";
     |ESTADO_CONTROL nBtnEOS, "ENABLE";

     |FIN_CONTROL_WINDOWS nLblImp;

     |CONTROL_SIMPLE nPanta, "LABEL,{{10}}IMPORTACION CONTABILIDAD", 0835, 0898, NULL;
     nLblImp = FSalida;

     |HAZ T0C0;
|FPRC;

|PROGRAMA;
     |LEE 000#camaasil.p;
     |SI FSalida = 0;
         |CONFI "0000NYa existe movimientos en esta empresa/periodo. Continuamos con la importaci¢n?";
         |SI FSalida ! 0;  |ACABA;  |FINSI;
     |SINO;
         |LEE 000#camaniva.p;
         |SI FSalida = 0;
             |MENAV "0000Ya existe movimientos de IVA en esta empresa/periodo. Cancelamos la importaci¢n";
             |ACABA;
         |FINSI;
     |FINSI;

     aTextoVentana = "Importaci¢n CSV NCS";
     |VENTANA 0501, 3599, -1, 16, aTextoVentana;
     n0Vent = FSalida;

     |PINPA #0#dscsvz15;  nPanta = FSalida;
     |PINDA #0#dscsvz15;
     |PTEC 802;  |PAUSA;

     aModAsiento = "NO";

     |CONTROL_SIMPLE nPanta, "LABEL,{{15}}Asientos e IVA", 2502, 2515, NULL;
     |CONTROL_SIMPLE nPanta, "LABEL,{{15}}se dejan modificables", 2521, 2540, NULL;

     aAlfa = "BOTON," + aModAsiento;
     |CONTROL_SIMPLE nPanta, aAlfa, 2416, 2619, ModAsiento; nBtnUni = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{137}}Instrucciones", 0585, 0599, Instrucciones;
     nBtnInfo = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}EOS",          0630, 0637, BtnEOS; nBtnEOS = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Contabilidad", 0640, 0655, BtnCon; nBtnCon = FSalida;
     |CONTROL_SIMPLE nPanta, "LABEL,{{10}}IMPORTACION EOS", 0835, 0898, NULL; nLblImp = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}", 1185, 1187, Ubica; nBtnCar1 = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}", 1485, 1487, Ubica; nBtnCar2 = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}", 1785, 1787, Ubica; nBtnCar3 = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}", 2085, 2087, Ubica; nBtnCar4 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Definir Tipo de Operaci¢n", 3502, 3525, EntraM11;
     nBtnDef = FSalida;

     |ESTADO_CONTROL nBtnEOS,  "DISABLE";
     |ESTADO_CONTROL nBtnCar1, "DISABLE";
     |ESTADO_CONTROL nBtnCar2, "DISABLE";
     |ESTADO_CONTROL nBtnDef,  "DISABLE";

     |CONTROL_SIMPLE 0, "BOTON,{{207}}Cancelar", 3570, 3582, Cancelar;
     nBtnCan = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::{{230}}Continuar";
          |LEETECLA aTecla;

          |SI aTecla = aEsc1;  |O  aTecla = aRetu;
              nError = 0;
              |HAZ Chequeo;
              |SI nError = 0;  |SAL;  |FINSI;
          |FINSI;

          |SI aTecla = aEsc2; |SAL;  |FINSI;
     |SIGUE;

     |SI aTecla = aEsc2;
         |MENAV "0000Proceso cancelado.";
         |FINVENTANA n0Vent;
         |ACABA;
     |FINSI;

     |FIN_CONTROL_WINDOWS nBtnCar1;
     |FIN_CONTROL_WINDOWS nBtnCar2;
     |FIN_CONTROL_WINDOWS nBtnCar3;
     |FIN_CONTROL_WINDOWS nBtnCar4;
     |FIN_CONTROL_WINDOWS nBtnDef;
     |FIN_CONTROL_WINDOWS nBtnCan;
     |FIN_CONTROL_WINDOWS nBtnEOS;
     |FIN_CONTROL_WINDOWS nBtnCon;

     |HAZ Importacion;

     |FINVENTANA n0Vent;

     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
|FPRO;

|PROCESO BorraDocs;
     aAlfa = aPathTmp + "diario.csv";        |FBORRA aAlfa;
     aAlfa = aPathTmp + "operacion.csv";     |FBORRA aAlfa;
     aAlfa = aPathTmp + "ivaventa.csv";      |FBORRA aAlfa;
     aAlfa = aPathTmp + "ivacompra.csv";     |FBORRA aAlfa;
     aAlfa = aPathTmp + "conceptos.csv";     |FBORRA aAlfa;
|FPRC;

|PROCESO T80;  |TIPO 80;
     nChequeo = 0;
     eaIP     = "";  |IP_REMOTO eaIP;

     |HAZ LeeUnidadBasico;

     aPathLocal = eaUnidadBasico + "\documentos";   |RMKDIR aPathLocal;
     aPathLocal = aPathLocal + "\tmp";              |RMKDIR aPathLocal;
     aPathLocal = aPathLocal + "\";

     |DBASE aBaseConta;
     aPathTmp   = aBaseConta + "tmp";           |MKDIR aPathTmp;
     aPathTmp   = aPathTmp   + "/" + Usuario;   |MKDIR aPathTmp;
     aPathTmp   = aPathTmp   + "/";

     aPathContagen = eaUnidadBasico + "\documentos";   |RMKDIR aPathContagen;
     aPathContagen = aPathContagen + "\contagen\";     |RMKDIR aPathContagen;

     |HAZ BorraDocs;

     |ABRET #cacuenta;

     |HAZ inicio;

     |ABRET #dscsvm11;
     |ABRET #caclipro;
     |ABRET #camaasic;
     |ABRET #camaasil;
     |ABRET #camaniva;
     |ABRET #caivalin;
     |ABRET #dsmom030;
     |ABRET #caintiva;
     |ABRET #cainliva;
     |ABRET #cacuiv2c;

     |PULSATECLA;
     |PULSATECLA;
|FPRC;

|PROCESO T90;  |TIPO 90;
     |HAZ BorraDocs;

     |CIERRAT #dscsvm11;
     |CIERRAT #cacuenta;
     |CIERRAT #caclipro;
     |CIERRAT #camaasic;
     |CIERRAT #camaasil;
     |CIERRAT #camaniva;
     |CIERRAT #caivalin;
     |CIERRAT #caintiva;
     |CIERRAT #cainliva;
     |CIERRAT #dsmom030;
     |CIERRAT #cacuiv2c;

     aFicTmp = "dscsvw06" + Usuario;  |NOME_DAT #dscsvw06#aFicTmp#;
     aFicTmp = "dscsvw10" + Usuario;  |NOME_DAT #dscsvw10#aFicTmp#;
     aFicTmp = "dscsvw20" + Usuario;  |NOME_DAT #dscsvw20#aFicTmp#;
     aFicTmp = "dscsvw21" + Usuario;  |NOME_DAT #dscsvw21#aFicTmp#;

     |ABRE #dscsvw06;  |CIERRA #dscsvw06;  |DELINDEX #dscsvw06;
     |ABRE #dscsvw10;  |CIERRA #dscsvw10;  |DELINDEX #dscsvw10;
     |ABRE #dscsvw20;  |CIERRA #dscsvw20;  |DELINDEX #dscsvw20;
     |ABRE #dscsvw21;  |CIERRA #dscsvw21;  |DELINDEX #dscsvw21;

     |PULSATECLA;
     |PULSATECLA;
|FPRC;
