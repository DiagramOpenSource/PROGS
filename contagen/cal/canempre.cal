|FICHEROS;
   canempre #0;         || para que sirvan las rutinas del 'basica'
   canempre #1;

   caportad #12;
   :/basica/def/agicen14  #114;
   :/modelos/def/dsmom100 #100;

   ca8ctrle #400;
   ca8comay #401;
   Referen@ #5000;
   ReferLi@ #5001;
|FIN;

|VARIABLES;
   &entrada  = 1;

   error1    = "0000Nivel repetido o valor mas bajo que los anteriores";
   traba     = "";
   traba1    = 0;
   alfa1     = "";
   alfa2     = "";
   alfa3     = "";
   alfa4     = "";
   aAlfa1    = "";
   aAlfa2    = "";
   aAlfa3    = "";
   nume0     = 0;
   nume1     = 0;
   perio     = 0;
   men6      = "    NConfirma el Alta en 'Clientes General' (S/N): ";

   con       = 0;
   aste      = "";
   longi     = "";
   num       = 0;
   limpe1    = 0;
   limpe2    = 0;
   men1      = "2405Desde Periodo: ";
   men2      = "2440Hasta Periodo: ";
   opcion    = 0;
   {-1}menu  = "";
   menu1     = "2400";
   menu2     = "1";
   menu3     = " Elija ordenacion del informe: ";
   menu4     = "AC";
   menu5     = " Alfabetica ";
   menu6     = " Codigos ";
   dire      = "";

   aCampo30  = "";
   aCampo31  = "";
   letra     = "";
   final     = "";
   x         = "";
   nume2     = 0;
   lugar     = 0;
   nifnum    = 0;
   resto     = 0;
   Comodin   = "";
   elCodPostal = "";
   &any_pos  = 0;
   nSwCambia = 0;
   aAlfa     = "";
   nNume     = 0;

   swEntrada  = 0;

  &enEjerci  = 0;
  &enEmpres  = 0;
  &enPeriod  = 0;
  &enActivi  = 0;
  &eaCodSub  = "";
  &eaVarDni  = "";
  &enCalcCif = 0;

  fFecha1  = @;
   nCam4   = 0;
   nCam5   = 0;
   nCam6   = 0;
   nCam7   = 0;
   nCam8   = 0;
   nCam9   = 0;
   nCam10  = 0;
   nSwCarga = 0;

   aDepAnt = "";
   aSubAnt = "";
   &enCambio = 0;
   &enDesde  = 0;
   &eaPlan0  = "";
   &eaPlan1  = "";
|FIN;

|INCLUYE dscont_i;
|INCLUYE datpor_i;

|PROCESO letra_0;  |TIPO 6;
   |SI FSalida = 10;
      enCalcCif = 1;
      eaVarDni  = #canempre#Campo#;
      |HAZ CalculoDNI;
      #canempre#Campo# = eaVarDni;
      |PINTA #canempre#Campo#;
      |ERROR;
      |ACABA;
   |FINSI;

     eaVarDni  = #canempre#Campo#;
     enCalcCif = 0;
     |HAZ CalculoDNI;
     eaVarDni = (eaVarDni + "                ") % 115;
     |SI #canempre#Campo# ! eaVarDni;
         |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
         |SI FSalida = 0;
             #canempre#Campo# = eaVarDni;  |PINTA #canempre#Campo#;
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO clave; |TIPO 0;
   traba1 = ((#canempre#2*10) + #canempre#3);
   traba = traba1;
   traba = "0000000" + traba;
   alfa1 = traba % -106;
   #canempre#0 = alfa1;
|FPRC;

|PROCESO mismos; |TIPO 0;

   nume1 = (FEntrada / 100) ! 0;

   |SI nume1 = 1;
       perio = #canempre#3;
       #canempre#3 = #canempre#3 - 1;
       |SI #canempre#3 < 0; #canempre#3 = 9; |FINSI;
       |LEE 001#1=;
       |SI FSalida = 0;
           #canempre#26 = #canempre#26 + 1;
           |SI #canempre#26 = 100;
               #canempre#26 = 0;
           |FINSI;
       |SINO;
           |SI nSwCarga = 0;
               |HAZ LeeStandar;
                nSwCarga = 1;
           |FINSI;
           #canempre#13 = nCam4;
           #canempre#14 = nCam5;
           #canempre#15 = nCam6;
           #canempre#16 = nCam7;
           #canempre#17 = nCam8;
           #canempre#18 = nCam9;
           #canempre#19 = nCam10;
           fFecha1 = ~;
           aAlfa1        = fFecha1; aAlfa1 = aAlfa1 % -102;
           #canempre#26  = aAlfa1;
       |FINSI;
       #canempre#3 = perio;
       traba1 = ((#canempre#2*10) + #canempre#3);
       traba = traba1;
       traba = "0000000" + traba;
       alfa1 = traba % -106;
       #canempre#0 = alfa1;
       #canempre#34 = "01.01.0000";
       #canempre#35 = "";
       #canempre#28 = "E";
       #canempre#29 = "";
       |PINDA #0#1;

   |FINSI;
|FPRC;

|PROCESO mes; |TIPO 0;
   |SI #canempre#11 = 1;       ||Si el mes desde es 1 entonces el hasta es 12
      #canempre#12 = 12;
   |SINO;
      #canempre#12 = #canempre#11 - 1; || Si no es uno menos
   |FINSI;
   |PINTA #canempre#12;
|FPRC;

|PROCESO nivel;|TIPO 7;
   |CAMPO_MODIFICA #canempre#14, 1,"S";
   |CAMPO_MODIFICA #canempre#15, 1,"S";
   |CAMPO_MODIFICA #canempre#16, 1,"S";
   |CAMPO_MODIFICA #canempre#17, 1,"S";
   |CAMPO_MODIFICA #canempre#18, 1,"S";
   |CAMPO_MODIFICA #canempre#19, 1,"S";
   |SI #canempre#13 < 6;
      |CAMPO_MODIFICA #canempre#19, 1,"N";
      #canempre#19 = 0;
      |PINTA #canempre#19;
      |SI #canempre#13 < 5;
         |CAMPO_MODIFICA #canempre#18, 1,"N";
         #canempre#18 = 0;
         |PINTA #canempre#18;
         |SI #canempre#13 < 4;
            |CAMPO_MODIFICA #canempre#17, 1,"N";
            #canempre#17 = 0;
            |PINTA #canempre#17;
            |SI #canempre#13 < 3;
               |CAMPO_MODIFICA #canempre#16, 1, "N";
               #canempre#16 = 0;
               |PINTA #canempre#16;
               |SI #canempre#13 < 2;
                  |CAMPO_MODIFICA #canempre#15, 1,"N";
                  #canempre#15 = 0;
                  |PINTA #canempre#15;
               |FINSI;
            |FINSI;
         |FINSI;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO nivelrep;|TIPO 0;  || Para comprobar que no repetimos los niveles
   |SI FSalida = 2; |ACABA; |FINSI;

   |SI Campo = 15;  |Y #canempre#15 = 0; |Y #canempre#13 ] 2;
      |MENAV error1;
      |ERROR;
      |ACABA;
   |FINSI;

   |SI Campo = 16;  |Y #canempre#16 = 0; |Y #canempre#13 ] 3;
      |MENAV error1;
      |ERROR;
      |ACABA;
   |FINSI;
   |SI Campo = 17;  |Y #canempre#17 = 0; |Y #canempre#13 ] 4;
      |MENAV error1;
      |ERROR;
      |ACABA;
   |FINSI;
   |SI Campo = 18;  |Y #canempre#18 = 0; |Y #canempre#13 ] 5;
      |MENAV error1;
      |ERROR;
      |ACABA;
   |FINSI;
   |SI Campo = 19;  |Y #canempre#19 = 0; |Y #canempre#13 ] 6;
      |MENAV error1;
      |ERROR;
      |ACABA;
   |FINSI;

   |SI Campo = 15; |Y #canempre#15 ! 0;    || Si hay dos niveles iguales o menor error
      |SI #canempre#15 [ #canempre#14;
         |MENAV error1;
         |ERROR;
         |ACABA;
      |FINSI;
   |FINSI;

   |SI Campo = 16; |Y #canempre#16 ! 0; || Si hay dos niveles iguales o menor error
      |SI #canempre#16 [ #canempre#14; |O #canempre#16 [ #canempre#15;
         |MENAV error1;
         |ERROR;
         |ACABA;
      |FINSI;
   |FINSI;

   |SI Campo = 17; |Y #canempre#17 ! 0; || Si hay dos niveles iguales o menor error
      |SI #canempre#17 [ #canempre#14; |O #canempre#17 [ #canempre#15; |O #canempre#17 [ #canempre#16;
         |MENAV error1;
         |ERROR;
         |ACABA;
      |FINSI;
   |FINSI;

   |SI Campo = 18; |Y #canempre#18 ! 0; || Si hay dos niveles iguales o menor error
      |SI #canempre#18 [ #canempre#14; |O #canempre#18 [ #canempre#15; |O #canempre#18 [ #canempre#16; |O #canempre#18 [ #canempre#17;
         |MENAV error1;
         |ERROR;
         |ACABA;
      |FINSI;
   |FINSI;

   |SI Campo = 19; |Y #canempre#19 ! 0;    || Si hay dos niveles iguales o menor error
      |SI #canempre#19 [ #canempre#14; |O #canempre#19 [ #canempre#15; |O #canempre#19 [ #canempre#16; |O #canempre#19 [ #canempre#17; |O #canempre#19 [ #canempre#18;
         |MENAV error1;
         |ERROR;
         |ACABA;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO mkdir; |TIPO 30;
   nume0 = FEntrada / 100;  nume0 = nume0 ! 0;  || alta

   |SI nume0 = 1;

       |DFICE alfa2;
       alfa3 = #canempre#0;    alfa3 = "000000" + #canempre#0;    alfa3 = alfa3 % -106;
       alfa1 = alfa2 + alfa3;
       |MKDIR alfa1;
       fich_alta = alfa1 + "/";

       enEmpresa = #canempre#2;
       enPeriodo = #canempre#3;
       eaMoneda = #canempre#28;
       swOperacion = 3;
       |SUB_EJECUTA_NP "camoneda";
       swOperacion = 0;

       ||Creo la ficha en el fichero de portadas de modelos
       |HAZ CreaPortada;
       any_pos = #canempre#26;
       |SUB_EJECUTA "cacrebal";

       || Compruebo si hay cartera y si la hay compruebo que el ejercicio
       ||    anterior estaba en el control de recibos de contabilidad. Si es
       ||    asi, creo el registro del nuevo periodo en el control
       |DBASS aAlfa; |QBF aAlfa;
       aAlfa = aAlfa + "dscarter/def/dscam042.mas";
       |CARGA_DEF aAlfa, Referen@;
       |SI FSalida = 0;
          |DBASS aAlfa; |QBF aAlfa;
          aAlfa = aAlfa + "dscarter/def/dscam043.mas";
          |CARGA_DEF aAlfa, ReferLi@;

          |DBASS aAlfa; |QBF aAlfa;
          aAlfa = aAlfa + "dscarter/fich/";
          |PATH_DAT #Referen@#aAlfa#; |PATH_DAT #ReferLi@#aAlfa#;
          |ABRE #Referen@; |ABRE #ReferLi@;
          #Referen@#0 = #canempre#2;
          #Referen@#1 = #canempre#3;
          |LEE 000#Referen@.=;
          |SI FSalida ! 0;
             #Referen@#0 = #canempre#2;
             #Referen@#1 = #canempre#3 - 1;
             |LEE 000#Referen@.=;
             |SI FSalida = 0;
                #ReferLi@#0 = #Referen@#0;
                #ReferLi@#1 = #Referen@#1;
                #ReferLi@#2 = 1;
                |LEE 000#ReferLi@.];
                |PARA; |SI FSalida = 0; |Y #ReferLi@#0 = #Referen@#0; |Y #ReferLi@#1 = #Referen@#1; |HACIENDO;
                   |SALVA_FICHA #ReferLi@;
                   #ReferLi@#1 = #canempre#3;
                   |GRABA 020#ReferLi@.n;
                   |REPON_FICHA #ReferLi@;
                   |LEE 000#ReferLi@.s;
                |SIGUE;
                #Referen@#1 = #canempre#3;
                |GRABA 020#Referen@.n
             |FINSI;
          |FINSI;
          |CIERRA #Referen@; |CIERRA #ReferLi@;
          |DESCARGA_DEF #Referen@; |DESCARGA_DEF #ReferLi@;
       |FINSI;
  |SINO;
       |SI aDepAnt ! #canempre#30; |O aSubAnt ! #canempre#31;
           enCambio = 0; |SI aDepAnt ! #1#30; enCambio = 1; |FINSI;
           enEmpresa = #canempre#2;
           enPeriodo = #canempre#3;
           eaDfDepC  = #canempre#30;
           eaDfSubC  = #canempre#31;
           eaDfDepN  = #canempre#32;
           eaDfSubN  = #canempre#33;
           eaDepar   = #canempre#24;
           eaSubde   = #canempre#22;
           |SUB_EJECUTA_NP "caut0003;1";
       |FINSI;
  |FINSI;
|FPRC;

|PROCESO CreaPortada;
   |ABRE #caportad;
   |DDEFECTO #caportad;
   #caportad#0  = #canempre#2;             || Empresa
   #caportad#1  = #canempre#26;            || Ejercicio
   |LEE 101#caportad.=;
   |SI FSalida ! 0;
      |DDEFECTO #caportad;
      #caportad#0  = #canempre#2;             || Empresa
      #caportad#1  = #canempre#26;            || Ejercicio
      |GRABA 020#caportad.b;
   |FINSI;

  |SI HayAcceso = 0; |Y Haybasica = 0; ||No existe ni acceso ni basica
      |HAZ desdecontab;                ||Lee de Canempre
  |SINO;
      enCodCli = #caportad#0;
      |HAZ LeePersona;
      |SI ewClien = 0;
          |HAZ desdebasica;
      |SINO;
          |HAZ desdecontab;
      |FINSI;
      |HAZ AltaNif;
  |FINSI;
  |GRABA 020#caportad.a;
  |LIBERA #caportad;
  |CIERRA #caportad;
|FPRC;

|PROCESO rmdir; |TIPO 2;
   nume0 = FEntrada / 100;
   nume0 = nume0 ! 0;
   |SI nume0 ! 3;  |ACABA;  |FINSI;
   |DFICE alfa2;
   alfa3 = #canempre#0;    alfa3 = "000000" + #canempre#0;    alfa3 = alfa3 % -106;
   dire = "";
   |DIRECTORIO dire;
   alfa1 = dire + "bin/agirm -r " + alfa2 + alfa3;
   |SYSTEM alfa1;
|FPRC;

||*****************************************************************
||  MANTENIMIENTO GENERAL
||*****************************************************************

|PROCESO bajacli; |TIPO 1;
   aDepAnt = #canempre#30;
   aSubAnt = #canempre#31;

   con = (FEntrada / 100) ! 0;
   |SI con ! 3;      |ACABA;       |FINSI; || Solo Baja

   enEmpresa = #canempre#2;
   enPeriodo = #canempre#3;
   swOperacion = 2;
   |SUB_EJECUTA_NP "camoneda.tab";
   swOperacion = 0;

   |ABRE #400;
   #400#0 = #canempre#2;
   #400#1 = #canempre#3;
   |LEE 141#400=;
   |SI FSalida = 0;
       |BORRA 040#400a;
   |FINSI;
   |LIBERA #400;
   |CIERRA #400;

   |ABRE #caportad;
   #caportad#0  = #canempre#2;             || Empresa
   #caportad#1  = #canempre#26;            || Ejercicio
   |LEE 141#caportad.=;
   |SI FSalida = 0;
       |BORRA 020#caportad.a;
   |FINSI;
   |LIBERA #caportad;
   |CIERRA #caportad;
|FPRC;

|PROCESO altacli; |TIPO 0;
   con = (FEntrada / 100) ! 0;
   |SI con ! 1;      |ACABA;       |FINSI; || Solo Alta

   |SI HayAcceso = 0; |Y Haybasica = 0; |ACABA; |FINSI;

   |ABRE #clientes@;
   #clientes@#0 = #canempre#2;
   |LEE 101#clientes@.=;
   |SI FSalida = 0;                || Recoge variables del basica
       |SI HayAcceso = 1;
           #clientes@#65 = "S";
       |SINO;
           #clientes@#23 = "S";
       |FINSI;
       |GRABA 020#clientes@.a;             || Graba el basica
       |HAZ pinta_cli;
   |SINO;
       |CONFI men6;
       |SI FSalida = 0;
           |PUSHV 0501 2380;
           |PINPA #0#clientes@;
           |DDEFECTO #clientes@;
           |PINDA #0#clientes@;
           #clientes@#0 = #canempre#2;
           |PINTA #clientes@#0;
           |ENDATOS #1#clientes@;
           |SI FSalida = 0;
               |POPV;
               |SI HayAcceso = 1;
                   #clientes@#65 = "S";
               |SINO;
                   #clientes@#23 = "S";
               |FINSI;
               |GRABA 020#clientes@.n;                 || Graba el basica
               |HAZ pinta_cli;
            |SINO;
               |POPV;
               |ERROR;
            |FINSI;
       |FINSI;
   |FINSI;
   |LIBERA #clientes@;
   |CIERRA #clientes@;
|FPRC;

|PROCESO pinta_cli;
   |SI HayAcceso = 1;
      #canempre#1  = #clientes@#2;   |PINTA #canempre#1;       || nombre
      #canempre#4  = #clientes@#1;   |PINTA #canempre#4;       || razon social
      alfa1 = #clientes@#30;   |QBF alfa1;   alfa2 = #clientes@#31;
      alfa1 = alfa1 + ", " + alfa2;
      #canempre#5  = alfa1;  |PINTA #canempre#5;        || direccion (numero incluido)
      |HAZ postal;
      #canempre#6  = elCodPostal; |PINTA #canempre#6;        || codigo postal
      #canempre#7  = #clientes@#37; |PINTA #canempre#7;        || poblacion
      #canempre#8  = #clientes@#3;  |PINTA #canempre#8;        || cif
      #canempre#9  = #clientes@#54; |PINTA #canempre#9;        || telefono
      #canempre#10 = #clientes@#55; |PINTA #canempre#10;       || fax
   |SINO;
      #canempre#1  = #clientes@#2;   |PINTA #canempre#1;       || nombre
      #canempre#4  = #clientes@#1;   |PINTA #canempre#4;       || razon social
      alfa1 = #clientes@#4;   |QBF alfa1;   alfa2 = #clientes@#5;
      alfa1 = alfa1 + ", " + alfa2;
      #canempre#5  = alfa1;  |PINTA #canempre#5;        || direccion (numero incluido)
      |HAZ postal;
      #canempre#6  = elCodPostal; |PINTA #canempre#6;        || codigo postal
      #canempre#7  = #clientes@#11; |PINTA #canempre#7;        || poblacion
      #canempre#8  = #clientes@#13; |PINTA #canempre#8;        || cif
      #canempre#9  = #clientes@#14; |PINTA #canempre#9;        || telefono
      #canempre#10 = #clientes@#15; |PINTA #canempre#10;       || fax
   |FINSI;
|FPRC;

|PROCESO postal;
   |SI HayAcceso = 1;
      alfa1 = #clientes@#35;  alfa1 = "00"  + alfa1;  alfa1 = alfa1 % -102;
      alfa2 = #clientes@#36;  alfa2 = "000" + alfa2;  alfa2 = alfa2 % -103;
   |SINO;
      alfa1 = #clientes@#09;  alfa1 = "00"  + alfa1;  alfa1 = alfa1 % -102;
      alfa2 = #clientes@#10;  alfa2 = "000" + alfa2;  alfa2 = alfa2 % -103;
   |FINSI;
   elCodPostal = alfa1 + alfa2;
|FPRC;


|| **********************************************************************
|PROCESO detalle;
   |PRINT;
|FPRC;

|DEFBUCLE codigo;
   #0#1;
   ;
   1, limpe1;
   99999, limpe2;
   e;
   NULL, detalle;
|FIN;

|DEFBUCLE nombre;
   #0#1;
   ;
   1, limpe1;
   99999, limpe2;
   e;
   NULL, NULL, NULL, NULL, NULL, detalle;
   #0#1, #0#2, #0#3;
|FIN;

|PROCESO tipo4; |TIPO 4;
|| ventana de periodo
   |PUSHV 2053 2372;
   |ATRI I;
   |CUADRO 20 53 23 72;
   |ATRI N;
   |PINTA 2154 ," Desde Periodo:   ";
   |PINTA 2254 ," Hasta Periodo:   ";
   |ATRI 0;
   E_inf = 0;
   E_sup = 9;
   limpe1 = 2170 ? 1;
   |SI FSalida ! 0; |POPV; |ACABA; |FINSI;
   E_inf = 0;
   E_sup = 9;
   limpe2 = 2270 ? 1;
   |SI FSalida ! 0; |POPV; |ACABA; |FINSI;
   |POPV;
   |MENU menu;
   opcion = FSalida;
   |SI opcion > 0;
      |IMPRE 0;
      |SI FSalida = 0;
         |INFOR "canempre";
         |SI opcion = 1;  |BUCLE nombre; |FINSI;
         |SI opcion = 2;  |BUCLE codigo; |FINSI;
         |PIEINF;
         |FININF;
      |SINO;
         |MENAV "    Proceso abortado ...";
      |FINSI;
      |FINIMP;
   |FINSI;
|FPRC;

|PROCESO SiCambia; |TIPO 7;
|FPRC;

|PROCESO PonTitulo;  |TIPO 7;
     aAlfa = #0#29;  |QBF aAlfa;
     |SI Haybasica = 0;
         |SI aAlfa = "";
             |SI #0#26 ] 80;
                  nNume = 1900 + #0#26;
             |SINO;
                  nNume = 2000 + #0#26;
             |FINSI;

            aAlfa1 = #0#1;     |QBF aAlfa1;
            aAlfa =  "[" + (("00000" + #0#2) % -105) + "/" + nNume + "] " + aAlfa1;;

         |FINSI;

     |SINO;

         |ABRE #114;
         #114#0 = #0#2;
         #114#1 = 1;
         |LEE 040#114];
         |SI FSalida ! 0;  |O #114#0 ! #0#2;
             |DDEFECTO #114;
         |FINSI;
         |CIERRA #114;

         |SI aAlfa = "";
             |SI #0#26 ] 80;
                  nNume = 1900 + #0#26;
             |SINO;
                  nNume = 2000 + #0#26;
             |FINSI;

            aAlfa1 = #0#1;     |QBF aAlfa1;
            aAlfa2 = #114#8;   |QBF aAlfa2;
            aAlfa3 = #114#10;  |QBF aAlfa3;
            aAlfa =  "[" + (("00000" + #0#2) % -105) + "/" + nNume + "] " + aAlfa1 + " " + aAlfa2 + " " + aAlfa3;
         |FINSI;
     |FINSI;
     #0#29 = aAlfa;
     |PINTA #0#29;
|FPRC;

|CALCULO losDatos; |TIPO 13;
 nume1    = (FEntrada / 100) ! 0;

 enEmpres = #canempre#2;
 enPeriod = #canempre#3;
 enEjerci = #canempre#26;
 enActivi = #canempre#30;
 eaCodSub = #canempre#31;

 |SI enEjerci > 80;
     enEjerci = enEjerci + 1900;
 |SINO;
     enEjerci = enEjerci + 2000;
 |FINSI;

 |PINTA 0862, "                  ";
 |SI swEntrada = 1;
     |PUSHV 2401 2480;
     |SUB_EJECUTA_NP "caz00002";
     |POPV;
     |POPV;
 |FINSI;
 swEntrada = 1;

 |SI #canempre#34 > "01.01.0000";
     |ATRI I; |PINTA 1257, "F.Bloqueo: "; |ATRI 0;
     |C_V #canempre#34,1,"S";
 |SINO;
     |PINTA 1257, "                      ";
     |C_V #canempre#34,1,"N";
 |FINSI;

 |SI enDesde = 1;
     #400#2 = eaPlan0;
     #401#2 = eaPlan1;
     |ATRI P; |PINTA 0862, "PCG: "; |ATRI 0;
     |PINTA 0867, #400#2;
     |PINTA 0870, #401#2;
 |FINSI;
|FCAL;

|PROCESO LeeStandar;
     |SALVA_FICHA #canempre;

     nCam4  = 4; nCam5  = 1;
     nCam6  = 2; nCam7  = 3;
     nCam8  = 4; nCam9  = 0;
     nCam10 = 0;
     |ABRE #100;
     |LEE 040#100p;
     |SI FSalida = 0;
         #canempre#2 = #100#1;
         #canempre#3 = #100#2;
         |LEE 040#canempre.=;
         |SI FSalida = 0;
             nCam4  = #canempre#13;
             nCam5  = #canempre#14;
             nCam6  = #canempre#15;
             nCam7  = #canempre#16;
             nCam8  = #canempre#17;
             nCam9  = #canempre#18;
             nCam10 = #canempre#19;
         |FINSI;
    |FINSI;
    |CIERRA #100;
    |REPON_FICHA #canempre;
|FPRC;

|CALCULO defDep; |TIPO 0;
 aAlfa1 = #canempre#24;
 |C_M #canempre#30,1,aAlfa1;
|FCAL;

|CALCULO AntesDep; |TIPO 7;
   |C_M #canempre#Campo#, 0, aAlfa1;
   |SI aAlfa1 = "S"
      |SI Haybasica = 1;
          |MENSA "    Actividad por defecto. <F2> Consultar Actividades";
      |SINO;
          |MENSA "    Departamento por defecto. <F2> Consultar Departamentos";
      |FINSI;
   |FINSI;
|FCAL;

|CALCULO MiraActiv; |TIPO 0;
   |C_M #canempre#Campo#, 0, aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   aCampo30  = Contenido;
   enEmpresa = #canempre#2;
   enPeriodo = #canempre#3;

   eOpDep = 0; || 1 = Consulta pantalla
   |SI FSalida = 10; eOpDep = 1;  |FINSI;
   enActividad = #canempre#Campo#;
   eaCodDep    = #canempre#Campo#;
   |HAZ Actividad;
   |SI eswLect = 1;
       |CONFI "    NError.! No Existe Actividad / Departamento. Continuar S/N ?";
       |SI FSalida ! 0;
           |ERROR;
           |ACABA;
       |FINSI;
       #canempre#32 = "";
   |SINO;
       aAlfa1 = enActividad;
       |SI Haybasica = 1;
           aAlfa1 = ("00" + aAlfa1) % A02;
       |FINSI;
       #canempre#30 = eaCodDep; |PINTA #canempre#30;
       #canempre#32 = eaNombreAct;
       |SI efActFin > "01.01.1900";
           aAlfa1 = "    NAtencion!!!. Actividad con Fecha de BAJA " + efActFin + ", Desea introducir Facturas ? ";
           |CONFI aAlfa1;
           |SI FSalida ! 0;
               |ERROR;
               |ACABA;
           |FINSI;
       |FINSI;
       |SI eaEsCom = "S";
           |CONFI "    NAtencion!!!. Actividad Comunera, ¨ Desea Continuar ? ";
           |SI FSalida ! 0;
               |ERROR;
               |ACABA;
           |FINSI;
       |FINSI;
   |FINSI;

   |SI aCampo30 ! #canempre#30; |HAZ losDatos; |FINSI;
|FCAL;

|CALCULO defSub; |TIPO 0;
 aAlfa1 = "S";
 |SI #canempre#22 = "N"; aAlfa1 = "N"; |FINSI;

 |C_M #canempre#31,1,aAlfa1;
 eaSubde = #canempre#22;
 enSubde = 0; |SI eaSubde = "O"; eaSubde = "S"; enSubde = 1; |FINSI;
|FCAL;

|CALCULO AntesSub; |TIPO 7;
   |C_M #canempre#Campo#, 0, aAlfa1;
   |SI aAlfa1  = "S";
      |SI Haybasica = 1;
          |SI enArrend = 1;
              |MENSA "    Subdepartamento por defecto. <F2> Consultar Referencias Catastrales";
          |SINO;
              |MENSA "    Subdepartamento por defecto. <F2> Consultar Cultivos";
          |FINSI;
      |FINSI;
   |FINSI;
|FCAL;

|CALCULO TrasSub; |TIPO 0;
   |SI #canempre#22 = "N"; |ACABA; |FINSI;

   aCampo31 = Contenido;
   eOpSub = 0; || 1 = Consulta pantalla
   |SI FSalida = 10; eOpSub = 1;  |FINSI;
   enConcepto  = 1;
   eaCodSubdep = #canempre#Campo#;
   enQue = 1;
   |HAZ SubActividad;
   enQue = 0;
   |SI eswLect = 1;
       |CONFI "    NError.! No Existe SubDepartamento. Continuar S/N ?";
       |SI FSalida ! 0;
           |ERROR;
       |FINSI;
       #canempre#33 = "";
   |SINO;
       #canempre#31 = eaCodSubdep; |PINTA #canempre#31;
       #canempre#33 = eaNomSubdep;
   |FINSI;
   |SI aCampo31 ! #canempre#31; |HAZ losDatos; |FINSI;
|FCAL;
