|FICHEROS;
  ca8ut002 #0;

  canempre #30;
  ca8ctrle #400;  ||Control Empresa
  ca8eqmay #402;  ||Los cambios
  ca8m0003 #403;  ||Tabla de Esta Empresa
  ca8m0004 #404;  ||Tabla del Cierre

  caclipro #1;
  cacuenta #2;
  camaasil #4;

  canempre #30;
  caselem1 #998;
|FIN;

|VARIABLES;
   &entrada = 1;
   &tinforme = "";
   aAlfa1    = "";
   nNume1    = 0;
   nNume2    = 0;

   informe   = "ca8ut002";
   desde = "";
   aDCuenta = "";
   aHCuenta = "";
   aDNombre = "";
   aHNombre = "";
   &swInf = 0;
   &swImprimir = 0;
   &aError  = "";
   swSelec  = 0;
   sw = 0;
   aTitulo  = "";
   nImporte = 0;
   &nError   = 0;
   nCambia  = 0;
   aCuenta  = "";
   uno      = "";
   dos      = "";
|FIN;

|INCLUYE dscont_i;

|CALCULO LaSeleccion; |TIPO 0;
 |C_M #0#1, 1,"S"; |C_M #0#2, 1,"S";
 |C_M #0#3, 1,"S"; |C_M #0#4, 1,"S";
 |C_M #0#5, 1,"S"; |C_M #0#6, 1,"S";
 |C_M #0#7, 1,"S"; |C_M #0#8, 1,"S";
 |C_M #0#9, 1,"S"; |C_M #0#10,1,"S";
 |C_M #0#11,1,"S"; |C_M #0#12,1,"S";
 |C_M #0#13,1,"S"; |C_M #0#14,1,"S";
 |SI #0#0 = "C";
      |C_M #0#7, 1,"N"; |C_M #0#8, 1,"N";
      |C_M #0#9, 1,"N"; |C_M #0#10,1,"N";
      |C_M #0#11,1,"N"; |C_M #0#12,1,"N";
      |C_M #0#13,1,"N"; |C_M #0#14,1,"N";
 |FINSI;
 |SI #0#0 = "P";
      |C_M #0#1, 1,"N"; |C_M #0#2, 1,"N";
      |C_M #0#3, 1,"N"; |C_M #0#4, 1,"N";
      |C_M #0#5, 1,"N"; |C_M #0#6, 1,"N";
 |FINSI;
|FCAL;

|CALCULO ElPtec; |TIPO 7;
 |C_M #0Campo,0,aAlfa1;
 |SI aAlfa1 = "S";
     |PTEC 816;
 |FINSI;
|FCAL;

|CALCULO Hasta; |TIPO 0;
 |C_M #0Campo,0,aAlfa1;
 |SI aAlfa1 = "S";
     nNume1 = Campo - 1;
     |SI #0Campo < #0nNume1;
         |MENAV "    Error!. Cuenta Hasta Menor que la Cuenta Desde";
         |ERROR;
     |FINSI;
 |FINSI;
|FCAL;

|CALCULO cuenta;|TIPO 6;
     |C_M #0Campo, 0, aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta; |PINTA #0Campo;
|FCAL;

|CALCULO AntesModi; |TIPO 7;
 aAlfa1 = "S";
 |SI #0#15 = "N";
     aAlfa1 = "N";
     #0#19 = "N"; |PINTA #0#19;
 |FINSI;
 |C_M #0#19, 1, aAlfa1;
|FCAL;

|| **********************************************************************

|CALCULO impre;|TIPO 3;

 |SI #0#16 = "S"
     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |INFOR informe;
     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;
 |FINSI;

 |SI #48#27 = "S";
     |HAZ MultiEmpresa;
 |SINO;
     |DFICE dirfich0; |QBF dirfich0;
     |HAZ HazTodo;
 |FINSI;
 |SI #0#16 = "S"
     |FININF;
     |FINIMP;
 |FINSI;
|FCAL;

||************************************************************************
|PROCESO Actualiza;
   MasMenos     = nImporte;
   DebeHaber    = #4#8;
   aCUENTA      = #4#4;
   nREGISTRO    = #4#24;
   nDIARIO      = #4#0;
   aFECHA       = #4#1;
   nDOCUMENTO   = #4#2;
   nNIVELCUENTA = #4#5;
   |HAZ ActCuenta;          || cuenta
|FPRC;

|PROCESO CambioApertura;
  |SI #404#14 ! " "; |ACABA; |FINSI; ||Se ha modificado manualmente
  #4#0 = #404#10;
  #4#1 = #404#11;
  #4#2 = #404#12;
  #4#3 = #404#0;
  |LEE 101#4=;
  |SI FSalida = 0;
      nImporte = - #4#9;
      |HAZ Actualiza;

      #4#4 = #403#0;
      |SI #4#8 = "D";
           #4#16 = #4#4;
           #4#17 = #4#5;
      |SINO;
           #4#10 = #4#4;
           #4#11 = #4#5;
      |FINSI;
      nImporte = #4#9;
      |HAZ Actualiza;
      |GRABA 020#4a;
  |FINSI;
  |LIBERA #4;

  nError = 3;
  aError = "MODIFICADO EN EL ASIENTO DE APERTURA";
  |SI #0#16 = "S"; |PRINT; sw = 1; |FINSI;

  #404#8 = #403#0;
  |GRABA 020#404a;
  |LIBERA #404;
|FPRC;

|DEFBUCLE CambioApertura;
 #404#2;
 ;
 #403#0, 0001;
 #403#0, 9999;
 be;
 NULL, CambioApertura;
|FIN;

|PROCESO porimpre;
   |SI #403#0 = #403#2; |LIBERA #403; |ACABA; |FINSI;  ||es la misma cuenta
   |SI #403#6 ! " ";    |LIBERA #403; |ACABA; |FINSI;  ||cambiada manualmente

   swSelec = 0;
   |SI desde = "C";
       |SI #0#2 ! doce;
           |SI #403#0 ] #0#1; |Y #403#0 [ #0#2; swSelec = 1; |FINSI;
       |FINSI;
       |SI #0#4 ! doce;
           |SI #403#0 ] #0#3; |Y #403#0 [ #0#4; swSelec = 1; |FINSI;
       |FINSI;
       |SI #0#6 ! doce;
           |SI #403#0 ] #0#5; |Y #403#0 [ #0#6; swSelec = 1; |FINSI;
       |FINSI;
   |FINSI;
   |SI desde = "P";
       |SI #0#8 ! doce;
           |SI #403#0 ] #0#7; |Y #403#0 [ #0#8; swSelec = 1; |FINSI;
       |FINSI;
       |SI #0#10 ! doce;
           |SI #403#0 ] #0#9; |Y #403#0 [ #0#10; swSelec = 1; |FINSI;
       |FINSI;
       |SI #0#12 ! doce;
           |SI #403#0 ] #0#11; |Y #403#0 [ #0#12; swSelec = 1; |FINSI;
       |FINSI;
       |SI #0#14 ! doce;
           |SI #403#0 ] #0#13; |Y #403#0 [ #0#14; swSelec = 1; |FINSI;
       |FINSI;
   |FINSI;
   |SI swSelec = 0; |LIBERA #403; |ACABA; |FINSI;

   #2#0 = #403#2;
   |LEE 001#2=;
   |SI FSalida ! 0; |ACABA; |FINSI;

   |SI #0#19 = "S";
       aTitulo = "";
       #1#23   = "C";
       #1#10   = #2#0;
       |LEE 001#1=;
       |SI FSalida ! 0;
           #1#23 = "P";
           #1#10 = #2#0;
           |LEE 001#1=;
           |SI FSalida = 0;  aTitulo = #1#1; |FINSI;
       |SINO;
           aTitulo = #1#1;
       |FINSI;

       |QBF aTitulo; aAlfa1 = #2#2; |QBF aAlfa1;
       |SI aAlfa1 ! aTitulo; |Y aTitulo ! "";
           nError = 1;
           aError = "TITULO DE CUENTA DIFERENTE AL DE LA FICHA";
           |SI #0#16 = "S"; |PRINT; sw = 1; |FINSI;
           #2#2 = aTitulo;
           |GRABA 020#2a;
       |FINSI;
   |FINSI;

   nCambia = 0;
   eaCuenta = #403#0;
   |HAZ MiraSiCambia;
   |SI nCambia = 1; |LIBERA #403; |ACABA; |FINSI; ||tiene cambio

   |ABRE #4;
   |BUCLE CambioApertura;
   |CIERRA #4;

   nError = 2;
   aError = "IGUALDAD ERRONEA EN LA TABLA DE EQUIVALENCIA";
   |SI #0#16 = "S"; |PRINT; sw = 1; |FINSI;
   #403#2 = #403#0;
   |GRABA 020#403a;
   |LIBERA #403;
|FPRC;

|DEFBUCLE Lee_codigo;
   #403#1;
   ;
   aDCuenta;
   aHCuenta;
   e;
   NULL, porimpre;
|FIN;

|PROCESO Leeimprime;
 sw = 0;

 tinforme = "PROVEEDORES";
 |SI desde = "C"; tinforme = "CLIENTES"; |FINSI;

 |ABRE #402;
 |ABRE #1;
 |ABRE #2;
 |BUCLE Lee_codigo;
 |CIERRA #2;
 |CIERRA #1;
 |CIERRA #402;

 |SI sw = 1;
     |SI #0#16 = "S"; |PIEINF; |FINSI;
 |FINSI;
|FPRC;

|PROCESO HazTodo;

 |SI #0#0 ! "P";
     desde = "C";

     aDCuenta = #0#1;
     |SI aDCuenta > #0#3; |Y #0#4 ! doce; aDCuenta = #0#3; |FINSI;
     |SI aDCuenta > #0#5; |Y #0#6 ! doce; aDCuenta = #0#5; |FINSI;
     aHCuenta = #0#2;
     |SI aHCuenta < #0#4;                 aHCuenta = #0#4; |FINSI;
     |SI aHCuenta < #0#6;                 aHCuenta = #0#6; |FINSI;

     |HAZ Leeimprime;
 |FINSI;

 |SI #0#0 ! "C";
     desde = "P";

     aDCuenta = #0#7;
     |SI aDCuenta > #0#9;  |Y #0#10 ! doce; aDCuenta = #0#9;  |FINSI;
     |SI aDCuenta > #0#11; |Y #0#12 ! doce; aDCuenta = #0#11; |FINSI;
     |SI aDCuenta > #0#13; |Y #0#14 ! doce; aDCuenta = #0#13; |FINSI;
     aHCuenta = #0#8;
     |SI aHCuenta < #0#10;                 aHCuenta = #0#10; |FINSI;
     |SI aHCuenta < #0#12;                 aHCuenta = #0#12; |FINSI;
     |SI aHCuenta < #0#14;                 aHCuenta = #0#14; |FINSI;

     |HAZ Leeimprime;
 |FINSI;
|FPRC;

|| **********************************************************************
|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;
   |SI enEjercicio ! 2008; |ACABA; |FINSI;
   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #2 dirfich0;
   |PATH_DAT #4 dirfich0;
   |PATH_DAT #403 dirfich0;
   |PATH_DAT #404 dirfich0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 000001;
 999999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;

|PROCESO MiraSiCambia;
   nCambia = 0;
   |DDEFECTO #402;
   uno = eaCuenta; |QBF uno;
   dos = uno + "000000000000";

   uno = dos % A104;
   #402#0 = eNum2008;
   #402#1 = uno;
   |LEE 040#402=;
   |SI FSalida = 0;  |HAZ Datos;  |ACABA;  |FINSI;

   uno = dos % A103;
   #402#0 = eNum2008;
   #402#1 = uno;
   |LEE 040#402=;
   |SI FSalida = 0;  |HAZ Datos;  |ACABA;  |FINSI;

   uno = dos % A102;
   #402#0 = eNum2008;
   #402#1 = uno;
   |LEE 040#402=;
   |SI FSalida = 0;  |HAZ Datos;  |FINSI;
|FPRC;

|PROCESO Datos;
   eaPlan8  = #402#3; |QBF eaPlan8;
   aAlfa1 = eaPlan8 % 0;

   nNume1 = aAlfa1;  || Longitud del Cambio
   nNume2 = nNume1 + 100;
   aAlfa1 = eaCuenta % nNume2;

   |SI aAlfa1 ! eaPlan8; nCambia = 1; |FINSI;
|FPRC;
