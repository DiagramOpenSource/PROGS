|FICHEROS;
   cajainli #0;
   cajainca #1;
   cafantas #2;
   cajasald #3;
   cacuenta #4;
   caconasi #20;
   canempre #30;

   caivaasi #200;
   :/integral/def/dsam0103,MANTE;
|FIN;

|VARIABLES;
   aAlfa1 = "";
   &entrada = 1;
   &esalta = 0;
   &adocum = 0;
   &adiari = 0;
   &afecha = @;
   &dpta   = "";
   &sdta   = "";
   &cpartida = "";
   &dpartida = 0;
   paso = 0;
   &cuant = "";
   &diant = 0;
   &coant = 1;
   &deant = "";
   &dhant = "C";
   &dpant = "01";
   &sdant = "";
   &imant = 0;
   &avisos = "";
   alfa1 = "";
   nume1 = 0;
   nume2 = 0;
   sw_movim = 0;
   fecnum = 0;
   aBlancs = "";
   &nSwOpera = 0;
   &snFiltrA= "";
   &snFiltr = "";
   &snFilAc = "";
   &snFilBa = "";

   fFecInicio = @;
   fFecFinal  = @;
   aAlfa      = "";

   nTotal     = 0;
   fFecha     = @;
|FIN;

|INCLUYE dscont_i;

|PROCESO comienza; |TIPO 8;
   |HAZ inicial;
   |SI swerror = 2; |PTEC 807; |ACABA; |FINSI;
   |HAZ DirAplicSt;

   |ABRE #3;
   |LEE 101#3p;
   |SI FSalida ! 0;
      |MENAV "    Error, no Existe fichero <Saldo Dia Anterior> ";
      |LIBERA #3;
      |CIERRA #3;
      |PTEC 807;
      |ACABA;
   |FINSI;
   cpartida = #cajasald#2;
   dpartida = #cajasald#3;
   afecha   = #cajasald#7;
   |SI afecha < fec1;
      fecnum = fec1 - 1;
      afecha = fecnum;
   |FINSI;
   |LIBERA #3;
   |CIERRA #3;
   || ..... Los ultimos;
   |DDEFECTO #1;
   |LEE 001#1u;
   |SI FSalida = 0;
      afecha = #cajainca#1;
      paso = 1;
   |FINSI;
   adiari = #cajainca#0;
   adocum = #cajainca#4;
   dpta   = #cajasald#6;
   sdta   = #cajasald#8;

   enFilConc = 1;          || Conceptos sin IVA
   |HAZ CreaConcepto;

   nSwOpera = 0;
   |SUB_EJECUTA_NP "caz00017";
   aAlfa1 = "cactaesp"; |NOME_DAT #4  aAlfa1;

   |ABRE #200;
   |LEE 000#200p;
   |CIERRA #200;
   snFiltr = #200#95;
   snFilAc = #200#118;
   snFilBa = #200#119;
   snFiltrA = #200#185;
   eaPaDOM1 = #200#114;  ||documento automatico
   eaPaDOM2 = #200#133;  ||permitir repetir diferentes diarios
   eaPaDOM3 = #200#134;  ||permitir ultimo + 1
   eaPaCTA1 = #200#135;  ||permitir alta no estructuras 2008
   eaPaCTA2 = #200#136;  ||Aviso 1990
   eaPaNif1 = #200#152;
   eaPaNif2 = #200#153;
   eaSobreEs = #200#155;
   eaDscomer9 = #200#191;
   eaABanco  = #200#161;
   eaCP347   = #200#164;
   eaTEmbargo = #200#165;
   enCEmbargo = #200#166;
   eaObClPr   = #200#186;

   |SI eaCP347 = "S";
       |C_V #cajainli#19, 1, "S";
       |C_M #cajainli#19, 1, "S";
   |FINSI;
|FPRC;

|PROCESO salir; |TIPO 9;
   |SI sw_movim = 0; |ACABA; |FINSI;

   |AVISO;
   |CONFI "2400SDesea actualizar el Diario de Caja S/N : ";
   |SI FSalida ! 0; |ACABA; |FINSI;

   |PUSHV 1019 1555;
   |BLANCO 1019 1555;
   |CUADRO 1120 1554;
   |ATRI R; |PINTA 1121, " Seleccione fechas actualizacion "; |ATRI N;
   E_sup = 0; E_inf = 0;
   fFecInicio = ~; fFecFinal = ~;
   |PINTA 1225, "Desde : ";
   |PINTA 1325, "Hasta : ";
   |PTEC 802; fFecFinal = 1334 ? 1;
|ET Inicio;
   fFecInicio = 1234 ? 1;
   |SI FSalida = 2; |VETE Inicio; |FINSI;
   |SI FSalida = 0; |O FSalida = 3;
      fFecFinal  = 1334 ? 1;
      |SI FSalida = 2; |VETE Inicio; |FINSI;
      |SI FSalida = 0; |O FSalida = 3;
         aAlfa = "cajaactu.tab;" + fFecInicio + "," + fFecFinal;
         |SUB_EJECUTA_NP aAlfa;
      |FINSI;
   |FINSI;
   |POPV;

|FPRC;

|PROCESO def_fecha; |TIPO 7;
   esalta = (FEntrada / 100) ! 0;
   |SI esalta = 1; #cajainca#1 = afecha + 1; |PINTA #cajainca#1; |FINSI;
|FPRC;

|PROCESO CalcMovtos;
   |SI #cajainli#8 = "P";
      #cajainca#2 = #cajainca#2 + #0#9;
   |SINO;
      #cajainca#3 = #cajainca#3 + #0#9;
   |FINSI;
|FPRC;

|DEFBUCLE CalcMovtos;
#cajainli#1;
;
#cajainca#1, 0000;
#cajainca#1, 9999;
;
NULL, CalcMovtos;
|FIN;

|PROCESO CalcCaja;
   || Primero recalculo la cabecera de la caja .....
   #cajainca#3 = 0; #cajainca#2 = 0;
   |BUCLE CalcMovtos;
   |GRABA 020#cajainca.a;

   nTotal = nTotal + (#cajainca#3 - #cajainca#2);
   nTotal = nTotal ! 2;
|FPRC;

|DEFBUCLE CalcCaja;
#cajainca#1;
;
"01.01.0000";
fFecha;
be;
NULL, CalcCaja;
|FIN;

|PROCESO saldo; |TIPO 0;

   esalta = (FEntrada / 100) ! 0;
   |SI #cajainca#1 < fec1;|O #cajainca#1 > fec2;
      |MENAV "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO ";
      |ERROR;
      |ACABA;
   |FINSI;

   |SI #cajainca#1 [ fec3; |Y esalta < 3;
       |MENAV "    ERROR FECHA INFERIOR O IGUAL AL BLOQUEO DE DATOS";
       |ERROR;
       |ACABA;
   |FINSI;

   nTotal = 0; fFecha = #cajainca#1;
   |ABRE #cajasald;
   |LEE 000#cajasald.p;
   |SI FSalida ! 0; |DDEFECTO #cajasald; |FINSI;
   |CIERRA #cajasald;
   nTotal = #cajasald#1;
   |SALVA_FICHA #cajainca;
   |BUCLE CalcCaja;
   |REPON_FICHA #cajainca;

   #cafantas#0 = nTotal;
   |ATRI I;
   |PINTA 0967, #cafantas#0;
   |ATRI 0;
   |SI esalta = 1; dpant = dpta; sdant = sdta; |FINSI;
|FPRC;

|PROCESO la_fecha; |TIPO 13;
   nTotal = 0; fFecha = #cajainca#1;
   |ABRE #cajasald;
   |LEE 000#cajasald.p;
   |SI FSalida ! 0; |DDEFECTO #cajasald; |FINSI;
   |CIERRA #cajasald;
   nTotal = #cajasald#1;
   |SALVA_FICHA #cajainca;
   |BUCLE CalcCaja;
   |REPON_FICHA #cajainca;

   #cafantas#0 = #cajainca#2 - #cajainca#3;
   #cafantas#0 =  nTotal;
   |ATRI I;
   |PINTA 0967, #cafantas#0;
   |ATRI 0;
   |PINTA 0739, "             ³";
   |PINTA 0753, "             ³";
   |PINTA 0767, "             ³";
   |PINTA 0638, "                                      ";
   aBlancs = " " * 78;
   |PINTA 2202, aBlancs;
   |PINTA 2302, aBlancs;

   |SI eaCP347 = "S";
       |PINTA 1075, "ÂÄÄÄÄ´";
       |PINTA 1176,  "    ³";
       |ATRI R; |PINTA 1176,  "Ej.O";|ATRI 0;
       |PINTA 1275, "ÅÄÄÄÄ´"
       |PINTA 1376,  "    ³";
       |PINTA 1476,  "    ³";
       |PINTA 1576,  "    ³";
       |PINTA 1676,  "    ³";
       |PINTA 1776,  "    ³";
       |PINTA 1876,  "    ³";
       |PINTA 1976,  "    ³";
       |PINTA 2076,  "    ³";
       |PINTA 2175, "ÁÄÄÄÄÙ"
   |FINSI;
|FPRC;

|PROCESO def_dia; |TIPO 7;
   esalta = (FEntrada / 100) ! 0;
   |SI esalta = 1; #cajainca#0 = adiari; |PINTA #cajainca#0; |FINSI;
|FPRC;

|PROCESO def_doc; |TIPO 7;
   esalta = (FEntrada / 100) ! 0;
   |SI esalta ! 1; |ACABA; |FINSI;
   |SI paso = 0;
      |HAZ contador;
      adocum = nDOCUMENTO;
      paso = 1;
   |FINSI;
   #cajainca#4 = adocum;
   |PINTA #cajainca#4;
|FPRC;

|PROCESO guarda_doc; |TIPO 0;
   |SI FSalida = 9;
      |HAZ contador;
      #cajainca#4 = nDOCUMENTO;
      |PINTA #cajainca#4;
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO guarda_def; |TIPO 30;
   sw_movim = 1;
   esalta = (FEntrada / 100) ! 0;
   |SI esalta = 1;
      afecha = #cajainca#1;
      adiari = #cajainca#0;
      adocum = #cajainca#4;
   |FINSI;
|FPRC;

|PROCESO modbaj; |TIPO 1;
   esalta = (FEntrada / 100) ! 0;


   |SI #cajainca#1 [ fec3; |Y esalta ! 4;
       |MENAV "    ERROR FECHA INFERIOR O IGUAL AL BLOQUEO DE DATOS. SOLO BAJA";
       |SI esalta = 2;
           |ERROR;
       |FINSI;
       |ACABA;
   |FINSI;
   |SI esalta= 2;
      |ABRE #0;
      #cajainli#1 = #cajainca#1;
      #cajainli#3 = 9999;
      |LEE 001#0];
      |SI FSalida = 0;
         |LEE 001#0a;
         |SI FSalida ! 0; |O #cajainli#1 ! #cajainca#1;
            |CIERRA #0;
            |ACABA;
         |FINSI;
      |SINO;
         |LEE 001#0u;
         |SI FSalida ! 0; |O #cajainli#1 ! #cajainca#1;
            |CIERRA #0;
            |ACABA;
         |FINSI;
      |FINSI;
      cuant = #cajainli#4;
      diant = #cajainli#5;
      coant = #cajainli#6;
      deant = #cajainli#7;
      dpant = #cajainli#12;
      dhant = #cajainli#8;
      imant = #cajainli#9;
      sdant = #cajainli#18;
      |CIERRA #0;
   |FINSI;
|FPRC;

|PROCESO aviso5;         || desde calculo 'c_importe' (camaasil)
   |PUSHV 09101171;
   |HAZ color_f;
   |CUADRO 09 10 11 71;
   ||            123456789012345678901234567890123456789012345678901234567890
   |HAZ color_c;

   |QBF avisos;
   alfa1 = avisos % 0;  nume1 = alfa1;
   nume2 = ((60 - nume1) / 2) ! 0;     nume1 = 60 - nume1 - nume2;
   avisos = (" " * nume1) + avisos + (" " * nume2);        || lo centra

   |PINTA 1011, avisos;
   |ATRI 0;
   |AVISO;
|FPRC;

|PROCESO aviso9;         || desde calculo 'descuadre', 'modbaj'
   |ATRI 0;
   |POPV;
|FPRC;

|PROCESO color_c;        || activa el color caracter en avisos
   |COLOR 14, 0;
   |ATRI 0;
|FPRC;

|PROCESO color_f;        || activa el color graficos en avisos
   |COLOR 4, 0;
   |ATRI 0;
|FPRC;
