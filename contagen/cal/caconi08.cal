|FICHEROS;
     caconi08 #0;
     :/modelos/def/dsmom030 #2;
     cacuenta #4;
     ca8cumay #410;
     caivaasi #200;
|FIN;

|VARIABLES;
     aParam  = "";
     aAlfa   = "";
     aCuenta = "";
     aLong   = "";
     nLong   = 0;
     aCaracter = "";

     nSw     = 0;
     nCampo1 = 0;
     nCampo2 = 0;
     nPosi   = 0;
     nCampo  = 0;
     p_alfa1 = "";

     nSwBona = 0;
     nNume   = 0;
     nNume1  = 0;
     nNume2  = 0;
     nNume3  = 0;
     nNume4  = 0;
     aNombre = "";
|FIN;

|INCLUYE dscont_i;

|PROCESO Punto;
     aAlfa   = "";
     |PARA nCampo = 1;  |SI nCampo [ 12;  |HACIENDO nCampo = nCampo + 1;
           nPosi = (nCampo * 100) + 1;
           aCaracter = #0Campo % nPosi;
           |SI aCaracter = "0"; |O aCaracter = "1"; |O aCaracter = "2";
            |O aCaracter = "3"; |O aCaracter = "4"; |O aCaracter = "5";
            |O aCaracter = "6"; |O aCaracter = "7"; |O aCaracter = "8";
            |O aCaracter = "9";
                   aAlfa = aAlfa + aCaracter;
          |FINSI;
     |SIGUE;
     aAlfa = aAlfa + ".a";
     #0Campo = aAlfa;  |PINTA #0Campo;
|FPRC;

|PROCESO Cuenta;   |TIPO 6;
      aAlfa   = #0Campo;
      p_alfa1 = #0Campo;

      aCuenta = #0Campo; |QBT aCuenta;
      aAlfa   = aCuenta % -101;
      |SI aAlfa = "*"; |O aAlfa = "u"; |O aAlfa = "a";
          |SI Campo = 1; |O Campo = 4; |O Campo = 7; |O Campo = 28;
              |O Campo = 31; |O Campo = 34; |O Campo = 63;
              |SI aAlfa = "*";
                  |MENAV "    Introduzca una Cuenta, no grupo de Cuentas";
                  |ERROR;
              |FINSI;
          |FINSI;
          |SI Campo = 34; |O Campo = 63;
              |SI aAlfa = "u"; |O aAlfa = "a";
                  |MENAV "    Introduzca una Cuenta de pase directo Concreta";
                  |ERROR;
              |FINSI;
          |FINSI;
          |ACABA;
      |FINSI;
      aAlfa   = aCuenta % -102;
      |SI aAlfa = ".0";
          |ACABA;
      |FINSI;

      eaCuenta = #0Campo;
      salidas  = FSalida;
      |HAZ FiltraPuntos;
      #0Campo = eaCuenta;
      |PINTA #0Campo;

      |SI #0Campo = "            ";  |ACABA;  |FINSI;

      aCuenta = #0Campo;   |QBT aCuenta;
      aLong   = aCuenta % A0;
      nSw     = 0;
      nCampo1 = Campo + 1;

      aAlfa   = aCuenta % -101;
      |SI aAlfa = "*";  #0nCampo1 = 0;  |ACABA;  |FINSI;
      nLong = aLong;
      |SI nLong > 4;
          |SI aLong = dig4; nSw = 1; #0nCampo1 = 1; |FINSI;
          |SI aLong = dig5; nSw = 1; #0nCampo1 = 2; |FINSI;
          |SI aLong = dig6; nSw = 1; #0nCampo1 = 3; |FINSI;
          |SI aLong = dig7; nSw = 1; #0nCampo1 = 4; |FINSI;
          |SI aLong = dig8; nSw = 1; #0nCampo1 = 5; |FINSI;
          |SI aLong = dig9; nSw = 1; #0nCampo1 = 6; |FINSI;

          |SI nSw = 0;
              |MENAV  "     Atencion. Longitud de Cuenta Fuera de Nivel !";
          |FINSI;
      |FINSI;
|FPRC;

|PROCESO LeeMayor;
  |ABRE #410;
  #410#12 = enEjerBal;
  #410#0  = aCuenta;
  #410#10 = eNum2008;
  |LEE 001#410=;
  |SI FSalida = 0;
      aNombre = #410#9;
  |FINSI;
  |CIERRA #410;
|FPRC;

|PROCESO NombreCuenta;  |TIPO 0;
     |SI #0Campo = "            ";  |ACABA;  |FINSI;

     |SI Campo = 1; |Y FSalida = 10;
         |DDEFECTO #0;
         |PINDA #0#0;
     |FINSI;

     nCampo1 = Campo + 1;
     nCampo2 = Campo + 2;

     aCuenta = #0Campo;   |QBT aCuenta;
     aAlfa   = aCuenta % -101;
     |SI aAlfa = "*";
          aCuenta = aCuenta - aAlfa;
     |FINSI;
     |SI aAlfa = "u"; |O aAlfa = "a";
          aCuenta = aCuenta - aAlfa;
          aCuenta = aCuenta - ".";
      |FINSI;
     aAlfa   = aCuenta % -102;
     |SI aAlfa = ".0";
          aCuenta = aCuenta - aAlfa;
     |FINSI;

     aLong   = aCuenta % A0;
     nLong   = aLong;
     aNombre = "";
     |ABRE #4;
     |SELECT #1#4;
     #4#0 = #0Campo;
     |LEE 000#4=;
     |SI FSalida ! 0;
         |SI nLong [ 4;
             |HAZ LeeMayor;
         |SINO;
              |MENAV "      Atencion. No existe Cuenta ";
         |FINSI;
     |SINO;
         aNombre = #4#2;
     |FINSI;

     |SI aNombre ! "";
         #0nCampo2 = aNombre;
     |FINSI;
     |PINTA #nCampo2;

     |SI #4#3 = "N";
         |SI nLong > 4;
             |MENAV "      Esta Cuenta NO tiene Pase directo";
         |FINSI;
     |FINSI;
     |CIERRA #4;
|FPRC;

|PROCESO repetir;
 |SI FSalida = 15;
     nNume1 = Campo + 1; ||Descripcion
     |SI Campo ! 37;
         nNume2 = Campo - 2;
         nNume3 = Campo - 1;
         #0Campo  = #0nNume2; |PINTA #0Campo;
         #0nNume1 = #0nNume3; |PINTA #0nNume1;
     |FINSI;

     nNume2 = Campo + 2;
     |PARA nNume3 = nNume2; |SI nNume3 [ 59; |HACIENDO nNume3 = nNume3 + 2;
           nNume4 = nNume3 + 1;
           #0nNume3 = #0Campo; |PINTA #0nNume3;
           #0nNume4 = #0nNume1;|PINTA #0nNume4;
     |SIGUE;
     |ACABA;
 |FINSI;

  nNume2 = Contenido;
  nNume1 = Campo + 1; ||Descripcion
  aAlfa = #0nNume1; |QBF aAlfa;
  |DDEFECTO #2;
  |ABRE #2;
  #2#0 = #0Campo;
  |LEE 001#2=;

  |SI #0Campo ! nNume2; |O aAlfa  = "";
      #0nNume1 = #2#1; |PINTA #0nNume1;
  |FINSI;
|FPRC;

|PROCESO ElPtec; |TIPO 7;
       |SI #200#155 ! "S";
           |PTEC 816;
       |FINSI;
|FPRC;

|PROCESO Tipo0C72; |TIPO 0;
         nNume   = #0#72;

         nSwBona = 0;
         |SI nNume < 305; |O nNume > 416;
             nSwBona = 1;
             |SI nNume = 503; |O nNume = 504; |O nNume = 505;
                 |O nNume = 517; |O nNume = 250; |O nNume = 251;
                 nSwBona = 0;
             |FINSI;
         |SINO;
             |SI nNume = 340; |O nNume = 344; |O nNume = 351; |O nNume = 353;
                 |O nNume = 364; |O nNume = 366; |O nNume = 368; |O nNume = 371;
                    |O nNume = 395; |O nNume = 397; |O nNume = 399; |O nNume = 401;
                       |O nNume = 402; |O nNume = 408;
                 nSwBona = 1;
             |FINSI;
         |FINSI;
         |SI nSwBona = 1;
             aAlfa = "    La Casilla " + #0#72 + " NO es correcta";
             |MENAV aAlfa;
             |ERROR;
         |FINSI;
|FPRC;

|PROGRAMA;
    |HAZ inicio;

    |ABRE #200;
    |DDEFECTO #200;
    |LEE 041#200p;
    |CIERRA #200;

    |CLS;
    |CABEZA "Asiento Impuesto Sociedades";
    |ABRET #0;
    |LEE 101#0p;
    |SI FSalida ! 0;
       |DDEFECTO #0;
       |GRABA 020#0b;
    |FINSI;

    |PINPA #0#0;
    |PINDA #0#0;
    |ENDATOS #1#0;
    |SI FSalida = 0;  |GRABA 020#0a;  |FINSI;
    |LIBERA #0;
    |CIERRA #0;
|FPRO;
