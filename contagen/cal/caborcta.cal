|FICHEROS;
   caborcta #0;
   cacuenta #1;
   camaasil #10;
   caclipro #21;

   canempre #30;
   caselem1 #998;
   caborctt #999,MANTE;
|FIN;

|VARIABLES;
   &entrada = 0;

   informe  = "caborcta";

   &enContar = 0;
   &eaMov    = "";
   aDDir    = "";
   aHDir    = "";
   nSwAcum  = 0;

   aAlfa1   = "";
   nNume1   = 0;
   aFichero = "";
|FIN;

|INCLUYE dscont_i;

|PROCESO Tipo8bor; |TIPO 8;
  |HAZ inicio;
  aFichero = ("tb" + Usuario) % 108;
  |NOME_DAT #999 aFichero;
  |DELINDEX #999;
|FPRC;

|CALCULO cuenta; |TIPO 0;
     eaCuenta = #Fichero@#Campo#;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #Fichero@#Campo# = eaCuenta;
     |PINTA #Fichero@#Campo#;
|FCAL;

|PROCESO Mas999;
 #999#0 = doce;
|FPRC;

|PROCESO Menos999;
 #999#0 = "zzzzzzzzzzzz";
|FPRC;

|CALCULO losdefectos; |TIPO 7;
 #0#4 = dig3; |PINTA #0#4;
 |ENTLINEAL #1#999, -1, 7, 16, 0, Mas999, Menos999;
|FCAL;

|CALCULO VerNivel;
  |SI #0Campo > dig3;
      |MENAV "    El numero de niveles no coincide con el definido en la Empresa ...";
      |ERROR;
  |FINSI;
|FCAL;

|CALCULO LaParrilla;
 |SI #0#8 = "S";
     |C_M #0#0, 1, "N"; #0#0 = "";     |PINTA #0#0;
     |C_M #0#1, 1, "N"; #0#1 = "9999"; |PINTA #0#1;
     |C_M #0#3, 1, "N"; #0#3 = 1;      |PINTA #0#3;
     |C_M #0#4, 1, "N"; #0#4 = dig3;   |PINTA #0#4;
     |C_M #0#5, 1, "N"; #0#5 = "T";    |PINTA #0#5;
     |ENTLINEAL #1#999, -1, 2, 16, 0, Mas999, Menos999;
 |SINO;
     |C_M #0#0, 1, "S";
     |C_M #0#1, 1, "S";
     |C_M #0#3, 1, "S";
     |C_M #0#4, 1, "S";
     |C_M #0#5, 1, "S";
 |FINSI;
|FCAL;

||--------------------------------------------------------------------
|CALCULO Tipo11_t; |TIPO 11;
     |SI FSalida ! 20;  |ACABA;  |FINSI;

     salidas  = FSalida;
     |HAZ FiltraPuntos;
|FCAL;

|CALCULO cuefin_t; |TIPO 0;
   emCta    = 1;
   eaCuenta = #999#0;
   |HAZ SacaNivel;
   |SI eswCta ! 0;  |ERROR; |ACABA; |FINSI;  ||Cuenta erronea
   #999#1 = enNivel;

   |HAZ LeeCuenta;
   |SI eswCta ! 0;  |ERROR; |ACABA; |FINSI;  ||Cuenta erronea
   #999#2 = #cacuenta#2; |PINTA #999#2;
   #999#3 = #cacuenta#3;
|FCAL;

||--------------------------------------------------------------------
|PROCESO bclipro;
   aAlfa1 = #1#0; aAlfa1 = aAlfa1 % 101;
   |SI aAlfa1 = "6"; |O aAlfa1 = "7";  |ACABA;  |FINSI;

   |ABRE #21;
   #21#23 = "C";
   #21#10 = #1#0;
   #21#11 = #1#1;
   |LEE 101#21=;
   |SI FSalida = 0;
       |BORRA 020#21a;
   |SINO;
       #21#23 = "P";
       #21#10 = #1#0;
       #21#11 = #1#1;
       |LEE 101#21=;
       |SI FSalida = 0;
          |BORRA 020#21a;
       |FINSI;
   |FINSI;
   |LIBERA #21;
   |CIERRA #21;
|FPRC;

|PROCESO LeoCuentas;

  eaCuenta = #1#0;
  |HAZ SacaNivel;
  |SI enNivel < #0#3; |O enNivel > #0#4;
      |ACABA;
  |FINSI;

  nSwAcum = 0;
  eaMov   = "NO";

      |PARA nNume1 = 9; |SI nNume1 [ 53; |HACIENDO nNume1 = nNume1 + 1;
            |SI #1nNume1 ! 0;
                nSwAcum = 1;
                |SAL;
            |FINSI;
      |SIGUE;

      |SI nSwAcum = 0; |Y #1#3 = "S";
          |ABRE #10;
          |SELECT #3#10;
          #10#4 = #1#0;
          #10#5 = #1#1;
          #10#0 = 0;
          #10#1 = "01.01.0000";
          #10#2 = 0;
          |LEE 000#10];
          |SI FSalida = 0; |Y #10#4 = #1#0;
              nSwAcum = 1;
          |FINSI;
          |SELECT #1#10;
          |CIERRA #10;
      |FINSI;

  |SI nSwAcum = 0;
      eaMov = "NO";
  |SINO;
      eaMov = "SI";
  |FINSI;
  |SI nSwAcum = 1; |Y #0#2 = "S"; nSwAcum = 0; |FINSI;

  |SI nSwAcum = 0;
      |HAZ bclipro;
      |BORRA 020#1a;
      enContar = enContar + 1;
      |SI #0#7 = "S";
          |PRINT;
      |FINSI;
  |FINSI;

  |LIBERA #1;
|FPRC;

|DEFBUCLE LeoCuentas;
   #1#1;
   3;
   #0#0, aDDir;
   #0#1, aHDir;
   be;
   NULL, LeoCuentas;
|FIN;

|PROCESO LeoAuxiliar;
  |ABRE #1;
  #1#0 = #999#0;
  |LEE 101#1=;
  |SI FSalida = 0;
      |HAZ LeoCuentas;
  |FINSI;
  |CIERRA #1;
|FPRC;

|DEFBUCLE LeoAuxiliar;
   #999#1;
   ;
   "            ";
   "zzzzzzzzzzzz";
   ;
   NULL, LeoAuxiliar;
|FIN;

|PROCESO HazTodo;
  enContar = 0;
  |SI #0#8 = "S";
      |BUCLE LeoAuxiliar;
  |SINO;
      |BUCLE LeoCuentas;
  |FINSI;
  |SI enContar ! 0; |Y #0#7 = "S";
      |PIEINF;
  |FINSI;
|FPRC;

|CALCULO cero; |TIPO 3;
   |SI #0#6 = "N"; |ACABA; |FINSI;

   aDDir = #0#5;
   aHDir = #0#5;
   |SI #0#5 = "T";
       aDDir = " ";
       aHDir = "z";
   |FINSI;

   |SI #0#7 = "S";
       |IMPRE 0;
       |SI FSalida ! 0; |ACABA; |FINSI;

       |INFOR informe;
       |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;
   |FINSI;

   |SI #48#27 = "S";
       |HAZ MultiEmpresa;
   |SINO;
       |DFICE dirfich0; |QBF dirfich0;
       |HAZ HazTodo;
   |FINSI;

   |SI #0#7 = "S";
       |FINIMP;
   |FINSI;

   |DELINDEX #999;
|FCAL;

|| **********************************************************************
|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;
   |PATH_DAT #1  dirfich0;
   |PATH_DAT #10 dirfich0;
   |PATH_DAT #21 dirfich0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 000001;
 999999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;
