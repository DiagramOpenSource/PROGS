|FICHEROS;
     dsz00001 #0;
     caclipro #1;
     cacuenta #2,MANTE;
    :/contagen/def/cactrlal #11;

     canempre;
|FIN;

|VARIABLES;
    &entrada  = 0;
     aTipo    = "";
     aNivel   = "";
     aCta     = "";
     aAlfa    = "";
     mesdes   = "";
     meshas   = "";
     mascara  = "";

   &raf_empresa = 0;
   &raf_periodo = 0;
   &raf_ejercic = 0;
   &swaltas     = 0;
   &enNoAltaCli = 0;

   nEstado = 0;
   nObliC  = 0;
   nObliP  = 0;
|FIN;

|INCLUYE dscont_i;

|CALCULO ParaEntrar;
   entrada = 0;
   |HAZ inicio;

   |PARA NumMes = 1;|SI NumMes [ 12;|HACIENDO NumMes = NumMes + 1;
      |SI #canempre#11 = NumMes;
          |HAZ NombreMes;
          mesdes = NombreMes;
          |SAL;
      |FINSI;
   |SIGUE;
   |PARA NumMes = 1;|SI NumMes [ 12;|HACIENDO NumMes = NumMes + 1;
      |SI #canempre#12 = NumMes;
          |HAZ NombreMes;
          meshas = NombreMes;
          |SAL;
      |FINSI;
   |SIGUE;
   raf_empresa = #canempre#2;
   raf_periodo = #canempre#3;
   raf_ejercic = #canempre#26;

   |ABRE #11;
   |DDEFECTO #11;
   |LEE 101#11p;
   |SI FSalida ! 0; swaltas = 1; |FINSI;
   |LIBERA #11;
   |CIERRA #11;
|FCAL;


|PROGRAMA;

     |HAZ ParaEntrar;

     aTipo = PARAMETRO % 101;
     aNivel = PARAMETRO % 201;
     aCta = PARAMETRO % 3;
     eaCuenta = aCta;
     enNivel  = aNivel;

     aAlfa    = aCta % 102;
     |SI aAlfa = "40"; |O aAlfa = "41"; nObliP = 1; |FINSI;
     |SI aAlfa = "43"; |O aAlfa = "44"; nObliC = 1; |FINSI;
     |SI aTipo = "C"; |O aTipo= "P";
         |ABRE #1;
         #1#23 = aTipo;
         #1#10 = aCta;
         #1#11 = aNivel;
         |LEE 041#1=;
         nEstado = FSalida;
         |CIERRA #1;
     |FINSI;


     |ABRE #2;
     #2#0 = aCta;
     |LEE 000#2=;
     |SI FSalida ! 0;
          |HAZ niveldsz;
          |PINPA #0#2;
          |PINDA #0#2;
          #2#1 = enNivel;
          |GRABA 000#2b;
          |SI FSalida = 0;

              |CONFI "0000NConfirma el alta (S/N)...:";
              |SI FSalida = 0;
                  |SI nObliC = 1; |Y aTipo = "C"; |PTEC 823; |FINSI;
                  |SI nObliP = 1; |Y aTipo = "P"; |PTEC 823; |FINSI;
                  |ENDATOS #1#2;
                  |SI FSalida = 0;
                      |GRABA 020#2a;
                  |SINO;
                      |BORRA 020#2c;
                  |FINSI;
              |SINO;
                  |BORRA 020#2c;
              |FINSI;
          |FINSI;
          #2#0 = aCta;
          |LEE 000#2=;
          |SI FSalida ! 0;
              |CIERRA #2;
              |ACABA;
          |FINSI;
     |SINO;
          |SI nEstado ! 0;
              enNoAltaCli = 0;
              |SI nObliC = 1; |Y aTipo = "C"; enNoAltaCli = 1; |FINSI;
              |SI nObliP = 1; |Y aTipo = "P"; enNoAltaCli = 1; |FINSI;
              |SI enNoAltaCli = 1;
                  |PINPA #0#2;
                  |PINDA #0#2;
                  |PTEC 823;
                  |ENDATOS #2#2;
             |FINSI;
          |FINSI;
     |FINSI;
|FPRO;

|PROCESO niveldsz;
   ||***** 1a Clave;
   ||***** nivel = nivel de la cta;
   #cacuenta#1 = enNivel;
   || la 3a clave esta formada por la cta + zzzz y el campo fantasma del nivel;
   mascara = #cacuenta#0;
   |QBF mascara;
   mascara = mascara +"zzzzzzzzzzzz";
   mascara = mascara % 112;
   #cacuenta#54 = mascara;
   #cacuenta#55 = enNivel;
   #cacuenta#56 = mesdes;
   #cacuenta#57 = meshas;
   |SI #cacuenta#1 = #canempre#13; |O #cacuenta#0 = doce;
       #cacuenta#3 = "S";
   |SINO;
       #cacuenta#3 = "N";
   |FINSI;
|FPRC;
