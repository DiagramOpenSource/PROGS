|FICHEROS;
  ca8yc001 #0;
  ca8m0031 #804;
  ca8m0032 #814,MANTE;

  &imagen@ #910;

  &ficher06@ #442;  || Def Balance Estado
  &ficher07@ #443;  || Lineas
  &ficher12@ #444;  || Columnas
  &ficher13@ #445;  || Lineas Texto
|FIN;

|VARIABLES;
   Seguir  = 0;
   inkey     = 0;

   aAlfa     = "";
   aAlfa1    = "";
   aAlfa2    = "";
   nNume1    = 0;
   nNume2    = 0;
   nNumC1    = 0;
   nNumC2    = 0;
   nNumC3    = 0;
   nValor1   = 0;
   nValor2   = 0;
   nValor3   = 0;
   nValor4   = 0;
   nColum    = 0;
   nNoExiste = 0;
   nBoton1   = 0;
   nBoton2   = 0;
   nBoton3   = 0;
   nBoton4   = 0;
   nTecla    = 0;
   nModoEntrada = 0;
   aMas      = "";
   aDef      = "";
   &enCam2   = 0;
   &eaCam3   = "";
   &enCam4   = 0;
   &enCam5   = 0;
   &enCam6   = 0;
   &eaCam8   = "";
   &eaCam9   = "";
   &eaCam10  = "";
   &eaCam11  = "";
   informe   = "ca8m0032";
   &enSwCero = 0;

   &nBalance = 0;
   &nBalNum  = 0;
   &anyo2    = 0;
   &anyo1    = 0;

   &nCamp0  = 0;     ||desde mes
   &nCamp1  = 0;     ||hasta mes
   &aCamp2  = "";    ||nombre Mes
   &aCamp3  = "";    ||Nombre mes
   &fCamp12 = @;     ||fecha
   &aCamp13 = "";    ||comparativo
   &aCamp18 = "";    ||Datos empresa
   &aCamp29 = "";    ||Titulo Balance
   &nCamp30 = 0;     ||Pagina inicial
   &nombre =  "";
   &nif    = "";

   &nIntTodo = 0;
   avisos   = "";
   alfa1    = "";
   nume1    = 0;
   nume2    = 0;
|FIN;

|INCLUYE dscont_i;

|PROCESO Imprimir;
   nCamp0  = #0#0;     ||desde mes
   nCamp1  = #0#1;     ||hasta mes
   aCamp2  = #0#2;     ||nombre Mes
   aCamp3  = #0#3;     ||Nombre mes
   fCamp12 = #0#12;    ||fecha
   aCamp13 = #0#13;    ||comparativo
   aCamp18 = #0#18;    ||Datos empresa
   aCamp29 = #0#29;    ||Titulo Balance
   nCamp30 = #0#30;    || Pagina inicial

   |HAZ eDesCargaECPN;
   |SUB_EJECUTA_NP "ca8yfc02;3";
   |PONCONTROL 23, "SI";
   |HAZ eCargaECPN;
|FPRC;

|PROCESO Alta4;
   #804#0 = enEmpresa;
   #804#1 = enEjercicio;
   #804#2 = 001;
|FPRC;

|PROCESO Baja4;
   #804#0 = enEmpresa;
   #804#1 = enEjercicio;
   #804#2 = 999;
|FPRC;

|PROCESO Cons_bal4;
   |ABRE #804;
   |CONSULTA_F_CLAVE #1#804, Alta4, Baja4;
   |CIERRA #804;
|FPRC;

|PROCESO LeeImagen;
 enCam2 = #910#2;
 eaCam3 = #910#3;
 enCam4 = #910#4;
 enCam5 = #910#5;
 enCam6 = #910#6;
 eaCam8 = #910#8;
 eaCam9 = #910#9;
 eaCam9 = ("000" + eaCam9) % -103;

 |ABRE #444;
 |DDEFECTO #444;
 #444#5 = enEjerBal;
 #444#0 = nBalNum;
 #444#1 = #910#5;
 |LEE 001#444=;
 |CIERRA #444;
 eaCam11 = #444#2;

 |ABRE #445;
 |DDEFECTO #445;
 #445#8 = enEjerBal;
 #445#0 = nBalNum;
 #445#1 = #910#4;
 |LEE 001#445=;
 |CIERRA #445;
 eaCam10 = #445#2;

 |PRINT;
|FPRC;

|DEFBUCLE LeeImagen;
 #910#1003;
 ;
 enEmpresa, enEjercicio, 0001;
 enEmpresa, enEjercicio, 9999;
 e;
 NULL, LeeImagen;
|FIN;

|PROCESO ImprimirV;

   nombre  = eaNombreEmp;
   nif     = eaNifEmp;

   |IMPRE 0;
   |SI FSalida ! 0;
       |ACABA;
   |FINSI;

   aAlfa = "";
   |SI Impresora = "CrystalReport";
      aAlfa = informe;
      informe = "cc" + (informe % 0306);

       |CONFI "0000NOrientacion horizontal";
       |SI FSalida = 0;
          informe = (informe % 0103) + "x" + (informe % 504);
       |FINSI;
   |FINSI;

   |INFOR informe;
   |SI FSalida ! 0;
       |MENAV "    No existe informe";
       |FINIMP;
       |ACABA;
   |FINSI;

   |BUCLE LeeImagen;

   |PIEINF;
   |FININF;
   |FINIMP;

   |SI aAlfa ! "";
      informe = aAlfa;
   |FINSI;
|FPRC;

|PROCESO ImprTextos;
  |C_A #442#6, 1, 40;  |PINTA 1006, #442#6;
  |C_A #444#2, 1, 30;  |PINTA 0948, #444#2;
             |ATRI P;  |PINTA 1048, "Importe en el ECPN: "; |ATRI 0;
  #0#35 = nValor1;     |PINTA 1068, #0#35;
|FPRC;

|PROCESO LeeValorCasilla;
 nValor1 = 0;             || Lee la Casilla ahora
 nValor2 = 0;
 nValor3 = 0;

 nColum = #814#5;
 nNumC1 = nColum + 5;
 nNumC2 = nColum + 18;
 nNumC3 = nColum + 30;

 |ABRE #804;
 |DDEFECTO #804;
 #804#0 = #814#0;
 #804#1 = #814#1;
 #804#2 = #814#4;
 |LEE 001#804=;
 |SI FSalida = 0;
     nValor1 = #804nNumC1;
     nValor2 = #804nNumC2;
     nValor3 = #804nNumC3;
 |FINSI;
 |CIERRA #804;
|FPRC;

|PROCESO LeeColumna;
 |ABRE #444;
 |DDEFECTO #444;
 #444#5 = enEjerBal;
 #444#0 = nBalNum;
 #444#1 = #814#5;
 |LEE 001#444=;
 |CIERRA #444;
|FPRC;

|PROCESO LeeDefinicion;
 nNoExiste = 0;
 |ABRE #442;
 |DDEFECTO #442;
 #442#34= enEjerBal;
 #442#0 = nBalNum;
 #442#1 = #814#4;
 |LEE 001#442=;
 |SI FSalida ! 0;
     nNoExiste = 1;
 |FINSI;
 |CIERRA #442;
|FPRC;

|CALCULO Tipo7; |TIPO 7;
  nModoEntrada = (FEntrada / 100) ! 0;
  |SI nModoEntrada < 2;
      #442#6 = ""; #444#2 = "";
      nValor1= 0; nValor2 = 0; nValor3 = 0;
  |SINO;
      |HAZ LeeDefinicion;
      |HAZ LeeColumna;
      |HAZ LeeValorCasilla;
  |FINSI;
  |HAZ ImprTextos;

  |BLIN 22;
  |CONTROL_SIMPLE 0, "BOTON,F2 Con.Estado Cambios P.Neto", 2215, 2238, Boton1;
  nBoton1 = FSalida;
  |CONTROL_SIMPLE 0, "BOTON,F3 Imprimir Balance", 2240, 2258, Boton2;
  nBoton2 = FSalida;
  |CONTROL_SIMPLE 0, "BOTON,F4 Imprimir Variaciones", 2260, 2278, Boton3;
  nBoton3 = FSalida;
|FCAL;

|PROCESO Boton1;
     |PTEC 824;
|FPRC;

|PROCESO Boton2;
     |PTEC 825;
|FPRC;

|PROCESO Boton3;
     |PTEC 826;
|FPRC;

|PROCESO Tipo0;  |TIPO 0;
     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;
     |PULSATECLA;
|FPRC;

|PROCESO Tipo11;  |TIPO 11;
     nTecla = FSalida;

     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;
     |PULSATECLA;

     |SI nTecla ] 10; |Y nTecla [ 13;
         |SI nTecla = 13;
             |BLIN 22;
             |PINTA 2202, " Actualizando las variaciones en el Estado de Cambios Pat. Neto";
             enSwCero = 1;
             |HAZ eElCalFinal804;
             nTecla = 10;
         |FINSI;
         |SI nTecla = 10;  |HAZ Cons_bal4; |FINSI;
         |SI nTecla = 11;  |HAZ Imprimir; |FINSI;
         |SI nTecla = 12;  |HAZ ImprimirV; |FINSI;
         |ERROR;
         |HAZ Tipo7;
    |FINSI;
|FPRC;

|PROCESO Alta11;
 #445#8 = enEjerBal;
 #445#0 = nBalNum;
 #445#1 = 1;
|FPRC;

|PROCESO Baja11;
 #445#8 = enEjerBal;
 #445#0 = nBalNum;
 #445#1 = 999;
|FPRC;

|PROCESO Alta12;
 #444#5 = enEjerBal;
 #444#0 = nBalNum;
 #444#1 = 1;
|FPRC;

|PROCESO Baja12;
 #444#5 = enEjerBal;
 #444#0 = nBalNum;
 #444#1 = 12;
|FPRC;

|CALCULO elTipo0; |TIPO 0;
  inkey = FSalida;
  |SI inkey = 10;
      |ABRE #445;
      #445#1 = #814#4;
      |CONSULTA_F_CLAVE #1#445, Alta11, Baja11;
      #814#4 = #445#1; |PINTA #814#4;
      |CIERRA #445;
  |FINSI;
  |HAZ LeeDefinicion;
  |SI nNoExiste = 1;
      |MENAV "    No existe la Definicion.";
      |ERROR;
      |ACABA;
  |FINSI;
  #814#8 = #442#2; |PINTA #814#8;
  #814#9 = #442#3; |PINTA #814#9;

  |HAZ LeeValorCasilla;

  nValor1 = nValor1 + #814#6;  ||ya lo ha restado anteriormente

  |HAZ ImprTextos;
|FCAL;

|CALCULO LaCol; |TIPO 0;
  inkey = FSalida;
  |SI inkey = 2; |ACABA; |FINSI;
  |SI inkey = 10;
      |ABRE #444;
      #444#1 = #814#5;
      |CONSULTA_F_CLAVE #1#444, Alta12, Baja12;
      #814#5 = #444#1; |PINTA #814#5;
      |CIERRA #444;
  |FINSI;
  |HAZ LeeColumna;

   nColum = #814#5;
   nNume1 = nColum + 6;
   |SI #442nNume1 = "X";
       |MENAV "    Linea - Columna del Balance No Activa ";
       |ERROR;
   |SINO;
       |HAZ LeeValorCasilla;
       nValor1 = nValor1 + #814#6;  ||ya lo ha restado anteriormente
       |HAZ ImprTextos;
   |FINSI;
|FCAL;

|PROCESO color_f;
   |COLOR 4, 0;
   |ATRI 0;
|FPRC;

|PROCESO color_c;
   |COLOR 14, 0;
   |ATRI 0;
|FPRC;

|PROCESO avisos;         || desde calculo 'c_importe' (camaasil)
   |PUSHV  12311459;
   |BLANCO 12311459;
   |HAZ color_f;
   |CUADRO 1231 1458;
||   |HAZ color_c;

   |PINTA  1332, avisos;
   |ATRI 0;
   |AVISO;
|FPRC;

|CALCULO QueIntro; |TIPO 7;
   |SI nIntTodo = 1;
       |MENSA "    <F2> Cambiar para introducir Imp.Variacion";
       #814#6 = nValor1; |PINTA #814#6;
       avisos = "      IMPORTE CASILLA     ";
   |SINO;
       |MENSA "    <F2> Cambiar para introducir Imp.Casilla";
       avisos = "     IMPORTE VARIACION    ";
   |FINSI;

   |HAZ avisos;
|FCAL;

|CALCULO QueSale; |TIPO 0;
   |POPV;

   |SI FSalida = 10;
       |SI nIntTodo = 0;
           nIntTodo = 1;
       |SINO;
           nIntTodo = 0;
           #814#6 = nValor1 - nValor2 - nValor3;
       |FINSI;
       |HAZ QueIntro;
       |ERROR;
       |ACABA;
   |FINSI;

   |SI nIntTodo = 1;
       nValor4 = #814#6 - nValor2 - nValor3;
       nValor1 = #814#6;
       #814#6  = nValor4;  |PINTA #814#6;
       #0#35   = nValor1;  |PINTA 1068, #0#35;
   |FINSI;
|FCAL;

|CALCULO Tipo3; |TIPO 3;
 |SI #814#8 = " "; |O #814#9 = 0;
     |MENAV "    Error. Falta Informar Apartado y Casilla de las Ctas Anuales";
     |ERROR;
     |ACABA;
 |FINSI;
|FCAL;

|CALCULO Tipo2; |TIPO 2;
 nModoEntrada = (FEntrada / 100) ! 0;

 nColum = #814#5;
 nNume1 = nColum + 6;
 |SI #442nNume1 = "X"; |ACABA; |FINSI;
 |ABRE #804;
 #804#0 = #814#0;
 #804#1 = #814#1;
 #804#2 = #814#4;
 |LEE 101#804=;
 |SI FSalida = 0;
     nNume1 = nColum + 30;
     #804nNume1 = #804nNume1 +. #814#6;
     |GRABA 020#804a;
 |FINSI;
 |LIBERA #804;
 |CIERRA #804;

 enSwCero = 2;
 |HAZ eElCalFinal804;
|FCAL;
