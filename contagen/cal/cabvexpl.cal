|FICHEROS;
   cabvexpl #0;
   cacuenta #1;
   caparexp #2;
   cabaexp2 #5; || fichero trabajo cuentas
   caexistc #6;
   caexistl #7;
   caconimp #8;
   canempre #30;

   caselem1 #998;
   cawdatos #900;
|FIN;

|VARIABLES;
   &pathbal  = "";
   &balance  = 1;
   &entrada  = 0;
   &aparam   = "";
   aFichero  = "";
   informe   = "cabvexpl";
   alfa1     = "";

   nume1 = 0;
   x = 0;
   y = 0;
   &r_emp = 0;
   &r_per = 0;
   &d_mes = 0;
   &h_mes = 0;
   &estru = 0;
   &r_eje = 0;
   &r_nom = "";

   varcamp = 0;
   mes = 0;
   nivel = 0; || nivel mayor;
   menor = 0; || nivel menor;
   sw_error = 0;

   traraf = 0;
   acor_c = "";
   alar_c = "";
   acor_d = 0;
   t_agru = "";
   t_epig = "";
   t_part = "";

   &eldesde = 0;
   &elhasta = 0;
   des_exi = 0;
   has_exi = 0;
   d_cuen = "";
   h_cuen = "";
   d_digi = 0;
   h_digi = 9;

   cue_perdi = "";
   dig_perdi = 0;
   act_cuent = "";
   act_nivel = 0;
   saldo  = 0;
   debe   = 0;
   haber  = 0;
   sw_exp = 0;
   descar = 0;
   activo = 0;
   pasivo = 0;
   explo = 0;
   debe1  = 0;
   debe2  = 0;
   debe3  = 0;
   debe4  = 0;
   haber1 = 0;
   haber2 = 0;
   haber3 = 0;
   haber4 = 0;
   tipo = "";
   bloque = 0;
   agrupaci = 0;
   epigrafe = 0;
   partida = 0;

   totepig = 0;
   canti = 0;
   punto_h1 = 0;
   punto_h2 = 0;
   punto_h3 = 0;
   punto_h4 = 0;
   punto_h5 = 0;
   punto_h6 = 0;
   punto_d1 = 0;
   punto_d2 = 0;
   punto_d3 = 0;
   punto_d4 = 0;
   punto_d5 = 0;
   punto_d6 = 0;

   sw = 0;
   sw_agr = 0;
   sw_epi = 0;
   sw_alguno = 0;
   sw_h = 0;
   sw_d = 0;

   &titulo = "";
   &sw_inf = 0;
   &cantidad = 0;
   &cantidad2 = 0;
   &variable = "";
   &cta = "";
   &nom = "";
   &mesi = "";
   &mesf = "";
   &fec  = @;
   contar = 0;

   Comodin  = "";
   Comodin1 = "";

   Empresa = 0;
   Periodo = 0;

   aNomCta = "";
|FIN;

|INCLUYE dscont_i;
|INCLUYE digito_i;

|| ========================================================================
||                        CALCULOS PANTALLA
|| ========================================================================
|CALCULO inicio_b; |TIPO 8;
  |HAZ inicial;
  |SI swerror = 2; |PTEC 807; |ACABA; |FINSI;
  |SI swerror = 3; |PTEC 831; |PTEC 807; |ACABA; |FINSI;
  |HAZ DirAplicSt;
|FCAL;

|CALCULO estruc7; |TIPO 7;
 #0#6 = 1;
 |SI Haybasica = 1; |Y eaDepar = "S";
     #0#6 = 0;
 |FINSI;
 |PINTA #0#6;
|FCAL


|PROCESO pintames; |TIPO 0;
   varcamp = Campo + 2;                || Coge el campo del nombre del mes
   |SI #0Campo ! 0;|Y #0Campo ! 13;
       NumMes = dig1 + (#0Campo - 1);   || Operacion para saber que mes es
       |SI NumMes > 12; NumMes = NumMes - 12; |FINSI;
   |SINO;
       NumMes = #0Campo;
   |FINSI;
   |HAZ NombreMes;
   #0varcamp = NombreMes;
   |PINTA #0varcamp;
   |SI Campo = 1;  |Y #0#0 > #0#1;
      |MENAV "    Error de Entrada ...";
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO pintames2; |TIPO 0;
   |SI #0Campo ! 0;|Y #0Campo ! 13;
       NumMes = dig1 + (#0Campo - 1);   || Operacion para saber que mes es
       |SI NumMes > 12; NumMes = NumMes - 12; |FINSI;
   |SINO;
       NumMes = #0Campo;
   |FINSI;
   |HAZ NombreMes;
   |ATRI I;
   |SI Campo = 7;  |PINTA 1424, NombreMes;  |FINSI;
   |SI Campo = 8;  |PINTA 1460, NombreMes;  |FINSI;
   |ATRI 0;
   |SI Campo = 8;  |Y #0#7 > #0#8;
      |MENAV "    Error de Entrada ...";
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO numnivel; |TIPO 0;
   |SI #0Campo > dig3;
      |MENAV "    El numero de niveles no coincide con el definido en la Empresa ...";
      |ERROR;
   |FINSI;
   nivel = 0;
   |C_M #0#19,1,"S";
   |C_M #0#20,1,"S";
   |C_M #0#21,1,"S";
   |C_M #0#22,1,"S";
   |C_M #0#23,1,"S";
|FPRC;

|PROCESO nivelet; |TIPO 7;
   |SI #0#17 < 6; #0#23 = 0; |PINTA #0#23; |C_M #0#23,1,"N"; |FINSI;
   |SI #0#17 < 5; #0#22 = 0; |PINTA #0#22; |C_M #0#22,1,"N"; |FINSI;
   |SI #0#17 < 4; #0#21 = 0; |PINTA #0#21; |C_M #0#21,1,"N"; |FINSI;
   |SI #0#17 < 3; #0#20 = 0; |PINTA #0#20; |C_M #0#20,1,"N"; |FINSI;
   |SI #0#17 < 2; #0#19 = 0; |PINTA #0#19; |C_M #0#19,1,"N"; |FINSI;
|FPRC;

|PROCESO nivelrep; |TIPO 0;
   |SI nivel = 0;
      nivel = #0#18;
   |FINSI;
   |PARA x = Campo - 1;  |SI x ] 18;  |HACIENDO x = x - 1;
         |SI #0Campo = #0x;  |Y #0Campo ! 0;
             |MENAV "     Nivel repetido ...";
             |ERROR;
             |SAL;
             |ACABA;
         |FINSI;
   |SIGUE;
   |SI nivel < #0Campo;
       nivel = #0Campo;
   |FINSI;
|FPRC;

|PROCESO estruct; |TIPO 0;
   |SI #0#6 = 0;  |ACABA;  |FINSI;
   |ABRE #6;
   |DDEFECTO #6;
   #6#0 = #0#6;
   |LEE 040#6=;
   |SI FSalida ! 0;
      sw_error = 1;
      |MENAV "    No existe la estructura de descarga.";
      |ERROR;
   |FINSI;
   |CIERRA #6;
|FPRC;

|| ... procesos iniciales
|| =============================
|PROCESO antesque;
   aNomCta = "cacuenta";
   aparam = PARAMETRO;
   |QBF aparam; |SI aparam = " N"; aparam = ""; |FINSI;
   |SI aparam = "fech";
       aNomCta = "cabalano";
   |FINSI;
   |NOME_DAT #1 aNomCta;
|FPRC;

|PROCESO conexi; |TIPO 0;
   enTExi = 1;
   d_mes = #0#7;
   h_mes = #0#8;
   estru = #0#6;
   r_emp = empre;
   r_per = ejerc;
   r_eje = #canempre#26;
   r_nom = #canempre#1;
   |SUB_EJECUTA "caexicon.run";
|FPRC;

|PROCESO elmenor;
   menor = -1;
   |PARA x = 18; |SI x [ 23; |HACIENDO x = x + 1;
      |SI #0x ! 0;
         |SI menor = -1;  menor = #0x;  |FINSI;
         |SI menor > #0x; menor = #0x;  |FINSI;
      |FINSI;                   || saca el nivel elegido mas peque¤o
   |SIGUE;
   |SI nivel = menor; menor = -1; |FINSI;
|FPRC;

|| ========================================================================
||                     CALCULOS de FICHEROS AUXILIARES
|| ========================================================================

|PROCESO grabatra;  || ... Graba temporal de Agrupaciones Exp0;

   |SI saldo = 0; |ACABA; |FINSI;

   || ... Grabar tantas cuentas como niveles elegidos
   act_cuent = #1#0;
   act_nivel = #1#1;

   |PARA x = act_nivel; |SI x ] 1; |HACIENDO x = x -1;
         sw_error = 1;
         |SI act_nivel ! x;
             sw_error = 0;
             |SI x = #0#18; |O x = #0#19; |O x = #0#20; |O x = #0#21; |O x = #0#22; |O x = #0#23;
                 sw_error = 1;
             |FINSI;
         |FINSI;
         |SI sw_error = 1;
             |SI act_nivel ! x;
                 acor_d = x; acor_c = act_cuent;
                 |HAZ acortacuen2;
                 #1#0 = acor_c; #1#1 = acor_d;
             |FINSI;
             || ... Graba Auxiliar de Cuentas.
             #5#0 = #1#5;
             #5#1 = #1#4;
             #5#2 = #1#6;
             #5#3 = #1#7;
             #5#4 = 00; || ... grabar siempre partida 0; #1#8;
             #5#5 = #1#0;
             #5#6 = #1#1;
             #5#7 = #1#2;
             |LEE 000#5=;
             |SI FSalida ! 0;
                 |DDEFECTO #5;
                 #5#0 = #1#5;
                 #5#1 = #1#4;
                 #5#2 = #1#6;
                 #5#3 = #1#7;
                 #5#4 = 00; || .... #1#8;
                 #5#5 = #1#0;
                 #5#6 = #1#1;
                 #5#7 = #1#2;
                 |HAZ leexplot;
                 #5#8 = t_agru;
                 #5#9 = t_epig;
                 #5#10 = t_part;
                 #5#11 = 0;
                 #5#12 = 0;
                 |GRABA 020#5c;
             |FINSI;
             #5#11 = #5#11 + saldo; #5#12 = 0;
             |SI act_nivel ! x; #5#12 = -1; |FINSI;
             |GRABA 020#5a;
             |LIBERA #5;
         |FINSI;
   |SIGUE;
|FPRC;

|PROCESO exis_expl;
   descar = #7des_exi - #7has_exi; || iniciales - finales
   |SI descar = 0; |ACABA; |FINSI;
   |SI descar ] 0;
      act_cuent = #7#5;     || cuenta descarga D
      act_nivel = #7#6;     || digito descarga D
   |SINO;
      act_cuent = #7#8;     || cuenta descarga H
      act_nivel = #7#9;     || digito descarga H
   |FINSI;

   eaCuenta = act_cuent;
   |HAZ SacaNivel;
   act_nivel = enNivel;

   acor_c = act_cuent; acor_d = act_nivel;
   |HAZ acortacuen;
   act_cuent = acor_c; act_nivel = acor_d;

   |SI act_cuent = #1#0;
      sw_exp = 1;
      |SI descar ] 0;
         debe = debe + descar;
      |SINO;
         haber = haber - descar;
      |FINSI;
      saldo = debe - haber;
   |FINSI;
|FPRC;

|DEFBUCLE exis2;
   #7#1;
   ;
   #0#6, 01;
   #0#6, 99;
   e;
   NULL, exis_expl;
|FIN;

|PROCESO calcular;
   saldo   = 0;
   debe    = 0;
   haber   = 0;
   sw_exp  = 0;
   alfa1   = #1#0 % 101;

   |SI alfa1 = "6"; |O alfa1 = "7"; |BUCLE exis2; |FINSI;

   |SI sw_exp = 1; |ACABA; |FINSI;

   |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
      y = x + 1;
      debe  = debe  + #1x;
      haber = haber + #1y;
      saldo = debe - haber;
   |SIGUE;
|FPRC;

|PROCESO grabar;
   |HAZ digitos2;
   |SI d_error ! 0;  |ACABA;  |FINSI;

   |SI #1#55 > nivel;                  |ACABA;  |FINSI;
   |SI #1#3 = "N";  |Y #1#55 ! nivel;  |ACABA;  |FINSI;

   alar_c = #1#0;
   |HAZ alargacuen;

   |HAZ calcular;
   |SI saldo ! 0;
      |SI #1#5 = "H"; |O #1#5 = "P";
         saldo = - saldo;
      |FINSI;
      |HAZ grabatra;
      |HAZ sumasaldo;
   |FINSI;
|FPRC;

|DEFBUCLE cabvexpl0;
   #1#1;
   5,4;
   d_cuen, "D", 1;
   h_cuen, "H", 4;
   e;
   NULL, grabar;
|FIN;

|| ************************************************************************

|PROCESO sumasaldo;    || Calculamos cada cuenta
   |SI #1#5 = "D";
      |SI #1#4 = 1; debe1 = debe1 + saldo; |FINSI;
      |SI #1#4 = 2; debe2 = debe2 + saldo; |FINSI;
      |SI #1#4 = 3; debe3 = debe3 + saldo; |FINSI;
      |SI #1#4 = 4; debe4 = debe4 + saldo; |FINSI;
   |FINSI;
   |SI #1#5 = "H";
      |SI #1#4 = 1; haber1 = haber1 + saldo; |FINSI;
      |SI #1#4 = 2; haber2 = haber2 + saldo; |FINSI;
      |SI #1#4 = 3; haber3 = haber3 + saldo; |FINSI;
      |SI #1#4 = 4; haber4 = haber4 + saldo; |FINSI;
   |FINSI;
|FPRC;

|| ***********************************************************************

|PROCESO gr_final; || Grabar totales
   punto_d1 = 0;   punto_h1 = 0;
   punto_d2 = 0;   punto_h2 = 0;
   punto_d3 = 0;   punto_h3 = 0;
   punto_d4 = 0;   punto_h4 = 0;
   punto_d5 = 0;   punto_h5 = 0;
   punto_d6 = 0;   punto_h6 = 0;

   canti = haber1 - debe1;
   |SI canti < 0;
      punto_h1 = - canti;
   |SINO;
      punto_d1 = canti;
   |FINSI;

   canti = haber2 - debe2;
   |SI canti < 0;
      punto_h2 = - canti;
   |SINO;
      punto_d2 = canti;
   |FINSI;

   canti = punto_d1 + punto_d2 - punto_h1 - punto_h2;
   |SI canti < 0;
      punto_h3 = - canti;
   |SINO;
      punto_d3 = canti;
   |FINSI;

   canti = haber3 - debe3;
   |SI canti < 0;
      punto_h4 = - canti;
   |SINO;
      punto_d4 = canti;
   |FINSI;

   canti = punto_d3 + punto_d4 - punto_h3 - punto_h4;
   |SI canti < 0;
      punto_h5 = - canti;
   |SINO;
      punto_d5 = canti;
   |FINSI;

   punto_h6 = punto_h5;
   |SI punto_h5 > 0;
      punto_h6 = punto_h6 + debe4;
   |FINSI;
   |SI punto_h6 < 0; punto_h6 = 0; |FINSI;

   punto_d6 = punto_d5;
   ||SI debe4 > 0;                       || R.PRIEGO (24.03.99) impuesto neg.
   punto_d6 = punto_d6 - debe4;
   ||FINSI;
   |SI punto_d6 < 0; punto_d6 = 0; |FINSI;
|FPRC;

|| ***********************************************************************
|| ********  I M P R E S I O N *******************************************
|| ***********************************************************************
|PROCESO LeeNombre;
 |SI #5#12 = -1;
     |ABRE #1;
     |DDEFECTO #1;
     #1#0 = #5#5;
     #1#1 = #5#6;
     |LEE 001#1=;
     |SI FSalida = 0;
         #5#7 = #1#2;
     |FINSI;
     |CIERRA #1;
 |FINSI;
|FPRC;

|PROCESO impresio;
   sw = 1;
   |SI sw_alguno = 0;
      epigrafe = #5#3;
      totepig = 0;
      t_epig = #5#9;
      sw_alguno = 1;
   |FINSI;

   |SI epigrafe ! #5#3; |HAZ canv_epi; |FINSI;

   cta    = #5#5;
   acor_d = #5#6;
   |HAZ LeeNombre;
   nom    = #5#7;
   |HAZ descripcio;
   cantidad = #5#11;

   sw_inf = 1; |SI acor_d = menor; sw_inf = 0; |FINSI;
   |SI cantidad ! 0;
      |SI tipo = "D"; cantidad = -cantidad; |FINSI;
      |PRINT;
   |FINSI;
   partida = #5#4;
   |SI #5#12 = 0; totepig = totepig + cantidad; |FINSI;
|FPRC;

|PROCESO canv_epi; || subtotales por tipo + epigrafe
   variable = "               " + t_epig;
   cantidad = totepig;
   sw_inf = 2;
   |PRINT;
   totepig = 0;
   t_epig = #5#9;
   epigrafe = #5#3;
   contar = contar + 1;
|FPRC;

|PROCESO canv_epi1; || subtotales por tipo + epigrafe
   |SI contar ] 1;
      |HAZ canv_epi;
   |FINSI;
|FPRC;

|DEFBUCLE cabvexpl2;
   #5#1;
   ;
   tipo, bloque, 00, 00, 00, "            ", 0;
   tipo, bloque, 99, 99, 99, "zzzzzzzzzzzz", 9;
   e;
   NULL, impresio;
|FIN;

|PROCESO cabvexpl1;
   titulo = #0#24;
   sw = 0;

   |PARA bloque = 1; |SI bloque [ 4; |HACIENDO bloque = bloque + 1;

   || ... primero Haber
      tipo = "H";
      sw_alguno = 0;
      sw_h = 0;
      sw_d = 0;
      contar = 0;
      |BUCLE cabvexpl2;
      |SI sw_alguno = 1; sw_h = 1; |HAZ canv_epi1; |HAZ subtblH; |FINSI;

      || ... Segundo Debe
      tipo = "D";
      sw_alguno = 0;
      contar = 0;
      |BUCLE cabvexpl2;
      |SI sw_alguno = 1; sw_d= 1; |HAZ canv_epi1; |HAZ subtblD; |FINSI;

      || ... Tercero total BLOQUE
      |SI sw_h = 1; |O sw_d = 1;
         |HAZ totalbloque;
      |FINSI;
   |SIGUE;

   |SI sw = 1; |PIEINF; |FINSI;

|FPRC;

|PROCESO subtblH;
   cantidad = 0;
   |HAZ titulillo;
   |SI bloque = 1; cantidad = haber1; |FINSI;
   |SI bloque = 2; cantidad = haber2; |FINSI;
   |SI bloque = 3; cantidad = haber3; |FINSI;
   |SI bloque = 4; cantidad = haber4; |FINSI;
   sw_inf = 3;
   |PRINT;
|FPRC;

|PROCESO subtblD;
   cantidad = 0;
   |HAZ titulillo;
   |SI bloque = 1; cantidad = - debe1; |FINSI;
   |SI bloque = 2; cantidad = - debe2; |FINSI;
   |SI bloque = 3; cantidad = - debe3; |FINSI;
   |SI bloque = 4; cantidad = - debe4; |FINSI;
   sw_inf = 3;
   |PRINT;
|FPRC;

|PROCESO totalbloque; || total del bloque 1/2/3/4
   cantidad = 0;
   |SI bloque = 1;
      cantidad = punto_d1 - punto_h1;
      nume1 = 1;
      tipo = "H"; |SI cantidad ] 0; tipo = "D"; |FINSI;
      |HAZ titulos;
      sw_inf = 4;
      |SI cantidad ! 0; |PRINT; |FINSI;
   |FINSI;
   |SI bloque = 2;
      cantidad = punto_d2 - punto_h2;
      nume1 = 2;
      tipo = "H"; |SI cantidad ] 0; tipo = "D"; |FINSI;
      |HAZ titulos;
      sw_inf = 4;
      |SI cantidad ! 0; |PRINT; |FINSI;
      cantidad = punto_d3 - punto_h3;
      nume1 = 3;
      tipo = "H";|SI cantidad ] 0; tipo = "D"; |FINSI;
      sw_inf = 2;
      |HAZ titulos;
      |SI cantidad ! 0; |PRINT; |FINSI;
   |FINSI;
   |SI bloque = 3;
      cantidad = punto_d4 - punto_h4;
      nume1 = 4;
      tipo = "H";|SI cantidad ] 0; tipo = "D"; |FINSI;
      |HAZ titulos;
      sw_inf = 4;
      |SI cantidad ! 0; |PRINT; |FINSI;
      cantidad = punto_d5 - punto_h5;
      nume1 = 5;
      tipo = "H";|SI cantidad ] 0; tipo = "D"; |FINSI;
      |HAZ titulos;
      sw_inf = 2;
      |SI cantidad ! 0; |PRINT; |FINSI;
   |FINSI;
   |SI bloque = 4;
      cantidad = punto_d6 - punto_h6;
      nume1 = 6;
      tipo = "H";|SI cantidad ] 0; tipo = "D"; |FINSI;
      |HAZ titulos;
      sw_inf = 4;
      |SI cantidad ! 0; |PRINT; |FINSI;
   |FINSI;
|FPRC;

|| ******************** Programa por TIPO 3 **************************

|PROCESO impre; |TIPO 3;

   |HAZ elmenor;

   aFichero = ("x"+ Usuario)  % 108;
   |NOME_DAT #5 aFichero;

   |IMPRE 0;
   |SI FSalida ! 0;  |ACABA;  |FINSI;
   |INFOR informe;
   |SI FSalida ! 0;
      |MENAV "     Error en informe.";
      |FINIMP;
      |ACABA;
   |FINSI;

   |SI #48#27 = "S";
       |HAZ MultiEmpresa;
   |SINO;
       |DFICE dirfich0;
       |HAZ HazTodo;
   |FINSI;

   |FININF;
   |FINIMP;
|FPRC;

|PROCESO HazTodo;

      |HAZ antesque;
      |ABRE #5;
      |CIERRA #5;
      |DELINDEX #5;

      eaIa = #canempre#21;
      enM1 = #0#0;
      enM2 = #0#1;
      |HAZ LeeDatosEmpresa;
      |SI aparam = "fech";
          |SI enM1 ! enM1f;
              NumMes = enM1f; |HAZ NombreMes; #0#2 = NombreMes;
          |FINSI;
          |SI enM2 ! enM2f;
              NumMes = enM2f; |HAZ NombreMes; #0#3 = NombreMes;
          |FINSI;
      |FINSI;
      |SI enMAper ! dig1;
          |SI #0#1 < enMAper; |Y #0#1 ! 0; |ACABA; |FINSI;
      |FINSI;

      |SI #0#25 = "S"; |HAZ conexi; |FINSI;
      mesi = #0#2;
      mesf = #0#3;
      fec  = #0#12;

      eldesde = (#0#0 * 3) + 9;
      elhasta = (#0#1 * 3) + 9;
      des_exi =  #0#7 + 11;
      has_exi =  #0#8 + 11;
      d_cuen = "            "; d_digi = 0;
      h_cuen = "999999999999"; h_digi = 9;

      debe1  = 0;
      debe2  = 0;
      debe3  = 0;
      debe4  = 0;
      haber1 = 0;
      haber2 = 0;
      haber3 = 0;
      haber4 = 0;

      ||... Modificar Cuentas a balance segun saldo
      r_emp = empre;
      r_per = ejerc;
      eldesde = (#0#0 * 3) + 11;
      elhasta = (#0#1 * 3) + 11;
      enSwOper = 0;
      |SUB_EJECUTA "carecsig.run";

     |ABRE #8;
     |DDEFECTO #8;
     #8#0 = 1;
     |LEE 000#8=;
     |CIERRA #8;
     |SI #8#2 = "S";  |Y #8#3 = "N";  |Y aparam = "";
         pathbal = dirfich0;
         balance = 1;
         |SI #8#10 = "S";
             enSwOper = 2;
             |SUB_EJECUTA "carecsig";
             enSwOper = 0;
             |SI enECuadre = 1; |ACABA; |FINSI;
             |NOME_DAT #1 eaNomFCta;
         |SINO;
             |SUB_EJECUTA "careccue";
         |FINSI;
     |FINSI;

      eldesde = (#0#0 * 3) + 9;
      elhasta = (#0#1 * 3) + 9;
      || ---------------------------------------------------------------------

      |ABRE #2;
      |ABRE #5;
      |BUCLE cabvexpl0;      || Bucle de Calculos cuentas
      |HAZ gr_final;         || Graba Totales

      |CIERRA #2;
      |CIERRA #5;

      || ==================================================================

      |HAZ cabvexpl1;      || Bucle de impresion

      |DELINDEX #5;
|FPRC;


|PROCESO leexplot;  || Lee el fichero de explotacion
   t_agru = "";
   t_epig = "";
   t_part = "";
   #2#0 = #1#4;
   #2#1 = #1#5;
   #2#2 = #1#6;
   #2#4 = #1#7;
   #2#6 = #1#8;
   |LEE 040#2=; |SI FSalida ! 0; |DDEFECTO #2; |FINSI;
   t_agru = #2#3;
   t_epig = #2#5;
   t_part = #2#7;
|FPRC;

|PROCESO acortacuen;
   |SI nivel < acor_d;
      |SI nivel = 1;  traraf = 100 + dig4;  |FINSI;
      |SI nivel = 2;  traraf = 100 + dig5;  |FINSI;
      |SI nivel = 3;  traraf = 100 + dig6;  |FINSI;
      |SI nivel = 4;  traraf = 100 + dig7;  |FINSI;
      |SI nivel = 5;  traraf = 100 + dig8;  |FINSI;
      acor_c = acor_c % traraf;
      acor_d = nivel;
      acor_c = acor_c + "              ";
      acor_c = acor_c % 112;
   |FINSI;
|FPRC;

|PROCESO acortacuen2;
   |SI acor_d = 1;  traraf = 100 + dig4;  |FINSI;
   |SI acor_d = 2;  traraf = 100 + dig5;  |FINSI;
   |SI acor_d = 3;  traraf = 100 + dig6;  |FINSI;
   |SI acor_d = 4;  traraf = 100 + dig7;  |FINSI;
   |SI acor_d = 5;  traraf = 100 + dig8;  |FINSI;
   acor_c = acor_c % traraf;
   acor_c = acor_c + "              ";
   acor_c = acor_c % 112;
|FPRC;

|PROCESO alargacuen;
   |QBF alar_c;
   alar_c = alar_c + "99999999999999";
   alar_c = alar_c % 112;
|FPRC;

|PROCESO titulillo;
   variable = "";
   |SI tipo = "D";
      |SI bloque = 1; variable = "GASTOS DE EXPLOTACION"; |FINSI;
      |SI bloque = 2; variable = "GASTOS FINANCIEROS"; |FINSI;
      |SI bloque = 3; variable = "GASTOS EXTRAORDINARIOS"; |FINSI;
      |SI bloque = 4; variable = ""; |FINSI;
   |SINO;
      |SI bloque = 1; variable = "INGRESOS DE EXPLOTACION"; |FINSI;
      |SI bloque = 2; variable = "INGRESOS FINANCIEROS"; |FINSI;
      |SI bloque = 3; variable = "INGRESOS EXTRAORDINARIOS"; |FINSI;
      |SI bloque = 4; variable = ""; |FINSI;
   |FINSI;
   variable = "          " + variable;
|FPRC;

|PROCESO titulos;
   variable = "";
   |SI tipo = "D";
      |SI nume1 =  1; variable = "  I. BENEFICIOS DE EXPLOTACION ............................."; |FINSI;
      |SI nume1 =  2; variable = " II. RESULTADOS FINANCIEROS POSITIVOS ......................"; |FINSI;
      |SI nume1 =  3; variable = "III. BENEFICIOS DE LAS ACTIVIDADES ORDINARIAS .............."; |FINSI;
      |SI nume1 =  4; variable = " IV. RESULTADOS EXTRAORDINARIOS POSITIVOS .................."; |FINSI;
      |SI nume1 =  5; variable = "  V. BENEFICIO ANTES DE IMPUESTOS .........................."; |FINSI;
      |SI nume1 =  6; variable = " VI. RESULTADO DEL EJERCICIO (BENEFICIOS) .................."; |FINSI;
   |SINO;
      |SI nume1 =  1; variable = "  I. PERDIDAS DE EXPLOTACION ..............................."; |FINSI;
      |SI nume1 =  2; variable = " II. RESULTADOS FINANCIEROS NEGATIVOS ......................"; |FINSI;
      |SI nume1 =  3; variable = "III. PERDIDAS DE LAS ACTIVIDADES ORDINARIAS ................"; |FINSI;
      |SI nume1 =  4; variable = " IV. RESULTADOS EXTRAORDINARIOS NEGATIVOS .................."; |FINSI;
      |SI nume1 =  5; variable = "  V. PERDIDAS ANTES DE IMPUESTOS ..........................."; |FINSI;
      |SI nume1 =  6; variable = " VI. RESULTADO DEL EJERCICIO (PERDIDAS) ...................."; |FINSI;
   |FINSI;
|FPRC;

|PROCESO descripcio;
   variable = "";
   |SI acor_d = 1;  nume1 = dig4; variable = ""; |FINSI;
   |SI acor_d = 2;  nume1 = dig5; variable = " "; |FINSI;   || construye el hasta segun
   |SI acor_d = 3;  nume1 = dig6; variable = "  "; |FINSI;   ||/el nivel mas bajo elegido
   |SI acor_d = 4;  nume1 = dig7; variable = "   "; |FINSI;
   |SI acor_d = 5;  nume1 = dig8; variable = "    "; |FINSI;
   |SI acor_d = 6;  nume1 = dig9; variable = "     "; |FINSI;
   nume1 = 100 + nume1;
   cta = cta % nume1;
   variable = variable + cta + " ";
   variable = variable + nom;
|FPRC;

|| *****************************
|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   nYaLoHeDicho = 0;
   |PATH_DAT #1 dirfich0;
   |PATH_DAT #2 dirfich0;
   |PATH_DAT #6 dirfich0;
   |PATH_DAT #7 dirfich0;
   |PATH_DAT #8 dirfich0;

   |HAZ HazTodo;

|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 000001;
 999999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;
