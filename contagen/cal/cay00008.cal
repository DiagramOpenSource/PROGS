|FICHEROS;
  cay00008 #0;
  cacuenta #1;
  caexistc #6;
  caexistl #7;
  caconimp #8;

  canempre #30;
  cawdatos #900;
  caselem1 #998;
  cabaexp1 #999;
|FIN;

|VARIABLES;
 aAlfa1 = "";
 aAlfa2 = "";
 aAlfa3 = "";
 nNume1 = 0;
 nNume2 = 0;
 nNume3 = 0;

 aFichero    = "";
 eldesde     = 0;
 elhasta     = 0;
 elmesini    = 0;
 elmesfin    = 0;
 sw          = 0;
 Linea       = 0;
 nEstructura = 0;
 descar      = 0;
 nSaldo      = 0;
 x           = 0;
 informe = "cay00008";
|FIN;

|INCLUYE dscont_i;

|CALCULO inicio_b; |TIPO 8;
  || ... |HAZ inicial;
  || ... |SI swerror = 2; |PTEC 807; |ACABA; |FINSI;
  || ... |SI swerror = 3; |PTEC 831; |PTEC 807; |ACABA; |FINSI;
  |HAZ inicio;
  |HAZ DirAplicSt;
|FCAL;

|PROCESO deentrada;  |TIPO 7;
 #0#0 = 1; #0#1 = 99;
 |SI Haybasica = 1; |Y eaDepar = "S";
     #0#0 = 0;
 |FINSI;
 |PINTA #0#0;
 |PINTA #0#1;

 |ABRE #8;
 |DDEFECTO #8;
 #8#0 = 1;
 |LEE 011#8=;
 |CIERRA #8;
 #0#10 = #8#7; |PINTA #0#10;
 aAlfa1 = #8#8; |C_M #0#10,1,aAlfa1;
|FPRC;

|PROCESO PintaMes; |TIPO 0;
  nNume1 = Campo + 2;
  |SI #0Campo ! 0; |Y #0Campo ! 13;
       NumMes = dig1 + (#0Campo - 1);
       |SI NumMes > 12;
           NumMes = NumMes - 12;
       |FINSI;
  |SINO;
       NumMes = #0Campo;
  |FINSI;
  |HAZ NombreMes;
  #0nNume1 = NombreMes; |PINTA #0nNume1;

  |SI Campo = 3;  |Y #0#2 > #0#3;
       |MENAV "    Error de Entrada ...";
       |ERROR;
   |FINSI;
|FPRC;

|| ======================================================================
|PROCESO CalSaldo;
   nSaldo = 0;
   |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
         nSaldo = nSaldo + #1x;
   |SIGUE;
|FPRC;

|PROCESO ExisLineas;

   |DDEFECTO #1;
   #1#0 = #7#2;
   #1#1 = #7#3;
   |LEE 000#1=;

   nSaldo = 0;
   |SI #0#7 = "S"; |HAZ CalSaldo; |FINSI;
   |SI #0#6 = "N";
       |SI #7elmesini = 0; |Y #7elmesfin = 0; |Y nSaldo = 0;
           |ACABA;
       |FINSI;
   |FINSI;

   sw    = 1;
   Linea = Linea + 1;

   #999#0 = Linea;
   #999#8 = #6#0;
   |LEE 001#999=;
   |SI FSalida ! 0;
       |DDEFECTO #999;
       #999#0 = Linea;    || linea
       #999#8 = #6#0;     || estructura
       |GRABA 020#999c;
   |FINSI;
   #999#1 = #1#0;     || cuenta
   #999#2 = #1#1;     || digito
   #999#3 = #1#2;     || descripcion

   #999#4 = #7elmesini;    || existencias  iniciales
   #999#5 = #7elmesfin;    || exist. finales
   descar = (#999#4 - #999#5) ! EURODB;
   |SI descar ] 0;
       #999#6 = #7#5;     || cuenta descarga D
       #999#7 = #7#6;     || digito descarga D
   |SINO;
       #999#6 = #7#8;     || cuenta descarga H
       #999#7 = #7#9;     || digito descarga H
   |FINSI;

   #999#9  = descar;
   #999#10 = nSaldo;
   |GRABA 020#999a;
|FPRC;

|DEFBUCLE ExisLineas;
  #7#2;
  ;
  #6#0, "            ";
  #6#0, "zzzzzzzzzzzz";
  ;
  NULL, ExisLineas;
|FIN;

|PROCESO ExisCabecera;
   Linea = 0;
   |BUCLE ExisLineas;
|FPRC;

|DEFBUCLE ExisCabecera;
 #6#1;
 ;
 #0#0;
 #0#1;
 e;
 NULL, ExisCabecera;
|FIN;

|| ************************************************************
|PROCESO LeeActv;
 enActividad = nEstructura;
 |HAZ LeeDatosActividad;
 |ABRE #6;
 |DDEFECTO #6;
 #6#0 = nEstructura;
 |LEE 001#6=;
 |CIERRA #6;
|FPRC;

|PROCESO LeeAuxiliar;
 |SI sw = 0;
     sw = 1;
     nEstructura = #999#8;
     |HAZ LeeActv;
 |FINSI;

 |SI nEstructura ! #999#8;
     |PIEINF;
     nEstructura = #999#8;
     |HAZ LeeActv;
 |FINSI;
 |PRINT;
|FPRC;

|DEFBUCLE LeeAuxiliar;
 #999#1;
 ;
 #0#0, 001;
 #0#1, 999;
 ;
 NULL, LeeAuxiliar;
|FIN;

|| ************************************************************
|PROCESO ExiMn;
 #999#0 = 001;
 #999#8 = 00;
|FPRC;

|PROCESO ExiMy;
 #999#0 = 999;
 #999#8 = 99;
|FPRC;

|| ************************************************************
|PROCESO HazTodo;
   |DELINDEX #999;

   sw = 0;
   |ABRE #1;
   |ABRE #999;
   |BUCLE ExisCabecera;
   |CIERRA #999;
   |CIERRA #1;

   |SI sw = 1;   || Existen Datos
      eaIa = #canempre#21;
      enM1 = #0#2;
      enM2 = #0#3;
      |HAZ LeeDatosEmpresa;
      |SI enMAper ! dig1;
          |SI #0#2 < enMAper; |Y #0#2 ! 0;
              |ACABA;
          |FINSI;
     |FINSI;
     sw = 0;
     |BUCLE LeeAuxiliar;
     |PIEINF;

   |FINSI;
|FPRC;

|| *************************************************************************
|CALCULO Imprime; |TIPO 3;
   aFichero = ("1e" + Usuario) % 108;
   |NOME_DAT #999 aFichero;
   |DELINDEX #999;

   enDatos = 0; |SI #0#10 = "S"; enDatos = 1; |FINSI;
   eldesde = (#0#2 * 3) + 11;
   elhasta = (#0#3 * 3) + 11;
   elmesini = #0#2 + 11;
   elmesfin = #0#3 + 11;

   |IMPRE 0;
   |SI FSalida ! 0; |ACABA; |FINSI;

   |INFOR informe;
   |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

   |SI #48#27 = "S";
       |HAZ MultiEmpresa;
   |SINO;
       |DFICE dirfich0;
       |HAZ HazTodo;
   |FINSI;
   |FININF;
   |FINIMP;
|FCAL;
|| *************************************************************************
|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   nYaLoHeDicho = 0;
   |PATH_DAT #1 dirfich0;
   |PATH_DAT #6 dirfich0;
   |PATH_DAT #7 dirfich0;
   |PATH_DAT #8 dirfich0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 001;
 999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;
