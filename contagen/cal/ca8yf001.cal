|FICHEROS;
    ca8yf001 #0;

    ca8m0033 #805,MANTE;

    cacuenta #1;         || cuentas
    cabaexp1 #4;         || temporal existencias

    caexistc #6;         || descarga cabs.
    caexistl #7;         || descarga lins.
    caconimp #8;

    canempre #30;        || Empresa

    ca8comay #401;  || Control de Balances
    ca8cumay #410;  || Control de Balances
    ca8m0000 #420;  || Descripcion Numero de Balance
    ca8m0001 #421;  || Def Balance Textos
    ca8m0002 #422;  || Def Balance Linea Cuentas

    ca8ys001 #801;  || Balance de Situacion
    &imagen@ #910;
|FIN;

|VARIABLES;
   &entrada    = 1;
   &aparam     = "";
   &pathbal    = "";
   &balance    = 1;

   aFichero    = "";
   &nBalance   = 0;
   &nBalNum    = 0;

   aNomCta     = "";
   aNomCta1    = "";
   &aNomPri    = "";

   &nPagDup    = 0;
   &nSwPagDup  = 0;
   &pagina     = 0;
   sw_pagina   = 0;
   &eaNomFEst  = "";  ||Nombre del Fichero Creado
   &eaEstruc   = "";
   &eaEstrucN  = "";
   &enCam1     = 0;
   &enCam2     = 0;

   swNoExiste  = 0;
   aAlfa1      = "";
   aAlfa2      = "";
   aAlfa3      = "";
   nNume1      = 0;

   nivel       = 0;
   nLong       = 0;
   nLongT      = 0;
   nLinea      = 0;
   aCuenta     = "";
   aTitulo     = "";
   nNivel      = 0;
   &eldesde    = 0;
   &elhasta    = 0;

   nCampo      = 0;
   nCampo0     = 0;
   nCampo1     = 0;
   nCampo2     = 0;
   aCampo3     = "";
   aCampo4     = "";
   nCampo4     = 0;
   nCampo5     = 0;
   nCampo6     = 0;
   nCampo7     = 0;
   nCampo8     = 0;
   nCampo16    = 0;
   nSi         = 0;
   nDCampo4    = 0;
   nHCampo4    = 0;
   nDCampo5    = 0;
   nDCampo6    = 0;
   nDCampo7    = 0;
   nDCampo8    = 0;
   nHCampo5    = 0;
   nHCampo6    = 0;
   nHCampo7    = 0;
   nHCampo8    = 0;

   aMas        = "";
   aDef        = "";

   nImporte    = 0;
   nImporte1   = 0;
   nImporte2   = 0;
   nImporte3   = 0;
   nImporte4   = 0;
   nImporte5   = 0;
   nImporte6   = 0;

   nImpC5      = 0;  ||inicial
   nImpC6      = 0;  || debe
   nImpC7      = 0;  || haber
   nImpC8      = 0;  || saldo actual

   aAPR        = "";
   acumula     = 0;
   e1          = 0;
   e2          = 0;
   e3          = 0;
   e4          = 0;
   aDCta       = "";
   aHCta       = "";
   nCam1       = 0;
   nCam2       = 0;
   nCam3       = 0;

   &d_mes = 0;
   &h_mes = 0;
   &estru = 0;
   &r_eje = 0;
   &r_nom = "";
   &r_emp = 0;
   &r_per = 0;
   &anu1  = 0;
   &anu   = 0;
   saldo  = 0;
   nom1   = "";

   x           = 0;
   y           = 0;
   cue_perdi   = "";
   dig_perdi   = 0;
   mes_fin     = 0;
   descar      = 0;
   traraf      = 0;
   sw_exp      = 0;

   swImp       = 0;
   dos         = 0;
   &anyo1      = 0;
   &anyo2      = 0;
   &sw_mod     = 0
   nPrimero    = 0;

   &nCamp0     = 0;     ||desde mes
   &nCamp1     = 0;     ||hasta mes
   &aCamp2     = "";    ||nombre Mes
   &aCamp3     = "";    ||Nombre mes
   &fCamp12    = @;     ||fecha
   &aCamp13    = "";    ||comparativo
   &aCamp18    = "";    ||Datos empresa
   &aCamp29    = "";    ||Titulo Balance
   &nCamp30    = 0;     ||Pagina inicial
   nSwLegalia  = 0;

   CodigoEmpresa    = 0;
   EjercicioEmpresa = 0;
   NombreEmpresa    = "";
   nSwMen           = 0;
   &enNumPag        = 0;
   nSwLInv          = 0;
   Comodin          = "";
   Comodin1         = "";
   Calc             = 0;
   Compara          = 0;
   Per1             = 0;
   Per2             = 0;
   nCalculo         = 0;
   nSwErrorBal      = 0;
   nImprimir        = 0;
   nSwDescuadre     = 0;
   nValor           = 0;
   nValor1          = 0;
   nValor2          = 0;
   nCasilla         = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoto_i;
|INCLUYE digito_i;
|INCLUYE masivo_i;
|INCLUYE dslegv_i;

|| **********************************************************
|PROCESO antesque;
   aparam = PARAMETRO;    || Por ahora no hay parametro
   |QBF aparam; |SI aparam = " N"; aparam = ""; |FINSI;
   aNomCta = "cacuenta";
   aNomPri = "ca8m0033";
   |SI aparam = "fech";
       aNomCta = "cabalano";
       aNomPri = "ca8mf033";
   |FINSI;
   |SI aparam = "Analitica";
       aNomCta =  eaUsuAna;
       aNomPri = "ca8ma033";
   |FINSI;
   |SI aparam = "otp";
       aNomCta =  eaNomFEst;
       aNomPri = "ca8mo033";
       #0#9 = 0;
       #0#10 = 0;
       #0#11 = "";
       d_desde = 0;
       d_hasta = 0;
       d_masca = "";
   |FINSI;

   |NOME_DAT #1 aNomCta;
   |NOME_DAT #805 aNomPri;
   aNomCta1 = aNomCta;
|FPRC;

|CALCULO AnaT13; |TIPO 13;
 aparam = PARAMETRO; |QBF aparam;
 |SI aparam = " N"; aparam = ""; |FINSI;
 |SI aparam = "Analitica";
     |ATRI P;
     |PINTA 0572, "ANALITICO";
     |ATRI 0;
     #30#1 = eaNomAna;
 |FINSI;
  |SI aparam = "otp";
      #0#9  = enCam1; |PINTA #0#9;
      #0#10 = enCam2; |PINTA #0#10;
      #0#11 = eaEstruc; |PINTA #0#11;
      |C_M #0#9,1,"N";
      |C_M #0#10,1,"N";
      |C_M #0#11,1,"N";
      |ATRI P;
      |PINTA 0570, "ESTRUCTURA";
      |ATRI 0;
  |FINSI;
|FCAL;

|| ========================================================================
||                        CALCULOS PANTALLA
|| ========================================================================
|PROCESO Tipo7C031; |TIPO 7;
  |HAZ antesque;

  |C_M #0#31,1,"S";
  |ABRE #805;
  |LEE 001#805p;
  |SI FSalida ! 0;
      |C_M #0#31,1,"N";
      #0#31 = "S"; |PINTA #0#31;
      #0#32 = "S"; |PINTA #0#32;
  |FINSI;
  |CIERRA #805;
  |HAZ deentrada;
|FPRC;

|PROCESO Tipo0C031; |TIPO 0;

  aAlfa1 = #0#31;
  |SI aAlfa1 = "B"; aAlfa1 = "N"; |FINSI;

  |C_M #0#0, 1, aAlfa1;
  |C_M #0#1, 1, aAlfa1;
  |C_M #0#6, 1, aAlfa1;
  |C_M #0#7, 1, aAlfa1;
  |C_M #0#8, 1, aAlfa1;
  |C_M #0#9, 1, aAlfa1;
  |C_M #0#10, 1, aAlfa1;
  |C_M #0#11, 1, aAlfa1;
  |C_M #0#13, 1, aAlfa1;
  |C_M #0#14, 1, aAlfa1;
  |C_M #0#15, 1, aAlfa1;
  |C_M #0#17, 1, aAlfa1;
  |C_M #0#26, 1, aAlfa1;
  |C_M #0#26, 1, aAlfa1;
  |C_M #0#32, 1, aAlfa1;

  |C_M #0#29, 1, "S";
  |C_M #0#12, 1, "S";
  |C_M #0#30, 1, "S";
  |C_M #0#18, 1, "S";
  |SI #0#31 = "B";
      |C_M #0#29, 1, aAlfa1;
      |C_M #0#12, 1, aAlfa1;
      |C_M #0#30, 1, aAlfa1;
      |C_M #0#18, 1, aAlfa1;

      |CONFI "    SDesea Borrar el Balance Actual ";
      |SI FSalida ! 0;
          |ERROR;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO deentrada;
  |SI #0#14 = 0;  |Y #0#15 = 0;
      #0#14 = empre;
      #0#15 = ejerc - 1;
      |SI #0#15 < 0; #0#15 = 9; |FINSI;
      |PINTA #0#14;
      |PINTA #0#15;
  |FINSI;

  #0#6 = 1;
  |SI Haybasica = 1; |Y eaDepar = "S";
      #0#6 = 0;
  |FINSI;
  |PINTA #0#6;
  |HAZ DatosEmp;
|FPRC;

|PROCESO mayor1;
  |C_M #0Campo, 0, aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

  |SI #0Campo = "N";
      |C_M #0#14,1,"N";
      |C_M #0#15,1,"N";
      #0#32 = "N"; |SI nPrimero ! #0#26; #0#32 = "S"; |FINSI;
      |C_M #0#32,1,"N"; |PINTA #0#32;
  |SINO;
      |C_M #0#14,1,"S";
      |C_M #0#15,1,"S";
      |C_M #0#32,1,"S";
      |SI nPrimero ! #0#26;
          |C_M #0#32,1,"N";
          #0#32 = "S"; |PINTA #0#32;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO esbuena;
  |SI FSalida = 9;  |ACABA;  |FINSI;
  |ABRE #30;
  #30#2 = #0#14;
  #30#3 = 0;
  |LEE 011#30];
  |SI FSalida ! 0;  |O #30#2 ! #0#14;
      |SI nSwMen = 0;
          |MENAV "     NO Existe la Empresa Contable";
          |ERROR;
      |FINSI;
  |FINSI;
  |CIERRA #30;
|FPRC;

|PROCESO consulta;
  |SI FSalida ! 9;  |ACABA;  |FINSI;
  |CIERRAT #30;
  r_emp = 0;
  |SUB_EJECUTA "caconemp.run";
  |ABRET #30;
  |ERROR;
  #0#14 = r_emp;
  #0#15 = r_per;
  |PTEC 802;
  |PTEC 802;
|FPRC;

|PROCESO esbuena1;

  |SI FSalida = 2; |ACABA; |FINSI;
  swNoExiste = 0;
  |ABRE #30;
  #30#2 = #0#14;
  #30#3 = #0#15;
  |LEE 011#30=;
  |SI FSalida ! 0;
      |SI nSwMen = 0;
          |MENAV "     NO Existe Periodo en la empresa Contable";
          |ERROR;
      |FINSI;
      swNoExiste = 1;
      |VETE LeoActual;
  |FINSI;
  |SI #30#13 = 1; nNume1 = #30#14; |FINSI;
  |SI #30#13 = 2; nNume1 = #30#15; |FINSI;
  |SI #30#13 = 3; nNume1 = #30#16; |FINSI;
  |SI #30#13 = 4; nNume1 = #30#17; |FINSI;
  |SI #30#13 = 5; nNume1 = #30#18; |FINSI;
  |SI #30#13 = 6; nNume1 = #30#19; |FINSI;
  |SI MaximoDigitos ! nNume1;
      |SI nSwMen = 0;
          |MENAV "     NO CORRESPONDEN LOS NIVELES";
          |ERROR;
      |FINSI;
      swNoExiste = 1;
      |VETE LeoActual;
  |FINSI;

  #0#16 = #30#1; |PINTA #0#16;
  anu1 = 1900 + #30#26; |SI anu1 [ 1980; anu1 = anu1 + 100; |FINSI;
  nom1 = #30#1;
  |SI anu1 < 2008;
      |SI nSwMen = 0;
          |MENAV "    Periodo Seleccionado anterior al Ejercicio 2008.   NO COMPARABLE";
          |ERROR;
      |FINSI;
      swNoExiste = 1;
  |FINSI;

|ET LeoActual;
  #30#2 = empre;
  #30#3 = ejerc;
  |LEE 011#30=;
  anu = 1900 + #30#26; |SI anu [ 1980; anu = anu + 100; |FINSI;
  |CIERRA #30;
|FPRC;

|PROCESO pintames; |TIPO 0;
  nNume1 = Campo + 2;
  |SI #0Campo ! 0; |Y #0Campo ! 13;
      NumMes = dig1 + (#0Campo - 1);
      |SI NumMes > 12;
          NumMes = NumMes - 12;
      |FINSI;
  |SINO;
      NumMes = #0Campo;
  |FINSI;
  |HAZ NombreMes;
  #0nNume1 = NombreMes; |PINTA #0nNume1;

  |SI Campo = 1; |Y  #0#0 > #0#1;
      |SI nSwMen = 0;
          |MENAV "    Error de Entrada ...";
          |ERROR;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO pintames2; |TIPO 0;
  |SI #0Campo ! 0; |Y #0Campo ! 13;
      NumMes = dig1 + (#0Campo - 1);
      |SI NumMes > 12;
          NumMes = NumMes - 12;
      |FINSI;
  |SINO;
      NumMes = #0Campo;
  |FINSI;
  |HAZ NombreMes;

  |ATRI I;
  |SI Campo = 7;  |PINTA 1519, NombreMes;  |FINSI;
  |SI Campo = 8;  |PINTA 1554, NombreMes;  |FINSI;
  |ATRI 0;
  |SI Campo = 8;  |Y #0#7 > #0#8;
      |SI nSwMen = 0;
          |MENAV "    Error de Entrada ...";
          |ERROR;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO nivel; |TIPO 7;
 aAlfa1 = #0#4;
 |C_M #0#5, 1,aAlfa1;
 |SI aAlfa1 = "N";
     #0#5  = MaximoDigitos; |PINTA #0#5;
 |FINSI;
|FPRC;

|PROCESO miraniv; |TIPO 0;
  nLongT = #0#5;
  |SI nLongT > 4;
      nivel = 0;
      |SI nLongT = dig4; nivel = 1;  |FINSI;
      |SI nLongT = dig5; nivel = 2;  |FINSI;
      |SI nLongT = dig6; nivel = 3;  |FINSI;
      |SI nLongT = dig7; nivel = 4;  |FINSI;
      |SI nLongT = dig8; nivel = 5;  |FINSI;
      |SI nLongT = dig9; nivel = 6;  |FINSI;
      |SI nivel = 0;
          |MENAV "    Error. Longitud incorrecta";
          |ERROR;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO estruct; |TIPO 0;
  |SI #0#6 = 0;  |ACABA;  |FINSI;

  |ABRE #6;
  #6#0 = #0#6;
  |LEE 040#6=;
  |SI FSalida ! 0;
      |CIERRA #6;
      |SI nSwMen = 0;
          |MENAV "    No existe la estructura de descarga.";
          |ERROR;
      |FINSI;
      |ACABA;
  |FINSI;
  |HAZ cue_per;
  |CIERRA #6;
|FPRC;

|PROCESO cue_per; || calcula la cuenta de perdidas y ganancias
  cue_perdi = #6#2;
  dig_perdi = #6#3;
  |SI #0#4 = "N";
      aAlfa1 = #6#2; |QBF aAlfa1;
      aAlfa1 = aAlfa1 % 0;
      nLong  = aAlfa1;
  |SINO;
      traraf = 100 + #0#5;
      cue_perdi = #6#2 % traraf;
      dig_perdi = nivel;
  |FINSI;
  cue_perdi = cue_perdi + "            ";
  cue_perdi = cue_perdi % 112;
|FPRC;

|PROCESO DatosEmp; |TIPO 7;
 |SI #0#31 = "B"; |ACABA; |FINSI;
 |ABRE #8;
 |DDEFECTO #8;
 #8#0 = 1;
 |LEE 011#8=;
 |CIERRA #8;
 #0#18 = #8#7; |PINTA #0#18;
 aAlfa1 = #8#8; |C_M #0#18,1,aAlfa1;
|FPRC;

|PROCESO descri; |TIPO 6;
 nSwErrorBal = 0;
 |ABRE #420;
 |DDEFECTO #420;
 #420#0 = #0#26;
 |LEE 001#420=;
 |SI FSalida ! 0;
     |SI nSwMen = 0;
         |MENAV "    Error. No existe Numero de Balance";
         |ERROR;
     |FINSI;
     nSwErrorBal = 1;
 |FINSI;
 |CIERRA #420;
 #0#27 = #420#1; |SI nSwMen = 0; |PINTA #0#27; |FINSI;

 |SI #420#2 ! 1;
     |SI nSwMen = 0;
         |MENAV "    Informe solo para Balance NORMAL";
         |ERROR;
     |FINSI;
     nSwErrorBal = 1;
 |FINSI;

 eaNomIBal = "";
 |ABRE #8;
 |DDEFECTO #8;
 |LEE 011#8=;
 |CIERRA #8;
 |SI #8#16 = "S";
     |SI #420#2 = 1; eaNomIBal = "(NORMAL)   "; |FINSI;
     |SI #420#2 = 2; eaNomIBal = "(ABREVIADO)"; |FINSI;
     |SI #420#2 = 3; eaNomIBal = "(PYME)     "; |FINSI;
 |FINSI;
|FPRC;

|| ****************************************************************
|| ****************************************************************
|PROCESO conexi;
  enTExi = 1;
  nCam1 = empre;
  nCam2 = ejerc;
  nCam3 = enEjercicio;
  d_mes = #0#7;
  h_mes = #0#8;
  estru = #0#6;
  r_emp = empre;
  r_per = ejerc;
  r_eje = #30#26;
  r_nom = #30#1;
  |SUB_EJECUTA "caexicon.run";

  |SI #0#13 = "N"; |ACABA; |FINSI;
  |SI #0#32 = "N"; |ACABA; |FINSI;

  r_emp = #0#14;
  r_per = #0#15;
  r_eje = anu1 - 2000; |SI r_eje < 0; r_eje = anu1 - 1900; |FINSI;
  r_nom = nom1;
  |SUB_EJECUTA "caexicon.run";

  empre = nCam1;
  ejerc = nCam2;
  enEjercicio = nCam3;
|FPRC;

|| *****************************************************************
|PROCESO LeoTotales;
 |SI #805#8 = "C"
     nImporte = nImporte + #805acumula;
 |FINSI;
|FPRC;

|DEFBUCLE LeoTotales;
 #805#2;
 ;
 enEmpresa,enEjercicio, nCampo2, aCampo3, nDCampo4, 0, 00, 00,0;
 enEmpresa,enEjercicio, nCampo2, aCampo3, nHCampo4, 9, 99, 99,9;
 ;
 NULL, LeoTotales;
|FIN;

|PROCESO elTotal;
  |SI #421#8 = "A"; |ACABA; |FINSI;
  nImporte = 0;
  nCampo2 = #421#2;  ||Bloque
  aCampo3 = #421#3;  ||Tipo
  nCampo4 = #421#4;  ||Agrupacion
  nCampo5 = #421#5;  ||Subagrupacion

  nDCampo4 = 00; nHCampo4 = 99;
  |BUCLE LeoTotales;

  |SI nCampo5 = 5;
      nImporte  = nImporte1  + nImporte2  + nImporte3  + nImporte4;
  |FINSI;

  |SI nCampo5 ! 4;
      |ABRE #805;
      |SELECT #2#805;
      nCampo6 = #421#6;
      nCampo7 = #421#7;
      nCampo8 = #421#10;
      |HAZ cargatra_805;
      |SELECT #1#805;
      |CIERRA #805;
  |FINSI;

  |SI nCampo5 = 1; nImporte1 = nImporte; |FINSI;
  |SI nCampo5 = 2; nImporte2 = nImporte; |FINSI;
  |SI nCampo5 = 3; nImporte3 = nImporte; |FINSI;
  |SI nCampo5 = 4; nImporte4 = nImporte; |FINSI;
  |SI nCampo5 = 5; nImporte5 = nImporte; |FINSI;
|FPRC;

|DEFBUCLE elTotal;
 #421#1;
 ;
 enEjerBal, nBalNum, nBalance, 7, " ", 00, 1, 00, 00, 0;
 enEjerBal, nBalNum, nBalance, 9, "Z", 99, 9, 99, 99, 9;
 e;
 NULL, elTotal;
|FIN;

|PROCESO ElCalculoFinal805;
    nImporte1 = 0;
    nImporte2 = 0;
    nImporte3 = 0;
    nImporte4 = 0;
    nImporte5 = 0;
    |BUCLE elTotal;
|FPRC;

|| *****************************************************************
|PROCESO descarga;
  |HAZ digitos2;
  |SI d_error ! 0;  |ACABA;  |FINSI;

  nLinea = nLinea + 1;
  #4#0 = nLinea;
  #4#8 = #0#6;
  |LEE 001#4=;
  |SI FSalida = 0;  |BORRA 020#4a;  |FINSI;

  |DDEFECTO #4;
  #4#0 = nLinea;    || linea
  #4#8 = #0#6;     || estructura
  #4#1 = #1#0;     || cuenta
  #4#2 = #1#1;     || digito
  #4#3 = #1#2;     || descripcion

  mes_fin  = #0#7 + 11;  || mes iniciales
  #4e1     = #7mes_fin;  || exist. iniciales
  mes_fin  = #0#8 + 11;  || mes finales
  #4e2     = #7mes_fin;  || exist. finales

  descar   = (#4e1 - #4e2) ! EURODB; || ... iniciales - finales

  |SI descar ] 0;
      #4e3 = #7#5;     || cuenta descarga D
      #4e4 = #7#6;     || digito descarga D
  |SINO;
      #4e3 = #7#8;     || cuenta descarga H
      #4e4 = #7#9;     || digito descarga H
  |FINSI;
  |GRABA 020#4n;
|FPRC;

|PROCESO lineades;
  #1#0 = #7#2;
  #1#1 = #7#3;
  |LEE 000#1=;
  |SI FSalida = 0; |Y #1#0 > doce;
      |HAZ descarga;
  |FINSI;
|FPRC;

|PROCESO pideexis;
  nLinea = 0;
  |ABRE #6;
  #6#0 = #0#6;
  |LEE 001#6=;
  |HAZ cue_per;
  |BUCLELIN 2lineades#6;
  |CIERRA #6;
|FPRC;
|| *****************************************************************

|PROCESO ver_exp;
 |SI #4e3 = #1#0; |Y #4e4 = #1#1;
     sw_exp    = 1;
     descar    = (#4e1-#4e2) ! EURODB;
     nImporte  = (nImporte + descar) ! EURODB;
 |FINSI;
|FPRC;

|DEFBUCLE cabasitu9;
 #4#1;
 ;
 #0#6, 001;
 #0#6, 999;
 e;
 NULL, ver_exp;
|FIN;

|| *****************************************************************
|PROCESO cargatra_805;  ||Carga Trabajo de Cuentas
  #805#0  = enEmpresa;
  #805#1  = enEjercicio;
  #805#9  = nCampo2;
  #805#10 = aCampo3;
  #805#11 = nCampo4;
  #805#12 = nCampo5;
  #805#13 = nCampo6;
  #805#14 = nCampo7;
  #805#15 = nCampo8;
  |LEE 101#805=;
  |SI FSalida = 0; |Y #805#8 ! "A";
      #805acumula = #805acumula + nImporte;
      |GRABA 020#805a;
  |FINSI;
  |LIBERA #805;
|FPRC;

|PROCESO LosTotales_805;
  nCampo2 = #422#2;
  aCampo3 = #422#3;
  nCampo4 = #422#4;
  nCampo5 = #422#5;
  nCampo6 = #422#6;
  nCampo7 = #422#7;
  nCampo8 = #422#12;
  nCampo16 = #421#11;

  |HAZ cargatra_805;

  |SI nCampo8 ! 0;    ||Subpartida (A/P = 0)
      nCampo8 = 0;
      |HAZ cargatra_805;
  |FINSI;

  |SI nCampo7 ! 0;   ||Partida
      nCampo8 = 0;
      nCampo7 = 0;
      |HAZ cargatra_805;
  |FINSI;

  saldo = (saldo + nImporte) ! 2;
|FPRC;

|PROCESO calculit;

  aCuenta = #1#0;
  nNivel  = #1#1;
  aAPR    = #1#5;

  |HAZ digitos2;
  |SI d_error ! 0;  |ACABA;  |FINSI;

  aAlfa1 = #1#0; |QBF aAlfa1;
  aAlfa1 = aAlfa1 % 0;
  nLong  = aAlfa1;
  aCuenta = #1#0;
  nNivel  = #1#1;
  aTitulo = #1#2;

  nImporte = 0;
  sw_exp   = 0;
  |BUCLE cabasitu9;
  |SI sw_exp = 0;
      nImpC5 = 0; nImpC6 = 0; nImpC7 = 0; nImpC8 = 0;
      |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
            |SI x = 11;
                nImpC5 = (nImpC5 + #1x) ! EURODB;
            |SINO;
                y = x - 2; nImpC6 = (nImpC6 + #1y) ! EURODB;
                y = x - 1; nImpC7 = (nImpC7 + #1y) ! EURODB;
            |FINSI;
            nImpC8 = (nImpC8 + #1x) ! EURODB;
      |SIGUE;
      |SI swImp = 1; nImporte = nImpC8; |FINSI;
      |SI swImp = 2; nImporte = nImpC6 - nImpC7; |FINSI;
      |SI swImp ] 3;
          |SI swImp = 3; nImpC7 = 0; |FINSI;  ||solo debe
          |SI swImp = 4; nImpC6 = 0; |FINSI;  ||solo haber
          nImporte = nImpC6 - nImpC7;
      |FINSI;
  |FINSI;

  |SI nImporte ! 0;
      |SI #422#10 = "-"; nImporte = - nImporte; |FINSI;
      |SI nImporte ! 0;
          |HAZ LosTotales_805;
      |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE cabasitu805;
 #1#1;
 3;
 aDCta, "S";
 aHCta, "S";
 e;
 NULL,calculit;
|FIN;

|PROCESO calculit_805;
 aAlfa1 = #422#9; |QBF aAlfa1;
 aDCta = (aAlfa1 + "            ") % 112;
 aHCta = (aAlfa1 + "zzzzzzzzzzzz") % 112;
 swImp = #422#13;

 |BUCLE cabasitu805;
|FPRC;

|DEFBUCLE cabasitu4_805;
 #422#1;
 ;
 enEjerBal, nBalNum, nBalance, 7, " ", 00, 0, 00, 00, 0, 001;
 enEjerBal, nBalNum, nBalance, 9, "Z", 99, 9, 99, 99, 9, 999;
 e;
 NULL,calculit_805;
|FIN;

|| *****************************************************************
|PROCESO Leo421;
 nSi = 0;
 #421#14 = enEjerBal;
 #421#0  = nCampo0;
 #421#1  = nCampo1;
 #421#2  = nCampo2
 #421#3  = aCampo3;
 #421#4  = nCampo4;
 #421#5  = nCampo5;
 #421#6  = nCampo6;
 #421#7  = nCampo7;
 #421#10 = nCampo8;
 |LEE 040#421=;
 |SI FSalida = 0; nSi = 1; |FINSI;
|FPRC;

|| **** Poner a Cero los Acumulados
|PROCESO PonaCero805;
  nCampo0 = nBalNum;
  nCampo1 = 4;
  nCampo2 = #805#9;
  aCampo3 = #805#10;
  nCampo4 = #805#11;
  nCampo5 = #805#12;
  nCampo6 = #805#13;
  nCampo7 = #805#14;
  nCampo8 = #805#15;
  |HAZ Leo421;
  |SI nSi = 1;
      #805#3  = #421#9;
      #805#8  = #421#8;
      #805#16 = #421#11;
      #805#17 = #421#12;
  |FINSI;
  #805#7 = 0;
  |SI #0#32 = "S"; #805#6 = 0; |FINSI;
  |GRABA 020#805a;
  |LIBERA #805;
|FPRC;

|DEFBUCLE PonaCero805;
 #805#1;
 ;
 enEmpresa, enEjercicio, 0001;
 enEmpresa, enEjercicio, 9999;
 be;
 NULL, PonaCero805;
|FIN;

|| **** Borrar Fichero Completo
|PROCESO DardeBaja;
 |BORRA 020#805a;
 |LIBERA #805;
|FPRC;

|DEFBUCLE DardeBaja;
 #805#1;
 ;
 enEmpresa, enEjercicio, 0001;
 enEmpresa, enEjercicio, 9999;
 be;
 NULL, DardeBaja;
|FIN;

|| ************************************************************************
|| ************************************************************************
|PROCESO PaseFlujoEfectivos;
   aFichero = ("8e" + Usuario) % 108;
   |NOME_DAT #4 aFichero;|DELINDEX #4;

   eaIa = #canempre#21;
   enM1 = #0#0;
   enM2 = #0#1;
   |HAZ LeeDatosEmpresa;
   |SI enMAper ! dig1;
       |SI #0#1 < enMAper; |Y #0#1 ! 0;
           |ACABA;
       |FINSI;
   |FINSI;
   |SI #0#17 = "S"; |HAZ conexi; |FINSI;

   nLongT = #0#5;
   |SI #0#4 = "N";
       |SI dig3 = 1; nLongT = dig4; nivel = 1; |FINSI;
       |SI dig3 = 2; nLongT = dig5; nivel = 2; |FINSI;
       |SI dig3 = 3; nLongT = dig6; nivel = 3; |FINSI;
       |SI dig3 = 4; nLongT = dig7; nivel = 4; |FINSI;
       |SI dig3 = 5; nLongT = dig8; nivel = 5; |FINSI;
       |SI dig3 = 6; nLongT = dig9; nivel = 6; |FINSI;
   |SINO;
       nivel = 0;
       |SI nLongT = dig4; nivel = 1; |FINSI;
       |SI nLongT = dig5; nivel = 2; |FINSI;
       |SI nLongT = dig6; nivel = 3; |FINSI;
       |SI nLongT = dig7; nivel = 4; |FINSI;
       |SI nLongT = dig8; nivel = 5; |FINSI;
       |SI nLongT = dig9; nivel = 6; |FINSI;
  |FINSI;

  nBalNum = #0#26;
  |SI #0#32 = "S"; |BUCLE DardeBaja; |FINSI;

  |ABRE #805;
  #805#0 = enEmpresa;
  #805#1 = enEjercicio;
  #805#2 = 0000;
  |LEE 001#805];
  |SI FSalida ! 0; |O #805#0 ! enEmpresa; |O #805#1 ! enEjercicio;
      |CIERRA #805;
      |HAZ eCrearVacio;
  |SINO;
      |CIERRA #805;
      |ABRE #421;
      |BUCLE PonaCero805;
      |CIERRA #421;
  |FINSI;

|| .... Proceso de creacion del Fichero.

  eldesde = (#0#0 * 3) + 11;
  elhasta = (#0#1 * 3) + 11;

  |PARA dos = 1; |SI dos [ 2; |HACIENDO dos = dos + 1;

       |NOME_DAT #1   aNomCta;

       acumula = 7;
       e1 = 4;
       e2 = 5;
       e3 = 6;
       e4 = 7;

       |SI dos = 2;
           |SI #0#13 = "N";  |O #0#32 = "N";
               |SAL;
           |FINSI;
       |FINSI;

       |SI dos = 2;
           acumula = 6;
           e1 =  9;
           e2 = 10;
           e3 = 11;
           e4 = 12;

           |DBASE dir_fich;
           dir_fich = dir_fich + "fich/";
           aAlfa1 = #0#14;   aAlfa1 = "00000" + aAlfa1;   aAlfa1 = aAlfa1 % -105;
           aAlfa2 = #0#15;   aAlfa2 = "0"   + aAlfa2;   aAlfa2 = aAlfa2 % -101;
           aAlfa3 = dir_fich + aAlfa1 + aAlfa2 + "/";

           |PATH_DAT #1 aAlfa3;
           |PATH_DAT #6 aAlfa3;
           |PATH_DAT #7 aAlfa3;
           |PATH_DAT #8 aAlfa3;

           |ABRE #30;
           #30#2 = #0#14;
           #30#3 = #0#15;
           |LEE 011#30=;
           |CIERRA #30;
           dig3 = #30#13; dig4 = #30#14; dig5 = #30#15;
           dig6 = #30#16; dig7 = #30#17; dig8 = #30#18; dig9 = #30#19;

           pathbal = dirfich0;
           dirfich0 = aAlfa3;
           |HAZ FecApeCie;
           dirfich0 = pathbal;
           |SI enMAper ! #30#11;
               |SI #0#1 < enMAper; |Y #0#1 ! 0; |SAL; |FINSI;
           |FINSI;
       |FINSI;

       |SI dos = 1;
           r_emp = empre;
           r_per = ejerc;
       |SINO;
           r_emp = #0#14;
           r_per = #0#15;
       |FINSI;

       |ABRE #8;
       |DDEFECTO #8;
       #8#0 = 1;
       |LEE 000#8=;
       |CIERRA #8;

|| ---------------------------------------------------------------------
       saldo = 0;
       |ABRE #1;
       |ABRE #410;
       |ABRE #4;
       |HAZ pideexis;        || calcula existencias y graba
       |CIERRA #1;
       |CIERRA #4;

       |ABRE #805;
       |SELECT #2#805;
       |BUCLE cabasitu4_805;
       |SELECT #1#805;
       |CIERRA #805;

       |HAZ ElCalculoFinal805;
       |CIERRA #410;
       |DELINDEX #4;
  |SIGUE; || ==================================================================

  |SI #0#13 = "S";
       |ABRE #30;
       #30#2 = empre;
       #30#3 = ejerc;
       |LEE 011#30=;
       |CIERRA #30;
       dig3 = #30#13; dig4 = #30#14; dig5 = #30#15;
       dig6 = #30#16; dig7 = #30#17; dig8 = #30#18; dig9 = #30#19;
  |FINSI;

|FPRC;

|| ****************************************************************
|PROCESO pintaany;
 anyo2 = anu1;
 anyo1 = enEjercicio;
 |PINTA 0802, #0#29;

 |PINTA 0617, #30#1;
 |SI #0#13 = "N";
     |ATRI P; |PINTA 875, anyo1; |ATRI 0;
     |C_V #805#6, 1, "N";
     |C_P #805#7, 1, 1068;
     |PINTA 0754, "Ä";
     |PINTA 0854, "       ";
     |PINTA 0954, "Ä";
     |PINTA 1054, " ";
     |PINTA 1154, " ";
     |PINTA 1254, " ";
     |PINTA 1354, " ";
     |PINTA 1454, " ";
     |PINTA 1554, " ";
     |PINTA 1654, " ";
     |PINTA 1754, " ";
     |PINTA 1854, " ";
     |PINTA 1954, " ";
     |PINTA 2054, " ";
     |PINTA 2154, " ";
     |PINTA 2254, "Ä";
     |C_A #805#3,1,80;
 |SINO;
     |ATRI P; |PINTA 862, anyo1;
     |PINTA 875, anyo2; |ATRI 0;
 |FINSI;
|FPRC;

|| ****************************************************************
|PROCESO LeoParaImprimir;
  |SI #805#6 ! 0; |O #805#7 ! 0;
      nImprimir = 1;
      |ERROR10;
  |FINSI;
|FPRC;

|DEFBUCLE LeoParaImprimir;
 #805#1;
 ;
 enEmpresa, enEjercicio, 0001;
 enEmpresa, enEjercicio, 9999;
 e;
 NULL, LeoParaImprimir;
|FIN;

|| ****************************************************************
|PROCESO LeoCasilla;
 nValor = 0;
 |DDEFECTO #805;
 #805#0  = enEmpresa;
 #805#1  = enEjercicio;
 #805#16 = nCasilla;
 |LEE 041#805=;
 |SI FSalida = 0;
     nValor = #805#7;
 |FINSI;
|FPRC;

|PROCESO MiroDescuadre;
 nSwDescuadre = 0;
 nValor1 = 0;
 nValor2 = 0;

 |ABRE #805;
 |SELECT #3#805;
 nCasilla = 65000;  |HAZ LeoCasilla;  nValor1 = nValor;
 nCasilla = 65200;  |HAZ LeoCasilla;  nValor2 = nValor;
 nCasilla = 65100;  |HAZ LeoCasilla;  nValor2 = (nValor2 - nValor) ! EURODB;
 |SI nValor1 ! nValor2;
     nSwDescuadre = 1;
 |FINSI;
 |SELECT #1#805;
 |CIERRA #805;
|FPRC;

|| ****************************************************************
|PROCESO Inferior0;
 #805#0 = enEmpresa;
 #805#1 = enEjercicio;
 #805#2 = 0001;
|FPRC;

|PROCESO Superior0;
 #805#0 = enEmpresa;
 #805#1 = enEjercicio;
 #805#2 = 9999;
|FPRC;

|PROCESO impre; |TIPO 3;

  |SI #0#31 = "S";
      |HAZ PaseFlujoEfectivos;
  |FINSI;
  |SI #0#31 = "B";
      nBalNum = #0#26;
      |BUCLE DardeBaja;
      |ACABA;
  |FINSI;

  |SI nSwLegalia = 2;  |ACABA; |FINSI;

  |SI nSwLegalia = 0;

      |DBASS aMas;
      aDef = aMas + "contagen/def/ca8m0033";
      |CARGA_DEF aDef, imagen@;
      |SI FSalida ! 0; |ACABA; |FINSI;
      |NOME_DAT #910 aNomPri;

      sw_mod = 1; |SI #0#13 = "S"; sw_mod = 0; |FINSI;
      nBalance = 4; ||Flujos de estado

      nSwDescuadre = 1;
      |PARA; |SI nSwDescuadre = 1; |HACIENDO;
             |PINPA #0#805;
             |HAZ pintaany;
             |ENTLINEAL #1#805, -3, 4, 21, 0, Inferior0, Superior0;
             |HAZ MiroDescuadre;
             |SI nSwDescuadre = 1;
                 |ATRI S;
                 |PINTA 1215, " Atencion!. No cuadra la variacion neta del ejercicio ";
                 |PINTA 1315, " con la diferencia entre saldo inicial y final de las ";
                 |PINTA 1415, " cuentas de tesoreria. Revise los datos introducidos. ";
                 |PINTA 1515, " CONTINUAR S/N:                                       ";
                 |ATRI 0;
                 |CUADRO 1114 1568;
                 |ATRI P;
                 aAlfa1 = "N";
                 E_sup  = 1;
                 E_inf  = "SNsn";
                 aAlfa1 = 1531 ? 0; aAlfa1 = aAlfa1 > ""; |PINTA 1531, aAlfa1;
                 |SI aAlfa1 = "S";
                     nSwDescuadre = 0;
                 |FINSI;
             |FINSI;
      |SIGUE;
      |DESCARGA_DEF #910;
  |SINO;

       anyo2 = anu1;
       anyo1 = enEjercicio;
       nCamp0  = #0#0;     ||desde mes
       nCamp1  = #0#1;     ||hasta mes
       aCamp2  = #0#2;     ||nombre Mes
       aCamp3  = #0#3;     ||Nombre mes
       fCamp12 = #0#12;    ||fecha
       aCamp13 = #0#13;    ||comparativo
       aCamp18 = #0#18;    ||Datos empresa
       aCamp29 = #0#29;    ||Titulo Balance
       nCamp30 = #0#30;    || Pagina inicial
       nImprimir = 0;
       |BUCLE LeoParaImprimir;
       |SI nImprimir = 1;
           |SI nSwLInv = 1;
              |SUB_EJECUTA_NP "ca8yfc02;4Libro";
           |SINO;
              |SUB_EJECUTA_NP "ca8yfc02;4Legalia";
           |FINSI;
       |FINSI;
  |FINSI;
|FPRC;

|PROGRAMA;
   nSwLegalia = 0;
   nSwMen = 1;
   aparam = PARAMETRO; |QBF aparam;
   |SI aparam = ""; |O aparam = "Analitica"; |O aparam = "fech"; |O aparam = "otp"; |O aparam = " N";
       |HAZ inicial;
       |SI swerror = 2; |PTEC 807; |ACABA; |FINSI;
       |SI swerror = 3; |PTEC 831; |PTEC 807; |ACABA; |FINSI;
       nSwMen = 0;
   |FINSI;
   |SI enEjercicio < 2008;
       |MENAV "    Informe para la Estructuras del 2008, no factible en este Ejercicio";
       |ACABA;
   |FINSI;
   |HAZ DirAplicSt;

   anu = enEjercicio;
   nBalance = 4; || Estado de Flujo de Efectivo
                 || eNum2008 = Codigo de las Cuentas de Mayor

   |SI aparam = "PDF";
       nSwLegalia = 2;
       aNomCta = "cacuenta";
       aNomPri = "ca8m0033";

       nSwMen = 1;
       |DDEFECTO #0;
       #0#0  = nCTodo0;
       #0#1  = nCTodo1;
       #0#2  = aCTodo2;
       #0#3  = aCTodo3;
       #0#4  = aCTodo4;
       #0#5  = nCTodo5;
       #0#6  = nCTodo6;
       #0#7  = nCTodo7;
       #0#8  = nCTodo8;
       #0#12 = fCTodo12;
       #0#13 = aCTodo13;
       #0#14 = nCTodo14;
       #0#15 = nCTodo15;
       #0#17 = "N";
       #0#18 = aCTodo18;
       #0#26 = nCTodo26;
       #0#31 = aCTodo35;
       #0#32 = aCTodo37;
       |HAZ descri;
       |SI nSwErrorBal = 1; |ACABA; |FINSI;

       |PATH_DAT #0   dirfich0;
       |PATH_DAT #801 dirfich0;
       |PATH_DAT #805 dirfich0;
       |PATH_DAT #1   dirfich0;
       |PATH_DAT #6   dirfich0;
       |PATH_DAT #7   dirfich0;
       |PATH_DAT #8   dirfich0;

       |ABRE #805;
       |LEE 001#805p;
       |SI FSalida ! 0;
           #0#31 = "S";
           #0#32 = "S";
       |FINSI;
       |CIERRA #805;

       |HAZ impre;
       |ACABA;
   |FINSI;

   |SI aparam ! ""; |Y aparam ! "Analitica"; |Y aparam ! "fech"; |Y aparam = "otp"; |O aparam ! " N";
       |HAZ Legalia;
   |SINO;
       |SI enSwMvo = 0;
           |CLS;
           |CABEZA "Estado de Flujo de Efectivo";
        |FINSI;

        nPrimero = -1;
        |ABRE #0;
        #0#28 = "A";
        |LEE 041#0=;
        |SI FSalida ! 0;
            |DDEFECTO #0;
            #0#6 = 1;
            |SI Haybasica = 1; |Y eaDepar = "S";  #0#6 = 0; |FINSI;
            |SI eNum2008 =  1; #0#26 =  1; |FINSI;
            |SI eNum2008 = 20; #0#26 = 20; |FINSI;
            |SI eNum2008 = 21; #0#26 = 21; |FINSI;
            |ABRE #801;
            #801#28 = "A";   || Lee la pantalla de balance de situacion
            |LEE 041#801=;
            |SI FSalida = 0; #0#26 = #801#26; |FINSI;
            |CIERRA #801;
            |HAZ DatosEmp;
            |GRABA 001#0c;
        |SINO;
            nPrimero = #0#26;
        |FINSI;
        #0#12 = ~;
        |SI enSwMvo = 0;
            |PINPA #0#0;
            |PINDA #0#0;

            |ENDATOS #1#0;
            |SI FSalida = 0;
                #0#31 = "N";
                #0#32 = "N";
                |GRABA 001#0a;
            |FINSI;
        |SINO;
            |HAZ impre;
        |FINSI;
        |CIERRA #0;
   |FINSI;
|FPRO;

|| ************************************************************************

|PROCESO Legalia;
    nSwLegalia = 1;
    aNomCta = "cacuenta";
    aNomPri = "ca8m0033";

    nSwMen = 1;

    Comodin = PARAMETRO;
    Comodin = Comodin - ",C";
    |SI FSalida ! 0;
        Compara = 1;
    |SINO;
        Compara = 0;
    |FINSI;
    Comodin = Comodin - ",L";
    |SI FSalida ! 0;
        nSwLInv = 1;
    |SINO;
        nSwLInv = 0;
    |FINSI;

    Comodin = Comodin - ",";
    |SI FSalida ! 0;
        Calc = ((FSalida / 100) ! 0) + 99;
        Comodin1 = Comodin % 0101;
        Per1 = Comodin1; Comodin = Comodin - Comodin1;
        Per2 = Comodin;
    |FINSI;

    cod1 = eaLegEmp;
    cod2 = eaLegPer;
    emEmp = 1;  || Sin mensaje
    |HAZ leelo;
    |SI swerror ! 0;  |ACABA;   |FINSI;
    empre = eaLegEmp;
    ejerc = eaLegPer;

    |DBASE fich_alta; |QBF fich_alta;
    dirfich0 = fich_alta + "fich/" + eaLegEmp + eaLegPer + "/";

    |PATH_DAT #0   dirfich0;
    |PATH_DAT #801 dirfich0;
    |PATH_DAT #805 dirfich0;
    |PATH_DAT #1   dirfich0;
    |PATH_DAT #6   dirfich0;
    |PATH_DAT #7   dirfich0;
    |PATH_DAT #8   dirfich0;

    |ABRE #8;
    |DDEFECTO #8;
    #8#0 = 1;
    |LEE 000#8=;
    |CIERRA #8;

    |ABRE #0;
    |DDEFECTO #0;
    #0#28 = "A";
    |LEE 041#0=;
    |SI FSalida ! 0;
        #0#6 = 1;
        |SI Haybasica = 1; |Y eaDepar = "S";  #0#6 = 0; |FINSI;
        |SI eNum2008 =  1; #0#26 =  1; |FINSI;
        |SI eNum2008 = 20; #0#26 = 20; |FINSI;
        |SI eNum2008 = 21; #0#26 = 21; |FINSI;
        |ABRE #801;
        #801#28 = "A";   || Lee la pantalla de balance de situacion
        |LEE 041#801=;
        |SI FSalida = 0; #0#26 = #801#26; |FINSI;
        |CIERRA #801;
        |HAZ DatosEmp;
        |GRABA 001#0n;
         #0#31 = "S";
         #0#32 = "S";
    |FINSI;
    |CIERRA #0;

    |HAZ descri;
    |SI  nSwErrorBal = 1; |ACABA; |FINSI;

    |ABRE #805;
    |LEE 001#805p;
    |SI FSalida ! 0;
        #0#31 = "S";
        #0#32 = "S";
    |FINSI;
    |CIERRA #805;

    |SI Compara = 1;
         nCalculo = 0;
         |SI #0#31 = "S"; |Y #0#32 = "S";
              nCalculo = 1;
         |SINO;
             |SI #0#13 = "N"; |Y #0#14 = eaLegEmp; |Y #0#15 = eaLegPer;
                 nCalculo = 1;
                 #0#31 = "S";
                 #0#32 = "S";
             |FINSI;
         |FINSI;
         #0#13 = "S";
         |SI nCalculo = 1;
             #0#14 = eaLegEmp;
             |SI eaLegPer > 0;
                 #0#15 = eaLegPer - 1;
             |SINO;
                 #0#15 = 9;
             |FINSI;
         |FINSI;
         |HAZ esbuena1;
         |SI swNoExiste = 1;
             Compara = 0;
             #0#13 = "N";
         |FINSI;
     |SINO;
         #0#13 = "N"; #0#32 = "N";
     |FINSI;

     |SI #0#31 = "N";
         |SI #0#0 ! Per1; |O #0#1 ! Per2; #0#31 = "S"; #0#32 = "S"; |FINSI;
           || ... El ultimo calculado no era correcto
     |FINSI;

     #0#0  = Per1; #0#7 = Per1;
     #0#1  = Per2; #0#8 = Per2;
     Campo = 0; |HAZ pintames;
     Campo = 1; |HAZ pintames;
     Campo = 7; |HAZ pintames;
     Campo = 8; |HAZ pintames;

     #0#6   = enDcarLeg;
     #0#17  = "N";
     #0#12  = efFecLeg;
     #0#18  = eaDatLeg;
     aMonedaA = eaMoneda;
     |SI #0#13 = "N"; aMonedaC = aMonedaA; |FINSI;
     |HAZ impre;
|FPRC;
