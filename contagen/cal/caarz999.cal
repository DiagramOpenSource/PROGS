|FICHEROS;
    camaasic #02;
    camaasil #03;
    cacuenta #05;

    camandia #10;
    cacuebal #11;
    caconasi #20;

    canempre #30;

    caivaasi;
    caivaaxx;

    camaniva #204;
    caivalin #205;
    calibros #206;
    caivases #207;
    caclipro #208;
    camanref #1002;

    caivatm4;
|FIN;

|VARIABLES;
     &entrada    = 1;

     aAlfa       = "";
     aAlfa1      = "";
     aAlfa2      = "";
     aAlfa3      = "";
     aAlfa5      = "";
     aAlfa7      = "";
     nNume1      = 0;
     nNume2      = 0;
     nNume3      = 0;
     nPerio      = 0;
     nEjerC      = 0;

     nAny        = 0;

     &NumDoc     = 0;
     &aParam     = "";
     &fRegistro  = @;
     &nLibro     = 0;
     &swIVA      = 0;
     &eaModif    = "";
     &nDebe      = 0;
     &nHaber     = 0;
     &nConta     = 0;
     &fFecConta   = @;
     &nDiario     = 0;
     &nDocumento  = 0;
     &elDocumen   = 0;
     &nImporte    = 0;
     &nPeriodo    = 0;
     &nEmpresa    = 0;
     &nApunte     = 0;
     &nConcepto   = 0;
     &aDescrip    = "";
     &aDescrIVA   = "";
     &aCampo19    = "";
     &nCampo20    = 0;
     &nFactura    = 0;
     &aSerie      = "";
     &aSigno      = "";
     &eaNomSocio  = "";
     &eaNifSocio  = "";
     &nBase       = 0;
     &nTipIRPF    = 0;
     &nTipIVA     = 0;
     &aCtaAna     = "";
     &eaReferen   = "";
     &eaAnyadir   = "";
     aDCon1 = "";
     aDCon2 = "";
     nDCon0 = 0;
     nDCon1 = 0;
     &nAlguno = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoiv_i;
|INCLUYE modulo_i;

|| ************************************************************************
|PROCESO GrabaDatosSii;
   #caivatm4#0 = enEmpresa;
   #caivatm4#1 = enPeriodo;
   #caivatm4#2 = #camaniva#0;
   #caivatm4#3 = #camaniva#1;
   |LEE 041#caivatm4.=;
   |SI FSalida ! 0;
       #caivatm4#0 = enEmpresa;
       #caivatm4#1 = enPeriodo;
       #caivatm4#2 = #camaniva#0;
       #caivatm4#3 = #camaniva#1;
       |GRABA 020#caivatm4.n;
       nAlguno     = 1;
   |FINSI;
|FPRC;

|PROCESO ActIvas;
  |DDEFECTO #camaniva;
  #camaniva#0  = nLibro;          || Libro
  #camaniva#1  = NumDoc;          || N§ de registro
  |LEE 101#camaniva.=;

  |SI #3#19 = "F";
      #camaniva#10 = #3#21;     || Departamento (Actividad)
      #camaniva#11 = #3#28;     || SubDepartamento (Ref.Catastral/Cultivo)
      #camaniva#24 = eaNombreAct;
      #camaniva#25 = eaNomSubdep;
      #camaniva#12 = #3#4;      || Cuenta Cli/Pro
      #camaniva#13 = eaNomSocio;
      #camaniva#14 = eaNifSocio;
      #camaniva#22 = eaReferen;    || N§ Factura Proveedor
      #camaniva#27 = #3#6;         || Concepto Cli/Pro
      #camaniva#28 = aDescrIVA;    || Descripcion Cli/Pro
      #camaniva#29 = #3#6;         || Concepto Cli/Pro
      #camaniva#30 = aDescrIVA;    || Descripcion Cli/Pro
      #camaniva#38 = 1;            || Numero de Facturas

      |GRABA 020#camaniva.a;
      |LIBERA #camaniva;
      |ACABA;
  |FINSI;

  |SI #3#20 = 0; |ACABA; |FINSI;

  #caivalin#0 = #camaniva#0;
  #caivalin#1 = #camaniva#1;
  #caivalin#2 = #3#20;
  |LEE 010#caivalin.=;
  |SI FSalida ! 0;
      |DDEFECTO #caivalin;
      #caivalin#0 = #camaniva#0;  || Libro
      #caivalin#1 = #camaniva#1;  || Documento
      #caivalin#2 = #3#20; || Linea
      |GRABA 020#caivalin.b;
  |FINSI;

  #caivalin#5  = "S";

  |SI #3#19 = "B";
      #caivalin#3  = #3#6;                || Concepto
      #caivalin#4  = aDescrIVA;           || Descripcion
      #caivalin#6  = #3#9;                || Base imponible
      #caivalin#12 = #3#4;
      #caivalin#28 = #3#29;
      #caivalin#30 = #3#21;
      #caivalin#31 = #3#28;
  |FINSI;

  |SI #3#19 = "I";
      #caivalin#7  = nTipIVA;
      #caivalin#8  = #3#9;
      #caivalin#13 = #3#4;
      #camaniva#29 = #3#6;      || Concepto Cli/Pro
      #camaniva#30 = aDescrIVA; || Descripcion Cli/Pro
  |FINSI;

  |SI #3#19 = "P";
      #caivalin#18 = #3#6;                  || Concepto IRPF
      #caivalin#19 = aDescrIVA;             || Descripcion concepto IRPF
      #caivalin#20 = nBase;                 || Base imponible
      #caivalin#21 = nTipIRPF;              || Tipo IRPF
      #caivalin#22 = #3#9;                  || Cuota IRPF
      #caivalin#23 = #3#4;                  || Cuenta
  |FINSI;
   #caivalin#30 = #camaniva#10;
   #caivalin#31 = #camaniva#11;

   efFechaIVA = #camaniva#4;  enLineaIVA = 0;
   enPorcIVA  = #caivalin#7;  enPorcREC = #caivalin#9;
   eaCtaIVA   = #caivalin#13; eaCtaREC  = #caivalin#14;
   |HAZ BuscaTipoIVA;
   #caivalin#16 = enLineaIVA;
   #camaniva#31 = eaDesgIVA;
   #camaniva#32 = eaDesgCTP;
   #camaniva#33 = "N";
   #camaniva#34 = eaDesgDTO;

   |SI #3#19 = "B";
       #camaniva#15 = #camaniva#15 + #caivalin#6;
       #camaniva#19 = #camaniva#19 + #caivalin#6;
   |FINSI;
   |SI #3#19 = "P";
       #camaniva#20 = #camaniva#20 + #caivalin#22;
   |FINSI;
   |SI #3#19 = "I";
       #camaniva#23 = #camaniva#23 + #caivalin#8;
   |FINSI;
   |SI #camaniva#36 = "N";
       #camaniva#21 = #camaniva#19 + #camaniva#23 - #camaniva#26 - #camaniva#20;
   |SINO;
       #camaniva#21 = #camaniva#19 + #camaniva#23 - #camaniva#20;
   |FINSI;

   |GRABA 020#caivalin.a;
   |GRABA 020#camaniva.a;
   |LIBERA #camaniva;
   |LIBERA #caivalin;
|FPRC;

|PROCESO CabIvas;
  |ABRE #206;
  #206#0 = nLibro;
  |LEE 001#206=;
  |SI FSalida ! 0;
      |DDEFECTO #206;
      #206#0 = nLibro;
      #206#2 = aTipoIVA;
      |GRABA 020#206n;
  |FINSI;
  |CIERRA #206;

  aAlfa1 = #3#1;
  aAlfa1 = aAlfa1 % 402;
  nPerio = aAlfa1; nNume2 = nPerio;
  |SI enEjercicio [ 2009;
      aAlfa = #caivaasi#46; |SI aTipoIVA = "N"; aAlfa = #caivaasi#149; |FINSI;
      |SI aAlfa = "A"; nPerio = 0; |FINSI;
      |SI aAlfa = "T";
          nPerio = 1;
          |SI nNume2 ] 4; nPerio = 2; |FINSI;
          |SI nNume2 ] 7; nPerio = 3; |FINSI;
          |SI nNume2 ] 10;nPerio = 4; |FINSI;
     |FINSI;
  |FINSI;

  |SI elDocumen = 0;
      NumDoc = 1;
      #camaniva#0 = nLibro;
      #camaniva#1 = 9999999999;
      |LEE 010#camaniva.];
      |SI FSalida = 0;
          |LEE 040#camaniva.a;
      |SINO;
          |LEE 040#camaniva.u;
      |FINSI;
      |SI FSalida = 0; |Y #camaniva#0 = nLibro;
          NumDoc = #camaniva#1 + 1;
      |FINSI;
  |FINSI;

  |DDEFECTO #camaniva;
  #camaniva#0  = nLibro;
  #camaniva#1  = NumDoc;    || N§ de registro
  #camaniva#2  = #3#15;     || Serie
  #camaniva#3  = #3#14;     || Factura
  #camaniva#4  = fRegistro; || Fecha factura
  #camaniva#5  = nPerio;    || Periodo
  #camaniva#7  = #3#0;      || Diario
  #camaniva#8  = #3#1;      || Fecha contable
  #camaniva#9  = #3#2;      || Documento
  #camaniva#36 = aTipoIVA;  || Tipo de IVA/IRPF

  |GRABA 020#camaniva.n;
  swIVA = 1;
  |SI enEjercicio ] 2017;
      |HAZ GrabaDatosSii;
  |FINSI;
  |SI #caivaasi#189 = "S"; |Y aTipoIVA = "R"; |Y enEjercicio ] 2017;
       aAlfa1 = #camaniva#3;
       |SI #caivaasi#98 = "S"; aAlfa1 = ("000000" + aAlfa1) % -106; |FINSI;
       aAlfa2 = #camaniva#2; |QBF aAlfa2;
       |SI aAlfa2 ! "";  aAlfa1 = aAlfa2 + #caivaasi#97 + aAlfa1; |FINSI;
       eaReferen = aAlfa1;
    |FINSI;
|FPRC;

|PROCESO cabecera;       || actualiza cabeceras
   |DDEFECTO #2;
   #camaasic#00 = #3#0; ||  diario
   #camaasic#01 = #3#1; ||  fecha
   #camaasic#02 = #3#2; ||  documento
   #camaasic#03 = #3#2; ||  asiento
   |LEE 101#2=;
   |SI FSalida ! 0;
       |DDEFECTO #2;
       #camaasic#00 = #3#0; ||  diario
       #camaasic#01 = #3#1; ||  fecha
       #camaasic#02 = #3#2; ||  documento
       #camaasic#03 = #3#2; ||  asiento
       |GRABA 020#2b;
   |FINSI;
   |SI #3#08 = "D";
       nDebe = nDebe + #3#9;
       #camaasic#04 = #camaasic#04 + #3#09;
       |SI #camaasic#09 = 0; #camaasic#08 = #3#4; #camaasic#09 = #3#5; |FINSI;  || contrp.HABER
   |SINO;
       nHaber = nHaber + #3#9;
       #camaasic#05 = #camaasic#05 + #3#09;
       |SI #camaasic#11 = 0; #camaasic#10 = #3#4; #camaasic#11 = #3#5; |FINSI;  || contrp.DEBE
   |FINSI;
   #camaasic#6  = #camaasic#4 - #camaasic#5;      || saldo
   #camaasic#12 = #3#24;
   #camaasic#13 = enTipoAsto;
   |SI eaModif = "N";
       |SI enTipoAsto = 0; enTipoAsto = 6; |FINSI;
       #camaasic#13 = enTipoAsto + 100;
    |FINSI;
   |GRABA 020#2a;
   |LIBERA #2;
|FPRC;

|| ///////////////////////////////////////////////////////////
|PROCESO Linea;
   |DDEFECTO #3;
   #3#00 = nDiario;
   #3#01 = fFecConta;
   #3#02 = nDocumento;

   #3#03 = nApunte;       || apunte
   #3#04 = eaCuenta;      || cuenta
   |HAZ SacaNivel;
   #3#05 = enNivel;       || digito
   #3#06 = nConcepto;     || concepto
   #3#07 = aDescrip;      || descripcion
   #3#08 = aSigno;        || signo

   #3#09 = nImporte;      || importe
   |SI #3#08 = "D";
       #3#16 = #3#4;
       #3#17 = #3#5;
       #3#10 = "";
       #3#11 = 0;
   |SINO;
       #3#16 = "";
       #3#17 = 0;
       #3#10 = #3#4;
       #3#11 = #3#5;
   |FINSI;

   |SI aTipoIVA ! "";
       #3#12 = aTipoIVA;  || Tipo IVA
       #3#13 = nLibro;    || Libro IVA
       #3#14 = nFactura;
       #3#15 = aSerie;
       #3#19 = aCampo19;  || Tipo Acumulado
       #3#20 = nCampo20;  || Numero Acumulador
   |FINSI;
   #3#21 = eaCodDep;
   #3#28 = eaCodSubdep;
   |SI #3#19 = "B";
       #3#29 = aCtaAna;
   |FINSI;
   nDOCUMENTO = #3#2;
   nREGISTRO  = #3#24;
   |SI swIVA = 0; |Y aTipoIVA ! "";
       |HAZ CabIvas;
   |FINSI;
   |HAZ LaDescripcion;

   |GRABA 020#3n;

   |HAZ cabecera;           || cabecera apunte
   MasMenos     = #3#9;
   DebeHaber    = #3#8;
   aCUENTA      = #3#4;
   nREGISTRO    = #3#24;
   nDIARIO      = #3#0;
   aFECHA       = #3#1;
   nDOCUMENTO   = #3#2;
   nNIVELCUENTA = #3#5;
   |SI aParam = "basica";
       nPIDECUENTA  = 1;
   |FINSI;
   |HAZ ActDiario;          || diario
   |HAZ ActCuenta;          || cuenta
   |HAZ ActTesteo;          || Para recalculo en informes
   |SI aTipoIVA = "R"; |O aTipoIVA = "S";
       |HAZ ActIvas;
   |FINSI;
   nPIDECUENTA  = 0;
|FPRC;

|| ******************************************************************
|PROCESO LaDescripcion;
  aSepSer   = #caivaasi#97;
  aNumIzq   = #caivaasi#98;

  |SI #3#19 ! "X"; aDescrIVA = #3#7; |FINSI;

  |SI #3#19 ! "P";
      aAlfa5 = "";
      aAlfa1 = #3#7;  |QBF aAlfa1;  || Descripcion

      aAlfa2 = #caivaasi#45;
      |SI #3#19 = "I"; |O #3#19 = "R"; |O #3#19 = "B";
          |O #3#19 = "i"; |O #3#19 = "r"; |O #3#19 = "b";
            |SI #caivaasi#130 ! "=  ";
                aAlfa2 = #caivaasi#130;
            |FINSI;
      |FINSI;
      |SI #3#19 = "X"; aAlfa2 = eaAnyadir; |FINSI;

      |QBF aAlfa2;  || lo que a¤ade

      aAlfa3 = aAlfa2 % 0;
      nDCon0 = aAlfa3;
      |PARA nDCon1 = 1; |SI nDCon1 [ nDCon0; |HACIENDO nDCon1 = nDCon1 + 1;
            aAlfa3 = nDCon1;
            aAlfa3 = aAlfa3 + "01";
            aDCon2 = aAlfa2 % aAlfa3;
            |HAZ adapta2_c;
            aAlfa5 = aAlfa5 + aDCon1;
      |SIGUE;
      #3#7 = aAlfa1 + aAlfa5;
   |SINO;
        aAlfa5 = "";
        aAlfa1 = #3#7;  |QBF aAlfa1;  || Descripcion
        aAlfa2 = #caivaasi#96; |QBF aAlfa2;  || lo que a¤ade
        aAlfa3 = aAlfa2 % 0;

        nDCon0 = aAlfa3;
        |PARA nDCon1 = 1; |SI nDCon1 [ nDCon0; |HACIENDO nDCon1 = nDCon1 + 1;
              aAlfa3 = nDCon1;
              aAlfa3 = aAlfa3 + "01";
              aDCon2 = aAlfa2 % aAlfa3;
              |HAZ adapta2_c;
              aAlfa5 = aAlfa5 + aDCon1;
        |SIGUE;
        #3#7 = aAlfa1 +  aAlfa5;
   |FINSI;
   aDescrip = #3#7;
   |SI #caivaasi#131 = "S"; |Y #3#19 ! "X"; aDescrIVA = aDescrip; |FINSI;
|FPRC;

|PROCESO adapta2_c;  ||El concepto
     aDCon1 = "";
     |SI aDCon2 = "S";
         aDCon1 = #camaniva#2; |QBF aDCon1;
         |SI aDCon1 ! ""; aDCon1 = aDCon1 + aSepSer; |QBF aDCon1; |FINSI;
     |FINSI;                                         || Serie
     |SI aDCon2 = "F";
         aDCon1 = #camaniva#3; |QBF aDCon1;
         |SI aNumIzq = "S";
             aDCon1 = ("000000" + aDCon1) % -106;
         |FINSI;
     |FINSI;                                         || Factura
     |SI aDCon2 = "C";
         aDCon1 = #camaniva#12; |QBF aDCon1;
     |FINSI;                                         || Cuenta Cliente
     |SI aDCon2 = "N";
         aDCon1 = #camaniva#13; |QBF aDCon1;
     |FINSI;                                         || Nombre Cliente
     |SI aDCon2 = "R";
         |SI eaReferen ! "";
             aDCon1 = eaReferen; |QBF aDCon1;
         |FINSI;
     |FINSI;
                                     || Referencia
     |SI aDCon2 = "P";
         aDCon1 = eaReferen;
         |QBF aDCon1;
     |FINSI;                                         || Referencia Comersan

     |SI aDCon1 ! "";
         |SI aDCon2 = "F";
             aAlfa7 = aAlfa2 - "S";
             |SI aAlfa7 ! aAlfa2; |Y #camaniva#2 > "     ";
                 |ACABA;
             |FINSI;
         |FINSI;
         aDCon1 = " " + aDCon1;
     |FINSI;
|FPRC;
|| ///////////////////////////////////////////////////////////////////////

|PROCESO SituaEmpresa;
    cod1 = nEmpresa;
    cod2 = nPeriodo;
    emEmp = 1;  || Sin mensaje
    |HAZ leelo;
    |SI swerror ! 0;  nConta = 0; |FINSI;

    |SI nConta = 1;
        aAlfa1 = ("00000" + nEmpresa) % -105;
        aAlfa2=  ("0"     + nPeriodo) % -101;
        dirfich0 =  aPContag + "fich/" + aAlfa1 + aAlfa2 + "/";

        |PATH_DAT #02 dirfich0;
        |PATH_DAT #03 dirfich0;
        |PATH_DAT #05 dirfich0;
        |PATH_DAT #10 dirfich0;
        |PATH_DAT #11 dirfich0;
        |PATH_DAT #20 dirfich0;
        |PATH_DAT #caivaasi#dirfich0#;
        |PATH_DAT #caivaaxx#dirfich0#;
        |PATH_DAT #204 dirfich0;
        |PATH_DAT #205 dirfich0;
        |PATH_DAT #206 dirfich0;
        |PATH_DAT #207 dirfich0;
        |PATH_DAT #208 dirfich0;

        |HAZ eLeeParametro;
    |FINSI;
|FPRC;

|PROCESO CalLaFecha;
  |SI #30#26 < 80;
      nAny = 2000 + #30#26;
  |SINO;
      nAny = 1900 + #30#26;
  |FINSI;

  aAlfa1 = nAny;
  aAlfa2 = #30#11; |QBF aAlfa2;
  aAlfa2 = ("00" + aAlfa2) % -102;
  aAlfa2 = "01." + aAlfa2 + "." + aAlfa1;
  fec1  = aAlfa2;

  |SI #30#11 ! 1;
      nAny = nAny + 1;
      aAlfa1 = nAny;
  |FINSI;
  aAlfa2 = #30#12; |QBF aAlfa2;
  aAlfa2 = ("00" + aAlfa2) % -102;

  aAlfa3 = "31.";
  |SI #30#12 = 4; |O #30#12 = 6; |O #30#12 = 9; |O #30#12 = 11;
      aAlfa3 = "30.";
  |SINO;
      |SI #30#12 = 2;
          aAlfa3 = "28.";
          nNume1 = nAny / 4; nNume2 = (nAny / 4) ! 0;
          |SI nNume1 = nNume2;
              aAlfa3 = "29.";
          |FINSI;
      |FINSI;
  |FINSI;
  aAlfa2 = aAlfa3 + aAlfa2 + "." + aAlfa1;
  fec2 = aAlfa2;
|FPRC;
