|FICHEROS;
    cacamctm #0;     || pantalla
    cacamcts #1,MANTE;
    canempre #2;
    cacamctx #10,MANTE;
    cacuenta #69;    || Cuentas
|FIN;

|VARIABLES;
    &swTabla = 0;
    &aCampo0 = "";
    &nCampo1 = 0;
    &aCampo2 = "";
    &aCampo3 = "";
    &nCampo4 = 0;
    &aCampo5 = "";
    &aCampo6 = "";
    &aCampo9  = "";
    &nCampo10 = 0;
    &nCampo11 = 0;
    &fCampo12 = @;
    &fCampo13 = @;
    &nCampo14 = 0;
    &nCampo15 = 0;
    &nCampo16 = 0;
    &nCampo17 = 0;
    &pathbal = "";
    &balance = 1;
    aAlfa1   = "";
    aMultiEmp = "";
    &nLaEmpresa = 0;
    &nSwTipo    = 0;

    mens      = "";
    x         = 0;
    sw_ya     = 0;
    sw_ya0    = 0;
    aAlfa     = "";
    nPara1    = 0;
    nNume1    = 0;

    nAny      = 0;
    aAny      = "";
    aMes      = "";
    aDia      = "";
    fFechaBlq = @;
    fFechaFin = @;
|FIN;

|INCLUYE dscont_i;
____________________________________/ CALCULOS DE PANTALLA

|PROCESO cuenta; |TIPO 6;
     |SI FSalida = 2; |ACABA; |FINSI;

     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;

     |SI #0Campo = doce; |ERROR; |ACABA; |FINSI;

     x = Campo + 1;
     emCta = 1; |SI Campo = 0; emCta = 0; |FINSI;
     eaCuenta = #0Campo;
     |HAZ SacaNivel;
     |SI eswCta = 1;
         |SI Campo = 0;
             |CONFI "    SLONGITUD DE CUENTA FUERA DE NIVEL! Continuar S/N";
             |SI FSalida ! 0;
                 |ERROR;
                 |ACABA;
             |FINSI;
         |SINO;
             |ACABA;
         |FINSI;
     |FINSI;
     emCta = 0;
     #0x = enNivel;

     |SI Campo = 0; sw_ya0 = 0; |FINSI;
     |SI Campo = 3; sw_ya  = 0; |FINSI;
     |ABRE #69;
     #cacuenta#0 = #0Campo;
     #cacuenta#1 = #0x;
     |LEE 001#69=;
     |SI Campo = 0;
         |SI FSalida ! 0;
             sw_ya0 = 1;
             |CONFI "    NNo existe la Cuenta ... Continuar S/N";
             |SI FSalida ! 0;
                 sw_ya0 = 0;
                 |ERROR;
             |FINSI;
         |FINSI;
     |SINO;
          sw_ya = 0;
          |SI FSalida = 0;
              sw_ya = 1;
              |CONFI "0000NLa cuenta Ya existe. ¨Aceptarla S/N? ";
              |SI FSalida ! 0;
                  |ERROR;
              |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #69;
|FPRC;

|PROCESO cuefin; |TIPO 0;
   x = Campo + 2;
   |SI Campo = 3; |Y sw_ya  = 0; |ACABA; |FINSI;
   |SI Campo = 0; |Y sw_ya0 = 1; |ACABA; |FINSI;

   |SI #cacuenta#3 = "N";|Y FSalida = 0;
       |MENAV "    Cuenta sin Pase Directo ...";
       |CIERRA #69;
       |ERROR;
       |ACABA;
   |FINSI;

   #0x = #cacuenta#2;
   |PINTA #0x;
|FPRC;

|PROCESO desant; |TIPO 7;
   |C_M #0#5,1,"N";
   |SI sw_ya = 0;
      #0#5 = #0#2; |PINTA #0#5;
      |SI sw_ya0 = 1;
          |C_M #0#5,1,"S";
      |FINSI;
   |FINSI;
|FPRC;

||**************************************************************************
|PROCESO CalFechas;
   nAny = #canempre#26;
   |SI #canempre#11 > #canempre#12;
       nAny = nAny + 1;
       |SI nAny > 99; nAny = nAny - 100; |FINSI;
   |FINSI;
   |SI nAny > 80;
       nAny = nAny + 1900;
   |SINO;
       nAny = nAny + 2000;
   |FINSI;
   aAny = nAny;
   aMes = ("00" + #canempre#12) % -102;
   aDia = "31";
   |SI #canempre#12 = 4; |O #canempre#12 = 6; |O #canempre#12 = 9; |O #canempre#12 = 11;
       aDia = "30";
   |FINSI;
   |SI #canempre#12 = 2;
       aDia = "28";
      |SI nAny = 2000; |O nAny = 2004; |O nAny = 2008; |O nAny = 2012;
          |O nAny = 2016; |O nAny = 2020; |O nAny = 2024; |O nAny = 2028;
         aDia = "29";
      |FINSI;
   |FINSI;
   aAlfa = aDia + "." + aMes + "." + aAny;
   fFechaFin = aAlfa;
|FPRC;

|PROCESO ElBloqueo;
  nSwTipo = 0;
  fFechaBlq = fec3;
  fFechaFin = fec2;
  aAlfa1    = ("00000" + #canempre#2) % -105 + " " + #canempre#1; |QBF aAlfa1;

  |SI aMultiEmp = "S";
      fFechaBlq = #canempre#34;
      |HAZ CalFechas;
  |FINSI;

  |SI fFechaBlq > "01.01.0000";
      |SI fFechaBlq ] fFechaFin;  nSwTipo = 1;  |FINSI;
      |SI fFechaBlq < fFechaFin;
          |SI #10#1 = "N";
              nSwTipo = 2;
          |SINO;
              |SI fFechaBlq ] #10#4; nSwTipo = 2; |FINSI;
          |FINSI;
      |FINSI;
  |FINSI;

  |SI nSwTipo ! 0; |Y aMultiEmp = "S";
      aAlfa  = "0000Empresa: " + aAlfa1;
      |MENAV aAlfa;
  |FINSI;

  |SI nSwTipo = 1;
      aAlfa  = "0000Empresa bloqueada a fecha Fin de Ejercicio. Proceso no permitido";
      |MENAV aAlfa;
  |FINSI;

  |SI nSwTipo = 2;
      nSwTipo = 1;

      aAlfa = "0000NEmpresa bloqueada a " + fFechaBlq + ". Desea continuar con el cambio";
      |CONFI aAlfa;
      |SI FSalida = 0; nSwTipo = 0; |FINSI;

  |FINSI;
|FPRC;

||**************************************************************************
|PROCESO camcta;
     aAlfa = #0#3; |QBF aAlfa;
     |SI aAlfa = ""; |ACABA; |FINSI;

     swTabla = 1;
     aCampo0 = #0#0;
     nCampo1 = #0#1;
     aCampo2 = #0#2;
     aCampo3 = #0#3;
     nCampo4 = #0#4;
     aCampo5 = #0#5;
     aCampo6 = #0#6;
     aCampo9  = #10#1;
     nCampo10 = #10#2;
     nCampo11 = #10#3;
     fCampo12 = #10#4;
     fCampo13 = #10#5;
     nCampo14 = #10#6;
     nCampo15 = #10#7;
     nCampo16 = #10#8;
     nCampo17 = #10#9;
     |SI #10#1 = "S"; #0#6 = "N"; aCampo6 = "N"; |FINSI;
     |SI aMultiEmp = "S";
         aAlfa =  ":contagen/cacamcta canempr1 " + (("00000" + #2#2) % -105) + #2#3;
         |SUB_EJECUTA_NP aAlfa;
     |SINO;
         |SUB_EJECUTA_NP "cacamcta";
     |FINSI;
|FPRC;

|DEFBUCLE leeTemporal;
     #0#1;
     ;
     0001;
     9999;
     ;
     NULL, camcta;
|FIN;

|PROCESO canempre;

      |SI #1#0 = "S";
          |SI #1#79 ! "T";
              |SI #2#21 ! #1#79; |ACABA; |FINSI;
          |FINSI;

          nLaEmpresa = #2#2;
          |HAZ ElTipoEmpresa;
          |SI nSwTipo = 1; |ACABA; |FINSI;
      |FINSI;

      |HAZ ElBloqueo;
      |SI nSwTipo = 1; |ACABA; |FINSI;

      |BUCLE leeTemporal;

      |SI #1#65 = "S";
          |DFICB pathbal; |QBF pathbal;
          pathbal = pathbal + (("00000" + #2#2) % -105) + #2#3 + "/";
          balance = 1;
          |SUB_EJECUTA "careccue";
      |FINSI;
|FPRC;

|DEFBUCLE canempre;
     #canempre#1;
     ;
     #1#1, #1#2;
     #1#3, #1#4;
     ;
     NULL, canempre;
|FIN;

|PROCESO elCambio;
     |ABRE #10;
     |DDEFECTO #10;
     |LEE 001#10p;
     |CIERRA #10;

     |SI aMultiEmp = "S";
         |SI #1#0 = "S";
             |BUCLE canempre;
         |SINO;
             |ABRE #2;
             |PARA nPara1 = 5; |SI nPara1 [ 62; |HACIENDO nPara1 = nPara1 + 3;
                   nNume1 = nPara1 + 1;
                   |SI #1nPara1 ! 0;
                       #canempre#2 = #1nPara1;
                       #canempre#3 = #1nNume1;
                       |LEE 000#canempre.=;
                       |SI FSalida = 0;
                           |HAZ canempre;
                       |FINSI;
                   |FINSI;
            |SIGUE;
            |CIERRA #2;
         |FINSI;

         |CONFI "2410NDesea Borrar la Tabla de Cambio de Cuentas S/N ";
         |SI FSalida = 0;
             |DELINDEX #0;
         |FINSI;

         |ABRE #2;
         #canempre#2 = #48#2;
         #canempre#3 = #48#3;
         |LEE 000#canempre.=;
         |SI FSalida = 0;
             |PTEC 807;
             aAlfa =  ":contagen/cacuenta canempr1 " + (("00000" + #2#2) % -105) + #2#3;
             |SUB_EJECUTA_NP aAlfa;
         |FINSI;
         |CIERRA #2;
     |SINO;

         |HAZ ElBloqueo;
         |SI nSwTipo = 1; |ACABA; |FINSI;

         |BUCLE leeTemporal;

         |CONFI "2410NDesea Borrar la Tabla de Cambio de Cuentas S/N ";
         |SI FSalida = 0;
             |DELINDEX #0;
         |FINSI;

         |DFICE pathbal;
         |CONFI "2410SDesea Realizar el Recalculo de Saldos: ";
         |SI FSalida = 0;
             balance = 1;
             |SUB_EJECUTA "careccue";
         |FINSI;
     |FINSI;
|FPRC;

|| ********************************************************************
|PROCESO EntraMultiEmp;
     |PUSHV 0501 2380;
     |PINPA #0#1;
     |PINDA #0#1;
     |ENDATOS #1#1;
     |POPV;
|FPRC;

|PROCESO Tipo11ctm; |TIPO 11;
     |SI FSalida = 10;  |HAZ Limites; |FINSI;

     |SI aMultiEmp = "N";  |ACABA;  |FINSI;

     |SI FSalida = 11;
         |HAZ EntraMultiEmp;
     |FINSI;

|FPRC;

|RUTINA mix;
 #0#8 = 1;
|FRUT;

|RUTINA max;
 #0#8 = 9999;
|FRUT;

|PROCESO ElFGral;
  |DFICB aAlfa1; |QBF aAlfa1;
  aAlfa1 = aAlfa1 + "000000/";
  |PATH_DAT #0 aAlfa1;
|FPRC;

|PROCESO Selec12; |TIPO 12;
|FPRC;

|PROCESO Seleccion;
  aAlfa1 = #10#1;
  |C_M #10#2, 1, aAlfa1; |C_M #10#3, 1, aAlfa1;
  |C_M #10#4, 1, aAlfa1; |C_M #10#5, 1, aAlfa1;
  |C_M #10#6, 1, aAlfa1; |C_M #10#7, 1, aAlfa1;
  |C_M #10#8, 1, aAlfa1; |C_M #10#9, 1, aAlfa1;
  |SI aAlfa1 = "N";
      #10#2 = 00;     |PINTA #10#2;
      #10#3 = 99;     |PINTA #10#3;
      #10#4 = fec1;   |PINTA #10#4;
      #10#5 = fec2;   |PINTA #10#5;
      #10#6 = 000001; |PINTA #10#6;
      #10#7 = 999999; |PINTA #10#7;
      #10#8 = 001;    |PINTA #10#8;
      #10#9 = 999;    |PINTA #10#9;
 |FINSI;
|FPRC;

|PROCESO Limites;
 |ABRE #10;
 |LEE 101#10p;
 |SI FSalida ! 0;
     |DDEFECTO #10;
     #10#4 = fec1; #10#5 = fec2;
     |GRABA 020#10b;
 |FINSI;
 |PINDA #0#10;

 |ENDATOS #1#10;
 |GRABA 020#10a;
 |LIBERA #10;
 |CIERRA #10;
|FPRC;

|PROGRAMA;
     |HAZ inicio;
     |CLS;
     |CABEZA "Cambio Cuentas";
     |PINPA #0#0;

     |PUSHV 0501 0738;
     |ATRI P;
     |PINTA 0601, " Trabajar con Multiempresa S/N ";
     E_sup     = 1;
     E_inf     = "SNsn";
     aMultiEmp = "N";
     aMultiEmp = 0635 ? 0;
     aMultiEmp = aMultiEmp  > " "; |PINTA 0635, aMultiEmp;
     |ATRI 0;
     |POPV;

     |SI aMultiEmp = "S";
         |ATRI P;
         |PINTA 0602, "TABLA STANDAR (Empresa 000000)";
         |PINTA 0502, "<F3> Ver seleccion de empresas";
         |ATRI 0;
         |HAZ ElFGral;
         |HAZ EntraMultiEmp;
     |FINSI;

     |ENTLINEAL #1#0, -1, 7, 17, 0, mix, max;
     |PINPA #0#10;
     |HAZ Limites;

     |ENTLINEAL #1#0, -1, 1, 17, 0, mix, max;
     |SI FSalida = 0;
         |CONFI "1234SRealizar ahora el cambio S/N";
         |SI FSalida = 0;
             |HAZ elCambio;
         |FINSI;
     |FINSI;
|FPRO;
