|FICHEROS;
  cacuenta #0;
  camandia #10;

  caentasl #200;
  caentasc #201;
  cientasl #210;
  cientcon #211;
  cajainli #220;
  cajasald #221;

  cacuefus #300;
  cadiafus #301;

  imagen@  #900;

  caz00017 #917;
|FIN;

|INCLUYE dscont_i;

|VARIABLES;
  &nSwOpera = 0;
  aAlfa     = "";
  aAlfa1    = "";
  Ebloque   = "";
  Efichero  = "";
  Enum      = 0;
  Esigno    = "";
  Elacontra = "";
  Ecobro    = 0;
  Epagos    = 0;
  aOrigen   = "";
  aDestino  = "";

  nHandle   = 0;
  aDef      = "";
  aMas      = "";
|FIN;

|| *************************************************************************
|PROCESO Comp_Alta;

   aAlfa1 = "cactaesp"; |NOME_DAT #300 aAlfa1;

   |ABRE #300;
   #300#0 = eaCuenta;
   |LEE 000#300=;

   |ABRE #0;
   #0#0 = eaCuenta;
   |LEE 000#0=;
   |SI FSalida ! 0;
       |DDEFECTO #0;
       #0#0 = #300#0; #0#1 = #300#1;
       #0#2 = #300#2; #0#3 = #300#3;
       #0#4 = #300#4; #0#5 = #300#5;
       #0#6 = #300#6; #0#7 = #300#7;
       #0#8 = #300#8;
       #0#54 = #300#54; #0#55 = #300#55;
       #0#56 = #300#56; #0#57 = #300#57;
       #0#61 = #300#61;
       |GRABA 020#0n;
   |FINSI;

   |CIERRA #0;
   |CIERRA #300;
|FPRC;

|PROCESO Comp_Alta_Primera;
   |ABRE #0;
   |DDEFECTO #0;
   #0#0 = aCUENTA;
   |LEE 000#0=;
   |SI FSalida = 0;
       #900#0 = aCUENTA;
       |LEE 000#900=;
       |SI FSalida ! 0;
           |DDEFECTO #900;
           #900#0 = aCUENTA; #900#1 = #0#1;
           #900#2 = #0#2; #900#3 = #0#3;
           #900#4 = #0#4; #900#5 = #0#5;
           #900#6 = #0#6; #900#7 = #0#7;
           #900#8 = #0#8;
           #900#54 = #0#54; #900#55 = #0#55;
           #900#56 = #0#56; #900#57 = #0#57;
           #900#61 = #0#61;
           |GRABA 020#900n;
      |FINSI;
   |FINSI;
   |CIERRA #0;
|FPRC;

|| *************************************************************************

|PROCESO Linea2_Bloque;
    MasMenos     = #caentasl#9;
    DebeHaber    = #caentasl#8;
    aCUENTA      = #caentasl#4;
    nREGISTRO    = -1;
    nDIARIO      = #caentasl#2;
    aFECHA       = #caentasl#3;
    nDOCUMENTO   = #caentasl#21;
    nNIVELCUENTA = #caentasl#5;
    |HAZ ActDiario;          || diario
    |HAZ ActCuentM;          || cuenta

    |HAZ Comp_Alta_Primera;
|FPRC;

|DEFBUCLE Linea1_Bloque;
   #caentasl#1;
   ;
   INICIO;
   FINAL;
   e;
   NULL, Linea2_Bloque;
|FIN;

|PROCESO Linea_Bloque;

  Ebloque = #caentasc#0;
  Efichero = Ebloque;
  |QBF Efichero;
  Efichero = Efichero + "zzzzzzzz";
  Efichero = Efichero % 107;
  Efichero = Efichero + "l";

  |NOME_DAT #caentasl #Efichero#;

  |BUCLE Linea1_Bloque;
|FPRC;

|DEFBUCLE Cabec_Bloque;
 #caentasc#1;
 ;
 "        ";
 "zzzzzzzz";
 e;
 NULL, Linea_Bloque;
|FIN;

|| *************************************************************************
|| *************************************************************************
|PROCESO Linea3_Cta;
   MasMenos     = #cientcon#8;
   DebeHaber    = Esigno;
   aCUENTA      = #cientcon#4;
   nREGISTRO    = -1;
   nDIARIO      = -1;
   aFECHA       = #cientasl#3;
   nDOCUMENTO   = -1;
   nNIVELCUENTA = #cientcon#5;
                || |HAZ ActDiario;          || NO TIENE DIARIO ASIGNADO
   |HAZ ActCuentM;          || cuenta
   |HAZ Comp_Alta_Primera;
|FPRC;

|DEFBUCLE Linea2_Cta;
   #cientcon#1;
   ;
   #cientasl#0, #cientasl#2, 00001;
   #cientasl#0, #cientasl#2, 99999;
   e;
   NULL, Linea3_Cta;
|FIN;

|PROCESO Linea1_Cta;
   |SI #cientasl#7 = 0; |ACABA; |FINSI;

   || ..................... Actualizacion Cuenta
   MasMenos     = #cientasl#7;
   DebeHaber    = #cientasl#6;
   aCUENTA      = #cientasl#0;
   nREGISTRO    = -1;
   nDIARIO      = -1;
   aFECHA       = #cientasl#3;
   nDOCUMENTO   = -1;
   nNIVELCUENTA = #cientasl#1;
                      || |HAZ ActDiario;          || NO TIENE DIARIO ASIGNADO
   |HAZ ActCuentM;          || cuenta
   |HAZ Comp_Alta_Primera;

   |SI #cientasl#6 = "D";
      Esigno = "H";
   |SINO;
      Esigno = "D";
   |FINSI;
   || ............ Contrapartidas;
   Elacontra = #cientasl#8;
   |QBF Elacontra;
   |SI Elacontra ! "";
       MasMenos     = #cientasl#7;
       DebeHaber    = Esigno;
       aCUENTA      = #cientasl#8;
       nREGISTRO    = -1;
       nDIARIO      = -1;
       aFECHA       = #cientasl#3;
       nDOCUMENTO   = -1;
       nNIVELCUENTA = #cientasl#9;
                          || |HAZ ActDiario;          || NO TIENE DIARIO ASIGNADO
       |HAZ ActCuentM;          || cuenta

       |HAZ Comp_Alta_Primera;
   |SINO;
       |BUCLE Linea2_Cta;
   |FINSI;
|FPRC;

|DEFBUCLE Linea_Cta;
   #cientasl#1;
   ;
   INICIO;
   FINAL;
   be;
   NULL, Linea1_Cta;
|FIN;

|| *************************************************************************
|| *************************************************************************
|PROCESO Linea1_Caja;
   |SI #cajainli#9 = 0; |ACABA; |FINSI;

   Ecobro = 0;
   Epagos = 0;
   |SI #cajainli#8 = "P";
       Esigno= "D";
       Epagos = Epagos + #cajainli#9;
   |SINO;
       Esigno = "H";
       Ecobro = Ecobro + #cajainli#9;
   |FINSI;   || cobro(haber),pago(debe)

   DebeHaber    = Esigno;
   MasMenos     = #cajainli#9;
   aCUENTA      = #cajainli#4;
   nREGISTRO    = -1;
   nDIARIO      = #cajainli#0;
   aFECHA       = #cajainli#1;
   nDOCUMENTO   = #cajainli#2;
   nNIVELCUENTA = #cajainli#5;
   |HAZ ActDiario;          || diario
   |HAZ ActCuentM;          || cuenta
   |HAZ Comp_Alta_Primera;

|| ... Contrapartida de Caja
   aCUENTA      = #cajasald#2;
   nNIVELCUENTA = #cajasald#3;
   |SI Epagos ! 0;
       DebeHaber = "H";
       MasMenos  = Epagos;
       |HAZ ActDiario;          || diario
       |HAZ ActCuentM;          || cuenta
   |FINSI;
   |SI Ecobro ! 0;
       DebeHaber = "D";
       MasMenos  = Ecobro;
       |HAZ ActDiario;          || diario
       |HAZ ActCuentM;          || cuenta
   |FINSI;
   |LIBERA #cajainli;
   |HAZ Comp_Alta_Primera;
|FPRC;

|DEFBUCLE Linea_Caja;
   #cajainli#1;
   ;
   INICIO;
   FINAL;
   e;
   NULL, Linea1_Caja;
|FIN;

|| *************************************************************************
|PROCESO Ctas_EntrEsp;  ||Tratamiento de las Cuentas en Entradas Especiales
    |DBASE aMas;
    aDef = aMas + "def/cacuenta";
    |CARGA_DEF aDef, imagen@;
    |SI FSalida ! 0;
        |ACABA;
    |FINSI;

    |HAZ E_copiando;

    aAlfa1 = "cactaesp"; |NOME_DAT #0  aAlfa1;
    aAlfa1 = "cadiaesp"; |NOME_DAT #10 aAlfa1;

    Ecobro = 0;
    Epagos = 0;

    |ABRE #900;
    |BUCLE Cabec_Bloque;
    |BUCLE Linea_Cta;

    |ABRE #cajasald;
    |LEE 001#cajasald.p;
    |SI FSalida = 0;
        |BUCLE Linea_Caja;
    |FINSI;
    |CIERRA #cajasald;

    |CIERRA #900;
    |DESCARGA_DEF #900;
|FPRC;

|PROGRAMA;

  aAlfa1 = "cacuenta"; |NOME_DAT #0  aAlfa1;
  aAlfa1 = "camandia"; |NOME_DAT #10 aAlfa1;

  cactaesp = "cactaesp";
  cadiaesp = "cadiaesp";
  |SI nSwOpera = 0;  || Crear Fichero
      |HAZ Ctas_EntrEsp;
  |FINSI;
  |SI nSwOpera = 1; || Comprobar altas
      |HAZ Comp_Alta;
  |FINSI;

  aAlfa1 = "cactaesp"; |NOME_DAT #0  aAlfa1;
  aAlfa1 = "cadiaesp"; |NOME_DAT #10 aAlfa1;
|FPRO;

|| *************************************************************************
||  Proceso para recoger Plan Contable
|PROCESO cadiaesp1;
   #301#0 = #camandia#0;
   |LEE 000#301=;
   |SI FSalida ! 0;
       #301#0 = #camandia#0;
       #301#1 = #camandia#1;
       #301#2 = #camandia#2;
       #301#3 = #camandia#3
       |GRABA 020#301n;
   |FINSI;
|FPRC;

|DEFBUCLE cadiaesp1;
 #camandia#1;
 ;
 00;
 99;
 ;
 NULL, cadiaesp1;
|FIN;

|PROCESO cactaesp10;
   #0#0 = #300#0;
   |LEE 141#0=;
   |SI FSalida ! 0;
       |DDEFECTO #0;
       #0#0 = #300#0; #0#1 = #300#1;
       #0#2 = #300#2; #0#3 = #300#3;
       #0#4 = #300#4; #0#5 = #300#5;
       #0#6 = #300#6; #0#7 = #300#7;
       #0#8 = #300#8;
       #0#54 = #300#54; #0#55 = #300#55;
       #0#56 = #300#56; #0#57 = #300#57;
       #0#61 = #300#61;
       |GRABA 020#0n;
   |FINSI;
   |LIBERA #0;
|FPRC;

|DEFBUCLE cactaesp1;
 #300#6;
 ;
 "S","            ";
 "S","zzzzzzzzzzzz";
 ;
 NULL, cactaesp10;
|FIN;

|PROCESO E_copiando;

   aAlfa1 = "cactaesp"; |NOME_DAT #300 aAlfa1;
   aAlfa1 = "cadiaesp"; |NOME_DAT #301 aAlfa1;

   |ABRE #917;
   |LEE 000#917p;
   |SI FSalida ! 0; |O #917#1 ! "S";
       |ABRE #0;
       |BUCLE cactaesp1;
       |CIERRA #0;
       #917#1 = "S";
       |GRABA 020#917n;
   |FINSI;
   |CIERRA #917;

   |DELINDEX #300;
   |DELINDEX #301;

   |ABRE #300;
   |ABRE #301;

   |DFICE aOrigen;   |QBF aOrigen;  aOrigen  = aOrigen  + "cacuenta.dat";
   |DFICE aDestino;  |QBF aDestino; aDestino = aDestino + "cactaesp.dat";
   |COPIA_FICHERO aOrigen, aDestino;
   |DFICE aOrigen;   |QBF aOrigen;  aOrigen  = aOrigen  + "cacuenta.ddx";
   |DFICE aDestino;  |QBF aDestino; aDestino = aDestino + "cactaesp.ddx";
   |COPIA_FICHERO aOrigen, aDestino;
   |DFICE aOrigen;   |QBF aOrigen;  aOrigen  = aOrigen  + "cacuenta.ixx";
   |DFICE aDestino;  |QBF aDestino; aDestino = aDestino + "cactaesp.ixx";
   |COPIA_FICHERO aOrigen, aDestino;

   |BUCLE cadiaesp1;

   |CIERRA #300;
   |CIERRA #301;
|FPRC;
|| *************************************************************************
