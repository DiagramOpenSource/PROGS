|FICHEROS;
 caut0002 #0;

 camaasil #1;
 camaniva #2;
 caivalin #3;

 caw00005 #5,MANTE;
 caw00006 #6,MANTE;

 caivaasi #200;
|FIN;

|VARIABLES;
 nInkey   = 0;
 aFichero = "";

 swHago = 0;
 aAlfa1 = "";
 aAlfa2 = "";
 nNume1 = 0;
 nNume2 = 0;
 nPara1 = 0;
 nPara2 = 0;

 nContDe = 0;
 nContSu = 0;

 SwCambia = 0;
 aElDepar = "";
 aElSubde = "";
 &snFilBa = "";
|FIN;

|INCLUYE dscont_i;

|| **************************************
|CALCULO Primero; |TIPO 8;
  |HAZ inicio;

   |SI eaDepar = "N"; |Y eaSubde = "N";
      |CONFI "0000NEsta Empresa No tiene Departamento ni Subdepartamento. Desea Continuar";
      |SI FSalida ! 0;
          |PTEC 807;
          |ACABA;
      |SINO;
          eaDepar = "S";
          eaSubde = "S";
      |FINSI;
   |FINSI;
   |HAZ DirAplicSt;
   |ABRE #200;
   |DDEFECTO #200;
   |LEE 001#200p;
   |CIERRA #200;
   snFilBa = #200#119;

|FCAL;

|CALCULO Defectos; |TIPO 7;
 #0#3 = fec1; |PINTA #0#3;
 #0#4 = fec2; |PINTA #0#4;

 |C_M #0#8, 1,eaDepar; |C_M #0#9,1,eaDepar;
 |C_M #0#10,1,eaDepar; |C_M #0#11,1,eaDepar;

 |SI eaDepar = "N";
     #0#8 = "N"; |PINTA #0#8;
 |FINSI;

 |C_M #0#12, 1,eaSubde; |C_M #0#13,1,eaSubde;
 |C_M #0#14, 1,eaSubde; |C_M #0#15,1,eaSubde;

 |SI eaSubde = "N";
     #0#12 = "N"; |PINTA #0#12;
 |FINSI;
|FCAL;

|CALCULO LaFecha; |TIPO 0;
 |SI #0Campo < fec1; |O #0Campo  > fec2;
     |MENAV "    Error, Fecha Incorrecta ";
     |ERROR;
     |ACABA;
 |FINSI;
 |SI Campo = 4; |Y #0#3 > #0#4;
     |MENAV "    Error, Fecha Incorrecta ";
     |ERROR;
 |FINSI;
|FCAL;

|CALCULO CambioDepar; |TIPO 0;
  |C_M #0Campo,0,aAlfa1;
  |SI aAlfa1 = "N";  |ACABA; |FINSI;

  aAlfa1 = #0#8;
  |C_M #0#9,  1, aAlfa1;
  |C_M #0#10, 1, aAlfa1;
  |C_M #0#11, 1, aAlfa1;
|FCAL;

|CALCULO CambioSubde; |TIPO 0;
  |C_M #0Campo,0,aAlfa1;
  |SI aAlfa1 = "N";  |ACABA; |FINSI;

  aAlfa1 = #0#12;
  |C_M #0#13, 1, aAlfa1;
  |C_M #0#14, 1, aAlfa1;
  |C_M #0#15, 1, aAlfa1;
|FCAL;

|| **********************************************************************
|CALCULO  LaAct_5; |TIPO 0;
  eOpDep = 0; || 1 = Consulta pantalla
  |SI FSalida = 10; eOpDep = 1;  |FINSI;
  enActividad = #5#2;
  eaCodDep    = #5#2;
  |HAZ Actividad;
  |SI eswLect = 1;
      |MENAV "    Error.! No Existe Actividad / Departamento";
      |ERROR;
  |SINO;
      aAlfa1 = enActividad;
      |SI Haybasica = 1;
          aAlfa1 = ("00" + aAlfa1) % A02;
      |FINSI;
      #5#2 = eaCodDep;    |PINTA #5#2;
      #5#3 = eaNombreAct; |PINTA #5#3;
      |SI efActFin > "01.01.1900"; |Y snFilBa = "S";
           aAlfa1 = "    NAtencion!!!. Actividad con Fecha de BAJA " + efActFin + ", Desea introducir Facturas ? ";
           |CONFI aAlfa1;
          |SI FSalida ! 0;
              |ERROR;
              |ACABA;
          |FINSI;
      |FINSI;
      |SI eaEsCom = "S";
          |CONFI "    NAtencion!!!. Actividad Comunera, ¨ Desea aceptar ? ";
          |SI FSalida ! 0;
              |ERROR;
              |ACABA;
          |FINSI;
      |FINSI;
   |FINSI;
|FCAL;

|CALCULO  LaAct_6; |TIPO 0;
  eOpDep = 0; || 1 = Consulta pantalla
  |SI FSalida = 10; eOpDep = 1;  |FINSI;
  enActividad = #6#1;
  eaCodDep    = #6#1;
  |HAZ Actividad;
  |SI eswLect = 1;
      |MENAV "    Error.! No Existe Actividad / Departamento";
      |ERROR;
  |SINO;
      aAlfa1 = enActividad;
      |SI Haybasica = 1;
          aAlfa1 = ("00" + aAlfa1) % A02;
      |FINSI;
      #6#1 = eaCodDep;    |PINTA #6#1;
      |SI efActFin > "01.01.1900"; |Y snFilBa = "S";
          aAlfa1 = "    NAtencion!!!. Actividad con Fecha de BAJA " + efActFin + ", Desea introducir Facturas ? ";
          |CONFI aAlfa1;
          |SI FSalida ! 0;
              |ERROR;
              |ACABA;
          |FINSI;
      |FINSI;
      |SI eaEsCom = "S";
          |CONFI "    NAtencion!!!. Actividad Comunera, ¨ Desea aceptar ? ";
          |SI FSalida ! 0;
              |ERROR;
              |ACABA;
          |FINSI;
      |FINSI;
   |FINSI;
|FCAL;

|CALCULO ElSubd_6; |TIPO 0;
   eOpSub = 0; || 1 = Consulta pantalla
   |SI FSalida = 10; eOpSub = 1;  |FINSI;
   enConcepto  = 001;
   eaCodSubdep = #6#3;
   enQue = 1;
   |HAZ SubActividad;
   enQue = 0;
   |SI eswLect = 1;
       |MENAV "    Error.! No Existe SubDepartamento";
       |ERROR;
   |SINO;
       #6#3 = eaCodSubdep; |PINTA #6#3;
       #6#4 = eaNomSubdep; |PINTA #6#4;
   |FINSI;
|FCAL;

|| **********************************************************************



|RUTINA ClaveBajaW5;
 #5#0 = 001;
|FRUT;

|RUTINA ClaveAltaW5;
 #5#0 = 999;
|FRUT;

|RUTINA ClaveBajaW6;
 #6#0 = 001;
|FRUT;

|RUTINA ClaveAltaW6;
 #6#0 = 999;
|FRUT;

|CALCULO ElCambio; |TIPO 12;
  nInkey = FSalida;
  |SI nInkey = 7; |O #0#16 = "N";
      |ACABA;
  |FINSI;

  aFichero = ("w5" + Usuario) % 108;
  |NOME_DAT #5 aFichero;
  aFichero = ("w6" + Usuario) % 108;
  |NOME_DAT #6 aFichero;
  |ABRE #5; |CIERRA #5; |DELINDEX #5;
  |ABRE #6; |CIERRA #6; |DELINDEX #6;

  nContDe = 0;
  nContSu = 0;
  |SI #0#8 = "S";      ||Cambia los departamentos
      |PINPA #0#5;
      |SI #0#11 = "S";
          |HAZ DeparAuto;
          |ENTLINEAL #1#5, -1, 7, 12, 0, ClaveBajaW5, ClaveAltaW5;
      |FINSI;
  |FINSI;

  |SI #0#12 = "S";      ||Cambia los subdepartamentos
      |PINPA #0#6;
      |SI #0#15 = "S";
          |HAZ SubdeparAuto;
          |ENTLINEAL #1#6, -1, 7, 22, 0, ClaveBajaW6, ClaveAltaW6;
      |FINSI;
  |FINSI;

  |SI #0#8 = "S";      ||Cambia los departamentos
      |ENTLINEAL #1#5, -1, 2, 12, 0, ClaveBajaW5, ClaveAltaW5;
  |FINSI;
  |SI #0#12 = "S";      ||Cambia los subdepartamentos
      |ENTLINEAL #1#6, -1, 2, 22, 0, ClaveBajaW6, ClaveAltaW6;
  |FINSI;

  |CONFI "    SEjecutar Proceso ? ";
  |SI FSalida = 0;
      |HAZ HazTodo;
  |FINSI;

  |DELINDEX #5;
  |DELINDEX #6;
|FCAL;

|| //////////////////////////////////////////////////////
|PROCESO LeeDep;
 #5#1 = aElDepar;
 |LEE 001#5=;
 |SI FSalida = 0;
     aElDepar    = #5#2;
     eaNombreAct = #5#3;
     SwCambia = 1;
 |FINSI;
|FPRC;

|PROCESO LeeSub;
 #6#1 = aElDepar;
 #6#2 = aElSubde;
 |LEE 001#6=;
 |SI FSalida = 0;
     aElSubde    = #6#3;
     eaNomSubdep = #6#4;
     SwCambia = 1;
 |FINSI;
|FPRC;

|| ////////////////////////////////////////////////////////////////////////
|| ------------------------------------------------------------------------

|PROCESO LineasAsto0;
 SwCambia = 0;
 |SI eaDepar = "S"; |Y #0#8 = "S";
     aElDepar = #1#21;
     |HAZ LeeDep;
     #1#21 = aElDepar;
 |FINSI;
 |SI eaSubde = "S"; |Y #0#12 = "S";
     aElDepar = #1#21;
     aElSubde = #1#28;
     |HAZ LeeSub;
     #1#28 = aElSubde;
 |FINSI;
 |SI SwCambia = 1;
     |GRABA 020#1a;
 |FINSI;
 |LIBERA #1;
|FPRC;

|DEFBUCLE LineasAsto;
 #1#1;
 ;
 #0#1, #0#3, #0#5, 0001;
 #0#2, #0#4, #0#6, 9999;
 be;
 NULL, LineasAsto0;
|FIN;

|| ---------------------------------
|PROCESO LineasIVA0;
 aAlfa1 = #3#30; |QBF aAlfa1;
 |SI aAlfa1 = "";
     #3#30  = #2#10;
     aAlfa1 = #3#31; |QBF aAlfa1; |SI aAlfa1 = ""; #3#31 = #2#11; |FINSI;
 |FINSI;

 SwCambia = 0;
 |SI eaDepar = "S"; |Y #0#8 = "S";
     aElDepar    = #3#30;
     |HAZ LeeDep;
     #3#30 = aElDepar;
 |FINSI;
 |SI eaSubde = "S"; |Y #0#12 = "S";
     aElDepar    = #3#30;
     aElSubde    = #3#31;
     |HAZ LeeSub;
     #3#31 = aElSubde;
 |FINSI;
 |SI SwCambia = 1;
     |GRABA 020#3a;
 |FINSI;
 |LIBERA #3;
|FPRC;

|DEFBUCLE  LineasIVA;
 #3#1;
 ;
 #2#0, #2#1, 01;
 #2#0, #2#1, 99;
 be;
 NULL, LineasIVA0;
|FIN;

|PROCESO CabecIVA0;
 SwCambia = 0;
 |SI eaDepar = "S"; |Y #0#8 = "S";
     aElDepar    = #2#10;
     eaNombreAct = #2#24;
     |HAZ LeeDep;
     #2#10 = aElDepar;
     #2#24 = eaNombreAct;
 |FINSI;
 |SI eaSubde = "S"; |Y #0#12 = "S";
     aElDepar    = #2#10;
     aElSubde    = #2#11;
     eaNomSubdep = #2#25;
     |HAZ LeeSub;
     #2#11 = aElSubde;
     #2#25 = eaNomSubdep;
 |FINSI;
 |SI SwCambia = 1;
     |GRABA 020#2a;
 |FINSI;
 |LIBERA #2;
 |BUCLE LineasIVA;
|FPRC;

|DEFBUCLE CabecIVA;
 #2#2;
 ;
 #0#1, #0#3, #0#5;
 #0#2, #0#4, #0#6;
 be;
 NULL, CabecIVA0;
|FIN;
|| ---------------------------------

|PROCESO HazTodo;
 |ABRE #5;
 |ABRE #6;

 |SELECT #2#5;
 |SELECT #2#6;

 |SI #0#0 ! "R";
     |BUCLE LineasAsto;
 |FINSI;
 |SI #0#0 ! "A";
     |BUCLE CabecIVA;
 |FINSI;

 |SELECT #1#6;
 |SELECT #1#5;

 |CIERRA #5;
 |CIERRA #6;
|FPRC;

|| **************************************************************************
|PROCESO DeparAuto;
 |ABRE #5;

 |PARA nPara1 = 0; |SI nPara1 [ 9; |HACIENDO nPara1 = nPara1 + 1;
       aAlfa1 = nPara1; |QBF aAlfa1;
       |SI aAlfa1 ] #0#9; |Y aAlfa1 [ #0#10;
           aAlfa2 = ("00" + aAlfa1) % -102;
           eOpDep      = 0;
           enActividad = aAlfa2;
           eaCodDep    = aAlfa2;
           eSwNoalta   = 1;
           |HAZ Actividad;
           |SI eswLect = 0;
               nContDe = nContDe + 1;
               #5#0 = nContDe;
               #5#1 = aAlfa1;
               #5#2 = aAlfa2;
               #5#3 = eaNombreAct;
               |GRABA 020#5n;
           |FINSI;
       |FINSI;
 |SIGUE;
 |CIERRA #5;
|FPRC;

|PROCESO SubdeparAuto;
 |ABRE #6;

 |PARA nPara2 = 0; |SI nPara2 [ 99; |HACIENDO nPara2 = nPara2 + 1;
       eOpDep      = 0;
       enActividad = nPara2;
       eaCodDep    = nPara2;
       eSwNoalta   = 1;
       |HAZ Actividad;
       |SI eswLect = 0;      || Existe Actividad

       |PARA nPara1 = 0; |SI nPara1 [ 99; |HACIENDO nPara1 = nPara1 + 1;
             aAlfa1 = nPara1; |QBF aAlfa1;
             |SI aAlfa1 ] #0#13; |Y aAlfa1 [ #0#14;
                 aAlfa2 = ("000" + aAlfa1) % -103;
                 eaCodSubdep = aAlfa2;
                 |HAZ OtroSubActividad;
                 |SI eswLect = 0;
                     nContSu = nContSu + 1;
                     #6#0 = nContSu;
                     #6#1 = eaCodDep;
                     #6#2 = aAlfa1;
                     #6#3 = aAlfa2;
                     #6#4 = eaNomSubdep;
                     |GRABA 020#6n;
                |FINSI;
             |FINSI;
       |SIGUE;

       |FINSI;
  |SIGUE;

  |CIERRA #6
|FPRC;
|| **************************************************************************
