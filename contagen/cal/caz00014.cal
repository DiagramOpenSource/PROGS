|FICHEROS;
   caz00014 #0;

   camaasil #10;
   cacuenta #11;

   anm00007 #12;
   caclipro #13;
   caconcic #14;
   caconvec #15;
   cacuedep #16;
   caexistl #17;

   cainliva #18;
   camaniva #19;
   camoauxc #20;
   catesfin #21;
   cavengca #22;
   cientasc #23;
   cisalant #24;

   caconiso #101;
   cafinanc #102;
   caarm001 #103;
   cacambix #99;
   canempre #30;
   caselem1 #998;
|FIN;

|VARIABLES;

   &entrada = 1;
   &eaTitulo = "";
   informe  = "caborcta";
   nLaPos   = 0;
   nLonCam0 = 0;
   aCampo0  = "";  || Literal pantalla origen
   aCampo1  = "";  || Literal pantalla destino
   aCampo0M = "";  || origen mayuscula
   aCampo1M = "";  || destino mayuscula
   aCampo1m = "";  || destino minuscula
   aCampo1x = "";  || destino primera mayuscula, resto minuscula
   aTitul1  = "";
   aTitul2  = "";
   aTitul1M = "";
   aCampoCam = "";

   &enContar = 0;
   &eaMov    = "";
   aDDir     = "";
   aHDir     = "";
   aDCuenta  = "";
   aHCuenta  = "";
   nDDig     = 0;
   nHDig     = 0;
   aMov      = "";
   aInf      = "";
   nSwAcum   = 0;

   nSw      = 0;
   nSwT     = 0;
  nSwConiso = 0;

   aAlfa1  = "";
   aAlfa2  = "";
   aAlfa3  = "";
   aAlfa4  = "";
   aAlfa5  = "";
   aAlfa6  = "";
   nNume1  = 0;
   nNume2  = 0;
   nNume3  = 0;
   nNume4  = 0;
   nSalir  = 0;

   aFichero = "";
   nPaseS   = 0;
|FIN;

|INCLUYE dscont_i;

|CALCULO el12; |TIPO 12;
|FCAL;

|CALCULO cuenta; |TIPO 0;
     |C_M #0Campo, 0, aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;
|FCAL;

|CALCULO VerNivel;
     |C_M #0Campo, 0, aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     |SI #0Campo > dig3;
         |MENAV "    El numero de niveles no coincide con el definido en la Empresa ...";
         |ERROR;
     |FINSI;
|FCAL;

||**************************************************************************
|PROCESO CambioTitulo;

  nSw = 0;
  aTitul2 = aTitul1;

  |SI aCampo0M = "=";
      aTitul2 = #11#2;
      nSw = 1;
      #0#3 = #11#0 + #11#2; |PINTA #0#3;
      |ACABA;
  |FINSI;

  aTitul1M = aTitul1;
  |SI #0#2 = "S"; aTitul1M = aTitul1M > " "; |FINSI;

  aAlfa1 = aTitul1M - aCampo0M;
  nLaPos = FSalida;
  |SI nLaPos ! 0;
      nSw = 1;
      aAlfa2 = nLaPos;
      aAlfa3 = aTitul1 % aAlfa2;

      aAlfa4 = aAlfa3 > " ";  || Esta en mayuscula
      |SI aAlfa4 = aAlfa3;
          aCampoCam = aCampo1M;
      |SINO;
          aAlfa4 = aAlfa3 < " ";
          |SI aAlfa4 = aAlfa3;
              aCampoCam = aCampo1m;  ||Esta en minusculas
          |SINO;
              aCampoCam = aCampo1x;
          |FINSI;
      |FINSI;

      aAlfa4 = "";
      aAlfa5 = "";
      aAlfa3 = aAlfa2 - (aAlfa2 % -102);
      nNume3 = aAlfa3; nNume3 = nNume3 - 1;
      |SI nNume3 ! 0;
          aAlfa3 = nNume3; aAlfa3 = "1" + (("00" + aAlfa3) % -102);
          aAlfa4 = aTitul1 % aAlfa3;
      |FINSI;
      aAlfa3 = aAlfa2 % -102; nNume2 = aAlfa3;
      nNume4 = nNume3 + nNume2 + 1;
      aAlfa3 = nNume4; aAlfa3 = aAlfa3 + "36";
      aAlfa5 = aTitul1 % aAlfa3;
      aTitul2 = aAlfa4 + aCampoCam + aAlfa5;

      aAlfa1 = aAlfa6; |QBF aAlfa1;
      aAlfa1 = aAlfa1 + " " + aTitul2;
      #0#3 = aAlfa1; |PINTA #0#3;

  |FINSI;
|FPRC;

|PROCESO CambioPorCta;
 nSw = 0;
 |ABRE #99;
 #99#0 = eaCuenta;
 #99#1 = 0;
 |LEE 001#99=;
 |SI FSalida = 0;
     nSw     = 1;
     aTitul2 = #99#5;
 |FINSI;
 |CIERRA #99;
 |SI eaCuenta = "            ";
     nSw      = 1;
     aTitul2  = "";
     #99#5    = "";
 |FINSI;
|FPRC;

||**************************************************************************
|| /////////////////////
|PROCESO CaFinanc0;

 nSwT = 0;
 |PARA nNume1 = 12; |SI nNume1 [ 16; |HACIENDO nNume1 = nNume1 + 1;
       nNume2 = nNume1 - 5;
       eaCuenta = #102nNume2;  |HAZ CambioPorCta;
       |SI nSw = 1; #102nNume1 = aTitul2; nSwT = 1; |FINSI;
 |SIGUE;

 eaCuenta = #102#35;  |HAZ CambioPorCta;
 |SI nSw = 1; #102#36 = aTitul2; nSwT = 1; |FINSI;

 eaCuenta = #102#41;  |HAZ CambioPorCta;
 |SI nSw = 1; #102#42 = aTitul2; nSwT = 1; |FINSI;

 |SI nSwT = 1;
     |GRABA 020#102a;
 |FINSI;
 |LIBERA #102;

|FPRC;

|DEFBUCLE CaFinanc;
 #102#1;
 ;
 enEmpresa, 00001;
 enEmpresa, 99999;
 be;
 NULL, CaFinanc0;
|FIN;

||**************************************************************************
|| /////////////////////
|PROCESO CaArm0010;

 nSwT = 0;
 |PARA nNume1 = 15; |SI nNume1 [ 22; |HACIENDO nNume1 = nNume1 + 2;
       nNume2 = nNume1 - 1;
       eaCuenta = #103nNume2;  |HAZ CambioPorCta;
       |SI nSw = 1; #103nNume1 = aTitul2; nSwT = 1; |FINSI;
       |SI nNume1 = 19; nNume1 = 20; |FINSI;
 |SIGUE;

 |SI nSwT = 1;
     |GRABA 020#103a;
 |FINSI;
 |LIBERA #103;
|FPRC;

|DEFBUCLE CaArm001;
 #103#1;
 ;
 enEmpresa, 00001;
 enEmpresa, 99999;
 be;
 NULL, CaArm0010;
|FIN;
|| ///////////////////////////////
|PROCESO CaConiso;

  |ABRE #101;
  |LEE 101#101p;
  |SI FSalida = 0;
      nSwT = 0;
      |PARA nNume1 = 3; |SI nNume1 [ 33; |HACIENDO nNume1 = nNume1 + 3;
            aTitul1 = #101nNume1;  |HAZ CambioTitulo;
            |SI nSw = 1; #101nNume1 = aTitul2; nSwT = 1; |FINSI;
      |SIGUE;

      |SI nSwT = 1;  |GRABA 020#101a;  |FINSI;

  |FINSI;
  |LIBERA #101;
  |CIERRA #101;
|FPRC;

|| /////////////////////
|PROCESO CiSalant0;

 nSwT = 0;
 eaCuenta = #24#0;  |HAZ CambioPorCta;
 |SI nSw = 1; #24#2 = aTitul2; nSwT = 1; |FINSI;

 eaCuenta = #24#5;  |HAZ CambioPorCta;
 |SI nSw = 1; #24#7 = aTitul2; nSwT = 1; |FINSI;

 |SI nSwT = 1;
     |GRABA 020#24a;
 |FINSI;
 |LIBERA #24;

|FPRC;

|DEFBUCLE CiSalant;
 #24#1;
 ;
 "            ";
 "zzzzzzzzzzzz";
 be;
 NULL, CiSalant0;
|FIN;

|| /////////////////////
|PROCESO CaVengca0;

 eaCuenta = #22#4; |HAZ CambioPorCta;
 |SI nSw = 1;
     #22#8 = aTitul2;
     |GRABA 020#22a;
 |FINSI;
 |LIBERA #22;

|FPRC;

|DEFBUCLE CaVengca;
 #22#1;
 ;
 "01.01.0000";
 "31.12.9999";
 be;
 NULL, CaVengca0;
|FIN;

|| /////////////////////
|PROCESO CaMoauxc0;

 eaCuenta = #20#0;  |HAZ CambioPorCta;
 |SI nSw = 1;
     #20#2 = aTitul2;
     |GRABA 020#20a;
 |FINSI;
 |LIBERA #20;

|FPRC;

|DEFBUCLE CaMoauxc;
 #20#1;
 ;
 "  ", aDCuenta;
 "zz", aHCuenta;
 be;
 NULL, CaMoauxc0;
|FIN;

|| /////////////////////
|PROCESO CaManiva0;

 eaCuenta = #19#12;  |HAZ CambioPorCta;
 |SI nSw = 1;
     #19#13 = aTitul2;
     |GRABA 020#19a;
 |FINSI;
 |LIBERA #19;

|FPRC;

|DEFBUCLE CaManiva;
 #19#1;
 ;
 00, 0000000001;
 99, 9999999999;
 be;
 NULL, CaManiva0;
|FIN;

|| /////////////////////
|PROCESO CaInliva0;

 nSwT = 0;
 eaCuenta = #18#5;  |HAZ CambioPorCta;
 |SI nSw = 1;  #18#27 = aTitul2;  nSwT = 1; |FINSI;

 eaCuenta = #18#17;  |HAZ CambioPorCta;
 |SI nSw = 1;  #18#28 = aTitul2;  nSwT = 1; |FINSI;

 eaCuenta = #18#40;  |HAZ CambioPorCta;
 |SI nSw = 1;  #18#42 = aTitul2;  nSwT = 1; |FINSI;

 |SI nSwT = 1;
     |GRABA 020#18a;
 |FINSI;
 |LIBERA #18;

|FPRC;

|DEFBUCLE CaInliva;
 #18#1;
 ;
 00, 001;
 99, 999;
 be;
 NULL, CaInliva0;
|FIN;

|| /////////////////////
|PROCESO CaExistl0;

 nSwT = 0;
 eaCuenta = #17#2;  |HAZ CambioPorCta;
 |SI nSw = 1;  #17#4 = aTitul2;  nSwT = 1; |FINSI;

 eaCuenta = #17#5;  |HAZ CambioPorCta;
 |SI nSw = 1;  #17#7 = aTitul2;  nSwT = 1; |FINSI;

 eaCuenta = #17#8; |HAZ CambioPorCta;
 |SI nSw = 1;  #17#10 = aTitul2;  nSwT = 1; |FINSI;

 |SI nSwT = 1;
     |GRABA 020#17a;
 |FINSI;
 |LIBERA #17;

|FPRC;

|DEFBUCLE CaExistl;
 #17#1;
 ;
 00, 00;
 99, 99;
 be;
 NULL, CaExistl0;
|FIN;

|| /////////////////////
|PROCESO CaCuedep0;

 eaCuenta = #16#0;
 |HAZ CambioPorCta;
 |SI nSw = 1;
     #16#2 = aTitul2;
     |GRABA 020#16a;
 |FINSI;
 |LIBERA #16;

|FPRC;

|DEFBUCLE CaCuedep;
 #16#1;
 ;
 aDCuenta, "  ";
 aHCuenta, "zz";
 be;
 NULL, CaCuedep0;
|FIN;

|| /////////////////////
|PROCESO CaConvec0;
 eaCuenta = #15#2;
 |HAZ CambioPorCta;
 |SI nSw = 1;
     #15#3 = aTitul2;
     |GRABA 020#15a;
 |FINSI;
 |LIBERA #15;
|FPRC;

|DEFBUCLE CaConvec;
 #15#1;
 ;
 "  ";
 "zz";
 be;
 NULL, CaConvec0;
|FIN;

|PROCESO OtroTemp;

 |ABRE #12;    || anm00007
 #12#0 = #99#0;
 |LEE 101#12=;
 |SI FSalida = 0;
     #12#2 = #99#5;
     |GRABA 020#12a;
 |FINSI;
 |LIBERA #12;
 |CIERRA #12;

 |ABRE #14;     || caconcic
 #14#0 = #99#0;
 |LEE 101#14=;
 |SI FSalida = 0;
     #14#2 = #99#5;
     |GRABA 020#14a;
 |FINSI;
 |LIBERA #14;
 |CIERRA #14;

 |ABRE #21;  || CaTesfin
 #21#0 = #99#0;
 |LEE 101#21=;
 |SI FSalida = 0;
     #21#2 = #99#5;
     |GRABA 020#21a;
 |FINSI;
 |LIBERA #21;
 |CIERRA #21;

 |ABRE #23; || CiEntasc
 #23#0 = #99#0;
 |LEE 101#23=;
 |SI FSalida = 0;
     #23#2 = #99#5;
     |GRABA 020#23a;
 |FINSI;
 |LIBERA #23;
 |CIERRA #23;

|FPRC;

|DEFBUCLE OtroTemp;
 #99#1;
 ;
 "            ", 0;
 "zzzzzzzzzzzz", 0;
 ;
 NULL, OtroTemp;
|FIN;

|PROCESO ElRestoCambio;

  aAlfa6 = "Otros Ficheros";

  |BUCLE OtroTemp;

  aAlfa6 = "Ventas Automaticas:";
  |BUCLE CaConvec;

  aAlfa6 = "Cuentas por Departamentos:";
  |BUCLE CaCuedep;

  aAlfa6 = "Existencias:";
  |BUCLE CaExistl;

  aAlfa6 = "Registro IVA:";
  |BUCLE CaInliva;

  aAlfa6 = "Registro IVA/IRPF:";
  |BUCLE CaManiva;

  aAlfa6 = "Inventario: ";
  |BUCLE CaMoauxc;

  aAlfa6 = "Ventas Agrupadas:";
  |BUCLE CaVengca;

  aAlfa6 = "Apuntes Cuentas:";
  |BUCLE CiSalant;

  aAlfa6 = "Operaciones Financieras:";
  |BUCLE CaFinanc;

  aAlfa6 = "Contratos de Arrendamiento: ";
  |BUCLE CaArm001;
|FPRC;

|| /////////////////////////////////////////////////////////////////
||--------------------------------------------------------------------
|PROCESO camclipro;
   aAlfa1 = #11#0; aAlfa1 = aAlfa1 % 101;
   |SI aAlfa1 = "6"; |O aAlfa1 = "7";  |ACABA;  |FINSI;

   |ABRE #13;
   #13#23 = "C";
   #13#10 = #11#0;
   #13#11 = #11#1;
   |LEE 101#13=;
   |SI FSalida = 0;
       #13#1 = #11#2;
       |GRABA 020#13a;
   |SINO;
       #13#23 = "P";
       #13#10 = #11#0;
       #13#11 = #11#1;
       |LEE 101#13=;
       |SI FSalida = 0;
           #13#1 = #11#2;
           |GRABA 020#13a;
       |FINSI;
   |FINSI;
   |LIBERA #13;
   |CIERRA #13;
|FPRC;

|PROCESO CaCuenta0;

 eaCuenta = #11#0; |HAZ SacaNivel;
 #11#1 = enNivel;
 |SI #11#1 < nDDig; |O #11#1 > nHDig; |ACABA; |FINSI;

 aTitul1 = #11#2;
 |HAZ CambioTitulo;
 |SI nSw = 1;
     nSwAcum = 0;
     eaMov   = "NO";
     |PARA nNume1 = 9; |SI nNume1 [ 53; |HACIENDO nNume1 = nNume1 + 1;
           |SI #11nNume1 ! 0;
                nSwAcum = 1;
                |SAL;
           |FINSI;
     |SIGUE;
     |SI nSwAcum = 0; |Y #11#3 = "S";
         |ABRE #10;
         |SELECT #3#10;
         #10#4 = #11#0;
         #10#5 = #11#1;
         #10#0 = 0;
         #10#1 = "01.01.0000";
         #10#2 = 0;
         |LEE 000#10];
         |SI FSalida = 0; |Y #10#4 = #11#0;
             nSwAcum = 1;
         |FINSI;
         |SELECT #1#10;
         |CIERRA #10;
     |FINSI;

     eaMov = "SI"; |SI nSwAcum = 0; eaMov = "NO"; |FINSI;
     |SI nSwAcum = 1; |Y aMov = "S"; nSwAcum = 0; |FINSI;

     |SI nSwAcum = 0;
         #11#2 = aTitul2;
         |GRABA 020#11a;

         |HAZ camclipro;
         enContar = enContar + 1;
         |SI aInf = "S"; |PRINT; |FINSI;

         |SI #11#3 = "S";
             |ABRE #99;
             #99#0 = #11#0;
             #99#1 = 0;
             |LEE 001#99=;
             |SI FSalida ! 0;
                 |DDEFECTO #99;
                 #99#0 = #11#0;
                 #99#1 = 0;
                 |GRABA 020#99c;
             |FINSI;
             #99#5 = #11#2;
             |GRABA 020#99a;
             nPaseS = 1;
             |CIERRA #99;
         |FINSI;

     |FINSI;
 |FINSI;

 |LIBERA #11;
|FPRC;

|DEFBUCLE CaCuenta;
   #11#1;
   3;
   aDCuenta, aDDir;
   aHCuenta, aHDir;
   be;
   NULL, CaCuenta0;
|FIN;

||**************************************************************************
||**************************************************************************
|PROCESO camcta;

     aCampo0 = #0#0; |QBF aCampo0;

     |SI aCampo0 ! "";

         aAlfa1   = aCampo0 % 0;
         nLonCam0 = aAlfa1;
         aCampo0M = aCampo0;
         |SI #0#2 = "S"; aCampo0M = aCampo0M > " "; |FINSI;

         aCampo1  = #0#1; |QBF aCampo1;
         aCampo1M = aCampo1 > " ";
         aCampo1m = aCampo1 < " ";
         aCampo1x = (aCampo1M % 101) + (aCampo1m % 235);

         aAlfa6 = "Cuentas:";
         |BUCLE CaCuenta;

         |SI nSwConiso = 0;  |HAZ CaConiso;  |FINSI;
    |FINSI;
    |SI aCampo0 = "=";
        |SI aDCuenta = "            ";
            nPaseS = 1;
            enContar = enContar + 1;
            |SI nSwConiso = 0;  |HAZ CaConiso;  |FINSI;
        |FINSI;
    |FINSI;
|FPRC;

|DEFBUCLE leeTemporal;
 #0#1;
 ;
 0001;
 9999;
 e;
 NULL, camcta;
|FIN;

|PROCESO HazTodo;
  |DELINDEX #99;

  enContar = 0;
  nPaseS   = 0;
  |PINTA 2303, "En Proceso: ";
  |BUCLE leeTemporal;

  |SI enContar ! 0;
      |SI aInf = "S"; |PIEINF; |FINSI;
      |SI nPaseS = 1;
          |HAZ ElRestoCambio;
      |FINSI;
  |FINSI;

  nSwConiso = 1;
|FPRC;

|PROCESO elCambio;

   eaTitulo = "RELACION DE CUENTAS MODIFICAS";

   aFichero = ("14z" + Usuario) % 108;
   |NOME_DAT #99 aFichero;

   |SI aInf = "S";
       |IMPRE 0;
       |SI FSalida ! 0; |ACABA; |FINSI;

       |INFOR informe;
       |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;
   |FINSI;

   |C_V #0#3,1,"S";
   |SI #48#27 = "S";
       |HAZ MultiEmpresa;
   |SINO;
       |DFICE dirfich0; |QBF dirfich0;
       |HAZ HazTodo;
   |FINSI;

   |SI aInf = "S"; |FINIMP; |FINSI;
   |DELINDEX #0;
   |DELINDEX #99;

|FPRC;

|| ********************************************************************
|| ********************************************************************
|RUTINA mix;
 #0#4 = 1;
|FRUT;

|RUTINA max;
 #0#4 = 9999;
|FRUT;

|PROCESO LosCampos;
 |C_M #0#0,1, aAlfa2;  |C_V #0#0,1,aAlfa2;
 |C_M #0#1,1, aAlfa2;  |C_V #0#1,1,aAlfa2;
 |C_M #0#2,1, aAlfa2;  |C_V #0#2,1,aAlfa2;
                       |C_V #0#4,1,aAlfa2;
 |C_M #0#5,1, aAlfa1;  |C_V #0#5,1,aAlfa1;
 |C_M #0#6,1, aAlfa1;  |C_V #0#6,1,aAlfa1;
 |C_M #0#7,1, aAlfa1;  |C_V #0#7,1,aAlfa1;
 |C_M #0#8,1, aAlfa1;  |C_V #0#8,1,aAlfa1;
 |C_M #0#9,1, aAlfa1;  |C_V #0#9,1,aAlfa1;
 |C_M #0#10,1, aAlfa1; |C_V #0#10,1,aAlfa1;
 |C_M #0#11,1, aAlfa1; |C_V #0#11,1,aAlfa1;
|FPRC;

|PROCESO Elregistro0;

 nSalir = 0;
 |ENTLINEAL #1#0, -1, 7, 21, 0, mix, max;

 |ABRE #0;
 #0#4 = 0;
 |LEE 101#0=;
 |SI FSalida ! 0;
     |DDEFECTO #0;
     #0#4 = 0;
     #0#9 = dig3;
     |GRABA 020#0b;
 |FINSI;

 aAlfa1 = "S"; aAlfa2 = "N";
 |HAZ LosCampos;

 |PINDA #0#0;
 |ENDATOS #2#0;
 |SI FSalida ! 0;
     nSalir = 1;
     |LIBERA #0;
     |CIERRA #0;
     |ACABA;
 |FINSI;

 aAlfa1 = "N"; aAlfa2 = "S";
 |HAZ LosCampos;
 |GRABA 020#0a;
 |LIBERA #0;
 |CIERRA #0;

 aDCuenta = #0#5; aHCuenta = #0#6;
 nDDig    = #0#8; nHDig    = #0#9;
 aMov     = #0#7;
 aDDir    = #0#10; aHDir   = #0#10;
 |SI #0#10 = "T";  aDDir = " "; aHDir = "z"; |FINSI;
 aInf     = #0#11;
|FPRC;

|PROGRAMA;

 |HAZ inicio;
 |CLS;
 |CABEZA "Cambio Titulo Cuentas";
 |PINPA #0#0;

 |HAZ Elregistro0;
 |SI nSalir = 1;
     |ACABA;
 |FINSI;

 |ENTLINEAL #1#0, -1, 1, 21, 0, mix, max;
 |SI FSalida = 0;
     |CONFI "1234SRealizar ahora el cambio S/N";
     |SI FSalida = 0;
         |HAZ elCambio;
     |FINSI;
 |FINSI;
|FPRO;

||*********************************************************************

|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #10 dirfich0;
   |PATH_DAT #11 dirfich0;
   |PATH_DAT #12 dirfich0;
   |PATH_DAT #13 dirfich0;
   |PATH_DAT #14 dirfich0;
   |PATH_DAT #15 dirfich0;
   |PATH_DAT #16 dirfich0;
   |PATH_DAT #17 dirfich0;
   |PATH_DAT #18 dirfich0;
   |PATH_DAT #19 dirfich0;
   |PATH_DAT #20 dirfich0;
   |PATH_DAT #21 dirfich0;
   |PATH_DAT #22 dirfich0;
   |PATH_DAT #23 dirfich0;
   |PATH_DAT #24 dirfich0;

   |ATRI R; |PINTA 2203, "Empresa: ";      |ATRI 0;
   |PINTA 2212, #30#2; |PINTA 2218, #30#1; |ATRI 0;

   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 000001;
 999999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;
