|FICHEROS;
  caparpen #0;
  cacuenta #1;

  camaasil #2; ||Asientos.
  reflejo@ #3;

  canempre #30;
  caselem0 #997;
  caselem1 #998;
|FIN;

|VARIABLES;
  &entrada = 1;
  nSalir = 0;
  informe  = "caparpen";

  aAlfa1 = "";
  aAlfa2 = "";
  aAlfa3 = "";
  nNume1 = 0;

  aDef     = "";
  aDSigno  = "";
  aHSigno  = "";
  aSigno   = "";
  nImpBus  = 0;
  nImporte = 0;
  nDImport = 0;
  nHImport = 0;

  swHay   = 0;
  swVale  = 0;
  swImp   = 0;
  &swInf  = 0;
  &enImD  = 0;
  &enImH  = 0;

  nEmpMulti = 0;
  &eSwMul   = 0;
  &espe15   = "";
  &espe16   = 0;
  nSwOpera  = 0;
  nSw       = 0;
  aFichero  = "";
  nPara1    = 0;

  CodigoEmpresa  = 0;
  PeriodoEmpresa = 0;
  dirfichOri     = "";
  nSitua         = 0;
|FIN;

|INCLUYE dscont_i;

|| **********************************
|| Procesos de Pantalla
|| **********************************

|PROCESO defectos;
  aAlfa1 = Contenido;
  |SI aAlfa1 ! #0#31;
      |SI #0#31 = "N";
          #0#2  = fec1;
      |SINO;
          #0#2  = "01.01.2000";
      |FINSI;
      |PINTA #0#2;
      #0#3 = fec2; |PINTA #0#3;
  |FINSI;
|FPRC;

|CALCULO cuenta; |TIPO 6;
     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;
|FCAL;

|CALCULO cuefin; |TIPO 0;
   emCta    = 1;
   eaCuenta = #0#0;
   |HAZ SacaNivel;
   |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea

   |HAZ LeeCuenta;
   |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea

   aAlfa1 = #cacuenta#5;
   |SI aAlfa1 = "A"; aAlfa1 = "D"; |FINSI;
   |SI aAlfa1 = "P"; aAlfa1 = "H"; |FINSI;
   #0#4 = aAlfa1; |PINTA #0#4;
|FCAL;

|CALCULO fechas; |TIPO 0;
  |SI #0#31 = "N";
      |SI #0Campo > fec2;|O #0Campo < fec1;
          |MENAV "    Fecha fuera de Periodo ...";
          |ERROR;
          |ACABA;
      |FINSI;
  |FINSI;
  |SI Campo = 3; |Y #0#2 > #0#3;
      |ERROR;
  |FINSI;
|FCAL;

|CALCULO nodepar; |TIPO 7;
  |C_M #0#7,1,eaDepar;
  |C_M #0#8,1,eaDepar;
|FCAL;

|CALCULO nosubde; |TIPO 7;
  |C_M #0#9, 1,eaSubde;
  |C_M #0#10,1,eaSubde;
|FCAL;

|| ************************************************************************
|| ====================================== PROCESO INFORME
|PROCESO Imprimir;

 |SI #0#11 = "S"; |Y #3#18 = " "; |ACABA; |FINSI;
 |SI #0#11 = "N"; |Y #3#18 ! " "; |ACABA; |FINSI;

 |SI #3#4 = #0#0; |ACABA; |FINSI;  || Es la misma cuenta

||poner campos para imprimir
  |PDEFECTO #0, 15,28;

  |PARA nNume1 = 1; |SI nNume1 [ #0#6; |HACIENDO nNume1 = nNume1 + 1;
       nImporte = nImpBus * nNume1;
       nDImport = nImporte - #0#5;
       nHImport = nImporte + #0#5;

       |SI #3#9 ] nDImport;  |Y #3#9 [ nHImport;
           #0#15 = #3#0;         ||Diario
           #0#16 = #3#1;         ||Fecha
           #0#17 = #3#2;         ||Documento
           #0#18 = #3#3;         ||Apunte
           #0#19 = #3#4;         ||Cuenta
           emCta    = 0;
           eaCuenta = #3#4; |HAZ LeeCuenta;
           #0#20 = #cacuenta#2;  ||Nombre Cuenta
           #0#21 = #3#6;         ||Concepto
           #0#22 = #3#7;         ||Descripcion
           #0#23 = #3#21;        ||Departamento
           #0#24 = #3#28;        ||Subdepartamento
           |SI #3#8 = "D";
               #0#25 = #3#9;
           |SINO;
               #0#26 = #3#9;
           |FINSI;
           |SI #3#18 ! " "; #0#27 = "S"; |FINSI;
           |SI #3#18 = " "; #0#27 = "N"; |FINSI;

           |SI swVale = 0;|Y #0#30 = "N";
               swInf = 0;
               |PRINT; swImp  = 1;
           |FINSI;
           swVale = 1;
           swInf = 1;
           |PRINT; swImp  = 1;
           |SAL;
       |FINSI;
 |SIGUE;

|FPRC;

|DEFBUCLE LeeEspejo;
 #3#1004;
 4,8;
 #0#28, #0#2, 000000, 0001, #0#12, aSigno;
 #0#29, #0#3, 999999, 9999, #0#13, aSigno;
 ;
 NULL,Imprimir;
|FIN;

|| ..................................
|PROCESO LeeExtracto0;

 |SI Haybasica = 0
     |SI #2#21 < #0#7; |O #2#21 > #0#8;   |ACABA;  |FINSI;
 |SINO;
     nDepar = #2#21;
     |SI nDepar < #0#7; |O nDepar > #0#8;  ||desde/hasta actividad
         |ACABA;
     |FINSI;
 |FINSI;
 |SI #0#11 = "S"; |Y #2#18 = " "; |ACABA; |FINSI;
 |SI #0#11 = "N"; |Y #2#18 ! " "; |ACABA; |FINSI;

 |SI #2#8 = "D"; enImD = #2#9; enImH = 0; aSigno = "H"; |FINSI;
 |SI #2#8 = "H"; enImH = #2#9; enImD = 0; aSigno = "D"; |FINSI;
 nImpBus = #2#9;


 swVale = 0;
|SI #0#30 = "S";
    swInf  = 0;
    |PRINT; swImp  = 1;
|FINSI;

 |BUCLE LeeEspejo;
 |SI swVale = 0; |Y #0#30 = "S";
     swInf  = 2;
     |PRINT; swImp  = 1;
     swInf = 3;  ||Linea en Blanco
     |PRINT; swImp  = 1;
 |FINSI;
 |SI swVale = 1;
     swInf = 3;  ||Linea en Blanco
     |PRINT; swImp  = 1;
 |FINSI;
|FPRC;

|DEFBUCLE LeeExtracto;
 #2#3;
 0, 8, 28;
 #0#0, #0#2, 000000, 0001, #0#28, aDSigno, #0#9;
 #0#0, #0#3, 999999, 9999, #0#29, aHSigno, #0#10;
 ;
 NULL,LeeExtracto0;
|FIN;

|| ......................

|PROCESO HazTodo;

 |SI eSwMul ! 0; enEjercicio = #caselem0#4; |FINSI;

|| Lee otra vez la cuenta porque es proceso multiempresa
 |ABRE #1;
 #1#0 = #0#0; || Cuenta Contable
 |LEE 010#1=;
 |SI FSalida ! 0;
     |CIERRA #1;
     |ACABA;
 |FINSI;
 #0#1 = #1#2;
 |CIERRA #1;

 |SI nEmpMulti = -1; |O eSwMul = 0;
     swImp = 0;
     nEmpMulti = #30#2;
 |FINSI;
 |SI eSwMul ! 0; |Y nEmpMulti ! #30#2;
     |SI swImp ! 0;
         |PIEINF;
     |FINSI;
     swImp = 0;
 |FINSI;

 |BUCLE LeeExtracto;
 |SI eSwMul = 0;
      |SI swImp ! 0;
          |PIEINF;
      |FINSI;
 |FINSI;
|FPRC;

|| **********************************************************************
|CALCULO engancha; |TIPO 3;

 |IMPRE 0;
 |SI FSalida ! 0; |ACABA; |FINSI;

 |INFOR informe;
 |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

 |SI Haybasica = 1;
     |SI #0#8 = "zz"; #0#8 = "99"; |FINSI;
 |FINSI;
 aDSigno = #0#4; aHSigno = #0#4;
 |SI #0#4 = "T"; aDSigno = "D"; aHSigno = "H"; |FINSI;

 aLosDefs = aPContag + "def/";
 aDef     = aLosDefs + "camaasil.mas";
 |CARGA_DEF aDef, reflejo@;
 |SI FSalida ! 0;
     aAlfa1 = "     Error en cargar el Def camaasil.mas";
     |MENAV aAlfa1;
     |ERROR;
 |FINSI;

 nSitua = 0;
 eSwMul = 0;
 |SI #0#31 = "N";
     |SI #48#27 = "S";
         |HAZ MultiEmpresa;
     |SINO;
         |DFICE dirfich0; |QBF dirfich0;
         |HAZ HazTodo;
     |FINSI;
 |SINO;
     eSwMul = 1;
     |SI #48#27 = "S";  eSwMul = 2; |FINSI;
     |HAZ MultiEjercicio;
 |FINSI;

 |SI nSitua = 1;
    cod1 = CodigoEmpresa;
    cod2 = PeriodoEmpresa;
    emEmp = 1;  || Sin mensaje
    |HAZ leelo;
    |PATH_DAT #1 dirfichOri;
    |PATH_DAT #2 dirfichOri;
    |PATH_DAT #3 dirfichOri;
 |FINSI;
 |DESCARGA_DEF #reflejo@;

 |FININF;
 |FINIMP;

|FCAL;
|| **********************************************************************

|PROCESO MultiEmp1;
   nSitua = 1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;
   |SI #0#2 > #caselem1#5; |O #0#3 < #caselem1#4;
       |ACABA;  || Fuera de la seleccion
   |FINSI;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #2 dirfich0;
   |SI #0#31 = "N";
       |PATH_DAT #3 dirfich0;
   |FINSI;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 000001;
 999999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;

|PROCESO PasoAuxiliar;
  |SI #0#11 = "S"; |Y #2#18 = " "; |ACABA; |FINSI;
  |SI #0#11 = "N"; |Y #2#18 ! " "; |ACABA; |FINSI;

  |SI #2#4 = #0#0; |ACABA; |FINSI;  || Es la misma cuenta

  |PARA nPara1 = 0; |SI nPara1 [ 30; |HACIENDO nPara1 = nPara1 + 1;
        #3nPara1 = #2nPara1;
  |SIGUE;
  |GRABA 000#3n;
  nSw = 1;
|FPRC;

|DEFBUCLE PasoAuxiliar;
   #2#1;
   4;
   #0#28, #0#2, 000000, 0001, #0#12;
   #0#29, #0#3, 999999, 9999, #0#13;
   ;
   NULL, PasoAuxiliar;
|FIN;

|PROCESO MultiEmpEjer1;

   |SI #0#2 > #caselem1#5; |O #0#3 < #caselem1#4;
       |ACABA;  || Fuera de la seleccion
   |FINSI;

   |SI nSwOpera = 1;

       |HAZ MultiEmp1;

   |SINO;
       dirfich0 = #caselem1#3; |QBF dirfich0;

       |PATH_DAT #2 dirfich0;

       |BUCLE PasoAuxiliar;
       nSw = 1;
   |FINSI;
|FPRC;

|DEFBUCLE MultiEmpEjer;
   #caselem1#2;
   ;
   #997#0, "01.01.0000";
   #997#0, "31.12.2999";
   ;
   NULL,MultiEmpEjer1;
|FIN;

|PROCESO MultiEjer1;
   nSw = 0;
   nSwOpera = 0;
   |DELINDEX #3;
   |ABRE #3;
   |BUCLE MultiEmpEjer;
   |CIERRA #3;

   nEmpMulti = -1;
   |SI nSw ! 0;
       nSwOpera = 1;
       |BUCLE MultiEmpEjer;
       |SI swImp ! 0;
           |PIEINF;
       |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE MultiEjer0;
   #caselem0#1;
   ;
   00001;
   99999;
   ;
   NULL,MultiEjer1;
|FIN;

|PROCESO MultiEjercicio;

   espe15 = "S";

   |SUB_EJECUTA "caselemp";
   |NOME_DAT #998 eaFichsel;
   |NOME_DAT #997 eaFichres;

   aFichero = "1r" + Usuario;
   |NOME_DAT #3 aFichero;

   |BUCLE MultiEjer0;

   |DELINDEX #998;
   |DELINDEX #997;
   |DELINDEX #3;
|FPRC;

|PROGRAMA;
 |CLS;
 |CABEZA "Partidas Ptes. Astos";
 |HAZ inicio;
 |HAZ DirAplicSt;

 CodigoEmpresa    = enEmpresa;
 PeriodoEmpresa   = enPeriodo;
 |DFICE dirfichOri;

 |DDEFECTO #0;

 #0#2 = fec1;
 #0#3 = fec2;

 nSalir = 0;
 |PARA; |SI nSalir = 0; |HACIENDO;
        |PINPA #0#0;
        |PINDA #0#0;                    || Pinta los datos por defecto
        |ENDATOS #1#0;                  || Seleccion informe
        |SI FSalida ! 0;
            nSalir = 1;
        |FINSI;
 |SIGUE;
|FPRO;
