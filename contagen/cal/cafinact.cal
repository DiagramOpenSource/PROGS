|FICHEROS;
   cafinact #00;
   camaasic #02;
   camaasil #03;
   cacuenta #05;
   camautoc #06;
   camautol #07;
   catrasi1 #09;
   camandia #10;

   cafinanc #11;
   cafinanl #12;
   caconasi #20;

   canempre #30;
   caselem1 #998;

  caivaasi #200;

  camaniva #204;
  caivalin #205;
  calibros #206;
  caivases #207;
  caclipro #208;

  caivatm4;
|FIN;

|VARIABLES;
   &entrada = 1;
   aAlfa1 = "";
   aAlfa2 = "";
   aAlfa3 = "";
   aAlfa4 = "";
   nNume1 = 0;
   nNume2 = 0;
   nNume3 = 0;
   nNume4 = 0;
   nIva0  = 0;
   nIva1  = 0;
   nIva2  = 0;
   fFech1 = @;
   fFech2 = @;
   fFechaAsto = @;
   nPara1 = 0;


   diar = 0;
   fech = @;
   asie = 0;
   apun = 0;
   cuen = "";
   digi = 0;
   sign = "";
   ptas = 0;
   conc = 0;
   desc = "";

   sw = 0;
   estado = 0;

   dpar = "";
   subd = "";
   swpr = 0;
   documen = 0;
   escon = 0;
   inkey = 0;
   swsel = 0;
   swContar = 0;
   swIVA = 0;

   nLibro = 0;
   nPerio = 0;
   NumDoc = 0;
   NumFactura = 0;
   eaTitCuenta = "";
   aSerie = "";
   enQSerie = 0;
   aParam  = "";
   nParam  = 0;
   SwAsiento = 0;
   nAlguno   = 0;

   &antdia  = 0;  ||Variables para el Informe de posibles errores
   &antfec  = @;
   &antdoc  = 0;
   &antapu  = 0;
   &lerror  = 0;
   &men     = "";
   informe = "cacheapu";
   swImpresora = 0;

   aDcon = "";
   aHcon = "";
   nAlgun = 0;
   nNoMulti = 0;
   nIncrApte = 0;
   aElSigno  = "";
   &eaNomFich = "";
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoan_i;

|| ************************************************************************
|| *********** CALCULOS PANTALLA ******************************************
|| ************************************************************************
|CALCULO fTipo8; |TIPO 8;
  aParam = PARAMETRO; |QBT aParam;
  nParam = aParam;
  |HAZ inicio;
|FCAL;

|CALCULO elTipo7; |TIPO 7;
 #0#15 = fec1; |PINTA #0#15;
 #0#16 = fec2; |PINTA #0#16;
 |SI nParam ! 0;
     #0#3 = nParam;
     |HAZ LeeAsto;
     |SI SwAsiento = 0;
         |C_M #0#3,1,"N"; |PINTA #0#3;
         |PINTA #0#4; |PINTA #0#5; |PINTA #0#6;
     |FINSI;
 |FINSI;
|FCAL;

|CALCULO LeeAsto; |TIPO 0;
 SwAsiento = 0;
 |ABRE #6;
 #6#0 = #0#3;
 |LEE 001#6=;
 |SI FSalida ! 0;
     SwAsiento = 1;
 |SINO;
    |SI #6#12 = "N";
        SwAsiento = 2;
    |SINO;
        #0#4 = #6#1;
        #0#5 = #6#11;
        #0#6 = #6#14;
    |FINSI;
 |FINSI;
 |CIERRA #6;
|FCAL;

|CALCULO LeeAsiento; |TIPO 0;
 |C_M #0#3,0,aAlfa1;
 |SI aAlfa1 = "N"; |ACABA; |FINSI;

 |HAZ LeeAsto;
 |SI SwAsiento ! 0;
     |SI SwAsiento = 1;
         |MENAV "    Error, Asiento Fijo no existe ";
     |SINO;
         |MENAV "    Error, este Asiento Fijo no es de Operacion Financiera";
     |FINSI;
     |ERROR;
 |FINSI;
|FCAL;

|RUTINA inffinact2;
   #11#0 = enEmpresa;
   #11#1 = 00001;
|FRUT;

|RUTINA supfinact2;
   #11#0 = enEmpresa;
   #11#1 = 99999;
|FRUT;

|CALCULO laOpera; |TIPO 0;
   |SI FSalida = 10;
      |ABRE #11;
      |CONSULTA_F_CLAVE #1#11,inffinact2,supfinact2;
      #0Campo = #11#1; |PINTA #0Campo;
      |ERROR;
      |CIERRA #11;
   |FINSI;
|FCAL;

|CALCULO antesfecha; |TIPO 7;
 |C_V #0#7,1, "S";
 |SI #0#6 = "P";
     |C_M #0#7,1, "S";
 |SINO;
     |C_M #0#7,1, "N";
     |SI #0#6 = "F";
         aAlfa1 = #6#6; |QBF aAlfa1;
         aAlfa1 =( "0000" + aAlfa1) % -104;
         aAlfa1 = (aAlfa1 % 102) + "." + (aAlfa1 % -102);
         aAlfa2 = enEjercicio; |QBF aAlfa2;
         aAlfa3 = aAlfa1 + "." + aAlfa2;
         #0#7 = aAlfa3; |PINTA #0#7;
     |SINO;
         |C_V #0#7,1, "N"; |ATRI P;
         |SI #0#6 = "C"; |PINTA 1763, " CONTRATO   "; |FINSI;
         |SI #0#6 = "L"; |PINTA 1763, " LINEA      "; |FINSI;
         |ATRI 0;
     |FINSI;
 |FINSI;
|FCAL;

|CALCULO limite; |TIPO 0;
   |C_M #0#7,0,aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   |SI #0Campo < fec1; |O #0Campo > fec2;
      |MENAV "    ESTA FECHA NO CORRESPONDE AL EJERCICIO";
      |ERROR;
   |SINO;
      |SI #0Campo [ fec3;
          |MENAV "    ERROR FECHA INFERIOR O IGUAL AL BLOQUEO DE DATOS";
          |ERROR;
      |FINSI;
   |FINSI;
|FCAL;

|| ///////////////////////////////////////////////////////////////////////
|| ///////////////////////////////////////////////////////////////////////
|PROCESO LineasFin0;
  |SI #6#13 = "S";
      |SI #7#11 = "I"; #7#13 = #12#7; |FINSI;
  |FINSI;
  |SI #7#10 = 21;  ptas = ptas + #12#4; |FINSI;
  |SI #7#10 = 22;  ptas = ptas + #12#5; |FINSI;
  |SI #7#10 = 23;  ptas = ptas + #12#8; |FINSI;
  |SI #7#10 = 24;  ptas = ptas + #12#6; |FINSI;
  |SI #7#10 = 25;  ptas = ptas + #12#9; |FINSI;
  |SI #7#10 = 26; |O #7#10 = 27;
      nIva1 = 0; nIva2 = 0;
      nIva1 = (#12#4 % #12#7) ! EURODB;
      nIva2 = (#12#5 % #12#7) ! EURODB;
      nIva0 = (#12#8 - (nIva1 + nIva2)) ! EURODB;
      |SI nIva0 ! 0; nIva1 = nIva1 + nIva0; |FINSI;
      |SI #7#10 = 26; ptas = nIva1; |FINSI;
      |SI #7#10 = 27; ptas = nIva2; |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE LineasFin;
 #12#1;
 3,11;
 #11#0, #11#1, 001, #0#15, aDcon;
 #11#0, #11#1, 999, #0#16, aHcon;
 e;
 NULL, LineasFin0;
|FIN;

|PROCESO CalImporte;
|| //// Importes de Cabecera \\\\\\\\\\
  |SI #7#10 = 1;  ptas = #11#22; |FINSI;
  |SI #7#10 = 2;  ptas = #11#23; |FINSI;
  |SI #7#10 = 3;  ptas = #11#24; |FINSI;
  |SI #7#10 = 4;  ptas = #11#25; |FINSI;
  |SI #7#10 = 5;  ptas = #11#26; |FINSI;
  |SI #7#10 = 6;  ptas = #11#27; |FINSI;
  |SI #7#10 = 7;  ptas = #11#28; |FINSI;
  |SI #7#10 = 8;  ptas = #11#29; |FINSI;
  |SI #7#10 = 9;  ptas = #11#30; |FINSI;
  |SI #7#10 = 10; ptas = #11#31; |FINSI;
  |SI #7#10 = 11; ptas = #11#38; |FINSI;

|| //// Importes de Cabecera \\\\\\\\\\
  |SI #7#10 ] 21;
      |SI #6#14 ! "L";
          |BUCLE LineasFin;
      |SINO;
          ptas = 0; |HAZ LineasFin0;
      |FINSI;
  |FINSI;

  aMonedaC = #11#40;  ||Moneda de la Operacion Financiera
  |SI aMonedaA ! aMonedaC;
      impMoneda = ptas; |HAZ ElCambioMoneda; ptas = impMoneda;
  |FINSI;
|FPRC;

|PROCESO leecta;
   sign = "D";
   ptas = 0;
   |ABRE #5;
   #5#0 = cuen;
   |LEE 001#5=;
   |SI FSalida ! 0;
       |DDEFECTO #5;
   |FINSI;
   |SI #5#53 < 0; sign = "D"; ptas = - #5#53; |FINSI;
   |SI #5#53 ] 0; sign = "H"; ptas =   #5#53; |FINSI;
   |CIERRA #5;
|FPRC;

|PROCESO leelineas;
  eaCtaAna = "";
  ptas = 0;
  cuen  = #7#2;
  aAlfa1 = cuen; |QBF aAlfa1;
  aAlfa2 = aAlfa1 % 101;
  |SI aAlfa2 = "C";
      aAlfa3 = aAlfa1 % 201; nNume1 = aAlfa3
      |SI nNume1 = 1; cuen = #11#35; eaCtaAna = #11#47; |FINSI;
      |SI nNume1 = 2; cuen = #11#7;  eaCtaAna = #11#48; |FINSI;
      |SI nNume1 = 3; cuen = #11#8;  eaCtaAna = #11#49; |FINSI;
      |SI nNume1 = 4; cuen = #11#9;  eaCtaAna = #11#50; |FINSI;
      |SI nNume1 = 5; cuen = #11#10; eaCtaAna = #11#51; |FINSI;
      |SI nNume1 = 6; cuen = #11#11; eaCtaAna = #11#53; |FINSI;
      |SI nNume1 = 7; cuen = #11#41; eaCtaAna = #11#52; |FINSI;
  |SINO;
       |SI enSwAna = 1;
          eaCuenta = cuen;
          aAlfa1 = #7#14; |QBF aAlfa1;
          |SI aAlfa1 = "*TABLA";
              |HAZ LeeTablaAna;
          |SINO;
              |SI aAlfa1 ! "*REGIS";
                  eaCtaAna = #7#14;
              |FINSI;
          |FINSI;
      |FINSI;
  |FINSI;

  eaCuenta = cuen; |HAZ SacaNivel;
  digi     = enNivel;
  conc     = #7#4;
  aAlfa2   = #7#5; |QBF aAlfa2;
  aAlfa1   = #0#8; aAlfa1 = " " + aAlfa1;
  desc     = aAlfa2 + aAlfa1;
  dpar     = #11#33; |QBF dpar;
  dpar     = ( "00" + dpar) % -102;
  |SI #6#22 = "N"; dpar = #7#6;     |FINSI;
  |SI #6#22 = "S"; dpar = eaDfDepC; |FINSI;
  subd     = #7#9;
  |SI #6#23 = "N"; subd = #7#9;     |FINSI;
  |SI #6#23 = "S"; subd = eaDfSubC; |FINSI;

  aElSigno = #7#15;

  |SI #7#7 = "S";    ||saldo de la cuenta
      |HAZ leecta;
  |SINO;
      |SI #7#7 = "C";  || Cuadre de Asiento
          |SI #0#14 ] 0; sign = "H"; ptas = #0#14;   |FINSI;
          |SI #0#14 < 0; sign = "D"; ptas = - #0#14; |FINSI;
      |SINO;
          sign = #7#7;     ||debe o haber
          |SI #7#10 = 0;   ||Importe del asiento Fijo
              ptas = #7#8;
          |SINO;
             |HAZ CalImporte;  || Calcula importe Op. Financiero
          |FINSI;
      |FINSI;
  |FINSI;

   |SI #7#7 = "S"; |O #7#7 = "C";
       |SI aElSigno = "+"; |Y sign = "H"; ptas = 0; |FINSI;
       |SI aElSigno = "-"; |Y sign = "D"; ptas = 0; |FINSI;
   |FINSI;

  |SI ptas ! 0;
      apun  = apun + nIncrApte;
      |HAZ lineas;
      sw   = 1;

      |SI sign = "D"; #0#12 = #0#12 + ptas; |FINSI;
      |SI sign = "H"; #0#13 = #0#13 + ptas; |FINSI;
      #0#14 = #0#12 - #0#13;
  |FINSI;
|FPRC;

|PROCESO contab0;      || controla la grabacion de apuntes
  #0#10 = fFechaAsto;
  #0#11 = documen;
  #0#12 = 0;  #0#13 = 0; #0#14 = 0;

  |ATRI 0; |CUADRO 21132370;
  |PINTA 2214, " Pasando Operacion:                                ";
  |PINTA 2234, #11#1;
  |PINTA 2240, #11#2;

  |ABRE #9;
  |DDEFECTO #9;
  apun = 1 - nIncrApte;
  sw = 0;
  |BUCLELIN 2leelineas#6;  || Lee las Lineas del asiento fijo
  |CIERRA #9;
  |SI sw = 1;  |HAZ pasa_asto; |FINSI;
  |DELINDEX #9;
|FPRC;

|| ************************************************************************
|| Proceso para Asientos Lineales o Mensuales
|| ************************************************************************
|PROCESO contab2;
  #11#0 = #12#0;   || Lee Cabecera
  #11#1 = #12#1;
  |LEE 101#11=;
  |SI FSalida ! 0;  |ACABA; |FINSI;
  |SI #11#3 ! #0#2; |ACABA; |FINSI;

  fFechaAsto = #12#3;  || Fecha de la Linea de la Operacion
  |SI #11#54 = "I";   ||Elemento inactivo
      |SI #11#55 > "01.01.1900"; |Y #11#55 [ fFechaAsto;
          |ACABA;
      |FINSI;
  |FINSI;
  |SI fFechaAsto < fec1; |O fFechaAsto > fec2;  |ACABA;  |FINSI;
  |SI fFechaAsto [ fec3;                        |ACABA;  |FINSI;  || fecha de bloqueo

  |HAZ contab0;
  |LIBERA #11;
  #12#11 = "S";
  |GRABA 020#12a;
  |LIBERA #12;
|FPRC;

|DEFBUCLE cafinact1;
   #12#1;
   3,11;
   enEmpresa, #0#0, 001, #0#15, aDcon;
   enEmpresa, #0#1, 999, #0#16, aHcon;
   be;
   NULL, contab2;
|FIN;


|| ************************************************************************
|| Proceso para Asientos Anuales
|| ************************************************************************
|PROCESO contab1;

  fFechaAsto = #0#7;  || Fecha del Asiento en Pantalla
  |SI #6#14 = "C";
      fFechaAsto = #11#5;                           || Fecha del Contrato
  |FINSI;

  |SI fFechaAsto < fec1; |O fFechaAsto > fec2;
      |ACABA;                                   || no contabiliza este contrato
  |FINSI;
  |SI fFechaAsto [ fec3; |ACABA |FINSI;          ||fecha bloqueo

  |SI #11#54 = "I";   ||Elemento inactivo
      |SI #11#55 > "01.01.1900"; |Y #11#55 [ fFechaAsto;
          |ACABA;
      |FINSI;
  |FINSI;

  |HAZ contab0;
  |LIBERA #11;
|FPRC;

|DEFBUCLE cafinact0;
   #11#1;
   3;
   enEmpresa, #0#0, #0#2;
   enEmpresa, #0#1, #0#2;
   be;
   NULL, contab1;
|FIN;


|| **************************************************************************
|PROCESO HazTodo;
   |ABRE #9;  |CIERRA #9;  |DELINDEX #9;

   |HAZ eLeeParametro;
   aMonedaA =  eaMoneda;
   |HAZ contador;
   documen = nDOCUMENTO;

   nAlguno    = 0;
   eaNomFich  = ("0p" + Usuario) % 108;
   |NOME_DAT #caivatm4#eaNomFich#;
   |DELINDEX #caivatm4;

 || ////  Lee el Asiento Fijo
   |ABRE #6;
   #6#0 = #0#3;  || Asiento Fijo
   |LEE 001#6=;
   |SI FSalida ! 0;
       |CIERRA #6;  ||En esta empresa no esta el asiento elegido
       |ACABA;
   |FINSI;
   |SI #6#12 = "N";
       |CIERRA #6;  || NO es de operacion financiera
       |ACABA;
   |FINSI;
   nIncrApte = #6#26; |SI nIncrApte = 0; nIncrApte = 10; |FINSI;

   lerror = 0;
   nAlgun = 0;
   |SI #6#14 ! "L";   ||Un Asiento por Operacion Financiera.
       |BUCLE cafinact0;  || Bucle cabeceras Operaciones
   |SINO;
       |ABRE #11;
       |BUCLE cafinact1;  || Bucle Lineas
       |CIERRA #11;
   |FINSI;

   |SI lerror ! 0;
       |SI swImpresora = 1;
           |PIEINF;
       |SINO;
           aAlfa1 = enEmpresa; aAlfa1 = ("00000" + aAlfa1) % -105;
           aAlfa1 = "    Errores en Asientos de la Empresa " + aAlfa1;
           |MENAV aAlfa1;
       |FINSI;
   |SINO;
       |SI nAlgun = 0;
           aAlfa1 = "    Seleccion Vacia";
       |SINO;
           aAlfa1 = "    Asiento de Op. Financieras Contabilizados Correctamente";
       |FINSI;
       |SI nNoMulti = 0;
           |MENAV aAlfa1;
       |FINSI;
   |FINSI;
   |CIERRA #6;
   |SI nAlguno = 1; |SUB_EJECUTA_NP "caut0018;PASE"; |FINSI;
|FPRC;


|CALCULO proceso; |TIPO 3;

   |HAZ DirAplicSt;
   |HAZ nom_usu;
   aAlfa1 = "l" + nom_tmp;
   |NOME_DAT #catrasi1 #aAlfa1#;

   aDcon = " "; aHcon = "z";
   |SI #0#17 = "N"; aDcon = "N"; aHcon = "N"; |FINSI;

   |SI #48#27 = "S";
       nNoMulti = 1;
       |HAZ MultiEmpresa;
   |SINO;
       |DFICE dirfich0; |QBF dirfich0;
       |HAZ HazTodo;
   |FINSI;
   |SI swImpresora = 1;
       |FININF;
       |FINIMP;
   |FINSI;
|FCAL;

|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #2 dirfich0;
   |PATH_DAT #3 dirfich0;
   |PATH_DAT #5 dirfich0;
   |PATH_DAT #6 dirfich0;
   |PATH_DAT #7 dirfich0;
   |PATH_DAT #10 dirfich0;
   |PATH_DAT #20 dirfich0;
   |PATH_DAT #200 dirfich0;
   |PATH_DAT #204 dirfich0;
   |PATH_DAT #205 dirfich0;
   |PATH_DAT #206 dirfich0;
   |PATH_DAT #207 dirfich0;
   |PATH_DAT #208 dirfich0;

   |ATRI N;
   |PINTA 2014, "EMPRESA: ";
   |ATRI P; |PINTA 2023, #30#2;
   |PINTA 2029, #30#1; |ATRI 0;

   |CUADRO 2113 2370;
   |PINTA 2214, "                                                       ";
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 000001;
 999999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;

|| ************************************************************************
|PROCESO lineas;    || graba apunte
   |DDEFECTO #9;
   #catrasi1#00 = #0#9;
   #catrasi1#01 = #0#10;
   #catrasi1#02 = #0#11;

   #catrasi1#03 = apun;   || apunte
   #catrasi1#04 = cuen;   || cuenta
   #catrasi1#05 = digi;   || digito
   #catrasi1#06 = conc;   || concepto
   #catrasi1#07 = desc;   || descripcion
   #catrasi1#08 = sign;   || signo
   #catrasi1#09 = ptas;   || importe
   |SI #catrasi1#08 = "D";
      #catrasi1#16 = #catrasi1#4;
      #catrasi1#17 = #catrasi1#5;
      #catrasi1#10 = "";
      #catrasi1#11 = 0;
   |SINO;
      #catrasi1#16 = "";
      #catrasi1#17 = 0;
      #catrasi1#10 = #catrasi1#4;
      #catrasi1#11 = #catrasi1#5;
   |FINSI;
   |SI #6#13 = "S";  ||Si Crea IVA
       #catrasi1#12 = #6#16;  || Tipo IVA
       #catrasi1#13 = #6#17;  || Libro IVA
       #catrasi1#14 = #7#13 * 100; || % IVA falta numero
       #catrasi1#15 = #6#18;  || Serie
       #catrasi1#19 = #7#11;  || Tipo Acumulado
       #catrasi1#20 = #7#12;  || Numero Acumulador
   |FINSI;
   #catrasi1#21 = dpar;
   #catrasi1#28 = subd;
   #catrasi1#29 = eaCtaAna;
   |GRABA 020#9n;
|FPRC;

|PROCESO pasa_asto1;    || graba apunte
   |DDEFECTO #3;
   |PARA nPara1 = 0; |SI nPara1 [ 30; |HACIENDO nPara1 = nPara1 + 1;
         #camaasil#nPara1# = #catrasi1#nPara1#;
   |SIGUE;
   #camaasil#2  = nDOCUMENTO;
   #camaasil#24 = nREGISTRO;
   |SI #6#13 = "S";  ||Si Crea IVA
       |SI swIVA = 0;
           |HAZ CabIvas;
       |FINSI;
       #camaasil#12 = #camaniva#36;
       #camaasil#13 = #204#0;
       #camaasil#14 = #204#3;
       #camaasil#15 = #204#2;
   |FINSI;
   |GRABA 020#3n;
   |HAZ cabecera;           || cabecera apunte
   MasMenos     = #camaasil#9;
   DebeHaber    = #camaasil#8;
   aCUENTA      = #camaasil#4;
   nREGISTRO    = #camaasil#24;
   nDIARIO      = #camaasil#0;
   aFECHA       = #camaasil#1;
   nDOCUMENTO   = #camaasil#2;
   nNIVELCUENTA = #camaasil#5;
   |HAZ ActDiario;          || diario
   |HAZ ActCuenta;          || cuenta
   |HAZ ActTesteo;          || cuenta
   |SI #6#13 = "S";  |HAZ ActIvas; |FINSI;

   |HAZ HazElChequeo;
   nAlgun = 1;
|FPRC;

|DEFBUCLE pasa_asto0;
   #9#1;
   ;
   00, "01.01.1900", 000001, 0001;
   99, "31.12.2090", 999999, 9999;
   e;
   NULL, pasa_asto1;
|FIN;

|PROCESO pasa_asto;
   |HAZ Multi_contador;
   |ABRE #2;
   |ABRE #3;
   |ABRE #204;
   |ABRE #205;
   |ABRE #caivatm4;
   swIVA = 0;
   |BUCLE pasa_asto0;
   |CIERRA #caivatm4;
   |CIERRA #205;
   |CIERRA #204;
   |CIERRA #3;
   |CIERRA #2;

   documen = #camaasil#2 + 1;
   aAlfa1 = #camaasil#0; aAlfa1 = ("00" + aAlfa1) % -102;
   #camautoc#7  = aAlfa1;
   #camautoc#8  = #camaasil#1;
   aAlfa1  = #camaasil#2; aAlfa1 = ("000000" + aAlfa1) % -106;
   #camautoc#9  = aAlfa1;
   |GRABA 020#6a;
|FPRC;

|PROCESO cabecera;       || actualiza cabeceras
   |DDEFECTO #2;
   #camaasic#00 = #camaasil#0; ||  diario
   #camaasic#01 = #camaasil#1; ||  fecha
   #camaasic#02 = #camaasil#2; ||  documento
   #camaasic#03 = #camaasil#2; ||  asiento
   |LEE 101#2=;
   |SI FSalida ! 0;
       |DDEFECTO #2;
       #camaasic#00 = #camaasil#0; ||  diario
       #camaasic#01 = #camaasil#1; ||  fecha
       #camaasic#02 = #camaasil#2; ||  documento
       #camaasic#03 = #camaasil#2; ||  asiento
       |GRABA 020#2b;
   |FINSI;
   |SI #camaasil#08 = "D";
       #camaasic#04 = #camaasic#04 + #camaasil#09;
       |SI #camaasic#09 = 0; #camaasic#08 = #camaasil#4; #camaasic#09 = #camaasil#5; |FINSI;  || contrp.HABER
   |SINO;
       #camaasic#05 = #camaasic#05 + #camaasil#09;
       |SI #camaasic#11 = 0; #camaasic#10 = #camaasil#4; #camaasic#11 = #camaasil#5; |FINSI;  || contrp.DEBE
   |FINSI;
   #camaasic#6  = #camaasic#4 - #camaasic#5;      || saldo
   #camaasic#12 = #camaasil#24;
   |SI #camaasil#12 = "R"; #camaasic#13 = 1; |FINSI;
   |SI #camaasil#12 = "S"; #camaasic#13 = 2; |FINSI;
   |SI #camaasil#12 = "N"; #camaasic#13 = 3; |FINSI;
   |GRABA 020#2a;
   |LIBERA #2;
|FPRC;

|| ******************* PROCESO DE CREAR FACTURAS *************************
|PROCESO GrabaDatosSii;
   #caivatm4#0 = enEmpresa;
   #caivatm4#1 = enPeriodo;
   #caivatm4#2 = #camaniva#0;
   #caivatm4#3 = #camaniva#1;
   |LEE 041#caivatm4.=;
   |SI FSalida ! 0;
       #caivatm4#0 = enEmpresa;
       #caivatm4#1 = enPeriodo;
       #caivatm4#2 = #camaniva#0;
       #caivatm4#3 = #camaniva#1;
       |GRABA 020#caivatm4.n;
       nAlguno     = 1;
   |FINSI;
|FPRC;

|PROCESO CabIvas;
  nLibro = #camaasil#13;
  aSerie = #camaasil#15;
  |ABRE #calibros;
  #calibros#0 = nLibro;
  |LEE 001#calibros.=;
  |SI FSalida ! 0;
      |CIERRA #calibros;
      |ACABA;
  |FINSI;
  aTipoIVA = #calibros#2;
  |CIERRA #calibros;

  efFechaIVA = ~; enLineaIVA = 1; |HAZ SacaIVA;

  |ABRE #caivaasi;
  |DDEFECTO #caivaasi;
  |LEE 010#caivaasi.p;
  enQSerie = 0; |SI #caivaasi#44 = "S"; enQSerie = 1; |FINSI;
  |CIERRA #caivaasi;

  aAlfa1 = #camaasil#1;
  aAlfa1 = aAlfa1 % 402;
  nNume1 = aAlfa1; nPerio = nNume1;
  |SI enEjercicio [ 2009;
      |SI #caivaasi#46 = "A"; nNume1 = 0; |FINSI;
      |SI #caivaasi#46 = "T";
          nPerio = 1;
          |SI nNume1 ] 4; nPerio = 2; |FINSI;
          |SI nNume1 ] 7; nPerio = 3; |FINSI;
          |SI nNume1 ] 10;nPerio = 4; |FINSI;
      |FINSI;
   |FINSI;

   NumDoc = 1;
   #204#0 = nLibro;
   #204#1 = 9999999999;
   |LEE 010#204];
   |SI FSalida = 0;
       |LEE 040#204a;
   |SINO;
       |LEE 040#204u;
   |FINSI;
   |SI FSalida = 0; |Y #204#0 = nLibro;
       NumDoc = #204#1 + 1;
   |FINSI;
   |HAZ ult_fra;

  |DDEFECTO #204;
  #camaniva#0  = nLibro;
  #camaniva#1  = NumDoc;          || N§ de registro
  #camaniva#2  = #camaasil#15;    || Serie
  #camaniva#3  = NumFactura;      || Factura
  #camaniva#4  = #camaasil#1;     || Fecha factura
  #camaniva#5  = nPerio;          || Periodo
  #camaniva#7  = #camaasil#0;     || Diario
  #camaniva#8  = #camaasil#1;     || Fecha contable
  #camaniva#9  = #camaasil#2;     || Documento
  #camaniva#36 = aTipoIVA;        || Tipo de IVA

  |GRABA 020#camaniva.b;
  swIVA = 1;
  |HAZ ActNumero;
  |HAZ GrabaDatosSii;
|FPRC;

|PROCESO ActIvas;
 |SI swIVA = 0; |ACABA; |FINSI;
 |SI #camaasil#19 = "F";
     #camaniva#10 = #camaasil#21;     || Departamento (Actividad)
     #camaniva#11 = #camaasil#28;     || SubDepartamento (Ref.Catastral/Cultivo)
     |HAZ Departamento;
     #camaniva#24 = eaNombreAct;
     #camaniva#25 = eaNomSubdep;
     #camaniva#12 = #camaasil#4;      || Cuenta Cli/Pro
     |HAZ LeeCliPro;
     #camaniva#13 = eaTitCuenta;
     #camaniva#14 = eaCodNif;
     #camaniva#22 = "";               || N§ Factura Proveedor
     #camaniva#27 = #camaasil#6;      || Concepto Cli/Pro
     #camaniva#28 = #camaasil#7;      || Descripcion Cli/Pro
     #camaniva#31 = eaDesgIVA;
     #camaniva#32 = eaDesgCTP;
     #camaniva#33 = "N";
     #camaniva#34 = eaDesgDTO;
     #camaniva#36 = aTipoIVA;
     #camaniva#38 = 1;     || Numero de Facturas
  |FINSI;
  |SI #camaasil#19 = "I";
      #camaniva#29 = #camaasil#6;      || Concepto IVA
      #camaniva#30 = #camaasil#7;      || Descripcion IVA
  |FINSI;

  |GRABA 020#camaniva.a;
  |LIBERA #camaniva;

  #camaniva#0  = nLibro;          || Libro
  #camaniva#1  = NumDoc;          || N§ de registro
  |LEE 101#camaniva.=;

  #caivalin#0 = #camaniva#0;
  #caivalin#1 = #camaniva#1;
  #caivalin#2 = #camaasil#20;
  |LEE 010#caivalin.=;
  |SI FSalida ! 0;
      |DDEFECTO #caivalin;
      #caivalin#0 = #camaniva#0;  || Libro
      #caivalin#1 = #camaniva#1;  || Documento
      #caivalin#2 = #camaasil#20; || Linea
      |GRABA 020#caivalin.b;
  |FINSI;

  |SI #camaasil#19 = "B";
      #caivalin#3  = #camaasil#6;                  || Concepto
      #caivalin#4  = #camaasil#7;                  || Descripcion
      #caivalin#6  = #caivalin#6 + #camaasil#9;   || Base imponible
      #caivalin#12 = #camaasil#4;                  || Contrapartida
      #caivalin#17 = #camautoc#19;                 || Inversion
      #camaniva#19 = #camaniva#19 + #camaasil#9;
      #camaniva#15 = #camaniva#15 + #camaasil#9;
      #caivalin#30 = #camaasil#21;     || Departamento (Actividad)
      #caivalin#31 = #camaasil#28;     || SubDepartamento (Ref.Catastral/Cultivo)
  |FINSI;

  |SI #camaasil#19 = "I";
      #caivalin#7  = #catrasi1#14 / 100;          || Tipo de IVA
      #caivalin#8  = #caivalin#8 + #camaasil#9;   || Cuota de IVA
      #caivalin#13 = #camaasil#4;                 || Cuenta de IVA
      #camaniva#23 = #camaniva#23 + #camaasil#9;
  |FINSI;

  |SI #camaasil#19 = "R";
      #caivalin#9  = #catrasi1#14 / 100;           || Tipo de recargo
      #caivalin#10 = #caivalin#10 + #camaasil#9;   || Cuota de recargo
      #caivalin#14 = #camaasil#4;                  || Cuenta de recargo
      #camaniva#23 = #camaniva#23 + #camaasil#9;
  |FINSI;

  |SI #camaasil#19 = "D";
      #caivalin#11 = #caivalin#11 + #camaasil#9;  || Descuento
      #caivalin#15 = #camaasil#4;                  || Cuenta de descuento
      #camaniva#26 = #camaniva#26 + #camaasil#9;
  |FINSI;

  |SI #camaasil#19 = "P";
      #caivalin#18 = #camaasil#6;                  || Concepto IRPF
      #caivalin#19 = #camaasil#7;                  || Descripcion concepto IRPF
      #caivalin#21 = #catrasi1#14 / 100;           || Tipo IRPF
      #caivalin#22 = #caivalin#22 + #camaasil#9; || Cuota IRPF
      #caivalin#20 = ((100 * #caivalin#22) / #caivalin#21) ! EURODB; || Base IRPF
      #caivalin#23 = #camaasil#4;                  || Cuenta IRPF
      #camaniva#20 = #camaniva#20 + #camaasil#9;
  |FINSI;

  #caivalin#5  = #camautoc#20;      || Deducible S/N
|| #caivalin#16 = #catrasi1#14;      || Linea de tipo de iva

  efFechaIVA = #camaniva#4;
  enPorcIVA  = #caivalin#7; enPorcREC  = #caivalin#9;
  |HAZ BuscaTipoIVA;
  #caivalin#16 = enLineaIVA;

  #camaniva#17 = #camaniva#15 %  #camaniva#16;
  #camaniva#21 = #camaniva#19 + #camaniva#23 - #camaniva#26 - #camaniva#20;

  |GRABA 020#caivalin.a;
  |GRABA 020#camaniva.a;
  |LIBERA #camaniva;
  |LIBERA #caivalin;
|FPRC;

|PROCESO Departamento;
   eOpDep = 0;
   enActividad = #camaasil#21;
   eaCodDep    = #camaasil#21;
   |HAZ Actividad;
   eOpSub = 0;
   enConcepto  = #camaasil#6;
   eaCodSubdep = #camaasil#28;
   |HAZ SubActividad;
|FPRC;

|| ---------------- Proceso Lectura i actualizacion de Clientes y Proveedores
|PROCESO LeeCliPro;
  eaTitCuenta = "";
  eaCodNif    = "";
  |ABRE #caclipro;
  #caclipro#23 = "P";
  aAlfa1 = #camaasil#4 % 0102; |QBF aAlfa1;
  |SI aAlfa1 = "43"; |O aAlfa1 = "44"; #caclipro#23 = "C"; |FINSI;
  #caclipro#10 = #camaasil#4;
  #caclipro#11 = #camaasil#5;
  |LEE 010#caclipro.=;
  |SI FSalida = 0;
      eaTitCuenta  = #caclipro#1;
      eaCodNif     = #caclipro#9;
  |SINO;
      |ABRE #5;
      #5#0 = #camaasil#4;
      |LEE 001#5=;
      |SI FSalida = 0;
          eaTitCuenta = #5#2;
      |FINSI;
      |CIERRA #5;
  |FINSI;
  |CIERRA #caclipro;
|FPRC;

|PROCESO ult_fra;
 NumFactura = 1;
 |SI enQSerie = 0;
     |ABRE #calibros;
     #calibros#0 = nLibro;
     |LEE 101#calibros.=;
     |SI FSalida = 0;
         NumFactura = #calibros#4;
     |FINSI;
     |LIBERA #calibros;
     |CIERRA #calibros;
 |SINO;
     |ABRE #caivases;
     #caivases#0 = aSerie;
     #caivases#1 = aTipoIVA;
     |LEE 101#caivases.=;
     |SI FSalida = 0;
         NumFactura = #caivases#3;
     |FINSI;
     |LIBERA #caivases;
     |CIERRA #caivases;
  |FINSI;
|| ...  NumFactura = NumFactura - 1; ||numero anterior
|FPRC;

|PROCESO ActNumero;
 |SI enQSerie = 0;
     |ABRE #calibros;
     #calibros#0 = nLibro;
     |LEE 101#calibros.=;
     |SI FSalida ! 0;
         |DDEFECTO #calibros;
         #calibros#0 = nLibro;
         #calibros#2 = aTipoIVA;
         #calibros#3 = 1;
         |GRABA 020#calibros.b;
     |FINSI;
     #calibros#4 = NumFactura + #calibros#3;
     |GRABA 020#calibros.a;
     |LIBERA #calibros;
     |CIERRA #calibros;
 |SINO;
     |ABRE #caivases;
      #caivases#0 = aSerie;
      #caivases#1 = aTipoIVA;
      |LEE 101#caivases.=;
      |SI FSalida ! 0;
          |DDEFECTO #caivases;
          #caivases#0 = aSerie;
          #caivases#1 = aTipoIVA;
          #caivases#4 = 1;
          |GRABA 020#caivases.b;
      |FINSI;
      #caivases#3 = NumFactura  + #caivases#4;
      |GRABA 020#caivases.a;
      |LIBERA #caivases;
      |CIERRA #caivases;
  |FINSI;
|FPRC;
|| ************************* Proceso para sacar posibles errores

|PROCESO HazElChequeo;
  eaCuenta = #camaasil#4; |QBF eaCuenta;

  |SI eaCuenta = "";
      men = "CUENTA CONTABLE EN BLANCO";
      |HAZ HazPrint;
  |SINO;
      |HAZ SacaNivel;
      |SI enNivel = 0;
          men = "CUENTA CONTABLE FUERA DE NIVEL";
          |HAZ HazPrint;
      |FINSI;
  |FINSI;
  |ABRE #cacuenta;
  #cacuenta#0 = #camaasil#4;
  #cacuenta#1 = #camaasil#5;
  |LEE 001#5=;
  |SI FSalida ! 0;
      men = "NO EXISTE CUENTA CONTABLE";
      |HAZ HazPrint;
  |SINO;
      |SI #cacuenta#3 = "N";
         men = "CUENTA CONTABLE SIN ASIENTO DIRECTO";
         |HAZ HazPrint;
      |FINSI;
  |FINSI;
|CIERRA #cacuenta;

  |SI #camaasil#9 = 0;                   || Importe Cero
      men = "IMPORTE IGUAL A CERO";
      |HAZ HazPrint;
  |FINSI;

  |SI #camaasil#6 = 0;                   || Concepto
      men = "CONCEPTO AUTOMATICO IGUAL A CERO";
      |HAZ HazPrint;
  |FINSI;
|FPRC;

|PROCESO HazPrint;
  |SI swImpresora = 0;
      swImpresora = 1;
      |IMPRE 0;
      |SI FSalida ! 0;  swImpresora = 2; |ACABA; |FINSI;
      |INFOR informe;
      |SI FSalida ! 0;  swImpresora = 3; |FINIMP;  |ACABA;  |FINSI;
 |FINSI;
 |SI swImpresora = 1;
     antdia  = #camaasil#0;
     antfec  = #camaasil#1;
     antdoc  = #camaasil#2;
     antapu  = #camaasil#3;
     |PRINT;
 |FINSI;
 lerror = lerror + 1;
|FPRC;
