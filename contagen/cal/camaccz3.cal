|FICHEROS;
   camaccz3 #0;

   caconimp #8;
   camaniva #11;
   caivalin #12;

   caivaasi #200;

   camacc01 #501;
   camacc02 #502;
   camacc03 #503;
   camacc1@ #901;
   camacc2@ #902;
   camacc3@ #903;

   cawdatos #900;
   camaccw1 #991;
   camaccw4 #999;
   canempre #30;
|FIN;

|VARIABLES;
    aParam     = "";
    aParam1    = "";

    nEmpresa   = 0;
    nPeriodoO  = 0;
    nPeriodoD  = 0;
    &enPeriodoO = 0;
    &enPeriodoD = 0;
    aPathOrigen  = "";
    aPathDestino = "";

    aAlfa1   = "";
    aAlfa2   = "";
    nNume1   = 0;
    nNume2   = 0;
    nPara1   = 0;
    aFichero = "";
    nCampo   = 0;
    aMas     = "";
    aDef     = "";
    fDFecha  = @;
    aDTipo   = "";
    aHTipo   = "";
    nSwAlgun = 0;
    aMulDep  = "";
    nDdepar  = 0;
    nHdepar  = 0;
    nSwSel   = 0;
    nSw      = 0;

    &aCif      = "";
    &enSwLinea = 0;
    &enTotal   = 0;
    &enTBases  = 0;
    &enTCuotas = 0;
    &eaTexto   = "";
    nOp        = 0;
    nError     = 0;
    dfec       = @;
    hfec       = @;
    &eaError   = "";
    nImpre     = 0;
    informe    = "";
    nSwPrint   = 0;
    nLibro     = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoiv_i;

|| ***************** Calculos de Pantalla
|CALCULO Primero;
  aParam1 = aParam % 204; |QBF aParam1;
  |SI aParam1 ! ""; enNoBloq = 1; |FINSI;
  |HAZ inicio;
  enNoBloq = 0;

  |ABRE #200;
  |LEE 000#200p;
  |SI FSalida ! 0;
      |DDEFECTO #200;
      |GRABA 040#200n;
  |FINSI;
  |CIERRA #200;
  aMulDep = #200#132;
  aAlfa1  = #200#44;
  |C_M #0#7,1,aAlfa1;
  |C_M #0#8,1,aAlfa1;
|FCAL;

|CALCULO QueIva; |TIPO 0;
   |SI #0#13 = "R"; eaTexto = "Cobrados"; |FINSI;
   |SI #0#13 = "S"; eaTexto = "Pagados "; |FINSI;
   |SI #0#13 = "T"; eaTexto = "Operacion"; |FINSI;
   aTipoIVA = #0#13;
|FCAL;

|CALCULO defectos; |TIPO 7;
    #0#4 = dfec; |PINTA #0#4;
    #0#5 = hfec; |PINTA #0#5;
|FCAL;

|CALCULO fecha; |TIPO 0;
   |SI Campo = 5; |Y #0#5 < #0#4;
       |MENAV "    FECHA ERRONEA ";
       |ERROR;
   |FINSI;
|FCAL;

|CALCULO nodepar; |TIPO 7;
   |C_M #0#9, 1, eaDepar;
   |C_M #0#10, 1, eaDepar;
|FCAL;

|CALCULO cuenta;|TIPO 6;
     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta; |PINTA #0Campo;
|FCAL;

|| *************************************************************************
|PROCESO Pasa503;
        #903#0 = #503#0;
        #903#1 = #503#1;
        #903#2 = #503#2;
        #903#3 = #503#3;
        #903#4 = #503#4;
        #903#5 = #503#5;
        #903#6 = #503#6;

        #903#7 = #902#17;
        |GRABA 020#903n;
|FPRC;

|DEFBUCLE Pasa503;
    #503#1;
    ;
    #502#17, #502#0, #502#1, #502#2, 01;
    #502#17, #502#0, #502#1, #502#2, 99;
    e;
    NULL, Pasa503;
|FIN;

|PROCESO Pasa502;
    |SI #0#6 = "I";
        #502#16 = #0#16;
        #502#18 = #0#16;
    |FINSI;
    |SI #0#6 = "D";
        #502#16 = -1;
        #502#18 = -1;
    |FINSI;

    |GRABA 020#502a;
    |LIBERA #502;

    |SI #0#6 = "I";
        |PARA nPara1 = 0; |SI nPara1 [ 15; |HACIENDO nPara1 = nPara1 + 1;
              #902nPara1 = #502nPara1;
        |SIGUE;
        #902#16 = #0#16;
        #902#17 = #0#17;
        #902#18 = #0#16;
        |GRABA 020#902n;

        |ABRE #903;
        |BUCLE Pasa503;
        |CIERRA #903;
    |FINSI;
|FPRC;

|DEFBUCLE Pasa502;
    #502#1;
    ;
    #501#17, #501#0, #501#1, 01;
    #501#17, #501#0, #501#1, 99;
    be;
    NULL, Pasa502;
|FIN;

|| *************************************************************************
|PROCESO Borra903;
    |BORRA 020#903a;
    |LIBERA #903;
|FPRC;

|DEFBUCLE Borra903;
    #903#1005;
    ;
    #901#17, #901#0, #901#1, 01, 01;
    #901#17, #901#0, #901#1, 99, 99;
    be;
    NULL, Borra903;
|FIN;

|PROCESO Borra902;
    |BORRA 020#902a;
    |LIBERA #902;
|FPRC;

|DEFBUCLE Borra902;
    #902#1004;
    ;
    #901#17, #901#0, #901#1, 01;
    #901#17, #901#0, #901#1, 99;
    be;
    NULL, Borra902;
|FIN;

|PROCESO Borra901;
    |BUCLE Borra902;
    |BUCLE Borra903;
    |BORRA 020#901a;
    |LIBERA #901;
|FPRC;

|| *************************************************************************
|PROCESO OpImportar;

   |SI #501#5 = "S"; |ACABA; |FINSI; || esta toda pagada/cobrada
   |SI #501#6 = #999#8; |Y #501#7 = #999#9;
       |ACABA;
   |FINSI;

   |SI #501#18 = -1; ||aun no estaba importada

       |DDEFECTO #901;
       #901#0  = #501#0;
       #901#1  = #501#1;
       #901#17 = #0#17;
       |LEE 141#901=;
       |SI FSalida = 0;
           nSwSel = 4;
           eaError = "Ya existe Registro en el Ejer. Destino";
           |ACABA;
           || |HAZ  Borra901;
       |FINSI;

       |PARA nPara1 = 0; |SI nPara1 [ 16; |HACIENDO nPara1 = nPara1 + 1;
             #901nPara1 = #501nPara1;
       |SIGUE;

       #901#17 = #0#17;
       #901#18 = #0#16;

       |ABRE #902;
       |BUCLE Pasa502;
       |CIERRA #902;

       |GRABA 040#901n;
       |LIBERA #901;

       #501#18 = #0#16;
       |GRABA 020#501a;

       |SI nLibro ! 0; |Y nLibro ! #501#0;
           |SI nSwPrint = 1; |Y #0#20 = "S";
               |PIEINF;
           |FINSI;
           nSwPrint = 0;
       |FINSI;

       |SI #501#13 = "R";
           eaTexto = "Cobrados";
       |SINO;
           eaTexto = "Pagados";
       |FINSI;

       nLibro = #501#0;
       |SI #0#20 = "S";
           |PRINT;
           nSwPrint = 1;
       |FINSI;
       nSwAlgun = 1;
   |FINSI;

   |LIBERA #501;
|FPRC;

|PROCESO Comprobar902;
    #502#17 = 0;
    #502#0  = #902#0;
    #502#1  = #902#1;
    #502#2  = #902#2;
    |LEE 041#502=;
    |SI FSalida ! 0;
        nSw = 1;
        |ERROR10;
    |FINSI;
|FPRC;

|DEFBUCLE Comprobar902;
    #902#1004;
    ;
    #901#17, #901#0, #901#1, 01;
    #901#17, #901#0, #901#1, 99;
    e;
    NULL, Comprobar902;
|FIN;

|PROCESO OpDesImpor;

   |SI #501#18 ! -1; || Ya estaba importada
        nSw = 0;
       |DDEFECTO #901;
       #901#0  = #501#0;
       #901#1  = #501#1;
       #901#17 = #0#17;
       |LEE 141#901=;
       |SI FSalida = 0;
           |ABRE #502;
           |BUCLE Comprobar902;
           |CIERRA #502;
           |SI nSw = 1;
               eaError = "Existen movimientos en el Ejer.posterior";
               nSwSel = 3;
           |SINO;
               |HAZ  Borra901;
           |FINSI;
       |FINSI;

       |SI nSw = 0;
           |BUCLE Pasa502;

           #501#18 = -1;
           |GRABA 020#501a;

           |SI nLibro ! 0; |Y nLibro ! #501#0;
               |SI nSwPrint = 1; |Y #0#20 = "S";
                   |PIEINF;
               |FINSI;
               nSwPrint = 0;
           |FINSI;

           |SI #501#13 = "R";
               eaTexto = "Cobrados";
           |SINO;
               eaTexto = "Pagados";
           |FINSI;

           nLibro = #501#0;
           |SI #0#20 = "S";
               |PRINT;
               nSwPrint = 1;
           |FINSI;

           nSwAlgun = 1;
       |FINSI;
   |FINSI;
   |LIBERA #501;
|FPRC;
|| *************************************************************************

|PROCESO IgualoCampos;
     |DDEFECTO #999;
     #999#0  = #camaniva#0;
     #999#1  = #camaniva#1;
     #999#2  = #camaniva#03;
     #999#3  = #camaniva#02;
     #999#4  = #camaniva#04;
     #999#5  = #camaniva#12;
     #999#6  = #camaniva#27;
     #999#7  = #camaniva#28;
     #999#8  = #camaniva#19;
     #999#9  = #camaniva#23;
     #999#10 = #camaniva#21;
     #999#11 = #camaniva#13;
     #999#12 = #camaniva#14;
     #999#13 = aTipoIVA;
     #999#14 = "N";
     #999#15 = #camaniva#04;
     #999#17 = #camaniva#5;
     #999#21 = #camaniva#7;
     #999#22 = #camaniva#8;
     #999#23 = #camaniva#9;
|FPRC;

|PROCESO GrabaAuxiliar;

     #991#0  = #501#0;
     #991#1  = #501#1;
     #991#26 = #501#17;
     |LEE 041#991=;
     |SI FSalida ! 0;
         |DDEFECTO #991;
         #991#0  = #501#0;
         #991#1  = #501#1;
         #991#26 = #501#17;
         |GRABA 020#991c;
     |FINSI;

     |SI nSwSel ! 2;
         |PARA nPara1 = 2; |SI nPara1 [ 27; |HACIENDO nPara1 = nPara1 + 1;
               #991nPara1 = #999nPara1;
         |SIGUE;
     |FINSI;
     #991#11 = eaError;
     |GRABA 020#991a;
     |LIBERA #991;
      nError = 1;
|FPRC;

|| ////////////////////////////////////////
|PROCESO LinLibro0;

  aAlfa1 = #12#30; |QBF aAlfa1; |SI aAlfa1 = ""; #12#30 = #11#10; |FINSI;
  nSwSel = 1;
  |SI Haybasica = 1;
      nDepar  = #12#30; |SI nDepar = 0;  nDepar = 1;   |FINSI;
      nDdepar = #0#9;
      nHdepar = #0#10;
      |SI nDepar < nDdepar; |O nDepar > nHdepar; nSwSel = 0; |FINSI;
  |SINO;
      |SI #12#30 < #0#9;  |O #12#30 > #0#10;  nSwSel = 0; |FINSI;
  |FINSI;

  |SI nSwSel = 1; |ERROR10; |FINSI;
|FPRC;

|DEFBUCLE LinLibro;
 #12#1;
 ;
 #11#0, #11#1, 01;
 #11#0, #11#1, 99;
 ;
 NULL, LinLibro0;
|FIN;
|| //////////////////////////////

|PROCESO Facturas;

  |SI #0#13 ! "T"; |Y #11#36 ! #0#13;    |ACABA; |FINSI;

  |SI #11#12 < #0#11; |O #11#12 > #0#12; |ACABA; |FINSI;
  |SI #11#2  < #0#7;  |O #11#2  > #0#8;  |ACABA; |FINSI;
  |SI #11#3  < #0#2;  |O #11#3  > #0#3;  |ACABA; |FINSI;
  |SI #11#4  < #0#4;  |O #11#4  > #0#5;  |ACABA; |FINSI;

  aTipoIVA = #11#36;
  |SI aMulDep ! "S";
      |SI Haybasica = 1;
          nDepar  = #11#10; |SI nDepar = 0;  nDepar = 1;   |FINSI;
          nDdepar = #0#9;
          nHdepar = #0#10;
          |SI nDepar < nDdepar; |O nDepar > nHdepar; |ACABA; |FINSI;
      |SINO;
          |SI #11#10 < #0#9;  |O #11#10 > #0#10;  |ACABA;  |FINSI;
      |FINSI;
  |SINO;
      |BUCLE LinLibro;
      |SI nSwSel = 0; |ACABA; |FINSI;
  |FINSI;

  enSwCaja = 0;
  |SI enEjercicio ] 2014;
     |SI #camaniva#42 = "Z"; |O #camaniva#42 = "1"; |O #camaniva#42 = "2";
         |O #camaniva#42 = "3"; |O #camaniva#42 = "4"; |O #camaniva#42 = "5";
         |O #camaniva#42 = "6"; |O #camaniva#42 = "7"; |O #camaniva#42 = "8";
            enSwCaja = 1;
     |FINSI;
  |FINSI;
  |SI enSwCaja = 0; nSwSel = 0; |ACABA; |FINSI;

   nSwSel = 1;
   |HAZ IgualoCampos;

    #999#16 = #501#12;
    #999#17 = #501#8;
    #999#18 = #501#16;
    #999#19 = #501#6;
    #999#20 = #501#7;

    #999#26 = #501#17;
    #999#27 = #501#18;
    ||  |GRABA 020#999n;  no graba
|FPRC;

|PROCESO RegCajaOrigen;
   nSwSel = 0;
   #11#0 = #501#0;
   #11#1 = #501#1;
   |LEE 041#11=;
   |SI FSalida = 0;
       |HAZ Facturas;
   |SINO;
       nSwSel = 2;
       eaError = "No existe Factura";
       |DDEFECTO #999;
   |FINSI;
   |SI nSwSel = 1;
       |SI #0#6 = "I"; |HAZ OpImportar; |FINSI;
       |SI #0#6 = "D"; |HAZ OpDesImpor; |FINSI;
   |FINSI;
   |SI nSwSel ] 2;
       |HAZ GrabaAuxiliar;
   |FINSI;
|FPRC;

|DEFBUCLE RegCajaOrigen;
   #501#1;
   ;
   0000, #0#0, 0000000001;
   0000, #0#1, 9999999999;
   e;
   NULL, RegCajaOrigen;
|FIN;

|| ******************************************************
|PROCESO MiraErrores;
   |PRINT;
   nSwPrint = 1;
|FPRC;

|DEFBUCLE MiraErrores;
   #999#1;
   ;
   INICIO;
   FINAL;
   ;
   MiraErrores;
|FIN;
|| ******************************************************
|CALCULO impre; |TIPO 3;

   aFichero = "x" + Usuario; |NOME_DAT #999 aFichero; |DELINDEX #999;
   aFichero = "y" + Usuario; |NOME_DAT #991 aFichero; |DELINDEX #991;

    enEmpresa  = #0#14;
    ePerioCaja = #0#16; |HAZ ePathCaja; aPathOrigen = ePathCaja;
    ePerioCaja = #0#18; |HAZ ePathCaja; aPathDestino= ePathCaja;

    |DBASE aMas;
    aDef = aMas + "def/camacc01";
    |CARGA_DEF aDef, camacc1@;
    |SI FSalida ! 0;
        |MENAV "    Error cargando def camacc01";
        |ACABA;
    |FINSI;

    aDef = aMas + "def/camacc02";
    |CARGA_DEF aDef, camacc2@;
    |SI FSalida ! 0;
        |DESCARGA_DEF #camacc1@;
        |MENAV "    Error cargando def camacc02";
        |ACABA;
    |FINSI;

    aDef = aMas + "def/camacc03";
    |CARGA_DEF aDef, camacc3@;
    |SI FSalida ! 0;
        |DESCARGA_DEF #camacc2@;
        |DESCARGA_DEF #camacc1@;
        |MENAV "    Error cargando def camacc03";
        |ACABA;
    |FINSI;

    |PATH_DAT #901 aPathDestino;
    |PATH_DAT #902 aPathDestino;
    |PATH_DAT #903 aPathDestino;

    |PATH_DAT #501 aPathOrigen;
    |PATH_DAT #502 aPathOrigen;
    |PATH_DAT #503 aPathOrigen;

    |PATH_DAT #11 aPathOrigen;
    |PATH_DAT #12 aPathOrigen;

    |HAZ DirAplicSt;
    |SI Haybasica = 1; |Y #0#10 = "zz"; #0#10 = "99"; |FINSI;
    |SI Haybasica = 0;  || si selecciona todos los departamentos no es necesario ver lineas
        |SI #0#9 = "  "; |Y #0#10 = "zz"; aMulDep = "N"; |FINSI;
    |SINO;
        |SI #0#9 [ 1;    |Y #0#10 = 99;   aMulDep = "N"; |FINSI;
    |FINSI;

    nSwAlgun = 0;
    aDTipo = #0#13;
    aHTipo = #0#13;
    |SI #0#13 = "T";
        aDTipo = "R";
        aHTipo = "S";
    |FINSI;

    |ABRE #8;
    |DDEFECTO #8;
    |LEE 001#8=;
    |CIERRA #8;
    enDatos = 0; |SI #8#7 = "S"; enDatos=1; |FINSI;
    eaIa = #canempre#21;
    |HAZ LeeDatosEmpresa;
    |SI #0#20 = "S";

        |IMPRE 0;
        |SI FSalida ! 0;
            |MENAV "    Error en Impresora. Proceso abortado";
            |ACABA;
        |FINSI;
        nImpre = 1;

        |INFOR "camaccz3";
        |SI FSalida ! 0;
            |MENAV "    Error en Informe. Proceso abortado";
            |FINIMP;
            |ACABA;
        |FINSI;
    |FINSI;

    |SI #0#6 = "D";
        #0#21 = "INFORME DE FACTURAS EN RECC ANULADAS EL TRASPASO ";
    |FINSI;

    |ABRE #991;
    |ABRE #901;
    |ABRE #11;

    nLibro   = 0;
    nError   = 0;
    nSwPrint = 0;
    |BUCLE RegCajaOrigen;
    |SI nImpre = 1;
        |SI nSwPrint = 1;  |PIEINF;  |FINSI;
        |FININF;
        nSwPrint = 0;
    |FINSI;

    |SI nSwAlgun = 0; |Y aParam1 = "";
        aAlfa1 = "0000No hay Facturas RECC para traspasar";
        |SI #0#6 = "D";
            aAlfa1 = "0000No hay Facturas traspasadas al ejercicio posterior";
        |FINSI;
        |MENAV aAlfa1;
    |FINSI;

    |CIERRA #11;
    |CIERRA #901;
    |CIERRA #991;

    |SI nError ! 0;
        |SI nImpre = 0;
            |IMPRE 0;
            |SI FSalida ! 0;
                |MENAV "    Error en impresora. Proceso abortado";
                |VETE Salir;
            |FINSI;
            nImpre = 1;
        |FINSI;

        |INFOR "camaccy3";
        |SI FSalida ! 0;
            |MENAV "    Error en informe. Proceso abortado";
            |VETE Salir;
        |FINSI;

        nSwPrint = 0;
        |BUCLE MiraErrores;
        |PIEINF;
        |FININF;
    |FINSI;

|ET Salir;
    |SI nImpre = 1;
        |FINIMP;
        nImpre = 0;
    |FINSI;

    |DESCARGA_DEF #903;
    |DESCARGA_DEF #902;
    |DESCARGA_DEF #901;
|FCAL;

|PROCESO BuscaEmpresas;
   nError = 0;
   |ABRE #30;
   |SELECT #4#30;
   |SI #0#16 = -1;  aAlfa1 = #0#17;  |FINSI;
   |SI #0#18 = -1;  aAlfa1 = #0#19;  |FINSI;
   aAlfa1 = aAlfa1 % -102;
   #30#2  = #0#14;
   #30#26 = aAlfa1;
   |LEE 041#30=;
   |SI FSalida ! 0;
       nError = 1;
   |SINO;
       |SI #0#16 = -1; #0#16 = #30#3; |FINSI;
       |SI #0#18 = -1; #0#18 = #30#3; |FINSI;
   |FINSI;

   |SELECT #1#30;
   |CIERRA #30;
|FPRC;

|PROGRAMA;
   aParam = PARAMETRO; |QBF aParam;

   |SI aParam = ""; aParam = "I"; |FINSI;

   |HAZ Primero;
   |SI enEjercicio < 2014;
       |MENAV "    Fras en Regimen Especial de Cr. de Caja a partir del Ejercicio 2014";
       |ACABA;
   |FINSI;

   aParam1 = aParam % 204;
   aParam  = aParam % 101;
   #0#6    = aParam;

   |SI aParam1 = "";
       #0#14 = enEmpresa;
       #0#15 = eaNombreEmp;
       |SI aParam = "I";
           #0#16 = -1;
           #0#17 = enEjercicio - 1;
           #0#18 = enPeriodo;
           #0#19 = enEjercicio;
           aAlfa2 = "Origen";
       |SINO;
           #0#16 = enPeriodo;
           #0#17 = enEjercicio;
           #0#18 = -1;
           #0#19 = enEjercicio + 1;
           aAlfa2 = "Destino";
       |FINSI;
       |HAZ BuscaEmpresas;

       |SI nError = 1;
           aAlfa2 = "0000Error no Existe Empresa " + aAlfa2;
           |MENAV aAlfa2;
           |ACABA;
       |FINSI;
       |SI #0#17 < 2014;
           aAlfa2 = "0000Error Empresa Origen con Ejercicio inferior a 2014";
           |MENAV aAlfa2;
           |ACABA;
       |FINSI;

       |SI #0#6 = "I";
           aAlfa2 = #0#17;
           aAlfa1 = fec1 % 106; aAlfa1 = aAlfa1 + aAlfa2; dfec = aAlfa1;
           aAlfa1 = fec2 % 106;
           |SI dig1 ! 1; aAlfa2 = #0#19; |FINSI;
           aAlfa1 = aAlfa1 + aAlfa2; hfec = aAlfa1;
       |SINO;
           dfec = fec1;
           hfec = fec2;
       |FINSI;

       |CLS;
       |PINPA #0#0;
       |SI aParam = "D";
           |PINTA 0606, "                                                                  ";
           |ATRI R; |PINTA 0606, "Anular Traspaso de Facturas RECC pendientes"; |ATRI 0;
           |PINTA 1254, "Anuladas t.: ";
           |PINTA 2207, "Modificaremos Cobros/Pagos de las Fras en Ejercicio Origen";
       |FINSI;
       |PINDA #0#0;

       |ENDATOS #1#0;
   |FINSI;

   |SI aParam1 ! "";
       enEjercicio = aParam1;
       |DDEFECTO #0;
       #0#6  = aParam;
       #0#14 = enEmpresa;
       #0#15 = eaNombreEmp;
       #0#16 = enPeriodoO;
       #0#17 = enEjercicio;
       #0#18 = enPeriodoD;
       #0#19 = enEjercicio + 1;
       #0#4  = "01.01.2000";
       #0#5  = "01.01.2099";
       #0#20 = "N";
       |HAZ impre;
   |FINSI;
|FPRO;
