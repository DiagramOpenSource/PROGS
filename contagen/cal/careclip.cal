|FICHEROS;
   careclip #0;
   camaniva #1;    || iva
   cacuenta #4;    || Cuentas
   caclipro #18;   || Clientes y Proveedores

   canempre #30;
   caselem1 #998;

   &regisnif@ #709;
   :/basica/def/agim0003  #710;
   :/integral/def/dsam0103 #711;

   &poblaci@  #703;
   &sgpaises@ #718;
|FIN;

|VARIABLES;
   &entrada = 1;

   aNombre  = "";
   aNif     = "";
   aAlfa1   = "";
   aAlfa2   = "";
   nNume1   = 0;
   aPath    = "";

   DesdeTipo = "";
   HastaTipo = "";
   nSwPr     = 0;
   informe   = "careclip";

   aNifFra   = "";
   nRegistro = 0;
   nSwError  = 0;

   aNomFra   = "";
   aParam    = "";
   &efFecha1 = @;
   &efFecha2 = @;
   aTipo     = "";
|FIN;

|INCLUYE dscont_i;

____________________________________/ CALCULOS DESDE PANTALLA

|CALCULO def_fec; |TIPO 7;
   #0#5 = fec1;    |PINTA #0#5;
   #0#6 = fec2;    |PINTA #0#6;
|FCAL;

|CALCULO cuenta;|TIPO 6;
     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta; |PINTA #0Campo;
|FCAL;

|| **************************************************************************
|PROCESO Igualdad;
  |SI #1#36 = "R"; #18#23 = "C"; |FINSI;
  |SI #1#36 = "S"; #18#23 = "P"; |FINSI;

  #18#10 = eaCuenta;
  #18#11 = enNivel;

  #18#9 = #709#0;
  #18#1 = #709#1;

  aAlfa1 = #709#9;
  |QBF aAlfa1;
  |SI aAlfa1 = "";
      #18#2 = #709#2;
  |SINO;
      #18#2 = aAlfa1 + " " + #709#2;
  |FINSI;
  aAlfa1 = #18#2; |QBF aAlfa1;
  #18#2 = aAlfa1 + ", " + #709#3;
  aAlfa1 = #18#2; |QBF aAlfa1;
  #18#2 = aAlfa1 + " " + #709#10;
  aAlfa1 = #18#2; |QBF aAlfa1;
  #18#2 = aAlfa1 + " " + #709#11;

  #18#3 = #709#4;
  #18#4 = #709#5;
  #18#5 = #709#7;
  #18#6 = #709#6;
  #18#7 = #709#8;
  #18#8 = #709#17;
  #18#24 = #709#12;

  #18#44 = #18#2;
  #18#45 = #18#3;
  #18#46 = #18#4;
  #18#47 = #18#5;
  #18#48 = #18#6;
  #18#49 = #18#27;
  #18#50 = #18#25;
|FPRC;

|PROCESO leyendo;

   nSwError = 0;
   |HAZ SacaNivel;
   aNombre   = "";
   aNif      = "";
   nRegistro = 0;
   || ................................................

|SI #1#36 ! "N";

    #18#23 = "C";
    #18#10 = eaCuenta;
    #18#11 = enNivel;
    |LEE 001#18=;
    |SI FSalida = 0;
        aNombre = #18#1;        || nombre del cliente
        aNif    = #18#9;
        nRegistro = 1;
    |SINO;
        #18#23 = "P";
        #18#10 = eaCuenta;
        #18#11 = enNivel;
        |LEE 001#18=;
        |SI FSalida ! 0;
            |DDEFECTO #18;
            |HAZ lee_cuent;
            aNombre = #4#2;         || nombre de la cuenta
        |SINO;
            aNombre = #18#1;        || nombre del proveedor
            aNif    = #18#9;
            nRegistro = 1;
        |FINSI;
     |FINSI;

     |QBF aNif;
     |SI aNif = "";
        nSwError = 1;
        |SI ewNif = 1; |ACABA; |FINSI;

        |ABRE #709;
        |SELECT #2#709;
        #709#1 = aNombre;
        |LEE 141#709=;
        |SI FSalida = 0;
            aNif = #709#0;
            |SI nRegistro = 1;
                #18#9 = aNif;
                |GRABA 020#18a;
            |SINO;
                |HAZ Igualdad;
                |GRABA 020#18n;
            |FINSI;
            nSwError = 0;
        |FINSI;
        |SELECT #1#709;
        |CIERRA #709;
    |FINSI;

|SINO;

    |HAZ lee_cuent;
    aNombre = #4#2;         || nombre de la cuenta
    aNif    = aNifFra;
    |SI ewNif = 1; |ACABA; |FINSI;

    |ABRE #709;
    |SI aNif = "";
        nSwError = 1;
        |ABRE #709;
        |SELECT #2#709;
        #709#1 = aNombre;
        |LEE 141#709=;
        |SI FSalida = 0;
            aNif = #709#0;
            nSwError = 0;
        |FINSI;
        |SELECT #1#709;
    |SINO;
        #709#0 = aNif;
        |LEE 141#709=;
        |SI FSalida = 0;
            aNombre = #709#1;
            nSwError = 0;
        |FINSI;
    |FINSI;
    |CIERRA #709;

|FINSI;

|FPRC;

|PROCESO lee_cuent;
   |ABRE #4;
   #4#0 = eaCuenta;
   #4#1 = enNivel;
   |LEE 001#4=;
   |SI FSalida ! 0;
       |DDEFECTO #4;
   |FINSI;
   |CIERRA #4;
|FPRC;

|| **************************************************************************

|PROCESO lee_iva1;
   |ATRI I;
   aAlfa1 = #1#0;   aAlfa1 = ("00" + aAlfa1) % -102;     |PINTA 2238, aAlfa1;
   aAlfa1 = #1#3;   aAlfa1 = ("000000" + aAlfa1) % -106; |PINTA 2241, aAlfa1;
   aAlfa1 = #1#2;                                        |PINTA 2248, aAlfa1;
   |ATRI 0;

   aNomFra = #1#13;
   aNifFra = #1#14; |QBF aNifFra;
   |SI aNifFra ! ""; |Y #0#14 = "N";
       eaCuenta = #1#12;
       |HAZ leyendo;
       |LIBERA #1;
       |ACABA;
   |FINSI;

   eaCuenta = #1#12;
   |HAZ leyendo;
   |SI nSwError = 0;
       #1#13 = aNombre;
       #1#14 = aNif;
   |SINO;
       #1#13 = aNombre;
       #1#14 = aNifFra;
       |PRINT;
       nSwPr = 1;
   |FINSI;
   |GRABA 020#1a;
   |LIBERA #1;
|FPRC;

|DEFBUCLE lee_iva;
   #1#3;
   4,12,36;
   #0#1, #0#12, #0#3, #0#5, #0#8, DesdeTipo;
   #0#2, #0#13, #0#4, #0#6, #0#9, HastaTipo;
   be;
   NULL, lee_iva1;
|FIN;
|| **************************************************************************

|PROCESO HazTodo;
       nSwPr = 0;
       |ABRE #18;
       |BUCLE lee_iva;
       |CIERRA #18;
       |SI nSwPr = 1;
           |PIEINF;
       |FINSI;
|FPRC;

|CALCULO tipo3; |TIPO 3;

   |SI #0#7 ! "SI"; |ACABA; |FINSI;

   DesdeTipo = #0#0; HastaTipo = #0#0;
   |SI #0#0 = "I"; DesdeTipo = "N"; HastaTipo = "N"; |FINSI;
   |SI #0#0 = "T"; DesdeTipo = "N"; HastaTipo = "S"; |FINSI;

   |IMPRE 0;
   |SI FSalida ! 0;  |ACABA; |FINSI;

   |INFOR informe;
   |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;


   |SI #48#27 = "S"; |Y aParam = "";
       |HAZ MultiEmpresa;
   |SINO;
       |SI aParam = "";
           |DFICE dirfich0; |QBF dirfich0;
       |FINSI;
       |HAZ HazTodo;
   |FINSI;

   |FININF;
   |FINIMP;
|FCAL;

|| **************************************************************************
|PROGRAMA;
   aParam = PARAMETRO; |QBF aParam;

   |HAZ inicio;
   ewNif = 0;
   enSwDeDonde = -1;
   |HAZ DirAplicSt;
   |SI HayAcceso = 1;
       enSwDeDonde = 1;
       |REFERENCIA_DEF regisnif@, #711;
   |SINO;
       |SI Haybasica = 1;
           enSwDeDonde = 0;
           |REFERENCIA_DEF regisnif@, #710;
       |FINSI;
   |FINSI;
   |SI enSwDeDonde = -1; ewNif = 1; |FINSI; || no existe Basica ni Integrada


   |SI aParam ! "";
       aTipo = aParam % 701;
       |PINPA #0#0;
       |DDEFECTO #0;
       #0#0 = aTipo;
       #0#5 = efFecha1;
       #0#6 = efFecha2;
       #0#7 = "SI";
       |PINDA #0#0;
       aAlfa1 = aParam % 105;
       aAlfa2 = aParam % 601;
       cod1 = aAlfa1;
       cod2 = aAlfa2;
       emEmp = 1;  || Sin mensaje
       |HAZ leelo;
       |SI swerror ! 0;  |ACABA;   |FINSI;

       |DFICB aPath; |QBF aPath
       dirfich0 = aPath + aParam + "/";

       |PATH_DAT #1 dirfich0;
       |PATH_DAT #4 dirfich0;
       |PATH_DAT #18 dirfich0;

       |ATRI N; |CUADRO 1508 1850;
       |ATRI R; |PINTA 1609, "Empresa: ";      |ATRI 0;
       |PINTA 1618, #30#2; |PINTA 1709, #30#1; |ATRI 0;
       |PINTA 1910, "                                                      ";
       |PINTA 2010, "                                                      ";

       |HAZ HazTodo;
  |SINO;
       |MANTE #0;
  |FINSI;
|FPRO;

|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #4 dirfich0;
   |PATH_DAT #18 dirfich0;

   |ATRI N; |CUADRO 1508 1850;
   |ATRI R; |PINTA 1609, "Empresa: ";      |ATRI 0;
   |PINTA 1618, #30#2; |PINTA 1709, #30#1; |ATRI 0;
   |PINTA 1910, "                                                      ";
   |PINTA 2010, "                                                      ";

   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 001;
 999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;
