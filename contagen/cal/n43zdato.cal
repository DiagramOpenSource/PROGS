|FICHEROS;
    n43zdato #0;
    n43datos #1,MANTE; || Este Def.
    cacuenta #2;       || Cuentas Contables.
    espej43@ #10;
|FIN;

|VARIABLES;
    &entrada = 1;
    men2  = "     Longitud de Cuenta Fuera de Nivel !";
    vacio = "            ";
     x = "";

     cuentat = "";
     inkey   = 0;
     aMas    = "";
     aDef    = "";
     nTecla  = 0;
     nConta  = 0;
     nModo   = 0;
     aParam  = "";
     &ext1   = "";
     &ext2   = "";
     &ext3   = "";
|FIN;

|INCLUYE dscont_i;

|PROCESO cuenta;|TIPO 6;

   |SI #0Campo ! vacio;
       eaCuenta = #0Campo;
       salidas  = FSalida;
       |HAZ FiltraPuntos;
       #0Campo = eaCuenta; |PINTA #0Campo;
   |SINO;
        |ABRE #1;
        |LEE 040#1u;
        |SI FSalida = 0;
            #0Campo = #1#1; |PINTA #0Campo;
        |SINO;
            |CIERRA #1;
            |MENAV "    No existen Datos";
            |ERROR;
            |ACABA;
        |FINSI;
        |CIERRA #1;
   |FINSI;

   eaCuenta = #0#0;
   |HAZ SacaNivel; #0#1 = enNivel;
   |SI eswCta = 1;
       |MENAV men2;
       |ERROR;
   |FINSI;
   cuentat = #0#0;
|FPRC;


|RUTINA inf;
     #1#1 = cuentat;
     #1#0 = 000001;
|FRUT;

|RUTINA sup;
     #1#0 = 999999;
     #1#1 = cuentat;
|FRUT;

|RUTINA inf4;
     #1#1 = cuentat;
     #1#0 = 000001;
     #1#9  = "N";
     #1#11 = "N";
|FRUT;

|RUTINA sup4;
     #1#0 = 999999;
     #1#1 = cuentat;
     #1#9  = "N";
     #1#11 = "N";
|FRUT;

|PROCESO Tipo12zdato; |TIPO 12;
   |SI FSalida = 1; |O FSalida = 7;
       inkey = 1;
   |SINO;
       inkey = 0;
       |SI aParam = "";
           |ENTLINEAL #2#1, -2, 2, 17, 0, inf , sup;
       |SINO;
           |ENTLINEAL #4#1, -4, 4, 17, 0, inf4, sup4;
       |FINSI;
   |FINSI;
|FPRC;

|PROGRAMA;
     aParam = PARAMETRO; |QBF aParam;

     |SI aParam = "";
        |HAZ inicio;
     |FINSI;

     |DBASE aMas;
     aDef = aMas + "def/n43datos";
     |CARGA_DEF aDef, espej43@;
     |SI FSalida ! 0;
         |MENAV "    Error cargando def n43datos";
         |ACABA;
     |FINSI;

     |CLS;
     |CABEZA "Mantenimiento Datos Importados";
     |PINPA #0#0;

     |SI aParam = "";

         |PARA; |SI; |HACIENDO;
                |ENDATOS #2#0;
                |SI inkey ! 0; |SAL; |FINSI;
         |SIGUE;
     |SINO;
         #0#0 = ext1; |PINTA #0#0;
         #0#1 = ext2;
         #0#2 = ext3; |PINTA #0#2;
         cuentat = #0#0;
         |HAZ Tipo12zdato;
     |FINSI;

     |DESCARGA_DEF #10;
|FPRO;

|PROCESO tecla; |TIPO 11;
 nTecla = FSalida;
 nModo = (FEntrada / 100) ! 0;

 |SI nTecla = 10;
         nConta = 0;
         |DDEFECTO #10;
         |ABRE #10;
         |LEE 001#10u;
         nConta = #10#0 + 1;
         |CIERRA #10;
         #1#0 = nConta;
     |SI nModo = 1;
         |PTEC 802;
     |SINO;
         |PTEC 801;
         |PTEC 802;
         |PTEC 809;
         |PTEC 808;
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO Todos; |TIPO 3;
 #1#10 = #0#2;
 #1#1  = #0#0;
 #1#12 = #0#1;
|FPRC;

|PROCESO Miro; |TIPO 0;
 |ABRE #10;
 |DDEFECTO #10;
 #10#0 = #1#0;
 |LEE 001#10=;
 |SI FSalida = 0;
     |SI #10#1 ! #0#0;
         |MENAV "      Este Apunte no corresponde a la Cuenta Contable";
         |ERROR;
     |FINSI;
 |FINSI;
 |CIERRA #10;
|FPRC;
