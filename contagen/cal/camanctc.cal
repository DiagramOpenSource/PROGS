|FICHEROS;
   camanctc #1;
   camanctl #0,MANTE;

   camaasil #2;
   camaasic #3;

   calicont #31;

   caw00018 #995,MANTE;
|FIN;

|VARIABLES;
   &entrada = 1;

   nModoEntrada = 0;
   nSw     = 0;
   nEstado = 0;
   nCampo  = 0;
   nInkey  = 0;
   aAlfa1  = "";
   aAlfa2  = "";
   nNume1  = 0;
   nNume2  = 0;
   nSwError = 0;
   aContra  = "";
   nNuevo   = 0;
   aFichero = "";
   nUltLin  = 0;
   nSwSal   = 0;

   nDiario = 0;
   fFecha  = @;
   nDocume = 0;
   nSwPrim = 0;
|FIN;

|INCLUYE dscont_i;

|| *********************************************************
|CALCULO ElTipo12; |TIPO 12;
 nInkey = FSalida;
 |SI nInkey = 1; nSwSal = 1; |FINSI;
|FCAL;

|RUTINA inf1;
   |SI FSalida = "POSICION";
       #995#0 = #1#0;
       #995#1 = #1#1;
       #995#2 = #1#2;
   |SINO;
       #995#0 = 00;
       #995#1 = "01.01.1900";
       #995#2 = 000001;
   |FINSI;
|FRUT;

|RUTINA sup1;
   #995#0 = 99;
   #995#1 = "31.12.2999";
   #995#2 = 999999;
|FRUT;

|CALCULO Lista; |TIPO 0;
   nInkey = FSalida;
   nCampo = Campo;

   |SI nInkey = 10;

      |PUSHV 0501 2380;
      |SI nSw = 0;  |HAZ CreoLosAstos; |FINSI;
      |PINPA #0#995;
      |ENTLINEAL #1#995, -3, 5, 22, 1, inf1, sup1;
      |POPV;

      #1#0 = #995#0; |PINTA #1#0;
      #1#1 = #995#1; |PINTA #1#1;
      #1#2 = #995#2; |PINTA #1#2;

      |SI nCampo = 0; |PTEC 802; |FINSI;
      |SI nCampo = 2; |ERROR;    |FINSI;
  |FINSI;

|FCAL;

|PROCESO LeeAsto;
 nSwError = 0;
 |ABRE #3;
 #3#0 = #1#0;
 #3#1 = #1#1;
 #3#2 = #1#2;
 |LEE 001#3=;
 |SI FSalida ! 0;
     nSwError = 1;
 |FINSI;
 |CIERRA #3;
|FPRC;

|CALCULO MiroSiEsta; |TIPO 0;
 nInkey = FSalida;
 |SI nInkey = 2; |ACABA; |FINSI;

 |HAZ LeeAsto;
 |SI nSwError = 1;
     |MENAV "    No existe Asiento Contable";
     |ERROR;
 |FINSI;
|FCAL;

|| -----------------------------------
|PROCESO RellenaTempo;
   nUltLin = #2#3;
   aContra = "";

   |DDEFECTO #0;
   #0#0 = #2#0;
   #0#1 = #2#1;
   #0#2 = #2#2;
   #0#3 = #2#3;
   |LEE 001#0=;
   nEstado = FSalida;
   |SI nEstado = 0; |Y nNuevo = 0;
       |ACABA;
   |FINSI;

   #31#0 = #2#0;
   #31#1 = #2#1;
   #31#2 = #2#2;
   #31#3 = #2#3;
   |LEE 010#31=;
   |SI FSalida = 0;
       aContra = #31#4;
   |SINO;
       |SI #2#8 = "D";
           aContra = #3#10;
       |SINO;
           aContra = #3#8;
       |FINSI;
   |FINSI;

   #0#0  = #2#0;
   #0#1  = #2#1;
   #0#2  = #2#2;
   #0#3  = #2#3;
   #0#12 = #2#8;
   |SI #2#8 = "D";
       #0#4  = #2#4;
       #0#5  = #2#5;
   |SINO;
       #0#6  = #2#4;
       #0#7  = #2#5;
   |FINSI;
   #0#8  = aContra;
   eaCuenta = #0#8; |HAZ SacaNivel;
   #0#9  = enNivel;
   #0#10 = #2#7;
   #0#11 = #2#24;
   |SI nEstado ! 0; |GRABA 020#0n; |FINSI;
   |SI nEstado = 0; |GRABA 020#0a; |FINSI;
|FPRC;

|DEFBUCLE RellenaTempo;
  #2#1;
  ;
  #1#0, #1#1, #1#2, 0001;
  #1#0, #1#1, #1#2, 9999;
  e;
  NULL, RellenaTempo;
|FIN;
||-------------------------------------

|RUTINA inf;
   #0#0 = #1#0;
   #0#1 = #1#1;
   #0#2 = #1#2;
   #0#3 = 1;
|FRUT;

|RUTINA sup;
   #0#0 = #1#0;
   #0#1 = #1#1;
   #0#2 = #1#2;
   #0#3 = nUltLin;
|FRUT;

|CALCULO ElTipo3; |TIPO 3;

 |SI nSwSal = 1; |ACABA; |FINSI;
 |HAZ LeeAsto;
 |SI nSwError = 1; |ACABA; |FINSI;

 |ABRE #0;
 |ABRE #31;
 nUltLin = 0;
 nNuevo  = 0;
 |BUCLE RellenaTempo;
 |CIERRA #31;
 |CIERRA #0;
 |SI nUltLin = 0; |ACABA; |FINSI;

 |ENTLINEAL #1#0, -4,  2,  22, 0, inf, sup;
 |SI FSalida ! 0;
    |ABRE #0;
    |ABRE #31;
    nUltLin = 0;
    nNuevo  = 1;
    |BUCLE RellenaTempo;
    |CIERRA #31;
    |CIERRA #0;
 |FINSI;
|FCAL;

|| ---------------------------------------------------------------
|CALCULO cuenta; |TIPO 6;
     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;
|FCAL;

|CALCULO cuefin; |TIPO 0;
   emCta    = 1;
   eaCuenta = #0#8;
   |HAZ SacaNivel;
   |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea
   #0#9 = enNivel;

   |HAZ LeeCuenta;
   |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea
|FCAL;

|CALCULO noAlta; |TIPO 2;
 nModoEntrada = (FEntrada / 100) ! 0;
 |SI nModoEntrada ! 2;
     |ERROR;
|FINSI;
|FCAL;
|| ---------------------------------------------------------------
|| ---------------------------------------------------------------
|PROCESO VuelcaTempo;
   |SI nSwPrim = 0;
       #1#0 = #0#0;
       #1#1 = #0#1;
       #1#2 = #0#2;
       |HAZ LeeAsto;
       nSwPrim = 1;
   |FINSI;
   |SI #1#0 ! #0#0; |O #1#1 ! #0#1; |O #1#2 ! #0#2;
       #1#0 = #0#0;
       #1#1 = #0#1;
       #1#2 = #0#2;
       |HAZ LeeAsto;
   |FINSI;

   |SI #0#12 = "D"; eaCuenta = #3#10;   |FINSI;
   |SI #0#12 = "H"; eaCuenta = #3#8;    |FINSI;

   #31#0 = #0#0;
   #31#1 = #0#1;
   #31#2 = #0#2;
   #31#3 = #0#3;
   |LEE 110#31=;
   nEstado = FSalida;

   #31#4 = #0#8;
   #31#5 = #0#9;
   #31#6 = #0#11;
   |SI #31#4 ! eaCuenta;
       |SI nEstado = 0; |GRABA 020#31a; |FINSI;
       |SI nEstado ! 0; |GRABA 020#31n; |FINSI;
   |SINO;
       |SI nEstado = 0; |BORRA 020#31a; |FINSI;
   |FINSI;
   |LIBERA #31;
|FPRC;

|DEFBUCLE VuelcaTempo;
  #0#1;
  ;
  00, "01.01.0000", 000001, 0001;
  99, "31.12.2999", 999999, 9999;
  ;
  NULL,VuelcaTempo;
|FIN;

|| -----------------------------------------------------------------
|PROCESO TomaAstos;
  |ABRE #2;
  #2#0 = #3#0;
  #2#1 = #3#1;
  #2#2 = #3#2;
  #2#3 = 1;
  |LEE 001#2];
  |SI FSalida = 0; |Y #2#0 = #3#0; |Y #2#1 = #3#1; |Y #2#2 = #3#2;
      |DDEFECTO #995;
      #995#0 = #3#0;
      #995#1 = #3#1;
      #995#2 = #3#2;
      #995#3 = #2#7;
      |GRABA 020#995n;
  |FINSI;
  |CIERRA #2;
|FPRC;

|DEFBUCLE TomaAstos;
 #3#1;
 ;
 00, "01.01.0000", 000001;
 99, "31.12.2999", 999999;
 e;
 NULL, TomaAstos;
|FIN;

|PROCESO CreoLosAstos;
   |ABRE #995;
   |BUCLE TomaAstos;
   |CIERRA #995;
   nSw = 1;
|FPRC;

|| ********************************************************
|PROGRAMA;
   |HAZ inicio;
   |CLS;

   |CABEZA "Modif. Contrapartidas";

   aFichero = ("0l" + Usuario) % 108;
   |NOME_DAT #0 aFichero;
   aFichero = ("18w" + Usuario) % 108;
   |NOME_DAT #995 aFichero;

   |ABRE #0;   |CIERRA #0;   |DELINDEX #0;
   |ABRE #995; |CIERRA #995; |DELINDEX #995;

   nSw = 0;
   FSalida = 0;
   |PARA; |SI FSalida = 0; |HACIENDO;
          |PINPA #0#1;
          |PINDA #0#1;
          |ENDATOS #1#1;
          |SI nSwSal = 1;
              |SAL;
          |FINSI;
   |SIGUE;

   |CONFI "    N¨Actualizar Datos de Contrapartidas introducidos?";
   |SI FSalida = 0;
       nSwPrim = 0;
       |ABRE #31;
       |BUCLE VuelcaTempo;
       |CIERRA #31;
   |FINSI;

   |DELINDEX #995;
   |DELINDEX #0;
|FPRO;
