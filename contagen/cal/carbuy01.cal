|FICHEROS;
   carbuy01 #0;
   caivalin #1;
   camaniva #2;
   caconimp #8;
   carbum01 #304;
   caivaasi #200;

   canempre #30;
   caselem1 #998;
   caselem0 #997;
   cawdatos #900;
|FIN;

|VARIABLES;
  aAlfa1     = "";
  informe    = "carbuy01";
  nSwSel     = 0;
  SwHayDatos = 0;
  fDFechaV   = @;
  fHFechaV   = @;
  fDFechaC   = @;
  fHFechaC   = @;
  nDdepar    = 0;
  nHdepar    = 0;
  &SwLinea   = 0;

  &eSwMul   = 0;
  &espe15   = "";
  &espe16   = 0;
|FIN;

|INCLUYE dscont_i;

|| *****************************************************************
|CALCULO defectos; |TIPO 7;
  |ABRE #200;
  |DDEFECTO #200;
  |LEE 001#200p;
  |CIERRA #200;
  aAlfa1 = #200#44;
  |C_M #0#5, 1,aAlfa1; |C_M #0#6, 1,aAlfa1;
  |C_M #0#13,1,aAlfa1; |C_M #0#14,1,aAlfa1;
  |HAZ DatosEmp;

  #0#1  = fec1; |PINTA #0#1;
  #0#2  = fec2; |PINTA #0#2;
  #0#10 = fec2; |PINTA #0#10;
|FCAL;

|CALCULO Tipo0C0; |TIPO 0;
  aAlfa1 = "S";
  |SI #0#0 = "P";
      aAlfa1 = "N";
      #0#1 = "01.01.0000"; |PINTA #0#1;
      #0#2 = "01.01.0000"; |PINTA #0#2;
      #0#3 = 00;           |PINTA #0#3;
      #0#4 = 00;           |PINTA #0#4;
      #0#5 = "     ";      |PINTA #0#5;
      #0#6 = "     ";      |PINTA #0#6;
      #0#7 = 000000;       |PINTA #0#7;
      #0#8 = 000000;       |PINTA #0#8;
  |SINO;
      #0#1 = "01.01.0000";
      |SI #0#0 = "V";  #0#1 = fec1;  |FINSI;
                           |PINTA #0#1;
      #0#2 = fec2;         |PINTA #0#2;
      #0#3 = 00;           |PINTA #0#3;
      #0#4 = 99;           |PINTA #0#4;
      #0#5 = "     ";      |PINTA #0#5;
      #0#6 = "zzzzz";      |PINTA #0#6;
      #0#7 = 000000;       |PINTA #0#7;
      #0#8 = 999999;       |PINTA #0#8;
  |FINSI;
  |C_M #0#1, 1, aAlfa1; |C_M #0#2, 1, aAlfa1;
  |C_M #0#3, 1, aAlfa1; |C_M #0#4, 1, aAlfa1;
  |C_M #0#5, 1, aAlfa1; |C_M #0#6, 1, aAlfa1;
  |C_M #0#7, 1, aAlfa1; |C_M #0#8, 1, aAlfa1;
|FCAL;

|CALCULO fechad; |TIPO 0;
  |C_M #0#1, 0, aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

  |SI FSalida = 2; |ACABA; |FINSI;
  |SI #0#0 = "V";
      |SI #0#1 < fec1; |O #0#1 > fec2;
          |MENAV "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
          |ERROR;
      |FINSI;
  |FINSI;
|FCAL;

|CALCULO fechah; |TIPO 0;
  |C_M #0#2, 0, aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

  |SI FSalida = 2; |ACABA; |FINSI;
  |SI #0#0 = "V";
      |SI #0#2 < fec1; |O #0#2 > fec2;
          |MENAV "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
          |ERROR;
          |ACABA;
      |FINSI;
  |FINSI;

  |SI #0#2 < #0#1;
       |MENAV "    Fecha Erronea ! ";
       |ERROR;
   |FINSI;
|FCAL;

|CALCULO fechahc; |TIPO 0;
  |SI FSalida = 2; |ACABA; |FINSI;
  |SI #0#10 < #0#9;
       |MENAV "    Fecha Erronea ! ";
       |ERROR;
   |FINSI;
|FCAL;

|CALCULO nodepar; |TIPO 7;
   |C_M #0#19, 1, eaDepar;
   |C_M #0#20, 1, eaDepar;
|FCAL;

|PROCESO DatosEmp; |TIPO 7;
 |ABRE #8;
 |DDEFECTO #8;
 #8#0 = 1;
 |LEE 011#8=;
 |CIERRA #8;
 #0#23 = #8#7; |PINTA #0#23;
 aAlfa1 = #8#8; |C_M #0#23,1,aAlfa1;
|FPRC;

|| ******************************************************************

|PROCESO Facturas;

   |SI Haybasica = 1;
       nDepar  = #304#3; |SI nDepar = 0;  nDepar = 1;   |FINSI;
       nDdepar = #0#19;
       nHdepar = #0#20;
       |SI nDepar < nDdepar; |O nDepar > nHdepar; |ACABA; |FINSI;
   |SINO;
       |SI #304#3 < #0#19;  |O #304#3 > #0#20;  |ACABA;  |FINSI;
   |FINSI;

   |SI #0#0 = "P"; |Y #304#15 ! "01.01.0000"; |ACABA; |FINSI;
   |SI #0#0 = "V"; |Y #304#15 = "01.01.0000"; |ACABA; |FINSI;

   nSwSel = 1;
   |SI #0#0 ! "P";
       |SI #304#15 < #0#1; |O #304#15 > #0#2; nSwSel = 0; |FINSI;
       |SI #304#11 < #0#3; |O #304#11 > #0#4; nSwSel = 0; |FINSI;
       |SI #304#13 < #0#5; |O #304#13 > #0#6; nSwSel = 0; |FINSI;
       |SI #304#14 < #0#7; |O #304#14 > #0#8; nSwSel = 0; |FINSI;
   |FINSI;
   |SI #304#8 < #0#9;  |O #304#8 > #0#10; nSwSel = 0; |FINSI;
   |SI #304#4 < #0#11; |O #304#4 > #0#12; nSwSel = 0; |FINSI;
   |SI #304#6 < #0#13; |O #304#6 > #0#14; nSwSel = 0; |FINSI;
   |SI #304#7 < #0#15; |O #304#7 > #0#16; nSwSel = 0; |FINSI;
   |SI nSwSel = 0; |ACABA; |FINSI;

   SwLinea = 0; |SI #304#15 = "01.01.0000";  SwLinea = 1;  |FINSI;
   |PRINT;

   SwHayDatos = 1;
|FPRC;

|DEFBUCLE Facturas3;
   #304#3;
   ;
   enEmpresa, fDFechaV, fDFechaC, #0#17;
   enEmpresa, fHFechaV, fHFechaC, #0#18;
   ;
   NULL, Facturas;
|FIN;

|DEFBUCLE Facturas2;
   #304#2;
   ;
   enEmpresa, fDFechaC, fDFechaV, #0#17;
   enEmpresa, fHFechaC, fHFechaV, #0#18;
   ;
   NULL, Facturas;
|FIN;

|DEFBUCLE Facturas1;
   #304#1;
   8,15;
   enEmpresa, #0#17, fDFechaC, fDFechaV;
   enEmpresa, #0#18, fHFechaC, fHFechaV;
   ;
   NULL, Facturas;
|FIN;

|PROCESO HazTodo;

     |ABRE #200;
     |DDEFECTO #200;
     |LEE 000#200p;
     |CIERRA #200;

     enM1 = 1;
     enM2 = 12;
     eaIa = #30#21;
     |HAZ LeeDatosEmpresa;

     SwHayDatos = 0;

     |SI #0#24 = 1;  |BUCLE Facturas1;  |FINSI;
     |SI #0#24 = 2;  |BUCLE Facturas2;  |FINSI;
     |SI #0#24 = 3;  |BUCLE Facturas3;  |FINSI;

     |SI SwHayDatos = 1;
         |PIEINF;
     |FINSI;
|FPRC;

|CALCULO ElTipo3; |TIPO 3;

   |HAZ DirAplicSt;
   |SI Haybasica = 1; |Y #0#20 = "zz"; #0#20 = "99"; |FINSI;

   enDatos = 0; |SI #0#23 = "S"; enDatos = 1; |FINSI;
   fDFechaV = #0#1; fHFechaV = #0#2;
   fDFechaC = #0#9; fHFechaC = #0#10;
   |SI #0#0 = "P";
       fDFechaV = "01.01.0000"; fHFechaV = "01.01.0000";
   |FINSI;

   |IMPRE 0;
   |SI FSalida < 0; |ACABA; |FINSI;

   |INFOR informe;
   |SI FSalida ! 0;
       |MENAV "    No existe informe";
       |FINIMP;
       |ACABA;
   |FINSI;

   |SI #48#27 = "S";
       eSwMul = 2;
       |HAZ MultiEjercicio;
   |SINO;
       |DFICE dirfich0; |QBF dirfich0;
       |HAZ HazTodo;
   |FINSI;

   |FININF;
   |FINIMP;
|FCAL;

|| ////////////////////////////////////
|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #2 dirfich0;
   |PATH_DAT #200 dirfich0;

   |HAZ HazTodo;
|FPRC;

|PROCESO MultiEjer1;
  #caselem1#1 = #caselem0#0;
  #caselem1#2 = #caselem0#5;
  |LEE 041#caselem1.=;
  |SI FSalida = 0;
      |HAZ MultiEmp1;
  |FINSI;
|FPRC;

|DEFBUCLE MultiEjer0;
   #caselem0#1;
   ;
   00001;
   99999;
   ;
   NULL,MultiEjer1;
|FIN;

|PROCESO MultiEjercicio;
   espe15 = "S";
   |SUB_EJECUTA "caselemp";
   |NOME_DAT #998 eaFichsel;
   |NOME_DAT #997 eaFichres;

   |ABRE #998;
   |SELECT #3#998;
   |BUCLE MultiEjer0;
   |SELECT #1#998;
   |CIERRA #998;

   |DELINDEX #998;
   |DELINDEX #997;
|FPRC;
