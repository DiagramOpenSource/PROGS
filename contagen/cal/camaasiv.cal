|FICHEROS;
   camaasiv #0;
   camaniva #1;
   caivases #3;
   calibros #4;

   caivaasi;
   camasiv@ #900;
   camaref@ #901;

   casieali #12;
   casieal@ #912;
|FIN;

|VARIABLES;
   &entrada = 1;
   &ext1    = "";          || fecha
   &ext2    = "";          || libro
   &ext3    = "";          || FSalida
   &ext4    = "";          || serie
   &ext5    = "";          || factura
   &ext6    = "";          || Tipo
   &ext10   = "";          || Referencia
   &ext11   = "";          || tipo IVA
   &ext12   = "";          || N§ Fra.Proveedor
   &ext13   = 0;           || Numero de Predefinidos
   &ext21   = "";          || Departamento

   &ext38  = 1;
   &ext42  = "";
   &ext43  = "";
   &ext44  = "";

   &ext00  = "";
   &ext01  = "";
   &ext02  = "";

   &eaCobPag = "";         || Cobros/Pagos = "S/N"
   SerieAnt = "   XX";
   ComodNum = 0;

   UltFact     = 0;
   ModoEntrada = 0;
   NumFactura  = 0;
   NumFraOtro  = 0;
   SerieOtro   = "";
   Serie       = "";
   Libro       = 0;
   Comodin     = "";
   mensa1      = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
   mensa4      = "    ATENCION. FECHA FACTURA INFERIOR O IGUAL AL BLOQUEO DE DATOS";
   aAlfa1      = "";
   &NumDocum   = 0;
   &r_entra    = "";
   &r_alfa1    = "";
   swPase      = 0;
   aUsu        = 0;
   aDef        = "";
   aMas        = "";
   nExiste     = 0;
   nNume       = 0;
   aAlfa2      = "";
   aAlfa3      = "";
   aAlfa4      = "";
   nNume1      = 0;
   nNume2      = 0;
   nSumaIVA    = 0;
   nHayRec     = 0;

   aChar       = "";

   nError      = 0;
   nAntes      = 0;
   nPara1      = 0;
   nPara2      = 0;
   aParApte    = "";

   &enSi111   = 0;
   &enSi303   = 0;
   &eSwReten  = 0;
   &i_perio   = 0;
   &enEmiMod  = 0;
   &enPanMod  = 0;

   fFecha     = @;
   &enAParam  = 0;
   nSw1       = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscosi_i;
|INCLUYE modulo_i;

|| ********************************************************************
|PROCESO DefPeriodo;
   enEmiMod = 0;
   |SI ext6 = "N"; |ACABA; |FINSI;
   Comodin  = fFecha;
   Comodin  = Comodin % 0402;
   |SI enEjercicio [ 2009;
       |SI #caivaasi#46 = "T";
           |SI Comodin ] "01"; |Y Comodin [ "03"; Comodin = "01"; |FINSI;
           |SI Comodin ] "04"; |Y Comodin [ "06"; Comodin = "02"; |FINSI;
           |SI Comodin ] "07"; |Y Comodin [ "09"; Comodin = "03"; |FINSI;
           |SI Comodin ] "10"; |Y Comodin [ "12"; Comodin = "04"; |FINSI;
       |SINO;
           |SI #caivaasi#46 = "A"; Comodin = "00"; |FINSI;
       |FINSI;
  |FINSI;
  i_perio = Comodin;

  |SI enSi303 = 1; |O enSi111 = 1;
      |SI #caivaasi#167 ! "X";
          eSwReten = 0;
          enPanMod = 0;
          enPerMod = i_perio;
          |HAZ ConsulModIVA;
      |FINSI;
  |FINSI;
|FPRC;
|| ********************************************************************

|PROCESO LeoEspejo;
  aAlfa4 = #912#56; aAlfa4 = aAlfa4  > " ";

  |SI aAlfa4 = "I"; |O aAlfa4 = "R";
      nSumaIVA = nSumaIVA + #912#60;
      |SI aAlfa4 = "R"; nHayRec = 1; |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE LeoEspejo;
 #912#1002;
 ;
 ext13, 01;
 ext13, 99;
 e;
 NULL, LeoEspejo;
|FIN;

|PROCESO MiroEspejo;
  nSumaIVA = 0;
  nHayRec  = 0;
  |BUCLE LeoEspejo;
  |SI nHayRec = 1; |O nSumaIVA = nNume2;
      aAlfa2 = "";
  |FINSI;
|FPRC;

|PROCESO CambioNumero;

      |SI nAntes = 0;
          |SI nNume1 = 18;  |O nNume1 = 1.18; |O nNume1 = 0.18; aAlfa2 = "18%";  nNume2 = 18;  |FINSI;
          |SI nNume1 =  8;  |O nNume1 = 1.08; |O nNume1 = 0.08; aAlfa2 = "8%";   nNume2 = 8;   |FINSI;
          |SI nNume1 = 10;  |O nNume1 = 1.10; |O nNume1 = 0.10; aAlfa2 = "10%";  nNume2 = 10;  |FINSI;
          |SI nNume1 = 8.5; |O nNume1 = 0.85;                   aAlfa2 = "8.5%"; nNume2 = 8.5; |FINSI;

          |SI nNume1 = 21;  |O nNume1 = 1.21; |O nNume1 = 0.21; aAlfa2 = "21%";   nNume2 = 21;  |FINSI;
          |SI nNume1 = 10.5;|O nNume1 = 1.05;                   aAlfa2 = "10.5%"; nNume2 = 10.5;|FINSI;

      |FINSI;

      |SI nAntes = 1;
          |SI nNume1 = 16;  |O nNume1 = 1.16; |O nNume1 = 0.16; aAlfa2 = "16%";  nNume2 = 16;  |FINSI;
          |SI nNume1 =  7;  |O nNume1 = 1.07; |O nNume1 = 0.07; aAlfa2 = "7%";   nNume2 = 7;   |FINSI;
          |SI nNume1 =  9;  |O nNume1 = 1.09; |O nNume1 = 0.09; aAlfa2 = "9%";   nNume2 = 9;   |FINSI;
          |SI nNume1 = 7.5; |O nNume1 = 0.75;                   aAlfa2 = "7.5%"; nNume2 = 7.5; |FINSI;

          |SI nNume1 = 21;  |O nNume1 = 1.21; |O nNume1 = 0.21; aAlfa2 = "21%";   nNume2 = 21;  |FINSI;
          |SI nNume1 = 10.5;|O nNume1 = 1.05;                   aAlfa2 = "10.5%"; nNume2 = 10.5;|FINSI;
      |FINSI;

      |SI nAntes = 2;
          |SI nNume1 = 18;  |O nNume1 = 1.18; |O nNume1 = 0.18; aAlfa2 = "18%";  nNume2 = 18;  |FINSI;
          |SI nNume1 =  8;  |O nNume1 = 1.08; |O nNume1 = 0.08; aAlfa2 = "8%";   nNume2 = 8;   |FINSI;
          |SI nNume1 = 8.5; |O nNume1 = 0.85;                   aAlfa2 = "8.5%"; nNume2 = 8.5; |FINSI;

          |SI nNume1 = 16;  |O nNume1 = 1.16; |O nNume1 = 0.16; aAlfa2 = "16%";  nNume2 = 16;  |FINSI;
          |SI nNume1 =  7;  |O nNume1 = 1.07; |O nNume1 = 0.07; aAlfa2 = "7%";   nNume2 = 7;   |FINSI;
          |SI nNume1 =  9;  |O nNume1 = 1.09; |O nNume1 = 0.09; aAlfa2 = "9%";   nNume2 = 9;   |FINSI;
          |SI nNume1 = 7.5; |O nNume1 = 0.75;                   aAlfa2 = "7.5%"; nNume2 = 7.5; |FINSI;
      |FINSI;
|FPRC;

|PROCESO CambioNumeroR;

      |SI nAntes ! 2;
          |SI nNume1 = 5.20; |O nNume1 = 1.52; |O nNume1 = 0.52; aAlfa2 = "5.20%";  nNume2 = 5.20;  |FINSI;
          |SI nNume1 = 1.40; |O nNume1 = 1.14; |O nNume1 = 0.14; aAlfa2 = "1.40%";  nNume2 = 1.40;  |FINSI;
      |FINSI;

      |SI nAntes = 2;
          |SI nNume1 = 4;                     |O nNume1 = 0.40; aAlfa2 = "4.00%";  nNume2 = 4.00;  |FINSI;
          |SI nNume1 = 1;   |O nNume1 = 1.10; |O nNume1 = 0.10; aAlfa2 = "1.00%";  nNume2 = 1;     |FINSI;
      |FINSI;
|FPRC;

|PROCESO LeoPredefinidos;
  aParApte = #12#56; aParApte = aParApte  > " ";

  |SI #12#60 ! 0; |Y aParApte = "I";
      nNume1 = #12#60;
      |HAZ CambioNumero;
  |FINSI;

  |SI #12#60 ! 0; |Y aParApte = "R";
      nNume1 = #12#60;
      |HAZ CambioNumeroR;
  |FINSI;

  |SI aAlfa2 ! "";  |ERROR10;  |FINSI;
|FPRC;

|DEFBUCLE LeoPredefinidos;
  #12#1;
  ;
  ext13, 01;
  ext13, 99;
  e;
  NULL, LeoPredefinidos;
|FIN;

|CALCULO VerFechas;

  |SI ext6 = "N"; |ACABA; |FINSI;

  |DBASE aMas; |QBF aMas;
  aDef = aMas + "def/casieali";
  |CARGA_DEF aDef, casieal@;
  |SI FSalida ! 0;
      |ACABA;
  |FINSI;

  nError  = 0;
  nAntes  = 0;
  aAlfa2  = "";
  aAlfa1  = "Fecha Factura ANTERIOR a 01.07.2010 y Asto Predefinido con IVA al ";
  |SI #0#2 ] "01.07.2010"; |Y #0#2 < "01.09.2012";
      aAlfa1  = "Fecha Factura entre 01.07.2010 y 31.08.2012, y Asto Predefinido con IVA al ";
      nAntes  = 1;
  |FINSI;
  |SI #0#2 ] "01.09.2012";
      aAlfa1  = "Fecha Factura POSTERIOR a 01.09.2012 y Asto Predefinido con IVA al ";
      nAntes  = 2;
  |FINSI;

  |BUCLE LeoPredefinidos;
  |SI aAlfa2 ! "";
      aAlfa1 = "    N" + aAlfa1 + aAlfa2 + ". Continuar ";
      |CONFI aAlfa1;
      |SI FSalida ! 0;
          nError = 1;
      |FINSI;
  |FINSI;

  |DESCARGA_DEF #casieal@;
|FCAL;

|CALCULO MiraFecha; |TIPO 0;
 |C_M #0Campo,0,aAlfa1;
 |SI aAlfa1 = "N"; |ACABA; |FINSI;

 fFecha = fec1;
 |SI enSwSelFc = 1; fFecha = "01.01.0000"; |FINSI;
 |SI #0Campo < fFecha; |O #0Campo > fec2;
     |MENAV mensa1;
     |ERROR;
     |ACABA;
 |FINSI;

 |SI enSwSelFc = 0;
     |SI #0Campo [ fec3;
         |MENAV mensa4;
     |FINSI;
 |FINSI;

 nError = 0;
 |SI #caivaasi#168 ! "A";  || fecha de la factura
     fFecha = #0#2;
     |HAZ DefPeriodo;
     |SI enEmiMod = 1; |Y #caivaasi#167 = "N";
         nError = 1;
         |ERROR;
     |FINSI;
 |FINSI;
 |SI enEjercicio ] 2010; |Y nError = 0;
     |HAZ VerFechas;
     |C_M #0Campo,0,aAlfa1;
     |SI nError = 1;
         |PTEC 807;
     |FINSI;
 |FINSI;
|FCAL;

|CALCULO an_refe; |TIPO 7;
  eaSiiRef = #0#5; |QBF eaSiiRef;
  eaSiiSer = #0#1;  |QBF eaSiiSer;
  enSiiFra = #0#0;
  |HAZ NReferencia;
  #0#5 = eaSiiRef;
  |PINTA #0#5;

  |C_M #0#5, 1, "S";
  aAlfa1 = #0#5; |QBF aAlfa1;
  |SI aAlfa1 = "";
     |SI #caivaasi#116 = "B"; |C_M #0#5,1,"N"; |FINSI;
  |FINSI;
  |HAZ EsComersan;
  |SI FSalida = 0; |C_M #0#5,1,"N"; |FINSI;
|FCAL;

|CALCULO r_referen; |TIPO 6;
   |SI eaRefer = "N"; enLgRef = #caivaasi#117; |FINSI;
   |SI #caivaasi#116 ! "S"; |ACABA; |FINSI;
   r_entra = entrada;
   r_alfa1 = #0Campo;
   |HAZ r1_punto;
   #0Campo = r_alfa1;
   |PINTA #0Campo;
|FCAL;

|CALCULO CompSerie; |TIPO 7;
    || aAlfa1 = #caivaasi#44;
    || |C_M #0#1,1,aAlfa1;
|FCAL;

|CALCULO MiraSerie;
   |HAZ EsComersan;
   |SI FSalida = 0;
      |SI #0#0 ! 0; |ACABA; |FINSI;
   |FINSI;

   ModoEntrada = FEntrada $ 100;

   |SI SerieAnt ! #0#1; |Y SerieAnt ! "   XX"; swPase = 0; #0#0 = 0; |FINSI;

   |SI #0#0 = 0; |Y swPase = 0;
       |SI #caivaasi#44 = "N";
           |HAZ LeeLibros;
       |SINO;
           |HAZ LeeSerie;
       |FINSI;
       swPase = 1;
   |FINSI;
   #0#0 = NumFactura; |PINTA #0#0;
   |GRABA 040#0a;

   |SALVA_DATOS #0;
   |LEE 020#0=;
   |SI #0#0 ! NumFactura; |O #0#1 ! Serie;
       NumFraOtro = #0#0;
       SerieOtro  = #0#1;
       |REPON_DATOS #0;
   |FINSI; ||ha grabado alguien mas
|FCAL;

|| ********************************************************************
|PROCESO BuscaElDoc;
   #1#0 = ext2;
   #1#1 = 9999999999;
   |LEE 010#1];
   |SI FSalida = 0;
       |LEE 040#1a;
   |SINO;
       |LEE 040#1u;
   |FINSI;
   NumDocum = 1;
   |SI FSalida = 0; |Y #1#0 = ext2;
       NumDocum = #1#1 + 1;
   |FINSI;
|FPRC;

|PROCESO nolosaques;
   #0#0  = 0;
   ext3  = 0;
   ext4  = #0#1;
   |HAZ MiraSerie;
   ext5  = #0#0;
   ext10 = #0#5;
   nError = 0;
   |SI #caivaasi#168 ! "A";  || fecha de la factura
       fFecha = #0#2;
       |HAZ DefPeriodo;
       |SI enEmiMod = 1; |Y #caivaasi#167 = "N";
           nError = 1;
       |FINSI;
   |FINSI;
   |SI enEjercicio ] 2010; |Y nError = 0;
       |HAZ VerFechas;
   |FINSI;
|FPRC;

|PROCESO SumaSerie;
   #3#0 = Serie;
   #3#1 = ext6;
   |LEE 111#3=;
   |SI FSalida = 0;
       NumFactura = #3#3;
       #3#3 = #3#3 + #3#4;
       |GRABA 020#3a;
   |FINSI;
   |LIBERA #3;
|FPRC;

|PROCESO ActualSerie;
   #3#0 = Serie;
   #3#1 = ext6;
   |LEE 111#3=;
   |SI FSalida = 0;
       #3#3 = NumFactura + #3#4;
       |GRABA 020#3a;
   |FINSI;
   |LIBERA #3;
|FPRC;

|PROCESO RestoSerie;
   #3#0 = SerieAnt;
   #3#1 = ext6;
   |LEE 111#3=;
   |SI FSalida = 0;
       NumFactura = NumFactura + #3#4;
       |SI #3#3  = NumFactura;
           #3#3 = #3#3 - #3#4;
           |GRABA 020#3a;
       |FINSI;
   |FINSI;
   |LIBERA #3;
|FPRC;

|PROCESO LeeSerie;
   Libro = ext2;
   |ABRE #3;
   |SI SerieAnt ! #0#1; |Y SerieAnt ! "   XX";
       |HAZ RestoSerie;
   |FINSI;
   Serie = #0#1;
   |HAZ SumaSerie;

   |HAZ VeoSiVale;
   |SI #3#3 [ NumFactura;
       |HAZ ActualSerie;
   |FINSI;
   |CIERRA #3;
   SerieAnt = #0#1;
|FPRC;

|PROCESO SumoLibro;
    #4#0 = Libro;
    |LEE 111#4=;
    |SI FSalida = 0;
        NumFactura  = #4#4;
        #4#4 = #4#4 + #4#3;
        |GRABA 020#4a;
    |FINSI;
    |LIBERA #4;
|FPRC;

|PROCESO ActualLibro;
   #4#0 = Libro;
   |LEE 101#4=;
   |SI FSalida = 0;
       #4#4 = NumFactura + #4#3;
       |GRABA 020#4a;
   |FINSI;
   |LIBERA #4;
|FPRC;

|PROCESO RestoLibro;
   #4#0 = Libro;
   |LEE 101#4=;
   |SI FSalida = 0;
       NumFactura = NumFactura + #4#3;
       |SI #4#4 = NumFactura;
           #4#4 = #4#4 - #4#3;
           |GRABA 020#4a;
       |FINSI;
   |FINSI;
   |LIBERA #4;
|FPRC;

|PROCESO LeeLibros;
    |ABRE #4;
    Libro = ext2;
    Serie = ext4;
    |HAZ SumoLibro;

    |HAZ VeoSiVale;
    |SI NumFactura ] #4#4;
        |HAZ ActualLibro;
    |FINSI;
    |CIERRA #4;
|FPRC;

|PROCESO VeoSiVale;
    |ABRE #1;
    |SELECT #3#1;
    #1#0 = Libro;
    #1#2 = Serie;
    #1#3 = NumFactura;
    |LEE 010#1];
    |PARA; |SI FSalida = 0; |Y #1#2 = Serie; |Y #1#0 = Libro; |HACIENDO;
           NumFactura = #1#3 + 1;
           |LEE 010#1s;
    |SIGUE;
    |SELECT #1#1;
    |CIERRA #1;
|FPRC;

|PROCESO ParaRestar;
    |ABRE #1;
    |SELECT #3#1;
    #1#0 = Libro;
    #1#2 = Serie;
    #1#3 = 999999;
    |LEE 010#1];
    |SI FSalida = 0;
        |LEE 040#1a;
    |SINO;
       |LEE 040#1u;
    |FINSI;
    nNume = 1;
    |SI FSalida = 0; |Y #1#0 = Libro; |Y #1#2 = Serie;
         nNume  = #1#3;
    |FINSI;
    |SELECT #1#1;
    |CIERRA #1;
    |SI nNume > NumFactura;
        NumFactura = nNume;
    |FINSI;
|FPRC;

|PROCESO MiroCamaniva;
   nExiste = 0;
   |ABRE #1;
   |SELECT #3#1;
   #1#0 = ext2;
   #1#2 = #0#1;
   #1#3 = #0#0;
   |LEE 010#camaniva.=;
   |SI FSalida = 0; nExiste = 1; |FINSI;
   |SELECT #1#1;
   |CIERRA #1;
|FPRC;

|PROCESO MiroCamaasiv;
 |SI #0#7 ! #900#7;
     |SI #0#0 = #900#0; |Y #0#1 = #900#1;
         nExiste = 1;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE MiroCamaasiv;
 #900#1002;
 ;
 "        ", ext2;
 "zzzzzzzz", ext2;
 ;
 NULL, MiroCamaasiv;
|FIN;

|PROCESO MiraNumero;
   |SI FSalida = 2; |ACABA; |FINSI;
   |HAZ MiroCamaniva;
   |SI nExiste = 1;
       |MENAV "0400La factura ya existe";
       |ERROR;
       #0#0 = 0; |PINTA #0#0;
       |GRABA 040#0a;
       |LEE 040#0=;
       |ACABA;
   |FINSI;

   nExiste = 0;
   |BUCLE MiroCamaasiv;
   |SI nExiste = 1;
       |MENAV "0400La Factura ya existe";
       |ERROR;
       #0#0 = 0; |PINTA #0#0;
       |GRABA 040#0a;
       |LEE 040#0=;
       |ACABA;
   |FINSI;

   NumFactura = #0#0;
   |GRABA 040#0a;
   |LEE 040#0=;
|FPRC;

|CALCULO AntesDeparT; |TIPO 7;
   |C_M #camaasiv#13,0,aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   |SI nSw1 = 0;
       |PTEC 802;
       nSw1 = 1;
   |FINSI;
|FCAL;

|CALCULO TrasDeparT; |TIPO 0;
   |C_M #camaasiv#13,0,aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   eOpDep = 0;                            || 1 = Consulta pantalla
   |SI FSalida = 10; eOpDep = 1;  |FINSI;
   enActividad = #camaasiv#13;
   eaCodDep    = #camaasiv#13;
   |HAZ Actividad;
   |SI eswLect = 1;
       |MENAV "    Error.! No Existe Actividad / Departamento";
       |ERROR;
   |SINO;
       eaNombreAct = (eaNombreAct % 129);
       #camaasiv#13 = eaCodDep; |PINTA #camaasiv#13;
       |ATRI S; |PINTA 0930, eaNombreAct; |ATRI 0;
       |SI efActFin > "01.01.1900"; |Y #caivaasi#119 = "S";
           aAlfa1 = "    NAtencion!!!. Actividad con Fecha de BAJA " + efActFin + ", Desea introducir Facturas ? ";
           |CONFI aAlfa1;
           |SI FSalida ! 0;
               |ERROR;
               |ACABA;
           |FINSI;
       |FINSI;
       |SI eaEsCom = "S";
           |CONFI "    NAtencion!!!. Actividad Comunera, ¨ Desea introducir Facturas ? ";
           |SI FSalida ! 0;
               |ERROR;
           |FINSI;
       |FINSI;
   |FINSI;

   |SI nSw1 = -1;
       nSw1 = 0;
   |FINSI;
|FCAL;

|PROCESO PedirDepar;
    |C_V #camaasiv#13, 1, "S";
    |C_M #camaasiv#13, 1, "S";
    #camaasiv#13 = #caivaasi#19;

    nSw1 = -1;
    |PINPA #0#camaasiv;
    |BLANCO 0820 1060;
    |CUADRO 0820 1059;
    |PINTA 0921, " Dep:"; |ATRI 0;
    |PINTA 0927, #camaasiv#13;
    |ENCAMPO #13#camaasiv;
    |SI FSalida = 7; |PTEC 806; |ACABA; |FINSI;
    enAParam = #camaasiv#13;
    |HAZ eActivParam;
|FPRC;

|PROGRAMA;
   |DBASE aMas; |QBF aMas;
   aDef = aMas + "def/camaasiv";
   |CARGA_DEF aDef, camasiv@;
   |SI FSalida ! 0;
       |MENAV "    Error cargando camaasiv ";
       ext3 = "-1";
       |ACABA;
   |FINSI;

   |HAZ eParaModelos;

   |HAZ EsComersan;
   |SI FSalida = 0;
      |DBASE aMas; |QBF aMas;
      aDef = aMas + "def/camanref.mas";
      |CARGA_DEF aDef, camaref@;
      |SI FSalida ! 0;
         |DESCARGA_DEF #camasiv@;
         |MENAV "    Error cargando camanref ";
         |ACABA;
      |FINSI;
   |FINSI;

   aUsu = Usuario;
   |ABRE #0;
   |DDEFECTO #0;
   #0#7 = aUsu;
   #0#6 = ext2;
   |LEE 010#0=;
   |SI FSalida ! 0;
      |DDEFECTO #0;
      #0#7 = aUsu;
      #0#6 = ext2;
      #0#0 = 0;
      #0#8 = "";
      |GRABA 020#0n;
      |LEE 010#0=;
   |FINSI;
   |SI #0#0 ! 0;
       SerieOtro  = #0#1;
       NumFraOtro = #0#0;
       #0#0       = 0;
   |FINSI;
   #0#8 = "";

   |ABRE #4;
   |DDEFECTO #4;
   #4#0 = ext2;
   |LEE 000#4=;
   |CIERRA #4;

   ext6 = #4#2;
   |SI #4#2 = "R"; |CABEZA "Numeracion Iva Repercutido"; |FINSI;
   |SI #4#2 = "S"; |CABEZA "Numeracion Iva Soportado";   |FINSI;
   |SI #4#2 = "N"; |CABEZA "Numeracion Registro sin Iva"; |FINSI;
   aTipoIVA = #4#2;

   #0#3  = ext6;  || Tipo Factura
   #0#5  = ext10; || Campo Referencia
   #0#1  = ext4;  || Serie
   #0#2  = ext1;  || fecha del asiento por defecto
   #0#9  = ext42; || Clave 340
   #0#10 = ext43; || primer numero
   #0#11 = ext44; || ultimo numero
   #0#12 = ext38; || numero de facturas

   |ABRE #caivaasi;
   |DDEFECTO #caivaasi;
   |LEE 040#caivaasi.p;
   |CIERRA #caivaasi;
   |SI #caivaasi#187 = "S";
       |HAZ PedirDepar;
   |FINSI;

   |SI #caivaasi#168 = "A";  || fecha del asiento
       fFecha = ext1;
       |HAZ DefPeriodo;
       |SI enEmiMod = 1; |Y #caivaasi#167 = "N";
           ext3 = "-1";
           |ACABA;
       |FINSI;
   |FINSI;

   |HAZ EsComersan;
   |SI FSalida = 0;
      aChar    = #caivaasi#97;
      aAlfa2   = ext10; |QBF aAlfa2;
      aAlfa3   = aAlfa2 % -101; aAlfa2 = aAlfa2 % -299;
      |PARA; |SI aAlfa2 ! ""; |Y aAlfa3 ! aChar; |HACIENDO;
         aAlfa3 = aAlfa2 % -101; aAlfa2 = aAlfa2 % -299;
      |SIGUE;
      |SI aAlfa2 ! "";
         aAlfa3 = aAlfa2;
         aAlfa2  = ext10; |QBF aAlfa2;
         aAlfa2 = aAlfa2 - aAlfa3; aAlfa2 = aAlfa2 % 0299;
         #0#0 = aAlfa2;
         #0#1 = aAlfa3;
      |FINSI;
   |FINSI;

   |SI #caivaasi#25 = "N";  |Y #caivaasi#26 = "N"; |Y eaRefer = "S";
       |SI #0#9 ! "A"; |Y #0#9 ! "B";
           |HAZ nolosaques;
           |SI nError = 1; ext3 = "-1"; |FINSI;
           |VETE Fin;
       |FINSI;
   |SINO;
       |SI #caivaasi#26 = "N"; |Y  enEjercicio ] 2010;
           |HAZ VerFechas;
           |SI nError = 1;
               ext3 = "-1";
              |VETE Fin;
           |FINSI;
        |FINSI;
   |FINSI;

   Comodin = #caivaasi#25; |C_M #0#0,1,Comodin;
                      |C_M #0#1,1,Comodin;
                      |SI #caivaasi#44 = "N"; |C_M #0#1,1,"N"; |FINSI;
   Comodin = #caivaasi#26; |C_M #0#2,1,Comodin;

   |PINPA #0#0;
   |SI #0#9 = "A"; |O #0#9 = "B";
       |CUADRO 1020 2059;
       |PINTA 1721, "³Clave 340..:         Num.Fras:      ³³";
       |PINTA 1821, "³Primer Num.:                        ³³";
       |PINTA 1921, "³Ultimo Num.:                        ³³";
       |C_V #0#9, 1, "S";
       |C_V #0#10, 1, "S"; |C_M #0#10, 1, "S";
       |C_V #0#11, 1, "S"; |C_M #0#11, 1, "S";
       |C_V #0#12, 1, "S"; |C_M #0#12, 1, "S";
   |FINSI;

   |SI #caivaasi#25 = "N"; |HAZ MiraSerie; |FINSI;
   |HAZ EsComersan;
   |SI FSalida = 0; |Y #4#2 = "S";
      |SI eaCobPag = "S";
         |PINTA 1622, "N§ Fra.Prov.:";
         |C_M #camaasiv#8, 1, "S"; |C_V #camaasiv#8, 1, "S";
      |SINO;
         |PINTA 1622, "             ";
         |C_M #camaasiv#8, 1, "N"; |C_V #camaasiv#8, 1, "N";
      |FINSI;
   |SINO;
      |PINTA 1622, "             ";
      |C_M #camaasiv#8, 1, "N"; |C_V #camaasiv#8, 1, "N";
   |FINSI;

   |PINDA #0#0;
   |ENDATOS #1#0;
   ext3 = FSalida;
   |SI nError = 1; ext3 = "-1"; |FINSI;

   |SI ext3 ! 0; NumFactura = 0; |FINSI;
   |HAZ ParaRestar;
   |SI #caivaasi#44 = "N";
           |ABRE #4;
           |HAZ ActualLibro;
           |CIERRA #4;
   |SINO;
           |ABRE #3;
           |HAZ ActualSerie;
           |CIERRA #3;
   |FINSI;

   |ET Fin;

   ext1  = #0#2;         || fecha
   ext4  = #0#1;         || serie  (inhibida)
   ext5  = #0#0;         || factura
   ext10 = #0#5;         || referencia
   ext21 = #0#13;        || departamento
   ext42 = #0#9;
   ext43 = #0#10;
   ext44 = #0#11;
   ext38 = #0#12;
   #0#0  = 0;
   |GRABA 020#0a;
   |CIERRA #0;

   |SI ext3 = 0;
       |ABRE #1;
       |SELECT #1#1;
       |HAZ BuscaElDoc;
       |DDEFECTO #1;
       #1#0 = ext2;
       #1#1 = NumDocum;
       #1#2 = ext4;
       #1#3 = ext5;
       #1#7 = ext00;
       #1#8 = ext01;
       #1#9 = ext02;
       #1#36 = ext11;
       |GRABA 000#1n;
       |CIERRA #1;

       |HAZ EsComersan;
       |SI FSalida = 0; |Y #4#2 = "S";
          |SI eaCobPag = "S";
             |ABRE #camaref@;
             #camaref@#0 = ext2;
             #camaref@#1 = NumDocum;
             #camaref@#2 = #camaasiv#8;
             |GRABA 020#camaref@.n;
             |CIERRA #camaref@;
          |FINSI;
       |FINSI;
   |FINSI;

   |SI aDef ! "";
      |HAZ EsComersan;
      |SI FSalida = 0;
         |DESCARGA_DEF #camaref@;
      |FINSI;
      |DESCARGA_DEF #900;
   |FINSI;
|FPRO;
