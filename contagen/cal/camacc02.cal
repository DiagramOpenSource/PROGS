|FICHEROS;
  camaniva #0;
  caivalin #1;

  catmpasi #14;

  camaccw1 #599;

  camacc01 #501;
  camacc02 #502,MANTE;
  camacc03 #503;
|FIN;

|VARIABLES;
  nLinea  = 0;
  nDLinea = 0;
  nHLinea = 0;
  nNumero = 0;
  aAlfa   = "";
  aAlfa1  = "";
  aAlfa2  = "";
  fFecha1 = @;
  nNume1  = 0;
  nNume2  = 0;
  nNume3  = 0;
  nTp     = 0;
  nModo   = 0;

  nSuma1    = 0;
  nSuma2    = 0;
  nSuma3    = 0;
  nNumBase  = 0;
  nLineaUlt = 0;
  nSwOp     = 0;
  &eSwCambio = 0;
  &ePrograma = 0;
  nMultiplo  = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoiv_i;

|| ///////////////////////////
|CALCULO Tipo12CC02; |TIPO 12;
  || Para que no pregunte conforme
  |SI nModo = 1; eSwCambio = 1; |FINSI;
|FCAL;

|CALCULO Tipo7CC02; |TIPO 7;
   |BLIN 4;
   |SI amCCaja = "M"; |O ePrograma = 1; |O eModoCaja = 4;
       |SI #502#5 ! 0;
           |MENSA "    Pulse <F2> para Consultar Asiento";
       |FINSI;
   |SINO;
       |SI #502#5 ! 0;
           |MENSA "    Pulse <F2> Consultar Asto, <F3> Modificar Registro, <F4> Baja Registro";
       |SINO;
           |MENSA "    <F3> Modificar Registro, <F4> Baja Registro";
       |FINSI;
   |FINSI;
|FCAL;

|CALCULO Tipo0CC02; |TIPO 0;
     |BLIN 4;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1; |Y #599#16 = #599#10;
         aAlfa = "    NFactura cobrada. Desea dar de alta otra linea";
         |SI #599#13 = "S";
             aAlfa = "    NFactura pagada. Desea dar de alta otra linea";
         |FINSI;
         |CONFI aAlfa;
         |SI FSalida ! 0;
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;
     |SI nModo = 1; |Y #502#2 = 1;
         #502#4 = #599#4; |PINTA #502#4;
         aAlfa1 = #502#4; aAlfa1 = aAlfa1 % 0402;
         #502#7 = aAlfa1; |PINTA #502#7;
     |FINSI;
|FCAL;

|PROCESO LosTotales;
      #599#16 = #599#16 + (nMultiplo * #502#10);
      #599#18 = #599#18 + (nMultiplo * #502#13);
      #599#19 = #599#19 + (nMultiplo * #502#11);
      #599#20 = #599#20 + (nMultiplo * #502#12);

      #501#6  = #501#6  + (nMultiplo * #502#11);
      #501#7  = #501#7  + (nMultiplo * #502#12);
      #501#12 = #501#12 + (nMultiplo * #502#10);
      #501#16 = #501#16 + (nMultiplo * #502#13);

      |SI ePrograma = 0;
         |PINTA 2237, #599#16;
         |PINTA 2253, #599#18;
      |FINSI;
|FPRC;

|CALCULO Tipo2CC02; |TIPO 2;

     nModo = (FEntrada / 100) ! 0;
     |SI #502#18 ! -1; |Y nModo = 3;
         |MENAV "    Movimiento de otro Ejercicio. No puede borrarlo";
         |ERROR;
         |ACABA;
     |FINSI;

     |SI nModo = 3;
         |SI #502#5 ! 0;
             |MENAV "    Movimento contabilizado, si lo da de baja no se borrara el asiento.";
             |CONFI "    NContinuar";
             |SI FSalida ! 0;
                 |ERROR;
                 |ACABA;
             |FINSI;
         |FINSI;
      |FINSI;

      nMultiplo = 0 +. 1;
      |HAZ LosTotales;
|FCAL;

|PROCESO Borracc03
   |BORRA 020#503a;
   |LIBERA #503;
|FPRC;

|DEFBUCLE Borracc03;
    #503#1;
    ;
    #502#17, #502#0, #502#1, #502#2, 01;
    #502#17, #502#0, #502#1, #502#2, 99;
    be;
    NULL, Borracc03;
|FIN;

|CALCULO Tipo1CC02; |TIPO 1;
     nModo = (FEntrada / 100) ! 0;

     |SI #502#18 ! -1;
         aAlfa =  "    Movimiento de otro Ejercicio. No puede ";
         |SI nModo = 2; aAlfa = aAlfa + "modificarlo"; |FINSI;
         |SI nModo = 3; aAlfa = aAlfa + "borrarlo"; |FINSI;
         |MENAV aAlfa;
         |ERROR;
         |ACABA;
     |FINSI;

     |SI nModo = 3;
         |BUCLE Borracc03;
     |FINSI;
|FCAL;

|PROCESO ModiPan_4;
   |SI eaSubde = "N";
        |C_A #14#7, 1, 80;
        |C_P #14#21,1, 1467;
        |C_V #14#28,1, "N";
        |PINTA 1162, "ÄÄÄÄÂÄÄ";
        |PINTA 1262, "    ³  ";
        |PINTA 1362, "ÄÄÄÄÅÄÄ";
        |PINTA 1462, "    ³  ";
        |PINTA 1562, "    ³  ";
        |PINTA 1662, "    ³  ";
        |PINTA 1762, "    ³  ";
        |PINTA 1862, "    ³  ";
        |PINTA 1962, "    ³  ";
        |PINTA 2062, "    ³  ";
        |PINTA 2162, "    ³  ";
        |PINTA 2262, "    ³  ";
        |ATRI R; |PINTA 1262, "    "; |PINTA 1267, "Dp"; |ATRI 0;
   |FINSI;
|FPRC;

|RUTINA Astoinf;
   #14#0 = #502#3;
   #14#1 = #502#4;
   #14#2 = #502#5;
   #14#3 = 0001;
   |SI FSalida = "POSICION";
       #14#3 = #502#6;
   |FINSI;
|FRUT;

|RUTINA Astosup;
   #14#0 = #502#3;
   #14#1 = #502#4;
   #14#2 = #502#5;
   #14#3 = 9999;
|FRUT;

|CALCULO Tipo11CC02; |TIPO 11;

     |SI FSalida = 10;
        |SI #502#5 ! 0;
            ePerioCaja = #502#18;
            |HAZ ePathCaja;
            |CIERRA #14;
            |PATH_DAT #14 ePathCaja;

            |BLIN 4;
            |PUSHV 05012380;
            |SI enSwAna = 1;
                |C_V #14#26,1,"S";
                |C_V #14#29,1,"S";
            |FINSI;
            |PINPA #0#14; |HAZ ModiPan_4;
            |ENTLINEAL #1#14,-4,4,22,0,Astoinf,Astosup;
            |POPV;
        |FINSI;
        |ERROR;
     |FINSI;

     |SI eModoCaja = 4;
         |SI FSalida = 11; |O FSalida = 12;
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;

     |SI FSalida = 11;
         |SI amCCaja = "A"; |Y ePrograma = 0;

             |LEE 141#502=;

             aAlfa1 = "N";
             |SI #502#5 = 0;  aAlfa1 = "S";  |FINSI;
             |C_M #502#4,1,aAlfa1;
             |C_M #502#10,1,aAlfa1;

             nMultiplo = -1; |HAZ LosTotales;

          |ET campo4;
             |ENCAMPO #4#502;

          |ET campo7;
             |ENCAMPO #7#502;
             |SI FSalida = 2; |VETE campo4; |FINSI;

          |ET campo10;
              |ENCAMPO #10#502;
              |SI FSalida = 2; |VETE campo7; |FINSI;

          |ET campo14;
             |ENCAMPO #14#502;
             |SI FSalida = 2;
                 |SI #502#5 = 0; |VETE campo10; |FINSI;
                 |SI #502#5 ! 0; |VETE campo7;  |FINSI;
             |FINSI;

             |ENCAMPO #15#502;
             |SI FSalida = 2; |VETE campo14; |FINSI;
             |SI FSalida = 0;

                 nMultiplo = 1; |HAZ LosTotales;
                 |GRABA 020#502a;
             |FINSI;
             |LIBERA #502;

             |PONCONTROL 23, "SI";
         |FINSI;
         |ERROR;
     |FINSI;

     |SI FSalida = 12;
        |SI ePrograma = 0;

            |LEE 141#502=;
            |SI #502#5 ! 0;
                |MENAV "2400Movimiento Contabilizado, si lo da de baja no se borrara el asiento.";
                aAlfa1 = "2400NDesea Continuar con la Baja del Registro";
            |SINO;
                aAlfa1 = "2400NDesea dar de Baja el Registro";
            |FINSI;
            |CONFI aAlfa1;
            |SI FSalida ! 0;
                |LIBERA #502;
                |ERROR;
                |ACABA;
            |FINSI;

            nMultiplo = 0 - 1; |HAZ LosTotales;
            nModo = 3; |BUCLE Borracc03;
            |BORRA 020#502a;
            |LIBERA #502;

            |PONCONTROL 23, "SI";
        |FINSI;
    |FINSI;
|FCAL;

|CALCULO Tipo0CC0204; |TIPO 0;   || Fecha

   |C_M #502Campo,0,aAlfa;
   |SI aAlfa = "N"; |ACABA; |FINSI;

   fFecha1 = Contenido;
   |SI #502#4 < #599#4; |Y fFecha1 ! #502#4;
       aAlfa = "    NError. Fecha del cobro inferior a la Fecha de la Factura. Continuar";
       |SI #599#13 = "S";
           aAlfa = "    NError. Fecha del pago inferior a la Fecha de la Factura. Continuar";
       |FINSI;
       |CONFI aAlfa;
       |SI FSalida ! 0;
           |ERROR;
           |ACABA;
       |FINSI;
   |FINSI;

   aAlfa1 = #502#4;
   aAlfa1 = aAlfa1 % 0402;
   |SI fFecha1 ! #502#4;
       #502#7 = aAlfa1; |PINTA #502#7;
   |FINSI;
|FCAL;

|CALCULO Tipo7CC0210; |TIPO 7;
   aAlfa = "    Pulse <F2> para cuadrar Importe Total ";
   |SI #599#13 = "S";
       aAlfa = aAlfa + "pagado";
   |SINO;
       aAlfa = aAlfa + "cobrado";
   |FINSI;
   |MENSA aAlfa;
|FCAL;

|CALCULO Tipo0CC0210; |TIPO 0;   || Total Cobrado.
   |C_M #502Campo,0,aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   |SI FSalida = 10;
       #502#10 = #599#10 - #599#16; |PINTA #502#10;
       #502#13 = 100 - #599#18;
   |FINSI;

   nNume1 = (#599#16 + #502#10) ! 2;
   |SI nNume1 > #599#10; |Y #599#10 > 0;
       |MENAV "1234Error, Importe superior al Total Factura";
       |ERROR;
       |ACABA;
   |FINSI;

   |SI nNume1 < #599#10; |Y #599#10 < 0;
       |MENAV "1234Error, Importe superior al Total Factura";
       |ERROR;
       |ACABA;
   |FINSI;

   nNume1 = Contenido;
   |SI nNume1 = #502Campo;
       |ACABA;
   |FINSI;

   nNume1 = (#599#16 + #502#10) ! 2;
   nNume3 = (#599#8 + #599#9) ! 2; ||sumo base + iva factura
   eSwCambio = 1;
   |SI #599#10 = #502#10;
       nTp = 100;
       #502#11 = #599#8;
       #502#12 = #599#9;
   |SINO;
       nTp   = ((#502#10 * 100) / #599#10) ! 4;
       #502#11 = (#599#8 % nTp) ! 2;
       #502#12 = (#599#9 % nTp) ! 2;
       |SI nNume1 = #599#10;
           nTp = (100 - #599#18) ! 4;
           #502#11 = #599#8 - #599#19;
           #502#12 = #599#9 - #599#20;
        |FINSI;
        |SI nNume3 = #599#10;   ||no hay IRPF
            nNume2 = #502#11 + #502#12;
            |SI nNume2 ! #502#10;     ||para cuadrar lo mas posible
                  #502#12 = (#502#12 + (#502#10 - nNume2)) ! 2;
            |FINSI;
       |FINSI;
   |FINSI;
   #502#13 = nTp;
|FCAL;
