|FICHEROS;
  :/contagen/def/ca8m0000 #0;
  :/contagen/def/ca8m0006 #1,MANTE;
  :/contagen/def/ca8m0007 #2,MANTE;

  ca8mtlin #11;
  ca8mtcol #12;

  ca8zso02 #10;

  ca8w0001 #50,MANTE;
|FIN;

|VARIABLES;
 aParam    = "";
 &nBalNum  = 0;
 &nBalance = 0;
 nInkey    = 0;
 aAlfa    = "";
 aAlfa1    = "";
 aAlfa2    = "";
 aAlfa3    = "";
 nNumero   = 0;
 &eaExtension = "";
   &enDPlan     = 0;
   &wPatOfi     = 0;
   &wTipoPat    = 0;
|FIN;

|INCLUYE dscont_i;

|| ******************************************** Calculos Pantalla
|CALCULO wTipo12; |TIPO 12;
|FCAL;

|CALCULO descri;  |TIPO 0;
  nInkey = FSalida;
  |SI nInkey = 1; nInkey =  -1; |ACABA; |FINSI;
  |SI nInkey = 10;
      |HAZ CopiarNum1;
      |ERROR;
      |ACABA;
  |FINSI;

  enDPlan  = #10#4;
  nBalNum  = #10#2;
  wTipoPat = 0;
  |HAZ eVerEsPatron;
  |SI wPatOfi = 1;
         |CONFI "    NCodigo de Patron Oficial. Continuar S/N";
         |SI FSalida ! 0;
             |ERROR;
             |ACABA;
         |FINSI;
  |FINSI;

  |SI nInkey = 11;
      |CONFI "    NConfirmacion baja(S/N) ...";
      |SI FSalida = 0;
          |HAZ Borrar;
          |ERROR;
      |FINSI;
  |FINSI;
|FCAL;

|| /////////////////////// PROCESOS DE TRABAJO \\\\\\\\\\\\\\\\\\\\
|RUTINA min;
  #1#34 = #10#4;
  #1#0  = #10#2;
  #1#1  = 001;
|FRUT;

|RUTINA max;
  #1#34 = #10#4;
  #1#0  = #10#2;
  #1#1  = 999;
|FRUT;

|PROCESO Tipo3;  |TIPO 3;
  |ENTLINEAL #1#1, -3, 2, 22, 0, min, max;
|FPRC;
|| **************************************************************************

|PROCESO BorroCtas;
 |BORRA 140#2a;
 |LIBERA #2;
|FPRC;

|DEFBUCLE BorroCtas;
 #2#1;
 ;
 #1#34, #1#0, #1#1, 01, 001;
 #1#34, #1#0, #1#1, 99, 999;
 be;
 NULL, BorroCtas;
|FIN;

|PROCESO BorroLineas;
 |BORRA 140#1a;
 |LIBERA #1;
 |BUCLE BorroCtas;
|FPRC;

|DEFBUCLE BorroLineas;
 #1#1;
 ;
 #10#4, nNumero, 001;
 #10#4, nNumero, 999;
 be;
 NULL, BorroLineas;
|FIN;

|PROCESO Borrar;
 nNumero = #10#2;
 |BUCLE BorroLineas;
 |SI nNumero ! 0; |Y nNumero ! 1; |Y nNumero ! 10;
     |Y nNumero ! 30; |Y nNumero ! 31; |Y nNumero ! 32;
     nBalNum = nNumero;
     |HAZ eBorraCa8m0000;
 |FINSI;
 |PINPA #0#10;
 #10#2 = 0;
 #10#3 = "";
 |PINDA #0#10;
|FPRC;

|| ****************** PROCESO para copiar a¤os
|PROCESO CopiaCtas;
     |SALVA_FICHA #2;
     #2#0 = #50#1;
     |GRABA 020#2n;
     |REPON_FICHA #2;
     |LIBERA #2;
|FPRC;

|DEFBUCLE CopiaCtas;
 #2#1;
 ;
 #1#34, #1#0, #1#1, 01, 001;
 #1#34, #1#0, #1#1, 99, 999;
 e;
 NULL, CopiaCtas;
|FIN;

|PROCESO CopiarLin;
     |SALVA_FICHA #1;
     #1#0 = #50#1;
     |GRABA 020#1n;
     |REPON_FICHA #1;
     |LIBERA #1;
     |BUCLE CopiaCtas;
|FPRC;

|DEFBUCLE CopiaLineas;
     #1#1;
     ;
     #10#4, #50#0, 001;
     #10#4, #50#0, 999;
     be;
     NULL, CopiarLin;
|FIN;

|PROCESO LeeDefinicion;
 |SI Campo = 1; |Y #50#1 = #50#0;
     |MENAV "    Error, origen y destino igual";
     |ERROR;
     |ACABA;
 |FINSI;

 |SI Campo = 1;
     enDPlan  = #10#4;
     nBalNum  = #50#1;
     wTipoPat = 0;
     |HAZ eVerEsPatron;
     |SI wPatOfi = 1;
          |CONFI "    NCodigo de Patron Oficial. Continuar S/N";
          |SI FSalida ! 0;
              |ERROR;
              |ACABA;
          |FINSI;
     |FINSI;
 |FINSI;

 |ABRE #0;
 #0#0 = #50Campo;
 |LEE 101#0=;
 |SI FSalida = 0;
     |SI Campo = 0; #50#2 = #0#1; |PINTA #50#2; |FINSI;
     |SI Campo = 1;
         #50#3 = #0#1; |PINTA #50#3;
         |CONFI "    NYa existe Definicion, Continuar ";
         |SI FSalida ! 0;
             |ERROR;
         |FINSI;
     |FINSI;
 |SINO;
     |SI Campo = 1; #50#3 = #50#2; |PINTA #50#3; |FINSI;
     |SI Campo = 0;
         |MENAV "    No existe definicion Origen ";
         |ERROR;
     |FINSI;
 |FINSI;
 |LIBERA #0;
 |CIERRA #0;
|FPRC;

|CALCULO CopiarNum1;

 |PUSHV 0809 1457;
 |PINPA #0#50;
 #50#0 = #10#2;
 #50#1 = #10#2 + 1; |SI #50#1 > 0; #50#1 = 1; |FINSI;
 #50#4 = "Definicion de Balances";
 |PINDA #0#50;
 |ENDATOS #1#50;
 |SI FSalida ! 0;
     |POPV;
     |ACABA;
 |FINSI;

 |ABRE #1;
 |DDEFECTO #1;
 #1#34= #10#4;
 #1#0 = #50#0;
 #1#1 = 001;
 |LEE 001#1];
 |SI FSalida ! 0; |O #1#0 ! #50#0; |O #1#34 ! #10#4;
     |MENAV "    Copia cancelada. No existe Tabla Origen ...";
     |POPV;
     |CIERRA #1;
     |ACABA;
 |FINSI;

 |DDEFECTO #1;
 #1#34= #10#4;
 #1#0 = #50#1;
 #1#1 = 001;
 |LEE 101#1];
 |SI FSalida = 0; |Y #1#0 = #50#1; |Y #1#34 = #10#4;
     |AVISO;
     |CONFI "    NRegrabar Tabla Destino YA EXISTENTE (S/N): ";
     |SI FSalida ! 0;
         |MENAV "    Copia cancelada ...";
         |POPV;
         |CIERRA #1;
         |ACABA;
     |SINO;
         |CIERRA #1;
         nNumero = #1#0;
         |BUCLE BorroLineas;
     |FINSI;
 |SINO;
     |CIERRA #1;
 |FINSI;

 |ABRE #0;
 #0#0 = #50#1;
 |LEE 101#0=;
 |SI FSalida ! 0;
     #0#1 = #50#3;
     |GRABA 020#0n;
 |SINO;
     #0#1 = #50#3;
     |GRABA 020#0a;
 |FINSI;
 |LIBERA #0;
 |CIERRA #0;

 |BUCLE CopiaLineas;
 |POPV;

 #10#2 = #50#1;
 #10#3 = #50#3;
 |PTEC 809;
 |PTEC 808;
|FCAL;

|| **************************************************************************
|PROGRAMA;
   aParam = PARAMETRO; |QBF aParam;
   |SI aParam = "";
       |HAZ inicio;
   |SINO;
        enEjerBal = aParam; |SI enEjerBal < 2008; enEjerBal = 2008; |FINSI;
   |FINSI;

   |HAZ DirAplicSt;

   |SI HaySociedad = 1;

       aAlfa = enEjerBal;
       aAlfa = ":contasoc/soda8z02;" + aAlfa;
       |SUB_EJECUTA_NP aAlfa;

   |SINO;
     || ... Tablas estandar que necesita
         |ABRE #11;
         |LEE 001#11p;
         |SI FSalida ! 0;
             |CIERRA #11;
             eaPrograma  = "ca8mtlin";
             eaExtension = "gen";
             |SUB_EJECUTA_NP ":basica/dspatron";
         |FINSI;
         |CIERRA #11;
         |ABRE #12;
         |LEE 001#12p;
         |SI FSalida ! 0;
             |CIERRA #12;
             eaPrograma  = "ca8mtcol";
             eaExtension = "gen";
             |SUB_EJECUTA_NP ":basica/dspatron";
         |FINSI;
         |CIERRA #12;
         |ABRE #1;
         |LEE 001#1p;
         |SI FSalida ! 0;
             |CIERRA #1;
             eaPrograma  = "ca8m0006";
             eaExtension = "gen";
             |SUB_EJECUTA_NP ":basica/dspatron";
             eaPrograma  = "ca8m0007";
             eaExtension = "gen";
             |SUB_EJECUTA_NP ":basica/dspatron";
         |FINSI;
         |CIERRA #1;
    || ......................................................................

       |DDEFECTO #10;
       #10#0 = 0;
       #10#1 = "ESTADO TOTAL DE CAMBIOS EN EL PATRIMONIO NETO";

       |CLS;
       |CABEZA "Defincion de Balances";
       |PINPA #0#10;

       #10#4 = enEjerBal;
       |PINDA #0#10;

       FSalida = 1;
       |PARA; |SI 0 < FSalida; |HACIENDO;
              |ENDATOS #1#10;
              |SI 0 [ FSalida;
                  |SI nInkey = -1; |SAL; |FINSI;
                  |HAZ Tipo3;
                  FSalida = 1;
              |FINSI;
       |SIGUE;
   |FINSI;
|FPRO;
