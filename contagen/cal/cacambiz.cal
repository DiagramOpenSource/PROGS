|FICHEROS;
   cacambiz #0;
   cacambix #10;

   Refere1@ #1000;
   Refere2@ #1001;
   Refere3@ #1002;
|FIN;

|VARIABLES;
   aAlfa       = "";
   aAlfa1      = "";
   aFichero    = "";
   aPathDef    = "";
   aPathFich   = "";

   fDFechaPer  = @;
   fHFechaPer  = @;

   nCampo      = 0;
   nCampoFecha = 0;
   nSwDsPrevex = 0;
   nSwCab      = 0;
   nSwPasa     = 0;

   nHandle     = 0;
   nCalc       = 0;
|FIN;

|PROCESO T00EmpGest;
   aAlfa = #cacambiz#2; |QBF aAlfa;
   aAlfa1 = aAlfa + "dscomer9/def/agifa041.mas";
   |CARGA_DEF aAlfa1, Refere1@;
   |SI FSalida = 0;
      aAlfa1 = aAlfa + "dscomer9/fich/"; |PATH_DAT #Refere1@#aAlfa1#;
      |ABRE #Refere1@;
      #Refere1@#0 = #cacambiz#3;
      |LEE 000#Refere1@.=;
      |SI FSalida = 0;
         #cacambiz#6 = #Refere1@#1; |PINTA #cacambiz#6;
      |SINO;
         |MENAV "    Empresa inexistente"; |ERROR;
      |FINSI;
      |CIERRA #Refere1@; |DESCARGA_DEF #Refere1@;
   |FINSI;
|FPRC;

|PROCESO T66EmpGest; |TIPO 66;
   aAlfa = #cacambiz#2; |QBF aAlfa;
   aAlfa1 = aAlfa + "dscomer9/def/agifa041.mas";
   |CARGA_DEF aAlfa1, Refere1@;
   |SI FSalida = 0;
      aAlfa1 = aAlfa + "dscomer9/fich/"; |PATH_DAT #Refere1@#aAlfa1#;
      |ABRE #Refere1@;
      |CONSULTA_CLAVE #1#Refere1@;
      |SI FSalida = 0;
         #cacambiz#3 = #Refere1@#0; |PINTA #cacambiz#3;
         #cacambiz#6 = #Refere1@#1; |PINTA #cacambiz#6;
      |FINSI;
      |CIERRA #Refere1@; |DESCARGA_DEF #Refere1@;
   |FINSI;
|FPRC;

|PROCESO T00EmpCart;
   aAlfa = #cacambiz#2; |QBF aAlfa;
   aAlfa1 = aAlfa + "dscarter/def/dscam005.mas";
   |CARGA_DEF aAlfa1, Refere1@;
   |SI FSalida = 0;
      aAlfa1 = aAlfa + "dscarter/fich/"; |PATH_DAT #Refere1@#aAlfa1#;
      |ABRE #Refere1@;
      #Refere1@#0 = #cacambiz#4;
      |LEE 000#Refere1@.=;
      |SI FSalida = 0;
         #cacambiz#7 = #Refere1@#1; |PINTA #cacambiz#7;
      |SINO;
         |MENAV "    Empresa inexistente"; |ERROR;
      |FINSI;
      |CIERRA #Refere1@; |DESCARGA_DEF #Refere1@;
   |FINSI;
|FPRC;

|PROCESO T66EmpCart; |TIPO 66;
   aAlfa = #cacambiz#2; |QBF aAlfa;
   aAlfa1 = aAlfa + "dscarter/def/dscam005.mas";
   |CARGA_DEF aAlfa1, Refere1@;
   |SI FSalida = 0;
      aAlfa1 = aAlfa + "dscarter/fich/"; |PATH_DAT #Refere1@#aAlfa1#;
      |ABRE #Refere1@;
      |CONSULTA_CLAVE #1#Refere1@;
      |SI FSalida = 0;
         #cacambiz#4 = #Refere1@#0; |PINTA #cacambiz#4;
         #cacambiz#7 = #Refere1@#1; |PINTA #cacambiz#7;
      |FINSI;
      |CIERRA #Refere1@; |DESCARGA_DEF #Refere1@;
   |FINSI;
|FPRC;

|PROCESO RevisaPath;
   aAlfa = #cacambiz#Campo#; |QBF aAlfa;
   aAlfa1 = aAlfa % -101;
   |SI aAlfa1 ! "/"; |Y aAlfa1 ! "\";
      aAlfa1 = aAlfa - "/";
      |SI FSalida = 0;
         aAlfa = aAlfa + "\";
      |SINO;
         aAlfa = aAlfa + "/";
      |FINSI;
      #cacambiz#Campo# = aAlfa; |PINTA #cacambiz#Campo#;
   |FINSI;
|FPRC;

|PROCESO EjecutaCambioCta;
   aAlfa1 = aPathDef + aFichero + ".mas";
   |CARGA_DEF aAlfa1, Refere2@;
   |SI FSalida = 0;
      nSwCab = 0;
      |SI aFichero = "dscam013"; |O aFichero = "dscam022";
       |O aFichero = "dscam047"; |O aFichero = "dscam050";
       |O aFichero = "dscam096";
         |SI aFichero = "dscam013"; aAlfa1 = aPathDef + "dscam012.mas"; |FINSI;
         |SI aFichero = "dscam022"; aAlfa1 = aPathDef + "dscam021.mas"; |FINSI;
         |SI aFichero = "dscam047"; aAlfa1 = aPathDef + "dscam046.mas"; |FINSI;
         |SI aFichero = "dscam050"; aAlfa1 = aPathDef + "dscam049.mas"; |FINSI;
         |SI aFichero = "dscam096"; aAlfa1 = aPathDef + "dscam095.mas"; |FINSI;
         |CARGA_DEF aAlfa1, Refere3@;
         |PATH_DAT #Refere3@#aPathFich#; |ABRE #Refere3@;
         nSwCab = 1;
      |FINSI;
      |PATH_DAT #Refere2@#aPathFich#;
      |ABRE #Refere2@;
      |LEE 101#Refere2@.p;
      |PARA; |SI FSalida = 0; |HACIENDO;
         nSwPasa = 0;
         |SI nSwCab = 1;
            #Refere3@#0 = #Refere2@#0;
            |LEE 000#Refere3@.=;
            |SI FSalida = 0;
               |SI fDFechaPer [ #Refere3@#nCampoFecha#; |Y #Refere3@#nCampoFecha# [ fHFechaPer;
                  nSwPasa = 1;
               |FINSI;
            |FINSI;
         |SINO;
            |SI nCampoFecha < 0;
               nSwPasa = 1;
            |SINO;
               |SI fDFechaPer [ #Refere2@#nCampoFecha#; |Y #Refere2@#nCampoFecha# [ fHFechaPer;
                  nSwPasa = 1;
               |FINSI;
            |FINSI;
         |FINSI;

         |SI nSwPasa = 1;
            |DDEFECTO #cacambix;
            #cacambix#0 = #Refere2@#nCampo#;
            |LEE 000#cacambix.];
            |SI FSalida = 0; |Y #cacambix#0 = #Refere2@#nCampo#;
               #Refere2@#nCampo# = #cacambix#2;
               |GRABA 020#Refere2@.a;
            |FINSI;
         |FINSI;
         |LIBERA #Refere2@; |LEE 101#Refere2@.s;
      |SIGUE;
      |SI nSwCab = 1; |CIERRA #Refere3@; |DESCARGA_DEF #Refere3@; |FINSI;
      |LIBERA #Refere2@;
      |CIERRA #Refere2@; |DESCARGA_DEF #Refere2@;
   |FINSI;
|FPRC;

|PROCESO EjecutaCambio;
   |ABRE #cacambix;

   || Primero revisamos los ficheros de gestion
   aPathDef = #cacambiz#2; |QBF aPathDef; aPathFich = aPathDef;
   aPathDef = aPathDef + "dscomer9/def/";
   aPathFich = aPathFich + "dscomer9/fich/" + (("00000" + #cacambiz#3) % -105) + "/";

   |BLIN 22;
   |ATRI I; |PINTA 2202, " Cambiando cuentas en gestion comercial"; |ATRI N;

   |BLIN 23;
   |ATRI I; |PINTA 2305, " Tratando el fichero de clientes              "; |ATRI N;
   nCampoFecha = -1;
   aFichero = "agifa024"; nCampo = 16; |HAZ EjecutaCambioCta;

   |ATRI I; |PINTA 2305, " Tratando el fichero de proveedores           "; |ATRI N;
   nCampoFecha = -1;
   aFichero = "agifa112"; nCampo = 10; |HAZ EjecutaCambioCta;

   |ATRI I; |PINTA 2305, " Tratando el fichero de facturas de venta     "; |ATRI N;
   nCampoFecha = 1;
   aFichero = "agifa134"; nCampo = 12; |HAZ EjecutaCambioCta;

   |ATRI I; |PINTA 2305, " Tratando el fichero de facturas de compra    "; |ATRI N;
   nCampoFecha = 1;
   aFichero = "agifa079"; nCampo = 12; |HAZ EjecutaCambioCta;

   |ATRI I; |PINTA 2305, " Tratando el fichero de facturas directas     "; |ATRI N;
   nCampoFecha = 1;
   aFichero = "agifa084"; nCampo = 40; |HAZ EjecutaCambioCta;

   |ATRI I; |PINTA 2305, " Tratando el fichero de familias de articulos "; |ATRI N;
   nCampoFecha = -1;
   aFichero = "agifa060"; nCampo = 2; |HAZ EjecutaCambioCta;
   aFichero = "agifa060"; nCampo = 3; |HAZ EjecutaCambioCta;

   |ATRI I; |PINTA 2305, " Tratando el fichero de representantes        "; |ATRI N;
   nCampoFecha = -1;
   aFichero = "agifa025"; nCampo = 56; |HAZ EjecutaCambioCta;

   || ... y ahora revisamos los ficheros de cartera
   aPathDef = #cacambiz#2; |QBF aPathDef; aPathFich = aPathDef;
   aPathDef = aPathDef + "dscarter/def/";
   aPathFich = aPathFich + "dscarter/fich/" + (("00000" + #cacambiz#4) % -105) + "/";

   |ATRI I; |PINTA 2202, " Cambiando cuentas en cartera          "; |ATRI N;
   |ATRI I; |PINTA 2305, " Tratando el fichero de recibos de venta      "; |ATRI N;
   nCampoFecha = 4;
   aFichero = "dscam003"; nCampo = 5; |HAZ EjecutaCambioCta;

   |ATRI I; |PINTA 2305, " Tratando el fichero de historico de cobros   "; |ATRI N;
   nCampoFecha = 6;
   aFichero = "dscam004"; nCampo = 4; |HAZ EjecutaCambioCta;

   |ATRI I; |PINTA 2305, " Tratando el fichero de recibos de compra     "; |ATRI N;
   nCampoFecha = 4;
   aFichero = "dscam008"; nCampo = 5; |HAZ EjecutaCambioCta;

   |ATRI I; |PINTA 2305, " Tratando el fichero de historico de pagos    "; |ATRI N;
   nCampoFecha = 6;
   aFichero = "dscam009"; nCampo = 4; |HAZ EjecutaCambioCta;

   |ATRI I; |PINTA 2305, " Tratando el fichero de cabecera de remesas   "; |ATRI N;
   nCampoFecha = 1;
   aFichero = "dscam012"; nCampo = 14; |HAZ EjecutaCambioCta;

   |ATRI I; |PINTA 2305, " Tratando el fichero de lineas de remesas     "; |ATRI N;
   nCampoFecha = 1;
   aFichero = "dscam013"; nCampo = 11; |HAZ EjecutaCambioCta;

   |ATRI I; |PINTA 2305, " Tratando el fichero de bancos propios        "; |ATRI N;
   nCampoFecha = -1;
   aFichero = "dscam014"; nCampo = 10; |HAZ EjecutaCambioCta;

   |ATRI I; |PINTA 2305, " Tratando el fichero de cabecera de pagares   "; |ATRI N;
   nCampoFecha = 1;
   aFichero = "dscam021"; nCampo = 10; |HAZ EjecutaCambioCta;

   |ATRI I; |PINTA 2305, " Tratando el fichero de lineas de pagares     "; |ATRI N;
   nCampoFecha = 1;
   aFichero = "dscam022"; nCampo = 8;  |HAZ EjecutaCambioCta;

   |ATRI I; |PINTA 2305, " Tratando el fichero de cabecera de talones   "; |ATRI N;
   nCampoFecha = 1;
   aFichero = "dscam046"; nCampo = 10; |HAZ EjecutaCambioCta;

   |ATRI I; |PINTA 2305, " Tratando el fichero de lineas de talones     "; |ATRI N;
   nCampoFecha = 1;
   aFichero = "dscam047"; nCampo = 8;  |HAZ EjecutaCambioCta;

   |ATRI I; |PINTA 2305, " Tratando el fichero de cabecera de confirmes "; |ATRI N;
   nCampoFecha = 1;
   aFichero = "dscam049"; nCampo = 10; |HAZ EjecutaCambioCta;

   |ATRI I; |PINTA 2305, " Tratando el fichero de lineas de confirmes   "; |ATRI N;
   nCampoFecha = 1;
   aFichero = "dscam050"; nCampo = 8;  |HAZ EjecutaCambioCta;

   |ATRI I; |PINTA 2305, " Tratando el fichero de bloqueo de proveedores"; |ATRI N;
   nCampoFecha = -1;
   aFichero = "dscam104"; nCampo = 8;  |HAZ EjecutaCambioCta;

   |ATRI I; |PINTA 2305, " Tratando los pagares/talones de clientes     "; |ATRI N;
   nCampoFecha = 71;
   aFichero = "dscam003"; nCampo = 74; |HAZ EjecutaCambioCta;

   |ATRI I; |PINTA 2305, " Tratando el fichero de cabecera de pagos dom."; |ATRI N;
   nCampoFecha = 1;
   aFichero = "dscam095"; nCampo = 14; |HAZ EjecutaCambioCta;

   |ATRI I; |PINTA 2305, " Tratando el fichero de lineas de pagos domic."; |ATRI N;
   nCampoFecha = 1;
   aFichero = "dscam096"; nCampo = 11;  |HAZ EjecutaCambioCta;

   |SI nSwDsPrevex = 1;
   || ... y en el caso que lo hubiera, hago el cambio en DsPrevex buscando
   ||     primero la empresa predeterminada
      |DBASS aPathDef; |QBF aPathDef; aPathFich = aPathDef;
      aPathDef = aPathDef + "dspreven/def/";
      aPathFich = aPathFich + "dspreven/fich/";
      aAlfa = aPathDef; |QBF aAlfa; aAlfa = aAlfa + "visalm00.mas";
      |CARGA_DEF aAlfa, Refere2@;
      |SI FSalida = 0;
         |PATH_DAT #Refere2@#aPathFich#;
         |ABRE #Refere2@;
         |LEE 000#Refere2@.p;
         |PARA; |SI FSalida = 0; |HACIENDO;
            |SI #Refere2@#3 = "S"; |SAL; |FINSI;
            |LEE 000#Refere2@.s;
         |SIGUE;
         |SI #Refere2@#3 = "S";
            aPathFich = aPathFich + (("00000" + #Refere2@#0) % -105) + "/";
         |SINO;
            aPathFich = "";
         |FINSI;
         |CIERRA #Refere2@; |DESCARGA_DEF #Refere2@;
      |FINSI;

      |SI aPathFich ! "";
         |BLIN 22;
         |ATRI I; |PINTA 2202, " Cambiando cuentas en prevencion y vigilancia de riesgos laborales"; |ATRI N;

         |ATRI I; |PINTA 2305, " Tratando el fichero de productos             "; |ATRI N;
         aFichero = "maesm002"; nCampo = 5;  |HAZ EjecutaCambioCta;
         aFichero = "maesm002"; nCampo = 7;  |HAZ EjecutaCambioCta;

         |ATRI I; |PINTA 2305, " Tratando el fichero de colaboradores         "; |ATRI N;
         aFichero = "dspvw027"; nCampo = 56;  |HAZ EjecutaCambioCta;

         |ATRI I; |PINTA 2305, " Tratando el fichero de incidencias           "; |ATRI N;
         aFichero = "dspvm002"; nCampo = 128;  |HAZ EjecutaCambioCta;
      |FINSI;
   |FINSI;

   |CIERRA #cacambix;
|FPRC;

|PROGRAMA;
   |ABRE #cacambiz;
   |LEE 000#cacambiz.p;
   |SI FSalida ! 0;
      |DDEFECTO #cacambiz;
      |GRABA 020#cacambiz.c;
   |FINSI;

   |CLS;
   |CABEZA "Cambio de nivel para aplicaciones de gestion y cartera";
   |PINPA #0#cacambiz; |PINDA #0#cacambiz;
   |ENDATOS #1#cacambiz;
   |SI FSalida = 0;
      |GRABA 020#cacambiz.a;

      |SI #cacambiz#1 = "S";
         aAlfa = "01." + (("00" + #48#11) % -102) + ".";
         |SI #48#26 ] 80; aAlfa = aAlfa + "19"; |SINO; aAlfa = aAlfa + "20"; |FINSI;
         aAlfa = aAlfa + #48#26; fDFechaPer = aAlfa;
         |SI #48#12 = 12;
            aAlfa = "31.12.";
         |SINO;
            nCalc = #48#12 + 1;
            aAlfa = nCalc;
            aAlfa = "01." + (("00" + aAlfa) % -102) + ".";
         |FINSI;
         |SI #48#26 ] 80; aAlfa = aAlfa + "19"; |SINO; aAlfa = aAlfa + "20"; |FINSI;
         aAlfa = aAlfa + #48#26; fHFechaPer = aAlfa;
         |SI #48#12 ! 12;
            nCalc = fHFechaPer; nCalc = nCalc - 1; fHFechaPer = nCalc;
         |FINSI;
      |SINO;
         fDFechaPer = "01.01.0000"; fHFechaPer = "31.12.9999";
      |FINSI;

      |DBASS aAlfa;
      aAlfa = aAlfa + "dspreven/def/visalm00.mas";
      |XABRE "E", aAlfa, nHandle;
      |SI FSalida ] 0;
         nSwDsPrevex = 1;
      |SINO;
         nSwDsPrevex = 0;
      |FINSI;

      |HAZ EjecutaCambio;
   |FINSI;

   |CIERRA #cacambiz;
|FPRO;
