|FICHEROS;
   cafantas #2;

   caextrpa #4;         || display extractos
   cacuenta #5;         || cuentas
   caextrp4 #6;         || display asientos completo

   camaasil #7;         || apuntes lins.
   camaasic #11;
   calicont #31;

   caextm01 #101;
   caextm02 #102;

   &imagen@  #999;

   caextmw4 #904;
   canempre;
|FIN;

|VARIABLES;
   &enBEjer = 0;
   aFichero = "";
   aAlfa = "";
   alfa1 = "";
   alfa2 = "";
   alfa3 = "";
   alfa4 = "";
   alfa5 = "";
   nNume1 = 0;
   nNume2 = 0;
   FEstado = 0;
   inkey = 0;
   seleccio = 0;

   &d_abajo = 0;
   &h_abajo = 0;
   &s_abajo = 0;
   lalinea = 0;
   totallin = 0;
   &nDeb1   = 0;
   &nHab1   = 0;
   nDebe    = 0;
   nHaber   = 0;
   nApunte  = 0;
   informe  = "";

   &enDiario          = 0;
   &efFecha           = @;
   &enDocumento       = 0;
   &enDiarioDentro    = 0;
   &efFechaDentro     = @;
   &enDocumentoDentro = 0;

   &aFitxer = "";
   aMas = "";
   &enDi = 0;
   &efFe = @;
   &enDo = 0;
   &enAp = 0;
   &eaDes = "";
   &eaD = "";
   &eaS = "";
   &enDeb = 0;
   &enHar = 0;
   &enSal = 0;
   &enTS  = 0;
   &enTD  = 0;
   &enTH  = 0;
   &eaContra = "";
   nPrim  = 0;
   &eaFEmp   = "";

   &enDnDIARIO   = 0;
   &enHnDIARIO   = 0;
   &efDFechas    = @;
   &efHFechas    = @;
   &enDDocume    = 0;
   &enHDocume    = 0;
   &enBorraAu    = 0;

   &swVarios     = 0;
   &eaMonOrg     = "";
   aBlancs       = "";
   &enDesAna     = 0;
   nSwAper = 0;
   aCta08  = "";
   aCta90  = "";
   aCtaSe  = "";

   &enNumero  = 0;
   &eaEldef   = "";
   nSwF4      = 0;
   nSwF5      = 0;

     &enDiaGAD     = 0;
     &efFecGAD     = @;
     &enDocGAD     = 0;
     &enLibGAD     = 0;
     &enRegGAD     = 0;
     &eaExiGAD     = "";

   &eaEspTabla = "";
   &enInkey    = 0;
   &nQuantHay  = 0;
   &enEste     = 0;
   nSwPus      = 0;
   nAuxiliar   = 0;
   &nSwAsto    = 0;
   nNumero     = 0;
   nTipoEx     = 0;
   nHayTipo2   = 0;
   nHayTipo3   = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscopi_i;

|PROCESO pinta13; |TIPO 13;
   |C_V #4#23,0,alfa4;
   |SI alfa4 = "S";
       |ATRI I; |PINTA 0569, "Emp.:"; |ATRI 0;
   |FINSI;

   #4#15 = d_abajo;
   #4#16 = h_abajo;
   #4#17 = s_abajo;
   |PINTA #4#15;
   |PINTA #4#16;
   |PINTA #4#17;
   |SI nSwAsto = 0;
       alfa1 = "ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ";
       alfa2 = "<F9> Ver Anotac.";
   |SINO;
       alfa1 = "<F9> Ver Asiento";
       alfa2 = "ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ";
       |PINTA 1701, alfa2;
       alfa2 = "";
   |FINSI;
   |PINTA 1802,"                                                                              ";
   |ATRI S; |PINTA 1763, alfa1;
            |PINTA 1863, alfa2; |ATRI 0;

   alfa1 = (#caextrpa#40 % 128); |QBF alfa1;
   |SI alfa1 ! "";
       |ATRI I; |PINTA 0707, "Ref: "; |ATRI 0;
       |ATRI S; |PINTA 0712, alfa1; |ATRI 0;
   |SINO;
       |PINTA 0707, "ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ";
   |FINSI;

   |SI swVarios = 1; |HAZ Situafich0; |FINSI;
   |HAZ MasDatos;
|FPRC;

|PROCESO Tipo7Cp2; |TIPO 7;
     enDiaGAD     = #caextrpa#18;
     efFecGAD     = #caextrpa#8;
     enDocGAD     = #caextrpa#9;
     |HAZ EnGadDiario;

     aAlfa = eaExiGAD; |QBF aAlfa;
     |SI aAlfa ! "";
         |ATRI P;
         |PINTA 0829, eaExiGAD;
         |ATRI 0;
     |FINSI;

     nSwF4 = 0; nSwF5 = 0;
     alfa1 = "    F2-Consulta;F3-Informe;F4-Modificar;F5-Baja;F6-Anotaciones;F7-Buscar Apte.";
     |SI enModoPINET2 = 1;
         alfa1 = alfa1 - "F4-Modificar;";
         nSwF4 = 1;
     |FINSI;
     |SI enModoPINET3 = 1;
         alfa1 = alfa1 - "F5-Baja;";
         nSwF5 = 1;
     |FINSI;
     |MENSA alfa1;
|FPRC;

|PROCESO pintando; |TIPO 11;
   inkey = FSalida;

   |BLIN 4;
   nSwPus = 0;
   |SI inkey ] 10; |Y inkey [ 15;
       nSwPus = 1;
       |PUSHV 2401 2479;
       |BLIN 24;
   |FINSI;
   |SI swVarios = 1; |HAZ Situafich0; |FINSI;
   |SI inkey = 10;  |HAZ asiento;      |FINSI;
   |SI inkey = 11;  |HAZ ElInforme;    |FINSI;
   |SI inkey = 12; |Y nSwF4 = 0; |HAZ VeteAsiento2; |FINSI;
   |SI inkey = 13; |Y nSwF5 = 0; |HAZ VeteAsiento4; |FINSI;
   |SI inkey = 14;  |HAZ extraco;      |FINSI;
   |SI inkey = 15;  |HAZ LaBusqueda2;  |FINSI;
   |SI inkey = 17;
       |SI nSwAsto = 1; nSwAsto = 0; |SINO; nSwAsto = 1; |FINSI;
   |FINSI;
   |SI inkey = 181;  |Y eaEspTabla = "S";
       |SI enEste > 1;
           enInkey = inkey;
           |PTEC 806;
       |FINSI;
       |SI enEste = 1;
           |MENAV "0000Primera Cuenta Seleccionada";
       |FINSI;
   |FINSI;
   |SI inkey = 179; |Y eaEspTabla = "S";
       |SI enEste = nQuantHay;
           |MENAV "0000Ultima Cuenta Seleccionada";
       |FINSI;
       |SI enEste < nQuantHay;
           enInkey = inkey;
           |PTEC 806;
       |FINSI;
   |FINSI;
   |SI nSwPus = 1;
       nSwPus = 0;
       |POPV;
   |FINSI;
|FPRC;
_____________________________________________________________________________
_____________________________________________________________________________
|PROCESO ModiPan_2;
   alfa1 = "N";
   |SI #11#7 ! 0;
       alfa1 = "S";
       |ATRI R; |PINTA 0861, "Asto Predef.: "; |ATRI 0;
   |FINSI;
   |C_V #6#17, 1, alfa1;

   |SI eaSubde = "N";
        |C_A #6#6, 1, 29;
        |C_P #6#7, 1, 1266;
        |C_V #6#16,1, "N";
        |PINTA 0961, "ÄÄÄÄÂÄÄ";
        |PINTA 1061, "    ³  ";
        |PINTA 1161, "ÄÄÄÄÅÄÄ";
        |PINTA 1261, "    ³  ";
        |PINTA 1361, "    ³  ";
        |PINTA 1461, "    ³  ";
        |PINTA 1561, "    ³  ";
        |PINTA 1661, "    ³  ";
        |PINTA 1761, "    ³  ";
        |PINTA 1861, "    ³  ";
        |PINTA 1961, "    ³  ";
        |ATRI R; |PINTA 1061, "    "; |PINTA 1066, "Dp"; |ATRI 0;
   |SINO;
        |SI enSubde = 1; |Y eaDepar = "N";
            |C_A #6#6, 1, 28;
            |C_V #6#7, 1, "N";
            |PINTA 0961, "Ä";
            |ATRI R; |PINTA 1061, "   "; |ATRI 0;
            |PINTA 1161, " ";
            |PINTA 1261, " ";
            |PINTA 1361, " ";
            |PINTA 1461, " ";
            |PINTA 1561, " ";
            |PINTA 1661, " ";
            |PINTA 1761, " ";
            |PINTA 1861, " ";
            |PINTA 1961, " ";
            |PINTA 2061, " ";
        |FINSI;
   |FINSI;
|FPRC;

|PROCESO lee_asiento1;             || CARGA LA PANTALLA DEL ASIENTO COMPLETO
   seleccio = 1;

   |ABRE #5;
   |SELECT #1#5;
   #cacuenta#0 = #7#4;         || cuenta
   #cacuenta#1 = #7#5;         || digito
   |LEE 001#5=;
   |SI FSalida ! 0;
      |DDEFECTO #5;
      #cacuenta#2 = "<CUENTA INEXISTENTE>";
   |FINSI;
   |CIERRA #5;

   |DDEFECTO #6;
   #caextrp4#00 = #7#3;        || apunte
   #caextrp4#01 = #7#16;       || cuenta DEBE
   #caextrp4#02 = #7#17;       || digito DEBE
   #caextrp4#03 = #7#10;       || cuenta HABER
   #caextrp4#04 = #7#11;       || digito HABER
   #caextrp4#05 = #7#6;        || concepto
   #caextrp4#06 = #7#7;        || descripcion
   #caextrp4#07 = #7#21;       || departamento
   #caextrp4#08 = #7#9;        || importe
   #caextrp4#09 = #cacuenta#2;        || descripcion
   #caextrp4#10 = #cacuenta#51;       || debe
   #caextrp4#11 = #cacuenta#52;       || haber
   #caextrp4#12 = #cacuenta#53;       || saldo
   #caextrp4#13 = #7#0;        || diario
   #caextrp4#14 = #7#1;        || fecha
   #caextrp4#15 = #7#2;        || documento
   #caextrp4#16 = #7#28;       || Subdepartamento
   #caextrp4#17 = #11#7;       || Asto Predefinido
   |SI #caextrp4#0 = #4#19;
       lalinea = #caextrp4#0;
   |FINSI;
   |GRABA 020#6n;
   totallin = totallin + 1;
|FPRC;

|DEFBUCLE lee_asiento;
   #7#1;
   ;
   #4#18, #4#8, #4#9,    1;
   #4#18, #4#8, #4#9, 9999;
   e;
   NULL, lee_asiento1;
|FIN;

|PROCESO CargoAsiento;
   aFichero = ("y" + Usuario);
   aFichero = (aFichero + "zzzzzzzz") % 108;
   |NOME_DAT #6 aFichero;
   |DELINDEX #6;

   seleccio = 0;

   |ABRE #11;
   |DDEFECTO #11;
   #11#0 = #4#18;
   #11#1 = #4#8;
   #11#2 = #4#9;
   |LEE 001#11=;
   |CIERRA #11;

   |ABRE #6;
   lalinea = 1;
   totallin = 0;
   |BUCLE lee_asiento;
   |CIERRA #6;
|FPRC;

|PROCESO mini;
   |SI FSalida = "POSICION";  |Y totallin > 8;
      #caextrp4#0 = lalinea;
   |SINO;
      #caextrp4#0 = 1;
   |FINSI;
|FPRC;

|PROCESO maxi;
   #caextrp4#0 = 9999;
|FPRC;

|PROCESO asiento;
   |HAZ CargoAsiento;
   |SI seleccio ! 0;
      |PUSHV 07012380;
      |PINPA #0#6;
      |HAZ ModiPan_2;
      |ENTLINEAL #1#6, -1, 4, 20, 0, mini, maxi;
      |POPV;
   |SINO;
      |MENAV "    Seleccion vacia ...";
   |FINSI;
   |DELINDEX #6;
|FPRC;

_____________________________________________________________________________

|PROCESO extraco;             || CARGA LA PANTALLA DE LA ANOT.EXTRACONTABLE
    enDiarioEx = #4#18;
    efFechaEx  = #4#8;
    enDocumEx  = #4#9;
    eaLaCtaEx  = #4#1;
    eaCtaDeEx  = #4#2;
    enConNuEx  = #4#29;
    eaConDeEx  = #4#10;
    enEmpresa  = #4#23;
    enPerEx    = #4#24;
    aAlfa = #4#25; |QBF aAlfa;
    |SI aAlfa ! "";
        dirfich0 = aAlfa;
    |SINO;
        |DFICE dirfich0;
    |FINSI;
    enTipoEx = 0;
    |SUB_EJECUTA_NP "caextmz1;extracto";
|FPRC;

|PROCESO LaBusqueda2;
       |ERROR;
       eaEldef = "caextrpa";
       |SUB_EJECUTA_NP "caw00044";

       |SI enNumero ! 0;
           #4#48 = enNumero;
           |PTEC 802;
           |PONCONTROL 23, "SI";
       |FINSI;
|FPRC;
_____________________________________________________________________________
|PROCESO LaContra2;
    #4#46 = "";
    #4#47 = "";

    |ABRE #31;
    #31#0 = #4#18;
    #31#1 = #4#8;
    #31#2 = #4#9;
    #31#3 = #4#19;
    |LEE 001#31=;
    |SI FSalida = 0;
        #4#46 = #31#4;
    |SINO;
        |ABRE #11;
        #11#0 = #4#18;
        #11#1 = #4#8;
        #11#2 = #4#9;
        |LEE 001#11=;
        |SI FSalida = 0;
            |SI #4#11 = "D"; #4#46 = #11#10; |FINSI;
            |SI #4#11 = "H"; #4#46 = #11#8;  |FINSI;
        |FINSI;
        |CIERRA #11;
   |FINSI;
   |CIERRA #31;
   |ABRE #5;
   #5#0 = #4#46;
   |LEE 001#5=;
   |SI FSalida = 0;
       #4#47 = #5#2;
   |FINSI;
   |CIERRA #5;
|FPRC;

|PROCESO CalculaTotales2;

    |SI swVarios = 1;
        dirfich0 = #4#25; |QBF dirfich0;
        |PATH_DAT #5  dirfich0;
        |PATH_DAT #7  dirfich0;
        |PATH_DAT #11 dirfich0;
        |PATH_DAT #31 dirfich0;
    |FINSI;

    |ABRE #7;
    #7#0 = #4#18;        || diario
    #7#1 = #4#8;         || fecha
    #7#2 = #4#9;         || documento
    #7#3 = #4#19;        || linea apunte
    |LEE 040#7=;
    |SI FSalida ! 0;
        |BORRA 020#4a;
    |SINO;
        #4#5  = nDebe;
        #4#6  = nHaber;
        #4#7  = #4#5 - #4#6;
        #4#11 = #7#8;
        |SI #4#11 = "D";
            #4#12 = #7#9;                || importe debe
            #4#13 = 0;                          || importe haber detalle
        |SINO;
            #4#13 = #7#9;                || importe haber detalle
            #4#12 = 0;                          || importe debe
        |FINSI;
        #4#14 = #4#7 + (#4#12 - #4#13);

        nDebe  = nDebe + #4#12;
        nHaber = nHaber + #4#13;

        #4#20 = #7#21;
        #4#21 = #7#28;
        #4#22 = #7#18;
        #4#30 = #7#12;
        #4#31 = #7#13;
        #4#32 = #7#14;
        #4#33 = #7#15;
        #4#34 = #7#19;
        #4#35 = #7#20;
        #4#36 = #7#22;
        #4#37 = #7#23;
        #4#38 = #7#24;
        #4#39 = #7#25;
        #4#40 = #7#26;
        #4#41 = #7#27;
        #4#42 = #7#29;
        alfa5 = #7#1;
        alfa2 = alfa5 % 102;
        alfa3 = alfa5 % 402;
        alfa4 = alfa5 % 902;
        alfa5 = alfa2 + alfa3 + alfa4;
        #4#43 = alfa2 + "/" + alfa3 + "/" + alfa4;
        |HAZ LaContra2;

        |GRABA 020#4a;
    |FINSI;
    |LIBERA #4;
    |CIERRA #7;
|FPRC;

|DEFBUCLE caextrpa;
     #4#3;
     ;
     000000;
     999999;
     e;
     NULL, CalculaTotales2;
|FIN;

|PROCESO VeteAsiento2;

   |SI #4#8 [ #4#44;
       |MENAV "    ERROR FECHA INFERIOR O IGUAL AL BLOQUEO DE DATOS";
       |ACABA;
   |FINSI;

   enDiario    = #4#18;        || diario
   efFecha     = #4#8;         || fecha
   enDocumento = #4#9;         || documento

   |SI enDiarioDentro = enDiario;  |Y efFechaDentro = efFecha;
    |Y enDocumentoDentro = enDocumento;
       |MENAV "      Registro Bloqueado, usted a llamado al extracto desde este asiento.";
       |ERROR;
       |ACABA;
   |FINSI;

   |LEE 001#4=;
   |SI FSalida ! 0;  |ACABA;  |FINSI;

   #4#12 = 0;
   #4#13 = 0;

    |SI enDesAna = 1;
        |SUB_EJECUTA_NP "anz00010;NOMEQUITESQUETECORTOLOSGUEVOS";
    |SINO;
        |SUB_EJECUTA_NP "camaasic;NOMEQUITESQUETECORTOLOSGUEVOS";
    |FINSI;

   |ABRE #7;
   #7#0 = #4#18;        || diario
   #7#1 = #4#8;         || fecha
   #7#2 = #4#9;         || documento
   #7#3 = #4#19;        || linea apunte
   |LEE 040#7=;
   |SI FSalida ! 0;  |DDEFECTO #7;  |FINSI;
   |CIERRA #7;

   #4#11 = #7#8;             || signo (no en pantalla!!!)
   |SI #4#11 = "D";
       #4#12 = #7#9;                || importe debe
       #4#13 = 0;                          || importe haber detalle
   |SINO;
       #4#13 = #7#9;                || importe haber detalle
       #4#12 = 0;                          || importe debe
   |FINSI;

   |GRABA 020#4a;
   |LIBERA #4;

   nApunte = #4#48;
   nDebe   = nDeb1;
   nHaber  = nHab1;

   |BUCLE caextrpa;

   d_abajo = nDebe;
   h_abajo = nHaber;
   s_abajo = d_abajo - h_abajo;

   #4#48 = nApunte;
   |LEE 040#4=;

   |PONCONTROL 23, "SI";
||   |PTEC 809;
|FPRC;


|PROCESO VeteAsiento4;  ||baja Asiento, primero lo vemos.

   |SI #4#8 [ #4#44;
       |MENAV "    ERROR FECHA INFERIOR O IGUAL AL BLOQUEO DE DATOS";
       |ACABA;
   |FINSI;

   enDiario    = #4#18;        || diario
   efFecha     = #4#8;         || fecha
   enDocumento = #4#9;         || documento

   |SI enDiarioDentro = enDiario;  |Y efFechaDentro = efFecha;
    |Y enDocumentoDentro = enDocumento;
       |MENAV "      Registro Bloqueado, usted a llamado al extracto desde este asiento.";
       |ERROR;
       |ACABA;
   |FINSI;

   |LEE 001#4=;
   |SI FSalida ! 0;  |ACABA;  |FINSI;

   aFichero = ("q" + Usuario);
   aFichero = (aFichero + "zzzzzzzz") % 108;
   |NOME_DAT #6 aFichero;
   |DELINDEX #6;

   seleccio = 0;

   |ABRE #11;
   |DDEFECTO #11;
   #11#0 = #4#18;
   #11#1 = #4#8;
   #11#2 = #4#9;
   |LEE 001#11=;
   |CIERRA #11;

   |ABRE #6;
   lalinea = 1;
   totallin = 0;
   |BUCLE lee_asiento;
   |CIERRA #6;

   |SI seleccio ! 0;
      |PUSHV 07012380;

      |PINPA #0#6;
      |PINTA #6#13; |PINTA #6#14; |PINTA #6#15;
      |ATRI P; |PINTA 0861, "BAJA ASIENTO"; |ATRI 0;

      |ENTLINEAL #1#6, -1, 7, 20, 0, mini, maxi;
      |CONFI "    NBorrar Asiento S/N";
      |SI FSalida = 0;
          |HAZ BorroAsiento;
      |FINSI;
      |POPV;
   |FINSI;

   |DELINDEX #6;

|FPRC;

|PROCESO BorroAsiento;
   enDnDIARIO = enDiario;            || diario
   enHnDIARIO = enDiario;
   efDFechas  = efFecha;             || fecha
   efHFechas  = efFecha;             || fecha
   enDDocume  = enDocumento;         || documento
   enHDocume  = enDocumento;         || documento
   enBorraAu  = 2;

   |SUB_EJECUTA_NP "caborasi;NOMEQUITESQUETECORTOLOSGUEVOS";

   eaMoneda = eaMonOrg;
   |HAZ Monedas;

   nApunte = #4#48;
   nDebe   = nDeb1;
   nHaber  = nHab1;

   |BUCLE caextrpa;

   d_abajo = nDebe;
   h_abajo = nHaber;
   s_abajo = d_abajo - h_abajo;

   #4#48 = nApunte;
   |LEE 040#4=;

   |PONCONTROL 23, "SI";
||   |PTEC 809;
|FPRC;

|| ----------------------------------------------------------------------
|PROCESO LeeAuxiliar;
 enDi  = #999#18;
 efFe  = #999#8;
 enDo  = #999#9;
 enAp  = #999#19;
 eaDes = #999#10;
 eaD   = #999#20;
 eaS   = #999#21;
 enDeb = #999#12;
 enHar = #999#13;
 enSal = #999#14;
 eaContra = #999#47;
 eaRefer  = #999#40;
 |SI nPrim = 0;
     nPrim = 1;
     enTD = #999#5;
     enTH = #999#6;
 |FINSI;
 |PRINT;
 enTD = enTD + #999#12;
 enTH = enTH + #999#13;
 enTS = enTD - enTH;
|FPRC;

|DEFBUCLE LeeAuxiliar;
 #999#2006;
 ;
 "01.01.0000", 00, 000000, 0000, 00001, 000001;
 "31.12.2999", 99, 999999, 9999, 99999, 999999;
 ;
 NULL, LeeAuxiliar;
|FIN;

|PROCESO ElInforme;
 informe = "caextrpa";
 |CONFI "0000SImprimir descripcion contrapartida ?";
 |SI FSalida = 0;
     informe = "caextrpc";
 |FINSI;
 |PATH_DAT #5 eaFEmp;
 |ABRE #5;
 |DDEFECTO #5;
 #5#0 = #4#1;
 |LEE 001#5=;
 |CIERRA #5;

 |DBASE alfa1;
 aMas  = alfa1 + "def/caextrpa.mas";

 |CARGA_DEF aMas, imagen@;
 |SI FSalida = 0;

     |IMPRE 0;
     |SI FSalida ! 0;
         |PATH_DAT #5 dirfich0;
         |DESCARGA_DEF #imagen@;
         |ACABA;
     |FINSI;

     |INFOR informe;
     |SI FSalida ! 0;
         |PATH_DAT #5 dirfich0;
         |DESCARGA_DEF #imagen@;
         |FINIMP;
         |ACABA;
     |FINSI;

     |NOME_DAT #999 aFitxer;
     enTD = 0;
     enTH = 0;
     enTS = 0;
     nPrim = 0;
     enEjercicio =enBEjer;
     |BUCLE LeeAuxiliar;
     |PIEINF;

     |FININF;
     |FINIMP;
     |DESCARGA_DEF #imagen@;
 |FINSI;
 |PATH_DAT #5 dirfich0;
|FPRC;

|PROCESO Situafich0;
   enEmpresa = #4#23;
   enPeriodo = #4#24;
   dirfich0  = #4#25; |QBF dirfich0;
   eaMoneda  = #4#26;
   |PATH_DAT #5 dirfich0;
   |PATH_DAT #7 dirfich0;
   |PATH_DAT #11 dirfich0;
   |PATH_DAT #31 dirfich0;

   enEmpresaPINET = enEmpresa;
   |HAZ RestriccionPINET;
|FPRC;

|PROCESO PosicionesA;
  nNume1 = nAuxiliar + 02; |C_P #6#0, 1, nNume1;
  nNume1 = nAuxiliar + 07; |C_P #6#1, 1, nNume1;
  nNume1 = nAuxiliar + 20; |C_P #6#3, 1, nNume1;
  nNume1 = nAuxiliar + 33; |C_P #6#5, 1, nNume1;
  nNume1 = nAuxiliar + 36; |C_P #6#6, 1, nNume1;
  nNume1 = nAuxiliar + 62; |C_P #6#7, 1, nNume1;
  nNume1 = nAuxiliar + 65; |C_P #6#16, 1, nNume1;
  nNume1 = nAuxiliar + 69; |C_P #6#8, 1, nNume1;

  alfa1 = "S";
  |SI nAuxiliar = 2000;
      alfa1 = "N";
  |FINSI;
  |C_V #6#9,  1, alfa1;
  |C_V #6#10, 1, alfa1;
  |C_V #6#11, 1, alfa1;
  |C_V #6#12, 1, alfa1;
  |C_V #6#13, 1, alfa1;
  |C_V #6#14, 1, alfa1;
  |C_V #6#15, 1, alfa1;
|FPRC;

|PROCESO mini904;
   #caextmw4#3 = "01.01.0000";
   #caextmw4#0 = 0001;
|FPRC;

|PROCESO maxi904;
   #caextmw4#3 = "31.12.2099";
   #caextmw4#0 = 9999;
|FPRC;

|PROCESO MasDatos;
  |BLANCO 1902 2379;

  |SI nSwAsto = 0;
      |HAZ CargoAsiento;

      nAuxiliar = 2000; |HAZ PosicionesA;
      |ATRI R; |PINTA 1802, "Diario:";
               |PINTA 1813, "Fecha:";
               |PINTA 1831, "Documento:";
      |PINTA 1902,"Apu.³Cuenta DEBE ³Cuenta HABER³ Concepto / Descripcion     ³Dp³Sdp³    Importe";
      |ATRI 0; |PINTA 1906, "³";
               |PINTA 1919, "³";
               |PINTA 1932, "³";
               |PINTA 1961, "³";
               |PINTA 1964, "³";
               |PINTA 1968, "³";
      |PINTA 1810, #4#18;
      |PINTA 1820, #4#8;
      |PINTA 1842, #4#9;
      |ENTLINEAL #1#6, -1, 7, 23, 0, mini, maxi;

      alfa1 = #caextrpa#47; |QBF alfa1;
      |SI alfa1 ! "";
          |PINTA 1702, "Contrapartida: ";
          |PINTA 1717, #caextrpa#47;
      |FINSI;

      nAuxiliar = 1200; |HAZ PosicionesA;
  |SINO;

      |HAZ CargoAnotacion;
      |ATRI R; |PINTA 1802,"Estado Fecha    Anotacion                                                     ";
      alfa1 = "";
      |SI nHayTipo2 = 1; alfa1 = "Cuenta: " + #4#1 + "  "; |FINSI;
      alfa2 = "";
      |SI nHayTipo3 = 1;
          alfa2 = "Asiento: " + (("00" + #4#18) % -102) + "-" + #4#8 + "-" + (("000000" + #4#9) % -106);
      |FINSI;
      alfa1 = alfa1 + alfa2;
      |PINTA 1830, alfa1;
      |ATRI 0; |PINTA 1808, "³";
               |PINTA 1817, "³";
      |ENTLINEAL #2#904, -1, 7, 23, 0, mini904, maxi904;
  |FINSI;
|FPRC;

|PROCESO PasarTodasLineas;
  alfa2 = #102#2; |QBF alfa2;
  |SI alfa2 ! "";
      |SI alfa1 ! "";
          alfa1 = alfa1 + " " + alfa2;
      |SINO;
          alfa1 = alfa2;
      |FINSI;
  |FINSI;
  alfa2 = alfa1 % 0;
  nNume2 = alfa2;
  |SI nNume2 ] 100; |ERROR10; |FINSI;
|FPRC;

|DEFBUCLE PasarTodasLineas;
   #102#1;
   ;
   #101#13, #101#0, 0001;
   #101#13, #101#0, 9999;
   e;
   NULL, PasarTodasLineas;
|FIN;

|PROCESO PasarTodas;
   nNumero = nNumero + 1;
   #904#0 = nNumero;
   #904#1 = #101#2;
   #904#2 = "PDTE";
   |SI #101#8 = "R"; #904#2 = "RZDO"; |FINSI;
   |SI #101#8 = "C"; #904#2 = "CANC"; |FINSI;
   |SI #101#8 = "X"; #904#2 = "EJ.P"; |FINSI;
   alfa1 = #101#9;
   alfa1 = (alfa1 % 102) + "/" + (alfa1 % 402) + "/" + (alfa1 % -102);
   #904#3 = alfa1;
   alfa1 = "";
   |BUCLE PasarTodasLineas;
   #904#4 = alfa1;
   |GRABA 041#904n;
   |LIBERA #101;
   |SI #101#2 = 2; nHayTipo2 = 1; |FINSI;
   |SI #101#2 = 3; nHayTipo3 = 1; |FINSI;
|FPRC;

|DEFBUCLE PasarTodas2;
   #101#3;
   ;
   #4#23, nTipoEx, #4#1, "01.01.0000", 0001;
   #4#23, nTipoEx, #4#1, "31.12.2099", 9999;
   e;
   NULL, PasarTodas;
|FIN;

|DEFBUCLE PasarTodas3;
   #101#2;
   12;
   #4#23, nTipoEx, #4#8, #4#18, #4#9, "01.01.0000", 0001, #4#24;
   #4#23, nTipoEx, #4#8, #4#18, #4#9, "31.12.2099", 9999, #4#24;
   e;
   NULL, PasarTodas;
|FIN;

|PROCESO CargoAnotacion;
   aFichero = ("2k" + Usuario);
   aFichero = (aFichero + "zzzzzzzz") % 108;
   |NOME_DAT #904 aFichero;
   |DELINDEX #904;

   nNumero = 0;
   nHayTipo2 = 0;
   nHayTipo3 = 0;
   |ABRE #904;
   nTipoEx = 2; |BUCLE PasarTodas2;
   nTipoEx = 3; |BUCLE PasarTodas3;
   |CIERRA #904;
|FPRC;
