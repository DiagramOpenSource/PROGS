|FICHEROS;
    n43ztr01 #0;

    n43datos #1;
    cacuenta #10;
    canempre #30;

    n43dato@ #11;
|FIN;

|VARIABLES;
   &entrada = 1;

   &efDFecha  = @;
   &efHFecha  = @;
   nSw        = 0;
   nCampo     = 0;
   nNume1     = 0;
   nPara1     = 0;
   nPara2     = 0;
   &r_emp     = 0;
   &r_per     = 0;
   aParam     = "";
   aAlfa1     = "";
   aAlfa2     = "";

   aDef       = "";
   aPath      = "";
   aPathFich  = "";

   fDFecha    = @;
   fHFecha    = @;
   nAny       = 0;
   nMes1      = 0;
   nMes2      = 0;
   aDias      = "";

   nUltimo    = 0;
   aQueQuiero = "";
   aNumCampos = "";
   nNumCampos = 0;
|FIN;

|INCLUYE dscont_i;

|| ********************************************************************
|CALCULO consulta;
  |SI FSalida ! 9;  |ACABA;  |FINSI;
  r_emp = 0;
  |SUB_EJECUTA_NP "caconemp";
  |ERROR;
  #0#6 = r_emp;
  |PTEC 802;
|FCAL;

|CALCULO esbuena;
  |SI FSalida = 9;  |ACABA;  |FINSI;

  |SI aParam = "";
      |ABRE #30;
      #30#2 = #0#6;
      #30#3 = 0;
      |LEE 011#30];
      |SI FSalida ! 0;  |O #30#2 ! #0#6;
          |MENAV "     NO Existe la Empresa Contable";
          |ERROR;
      |SINO;
          #0#7 = #30#1; |PINTA #0#7;
      |FINSI;
      |CIERRA #30;
  |FINSI;
|FCAL;

|PROCESO cuenta;|TIPO 6;
   |C_M #0Campo,0,aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   eaCuenta = #0Campo;
   salidas  = FSalida;
   |HAZ FiltraPuntos;
   #0Campo = eaCuenta; |PINTA #0Campo;
|FPRC;

|CALCULO CuentaHasta; | TIPO 0;   ||  hasta diario
   |SI FSalida = 2; |ACABA; |FINSI;

   |SI #0#1 < #0#0;  |ERROR;  |FINSI;
|FCAL;

|CALCULO FechaHasta; | TIPO 0;   ||  hasta fecha
   |SI FSalida = 2; |ACABA; |FINSI;

   |SI #0#3 < #0#2;
       |MENAV "    Error! Fecha Hasta inferior a Fecha Desde";
       |ERROR;
       |ACABA;
   |FINSI;

   |SI enEmpresa = #0#6;
       |SI #0#2 ] fec1; |Y #0#3 [ fec2;
           |MENAV "    Error.! La Empresa Destino es la Empresa y Periodo Actuales";
           |ERROR;
           |ACABA;
       |FINSI;
   |FINSI;

||   nSw = 0;
||   |HAZ Empresas;
|FCAL;

|CALCULO ContadorHasta; | TIPO 0;   ||  hasta documento
   |SI FSalida = 2; |ACABA; |FINSI;

   |SI #0#5 < #0#4;  |ERROR;  |FINSI;
|FCAL;

|| *************************************************************
|PROCESO CalFechas;
   |SI nAny > 80;
       nAny = 1900 + nAny;
   |SINO;
       nAny = 2000 + nAny;
   |FINSI;
   aAlfa2 = nAny;

   aAlfa1 = nMes1; aAlfa1 = ("00" + aAlfa1) % -102;
   aAlfa1 = "01." + aAlfa1 + "." + aAlfa2;
   fDFecha = aAlfa1;

   |SI nMes1 > nMes2;
       nAny = nAny + 1;
       aAlfa2 = nAny;
   |FINSI;

   aAlfa1 = nMes2; aAlfa1 = ("00" + aAlfa1) % -102;
   aDias  = "31";
   |SI nMes2 = 4; |O nMes2 = 6; |O nMes2 = 9; |O nMes2 = 11;
       aDias = "30";
   |FINSI;
   |SI nMes2 = 2;
       aDias = "28";
       |SI nAny = 2000; |O nAny = 2004; |O nAny = 2008; |O nAny = 2012;
          |O nAny = 2016; |O nAny = 2020; |O nAny = 2024; |O nAny = 2028;
         aDias = "29";
      |FINSI;
   |FINSI;
   aAlfa1 = aDias + "." + aAlfa1 + "." + aAlfa2;
   fHFecha = aAlfa1;
|FPRC;

|PROCESO LeeEmpresas;

  |SI #30#2 = enEmpresa; |Y #30#3 = enPeriodo;
      || ... La Empresa Actual
      |ACABA;
  |FINSI;

  nAny  = #30#26;
  nMes1 = #30#11;
  nMes2 = #30#12;
  |HAZ CalFechas;
  |SI #0#2 [ fHFecha; |Y #0#3 ] fDFecha;
      aAlfa1 = #30#2; aAlfa1 = ("00000" + aAlfa1) % -105;
      aAlfa2 = #30#3; aAlfa2 = ("0"     + aAlfa2) % -101;
      aPathFich = aPath + "fich/" + aAlfa1 + aAlfa2 + "/";

                           #0nCampo = #30#3;
      nNume1 = nCampo + 1; #0nNume1 = fDFecha;
      nNume1 = nCampo + 2; #0nNume1 = fHFecha;
      nNume1 = nCampo + 3; #0nNume1 = aPathFich;

      nSw    = 1;
      nCampo = nCampo + 4;
  |FINSI;
|FPRC;

|DEFBUCLE LeeEmpresas;
  #30#1;
  ;
  #0#6, 0;
  #0#6, 9;
  e;
  NULL, LeeEmpresas;
|FIN;

|PROCESO Empresas;

   |PDEFECTO #0, 9, 49;

   nSw    = 0;
   nCampo = 9;
   |BUCLE LeeEmpresas;
   |SI nSw = 0;
       |MENAV "    Error. No existe Periodos dentro de esta Seleccion";
   |FINSI;
|FPRC;

|| **************************************************************************
|PROCESO pasalo;
   nUltimo = nUltimo + 1;

   #11#0 = nUltimo;

   |PARA nPara2 = 1; |SI nPara2 [ nNumCampos; |HACIENDO nPara2 = nPara2 + 1;
         #11nPara2 = #1nPara2;
   |SIGUE;

   |GRABA 040#11n;

   |BORRA 020#1a;
   |LIBERA #1;
|FPRC;

|DEFBUCLE n43datos;
   #1#3;
   9, 11;
   #0#0, fDFecha, "N", "N";
   #0#1, fHFecha, "N", "N";
   be;
   NULL, pasalo;
|FIN;

|PROCESO HazTodo;

   |PARA nPara1 = 9; |SI nPara1 [ 45; |HACIENDO nPara1 = nPara1 + 4;

         |SI #0nPara1 ! -1;
             nCampo    = nPara1 + 1; fDFecha   = #0nCampo;
             nCampo    = nPara1 + 2; fHFecha   = #0nCampo;
             nCampo    = nPara1 + 3; aPathFich = #0nCampo; |QBF aPathFich;
             |PATH_DAT #11 aPathFich;

             nUltimo = 0;
             |ABRE #11;
             |LEE 040#11u;
             |SI FSalida = 0;
                 nUltimo = #11#0;
             |FINSI;
             |CIERRA #11;

             |SI fDFecha < #0#2; fDFecha = #0#2; |FINSI;
             |SI fHFecha > #0#3; fHFecha = #0#3; |FINSI;

             |ABRE #11;
             |BUCLE n43datos;
             |CIERRA #11;
         |FINSI;
   |SIGUE;
|FPRC;

|PROCESO pase; |TIPO 3;
   |SI #0#8 = "SI";
       |HAZ Empresas;

       |SI nSw = 1;

           aQueQuiero = "|NC";
           aNumCampos = #1#aQueQuiero#;
           nNumCampos = aNumCampos;
           nNumCampos = nNumCampos - 1;

           aDef = aPath + "def/n43datos.mas";
           |CARGA_DEF aDef, n43dato@;
           |SI FSalida ! 0;
               |MENAV "    Error cargando el def " + aDef;
               |ACABA;
           |FINSI;
           |HAZ HazTodo;

           |DESCARGA_DEF #n43dato@;
       |FINSI;
   |FINSI;
|FPRC;

|PROGRAMA;
   aParam = PARAMETRO; |QBF aParam;

   |SI aParam = "";
       |HAZ inicio;
   |FINSI;

   |DBASE aPath; |QBF aPath;

   |DDEFECTO #0;
   #0#6 = enEmpresa;
   #0#7 = eaNombreEmp;

   |SI aParam ! "";
       |C_M #0#6, 1, "N";
       #0#0 = eaCuenta; |C_M #0#0, 1, "N";
       #0#1 = eaCuenta; |C_M #0#1, 1, "N";
       #0#2 = efDFecha;
       #0#3 = efHFecha;
       #0#8 = "SI";
   |SINO;
       |ABRE #1;
       |SELECT #2#1;
       |LEE 001#1p;
       |SI FSalida ! 0;
           |MENAV "    Atencion. No existe Datos Importados de la Norma 43";
           |CIERRA #1;
           |ACABA;
       |FINSI;
       #0#0 = #1#1;

       |LEE 001#1u;
       #0#1 = #1#1;

       |SELECT #5#1;
       |LEE 001#1p;
       #0#2 = #1#2;

       |LEE 001#1u;
       #0#3 = #1#2;

       |SELECT #1#1;
       |CIERRA #1;
   |FINSI;

   |CLS;
   |CABEZA "    Mover Datos Importados";
   |PINPA #0#0;
   |PINDA #0#0;

   |ENDATOS #1#0;
|FPRO;
