|FICHEROS;
  cainvaux #0;
  camoauxl #1;
  camoauxc #2;
  cacuenta #3;
  catraaux #4;
  caconimp #8;
  cafantas #10;

  canempre #30;
  caselem1 #998;
  cawdatos #900;
|FIN;

|VARIABLES;
   informe = "";
    &pagina = 0;
    sw_pagina = 0;
    error1 = "0000Nivel repetido";
    error2 = "0000No existe informe";
    error3 = "0000El numero de nivel no coincide con el numero de nivel de la empresa";
    error4 = "0000El numero de nivel coincide con el nivel de las cuentas de trabajo";
    men2  = "     Longitud de Cuenta Fuera de Nivel !";

    sw = 0;
    para1 = 0;
    nume1 = 0;
    nume2 = 0;
    aAlfa = "";
    alfa1 = "";
    alfa2 = "";
    digito = 0;

   nivel = 0;
   menor = 0;

   cue_ac = "";
   dig_ac = 0;

   p_alfa1 = "";
   p_error = 0;

   &aElDepar = "";
   &a_depar = "";
   &a_cuen = "";
   &a_digi = 0;
   &a_desc = "";
   &a_busc = "";
   &a_impo = 0;
   &total  = 0;
   &swlinea = 0;
   &descri = "";

   salida = 0;
   descue = "";
   hascue = "";
   swgraba = 0;
   swalgun = 0;

   eaDDepar = "";
   eaHDepar = "";

   &enNumPag = 0;
   nSwLInv   = 0;
   aParam    = "";
   aFichero  = "";
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoto_i;
|INCLUYE dslegv_i;

|| ** LEGALIZACION SOPORTE MAGNETICO

|PROCESO Legalia;

     #48#2 = eaLegEmp;
     #48#3 = eaLegPer;
     |LEE 000#48=;
     |SI FSalida = 0;
          |DBASE fich_alta;
          |QBF fich_alta;
          fich_alta = fich_alta + "fich/" + eaLegEmp + eaLegPer + "/";
          |PATH_DAT #1 fich_alta;
          |PATH_DAT #2 fich_alta;
          |PATH_DAT #3 fich_alta;
          |PATH_DAT #4 fich_alta;

          |HAZ inicio;

          #0#4 = 1;
          |SI dig4 [ 4; |Y dig4 ! 0; #0#5   = 1; |FINSI;
          |SI dig5 [ 4; |Y dig5 ! 0; #0#5   = 2; |FINSI;
          |SI dig6 [ 4; |Y dig6 ! 0; #0#5   = 3; |FINSI;
          |SI dig7 [ 4; |Y dig7 ! 0; #0#5   = 4; |FINSI;
          |SI dig8 [ 4; |Y dig8 ! 0; #0#5   = 5; |FINSI;
          |SI dig9 [ 4; |Y dig9 ! 0; #0#5   = 6; |FINSI;
          nivel = #0#5;
          #0#6 = 0;
          #0#7 = 0;
          #0#8 = 0;
          #0#9 = 0;
          #0#10 = 0;
          #0#11 = efFecLeg;
          |SI nSwLInv = 1; #0#12 = enNumPag; |FINSI;
          #0#18 = "  ";
          #0#19 = "zz";
          |HAZ impre;
          enNumPag = pagina;
   |FINSI;
|FPRC;

|| ***********************************************************************

|PROCESO &c_pagina;
  |SI sw_pagina = 0;
      pagina = #0#12 + 1;
      sw_pagina = 1;
  |SINO;
      pagina = pagina + 1;
  |FINSI;
|FPRC;

|| ***********************************************************************
||                       CALCULOS DE PANTALLA
|| ***********************************************************************
||----------------------------------------------------
|CALCULO AntesDepar; |TIPO 7;
   |C_M #0#18,1,eaDepar;
   |C_M #0#19,1,eaDepar;
|FCAL;

|PROCESO DatosEmp; |TIPO 7;
 |ABRE #8;
 |DDEFECTO #8;
 #8#0 = 1;
 |LEE 011#8=;
 |CIERRA #8;
 #0#23 = #8#7; |PINTA #0#23;
 alfa1 = #8#8; |C_M #0#23,1,alfa1;
|FPRC;

|CALCULO numnivel;|TIPO 0;
  |SI #0Campo ] #30#13;
      |MENAV error3;
      |ERROR;
  |FINSI;
|FCAL;

|CALCULO nivel;|TIPO 7;  || PROCESO para hacer no modificable

 |CAMPO_MODIFICA #0Campo, 1,"S";
 |SI Campo = 6; |Y #0#4 < 2; || Si solo hemos seleccionado
     #0Campo = 0; |PINTA #0Campo;
     |CAMPO_MODIFICA #0Campo, 1,"N";
 |FINSI;
 |SI Campo = 7; |Y #0#4 < 3; || Si solo hemos seleccionado
     #0Campo = 0; |PINTA #0Campo;
     |CAMPO_MODIFICA #0Campo, 1,"N";
 |FINSI;
 |SI Campo = 8; |Y #0#4 < 4; || Si solo hemos seleccionado
     #0Campo = 0; |PINTA #0Campo;
     |CAMPO_MODIFICA #0Campo, 1,"N";
 |FINSI;
 |SI Campo = 9; |Y #0#4 < 5; || Si solo hemos seleccionado
     #0Campo = 0; |PINTA #0Campo;
     |CAMPO_MODIFICA #0Campo, 1,"N";
 |FINSI;
 |SI Campo = 10; |Y #0#4 < 6; || Si solo hemos seleccionado
    #0Campo = 0; |PINTA #0Campo;
    |CAMPO_MODIFICA #0Campo, 1,"N";
 |FINSI;
|FCAL;

|CALCULO nivelrep; |TIPO 0;  || Para comprobar que no repetimos los niveles

 |SI #0Campo ! 0; |Y #0Campo ] #30#13;
      |MENAV error4;
      |ERROR;
      |ACABA;
 |FINSI;

 |SI nivel = 0;        || Buscamo el nivel mas alto;
     nivel = #0#5;
 |FINSI;

 |SI Campo = 6;
    |SI #0#6 = #0#5;|Y #0#6 ! 0;    || Si hay dos niveles iguales error
      |MENAV error1;
      |ERROR;
      |ACABA;
   |SINO;
       |SI #0#6 > #0#5;  || Busca el nivel mas alto
           nivel = #0#6;
       |SINO;
           nivel = #0#5;
       |FINSI;
   |FINSI;
 |FINSI;

 |SI Campo = 7; |Y #0#7 ! 0;
     |SI #0#7 = #0#5; |O #0#7 = #0#6; || Si hay dos niveles iguales error
       |MENAV error1;
       |ERROR;
       |ACABA;
    |FINSI;

    |SI #0#7 > nivel;  || Busca el nivel mas alto
        nivel = #0#7;
    |FINSI;
 |FINSI;

 |SI Campo = 8; |Y #0#8 ! 0;      || Si hay dos niveles iguales error
    |SI #0#8 = #0#5; |O #0#8 = #0#6; |O #0#8 = #0#7;
       |MENAV error1;
       |ERROR;
       |ACABA;
    |FINSI;

    |SI #0#8 > nivel;  || Busca el nivel mas alto
        nivel = #0#8;
    |FINSI;
 |FINSI;

 |SI Campo = 9; |Y #0#9 ! 0;     || Si hay dos niveles iguales error
    |SI #0#9 = #0#5; |O #0#9 = #0#6; |O #0#9 = #0#7; |O #0#9 = #0#8;
       |MENAV error1;
       |ERROR;
       |ACABA;
    |FINSI;

    |SI #0#9 > nivel;  || Busca el nivel mas alto
        nivel = #0#9;
    |FINSI;
 |FINSI;

 |SI Campo = 10; |Y #0#10 ! 0;    || Si hay dos niveles iguales error
    |SI #0#10 = #0#5; |O #0#10 = #0#6; |O #0#10 = #0#7; |O #0#10 = #0#8; |O #0#10 = #0#9;
       |MENAV error1;
       |ERROR;
       |ACABA;
    |FINSI;

    |SI #0#10 > nivel;  || Busca el nivel mas alto
        nivel = #0#10;
    |FINSI;
 |FINSI;
|FCAL;

|CALCULO cuenta; |TIPO 6;
    eaCuenta = #cainvaux#Campo#;
    salidas  = FSalida;
    |HAZ FiltraPuntos;
    #cainvaux#Campo# = eaCuenta;
    |PINTA #cainvaux#Campo#;

    digito = Campo + 2;
    eaCuenta = #0Campo; |HAZ SacaNivel; #0digito = enNivel;
|FCAL;

|| **********************************************************************
||                       CALCULOS DE IMPRESION
|| **********************************************************************
|PROCESO lee_cue;
 swgraba = 0;
 |SI #3#0 = #2#0; |Y #3#1 = #2#1; |ACABA; |FINSI;

 |SI #3#1 ! 0;
     |SI #3#1 = #0#5;|O #3#1 = #0#6;|O #3#1 = #0#7;
         swgraba = 1;
     |SINO;
         |SI #3#1 = #0#8;|O #3#1 = #0#9;|O #3#1 = #0#10;
             swgraba = 1;
         |FINSI;
     |FINSI;
 |FINSI;
 |SI swgraba = 1;
     cue_ac = #2#0; dig_ac = #3#1;
     |HAZ acortacuen;
     |SI #3#0 = cue_ac;
         a_cuen = #3#0;
         a_digi = #3#1;
         a_desc = #3#2;
         a_busc = "N";
         |HAZ graba_reg;
    |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE cainvaux1;
 #3#1;
 ;
 descue;
 hascue;
 e;
 NULL, lee_cue;
|FIN;

|PROCESO lee_aux;
   a_depar  = #2#6;
   a_cuen   = #2#0;
   eaCuenta = #2#0; |HAZ SacaNivel; #2#1 = enNivel;
   a_digi   = #2#1;
   a_desc   = #2#2;
   a_busc   = "S";
   a_impo   = #2#3;
   |HAZ graba_reg;

|| .............. Cuenta desde
   cue_ac = #2#0; dig_ac = menor; |HAZ acortacuen;
   descue = cue_ac;
   cue_ac = #2#0; dig_ac = nivel; |HAZ acortacuen;
   hascue = cue_ac;

   |BUCLE cainvaux1;
|FPRC;

|DEFBUCLE cainvaux0;
 #2#1;
 ;
 #0#18, #0#0;
 #0#19, #0#1;
 e;
 NULL, lee_aux;
|FIN;

|| //////////////////////////////////////////////////////////

|PROCESO acortacuen;
    |SI dig_ac = 1;  nume1 = dig4;  |FINSI;
    |SI dig_ac = 2;  nume1 = dig5;  |FINSI;   || construye el hasta segun
    |SI dig_ac = 3;  nume1 = dig6;  |FINSI;   ||/el nivel mas bajo elegido
    |SI dig_ac = 4;  nume1 = dig7;  |FINSI;
    |SI dig_ac = 5;  nume1 = dig8;  |FINSI;
    |SI dig_ac = 6;  nume1 = dig9;  |FINSI;
    nume1 = 100 + nume1;
    cue_ac = cue_ac % nume1;
    cue_ac = cue_ac + "            ";
    cue_ac = cue_ac % 112;
|FPRC;

|| ......................................
|PROCESO graba_reg;
  #4#5 = a_depar;
  #4#0 = a_cuen;
  #4#1 = a_digi;
  |LEE 101#4=;
  salida = FSalida;
  |SI salida ! 0;
      |DDEFECTO #4;
      #4#5 = a_depar;
      #4#0 = a_cuen;
      #4#1 = a_digi;
  |FINSI;
  #4#2 = a_desc;
  #4#3 = a_busc;
  #4#4 = #4#4 + a_impo;
  |SI salida = 0; |GRABA 020#4a; |FINSI;
  |SI salida ! 0; |GRABA 020#4n; |FINSI;
  |LIBERA #4;
|FPRC;
|| ............................................
|| ////////////////////////////////////////////////////////////////////

|PROCESO implineas;
 swalgun = 1;
 swlinea = 2;
 |PRINT;
|FPRC;

|PROCESO imprimir;

  |SI sw = 0;
      aElDepar = #4#5;
      |HAZ VerDepar;
      |SI eswLect = 1;
          |ACABA;
      |FINSI;
      sw = 1;
  |FINSI;

  |SI aElDepar ! #4#5;
      |SI sw = 2; |PIEINF; |FINSI;
      total = 0;
      || ... sw_pagina = 0;
      sw = 1;
      aElDepar = #4#5;
      |HAZ VerDepar;
      |SI eswLect = 1; |ACABA; |FINSI;
  |FINSI;

  a_depar = #4#5;
  a_cuen = #4#0;
  a_digi = #4#1;
  a_desc = #4#2;
  a_impo = #4#4;
  |HAZ descripcion;
  |SI #4#3 = "S";
      swalgun = 0;
      #2#6 = #4#5;
      #2#0 = #4#0;
      #2#1 = #4#1;
      |LEE 000#2=;
      |SI FSalida = 0;
          |BUCLELIN 2implineas#2;
      |FINSI;
      swlinea = 1;
      |SI swalgun = 1; swlinea = 3; |FINSI;
      total = total + #4#4;
  |SINO;
      a_impo = 0;
      swlinea = 1;
      |SI #4#1 = menor; swlinea = 0; |FINSI;
  |FINSI;
  |PRINT; sw = 2;
|FPRC;

|DEFBUCLE cainvaux2;
 #4#1;
 ;
 "  ", "            ";
 "zz", "zzzzzzzzzzzz";
 e;
 NULL, imprimir;
|FIN;

|PROCESO descripcion;
    descri = "";
    |SI a_digi = 1;  nume1 = dig4; alfa2 = ""; |FINSI;
    |SI a_digi = 2;  nume1 = dig5; alfa2 = "  "; |FINSI;   || construye el hasta segun
    |SI a_digi = 3;  nume1 = dig6; alfa2 = "    "; |FINSI;   ||/el nivel mas bajo elegido
    |SI a_digi = 4;  nume1 = dig7; alfa2 = "      "; |FINSI;
    |SI a_digi = 5;  nume1 = dig8; alfa2 = "        "; |FINSI;
    |SI a_digi = 6;  nume1 = dig9; alfa2 = "          "; |FINSI;
    nume1 = 100 + nume1;
    descri = a_cuen % nume1;
    descri = alfa2 + descri + "  ";
    descri = descri + a_desc;
|FPRC;

|| ***********************************************************************
|| ***************** PROCESO PRINCIPAL CON TIPO 3 ************************
|| ***********************************************************************

|PROCESO elmenor;
    menor = -1;
|PARA para1 = 5; |SI para1 [ 10; |HACIENDO para1 = para1 + 1;
    |SI #0para1 ! 0;
        |SI menor = -1;  menor = #0para1;  |FINSI;
        |SI menor > #0para1;  menor = #0para1;  |FINSI;
    |FINSI;                   || saca el nivel elegido mas peque¤o
|SIGUE;
|FPRC;

|| ***********************************************************************
|PROCESO impre;

 |SI aParam ! "PDF";
     |SI aParam ! "";
          |SI nSwLInv = 0;
              Impresora = eaNomImp + "?"+ eaUnidadBTmp + "\tmp1,,,?Inventario";
              |SI enBasImp ! 0;
                  |IMPRE -1;
              |SINO;
                  |IMPRE 0;
              |FINSI;
              |SI FSalida ! 0; |ACABA; |FINSI;
          |FINSI;
     |SINO;
          Impresora = "Crystal Reports";
          |IMPRE 0;
          |SI FSalida ! 0; |ACABA; |FINSI;
     |FINSI;
 |FINSI;

 |HAZ elmenor;

 aFichero = ("1i"+ Usuario) % 108;
 |NOME_DAT #4 aFichero;

 |SI aParam ! "PDF";
     informe = "cainvaux";
     |SI #0#17 = "S"; informe = "cainvau2"; |FINSI;
     |SI #0#23 = "N";
         informe = (informe % 102) + "n" + (informe % -105);
     |FINSI;
     aAlfa = Impresora;
     aAlfa = aAlfa % 113;
     |SI aAlfa = "CrystalReport";
         informe = "cc" + (informe % 0306);
     |FINSI;

     |INFOR informe;
     |SI FSalida ! 0;
         |MENAV error2;
         |SI nSwLInv = 0; |FINIMP; |FINSI;
         |ACABA;
     |FINSI;
 |FINSI;

 |SI aParam = "";
     |SI #48#27 = "S";
         |HAZ MultiEmpresa;
     |SINO;
         |DFICE dirfich0; |QBF dirfich0;
         |HAZ HazTodo;
     |FINSI;
 |SINO;
     |HAZ HazTodo;
 |FINSI;

 |SI aParam ! "PDF";
     |FININF;
     |SI nSwLInv = 0; |FINIMP; |FINSI;
 |FINSI;

|FPRC;


|PROCESO HazTodo;
  |SI aParam ! "PDF";
      eaIa = #30#21;
      enM1 = #30#11;
      enM2 = #30#12;
      |HAZ LeeDatosEmpresa;
      |SI #900#13 < #900#9;  |Y #900#9  > "01.01.0000"; #900#13 = #900#9;  |FINSI;
      |SI #900#14 > #900#10; |Y #900#10 > "01.01.0000"; #900#14 = #900#10; |FINSI;
  |FINSI;

  total = 0;
  sw_pagina = 0;
  |DELINDEX #4;

  |ABRE #4;
  |BUCLE cainvaux0;
  |CIERRA #4;

  |SI aParam ! "PDF";
      sw = 0;
      |ABRE #2;
      |BUCLE cainvaux2;
      |SI sw ] 1;
          |SI sw = 2; |PIEINF; |FINSI;
      |FINSI;
      |CIERRA #2;

      |DELINDEX #4;
  |FINSI;
|FPRC;

|PROCESO HagoPdf;
    |DDEFECTO #0;
    #0#0  = aCTodo44;
    #0#1  = aCTodo45;
    #0#4  = nCTodo42;
    #0#5  = nCTodo19;
    #0#6  = nCTodo20;
    #0#7  = nCTodo21;
    #0#8  = nCTodo22;
    #0#9  = nCTodo23;
    #0#10 = nCTodo24;

    |SI dig3 = #0#5;  #0#5  = 0; |FINSI;
    |SI dig3 = #0#6;  #0#6  = 0; |FINSI;
    |SI dig3 = #0#7;  #0#7  = 0; |FINSI;
    |SI dig3 = #0#8;  #0#8  = 0; |FINSI;
    |SI dig3 = #0#9;  #0#9  = 0; |FINSI;
    |SI dig3 = #0#10; #0#10 = 0; |FINSI;

   nume1 = 0; nume2 = 0;
   |SI #0#5 ! 0; nume1 = nume1 + 1; |FINSI;
   |SI #0#6 ! 0; nume1 = nume1 + 1; |FINSI;
   |SI #0#7 ! 0; nume1 = nume1 + 1; |FINSI;
   |SI #0#8 ! 0; nume1 = nume1 + 1; |FINSI;
   |SI #0#9 ! 0; nume1 = nume1 + 1; |FINSI;

   |SI #0#10 ! 0;
       |SI #0#5 = 0;
           nume2 = 5;
       |SINO;
           |SI #0#6 = 0;
               nume2 = 6;
           |SINO;
               |SI #0#7 = 0;
                   nume2 = 7;
               |SINO;
                   |SI #0#8 = 0;
                       nume2 = 8;
                   |SINO;
                      |SI #0#9 = 0;
                          nume2 = 9;
                      |FINSI;
                   |FINSI;
               |FINSI;
           |FINSI;
       |FINSI;
       nume1 = nume1 + 1;
       |SI nume2 ! 0;
           #0nume2 = #0#10; #0#10 = 0;
       |FINSI;

   |FINSI;
   |SI nume1 ! 0;
       #0#4 = nume1;
   |SINO;
       #0#4 = 1;
       #0#5 = dig3 - 1;
   |FINSI;
   nivel = #0#5;
   |SI #0#6 > nivel; nivel = #0#6; |FINSI;
   |SI #0#7 > nivel; nivel = #0#7; |FINSI;
   |SI #0#8 > nivel; nivel = #0#8; |FINSI;
   |SI #0#9 > nivel; nivel = #0#9; |FINSI;
   #0#17 = aCTodo46;
   #0#18 = aCTodo47;
   #0#19 = aCTodo48;

   |PATH_DAT #1  dirfich0;
   |PATH_DAT #2  dirfich0;
   |PATH_DAT #3  dirfich0;
   |PATH_DAT #8  dirfich0;
   |HAZ impre;

|FPRC;


|PROGRAMA;
   aParam = PARAMETRO; |QBF aParam;

   |HAZ DirAplicSt;
   |SI aParam = "";
          |CLS;
          |CABEZA "Inv.Existencias Aux";
          |HAZ inicio;
          |PINPA #0#0;
          |PINDA #0#0;
          |ENDATOS #1#0;
          |SI FSalida = 0;
               |HAZ impre;
          |FINSI;
   |SINO;
          nSwLInv = 0;
          |SI aParam = "L"; nSwLInv = 1; |FINSI;

          |SI aParam = "PDF";
              |HAZ HagoPdf;
              |ACABA;

          |FINSI;

          #0#11 = efFecLeg;
          #0#23 = eaDatLeg;
          |ABRE #48;
          |HAZ Legalia;
          |CIERRA #48;
   |FINSI;
|FPRO;

||*************************************************************************
|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #2 dirfich0;
   |PATH_DAT #3 dirfich0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 001;
 999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;

||*************************************************************************

|PROCESO VerDepar;
  eswLect = 0;
  #0#20  = "";
  #0#21 = "01.01.0000";
  #0#22 = "01.01.0000";
  eOpDep = 0;
  enActividad = aElDepar;
  eaCodDep    = aElDepar;
  |HAZ Actividad;
  |SI eswLect = 0;
      #0#20  = eaNombreAct;
      #0#21  = efActIni;
      #0#22  = efActFin;
  |FINSI;
|FPRC;
