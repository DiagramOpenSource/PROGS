|FICHEROS;
    camaasic #10;
    camaasil #11;
    cacuenta #2,MANTE;
    caextrp4 #6;
    caivaasi #200;
    caivaaxx;
    camaniva #201;
    caivalin #202;

    canempre #30;
|FIN;

|VARIABLES;
     &entrada    = 1;
     aAlfa       = "";
     aAlfa1      = "";
     aAlfa2      = "";
     aAlfa3      = "";
     nNume1      = 0;
     nNume2      = 0;
     nNume3      = 0;
     nPerio      = 0;
     nEjerC      = 0;
     &NumDoc     = 0;
     &enOpera    = 0;
     &nConta     = 0;
     &nEmpresa   = 0;
     &nPeriodo   = 0;
     &fFecConta  = @;
     nAny        = 0;
     seleccio    = 0;
     aFichero    = "";
     totallin    = 0;
     &enDnDIARIO = 0;
     &enHnDIARIO = 0;
     &efDFechas  = @;
     &efHFechas  = @;
     &enDDocume  = 0;
     &enHDocume  = 0;
     &enBorraAu  = 0;
     &enNoExis   = 0;
     &elDiario   = 0;
     &elDocumen  = 0;
     &laFactura  = 0;
     &eaModif    = "";
|FIN;

|INCLUYE dscont_i;

|| ************************************************************************
|PROCESO SituaEmpresa;
    cod1 = enEmpresa;
    cod2 = enPeriodo;
    emEmp = 1;  || Sin mensaje
    |HAZ leelo;
    |SI swerror ! 0;
        |MENAV "0000Error, ya no existe la Empresa Contable";
        |ACABA;
    |FINSI;

    aAlfa1 = ("00000" + enEmpresa) % -105;
    aAlfa2=  ("0"     + enPeriodo) % -101;
    dirfich0 =  aPContag + "fich/" + aAlfa1 + aAlfa2 + "/";

    |PATH_DAT #10 dirfich0;
    |PATH_DAT #11 dirfich0;
    |PATH_DAT #2  dirfich0;
    |PATH_DAT #200 dirfich0;
    |PATH_DAT #caivaaxx#dirfich0#;
    |PATH_DAT #201 dirfich0;
    |PATH_DAT #202 dirfich0;

    |HAZ eActivParam;
|FPRC;

|PROCESO ModiPan_2;
   aAlfa1 = "N";
   |SI #10#7 ! 0;
       aAlfa1 = "S";
       |ATRI R; |PINTA 0861, "Asto Predef.: "; |ATRI 0;
   |FINSI;
   |C_V #6#17, 1, aAlfa1;

   |SI eaSubde = "N";
        |C_A #6#6, 1, 29;
        |C_P #6#7, 1, 1266;
        |C_V #6#16,1, "N";
        |PINTA 0961, "ÄÄÄÄÂÄÄ";
        |PINTA 1061, "    ³  ";
        |PINTA 1161, "ÄÄÄÄÅÄÄ";
        |PINTA 1261, "    ³  ";
        |PINTA 1361, "    ³  ";
        |PINTA 1461, "    ³  ";
        |PINTA 1561, "    ³  ";
        |PINTA 1661, "    ³  ";
        |PINTA 1761, "    ³  ";
        |PINTA 1861, "    ³  ";
        |PINTA 1961, "    ³  ";
        |ATRI R; |PINTA 1061, "    "; |PINTA 1066, "Dp"; |ATRI 0;
   |SINO;
        |SI enSubde = 1; |Y eaDepar = "N";
            |C_A #6#6, 1, 28;
            |C_V #6#7, 1, "N";
            |PINTA 0961, "Ä";
            |ATRI R; |PINTA 1061, "   "; |ATRI 0;
            |PINTA 1161, " ";
            |PINTA 1261, " ";
            |PINTA 1361, " ";
            |PINTA 1461, " ";
            |PINTA 1561, " ";
            |PINTA 1661, " ";
            |PINTA 1761, " ";
            |PINTA 1861, " ";
            |PINTA 1961, " ";
            |PINTA 2061, " ";
        |FINSI;
   |FINSI;
|FPRC;

|PROCESO lee_asiento1;             || CARGA LA PANTALLA DEL ASIENTO COMPLETO
   seleccio = 1;

   |ABRE #2;
   |DDEFECTO #2;
   #2#0 = #11#4;
   |LEE 001#2=;
   |CIERRA #2;

   |DDEFECTO #6;
   #caextrp4#00 = #11#3;        || apunte
   #caextrp4#01 = #11#16;       || cuenta DEBE
   #caextrp4#02 = #11#17;       || digito DEBE
   #caextrp4#03 = #11#10;       || cuenta HABER
   #caextrp4#04 = #11#11;       || digito HABER
   #caextrp4#05 = #11#6;        || concepto
   #caextrp4#06 = #11#7;        || descripcion
   #caextrp4#07 = #11#21;       || departamento
   #caextrp4#08 = #11#9;        || importe
   #caextrp4#09 = #2#2;        || descripcion
   #caextrp4#10 = #2#51;       || debe
   #caextrp4#11 = #2#52;       || haber
   #caextrp4#12 = #2#53;       || saldo
   #caextrp4#13 = #11#0;        || diario
   #caextrp4#14 = #11#1;        || fecha
   #caextrp4#15 = #11#2;        || documento
   #caextrp4#16 = #11#28;       || Subdepartamento
   #caextrp4#17 = #10#7;
   |GRABA 020#6n;
   totallin = totallin + 1;
|FPRC;

|DEFBUCLE lee_asiento;
   #11#1;
   ;
   #201#7, #201#8, #201#9,    1;
   #201#7, #201#8, #201#9, 9999;
   e;
   NULL, lee_asiento1;
|FIN;

|PROCESO mini;
   #caextrp4#0 = 1;
|FPRC;

|PROCESO maxi;
   #caextrp4#0 = 9999;
|FPRC;

|PROCESO Asiento;

   aFichero = ("q" + Usuario);
   aFichero = (aFichero + "zzzzzzzz") % 108;
   |NOME_DAT #6 aFichero;
   |DELINDEX #6;

   seleccio = 0;
   |ABRE #10;
   |DDEFECTO #10;
   #10#0 = #201#7;
   #10#1 = #201#8;
   #10#2 = #201#9;
   |LEE 001#10=;
   |CIERRA #10;

   |ABRE #6;
   totallin = 0;
   |BUCLE lee_asiento;
   |CIERRA #6;

   |SI seleccio ! 0;
      |PUSHV 07012380;
      |PINPA #0#6; |HAZ ModiPan_2;
      |ENTLINEAL #1#6, -1, 4, 20, 0, mini, maxi;
      |POPV;
   |SINO;
      |MENAV "    Seleccion vacia ...";
   |FINSI;
   |DELINDEX #6;
|FPRC;

|RUTINA inf;
   #202#0 = #201#0;
   #202#1 = #201#1;
   #202#2 = 01;
|FRUT;

|RUTINA sup;
   #202#0 = #201#0;
   #202#1 = #201#1;
   #202#2 = 99;
|FRUT;

|PROCESO Consultas;
    enNoExis = 0;
    |HAZ SituaEmpresa;
    |SI swerror ! 0;
        |ACABA;
    |FINSI;

    |ABRE #201;
    #201#0 = enLibro;
    #201#1 = NumDoc;
    |LEE 041#201=;
    |SI FSalida ! 0;
        |MENAV "0000No existe Factura ";
        |CIERRA #201;
        |ACABA;
    |FINSI;

    |ABRE #10;
    |DDEFECTO #10;
    #10#0 = #201#7;
    #10#1 = #201#8;
    #10#2 = #201#9;
    |LEE 001#10=;
    |CIERRA #10;
    eaModif = "S";
    |SI #10#13 > 100;  eaModif = "N"; |FINSI;

    enNoExis  = 1;
    laFactura = #201#3;
    elDiario  = #201#7;
    elDocumen = #201#9;

    |SI enOpera = 12;
        |PUSHV 0501 2380;
        |PINPA #0#201;
        |PINDA #0#201;
        |ENTLINEAL #1#202, -3, 4, 22,1,inf,sup;
        |POPV;
    |FINSI;

    |SI enOpera = 13;
        |HAZ Asiento;
    |FINSI;

    |SI enOpera = 3; |O enOpera = 2;
        enDnDIARIO = #201#7;             || diario
        enHnDIARIO = #201#7;
        efDFechas  = #201#8;             || fecha
        efHFechas  = #201#8;             || fecha
        enDDocume  = #201#9;             || documento
        enHDocume  = #201#9;             || documento
        enBorraAu  = 4;

        |SUB_EJECUTA_NP "caborasi;NOMEQUITESQUETECORTOLOSGUEVOS";
    |FINSI;

|FPRC;

|PROCESO BuscoPeriodo;
    nConta = 0;
    aAlfa1 = fFecConta;
    aAlfa1 = aAlfa1 % -102;
    nEjerC = aAlfa1;

    |ABRE #30;
    |SELECT #4#30;
    |PARA nNume1 = -1; |SI nNume1 [ 1; |HACIENDO nNume1 = nNume1 + 1;
         nNume2 = nEjerC + nNume1;
         |DDEFECTO #30;
         #30#2  = nEmpresa;
         #30#26 = nNume2;
         |LEE 040#30=;
         |SI FSalida = 0;
             |SI  #30#11 = 1; |Y #30#26 = nEjerC;
                  nConta = 1;       || hay contabilidad
             |SINO;
                  |SI #30#11 ! 1;
                      |HAZ CalLaFecha;
                      |SI fFecConta ] fec1; |Y fFecConta [ fec2;
                           nConta = 1;
                      |FINSI;
                  |FINSI;
             |FINSI;
         |FINSI;
         |SI nConta = 1; nPeriodo = #30#3; |SAL; |FINSI;
    |SIGUE;
    |SELECT #1#30;
    |CIERRA #30;
|FPRC;

|PROCESO CalLaFecha;
  |SI #30#26 < 80;
      nAny = 2000 + #30#26;
  |SINO;
      nAny = 1900 + #30#26;
  |FINSI;

  aAlfa1 = nAny;
  aAlfa2 = #30#11; |QBF aAlfa2;
  aAlfa2 = ("00" + aAlfa2) % -102;
  aAlfa2 = "01." + aAlfa2 + "." + aAlfa1;
  fec1  = aAlfa2;

  |SI #30#11 ! 1;
      nAny = nAny + 1;
      aAlfa1 = nAny;
  |FINSI;
  aAlfa2 = #30#12; |QBF aAlfa2;
  aAlfa2 = ("00" + aAlfa2) % -102;

  aAlfa3 = "31.";
  |SI #30#12 = 4; |O #30#12 = 6; |O #30#12 = 9; |O #30#12 = 11;
      aAlfa3 = "30.";
  |SINO;
      |SI #30#12 = 2;
          aAlfa3 = "28.";
          nNume1 = nAny / 4; nNume2 = (nAny / 4) ! 0;
          |SI nNume1 = nNume2;
              aAlfa3 = "29.";
          |FINSI;
      |FINSI;
  |FINSI;
  aAlfa2 = aAlfa3 + aAlfa2 + "." + aAlfa1;
  fec2 = aAlfa2;
|FPRC;
