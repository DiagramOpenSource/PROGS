|FICHEROS;
   cay00001 #0;
   catesfin #1;
   caconimp #8;
   cacuenta #10;
   caz00001 #999;

   :/integral/def/dsam0001 #40;
   canempre #30;
   cawdatos #900;
   caselem1 #998;
|FIN;

|VARIABLES;
    aAlfa1    = "";
    aPuntearD = "";
    aPuntearH = "";
    aCuenta   = "";
    aAlfa     = "";
    nDigito   = 0;
    nPosi     = 0;
    aPosi     = "";
    nSwPrint  = 0;
    informe   = "";
    &eAlfa    = "";
    &nDelMant = 0;
    aBlancs   = "";
    aNum1     = "";
    nNume0    = 0;
    nNume1    = 0;
    nNume2    = 0;
    nNume3    = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE masivo_i;

|PROCESO DatosEmp; |TIPO 7;
 |ABRE #8;
 |DDEFECTO #8;
 #8#0 = 1;
 |LEE 011#8=;
 |CIERRA #8;
 #0#14 = #8#7; |PINTA #0#14;
 aAlfa1 = #8#8; |C_M #0#14,1,aAlfa1;
|FPRC;

|PROCESO SiInforme; |TIPO 0;
 |SI #0#1 = "C";
     |C_M #0#14,1,"N";
 |SINO;
     |HAZ DatosEmp;
 |FINSI;
|FPRC;
|| *******************************************************************

|PROCESO Imprime;
     |SI #1#3 > 0;  #999#0 = #999#0 + #1#3;     |FINSI;
     |SI #1#3 < 0;  #999#1 = #999#1 + (-#1#3);  |FINSI;
     #999#2 = #999#0 - #999#1;

     |SI #1#1 = 1;  nDigito = dig4;  |FINSI;
     |SI #1#1 = 2;  nDigito = dig4;  |FINSI;
     |SI #1#1 = 3;  nDigito = dig5;  |FINSI;
     |SI #1#1 = 4;  nDigito = dig6;  |FINSI;
     |SI #1#1 = 5;  nDigito = dig7;  |FINSI;
     |SI #1#1 = 6;  nDigito = dig8;  |FINSI;

     nPosi = 100 + nDigito;
     aPosi = nPosi;
     aCuenta = #1#0 % aPosi;

     #10#0 = aCuenta;
     |LEE 040#10=;
     |SI FSalida ! 0;  |DDEFECTO #10;  |FINSI;

     |SI #1#5 = "A";  eAlfa = "ACTIVA  ";  |FINSI;
     |SI #1#5 = "I";  eAlfa = "INACTIVA";  |FINSI;
     nSwPrint = 1;
     |PRINT;
|FPRC;

|DEFBUCLE catesfin;
     #1#1;
     6;
     "            ", aPuntearD;
     "zzzzzzzzzzzz", aPuntearH;
     ;
     NULL, Imprime;
|FIN;

|PROCESO HazTodo;

    |SI #0#1 = "I"; |Y #0#14 = "S";
        eaIa = #30#21;
        enM1 = #30#11;
        enM2 = #30#12;
        |HAZ LeeDatosEmpresa;
    |FINSI;

     nSwPrint = 0;
     #999#0 = 0;
     #999#1 = 0;
     #999#2 = 0;

     |ABRE #10;
     |BUCLE catesfin;
     |CIERRA #10;
     |SI nSwPrint = 1;
         |PIEINF;
     |FINSI;
|FPRC;

|PROCESO Tipo3;
|| ... Campos de la Direccion
     aAlfa = #0#5; |QBF aAlfa;     || ... Direccion
     aNum1 = aAlfa % 0; nNume1 = aNum1;

     aAlfa = #0#7;   |QBF aAlfa;
     |SI aAlfa ! ""; aAlfa = "("+aAlfa+")"; |FINSI;
     #0#10 = aAlfa; aAlfa = #0#6;   |QBF aAlfa;
     #0#10 = aAlfa + " " + #0#10;

     aAlfa = #0#10; |QBF aAlfa;     || ... Poblacion
     aNum1 = aAlfa % 0; nNume2 = aNum1;

     aAlfa = #0#8; |QBF aAlfa;      || ... Telefono
     aAlfa = "Tfno.: " + aAlfa;
     aNum1 = aAlfa % 0; nNume3 = aNum1;

     nNume0 = nNume1; |SI nNume0 < nNume2; nNume0 = nNume2; |FINSI;
                      |SI nNume0 < nNume3; nNume0 = nNume3; |FINSI;
     |SI nNume0  < 42;
         nNume0  = (42 - nNume0);
         aBlancs = " " * nNume0;
         #0#11  = aBlancs + #0#5;
         #0#10 = aBlancs + #0#10;
         #0#12  = aBlancs + "Tfno.: " + #0#8;
         #0#13  = aBlancs + "       " + #0#9;
     |FINSI;

||.....................................................................
     |SI enSwMvo = 0;
         |IMPRE 0;
         |SI FSalida ! 0;  |ACABA;  |FINSI;
     |SINO;
         |SI enNuMvo = 1; #0#1 = "I"; |FINSI;
         |SI enNuMvo = 2; #0#1 = "C"; |FINSI;
     |FINSI;

     |SI #0#1 = "I";  |Y #0#14 = "N"; enDatos = 0; informe = "cay10001";  |FINSI;
     |SI #0#1 = "I";  |Y #0#14 = "S"; enDatos = 1; informe = "cay1n001";  |FINSI;
     |SI #0#1 = "C";  informe = "cay20001";  |FINSI;

     |INFOR informe;
     |SI FSalida ! 0;
         |SI enSwMvo = 0; |FINIMP; |FINSI;
         |ACABA;
     |FINSI;

     aPuntearD = " "; aPuntearH = "z";
     |SI #0#1 = "C";
         aPuntearD = "S";
         aPuntearH = "S";
     |FINSI;

     |SI #48#27 = "S"; |Y nDelMant = 0; |Y enSwMvo = 0;
         |HAZ MultiEmpresa;
     |SINO;
         |DFICE dirfich0; |QBF dirfich0;
         |HAZ HazTodo;
     |FINSI;
     |FININF;
     |SI enSwMvo = 0; |FINIMP; |FINSI;
|FPRC;

|| ::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
|PROGRAMA;
     |HAZ inicio;
     |HAZ DirAplicSt;

     |PDEFECTO #0, 1, 3;
     |HAZ LeeDespacho;

     |SI enSwMvo = 0;
         |CLS;
         |CABEZA "Impresion S.Tesoreria y Financiacion";
         |PINPA #0#0;
         |PINDA #0#0;
         |ENDATOS #1#0;
         |SI FSalida = 0;
             |HAZ Tipo3;
         |FINSI;
     |SINO;
         |HAZ Tipo3;
     |FINSI;
|FPRO;

|PROCESO LeeDespacho;
 |SI HayAcceso = 0; |ACABA; |FINSI;
 |ABRE #40;
 |LEE 101#40p;
 |SI FSalida = 0;
     #0#3  = #40#1;
     #0#4  = #40#2;
     #0#5  = #40#3;
     #0#6  = #40#4;
     #0#7  = #40#5;
     #0#8  = #40#6;
     #0#9  = #40#7;
 |FINSI;
 |CIERRA #40;
|FPRC;

|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #10 dirfich0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 001;
 999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;
