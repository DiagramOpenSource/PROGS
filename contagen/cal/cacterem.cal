|FICHEROS;
    cacterem #0;

    camaasic #02;
    camaasil #03;
    cacuenta #05;

    camandia #10;
    cacuebal #11;
    caconasi #20;

    canempre #30;
    imagen@  #330;

    caivaasi #200;
    cadeflab #210;

    camaniva #204;
    caivalin #205;
    calibros #206;

    :/basica/def/agifigen #100;
    :/basica/def/socm0001 #101;
    :/basica/def/socm0002 #102;
|FIN;

|VARIABLES;
     &entrada    = 1;
     x           = 0;
     nCampo      = 0;
     nSwTipo     = 0;
     nPin1       = 0;
     nHay        = 0;
     aFichero    = "";
     nDEmpresa   = 0;
     nHEmpresa   = 0;
     fDFecha     = @;
     fHFecha     = @;
     aAlfa1      = "";
     aAlfa2      = "";
     aAlfa3      = "";
     fFecha      = @;
     nPara1      = 0;
     nPara2      = 0;
     nNume1      = 0;
     nNume2      = 0;
     nNume3      = 0;
     nEjer       = 0;
     aPath       = "";
     nSwConta    = 0;
     nSw         = 0;
     nSwOp       = 0;
     aCuenta     = "";
     nAlgun      = 0;

     &nEmpresa   = 0;
     &nConta      = 0;
     &fRegistro   = @;
     &fFecConta   = @;
     &nDiario     = 0;
     &nDocumento  = 0;
     &nImporte    = 0;
     &nPeriodo    = 0;
     &swIVA       = 0;
     &eaModif     = "";
     &nApunte     = 0;
     &nConcepto   = 0;
     &aDescrip    = "";
     &aCampo19    = "";
     &nCampo20    = 0;
     &nDebe       = 0;
     &nHaber      = 0;
     &aSigno      = "";
     &eaNomSocio  = "";
     &eaNifSocio  = "";
     &nBase       = 0;
     &nLibro      = 0;
     &nTipIRPF    = 0;
     &eaTituloL   = "";
     aDirInicio  = "";
     aMas        = "";
     swImpresora = 0;
     &aParam     = "";
     &enCalcCif  = 0;
     &eaVarDni   = "";
     f1          = @;
     f2          = @;
     aEstado     = "";
     nError      = 0;

     &men        = "";
     informe     = "cacte999";

  CodigoEmpresa  = 0;
  PeriodoEmpresa = 0;
 &enEmpresaPINET = 0;
 &enErrorPINET   = 0;

     &enSwMensajes = 0;
     &enSoCtb    = 0;
     &enSoCtb0   = 0;
     &eaSoCtb1   = "";
     &efSoCtb2   = @;
     &enSoCtbE   = 0;
     &enSoDiario = 0;
     &efSoFecha  = @;

     &eaSoc704   = "";
     &enSoc705   = 0;
     &eaSoc706   = "";
     &enSoc707   = 0;
     &eaSoc708   = "";
     &enSoc709   = 0;
     &eaSoc710   = "";
     &enSoc711   = 0;
     &eaSoc712   = "";
     &eaSoc713   = "";
|FIN;

|INCLUYE dscont_i;

|| *************************************************************************
||                        CALCULOS DE PANTALLA
|| *************************************************************************
|PROCESO Mayor;
     |C_M #0Campo, 0, aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     |SI #0Campo = "N";
         |PARA x = 5;  |SI x [ 24;  |HACIENDO x = x + 1;
               |C_M #0x,1,"S";
         |SIGUE;
         |C_M #0#3,1,"N"; #0#3 = 0; |PINTA #0#3;
         |C_M #0#4,1,"N"; #0#4 = 0; |PINTA #0#4;
     |SINO;
         |PARA x = 5;  |SI x [ 24;  |HACIENDO x = x + 1;
               |C_M #0x,1,"N"; #0x = 0; |PINTA #0x;
         |SIGUE;
         |C_M #0#3,1,"S";
         |C_M #0#4,1,"S";
     |FINSI;
|FPRC;

|CALCULO SelTipo0;
     |C_M #0Campo, 0, aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     |SI #0#38 = "N";
         #0#26 = 0;   |PINTA #0#26;  |C_M #0#26, 1, "N";
         #0#27 = 99;  |PINTA #0#27;  |C_M #0#27, 1, "N";
         |PARA nCampo = 28;  |SI nCampo [ 37;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |C_M #0#26, 1, "S";
         |C_M #0#27, 1, "S";
         |PARA nCampo = 28;  |SI nCampo [ 37;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO SelTipo1;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 37;  |HACIENDO nCampo = nCampo + 1;
               #0Campo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 37;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|PROCESO DesHas;
     |SI #0#4 < #0#3;
         |MENAV "      Error en Introduccion";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO SiModi; |TIPO 7;
     |PARA x = Campo + 1;  |SI x [ 24;  |HACIENDO x = x + 1;
           |C_M #0x,1,"S";
     |SIGUE;
|FPRC;

|PROCESO NoModi;
     |PARA x = Campo + 1;  |SI x [ 24;  |HACIENDO x = x + 1;
           |C_M #0x,1,"N"; #0x = 0; |PINTA #0x;
     |SIGUE;
|FPRC;

|PROCESO EsBuena; || desde campo de empresas seleccion
     |SI FSalida = 2; |ACABA; |FINSI;
     |SI #0Campo = 0; |HAZ NoModi;  |ACABA;  |FINSI;

     |ABRE #30;
     #30#2 = #0Campo;
     #30#3 = 0;
     |LEE 011#30];
     |SI FSalida ! 0;  |O #30#2 ! #0Campo;
         |MENAV "     NO Existe la Empresa Contable";
         |ERROR;
     |FINSI;
     |CIERRA #30;
|FPRC;

|PROCESO elDNI; |TIPO 6;
  |SI FSalida = 2; |ACABA; |FINSI;

  |SI FSalida = 10;
      enCalcCif = 1;
      eaVarDni  = #0Campo;
      |HAZ CalculoDNI;
      #0Campo = eaVarDni;
      |PINTA #0Campo;
      |ERROR;
  |FINSI;

  |SI Campo = 40; |Y #0#39 > #0#40;
      |MENAV "    Error en la Introduccion";
      |ERROR;
  |FINSI;
|FPRC;

|CALCULO Fecha;
   |SI #0#41 > #0#42;
       |MENAV "    Error en la Introduccion";
       |ERROR;
   |FINSI;
|FCAL;

|CALCULO LaFecha; |TIPO 0;
   aAlfa1 = "S";
   |SI #0#44 = "F"; aAlfa1 = "N"; |FINSI;
   |C_M #0#45,1,aAlfa1;
|FCAL;

|CALCULO LafechaP; |TIPO 0;
   |C_M #0Campo,0,aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   |SI aParam = "C";
       |SI #0Campo < fec1; |O #0Campo > fec2;
           |MENAV "    Fecha Erronea";
           |ERROR;
       |FINSI;
   |FINSI;
|FCAL;

|CALCULO cuenta; |TIPO 6;
    |C_M #0Campo,0,aAlfa1;
    |SI aAlfa1 = "N"; |ACABA; |FINSI;

    eaCuenta = #0Campo;
    salidas  = FSalida;
    |HAZ FiltraPuntos;
    #0Campo = eaCuenta;
    |PINTA #0Campo;

    |SI #0Campo = "            ";
        |MENAV "0000Introduzca una cuenta";
        |ERROR;
    |FINSI;
|FCAL;

|CALCULO TrasConcepto;
    |C_M #0Campo,0,aAlfa1;
    |SI aAlfa1 = "N"; |ACABA; |FINSI;

    |SI FSalida = 2; |ACABA; |FINSI;

    |SI Campo = 53;
        enLibIVA   = #0#49;
        enFilConc  = 3;        || Concepto de IRPF
        enConcepto = #0Campo;
        |HAZ FiltroConcepto;  |SI eswLect ! 0; |ACABA; |FINSI;
    |FINSI;
|FCAL;

|CALCULO ElPtec; |TIPO 7;
    |PTEC 816;
|FCAL;

|CALCULO TrasLibro; |TIPO 6;

     |ABRE #206;
     #206#0 = #0Campo;
     |LEE 041#206=;
     |SI FSalida ! 0;
         |MENAV "    No existe Libro";
         |ERROR;
     |SINO;
         |SI #206#2 ! "N";
             |MENAV "    ATENCION!!! Libro de I.V.A. ";
             |ERROR;
         |FINSI;
     |FINSI;
     |CIERRA #206;
|FCAL;

|CALCULO Retencion;
   aAlfa1 = #0Campo;
   |C_M #0#51, 1, aAlfa1;
   |C_M #0#52, 1, aAlfa1;
   |C_M #0#53, 1, aAlfa1;
   |C_M #0#54, 1, aAlfa1;
|FCAL;

|| **********************************************************************
|| **********************************************************************

|PROCESO Contabiliza;
   |HAZ Multi_contador;
   enTipoAsto = 4;
   nDiario    = #0#43;
   nLibro     = #0#49;
   nTipIRPF   = #0#51;
   nDocumento = nDOCUMENTO;

   nDebe     = 0;
   nHaber    = 0;
   swIVA     = 0;

   nApunte   = 1;
   eaCuenta  = #0#46;
   aSigno    = "D";
   nImporte  = nBase;
   nConcepto = #0#47;
   aDescrip  = #0#48;
   aCampo19  = "B";
   nCampo20   = 1;
   |HAZ Linea;

   |SI #0#50 = "S";
       nApunte   = nApunte + 10;
       eaCuenta  = #0#52;
       aSigno    = "H";

       nImporte  = (nBase % #0#51) ! 2;
       nConcepto = #0#53;
       aDescrip  = #0#54;
       aCampo19  = "P";
       nCampo20   = 2;
       |HAZ Linea;
   |FINSI;

   nApunte = nApunte + 10;
   eaCuenta = aCuenta;
   aNOMCUENTA = eaNomSocio;
   aSigno = "H";
   nImporte  = nDebe - nHaber;
   nConcepto = #0#47;
   aDescrip  = #0#48;
   aCampo19  = "F";
   nCampo20  = 0;
   |HAZ Linea;

   nSwConta = 1;
   aNOMCUENTA = "";
|FPRC;

|PROCESO SociosLineas;
   nSw = 1;
   |SI nSwOp = 0;
       |ERROR10;
   |FINSI;

   |SI nSwOp = 1;
       |DDEFECTO #101;
       #101#0 = #102#0;
       #101#1 = #102#1;
       |LEE 041#101=;
       aCuenta = #101#17;
       eaNomSocio = #101#2;
       eaNifSocio = #101#1;
       |SI aCuenta = doce;
           nError = 2; |HAZ GrabaErrores;
           |LIBERA #102;
           |ACABA;
       |FINSI;

       fRegistro = #102#2;
       |SI #0#44 = "F";  fFecConta = #102#2;  |FINSI;
       |SI fFecConta [ fec3;
           nError = 4; |HAZ GrabaErrores;
           |LIBERA #102;
           |ACABA;
       |FINSI;

       nBase = #102#3;
       |HAZ Contabiliza;
       #102#4 = "S";
       #102#5 = nDiario;
       #102#6 = fFecConta;
       #102#7 = nDocumento;
       #102#8 = nPeriodo;
       enSoCtbE = 0;
       |GRABA 020#102a;
   |FINSI;

   |LIBERA #102;
|FPRC;

|DEFBUCLE SociosLineas;
   #102#2;
   4;
   nEmpresa, #0#39, fDFecha, "N";
   nEmpresa, #0#40, fHFecha, "N";
   be;
   NULL, SociosLineas;
|FIN;

|PROCESO PorEmpContable;
   #30#11 = #330#11;
   #30#12 = #330#12;
   #30#26 = #330#26;
   |HAZ CalLaFecha;
   |SI fec2 < #0#41; |ACABA; |FINSI;
   |SI fec1 > #0#42; |ACABA; |FINSI;

   nPeriodo = #330#3;
   nConta = 1;
   |HAZ SituaEmpresa;
   |SI nConta = 1;
       fDFecha = fec1; |SI fDFecha < #0#41; fDFecha = #0#41; |FINSI;
       fHFecha = fec2; |SI fHFecha > #0#42; fHFecha = #0#42; |FINSI;

       nSwOp    = 1;
       nSwConta = 0;
       |ABRE #2;
       |ABRE #3;
       |ABRE #204;
       |ABRE #205;
       |ABRE #101;
       |BUCLE SociosLineas;
       |CIERRA #101;
       |CIERRA #205;
       |CIERRA #204;
       |CIERRA #3;
       |CIERRA #2;
       nAlgun = 1;
   |FINSI;
|FPRC;

|DEFBUCLE PorEmpContables;
   #330#1002;
   ;
   nEmpresa, 0;
   nEmpresa, 9;
   e;
   NULL, PorEmpContable;
|FIN;
|| ///////////////////////////////////////////////////////////
|PROCESO GrabaErrores;
  eaTituloL = "LAS RETRIBUCIONES DE ADMINISTRADORES";
  |SI swImpresora = 0;
      swImpresora = 1;
      |IMPRE 0;
      |SI FSalida ! 0;
          |MENAV "    Error en la Impresora";
          swImpresora = 2;
          |ACABA;
      |FINSI;
      |INFOR informe;
      |SI FSalida ! 0;
          swImpresora = 3;
          |MENAV "     Error en el Informe";
          |FINIMP;
          |ACABA;
      |FINSI;
 |FINSI;
 |SI swImpresora = 1;
     |SI nError = 1;
         men = "No existe Ejercicio " + enEjercicio;
     |FINSI;
     |SI nError = 2;
         men = "Cuenta en Blanco en Retribucion de " + #102#1 + " fecha " + #102#2;
     |FINSI;
     |SI nError = 3;
         men = "Fecha de Bloqueo " + fec3;
     |FINSI;
     |SI nError = 4;
         men = "Fecha de Bloqueo " + fec3 + " y Retribucion de " + #102#1 + " fecha " + #102#2;
     |FINSI;

     |PRINT;
 |FINSI;
|FPRC;

|| ///////////////////////////////////////////////////////////

|PROCESO LaEmpresa;

||    enEmpresaPINET = #agifigen#0;
||    |HAZ RestriccionPINET;
||    |SI enErrorPINET = 1;  |ACABA;  |FINSI;

    nSwTipo = 1;
    nPin1 = #agifigen#87;
    |SI #0#38 = "S";
        |SI nPin1 ] #0#26; |Y nPin1 [ #0#27;
            nSwTipo = 0;
        |FINSI;
    |SINO;
        |SI nPin1 = #0#28;
            nSwTipo = 0;
        |SINO;
            |PARA nCampo = 29;  |SI nCampo [ 37;  |HACIENDO nCampo = nCampo + 1;
                  |SI nPin1 = #0nCampo; |Y nPin1 ! 0;
                      nSwTipo = 0;
                      |SAL;
                  |FINSI;
            |SIGUE;
        |FINSI;
    |FINSI;
    |SI nSwTipo = 1; |ACABA; |FINSI;

    aEstado = "A";
    |SI #100#94 > "01.01.0000"; |Y #100#94 [ #0#39;
        aEstado = "I";
    |FINSI;
    |SI aEstado ! #0#25; |Y #0#25 ! "T";
        |ACABA;
    |FINSI;

|| ===============================================================
    nEmpresa = #100#0;
    fDFecha  = #0#41;
    fHFecha  = #0#42;
    nSw   = 0;
    nSwOp = 0;
    |BUCLE SociosLineas;
    |SI nSw = 1;          || si que ha lineas de socios
        nHay = 1;
        |SI #0#44 = "P";      || Solo un periodo
            fDFecha   = #0#41;
            fHFecha   = #0#42;
            fFecConta = #0#45;
            |HAZ BuscoPeriodo;
            |SI nConta = 0;
                nError = 1; |HAZ GrabaErrores;
                |ACABA;
            |FINSI;
            |SI fFecConta [ fec3;
                nError = 3; |HAZ GrabaErrores;
                |ACABA;
            |FINSI;
            nSwOp    = 1;
            nSwConta = 0;
            |ABRE #2;
            |ABRE #3;
            |ABRE #204;
            |ABRE #205;
            |ABRE #101;
            |BUCLE SociosLineas;
            |CIERRA #101;
            |CIERRA #205;
            |CIERRA #204;
            |CIERRA #3;
            |CIERRA #2;
        |SINO;
            nAlgun = 0;
            |BUCLE PorEmpContables;
            |SI nAlgun = 0;
                nError = 1; |HAZ GrabaErrores;
            |FINSI;
        |FINSI;
    |FINSI;
|FPRC;

|DEFBUCLE agifigen;
     #100#1;
     ;
     nDEmpresa;
     nHEmpresa;
     e;
     NULL, LaEmpresa;
|FIN;

|PROCESO Tipo3; |TIPO 3;

     aTipoIVA    = "N";
     eaModif     = #0#55;
     swImpresora = 0;
     nHay = 0;
     |SI #0#2 = "S";
         nDEmpresa = #0#3;
         nHEmpresa = #0#4;
         |BUCLE agifigen;
     |SINO;
         |PARA nPara1 = 5; |SI nPara1 [ 24; |HACIENDO nPara1 = nPara1 + 1;
               |SI #0nPara1 ! 0;
                   nDEmpresa = #0nPara1;
                   nHEmpresa = #0nPara1;
                   |BUCLE agifigen;
               |FINSI;
         |SIGUE;
     |FINSI;

     |SI nHay = 0;
         |SI enSwMensajes = 0; |MENAV "    Seleccion vacia"; |FINSI;
     |SINO;
         |SI swImpresora = 1;
             |PIEINF;
             |FININF;
             |FINIMP;
         |FINSI;
     |FINSI;
|FPRC;

|PROGRAMA;
     aParam = PARAMETRO;

     |SI aParam ! "basica";
         |HAZ inicio;
     |FINSI;

     |HAZ DirAplicSt;
     CodigoEmpresa  = enEmpresa;
     PeriodoEmpresa = enPeriodo;

     aPath = aPGenral + "fich/";
     |PATH_DAT #102 aPath;

     fFecha   = ~;
     aAlfa1   = fFecha;
     aAlfa1   = aAlfa1 % -104;
     nEjer    = aAlfa1;
     aAlfa1   = "01.01." + nEjer; f1 = aAlfa1;
     aAlfa2   = "31.12." + nEjer; f2 = aAlfa2;

     |DBASE aDirInicio;
     aMas  = aDirInicio + "def/canempre";
     |CARGA_DEF aMas, imagen@;
     |SI FSalida ! 0;
         |MENAV "    Error Cargado Fichero de Contabilidad. Proceso Interrumpido";
         |ACABA;
     |FINSI;

     |ABRE #0;
     #0#0 = 0;
     |LEE 141#0=;
     |SI FSalida ! 0;
         |DDEFECTO #0;
         #0#0 = 0;
         #0#1 = nEjer;
         |SI nEmpresa ! 0;
             #0#3 = nEmpresa;
             #0#4 = nEmpresa;
         |FINSI;
         #0#41 = f1;
         #0#42 = f2;
         |GRABA 020#0b;
     |FINSI;

     |SI aParam ! "basica";
         |CLS;
         |PINPA #0#0;
         |PINDA #0#0;
         |ENDATOS #1#0;
         |SI FSalida = 0;
             |GRABA 020#0a;
         |FINSI;
     |SINO;
         aAlfa1 = efSoFecha; aAlfa1 = aAlfa1 % -104;
         #0#1 = aAlfa1;
         #0#2 = "S";
         #0#3 = enSoCtb0;
         #0#4 = enSoCtb0;
         |PDEFECTO #0, 5, 39;
         #0#39 = eaSoCtb1;
         #0#40 = eaSoCtb1;
         #0#41 = efSoCtb2;
         #0#42 = efSoCtb2;
         #0#43 = enSoDiario;
         #0#44 = "P";
         #0#45 = efSoFecha;
         #0#46 = eaSoc704;
         #0#47 = enSoc705;
         #0#48 = eaSoc706;
         #0#49 = enSoc707;
         #0#50 = eaSoc708;
         #0#51 = enSoc709;
         #0#52 = eaSoc710;
         #0#53 = enSoc711;
         #0#54 = eaSoc712;
         #0#55 = eaSoc713;
         |HAZ Tipo3;
     |FINSI;
     |LIBERA #0;
     |CIERRA #0;

     |DESCARGA_DEF #imagen@;
|FPRO;
