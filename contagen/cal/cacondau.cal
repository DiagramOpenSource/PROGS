|FICHEROS;
   cacondau #0;
   camaasil #1;

   canempre #30;
   caselem0 #997;
   caselem1 #998;
|FIN;

|VARIABLES;
   &entrada = 1;

   aFichero = "";
   nCampo   = 0;
   nPara1   = 0;
   aAlfa1   = "";

   nDDiario = 0;
   nHDiario = 0;
   fDFecha  = @;
   fHFecha  = @;
   aDDepar  = "";
   aHDepar  = "";

   SwError = 0;
   nContador = 0;
   nImporte = 0;
   aSigno = "";

   TotDebe = 0;
   TotHabe = 0;
   HaySaldo = 0;
   Linea = 0;
   p_error = 0;

   aDCuenta = "";
   aHCuenta = "";

   nSw       = 0;
   nEmpMulti = 0;
   &eSwMul   = 0;
   &espe15   = "";
   &espe16   = 0;
   nSwOpera  = 0;
   nPerAct   = 0;
|FIN;

|INCLUYE dscont_i;

|| *********************************************************************
|| ************* Calculos de la Pantalla
|| *********************************************************************
|CALCULO losdefectos; |TIPO 0;
 |SI #0#34 = "N";
     #0#30 = fec1;
     #0#28 = 00;
 |SINO;
     #0#30 = "01.01.2000";
     #0#28 = 01;
 |FINSI;
 |PINTA #0#30;
 #0#31 = fec2; |PINTA #0#31;
 |PINTA #0#28;
|FCAL;

|CALCULO MiraSelec; |TIPO 0;
  |SI #0#0 = "S";
      |C_M #0#1,1,"S";
      |C_M #0#2,1,"S";
      |PARA nCampo = 3; |SI nCampo [ 26; |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo,1,"N";
      |SIGUE;
   |SINO;
      |C_M #0#1,1,"N";
      |C_M #0#2,1,"N";
      |PARA nCampo = 3; |SI nCampo [ 26; |HACIENDO nCampo = nCampo + 1;
            |C_M #0nCampo,1,"S";
      |SIGUE;
   |FINSI;
|FCAL;

|CALCULO NoMasCuentas; |TIPO 0;
   aAlfa1 = #0Campo; |QBF aAlfa1;
   |SI aAlfa1 = "";
      |PARA nCampo = Campo + 1;  |SI nCampo [ 26;  |HACIENDO nCampo = nCampo + 1;
            #0nCampo = "";  |C_M #0nCampo, 1, "N";
      |SIGUE;
   |SINO;
      |PARA nCampo = Campo + 1;  |SI nCampo [ 26;  |HACIENDO nCampo = nCampo + 1;
            |C_M #0nCampo, 1, "S";
      |SIGUE;
  |FINSI;
|FCAL;

|CALCULO BuscaCuenta; |TIPO 6;
     |C_M #0Campo,0,aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;
|FCAL;

|| ************************************************************************
|| ************************************************************************
|PROCESO Actualiza0;
   aAlfa1 = #1#18; |QBF aAlfa1;
   |SI aAlfa1 = ""; |ACABA; |FINSI;

   |SI Haybasica = 0
       |SI #1#21 < #0#32; |O #1#21 > #0#33;
           |ACABA;
       |FINSI;
   |SINO;
       nDepar = #1#21; |SI nDepar = 0;  nDepar = 1;   |FINSI;
       |SI nDepar < #0#32; |O nDepar > #0#33;  ||desde/hasta actividad
           |ACABA;
       |FINSI;
   |FINSI;

   #1#18 = " ";
   |GRABA 020#1a;
   |LIBERA #1;
|FPRC;

|DEFBUCLE Actualiza;
   #1#3;
   0;
   aDCuenta, fDFecha, 000000, 0001, nDDiario;
   aHCuenta, fHFecha, 999999, 9999, nHDiario;
   be;
   NULL, Actualiza0;
|FIN;

|| **********************************************************************
|PROCESO HazTodo;

  |SI #0#0 = "S";
      aDCuenta = #0#1;
      aHCuenta = #0#2;
      |BUCLE Actualiza;
  |SINO;
      |PARA nPara1 = 3; |SI nPara1 [ 26; |HACIENDO nPara1 = nPara1 + 1;
            aAlfa1= #0nPara1; |QBF aAlfa1;
            |SI aAlfa1 ! "            ";
                aDCuenta = #0nPara1;
                aHCuenta = #0nPara1;
                |BUCLE Actualiza;
            |FINSI;
      |SIGUE;
  |FINSI;
|FPRC;
|| **********************************************************************

|CALCULO Conciliar; |TIPO 3;
   |HAZ DirAplicSt;
   |SI Haybasica = 1;
       |SI #0#33 = "zz"; #0#33 = "99"; |FINSI;
   |FINSI;

   nDDiario = #0#28; nHDiario = #0#29; aDDepar = #0#32;
   fDFecha  = #0#30; fHFecha  = #0#31; aHDepar = #0#33;

   |SI #0#34 = "N";
       |SI #48#27 = "S";
           |HAZ MultiEmpresa;
       |SINO;
           |DFICE dirfich0; |QBF dirfich0;
           |HAZ HazTodo;
       |FINSI;
   |SINO;
       eSwMul = 1;
       |SI #48#27 = "S";  eSwMul = 2; |FINSI;
       |HAZ MultiEjercicio;
   |FINSI;
|FCAL;

|| **********************************************************************
|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   |SI #0#30 > #caselem1#5; |O #0#31 < #caselem1#4;
       |ACABA;  || Fuera de la seleccion
   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #1 dirfich0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
   #caselem1#1;
   ;
   000001;
   999999;
   ;
   NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;

|PROCESO MultiEjercicio;
   espe15 = "S";

   |SUB_EJECUTA "caselemp";
   |NOME_DAT #998 eaFichsel;
   |NOME_DAT #997 eaFichres;

   nEmpMulti = -1;
   |BUCLE MultiEmp0;
   |DELINDEX #998;
   |DELINDEX #997;
|FPRC;
