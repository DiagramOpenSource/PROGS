|FICHEROS;
   caybalsy #0;       || Informe comprobacion suma y saldos
   cacuenta #1;       || Plan contable
   caconimp #8;

   canempre #30;
   caselem1 #998;
   cawdatos #900;

   cuentat@ #999;
|FIN;

|VARIABLES;
   &pathbal = "";
   &balance = 1;
   &entrada = 1;
   potra = 0;
   x = 0;
   xy = 0;
   men1 = "     NO EXISTE EMPRESA !";
   informe = "";
   mes = 0;                  || Variable para saber el mes seleccionado
   letrames = "";            || Variable para poner el mes en letra
   varcamp = 0;              || Variable para posicionarse en un campo
   xxxxfiac = "  ";          || Variables para no modificable
   xxaponer = "  ";
   xrelleno = "                              ";
   error1 = "0000Nivel repetido";
   error2 = "0000No existe informe";
   error3 = "0000El numero de nivel no coincide con el numero de nivel de la empresa";
   swprint = 0;       || Variable para saber si imprimimos o no
   swsuma = 0;        || Variable para saber si sumamos o no
   nivel = 0;         || Variable que posee el nivel mas alto
   saldo = 0;         || Resta entre el debe y el haber;
   sw = 0;            || sw para nivel anterior
   nivelact = 0;      || Nivel actual
   rellenos = "";     || Variable para rellenar el hasta de la cuenta en zetas
   &i_debe = 0;       || Total del debe por registro
   &i_haber = 0;      || Total del haber por registro
   &i_acumdebe = 0;   || Total acumulado debe por registro
   &i_acumhaber = 0;  || Total acumulado haber por registro
   &i_deudor = 0;     || Total saldo deudor por registro
   &i_acreedor = 0;   || Total saldo acreedor por registro
   &t_debe = 0;       || Total del debe del informe
   &t_haber = 0;      || Total del haber del informe;
   &t_acumdebe = 0;   || Total del acumulado debe del informe
   &t_acumhaber = 0;  || Total del acumulado haber del informe
   &t_deudor = 0;     || Total saldo deudor por informe
   &t_acreedor = 0;   || Total saldo acreedor por informe
   &i_debeap   = 0;
   &i_haberap  = 0;
   &t_debeap   = 0;
   &t_haberap  = 0;
   &swlinea = 0;      || Pone o no linea en blanco en informe
   &swcomen = 0;
   &i_fecha = @;
   men2  = "     Longitud de Cuenta Fuera de Nivel !";
   long = "  ";
   vermov = 0;
   cta  = 0;
   num   = 0;
   swdig = 0;
   digito = 0;
   menor = 0;
   para1 = 0;
   nume1 = 0;
   sw_pagina = 0;
   &pagina = 0;
   &aparam = "";
   {-1}form  = "";           || Variables menu formato <1> o <2>
   form1 = "2400";
   form2 = "1";
   form3 = "Elija formato: ";
   form4 = "123";
   form5 = " < 1 > ";
   form6 = " < 2 > ";
   form7 = " < 3 > ";
   op_form = 0;
   d_mes_f = 0;
   h_mes_f = 0;

   &nPagDup = 0;
   &nSwPagDup = 0;

   &r_emp    = 0;
   &r_per    = 0;

   aAlfa1    = "";
   aAlfa2    = "";
   nSwLInv   = 0;
   &enNumPag = 0;

   Comodin  = "";
   Comodin1 = "";
   aAlfa    = "";
   Calc     = 0;
   Per1     = 0;
   Per2     = 0;
   &efFecLeg = @;
   &eaDatLeg = "";
   aNomCta   = "";
   nSwOp     = 0;
   nSeguir   = 0;

   nEmpTodo    = 0;
   nPerTodo    = 0;
   &espe17     = "";
   &eSwSuma    = 0;
   dirfichOri  = "";
   nSw         = 0;
   nPara1      = 0;
   aMas        = "";
   aDef        = "";
   aFichero    = "";

   aHora       = "";
   &eaHora     = "";
|FIN;

|INCLUYE dscont_i;
|INCLUYE digito_i;

|PROCESO inicio_y; |TIPO 8;
  |HAZ inicial;
  |SI swerror = 2; |PTEC 807; |ACABA; |FINSI;
  |SI swerror = 3; |PTEC 831; |PTEC 807; |ACABA; |FINSI;
|FPRC;

|PROCESO antesque;
   aNomCta = "cacuenta";
   aparam = PARAMETRO;
   |QBF aparam; |SI aparam = " N"; aparam = ""; |FINSI;
   |SI aparam = "fech";
       aNomCta = "cabalano";
   |FINSI;
   |SI aparam = "Analitica";
       aNomCta = eaUsuAna;
   |FINSI;
   |NOME_DAT #1 aNomCta;
|FPRC;

|PROCESO &c_pagina;
   |SI sw_pagina = 0;
      pagina = #0#22;
      sw_pagina = 1;

      nPagDup = #0#22;
      nSwPagDup = 1;
   |SINO;
      pagina = pagina + 1;

      |HAZ PDuplex;
   |FINSI;
|FPRC;

|CALCULO AnaT13; |TIPO 13;
 aparam = PARAMETRO;
 |SI aparam = "Analitica";
     |ATRI P;
     |PINTA 0572, "ANALITICO";
     |ATRI 0;
     #30#1 = eaNomAna;
 |FINSI;
|FCAL;

|PROCESO DatosEmp; |TIPO 7;
 |ABRE #8;
 |DDEFECTO #8;
 #8#0 = 1;
 |LEE 011#8=;
 |CIERRA #8;
 #0#24  = #8#7; |PINTA #0#24;
 aAlfa1 = #8#8; |C_M #0#24,1,aAlfa1;

 aAlfa1 = "S";
 |SI #8#18 = "N"; |O #8#19 = "N"; aAlfa1 = "N"; |FINSI;
 |C_M #0#25, 1, aAlfa1;
 |C_M #0#26, 1, aAlfa1;
 |SI #8#18 = "N";
     |C_V #0#25, 1, aAlfa1;
     |C_V #0#26, 1, aAlfa1;
     |PINTA 2108, "                          ";
 |SINO;
     |HAZ PonHora;
 |FINSI;
|FPRC;

|PROCESO PonHora; |TIPO 7;
     |C_M #0#25, 0 , aAlfa1;
     |SI aHora = ""; |O aAlfa1 = "N";
         |HORA aHora;
         aAlfa = aHora % 102;  #0#25 = aAlfa;  |PINTA #0#25;
         aAlfa = aHora % 402;  #0#26 = aAlfa;  |PINTA #0#26;
     |FINSI;
|FPRC;

|| ***********************************************************************
||                       CALCULOS DE PANTALLA
|| ***********************************************************************

|PROCESO cuenta; |TIPO 6;
     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;
|FPRC;

|PROCESO cuefin; |TIPO 0;
   eaCuenta = #0Campo;
   |HAZ SacaNivel;
|FPRC;

|PROCESO mesconta; |TIPO 0;
   varcamp = Campo + 1;                || Coge el campo del nombre del mes
   mes = #0Campo;
   |HAZ mesletra;                      || Recoge el mes en letra
   #0varcamp = letrames;
   potra = Campo;
   |PINTA #0varcamp;                   || Pinta el nombre del mes
   Campo = potra;

   |SI Campo = 4;
      |SI #0#2 > #0#4;
         |ERROR;
         |ACABA;     || Si el mes hasta es menor al desde
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO mesletra;
  |SI mes ! 0; |Y mes ! 13;
      mes = #canempre#11 + mes - 1;     || Operacion para seber que mes es
      |SI mes > 12; mes = mes - 12; |FINSI;
   |FINSI;

   |SI mes = 0;  letrames = "Apertura  "; |FINSI;
   |SI mes = 1;  letrames = "Enero     "; |FINSI;
   |SI mes = 2;  letrames = "Febrero   "; |FINSI;
   |SI mes = 3;  letrames = "Marzo     "; |FINSI;
   |SI mes = 4;  letrames = "Abril     "; |FINSI;
   |SI mes = 5;  letrames = "Mayo      "; |FINSI;
   |SI mes = 6;  letrames = "Junio     "; |FINSI;
   |SI mes = 7;  letrames = "Julio     "; |FINSI;
   |SI mes = 8;  letrames = "Agosto    "; |FINSI;
   |SI mes = 9;  letrames = "Septiembre"; |FINSI;
   |SI mes = 10; letrames = "Octubre   "; |FINSI;
   |SI mes = 11; letrames = "Noviembre "; |FINSI;
   |SI mes = 12; letrames = "Diciembre "; |FINSI;
   |SI mes = 13; letrames = "Cierre    "; |FINSI;
|FPRC;

|PROCESO numnivel;|TIPO 0;
   |SI #0#8 > #canempre#13;
      |MENAV error3;
      |ERROR;
      |ACABA;
   |FINSI;
|FPRC;

|PROCESO nivel;|TIPO 7;  || Calculo para hacer no modificable
   |C_M #0Campo, 1, "S";

   |SI Campo = 10; |Y #0#8 < 2; || Si solo hemos seleccionado
      #0Campo = 0;
      |PINTA #0Campo;
      |HAZ nomodifi;              || un solo nivel;
   |FINSI;
   |SI Campo = 11; |Y #0#8 < 3; || Si solo hemos seleccionado
      #0Campo = 0;
      |PINTA #0Campo;
      |HAZ nomodifi;              || dos niveles;
   |FINSI;
   |SI Campo = 12; |Y #0#8 < 4; || Si solo hemos seleccionado
      #0Campo = 0;
      |PINTA #0Campo;
      |HAZ nomodifi;              || tres niveles;
   |FINSI;
   |SI Campo = 13; |Y #0#8 < 5; || Si solo hemos seleccionado
      #0Campo = 0;
      |PINTA #0Campo;
      |HAZ nomodifi;              || cuatro niveles;
   |FINSI;
   |SI Campo = 14; |Y #0#8 < 6; || Si solo hemos seleccionado
      #0Campo = 0;
      |PINTA #0Campo;
      |HAZ nomodifi;              || cinco niveles;
   |FINSI;
|FPRC;

|PROCESO nomodifi;             || RUTINA DE CONVERSION A NO MODIFICABLES
|| VARIABLES: A_
   xxaponer = Anono;
   xxxxfiac = Control % A1001;
   Control = Control + xxxxfiac;
   Control = Control + xrelleno;
   Control = Control % A120;
   Control = Control + xxaponer;
|FPRC;

|PROCESO nivelrep;|TIPO 0;  || Para comprobar que no repetimos los niveles

   |SI nivel = 0;        || Buscamo el nivel mas alto;
      nivel = #0#9;
   |FINSI;

   |SI Campo = 10;
      |SI #0#10 = #0#9;|Y #0#10 ! 0;    || Si hay dos niveles iguales error
         |MENAV error1;
         |ERROR;  |ACABA;
      |SINO;
         |SI #0#10 > #0#9;  || Busca el nivel mas alto
            nivel = #0#10;
         |SINO;
            nivel = #0#9;
         |FINSI;
      |FINSI;
   |FINSI;

   |SI Campo = 11; |Y #0#11 ! 0;
      |SI #0#11 = #0#9; |O #0#11 = #0#10; || Si hay dos niveles iguales error
         |MENAV error1;
         |ERROR;  |ACABA;
      |FINSI;
      |SI #0#11 > nivel;  || Busca el nivel mas alto
         nivel = #0#11;
      |FINSI;
   |FINSI;

   |SI Campo = 12; |Y #0#12 ! 0;      || Si hay dos niveles iguales error
      |SI #0#12 = #0#9; |O #0#12 = #0#10; |O #0#12 = #0#11;
         |MENAV error1;
         |ERROR; |ACABA;
      |FINSI;
      |SI #0#12 > nivel;  || Busca el nivel mas alto
         nivel = #0#12;
      |FINSI;
   |FINSI;

   |SI Campo = 13; |Y #0#13 ! 0;     || Si hay dos niveles iguales error
      |SI #0#13 = #0#9; |O #0#13 = #0#10; |O #0#13 = #0#11; |O #0#13 = #0#12;
         |MENAV error1;
         |ERROR; |ACABA;
      |FINSI;
      |SI #0#13 > nivel;  || Busca el nivel mas alto
         nivel = #0#13;
      |FINSI;
   |FINSI;

   |SI Campo = 14; |Y #0#14 ! 0;    || Si hay dos niveles iguales error
      |SI #0#14 = #0#9; |O #0#14 = #0#10; |O #0#14 = #0#11; |O #0#14 = #0#12; |O #0#14 = #0#13;
         |MENAV error1;
         |ERROR;  |ACABA;
      |FINSI;
      |SI #0#14 > nivel;  || Busca el nivel mas alto
         nivel = #0#14;
      |FINSI;
   |FINSI;
|FPRC;

|| **********************************************************************
||                       CALCULOS DE IMPRESION
|| **********************************************************************
|PROCESO GrabaAuxiliar;

   #999#0 = #1#0;
   |LEE 041#999=;
   |SI FSalida ! 0;
       |DDEFECTO #999;
       #999#0  = #1#0;  #999#1 = #1#1;
       #999#2  = #1#2;  #999#3 = #1#3;
       #999#4  = #1#4;  #999#5 = #1#5;
       #999#6  = #1#6;  #999#7 = #1#7;
       #999#8  = #1#8;
       #999#54 = #1#54; #999#55 = #1#55;
       #999#56 = #1#56; #999#57 = #1#57;
       #999#58 = #1#58; #999#59 = #1#59;
       #999#60 = #1#60; #999#61 = #1#61;
       |GRABA 020#999c;
   |FINSI;

   |PARA nPara1 = 9; |SI nPara1 [ 53; |HACIENDO nPara1 = nPara1 + 1;
         #999nPara1 = #999nPara1 + #1nPara1;
   |SIGUE;
   |GRABA 020#999a;
   |LIBERA #1;
|FPRC;

|PROCESO condicion;    || Calculo condiciones de impresion
   |HAZ digitos2;
   |SI d_error ! 0;  |ACABA;  |FINSI;

   vermov = 0;
   swprint = 0;           || Variable para saber si imprimimos o no
   swsuma = 0;            ||Variable para saber si sumamos o no
   i_debe = 0;            || Pone a cero el total debe del registro
   i_haber = 0;           || Pone a cero el total haber del registro
   i_acumdebe = 0;        || Pone a cero el total acumulado debe del registro
   i_acumhaber = 0;       || Pone a cero el total acumulado haber del registro
   i_debeap = 0;
   i_haberap = 0;

   |SI sw = 0;
       nivelact = #cacuenta#55;  || Nivel actual
       sw = 1;
   |FINSI;

   |SI #cacuenta#55 ! 0;     || Condicion para saber si el nivel esta seleccionado
      |SI #cacuenta#55 = #0#9;|O #cacuenta#55 = #0#10;|O #cacuenta#55 = #0#11; || Compara 3 niveles
         swprint = 1;
      |SINO;
         |SI #cacuenta#55 = #0#12;|O #cacuenta#55 = #0#13;|O #cacuenta#55 = #0#14; || Compara 3 niveles
            swprint = 1;
         |SINO;
            swprint = 0;
         |FINSI;
      |FINSI;
   |FINSI;

   |SI swprint = 1; |Y #cacuenta#55 = nivel;  || Si es el nivel mas alto entoces
      swsuma = 1;                    || se suma.
   |FINSI;

   |SI #cacuenta#3 = "S"; |Y #cacuenta#55 [ nivel;   || Si el nivel no esta seleccionado
      swsuma = 1;                    || pero tiene en el campo pase una S
      swprint = 1;                   || entonces se imprime y se suma.
   |FINSI;

   |SI swprint = 1; |Y nSwOp = 2;
       |HAZ GrabaAuxiliar;
       |ACABA;
   |FINSI;

   |SI swprint = 1;    || Si se puede imprimir
      |SI #0#2 = 0;  varcamp =  9; |FINSI;    || Se situa en el campo 0  debe
      |SI #0#2 = 1;  varcamp = 12; |FINSI;    || Se situa en el campo 1  debe
      |SI #0#2 = 2;  varcamp = 15; |FINSI;    || Se situa en el campo 2  debe
      |SI #0#2 = 3;  varcamp = 18; |FINSI;    || Se situa en el campo 3  debe
      |SI #0#2 = 4;  varcamp = 21; |FINSI;    || Se situa en el campo 4  debe
      |SI #0#2 = 5;  varcamp = 24; |FINSI;    || Se situa en el campo 5  debe
      |SI #0#2 = 6;  varcamp = 27; |FINSI;    || Se situa en el campo 6  debe
      |SI #0#2 = 7;  varcamp = 30; |FINSI;    || Se situa en el campo 7  debe
      |SI #0#2 = 8;  varcamp = 33; |FINSI;    || Se situa en el campo 8  debe
      |SI #0#2 = 9;  varcamp = 36; |FINSI;    || Se situa en el campo 9  debe
      |SI #0#2 = 10; varcamp = 39; |FINSI;    || Se situa en el campo 10 debe
      |SI #0#2 = 11; varcamp = 42; |FINSI;    || Se situa en el campo 11 debe
      |SI #0#2 = 12; varcamp = 45; |FINSI;    || Se situa en el campo 12 debe
      |SI #0#2 = 13; varcamp = 48; |FINSI;    || Se situa en el campo 13 debe


      || ************ Bucle del debe y haber periodo *************

      |PARA x = #0#2; |SI x [ #0#4; |HACIENDO x = x + 1;
            i_debe = i_debe + #cacuenta#varcamp#;    || Suma el debe
            |SI #cacuenta#varcamp# ! 0; vermov = 1; |FINSI;
            varcamp = varcamp + 1;          || Se situa en  el campo del haber
            i_haber = i_haber + #cacuenta#varcamp#;  || Suma el haber
            |SI #cacuenta#varcamp# ! 0; vermov = 1; |FINSI;
            varcamp = varcamp + 2;          || Se situa en el campo debe
      |SIGUE;

      |SI op_form = 3;
          |SI #0#2 = 0;
              i_debe  = i_debe - #cacuenta#9;
              i_haber = i_haber - #cacuenta#10;
          |FINSI;
          i_debeap  = #cacuenta#9;
          i_haberap = #cacuenta#10;
      |FINSI;
      i_debe    = i_debe    ! EURODB;
      i_haber   = i_haber   ! EURODB;
      i_debeap  = i_debeap  ! EURODB;
      i_haberap = i_haberap ! EURODB;

      || ************ Bucle del debe y haber acumulado *************

      varcamp = 9;
      |PARA x = 0; |SI x [ #0#4; |HACIENDO x = x + 1;  || Bucle de campos
         i_acumdebe = i_acumdebe + #cacuenta#varcamp#;   || Suma el acumulado debe
         varcamp = varcamp + 1;                 || Se situa en  el campo del haber
         i_acumhaber = i_acumhaber + #cacuenta#varcamp#; || Suma el acumulado haber
         varcamp = varcamp + 2;          || Se situa en el campo debe
      |SIGUE;
      i_acumdebe  = i_acumdebe  ! EURODB;
      i_acumhaber = i_acumhaber ! EURODB;

      saldo = (i_acumdebe - i_acumhaber) ! EURODB;    || Coge la diferencia
      |SI saldo > 0; i_deudor = saldo; i_acreedor = 0; |FINSI;
      |SI saldo < 0; i_deudor = 0; i_acreedor = saldo * (-1); |FINSI;
      |SI saldo = 0; i_deudor = 0; i_acreedor = 0; |FINSI;

      |SI i_acumdebe = 0; |Y i_acumhaber = 0; |Y vermov = 0; |Y #0#6 = "N";
          |ACABA;
      |FINSI;

      |SI nivelact ! #cacuenta#55;    || Si el nivel actual es distinto al
         swlinea = 1;         || anterior pone una linea en blanco
         nivelact = #cacuenta#55;    || Actualiza el nivel
      |SINO;
         swlinea = 0;          || Si no no pone linea en blanco
      |FINSI;

      |SI nSwOp = 0; |PRINT; |FINSI;
      swcomen = 1;        || Para imprimir el saldo anterior
      |SI swsuma = 1;    || Si se puede sumar los totales
         t_debe      = (t_debe      + i_debe) ! EURODB;       || Suma el total del debe
         t_haber     = (t_haber     + i_haber) ! EURODB;      || Suma el total del haber
         t_acumdebe  = (t_acumdebe  + i_acumdebe) ! EURODB;   || Suma el total acumulado debe
         t_acumhaber = (t_acumhaber + i_acumhaber) ! EURODB;  || Suma el total acumulado haber
         t_deudor    = (t_deudor    + i_deudor) ! EURODB;     || Suma total saldo deudor
         t_acreedor  = (t_acreedor  + i_acreedor) ! EURODB;   || Suma total saldo acreedor
         t_debeap    = (t_debeap    + i_debeap)  ! EURODB;
         t_haberap   = (t_haberap   + i_haberap) ! EURODB;
      |FINSI;

   |FINSI;
|FPRC;

|DEFBUCLE caybalsy0;     || Bucle del plan contable
   #1#3;                 || Lee por la tercera clave
   ;
   #0#0,     1;
   rellenos, 6;
   e;
   NULL, condicion;
|FIN;

|DEFBUCLE caybalsy1;     || Bucle del plan contable
   #1#1;                 || Lee por la primera clave
   ;
   #0#0;
   #0#1;
   e;
   NULL,condicion;
|FIN;

|PROCESO Chequeo;
   i_debe = 0;            || Pone a cero el total debe del registro
   i_haber = 0;           || Pone a cero el total haber del registro

   varcamp = 9 + (#0#2 * 3);
   |PARA x = #0#2; |SI x [ #0#4; |HACIENDO x = x + 1;
         i_debe = i_debe + #cacuenta#varcamp#;    || Suma el debe
         varcamp = varcamp + 1;          || Se situa en  el campo del haber
         i_haber = i_haber + #cacuenta#varcamp#;  || Suma el haber
         varcamp = varcamp + 2;          || Se situa en el campo debe
   |SIGUE;
   i_debe    =  i_debe  ! EURODB;
   i_haber   =  i_haber ! EURODB;

   t_debe     = (t_debe + i_debe) ! EURODB;            || debe
   t_haber    = (t_haber + i_haber) ! EURODB;          || haber
|FPRC;

|DEFBUCLE caybalsyC;     || Chequeo Cuadre
   #1#1;
   3;
   "            ", "S";
   "zzzzzzzzzzzz", "S";
   e;
   NULL,Chequeo;
|FIN;

|PROCESO rellena;
   rellenos = "zzzzzzzzzzzz";
   menor = -1;

   |PARA para1 = 9; |SI para1 [ 14; |HACIENDO para1 = para1 + 1;
      |SI #0para1 ! 0;
         |SI menor = -1;  menor = #0para1;  |FINSI;
         |SI menor > #0para1;  menor = #0para1;  |FINSI;
      |FINSI;                   || saca el nivel elegido mas peque¤o
   |SIGUE;

   |SI menor ! 0;
      |SI menor = 1;  nume1 = dig4;  |FINSI;
      |SI menor = 2;  nume1 = dig5;  |FINSI;   || construye el hasta segun
      |SI menor = 3;  nume1 = dig6;  |FINSI;   ||/el nivel mas bajo elegido
      |SI menor = 4;  nume1 = dig7;  |FINSI;
      |SI menor = 5;  nume1 = dig8;  |FINSI;
      |SI menor = 6;  nume1 = dig9;  |FINSI;
      nume1 = 100 + nume1;
      rellenos = #0#1 % nume1;
      rellenos = rellenos + "zzzzzzzzzzzz";
      rellenos = rellenos % 112;
   |FINSI;
|FPRC;

|PROCESO impre;       || Impresion por impresora;

   || .... Aqu¡ seleccion de Balances por mes
      d_mes_f = #0#2;
      h_mes_f = #0#4;

     |PARA xy = d_mes_f; |SI xy [ h_mes_f; |HACIENDO xy = xy + 1;

         |SI #0#23 = "S";
             #0#2 = xy; mes = #0#2; |HAZ mesletra; #0#3 = letrames;
             #0#4 = xy; mes = #0#4; |HAZ mesletra; #0#5 = letrames;

             |SI enMAper ! dig1;
                 |SI #0#4 < enMAper; |Y #0#4 ! 0;
                     |VETE EsteMesNo;
                 |FINSI;
            |FINSI;
            |SI #0#24 = "S";
                 enM1 = #0#2;
                 enM2 = #0#4;
                 |HAZ LaFechaEmp;
             |FINSI;
         |FINSI;

         |INFOR informe;                || Abrimos informe
         |SI FSalida = 0;
            |SI #0#16 = "D";
               |BUCLE caybalsy0;
            |SINO;
               |BUCLE caybalsy1;
            |FINSI;
            |PIEINF;
            |FININF;
         |SINO;
            |MENAV error2;
         |FINSI;

         |ET EsteMesNo;
         |SI #0#23 = "N"; |SAL; |FINSI;
         |HAZ borrar;
      |SIGUE;

      |SI #0#23 = "S";
          #0#2 = d_mes_f; mes = #0#2; |HAZ mesletra; #0#3 = letrames;
          #0#4 = h_mes_f; mes = #0#4; |HAZ mesletra; #0#5 = letrames;
      |FINSI;

      || _______________________________________________________/
|FPRC;

|PROCESO borrar;
   sw = 0;
   swcomen = 0;
   i_debe = 0;
   i_haber = 0;
   i_acumdebe = 0;
   i_acumhaber = 0;
   i_deudor = 0;
   i_acreedor = 0;
   t_debe = 0;
   t_haber = 0;
   t_acumdebe = 0;
   t_acumhaber = 0;
   t_deudor = 0;
   t_acreedor = 0;
   t_debe = 0;
   t_haber = 0;
   t_deudor = 0;
   t_acreedor = 0;
   i_debeap   = 0;
   i_haberap  = 0;
   t_debeap   = 0;
   t_haberap  = 0;
|FPRC;

|| *********************************************************************
||                             COMIENZO
|| *********************************************************************

|PROCESO HazTodo;

   |CIERRAT #1;
   |HAZ rellena;
   |NOME_DAT #1 aNomCta;

   |SI aparam ! "consol";
       eaIa = #30#21;
       enM1 = #0#2;
       enM2 = #0#4;
       |HAZ LeeDatosEmpresa;
       |SI aparam = "fech";
           |SI enM1 ! enM1f;
               mes = enM1f; |HAZ mesletra; #0#3 = letrames;
           |FINSI;
           |SI enM2 ! enM2f;
                mes = enM2f; |HAZ mesletra; #0#5 = letrames;
           |FINSI;
       |FINSI;
   |FINSI;

   |SI enMAper ! dig1;
       |SI #0#4 < enMAper; |Y #0#4 ! 0;
           |ACABA;
       |FINSI;
   |FINSI;

   enSwOper = 1;     || 3§ clave
   r_emp = empre;
   r_per = ejerc;
   |SUB_EJECUTA "carecsig";
   enSwOper = 0;

   |ABRE #8;
   |DDEFECTO #8;
   #8#0 = 1;
   |LEE 000#8=;
   |CIERRA #8;
   |SI #caconimp#2 = "S";  |Y #caconimp#3 = "N";  |Y aparam = "";
       pathbal = dirfich0;
       balance = 1;
       |SI #caconimp#10 = "S";
           enSwOper = 2;
           r_emp = empre;
           r_per = ejerc;
           |SUB_EJECUTA "carecsig";
           enSwOper = 0;
           |SI enECuadre = 1; |ACABA; |FINSI;
           |NOME_DAT #1 eaNomFCta;
       |SINO;
           |SUB_EJECUTA "careccue";
       |FINSI;
   |FINSI;

   sw_pagina = 0;
   i_fecha   = #0#15;

   nSwOp   = 0;
   nSeguir = 0;
   |SI #8#14 = "S"; |Y aparam = "";
       nSwOp = 1;
       |BUCLE caybalsyC;
       |SI t_debe ! t_haber;
           nSeguir = 1;
           aAlfa1 = #30#2; aAlfa1 = ("00000"+ aAlfa1) % -105;
           aAlfa2 = #30#3; aAlfa2 = ("0"    + aAlfa2) % -101;
           |SI #8#15 = "N";
               aAlfa1 = "    ATENCION.   BALANCE DESCUADRADO. EMPRESA: "+ aAlfa1 + " PERIODO: " + aAlfa2;
               |MENAV aAlfa1;
           |SINO;
               aAlfa1 = "    SATENCION.   BALANCE DESCUADRADO. EMPRESA: "+ aAlfa1 + " PERIODO: " + aAlfa2 + ". Continuar";
               |CONFI aAlfa1;
               |SI FSalida = 0; nSeguir = 0; |FINSI;
           |FINSI;
       |FINSI;
       |HAZ borrar;
       nSwOp = 0;
   |FINSI;
   |SI nSeguir = 0;
       |HAZ impre;
       |HAZ borrar;
   |FINSI;
|FPRC;

|PROCESO HazConsol;

   |CIERRAT #1;
   |HAZ rellena;

   eaIa = #30#21;
   enM1 = #0#2;
   enM2 = #0#4;
   |HAZ LeeDatosEmpresa;

   |SI enMAper ! dig1;
       |SI #0#4 < enMAper; |Y #0#4 ! 0;
           |ACABA;
       |FINSI;
   |FINSI;

   nSw = 1;
   enSwOper = 1;     || 3§ clave
   r_emp = empre;
   r_per = ejerc;
   |SUB_EJECUTA "carecsig";
   enSwOper = 0;

   nSwOp = 2;
   |ABRE #999;
   |BUCLE caybalsy1;
   |CIERRA #999;
   nSwOp = 0;
|FPRC;

|CALCULO Tipo3; |TIPO 3;

   eaHora = (("00" + #0#25) % -102) + ":" + (("00" + #0#26) % -102):
   eaHora = "Hora: " + eaHora;
   |SI #8#18 = "N"; eaHora = ""; |FINSI;

   |HAZ antesque;

   |MENU form;
   op_form = FSalida;
   |SI op_form < 1; |ACABA; |FINSI;

   |HAZ los_informes;
   |SI op_form = 3; |Y #0#2 > 1; op_form = 1; |FINSI;

   |SI op_form = 1; informe = que_i1; |FINSI;
   |SI op_form = 2; informe = que_i2; |FINSI;
   |SI op_form = 3;
       informe = que_i1 % 103; |SI informe = "cly"; informe = "cry"; |FINSI;
       informe = informe + "bals3";
   |FINSI;
   enDatos = 0;
   |SI #0#24 = "S";
       enDatos = 1;
       aAlfa1 = informe % 106;
       aAlfa2 = informe % -101;
       |SI op_form ! 3; informe = aAlfa1 + "e" + aAlfa2; |FINSI;
   |FINSI;
   aAlfa1 = informe % 102;
   |SI aAlfa1 = "cl"; eaHora = eaHora > " "; |FINSI;

   |IMPRE 0;                          || Abrimos impresora
   |SI FSalida ! 0; |ACABA; |FINSI;

  |DFICE dirfichOri;
   nSw = 0;
   nEmpTodo = enEmpresa;
   nPerTodo = enPeriodo;

   |SI #48#27 = "S"; |Y aparam ! "Analitica";
       |HAZ MultiEmpresa;
   |SINO;
       |DFICE dirfich0;
       |HAZ HazTodo;
   |FINSI;

   |FINIMP;
|FCAL;

|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   nYaLoHeDicho = 0;
   |PATH_DAT #1 dirfich0;
   |PATH_DAT #8 dirfich0;

   |SI espe17 ! "S";
       |HAZ HazTodo;
   |SINO;
       |HAZ HazConsol;
   |FINSI;
|FPRC;

|DEFBUCLE MultiEmp0;
  #caselem1#1;
  ;
  000001;
  999999;
  ;
  NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  eSwSuma = 0; espe17  = "";
  |SI #8#17 = "S"; |Y aparam = ""; eSwSuma = 1; |FINSI;

  |SUB_EJECUTA "caselemp";

  |SI espe17 = "S";

      |DBASE aMas;
      aDef  = aMas + "def/cacuenta";
      |CARGA_DEF aDef, cuentat@;
      |SI FSalida ! 0;
          |MENAV "    Error Cargado Fichero de Cuentas. Proceso Interrumpido";
          |ACABA;
      |FINSI;
      aFichero = "0y" + Usuario;
      |NOME_DAT #999 aFichero;
      |DELINDEX #999;
  |FINSI;

  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;

  |SI espe17 = "S"; |Y  nSw ! 0;
      cod1 = nEmpTodo;
      cod2 = nPerTodo;
      |HAZ leelo;

      dirfich0 = dirfichOri;
      eaIa = #30#21;
      enM1 = #0#2;
      enM2 = #0#4;
      |HAZ LeeDatosEmpresa;
      aAlfa1 = #cawdatos#2; |QBF aAlfa1;
      aAlfa1 = aAlfa1 + " (Consolidada)";
      #cawdatos#2 = aAlfa1;
      aparam = "consol";

      aNomCta   = aFichero;
      eaNomFCta = aFichero;
      |PATH_DAT #1 dirfich0;
      |PATH_DAT #8 dirfich0;

      |HAZ borrar;
      |HAZ HazTodo;
  |FINSI;
|FPRC;


|PROGRAMA;
   aparam = PARAMETRO; |QBF aparam;
   aAlfa1 = aparam % -101;
   |SI aAlfa1 ! "L";
        nSwLInv = 0;
        |MANTE #0;
   |SINO;
        nSwLInv = 1;
        |HAZ Legalia;
   |FINSI;
|FPRO;

|PROCESO Legalia;
       aparam = aparam - ",L";

       Comodin = aparam;
       aAlfa = Comodin % -102; Comodin = Comodin - aAlfa;

       |DDEFECTO #0;
       #0#6  = "N";|SI aAlfa = ",S";  #0#6 = "S"; |FINSI;

       Comodin = Comodin - ",";
       |SI FSalida ! 0;
          Calc = ((FSalida / 100) ! 0) + 99;
          Comodin1 = Comodin % 0101;
          Per1 = Comodin1; Comodin = Comodin - Comodin1;
          Per2 = Comodin;
       |FINSI;

       ||... Legalia
       cod1 = eaLegEmp;
       cod2 = eaLegPer;
       emEmp = 1;  || Sin mensaje
       |HAZ leelo;
       |SI swerror ! 0;  |ACABA;   |FINSI;

       |DBASE fich_alta; |QBF fich_alta;
       fich_alta = fich_alta + "fich/" + eaLegEmp + eaLegPer + "/";
       dirfich0 = fich_alta;

       |PATH_DAT #1 dirfich0;
       |PATH_DAT #8 dirfich0;
       |ABRE #8;
       |DDEFECTO #8;
       #8#0 = 1;
       |LEE 000#8=;
       |CIERRA #8;

       #0#0  = "            "; #0#20 = 1;
       #0#1  = "zzzzzzzzzzzz"; #0#21 = dig3;

       |SI Per1 ] 0; |O Per2 ] 0;
           #0#2  = Per1;       #0#4  = Per2;
       |SINO;
           #0#2  = #30#11;     #0#4  = #30#12;
       |FINSI;

       #0#8 = 1;
       |SI dig4 [ 4; |Y dig4 ! 0; #0#9   = 1; |FINSI;
       |SI dig5 [ 4; |Y dig5 ! 0; #0#9   = 2; |FINSI;
       |SI dig6 [ 4; |Y dig6 ! 0; #0#9   = 3; |FINSI;
       |SI dig7 [ 4; |Y dig7 ! 0; #0#9   = 4; |FINSI;
       |SI dig8 [ 4; |Y dig8 ! 0; #0#9   = 5; |FINSI;
       |SI dig9 [ 4; |Y dig9 ! 0; #0#9   = 6; |FINSI;
       nivel = #0#9;

       #0#22 = enNumPag + 1;
       #0#23 = "S";
       #0#24 = eaDatLeg;
       #0#15 = efFecLeg;
       |SI #0#24 = "S";
           eaIa = #30#21;
           enM1 = #0#2;
           enM2 = #0#4;
           |HAZ LeeDatosEmpresa;
       |FINSI;

       |HAZ los_informes;
       informe = que_i1;
       |SI #0#24 = "S";
           aAlfa1 = informe % 106;
           aAlfa2 = informe % -101;
           informe = aAlfa1 + "e" + aAlfa2;
       |FINSI;

       sw_pagina = 0;
       |HAZ rellena;

       enSwOper = 1;
       r_emp = empre;
       r_per = ejerc;
       |SUB_EJECUTA "carecsig";
       enSwOper = 0;

       sw_pagina = 0;
       i_fecha = #0#15;
       |HAZ impre;
       enNumPag = nPagDup;
       |HAZ borrar;
|FPRC;
