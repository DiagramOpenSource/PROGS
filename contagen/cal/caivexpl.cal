|FICHEROS;
   caivexpl #0;
   cacuenta #1;         || cuentas
   caparexp #2;         || partidas de explotacion
   cabaexp2 #5;         || temporal situacion
   caexistc #6;         || descarga cabs.
   caexistl #7;         || descarga lins.
   caconimp #8;
   canempre #30;
|FIN;

|VARIABLES;
   &pathbal = "";
   &balance = 1;
   potra = 0;
   varcamp = 0;
   mes = 0;
   alfa1 = "";
   alfa2 = "";
   alfa3 = "";
   traraf = 0;
   &sw_inf = 0;
   &cantidad = 0;
   &cantidad1 = 0;
   &variable = "";
   &canti = 0;
   &canti1 = 0;
   &doh = "";
   &anu = 0;
   &anu1 = 0;
   &raya1 = "";
   &raya11 = "";
   ray1 = "            -------------------------------------------------------------------"
   ray2 = "                                              ---------------------------------"
   ray3 = "                                              ================================="
   x = 0;
   nivelet = 0;
   informe = "";
   sw = 0;
   eldesde = 0;
   elhasta = 0;
   acdesde = 0;
   achasta = 0;
   acumula = 0;
   dir_ficha = "";
   dir_fichb = "";
   saldo1 = 0;
   saldo2 = 0;
   saldo3 = 0;
   saldo4 = 0;
   saldo11 = 0;
   saldo12 = 0;
   saldo13 = 0;
   saldo14 = 0;
   importe = 0;
   descar = 0;
   ult_cuent = "";
   ult_nivel = 0;
   nue_cuent = "";
   nue_nivel = 0;
   act_cuent = "";
   act_nivel = 0;
   carac = 0;
   guarda_can = 0;
   guarda_can1 = 0;
   suma1 = 0;
   suma2 = 0;
   suma3 = 0;
   suma4 = 0;
   suma5 = 0;
   suma6 = 0;
   suma11 = 0;
   suma12 = 0;
   suma13 = 0;
   suma14 = 0;
   suma15 = 0;
   suma16 = 0;
   llegoa = 0;
   nume1 = 0;
   tipo = "";
   &ant_agru = "";
   &ant_epig = "";
   &ant_impo = 0;
   &ant_impo1 = 0;
   t_agru = 0;
   t_agru1 = 0;
   t_epig = 0;
   t_epig1 = 0;
   grupo = 0;
   gru_nou = 0;
   agrupaci = 0;
   epigrafe = 0;
   partida = 0;
   antes = "";
   &r_emp = 0;
   &r_per = 0;
   &d_mes = 0;
   &h_mes = 0;
   &estru = 0;
   &r_eje = 0;
   &r_nom = "";
   mesini = 0;
   &aparam = "";
   aNomCta = "";
|FIN;

|INCLUYE dscont_i;
|INCLUYE digito_i;

|PROCESO antesque;
   aNomCta = "cacuenta";
   aparam = PARAMETRO;
   |QBF aparam; |SI aparam = " N"; aparam = ""; |FINSI;
   |SI aparam = "fech";
       aNomCta = "cabalano";
   |FINSI;
   |SI aparam = "Analitica";
       aNomCta = eaUsuAna;
   |FINSI;
   |NOME_DAT #1 aNomCta;
|FPRC;

|CALCULO inicio_b; |TIPO 8;
  |HAZ inicial;
  |SI swerror = 2; |PTEC 807; |ACABA; |FINSI;
  |SI swerror = 3; |PTEC 831; |PTEC 807; |ACABA; |FINSI;
  |HAZ DirAplicSt;
|FCAL;

|CALCULO estruc7; |TIPO 7;
 #0#8 = 1;
 |SI Haybasica = 1; |Y eaDepar = "S";
     #0#8 = 0;
 |FINSI;
 |PINTA #0#8;
|FCAL

____________________________________/ CALCULOS DE PANTALLA
|CALCULO AnaT13; |TIPO 13;
 aparam = PARAMETRO; |SI aparam = " N"; aparam = ""; |FINSI;
 |SI aparam = "Analitica";
     |ATRI P;
     |PINTA 0572, "ANALITICO";
     |ATRI 0;
     #30#1 = eaNomAna;
 |FINSI;
|FCAL;

|PROCESO pintames; |TIPO 0;
   |SI #caivexpl#Campo# ! 0; |Y #caivexpl#Campo# ! 13;
       NumMes = #canempre#11 + (#caivexpl#Campo# - 1);   || Operacion para saber que mes es
       |SI NumMes > 12; NumMes = NumMes - 12; |FINSI;
   |SINO;
       NumMes = #caivexpl#Campo#;
   |FINSI;
   |HAZ NombreMes;                          || Recoge el mes en letra
   |SI Campo > 5;
      |ATRI I;
      |SI Campo =  9;|PINTA 1523, NombreMes; |FINSI;
      |SI Campo = 10;|PINTA 1623, NombreMes; |FINSI;
      |SI Campo = 15;|PINTA 1547, NombreMes; |FINSI;
      |SI Campo = 16;|PINTA 1647, NombreMes; |FINSI;
      |ATRI 0;
   |SINO;
      varcamp = Campo + 2;
      #caivexpl#varcamp# = NombreMes;
      |PINTA #caivexpl#varcamp#;                       || Pinta el nombre del mes
   |FINSI;

   |SI Campo = 0; |O Campo = 4; |O Campo = 9; |O Campo = 15;
      |ACABA;
   |SINO;
      varcamp = Campo - 1;
      |SI #caivexpl#varcamp# > #caivexpl#Campo#;
         |MENAV "    Error de Entrada ...";
         |ERROR;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO estruct; |TIPO 0;
   |ABRE #6;
   #caexistc#0 = #caivexpl#8;
   |LEE 000#6=;
   |SI FSalida ! 0;
      |MENAV "    No existe la estructura de descarga ...";
      |ERROR;
   |FINSI;
   |CIERRA #6;
|FPRC;

|PROCESO numnivel; |TIPO 0;
   |SI #caivexpl#Campo# > #canempre#13;
      |MENAV "    El numero de niveles no coincide con el definido en la Empresa ...";
      |ERROR;
   |FINSI;
   nivelet = 0;
   |CAMPO_MODIFICA #caivexpl#19,1,"S";
   |CAMPO_MODIFICA #caivexpl#20,1,"S";
   |CAMPO_MODIFICA #caivexpl#21,1,"S";
   |CAMPO_MODIFICA #caivexpl#22,1,"S";
   |CAMPO_MODIFICA #caivexpl#23,1,"S";
|FPRC;

|PROCESO nivelet; |TIPO 7;
   |SI #caivexpl#17 < 6; #caivexpl#23 = 0; |PINTA #caivexpl#23; |CAMPO_MODIFICA #caivexpl#23,1,"N"; |FINSI;
   |SI #caivexpl#17 < 5; #caivexpl#22 = 0; |PINTA #caivexpl#22; |CAMPO_MODIFICA #caivexpl#22,1,"N"; |FINSI;
   |SI #caivexpl#17 < 4; #caivexpl#21 = 0; |PINTA #caivexpl#21; |CAMPO_MODIFICA #caivexpl#21,1,"N"; |FINSI;
   |SI #caivexpl#17 < 3; #caivexpl#20 = 0; |PINTA #caivexpl#20; |CAMPO_MODIFICA #caivexpl#20,1,"N"; |FINSI;
   |SI #caivexpl#17 < 2; #caivexpl#19 = 0; |PINTA #caivexpl#19; |CAMPO_MODIFICA #caivexpl#19,1,"N"; |FINSI;
|FPRC;

||
|PROCESO sacacambio;
   alfa1 = alfa1 + "            ";
   #cacuenta#0 = alfa1 % 112;
   #cacuenta#1 = x;
   |LEE 000#1=;
   |SI FSalida ! 0;
      #cacuenta#0 = alfa1 % 112;
      #cacuenta#2 = "NO EXISTE CUENTA EN MAESTRO";
   |FINSI;
   guarda_can = cantidad;
   guarda_can1 = cantidad1;
   |SI x = 1; cantidad = suma1; cantidad1 = suma11; suma1 = 0; suma11 = 0; |FINSI;
   |SI x = 2; cantidad = suma2; cantidad1 = suma12; suma2 = 0; suma12 = 0; |FINSI;
   |SI x = 3; cantidad = suma3; cantidad1 = suma13; suma3 = 0; suma13 = 0; |FINSI;
   |SI x = 4; cantidad = suma4; cantidad1 = suma14; suma4 = 0; suma14 = 0; |FINSI;
   |SI x = 5; cantidad = suma5; cantidad1 = suma15; suma5 = 0; suma15 = 0; |FINSI;
   |SI x = 6; cantidad = suma6; cantidad1 = suma16; suma6 = 0; suma16 = 0; |FINSI;
   sw_inf = 6;
   |PRINT;
   cantidad = guarda_can;
   cantidad1 = guarda_can1;
   llegoa = x;
|FPRC;


|PROCESO haycambio;
   x = act_nivel - 1;
   |ET repetimos;
   |SI x = #caivexpl#18;  |O x = #caivexpl#19;  |O x = #caivexpl#20;  |O x = #caivexpl#21;
      |O x = #caivexpl#22;  |O x = #caivexpl#23;
      carac = 0;
      |SI x = 6;   carac = dig9;  |FINSI;
      |SI x = 5;   carac = dig8;  |FINSI;
      |SI x = 4;   carac = dig7;  |FINSI;
      |SI x = 3;   carac = dig6;  |FINSI;
      |SI x = 2;   carac = dig5;  |FINSI;
      |SI x = 1;   carac = dig4;  |FINSI;
      |SI carac ! 0;
         nume1 = carac + 100;
         alfa1 = act_cuent % nume1;
         alfa2 = nue_cuent % nume1;
         |SI alfa1 ! alfa2;
            |HAZ sacacambio;
         |FINSI;
      |FINSI;
   |FINSI;
   x = x - 1;
   |SI x > 0;  |VETE repetimos;  |FINSI;
|FPRC;

|PROCESO cambio1;
   x = llegoa - 1;
   |SI x [ 0;  |ACABA;  |FINSI;
   |ET repeti1;
   |SI x = #caivexpl#18;  |O x = #caivexpl#19;  |O x = #caivexpl#20;  |O x = #caivexpl#21;
      |O x = #caivexpl#22;  |O x = #caivexpl#23;
      carac = 0;
      |SI x = 6;   carac = dig9;  |FINSI;
      |SI x = 5;   carac = dig8;  |FINSI;
      |SI x = 4;   carac = dig7;  |FINSI;
      |SI x = 3;   carac = dig6;  |FINSI;
      |SI x = 2;   carac = dig5;  |FINSI;
      |SI x = 1;   carac = dig4;  |FINSI;
      |SI carac ! 0;
         nume1 = carac + 100;
         alfa1 = act_cuent % nume1;
         |HAZ sacacambio;
      |FINSI;
   |FINSI;
   x = x - 1;
   |SI x ! 0;  |VETE repeti1;  |FINSI;
   llegoa = nivelet;
|FPRC;

|PROCESO rup_tipo;
   |SI #cabaexp2#0 = "H";
      doh = " H A B E R ";
   |FINSI;
   |PIEINF;
   tipo = #cabaexp2#0;
|FPRC;

|PROCESO rup_agru;
   |SI ant_agru ! "";
      ant_impo = t_agru;
      ant_impo1  = t_agru1;
      raya1 = ray1;
      sw_inf = 10; |PRINT;  || raya
      sw_inf = 7;  |PRINT;  || agrupacion (con importe)
      sw_inf = 5;  |PRINT;  || linea blanco
   |FINSI;
   agrupaci = #cabaexp2#2;

   t_agru = 0;
   t_agru1 = 0;
|FPRC;

|PROCESO rup_epig;
   |SI ult_nivel ! 1;
      nue_cuent = "";
      nue_nivel = 0;
   |FINSI;

   |SI ant_epig ! "";
      ant_impo = t_epig;
      ant_impo1 = t_epig1;
      raya1 = ray2;
      sw_inf = 10; |PRINT;  || raya
      sw_inf = 2;  |PRINT;  || epigrafe
      sw_inf = 5;  |PRINT;  || linea blanco
   |FINSI;
   epigrafe = #cabaexp2#3;
   partida = #cabaexp2#4;

   t_epig = 0;
   t_epig1 = 0;
   ult_cuent = "";
   ult_nivel = 0;
|FPRC;

|PROCESO imprires;
   |SI tipo = "D";
      |SI ant_impo [ 0;
         ant_impo = -ant_impo;
      |SINO;
         ant_impo = 0;
      |FINSI;
      |SI ant_impo1 [ 0;
         ant_impo1 = -ant_impo1;
      |SINO;
         ant_impo1 = 0;
      |FINSI;
   |SINO;
      |SI ant_impo [ 0;
         ant_impo = 0;
      |FINSI;
      |SI ant_impo1 [ 0;
         ant_impo1 = 0;
      |FINSI;
   |FINSI;
   |SI ant_impo = 0;  |Y ant_impo1 = 0;
      |ACABA;
   |FINSI;
   raya1 = ray3;
   sw_inf = 10; |PRINT;  || raya
   sw_inf = 7;  |PRINT;  || agrupacion (con importe)
   sw_inf = 5;  |PRINT;  || linea blanco
   ant_agru = antes;
|FPRC;

|PROCESO beneperdi;
   gru_nou = #cabaexp2#1;
   |SI #cabaexp2#0 ! tipo;  || ... para que salte todos
      gru_nou = 9999;
   |FINSI;
   |SI tipo = "D";
      |SI grupo = 1;
         antes = ant_agru;
         ant_impo = saldo1;
         ant_impo1 = saldo11;
         ant_agru = "BENEFICIOS DE LA EXPLOTACION";
         |HAZ imprires;
         grupo = grupo + 1;
         |SI gru_nou = grupo;
            |ACABA;
         |FINSI;
      |FINSI;
      |SI grupo = 2;
         antes = ant_agru;
         ant_impo = saldo2;
         ant_impo1 = saldo12;
         ant_agru = "RESULTADOS FINANCIEROS POSITIVOS";
         |HAZ imprires;
         antes = ant_agru;
         ant_impo = saldo1 + saldo2;
         ant_impo1 = saldo11 + saldo12;
         ant_agru = "BENEFICIOS DE LAS ACTIVIDADES ORDINARIAS";
         |HAZ imprires;
         grupo = grupo + 1;
         |SI gru_nou = grupo;
            |ACABA;
         |FINSI;
      |FINSI;
      |SI grupo = 3;
         antes = ant_agru;
         ant_impo = saldo3;
         ant_impo1 = saldo13;
         ant_agru = "RESULTADOS EXTRAORDINARIOS POSITIVOS";
         |HAZ imprires;
         antes = ant_agru;
         ant_impo = saldo1 + saldo2 + saldo3;
         ant_impo1 = saldo11 + saldo12 + saldo13;
         ant_agru = "BENEFICIOS ANTES DE IMPUESTOS";
         |HAZ imprires;
         grupo = grupo + 1;
         |SI gru_nou = grupo;
            |ACABA;
         |FINSI;
      |FINSI;
      |SI grupo = 4;
         antes = ant_agru;
         ant_impo = saldo1 + saldo2 + saldo3 + saldo4;
         ant_impo1 = saldo11 + saldo12 + saldo13 + saldo14;
         ant_agru = "RESULTADO DEL EJERCICIO (BENEFICIOS)";
         |HAZ imprires;
         grupo = grupo + 1;
      |FINSI;
   |SINO;
      |SI grupo = 1;
         antes = ant_agru;
         ant_impo = saldo1;
         ant_impo1 = saldo11;
         ant_agru = "PERDIDAS DE LA EXPLOTACION";
         |HAZ imprires;
         grupo = grupo + 1;
         |SI gru_nou = grupo;
            |ACABA;
         |FINSI;
      |FINSI;
      |SI grupo = 2;
         antes = ant_agru;
         ant_impo = saldo2;
         ant_impo1 = saldo12;
         ant_agru = "RESULTADOS FINANCIEROS NEGATIVOS";
         |HAZ imprires;
         antes = ant_agru;
         ant_impo = saldo1 + saldo2;
         ant_impo1 = saldo11 + saldo12;
         ant_agru = "PERDIDAS DE LAS ACTIVIDADES ORDINARIAS";
         |HAZ imprires;
         grupo = grupo + 1;
         |SI gru_nou = grupo;
            |ACABA;
         |FINSI;
      |FINSI;
      |SI grupo = 3;
         antes = ant_agru;
         ant_impo = saldo3;
         ant_impo1 = saldo13;
         ant_agru = "RESULTADOS EXTRAORDINARIOS NEGATIVOS";
         |HAZ imprires;
         antes = ant_agru;
         ant_impo = saldo1 + saldo2 + saldo3;
         ant_impo1 = saldo11 + saldo12 + saldo13;
         ant_agru = "PERDIDAS ANTES DE IMPUESTOS";
         |HAZ imprires;
         grupo = grupo + 1;
         |SI gru_nou = grupo;
            |ACABA;
         |FINSI;
      |FINSI;
      |SI grupo = 4;
         antes = ant_agru;
         ant_impo = saldo1 + saldo2 + saldo3 + saldo4;
         ant_impo1 = saldo11 + saldo12 + saldo13 + saldo14;
         ant_agru = "RESULTADO DEL EJERCICIO (PERDIDAS)";
         |HAZ imprires;
         grupo = grupo + 1;
      |FINSI;
   |FINSI;
   grupo = #cabaexp2#1;
|FPRC;

|PROCESO titulillo;
   |SI #cabaexp2#0 = "D";
      |SI #cabaexp2#1 = 1;
         raya11 = "GASTOS DE EXPLOTACION";
      |SINO;
         |SI #cabaexp2#1 = 2;
            raya11 = "GASTOS FINANCIEROS";
         |SINO;
            |SI #cabaexp2#1 = 3;
               raya11 = "GASTOS EXTRAORDINARIOS";
            |SINO;
               raya11 = "";
            |FINSI;
         |FINSI;
      |FINSI;
   |SINO;
      |SI #cabaexp2#1 = 1;
         raya11 = "INGRESOS DE EXPLOTACION";
      |SINO;
         |SI #cabaexp2#1 = 2;
            raya11 = "INGRESOS FINANCIEROS";
         |SINO;
            |SI #cabaexp2#1 = 3;
               raya11 = "INGRESOS EXTRAORDINARIOS";
            |SINO;
               raya11 = "";
            |FINSI;
         |FINSI;
      |FINSI;
   |FINSI;
   sw_inf = 11;
   |PRINT;
|FPRC;

|PROCESO impresio;
   |SI #cabaexp2#11 = 0; |Y #cabaexp2#12 = 0;  |ACABA;  |FINSI;
   cantidad = #cabaexp2#11;
   cantidad1 = #cabaexp2#12;
   |SI sw = 0;
      tipo = #cabaexp2#0;
      grupo = #cabaexp2#1;
      agrupaci = #cabaexp2#2;
      epigrafe = #cabaexp2#3;
      partida = #cabaexp2#4;
      |SI #cabaexp2#0 = "D";
         doh = "  D E B E";
      |SINO;
         doh = " H A B E R ";
      |FINSI;
      act_cuent = #cabaexp2#5;
      act_nivel = #cabaexp2#6;
      sw = 1;
      |HAZ titulillo;
   |FINSI;

   nue_cuent = #cabaexp2#5;
   nue_nivel = #cabaexp2#6;

   |HAZ haycambio;


   |SI tipo ! #cabaexp2#0;
      |HAZ cambio1;
      |HAZ rup_epig;
      |HAZ rup_agru;
      |HAZ beneperdi;
      |HAZ rup_tipo;
      |HAZ titulillo;
   |SINO;
      |SI grupo ! #cabaexp2#1;
         |HAZ cambio1;
         |HAZ rup_epig;
         |HAZ rup_agru;
         |HAZ beneperdi;
         |HAZ titulillo;
      |SINO;
         |SI agrupaci ! #cabaexp2#2;
            |HAZ cambio1;
            |HAZ rup_epig;
            |HAZ rup_agru;
         |SINO;
            |SI epigrafe ! #cabaexp2#3;
               |HAZ cambio1;
               |HAZ rup_epig;
            |FINSI;
         |FINSI;
      |FINSI;
   |FINSI;
   act_cuent = #cabaexp2#5;
   act_nivel = #cabaexp2#6;
   tipo = #cabaexp2#0;
   grupo = #cabaexp2#1;
   agrupaci = #cabaexp2#2;
   epigrafe = #cabaexp2#3;
   partida = #cabaexp2#4;

   #cacuenta#0 = #cabaexp2#5;
   #cacuenta#2 = #cabaexp2#7;
   sw_inf = 6;
   |PRINT;
   llegoa = nivelet;
   suma1 = suma1 + cantidad;
   suma2 = suma2 + cantidad;
   suma3 = suma3 + cantidad;
   suma4 = suma4 + cantidad;
   suma5 = suma5 + cantidad;
   suma6 = suma6 + cantidad;
   suma11 = suma11 + cantidad1;
   suma12 = suma12 + cantidad1;
   suma13 = suma13 + cantidad1;
   suma14 = suma14 + cantidad1;
   suma15 = suma15 + cantidad1;
   suma16 = suma16 + cantidad1;
   nue_cuent = #cabaexp2#5;
   nue_nivel = #cabaexp2#6;
   ant_agru = #cabaexp2#8;
   |QBF ant_agru;

   ant_epig = #cabaexp2#9;
   |QBF ant_epig;

   t_agru = t_agru + #cabaexp2#11;
   t_agru1 = t_agru1 + #cabaexp2#12;
   t_epig = t_epig + #cabaexp2#11;
   t_epig1 = t_epig1 + #cabaexp2#12;
|FPRC;

|DEFBUCLE imprimiendo;
   #5#1;
   ;
   "D", 1, 01, 00, 00, "            ", 0;
   "H", 4, 99, 99, 99, "zzzzzzzzzzzz", 9;
   ;
   NULL, impresio;
|FIN;

|PROCESO nivelrep; |TIPO 0;
   |SI nivelet = 0;
      nivelet = #caivexpl#18;
   |FINSI;
   |PARA x = Campo - 1;  |SI x ] 18;  |HACIENDO x = x - 1;
      |SI #caivexpl#Campo# = #caivexpl#x#;  |Y #caivexpl#Campo# ! 0;
         |MENAV "     Nivel repetido ...";
         |ERROR;
         |ACABA;
         |SAL;
      |FINSI;
      |SI nivelet < #caivexpl#x#;
         nivelet = #caivexpl#x#;
      |FINSI;
   |SIGUE;
|FPRC;

|PROCESO let_nivel;
   |SI nivelet = 0;
      nivelet = #caivexpl#18;
   |FINSI;
   |PARA x = 23;  |SI x ] 18;  |HACIENDO x = x - 1;
      |SI nivelet < #caivexpl#x#;
         nivelet = #caivexpl#x#;
      |FINSI;
   |SIGUE;
|FPRC;

|PROCESO final;
   |HAZ cambio1;
   |HAZ rup_epig;
   |HAZ rup_agru;
   #cabaexp2#1 = 9999;
   |HAZ beneperdi;
   |HAZ rup_tipo;
|FPRC;

|PROCESO acortacuen;
   |SI nivelet < #cacuenta#1;
      |SI nivelet = 1;  traraf = 100 + dig4;  |FINSI;
      |SI nivelet = 2;  traraf = 100 + dig5;  |FINSI;
      |SI nivelet = 3;  traraf = 100 + dig6;  |FINSI;
      |SI nivelet = 4;  traraf = 100 + dig7;  |FINSI;
      |SI nivelet = 5;  traraf = 100 + dig8;  |FINSI;
      #cacuenta#0 = #cacuenta#0 % traraf;
      #cacuenta#1 = nivelet;
      #cacuenta#0 = #cacuenta#0 + "              ";
      #cacuenta#0 = #cacuenta#0 % 112;
   |FINSI;
|FPRC;

|PROCESO cargatra;
   #cabaexp2#0 = #cacuenta#5;
   #cabaexp2#1 = #cacuenta#4;
   #cabaexp2#2 = #cacuenta#6;
   #cabaexp2#3 = #cacuenta#7;
   #cabaexp2#4 = #cacuenta#8;
   |HAZ acortacuen;
   #cabaexp2#5 = #cacuenta#0;
   #cabaexp2#6 = #cacuenta#1;
   #cabaexp2#7 = #cacuenta#2;
   |LEE 000#5=;
   |SI FSalida ! 0;
      |DDEFECTO #5;
      #cabaexp2#0 = #cacuenta#5;
      #cabaexp2#1 = #cacuenta#4;
      #cabaexp2#2 = #cacuenta#6;
      #cabaexp2#3 = #cacuenta#7;
      #cabaexp2#4 = #cacuenta#8;
      |HAZ acortacuen;
      #cabaexp2#5 = #cacuenta#0;
      #cabaexp2#6 = #cacuenta#1;
      #cabaexp2#7 = #cacuenta#2;
      #cabaexp2#11 = 0;
      #cabaexp2#12 = 0;
      |GRABA 020#5c;
   |FINSI;
   |DDEFECTO #2;
   #caparexp#0 = #cacuenta#4;
   #caparexp#1 = #cacuenta#5;
   #caparexp#2 = #cacuenta#6;
   #caparexp#4 = #cacuenta#7;
   #caparexp#6 = #cacuenta#8;
   |LEE 000#2=;
   |SI FSalida ! 0;
      #caparexp#3 = "";
      #caparexp#5 = "";
      #caparexp#7 = "";
   |FINSI;
   #cabaexp2#8 = #caparexp#3;
   #cabaexp2#9 = #caparexp#5;
   #cabaexp2#10 = #caparexp#7;
|FPRC;

|PROCESO sumadeha;
   |SI acumula = 11;
      |SI #cacuenta#4 = 1;
         saldo1 = saldo1 + importe;
      |SINO;
         |SI #cacuenta#4 = 2;
            saldo2 = saldo2 + importe;
         |SINO;
            |SI #cacuenta#4 = 3;
               saldo3 = saldo3 + importe;
            |SINO;
               saldo4 = saldo4 + importe;
            |FINSI;
         |FINSI;
      |FINSI;
   |SINO;
      |SI #cacuenta#4 = 1;
         saldo11 = saldo11 + importe;
      |SINO;
         |SI #cacuenta#4 = 2;
            saldo12 = saldo12 + importe;
         |SINO;
            |SI #cacuenta#4 = 3;
               saldo13 = saldo13 + importe;
            |SINO;
               saldo14 = saldo14 + importe;
            |FINSI;
         |FINSI;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO calculit;
   |HAZ digitos2;
   |SI d_error ! 0;     |ACABA;  |FINSI;
   |SI #cacuenta#1 > nivelet;  |ACABA;  |FINSI;
   |SI #cacuenta#3 = "S";  |O #cacuenta#1 = nivelet;
      |HAZ cargatra;
      || .. Suma ---------- PERIODO
      acumula = 11;
      |SI #cacuenta#5 = "D";
         |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
            #cabaexp2#acumula# = #cabaexp2#acumula# + #cacuenta#x#;
            importe = #cacuenta#x#;
            |HAZ sumadeha;
         |SIGUE;
      |SINO;
         |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
            #cabaexp2#acumula# = #cabaexp2#acumula# + (#cacuenta#x# * -1);
            importe = #cacuenta#x#;
            |HAZ sumadeha;
         |SIGUE;
      |FINSI;
      || .. Suma ---------- ACUMULADO
      acumula = 12;
      |SI #cacuenta#5 = "D";
         |PARA x = acdesde; |SI x [ achasta; |HACIENDO x = x + 3;
            #cabaexp2#acumula# = #cabaexp2#acumula# + #cacuenta#x#;
            importe = #cacuenta#x#;
            |HAZ sumadeha;
         |SIGUE;
      |SINO;
         |PARA x = acdesde; |SI x [ achasta; |HACIENDO x = x + 3;
            #cabaexp2#acumula# = #cabaexp2#acumula# + (#cacuenta#x# * -1);
            importe = #cacuenta#x#;
            |HAZ sumadeha;
         |SIGUE;
      |FINSI;
      |GRABA 020#5a;
      |LIBERA #5;
   |FINSI;
|FPRC;

|DEFBUCLE carga_ctas;
   #1#1;
   5,4,6,7,8;
   "            ", "D", 1, 01, 00, 00;
   "zzzzzzzzzzzz", "H", 4, 99, 99, 99;
   e;
   NULL, calculit;
|FIN;

|PROCESO conexi; |TIPO 0;
   enTExi = 1;
   d_mes = #caivexpl#9;
   h_mes = #caivexpl#10;
   estru = #caivexpl#8;
   r_emp = empre;
   r_per = ejerc;
   r_eje = #canempre#26;
   r_nom = "PERIODO " + #canempre#1;
   |SUB_EJECUTA "caexicon.run";
   |SI #caivexpl#9 ! #caivexpl#15;  |O #caivexpl#10 ! #caivexpl#16;
      d_mes = #caivexpl#15;
      h_mes = #caivexpl#16;
      estru = #caivexpl#8;
      r_emp = empre;
      r_per = ejerc;
      r_eje = #canempre#26;
      r_nom = "ACUMULADOS " + #canempre#1;
      |SUB_EJECUTA "caexicon.run";
   |FINSI;
|FPRC;

____________________________________/ CALCULO PRINCIPAL
|PROCESO impre; |TIPO 3;

   |HAZ FecApeCie;
   |SI enMAper ! dig1;
       |SI #0#1 < enMAper; |Y #0#1 ! 0; |ACABA; |FINSI;
       |SI #0#5 < enMAper; |Y #0#5 ! 0;
           #0#5 = enMAper;
       |FINSI;
   |FINSI;

   |HAZ let_nivel;
   |HAZ antesque;
   |SI #0#24 = "S";  |HAZ conexi; |FINSI;
   eldesde = (#caivexpl#4 * 3 ) + 11;
   elhasta = (#caivexpl#5 * 3 ) + 11;
   enSwOper = 0;
   |SUB_EJECUTA "carecsig.tab";

   |ABRE #8;
   |DDEFECTO #8;
   #8#0 = 1;
   |LEE 000#8=;
   |CIERRA #8;
   |SI #caconimp#2 = "S";  |Y #caconimp#3 = "N";  |Y aparam = "";
      |DFICE pathbal;
      balance = 1;
      |SI #caconimp#10 = "S";
          enSwOper = 2;
          |SUB_EJECUTA "carecsig";
          enSwOper = 0;
          |SI enECuadre = 1; |ACABA; |FINSI;
          |NOME_DAT #1 eaNomFCta;
      |SINO;
          |SUB_EJECUTA "careccue";
      |FINSI;
   |FINSI;


   |ABRE #5;
   |CIERRA #5;
   |DELINDEX #5;

   |IMPRE 0;
   |SI FSalida ! 0;  |ACABA;  |FINSI;
   |HAZ los_informes;
   informe = que_i1;
   |INFOR informe;
   |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

   sw = 0;
   eldesde = (#caivexpl#0 * 3 ) + 11;
   elhasta = (#caivexpl#1 * 3 ) + 11;
   acdesde = (#caivexpl#4 * 3 ) + 11;
   achasta = (#caivexpl#5 * 3 ) + 11;

   |ABRE #5;

   saldo1  = 0;  saldo2  = 0;  saldo3  = 0;  saldo4  = 0;
   saldo11 = 0;  saldo12 = 0;  saldo13 = 0;  saldo14 = 0;

   |ABRE #2;
   |BUCLE carga_ctas;
   |CIERRA #2;

   |HAZ borractas;
   |HAZ cargades;
   |ABRE #1;
   |BUCLE imprimiendo;

   |HAZ final;

   |CIERRA #1;
   |CIERRA #5;
   |FININF;
   |FINIMP;
|FPRC;

|| ------------------------ cuentas de descarga

|PROCESO borrades;
   #cabaexp2#0 = #cacuenta#5;
   #cabaexp2#1 = #cacuenta#4;
   #cabaexp2#2 = #cacuenta#6;
   #cabaexp2#3 = #cacuenta#7;
   #cabaexp2#4 = #cacuenta#8;
   |HAZ acortacuen;
   #cabaexp2#5 = #cacuenta#0;
   #cabaexp2#6 = #cacuenta#1;
   #cabaexp2#7 = #cacuenta#2;
   |LEE 000#5=;
   |SI FSalida = 0;
      importe = -#cabaexp2#11;
      |SI #cacuenta#5 = "H"; importe = -importe; |FINSI;
      acumula = 11;
      |HAZ sumadeha;
      importe = -#cabaexp2#12;
      |SI #cacuenta#5 = "H"; importe = -importe; |FINSI;
      acumula = 12;
      |HAZ sumadeha;
      |BORRA 000#5a;
   |FINSI;
|FPRC;

|PROCESO lineabor;
   #cacuenta#0 = #caexistl#5;
   #cacuenta#1 = #caexistl#6;
   |LEE 000#1=;
   |SI FSalida = 0;
      |HAZ borrades;
   |FINSI;
   #cacuenta#0 = #caexistl#8;
   #cacuenta#1 = #caexistl#9;
   |LEE 000#1=;
   |SI FSalida = 0;
      |HAZ borrades;
   |FINSI;
|FPRC;


|PROCESO descarg;
   |HAZ digitos2;
   |SI d_error ! 0;  |ACABA;  |FINSI;

   || --- periodo
   acumula = 11;
   mesini = #caivexpl#9 + 11;              || iniciales
   descar = #caexistl#mesini#;
   mesini = #caivexpl#10 + 11;              || Finales
   descar = descar - #caexistl#mesini#;
   |SI descar ] 0;
      #cacuenta#0 = #caexistl#5;     || cuenta descarga D
      #cacuenta#1 = #caexistl#6;     || digito descarga D
   |SINO;
      #cacuenta#0 = #caexistl#8;     || cuenta descarga H
      #cacuenta#1 = #caexistl#9;     || digito descarga H
   |FINSI;
   |LEE 000#1=;             || lee cta. descarga
   |SI FSalida = 0;
      |HAZ cargatra;
      |SI #cacuenta#5 = "D";
         #cabaexp2#acumula# = #cabaexp2#acumula# + descar;
      |SINO;
         #cabaexp2#acumula# = #cabaexp2#acumula# - descar;
      |FINSI;
      importe = descar;
      |HAZ sumadeha;
      |GRABA 020#5a;
      |LIBERA #5;
   |FINSI;

   || --- acumulado
   acumula = 12;
   mesini = #caivexpl#15 + 11;              || Iniciales
   descar = #caexistl#mesini#;
   mesini = #caivexpl#16 + 11;              || Finales
   descar = descar - #caexistl#mesini#;
   |SI descar ] 0;
      #cacuenta#0 = #caexistl#5;     || cuenta descarga D
      #cacuenta#1 = #caexistl#6;     || digito descarga D
   |SINO;
      #cacuenta#0 = #caexistl#8;     || cuenta descarga H
      #cacuenta#1 = #caexistl#9;     || digito descarga H
   |FINSI;
   |LEE 000#1=;             || lee cta. descarga
   |SI FSalida = 0;
      |HAZ cargatra;
      |SI #cacuenta#5 = "D";
         #cabaexp2#acumula# = #cabaexp2#acumula# + descar;
      |SINO;
         #cabaexp2#acumula# = #cabaexp2#acumula# - descar;
      |FINSI;
      importe = descar;
      |HAZ sumadeha;
      |GRABA 020#5a;
      |LIBERA #5;
   |FINSI;
|FPRC;

|PROCESO lineades;
   #cacuenta#0 = #caexistl#2;
   #cacuenta#1 = #caexistl#3;
   |LEE 000#1=;
   |SI FSalida = 0; |Y #cacuenta#0 > doce;
      |HAZ descarg;
   |FINSI;
|FPRC;

|PROCESO borractas;
||----------------- ESTE PROCESO BORRA LAS CUENTAS CARGADAS de DESCARGA
   |ABRE #1;
   |ABRE #2;
   |ABRE #6;
   #caexistc#0 = #caivexpl#8;
   |LEE 000#6=;
   |BUCLELIN 2lineabor#6;
   |CIERRA #1;
   |CIERRA #2;
   |CIERRA #6;
|FPRC;

|PROCESO cargades;
||----------------- ESTE PROCESO GRABA LAS CUENTAS de DESCARGA
   |ABRE #1;
   |ABRE #2;
   |ABRE #6;
   #caexistc#0 = #caivexpl#8;
   |LEE 000#6=;
   |BUCLELIN 2lineades#6;
   |CIERRA #1;
   |CIERRA #2;
   |CIERRA #6;
|FPRC;
