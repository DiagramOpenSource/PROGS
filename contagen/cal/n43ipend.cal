|FICHEROS;
   n43ipend #0;

   n43consl #1;
   camaasil #2;
   calicont #4;
   camaasic #5;

   n43saldo #15;
   cacuenta #10;
   espejo@  #11;

   canempre #30;
   caselem1 #998;
   n43wpend #999;

   cabaexp4 #98;
   caw00039 #99;
|FIN;

|VARIABLES;
    &entrada = 1;
    informe  = "";
    inform1  = "n43xpend";
    inform2  = "n43ipend";
    aDef     = "";
    aMas     = "";
    aAlfa1   = "";
    nBanco   = 0;
    aDCuenta = "";
    aHCuenta = "";
    sw       = 0;
    swPie    = 0;
    swAlgun  = 0;
    swHay    = 0;
    aDMarca  = "";
    aHMarca  = "";
    aCuenta  = "";

    aFichero  = "";
    &nSwLinea = 0;

    alfa1    = "";
    alfa2    = "";
    nEjerCam = 0;
    nEjer    = 0;
    nPeriodo = 0;
    nCambio  = 0;
    nHay     = 0;
    nEstado  = 0;

    CodigoEmpresa  = 0;
    PeriodoEmpresa = 0;
    dirfichOri     = "";
    nSitua         = 0;
    nMensa         = 0;
    nPrint         = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE dsco08_i;

|CALCULO defecto; |TIPO 7;
  |SI #0#48 = "S";
      #0#2 = "01.01.2000";
      #0#7 = 01;
  |SINO;
      #0#2 = fec1; |PINTA #0#2;
      #0#7 = 00;
  |FINSI;
  |PINTA #0#2;
  #0#3 = fec2; |PINTA #0#3;
  |PINTA #0#7;
|FCAL;

|CALCULO eldefecto; |TIPO 0;
  |HAZ defecto;
|FCAL;

|CALCULO ElTitulo; |TIPO 0;
  |SI #0#4 = "S";
      #0#35 = "INFORME DE ASIENTOS CONCILIADOS";
  |SINO;
      #0#35 = "INFORME DE ASIENTOS PENDIENTES DE CONCILIAR  ";
  |FINSI;
  |PINTA #0#35;
|FCAL;

|CALCULO cuenta;|TIPO 6;
   eaCuenta = #0Campo;
   salidas  = FSalida;
   |HAZ FiltraPuntos;
   #0Campo = eaCuenta; |PINTA #0Campo;
|FCAL;

|CALCULO fechas; |TIPO 0;
  |SI #0#28 = "N";
      |SI #0Campo > fec2;|O #0Campo < fec1;
          |MENAV "    Fecha fuera de Periodo ...";
          |ERROR;
          |ACABA;
      |FINSI;
  |FINSI;
  |SI #0#3 < #0#2; |Y Campo = 3;
       |MENAV "    Error, fecha Hasta Inferior";
       |ERROR;
   |FINSI;
|FCAL;

|CALCULO DesHasBanco;
   |SI #0#6 < #0#5;
       |MENAV "    Error, Codigo del Banco Hasta Inferior";
       |ERROR;
   |FINSI;
|FCAL;

|CALCULO DesHasDiario;
   |SI #0#8 < #0#7;
       |MENAV "    Error, Codigo del Diario Hasta Inferior";
       |ERROR;
   |FINSI;
|FCAL;

|| *************************************************************************
||                       PROCESOS DEL EXTRACTO POR PANTALLA
|| *************************************************************************
|PROCESO ContraPart;
     aCuenta = "";
     |SI #2#8 = "D"; aCuenta = #5#10; |FINSI;
     |SI #2#8 = "H"; aCuenta = #5#08; |FINSI;

     |DDEFECTO #4;
     #4#0 = #2#0;
     #4#1 = #2#1;
     #4#2 = #2#2;
     #4#3 = #2#3;
     |LEE 001#4=;
     |SI FSalida = 0; aCuenta = #4#4; |FINSI;

     |ABRE #11;
     #11#0 = aCuenta;
     |LEE 040#11=;
     |SI FSalida ! 0;  |DDEFECTO #11;  |FINSI;
     #0#26 = aCuenta;
     #0#27 = #11#2;
     |CIERRA #11;
|FPRC;

|PROCESO saldo_auxiliar;

     |SI sw = 0;
         |SI #0#41 = "C"; #0#46 = #999#8; |FINSI;
         |SI #0#41 = "T"; #0#47 = #999#4; |FINSI;
         #0#43 = 0;
         #0#44 = 0;
         sw    = 1;
     |FINSI;

     |SI #0#41 = "C";
         |SI #0#46 ! #999#8;
             |SI #0#42 = "S"; |Y swAlgun = 1;
                 nSwLinea = 1; |PRINT; nPrint = 1; swPie = 1;
             |FINSI;
             #0#46 = #999#8;
             #0#43   = 0;
             #0#44   = 0;
             swAlgun = 0;
        |FINSI;
     |FINSI;
     |SI #0#41 = "T";
         |SI #0#47 ! #999#4;
             |SI #0#42 = "S"; |Y swAlgun = 1;
                 nSwLinea = 1; |PRINT; nPrint = 1; swPie = 1;
             |FINSI;
             #0#47 = #999#4;
             #0#43   = 0;
             #0#44   = 0;
             swAlgun = 0;
        |FINSI;
     |FINSI;

     #0#18 = #999#0;
     #0#19 = #999#1;
     #0#20 = #999#2;
     #0#21 = #999#3;
     #0#22 = #999#4;
     #0#23 = #999#5;
     #0#24 = #999#6;
     #0#25 = #999#7;
     #0#26 = #999#8;
     #0#27 = #999#9;
     #0#28 = #999#10;
     #0#40 = #999#11;

     |SI #0#28 = "N"; |Y #0#4 ! "S";
         nSwLinea = 0;
         |PRINT; nPrint = 1; swPie = 1;
         swAlgun = 1;
         #0#38 = #0#38 + #0#24;
         #0#39 = #0#39 + #0#25;

         #0#43 = #0#43 + #0#24;
         #0#44 = #0#44 + #0#25;
     |FINSI;

     |SI #0#28 ! "N"; |Y #0#4 ! "N";
         nSwLinea = 0;
         |PRINT; nPrint = 1; swPie = 1;
         swAlgun = 1;
         #0#38 = #0#38 + #0#24;
         #0#39 = #0#39 + #0#25;

         #0#43 = #0#43 + #0#24;
         #0#44 = #0#44 + #0#25;
     |FINSI;

     |SI #0#28 = "N";
         #0#29 = #0#29 + #0#24;
         #0#30 = #0#30 + #0#25;
         #0#31 = #0#29 - #0#30;
     |SINO;
         #0#32 = #0#32 + #0#24;
         #0#33 = #0#33 + #0#25;
         #0#34 = #0#32 - #0#33;
     |FINSI;
     swHay = 1;
|FPRC;

|DEFBUCLE auxiliar_C;
     #999#1;
     ;
     "            ", #0#2, 000000, 0001;
     "zzzzzzzzzzzz", #0#3, 999999, 9999;
     ;
     NULL, saldo_auxiliar;
|FIN;

|DEFBUCLE auxiliar_T;
     #999#2;
     ;
     000, "            ", #0#2, 000000, 0001;
     999, "zzzzzzzzzzzz", #0#3, 999999, 9999;
     ;
     NULL, saldo_auxiliar;
|FIN;

|PROCESO GrabaAuxiliar;
     #999#0  = #0#18;
     #999#1  = #0#19;
     #999#2  = #0#20;
     #999#3  = #0#21;
     #999#4  = #0#22;
     #999#5  = #0#23;
     #999#6  = #0#24;
     #999#7  = #0#25;
     #999#8  = #0#26;
     #999#9  = #0#27;
     #999#10 = #0#28;
     #999#11 = #0#40;
     |GRABA 040#999n;
     swHay = 1;
|FPRC;

|PROCESO saldo;

     |PDEFECTO #0, 18, 29;
     #0#40 = "";

     |SI #0#41 = "F";
         |SI sw = 0;
             #0#45 = #2#1;
             #0#43 = 0;
             #0#44 = 0;
             sw    = 1;
         |FINSI;

         |SI #0#45 ! #2#1;
             |SI #0#42 = "S"; |Y swAlgun = 1;
                 nSwLinea = 1; |PRINT; nPrint = 1; swPie = 1;
             |FINSI;
             #0#45 = #2#1;
             #0#43   = 0;
             #0#44   = 0;
             swAlgun = 0;
        |FINSI;
     |FINSI;

     #5#0 = #2#0;
     #5#1 = #2#1;
     #5#2 = #2#2;
     |LEE 040#5=;
     |SI FSalida ! 0;  |DDEFECTO #5;  |FINSI;

     |SI #2#18 = " ";
         #1#10 = #2#4;   || Cuenta
         #1#12 = #2#1;   || Fecha
         #1#13 = #2#2;   || Documento
         #1#18 = #2#0;   || Diario
         #1#19 = #2#3;   || Apunte
         |LEE 041#1=;
         |SI FSalida = 0;
             #0#40 = "(P)";
             |SI #0#37 = "S";
                 #2#18 = "û";
             |FINSI;
         |FINSI;
     |FINSI;

     #0#18 = #2#0;
     #0#19 = #2#1;
     #0#20 = #2#2;
     #0#21 = #2#3;
     #0#22 = #2#6;
     #0#23 = #2#7;
     |SI #2#8 = "D";
         #0#24 = #2#9;
         #0#25 = 0;
     |SINO;
         #0#24 = 0;
         #0#25 = #2#9;
     |FINSI;

     |HAZ ContraPart;

     |SI #2#18 = " ";
         #0#28 = "N";
     |SINO;
         #0#28 = "S";
     |FINSI;

     |SI #0#41 ! "F";
         |HAZ GrabaAuxiliar;
         |ACABA;
     |FINSI;

     |SI #2#18 = " "; |Y #0#4 ! "S";
         nSwLinea = 0;
         |PRINT; nPrint = 1; swPie = 1;
         swAlgun = 1;
         #0#38 = #0#38 + #0#24;
         #0#39 = #0#39 + #0#25;

         #0#43 = #0#43 + #0#24;
         #0#44 = #0#44 + #0#25;
     |FINSI;

     |SI #2#18 ! " "; |Y #0#4 ! "N";
         nSwLinea = 0;
         |PRINT; nPrint = 1; swPie = 1;
         swAlgun = 1;
         #0#38 = #0#38 + #0#24;
         #0#39 = #0#39 + #0#25;

         #0#43 = #0#43 + #0#24;
         #0#44 = #0#44 + #0#25;
     |FINSI;

     |SI #0#28 = "N";
         #0#29 = #0#29 + #0#24;
         #0#30 = #0#30 + #0#25;
         #0#31 = #0#29 - #0#30;
     |SINO;
         #0#32 = #0#32 + #0#24;
         #0#33 = #0#33 + #0#25;
         #0#34 = #0#32 - #0#33;
     |FINSI;
     swHay = 1;
|FPRC;

|DEFBUCLE camaasil;
     #2#3;
     0;
     #10#0, #0#2, 000000, 0001, #0#7;
     #10#0, #0#3, 999999, 9999, #0#8;
     e;
     NULL, saldo;
|FIN;

|PROCESO LeoAuxW039p;
   dirfich0 = #99#6;
   |QBF dirfich0;

   |CIERRA #1;
   |CIERRA #2;
   |CIERRA #4;
   |CIERRA #5;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #2 dirfich0;

   |PATH_DAT #4 dirfich0;
   |PATH_DAT #5 dirfich0;

   |ABRE #2;
   #2#0 = #99#0;
   #2#1 = #99#1;
   #2#2 = #99#2;
   #2#3 = #99#3;
   |LEE 001#2=;
   |SI FSalida = 0;
       |ABRE #1;
       |SELECT #3#1;
       |ABRE #4;
       |ABRE #5;
       |HAZ saldo;
       |CIERRA #5;
       |CIERRA #4;
       |CIERRA #1;
   |FINSI;
   |CIERRA #2;
|FPRC;

|DEFBUCLE LeoAuxW039;
     #99#2;
     ;
     #10#0, #0#2, 000000, 0001;
     #10#0, #0#3, 999999, 9999;
     e;
     NULL, LeoAuxW039p;
|FIN;

|PROCESO cabdife;

    |PDEFECTO #0, 10, 35;
    #0#38 = 0; #0#39 = 0;
    #0#40 = "";
    #0#43 = 0; #0#44 = 0;
    #0#45 = "01.01.0000";
    #0#46 = "            ";
    #0#47 = 0;
    |DELINDEX #999;

    nBanco = 0;
    |DDEFECTO #15;
    #15#6 = #10#0;
    |LEE 040#15=;
    |SI FSalida = 0;
        nBanco = #15#0;
    |FINSI;
    |SI nBanco < #0#5; |O nBanco > #0#6;
        |ACABA;        || ... No esta dentro de la Seleccion de bancos
    |FINSI;

    |SI nBanco ! 0;
        #0#10 = nBanco;
        #0#11 = #15#1;
        aAlfa1 = #15#2;          |QBF aAlfa1; |SI aAlfa1 ! ""; aAlfa1 = aAlfa1 + " "; |FINSI;
        aAlfa1 = aAlfa1 + #15#3; |QBF aAlfa1; |SI aAlfa1 ! ""; aAlfa1 = aAlfa1 + " "; |FINSI;
        aAlfa1 = aAlfa1 + #15#4; |QBF aAlfa1; |SI aAlfa1 ! ""; aAlfa1 = aAlfa1 + "/"; |FINSI;
        aAlfa1 = aAlfa1 + #15#5;
        #0#12 = aAlfa1;
        #0#13 = #15#9;
    |SINO;
        #0#10 = 0;
        #0#11 = #10#2;
    |FINSI;
    #0#14 = #10#0;
    #0#15 = #10#51 - #10#48;   ||Saldo sin asiento de Cierre
    #0#16 = #10#52 - #10#49;
    #0#17 = #0#15  - #0#16;

    sw      = 0;
    swAlgun = 0;
    swPie   = 0;
    swHay   = 0;

    |ABRE #999;
    |SI #0#48 = "N";
        |BUCLE camaasil;
    |SINO;
        |BUCLE LeoAuxW039;
    |FINSI;
    |CIERRA #999;

    |SI #0#41 = "C"; |Y swHay = 1;
        sw      = 0;
        swHay   = 0;
        swAlgun = 0;
        |BUCLE auxiliar_C;
    |FINSI;
    |SI #0#41 = "T"; |Y swHay = 1;
        sw      = 0;
        swHay   = 0;
        swAlgun = 0;
        |BUCLE auxiliar_T;
    |FINSI;

    |SI swHay = 1;
        |SI #0#42 = "S"; |Y swAlgun = 1;
            nSwLinea = 1; |PRINT; nPrint = 1; swPie = 1;
        |FINSI;
        |SI swPie = 1; |PIEINF; |FINSI;
    |FINSI;
|FPRC;

|DEFBUCLE caconcic;
     #10#1;
     3;
     aDCuenta, "S";
     aHCuenta, "S";
     e;
     NULL, cabdife;
|FIN;

|PROCESO LeeAuxCta;
     |DDEFECTO #10;
     #10#0 = #98#0;
     |LEE 001#10=;
     |SI FSalida ! 0;
         #10#0 = #98#0;
         #10#2 = #98#2;
     |FINSI;
     |HAZ cabdife;
|FPRC;

|DEFBUCLE LeeAuxCta;
     #98#1;
     ;
     "            ";
     "zzzzzzzzzzzz";
     e;
     NULL, LeeAuxCta;
|FIN;
|| *************************************************************************
|PROCESO HazTodo;

     aDCuenta = #0#0; aHCuenta = #0#1;
     aDMarca  = " "; aHMarca = "û";

     |SI #0#48 = "N";
         |ABRE #1;
         |SELECT #3#1;
         |ABRE #4;
         |ABRE #5;
         |ABRE #15;
         |SELECT #2#15;
         |BUCLE caconcic;
         |SELECT #1#15;
         |CIERRA #15;
         |CIERRA #5;
         |CIERRA #4;
         |CIERRA #1;
    |SINO;
         |ABRE #10;
         |ABRE #15;
         |SELECT #2#15;
         |BUCLE LeeAuxCta;
         |CIERRA #15;
         |CIERRA #10;
    |FINSI;
|FPRC;

|PROCESO impre;|TIPO 3;
      CodigoEmpresa    = enEmpresa;
      PeriodoEmpresa   = enPeriodo;
      |DFICE dirfichOri;
      dirfich0 = dirfichOri;
      |HAZ eCam_Primero;

      aFichero = "9w" + Usuario; |NOME_DAT #999 aFichero; |DELINDEX #999;
      aFichero = "8w" + Usuario; |NOME_DAT #98  aFichero; |DELINDEX #98;
      aFichero = "7w" + Usuario; |NOME_DAT #99  aFichero; |DELINDEX #99;

      |DBASE aMas;
      aDef = aMas + "def/cacuenta";
      |CARGA_DEF aDef, espejo@;
      |SI FSalida ! 0;
          |MENAV "    Error cargando cacuenta ... ";
          |ACABA;
      |FINSI;

     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     informe = inform1;
     |SI #0#9 = "S";  informe = inform2;  |FINSI;
     |INFOR informe;
     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

     nPrint = 0;
     |SI #0#48 = "N";
         |SI #48#27 = "S";
             nMensa = 1; |HAZ MultiEmpresa;
         |SINO;
             |DFICE dirfich0;
             |HAZ HazTodo;
         |FINSI;
     |SINO;
         |HAZ MultiEjercicio;
     |FINSI;
     |SI nMensa = 0; |Y nPrint = 0;
         |MENAV "    Seleccion vacia";
     |FINSI;

     |FININF;
     |FINIMP;
     |DESCARGA_DEF #11;
|FPRC;

|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #2 dirfich0;
   |PATH_DAT #4 dirfich0;
   |PATH_DAT #5 dirfich0;
   |PATH_DAT #10 dirfich0;
   |PATH_DAT #11 dirfich0;
   |SI #0#48 = "N"; |PATH_DAT #15 dirfich0; |FINSI;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 00001;
 99999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;

|PROCESO PasoAuxW039;
       nHay = 1;
       #99#0 = #2#0;
       #99#1 = #2#1;
       #99#2 = #2#2;
       #99#3 = #2#3;
       #99#4 = eaCuenta;
       #99#5 = nPeriodo;
       #99#6 = dirfich0;
       #99#7 = enEjercicio;
       #99#8 = #2#8;
       #99#9 = #2#9;
       #99#10 = #2#7;
       |GRABA 020#99n;
|FPRC;

|DEFBUCLE PasoAuxW039;
     #2#3;
     0;
     #10#0, #0#2, 000000, 0001, #0#7;
     #10#0, #0#3, 999999, 9999, #0#8;
     e;
     NULL, PasoAuxW039;
|FIN;

|PROCESO PasoAuxiliar;
     eaCuenta = #10#0;
     |SI nCambio ! 0;
         eaCta08 = eaCuenta;
         eaCta90 = eaCuenta;
         enBEjer = enEjercicio;
         |HAZ eCam_Segundo;
         |SI nEjer ] 2008;
             eaCuenta = eaCta08;
         |SINO;
             eaCuenta = eaCta90;
         |FINSI;
     |FINSI;
     |SI eaCuenta < #0#0; |O eaCuenta > #0#1;
         |ACABA; ||no entra en la seleccion
     |FINSI;

     nBanco = 0;
     |DDEFECTO #15;
     #15#6 = eaCuenta;
     |LEE 040#15=;
     |SI FSalida = 0;
         nBanco = #15#0;
     |FINSI;
     |SI nBanco < #0#5; |O nBanco > #0#6;
         |ACABA;        || ... No esta dentro de la Seleccion de bancos
     |FINSI;

     nEstado = 0;
     #98#0 = eaCuenta;
     #98#1 = #10#1;
     #98#2 = #10#2;
     #98#3 = #10#5;
     |LEE 041#98=;
     |SI FSalida ! 0;
         |DDEFECTO #98;
         #98#0 = eaCuenta;
         #98#1 = #10#1;
         #98#2 = #10#2;
         #98#3 = #10#5;
         nEstado = 1;
     |FINSI;

     nHay = 0;
     |BUCLE PasoAuxW039;
     |SI nHay = 1;
         |SI nEstado = 1; |GRABA 040#98n; |FINSI;
         |SI nEstado = 0; |GRABA 040#98a; |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE PasoAuxiliar;
     #10#1;
     3;
     aDCuenta, "S";
     aHCuenta, "S";
     ;
     NULL, PasoAuxiliar;
|FIN;

|PROCESO HazAuxiliar;
    nCambio = 0;
    |SI enEjercicio < 2008; |Y nEjer ] 2008; nCambio = 1; |FINSI;
    |SI enEjercicio ] 2008; |Y nEjer < 2008; nCambio = 1; |FINSI;

    aDCuenta = #0#0; aHCuenta = #0#1;
    |SI nCambio ! 0; aDCuenta = ""; aHCuenta = "zzzzzzzzzzzzz"; |FINSI;
    aDMarca  = " "; aHMarca = "û";

     |ABRE #15;
     |SELECT #2#15;
     |BUCLE PasoAuxiliar;
     |CIERRA #15;
     |CIERRA #1;
|FPRC;

|PROCESO MultiEmpM1;

    |HAZ CalFechas;
    |SI #0#2 > fec2; |O #0#3 < fec1;
        |ACABA;  || Fuera de la seleccion
    |FINSI;

    nSitua = 1;
    |DBASE dir_fich; |QBF dir_fich;
    dir_fich = dir_fich + "fich/";
    alfa1 = #30#2; |QBF alfa1;
    alfa2 = #30#3; |QBF alfa2;
    alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
    alfa2 = "0"     + alfa2;   alfa2 = alfa2 % -101;
    dirfich0 = dir_fich + alfa1 + alfa2 + "/";
    |QBF dirfich0;
    nPeriodo = #30#3;

    |PATH_DAT #1 dirfich0;
    |PATH_DAT #2 dirfich0;
    |PATH_DAT #4 dirfich0;
    |PATH_DAT #5 dirfich0;
    |PATH_DAT #10 dirfich0;
    |PATH_DAT #11 dirfich0;
    ||... |PATH_DAT #15 dirfich0;  ||solo saco los de este periodo
    |HAZ HazAuxiliar;
|FPRC;

|DEFBUCLE MultiEmpM0;
    #30#1;
    ;
    enEmpresa, 0;
    enEmpresa, 9;
    ;
    NULL,MultiEmpM1;
|FIN;

|PROCESO MultiEjercicio;
   nEjer = enEjercicio;

   |DELINDEX #98;
   |DELINDEX #99;

   |ABRE #99;
   |ABRE #98;
   |BUCLE MultiEmpM0;
   |CIERRA #98;
   |CIERRA #99;

   #caselem1#1 = CodigoEmpresa;
   #caselem1#2 = PeriodoEmpresa;
   #caselem1#3 = dirfichOri;
   |HAZ MultiEmp1;
|FPRC;
|| *******************************************************************
|PROCESO CalFechas;
   |SI #30#26 < 80;
       enEjercicio = #30#26 + 2000;
   |SINO;
       enEjercicio = #30#26 + 1900;
   |FINSI;

   cod2 = #30#26;
   tra1 = 100 + cod2;
   clave2 = tra1 % -102;
   tra1 = 100 + #30#11;
   mes1 = tra1 % -102;
   |SI cod2 < 80;
      work1 = "01." + mes1 + ".20" + clave2;
   |SINO;
      work1 = "01." + mes1 + ".19" + clave2;
   |FINSI;
   fec1 = work1;
   tra1 = 100 + #30#12;
   mes1 = tra1 % -102;
   |SI #30#11 > #30#12;
      cod2 = cod2 + 1;
      |SI cod2 > 99; cod2 = cod2 - 100;    |FINSI;
   |FINSI;
   dia1 = "31";
   |SI #30#12 = 4; |O #30#12 = 6; |O #30#12 = 9; |O #30#12 = 11;
      dia1 = "30";
   |FINSI;
   |SI #30#12 = 2;
      dia1 = "28";
      |SI cod2 = 88; |O cod2 = 92; |O cod2 = 96; |O cod2 = 0; |O cod2 = 4; |O cod2 = 8;
          |O cod2 = 12; |O cod2 = 16; |O cod2 = 20; |O cod2 = 24;
         dia1 = "29";
      |FINSI;
   |FINSI;
   tra1 = 100 + cod2;
   clave2 = tra1 % -102;
   |SI cod2 < 80;
      work1 = dia1 + "." + mes1 + ".20" + clave2;
   |SINO;
      work1 = dia1 + "." + mes1 + ".19" + clave2;
   |FINSI;
   fec2 = work1;
|FPRC;
|| *******************************************************************
