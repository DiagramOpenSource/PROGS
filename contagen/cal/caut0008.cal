|FICHEROS;
 caut0008 #0;

 cafinanc #1;
 caarm001 #11;
 :/basica/def/agicen17 #17;
 :/modelos/def/dsmom041 #41;
 :/modelos/def/dsmom042 #42;
 :/modelos/def/dsmom046 #46;

 canempre #30;
 caselem1 #998;
|FIN;

|VARIABLES;
 nInkey   = 0;

 swHago = 0;
 aAlfa1 = "";
 aAlfa2 = "";
 nNume1 = 0;
 nNume2 = 0;
 nPara1 = 0;
 nPara2 = 0;

 nContDe = 0;

 SwCambia = 0;
 aElDepar = "";
 &snFilBa = "";
 aParam = "";
 aDepCod = "";
 aDepNom = "";
 aDTipo  = "";
 aHTipo  = "";
|FIN;

|INCLUYE dscont_i;

|| **************************************

|CALCULO Defectos; |TIPO 7;
 |SI Haymodelo = 0;
     #0#0 = "N"; |PINTA #0#0; |C_M #0#0, 1, "N";
 |FINSI;
|FCAL;

|CALCULO QueHago;
  aAlfa1 = #0Campo;
  |SI Campo = 0;
      |C_M #0#5, 1, aAlfa1;
      |C_M #0#6, 1, aAlfa1;
      |C_M #0#7, 1, aAlfa1;
      |C_M #0#8, 1, aAlfa1;
  |FINSI;
  |SI Campo = 15;
      |C_M #0#1, 1, aAlfa1;
      |C_M #0#2, 1, aAlfa1;
      |C_M #0#3, 1, aAlfa1;
      |C_M #0#4, 1, aAlfa1;
  |FINSI;
  |SI Campo = 16;
      |C_M #0#17, 1, aAlfa1;
      |C_M #0#18, 1, aAlfa1;
      |C_M #0#19, 1, aAlfa1;
  |FINSI;
|FCAL;

|CALCULO CambioDepar; |TIPO 0;
  aAlfa1 = "S";
  |SI #0#9 = "D";
      aAlfa1 = "N";
      #0#12 = eaDfDepC; |PINTA #0#12;
      #0#14 = eaDfDepN; |PINTA #0#14;
  |FINSI;
  |C_M #0#12, 1, aAlfa1;
|FCAL;

|| **********************************************************************
|CALCULO  LaAct; |TIPO 0;
  |C_M #0#12,0,aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

  eOpDep = 0; || 1 = Consulta pantalla
  |SI FSalida = 10; eOpDep = 1;  |FINSI;
  enActividad = #0#12;
  eaCodDep    = #0#12;
  |HAZ Actividad;
  |SI eswLect = 1;
      |CONFI "    NError.! No Existe Actividad / Departamento";
      |SI FSalida ! 0;
          |ERROR;
      |FINSI;
  |SINO;
      aAlfa1 = enActividad;
      |SI Haybasica = 1;
          aAlfa1 = ("00" + aAlfa1) % A02;
          eaCodDep = aAlfa1;
      |FINSI;
      #0#12 = eaCodDep; |PINTA #0#12;
      |SI efActFin > "01.01.1900"; |Y snFilBa = "S";
          aAlfa1 = "    NAtencion!!!. Actividad con Fecha de BAJA " + efActFin + ", Desea introducir Facturas ? ";
          |CONFI aAlfa1;
          |SI FSalida ! 0;
              |ERROR;
              |ACABA;
          |FINSI;
      |FINSI;
      |SI eaEsCom = "S";
          |CONFI "    NAtencion!!!. Actividad Comunera, ¨ Desea aceptar ? ";
          |SI FSalida ! 0;
              |ERROR;
              |ACABA;
          |FINSI;
      |FINSI;
   |FINSI;
   #0#14 = eaNombreAct; |PINTA #0#14;
|FCAL;


|| **********************************************************************
|CALCULO ElCambio; |TIPO 12;
  nInkey = FSalida;
  |SI nInkey = 7; |O #0#13 = "N"; |O aParam ! "";
      |ACABA;
  |FINSI;
  |SI #0#0 = "N"; |Y #0#15 = "N"; |Y #0#16 = "N";
      |MENAV "0000No seleccionado ningun Cambio";
      |ACABA;
  |FINSI;

  |SI #48#27 = "S"; |Y #0#9 = "D";
      |HAZ MultiEmpresa;
  |SINO;
      |DFICE dirfich0; |QBF dirfich0;
      |HAZ HazTodo;
  |FINSI;
|FCAL;

|| //////////////////////////////////////////////////////
|PROCESO LasLineas;
 #42#1 = #41#1;
 |GRABA 020#42a;
 |LIBERA #42;
|FPRC;

|DEFBUCLE LasLineas;
 #42#1;
 ;
 #41#0, #41#2, #41#3, 001;
 #41#0, #41#2, #41#3, 999;
 be;
 NULL, LasLineas;
|FIN;

|PROCESO Amortizacion0;
 #46#0 = #41#0;
 #46#2 = #41#2;
 #46#3 = #41#3;
 #46#1 = 00;
 #46#6 = "   ";
 |LEE 001#46];
 |SI FSalida = 0; |Y #46#0 = #41#0; |Y #46#2 = #41#2; |Y #46#3 = #41#3;
     |ACABA;   || es un reparto de actividades
 |FINSI;

 #41#1  = aDepCod;
 #41#33 = aDepNom;
 |GRABA 020#41a;
 |LIBERA #41;

 |BUCLE LasLineas;

 |SI #41#36 ! 0; |Y #0#0 = "A";
     |ABRE #1;
     #1#0 = enEmpresa;
     #1#1 = #41#36;
     |LEE 101#1=;
     |SI FSalida = 0;
          #1#33 = aDepCod;
          #1#34 = aDepNom;
          |GRABA 020#1a;
     |FINSI;
     |LIBERA #1;
     |CIERRA #1;
 |FINSI;
|FPRC;

|DEFBUCLE Amortizacion;
 #41#1;
 1;
 enEmpresa, #0#5, #0#7, #0#10;
 enEmpresa, #0#6, #0#8, #0#11;
 be;
 NULL, Amortizacion0;
|FIN;

|PROCESO Financieras0;
 #1#33 = aDepCod;
 #1#34 = aDepNom;
 |GRABA 020#1a;
 |LIBERA #1;

 |SI #1#44 ! "        "; |Y #0#0 = "F";
     |ABRE #41;
     #41#0 = enEmpresa;
     #41#2 = #1#44;
     #41#3 = #1#45;
     |LEE 101#41=;
     |SI FSalida = 0; |Y #41#36 = #1#1;
         #41#1  = aDepCod;
         #41#33 = aDepNom;
         |GRABA 020#41a;
         |BUCLE LasLineas;
     |FINSI;
     |LIBERA #41;
     |CIERRA #41;
 |FINSI;
|FPRC;

|DEFBUCLE Financieras;
 #1#1;
 3,33;
 enEmpresa, #0#1, #0#3, #0#10;
 enEmpresa, #0#2, #0#4, #0#11;
 be;
 NULL, Financieras0;
|FIN;

|PROCESO Contratos0;

 #11#11 = aDepCod;
 #11#24 = aDepNom;
 |SI #11#12 ! "   ";
     |DDEFECTO #17;
     #17#0 = #11#0;
     #17#1 = #11#11;
     #17#2 = #11#12;
     |LEE 041#17=;
     #11#25 = #17#3;
 |FINSI;
 |GRABA 020#11a;
 |LIBERA #11;
|FPRC;

|DEFBUCLE Contratos;
 #11#1;
 4;
 enEmpresa, #0#17, aDTipo;
 enEmpresa, #0#18, aHTipo;
 be;
 NULL, Contratos0;
|FIN;

|PROCESO HazTodo;
 |SI #0#9 = "D";
     aDepCod = eaDfDepC;
     aDepNom = eaDfDepN;
 |SINO;
     aDepCod = #0#12;
     aDepNom = #0#14;
 |FINSI;

 |SI #0#0 = "S";
     |ABRE #46;
     |BUCLE Amortizacion;
     |CIERRA #46;
 |FINSI;

 |SI #0#15 = "S";
     |BUCLE Financieras;
 |FINSI;

 |SI #0#16 = "S";
     aDTipo = #0#19;
     aHTipo = #0#19;
     |SI #0#19 = "A";
         aDTipo = " ";
         aHTipo = "z";
     |FINSI;
     |BUCLE Contratos;
 |FINSI;
|FPRC;
|| **************************************************************************

|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |ATRI N; |CUADRO 1508 1850;
   |ATRI R; |PINTA 1609, "Empresa: ";      |ATRI 0;
   |PINTA 1618, #30#2; |PINTA 1709, #30#1; |ATRI 0;
   |PINTA 1910, "                                                      ";
   |PINTA 2010, "                                                      ";

   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 000001;
 999999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;


|PROGRAMA;
   aParam = PARAMETRO; |QBF aParam;

   |SI aParam = "";
       |HAZ inicio;
       |SI eaDepar = "N";
           |MENAV "    Esta Empresa No tiene Departamento ";
           |ACABA;
       |FINSI;
   |FINSI;

   |HAZ DirAplicSt;
   |SI Haymodelo = 0;
       |MENAV "    Error.! No Existe Aplicacion MODELOS.";
   |SINO;
      aAlfa1 = aPModelo + "fich/";
      |PATH_DAT #dsmom041 #aAlfa1#;
      |PATH_DAT #dsmom042 #aAlfa1#;
  |FINSI;

  |SI aParam ! "";
      |CLS;
      |PINPA #0#0;
      #0#9 = "D"; |C_M #0#9,1,"N";
      #0#12 = eaDfDepC;
      #0#13 = "S";
      #0#14 = eaDfDepN;
      |PINDA #0#0;
      |ENDATOS #1#0;
      |SI FSalida = 0; |Y #0#13 = "S";
          |HAZ HazTodo;
      |FINSI;
  |SINO;
      |CLS;
      |MANTE #0;
  |FINSI;
|FPRO;
