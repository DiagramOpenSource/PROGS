|FICHEROS;
   camautoc #1;
   camautol #0;
   cacuenta #2;

   :/modelos/def/dsmom030 #130;
   camautti #4;
   calibros #10;
   caivaasi #200;

   :/basica/def/agitab99 #199;
   caw00098 #901,MANTE;
|FIN;

|VARIABLES;
   caant = "";
   alfa1 = "";
   &entrada  = 1;
   nSwCopiar = 0;
   &enNFijo  = 0;
   &enNFijE  = 0;
   aAlfa    = "";
   aAlfa1   = "";
   aAlfa2   = "";
   aAlfa3   = "";

   nNume1 = 0;
   nNume2 = 0;
   nNume3 = 0;

   cuant = "";
   diant = 0;
   coant = 0;
   deant = "";
   dhant = "";
   imant = 0;
   dpant = "";
   sdant = "";
   tfant = 0;
   modo  = 0;
   modoL = 0;
   &tex  = "";
   &tex2 = "";
   &swinf  = 0;
   swlin = 0;

   &eaPath    = "";
   &eaRetorno = "";
   sw         = 0;
   swTipCta   = 0;
   swModelo   = 0;
   &enModelo  = 0;
   &enSwConta = 0;  || 1= Para dar de alta codigos

   nUltCod    = 0;
   nInkey     = 0;
   nCasilla   = 0;
   &eaTabMod  = "";
   &enwModAu  = 0;

   &dLibro = 0;
   &dSerie = "";
   &dInver = "";
   &dDeduc = "";
   &enQSerie = 0;

   nErrCta  = 0;
   aMensa   = "";

   {40}aPopup        = "";
      nID            = 0;
      aID            = "";
      aTexto         = "";
   &enQueSoc  = 0;
   &snFiltr   = "";
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoan_i;
|INCLUYE cla340_i;

|| ********************************************************************
|CALCULO Atipo8; |TIPO 8;
  |HAZ inicio;
  |HAZ DirAplicSt;

  |ABRE #200;
  |DDEFECTO #200;
  |LEE 000#200p;
  enQSerie = 0; |SI #200 = "S"; enQSerie = 1; |FINSI;
  eaSobreEs = #200#155;
  |CIERRA #200;
  enQueSoc = enEjercicio - 1;
  |SI Haybasica = 1;
      enwModAu  = 0;
      |HAZ CargaAuxModelo;
      |NOME_DAT #901 eaTabMod;
  |FINSI;

  |HAZ Analitica;
  |SI enSwAna = 0;
      |C_M #0#14,1,"N";
      |C_V #0#14,1,"N";
      |C_A #0#5,1,24;
  |FINSI;
|FCAL;

|CALCULO ATipo9; |TIPO 9;
  |SI Haybasica = 1; |DELINDEX #901; |FINSI;
|FCAL;

|CALCULO mirosiexiste; |TIPO 7;
|FCAL;

|CALCULO ElTipo30; |TIPO 30;
 |SI nSwCopiar = 1;
     |PUSHV 0501 1180;
     enNFijo = #1#0;
     |SUB_EJECUTA_NP "caut0004";
 |FINSI;
 nSwCopiar = 0;
 |PINTA 0574,"ÄÄÄÄÄÄ¿";
|FCAL;

|CALCULO ParaCopiar; |TIPO 0;
 modo = (FEntrada / 100) ! 0;
 |SI modo = 2; |HAZ VerMod303; |FINSI;

 |SI FSalida = 10;
     nSwCopiar = 1;
     |ATRI P; |PINTA 0574, "COPIAR"; |ATRI 0;
 |FINSI;
|FCAL;

|CALCULO alta; |TIPO 7;
   modo = (FEntrada / 100) ! 0;
   nNume1 = 0;
   |SI modo = 1;
       |DDEFECTO #1;
       |LEE 001#1u;
       |SI FSalida = 0;
           nNume1 = #1#0;
       |FINSI;
       |DDEFECTO #1;
       #1#0 = nNume1 + 1;
       |PINTA #1#0;
   |FINSI;
   cuant = "";
   diant = 0;
   coant = 0;
   deant = "";
   dhant = "";
   imant = 0;
   dpant = eaDfDepC;
   sdant = eaDfSubC;
   tfant = 0;
|FCAL;

|CALCULO ElTipo0; |TIPO 0;
 modo = (FEntrada / 100) ! 0;
 |SI modo = 1; |O #1#12 = "N"; |O #1#14 ! "F";
     |C_V #1#6, 1,"N"; |PINTA 1341, "       ";
 |SINO;
     |C_V #1#6, 1,"S";
 |FINSI;
|FCAL;

|CALCULO prifecha; |TIPO 0;
 |C_M #1Campo,0,aAlfa1;
 |SI aAlfa1 = "N"; |ACABA; |FINSI;

 aAlfa1 = #1Campo;
 |HAZ com_fecha;
 |SI swerror = 1;
     |MENAV "    Fecha Erronea ";
     |ERROR;
 |FINSI;
|FCAL;

|PROCESO com_fecha;
 swerror = 0;
 aAlfa1 = ("0000" + aAlfa1) % -104;
 aAlfa2 = aAlfa1 % 102;
 aAlfa3 = aAlfa1 % 302;
 |SI aAlfa3 > "12"; swerror = 1; |ACABA; |FINSI;
 |SI aAlfa2 > "31"; swerror = 1; |ACABA; |FINSI;
 |SI aAlfa2 < "29"; |ACABA; |FINSI;
 |SI aAlfa3 = "02"; swerror = 1; |ACABA; |FINSI;
 |SI aAlfa2 [ "30"; |ACABA; |FINSI;
 |SI aAlfa3 = "04"; |O aAlfa3 = "06"; |O aAlfa3 = "09"; |O aAlfa3 = "11";
     swerror = 1;
 |FINSI;
|FPRC;

|CALCULO financiacion; |TIPO 0;
 aAlfa1 = #1#12;
 |C_M #1#14, 1,aAlfa1;
 |C_M #1#6,  1,aAlfa1;
 |SI aAlfa1 = "N";
     #1#14 = "P"; |PINTA #1#14;
     |C_V #1#6, 1,"N"; |PINTA 1341, "       ";
     #1#6  = "0101";
 |SINO;
     |C_V #1#6, 1,"S"; |PINTA #1#6;
 |FINSI;

 |SI #1#12 = "N"; |Y #1#24 = "N";
     |SI #1#22 = "R"; #1#22 = "S"; |PINTA #1#22; |FINSI;
     |SI #1#23 = "R"; #1#23 = "S"; |PINTA #1#23; |FINSI;
 |FINSI;
|FCAL;

|CALCULO fechatipo; |TIPO 7;
  |C_M #1#14,0,aAlfa1; |SI aAlfa1 = "N"; |ACABA; |FINSI;
  |PUSHV 0706 1537;
  |CUADRO 07061537;
  |ATRI R;
  |PINTA 0807, "ANUAL/Un asiento por Operacion";
  |PINTA 0907, " C => Fecha del Contrato      ";
  |PINTA 1007, " P => Introducida en Pantalla ";
  |PINTA 1107, " F => Fecha fija              ";
  |PINTA 1207, "------------------------------";
  |PINTA 1307, "MENSUAL/Asiento por Lineas Op.";
  |PINTA 1407, " L => Fecha de la Linea       ";
  |ATRI 0;
|FCAL;

|CALCULO tipofecha; |TIPO 0;
 |C_M #1#14,0,aAlfa1; |SI aAlfa1 = "N"; |ACABA; |FINSI;
 |POPV;
 |SI #1#14 = "F";
    |C_V #1#6, 1,"S";
    |C_M #1#6, 1,"S"; |PINTA #1#6;
 |SINO;
    |C_V #1#6, 1,"N"; |PINTA 1341, "       ";
    |C_M #1#6, 1,"N";
 |FINSI;
|FCAL;

|CALCULO CreaIVA; |TIPO 0;
 aAlfa1 = #1#13;
 |C_M #1#16,1,aAlfa1;
 |C_M #1#17,1,aAlfa1;
 |C_M #1#18,1,aAlfa1;
 |C_M #1#19,1,aAlfa1;
 |C_M #1#20,1,aAlfa1;
 |C_M #1#27,1,aAlfa1;
 |SI aAlfa1 = "N";
     |C_M #1#24, 1, "S";
     |C_M #1#21, 1, "S";
     #1#16 = ""; |PINTA #1#16;
     #1#17 = 0;  |PINTA #1#17;
     #1#18 = ""; |PINTA #1#18;
     #1#19 = "N"; |PINTA #1#19;
     #1#20 = "N"; |PINTA #1#20;
     #1#27 = " "; |PINTA #1#27;
 |SINO;
     |C_M #1#24,1,"N"; #1#24 = "N"; |PINTA #1#24;
     |C_M #1#25,1,"N";
     |C_M #1#21,1,"N"; #1#21 = 0; |PINTA #1#21;
     aAlfa1 = " " * 42; |PINTA 1228, aAlfa1;
 |FINSI;
 |SI enQSerie  = 0; |C_M #1#18,1,"N"; |FINSI;
 |SI Haybasica = 0; |C_M #1#21,1,"N"; |FINSI;
|FCAL;

|CALCULO TipoIVA; |TIPO 0;
 |C_M #1#16,0,aAlfa1;
 |SI aAlfa1 = "N"; |ACABA; |FINSI;

 |SI #1#16 = " "; |ERROR; |ACABA; |FINSI;
 modo = (FEntrada / 100) ! 0;
 |SI modo = 1; |O #1#16 ! Contenido;
     aTipoIVA  = #1#16;
     |HAZ LeeDefIVA;
     #1#17 = dLibro;  |PINTA #1#17;
     #1#18 = dSerie;  |PINTA #1#18;
     #1#19 = dInver;  |PINTA #1#19;
     #1#20 = dDeduc;  |PINTA #1#20;
 |FINSI;
 |C_M #1#27,1,"S";
 |SI #1#16 = "N";
     |C_M #1#27,1,"N";
      #1#27 = ""; |PINTA #1#27;
 |FINSI;
|FCAL;

|CALCULO cheqLibro; |TIPO 0;
   |SI #1#13 = "N"; |ACABA; |FINSI;

   |ABRE #10;
   #10#0 = #1#17;
   |LEE 001#10=;
   |SI FSalida ! 0;  |DDEFECTO #10;  |FINSI;
   |CIERRA #10;
   |SI #10#2 ! #1#16;
      |MENAV "    ATENCION!!! Libro de I.V.A. no CORRECTO ";
      |ERROR;
   |FINSI;
   |C_M #1#27,1,"S";
   |SI #10#6 ! " ";
        #1#27 = #10#6; |PINTA #1#27;
        |C_M #1#27,1,"N";
   |FINSI;
|FCAL;

|CALCULO LeoIVA; |TIPO 0;
 |C_M #1#24,0,aAlfa1;
 |SI aAlfa1 = "N"; |ACABA; |FINSI;

 aAlfa1 = #1#24; |C_M #1#25, 1, aAlfa1;
 |SI #1#24 = "N";
     aAlfa1 = "S";
 |SINO;
     #1#21 = 0;   |PINTA #1#21;
     aAlfa1 = " " * 42; |PINTA 1228, aAlfa1;
     #1#12 = "N"; |PINTA #1#12;
     #1#14 = "P"; |PINTA #1#14;
     #1#6 = "0101";
     aAlfa1 = "N";
 |FINSI;
 |C_M #1#21,1,aAlfa1;
 |C_M #1#12,1,aAlfa1;
 |C_M #1#14,1,aAlfa1;
 |C_M #1#6 ,1,aAlfa1;
 |SI Haybasica = 0; |C_M #1#21,1,"N"; |FINSI;
|FCAL;

|PROCESO Tipo7C27;  |TIPO 7;
   |SI enEjercicio > 2011;
       aAlfa1 = "<*> No se pasa al 340.";
   |SINO;
       aAlfa1 = "<X> No se pasa al 340.";
   |FINSI;
   aAlfa1 = "    <F11> para consultar Tipo Operaciones Mod. 340." + aAlfa1;
   |MENSA aAlfa1;
|FPRC;

|PROCESO Tipo66C27;  |TIPO 66;
    |C_M #1#27,0,aAlfa1;
    |SI aAlfa1 = "N"; |ACABA; |FINSI;

    |SI #1#25 ! "S"; |Y #1#25 ! "R"; |ACABA; |FINSI;

     aAlfa = "|C027WID";
     aID   = #1#aAlfa#;
     nID   = aID;

     aPopup1  = nID;  || Si fuera -1 en la posicion
     |SI #1#25 = "S";
         |SI enEjercicio [ 2011;
             aPopup2  = 14;
         |SINO;
             aPopup2  = 18;
             |SI enEjercicio ] 2014;
                 aPopup2  = 27;
             |FINSI;
         |FINSI;
         aPopup3  = "  Operación habitual";
         aPopup4  = "A Asiento resumen de facturas";
         aPopup5  = "B Asiento resumen de tiques";
         aPopup6  = "C Factura con varios asientos (varios tipo impositivos)";
         aPopup7  = "D Factura rectificativa";
         aPopup8  = "F Adquisiciones realizadas por las agencias de viajes directamente en interés del viajero ";
         aPopup9  = "G Régimen especial de grupo de entidades en IVA o IGIC";
         aPopup10 = "H Régimen especial de oro de inversión";
         aPopup11 = "I Inversión del Sujeto pasivo (ISP)";
         aPopup12 = "J Tiques";
         aPopup13 = "K Rectificación anotaciones registrales";
         aPopup14 = "L Adquisiciones a comerciantes minoristas del IGIC";
         aPopup15 = "P Adquisiciones intracomunitarias de bienes.";
         aPopup16 = "Q Operaciones a las que se aplique el régimen especial de bienes usados, ...";
         |SI enEjercicio > 2011;
             aPopup17 = "R Operación de arrendamiento de local de negocio.";
             aPopup18 = "S Subvenciones, auxilios o ayudas satisfechas o recibidas, tanto por parte de Administraciones públicas o entidades privadas.";
             aPopup19 = "W Operaciones sujetas al Impuesto sobre la Producción, los Servicios y la Importación en las Ciudades de Ceuta y Melilla.";
             aPopup20 = "X Operaciones por las que los empresarios o profesionales que satisfagan compensaciones agrícolas hayan expedido el recibo";
             |SI enEjercicio ] 2014;
                 aPopup21 = "Z Operaciones en Régimen especial de criterio de caja";
                 aPopup22 = "1 IVA criterio de caja. Asiento resumen de facturas";
                 aPopup23 = "2 IVA criterio de caja. Factura con varios asientos";
                 aPopup24 = "3 IVA criterio de caja. Factura rectificativa";
                 aPopup25 = "4 IVA criterio de caja. Adqusiciones realizadas por las agencias de viajes en interés del viajero";
                 aPopup26 = "5 IVA criterio de caja. Factura simplificada";
                 aPopup27 = "6 IVA criterio de caja. Rectificación de errores registrales";
                 aPopup28 = "7 IVA criterio de caja. Facturación de las prestaciones de servicios de agencias de viajes";
                 aPopup29 = "8 IVA criterio de caja. Operación de arrendamiento de local de negocio";
             |FINSI;
         |FINSI;
     |SINO;
         aPopup1  = nID;  || Si fuera -1 en la posicion
         |SI enEjercicio [ 2011;
             aPopup2  = 15;
         |SINO;
             aPopup2  = 21;
             |SI enEjercicio ] 2014;
                 aPopup2  = 30;
             |FINSI;
         |FINSI;
         aPopup3  = "  Operación habitual";
         aPopup4  = "A Asiento resumen de facturas";
         aPopup5  = "B Asiento resumen de tiques";
         aPopup6  = "C Factura con varios asientos (varios tipo impositivos)";
         aPopup7  = "D Factura rectificativa";
         aPopup8  = "E IVA devengado pendiente de emitir factura";
         aPopup9  = "G Régimen especial de grupo de entidades en IVA o IGIC";
         aPopup10 = "H Régimen especial de oro de inversión";
         aPopup11 = "I Inversión del Sujeto pasivo (ISP)";
         aPopup12 = "J Tiques";
         aPopup13 = "K Rectificación anotaciones registrales";
         aPopup14 = "M IVA facturado pendiente de devengar (emitida factura)";
         aPopup15 = "N Facturación de las prestaciones de servicios de agencias de viaje que actúan como mediadoras en nombre y por cuenta ajena";
         aPopup16 = "O Factura emitida en sustitución de tiques facturados y declarados.";
         aPopup17 = "Q Operaciones a las que se aplique el régimen especial de bienes usados, ...";
         |SI enEjercicio > 2011;
             aPopup18 = "R Operación de arrendamiento de local de negocio.";
             aPopup19 = "S Subvenciones, auxilios o ayudas satisfechas o recibidas, tanto por parte de Administraciones públicas o entidades privadas.";
             aPopup20 = "T Cobros por cuenta de terceros de honorarios profesionales o de derechos derivados ...";
             aPopup21 = "U Operación de seguros.";
             aPopup22 = "V Compras de Agencias viajes: operaciones de prestación de servicios de mediación en nombre y por cuenta ajena relativos ...";
             aPopup23 = "W Operaciones sujetas al Impuesto sobre la Producción, los Servicios y la Importación en las Ciudades de Ceuta y Melilla.";
             |SI enEjercicio ] 2014;
                 aPopup24 = "Z Operaciones en Régimen especial de criterio de caja";
                 aPopup25 = "1 IVA criterio de caja. Asiento resumen de facturas";
                 aPopup26 = "2 IVA criterio de caja. Factura con varios asientos";
                 aPopup27 = "3 IVA criterio de caja. Factura rectificativa";
                 aPopup28 = "4 IVA criterio de caja. Adqusiciones realizadas por las agencias de viajes en interés del viajero";
                 aPopup29 = "5 IVA criterio de caja. Factura simplificada";
                 aPopup30 = "6 IVA criterio de caja. Rectificación de errores registrales";
                 aPopup31 = "7 IVA criterio de caja. Facturación de las prestaciones de servicios de agencias de viajes";
                 aPopup32 = "8 IVA criterio de caja. Operación de arrendamiento de local de negocio";
             |FINSI;
         |FINSI;
     |FINSI;

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
         IaPopup  = aPopup3  <-;
         IaPopup  = IaPopup + (FSalida - 1);

         aAlfa = aPopup % 101;  #1#27 = aAlfa;
     |FINSI;

     |PINTA #1#27;
|FPRC;

|PROCESO Tipo0C27;  |TIPO 0;
     |BLIN 4;
     aAlfa = "";
     |SI #1#25 ! "R"; |Y #1#25 ! "S"; |ACABA; |FINSI;

     |SI #1#27 = " "; aAlfa = "Operaci¢n habitual"; |FINSI;
     |SI #1#27 = "A"; aAlfa = "Asiento resumen de facturas"; |FINSI;
     |SI #1#27 = "B"; aAlfa = "Asiento resumen de tiques"; |FINSI;
     |SI #1#25 = "R";
         |SI #1#27 = "C"; aAlfa = "Factura con varios asientos (varios tipo impositivos)"; |FINSI;
         |SI #1#27 = "D"; aAlfa = "Factura rectificativa"; |FINSI;
         |SI #1#27 = "E"; aAlfa = "IVA devengado pendiente de emitir factura"; |FINSI;
         |SI #1#27 = "G"; aAlfa = "R‚gimen especial de grupo de entidades en IVA o IGIC"; |FINSI;
         |SI #1#27 = "H"; aAlfa = "R‚gimen especial de oro de inversi¢n"; |FINSI;
         |SI #1#27 = "I"; aAlfa = "Inversi¢n del Sujeto pasivo (ISP)";|FINSI;
         |SI #1#27 = "J"; aAlfa = "Tiques"; |FINSI;
         |SI #1#27 = "K"; aAlfa = "Rectificación anotaciones registrales"; |FINSI;
         |SI #1#27 = "M"; aAlfa = "IVA facturado pendiente de devengar (emitida factura)"; |FINSI;
         |SI #1#27 = "N"; aAlfa = "Facturaci¢n de las prestaciones de servicios de agencias de viaje que actúan como mediadoras en nombre y por cuenta ajena"; |FINSI;
         |SI #1#27 = "O"; aAlfa = "Factura emitida en sustituci¢n de tiques facturados y declarados."; |FINSI;
         |SI #1#27 = "Q"; aAlfa = "Operaciones a las que se aplique el r‚gimen especial de bienes usados, ..."; |FINSI;
         |SI enEjercicio > 2011;
             |SI #1#27 = "R"; aTexto = "Operaci¢n de arrendamiento de local de negocio."; |FINSI;
             |SI #1#27 = "S"; aTexto = "Subvenciones, auxilios o ayudas satisfechas o recibidas, tanto por parte de Administraciones públicas o entidades privadas."; |FINSI;
             |SI #1#27 = "T"; aTexto = "Cobros por cuenta de terceros de honorarios profesionales o de derechos derivados ..."; |FINSI;
             |SI #1#27 = "U"; aTexto = "Operación de seguros."; |FINSI;
             |SI #1#27 = "V"; aTexto = "Compras de Agencias viajes: operaciones de prestación de servicios de mediación en nombre y por cuenta ajena relativos ..."; |FINSI;
             |SI #1#27 = "W"; aTexto = "Operaciones sujetas al Impuesto sobre la Producción, los Servicios y la Importación en las Ciudades de Ceuta y Melilla."; |FINSI;
             |SI enEjercicio ] 2014;
                 |SI #1#27 = "Z"; aTexto = "Operaciones en Régimen especial de criterio de caja"; |FINSI;
                 |SI #1#27 = "1"; aTexto = "IVA criterio de caja. Asiento resumen de facturas"; |FINSI;
                 |SI #1#27 = "2"; aTexto = "IVA criterio de caja. Factura con varios asientos"; |FINSI;
                 |SI #1#27 = "3"; aTexto = "IVA criterio de caja. Factura rectificativa"; |FINSI;
                 |SI #1#27 = "4"; aTexto = "IVA criterio de caja. Adqusiciones realizadas por las agencias de viajes"; |FINSI;
                 |SI #1#27 = "5"; aTexto = "IVA criterio de caja. Factura simplificada"; |FINSI;
                 |SI #1#27 = "6"; aTexto = "IVA criterio de caja. Rectificaci¢n de errores registrales"; |FINSI;
                 |SI #1#27 = "7"; aTexto = "IVA criterio de caja. Facturaci¢n de las prestaciones servicios agencias viajes"; |FINSI;
                 |SI #1#27 = "8"; aTexto = "IVA criterio de caja. Operaci¢n de arrendamiento de local de negocio"; |FINSI;
             |FINSI;
         |FINSI;
     |SINO;
         |SI #1#27 = "C"; aAlfa = "Factura con varios asientos (varios tipo impositivos)"; |FINSI;
         |SI #1#27 = "D"; aAlfa = "Factura rectificativa";|FINSI;
         |SI #1#27 = "F"; aAlfa = "Adquisiciones realizadas por las agencias de viajes directamente en inter‚s del viajero";|FINSI;
         |SI #1#27 = "G"; aAlfa = "R‚gimen especial de grupo de entidades en IVA o IGIC"; |FINSI;
         |SI #1#27 = "H"; aAlfa = "R‚gimen especial de oro de inversi¢n"; |FINSI;
         |SI #1#27 = "I"; aAlfa = "Inversi¢n del Sujeto pasivo (ISP)"; |FINSI;
         |SI #1#27 = "J"; aAlfa = "Tiques"; |FINSI;
         |SI #1#27 = "K"; aAlfa = "Rectificación anotaciones registrales"; |FINSI;
         |SI #1#27 = "L"; aAlfa = "Adquisiciones a comerciantes minoristas del IGIC"; |FINSI;
         |SI #1#27 = "P"; aAlfa = "Adquisiciones intracomunitarias de bienes."; |FINSI;
         |SI #1#27 = "Q"; aAlfa = "Operaciones a las que se aplique el r‚gimen especial de bienes usados, ..."; |FINSI;
         |SI enEjercicio > 2011;
             |SI #1#27 = "R"; aTexto = "Operaci¢n de arrendamiento de local de negocio."; |FINSI;
             |SI #1#27 = "S"; aTexto = "Subvenciones, auxilios o ayudas satisfechas o recibidas, tanto por parte de Administraciones públicas o entidades privadas."; |FINSI;
             |SI #1#27 = "W"; aTexto = "Operaciones sujetas al Impuesto sobre la Producción, los Servicios y la Importación en las Ciudades de Ceuta y Melilla."; |FINSI;
             |SI #1#27 = "X"; aTexto = "Operaciones por las que los empresarios o profesionales que satisfagan compensaciones agrícolas hayan expedido el recibo"; |FINSI;
             |SI enEjercicio ] 2014;
                 |SI #1#27 = "Z"; aTexto = "Operaciones en Régimen especial de criterio de caja"; |FINSI;
                 |SI #1#27 = "1"; aTexto = "IVA criterio de caja. Asiento resumen de facturas"; |FINSI;
                 |SI #1#27 = "2"; aTexto = "IVA criterio de caja. Factura con varios asientos"; |FINSI;
                 |SI #1#27 = "3"; aTexto = "IVA criterio de caja. Factura rectificativa"; |FINSI;
                 |SI #1#27 = "4"; aTexto = "IVA criterio de caja. Adqusiciones realizadas por las agencias de viajes"; |FINSI;
                 |SI #1#27 = "5"; aTexto = "IVA criterio de caja. Factura simplificada"; |FINSI;
                 |SI #1#27 = "6"; aTexto = "IVA criterio de caja. Rectificaci¢n de errores registrales"; |FINSI;
                 |SI #1#27 = "7"; aTexto = "IVA criterio de caja. Facturaci¢n de las prestaciones servicios agencias viajes"; |FINSI;
                 |SI #1#27 = "8"; aTexto = "IVA criterio de caja. Operaci¢n de arrendamiento de local de negocio"; |FINSI;
             |FINSI;
         |FINSI;
     |FINSI;

     |SI enEjercicio [ 2011;
         |SI #1#27 = "X"; aAlfa1 = "No pasa al 340"; |FINSI;
     |SINO;
         |SI #1#27 = "*"; aAlfa1 = "No pasa al 340"; |FINSI;
     |FINSI;

     |SI aAlfa = "";
         |MENAV "0000 Clave de operaci¢n incorrecta";
         |ERROR;
         |ACABA;
     |FINSI;
|FPRC;

|CALCULO ElModelo; |TIPO 0;
 |C_M #0#15,1,"N";
 |C_M #1#21,0,aAlfa1;
 |SI aAlfa1 = "N"; |ACABA; |FINSI;

 |SI #1#21 ! 0;
     |C_M #0#15,1,"S";
     #1#12 = "N"; |PINTA #1#12;
     #1#14 = "P"; |PINTA #1#14;
     #1#6 = "0101";
     aAlfa1 = "N";
     |HAZ LeeModelo;
     |SI swModelo = 1;
         |MENAV "    Error, no existe Modelo Oficial";
         |ERROR;
     |FINSI;
     |SI #1#21 = 303; |Y enEjercicio ] 2014; |Y modo ! 2; #1#3 = 2; |FINSI;
 |SINO;
     aAlfa1 = " " * 42; |PINTA 1228, aAlfa1;
     aAlfa1 = "S";
 |FINSI;
 |C_M #1#12,1,aAlfa1;
 |C_M #1#14,1,aAlfa1;
 |C_M #1#6 ,1,aAlfa1;
|FCAL;

|PROCESO LeeModelo;
 swModelo = 0;
 aAlfa1 = " " * 42;
 |PINTA 1228, aAlfa1;
 |SI Haybasica = 0; swModelo = 1; |ACABA; |FINSI;

 |SI #1#21 = 0; |ACABA; |FINSI;
 |ABRE #199;
 #199#0 = #1#21;
 |LEE 001#199=;
 |SI FSalida ! 0;
     |DDEFECTO #199;
     swModelo = 1;
 |FINSI;
 |PINTA 1229,#199#1;
 |CIERRA #199;
|FPRC;

|CALCULO Tipo13_C; |TIPO 13;
 |SI enSwAna = 0;
     alfa1 = &205;
     |ATRI R; |PINTA 1440, "        ";|ATRI 0;
     |PINTA 2140, alfa1;
 |FINSI;
 |SI #1#14 = "F";
    |C_V #1#6, 1,"S";
 |SINO;
    |C_V #1#6, 1,"N"; |PINTA 1341, "       ";
 |FINSI;
 |HAZ LeeModelo;
 |PINTA 2301, "                                        ";
 |PINTA 2341, "                                        ";
|FCAL;


|| ------------------------------------------------------------------------/
|| Proceso para imprimirlos
|| ------------------------------------------------------------------------/
|PROCESO NomCta;
  aAlfa1 = #0#2; |QBF aAlfa1;
  aAlfa2 = aAlfa1 % 101;
  |SI aAlfa2 ! "C"; |ACABA; |FINSI;
  aAlfa3 = aAlfa1 % 201; nNume1 = aAlfa3

  |SI #1#12 = "S";
      |SI nNume1 = 1; aAlfa1 = "Cuenta Proveedor de Compra del Bien "; |FINSI;
      |SI nNume1 = 2; aAlfa1 = "Cta Prov.Inmovilizado a Largo Plazo "; |FINSI;
      |SI nNume1 = 3; aAlfa1 = "Cta Prov.Inmovilizado a Corto Plazo "; |FINSI;
      |SI nNume1 = 4; aAlfa1 = "Cuenta Cargo de Inmovilizado        "; |FINSI;
      |SI nNume1 = 5; aAlfa1 = "Cuenta Cargo de Intereses Diferidos "; |FINSI;
      |SI nNume1 = 6; aAlfa1 = "Cta Cargo de IVA Soportado Diferido "; |FINSI;
      |SI nNume1 = 7; aAlfa1 = "Cuenta Gastos de Intereses          "; |FINSI;
  |SINO;
      |SI nNume1 = 1;
          |SI #1#25 = "R"; aAlfa1 = "Cuenta Cliente   "; |FINSI;
          |SI #1#25 = "S"; aAlfa1 = "Cuenta Proveedor "; |FINSI;
          |SI #1#25 = "N"; aAlfa1 = "Cuenta Titular   "; |FINSI;
      |FINSI;
      |SI nNume1 = 2; aAlfa1 = "Cuenta Contrapartida "; |FINSI;
      |SI nNume1 = 3; aAlfa1 = "Cuenta I.V.A.        "; |FINSI;
      |SI nNume1 = 4; aAlfa1 = "Cuenta Recargo       "; |FINSI;
      |SI nNume1 = 5; aAlfa1 = "Cuenta I.R.P.F.      "; |FINSI;
      |SI nNume1 = 6; aAlfa1 = "Cuenta Descuentos    "; |FINSI;
  |FINSI;
  #2#2 =  aAlfa1;
|FPRC;

|PROCESO imprime2;
  swTipCta = 0;
  aAlfa1 = "                                    ";
  |SI #1#12 = "S"; |O #1#24 = "S";
      aAlfa1 = #0#2; |QBF aAlfa1;
      aAlfa2 = aAlfa1 % 101;
      |SI aAlfa2 = "C"; swTipCta = 1; |FINSI;
  |FINSI;
  |SI swTipCta = 0;
      #2#0 = #0#2;
      |LEE 001#2=;
      |SI FSalida ! 0;
         #2#2 = "NO EXISTE CUENTA";
      |FINSI;
  |SINO;
      |HAZ NomCta;
  |FINSI;

   tex2 = "";
   |SI #0#7 = "D"; tex2 = "al DEBE     "; |FINSI;
   |SI #0#7 = "H"; tex2 = "al HABER    "; |FINSI;
   |SI #0#7 = "S"; tex2 = "segun SALDO "; |FINSI;
   |SI #0#7 = "C"; tex2 = "CUADRE ASTO."; |FINSI;

   swinf = 1;
   |PRINT;
   swlin = 1;
|FPRC;

|PROCESO impresion; |TIPO 4;
   sw = 0;
   |IMPRE 0;
   |SI FSalida = 0;
       |INFOR "camautin";
       |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

      tex = "";
      |SI #1#12 = "S"; tex = "Operacion Financiera"; |FINSI;
      |SI #1#21 ! 0;   tex = "Modelo Oficial"; |FINSI;
      |SI #1#24 = "S";
          tex = "Datos de Registros ";
          |SI #1#25 = "R"; tex = tex + " de IVA Repercutido"; |FINSI;
          |SI #1#25 = "S"; tex = tex + " de IVA Soportado"; |FINSI;
          |SI #1#25 = "N"; tex = tex + " de I.R.P.F."; |FINSI;
      |FINSI;
      swinf = 0;
      |PRINT;
      swlin = 0;
      |ABRET #2;
      |BUCLELIN 2imprime2#1;
      |SI swlin = 1;
          swinf = 2;
          |PRINT;
      |FINSI;
      |CIERRAT #2;

      |PIEINF;
      |FININF;
   |SINO;
      |MENAV "    Proceso Abortado ...";
   |FINSI;
   |FINIMP;
|FPRC;

|| ------------------------------------------------------------------------/
|| *********************************** PROCESOS de Lineas
|PROCESO VerMod303;
   |SI modo = 2; |Y #1#21 = 303;
       swerror = 0;
       |SI enEjercicio ] 2014; |Y #1#3 = 1; swerror = 1; |FINSI;
       |SI enEjercicio < 2014; |Y #1#3 ! 1; swerror = 1; |FINSI;
       |SI swerror = 1;
           |MENAV "2400El Formato del Modelo 303 no correspondiente a este Ejercicio. ";
           |PTEC 807;
       |FINSI;
   |FINSI;
|FPRC;

|CALCULO SinBarraMod; |TIPO 5;
   |HAZ SinBarra;
   |HAZ VerMod303;
|FCAL;

|PROCESO nodepar; |TIPO 7;
   |CAMPO_MODIFICA #0#6, 1, eaDepar;
   |SI eaDepar  = "S";
      |SI Haybasica = 1;
          |MENSA "    <F2> Consulta de actividades";
      |SINO;
          |MENSA "    <F2> Consulta de departamentos";
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO nosubde; |TIPO 7;
   |CAMPO_MODIFICA #0#9, 1, eaSubde;
   |SI eaSubde  = "S";
      |SI Haybasica = 1;
         |SI enArrend = 1;
            |MENSA "    Subdepartamento. <F2> Consulta de Referencias Catastrales";
         |SINO;
            |MENSA "    Subdepartamento. <F2> Consulta de Cultivos";
         |FINSI;
      |FINSI;
   |FINSI;
|FPRC;

|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|CALCULO AntesParam; |TIPO 7;
 aAlfa1 = #1#13;
 |C_M #0#11,1,aAlfa1;
 |C_M #0#12,1,aAlfa1;
 |SI aAlfa1 = "N";
     #0#11 = " "; |PINTA #0#11;
     #0#12 = 0;   |PINTA #0#12;
     #0#13 = 0;   |PINTA #0#13;
 |FINSI;
|FCAL;

|CALCULO TrasParam; |TIPO 0;

 |SI FSalida = 2; |ACABA; |FINSI;

 |C_M #1Campo,0,aAlfa1;
 |SI aAlfa1 = "N"; |ACABA; |FINSI;

 |SI #1#13 = "S";
     |SI #0#11 = "x"; #0#11 = "X"; |PINTA #0#11; |FINSI;

     |SI #0#11 = " "; #0#11 = "F"; |PINTA #0#11; |ERROR; |ACABA; |FINSI;

     |C_M #0#12,1,"S";
     |SI #0#11 = "F"; |O #0#11 = "X"; |O #0#11 = "f";
         |C_M #0#12,1,"N";
         #0#12 = 0;
         |PINTA #0#12;
     |FINSI;

     |C_M #0#13,1,"S";
     |SI #0#11 ! "I"; |Y #0#11 ! "R"; |Y #0#11 ! "P";
         |C_M #0#13,1,"N";
         #0#13 = 0;
         |PINTA #0#13;
     |FINSI;
     |SI #0#11 ! "X";
         |SI #0#11 ! "P";
             enFilConc  = 2;
             enConcepto = #0#4;
             enLibIVA   = #1#17;
             |HAZ FiltroConcepto;
         |SINO;
             enFilConc  = 3;
             enConcepto = #0#4;
             enLibIVA   = #1#17;
             |HAZ FiltroConcepto;
         |FINSI;
    |FINSI;
 |FINSI;
|FCAL;

|CALCULO TrasLin; |TIPO 0;
 |SI #1#13 = "S";
     |SI #0#11 ! "F"; |Y #0#11 ! "X"; |Y #0#11 ! "f";
         |SI #0#12 = 0;
             #0#12 = 1; |PINTA #0#12;
         |FINSI;
     |FINSI;
 |FINSI;
|FCAL;

|CALCULO AntesCta; |TIPO 7;
  |SI #1#12 = "N"; |Y #1#24 = "N"; |ACABA; |FINSI;

  |PUSHV 1120 2380;
  |ATRI R;
  |SI #1#12 = "S";
      |PINTA 1120, " CUENTAS PARA EL APUNTE DE OPERACION FINANCIERA       ";
      |PINTA 1220, "======================================================";
      |PINTA 1320, " Numero de la Cuenta, o los siguientes codigos:       ";
      |PINTA 1420, " C1 => Cuenta Proveedor de Compra del Bien            ";
      |PINTA 1520, " C2 => Cuenta Proveedor de Inmovilizado a Largo Plazo ";
      |PINTA 1620, " C3 => Cuenta Proveedor de Inmovilizado a Corto Plazo ";
      |PINTA 1720, " C4 => Cuenta Cargo de Inmovilizado                   ";
      |PINTA 1820, " C5 => Cuenta Cargo de Intereses Diferidos            ";
      |PINTA 1920, " C6 => Cuenta Cargo de IVA Soportado Diferido         ";
      |PINTA 2020, " C7 => Cuenta Gastos de Intereses                     ";
  |SINO;
      aAlfa1 = "IVA REPERCUTIDO ";
      aAlfa2 = "Cliente  ";
      |SI #1#25 = "S";
          aAlfa1 = "IVA SOPORTADO   ";
          aAlfa2 = "Proveedor";
      |FINSI;
      |SI #1#25 = "N";
          aAlfa1 = "I.R.P.F.        ";
          aAlfa2 = "Titular  ";
      |FINSI;
      aAlfa1 = " CUENTAS PARA EL APUNTE RELACIONADO CON " + aAlfa1;
      aAlfa2 = " C1 => Cuenta "+ aAlfa2 + "                                 ";

      |PINTA 1120, aAlfa1;
      |PINTA 1220, "========================================================";
      |PINTA 1320, " Numero de la Cuenta, o los siguientes codigos:         ";
      |PINTA 1420, aAlfa2;
      |PINTA 1520, " C2 => Cuenta Contrapartida                             ";
      |SI #1#25 ! "N";
          |PINTA 1620, " C3 => Cuenta I.V.A.                                    ";
          |PINTA 1720, " C4 => Cuenta Recargo                                   ";
          |PINTA 1820, " C5 => Cuenta I.R.P.F.                                  ";
          |PINTA 1920, " C6 => Cuenta Descuentos                                ";
      |FINSI;
  |FINSI;
  |ATRI 0;
|FCAL;

|CALCULO cuenta; |TIPO 6;
  swTipCta  = 0;

  |SI #1#12 = "S"; |O #1#24 = "S";
      |POPV;

      aAlfa1 = #0#2; |QBF aAlfa1;
      aAlfa2 = aAlfa1 % 101; aAlfa2 = aAlfa2 > " ";
      |SI aAlfa2 = "C";
          aAlfa3 = aAlfa1 % 201;
          nNume1 = aAlfa3
          aMensa = "";
          nErrCta = 0;
          |SI nNume1 <1; |O nNume1 > 7;
              aMensa = "    Error. Cuenta Contable de la Operacion Financiera mal introducida";
              |SI #1#24 = "S";
                  aMensa = "    Error. Cuenta Contable del Registro IVA/IRPF mal introducida";
              |FINSI;
              nErrCta = 1;
          |SINO;
              |SI #1#24 = "S"; |Y #1#25 = "N";
                  |SI nNume1 > 2;
                      aMensa = "    Error. Cuenta Contable del Registro IVA/IRPF mal introducida";
                      nErrCta = 1;
                  |FINSI;
              |FINSI;
          |FINSI;

          |SI nErrCta = 1;
              |MENAV aMensa;
              swTipCta = -1;  || cuenta mal
              |ERROR;
          |SINO;
               swTipCta = 1;
               aAlfa1 = aAlfa2 + aAlfa3;
               #0#2 = aAlfa1; |PINTA #0#2;
          |FINSI;
          |ACABA;
      |FINSI;

  |FINSI;
     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;
|FCAL;

|CALCULO cuefin; |TIPO 0;
   |SI swTipCta = -1; |ACABA; |FINSI;  || Cuenta Erronea

   |SI swTipCta = 0;   || Es el numero de una cuenta contable
       aAlfa1 = #0#2; |QBF aAlfa1;
       |SI aAlfa1 = "";
           |ERROR;
           |ACABA;
       |FINSI;
       emCta    = 1;
       eaCuenta = #0#2;
       |HAZ SacaNivel;
       |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea

       |HAZ LeeCuenta;
       |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea
       aAlfa1 = #cacuenta#2;
   |FINSI;
   |HAZ PintaCta;
|FCAL;

|| ////////////////////////////////////////////////////////////
|PROCESO ElMensa; |TIPO 7;
   |C_M #0#15, 1, "N";
   |SI #1#21 ! 0;
       |C_M #0#15, 1, "S";
       |SI #0#7 = "S"; |O #0#7 = "C";
           |MENSA "    (+) Asiento al Debe, (-) Asiento al Haber, (=) siempre";
       |SINO;
           |MENSA "    (+) si importe es positivo, (-) si importe es negativo, (=) siempre";
       |FINSI;
   |SINO;
       |SI #0#7 = "S"; |O #0#7 = "C";
           |C_M #0#15, 1, "S";
           |MENSA "    (+) Asiento al Debe, (-) Asiento al Haber, (=) siempre";
        |FINSI;
   |FINSI;
|FPRC;

|PROCESO concepto; |TIPO 0;

   |SI FSalida = 10;
       enConcepto = #0#4;
       |HAZ VerConcepto30;
       #0#4 = enConcepto; |PINTA #0#4;
   |FINSI;

   enFilNO = 0;
   enFilConc = 1;
   |SI #1#13 = "S"; enFilConc = 0; |FINSI;
   eaCuenta = "";
   |SI swTipCta = 0; eaCuenta = #0#2; |FINSI;
   |SI eaCuenta ! "";
       |SI #1#13 = "S"; |Y #1#16 ! "N";
           aAlfa1 = eaCuenta % 103;
           |SI aAlfa1 = "477"; |O aAlfa1 = "472";
               enFilNO = 1;
           |FINSI;
       |FINSI;
   |FINSI;
   enConcepto = #0#4;
   enLibIVA   = #1#17;
   |HAZ FiltroConcepto;  |SI eswLect ! 0; |ACABA; |FINSI;

   |SI FEntrada = 101;
      #0#5 = #dsmom030#1;
   |SINO;
      |SI #0#4 = coant;
          #0#5 = deant;
      |SINO;
          #0#5 = #dsmom030#1;
      |FINSI;
   |FINSI;
   |PINTA #0#5;

   |SI #1#13 = "S"; |Y #1#16 ! "N"; |Y #1#27 = "D";
       aTipoIVA   = #1#16;
       enConcepto = #0#4;
       |HAZ VeoRecti;
    |FINSI;
|FPRC;

|PROCESO descrip; |TIPO 0;
   nInkey = FSalida;
   |SI nInkey = 15;
      #0#5 = deant;
      |PINTA #0#5;
   |FINSI;
|FPRC;

|PROCESO c_descrip; |TIPO 7;
       |SI eaSobreEs ! "S";
           |PTEC 816;
       |FINSI;
|FPRC;

|CALCULO AntesSigno; |TIPO 7;
 |SI #1#12 = "N"; |Y #1#21 = 0; |Y #1#24 = "N";
     |C_M #0#8,  1,"S";
     |C_M #0#10, 1,"N"; #0#10 = 0; |PINTA #0#10;
 |SINO;
     |C_M #0#8,  1,"N"; #0#8 = 0; |PINTA #0#8;
     |C_M #0#10, 1,"S";
     aAlfa1 = #0#2; |QBF aAlfa1;
     aAlfa2 = aAlfa1 % 101;
     |SI aAlfa2 = "C"; |Y #1#12 = "S";
         aAlfa3 = aAlfa1 % 201; nNume1 = aAlfa3;
         |SI nNume1 [ 3; #0#7 = "D"; |SINO; #0#7 = "H"; |FINSI;
         |SI nNume1 = 7; #0#7 = "D"; |FINSI;
         |PINTA #0#7;
     |FINSI;
 |FINSI;
|FCAL;

|CALCULO mayusl; |TIPO 0;
 |SI #1#12 = "S";
     |SI #0#7 = "S"; |ERROR; |ACABA; |FINSI;
     |SI #0#7 = "C";
         |C_M #0#8, 1,"N";  #0#8  = 0; |PINTA #0#8;
         |C_M #0#10,1,"N";  #0#10 = 0; |PINTA #0#10;
     |FINSI;
 |SINO;
     |SI #1#21 ! 0; |O #1#24 = "S";
         |SI #0#7 = "S"; |O #0#7 = "C";
             |C_M #0#8, 1,"N";  #0#8  = 0; |PINTA #0#8;
             |C_M #0#10,1,"N";  #0#10 = 0; |PINTA #0#10;
         |FINSI;
     |SINO;
         |SI #0#7 = "S"; |O #0#7 = "C";
             |C_M #0#8,1,"N";  #0#8 = 0; |PINTA #0#8;
         |FINSI;
     |FINSI;
 |FINSI;
|FCAL;

|CALCULO AntesTFinan; |TIPO 7;
  |C_M #0#10, 0, aAlfa1; |SI aAlfa1 = "N"; |ACABA; |FINSI;

  |SI #1#12 = "S";
      |MENSA "    Introduzca Tipo de Importe En Operacion Financiera";
  |SINO;
      |SI #1#21 ! 0;
           |MENSA "    Introduzca Campo del Modelo / <F2> Consulta Campos";
      |SINO;
           |MENSA "    Introduzca Tipo de Importe del Registro IVA/IRPF";
      |FINSI;
  |FINSI;

  |PUSHV 0701 2157;
|SI #1#12 = "S";
  |ATRI R;
  |PINTA 0701, " IMPORTE DEL APUNTE DE OPERACION FINANCIERA              ";
  |PINTA 0801, " ======================================================= ";
  |PINTA 0901, "  0= Introducir Importe                                  ";
  |PINTA 1001, "  1= Valor del Bien            21= Capital Amortizado    ";
  |PINTA 1101, "  2= Desembolso Inicial        22= Intereses             ";
  |PINTA 1201, "  3= Importe Nominal Prestamo  23= Cuota de IVA          ";
  |PINTA 1301, "  4= Intereses Aplazamiento    24= Capital mas intereses ";
  |PINTA 1401, "  5= Comision por Apertura     25= Total a Pagar         ";
  |PINTA 1501, "  6= Otros Gastos Cargados     26= IVA amortizacion      ";
  |PINTA 1601, "  7= Fianza Depositada         27= IVA intereses         ";
  |PINTA 1701, "  8= Importe de IVA                                      ";
  |PINTA 1801, "  9= Valor Residual                                      ";
  |PINTA 1901, " 10= IVA del Valor Residual                              ";
  |PINTA 2001, " 11= Total Operacion                                     ";
  |SI #1#14 = "L";
      |ATRI P; |PINTA 0932, "Importes mensuales";
  |SINO;
      |ATRI P; |PINTA 0932, "Importes anuales";
  |FINSI;
  |ATRI 0;

|SINO;

  |SI #1#21 ! 0;
      |HAZ TabModelos;
  |SINO;
      aAlfa1 = "IVA REPERCUTIDO ";
      |SI #1#25 = "S"; aAlfa1 = "IVA SOPORTADO   "; |FINSI;
      |SI #1#25 = "N"; aAlfa1 = "I.R.P.F.        "; |FINSI;
      aAlfa1 = " IMPORTE DEL APUNTE DE "+ aAlfa1 + "                  ";

      |ATRI R;
      |PINTA 0801, aAlfa1;
      |PINTA 0901, " ======================================================= ";
      |PINTA 1001, "  0= Introducir Importe                                  ";
      |PINTA 1101, " ---- Apuntes por Totales     ----- Apuntes por Lineas   ";
      |PINTA 1201, "  1= Total Bases               11= Base Imponible        ";
      |PINTA 1301, "  2= Total IRPF                12= Base I.R.P.F.         ";
      |PINTA 1401, "  3= Total Cuotas              13= Cuota I.R.P.F.        ";
      |PINTA 1501, "  4= Total Descuentos          14= Cuota IVA             ";
      |PINTA 1601, "  5= Total Factura             15= Cuota Recargo         ";
      |PINTA 1701, "  6= Total Cuotas IVA          16= Descuento             ";
      |PINTA 1801, "  7= Total Cuotas Recargo                                ";
      |ATRI 0;
  |FINSI;
|FINSI;

|FCAL;

|PROCESO LeeCodTab;
 nUltCod = 0;
 |ABRE #901;
 #901#0 = enModelo;
 #901#1 = nCasilla;
 |LEE 001#901=;
 |SI FSalida ! 0;
     nUltCod = 1;
 |FINSI;
 |CIERRA #901;
|FPRC;

|CALCULO TipoFinan; |TIPO 0;
 |C_M #0#10, 0, aAlfa1;
 |SI aAlfa1 = "N"; |ACABA; |FINSI;
 nInkey = FSalida;
 |POPV;
 |SI #1#12 = "S";
     |SI #0#10 > 11; |Y #0#10 < 21; |ERROR; |ACABA; |FINSI;
     |SI #0#10 > 27; |ERROR; |ACABA; |FINSI;
 |SINO;
     |SI #1#21 ! 0;
         |SI nInkey = 10;
             |HAZ TabModelos;
         |FINSI;
         nCasilla = #0#10;
         enModelo = #1#21;
         |HAZ LeeCodTab;
         |SI nUltCod = 1;
             |ERROR;
             |ACABA;
         |FINSI;
     |SINO;
         |SI #0#10 > 7; |Y #0#10 < 11; |ERROR; |ACABA; |FINSI;
         |SI #0#10 > 16; |ERROR; |ACABA; |FINSI;
         |SI #0#10 > 10;
             aAlfa1 = #0#2; |QBF aAlfa1;
             aAlfa2 = aAlfa1 % 101;
             |SI aAlfa1 = "C1"; |O aAlfa2 ! "C";
                 |ERROR;
                 |ACABA;
             |FINSI;
         |FINSI;
     |FINSI;
 |FINSI;

 |SI #0#10 = 0;
    |C_M #0#8, 1,"S";
 |SINO;
    |C_M #0#8,1,"N";  #0#8 = 0; |PINTA #0#8;
 |FINSI;
|FCAL;

|RUTINA ClInf901;
 #901#0 = enModelo;
 |SI FSalida = "POSICION"; |Y tfant ! 0;
     #901#1 = tfant;
 |SINO;
     #901#1 = 1;
 |FINSI;
|FRUT;

|RUTINA ClSup901;
 #901#0 = enModelo;
 #901#1 = 999;
|FRUT;

|PROCESO TabModelos;
 |SI Haybasica = 0; |ACABA; |FINSI;
 enModelo = #1#21; |SI enModelo = 0; enModelo = 1; |FINSI;
 |PUSHV 0701 2157;
 nCasilla = #0#10;
 |PINPA #0#901;
 #901#0 = enModelo;    |PINTA #901#0;
 |SI enModelo ] 200; |Y enModelo [ 299;
     |ATRI P;
     |PINTA 0741, "Ejercicio ";
     |SI enModelo ! 202; |PINTA 0751, enQueSoc; |FINSI;
     |SI enModelo = 202; |PINTA 0751, enEjercicio; |FINSI;
     |ATRI 0;
 |FINSI;

 |ENTLINEAL #1#901, -2, 4, 20, 0, ClInf901, ClSup901;
 |POPV;
 #0#10 = nCasilla; |PINTA #0#10;
|FPRC;

|PROCESO nocero; |TIPO 0;
   |C_M #0Campo,0,aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   |SI #0Campo = 0;
      |MENAV "    ATENCION!!! Importe cero ...";
      || |ERROR;
   |FINSI;
|FPRC;

|PROCESO tipo3; |TIPO 3;
   aAlfa1 = #0#2;  |QBF aAlfa1;
   |SI aAlfa1 = "";
      |MENAV "    ATENCION!!! Apunte sin cuentas ...";
      |ERROR;
      |ACABA;
   |FINSI;
   |SI #0#8 = 0; |Y #0#10 = 0;
      |SI #0#7 = "D"; |O #0#7 = "H";
||          |MENAV "    ATENCION!!! Importe cero ...";
||          |ERROR;
||          |ACABA;
      |FINSI;
   |FINSI;
   cuant = #0#2;
   diant = #0#3;
   coant = #0#4;
   deant = #0#5;
   dhant = #0#7;
   imant = #0#8;
   dpant = #0#6;
   sdant = #0#9;
   tfant = #0#10;
|FPRC;

|PROCESO tipo1; |TIPO 1;
   cuant = #0#2;
   diant = #0#3;
   coant = #0#4;
   deant = #0#5;
   dhant = #0#7;
   imant = #0#8;
   dpant = #0#6;
   sdant = #0#9;
   tfant = #0#10;
|FPRC;

|PROCESO tipo13; |TIPO 13;
  swTipCta = 0;
  aAlfa1 = "                                    ";
  |SI #1#12 = "S"; |O #1#24 = "S";
      aAlfa1 = #0#2; |QBF aAlfa1;
      aAlfa2 = aAlfa1 % 101;
      |SI aAlfa2 = "C"; swTipCta = 1; |FINSI;
  |FINSI;
  |SI #0#2 ! doce; |Y swTipCta = 0;
      |ABRE #2;
      #cacuenta#0 = #0#2;
      |LEE 001#2=;
      |SI FSalida ! 0;  |DDEFECTO #2;  |FINSI;
      aAlfa1 = #cacuenta#2;
      |CIERRA #2;
   |SINO;
      |DDEFECTO #2;
   |FINSI;
   |HAZ PintaCta;

   modoL = (FEntrada / 100) ! 0;
   |PINTA 2301, "                                        ";
   |PINTA 2341, "                                        ";
   |SI modoL = 1; |ACABA; |FINSI;
   |SI eaDepar = "S";
       eOpDep = 0;
       enActividad = #0#6;
       eaCodDep    = #0#6;
       eaNombreAct = "";
       |HAZ Actividad;
       |ATRI S; |PINTA 2301, eaNombreAct; |ATRI 0;
   |FINSI;
   |PINTA 2341, "                                        ";
   |SI eaSubde = "S";
       eOpSub = 0;
       enConcepto  = #0#4;
       eaCodSubdep = #0#9;
       eaNomSubdep = "";
       |HAZ SubActividad;
       |ATRI S; |PINTA 2341, eaNomSubdep; |ATRI 0;
   |FINSI;
|FPRC;

|PROCESO PintaCta;
 |SI swTipCta = 1;
     aAlfa1 = #0#2; |QBF aAlfa1;
     aAlfa2 = aAlfa1 % 101;
     |SI aAlfa2 ! "C"; |ACABA; |FINSI;
     aAlfa3 = aAlfa1 % 201; nNume1 = aAlfa3
     |SI #1#12 = "S";
         |SI nNume1 = 1; aAlfa1 = "Cuenta Proveedor de Compra del Bien "; |FINSI;
         |SI nNume1 = 2; aAlfa1 = "Cta Prov.Inmovilizado a Largo Plazo "; |FINSI;
         |SI nNume1 = 3; aAlfa1 = "Cta Prov.Inmovilizado a Corto Plazo "; |FINSI;
         |SI nNume1 = 4; aAlfa1 = "Cuenta Cargo de Inmovilizado        "; |FINSI;
         |SI nNume1 = 5; aAlfa1 = "Cuenta Cargo de Intereses Diferidos "; |FINSI;
         |SI nNume1 = 6; aAlfa1 = "Cta Cargo de IVA Soportado Diferido "; |FINSI;
         |SI nNume1 = 7; aAlfa1 = "Cuenta Gastos de Intereses          "; |FINSI;
     |SINO;
         |SI nNume1 = 1;
             |SI #1#25 = "R"; aAlfa1 = "Cuenta Cliente                      "; |FINSI;
             |SI #1#25 = "S"; aAlfa1 = "Cuenta Proveedor                    "; |FINSI;
             |SI #1#25 = "N"; aAlfa1 = "Cuenta Titular                      "; |FINSI;
         |FINSI;
         |SI nNume1 = 2; aAlfa1 = "Cuenta Contrapartida                "; |FINSI;
         |SI nNume1 = 3; aAlfa1 = "Cuenta I.V.A.                       "; |FINSI;
         |SI nNume1 = 4; aAlfa1 = "Cuenta Recargo                      "; |FINSI;
         |SI nNume1 = 5; aAlfa1 = "Cuenta I.R.P.F.                     "; |FINSI;
         |SI nNume1 = 6; aAlfa1 = "Cuenta Descuentos                   "; |FINSI;
     |FINSI;
 |FINSI;

 |ATRI I;
 |PINTA 2221, aAlfa1;
 |ATRI 0;
|FPRC;

||***************************** Procesos para Departamento y Subdepartamento
|| ==========================================================================
|PROCESO cActividad; |TIPO 0;
   |SI eaDepar = "N"; |ACABA; |FINSI;
   |PINTA 2301, "                                        ";
   eOpDep = 0; || 1 = Consulta pantalla
   |SI FSalida = 10;  eOpDep = 1; |FINSI;
   enActividad = #0#6;
   eaCodDep    = #0#6;
   |HAZ Actividad;
   |SI eswLect = 1;
       |MENAV "    Error.! No Existe Actividad / Departamento";
       |ERROR;
   |SINO;
       aAlfa1 = enActividad;
       |SI Haybasica = 1;
           aAlfa1 = ("00" + aAlfa1) % A02;
       |FINSI;
       #0#6 = eaCodDep; |PINTA #0#6;
       |ATRI S; |PINTA 2301, eaNombreAct; |ATRI 0;
   |FINSI;
|FPRC;

|PROCESO cSubActividad; |TIPO 0;
   |SI eaSubde = "N"; |ACABA; |FINSI;

   eOpSub = 0; || 1 = Consulta pantalla
   |SI FSalida = 10; eOpSub = 1; |FINSI;

   |PINTA 2341, "                                        ";
   enConcepto  = #0#4;
   eaCodSubdep = #0#9;
   enQue = 1;
   |HAZ SubActividad;
   enQue = 0;
   |SI eswLect = 1;
       |MENAV "    Error.! No Existe SubDepartamento";
       |ERROR;
   |SINO;
       #0#9 = eaCodSubdep; |PINTA #0#9;
       |ATRI S; |PINTA 2341, eaNomSubdep; |ATRI 0;
   |FINSI;
|FPRC;

|CALCULO wTip11; |TIPO 11;
 nInkey = FSalida;
 |SI nInkey = 1; |O nInkey = 0;
     nCasilla = #901#1;
 |FINSI;
|FCAL;

|CALCULO wTip5; |TIPO 0;
 nInkey = FSalida;
 |SI nInkey = 0;
     nCasilla = #901#1;
     |PTEC 806;
     |PTEC 808;
 |FINSI;
|FCAL;

|CALCULO AnAna; |TIPO 7;
 |SI #1#12 = "N"; |Y #1#24 = "N";
     |MENSA "    (*TABLA) Valor Tabla de relacion, <F2> Consulta, <F7> Repite anterior";
 |SINO;
     |MENSA "    (*TABLA) Valor Tabla relacion, (*REGIS) Valor Ficha, <F2> Consulta, <F7> Repite";
 |FINSI;

 modoL = (FEntrada / 100) ! 0;
 |SI modoL = 1;
     |SI #1#12 = "S"; |O #1#24 = "S";
         |SI #1#24 = "S"; #0Campo = "*REGIS"; |FINSI;
         |SI #1#12 = "S"
             aAlfa1 = #0#2; aAlfa1 = aAlfa1 % 101;
             |SI aAlfa1 = "C"; #0Campo = "*REGIS "; |FINSI;
         |FINSI;
         |PINTA #0Campo;
     |FINSI;
 |FINSI;
|FCAL;

|CALCULO anacta; |TIPO 0;
   nInkey = FSalida;
   |SI enSwAna = 0; |ACABA; |FINSI;
   |SI nInkey = 2;  |ACABA; |FINSI;

   |SI nInkey = 15;
       #0Campo = caant;
       |PINTA #0Campo;
   |FINSI;

   |SI #0Campo ! "*TABLA ";
       salidas = FSalida; |HAZ FPuntosAna;
       eOpAna = 0; || 1 = Consulta pantalla
       |SI nInkey = 10; eOpAna = 1;  |FINSI;
       eaCtaAna = #0Campo;
       |HAZ LeeCtaAna;
       #0Campo = eaCtaAna;
       |PINTA #0Campo;
   |FINSI;

   |SI #1#12 = "N"; |Y #1#24 = "N";
       |SI #0Campo = "*REGIS ";
           |MENAV "    ERROR EN CUENTA ANALITICA";
           |ERROR;
           |ACABA;
       |FINSI;
   |FINSI;
   |SI #1#12 = "S";
       aAlfa1 = #0#2; aAlfa1 = aAlfa1 % 101;
       |SI aAlfa1 ! "C"; |Y #0Campo = "*REGIS ";
           |MENAV "    ERROR EN CUENTA ANALITICA";
           |ERROR;
           |ACABA;
       |FINSI;
   |FINSI;

   caant = #0Campo;
|FCAL;
