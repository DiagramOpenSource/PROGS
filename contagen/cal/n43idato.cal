|FICHEROS;
    n43idato #0;
    n43datos #1;
    cacuenta #2; || Cuentas Contables.
|FIN;

|VARIABLES;

   &entrada = 1;
   informe  = "n43idato";
   sw       = 0;
   nSwPie   = 0;
   cuenta   = "";
   &debe    = 0;
   &haber   = 0;
   nSwPrint = 0;
|FIN;

|INCLUYE dscont_i;

|CALCULO defecto; |TIPO 7;
 #0#2 = fec1; |PINTA #0#2;
 #0#3 = fec2; |PINTA #0#3;
|FCAL;

|PROCESO cuenta;|TIPO 6;
   eaCuenta = #0Campo;
   salidas  = FSalida;
   |HAZ FiltraPuntos;
   #0Campo = eaCuenta; |PINTA #0Campo;
|FPRC;

|| **************************************************************

|CALCULO imprimir;
     |SI #1#11 ! "S";

         |SI sw = 0;
             cuenta = #1#1;
             sw     = 1;
             debe   = 0;
             haber  = 0;
         |FINSI;

         |SI cuenta ! #1#1;
             |SI nSwPie = 1; |PIEINF; nSwPie = 0; |FINSI;
             cuenta = #1#1;
             debe   = 0;
             haber  = 0;
         |FINSI;

         nSwPrint = 1;
         |SI #0#4 = "S";
             |SI #1#9 ! "S"; nSwPrint = 0; |FINSI;
         |SINO;
             |SI #0#4 = "N";
                 |SI #1#9 ! "N"; nSwPrint = 0; |FINSI;
             |FINSI;
         |FINSI;

         |SI nSwPrint = 1;
             |PRINT;
             nSwPie = 1;
             |SI #1#6 = "H";
                 haber = haber + #1#7;
             |SINO;
                 debe  = debe  + #1#7;
             |FINSI;
         |FINSI;

    |FINSI;
|FCAL;

|DEFBUCLE cpentrad;
   #1#3;
   ;
   #0#0, #0#2;
   #0#1, #0#3;
   ;
   NULL, imprimir;
|FIN;

|CALCULO impre;|TIPO 3;

    nSwPie = 0;
    sw     = 0;
    |IMPRE 0;
    |SI FSalida ! 0;
        |MENAV "    Proceso interrumpido.";
        |ACABA;
    |FINSI;

    |INFOR informe;
    |SI FSalida ! 0;
        |MENAV "    No existe informe";
        |FINIMP;
        |ACABA;
    |FINSI;

    |BUCLE cpentrad;
    |SI nSwPie ! 0;
        |PIEINF;
    |FINSI;

    |FININF;
    |FINIMP;
|FCAL;
