|FICHEROS;
    cabaresu #0;
    cacuenta #1;         || cuentas
    cabaexp3 #3;         || Auxiliar de Cuentas
    caexistc #6;         || descarga cabs.
    caexistl #7;         || descarga lins.
    caconimp #8;

    canempre #30;
    caselem1 #998;
    cawdatos #900;
|FIN;

|VARIABLES;

  aAlfa1 = "";
  aAlfa2 = "";
  aAlfa3 = "";
  nNume1 = 0;
  nNume2 = 0;
  nNume3 = 0;

  swAlgun = 0;
  nSwSuma = 0;

  &pathbal  = "";
  &balance  = 1;
  &aparam   = "";

  linea     = 0;
  inform1   = "cabaresu";
  informe   = "cabares1";
  alfa1     = "";
  alfa2     = "";
  nume1     = 0;

  mes       = 0;
  letrames  = "";
  nivel     = 0;
  traraf    = 0;
  x         = 0;
  y         = 0;
  sw_exp    = 0;
  sw        = 0;
  sw_error  = 0;

  &r_emp = 0;
  &r_per = 0;
  &d_mes = 0;
  &h_mes = 0;
  &estru = 0;
  &r_eje = 0;
  &r_nom = "";

  empreant = 0;
  ejercant = 0;

  &eldesde  = 0;
  &elhasta  = 0;
  des_exi   = 0;
  has_exi   = 0;
  d_cuen    = "";
  h_cuen    = "";
  act_cuent = "";
  act_nivel = 0;
  cue_perdi = "";
  dig_perdi = 0;
  acor_c    = "";
  acor_d    = 0;
  alar_c    = "";
  alar_d    = 0;

  tipo    = "";
  debe    = 0;
  haber   = 0;
  saldo   = 0;
  explota = 0;
  descar  = 0;
  tipoAD  = "";
  contaAD = 0;
  nLaCol  = 0;

  lin     = 0;
  swfinal = 0;
  importe = 0;

  &titulo = "";
  &sw_inf = 0;
  &var1   = "";
  &var2   = "";
  &tot1   = 0;
  &tot2   = 0;
  &eaT1   = "";
  &eaT2   = "";
  &eaT3   = "";
  &enN1   = 0;
  &enN2   = 0;
  &enN3   = 0;
  &enN4   = 0;
  &nFull  = 0;

  {-1}menu  = "";           || Variables menu impresora o pantalla
      menu1 = "2400";
      menu2 = "1";
      menu3 = "Elija opcion: ";
      menu4 = "PI";
      menu5 = " Pantalla ";
      menu6 = " Impresora ";


     aNomCta  = "";
|FIN;

|INCLUYE dscont_i;
|INCLUYE digito_i;
|INCLUYE masivo_i;

|| **********************************************************************
|CALCULO inicio_b; |TIPO 8;
  |HAZ inicial;
  |SI swerror = 2; |PTEC 807; |ACABA; |FINSI;
  |SI swerror = 3; |PTEC 831; |PTEC 807; |ACABA; |FINSI;

  |HAZ DirAplicSt;
|FCAL;

|CALCULO estruc7; |TIPO 7;
 #0#6 = 1;
 |SI Haybasica = 1; |Y eaDepar = "S";
     #0#6 = 0;
 |FINSI;
 |PINTA #0#6;
|FCAL

|PROCESO antesque;
  aNomCta = "cacuenta";
  aparam = PARAMETRO; |QBF aparam;
  |SI aparam = " N"; aparam = ""; |FINSI;
  |SI aparam = "fech";
      aNomCta = "cabalano";
  |FINSI;
  |NOME_DAT #1 aNomCta;
|FPRC;

|| ========================================================================
||                        CALCULOS PANTALLA
|| ========================================================================

|PROCESO mayor;
  alfa1 = #0Campo > " ";
  #0Campo = alfa1;
  |PINTA #0Campo;
|FPRC;

|PROCESO pintames; |TIPO 0;
  nume1 = Campo + 2;                || Coge el campo del nombre del mes
  |SI #0Campo ! 0; |Y #0Campo ! 13;
      NumMes = dig1 + (#0Campo - 1);   || Operacion para saber que mes es
      |SI NumMes > 12; NumMes = NumMes - 12; |FINSI;
  |SINO;
      NumMes = #0Campo;
  |FINSI;
  |HAZ NombreMes;
  #0nume1 = NombreMes;
  |PINTA #0nume1;                         || Pinta el nombre del mes

  |SI Campo = 1; |Y  #0#0 > #0#1;
      |MENAV "    Error de Entrada ...";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO pintames2; |TIPO 0;
  |SI #0Campo ! 0; |Y #0Campo ! 13;
      NumMes = dig1 + (#0Campo - 1);   || Operacion para saber que mes es
      |SI NumMes > 12; NumMes = NumMes - 12; |FINSI;
  |SINO;
      NumMes = #0Campo;
  |FINSI;
  |HAZ NombreMes;
  |ATRI I;
  |SI Campo = 7;  |PINTA 1424, NombreMes;  |FINSI;
  |SI Campo = 8;  |PINTA 1461, NombreMes;  |FINSI;
  |ATRI 0;
  |SI Campo = 8;  |Y #0#7 > #0#8;
      |MENAV "    Error de Entrada ...";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO miraniv; |TIPO 0;
  nivel = #0#5;
  |SI nivel = 1;|Y dig4 = 0;  |AVISO;  |ERROR;  |FINSI;
  |SI nivel = 2;|Y dig5 = 0;  |AVISO;  |ERROR;  |FINSI;
  |SI nivel = 3;|Y dig6 = 0;  |AVISO;  |ERROR;  |FINSI;
  |SI nivel = 4;|Y dig7 = 0;  |AVISO;  |ERROR;  |FINSI;
  |SI nivel = 5;|Y dig8 = 0;  |AVISO;  |ERROR;  |FINSI;
  |SI nivel = 6;|Y dig9 = 0;  |AVISO;  |ERROR;  |FINSI;
|FPRC;

|PROCESO estruct; |TIPO 0;
  |SI #0#6 = 0;  |ACABA;  |FINSI;
  |HAZ cue_per;
  |SI sw_error ! 0;
      |MENAV "    No existe la estructura de descarga.";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO cue_per; || calcula la cuenta de perdidas y ganancias
  sw_error = 0;
  |ABRE #6;
  |DDEFECTO #6;
  #6#0 = #0#6;
  |LEE 040#6=;
  |SI FSalida ! 0;
      sw_error = 1;
      |CIERRA #6;
      |ACABA;
  |FINSI;
  cue_perdi = #6#2;
  dig_perdi = #6#3;
  |SI #0#4 = "S";
      acor_c = cue_perdi;
      acor_d = dig_perdi;
      |HAZ acortacuen;
      cue_perdi = acor_c;
      dig_perdi = acor_d;
  |FINSI;
  |CIERRA #6;
|FPRC;

|PROCESO conexi;
  enTExi = 1;
  d_mes = #0#7;
  h_mes = #0#8;
  estru = #0#6;
  r_emp = empre;
  r_per = ejerc;
  r_eje = #30#26;
  r_nom = #30#1;
  |SUB_EJECUTA "caexicon.run";
|FPRC;

|| ========================================================================
||                     CALCULOS de FICHEROS AUXILIARES
|| ========================================================================
|PROCESO grabatra;  || ... Graba temporal de Agrupaciones Exp0;

 |SI #1#5 = "A"; |O #1#5 = "P";
     tipoAD = "A"; contaAD = 13;
     |SI #1#5 = "P"; contaAD = 14; |FINSI;
 |FINSI;                             || Situacion
 |SI #1#5 = "D"; |O #1#5 = "H";
     tipoAD = "D"; contaAD = 15;
     |SI #1#5 = "H"; contaAD = 16; |FINSI;
 |FINSI;                             || Explotacion

  #3#0 = tipoAD;
  #3#1 = #0contaAD + 1;
  |LEE 101#3=;
  |SI FSalida ! 0;
      |DDEFECTO #3;
      #3#0 = tipoAD;
      #3#1 = #0contaAD + 1;
      |GRABA 020#3c;
  |FINSI;

  |SI #1#5 = "A"; |O #1#5 = "D";
      #3#2  = 1;
      #3#3  = #1#0;  || Cuenta
      #3#4  = #1#1;  || Digito
      #3#5  = #1#2;  || Descripcion
      #3#6  = saldo; || Cantidad
  |SINO;
      #3#8  = 1;
      #3#9  = #1#0;  || Cuenta
      #3#10 = #1#1;  || Digito
      #3#11 = #1#2;  || Descripcion
      #3#12 = saldo; || Cantidad
  |FINSI;

  |GRABA 020#3a;
  |LIBERA #3;
  swAlgun = 1;

  || ***  sumasaldo;

  #0contaAD = #0contaAD + 1;     || Suma 1 al contador
  contaAD = contaAD + 4;
  |SI nSwSuma = 0;
      #0contaAD = #0contaAD + saldo; || Suma el Saldo al total A. P. D. o H.
  |FINSI;
|FPRC;

|PROCESO exis_expl;
  descar = #7des_exi - #7has_exi; || iniciales - finales
  |SI descar = 0; |ACABA; |FINSI;
  |SI descar ] 0;
      act_cuent = #7#5;     || cuenta descarga D
      act_nivel = #7#6;     || digito descarga D
  |SINO;
      act_cuent = #7#8;     || cuenta descarga H
      act_nivel = #7#9;     || digito descarga H
  |FINSI;

  eaCuenta = act_cuent;
  |HAZ SacaNivel;
  act_nivel = enNivel;

  acor_c = act_cuent; acor_d = act_nivel;
  |HAZ acortacuen;
  act_cuent = acor_c; act_nivel = acor_d;

  |SI act_cuent = #1#0;
      sw_exp = 1;
      |SI descar ] 0;
          debe = debe + descar;
      |SINO;
          haber = haber - descar;
      |FINSI;
       saldo = debe - haber;
  |FINSI;
|FPRC;

|DEFBUCLE exis2;
 #7#1;
 ;
 #0#6, 01;
 #0#6, 99;
 e;
 NULL, exis_expl;
|FIN;

|PROCESO exis_3000;
  sw_exp  = 1;
  saldo   = saldo + #7has_exi;
|FPRC;

|DEFBUCLE exis1;
 #7#2;
 ;
 #0#6, #1#0;
 #0#6, alar_c;
 e;
 NULL,exis_3000;
|FIN;

|PROCESO calcular;
 saldo   = 0;
 debe    = 0;
 haber   = 0;
 sw_exp  = 0;
 alfa1   = #1#0 % 101;
 |SI alfa1 = "3";                 |BUCLE exis1; |FINSI;
 |SI alfa1 = "6"; |O alfa1 = "7"; |BUCLE exis2; |FINSI;

 |SI sw_exp = 1; |ACABA; |FINSI;

 |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
       y = x + 1;
       debe  = debe  + #1x;
       haber = haber + #1y;
       saldo = debe - haber;
 |SIGUE;

|FPRC;

|PROCESO grabar;
  |HAZ digitos2;
  |SI d_error ! 0;  |ACABA;  |FINSI;
  |SI #0#4 = "N";  |Y #1#3 = "N";                     |ACABA;  |FINSI;
  |SI #0#4 = "S";  |Y #1#55 > nivel;                  |ACABA;  |FINSI;
  |SI #0#4 = "S";  |Y #1#3 = "N";  |Y #1#55 ! nivel;  |ACABA;  |FINSI;

  alar_c = #1#0;
  |HAZ alargacuen;

      |HAZ calcular;

      alfa2 = saldo; |QBF alfa2;
      |SI alfa2 = "0"; |O alfa2 = "-0";    saldo = 0;   |FINSI;

      |SI saldo ! 0;
          |SI #1#5 = "H"; |O #1#5 = "P";
              saldo = - saldo;
          |FINSI;
          |HAZ grabatra;
      |FINSI;
|FPRC;

|DEFBUCLE cabaresu0;
 #1#1;
 ;
 "            ";
 "zzzzzzzzzzzz";
 e;
 NULL, grabar;
|FIN;

|| ***********************************************************************
|PROCESO gr_final; || Grabar totales
 debe    = 0;
 haber   = 0;
 nSwSuma = 0;

|| ... Grabar Total Debe y Total haber
  nSwSuma = 1;
  saldo   = #0#17; || Total Debe
  #1#0  = "";
  #1#2 = "SUMA DEBE "; #1#5 = "A";
  |HAZ grabatra;
  nSwSuma = 1;
  saldo   = #0#18; || Total Ingresos
  #1#0  = "";
  #1#2 = "SUMA HABER "; #1#5 = "P";
  |HAZ grabatra;
  nSwSuma = 0;

|| ..................................................................

|| ...  saldo   = saldo + ( #0#17 - #0#18);  || saldo129 + activo - pasivo
                                             || explota = saldo;

  saldo   = #0#17 - #0#18;  || activo - pasivo

  alfa2 = saldo; |QBF alfa2;
  |SI alfa2 = "0"; |O alfa2 = "-0";    saldo = 0;   |FINSI;

|| Grabar total ACTIVO Y PASIVO
 #1#0    = "";
 |SI saldo ] 0; #1#2 = "BENEFICIO"; #1#5 = "P";                 |FINSI;
 |SI saldo < 0; #1#2 = "PERDIDA  "; #1#5 = "A"; saldo = - saldo;|FINSI;
 |SI saldo = 0; #1#2 = "RESULTADO CERO "; #1#5 = "A";           |FINSI;
 |HAZ grabatra; || ....................... Graba la cuenta 129. en blanco

|| Grabar total DEBE Y HABER

|| ... Grabar Total Gastos y Total Ingresos
  nSwSuma = 1;
  saldo   = #0#19; || Total Gastos
  #1#0  = "";
  #1#2 = "SUMA GASTOS "; #1#5 = "D";
  |HAZ grabatra;
  nSwSuma = 1;
  saldo   = #0#20; || Total Ingresos
  #1#0  = "";
  #1#2 = "SUMA INGRESOS "; #1#5 = "H";
  |HAZ grabatra;
  nSwSuma = 0;

|| ..................................................................

                                       || saldo = explota;
  saldo   = #0#19 - #0#20;             || debe - haber

  alfa2 = saldo; |QBF alfa2;
  |SI alfa2 = "0"; |O alfa2 = "-0";    saldo = 0;   |FINSI;

 #1#0  = "";
 |SI saldo > 0; #1#2 = "PERDIDA  "; #1#5 = "H";                 |FINSI;
 |SI saldo < 0; #1#2 = "BENEFICIO"; #1#5 = "D"; saldo = - saldo;|FINSI;
 |SI saldo = 0; #1#2 = "RESULTADO CERO "; #1#5 = "D";           |FINSI;
 |HAZ grabatra; || ....................... Graba la cuenta 129. en blanco

|FPRC;

|| ***************************************************************************
|| *********************  CALCULO DE IMPRESION *******************************
|| ***************************************************************************
|PROCESO impresio;
  |SI sw = 0;
      |SI menu2 = 1; |HAZ panta; |FINSI;
      sw = 1;
  |FINSI;
  sw_inf = 0;
  #3#7  = 0;
  #3#13 = 0;
  |SI #3#0 = "A";
      |SI #0#17 ! 0; #3#7  = ( #3#6  * 100  / #0#17 ) ! 2; |FINSI;
      |SI #0#18 ! 0; #3#13 = ( #3#12 * 100  / #0#18 ) ! 2; |FINSI;
  |SINO;
      |SI #0#19 ! 0; #3#7  = ( #3#6  * 100  / #0#19 ) ! 2; |FINSI;
      |SI #0#20 ! 0; #3#13 = ( #3#12 * 100  / #0#20 ) ! 2; |FINSI;
  |FINSI;
  |SI #3#2 = 1; |Y #3#3 = "            "; sw_inf = 1; |FINSI;
  |SI #3#8 = 1; |Y #3#9 = "            ";
      |SI sw_inf = 0;
          sw_inf = 2;
      |SINO;
          sw_inf = 3;
      |FINSI;
  |FINSI;

  |SI #3#3 = doce; |Y #3#2 = 1;
      aAlfa1 = #3#5; aAlfa1 = aAlfa1 % 101;
      |SI aAlfa1 = "S";
          #3#7 = 0;
          eaT1 = #3#5;
          enN1 = #3#6;
      |SINO;
          eaT3 = #3#5;
          enN3 = #3#6;
          enN4 = #3#7;
          nLaCol  = 1;
      |FINSI;
  |FINSI;
  |SI #3#9 = doce; |Y #3#8 = 1;
      aAlfa1 = #3#11; aAlfa1 = aAlfa1 % 101;
      |SI aAlfa1 = "S";
          #3#13 = 0;
          eaT2 = #3#11;
          enN2 = #3#12;
      |SINO;
          eaT3 = #3#11;
          enN3 = #3#12;
          enN4 = #3#13;
          nLaCol  = 2;
      |FINSI;
  |FINSI;

  |SI menu2 = 1;
      |HAZ operapanta;
      |SI sw_error = 1; |ERROR10; |FINSI;
  |FINSI;
  |SI menu2 = 2; |PRINT; |FINSI;
  sw = 1;
|FPRC;

|DEFBUCLE cabaresu2;
 #3#1;
 ;
 tipo, 00000;
 tipo, 99999;
 be;
 NULL, impresio;
|FIN;

|PROCESO cabaresu1;
  nFull = 0;
  sw_error = 0;
  |SI #0#21 = "S";  |O #0#21 = "A";
      titulo = "     < BALANCE DE SITUACION >     ";
      var1   = "TOTAL ACTIVO .................";
      var2   = "TOTAL PASIVO .................";
      tipo = "A";
      sw = 0;
      nLaCol = 0;
      enN1 = 0; enN2 = 0; enN3 = 0; enN4 = 0;
      eaT1 = ""; eaT2 = ""; eaT3 = 0;
      |BUCLE cabaresu2;
      |SI sw_error = 1; |ACABA; |FINSI;
      |SI sw = 1;
          tot1   = #0#17;
          tot2   = #0#18;
          |SI menu2 = 2;
              sw_inf = nLaCol;
              |PIEINF;
          |SINO;
              swfinal = 1;
              |HAZ cambiapag;
              |SI sw_error = 1; |ACABA; |FINSI;
              swfinal = 0;
          |FINSI;
      |FINSI;
  |FINSI;
  |SI #0#21 = "P";  |O #0#21 = "A";
      titulo = "< CUENTA DE PERDIDAS Y GANANCIAS >";
      var1   = "TOTAL DEBE ...................";
      var2   = "TOTAL HABER ...................";
      tipo = "D";
      sw = 0;
      nLaCol = 0;
      enN1 = 0; enN2 = 0; enN3 = 0; enN4 = 0;
      eaT1 = ""; eaT2 = ""; eaT3 = 0;
      |BUCLE cabaresu2;
      |SI sw_error = 1; |ACABA; |FINSI;
      |SI sw = 1;
          tot1   = #0#19;
          tot2   = #0#20;
          |SI menu2 = 2;
              sw_inf = nLaCol;
              |PIEINF;
          |SINO;
              swfinal = 1;
              |HAZ cambiapag;
              |SI sw_error = 1; |ACABA; |FINSI;
              swfinal = 0;
          |FINSI;
      |FINSI;
  |FINSI;
|FPRC;


|| ******************************** CALCULO TIPO 3
|PROCESO HazTodo;

  |HAZ antesque;
  |SI #cabaresu#23 = "S"; |HAZ conexi; |FINSI;

  eaIa = #canempre#21;
  enM1 = #0#0;
  enM2 = #0#1;
  |HAZ LeeDatosEmpresa;

   |SI enMAper ! dig1;
       |SI #0#1 < enMAper; |Y #0#1 ! 0;
           |ACABA;
       |FINSI;
   |FINSI;


  ||... Modificar Cuentas a balance segun saldo
  r_emp = empre;
  r_per = ejerc;
  eldesde = (#0#0 * 3) + 11;
  elhasta = (#0#1 * 3) + 11;
  enSwOper = 0;
  |SUB_EJECUTA "carecsig";

  eldesde = (#0#0 * 3) + 9;
  elhasta = (#0#1 * 3) + 9;
  des_exi = #0#7 + 11;
  has_exi = #0#8 + 11;

  |ABRE #8;
  |DDEFECTO #8;
  #8#0 = 1;
  |LEE 000#8=;
  |CIERRA #8;
  |SI #8#2 = "S";  |Y #8#3 = "N";  |Y aparam = "";
      pathbal = dirfich0;
      balance = 1;
      |SI #8#10 = "S";
          enSwOper = 2;
          |SUB_EJECUTA "carecsig";
          enSwOper = 0;
          |SI enECuadre = 1; |ACABA; |FINSI;
          |NOME_DAT #1 eaNomFCta;
      |SINO;
           |SUB_EJECUTA "careccue";
      |FINSI;
  |FINSI;

  || ---------------------------------------------------------------------

  nivel = #0#5;
  |HAZ cue_per;

  |ABRE #3; |CIERRA #3; |DELINDEX #3;

  |PDEFECTO #cabaresu,13,21;

  |ABRET #1;
  |ABRE #3;
  swAlgun = 0;
  |BUCLE cabaresu0;
  |HAZ gr_final;
  |CIERRA #3;
  |CIERRAT #1;

  |SI swAlgun = 1;
      |HAZ cabaresu1;
  |FINSI;

  |DELINDEX #3;
|FPRC;

|PROCESO impre; |TIPO 3;

  |SI enSwMvo = 0; |Y aparam ! "Impuesto";
      |MENU menu;
      menu2 = FSalida;
      |SI menu2 [ 0; |ACABA; |FINSI;
  |SINO;
      menu2 = 2;
  |FINSI;

  |SI menu2 = 2;
      |SI enSwMvo = 0; |Y aparam ! "Impuesto";
          |IMPRE 0;
          |SI FSalida ! 0;  |ACABA;  |FINSI;
      |FINSI;

      |INFOR informe;
      |SI FSalida ! 0;
        |MENAV "     Error en informe.";
        |SI enSwMvo = 0; |Y aparam ! "Impuesto"; |FINIMP; |FINSI;
        |ACABA;
      |FINSI;
  |FINSI;

  |SI #48#27 = "S"; |Y enSwMvo = 0; |Y aparam ! "Impuesto";
     |HAZ MultiEmpresa;
  |SINO;
     |SI aparam ! "Impuesto"; |DFICE dirfich0; |FINSI;
     |HAZ HazTodo;
  |FINSI;

  |SI menu2 = 2;
     |FININF;
     |SI enSwMvo = 0; |Y aparam ! "Impuesto"; |FINIMP;|FINSI;
  |FINSI;
|FPRC;

|| *************************************************************************
|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;
   nYaLoHeDicho = 0;
   |PATH_DAT #1 dirfich0;
   |PATH_DAT #6 dirfich0;
   |PATH_DAT #7 dirfich0;
   |PATH_DAT #8 dirfich0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 000001;
 999999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;
|| *******************************************************************
|| *** Rutinas Varias

|PROCESO alargacuen;
  |QBF alar_c;
  alar_c = alar_c + "99999999999999";
  alar_c = alar_c % 112;
|FPRC;

|PROCESO acortacuen;
  |SI nivel < acor_d;
      |SI nivel = 1;  traraf = 100 + dig4;  |FINSI;
      |SI nivel = 2;  traraf = 100 + dig5;  |FINSI;
      |SI nivel = 3;  traraf = 100 + dig6;  |FINSI;
      |SI nivel = 4;  traraf = 100 + dig7;  |FINSI;
      |SI nivel = 5;  traraf = 100 + dig8;  |FINSI;
      acor_c = acor_c % traraf;
      acor_d = nivel;
      acor_c = acor_c + "              ";
      acor_c = acor_c % 112;
  |FINSI;
|FPRC;

|PROCESO operapanta;   || Operaciones por pantalla;
 |SI lin > 1300;
     swfinal = 0;
     |HAZ cambiapag;  || Cambia de pagina
     |SI sw_error = 1; |ACABA; |FINSI;
 |FINSI;

 |C_V #3#3, 1,"N"; |C_V #3#6, 1,"N";
 |C_V #3#7, 1,"N"; |C_V #3#9, 1,"N";
 |C_V #3#12,1,"N"; |C_V #3#13,1,"N";
 |C_V #3#5,1,"N";  |C_V #3#11,1,"N";

 |SI #3#2 = 1;
     |C_V #3#3, 1,"S"; |C_V #3#6, 1,"S"; |C_V #3#7, 1,"S";
 |FINSI;
 |SI #3#8 = 1;
     |C_V #3#9, 1,"S"; |C_V #3#12, 1,"S"; |C_V #3#13, 1,"S";
 |FINSI;

 |SI #3#2 = 1; |Y #3#3 = "            ";
     |C_V #3#3, 1,"N"; |C_V #3#5, 1,"S";
     aAlfa1 = #3#5; aAlfa1 = aAlfa1 % 101;
     |SI aAlfa1 = "S"; |C_V #3#7,1,"N"; |FINSI;
 |FINSI;

 |SI #3#8 = 1; |Y #3#9 = "            ";
     |C_V #3#9, 1,"N"; |C_V #3#11, 1,"S";
     aAlfa1 = #3#11; aAlfa1 = aAlfa1 % 101;
     |SI aAlfa1 = "S"; |C_V #3#13,1,"N"; |FINSI;
 |FINSI;

 Pos = lin;
 |PINDA #0#3;         || Pinta las lineas
 Pos = -1;
 lin = lin + 100;     || Posicionar una linea mas abajo
|FPRC;

|PROCESO cambiapag;
 sw_error = 0;
 lin = 100;
 |PAUSA;
 |SI 1 = FSalida;|O 7 = FSalida;
     sw_error = 1;
 |SINO;
     |SI swfinal = 0; |HAZ panta; |FINSI;
 |FINSI;
|FPRC;

|PROCESO panta;
    lin = 100;
    |ATRI I;
    |PINPA #0#3;
    alfa1 = " "; alfa1 = alfa1 * 80;
    |PINTA 0401, alfa1;
    |ATRI S;
    |PINTA 0401, #canempre#2;
    |PINTA 0407, #canempre#1;
    |PINTA 0472, "A¤o: ";
    |PINTA 0477, #canempre#26;
    |ATRI I;
    |PINTA 0514, #0#2;  || mes Inicio
    |PINTA 0525, #0#3;  || mes Final
    |PINTA 0564, #0#12; || Fecha
    |ATRI 0;
    var1 = var1 % 120;
    var2 = var2 % 120;
    |ATRI R;
    |PINTA 0725, titulo;
    |PINTA 2203, "                                    ";
    |PINTA 2204, var1;
    |PINTA 2243, "                                    ";
    |PINTA 2244, var2;
    |ATRI 0;
    |SI tipo = "A";
        |C_V #0#17,1,"S"; |PINTA #0#17;
        |C_V #0#18,1,"S"; |PINTA #0#18;
    |SINO;
        |C_V #0#19,1,"S"; |PINTA #0#19;
        |C_V #0#20,1,"S"; |PINTA #0#20;
    |FINSI;
|FPRC;

|PROCESO MesNom;
     nNume1 = nNume2 + 2;
     |SI #0nNume2 ! 0; |Y #0nNume2 ! 13;
         NumMes = dig1 + (#0nNume2 - 1);
         |SI NumMes > 12; NumMes = NumMes - 12; |FINSI;
     |SINO;
          NumMes = #0nNume2;
     |FINSI;
     |HAZ NombreMes;
     #0nNume1 = NombreMes;
|FPRC;

|PROGRAMA;
 aparam = PARAMETRO; |QBF aparam;

 |SI enSwMvo = 1;
     |HAZ inicio_b;
     |DDEFECTO #0;
     alfa1 = efDFecM; alfa1 = alfa1 % 402; #0#0 = alfa1;
     alfa1 = efDFecM; alfa1 = alfa1 % 105;
     |SI alfa1 = "01.01"; #0#0 = 0; |FINSI;
     alfa1 = efHFecM; alfa1 = alfa1 % 402; #0#1 = alfa1;
     nNume2 = 0; |HAZ MesNom;
     nNume2 = 1; |HAZ MesNom;
     #0#5   = dig3; nivel = #0#5;
     #0#6   = 1; |SI Haybasica = 1; |Y eaDepar = "S"; #0#6 = 0; |FINSI;
     |HAZ impre;
 |SINO;
      |SI aparam = "Impuesto";
          |DDEFECTO #0;
          nNume2 = 0; |HAZ MesNom;
          nNume2 = 1; |HAZ MesNom;
          #0#5   = dig3 - 1; nivel = #0#5;
          #0#6   = 1; |SI Haybasica = 1; |Y eaDepar = "S"; #0#6 = 0; |FINSI;
          |PATH_DAT #1 dirfich0;
          |PATH_DAT #6 dirfich0;
          |PATH_DAT #7 dirfich0;
          |PATH_DAT #8 dirfich0;
          |HAZ impre;
      |SINO;
          |MANTE #0;
      |FINSI;
 |FINSI;
|FPRO;
