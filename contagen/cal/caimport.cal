|FICHEROS;
   caimport #0;

   caenlace #10;
   caconasi #11;
   camaasic #12;
   calibros #13;
   caclipro #14;
   cacuenta #15;

   cacfgimp #20;
|FIN;

|VARIABLES;
   aAlfa     = "";
   aAlfa1    = "";
   aAlfa2    = "";
   aAlfa3    = "";
   aLong     = "";
   aCuenta   = "";
   aIP       = "";
   aHoja     = "";

   nCalc     = 0;
   nCalc1    = 0;
   nLong     = 0;
   nApunte   = 0;
   nMaxDigit = 0;
   nCont     = 0;
   nCont1    = 0;
   nAntDocum = 0;
   nNumFra   = 0;
   nHandle   = 0;
   nNivel    = 0;
   nLinea    = 0;

   nDiario    = 0;
   fFecha     = @;
   nNumDocum  = 0;
   aDescrAsto = "";
   aSigno     = "";
   aCodigo    = "";
   nTotalF    = 0;
   fFechaF    = @;

{5}nBase      = 0;
{5}nTipo      = 0;
{5}nCuota     = 0;
|FIN;

|PROCESO MiraHoja;
   aAlfa = #0Campo; |QBF aAlfa;
   |SI aAlfa = "";
      aAlfa = "F*.csv";
      |BROWSE aAlfa;
      aAlfa = aAlfa % 0299;
      #caimport#Campo# = aAlfa; |PINTA #caimport#Campo#;
   |FINSI;

   aAlfa = #0Campo; |QBF aAlfa;
   |XABRE "CE", aAlfa, nHandle;
   |SI FSalida < 0;
      |SI Campo = 1;
         |MENAV "    Hoja excel de datos de facturacion inexistente";
      |SINO;
         |MENAV "    Hoja excel de datos de clientes y proveedores inexistente";
      |FINSI;
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO Contador;
   |ABRE #caconasi;
   |LEE 101#caconasi.p;
   |SI FSalida ! 0;
      |DDEFECTO #caconasi;
      |GRABA 020#caconasi.n;
      |LEE 101#caconasi.p;
   |FINSI;

   #caconasi#1 = #caconasi#1 + 1; nNumDocum = #caconasi#1;
   aAlfa = "|NC"; nCalc = #caconasi#aAlfa#;
   |SI nCalc > 2; #caconasi#2 = #caconasi#2 + 1; |FINSI;

   |GRABA 020#caconasi.a; |LIBERA #caconasi;
   |CIERRA #caconasi;

   |ABRE #camaasic;
   #camaasic#0 = #caenlace#0;
   #camaasic#1 = #caenlace#1;
   #camaasic#2 = nNumDocum;
   |LEE 000#camaasic.=;
   |SI FSalida = 0; nNumDocum = -1; |FINSI;
   |CIERRA #camaasic;
|FPRC;

|PROCESO SacaNivel;
   nNivel = 0;
   aLong = aCuenta % 0; nLong = aLong;

   |SI #48#14 = nLong; nNivel = 1; |FINSI;
   |SI #48#15 = nLong; nNivel = 2; |FINSI;
   |SI #48#16 = nLong; nNivel = 3; |FINSI;
   |SI #48#17 = nLong; nNivel = 4; |FINSI;
   |SI #48#18 = nLong; nNivel = 5; |FINSI;
   |SI #48#19 = nLong; nNivel = 6; |FINSI;
|FPRC;

|PROCESO FiltraCeros;
   aAlfa1 = aAlfa % 0101;
   |PARA; |SI aAlfa1 = "0"; |HACIENDO;
      aAlfa  = aAlfa % 0299;
      aAlfa1 = aAlfa % 0101;
   |SIGUE;
|FPRC;

|PROCESO SacaDatosHojaGest;
   || Recojo los datos de la clave del registro
   aAlfa = "A" + nLinea;  |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; nDiario = aAlfa;
   ||aAlfa = "B" + nLinea;  |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; fFecha = aAlfa;
   aAlfa = "AY" + nLinea;  |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; fFecha = aAlfa;
   aAlfa = "C" + nLinea;  |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; nNumDocum = aAlfa;
   aAlfa = "K" + nLinea;  |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; aDescrAsto = aAlfa;
   aAlfa = "L" + nLinea;  |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; aSigno  = aAlfa;
   aAlfa = "T" + nLinea;  |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; aCodigo = aAlfa;
   aAlfa = "V" + nLinea;  |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa;
   |HAZ FiltraCeros; nBase1  = aAlfa;
   aAlfa = "W" + nLinea;  |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa;
   |HAZ FiltraCeros; nBase2  = aAlfa;
   aAlfa = "X" + nLinea;  |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa;
   |HAZ FiltraCeros; nBase3  = aAlfa;
   aAlfa = "Y" + nLinea;  |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa;
   |HAZ FiltraCeros; nBase4  = aAlfa;
   aAlfa = "Z" + nLinea;  |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa;
   |HAZ FiltraCeros; nBase5  = aAlfa;
   aAlfa = "AA" + nLinea; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; nTipo1  = aAlfa;
   aAlfa = "AB" + nLinea; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; nTipo2  = aAlfa;
   aAlfa = "AC" + nLinea; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; nTipo3  = aAlfa;
   aAlfa = "AD" + nLinea; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; nTipo4  = aAlfa;
   aAlfa = "AE" + nLinea; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; nTipo5  = aAlfa;
   aAlfa = "AF" + nLinea; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa;
   |HAZ FiltraCeros; nCuota1  = aAlfa;
   aAlfa = "AG" + nLinea; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa;
   |HAZ FiltraCeros; nCuota2  = aAlfa;
   aAlfa = "AH" + nLinea; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa;
   |HAZ FiltraCeros; nCuota3  = aAlfa;
   aAlfa = "AI" + nLinea; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa;
   |HAZ FiltraCeros; nCuota4  = aAlfa;
   aAlfa = "AJ" + nLinea; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa;
   |HAZ FiltraCeros; nCuota5  = aAlfa;
   aAlfa = "AU" + nLinea; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa;
   |HAZ FiltraCeros; nTotalF  = aAlfa;
   aAlfa = "AY" + nLinea; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; fFechaF = aAlfa;

   aAlfa = "    Procesando apunte " + (("00" + nDiario) % -102) + "-";
   aAlfa = aAlfa + fFecha + "-";
   aAlfa = aAlfa + (("000000" + nNumDocum) % -106);
   |MENSA aAlfa;
|FPRC;

|PROCESO GrabaDefectos;
   |DDEFECTO #caenlace;
   #caenlace#0 = nDiario;    || Diario
   #caenlace#1 = fFecha;     || Fecha
   #caenlace#2 = nNumDocum;  || Documento
   #caenlace#3 = nApunte;  nApunte = nApunte + 1;  || Asiento
   #caenlace#7 = aDescrAsto; || Descripcion asiento
   |SI aSigno = "H";
      #caenlace#6  = #cacfgimp#3; || Codigo concepto repercutido
      #caenlace#10 = "R";    || Soportado / Repercutido
      #caenlace#11 = #cacfgimp#1; || Libro de iva repercutido
   |SINO;
      #caenlace#6  = #cacfgimp#8; || Codigo concepto soportado
      #caenlace#10 = "S";    || Soportado / Repercutido
      #caenlace#11 = #cacfgimp#6; || Libro de iva soportado
   |FINSI;

   |SI nAntDocum ! nNumDocum;
      #calibros#0 = #caenlace#11;
      |LEE 101#calibros.=;
      |SI FSalida = 0;
         nNumFra = #calibros#4;
         |SI aSigno = "H";
            #calibros#4 = #calibros#4 + 1;
         |SINO;
            #calibros#4 = #calibros#4 + #calibros#3;
         |FINSI;
         |GRABA 020#calibros.a; |LIBERA #calibros;
      |FINSI;
      nAntDocum = nNumDocum;
   |FINSI;
   #caenlace#13 = nNumFra;
   #caenlace#37 = #48#2;   || Empresa destino
   #caenlace#38 = #48#3;   || Periodo destino
|FPRC;

|PROCESO GrabaPuente;
   |HAZ SacaDatosHojaGest;

   |SI nTipo1 ! 16; nTipo1 = 0; nCuota1 = 0; |FINSI;
   |SI nTipo2 ! 16; nTipo2 = 0; nCuota2 = 0; |FINSI;
   |SI nTipo3 ! 16; nTipo3 = 0; nCuota3 = 0; |FINSI;
   |SI nTipo4 ! 16; nTipo4 = 0; nCuota4 = 0; |FINSI;
   |SI nTipo5 ! 16; nTipo5 = 0; nCuota5 = 0; |FINSI;

   |SI nTipo1 ! 0; |Y nCuota1 = 0; nTipo1 = 0; |FINSI;
   |SI nTipo2 ! 0; |Y nCuota2 = 0; nTipo2 = 0; |FINSI;
   |SI nTipo3 ! 0; |Y nCuota3 = 0; nTipo3 = 0; |FINSI;
   |SI nTipo4 ! 0; |Y nCuota4 = 0; nTipo4 = 0; |FINSI;
   |SI nTipo5 ! 0; |Y nCuota5 = 0; nTipo5 = 0; |FINSI;

   nTotalF =           nBase1 + nCuota1 + nBase2 + nCuota2;
   nTotalF = nTotalF + nBase3 + nCuota3 + nBase4 + nCuota4;
   nTotalF = nTotalF + nBase5 + nCuota5;
   nTotalF = nTotalF ! 2;

   nApunte = 1;
   || Registro del total factura
   |HAZ GrabaDefectos;
   |SI aSigno = "H";
      aAlfa = #cacfgimp#4;
      #caclipro#23 = "C";
   |SINO;
      aAlfa = #cacfgimp#9;
      #caclipro#23 = "P";
   |FINSI;
   |QBF aAlfa; aLong = aAlfa % 0; nLong = aLong;
   aAlfa1 = aCodigo; |QBF aAlfa1; aAlfa1 = aAlfa1 % 0;
   nCalc  = aAlfa1; nLong = nLong + nCalc;
   nCalc = nMaxDigit - nLong;
   aCuenta = aAlfa + ("0" * nCalc) + aCodigo;
   |HAZ SacaNivel;

   #caclipro#10 = aCuenta;
   #caclipro#11 = nNivel;
   |LEE 000#caclipro.=;
   |SI FSalida ! 0; |DDEFECTO #caclipro; |FINSI;

   #caenlace#4 = aCuenta;
   #caenlace#5 = nNivel;
   #caenlace#26 = "F";     || Tipo de acumulador
   |SI aSigno = "H";
      #caenlace#8 = "D";        || Signo del apunte
      |SI nTotalF < 0;
         nTotalF = - nTotalF;    || Importe del apunte
         #caenlace#26 = "f";         || Tipo de acumulador
         #caenlace#8 = "H";        || Signo del apunte
      |FINSI;
   |SINO;
      #caenlace#8 = "H";        || Signo del apunte
      |SI nTotalF < 0;
         nTotalF = - nTotalF;    || Importe del apunte
         #caenlace#26 = "f";         || Tipo de acumulador
         #caenlace#8 = "D";        || Signo del apunte
      |FINSI;
   |FINSI;
   #caenlace#9 = nTotalF;    || Importe del apunte
   #caenlace#27 = 0;       || N§ de acumulador
   #caenlace#51 = fFechaF; || Fecha de Operacion
   #caenlace#29 = #caenlace#1; || Fecha de Registro
   #caenlace#43 = #caclipro#1; || Nombre cliente/proveedor
   #caenlace#44 = #caclipro#9; || CIF cliente/proveedor
   |GRABA 020#caenlace.n;

   ||Registros de bases y cuotas de iva
   |PARA nCont = 1; |SI nCont [ 5; |HACIENDO nCont = nCont + 1;
      InBase  = nBase1<-;  InBase  = InBase  + (nCont - 1);
      InTipo  = nTipo1<-;  InTipo  = InTipo  + (nCont - 1);
      InCuota = nCuota1<-; InCuota = InCuota + (nCont - 1);

      |SI nBase ! 0;
         |HAZ GrabaDefectos;
         nCalc = nMaxDigit - 3; nCalc1 = -100 - nCalc;
         |SI aSigno = "H";
            aCuenta = "477";
         |SINO;
            aCuenta = "472";
         |FINSI;
         aCuenta = aCuenta + (("0" * nCalc) % nCalc1);
         |HAZ SacaNivel;

         #caenlace#4 = aCuenta;
         #caenlace#5 = nNivel;
         #caenlace#26 = "I";         || Tipo de acumulador
         |SI aSigno = "H";
            #caenlace#8 = "H";          || Signo del apunte
            |SI nCuota < 0;
               nCuota = 0 - nCuota;
               #caenlace#26 = "i";         || Tipo de acumulador
               #caenlace#8 = "D";          || Signo del apunte
            |FINSI;
         |SINO;
            #caenlace#8 = "D";          || Signo del apunte
            |SI nCuota < 0;
               nCuota = 0 - nCuota;
               #caenlace#26 = "i";         || Tipo de acumulador
               #caenlace#8 = "H";          || Signo del apunte
            |FINSI;
         |FINSI;
         #caenlace#9 = nCuota;       || Importe del apunte
         #caenlace#27 = nCont;       || N§ de acumulador
         #caenlace#51 = fFechaF; || Fecha de Operacion
         #caenlace#29 = #caenlace#1; || Fecha de Registro
         #caenlace#43 = #caclipro#1; || Nombre cliente/proveedor
         #caenlace#44 = #caclipro#9; || CIF cliente/proveedor
         |GRABA 020#caenlace.n;

         |HAZ GrabaDefectos;

         |SI aSigno = "H";
            aCuenta = #cacfgimp#5;
         |SINO;
            aCuenta = #cacfgimp#10;
         |FINSI;
         |HAZ SacaNivel;
         #caenlace#4 = aCuenta;
         #caenlace#5 = nNivel;
         #caenlace#26 = "B";         || Tipo de acumulador
         |SI aSigno = "H";
            #caenlace#8 = "H";          || Signo del apunte
            |SI nBase < 0;
               nBase = 0 - nBase;
               #caenlace#26 = "b";         || Tipo de acumulador
               #caenlace#8 = "D";          || Signo del apunte
            |FINSI;
         |SINO;
            #caenlace#8 = "D";          || Signo del apunte
            |SI nBase < 0;
               nBase = 0 - nBase;
               #caenlace#26 = "b";         || Tipo de acumulador
               #caenlace#8 = "H";          || Signo del apunte
            |FINSI;
         |FINSI;
         #caenlace#9 = nBase;        || Importe del apunte
         #caenlace#27 = nCont;       || N§ de acumulador
         #caenlace#28 = nTipo;       || Tipo de IVA
         #caenlace#51 = fFechaF; || Fecha de Operacion
         #caenlace#29 = #caenlace#1; || Fecha de Registro
         #caenlace#43 = #caclipro#1; || Nombre cliente/proveedor
         #caenlace#44 = #caclipro#9; || CIF cliente/proveedor
         |GRABA 020#caenlace.n;
      |FINSI;
   |SIGUE;
|FPRC;

|PROCESO GrabaCliPro;
   aAlfa = "AC" + nLinea;  |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa;
   #caclipro#23 = aAlfa;

   aAlfa = "A" + nLinea;   |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa;
   aCodigo = aAlfa;
   |SI #caclipro#23 = "C";
      aAlfa = #cacfgimp#4;
   |SINO;
      aAlfa = #cacfgimp#9;
   |FINSI;

   |QBF aAlfa; aLong = aAlfa % 0; nLong = aLong;
   aAlfa1 = aCodigo; |QBF aAlfa1; aAlfa1 = aAlfa1 % 0;
   nCalc  = aAlfa1; nLong = nLong + nCalc;
   nCalc = nMaxDigit - nLong;
   aCuenta = aAlfa + ("0" * nCalc) + aCodigo;
   |HAZ SacaNivel;

   #caclipro#10 = aCuenta;
   #caclipro#11 = nNivel;
   |LEE 101#caclipro.=;
   |SI FSalida ! 0;
      |DDEFECTO #caclipro;
      aAlfa = "AC" + nLinea;  |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa;
      #caclipro#23 = aAlfa;
      #caclipro#10 = aCuenta;
      #caclipro#11 = nNivel;
      |GRABA 020#caclipro.b;
   |FINSI;
   aAlfa = "B" + nLinea;  |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa;
   #caclipro#1 = aAlfa;
   aAlfa = "D" + nLinea;  |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa;
   #caclipro#2 = aAlfa;
   aAlfa = "AE" + nLinea;  |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa;
   aAlfa1 = aAlfa % 0102;
   #caclipro#3 = aAlfa1;
   aAlfa1 = aAlfa % 0303;
   #caclipro#4 = aAlfa1;
   aAlfa = "L" + nLinea;  |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa;
   #caclipro#6 = aAlfa;
   aAlfa = "M" + nLinea;  |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa;
   #caclipro#7 = aAlfa;
   aAlfa = "N" + nLinea;  |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa;
   #caclipro#8 = aAlfa;
   aAlfa = "P" + nLinea;  |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa;
   #caclipro#9 = aAlfa;
   aAlfa = "AD" + nLinea;  |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa;
   #caclipro#24 = aAlfa;
   |GRABA 020#caclipro.a; |LIBERA #caclipro;

   aAlfa = "    Procesando Cliente/Proveedor " + #caclipro#9; |QBF aAlfa;
   aAlfa = aAlfa + " " + #caclipro#1; |QBF aAlfa;
   aLong = aAlfa % 0; nLong = aLong;
   |SI nLong > 80; aAlfa = aAlfa % 0180; |FINSI;
   |MENSA aAlfa;

   #cacuenta#0 = aCuenta;
   #cacuenta#1 = nNivel;
   |LEE 000#cacuenta.=;
   |SI FSalida ! 0;
      |DDEFECTO #cacuenta;
      #cacuenta#0 = aCuenta;
      #cacuenta#1 = nNivel;
      aAlfa = "B" + nLinea;  |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa;
      #cacuenta#2 = aAlfa;
      #cacuenta#3 = "S";
      |GRABA 020#cacuenta.n;
   |FINSI;
|FPRC;

|PROGRAMA;
   |ABRE #cacfgimp;
   |LEE 000#cacfgimp.p;
   |SI FSalida ! 0; |DDEFECTO #cacfgimp; |FINSI;
   |CIERRA #cacfgimp;

   |ABRE #caimport;
   |LEE 101#caimport.p;
   |SI FSalida ! 0;
      |DDEFECTO #caimport;
      |GRABA 020#caimport.b;
   |FINSI;

   |CLS;
   |CABEZA " Importacion datos gestion IN-LINE ";

   |PINPA #0#0; |PINDA #0#0;
   |ENDATOS #1#caimport;
   |SI FSalida = 0;
      |GRABA 020#caimport.a;

      |CARGADLL "agixl97.dll";
      |SI FSalida < 0;
         |MENAV "    No se puede usar agixl97.dll";
      |SINO;
         |MENSA "    Recogiendo datos de clientes/proveedores";
         nMaxDigit = 0;
         |PARA nCont = 14; |SI nCont [ 19; |HACIENDO nCont = nCont + 1;
            |SI #48nCont ] nMaxDigit; nMaxDigit = #48nCont; |FINSI;
         |SIGUE;

         |ABRE #caclipro; |ABRE #cacuenta;

         aAlfa = #0#2; |QBF aAlfa;
         |PARA; |SI aAlfa1 ! "/"; |Y aAlfa1 ! "\"; |HACIENDO;
            aAlfa1 = aAlfa % -101; aAlfa = aAlfa % -299;
            |SI aAlfa1 ! "/"; |Y aAlfa1 ! "\";
               aAlfa2 = aAlfa1 + aAlfa2;
            |FINSI;
         |SIGUE;
         aAlfa = aAlfa2; aAlfa2 = "";
         |PARA; |SI aAlfa1 ! "."; |HACIENDO;
            aAlfa1 = aAlfa % 0101; aAlfa = aAlfa % 0299;
            |SI aAlfa1 ! "."; aAlfa2 = aAlfa2 + aAlfa1; |FINSI;
         |SIGUE;
         aHoja = aAlfa2;

         aAlfa = #0#2; |QBF aAlfa;
         |ALFACALLDLL "agixl97.dll", "carga_book", aAlfa;
         |ALFACALLDLL "agixl97.dll", "selecciona_hoja", aHoja;

         nCont1 = 0;
         |PARA nLinea = 2; |SI nCont1 [ 10; |HACIENDO nLinea = nLinea + 1;
            || Compruebo si la linea est  vacia
            aAlfa = "A" + nLinea; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa;
            |QBF aAlfa;
            |SI aAlfa = "";
               nCont1 = nCont1 + 1;
            |SINO;
               nCont1 = 0;
               |HAZ GrabaCliPro;
            |FINSI;
         |SIGUE;

         |ALFACALLDLL "agixl97.dll", "fin_book", "salva";

         |MENSA "    Recogiendo datos de facturacion";
         aAlfa = "pu" + Usuario; |NOME_DAT #caenlace#aAlfa#;
         |DELINDEX #caenlace;
         |ABRE #caenlace; |ABRE #caconasi; |ABRE #calibros;

         aAlfa = #0#1; |QBF aAlfa;
         |PARA; |SI aAlfa1 ! "/"; |Y aAlfa1 ! "\"; |HACIENDO;
            aAlfa1 = aAlfa % -101; aAlfa = aAlfa % -299;
            |SI aAlfa1 ! "/"; |Y aAlfa1 ! "\";
               aAlfa2 = aAlfa1 + aAlfa2;
            |FINSI;
         |SIGUE;
         aAlfa = aAlfa2; aAlfa2 = "";
         |PARA; |SI aAlfa1 ! "."; |HACIENDO;
            aAlfa1 = aAlfa % 0101; aAlfa = aAlfa % 0299;
            |SI aAlfa1 ! "."; aAlfa2 = aAlfa2 + aAlfa1; |FINSI;
         |SIGUE;
         aHoja = aAlfa2;

         aAlfa = #0#1; |QBF aAlfa;
         |ALFACALLDLL "agixl97.dll", "carga_book", aAlfa;
         |ALFACALLDLL "agixl97.dll", "selecciona_hoja", aHoja;

         nCont1 = 0;
         |PARA nLinea = 2; |SI nCont1 [ 10; |HACIENDO nLinea = nLinea + 1;
            || Compruebo si la linea est  vacia
            aAlfa = "C" + nLinea; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa;
            |QBF aAlfa;
            |SI aAlfa = "";
               nCont1 = nCont1 + 1;
            |SINO;
               nCont1 = 0;
               |HAZ GrabaPuente;
            |FINSI;
         |SIGUE;

         |ALFACALLDLL "agixl97.dll", "fin_book", "salva";
         |DESCARGADLL "agixl97.dll";

         |DFICE aAlfa; |QBF aAlfa;
         aAlfa = "dsapunte.tab;" + aAlfa + ",pu" + Usuario;
         |SUB_EJECUTA aAlfa;
         |CIERRA #caenlace; |DELINDEX #caenlace;
         |CIERRA #caconasi; |CIERRA #calibros;
         |CIERRA #caclipro; |CIERRA #cacuenta;

         |MENSA "    Recalculando saldos";
         |SUB_EJECUTA "carecupe.tab;F";
      |FINSI;
   |FINSI;

   |LIBERA #caimport; |CIERRA #caimport;
|FPRO;
