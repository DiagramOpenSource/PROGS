|FICHEROS;
   cadiario #0;
   camaasic #1;
   camaasil #2;
   cacuenta #4;
   caconimp #8;
   ca8m0004 #404;

   canempre #30;
   caselem1 #998;
   cawdatos #900;
   catmport #901;
   catmpor1 #902;
|FIN;

|VARIABLES;
     ||DSARCHI
     &nArchi = 0;
 {-1}aMenu  = "";
     aMenu1 = "2400";
     aMenu2 = "1";
     aMenu3 = "Archivo documental activado. Elija opci¢n: ";
     aMenu4 = "IAB";
     aMenu5 = "Imprimir";
     aMenu6 = "Archivar";
     aMenu7 = "amBos";
     &nOpcion = 0;
     &aImpresora = "";
     &swPasada = 0;
     &swErrorDSArchi = 0;
     &eaInforme = "";
     &eaVengoDe = "";
     &enSwDesde9 = 1;
     &enSwMeteGrupoSubTipo = 0;
     &eaObservaciones = "";
     nOpSegur = 0;
     SwCierre = 0;
     SwApertura = 0;

   &eaImpresora = "";
   nCalc    = 0;
   nCalc1   = 0;
   nDato    = 0;
   nHandTxt = 0;

   aAlfaX  = "";
   aAlfaX1 = "";
   aAlfaX2 = "";

   nNume1 = 0;
   aNoUlt = "";
   siai = 0;
   &entrada = 1;
   sw = 0;
   fecha = 0;
   fecalf = "";
   mensa1 = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
   traba1 = "";
   varalf = "";
   &nPagDup = 0;
   &nSwPagDup = 0;
   &swlin = 0;
   &descuadre = 0;
   &a_debe = 0;
   &a_haber = 0;
   &tde = 0;
   &tha = 0;
   &hoja = 0;
   d = "D";
   h = "H";
   informe = "";
   alfa1 = "";
   alfa2 = "";
   alfa3 = "";
   alfa4 = "";

   nume1 = 0;
   nume2 = 0;
   nume3 = 0;
   nume4 = 0;

   nSwAlgun = 0;
   Comodin = "";
   Comodin1 = "";
   aAlfa    = "";
   Handle  = 0;
   Numeracion = 0;

   Empresa = 0;
   Periodo = 0;
  primero    = "";
  temporal1  = "";
  aRegistros = "";
  nRegistros = 0;
  nSwDos     = 0;
  fLaFecha1  = @;
  fLaFecha2  = @;
  fLaFecha3  = @;
  fLaFecha4  = @;

  nPara1     = 0;

  &enEjer    = 0;
  &eaTituloL = "";

  nDiaAper     = 0;
  nIgual       = 0;
  nConcepto    = 0;
  aDescripcion = "";
  aSubdep      = "";
  nOpe         = 0;
  nLinea       = 0;
  nDia8 = 0;
  fFec8 = @;
  nDoc8 = 0;

  aCampo21 = "";
  aCampo23 = "";
  nSumo  = 0;
  aIP = "";

  {1}aContiene = "";
|FIN;

|INCLUYE dscont_i;
|INCLUYE dslegv_i;

|| -------------------------------------------------------------------------

|PROCESO DatosEmp; |TIPO 7;
 |ABRE #8;
 |DDEFECTO #8;
 #8#0 = 1;
 |LEE 000#8=;
 |CIERRA #8;
 #0#18 = #8#7; |PINTA #0#18;
 alfa1 = #8#8; |C_M #0#18,1,alfa1;
|FPRC;

|CALCULO nodepar; |TIPO 7;
   |CAMPO_MODIFICA #0#14,1,eaDepar;
   |CAMPO_MODIFICA #0#15,1,eaDepar;
|FCAL;

|CALCULO nosubde; |TIPO 7;
   |CAMPO_MODIFICA #0#16, 1, eaSubde;
   |CAMPO_MODIFICA #0#17, 1, eaSubde;
|FCAL;

|PROCESO desasien;
   |SI #0#13 < #0#12;
      |MENAV "     Error en Introduccion";
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO desdiari;
   |SI #0#7 < #0#6;
      |MENAV "     Error en Introduccion";
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO mayor1;
   |SI #0Campo = "S";
      #0#9 = 0;
      #0#10 = 0;
      |PINTA #0#9;
      |PINTA #0#10;
      |CAMPO_MODIFICA #0#9,1,"N";
      |CAMPO_MODIFICA #0#10,1,"N";
   |SINO;
      |CAMPO_MODIFICA #0#9,1,"S";
      |CAMPO_MODIFICA #0#10,1,"S";
   |FINSI;
|FPRC;

|PROCESO estacua;
   |SI #0#9 ! #0#10;
      |MENAV "      ATENCION descuadre en DEBE y HABER";
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO fechad;
   |SI #0#0 < fec1; |O #0#0 > fec2;
      |MENAV mensa1;
      |ERROR;
      |ACABA;
   |FINSI;
|FPRC;

|PROCESO fechah;
   |SI #0#1 < fec1; |O #0#1 > fec2; |O #0#1 < #0#0;
      |MENAV mensa1;
      |ERROR;
      |ACABA;
   |FINSI;
|FPRC;

|PROCESO AsAper_d; |TIPO 0;
 |C_M #0#21, 0, alfa1;
 |SI alfa1 = "N"; |ACABA; |FINSI;
 alfa1 = #0#21;
 |C_M #0#23, 1, alfa1;
|FPRC;

|| ----------------------------------------------------------------------
|PROCESO VQuants;

  nSwDos     = 0;
  nRegistros = 0;
  primero   = "camaasil";
  temporal1 = fich_alta;
  |LEEDEDEF primero, temporal1;
  |SI FSalida ] 0;
      temporal1 = FSalida % 375;
      aRegistros = (A\temporal1 % 1615);
      nRegistros = aRegistros;
  |FINSI;

  fLaFecha1 = fec2;
  fLaFecha2 = fec2;
  fLaFecha3 = fec2;
  fLaFecha4 = fec2;

  |SI nRegistros > 35000;

      nSwDos = 3;

      alfa1 = fec1;
      alfa2 = alfa1 % 402;
      alfa3 = alfa1 % 704;
      nume1 = alfa2;
      nume1 = nume1 + 2;
      |HAZ CalFecha;
      fLaFecha1 = alfa1;  || .. 31.03

      alfa1 = fec1;
      alfa2 = alfa1 % 402;
      alfa3 = alfa1 % 704;
      nume1 = alfa2;
      nume1 = nume1 + 5;
      |HAZ CalFecha;
      fLaFecha2 = alfa1;  || .. 30.06

      alfa1 = fec1;
      alfa2 = alfa1 % 402;
      alfa3 = alfa1 % 704;
      nume1 = alfa2;
      nume1 = nume1 + 8;
      |HAZ CalFecha;
      fLaFecha3 = alfa1;  || .. 31.09

      alfa1 = fec1;
      alfa2 = alfa1 % 402;
      alfa3 = alfa1 % 704;
      nume1 = alfa2;
      nume1 = nume1 + 11;
      |HAZ CalFecha;
      fLaFecha4 = alfa1;  || .. 31.12

   |FINSI;
|FPRC;

|PROCESO CalFecha;

      |SI nume1 > 12;
          nume1 = nume1 - 12;
          nume2 = alfa3; nume2 = nume2 + 1; alfa3 = nume2;
      |FINSI;
      alfa2 = nume1; alfa2 = ("00" + alfa2) % -102;

      alfa1 = "31.";
      |SI nume1 = 4; |O nume1 = 6; |O nume1 = 9; |O nume1 = 11;
          alfa1 = "30.";
      |SINO;
          |SI nume1 = 2;
              alfa1 = "28.";
              nume2 = alfa3;
              nume3 = nume2 / 4; nume4 = (nume2 / 4) ! 0;
              |SI nume3 = nume4;
                  alfa1 = "29.";
              |FINSI;
          |FINSI;
      |FINSI;

      alfa1 = alfa1 + alfa2 + "." + alfa3;
|FPRC;

|PROCESO Legalia;

     ||... Legalia
     cod1 = eaLegEmp;
     cod2 = eaLegPer;
     emEmp = 1;  || Sin mensaje
     |HAZ leelo;
     |SI swerror ! 0;  |ACABA;   |FINSI;

     enLegEje = enEjercicio;

     |DBASE fich_alta;
     |QBF fich_alta;
     fich_alta = fich_alta + "fich/" + eaLegEmp + eaLegPer + "/";
     |PATH_DAT #0 fich_alta;
     |PATH_DAT #1 fich_alta;
     |PATH_DAT #2 fich_alta;
     |PATH_DAT #4 fich_alta;
     |PATH_DAT #8 fich_alta;
     |PATH_DAT #404 fich_alta;

     |SI eaFormato = "S";
         nSwDos = 0; || no podemos dividir por trimestres
     |SINO;
         |HAZ VQuants;
     |FINSI;

     aNoUlt = "112";
     |SI eaUltNiv = "N";
         |SI dig3 = 1; nNume1 = dig4; |FINSI;
         |SI dig3 = 2; nNume1 = dig4; |FINSI;
         |SI dig3 = 3; nNume1 = dig5; |FINSI;
         |SI dig3 = 4; nNume1 = dig6; |FINSI;
         |SI dig3 = 5; nNume1 = dig7; |FINSI;
         |SI dig3 = 6; nNume1 = dig8; |FINSI;
         nNume1 = 100 + nNume1;
         aNoUlt = nNume1;
     |FINSI;

     |PARA nPara1 = 0; |SI nPara1 [ nSwDos; |HACIENDO nPara1 = nPara1 + 1;

          |ABRE #0;
          #0#8 = 1;
          |LEE 141#0=;
          |SI FSalida ! 0;
              |DDEFECTO #0;
              #0#0 = fec1;
              #0#1 = fec2;
              #0#8 = 1;
              |HAZ LeoConimp8;
              |GRABA 001#0c;
          |FINSI;
          aCampo21 = #0#21;
          aCampo23 = #0#23;

         |SI nSwDos = 0;
             |DDEFECTO #0;
             #0#0 = fec1;
             #0#1 = fec2;
             #0#21 = aCampo21;
             #0#23 = aCampo23;
         |SINO;
             |SI nPara1 = 0;
                 |DDEFECTO #0;
                 #0#0 = fec1;
                 #0#1 = fLaFecha1;
                 #0#21 = aCampo21;
                 #0#23 = aCampo23;
             |SINO;
                 |SI nPara1 = 1; #0#1 = fLaFecha2; |FINSI;
                 |SI nPara1 = 2; #0#1 = fLaFecha3; |FINSI;
                 |SI nPara1 = 3; #0#1 = fLaFecha4; |FINSI;
             |FINSI;
         |FINSI;
         sw = 0;
         #0#5 = efFecLeg;
         #0#8 = 1;
         nPagDup = #0#4;
         nSwPagDup = 0;

         |SI eaFormato ! "S";
             Impresora = eaNomImp + "?"+ eaUnidadBTmp + "\tmp1,,,?Diario";
             |SI nSwDos ! 0;
                 |SI nPara1 = 0;  Impresora = Impresora + "1T"; |FINSI;
                 |SI nPara1 = 1;  Impresora = Impresora + "2T"; |FINSI;
                 |SI nPara1 = 2;  Impresora = Impresora + "3T"; |FINSI;
                 |SI nPara1 = 3;  Impresora = Impresora + "4T"; |FINSI;
             |FINSI;
         |SINO;
             Impresora = eaNomImp + ";" + eaUnidadBTmp +"\Diario";
             Impresora = Impresora + ".pdf";
             nSumo = 1;
         |FINSI;
         #0#18 = eaDatLeg;

         nArchi = 0;
         |HAZ impre;

         efFecha1 = #0#0;
         efFecha2 = #0#1;
         |SI nSumo = 0;
            #0#9  = tde;
            #0#10 = tha;
         |FINSI;
         #0#4 = hoja;
         |SI #0#1 ! fec2;
             #0#0 = #0#1 + 1;
         |SINO;
             #0#0 = fec2;
         |FINSI;

         #0#1  = fec2;
         #0#11 = "S";
         |SI nSwDos ! 0; |Y nPara1 ! 3; #0#11 = "N"; |FINSI;
         |GRABA 001#0a;
         |CIERRA #0;

         |HAZ borrar;

    |SI nSwDos = 0; |O nPara1 = 3;

        |DBASE Comodin1; |QBF Comodin1;
        Comodin1 = Comodin1 + "PresLibr/" + eaLegEmp + eaLegPer + "/";
        |SI eaFormato ! "S";
            Comodin1 = Comodin1 + "DIARIO*.xls";
        |SINO;
            Comodin1 = Comodin1 + "DIARIO*.*";
        |FINSI;

        Numeracion = enNumeLeg;
        |_PDIR Comodin1;
        |PARA; |SI FSalida = 0; |HACIENDO;
               Comodin = Comodin1 % -107; Comodin = Comodin % 0103;
               Numeracion = Comodin;
               Numeracion = Numeracion + 1;
               |_SDIR Comodin1;
        |SIGUE;

        |SI eaFormato ! "S";
            Comodin = eaUnidadBTmp + "/tmp1.xls";
        |SINO;
            Comodin = eaUnidadBTmp + "/Diario.pdf";
        |FINSI;
        |XABRE "C", Comodin, Handle;
        |SI FSalida ] 0;
            |XCIERRA Handle;
            |DBASE Comodin1; |QBF Comodin1;
            Comodin1 = Comodin1 + "PresLibr/";
            |MKDIR Comodin1;
            Comodin1 = Comodin1 + eaLegEmp + eaLegPer + "/";
            |MKDIR Comodin1;
            |SI eaFormato ! "S";
                Comodin1 = Comodin1 + "DIARIO_" + (("000" + Numeracion) % -103) + ".xls";
                aNomFLeg = "DIARIO_" + (("000" + Numeracion) % -103) + ".xls";
            |SINO;
                Comodin1 = Comodin1 + "DIARIO_" + (("000" + Numeracion) % -103) + ".pdf";
                aNomFLeg = "DIARIO_" + (("000" + Numeracion) % -103) + ".pdf";
            |FINSI;
             aAlfa = "";
             |IP_REMOTO aAlfa; |QBF aAlfa;
             |SI aAlfa ! "";
                 |RECIBE_FICHERO Comodin, Comodin1;
             |SINO;
                 |COPIA_FICHERO Comodin, Comodin1;
             |FINSI;
             FSalida = 1;

             |PARA; |SI FSalida ! 0; |HACIENDO;
                    |RFBORRA Comodin;
                    |SI FSalida ! 0;
                        |SI eaFormato ! "S";
                            |MENAV "    Termine la tarea de Excel que tiene activa";
                        |FINSI;
                    |FINSI;
             |SIGUE;
             nTipoL   = 1;
             nNumer   = Numeracion;
             enRegisLeg = 0;
             |HAZ eGrabaHistLegal;
             |SI efFecha3 < efFecha2; efFecha3 = efFecha2; |FINSI;
         |FINSI;
      |FINSI;
   |SIGUE;
|FPRC;

|| *************************************************************************
||                          MEOLLO
|| *************************************************************************
|PROCESO VerAper08_d;
  nIgual = 0;
  |ABRE #404;
  |LEE 001#404p;
  |SI FSalida = 0;
      |SI #404#10 = #1#0; |Y #404#11 = #1#1; |Y #404#12 = #1#2;
          nIgual = 1;
          nDia8 = #404#10;
          fFec8 = #404#11;
          nDoc8 = #404#12;
      |FINSI;
  |FINSI;
  |CIERRA #404;
|FPRC;

|PROCESO AImprimir;
   |SI nOpe = 3; |Y #camaasil#8 ! "D";
       nLinea = nLinea - 1;
       |ACABA;    || No lo imprime
   |FINSI;
   |SI nOpe = 4; |Y #camaasil#8 ! "H";
       nLinea = nLinea - 1;
       |ACABA;    || No lo imprime
   |FINSI;

   swlin = 0;
   |SI #camaasil#8 = "D";
       a_debe = #camaasil#9;
       a_haber = 0;
   |SINO;
       a_debe = 0;
       a_haber = #camaasil#9;
   |FINSI;

   |SI eaUltNiv = "N";
       #camaasil#4 = #camaasil#4 % aNoUlt;
   |FINSI;
   |PRINT; nSwAlgun = 1;
|FPRC;

|PROCESO Leo8m004;
   |SI nOpe = 2;      || Sumar solo ls diferencias
       |SI #404#1 ! #404#8;
           tde = tde + (#404#4 + #404#5) ! EURODB;
           tha = tha + (#404#4 + #404#5) ! EURODB;
       |FINSI;
       |ACABA;
   |FINSI;

   |SI #404#13 < #0#14;  |O #404#13 > #0#15;  |ACABA;  |FINSI;

   #2#0 = #404#10;
   #2#1 = #404#11;
   #2#2 = #404#12;
   #2#21 = #404#13;
   #2#28 = aSubdep;
   |SI nOpe = 0;      ||primer asiento
       #2#3 = #404#0;  || La Linea
       #2#4 = #404#1;  || Cuenta Anterior
       #2#5 = #404#7;
       #2#6 = nConcepto;
       #2#7 = aDescripcion;
       #2#8 = #404#3;
       #2#9 = #404#4 + #404#5;
       #4#2 = #404#2;
       |HAZ AImprimir;
       |SI #404#1 ! #404#8;
           #1#4 = #1#4 + #2#9;
           #1#5 = #1#5 + #2#9;
       |FINSI;
   |FINSI;

   |SI nOpe = 1; |O nOpe = 3; |O nOpe = 4;
                                            ||primer asiento
       |SI #404#1 = #404#8; |ACABA; |FINSI; ||no ya cambio de cuenta

       nLinea = nLinea + 1;
       #2#3 = nLinea;  || La Linea
       #2#4 = #404#1;  || Cuenta Anterior
       #2#5 = #404#7;
       #2#6 = #0#22;
       #2#7 = #0#23;
       |SI #404#3 = "D";
           #2#8 = "H";
       |SINO;
           #2#8 = "D";
       |FINSI;
       #2#9 = #404#4 + #404#5;
       #4#2 = #404#2;
       |HAZ AImprimir;

       nLinea = nLinea + 1;
       #2#3 = nLinea;  || La Linea
       #2#4 = #404#8;  || Cuenta Anterior
       #2#5 = #404#9;
       #2#6 = #0#22;
       #2#7 = #0#23;
       #2#8 = #404#3;
       #2#9 = #404#4 + #404#5;
       #4#0 = #2#4;
       #4#1 = #2#5;
       |LEE 040#4=;
       |SI FSalida ! 0; |DDEFECTO #4; |FINSI;
       |HAZ AImprimir;
   |FINSI;
|FPRC;

|DEFBUCLE Leo8m004;
 #404#1;
 ;
 nDia8, fFec8, nDoc8, 0001;
 nDia8, fFec8, nDoc8, 9999;
 ;
 NULL, Leo8m004;
|FIN;

|DEFBUCLE Leo8m004_2;
 #404#4;
 ;
 nDia8, fFec8, nDoc8, " ", 0001;
 nDia8, fFec8, nDoc8, "z", 9999;
 ;
 NULL, Leo8m004;
|FIN;


|PROCESO Apertura2008;

   |SALVA_DATOS #1;
   #1#3 = #1#3 - 1; |SI #1#3 [ 0; #1#3 = 1; |FINSI;
   descuadre = (#1#4 - #1#5) ! EURODB;
   swlin = 1;
   |SI descuadre ! 0;
      swlin = 2;
      |SI descuadre < 0; descuadre = descuadre * (-1); |FINSI;
   |FINSI;
   |PRINT; nSwAlgun = 1;

   #1#4 = 0; #1#5 = 0; #1#6 = 0;
   nOpe = 0;
   |SI #0#3 = "S";
       |BUCLE Leo8m004_2;
   |SINO;
       |BUCLE Leo8m004;
   |FINSI;

   #1#3 = #1#3 + 1;
   descuadre = (#1#4 - #1#5) ! EURODB;
   swlin = 1;
   |SI descuadre ! 0;
      swlin = 2;
      |SI descuadre < 0; descuadre = descuadre * (-1); |FINSI;
   |FINSI;
   |PRINT; nSwAlgun = 1;

   nLinea = 0;
   nOpe   = 1;
   |SI #0#3 = "S";
       nOpe = 3; |BUCLE Leo8m004_2;  ||Debe
       nOpe = 4; |BUCLE Leo8m004_2;  ||Haber
   |SINO;
       |BUCLE Leo8m004;
   |FINSI;

   |REPON_DATOS #1;
|FPRC;

|| *************************************************************************
|| *************************************************************************
|PROCESO mirasiai;
   |SI #camaasil#21 < #0#14; |O #camaasil#21 > #0#15; |ACABA; |FINSI;
   |SI #camaasil#28 < #0#16; |O #camaasil#28 > #0#17; |ACABA; |FINSI;
   siai = 1;
   nConcepto    = #2#6;
   aDescripcion = #2#7;
   aSubdep      = #2#28;
|FPRC;

|PROCESO porimpre;
   |SI #camaasil#21 < #0#14; |O #camaasil#21 > #0#15; |ACABA; |FINSI;
   |SI #camaasil#28 < #0#16; |O #camaasil#28 > #0#17; |ACABA; |FINSI;
   #cacuenta#0 = #camaasil#4;
   #cacuenta#1 = #camaasil#5;
   |LEE 040#4=;
   |SI FSalida ! 0;
      |DDEFECTO #4;
   |FINSI;

   swlin = 0;
   |SI #camaasil#8 = "D";
      a_debe = #camaasil#9;
      a_haber = 0;
   |SINO;
      a_debe = 0;
      a_haber = #camaasil#9;
   |FINSI;

   |SI eaUltNiv = "N";
       #camaasil#4 = #camaasil#4 % aNoUlt;
   |FINSI;

   |PRINT; nSwAlgun = 1;
|FPRC;

|DEFBUCLE cadiario2;
   #2#2;
   3;
   #1#0, #1#1, #1#2, d, 0001;
   #1#0, #1#1, #1#2, h, 9999;
   be;
   NULL,porimpre;
|FIN;

|PROCESO bucleimp;

   |SI #0#2 = "S";
      |SI sw = 0; fecha = #camaasic#1; sw = 1; |FINSI;
      |SI fecha ! #camaasic#1; |PIEINF; fecha = #camaasic#1; |FINSI;
   |FINSI;

   nConcepto    = 0;
   aDescripcion = "";
   aSubdep      = "";
   siai = 0;
   |BUCLELIN 0mirasiai#1;
   |SI siai ! 0;

       |SI #0#21 = "S";
           |SI #1#0 = 0; |Y nDiaAper = 0;
               |HAZ VerAper08_d;
               |SI nIgual = 1;
                   |HAZ Apertura2008;
                   nDiaAper = 1;
                   |ACABA;           ||este asiento se trata de forma diferente
               |FINSI;
           |FINSI;
       |FINSI;

      descuadre = #camaasic#4 - #camaasic#5;
      |SI descuadre = 0;
         swlin = 1;
         |PRINT; nSwAlgun = 1;
      |SINO;
         swlin = 2;
         |SI descuadre < 0; descuadre = descuadre * (-1); |FINSI;
         |PRINT; nSwAlgun = 1;
      |FINSI;

      |SI #0#3 = "S";
         |BUCLE cadiario2;
      |SINO;
         |BUCLELIN 0porimpre#1;
      |FINSI;
       |SI nSumo = 1;
           #0#9  = (#0#9  + #camaasic#4) ! EURODB;
           #0#10 = (#0#10 + #camaasic#5) ! EURODB;
       |FINSI;

   |FINSI;
|FPRC;

|DEFBUCLE cadiario1;
   #1#2;
   ;
   #0#12, #0#0, #0#6, 000001;
   #0#13, #0#1, #0#7, 999999;
   e;
   NULL,NULL,NULL,NULL,NULL,bucleimp;
   #1#1,#1#3,#1#0,#1#2;
|FIN;

|PROCESO contar;
   |SI #camaasic#1 < #0#0;
       tde = (tde + #camaasic#4) ! EURODB;
       tha = (tha + #camaasic#5) ! EURODB;
       |SI nSumo = 1;
           #0#9  = (#0#9  + #camaasic#4) ! EURODB;
           #0#10 = (#0#10 + #camaasic#5) ! EURODB;
       |FINSI;
       |SI #0#21 = "S";
           |SI #1#0 = 0; |Y nDiaAper = 0;
               |HAZ VerAper08_d;
               |SI nIgual = 1;
                   nOpe = 2;|BUCLE Leo8m004;   || Solo Suma el ficticio
                   nDiaAper = 1;
               |FINSI;
           |FINSI;
       |FINSI;
   |FINSI;
|FPRC;


|DEFBUCLE cadiario0;
   #1#2;
   ;
        1, fec1, #0#6, 000001;
   999999, #0#0, #0#7, 999999;
   e;
   NULL,contar;
|FIN;

|| ------------------------------------ aqui
|PROCESO RecogeDato;
   nCalc = nDato - 1;
   aAlfaX1 = aAlfaX;
   |PARA; |SI nCalc > 0; |HACIENDO;
      aAlfaX2 = aAlfaX1 - &9;
      |SI FSalida ! 0;
         nCalc1 = ((FSalida / 100) ! 0) + 99;
         aAlfaX2 = aAlfaX1 % nCalc1;
         aAlfaX1 = aAlfaX1 - aAlfaX2;
         aAlfaX1 = aAlfaX1 - &9;
      |FINSI;
      nCalc = nCalc - 1;
   |SIGUE;
   aAlfaX2 = aAlfaX1 - &9;
   |SI FSalida ! 0;
      nCalc1 = ((FSalida / 100) ! 0) + 99;
      aAlfaX1 = aAlfaX1 % nCalc1;
   |FINSI;
|FPRC;

|PROCESO PortadaCrystal;
   aAlfaX = "pr" + Usuario; |NOME_DAT #catmpor1#aAlfaX#;
   |DELINDEX #catmpor1; |ABRE #catmpor1;

   |DBASS aAlfaX; |QBF aAlfaX;
   aAlfaX = aAlfaX + "basica/fich/port" + Usuario + ".txt";
   |XABRE "", aAlfaX, nHandTxt;
   |PARA; |SI FSalida ] 0; |HACIENDO;
      |XLEE nHandTxt, 250, aAlfaX;
      |SI FSalida ] 0;
         nDato = 1; |HAZ RecogeDato;
         |SI aAlfaX1 = "0";
            |DDEFECTO #catmport;
            nDato = 2; |HAZ RecogeDato; #catmport#0 = aAlfaX1;
            nDato = 3; |HAZ RecogeDato; #catmport#1 = aAlfaX1;
         |FINSI;
         |SI aAlfaX1 = "1";
            nDato = 2; |HAZ RecogeDato; #catmport#2 = aAlfaX1;
            nDato = 3; |HAZ RecogeDato; #catmport#3 = aAlfaX1;
            nDato = 4; |HAZ RecogeDato; #catmport#4 = aAlfaX1;
            nDato = 5; |HAZ RecogeDato; #catmport#5 = aAlfaX1;
            nDato = 6; |HAZ RecogeDato; #catmport#6 = aAlfaX1;
            nDato = 7; |HAZ RecogeDato; #catmport#7 = aAlfaX1;
            nDato = 8; |HAZ RecogeDato; #catmport#8 = aAlfaX1;
            nDato = 9; |HAZ RecogeDato; #catmport#9 = aAlfaX1;
            nDato = 10; |HAZ RecogeDato; #catmport#10 = aAlfaX1;
            nDato = 11; |HAZ RecogeDato; #catmport#11 = aAlfaX1;
            nDato = 12; |HAZ RecogeDato; #catmport#12 = aAlfaX1;
            nDato = 13; |HAZ RecogeDato; #catmport#13 = aAlfaX1;
            nDato = 14; |HAZ RecogeDato; #catmport#14 = aAlfaX1;
         |FINSI;
         |SI aAlfaX1 = "2";
            |DDEFECTO #catmpor1;
            #catmpor1#0  = #catmport#3;
            #catmpor1#10 = "A";
            nDato = 2; |HAZ RecogeDato; #catmpor1#1  = aAlfaX1;
            nDato = 3; |HAZ RecogeDato; #catmpor1#2 = aAlfaX1;
            nDato = 4; |HAZ RecogeDato; #catmpor1#3 = aAlfaX1;
            nDato = 5; |HAZ RecogeDato; #catmpor1#4 = aAlfaX1;
            nDato = 6; |HAZ RecogeDato; #catmpor1#5 = aAlfaX1;
            nDato = 7; |HAZ RecogeDato; #catmpor1#6 = aAlfaX1;
            nDato = 8; |HAZ RecogeDato; #catmpor1#7 = aAlfaX1;
            |GRABA 020#catmpor1.n;
         |FINSI;
         |SI aAlfaX1 = "3";
            |DDEFECTO #catmpor1;
            #catmpor1#0  = #catmport#3;
            #catmpor1#10 = "R";
            nDato = 2; |HAZ RecogeDato; #catmpor1#1  = aAlfaX1;
            nDato = 3; |HAZ RecogeDato; #catmpor1#8 = aAlfaX1;
            nDato = 4; |HAZ RecogeDato; #catmpor1#9 = aAlfaX1;
            |GRABA 020#catmpor1.n;
         |FINSI;
      |FINSI;
   |SIGUE;

   |CIERRA #catmpor1;
   |DFICE aAlfaX; |QBF aAlfaX;
   aAlfaX1 = aAlfaX + "pr" + Usuario + ".dbf";

   aIP = ""; |IP_REMOTO aIP;

   aContiene = "";
   |ESPECIFICA "RBASS", aContiene;
   aAlfaX2 = aContiene1;

   aAlfaX2 = aAlfaX2 + "crystal";
   |SI aIP ! ""; |RMKDIR aAlfaX2; |SINO; |MKDIR aAlfaX2; |FINSI;
   aAlfaX2 = aAlfaX2 + "/detporta.dbf"; |QBF aAlfaX2;
   |SI aIP ! ""; |ENVIA_FICHERO aAlfaX1, aAlfaX2; |SINO; |COPIA_FICHERO aAlfaX1, aAlfaX2; |FINSI;

   |DELINDEX #catmpor1;
|FPRC;

|PROCESO HazTodo;

       eaIa = #30#21;
       alfa1 = #0#0; alfa1 = alfa1 % 0402; enM1 = alfa1;
       alfa1 = #0#1; alfa1 = alfa1 % 0402; enM2 = alfa1;
       |HAZ LeeDatosEmpresa;
       #900#13 = #0#0; #900#14 = #0#1;
       |SI PARAMETRO = "LEGALIA";
           |SI #900#13 < #900#9;  |Y #900#9  > "01.01.0000"; #900#13 = #900#9;  |FINSI;
           |SI #900#14 > #900#10; |Y #900#10 > "01.01.0000"; #900#14 = #900#10; |FINSI;
       |FINSI;

|| ... Observaciones DsArchi
       eaObservaciones = "";
       |SI #0#6 = #0#7;
           |SI #0#6 = 00; eaObservaciones = "DE APERTURA"; |FINSI;
           |SI #0#6 = 99; eaObservaciones = "DE CIERRE";   |FINSI;
       |FINSI;
       |SI eaObservaciones = "";
           SwApertura = 0;
           SwCierre   = 0;
           NumMes = enM1; |HAZ NombreMes; NombreMes = " " + NombreMes; |QBF NombreMes;
           |SI #0#0 = fec1; |Y #0#6 = 00; NombreMes = " LA APERTURA"; SwApertura = 1; |FINSI;
           eaObservaciones = "DE" + NombreMes + " ";

           NumMes = enM2; |HAZ NombreMes; NombreMes = " " + NombreMes;
           |SI #0#1 = fec2; |Y #0#7 = 99; NombreMes = "L CIERRE"; SwCierre = 1; |FINSI;
           |SI enM1 ! enM2; |O SwApertura = 1; |O SwCierre = 1;
               eaObservaciones = eaObservaciones + "A" + NombreMes;
           |FINSI;
       |FINSI;
       eaObservaciones = eaObservaciones > " ";

       alfa4 = Impresora % 113;
       eaTituloL = #0#20; ||Titulo del Libro
       |SI #0#19 = "S"; |O alfa4 = "CrystalReport";
                           || ... .... Hay que generarlo, aunque no se imprima
           eaImpresora = Impresora;
           |SI alfa4 = "CrystalReport"; eaImpresora = alfa4; |FINSI;
           enEjer = enEjercicio;
           |SUB_EJECUTA_NP ":basica/agiy0016";
           eaImpresora = "";
           |SI alfa4 = "CrystalReport"; |HAZ PortadaCrystal; |FINSI;
       |FINSI;

       |SI #cadiario#24 = "A"; |Y Impresora = "CrystalReport";
          aAlfa = informe; |QBF aAlfa;
          aAlfa = (aAlfa % 0106) + "ap";
          informe = aAlfa;
       |FINSI;

      |SI nOpcion = 1;
           |INFOR informe;
           |SI FSalida ! 0; |MENAV "     NO existe informe"; |ACABA;  |FINSI;
      |SINO;
           eaInforme = informe;
           |HAZ ElIMPREArchi;
           |SI swErrorDSArchi = 1;
               |ACABA;
           |FINSI;
      |FINSI;

       nSwAlgun = 0;
       nDiaAper = 0;
       |ABRE #4;
       |SI #0#11 = "S";
           |BUCLE cadiario0;
       |SINO;
           tde = #0#9;
           tha = #0#10;
       |FINSI;

      |BUCLE cadiario1;
      |CIERRA #4;
      |SI nSwAlgun = 1; |PIEINF; |FINSI;

      |FININF;

      |SI nOpcion = 2;
         |FINIMP;
         |SI swErrorDSArchi = 0;
             |HAZ Archivando;
         |FINSI;
         Impresora = "CrystalReport";
      |FINSI;
|FPRC;

|PROCESO impre;

     |SI nArchi = 1;
          |MENU aMenu;
          nOpcion = FSalida;
          |SI nOpcion = 0;
               |ACABA;
          |FINSI;
     |SINO;
          nOpcion = 1;
     |FINSI;

     |SI nOpcion = 3;  |SALVA_DATOS #0;  |FINSI;

     nOpSegur = nOpcion;
     |SI nOpSegur = 1; |O nOpSegur = 3;
         nOpcion = 1;
         |HAZ impre_Uno;
         |SI nOpSegur = 3;  |REPON_DATOS #0;  |FINSI;
     |FINSI;

     |SI nOpSegur = 2; |O nOpSegur = 3;
         nOpcion = 2;
         |HAZ impre_Uno;
     |FINSI;
|FPRC;

|PROCESO impre_Uno;
     hoja = #0#4;

     ||Archivar
     |SI nOpcion = 2;
         Impresora = "CrystalReport";
     |FINSI;
     |SI nOpcion = 1;
         |SI PARAMETRO ! "";
              |SI PARAMETRO = "AGENCIA";
                 Impresora = "CrystalReport";
                 |IMPRE -1;
              |SINO;
                 |SI enBasImp = 1;
                     |IMPRE -1;
                 |SINO;
                     Impresora = "Crystal Reports";
                     |IMPRE 0;
                 |FINSI;
              |FINSI;
              |SI FSalida ! 0;  |ACABA;  |FINSI;
         |SINO;
              Impresora = "Crystal Reports";
              |IMPRE 0;
              |SI FSalida ! 0;  |MENAV "     NO existe impresora";  |ACABA;  |FINSI;
         |FINSI;
     |FINSI;

   |HAZ los_informes;
   informe = que_i1;
   enDatos = 0; |SI #0#18 = "S"; enDatos = 1; |FINSI;

   alfa4 = Impresora;
   alfa4 = alfa4 % 113;
   |SI alfa4 = "CrystalReport"; |O nOpcion = 2;
       informe = "cc" + (informe %0306);
       nSumo   = 1;
   |SINO;
       |SI PARAMETRO ! ""; |Y eaFormato = "S";
           informe = "cc" + (informe %0306);
           nSumo   = 1;
       |FINSI;
   |FINSI;

   |SI #48#27 = "S"; |Y PARAMETRO = "";
       |HAZ MultiEmpresa;
   |SINO;
       |SI PARAMETRO = "";
           |DFICE dirfich0; |QBF dirfich0;
       |SINO;
           dirfich0 = fich_alta;
       |FINSI;
       |HAZ HazTodo;
   |FINSI;

   |SI nOpcion = 1;
       |FINIMP;
   |FINSI;
|FPRC;

|PROCESO borrar;
   swlin     = 0;
   descuadre = 0;
   a_debe    = 0;
   a_haber   = 0;
   tde       = 0;
   tha       = 0;
   hoja      = 0;
   nDiaAper  = 0;
   #0#9      = 0;
   #0#10     = 0;
|FPRC;

|| -------------------------------------------------------------------------
|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #0 dirfich0;
   |PATH_DAT #1 dirfich0;
   |PATH_DAT #2 dirfich0;
   |PATH_DAT #4 dirfich0;
   |PATH_DAT #8 dirfich0;
   |PATH_DAT #404 dirfich0;
   |HAZ HazTodo;
   |HAZ borrar;
|FPRC;

|DEFBUCLE MultiEmp0;
   #caselem1#1;
   ;
   000001;
   999999;
   ;
   NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SI nOpSegur ! 3; |O nOpcion = 1;
      |SUB_EJECUTA "caselemp";
      |NOME_DAT #998 eaFichsel;
  |FINSI;
  |BUCLE MultiEmp0;
  |SI nOpSegur ! 3; |O nOpcion = 2;
     |DELINDEX #998;
  |FINSI;
|FPRC;

|| *********************************************************************
||                             COMIENZO
|| *********************************************************************
|PROCESO LeoConimp8;
   |ABRE #8;
   |DDEFECTO #8;
   #8#0 = 1;
   |LEE 001#8=;
   |CIERRA #8;
   #0#21 = #8#11;
   #0#23 = #8#13;
|FPRC;

|PROGRAMA;

   nArchi = 0;
   |SI PARAMETRO = "";

      |HAZ ActivaPLB_arc1;

      eaVengoDe = "cadiario";
      |HAZ MiraSiArchiva;

      eaUltNiv = "S";
      |CLS;
      |CABEZA "Listado Diario Oficial";
      |ABRE #0;
      |HAZ inicio;

      #0#8 = 1;
      |LEE 141#0=;
      |SI FSalida ! 0;
         |DDEFECTO #0;
         #0#0 = fec1;
         #0#1 = fec2;
         #0#8 = 1;
         |HAZ LeoConimp8;
         |GRABA 001#0c;
      |FINSI;
      FSalida = 1;

      |PARA;|SI FSalida = 1; |HACIENDO;
         sw = 0;
         |PINPA #0#0;
         #0#8 = 1;
         |LEE 141#0=;
         #0#5 = ~;
         |SI enEjercicio ! 2008;
             #0#21 = "N";
             |C_V #0#21,1,"N"; |C_M #0#21,1,"N";
             |C_V #0#23,1,"N"; |C_M #0#23,1,"N";
             alfa1 = "ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ";
             alfa2 = "                                                             ";
             |PINTA 2011, alfa1;
             |PINTA 2111, alfa2;
             |PINTA 2211, alfa2;
             |PINTA 2311, alfa2;
         |FINSI;
         |PINDA #0#0;                    || Pinta los datos por defecto
         |ENDATOS #1#0;                  || Seleccion informe
         |SI FSalida = 0;                || Si el conforme es igua a S
             |HAZ impre;                 || Calculo impresion

             |SI nSumo = 0;
                 #0#9  = tde;
                 #0#10 = tha;
             |FINSI;
             #0#4  = hoja;
             |SI #0#1 ! fec2;
                 #0#0 = #0#1 + 1;
             |SINO;
                 #0#0 = fec2;
             |FINSI;
             #0#1  = fec2;
             #0#11 = "S";
             |GRABA 001#0a;
             |HAZ borrar;                || Borra acumulado
             FSalida = 1;                || Vuelve al bucle
             |SI nArchi = 1; FSalida = 0; |FINSI;
         |SINO;
             FSalida = 0;
         |FINSI;
      |SIGUE;
      |CIERRA #0;

   |SINO;

      |ABRE #0;
      |HAZ Legalia;
      |CIERRA #0;

   |FINSI;
|FPRO;
