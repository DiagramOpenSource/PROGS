|FICHEROS;
     n43asico #0;

     n43temp1 #1;
     n43temp2 #2;
     n43temp3 #3;

     n43datos #201;
     camaasil #202;

     ||n43histo #555;            ||Historico consolidacion
     n43consl #90;             ||Conciliados

     n43tempb #50,MANTE;       ||Temporal busquedas.

     referen@ #999;            ||Para CARGA_DEF del tempo original aTempo1/2

     n43wctac #101,MANTE;      ||Aux. Pedir Cta.Conta
     n43wmarg #102,MANTE;

     refere1@ #301;
     refere2@ #302;
|FIN;

|VARIABLES;
     nNume = 0;
     fFechaMin = @;
     fFechaMax = @;
     nVenMargen = 0;
     nMargenDias = 0;
     nLblMargen = -1;
     aPath = "";
     nSwCheq = "";
     nError = 0;
     fFechaCheq = @;
     aColorVerde = "0,185,0;255,255,255";
     aColorNegro = "0,0,0;0,0,0";
     aMarca = "";
     aCtaImpo = "";
     aLabel = "";
     aCtaContable = "";
     aDesCtaConta = "";
     nBotonCta = 0;

     men2 = "0000Longitud de Cuenta Fuera de Nivel !";
     aAux = "";
     nVentanaSel = 0;
     nPantallaSel = 0;
     nSwSelCtaOK = 0;

     nEntradaCon = 0;
     nDiarioCon = 0;
     fFechaCon = @;
     nDocCon = 0;
     nApunteCon = 0;

     nEntradaDesCon = 0;
     nDiarioDesCon = 0;
     fFechaDesCon = @;
     nDocDesCon = 0;
     nApunteDesCon = 0;

     aDef = "";
     nVoy = 0;
     nContador = 0;
     nTipoCon = 0;            ||Tipo conciliacion 0, 1, 2.
     nImpoA = 0;
     nImpoB = 0;
     nRegSel1 = 0;
     nRegSel2 = 0;
     nSuma1 = 0;
     aSuma1 = "";
     nSuma2 = 0;
     aSuma2 = "";
     aRGB = "";
     aLabel1 = "";
     aLabel2 = "";

     ||Busqueda
     fFecha = @;
     aFecha = "";
     aDia = "";
     aMes = "";
     aAny = "";
     aFechaDesde = "";
     aFechaHasta = "";
     aFicBorra = "";
     nRegistros = 0;
     aFichSQL = "";
     aSQL = "";
     aRespuesta = "";
     &Tempo = "";
     aTempoBusA = "";
     aTempoBusB = "";
     aBorraTempo = "";
     nSwBusquedaOk = 0;
     nVentana = 0;
     {-1}aVent        = "";
         aVent1       = 0;
         aVent2       = 0;
         aVent3       = 0;
         aVent4       = 0;
         aVent5       = "";

     aSwModoBusqueda = "";    || "B"usqueda o "I"mporte igual
     nImporteBuscar = 0;

     aZona = "";              ||A Grid Superior. Movimientos Importados
                              ||B Grid Inferior. Movimientos Extracto Contable

     aModoVerA = "";          ||(C)onciliados / (P)endientes / (T)odos
     aModoVerB = "";          ||(C)onciliados / (P)endientes / (T)odos
     nBotonLabel = 0;
     aPathDatos = "";
     aOrigen = "";
     aDestino = "";

     ||Ver:  Conciliado, Pendiente, Todos
     aDesdeA = "";
     aHastaA = "";
     aDesdeB = "";
     aHastaB = "";

     nRango = 0;
     aAlfa = "";
     &eaUnidadBasico = "";
     aMensaje = "";
     nPantalla = 0;
     nPantallaB = 0;
     aBase43 = "";
     aFotoOrigen = "";
     aFotoDestino = "";
     aExt = "";
     aDirLocal = "";
     {1}aContiene = "";
     aDocRemoto = "";
     aDocServidor = "";
     aIPRemoto = "";
     aFoto = "";
     nBotonFoto = 0;
     nBotonFotoB = 0;

     nGridA = 0;
     nGridB = 0;
     nBotonA1 = 0;
     nBotonA2 = 0;
     nBotonA3 = 0;
     nBotonA4 = 0;
     nBotonA5 = 0;
     nBotonA6 = 0;
     nBotonA7 = 0;
     nBotonA8 = 0;
     nBotonB1 = 0;
     nBotonB2 = 0;
     nBotonB3 = 0;
     nBotonB4 = 0;
     nBotonB5 = 0;
     nBotonB6 = 0;
     nBotonB7 = 0;
     nBotonB8 = 0;

     nBotonF1 = 0;
     nBotonF2 = 0;
     nBtnMargen = 0;

     aEsc1            = "";
     aEsc2            = "";
     aRetu            = "";
     aTecla           = "";

     aTempo1 = "";
     aTempo2 = "";
     aTempo3 = "";
|FIN;

|INCLUYE dscont_i;

|PROCESO Tipo12tempb; |TIPO 12;
|FPRC;

|PROCESO Tipo12_n43wctac; |TIPO 12;
|FPRC;

|PROCESO Cuenta_n43wctac; |TIPO 6;
     aAux = #n43wctac#0;
     |QBF aAux;
     |SI aAux ! "";
          eaCuenta = #n43wctac#0;
          |HAZ FiltraPuntos;
          #n43wctac#0 = eaCuenta;
          |PINTA #n43wctac#0;
     |SINO;
          |ABRE #201;
          |LEE 000#201.u;
          |SI FSalida = 0;
              #n43wctac#0 = #201#1;
              |PINTA #n43wctac#0;
              eaCuenta = #n43wctac#0;
          |SINO;
              |CIERRA #201;
              |MENAV "    No existen Datos";
              |ERROR;
              |ACABA;
          |FINSI;
          |CIERRA #201;
   |FINSI;

   |QBF eaCuenta;
   |SI eaCuenta ! "";
       eswCta = 0;
       |HAZ SacaNivel;
       |SI eswCta = 1;
           |MENAV men2;
           |ERROR;
       |FINSI;
   |FINSI;
|FPRC;



|PROCESO ActualizaTempo1;
     |DBASE aBase43;  |QBF aBase43;
     aDef = aBase43 + "def/n43temp1";

     |CARGA_DEF aDef, referen@;
     |SI FSalida = 0;
          |NOME_DAT #referen@#aTempo1#;
          |ABRE #referen@;
          |SELECT #3#referen@;
          |DDEFECTO #referen@;
          #referen@#0 = #n43temp1#0;
          |LEE 101#referen@.=;
          |SI FSalida = 0;
               |PARA nVoy = 1; |SI nVoy [ 12; |HACIENDO nVoy = nVoy + 1;
                     #referen@#nVoy# = #n43temp1#nVoy#;
               |SIGUE;
               |GRABA 020#referen@.a;
               |LIBERA #referen@;
          |FINSI;
          |CIERRA #referen@;
          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;

|PROCESO ActualizaTempo2;
     |DBASE aBase43;  |QBF aBase43;
     aDef = aBase43 + "def/n43temp2";

     |CARGA_DEF aDef, referen@;
     |SI FSalida = 0;
          |NOME_DAT #referen@#aTempo2#;
          |ABRE #referen@;
          |SELECT #3#referen@;
          |DDEFECTO #referen@;
          #referen@#0 = #n43temp2#0;
          #referen@#1 = #n43temp2#1;
          #referen@#2 = #n43temp2#2;
          #referen@#3 = #n43temp2#3;
          |LEE 101#referen@.=;
          |SI FSalida = 0;
               |PARA nVoy = 4; |SI nVoy [ 32; |HACIENDO nVoy = nVoy + 1;
                     #referen@#nVoy# = #n43temp2#nVoy#;
               |SIGUE;
               |GRABA 020#referen@.a;
               |LIBERA #referen@;
          |FINSI;
          |CIERRA #referen@;
          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;

|PROCESO Tipo80; |TIPO 80;
     FSalida = 3699;
|FPRC;

|PROCESO LeeUnidadBasico2;
     aContiene = "";
     |ESPECIFICA "RBASS", aContiene;
     eaUnidadBasico = (aContiene1 % 102) > "";

     aAlfa = eaUnidadBasico % -101;
     |SI aAlfa ! ":";
         eaUnidadBasico = "";
     |FINSI;
|FPRC;

|PROCESO PonFoto;
     |DBASE aBase43;  |QBF aBase43;

     aFotoOrigen = aBase43 + "logos/logods.jpg";
     aExt = ".jpg";

     |HAZ LeeUnidadBasico2;

     aDirLocal    = eaUnidadBasico + "\DOCUMENTOS";                      |RMKDIR aDirLocal;
     aDirLocal    = eaUnidadBasico + "\DOCUMENTOS\CONTAGEN";             |RMKDIR aDirLocal;
     aDirLocal    = eaUnidadBasico + "\DOCUMENTOS\CONTAGEN\LOGOS";       |RMKDIR aDirLocal;
     aFotoDestino = aDirLocal + "/logods" + aExt;

     aContiene1 = aFotoDestino;
     |ESPECIFICA "REMOTOMD5", aContiene;
     aDocRemoto = FSalida;  |QBF aDocRemoto;

     aContiene1 = aFotoOrigen;
     |ESPECIFICA "MD5", aContiene;
     aDocServidor = FSalida;  |QBF aDocServidor;

     |SI aDocRemoto ! aDocServidor;  |O aDocRemoto = "";
         aIPRemoto = "";
         |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aFotoOrigen, aFotoDestino;
         |SINO;
             |COPIA_FICHERO aFotoOrigen, aFotoDestino;
         |FINSI;
     |FINSI;

     aFoto = "IMAGEN,"  + aFotoDestino;
     |CONTROL_SIMPLE nPantalla, aFoto, 0501, 0928, NULL;
     nBotonFoto = FSalida;
|FPRC;

|PROCESO BotonF1;
|FPRC;

|PROCESO BotonF2;
|FPRC;

|PROCESO n43temp1_limpia;
     aRGB = #n43temp1#13;
     |QBF aRGB;
     |SI aRGB ! aColorNegro;
          #n43temp1#13 = aColorNegro;
          |GRABA 020#n43temp1.a;
     |FINSI;
|FPRC;

|DEFBUCLE n43temp1_limpia;
 #n43temp1#1;
 ;
 INICIO;
 FINAL;
 be;
 NULL, n43temp1_limpia;
|FIN;

|PROCESO LimpiaA;             ||Deselecciona TODO lo marcado
     |BUCLE n43temp1_limpia;
     nSuma1 = 0;
     nRegSel1 = 0;
|FPRC;

|PROCESO n43temp2_limpia;
     aRGB = #n43temp2#31;
     |QBF aRGB;
     |SI aRGB ! aColorNegro;
          #n43temp2#31 = aColorNegro;
          |GRABA 020#n43temp2.a;
     |FINSI;
|FPRC;

|DEFBUCLE n43temp2_limpia;
 #n43temp2#1;
 ;
 INICIO;
 FINAL;
 be;
 NULL, n43temp2_limpia;
|FIN;

|PROCESO LimpiaB;             ||Deselecciona TODO lo marcado
     |BUCLE n43temp2_limpia;
     nSuma2 = 0;
     nRegSel2 = 0;
|FPRC;

|PROCESO PintaLabel1;
     aSuma1 = ("           " + nSuma1) % -111;
     aLabel1 = "IMPORTE A CONCILIAR:  " + aSuma1;
     aLabel1 = "LABEL,{{001}}" + aLabel1;
     |CONTROL_SIMPLE nPantalla, aLabel1, 0562, 0598, NULL;
|FPRC;

|PROCESO PintaLabel2;
     aSuma2 = ("          " + nSuma2) % -110;
     aLabel2 = "IMPORTE CONCILIADO:   " + aSuma2;
     aLabel2 = "LABEL,{{001}}" + aLabel2;
     |CONTROL_SIMPLE nPantalla, aLabel2, 0662, 0698, NULL;
|FPRC;

|PROCESO CheqMarcados2;
     |SI nSwCheq = 0;
          |SI aAlfa = ""; aAlfa = #refere2@#1; |FINSI;
          |SI aAlfa ! #refere2@#1;
               nSwCheq = 1; |ERROR10;
          |FINSI;
     |SINO;
          |SI #refere2@#1 ! fFechaCheq;
               aAlfa = "0000NRegistro con fecha distinta. ¨Continuar?";
               |CONFI aAlfa;
               nError = FSalida;
               |ERROR10;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CheqMarcados1;
     |SI nSwCheq = 0;
          |SI aAlfa = ""; aAlfa = #refere1@#2; |FINSI;
          |SI aAlfa ! #refere1@#2;
               nSwCheq = 1; |ERROR10;
          |FINSI;
     |SINO;
          |SI #refere1@#2 ! fFechaCheq;
               aAlfa = "0000NRegistro con fecha distinta. ¨Continuar?";
               |CONFI aAlfa;
               nError = FSalida;
               |ERROR10;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE CheqMarcados2;
     #refere2@#2001;
     32;
     aColorVerde, "N";
     aColorVerde, "N";
     ;
     NULL, CheqMarcados2;
|FIN;

|DEFBUCLE CheqMarcados1;
     #refere1@#2001;
     9;
     aColorVerde, "N";
     aColorVerde, "N";
     ;
     NULL, CheqMarcados1;
|FIN;

|PROCESO CheqFecha;
     |DDEF aPath;
     aAlfa = aPath + "n43temp1";
     |CARGA_DEF aAlfa, refere1@;
     |SI FSalida = 0;
          aAlfa = aPath + "n43temp2";
          |CARGA_DEF aAlfa, refere2@;
          |SI FSalida = 0;
               |SI aTempoBusA = "";
                    |NOME_DAT #refere1@#aTempo1#;
               |SINO;
                    |NOME_DAT #refere1@#aTempoBusA#;
               |FINSI;
               |SI aTempoBusB = "";
                    |NOME_DAT #refere2@#aTempo2#;
               |SINO;
                    |NOME_DAT #refere2@#aTempoBusB#;
               |FINSI;
               nSwCheq = 0; aAlfa = "";
               |BUCLE CheqMarcados1;
               |SI nSwCheq = 0; |BUCLE CheqMarcados2; |FINSI;
               |SI nSwCheq = 0;
                    nSwCheq = 1; aAlfa = "";
                    |BUCLE CheqMarcados1;
                    |SI aAlfa = ""; |BUCLE CheqMarcados2; |FINSI;
               |FINSI;
               |DESCARGA_DEF #refere2@;
          |FINSI;
          |DESCARGA_DEF #refere1@;
     |FINSI;
|FPRC;

|PROCESO EventoA;
     |SI FSalida = 1;  |O FSalida = 2;
          |ESTADO_CONTROL nBotonA7, "DISABLE";
          |LEE 000#n43temp1.=;
          |SI FSalida = 0;
               |SI #n43temp1#9 = "S";
                    |ESTADO_CONTROL nBotonA7, "ENABLE";
               |FINSI;
          |FINSI;
          |SI nSuma1 ! 0; |Y nSuma1 = nSuma2;
               |ESTADO_CONTROL nBotonA6, "ENABLE";
               |ESTADO_CONTROL nBotonB6, "ENABLE";
          |SINO;
               |ESTADO_CONTROL nBotonA6, "DISABLE";
               |ESTADO_CONTROL nBotonB6, "DISABLE";
          |FINSI;
     |FINSI;

     |SI FSalida = 4;
          |LEE 101#n43temp1.=;
          |SI FSalida = 0;
               |SI #n43temp1#9 = "N";       ||No marcados
                    nError = 0;
                    ||Poner la linea verde
                    aRGB = #n43temp1#13;
                    |QBF aRGB;
                    |SI aRGB = aColorNegro;
                         fFechaCheq = #n43temp1#2;
                         |HAZ CheqFecha;
                         |SI nError = 0;
                              nImpoA = #n43temp1#7;
                              nSuma1 = nSuma1 + #n43temp1#7;
                              nRegSel1 = nRegSel1 + 1;
                              aRGB = aColorVerde;
                         |FINSI;
                    |SINO;
                         nSuma1 = nSuma1 - #n43temp1#7;
                         nRegSel1 = nRegSel1 - 1;
                         nImpoA = 0;
                         aRGB = aColorNegro;
                    |FINSI;
                    |SI nError = 0;
                         #n43temp1#13 = aRGB;
                         |GRABA 020#n43temp1.a;
                         |LIBERA #n43temp1;
                         ||PINTA #n43temp1#13;

                         |HAZ PintaLabel1;

                         |REFRESCACONTROL nGridA;
                    |SINO;
                         |LIBERA #n43temp1;
                    |FINSI;
               |SINO;
                    ||MARCADOS
                    aMensaje = "0000Movimiento YA marcado." + &13 + "NO es posible seleccionarlo para marcarlo.";
                    |MENAV aMensaje;
               |FINSI;
          |SINO;
               aMensaje = "0000No hay registro para seleccionar";
               |MENAV aMensaje;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO EventoB;
     |SI FSalida = 1;  |O FSalida = 2;
          |ESTADO_CONTROL nBotonB7, "DISABLE";
          |LEE 000#n43temp2.=;
          |SI FSalida = 0;
               |SI #n43temp2#32 = "S";
                    |ESTADO_CONTROL nBotonB7, "ENABLE";
               |FINSI;
          |FINSI;
          |SI nSuma1 ! 0; |Y nSuma1 = nSuma2;
               |ESTADO_CONTROL nBotonA6, "ENABLE";
               |ESTADO_CONTROL nBotonB6, "ENABLE";
          |SINO;
               |ESTADO_CONTROL nBotonA6, "DISABLE";
               |ESTADO_CONTROL nBotonB6, "DISABLE";
          |FINSI;
     |FINSI;

     |SI FSalida = 4;
          |LEE 101#n43temp2.=;
          |SI FSalida = 0;
               |SI #n43temp2#32 = "N";
                    nError = 0;
                    ||Poner la linea verde
                    aRGB = #n43temp2#31;
                    |QBF aRGB;
                    |SI aRGB = aColorNegro;
                         fFechaCheq = #n43temp2#1;
                         |HAZ CheqFecha;
                         |SI nError = 0;
                              nImpoB = #n43temp2#9;
                              nSuma2 = nSuma2 + #n43temp2#9;
                              nRegSel2 = nRegSel2 + 1;
                              aRGB = aColorVerde;
                         |FINSI;
                    |SINO;
                         nSuma2 = nSuma2 - #n43temp2#9;
                         nRegSel2 = nRegSel2 - 1;
                         nImpoB = 0;
                         aRGB = aColorNegro;
                    |FINSI;
                    |SI nError = 0;
                         #n43temp2#31 = aRGB;
                         |GRABA 020#n43temp2.a;
                         |LIBERA #n43temp2;

                         |HAZ PintaLabel2;

                         |REFRESCACONTROL nGridB;
                    |SINO;
                         |LIBERA #n43temp2;
                    |FINSI;
               |SINO;
                    ||MARCADOS
                    aMensaje = "0000Movimiento YA marcado." + &13 + "NO es posible seleccionarlo para marcarlo.";
                    |MENAV aMensaje;
               |FINSI;
          |SINO;
               aMensaje = "0000No hay registro para seleccionar";
               |MENAV aMensaje;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO BajaA;
     #n43temp1#9 = aDesdeA;
     #n43temp1#0 = 000001;
|FPRC;

|PROCESO AltaA;
     #n43temp1#9 = aHastaA;
     #n43temp1#0 = 999999;
|FPRC;

|PROCESO BajaB;
     #n43temp2#32 = aDesdeB;
     #n43temp2#0 = 00;
     #n43temp2#1 = "01.01.0000";
     #n43temp2#2 = 000001;
     #n43temp2#3 = 0001;
|FPRC;

|PROCESO AltaB;
     #n43temp2#32 = aHastaB;
     #n43temp2#0 = 99;
     #n43temp2#1 = "31.12.2099";
     #n43temp2#2 = 999999;
     #n43temp2#3 = 9999;
|FPRC;

|PROCESO Lineales;
     nRango = 2 + 4 + 8 + 16 + 32;

     |LINEAL_SIMPLE #1#n43temp1, nRango, 1102, 2098, BajaA, AltaA, EventoA;
     nGridA = FSalida;

     |LINEAL_SIMPLE #1#n43temp2, nRango, 2402, 3398, BajaB, AltaB, EventoB;
     nGridB = FSalida;
|FPRC;

|PROCESO BotonA1;        ||Buscar
     aZona = "A";
     |HAZ Buscar;
|FPRC;

|PROCESO BotonA2;        ||Borrar Busqueda
     aZona = "A";
     |HAZ BorraBus;
|FPRC;

|PROCESO BotonA3;        ||Ver Conciliados
     aZona = "A";
     |HAZ VerCon;
|FPRC;

|PROCESO BotonA4;        ||Ver Pendientes
     aZona = "A";
     |HAZ VerPen;
|FPRC;

|PROCESO BotonA5;        ||Ver Todos
     aZona = "A";
     |HAZ VerTod;
|FPRC;

|PROCESO BotonA6;        ||Marcar
     aZona = "A";
     |HAZ Marcar;
|FPRC;

|PROCESO BotonA7;        ||Desmarcar
     aZona = "A";
     |HAZ Desmarcar;
|FPRC;

|PROCESO BotonA8;        ||Importe Igual
     aZona = "A";
     |HAZ ImporteIgual;
|FPRC;

|PROCESO BotonB1;        ||Buscar
     aZona = "B";
     |HAZ Buscar;
|FPRC;

|PROCESO BotonB2;        ||Borrar Busqueda
     aZona = "B";
     |HAZ BorraBus;
|FPRC;

|PROCESO BotonB3;        ||Ver Conciliados
     aZona = "B";
     |HAZ VerCon;
|FPRC;

|PROCESO BotonB4;        ||Ver Pendientes
     aZona = "B";
     |HAZ VerPen;
|FPRC;

|PROCESO BotonB5;        ||Ver Todos
     aZona = "B";
     |HAZ VerTod;
|FPRC;

|PROCESO BotonB6;        ||Marcar
     aZona = "B";
     |HAZ Marcar;
|FPRC;

|PROCESO BotonB7;        ||Desmarcar
     aZona = "B";
     |HAZ Desmarcar;
|FPRC;

|PROCESO BotonB8;        ||Importe Igual
     aZona = "B";
     |HAZ ImporteIgual;
|FPRC;


||BOTONES (8) de los GRIDS.
|PROCESO Buscar;
     ||Se utiliza tambien para el boton Importe Igual
     aSwModoBusqueda = "B";
     |HAZ NucleoBuscar;
|FPRC;

|PROCESO NucleoBuscar;
     ||aSwModoBusqueda = "B" o "I";

     nSwBusquedaOk = 0;
     |SI aSwModoBusqueda = "B";
          |VENTANA 0501, 3799, -1, -1, "Busqueda";
          nVentana = FSalida;

          aVent1 = -1;
          aVent2 = -1;
          aVent3 = 0;
          aVent4 = nVentana;
          aVent5 = "MODAL";
          |ESPECIFICA "ESTADO_VENTANA", aVent;

          |DDEFECTO #n43tempb;
          |PINPA #0#n43tempb;
          nPantallaB = FSalida;

          aFoto = "IMAGEN," + aFotoDestino;
          |CONTROL_SIMPLE nPantallaB, aFoto, 0501, 0928, NULL;
          nBotonFotoB = FSalida;

          |PINDA #0#n43tempb;
          |ENDATOS #1#n43tempb;
          |SI FSalida = 0;
               |HAZ BusquedaReal;
          |SINO;
               nSwBusquedaOk = 0;
               ||Se cancela la busqueda
          |FINSI;

          |FIN_CONTROL_WINDOWS nBotonFotoB;

          aVent1 = -1;
          aVent2 = -1;
          aVent3 = 0;
          aVent4 = nVentana;
          aVent5 = "MODELESS";
          |ESPECIFICA "ESTADO_VENTANA", aVent;

          |FINVENTANA nVentana;
     |SINO;
          |DDEFECTO #n43tempb;
          #n43tempb#3 = nImporteBuscar;
          #n43tempb#4 = nImporteBuscar;
          |HAZ BusquedaReal;
     |FINSI;
     |SI nSwBusquedaOk = 1;
          |SI aZona = "A";
               |ESTADO_CONTROL nBotonA2, "ENABLE";
          |SINO;
               |ESTADO_CONTROL nBotonB2, "ENABLE";
          |FINSI;
     |FINSI;
     |HAZ PintaLabel1;
     |HAZ PintaLabel2;
|FPRC;

|PROCESO EliminaFisicamente;
     ||Para borrar los temporales que se quedan "huerfanos".

     aFicBorra = aPathDatos + aBorraTempo + ".dat";
     |FBORRA aFicBorra;
     aFicBorra = aPathDatos + aBorraTempo + ".ddx";
     |FBORRA aFicBorra;
     aFicBorra = aPathDatos + aBorraTempo + ".ixx";
     |FBORRA aFicBorra;
|FPRC;

|PROCESO BusquedaReal;
     |SI aSwModoBusqueda = "I";
          ||Busqueda por importe, buscamos en el GRID CONTRARIO
          |SI aZona = "A";
               aZona = "B";
          |SINO;
               aZona = "A";
          |FINSI;
     |FINSI;

     |SI aZona = "A"; |Y nSuma1 ! 0;
          |HAZ LimpiaA;
          |REFRESCACONTROL nGridA;
          nRegSel1 = 0;
     |FINSI;
     |SI aZona = "B"; |Y nSuma2 ! 0;
          |HAZ LimpiaB;
          |REFRESCACONTROL nGridB;
          nRegSel2 = 0;
     |FINSI;

     |SI aZona = "A";
          ||SQL
          |SI aTempoBusA ! "";
               aBorraTempo = aTempoBusA;
          |FINSI;
          |HAZ CreaTempo;
          aTempoBusA = Tempo;
          aFichSQL = aTempoBusA;

          aSQL = "SELECT * FROM n43temp1 INTO " + aFichSQL + " WHERE ";
          ||FECHA
          |SI aSwModoBusqueda = "I";
               aSQL = aSQL + " 2 BETWEEN " + fFechaMin + "," + fFechaMax;
          |FINSI;
/*
          ||Finalmente ESTO no es necesario.
          fFecha = #n43tempb#1;
          aFecha = fFecha;
          aDia = aFecha % 102;
          aMes = aFecha % 402;
          aAny = aFecha % -104;
          aFecha = aDia + "." + aMes + "." + aAny;
          aFechaDesde = aFecha;

          fFecha = #n43tempb#2;
          aFecha = fFecha;
          aDia = aFecha % 102;
          aMes = aFecha % 402;
          aAny = aFecha % -104;
          aFecha = aDia + "." + aMes + "." + aAny;
          aFechaHasta = aFecha;
          aSQL = aSQL + " 2 BETWEEN " + aFechaDesde + "," + aFechaHasta;
*/
          aSQL = aSQL + " 2 BETWEEN " + #n43tempb#1 + "," + #n43tempb#2;
          ||IMPORTE
          aSQL = aSQL + " AND 7 BETWEEN " + #n43tempb#3 + "," + #n43tempb#4;
          ||DEBE/HABER
          |SI #n43tempb#5 ! "A";
              |SI #n43tempb#5 = "D";
                  aSQL = aSQL + " AND 6 == D";
              |SINO;
                  aSQL = aSQL + " AND 6 == H";
              |FINSI;
          |FINSI;

/*
          ||COBRO/PAGO. LO QUITAMOS DE LA BUSQUEDA.
          |SI #n43tempb#6 ! "A";
              |SI #n43tempb#6 = "C";
                  aSQL = aSQL + " 6 == D";
              |SINO;
                  aSQL = aSQL + " 6 == H";
              |FINSI;
          |FINSI;
*/
     |SINO;
          ||SQL
          |SI aTempoBusB ! "";
               aBorraTempo = aTempoBusB;
          |FINSI;
          |HAZ CreaTempo;
          aTempoBusB = Tempo;
          aFichSQL = aTempoBusB;

          aSQL = "SELECT * FROM n43temp2 INTO " + aFichSQL + " WHERE ";
          ||FECHA
          |SI aSwModoBusqueda = "I";
               aSQL = aSQL + " 1 BETWEEN " + fFechaMin + "," + fFechaMax;
          |SINO;
               aSQL = aSQL + " 1 BETWEEN " + #n43tempb#1 + "," + #n43tempb#2;
          |FINSI;

          ||IMPORTE
          aSQL = aSQL + " AND 9 BETWEEN " + #n43tempb#3 + "," + #n43tempb#4;
          ||DEBE/HABER
          |SI #n43tempb#5 ! "A";
              |SI #n43tempb#5 = "D";
                  aSQL = aSQL + " AND 8 == D";
              |SINO;
                  aSQL = aSQL + " AND 8 == H";
              |FINSI;
          |FINSI;
     |FINSI;

     |SQL aSQL;
     aRespuesta = FSalida;
     |QBF aRespuesta;
     aRespuesta = aRespuesta - "0 Seleccionados:";
     nRegistros = aRespuesta;
     |SI nRegistros = 0;
          |SI aSwModoBusqueda = "B";
               aMensaje = "0000No se ha encontrado ningun registro que cumpla las condiciones.";
          |SINO;
               |SI aZona = "A";
                    aMensaje = "0000No existen Movimientos con importe " + nImporteBuscar;
               |SINO;
                    aMensaje = "0000No existen Apuntes con importe " + nImporteBuscar;
               |FINSI;
               aMensaje = aMensaje + " para conciliar con esta Cuenta.";
          |FINSI;
          |MENAV aMensaje;
          |ACABA;
     |FINSI;

     nSwBusquedaOk = 1;

     |SI aZona = "A";
          |CIERRA #n43temp1;
          |NOME_DAT #1 aTempoBusA;
          |ABRE #n43temp1;

          |SI aBorraTempo ! "";
                Tempo = aBorraTempo;
                |HAZ EliminaFisicamente;
                |HAZ BoraTempo;
                aBorraTempo = "";
          |FINSI;
          |REFRESCACONTROL nGridA;
     |SINO;
          |CIERRA #n43temp2;
          |NOME_DAT #2 aTempoBusB;
          |ABRE #n43temp2;

          |SI aBorraTempo ! "";
                Tempo = aBorraTempo;
                |HAZ EliminaFisicamente;
                |HAZ BoraTempo;
                aBorraTempo = "";
          |FINSI;
          |REFRESCACONTROL nGridB;
     |FINSI;
|FPRC;

|PROCESO BorraBus;
     |SI aZona = "A";
          |SI aTempoBusA ! "";
               |CIERRA #n43temp1;
               |NOME_DAT #1 aTempo1;
               |ABRE #n43temp1;

                aBorraTempo = aTempoBusA;
                Tempo = aBorraTempo;
                |HAZ EliminaFisicamente;
                |HAZ BoraTempo;
                aBorraTempo = "";
                aTempoBusA = "";
          |FINSI;
          |HAZ LimpiaA;
          |HAZ PintaLabel1;
          |REFRESCACONTROL nGridA;
          |ESTADO_CONTROL nBotonA2, "DISABLE";
     |SINO;
          |SI aTempoBusB ! "";
               |CIERRA #n43temp2;
               |NOME_DAT #2 aTempo2;
               |ABRE #n43temp2;

                aBorraTempo = aTempoBusB;
                Tempo = aBorraTempo;
                |HAZ EliminaFisicamente;
                |HAZ BoraTempo;
                aBorraTempo = "";
                aTempoBusB = "";
          |FINSI;
          |HAZ LimpiaB;
          |HAZ PintaLabel2;
          |REFRESCACONTROL nGridB;
          |ESTADO_CONTROL nBotonB2, "DISABLE";
     |FINSI;
|FPRC;

|PROCESO VerCon;
     |SI aZona = "A";
          aModoVerA = "C";
          aDesdeA = "S";
          aHastaA = "S";
          |REFRESCACONTROL nGridA;
     |SINO;
          aModoVerB = "C";
          aDesdeB = "S";
          aHastaB = "S";
          |REFRESCACONTROL nGridB;
     |FINSI;
     |HAZ ControlBotonesVer;
|FPRC;

|PROCESO VerPen;
     |SI aZona = "A";
          aModoVerA = "P";
          aDesdeA = "N";
          aHastaA = "N";
          |REFRESCACONTROL nGridA;
     |SINO;
          aModoVerB = "P";
          aDesdeB = "N";
          aHastaB = "N";
          |REFRESCACONTROL nGridB;
     |FINSI;
     |HAZ ControlBotonesVer;
|FPRC;

|PROCESO VerTod;
     |SI aZona = "A";
          aModoVerA = "T";
          aDesdeA = " ";
          aHastaA = "z";
          |REFRESCACONTROL nGridA;
     |SINO;
          aModoVerB = "T";
          aDesdeB = " ";
          aHastaB = "z";
          |REFRESCACONTROL nGridB;
     |FINSI;
     |HAZ ControlBotonesVer;
|FPRC;

|PROCESO n43temp2_marcar;
     ||aMensaje = "0000Marcar " + #n43temp1#0 + " con apunte " + #n43temp2#2;
     ||MENAV aMensaje;

     |DDEFECTO #n43consl;
     |LEE 000#n43consl.u;
     nContador = #n43consl#0;
     nContador = nContador + 1;

     |DDEFECTO #n43consl;
     #n43consl#0 = nContador;
     |LEE 101#n43consl.=;
     |SI FSalida ! 0;
          #n43consl#1  = #n43temp1#0;
          #n43consl#2  = #n43temp1#1;
          #n43consl#3  = #n43temp1#2;
          #n43consl#4  = #n43temp1#3;
          #n43consl#5  = #n43temp1#4;
          #n43consl#6  = #n43temp1#5;
          #n43consl#7  = #n43temp1#6;
          #n43consl#8  = #n43temp1#7;
          #n43consl#9  = #n43temp1#8;
          #n43consl#10 = #n43temp2#4;
          #n43consl#11 = #n43temp2#5;
          #n43consl#12 = #n43temp2#1;
          #n43consl#13 = #n43temp2#2;
          #n43consl#14 = #n43temp2#6;
          #n43consl#15 = #n43temp2#7;
          #n43consl#16 = #n43temp2#8;
          #n43consl#17 = #n43temp2#9;
          #n43consl#18 = #n43temp2#0;
          #n43consl#19 = #n43temp2#3;
          #n43consl#20 = nTipoCon;
          |GRABA 020#n43consl.n;
          |LIBERA #n43consl;
     |FINSI;
|FPRC;

|DEFBUCLE n43temp2_marcar;
 #n43temp2#2;
 ;
 aColorVerde;
 aColorVerde;
 ;
 NULL, n43temp2_marcar;
|FIN;

|PROCESO n43temp1_marcar;
     |BUCLE n43temp2_marcar;
|FPRC;

|DEFBUCLE n43temp1_marcar;
 #n43temp1#2;
 ;
 aColorVerde;
 aColorVerde;
 ;
 NULL, n43temp1_marcar;
|FIN;

|PROCESO n43temp2_act;
     #n43temp2#31 = aColorVerde;
     #n43temp2#32 = "S";
     |GRABA 020#n43temp2.a;

     |SI aTempoBusB ! "";
          |HAZ ActualizaTempo2;
     |FINSI;
|FPRC;

|DEFBUCLE n43temp2_act;
 #n43temp2#2;
 ;
 aColorVerde;
 aColorVerde;
 be;
 NULL, n43temp2_act;
|FIN;

|PROCESO n43temp1_act;
     #n43temp1#13 = aColorVerde;
     #n43temp1#9 = "S";

     |DDEFECTO #n43datos;
     #n43datos#0 = #n43temp1#0;
     |LEE 101#n43datos.=;
     |SI FSalida = 0;
          #n43datos#9 = "S";
          |GRABA 020#n43datos.a;
          |LIBERA #n43datos;
     |FINSI;

     |GRABA 020#n43temp1.a;

     |SI aTempoBusA ! "";
          |HAZ ActualizaTempo1;
     |FINSI;
|FPRC;

|DEFBUCLE n43temp1_act;
 #n43temp1#2;
 ;
 aColorVerde;
 aColorVerde;
 be;
 NULL, n43temp1_act;
|FIN;

|PROCESO Marcar;
     |SI nRegSel1 ! 1; |Y nRegSel2 ! 1;
          ||aMensaje = "0000No es posible marcar VARIOS movimientos importados" + &13 + "contra VARIOS movimientos extracto contable";
          ||MENAV aMensaje;
          ||ACABA;
          nTipoCon = 3;
     |SINO;
          |SI nRegSel1 = 1; |Y nRegSel2 = 1;
               nTipoCon = 0;
          |SINO;
               |SI nRegSel1 = 1; |Y nRegSel2 ! 1;
                    nTipoCon = 1;
               |SINO;
                    nTipoCon = 2;
               |FINSI;
          |FINSI;
     |FINSI;

     |ABRE #n43consl;
     |CIERRA #n43temp1;
     |CIERRA #n43temp2;

     ||Marcar. Lo meto en el n43consl;
     |BUCLE n43temp1_marcar;

     ||Marco y Actualizo .
     ||Actualizo el n43temp1 y n43temp2
     ||Actualizo el n43datos (el camaasil ya no lo actualizo)
     |BUCLE n43temp1_act;
     |BUCLE n43temp2_act;

     |ABRE #n43temp1;
     |ABRE #n43temp2;
     |CIERRA #n43consl;

     ||Leo ambos por la clave 2 ... Luego devuelvo la clave al 1
     ||Actualizo el n43temp1 y n43temp2
     ||Actualizo el n43datos y camaasil
     ||Refresco los grid

     nSuma1 = 0;
     nRegSel1 = 0;
     nSuma2 = 0;
     nRegSel2 = 0;
     |HAZ PintaLabel1;
     |HAZ PintaLabel2;
     |SELECT #1#n43temp1;
     |SELECT #1#n43temp2;
     |REFRESCACONTROL nGridA;
     |REFRESCACONTROL nGridB;
|FPRC;

|PROCESO DesconciliaUno;
     ||Desconciliar n43temp1
       ||-> Actualizar el aTempo1 SI aTempoBusA ! "";
     ||Desconciliar n43datos

     |SELECT #3#n43temp1;
     #n43temp1#0 = nEntradaDesCon;
     |LEE 101#n43temp1.=;
     |SI FSalida = 0;
          #n43temp1#9 = "N";
          #n43temp1#13 = aColorNegro;
          |GRABA 020#n43temp1.a;
          |LIBERA #n43temp1;
          |SI aTempoBusA ! "";
               |HAZ ActualizaTempo1;
          |FINSI;
     |FINSI;

     #n43datos#0 = nEntradaDesCon;
     |LEE 101#n43datos.=;
     |SI FSalida = 0;
          #n43datos#9 = "N";
          |GRABA 020#n43datos.a;
          |LIBERA #n43datos;
     |FINSI;

     |SELECT #1#n43temp1;
|FPRC;

|PROCESO DesconciliaDos;
     ||Desconciliar n43temp2
       ||-> Actualizar el aTempo2 SI aTempoBusB ! "";
     ||Desconciliar camaasil
     |SELECT #3#n43temp2;

     #n43temp2#0 = nDiarioDesCon;
     #n43temp2#1 = fFechaDesCon;
     #n43temp2#2 = nDocDesCon;
     #n43temp2#3 = nApunteDesCon;
     |LEE 101#n43temp2.=;
     |SI FSalida = 0;
          #n43temp2#32 = "N";
          #n43temp2#31 = aColorNegro;
          |GRABA 020#n43temp2.a;
          |LIBERA #n43temp2;
          |SI aTempoBusB ! "";
               |HAZ ActualizaTempo2;
          |FINSI;
     |FINSI;

     |SELECT #1#n43temp2;
|FPRC;

|PROCESO n43conslUno;
     nDiarioDesCon = #n43consl#18;
     fFechaDesCon = #n43consl#12;
     nDocDesCon = #n43consl#13;
     nApunteDesCon = #n43consl#19;
     |HAZ DesconciliaDos;

     |BORRA 020#n43consl.a;
|FPRC;

|DEFBUCLE n43conslUno;
 #n43consl#5;
 ;
 nEntradaCon;
 nEntradaCon;
 be;
 NULL, n43conslUno;
|FIN;

|PROCESO n43conslDos;
     nEntradaDesCon = #n43consl#1;
     |HAZ DesconciliaUno;

     |BORRA 020#n43consl.a;
|FPRC;

|DEFBUCLE n43conslDos;
 #n43consl#4;
 ;
 nDiarioCon, fFechaCon, nDocCon, nApunteCon;
 nDiarioCon, fFechaCon, nDocCon, nApunteCon;
 be;
 NULL, n43conslDos;
|FIN;

|PROCESO Tmp3;
     nEntradaCon = #3#1;
     nDiarioCon = #3#2;
     fFechaCon = #3#3;
     nDocCon = #3#4;
     nApunteCon = #3#5;
     |BUCLE n43conslDos;

     nDiarioDesCon = nDiarioCon;
     fFechaDesCon = fFechaCon;
     nDocDesCon = nDocCon;
     nApunteDesCon = nApunteCon;
     |HAZ DesconciliaDos;
|FPRC;

|DEFBUCLE Tmp3;
     #3#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tmp3;
|FIN;

|PROCESO n43conslTres;
     #3#0 = #n43consl#0;
     #3#1 = #n43consl#1;
     #3#2 = #n43consl#18;
     #3#3 = #n43consl#12;
     #3#4 = #n43consl#13;
     #3#5 = #n43consl#19;
     |GRABA 020#3n;
|FPRC;

|DEFBUCLE n43conslTres;
     #n43consl#5;
     ;
     nEntradaCon;
     nEntradaCon;
     ;
     NULL, n43conslTres;
|FIN;

|PROCESO Desmarcar;
     |SI aZona = "A";
          |ABRE #n43consl;
          |SELECT #5#n43consl;
          #n43consl#1 = #n43temp1#0;
          |LEE 000#n43consl.=;
          |SI FSalida ! 0;
               aMensaje = "0000No encuentro el registro en el mantenimiento datos conciliados.";
               |MENAV aMensaje;
               |CIERRA #n43consl;
               |ACABA;
          |FINSI;
          nTipoCon = #n43consl#20;
          nEntradaCon = #n43consl#1;
          nDiarioCon = #n43consl#18;
          fFechaCon = #n43consl#12;
          nDocCon = #n43consl#13;
          nApunteCon = #n43consl#19;
          |CIERRA #n43consl;
     |SINO;
          ||TODO CCC
          ||Equivalente .. pero a la inversa.
          |ABRE #n43consl;
          |SELECT #4#n43consl;
          #n43consl#18 = #n43temp2#0;
          #n43consl#12 = #n43temp2#1;
          #n43consl#13 = #n43temp2#2;
          #n43consl#19 = #n43temp2#3;
          |LEE 000#n43consl.=;
          |SI FSalida ! 0;
               aMensaje = "0000No encuentro el registro en el mantenimiento datos conciliados.";
               |MENAV aMensaje;
               |CIERRA #n43consl;
               |ACABA;
          |FINSI;
          nTipoCon = #n43consl#20;
          nEntradaCon = #n43consl#1;
          nDiarioCon = #n43consl#18;
          fFechaCon = #n43consl#12;
          nDocCon = #n43consl#13;
          nApunteCon = #n43consl#19;
          |CIERRA #n43consl;
     |FINSI;

     ||DesconciliaUno
     ||Desconciliar n43temp1
       ||-> Actualizar el aTempo1 SI aTempoBusA ! "";
     ||Desconciliar n43datos

     ||DesconciliaDos
     ||Desconciliar n43temp2
       ||-> Actualizar el aTempo2 SI aTempoBusB ! "";
     ||Desconciliar camaasil

     ||Borrar el registro en n43consl

     |SI nTipoCon = 0; |O nTipoCon = 1;
          |BUCLE n43conslUno;
          nEntradaDesCon = nEntradaCon;
          |HAZ DesconciliaUno;
     |FINSI;

     |SI nTipoCon = 2;
          |BUCLE n43conslDos;
          nDiarioDesCon = nDiarioCon;
          fFechaDesCon = fFechaCon;
          nDocDesCon = nDocCon;
          nApunteDesCon = nApunteCon;
          |HAZ DesconciliaDos;
     |FINSI;

     |SI nTipoCon = 3;
          |DELINDEX #3;
          |ABRE #3;
          |BUCLE n43conslTres;     || Cargo el temporal
          |CIERRA #3;
          |BUCLE Tmp3;             || Lo recorre e igual que nTipoCon=2
     |FINSI;

     |HAZ PintaLabel1;
     |HAZ PintaLabel2;
     |SELECT #1#n43temp1;
     |SELECT #1#n43temp2;
     |REFRESCACONTROL nGridA;
     |REFRESCACONTROL nGridB;
|FPRC;

|PROCESO PeriMarcados1;
     |SI #refere1@#2 > fFechaMax; fFechaMax = #refere1@#2; |FINSI;
     |SI #refere1@#2 < fFechaMin; fFechaMin = #refere1@#2; |FINSI;
|FPRC;

|PROCESO PeriMarcados2;
     |SI #refere2@#1 > fFechaMax; fFechaMax = #refere2@#1; |FINSI;
     |SI #refere2@#1 < fFechaMin; fFechaMin = #refere2@#1; |FINSI;
|FPRC;

|DEFBUCLE PeriMarcados2;
     #refere2@#2001;
     32;
     aColorVerde, "N";
     aColorVerde, "N";
     ;
     NULL, PeriMarcados2;
|FIN;

|DEFBUCLE PeriMarcados1;
     #refere1@#2001;
     9;
     aColorVerde, "N";
     aColorVerde, "N";
     ;
     NULL, PeriMarcados1;
|FIN;

|PROCESO PeriodoBus;
     |SI nMargenDias = 0;
          fFechaMin = "01.01.0000";
          fFechaMax = "31.12.9999";
     |SINO;
          fFechaMin = "31.12.9999";
          fFechaMax = "01.01.0000";
          |DDEF aPath;
          aAlfa = aPath + "n43temp1";
          |CARGA_DEF aAlfa, refere1@;
          |SI FSalida = 0;
               aAlfa = aPath + "n43temp2";
               |CARGA_DEF aAlfa, refere2@;
               |SI FSalida = 0;
                    |SI aTempoBusA = "";
                         |NOME_DAT #refere1@#aTempo1#;
                    |SINO;
                         |NOME_DAT #refere1@#aTempoBusA#;
                    |FINSI;
                    |SI aTempoBusB = "";
                         |NOME_DAT #refere2@#aTempo2#;
                    |SINO;
                         |NOME_DAT #refere2@#aTempoBusB#;
                    |FINSI;
                    |SI aZona = "A";
                         |BUCLE PeriMarcados1;
                    |SINO;
                         |BUCLE PeriMarcados2;
                    |FINSI;
                    |DESCARGA_DEF #refere2@;
               |FINSI;
               |DESCARGA_DEF #refere1@;
          |FINSI;
          nNume = fFechaMin; nNume = nNume - nMargenDias; fFechaMin = nNume;
          nNume = fFechaMax; nNume = nNume + nMargenDias; fFechaMax = nNume;
     |FINSI;
|FPRC;

|PROCESO ImporteIgual;
     |SI aZona = "A";
          nImporteBuscar = nSuma1;
     |SINO;
          nImporteBuscar = nSuma2;
     |FINSI;
     |SI nImporteBuscar = 0;
          aMensaje = "0000Seleccione con [DobleClick] el registro base" + &13 + "para buscar por <Importe Igual>";
          |MENAV aMensaje;
          |ACABA;
     |FINSI;

     |HAZ PeriodoBus;

     aSwModoBusqueda = "I";
     |HAZ NucleoBuscar;
|FPRC;

|PROCESO BtnMargen;
     |VENTANA 1033, 1665, -1, 17, "Dias de margen";
     nVenMargen = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVenMargen;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     #102#0 = nMargenDias;
     |PINPA #0#102;
     |PINDA #0#102;
     |ENDATOS #1#102;
     aAlfa = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVenMargen;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVenMargen;
     |PULSATECLA;

     |SI aAlfa = 0;
          nMargenDias = #102#0;
          |HAZ PinMargen;
     |FINSI;
|FPRC;

|PROCESO PinMargen;
     |SI nLblMargen ! -1;
          |FIN_CONTROL_WINDOWS nLblMargen;
     |FINSI;
     |SI nMargenDias = 0;
          aAlfa = "Sin dias de margen";
     |SINO;
          aAlfa = "Dias des margen " + nMargenDias;
     |FINSI;
     aAlfa = "LABEL,{{001}}" + aAlfa;
     |CONTROL_SIMPLE nPantalla, aAlfa, 979, 999, NULL;
     nLblMargen = FSalida;
|FPRC;

|PROCESO Botones;
     ||GRIDA
     |CONTROL_SIMPLE nPantalla, "BOTON,{{163}} Busqueda", 1063, 1073, BotonA1;
     nBotonA1 = FSalida;

     |CONTROL_SIMPLE nPantalla, "BOTON,{{207}} Borrar Busqueda", 1075, 1089, BotonA2;
     nBotonA2 = FSalida;

     |CONTROL_SIMPLE nPantalla, "BOTON,Margen", 1091, 1098, BtnMargen;
     nBtnMargen = FSalida;

     |CONTROL_SIMPLE nPantalla, "BOTON,Marcados", 2106, 2116, BotonA3;
     nBotonA3 = FSalida;

     |CONTROL_SIMPLE nPantalla, "BOTON,Pendientes", 2118, 2128, BotonA4;
     nBotonA4 = FSalida;

/*
     |CONTROL_SIMPLE nPantalla, "BOTON,Todos", 2130, 2140, BotonA5;
     nBotonA5 = FSalida;
*/

     |CONTROL_SIMPLE nPantalla, "BOTON,{{238}}Marcar", 2155, 2168, BotonA6;
     nBotonA6 = FSalida;

     |CONTROL_SIMPLE nPantalla, "BOTON,{{236}}Desmarcar", 2170, 2183, BotonA7;
     nBotonA7 = FSalida;

     |CONTROL_SIMPLE nPantalla, "BOTON,{{240}}Importe Igual", 2185, 2198, BotonA8;
     nBotonA8 = FSalida;

     ||GRIDB
     |CONTROL_SIMPLE nPantalla, "BOTON,{{163}} Busqueda", 2363, 2373, BotonB1;
     nBotonB1 = FSalida;

     |CONTROL_SIMPLE nPantalla, "BOTON,{{207}} Borrar Busqueda", 2375, 2389, BotonB2;
     nBotonB2 = FSalida;

     |CONTROL_SIMPLE nPantalla, "BOTON,Marcados", 3406, 3416, BotonB3;
     nBotonB3 = FSalida;

     |CONTROL_SIMPLE nPantalla, "BOTON,Pendientes", 3418, 3428, BotonB4;
     nBotonB4 = FSalida;

/*
     |CONTROL_SIMPLE nPantalla, "BOTON,Todos", 3430, 3440, BotonB5;
     nBotonB5 = FSalida;
*/

     |CONTROL_SIMPLE nPantalla, "BOTON,{{238}}Marcar", 3455, 3468, BotonB6;
     nBotonB6 = FSalida;

     |CONTROL_SIMPLE nPantalla, "BOTON,{{236}}Desmarcar", 3470, 3483, BotonB7;
     nBotonB7 = FSalida;

     |CONTROL_SIMPLE nPantalla, "BOTON,{{240}}Importe Igual", 3485, 3498, BotonB8;
     nBotonB8 = FSalida;

/*
     |CONTROL_SIMPLE nPantalla, "BOTON,Conciliacion Automatica", 3660, 3678, BotonF1;
     nBotonF1 = FSalida;

     |CONTROL_SIMPLE nPantalla, "BOTON,Conciliacion por Importes", 3680, 3698, BotonF2;
     nBotonF2 = FSalida;
*/
     |HAZ PinMargen;
|FPRC;

|PROCESO n43datos;
     aCtaImpo = #n43datos#1;
     |QBF aCtaImpo;
     |SI aCtaImpo ! aCtaContable;
          |ACABA;
     |FINSI;

     |DDEFECTO #n43temp1;
     #n43temp1#9 = #n43datos#9;
     #n43temp1#0 = #n43datos#0;
     |LEE 101#n43temp1.=;
     |SI FSalida ! 0; |GRABA 020#n43temp1.b; |FINSI;
     #n43temp1#1 = #n43datos#1;
     #n43temp1#2 = #n43datos#2;
     #n43temp1#3 = #n43datos#3;
     #n43temp1#4 = #n43datos#4;
     #n43temp1#5 = #n43datos#5;
     #n43temp1#6 = #n43datos#6;
     #n43temp1#7 = #n43datos#7;
     #n43temp1#8 = #n43datos#8;
     #n43temp1#10 = #n43datos#10;
     #n43temp1#11 = #n43datos#11;
     #n43temp1#12 = #n43datos#12;
     |SI #n43temp1#9 = "S";
          #n43temp1#13 = aColorVerde;
     |SINO;
          #n43temp1#13 = aColorNegro;
     |FINSI;
     |GRABA 020#n43temp1.a;
     |LIBERA #n43temp1;
|FPRC;

|DEFBUCLE n43datos;
 #n43datos#1;
 11;
 000001, "N";
 999999, "N";
 ;
 NULL, n43datos;
|FIN;

|PROCESO camaasil;
     |SI #camaasil#18 = "û"; |ACABA; |FINSI;

     aMarca = "N";
     |SELECT #4#n43consl;
     #n43consl#18 = #camaasil#0;
     #n43consl#12 = #camaasil#1;
     #n43consl#13 = #camaasil#2;
     #n43consl#19 = #camaasil#3;
     |LEE 000#n43consl.=;
     |SI FSalida = 0;
          aMarca = "S";
     |FINSI;

     |DDEFECTO #n43temp2;
     #n43temp2#32 = aMarca;
     #n43temp2#0 = #camaasil#0;
     #n43temp2#1 = #camaasil#1;
     #n43temp2#2 = #camaasil#2;
     #n43temp2#3 = #camaasil#3;
     |LEE 101#n43temp2.=;
     |SI FSalida ! 0; |GRABA 020#n43temp2.b; |FINSI;
     #n43temp2#4 = #camaasil#4;
     #n43temp2#5 = #camaasil#5;
     #n43temp2#6 = #camaasil#6;
     #n43temp2#7 = #camaasil#7;
     #n43temp2#8 = #camaasil#8;
     #n43temp2#9 = #camaasil#9;
     #n43temp2#10 = #camaasil#10;
     #n43temp2#11 = #camaasil#11;
     #n43temp2#12 = #camaasil#12;
     #n43temp2#13 = #camaasil#13;
     #n43temp2#14 = #camaasil#14;
     #n43temp2#15 = #camaasil#15;
     #n43temp2#16 = #camaasil#16;
     #n43temp2#17 = #camaasil#17;
     #n43temp2#18 = #camaasil#18;
     #n43temp2#19 = #camaasil#19;
     #n43temp2#20 = #camaasil#20;
     #n43temp2#21 = #camaasil#21;
     #n43temp2#22 = #camaasil#22;
     #n43temp2#23 = #camaasil#23;
     #n43temp2#24 = #camaasil#24;
     #n43temp2#25 = #camaasil#25;
     #n43temp2#26 = #camaasil#26;
     #n43temp2#27 = #camaasil#27;
     #n43temp2#28 = #camaasil#28;
     #n43temp2#29 = #camaasil#29;
     #n43temp2#30 = #camaasil#30;
     |SI #n43temp2#32 = "S";
          #n43temp2#31 = aColorVerde;
     |SINO;
          #n43temp2#31 = aColorNegro;
     |FINSI;
     |GRABA 020#n43temp2.a;
     |LIBERA #n43temp2;
|FPRC;

|DEFBUCLE camaasil;
 #camaasil#3;
 ;
 aCtaContable, "01.01.0000", 000001, 0001;
 aCtaContable, "31.12.2099", 999999, 9999;
 ;
 NULL, camaasil;
|FIN;


|PROCESO CreaTemporales;
     ||ARA39

     |CONTROL_SIMPLE 0, "LABEL,{{5}}Cargando informacion ...", 1315, 1680, NULL;
     nBotonLabel = FSalida;
     |PTEC 802;  |PAUSA;

     |CIERRA #n43temp1;
     |CIERRA #n43temp2;

     aTempo1 = ("43" + Usuario) % -108;
     aTempo2 = ("44" + Usuario) % -108;
     aTempo3 = ("45" + Usuario) % -108;
     |NOME_DAT #1 aTempo1;
     |NOME_DAT #2 aTempo2;
     |NOME_DAT #3 aTempo3;
     |DELINDEX #n43temp1;
     |DELINDEX #n43temp2;
     |DELINDEX #n43temp3;

/*

     ||COMENTADO CON PRIEGO. NADA DE REINDEXADOS
     ||Si se a¤aden campos al n43datos o camaasil
     ||Hay que adaptar n43temp1 y n43temp2. (A¤adir en la posicion donde corresponda)
     ||Y mover de posicion el campo RGBCOLOR (siempre sera la ultima)
     ||Adaptando el cambio tb. en el codigo.

     aOrigen = aPathDatos + "n43datos.dat";
     aDestino = aPathDatos + aTempo1 + ".dat";
     |COPIA_FICHERO aOrigen, aDestino;
     aOrigen = aPathDatos + "n43datos.ddx";
     aDestino = aPathDatos + aTempo1 + ".ddx";
     |COPIA_FICHERO aOrigen, aDestino;
     aOrigen = aPathDatos + "n43datos.ixx";
     aDestino = aPathDatos + aTempo1 + ".ixx";
     |COPIA_FICHERO aOrigen, aDestino;

     aOrigen = aPathDatos + "camaasil.dat";
     aDestino = aPathDatos + aTempo2 + ".dat";
     |COPIA_FICHERO aOrigen, aDestino;
     aOrigen = aPathDatos + "camaasil.ddx";
     aDestino = aPathDatos + aTempo2 + ".ddx";
     |COPIA_FICHERO aOrigen, aDestino;
     aOrigen = aPathDatos + "camaasil.ixx";
     aDestino = aPathDatos + aTempo2 + ".ixx";
     |COPIA_FICHERO aOrigen, aDestino;

*/

     |ABRE #n43temp1;
     |ABRE #n43temp2;

     |BUCLE n43datos;
     |BUCLE camaasil;

     |FIN_CONTROL_WINDOWS nBotonLabel;
|FPRC;

|PROCESO ControlBotonesVer;
     |SI aModoVerA = "P";
          |ESTADO_CONTROL nBotonA3, "ENABLE";
          |ESTADO_CONTROL nBotonA4, "DISABLE";
          |ESTADO_CONTROL nBotonA5, "ENABLE";
     |FINSI;
     |SI aModoVerA = "C";
          |ESTADO_CONTROL nBotonA3, "DISABLE";
          |ESTADO_CONTROL nBotonA4, "ENABLE";
          |ESTADO_CONTROL nBotonA5, "ENABLE";
     |FINSI;
     |SI aModoVerA = "T";
          |ESTADO_CONTROL nBotonA3, "ENABLE";
          |ESTADO_CONTROL nBotonA4, "ENABLE";
          |ESTADO_CONTROL nBotonA5, "DISABLE";
     |FINSI;
     |SI aModoVerB = "P";
          |ESTADO_CONTROL nBotonB3, "ENABLE";
          |ESTADO_CONTROL nBotonB4, "DISABLE";
          |ESTADO_CONTROL nBotonB5, "ENABLE";
     |FINSI;
     |SI aModoVerB = "C";
          |ESTADO_CONTROL nBotonB3, "DISABLE";
          |ESTADO_CONTROL nBotonB4, "ENABLE";
          |ESTADO_CONTROL nBotonB5, "ENABLE";
     |FINSI;
     |SI aModoVerB = "T";
          |ESTADO_CONTROL nBotonB3, "ENABLE";
          |ESTADO_CONTROL nBotonB4, "ENABLE";
          |ESTADO_CONTROL nBotonB5, "DISABLE";
     |FINSI;
|FPRC;

|PROCESO CtaContable;
     |HAZ SelCtaContable;
     |SI nSwSelCtaOK = 0; |O aCtaContable = "";
          |ACABA;
     |FINSI;

     ||Tendra que inicializar las variables de Busqueda
     ||o similares que tengamos para controlar la aplicacion.  BotonA2/B2
     |HAZ BotonA2;
     |HAZ BotonB2;

     |HAZ CreaTemporales;      ||Crea los 2 temporales, actualizando la informacion
     |REFRESCACONTROL nGridA;
     |REFRESCACONTROL nGridB;
|FPRC;

|PROCESO SelCtaContable;
     |VENTANA 1701, 2799, -1, -1, "Cuenta Contable";
     nVentanaSel = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentanaSel;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |DDEFECTO #n43wctac;
     |PINPA #0#n43wctac;
     nPantallaSel = FSalida;
     |PINDA #0#n43wctac;

     |ENDATOS #1#n43wctac;
     |SI FSalida = 0;
          aCtaContable = #n43wctac#0;
          aDesCtaConta = #n43wctac#1;
          |QBF aCtaContable;
          |QBF aDesCtaConta;
          |SI aCtaContable ! "";
               nSwSelCtaOK = 1;
          |SINO;
               nSwSelCtaOK = 0;
          |FINSI;
     |SINO;
          aCtaContable = "";
          aDesCtaConta = "";
          nSwSelCtaOK = 0;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentanaSel;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVentanaSel;

     |SI nSwSelCtaOK = 1;
          aLabel = "Cuenta Contable: " + aCtaContable + "/" + aDesCtaConta;
          aLabel = aLabel % 180;
          |SI nBotonCta ! 0;
               |FIN_CONTROL_WINDOWS nBotonCta;
          |FINSI;
          aLabel = "BOTON," + aLabel;
          |CONTROL_SIMPLE nPantalla, aLabel, 3602, 3778, CtaContable;
          nBotonCta = FSalida;
     |FINSI;
|FPRC;

|PROGRAMA;
     |CLS;

     |HAZ inicio;

     |DFICE aPathDatos;
     |QBF aPathDatos;

     |PINPA #0#n43asico;
     nPantalla = FSalida;

     |HAZ PonFoto;

     |ABRE #n43temp1;
     |ABRE #n43temp2;

     |ABRET #n43datos;
     |ABRET #camaasil;
     |ABRET #n43consl;

     aModoVerA = "P";
     aDesdeA = "N";
     aHastaA = "N";
     aModoVerB = "P";
     aDesdeB = "N";
     aHastaB = "N";

     aCtaContable = "";
     |HAZ SelCtaContable;
     |SI nSwSelCtaOK = 0; |O aCtaContable = "";
          aMensaje = "000OProceso cancelado";
          |MENAV aMensaje;
          |ACABA;
     |FINSI;
     |HAZ CreaTemporales;      ||Crea los 2 temporales, actualizando la informacion

     |HAZ Lineales;

     |HAZ Botones;

     |HAZ PintaLabel1;
     |HAZ PintaLabel2;

     |ESTADO_CONTROL nBotonA2, "DISABLE";
     |ESTADO_CONTROL nBotonA6, "DISABLE";
     |ESTADO_CONTROL nBotonA7, "DISABLE";
     |ESTADO_CONTROL nBotonB2, "DISABLE";
     |ESTADO_CONTROL nBotonB6, "DISABLE";
     |ESTADO_CONTROL nBotonB7, "DISABLE";

     |HAZ ControlBotonesVer;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::Salir," + nGridA;
          |LEETECLA aTecla;
          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
          |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |CIERRAT #n43datos;
     |CIERRAT #camaasil;
     |CIERRAT #n43consl;
     |CIERRA #n43temp1;
     |CIERRA #n43temp2;
     |DELINDEX #n43temp1;
     |DELINDEX #n43temp2;

     |SI aTempoBusA ! "";
          aBorraTempo = aTempo1;
          |HAZ EliminaFisicamente;
     |FINSI;
     |SI aTempoBusB ! "";
          aBorraTempo = aTempo2;
          |HAZ EliminaFisicamente;
     |FINSI;

     |FIN_CONTROL_WINDOWS nBotonA1;
     |FIN_CONTROL_WINDOWS nBotonA2;
     |FIN_CONTROL_WINDOWS nBotonA3;
     |FIN_CONTROL_WINDOWS nBotonA4;
     |FIN_CONTROL_WINDOWS nBotonA5;
     |FIN_CONTROL_WINDOWS nBotonA6;
     |FIN_CONTROL_WINDOWS nBotonA7;
     |FIN_CONTROL_WINDOWS nBotonA8;
     |FIN_CONTROL_WINDOWS nBotonB1;
     |FIN_CONTROL_WINDOWS nBotonB2;
     |FIN_CONTROL_WINDOWS nBotonB3;
     |FIN_CONTROL_WINDOWS nBotonB4;
     |FIN_CONTROL_WINDOWS nBotonB5;
     |FIN_CONTROL_WINDOWS nBotonB6;
     |FIN_CONTROL_WINDOWS nBotonB7;
     |FIN_CONTROL_WINDOWS nBotonB8;
     |FIN_CONTROL_WINDOWS nBotonFoto;
     |SI nBotonCta ! 0;
          |FIN_CONTROL_WINDOWS nBotonCta;
     |FINSI;
     |FIN_CONTROL_WINDOWS nBtnMargen;
|FPRO;
