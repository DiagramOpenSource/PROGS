|FICHEROS;
   carbum01 #0,MANTE;
   carbuz02 #10;
   camaniva #1;
   caivalin #2;

   canempre #30;
   cafantas #98;
   :/modelos/def/dsmom030 #130;

  &espejo@  #900;
   carbuw02 #999,MANTE;
|FIN;

|VARIABLES;
   aAlfa1    = "";
   aAlfa2    = "";
   nInkey    = 0;
   esalta    = 0;
   &snFilBa  = "";
   nNume1    = 0;
   nNume2    = 0;
   nPara1    = 0;
   fDFecha   = @;
   fHFecha   = @;
   fDFechaC  = @;
   fHFechaC  = @;
   fDFechaV  = @;
   fHFechaV  = @;
   nClave    = 0;
   aPeriodo   = "";
   nLibro     = 0;
   nDocumento = 0;
   aPath      = "";
   nSwOp      = 0;
   nError     = 0;
   fFecha     = @;
   nEjer      = 0;
   nIva       = 0;
   nREBUReg   = 0;
   nCopia     = 0;
   nNumero    = 0;
   aFichero   = "";
   nSwIgual   = 0;
   nCambioC   = 0;
   nCambioV   = 0;
   nConcepto  = 0;

   &enSwAlta     = 0;
   &enSwPregunta = 0;
   &nSwPinta     = 0;
   &enPrimero    = 0;
   &enSalir      = 0;
   &enNuevaAlta  = 0;
|FIN;

|INCLUYE dscont_i;

**************************************************************************
|CALCULO PresentaLinea;  |TIPO 7;
    |BLIN 4;
    #10#3 = "";
    enEmpresa   = #0#0;
    enActividad = #0#3;
    eaCodDep    = #0#3;
    |HAZ Actividad;
    |SI eswLect = 0;
       #10#3 = eaNombreAct;
    |FINSI;
    |PINTA #10#3;
|FCAL;

|CALCULO elTipo1;  |TIPO 1;
    esalta = (FEntrada / 100) ! 0;
    |SI esalta ! 2; |ERROR; |FINSI;
|FCAL;

|CALCULO AntesDepar; |TIPO 7;
   |SI eaDepar = "N"; |ACABA; |FINSI;
   |SI Haybasica = 1;
       |MENSA "    <F2> Consultar Actividades";
   |SINO;
       |MENSA "    <F2> Consultar Departamentos";
   |FINSI;
|FCAL;

|CALCULO TrasDepar; |TIPO 0;
   |SI eaDepar = "N"; |ACABA; |FINSI;
   eOpDep = 0; || 1 = Consulta pantalla
   |SI FSalida = 10; eOpDep = 1;  |FINSI;
   enEmpresa   = #0#0;
   enActividad = #0#3;
   eaCodDep    = #0#3;
   |HAZ Actividad;
   |SI eswLect = 1;
       |MENAV "    Error.! No Existe Actividad / Departamento";
       |ERROR;
   |SINO;
       #0#3  = eaCodDep;    |PINTA #0#3;
       #10#3 = eaNombreAct; |PINTA #10#3;
       |SI efActFin > "01.01.1900"; |Y snFilBa = "S";
           aAlfa1 = "    NAtencion!!!. Actividad con Fecha de BAJA " + efActFin + ", Desea introducir Facturas ? ";
           |CONFI aAlfa1;
           |SI FSalida ! 0;
               |ERROR;
               |ACABA;
           |FINSI;
       |FINSI;
       |SI eaEsCom = "S";
           |CONFI "    NAtencion!!!. Actividad Comunera, ¨ Desea introducir Movimientos ? ";
           |SI FSalida ! 0;
               |ERROR;
               |ACABA;
           |FINSI;
       |FINSI;
   |FINSI;
|FCAL;

||////////////////////////// Ver facturas
|PROCESO MiroOtrasOp;
      |ABRE #900;
      |SI aTipoIVA = "S";
          |SELECT #4#900;
      |SINO;
          |SELECT #5#900;
      |FINSI;
      #900#0  = enEmpresa;
      |SI aTipoIVA = "S";
          #900#20 = aPeriodo;
          #900#4  = #1#0;
          #900#5  = #1#1;
      |SINO;
          #900#21 = aPeriodo;
          #900#11 = #1#0;
          #900#12 = #1#1;
      |FINSI;
      |LEE 041#900=;
      |SI FSalida = 0; |Y #900#1 ! #1#1;
          nError = 4;
      |FINSI;
      |SELECT #1#900;
      |CIERRA #900;
|FPRC;

|PROCESO MiroConcepto;
  |DDEFECTO #dsmom030;
  #dsmom030#0 = nConcepto;
  |LEE 001#dsmom030.=;

  nREBUReg = 0;
  |SI #dsmom030#24 = 5; |O #dsmom030#24 = 6;
      nREBUReg = 1;
  |SINO;
      |PARA nNume1 = 78; |SI nNume1 [ 101; |HACIENDO nNume1 = nNume1 + 1;
            |SI #dsmom030#nNume1# = 5; |O #dsmom030#nNume1# = 6;
                nREBUReg = 1;
                |SAL;
            |FINSI;
            |SI nNume1 = 89; nNume1 = 93; |FINSI;
            |SI nNume1 = 95; nNume1 = 99; |FINSI;
      |SIGUE;
  |FINSI;

  nCopia = 0;
  |SI nREBUReg = 1;
      |SI aTipoIVA = "R"; |Y #dsmom030#34 = 5; nCopia = 1; |FINSI;
      |SI aTipoIVA = "S"; |Y #dsmom030#36 = 4; nCopia = 1; |FINSI;
  |FINSI;
|FPRC;

|PROCESO ElPath;
      |DFICB aPath;
      aAlfa1 = enEmpresa; aAlfa1 = ("00000" + aAlfa1) % -105;
      aAlfa2 = aPeriodo;  aAlfa2 = ("0"     + aAlfa2) % -101;
      aPath  = aPath + aAlfa1 + aAlfa2 + "/";
      |PATH_DAT #1 aPath;
      |PATH_DAT #2 aPath;
|FPRC;

|PROCESO SituaConta;
  nError = 0;
  |SI aPeriodo = " "; |Y fFecha ! "01.01.0000";
      aAlfa1 = fFecha;
      aAlfa1 = aAlfa1 % -102;
      nEjer  = aAlfa1;
      |ABRE #30;
      |SELECT #4#30;
      #30#2  = enEmpresa;
      #30#26 = nEjer;
      |LEE 001#30=;
      |SI FSalida = 0;
          aPeriodo = #30#3;
          |SI aTipoIVA = "S"; #0#20 = aPeriodo; |FINSI;
          |SI aTipoIVA = "R"; #0#21 = aPeriodo; |FINSI;
      |FINSI;
      |SELECT #1#30;
      |CIERRA #30;
  |FINSI;

  |SI aPeriodo ! " ";
      |HAZ ElPath;
  |SINO;
      aPeriodo = " ";
      nError   = 2;
  |FINSI;
|FPRC;

|PROCESO BuscaDato;
  #900#0  = enEmpresa;
  |SI aTipoIVA = "S";
      #900#20 = aPeriodo;
      #900#4  = #1#0;
      #900#5  = #1#1;
  |SINO;
      #900#21 = aPeriodo;
      #900#11 = #1#0;
      #900#12 = #1#1;
  |FINSI;
  |LEE 041#900=;
  |SI FSalida = 0;
      #999#15 = #900#1;
      #999#16 = #900#2;
  |FINSI;
|FPRC;

|PROCESO caivalin;
  |SI #2#7 ! 0;  nIva = #2#7; |FINSI;
|FPRC;

|DEFBUCLE caivalin
  #2#1;
  ;
  #1#0, #1#1, 01;
  #1#0, #1#1, 99;
  e;
  NULL, caivalin;
|FIN;

|PROCESO IgualaCampos;
  |SI aTipoIVA = "S";
      #0#8  = #1#4;  |PINTA #0#8;    || Fecha factura
      #0#4  = #1#0;  |PINTA #0#4;    || Libro
      #0#5  = #1#1;  |PINTA #0#5;    || Documento
      #0#6  = #1#2;  |PINTA #0#6;    || Serie
      #0#7  = #1#3;  |PINTA #0#7;    || Factura
      #0#9  = #1#22; |PINTA #0#9;    || Referencia
      #0#10 = #1#21; |PINTA #0#10;   || Importe
  |SINO;
      nIva = 0;
      |BUCLE caivalin;
      #0#15 = #1#4;  |PINTA #0#15;   || Fecha factura
      #0#11 = #1#0;  |PINTA #0#11;   || Libro
      #0#12 = #1#1;  |PINTA #0#12;   || Documento
      #0#13 = #1#2;  |PINTA #0#13;   || Serie
      #0#14 = #1#3;  |PINTA #0#14;   || Factura
      #0#16 = #1#22; |PINTA #0#16;   || Referencia
      #0#17 = #1#21; |PINTA #0#17;   || Importe
      #0#18 = nIva;  |PINTA #0#18;   || % IVA
      #0#19 = #1#23; |PINTA #0#19;   || Cuotas
  |FINSI;
|FPRC;

|PROCESO IgualaDesdeIVA;
     |SI #10#4 = "S";
         #0#8  = #10#10;  #0#4  = #10#6;
         #0#5  = #10#7;   #0#6  = #10#8;
         #0#7  = #10#9;   #0#9  = #10#11;
         #0#10 = #10#12;
         #0#20 = enPeriodo;
     |SINO;
         #0#15 = #10#10;  #0#11 = #10#6;
         #0#12 = #10#7;   #0#13 = #10#8;
         #0#14 = #10#9;   #0#16 = #10#11;
         #0#17 = #10#12;  #0#18 = #10#13;
         #0#19 = #10#14;
         #0#21 = enPeriodo;
     |FINSI;

     |SI nSwPinta = 1;
         |SI enSwAlta = 2;
             |PINDA #0#0;
         |SINO;
             |SI #10#4 = "S";
                 |PINTA #0#8;  |PINTA #0#4;
                 |PINTA #0#5;  |PINTA #0#6;
                 |PINTA #0#7;  |PINTA #0#9;
                 |PINTA #0#10;
             |SINO;
                 |PINTA #0#15; |PINTA #0#11;
                 |PINTA #0#12; |PINTA #0#13;
                 |PINTA #0#14; |PINTA #0#16;
                 |PINTA #0#17; |PINTA #0#18;
                 |PINTA #0#19;
             |FINSI;
        |FINSI;
    |FINSI;
|FPRC;

|PROCESO CargarIVA;

  nConcepto = #1#27;
  |HAZ MiroConcepto;
  |SI nCopia = 1;
      nNumero = nNumero + 1;
      |DDEFECTO #999;
      #999#0  = nNumero;
      #999#1  = aTipoIVA;
      #999#2  = #1#0;
      #999#3  = #1#1;
      #999#4  = #1#2;
      #999#5  = #1#3;
      #999#6  = #1#27;
      #999#7  = #1#28;
      #999#8  = #1#19;
      #999#9  = #1#23;
      #999#10 = #1#21;
      #999#11 = #1#4;
      #999#12 = #1#12;
      #999#13 = #1#13;
      #999#14 = #1#22;
      |HAZ BuscaDato;
      #999#17 = aPeriodo;
      #999#18 = aPath;
      |SI aTipoIVA = "R";
         nIva = 0;
         |BUCLE caivalin;
         #999#19 = nIva;
      |FINSI;
      |GRABA 020#999n;
  |FINSI;
|FPRC;

|DEFBUCLE CargarIVA;
  #1#1;
  4,36;
  01, 0000000001, fDFecha, aTipoIVA;
  99, 9999999999, fHFecha, aTipoIVA;
  e;
  NULL, CargarIVA;
|FIN;

|PROCESO HazTodo;
      |ABRE #130;
      |ABRE #999;
      |ABRE #900;
      |SI aTipoIVA = "S";
          |SELECT #4#900;
      |SINO;
          |SELECT #5#900;
      |FINSI;
      |BUCLE CargarIVA;
      |SELECT #1#900;
      |CIERRA #900;
      |CIERRA #999;
      |CIERRA #130;
|FPRC;

|PROCESO LeeCanempre;
      aPeriodo = #30#3;
      |HAZ ElPath;
      |HAZ HazTodo;
|FPRC;

|DEFBUCLE LeeCanempre;
   #30#1;
   ;
   enEmpresa, 0;
   enEmpresa, 9;
   ;
   NULL, LeeCanempre;
|FIN;

|PROCESO IgualaAuxiliar;
  |SI aTipoIVA = "S";
      #0#8  = #999#11;     || Fecha factura
      #0#4  = #999#2;      || Libro
      #0#5  = #999#3;      || Documento
      #0#6  = #999#4;      || Serie
      #0#7  = #999#5;      || Factura
      #0#9  = #999#14;     || Referencia
      #0#10 = #999#10;     || Importe
      #0#20 = #999#17;
  |SINO;
      #0#15 = #999#11;     || Fecha factura
      #0#11 = #999#2;      || Libro
      #0#12 = #999#3;      || Documento
      #0#13 = #999#4;      || Serie
      #0#14 = #999#5;      || Factura
      #0#16 = #999#14;     || Referencia
      #0#17 = #999#10;     || Importe
      #0#18 = #999#19;     || % IVA
      #0#19 = #999#9;      || Cuotas
      #0#21 = #999#17;
  |FINSI;
  nSwIgual = 1;
|FPRC;

|PROCESO pidetecla; |TIPO 11;
  nInkey = FSalida;
  |SI nInkey = 10;
      |SI #999#15 ! 0; |Y #999#15 ! #0#1;
          |MENAV "    Error. Esta factura ya esta asignada a un Codigo Operacion";
          |ERROR;
          |ACABA;
      |FINSI;
      |HAZ IgualaAuxiliar;
      |PTEC 806;
  |FINSI;
|FPRC;

|PROCESO Inf999;
      #999#0 = 1;
|FPRC;

|PROCESO Sup999;
      #999#0 = nNumero;
|FPRC;

|PROCESO BuscarFra;
      aFichero = ("1z" + Usuario) % 108;
      |NOME_DAT #999 aFichero;
      |DELINDEX #999;

      nSwIgual = 0;
      nNumero  = 0;
      |SI fFecha > "01.01.1900";
          fDFecha = fFecha;
      |SINO;
          fDFecha  = fec1;
      |FINSI;
      fHFecha  = fec2;
      #98#10 = fDFecha;  #98#11 = fHFecha;

      |PUSHV 1022 2350;
      |PINTA 1023, "                           ";
      |PINTA 1123, " Desde Fecha:              ";
      |PINTA 1223, " Hasta Fecha:              ";
      |PINTA 1323, "                           ";
      |CUADRO 1022 1350;
      |C_P #98#10, 1, 1137; |PINTA #98#10;
      |C_P #98#11, 1, 1237; |PINTA #98#11;

    |ET Camp10;
       |ENCAMPO #10#98;
       |SI FSalida = 1; |VETE  Campo0; |FINSI;
       |SI aTipoIVA = "R";
           |SI #98#10 < fec1; |O #98#10 > fec2;
               |MENAV "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
               |VETE Camp10;
           |FINSI;
       |FINSI;

    |ET Camp11;
       |ENCAMPO #11#98;
       |SI FSalida = 2; |VETE Camp10; |FINSI;
       |SI #98#11 < #98#10;
           |MENAV "    Error. Fecha hasta inferior a la Fecha desde";
           |VETE Camp11;
       |FINSI;
       |SI aTipoIVA = "R";
           |SI #98#11 < fec1; |O #98#11 > fec2;
               |MENAV "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
               |VETE Camp11;
           |FINSI;
       |FINSI;

    |ET Campo0;
       |POPV;
       fDFecha = #98#10;
       fHFecha = #98#11;


  |SI fDFecha ] fec1; |Y fHFecha [ fec2;
      aPeriodo = enPeriodo;
      |DFICE aPath;
      |PATH_DAT #1 aPath;
      |PATH_DAT #2 aPath;
      |HAZ HazTodo;
  |SINO;
      |BUCLE LeeCanempre;
  |FINSI;

      |SI nNumero ! 0;
          |PUSHV 0501 2380;
          |PINPA #0#999;
          |PINTA 0716, #0#1;
          |PINTA 0816, #0#2;
          aAlfa1 = "COMPRA";|SI aTipoIVA = "R"; aAlfa1 = "VENTA"; |FINSI;
          aAlfa1 = "BUSCANDO LA FACTURA DE " + aAlfa1;
          |ATRI P; |PINTA 0740, aAlfa1; |ATRI 0;
          |ENTLINEAL #1#999, -1, 4, 22, 0, Inf999, Sup999;
          |POPV;
          |SI nSwIgual = 1;
              |SI aTipoIVA = "S";
                  |PINTA #0#8;  |PINTA #0#4;
                  |PINTA #0#5;  |PINTA #0#6;
                  |PINTA #0#7;  |PINTA #0#9;
                  |PINTA #0#10;
              |SINO;
                  |PINTA #0#15;  |PINTA #0#11;
                  |PINTA #0#12;  |PINTA #0#13;
                  |PINTA #0#14;  |PINTA #0#16;
                  |PINTA #0#17;  |PINTA #0#18;
                  |PINTA #0#19;
              |FINSI;
          |FINSI;
      |SINO;
          |MENAV "    No existe Facturas en la seleccion";
      |FINSI;
|FPRC;

|PROCESO ConsultaFra;
  nError = 0;
  |HAZ SituaConta;
  |SI nError = 0;
      |ABRE #1;
      #1#0 = nLibro;
      #1#1 = nDocumento;
      |LEE 001#1=;
      |SI FSalida ! 0;
          nError = 1;
          |SI nSwOp = 1;
              |MENAV "    No existe Registro de la Factura";
          |FINSI;
      |SINO;
          |SI nSwOp = 1;
             |PUSHV 0501 2380;
             |PINPA #0#1;
             |PTEC 815;
             |PTEC 802;
             |PTEC 802;
             |CONSULTA #0#1;
             |POPV;
          |SINO;
             nConcepto = #1#27;
             |ABRE #30; |HAZ MiroConcepto; |CIERRA #30;
             |SI nCopia = 1;
                 |HAZ IgualaCampos;
             |SINO;
                 nError = 3;
             |FINSI;
             |HAZ MiroOtrasOp;
          |FINSI;
      |FINSI;
      |CIERRA #1;
  |SINO;
      |SI nSwOp = 1;
          |MENAV "    No existe Ejercicio Contable";
      |FINSI;
  |FINSI;
|FPRC;

|CALCULO VerFraCompra; |TIPO 0;
 nInkey = FSalida;

 |C_M #0Campo, 0, aAlfa1;
 |SI aAlfa1 = "N"; |ACABA; |FINSI;

 aTipoIVA   = "S";
 fFecha     = #0#8;
 aPeriodo   = #0#20;
 nLibro     = #0#4;
 nDocumento = #0#5;

 |SI nInkey = 10; |O nInkey = 11;
     |SI #0#4 = 00; |Y #0#5 = 0; |Y nInkey = 11;
         |ERROR;
         |ACABA;
     |FINSI;

     |SI nInkey = 10;
         |HAZ BuscarFra;
     |FINSI;

     |SI nInkey = 11;
         nSwOp = 1; |HAZ ConsultaFra;
         |ACABA;
     |FINSI;
 |SINO;
     |SI Campo = 4; |O Campo = 5;
         nNume1 = Contenido;
         |SI nNume1 ! #0Campo; nCambioC = 1; |FINSI;
     |FINSI;
 |FINSI;

 |SI #0#4 = 00;
     |C_M #0#5, 1, "N";
     #0#5 = 0;  |PINTA #0#5;
     #0#6 = ""; |PINTA #0#6;
     #0#7 = 0;  |PINTA #0#7;
 |SINO;
     |C_M #0#5, 1, "S";
 |FINSI;

 |SI Campo = 5;
     |SI #0#4 ! 0; |Y #0#5 ! 0; |Y nCambioC = 1;
         nSwOp = 0; |HAZ ConsultaFra; nCambioC = 0;
         |SI nError ! 0;
             aAlfa1 = "    NNo existe Factura. Continuar";
             |SI nError = 3; aAlfa1 = "    NLa Factura No tiene el Concepto Correcto. Continuar"; |FINSI;
             |SI nError = 4; aAlfa1 = "    NFactura ya asignada a otra operacion. Continuar"; |FINSI;
             |CONFI aAlfa1;
             |SI FSalida ! 0;
                 |ERROR;
                 aPeriodo = " ";
             |FINSI;
         |FINSI;
         #0#20 = aPeriodo;
     |FINSI;
 |FINSI;

|FCAL;

|CALCULO VerFraVenta; |TIPO 0;
 nInkey = FSalida;

 |C_M #0Campo, 0, aAlfa1;
 |SI aAlfa1 = "N"; |ACABA; |FINSI;

 aTipoIVA   = "R";
 fFecha     = #0#15;
 aPeriodo   = #0#21;
 nLibro     = #0#11;
 nDocumento = #0#12;

 |SI nInkey = 10; |O nInkey = 11;
     |SI #0#11 = 00; |Y #0#12 = 0; |Y nInkey = 11;
         |ERROR;
         |ACABA;
     |FINSI;

     |SI nInkey = 10;
         |HAZ BuscarFra;
     |FINSI;

     |SI nInkey = 11;
         nSwOp = 1; |HAZ ConsultaFra;
         |ACABA;
    |FINSI;

 |SINO;
    |SI Campo = 10; |O Campo = 11;
        nNume1 = Contenido;
        |SI nNume1 ! #0Campo; nCambioV = 1; |FINSI;
    |FINSI;
 |FINSI;

 |SI #0#11 = 00;
     |C_M #0#12, 1, "N";
     #0#12 = 0;  |PINTA #0#12;
     #0#13 = ""; |PINTA #0#13;
     #0#14 = 0;  |PINTA #0#14;
 |SINO;
     |C_M #0#12, 1, "S";
 |FINSI;

 |SI Campo = 12;
     |SI #0#11 ! 0; |Y #0#2 ! 0; |Y nCambioV = 1;
         nSwOp = 0; |HAZ ConsultaFra; nCambioV = 0;
         |SI nError ! 0;
             aAlfa1 = "    NNo existe Factura. Continuar";
             |SI nError = 3; aAlfa1 = "    NLa Factura No tiene el Concepto Correcto. Continuar";|FINSI;
             |SI nError = 4; aAlfa1 = "    NFactura ya asignada a otra operacion. Continuar"; |FINSI;
             |CONFI aAlfa1;
             |SI FSalida ! 0;
                 |ERROR;
                 aPeriodo = " ";
             |FINSI;
         |FINSI;
         #0#21 = aPeriodo;
     |FINSI;
 |FINSI;

|FCAL;

|CALCULO AntesFecha; |TIPO 7;
   |SI Campo = 8; |Y #10#4 = "R";
       |MENSA "    Pulse <F2> buscar factura, <F3> consultar factura";
   |FINSI;
   |SI Campo = 15; |Y #10#4 = "S";
       |MENSA "    Pulse <F2> buscar factura, <F3> consultar factura";
   |FINSI;
|FCAL;

|CALCULO CalFecha; |TIPO 0;
  nInkey = FSalida;
  |BLIN 4;
  |SI nInkey = 2; |ACABA; |FINSI;

  |SI nInkey = 10; |O nInkey = 11;
      |SI Campo = 8;  |Y #10#4 = "S"; |ERROR; |ACABA; |FINSI;
      |SI Campo = 15; |Y #10#4 = "R"; |ERROR; |ACABA; |FINSI;

      |SI Campo = 8;  |HAZ VerFraCompra; |FINSI;
      |SI Campo = 15; |HAZ VerFraVenta;  |FINSI;
      |ACABA;
  |FINSI;

  |SI Campo = 8;
      |SI #0#8 [ "01.01.1900";
          |MENAV "    Error. No puede dejar la Fecha de Compra en Blanco";
          |ERROR;
          |ACABA;
      |FINSI;
      |SI #10#4 = "S";
          |SI #0#8 < fec1; |O #0#8 > fec2;
              |MENAV "    Fecha de Compra fuera del Ejercicio. ";
              |ERROR;
          |FINSI;
      |FINSI;
  |FINSI;
  |SI Campo = 15;
      |SI #0#15 > "01.01.0000";
          |SI #0#15 < #0#8;
              |MENAV "    Error, Fecha de Venta inferior a Fecha de Compra";
              |ERROR;
              |ACABA;
          |FINSI;
          |SI #0#15 < fec1; |O #0#15 > fec2;
              |MENAV "    Fecha de Venta fuera del Ejercicio.";
              |ERROR;
          |FINSI;
      |FINSI;
  |FINSI;
|FCAL;

|CALCULO CalIVA; |TIPO 0;
  nNume1 = Contenido;
  |SI nNume1 = #0Campo; |ACABA; |FINSI;

  nNume1 = #0#17 - #0#10;
  |SI #0#18 ! 0;
      nNume2 = (nNume1 * 100) / (100 + #0#18);
      #0#19  = (nNume2 % #0#18) ! 2;
  |SINO;
      #0#19 = 0;
  |FINSI;
  |PINTA #0#19;
|FCAL;

|CALCULO elTipo0; |TIPO 0;
    nInkey = FSalida;
    esalta = (FEntrada / 100) ! 0;
    |SI esalta = 1; |ERROR; |FINSI;
    |BLIN 4;
|FCAL;

|CALCULO elTipo7C2; |TIPO 7;
  nSwPinta = 1; |HAZ IgualaDesdeIVA;
|FCAL;

|CALCULO elTipo3; |TIPO 3;

    |SI FSalida = 7; enSalir = 1; |ACABA; |FINSI;

    #10#16 = 0;
    #0#0  = #10#1;
    |SI #0#8 [ "01.01.1900";
        |MENAV "    Error. No puede dejar los datos de la Compra en Blanco";
        |ERROR;
        |ACABA;
    |FINSI;
    |SI #0#15 > "01.01.0000";
        |SI #0#15 < #0#8;
            |MENAV "    Error, Fecha de Venta inferior a Fecha de Compra";
            |ERROR;
            |ACABA;
        |FINSI;
    |FINSI;
    nCambioV = 0;
    nCambioC = 0;

    |SI enSwAlta = 2;

        #10#16 = #0#1; enSalir = 4;

    |SINO;
        enNuevaAlta = 1;
        |CONFI "    SDatos Correctos S/N";
        |SI FSalida ! 0;
            enSalir = 1;  ||hay error y no graba y salir

            |SI enSwPregunta = 0;
                |SI #10#4 = "S";
                    aAlfa1 = "    SDesea Modificar un elemeto Existente";
                |SINO;
                    aAlfa1 = "    SDesea dar de Alta un Elemento Nuevo";
                |FINSI;
                |CONFI aAlfa1;
                |SI FSalida = 0;
                    |SI #10#4 = "S"; enSalir = 2; |FINSI;
                    |SI #10#4 = "R"; enSalir = 3; |FINSI;
                |FINSI;
            |FINSI;
            |PTEC 806;
        |SINO;
            #10#16 = #0#1; enSalir = 4;  ||bueno y salir
            |HAZ Actu900;
            |PTEC 806;
        |FINSI;
    |FINSI;
|FCAL;

|PROCESO Inf900;
   #900#0  = enEmpresa;
   #900#8  = fDFechaC;
   #900#15 = fDFechaV;
   #900#1  = 000001;
|FPRC;

|PROCESO Sup900;
   #900#0  = enEmpresa;
   #900#8  = fHFechaC;
   #900#15 = fHFechaV;
   #900#1  = 999999;
|FPRC;

|PROCESO Actu900;
  |ABRE #900;
  #900#0 = #0#0;
  #900#1 = #0#1;
  |LEE 041#900=;
  |SI FSalida ! 0;
      |DDEFECTO #900;
      #900#0 = #0#0;
      #900#1 = #0#1;
      |GRABA 020#900b;
  |FINSI;
  |PARA nPara1 = 2; |SI nPara1 [ 21; |HACIENDO nPara1 = nPara1 + 1;
        #900nPara1 = #0nPara1;
  |SIGUE;
  |GRABA 020#900a;
  |LIBERA #900;
  |CIERRA #900;
|FPRC;

|CALCULO elTipo11; |TIPO 11;
   nInkey = FSalida;

   |SI nInkey ] 10; |Y nInkey [ 13;

       |SI nInkey = 10;   || Todas
           fDFechaC = "01.01.0000";
           fHFechaC = "31.12.2999";
           fDFechaV = "01.01.0000";
           fHFechaV = "31.12.2999";
           nClave = 1;
       |FINSI;
       |SI nInkey = 11;   || Pendientes Venta
           fDFechaC = "01.01.0000";
           fHFechaC = "31.12.2999";
           fDFechaV = "01.01.0000";
           fHFechaV = "01.01.0000";
           nClave = 3;
       |FINSI;
       |SI nInkey = 12;  || Compras Ejercicio
           fDFechaC = fec1;
           fHFechaC = fec2;
           fDFechaV = "01.01.0000";
           fHFechaV = "31.12.2999";
           nClave = 2;
       |FINSI;
       |SI nInkey = 13;  || Ventas Ejercicio
           fDFechaC = "01.01.0000";
           fHFechaC = "31.12.2999";
           fDFechaV = fec1;
           fHFechaV = fec2;
           nClave = 3;
       |FINSI;

       |ABRE #900;

       #900#0 = #0#0;
       #900#1 = #0#1;
       |SI nClave = 2;
           |CONSULTA_F_CLAVE #2#900, Inf900, Sup900;
       |FINSI;
       |SI nClave = 3;
           |CONSULTA_F_CLAVE #3#900, Inf900, Sup900;
       |FINSI;
       |SI nClave = 1;
           |CONSULTA_F_CLAVE #1#900, Inf900, Sup900;
       |FINSI;
       |CIERRA #900;
 |FINSI;
|FCAL;
