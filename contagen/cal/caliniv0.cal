|FICHEROS;
   camaniva #0;
   caivalin #1,MANTE;
   cacuenta #2;
   caclipro #8,MANTE;
   caivaasi #200;
   :/modelos/def/dsmom030 #130;

   caliniv0 #900,MANTE;
   caliniva #901,MANTE;
   calinivr #902,MANTE;

   anm00001 #110;
   anm00012 #112;
   anm00013 #113;

   anw00012 #212;
   anw00013 #213;

   caivalin@ #910;
   caliniva@ #911;

   camanref #1002;
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoan_i;
|INCLUYE dscoiv_i;

|VARIABLES;
  &enDiarioNivel = -1;

  nSwOpR = 0;
  nSwOp  = 0;
  inkey  = 0;
  alfa1  = "";
  alfa2  = "";

  nNume1 = 0;
  nBase  = 0;
  nume1  = 0;
  nume2  = 0;
  nume3  = 0;
  nume4  = 0;
  nume5  = 0;
  nume6  = 0;
  nume7  = 0;
  nume8  = 0;
  nume9  = 0;
  nume10 = 0;
  nume11 = 0;

  aFichero    = "";
  nTipoAnt    = 0;
  nModoLin    = 0;
  nAnterior   = 0;
  &aDescAnt   = "";
  &aAntesDes0 = "";
  &aAntesDes1 = "";

  xx     = 0;
  aAlfa  = "";
  aAlfa1 = "";
  aAlfa2 = "";
  aAlfa3 = "";
  aAlfa4 = "";
  aAlfa5 = "";
  aAlfa6 = "";
  aAlfa7 = "";
  aDCon1 = "";
  aDCon2 = "";
  nDCon0 = 0;
  nDCon1 = 0;

  &aFich1     = "";
  &aCPartida  = "";
  &eaCtaIVAEs = "";
  nSalir      = 0;
  nUltima     = 0;
  nUltima_1   = 0;
  nUltima_2   = 0;
  nUltima_3   = 0;
  nUltima_4   = 0;
  nUltima_5   = 0;
  aMas        = "";
  aDef        = "";
  &enNoHay    = 0;
  caant       = "";

  &enDiAna = 0;
  &efFeAna = @;
  &enDoAna = 0;
  &enApAna = 0;
  &eaGrAna = "";
  &enImAna = 0;
  &enQueAna = 0;
  &nSwGrupo = 0;
  &eaFitnW12 = "";
  &eaFitnW13 = "";

  nDesplegar = 0;
  nSalirR    = 0;
  &nTPorCien = 0;
  nEstado    = 0;
  nPrimero   = 0;
  nLinea     = 0;
  nLineaAct  = 0;
  nLineaNue  = 0;
  nEl06      = 0;
  nEl12      = 0;
  nEl13      = 0;
  aCampo3    = "";
  nCampo4    = 0;
  aCampo5    = "";
  nCampo6    = 0;
  aCampo8    = "";
  aCampo9    = "";
  aCampo10   = "";
  nCampo11   = 0;
  nCampo12   = 0;
  nCampo13   = 0;
  aCampo14   = "";
  aCampo15   = "";

  &enDesglose = 0;
  nDesgDepar  = 0;
  aDirecPLB   = "";
  aFichePLB   = "";
|FIN;

|PROCESO LosCampos;
  nume1 = nBase +  1;     ||Base
  nume2 = nBase +  6;     ||% IVA
  nume3 = nBase + 11;     ||Cuota IVA
  nume4 = nBase + 16;     ||% Recargo
  nume5 = nBase + 21;     ||Cuota Recargo

  nume6 = nBase + 27;     ||Tipo IVA
  nume7 = nBase + 38;     ||Cuenta IVA
  nume8 = nBase + 43;     ||Cuenta Recargo

  nume9  = nBase + 51;     ||Total Base
  nume10 = nBase + 56;     ||Total Iva
  nume11 = nBase + 61;     ||Total Recargo
|FPRC;

|CALCULO suma1;  |TIPO 0;
   |SI #900Campo = Contenido; |ACABA; |FINSI;

   nBase = Campo - 1;
   |HAZ LosCampos;

   #900nume3 = (#900Campo % #900nume2) ! EURODB; |PINTA #900nume3;
   #900nume5 = (#900Campo % #900nume4) ! EURODB; |PINTA #900nume5;

   |HAZ Total_0;
|FCAL;

|CALCULO ACIVA_0; |TIPO 7;
    alfa1 = #caivaasi#87;
    nume2 = Campo - 5;
    |SI #900nume2 = 0; alfa1 = "N"; |FINSI;
    |C_M #900Campo,1,alfa1;
|FCAL;

|CALCULO ACRec_0; |TIPO 7;
    alfa1 = #caivaasi#88;
    nume4 = Campo - 5;
    |SI #900nume4 = 0; alfa1 = "N"; |FINSI;
    |C_M #900Campo,1,alfa1;
|FCAL;

|CALCULO Total_0;
    #900#27 =           #900#2 + #900#12 + #900#22;
    #900#27 = #900#27 + #900#3 + #900#13 + #900#23;
    #900#27 = #900#27 + #900#4 + #900#14 + #900#24;
    #900#27 = #900#27 + #900#5 + #900#15 + #900#25;
    #900#27 = #900#27 + #900#6 + #900#16 + #900#26; |PINTA #900#27;

    #900#49 = #900#2  + #900#3  + #900#4  + #900#5  + #900#6;
    #900#50 = #900#12 + #900#13 + #900#14 + #900#15 + #900#16;
    #900#51 = #900#22 + #900#23 + #900#24 + #900#25 + #900#26;
|FCAL;


|CALCULO control1; |TIPO 0;
   |SI FSalida = 2; |ACABA; |FINSI;

   |C_M #900Campo,0,aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   |SI FSalida = 10;
      efFechaIVA = #camaniva#4; |HAZ SelecTipoIVA;
      |SI enLineaIVA < 0;
         |MENAV "    Control de tipos de iva no definido";
         enLineaIVA = 0;
      |FINSI;
      #900Campo = enLineaIVA; |PINTA #900Campo;
      |ERROR;
   |FINSI;

   nBase = Campo - 27;
   |HAZ LosCampos;

   |C_M #900nume1,1,"S";
   |C_M #900nume3,1,"S";
   |C_M #900nume5,1,"S";
   |SI #900Campo = 0;
       |C_M #900nume1,1,"N";
       |C_M #900nume3,1,"N";
       |C_M #900nume5,1,"N";
       #900nume1 = 0; |PINTA #900nume1;
       #900nume2 = 0; |PINTA #900nume2;
       #900nume3 = 0; |PINTA #900nume3;
       #900nume4 = 0; |PINTA #900nume4;
       #900nume5 = 0; |PINTA #900nume5;
       |ACABA;
   |FINSI;

   nTipoAnt = Contenido;

   efFechaIVA = #camaniva#4; enLineaIVA = #900Campo; |HAZ SacaIVA;
   |SI enLineaIVA < 0;
       |MENAV "    Control de cuentas no definido";
       |ERROR; |ACABA;
   |FINSI;

   #900nume7 = eaCtaIVA;
   #900nume8 = eaCtaREC;
   #900nume2 = enPorcIVA;  |PINTA #900nume2;
   #900nume4 = enPorcREC;  |PINTA #900nume4;

   #camaniva#31 = eaDesgIVA;
   #camaniva#32 = eaDesgCTP;
   #camaniva#33 = "N";
   #camaniva#34 = eaDesgDTO;

   |SI #900#33  = "N";
       |SI dCtaNoD ! "";
           #900nume7 = dCtaNoD;
           #900nume8 = dCtaNoD;
       |SINO;
           #900nume7 = #901#3;
           |SI #900nume2 ! 0; #900nume8 = #901#3; |FINSI;
       |FINSI;
   |FINSI;

   |SI #900Campo ! nTipoAnt;
       #900nume3 = (#900nume1 % #900nume2) ! EURODB; |PINTA #900nume3;
       #900nume5 = (#900nume1 % #900nume4) ! EURODB; |PINTA #900nume5;
       |HAZ Total_0;
   |FINSI;
|FCAL;
|| ---------------------------------------------------------------

|CALCULO Labase7; |TIPO 7;
  |C_M #901#11,1,"N";

  nModoLin = (FEntrada / 100) ! 0;
  |SI #900#3 ! 0; |O #900#4 ! 0; |O #900#5 ! 0; |O #900#6 ! 0;
      |C_M #901#11, 1, "S";
      |SI nModoLin = 1; |Y nBase ! 0;
          #901#11 = nBase;
      |FINSI;
  |SINO;
      #901#11 = 1; |PINTA #901#11;
  |FINSI;

|FCAL;

|CALCULO Labase0; |TIPO 0;
 nBase = #901#11;
 |HAZ LosCampos;
 |SI #900nume1 = 0;
     |MENAV "    No existe importe en esta Base";
     |ERROR;
 |FINSI;
|FCAL;

|CALCULO liniva_2; |TIPO 2;
   #900#38 = #900#38 +. #901#6;

   nBase = #901#11;
   |HAZ LosCampos;

   #900nume9  = #900nume9  +. #901#6;  |PINTA #900nume9;
   #900nume10 = #900nume10 +. #901#12;
   #900nume11 = #900nume11 +. #901#13;
|FCAL;

|CALCULO Calcul_7; |TIPO 7;
 aAlfa1 = #901#11;
 aAlfa1 = "    <F3> Cuadrar base " + aAlfa1;
 |MENSA aAlfa1;
|FCAL;

|CALCULO Calcul_0;
   inkey = FSalida;

   nBase = #901#11;
   |HAZ LosCampos;

   |SI inkey = 11;
       #901#6 = #900nume1 - #900nume9;
       |PINTA #901#6;
   |FINSI;

   nNume1 = (#901#6 + #900nume9) ! EURODB;
   |SI nNume1 > #900nume1;
       aAlfa1 = #901#11;
       aAlfa1 = "    NHa superado el Importe de la Base " + aAlfa1 + ".Continuar";
       |CONFI aAlfa1;
       |SI FSalida ! 0;
           |ERROR;
           |ACABA;
       |FINSI;
   |FINSI;

   #901#12 = (#901#6 % #900nume2) ! EURODB;
   #901#13 = (#901#6 % #900nume4) ! EURODB;
|FCAL;

|CALCULO liniva_7; |TIPO 7;
  #901#0 = #900#0;
  #901#1 = #900#1;
|FCAL;

|CALCULO  liniva_1;  |TIPO 1;
  |SI enSwAna = 0;  |ACABA; |FINSI;    || si no es analitica

  nModoLin = (FEntrada / 100) ! 0;
  |SI nModoLin = 3;
      |HAZ BorroGru_0;
  |FINSI;
|FCAL;

|CALCULO  liniva_3;  |TIPO 3;
  |SI nDesplegar = 1;
      |HAZ Altavarias;
  |FINSI;
  nDesplegar = 0;
  caant      = #901#9;
|FCAL;

|PROCESO BorroGru_0;
  alfa1 = #901#9; alfa1 = alfa1 % 103;
  |SI alfa1 = "GRU";
      |ABRE #212;
      #212#0 = #camaniva#0;
      #212#2 = #camaniva#1;
      #212#3 = #901#2;
      #212#4 = #901#9;
      |LEE 101#212=;
      |SI FSalida = 0;
          |BUCLELIN 1NULL#212;
          |BORRA 020#212a;
      |FINSI;
      |LIBERA #212;
      |CIERRA #212;
  |FINSI;
|FPRC;

|CALCULO liniva_0; |TIPO 0;
  nModoLin = (FEntrada / 100) ! 0;

  alfa1 = #caivaasi#70; |C_M #901#3,1, alfa1;

  |SI nModoLin = 1;
      #901#4 = dConc1;       |PINTA #901#4;
      #901#5 = dDesc1;       |PINTA #901#5;
      #901#7 = #camaniva#10; |PINTA #901#7;
      #901#8 = #camaniva#11; |PINTA #901#8;
 |FINSI;
 nAnterior = #901#4;

 alfa1 = #901#7; |QBF alfa1;
 |SI alfa1 = "";
     #901#7 = #camaniva#10; |PINTA #901#7;
     alfa1 = #901#8; |QBF alfa1;
     |SI alfa1 = ""; #901#8 = #camaniva#11; |PINTA #901#8; |FINSI;
 |FINSI;
|FCAL;

|CALCULO AConcep_07; |TIPO 7;
  nModoLin = (FEntrada / 100) ! 0;
  |SI nModoLin = 1;
      #901#4 = #camaniva#27; |PINTA #901#4;
      #901#5 = #camaniva#28;
      |SI #caivaasi#131  = "S"; |Y #caivaasi#130 ! "=  ";
          |ABRE #dsmom030;
          #dsmom030#0 = #901#4;
          |LEE 000#dsmom030.=;
          |CIERRA #dsmom030;
          #901#5 = #dsmom030#1;
      |FINSI;
      |PINTA #901#5;
  |FINSI;
  nAnterior = #901#4;
|FCAL;

|CALCULO DConcep_0; |TIPO 0;

   |C_M #901Campo,0,alfa1;

   |SI FSalida = 2; |ACABA; |FINSI;

   |SI FSalida = 10;
       eaFiCon    = aFich1;
       enConcepto = #901Campo;
       |HAZ VerConcepto;
       #901Campo = enConcepto; |PINTA #901Campo;
   |FINSI;
   enLibIVA  = #camaniva#0;

   enFilConc  = 2;          || Concepto de IVA
   eaReperc   = " ";
   enConcepto = #901Campo;
   |HAZ FiltroConcepto;  |SI eswLect ! 0; |ACABA; |FINSI;

   |SI eaDepar = "S";
       |SI Haybasica = 1;
           enFilRegIVA = 1;
           |HAZ FilRegimenIVA;
       |FINSI;
   |FINSI;

   nume1 = Campo + 1;
   |SI #901Campo ! nAnterior;
       #901nume1 = #dsmom030#1; |PINTA #901nume1;
   |FINSI;

|SI nModoLin = 1;
    #901#10 = #dsmom030#72; |PINTA #901#10;

    #901#3 = eaCtaIVA; |PINTA #901#3;
    |SI aCPartida ! "";
       #901#3 = aCPartida;
   |FINSI;
   |PINTA #901#3;

   emCta    = 0;

   enDiarioNivel = #camaniva#7;
   eaCuenta = #901#3; |HAZ SacaNivel;
   |HAZ LeeCuenta;
   |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea
|FINSI;
|FCAL;

|CALCULO ADescrip_07; |TIPO 7;
   |C_M #901Campo,0,alfa1;
   |SI alfa1 = "S";
       |SI eaSobreEs ! "S";
           |PTEC 816;
       |FINSI;
   |SINO;
       |ACABA;
   |FINSI;
   |SI aDescAnt ! "";
       |MENSA "    <F7> Descripcion anterior, <F8> Recoger A¤adido";
   |SINO;
      |PINTA 0420, "                                 ";
   |FINSI;
   |SI #caivaasi#131 = "S";
       || ... Cambio siempre por si modifica alguna cosa
          xx = 4;
          |HAZ adaptaC2_0;
          #901Campo = aAlfa1;
          |PINTA #901Campo;
   |FINSI;
|FCAL;

|CALCULO DDescrip_0; |TIPO 0;

   |SI FSalida = 15;
       #901#5 = aDescAnt; |PINTA #901#5;
       |ERROR;
   |FINSI;
   |SI FSalida = 16;
       aAntesDes1 = #camaniva#28; |QBF aAntesDes1;
       aAlfa1     = aAntesDes1 - aAntesDes0;
       aAlfa2     = #dsmom030#1; |QBF aAlfa2;
       aAlfa2     = aAlfa2 + aAlfa1;
       #901Campo  = aAlfa2; |PINTA #901Campo;
   |FINSI;
   aDescAnt = #901Campo;
|FCAL;

|CALCULO ADepar_0; |TIPO 7;
   alfa1 = #901Campo; |QBF alfa1;
   |SI alfa1 = "";
       #901Campo = #camaniva#10; |PINTA #901Campo;
   |FINSI;

   nDesgDepar = 0;
   |SI Haybasica = 1;
       nModoLin   = (FEntrada /100) ! 0;
       |SI nModoLin = 1; |O nModoLin = 2;
           nDesgDepar = 1;
           |SI nModoLin = 2;
               nDesgDepar = 2;
               |ABRE #911;
               #911#0 = #901#0;
               #911#1 = #901#1;
               #911#2 = #901#2 + 1;
               |LEE 001#911];
               |SI FSalida = 0; |Y #911#0 = #901#0; |Y #911#1 = #901#1;
                   nDesgDepar = 0;
               |FINSI;
               |CIERRA #911;
           |FINSI;
       |FINSI;
   |FINSI;

   |C_M #901Campo, 0, alfa1;
   |SI alfa1 = "S"
      |SI Haybasica = 1;
          |SI nDesgDepar ! 0;
              |MENSA "    <F2> Consultar Actividades. <00> Repartir entre Actividades";
          |SINO;
              |MENSA "    <F2> Consultar Actividades.";
          |FINSI;
      |SINO;
          |MENSA "    <F2> Consultar Departamentos. ";
      |FINSI;
   |FINSI;
|FCAL;

|PROCESO Asiinf902;
  #902#0 = 01;
|FPRC;

|PROCESO Asisup902;
  #902#0 = 99;
|FPRC;

|PROCESO SumaTanto;
  |SI nSwOpR = 0;
      nTPorCien = (nTPorCien + #902#2) ! 2;
  |SINO;
      |SI #902#2 ! 0;
          |SI nPrimero = 0;
              nLinea = nLineaAct;
          |SINO;
              nLineaNue = nLineaNue + 1;
              nLinea = nLineaNue;
          |FINSI;
          nPrimero = 1;
          #901#0 = #900#0;
          #901#1 = #900#1;
          #901#2 = nLinea;
          |LEE 040#901=;
          nEstado = FSalida;

          alfa1   = #902#0; alfa1 = ("00" + alfa1) % -102;
          #901#2  = nLinea;
          #901#3  = aCampo3;
          #901#4  = nCampo4;
          #901#5  = aCampo5;
          #901#6  = nCampo6  % #902#2;;  nEl06 = (nEl06 - #901#06) ! EURODB;
          #901#7  = alfa1;
          #901#8  = aCampo8;
          #901#9  = aCampo9;
          #901#10 = aCampo10;
          #901#11 = nCampo11;
          #901#12 = nCampo12 % #902#2;;  nEl12 = (nEl12 - #901#12) ! EURODB;
          #901#13 = nCampo13 % #902#2;;  nEl13 = (nEl13 - #901#13) ! EURODB;
          #901#14 = aCampo14;
          #901#15 = aCampo15;
          |SI nEstado ! 0; |GRABA 020#901n; |FINSI;
          |SI nEstado = 0; |GRABA 020#901a; |FINSI;
          |LIBERA #901;
      |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE SumaTanto;
 #902#1;
 ;
 00;
 99;
 ;
 NULL, SumaTanto;
|FIN;

|PROCESO RepartoAct;
   nTPorCien = 0;
   nSwOpR    = 0;
   |BUCLE SumaTanto;
   |PUSHV 0501 2380;
   |PINPA #0#902;
   |PINTA 1850, nTPorCien;

   nSalirR = 0;
   |PARA; |SI nSalirR = 0; |HACIENDO;
          |ENTLINEAL #1#902,-1, 2, 22, 0,Asiinf902,Asisup902;
          |SI FSalida = 0;
              nDesplegar = 1;
              nSalirR    = 1;
              |SI nTPorCien  ! 100;
                  |CONFI "    NNo ha repartido el 100%. Continuar";
                  |SI FSalida ! 0;
                      nDesplegar = 0;
                      nSalirR    = 0;
                  |FINSI;
              |FINSI;
          |SINO;
              nDesplegar = 0;
              nSalirR = 1;
          |FINSI;
   |SIGUE;
   |POPV;
|FPRC;

|PROCESO Altavarias;
     |GRABA 020#901a;
     |LIBERA #901;

     nPrimero  = 0;
     nLineaAct = #901#2;

     aCampo3   = #901#3;
     nCampo4   = #901#4;
     aCampo5   = #901#5;
     nCampo6   = #901#6;
     aCampo8   = #901#8;
     aCampo9   = #901#9;
     aCampo10  = #901#10;
     nCampo11  = #901#11;
     nCampo12  = #901#12;
     nCampo13  = #901#13;
     aCampo14  = #901#14;
     aCampo15  = #901#15;

     |LEE 040#901u;
     nLineaNue = #901#2;

     nEl06 = #901#6;
     nEl12 = #901#12;
     nEl13 = #901#13;

     nSwOpR = 1;
     |BUCLE SumaTanto;

     #901#0 = #900#0;
     #901#1 = #900#1;
     #901#2 = nLineaAct;
     |LEE 101#901=;
     |SI FSalida = 0;
         #901#6  = #901#6  + nEl06;
         #901#12 = #901#12 + nEl12;
         #901#13 = #901#13 + nEl13;
         |GRABA 020#901a;
     |FINSI;
     |LIBERA #901;

     |PTEC 815;
     |PTEC 815;
     |PTEC 814;
|FPRC;
|| ///////////////////////////////////////////////////////////////

|CALCULO DDepar_0; |TIPO 0;
   |SI eaDepar = "N"; |ACABA; |FINSI;
   |SI FSalida = 2;   |ACABA;  |FINSI;

   |C_M #901Campo, 0, alfa1;
   |SI alfa1 = "N"; |ACABA; |FINSI;

   eOpDep = 0; || 1 = Consulta pantalla
   |SI FSalida = 10; eOpDep = 1;  |FINSI;
   |SI eOpDep = 0; |Y #901#7 = #camaniva#10; |ACABA; |FINSI;

   nDesplegar = 0;
   nModoLin   = (FEntrada /100) ! 0;
   |SI #901#7 = "00"; |Y Haybasica = 1;
       |SI nDesgDepar ! 0;
           |HAZ RepartoAct;
       |SINO;
           |MENAV "    Opcion no permitida";
           |ERROR;
       |FINSI;
       |ACABA;
   |FINSI;

   enActividad = #901#7;
   eaCodDep    = #901#7;
   |HAZ Actividad;
   |SI eswLect = 1;
       |MENAV "    Error.! No Existe Actividad / Departamento";
       |C_M #901#7, 1, "S";
       |ERROR;
   |SINO;
       alfa1 = enActividad;
       |SI Haybasica = 1;
           alfa1 = ("00" + alfa1) % A02;
           |SI aTipoIVA ! "N";
               enFilRegIVA = 1;
               enConcepto  = #901#4;
               |HAZ FilRegimenIVA;
               |SI eswLect ! 0; |ACABA; |FINSI;
           |FINSI;
       |FINSI;

       #901#7 = eaCodDep;  |PINTA #901#7;
       |SI efActFin > "01.01.1900"; |Y snFilBa = "S";
           aAlfa1 = "    NAtencion!!!. Actividad con Fecha de BAJA " + efActFin + ", Desea introducir Facturas ? ";
           |CONFI aAlfa1;
           |SI FSalida ! 0;
               |ERROR;
               |ACABA;
           |FINSI;
       |FINSI;
       |SI eaEsCom = "S";
           |CONFI "    NAtencion!!!. Actividad Comunera, ¨ Desea introducir Facturas ? ";
           |SI FSalida ! 0;
               |ERROR;
           |FINSI;
       |FINSI;
   |FINSI;
|FCAL;

|CALCULO ASubde_0; |TIPO 7;
   |C_M #901Campo, 0, alfa1;
   |SI alfa1  = "S";
      |SI Haybasica = 1;
          |SI enArrend ! 2;
              |MENSA "    Subdepartamento: Referencias Catastrales <F2> Consultar <F3> Modificar";
          |SINO;
              |MENSA "    Subdepartamento. <F2> Consultar Cultivos";
          |FINSI;
      |FINSI;
   |FINSI;
   alfa1 = #901Campo; |QBF alfa1;
   |SI alfa1 = "";
       #901Campo = #camaniva#11; |PINTA #901Campo;
   |FINSI;
|FCAL;

|CALCULO DSubde_0; |TIPO 0;
   |SI eaSubde = "N"; |ACABA; |FINSI;

   |C_M #901Campo, 0, alfa1;
   |SI alfa1 = "N"; |ACABA; |FINSI;

   eOpSub = 0; || 1 = Consulta pantalla
   |SI FSalida = 10; eOpSub = 1;  |FINSI;
   |SI FSalida = 11; eOpSub = 2;  |FINSI;

   |SI eOpSub = 0; |Y #901#8 = #camaniva#11; |ACABA; |FINSI;

   enConcepto  = #901#4;
   eaCodSubdep = #901#8;
   enQue = 1;
   |HAZ SubActividad;
   enQue = 0;
   |SI eswLect = 1;
       |MENAV "    Error.! No Existe Subdepartamento";
       |ERROR;
   |SINO;
       #901#8 = eaCodSubdep; |PINTA #901#8;
   |FINSI;
|FCAL;

|CALCULO DCuenta_06; |TIPO 6;             || Contrapartida
   |C_M #901Campo,0,alfa1;
   |SI alfa1 = "N"; |ACABA; |FINSI;

   |HAZ NoBlanca;

   eaCuenta = #901Campo;
   salidas  = FSalida;
   |HAZ FiltraPuntos;
   #901Campo = eaCuenta;
   |PINTA #901Campo;
|FCAL;

|CALCULO DCuenta_0; |TIPO 0;
   |C_M #901Campo,0,alfa1; |SI alfa1 = "N"; |ACABA; |FINSI;

   emCta    = 1;
   enDiarioNivel = #camaniva#7;
   eaCuenta = #901Campo;
   |HAZ SacaNivel;
   |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea
   |HAZ LeeCuenta;
   |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea

   enConcepto = #901#4;
   |HAZ FiltraCtaIG;
|FCAL;

|CALCULO AAna_07; |TIPO 7;
 nModoLin = (FEntrada / 100) ! 0;

 alfa1 = "    <F2> Consulta Cta Analitica";
 alfa2 = #901#9; alfa2 = alfa2 % 103;
 |SI alfa2 = "GRU"; |Y eaGAna ! "S";
      alfa1 = "    <F2>Consulta Cta Analitica, Grupo:<F3>Modificar,<F4>Recalcular,<F5>Consulta";
 |FINSI;
 |MENSA alfa1;

 |SI enSwAna = 1;
     eaCuenta = #901#3;
     |HAZ LeeTablaAna;
     |SI swRelac = 0;
         |SI nModoLin = 1; #901#9 = eaCtaAna; |FINSI;
         |C_M #901#9,1,eaAnaMod;
     |SINO;
         |SI nModoLin = 1; #901#9 = ""; |FINSI;
         |SI eaPAna = "S";
             |C_M #901#9,1,"N";
         |SINO;
             |C_M #901#9,1,"S";
         |FINSI;
     |FINSI;
     |PINTA #901#9;
     eaCtaAna = #901#9;
     |HAZ eLimCtaAna7;
     |SI enBuenaAna ! 0; |C_M #901#9,1,"S"; |FINSI;
 |FINSI;
|FCAL;

|CALCULO DAna_0; |TIPO 0;
   inkey = FSalida;
   |SI inkey = 2;   |ACABA; |FINSI;
   |SI enSwAna = 0; |ACABA; |FINSI;

   |SI inkey = 15;
       #901#9 = caant;
       |PINTA #901#9;
       |VETE ParaSalir;
   |FINSI;

   salidas = FSalida; |HAZ FPuntosAna;

   eOpAna = 0; || 1 = Consulta pantalla
   |SI inkey = 10; eOpAna = 1;  |FINSI;
   eaCtaAna = #901#9;
   |HAZ LeeCtaAna;
   |SI eswLect = 1;
        |MENAV "    Error.! No Existe Cuenta Analitica";
        |ERROR;
        |ACABA;
   |SINO;
       #901#9 = eaCtaAna;
       |PINTA #901#9;
       caant = #901#9;
  |FINSI;
|| ---------------------------------------------------------

 |ET ParaSalir;
   |SI nDesplegar = 1;
       alfa1 = #901#9 % 103;
       |SI alfa1 = "GRU";
           |MENAV "    Grupo Analitica no valido para reparto de Actividades";
           |ERROR;
           |ACABA;
       |FINSI;
   |FINSI;
|| ...  Comprobar validez de la cuenta analitica segun limites definidos
   eaCtaAna = #901#9;
   |HAZ eLimCtaAna;
   |SI enBuenaAna ! 0;
       |MENAV eaBuenaAna;
       |ERROR;
       |ACABA;
   |FINSI;

   enQueAna = 5;
   |SI inkey = 13; enQueAna = 0; |FINSI;
   |SI inkey = 11; enQueAna = 1; |FINSI;
   |SI inkey = 12; enQueAna = 2; |FINSI;
   |HAZ GrupoAna_0;
|FCAL;

|PROCESO GrupoAna_0;

  |SI eaGAna = "S"; |ACABA; |FINSI; ||No existe reparto especifico

  alfa1 = #901#9; alfa1 = alfa1 % 103;
  |SI alfa1 ! "GRU";
      enQueAna = 4;
  |FINSI;

  nSwGrupo = 0;
  enDiAna = #camaniva#0;
  efFeAna = #camaniva#4;
  enDoAna = #camaniva#1;
  enApAna = #901#2;
  eaGrAna = #901#9;
  enImAna = #901#6;

  |SUB_EJECUTA_NP "anw00012";
  |SI nSwGrupo = 1;
      |ERROR;
  |FINSI;
|FPRC;

|| -----------------------------------------------------------------------
|PROCESO contrap;
 |SI eaDesgDef ! "S"; |ACABA; |FINSI;

 |ABRE #901;
 #901#0 = #900#0;
 #901#1 = #900#1;
 #901#2 = nLinea;
 |LEE 041#901=;
 |SI FSalida ! 0;
     #901#0 = #900#0;
     #901#1 = #900#1;
     #901#2 = nLinea;
     #901#3 = eaCtaIVAEs;
     |SI aCPartida ! ""; #901#3 = aCPartida; |FINSI;
     #901#4 = dConc1;
     #901#5 = dDesc1;
     |SI #caivaasi#131 = "S";
         || ... Cambio siempre por si modifica alguna cosa
          xx = 4;
          #dsmom030#1 = dDesc1;
          |HAZ adaptaC2_0;
          #901#5 = aAlfa1;
     |FINSI;
     #901#7 = #camaniva#10;
     #901#8 = #camaniva#11;
     #901#11 = nLinea;
     nNume1 = nLinea + 1;  #901#6 = #900nNume1;
     nNume1 = nLinea + 11; #901#12 = #900nNume1;
     nNume1 = nLinea + 21; #901#13 = #900nNume1;
     |HAZ liniva_2;
     |GRABA 020#901n;
 |FINSI;
 |LIBERA #901;
 |CIERRA #901;
|FPRC;

|PROCESO contrap_modi;
 nLinea = 1;
 |ABRE #901;
 #901#0 = #900#0;
 #901#1 = #900#1;
 #901#2 = nLinea;
 |LEE 041#901=;
 |SI FSalida ! 0;
     |DDEFECTO #901;
     #901#0 = #900#0;
     #901#1 = #900#1;
     #901#2 = nLinea;
     |GRABA 020#901c;
 |FINSI;
 #901#3 = #caivalin#12;
 #901#4 = #caivalin#3;
 #901#5 = #caivalin#4;
 #901#6 = #900#2;
 #901#7 = #caivalin#30;
 #901#8 = #caivalin#31;
 #901#9 = #caivalin#28;
 #901#10= #caivalin#25;
 #901#11 = nLinea;
 #901#12 = #900#12;
 #901#13 = #900#22;
 #901#14 = #caivalin#26;
 #901#15 = #caivalin#27;

 #900#38 = #901#6;
 #900#52 = #901#6;
 #900#57 = #901#12;
 #900#62 = #901#13;
 |GRABA 020#901a;
 |LIBERA #901;
 |CIERRA #901;
|FPRC;


|| -----------------------------------------------------------------------
|| -----------------------------------------------------------------------
|RUTINA Asiinf901;
   #901#0 = #900#0;
   #901#1 = #900#1;
   #901#2 = 01;
|FRUT;

|RUTINA Asisup901;
   #901#0 = #900#0;
   #901#1 = #900#1;
   #901#2 = 99;
|FRUT;

|PROCESO Tipo12_0; |TIPO 12;
  inkey = FSalida;

  #900#52 = 0; #900#53 = 0;
  #900#54 = 0; #900#55 = 0;
  #900#56 = 0;

  |SI enDesglose ! 2;
      |SI #900#2 ! 0;
          nLinea = 1; |HAZ contrap;
      |FINSI;
  |SINO;
      |HAZ contrap_modi;
  |FINSI;
  |SI #900#2 ! 0;
      |ATRI S; |PINTA 2307, "1-"; |ATRI 0;
      |C_V #900#52, 1, "S"; |PINTA #900#52;
  |FINSI;

  |SI #900#3 ! 0;
      |ATRI S; |PINTA 2321, "2-"; |ATRI 0;
      |C_V #900#53, 1, "S"; |PINTA #900#53;
      nLinea = 2; |HAZ contrap;
  |FINSI;
  |SI #900#4 ! 0;
      |ATRI S; |PINTA 2335, "3-"; |ATRI 0;
      |C_V #900#54, 1, "S"; |PINTA #900#54;
      nLinea = 3; |HAZ contrap;
  |FINSI;
  |SI #900#5 ! 0;
      |ATRI S; |PINTA 2349, "4-"; |ATRI 0;
      |C_V #900#55, 1, "S"; |PINTA #900#55;
      nLinea = 4; |HAZ contrap;
  |FINSI;
  |SI #900#6 ! 0;
      |ATRI S; |PINTA 2363, "5-"; |ATRI 0;
      |C_V #900#56, 1, "S"; |PINTA #900#56;
      nLinea = 5; |HAZ contrap;
  |FINSI;

  nSalir = 0;
  |SI inkey = 7; nSalir = 2; |FINSI;
  nBase = 1;
  |PARA; |SI nSalir = 0; |HACIENDO;
         |ENTLINEAL #1#901,-3, 2, 22, 0,Asiinf901,Asisup901;
         |SI #900#38 ! #900#49;
             nume1 = #900#49 - #900#38;
             alfa1 = nume1;
             alfa1 = "    NATENCION!!! Descuadre de Bases Imponibles > " + alfa1 + ". Anular Introduccion";
             |CONFI alfa1;
             |SI FSalida = 0; nSalir = 2; |FINSI;
         |SINO;
             |CONFI "    SIntroduccion Correcta ";
             |SI FSalida = 0;
                 nSalir = 1;
             |SINO;
                 |CONFI "    NAnular Introduccion";
                 |SI FSalida = 0; nSalir = 2; |FINSI;
             |FINSI;
         |FINSI;
  |SIGUE;

|FPRC;

|PROCESO Los_graba;

  |HAZ LosCampos;
  |SI #900nume1 ! #900nume9; |O #900nume3 ! #900nume10;
      |O #900nume5 ! #900nume11;
        #910#0 = #900#0;
        #910#1 = #900#1;
        #910#2 = nUltima;
        |LEE 101#910=;
        |SI FSalida = 0;

            nNume1 = (#900nume1 - #900nume9)  ! EURODB;
            |SI nNume1 ! 0;
                #910#6  = #910#6  + nNume1;
                #900#49 = #900#49 + nNume1;
            |FINSI;

            nNume1 = (#900nume3 - #900nume10) ! EURODB;
            |SI nNume1 ! 0;
                #910#8  = #910#8  + nNume1;
                #900#50 = #900#50 + nNume1;
            |FINSI;

            nNume1 = (#900nume5 - #900nume11) ! EURODB;
            |SI nNume1 ! 0;
                #910#10 = #910#10 + nNume1;
                #900#51 = #900#51 + nNume1;
            |FINSI;

            |GRABA 020#910a;
        |FINSI;
        |LIBERA #910;
    |FINSI;
|FPRC;

|PROCESO LosCuadres;
    |SI nUltima_1 ! 0;
        nUltima = nUltima_1;
        nBase   = 1;
        |HAZ Los_graba;
    |FINSI;
    |SI nUltima_2 ! 0;
        nUltima = nUltima_2;
        nBase   = 2;
        |HAZ Los_graba;
    |FINSI;
    |SI nUltima_3 ! 0;
        nUltima = nUltima_3;
        nBase   = 3;
        |HAZ Los_graba;
    |FINSI;
    |SI nUltima_4 ! 0;
        nUltima = nUltima_4;
        nBase   = 4;
        |HAZ Los_graba;
    |FINSI;
    |SI nUltima_5 ! 0;
        nUltima = nUltima_5;
        nBase   = 5;
        |HAZ Los_graba;
    |FINSI;
|FPRC;

|PROCESO AltaLasLineas;
  |SI nSwOp = 0;
      nBase = #901#11;
      |HAZ LosCampos;
      |DDEFECTO #910;
      |LIBERA #910;
      #910#0  = #901#0;
      #910#1  = #901#1;
      #910#2  = #901#2;
      |LEE 141#910=;
      |SI FSalida ! 0;
          #910#0  = #901#0;
          #910#1  = #901#1;
          #910#2  = #901#2;
          |GRABA 020#910b;
      |FINSI;
      #910#3  = #901#4;
      #910#4  = #901#5;
      #910#5  = #900#33;
      #910#6  = #901#6;
      #910#7  = #900nume2;
      #910#8  = #901#12;
      #910#9  = #900nume4;
      #910#10 = #901#13;
      #910#12 = #901#3;
      #910#13 = #900nume7;
      #910#14 = #900nume8;
      #910#16 = #900nume6;
      #910#17 = #900#34;
      #910#25 = #901#10;
      #910#28 = #901#9;
      #910#29 = #900#36;
      #910#30 = #901#7;
      #910#31 = #901#8;
      |GRABA 040#910a;
      |LIBERA #910;
      |SI nBase = 1; nUltima_1 = #910#2; |FINSI;
      |SI nBase = 2; nUltima_2 = #910#2; |FINSI;
      |SI nBase = 3; nUltima_3 = #910#2; |FINSI;
      |SI nBase = 4; nUltima_4 = #910#2; |FINSI;
      |SI nBase = 5; nUltima_5 = #910#2; |FINSI;
      #900#49 = #900#49 + #901#6;
      #900#50 = #900#50 + #901#12;
      #900#51 = #900#51 + #901#13;

      #900nume9  = #900nume9  + #901#6;
      #900nume10 = #900nume10 + #901#12;
      #900nume11 = #900nume11 + #901#13;
  |SINO;
      |HAZ BorroGru_0;
  |FINSI;
|FPRC;

|DEFBUCLE AltaLasLineas;
 #901#1;
 ;
 #900#0, #900#1,01;
 #900#0, #900#1,99;
 ;
 NULL, AltaLasLineas;
|FIN;

|PROCESO AltaLineas;
    nSwOp     = 0;
    nUltima   = 0;
    nUltima_1 = 0;
    nUltima_2 = 0;
    nUltima_3 = 0;
    nUltima_4 = 0;
    nUltima_5 = 0;
    #900#38   = 0;
    |PDEFECTO #900, 49,67;

    #camaniva#19 = 0;
    #camaniva#15 = 0;
    #camaniva#17 = 0;
    #camaniva#23 = 0;
    #camaniva#21 = 0;

    |ABRE #caivalin@;
    |BUCLE AltaLasLineas;
    |HAZ LosCuadres;
    |CIERRA #caivalin@;

    #camaniva#19 = #camaniva#19 +  #900#49;
    #camaniva#15 = #camaniva#15 +  #900#49;
    #camaniva#17 = #camaniva#15 %  #camaniva#16;
    #camaniva#23 = #camaniva#23 +  #900#50 + #900#51;
    #camaniva#21 = #camaniva#19 + #camaniva#23 - #camaniva#20;
|FPRC;

|PROCESO PrimeraCarga;
    #900#2  = #caivalin#6;
    #900#7  = #caivalin#7;
    #900#12 = #caivalin#8;
    #900#17 = #caivalin#9;
    #900#22 = #caivalin#10;
    #900#28 = #caivalin#16;
    #900#33 = #caivalin#5;
    #900#34 = #caivalin#17;
    #900#35 = #caivalin#25;
    #900#36 = #caivalin#29;
    #900#39 = #caivalin#13;
    #900#44 = #caivalin#14;
    |HAZ Total_0;

    |HAZ contrap_modi;
|FPRC;

|PROCESO PorDesglose;
 |PULSATECLA;

    |DBASE aMas; |QBF aMas;
    aDef = aMas + "def/caivalin.mas";
    |CARGA_DEF aDef, caivalin@;
    |SI FSalida ! 0;
        |MENAV "    No se puede utilizar esta opcion";
        |ACABA;
    |FINSI;

   aFichero = ("x" + Usuario) %108;
   |NOME_DAT #901 aFichero;
   |DELINDEX #901;

    |DBASE aMas; |QBF aMas;
    aDef = aMas + "def/caliniva.mas";
    |CARGA_DEF aDef, caliniva@;
    |SI FSalida ! 0;
        |MENAV "    No se puede utilizar esta opcion";
        |DESCARGA_DEF #caivalin@;
        |ACABA;
    |FINSI;
   |NOME_DAT #911 aFichero;

   |C_V #900#52, 1, "N"; |C_V #900#53, 1, "N";
   |C_V #900#54, 1, "N"; |C_V #900#55, 1, "N";
   |C_V #900#56, 1, "N";

   alfa1 = #caivaasi#20;   ||Departamento
   |SI eaMulDep ! "S"; alfa1 = "N"; |FINSI;
   |SI eaDepar  = "N"; alfa1 = "N"; |FINSI;
   |C_M #901#7, 1, alfa1;

   alfa1 = #caivaasi#68;   ||Subdepartamento
   |SI eaMulDep ! "S"; alfa1 = "N"; |FINSI;
   |SI eaSubde  = "N"; alfa1 = "N"; |FINSI;
   |C_M #901#8, 1, alfa1;

   |C_V #900#36,1,"S"; |C_M #900#36,1,"N";
   |SI #camaniva#36 = "R"; |C_V #900#36,1,"N"; |FINSI;
   |SI #camaniva#36 = "S"; |Y enNumAct > 1; |C_M #900#36,1,"S"; |FINSI;

   |PUSHV 10012380;
   |PINPA #0#900;
   |C_P #901#14,1,1969;
   |C_P #901#15,1,1971;
   |C_V #901#9,1,"N"; |C_M #901#9,1,"N";
   |SI enSwAna = 1;
       |C_P #901#14,1,1977;
       |C_P #901#15,1,1979;
       |C_V #901#9,1,"S"; |C_M #901#9,1,"S";
       |PINTA 1869,  "Cta.Ana³E³R³";
       |PINTA 1969,  "       ³ ³ ³";
       |PINTA 2069,  "       ³ ³ ³";
       |PINTA 2169,  "       ³ ³ ³";
       |PINTA 2269,  "       ³ ³ ³";
       |PINTA 2368, "ÁÄÄÄÄÄÄÄÁÄÁÄÙ";
       |ATRI R; |PINTA 1869,"Cta Ana";
                |PINTA 1877,"E";
                |PINTA 1879,"R"; |ATRI 0;
   |FINSI;
   |SI #camaniva#36 = "R";
       |PINTA 1665, "             ";
   |FINSI;

   enNoHay = 0;
   |DDEFECTO #900;
   #900#0  = #0#0;
   #900#1  = #0#1;
   #900#37 = aTipoIVA;
   #900#28 = dTiva;
   |C_M #900#33, 1,snDeduc; #900#33 = dDeduc;
   |C_M #900#34, 1,snInver; #900#34 = dInver;


   |SI enDesglose = 2;    ||Es modificacion
       |HAZ PrimeraCarga;
   |FINSI;

   |PINDA #0#900;
   |ENTLINEAL #1#901,-3, 7, 22, 0,Asiinf901,Asisup901;

   |SI enTipoAsto > 100;
       |PTEC 801;
   |FINSI;

   |ENDATOS #2#900;
   |SI FSalida = 0;
       |SI nSalir = 1;
           |HAZ AltaLineas;
           enNoHay = 1;
       |SINO;
           nSwOp = 1;
           |BUCLE AltaLasLineas;
       |FINSI;
   |FINSI;
   |POPV;
   |PINTA  #camaniva#19;
   |PINTA  #camaniva#15;
   |PINTA  #camaniva#17;
   |PINTA  #camaniva#23;
   |PINTA  #camaniva#21;

   |DESCARGA_DEF #caliniva@;
   |DESCARGA_DEF #caivalin@;

   |DELINDEX #901;
|FPRC;

|| -------------------------------------------------------------------------
|PROCESO adaptaC2_0;  ||El concepto en IVA
     aAlfa5 = "";
     aAlfa1 = #dsmom030#1;  |QBF aAlfa1;  || Descripcion
     aAlfa2 = #caivaasi#130;
     |SI aAlfa2 = "=  "; aAlfa2 = #caivaasi#45; |FINSI;
     |QBF aAlfa2;  || lo que a¤ade

     aAlfa3 = aAlfa2 % 0;
     nDCon0 = aAlfa3;
     |PARA nDCon1 = 1; |SI nDCon1 [ nDCon0; |HACIENDO nDCon1 = nDCon1 + 1;
           aAlfa3 = nDCon1;
           aAlfa3 = aAlfa3 + "01";
           aDCon2 = aAlfa2 % aAlfa3;
           |HAZ adapta2_c1;
           aAlfa5 = aAlfa5 + aDCon1;
     |SIGUE;
     aAlfa1  = aAlfa1 + aAlfa5;
|FPRC;

|PROCESO adapta2_c1;  ||El concepto
     aDCon1 = "";
     |SI aDCon2 = "S";
         aDCon1 = #camaniva#2; |QBF aDCon1;
         |SI aDCon1 ! ""; aDCon1 = aDCon1 + aSepSer; |QBF aDCon1; |FINSI;
     |FINSI;                                         || Serie
     |SI aDCon2 = "F";
         aDCon1 = #camaniva#3; |QBF aDCon1;
         |SI aNumIzq = "S";
             aDCon1 = ("000000" + aDCon1) % -106;
         |FINSI;
     |FINSI;                                         || Factura
     |SI aDCon2 = "C";
         aDCon1 = #camaniva#12; |QBF aDCon1;
     |FINSI;                                         || Cuenta Cliente
     |SI aDCon2 = "N";
         aDCon1 = #camaniva#13; |QBF aDCon1;
     |FINSI;                                         || Nombre Cliente
     |SI aDCon2 = "R";
         |SI #camaniva#22 ! "                    ";
             aDCon1 = #camaniva#22; |QBF aDCon1;
         |FINSI;
     |FINSI;                                         || Referencia
     |SI aDCon2 = "P";
         aDCon1 = #camaniva#22;
         |SI #camaniva#36 = "S";
             |HAZ EsComersan_0;
             |SI FSalida = 0;
                 |ABRE #camanref;
                 |DDEFECTO  #camanref;
                 #camanref#0 = #camaniva#0;
                 #camanref#1 = #camaniva#1;
                 |LEE 000#camanref.=;
                 aDCon1 = #camanref#2;
                 |CIERRA #camanref;
             |FINSI;
         |FINSI;
         |QBF aDCon1;
     |FINSI;                                         || Referencia Comersan

     |SI aDCon1 ! "";
         |SI aDCon2 = "F";
             aAlfa7 = aAlfa2 - "S";
             |SI aAlfa7 ! aAlfa2; |Y #camaniva#2 > "     ";
                 |ACABA;
             |FINSI;
         |FINSI;
         aDCon1 = " " + aDCon1;
     |FINSI;
|FPRC;

|PROCESO EsComersan_0;
   |DDEF aDirecPLB; |QBF aDirecPLB;
   aFichePLB = aDirecPLB + "comz0001.mas";
   |XABRE "E", aFichePLB, 0;
   |SI FSalida = 0;
      aFichePLB = aDirecPLB + "salud001.mas";
      |XABRE "E", aFichePLB, 0;
      |SI FSalida = 0;
         FSalida = 1;
      |SINO;
         FSalida = 0;
      |FINSI;
   |FINSI;

   || ... Solo para pruebas  FSalida = 0; || Cuidado, quitar antes de sacar
|FPRC;
