|INCLUYE dscont_i;
|FICHEROS;
   caborblo #0;
   caentasl #1;    || apuntes Rapida lins.
   caentasc #2;    || apuntes Rapida cabs.
   cacuenta #3;    || plan contable

   camandia #10;   || diarios

   caivaasi #8;
   &Puente@ #9;

   caliconb #31;

   camaniva #6;
   caivalin #7;

|FIN;

|VARIABLES;
   &entrada = 1;
   mensa1 = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
   mensa3 = "    NO EXISTE BLOQUE DE APUNTES ";

   cam = 0;
   alfa1 = "";
   nume1 = 0;
   bloque = "";
   fichero = "";
   estado = 0;

   desli = 0;
   hasli = 0;
   desdi = 0;
   hasdi = 0;
   desfe = @;
   hasfe = @;
   desdo = 0;
   hasdo = 0;

   ini = 0;
   fecalf = "";
   mes = "";
   mesfec = 0;
   mesac = 0;
   a = 0;
   b = 0;
   c = 0;


   Comodin = "";
   &eaPath = "";  &eaRetorno = "";
|FIN;



|| *************************************************************************
||                        DESDE CAMPOS DE PANTALLA
|| *************************************************************************

|PROCESO defecto; |TIPO 7;
   #caborblo#4 = fec1; |PINTA #caborblo#4;
   #caborblo#8 = fec2; |PINTA #caborblo#8;
   |PARA nume1 = 2; |SI nume1 [ 9; |HACIENDO nume1 = nume1 + 1;
      |C_M #caborblo#nume1#,1,"N";
   |SIGUE;
|FPRC;

|PROCESO leebloq; |TIPO 6;
   |ABRE #2;
   #caentasc#0 = #caborblo#0;
   |LEE 001#2=;
   |SI FSalida ! 0;
      |MENAV mensa3;
      |ERROR;
   |SINO;
      #caborblo#11 = #caentasc#4; |PINTA #caborblo#11;
      #caborblo#12 = #caentasc#5; |PINTA #caborblo#12;
      #caborblo#13 = #caentasc#6; |PINTA #caborblo#13;
   |FINSI;
   |CIERRA #2;
|FPRC;

|PROCESO mayor; |TIPO 0;
   alfa1 = #caborblo#Campo# > " ";
   #caborblo#Campo# = alfa1;
   |PINTA #caborblo#Campo#;
|FPRC;

|PROCESO mayor1; |TIPO 0;
   |HAZ mayor;
   |SI #caborblo#1 = "S";
      |PDEFECTO #0,2,10;
      #caborblo#4 = fec1; #caborblo#8 = fec2;
      |PARA nume1 = 2; |SI nume1 [ 9; |HACIENDO nume1 = nume1 + 1;
         |C_M #caborblo#nume1#,1,"N"; |PINTA #caborblo#nume1#;
      |SIGUE;
   |SINO;
      |PARA nume1 = 2; |SI nume1 [ 9; |HACIENDO nume1 = nume1 + 1;
         |C_M #caborblo#nume1#,1,"S";
      |SIGUE;
   |FINSI;
|FPRC;

|PROCESO lineah; | TIPO 0;   ||  hasta Linea
   |SI #caborblo#6 < #caborblo#2;  |ERROR;  |FINSI;
|FPRC;

|PROCESO diarh; | TIPO 0;   ||  hasta diario
   |SI #caborblo#7 < #caborblo#3;  |ERROR;  |FINSI;
|FPRC;

|PROCESO fecha; | TIPO 0;   || Desde/hasta fecha
   |SI #caborblo#Campo# < fec1; |O #caborblo#Campo# > fec2;
      |MENAV mensa1;
      |ERROR;
   |FINSI;
   |SI Campo = 8;
      |SI #caborblo#8 < #caborblo#4; |ERROR; |FINSI;
   |FINSI;
|FPRC;

|PROCESO documh; | TIPO 0;   ||  hasta documento
   |SI #caborblo#9 < #caborblo#5;  |ERROR;  |FINSI;
|FPRC;

|| *************************************************************************
||                  ACTUALIZA
|| *************************************************************************
|PROCESO actua_cab; || actualiza diario
   |SI #caentasl#8 = "D";
      #caborblo#11 = #caborblo#11 - #caentasl#9;
   |SINO;
      #caborblo#12 = #caborblo#12 - #caentasl#9;
   |FINSI;
   #caborblo#13 = #caborblo#11 - #caborblo#12;
   |PINTA #caborblo#11;
   |PINTA #caborblo#12;
   |PINTA #caborblo#13;
|FPRC;

|PROCESO LineasIva;
   |BORRA 020#caivalin.a;
|FPRC;

|DEFBUCLE LineasIva;
#caivalin#1;
;
#camaniva#0,#camaniva#1,001;
#camaniva#0,#camaniva#1,999;
;
NULL,LineasIva;
|FIN;

|PROCESO a_iva;
   |ABRE #camaniva; |ABRE #caivalin;
   |SELECT #3#camaniva;
   #camaniva#0  = #caentasl#11;      || libro
   #camaniva#2  = #caentasl#12;      || factura
   #camaniva#27 = #caentasl#13;      || serie
   |LEE 101#7=;
   |SI FSalida = 0;
      |ABRE #caivaasi;
      |LEE 000#caivaasi.p;
      |SI FSalida ! 0;
         |DDEFECTO #caivaasi;
         |GRABA 020#caivaasi.n;
         |LEE 000#caivaasi.p;
      |FINSI;
      Comodin = #caivaasi#56; |QBF Comodin;
      |CIERRA #caivaasi;
      |SI Comodin = ""; |ACABA; |FINSI;

      eaPath = Comodin;
      |HAZ CreaPuente;
      |SI eaRetorno = "ERROR"; |ACABA; |FINSI;

      |DDEFECTO #Puente@;
      #Puente@#0 = #camaniva#0;
      #Puente@#1 = #camaniva#2;
      #Puente@#2 = #camaniva#3;
      #Puente@#3 = 0;
      #Puente@#27 = "V";
      #Puente@#28 = "B";
      |GRABA 020#Puente@.n;

      |DDEFECTO #Puente@;
      #Puente@#0 = #camaniva#0;
      #Puente@#1 = #camaniva#2;
      #Puente@#2 = #camaniva#3;
      #Puente@#3 = 0;
      #Puente@#27 = "C";
      #Puente@#28 = "B";
      |GRABA 020#Puente@.n;

      |HAZ CierraPuente;
      |BUCLE LineasIva;
      |BORRA 020#camaniva.a;
   |FINSI;
   |LIBERA #camaniva;
   |SELECT #3#camaniva;
   |CIERRA #caivalin; |CIERRA #camaniva;
|FPRC;

|| *************************************************************************

|PROCESO borrobloq;  || trabajo por cada lineas
    MasMenos     = - #caentasl#9;
    DebeHaber    = #caentasl#8;
    aCUENTA      = #caentasl#4;
    nREGISTRO    = -1;
    nDIARIO      = #caentasl#2;
    aFECHA       = #caentasl#3;
    nDOCUMENTO   = #caentasl#21;
    nNIVELCUENTA = #caentasl#5;

    |HAZ actua_cab;
    |HAZ a_iva;

    |BORRA 020#1a;  ||   borra l¡nea
|FPRC;

|DEFBUCLE borbloque;
   #1#1;
   2,3,21;
   #0#0, desli, desdi, desfe, desdo;
   #0#0, hasli, hasdi, hasfe, hasdo;
   e;
   NULL, borrobloq;
|FIN;

|PROCESO borrocont;     || contrapartidas
   |BORRA 020#31a;        || borra contrap.
|FPRC;

|DEFBUCLE borcontra;
   #31#1;
   ;
   #0#0, desli;
   #0#0, hasli;
   e;
   NULL, borrocont;
|FIN;

|| **************************************************************************

|PROCESO finbloque;
   |ABRE #2;
   #caentasc#0 = #caborblo#0;
   |LEE 001#2=;
   estado = FSalida;
   |SI cam = 0;
      |BORRA 020#2a;
      |DELINDEX #1;
   |SINO;
      |ABRE #1;
      |DDEFECTO #1;
      |LEE 001#1u;
      |CIERRA #1;
      #caentasc#0 = #caborblo#0;
      #caentasc#1 = #caentasl#2;
      #caentasc#2 = #caentasl#3;
      #caentasc#3 = #caentasl#21;
      #caentasc#4 = #caborblo#11;
      #caentasc#5 = #caborblo#12;
      #caentasc#6 = #caborblo#13;
      |SI estado = 0; |GRABA 020#2a; |FINSI;
      |SI estado ! 0; |GRABA 020#2n; |FINSI;
   |FINSI;
   |CIERRA #2;
|FPRC;

|PROCESO combloq;
   cam = 1;
   |ERROR10;
|FPRC;

|DEFBUCLE combloque;
   #1#1;
   ;
   #0#0,     1;
   #0#0, 99999;
   e;
   NULL, combloq;
|FIN;

|| **************************************************************************

|| ------------- Anulando los asientos --------------------

|PROCESO borrado; |TIPO 3;
   |SI #caborblo#10 = "SI";
      bloque = #caborblo#0;
      fichero = bloque;
      |QBF fichero;
      fichero = fichero + "zzzzzzzz";
      fichero = fichero % 107;
      fichero = fichero + "l";

      |NOME_DAT #caentasl #fichero#;

      |ABRE #3;
      |ABRE #10;
      desli = #caborblo#2; hasli = #caborblo#6;
      desdi = #caborblo#3; hasdi = #caborblo#7;
      desfe = #caborblo#4; hasfe = #caborblo#8;
      desdo = #caborblo#5; hasdo = #caborblo#9;

      |BUCLE borbloque;   || borra lineas
      |BUCLE borcontra;   || Borro contraparitdas
      cam = 0;
      |BUCLE combloque;   || Comprobar si queda algo;
      |HAZ finbloque;

      |CIERRA #3;
      |CIERRA #10;
   |FINSI;
|FPRC;

|| **************************************************************************
