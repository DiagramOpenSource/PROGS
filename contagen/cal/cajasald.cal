|FICHEROS;
   cajasald #0;
   canempre #30;
   caivaasi #200;
|FIN;

|VARIABLES;
   &entrada  = 1;
   sw        = 0;
   num       = 0;
   men2      = "     Longitud de Cuenta Fuera de Nivel !";
   aAlfa1    = "";
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoiv_i;


|PROCESO cuenta; |TIPO 6;
   entrada = 1;

   eaCuenta = #0Campo;
   salidas  = FSalida;
   |HAZ FiltraPuntos;
   #0Campo = eaCuenta;
   |PINTA #0Campo;

   emCta    = 1;
   |HAZ SacaNivel;
   |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea
   #0#3 = enNivel;
|FPRC;

|CALCULO c_departa; |TIPO 7;
   |C_M #0Campo,1,eaDepar;
   |SI eaDepar = "N"; |ACABA; |FINSI;
   |SI Haybasica = 1;
       |MENSA "    <F2> Consultar Actividades";
   |SINO;
       |MENSA "    <F2> Consultar Departamentos";
   |FINSI;
|FCAL;

|CALCULO TrasDepar; |TIPO 0;
   |SI eaDepar = "N"; |ACABA; |FINSI;

   |BLIN 4;
   eOpDep = 0; || 1 = Consulta pantalla
   |SI FSalida = 10; eOpDep = 1;  |FINSI;
   enActividad = #0#6;
   eaCodDep    = #0#6;
   |HAZ Actividad;
   |SI eswLect = 1;
       |MENAV "    Error.! No Existe Actividad / Departamento";
       |ERROR;
   |SINO;
       #0#6 = eaCodDep;    |PINTA #0#6;
       |SI efActFin > "01.01.1900"; |Y snFilBa = "S";
           aAlfa1 = "    NAtencion!!!. Actividad con Fecha de BAJA " + efActFin + ", Desea introducir Facturas ? ";
           |CONFI aAlfa1;
           |SI FSalida ! 0;
               |ERROR;
               |ACABA;
           |FINSI;
       |FINSI;
       |SI eaEsCom = "S";
           |CONFI "    NAtencion!!!. Actividad Comunera, ¨ Desea introducir Movimientos ? ";
           |SI FSalida ! 0;
               |ERROR;
               |ACABA;
           |FINSI;
       |FINSI;
   |FINSI;
|FCAL;

|CALCULO nosubde; |TIPO 7;
   |C_M #0Campo,1,eaSubde;

   |SI eaSubde = "N"; |ACABA; |FINSI;
   |SI Haybasica = 1;
       |SI enArrend = 1;
           |MENSA "    Subdepartamento. <F2> Consultar Referencias Catastrales";
       |SINO;
           |MENSA "    Subdepartamento. <F2> Consultar Cultivos";
       |FINSI;
   |FINSI;
|FCAL;

|CALCULO TrasSub; |TIPO 0;
   |SI eaSubde = "N"; |ACABA; |FINSI;

   |BLIN 4;
   eOpSub = 0; || 1 = Consulta pantalla
   |SI FSalida = 10; eOpSub = 1;  |FINSI;

   eaCodSubdep = #0#8;
   enQue = 1;
   |HAZ SubActividad;
   enQue = 0;
   |SI eswLect = 1;
       |MENAV "    Error.! No Existe SubDepartamento";
       |ERROR;
   |SINO;
       #0#8 = eaCodSubdep; |PINTA #0#8;
   |FINSI;
|FCAL;

|CALCULO TrasConcepto; |TIPO 6;
   enFilConc  = 1;
   eaCuenta   = #0#2;
   enConcepto = #0Campo;
   |HAZ FiltroConcepto;  |SI eswLect ! 0; |ACABA; |FINSI;
|FCAL;


|PROGRAMA;

   |HAZ inicial;
   |SI swerror = 2;
       |PTEC 807;
       |ACABA;
   |FINSI;

   |HAZ eLeeParametro;

   |CLS;
   |CABEZA "Saldo Dia Anterior";
   |PINPA #0#0;
   |DDEFECTO #0;
   |ABRE #0;
   |LEE 101#0p;
   |SI FSalida ! 0;
      |DDEFECTO #0;
      #0#6 = eaDfDepC;
      #0#8 = eaDfSubC;
      |GRABA 020#0n;
      |LEE 101#0p;
   |FINSI;
   |PINDA #0#0;
   |ENDATOS #1#0;
   |SI FSalida = 0;
      |GRABA 020#0a;
   |FINSI;
   |LIBERA #0;
   |CIERRA #0;
|FPRO;
