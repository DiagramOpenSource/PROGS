|FICHEROS;
   cacuenta #0;
   catestcu #2;
   cacuebal #5;
   ca8cumay #410;
   ca8m0002 #422;
   ca8m0003 #403;

   camaasil #1;
   cacamctm #10;

   canempre #30;
   caselem1 #998;

   espejo@  #900;
|FIN;

|VARIABLES;
   &sw_conase4 = 0;
   &entrada = 1;
   informe = "catestcu";
   &error = "";
   &arreg = "";
   &ercue = "";
   &erdes = "";
   &erniv = 0;
   men1  = "     NO EXISTE EMPRESA !";
   long = "  ";
   valo = 0;
   saber = "";
   cuini = "            ";
   cufin = "zzzzzzzzzzzz";
   niini = 0;
   nifin = 9;
   prim = 0;
   nivant = 0;
   cuante = "";
   cuante1 = "";
   cuante2 = "";
   pasant = "";
   cuenta = "";
   desant = "";
   alfa1  = "";
   alfa2  = "";
   cta = "";
   nivel = 0;
   nume1 = 0;

   nAvisoL    = 0;
   swLineal   = 0;
   swBorrar   = 0;
   swGrabar   = 0;
   nSwAlgun   = 0;
   aDirInicio = "";
   aMas       = "";
   nerror     = 0;
   nPara1     = 0;

   aParam     = "";
   nCam4      = 0;
   aCam5      = "";
   nCam6      = 0;
   nCam7      = 0;
   nCam8      = 0;
   &swlin     = 0;
   nNumero    = 0;
   aCuenta    = "";
   aAlfa      = "";
   aAlfa1     = "";
   aAlfa2     = "";
   aAlfa3     = "";
   aAlfa4     = "";
   nNume1     = 0;
   nNume2     = 0;
   nNume3     = 0;
   nNume4     = 0;
   nLongi     = 0;
   nError1    = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE defect_i;

|CALCULO conase4; |TIPO 7;
  |SI enEjercicio ] 2008;
      #2#4 = "X";
  |SINO;
      #2#4 = "N";
  |FINSI;
  |PINTA #2#4;

  |SI sw_conase4 = 1;
      #2#0 = "SI";     |PINTA #2#0;
      #2#1 = "N";      |PINTA #2#1;
      #2#2 = "N";      |PINTA #2#2;
      #2#3 = "N";      |PINTA #2#3;
      #2#4 = "N";      |PINTA #2#4;
      |PTEC 802;
      |PTEC 802;
      |PTEC 802;
      |PTEC 802;
      |PTEC 802;
      |PTEC 802;
  |FINSI;
|FCAL;

|| .... Proceso Balances  Asignacion balances

|PROCESO balance;
   nCam4  = #cacuenta#4; aCam5 = #cacuenta#5;
   nCam6  = #cacuenta#6; nCam7 = #cacuenta#7;
   nCam8  = #cacuenta#8;
   nMensa = 1; nMensa9= 1;
   |HAZ defect;
   |SI nNoBal = 1;
      ercue = #cacuenta#0;
      erniv = #cacuenta#1;
      erdes = #cacuenta#2;
      |SI #2#4 ! "X";
          error = "NO EXISTE EN BALANCES 1990";
          arreg = "N";
          |SI enAutom = 0; |PRINT; |FINSI;
          nSwAlgun = 1;
      |FINSI;
   |SINO;
      |SI #cacuenta#4 ! nCam4; |O #cacuenta#5 ! aCam5; |O #cacuenta#6 ! nCam6;
          |O #cacuenta#7 ! nCam7; |O #cacuenta#8 ! nCam8;
          ercue = #cacuenta#0;
          erniv = #cacuenta#1;
          erdes = #cacuenta#2;
          |SI #2#4 ! "X";
              error = "NO CORRESPONDE CON BALANCES 1990";
              arreg = "S";
              swGrabar = 1;
              |SI enAutom = 0; |PRINT; |FINSI;
              nSwAlgun = 1;
          |FINSI;
      |FINSI;
   |FINSI;

   |SI #2#4 = "X";
       nError1 = 0;
       aAlfa1 = #cacuenta#0 % 101;
       |SI aAlfa1 ] "1"; |Y  aAlfa1 [ "5"
           |SI aCam5 ! "A"; |Y aCam5 ! "P";
               nError1  = 1;
           |FINSI;
       |FINSI;
       |SI aAlfa1 = "6"; |O  aAlfa1 = "7"
           |SI aCam5 ! "D"; |Y aCam5 ! "H";
               nError1  = 1;
           |FINSI;
       |FINSI;
       |SI aAlfa1 = "8"; |O  aAlfa1 = "9"
           |SI aCam5 ! "I"; |Y aCam5 ! "G";
               nError1  = 1;
           |FINSI;
       |FINSI;
       |SI nError1 = 1;
          ercue = #cacuenta#0;
          erniv = #cacuenta#1;
          erdes = #cacuenta#2;
          error = "NO CORRESPONDE TIPO BALANCES";
          arreg = "S";
          swGrabar = 1;
          |SI enAutom = 0; |PRINT; |FINSI;
          nSwAlgun = 1;
       |FINSI;
  |FINSI;

   |SI nNoBalX = 1;
      ercue = #cacuenta#0;
      erniv = #cacuenta#1;
      erdes = #cacuenta#2;
      error = eNum2008; error = ("00" + error) % -102;
      error = "NO EXISTE EN PLAN CTBLE: " + error;
      arreg = "N";
      |SI #2#4 ! "N";
          |SI enAutom = 0; |PRINT; |FINSI;
          nSwAlgun = 1;
      |FINSI;
   |FINSI;
   |SI nNoBal8 = 1;
         ercue = #cacuenta#0;
         erniv = #cacuenta#1;
         erdes = #cacuenta#2;
         error = "NO EXISTE EN BALANCES 2008";
         arreg = "N";
         |SI #2#4 ! "N";
            |SI enAutom = 0; |PRINT; |FINSI;
            nSwAlgun = 1;
         |FINSI;
   |FINSI;
|FPRC;

|| ============================================================
|PROCESO CambioCta;

   aAlfa1 = aCuenta;  |QBF aAlfa1;
   aAlfa2 = aAlfa1 % 0;
   nLongi = aAlfa2;

   |SI nLongi < MaximoDigitos; |Y nLongi > 4;

       nNume1 = MaximoDigitos - nLongi;
       aAlfa1 = "0" * nNume1;     ||digitos que hacen falta para rellenar

       aAlfa2 = aCuenta; |QBF aAlfa2;

       nNume4 = 104;
       aAlfa3 = nNume4; |QBF aAlfa3;  || digitos del principio

       nNume1 = nLongi - 4;
       nNume4 = 0 - (100 + nNume1);   || parte posterior
       aAlfa4 = nNume4;

       aCuenta = (aAlfa2 % aAlfa3) + aAlfa1 + (aAlfa2 % aAlfa4);
   |FINSI;
|FPRC;

|| ============================================================
|PROCESO ModiNO;
 |ABRE #900;
 #900#0 = ercue;
 |LEE 101#900=;
 |SI FSalida = 0;
     #900#3 = "N";
     arreg = "S";
     |GRABA 020#900a;
 |FINSI;
 |LIBERA #900;
 |CIERRA #900;
|FPRC;

|PROCESO RegrabarFicha;
  alfa1 = #cacuenta#0;
  |QBF alfa1;
  alfa1 = alfa1 + "zzzzzzzzzzzz";
  #cacuenta#54 = alfa1 % 112;
  #cacuenta#55 = #cacuenta#1;
  |GRABA 020#0a;
  |LIBERA #0;
|FPRC;

|PROCESO AlLineal;
   swLineal = 0;
   |ABRE #1;
   |SELECT #3#1;
   #1#4 = #0#0;
   #1#0 = 00;
   #1#1 = "01.01.0000";
   #1#2 = 000001;
   |LEE 001#1];
   |SI FSalida = 0; |Y #0#0 = #1#4;
       swLineal = 1;
   |FINSI;
   |SELECT #3#1;
   |CIERRA #1;
   |SI swLineal = 1;
       |SI nAvisoL = 0;
           |DELINDEX #10;
       |FINSI;
       |ABRE #10;
       |DDEFECTO #10;
       |LEE 001#10u;
       |SI FSalida = 0;
            nNumero = #10#8 + 1;
       |SINO;
            nNumero = 1;
       |FINSI;
       |DDEFECTO #10;
       #10#8 = nNumero;
       #10#0 = #0#0;
       #10#1 = #0#1;
       #10#2 = #0#2;
       aCuenta = #0#0;
       |HAZ CambioCta;
       #10#3 = aCuenta;
       #10#4 = dig3;
       #10#5 = #0#2;
       #10#6 = "S"; |SI #2#1 = "S"; #10#6 = "N"; |FINSI;
       |GRABA 040#10n;
       nAvisoL = 1;
       |CIERRA #10;
   |FINSI;
|FPRC;

|PROCESO BorrarFicha;
  |BORRA 020#0a;
  |LIBERA #0;
|FPRC;

|PROCESO mayor;
   |SI cuenta ! cuante1;
       |SI nivel ! 1;
           |HAZ guard2;
           ercue = cuante2;
           erdes = "";
           erniv = nivel - 1;
           error = "NO EXISTE NIVEL";
           arreg = "N";
           |SI enAutom = 0; |PRINT; |FINSI;
           nSwAlgun = 1;
       |FINSI;
   |SINO;
       |SI pasant = "S";
           ercue = cuante1;
           erdes = desant;
           erniv = nivant;
           error = "NO ES DE PASE DIRECTO";
           arreg = "N";
           |SI #2#3 = "S"; |HAZ ModiNO; |FINSI;
           |SI enAutom = 0; |PRINT; |FINSI;
           nSwAlgun = 1;
       |FINSI;
   |FINSI;
|FPRC;

|PROCESO igual;
   |SI cuante ! cuante1;
      |SI nivel ! 1;
         |HAZ guard2;
         ercue = cuante2;
         erdes = "";
         erniv = nivel - 1;
         error = "NO EXISTE NIVEL";
         arreg = "N";
         |SI enAutom = 0; |PRINT; |FINSI;
         nSwAlgun = 1;
      |SINO;
         ercue = cuante1;
         erdes = desant;
         erniv = nivant;
         error = "NO ES DE PASE DIRECTO";
         arreg = "N";
         |SI #2#3 = "S"; |HAZ ModiNO; |FINSI;
         |SI enAutom = 0; |PRINT; |FINSI;
         nSwAlgun = 1;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO menor;
   |SI pasant = "N"; |Y #2#3 ! "S";
      ercue = cuenta;
      erdes = desant;
      erniv = nivant;
      error = "DEBERIA SER DE PASE DIRECTO";
      arreg = "N"
      |SI enAutom = 0; |PRINT; |FINSI;
      nSwAlgun = 1;
   |FINSI;
|FPRC;

|PROCESO guarda;
   cuenta = #cacuenta#0;
   |QBF cuenta;
   nivant = #cacuenta#1;
   desant = #cacuenta#2;
   pasant = #cacuenta#3;
   |SI nivel = 6;  cuante = #cacuenta#0 % dig8;  |FINSI;
   |SI nivel = 5;  cuante = #cacuenta#0 % dig7;  |FINSI;
   |SI nivel = 4;  cuante = #cacuenta#0 % dig6;  |FINSI;
   |SI nivel = 3;  cuante = #cacuenta#0 % dig5;  |FINSI;
   |SI nivel = 2;  cuante = #cacuenta#0 % dig4;  |FINSI;
   |SI nivel = 1;  cuante = #cacuenta#0 % dig4;  |FINSI;
|FPRC;

|PROCESO guard1;
   |SI nivel = 6;  cuante1 = #cacuenta#0 % dig8;  |FINSI;
   |SI nivel = 5;  cuante1 = #cacuenta#0 % dig7;  |FINSI;
   |SI nivel = 4;  cuante1 = #cacuenta#0 % dig6;  |FINSI;
   |SI nivel = 3;  cuante1 = #cacuenta#0 % dig5;  |FINSI;
   |SI nivel = 2;  cuante1 = #cacuenta#0 % dig4;  |FINSI;
|FPRC;

|PROCESO guard2;
   |SI nivel = 6;  cuante2 = #cacuenta#0 % dig8;  |FINSI;
   |SI nivel = 5;  cuante2 = #cacuenta#0 % dig7;  |FINSI;
   |SI nivel = 4;  cuante2 = #cacuenta#0 % dig6;  |FINSI;
   |SI nivel = 3;  cuante2 = #cacuenta#0 % dig5;  |FINSI;
   |SI nivel = 2;  cuante2 = #cacuenta#0 % dig4;  |FINSI;
|FPRC;

|PROCESO buscando;
   swBorrar = 0;
   swGrabar = 0;
   swLineal = 0;

   saber = "Procesando Cuenta: " + #cacuenta#0;
   |PINTA 2210,saber;

   cta = #cacuenta#0; |QBF cta; |SI cta = ""; |ACABA; |FINSI;

   nerror = 0;
   |PARA nPara1 = 1; |SI nPara1 [ 12; |HACIENDO nPara1 = nPara1 + 1;
         alfa1 = nPara1; alfa1 = alfa1 + "01";
         alfa2 = #cacuenta#0 % alfa1;
         |SI alfa2 < "0"; |O alfa2 > "9";
             |SI alfa2 ! " ";
                 nerror = 1;
                 |SAL;
             |FINSI;
         |FINSI;
   |SIGUE;
   |SI nerror = 1;
       ercue = #cacuenta#0;
       erniv = #cacuenta#1;
       erdes = #cacuenta#2;
       error = "CUENTA CON CARACTERES EXTRA¤OS";
       arreg = "S";
       swBorrar =  1;
       swLineal =  1;
       |SI enAutom = 0; |PRINT; |FINSI;
       nSwAlgun = 1;
       |VETE finBusqueda;
   |FINSI;

   nivel = 0;
   long  = cta % 0;
   nume1 = long;

   |SI nume1 = #canempre#14; nivel = 1; |FINSI;
   |SI nume1 = #canempre#15; nivel = 2; |FINSI;
   |SI nume1 = #canempre#16; nivel = 3; |FINSI;
   |SI nume1 = #canempre#17; nivel = 4; |FINSI;
   |SI nume1 = #canempre#18; nivel = 5; |FINSI;
   |SI nume1 = #canempre#19; nivel = 6; |FINSI;

|| .... Testeo coherencia entre nivel y Longitud de la Cuenta
   |SI nivel = 0;
       ercue = #cacuenta#0;
       erniv = #cacuenta#1;
       erdes = #cacuenta#2;
       error = "LONGITUD CTA FUERA DE NIVEL";
       arreg = "N";
       swLineal =  1;
       |SI #2#1 = "S";
           swBorrar =  1;
           arreg    = "S";
       |FINSI;
       |SI enAutom = 0; |PRINT; |FINSI;
       nSwAlgun = 1;
       |VETE finBusqueda;
   |SINO;
       |SI nivel ! #cacuenta#1;
           ercue = #cacuenta#0;
           erniv = #cacuenta#1;
           erdes = #cacuenta#2;
           error = "NIVEL NO CORRESPONDE A LONGITUD";
           arreg = "N";
           |SI #2#2 = "S";
               #cacuenta#1 = nivel;
               swGrabar =  1;
               arreg    = "S";
           |FINSI;
           |SI enAutom = 0; |PRINT; |FINSI;
           nSwAlgun = 1;
      |FINSI;
   |FINSI;

   |SI prim = 0;
      |SI nivel ! 1;
          ercue = #cacuenta#0 % dig4;
          erniv = #cacuenta#1;
          erdes = #cacuenta#2;
          error = "FALTA ESTA CUENTA DE NIVEL 1";
          arreg = "N";
          |SI enAutom = 0; |PRINT; |FINSI;
          nSwAlgun = 1;
      |FINSI;
       prim = 1;
   |SINO;
      |HAZ guard1;
      |SI nivant < nivel;  |HAZ mayor;  |FINSI;
      |SI nivant = nivel;  |HAZ igual;  |FINSI;
      |SI nivant > nivel;  |HAZ menor;  |FINSI;
      |SI nivel = dig3; |Y #cacuenta#3 = "N";
          ercue = #cacuenta#0;
          erniv = #cacuenta#1;
          erdes = #cacuenta#2;
          error = "DEBE SER CUENTA DE PASE DIRECTO";
          #cacuenta#3 = "S";
          swGrabar = 1;
          arreg = "S";
          |SI enAutom = 0; |PRINT; |FINSI;
          nSwAlgun = 1;
      |FINSI;
      |SI nivel ! dig3; |Y #cacuenta#3 = "S"; |Y #2#3 = "S";
          ercue = #cacuenta#0;
          erniv = #cacuenta#1;
          erdes = #cacuenta#2;
          error = "NO ES DE PASE DIRECTO";
          #cacuenta#3 = "N";
          swGrabar = 1;
          arreg = "S";  || pero no lo imprime
      |FINSI;
   |FINSI;

|ET finBusqueda;

   |SI swLineal = 1;
       |HAZ AlLineal;
   |FINSI;

   |SI swBorrar = 1;
       |HAZ BorrarFicha;  ||no la guarda como la anterior
       |LIBERA #0;
       |ACABA;
   |SINO;
       |HAZ balance;
       |SI swGrabar = 1;
           |HAZ RegrabarFicha;
       |FINSI;
   |FINSI;

   |HAZ guarda;
   |LIBERA #0;
|FPRC;


|DEFBUCLE catestcu0; || ... Lee por primera clave para poder modificar nivel
   #0#1;
   ;
   cuini;
   cufin;
   be;
   NULL, buscando;
|FIN;

|PROCESO catestcu1;
 #0#0 = #403#2;
 |LEE 041#0=;
 |SI FSalida ! 0;
     ercue = #403#2;
     erniv = 0;
     erdes = #403#3;
     error = "NO EXISTE EN PLAN CONTABLE";
     arreg = "N";
     |SI #2#1 = "S";
         |BORRA 020#403a;
          arreg    = "S";
     |FINSI;
     |SI enAutom = 0; |PRINT; |FINSI;
     nSwAlgun = 1;
     swlin = 2;
 |FINSI;
 |LIBERA #403;
|FPRC;

|DEFBUCLE catestcu1;
 #403#1;
 ;
 "            ";
 "zzzzzzzzzzzz";
 be;
 NULL, catestcu1;
|FIN;

|PROCESO HazTodo;
   dig1 = #canempre#11;
   dig3 = #canempre#13;
   dig4 = 100 + #canempre#14;
   dig5 = 100 + #canempre#15;
   dig6 = 100 + #canempre#16;
   dig7 = 100 + #canempre#17;
   dig8 = 100 + #canempre#18;
   dig9 = 100 + #canempre#19;

   nAvisoL  = 0;
   swlin    = 0;
   prim     = 0;
   nSwAlgun = 0;
   |BUCLE catestcu0;
   |SI #canempre#26 = 8; ||ejercicio 2008
       |ABRE #0;
       swlin = 1;
       |BUCLE catestcu1;
       |CIERRA #0;
   |FINSI;
   |SI nSwAlgun = 1;
       |PIEINF;
   |FINSI;
   dig4 = #canempre#14;
   dig5 = #canempre#15;
   dig6 = #canempre#16;
   dig7 = #canempre#17;
   dig8 = #canempre#18;
   dig9 = #canempre#19;

   |SI nAvisoL = 1; |Y enAutom = 0;
       aAlfa = "    En la Empresa : " + #canempre#2 + " Periodo: " + #canempre#3;
       aAlfa = aAlfa + " Hay cuentas erroneas en el Diario. Lance el Cambio de Cuentas";
       |MENAV aAlfa;
   |FINSI;

|FPRC;

|PROCESO creacion; |TIPO 3;
   |SI #2#0 = "NO"; |ACABA; |FINSI;

   |SI enAutom = 0;
      |IMPRE 0;
      |SI FSalida ! 0;  |ACABA;  |FINSI;
      |INFOR informe;
      |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;
   |FINSI;

   |DBASE aDirInicio;
   aMas  = aDirInicio + "def/cacuenta";
   |CARGA_DEF aMas, espejo@;
   |SI FSalida ! 0;
       |MENAV "    Error Cargado Fichero de Cuentas. Proceso Interrumpido";
       |ACABA;
   |FINSI;

   |SI #48#27 = "S"; |Y aParam = "";
       |HAZ MultiEmpresa;
   |SINO;
       |DFICE dirfich0; |QBF dirfich0;
       |HAZ HazTodo;
   |FINSI;

   |DESCARGA_DEF #espejo@;

   |SI enAutom = 0;
      |FININF;
      |FINIMP;
   |FINSI;
|FPRC;

|| **********************************************************************
|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #0   dirfich0;
   |PATH_DAT #1   dirfich0;
   |PATH_DAT #10  dirfich0;
   |PATH_DAT #5   dirfich0;
   |PATH_DAT #403 dirfich0;
   |PATH_DAT #900 dirfich0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 000001;
 999999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;


|PROGRAMA;
 aParam = PARAMETRO; |QBF aParam;
 |SI aParam = "";
     |MANTE #2;
 |SINO;
     |SI enAutom = 0;
        |CLS;
        |HAZ inicio;
        |PINPA #0#2;
     |FINSI;
     #2#0 = "SI";
     #2#1 = "S";
     #2#2 = "S";
     #2#3 = "S";
     |SI enAutom = 0;
        |PINDA #0#2;
        |ENDATOS #1#2;
     |SINO;
        |HAZ creacion;
     |FINSI;
     || ... |HAZ creacion;
 |FINSI;
|FPRO;
