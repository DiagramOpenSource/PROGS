|FICHEROS;
   caditra0 #0;
   calicont #31;

   canempre #30;
   cawdatos #900;
   caselem1 #998;
|FIN;

|VARIABLES;
   &entrada = 1;
   lin = 0;
   sw_desc = 0;
   fecalf = "";
   men1   = "    NO EXISTE EMPRESA";
   traba1 = "";

   &swlin = 0;
   &descuadre = 0;
   &a_debe = 0;
   &a_haber = 0;
   &contra = "";
   contra_d = "";
   contra_h = "";
   swex_con = 0;
   informe = "";
   inform1 = "caditra0";
   inform2 = "caditra2";
   {-1}menu = "";
   menu1 = "2400";
   menu2 = "2";
   menu3 = " Elija opcion: ";
   menu4 = "PI";
   menu5 = " Pantalla ";
   menu6 = " Impresora ";
   &hiva_1 = "";
   &hiva_2 = "";
   &hiva_3 = "";
   &hiva_4 = @;
   nSwAlgun = 0;
   aAlfa    = "";
|FIN;

|INCLUYE dscont_i;
|INCLUYE diario_i;

|| *********************************************************************
||                             COMIENZO
|| *********************************************************************

|PROGRAMA;
     ||si en "PARAMETRO" viene "WEB" sale al menu
   |CLS;
   |CABEZA "Listado Diario de trabajo";
   |HAZ inicio;

   |ABRE #8;
   |DDEFECTO #8;
   #8#0 = 1;
   |LEE 001#8=;
   |CIERRA #8;

   |DDEFECTO #0;
   #0#17 = #8#11;
   #0#19 = #8#13;
   #0#20 = #8#7;
   alfa1 = #8#8; |C_M #0#20,1,alfa1;

   FSalida = 1;
   |PARA; |SI FSalida = 1; |HACIENDO;
          |PINPA #0#0;
          |SI enEjercicio ! 2008;
              #0#17 = "N";
              |C_V #0#17,1,"N"; |C_M #0#17,1,"N";
              |C_V #0#19,1,"N"; |C_M #0#19,1,"N";
              alfa1 = "읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴켸";
              alfa2 = "                                                              ";
              |PINTA 1810, alfa1;
              |PINTA 1910, alfa2;
              |PINTA 2010, alfa2;
              |CUADRO 2123 2358;
          |FINSI;

          |PINDA #0#0;                    || Pinta los datos por defecto
          |ENDATOS #1#0;                  || Seleccion informe
          |SI FSalida ! 0;  |SAL;  |FINSI;
          |HAZ LaImpresion;
          |SI PARAMETRO ! "WEB";        || Controlamos que salga al menu
               FSalida = 1;             || Vuelve al bucle
          |SINO;
               FSalida = 0;
          |FINSI;
   |SIGUE;
|FPRO;


|PROCESO LaImpresion;

  |MENU menu;
  opcion = FSalida;
  |SI opcion < 1; |ACABA; |FINSI;

  d_iva   = #0#12;
  m_orden = #0#11;
  |SI opcion = 2;

      |SI PARAMETRO = "AGENCIA";
         Impresora = "CrystalReport";
         |IMPRE -1;
      |SINO;
         |IMPRE 0;
      |FINSI;
      |SI FSalida ! 0;  |ACABA;  |FINSI;

      aAlfa = Impresora;
      aAlfa = aAlfa % 113;
      |SI aAlfa = "CrystalReport"; |Y PARAMETRO ! "AGENCIA";
          inform1 = "ccditra0";
      |SINO;
          inform1 = "caditra0";
      |FINSI;

      informe = inform1; |SI d_iva = "S";  informe = inform2;  |FINSI;

      |INFOR informe; |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;
  |FINSI;
  enDatos = 0; |SI #0#20 = "S"; enDatos = 1; |FINSI;

  |HAZ DirAplicSt;
  |SI Haybasica = 1; |Y #0#14 = "zz"; #0#14 = "99"; |FINSI;

  |SI #48#27 = "S";
      |HAZ MultiEmpresa;
  |SINO;
      |DFICE dirfich0; |QBF dirfich0;
      |HAZ HazTodo;
  |FINSI;

  |SI opcion = 2;
      |FININF;
      |FINIMP;
  |FINSI;
|FPRC;

|| *************************************************************************
||                          MEOLLO
|| *************************************************************************
|PROCESO AImprimir_i;
   swlin = 0;
   contra = "";
   contra_d = #1#10;
   contra_h = #1#8;
   |SI #2#8 = "D";
      a_debe = #2#9;
      a_haber = 0;
      contra = contra_d;
   |SINO;
      a_debe = 0;
      a_haber = #2#9;
      contra = contra_h;
   |FINSI;
   |PRINT;
|FPRC;

|PROCESO Apertura2008_i;
   |SALVA_DATOS #1;
   #1#3 = #1#3 - 1; |SI #1#3 [ 0; #1#3 = 1; |FINSI;
   descuadre = (#1#4 - #1#5) ! EURODB;
   swlin = 1;
   |SI descuadre ! 0;
      swlin = 2;
      |SI descuadre < 0; descuadre = descuadre * (-1); |FINSI;
   |FINSI;
   |PRINT;

   #1#4 = 0; #1#5 = 0; #1#6 = 0;
   nOpe = 0; |BUCLE Leo8m004;

   #1#3 = #1#3 + 1;
   descuadre = (#1#4 - #1#5) ! EURODB;
   swlin = 1;
   |SI descuadre ! 0;
      swlin = 2;
      |SI descuadre < 0; descuadre = descuadre * (-1); |FINSI;
   |FINSI;
   |PRINT;

   nLinea = 0;
   nOpe   = 1; |BUCLE Leo8m004;
   nSwAlgun = 1;
   |REPON_DATOS #1;
|FPRC;


|PROCESO porimpre;

|SI Haybasica = 1;
   nDepar  = #2#21; |SI nDepar = 0;  nDepar = 1;   |FINSI;
   nDdepar = #0#13;
   nHdepar = #0#14;
   |SI nDepar < nDdepar; |O nDepar > nHdepar; |ACABA; |FINSI;
|SINO;
   |SI #2#21 < #0#13;  |O #2#21 > #0#14;  |ACABA;  |FINSI;
|FINSI;

   |SI #2#28 < #0#15;  |O #2#28 > #0#16;  |ACABA;  |FINSI;
   |SI #2#6 < #0#9;  |O #2#6 > #0#10;  |ACABA;  |FINSI;
   #4#0 = #2#4;
   #4#1 = #2#5;
   |LEE 040#4=;
   |SI FSalida ! 0;
      |DDEFECTO #4;
   |FINSI;
   swlin = 0;

   contra = "";
   contra_d = #1#10;
   contra_h = #1#8;
   |HAZ leocontra;
   |SI #2#8 = "D";
      a_debe = #2#9;
      a_haber = 0;
      contra = contra_d;       || contrapartida DEBE
   |SINO;
      a_debe = 0;
      a_haber = #2#9;
      contra = contra_h;        || contrapartida HABER
   |FINSI;
   |SI d_iva = "S";
      |HAZ lecturaIVA;
   |FINSI;
   |PRINT;
|FPRC;

|PROCESO lecturaIVA;
   hiva_1 = ""; hiva_2 = "";
   hiva_3 = ""; hiva_4 = "";
   |SI #2#19 = "F"; |O #2#19 = "f";
      |ABRE #camaniva;
      |SELECT #3#camaniva;
      #camaniva#0 = #2#13;  || Libro
      #camaniva#2 = #2#15;  || Serie
      #camaniva#3 = #2#14;  || Factura
      |LEE 001#camaniva.=;
      |SI FSalida = 0;
         hiva_1 = #camaniva#0;
         hiva_2 = #camaniva#3;
         hiva_3 = #camaniva#2;
         hiva_4 = #camaniva#4;
      |FINSI;
      |SELECT #1#camaniva;
      |CIERRA #camaniva;
   |FINSI;

   |QBF hiva_1; |SI hiva_1 ! ""; hiva_1 = ("00"+hiva_1) % -102; |FINSI;
   |QBF hiva_2; |SI hiva_2 ! ""; hiva_2 = ("000000"+hiva_2) % -106; |FINSI;
   |QBF hiva_3; |SI hiva_3 ! ""; hiva_3 = "/"+hiva_3; |FINSI;
|FPRC;

|PROCESO leocontra;
   swex_con = 0;
   #calicont#0 = #2#0;
   #calicont#1 = #2#1;
   #calicont#2 = #2#2;
   #calicont#3 = #2#3;
   |LEE 001#31=;
   |SI FSalida = 0;
      swex_con = 1;
      contra_h = #calicont#4;
      contra_d = #calicont#4;
   |FINSI;
|FPRC;

|PROCESO bucleimp;
   nConcepto    = 0;
   aDescripcion = "";
   aSubdep      = "";
   sw_alguno    = 0;
   |BUCLELIN 2alguno#1;
   |SI sw_alguno = 0;  |ACABA;  |FINSI;

   |SI #0#17 = "S";
       |SI #1#0 = 0; |Y nDiaAper = 0;
           |HAZ VerAper08_d;
           |SI nIgual = 1;
               |HAZ Apertura2008_i;
               nDiaAper = 1;
               |ACABA;           ||este asiento se trata de forma diferente
           |FINSI;
       |FINSI;
   |FINSI;

   descuadre = #1#4 - #1#5;
   |SI descuadre = 0;
      swlin = 1;
      |PRINT;
   |SINO;
      swlin = 2;
      |SI descuadre < 0; descuadre = descuadre * (-1); |FINSI;
      |PRINT;
   |FINSI;
   |BUCLELIN 0porimpre#1;
   nSwAlgun = 1;
|FPRC;

|DEFBUCLE caditra01;
   #1#2;
   ;
   #0#0, #0#4, #0#7, #0#2;
   #0#1, #0#5, #0#8, #0#3;
   e;
   NULL, bucleimp;
|FIN;

|DEFBUCLE caditra02;
   #1#1;
   3;
   #0#7, #0#4, #0#2, #0#0;
   #0#8, #0#5, #0#3, #0#1;
   e;
   NULL, bucleimp;
|FIN;

|DEFBUCLE caditra03;
   #1#3;
   3,1,0;
   #0#2,  #0#0, #0#4, #0#7;
   #0#3,  #0#1, #0#5, #0#8;
   e;
   NULL, bucleimp;
|FIN;

|PROCESO impresora;
   nSwAlgun = 0;
   |ABRE #31;
   |SI m_orden = 1; |BUCLE caditra01; |FINSI;
   |SI m_orden = 2; |BUCLE caditra02; |FINSI;
   |SI m_orden = 3; |BUCLE caditra03; |FINSI;
   |CIERRA #31;
   |SI nSwAlgun = 1; |PIEINF; |FINSI;
|FPRC;

|PROCESO HazTodo;

   m_orden  = #0#11;
   d_iva    = #0#12;
   nDiaAper = 0;
   eaIa = #30#21;
   alfa1 = #0#4; alfa1 = alfa1 % 0402; enM1 = alfa1;
   alfa1 = #0#5; alfa1 = alfa1 % 0402; enM2 = alfa1;
   |HAZ LeeDatosEmpresa;
   #900#13 = #0#4; #900#14 = #0#5;

   |ABRE #4;
   |SI opcion = 1;  |HAZ pantalla;   |FINSI;
   |SI opcion = 2;  |HAZ impresora;  |FINSI;
   |CIERRA #4;

   |DDEFECTO #3;
|FPRC;

||********************************************************************
|PROCESO MultiEmp1;
   nSwSalir = 0;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #2 dirfich0;
   |PATH_DAT #4 dirfich0;
   |PATH_DAT #8 dirfich0;
   |PATH_DAT #15 dirfich0;
   |PATH_DAT #31 dirfich0;
   |PATH_DAT #404 dirfich0;
   |HAZ HazTodo;
   |SI nSwSalir = 1;
       |ERROR10;
   |FINSI;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 000001;
 999999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;
