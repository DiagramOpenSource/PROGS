|FICHEROS;
   cafaccue #0;
   caivalin #1;
   camaniva #2;
   calibros #3;
   cacuenta #4;
   caclipro #5;
   caconimp #8;
   caivaasi #200;

   canempre #30;
   caselem1 #998;
   caselem0 #997;
   cawdatos #900;

   caw00038 #999;
|FIN;

|VARIABLES;
  nDdepar    = 0;
  nHdepar    = 0;
  DInv       = "";
  HInv       = "";
  &CuentaAnt = "";
  &NifAnt    = "";

  swAlgun   = 0;
  Primero   = 0;
  &SwLin    = 0;
  &SwCuenta = 0;
  &aTitCta  = "";
  &aLitCta  = "";
  informe   = "";
  inform1   = "cafaccue";
  inform2   = "cafaccu1";
  inform3   = "cafaccu2";
  inform4   = "cafaccu3";
  alfa1     = "";
  alfa2     = "";
  &enDatCu  = 0;
  aMulDep   = "";
  nOpMdep   = 0;
  nAlSi     = 0;
  nAlNo     = 0;
  aAlfa1    = "";
  nMensa    = 0;

   nEmpMulti  = 0;
   &eSwMul    = 0;
   &espe15    = "";
   &espe16    = 0;
   aFichero   = "";
   nSw        = 0;
   fDFecha    = @;
|FIN;

|INCLUYE dscont_i;
|INCLUYE masivo_i;

|| *****************************************************************
|CALCULO fechad; |TIPO 0;
  |SI #0#19 = "N";
      fDFecha = fec1;
      |SI enSwSelFc = 1; |Y #0#12 ! "N"; fDFecha = "01.01.2000"; |FINSI;
      |SI #0#6 < fDFecha; |O #0#6 > fec2;
          |MENAV "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
          |ERROR;
      |FINSI;
  |FINSI;
|FCAL;

|CALCULO fechah; |TIPO 0;
  |SI #0#19 = "N";
      fDFecha = fec1;
      |SI enSwSelFc = 1; |Y #0#12 ! "N"; fDFecha = "01.01.0000"; |FINSI;
      |SI #0#7 < fDFecha; |O #0#7 > fec2;
          |MENAV "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
          |ERROR;
          |ACABA;
      |FINSI;
   |FINSI;
   |SI #0#7 < #0#6;
       |MENAV "    ERROR FECHA ERRONEA";
       |ERROR;
   |FINSI;
|FCAL;

|CALCULO cuenta; |TIPO 0;
     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;
|FCAL;

|CALCULO defectos; |TIPO 0;
  fDFecha = fec1;
  |SI enSwSelFc = 1;|Y #0#12 ! "N"; fDFecha = "01.01.2000"; |FINSI;

  |SI #0#19 = "N";
      #0#6 = fDFecha;
  |SINO;
      #0#6 = "01.01.2000";
  |FINSI;

  |PINTA #0#6;
  #0#7 = fec2; |PINTA #0#7;
  |SI #0#12 = "R";
      #0#2  = "43.";
      #0#3  = "4499.";
  |FINSI;
  |SI #0#12 = "S";
      #0#2  = "40.";
      #0#3  = "4199.";
  |FINSI;
  |SI #0#12 = "N";
      #0#2  = "465.";
      #0#3  = "46599.";
  |FINSI;
  |PINTA #0#2;
  |PINTA #0#3;
|FCAL;

|PROCESO nodepar; |TIPO 7;
   |CAMPO_MODIFICA #0#13, 1, eaDepar;
   |CAMPO_MODIFICA #0#14, 1, eaDepar;
|FPRC;

|CALCULO nosubde; |TIPO 7;
    |CAMPO_MODIFICA #0#20, 1, eaSubde;
    |CAMPO_MODIFICA #0#21, 1, eaSubde;
|FCAL;

|PROCESO DatosEmp; |TIPO 7;
 |ABRE #8;
 |DDEFECTO #8;
 #8#0 = 1;
 |LEE 000#8=;
 |CIERRA #8;
 #0#15 = #8#7; |PINTA #0#15;
 alfa1 = #8#8; |C_M #0#15,1,alfa1;
|FPRC;

|PROCESO QuePasa; |TIPO 7;
  |SI #0#12 = "N";
      |C_M #0#18,1,"N"; #0#18 = "A"; |PINTA #0#18;
  |SINO;
      |C_M #0#18,1,"S";
  |FINSI;
|FPRC;
|| ******************************************************************

|PROCESO LaCuenta;
 enDatCu = 0;

 |ABRE #5;
 |DDEFECTO #5;
 |SI #0#17 = "N";
     |SELECT #4#5;
     #5#9 = NifAnt;
     |LEE 001#5=;
     |SI FSalida = 0; enDatCu = 1; |FINSI;
     |SELECT #1#5;
 |SINO;
      #5#23 = "C";
      #5#10 = CuentaAnt;
      |LEE 001#5=;
      |SI FSalida ! 0;
          #5#23 = "P";
          #5#10 = CuentaAnt;
          |LEE 001#5=;
          |SI FSalida = 0; enDatCu = 1; |FINSI;
      |SINO;
          enDatCu = 1;
      |FINSI;
 |FINSI;
 |CIERRA #5;
 |SI #0#16 = "N"; |DDEFECTO #5; enDatCu = 0; |FINSI;
 |SI enDatCu = 1;
     alfa1 = #5#3; |QBF alfa1; alfa1 = ("00"  + alfa1) % -102;
     alfa2 = #5#4; |QBF alfa2; alfa2 = ("000" + alfa2) % -103;
     #5#25 = alfa1 + alfa2;
 |SINO;
     #5#10 = CuentaAnt;
     #5#1  = #2#13;
 |FINSI;
 aTitCta = #2#13;

 |SI #0#12 ! "N"; |ACABA; |FINSI;
 |SI #0#17 = "N"; |ACABA; |FINSI;

 |ABRE #4;
 #4#0 = CuentaAnt;
 |LEE 041#4=;
 |SI FSalida = 0;
     aTitCta = #4#2;
 |FINSI;
 |CIERRA #4;
|FPRC;

|PROCESO LinLibro0;
  aAlfa1 = #1#30; |QBF aAlfa1;
  |SI aAlfa1 = "";
      #1#30 = #2#10;
      aAlfa1 = #1#31; |QBF aAlfa1; |SI aAlfa1 = ""; #1#31 = #2#11; |FINSI;
  |FINSI;

  |SI aMulDep = "S";
      |SI Haybasica = 1;
          nDepar  = #1#30; |SI nDepar = 0;  nDepar = 1;   |FINSI;
          nDdepar = #0#13;
          nHdepar = #0#14;
          |SI nDepar < nDdepar; |O nDepar > nHdepar; nAlNo = 1;|ACABA; |FINSI;
     |SINO;
          |SI #1#30 < #0#13;  |O #1#30 > #0#14;  nAlNo = 1; |ACABA;  |FINSI;
     |FINSI;
     |SI #1#31 < #0#20;  |O #1#31 > #0#21;  nAlNo = 1; |ACABA;  |FINSI;
 |FINSI;

 nAlSi = 1;
 |SI nOpMdep = 1;
     #2#19 = #2#19 + #1#6;
     #2#15 = #2#15 + #1#6;
     #2#17 = #2#15 % #2#16;
     #2#20 = #2#20 + #1#22;
     #2#23 = #2#23 + (#1#8 + #1#10);
     #2#26 = #2#26 + #1#11;
     |SI #2#36 = "N";
         #2#21 = #2#19 + #2#23 - #2#26 - #2#20;
     |SINO;
         #2#21 = #2#19 + #2#23 - #2#20;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE LinLibro;
 #1#1;
 ;
 #2#0, #2#1, 01;
 #2#0, #2#1, 99;
 ;
 NULL, LinLibro0;
|FIN;

|PROCESO Facturas;
 |SI #0#18 ! "A";
     |SI #0#18 = "S"; |Y #2#20 = 0; |ACABA; |FINSI;
     |SI #0#18 = "N"; |Y #2#20 ! 0; |ACABA; |FINSI;
 |FINSI;

 |SI aMulDep ! "S";
     |SI Haybasica = 1;
         nDepar  = #2#10; |SI nDepar = 0;  nDepar = 1;   |FINSI;
         nDdepar = #0#13;
         nHdepar = #0#14;
         |SI nDepar < nDdepar; |O nDepar > nHdepar; |ACABA; |FINSI;
     |SINO;
         |SI #2#10 < #0#13;  |O #2#10 > #0#14;  |ACABA;  |FINSI;
     |FINSI;
     |SI #2#11 < #0#20; |O #2#11 > #0#21; |ACABA; |FINSI;
 |SINO;
     nOpMdep = 0;
     nAlSi   = 0;
     nAlNo   = 0;
     |BUCLE LinLibro;
     |SI nAlSi = 0; |ACABA; |FINSI;
     |SI nAlNo = 1;
         nOpMdep = 1;
         #2#19 = 0; #2#15 = 0;
         #2#17 = 0; #2#20 = 0;
         #2#23 = 0; #2#26 = 0;
         #2#21 = 0;
         |BUCLE LinLibro;   ||hace un recalculo de la factura
     |FINSI;
 |FINSI;

 |SI Primero = 0;
     Primero = 1;
     CuentaAnt = #2#12;
     NifAnt    = #2#14;
     SwCuenta = 1;
     |HAZ LaCuenta;
 |FINSI;

 |SI #0#17 = "N";
     |SI NifAnt ! #2#14;
         SwLin = 2; |PRINT;
         CuentaAnt = #2#12;
         NifAnt    = #2#14;
         SwCuenta = 1;
         |HAZ LaCuenta;
     |FINSI;
 |SINO;
     |SI CuentaAnt ! #2#12;
         SwLin = 2; |PRINT;
         NifAnt    = #2#14;
         CuentaAnt = #2#12;
         SwCuenta = 1;
         |HAZ LaCuenta;
     |FINSI;
 |FINSI;

 SwLin = 0;
 |PRINT;
 swAlgun = 1;
 SwCuenta = 0;
|FPRC;

|DEFBUCLE Facturas;
 #2#4;
 2,3,35,36;
 #0#2,#0#6,#0#0,0000000001, #0#9,  #0#4, DInv, #0#12;
 #0#3,#0#7,#0#1,9999999999, #0#10, #0#5, HInv, #0#12;
 ;
 NULL, Facturas;
|FIN;

|DEFBUCLE Facturas_Nif;
 #2#4;
 2,3,35,36;
 #0#2,#0#6,#0#0,0000000001, #0#9,  #0#4, DInv, #0#12;
 #0#3,#0#7,#0#1,9999999999, #0#10, #0#5, HInv, #0#12;
 ;
 NULL, NULL, NULL, NULL, NULL, Facturas;
 #2#14,#2#4,#2#0,#2#1;
|FIN;

|PROCESO Facturas_Auxi;
   dirfich0 = #999#6;
   |QBF dirfich0;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #2 dirfich0;

   |PATH_DAT #3 dirfich0;
   |PATH_DAT #4 dirfich0;
   |PATH_DAT #5 dirfich0;

   |ABRE #2;
   #2#0 = #999#1;
   #2#1 = #999#2;
   |LEE 001#2=;
   |SI FSalida = 0;
       |HAZ Facturas;
   |FINSI;
   |CIERRA #2;
|FPRC;

|DEFBUCLE FrasAuxi;
   #999#2;
   ;
   #0#2,#0#6,#0#0,0000000001;
   #0#3,#0#7,#0#1,9999999999;
   ;
   NULL, Facturas_Auxi;
|FIN;

|DEFBUCLE FrasAuxi_Nif;
   #999#3;
   ;
   "               ", #0#6,#0#0,0000000001;
   "zzzzzzzzzzzzzzz", #0#7,#0#1,9999999999;
   ;
   NULL, Facturas_Auxi;
|FIN;

|PROCESO HazTodo;

   |ABRE #200;
   |DDEFECTO #200;
   |LEE 000#200p;
   |CIERRA #200;

   aMulDep = #200#132;
   |SI Haybasica = 0;  || si selecciona todos los departamentos no es necesario ver lineas
       |SI #0#13 = "  "; |Y #0#14 = "zz";
         |Y #0#20 = "   "; |Y #0#21 = "zzz";
           aMulDep = "N";
       |FINSI;
   |SINO;
       |SI #0#13 [ 1;    |Y #0#14 = 99;
         |Y #0#20 = "   "; |Y #0#21 = "zzz";
           aMulDep = "N";
       |FINSI;
   |FINSI;

   eaIa = #30#21;
   alfa1 = #0#6; alfa1 = alfa1 % 0402; enM1 = alfa1;
   alfa1 = #0#7; alfa1 = alfa1 % 0402; enM2 = alfa1;
   |HAZ LeeDatosEmpresa;
   #900#13 = #0#6; #900#14 = #0#7;
   |SI eSwMul ! 0; #900#12 = #caselem0#4; |FINSI;

   informe = inform1; |SI #0#17 = "N"; informe = inform3; |FINSI;
   |SI #0#12 = "N";
       informe = inform2; |SI #0#17 = "N"; informe = inform4; |FINSI;
   |FINSI;

   |INFOR informe;
   |SI FSalida < 0; |ACABA; |FINSI;

   swAlgun = 0;
   Primero = 0;

   |SI #0#19 = "N";
       |SI #0#17 = "N";
           |BUCLE Facturas_Nif;
       |SINO;
           |BUCLE Facturas;
       |FINSI;
   |SINO;
       |SI #0#17 = "N";
           |BUCLE FrasAuxi_Nif;
       |SINO;
           |BUCLE FrasAuxi;
       |FINSI;
   |FINSI;
   |SI swAlgun = 1;
       SwLin = 2; |PRINT;
       |PIEINF;
   |SINO;
       |SI nMensa = 1;
           |MENAV "    Seleccion vacia";
       |FINSI;
   |FINSI;
   |FININF;
|FPRC;

|| ************************************************
|CALCULO ElTipo3; |TIPO 3;

  aFichero = "8w" + Usuario;
  |NOME_DAT #999 aFichero;
  |DELINDEX #999;

  |HAZ DirAplicSt;
  |SI Haybasica = 1; |Y #0#14 = "zz"; #0#14 = "99"; |FINSI;

  |SI #0#11 = "S";
      DInv = "A"; HInv = "I";
  |SINO;
      |SI #0#11 = "N";
          DInv = "N"; HInv = "N";
      |SINO;
          DInv = "A"; HInv = "N";
      |FINSI;
  |FINSI;
  |SI #0#12 = "R"; aLitCta  = "Cliente ..";|FINSI;
  |SI #0#12 = "S"; aLitCta  = "Proveedor.";|FINSI;
  |SI #0#12 = "N"; aLitCta  = "Trabajador";|FINSI;

  |SI enSwMvo = 0;
      |IMPRE 0;
      |SI FSalida < 0; |ACABA; |FINSI;
  |FINSI;

  enDatos = 0; |SI #0#15 = "S"; enDatos = 1; |FINSI;
  eSwMul = 0;
  |SI #0#19 = "N";
      |SI #48#27 = "S"; |Y enSwMvo = 0;
          |HAZ MultiEmpresa;
      |SINO;
          |DFICE dirfich0; |QBF dirfich0;
          nMensa = nMensa + 1;
          |HAZ HazTodo;
      |FINSI;
  |SINO;
       eSwMul = 1;
       |SI #48#27 = "S";  eSwMul = 2; |FINSI;
       |HAZ MultiEjercicio;
  |FINSI;
  |SI enSwMvo = 0;  |FINIMP; |FINSI;
  |DELINDEX #999;
|FCAL;

|| ------------------------------------------------------------------------
|| ////////////////////////////////////
|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #4 dirfich0;
   |PATH_DAT #1 dirfich0;
   |PATH_DAT #2 dirfich0;
   |PATH_DAT #3 dirfich0;
   |PATH_DAT #5 dirfich0;
   |PATH_DAT #8 dirfich0;
   |PATH_DAT #200 dirfich0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
   #caselem1#1;
   ;
   000001;
   999999;
   ;
   NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
   |SUB_EJECUTA "caselemp";
   |NOME_DAT #998 eaFichsel;
   |BUCLE MultiEmp0;
   |DELINDEX #998;
|FPRC;

|PROCESO PasoAuxiliar;
 |SI #0#18 ! "A";
     |SI #0#18 = "S"; |Y #2#20 = 0; |ACABA; |FINSI;
     |SI #0#18 = "N"; |Y #2#20 ! 0; |ACABA; |FINSI;
 |FINSI;

 |SI aMulDep ! "S";
     |SI Haybasica = 1;
         nDepar  = #2#10; |SI nDepar = 0;  nDepar = 1;   |FINSI;
         nDdepar = #0#13;
         nHdepar = #0#14;
         |SI nDepar < nDdepar; |O nDepar > nHdepar; |ACABA; |FINSI;
     |SINO;
         |SI #2#10 < #0#13;  |O #2#10 > #0#14;  |ACABA;  |FINSI;
     |FINSI;
     |SI #2#11 < #0#20;  |O #2#11 > #0#21;  |ACABA;  |FINSI;
 |SINO;
     nOpMdep = 0;
     nAlSi   = 0;
     nAlNo   = 0;
     |BUCLE LinLibro;
     |SI nAlSi = 0; |ACABA; |FINSI;
 |FINSI;

 #999#0 = #998#2;   || Periodo Contable
 #999#1 = #2#0;     || Libro
 #999#2 = #2#1;     || Registro
 #999#3 = #2#12;    || Cuenta Contable
 #999#4 = #2#14;    || Nif
 #999#5 = #2#4;     || Fecha Factura
 #999#6 = #998#3;   || Path
 #999#7 = #2#36;    || R/S
 |GRABA 000#999n;
 nSw = 1;
|FPRC;

|DEFBUCLE PasoAuxiliar;
 #2#4;
 2,3,35,36;
 #0#2,#0#6,#0#0,0000000001, #0#9,  #0#4, DInv, #0#12;
 #0#3,#0#7,#0#1,9999999999, #0#10, #0#5, HInv, #0#12;
 ;
 NULL, PasoAuxiliar;
|FIN;

|PROCESO MultiEmpEjer1;
   dirfich0 = #caselem1#3; |QBF dirfich0;

   |SI enSwSelFc = 0;
       |SI #0#6 > #caselem1#5; |O #0#7 < #caselem1#4;
           |ACABA;  || Fuera de la seleccion
       |FINSI;
   |FINSI;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #2 dirfich0;
   |PATH_DAT #200 dirfich0;

   |ABRE #200;
   |DDEFECTO #200;
   |LEE 000#200p;
   |CIERRA #200;

   aMulDep = #200#132;
   |SI Haybasica = 0;  || si selecciona todos los departamentos no es necesario ver lineas
       |SI #0#13 = "  "; |Y #0#14 = "zz";
          |Y #0#20 = "   "; |Y #0#21 = "zzz";
           aMulDep = "N";
       |FINSI;
   |SINO;
       |SI #0#13 [ 1;    |Y #0#14 = 99;
          |Y #0#20 = "   "; |Y #0#21 = "zzz";
           aMulDep = "N";
       |FINSI;
   |FINSI;

   |BUCLE PasoAuxiliar;
|FPRC;

|DEFBUCLE MultiEmpEjer;
  #caselem1#2;
  ;
  #997#0, "01.01.0000";
  #997#0, "31.12.2999";
  ;
  NULL,MultiEmpEjer1;
|FIN;

|PROCESO MultiEjer1;
   nSw = 0;
   |DELINDEX #999;
   |ABRE #999;
   |BUCLE MultiEmpEjer;
   |CIERRA #999;

   |SI nSw ! 0;
       |ABRE #998;
       |SELECT #3#998;
       #998#1 = #997#0;
       #998#2 = #997#5;
       |LEE 001#998=;
       |SI FSalida = 0;
           |HAZ MultiEmp1;
       |FINSI;
       |SELECT #1#998;
       |CIERRA #998;
   |FINSI;
|FPRC;

|DEFBUCLE MultiEjer0;
   #caselem0#1;
   ;
   00001;
   99999;
   ;
   NULL,MultiEjer1;
|FIN;

|PROCESO MultiEjercicio;
   espe15 = "S";

   |SUB_EJECUTA "caselemp";
   |NOME_DAT #998 eaFichsel;
   |NOME_DAT #997 eaFichres;

   nEmpMulti = -1;
   |BUCLE MultiEjer0;
   |DELINDEX #998;
   |DELINDEX #997;
|FPRC;

|PROGRAMA;
  |HAZ inicio;
  |HAZ eParaModelos;

 |SI enSwMvo = 1;
     nMensa = -1;
     |DDEFECTO #0;
     #0#12 = "R"; |HAZ ElTipo3;
     #0#12 = "S"; |HAZ ElTipo3;
     #0#12 = "N"; |HAZ ElTipo3;
 |SINO;
     |MANTE #0;
 |FINSI;
|FPRO;
