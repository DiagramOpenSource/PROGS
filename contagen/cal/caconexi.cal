|FICHEROS;
   caconexi #0;
   caconcil #1;
   camaasil #2;
   caconex1 #3;
   caconcic #4;
   camaasic #5;
   caclipro #15;
   cacuenta #10;
   espejo@  #11;

   cabaexp4 #98;
   caw00039 #99;

   canempre #30;
   caselem1 #998;
|FIN;

|VARIABLES;
    &entrada = 1;
    &ext1     = "";
    &ext2     = "";
    &exf0     = "";
    &exf1     = @;
    &exf2     = @;
    sw       = 0;
    nCampo   = 0;
    aDCuenta = "";
    aHCuenta = "";
    aCuenta  = "";
    nDigito  = 0;

    p_alfa1  = "";
    p_error  = 0;

    SwPrint  = 0;
    informe  = "";
    inform1  = "caconexi";
    inform2  = "caconexp";
    &direc   = "";
    &codpos  = "";
    &pobla   = "";
    &provi   = "";
    &cif     = "";
    &cifT    = "";
    &telef   = "";
    &nSwDat  = 0;
    swCliPro = 0;
    varalf   = "";
    aDef     = "";
    aMas     = "";

    alfa1    = "";
    alfa2    = "";
    nEjerCam = 0;
    nEjer    = 0;
    nPeriodo = 0;
    aFichero = "";
    nCambio  = 0;
    nHay     = 0;
    nEstado  = 0;

    CodigoEmpresa  = 0;
    PeriodoEmpresa = 0;
    dirfichOri     = "";
    nSitua         = 0;
    &nSwLinea      = 0;

    nDDiario       = 0;
    nHDiario       = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE dsco08_i;

|PROCESO Parrilla;
     |SI #0#3 = "N";
         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";  #0nCampo = "  ";  |PINTA #0nCampo;
         |SIGUE;
         |C_M #0#0, 1, "S";
         |C_M #0#1, 1, "S";
     |SINO;
         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
         |C_M #0#0, 1, "N";  #0#0 = "            ";  |PINTA #0#0;
         |C_M #0#1, 1, "N";  #0#1 = "zzzzzzzzzzzz";  |PINTA #0#1;
     |FINSI;
|FPRC;

|PROCESO NoPasa;
     |SI #0Campo = "            ";
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = "  ";  |C_M #0nCampo, 1, "N";  |PINTA #0nCampo;
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO cuenta; |TIPO 0;
     p_alfa1 = #0Campo;
     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;
|FPRC;

|CALCULO tipo7C3; |TIPO 7;
   |HAZ defectos;
|FCAL;

|CALCULO defectos; |TIPO 0;
  |SI #0#28 = "N";
      #0#26  = fec1;
  |SINO;
      #0#26 = "01.01.2000";
  |FINSI;
  |PINTA #0#26;
  #0#27 = fec2; |PINTA #0#27;
|FCAL;

|CALCULO fechas; |TIPO 0;
  |SI #0#28 = "N";
      |SI #0Campo > fec2;|O #0Campo < fec1;
          |MENAV "    Fecha fuera de Periodo ...";
          |ERROR;
      |FINSI;
  |FINSI;
|FCAL;
|| *************************************************************************
||                       PROCESOS DEL EXTRACTO POR PANTALLA
|| *************************************************************************

|PROCESO diferenp;
     sw = 1;
     |SI #1#3  = 1;
         #3#6  = #1#2;
         #3#7  = #1#6;
         #3#8  = #1#7;
         #3#9  = #1#4;
         #3#10 = #1#5;
         #3#11 = 0;
         #3#12 = 0;
         #3#13 = 0;
     |SINO;
         #3#6  = #1#2;
         #3#7  = 0;
         #3#8  = 0;
         #3#9  = #1#4;
         #3#10 = #1#5;
         #3#11 = 0;
         #3#12 = #1#6;
         #3#13 = #1#7;
     |FINSI;

     #3#14 = #3#14 + #3#7;
     #3#15 = #3#15 + #3#8;
     #3#16 = #3#16 + #3#12;
     #3#17 = #3#17 + #3#13;

     |PRINT;
     nSwLinea = 1;
|FPRC;

|DEFBUCLE caconcilp;
     #1#1;
     8;
     #10#0, 0001, " ";
     #10#0, 9999, " ";
     e;
     NULL, diferenp;
|FIN;

|PROCESO ContraPart;
     |ABRE #11;
     #11#0 = aCuenta;
     #11#1 = nDigito;
     |LEE 040#11=;
     |SI FSalida ! 0;  |DDEFECTO #11;  |FINSI;

     #3#26 = #11#2;
     |CIERRA #11;
|FPRC;

|PROCESO asientosp;
     #5#0 = #2#0;
     #5#1 = #2#1;
     #5#2 = #2#2;
     |LEE 040#5=;
     |SI FSalida ! 0;  |DDEFECTO #5;  |FINSI;

     sw = 1;
     #3#6  = #2#3;
     #3#7  = 0;
     #3#8  = 0;
     #3#9  = #2#1;
     #3#10 = #2#7;
     #3#11 = #2#2;
     |SI #2#8 = "D";
         #3#12 = #2#9;
         #3#13 = 0;
         aCuenta = #5#10;
         nDigito = #5#11;
         |HAZ ContraPart;
     |SINO;
         #3#13 = #2#9;
         #3#12 = 0;
         aCuenta = #5#8;
         nDigito = #5#9;
         |HAZ ContraPart;
     |FINSI;
     #3#14 = #3#14 + #3#7;
     #3#15 = #3#15 + #3#8;
     #3#16 = #3#16 + #3#12;
     #3#17 = #3#17 + #3#13;

     |PRINT;
     nSwLinea = 1;
|FPRC;

|DEFBUCLE camaasilp;
     #2#3;
     18, 0;
     #10#0, #0#26, 000000, 0001, " ", nDDiario;
     #10#0, #0#27, 999999, 9999, " ", nHDiario;
     e;
     NULL, asientosp;
|FIN;

|PROCESO LeoAuxW039p;
     sw = 1;
     #3#6  = #99#3;
     #3#7  = 0;
     #3#8  = 0;
     #3#9  = #99#1;
     #3#10 = #99#10;
     #3#11 = #99#2;
     |SI #99#8 = "D";
         #3#12 = #99#9;
         #3#13 = 0;
     |SINO;
         #3#13 = #99#9;
         #3#12 = 0;
     |FINSI;
     #3#26 = #99#11;
     #3#14 = #3#14 + #3#7;
     #3#15 = #3#15 + #3#8;
     #3#16 = #3#16 + #3#12;
     #3#17 = #3#17 + #3#13;

     |PRINT;
     nSwLinea = 1;
|FPRC;

|DEFBUCLE LeoAuxW039;
     #99#2;
     ;
     #10#0, #0#26, 000000, 0001;
     #10#0, #0#27, 999999, 9999;
     e;
     NULL, LeoAuxW039p;
|FIN;

|PROCESO impresora;
     sw    = 0;

     #3#0 = #10#0;
     #3#1 = #10#2;
     #3#2 = #0#2;

     |BUCLE caconcilp;
     |SI #0#28 = "N";
         |BUCLE camaasilp;
     |SINO;
         |BUCLE LeoAuxW039;
     |FINSI;

     |SI sw = 1;
         #3#18 = #3#3;
         #3#19 = #3#5;
         #3#20 = #3#14;
         #3#21 = #3#16;
         #3#22 = #3#15;
         #3#23 = #3#17;

         #3#24 = #3#18 - #3#20 + #3#22;
         #3#25 = #3#19 - #3#21 + #3#23;
         |SI #0#29 = "S";
             |PIEINF;
         |SINO;
             nSwLinea = 2;
             |PRINT;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO saldo;
     |SI #2#8 = "D";
         #3#5 = #3#5 + #2#9;
     |SINO;
         #3#5 = #3#5 - #2#9;
     |FINSI;
|FPRC;

|DEFBUCLE camaasil;
     #2#3;
     0;
     #10#0, #0#26, 000000, 0001, nDDiario;
     #10#0, #0#27, 999999, 9999, nHDiario;
     e;
     NULL, saldo;
|FIN;

|PROCESO cabdife;
     direc  = "";
     codpos = "";
     pobla  = "";
     provi  = "";
     cif    = "";
     cifT   = "";
     telef  = "";
     nSwLinea = 0;
     nSwDat   = 0;
     swCliPro = 0;
     |ABRE #15;
     |DDEFECTO #15;
     #15#23 = "C";
     #15#10 = #10#0;
     |LEE 040#15=;
     |SI FSalida = 0;
         swCliPro = 1;
     |SINO;
         #15#23 = "P";
         #15#10 = #10#0;
         |LEE 040#15=;
         |SI FSalida = 0;
             swCliPro = 1;
        |FINSI;
     |FINSI;
     |CIERRA #15;
     |SI swCliPro = 1;
         direc  = #caclipro#2;
         varalf = #caclipro#3;
         varalf = "0" + varalf;
         varalf = varalf % -102;
         codpos = varalf;
         varalf = #caclipro#4;
         varalf = "00" + varalf;
         varalf = varalf % -103;
         codpos = codpos + varalf;
         |SI codpos = "00000"; codpos = ""; |FINSI;
         pobla = #caclipro#6;
         provi = #caclipro#5;
         cif   = #caclipro#9;
         telef = #caclipro#7;
    |FINSI;
    |QBF cif; |SI cif ! ""; cifT= "NIF"; |FINSI;
    |QBF direc;
    |QBF codpos;
    |QBF pobla;
    |QBF provi;
    |QBF telef;
    |SI direc ! ""; |O codpos ! ""; |O pobla ! "";
        |O provi ! ""; |O telef ! "";
           nSwDat = 1;
    |FINSI;

     #4#0 = #10#0;
     #4#1 = #10#1;
     |LEE 000#4=;
     |SI FSalida ! 0;
         |DDEFECTO #4;
         #4#0 = #10#0;
         #4#1 = #10#1;
     |FINSI;

     |DDEFECTO #3;
         #3#4 = #4#5;
         #3#3 = #4#4;
     |SI #3#4 = "H"; #3#3 = 0 - #3#3; |FINSI;

     |SI #0#28 = "N";
         |BUCLE camaasil;
     |SINO;
          #3#5 = #98#4;
     |FINSI;

     |SI #0#25 = "N";
        |SI #3#5 ! 0; |HAZ impresora; |FINSI;
     |SINO;
        |HAZ impresora;
     |FINSI;
|FPRC;

|DEFBUCLE caconcic;
     #10#1;
     3;
     aDCuenta, "S";
     aHCuenta, "S"
     e;
     NULL, cabdife;
|FIN;

|PROCESO LeeAuxCta;
     |DDEFECTO #10;
     #10#0 = #98#0;
     |LEE 001#10=;
     |SI FSalida ! 0;
         #10#0 = #98#0;
         #10#2 = #98#2;
     |FINSI;
     |HAZ cabdife;
|FPRC;

|DEFBUCLE LeeAuxCta;
     #98#1;
     ;
     "            ";
     "zzzzzzzzzzzz";
     e;
     NULL, LeeAuxCta;
|FIN;

|| *************************************************************************
|PROCESO HazTodo;

     |ABRE #4;
     |ABRE #5;
     |SI #0#28 = "N";
         |SI #0#3 = "N";
             aDCuenta = #0#0;
             aHCuenta = #0#1;
             |BUCLE caconcic;
         |SINO;
             |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
                   |SI #0nCampo ! "            ";
                       aDCuenta = #0nCampo;
                       aHCuenta = #0nCampo;
                       |BUCLE caconcic;
                   |FINSI;
             |SIGUE;
         |FINSI;
     |SINO;
        |ABRE #10;
        |BUCLE LeeAuxCta;
        |CIERRA #10;
     |FINSI;
     |CIERRA #4;
     |CIERRA #5;
|FPRC;

|PROCESO impre;|TIPO 3;

     nDDiario = 00;
     nHDiario = 98;
     |SI #0#28 = "S"; nDDiario = 01; nHDiario = 98; |FINSI;

      |DBASE aMas;
      aDef = aMas + "def/cacuenta";
      |CARGA_DEF aDef, espejo@;
      |SI FSalida ! 0;
          |MENAV "    Error cargando cacuenta ... ";
          |ACABA;
      |FINSI;

     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI #0#29 = "N";
         inform1 = "caconexy";
         inform2 = "caconexq";
     |FINSI;

     informe = inform1;
     |SI #0#24 = "S";
         informe = inform2;
     |FINSI;
     |INFOR informe;
     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

     nSitua = 0;
     |SI #0#28 = "N";
         |SI #48#27 = "S"; |Y ext1 = "";
             |HAZ MultiEmpresa;
         |SINO;
             |DFICE dirfich0; |QBF dirfich0;
             |HAZ HazTodo;
         |FINSI;
     |SINO;
         |HAZ MultiEjercicio;
     |FINSI;

     |SI #0#29 = "N";
         |PIEINF;
     |FINSI;

     |FININF;
     |FINIMP;

     |DESCARGA_DEF #11;
|FPRC;

|| ***************************************************************
|PROCESO MultiEmp1;
   nSitua = 1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #2 dirfich0;
   |PATH_DAT #4 dirfich0;
   |PATH_DAT #5 dirfich0;
   |PATH_DAT #10 dirfich0;
   |PATH_DAT #11 dirfich0;
   |PATH_DAT #15 dirfich0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 000001;
 999999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;

|| //////////////////////////////////
|PROCESO PasoAuxW039;
   |SI #2#8 = "D";
       #98#4 = #98#4 + #2#9;
   |SINO;
       #98#4 = #98#4 - #2#9;
   |FINSI;
   nHay = 1;

   |SI #2#18 = " ";
       #99#0 = #2#0;
       #99#1 = #2#1;
       #99#2 = #2#2;
       #99#3 = #2#3;
       #99#4 = eaCuenta;
       #99#5 = nPeriodo;
       #99#6 = dirfich0;
       #99#7 = enEjercicio;
       #99#8 = #2#8;
       #99#9 = #2#9;
       #99#10 = #2#7;
       |GRABA 020#99n;
   |FINSI;
|FPRC;

|DEFBUCLE PasoAuxW039;
     #2#3;
     0;
     #10#0, #0#26, 000000, 0001, nDDiario;
     #10#0, #0#27, 999999, 9999, nHDiario;
     e;
     NULL, PasoAuxW039;
|FIN;

|PROCESO PasoAuxiliar;
     eaCuenta = #10#0;
     |SI #0#3 = "N";
         |SI nCambio ! 0;
             eaCta08 = eaCuenta;
             eaCta90 = eaCuenta;
             enBEjer = enEjercicio;
             |HAZ eCam_Segundo;
             |SI nEjer ] 2008;
                 eaCuenta = eaCta08;
             |SINO;
                 eaCuenta = eaCta90;
             |FINSI;
         |FINSI;
         |SI eaCuenta < #0#0; |O eaCuenta > #0#1;
             |ACABA; ||no entra en la seleccion
         |FINSI;
     |FINSI;

     nEstado = 0;
     #98#0 = eaCuenta;
     #98#1 = #10#1;
     #98#2 = #10#2;
     #98#3 = #10#5;
     |LEE 041#98=;
     |SI FSalida ! 0;
         |DDEFECTO #98;
         #98#0 = eaCuenta;
         #98#1 = #10#1;
         #98#2 = #10#2;
         #98#3 = #10#5;
         nEstado = 1;
     |FINSI;

     nHay = 0;
     |BUCLE PasoAuxW039;
     |SI nHay = 1;
         |SI nEstado = 1; |GRABA 040#98n; |FINSI;
         |SI nEstado = 0; |GRABA 040#98a; |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE PasoAuxiliar;
     #10#1;
     3;
     aDCuenta, "S";
     aHCuenta, "S";
     ;
     NULL, PasoAuxiliar;
|FIN;

|PROCESO HazAuxiliar;
    nCambio = 0;
    |SI enEjercicio < 2008; |Y nEjer ] 2008; nCambio = 1; |FINSI;
    |SI enEjercicio ] 2008; |Y nEjer < 2008; nCambio = 1; |FINSI;

    |ABRE #4;
    |ABRE #5;
    |SI #0#3 = "N";
         aDCuenta = #0#0;
         aHCuenta = #0#1;
         |SI nCambio ! 0; aDCuenta = ""; aHCuenta = "zzzzzzzzzzzzz"; |FINSI;
         |BUCLE PasoAuxiliar;
     |SINO;
         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               eaCuenta = #0nCampo;
               |SI eaCuenta ! "            ";
                   aDCuenta = eaCuenta;
                   aHCuenta = eaCuenta;
                   |SI nCambio ! 0;
                       eaCta08 = eaCuenta;
                       eaCta90 = eaCuenta;
                       enBEjer = enEjercicio;
                       |HAZ eCam_Segundo;
                       |SI enEjercicio < 2008;  || para buscar en el bucle
                           aDCuenta = eaCta90;
                           aHCuenta = eaCta90;
                       |SINO;
                           aDCuenta = eaCta08;
                           aHCuenta = eaCta08;
                       |FINSI;
                    |FINSI;
                    |BUCLE PasoAuxiliar;
                |FINSI;
          |SIGUE;
      |FINSI;
      |CIERRA #4;
      |CIERRA #5;
|FPRC;

|PROCESO MultiEmpM1;

    |HAZ CalFechas;
    |SI #0#26 > fec2; |O #0#27 < fec1;
        |ACABA;  || Fuera de la seleccion
    |FINSI;

    nSitua = 1;
    |DBASE dir_fich; |QBF dir_fich;
    dir_fich = dir_fich + "fich/";
    alfa1 = #30#2; |QBF alfa1;
    alfa2 = #30#3; |QBF alfa2;
    alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
    alfa2 = "0"     + alfa2;   alfa2 = alfa2 % -101;
    dirfich0 = dir_fich + alfa1 + alfa2 + "/";
    |QBF dirfich0;
    nPeriodo = #30#3;

    |PATH_DAT #1 dirfich0;
    |PATH_DAT #2 dirfich0;
    |PATH_DAT #4 dirfich0;
    |PATH_DAT #5 dirfich0;
    |PATH_DAT #10 dirfich0;
    |PATH_DAT #11 dirfich0;
    |PATH_DAT #15 dirfich0;
    |HAZ HazAuxiliar;
|FPRC;

|DEFBUCLE MultiEmpM0;
    #30#1;
    ;
    enEmpresa, 0;
    enEmpresa, 9;
    ;
    NULL,MultiEmpM1;
|FIN;

|PROCESO MultiEjercicio;
   nEjer = enEjercicio;

   aFichero = "0w" + Usuario; |NOME_DAT #99 aFichero;
   aFichero = "1w" + Usuario; |NOME_DAT #98 aFichero;

   |DELINDEX #98;
   |DELINDEX #99;

   |ABRE #99;
   |ABRE #98;
   |BUCLE MultiEmpM0;
   |CIERRA #98;
   |CIERRA #99;

   #caselem1#1 = CodigoEmpresa;
   #caselem1#2 = PeriodoEmpresa;
   #caselem1#3 = dirfichOri;
   |HAZ MultiEmp1;
|FPRC;

|PROGRAMA;

   |HAZ inicio;
   CodigoEmpresa    = enEmpresa;
   PeriodoEmpresa   = enPeriodo;
   |DFICE dirfichOri;
   dirfich0 = dirfichOri;
   |HAZ eCam_Primero;

   |SI ext1 ! "";
      |DDEFECTO #0;
      #0#3 = "S";
      #0#4 = ext1;
      #0#28 = exf0;
      #0#26 = exf1;
      #0#27 = exf2;
      |HAZ impre;
  |SINO;
      |MANTE #0;
  |FINSI;

|FPRO;

|| *******************************************************************
|PROCESO CalFechas;
   |SI #30#26 < 80;
       enEjercicio = #30#26 + 2000;
   |SINO;
       enEjercicio = #30#26 + 1900;
   |FINSI;

   cod2 = #30#26;
   tra1 = 100 + cod2;
   clave2 = tra1 % -102;
   tra1 = 100 + #30#11;
   mes1 = tra1 % -102;
   |SI cod2 < 80;
      work1 = "01." + mes1 + ".20" + clave2;
   |SINO;
      work1 = "01." + mes1 + ".19" + clave2;
   |FINSI;
   fec1 = work1;
   tra1 = 100 + #30#12;
   mes1 = tra1 % -102;
   |SI #30#11 > #30#12;
      cod2 = cod2 + 1;
      |SI cod2 > 99; cod2 = cod2 - 100;    |FINSI;
   |FINSI;
   dia1 = "31";
   |SI #30#12 = 4; |O #30#12 = 6; |O #30#12 = 9; |O #30#12 = 11;
      dia1 = "30";
   |FINSI;
   |SI #30#12 = 2;
      dia1 = "28";
      |SI cod2 = 88; |O cod2 = 92; |O cod2 = 96; |O cod2 = 0; |O cod2 = 4; |O cod2 = 8;
          |O cod2 = 12; |O cod2 = 16; |O cod2 = 20; |O cod2 = 24;
         dia1 = "29";
      |FINSI;
   |FINSI;
   tra1 = 100 + cod2;
   clave2 = tra1 % -102;
   |SI cod2 < 80;
      work1 = dia1 + "." + mes1 + ".20" + clave2;
   |SINO;
      work1 = dia1 + "." + mes1 + ".19" + clave2;
   |FINSI;
   fec2 = work1;
|FPRC;
|| *******************************************************************
