|FICHEROS;
   caextmz1 #0;

   caextmw2 #2,MANTE;
   caextmw3 #3;

   caextm01 #11;
   caextm02 #12;
|FIN;

|VARIABLES;
   nNumero = 0;
   nSw     = 0;
   aParam  = "";
   aQueQuiero = "";
   aNumCampos = "";
   nNumCampoC = 0;
   nNumCampoL = 0;
   nCampo     = 0;
   nNume1     = 0;
   aFichero   = "";

   nCodigo    = 0;
   nEstado    = 0;
   nTipoEx    = 0;
|FIN;

|INCLUYE dscont_i;

|PROCESO MiroUltimo;
  nNumero = 0;
  #11#13 = enEmpresa;
  #11#0  = 9999;
  |LEE 041#11];
  |SI FSalida = 0;
      |LEE 041#11a;
  |SINO;
      |LEE 041#11u;
  |FINSI;
  |SI FSalida = 0; |Y #11#13 = enEmpresa;
      nNumero = #11#0;
  |FINSI;
  nCodigo = nNumero + 1;
|FPRC;

|PROCESO RecogeTodasLineas;
  |PARA nCampo = 0; |SI nCampo [ nNumCampoL; |HACIENDO nCampo = nCampo + 1;
        #12nCampo = #3nCampo;
  |SIGUE;
  #12#0 = nCodigo;
  #12#3 = enEmpresa;
  |GRABA 020#12n
|FPRC;

|DEFBUCLE RecogeTodasLineas;
   #3#1;
   ;
   #2#0, 0001;
   #2#0, 9999;
   e;
   NULL, RecogeTodasLineas;
|FIN;

|PROCESO BlancoTexto;
   |BORRA 020#12a;
   |LIBERA #12;
|FPRC;

|DEFBUCLE BlancoTexto;
   #12#1;
   ;
   #11#13, #11#0, 0001;
   #11#13, #11#0, 9999;
   be;
   NULL, BlancoTexto;
|FIN;

|PROCESO RecogeAnotaciones;

   |SI #2#13 = 0;
       |HAZ MiroUltimo;
   |SINO;
       nCodigo = #2#13;
   |FINSI;

   |DDEFECTO #11;
   #11#13 = enEmpresa;
   #11#0  = nCodigo;
   |LEE 141#11=;
   nEstado = FSalida;
   |SI nEstado = 0;
       |BUCLE BlancoTexto;
   |FINSI;

   #11#13 = enEmpresa;
   #11#0  = nCodigo;
   #11#1  = #2#1;
   #11#2  = #2#2;
   |SI #11#2 = 2;
       #11#6  = #0#3;
       #11#7  = #0#4;
   |FINSI;
   |SI #11#2 = 3;
       #11#3  = #0#0;
       #11#4  = #0#1;
       #11#5  = #0#2;
       #11#10 = #0#5;
       #11#11 = #0#6;
   |FINSI;
   #11#8 =  #2#8;
   #11#9  = #2#9;
   #11#12 = #2#12;
   |SI nEstado ! 0; |GRABA 041#11n; |FINSI;
   |SI nEstado = 0; |GRABA 041#11a; |FINSI;

   |ABRE #12;
   |BUCLE RecogeTodasLineas;
   |CIERRA #12;
   |LIBERA #11;
|FPRC;

|DEFBUCLE RecogeAnotaciones;
   #2#1;
   ;
   0001;
   9999;
   e;
   NULL, RecogeAnotaciones;
|FIN;

|| //////////////////////////////////////////////////////////////////////
|PROCESO PasarTodasLineas;

  |PARA nCampo = 0; |SI nCampo [ nNumCampoL; |HACIENDO nCampo = nCampo + 1;
        #3nCampo = #12nCampo;
  |SIGUE;
  #3#0 = nNumero;
  |GRABA 020#3n
|FPRC;

|DEFBUCLE PasarTodasLineas;
   #12#1;
   ;
   #11#13, #11#0, 0001;
   #11#13, #11#0, 9999;
   e;
   NULL, PasarTodasLineas;
|FIN;

|PROCESO PasarTodas;

   nSw = 0;
   |SI #11#2 = 1;  nSw = 1;  |FINSI;
   |SI #11#2 = 2;
       |SI #11#6 = #0#3;  nSw = 1;  |FINSI;
   |FINSI;
   |SI #11#2 = 3;
       |SI #11#3 = #0#0; |Y #11#4 = #0#1; |Y #11#5 = #0#2; |Y #11#12 = #0#7;
           nSw = 1;
       |FINSI;
   |FINSI;

   |SI nSw = 1;

       nNumero = nNumero + 1;
       |PARA nCampo = 0; |SI nCampo [ 12; |HACIENDO nCampo = nCampo + 1;
             #2nCampo = #11nCampo;
       |SIGUE;
       #2#0  = nNumero;
       #2#13 = #11#0;

       |BUCLE PasarTodasLineas;
       |GRABA 041#2n;
   |FINSI;
   |LIBERA #11;
|FPRC;

|DEFBUCLE PasarTodas1;
   #11#1;
   2;
   enEmpresa, 0001, nTipoEx;
   enEmpresa, 9999, nTipoEx;
   e;
   NULL, PasarTodas;
|FIN;

|DEFBUCLE PasarTodas2;
   #11#3;
   ;
   enEmpresa, nTipoEx, #0#3, "01.01.0000", 0001;
   enEmpresa, nTipoEx, #0#3, "31.12.2099", 9999;
   e;
   NULL, PasarTodas;
|FIN;

|DEFBUCLE PasarTodas3;
   #11#2;
   ;
   enEmpresa, nTipoEx, #0#1, #0#0, #0#2, "01.01.0000", 0001;
   enEmpresa, nTipoEx, #0#1, #0#0, #0#2, "31.12.2099", 9999;
   e;
   NULL, PasarTodas;
|FIN;

|PROCESO ClaveAlta;
   #2#0 = 0001;
||   |SI FSalida = "POSICION"; |Y nNumero ! 0;
||       #2#0 = nNumero;
||   |FINSI;
|FPRC;

|PROCESO ClaveBaja;
   #2#0 = 9999;
|FPRC;

|PROGRAMA;
  |HAZ eVerNuevaAnotacion;

  aParam = PARAMETRO; |QBF aParam;

  aQueQuiero = "|NC";
  aNumCampos = #caextm02#aQueQuiero#;
  nNumCampoL = aNumCampos;
  nNumCampoL = nNumCampoL - 1;
  aNumCampos = #caextm01#aQueQuiero#;
  nNumCampoC = aNumCampos;
  nNumCampoC = nNumCampoC - 1;

  aFichero = ("2k" + Usuario) % -108;
  |NOME_DAT #2 aFichero;
  |DELINDEX #2;

  aFichero = ("3k" + Usuario) % -108;
  |NOME_DAT #3 aFichero;
  |DELINDEX #3;

  #0#0 = enDiarioEx;
  #0#1 = efFechaEx;
  #0#2 = enDocumEx;
  #0#3 = eaLaCtaEx;

  #0#4 = eaCtaDeEx;
  #0#5 = enConNuEx;
  #0#6 = eaConDeEx;
  #0#7 = enPerEx;

  nNumero = 0;
  |ABRE #2;
  |ABRE #3;
  |SI enTipoEx = 0; |O enTipoEx = 1; nTipoEx = 1; |BUCLE PasarTodas1; |FINSI;
  |SI enTipoEx = 0; |O enTipoEx = 2; nTipoEx = 2; |BUCLE PasarTodas2; |FINSI;
  |SI enTipoEx = 0; |O enTipoEx = 3; nTipoEx = 3; |BUCLE PasarTodas3; |FINSI;
  |CIERRA #3;
  |CIERRA #2;

  |BLIN 4;
  |PINPA #0#0;
  |PINDA #0#0;

  |ENTLINEAL #1#2, -1, 1, 21, 0, ClaveAlta, ClaveBaja;

  |ABRE #11;
  |BUCLE RecogeAnotaciones;
  |CIERRA #11;

  |DELINDEX #3;
  |DELINDEX #2;
|FPRO;
