|FICHEROS;
   carecexi #0;

   cacuenta #1;
   cacuedep #11;

   caexistc #6;
   caexistl #7;

   canempre #30;
   caselem1 #998;
|FIN;

|VARIABLES;
   &entrada = 1;
   aAlfa1 = "";
   aAlfa2 = "";
   nNume1 = 0;
   nNume2 = 0;
   nNume3 = 0;
   nPara1 = 0;
   &r_emp= 0;
   &r_per= 0;
   &r_eje= 0;
   &r_nom="";

   aElDepar = "";
   &eSwSelec  = 2;
   &eaDDepar  = "";
   &eaHDepar  = "";
   &efDFecha  = @;
   &efHFecha  = @;
   &eaNomFDep = "";  ||Nombre del Fichero Creado
   nImpor = 0;
|FIN;

|INCLUYE dscont_i;

|PROCESO estruct; |TIPO 0;
   |ABRE #6;
   #6#0 = #0#2;
   |LEE 001#6=;
   |SI FSalida ! 0;
      |MENAV "    No existe la estructura de descarga.";
      |ERROR;
   |FINSI;
   |CIERRA #6;
|FPRC;

|CALCULO pintames; |TIPO 0;
  |SI #0Campo ! 0; |Y #0Campo ! 13;
      NumMes = dig1 + (#0Campo - 1);   || Operacion para saber que mes es
      |SI NumMes > 12; NumMes = NumMes - 12; |FINSI;
  |SINO;
      NumMes = #0Campo;
  |FINSI;
  |HAZ NombreMes;
  nNume1 = Campo + 4;
  #0nNume1 = NombreMes; |PINTA #0nNume1;
|FCAL;

|CALCULO LosDep; |TIPO 7;
 |SI #0#2 = 0;
     #0#6 = "N"; |PINTA #0#6;
     |PTEC 802;
 |FINSI;
|FCAL;

|CALCULO Recuperar; |TIPO 3;
   |SI #48#27 = "S";
       |HAZ MultiEmpresa;
   |SINO;
       |DFICE dirfich0; |QBF dirfich0;
       |HAZ HazTodo;
   |FINSI;
|FCAL;

|| *************************************************************
|PROCESO ActualExi1;

 nNume1 = 11 + #0#0;  || Desde Mes
 nNume2 = 11 + #0#1;  || Hasta Mes
 |PARA nPara1 = nNume1; |SI nPara1 [ nNume2; |HACIENDO nPara1 = nPara1 + 1;
       #7nPara1 = 0;
 |SIGUE;

 #1#0 = #7#2;
 |LEE 001#1=;
 |SI FSalida ! 0;
     |PDEFECTO #1,9,54;
 |FINSI;

 |ATRI S;
 |PINTA 1910, "Tratando Cuenta: ";
 |ATRI 0;
 |PINTA 1927, #1#0;
 |PINTA 2027, #1#2;

 nImpor = 0;

 |SI #0#6 = "S";

     |DDEFECTO #11;
     #11#0  = #7#2;
     #11#57 = aElDepar;
     |LEE 001#11=;
     |SI FSalida = 0;

         |PARA nPara1 = nNume1; |SI nPara1 [ nNume2; |HACIENDO nPara1 = nPara1 + 1;
               |SI nPara1 = 11;
                   nNume3 = 11;
               |SINO;
                   nNume3 = nPara1 + 46;
               |FINSI;
               #7nPara1 = #11nNume3;
        |SIGUE;
     |FINSI;

 |SINO;

     |PARA nPara1 = 11; |SI nPara1 [ nNume2; |HACIENDO nPara1 = nPara1 + 1;

           nNume3 = 11 + ( 3 * (nPara1 - 11));
           nImpor = nImpor + #1nNume3;

           |SI nPara1 ] nNume1; |Y nPara1 [ nNume2;
               #7nPara1 = nImpor;
           |FINSI;
     |SIGUE;
 |FINSI;

 |GRABA 020#7.a;
 |LIBERA #7;
|FPRC;

|DEFBUCLE ActualExi;
 #7#1;
 ;
 #0#2,01;
 #0#2,99;
 e;
 NULL, ActualExi1;
|FIN;

|PROCESO HazTodo;

 |SI #0#2 ! 0;

     aAlfa1 = #0#2; |QBF aAlfa1;
     aAlfa1 = ("00" + aAlfa1) % -102;
     aElDepar = aAlfa1;
     eaDDepar  = aElDepar;  eaHDepar  = aElDepar;

     |SI #0#6 = "S";
         efDFecha  = fec1;
         efHFecha  = fec2;
         |ATRI I;
         |PINTA 2302, "Procesando Cuentas por Departamentos ... Espere ";
         |ATRI 0;
         |SUB_EJECUTA_NP "cacuedep";
         |PINTA 2302, "                                                ";
         |NOME_DAT #11 eaNomFDep;
     |FINSI;

     |ABRE #cacuenta;
     |ABRE #cacuedep;

     |BUCLE ActualExi;

     |CIERRA #cacuenta;
     |CIERRA #cacuedep;
 |FINSI;

  r_emp= enEmpresa;
  r_per= enPeriodo;
  |HAZ ExisGeneral;
|FPRC;

|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #6 dirfich0;
   |PATH_DAT #7 dirfich0;

   |ATRI N; |CUADRO 1508 1850;
   |ATRI R; |PINTA 1609, "Empresa: ";      |ATRI 0;
   |PINTA 1618, #30#2; |PINTA 1709, #30#1; |ATRI 0;
   |PINTA 1910, "                                                      ";
   |PINTA 2010, "                                                      ";

   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 001;
 999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;
