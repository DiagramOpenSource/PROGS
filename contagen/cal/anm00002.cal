|FICHEROS;
 anm00003 #0;
 anm00002 #1;

 anm0003@ #107;
|FIN;

|VARIABLES;
 nModoEntrada = 0;
 aAlfa1 = "";
 aMas   = "";
 aDef   = "";
 nSwError = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoan_i;

|| *********************************************************************

|CALCULO grupo7; |TIPO 7;
  nModoEntrada = (FEntrada / 100) ! 0;
  |SI nModoEntrada = 1;
      #1#0 = "GRU";
      |PINTA #1#0;
      |PTEC 816;
  |FINSI;
|FCAL;

|CALCULO grupo6; |TIPO 6;
  salidas = FSalida;
  |HAZ FPuntosAna;
|FCAL;

|CALCULO grupo0; |TIPO 0;
  aAlfa1 = #1Campo % 103;
  |SI aAlfa1 ! "GRU";
      |MENAV "    Error. Codigo de Grupo Erroneo ";
      |ERROR;
  |FINSI;
|FCAL;

|| **************************************************************************
|PROCESO LeeEspejo;
 aAlfa1 = #107#2 % 103;
 |SI aAlfa1 = "GRU";
     nSwError = 1;  || Si que tiene informado este grupo
 |FINSI;
|FPRC;

|DEFBUCLE LeeEspejo;
 #107#1002;
 ;
 #0#2, 001;
 #0#2, 999;
 ;
 NULL, LeeEspejo;
|FIN;

|PROCESO BuscoCoherencia;
 nSwError = 0;
 |DBASE aMas;
 aDef = aMas + "def/anm00003";
 |CARGA_DEF aDef, anm0003@;
 |SI FSalida ! 0;
     |ACABA;
 |FINSI;

 |BUCLE LeeEspejo;
 |DESCARGA_DEF #107;
|FPRC;


|CALCULO anacta; |TIPO 6;
  salidas = FSalida;
  |HAZ FPuntosAna;
|FCAL;

|CALCULO MiraCohe; |TIPO 0;
  |SI #1#0 = #0#2;
      |MENAV "    Error, esta introduciendo el mismo grupo.";
      |ERROR;
      |ACABA;
  |FINSI;

  aAlfa1 = #0#2 % 103;
  |SI aAlfa1 = "GRU";
      |HAZ BuscoCoherencia;
      |SI nSwError = 1;
          |MENAV "    Error, el grupo introducido tiene otros grupos.";
          |ERROR;
      |FINSI;
  |FINSI;
|FCAL;

|CALCULO sumar; |TIPO 2;
  #1#2 = #1#2 +. #0#4; |PINTA #1#2;
|FCAL;

|CALCULO grupo3; |TIPO 3;
  |SI #1#2 ! 100;
      |MENAV "    ATENCION!!! Tanto por cien incorrecto ... ";
  |FINSI;
|FCAL;
