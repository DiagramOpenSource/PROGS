d|FICHEROS;
   cacondcu #0;
   camaasil #1;
   zdescili #2,MANTE;
   cacuenta #10;

   canempre #30;
   caselem1 #998;
|FIN;

|VARIABLES;
    &entrada = 1;
    cta       = 0;
    cta_t     = 0;
    dig_t     = 0;
    long      = "  ";
    sw        = 0;
    error     = 0;
    contador  = 0;
    feini     = @;
    fefin     = @;
    p_alfa1   = "";
    p_error   = 0;
    alfa1     = "";
    nSw       = 0;
    nEmpMulti = 0;
    &eSwMul   = 0;
    &espe15   = "";
    &espe16   = 0;
    nSwOpera  = 0;
    CodigoEmpresa  = 0;
    PeriodoEmpresa = 0;
    dirfichOri     = "";
    nSitua         = 0;
    nPerAct        = 0;
    nDDiario       = 0;
    nHDiario       = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE dsco08_i;

|| ---------------------------------------------------------------------
|CALCULO cuenta; |TIPO 6;
     p_alfa1  = #0Campo;
     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;

     cta_t = Campo;
     dig_t = Campo + 1;
     |HAZ cuenta_t;
|FCAL;

|CALCULO cuenta_t;
   cta  = #0cta_t;
   long = cta % 0;
   sw   = 0;
|SI long = dig4; sw = 1; #0dig_t = 1; |FINSI;
|SI long = dig5; sw = 1; #0dig_t = 2; |FINSI;
|SI long = dig6; sw = 1; #0dig_t = 3; |FINSI;
|SI long = dig7; sw = 1; #0dig_t = 4; |FINSI;
|SI long = dig8; sw = 1; #0dig_t = 5; |FINSI;
|SI long = dig9; sw = 1; #0dig_t = 6; |FINSI;

|SI sw = 0;
    #0dig_t = 0;
    |MENAV "    Longitud de cuenta fuera de Nivel !";
    |ERROR;
|FINSI;
|FCAL;

|CALCULO cuefin; |TIPO 0;
  error = 0;
  |ABRE #10;
  #10#0 = #0cta_t;
  #10#1 = #0dig_t;
  |LEE 040#10=;
  |SI FSalida ! 0; error = 1; |CIERRA #10; |ERROR; |ACABA; |FINSI;
  |SI #10#3 = "N";
      error = 1;
      |CIERRA #10;
      |MENAV "      La cuenta no tiene pase DIRECTO !";
      |ERROR;
      |ACABA;
  |FINSI;
  #0#4  = #10#2; |PINTA #0#4;
  #0#7  = #10#51; |PINTA #0#7;
  #0#11 = #10#52; |PINTA #0#11;
  #0#5  = #10#53; |PINTA #0#5;

  |CIERRA #10;
|FCAL;

|CALCULO defectos; |TIPO 0;
  |SI #0#15 = "N";
      #0#16  = fec1;
  |SINO;
      #0#16 = "01.01.2000";
  |FINSI;
  |PINTA #0#16;
  #0#17 = fec2; |PINTA #0#17;
|FCAL;

|CALCULO fechas; |TIPO 0;
  |SI #0#15 = "N";
      |SI #0Campo > fec2;|O #0Campo < fec1;
          |MENAV "    Fecha fuera de Periodo ...";
          |ERROR;
      |FINSI;
  |FINSI;
|FCAL;

|| **************************************************************************
|PROCESO PintaCamps;
 #0#3 = #0#9  - #0#13;
 #0#6 = #0#10 - #0#14;
 |PINTA #0#9;  |PINTA #0#13; |PINTA #0#3;
 |PINTA #0#10; |PINTA #0#14; |PINTA #0#6;
|FPRC;

||... Procesos de Zdescili
|PROCESO pintali1; |TIPO 0;
   |CAMPO_ATRIBUTO #2#0, R, 0, 0; |PINTA #2#0;
   |CAMPO_ATRIBUTO #2#2, R, 0, 0; |PINTA #2#2;
   |CAMPO_ATRIBUTO #2#3, R, 0, 0; |PINTA #2#3;
   |CAMPO_ATRIBUTO #2#5, R, 0, 0; |PINTA #2#5;
   |CAMPO_ATRIBUTO #2#6, R, 0, 0; |PINTA #2#6;
   |CAMPO_ATRIBUTO #2#7, R, 0, 0; |PINTA #2#7;
   |CAMPO_ATRIBUTO #2#8, R, 0, 0; |PINTA #2#8;
   |HAZ pidetecl;
|FPRC;

|PROCESO pidetecl; |TIPO 0;
   |SI FSalida ! 0;  |ACABA;  |FINSI;
   |LEE 101#0=;
   |SI #2#8 = "*";
       #2#8 = " ";
       #0#9  = #0#9  + #2#6;
       #0#13 = #0#13 + #2#7;

       #0#10 = #0#10 + #2#6;
       #0#14 = #0#14 + #2#7;
   |SINO;
       #2#8 = "*";
       #0#9  = #0#9  - #2#6;
       #0#13 = #0#13 - #2#7;

       #0#10 = #0#10 - #2#6;
       #0#14 = #0#14 - #2#7;
   |FINSI;
   |PINTA #2#8;
   |GRABA 020#2a;
   |LIBERA #2;
   |HAZ PintaCamps;
|FPRC;

|PROCESO pintali2; |TIPO 0;
   |CAMPO_ATRIBUTO #2#0, D, 0, 0;
   |CAMPO_ATRIBUTO #2#2, D, 0, 0;
   |CAMPO_ATRIBUTO #2#3, D, 0, 0;
   |CAMPO_ATRIBUTO #2#5, D, 0, 0;
   |CAMPO_ATRIBUTO #2#6, D, 0, 0;
   |CAMPO_ATRIBUTO #2#7, D, 0, 0;
   |CAMPO_ATRIBUTO #2#8, D, 0, 0;
   |PINTA #2#0;
   |PINTA #2#2;
   |PINTA #2#3;
   |PINTA #2#5;
   |PINTA #2#6;
   |PINTA #2#7;
   |PINTA #2#8;
|FPRC;

|| *************************************************************
|CALCULO grabaapun;
     |SI nPerAct = -1; |O nPerAct ! #2#10;
        |SI nPerAct ! -1;
             |CIERRA #1;
         |FINSI;
         dirfich0 = #2#11; |QBF dirfich0;
         nPerAct  = #2#10;
         |PATH_DAT #1 dirfich0;
         |ABRE #1;
     |FINSI;

     #1#0 = #2#1;
     #1#1 = #2#2;
     #1#2 = #2#3;
     #1#3 = #2#4;
     |LEE 101#1=;
     |SI FSalida = 0;
         #1#18 = " ";
         |GRABA 020#1a;
     |FINSI;
     |LIBERA #1;
|FCAL;

|DEFBUCLE zconcili;
    #2#1;
    8;
    000001, " ";
    999999, " ";
    e;
    NULL, grabaapun;
|FIN;

|| **********************************************************
|PROCESO calcuant;
  |SI #1#8 = "D";
      #0#7  = #0#7  + #1#9;
  |SINO;
      #0#11 = #0#11 + #1#9;
  |FINSI;
  #0#5 = #0#7 - #0#11;

  |SI #1#18 = " ";              || Debe, Haber y saldo Pendiente Conciliar
      |SI #1#8 = "D";
          #0#10 = #0#10 + #1#9;
      |SINO;
          #0#14 = #0#14 + #1#9;
      |FINSI;
      |ACABA;
  |FINSI;

  || Apuntes Conciliados

  |SI #1#8 = "D";              || Debe, haber y saldo Conciliado
      #0#8  = #0#8 + #1#9;
  |SINO;
      #0#12 = #0#12 + #1#9;
  |FINSI;
  #0#2 = #0#8 - #0#12;

  contador = contador + 1;
  #2#0 = contador;
  #2#1 = #1#0;
  #2#2 = #1#1;
  #2#3 = #1#2;
  #2#4 = #1#3;
  #2#5 = #1#7;
  |SI #1#8 = "D";
      #2#6 = #1#9;
      #2#7 = 0;
  |SINO;
      #2#7 = #1#9;
      #2#6 = 0;
  |FINSI;
  #2#8 = "*";
     #2#10 = enPeriodo;
     #2#11 = dirfich0;
     #2#12 = enEjercicio;
  |GRABA 020#2n;
|FPRC;

|DEFBUCLE camaasil0;
 #1#3;
 0;
 aCtaSe, feini, 000000, 0001, nDDiario;
 aCtaSe, fefin, 999999, 9999, nHDiario;
 e;
 NULL, calcuant;
|FIN;
|| ---------------------------------------------------------------------

|PROCESO HazTodo;
    aCtaSe = #0#0;
    |SI #0#15 = "S";
        aCtaSe = eaCta08;
        |SI enEjercicio < 2008;
            aCtaSe = eaCta90;
        |FINSI;
    |FINSI;
    |BLIN 24;
    |BUCLE camaasil0;
|FPRC;

|| ******************************************************************
|PROCESO MultiEmp1;
   nSitua = 1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;
   |SI #0#16 > #caselem1#5; |O #0#17 < #caselem1#4;
       |ACABA;  || Fuera de la seleccion
   |FINSI;

   |PATH_DAT #1 dirfich0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmpEjer;
   #caselem1#2;
   ;
   enEmpresa, "01.01.0000";
   enEmpresa, "31.12.2999";
   ;
   NULL, MultiEmp1;
|FIN;

|PROCESO MultiEjercicio;
   espe15 = "S";

   |SUB_EJECUTA "caselemp";
   |NOME_DAT #998 eaFichsel;

   |BUCLE MultiEmpEjer;

   |DELINDEX #998;
|FPRC;
|| ******************************************************************
|RUTINA inferior;
   #2#0 = 1;
|FRUT;

|RUTINA superior;
   #2#0 = contador;
|FRUT;

|CALCULO concilia; |TIPO 3;
     |SI error = 1; |ACABA; |FINSI;

     nDDiario = 00;
     nHDiario = 98;
     |SI #0#15 = "S"; nDDiario = 01; nHDiario = 98; |FINSI;

     |DFICE dirfich0;
     eaCta08 = #0#0;
     eaCta90 = #0#0;
     aCtaSe  = #0#0;

     CodigoEmpresa    = enEmpresa;
     PeriodoEmpresa   = enPeriodo;
     dirfichOri = dirfich0;
     |SI #0#15 = "S";
         |HAZ eCam_Primero;
         eaCuenta = #0#0;
         enBEjer  = enEjercicio;
         |HAZ eCam_Segundo;
     |FINSI;

    |HAZ nom_usu;
    alfa1 = "i" + nom_tmp;
    |NOME_DAT #2 #alfa1#;
    |DELINDEX #2;

    |MENSA "2400 Procesando ......";

    |ABRE #2;
    contador = 0;
    feini = #0#16;
    fefin = #0#17;
    #0#7  = 0;
    #0#11 = 0;
    #0#5  = 0;

    nSitua = 0;
    eSwMul = 0;
    |SI #0#15 = "N";
        dirfich0 = dirfichOri;
        |HAZ HazTodo;
    |SINO;
        eSwMul = 1;
        |HAZ MultiEjercicio;
   |FINSI;
   |CIERRA #2;
   |BLIN 24;

   |PINTA #0#8;  |PINTA #0#12; |PINTA #0#2;
   #0#9 = 0; #0#13 = 0; #0#3 = 0;
   |HAZ PintaCamps;

   |SI contador = 0;
        |MENAV "      No hay nada para Desconciliar !!!";
        |DELINDEX #2;
        |ACABA;
    |FINSI;

    |ENTLINEAL #1#2, -1, 4, 22, 0, inferior, superior;
    |CONFI "2400SActualizar Desconciliacion: ";
    |SI FSalida = 0;
        nPerAct = -1;
        |SI #0#16 = "N";
            |ABRE #1;
            nPerAct = PeriodoEmpresa;
        |FINSI;
        |BUCLE zconcili;
        |CIERRA #1;
   |FINSI;
   |DELINDEX #2;
|FCAL;
