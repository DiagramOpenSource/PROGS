|FICHEROS;
   cacheapu #0;
   camaasil #1;    || apuntes lins.
   camaasic #2;    || apuntes cabs.

   camaniva #3;    || Mantenimiento registros IVA/IRPF
   caivalin #4;    || Lineas mantenimiento registros IVA/IRPF
   cacuenta #5;    || Plan Contable
   calibros #6;

   canempre #30;
   caselem1 #998;
|FIN;

|VARIABLES;
   aParam  = "";
   informe = "cacheapu";
   fech1   = @;
   fech2   = @;
   sw      = 0;

   &antdia  = 0;
   &antfec  = @;
   &antdoc  = 0;
   &antapu  = 0;
   &lerror  = 0;
   &men     = "";
   debe_p   = 0;
   haber_p  = 0;
   swmalo   = 0;
   swque    = 0;
   alfa1   = "";
   alfa2   = "";
   alfa3   = "";
   alfa4   = "";
   aAlfa1  = "";
   nume1   = 0;
   nume2   = 0;
   nume3   = 0;
   nNume1  = 0;
   nNume2  = 0;

   nivel   = 0;
   cta     = "";
   long    = "";
   nDLinea = 0;
   nHLinea = 0;

   atip = "";
   alib = 0;
   afac = 0;
   aser = "";

   npara1 = 0;

   nHay   = 0;
   nEsta  = 0;
   nSwRepes = 0;

   &eaDirOrigen = "";
   &enEmpCons   = 0;
   &enPerCons   = 0;
|FIN;

|INCLUYE dscont_i;

|| ************************** CALCULOS DESDE PANTALLA
|| ****************************************************************
|PROCESO haycambio;
   |SI antdoc = 0;
        swmalo = 1;
        antapu = 9999;
        men = "NUMERO DOCUMENTO IGUAL A CERO";
        |PRINT;
   |FINSI;
   |SI antapu > 9999;
        swmalo = 1;
        men = "ASIENTO CON MAS DE 9999 APUNTES ";
        antapu = 9999;
        |PRINT;
   |FINSI;
   |SI antdia = 0; |Y antfec ! fec1;
       swmalo = 1;
       men     = "ASTO EN DIARIO 00 CON FECHA DIFERENTE A APERTURA "
       antapu = 9999;
       |PRINT;
   |FINSI;

   #camaasic#0 = antdia;
   #camaasic#1 = antfec;
   #camaasic#2 = antdoc;
   |LEE 011#2=;
   |SI FSalida = 0;
       debe_p = debe_p ! EURODB;
       haber_p = haber_p ! EURODB;
       nNume1 = (#camaasic#4 - debe_p) ! EURODB;
       aAlfa1 = nNume1; |QBF aAlfa1;
       |SI aAlfa1 = "0"; |O aAlfa1 = "-0";
           nNume1 = 0;
       |FINSI;
       nNume2 = (#camaasic#5 - haber_p) ! EURODB;
       aAlfa1 = nNume2; |QBF aAlfa1;
       |SI aAlfa1 = "0"; |O aAlfa1 = "-0";
           nNume2 = 0;
       |FINSI;

      |SI nNume1 ! 0; |O nNume2 ! 0;
         swmalo = 1;
         antapu = 9999;
         men = "SALDOS ASIENTO Y APUNTES DISTINTOS.";
         |PRINT;
      |SINO;
         nNume1 = (debe_p - haber_p) ! EURODB;
         aAlfa1 = nNume1; |QBF aAlfa1;
         |SI aAlfa1 = "0"; |O aAlfa1 = "-0";
             nNume1 = 0;
         |FINSI;
         |SI nNume1 ! 0;
            swmalo = 1;
            antapu = 9999;
            men = "ASIENTO DESCUADRADO.";
            |PRINT;
         |FINSI;
      |FINSI;
   |FINSI;

   antdia  = #1#0;
   antfec  = #1#1;
   antdoc  = #1#2;
   antapu  = #1#3;
   debe_p  = 0;
   haber_p = 0;
|FPRC;

|PROCESO VerLibro;
   |DDEFECTO #calibros;
   #calibros#0 = #camaasil#13;
   |LEE 041#calibros.=;
   |SI #calibros#2 ! #camaasil#12;
       swmalo = 1;
       alfa1 = #1#13; |QBF alfa1; alfa1 = ("00" + alfa1) % -102;
       alfa2 = #1#14; |QBF alfa2; alfa2 = ("000000" + alfa2) % -106;
       alfa3 = #1#15;
       alfa4 = alfa1 + "/" + alfa2 + "/" + alfa3;
       men = "LIBRO IVA DIFERENTE A TIPO ASTO -FRA." + alfa4;
       |PRINT;
   |FINSI;
|FPRC;

|PROCESO VerFra_Asto;
    nHay = nHay + 1;
    |SI  #camaniva#7 = #1#0; |Y #camaniva#8 = #1#1; |Y #camaniva#9 = #1#2;
         nEsta = 1;
    |FINSI;
|FPRC;

|DEFBUCLE VerFra_Asto;
  #camaniva#3;
  ;
  #1#13, #1#15, #1#14;
  #1#13, #1#15, #1#14;
  ;
  NULL, VerFra_Asto;
|FIN;

|PROCESO VerAsto_Fra;
         nHay  = 0;
         nEsta = 0;
         |BUCLE VerFra_Asto;
         |SI nHay = 0;
             swmalo = 1;
             alfa1 = #1#13; |QBF alfa1; alfa1 = ("00" + alfa1) % -102;
             alfa2 = #1#14; |QBF alfa2; alfa2 = ("000000" + alfa2) % -106;
             alfa3 = #1#15;
             alfa4 = alfa1 + "/" + alfa2 + "/" + alfa3;
             men = "ASIENTO DE IVA " + alfa4 + " INEXISTENTE";
             |PRINT;
         |SINO;
             |SI nEsta = 0;
                 swmalo = 1;
                 alfa1 = #1#13; |QBF alfa1; alfa1 = ("00" + alfa1) % -102;
                 alfa2 = #1#14; |QBF alfa2; alfa2 = ("000000" + alfa2) % -106;
                 alfa3 = #1#15;
                 alfa4 = alfa1 + "/" + alfa2 + "/" + alfa3;
                 men = "ASIENTO MAL RELACIONADO CON LA FRA. " + alfa4;
                 |PRINT;
             |FINSI;
             |SI nHay > 1;
                 nSwRepes = 1;
             |FINSI;
             |HAZ VerLibro;
         |FINSI;
|FPRC;

|PROCESO informa;
   swmalo = 0;
   |SI sw = 0;
      sw = 1;
      antdia  = #1#0;
      antfec  = #1#1;
      antdoc  = #1#2;
      antapu  = #1#3;
      debe_p  = 0;
      haber_p = 0;
   |FINSI;

   |SI #1#0 ! antdia;  |O #1#1 ! antfec;  |O #1#2 ! antdoc;
      |HAZ haycambio;
   |FINSI;

   antapu = #1#3;
   || ......... Testeo
   #camaasic#0 = #1#0;
   #camaasic#1 = #1#1;
   #camaasic#2 = #1#2;
   |LEE 001#2=;                    || Lectura Cabecera
   |SI FSalida ! 0;
      swmalo = 1;
      men = "NO EXISTE CABECERA";
      |PRINT;
   |FINSI;

   |SI #1#1 < fec1;                              || Fecha
      swmalo = 1;
      men = "FECHA ASIENTO INFERIOR AL EJERCICIO CONTABLE";
      |PRINT;
   |FINSI;
   |SI #1#1 > fec2;
      swmalo = 1;
      men = "FECHA ASIENTO SUPERIOR AL EJERCICIO CONTABLE";
      |PRINT;
   |FINSI;
   |SI #1#3 [ 0; |Y #1#2 ! 0;
        swmalo = 1;
        men = "APUNTE IGUAL O MENOR A CERO";
        |PRINT;
   |FINSI;

   cta = #1#4; |QBF cta;           || Cuenta Contable
   |SI cta = "";
      swmalo = 1;
      men = "CUENTA CONTABLE EN BLANCO";
      |PRINT;
   |SINO;
      nivel = 0;
      long  = cta % 0;
      nume1 = long;
      |SI nume1 = dig4; nivel = 1; |FINSI;
      |SI nume1 = dig5; nivel = 2; |FINSI;
      |SI nume1 = dig6; nivel = 3; |FINSI;
      |SI nume1 = dig7; nivel = 4; |FINSI;
      |SI nume1 = dig8; nivel = 5; |FINSI;
      |SI nume1 = dig9; nivel = 6; |FINSI;
      |SI nivel = 0;
         swmalo = 1;
         men = "CUENTA CONTABLE FUERA DE NIVEL";
         |PRINT;
      |SINO;
         |SI nivel ! #1#5;
             #1#5 = nivel;
            ||  swmalo = 1;
            ||  men = "CUENTA CONTABLE CON NIVEL ERRONEO";
            ||  |PRINT;
         |FINSI;
      |FINSI;
      swque = 0;
      |PARA npara1 = 1; |SI npara1 [ nume1; |HACIENDO npara1 = npara1 + 1;
            nume2 = (npara1 * 100) + 1;
            alfa1 = nume2; |QBF alfa1;
            alfa2 = cta % alfa1;
            |SI alfa2 < "0"; |O alfa2 > "9";
                swque = 1;
                |SAL;
            |FINSI;
      |SIGUE;
      |SI swque = 1;
          swmalo = 1;
          men = "CUENTA CONTABLE CON CARACTERES EXTRA¥OS";
          |PRINT;
      |FINSI;
   |FINSI;

   #cacuenta#0 = #1#4;
   #cacuenta#1 = #1#5;
   |LEE 001#5=;
   |SI FSalida ! 0;
      swmalo = 1;
      men = "NO EXISTE CUENTA CONTABLE";
      |PRINT;
   |SINO;
      |SI #cacuenta#3 = "N";
         swmalo = 1;
         men = "CUENTA CONTABLE SIN ASIENTO DIRECTO";
         |PRINT;
      |FINSI;
   |FINSI;

   |SI #1#9 = 0;  |Y #0#1 = "S";                 || Importe Cero
      swmalo = 1;
      men = "IMPORTE IGUAL A CERO";
      |PRINT;
   |FINSI;

   |SI #1#6 = 0;                   || Concepto
      swmalo = 1;
      men = "CONCEPTO AUTOMATICO IGUAL A CERO";
      |PRINT;
   |FINSI;

   |SI #1#0 ! 0;
       |SI #1#12 = "S"; |O #1#12 = "R";
           |SI #1#19 ! " ";
               |SI atip ! #1#12; |O alib ! #1#13; |O afac ! #1#14; |O aser ! #1#15;
                    |HAZ VerAsto_Fra;
               |FINSI;
           |FINSI;
           atip = #1#12; alib = #1#13; afac = #1#14; aser = #1#15;
       |FINSI;
   |FINSI;

   |SI #1#8 = "D";
      debe_p = debe_p + #1#9;
   |SINO;
      haber_p = haber_p + #1#9;
   |FINSI;
   |SI swmalo = 1;
      lerror = lerror + 1;
   |FINSI;
|FPRC;

|DEFBUCLE lee_asientos;
   #1#1;
   ;
   00, fech1, 000000, nDLinea;
   99, fech2, 999999, nHLinea;
   e;
   NULL, informa;
|FIN;

|PROCESO HazTodo;
   nSwRepes = 0;
   lerror   = 0;
   sw       = 0;
   nDLinea = -10000;
   nHLinea = 999999;

   |ABRE #2;
   |ABRE #5;
   |ABRE #6;
   |BUCLE lee_asientos;
   |SI sw ! 0;
       |HAZ haycambio; |SI swmalo = 1; lerror = lerror + 1; |FINSI;
       |SI nSwRepes = 1;
           antdia  = 0;
           antfec  = "";
           antdoc  = 0;
           antapu  = 0;
           men = "EXISTE LIBRO+SERIE+FACTURA REPETIDAS";
           lerror = lerror + 1;
           |PRINT;
       |FINSI;
   |FINSI;
   |CIERRA #6;
   |CIERRA #5;
   |CIERRA #2;
   |SI lerror ! 0;
       |PIEINF;
   |FINSI;
|FPRC;

|CALCULO chequeo; |TIPO 3;
   |SI #0#0 = "N";  |ACABA;  |FINSI;

   |SI aParam ! "COTP";
       |IMPRE 0;
       |SI FSalida ! 0;  |ACABA;  |FINSI;
   |FINSI;

   |INFOR informe;
   |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;
   fech1 = "00.00.0000";
   fech2 = "31.12.9999";

   |SI #48#27 = "S"; |Y aParam ! "COTP";
       |HAZ MultiEmpresa;
   |SINO;
       |SI aParam ! "COTP";
           |DFICE dirfich0; |QBF dirfich0;
       |FINSI;
       |HAZ HazTodo;
   |FINSI;
   |FININF;

   |SI aParam ! "COTP"; |FINIMP; |FINSI;
|FCAL;

|| *************************************************************
|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #2 dirfich0;
   |PATH_DAT #3 dirfich0;
   |PATH_DAT #4 dirfich0;
   |PATH_DAT #5 dirfich0;
   |PATH_DAT #6 dirfich0;

   |ATRI N; |CUADRO 2005 2347;
   |ATRI R; |PINTA 2106, "Empresa: ";      |ATRI 0;
   |PINTA 2115, #30#2; |PINTA 2206, #30#1; |ATRI 0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 000001;
 999999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;

|PROGRAMA;
     aParam = PARAMETRO; |QBF aParam;
     |SI PARAMETRO = "";
         |MANTE #cacheapu;
     |SINO;
         |SI aParam = "COTP";
             |PATH_DAT #1 eaDirOrigen
             |PATH_DAT #2 eaDirOrigen
             |PATH_DAT #3 eaDirOrigen
             |PATH_DAT #4 eaDirOrigen
             |PATH_DAT #5 eaDirOrigen
             |PATH_DAT #6 eaDirOrigen
             #30#2 = enEmpresa;
             #30#3 = enPeriodo;
             #30#1 = eaNombreEmp;
             #30#26 = enEjercicio - 2000;
         |SINO;
             |HAZ inicio;
         |FINSI;
         #cacheapu#0 = "S";
         #cacheapu#1 = "S";
         |HAZ chequeo;
     |FINSI;
|FPRO;
