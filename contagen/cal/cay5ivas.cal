|FICHEROS;
   cay5ivas #0;

   caconimp #8;
   camaniva #11;
   caivalin #12;
   caivaasi #200;

   cawdatos #900;
   canempre #30;
   caselem1 #998;
|FIN;

|VARIABLES;
    aAlfa1   = "";
    aAlfa2   = "";
    aAlfa3   = "";
    aAlfa4   = "";
    nNume1   = 0;
    nNume2   = 0;

    informe  = "";
    inform1  = "cay5ivas";
    inform2  = "cay5ivat";
    nExis    = 0;
    nSwOpe   = 0;

    nSwAlgun   = 0;
    &enIva     = 0;
    aTipo      = "";
    nCampo     = 0;
    &enSwLinea = 0;
    &eaFecha   = "";
    aParam     = "";

    aMulDep    = "";
    fDFecha    = @;
   &SwCabIva = 0;
   &SwLinIva = 0;
   &SwTotal = 0;

   &TotalBases  = 0;
   &TotalIvas   = 0;
   &TotalRecs   = 0;
   &TotalCuotas = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE caliva_i;

|| ***************** Calculos de Pantalla
|CALCULO Primero; |TIPO 8;
  |HAZ eParaModelos;
  |ABRE #200;
  |LEE 000#200p;
  |SI FSalida ! 0;
      |DDEFECTO #200;
      |GRABA 040#200n;
  |FINSI;
  |CIERRA #200;
  aAlfa1 = #200#44;
  |C_M #0#7,1,aAlfa1;
  |C_M #0#8,1,aAlfa1;
|FCAL;

|CALCULO defectos; |TIPO 7;
  fDFecha = fec1;
  |SI enSwSelFc = 1; fDFecha = "01.01.2000"; |FINSI;
  #0#4 = fDFecha; |PINTA #0#4;
  #0#5 = fec2;    |PINTA #0#5;
  |HAZ DatosEmp;
|FCAL;

|CALCULO fecha; |TIPO 0;
  fDFecha = fec1;
  |SI enSwSelFc = 1; fDFecha = "01.01.0000"; |FINSI;
  |SI #0Campo < fDFecha; |O #0Campo > fec2;
      |MENAV "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
      |ERROR;
      |ACABA;
  |FINSI;
  |SI Campo = 5; |Y #0#5 < #0#4;
      |MENAV "    FECHA ERRONEA ";
      |ERROR;
  |FINSI;
|FCAL;

|CALCULO no_dep; |TIPO 7;
    |C_M #0#11, 1, eaDepar;
    |C_M #0#12, 1, eaDepar;
|FCAL;

|PROCESO DatosEmp; |TIPO 7;
 |ABRE #8;
 |DDEFECTO #8;
 #8#0 = 1;
 |LEE 011#8=;
 |CIERRA #8;
 #0#10 = #8#7; |PINTA #0#10;
 aAlfa1 = #8#8; |C_M #0#10,1,aAlfa1;
|FPRC;

|| *************************************************************************
|| **************************   MEOLLO   ***********************************
|| *************************************************************************
|PROCESO LasLineas0;
  aAlfa1 = #12#30; |QBF aAlfa1; |SI aAlfa1 = ""; #12#30 = #11#10; |FINSI;
  |SI aMulDep = "S";
      |SI Haybasica = 0
          |SI #12#30 < #0#11; |O #12#30 > #0#12;
              |ACABA;
          |FINSI;
      |SINO;
          nDepar = #12#30; |SI nDepar = 0;  nDepar = 1;   |FINSI;
          |SI nDepar < #0#11; |O nDepar > #0#12;  ||desde/hasta actividad
              |ACABA;
          |FINSI;
      |FINSI;
  |FINSI;

  |SI nSwOpe = 0;
     nExis  = 1;
     |ERROR10;
  |SINO;
     enIva  = enIva  + #12#8;
     |PRINT;
     enSwLinea = 1;
     SwCabIva = 0; SwTotal = 0;

     |HAZ sumaLiqIVA;
     TotalBases  = (TotalBases + #caivalin#6) ! EURODB;
     TotalIvas   = (TotalIvas  + #caivalin#8) ! EURODB;
     TotalRecs   = (TotalRecs  + #caivalin#10) ! EURODB;
     TotalCuotas = (TotalIvas + TotalRecs) ! EURODB;
  |FINSI;
|FPRC;

|DEFBUCLE LasLineas;
 #12#1;
 29;
 #11#0, #11#1, 01, "S";
 #11#0, #11#1, 99, "S";
 e;
 NULL, LasLineas0;
|FIN;

|PROCESO caymaniva;
  |SI aMulDep ! "S";
      |SI Haybasica = 0
          |SI #11#10 < #0#11; |O #11#10 > #0#12;
              |ACABA;
          |FINSI;
      |SINO;
          nDepar = #11#10; |SI nDepar = 0;  nDepar = 1;   |FINSI;
          |SI nDepar < #0#11; |O nDepar > #0#12;  ||desde/hasta actividad
              |ACABA;
          |FINSI;
      |FINSI;
  |FINSI;

  nExis   = 0;
  nSwOpe  = 0;
  |BUCLE LasLineas;
  |SI nExis = 0; |ACABA; |FINSI;

  aAlfa1 = #11#4;
  eaFecha = aAlfa1 % 105;
  enSwLinea = 0;
  nSwOpe    = 1;
  |BUCLE LasLineas;
||  enSwLinea = 2;
||  |PRINT;
  nSwAlgun  = 1;
|FPRC;

|DEFBUCLE caymaniva;
 #11#5;
 2,3,36;
 #0#4, #0#0, 0000000001, #0#7, #0#2, aTipo;
 #0#5, #0#1, 9999999999, #0#8, #0#3, aTipo;
 e;
 NULL,caymaniva;
|FIN;

|PROCESO HazTodo;

   informe = inform1;
   |SI #0#14 = 2; informe = inform2; |FINSI;

   |INFOR informe;
   |SI FSalida ! 0; |ACABA;  |FINSI;
   enDatos = 0; |SI #0#10 = "S"; enDatos = 1; |FINSI;

   |ABRE #200;
   |DDEFECTO #200;
   |LEE 000#200p;
   |CIERRA #200;
   aMulDep = #200#132;
   |SI Haybasica = 0;  || si selecciona todos los departamentos no es necesario ver lineas
       |SI #0#11 = "  "; |Y #0#12 = "zz"; aMulDep = "N"; |FINSI;
   |SINO;
       |SI #0#11 [ 1;    |Y #0#12 = 99;   aMulDep = "N"; |FINSI;
   |FINSI;

   eaIa = #canempre#21;
   enM1 = #canempre#11;
   enM2 = #canempre#12;
   |HAZ LeeDatosEmpresa;
   #900#13 = #0#4; #900#14 = #0#5;

   enIva    = 0;
   nSwAlgun = 0;
   aTipo    = "S";
   |HAZ BorraAcum;
   |BUCLE caymaniva;
   |SI nSwAlgun = 1;
       |PIEINF;
   |FINSI;
   |FININF;
   |SI #0#13 = "S"; |HAZ imprimeLiqIVA; |FINSI;
|FPRC;

|| ********************************************************************
|CALCULO impre; |TIPO 3;
   |IMPRE 0;
   |SI FSalida ! 0;  |ACABA;  |FINSI;

   |SI aParam = "";
       |SI #48#27 = "S";
           |HAZ MultiEmpresa;
       |SINO;
           |DFICE dirfich0; |QBF dirfich0;
           |HAZ HazTodo;
       |FINSI;
   |SINO;
       |HAZ HazTodo;
   |FINSI;

   |FINIMP;
|FCAL;

|| **********************************************************************
|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;
   |PATH_DAT #8  dirfich0;
   |PATH_DAT #11  dirfich0;
   |PATH_DAT #12  dirfich0;
   |PATH_DAT #200  dirfich0;

   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 00001;
 99999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;

|PROGRAMA;

  aParam = PARAMETRO; |QBF aParam;

  |SI aParam = "";
      |HAZ inicio;
  |SINO;
      cod1 = enEmpresa;
      cod2 = enPeriodo;
      |HAZ leelo;
      |DBASS dirfich0; |QBT dirfich0;
      dirfich0 = dirfich0 + "contagen/fich/" + (("00000" + enEmpresa) % -105) + enPeriodo + "/";
      |PATH_DAT #8 dirfich0;
      |PATH_DAT #11 dirfich0;
      |PATH_DAT #12 dirfich0;
      |PATH_DAT #200 dirfich0;
  |FINSI;

  |CLS;
  |MANTE #0;
|FPRO;
