|FICHEROS;
   cachecur #0;
   caclipro #1;
   cacuenta #2;

   canempre #30;
   caselem1 #998;

   &regisnif@ #709;
   :/basica/def/agim0003   #710;
   :/integral/def/dsam0103 #711;

   &poblaci@  #703;
   &sgpaises@ #718;
|FIN;

|VARIABLES;
   &entrada = 1;
   &tinforme = "";
   aAlfa1    = "";
   aAlfa2    = "";
   nNume1    = 0;

   informe   = "cachecur";
   desde = "";
   aDCuenta = "";
   aHCuenta = "";
   aDNombre = "";
   aHNombre = "";

   &swImprimir = 0;
   aError      = "";
   swSelec     = 0;
   sw          = 0;

   aNombre = "";
   aNif    = "";
   aCP     = "";
   nNum1   = 0;
   nNum2   = 0;
|FIN;

|INCLUYE dscont_i;

|PROCESO Cheq_8; |TIPO 8;
   |HAZ inicio;
   ewNif = 0;
   enSwDeDonde = -1;
   |HAZ DirAplicSt;
   |SI HayAcceso = 1;
       enSwDeDonde = 1;
       |REFERENCIA_DEF regisnif@, #711;
   |SINO;
       |SI Haybasica = 1;
           enSwDeDonde = 0;
           |REFERENCIA_DEF regisnif@, #710;
       |FINSI;
   |FINSI;
   |SI enSwDeDonde = -1;
       |MENAV "    Error, no esta instalada BASICA ni INTEGRADA. No puede lanzar este Informe";
       |PTEC 807;
       ewNif = 1;
   |FINSI; || no existe Basica ni Integrada
|FPRC;

|CALCULO LaSeleccion; |TIPO 0;
 |C_M #0#1, 1,"S"; |C_M #0#2, 1,"S";
 |C_M #0#3, 1,"S"; |C_M #0#4, 1,"S";
 |C_M #0#5, 1,"S"; |C_M #0#6, 1,"S";
 |C_M #0#7, 1,"S"; |C_M #0#8, 1,"S";
 |C_M #0#9, 1,"S"; |C_M #0#10,1,"S";
 |C_M #0#11,1,"S"; |C_M #0#12,1,"S";
 |C_M #0#13,1,"S"; |C_M #0#14,1,"S";
 |SI #0#0 = "C";
      |C_M #0#7, 1,"N"; |C_M #0#8, 1,"N";
      |C_M #0#9, 1,"N"; |C_M #0#10,1,"N";
      |C_M #0#11,1,"N"; |C_M #0#12,1,"N";
      |C_M #0#13,1,"N"; |C_M #0#14,1,"N";
 |FINSI;
 |SI #0#0 = "P";
      |C_M #0#1, 1,"N"; |C_M #0#2, 1,"N";
      |C_M #0#3, 1,"N"; |C_M #0#4, 1,"N";
      |C_M #0#5, 1,"N"; |C_M #0#6, 1,"N";
 |FINSI;
|FCAL;

|CALCULO ElPtec; |TIPO 7;
 |C_M #0Campo,0,aAlfa1;
 |SI aAlfa1 = "S";
     |PTEC 816;
 |FINSI;
|FCAL;

|CALCULO Hasta; |TIPO 0;
 |C_M #0Campo,0,aAlfa1;
 |SI aAlfa1 = "S";
     nNume1 = Campo - 1;
     |SI #0Campo < #0nNume1;
         |MENAV "    Error!. Cuenta Hasta Menor que la Cuenta Desde";
         |ERROR;
     |FINSI;
 |FINSI;
|FCAL;

|CALCULO cuenta;|TIPO 6;
     |C_M #0Campo, 0, aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta; |PINTA #0Campo;
|FCAL;
|| **********************************************************************

|CALCULO impre;|TIPO 3;

 |IMPRE 0;
 |SI FSalida ! 0;  |ACABA;  |FINSI;

 |INFOR informe;
 |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

 |SI #48#27 = "S";
     |HAZ MultiEmpresa;
 |SINO;
     |DFICE dirfich0; |QBF dirfich0;
     |HAZ HazTodo;
 |FINSI;
 |FININF;
 |FINIMP;
|FCAL;

||************************************************************************
|PROCESO CodPostal;
   aAlfa1  = nNum1; aAlfa1 = ("00"  + aAlfa1) % -102;
   aAlfa2  = nNum2; aAlfa2 = ("000" + aAlfa2) % -103;
   aCP     = aAlfa1 + aAlfa2;
|FPRC;

|PROCESO porimpre;

   swSelec = 0;
   |SI desde = "C";
       |SI #0#2 ! doce;
           |SI #1#10 ] #0#1; |Y #1#10 [ #0#2; swSelec = 1; |FINSI;
       |FINSI;
       |SI #0#4 ! doce;
           |SI #1#10 ] #0#3; |Y #1#10 [ #0#4; swSelec = 1; |FINSI;
       |FINSI;
       |SI #0#6 ! doce;
           |SI #1#10 ] #0#5; |Y #1#10 [ #0#6; swSelec = 1; |FINSI;
       |FINSI;
   |FINSI;
   |SI desde = "P";
       |SI #0#8 ! doce;
           |SI #1#10 ] #0#7; |Y #1#10 [ #0#8; swSelec = 1; |FINSI;
       |FINSI;
       |SI #0#10 ! doce;
           |SI #1#10 ] #0#9; |Y #1#10 [ #0#10; swSelec = 1; |FINSI;
       |FINSI;
       |SI #0#12 ! doce;
           |SI #1#10 ] #0#11; |Y #1#10 [ #0#12; swSelec = 1; |FINSI;
       |FINSI;
       |SI #0#14 ! doce;
           |SI #1#10 ] #0#13; |Y #1#10 [ #0#14; swSelec = 1; |FINSI;
       |FINSI;
   |FINSI;
   |SI swSelec = 0; |ACABA; |FINSI;

   |PDEFECTO #0, 20, 28;

   aNombre = #1#1; aNombre = aNombre % 136; ||cuando viene del Plan contable
   aNif    = #1#9; aNif    = aNif % 112;
   nNum1   = #1#3; nNum2   = #1#4; |HAZ CodPostal;

   #0#20 = #1#10;
   #0#21 = aNombre; |QBF aNombre;
   #0#22 = aNif;
   #0#23 = aCP;

   swImprimir = 0;
   |ABRE #709;

   |SI aNif = "            ";
       #0#24 = "NIF en blanco en ficha de Cl./Pr.";
       swImprimir = 1;
   |SINO;
      |DDEFECTO #709;
      #709#0 = aNif;
      |LEE 141#709=;
      |SI FSalida ! 0;
          swImprimir = 1;
          #0#24 = "No existe NIFS en el Fichero";
      |SINO;
          #0#24 = #709#1;
          nNum1 = #709#4; nNum2 = #709#5; |HAZ CodPostal; #0#25 = aCP;
          |SI #0#24 = #0#21; |Y #0#23 = #0#25;
              |LIBERA #709;
              |CIERRA #709;
              |VETE OtraCosa;         || No existe error
          |SINO;
              swImprimir = 1;
          |FINSI;
      |FINSI;
      |LIBERA #709;
   |FINSI;

   |SI aNombre = "";
       #0#26 = "Nom. blanco";
       swImprimir = 1;
   |SINO;
       |DDEFECTO #709;
       |SELECT #2#709;
       #709#1 = #1#1;
       |LEE 141#709=;
       |SI FSalida ! 0;
           swImprimir = 1;
           #0#26 = "No existe ";
       |SINO;
           #0#26 = #709#0;
           nNum1 = #709#4; nNum2 = #709#5; |HAZ CodPostal; #0#27 = aCP;
           |SI #709#0 ! aNif; |O #0#23 ! #0#27;
               swImprimir = 1;
           |FINSI;
       |FINSI;
      |SELECT #1#709;
      |LIBERA #709;
   |FINSI;
   |CIERRA #709;

|ET OtraCosa;
   #2#0 = #1#10; || Cuenta Contable
   |LEE 041#2=;
   |SI FSalida ! 0;
       swImprimir = 1;
       #0#21 = "NO existe en el Plan Contable";
   |FINSI;

   |SI swImprimir = 1;
       |PRINT;
        sw = 1;
   |FINSI;
|FPRC;

|DEFBUCLE Lee_nif;
   #1#4;
   23,10;
   "               ", desde, aDCuenta;
   "zzzzzzzzzzzzzzz", desde, aHCuenta;
   e;
   NULL, porimpre;
|FIN;

|DEFBUCLE Lee_nombre;
   #1#2;
   23,10;
   aDNombre, desde, aDCuenta;
   aHNombre, desde, aHCuenta;
   e;
   NULL, porimpre;
|FIN;

|DEFBUCLE Lee_codigo;
   #1#1;
   ;
   desde, aDCuenta;
   desde, aHCuenta;
   e;
   NULL, porimpre;
|FIN;

|PROCESO Leeimprime;
 sw = 0;
 tinforme = "PROVEEDORES";
 |SI desde = "C"; tinforme = "CLIENTES"; |FINSI;

 |ABRE #2;
 |SI #0#15 = "C"; |BUCLE Lee_codigo; |FINSI;
 |SI #0#15 = "N"; |BUCLE Lee_nombre; |FINSI;
 |SI #0#15 = "D"; |BUCLE Lee_nif;    |FINSI;
 |CIERRA #2;

 |SI sw = 1; |PIEINF; |FINSI;
|FPRC;

|PROCESO HazTodo;
 aDNombre = " " * 40; aHNombre = "z" * 40;

 |SI #0#0 ! "P";
     desde = "C";

     aDCuenta = #0#1;
     |SI aDCuenta > #0#3; |Y #0#4 ! doce; aDCuenta = #0#3; |FINSI;
     |SI aDCuenta > #0#5; |Y #0#6 ! doce; aDCuenta = #0#5; |FINSI;
     aHCuenta = #0#2;
     |SI aHCuenta < #0#4;                 aHCuenta = #0#4; |FINSI;
     |SI aHCuenta < #0#6;                 aHCuenta = #0#6; |FINSI;

     |HAZ Leeimprime;
 |FINSI;

 |SI #0#0 ! "C";
     desde = "P";

     aDCuenta = #0#7;
     |SI aDCuenta > #0#9;  |Y #0#10 ! doce; aDCuenta = #0#9;  |FINSI;
     |SI aDCuenta > #0#11; |Y #0#12 ! doce; aDCuenta = #0#11; |FINSI;
     |SI aDCuenta > #0#13; |Y #0#14 ! doce; aDCuenta = #0#13; |FINSI;
     aHCuenta = #0#8;
     |SI aHCuenta < #0#10;                 aHCuenta = #0#10; |FINSI;
     |SI aHCuenta < #0#12;                 aHCuenta = #0#12; |FINSI;
     |SI aHCuenta < #0#14;                 aHCuenta = #0#14; |FINSI;

     |HAZ Leeimprime;
 |FINSI;
|FPRC;

|| **********************************************************************
|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #2 dirfich0;

   |ATRI N; |CUADRO 1508 1850;
   |ATRI R; |PINTA 1609, "Empresa: ";      |ATRI 0;
   |PINTA 1618, #30#2; |PINTA 1709, #30#1; |ATRI 0;
   |PINTA 1910, "                                                      ";
   |PINTA 2010, "                                                      ";

   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 000001;
 999999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;
|| **************************************************************************
