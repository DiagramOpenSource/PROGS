|FICHEROS;
   caz00018 #0;
   camoneda #1;
   canempre #2;
|FIN;

|VARIABLES;
   nPara1   = 0;
   nNume1   = 0;
   nNume2   = 0;
   nNume3   = 0;
   nNume4   = 0;
   nPer     = 0;
   nEmp     = 0;
   nCampo   = 0;
   nInkey   = 0;
   aAlfa1   = "";
   aAlfa2   = "";
   aMensa   = "";

   nSelError  = 0;
   nLaEmpresa = 0;

   aPath    = "";
   aPathE   = "";
   aDire    = "";
  &enEmpresaPINET = 0;
  &enErrorPINET   = 0;
|FIN;

|INCLUYE dscont_i;
|| *******************************************************************

|PROCESO Select; |TIPO 0;
  |SI #0#0 = "S";
      |C_M #0#1,1,"S"; |C_M #0#2,1,"S";
      |C_M #0#3,1,"S"; |C_M #0#4,1,"S";
      |PARA nPara1 = 5; |SI nPara1 [ 62; |HACIENDO nPara1 = nPara1 + 3;
            nNume1 = nPara1 + 1;
            nNume2 = nPara1 + 2;
            |C_M #0nPara1,1,"N"; #0nPara1 = 0;  |PINTA #0nPara1;
            |C_M #0nNume1,1,"N"; #0nNume1 = 0;  |PINTA #0nNume1;
                                 #0nNume2 = ""; |PINTA #0nNume2;
      |SIGUE;
   |SINO;
      |C_M #0#1,1,"N"; #0#1 = 1; |PINTA #0#1;
      |C_M #0#2,1,"N"; #0#2 = 0; |PINTA #0#2;
      |C_M #0#3,1,"N"; #0#3 = 99999; |PINTA #0#3;
      |C_M #0#4,1,"N"; #0#4 = 9;     |PINTA #0#4;
      |PARA nPara1 = 5; |SI nPara1 [ 62; |HACIENDO nPara1 = nPara1 + 3;
            nNume1 = nPara1 + 1;
            |C_M #0nPara1,1,"S";
            |C_M #0nNume1,1,"S";
      |SIGUE;
   |FINSI;
|FPRC;

|CALCULO MiraEmpresa; |TIPO 0;
   nInkey = FSalida;
   |SI nInkey = 2; |ACABA; |FINSI;

   nCampo = Campo - 1;
   |SI #0nCampo = 0; |Y #0Campo = 0; |ACABA; |FINSI;

   |SI #0#0 = "N";
       nEmp = #0nCampo;
       nPer = #0Campo;
       |HAZ SeldeEmpresa;
       |SI nSelError ! 0;
           |MENAV aMensa;
           |ERROR;
           |ACABA;
       |FINSI;
       nCampo = Campo + 1;
       #0nCampo = #canempre#1;  |PINTA #0nCampo;
   |FINSI;
|FCAL;

|CALCULO NoMasEmp; |TIPO 0;
     |SI #0Campo = 0;
         nNume1   = Campo + 1;
         #0nNume1 = 0;  |C_M #0nNume1, 1, "N";
         |PARA nCampo = Campo + 3;  |SI nCampo [ 62;  |HACIENDO nCampo = nCampo + 3;
               nNume1 = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
               #0nNume1 = 0;  |C_M #0nNume1, 1, "N";
         |SIGUE;
     |SINO;
         nNume1   = Campo + 1;
         |C_M #0nNume1, 1, "S";
         |PARA nCampo = Campo + 3;  |SI nCampo [ 62;  |HACIENDO nCampo = nCampo + 3;
               nNume1 = nCampo + 1;
               |C_M #0nCampo, 1, "S";
               |C_M #0nNume1, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|PROCESO SeldeEmpresa;
   |ABRE #canempre;
   aMensa    = "";
   nSelError = 0;
   #canempre#2 = nEmp;
   #canempre#3 = nPer;
   |LEE 010#canempre.=;
   |SI FSalida = 0;
       |SI #0#65 ! "T";
           |SI #canempre#21 ! #0#65;
               |SI #canempre#21 = "I";
                   aMensa = "    Empresa Inactiva";
               |SINO;
                   aMensa = "    Empresa Activa";
               |FINSI;
               nSelError = 1;
               |VETE Cierra;
           |FINSI;
       |FINSI;
   |SINO;
       nSelError  = 1;
       aMensa = "    No Existe Empresa ";
   |FINSI;

|ET Cierra;
   |CIERRA #canempre;
|FPRC;

|| ::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
|PROCESO LasBorro0;
    enEmpresaPINET = #canempre#2;
    |HAZ RestriccionPINET;
    |SI enErrorPINET = 1;  |ACABA;  |FINSI;


   |SI #0#65 ! "T";
       |SI #canempre#21 ! #0#65;
           |ACABA;
       |FINSI;
   |FINSI;

   |ATRI P;
   |PINTA 2203, "Borrando Datos de ... ";
   |ATRI 0;

   |PINTA 2312, #canempre#2;
   |PINTA 2319, #canempre#3;
   |PINTA 2322, #canempre#1;
   |PINTA 2375, #canempre#26;

   |DFICB aPath;
   aAlfa1 = #canempre#0;
   aPathE = aPath + ("000000" + aAlfa1) % -106;
   aDire = "";
   |DIRECTORIO aDire;
   aAlfa1 = aDire + "bin/agirm -r " + aPathE;
   |SYSTEM aAlfa1;

   #camoneda#0 = #canempre#2;
   #camoneda#1 = #canempre#3;
   |LEE 141#camoneda.=;
   |SI FSalida = 0;
       |BORRA 020#camoneda.a;
   |FINSI;
   |LIBERA #camoneda;

   |BORRA 020#canempre.a;
   |LIBERA #canempre;

|FPRC;

|DEFBUCLE LasBorro;
 #canempre#1;
 ;
 #0#1, #0#2;
 #0#3, #0#4;
 be;
 NULL, LasBorro0;
|FIN;


|| ::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

|CALCULO VoyaBorrar; |TIPO 3;
  |SI #0#66 = "NO";
      |ACABA;
  |FINSI;

  |CONFI "    SProceder a Borrar las Empresa Contables Seleccionadas";
  |SI FSalida ! 0;
      |ACABA;
  |FINSI;

  |BLIN 21;
  |BLIN 22;
  |BLIN 23;
  |CUADRO 2101 2480;
  |ATRI P;
  |PINTA 2303, "Empresa: ";
  |PINTA 2364, "Ejercicio: ";
  |ATRI 0;

  |ABRE #1;
  |SI #0#0 = "S";
      |BUCLE LasBorro;
  |SINO;
      |ABRE #2;
      |PARA nPara1 = 5; |SI nPara1 [ 62; |HACIENDO nPara1 = nPara1 + 3;
            nNume1 = nPara1 + 1;
            |SI #0nPara1 ! 0;
                #canempre#2 = #0nPara1;
                #canempre#3 = #0nNume1;
                |LEE 101#canempre.=;
                |SI FSalida = 0;
                    |HAZ LasBorro0;
                |FINSI;
            |FINSI;
      |SIGUE;
      |CIERRA #2;
   |FINSI;
   |CIERRA #1;
|FCAL;
||*********************************************************************
