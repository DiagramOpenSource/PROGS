|FICHEROS;
    any00002 #0;
    camaasil #1;
    anm00001 #3;
    anw00003 #4;

    canempre #30;
    caselem1 #998;

    anm00003 #7;         || Grupos Analiticas
    anm00013 #13;        || Grupos Analiticas  por apuntes
    anm0003@ #107;       || Espejo grupos analitica

    anw00014 #999;
|FIN;

|VARIABLES;
    &entrada = 1;
    txtot    = "Total cuenta";
    txsys    = "Suma y sigue";
    sw_con   = 0;
    x        = 0;
    sw_pinta = 0;
    sw       = 0;
    sw_acum  = 0;
    sw_print = 0;
    sw_error = 0;
    lin      = 0;
    cuenta   = "";
    cuen     = "";
    fecalf   = "";
    mensa1   = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
    traba1   = "";
    men1     = "     NO existe Grupo de Conceptos!";
    men2     = "     Longitud de Cuenta Fuera de Nivel !";
    long     = "  ";
    cta      = 0;
    cta1     = "";
    num      = 0;
    swdig    = 0;
    digito   = 0;
    alfa1    = "";
    alfa2    = "";
    alfa3    = "";
    alfa4    = "";
    nume1    = 0;
    nume2    = 0;
    nume3    = 0;
    finic    = @;
    sw_p     = 0;
    cant1    = 0;
    cant2    = 0;
    cant3    = 0;
    cant4    = 0;
    swsel    = 0;
    incta    = "";
    ficta    = "";
    nContador = 0;

    incta1 = "";
    ficta1 = "";
    swsel2 = 0;

    aMas       = "";
    aDef       = "";
    aFichero   = "";
    aQueQuiero = "";
    aNumCampos = "";
    nNumCampos = 0;
    nSwGApte   = 0;
    aCta2Gru   = "";
    ctanueva  = "";
    ctanom    = "";
    nImpAna    = 0;
    grupoc     = "";
    grupod     = 0;
    total      = 0;
    grupod2    = 0;
    total2     = 0;
    swgrupo    = 0;
    swgrupo2   = 0;
    ctaultima  = "";
    ctaultima2 = "";

    nPara1     = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoan_i;

|| *********************************************************************
||                             COMIENZO
|| *********************************************************************
|CALCULO AntesDesg; |TIPO 7;
 alfa1 = #0#43;
 |C_M #0#44, 1, alfa1;
|FCAL;

|CALCULO PorDef; |TIPO 0;
 |SI #0#42 = "G";
     #0#43 = "S"; |PINTA #0#43; |PTEC 802;
 |FINSI;
|FCAL;

|CALCULO cuenta; |TIPO 6;
     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;
|FCAL;

|CALCULO seleccio; |TIPO 0;
|SI #0#4 = "N";
    |PARA nume1 = 5; |SI nume1 [ 28; |HACIENDO nume1 = nume1 + 1;
          |CAMPO_MODIFICA #0nume1, 1, "S";
    |SIGUE;
    |CAMPO_MODIFICA #0#31, 1, "N";
    |CAMPO_MODIFICA #0#32, 1, "N";
    #0#31 = 1;       |PINTA #0#31;
    #0#32 = 999;     |PINTA #0#32;
|SINO;
    |PARA nume1 = 5; |SI nume1 [ 28; |HACIENDO nume1 = nume1 + 1;
          |CAMPO_MODIFICA #0nume1, 1, "N";
    |SIGUE;
    |CAMPO_MODIFICA #0#31, 1, "S";
    |CAMPO_MODIFICA #0#32, 1, "S";
|FINSI;
|FCAL;

|CALCULO fechad;
  |SI #0#2 < fec1; |O #0#2 > fec2;
      |MENAV mensa1;
      |ERROR;
  |FINSI;
|FCAL;

|CALCULO fechah;
  |SI #0#3 < fec1; |O #0#3 > fec2; |O #0#3 < #0#2;
      |MENAV mensa1;
      |ERROR;
  |FINSI;
|FCAL;

|CALCULO no_depar; |TIPO 7;
   |C_M #0#40, 1, eaDepar;
   |C_M #0#41, 1, eaDepar;
|FCAL;

|CALCULO nosubde; |TIPO 7;
   |C_M #0#29, 1, eaSubde;
   |C_M #0#30, 1, eaSubde;
|FCAL;

|| **********************************************************************
|PROGRAMA;
  |CLS;
  |CABEZA "Extractos Analitica Pantalla";
  |HAZ inicio;
  |HAZ Analitica;
  |SI  enSwAna = 0;
       alfa1 = "    CONTABILIDAD NO ANALITICA ";
       |SI eaAnNiv = "E";
           alfa1 = "    EMPRESA NO ANALITICA ";
       |FINSI;
       |MENAV alfa1;
       |ACABA;
  |FINSI;

  |DBASE aMas;
  aDef  = aMas + "def/anm00003";
  |CARGA_DEF aDef, anm0003@;
  |SI FSalida ! 0;
      |MENAV "    Error Cargado Fichero de Grupos Analiticos. Proceso Interrumpido";
      |ACABA;
  |FINSI;

  aQueQuiero = "|NC";
  aNumCampos = #999#aQueQuiero#;
  nNumCampos = aNumCampos;
  nNumCampos = nNumCampos - 1;

  aFichero = "14aw" + Usuario;
  |NOME_DAT #999 aFichero;
  |DELINDEX #999;

  #0#2 = fec1; #0#3 = fec2;

  FSalida = 1;
  |PARA;|SI FSalida = 1; |HACIENDO;
        |PINPA #0#0;
        |PINDA #0#0;                    || Pinta los datos por defecto
        |ENDATOS #1#0;                  || Seleccion informe
        |SI FSalida = 0;                || Si el conforme es igua a S
            |SI #48#27 = "S";
                |HAZ MultiEmpresa;
            |SINO;
                |DFICE dirfich0; |QBF dirfich0;
                |HAZ HazTodo;
            |FINSI;
            FSalida = 1;                || Vuelve al bucle
        |SINO;
            FSalida = 0;
        |FINSI;
  |SIGUE;

  |DESCARGA_DEF #107;
|FPRO;

|| *************************************************************************
||                          MEOLLO
|| *************************************************************************
|| ------------------------------------------------------
|| ... Segundo nivel de grupos
|CALCULO ver_grupo2;
 |SI #107#2 ] incta1; |Y #107#2 [ ficta1;
     swsel = 1;
     |ERROR10;
 |FINSI;
|FCAL;

|DEFBUCLE selegrupo2;
 #107#1002;
 ;
 aCta2Gru, 000;
 aCta2Gru, 999;
 e;
 NULL, ver_grupo2;
|FIN;

|PROCESO ver_grupo_apte;
 nSwGApte = 1;
 |SI #13#6 ] incta1; |Y #13#6 [ ficta1;
     swsel = 1;
     |ERROR10;
 |SINO;
     alfa1 = #13#6 % 103;
     |SI alfa1 = "GRU";
         aCta2Gru   = #13#6;
         |BUCLE selegrupo2;
         |SI swsel = 1;
             |ERROR10;
         |FINSI;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE selgruApte;
 #13#1;
 ;
 #1#0, #1#1, #1#2, #1#3, #1#29, 000;
 #1#0, #1#1, #1#2, #1#3, #1#29, 999;
 e;
 NULL, ver_grupo_apte;
|FIN;

|PROCESO ver_grupo;
 |SI #7#2 ] incta1; |Y #7#2 [ ficta1;
     swsel = 1;
     |ERROR10;
 |SINO;
     alfa1 = #7#2 % 103;
     |SI alfa1 = "GRU";
         aCta2Gru   = #7#2;
         |BUCLE selegrupo2;
         |SI swsel = 1;
             |ERROR10;
         |FINSI;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE selegrupo;
 #7#1;
 ;
 #1#29, 000;
 #1#29, 999;
 e;
 NULL, ver_grupo;
|FIN;

|PROCESO SelNuevo;
  |SI #1#29 ] incta1; |Y #1#29 [ ficta1;
      swsel = 1;
  |SINO;
     |SI alfa3 = "GRU";
         nSwGApte = 0;
         |BUCLE selgruApte;
         |SI nSwGApte = 0;
             |BUCLE selegrupo;
         |FINSI;
     |FINSI;
  |FINSI;
|FPRC;

|| ----------------------------------------------------
|PROCESO HazTodo;
  |C_V #4#9,1,"N"; |C_V #4#10,1,"S"; |C_V #4#24,1,"N"; |C_V #4#25,1,"N";
  alfa4 = "       Descripcion       ";
  |SI #0#42 = "R";
      |C_V #4#9,1,"S"; |C_V #4#10,1,"N"; |C_V #4#24,1,"N"; |C_V #4#25,1,"N";
      alfa4 = "       Referencia        ";
  |FINSI;
  |SI #0#42 = "G";
      |C_V #4#9,1,"N"; |C_V #4#10,1,"N"; |C_V #4#24,1,"S"; |C_V #4#25,1,"S";
      alfa4 = "  Grupo de Analitica     ";
  |FINSI;

  |HAZ impre;
  |HAZ borrar;
|FPRC;


|CALCULO concepto;
  sw_con = 0;
  |PARA x = 5; |SI x [ 28; |HACIENDO x = x + 1;
        |SI #0x  = #1#6;
            sw_con = 1;
            |SAL;
       |FINSI;
  |SIGUE;
|FCAL;

|CALCULO leecuen;
 #4#0 = #30#1;
 #4#1 = #1#29;

 #3#0 = #1#29;
 |LEE 040#3=;
 |SI FSalida = 0;
     #4#2 = #3#1;
 |FINSI;
 sw_pinta = 0;
 sw_print = 0;
|FCAL;

|CALCULO buclepan;
  sw_con = 1;
  |SI #0#4 = "N";  |HAZ concepto;  |FINSI;
  |SI sw_con = 0;  |ACABA;  |FINSI;

  |SI sw = 0;
      sw = 1;
      |HAZ borrar;
      |HAZ leecuen;
      cuenta = #1#29;
  |FINSI;

  |SI cuenta ! #1#29;
      sw_acum = 0;
      |SI sw_print = 1; |HAZ cambiapa1; |FINSI;
      |SI sw_error ! 0; |ACABA; |FINSI;
      |HAZ borrar;
      |HAZ leecuen;
      cuenta = #1#29;
   |FINSI;

   #4#18 = #1#0;                         || Diario
   #4#19 = #1#2;
   #4#22 = #1#3;
   nContador = nContador + 1;
   #4#23 = nContador;
   alfa1 = #1#1;
   #4#8  = (alfa1 % 102) + (alfa1 % 402); || Fecha
   #4#9  = #1#26;                        || Referencia
   #4#10 = #1#7;                         || Descripcion
   #4#11 = #1#8;                         || Signo
   #4#12 = #1#9;                         || Importe;
   #4#17 = #1#4;                         || Cuenta Contable
   |SI #4#11 = "D";
       #4#20 = #1#9;
       #4#21 = 0;
   |SINO;
       #4#21 = #1#9;
       #4#20 = 0;
   |FINSI;
   #4#24 = ctanueva;
   #4#25 = ctanom;

   |SI #1#1 ] #0#2; |Y #1#1 [ #0#3;
       |SI lin > 1200;
           |HAZ cambiapa2;
           sw_pinta = 0;
           |SI sw_error ! 0; |ACABA; |FINSI;
       |FINSI;

       |SI sw_pinta = 0; |HAZ pintacab; |FINSI;
       |HAZ suma_totales;

       #4#5  = #4#14;
       #4#6  = #4#15;
       #4#7  = #4#16;
       #4#13 = #4#16;

       Pos = lin;
       |PINDA #0#4;
       Pos = -1;
       lin = lin + 100;
       |HAZ impr_totales;
   |SINO;
       || .... suma saldos anteriores a la fecha de pantalla
       |HAZ suma_totales;
       #4#5  = #4#14;
       #4#6  = #4#15;
       #4#7  = #4#16;
       #4#13 = #4#16;
   |FINSI;
|FCAL;

|DEFBUCLE caextrpa0;
 #1#1;
 4, 6, 29, 26, 21, 28;
 #0#35, finic, 000000, 0000, #0#0, #0#31, incta, #0#33, #0#40, #0#29;
 #0#36, #0#3,  999999, 9999, #0#1, #0#32, ficta, #0#34, #0#41, #0#30;
 e;
 NULL, NULL, NULL, NULL, NULL, buclepan;
 #1#29, #1#1, #1#2, #1#3;
|FIN;

|| ///////////////////////////////////////
|PROCESO LeoAuxiliar;
 #1#0 = #999#0;
 #1#1 = #999#1
 #1#2 = #999#2
 #1#3 = #999#3
 |LEE 010#1=;

 #3#0 = #1#29;
 |LEE 040#3=;
 |SI FSalida ! 0; |DDEFECTO #3; |FINSI;
 ctanueva = #1#29;
 ctanom   = #3#1;

 |PARA nPara1 = 0; |SI nPara1 [ nNumCampos; |HACIENDO nPara1 = nPara1 + 1;
       #1nPara1 = #999nPara1;
 |SIGUE;
 |HAZ buclepan;
|FPRC;

|DEFBUCLE LeoAuxiliar;
 #999#1;
 ;
 "        ", "01.01.0000", 00, 000001, 0001;
 "zzzzzzzz", "31.12.2999", 99, 999999, 9999;
 ;
 NULL, LeoAuxiliar;
|FIN;
|| **********************************************************************

|PROCESO gragrup;
 #999#0 = #1#0;
 #999#1 = #1#1;
 #999#2 = #1#2;
 #999#3 = #1#3;
 #999#29 = ctanueva;
 |LEE 001#999=;
 |SI FSalida ! 0;
     |PARA nPara1 = 0; |SI nPara1 [ nNumCampos; |HACIENDO nPara1 = nPara1 + 1;
           #999nPara1 = #1nPara1;
     |SIGUE;
     #999#9  = 0;
     #999#29 = ctanueva;
     |GRABA 020#999c;
 |FINSI;
 #999#9  = #999#9 + nImpAna;
 |GRABA 020#999a;
|FPRC;

|| .....................................
|PROCESO ggrupos2;
 ctanueva = #107#2;
 nImpAna  = (grupod2 * #107#4 / 100) ! EURODB;
 |SI nImpAna ! 0;
     swsel2 = 0;
     |SI ctanueva ] #0#38; |Y ctanueva [ #0#39;
         swsel2 = 1;
         |HAZ gragrup;
     |FINSI;
 |FINSI;

 swgrupo2   = 1;
 ctaultima2 = #107#2;
 total2     = (total2 + nImpAna)   ! EURODB;
|FPRC;

|PROCESO fingrupo2;
 |SI swgrupo2 = 0; |ACABA; |FINSI;
 |SI total2 = grupod2; |ACABA; |FINSI;

 ctanueva = ctaultima2;
 nImpAna  = grupod2 - total2;
 |SI nImpAna ! 0;
     swsel2 = 0;
     |SI ctanueva ] #0#38; |Y ctanueva [ #0#39;
         swsel2 = 1;
         |HAZ gragrup;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE leegrupo2;
 #107#1002;
 ;
 aCta2Gru, 000;
 aCta2Gru, 999;
 e;
 NULL, ggrupos2, NULL, NULL, fingrupo2;
|FIN;

|PROCESO Subgrupo;
 aCta2Gru   = ctanueva;
 grupod2    = nImpAna;
 total2     = 0;
 swgrupo2   = 0;
 ctaultima2 = "";
 |BUCLE leegrupo2;
|FPRC;

|PROCESO TrataCtaGrupo;
 alfa3 = ctanueva % 103;
 |SI alfa3 ! "GRU";   ||Tratamiento normal de Cuenta Analitica
      swsel2 = 0;
      |SI ctanueva ] #0#38; |Y ctanueva [ #0#39;
          swsel2 = 1;
          |HAZ gragrup;
     |FINSI;
 |SINO;
     |SI #0#44 = "N";   || Si quiere grabar grupos
         swsel2 = 0;
         |SI ctanueva ] #0#38; |Y ctanueva [ #0#39;
             swsel2 = 1;
             |HAZ gragrup;
         |SINO;
             |HAZ Subgrupo;
         |FINSI;
     |SINO;
         |HAZ Subgrupo;
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO ggruposA;
 nSwGApte = 1;
 ctanueva = #13#6;
 nImpAna  = #13#9;

 |HAZ TrataCtaGrupo;
|FPRC;

|DEFBUCLE leegrApte;
 #13#1;
 ;
 #1#0, #1#1, #1#2, #1#3, grupoc, 000;
 #1#0, #1#1, #1#2, #1#3, grupoc, 999;
 e;
 NULL, ggruposA;
|FIN;

|PROCESO ggrupos;
 ctanueva = #7#2;
 nImpAna  = (grupod * #7#4 / 100) ! EURODB;

 swgrupo   = 1;
 ctaultima = #7#2;
 total     = (total + nImpAna)   ! EURODB;

 |SI nImpAna ! 0;
     |HAZ TrataCtaGrupo;
 |FINSI;
|FPRC;

|PROCESO fingrupo;
|| aqui

 |SI swgrupo = 0; |ACABA; |FINSI;
 |SI total = grupod; |ACABA; |FINSI;

 ctanueva = ctaultima;
 nImpAna  = (grupod - total) ! EURODB;
 |SI nImpAna ! 0;
     |HAZ TrataCtaGrupo;
 |FINSI;
|FPRC;

|DEFBUCLE leegrupo;
 #7#1;
 ;
 grupoc, 000;
 grupoc, 999;
 e;
 NULL, ggrupos, NULL, NULL, fingrupo;
|FIN;


|PROCESO BuscoOtros;
     nSwGApte = 0;
     grupoc  = ctanueva;
     |BUCLE leegrApte;
     |SI nSwGApte = 0;
         ctanueva = #1#29;
         swgrupo  = 0;
         grupod   = #1#9;
         ctaultima = "";
         total     = 0;
         |BUCLE leegrupo;
     |FINSI;
|FPRC;

|PROCESO CargaAuxiliar;
  sw_con = 1;
  |SI #0#4 = "N"; |HAZ concepto;  |FINSI;  || Seleccionar Conceptos
  |SI sw_con = 0; |ACABA;  |FINSI;

  swsel = 0;           || Solo comprueba si esta dentro de la seleccion
  alfa3 = #1#29 % 103;

  incta1 = #0#38;
  ficta1 = #0#39;
  |HAZ SelNuevo;
  |SI swsel = 0; |ACABA; |FINSI;

|| ... Ahora grabo tantas lineas de este apunte como cuentas seleccionadas

  nImpAna  = #1#9;
  ctanueva = #1#29;
  alfa3 = ctanueva % 103;
  |SI alfa3 ! "GRU";   ||Tratamiento normal de Cuenta Analitica
       |HAZ gragrup;
  |SINO;
       |SI #0#44 = "N";   || Si quiere grabar grupos
          swsel2 = 0;
          |SI ctanueva ] #0#38; |Y ctanueva [ #0#39;
              swsel2 = 1;
              |HAZ gragrup;
           |SINO;
               |HAZ BuscoOtros;
           |FINSI;
       |SINO;
           |HAZ BuscoOtros;
       |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE CargaAuxiliar;
 #1#1;
 4, 6, 26, 21, 28;
 #0#35, finic, 000000, 0000, #0#0, #0#31, #0#33, #0#40, #0#29;
 #0#36, #0#3,  999999, 9999, #0#1, #0#32, #0#34, #0#41, #0#30;
 e;
 NULL, CargaAuxiliar;
|FIN;

|PROCESO CreaAuxiliar;
   |DELINDEX #999;

   |ABRE #999;
   |BUCLE CargaAuxiliar;
   |CIERRA #999;
|FPRC;

|PROCESO PintaEmpresa;
  alfa1 = " " * 79;
  |PINTA 0502, alfa1;
  alfa1 = " Empresa: " + #30#2 + "  Periodo: " + #30#3 + "  Nombre: " + #30#1;
  |ATRI R;  |PINTA 0502, alfa1;
  |PINTA  0929, alfa4;
  |ATRI N;
|FPRC;

|CALCULO impre;
  lin = 100;
  |PINPA #0#4; |HAZ PintaEmpresa;

  |SI #0#43 = "S";
      |HAZ CreaAuxiliar;
  |FINSI;

  |ABRE #3;
  finic = fec1;
  |SI #0#37 = "N"; finic = #0#2; |FINSI;
  incta = #0#38;
  ficta = #0#39;
  |SI #0#43 = "S";
      |ABRE #1;
      |BUCLE LeoAuxiliar;
      |CIERRA #1;
  |SINO;
      |BUCLE caextrpa0;
  |FINSI;
  |CIERRA #3;

  |SI sw_error = 0;
      |PAUSA;
      |SI FSalida = 7; sw_error = 2; |FINSI;
  |FINSI;
  |SI sw_error = 2; |Y #48#27 = "S"; |ERROR10; |FINSI;
  sw = 0;
  sw_error = 0;
|FCAL;

|CALCULO cambiapa1;
  |ATRI R;
  |PINTA 2203, txtot;
  |ATRI 0;
  |HAZ cambiapa;
|FCAL;

|CALCULO cambiapa2;
  |ATRI R;
  |PINTA 2203, txsys;
  |ATRI 0;
  |HAZ cambiapa;
|FCAL;

|CALCULO cambiapa;
 sw_error = 0;
 |PAUSA;
 |SI FSalida = 1; |O FSalida = 7;
     |SI FSalida = 1; sw_error = 1; |FINSI;
     |SI FSalida = 7; sw_error = 2; |FINSI;
     |ERROR10;
 |SINO;
     |PINPA #0#4; |HAZ PintaEmpresa;
     lin = 100;
 |FINSI;
|FCAL;

|CALCULO pintacab;
 |PINTA #4#1;
 |PINTA #4#2;
 |SI sw_print = 1;
     |PINTA #4#5;
     |PINTA #4#6;
     |PINTA #4#7;
 |FINSI;
 sw_pinta = 1;
|FCAL;

|CALCULO borrar;
 |DDEFECTO #4;
 sw_print = 0;
 sw_pinta = 0;
 nContador = 0;
|FCAL;

|CALCULO impr_totales;
 |PINTA #4#14;
 |PINTA #4#15;
 |PINTA #4#16;
|FCAL;

|CALCULO suma_totales;
 |SI #1#8 = "D";
     #4#14 = #4#14 + #1#9;
 |SINO;
     #4#15 = #4#15 + #1#9;
 |FINSI;
  #4#16 = #4#14 - #4#15;
 sw_print = 1;
|FCAL;

|| **********************************************************************
|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #3 dirfich0;
   |PATH_DAT #7 dirfich0;
   |PATH_DAT #13 dirfich0;
   |PATH_DAT #107 dirfich0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 000001;
 999999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;
