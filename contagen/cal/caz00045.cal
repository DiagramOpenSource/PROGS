|FICHEROS;
   caz00045 #0;
   camw0006 #6;
   canempre #30;
   caw00029 #999,MANTE;

   ca8ctrle #400;
|FIN;

|VARIABLES;
   nPara1   = 0;
   nNume1   = 0;
   nNume2   = 0;
   nNume3   = 0;
   nNume4   = 0;
   fFecha   = @;
   nPer     = 0;
   nEmp     = 0;
   nCampo   = 0;
   nInkey   = 0;
   aAlfa    = "";
   aAlfa1   = "";
   aAlfa2   = "";
   aMensa   = "";

   nSelError  = 0;
   nLaEmpresa = 0;
   nEstadoEmp = 0;

   aPath    = "";
   aPathE   = "";
   aDire    = "";

   nPideConforme = 0;
   Existe   = 0;
   nCanal   = 0;
   aPassword = "";

   nYaExiste = 0;
   aFichero  = "";
   aEmpresa  = "";
   nSwAlguna = 0;
   nImprime  = 0;
   informe   = "caz00045";
   &enEmpArch = 0;
   &enPerArch = 0;
   &eaPathArc = "";
   &eaUsuaArc = "";
   &eaNomArch = "";
   &enAnyArch = 0;
   &eaNifArch = "";
   &enEmpNuev = 0;
   &enPerNuev = 0;
   &eaNomArchivo = "";
   &efFecArch    = @;
   &enOperar     = 0;

  &enEmpresaPINET = 0;
  &enErrorPINET   = 0;

   nDEjer    = 0;
   nHEjer    = 0;
   nDEmpresa = 0;
   nHEmpresa = 0;
   fDFecha   = @;
   fHFecha   = @;
|FIN;

|INCLUYE dscont_i;

|| ********************************************************************
|| **** DATOS PANTALLA
|| ********************************************************************
|PROCESO ElEjer;
   aAlfa = #0#0; aAlfa = aAlfa % -102; #0#4 = aAlfa;
   aAlfa = #0#1; aAlfa = aAlfa % -102; #0#5 = aAlfa;
|FPRC;

|PROCESO Inf6;
   #6#2  = nDEmpresa;
   #6#26 = nDEjer;
   #6#37 = fDFecha;
   #6#38 = "        ";
|FPRC;

|PROCESO Sup6;
   #6#2  = nHEmpresa;
   #6#26 = nHEjer;
   #6#37 = fHFecha;
   #6#38 = "zzzzzzzz";
|FPRC;

|PROCESO VeoEmpresa;
   nInkey = FSalida;

   |SI nInkey = 10;
       nDEjer    = 00;           nHEjer    = 99;
       nDEmpresa = 0;            nHEmpresa = 99999;
       fHFecha   = "01.01.0000"; fHFecha   = "31.12.2999";

       |SI Campo ] 1; nDEjer = #0#4;    |FINSI;
       |SI Campo ] 2; nHEjer = #0#5;    |FINSI;
       |SI Campo ] 3; nDEmpresa = #0#2; |FINSI;

       |ABRE #6;

       |SI Campo = 0; |O Campo = 1;
           aAlfa = #0Campo; aAlfa = aAlfa % -102;
           #6#26 = aAlfa;
       |FINSI;
       |SI Campo = 2; |O Campo = 3;
           #6#2  = #0Campo;
       |FINSI;

       |CONSULTA_F_CLAVE #3#6,Inf6, Sup6;
       |SI FSalida = 0;
           #0#0 = 2000 + #6#26;   |PINTA #0#0;
           #0#1 = 2000 + #6#26;   |PINTA #0#1;
           #0#2 = #6#2;           |PINTA #0#2;
           #0#3 = #6#2;           |PINTA #0#3;
       |SINO;
           |SI Campo = 0; |O Campo = 1;
               #0Campo = 2000 + #6#26;
           |FINSI;
           |SI Campo = 2; |O Campo = 3;
               #0Campo = #6#2;
           |FINSI;
           |PINTA #0Campo;
       |FINSI;

       |CIERRA #6;
  |FINSI;
|FPRC;
|| ::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

|PROCESO LasRecojo0;
  enEmpresaPINET = #6#2;
  |HAZ RestriccionPINET;
  |SI enErrorPINET = 1;  |ACABA;  |FINSI;

  aEmpresa = #6#2;
  aEmpresa = ("00000" + aEmpresa) % -105;

  |DDEFECTO #999;
  |SI #06#26 < 80;
      #999#0 = 2000 + #6#26;
  |SINO;
      #999#0 = 1900 + #6#26;
  |FINSI;
  #999#1 = #6#2;
  #999#2 = #6#3;
  #999#3 = #6#1;
  #999#4 = #6#8;
  #999#5 = #6#20;
  #999#6 = #6#26;
  #999#7 = #0#11;
  #999#8 = #0#10;
  #999#10 = #6#37;
  #999#11 = #6#38;
  #999#12 = #6#39;
  #999#13 = #6#3;
  |SI #0#12 ! -1;
      #999#13 = #0#12;
  |FINSI;
  #999#14 = #6#2;

  #30#2 = #999#14;
  #30#3 = #999#13;
  |LEE 041#30=;
  |SI FSalida = 0;
      #999#9 = "SI";
  |FINSI;
  |GRABA 040#999n;

  |LIBERA #6;
  nSwAlguna = 1;
|FPRC;

|DEFBUCLE LasRecojo;
   #6#1;
   ;
   #0#2, #0#4;
   #0#3, #0#5;
   be;
   NULL, LasRecojo0;
|FIN;
|| ////////////////////////////////////////////////////////////////////////

|PROCESO AltaLaEmpresa;
   nNume1 = enEmpresa;
   nNume2 = enPeriodo;

   |ABRE #30;
   #30#2 = #999#14;
   #30#3 = #999#13;
   |LEE 141#30=;
   nEstadoEmp = FSalida;
   |SI nEstadoEmp ! 0;
       |DDEFECTO #30;
       #30#0 = (#999#14 * 10) + #999#13;
       #30#2 = #999#14;
       #30#3 = #999#13;
       |GRABA 020#30b;
   |FINSI;

   #30#1 = #6#1;
   |PARA nPara1 = 4; |SI nPara1 [ 35; |HACIENDO nPara1 = nPara1 + 1;
         #30nPara1 = #6nPara1;
   |SIGUE;
   #30#25 = "";  ||Vacio
   |GRABA 020#30a;
   |LIBERA #30;
   |CIERRA #30;

   swOperacion = 3;
   enEmpresa = #30#2;
   enPeriodo = #30#3;
   eaMoneda  = #30#28;
   |SUB_EJECUTA_NP "camoneda";

   |ABRE #400;
   |DDEFECTO #400;
   #400#0 = #30#2;
   #400#1 = #30#3;
   |LEE 141#400=;
   |SI FSalida ! 0;
       |DDEFECTO #400;
       #400#0 = #30#2;
       #400#1 = #30#3;
       |GRABA 020#400b;
   |FINSI;
   #400#2 = #6#41;
   fFecha = ~;
   #400#6 = fFecha;
   #400#7 = #0#8;
   |GRABA 020#400a;
   |LIBERA #400;
   |CIERRA #400;

   |SI nEstadoEmp ! 0;
       |DBASE aAlfa1;
       aAlfa2 = #30#0; aAlfa2 = ("000000" + aAlfa2) % -106;
       aAlfa1 = aAlfa1 + "fich/" + aAlfa2;
       |MKDIR aAlfa1;
   |FINSI;

   enEmpresa = nNume1;
   enPeriodo = nNume2;
|FPRC;

|PROCESO ConsultaMw0006;

       |PUSHV 0501 2380;
       |PINPA #0#6;
       |PINDA #0#6;

       |ATRI P;
                |PINTA 2302, "DESTINO: EMPRESA: "; |ATRI 0;
                |PINTA 2320, #999#14;
       |ATRI P; |PINTA 2327, "PERIODO: ";          |ATRI 0;
                |PINTA 2336, #999#13;

       |PINTA 2345, "CONTINUAR : ";
       aAlfa = "S";
       E_inf = "SNsn";
       E_sup = 1;
       aAlfa = 2357 ? 0;
       aAlfa = aAlfa > " ";
       |POPV;
|FPRC;

|PROCESO LeoAuxiliar;
   |DDEFECTO #6;
   #6#2  = #999#1;
   #6#26 = #999#6;
   |LEE 141#6=;
   |SI FSalida ! 0;
       |ACABA;
   |FINSI;

   |SI nPideConforme = 1;
       |HAZ ConsultaMw0006;
       |SI aAlfa = "N"; |ACABA; |FINSI;
   |FINSI;

   |ATRI P;
   |PINTA 2203, "Recuperando Datos de ... ";
   |ATRI 0;

   |PINTA 2312, #6#2;
   |PINTA 2319, #6#3;
   |PINTA 2322, #6#1;
   |PINTA 2375, #6#26;

   |HAZ AltaLaEmpresa;

|| Aqui Recuperar
   eaNomArchivo = "z" + #999#0 + (("00000" + #999#1) % -105);

   enEmpArch = #6#2;
   enPerArch = #6#3;
   |HAZ CalculoPath;
   eaPathArc = aPath;
   eaNomArch = #6#1;
   enAnyArch = #6#26;
   eaNifArch = #6#8;
   enEmpNuev = #999#14;
   enPerNuev = #999#13;
   efFecArch = #6#37;

   |SUB_EJECUTA "caz00025;ARCHIVAR";
   |SI swerror = 1;
       |ACABA;
   |FINSI;

   |SI #999#7 = "S";
       |BORRA 020#6a;

       aFichero = eaPathArc; |QBF aFichero;
       aFichero = aFichero + eaNomArchivo + ".tgz";
       |FBORRA aFichero;
   |FINSI;
   |LIBERA #6;

   |SI nImprime = 1;
       |PRINT;
   |FINSI;

   nSwAlguna = nSwAlguna + 1;
|FPRC;

|DEFBUCLE LeoAuxiliar;
   #999#1;
   8;
   #0#0, 00001, 0, "S";
   #0#1, 99999, 9, "S";
   e;
   NULL, LeoAuxiliar;
|FIN;

|| ////////////////////////////////////////////////////////////////////////
|PROCESO ClaveAlta999;
   #999#0 = #0#0;
   #999#1 = 00001;
   #999#2 = 0;
|FPRC;

|PROCESO ClaveBaja999;
   #999#0 = #0#1;
   #999#1 = 99999;
   #999#2 = 9;
|FPRC;

|CALCULO VoyaArchivar; |TIPO 12;
   |SI #0#14 = "NO";
       |ACABA;
   |FINSI;

   aFichero = ("1b"+ Usuario) % 108;
   |NOME_DAT #999 aFichero;
   |DELINDEX #999;
   nSwAlguna = 0;

   |ABRE #999;
   |ABRE #30;
   |BUCLE LasRecojo;
   |CIERRA #30;
   |CIERRA #999;

   enOperar = 1;
   |SI nSwAlguna ! 0;

       |PINPA #0#999;
       |C_V #999#14, 1, "S";  |C_V #999#13, 1, "S";
       |PINTA 0972, "ÂÄÄÄÄÄÄÄ¿";
       |ATRI R; |PINTA 1064, "Recupera";
                |PINTA 1073, "Emp/Per";  |ATRI 0;
                |PINTA 1080, "³";
                |PINTA 1180, "³";
                |PINTA 1280, "³";
                |PINTA 1380, "³";
                |PINTA 1480, "³";
                |PINTA 1580, "³";
                |PINTA 1680, "³";
                |PINTA 1780, "³";
                |PINTA 1880, "³";
                |PINTA 1980, "³";
                |PINTA 2080, "³";
                |PINTA 2180, "³";
       |PINTA 2272, "ÁÄÄÄÄÄÄÄÙ";

       |HAZ PonBotonW29;
       |ENTLINEAL #1#999, -2, 4, 21, 0, ClaveAlta999, ClaveBaja999;
       |HAZ QuitaBotonW29;

   |SINO;
       |MENAV "    No existen Empresas en la Seleccion";
       |ACABA;
   |FINSI;

   |CONFI "    SProceder a Recuperar las Empresa Contables Seleccionadas";
   |SI FSalida ! 0;
       |ACABA;
   |FINSI;

   nPideConforme = 0;
   |CONFI "0000SDesea dar Conformidad a cada una de las Empresas";
   |SI FSalida = 0;
       nPideConforme = 1;
   |FINSI;

   nImprime = 0;
   |SI #0#13 = "S";
      nImprime = 1;
      |IMPRE 0;
      |SI FSalida ! 0;
          |MENAV "0000No existe Impresora";
          |CONFI "0000SContinuar con el Proceso de Recuperacion";
          |SI FSalida ! 0; |ACABA; |FINSI;
          nImprime = 0;
      |SINO;
          |INFOR informe;
          |SI FSalida ! 0;
              |MENAV "0000No existe Informe";
              |FINIMP;
              nImprime = 0;
          |FINSI;
      |FINSI;
  |FINSI;

  |BLIN 21;
  |BLIN 22;
  |BLIN 23;
  |CUADRO 2101 2480;
  |ATRI P;
  |PINTA 2303, "Empresa: ";
  |PINTA 2364, "Ejercicio: ";
  |ATRI 0;

  nSwAlguna = 0;
  |ABRE #6;
  |BUCLE LeoAuxiliar;
  |CIERRA #6;

  |SI nSwAlguna ! 0;
      |SI nImprime = 1; |PIEINF; |FINSI;
      aAlfa = "0000Empresas Recuperadas";
      |SI nSwAlguna = 1;
          aAlfa = "0000Empresa Recuperada";
      |FINSI;
      |QBF aAlfa; aAlfa = aAlfa % 180;
  |SINO;
      aAlfa = "0000No se ha recuperado Ninguna Empresa";
  |FINSI;
  |MENAV aAlfa;
  |SI nImprime = 1;
      |FININF;
      |FINIMP;
  |FINSI;
|FCAL;

||*********************************************************************
|PROCESO CalculoPath;  || Crea Directorio HistoricoConta
  |DBASE aBase;
  aPath = aBase + "historico";

  |MKDIR aPath;
  aPath  = aPath + "/";
|FPRC;
||*********************************************************************

|PROGRAMA;
     |SI PARAMETRO = "";
         |CUADRO 0501 2380;
         |BLANCO 0602 2279;
         |PINTA 1320, "Introduzca Password : ";
         E_inf = "";
         E_sup = "20";
         aPassword = 1342 ? -1;
         |SI aPassword ! "<MYPASWD>     ";
             |MENAV "     Permiso denegado.";
             |ACABA;
         |FINSI;
     |FINSI;

     |HAZ inicio;

     |ABRE #6;
     |SELECT #3#6;
     |LEE 041#6p;
     |SI FSalida = 0;
         #0#6 = #6#37;
     |FINSI;
     |LEE 041#6u;
     |SI FSalida = 0;
         #0#7 = #6#37;
     |FINSI;
     |SELECT #1#6;
     |CIERRA #6;

     #0#8 = Usuario % 108;
     #0#9 = #0#8;

     |CLS;
     |CABEZA "Desarchivar Empresas";
     |PINPA #0#0;
     |PINDA #0#0;

     |ENDATOS #1#0;
|FPRO;
