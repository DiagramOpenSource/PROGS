|FICHEROS;
  caivache #0;
  cacuenta #1;
  camaasil #2; ||Asientos.
  camaniva #3; ||Iva Cabecera
  caivalin #4; ||Iva Lineas

  canempre #30;
  caselem0 #997;
  caselem1 #998;
  caw00038 #999;
|FIN;

|VARIABLES;
  nSalir  = 0;
  vic     = 0;
  cta     = 0;
  long    = "";
  sw      = 0;
  alfa1   = "";
  alfa2   = "";
  alfa3   = "";
  swvale  = 1;
  importe = 0;
  importd = 0;
  importh = 0;
  inform1 = "caivache";
  inform2 = "caivach0";
  informe = "";
  &tipo   = 0;
  swimp   = 0;
  nNoVale = 0;
  nSwOp   = 0;

  nEmpMulti = 0;
  &eSwMul   = 0;
  &espe15   = "";
  &espe16   = 0;
  nSwOpera  = 0;
  nSw       = 0;

  CodigoEmpresa  = 0;
  PeriodoEmpresa = 0;
  dirfichOri     = "";
  nSitua         = 0;
  fDFecha    = @;
|FIN;

|INCLUYE dscont_i;

|CALCULO cuenta; |TIPO 6;
     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;
     |HAZ SacaNivel;
|FCAL;

|CALCULO cuefin; |TIPO 0;
     |ABRE #1;
     #1#0 = #0#0;
     #1#1 = #0#1;
     |LEE 001#1=;
     |SI #1#3 = "N";|Y FSalida = 0;
         |MENAV "    Cuenta sin Pase Directo ...";
         |ERROR;
     |FINSI;
     |CIERRA #1;

    #0#4 = "T";
    alfa1 = #0#0; |QBF alfa1;
    alfa2 = alfa1 % 101;
    |SI alfa2 = "4";
        alfa3 = alfa1 % 103;
        |SI alfa3 = "430"; |O alfa3 = "440"; #0#4  = "R"; #0#11 = "F"; |FINSI;
        |SI alfa3 = "400"; |O alfa3 = "410"; #0#4  = "S"; #0#11 = "F"; |FINSI;
        |SI alfa3 = "477"; #0#4  = "R"; #0#11 = "I"; |FINSI;
        |SI alfa3 = "472"; #0#4  = "S"; #0#11 = "I"; |FINSI;
    |FINSI;
    |SI alfa2 = "5"; #0#4  = "N"; #0#11 = "P"; |FINSI;
    |SI alfa2 = "6"; #0#4  = "S"; #0#11 = "B"; |FINSI;
    |SI alfa2 = "7"; #0#4  = "R"; #0#11 = "B"; |FINSI;
    |PINTA #0#4;
    |PINTA #0#11;
|FCAL;

|PROCESO defectos;
  alfa1 = Contenido;
  |SI alfa1 ! #0#23;
      |SI #0#23 = "N";
          #0#2  = fec1;
      |SINO;
          #0#2  = "01.01.2000";
      |FINSI;
      |PINTA #0#2;
      #0#3 = fec2; |PINTA #0#3;
 |FINSI;
|FPRC;

|CALCULO fechas; |TIPO 0;
  |SI #0#23 = "N";
      fDFecha = fec1;
      |SI enSwSelFc = 1; |Y Campo ! 13;  fDFecha = "01.01.0000"; |FINSI;
      |SI #0Campo > fec2;|O #0Campo < fDFecha;
          |MENAV "    Fecha fuera de Periodo ...";
          |ERROR;
      |FINSI;
  |FINSI;
|FCAL;

|CALCULO nodepar; |TIPO 7;
 |C_M #0#7,1,eaDepar;
 |C_M #0#8,1,eaDepar;
|FCAL;

|CALCULO nosubde; |TIPO 7;
 |C_M #0#9, 1,eaSubde;
 |C_M #0#10,1,eaSubde;
|FCAL;


|| ====================================== PROCESO INFORME
|PROCESO impLineas;
 #0#18  = #4#4;
 |SI #0#11 = "B";
     #0#17  = #4#12;
     #0#19 = #4#6;
 |FINSI;
 |SI #0#11 = "I";
     #0#17  = #4#13;
     #0#19 = #4#8;
 |FINSI;
 |SI #0#11 = "R";
     #0#17  = #4#14;
     #0#19 = #4#10;
 |FINSI;
 |SI #0#11 = "D";
     #0#17  = #4#15;
     #0#19 = #4#11;
 |FINSI;
 |SI #0#11 = "P";
     #0#17  = #4#23;
     #0#18  = #4#19;
     #0#19 = #4#22;
 |FINSI;
 |PARA vic = 1; |SI vic [ #0#6; |HACIENDO vic = vic + 1;
       importe   = #2#9 * vic;
       importd = importe - #0#5;
       importh = importe + #0#5;
       |SI #0#19 ] importd; |Y #0#19 [ importh;
            swvale = 1;
            tipo   = 1;
            |SI nSwOp = 0;
                |PRINT;
            |FINSI;
            |SAL;
       |FINSI;
 |SIGUE;
|FPRC;

|DEFBUCLE Lineas;
 #4#1;
 ;
 #3#0, #3#1, 00;
 #3#0, #3#1, 99;
 ;
 NULL, impLineas;
|FIN;

|PROCESO imprimir;
 #0#13 = #3#4;  ||Fecha
 #0#14 = #3#0;  ||Libro
 #0#15 = #3#2;  ||Serie
 #0#16 = #3#3;  ||Factura

 |SI #0#11 = "F";
     #0#17  = #3#12;
     #0#18  = #3#28;
     #0#19 = #3#21;
     |PARA vic = 1; |SI vic [ #0#6; |HACIENDO vic = vic + 1;
           importe   = #2#9 * vic;
           importd = importe - #0#5;
           importh = importe + #0#5;
           |SI #0#19 ] importd; |Y #0#19 [ importh;
                swvale = 1;
                tipo   = 1;
                |SI nSwOp = 0;
                    |PRINT;
                |FINSI;
                |SAL;
           |FINSI;
     |SIGUE;
 |SINO;
     |BUCLE Lineas;
 |FINSI;
 |SI nSwOp = 1; |Y swvale = 1;
     nNoVale = 0;
     |ERROR10;
 |FINSI;
|FPRC;

|DEFBUCLE camaniva;
 #3#5;
 36;
 #0#2, 00, 0000000000, #0#12;
 #0#3, 99, 9999999999, #0#12;
 ;
 NULL,imprimir;
|FIN;

|PROCESO Facturas_Auxi;
   dirfich0 = #999#6;
   |QBF dirfich0;

   |PATH_DAT #3 dirfich0;
   |PATH_DAT #4 dirfich0;

   |ABRE #3;
   #3#0 = #999#1;
   #3#1 = #999#2;
   |LEE 001#3=;
   |SI FSalida = 0;
       |HAZ imprimir;
   |FINSI;
   |CIERRA #3;
|FPRC;

|DEFBUCLE FrasAuxi;
   #999#3;
   7;
   "               ", #0#2, 00, 0000000001, #0#12;
   "zzzzzzzzzzzzzzz", #0#3, 99, 9999999999, #0#12;
   ;
   NULL, Facturas_Auxi;
|FIN;

|PROCESO MiroSi;
  swvale  = 0;
  nNoVale = 1;
  nSwOp   = 1;
  |SI #0#4 = "S"; |O #0#4 = "T";
      #0#12 = "S";
      |SI #0#23 = "N";
          |BUCLE camaniva;
      |SINO;
          |BUCLE FrasAuxi;
      |FINSI;
      |SI nNoVale = 0; |ACABA; |FINSI;
  |FINSI;
  |SI #0#4 = "R"; |O #0#4 = "T";
      #0#12 = "R";
      |SI #0#23 = "N";
          |BUCLE camaniva;
      |SINO;
          |BUCLE FrasAuxi;
      |FINSI;
      |SI nNoVale = 0; |ACABA; |FINSI;
  |FINSI;
  |SI #0#4 = "N"; |O #0#4 = "T";
      #0#12 = "N";
      |SI #0#23 = "N";
          |BUCLE camaniva;
      |SINO;
          |BUCLE FrasAuxi;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO camaasil;
 |SI Haybasica = 0
     |SI #camaasil#21 < #0#7; |O #camaasil#21 > #0#8;
         |ACABA;
     |FINSI;
 |SINO;
     nDepar = #camaasil#21;
     |SI nDepar < #0#7; |O nDepar > #0#8;  ||desde/hasta actividad
         |ACABA;
     |FINSI;
 |FINSI;

 |SI #0#20 = "S"; |Y #camaasil#18 = " "; |ACABA; |FINSI;
 |SI #0#20 = "N"; |Y #camaasil#18 ! " "; |ACABA; |FINSI;

 |SI #0#21 = "S";
     |HAZ MiroSi;
     |SI nNoVale = 1; |ACABA; |FINSI;
 |FINSI;

  nSwOp  = 0;
  swvale = 0;
  tipo   = 0;
  |PRINT;
  swimp  = 1;

  |SI #0#4 = "S"; |O #0#4 = "T";
      #0#12 = "S";
      |SI #0#23 = "N";
          |BUCLE camaniva;
      |SINO;
          |BUCLE FrasAuxi;
      |FINSI;
  |FINSI;
  |SI #0#4 = "R"; |O #0#4 = "T";
      #0#12 = "R";
      |SI #0#23 = "N";
          |BUCLE camaniva;
      |SINO;
          |BUCLE FrasAuxi;
      |FINSI;
  |FINSI;
  |SI #0#4 = "N"; |O #0#4 = "T";
      #0#12 = "N";
      |SI #0#23 = "N";
          |BUCLE camaniva;
      |SINO;
          |BUCLE FrasAuxi;
      |FINSI;
  |FINSI;
  |SI swvale = 0;
      tipo = 3;
      |PRINT;
  |FINSI;
|FPRC;

|DEFBUCLE camaasil;
   #2#3;
   28;
   #0#0, #0#2, 000000, 0001, #0#9;
   #0#0, #0#3, 999999, 9999, #0#10;
   ;
   NULL, camaasil;
|FIN;

|PROCESO HazTodo;
  |SI eSwMul ! 0; enEjercicio = #caselem0#4; |FINSI;
|| Lee otra vez la cuenta porque es proceso multiempresa
 |ABRE #1;
 #1#0 = #0#0; || Cuenta Contable
 |LEE 010#1=;
 |SI FSalida ! 0;
     |CIERRA #1;
     |ACABA;
 |FINSI;
 #0#1 = #1#2;
 |CIERRA #1;

 |SI nEmpMulti = -1; |O eSwMul = 0;
     swimp = 0;
     nEmpMulti = #30#2;
 |FINSI;
 |SI eSwMul ! 0; |Y nEmpMulti ! #30#2;
     |SI swimp ! 0;
         |PIEINF;
     |FINSI;
     swimp = 0;
 |FINSI;

 |BUCLE camaasil;

 |SI eSwMul = 0;
     |SI swimp ! 0;
         |PIEINF;
     |FINSI;
 |FINSI;
|FPRC;

|CALCULO engancha; |TIPO 3;

 |IMPRE 0;
 |SI FSalida ! 0; |ACABA; |FINSI;

 informe = inform1;
 |SI #0#21 = "X"; informe = inform2; |FINSI;

 |INFOR informe;
 |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

 |SI Haybasica = 1;
     |SI #0#8 = "zz"; #0#8 = "99"; |FINSI;
 |FINSI;

 nSitua = 0;
 eSwMul = 0;
 |SI #0#23 = "N";
     |SI #48#27 = "S";
         |HAZ MultiEmpresa;
     |SINO;
         |DFICE dirfich0; |QBF dirfich0;
         |HAZ HazTodo;
     |FINSI;
 |SINO;
     eSwMul = 1;
     |SI #48#27 = "S";  eSwMul = 2; |FINSI;
     |HAZ MultiEjercicio;
 |FINSI;

 |FININF;
 |FINIMP;

 |SI nSitua = 1;
    cod1 = CodigoEmpresa;
    cod2 = PeriodoEmpresa;
    emEmp = 1;  || Sin mensaje
    |HAZ leelo;
    |PATH_DAT #1 dirfichOri;
    |PATH_DAT #2 dirfichOri;
    |PATH_DAT #3 dirfichOri;
    |PATH_DAT #4 dirfichOri;
 |FINSI;
|FCAL;

|| **********************************************************************

|PROCESO MultiEmp1;
   nSitua = 1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #2 dirfich0;
   |PATH_DAT #3 dirfich0;
   |PATH_DAT #4 dirfich0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
    #caselem1#1;
    ;
    000001;
    999999;
    ;
    NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;

|PROCESO PasoAuxiliar;
  #999#0 = #998#2;   || Periodo Contable
  #999#1 = #3#0;     || Libro
  #999#2 = #3#1;     || Registro
  #999#3 = #3#12;    || Cuenta Contable
  #999#4 = ""        || #3#14 Nif en blanco para ordenar por fecha
  #999#5 = #3#4;     || Fecha Factura
  #999#6 = #998#3;   || Path
  #999#7 = #3#36;    || R/S/N
  |GRABA 000#999n;
  nSw = 1;
|FPRC;

|DEFBUCLE PasoAuxiliar;
   #3#5;
   36;
   #0#2, 00, 0000000000, #0#12;
   #0#3, 99, 9999999999, #0#12;
   ;
   NULL, PasoAuxiliar;
|FIN;

|PROCESO MultiEmpEjer1;
   |SI enSwSelFc = 0;
       |SI #0#2 > #caselem1#5; |O #0#3 < #caselem1#4;
           |ACABA;  || Fuera de la seleccion
       |FINSI;
   |FINSI;
   |SI nSwOpera = 1;

       |HAZ MultiEmp1;

   |SINO;
       dirfich0 = #caselem1#3; |QBF dirfich0;

       |PATH_DAT #3 dirfich0;
       |PATH_DAT #4 dirfich0;

       |SI #0#4 = "S"; |O #0#4 = "T";
           #0#12 = "S"; |BUCLE PasoAuxiliar;
       |FINSI;
       |SI #0#4 = "R"; |O #0#4 = "T";
           #0#12 = "R"; |BUCLE PasoAuxiliar;
       |FINSI;
       |SI #0#4 = "N"; |O #0#4 = "T";
           #0#12 = "N"; |BUCLE PasoAuxiliar;
       |FINSI;
       nSw = 1;
   |FINSI;
|FPRC;

|DEFBUCLE MultiEmpEjer;
   #caselem1#2;
   ;
   #997#0, "01.01.0000";
   #997#0, "31.12.2999";
   ;
   NULL,MultiEmpEjer1;
|FIN;

|PROCESO MultiEjer1;
   nSw = 0;
   nSwOpera = 0;
   |DELINDEX #999;
   |ABRE #999;
   |BUCLE MultiEmpEjer;
   |CIERRA #999;

   nEmpMulti = -1;
   |SI nSw ! 0;
       nSwOpera = 1;
       |BUCLE MultiEmpEjer;
       |SI swimp ! 0;
           |PIEINF;
       |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE MultiEjer0;
   #caselem0#1;
   ;
   00001;
   99999;
   ;
   NULL,MultiEjer1;
|FIN;

|PROCESO MultiEjercicio;
   espe15 = "S";

   |SUB_EJECUTA "caselemp";
   |NOME_DAT #998 eaFichsel;
   |NOME_DAT #997 eaFichres;

   |BUCLE MultiEjer0;

   |DELINDEX #998;
   |DELINDEX #997;
|FPRC;

|PROGRAMA;
 |CLS;
 |CABEZA "Partidas Ptes. IVA";
 |HAZ inicio;
 |HAZ eParaModelos;

 CodigoEmpresa    = enEmpresa;
 PeriodoEmpresa   = enPeriodo;
 |DFICE dirfichOri;
 |DDEFECTO #0;
 #0#2 = fec1;
 #0#3 = fec2;

 nSalir = 0;
 |PARA; |SI nSalir = 0; |HACIENDO;
        |PINPA #0#0;
        |PINDA #0#0;                    || Pinta los datos por defecto
        |ENDATOS #1#0;                  || Seleccion informe
        |SI FSalida ! 0;
            nSalir = 1;
        |FINSI;
 |SIGUE;
|FPRO;
