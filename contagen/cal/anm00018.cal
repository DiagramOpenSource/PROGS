|FICHEROS;
   anm00018 #0;
   cacuenta #1;
   anm00017 #17;
   anm00001 #05;
   canempre #30;
|FIN;

|VARIABLES;

   &entrada = 1;
   nModo  = 0;
   alfa1  = "";
   aAlfa  = "";
   nume1  = 0;
   nume2  = 0;
   nume3  = 0;
   nume4  = 0;

   nTotal = 0;
   nCont  = 0;
   nOpc   = 0;
   nInkey = 0;
   aDesg = "";
   nNivel = 0;
   aMensa = "";
   swCta  = 0;

{-1}aMenu = "";
    aMenu1 = "2400";
    aMenu2 = "1";
    aMenu3 = "";
    aMenu4 = "BC";
    aMenu5 = "Balance Sumas y Saldos";
    aMenu6 = "Comprobacion Fichas ";
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoan_i;

|PROCESO LeeCuentaP;
   swCta  = 0;
   aMensa = "";

   |ABRE #1;
   #1#0 = eaCuenta;
   |LEE 001#1=;
   |SI FSalida ! 0;
       swCta = 1;
       aMensa = "0000No Existe Cuenta Contable ";
       |CIERRA #1;
       |ACABA;
   |FINSI;
   |CIERRA #1;

   |SI eaCuenta ! "            ";
       |SI nNivel = dig3;
           |SI #1#3 ! "S";
               swCta = 1;
               aMensa = "0000Esta Cuenta NO tiene Pase Directo.";
           |FINSI;
       |SINO;
           |SI enNivel ! nNivel;
               swCta = 1;
               aMensa = "0000Cuenta fuera del nivel indicado en la Agrupacion";
          |FINSI;
      |FINSI;
   |FINSI;
|FPRC;

|CALCULO Leeagrupa; |TIPO 0;
   aDesg  = "S";
   nNivel = dig3;

   |ABRE #17;
   #17#0 = #0Campo;
   |LEE 001#17=;
   |SI FSalida = 0;
       aDesg  = #17#2;
       |SI aDesg = "S"; |Y #17#3 = 0;  #17#3 = dig3; |FINSI;
       nNivel = #17#3;

       |SI aDesg = "S";
           |ATRI P; |PINTA 0765, "Desgl: "; |ATRI 0; |PINTA 0772, #17#2;
           |ATRI P; |PINTA 0865, "Nivel: "; |ATRI 0; |PINTA 0872, #17#3;
       |SINO;
                    |PINTA 0765, "ÄÄÄÄÄÄÄÄÄ";
           |ATRI P; |PINTA 0865, "Desgl: "; |ATRI 0; |PINTA 0872, #17#2;
       |FINSI;
   |FINSI;

   |C_M #0#20, 1, "S";
   |SI aDesg = "N";
       |C_M #0#20, 1, "N"; #0#20 = ""; |PINTA #0#20;
   |FINSI;
|FCAL;

|PROCESO Tipo13; |TIPO 13;
   |HAZ Leeagrupa;
|FPRC;

|PROCESO deshas; |TIPO 7;
   nModo = (FEntrada / 100) ! 0;
   |SI nModo = 1;
       NumMes = #30#11; |HAZ NombreMes; #0#17 = NombreMes; |PINTA #0#17;
       NumMes = #30#12; |HAZ NombreMes; #0#18 = NombreMes; |PINTA #0#18;
   |FINSI;
|FPRC;

|PROCESO tipopre; |TIPO 0;
   alfa1 = "S";
   |SI #0Campo = "M";  alfa1 = "N"; |FINSI;
   |C_M #0#16, 1, alfa1;
|FPRC;

|PROCESO divide; |TIPO 0;
   |SI #0#3 = "A";
       nume2 = #0#16 / 12;
       nume4 = 0;
       |PARA nume3 = 4; |SI nume3 [ 14; |HACIENDO nume3 = nume3 + 1;
             #0nume3 = nume2;
             nume4 = nume4 + #0nume3;
      |SIGUE;
      #0#15 = #0#16 - nume4;
   |FINSI;
   |PINDA #0#0;
|FPRC;

|PROCESO Controla; |TIPO 0;
   nTotal = 0;
   |PARA nCont = 4; |SI nCont [ 15; |HACIENDO nCont = nCont + 1;
         nTotal = nTotal + #0nCont; nTotal = nTotal ! 2;
   |SIGUE;
   |SI #0#3 = "A";
       |SI nTotal > #0#16;
          |MENAV "    Total anual sobrepasado";
          |ERROR;
       |FINSI;
   |SINO;
       #0#16 = nTotal; |PINTA #0#16;
   |FINSI;
|FPRC;

|PROCESO cuenta; |TIPO 6;
     |C_M #0Campo, 0, alfa1;
     |SI alfa1 = "N"; |ACABA; |FINSI;

     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;
|FPRC;

|CALCULO cuefin; |TIPO 0;
     |C_M #0Campo, 0, alfa1;
     |SI alfa1 = "N"; |ACABA; |FINSI;

     |SI #0Campo = doce; |Y aDesg = "S";
          nModo = (FEntrada / 100) ! 0;
          |SI nModo = 1;
              |CONFI "    NCuenta Contable en Blanco en una Agrupacion con desglose, Continuar ";
              |SI FSalida ! 0;
                  |ERROR;
                  |ACABA;
              |FINSI;
          |FINSI;
     |FINSI;
     emCta    = 1;
     eaCuenta = #0Campo;
     |HAZ SacaNivel;
     |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea

     |HAZ LeeCuentaP;
     |SI swCta ! 0;
         |MENAV aMensa;
         |ERROR;
     |FINSI;  ||Cuenta erronea
|FCAL;

|PROCESO Tipo20; |TIPO 20;
   |SI FSalida = "OPCION";
      FSalida = "Informes";
   |SINO;
      nModo = (FEntrada / 100) ! 0;
      |MENU aMenu;
      nOpc = FSalida;
      |SI nOpc < 1; |O nOpc > 2; |ACABA; |FINSI;
      |SI nModo ! 0; aAlfa = #0#19; |FINSI;
      |SI nModo = 0; aAlfa = "todas"; |FINSI;
      |SI nOpc = 1; aAlfa = "any00007.tab;" + aAlfa; |SUB_EJECUTA aAlfa; |FINSI;
      |SI nOpc = 2; aAlfa = "any00008.tab;" + aAlfa; |SUB_EJECUTA aAlfa; |FINSI;
   |FINSI;
|FPRC;

|PROCESO Ajusta; |TIPO 7;
   |SI #0#3 ! "A"; |ACABA; |FINSI;
   nTotal = 0;
   |PARA nCont = 4; |SI nCont [ 14; |HACIENDO nCont = nCont + 1;
      nTotal = nTotal + #0nCont; nTotal = nTotal ! 2;
   |SIGUE;
   nTotal = #0#16 - nTotal;
   #0#15 = nTotal;
|FPRC;

|PROCESO Prueba;
   |SI FSalida = 2; |ACABA; |FINSI;
   |SI #0#3 = "A";
      nTotal = 0;
      |PARA nCont = 4; |SI nCont [ 15; |HACIENDO nCont = nCont + 1;
         nTotal = nTotal + #anm00018#nCont#; nTotal = nTotal ! 2;
      |SIGUE;
      |SI nTotal ! #0#16;
         |CONFI "    NEl importe total no coincide con el de desglose. Ajustar ? ";
         |SI FSalida = 0; |HAZ Ajusta; |FINSI;
         |ERROR;
      |FINSI;
   |FINSI;
|FPRC;

|CALCULO anacta; |TIPO 6;
   nInkey = FSalida;
   |SI nInkey = 2;   |ACABA; |FINSI;

   salidas = FSalida;
   |HAZ FPuntosAna;
|FCAL;

|CALCULO finana; |TIPO 0;

   eOpAna = 0; || 1 = Consulta pantalla
||   |SI nInkey = 10; eOpAna = 1;  |FINSI;
   eaCtaAna = #0#0;
   |HAZ LeeCtaAna;
   |SI eswLect = 1;
        |MENAV "    Error.! No Existe Cuenta Analitica";
        |ERROR;
        |ACABA;
   |SINO;
        #0#0 = eaCtaAna; |PINTA #0#0;
        |PINTA 1131, #5#1;
  |FINSI;
  nModo = (FEntrada / 100) ! 0;
  |SI nModo = 1;
      #0#2 = #1#5; |SI #1#5 = "A"; #0#2 = "D"; |FINSI;
                   |SI #1#5 = "P"; #0#2 = "H"; |FINSI;
      |PINTA #0#2;
  |FINSI;
|FCAL;
