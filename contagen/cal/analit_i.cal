|| Subrutinas de las cuentas analiticas

|FICHEROS;
  anm00001 #58;
  anm00002 #602;
  anm00003 #603;
  anm00007 #607;
  anm00010 #610;
  anm00019 #619;
|FIN;

|VARIABLES;
    y_guarda = 0;
    y_entra = 0;
    y_lugar = 0;
    y_cuan1 = 0;
    y_cuan2 = 0;
    y_cuan3 = 0;
    y_cuan4 = 0;
    y_cuan5 = 0;
    y_alfa1 = "";
    y_alfa2 = "";
    y_alfa3 = "";
    y_alfa4 = "";
    y_alfa5 = "";
    y_alfa6 = "";

    y_nume1 = 0;
    y_nume2 = 0;
    y_ceros = "";

    y_cuen111 = "";
    y_cuen211 = "";
    y_cuen311 = "";

    y_error = 0;

    aAlfa1   = "";
    aUsu     = "";
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoan_i;


|PROCESO FPuntosAna; |TIPO 6;
  y_guarda = FSalida;
  y_alfa1  = #Fichero@#Campo#;

  y_lugar = 0;
  y_cuan1 = 0;
  y_cuan2 = 0;
  y_cuan3 = 0;
  y_cuan4 = 0;
  y_cuan5 = 7;
  y_alfa2 = "";
  y_alfa3 = "";
  y_alfa4 = "";
  y_alfa5 = "";
  y_alfa6 = "";

  y_alfa2 = y_alfa1 - ".";
  |SI FSalida ! 0;
      y_nume1 = FSalida;
      y_nume2 = 100 + (((y_nume1 / 100) ! 0) -1);
      y_alfa2 = y_alfa1 % y_nume2;

      y_nume2 = ((y_nume1 / 100) ! 0) * 100 + 199;
      y_alfa3 = y_alfa1 % y_nume2;
      |QBF y_alfa2;
      |QBF y_alfa3;
      FSalida = y_nume1;
      |PARA; |SI FSalida ! 0; |HACIENDO;
             y_alfa3 = y_alfa3 - ".";  || limpiar mas puntos (los ignoramos)
      |SIGUE;
      y_nume2 = MaxAnalitica - ( (A\y_alfa2 % 0) + (A\y_alfa3 % 0) );
      y_alfa4 = (A\ "0" * y_nume2);

      y_cuen111 = y_alfa3 % 101;
      y_cuen111 = y_cuen111 > " ";
      |SI y_cuen111 = "U";  |O y_cuen111 = "A";
          y_cuen311 = y_cuen111;
          |HAZ ult_anacta;
           #Fichero@#Campo# = y_alfa5;
      |SINO;
           #Fichero@#Campo# = (A\ y_alfa2 + y_alfa4 + y_alfa3);
      |FINSI;
      |PINTA #Fichero@#Campo#;
   |FINSI;
   FSalida = y_guarda;
|FPRC;

|PROCESO ult_anacta;
  y_cuan2   =  y_cuan3 + 100; || longitud de la 1¦ parte en Alfa.
  y_cuen211 =  y_cuan2; || longitud de la 1¦ parte en Alfa.
  y_cuan1 = (y_cuan5 - y_cuan3) + 100;
  y_cuan1 = - y_cuan1;
  y_cuen311 = y_cuan1; || longitud de la 2¦ parte en Alfa.

  y_alfa2 = y_alfa3 + "9999999";
  y_alfa2 = y_alfa2 % 107;

  |ABRE #58;
  #58#0 = y_alfa2;

  |LEE 000#58];
  |SI FSalida ! 0;
      |LEE 011#58u;
  |SINO;
      |LEE 011#58a;
      |SI FSalida ! 0;
          |LEE 011#58p;
      |FINSI;
  |FINSI;

  y_alfa2 = #58#0 % y_cuen211;
  y_alfa6 = #58#0 % y_cuen311;

  |SI y_cuen111 = "U";
      y_alfa5 = #58#0;
  |SINO;
      |SI y_alfa2 ! y_alfa3;
          y_nume1 = (y_cuan5 - y_cuan3 - 1);
          y_ceros = ("0" * y_nume1) + "1";
          y_alfa5 = y_alfa3 + y_ceros;
      |SINO;
          y_nume2 = y_alfa6;
          y_nume2 = y_nume2 + 1;
          y_alfa6 = y_nume2;
          y_alfa6 = "0000000" + y_alfa6;
          y_alfa6 = y_alfa6 % y_cuen311;
          y_alfa5 = y_alfa2 + y_alfa6;
      |FINSI;
  |FINSI;
  |DDEFECTO #58;
  |CIERRA #58;
|FPRC;

|PROCESO LeeTablaAna;
  swRelac = 0;
  |ABRE #607;
  |DDEFECTO #607;
  #607#0 = eaCuenta;
  |LEE 001#607=;
  |SI FSalida ! 0;
      swRelac = 1;
  |SINO;
      eaCtaAna = #607#3;
      eaCtaAnd = #607#3;   || Cuenta por defecto
      eaAnaMod = #607#4;
  |FINSI;
  |CIERRA #607;
|FPRC;

|PROCESO LimTablaA;
  |SI eaCtaAna ] #610#2; |Y eaCtaAna [ #610#3;
      eaBuenaAna = "";
      enBuenaAna = 0;
      |ERROR10;
  |FINSI;
|FPRC;

|DEFBUCLE LimTablaA;
 #610#1;
 ;
 eaCuenta, 001;
 eaCuenta, 999;
 e;
 NULL, LimTablaA;
|FIN;


|PROCESO LimUsuAna;
  |SI eaCtaAna ] #619#2; |Y eaCtaAna [ #619#3;
      eaBuenaAna = "";
      enBuenaAna = 0;
      |ERROR10;
  |FINSI;
|FPRC;

|DEFBUCLE LimUsuAna;
 #619#1;
 ;
 aUsu, 001;
 aUsu, 999;
 e;
 NULL, LimUsuAna;
|FIN;

|PROCESO MiroUsuario;
  aUsu = Usuario % 810;
  enBuenaAna = 1;
  |BUCLE LimUsuAna;
|FPRC;

|PROCESO eLimCtaAna;  ||Proceso para verificar ctas analiticas

  enBuenaAna = 0;   || = 0 Buena,  ! 0 Erronea:  1 = en blanco 6y7
                    ||                           2 = No Usuario
                    ||                           3 = Fuera de los Limites

  eaBuenaAna = "";
  aAlfa1 = eaCtaAna;  |HAZ LeeTablaAna;  eaCtaAna = aAlfa1;

  |SI eaCtaAna = "       ";
      |SI eaCuenta < "6           "; |O eaCuenta > "799999999999";
          |SI swRelac = 1;
              |ACABA;       || No es 6y7, y no existe.
          |FINSI;
      |SINO;
          |SI eaOAna = "S";
              eaBuenaAna = "    Error! No puede dejar la Cta Analitica en Blanco para Ctas Cbles del grupo 6 y 7";
              enBuenaAna = 1;
              |ACABA;
          |FINSI;
      |FINSI;
  |FINSI;

  |SI eaUAna = "S";
      |HAZ MiroUsuario;
      |SI enBuenaAna ! 0;
          eaBuenaAna = "    Esta Cta Analitica no puede utilizarse por el Usuario Actual";
          |ACABA;
      |FINSI;
  |FINSI;

  |SI eaAnaMod = "S"; |Y swRelac = 0;
      |SI eaCtaAna = eaCtaAnd; |ACABA; |FINSI; ||es buena
      eaBuenaAna = "    Error! La Cta Analitica no esta definida para la Cta Contable: " + eaCuenta;
      enBuenaAna = 3;
      |BUCLE LimTablaA;
  |FINSI;
|FPRC;

|PROCESO eLimCtaAna7;  ||Proceso para Modificable o No Analitica
  enBuenaAna = 0;
  aAlfa1 = eaCtaAna; |QBF aAlfa1;
  |SI aAlfa1 = ""; |Y eaCuenta ] "6           "; |Y eaCuenta [ "799999999999"; |Y eaOAna = "S";
      enBuenaAna = 1;
  |FINSI;
  |SI eaUAna = "S"; |HAZ MiroUsuario; |FINSI;
|FPRC;

|RUTINA inferAna;
  #58#0 = "       ";
|FRUT;

|RUTINA superAna;
  #58#0 = "zzzzzzz";
|FRUT;

|PROCESO LeeCtaAna;
  eaCtaAn1 = "";
  eswLect = 0;
  |ABRE #58;
  |SI eOpAna = 1;
      |CONSULTA_F_CLAVE #1#58,inferAna,superAna; ||   ,inferAna,superAna;
      eaCtaAna = #58#0;
  |FINSI;
  #58#0 = eaCtaAna;
  |LEE 000#58=;
  |SI FSalida ! 0;
      |DDEFECTO #58;
      eswLect = 1;
  |FINSI;
  eaCtaAn1 = #58#1;
  |CIERRA #58;
|FPRC
