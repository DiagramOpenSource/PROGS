|FICHEROS;
    camaasic #1;
    camaasil #2;
    caditra1 #3;
    cacuenta #4;
    caconimp #8;
    camaniva #15;

    ca8m0004 #404;
|FIN;

|VARIABLES;
    sw_alguno = 0;
    m_orden   = 1;
    opcion    = 1;
    d_iva     = "";
    f_iva     = @;
    nDdepar   = 0;
    nHdepar   = 0;
    nSwSalir  = 0;
    mensa1    = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
    alfa1     = "";
    alfa2     = "";

    nDiaAper     = 0;
    nIgual       = 0;
    nConcepto    = 0;
    aDescripcion = "";
    aSubdep      = "";
    nOpe         = 0;
    nLinea       = 0;

    nDia8 = 0;
    fFec8 = @;
    nDoc8 = 0;
|FIN;

|| ..... Calculos Pantallas
|CALCULO inilocal; |TIPO 7;
 #0#4 = fec1; |PINTA #0#4;
 #0#5 = fec2; |PINTA #0#5;
|FCAL;

|CALCULO fechad;
 |SI #0#4 < fec1; |O #0#4 > fec2;
     |MENAV mensa1;
     |ERROR;
 |FINSI;
|FCAL;

|CALCULO fechah;
 |SI #0#5 < fec1; |O #0#5 > fec2; |O #0#5 < #0#4;
     |MENAV mensa1;
     |ERROR;
 |FINSI;
|FCAL;

|CALCULO nodepar; |TIPO 7;
   |CAMPO_MODIFICA #0#13, 1, eaDepar;
   |CAMPO_MODIFICA #0#14, 1, eaDepar;
|FCAL;

|CALCULO nosubde; |TIPO 7;
   |CAMPO_MODIFICA #0#15, 1, eaSubde;
   |CAMPO_MODIFICA #0#16, 1, eaSubde;
|FCAL;

|PROCESO AsAper_d; |TIPO 0;
 |C_M #0#17, 0, alfa1;
 |SI alfa1 = "N"; |ACABA; |FINSI;

 alfa1 = #0#17;
 |C_M #0#19, 1, alfa1;
|FPRC;

|| ***************************************************************
|PROCESO VerAper08_d;
  nIgual = 0;
  |ABRE #404;
  |LEE 001#404p;
  |SI FSalida = 0;
      |SI #404#10 = #1#0; |Y #404#11 = #1#1; |Y #404#12 = #1#2;
          nIgual = 1;
          nDia8 = #404#10;
          fFec8 = #404#11;
          nDoc8 = #404#12;
      |FINSI;
  |FINSI;
  |CIERRA #404;
|FPRC;

|PROCESO alguno;
  |SI Haybasica = 1;
      nDepar  = #2#21; |SI nDepar = 0;  nDepar = 1;   |FINSI;
      nDdepar = #0#13;
      nHdepar = #0#14;
      |SI nDepar < nDdepar; |O nDepar > nHdepar; |ACABA; |FINSI;
  |SINO;
     |SI #2#21 < #0#13;  |O #2#21 > #0#14;  |ACABA;  |FINSI;
  |FINSI;

  |SI #2#28 < #0#15; |O #2#28 > #0#16; |ACABA; |FINSI;
  |SI #2#6 ] #0#9;|Y #2#6 [ #0#10;    || comprueba si hay algun apunte
      sw_alguno = 1;                  || valido en el apunte
      nConcepto    = #2#6;
      aDescripcion = #2#7;
      aSubdep      = #2#28;
  |FINSI;
|FPRC;

|| *****************************************************************
|PROCESO AImprimir;
 |SI opcion = 1; |HAZ porpan; |FINSI;
 |SI opcion = 2; |HAZ AImprimir_i; |FINSI;
|FPRC;

|PROCESO Leo8m004;
   |SI Haybasica = 1;
      nDepar  = #404#13; |SI nDepar = 0;  nDepar = 1;   |FINSI;
      nDdepar = #0#13;
      nHdepar = #0#14;
      |SI nDepar < nDdepar; |O nDepar > nHdepar; |ACABA; |FINSI;
   |SINO;
      |SI #404#13 < #0#13;  |O #404#13 > #0#14;  |ACABA;  |FINSI;
   |FINSI;

   #2#0 = #404#10;
   #2#1 = #404#11;
   #2#2 = #404#12;
   #2#21 = #404#13;
   #2#28 = aSubdep;
   |SI nOpe = 0;      ||primer asiento
       #2#3 = #404#0;  || La Linea
       #2#4 = #404#1;  || Cuenta Anterior
       #2#5 = #404#7;
       #2#6 = nConcepto;
       #2#7 = aDescripcion;
       #2#8 = #404#3;
       #2#9 = #404#4 + #404#5;
       #4#2 = #404#2;
       |HAZ AImprimir;
       |SI #404#1 ! #404#8;
           #1#4 = #1#4 + #2#9;
           #1#5 = #1#5 + #2#9;
       |FINSI;
   |FINSI;

   |SI nOpe = 1;      ||primer asiento
       |SI #404#1 = #404#8; |ACABA; |FINSI; ||no ya cambio de cuenta

       nLinea = nLinea + 1;
       #2#3 = nLinea;  || La Linea
       #2#4 = #404#1;  || Cuenta Anterior
       #2#5 = #404#7;
       #2#6 = #0#18;
       #2#7 = #0#19;
       |SI #404#3 = "D";
           #2#8 = "H";
           #1#8 = #404#8;
       |SINO;
           #2#8 = "D";
           #1#10 = #404#8;
       |FINSI;
       #2#9 = #404#4 + #404#5;
       #4#2 = #404#2;
       |HAZ AImprimir;

       nLinea = nLinea + 1;
       #2#3 = nLinea;  || La Linea
       #2#4 = #404#8;  || Cuenta Anterior
       #2#5 = #404#9;
       #2#6 = #0#18;
       #2#7 = #0#19;
       #2#8 = #404#3;
       #1#8  = #404#1;#1#10 = #404#1;
       #2#9 = #404#4 + #404#5;
       #4#0 = #2#4;
       #4#1 = #2#5;
       |LEE 040#4=;
       |SI FSalida ! 0; |DDEFECTO #4; |FINSI;
       |HAZ AImprimir;
   |FINSI;
|FPRC;

|DEFBUCLE Leo8m004;
 #404#1;
 ;
 nDia8, fFec8, nDoc8, 0001;
 nDia8, fFec8, nDoc8, 9999;
 ;
 NULL, Leo8m004;
|FIN;

|PROCESO Apertura2008_p;
   nConcepto    = 0;
   aDescripcion = "";
   aSubdep      = "";
   |ABRE #2;
   #2#0 = #1#0;
   #2#1 = #1#1;
   #2#2 = #1#2;
   #2#3 = 0001;
   |LEE 001#2];
   |SI FSalida = 0; |Y #2#0 = #1#0; |Y #2#1 = #1#1; |Y #2#2 = #1#2;
       nConcepto    = #2#6;
       aDescripcion = #2#7;
       aSubdep      = #2#28;
   |FINSI;
   |CIERRA #2;

   |SALVA_DATOS #1;
   #1#3 = #1#3 - 1; |SI #1#3 [ 0; #1#3 = 1; |FINSI;  || Apertura Cabecera 1§ asiento
   #1#4 = 0; #1#5 = 0; #1#6 = 0;
   nOpe = 0; |BUCLE Leo8m004;
   |SI #3#12 ! 0;
       sw_desc = 1;
   |SINO;
       sw_desc = 0;
   |FINSI;
   |HAZ cambiapa;
   #3#10 = 0;
   #3#11 = 0;
   #3#12 = 0;

   #1#3 = #1#3 + 1;
   nLinea = 0;
   nOpe   = 1; |BUCLE Leo8m004;
   |SI #3#12 ! 0;
       sw_desc = 1;
   |SINO;
       sw_desc = 0;
   |FINSI;
   |HAZ cambiapa;
   #3#10 = 0;
   #3#11 = 0;
   #3#12 = 0;
   |REPON_DATOS #1;
|FPRC;
|| *****************************************************************

|PROCESO porpan;
 |SI Haybasica = 1;
     nDepar  = #2#21; |SI nDepar = 0;  nDepar = 1;   |FINSI;
     nDdepar = #0#13;
     nHdepar = #0#14;
     |SI nDepar < nDdepar; |O nDepar > nHdepar; |ACABA; |FINSI;
 |SINO;
     |SI #2#21 < #0#13;  |O #2#21 > #0#14;  |ACABA;  |FINSI;
 |FINSI;

 |SI #2#28 < #0#15; |O #2#28 > #0#16; |ACABA; |FINSI;
 |SI #2#6 ] #0#9;|Y #2#6 [ #0#10;
     sw_alguno = 1;
     |SI lin > 1000;
         sw_desc = 0;
         |HAZ cambiapa;
     |FINSI;
     #3#0 = #2#1;
     #3#1 = #2#0;
     #3#2 = #1#3;
     #3#3 = #2#2;
     #3#4 = #2#4;
     #3#5 = #2#6;
     #3#6 = #2#7;
     #3#7 = #2#8;
     |SI #3#7 = "D";
         #3#8 = #2#9;          || debe
         #3#16 = 0;             || haber
     |SINO;
         #3#8 = 0;
         #3#16 = #2#9;
     |FINSI;
     #3#9 = #2#10;

     |SI #2#8 = "D";
         #3#10 = #3#10 + #2#9;
         #3#13 = #3#13 + #2#9;
     |SINO;
         #3#11 = #3#11 + #2#9;
         #3#14 = #3#14 + #2#9;
     |FINSI;
     #3#12 = #3#10 - #3#11;
     #3#15 = #3#13 - #3#14;

     |SI d_iva = "S";
         |HAZ delIVA;
     |FINSI;
|| |SINO;
         |ATRI I;
         |PINTA 0628, #3#0;
         |PINTA 0616, #3#1;
         |PINTA 0670, #3#2;
         |PINTA 0652, #3#3;
         |ATRI 0;
         Pos = lin;
         |PINDA #0#3;
         Pos = -1;
         |ATRI I;
         |PINTA 2126, #3#10;
         |PINTA 2146, #3#11;
         |PINTA 2166, #3#12;
         |PINTA 2226, #3#13;
         |PINTA 2246, #3#14;
         |PINTA 2266, #3#15;
         |ATRI 0;
         lin = lin + 100;
   ||  |FINSI;
 |FINSI;
|FPRC;

|PROCESO delIVA;
  f_iva = "";
  |SI #2#19 = "F"; |O #2#19 = "f";
     |ABRE #camaniva;
     |SELECT #3#camaniva;
     #camaniva#0  = #2#13;
     #camaniva#2  = #2#15;
     #camaniva#3  = #2#14;
     |LEE 001#camaniva.=;
     |SI FSalida = 0;
        f_iva = #camaniva#4;
     |FINSI;
     |SELECT #1#camaniva;
     |CIERRA #camaniva;
  |FINSI;
  #3#17 = f_iva;
|FPRC;

|PROCESO buclepan;
 |SI #0#17 = "S";
     |SI #1#0 = 0; |Y nDiaAper = 0;
         |HAZ VerAper08_d;
         |SI nIgual = 1;
             |HAZ Apertura2008_p;
             nDiaAper = 1;
             |ACABA;           ||este asiento se trata de forma diferente
          |FINSI;
     |FINSI;
 |FINSI;

 sw_alguno = 0;
 |BUCLELIN 0porpan#1;
 |SI sw_alguno ! 0;
    |SI #3#12 ! 0;
        sw_desc = 1;
    |SINO;
        sw_desc = 0;
    |FINSI;
    |HAZ cambiapa;
    #3#10 = 0;
    #3#11 = 0;
    #3#12 = 0;
 |FINSI;
|FPRC;

|DEFBUCLE caditrab1;
 #1#2;
 ;
 #0#0, #0#4, #0#7, #0#2;
 #0#1, #0#5, #0#8, #0#3;
 e;
 NULL, buclepan;
|FIN;

|DEFBUCLE caditrab2;
 #1#1;
 3;
 #0#7, #0#4, #0#2, #0#0;
 #0#8, #0#5, #0#3, #0#1;
 e;
 NULL, buclepan;
|FIN;

|DEFBUCLE caditrab3;
 #1#3;
 3,1,0;
 #0#2,  #0#0, #0#4, #0#7;
 #0#3,  #0#1, #0#5, #0#8;
 e;
 NULL, buclepan;
|FIN;

|PROCESO PintaEmpresa;
    alfa1 = " " * 79;
    |PINTA 0502, alfa1;
    alfa1 = " Empresa: " + #canempre#2 + "  Periodo: " + #canempre#3 + "  Nombre: " + #canempre#1;
    |ATRI R;  |PINTA 0502, alfa1; |ATRI 0;
|FPRC;

|PROCESO pantalla;
  |HAZ DirAplicSt;
  |SI Haybasica = 1; |Y #0#14 = "zz"; #0#14 = "99"; |FINSI;

  lin = 100;
  |SI d_iva = "S";
      |PINPA #1#3; |HAZ PintaEmpresa;
      |C_P #3#4, 1, 1002;
      |C_P #3#5, 1, 1015;
      |C_P #3#6, 1, 1018; |C_A #3#6, 1, 25;
      |C_P #3#8, 1, 1057;
      |C_P #3#16, 1, 1069;
      |C_V #3#17, 1, "S";
  |SINO;
      |PINPA #0#3; |HAZ PintaEmpresa;
      |C_P #3#4, 1, 1006;
      |C_P #3#5, 1, 1019;
      |C_P #3#6, 1, 1022; |C_A #3#6, 1, 80;
      |C_P #3#8, 1, 1053;
      |C_P #3#16, 1, 1065;
      |C_V #3#17, 1, "N";
  |FINSI;

  |SI m_orden = 1; |BUCLE caditrab1; |FINSI;
  |SI m_orden = 2; |BUCLE caditrab2; |FINSI;
  |SI m_orden = 3; |BUCLE caditrab3; |FINSI;

|FPRC;

|PROCESO cambiapa;
 |SI sw_desc = 1;
     |AVISO;
     |AVISO;
     |ATRI P;
     |PINTA 0430,"===  DESCUADRE ===";
     |ATRI 0;
 |FINSI;

 |PAUSA;
 nSwSalir = FSalida;
 |SI FSalida = 1; |O FSalida = 7;
     |ERROR10;
 |FINSI;
 |ATRI R; |BLIN 04; |ATRI 0;

 |SI d_iva = "S";
     |PINPA #1#3; |HAZ PintaEmpresa;
 |SINO;
     |PINPA #0#3; |HAZ PintaEmpresa;
 |FINSI;
 lin = 100;
|FPRC;
