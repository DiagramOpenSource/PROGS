|FICHEROS;
    any00001 #0;
    camaasil #1;
    camaasic #2;
    cacuenta #3;
    anm00001 #4;

    anm00003 #7;         || Grupos Analiticas
    anm00013 #13;        || Grupos Analiticas  por apuntes
    anm0003@ #107;       || Espejo grupos analitica

    canempre #30;
    calicont #31;
    caselem1 #998;

    anw00014 #999;
|FIN;

|VARIABLES;
    &entrada = 1;

    mesnum = 0;
    columna = 0;
    sw_con = 0;          || 1 = EL CONCEPTO ES BUENO
    x = 0;
    t_debe = 0;
    t_haber = 0;
    t_saldo = 0;
    campo = 0;
    d = 0;
    h = 0;
    sw_print = 0;        || 1 = NO DEBO ESCRIBIR
    sw_prim = 0;         || 0 = PRIMERA VEZ
    mesh = "";
    diah = "";
    anyoh = "";
    &cuenta = "";
    xxxxfiac = "  ";
    xxaponer = "  ";
    xrelleno = "                              ";
    fecalf = "";
    mensa1 = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
    traba1 = "";
    varalf = "";
    &swant = 0;
    &s_debe = 0;
    &s_haber = 0;
    &s_saldo = 0;
    &debe = 0;
    &haber = 0;
    &direc = "";
    &codpos = "";
    &pobla = "";
    &provi = "";
    &cif = "";
    &telef = "";
    &contra   = "";
    &contra_t = "";
    contra_td = "";
    contra_th = "";
    contra_h  = "";
    contra_d  = "";
    swex_con = 0;
    informe = "";
    men2  = "     Longitud de Cuenta Fuera de Nivel !";
    long = "  ";
    cta  = 0;
    cta1 = "";
    num   = 0;
    swdig = 0;
    digito = 0;
    &sw_inf = 0;
    &sw_cab = 0;
    &y = 0;
    xy = 0;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nPara1 = 0;
    sw_pagina = 0;
    &pagina = 0;
    sw_cuen = 0;          ||1 = Hemos Leido la cuenta

    incta1 = "";
    ficta1 = "";
    incta  = "";
    ficta  = "";
    swsel  = 0;
    swsel2 = 0;
  swtotal  = 0;

     aMas       = "";
     aDef       = "";
     aFichero   = "";
     aQueQuiero = "";
     aNumCampos = "";
     nNumCampos = 0;
     nSwGApte   = 0;
     aCta2Gru   = "";
     &ctanueva  = "";
     &ctanom    = "";
     nImpAna    = 0;
     grupoc     = "";
     grupod     = 0;
     total      = 0;
     grupod2    = 0;
     total2     = 0;
     swgrupo    = 0;
     swgrupo2   = 0;
     ctaultima  = "";
     ctaultima2 = "";
     &enSw1     = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoan_i;

|CALCULO &c_pagina;
  |SI sw_pagina = 0;
      pagina = #0#38;
      sw_pagina = 1;
  |SINO;
      pagina = pagina + 1;
  |FINSI;
  sw_cab = 1;
|FCAL;
|| *********************************************************************
||                      Calculos de Pantalla
|| *********************************************************************
|CALCULO AntesDesg; |TIPO 7;
  alfa1 = #0#59;
  |C_M #0#60,1,alfa1;
|FCAL;

|CALCULO seleccio; |TIPO 0;
|SI #0#4 = "N";
    |C_M #0#05, 1, "S"; |C_M #0#06, 1, "S"; |C_M #0#07, 1, "S";
    |C_M #0#08, 1, "S"; |C_M #0#09, 1, "S"; |C_M #0#10, 1, "S";
    |C_M #0#11, 1, "S"; |C_M #0#12, 1, "S"; |C_M #0#13, 1, "S";
    |C_M #0#14, 1, "S"; |C_M #0#15, 1, "S"; |C_M #0#16, 1, "S";
    |C_M #0#17, 1, "S"; |C_M #0#18, 1, "S"; |C_M #0#19, 1, "S";
    |C_M #0#20, 1, "S"; |C_M #0#21, 1, "S"; |C_M #0#22, 1, "S";
    |C_M #0#23, 1, "S"; |C_M #0#24, 1, "S"; |C_M #0#25, 1, "S";
    |C_M #0#26, 1, "S"; |C_M #0#27, 1, "S"; |C_M #0#28, 1, "S";
    |C_M #0#33, 1, "N"; |C_M #0#34, 1, "N";
        #0#33 = 1;       |PINTA #0#33;
        #0#34 = 999;     |PINTA #0#34;
|SINO;
    |C_M #0#05, 1, "N"; |C_M #0#06, 1, "N"; |C_M #0#07, 1, "N";
    |C_M #0#08, 1, "N"; |C_M #0#09, 1, "N"; |C_M #0#10, 1, "N";
    |C_M #0#11, 1, "N"; |C_M #0#12, 1, "N"; |C_M #0#13, 1, "N";
    |C_M #0#14, 1, "N"; |C_M #0#15, 1, "N"; |C_M #0#16, 1, "N";
    |C_M #0#17, 1, "N"; |C_M #0#18, 1, "N"; |C_M #0#19, 1, "N";
    |C_M #0#20, 1, "N"; |C_M #0#21, 1, "N"; |C_M #0#22, 1, "N";
    |C_M #0#23, 1, "N"; |C_M #0#24, 1, "N"; |C_M #0#25, 1, "N";
    |C_M #0#26, 1, "N"; |C_M #0#27, 1, "N"; |C_M #0#28, 1, "N";
    |C_M #0#33, 1, "S"; |C_M #0#34, 1, "S";
|FINSI;
|FCAL;

|CALCULO seleccio2; |TIPO 0;
|SI #0#41 = "N";
    |C_M #0#42, 1, "S"; |C_M #0#43, 1, "S";
    |C_M #0#44, 1, "S"; |C_M #0#45, 1, "S";
    |C_M #0#46, 1, "S"; |C_M #0#47, 1, "S";
    |C_M #0#48, 1, "S"; |C_M #0#49, 1, "S";
    |C_M #0#50, 1, "S"; |C_M #0#51, 1, "S";
    |C_M #0#52, 1, "S"; |C_M #0#53, 1, "S";
    |C_M #0#54, 1, "S"; |C_M #0#55, 1, "S";
    |C_M #0#56, 1, "N"; |C_M #0#57, 1, "N";
    #0#56 = "       "; |PINTA #0#56;
    #0#57 = "zzzzzzz"; |PINTA #0#57;
|SINO;
    |C_M #0#56, 1, "S"; |C_M #0#57, 1, "S";
    |C_M #0#42, 1, "N"; |C_M #0#43, 1, "N";
    |C_M #0#44, 1, "N"; |C_M #0#45, 1, "N";
    |C_M #0#46, 1, "N"; |C_M #0#47, 1, "N";
    |C_M #0#48, 1, "N"; |C_M #0#49, 1, "N";
    |C_M #0#50, 1, "N"; |C_M #0#51, 1, "N";
    |C_M #0#52, 1, "N"; |C_M #0#53, 1, "N";
    |C_M #0#54, 1, "N"; |C_M #0#55, 1, "N";
|FINSI;
|FCAL;

|CALCULO cuenta; |TIPO 6;
     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;
|FCAL;

|CALCULO fechad;
  |SI #0#2 < fec1; |O #0#2 > fec2;
      |MENAV mensa1;
      |ERROR;
  |FINSI;
|FCAL;

|CALCULO fechah;
  |SI #0#3 < fec1; |O #0#3 > fec2; |O #0#3 < #0#2;
      |MENAV mensa1;
      |ERROR;
  |FINSI;
|FCAL;

|CALCULO no_dep; |TIPO 7;
   |C_M #0#36, 1, eaDepar;
   |C_M #0#37, 1, eaDepar;
|FCAL;

|CALCULO nosubde; |TIPO 7;
   |C_M #0#31, 1, eaSubde;
   |C_M #0#32, 1, eaSubde;
|FCAL;

|| ******************************************************************
|PROGRAMA;
  |CLS;
  |CABEZA "Extractos Analitica Impresora";
  |HAZ inicio;
  |HAZ Analitica;
  |SI  enSwAna = 0;
       alfa1 = "    CONTABILIDAD NO ANALITICA ";
       |SI eaAnNiv = "E";
           alfa1 = "    EMPRESA NO ANALITICA ";
       |FINSI;
       |MENAV alfa1;
       |ACABA;
  |FINSI;

  |DBASE aMas;
  aDef  = aMas + "def/anm00003";
  |CARGA_DEF aDef, anm0003@;
  |SI FSalida ! 0;
      |MENAV "    Error Cargado Fichero de Grupos Analiticos. Proceso Interrumpido";
      |ACABA;
  |FINSI;

  aQueQuiero = "|NC";
  aNumCampos = #999#aQueQuiero#;
  nNumCampos = aNumCampos;
  nNumCampos = nNumCampos - 1;

  aFichero = "14aw" + Usuario;
  |NOME_DAT #999 aFichero;
  |DELINDEX #999;

  #0#2 = fec1; #0#3 = fec2;
  FSalida = 1;

  |PARA;|SI FSalida = 1; |HACIENDO;
         sw_pagina = 0;
         sw_prim = 0;
         sw_print = 0;
         sw_inf = 0;
         sw_cuen = 0;
         swtotal = 0;

         |PINPA #0#0;
         |PINDA #0#0;                    || Pinta los datos por defecto
         |ENDATOS #1#0;                  || Seleccion informe
         |SI FSalida = 0;                || Si el conforme es igua a S

             |SI #0#58 ! "X";
                 |MENAV "0000 Recuerde seleccionar el papel de la impresora en Horizontal";
             |FINSI;

             |IMPRE 0;
             |SI FSalida ! 0;
                 |MENAV "    Proceso abortado ... ";
                 FSalida = 0;
             |SINO;
                  |HAZ Imprimir;
                  |FINIMP;
                  FSalida = 1;
             |FINSI;
         |SINO;
              FSalida = 0;
         |FINSI;
  |SIGUE;

  |DESCARGA_DEF #107;
|FPRO;

|PROCESO Imprimir;
  enSw1 = 0;
  |SI #0#61 = "S"; enSw1 = 1; |FINSI;
  |SI #0#58 = "X";
      informe = "any00001";
      |SI #0#35 = "N"; informe = "any10001"; |FINSI;
  |SINO;
      |SI #0#58 = "N";
          informe = "any000n1";
          |SI #0#35 = "N"; informe = "any100n1"; |FINSI;
      |SINO;
          informe = "any000s1";
          |SI #0#35 = "N"; informe = "any100s1"; |FINSI;
      |FINSI;
   |FINSI;

   |INFOR informe;
   |SI FSalida ! 0; |ACABA; |FINSI;

   |SI #48#27 = "S";
       |HAZ MultiEmpresa;
   |SINO;
       |DFICE dirfich0; |QBF dirfich0;
       |HAZ HazTodo;
   |FINSI;
   |FININF;
|FPRC;

|| *************************************************************************
||                               MEOLLO
|| *************************************************************************
|| ...................... seleccion de Lineas
|CALCULO concepto;
  sw_con = 0;
  |PARA x = 5; |SI x [ 28; |HACIENDO x = x + 1;
        |SI #0x  = #1#6;
            sw_con = 1;
            |SAL;
       |FINSI;
  |SIGUE;
|FCAL;

|CALCULO selcuentas;
  sw_con = 0;
  |PARA x = 42; |SI x [ 55; |HACIENDO x = x + 1;
        |SI #0x  = #1#29; |Y #1#29 ! "       ";
             sw_con = 1;
            |SAL;
       |FINSI;
  |SIGUE;
|FCAL;
|| ------------------------------------------------------
|| ... Segundo nivel de grupos
|CALCULO ver_grupo2;
 |SI #107#2 ] incta1; |Y #107#2 [ ficta1;
     swsel = 1;
     |ERROR10;
 |FINSI;
|FCAL;

|DEFBUCLE selegrupo2;
 #107#1002;
 ;
 aCta2Gru, 000;
 aCta2Gru, 999;
 e;
 NULL, ver_grupo2;
|FIN;

|PROCESO ver_grupo_apte;
 nSwGApte = 1;
 |SI #13#6 ] incta1; |Y #13#6 [ ficta1;
     swsel = 1;
     |ERROR10;
 |SINO;
     alfa1 = #13#6 % 103;
     |SI alfa1 = "GRU";
         aCta2Gru   = #13#6;
         |BUCLE selegrupo2;
         |SI swsel = 1;
             |ERROR10;
         |FINSI;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE selgruApte;
 #13#1;
 ;
 #1#0, #1#1, #1#2, #1#3, #1#29, 000;
 #1#0, #1#1, #1#2, #1#3, #1#29, 999;
 e;
 NULL, ver_grupo_apte;
|FIN;

|PROCESO ver_grupo;
 |SI #7#2 ] incta1; |Y #7#2 [ ficta1;
     swsel = 1;
     |ERROR10;
 |SINO;
     alfa1 = #7#2 % 103;
     |SI alfa1 = "GRU";
         aCta2Gru   = #7#2;
         |BUCLE selegrupo2;
         |SI swsel = 1;
             |ERROR10;
         |FINSI;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE selegrupo;
 #7#1;
 ;
 #1#29, 000;
 #1#29, 999;
 e;
 NULL, ver_grupo;
|FIN;

|PROCESO SelNuevo;
  |SI #1#29 ] incta1; |Y #1#29 [ ficta1;
      swsel = 1;
  |SINO;
     |SI alfa3 = "GRU";
         nSwGApte = 0;
         |BUCLE selgruApte;
         |SI nSwGApte = 0;
             |BUCLE selegrupo;
         |FINSI;
     |FINSI;
  |FINSI;
|FPRC;

|PROCESO SelNuevo2;
  swsel2 = 0;
  |SI #0#41 = "N";
      |PARA x = 42; |SI x [ 55; |HACIENDO x = x + 1;
            |SI #0x ! "       "; |Y #0x = ctanueva;
                swsel2 = 1;
                |SAL;
            |FINSI;
      |SIGUE;
  |SINO;
      |SI ctanueva ] #0#56; |Y ctanueva [ #0#57;
          swsel2 = 1;
      |FINSI;
  |FINSI;
|FPRC;
|| --------------------------------------------------------------------
|PROCESO cabecera;
  |DDEFECTO #2;
   #camaasic#0 = #camaasil#0;
   #camaasic#1 = #camaasil#1;
   #camaasic#2 = #camaasil#2;
   |LEE 040#2=;
|FPRC;

|PROCESO leocontra;
  contra = "";
  contra_t = "";
  contra_td = "";
  contra_th = "";

  contra_d = #2#10;
  |SALVA_DATOS #3;  |DDEFECTO #3;  #3#0 = #2#10;  #3#1 = #2#11;  |LEE 000#3=;
  contra_td = #3#2;
  |REPON_DATOS #3;

  contra_h = #2#8;
  |SALVA_DATOS #3;  |DDEFECTO #3;  #3#0 = #2#8;   #3#1 = #2#9;   |LEE 000#3=;
  contra_th = #3#2;
  |REPON_DATOS #3;

  swex_con = 0;
  #31#0 = #1#0;
  #31#1 = #1#1;
  #31#2 = #1#2;
  #31#3 = #1#3;
  |LEE 001#31=;
  |SI FSalida = 0;
      swex_con = 1;
      contra_h = #31#4;
      contra_d = #31#4;
      |SALVA_DATOS #3;
      |DDEFECTO #3;
      #3#0 = #31#4;
      #3#1 = #31#5;
      |LEE 000#3=;
      contra_td = #3#2;
      contra_th = #3#2;
      |REPON_DATOS #3;
  |FINSI;
  |SI #1#8 = "D";  contra = contra_d; contra_t = contra_td; |FINSI;
  |SI #1#8 = "H";  contra = contra_h; contra_t = contra_th; |FINSI;
|FPRC;

|CALCULO leecuen1;
  sw_cuen = 1;
  #4#0 = #1#29;
  |LEE 040#4=;
  |SI FSalida ! 0; |DDEFECTO #4; |FINSI;
  t_debe = 0;
  t_haber = 0;
  t_saldo = 0;
|FCAL;

|CALCULO buclepan;
  sw_con = 1;
  |SI #0#41 = "N"; |HAZ selcuentas;  |FINSI;  || Seleccionar cuenta Analiticas
  |SI sw_con = 0; |ACABA;  |FINSI;

  |SI #0#4 = "N"; |HAZ concepto;  |FINSI;  || Seleccionar Conceptos
  |SI sw_con = 0; |ACABA;  |FINSI;

   |SI sw_prim = 0;
      cuenta = #1#29;
      sw_prim  = 1;
      |HAZ leecuen1;
  |FINSI;
  |SI cuenta ! #1#29;
      |SI sw_print = 0;
          |SI #0#35 = "N";
              sw_cab = 0;
              sw_inf = 2;
              |PRINT; swtotal = 1;
              sw_cab = 0;
          |SINO;
              |PIEINF;
          |FINSI;
      |FINSI;
      |HAZ borrar;
      cuenta = #1#29;
      |HAZ leecuen1;
  |FINSI;
  |SI #1#8 = "D";
      debe = #1#9;
      haber = 0;
  |SINO;
      debe = 0;
      haber = #1#9;
  |FINSI;
  |SI #1#1 ] #0#2; |Y #1#1 [ #0#3;

      |HAZ cabecera;
      |HAZ leocontra;
      |SI #0#35 = "N";  |Y sw_cuen = 1;
          sw_cab = 1;
          sw_inf = 0;
          |PRINT; swtotal = 1;
          sw_cab = 0;
          sw_cuen = 0;
      |FINSI;
      sw_inf = 1;
      sw_cab = 0;
      |PRINT; swtotal = 1;
      sw_cab = 0;
      sw_print = 0;
      swant = 1;
  |FINSI;
  |SI #1#1 < #0#2;
      |SI #1#8 = "D";
          s_debe = s_debe  + #1#9;
      |SINO;
          s_haber = s_haber + #1#9;
      |FINSI;
      s_saldo = s_debe - s_haber;
      sw_print = 1;
      swant = 1;
  |FINSI;
|FCAL;

|DEFBUCLE caextrim0;
 #1#1;
 4, 6, 29, 21, 28;
 #0#39, fec1, 000000, 0000, #0#0, #0#33, incta, #0#36, #0#31;
 #0#40, #0#3, 999999, 9999, #0#1, #0#34, ficta, #0#37, #0#32;
 e;
 NULL, NULL, NULL, NULL, NULL, buclepan;
 #1#29, #1#1, #1#2, #1#3;
|FIN;

|| ///////////////////////////////////////
|PROCESO LeoAuxiliar;
 #1#0 = #999#0;
 #1#1 = #999#1
 #1#2 = #999#2
 #1#3 = #999#3
 |LEE 010#1=;

 #4#0 = #1#29;
 |LEE 040#4=;
 |SI FSalida ! 0; |DDEFECTO #4; |FINSI;
 ctanueva = #1#29;
 ctanom   = #4#1;

 |PARA nPara1 = 0; |SI nPara1 [ nNumCampos; |HACIENDO nPara1 = nPara1 + 1;
       #1nPara1 = #999nPara1;
 |SIGUE;
 |HAZ buclepan;
|FPRC;

|DEFBUCLE LeoAuxiliar;
 #999#1;
 ;
 "        ", "01.01.0000", 00, 000001, 0001;
 "zzzzzzzz", "31.12.2999", 99, 999999, 9999;
 ;
 NULL, LeoAuxiliar;
|FIN;
|| **********************************************************************

|PROCESO gragrup;
 #999#0 = #1#0;
 #999#1 = #1#1;
 #999#2 = #1#2;
 #999#3 = #1#3;
 #999#29 = ctanueva;
 |LEE 001#999=;
 |SI FSalida ! 0;
     |PARA nPara1 = 0; |SI nPara1 [ nNumCampos; |HACIENDO nPara1 = nPara1 + 1;
           #999nPara1 = #1nPara1;
     |SIGUE;
     #999#9  = 0;
     #999#29 = ctanueva;
     |GRABA 020#999c;
 |FINSI;
 #999#9  = #999#9 + nImpAna;
 |GRABA 020#999a;
|FPRC;
|| .....................................
|PROCESO ggrupos2;
 ctanueva = #107#2;
 nImpAna  = (grupod2 * #107#4 / 100) ! EURODB;
 |SI nImpAna ! 0;
     |HAZ SelNuevo2;
     |SI swsel2 = 1;
         |HAZ gragrup;
     |FINSI;
 |FINSI;

 swgrupo2   = 1;
 ctaultima2 = #107#2;
 total2     = (total2 + nImpAna)   ! EURODB;
|FPRC;

|PROCESO fingrupo2;
 |SI swgrupo2 = 0; |ACABA; |FINSI;
 |SI total2 = grupod2; |ACABA; |FINSI;

 ctanueva = ctaultima2;
 nImpAna  = grupod2 - total2;
 |SI nImpAna ! 0;
     |HAZ SelNuevo2;
     |SI swsel2 = 1;
         |HAZ gragrup;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE leegrupo2;
 #107#1002;
 ;
 aCta2Gru, 000;
 aCta2Gru, 999;
 e;
 NULL, ggrupos2, NULL, NULL, fingrupo2;
|FIN;

|PROCESO Subgrupo;
 aCta2Gru   = ctanueva;
 grupod2    = nImpAna;
 total2     = 0;
 swgrupo2   = 0;
 ctaultima2 = "";
 |BUCLE leegrupo2;
|FPRC;

|PROCESO TrataCtaGrupo;
 alfa3 = ctanueva % 103;
 |SI alfa3 ! "GRU";   ||Tratamiento normal de Cuenta Analitica
     |HAZ SelNuevo2;
     |SI swsel2 = 1;
         |HAZ gragrup;
     |FINSI;
 |SINO;
     |SI #0#60 = "N";   || Si quiere grabar grupos
         |HAZ SelNuevo2;
         |SI swsel2 = 1;
             |HAZ gragrup;
         |SINO;
             |HAZ Subgrupo;
         |FINSI;
     |SINO;
         |HAZ Subgrupo;
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO ggruposA;
 nSwGApte = 1;
 ctanueva = #13#6;
 nImpAna  = #13#9;

 |HAZ TrataCtaGrupo;
|FPRC;

|DEFBUCLE leegrApte;
 #13#1;
 ;
 #1#0, #1#1, #1#2, #1#3, grupoc, 000;
 #1#0, #1#1, #1#2, #1#3, grupoc, 999;
 e;
 NULL, ggruposA;
|FIN;

|PROCESO ggrupos;
 ctanueva = #7#2;
 nImpAna  = (grupod * #7#4 / 100) ! EURODB;

 swgrupo   = 1;
 ctaultima = #7#2;
 total     = (total + nImpAna)   ! EURODB;

 |SI nImpAna ! 0;
     |HAZ TrataCtaGrupo;
 |FINSI;
|FPRC;

|PROCESO fingrupo;
|| aqui

 |SI swgrupo = 0; |ACABA; |FINSI;
 |SI total = grupod; |ACABA; |FINSI;

 ctanueva = ctaultima;
 nImpAna  = (grupod - total) ! EURODB;
 |SI nImpAna ! 0;
     |HAZ TrataCtaGrupo;
 |FINSI;
|FPRC;

|DEFBUCLE leegrupo;
 #7#1;
 ;
 grupoc, 000;
 grupoc, 999;
 e;
 NULL, ggrupos, NULL, NULL, fingrupo;
|FIN;


|PROCESO BuscoOtros;
     nSwGApte = 0;
     grupoc  = ctanueva;
     |BUCLE leegrApte;
     |SI nSwGApte = 0;
         ctanueva = #1#29;
         swgrupo  = 0;
         grupod   = #1#9;
         ctaultima = "";
         total     = 0;
         |BUCLE leegrupo;
     |FINSI;
|FPRC;

|PROCESO CargaAuxiliar;
  sw_con = 1;
  |SI #0#4 = "N"; |HAZ concepto;  |FINSI;  || Seleccionar Conceptos
  |SI sw_con = 0; |ACABA;  |FINSI;

  swsel = 0;           || Solo comprueba si esta dentro de la seleccion
  alfa3 = #1#29 % 103;
  |SI #0#41 = "N";
      |PARA x = 42; |SI x [ 55; |HACIENDO x = x + 1;
            |SI #0x ! "       ";
                incta1 = #0x;
                ficta1 = #0x;
                |HAZ SelNuevo;
                |SI swsel = 1; |SAL; |FINSI;
            |FINSI;
      |SIGUE;
  |SINO;
      incta1 = #0#56;
      ficta1 = #0#57;
      |HAZ SelNuevo;
  |FINSI;
  |SI swsel = 0; |ACABA; |FINSI;

|| ... Ahora grabo tantas lineas de este apunte como cuentas seleccionadas

  nImpAna  = #1#9;
  ctanueva = #1#29;
  alfa3 = ctanueva % 103;
  |SI alfa3 ! "GRU";   ||Tratamiento normal de Cuenta Analitica
       |HAZ gragrup;
  |SINO;
       |SI #0#60 = "N";   || Si quiere grabar grupos
           |HAZ SelNuevo2;
           |SI swsel2 = 1;
               |HAZ gragrup;
           |SINO;
               |HAZ BuscoOtros;
           |FINSI;
       |SINO;
           |HAZ BuscoOtros;
       |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE CargaAuxiliar;
 #1#1;
 4, 6, 21, 28;
 #0#39, fec1, 000000, 0000, #0#0, #0#33, #0#36, #0#31;
 #0#40, #0#3, 999999, 9999, #0#1, #0#34, #0#37, #0#32;
 e;
 NULL, CargaAuxiliar;
|FIN;

|PROCESO CreaAuxiliar;
   |DELINDEX #999;

   |ABRE #999;
   |BUCLE CargaAuxiliar;
   |CIERRA #999;
|FPRC;

|PROCESO HazTodo;

    |SI #0#59 = "S";
        |HAZ CreaAuxiliar;
    |FINSI;

    |HAZ borrar;
    sw_pagina = 0;
    sw_prim = 0;
    sw_print = 0;
    sw_inf = 0;
    sw_cuen = 0;
    swtotal = 0;


   |ABRE #2;
   |ABRE #3;
   |ABRE #4;
   |ABRE #31;

   |SI #0#41 = "N"; #0#56 = "       "; #0#57 = "zzzzzzz"; |FINSI;
   incta = #0#56;
   ficta = #0#57;
   swtotal = 0;
   |SI #0#59 = "S";
       |ABRE #1;
       |BUCLE LeoAuxiliar;
       |CIERRA #1;
   |SINO;
       |BUCLE caextrim0;
   |FINSI;
   |CIERRA #31;
   |CIERRA #4;
   |CIERRA #3;
   |CIERRA #2;
   |SI #0#35 = "N"; |Y sw_print = 0;
       sw_cab = 0;
       sw_inf = 2;
       |SI swtotal = 1; |PRINT; |FINSI;
       sw_cab = 0;
   |FINSI;
   |SI sw_print = 0; |Y swtotal = 1;
       |PIEINF;
   |FINSI;
|FPRC;

|PROCESO borrar;
  s_debe  = 0;
  s_haber = 0;
  s_saldo = 0;
  swant   = 0;
|FPRC;


|| **********************************************************************
|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #2 dirfich0;
   |PATH_DAT #3 dirfich0;
   |PATH_DAT #4 dirfich0;
   |PATH_DAT #7 dirfich0;
   |PATH_DAT #13 dirfich0;
   |PATH_DAT #107 dirfich0;
   |PATH_DAT #31 dirfich0;
   |PATH_DAT #999 dirfich0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 000001;
 999999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;
