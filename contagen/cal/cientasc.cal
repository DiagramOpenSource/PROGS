|FICHEROS;
   cientasc #0;
   cientasl #1;
   cacuenta #2;
   cisalant #3;
   cientcon #4,MANTE;
   cadiario #10;
   caclipro #18;

   cafantas #98;
   :/modelos/def/dsmom030 #5;
   &regisnif@ #709;

   :/integral/def/dsam0103,MANTE;

   caw00041 #998,MANTE;
   espejo@  #999;
|FIN;

|VARIABLES;
   aAlfa1       = "";
   nModoEntrada = 0;
   &entrada     = 1;
   saliext      = 0;
   &ext1 = "";
   &ext2 = 0;
   alfa1    = "";
   sw       = 0;
   cta      = "            ";
   ctanum   = 0;
   ctaalf   = " ";
   longalf  = " ";
   longnum  = " ";
   nivel    = 0;
   nada = "";
   modo = 0;
   ini = 0;
   fecalf = "";
   mes = "";
   mesfec = 0;
   mesac = 0;
   a = 0;
   b = 0;
   c = 0;
   signo = "";
   elimporte = 0;
   &total = 0;
   &sumal = 0;
   &difer = 0;
   &fecapu = @;
   &sigapu = "";
   &descrira = "";

   &fecant = @;
   descri = "";
   nume1 = 0;
   swalgun = 0;
   &coant = 1;
   sgant = "D";
   deant = "";
   dpant = "";
   sdant = "";
   swborra = 0;
   xx = 0;
   gua_3  = @;
   gua_4  = 0;
   gua_5  = "";
   gua_6  = "";
   gua_7  = 0;
   gua_8  = "";
   gua_9  = 0;
   gua_10 = 0;
   gua_11 = "";
   gua_12 = "";
   gua_13 = "";
 sw_movim = 0;
   aBlancs = "";
 nInkey = 0;
 &nSwOpera = 0;
   mensa1   = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
   mensa2   = "    ERROR FECHA INFERIOR O IGUAL AL BLOQUEO DE DATOS";
   &snFiltr = "";
   &snFilAc = "";
   &snFilBa = "";

   nDImporte = 0;
   nHImporte = 0;
   aDef      = "";
   aMas      = "";
   nNumero   = 0;
   aFichero  = "";
   nConNum   = 0;
   nAlta     = 0;
   &snFiltrA = "";
|FIN;

|INCLUYE dscont_i;

|CALCULO alinicio;  |TIPO 8;

   |HAZ inicial;
   |SI swerror = 2; |PTEC 807; |ACABA; |FINSI;

   |HAZ DirAplicSt;
   fecapu = ~;
   nume1 = fecapu;
   nume1 = nume1 - 1;
   fecant = nume1;
   enFilConc = 1;          || Conceptos sin IVA
   |HAZ CreaConcepto;

   nSwOpera = 0;
   |SUB_EJECUTA_NP "caz00017";
   aAlfa1 = "cactaesp"; |NOME_DAT #2  aAlfa1;
   aAlfa1 = "cadiaesp"; |NOME_DAT #10 aAlfa1;

   dpant = eaDfDepC;
   sdant = eaDfSubC;

   |HAZ eLeeParametro;
|FCAL;

|RUTINA Uno1;
   #0#0 = "            ";
|FRUT;

|RUTINA Dos1;
 #0#0 = "zzzzzzzzzzzz";
|FRUT;

|CALCULO elnivel;  |TIPO 6;
   nInkey = FSalida;

  |ABRE #3;
  #3#0 = "            ";
  |LEE 040#3=;
  |SI FSalida ! 0;
      |DDEFECTO #3;
      |GRABA 020#3n;
  |FINSI;
  |CIERRA #3;

  |SI nInkey ! 10; |Y nInkey ! 11;
      nModoEntrada = (FEntrada / 100) ! 0;
      |SI nModoEntrada = 1;
           eaCuenta = #0#0; |QBF eaCuenta;
           |SI eaCuenta = "";
               |MENAV "    Cuenta Erronea ";
               |ERROR;
           |FINSI;
       |FINSI;
   |FINSI;

  |SI nInkey = 11;
      |PUSHV 0501 2380;
      |CONSULTA_F_CLAVE #1#0, Uno1, Dos1;
      |POPV;
      |PINTA #0#0;
      |ERROR;
      |ACABA;
  |FINSI;

  eaCuenta = #0Campo;
  salidas  = nInkey;
  |HAZ FiltraPuntos;
  #0Campo = eaCuenta;
  |PINTA #0Campo;
  coant = 0;
|FCAL;

|CALCULO elnivel0;  |TIPO 0;
   emCta    = 1;
   eaCuenta = #0#0;
   |HAZ SacaNivel;
   |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea
   #0#1= enNivel;

   |HAZ LeeCuenta;
|FCAL;

|CALCULO OtroNivel0; |TIPO 0;
   emCta    = 0;
   eaCuenta = #0#0;
   |HAZ SacaNivel;
   #0#1= enNivel;
|FCAL;
||*******************************************************
|PROCESO LeoEspejo;
  nNumero = #999#2;
  nConNum = nConNum + 1;
  #998#0  = #999#0;
  #998#1  = #999#1;
  #998#2  = #999#2;
  #998#3  = #999#3;
  #998#4  = #999#4;
  #998#5  = #999#5;
  #998#6  = #999#6;
  #998#7  = #999#7;
  #998#8  = #999#8;
  #998#9  = #999#9;
  #998#10 = #999#10;
  #998#11 = #999#11;
  #998#12 = #999#12;
  #998#13 = #999#13;
  #998#14 = #999#14;
  #998#15 = #999#15;
  #998#16 = #999#16;
  #998#17 = #999#17;
  |GRABA 020#998n;
|FPRC;

|DEFBUCLE LeoEspejo;
 #999#1002;
 7;
 #1#0, 00001, nDImporte;
 #1#0, 99999, nHImporte;
 ;
 NULL, LeoEspejo;
|FIN;
||*******************************************************
|PROCESO Tipo11W41; |TIPO 11;
  nNumero = #998#2;
|FPRC;
||*******************************************************
|PROCESO Alta998;
 #998#2 = 00001;
|FPRC;

|PROCESO Baja998;
 #998#2 = 99999;
|FPRC;

|CALCULO Tipo11C1; |TIPO 11;

   |SI FSalida = 11;

       |ERROR;

       |PUSHV 1022 2353;
       |PINTA 1023, "                              ";
       |PINTA 1123, " Del importe:                 ";
       |PINTA 1223, " Al importe :                 ";
       |PINTA 1323, "                              ";
       |CUADRO 1022 1352;
       |C_P #98#12, 1, 1137; |PINTA #98#12;
       |C_P #98#13, 1, 1237; |PINTA #98#13;

    |ET Camp10;
       |ENCAMPO #12#98;
       |SI FSalida = 1; |VETE  Campo0; |FINSI;
       #98#13 = #98#12; |PINTA #98#13;

    |ET Camp11;
       |ENCAMPO #13#98;
       |SI FSalida = 2; |VETE Camp10; |FINSI;
       |SI #98#13 < #98#12;
           |MENAV "    Error. Importe inferior al desde";
           |VETE Camp11;
       |FINSI;

    |ET Campo0;
       |POPV;
       nDImporte = #98#12;
       nHImporte = #98#13;

       nNumero = 0;
       nConNum = 0;
       |PUSHV 0801 2080;
       |DBASE aMas;
       aDef = aMas + "def/cientasl";
       |CARGA_DEF aDef, espejo@;
       |SI FSalida ! 0;
           |MENAV "    Error al Carga fichero de cientasl";
           |POPV;
           |ACABA;
       |FINSI;

       aFichero = "5x" + Usuario;
       |NOME_DAT #998 aFichero;
       |DELINDEX #998;

       |ABRE #998;
       |BUCLE LeoEspejo;
       |CIERRA #998;

       |SI nConNum = 0;
           |MENAV "    No Existe Apuntes dentro de la Seleccion";
       |SINO;
           |SI nConNum ! 1;
               |PINPA #0#998;
               |ATRI R; |PINTA 0843, #98#12;
                        |PINTA 0863, #98#13; |ATRI 0;

               |ENTLINEAL #1#998, -1, 4, 20, 0, Alta998, Baja998;
           |FINSI;
       |FINSI;

       |DESCARGA_DEF #espejo@;
       |POPV;

       |SI nNumero ! 0;
           #1#2 = nNumero;
           |PTEC 802;
           |PONCONTROL 23, "SI";
       |FINSI;
  |FINSI;
|FCAL;

|| ***** Procesos desde las lineas
|PROCESO borracontra;
  elimporte = - #cientcon#8;
  |SI #1#6 = "D";  signo = "H";  |SINO;  signo = "D";  |FINSI;
  MasMenos     = elimporte;
  DebeHaber    = signo;
  aCUENTA      = #cientcon#4;
  nNIVELCUENTA = #cientcon#5;
  nREGISTRO    = -1;
  nDIARIO      = 01;     ||No tiene diario asignado
  aFECHA       = #1#3;
  nDOCUMENTO   = -1;
  |HAZ ActCuentM;          || cuenta de pase Directo S
  |BORRA 101#cientcon.a;
  |LIBERA #cientcon;
|FPRC;

|DEFBUCLE borracontra;
   #cientcon#1;
   ;
   #1#0, #1#2, 1;
   #1#0, #1#2, 99999;
   ;
   NULL,borracontra;
|FIN;

|CALCULO refresca; |TIPO 2;    || desde campo linea
   modo = ((FEntrada / 100) ! 0);

   |SI #1#6 = "D";
       #0#5 = #0#5 +. #1#7;
   |SINO;
       #0#6 = #0#6 +. #1#7;
   |FINSI;
   #0#7 = #0#4 + #0#5 - #0#6;
   |PINTA #0#5;
   |PINTA #0#6;
   |PINTA #0#7;

    signo = #1#6;
    MasMenos     = 0 +. #1#7;
    DebeHaber    = signo;
    aCUENTA      = #1#0;
    nNIVELCUENTA = #1#1;
    nREGISTRO    = -1;
    nDIARIO      = 01;     ||No tiene diario asignado
    aFECHA       = #1#3;
    nDOCUMENTO   = -1;
    |HAZ ActCuentM;          || cuenta de pase Directo S

    |SI #1#8 ! "            ";
        |HAZ a_cuentas;
        |HAZ pinta_cta;
   |SINO;
       |SI modo = 3;
           |BUCLE borracontra;
       |FINSI; ||modo baja
   |FINSI;
|FCAL;

|CALCULO alsalir; |TIPO 3;
   |SI #1#7 = 0;
      |MENAV "     ATENCION!  El importe NO puede ser CERO";
      |ERROR;
      |ACABA;
   |FINSI;

   |SI #1#8 ! "            ";
       enFilConc = 11;
       eaCuenta   = #1#8;
       enConcepto = #1#10;
       |HAZ FiltroConcepto;
       |SI eswLect ! 0;
           |ERROR;
           |ACABA;
       |FINSI;
   |FINSI;

   |SI #1#8 ! "            ";
       |BUCLE borracontra;
   |FINSI;

   coant = #1#4;
   deant = #1#5;
   dpant = #1#12;
   sdant = #1#13;
   sgant = #1#6;
         gua_3  = #1#3;
         gua_4  = #1#4;
         gua_5  = #1#5;
         gua_6  = #1#6;
         gua_7  = #1#7;
         gua_8  = #1#8;
         gua_9  = #1#9;
         gua_10 = #1#10;
         gua_11 = #1#11;
         gua_12 = #1#12;
         gua_13 = #1#13;
|FCAL;

|RUTINA a_cuentas;
    |SI signo = "D";  signo = "H";  |SINO;  signo = "D";  |FINSI;
    MasMenos     = 0 +. #1#7;
    DebeHaber    = signo;
    aCUENTA      = #1#8;
    nNIVELCUENTA = #1#9;
    nREGISTRO    = -1;
    nDIARIO      = 01;     ||No tiene diario asignado
    aFECHA       = #1#3;
    nDOCUMENTO   = -1;
    |HAZ ActCuentM;          || cuenta de pase Directo S
|FRUT;

|RUTINA pinta_cta_t;
   |ABRE #cacuenta;
   #cacuenta#0 = #1#8;
   #cacuenta#1 = #1#9;
   |LEE 001#cacuenta.=;
   |SI FSalida ! 0;  |DDEFECTO #cacuenta;  |FINSI;
   |CIERRA #cacuenta;
|FRUT;

|CALCULO pinta_cta; |TIPO 13; ||Desde campo Contrapartida y "refresca"
   |SI #1#8 ! "            ";
       |HAZ pinta_cta_t;
   |SINO;
       |DDEFECTO #cacuenta;
   |FINSI;
   |ATRI I;
   |PINTA 2102, #cacuenta#2;
   |PINTA 2140, #cacuenta#51;
   |PINTA 2154, #cacuenta#52;
   |PINTA 2168, #cacuenta#53;
   |ATRI 0;

   aBlancs = " " * 78;
   |PINTA 2202, aBlancs; |PINTA 2302, aBlancs;
   xx = (FEntrada / 100) ! 0; |SI xx = 1; |ACABA; |FINSI;
   |SI eaDepar = "S";
       eOpDep = 0;
       enActividad = #1#12;
       eaCodDep    = #1#12;
       |HAZ Actividad;
       |HAZ pDepartamento;
   |FINSI;
   |SI eaSubde = "S";
       eOpSub = 0;
       enConcepto  = #1#4
       eaCodSubdep = #1#13;
       |HAZ SubActividad;
       |HAZ pSubdepar;
   |FINSI;
|FCAL;

|CALCULO defectos; |TIPO 0;  ||Desde Campo Linea
   |ABRE #cisalant;
   |DDEFECTO #cisalant;
   #cisalant#0 = #0#0;
   |LEE 001#cisalant.=;
   |CIERRA #cisalant;

   modo = ((FEntrada / 100) ! 0);
   |SI modo =  1;
       #1#4  = coant;
       #1#12 = dpant;
       #1#13 = sdant;
       |SI coant = 0;
           #1#4  = #cisalant#8;
           #1#5  = #cisalant#10; deant = #1#5; |PINTA #1#5;
       |FINSI;

       #1#8 = #cisalant#5;
       #1#9 = #cisalant#6;
       #1#3 = fecapu;

       |PINTA #1#3;
       |PINTA #1#4;
       |SI #cisalant#14 = "S"; #1#5  = deant; |PINTA #1#5; |FINSI;
       |PINTA #1#8;
       |PINTA #1#13;
       |PINTA #1#12;
   |SINO;
        coant = #1#4;
        deant = #1#5;
        dpant = #1#12;
        sdant = #1#13;
        sgant = #1#6;
   |FINSI;

   aAlfa1 = #cisalant#9;  |C_M #1#4, 1, aAlfa1;
   aAlfa1 = #cisalant#11; |C_M #1#5, 1, aAlfa1;

   aAlfa1 = #cisalant#12; |C_M #1#10, 1, aAlfa1;
   aAlfa1 = #cisalant#13; |C_M #1#11, 1, aAlfa1;

|FCAL;

|CALCULO lafecha; |TIPO 0;  || Desde campo Fecha
   modo = ((FEntrada / 100) ! 0);

   fecapu = #1#3;
   |SI #1#3 < fec1;  |O #1#3 > fec2;
       |MENAV mensa1;
       |ERROR;
       |ACABA;
   |SINO;
       |SI #1#3 [ fec3; |Y modo [ 2;
           |MENAV mensa2;
           |ERROR;
           |ACABA;
       |FINSI;
   |FINSI;

   |SI #1#3 < #0#3;
      |MENAV "     FECHA YA ACTUALIZADA";
      |ERROR;
      |ACABA;
   |FINSI;
   fecant = #1#3;
|FCAL;

|CALCULO concepto; |TIPO 0;  || desde Campos conceptos

   |SI FSalida = 10;
       enConcepto = #1#4;
       |HAZ VerConcepto;
       #1#4 = enConcepto; |PINTA #1#4;
   |FINSI;

   modo = ((FEntrada / 100) ! 0);
   enFilConc  = 1;
   eaCuenta   = #0#0;
   enConcepto = #1#4;
   |HAZ FiltroConcepto;
   |SI eswLect ! 0;
       |C_M #1#4, 1, "S";
       |ACABA;
   |FINSI;

   |SI modo = 1;
       |SI #1#4 = coant; |Y #cisalant#14 = "S";
           #1#5 = deant;
       |SINO;
           |SI #1#4  = #cisalant#8;
               #1#5  = #cisalant#10;
           |SINO;
               #1#5  = #dsmom030#1;
          |FINSI;
       |FINSI;
   |SINO;
       |SI #1#4 = coant;
           #1#5 = deant;
       |SINO;
           #1#5 = #dsmom030#1;
       |FINSI;
   |FINSI;
   |PINTA #1#5;
   #1#10 = #1#4; |PINTA #1#10;
   #1#11 = #1#5; |PINTA #1#11;
   coant = #1#4;
|FCAL;

|CALCULO otroconce; |TIPO 0;  ||Desde Concepto 10/11
   |SI FSalida = 2;  |ACABA; |FINSI;

   modo = ((FEntrada / 100) ! 0);
   |SI modo = 1;  |Y #1#10 = #1#4;
       #1#11 = descrira;
       |PINTA #1#11;
   |FINSI;

   enFilConc = 1;
   eaCuenta   = #1#8;
   enConcepto = #1#10;
   |HAZ FiltroConcepto;
   |SI eswLect ! 0; |ACABA; |FINSI;

   |SI #1#10 ! #1#4;
       #1#11 = #dsmom030#1;
       |PINTA #1#11;
   |FINSI;
|FCAL;

|CALCULO alfin;  |TIPO 7;  ||Desde Campos descripcion Concepto
   |C_M #1Campo,0,aAlfa1;
   |SI aAlfa1 = "S"; |Y eaSobreEs ! "S";
       |PTEC 816;
   |FINSI;
|FCAL;

|CALCULO descripcion; |TIPO 0;  ||Desde Campo Descripcion Concepto
  |SI FSalida = 10;
       #1#5 = deant;
       |PINTA #1#5;
   |FINSI;

   #1#11 = #1#5; |PINTA #1#11;
   descrira = #1#5;
|FCAL;

|PROCESO lee_clien;
     |ABRE #18;
     #18#23 = "C";
     #18#10 = eaCuenta;
     |LEE 041#18=;
     |SI FSalida = 0;
         nAlta = 1;
     |SINO;
         |DDEFECTO #18;
         #18#23 = "P";
         #18#10 = eaCuenta;
         |LEE 041#18=;
         |SI FSalida = 0;
             nAlta = 1;
         |FINSI;
     |FINSI;
     |CIERRA #18;
|FPRC;

|PROCESO Para347;
     nAlta = 0;
     |SI eaCP347 = "S"; |Y #dsmom030#7 = "S";
         eaCuenta = #1#8;
         |HAZ lee_clien;
     |FINSI;

     |SI nAlta = 0;
         #1#14 = 0;
         #1#15 = 0;
         |C_V #1#14,1, "N";
         |C_M #1#14,1, "N";
     |SINO;
         |C_V #1#14,1, "S";
         |C_M #1#14,1, "S";
         |SI #1#14 = 0;
             #1#14 = enEjercicio;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO AntesPara347; |TIPO 7;
     |C_M #1#14,0,aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     |PUSHV 1153 1480;
     |PINTA 1155, "                       ";
     |PINTA 1255, "                       ";
     |PINTA 1355, "                       ";
     |PINTA 1455, "                       ";
     |D_CUADRO 1155,1478;
     |ATRI R; |PINTA 1256, " Ejercicio Operacion "; |ATRI 0;
     |PINTA #1#14;

|FPRC;

|PROCESO TrasPara347; |TIPO 0;
     |C_M #1#14,0,aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     |SI #1#14 < 2000; |ERROR; |ACABA; |FINSI;
     |POPV;
     |C_V #1#14,1, "N";
     |C_M #1#14,1, "N";
|FPRC;

|| ==========================================================================
|PROCESO c_departa; |TIPO 7;
   |SI eaDepar  = "N";
      |CAMPO_MODIFICA #1#12, 1, "N";
   |SINO;
      |CAMPO_MODIFICA #1#12, 1, "S";
      #1#12= dpant; |PINTA #1#12;
      |SI Haybasica = 1;
          |MENSA "    <F2> Consulta de actividades";
      |SINO;
          |MENSA "    <F2> Consulta de departamentos";
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO c_subdepar; |TIPO 7;
   |SI eaSubde  = "N";
      |CAMPO_MODIFICA #1#13, 1, "N";
   |SINO;
      |CAMPO_MODIFICA #1#13, 1, "S";
      #1#13= sdant; |PINTA #1#13;
      |SI Haybasica = 1;
         |SI enArrend ! 2;
            |MENSA "    Subdepartamento: Referencias Catastrales <F2> Consultar <F3> Modificar";
         |SINO;
            |MENSA "    Subdepartamento. <F2> Consulta de Cultivos";
         |FINSI;
      |FINSI;
   |FINSI;
|FPRC;

|CALCULO elsigno7; |TIPO 7;
 |SI sgant = ""; sgant = "D"; |FINSI;
 #1Campo = sgant;
 |PINTA #1Campo;
|FCAL;
***************************************************************************

|PROCESO coge_3; |TIPO 7;                || desce campo 'fecha'
  modo = (FEntrada / 100) ! 0;   |SI modo ! 1;  |ACABA;   |FINSI;
  |SI gua_3 [ "01.01.0000";     |ACABA;   |FINSI;
  #1Campo = gua_3;
  |PINTA #1Campo;
|FPRC;

|PROCESO coge_4; |TIPO 7;                || desce campo 'Concepto'
   modo = (FEntrada / 100) ! 0;   |SI modo ! 1;  |ACABA;   |FINSI;
  |SI gua_4 = 0; |ACABA;   |FINSI;
  #1Campo = gua_4;
  |PINTA #1Campo;
  |SI gua_10 = 0; gua_10 = #1Campo; |FINSI;
|FPRC;

|PROCESO lleva_4; |TIPO 0;
  modo = (FEntrada / 100) ! 0;  |SI modo ! 1;  |ACABA;   |FINSI;
  gua_10 = #1Campo;
  #1#10 = gua_10;  |PINTA #1#10;
|FPRC;

|PROCESO coge_5; |TIPO 7;                || desce campo 'Descripcion'
    modo = (FEntrada / 100) ! 0;
    |SI modo = 1;
        |SI #1#4 = gua_4;
            |PINTA #1Campo;
        |FINSI;
        |SI eaSobreEs ! "S";
            |PTEC 816;
        |FINSI;
    |FINSI;
|FPRC;

|PROCESO lleva_5; |TIPO 0;
 |SI FSalida = 0;
     gua_11 = #0Campo;
     #1#11 = gua_11;  |PINTA #1#11;
 |FINSI;
|FPRC;

|PROCESO coge_6; |TIPO 7;                || desce campo 'D/H'
   modo = (FEntrada / 100) ! 0;   |SI modo ! 1;  |ACABA;   |FINSI;
   |SI gua_6 ! "D";|Y gua_6 ! "H";    |ACABA;   |FINSI;

   #1Campo = gua_6;
   |PINTA #1Campo;
|FPRC;

|PROCESO coge_7; |TIPO 7;                || desce campo 'Importe'
    modo = (FEntrada / 100) ! 0;   |SI modo ! 1;  |ACABA;   |FINSI;
    #1Campo = gua_7;
   |PINTA #1Campo;
|FPRC;

|PROCESO coge_8; |TIPO 7;                || desce campo 'Contrapartida'
    modo = (FEntrada / 100) ! 0;   |SI modo ! 1;  |ACABA;   |FINSI;
    |SI gua_8 = "";     |ACABA;   |FINSI;
    #1Campo = gua_8;
    |PINTA #1Campo;
|FPRC;

|PROCESO coge_10; |TIPO 7;                || desce campo 'Concepto 2'
   modo = (FEntrada / 100) ! 0;   |SI modo ! 1;  |ACABA;   |FINSI;
   |SI gua_10 = 0;     |ACABA;   |FINSI;

   #1Campo = gua_10;
   |PINTA #1Campo;
|FPRC;

|PROCESO coge_11; |TIPO 7;                || desce campo 'Descrp.2'
    modo = (FEntrada / 100) ! 0;
    |SI modo = 1;
       |SI #1#10 = gua_10;
           #1Campo = gua_11;
           |PINTA #1Campo;
       |FINSI;
       |SI eaSobreEs ! "S";
           |PTEC 816;
       |FINSI;
   |FINSI;
|FPRC;

|| ================= desde  Aqui
|PROCESO iActividad; |TIPO 0;
   |SI eaDepar = "N"; |ACABA; |FINSI;

   aBlancs = " "* 78; |PINTA 2202, aBlancs;
   eOpDep = 0;
   |SI FSalida = 10;  eOpDep = 1; |FINSI;
   enActividad = #1#12;
   eaCodDep    = #1#12;
   |HAZ Actividad;
   |SI eswLect = 1;
       |MENAV "    Error.! No Existe Actividad / Departamento";
       |ERROR;
   |SINO;
       #1#12 = eaCodDep; |PINTA #1#12;
       |HAZ pDepartamento;
       |SI efActFin > "01.01.1900"; |Y snFilBa ! "N";
           aAlfa1 = "    NAtencion!!!. Actividad con Fecha de BAJA " + efActFin + ", Desea introducir Facturas ? ";
           |CONFI aAlfa1;
           |SI FSalida ! 0;
               |ERROR;
               |ACABA;
           |FINSI;
       |FINSI;
       |SI eaEsCom = "S";
           |CONFI "    NAtencion!!!. Actividad Comunera, ¨ Desea introducir Movimientos ? ";
           |SI FSalida ! 0;
               |ERROR;
               |ACABA;
           |FINSI;
       |FINSI;
   |FINSI;
   dpant = #1#12;
|FPRC;

|PROCESO iSubActividad; |TIPO 0;
   |SI eaSubde = "N"; |ACABA; |FINSI;

   eOpSub = 0; || 1 = Consulta pantalla
   |SI FSalida = 10; eOpSub = 1; |FINSI;
   |SI FSalida = 11; eOpSub = 2; |FINSI;

   aBlancs = " " * 78; |PINTA 2302, aBlancs;
   enConcepto  = #1#4;
   eaCodSubdep = #1#13;
   enQue = 1;
   |HAZ SubActividad;
   enQue = 0;
   |SI eswLect = 1;
       |MENAV "    Error.! No Existe SubDepartamento";
       |ERROR;
   |SINO;
       #1#13 = eaCodSubdep; |PINTA #1#13;
       |HAZ pSubdepar;
   |FINSI;
   sdant = #1#13;
|FPRC;
|| ================= Hasta aqui

|CALCULO debehaber; |TIPO 0;  ||Desde Campo Signo
   #1Campo = #1Campo > " ";
   |SI #1Campo = "0";  #1Campo = "D";  |FINSI;
   |SI #1Campo = "1";  #1Campo = "H";  |FINSI;
   |PINTA #1Campo;
   sigapu = #1Campo;
|FCAL;

|CALCULO importe; |TIPO 0;  ||Desde campo importe
   |SI #1Campo = 0;
      |MENAV  "    ATENCION IMPORTE CERO";
      |ERROR;
      |ACABA;
   |FINSI;
|FCAL;

|CALCULO duplico;  |TIPO 0;   ||Desde campo descripcion 11
   |SI FSalida = 10;
      #1#11 = descrira;
      |PINTA #1#11;
   |FINSI;

   |SI #1#8 ! "            ";
       |HAZ Para347;
   |SINO;
       #1#14 = 0;
   |FINSI;
|FCAL;


|| ********* PROCESOS DEL CAMPO DE CONTRAPARTIDA

|PROCESO elnivelin;  |TIPO 6;
     eaCuenta = #1Campo;
     saliext  = FSalida;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #1Campo = eaCuenta;
     |PINTA #1Campo;

     cta = #1Campo;
     |QBF cta;
     |SI cta = "";
         |HAZ contrapartidas;
         |ACABA;
     |FINSI;

     eaCuenta = #1#8; |HAZ SacaNivel; #1#9 = enNivel;
     |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea

     |SI saliext = 9;
         ext1 = #1#8;
         ext2 = #1#9;
         |SUB_EJECUTA "caextrap"
         saliext = 0;
     |FINSI;
|FPRC;

|PROCESO elniveli0;  |TIPO 0;
    cta = #1Campo;
    |QBF cta;
    |SI cta = "";  |ACABA; |FINSI;

    |SI #cacuenta#3 ! "S";
       |MENAV "     LA CUENTA NO ES DE PASE DIRECTO";
       |ERROR;
       |ACABA;
    |FINSI;

    |C_M #1#10, 0, aAlfa1;
    |SI aAlfa1 = "N";
        enFilConc = 11;
        eaCuenta   = #1#8;
        enConcepto = #1#10;
        |HAZ FiltroConcepto;
        |SI eswLect ! 0;
            |SI eswLect = 4;
               |ERROR;
           |SINO;
               |C_M #1#10, 1, "S";
           |FINSI;
           |ACABA;
       |FINSI;
   |FINSI;

   |HAZ pinta_cta;
   |HAZ comprueba_alta;
|FPRC;

|PROCESO quemodifi; |TIPO 0;
   |SI #1#8 = "            ";
      |CAMPO_MODIFICA #1#10, 1, "N";
      |CAMPO_MODIFICA #1#11, 1, "N";
   |SINO;
       aAlfa1 = #cisalant#12; |C_M #1#10, 1, aAlfa1;
       aAlfa1 = #cisalant#13; |C_M #1#11, 1, aAlfa1;
   |FINSI;
|FPRC;

|RUTINA menorl;
   #cientcon#0 = #1#0;
   #cientcon#1 = #1#1;
   #cientcon#2 = #1#2;
   #cientcon#3 = 1;
|FRUT;

|RUTINA mayorl;
   #cientcon#0 = #1#0;
   #cientcon#1 = #1#1;
   #cientcon#2 = #1#2;
   #cientcon#3 = 99999;
|FRUT;

|PROCESO hazlasuma;
   sumal = sumal + #cientcon#8;
   swalgun = 1;

   |SI #cisalant#12 = "N"; #cientcon#6 = #cientasl#4; |FINSI;
   |SI #cisalant#13 = "N"; |Y #cientcon#6 = #cientasl#4;
       #cientcon#7 = #cientasl#5;
   |FINSI;
   |SI #cisalant#12 = "N"; |O #cisalant#13 = "N";
       |GRABA 020#cientcon.a;
   |FINSI;
   |LIBERA #cientcon;
|FPRC;

|DEFBUCLE hazlasuma;
   #cientcon#1;
   ;
   #1#0, #1#2, 0;
   #1#0, #1#2, 99999;
   be;
   NULL,hazlasuma;
|FIN;

|PROCESO contrapartidas;
   |PUSHV 0701 2380;
   swalgun = 0;
   total = #1#7;
   sumal = 0;
   |BUCLE hazlasuma;
   difer = total - sumal;
   |SI swalgun = 0;
      |ABRE #cientcon;
      #cientcon#0 = #1#0;
      #cientcon#1 = #1#1;
      #cientcon#2 = #1#2;
      #cientcon#3 = 1;
      #cientcon#4 = #cisalant#5;
      #cientcon#5 = #cisalant#6;
      #cientcon#6 = #1#10;
      #cientcon#7 = #1#11;
      #cientcon#8 = #1#7;

      |SI #1#6 = "D";  signo = "H";  |SINO;  signo = "D";  |FINSI;
      MasMenos     = #cientcon#8;
      DebeHaber    = signo;
      aCUENTA      = #cientcon#4;
      nNIVELCUENTA = #cientcon#5;
      nREGISTRO    = -1;
      nDIARIO      = 01;     ||No tiene diario asignado
      aFECHA       = #1#3;
      nDOCUMENTO   = -1;
      |HAZ ActCuentM;          || cuenta de pase Directo S

      |GRABA 101#cientcon.n;
      |CIERRA #cientcon;

      sumal = #1#7;
      difer = 0;
   |FINSI;

   aBlancs = " " * 78;
   |PINTA 2102, aBlancs;
   |PINTA 2202, aBlancs;
   |PINTA 2302, aBlancs;
   |ENTLINEAL #1#cientcon, -3, 2, 20, 1, menorl, mayorl;
   |POPV;
|FPRC;

|PROCESO comprueba_alta;
   nSwOpera = 1;
   eaCuenta = #cacuenta#0;
   |SUB_EJECUTA_NP "caz00017";
|FPRC;

|PROCESO elTipo33; |TIPO 3;
 sw_movim = 1;
|FPRC;

|PROCESO salir; |TIPO 9;
 |SI sw_movim = 0; |ACABA; |FINSI;
 |AVISO;
 |CONFI "2400SDesea actualizar las Introduccion Realizada S/N : ";
 |SI FSalida =  0;
     |SUB_EJECUTA_NP "ciactasc";
 |FINSI;
|FPRC;
