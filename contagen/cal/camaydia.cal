|FICHEROS;
   camaydia #0;
   camaycab #1;
   camaylin #2;
   cacuenta #4;
   caconimp #8;

   canempre #30;
   caselem1 #998;
   cawdatos #900;
   catmport #901;
   catmpor1 #902;
|FIN;

|VARIABLES;
   alfa4 = "";
   &eaImpresora = "";
   &entrada = 1;
   aAlfa1 = "";
   aParam = "";
   sw = 0;
   fecalf = "";
   men1   = "    NO EXISTE EMPRESA";
   mensa1 = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
   traba1 = "";
   asien = 0;
   varalf = "";
   &a_debe = 0;
   &a_haber = 0;
   &tde = 0;
   &tha = 0;
   &hoja = 0;
   d = "D";
   h = "H";
   informe = "";
   &sumay = "";
   &swlin = 0;
   &eaLaFec = "";

   Comodin  = "";
   Comodin1 = "";
   Numeracion = 0;
   Calc       = 0;
   Handle     = 0;
   aAlfa      = "";
   nSwAlgun   = 0;
   &eaTituloL = "";
   &enEjer    = 0;
   &enErMayor = 0;
   nCrear     = 0;

   nCalc  = 0;
   nCalc1 = 0;
   nDato  = 0;
   nHandTxt = 0;

{1}aContiene = "";
   aAlfaX  = "";
   aAlfaX1 = "";
   aAlfaX2 = "";
   aIP     = "";
|FIN;

|INCLUYE dscont_i;
|INCLUYE dslegv_i;

|| ***************************************************************
|CALCULO empieza; |TIPO 7;
   #0#0 = fec1;  |PINTA #0#0;
   #0#1 = fec2;  |PINTA #0#1;
   |HAZ DatosEmp;
|FCAL;

|CALCULO fechad; |TIPO 0;
   |SI #0#0 < fec1; |O #0#0 > fec2;
      |MENAV mensa1;
      |ERROR;
   |FINSI;
|FCAL;

|CALCULO fechah;
   |SI #0#1 < fec1; |O #0#1 > fec2; |O #0#1 < #0#0;
      |MENAV mensa1;
      |ERROR;
   |FINSI;
|FCAL;

|PROCESO DatosEmp; |TIPO 7;
 |ABRE #8;
 |DDEFECTO #8;
 #8#0 = 1;
 |LEE 011#8=;
 |CIERRA #8;
 #0#5 = #8#7; |PINTA #0#5;
 aAlfa1 = #8#8; |C_M #0#5,1,aAlfa1;
|FPRC;

|| *************************************************************************
||                          MEOLLO
|| *************************************************************************

|PROCESO RecogeDato;
   nCalc = nDato - 1;
   aAlfaX1 = aAlfaX;
   |PARA; |SI nCalc > 0; |HACIENDO;
      aAlfaX2 = aAlfaX1 - &9;
      |SI FSalida ! 0;
         nCalc1 = ((FSalida / 100) ! 0) + 99;
         aAlfaX2 = aAlfaX1 % nCalc1;
         aAlfaX1 = aAlfaX1 - aAlfaX2;
         aAlfaX1 = aAlfaX1 - &9;
      |FINSI;
      nCalc = nCalc - 1;
   |SIGUE;
   aAlfaX2 = aAlfaX1 - &9;
   |SI FSalida ! 0;
      nCalc1 = ((FSalida / 100) ! 0) + 99;
      aAlfaX1 = aAlfaX1 % nCalc1;
   |FINSI;
|FPRC;

|PROCESO PortadaCrystal;
   aAlfaX = "pr" + Usuario; |NOME_DAT #catmpor1#aAlfaX#;
   |DELINDEX #catmpor1; |ABRE #catmpor1;

   |DBASS aAlfaX; |QBF aAlfaX;
   aAlfaX = aAlfaX + "basica/fich/port" + Usuario + ".txt";
   |XABRE "", aAlfaX, nHandTxt;
   |PARA; |SI FSalida ] 0; |HACIENDO;
      |XLEE nHandTxt, 250, aAlfaX;
      |SI FSalida ] 0;
         nDato = 1; |HAZ RecogeDato;
         |SI aAlfaX1 = "0";
            |DDEFECTO #catmport;
            nDato = 2; |HAZ RecogeDato; #catmport#0 = aAlfaX1;
            nDato = 3; |HAZ RecogeDato; #catmport#1 = aAlfaX1;
         |FINSI;
         |SI aAlfaX1 = "1";
            nDato = 2; |HAZ RecogeDato; #catmport#2 = aAlfaX1;
            nDato = 3; |HAZ RecogeDato; #catmport#3 = aAlfaX1;
            nDato = 4; |HAZ RecogeDato; #catmport#4 = aAlfaX1;
            nDato = 5; |HAZ RecogeDato; #catmport#5 = aAlfaX1;
            nDato = 6; |HAZ RecogeDato; #catmport#6 = aAlfaX1;
            nDato = 7; |HAZ RecogeDato; #catmport#7 = aAlfaX1;
            nDato = 8; |HAZ RecogeDato; #catmport#8 = aAlfaX1;
            nDato = 9; |HAZ RecogeDato; #catmport#9 = aAlfaX1;
            nDato = 10; |HAZ RecogeDato; #catmport#10 = aAlfaX1;
            nDato = 11; |HAZ RecogeDato; #catmport#11 = aAlfaX1;
            nDato = 12; |HAZ RecogeDato; #catmport#12 = aAlfaX1;
            nDato = 13; |HAZ RecogeDato; #catmport#13 = aAlfaX1;
            nDato = 14; |HAZ RecogeDato; #catmport#14 = aAlfaX1;
         |FINSI;
         |SI aAlfaX1 = "2";
            |DDEFECTO #catmpor1;
            #catmpor1#0  = #catmport#3;
            #catmpor1#10 = "A";
            nDato = 2; |HAZ RecogeDato; #catmpor1#1  = aAlfaX1;
            nDato = 3; |HAZ RecogeDato; #catmpor1#2 = aAlfaX1;
            nDato = 4; |HAZ RecogeDato; #catmpor1#3 = aAlfaX1;
            nDato = 5; |HAZ RecogeDato; #catmpor1#4 = aAlfaX1;
            nDato = 6; |HAZ RecogeDato; #catmpor1#5 = aAlfaX1;
            nDato = 7; |HAZ RecogeDato; #catmpor1#6 = aAlfaX1;
            nDato = 8; |HAZ RecogeDato; #catmpor1#7 = aAlfaX1;
            |GRABA 020#catmpor1.n;
         |FINSI;
         |SI aAlfaX1 = "3";
            |DDEFECTO #catmpor1;
            #catmpor1#0  = #catmport#3;
            #catmpor1#10 = "R";
            nDato = 2; |HAZ RecogeDato; #catmpor1#1  = aAlfaX1;
            nDato = 3; |HAZ RecogeDato; #catmpor1#8 = aAlfaX1;
            nDato = 4; |HAZ RecogeDato; #catmpor1#9 = aAlfaX1;
            |GRABA 020#catmpor1.n;
         |FINSI;
      |FINSI;
   |SIGUE;

   |CIERRA #catmpor1;
   |DFICE aAlfaX; |QBF aAlfaX;
   aAlfaX1 = aAlfaX + "pr" + Usuario + ".dbf";

   aIP = ""; |IP_REMOTO aIP;

   aContiene = "";
   |ESPECIFICA "RBASS", aContiene;
   aAlfaX2 = aContiene1;

   aAlfaX2 = aAlfaX2 + "crystal";
   |SI aIP ! ""; |RMKDIR aAlfaX2; |SINO; |MKDIR aAlfaX2; |FINSI;
   aAlfaX2 = aAlfaX2 + "/detporta.dbf"; |QBF aAlfaX2;
   |SI aIP ! ""; |ENVIA_FICHERO aAlfaX1, aAlfaX2; |SINO; |COPIA_FICHERO aAlfaX1, aAlfaX2; |FINSI;

   |DELINDEX #catmpor1;
|FPRC;

|PROCESO contar;
   |SI #1#1 < #0#0;
       tde = tde + #1#4;
       tha = tha + #1#5;
   |FINSI;
|FPRC;

|PROCESO porimpre;
   |ABRE #4;
   #4#0 = #2#4;
   #4#1 = #2#5;

   |LEE 040#4=;
   |SI FSalida ! 0;
      |DDEFECTO #4;
   |FINSI;
   |CIERRA #4;

   |SI #2#8 = "D";
      a_debe = #2#9;
      a_haber = 0;
   |SINO;
      a_debe = 0;
      a_haber = #2#9;
   |FINSI;

   |PRINT; nSwAlgun = 1;
|FPRC;

|PROCESO bucleimp;

   |SI sw = 0;
       asien = #1#2;
       sw = 1;
   |FINSI;

   |SI asien ! #1#2;
      sumay = "SUMA Y SIGUE";
      swlin = 1;
      |PIEINF;
      asien = #1#2;
   |FINSI;

   eaLaFec = #1#1;
   eaLaFec = (eaLaFec % 102) + "/" + (eaLaFec % 402)
   |BUCLELIN 0porimpre#1;
|FPRC;

|DEFBUCLE camaydia0;
   #1#2;
   ;
        1, fec1, 00, 000001;
   999999, #0#0, 99, 999999;
   e;
   NULL, contar;
|FIN;

|DEFBUCLE camaydia1;
   #1#2;
   ;
        1, #0#0, 00, 000001;
   999999, #0#1, 99, 999999;
   e;
   NULL,NULL,NULL,NULL,NULL,bucleimp;
   #1#1,#1#3,#1#0,#1#2;
|FIN;


|CALCULO ElDiario; |TIPO 3;

    Impresora = "Crystal Reports";
    |IMPRE 0;
    |SI FSalida ! 0;  |ACABA;  |FINSI;

    |SI #48#27= "S";
        |HAZ MultiEmpresa;
    |SINO;
        |DFICE dirfich0;
        |HAZ HazTodo;
    |FINSI;
    |FINIMP;
|FCAL;

|PROCESO HazTodo;

    eaIa = #30#21;
    enM1 = 0;  ||#30#11;
    enM2 = 12; ||#30#12;
    |HAZ LeeDatosEmpresa;

    eaTituloL = #0#7; ||Titulo del Libro
    #900#13 = #0#0;
    #900#14 = #0#1;

    alfa4 = Impresora;
    alfa4 = alfa4 % 113;
    |SI #0#6 = "S"; |O alfa4 = "CrystalReport";
                    || ....  hay que generarlo aunque no se imprima
        eaImpresora = Impresora;
        enEjer = enEjercicio;
        |SUB_EJECUTA_NP ":basica/agiy0016";
        eaImpresora = "";
        |SI Impresora = "CrystalReport"; |HAZ PortadaCrystal; |FINSI;
    |FINSI;

    |SI alfa4 = "CrystalReport";
       informe = "ccmaydia";
    |SINO;
       informe = "camaydia";
       |SI #0#5 = "N";  informe = "canaydia"; |FINSI;
    |FINSI;

    |INFOR informe;
    |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

    |HAZ Borrar;
    hoja = #0#4;
    |BUCLE camaydia0;
    |BUCLE camaydia1;
    |SI nSwAlgun = 1;
        sumay = "SUMA TOTAL";
        swlin = 0;
        |PIEINF;
    |FINSI;
    |FININF;
|FPRC;

|PROCESO Borrar;
   a_debe  = 0;
   a_haber = 0;
   tde     = 0;
   tha     = 0;
   hoja    = 0;
   sumay   = "";
   swlin   = 0;
   sw      = 0;
   nSwAlgun = 0;
|FPRC;

|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #2 dirfich0;
   |PATH_DAT #4 dirfich0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 00001;
 99999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;

|| ------------------------------------------------------------------

|PROGRAMA;
  aParam = PARAMETRO;
  |QBF aParam;
  |SI aParam = "";
      |HAZ inicio;
      |MANTE #0;
  |SINO;
      |HAZ Legalia;
  |FINSI;
|FPRO;

||********************************************************************
|PROCESO BuscoUno;
   enErMayor = 0;
   |SI eaUltNiv = "N";
       eaCuenta = #2#4;
       |HAZ SacaNivel;
       |SI enNivel = dig3;  enErMayor = 2; |FINSI;
   |FINSI;
   |SI eaUltNiv = "S";
       eaCuenta = #2#4;
       |HAZ SacaNivel;
       |SI enNivel ! dig3;  enErMayor = 3; |FINSI;
   |FINSI;
|FPRC;

|PROCESO ComprueboDatos;
  enErMayor = 1;
  nCrear    = 0;
  |ABRE #2;
  |DDEFECTO #2;
  #2#0 = 00;
  #2#1 = fec1;
  #2#2 = 999999;
  #2#3 = 9999;
  |LEE 001#2];
  |SI FSalida = 0;
      |HAZ BuscoUno;
  |FINSI;
  |CIERRA #2;
  |SI enErMayor = 1;
      |CONFI "    SNo existe el Diario Resumido, desea Crearlo ahora";
      |SI FSalida = 0;
          enErMayor = 0;
          nCrear = 1;
      |FINSI;
  |FINSI;
  |SI enErMayor = 2; |O enErMayor = 3;
      aAlfa1 = "    NEl diario Resumido esta Creado al Ultimo Nivel, Continuar ";
      |SI enErMayor = 3;
          aAlfa1 = "    NEl diario Resumido NO esta creado al Ultimo Nivel, Continuar ";
      |FINSI;
      |CONFI aAlfa1;
      |SI FSalida = 0;
          enErMayor = 0;
      |SINO;
          |CONFI "    SDesea Crearlo ahora a Nivel Correcto ";
          |SI FSalida = 0;
              enErMayor = 0;
              nCrear = 1;
          |FINSI;
      |FINSI;
  |FINSI;
  |SI nCrear = 1;
      |SUB_EJECUTA_NP "camaysel.tab;LEGALIA";
  |FINSI;
|FPRC;

|PROCESO Legalia;

     ||... Legalia
     cod1 = eaLegEmp;
     cod2 = eaLegPer;
     emEmp = 1;  || Sin mensaje
     |HAZ leelo;
     |SI swerror ! 0;  |ACABA;   |FINSI;

     enLegEje = enEjercicio;

     |DBASE Comodin; |QBF Comodin;
     Comodin = Comodin + "fich/" + eaLegEmp + eaLegPer + "/";

     |PATH_DAT #1 Comodin;
     |PATH_DAT #2 Comodin;
     |PATH_DAT #4 Comodin;

     enErMayor = 0;
     |HAZ ComprueboDatos;
     |SI enErMayor ! 0; |ACABA; |FINSI;


     |DDEFECTO #0;
     #0#0 = fec1;
     #0#1 = fec2;
     #0#4 = 0;
     #0#5 = eaDatLeg;
     #0#8 = efFecLeg;

     enDatos = 1;
     informe = "camaydia";
     |SI #0#5 = "N"; informe = "canaydia"; enDatos = 0; |FINSI;

     hoja = #0#4;

     |SI eaFormato ! "S";
         Impresora = eaNomImp + "?" + eaUnidadBTmp + "\tmp1,,,?Diario";
     |SINO;
         Impresora = eaNomImp + ";" + eaUnidadBTmp + "\Diario.pdf";
         informe = "ccmaydia";
     |FINSI;

     |SI enBasImp ! 0;
          |IMPRE -1;
     |SINO;
          |IMPRE 0;
     |FINSI;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |INFOR informe;
     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

     eaIa = #30#21;
     enM1 = #30#11;
     enM2 = #30#12;
     |HAZ LeeDatosEmpresa;

     eaTituloL = #0#7; ||Titulo del Libro
     #900#13 = #0#0;
     #900#14 = #0#1;

     alfa4 = Impresora % 113;
     |SI #0#6 = "S"; |O alfa4 = "CrystalReport";
                    || ....  hay que generarlo aunque no se imprima
        eaImpresora = Impresora;
        |SI alfa4 = "CrystalReport"; eaImpresora = alfa4; |FINSI;
        enEjer = enEjercicio;
        |SUB_EJECUTA_NP ":basica/agiy0016";
        eaImpresora = "";
        |SI alfa4 = "CrystalReport"; |HAZ PortadaCrystal; |FINSI;
     |FINSI;

     |HAZ Borrar;
     |BUCLE camaydia0;
     |BUCLE camaydia1;

     |SI nSwAlgun = 1;
         sumay = "SUMA TOTAL";
         swlin = 0;
         |PIEINF;
     |FINSI;

     |FININF;
     |FINIMP;

     |DBASE Comodin1; |QBF Comodin1;
     Comodin1 = Comodin1 + "PresLibr/" + eaLegEmp + eaLegPer + "/";
     |SI eaFormato ! "S";
         Comodin1 = Comodin1 + "DIARIO_*.xls";
     |SINO;
         Comodin1 = Comodin1 + "DIARIO_*.*";
     |FINSI;
     Numeracion = enNumeLeg;
     |_PDIR Comodin1;
     |PARA; |SI FSalida = 0; |HACIENDO;
          Comodin = Comodin1 % -107; Comodin = Comodin % 0103;
          Numeracion = Comodin; Numeracion = Numeracion + 1;
          |_SDIR Comodin1;
     |SIGUE;

     |SI eaFormato ! "S";
         Comodin = eaUnidadBTmp + "/tmp1.xls";
     |SINO;
         Comodin = eaUnidadBTmp + "/Diario.pdf";
     |FINSI;
     |XABRE "C", Comodin, Handle;
     |SI FSalida ] 0;
          |XCIERRA Handle;
          |DBASE Comodin1; |QBF Comodin1;
          Comodin1 = Comodin1 + "PresLibr/";
          |MKDIR Comodin1;
          Comodin1 = Comodin1 + eaLegEmp + eaLegPer + "/";
          |MKDIR Comodin1;
          |SI eaFormato ! "S";
              Comodin1 = Comodin1 + "DIARIO_" + (("000" + Numeracion) % -103) + ".xls";
              aNomFLeg = "DIARIO_" + (("000" + Numeracion) % -103) + ".xls";
          |SINO;
              Comodin1 = Comodin1 + "DIARIO_" + (("000" + Numeracion) % -103) + ".pdf";
              aNomFLeg = "DIARIO_" + (("000" + Numeracion) % -103) + ".pdf";
          |FINSI;
          aAlfa = "";
          |IP_REMOTO aAlfa; |QBF aAlfa;
          |SI aAlfa ! "";
               |RECIBE_FICHERO Comodin, Comodin1;
          |SINO;
               |COPIA_FICHERO Comodin, Comodin1;
          |FINSI;

          nTipoL   = 1;
          nNumer   = Numeracion;
          efFecha1 = #0#0;
          efFecha2 = #0#1;
          enRegisLeg = 0;
          |HAZ eGrabaHistLegal;
     |FINSI;
|FPRC;
