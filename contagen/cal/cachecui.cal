|FICHEROS;
   cachecui #0;

   caclipro #1;
   camaneof #2;
   canempre #30;

   caselem1 #998;
|FIN;

|VARIABLES;
   &entrada = 1;
   aAlfa    = "";
   aAlfa1   = "";
   aAlfa2   = "";
   nNume1   = 0;

   desde     = "";
   aDCuenta  = "";
   aHCuenta  = "";
   aDNombre  = "";
   aHNombre  = "";
   swSelec   = 0;
   nSw       = 0;
   nError    = 0;
   &eaError  = "";
   &eaErro1  = "";
   &nSwLinea = 0;
   &eaGraba  = "";
   &eaCta    = "";
   &eaPais   = "";
   &eaIBAN   = "";
   informe   = "";
   inform1   = "cachecui";
   inform2   = "cachecu2";
   nCampo    = 0;
   nSwIBAN   = 0;
   nSwBIC    = 0;
   &eaBIC    = "";
   &enCual   = 0;
   nPrint    = 0;
|FIN;

|INCLUYE dscont_i;

|CALCULO LaSeleccion; |TIPO 0;
 |C_M #0#1, 1,"S"; |C_M #0#2, 1,"S";
 |C_M #0#3, 1,"S"; |C_M #0#4, 1,"S";
 |C_M #0#5, 1,"S"; |C_M #0#6, 1,"S";
 |C_M #0#7, 1,"S"; |C_M #0#8, 1,"S";
 |C_M #0#9, 1,"S"; |C_M #0#10,1,"S";
 |C_M #0#11,1,"S"; |C_M #0#12,1,"S";
 |C_M #0#13,1,"S"; |C_M #0#14,1,"S";
 |SI #0#0 = "C";
      |C_M #0#7, 1,"N"; |C_M #0#8, 1,"N";
      |C_M #0#9, 1,"N"; |C_M #0#10,1,"N";
      |C_M #0#11,1,"N"; |C_M #0#12,1,"N";
      |C_M #0#13,1,"N"; |C_M #0#14,1,"N";
 |FINSI;
 |SI #0#0 = "P";
      |C_M #0#1, 1,"N"; |C_M #0#2, 1,"N";
      |C_M #0#3, 1,"N"; |C_M #0#4, 1,"N";
      |C_M #0#5, 1,"N"; |C_M #0#6, 1,"N";
 |FINSI;
|FCAL;

|CALCULO Hasta; |TIPO 0;
 |C_M #0Campo,0,aAlfa1;
 |SI aAlfa1 = "S";
     nNume1 = Campo - 1;
     |SI #0Campo < #0nNume1;
         |MENAV "    Error!. Cuenta Hasta Menor que la Cuenta Desde";
         |ERROR;
     |FINSI;
 |FINSI;
|FCAL;

|CALCULO ElPtec; |TIPO 7;
 |C_M #0Campo,0,aAlfa1;
 |SI aAlfa1 = "S";
     |PTEC 816;
 |FINSI;
|FCAL;

|CALCULO SiModifico; |TIPO 0;
  aAlfa1 = #0Campo;
  |SI aAlfa1 = "X"; aAlfa1 = "N"; |FINSI;
  nCampo = Campo - 1;
  |C_M #0nCampo,1,aAlfa1;
|FCAL;

|| **********************************************************************

|PROCESO porimpre;
   swSelec = 0;
   |SI desde = "C";
       |SI #0#2 ! doce;
           |SI #1#10 ] #0#1; |Y #1#10 [ #0#2; swSelec = 1; |FINSI;
       |FINSI;
       |SI #0#4 ! doce;
           |SI #1#10 ] #0#3; |Y #1#10 [ #0#4; swSelec = 1; |FINSI;
       |FINSI;
       |SI #0#6 ! doce;
           |SI #1#10 ] #0#5; |Y #1#10 [ #0#6; swSelec = 1; |FINSI;
       |FINSI;
   |FINSI;
   |SI desde = "P";
       |SI #0#8 ! doce;
           |SI #1#10 ] #0#7; |Y #1#10 [ #0#8; swSelec = 1; |FINSI;
       |FINSI;
       |SI #0#10 ! doce;
           |SI #1#10 ] #0#9; |Y #1#10 [ #0#10; swSelec = 1; |FINSI;
       |FINSI;
       |SI #0#12 ! doce;
           |SI #1#10 ] #0#11; |Y #1#10 [ #0#12; swSelec = 1; |FINSI;
       |FINSI;
       |SI #0#14 ! doce;
           |SI #1#10 ] #0#13; |Y #1#10 [ #0#14; swSelec = 1; |FINSI;
       |FINSI;
   |FINSI;

   |SI swSelec = 0;
       |LIBERA #1;
       |ACABA;
   |FINSI;

   nError   = 0;
   eaError  = "";
   eaErro1  = "";
   nSwLinea = 0;
   eaIBAN   = "";
   eaBIC    = "";
   nSwIBAN  = 0;
   nSwBIC   = 0;
   eaGraba  = "N";

   aAlfa = #1#29; |QBF aAlfa; |SI aAlfa = ""; nError = 1; |FINSI;
   aAlfa = #1#30; |QBF aAlfa; |SI aAlfa = ""; nError = 1; |FINSI;
   aAlfa = #1#31; |QBF aAlfa; |SI aAlfa = ""; nError = 1; |FINSI;
   aAlfa = #1#32; |QBF aAlfa; |SI aAlfa = ""; nError = 1; |FINSI;

   |SI nError = 1;
       eaError = "Datos de la Cuenta Corriente no Informados";
       eaGraba = "N";
       nSwLinea = 2; |PRINT; nSwLinea = 1; nSw = 1;
   |FINSI;

   nPrint = 0;
   |SI #0#16 ! "X";
       aAlfa = #1#24; |QBF aAlfa; |SI aAlfa = ""; #1#24 = "ES"; |FINSI;

       eaCta  = #1#29 + #1#30 + #1#31 + #1#32;
       eaPais = #1#24;
       |SI nError = 0;  |HAZ CalcuIBAN; |FINSI;

       aAlfa1 = #1#40; |QBF aAlfa1;

       |SI aAlfa1 ! eaIBAN; |Y aAlfa1 ! ""; |Y eaIBAN ! "";
           nError  = 1;
           eaError = "Codigo IBAN no corresponde al CCC informado";
           eaGraba = "N";
           |SI #0#15 = "S"; |Y #0#16 = "S";
               nSwIBAN = 1;
               eaGraba = "S";
           |FINSI;
           nPrint = 1; || |PRINT; nSwLinea = 1; nSw = 1;
       |FINSI;

       |SI aAlfa1 = ""; |Y  eaIBAN !  "";
           nError = 1;
           eaError = "Codigo IBAN en blanco";
           eaGraba = "N";
           |SI #0#16 = "S";
               nSwIBAN = 1;
               eaGraba = "S";
           |FINSI;
           nPrint = 1; || |PRINT; nSwLinea = 1; nSw = 1;
       |FINSI;
   |FINSI;

   |SI #0#18 ! "X";

       |DDEFECTO #2;
       #2#0 = #1#29;
       #2#1 = #1#30;
       |LEE 041#2=;
       aAlfa2 = #2#9; |QBF aAlfa2;
       eaBIC  = #2#9;

       aAlfa1 = #1#41; |QBF aAlfa1;
       |SI aAlfa1 = "";
           eaGraba = "N";
           nError = 1;
           eaErro1 = "Codigo BIC en blanco";
           |SI aAlfa2 ! ""; |Y #0#18 = "S";
               nSwBIC  = 1;
               eaGraba = "S";
           |FINSI;
           nPrint = 1; || |PRINT; nSwLinea = 1; nSw = 1;
       |FINSI;

       |SI aAlfa2 ! ""; |Y aAlfa1 ! ""; |Y aAlfa1 ! aAlfa2;
           eaGraba = "N";
           nError = 1;
           eaErro1 = "Codigo BIC diferente al de la Entidad";
           |SI #0#17 = "S"; |Y #0#18 = "S";
               nSwBIC = 1;
               eaGraba = "S";
           |FINSI;
           nPrint = 1; || |PRINT; nSwLinea = 1; nSw = 1;
       |FINSI;
   |FINSI;

   |SI nPrint = 1;
       |PRINT; nSwLinea = 1; nSw = 1;
   |FINSI;

   |SI nSwIBAN = 1; |O nSwBIC = 1;
       |SI nSwIBAN = 1; #1#40 = eaIBAN; |FINSI;
       |SI nSwBIC  = 1; #1#41 = eaBIC;  |FINSI;
       |GRABA 020#1a;
   |FINSI;

   |LIBERA #1;
|FPRC;

|DEFBUCLE Leeimprime;
   #1#1;
   ;
   desde, aDCuenta;
   desde, aHCuenta;
   be;
   NULL, porimpre;
|FIN;

|PROCESO HazTodo;
   nSw = 0;

   |ABRE #2;
   |SI #0#0 ! "P";
       desde = "C";

       aDCuenta = #0#1;
       |SI aDCuenta > #0#3; |Y #0#4 ! doce; aDCuenta = #0#3; |FINSI;
       |SI aDCuenta > #0#5; |Y #0#6 ! doce; aDCuenta = #0#5; |FINSI;
       aHCuenta = #0#2;
       |SI aHCuenta < #0#4;                 aHCuenta = #0#4; |FINSI;
       |SI aHCuenta < #0#6;                 aHCuenta = #0#6; |FINSI;

       |BUCLE Leeimprime;
   |FINSI;

   |SI #0#0 ! "C";
       desde = "P";

       aDCuenta = #0#7;
       |SI aDCuenta > #0#9;  |Y #0#10 ! doce; aDCuenta = #0#9;  |FINSI;
       |SI aDCuenta > #0#11; |Y #0#12 ! doce; aDCuenta = #0#11; |FINSI;
       |SI aDCuenta > #0#13; |Y #0#14 ! doce; aDCuenta = #0#13; |FINSI;
       aHCuenta = #0#8;
       |SI aHCuenta < #0#10;                 aHCuenta = #0#10; |FINSI;
       |SI aHCuenta < #0#12;                 aHCuenta = #0#12; |FINSI;
       |SI aHCuenta < #0#14;                 aHCuenta = #0#14; |FINSI;

       |BUCLE Leeimprime;
   |FINSI;
   |CIERRA #2;

   |SI nSw = 1;  |PIEINF; |FINSI;
|FPRC;

|CALCULO impre;|TIPO 3;

 |IMPRE 0;
 |SI FSalida ! 0;
     |MENAV "    Proceso interrumpido";
     |ACABA;
 |FINSI;

 enCual = 0;
 |SI #0#16 = "X"; |Y #0#18 ! "X";   enCual = 1;         |FINSI;
 informe = inform1;
 |SI #0#16 ! "X"; |Y #0#18 ! "X";   informe = inform2;  |FINSI;

 |INFOR informe;
 |SI FSalida ! 0;
     |MENAV "    No existe informe ";
     |FINIMP;
     |ACABA;
 |FINSI;

 |SI #48#27 = "S";
     |HAZ MultiEmpresa;
 |SINO;
     |DFICE dirfich0; |QBF dirfich0;
     |HAZ HazTodo;
 |FINSI;

 |FININF;
 |FINIMP;
|FCAL;

||************************************************************************
|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #2 dirfich0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 000001;
 999999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;
