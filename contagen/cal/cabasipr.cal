|FICHEROS;
   cabasipr #0;
   cacuentm #1;    || temporal cuentas presupuestos

   caparbal #2;
   cabaexp0 #3;
   cabaexp2 #5;
   caexistc #6;
   caexistl #7;

   cacuenta #10;   || plan contable
   cacuepre #11;   || fichas presupuestarias
   canempre #30;
|FIN;

|VARIABLES;
   &entrada = 1;
   informe  = "cabasipr";

   mes      = 0;
   letrames = "";
   nivel    = 0;
   linea    = 0;

   activo = 0;
   pasivo = 0;
   debe1  = 0;
   debe2  = 0;
   debe3  = 0;
   debe4  = 0;
   haber1 = 0;
   haber2 = 0;
   haber3 = 0;
   haber4 = 0;

   activo_p = 0;
   pasivo_p = 0;
   debe1_p  = 0;
   debe2_p  = 0;
   debe3_p  = 0;
   debe4_p  = 0;
   haber1_p = 0;
   haber2_p = 0;
   haber3_p = 0;
   haber4_p = 0;

   sw_error = 0;

   saldo = 0;
   debe  = 0;
   haber = 0;
   saldo_p = 0;
   debe_p  = 0;
   haber_p = 0;

   x = 0;
   y = 0;
   eldesde = 0;
   elhasta = 0;
   eldes_p = 0;
   elhas_p = 0;
   des_exi  = 0;
   has_exi  = 0;

   sw = 0;
   agrupaci = 0;
   epigrafe = 0;
   sw_epi = 0;

   d = "A";
   h = "P";
   tipo = "";

   cue_perdi = "";
   dig_perdi = 0;
   act_cuent = "";
   act_nivel = 0;
   acor_c = "";
   acor_d = 0;
   alar_c = "";
   alar_d = 0;

   t_agru = "";
   t_epig = "";
   t_part = "";
   sw_exp = 0;

   &sw_inf = 0;
   &cantidad = 0;
   &cantidad_p = 0;
   &variable = "";
   &canti = 0;
   &canti_p = 0;
   &doh = "";

   descar = 0;

   alfa1 = "";
   runnom = "cabasipr";

   traraf = 0;
   &r_emp = 0;
   &r_per = 0;
   &r_eje = 0;
   &r_nom = "";
   &d_mes = 0;
   &h_mes = 0;
   &estru = 0;
   &aparam = "";
   d_cuen = "";
   h_cuen = "";
   d_digi = 0;
   h_digi = 0;
   nume1 = 0;
   nume2 = 0;
   des_pre = " ";
   has_pre = "z";
   t_agrupa = 0;

   aFichero = "";
   nSw      = 0;
   &eaTexto = "";
|FIN;

|INCLUYE dscont_i;
|INCLUYE digito_i;

|CALCULO inicio_b; |TIPO 8;
  |HAZ inicio;
  |HAZ DirAplicSt;
|FCAL;

|CALCULO estruc7; |TIPO 7;
 #0#6 = 1;
 |SI Haybasica = 1; |Y eaDepar = "S";
     #0#6 = 0;
 |FINSI;
 |PINTA #0#6;
|FCAL

|PROCESO presup2;    || si no esta como ficha presupuestaria no sale SI

   |DDEFECTO #11;
   #cacuepre#19 = t_agrupa;
   #cacuepre#0 = #cacuenta#0;
   #cacuepre#1 = #cacuenta#1;
   |LEE 001#11=;

   |PARA nume1 = 0; |SI nume1 [ 58; |HACIENDO nume1 = nume1 + 1;
         #cacuentm#nume1# = #cacuenta#nume1#;
   |SIGUE;
   |PARA nume1 = 59; |SI nume1 [ 70; |HACIENDO nume1 = nume1 + 1;
         nume2 = nume1 - 55;
         #cacuentm#nume1# = #cacuepre#nume2#;
   |SIGUE;
   #cacuentm#71 = #cacuepre#02;  || signo
   |GRABA 020#1n;
|FPRC;

|DEFBUCLE bu_pres2;   || balances situacion y explotacion
   #10#4;
   ;
   des_pre, 0,  0,  0,  0;
   has_pre, 4, 99, 99, 99;
   e;
   NULL, presup2;
|FIN;

|PROCESO presup0; |TIPO 0;
   |SI FSalida = 0;

      |HAZ pre_aviso;
      aFichero = ("9p" + Usuario) % 108;
      |NOME_DAT #1 aFichero;
      |DELINDEX #1;
      |ABRE #1;
      |ABRE #11;
      |SI runnom ! "caybalpr";   |BUCLE bu_pres2;   |FINSI;
      |CIERRA #1;
      |CIERRA #11;
      |POPV;
   |FINSI;
|FPRC;

|PROCESO pre_aviso;
   |PUSHV 11181760;
   |ATRI P;
   |CUADRO 11 18 15 60;
   |ATRI R;
   |CUADRO 12 19 14 59;
   |ATRI 0;
   |ATRI R;
   |ATRI I;
   |PINTA 1320, " Procesando Fichas Presupuestarias ... ";
   |ATRI 0;
|FPRC;

|PROCESO antesque;  |TIPO 8;
   aparam = PARAMETRO;
   |QBF aparam;
   |SI aparam = "fech";
      |NOME_DAT #10 "cabalano";
      |NOME_DAT #11 "cabalpre";
   |FINSI;
|FPRC;

|| ========================================================================
||                        CALCULOS PANTALLA
|| ========================================================================

|PROCESO pintames; |TIPO 0;

   x = Campo + 2;                || Coge el campo del nombre del mes
   |SI #0Campo ! 0;|Y #0Campo ! 13;
       NumMes = #canempre#11 + (#0Campo - 1);   || Operacion para seber que mes es
       |SI NumMes > 12; NumMes = NumMes - 12; |FINSI;
   |SINO;
       NumMes = #0Campo;
   |FINSI;
   |HAZ NombreMes;                          || Recoge el mes en letra
   #0x = NombreMes;
   |PINTA #0x;                       || Pinta el nombre del mes

   |SI Campo = 1; |Y #0#0 > #0#1;
       |MENAV "    Error de Entrada ...";
       |ERROR;
   |FINSI;
|FPRC;

|PROCESO pintames2; |TIPO 0;
    |SI #0Campo ! 0;|Y #0Campo ! 13;
       NumMes = #canempre#11 + (#0Campo - 1);   || Operacion para saber que mes es
       |SI NumMes > 12; NumMes = NumMes - 12; |FINSI;
   |SINO;
        NumMes = #0Campo;
   |FINSI;
   |HAZ NombreMes;
   |ATRI I;
   |SI Campo = 10;  |PINTA 1727, NombreMes;  |FINSI;
   |SI Campo = 11;  |PINTA 1827, NombreMes;  |FINSI;
   |ATRI 0;
|FPRC;

|PROCESO nivel; |TIPO 7;
   |SI #0#4 = "N";
       |C_M #0#5,1,"N";
   |SINO;
       |C_M #0#5,1,"S";
   |FINSI;
|FPRC;


|PROCESO miraniv; |TIPO 0;
   nivel = #0#5;
   |SI nivel = 1;|Y dig4 = 0;  |AVISO;  |ERROR;  |FINSI;
   |SI nivel = 2;|Y dig5 = 0;  |AVISO;  |ERROR;  |FINSI;
   |SI nivel = 3;|Y dig6 = 0;  |AVISO;  |ERROR;  |FINSI;
   |SI nivel = 4;|Y dig7 = 0;  |AVISO;  |ERROR;  |FINSI;
   |SI nivel = 5;|Y dig8 = 0;  |AVISO;  |ERROR;  |FINSI;
   |SI nivel = 6;|Y dig9 = 0;  |AVISO;  |ERROR;  |FINSI;
|FPRC;

|PROCESO estruct; |TIPO 0;

   |SI #0#6 = 0;  |ACABA;  |FINSI;
   |ABRE #6;
   #caexistc#0 = #0#6;
   |LEE 040#6=;
   |SI FSalida ! 0;
      |MENAV "    No existe la estructura de descarga.";
      |CIERRA #6;
      |ERROR;
      |ACABA;
   |FINSI;
   |HAZ cue_per;
   |CIERRA #6;
|FPRC;

|PROCESO cue_per; || calcula la cuenta de perdidas y ganancias
   cue_perdi = #caexistc#2;
   dig_perdi = #caexistc#3;
   |SI #0#4 = "N";  nivel = #caexistc#3;  |FINSI;
   |SI nivel < #caexistc#3;
      |SI nivel = 1;  traraf = 100 + dig4;  |FINSI;
      |SI nivel = 2;  traraf = 100 + dig5;  |FINSI;
      |SI nivel = 3;  traraf = 100 + dig6;  |FINSI;
      |SI nivel = 4;  traraf = 100 + dig7;  |FINSI;
      |SI nivel = 5;  traraf = 100 + dig8;  |FINSI;
      cue_perdi = #caexistc#2 % traraf;
      dig_perdi = nivel;
   |FINSI;
   cue_perdi = cue_perdi + "            ";
   cue_perdi = cue_perdi % 112;
|FPRC;

|PROCESO conexi;
   d_mes = #0#10;
   h_mes = #0#11;
   estru = #0#6;
   r_emp = empre;
   r_per = ejerc;
   r_eje = #canempre#26;
   r_nom = #canempre#1;
   |SUB_EJECUTA "caexicon.run";
|FPRC;

|PROCESO LeeAgrupa;
    |SI t_agrupa ! #0#12; |O nSw = 0;
        #0#15 = "M";
        |ABRE #11;
        #11#19 = #0#12;
        #11#0  = doce;
        #11#1  = 0;
        |LEE 041#11];
        |SI FSalida = 0; |Y #11#19 = #0#12;
            #0#15 = #11#3;
        |FINSI;
        |CIERRA #11;
        |PINTA #0#15;
        nSw = 1;
        t_agrupa = #0#12;
    |FINSI;
|FPRC;
|| ========================================================================
||                     CALCULOS de FICHEROS AUXILIARES
|| ========================================================================
|PROCESO grabatra;  || ... Graba temporal de Agrupaciones Exp0;

   |SI saldo = 0; |Y saldo_p = 0; |ACABA; |FINSI;
   #cabaexp0#0 = #cacuentm#5;
   #cabaexp0#1 = #cacuentm#4;
   #cabaexp0#2 = #cacuentm#6;
   #cabaexp0#3 = #cacuentm#7;
   #cabaexp0#4 = #cacuentm#8;
   |LEE 101#3=;
   |SI FSalida ! 0;
      |DDEFECTO #3;
      #cabaexp0#0 = #cacuentm#5;
      #cabaexp0#1 = #cacuentm#4;
      #cabaexp0#2 = #cacuentm#6;
      #cabaexp0#3 = #cacuentm#7;
      #cabaexp0#4 = #cacuentm#8;
      |HAZ leexplot;
      #cabaexp0#5 = t_agru;
      #cabaexp0#6 = t_epig;
      #cabaexp0#7 = t_part;
      |GRABA 020#3c;
   |FINSI;
   #cabaexp0#8  = #cabaexp0#8  + saldo;
   #cabaexp0#10 = #cabaexp0#10 + saldo_p;
   |GRABA 020#3a;
   |LIBERA #3;

   || ... Graba Auxiliar de Cuentas.

   |SI #0#4 = "N"; |ACABA; |FINSI;

   #cabaexp2#0 = #cacuentm#5;
   #cabaexp2#1 = #cacuentm#4;
   #cabaexp2#2 = #cacuentm#6;
   #cabaexp2#3 = #cacuentm#7;
   #cabaexp2#4 = #cacuentm#8;
   #cabaexp2#5 = #cacuentm#0;
   #cabaexp2#6 = #cacuentm#1;
   #cabaexp2#7 = #cacuentm#2;
   |LEE 000#5=;
   |SI FSalida ! 0;
      |DDEFECTO #5;
      #cabaexp2#0 = #cacuentm#5;
      #cabaexp2#1 = #cacuentm#4;
      #cabaexp2#2 = #cacuentm#6;
      #cabaexp2#3 = #cacuentm#7;
      #cabaexp2#4 = #cacuentm#8;
      #cabaexp2#5 = #cacuentm#0;
      #cabaexp2#6 = #cacuentm#1;
      #cabaexp2#7 = #cacuentm#2;
      |HAZ leexplot;
      #cabaexp2#8 = t_agru;
      #cabaexp2#9 = t_epig;
      #cabaexp2#10 = t_part;
      #cabaexp2#11 = 0;
      #cabaexp2#12 = 0;
      |GRABA 020#5c;
   |FINSI;

   #cabaexp2#11 = #cabaexp2#11 + saldo;
   #cabaexp2#12 = #cabaexp2#12 + saldo_p;

   |GRABA 020#5a;
   |LIBERA #5;
|FPRC;

|PROCESO exis_expl;
   descar = #caexistl#des_exi# - #caexistl#has_exi#; || iniciales - finales
   |SI descar = 0; |ACABA; |FINSI;
   |SI descar ] 0;
      act_cuent = #caexistl#5;     || cuenta descarga D
      act_nivel = #caexistl#6;     || digito descarga D
   |SINO;
      act_cuent = #caexistl#8;     || cuenta descarga H
      act_nivel = #caexistl#9;     || digito descarga H
   |FINSI;

   acor_c = act_cuent; acor_d = act_nivel;
   |HAZ acortacuen;
   act_cuent = acor_c; act_nivel = acor_d;

   |SI act_cuent = #cacuentm#0; |Y act_nivel = #cacuentm#1;
      sw_exp = 1;
      |SI descar ] 0;
         debe = debe + descar;
      |SINO;
         haber = haber - descar;
      |FINSI;
      saldo = debe - haber;
   |FINSI;
|FPRC;

|DEFBUCLE exis2;
   #7#1;
   ;
   #0#6, 01;
   #0#6, 99;
   be;
   NULL, exis_expl;
|FIN;

|PROCESO exis_3000;
   sw_exp  = 1;
   saldo   = saldo + #caexistl#has_exi#;
|FPRC;

|DEFBUCLE exis1;
   #7#2;
   ;
   #0#6, #1#0;
   #0#6, alar_c;
   e;
   NULL,exis_3000;
|FIN;

|PROCESO calcular;
   saldo   = 0;  || Saldo Real
   saldo_p = 0;  || Saldo Presupuestado
   debe    = 0;
   haber   = 0;
   sw_exp  = 0;

   |PARA x = eldes_p; |SI x [ elhas_p; |HACIENDO x = x + 1;
      saldo_p = saldo_p + #cacuentm#x#;
   |SIGUE;

   alfa1   = #cacuentm#0 % 101;

   |SI alfa1 = "3";
      |BUCLE exis1;
      |SI sw_exp = 1; saldo_p = saldo; |FINSI;
   |FINSI;
   |SI alfa1 = "6"; |O alfa1 = "7";
      |BUCLE exis2;
      |SI sw_exp = 1; saldo_p = saldo; |FINSI;
   |FINSI;

   |SI sw_exp = 1; |ACABA; |FINSI;

   |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
      y = x + 1;
      debe  = debe  + #cacuentm#x#;
      haber = haber + #cacuentm#y#;
      saldo = debe - haber;
   |SIGUE;
|FPRC;

|PROCESO grabar;
   |HAZ digitos2;
   |SI d_error ! 0;  |ACABA;  |FINSI;
   |SI #0#4 = "N";  |Y #cacuentm#3 = "N";                     |ACABA;  |FINSI;
   |SI #0#4 = "S";  |Y #cacuentm#55 > nivel;                  |ACABA;  |FINSI;
   |SI #0#4 = "S";  |Y #cacuentm#3 = "N";  |Y #cacuentm#55 ! nivel;  |ACABA;  |FINSI;

   alar_c = #cacuentm#0;
   |HAZ alargacuen;

   |SI #cacuentm#0 ! cue_perdi; || no es perdidas y ganancias
      |HAZ calcular;
      |SI saldo ! 0;
         |SI #cacuentm#5  = "H"; |O #cacuentm#5 = "P"; saldo = - saldo; |FINSI;
         |SI #cacuentm#5 = "A"; |O #cacuentm#5 = "P"; |HAZ grabatra; |FINSI;
         |HAZ sumasaldo;
      |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE cabasipr0;
   #1#1;
   ;
   d_cuen, d_digi;
   h_cuen, h_digi;
   be;
   NULL, grabar;
|FIN;
|| ************************************************************************

|PROCESO sumasaldo;    || Calculamos cada cuenta
   |SI #cacuentm#5 = "A"; activo = activo + saldo; activo_p = activo_p + saldo_p; |FINSI;
   |SI #cacuentm#5 = "P"; pasivo = pasivo + saldo; pasivo_p = pasivo_p + saldo_p; |FINSI;

   |SI #cacuentm#5 = "D";
      |SI #cacuentm#4 = 1;
         debe1 = debe1 + saldo;
      |FINSI;
      |SI #cacuentm#4 = 2;
         debe2 = debe2 + saldo;
      |FINSI;
      |SI #cacuentm#4 = 3;
         debe3 = debe3 + saldo;
      |FINSI;
      |SI #cacuentm#4 = 4;
         debe4 = debe4 + saldo;
      |FINSI;
      |SI #cacuentm#71 = "D";
         debe1_p = debe1_p + saldo_p;
      |SINO;
         debe1_p = debe1_p - saldo_p;
      |FINSI;
   |FINSI;
   |SI #cacuentm#5 = "H";
      |SI #cacuentm#4 = 1;
         haber1 = haber1 + saldo;
      |FINSI;
      |SI #cacuentm#4 = 2;
         haber2 = haber2 + saldo;
      |FINSI;
      |SI #cacuentm#4 = 3;
         haber3 = haber3 + saldo;
      |FINSI;
      |SI #cacuentm#4 = 4;
         haber4 = haber4 + saldo;
      |FINSI;
      |SI #cacuentm#71 = "H";
         haber1_p = haber1_p + saldo_p;
      |SINO;
         haber1_p = haber1_p - saldo_p;
      |FINSI;
   |FINSI;
|FPRC;

|| ***********************************************************************
|PROCESO gr_final; || Grabar totales
   saldo_p = 0;
   debe    = 0;
   haber   = 0;
   #cacuentm#0 = cue_perdi;
   #cacuentm#1 = dig_perdi;
   |LEE 001#1=;
   |SI FSalida ! 0;
      sw_error = 1;
      |MENAV "    ATENCION !!!. NO existe la cuenta de Perdidas y Ganancias ";
      |ACABA;
   |FINSI;
   |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
      y = x + 1;
      debe  = debe  + #cacuentm#x#;
      haber = haber + #cacuentm#y#;
      saldo = debe - haber;
   |SIGUE;
   |PARA x = eldes_p; |SI x [ elhas_p; |HACIENDO x = x + 1;
      saldo_p = saldo_p + #cacuentm#x#;
   |SIGUE;

   debe  = debe1  + debe2  + debe3  + debe4;
   haber = haber1 + haber2 + haber3 + haber4;
   saldo = saldo + ( debe - haber);

   debe_p  = debe1_p;
   haber_p = haber1_p;
   |SI #cacuentm#5 = "P"; saldo = - saldo; |FINSI;

   |SI #cacuentm#71 = "D"; saldo_p = saldo_p + ( debe_p - haber_p); |FINSI;
   |SI #cacuentm#71 = "H"; saldo_p = saldo_p + ( haber_p - debe_p); |FINSI;

   |HAZ grabatra; || ....................... Graba la cuenta 129. como otra mas.
   |SI #cacuentm#5 = "A"; activo = activo + saldo; activo_p = activo_p + saldo_p; |FINSI;
   |SI #cacuentm#5 = "P"; pasivo = pasivo + saldo; pasivo_p = pasivo_p + saldo_p; |FINSI;
|FPRC;

|| ***************************************************************************
|| *********************  CALCULO DE IMPRESION *******************************
|| ***************************************************************************

|PROCESO impresio;

   |SI sw = 0;
      tipo = #cabaexp0#0;
      agrupaci = #cabaexp0#2;
      epigrafe = #cabaexp0#3;
      |SI #cabaexp0#0 = "A"; doh = "ACTIVO"; |FINSI;
      |SI #cabaexp0#0 = "P"; doh = "PASIVO"; |FINSI;
      |HAZ agrupacion;
      sw = 1;
   |FINSI;

   |SI tipo ! #cabaexp0#0;
      sw_inf = 5;  |PRINT;  |PRINT;
      sw_inf = 1;
      |SI tipo = "A";
         variable = "  TOTAL ACTIVO ....................";
         canti = activo;
         canti_p = activo_p;
         |PRINT;
         doh = "PASIVO";
      |SINO;
         variable = "  TOTAL PASIVO ....................";
         canti = pasivo;
         canti_p = pasivo_p;
         |PRINT;
      |FINSI;
      |PIEINF;
      tipo = #cabaexp0#0;
      agrupaci = #cabaexp0#2;
      epigrafe = #cabaexp0#3;
      |HAZ agrupacion;
   |FINSI;
   |HAZ epigraf;
|FPRC;

|PROCESO epigraf;
   |SI agrupaci ! #cabaexp0#2;
      sw_inf = 5;  |PRINT;       || linea en blanco
      |HAZ agrupacion;
      agrupaci = #cabaexp0#2;
   |FINSI;
   |HAZ rotura;
   sw_epi = 0;
|FPRC;

|PROCESO rotura;
   |SI epigrafe ! #cabaexp0#3; |O sw_epi = 1;
      |SI #cabaexp0#3 ! 0;
         sw_inf = 2;  |PRINT;        || epigrafe
      |FINSI;
   |FINSI;
   |HAZ partida;                                       || partida
   |HAZ detalle;                                       || cuentas
   epigrafe = #cabaexp0#3;
|FPRC;

|PROCESO agrupacion;
   sw_epi = 1;                || fuerza la impresion del epigrafe
   |SI #cabaexp0#3 = 0;                  || epigrafe=0
      sw_inf = 7;  |PRINT;   || agrupacion (con importe)
   |SINO;
      sw_inf = 0;  |PRINT;   || agrupacion (sin importe)
   |FINSI;
|FPRC;

|PROCESO partida;        || imprime titulo de partidas
   |SI #cabaexp0#4 ! 0;
      sw_inf = 3;
      |PRINT;
   |FINSI;
|FPRC;

|PROCESO cuentas;
   cantidad   = #cabaexp2#11;
   cantidad_p = #cabaexp2#12;
   sw_inf = 6;
   |SI cantidad ! 0; |O cantidad_p ! 0;
      |PRINT;
   |FINSI;
|FPRC;

|DEFBUCLE cabasipr4;
   #5#1;
   ;
   #3#0, #3#1, #3#2, #3#3, #3#4, "            ", 0;
   #3#0, #3#1, #3#2, #3#3, #3#4, "zzzzzzzzzzzz", 9;
   e;
   NULL, NULL, NULL, NULL, NULL, cuentas;
   #5#5, #5#6;
|FIN;


|PROCESO detalle;        || imprime detalle de cuentas
   |SI #0#4 = "S";
      |BUCLE cabasipr4;
      sw_inf=5;
      |PRINT;
   |FINSI;
|FPRC;

|| ------------------
|DEFBUCLE cabasipr1;
   #3#1;
   ;
   d, 0, 01, 00, 00;
   h, 0, 99, 99, 99;
   e;
   NULL, impresio;
|FIN;

|| **********************************************************************

|PROCESO impre; |TIPO 3;
   |HAZ antesque;

   |SI #0#14 = "S"; |HAZ conexi; |FINSI;
   |DELINDEX #3;
   |DELINDEX #5;

   |IMPRE 0;
   |SI FSalida ! 0;
       |ACABA;
   |FINSI;

   |INFOR informe;
   |SI FSalida ! 0;
       |MENAV "0000No existe Informe";
       |FINIMP;
       |ACABA;
   |FINSI;

   eaTexto = "Presupuesto Mensual";
   |SI #0#15 = "A";
       eaTexto = "Presupuesto Anual";
   |FINSI;
   t_agrupa = #0#12;
   |HAZ presup0;

   sw = 0;
   |HAZ sabercam; || Calculo para saber los limites de saldos

   |ABRET #1;
   |ABRE #2;
   |ABRE #3;
   |ABRE #5;

   |BUCLE cabasipr0;      || Bucle de Calculos de cuentas
   |HAZ gr_final;

   |CIERRA #1;
   |CIERRA #2;
   |CIERRA #3;
   |CIERRA #5;

   |SI sw_error = 1; |ACABA; |FINSI;

   || ========================================================================

   |BUCLE cabasipr1;
   |HAZ final;

   |PIEINF;
   |FININF;

   |DELINDEX #1;
   |DELINDEX #3;
   |DELINDEX #5;
|FPRC;

|PROCESO final;
   sw_inf = 5; |PRINT; |PRINT;
   sw_inf = 1;
   |SI tipo = "A";
      variable = "  TOTAL ACTIVO ....................";
      canti = activo;
      canti_p = activo_p;
      doh = "PASIVO";
      |PRINT;
   |SINO;
      variable = "  TOTAL PASIVO ....................";
      canti = pasivo;
      canti_p = pasivo_p;
      |PRINT;
   |FINSI;
|FPRC;

|| ***************** Rutinas ayuda

|PROCESO sabercam;
   eldesde = (#0#0 * 3) + 9;
   elhasta = (#0#1 * 3) + 9;
   des_exi =  #0#10 + 11;
   has_exi =  #0#11 + 11;
   d_cuen = "            "; d_digi = 0;
   h_cuen = "999999999999"; h_digi = 9;
   nivel = #0#5;

   |SI #0#15 = "M";
       eldes_p = 58 + #0#0; |SI #0#0 = 0;  eldes_p = 59; |FINSI;
       |SI #0#0 = 13; eldes_p = 70; |FINSI;

       elhas_p = 58 + #0#1; |SI #0#1 = 0;  elhas_p = 59; |FINSI;
       |SI #0#1 = 13; elhas_p = 70; |FINSI;
   |SINO;
       eldes_p = 59;
       elhas_p = 70;
   |FINSI;
|FPRC;

|PROCESO leexplot;
   #caparbal#0 = #cabaexp0#0;
   #caparbal#1 = #cabaexp0#2;
   #caparbal#3 = #cabaexp0#3;
   #caparbal#5 = #cabaexp0#4;
   |LEE 040#2=;
   |SI FSalida ! 0; |DDEFECTO #2; |FINSI;
   t_agru = #caparbal#2;
   t_epig = #caparbal#4;
   t_part = #caparbal#6;
|FPRC;

|PROCESO acortacuen;
   |SI #0#4 = "N"; |ACABA; |FINSI;
   |SI nivel < acor_d;
      |SI nivel = 1;  traraf = 100 + dig4;  |FINSI;
      |SI nivel = 2;  traraf = 100 + dig5;  |FINSI;
      |SI nivel = 3;  traraf = 100 + dig6;  |FINSI;
      |SI nivel = 4;  traraf = 100 + dig7;  |FINSI;
      |SI nivel = 5;  traraf = 100 + dig8;  |FINSI;
      acor_c = acor_c % traraf;
      acor_d = nivel;
      acor_c = acor_c + "              ";
      acor_c = acor_c % 112;
   |FINSI;
|FPRC;

|PROCESO alargacuen;
   |QBF alar_c;
   alar_c = alar_c + "99999999999999";
   alar_c = alar_c % 112;
|FPRC;
