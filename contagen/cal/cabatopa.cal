|FICHEROS;
   cabatopa #0;
   cacuenta #1;
   cabatopc #2;
   caexistc #6;
   caexistl #7;
   caconimp #8;

   cacuedep #11;

   canempre #30;
   caselem1 #998;
|FIN;

|VARIABLES;
   &eSwSelec  = 1;
   &eaDDepar  = "";
   &eaHDepar  = "";
   &efDFecha  = @;
   &efHFecha  = @;
   &eaNomFDep = "";  ||Nombre del Fichero Creado

   aBlancs = "";
   &pathbal = "";
   &balance = 1;
   &entrada = 0;
   &aparam = "";
   informe = "cabatopc";
   alfa1 = "";
   nume1 = 0;
   x = 0;
   y = 0;
   &r_emp = 0;
   &r_per = 0;
   &d_mes = 0;
   &h_mes = 0;
   &estru = 0;
   &r_eje = 0;
   &r_nom = "";
   varcamp = 0;
   mes = 0;
   sw_error = 0;
   cue_perdi = "";
   dig_perdi = 0;

   &eldesde = 0;
   &elhasta = 0;
   des_exi = 0;
   has_exi = 0;
   d_cuen = "";
   h_cuen = "";
   d_digi = 0;
   h_digi = 9;

   impuesto  = 0;
   act_cuent = "";
   act_nivel = 0;
   saldo   = 0;
   debe    = 0;
   haber   = 0;
   sw_exp  = 0;
   descar  = 0;
   bene    = 0;
   activo = 0;
   pasivo = 0;
   explo = 0;
   debe1  = 0;
   debe2  = 0;
   debe3  = 0;
   debe4  = 0;
   haber1 = 0;
   haber2 = 0;
   haber3 = 0;
   haber4 = 0;
   importe = 0;
   tipo = "";
   bloque = 0;
   agrupaci = 0;
   epigrafe = 0;
   partida = 0;

   canti = 0;
   punto_h1 = 0;
   punto_h2 = 0;
   punto_h3 = 0;
   punto_h4 = 0;
   punto_h5 = 0;
   punto_h6 = 0;
   punto_d1 = 0;
   punto_d2 = 0;
   punto_d3 = 0;
   punto_d4 = 0;
   punto_d5 = 0;
   punto_d6 = 0;

   sw = 0;
   sw_agr = 0;
   sw_epi  = 0;
   sw_alguno = 0;

   &titulo = "";
   &sw_inf = 0;
   &cantidad = 0;
   &variable = "";
   &mesi = "";
   &mesf = "";
   &fec  = @;
   d_tipo = "";
   h_tipo = "";
   d_bloque = 0;
   h_bloque = 0;
   tecla = "";
   t1 = "i";
   t2 = "I";

   swAlgun  = 0;
   &swDepar = 0;
   nInkey   = 0;
   &enActivo   = 0;
   &enPasivo   = 0;
   &enGastos   = 0;
   &enIngresos = 0;
   &enCam0     = 0;
   &enCam1     = 0;
   &enCam6     = 0;
   &enCam7     = 0;
   &enCam8     = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE digito_i;

|| ========================================================================
||                        CALCULOS PANTALLA
|| ========================================================================
|CALCULO elTipo8; |TIPO 8;
 |HAZ inicio;
 |HAZ DirAplicSt;
|FCAL;

|CALCULO estruc7; |TIPO 7;
 #0#6 = 1;
 |SI Haybasica = 1; |Y eaDepar = "S";
     #0#6 = 0;
 |FINSI;
 |PINTA #0#6;
|FCAL

|PROCESO pintames; |TIPO 0;
   varcamp = Campo + 2;                || Coge el campo del nombre del mes
   |SI #0Campo ! 0;|Y #0Campo ! 13;
       NumMes = #canempre#11 + (#0Campo - 1);   || Operacion para saber que mes es
       |SI NumMes > 12; NumMes = NumMes - 12; |FINSI;
   |SINO;
       NumMes = #0Campo;
   |FINSI;
   |HAZ NombreMes;                      || Recoge el mes en letra
   #0varcamp = NombreMes;
   |PINTA #0varcamp;                   || Pinta el nombre del mes
   |SI Campo = 1;  |Y #0#0 > #0#1;
      |MENAV "    Error de Entrada ...";
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO pintames2; |TIPO 0;
   |SI #0Campo ! 0;|Y #0Campo ! 13;
       NumMes = #canempre#11 + (#0Campo - 1);   || Operacion para saber que mes es
       |SI NumMes > 12; NumMes = NumMes - 12; |FINSI;
   |SINO;
       NumMes = #0Campo;
   |FINSI;
   |HAZ NombreMes;                      || Recoge el mes en letra
   |ATRI I;
   |SI Campo = 7;  |PINTA 1424, NombreMes;  |FINSI;
   |SI Campo = 8;  |PINTA 1460, NombreMes;  |FINSI;
   |ATRI 0;
   |SI Campo = 8;  |Y #0#7 > #0#8;
      |MENAV "    Error de Entrada ...";
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO estruct; |TIPO 0;
   |SI #0#6 = 0;  |ACABA;  |FINSI;
   |HAZ cue_per;
   |SI sw_error ! 0;
      |MENAV "    No existe la estructura de descarga.";
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO pActividad; |TIPO 0;
   nInkey = FSalida;
   |SI eaDepar = "N"; |ACABA; |FINSI;
   aBlancs = " "* 78; |PINTA 2202, aBlancs;

   |SI #0#6 = "00"; |Y nInkey ! 10;
       |ATRI I; |PINTA 2202, " TODAS LAS ACTIVIDADES / DEPARTAMENTOS "; |ATRI 0;
       |ACABA;
   |FINSI;
   eOpDep = 0; || 1 = Consulta pantalla
   |SI nInkey = 10;  eOpDep = 1; |FINSI;
   enActividad = #0#6;
   eaCodDep    = #0#6;
   |HAZ Actividad;
   |SI eaCodDep = ""; eswLect = 0; |FINSI;
   |SI eswLect = 1;
       |MENAV "    Error.! No Existe Actividad / Departamento";
       |ERROR;
   |SINO;
       #0#6 = eaCodDep; |PINTA #0#6;
       |HAZ pDepartamento;
   |FINSI;
|FPRC;

|PROCESO cue_per; || calcula la cuenta de perdidas y ganancias
   sw_error = 0;
   |ABRE #6;
   |DDEFECTO #6;
   #caexistc#0 = #0#6;
   |LEE 040#6=;
   |SI FSalida ! 0;
      sw_error = 1;
      |CIERRA #6;
      |ACABA;
   |FINSI;
   cue_perdi = #6#2;
   dig_perdi = #6#3;
   |CIERRA #6;
|FPRC;

|| ... procesos iniciales
|PROCESO antesque;
   aparam = PARAMETRO;
   |QBF aparam;
   |SI aparam = "fech";
      |NOME_DAT #1 "cabalano";
   |FINSI;
|FPRC;

|PROCESO conexi; |TIPO 0;
   enTExi = 1;
   d_mes = #0#7;
   h_mes = #0#8;
   estru = #0#6;
   r_emp = empre;
   r_per = ejerc;
   r_eje = #canempre#26;
   r_nom = #canempre#1;
   |SUB_EJECUTA "caexicon.run";
|FPRC;

|| ========================================================================
||                     CALCULOS de FICHEROS AUXILIARES
|| ========================================================================

|PROCESO exis_expl;
   descar = #caexistl#des_exi# - #caexistl#has_exi#; || iniciales - finales
   |SI descar = 0; |ACABA; |FINSI;
   |SI descar ] 0;
      act_cuent = #caexistl#5;     || cuenta descarga D
      act_nivel = #caexistl#6;     || digito descarga D
   |SINO;
      act_cuent = #caexistl#8;     || cuenta descarga H
      act_nivel = #caexistl#9;     || digito descarga H
   |FINSI;

   |SI act_cuent = #cacuenta#0;
      sw_exp = 1;
      |SI descar ] 0;
         debe = debe + descar;
      |SINO;
         haber = haber - descar;
      |FINSI;
      saldo = debe - haber;
   |FINSI;
|FPRC;

|DEFBUCLE exis2;
   #7#1;
   ;
   #0#6, 01;
   #0#6, 99;
   e;
   NULL, exis_expl;
|FIN;

|PROCESO exis_3000;
   sw_exp  = 1;
   saldo   = saldo + #caexistl#has_exi#;
|FPRC;

|DEFBUCLE exis1;
   #7#2;
   ;
   #0#6, #1#0;
   #0#6, #1#0;
   e;
   NULL,exis_3000;
|FIN;

|PROCESO calcular;
   saldo   = 0;
   debe    = 0;
   haber   = 0;
   sw_exp  = 0;
   alfa1   = #cacuenta#0 % 101;
   |SI alfa1 = "3";                 |BUCLE exis1; |FINSI;
   |SI alfa1 = "6"; |O alfa1 = "7"; |BUCLE exis2; |FINSI;

   |SI sw_exp = 1; |ACABA; |FINSI;

   |SI #0#6 = 0; |O aparam ! ""
       |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
             y = x + 1;
             debe  = debe  + #cacuenta#x#;
             haber = haber + #cacuenta#y#;
             saldo = debe - haber;
       |SIGUE;
   |SINO;
       #11#0  = #1#0;
       #11#57 = eaCodDep;
       |LEE 001#11=;
       |SI FSalida ! 0;
           |DDEFECTO #11;
           #11#0 = #1#0;
           #11#57 = #0#6;
       |FINSI;
       |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
             y = x + 1;
             debe  = debe  + #cacuedep#x#;
             haber = haber + #cacuedep#y#;
             saldo = debe - haber;
       |SIGUE;
   |FINSI;
|FPRC;

|PROCESO grabar;
   |HAZ digitos2;
   |SI d_error ! 0;  |ACABA;  |FINSI;
   |SI #cacuenta#3 = "N";   |ACABA;  |FINSI;

   |SI #cacuenta#0 ! cue_perdi; || no es perdidas y ganancias
      |HAZ calcular;
      |SI saldo ! 0;
         |SI #cacuenta#5 = "H"; |O #cacuenta#5 = "P";
            saldo = - saldo;
         |FINSI;
         |HAZ sumasaldo;
         |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE cabatopa0;
   #1#5;
   ;
   d_cuen, d_digi;
   h_cuen, h_digi;
   e;
   NULL, grabar;
|FIN;
|| ************************************************************************

|PROCESO sumasaldo;    || Calculamos cada cuenta
   swAlgun = 1;
   |SI #cacuenta#5 = "A"; activo = activo + saldo; |FINSI;
   |SI #cacuenta#5 = "P"; pasivo = pasivo + saldo; |FINSI;
   |SI #cacuenta#5 = "D";
      |SI #cacuenta#4 = 1;
         debe1 = debe1 + saldo;
      |FINSI;
      |SI #cacuenta#4 = 2;
         debe2 = debe2 + saldo;
      |FINSI;
      |SI #cacuenta#4 = 3;
         debe3 = debe3 + saldo;
      |FINSI;
      |SI #cacuenta#4 = 4;
         debe4 = debe4 + saldo;
         |SI #cacuenta#6 = 1; |Y #cacuenta#7 = 14; impuesto = impuesto + saldo; |FINSI;
      |FINSI;
   |FINSI;
   |SI #cacuenta#5 = "H";
      |SI #cacuenta#4 = 1;
         haber1 = haber1 + saldo;
      |FINSI;
      |SI #cacuenta#4 = 2;
         haber2 = haber2 + saldo;
      |FINSI;
      |SI #cacuenta#4 = 3;
         haber3 = haber3 + saldo;
      |FINSI;
      |SI #cacuenta#4 = 4;
         haber4 = haber4 + saldo;
         |SI #cacuenta#6 = 1; |Y #cacuenta#7 = 14; impuesto = impuesto - saldo; |FINSI;
      |FINSI;
   |FINSI;
|FPRC;

|| ***********************************************************************

|PROCESO gr_final; || Grabar totales
   |SI swAlgun = 0; |ACABA; |FINSI;
   debe = 0;
   haber = 0;
   #cacuenta#0 = cue_perdi;
   #cacuenta#1 = dig_perdi;
   |LEE 001#1=;
   |SI FSalida ! 0; |Y aparam ! "Chequeo";
      sw_error = 1;
      |MENAV "    ATENCION !!!. NO existe la cuenta de Perdidas y Ganancias ";
      |ACABA;
   |FINSI;

   |SI #0#6 = 0; |O aparam ! ""
       |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
             y = x + 1;
             debe  = debe  + #cacuenta#x#;
             haber = haber + #cacuenta#y#;
             saldo = debe - haber;
        |SIGUE;
   |SINO;
       #11#0  = #1#0;
       #11#57 = #0#6;
       |LEE 001#11=;
       |SI FSalida ! 0;
           |DDEFECTO #11;
           #11#0 = #1#0;
           #11#57 = #0#6;
       |FINSI;
       |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
             y = x + 1;
             debe  = debe  + #cacuedep#x#;
             haber = haber + #cacuedep#y#;
             saldo = debe - haber;
       |SIGUE;
   |FINSI;
   debe  = debe1  + debe2  + debe3  + debe4;
   haber = haber1 + haber2 + haber3 + haber4;
   saldo = saldo + ( debe - haber);
   |SI #cacuenta#5 = "P";
      saldo = - saldo;
   |FINSI;
   |SI #cacuenta#5 = "A"; activo = activo + saldo; |FINSI;
   |SI #cacuenta#5 = "P"; pasivo = pasivo + saldo; |FINSI;

   || .....calcula_tot;
   punto_d1 = 0;   punto_h1 = 0;
   punto_d2 = 0;   punto_h2 = 0;
   punto_d3 = 0;   punto_h3 = 0;
   punto_d4 = 0;   punto_h4 = 0;
   punto_d5 = 0;   punto_h5 = 0;
   punto_d6 = 0;   punto_h6 = 0;

   canti = haber1 - debe1;
   |SI canti < 0;
      punto_h1 = - canti;
   |SINO;
      punto_d1 = canti;
   |FINSI;

   canti = haber2 - debe2;
   |SI canti < 0;
      punto_h2 = - canti;
   |SINO;
      punto_d2 = canti;
   |FINSI;

   canti = punto_d1 + punto_d2 - punto_h1 - punto_h2;
   |SI canti < 0;
      punto_h3 = - canti;
   |SINO;
      punto_d3 = canti;
   |FINSI;

   canti = haber3 - debe3;
   |SI canti < 0;
      punto_h4 = - canti;
   |SINO;
      punto_d4 = canti;
   |FINSI;

   canti = punto_d3 + punto_d4 - punto_h3 - punto_h4;
   |SI canti < 0;
      punto_h5 = - canti;
   |SINO;
      punto_d5 = canti;
   |FINSI;

   punto_h6 = punto_h5;
   |SI punto_h5 > 0;
      punto_h6 = punto_h6 + debe4;
   |FINSI;
   |SI punto_h6 < 0; punto_h6 = 0; |FINSI;

   punto_d6 = punto_d5;
   ||SI debe4 > 0;                       || R.PRIEGO (24.03.99) impuesto neg.
   punto_d6 = punto_d6 - debe4;
   ||FINSI;
   |SI punto_d6 < 0; punto_d6 = 0; |FINSI;
|FPRC;

|| ***********************************************************************

|PROCESO cabatopa1;
   |DDEFECTO #2;
   |PINPA #0#2;
   #cabatopc#0 = #0#12;
   #cabatopc#1 = #canempre#2;
   #cabatopc#2 = #canempre#26;
   #cabatopc#3 = #canempre#1;
   #cabatopc#4 = activo;
   #cabatopc#5 = pasivo;
   #cabatopc#6 = debe1 + debe2 + debe3 + debe4;
   #cabatopc#7 = haber1 + haber2 + haber3 + haber4;
   #cabatopc#8 = punto_d1;
   #cabatopc#9 = punto_d2;
   #cabatopc#10 = punto_d3;
   #cabatopc#11 = punto_d4;
   #cabatopc#12 = punto_d5;
   #cabatopc#13 = punto_d6;
   #cabatopc#14 = punto_h1;
   #cabatopc#15 = punto_h2;
   #cabatopc#16 = punto_h3;
   #cabatopc#17 = punto_h4;
   #cabatopc#18 = punto_h5;
   #cabatopc#19 = punto_h6;

   || ........... Calculo de Impuesto
   #cabatopc#27 = 0; #cabatopc#28 = 0;
   |C_V #cabatopc#27,1,"N"; |C_V #cabatopc#28,1,"N";
   |SI impuesto > 0; #cabatopc#27 = impuesto;   |C_V #cabatopc#27,1,"S"; |FINSI;
   |SI impuesto < 0; #cabatopc#28 = - impuesto; |C_V #cabatopc#28,1,"S"; |FINSI;
   |SI impuesto = 0; #cabatopc#26 = "";         |FINSI;
   || ...............................

   cantidad = punto_d1 - punto_h1;
   nume1 = 1; tipo = "H"; |SI cantidad ] 0; tipo = "D"; |FINSI;
   |HAZ titulos;
   #cabatopc#20 = variable;

   cantidad = punto_d2 - punto_h2;
   nume1 = 2;
   tipo = "H"; |SI cantidad ] 0; tipo = "D"; |FINSI;
   |HAZ titulos;
   #cabatopc#21 = variable;

   cantidad = punto_d3 - punto_h3;
   nume1 = 3;
   tipo = "H";|SI cantidad ] 0; tipo = "D"; |FINSI;
   |HAZ titulos;
   #cabatopc#22 = variable;

   cantidad = punto_d4 - punto_h4;
   nume1 = 4;
   tipo = "H";|SI cantidad ] 0; tipo = "D"; |FINSI;
   |HAZ titulos;
   #cabatopc#23 = variable;

   cantidad = punto_d5 - punto_h5;
   nume1 = 5;
   tipo = "H";|SI cantidad ] 0; tipo = "D"; |FINSI;
   |HAZ titulos;
   #cabatopc#24 = variable;

   cantidad = punto_d6 - punto_h6;
   nume1 = 6;
   tipo = "H";|SI cantidad ] 0; tipo = "D"; |FINSI;
   |HAZ titulos;
   #cabatopc#25 = variable;

   |PARA nume1 = 8; |SI nume1 [ 19; |HACIENDO nume1 = nume1 + 1;
      |SI #cabatopc#nume1# ! 0;
         |C_V #cabatopc#nume1#,1,"S";
      |SINO;
         |C_V #cabatopc#nume1#,1,"N";
      |FINSI;
   |SIGUE;

   swDepar = 0;
   #2#29 = #0#6;
   #2#30 = "Todos los Departamentos / Actividades";
   #2#31 = "";
   #2#32 = "";
   |SI #0#6 ! 0; |Y aparam = "";
        enActividad = #0#6;
        eaCodDep    = #0#6;
        eOpDep = 0;
        |HAZ Actividad;
        |SI eaCodDep = ""; eswLect = 0; |FINSI;
        |SI eswLect = 0;
            swDepar = 1;
            #2#29 = eaCodDep;
            #2#30 = eaNombreAct;
            #2#31 = efActIni;
            |SI efActFin > "01.01.0000";
                #2#32 = efActFin;
                swDepar = 2;
            |FINSI;
        |FINSI;
   |FINSI;

   |PINDA #0#2;

   |ATRI R;
   |MENSA "2400Pulse <I> para imprimir el resultado en papel ";
   |ATRI 0;

   || t1 = &255 + "807";
   t1 = &105;
   t2 = &73;
   |LEETECLA tecla;
   |SI t1 = tecla; |O t2 = tecla;
      |BLIN 24;
      |HAZ imprimir;
   |FINSI;
|FPRC;

|PROCESO imprimir;
   |IMPRE 0;
   |SI FSalida ! 0;  |ACABA;  |FINSI;
   |INFOR informe;
   |SI FSalida ! 0;
      |MENAV "     Error en informe.";
      |FINIMP;
      |ACABA;
   |FINSI;
   |PRINT;
   |PIEINF;
   |FININF;
   |FINIMP;
|FPRC;

|| ******************** Programa por TIPO 3 **************************
|PROCESO impre; |TIPO 3;
    mesi = #0#2;   mesf = #0#3;
    fec  = #0#12;
    eldesde = (#0#0 * 3) + 9;
    elhasta = (#0#1 * 3) + 9;
    des_exi =  #0#7 + 11;
    has_exi =  #0#8 + 11;
    d_cuen = "            "; d_digi = 0;
    h_cuen = "999999999999"; h_digi = 9;

   |HAZ antesque;
   |SI #48#27 = "S"; |Y aparam ! "Chequeo";
       |HAZ MultiEmpresa;
   |SINO;
       |DFICE dirfich0;
       |HAZ HazTodo;
   |FINSI;
|FPRC;

|PROCESO HazTodo;

    |HAZ FecApeCie;
    |SI enMAper ! dig1;
        |SI #0#1 < enMAper; |Y #0#1 ! 0; |ACABA; |FINSI;
    |FINSI;

    |SI #0#13 = "S"; |HAZ conexi; |FINSI;

    |SI aparam = ""; |Y #0#6 ! 0;
        eSwSelec  = 2;
        eaDDepar  = eaCodDep; eaHDepar  = eaCodDep;
        efDFecha  = fec1;     efHFecha  = fec2;
        |ATRI I;
        |PINTA 2302, "Procesando Cuentas por Departamentos ... Espere ";
        |ATRI 0;
        |SUB_EJECUTA_NP "cacuedep";
        |PINTA 2302, "                                                ";
        |NOME_DAT #11 eaNomFDep;
        |PATH_DAT #11 dirfich0;
    |FINSI;

     activo = 0;
     pasivo = 0;
     explo = 0;
     debe1  = 0;
     debe2  = 0;
     debe3  = 0;
     debe4  = 0;
     haber1 = 0;
     haber2 = 0;
     haber3 = 0;
     haber4 = 0;

     ||... Modificar Cuentas a balance segun saldo
     r_emp = empre;
     r_per = ejerc;
     eldesde = (#0#0 * 3) + 11;
     elhasta = (#0#1 * 3) + 11;
     enSwOper = 0;
     |SUB_EJECUTA "carecsig";

     |ABRE #8;
     |DDEFECTO #8;
     #8#0 = 1;
     |LEE 000#8=;
     |CIERRA #8;
     |SI #8#2 = "S";  |Y #8#3 = "N";  |Y aparam = ""; |Y #0#6 = 0;
         pathbal = dirfich0;
         balance = 1;
         |SI #caconimp#10 = "S";
             enSwOper = 2;
             |SUB_EJECUTA "carecsig";
             enSwOper = 0;
             |SI enECuadre = 1; |ACABA; |FINSI;
             |NOME_DAT #1 eaNomFCta;
         |SINO;
             |SUB_EJECUTA "careccue";
         |FINSI;
     |FINSI;

     eldesde = (#0#0 * 3) + 9;
     elhasta = (#0#1 * 3) + 9;
     || ---------------------------------------------------------------------

     |HAZ cue_per;  || calculamos la cuenta de perdidas y ganancias.

     |ABRET #1;
     |ABRE #11;
     swAlgun = 0;
     |BUCLE cabatopa0;      || Bucle de Calculos cuentas
     |HAZ gr_final;         || Graba Totales

     |CIERRAT #1;
     |CIERRA #11;
     |SI sw_error = 1; |ACABA; |FINSI;
     || ==================================================================
     |SI aparam ! "Chequeo";
         |HAZ cabatopa1;      || Bucle de impresion
     |SINO;
         enActivo   = activo;
         enPasivo   = pasivo;
         enGastos   = (debe1 + debe2 + debe3 + debe4) ! EURODB;
         enIngresos = (haber1 + haber2 + haber3 + haber4) ! EURODB;
     |FINSI;
|FPRC;

|PROCESO titulos;
   variable = "";
   |SI tipo = "D";
      |SI nume1 =  1; variable = "I.  BENEFICIOS DE EXPLOTACION ............................."; |FINSI;
      |SI nume1 =  2; variable = "II. RESULTADOS FINANCIEROS POSITIVOS ......................"; |FINSI;
      |SI nume1 =  3; variable = "III.BENEFICIOS DE LAS ACTIVIDADES ORDINARIAS .............."; |FINSI;
      |SI nume1 =  4; variable = "IV. RESULTADOS EXTRAORDINARIOS POSITIVOS .................."; |FINSI;
      |SI nume1 =  5; variable = "V.  BENEFICIO ANTES DE IMPUESTOS .........................."; |FINSI;
      |SI nume1 =  6; variable = "VI. RESULTADO DEL EJERCICIO (BENEFICIOS) .................."; |FINSI;
   |SINO;
      |SI nume1 =  1; variable = "I.  PERDIDAS DE EXPLOTACION ..............................."; |FINSI;
      |SI nume1 =  2; variable = "II. RESULTADOS FINANCIEROS NEGATIVOS ......................"; |FINSI;
      |SI nume1 =  3; variable = "III.PERDIDAS DE LAS ACTIVIDADES ORDINARIAS ................"; |FINSI;
      |SI nume1 =  4; variable = "IV. RESULTADOS EXTRAORDINARIOS NEGATIVOS .................."; |FINSI;
      |SI nume1 =  5; variable = "V.  PERDIDAS ANTES DE IMPUESTOS ..........................."; |FINSI;
      |SI nume1 =  6; variable = "VI. RESULTADO DEL EJERCICIO (PERDIDAS) ...................."; |FINSI;
   |FINSI;
|FPRC;

|| *************************************************************************
|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   nYaLoHeDicho = 0;
   |PATH_DAT #1 dirfich0;
   |PATH_DAT #6 dirfich0;
   |PATH_DAT #7 dirfich0;
   |PATH_DAT #8 dirfich0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 000001;
 999999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;



|PROGRAMA;
  aparam = PARAMETRO; |QBF aparam;

  |SI aparam ! "Chequeo";
      |MANTE #0;
  |SINO;
      |DDEFECTO #0;
      #0#0 = enCam0;
      #0#1 = enCam1;
      #0#6 = enCam6;
      #0#7 = enCam7;
      #0#8 = enCam8;
      nYaLoHeDicho = 1;
      |HAZ impre;
  |FINSI;
|FPRO;
