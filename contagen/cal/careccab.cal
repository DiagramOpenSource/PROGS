|FICHEROS;
   careccab #0;
   camaasic #1;
   camaasil #2;
   camandia #10;

   caw00004 #904;

   caselem1 #998;
   canempre #30;
|FIN;

|VARIABLES;
   aParametro = "";
   &sw_conase4 = 0;
   &entrada    = 1;
   feini = @;
   fefin = @;
   doini = 1;
   dofin = 999999;
   prim = 0;
   cuant = "";
   lon = 0;
   aa = 0;
   ini = 0;
   fecalf = "";
   mes = "";
   mesfec = 0;
   mesac = 0;
   a = 0;
   b = 0;
   c = 0;
   mensa = "";
   aar = "";
   traba = "";
   mensaa = "";
   errant = "aaaaaaaaaaaa";
   uno = "";
   dos = "";
   mascara = "";
   nivel = 0;
   aFichero = "";
   nSwOp    = 0;
   nDtipo   = 0;
   aParam = "";

     &enNoModif = 0;
|FIN;

|INCLUYE dscont_i;

|PROCESO conase4; |TIPO 7;
   |SI sw_conase4 = 1;
      #0#0 = "S";
      |PTEC 802;
      |PTEC 802;
   |FINSI;
|FPRC;

|PROCESO mayus;
   |SI #0Campo = "S";
       aParametro = PARAMETRO;
       |QBF aParametro;
       |SI aParametro ! "AUTO";
            |CONFI "    NEsta Seguro de Borrar las Cabeceras";
            |SI FSalida ! 0;
                #0Campo = "N"; |PINTA #0Campo;
                |ERROR;
            |FINSI;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO acerar;
   |SI nSwOp = 0;
       #camaasic#4 = 0;       || debe
       #camaasic#5 = 0;       || haber
       #camaasic#6 = 0;       || saldo
       #camaasic#8 = "";      || contrap. HABER
       #camaasic#9 = 0;       || dig.contrap.HABER
       #camaasic#10 = "";     || contrap. DEBE
       #camaasic#11 = 0;      || dig.contrap.DEBE
       |GRABA 020#1a;
   |SINO;
       #904#0 = #1#0;
       #904#1 = #1#1;
       #904#2 = #1#2;
       #904#6 = #1#13;
       |GRABA 020#904n;
   |FINSI;
   |LIBERA #1;
|FPRC;

|DEFBUCLE careccab0;
   #1#1;
   13;
    0, feini,      0, nDtipo;
   99, fefin, 999999, 9999;
   be;
   NULL, acerar;
|FIN;

|PROCESO diarios;
  #10#2 = 0;
  #10#3 = 0;
  |GRABA 020#10a;
  |LIBERA #10;
|FPRC;

|DEFBUCLE diarios;
   #10#1;
   ;
   00;
   99;
   be;
   NULL, diarios;
|FIN;


|PROCESO actuali;

   #10#0 = #camaasil#0;
   |LEE 101#10=;
   |SI FSalida ! 0;
       |DDEFECTO #10;
       #10#0 = #camaasil#0;
       #10#1 = "<DIARIO SIN DEFINIR>";
       |GRABA 020#10b;
   |FINSI;
   |SI #camaasil#8 = "D";
       #10#2 = #10#2 + #camaasil#9;        || debe
   |SINO;
       #10#3 = #10#3 + #camaasil#9;        || haber
   |FINSI;
   |GRABA 020#10a;
   |LIBERA #10;

   |DDEFECTO #1;
   #camaasic#0 = #camaasil#0;
   #camaasic#1 = #camaasil#1;
   #camaasic#2 = #camaasil#2;
   |LEE 101#1=;
   |SI FSalida ! 0;
      |DDEFECTO #1;
      #camaasic#0 = #camaasil#0;
      #camaasic#1 = #camaasil#1;
      #camaasic#2 = #camaasil#2;
      #camaasic#3 = #camaasil#2;
      |GRABA 020#1b;
   |FINSI;

   |SI #camaasil#8 = "D";
       #camaasic#4 = #camaasic#4 + #camaasil#9;
       |SI #camaasic#09 = 0; #camaasic#08 = #camaasil#4; #camaasic#09 = #camaasil#5; |FINSI;      || contrp.HABER
   |SINO;
      #camaasic#5 = #camaasic#5 + #camaasil#9;
      |SI #camaasic#11 = 0; #camaasic#10 = #camaasil#4; #camaasic#11 = #camaasil#5; |FINSI;      || contrp.DEBE
   |FINSI;

   #camaasic#6  = #camaasic#4 - #camaasic#5;
   #camaasic#12 = #camaasil#24;

   |SI #camaasil#12 = "R";  #camaasic#13 = 1 + enNoModif; |FINSI;
   |SI #camaasil#12 = "S";  #camaasic#13 = 2 + enNoModif; |FINSI;
   |SI #camaasil#12 = "N";  #camaasic#13 = 3; |FINSI;
   |SI #0#0 = "S";
       |ABRE #904;
       #904#0 = #camaasil#0;
       #904#1 = #camaasil#1;
       #904#2 = #camaasil#2;
       |LEE 001#904=;
       |SI FSalida = 0;
           #camaasic#13 = #904#6;
       |FINSI;
       |CIERRA #904;
   |FINSI;

   #camaasic#15 = #camaasil#25;
   #camaasic#16 = #camaasil#27;
   #camaasic#17 = #camaasil#26;

   |GRABA 020#1a;
   |LIBERA #1;
   |LIBERA #2;
|FPRC;

|DEFBUCLE careccab1;
   #2#1;
   ;
    0, feini, doini,    1;
   99, fefin, dofin, 9999;
   be;
   NULL, actuali;
|FIN;

|PROCESO HazTodo;

   feini = "00.00.0000";
   fefin = "31.12.2999";
   nDtipo = 0;
   nSwOp  = 0;

   |SI #0#0 = "N";
       |PINTA 2410,"Poniendo a cero Saldos de Cabeceras";
       |BUCLE careccab0;
   |SINO;
       |PINTA 2410,"Guardando datos de Cabeceras";
       nSwOp = 1;
       nDtipo = 100;
       |ABRE #904;
       |BUCLE careccab0;
       |CIERRA #904;
       |DELINDEX #1;
   |FINSI;
   |BUCLE diarios;

   |BLIN 24;
   |PINTA 2410,"Recuperando Cabeceras";
   |ABRE #1;
   |ABRE #10;
   feini = "00.00.0000";
   fefin = "31.12.2999";
   |BUCLE careccab1;
   |CIERRA #10;
   |CIERRA #1;

   |DELINDEX #904;
|FPRC;

|PROCESO procal; |TIPO 3;

   aFichero = ("4w" + Usuario) % 108;
   |NOME_DAT #904 aFichero;
   |DELINDEX #904;

   |SI #48#27 = "S"; |Y sw_conase4 = 0;
      |HAZ MultiEmpresa;
   |SINO;
      |DFICE dirfich0;
      |HAZ HazTodo;
   |FINSI;
|FPRC;

|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #2 dirfich0;
   |PATH_DAT #10 dirfich0;

   |ATRI N; |CUADRO 2008 2266;
   |ATRI R; |PINTA 2109, "Empresa: ";      |ATRI 0;
   |PINTA 2118, #30#2; |PINTA 2124, #30#1; |ATRI 0;

   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 001;
 999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;

|PROGRAMA;
     |SI PARAMETRO ! "";
         |SI PARAMETRO = "100";  |O PARAMETRO = "000";
             enNoModif   = PARAMETRO;
         |FINSI;
         #careccab#0 = "N";
         |HAZ procal;
     |SINO;
         |MANTE #careccab;
     |FINSI;
|FPRO;
