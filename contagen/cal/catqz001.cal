|FICHEROS;
   catqz001 #0;

   camaasic #1;
   camaasil #2;

   caivases #4;
   calibros #5;
   caivalin #11;
   camaniva #12;
   caivaasi #200;
   :/modelos/def/dsmom030 #130;

   catqm001 #10;
   catqw001 #100;

   caivatm4;
|FIN;

|VARIABLES;
   &entrada = 1;
   fFecha   = @;
   aAlfa    = "";
   aAlfa1   = "";
   aAlfa2   = "";
   nNume1   = 0;
   nIva     = 0;
   nDebe    = 0;
   nHaber   = 0;

   mensa1   = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
   mensa2   = "    ERROR FECHA INFERIOR O IGUAL AL BLOQUEO DE DATOS";
   aFichero = "";
   nConte   = 0;
   nError   = 0;

   nSwHay   = 0;

   fFecUlt   = @;
   aSigAn    = "";
   aDeMas    = "";

   Comodin = "";
   &nLeer   = 0;
   nSw      = 0;
   nSal     = 0;
   nHay     = 0;

   &enQSerie  = 0;
   nAsiento   = 0;
   &eaCampo12   = "";
   &enCampo13   = 0;
   &enCampo14   = 0;
   &eaCampo15   = "";
   &eaCampo19   = "";
   &enCampo20   = 0;
   &enTFich   = 0;
   &Local     = 0;
   &enLlegoa9 = 0;
   &enNum9    = 0;
   &eaPath    = "";
   &eaPathExc = "";
   &eaFichero = "";

   &enError    = 0;
   &enDiario   = 0;
   &efFecha    = @;
   &enDocume   = 0;
   &enAsto     = 0;
   &enImporte  = 0;
   &eaDescrip  = "";
   &eaSigno    = "";

   &eaUnidadBasico = "";
   &eaOrigen  = "";
   &eaDestino = "";

   nLinea   = 0;
   nSwBueno = 0;
   aCol     = "";
   aExcel   = "";
   nXls     = 0;
   aExt     = "";

   nLibro     = 0;
   nSumaDoc   = 0;
   aSerie     = "";
   nElDoc     = 0;
   nFactu     = 0;
   aContra    = "";
   nPeriodo   = 0;
   nIncre     = 0;
   nCuota     = 0;
   nBase      = 0;
   aCtaIva    = "";

   &eaNomFich = "";
   &enAlguno  = 0;
   &enSiiLib  = 0;
   &enSiiDoc  = 0;
|FIN;

|INCLUYE dscont_i;

|| ************************************************************************

|CALCULO fecha;
   |SI #0Campo < fec1; |O #0Campo > fec2;
      |MENAV mensa1;
      |ERROR;
      |ACABA;
   |SINO;
      |SI #0Campo [ fec3;
          |MENAV mensa2;
          |ERROR;
          |ACABA;
      |FINSI;
   |FINSI;

   |SI Campo = 4;  |Y #0#3 > #0#4;
      |MENAV "     Atencion  fecha Erronea";
      |ERROR;
   |FINSI;
|FCAL;

|CALCULO BuscaDoc; |TIPO 0;
   |HAZ contador;
   #0#7 = nDOCUMENTO;
   #0#8 = #0#7;

   |C_M #0#7, 1, "S";
   |SI eaPaDOM1 = "S";
       |C_M #0#7, 1, "N"; |PINTA #0#7;
   |FINSI;
|FCAL;

|PROCESO RepDoc; |TIPO 0;
   |SI FSalida = 2; |ACABA; |FINSI;

   nConte = Contenido;
   |SI #0#7 ! nConte;
       |SI eaPaDOM3 ! "S";
           nNume1 = nConte + 1;
           |SI nNume1 < #0#7;
               nNume1 = 0;
               |SI eaPaDOM3 = "A";
                   |CONFI "    NNumero Documento superior al Ultimo + 1, Continuar";
                   |SI FSalida = 0;
                       nNume1 = 1;
                   |FINSI;
               |SINO;
                   |MENAV "    Error. Numero Documento superior al Ultimo + 1";
               |FINSI;
               |SI nNume1 = 0;
                   #0#7 = nConte;
                   |ERROR;
                   |ACABA;
                |FINSI;
            |FINSI;
         |FINSI;
    |FINSI;

    nError = 0;
    |SI eaPaDOM2 = "S"; |ACABA; |FINSI;  || no mira

    |ABRE #1;
    |SELECT #3#1;
    #1#2 = #0#7;
    |LEE 000#1=;
    |SI FSalida = 0;
        nError = 1;
        |SI eaPaDOM2 = "A";
           |CONFI "    NAtencion, Ya existe numero de Documento. Continuar";
           |SI FSalida = 0;
               nError = 0;
               |ACABA;
           |FINSI;
        |FINSI;
    |FINSI;
    |SELECT #1#1;
    |CIERRA #1;
    |SI nError = 1;
        |ERROR;
        #0#7 = nConte;
    |FINSI;
|FPRC;

|CALCULO AntesUsu;
   aAlfa = "S";
   |SI #0#10 = "S";
       aAlfa = "N";
       #0#11 = "N"; |PINTA #0#11;
   |FINSI;
   |C_M #0#11, 1, aAlfa;
|FCAL;

|CALCULO LeeLibro; |TIPO 0;
   |C_M #0Campo,0,aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   |DDEFECTO #5;
   |ABRE #5;
   #5#0 = #0Campo;
   |LEE 000#5=;
   |CIERRA #5;

   |SI #5#2 ! "R";
       |MENAV "    ATENCION!!! El Libro de I.V.A. no es de Repercutido ...";
       |ERROR;
   |FINSI;
|FCAL;

|CALCULO BuscaNum; |TIPO 7;
   |C_M #0Campo,0,aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   |SI enQSerie = 0;
       |ABRE #5;
       #5#0 = #0#17;
       |LEE 001#5=;
       |SI FSalida = 0;
           #0#19 = #5#4; |PINTA #0#19;
           nIncre = #5#3;
       |FINSI;
       |CIERRA #5;
   |SINO;
       |ABRE #4;
       #4#0 = #0#18;
       #4#1 = "R";
       |LEE 001#4=;
       |SI FSalida = 0;
           #0#19 = #4#3; |PINTA #0#19;
           nIncre = #4#4;
       |FINSI;
       |CIERRA #4;
   |FINSI;
|FCAL;

|CALCULO GraboFras;
   aAlfa1 = #0Campo;
   |C_M #0#17, 1, aAlfa1;
   |C_M #0#18, 1, aAlfa1;
   |C_M #0#19, 1, aAlfa1;

   |SI aAlfa1  = "N";
       #0#17 = 01;     |PINTA #0#17;
       #0#18 = "";     |PINTA #0#18;
       #0#19 = 000001; |PINTA #0#19;
   |FINSI;
   |SI aAlfa1 = "S";
       |SI enQSerie = 0;
           |C_M #0#18, 1, "N";
       |FINSI;
   |FINSI;
|FCAL;

|| ********************************************************************
|PROCESO GrabaAuxiliar;
     nLinea = nLinea + 1;
     #100#0 = efFecha;
     #100#1 = nLinea;
     |GRABA 020#100n;
     |SI efFecha > fFecUlt; fFecUlt = efFecha; |FINSI;
     nSwHay = 1;
|FPRC;

|PROCESO Una;
     ||| aCol contiene la columna, "A", "B" y nXls la fila
     aAlfa = aCol + nXls;
     |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa;
     |SI FSalida ! 0;
         nSal = 1;
    |SINO;
         |QBF aAlfa;
         |SI aAlfa ! ""; nHay = 1; |FINSI;
    |FINSI;
|FPRC;

|PROCESO CargaHoja;
     nLinea = 0;

     |PARA nXls = 2; |SI nXls < 65536; |HACIENDO nXls = nXls + 1;
           nSal = 0;
           nHay = 0;
           aCol = "B"; |HAZ Una; #100#2 = aAlfa;
           aCol = "O"; |HAZ Una; #100#3 = aAlfa;
           aCol = "P"; |HAZ Una; #100#4 = aAlfa;
           aCol = "Q"; |HAZ Una; #100#5 = aAlfa;
           aCol = "R"; |HAZ Una; #100#6 = aAlfa;

           |SI nSal = 1; |O nHay = 0;
               |SAL;
          |SINO;
               |HAZ GrabaAuxiliar;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO excel;
     aExcel = eaPathExc + eaFichero;

     |XABRE "CE", aExcel, 0;
     |SI FSalida < 0;
          aAlfa = "0000No existe " + aExcel;
          |MENAV aAlfa;
          |ACABA;
     |FINSI;

     aAlfa = "Pasando datos de la Hoja Excel " + eaFichero;
     |PINTA 2107, aAlfa;

     aAlfa1 = efFecha;
     |ALFACALLDLL "agixl97.dll", "carga_book", aExcel;
     |ALFACALLDLL "agixl97.dll", "selecciona_hoja", aAlfa1;
     |SI FSalida = 0;
         |ABRE #100;
         |HAZ CargaHoja;
         |CIERRA #100;
     |FINSI;
     |ALFACALLDLL "agixl97.dll", "fin_book", "salva";
|FPRC;

|PROCESO PasoaIndenxado;

    |CARGADLL "agixl97.dll";
    |SI FSalida < 0;
        |MENAV "0000No se puede usar agixl97.dll";
    |SINO;
        |HAZ excel;
        |DESCARGADLL "agixl97.dll";
    |FINSI;
|FPRC;

|PROCESO seleccion;
    aExt  = ".xlsx";
    aAlfa = eaFichero - aExt;
    |SI FSalida = 0;
        aExt = ".xls";
        aAlfa = eaFichero - aExt;
    |FINSI;

    nSwBueno = 0;
    aAlfa = aAlfa - ".";
    |SI FSalida = 0; |ACABA; |FINSI; || no es una fecha
    aAlfa = aAlfa - ".";
    |SI FSalida = 0; |ACABA; |FINSI; || no es una fecha

    aAlfa  = eaFichero - aExt;
    fFecha = aAlfa;
    |SI fFecha ] #0#3; |Y fFecha [ #0#4;
        nSwBueno = 1;
        efFecha = fFecha;
    |FINSI;
|FPRC;

|PROCESO VerDirectorio;
      nSw      = 0;
      eaOrigen = eaPath + "*.xl*";
      |_PDIR eaOrigen;
      |PARA; |SI FSalida = 0; |HACIENDO;
             nLeer    = 1;
             nSw      = 1;
             eaFichero = eaOrigen - eaPath;
             |HAZ seleccion;
             |SI nSwBueno = 1;
                 eaPathExc = eaPath;
                 |HAZ CopioAntes;
                 |HAZ PasoaIndenxado;
             |FINSI;
             |_SDIR eaOrigen;
       |SIGUE;

       |SI nSw = 0;
           eaOrigen = eaPath + "*.xl*";
           |R_PDIR eaOrigen;
           |PARA; |SI FSalida = 0; |HACIENDO;
                  nLeer  = 2;
                  nSw    = 1;
                  eaFichero = eaOrigen - eaPath;
                  |HAZ seleccion;
                  |SI nSwBueno = 1;
                      eaPathExc = eaPath;
                      |HAZ CopioAntes;
                      |HAZ PasoaIndenxado;
                  |FINSI;

                  |R_SDIR eaOrigen;
            |SIGUE;
       |FINSI;
|FPRC;

|| ********************************************************************
|| ********************************************************************
|PROCESO Anyade;
   aAlfa = eaDescrip; |QBF aAlfa;
   |SI aAlfa ! "";
       aAlfa = aAlfa + " " + efFecha;
   |SINO;
       aAlfa = efFecha;
   |FINSI;
   eaDescrip = aAlfa;
|FPRC;

|PROCESO Anyade1;
   |SI #0#11 = "S";
       aAlfa = eaDescrip; |QBF aAlfa;
       |SI aAlfa ! "";
           aAlfa = aAlfa + " " + #100#2;
       |SINO;
           aAlfa = #100#2;
      |FINSI;
      eaDescrip = aAlfa;
 |FINSI;
|FPRC;

|PROCESO BuscaNumero;
   #12#0 = #0#17;
   #12#1 = 9999999999;
   |LEE 010#12];
   |SI FSalida = 0;
       |LEE 040#12a;
   |SINO;
       |LEE 040#12u;
   |FINSI;
   nElDoc = 1;
   |SI FSalida = 0; |Y #12#0 = nLibro;
       nElDoc = #12#1 + 1;
   |FINSI;
|FPRC;

|| **************************************
|PROCESO Periodo;
  aAlfa1 = #12#4;
  aAlfa1 = aAlfa1 % 402;
  nNume1 = aAlfa1;
  nPeriodo = nNume1;
  |SI #200#46 = "A"; nPeriodo = 0; |FINSI;
  |SI #200#46 = "T"; |Y enEjercicio [ 2009;
      nPeriodo = 1;
      |SI nNume1 ] 4;  nPeriodo = 2; |FINSI;
      |SI nNume1 ] 7;  nPeriodo = 3; |FINSI;
      |SI nNume1 ] 10; nPeriodo = 4; |FINSI;
  |FINSI;
|FPRC;

|PROCESO BuscaIVA;
  efFechaIVA = #12#4;  enLineaIVA = 0;
  enPorcIVA  = #11#7;  enPorcREC = #11#9;
  eaCtaIVA   = #11#13; eaCtaREC  = #11#14;
  |HAZ BuscaTipoIVA;
  #11#16 = enLineaIVA;
  #11#13 = eaCtaIVA;
  #11#14 = eaCtaREC;
|FPRC;

|| **************************************
|PROCESO GrabaFactura;
       #12#0 = nLibro;
       #12#1 = nElDoc;
       |LEE 141#12=;
       |SI FSalida ! 0;
           |DDEFECTO #12;
           #12#0 = nLibro;
           #12#1 = nElDoc;
           |GRABA 020#12b;
       |FINSI;

       #12#2   = aSerie;
       #12#3   = nFactu;
       #12#4   = efFecha;
       |HAZ Periodo;
       #12#5  = nPeriodo;
       #12#7  = enDiario;
       #12#8  = efFecha;
       #12#9  = enDocume;
       #12#10 = #10#25;
       #12#11 = #10#26;
       #12#12 = #10#2;
       |SI #0#10 = "S";
           #12#13 = #10#9;
       |SINO;
           #12#13 = #100#2;
       |FINSI;
       #12#24 = eaNombreAct;
       #12#25 = eaNomSubdep;

       #12#27 = enConcepto;
       #12#28 = eaDescrip;
       #12#29 = #12#27;
       #12#30 = #12#28;
       #12#36 = aTipoIVA;

       |DDEFECTO #11;
       #11#0 = #12#0;
       #11#1 = #12#1;
       #11#2 = 1;
       |LEE 141#11=;
       |SI FSalida ! 0;
           #11#0 = #12#0;
           #11#1 = #12#1;
           #11#2 = 1;
           |GRABA 020#11b;
       |FINSI;

       #11#3 = #12#27;
       #11#4 = #12#28;

       #11#6 = nBase;
       #11#7 = #0#9;
       #11#8 = nCuota;
       #11#12 = aContra;
       #11#13 = aCtaIva;
       #11#16 = 0;
       |HAZ  BuscaIVA;
       #11#30 = #12#10;
       #11#31 = #12#11;
       |GRABA 020#11a;
       |LIBERA #11;

       #12#19 = #12#19 + #11#6;
       #12#15 = #12#15 + #11#6;
       #12#17 = #12#15 %  #12#16;
       #12#20 = #12#20 + #11#22;
       #12#23 = #12#23 + (#11#8 + #11#10);
       #12#26 = #12#26 + #11#11;
       #12#21 = #12#19 + #12#23 - #12#26 - #12#20;

       |GRABA 020#12a;
       |LIBERA #12;

       enSiiLib = nLibro;
       enSiiDoc = nElDoc;
       |HAZ AuxIvatm4;

       #0#19 = #0#19 + 1;
|FPRC;

|PROCESO cabecera_z1;
    enTipoAsto = 0;
    |SI #0#16 = "S"; |Y nAsiento = 1;
        enTipoAsto = 1;
    |FINSI;

    |HAZ cabecera;
    |SI enError = 1; |ACABA; |FINSI;

    eaCampo12 = "";
    enCampo13 = 0;
    enCampo14 = 0;
    eaCampo15 = "";
    eaCampo19 = "";
    enCampo20 = 0;

    |SI #0#16 = "S"; |Y nAsiento = 1;
        nLibro = #0#17;
        aSerie = #0#18;
        nFactu = #0#19;

        |HAZ BuscaNumero;
        eaCampo12 = "R";
        enCampo13 = nLibro;
        enCampo14 = nFactu;
        eaCampo15 = aSerie;
    |FINSI;
|FPRC;

|PROCESO ElAsiento;

   |SI #0#12 = 0; |Y #0#13 = 0; |Y #0#14 = 0; |Y #0#15 = 0;
       |ACABA;
   |FINSI;

   |PINTA 2107, "                                                         ";
   aAlfa = "Creando Asientos Contables a Fecha " + efFecha;
   |PINTA 2107, aAlfa;

|| ... Primer Asiento
   nAsiento = 1;
   enDiario = #0#6;
   enDocume = #0#7;
   enAsto   = #0#8;
   |HAZ cabecera_z1;
   |SI enError = 1; |ERROR10; |ACABA; |FINSI;

|| ... Apunte al Debe
   eaCuenta    = #10#2;
   enConcepto  = #10#16;
   eaDescrip   = #10#17;
   |SI #10#18 = "S"; |HAZ Anyade; |FINSI;
   |HAZ Anyade1;

   eaSigno     = "D";
   enImporte   = (#0#12 + #0#13 + #0#14 + #0#15) ! 2;
   |SI #0#16 = "S";
       eaCampo19 = "F";
       enCampo20 = 0;
   |FINSI;
   |HAZ GrabaLinea;
   nDebe = enImporte;

|| ... Apuntes al Haber
   eaCuenta    = #10#3;
   eaSigno     = "H";
   enImporte   = (enImporte / nIva) ! 2;

   |SI #0#16 = "S";
       aContra = eaCuenta;
       nBase   = enImporte;
       eaCampo19 = "B";
       enCampo20 = 01;
   |FINSI;
   |HAZ GrabaLinea;
   nHaber = enImporte;

   eaCuenta    = #10#8;
   enImporte   = (nDebe - nHaber) ! 2;
   |SI #0#16 = "S";
       aCtaIva   = eaCuenta;
       nCuota    = enImporte;
       eaCampo19 = "I";
       enCampo20 = 01;
   |FINSI;
   |HAZ GrabaLinea;

   |SI #0#16 = "S";
       |HAZ GrabaFactura;
   |FINSI;
|| .............................................................

   #0#7   = enDocume + 1;
   #0#8   = enAsto + 1;
   nDebe  = 0;
   nHaber = 0;

|| ... Segundo Asiento
   nAsiento = 2;
   enDiario = #0#6;
   enDocume = #0#7;
   enAsto   = #0#8;
   |HAZ cabecera_z1;
   |SI enError = 1; |ERROR10; |ACABA; |FINSI;

|| ... Apuntes al Debe
   eaCuenta    = #10#4;
   enConcepto  = #10#19;
   eaDescrip   = #10#20;
   |SI #10#21 = "S"; |HAZ Anyade; |FINSI;
   |HAZ Anyade1;

   eaSigno     = "D";
   enImporte   = #0#13;
   |HAZ GrabaLinea;
   nDebe = enImporte;

   eaCuenta    = #10#5;
   enImporte   = #0#12;
   |HAZ GrabaLinea;
   nDebe = nDebe + enImporte;

   eaCuenta    = #10#6;
   enImporte   = #0#14;
   |HAZ GrabaLinea;
   nDebe = nDebe + enImporte;

   eaCuenta    = #10#7;
   enImporte   = #0#15;
   |HAZ GrabaLinea;
   nDebe = nDebe + enImporte;

|| ... Apuntes al Haber
   eaCuenta    = #10#2;
   eaSigno     = "H";
   enImporte   = nDebe;
   |HAZ GrabaLinea;

   #0#7   = enDocume + 1;
   #0#8   = enAsto + 1;
   nDebe  = 0;
   nHaber = 0;

   |PDEFECTO #0, 12, 16;
|FPRC;

|PROCESO LeeAuxiliar;
   |SI nSw = 0;
       nSw = 1;
       efFecha = #100#0;
   |FINSI;

   |SI efFecha ! #100#0;
       |SI #0#10 = "S";
           |HAZ ElAsiento;
       |FINSI;
       efFecha = #100#0;
   |FINSI;

   #0#12 = #0#12 + #100#3;
   #0#13 = #0#13 + #100#4;
   #0#14 = #0#14 + #100#5;
   #0#15 = #0#15 + #100#6;

   |SI #0#10 = "N";
       |HAZ ElAsiento;
   |FINSI;
|FPRC;

|DEFBUCLE LeeAuxiliar;
   #100#1;
   ;
   #0#3, 0001;
   #0#4, 9999;
   e;
   NULL, LeeAuxiliar;
|FIN;

|| ********************************************************************
|PROCESO Departamento;
   eOpDep = 0;
   enActividad = #10#25;
   eaCodDep    = #10#25;
   |HAZ Actividad;
   eOpSub = 0;
   enConcepto  = #10#27;
   eaCodSubdep = #10#26;
   |HAZ OtroSubActividad;
|FPRC;

|PROCESO ActIVA;
   |SI #200#44 = "N";
       |ABRE #5;
       #5#0 = nLibro;
       |LEE 001#5=;
       |SI FSalida = 0;
           |SI nFactu ] #5#4;
               #5#4 = nFactu + #5#3;
           |FINSI;
           |GRABA 020#5a;
           |LIBERA #5;
       |FINSI;
       |CIERRA #5;
   |SINO;
       |ABRE #4;
       #4#0 = aSerie;
       #4#1 = aTipoIVA;
       |LEE 001#4=;
       |SI FSalida = 0;
           |SI nFactu ] #4#3;
               #4#3 = nFactu + #4#4;
           |FINSI;
           |GRABA 020#4a;
           |LIBERA #4;
       |FINSI;
       |CIERRA #4;
   |FINSI;
|FPRC;
|| **********************************************************************

|PROCESO elpase;

   nIva = 1 + (#0#9 / 100);

   |ABRE #10;
   |DDEFECTO #10;
   #10#0 = #0#1;
   |LEE 041#10=;
   |CIERRA #10;

   |HAZ Departamento;

   enTFich = 1;  ||fichero de recaudacion

   eaPath = #10#22; |QBF eaPath;
   |SI eaPath = "";
       |MENAV "    Error. Path de ficheros erroneo";
       |ACABA;
   |FINSI;

   Comodin = "";
   |IP_REMOTO Comodin;
   |QBF Comodin;
   |SI Comodin = "";
       Local = 1;
   |SINO;
       Local = 0;
   |FINSI;

   aFichero = "1h" + Usuario;
   |NOME_DAT #100 aFichero;
   |DELINDEX #100;

   enAlguno = 0;
   eaNomFich  = ("0p" + Usuario) % 108;
   |NOME_DAT #caivatm4#eaNomFich#;
   |DELINDEX #caivatm4;

   nSwHay = 0;
   |HAZ VerDirectorio;

   |SI nSwHay = 1;
       nSw    = 0;
       |PDEFECTO #0,12,16;

       |SI #0#16 = "S";
           |ABRE #11;
           |ABRE #12;
       |FINSI;

       |ABRE #2;
       |ABRE #1;
       |BUCLE LeeAuxiliar;
       |SI nSw = 1;
           |SI #0#10 = "S"; |HAZ ElAsiento; |FINSI;
           |HAZ ActContador;
       |FINSI;
       |CIERRA #1;
       |CIERRA #2;

       |SI #0#16 = "S";
           |CIERRA #12;
           |CIERRA #11;
           |HAZ ActIVA;
       |FINSI;

       |SI enAlguno = 1;
           |SUB_EJECUTA_NP "caut0018;PASE";
       |FINSI;
   |FINSI;

   |DELINDEX #100;
|FPRC;

|PROGRAMA;

   |HAZ inicio;
   |HAZ DirAplicSt;

   aTipoIVA = "R";
   |HAZ eLeeParametro;
   |C_M #0#18,1,"N";
   enQSerie = 0;
   |SI #200#44 = "S";
       enQSerie = 1;
       |C_M #0#18,1,"S";
   |FINSI;
   |HAZ LeeUnidadBasico;

   |CLS;
   |PINPA #0#0;
   |CABEZA "Contabilizar Recaudacion";

   |ABRE #0;
   #0#0 = "A";
   |LEE 141#0=;
   |SI FSalida ! 0;
       |DDEFECTO #0;
       #0#3 = fec1;
       #0#4 = fec2;
       |GRABA 020#0b;
   |FINSI;
   |HAZ BuscaDoc;

   |PINDA #0#0;
   |ENDATOS #2#0;
   |SI FSalida = 0;
       nNume1  = #0#3; nNume1 = nNume1 - 1;
       fFecUlt = nNume1;
       |HAZ elpase;
       nNume1  = fFecUlt; nNume1 = nNume1 + 1;
       #0#3    = nNume1;
       #0#4 = fec2;
       |SI #0#3 > #0#4; #0#3 = #0#4; |FINSI;
       |PDEFECTO #0,12,16;

       |GRABA 021#0a;
   |FINSI;
   |LIBERA #0;
   |CIERRA #0;
|FPRO;
