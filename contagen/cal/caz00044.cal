|FICHEROS;
   caz00044 #0;
   camoneda;
   camw0006 #6;
   canempre #30;
   caw00029 #999,MANTE;

   ca8ctrle #400;
|FIN;

|VARIABLES;
   nPara1   = 0;
   nNume1   = 0;
   nNume2   = 0;
   nNume3   = 0;
   nNume4   = 0;
   nPer     = 0;
   nEmp     = 0;
   nCampo   = 0;
   nInkey   = 0;
   aAlfa    = "";
   aAlfa1   = "";
   aAlfa2   = "";
   aMensa   = "";

   nSelError  = 0;
   nLaEmpresa = 0;

   aPath    = "";
   aPathE   = "";
   aDire    = "";

   Existe   = 0;
   nCanal   = 0;
   aPassword = "";

   nYaExiste = 0;
   aFichero  = "";
   aEmpresa  = "";
   nSwAlguna = 0;
   nImprime  = 0;
   informe   = "caz00044";
   &enEmpArch = 0;
   &enPerArch = 0;
   &eaPathArc = "";
   &eaUsuaArc = "";
   &eaNomArch = "";
   &enAnyArch = 0;
   &eaNifArch = "";
   &eaNomArchivo = "";
   &enOperar = 0;

  &enEmpresaPINET = 0;
  &enErrorPINET   = 0;
|FIN;

|INCLUYE dscont_i;

|| ********************************************************************
|| **** DATOS PANTALLA
|| ********************************************************************
|CALCULO PonBarra; |TIPO 0;

    nInkey = FSalida;
    |SI nInkey = 10;
        aAlfa1 = "D c:\";
        |BROWSE aAlfa1;
        aAlfa1 = aAlfa1 % 280;
        #0Campo = aAlfa1;
        |PINTA #0Campo
    |FINSI;

    aAlfa1 = #0Campo;
    |QBF aAlfa1;
    aAlfa2 = aAlfa1 % -101;
    |SI aAlfa2 ! "/";|Y aAlfa2 ! "\";
        #0Campo = aAlfa1 + "/";
        |PINTA #0Campo;
    |FINSI;
|FCAL;

|PROCESO ElEjer;
   aAlfa = #0#1;
   aAlfa = aAlfa % -102;
   #0#4 = aAlfa;
|FPRC;

|PROCESO Select; |TIPO 0;
  |SI #0#0 = "S";
      |C_M #0#2,1,"S"; |C_M #0#3,1,"S";
      |PARA nPara1 = 5; |SI nPara1 [ 32; |HACIENDO nPara1 = nPara1 + 3;
            nNume1 = nPara1 + 1;
            nNume2 = nPara1 + 2;
            |C_M #0nPara1,1,"N"; #0nPara1 = 0;  |PINTA #0nPara1;
                                 #0nNume1 = 0;  |PINTA #0nNume1;
                                 #0nNume2 = ""; |PINTA #0nNume2;
      |SIGUE;
   |SINO;
      |C_M #0#2,1,"N"; #0#2 = 1;     |PINTA #0#2;
      |C_M #0#3,1,"N"; #0#3 = 99999; |PINTA #0#3;
      |PARA nPara1 = 5; |SI nPara1 [ 32; |HACIENDO nPara1 = nPara1 + 3;
            |C_M #0nPara1,1,"S";
      |SIGUE;
   |FINSI;
|FPRC;

|CALCULO MiraEmpresa; |TIPO 0;
   nInkey = FSalida;
   |SI nInkey = 2; |ACABA; |FINSI;

   |SI #0Campo = 0; |ACABA; |FINSI;

   |SI #0#0 = "N";
       nEmp = #0Campo;
       |HAZ SeldeEmpresa;
       |SI nSelError ! 0;
           |SI nSelError ! 2;
               |MENAV aMensa;
           |FINSI;
           |ERROR;
           |ACABA;
       |FINSI;
       nCampo = Campo + 1;
       #0nCampo = #30#3;  |PINTA #0nCampo;
       nCampo = Campo + 2;
       #0nCampo = #30#1;  |PINTA #0nCampo;
   |FINSI;
|FCAL;

|CALCULO NoMasEmp; |TIPO 0;
     |BLIN 4;
     |SI #0Campo = 0;
         nNume1   = Campo + 1;
         #0nNume1 = 0; |PINTA #0nNume1;
         |PARA nCampo = Campo + 3;  |SI nCampo [ 32;  |HACIENDO nCampo = nCampo + 3;
               nNume1 = nCampo + 1;
               nNume2 = nCampo + 2;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N"; |PINTA #0nCampo;
               #0nNume1 = 0;                         |PINTA #0nNume1;
               #0nNume2 = "";                        |PINTA #0nNume2;
         |SIGUE;
     |SINO;
         nNume1   = Campo + 1;
         |PARA nCampo = Campo + 3;  |SI nCampo [ 32;  |HACIENDO nCampo = nCampo + 3;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
         nEmp = #0Campo; |HAZ MiraEmpresa;
     |FINSI;
|FCAL;

|PROCESO SeldeEmpresa;

   aMensa    = "";
   nSelError = 0;

   |ABRE #30;
   |SELECT #4#30;
   #30#2  = nEmp;
   #30#26 = #0#4;
   |LEE 010#30=;
   |SI FSalida = 0;
       |SI #0#35 ! "T";
           |SI #30#21 ! #0#35;
               |SI #30#21 = "I";
                   aMensa = "    Empresa Inactiva";
               |SINO;
                   aMensa = "    Empresa Activa";
               |FINSI;
               nSelError = 1;
               |VETE Cierra;
           |FINSI;
       |FINSI;
   |SINO;
       nSelError  = 1;
       aMensa = "    No Existe Empresa ";
   |FINSI;

|ET Cierra;
   |SELECT #1#30;
   |CIERRA #30;

   |SI nSelError = 0;
       |HAZ LeoArchivo;
       |SI nYaExiste = 1;
           aAlfa =  "0000Ya esta Archivada la Empresa " + aEmpresa + " Ejercicio " + #0#1;
           |MENAV aAlfa;
           aAlfa = "0000" + #0#44 + "Volver a Archivarla";
           |CONFI aAlfa;
           |SI FSalida ! 0;
               nSelError = 2;
           |FINSI;
       |FINSI;
   |FINSI;
|FPRC;

|PROCESO LeoArchivo;
  nYaExiste = 0;
  |ABRE #6;
  #6#2  = nEmp;
  #6#26 = #0#4;
  |LEE 040#6=;
  |SI FSalida = 0;
      nYaExiste = 1;
  |FINSI;
  |CIERRA #6;
|FPRC;
|| ::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

|PROCESO LasRecojo0;
  enEmpresaPINET = #30#2;
  |HAZ RestriccionPINET;
  |SI enErrorPINET = 1;  |ACABA;  |FINSI;

  aEmpresa = #30#2;
  aEmpresa = ("00000" + aEmpresa) % -105;

  #999#9 = "NO";
  |HAZ LeoArchivo;
  |SI nYaExiste = 1;
      #999#9 = "SI";
      |SI #0#0 = "S";
          |SI #0#44 = "N";
              |LIBERA #30;
              |ACABA;
          |FINSI;
      |FINSI;
  |FINSI;

  #999#0 = #0#1;
  #999#1 = #30#2;
  #999#2 = #30#3;
  #999#3 = #30#1;
  #999#4 = #30#8;
  #999#5 = #30#20;
  #999#6 = #30#26;
  #999#7 = #0#39;
  #999#8 = #0#40;
  #999#10 = #0#41;
  #999#11 = #0#42;
  #999#12 = #0#43;
  |GRABA 040#999n;

  |LIBERA #30;
  nSwAlguna = 1;
|FPRC;

|DEFBUCLE LasRecojo;
   #30#4;
   ;
   #0#2, #0#4;
   #0#3, #0#4;
   be;
   NULL, LasRecojo0;
|FIN;
|| ////////////////////////////////////////////////////////////////////////
|PROCESO LeoAuxiliar;

   |ATRI P;
   |PINTA 2203, "Archivando Datos de ... ";
   |ATRI 0;

   |DDEFECTO #canempre;
   #canempre#2 = #999#1;
   #canempre#3 = #999#2;
   |LEE 141#canempre.=;
   |SI FSalida ! 0;
       |LIBERA #canempre;
       |ACABA;
   |FINSI;

   |PINTA 2312, #canempre#2;
   |PINTA 2319, #canempre#3;
   |PINTA 2322, #canempre#1;
   |PINTA 2375, #canempre#26;

   |DDEFECTO #400;
   #400#0 = #canempre#2;
   #400#1 = #canempre#3;
   |LEE 000#400=;

|| Aqui Archivar
   eaNomArchivo = "z" + #0#1 + (("00000" + #canempre#2) % -105);

   enEmpArch = #canempre#2;
   enPerArch = #canempre#3;
   eaPathArc = #0#38;
   eaUsuaArc = #0#42;
   eaNomArch = #canempre#1;
   enAnyArch = #canempre#26;
   eaNifArch = #canempre#8;
   |SUB_EJECUTA "caz00026;ARCHIVAR";
   |SI swerror = 1;
       |ACABA;
   |FINSI;

   |DDEFECTO #6;
   #6#2  = #canempre#2;
   #6#26 = #canempre#26;
   |LEE 141#6=;
   |SI FSalida ! 0;
       |DDEFECTO #6;
       #6#2  = #canempre#2;
       #6#26 = #canempre#26;
       |GRABA 020#6b;
   |FINSI;

   |PARA nPara1 = 0; |SI nPara1 [ 35; |HACIENDO nPara1 = nPara1 + 1;
         #6nPara1 = #canempre#nPara1#;
   |SIGUE;
   #6#25 = #0#38;
   #6#36 = #999#7;
   #6#37 = #999#10;
   #6#38 = #999#11;
   #6#39 = #999#12;
   #6#40 = eaNomArchivo;
   #6#41 = #400#2;
   |GRABA 020#6a;
   |LIBERA #6;

   |SI nImprime = 1;
       |PRINT;
   |FINSI;

   |SI #999#7 = "S";
       |DFICB aPath;
       aAlfa1 = #canempre#0;
       aPathE = aPath + ("000000" + aAlfa1) % -106;
       aDire = "";
       |DIRECTORIO aDire;
       aAlfa1 = aDire + "bin/agirm -r " + aPathE;
       |SYSTEM aAlfa1;

       #camoneda#0 = #canempre#2;
       #camoneda#1 = #canempre#3;
       |LEE 141#camoneda.=;
       |SI FSalida = 0;
           |BORRA 020#camoneda.a;
       |FINSI;
       |LIBERA #camoneda;

       #400#0 = #canempre#2;
       #400#1 = #canempre#3;
       |LEE 141#400=;
       |SI FSalida = 0;
           |BORRA 020#400a;
       |FINSI;
       |LIBERA #400;

       |BORRA 020#canempre.a;
  |FINSI;

  |LIBERA #canempre;
  nSwAlguna = nSwAlguna + 1;
|FPRC;

|DEFBUCLE LeoAuxiliar;
   #999#1;
   8;
   #0#1, 00001, 0, "S";
   #0#1, 99999, 9, "S";
   e;
   NULL, LeoAuxiliar;
|FIN;

|| ////////////////////////////////////////////////////////////////////////
|PROCESO ClaveAlta999;
   #999#0 = #0#1;
   #999#1 = 00001;
   #999#2 = 0;
|FPRC;

|PROCESO ClaveBaja999;
   #999#0 = #0#1;
   #999#1 = 99999;
   #999#2 = 9;
|FPRC;

|CALCULO VoyaArchivar; |TIPO 12;
  |SI #0#36 = "NO";
      |ACABA;
  |FINSI;

  |SALVA_DATOS #0;
  |ABRE #0;
  #0#37 = "A";
  |LEE 141#0=;
  |SI FSalida ! 0;
      |DDEFECTO #0;
      #0#37 = "A";
      |GRABA 020#0b;
  |FINSI;
  |REPON_DATOS #0;
  #0#36 = "NO";
  |GRABA 020#0a;
  |LIBERA #0;
  |CIERRA #0;

  aFichero = ("1a"+ Usuario) % 108;
  |NOME_DAT #999 aFichero;
  |DELINDEX #999;
  nSwAlguna = 0;

  |ABRE #999;
  |ABRE #camoneda;
  |SI #0#0 = "S";
      |BUCLE LasRecojo;
  |SINO;
      |ABRE #30;
      |SELECT #4#30;
      |PARA nPara1 = 5; |SI nPara1 [ 32; |HACIENDO nPara1 = nPara1 + 3;
            nNume1 = nPara1 + 1;
            |SI #0nPara1 ! 0;
                #30#2  = #0nPara1;
                #30#26 = #0#4;
                |LEE 101#30.=;
                |SI FSalida = 0;
                    |HAZ LasRecojo0;
                |FINSI;
                |LIBERA #30;
            |FINSI;
      |SIGUE;
      |SELECT #1#30;
      |CIERRA #30;
   |FINSI;
   |CIERRA #camoneda;
   |CIERRA #999;

   enOperar = 0;
   |SI nSwAlguna ! 0;
       |PINPA #0#999;
       |HAZ PonBotonW29;
       |ENTLINEAL #1#999, -2, 4, 21, 0, ClaveAlta999, ClaveBaja999;
       |HAZ QuitaBotonW29;
   |SINO;
       |MENAV "    No existe Empresas en la Seleccion";
       |ACABA;
   |FINSI;

  |CONFI "    SProceder a Archivar las Empresa Contables Seleccionadas";
  |SI FSalida ! 0;
      |ACABA;
  |FINSI;

  nImprime = 0;
  |SI #0#45 = "S";
      nImprime = 1;
      |IMPRE 0;
      |SI FSalida ! 0;
          |MENAV "0000No existe Impresora";
          |CONFI "0000SContinuar con el Proceso de Archivar";
          |SI FSalida ! 0; |ACABA; |FINSI;
          nImprime = 0;
      |SINO;
          |INFOR informe;
          |SI FSalida ! 0;
              |MENAV "0000No existe Informe";
              |FINIMP;
              nImprime = 0;
          |FINSI;
      |FINSI;
  |FINSI;

  |BLIN 21;
  |BLIN 22;
  |BLIN 23;
  |CUADRO 2101 2480;
  |ATRI P;
  |PINTA 2303, "Empresa: ";
  |PINTA 2364, "Ejercicio: ";
  |ATRI 0;

  nSwAlguna = 0;
  |ABRE #6;
  |ABRE #30;
  |ABRE #400;
  |ABRE #camoneda;
  |BUCLE LeoAuxiliar;
  |CIERRA #camoneda;
  |CIERRA #400;
  |CIERRA #30;
  |CIERRA #6;

  |SI nSwAlguna ! 0;
      |SI nImprime = 1; |PIEINF; |FINSI;
      aAlfa = "0000Empresas Archivadas";
      |SI nSwAlguna = 1;
          aAlfa = "0000Empresa Archivada";
      |FINSI;
  |SINO;
      aAlfa = "0000No se ha archivado Ninguna Empresa";
  |FINSI;
  |MENAV aAlfa;
  |SI nImprime = 1;
      |FININF;
      |FINIMP;
  |FINSI;
|FCAL;

||*********************************************************************
|PROCESO CalculoPath;  || Crea Directorio HistoricoConta
  |DBASE aBase;
  aPath = aBase + "historico";

  |MKDIR aPath;
  aPath  = aPath + "/";
|FPRC;

||*********************************************************************
|PROGRAMA;
     |SI PARAMETRO = "";
         |CUADRO 0501 2380;
         |BLANCO 0602 2279;
         |PINTA 1320, "Introduzca Password : ";
         E_inf = "";
         E_sup = "20";
         aPassword = 1342 ? -1;
         |SI aPassword ! "<MYPASWD>     ";
             |MENAV "     Permiso denegado.";
             |ACABA;
         |FINSI;
     |FINSI;

     |HAZ inicio;

     |ABRE #0;
     #0#37 = "A";
     |LEE 141#0=;
     |SI FSalida ! 0;
         |DDEFECTO #0;
         #0#37 = "A";
         |GRABA 020#0b;
     |FINSI;
     |LIBERA #0;
     |CIERRA #0;

     |PDEFECTO #0, 41, 42;
     #0#42 = Usuario % 108;
     #0#43 = Usuario % 840;
     |HAZ CalculoPath;
     #0#38 = aPath;

     |CLS;
     |CABEZA "Archivar Empresas";
     |PINPA #0#0;
     |PINDA #0#0;

     |ENDATOS #1#0;
|FPRO;
