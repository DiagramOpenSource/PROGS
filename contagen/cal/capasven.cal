|FICHEROS;
   capasven #0;

   cacuenta #1;
   cavenaut #2;
   calibros #3;
   camaasic #4;
   camaasil #5;
   camandia #6;

   caconvec #10;
   caconvel #11;
   canempre #30;
   caconasi #20;
   camaniva #100;
   caivalin #101;

   caivaasi;
   caivases #203;
   caivatm4;

   :/modelos/def/dsmom030 #130;
|FIN;

|VARIABLES;
   &entrada = 1;
   &enQSerie = 0;
   swDVentas = 0;

   &DVentas = "";

   line = 0;
   DocumNuevo = 0;

   nCalc1 = 0;
   nCalc2 = 0;

   cta_t = 0;
   dig_t = 0;
   alfa1 = "";
   alfa2 = "";
   alfa3 = "";
   alfa4 = "";
   nMes  = 0;
   long = 0;
   sw = 0;
   x = 0;
   y = 0;

   fecasi = @;
   importe = 0;
   importe1 = 0;
   importe2 = 0;
   importe3 = 0;
   importe4 = 0;
   importe5 = 0;
   Timporte1 = 0;
   Timporte2 = 0;
   Timporte3 = 0;
   nume1 = 0;
   num = 0;
   num1 = 0;
   num2 = 0;
   num3 = 0;
   num4 = 0;
   num5 = 0;
   num6 = 0;
   desde = 0;
   hasta = 0;
   opera = "B";
   FEstado = 0;
   ini = 0;
   fecalf = @;
   mes = "";
   mesfec = 0;
   mesac = 0;
   a = 0;
   b = 0;
   c = 0;
   asie = 0;
   factura = 0;
   trim1 = @;
   trim2 = @;
   trim3 = @;
   {366}cant = 0;
   traba = "";
   nconc = 0;
   nYaCrea = 0;
   swpcta = 0;
   ModoEntrada = 0;
   nLibro = 0;
   nAlguno  = 0;
   mensa1   = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
   mensa2   = "    ERROR FECHA INFERIOR O IGUAL AL BLOQUEO DE DATOS";
   &snFiltr = "";
   &snFilAc = "";
   &snFilBa = "";
   nsw     = 0;

  {40}aPopup        = "";
     nID            = 0;
     aID            = "";
     aTexto         = "";

  nInkey    = 0;
  nMes1     = 0;
  nMes2     = 0;
  fFechaIni = @;
  fFechaFin = @;
  nError    = 0;
  nPerio    = 0;
  nPara1    = 0;

  &enSi303   = 0;
  &enSi111   = 0;
  &eSwReten  = 0;
  &enPanMod  = 0;
  &enEmiMod  = 0;
  &enAParam  = 0;
  &eaNomFich = "";
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoan_i;
|| ************************************************************************
|| PROCESOS DE PANTALLA
|| ************************************************************************
|CALCULO VerPeriodos;
     eSwReten = 0;
     enPanMod = 1;
     enPerMod = nPerio;
     |HAZ ConsulModIVA;
     |SI enEmiMod = 1; nError = 1; |FINSI;
|FCAL;

|PROCESO CtrlPeriodo;

   |SI enSi303 = 1; |Y #caivaasi#167 ! "X";

       nError = 0;
       fFechaIni = #0#1;
       fFechaFin = #0#2;
       |SI #caivaasi#168 = "A"; |Y #0#24 ! "D";
           fFechaIni = #0#25;
           fFechaFin = #0#25;
       |FINSI;

       num2 = 0; nPerio = -1;
       alfa1 = fFechaIni; alfa1 = alfa1 % 402; nMes1 = alfa1;
       alfa1 = fFechaFin; alfa1 = alfa1 % 402; nMes2 = alfa1;

       |PARA nPara1 = nMes1; |SI nPara1 [ nMes2; |HACIENDO nPara1 = nPara1 + 1;
             alfa1 = ("00" + nPara1) % -102;
             |SI enEjercicio [ 2009;
                 |SI #caivaasi#46 = "T";
                     |SI nPara1 ] 1;  |Y nPara1 [ 3;  alfa1 = "01"; |FINSI;
                     |SI nPara1 ] 4;  |Y nPara1 [ 6;  alfa1 = "02"; |FINSI;
                     |SI nPara1 ] 7;  |Y nPara1 [ 9;  alfa1 = "03"; |FINSI;
                     |SI nPara1 ] 10; |Y nPara1 [ 12; alfa1 = "04"; |FINSI;
                |SINO;
                     |SI #caivaasi#46 = "A"; alfa1 = "00"; |FINSI;
                |FINSI;
            |FINSI;
            num1 = alfa1;
            |SI num1 ! nPerio;
                num2   = num2 + 1;
                nPerio = num1;
                |HAZ VerPeriodos;
            |FINSI;
       |SIGUE;

       |SI nError = 1;
           alfa2 = "    AVISO";
           |SI #caivaasi#167 = "N"; alfa2 = "    ERROR"; |FINSI;
           |SI num2 [ 1;
               alfa1  = alfa2 + ". Ya elaborado el Modelo de IVA del A¤o y Periodo introducido ";
           |SINO;
               alfa1  = alfa2 + ". Ya elaborado el Modelo de IVA del A¤o y Periodos introducidos";
           |FINSI;
           |MENAV alfa1;
           |SI #caivaasi#167 = "N"; |ERROR; |FINSI;
       |FINSI;
 |FINSI;
|FPRC;

|CALCULO fechabien;
   |C_M #0Campo,0,alfa1;
   |SI alfa1 = "N"; |ACABA; |FINSI;

   |SI #0Campo < fec1; |O #0Campo > fec2;
      |MENAV mensa1;
      |ERROR;
      |ACABA;
   |SINO;
      |SI #0Campo [ fec3;
          |MENAV mensa2;
          |SI Campo = 25;
              |ERROR;
              |ACABA;
          |FINSI;
      |FINSI;
   |FINSI;

   |SI Campo = 2;  |Y #0#1 > #0#2;
      |MENAV "     Atencion  fecha Erronea";
      |ERROR;
      |ACABA;
   |FINSI;

   |SI Campo = 25;
       |HAZ CtrlPeriodo;
   |FINSI;
|FCAL;

|CALCULO eslibro; |TIPO 0;
   |ABRE #3;
   #3#0 = #0Campo;
   |LEE 011#3=;
   |SI FSalida ! 0;
      |MENAV "      Error no existe Libro de IVA";
      |CIERRA #3;
      |ERROR;
      |ACABA;
   |FINSI;
   |SI #3#2 ! "R";
      |MENAV "     El libro NO es de IVA Repercutido";
      |ERROR;
   |FINSI;
   |CIERRA #3;

|SI nYaCrea = 0; |O  enLibIVA ! #0#5;
   enLibIVA  = #0#5;
   enFilConc = 2;          || Conceptos de IVA
   |HAZ CreaConcepto;
   nYaCrea = 1;
|FINSI;
 enLibIVA = #0#5;
 aTipoIVA = "R";
|FCAL;

|CALCULO AntesConcepto; |TIPO 7;
  #0#16 = #0#15; |PINTA #0#16;
  #0#19 = #0#18; |PINTA #0#19;
  #0#17 = #0#15; |PINTA #0#17;
  #0#20 = #0#18; |PINTA #0#20;
|FCAL;

|CALCULO TrasConcepto;  |TIPO 0;     ||Concepto Factura
   |SI FSalida = 10;
       enConcepto = #0Campo;
       |HAZ VerConcepto;
       #0Campo = enConcepto; |PINTA #0Campo;
   |FINSI;

   enLibIVA  = #0#5;
   enFilConc = 2;          || Concepto de IVA
   enConcepto = #0Campo;
   |HAZ FiltroConcepto;  |SI eswLect ! 0; |ACABA; |FINSI;

   enActividad = #0#28;
   |SI Haybasica = 1;
       enFilRegIVA = 0;
       enConcepto  = #0Campo;
       |HAZ FilRegimenIVA;
   |FINSI;
   nume1 = Campo + 3;
   #0nume1 = #dsmom030#1; |PINTA #0nume1;

|SI Campo = 15; |O Campo = 16;
   #0#10= eaCtaIVA; |PINTA #0#10;
   emCta    = 0;
   eaCuenta = #0#10; |HAZ SacaNivel; #0#13 = enNivel;
   |HAZ LeeCuenta;
   |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea
|FINSI;
|FCAL;

|CALCULO fincon; |TIPO 7;
   |C_M #0Campo,0,alfa1;
   |SI alfa1 = "S";
       |SI eaSobreEs ! "S";
           |PTEC 816;
       |FINSI;
   |FINSI;
|FCAL;

|| ------- Cuenta
|CALCULO AntesCuenta; |TIPO 7;             ||Cuenta Cli/Pro
  |SI swpcta = 1; |O nconc = #0#15; |ACABA; |FINSI;

  alfa1 = "43";
  |SI #dsmom030#19 = "S"; alfa1 = #dsmom030#67; |FINSI;
  |QBT alfa1;
  alfa2 = (alfa1 + "0000") % 104;
  alfa2 = alfa2 + ".       "
  #0Campo = alfa2;
  |PTEC 816;
  swpcta = 1;
|FCAL;

|CALCULO Cuenta; |TIPO 6;
   |C_M #0Campo,0,alfa1;
   |SI alfa1 = "N"; |ACABA; |FINSI;

     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;
|FCAL;

|CALCULO TrasCuenta; |TIPO 0;
   emCta    = 1;
   eaCuenta = #0Campo;
   |HAZ SacaNivel;
   |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea

   nume1 = Campo + 3; #0nume1 = enNivel;
   |HAZ LeeCuenta;
   |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea

   aTipoIVA = "R";
   enLibIVA = #0#5;
   |SI Campo = 9;
      enConcepto = #0#15;
      |HAZ FiltraCtaCP;
   |FINSI;
   |SI Campo = 10;
      enConcepto = #0#16;
      |HAZ FiltraCtaIG;
   |FINSI;
|FCAL;

|CALCULO cuadro; |TIPO 7;
   |PUSHV 1734 2180;
   |CUADRO 1734 2180;
   |ATRI R;
   |PINTA 1835, " (D) Asiento por Dia de Venta                ";
   |PINTA 1935, " (R) Un Asiento con apuntes por Dia de Venta ";
   |PINTA 2035, " (T) Un Asiento con apuntes por el Total     ";
   |ATRI r;
|FCAL;

|CALCULO repon; |TIPO 0;
 |POPV;
 |SI FSalida = 2; |ACABA; |FINSI;

 |C_M #0#25,1,"N";
 |SI #0#24 ! "D";
     |C_M #0#25,1,"S";
     #0#25 = #0#2;
     |PINTA #0#25;
 |SINO;
     |HAZ CtrlPeriodo;
 |FINSI;
|FCAL;

|CALCULO AntesDepar; |TIPO 7;
   |C_M #0Campo, 0, alfa1;
   |SI alfa1 = "S"
      |SI Haybasica = 1;
          |MENSA "    <F2> Consultar Actividades";
      |SINO;
          |MENSA "    <F2> Consultar Departamentos";
      |FINSI;
   |FINSI;
|FCAL;

|CALCULO AntesSubde; |TIPO 7;
   |C_M #0Campo, 0, alfa1;
   |SI alfa1  = "S";
      |SI Haybasica = 1;
          |SI enArrend = 1;
              |MENSA "    Subdepartamento. <F2> Consultar Referencias Catastrales";
          |SINO;
              |MENSA "    Subdepartamento. <F2> Consultar Cultivos";
          |FINSI;
      |FINSI;
   |FINSI;
|FCAL;

|CALCULO TrasDepar; |TIPO 0;
   |SI eaDepar = "N"; |ACABA; |FINSI;
   eOpDep = 0; || 1 = Consulta pantalla
   |SI FSalida = 10; eOpDep = 1;  |FINSI;
   enActividad = #0#28;
   eaCodDep    = #0#28;
   |HAZ Actividad;
   |SI eswLect = 1;
       |MENAV "    Error.! No Existe Actividad / Departamento";
       |ERROR;
   |SINO;
       alfa1 = enActividad;
       |SI Haybasica = 1;
           alfa1 = ("00" + alfa1) % A02;
       |FINSI;
       #0#28 = eaCodDep; |PINTA #0#28;
       #0#30 = eaNombreAct; |PINTA #0#30;
   |FINSI;
|FCAL;

|CALCULO TrasSubde; |TIPO 0;
   |SI eaSubde = "N"; |ACABA; |FINSI;

   eOpSub = 0; || 1 = Consulta pantalla
   |SI FSalida = 10; eOpSub = 1;  |FINSI;
   enConcepto  = #0#15;
   eaCodSubdep = #0#29;
   enQue = 1;
   |HAZ SubActividad;
   enQue = 0;
   |SI eswLect = 1;
       |MENAV "    Error.! No Existe SubDepartamento";
       |ERROR;
   |SINO;
       #0#29 = eaCodSubdep; |PINTA #0#29;
       #0#31 = eaNomSubdep; |PINTA #0#31;
   |FINSI;
|FCAL;

|PROCESO Tipo7C35;  |TIPO 7;
   |SI enEjercicio > 2011;
       alfa1 = "<*> No se pasa al 340.";
   |SINO;
       alfa1 = "<X> No se pasa al 340.";
   |FINSI;
   alfa1 = "    <F11> para consultar Tipo Operaciones Mod. 340." + alfa1;
   |MENSA alfa1;
|FPRC;

|PROCESO Tipo66C35;  |TIPO 66;
     alfa1 = "|C035WID";
     aID   = #0#alfa1#;
     nID   = aID;

     aPopup1  = nID;  || Si fuera -1 en la posicion
     |SI enEjercicio [ 2011;
         aPopup2  = 15;
     |SINO;
         aPopup2  = 21;
         |SI enEjercicio ] 2014;
             aPopup2  = 30;
         |FINSI;
     |FINSI;
     aPopup3  = "  Operación habitual";
     aPopup4  = "A Asiento resumen de facturas";
     aPopup5  = "B Asiento resumen de tiques";
     aPopup6  = "C Factura con varios asientos (varios tipo impositivos)";
     aPopup7  = "D Factura rectificativa";
     aPopup8  = "E IVA devengado pendiente de emitir factura";
     aPopup9  = "G Régimen especial de grupo de entidades en IVA o IGIC";
     aPopup10 = "H Régimen especial de oro de inversión";
     aPopup11 = "I Inversión del Sujeto pasivo (ISP)";
     aPopup12 = "J Tiques";
     aPopup13 = "K Rectificación anotaciones registrales";
     aPopup14 = "M IVA facturado pendiente de devengar (emitida factura)";
     aPopup15 = "N Facturación de las prestaciones de servicios de agencias de viaje que actúan como mediadoras en nombre y por cuenta ajena";
     aPopup16 = "O Factura emitida en sustitución de tiques facturados y declarados.";
     aPopup17 = "Q Operaciones a las que se aplique el régimen especial de bienes usados, ...";
     |SI enEjercicio > 2011;
         aPopup18 = "R Operación de arrendamiento de local de negocio.";
         aPopup19 = "S Subvenciones, auxilios o ayudas satisfechas o recibidas, tanto por parte de Administraciones públicas o entidades privadas.";
         aPopup20 = "T Cobros por cuenta de terceros de honorarios profesionales o de derechos derivados ...";
         aPopup21 = "U Operación de seguros.";
         aPopup22 = "V Compras de Agencias viajes: operaciones de prestación de servicios de mediación en nombre y por cuenta ajena relativos ...";
         aPopup23 = "W Operaciones sujetas al Impuesto sobre la Producción, los Servicios y la Importación en las Ciudades de Ceuta y Melilla.";
         |SI enEjercicio ] 2014;
             aPopup24 = "Z Operaciones en Régimen especial de criterio de caja";
             aPopup25 = "1 IVA criterio de caja. Asiento resumen de facturas";
             aPopup26 = "2 IVA criterio de caja. Factura con varios asientos";
             aPopup27 = "3 IVA criterio de caja. Factura rectificativa";
             aPopup28 = "4 IVA criterio de caja. Adqusiciones realizadas por las agencias de viajes en interés del viajero";
             aPopup29 = "5 IVA criterio de caja. Factura simplificada";
             aPopup30 = "6 IVA criterio de caja. Rectificación de errores registrales";
             aPopup31 = "7 IVA criterio de caja. Facturación de las prestaciones de servicios de agencias de viajes";
             aPopup32 = "8 IVA criterio de caja. Operación de arrendamiento de local de negocio";
         |FINSI;
     |FINSI;
     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
         IaPopup  = aPopup3  <-;
         IaPopup  = IaPopup + (FSalida - 1);

         alfa1 = aPopup % 101;  #0#35 = alfa1;
     |FINSI;

     |PINTA #0#35;
|FPRC;

|CALCULO Tipo0C35; |TIPO 0;
     |BLIN 4;
     aTexto = "";
     |SI #0#35 = " "; aTexto = "Operaci¢n habitual"; |FINSI;
     |SI #0#35 = "A"; aTexto = "Asiento resumen de facturas"; |FINSI;
     |SI #0#35 = "B"; aTexto = "Asiento resumen de tiques"; |FINSI;
     |SI #0#35 = "C"; aTexto = "Factura con varios asientos (varios tipo impositivos)"; |FINSI;
     |SI #0#35 = "D"; aTexto = "Factura rectificativa"; |FINSI;
     |SI #0#35 = "E"; aTexto = "IVA devengado pendiente de emitir factura"; |FINSI;
     |SI #0#35 = "G"; aTexto = "R‚gimen especial de grupo de entidades en IVA o IGIC"; |FINSI;
     |SI #0#35 = "H"; aTexto = "R‚gimen especial de oro de inversi¢n"; |FINSI;
     |SI #0#35 = "I"; aTexto = "Inversi¢n del Sujeto pasivo (ISP)";|FINSI;
     |SI #0#35 = "J"; aTexto = "Tiques"; |FINSI;
     |SI #0#35 = "K"; aTexto = "Rectificación anotaciones registrales"; |FINSI;
     |SI #0#35 = "M"; aTexto = "IVA facturado pendiente de devengar (emitida factura)"; |FINSI;
     |SI #0#35 = "N"; aTexto = "Facturaci¢n de las prestaciones de servicios de agencias de viaje que actúan como mediadoras en nombre y por cuenta ajena"; |FINSI;
     |SI #0#35 = "O"; aTexto = "Factura emitida en sustituci¢n de tiques facturados y declarados."; |FINSI;
     |SI #0#35 = "Q"; aTexto = "Operaciones a las que se aplique el r‚gimen especial de bienes usados, ..."; |FINSI;
     |SI enEjercicio > 2011;
         |SI #0#35 = "R"; aTexto = "Operación de arrendamiento de local de negocio."; |FINSI;
         |SI #0#35 = "S"; aTexto = "Subvenciones, auxilios o ayudas satisfechas o recibidas, tanto por parte de Administraciones públicas o entidades privadas."; |FINSI;
         |SI #0#35 = "T"; aTexto = "Cobros por cuenta de terceros de honorarios profesionales o de derechos derivados ..."; |FINSI;
         |SI #0#35 = "U"; aTexto = "Operación de seguros."; |FINSI;
         |SI #0#35 = "V"; aTexto = "Compras de Agencias viajes: operaciones de prestación de servicios de mediación en nombre y por cuenta ajena relativos ..."; |FINSI;
         |SI #0#35 = "W"; aTexto = "Operaciones sujetas al Impuesto sobre la Producción, los Servicios y la Importación en las Ciudades de Ceuta y Melilla."; |FINSI;
         |SI enEjercicio ] 2014;
             |SI #0#35 = "Z"; aTexto = "Operaciones en Régimen especial de criterio de caja"; |FINSI;
             |SI #0#35 = "1"; aTexto = "IVA criterio de caja. Asiento resumen de facturas"; |FINSI;
             |SI #0#35 = "2"; aTexto = "IVA criterio de caja. Factura con varios asientos"; |FINSI;
             |SI #0#35 = "3"; aTexto = "IVA criterio de caja. Factura rectificativa"; |FINSI;
             |SI #0#35 = "4"; aTexto = "IVA criterio de caja. Adqusiciones realizadas por las agencias de viajes"; |FINSI;
             |SI #0#35 = "5"; aTexto = "IVA criterio de caja. Factura simplificada"; |FINSI;
             |SI #0#35 = "6"; aTexto = "IVA criterio de caja. Rectificaci¢n de errores registrales"; |FINSI;
             |SI #0#35 = "7"; aTexto = "IVA criterio de caja. Facturaci¢n de las prestaciones servicios agencias viajes"; |FINSI;
             |SI #0#35 = "8"; aTexto = "IVA criterio de caja. Operaci¢n de arrendamiento de local de negocio"; |FINSI;
         |FINSI;
     |FINSI;

     |SI #0#35 = "C"; aTexto = ""; |FINSI; || no puede elegir C, es automatico
     |SI enEjercicio [ 2011;
         |SI #0#35 = "X"; aTexto = "No pasa al 340"; |FINSI;
     |SINO;
         |SI #0#35 = "*"; aTexto = "No pasa al 340"; |FINSI;
     |FINSI;

     |SI aTexto = "";
         |MENAV "0000 Clave de operaci¢n incorrecta";
         |ERROR;
         |ACABA;
     |FINSI;

     |SI #0#35 ! "A"; |Y #0#35 ! "B"; |Y #0#35 ! "J";
         |CONFI "    NEs correcta la Clave Elegida";
         |SI FSalida ! 0;
             |ERROR;
         |FINSI;
     |FINSI;
|FCAL;
||----------------------------------------------------

|CALCULO TrasTipoIVA; |TIPO 0;
   |SI FSalida = 2; |ACABA; |FINSI;

   aTipoIVA = "R";
   |SI FSalida = 10;
      efFechaIVA = #0#1; enLineaIVA = 1; |HAZ SelecTipoIVA;
      |SI enLineaIVA < 0;
         |MENAV "    Control de cuentas no definido";
         |ERROR; |ACABA;
      |FINSI;
      #0#27 = enLineaIVA; |PINTA #0#27;
      |ERROR;
   |FINSI;

   efFechaIVA = #0#1; enLineaIVA = #0#27; |HAZ SacaIVA;
   nCalc1 = enPorcIVA; nCalc2 = enPorcREC;
   efFechaIVA = #0#2; enLineaIVA = #0#27; |HAZ SacaIVA;
   |SI nCalc1 ! enPorcIVA; |O nCalc2 ! enPorcREC;
      |MENAV "    !! ATENCION !! En el rango de fechas indicadas entran dos tipos diferentes de iva. Fraccione por periodo.";
      |ERROR; |ACABA;
   |FINSI;

   #0#11 = eaCtaIVA;
   #0#32 = eaCtaREC;
   |PINTA #0#11;

   #0#8  = enPorcIVA;  |PINTA #0#8;
   #0#33 = enPorcREC;  |PINTA #0#33;
|FCAL;

||----------------------------------------------------
|CALCULO anacta; |TIPO 0;
   nInkey = FSalida;
   |SI nInkey = 2;  |ACABA; |FINSI;
   |SI enSwAna = 0; |ACABA; |FINSI;

   salidas = FSalida;
   |HAZ FPuntosAna;

   eOpAna = 0; || 1 = Consulta pantalla
   |SI nInkey = 10; eOpAna = 1;  |FINSI;
   eaCtaAna = #0#36;
   |HAZ LeeCtaAna;
   |SI eswLect = 1;
       |MENAV "    Error.! No Existe Cuenta Analitica";
       |ERROR;
       |ACABA;
   |SINO;
       #0#36 = eaCtaAna;
       |PINTA #0#36;
  |FINSI;
|| ---------------------------------------------------------
|| ...  Comprobar validez de la cuenta analitica segun limites definidos
  eaCtaAna = #0#36;
  |HAZ eLimCtaAna;
  |SI enBuenaAna ! 0;
      |MENAV eaBuenaAna;
      |ERROR;
  |FINSI;
|FCAL;
|| ***************************************************************

|PROGRAMA;

   |HAZ inicial; |SI swerror = 2; |ACABA; |FINSI;
   |HAZ DirAplicSt;
   |HAZ Analitica;
   |SI enSwAna = 1;
       |C_V #0#36,1,"S"; |C_M #0#36,1,"S";
   |FINSI;

   |CLS;
   |PINPA #0#0;
   |CABEZA "Astos Ventas Automaticas";
   |SI enSwAna = 1;
       |ATRI P; |PINTA 1543, "Cta Analitica :"; |ATRI 0;
   |FINSI;

   |ABRE #caivaasi;
   |DDEFECTO #caivaasi;
   |LEE 001#caivaasi.p;
   enQSerie = 0;
   |SI #caivaasi#44 = "S"; |C_M #0#26,1,"S"; enQSerie = 1; |FINSI;
   snFiltr = #caivaasi#95;
   snFilAc = #caivaasi#118;
   snFilBa = #caivaasi#119;
   eaPaDOM1 = #caivaasi#114;  ||documento automatico
   eaPaDOM2 = #caivaasi#133;  ||permitir repetir diferentes diarios
   eaPaDOM3 = #caivaasi#134;  ||permitir ultimo + 1
   eaPaCTA1 = #caivaasi#135;  ||permitir alta no estructuras 2008
   eaPaCTA2 = #caivaasi#136;  ||Aviso 1990
   eaPaNif1 = #caivaasi#152;
   eaPaNif2 = #caivaasi#153;
   eaSobreEs = #caivaasi#155;
   eaABanco  = #caivaasi#161;
   eaObClPr  = #caivaasi#186;
   eaDscomer9 = #caivaasi#191;
   |CIERRA #caivaasi;
   |HAZ Consul303;

   |PINDA #0#0;

   |ABRE #0;
   |SI DVentas = "";
       #0#28 = #caivaasi#19;
       |ENCLAVE #0,0,1;
       DVentas = #0#28;
   |SINO;
       #0#28 = DVentas; |PINTA #0#28;
   |FINSI;
   |HAZ LeeVentas;
   |SI  swDVentas = 1;
        |CIERRA #0;
        |ACABA;
   |FINSI;
   #0#29 = eaDfSubC;
   #0#31 = eaDfSubN;

   |PINDA #0#0;
   |ENDATOS #2#0;
   |SI FSalida = 0;
       |HAZ elpase;
       #0#1 = #0#2 + 1;
       #0#2 = fec2;
       |SI #0#1 > #0#2; #0#1 = #0#2; |FINSI;
       |GRABA 001#0a;
       |SI nAlguno = 1;  |SUB_EJECUTA_NP "caut0018;PASE";  |FINSI;
   |FINSI;
   |CIERRA #0;
|FPRO;

|PROCESO primeravez;
   |DDEFECTO #0;
   |ABRE #6;
   #6#0 = #0#3;
   |LEE 011#6=;
   |SI FSalida = 0;
      #0#4 = #6#1;
   |FINSI;
   |CIERRA #6;

   #0#5 = #caivaasi#3;
   |ABRE #3;
   #3#0 = #0#5;
   |LEE 011#3=;
   |SI FSalida = 0;
      #0#6 = #3#1;
   |FINSI;
   |CIERRA #3;

   #0#15 = #caivaasi#5;  #0#18 = #caivaasi#7;
   #0#16 = #caivaasi#5;  #0#19 = #caivaasi#7;
   #0#17 = #caivaasi#13; #0#20 = #caivaasi#15;
   #0#1  = fec1;
   #0#2  = fec2;
   #0#25 = fec2;
|FPRC;

|| ********************************************************************
|| ***** EL PASE
|| ********************************************************************
|PROCESO cargaimpo;
   |ABRE #2;
   Icant = cant1 <-;
   |PARA y = 1;  |SI y [ 4;  |HACIENDO y = y + 1;
      #cavenaut#0 = 1;
      #cavenaut#1 = y;
      #cavenaut#95 = #0#28;
      |LEE 040#2=;
      |SI FSalida ! 0;
          |DDEFECTO #2;
          #cavenaut#0 = 1;
          #cavenaut#1 = y;
          #cavenaut#94 = enEjercicio;
          #cavenaut#95 = #0#28;
      |FINSI;
      num = 1
      |PARA x = 1;  |SI x [ 92;  |HACIENDO x = x + 1;
         num = num + 1;
         cant = #cavenaut#num#;
         Icant = Icant + 1;
         |SI x = 91; |Y y [ 2;
            |SAL;
         |FINSI;
      |SIGUE;
   |SIGUE;
   |CIERRA #2;
|FPRC;


|PROCESO descargaimpo;
   |ABRE #2;
   Icant = cant1 <-;
   |PARA y = 1;  |SI y [ 4;  |HACIENDO y = y + 1;
      #cavenaut#0 = 1;
      #cavenaut#1 = y;
      #cavenaut#95 = #0#28;
      |LEE 040#2=;
      |SI FSalida = 0;
         num = 1; nsw = 0;
         |PARA x = 1;  |SI x [ 92;  |HACIENDO x = x + 1;
              num = num + 1;
              #cavenaut#num# = cant;
              Icant = Icant + 1;
              |SI #cavenaut#num# ! 0; nsw = 1; |FINSI;
              |SI x = 91; |Y y [ 2;
                  |SAL;
              |FINSI;
         |SIGUE;
         |SI nsw = 1;
             |GRABA 020#cavenaut.a;
         |SINO;
             |BORRA 020#cavenaut.a;
         |FINSI;
      |FINSI;
   |SIGUE;
   |CIERRA #2;

   |ABRE #11;
   |PARA NumMes = 1; |SI NumMes [ 12; |HACIENDO NumMes = NumMes  + 1;
         #11#0 = #0#28;  || departamento
         #11#1 = NumMes;
         |LEE 101#11=;
         |SI FSalida = 0;
             |SI #11#4 < 0;
                 #11#4  = 0;
                 #11#13 = " ";
                 |GRABA 020#11a;
             |FINSI;
         |FINSI;
         |LIBERA #11;
   |SIGUE;
   |CIERRA #11;
|FPRC;


|PROCESO recufecha;
   |SI num1 > 0;    num2 = 1;   num3 = num1;        |FINSI;
   |SI num1 > 31;   num2 = 2;   num3 = num1 - 31;   |FINSI;
   |SI num1 > 60;   num2 = 3;   num3 = num1 - 60;   |FINSI;
   |SI num1 > 91;   num2 = 4;   num3 = num1 - 91;   |FINSI;
   |SI num1 > 121;  num2 = 5;   num3 = num1 - 121;  |FINSI;
   |SI num1 > 152;  num2 = 6;   num3 = num1 - 152;  |FINSI;
   |SI num1 > 182;  num2 = 7;   num3 = num1 - 182;  |FINSI;
   |SI num1 > 213;  num2 = 8;   num3 = num1 - 213;  |FINSI;
   |SI num1 > 244;  num2 = 9;   num3 = num1 - 244;  |FINSI;
   |SI num1 > 274;  num2 = 10;  num3 = num1 - 274;  |FINSI;
   |SI num1 > 305;  num2 = 11;  num3 = num1 - 305;  |FINSI;
   |SI num1 > 335;  num2 = 12;  num3 = num1 - 335;  |FINSI;
   num4 = 100 + num3;
   alfa1 = num4;
   alfa3 = alfa1 % 202;
   alfa3 = alfa3 + ".";
   num4 = 100 + num2;
   alfa1 = num4;
   alfa2 = alfa1 % 202;
   alfa3 = alfa3 + alfa2 + ".";
   alfa1 = #0#1 % 704;
   alfa3 = alfa3 + alfa1;
   fecasi = alfa3;
|FPRC;

|PROCESO desdehasta;
   alfa1 = #0#1 % 402;
   num = alfa1;
   |SI num = 1;  desde = 0;   |FINSI;
   |SI num = 2;  desde = 31;  |FINSI;
   |SI num = 3;  desde = 60;  |FINSI;
   |SI num = 4;  desde = 91;  |FINSI;
   |SI num = 5;  desde = 121; |FINSI;
   |SI num = 6;  desde = 152; |FINSI;
   |SI num = 7;  desde = 182; |FINSI;
   |SI num = 8;  desde = 213; |FINSI;
   |SI num = 9;  desde = 244; |FINSI;
   |SI num = 10; desde = 274; |FINSI;
   |SI num = 11; desde = 305; |FINSI;
   |SI num = 12; desde = 335; |FINSI;
   alfa1 = #0#1 % 102;
   num = alfa1;
   desde = desde + num;
   alfa1 = #0#2 % 402;
   num = alfa1;
   |SI num = 1;  hasta = 0;   |FINSI;
   |SI num = 2;  hasta = 31;  |FINSI;
   |SI num = 3;  hasta = 60;  |FINSI;
   |SI num = 4;  hasta = 91;  |FINSI;
   |SI num = 5;  hasta = 121; |FINSI;
   |SI num = 6;  hasta = 152; |FINSI;
   |SI num = 7;  hasta = 182; |FINSI;
   |SI num = 8;  hasta = 213; |FINSI;
   |SI num = 9;  hasta = 244; |FINSI;
   |SI num = 10; hasta = 274; |FINSI;
   |SI num = 11; hasta = 305; |FINSI;
   |SI num = 12; hasta = 335; |FINSI;
   alfa1 = #0#2 % 102;
   num = alfa1;
   hasta = hasta + num;
|FPRC;

|PROCESO queimporte;
   |SI #0#8 = 0; |Y #0#33 = 0;
      importe1 = cant;
      importe2 = cant;
      importe3 = 0;
      importe4 = 0;
      importe5 = 0;
   |SINO;
      |SI #0#7 = "N";
         importe2 = cant;
         importe5 = (importe2 * #0#8) / 100;
         importe5 = importe5 ! EURODB;
         importe4 = (importe2 * #0#33) / 100;
         importe4 = importe4 ! EURODB;
         importe3 = importe5 + importe4;

         importe1 = importe2 + importe3;
      |SINO;
         importe1 = cant;

         importe3 = ((#0#8 + #0#33) / 100) + 1;
         importe3 = importe1 / importe3;
         importe2 = importe3 ! EURODB;
         importe3 = importe1 - importe2;

         importe4 = (importe2 * #0#33) / 100;
         importe4 = importe4 ! EURODB;
         importe5 = importe3 - importe4;

      |FINSI;
   |FINSI;
|FPRC;

|PROCESO elpase;

   |SI #0#24 ! "D";
       |SI #0#25 [ fec3; |ACABA; |FINSI;
   |FINSI;

   |ATRI I;
   |PINTA 2410,"CARGANDO IMPORTES DE VENTAS      ";
   |HAZ cargaimpo;
   |PINTA 2410,"REALIZANDO ASIENTOS CONTABLES    ";
   |ATRI 0;

   eaNomFich  = ("0p" + Usuario) % 108;
   |NOME_DAT #caivatm4#eaNomFich#;
   |DELINDEX #caivatm4;

   enAParam = #0#28;
   |HAZ eActivParam;

   |ABRE #camaasic;
   |ABRE #camaasil;
   |ABRE #camaniva;
   |ABRE #caivalin;
   |ABRE #caivatm4;
   nAlguno = 0;
   |HAZ desdehasta;         || Desde Numero dia a Numero dia

   |SI #0#24 = "T";
       |HAZ elpaseT;
       |ACABA;
   |FINSI;

   line = 1;
   Icant = cant1 <-;
   Icant = Icant + desde;
   Icant = Icant - 1;
   |PARA num1 = desde;  |SI num1 [ hasta;  |HACIENDO num1 = num1 + 1;
      |SI cant =  0;  |VETE ajugar;  |FINSI;
      |HAZ recufecha;
      |SI fecasi [ fec3; |VETE ajugar; |FINSI;

      |SI line = 1;
          |HAZ contador;
          asie = #caconasi#1;
      |FINSI;
      |HAZ queimporte;

      traba = " PASANDO VENTA FECHA: " + fecasi + " ";
      |PINTA 2309, traba;
      |PARA x = 9;  |SI x [ 11;  |HACIENDO x = x + 1;
         |DDEFECTO #5;
         #camaasil#24 = nREGISTRO;
         #camaasil#0 = #0#3;
         #camaasil#1 = fecasi;
         |SI #0#24 ! "D"; #camaasil#1 = #0#25;  |FINSI;
         #camaasil#2 = asie;
         #camaasil#3 = line;
         |LEE 101#5=;
         FEstado = FSalida;
         num2 = x + 3;     ||tengo el digito
         num4 = x + 6;     ||tengo el concepto
         num5 = x + 9;     ||tengo la descripcion
         num6 = x + 12;    ||tengo fecha SINO;
         #camaasil#4 = #0x;
         eaCuenta = #camaasil#4; |HAZ SacaNivel;
         #camaasil#5 = enNivel;
         |SI x = 9;   ||ASIENTO DEBE
            #camaasil#8 = "D";
            #camaasil#9 = importe1;
            #camaasil#10 = "";
            #camaasil#11 = 0;
            #camaasil#16 = #camaasil#4;
            #camaasil#17 = #camaasil#5;
            #camaasil#19 = "F";
            #camaasil#20 = 0;
         |SINO;
            #camaasil#8 = "H";
            |SI x = 10;
               #camaasil#9 = importe2;
               #camaasil#19 = "B";
               |SI enSwAna = 1; #camaasil#29 = #0#36; |FINSI;
            |SINO;
               #camaasil#9 = importe3;
               #camaasil#19 = "I";
            |FINSI;
            #camaasil#10 = #camaasil#4;
            #camaasil#11 = #camaasil#5;
            #camaasil#16 = "";
            #camaasil#17 = 0;
            #camaasil#20 = 1;
         |FINSI;
         #camaasil#6 = #0num4;
         alfa3 = #0num5;
         |QBF alfa3;
         |SI #0num6 = "S";
            alfa3 = alfa3 + " " + fecasi;
         |FINSI;
         #camaasil#7 = alfa3;
         |SI x = 9;
             |HAZ pasaiva;
         |FINSI;
         #camaasil#12 = "R";
         #camaasil#13 = #camaniva#0;
         #camaasil#14 = #camaniva#3;
         #camaasil#15 = #camaniva#2;
         #camaasil#21 = #0#28;
         #camaasil#26 = #camaniva#22;
         #camaasil#28 = #0#29;
         |SI #camaasil#9 ! 0;
            |SI FEstado = 0;
               |GRABA 020#5a;
            |SINO;
               |GRABA 020#5n;
            |FINSI;
            |LIBERA #5;
            |HAZ cabecera;
            MasMenos     = #camaasil#9;
            DebeHaber    = #camaasil#8;
            aCUENTA      = #camaasil#4;
            nREGISTRO    = #camaasil#24;
            nDIARIO      = #camaasil#0;
            aFECHA       = #camaasil#1;
            nDOCUMENTO   = #camaasil#2;
            nNIVELCUENTA = #camaasil#5;
            |HAZ ActDiario;          || diario
            |HAZ ActCuenta;          || cuenta
            |HAZ ActTesteo;          || cuenta
            line = line + 10;
         |FINSI;
      |SIGUE;
      |SI #0#24 = "D"; line = 1; |FINSI;
      |SI #0#34 = "S"; cant = 0; |FINSI;
      |ET ajugar;
      Icant = Icant + 1;
   |SIGUE;
   traba = &178 * 33;
   |PINTA 2309, traba;

   |CIERRA #caivatm4;
   |CIERRA #caivalin;
   |CIERRA #camaniva;
   |CIERRA #camaasil;
   |CIERRA #camaasic;

   |SI sw = 1;
       |HAZ ActNumero;
       |SI #0#34 = "S"; |HAZ descargaimpo; |FINSI;
   |FINSI;
|FPRC;

|PROCESO elpaseT; || Cuando solo quiere tres apuntes

   Timporte1 = 0;
   Timporte2 = 0;
   Timporte3 = 0;
   |HAZ contador;
   asie = #caconasi#1;
   line = 1;
   #camaasil#24 = nREGISTRO;
   #camaasil#0 = #0#3;
   #camaasil#1 = #0#25;
   #camaasil#2 = asie;
   #camaasil#4 = #0#9; eaCuenta = #camaasil#4; |HAZ SacaNivel;
   #camaasil#5 = enNivel;
   #camaasil#6 = #0#15;
   #camaasil#7 = #0#18;
   Icant = cant1 <-;
   Icant = Icant + desde;
   Icant = Icant - 1;
   |PARA num1 = desde;  |SI num1 [ hasta;  |HACIENDO num1 = num1 + 1;
      |SI cant =  0;  |VETE ajugarT;  |FINSI;
      |HAZ recufecha;
      |HAZ queimporte;
      traba = " PASANDO VENTA FECHA: " + fecasi + " ";
      |PINTA 2309, traba;
      |HAZ pasaiva;
      Timporte1 = Timporte1 + importe1
      Timporte2 = Timporte2 + importe2
      Timporte3 = Timporte3 + importe3
      |SI #0#34 = "S"; cant = 0; |FINSI;
      |ET ajugarT;
      Icant = Icant + 1;
   |SIGUE;

   |PARA x = 9;  |SI x [ 11;  |HACIENDO x = x + 1;
      |DDEFECTO #5;
      #camaasil#24 = nREGISTRO;
      #camaasil#0 = #0#3;
      #camaasil#1 = #0#25;
      #camaasil#2 = asie;
      #camaasil#3 = line;
      |LEE 101#5=;
      FEstado = FSalida;
      num2 = x + 3;     ||tengo el digito
      num4 = x + 6;     ||tengo el concepto
      num5 = x + 9;     ||tengo la descripcion
      num6 = x + 12;    ||tengo fecha SINO;

      #camaasil#4 = #0#x#; eaCuenta = #camaasil#4; |HAZ SacaNivel;
      #camaasil#5 = enNivel;
      |SI x = 9;   ||ASIENTO DEBE
         #camaasil#8 = "D";
         #camaasil#9 = Timporte1;
         #camaasil#10 = "";
         #camaasil#11 = 0;
         #camaasil#16 = #camaasil#4;
         #camaasil#17 = #camaasil#5;
         #camaasil#19 = "F";
         #camaasil#20 = 0;
      |SINO;
         #camaasil#8 = "H";
         |SI x = 10;
            #camaasil#9 = Timporte2;
            #camaasil#19 = "B";
            |SI enSwAna = 1; #camaasil#29 = #0#36; |FINSI;
         |SINO;
            #camaasil#9 = Timporte3;
            #camaasil#19 = "I";
         |FINSI;
         #camaasil#10 = #camaasil#4;
         #camaasil#11 = #camaasil#5;
         #camaasil#16 = "";
         #camaasil#17 = 0;
         #camaasil#20 = 1;
      |FINSI;
      #camaasil#6 = #0num4;
      alfa3 = #0num5;
      |QBF alfa3;
      #camaasil#7 = alfa3;
      #camaasil#12 = "R";
      #camaasil#13 = #0#5;
      #camaasil#21 = #0#28;
      #camaasil#26 = "";
      #camaasil#28 = #0#29;

      |SI #camaasil#9 ! 0;
             |SI FEstado = 0;
                |GRABA 020#5a;
             |SINO;
                |GRABA 020#5n;
             |FINSI;
             |LIBERA #5;
             |HAZ cabecera;
         MasMenos     = #camaasil#9;
         DebeHaber    = #camaasil#8;
         aCUENTA      = #camaasil#4;
         nREGISTRO    = #camaasil#24;
         nDIARIO      = #camaasil#0;
         aFECHA       = #camaasil#1;
         nDOCUMENTO   = #camaasil#2;
         nNIVELCUENTA = #camaasil#5;
         |HAZ ActDiario;          || diario
         |HAZ ActCuenta;          || cuenta
         |HAZ ActTesteo;          || cuenta
         line = line + 10;
      |FINSI;
   |SIGUE;
   traba = &178 * 33;
   |PINTA 2309, traba;

   |CIERRA #caivatm4;
   |CIERRA #caivalin;
   |CIERRA #camaniva;
   |CIERRA #camaasil;
   |CIERRA #camaasic;
   |SI sw = 1;
       |SI #0#34 = "S"; |HAZ descargaimpo; |FINSI;
   |FINSI;
|FPRC;

|PROCESO cabecera;
   |SI #camaasil#9 = 0;  |ACABA;  |FINSI;
   |DDEFECTO #camaasic;
   #camaasic#12 = #camaasil#24; ||  Registro
   #camaasic#0 = #camaasil#0; ||  diario
   #camaasic#1 = #camaasil#1; ||  fecha
   #camaasic#2 = #camaasil#2; ||  documento
   #camaasic#3 = #camaasil#2; ||  asiento
   |LEE 101#4=;
   FEstado = FSalida;
   #camaasic#12 = #camaasil#24; ||  diario
   #camaasic#0 = #camaasil#0; ||  diario
   #camaasic#1 = #camaasil#1; ||  fecha
   #camaasic#2 = #camaasil#2; ||  documento
   #camaasic#3 = #camaasil#2; ||  asiento
   |SI #camaasil#8 = "D";
      #camaasic#04 = #camaasic#4 + #camaasil#9;
      |SI #camaasic#9 = 0;                         ||CONTRAPARTIDA
         #camaasic#8 = #camaasil#4;
         #camaasic#9 = #camaasil#5;
      |FINSI;
   |SINO;
      #camaasic#5 = #camaasic#5 + #camaasil#9;
      |SI #camaasic#11 = 0;
         #camaasic#10 = #camaasil#4;
         #camaasic#11 = #camaasil#5;
      |FINSI;
   |FINSI;
   #camaasic#13 = 1;
   #camaasic#17 = #camaasil#26;

   |SI FEstado = 0;  |GRABA 020#camaasic.a;  |FINSI;
   |SI FEstado ! 0;  |GRABA 020#camaasic.n;  |FINSI;
   |LIBERA #camaasic;

   |ABRE #11;
   alfa4 = fecasi; alfa4 = alfa4 % 402;
   nMes  = alfa4;
   #11#0 = #camaasil#21; || Departamento
   #11#1 = nMes;
   |LEE 101#11=;
   |SI FSalida = 0;
       |SI #0#34 = "S"; #11#4  = #11#4 - cant; |FINSI;
       #11#12 = "*";
       |GRABA 020#11a;
   |FINSI;
   |LIBERA #11;
   |CIERRA #11;
|FPRC;

|PROCESO pasaiva;
   |SI DocumNuevo = 0; |HAZ NuevoDocum; |FINSI;

   |DDEFECTO #100;
   #camaniva#0 = #0#5;
   #camaniva#1 = DocumNuevo;
   #camaniva#2 = #0#26;
   #camaniva#3 = factura;
   #camaniva#4 = fecasi;

   alfa1 = #camaniva#4;
   |SI #caivaasi#168 = "A"; alfa1 = #camaasil#1; |FINSI;

   alfa3 = alfa1 % 402;    || saca mes
   nume1 = alfa3;
   #camaniva#5 = nume1;
   |SI enEjercicio [ 2009;
       |SI #caivaasi#46 = "T";
           |SI nume1 [ 3;               #camaniva#5 = 1;  |FINSI;
           |SI nume1 > 3; |Y nume1 [ 6; #camaniva#5 = 2;  |FINSI;
           |SI nume1 > 6; |Y nume1 [ 9; #camaniva#5 = 3;  |FINSI;
           |SI nume1 > 9;               #camaniva#5 = 4;  |FINSI;
       |SINO;
           |SI #caivaasi#46 = "A";      #camaniva#5 = 0; |FINSI;
       |FINSI;
   |FINSI;
   #camaniva#6  = "N";
   #camaniva#7  = #camaasil#0;
   #camaniva#8  = #camaasil#1;
   #camaniva#9  = #camaasil#2;
   #camaniva#10 = #0#28;
   #camaniva#11 = #0#29;
   #camaniva#12 = #camaasil#4;
   |ABRE #cacuenta;
   #cacuenta#0  = #camaasil#4;
   |LEE 010#cacuenta.=;
   |SI FSalida = 0;
      #camaniva#13 = #cacuenta#2;
   |FINSI;
   |CIERRA #cacuenta;

   #camaniva#14 = "";
   #camaniva#15 = 0;
   #camaniva#16 = 0;
   #camaniva#17 = 0;
   #camaniva#18 = "";
   #camaniva#19 = importe2;
   #camaniva#20 = 0;
   #camaniva#23 = importe3;
   #camaniva#21 = importe1;
   #camaniva#22 = "";
   |SI #caivaasi#189 = "S";
       alfa1 = #camaniva#3;
       |SI #caivaasi#98 = "S";
            alfa1 = ("000000" + alfa1) % -106;
       |FINSI;
       alfa2 = #camaniva#2; |QBF alfa2;
       |SI alfa2 ! "";
           alfa1 = alfa2 + #caivaasi#97 + alfa1;
       |FINSI;
       #camaniva#22 = alfa1;
   |FINSI;
   #camaniva#24 = #0#30;
   #camaniva#25 = #0#31;
   #camaniva#26 = 0;
   #camaniva#27 = #camaasil#6;
   #camaniva#28 = #camaasil#7;
   #camaniva#29 = #0#17;
   #camaniva#30 = #0#20;
   #camaniva#36 = "R";
   #camaniva#42 = #0#35;
   |GRABA 020#100n;

   sw = 1;

   |DDEFECTO #caivalin;
   #caivalin#0  = #camaniva#0;
   #caivalin#1  = #camaniva#1;
   #caivalin#2  = 1;
   #caivalin#3  = #0#16;
   #caivalin#4  = #0#19;
   #caivalin#5  = "S";
   #caivalin#6  = importe2;
   #caivalin#7  = #0#8;
   #caivalin#8  = importe5;
   #caivalin#9  = #0#33;
   #caivalin#10 = importe4;
   #caivalin#11 = 0;
   #caivalin#12 = #0#10;
   #caivalin#13 = #0#11;
   |SI importe4 ! 0; #caivalin#14 = #0#11; |FINSI;
   #caivalin#15 = "";
   #caivalin#16 = #0#27;
   #caivalin#30 = #0#28;
   #caivalin#31 = #0#29;
   |SI enSwAna = 1; #caivalin#28 = #0#36; |FINSI;

   |GRABA 020#caivalin.n;

   |HAZ GrabaDatosSii;
   factura = factura + 1;
   DocumNuevo = DocumNuevo + 1;
|FPRC;

|PROCESO GrabaDatosSii;
   #caivatm4#0 = enEmpresa;
   #caivatm4#1 = enPeriodo;
   #caivatm4#2 = #camaniva#0;
   #caivatm4#3 = #camaniva#1;
   |LEE 041#caivatm4.=;
   |SI FSalida ! 0;
       #caivatm4#0 = enEmpresa;
       #caivatm4#1 = enPeriodo;
       #caivatm4#2 = #camaniva#0;
       #caivatm4#3 = #camaniva#1;
       |GRABA 020#caivatm4.n;
       nAlguno = 1;
   |FINSI;
|FPRC;

|| **************************************************************
|| PROCESOS CON LOS NUMEROS DE FACTURAS
|| ___________________/ Calcular el ultimo numero de factura del libro
|PROCESO NuevoDocum;
   nLibro = #0#5;
   #camaniva#0 = nLibro;
   #camaniva#1 = 9999999999;
   |LEE 010#100];
   |SI FSalida = 0;
       |LEE 040#100a;
   |SINO;
       |LEE 040#100u;
   |FINSI;
   DocumNuevo = 1;
   |SI FSalida = 0; |Y #camaniva#0 = nLibro;
       DocumNuevo = #camaniva#1 + 1;
   |FINSI;

|| ------------------------------ |PROCESO ult_fra;
 factura = 1;
 |SI enQSerie = 0;
     |ABRE #calibros;
     #3#0 = #0#5;
     |LEE 101#calibros.=;
     |SI FSalida = 0;
         factura = #3#4;
     |FINSI;
     |LIBERA #calibros;
     |CIERRA #calibros;
 |SINO;
     |ABRE #caivases;
     #caivases#0 = #0#26;
     #caivases#1 = "R";
     |LEE 101#caivases.=;
     |SI FSalida = 0;
         factura = #caivases#3;
     |FINSI;
     |LIBERA #caivases;
     |CIERRA #caivases;
  |FINSI;
|FPRC;

|PROCESO ActNumero;
 |SI enQSerie = 0;
     |ABRE #calibros;
     #3#0 = #0#5;
     |LEE 101#calibros.=;
     |SI FSalida ! 0;
         |DDEFECTO #calibros;
         #3#0 = #0#5;
         #3#1 = "LIBRO DE VENTAS AL CONTADO";
         #3#2 = "R";
         #3#3 = 1;
         |GRABA 020#calibros.b;
     |FINSI;
     #3#4 = factura + #3#3;
     |GRABA 020#calibros.a;
     |LIBERA #calibros;
     |CIERRA #calibros;
 |SINO;
     |ABRE #caivases;
      #caivases#0 = #0#26;
      #caivases#1 = "R";
      |LEE 101#caivases.=;
      |SI FSalida ! 0;
          |DDEFECTO #caivases;
          #caivases#0 = #0#26;
          #caivases#1 = "R";
          #caivases#2 = "SERIE DE VENTAS AL CONTADO";
          #caivases#4 = 1;
          |GRABA 020#caivases.b;
      |FINSI;
      #caivases#3 = factura  + #caivases#4;
      |GRABA 020#caivases.a;
      |LIBERA #caivases;
      |CIERRA #caivases;
  |FINSI;
|FPRC;

|PROCESO LeeVentas;
  swDVentas = 1;
  |ABRE #2;
  |PARA x = 1;  |SI x [ 4;  |HACIENDO x = x + 1;
        #cavenaut#0 = 1;
        #cavenaut#1 = x;
        #cavenaut#95 = #0#28;
        |LEE 001#2=;
        |SI FSalida = 0; swDVentas = 0; |FINSI;
  |SIGUE;
  |CIERRA #2;
  |SI swDVentas = 1;
      |MENAV "     NO existen Ventas Automaticas";
      |ACABA;
  |FINSI;

   #0#28 = DVentas;
   |LEE 101#0=;
   |SI FSalida ! 0;
       |HAZ primeravez;
       #0#28 = DVentas;
       |GRABA 020#0b;
   |SINO;
        nconc = #0#15;
   |FINSI;

   |ABRE #10;
   #10#0 = #0#28;
   |LEE 001#10=;
   |SI FSalida ! 0;
       |MENAV "     NO existen Ventas Automaticas";
       |CIERRA #10;
       swDVentas = 1;
       |ACABA;
   |FINSI;
   #0#30 = #10#1;
   #0#9  = #10#2;
   |CIERRA #10;
|FPRC;
