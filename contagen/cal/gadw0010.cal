|FICHEROS;
     gadw0010;
     canempre;
     gadm0005;

     :basica/agipatro;

     patron@;
     instal@;
|FIN;

|VARIABLES;
     nRango              = 0;
     nGrid               = -1;
     n0Vnt               = 0;
     nPan                = 0;
     nCmp                = 0;
     nOk                 = 0;
     nBtns               = -1;
     nPas                = 0;

     aEsc1               = "";
     aEsc2               = "";
     aRetu               = "";
     aTecla              = "";
     aAlfa               = "";
     aDef                = "";
     aMas                = "";
     aFic                = "";
     aPath               = "";
     aBase               = "";
     aAbre               = "";
     aMarca              = "";
     aVerde              = "0,185,0;255,255,255                               ";

 {-1}aVent               = "";
     aVent1              = 0;
     aVent2              = 0;
     aVent3              = 0;
     aVent4              = 0;
     aVent5              = "";

     &enEj               = 0;
     &enError            = 0;
     &enAct              = 0;
|FIN;

|PROCESO Patron;
     #canempre#2 = #gadw0010#0;
     #canempre#3 = #gadw0010#1;
     |LEE 000#canempre.=;

     |CONSULTA_CLAVE #1#canempre;
     |SI FSalida = 0;
         #gadw0010#0 = #canempre#2;
         #gadw0010#1 = #canempre#3;

         |PINTA #gadw0010#0;
         |PINTA #gadw0010#1;

         |HAZ SacaSeries;
     |FINSI;
|FPRC;

|PROCESO gadm0005Pat;
     #patron@#17 = aMarca;

     |GRABA 020#patron@.a;
     |LIBERA #patron@;
|FPRC;

|DEFBUCLE gadm0005Pat;
     #patron@#1002;
     ;
     00, "     ";
     99, "zzzzz";
     be;
     NULL, gadm0005Pat;
|FIN;

|PROCESO ActivarTodos;
     aMarca = aVerde;
     |BUCLE gadm0005Pat;

     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO DesactivarTodos;
     aMarca = "";
     |BUCLE gadm0005Pat;

     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO Evnt;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#patron@.=;
     |FINSI;

     |SI FSalida = 4;
         |LEE 101#patron@.=;
         |SI FSalida = 0;
             |SI #patron@#17 = aVerde;
                 #patron@#17 = "";
             |SINO;
                 #patron@#17 = aVerde;
             |FINSI;

             |GRABA 020#patron@.a;
             |LIBERA #patron@;

             |REFRESCACONTROL nGrid;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO gadm0005;
     #instal@#0 = #gadm0005#0;
     #instal@#1 = #gadm0005#1;
     |LEE 000#instal@.=;
     |SI FSalida = 0;  |ACABA;  |FINSI;

     |PARA nCmp = 0;  |SI nCmp [ 16;  |HACIENDO nCmp = nCmp + 1;
           #patron@#nCmp# = #gadm0005#nCmp#
     |SIGUE;

     |GRABA 020#patron@.n;
     |LIBERA #patron@;

     nOk = 1;
|FPRC;

|DEFBUCLE gadm0005;
     #gadm0005#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, gadm0005;
|FIN;

|PROCESO InstalaPatron;
     |ABRE #gadm0005;
     |LEE 000#gadm0005.p;
     |SI FSalida = 0;
         |CIERRA #gadm0005;
         |ACABA;
     |FINSI;
     |CIERRA #gadm0005;

     aAbre = aBase + "fich/000000/gadm0005.tgz";
     |XABRE "E", aAbre, 0;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     |DESCOMPRIME aAbre;

     aAbre = aBase + "fich/000000/gadm0005.tar";
     |DETAR aAbre, aPath;
     |FBORRA aAbre;
|FPRC;

|PROCESO SacaSeries;
     |SI nBtns = -1;
         |CONTROL_SIMPLE nPan, "BOTON,Activar todos", 0870, 0883, ActivarTodos;
         |CONTROL_SIMPLE nPan, "BOTON,Desactivar todos", 0885, 0898, DesactivarTodos;
         |CONTROL_SIMPLE nPan, "LABEL,{{7}}Doble click en el registro para activa o desactivar", 0802, 0860, NULL;

         nBtns = 0;
     |FINSI;

     |SI nGrid ! -1;
         |CIERRAT #patron@;
         |DESTRUYECONTROL nGrid;
     |FINSI;

     aFic = "gadm0005" + Usuario;
     |NOME_DAT #patron@#aFic#;

     |ABRE #patron@;  |CIERRA #patron@;  |DELINDEX #patron@;

     |ABRET #patron@;

     aPath = aBase + "fich/" + (("00000" + #gadw0010#0) % -105) + #gadw0010#1 + "/";
     |PATH_DAT #gadm0005#aPath#;

     |HAZ InstalaPatron;

     nOk = 0;
     |BUCLE gadm0005;

     |SI nOk = 0;
         |MENAV "0000No hay series a incluir";
     |FINSI;

     nRango = 2 + 4 + 8 + 16 + 32 + 128;

     |LINEAL_SIMPLE #1#patron@, nRango, 0902, 3698, NULL, NULL, Evnt;
     nGrid = FSalida;
|FPRC;

|PROCESO gadm0005Act;
     |SI nPas = 0;
         nOk = 1;
         |ERROR10;
         |ACABA;
     |FINSI;

     |PARA nCmp = 0;  |SI nCmp [ 16;  |HACIENDO nCmp = nCmp + 1;
           #instal@#nCmp#  = #patron@#nCmp#;
           #gadm0005#nCmp# = #patron@#nCmp#;
     |SIGUE;

     #instal@#3  = enAct;
     #gadm0005#3 = enAct;

     |GRABA 020#instal@.n;
     |LIBERA #instal@;

     enEj = #48#26;
     |HAZ GrabaSerieConta;
     |HAZ AltaDSCOMER_Auto;
|FPRC;

|DEFBUCLE gadm0005Act;
     #patron@#1002;
     17;
     00, "     ", aVerde;
     99, "zzzzz", aVerde;
     ;
     NULL, gadm0005Act;
|FIN;

|PROGRAMA;
     |HAZ PideComerAuto;
     |SI enError = 1;
         |ACABA;
     |FINSI;

     |VENTANA 0501, 3799, -1, 16, "Copiar series de patrones";
     n0Vnt = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n0Vnt;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PINPA #0#gadw0010;
     nPan = FSalida;

     #gadw0010#0 = #agipatro#1;
     #gadw0010#1 = #agipatro#2;
     |PINDA #0#gadw0010;

     |CONTROL_SIMPLE nPan, "BOTON,{{197}}", 0618, 0620, Patron;

     |SI #gadw0010#0 ! 0;
         |HAZ SacaSeries;

         FSalida = "::{{001}}Salir," + nGrid;
     |SINO;
         FSalida = "::{{001}}Salir";
     |FINSI;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     nOk  = 0;
     nPas = 0;  |BUCLE gadm0005Act;
     |SI nOk = 0;
         |MENAV "0000No hay series a incluir.";
     |SINO;
         aAlfa = "0000SIncluimos las series seleccionadas en la actividad ";
         aAlfa = aAlfa + "y grabamos las series en gesti¢n";
         |CONFI aAlfa;
         |SI FSalida = 0;
             nPas = 1;  |BUCLE gadm0005Act;
         |SINO;
             |MENAV "0000Proceso cancelado.";
         |FINSI;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n0Vnt;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n0Vnt;
|FPRO;

|PROCESO T80;  |TIPO 80;
     |ABRE #agipatro;
     |LEE 000#agipatro.p;
     |SI FSalida ! 0;
         |DDEFECTO #agipatro;
         |GRABA 020#agipatro.n;
     |FINSI;
     |CIERRA #agipatro;

     |ABRE #canempre;

     |DDEF aDef;
     aMas = aDef + "gadm0005.mas";
     |CARGA_DEF aMas, patron@;
     |CARGA_DEF aMas, instal@;

     |DBASE aBase;

     |ABRET #instal@;
|FPRC;

|PROCESO T90;  |TIPO 90;
     |ABRE #agipatro;
     |LEE 000#agipatro.p;
     |SI FSalida = 0;
         #agipatro#1 = #gadw0010#0;
         #agipatro#2 = #gadw0010#1;
         |GRABA 020#agipatro.a;
         |LIBERA #agipatro;
     |FINSI;
     |CIERRA #agipatro;
     |CIERRA #canempre;

     |SI nGrid ! -1;
         |CIERRAT #patron@;
         |DELINDEX #patron@;
     |FINSI;

     |CIERRAT #instal@;

     |DESCARGA_DEF #patron@;
     |DESCARGA_DEF #instal@;
|FPRC;
