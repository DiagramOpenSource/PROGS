|FICHEROS;
   cacambi1 #0;
   cacuenta #10;   || plan contable

   canempre #30;
   cacambiw #90;   || def. trabajo
   cacambix #91;   || tmp. trabajo
|FIN;

|VARIABLES;
   alfa1 = "";
   alfa2 = "";
   alfa3 = "";
   alfa4 = "";
   alfa5 = "";
   nume1 = 0;
   nume2 = 0;
   nume3 = 0;
   nume4 = 0;
   para1 = 0;
   para2 = 0;
   para3 = 0;
   para4 = 0;
   fech1 = @;
   fech2 = @;
   fech3 = @;
   fech4 = @;
   elsw1 = 0;
   elsw2 = 0;
   elsw3 = 0;
   elsw4 = 0;
   FEstado = 0;
   seleccio = 0;
   modo = 0;
   sw_rup = 0;

   a_empres = "";
   d_empres = "";
   a_nivels = 0;
   d_nivels = 0;
   longi = 0;
   listo = 0;
   a_cuentas = 0;
   d_cuentas = 0;
   n_cuentas = 0;

   dire = "";
   buf_dir = 0;
   ele_dir = 0;
   fiche = "";
   traba1 = "";
   traba = "";
   &r_emp = 0;
   &r_per = 0;
   varcamp = 0;
   temporal1 = "";
   temporal2 = "";
|FIN;

|INCLUYE dscont_i;
____________________________________/ CALCULOS DE PANTALLA

|PROCESO consulta;
   |SI FSalida ! 9;  |ACABA;  |FINSI;
   r_emp = 0;
   |SUB_EJECUTA "caconemp.run";
   |ERROR;
   |SI r_emp = 0; |ACABA; |FINSI;
   #0Campo  = r_emp;
   varcamp  = Campo + 1;
   #0varcamp = r_per;
   |PTEC 802;
   |PTEC 802;
|FPRC;


|PROCESO existe; |TIPO 0;
   |SI FSalida = 2;  |ACABA;  |FINSI;

   |DFICB alfa1;
   |QBF alfa1;
   |HAZ lee_actual;
   |SI #canempre#2 ! #cacambi1#0;|O #canempre#3 ! #cacambi1#1;
      |ABRE #30;
      #canempre#2 = #cacambi1#0;
      #canempre#3 = #cacambi1#1;
      |LEE 000#30=;
      |SI FSalida ! 0;
         |MENAV "    Empresa/Ejercicio no definida ...";
         |ERROR;
      |SINO;
         a_empres = #canempre#0;
         a_empres = ("000000" + a_empres) % -106;
         |PARA para1 = 0; |SI para1 [ 7; |HACIENDO para1 = para1 + 1;
               para2 = para1 + 13;
               #cacambiw#para1# = #canempre#para2#;
         |SIGUE;
         a_nivels = #cacambiw#0;
         |ATRI I;  |PINTA 1007, #canempre#1;  |ATRI 0;
      |FINSI;
      |CIERRA #30;
   |SINO;
      |MENAV "    Empresa Actual ...";
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO lee_actual;
   |DFICE alfa1;
   |QBF alfa1;
   alfa1 = alfa1 % -107;
   alfa2 = alfa1 % 105;      || empresa
   alfa3 = alfa1 % 601;      || ejercicio

   |ABRE #30;
   #canempre#2 = alfa2;
   #canempre#3 = alfa3;
   |LEE 020#30=;        || tiene que estar!
   |CIERRA #30;

   d_empres = #canempre#0;
   d_empres = ("0000000" + d_empres) % -106;
   |PARA para1 = 10; |SI para1 [ 16; |HACIENDO para1 = para1 + 1;
      para2 = para1 + 3;
      #cacambiw#para1# = #canempre#para2#;
   |SIGUE;
   d_nivels = #cacambiw#10 + 10;
|FPRC;
____________________________________/
|PROCESO caso3;

  |SI listo ! 0;  |ACABA;  |FINSI;

  nume1 = #cacuenta#1;                                                    || si es el ultimo nivel
  |SI #cacuenta#1 = a_nivels; |Y #cacambiw#nume1# ] #cacambiw#d_nivels#;  ||/y los digitos del
      nume2 = #cacambiw#nume1# - #cacambiw#d_nivels#;                     ||/ultimo nivel destino
      nume2 = (5 + nume2) * 100 + 12;

      alfa2 = #cacuenta#0 % 104;
      alfa3 = #cacuenta#0 % nume2;
      alfa2 = alfa2 + alfa3; |QBF alfa2;

      |DDEFECTO #91;
      #cacambix#0 = #cacuenta#0;         || cuenta actual
      #cacambix#1 = #cacuenta#1;         || nivel actual
      #cacambix#2 = alfa2;               || cuenta destino
      #cacambix#3 = d_nivels - 10;       || nivel destino
      d_cuentas   = d_cuentas + 1;
      #cacambix#4 = d_cuentas + 10000;
      #cacambix#5 = #cacuenta#2;         || nombre cuenta

      #cacambix#6 = #cacambix#2;
      |GRABA 020#91n;
      listo = 1;
   |FINSI;
|FPRC;

|PROCESO caso2;
  |SI listo ! 0;  |ACABA;  |FINSI;
  nume1 = #cacuenta#1;                                   || si es el ultimo nivel
  |SI #cacuenta#1 = a_nivels; |Y #cacambiw#nume1# < #cacambiw#d_nivels#;  ||/y los digitos del
      nume2 = #cacambiw#d_nivels# - #cacambiw#nume1#;              ||/ultimo nivel destino
      alfa1 = "0" * nume2;                         ||/son mayores
      alfa2 = #cacuenta#0;    |QBF alfa2;
      |DDEFECTO #91;
      #cacambix#0 = #cacuenta#0;         || cuenta actual
      #cacambix#1 = #cacuenta#1;         || nivel actual
      #cacambix#2 = alfa2 + alfa1; || cuenta destino
      #cacambix#3 = d_nivels - 10; || nivel destino
      d_cuentas = d_cuentas + 1;
      #cacambix#4 = d_cuentas + 10000;
      #cacambix#5 = #cacuenta#2;         || nombre cuenta

      #cacambix#6 = #cacambix#2;
      |GRABA 020#91n;
      listo = 1;
   |FINSI;
|FPRC;

|PROCESO caso1;
 |SI listo ! 0;  |ACABA;  |FINSI;
 |PARA para1 = 11; |SI para1 [ 16; |HACIENDO para1 = para1 + 1;
       |SI longi = #cacambiw#para1#;
           |DDEFECTO #91;
           #cacambix#0 = #cacuenta#0;          || cuenta actual
           #cacambix#1 = #cacuenta#1;          || nivel actual
           #cacambix#2 = #cacuenta#0;          || cuenta destino
           #cacambix#3 = para1 - 10;     || nivel destino
           d_cuentas = d_cuentas + 1;
           n_cuentas = n_cuentas + 1;
           #cacambix#4 = n_cuentas;
           #cacambix#5 = #cacuenta#2;          || nombre cuenta

           #cacambix#6 = #cacambix#2;
           |GRABA 020#91n;
           listo = 1;
           |SAL;
       |FINSI;
  |SIGUE;
|FPRC;

|PROCESO cacuenta1;
   alfa1 = #cacuenta#0;  |QBF alfa1;
   alfa2 = alfa1 % 0;
   longi = alfa2;
   listo = 0;      || switch para saber si la cuenta esta cambiada

   |HAZ caso1;         || cambia nivel, no cambia longitud
                       ||/no cambia nivel, no cambia longitud

   |HAZ caso2;         || cambia nivel, cambia longitud
                       ||/no cambia nivel, cambia longitud

   |HAZ caso3;         || cambia nivel, cambia longitud
                       ||/no cambia nivel, cambia longitud menor
|FPRC;

|DEFBUCLE cacuenta;
   #10#1;
   ;
   "            ";
   "zzzzzzzzzzzz";
   ;
   NULL, cacuenta1;
|FIN;
____________________________________/
|PROCESO registros1;
   a_cuentas = a_cuentas + 1;
|FPRC;

|DEFBUCLE registros;
   #10#1;
   ;
   "            ";
   "zzzzzzzzzzzz";
   ;
   NULL, registros1;
|FIN;
____________________________________/ CALCULO PRINCIPAL Y COPIA DE EMPRESA
|PROCESO copia_empresa;
   |DFICB alfa1;
   alfa2 = alfa1 + a_empres;
   alfa3 = alfa1 + d_empres;

   alfa2 = alfa2 + "/"; || borrando el fichero de preparacion de la empresa
   alfa3 = alfa3 + "/"; ||/origen no se machacara el que pudiera existir

   |PATH_DAT #cacambix #alfa2#;     ||/en la empresa actual
   |DELINDEX #91;
   |PATH_DAT #cacambix #alfa3#;

   |DBASS dir_fich;
   |DBASE aNOMAPLIC;
   aNOMAPLIC = aNOMAPLIC - dir_fich;
   aNOMAPLIC = aNOMAPLIC - "/";
   aNOMAPLIC = aNOMAPLIC + ".dir";

   dire = "";
   |DFICB dire;
   fiche = dire + aNOMAPLIC;
   |LEESECUENCIAL fiche, buf_dir, ele_dir, 1, 0;
   |SI ele_dir > 0;
      |PARA para1 = 1; |SI para1 [ ele_dir; |HACIENDO para1 = para1 + 5;
         para2 = para1 + 3;
         |DEL_BUF buf_dir, para2, alfa1;
         alfa1 = alfa1 % 101;
         |SI alfa1 = "1";|O alfa1 = "2";
            |DEL_BUF buf_dir, para1, alfa1;
            traba1 = alfa1;
            |HAZ copiando;
         |FINSI;
      |SIGUE;
   |SINO;
      alfa1 = "    [" + fiche + "] NO accesible en lectura.";
      |MENAV alfa1;
   |FINSI;
   |FINDIM buf_dir;
|FPRC;

|PROCESO copiando;
   traba = " COPIANDO FICHERO: " + traba1 + "        ";
   |PINTA 2211, traba;
   temporal1 = alfa2 +  traba1 + ".ixx";
   temporal2 = alfa3 + traba1 + ".ixx";
   |COPIA_FICHERO temporal1,temporal2;
   |SI FSalida ! 0;  |ACABA;  |FINSI;
   temporal1 = alfa2 + traba1 + ".dat";
   temporal2 = alfa3 + traba1 + ".dat";
   |COPIA_FICHERO temporal1,temporal2;
   |SI FSalida ! 0;
      temporal2 = alfa3 + traba1 + ".ixx";
      |FBORRA temporal2;
      |ACABA;
   |FINSI;
   temporal1 = alfa2 + traba1 + ".ddx";
   temporal2 = alfa3 + traba1 + ".ddx";
   |COPIA_FICHERO temporal1,temporal2;
|FPRC;

|PROCESO tipo3; |TIPO 3;
   |SI #cacambi1#2 ! "SI";  |ACABA;  |FINSI;
   |HAZ copia_empresa;
   |SI #cacambi1#3 = "N";
      |ATRI P;  |MENSA "    Preparando ...";    |ATRI 0;
      |DELINDEX #91;
      |ABRE #91;
      |BUCLE registros;
      |BUCLE cacuenta;
      alfa1 = "    Cuentas Origen: " + a_cuentas + " <> Cuentas Destino: " + d_cuentas;
      |MENAV alfa1;
      |CIERRA #91;
      |ATRI R;  |BLIN 4;                      |ATRI 0;
   |FINSI;
|FPRC;
