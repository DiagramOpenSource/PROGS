|FICHEROS;
   caz00023 #0;

   camaniva #10;
   caivalin #11;
   camaasil #12;

   caivaasi #20;

   dscam043@ #100;
   Recibos@  #101;
|FIN;

|VARIABLES;
   nCalc = 0;
   nBase = 0;

   nDiario = 0;
   fFecha  = @:
   nDocum  = 0;
   nAsto   = 0;
   aAcum   = "";
   nLinea  = 0;
   nSwCli  = 0;
   nSwIva  = 0;
   nDAcum  = 0;
   nHAcum  = 0;
   nConcepto = 0;

   nSwErr    = 0;
   nSwError  = 0;

   aAlfa  = "";
   aAlfa1 = "";
|FIN;

|PROCESO Asientos;
   |SALVA_FICHA #camaasil;
   |SI #camaasil#19 = "B";
      |SI #camaasil#20 ! 0;
         nCalc = 0;
         nDiario = #camaasil#0;
         fFecha  = #camaasil#1;
         nDocum  = #camaasil#2;
         nAsto   = #camaasil#3;
         nBase   = #camaasil#20;
         aAcum   = #camaasil#19;
         nAsto   = 1;
         |LEE 000#camaasil.];
         |PARA; |SI FSalida = 0; |Y nDiario = #camaasil#0; |Y fFecha  = #camaasil#1; |Y nDocum  = #camaasil#2; |HACIENDO;
            |SI nBase = #camaasil#20; |Y aAcum   = #camaasil#19;
               nCalc = nCalc + #camaasil#9;
            |FINSI;
            |LEE 000#camaasil.s;
         |SIGUE;
         #camaasil#0 = nDiario;
         #camaasil#1 = fFecha;
         #camaasil#2 = nDocum;
         #camaasil#3 = nAsto;
         |LEE 000#camaasil.=;

         #caivalin#0 = #camaniva#0;
         #caivalin#1 = #camaniva#1;
         #caivalin#2 = #camaasil#20;
         |LEE 101#caivalin.=;
         |SI FSalida = 0;
            |SI #caivalin#6 ! nCalc;
               #caivalin#6 = nCalc;
               |GRABA 020#caivalin.a; |LIBERA #caivalin;
            |FINSI;
         |FINSI;
      |SINO;
         nCalc = 0;
         #caivalin#0 = #camaniva#0;
         #caivalin#1 = #camaniva#1;
         #caivalin#2 = 1;
         |LEE 000#caivalin.=;
         |PARA; |SI FSalida = 0; |Y #caivalin#0 = #camaniva#0; |Y #caivalin#1 = #camaniva#1; |HACIENDO;
            nCalc = nCalc + #caivalin#6;
            nBase = #caivalin#2;
            |LEE 000#caivalin.s;
         |SIGUE;
         |SI nCalc ! #camaasil#9;
            nCalc = nCalc - #camaasil#9;
            #caivalin#0 = #camaniva#0;
            #caivalin#1 = #camaniva#1;
            #caivalin#2 = nBase;
            |LEE 101#caivalin.=;
            |SI FSalida = 0;
               #caivalin#6 = #caivalin#6 - nCalc;
               |GRABA 020#caivalin.a; |LIBERA #caivalin;
            |FINSI;
         |FINSI;
      |FINSI;
   |FINSI;
   |SI #camaasil#19 = "I";
      |SI #camaasil#20 ! 0;
         #caivalin#0 = #camaniva#0;
         #caivalin#1 = #camaniva#1;
         #caivalin#2 = #camaasil#20;
         |LEE 101#caivalin.=;
         |SI FSalida = 0;
            |SI #caivalin#8 ! #camaasil#9;
               #caivalin#8 = #camaasil#9;
               |GRABA 020#caivalin.a; |LIBERA #caivalin;
            |FINSI;
         |FINSI;
      |SINO;
         nCalc = 0;
         #caivalin#0 = #camaniva#0;
         #caivalin#1 = #camaniva#1;
         #caivalin#2 = 1;
         |LEE 000#caivalin.=;
         |PARA; |SI FSalida = 0; |Y #caivalin#0 = #camaniva#0; |Y #caivalin#1 = #camaniva#1; |HACIENDO;
            nCalc = nCalc + #caivalin#8;
            nBase = #caivalin#2;
            |LEE 000#caivalin.s;
         |SIGUE;
         |SI nCalc ! #camaasil#9;
            nCalc = nCalc - #camaasil#9;
            #caivalin#0 = #camaniva#0;
            #caivalin#1 = #camaniva#1;
            #caivalin#2 = nBase;
            |LEE 101#caivalin.=;
            |SI FSalida = 0;
               #caivalin#8 = #caivalin#8 - nCalc;
               |GRABA 020#caivalin.a; |LIBERA #caivalin;
            |FINSI;
         |FINSI;
      |FINSI;
   |FINSI;
   |REPON_FICHA #camaasil;
|FPRC;

|DEFBUCLE Asientos;
 #camaasil#5;
 ;
 #camaniva#0, #camaniva#2, #camaniva#3;
 #camaniva#0, #camaniva#2, #camaniva#3;
 ;
 NULL, Asientos;
|FIN;

|PROCESO RevisaLineas;
   #camaniva#19 = #camaniva#19 + #caivalin#6;    || Bases
   #camaniva#23 = #camaniva#23 + (#caivalin#8 + #caivalin#10); || Cuotas
   #camaniva#20 = #camaniva#20 + #caivalin#22;   || Cuota IRPF
   #camaniva#26 = #camaniva#26 + #caivalin#11;   || Descuentos
   |SI #camaniva#36 = "N";
       #camaniva#21 = #camaniva#19 + #camaniva#23 - #camaniva#26 - #camaniva#20;
   |SINO;
       #camaniva#21 = #camaniva#19 + #camaniva#23 - #camaniva#20;
   |FINSI;
   |SI nLinea = 0;
       |SI #caz00023#5 = "S"; |Y #camaniva#27 ! #caivalin#3;
           #camaniva#27 = #caivalin#3;
           nSwCli = 1;
       |FINSI;
       |SI #caz00023#6 = "S"; |Y #camaniva#29 ! #caivalin#3;
           #camaniva#29 = #caivalin#3;
           nSwIva = 1;
       |FINSI;
   |FINSI;
   nLinea = 1;
|FPRC;

|DEFBUCLE RevisaLineas;
    #caivalin#1;
    ;
    #camaniva#0,#camaniva#1,001;
    #camaniva#0,#camaniva#1,999;
    be;
    NULL,RevisaLineas;
|FIN;

|PROCESO CambiaApte;
  #camaasil#6 = nConcepto;
  |GRABA 020#camaasil.a;
  |LIBERA #camaasil;
|FPRC;

|DEFBUCLE CambiaApte;
  #camaasil#1;
  19,20;
  #camaniva#7, #camaniva#8, #camaniva#9, 0001, aAcum, nDAcum;
  #camaniva#7, #camaniva#8, #camaniva#9, 9999, aAcum, nHAcum;
  be;
  NULL, CambiaApte;
|FIN;

|PROCESO BuscaRecs;
   |SI #caivaasi#1 = "N"; |ACABA; |FINSI;
   |SI #camaniva#36 ! "R"; |Y #camaniva#36 ! "S"; |ACABA; |FINSI;

   aAlfa = #caivaasi#56; |QBF aAlfa;
   aAlfa1 = aAlfa + "def/dscam043.mas";
   |CARGA_DEF aAlfa, dscam043@;
   |SI FSalida = 0;
      aAlfa1 = aAlfa + "fich/"; |PATH_DAT #dscam043@#aAlfa1#;
      |ABRE #dscam043@;
      #dscam043@#0 = #48#2;
      #dscam043@#1 = #48#3;
      #dscam043@#2 = 1;
      |LEE 000#dscam043@.];
      |PARA; |SI FSalida = 0; |Y #dscam043@#0 = #48#2; |Y #dscam043@#1 = #48#3; |HACIENDO;
         nSwError = 0;
         |SI #camaniva#36 = "R"; |Y #dscam043@#3 = "C"; nSwError = 1; |FINSI;
         |SI #camaniva#36 = "S"; |Y #dscam043@#3 = "V"; nSwError = 1; |FINSI;

         |SI nSwError = 0;
            aAlfa1 = aAlfa + "def/";
            |SI #camaniva#36 = "R"; aAlfa1 = aAlfa1 + "dscam003.mas"; |FINSI;
            |SI #camaniva#36 = "S"; aAlfa1 = aAlfa1 + "dscam008.mas"; |FINSI;
            |CARGA_DEF aAlfa1, Recibos@;
            |SI FSalida = 0;
               aAlfa1 = aAlfa + "fich/" + (("00000" + #dscam043@#9) % -105) + "/";
               |PATH_DAT #Recibos@#aAlfa1#;
               |ABRE #Recibos@;
               |SI #camaniva#36 = "R"; |SELECT #9#Recibos@; |FINSI;
               |SI #camaniva#36 = "S"; |SELECT #2#Recibos@; |FINSI;
               #Recibos@#27 = #camaniva#0;
               #Recibos@#0  = #camaniva#2;
               #Recibos@#1  = #camaniva#3;
               #Recibos@#2  = 1;
               #Recibos@#40 = 1;
               |LEE 000#Recibos@.=;
               |PARA; |SI FSalida = 0; |Y #Recibos@#27 = #camaniva#0; |Y #Recibos@#0 = #camaniva#2;
                       |Y #Recibos@#1 = #camaniva#3; |HACIENDO;
                  |SI #Recibos@#4 = #camaniva#4;
                     |SI #Recibos@#14 ! "P"; nSwErr = 1; |SAL; |FINSI;
                  |FINSI;
                  |LEE 000#Recibos@.s;
               |SIGUE;
               |CIERRA #Recibos@; |DESCARGA_DEF #Recibos@;
            |FINSI;
         |FINSI;
         |SI nSwErr = 1; |SAL; |FINSI;

         |LEE 000#dscam043@.s;
      |SIGUE;
      |CIERRA #dscam043@; |DESCARGA_DEF #dscam043@;
   |FINSI;
|FPRC;

|PROCESO Facturas;
|| ..   |BUCLE Asientos;
   nLinea = 0;
   nSwCli = 0;
   nSwIva = 0;
   #camaniva#19 = 0; #camaniva#20 = 0; #camaniva#21 = 0;
   #camaniva#23 = 0; #camaniva#26 = 0;
   |BUCLE RevisaLineas;
   |SI nLinea ! 0;
       |GRABA 020#camaniva.a;

       |SI nSwCli = 1;
           nConcepto = #camaniva#27;
           aAcum = "F"; nDAcum = 00; nHAcum = 00; |BUCLE CambiaApte;
           aAcum = " "; nDAcum = 00; nHAcum = 00; |BUCLE CambiaApte;
       |FINSI;
       |SI nSwIva = 1;
           nConcepto = #camaniva#29;
           aAcum = "I"; nDAcum = 00; nHAcum = 99; |BUCLE CambiaApte;
           aAcum = "i"; nDAcum = 00; nHAcum = 99; |BUCLE CambiaApte;
       |FINSI;
   |SINO;
       |SI #caz00023#4 = "S";
          nSwErr = 0;
          |HAZ BuscaRecs;
          |SI nSwErr = 0;
             |BORRA 020#camaniva.a;
          |SINO;
             aAlfa = "    Recibos con movimientos. La factura " + #camaniva#2 + "/" + #camaniva#3 + " no se eliminar ";
             |MENAV aAlfa;
          |FINSI;
       |FINSI;
   |FINSI;
   |LIBERA #camaniva;
|FPRC;

|DEFBUCLE Facturas;
   #camaniva#3;
   ;
   00, #caz00023#0, #caz00023#2;
   99, #caz00023#1, #caz00023#3;
   be;
   NULL, Facturas;
|FIN;

|PROGRAMA;
   |SI PARAMETRO ! "";
       |DDEFECTO #caz00023;
       |ABRE #caivalin;
       |BUCLE Facturas;
       |CIERRA #caivalin;
       |ACABA;
   |FINSI;

   |CLS;
   |CABEZA " Revision cuadre asientos / ivas ";

   |ABRE #caivaasi;
   |LEE 000#caivaasi.p;
   |SI FSalida ! 0; |DDEFECTO #caivaasi; |FINSI;
   |CIERRA #caivaasi;

   |PINPA #0#0; |PINDA #0#0;

   |ENDATOS #1#0;
   |SI FSalida = 0;
      |ABRE #caivalin;
      |BUCLE Facturas;
      |CIERRA #caivalin;
   |FINSI;
|FPRO;
