|FICHEROS;
   caut0016 #0;

   canempre #30;
   canempr@ #330;
|FIN;

|VARIABLES;
   nPara1   = 0;
   nNume1   = 0;
   aAlfa1   = "";
   aAlfa2   = "";
   aMensa   = "";
   nEmp     = 0;
   nPer     = 0;
   nCampo   = 0;
   nInkey   = 0;
   nSelError  = 0;
   aPath    = "";
   aDef     = "";

   aParam     = "";
   fich_copi  = "";
   traba      = "";
   traba1     = "";
   temporal1  = "";
   temporal2  = "";
|FIN;

|INCLUYE dscont_i;

|| *******************************************************************
|PROCESO DefectosA; |TIPO 7;
    #0#2  = enPeriodo;   |PINTA #0#2;
    |HAZ DefectosN;
|FPRC;

|PROCESO DefectosN;
    #0#4  = #0#2 + 1; |SI #0#4 = 10; #0#4 = 0; |FINSI;
    |PINTA #0#4;
|FPRC;

|PROCESO CopioStandar; |TIPO 0;
    aAlfa1 = "S";
    |SI #0#31 = "S";  aAlfa1 = "N"; |FINSI;
    |C_M #0#2,1,aAlfa1;
|FPRC;

|PROCESO Select; |TIPO 0;
  |SI #0#0 = "S";
      |C_M #0#1,1,"S";
      |C_M #0#3,1,"S";
      |PARA nPara1 = 5; |SI nPara1 [ 27; |HACIENDO nPara1 = nPara1 + 2;
            nNume1 = nPara1 + 1;
            |C_M #0nPara1,1,"N"; #0nPara1 = 0;  |PINTA #0nPara1;
                                 #0nNume1 = ""; |PINTA #0nNume1;
      |SIGUE;
   |SINO;
      |C_M #0#1,1,"N"; #0#1 = 1; |PINTA #0#1;
      |C_M #0#3,1,"N"; #0#3 = 99999; |PINTA #0#3;
      |PARA nPara1 = 5; |SI nPara1 [ 27; |HACIENDO nPara1 = nPara1 + 2;
            |C_M #0nPara1,1,"S";
      |SIGUE;
   |FINSI;
|FPRC;

|CALCULO MiraEmpresa; |TIPO 0;
   nInkey = FSalida;
   |SI nInkey = 2; |ACABA; |FINSI;

   |SI #0Campo = 0; |ACABA; |FINSI;

   |SI #0#0 = "N";
       nEmp = #0Campo;
       nPer = #0#2;
       |HAZ SeldeEmpresa;
       |SI nSelError ! 0;
           |MENAV aMensa;
           |ERROR;
           |ACABA;
       |FINSI;
       nCampo = Campo + 1;
       #0nCampo = #30#1;  |PINTA #0nCampo;
   |FINSI;
|FCAL;

|CALCULO NoMasEmp; |TIPO 0;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 2;  |SI nCampo [ 27;  |HACIENDO nCampo = nCampo + 2;
               nNume1 = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
               #0nNume1 = ""; |PINTA #0nNume1;
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 2;  |SI nCampo [ 27;  |HACIENDO nCampo = nCampo + 2;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|PROCESO SeldeEmpresa;
   |ABRE #30;
   aMensa    = "";
   nSelError = 0;
   #30#2 = nEmp;
   #30#3 = nPer;
   |LEE 010#30=;
   |SI FSalida = 0;
       |SI #0#29 ! "T";
           |SI #30#21 ! #0#29;
               |SI #30#21 = "I";
                   aMensa = "    Empresa Inactiva";
               |SINO;
                   aMensa = "    Empresa Activa";
               |FINSI;
               nSelError = 1;
               |VETE Cierra;
           |FINSI;
       |FINSI;
   |SINO;
       nSelError  = 1;
       aMensa = "    No Existe Empresa ";
   |FINSI;

|ET Cierra;
   |CIERRA #30;
|FPRC;

|| ::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
|PROCESO LasCreo;

   |SI #0#29 ! "T";
       |SI #330#21 ! #0#29;
           |ACABA;
       |FINSI;
   |FINSI;

   |ATRI P;
   |PINTA 2203, "Copiando fichero cuentas de partidas pendientes ... ";
   |ATRI 0;

   |PINTA 2312, #330#2;
   |PINTA 2319, #330#3;
   |PINTA 2322, #330#1;
   |PINTA 2375, #330#26;


   aAlfa2 = #330#0;  aAlfa2 = "000000" + aAlfa2;  aAlfa2 = aAlfa2 % -106;
   fich_alta = aPath + aAlfa2;
   fich_alta = fich_alta + "/";

   |SI #0#31 = "N";
       #30#2 = #330#2;
       #30#3 = #0#2;
       |LEE 041#30=;
       |SI FSalida ! 0;   |ACABA; |FINSI;

       aAlfa2 = #30#0;  aAlfa2 = "000000" + aAlfa2;  aAlfa2 = aAlfa2 % -106;
       fich_copi = aPath + aAlfa2 + "/";

   |FINSI;

   traba1 = "cacamctl";  |HAZ copiando;
|FPRC;

|DEFBUCLE LasCreo;
   #330#1002;
   ;
   #0#1, #0#4;
   #0#3, #0#4;
   e;
   NULL, LasCreo;
|FIN;

|| ::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
|CALCULO VoyaAbrir;

  |DBASE aBase;
  aDef = aBase + "def/canempre.mas";
  |CARGA_DEF aDef, canempr@;
  |SI FSalida ! 0;
      |MENAV "    Error cargando fichero de Empresas.";
      |ACABA;
  |FINSI;

  |DFICB aPath;
  |SI #0#31 = "S";
       fich_copi = aPath + "000000/";
  |FINSI;

  |BLIN 21;
  |BLIN 22;
  |BLIN 23;
  |CUADRO 2101 2480;
  |ATRI P;
  |PINTA 2303, "Empresa: ";
  |PINTA 2364, "Ejercicio: ";
  |ATRI 0;

  |ABRE #30;
  |SI #0#0 = "S";
      |BUCLE LasCreo;
  |SINO;
      |ABRE #330;
      |PARA nPara1 = 5; |SI nPara1 [ 27; |HACIENDO nPara1 = nPara1 + 2;
            |SI #0nPara1 ! 0;
                #330#2 = #0nPara1;
                #330#3 = #0#4;
                |LEE 011#330=;
                |SI FSalida = 0;
                    |HAZ LasCreo;
                |FINSI;
            |FINSI;
      |SIGUE;
      |CIERRA #330;
   |FINSI;
   |CIERRA #30;

   |DESCARGA_DEF #canempr@;
|FCAL;
||*********************************************************************

|PROGRAMA;
   aParam = PARAMETRO;
   |SI aParam = "";
       |HAZ inicio;
   |FINSI;

   |CLS;
   |PINPA #0#0;
   |SI aParam ! "";
       #0#31 = aParam;
       #0#1  = enEmpresa;
       #0#2  = enPeriodo;
       #0#3  = enEmpresa;
       #0#4  = #0#2 + 1; |SI #0#4 = 10; #0#4 = 0; |FINSI;
   |FINSI;

   |PINDA #0#0;

   |ENDATOS #1#0;
   |SI FSalida = 0; |Y #0#30 = "S";
       |HAZ VoyaAbrir;
   |FINSI;
|FPRO;

|PROCESO copiando;
  traba = " COPIANDO FICHERO: " + traba1 + "        ";

  |PINTA 2201, traba;

  temporal1 = fich_copi + traba1 + ".ixx";
  temporal2 = fich_alta + traba1 + ".ixx";
  |COPIA_FICHERO temporal1,temporal2;
  |SI FSalida ! 0;  |ACABA;  |FINSI;
  temporal1 = fich_copi + traba1 + ".dat";
  temporal2 = fich_alta + traba1 + ".dat";
  |COPIA_FICHERO temporal1,temporal2;
  |SI FSalida ! 0;
      temporal2 = fich_alta + traba1 + ".ixx";
      |FBORRA temporal2;
      |ACABA;
  |FINSI;
  temporal1 = fich_copi + traba1 + ".ddx";
  temporal2 = fich_alta + traba1 + ".ddx";
  |COPIA_FICHERO temporal1,temporal2;
|FPRC;
