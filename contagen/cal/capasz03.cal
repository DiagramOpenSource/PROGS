|FICHEROS;
  capasz03 #0;
  cacuenta #10;
  :/modelos/def/dsmom100 #100;
  canempre #30;

  cacambiw #90;   || def. trabajo
  cacambix #91;   || tmp. trabajo
|FIN;

|VARIABLES;

 nSwError = 0;
 error1   = "0000Nivel repetido o valor mas bajo que los anteriores";
 aAlfa1   = "";
 aAlfa2   = "";
 aAlfa3   = "";
 aAlfa4   = "";
 nPara1   = 0;
 nNume1   = 0;
 nNume2   = 0;
 nNume3   = 0;
 nNume4   = 0;
 nNume5   = 0;
 aFichero   = "";
 aDirInicio = "";
 aDirBase   = "";
 &eaDirFich = "";
 nUlt1      = 0;
 nUlt2      = 0;
 aCambio    = "";
 Comodin    = "";
 Comodin1   = "";
 nLongi     = 0;
 nListo     = 0;
 nA_nivels  = 0;
 nD_nivels  = 0;
 nA_cuentas = 0;
 nD_cuentas = 0;
  {-1}punt1 = 0;

 &eSwCambio = 0;
 &eaPreg22_1 = "";
 &eaPreg22_2 = "";
 &eaPreg22_6 = "";
 &eaPreg22_8 = "";
 &eSwCambioEsp  = 0;
  nSoloEspecial = 0;

  aPaDic        = "";
|FIN;

|INCLUYE dscont_i;

|| **********************************************************************
|| ***** Calculos de Pantalla
|| **********************************************************************
|CALCULO nivel;|TIPO 7;
   |C_M #0#23, 1,"S";  |C_M #0#24, 1,"S";
   |C_M #0#25, 1,"S";  |C_M #0#26, 1,"S";
   |C_M #0#27, 1,"S";  |C_M #0#28, 1,"S";

   |SI #0#22 < 6;
      |C_M #0#28, 1,"N"; #0#28 = 0; |PINTA #0#28;
      |SI #0#22 < 5;
          |C_M #0#27, 1,"N"; #0#27 = 0; |PINTA #0#27;
          |SI #0#22 < 4;
              |C_M #0#26, 1,"N"; #0#26 = 0; |PINTA #0#26;
              |SI #0#22 < 3;
                  |C_M #0#25, 1, "N"; #0#25 = 0; |PINTA #0#25;
                  |SI #0#22 < 2;
                      |C_M #0#24, 1,"N"; #0#24 = 0; |PINTA #0#24;
                  |FINSI;
              |FINSI;
           |FINSI;
      |FINSI;
   |FINSI;
|FCAL;

|CALCULO nivelrep; |TIPO 0;
   |SI Campo = 24;  |Y #0#24 = 0; |Y #0#22 ] 2;
       |MENAV error1;
       |ERROR;
       |ACABA;
   |FINSI;

   |SI Campo = 25;  |Y #0#25 = 0; |Y #0#22 ] 3;
       |MENAV error1;
       |ERROR;
       |ACABA;
   |FINSI;
   |SI Campo = 26;  |Y #0#26 = 0; |Y #0#22 ] 4;
      |MENAV error1;
      |ERROR;
      |ACABA;
   |FINSI;
   |SI Campo = 27;  |Y #0#27 = 0; |Y #0#22 ] 5;
      |MENAV error1;
      |ERROR;
      |ACABA;
   |FINSI;
   |SI Campo = 28;  |Y #0#28 = 0; |Y #0#22 ] 6;
      |MENAV error1;
      |ERROR;
      |ACABA;
   |FINSI;

   |SI Campo = 24; |Y #0#24 ! 0;    || Si hay dos niveles iguales o menor error
      |SI #0#24 [ #0#23;
         |MENAV error1;
         |ERROR;
         |ACABA;
      |FINSI;
   |FINSI;

   |SI Campo = 25; |Y #0#25 ! 0; || Si hay dos niveles iguales o menor error
      |SI #0#25 [ #0#23; |O #0#25 [ #0#24;
         |MENAV error1;
         |ERROR;
         |ACABA;
      |FINSI;
   |FINSI;

   |SI Campo = 26; |Y #0#26 ! 0; || Si hay dos niveles iguales o menor error
      |SI #0#26 [ #0#23; |O #0#26 [ #0#24; |O #0#26 [ #0#25;
         |MENAV error1;
         |ERROR;
         |ACABA;
      |FINSI;
   |FINSI;

   |SI Campo = 27; |Y #0#27 ! 0; || Si hay dos niveles iguales o menor error
      |SI #0#27 [ #0#23; |O #0#27 [ #0#24; |O #0#27 [ #0#25; |O #0#27 [ #0#26;
         |MENAV error1;
         |ERROR;
         |ACABA;
      |FINSI;
   |FINSI;

   |SI Campo = 28; |Y #0#28 ! 0;    || Si hay dos niveles iguales o menor error
      |SI #0#28 [ #0#23; |O #0#28 [ #0#24; |O #0#28 [ #0#25; |O #0#28 [ #0#26; |O #0#28 [ #0#27;
         |MENAV error1;
         |ERROR;
         |ACABA;
      |FINSI;
   |FINSI;
|FCAL;

|| **********************************************************************
|PROCESO MiroPaseDirecto;
  eSwCambioEsp = 1;
|FPRC;

|DEFBUCLE MiroPaseDirecto;
   #10#1;
   1,3;
   "            ",      1, "S";
   "zzzzzzzzzzzz", nNume5, "S";
   e;
   NULL, MiroPaseDirecto;
|FIN;

|PROCESO CambioEspecial;
   |DBASE aAlfa1; |QBF aAlfa1;
   aAlfa2 = ("00000" + enEmpresa) % -105;
   aAlfa3 = ("0" + enPeriodo)     % -101;
   eaDirFich = aAlfa1 + "fich/"+ aAlfa2 + aAlfa3 + "/";

   |PATH_DAT #10  eaDirFich;

   nNume5 = #0#8 - 1;   ||el anterior al ultimo

   eSwCambioEsp = 0;
   |BUCLE MiroPaseDirecto;
|FPRC;

|PROGRAMA;
   eSwCambio     = 0;
   eSwCambioEsp  = 0;
   nSoloEspecial = 0;

|| ... Empresa Maestra
   |DBASS aDirInicio; |QBF aDirInicio;
   aDirBase = aDirInicio + "modelos/fich/";
   |PATH_DAT #100 aDirBase;
   |ABRE #100;
   |LEE 040#100p;
   |SI FSalida ! 0;
       |CIERRA #100;
       |ACABA;
       || ... No existe fichero de empresa maestra
   |FINSI;
   |CIERRA #100;

   |ABRE #30;
   #30#2 = #100#1;
   #30#3 = #100#2;
   |LEE 001#30=;
   |SI FSalida ! 0;
       |CIERRA #30;
       |ACABA;
       || ... No existe empresa maestra
   |FINSI;
   |CIERRA #30;

   #0#5  = #30#2;
   #0#6  = #30#3;
   #0#7  = #30#1;
   #0#15 = #30#13;
   #0#16 = #30#14;
   #0#17 = #30#15;
   #0#18 = #30#16;
   #0#19 = #30#17;
   #0#20 = #30#18;
   #0#21 = #30#19;

|| ... Empresa Actual
   cod1 = enEmpresa;
   cod2 = enPeriodo;
   emEmp = 1;
   |HAZ leelo;
   |SI swerror = 1; |ACABA; |FINSI;  || no existe empresa

   #0#0  = enEmpresa;
   #0#1  = enPeriodo;
   #0#2  = eaNombreEmp;
   #0#3  = enEjercicio;
   #0#4  = eaMoneda;
   #0#8  = dig3;
   #0#9  = dig4;
   #0#10 = dig5;
   #0#11 = dig6;
   #0#12 = dig7;
   #0#13 = dig8;
   #0#14 = dig9;

   |SI eaPreg22_8 = "S";
       |HAZ CambioEspecial;
   |FINSI;

   |SI #0#8 = #0#15; |Y #0#9  = #0#16; |Y #0#10 = #0#17; |Y #0#11 = #0#18;
                     |Y #0#12 = #0#19; |Y #0#13 = #0#20; |Y #0#14 = #0#21;

       |SI eSwCambioEsp = 0;
           |ACABA;
       |FINSI;
       nSoloEspecial = 1;
       || ... No existe diferencia entre esta empresa y la maestra
   |FINSI;

   #0#22 = #0#8;  #0#23 = #0#9;
   #0#24 = #0#10; #0#25 = #0#11;
   #0#26 = #0#12; #0#27 = #0#13;
   #0#28 = #0#14;
   nUlt1  = #0#8;  nNume1 = 22 + nUlt1; nNume3 = 8 + nUlt1;
   nUlt2  = #0#15; nNume2 = 15 + nUlt2;
   #0nNume1 = #0nNume2;
   |SI #0nNume1 = #0nNume3; |Y nSoloEspecial = 0;
       || |MENAV "    Solo cambia en numero de niveles el ultimo nivel es igual";
       |SI eSwCambioEsp = 0;
           |ACABA;
       |FINSI;
       nSoloEspecial = 1;
   |FINSI;

   #0#22 = #0#15; #0#23 = #0#16;
   #0#24 = #0#17; #0#25 = #0#18;
   #0#26 = #0#19; #0#27 = #0#20;
   #0#28 = #0#21;

   |CLS;
   |PINPA #0#0;
   |PINDA #0#0;

   |SI #0nNume3 [ 4;
       |PUSHV 1702 2174;
       |ATRI R;
       |PINTA 1803, " Atencion !!!.                                                          ";
       |PINTA 1903, " Las Cuentas de Trabajo con Longitud menor o igual a 4 (Ctas Mayor)     ";
       |PINTA 2003, " No se puede realizar el Cambio de Niveles en Esta Empresa              ";
       |PINTA 2103, " El pase a Contagen se ha completado de forma normal                    ";
       |PINTA 2203, " Es conveniente que modifique la configuracion de la Empresa.           ";
       |ATRI 0;
       |CUADRO 1702 2374;
       |PAUSA;
       |POPV;
       |ACABA;
   |FINSI;

   aCambio = "N";
   |SI eaPreg22_1 = "S";
       aCambio = "S";
   |SINO;
       |PUSHV 2011 2364;
       |CUADRO 2011 2364;
       |SI nSoloEspecial = 0;
           |PINTA 2113 , " Existe Diferencia en la Configuracion de Niveles ";
           |PINTA 2213 , " Desea realizar el Cambio en Plan Contable S/N    ";
       |SINO;
           |PINTA 2113 , " Existe Cuentas Con Pase Directo de Nivel inferior";
           |PINTA 2213 , " Desea realizar el Cambio al Ultimo Nivel  S/N    ";
       |FINSI;
       E_sup   = 1;
       E_inf   = "NSsn";
       aCambio =  2260 ? 0; |QBF aCambio; aCambio = aCambio > " ";
       |SI aCambio = "N";
           |POPV;
           |ACABA;
       |FINSI;

       |POPV;
   |FINSI;

   |SI eaPreg22_2 ! "S";
       |ENDATOS #1#0;
       |SI FSalida ! 0;
          |ACABA;
       |FINSI;
   |FINSI;

   |HAZ GraboEmpresa;

   |DBASE aAlfa1; |QBF aAlfa1;
   aAlfa2 = ("00000" + enEmpresa) % -105;
   aAlfa3 = ("0" + enPeriodo)     % -101;
   eaDirFich = aAlfa1 + "fich/"+ aAlfa2 + aAlfa3 + "/";

   |PATH_DAT #10  eaDirFich;
   |PATH_DAT #91  eaDirFich;

   |HAZ PreparaCambio;

   |SI eaPreg22_1 ! "S"; |O eaPreg22_2 ! "S";
       |SUB_EJECUTA_NP ":contagen/cacambix;Pase";
   |FINSI;
   |SUB_EJECUTA_NP ":contagen/cacambi2;Pase";
   eSwCambio = 1;

   |SI enEjercicio = 2008;
       |SUB_EJECUTA_NP ":contagen/ca8z0006;Pase8";
   |FINSI;
|FPRO;

|| **************************************************************************
____________________________________/
|PROCESO caso3;
      |SI nListo ! 0;  |ACABA;  |FINSI;
      |SI #10#0 [ "            "; |ACABA; |FINSI;
      nNume1 = #10#1;

      nNume2 = #90nD_nivels - #90nNume1;
      aAlfa1 = "0" * nNume2; ||digitos que hacen falta para rellenar
      aAlfa2 = #10#0;    |QBF aAlfa2;

      |DDEFECTO #91;
      #91#0 = #10#0;         || cuenta actual
      #91#1 = #10#1;         || nivel actual

      |SI eaPreg22_6 ! "N";
          nNume4 = 4;                    || 4 digitos
          nNume4 = 100 + nNume4;
          aAlfa3 = nNume4; |QBF aAlfa3;  || digitos del principio

          nNume4 = 4;                    || el nivel anterior siempre 4
          nNume4 = #90nNume1 - nNume4;   || Diferencia entre anterior y este
          nNume4 = 0 - (100 + nNume4);   || parte posterior
          aAlfa4 = nNume4;

          #91#2 = (aAlfa2 % aAlfa3) + aAlfa1 + (aAlfa2 % aAlfa4);
      |SINO;
          #91#2 = aAlfa2 + aAlfa1; || cuenta destino ||al final
      |FINSI;

      #91#3 = nD_nivels - 10; || nivel destino
      nD_cuentas  = nD_cuentas + 1;
      #91#4 = nD_cuentas + 10000;
      #91#5 = #10#2;         || nombre cuenta
      #91#6 = #91#2;
      #91#7 = aPaDic;
      |GRABA 020#91n;
      nListo = 1;
|FPRC;

|PROCESO caso2;
  |SI nListo ! 0;  |ACABA;  |FINSI;
  nNume1 = #10#1;                                      || si es el ultimo nivel

  |SI #10#1 = nA_nivels; |Y #90nNume1 < #90nD_nivels;  ||/y los digitos del
                                                       ||/ultimo nivel destino
                                                       ||/son mayores
      nNume2 = #90nD_nivels - #90nNume1;
      aAlfa1 = "0" * nNume2; ||digitos que hacen falta para rellenar
      aAlfa2 = #10#0;    |QBF aAlfa2;

      |DDEFECTO #91;
      #91#0 = #10#0;         || cuenta actual
      #91#1 = #10#1;         || nivel actual

      |SI eaPreg22_6 ! "N";
          nNume3 = nA_nivels - 1;         || el nivel anterior
          nNume4 = #90nNume3;            || el nivel anterior
          nNume4 = 100 + nNume4;
          aAlfa3 = nNume4; |QBF aAlfa3;  || digitos del principio

          nNume4 = #90nNume3;            || el nivel anterior
          nNume4 = #90nNume1 - nNume4;   ||Diferencia entre anterior y este
          nNume4 = 0 - (100 + nNume4);   || parte posterior
          aAlfa4 = nNume4;

          #91#2 = (aAlfa2 % aAlfa3) + aAlfa1 + (aAlfa2 % aAlfa4);
      |SINO;
          #91#2 = aAlfa2 + aAlfa1; || cuenta destino ||al final
      |FINSI;

      #91#3 = nD_nivels - 10; || nivel destino
      nD_cuentas  = nD_cuentas + 1;
      #91#4 = nD_cuentas + 10000;
      #91#5 = #10#2;         || nombre cuenta
      #91#6 = #91#2;
      #91#7 = "S";
      |GRABA 020#91n;
      nListo = 1;
   |FINSI;
|FPRC;

|PROCESO GrabaCaso1;
         |DDEFECTO #91;
         #91#0 = #10#0;          || cuenta actual
         #91#1 = #10#1;          || nivel actual
         #91#2 = #10#0;          || cuenta destino
         #91#3 = nPara1 - 10;     || nivel destino
         nD_cuentas = nD_cuentas + 1;
         #91#4 = nD_cuentas;
         #91#5 = #10#2;          || nombre cuenta
         #91#6 = #91#2;
         #91#7 = aPaDic;
         |GRABA 020#91n;
         nListo = 1;
|FPRC;

|PROCESO caso1;
 |SI nListo ! 0;  |ACABA;  |FINSI;

 |PARA nPara1 = 11; |SI nPara1 [ 16; |HACIENDO nPara1 = nPara1 + 1;
       |SI nLongi = #90nPara1;
           aPaDic = "N";
           nNume5 = nPara1 - 10;
           |SI eaPreg22_8 ! "S"; |O #10#3 = "N"; |O nNume5 = #0#22;
               |HAZ GrabaCaso1;
           |SINO;
               ||  ... |HAZ GrabaCaso1;
               ||  ... nListo = 0;
               |HAZ caso3;
           |FINSI;
           |SAL;
       |FINSI;
  |SIGUE;
|FPRC;

|PROCESO cacuenta1;
   aAlfa1 = #10#0;  |QBF aAlfa1;
   aAlfa2 = aAlfa1 % 0;
   nLongi = aAlfa2;
   nListo = 0;        || switch para saber si la cuenta esta cambiada

   |HAZ caso1;         || cambia nivel, no cambia Longitud
                       ||/no cambia nivel, no cambia longitud

   |HAZ caso2;         || cambia nivel, cambia longitud
                       ||/no cambia nivel, cambia longitud


   |SI nListo = 0; |Y #10#3 = "S"; |Y eaPreg22_8 = "S";
       aPaDic = "S";
       |HAZ caso3;
   |FINSI;
|FPRC;

|DEFBUCLE cacuenta;
   #10#1;
   ;
   "            ";
   "zzzzzzzzzzzz";
   ;
   NULL, cacuenta1;
|FIN;
____________________________________/
|PROCESO registros1;
   nA_cuentas = nA_cuentas + 1;
|FPRC;

|DEFBUCLE registros;
   #10#1;
   ;
   "            ";
   "zzzzzzzzzzzz";
   ;
   NULL, registros1;
|FIN;

|PROCESO PreparaCambio;

   |ATRI P; |PINTA 2301, " Preparando ... ";    |ATRI 0;
   |DELINDEX #91;

    #90#0  = #0#8;
    #90#1  = #0#9;  #90#2  = #0#10;
    #90#3  = #0#11; #90#4  = #0#12;
    #90#5  = #0#13; #90#6  = #0#14;
    nA_nivels = #90#0;

    #90#10 = #0#22;
    #90#11 = #0#23; #90#12 = #0#24;
    #90#13 = #0#25; #90#14 = #0#26;
    #90#15 = #0#27; #90#16 = #0#28;
    nD_nivels = #90#10 + 10;

    nA_cuentas = 0;
    nD_cuentas = 0;
    |ABRE #91;
    |BUCLE registros;
    |BUCLE cacuenta;
    |CIERRA #91;

    |SI eaPreg22_1 ! "S"; |O eaPreg22_2 ! "S";
        aAlfa1 = "    Cuentas Origen: " + nA_cuentas + " <> Cuentas Destino: " + nD_cuentas;
        |MENAV aAlfa1;
    |FINSI;
    |BLIN 23;
|FPRC;

|PROCESO GraboEmpresa;
   nSwError = 0;
   |ABRE #30;
   #30#2 = #0#0;
   #30#3 = #0#1;
   |LEE 101#30=;
   |SI FSalida ! 0;
       nSwError = 1;
       |CIERRA #30;
       |ACABA;
       || ... No existe empresa error raro
   |FINSI;

|| ... Nuevos niveles
   #30#13  = #0#22; dig3 = #30#13;
   #30#14  = #0#23; dig4 = #30#14;
   #30#15  = #0#24; dig5 = #30#15;
   #30#16  = #0#25; dig6 = #30#16;
   #30#17  = #0#26; dig7 = #30#17;
   #30#18  = #0#27; dig8 = #30#18;
   #30#19  = #0#28; dig9 = #30#19;
     || Recoger informacion sobre los niveles ...
     Ipunt1 = dig3<-;
     Ipunt1 = Ipunt1 + dig3;
     MaximoDigitos = punt1;


   |GRABA 020#30a;
   |LIBERA #30;
   |CIERRA #30;
|FPRC;
