|FICHEROS;
   caconexc #0;
   caconcil #1;
   camaasil #2;
   caconex1 #3;
   caconcic #4;
   camaasic #5;
   cacuenta #10;

   :/integral/def/dsam0001 #40;
   canempre #30;
   caselem1 #998;
|FIN;

|VARIABLES;
    &entrada = 1;
    sw       = 0;
    nCampo   = 0;
    aDCuenta = "";
    aHCuenta = "";
    aCuenta  = "";
    nDigito  = 0;
    nSwPrint = 0;
    aAlfa    = "";

    empresant = 0;
    ejercant  = 0;
    Comodin = "";

    p_alfa1 = "";
    p_error = 0;

    aNum1 = "";
    nNume0 = 0;
    nNume1 = 0;
    nNume2 = 0;
    nNume3 = 0;
    aBlancs = "";
|FIN;

|INCLUYE dscont_i;

|PROCESO Parrilla;
     |SI #0#3 = "N";
         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";  #0nCampo = "  ";  |PINTA #0nCampo;
         |SIGUE;
         |C_M #0#0, 1, "S";
         |C_M #0#1, 1, "S";
     |SINO;
         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
         |C_M #0#0, 1, "N";  #0#0 = "            ";  |PINTA #0#0;
         |C_M #0#1, 1, "N";  #0#1 = "zzzzzzzzzzzz";  |PINTA #0#1;
     |FINSI;
|FPRC;

|PROCESO NoPasa;
     |SI #0Campo = "            ";
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = "  ";  |C_M #0nCampo, 1, "N";  |PINTA #0nCampo;
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO cuenta; |TIPO 0;
     p_alfa1 = #0Campo;
     eaCuenta = #caconexc#Campo#;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #caconexc#Campo# = eaCuenta;
     |PINTA #caconexc#Campo#;
|FPRC;

|| *************************************************************************
||                       PROCESOS DEL EXTRACTO POR PANTALLA
|| *************************************************************************

|PROCESO diferenp;
     sw = 1;
     |SI #1#3  = 1;
         #3#6  = #1#2;
         #3#7  = #1#6;
         #3#8  = #1#7;
         #3#9  = #1#4;
         #3#10 = #1#5;
         #3#11 = 0;
         #3#12 = 0;
         #3#13 = 0;
     |SINO;
         #3#6  = #1#2;
         #3#7  = 0;
         #3#8  = 0;
         #3#9  = #1#4;
         #3#10 = #1#5;
         #3#11 = 0;
         #3#12 = #1#6;
         #3#13 = #1#7;
     |FINSI;

     #3#14 = #3#14 + #3#7;
     #3#15 = #3#15 + #3#8;
     #3#16 = #3#16 + #3#12;
     #3#17 = #3#17 + #3#13;

     nSwPrint = 1;  |PRINT;
|FPRC;

|PROCESO ContraPart;
     |SALVA_DATOS #10;
     #10#0 = aCuenta;
     #10#1 = nDigito;
     |LEE 040#10=;
     |SI FSalida ! 0;  |DDEFECTO #10;  |FINSI;

     #3#26 = #10#2;

     |REPON_DATOS #10;
     |LEE 040#10=;
|FPRC;

|PROCESO asientosp;
     #5#0 = #2#0;
     #5#1 = #2#1;
     #5#2 = #2#2;
     |LEE 040#5=;
     |SI FSalida ! 0;  |DDEFECTO #5;  |FINSI;

     sw = 1;
     #3#6  = #2#2;
     #3#7  = 0;
     #3#8  = 0;
     #3#9  = #2#1;
     #3#10 = #2#7;
     #3#11 = #2#2;
     |SI #2#8 = "D";
         #3#12 = #2#9;
         #3#13 = 0;
         aCuenta = #5#10;
         nDigito = #5#11;
         |HAZ ContraPart;
     |SINO;
         #3#13 = #2#9;
         #3#12 = 0;
         aCuenta = #5#8;
         nDigito = #5#9;
         |HAZ ContraPart;
     |FINSI;
     #3#14 = #3#14 + #3#7;
     #3#15 = #3#15 + #3#8;
     #3#16 = #3#16 + #3#12;
     #3#17 = #3#17 + #3#13;

     nSwPrint = 1;  |PRINT;
|FPRC;

|DEFBUCLE caconcilp;
     #1#1;
     8;
     #4#0, 0001, " ";
     #4#0, 9999, " ";
     e;
     NULL, diferenp;
|FIN;

|DEFBUCLE camaasilp;
     #2#3;
     18;
     #4#0, fec1, 000000, 0001, " ";
     #4#0, fec2, 999999, 9999, " ";
     e;
     NULL, asientosp;
|FIN;

|PROCESO impresora;
     sw    = 0;

     #3#0 = #4#0;
     #3#1 = #10#2;
     #3#2 = #0#2;

     |BUCLE caconcilp;
     |BUCLE camaasilp;
     |SI sw = 1;
         #3#18 = #3#3;
         #3#19 = #3#5;
         #3#20 = #3#14;
         #3#21 = #3#16;
         #3#22 = #3#15;
         #3#23 = #3#17;

         #3#24 = #3#18 - #3#20 + #3#22;
         #3#25 = #3#19 - #3#21 + #3#23;
     |FINSI;
|FPRC;

|PROCESO saldo;
     |SI #2#8 = "D";
         #3#5 = #3#5 + #2#9;
     |SINO;
         #3#5 = #3#5 - #2#9;
     |FINSI;
|FPRC;

|DEFBUCLE camaasil;
     #2#3;
     ;
     #4#0, fec1, 000000, 0001;
     #4#0, fec2, 999999, 9999;
     e;
     NULL, saldo;
|FIN;

|PROCESO cabdife;
     |DDEFECTO #4;
         #4#0 = #10#0;
         #4#1 = #10#1;
     |LEE 000#4=;

     |DDEFECTO #3;
         #3#4 = #4#5;
         #3#3 = #4#4;
     |SI #3#4 = "H"; #3#3 = 0 - #3#3; |FINSI;

     |BUCLE camaasil;

     |HAZ impresora;
|FPRC;

|DEFBUCLE caconcic;
     #10#1;
     3;
     aDCuenta, "S";
     aHCuenta, "S";
     e;
     NULL, cabdife;
|FIN;

|PROCESO HazTodo;
  nSwPrint = 0;
  |ABRE #4;
  |ABRE #5;
  |SI #0#3 = "N";
      aDCuenta = #0#0;
      aHCuenta = #0#1;
      |BUCLE caconcic;
  |SINO;
      |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
            |SI #0nCampo ! "            ";
                   aDCuenta = #0nCampo;
                   aHCuenta = #0nCampo;
                   |BUCLE caconcic;
            |FINSI;
      |SIGUE;
 |FINSI;
 |CIERRA #4;
 |CIERRA #5;
 |SI nSwPrint = 1;  |PIEINF;  |FINSI;
|FPRC;

|PROCESO Tipo3;
 || ... Campos de la Direccion
      aAlfa = #0#27; |QBF aAlfa;     || ... Direccion
      aNum1 = aAlfa % 0; nNume1 = aNum1;

      aAlfa = #0#29;   |QBF aAlfa;
      |SI aAlfa ! ""; aAlfa = "("+aAlfa+")"; |FINSI;
      #0#33 = aAlfa; aAlfa = #0#28;   |QBF aAlfa;
      #0#33 = aAlfa + " " + #0#33;

      aAlfa = #0#33; |QBF aAlfa;     || ... Poblacion
      aNum1 = aAlfa % 0; nNume2 = aNum1;

      aAlfa = #0#30; |QBF aAlfa;      || ... Telefono
      aAlfa = "Tfno.: " + aAlfa;
      aNum1 = aAlfa % 0; nNume3 = aNum1;

      nNume0 = nNume1; |SI nNume0 < nNume2; nNume0 = nNume2; |FINSI;
                       |SI nNume0 < nNume3; nNume0 = nNume3; |FINSI;
      |SI nNume0  < 40;
          nNume0  = (40 - nNume0);
          aBlancs = " " * nNume0;
          #0#34  = aBlancs + #0#27;
          #0#33  = aBlancs + #0#33;
          #0#35  = aBlancs + "Tfno.: " + #0#30;
          #0#36  = aBlancs + "       " + #0#31;
      |FINSI;
 ||.....................................................................

     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;
     |INFOR "caconexc";
     |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

     |SI #48#27 = "S";
         |HAZ MultiEmpresa;
     |SINO;
         |DFICE dirfich0; |QBF dirfich0;
         |HAZ HazTodo;
     |FINSI;
     |FININF;
     |FINIMP;
|FPRC;

|PROGRAMA;
     |HAZ inicio;
     |HAZ DirAplicSt;
     |CLS;
     |CABEZA "Carta Partidas Pdtes.";
     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0;
         |DDEFECTO #0;
         |GRABA 020#0b;
     |FINSI;
     |PDEFECTO #0, 1, 25;
     |HAZ LeeDespacho;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
         |HAZ Tipo3;
         |GRABA 020#0a;
     |FINSI;
     |LIBERA #0;
|FPRO;

|PROCESO LeeDespacho;
 |SI HayAcceso = 0; |ACABA; |FINSI;
 |ABRE #40;
 |LEE 101#40p;
 |SI FSalida = 0;
     #0#25 = #40#1;
     #0#26 = #40#2;
     #0#27 = #40#3;
     #0#28 = #40#4;
     #0#29 = #40#5;
     #0#30 = #40#6;
     #0#31 = #40#7;
 |FINSI;
 |CIERRA #40;
|FPRC;

|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #2 dirfich0;
   |PATH_DAT #4 dirfich0;
   |PATH_DAT #5 dirfich0;
   |PATH_DAT #10 dirfich0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 000001;
 999999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;
