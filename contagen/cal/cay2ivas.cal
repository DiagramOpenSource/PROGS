|FICHEROS;
   cay2ivas #0;

   cainliva #1;
   caclipro #2;
   camaniva #11;
   caivalin #12;

   caivaasi #200;

   :/basica/def/agifigen #100;
   canempre #30;
   caselem1 #998;

   lalinea@ #901;
   &regisnif@ #709;
|FIN;

|VARIABLES;
    &enSWAut = 0;
    &enLiAut = 0;
    &enOrAut = 0;
    &eaSeAut = "";

    aAlfa1   = "";
    aAlfa2   = "";
    nNume1   = 0;
    nNume2   = 0;

    informe  = "cay2ivas";

    nSwE     = 0;
    nSw      = 0;
    nSwPie   = 0;

    nLineas  = 0;
    &nSwLin  = 0;

    aDirInicio = "";
    aMas       = "";
    aParam     = "";
    nExis      = 0;
    nSwOp      = 0;
    fDFecha    = @;
|FIN;

|INCLUYE dscont_i;

|| ***************** Calculos de Pantalla
|CALCULO el13; |TIPO 13;
 |ATRI P;
 |SI enSWAut = 1;
     |PINTA 0668, "Facturas NO ";
     |PINTA 0768, "Actualizadas";
 |SINO;
     |PINTA 0667, "Facturas     ";
     |PINTA 0767, "Actualizadas ";
 |FINSI;
 |ATRI 0;
|FCAL;

|CALCULO defectos; |TIPO 7;
  fDFecha = fec1;
  |SI enSwSelFc = 1; fDFecha = "01.01.2000"; |FINSI;
  #0#4 = fDFecha; |PINTA #0#4;
  #0#5 = fec2;    |PINTA #0#5;
|FCAL;

|CALCULO fecha; |TIPO 0;
  fDFecha = fec1;
  |SI enSwSelFc = 1; fDFecha = "01.01.2000"; |FINSI;
  |SI #0Campo < fDFecha; |O #0Campo > fec2;
      |MENAV "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
      |ERROR;
      |ACABA;
  |FINSI;
  |SI Campo = 5; |Y #0#5 < #0#4;
      |MENAV "    FECHA ERRONEA ";
      |ERROR;
  |FINSI;
|FCAL;


|| *************************************************************************
|| **************************   MEOLLO   ***********************************
|| *************************************************************************
|PROCESO LeeNombre;

   nSwE = 0;

   aAlfa1 = #0#20; |QBF aAlfa1;
   |SI aAlfa1  = ""; nSwE = 1; |FINSI;
   |SI Haybasica = 0; |Y HayAcceso = 0; nSwE = 1; |FINSI;

   |SI nSwE = 0;
       nSwE = 1;
       eaCodNif = #0#20;
       |HAZ LeeNifs;
       |SI ewNif = 0;
           nSwE   = 0;
           #0#15  = #709#1;
           aAlfa1 = #709#9; |QBF aAlfa1;
           |SI aAlfa1 = "";
               #0#16 = #709#2;
           |SINO;
               #0#16 = aAlfa1 + " " + #709#2;
           |FINSI;
           aAlfa1 = #0#16; |QBF aAlfa1;
           #0#16  = aAlfa1 + ", " + #709#3;
           aAlfa1 = #0#16; |QBF aAlfa1;
           #0#16  = aAlfa1 + " " + #709#10;
           aAlfa1 = #0#16; |QBF aAlfa1;
           #0#16  = aAlfa1 + " " + #709#11;
           aAlfa1 = #709#4; |QBF aAlfa1; aAlfa1 = ("00"  + aAlfa1) % -102;
           aAlfa2 = #709#5; |QBF aAlfa2; aAlfa2 = ("000" + aAlfa2) % -103;
           #0#17  = aAlfa1 + aAlfa2;
           #0#18  = #709#6;
           #0#19  = #709#7;
       |FINSI;
   |FINSI;

   |SI nSwE = 1;
       eaCuenta = #0#21; |HAZ SacaNivel;
       |ABRE #2;
       |DDEFECTO #2;
       #2#23 = "P";
       #2#10 = #0#21;
       #2#11 = enNivel;
       |LEE 041#2=;
       |SI  FSalida ! 0;
            #2#23 = "C";
            #2#10 = #0#21;
            #2#11 = enNivel;
            |LEE 041#2=;
       |FINSI;
       #0#16 = #2#2;   || Direccion Proveedor
       #0#17 = #2#25;  || Codigo Postal
       #0#18 = #2#6;   || Poblacion
       #0#19 = #2#5;   || Provincia
       |CIERRA #2;
    |FINSI;
|FPRC;
||//////////////////////////////////////////////////////////////////////
|PROCESO LasLineas0;

 |SI nSwOp = 1;
     |SI #12#25 = "S";
         nExis = 1;
         |ERROR10;
     |FINSI;
 |SINO;
     |SI #12#25 = "S"; |O #11#42 = "I";
         #0#32 = #12#4;         || Descripcion
         #0#33 = #12#6;         || Base Imponible
         #0#34 = #12#7;         || % IVA
         #0#35 = #12#8;         || Cuota de IVA
         #0#36 = #12#6 + #12#8; || Total Factura
         |PRINT;

         nLineas = nLineas + 1;
         #0#29  = #0#29 + #12#6;
         #0#30  = #0#30 + #12#8;
         #0#31  = #0#31 + #0#36;
    |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE LasLineas;
 #12#1;
 ;
 #11#0, #11#1, 01;
 #11#0, #11#1, 99;
 e;
 NULL, LasLineas0;
|FIN;


|PROCESO imprimir;

  nExis = 0;
  |SI #11#42 ! "I";
      nSwOp = 1;
      |BUCLE LasLineas;
      |SI nExis = 0; |ACABA; |FINSI;
  |FINSI;

|| ... .... ... Cabecera
  |PDEFECTO #0, 15, 40;

  #0#22 = #11#0;   || Libro
  #0#23 = #11#2;   || Serie
  #0#24 = #11#3;   || Factura
  #0#25 = #11#4;   || Fecha
  #0#26 = #11#22;  || Referencia

  #0#15 = #11#13;  || Proveedor
  #0#20 = #11#14;  || NIF
  #0#21 = #11#12;  || Cuenta
  |HAZ LeeNombre;

  #0#27  = "DOCUMENTO EQUIVALENTE (Soportado)";
  aAlfa1 = "Autofactura de la factura n§ " + #11#22; |QBF aAlfa1;
  #0#28  = aAlfa1 + " del proveedor";

  |SI #11#42 = "I";
      #0#37 = "El sujeto pasivo del impuesto es el destinatario de la operacion segun";
      #0#38 = "establece el articulo 84.Uno.2 Ley 37/1992, del IVA (Articulo 6.3.R.D.";
      #0#39 = "1496/2003 por el que se regulan las obligaciones de facturacion).     ";
 |FINSI;
|| //////////////////////////////////////////////////////////////

  nLineas = 0;

  nSwOp = 0;
  |BUCLE LasLineas;

|| ... ... ... El Total
  |PIEINF;

  #0#27  = "DOCUMENTO EQUIVALENTE (Repercutido)";
  #0#28  = "";

  |SI nLineas [ 1;
      |PRINT;
  |SINO;
      nLineas = 0;
      #0#29 = 0;
      #0#30 = 0;
      #0#31 = 0;
      nSwOp = 0;
      |BUCLE LasLineas;
  |FINSI;
  |PIEINF;

|FPRC;

|DEFBUCLE caymaniva;
 #11#3;
 4;
 #0#0, #0#7, #0#2, #0#4;
 #0#1, #0#8, #0#3, #0#5;
 e;
 NULL,imprimir;
|FIN;

|| ******************************************************
|PROCESO LaCabeceraI;

 |PDEFECTO #0, 15, 40;

 #0#22 = #1#0;   || Libro
 #0#23 = #1#26;  || Serie
 #0#24 = #1#2;   || Factura
 #0#25 = #1#4;   || Fecha
 #0#26 = #1#53;  || Referencia

 #0#15 = #1#27;  || Proveedor
 #0#20 = #1#30;  || NIF
 #0#21 = #1#5;   || Cuenta
 |HAZ LeeNombre;

 #0#27  = "DOCUMENTO EQUIVALENTE (Soportado)";
 aAlfa1 = "Autofactura de la factura n§ " + #1#53; |QBF aAlfa1;
 #0#28  = aAlfa1 + " del proveedor";

 |SI #1#62 = "I";
     #0#37 = "El sujeto pasivo del impuesto es el destinatario de la operacion segun";
     #0#38 = "establece el articulo 84.Uno.2 Ley 37/1992, del IVA (Articulo 6.3.R.D.";
     #0#39 = "1496/2003 por el que se regulan las obligaciones de facturacion)      ";
 |FINSI;
 nLineas = 0;
|FPRC;

|PROCESO imprImagen;
 |SI #1#62 = "I"; |O #1#54 = "S";

     #0#32 = #901#8;  || Descripcion
     #0#33 = #901#12; || Base Imponible
     #0#34 = #901#13; || % IVA
     #0#35 = #901#14; || Cuota de IVA
     #0#36 = #901#29; || Total Factura
     |PRINT;
     nSwPie = 0;

     #0#29  = #0#29 + #901#12;
     #0#30  = #0#30 + #901#14;
     #0#31  = #0#31 + #901#29;
 |FINSI;
|FPRC;

|DEFBUCLE cayimagen;
 #901#2004;
 4;
 #0#22, #0#23, #0#24, 01, #0#4;
 #0#22, #0#23, #0#24, 99, #0#5;
 e;
 NULL,imprImagen;
|FIN;

|PROCESO ElTotalI;
     |PIEINF;
     nSwPie = 1;

     #0#27  = "DOCUMENTO EQUIVALENTE (Repercutido)";
     #0#28  = "";

     |SI nLineas [ 1;
         |PRINT;
     |SINO;
         nLineas = 0;
         #0#29 = 0;
         #0#30 = 0;
         #0#31 = 0;
         |BUCLE cayimagen;
     |FINSI;
     |PIEINF;
     nSwPie = 1;
|FPRC;

|PROCESO imprimirI;
 |SI #1#62 = "I"; |O #1#54 = "S";
      |SI nSw = 0;
          |HAZ LaCabeceraI;
          nSw = 1;
      |FINSI;

      |SI #0#22 ! #1#0; |O #0#23 ! #1#26; |O #0#24 ! #1#2;
          |HAZ ElTotalI;
          |HAZ LaCabeceraI;
      |FINSI;

      #0#32 = #1#8;  || Descripcion
      #0#33 = #1#12; || Base Imponible
      #0#34 = #1#13; || % IVA
      #0#35 = #1#14; || Cuota de IVA
      #0#36 = #1#29; || Total Factura
      |PRINT;
      nSwPie = 0;

      nLineas = nLineas + 1;
      #0#29  = #0#29 + #1#12;
      #0#30  = #0#30 + #1#14;
      #0#31  = #0#31 + #1#29;
 |FINSI;
|FPRC;

|DEFBUCLE cayinliva;
 #1#2;
 4;
 #0#0, #0#7, #0#2, 01, #0#4;
 #0#1, #0#8, #0#3, 99, #0#5;
 e;
 NULL,imprimirI;
|FIN;
|| *************************************

|PROCESO HazTodo;

   |PDEFECTO #0, 9, 40;

   nSwE = 0;
   |SI Haybasica = 1;
       |ABRE #100;
       #100#0 = #30#2; || Empresa
       |LEE 041#100=;
       |SI FSalida = 0;
           nSwE = 1;
           #0#9  = #100#2;   || Nombre
           aAlfa2 = "";
           aAlfa1 = #100#3; |QBF aAlfa1;
           |SI aAlfa1 ! "";
               aAlfa2 = aAlfa1 + " ";
           |FINSI;
           aAlfa2 = aAlfa2 + #100#4; |QBF aAlfa2;
           aAlfa1 = #100#5; |QBF aAlfa1;
           |SI aAlfa1 ! "";
               aAlfa2 = aAlfa2 + ", " + aAlfa1;
           |FINSI;
           #0#10  = aAlfa2;  || Direccion
           aAlfa1 = #100#9;  |QBF aAlfa1; aAlfa1 = ("00"  + aAlfa1) % -102;
           aAlfa2 = #100#10; |QBF aAlfa2; aAlfa2 = ("000" + aAlfa2) % -103;
           #0#11  = aAlfa1 + aAlfa2;   || Codigo Postal
           #0#12 = #100#11;   || Poblacion
           #0#13 = #100#12;   || Provincia
           #0#14 = #100#13;   || Nif
       |FINSI;
       |CIERRA #100;
   |FINSI;

   |SI nSwE = 0;
       #0#9  = #30#1;   || Nombre
       #0#10 = #30#5;   || Direccion
       #0#11 = #30#6;   || Codigo Postal
       #0#12 = #30#7;   || Poblacion
       #0#13 = "";
       #0#14 = #30#8;   || Nif
   |FINSI;

   |INFOR informe;
   |SI FSalida ! 0;  |ACABA;  |FINSI;

   nSwPie = 0;
   nSw    = 0;
   |SI enSWAut = 1;
       |BUCLE cayinliva;
       |SI nSwPie = 0; |Y nSw = 1;
           |HAZ ElTotalI;
       |FINSI;
   |SINO;
       |BUCLE caymaniva;
   |FINSI;

   |FININF;
|FPRC;

|| ********************************************************************
|CALCULO impre; |TIPO 3;

  |IMPRE 0;
  |SI FSalida ! 0;  |ACABA;  |FINSI;

  |SI #48#27 = "S"; |Y enSWAut = 0;
      |HAZ MultiEmpresa;
  |SINO;
      |DFICE dirfich0; |QBF dirfich0;
      |HAZ HazTodo;
  |FINSI;

  |FINIMP;
|FCAL;


|PROCESO impreComp;
  nExis = 0;
  |ABRE #11;
  |SELECT #3#11;
  #11#0 = #0#0;
  #11#2 = #0#7;
  #11#3 = #0#2;
  |LEE 040#11=;
  |SI FSalida = 0;
      |SI #11#42 ! "I";
          nExis = 0;
          nSwOp = 1;
          |BUCLE LasLineas;
      |SINO;
          nExis = 1;
      |FINSI;
  |FINSI;
  |SELECT #1#11;
  |CIERRA #11;

  |SI nExis = 1;
      |HAZ impre;
  |FINSI;
|FPRC;

|| **********************************************************************
|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;
   |PATH_DAT #1   dirfich0;
   |PATH_DAT #2   dirfich0;
   |PATH_DAT #11  dirfich0;
   |PATH_DAT #12  dirfich0;
   |PATH_DAT #901 dirfich0;

   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 000001;
 999999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;


|PROGRAMA;

  aParam = PARAMETRO; |QBF aParam;
  aParam = aParam > " ";

  |DBASE aDirInicio;
  aMas  = aDirInicio + "def/cainliva";
  |CARGA_DEF aMas, lalinea@;
  |SI FSalida ! 0;
       |MENAV "    Error Cargado Fichero de Contabilidad. Proceso Interrumpido";
       |ACABA;
  |FINSI;

  |ABRE #200;
  |LEE 000#200p;
  |SI FSalida ! 0;
      |DDEFECTO #200;
      |GRABA 040#200n;
  |FINSI;
  |CIERRA #200;
  aAlfa1 = #200#44;
  |C_M #0#7,1,aAlfa1;
  |C_M #0#8,1,aAlfa1;

  |HAZ eParaModelos;

  |SI enSWAut = 0;
      |SI aParam = "I";  enSWAut = 1;  |FINSI;
      |HAZ DirAplicSt;
      |CLS;
      |MANTE #0;
  |SINO;
      |DDEFECTO #0;
      #0#0 = enLiAut; #0#1 = enLiAut;
      #0#2 = enOrAut; #0#3 = enOrAut;
      #0#4 = "01.01.1900"; #0#5 = "31.12.2099";
      #0#7 = eaSeAut; #0#8 = eaSeAut;
      |ABRE #30;
      #30#2 = enEmpresa;
      #30#3 = enPeriodo;
      |LEE 001#30=;
      |CIERRA #30;

      |SI enSWAut = 1;
          |HAZ impre;
      |SINO;
          |HAZ impreComp;
      |FINSI;
  |FINSI;

  |DESCARGA_DEF #901;
|FPRO;
