|FICHEROS;
   caut0003 #0;

   caivaasi #200;
   cadeflab #210;

   canempre #30;
   caselem1 #998;
|FIN;

|VARIABLES;
   aFichero  = "";
   aAlfa1    = "";
   aAlfa2    = "";
   aAlfa3    = "";
   nSwError  = 0;
   aParam    = "";
   aConforme = "";
   aBlanc    = "";
   &enCambio = 0;
|FIN;

|INCLUYE dscont_i;

____________________________________/ CALCULOS DESDE PANTALLA

|CALCULO DeDonde; |TIPO 0;
  |C_M #0#1,1,"S";
  |C_M #0#2,1,"S";
  |C_M #0#3,1,"S";
  |SI #0#0 = 1; |C_M #0#1,1,"N"; #0#1 = "N"; |PINTA #0#1; |FINSI;
  |SI #0#0 = 2; |C_M #0#2,1,"N"; #0#2 = "N"; |PINTA #0#2; |FINSI;
  |SI #0#0 = 3; |C_M #0#3,1,"N"; #0#3 = "N"; |PINTA #0#3; |FINSI;
|FCAL;

|CALCULO Modifico; |TIPO 0;
||  |SI #0#4 = "S";
||      |C_M #0#1,1,"S";
||      |C_M #0#2,1,"S";
||      |C_M #0#3,1,"S";
||  |FINSI;
|FCAL;

|| ***********************************************************************
|PROCESO Actualizo;

   |ABRE #30;
   #30#2 = enEmpresa;
   #30#3 = enPeriodo;
   |LEE 141#30=;
   |SI FSalida ! 0;
       |LIBERA #30;
       |CIERRA #30;
       |ACABA;
   |FINSI;

   |SI #0#1 = "S";
       #30#30 = eaDfDepC;   ||Defecto Departamento
       #30#31 = eaDfSubC;   ||Defecto Subdepartamento
       #30#32 = eaDfDepN;   ||Defecto Nombre Departamento
       #30#33 = eaDfSubN;   ||Defecto Nombre Subdepartamento
       |GRABA 020#30a;
   |FINSI;

   |LIBERA #30;
   |CIERRA #30;

   |SI #0#2 = "S";
       |ABRE #210;
       |LEE 041#210p;
       |SI FSalida ! 0;
           |DDEFECTO #210;
           |GRABA 020#210n;
       |FINSI;
       #210#22 = eaDfDepC; |SI #30#24 = "N"; #210#52 = "N"; |FINSI;
       #210#23 = eaDfDepN;
       #210#57 = eaDfSubC; |SI #30#22 = "N"; #210#59 = "N"; |FINSI;
       #210#58 = eaDfSubN;
       |GRABA 020#210a;
       |CIERRA #210;
   |FINSI;
   |SI #0#3 = "S";
       |ABRE #200;
       |LEE 041#200p;
       |SI FSalida ! 0;
           |DDEFECTO #200;
           |GRABA 020#200n;
       |FINSI;
       #200#19 = eaDfDepC; |SI #30#24 = "N"; #200#20 = "N"; |FINSI;
       #200#67 = eaDfSubC; |SI #30#22 = "N"; #200#68 = "N"; |FINSI;
       |GRABA 020#200a;
       |CIERRA #200;
   |FINSI;
|FPRC;

|PROCESO DatosLaboral;
  nSwError = 0;
  |ABRE #210;
  |LEE 041#210p;
  |SI FSalida ! 0;
      nSwError = 1;
  |SINO;
      eaDfDepC = #210#22;
      eaDfDepN = #210#23;
      eaDfSubC = #210#57;
      eaDfSubN = #210#58;
  |FINSI;
  |CIERRA #210;
|FPRC;

|PROCESO DatosIVA;
  nSwError = 0;
  |ABRE #200;
  |LEE 041#200p;
  |SI FSalida ! 0;
      nSwError = 1;
  |SINO;
      eaDfDepC = #200#19;
      eaDfSubC = #200#67;
      |SI eaDepar = "S";
          enActividad = eaDfDepC;
          eaCodDep    = eaDfDepC;
          |HAZ Actividad;
          eaDfDepN = eaNombreAct;
      |FINSI;
      |SI eaSubde ! "N";
          enConcepto  = 1;
          eaCodSubdep = eaDfSubC;
          |HAZ SubActividad;
          eaDfSubN = eaNomSubdep;
      |FINSI;
  |FINSI;
  |CIERRA #200;
|FPRC;

|PROCESO HazTodo;
   |PATH_DAT #200 dirfich0;
   |PATH_DAT #210 dirfich0;

   nSwError = 0;
   |SI #0#0 = 2;  |HAZ DatosLaboral; |FINSI;
   |SI #0#0 = 3;  |HAZ DatosIVA;     |FINSI;

   |SI nSwError = 0;
       |HAZ Actualizo;
       ||  ... |SI #0#4 = "N"; |HAZ Actualizo; |FINSI;
   |FINSI;
|FPRC;

|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   |PINTA 2222, #30#2;
   |PINTA 2228, #30#3;
   |PINTA 2230, #30#1;

   dirfich0  = #caselem1#3;
   |QBF dirfich0;

   |HAZ HazTodo;
|FPRC;


|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 001;
 999;
 ;
 NULL,MultiEmp1;
|FIN;


|CALCULO recupera; |TIPO 3;

   |SUB_EJECUTA "caselemp";
   |NOME_DAT #998 eaFichsel;
   |ATRI R; |PINTA 2202, "Procesando Empresa: "; |ATRI 0;
   |BUCLE MultiEmp0;
   |DELINDEX #998;
|FCAL;


|PROGRAMA;
   aParam = PARAMETRO; |QBF aParam;

   |SI aParam ! "";
       |PUSHV 11111667;
       aBlanc = " " * 57;
       |PINTA 1111, aBlanc; |PINTA 1211, aBlanc;
       |PINTA 1311, aBlanc; |PINTA 1411, aBlanc;
       |PINTA 1511, aBlanc; |PINTA 1611, aBlanc;

       |CUADRO 1111 1667;
       |ATRI R;
       |PINTA 1212, "Cambiar Defectos de Departamento y Subdepartamento en: ";
       |ATRI P;
       |SI aParam = "1";
           |PINTA 1312, " 1- El Fichero de defectos de Entrada Laboral y";
           |PINTA 1412, " 2- El Fichero de defectos de Entrada de IVA.";
       |SINO;
           |PINTA 1312, " 1- Datos de Empresa Contable";
           |SI aParam = "3";
               |PINTA 1412, " 2- El Fichero de defectos de Entrada Laboral.";
           |SINO;
               |PINTA 1412, " 2- El Fichero de defectos de Entrada de IVA.";
           |FINSI;
       |FINSI;
       |ATRI I;
       |PINTA 1552,"Conforme S/N";
        E_sup     = 1;
        E_inf     = "SNsn";
        aConforme = "N";
        aConforme = 1565 ? 0; aConforme = aConforme > " ";
        |ATRI 0;
        |SI aConforme = "N";
           |MENAV "    NO se han cambiado los valores por defecto en el resto de ficheros";
           |POPV;
           |ACABA;
       |FINSI;

       |DFICB aAlfa1;
       aAlfa2 = enEmpresa; |QBF aAlfa2; aAlfa2 = ("00000" + aAlfa2) % -105;
       aAlfa3 = enPeriodo; |QBF aAlfa3; aAlfa3 = ("0"     + aAlfa3) % -101;
       dirfich0 = aAlfa1 + aAlfa2 + aAlfa3 + "/";
       |QBF dirfich0;

       #0#0 = aParam;
       |SI #0#0 = 1; #0#1 = "N"; #0#2 = "S"; #0#3 = "S"; |FINSI;
       |SI #0#0 = 2; #0#1 = "S"; #0#2 = "N"; #0#3 = "S"; |FINSI;
       |SI #0#0 = 3; #0#1 = "S"; #0#2 = "S"; #0#3 = "N"; |FINSI;
       #0#4 = "N";
       |HAZ HazTodo;
       |MENAV "    VALORES POR DEFECTO DE DEPARTAMENTO Y SUBDEP. CAMBIADOS EN LA APLICACION";

       |SI enCambio = 1;
           |ATRI I;
           |PINTA 1512,"Cambiar en Op. Financieras y Elementos Amortizacion ";
           E_sup     = 1;
           E_inf     = "SNsn";
           aConforme = "N";
           aConforme = 1565 ? 0; aConforme = aConforme > " ";
           |ATRI 0;
           |SI aConforme = "S";
               |SUB_EJECUTA_NP "caut0008;1";
           |FINSI;
       |FINSI;
       |POPV;
 |SINO;
     |CLS;
     |MANTE #0;
 |FINSI;
|FPRO;
