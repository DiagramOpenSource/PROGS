|FICHEROS;
    n43mbaex #0;       || Este Def.
    cacuenta #2;       || Cuentas Contables.
    n43datos #3;       || Datos Importados.
    n43tempd #4,MANTE; || Temporal Datos Importados.
    camaasil #5;       || Lineas Apuntes.
    n43tempe #6,MANTE; || Temporal Asientos.
    n43consl #10;      || consolidados.
|FIN;

|VARIABLES;
    &entrada  = 1;
    fichero1  = "";
    contador  = 0;
    contadorc = 0;
    fd        = @;
    fh        = @;
    fichero   = "";
    men2      = "     Longitud de Cuenta Fuera de Nivel !";
    vacio     = "            ";
    inkey      = 0;
    nSeleccion = 0;
    aAlfa      = "";
    nUltimo    = 0;
|FIN;

|INCLUYE dscont_i;

|PROCESO cuenta;|TIPO 6;

   |SI #0Campo ! vacio;
       eaCuenta = #0Campo;
       salidas  = FSalida;
       |HAZ FiltraPuntos;
       #0Campo = eaCuenta; |PINTA #0Campo;
   |SINO;
        |ABRE #3;
        |LEE 040#3u;
        |SI FSalida = 0;
            #0Campo = #3#1; |PINTA #0Campo;
            eaCuenta = #0Campo;
        |SINO;
            |CIERRA #3;
            |MENAV "    No existen Datos";
            |ERROR;
            |ACABA;
        |FINSI;
        |CIERRA #3;
   |FINSI;

   |HAZ SacaNivel;
   #0#1 = enNivel;
   |SI eswCta = 1;
       |MENAV men2;
       |ERROR;
   |FINSI;
|FPRC;
|| ********************************************************************

|PROCESO camaasil;

    |SI #5#18 ! "û";
        |SI nSeleccion = 1;
            |SI #4#7 ! #5#9;
                |LIBERA #5;
                |ACABA;
            |FINSI;
        |FINSI;

        #10#10  = #5#4; ||Cuenta Apunte;
        #10#11  = #5#5; ||Digito;
        #10#12  = #5#1; ||Fecha Apunte;
        #10#13  = #5#2; ||Documento Apunte;
        #10#18  = #5#0; ||Diario
        #10#19  = #5#3; ||Apunte

        |LEE 040#10=;  ||Lee Fichero Consolidaciones;
        |SI FSalida ! 0;
            contador = contador + 1;
            #6#0   = contador;
            #6#1   = #5#4;
            #6#2   = #5#0;
            #6#3   = #5#1;
            #6#4   = #5#2;
            #6#5   = #5#3;
            #6#6   = #5#5;
            #6#7   = #5#6;
            #6#8   = #5#7;
            #6#9   = #5#8;
            #6#10  = #5#9;
            #6#11  = " ";
            |GRABA 000#6n;
        |FINSI;
    |FINSI;

    |LIBERA #5;
|FPRC;

|DEFBUCLE camaasil;
   #5#3;
   ;
   #0#0, fd, 000000, 0001;
   #0#0, fh, 999999, 9999;
   be;
   NULL,camaasil;
|FIN;

|PROCESO cpentrad;
   #10#10  = #6#1; ||Cuenta Apunte;
   #10#11  = #6#6; ||Digito;
   #10#12  = #6#3; ||Fecha Apunte;
   #10#13  = #6#4; ||Documento Apunte;
   #10#18  = #6#2;
   #10#19  = #6#5;
   |LEE 040#10=;  ||Lee Fichero Consolidaciones;
   |SI FSalida ! 0;
           contadorc = contadorc + 1;
           #10#0  = contadorc; #10#1  = #4#0; #10#2  = #4#1;  #10#3  = #4#2;
           #10#4  = #4#3;      #10#5  = #4#4; #10#6  = #4#5;  #10#7  = #4#6;
           #10#8  = #4#7;      #10#9  = #4#8;

           #10#10 = #6#1;      #10#11 = #6#6; #10#12 = #6#3;  #10#13 = #6#4;
           #10#14 = #6#7;      #10#15 = #6#8; #10#16 = #6#9;  #10#17 = #6#10;
           #10#18 = #6#2;      #10#19 = #6#5;

           #10#20 = 1;  || Consolidacion Uno / Varios

           |GRABA 040#10n;
           #3#9 = "S";
           |GRABA 020#3a;
   |FINSI;
|FPRC;

|DEFBUCLE cpentrad;
  #6#1;
  11;
  #0#0,000000,"*";
  #0#0,999999,"*";
  ;
  NULL,cpentrad;
|FIN;

|PROCESO actua;
   contadorc = 0;
   |ABRE #10;
   |LEE 040#10u;
   |SI FSalida = 0;
       contadorc = #10#0;
   |FINSI;
   |SELECT #3#10;

   |ABRE #3;
   #3#0 = #4#0;
   |LEE 101#3=;
   |BUCLE cpentrad;           || Bucle entrada;
   |GRABA 020#3a;
   |CIERRA #10;
   |CIERRA #3;

|FPRC;

|| ////////////////////////////////////////////////////////////
|PROCESO graba;
     |SI #6#11 = "*";
          #6#11 = " ";
          #4#13 = #4#13 - #6#10;
          |PINTA 1163, #4#13;
     |SINO;
          #6#11 = "*";
          #4#13 = #4#13 + #6#10;
          |PINTA 1163, #4#13;
     |FINSI;
     |GRABA 000#6a;
|FPRC;

|PROCESO tecla2; |TIPO 11;
     |SI FSalida = 9;
          |PTEC 806;
          inkey = 9;
     |FINSI;
     |SI FSalida = 10;
          |HAZ graba;
          inkey = 0;
     |FINSI;
|FPRC;

|| ////////////////////////////////////////////////////////////
|RUTINA infa;
     #6#1 = #0#0;
     #6#0 = 000001;
|FRUT;

|RUTINA supa;
     #6#1 = #0#0;
     #6#0 = 999999;
|FRUT;

|PROCESO asiento;

     fichero1 = Usuario; fichero1 = "T1" + fichero1;
     |QBF fichero1;
     |NOME_DAT #6 fichero1;
     |DELINDEX #6;

     |ABRE #6;
     |ABRE #10;
     |SELECT #3#10;
     contador = 0;
     fd = "01.01.1900";
     fh = "31.12.2300";
     |BUCLE camaasil;
     |CIERRA #10;
     |SI contador = 0;
         aAlfa = "    No existe Apuntes para Conciliar de esta Cuenta.";
         |SI nSeleccion = 1;
             aAlfa = "    No existe Apuntes con importe " + #4#7 + " para Conciliar de esta Cuenta.";
         |FINSI;
         |MENAV aAlfa;
         |CIERRA #6;
         |ACABA;
     |FINSI;

   |ET asi;
     inkey = 0;
     |ENTLINEAL #1#6, -2 , 4 , 21 , 1 , infa , supa;
     |SI inkey ! 9; |VETE asi;  |FINSI;
     |SI #4#13 ! 0; |HAZ actua; |FINSI;

     |CIERRA #6;
     |DELINDEX #6;
|FPRC;

|PROCESO tecla; |TIPO 11;
     |SI FSalida = 9;
          |PTEC 806;
          inkey = 9;
     |FINSI;
     |SI FSalida = 10; |O FSalida = 11;
          nUltimo = #4#0;
          nSeleccion = 0;
          |SI FSalida = 11; nSeleccion = 1; |FINSI;
          |PINTA 1063, #4#7;
          |PINTA 1163, #4#13;
          |HAZ asiento;
          inkey = 10;
          |PTEC 806;
     |FINSI;
|FPRC;

|| ////////////////////////////////////////////////////////////////////
|PROCESO n43datos;
     #4#0   = #3#0;
     #4#1   = #3#1;
     #4#2   = #3#2;
     #4#3   = #3#3;
     #4#4   = #3#4;
     #4#5   = #3#5;
     #4#6   = #3#6;
     #4#7   = #3#7;
     #4#8   = #3#8;
     #4#9   = #3#9;
     #4#10  = #3#10;
     #4#11  = #3#11;
     #4#12  = #3#12;
     #4#13  = 0;
     |GRABA 000#4n;
|FPRC;

|DEFBUCLE n43datos;
   #3#2;
   9,11;
   #0#0, 000000, "N", "N";
   #0#0, 999999, "N", "N";
   ;
   NULL, n43datos;
|FIN;

|RUTINA inff;
     |SI FSalida = "POSICION";
         #4#0 = nUltimo;
     |SINO;
         #4#0 = 000001;
     |FINSI;
     #4#1 = #0#0;
|FRUT;

|RUTINA supf;
     #4#1 = #0#0;
     #4#0 = 999999;
|FRUT;

|PROCESO cargad;
     fichero = Usuario; fichero = "T0" + fichero;
     |QBF fichero;
     |NOME_DAT #4 fichero;

     nUltimo = 1;

   |ET enlisi;
     |DELINDEX #4;
     |ABRE #4;
     |BUCLE n43datos;
   |ET enli;
     |PINPA #0#4;
     inkey = 0;
     |ENTLINEAL #1#4, -2 , 4 , 12 , 0 , inff , supf;
     |SI inkey ! 9; |Y inkey ! 10; |VETE enli; |FINSI;
     |SI inkey = 10;
          |CIERRA #4;
          |VETE enlisi;
     |FINSI;
     |CIERRA #4;
     |DELINDEX #4;
|FPRC;

|PROGRAMA;
     |HAZ inicio;
     |CLS;
     |CABEZA "Consolidacion Manual";
     |PINPA #0#0;

     inkey = 0;
     |ET Vuelve;
         |ENCAMPO #0#0;
         |SI FSalida = 0;
             |HAZ cargad;
             |VETE Vuelve;
         |FINSI;
|FPRO;
