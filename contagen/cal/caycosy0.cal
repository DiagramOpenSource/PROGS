|FICHEROS;
   caycosy0 #0;       || Informe comprobacion suma y saldos
   cacuenta #1;       || Plan contable
   caycosy1 #2;
   caconimp #8;

   canempre #30;
   caselem1 #998;
   cawdatos #900;
   ||cacreano #260;

   cuentat@ #999;
|FIN;

|VARIABLES;
     ||DSARCHI
     &nArchi = 0;
 {-1}aMenu  = "";
     aMenu1 = "2400";
     aMenu2 = "1";
     aMenu3 = "Archivo documental activado. Elija opci¢n: ";
     aMenu4 = "IAB";
     aMenu5 = "Imprimir";
     aMenu6 = "Archivar";
     aMenu7 = "amBos";
     &nOpcion = 0;
     &aImpresora = "";
     &swPasada = 0;
     &swErrorDSArchi = 0;
     &eaInforme = "";
     &eaVengoDe = "";
     &enSwDesde9 = 1;
     &enSwMeteGrupoSubTipo = 0;
     &eaObservaciones = "";
     nOpSegur = 0;

  &eaNomFEst  = "";  ||Nombre del Fichero Creado
  &eaEstruc   = "";
  &eaEstrucN  = "";
  &enCam1     = 0;
  &enCam2     = 0;

   Numeracion = 0;
   Linea = 0;
   aFichero = "";

   &enLongNivel = 0;
   &balance = 1;
   &pathbal = "";
   &entrada = 1;
   &r_emp = 0;
   &r_per = 0;
   lin = 0;
   sw_error = 0;
   x = 0;
   informe = "";
   men1     = "     NO EXISTE EMPRESA !";
   mes = 0;                  || Variable para saber el mes seleccionado
   letrames = "";            || Variable para poner el mes en letra
   varcamp = 0;              || Variable para posicionarse en un campo
   xxxxfiac = "  ";          || Variables para no modificable
   xxaponer = "  ";
   xrelleno = "                              ";
   error1 = "0000Nivel repetido";
   error2 = "0000No existe informe";
   error3 = "0000El numero de nivel no coincide con el numero de nivel de la empresa";
   swprint = 0;       || Variable para saber si imprimimos o no
   swsuma = 0;        || Variable para saber si sumamos o no
   nivel = 0;         || Variable que posee el nivel mas alto
   saldo = 0;         || Resta entre el debe y el haber;
   sw = 0;            || sw para nivel anterior
   nivelact = 0;      || Nivel actual
   rellenos = "";     || Variable para rellenar el hasta de la cuenta en zetas
   &i_debe = 0;       || Total del debe por registro
   &i_haber = 0;      || Total del haber por registro
   &i_deudor = 0;     || Total saldo deudor por registro
   &i_acreedor = 0;   || Total saldo acreedor por registro
   &t_debe = 0;       || Total del debe del informe
   &t_haber = 0;      || Total del haber del informe;
   &t_deudor = 0;     || Total saldo deudor por informe
   &t_acreedor = 0;   || Total saldo acreedor por informe
   &i_saldoap  = 0;
   &t_saldoap  = 0;
   &swlinea = 0;      || Pone o no linea en blanco en informe
   &swcomen = 0;
   {-1}menu  = "";           || Variables menu impresora o pantalla
   menu1 = "2400";
   menu2 = "2";
   menu3 = "Elija opcion: ";
   menu4 = "PI";
   menu5 = " Pantalla ";
   menu6 = " Impresora ";
   {-1}form  = "";           || Variables menu impresora o pantalla
   form1 = "2400";
   form2 = "1";
   form3 = "Elija formato: ";
   form4 = "123";
   form5 = " < 1 > ";
   form6 = " < 2 > ";
   form7 = " < 3 > ";
   op_form = 0;
   men2  = "     Longitud de Cuenta Fuera de Nivel !";
   long = "  ";
   cta  = 0;
   num   = 0;
   swdig = 0;
   digito = 0;
   menor = 0;
   para1 = 0;
   nume1 = 0;
   nume2 = 0;
   nume3 = 0;
   nume4 = 0;
   alfa1 = "";
   alfa2 = "";
   alfa3 = "";
   alfa4 = "";
   dos_ant = "xx";
   &pagina = 0;
   sw_pagina = 0;
   &aparam = "";

   Comodin  = "";
   Comodin1 = "";
   Calc     = 0;
   Per1     = -1;
   Per2     = -1;

   &nPagDup = 0;
   &nSwPagDup = 0;

  aAlfa = "";
  aAlfa1 = "";
  aAlfa2 = "";
  aAlfa3 = "";
  nNume1 = 0;
  nNume2 = 0;
  nNume3 = 0;
  nSwClave = 0;

   &ext1      = "";
   &ext2      = "";
   &runnom    = "";
   aBlanc     = "";
   &efFecLeg  = @;
   &eaDatLeg  = "";
   &enNumeLeg = 0;
   &eaFormato = "";

   &espe15 = "";
   &espe16 = 0;
   aNomCta = "";
   aNomCtaO = "";
   nSwOp   = 0;
   nSeguir = 0;
   vermov  = 0;

   aMas     = "";
   aDef     = "";
   nCalc   = 0;
   nCont   = 0;
   &eaUnidadBasico = "";
   &eaUnidadBTmp   = "";

   nEmpTodo    = 0;
   nPerTodo    = 0;
   &espe17     = "";
   &eSwSuma    = 0;
   dirfichOri  = "";
   nSw         = 0;
   nPara1      = 0;
   nYaInforme  = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE digito_i;

|| *********************************************
|CALCULO AnaT13; |TIPO 13;
 aparam = PARAMETRO; |SI aparam = " N"; aparam = ""; |FINSI;
 |SI aparam = "Analitica";
     |ATRI P;
     |PINTA 0572, "ANALITICO";
     |ATRI 0;
     #30#1 = eaNomAna;
 |FINSI;
 |SI aparam = "otp";
     #0#16 = enCam1; |PINTA #0#16;
     #0#17 = enCam2; |PINTA #0#17;
     #0#18 = eaEstruc; |PINTA #0#18;
     |C_M #0#16,1,"N";
     |C_M #0#17,1,"N";
     |C_M #0#18,1,"N";
     |ATRI P;
     |PINTA 0570, "ESTRUCTURA";
     |ATRI 0;
 |FINSI;
|FCAL;

|PROCESO DatosEmp; |TIPO 7;
 |ABRE #8;
 |DDEFECTO #8;
 #8#0 = 1;
 |LEE 011#8=;
 |CIERRA #8;
 #0#23 = #8#7; |PINTA #0#23;
 alfa1 = #8#8; |C_M #0#23,1,alfa1;
|FPRC;

|PROCESO antesque;
   aNomCta = "cacuenta";
   |SI aparam = "fech";
       aNomCta = "cabalano";
   |FINSI;
   |SI aparam = "Analitica";
       aNomCta = eaUsuAna;
   |FINSI;
   |SI aparam = "otp";
       aNomCta = eaNomFEst;
       #0#16 = 0;
       #0#17 = 0;
       #0#18 = "";
       d_desde = 0;
       d_hasta = 0;
       d_masca = "";
   |FINSI;

   |NOME_DAT #1 aNomCta;

   eaNomFCta = aNomCta;
|FPRC;

|PROCESO &c_pagina;
   |SI sw_pagina = 0;
      pagina = #0#21;
      sw_pagina = 1;

      nPagDup = #0#21;
      nSwPagDup = 1;
   |SINO;
      pagina = pagina + 1;

      |HAZ PDuplex;
   |FINSI;
|FPRC;

|| ***********************************************************************
||                       CALCULOS DE PANTALLA
|| ***********************************************************************
|PROCESO nosaldo; |TIPO 7;
   alfa1 = "S";
   |SI #0#6 = "S";  alfa1 = "N";  #0#22 = "S"; |FINSI;
   |CAMPO_MODIFICA #0#22, 1, alfa1;
   |PINTA #0#22;
|FPRC;

|CALCULO cuenta; |TIPO 6;
     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;

     |SI #0Campo = "            ";  |ACABA;  |FINSI;

     |HAZ SacaNivel;
     digito = Campo + 19;
     #0digito = enNivel;
|FCAL;


|PROCESO mesconta; |TIPO 0;

   varcamp = Campo + 1;                || Coge el campo del nombre del mes
   mes = #0Campo;
   |HAZ mesletra;                      || Recoge el mes en letra
   #0varcamp = letrames;
   |PINTA #0varcamp;                   || Pinta el nombre del mes

   |SI Campo = 4;
       |SI #0#2 > #0#4;
           |MENAV "    ATENCION!!! Error de Entrada ...";
           |ERROR;
           |ACABA; || Si el mes hasta es menor al desde
       |FINSI;
   |FINSI;

|FPRC;

|PROCESO mesletra;
   |SI mes ! 0; |Y mes ! 13;
       mes = #canempre#11 + mes - 1;     || Operacion para saber que mes es
       |SI mes > 12; mes = mes - 12; |FINSI;
   |FINSI;
   |SI mes = 0;  letrames = "Apertura  "; |FINSI;
   |SI mes = 1;  letrames = "Enero     "; |FINSI;
   |SI mes = 2;  letrames = "Febrero   "; |FINSI;
   |SI mes = 3;  letrames = "Marzo     "; |FINSI;
   |SI mes = 4;  letrames = "Abril     "; |FINSI;
   |SI mes = 5;  letrames = "Mayo      "; |FINSI;
   |SI mes = 6;  letrames = "Junio     "; |FINSI;
   |SI mes = 7;  letrames = "Julio     "; |FINSI;
   |SI mes = 8;  letrames = "Agosto    "; |FINSI;
   |SI mes = 9;  letrames = "Septiembre"; |FINSI;
   |SI mes = 10; letrames = "Octubre   "; |FINSI;
   |SI mes = 11; letrames = "Noviembre "; |FINSI;
   |SI mes = 12; letrames = "Diciembre "; |FINSI;
   |SI mes = 13; letrames = "Cierre    "; |FINSI;
|FPRC;

|PROCESO numnivel;|TIPO 0;
   |SI #0#8 > #canempre#13;
      |MENAV error3;
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO nivel; |TIPO 7;  || Calculo para hacer no modificable
   |C_M #0Campo,1,"S";
   |SI Campo = 10; |Y #0#8 < 2; || Si solo hemos seleccionado
       #0Campo = 0; |PINTA #0Campo;
       |C_M #0Campo,1,"N";
   |FINSI;
   |SI Campo = 11; |Y #0#8 < 3; || Si solo hemos seleccionado
       #0Campo = 0; |PINTA #0Campo;
       |C_M #0Campo,1,"N";
   |FINSI;
   |SI Campo = 12; |Y #0#8 < 4; || Si solo hemos seleccionado
       #0Campo = 0; |PINTA #0Campo;
       |C_M #0Campo,1,"N";
   |FINSI;
   |SI Campo = 13; |Y #0#8 < 5; || Si solo hemos seleccionado
       #0Campo = 0; |PINTA #0Campo;
       |C_M #0Campo,1,"N";
   |FINSI;
   |SI Campo = 14; |Y #0#8 < 6; || Si solo hemos seleccionado
       #0Campo = 0; |PINTA #0Campo;
       |C_M #0Campo,1,"N";
   |FINSI;
|FPRC;

|PROCESO nivelrep;|TIPO 0;  || Para comprobar que no repetimos los niveles

   |SI #0Campo > #canempre#13;
      |MENAV error3;
      |ERROR;
      |ACABA;
   |FINSI;

   |SI nivel = 0;        || Buscamo el nivel mas alto;
      nivel = #0#9;
   |FINSI;

   |SI Campo = 9; |ACABA; |FINSI;
   |SI Campo = 10;
      |SI #0#10 = #0#9;|Y #0#10 ! 0;    || Si hay dos niveles iguales error
         |MENAV error1;
         |ERROR;
         |ACABA;
      |SINO;
         |SI #0#10 > #0#9;  || Busca el nivel mas alto
            nivel = #0#10;
         |SINO;
            nivel = #0#9;
         |FINSI;
      |FINSI;
   |FINSI;

   |SI Campo = 11; |Y #0#11 ! 0;
      |SI #0#11 = #0#9; |O #0#11 = #0#10; || Si hay dos niveles iguales error
         |MENAV error1;
         |ERROR;
         |ACABA;
      |FINSI;

      |SI #0#11 > nivel;  || Busca el nivel mas alto
         nivel = #0#11;
      |FINSI;
   |FINSI;

   |SI Campo = 12; |Y #0#12 ! 0;      || Si hay dos niveles iguales error
      |SI #0#12 = #0#9; |O #0#12 = #0#10; |O #0#12 = #0#11;
         |MENAV error1;
         |ERROR;
         |ACABA;
      |FINSI;

      |SI #0#12 > nivel;  || Busca el nivel mas alto
         nivel = #0#12;
      |FINSI;
   |FINSI;

   |SI Campo = 13; |Y #0#13 ! 0;     || Si hay dos niveles iguales error
      |SI #0#13 = #0#9; |O #0#13 = #0#10; |O #0#13 = #0#11; |O #0#13 = #0#12;
         |MENAV error1;
         |ERROR;
         |ACABA;
      |FINSI;

      |SI #0#13 > nivel;  || Busca el nivel mas alto
         nivel = #0#13;
      |FINSI;
   |FINSI;

   |SI Campo = 14; |Y #0#14 ! 0;    || Si hay dos niveles iguales error
      |SI #0#14 = #0#9; |O #0#14 = #0#10; |O #0#14 = #0#11; |O #0#14 = #0#12; |O #0#14 = #0#13;
         |MENAV error1;
         |ERROR;
         |ACABA;
      |FINSI;

      |SI #0#14 > nivel;  || Busca el nivel mas alto
         nivel = #0#14;
      |FINSI;
   |FINSI;


   enLongNivel = 12;
   |PARA nCont = 9; |SI nCont [ 14; |HACIENDO nCont = nCont + 1;
      |SI #0#nCont# ! 0;
         nCalc = #0#nCont#; nCalc = nCalc + 13;
         |SI #canempre#nCalc# < enLongNivel; enLongNivel = #canempre#nCalc#; |FINSI;
      |FINSI;
   |SIGUE;
|FPRC;

|| **********************************************************************
||                       CALCULOS DE IMPRESION
|| **********************************************************************
|PROCESO GrabaAuxiliar;

   #999#0 = #1#0;
   |LEE 041#999=;
   |SI FSalida ! 0;
       |DDEFECTO #999;
       #999#0  = #1#0;  #999#1 = #1#1;
       #999#2  = #1#2;  #999#3 = #1#3;
       #999#4  = #1#4;  #999#5 = #1#5;
       #999#6  = #1#6;  #999#7 = #1#7;
       #999#8  = #1#8;
       #999#54 = #1#54; #999#55 = #1#55;
       #999#56 = #1#56; #999#57 = #1#57;
       #999#58 = #1#58; #999#59 = #1#59;
       #999#60 = #1#60; #999#61 = #1#61;
       |GRABA 020#999c;
   |FINSI;

   |PARA nPara1 = 9; |SI nPara1 [ 53; |HACIENDO nPara1 = nPara1 + 1;
         #999nPara1 = #999nPara1 + #1nPara1;
   |SIGUE;
   |GRABA 020#999a;
   |LIBERA #1;
|FPRC;

|PROCESO condicion;      || Calculo condiciones de impresion
   |HAZ digitos2;
   |SI d_error ! 0;  |ACABA;  |FINSI;
   swprint = 0;         || Variable para saber si imprimimos o no
   swsuma = 0;          || Variable para saber si sumamos o no
   i_debe = 0;          || Pone a cero el total debe del registro
   i_haber = 0;         || Pone a cero el total haber del registro
   i_saldoap = 0;

   |SI sw = 0;
      nivelact = #1#55;     || Nivel actual
      sw = 1;
   |FINSI;

   |SI #1#55 ! 0;     || Condicion para saber si el nivel esta seleccionado
      |SI #1#55 = #0#9;|O #1#55 = #0#10;|O #1#55 = #0#11;      || Compara 3 niveles
         swprint = 1;
      |SINO;
         |SI #1#55 = #0#12;|O #1#55 = #0#13;|O #1#55 = #0#14; || Compara 3 niveles
            swprint = 1;
         |SINO;
            swprint = 0;
         |FINSI;
      |FINSI;
   |FINSI;

   |SI swprint = 1; |Y #1#55 = nivel;      || Si es el nivel mas alto entoces
       swsuma = 1;                         || se suma.
   |FINSI;

   |SI #1#3 = "S";|Y #1#55 [ nivel;   || Si el nivel no esta seleccionado
      swsuma = 1;                || pero tiene en el campo pase una S
      swprint = 1;               || entonces se imprime y se suma.
   |FINSI;

   |SI swprint = 1;    || Si se puede imprimir
      varcamp = ((#0#2 * 3) + 9);
      |SI nSwOp = 1;
          |SI swsuma = 1; |HAZ PrimeroSumo; |FINSI;
      |SINO;
          |SI nSwOp = 2;
              |HAZ GrabaAuxiliar;
          |SINO;
              |SI menu2 = 1; |HAZ operapanta; |FINSI; || Si es por pantalla
              |SI menu2 = 2; |HAZ operaimpre; |FINSI; || Si es por impresora
          |FINSI;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO PrimeroSumo;
   |PARA x = #0#2; |SI x [ #0#4; |HACIENDO x = x + 1;
      i_debe = i_debe + #1varcamp;                   || acumula debe
      varcamp = varcamp + 1;
      i_haber = i_haber + #1varcamp;                 || acumula haber
      varcamp = varcamp + 2;
   |SIGUE;
   i_debe    =  i_debe  ! EURODB;
   i_haber   =  i_haber ! EURODB;

   t_debe     = (t_debe + i_debe) ! EURODB;            || debe
   t_haber    = (t_haber + i_haber) ! EURODB;          || haber
|FPRC;

|PROCESO operapanta;   || Operaciones por pantalla;
   #2#2 = 0;
   #2#3 = 0;

   #2#0 = #1#0;     || Cuenta
   #2#1 = #1#2;     || Descripcion
   #2#12 = #1#3;    || Pase S/N

   vermov = 0;
   |PARA x = #0#2; |SI x [ #0#4; |HACIENDO x = x + 1;  || Bucle de campos

         #2#2 = #2#2 + #1varcamp;    || Suma el debe
         |SI #1varcamp ! 0; vermov = 1;  |FINSI;

         varcamp = varcamp + 1;      || Se situa en  el campo del haber
         #2#3 = #2#3 + #1varcamp;    || Suma el haber
         |SI #1varcamp ! 0; vermov = 1;  |FINSI;

         varcamp = varcamp + 2;      || Se situa en el campo debe
   |SIGUE;

   |SI #0#7 = "S";
       #2#2 = #2#2 + #1#9;         || Suma saldo debe inicial
       #2#3 = #2#3 + #1#10;        || Suma saldo haber inicial
   |FINSI;

   #2#4 = #2#2 - #2#3;             || Coge la diferencia
   aAlfa1 = #2#4; |QBF aAlfa1;
   |SI aAlfa1 = "-0"; |O aAlfa1 = "0";
       #2#4 = 0;
   |FINSI;

   FSalida = 0;

   |SI vermov = 0; |Y #0#6 = "N"; || ctas sin movimientos
      |ACABA;
   |FINSI;

   |SI #2#2 = #2#3; |Y #0#22 = "N"; || cuentas sin saldo
      |ACABA;
   |FINSI;

   |SI swsuma = 1;    || Si se puede sumar los totales
      #2#5 = #2#5 + #2#2;         || Suma el total del debe
      #2#6 = #2#6 + #2#3;         || Suma el total del haber
      #2#7 = #2#7 + #2#4;         || Suma total saldo
   |FINSI;
   #2#11 = Linea;
   Linea = Linea + 1;
   swcomen = 1;              || linea suma anterior
   |GRABA 020#2n;
   |SI 0 [ FSalida;    || Pinta linea por pantalla
         |PINTA #2#5; |PINTA #2#6; |PINTA #2#7;  || Pinta los totales
         Pos = lin;
         Pos = -1;
         lin = lin + 100;     || Posicionar una linea mas abajo
   |FINSI;
|FPRC;

|PROCESO operaimpre;     || impresora
   vermov = 0;
   |PARA x = #0#2; |SI x [ #0#4; |HACIENDO x = x + 1;
      i_debe = i_debe + #1varcamp;                   || acumula debe
      |SI #1varcamp ! 0; vermov = 1; |FINSI;

      varcamp = varcamp + 1;
      i_haber = i_haber + #1varcamp;                 || acumula haber
      |SI #1varcamp ! 0; vermov = 1; |FINSI;

      varcamp = varcamp + 2;
   |SIGUE;

   |SI op_form = 3;                      || acumula saldo inicial
       i_debe    = i_debe  - #1#9;       || debe
       i_haber   = i_haber - #1#10;      || haber
       i_saldoap = #1#11;
   |FINSI;

   i_debe    =  i_debe  ! EURODB;
   i_haber   =  i_haber ! EURODB;
   i_saldoap =  i_saldoap ! EURODB;

   saldo = i_debe - i_haber + i_saldoap;   || saldo
   aAlfa1 = saldo; |QBF aAlfa1;
   |SI aAlfa1 = "-0"; |O aAlfa1 = "0";
       saldo  = 0;
   |FINSI;

   |SI saldo > 0; i_deudor = saldo; i_acreedor = 0;            |FINSI;
   |SI saldo < 0; i_deudor = 0;     i_acreedor = saldo * (-1); |FINSI;
   |SI saldo = 0; i_deudor = 0;     i_acreedor = 0;            |FINSI;


||   |SI i_debe = 0; |Y i_haber = 0; |Y i_saldoap = 0; |Y #0#6 = "N";    || cuentas sin movimientos
||       |ACABA;
||   |FINSI;
   |SI vermov = 0; |Y #0#6 = "N"; |ACABA; |FINSI;

   |SI saldo = 0; |Y #0#22 = "N";    || cuentas sin saldo
       |ACABA;
   |FINSI;

   |SI nivelact ! #1#55;         || cambio de nivel, linea en blanco
      swlinea = 1;
      nivelact = #1#55;
   |SINO;
      swlinea = 0;
   |FINSI;

   |SI #0#8 = 1;  |HAZ artenvas; |FINSI;   || separacion lineas formato ARTENVAS

   |PRINT;
   swcomen = 1;              || linea suma anterior
   |SI swsuma = 1;     || acumula totales
      t_saldoap  = (t_saldoap + i_saldoap) ! EURODB;      || apertura
      t_debe     = (t_debe + i_debe) ! EURODB;            || debe
      t_haber    = (t_haber + i_haber) ! EURODB;          || haber
      t_deudor   = (t_deudor + i_deudor) ! EURODB;        || saldo deudor
      t_acreedor = (t_acreedor + i_acreedor) ! EURODB;    || saldo acreedor
   |FINSI;
|FPRC;

|PROCESO artenvas;
   alfa1 = #1#0;
   alfa1 = alfa1 % 102;

   |SI dos_ant = "xx"; dos_ant = alfa1;    |FINSI;

   |SI alfa1 ! dos_ant;
      dos_ant = alfa1;
      swlinea = 1;          || cuando solo se ha seleccionado un nivel y
   |SINO;                        ||/cambia en sus dos primeros digitos se
      swlinea = 0;          ||/imprime una linea en blanco!!!
   |FINSI;
|FPRC;

|DEFBUCLE caycosy00;      || plan contable (3ra. clave)
   #1#3;
   ;
   #0#0, 1;
   rellenos, 6;
   e;
   NULL, condicion;
|FIN;

|PROCESO Chequeo;
    varcamp = ((#0#2 * 3) + 9);
    i_debe  = 0;          || Pone a cero el total debe del registro
    i_haber  = 0;         || Pone a cero el total haber del registro
    |HAZ PrimeroSumo;
|FPRC;


|DEFBUCLE caycosyC;  ||Chequeo cuadre
   #1#1;
   3;
   "            ", "S";
   "zzzzzzzzzzzz", "S";
   e;
   NULL, Chequeo;
|FIN;

|PROCESO impre;
|| ... Observaciones DsArchi
       eaObservaciones = "";
       |SI #0#2 = #0#4;
           eaObservaciones = "DE " + #0#3;
       |SINO;
           NombreMes = #0#3; |QBF NombreMes;
           eaObservaciones = "DE " + NombreMes + " A ";
           NombreMes = #0#5; |QBF NombreMes;
           eaObservaciones = eaObservaciones + NombreMes;
       |FINSI;
       eaObservaciones = eaObservaciones > " ";

  |BUCLE caycosy00;
  |SI swcomen ! 0;
      |PIEINF;

      |SI nOpcion = 2;
          |FININF;
          nYaInforme = 0;
          |FINIMP;
          |SI swErrorDSArchi = 0;
              |HAZ Archivando;
           |FINSI;
           Impresora = "CrystalReport";
       |FINSI;
  |FINSI;
|FPRC;

|RUTINA inf;
   #2#11= 1;
|FRUT;

|RUTINA sup;
   #2#11 = 99999999999;
|FRUT;

|PROCESO evento;
 |LEE 001#2c;
 eaCuenta = #2#0; |HAZ SacaNivel;
 #2#1 = enNivel;
 |SI #2#12 ! "S"; |ACABA; |FINSI;

 aBlanc = " " * 79;
 |PINTA 0401, aBlanc;
 |SI aparam = "";
     runnom = "caycosy0";
     ext1 = eaCuenta;
     ext2 = enNivel;
     espe15 = "N";
     espe16 = 0;
     |SUB_EJECUTA_NP "caextrap";
 |FINSI;
|FPRC;

|PROCESO panta;
   lin = 100;
   |PINPA #0#2;       || Pinta la pantalla del informe

   |DELINDEX #2;
   |ABRE #2;

   |BUCLE caycosy00;
   |SI swcomen = 0;
       |CIERRA #2;
       |ACABA;
   |FINSI;

   |C_V #2#5,1,"N"; |C_V #2#6,1,"N";
   |C_V #2#7,1,"N"; |C_V #2#8,1,"N";
   |C_V #2#9,1,"N"; |C_V #2#10,1,"N";

   nSwClave = 0;
   |PARA; |SI nSwClave ! -1; |HACIENDO;

          |ATRI I;  |PINTA 0401, #canempre#2;
                    |PINTA 0407, #canempre#1; |ATRI 0;
          |SI aparam = "";
              |ATRI P;
              |PINTA 0456, "<Enter>=Extracto Cuenta";
              |ATRI 0;
          |FINSI;

          |LINEAL_SIMPLE #2#2,28,0502,2079,inf,sup,NULL;
          nSwClave = FSalida;
          |SI nSwClave ! -1;
              |HAZ evento;
          |FINSI;
   |SIGUE;

    |C_V #2#5,1,"S"; |C_V #2#6,1,"S";
    |C_V #2#7,1,"S"; |C_V #2#8,1,"S";
    |C_V #2#9,1,"S"; |C_V #2#10,1,"S";
    |CIERRA #2;

|FPRC;


|PROCESO borrar;
   |DDEFECTO #2;
   sw = 0;
   swcomen = 0;
   t_debe = 0;
   t_haber = 0;
   t_deudor = 0;
   t_acreedor = 0;
   t_saldoap  = 0;
|FPRC;

|| ***********************************************************************
||                       CALCULO ENTRADA DE DATOS
|| ***********************************************************************

|PROCESO rellena;
   rellenos = "zzzzzzzzzzzz";
   menor = -1;

   |PARA para1 = 9; |SI para1 [ 14; |HACIENDO para1 = para1 + 1;
      |SI #0para1 ! 0;
         |SI menor = -1;  menor = #0para1;  |FINSI;
         |SI menor > #0para1;  menor = #0para1;  |FINSI;
      |FINSI;                   || saca el nivel elegido mas peque¤o
   |SIGUE;

   |SI menor ! 0;
      |SI menor = 1;  nume1 = dig4;  |FINSI;
      |SI menor = 2;  nume1 = dig5;  |FINSI;   || construye el hasta segun
      |SI menor = 3;  nume1 = dig6;  |FINSI;   ||/el nivel mas bajo elegido
      |SI menor = 4;  nume1 = dig7;  |FINSI;
      |SI menor = 5;  nume1 = dig8;  |FINSI;
      |SI menor = 6;  nume1 = dig9;  |FINSI;
      nume1 = 100 + nume1;
      rellenos = #0#1 % nume1;
      rellenos = rellenos + "zzzzzzzzzzzz";
      rellenos = rellenos % 112;
   |FINSI;
|FPRC;


|PROCESO HazTodo;

   |SI menu2 = 2; |Y aparam ! "consol";
       eaIa = #30#21;
       enM1 = #0#2;
       enM2 = #0#4;
       |HAZ LeeDatosEmpresa;
       |SI aparam = "fech";
           |SI enM1 ! enM1f;
               mes = enM1f; |HAZ mesletra; #0#3 = letrames;
           |FINSI;
           |SI enM2 ! enM2f;
                mes = enM2f; |HAZ mesletra; #0#5 = letrames;
           |FINSI;
       |FINSI;
   |SINO;
       |HAZ FecApeCie;
   |FINSI;

   |SI enMAper ! dig1;
       |SI #0#4 < enMAper; |Y #0#4 ! 0;
           |ACABA;
       |FINSI;
   |FINSI;

   |NOME_DAT #1 aNomCta;
   |SI aparam ! "fech"; |Y aparam ! "Analitica"; |Y aparam ! "otp"; |Y aparam ! "consol"; |Y nOpcion ! 2;
       enSwOper = 1;
       r_emp = enEmpresa;
       r_per = enPeriodo;
       |SUB_EJECUTA "carecsig";
   |FINSI;
   enSwOper = 0;

   |ABRE #8;
   |DDEFECTO #8;
   #8#0 = 1;
   |LEE 000#8=;
   |CIERRA #8;

   |SI #caconimp#2 = "S";  |Y #caconimp#3 = "N"; |Y nOpcion ! 2;
       |Y aparam ! "fech"; |Y aparam ! "Analitica"; |Y aparam ! "otp"; |Y aparam ! "consol";
          pathbal = dirfich0;
          balance = 1;
          |SI #caconimp#10 = "S";
              enSwOper = 2;
              r_emp = enEmpresa;
              r_per = enPeriodo;
              |SUB_EJECUTA "carecsig";
              enSwOper = 0;
              |SI enECuadre = 1; |ACABA; |FINSI;
              |NOME_DAT #1 aNomCta;
          |SINO;
              |SUB_EJECUTA "careccue";
          |FINSI;
   |FINSI;

   sw_pagina = 0;
   |HAZ rellena;

   |SI nOpcion = 2;
       |SI nYaInforme = 0;
           eaInforme = informe;
           |HAZ ElIMPREArchi;
           |SI swErrorDSArchi = 1;
               |ACABA;
            |FINSI;
            nYaInforme = 1;
      |FINSI;
   |FINSI;

   nSwOp   = 0;
   nSeguir = 0;
   |SI #8#14 = "S"; |Y aparam = ""; |Y nOpcion ! 2;
       nSwOp = 1;
       |BUCLE caycosyC;
       |SI t_debe ! t_haber;
           nSeguir = 1;
           aAlfa1 = #30#2; aAlfa1 = ("00000"+ aAlfa1) % -105;
           aAlfa2 = #30#3; aAlfa2 = ("0"    + aAlfa2) % -101;
           |SI #8#15 = "N";
               aAlfa1 = "    ATENCION.   BALANCE DESCUADRADO. EMPRESA: "+ aAlfa1 + " PERIODO: " + aAlfa2;
               |MENAV aAlfa1;
           |SINO;
               aAlfa1 = "    SATENCION.   BALANCE DESCUADRADO. EMPRESA: "+ aAlfa1 + " PERIODO: " + aAlfa2 + ". Continuar";
               |CONFI aAlfa1;
               |SI FSalida = 0; nSeguir = 0; |FINSI;
           |FINSI;
       |FINSI;
       |HAZ borrar;
       nSwOp = 0;
   |FINSI;

   |SI nSeguir = 0;
       |SI menu2 = 1; |HAZ panta; |FINSI;  || Por pantalla
       |SI menu2 = 2; |HAZ impre; |FINSI;  || Por impresora
   |FINSI;
   |HAZ borrar;
|FPRC;

|PROCESO HazConsol;
   eaIa = #30#21;
   enM1 = #0#2;
   enM2 = #0#4;
   |HAZ LeeDatosEmpresa;

   |SI enMAper ! dig1;
       |SI #0#4 < enMAper; |Y #0#4 ! 0;
           |ACABA;
       |FINSI;
   |FINSI;

   nSw = 1;
   |NOME_DAT #1 aNomCta;
   enSwOper = 1;
   r_emp = enEmpresa;
   r_per = enPeriodo;
   |SUB_EJECUTA "carecsig";
   enSwOper = 0;

   sw_pagina = 0;
   |HAZ rellena;

   nSwOp = 2;
   |ABRE #999;
   |BUCLE caycosy00;
   |CIERRA #999;
   nSwOp = 0;
|FPRC;

||*************************************************************************
|PROCESO ProPrincipal;
   |DFICE dirfichOri;
   nSw = 0;
   nEmpTodo = enEmpresa;
   nPerTodo = enPeriodo;

  |SI #48#27 = "S"; |Y aparam ! "Analitica"; |Y aparam ! "otp";
      |HAZ MultiEmpresa;
  |SINO;
      |SI aparam ! "otp";
          |DFICE dirfich0; |QBF dirfich0;
      |FINSI;
      |HAZ HazTodo;
  |FINSI;

  |SI menu2 = 1;
      |DELINDEX #2;
  |SINO;
      |SI nOpcion = 1;
          |FININF;
          |FINIMP;
          nYaInforme = 0;
      |FINSI;
  |FINSI;
|FPRC;

|CALCULO imprimir; |TIPO 3;
  |SI aparam = "";
      |HAZ ActivaPLB_arc1;
      eaVengoDe = "caycosy0";
      |HAZ MiraSiArchiva;
  |FINSI;

  |CIERRAT #1;
  |HAZ antesque;
  aNomCtaO = aNomCta;

  |MENU menu;
  |SI FSalida < 1; |ACABA; |FINSI;
  menu2 = FSalida;

  enDatos = 0; |SI #0#23 = "S"; enDatos = 1; |FINSI;
  |SI menu2 = 1;

      aFichero = ("cos" + Usuario) % 108;
      |NOME_DAT #2 aFichero;
      |HAZ ProPrincipal;
      |DELINDEX #2;

  |SINO;

      |SI nArchi = 1;
          |MENU aMenu;
          nOpcion = FSalida;
          |SI nOpcion = 0;
             |ACABA;
          |FINSI;
      |SINO;
          nOpcion = 1;
      |FINSI;

      |SI nOpcion = 3;  |SALVA_DATOS #0;  |FINSI;

      nOpSegur = nOpcion;
      |SI nOpSegur = 1; |O nOpSegur = 3;
          nOpcion = 1;
          |HAZ impre_Uno;
          |SI nOpSegur = 3;  |REPON_DATOS #0;  |FINSI;
      |FINSI;

      |SI nOpSegur = 2; |O nOpSegur = 3;
          aNomCta = aNomCtaO;
          nOpcion = 2;
          |HAZ impre_Uno;
      |FINSI;
   |FINSI;
|FCAL;

|PROCESO impre_Uno;
   |SI nOpcion = 1; |Y nOpSegur ! 3;
      |MENU form;
      op_form = FSalida;
   |SINO;
      op_form = 1;
   |FINSI;

   |SI op_form > 0;
       |SI op_form = 3; |Y #0#2 > 0; op_form = 1; |FINSI;

       |SI op_form = 1;
           |HAZ los_informes;
           informe = que_i1;
           |SI #0#23 = "N"; |Y informe = "caycosy0"; informe = "caycosy1"; |FINSI;
       |SINO;
           |SI op_form = 2;
               |SI #0#23 = "S"; informe = "caycosz0"; |FINSI;
               |SI #0#23 = "N"; informe = "caycosz1"; |FINSI;
           |SINO;
               informe = "caycosya";
           |FINSI;
       |FINSI;
   |SINO;
       |ACABA;
   |FINSI;

   |SI nOpcion = 1;
       |SI PARAMETRO = "AGENCIA";
           Impresora = "CrystalReport";
           |IMPRE -1;
        |SINO;
           Impresora = "Crystal Reports";
           |IMPRE 0;
        |FINSI;
        |SI FSalida ! 0; |ACABA; |FINSI;
   |SINO;
        Impresora = "CrystalReport";
   |FINSI;

   aAlfa = Impresora;
   aAlfa = aAlfa % 113;
   |SI aAlfa = "CrystalReport";
       informe = "cc" + (informe % 0306);
       |SI informe = "ccycosy1"; informe = "ccycosy0"; |FINSI;
       |SI informe = "ccycosz1"; informe = "ccycosz0"; |FINSI;
       |SI nArchi = 0;
           |CONFI "0000NOrientacion horizontal";
           |SI FSalida = 0;
               informe = (informe % 0103) + "x" + (informe % 504);
           |FINSI;
       |FINSI;
   |FINSI;

   |SI nOpcion = 1;
       |INFOR informe;
       |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;
   |FINSI;

   |HAZ ProPrincipal;
|FPRC;

||********************************************************************
|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   nYaLoHeDicho = 0;
   |PATH_DAT #1 dirfich0;
   |PATH_DAT #8 dirfich0;

   |SI espe17 ! "S";
       |HAZ HazTodo;
   |SINO;
       |HAZ HazConsol;
   |FINSI;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 000001;
 999999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;

  |SI nOpSegur ! 3; |O nOpcion = 1;
      eSwSuma = 0; espe17  = "";
      |SI #8#17 = "S"; |Y aparam = ""; eSwSuma = 1; |FINSI;
      |SUB_EJECUTA "caselemp";
      |NOME_DAT #998 eaFichsel;
  |FINSI;

  |SI espe17 = "S";

      |DBASE aMas;
      aDef  = aMas + "def/cacuenta";
      |CARGA_DEF aDef, cuentat@;
      |SI FSalida ! 0;
          |MENAV "    Error Cargado Fichero de Cuentas. Proceso Interrumpido";
          |ACABA;
      |FINSI;
      aFichero = "0y" + Usuario;
      |NOME_DAT #999 aFichero;
      |DELINDEX #999;
  |FINSI;

  |BUCLE MultiEmp0;
  |SI nOpSegur ! 3; |O nOpcion = 2;
      |DELINDEX #998;
  |FINSI;

  |SI espe17 = "S"; |Y  nSw ! 0;

      cod1 = nEmpTodo;
      cod2 = nPerTodo;
      |HAZ leelo;

      dirfich0 = dirfichOri;
      eaIa = #30#21;
      enM1 = #0#2;
      enM2 = #0#4;
      |HAZ LeeDatosEmpresa;
      aAlfa1 = #cawdatos#2; |QBF aAlfa1;
      aAlfa1 = aAlfa1 + " (Consolidada)";
      #cawdatos#2 = aAlfa1;
      aparam = "consol";

      aNomCta   = aFichero;
      eaNomFCta = aFichero;
      |PATH_DAT #1 dirfich0;
      |PATH_DAT #8 dirfich0;

      |HAZ borrar;
      |HAZ HazTodo;
      nYaInforme = 0;
      |DESCARGA_DEF #cuentat@;
  |FINSI;

|FPRC;

|PROGRAMA;
   aparam = PARAMETRO; |QBF aparam;
   |SI aparam = ""; |O aparam = "fech"; |O aparam = "Analitica";
       |O aparam = "otp"; |O aparam = " N";
       |HAZ inicial;
       |SI swerror = 2; |ACABA; |FINSI;
       |SI swerror = 3; |PTEC 831; |ACABA; |FINSI;
       |SI aparam = " N"; aparam = ""; |FINSI;



       |MANTE #0;
   |SINO;
        |HAZ Legalia;
   |FINSI;
|FPRO;

|PROCESO Legalia;
       Comodin = PARAMETRO;
       aAlfa = Comodin % -102; Comodin = Comodin - aAlfa;

       |DDEFECTO #0;

       #0#22 = "N";
       |SI aAlfa = ",S";  #0#22 = "S"; |FINSI;

       Comodin = Comodin - ",";
       |SI FSalida ! 0;
          Calc = ((FSalida / 100) ! 0) + 99;
          Comodin1 = Comodin % 0101;
          Per1 = Comodin1; Comodin = Comodin - Comodin1;
          Per2 = Comodin;
       |FINSI;

       nArchi = 0;
       ||... Legalia
       cod1 = eaLegEmp;
       cod2 = eaLegPer;
       emEmp = 1;  || Sin mensaje
       |HAZ leelo;
       |SI swerror ! 0;  |ACABA;   |FINSI;

       |DBASE fich_alta; |QBF fich_alta;
       fich_alta = fich_alta + "fich/" + eaLegEmp + eaLegPer + "/";
       dirfich0 = fich_alta;
       #0#2 = Per1;
       #0#4 = Per2;

       |HAZ FecApeCie;
       |SI enMAper ! dig1;
           |SI #0#4 < enMAper; |Y #0#4 ! 0;
               |ACABA;
           |FINSI;
       |FINSI;

       |PATH_DAT #1 dirfich0;
       |PATH_DAT #8 dirfich0;

       Impresora = eaNomImp + "?" + eaUnidadBTmp + "\tmp1,,,?";
       |SI Per2 = 0;  Impresora = Impresora + "Apertura"; |FINSI;
       |SI Per2 = 3;  Impresora = Impresora + "1T"; |FINSI;
       |SI Per2 = 6;  Impresora = Impresora + "2T"; |FINSI;
       |SI Per2 = 9;  Impresora = Impresora + "3T"; |FINSI;
       |SI Per2 = 12; Impresora = Impresora + "4T"; |FINSI;

       |SI enBasImp ! 0;
           |IMPRE -1;
       |SINO;
           |IMPRE 0;
       |FINSI;

       #0#0  = "            "; #0#19 = 1;
       #0#1  = "zzzzzzzzzzzz"; #0#20 = dig3;

       |SI Per1 ] 0; |O Per2 ] 0;
           #0#2  = Per1;       #0#4  = Per2;
       |SINO;
           #0#2  = #30#11;     #0#4  = #30#12;
       |FINSI;

       mes = #0#2; |HAZ mesletra; #0#3 = letrames;
       mes = #0#4; |HAZ mesletra; #0#5 = letrames;

       #0#8  = #30#13;
       |SI eaUltNiv = "N"; #0#8 = #30#13 - 1; |FINSI;
       nivel = #0#8;
       |SI #0#8 ] 1; #0#9  = 1; |FINSI;
       |SI #0#8 ] 2; #0#10 = 2; |FINSI;
       |SI #0#8 ] 3; #0#11 = 3; |FINSI;
       |SI #0#8 ] 4; #0#12 = 4; |FINSI;
       |SI #0#8 ] 5; #0#13 = 5; |FINSI;
       |SI #0#8 ] 6; #0#14 = 6; |FINSI;

       menu2 = 2;
       #0#21 = 1;
       #0#23 = eaDatLeg;
       #0#15 = efFecLeg;
       |SI menu2 = 2;
           eaIa = #30#21;
           enM1 = #0#2;
           enM2 = #0#4;
           |HAZ LeeDatosEmpresa;
       |FINSI;

       sw_pagina = 0;
       |HAZ rellena;

       |HAZ los_informes;
       informe = que_i1;
       |SI #0#23 = "N"; |Y informe = "caycosy0"; informe = "caycosy1"; |FINSI;

       |INFOR informe;
       |SI FSalida = 0;
           |BUCLE caycosy00;
           |SI swcomen ! 0;
               |PIEINF;
           |FINSI;
           |FININF;
       |SINO;
           |MENAV error2;
       |FINSI;
       |FINIMP;

       |HAZ borrar;
|FPRC;
