|FICHEROS;
    n43ztras #0;
    n43datos #1;
    caconcic #2;
    caconcil #3;
    cacuenta #10;
|FIN;

|VARIABLES;
   &entrada = 1;
   sw     = 0;
   cuenta = "";
   digito = 0;
   linea  = 0;
   feini  = @;
   fefin  = @;
|FIN;

|INCLUYE dscont_i;

|PROCESO cuenta;|TIPO 6;
   eaCuenta = #0Campo;
   salidas  = FSalida;
   |HAZ FiltraPuntos;
   #0Campo = eaCuenta; |PINTA #0Campo;
|FPRC;

|CALCULO Tipo7; |TIPO 7;
   #0#2 = fec1; |PINTA #0#2;
   #0#3 = fec2; |PINTA #0#3;
|FCAL;

|| ***********************************************
|PROCESO miracuen;
   |DDEFECTO #10;
   #10#0 = eaCuenta;
   |LEE 040#10=;
   #2#2 = #10#2;
|FPRC;

|PROCESO EmpiezaCta;
   eaCuenta = cuenta;
   |HAZ SacaNivel;

   linea = 0;
   #2#0  = eaCuenta;
   #2#1  = enNivel;
   |LIBERA #2;
   |LEE 110#2=;
   |SI FSalida ! 0;
       |DDEFECTO #2;
       #2#0 = eaCuenta;
       #2#1 = enNivel;
       |HAZ miracuen;
       #2#3 = #0#4;  ||a fecha
       |GRABA 020#2n;
   |SINO;
       #2#3 = #0#4;  ||a fecha
       |GRABA 020#2a;
   |FINSI;

   linea = 0;
   #3#0 = eaCuenta;
   #3#2 = 999;
   |LEE 010#3];
   |SI FSalida = 0;
      |LEE 040#3a;
   |SINO;
      |LEE 040#3u;
   |FINSI;
   |SI FSalida = 0; |Y #3#0 = eaCuenta;
       linea = #3#2;
   |FINSI;
|FPRC;

|PROCESO pasalo;

   |SI sw = 0;
       cuenta = #1#1;
       |HAZ EmpiezaCta;
       sw     = 1;
   |FINSI;

   |SI cuenta ! #1#1;
       cuenta = #1#1;
       |HAZ EmpiezaCta;
   |FINSI;

   linea = linea + 1;
   #3#0 = #1#1;
   #3#1 = enNivel;
   #3#2 = linea;
   #3#3 = 1;
   #3#4 = #1#2;
   #3#5 = #1#5;
   |SI #1#6 = "D";
       #3#6 = #1#7;
       #3#7 = 0;
   |SINO;
       #3#7 = #1#7;
       #3#6 = 0;
   |FINSI;
   #3#8 = " ";
   #3#9 = #1#8;  ||documento
   |GRABA 040#3n;

   |BORRA 020#1a;
   |LIBERA #1;
|FPRC;

|DEFBUCLE n43datos;
   #1#3;
   9, 11;
   #0#0, feini, "N", "N";
   #0#1, fefin, "N", "N";
   be;
   NULL, pasalo;
|FIN;

|PROCESO pase; |TIPO 3;

   |SI #0#5 = "S";
       |ABRE #2;
       |ABRE #3;
       |ABRE #10;
       feini = #0#2;
       fefin = #0#3;
       |BUCLE n43datos;
       |CIERRA #10;
       |CIERRA #3;
       |CIERRA #2;
   |FINSI;
|FPRC;
