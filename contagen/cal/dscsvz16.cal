|FICHEROS;
     dscsvz16;

     canempre;
     cacuenta;
     caclipro;
     camaasic;
     camaasil;

     :modelos/dsmom030;

     dscsvw06;
     dscsvw07;
     dscsvw10;

     &regisnif@;
|FIN;

|VARIABLES;
     nDigiOrigen     = 0;
     nDigiDestino    = 0;
     nOrden1         = 0;
     nOrden2         = 0;
     nPausa          = 0;
     nImpor          = 0;
     nOk             = 0;

     aNombre1        = "";
     aNombre2        = "";
     aNombre3        = "";
     aNombre4        = "";
     aModificaAsiIva = "";
     aFicheroLCK     = "";
     aPathContagen   = "";
     aBorra          = "";
     aEjecuta        = "";
     aMensaje        = "";
     aFicheroMDB     = "";
     aFicheroAux     = "";
     nBtnInfo        = -1;
     aPathDavid      = "";
     aTextoVentana   = "";
     aDRef           = "";
     aHRef           = "";
     aBaseConta      = "";
     aAbre           = "";
     aNumAsi         = "";
     aNomCta         = "";
     aNIFCta         = "";
     a17             = "";
     a25             = "";
     aLen            = "";
     aCaracter       = "";

     n0Vent          = -1;
     n1Vent          = -1;
     n2Vent          = -1;
     nBtnUni         = -1;
     nBtnEOS         = -1;
     nBtnCon         = -1;
     nBtnDef         = -1;
     nBtnCan         = -1;
     nBtnCar1        = -1;
     nBtnCar2        = -1;
     nBtnCar3        = -1;
     nBtnCar4        = -1;
     nLblReg1        = -1;
     nLblReg2        = -1;
     nLblReg3        = -1;
     nLblReg4        = -1;
     nLblReg5        = -1;
     nLblImp         = -1;
     nGrid11         = -1;
     nLen            = 0;

     nRango          = 0;
     nHandle         = 0;
     nIde            = 0;
     nDCampo         = 0;
     nCampo          = 0;
     nPanta          = 0;
     nLong           = 0;
     nCarac          = 0;
     nPos            = 0;
     nFSalida        = 0;
     nColum          = 0;
     nNume1          = 0;
     nNume2          = 0;
     nRam1           = 0;
     nRam2           = 0;
     nPide           = 0;
     nFila           = 0;
     nCargar         = 0;
     nPosi           = 0;
     nTotCmp         = 0;
     nTCampos        = 0;
     nTValor         = 0;
     nReg            = 0;
     nPorce          = 0;
     nError          = 0;
     nCmp            = 0;
     nLongVar        = 0;
     nCarVar         = 0;
     nPosVar         = 0;
     nDife           = 0;
     nDesdeCar       = 0;
     nPos1           = 0;
     nPos2           = 0;
     nChequeo        = 0;
     nDocu           = 0;
     nFact           = 0;
     nAsiento        = 0;
     nApun           = 0;
     nBarra          = 0;
     nDia            = 0;
     nMes            = 0;
     nAny            = 0;
     nLibro          = 0;
     nIBas           = 0;
     nIIva           = 0;
     nITot           = 0;
     nIRet           = 0;
     nConcep         = 0;
     nBuscoCli       = 0;
     nPrint          = 0;
     nDiario         = 0;
     nDocum          = 0;
     nIVA            = 0;
     nLin            = 0;
     nNume           = 0;
     nPasada         = 0;

     aCtaBas         = "";
     aCtaIva         = "";
     aCtaTot         = "";
     aCtaRec         = "";
     aCtaRet         = "";
     aConcep         = "";
     aAlfa           = "";
     aAlf1           = "";
     aAlfa1          = "";
     aAlfa2          = "";
     aAlfa3          = "";
     aFecha          = "";
     aEsc1           = "";
     aEsc2           = "";
     aRetu           = "";
     aTecla          = "";
     aPathLocal      = "";
     aPathTmp        = "";
     aFicTree        = "";
     aTabla          = "";
     aTablaTree      = "";
     aFilaTree       = "";
     aDescri         = "";
     aRuta           = "";
     aLong           = "";
     aCadena         = "";
     aOrigen         = "";
     aDestino        = "";
     aDTabla         = "";
     aHTabla         = "";
     aBuscar         = "";
     aRefresca       = "";
     aCarac          = "";
     aContenido      = "";
     a100            = "";
     aVar            = "";
     aLee            = "";
     aC10            = "";
     aC13            = "";
     aFicTmp         = "";
     aFicCSV         = "";
     aLongVar        = "";
     aComilla        = "";
     aCarVar         = "";
     aTipo           = "";
     aAsiento        = "";
     aReferencia     = "";
     aLibro          = "";
     aParam1         = "";
     aParam2         = "";
     aParam3         = "";
     aProg           = "";
     aFicBrowse      = "";
     aVer1           = "";
     aVer2           = "";
     aModAsiento     = "";
     aNomNif         = "";

     fFecha          = @;
     fFechaFac       = @;

     {1}aConte       = "";

     {-1}sTree       = 0;
         sT_nID      = 0;
         sT_nOper    = 0;
         sT_aData    = "";

     {-1}aVent       = "";
         aVent1      = 0;
         aVent2      = 0;
         aVent3      = 0;
         aVent4      = 0;
         aVent5      = "";

     &eaUnidadBasico = "";
     &eaIP           = "";
     &eaModoImpo     = "";
     &eaRepeSopo     = "";
     &eaFicheroCSV   = "";
     &eaTipoCSV      = "";
     &eaFicheroIVA   = "";
     aAnyo           = "";
     &eaFicheroDatos = "";
     &eaNombreDatos  = "";
     &eaBinarioConve = "";
     &eaNombrePDF    = "";
|FIN;

|INCLUYE dscont_i;

|PROCESO CalculaFecha;
     |QBT aFecha;
     aLongVar = aFecha % 0;
     nLongVar = aLongVar;
     nBarra   = 0;
     aAlfa1   = "";
     aAlfa2   = "";
     aAlfa3   = "";
     |PARA nCarVar = 1;  |SI nCarVar [ nLongVar;  |HACIENDO nCarVar = nCarVar + 1;
           nPosVar = (nCarVar * 100) + 1;
           aCarVar = aFecha % nPosVar;

           |SI  aCarVar = "0"; |O aCarVar = "1"; |O aCarVar = "2";
             |O aCarVar = "3"; |O aCarVar = "4"; |O aCarVar = "5";
             |O aCarVar = "6"; |O aCarVar = "7"; |O aCarVar = "8";
             |O aCarVar = "9";
                |SI nBarra = 0;  aAlfa1 = aAlfa1 + aCarVar;  |FINSI;
                |SI nBarra = 1;  aAlfa2 = aAlfa2 + aCarVar;  |FINSI;
                |SI nBarra = 2;  aAlfa3 = aAlfa3 + aCarVar;  |FINSI;
           |SINO;
                |SI aCarVar = ".";  |O aCarVar = "/";  |O aCarVar = "\";
                    nBarra = nBarra + 1;
               |FINSI;
           |FINSI;
      |SIGUE;

      |QBF aAlfa1;
      |QBF aAlfa2;
      |QBF aAlfa3;

      nDia = aAlfa1;
      nMes = aAlfa2;
      nAny = aAlfa3;

      |SI nAny < 1000;
          nAny = 2000 + nAny;
      |FINSI;

      |SI nDia = 0;  nDia = 1;                    |FINSI;
      |SI nMes = 0;  nMes = 1;                    |FINSI;
      |SI nAny = 0;  nAny = 2000 + #canempre#26;  |FINSI;

      aFecha = (("00" + nDia) % -102);
      aFecha = aFecha + "." + (("00" + nMes) % -102);
      aFecha = aFecha + "." + (("0000" + nAny) % -104);
|FPRC;

|PROCESO MiraRaros;
     |QBF aAlf1;
     aCadena = "";
     aLen    = aAlf1 % 0;
     nLen    = aLen;
     |PARA nCampo = 1;  |SI nCampo [ nLen;  |HACIENDO nCampo = nCampo + 1;
           nPos = (100 * nCampo) + 1;
           aCaracter = aAlf1 % nPos;

           aCarac = &209;  |SI aCaracter = aCarac;  aCaracter = "¥"; |FINSI;
           aCarac = &241;  |SI aCaracter = aCarac;  aCaracter = "¤"; |FINSI;
           aCarac = &186;  |SI aCaracter = aCarac;  aCaracter = "§"; |FINSI;
           aCarac = &170;  |SI aCaracter = aCarac;  aCaracter = "¦"; |FINSI;
           aCarac = &225;  |SI aCaracter = aCarac;  aCaracter = " "; |FINSI;
           aCarac = &233;  |SI aCaracter = aCarac;  aCaracter = "‚"; |FINSI;
           aCarac = &237;  |SI aCaracter = aCarac;  aCaracter = "¡"; |FINSI;
           aCarac = &243;  |SI aCaracter = aCarac;  aCaracter = "¢"; |FINSI;
           aCarac = &250;  |SI aCaracter = aCarac;  aCaracter = "£"; |FINSI;
           aCarac = &210;  |SI aCaracter = aCarac;  aCaracter = "O"; |FINSI;
           aCarac = &211;  |SI aCaracter = aCarac;  aCaracter = "O"; |FINSI;

           aCadena = aCadena + aCaracter;
     |SIGUE;
     aAlf1 = aCadena;
|FPRC;

|PROCESO QuitaPuntos;
     |QBT aAlfa;
     aLongVar = aAlfa % 0;
     nLongVar = aLongVar;
     aAlfa1   = "";
     |PARA nCarVar = 1;  |SI nCarVar [ nLongVar;  |HACIENDO nCarVar = nCarVar + 1;
           nPosVar = (nCarVar * 100) + 1;
           aCarVar = aAlfa % nPosVar;

           |SI  aCarVar ! ".";
                aAlfa1 = aAlfa1 + aCarVar;
           |FINSI;
      |SIGUE;

      aAlfa = aAlfa1;
|FPRC;

|PROCESO CalculaCuenta;
     |QBF eaCuenta;
     |SI eaCuenta = "";  |ACABA;  |FINSI;

     aLongVar = eaCuenta % 0;
     nLongVar = aLongVar;

     |SI nLongVar ! MaximoDigitos;
         nDife = MaximoDigitos - nLongVar;
         |SI nDife < 0;  |ACABA;  |FINSI;

         nDesdeCar = 5;

         nPos1    = 100 + nDesdeCar;
         nPos2    = (100 * (nDesdeCar + 1)) + 99;
         aAlfa    = (eaCuenta % nPos1) + ("0" * nDife) + (eaCuenta % nPos2);
         eaCuenta = aAlfa;
     |FINSI;

     |HAZ SacaNivel;
|FPRC;

|PROCESO dscsvw10Campos;
     nTCampos  = nTCampos + 1;
|FPRC;

|DEFBUCLE dscsvw10Campos;
     #dscsvw10#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dscsvw10Campos;
|FIN;

|PROCESO GrabaAsiento;
     #dscsvw06#0  = nReg;

     |SI nCampo = 1;
         aFecha      = #dscsvw10#1;
         |HAZ CalculaFecha;
         #dscsvw06#2 = aFecha;
     |FINSI;

     |SI nCampo = 2;  #dscsvw06#5  = #dscsvw10#1;  |FINSI;
     |SI nCampo = 4;  #dscsvw06#8  = #dscsvw10#1;  |FINSI;

     |SI nCampo = 5;
         nNume = #dscsvw10#1;
         |SI nNume ! 0;
             |HAZ QuitaPuntos;
             #dscsvw06#10  = nNume;
             #dscsvw06#9   = "D";

             |SI #dscsvw06#10 < 0;
                 #dscsvw06#10  = -#dscsvw06#10;
                 #dscsvw06#9   = "H";
             |FINSI;
         |FINSI;
     |FINSI;

     |SI nCampo = 6;
         nNume = #dscsvw10#1;
         |SI nNume ! 0;
             |HAZ QuitaPuntos;
             #dscsvw06#10  = nNume;
             #dscsvw06#9   = "H";

             |SI #dscsvw06#10 < 0;
                 #dscsvw06#10  = -#dscsvw06#10;
                 #dscsvw06#9   = "D";
             |FINSI;
         |FINSI;
     |FINSI;

     |SI nCampo = 0;   #dscsvw06#25  = #dscsvw10#1;  |FINSI;  ||Asiento
|FPRC;

|PROCESO dscsvw10Carga;
     nCampo = #dscsvw10#0 - 1;

     |SI aTabla = "cacuenta";
                          #dscsvw07#0 = nReg;
         |SI nCampo = 0;  #dscsvw07#1 = #dscsvw10#1;  |FINSI;
         |SI nCampo = 1;  #dscsvw07#3 = #dscsvw10#1;  |FINSI;
     |FINSI;

     |SI aTabla = "asientos";
         |HAZ GrabaAsiento;
     |FINSI;
|FPRC;

|DEFBUCLE dscsvw10Carga;
     #dscsvw10#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dscsvw10Carga;
|FIN;

|PROCESO Print;
     |SI aTabla = "cacuenta";
         |SI nLblReg1 ! -1;
             |FIN_CONTROL_WINDOWS nLblReg1;
         |FINSI;

         aAlfa =  "LABEL,{{15}}" + (("00000" + nReg) % -105);
         |CONTROL_SIMPLE nPanta, aAlfa, 1190, 1198, NULL;
         nLblReg1 = FSalida;
     |FINSI;

     |SI aTabla = "asientos";
         |SI nLblReg2 ! -1;
             |FIN_CONTROL_WINDOWS nLblReg2;
         |FINSI;

         aAlfa =  "LABEL,{{15}}" + (("00000" + nReg) % -105);
         |CONTROL_SIMPLE nPanta, aAlfa, 1490, 1498, NULL;
         nLblReg2 = FSalida;
     |FINSI;
|FPRC;

|PROCESO GrabaDatos;
     |CIERRA #dscsvw10;
     nTCampos = 0;
     |BUCLE dscsvw10Campos;

     nError   = 0;
     nTValor  = nTotCmp;
     |SI nTCampos < nTValor;
         |DELINDEX #dscsvw10;
         |ACABA;
     |FINSI;

     nPorce = ((nTValor * 100) / nTCampos) ! 0;
     |SI nPorce < 70;
         |DELINDEX #dscsvw10;
         |ACABA;
     |FINSI;

     nReg = nReg + 1;
     |SI nReg = 1;
         |DELINDEX #dscsvw10;
         |ACABA;
     |FINSI;

     |SI aTabla = "cacuenta";
         |DDEFECTO #dscsvw07;

         |BUCLE dscsvw10Carga;

         |GRABA 020#dscsvw07.n;
         |LIBERA #dscsvw07;
     |FINSI;

     |SI aTabla = "asientos";
         |DDEFECTO #dscsvw06;

         |BUCLE dscsvw10Carga;

         |GRABA 020#dscsvw06.n;
         |LIBERA #dscsvw06;
     |FINSI;

     |DELINDEX #dscsvw10;
     |PULSATECLA;
|FPRC;


|PROCESO Grabaw10;
     aAlf1 = aVar;  |HAZ MiraRaros;  aVar = aAlf1;

     #dscsvw10#0 = nCmp;
     #dscsvw10#1 = aVar;
     |GRABA 020#dscsvw10.n;
     |LIBERA #dscsvw10;
|FPRC;

|PROCESO SacaDatos;
     aLong  = aLee % 0;
     nLong  = aLong;
     aC10   = &10;
     aC13   = &13;
     |PARA nCarac = 1;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos   = (nCarac * 100) + 1;
           aCarac = aLee % nPos;

           |SI aCarac = aC13;
               aCarac = ";";
           |FINSI;

           |SI aCarac = ";";
               nCmp   = nCmp + 1;
               |HAZ Grabaw10;

               aVar   = "";
               aCarac = "";
           |FINSI;

           |SI aCarac = aC10;
               aCarac = "";
           |FINSI;

           |SI nCmp = nTotCmp;  |Y nCmp ! 0;
               |HAZ GrabaDatos;

               |ABRE #dscsvw10;

               nCmp = 0;
           |FINSI;

           aVar = aVar + aCarac;
      |SIGUE;
|FPRC;

|PROCESO LeeCSV;
     aAlfa = "Procesando registros del fichero " + aTabla;
     |VENTANA 0501, 0540, -1, 16, aAlfa;
     n2Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n2Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     |ABRE #dscsvw10;

     nCmp  = 0;
     nReg  = 0;
     |XABRE "B", aAbre, nHandle;
     |PARA;  |SI;  |HACIENDO;
          |XLEE nHandle, 255, aLee;
          |SI FSalida [ 0;  |SAL;  |FINSI;

          |HAZ SacaDatos;
     |SIGUE;
     |XCIERRA nHandle;

     |LEE 000#dscsvw10.p;
     |SI FSalida = 0;
         nCmp   = nCmp + 1;
         |HAZ Grabaw10;
         |HAZ GrabaDatos;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n2Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n2Vent;

     |HAZ Print;

     |ABRE #dscsvw10; |CIERRA #dscsvw10; |DELINDEX #dscsvw10;
|FPRC;

|PROCESO dscsvw07Cheq;
     aAlfa = #dscsvw07#0;  |QBF aAlfa;
     |SI aAlfa = "";
         |MENAV "0000Registros con cuentas sin contenido. Cancelamos la importaci¢n";

         nError = 1;
         |ERROR10;
         |ACABA;
     |FINSI;

     eaCuenta = #dscsvw07#1;
     |HAZ CalculaCuenta;

     #dscsvw07#1 = eaCuenta;
     #dscsvw07#2 = dig3;

     |GRABA 020#dscsvw07.a;
     |LIBERA #dscsvw07;
|FPRC;

|DEFBUCLE dscsvw07Cheq;
     #dscsvw07#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL, dscsvw07Cheq;
|FIN;

|PROCESO dscsvw07Gra;
     #cacuenta#0 = #dscsvw07#1;
     |LEE 000#cacuenta.=;
     |SI FSalida = 0;  |ACABA;  |FINSI;

     #cacuenta#0 = #dscsvw07#1;
     #cacuenta#1 = #dscsvw07#2;
     #cacuenta#2 = #dscsvw07#3;
     #cacuenta#3 = "S";

     |GRABA 020#cacuenta.n;
     |LIBERA #cacuenta;

     nOk   = 0;
     aAlfa = #cacuenta#0 % 102;
     |SI aAlfa = "40";  nOk = 1;  |FINSI;
     |SI aAlfa = "41";  nOk = 1;  |FINSI;
     |SI aAlfa = "43";  nOk = 1;  |FINSI;
     |SI aAlfa = "44";  nOk = 1;  |FINSI;

     |SI nOk = 0;  |ACABA;  |FINSI;

     |SI aAlfa = "40";  |O aAlfa = "41";
         aTipo = "P";
     |FINSI;

     |SI aAlfa = "43";  |O aAlfa = "44";
         aTipo = "C";
     |FINSI;

     |SI aTipo = "";  |ACABA;  |FINSI;

     #caclipro#23 = aTipo;
     #caclipro#10 = #cacuenta#0;
     |LEE 101#caclipro.=;
     |SI FSalida ! 0;
         |DDEFECTO #caclipro;
         #caclipro#23 = aTipo;
         #caclipro#10 = #cacuenta#0;

         |GRABA 020#caclipro.b;
     |FINSI;

     #caclipro#1  = #cacuenta#2;

     |GRABA 020#caclipro.a;
     |LIBERA #caclipro;
|FPRC;

|DEFBUCLE dscsvw07Gra;
     #dscsvw07#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dscsvw07Gra;
|FIN;

|PROCESO CreaCuentas;
     aAbre = #dscsvz16#0;  |QBF aAbre;
     |SI aAbre = "";  |ACABA;  |FINSI;

     aFicTmp = "dscsvw07" + Usuario;  |NOME_DAT #dscsvw07#aFicTmp#;
     aFicTmp = "dscsvw10" + Usuario;  |NOME_DAT #dscsvw10#aFicTmp#;

     |ABRE #dscsvw07;  |CIERRA #dscsvw07;  |DELINDEX #dscsvw07;

     nTotCmp  = 2;

     |ABRE #dscsvw07;
     aTabla = "cacuenta";
     |HAZ LeeCSV;
     |CIERRA #dscsvw07;

     nError = 0;
     |BUCLE dscsvw07Cheq;
     |SI nChequeo = 1;
         |CONFI "0000SRegistros de cuentas preparados para importar. Quiere consultarlos antes?";
         |SI FSalida = 0;
             |ABRE #dscsvw07;
             |CONSULTA_CLAVE #1#dscsvw07;
             |CIERRA #dscsvw07;
         |FINSI;
     |FINSI;

     |BUCLE dscsvw07Gra;
|FPRC;

|PROCESO dscsvw06Cheq;
     aAlfa = #dscsvw06#8;  |QBF aAlfa;
     aAlfa = aAlfa - "TOTAL ASIENTO:";
     |SI FSalida ! 0;
         |BORRA 020#dscsvw06.a;
         |LIBERA #dscsvw06;
         |ACABA;
     |FINSI;

     aAlfa = aAlfa - "TOTAL GENERAL";
     |SI FSalida ! 0;
         |BORRA 020#dscsvw06.a;
         |LIBERA #dscsvw06;
         |ACABA;
     |FINSI;

     eaCuenta = #dscsvw06#5;
     |HAZ CalculaCuenta;
     #dscsvw06#5  = eaCuenta;
     #dscsvw06#6  = dig3;

     |SI #dscsvw06#25 ! 0;
         nApun    = 0;
         nDocu    = nDocu + 1;
         nAsiento = #dscsvw06#25;
     |SINO;
         #dscsvw06#25 = nAsiento;
     |FINSI;

     #dscsvw06#3  = nDocu;
     #dscsvw06#1  = 1;
     #dscsvw06#7  = 1;

     aAlfa = #dscsvw06#8 > "";

     aAlfa1 = aAlfa - "APERTURA";
     |SI FSalida ! 0;
         #dscsvw06#1 = 0;
         #dscsvw06#7 = 96;
     |FINSI;

     aAlfa1 = aAlfa - "SALDO INICIAL";
     |SI FSalida ! 0;
         #dscsvw06#1 = 0;
         #dscsvw06#7 = 96;
     |FINSI;

     aAlfa1 = aAlfa - "N/FRA";
     |SI FSalida ! 0;
         #dscsvw06#7 = 700;
     |FINSI;

     aAlfa1 = aAlfa - "S/FRA";
     |SI FSalida ! 0;
         #dscsvw06#7 = 600;
     |FINSI;

     aAlfa1 = aAlfa - "ASIENTO DE CIERRE";
     |SI FSalida ! 0;
         #dscsvw06#1 = 99;
         #dscsvw06#7 = 95;
     |FINSI;

     |SI #dscsvw06#9 = " ";
         #dscsvw06#9 = "D";
     |FINSI;

     |SI #dscsvw06#9 = "D";
         #dscsvw06#17 = #dscsvw06#5;
         #dscsvw06#18 = #dscsvw06#6;
     |FINSI;

     |SI #dscsvw06#9 = "H";
         #dscsvw06#11 = #dscsvw06#5;
         #dscsvw06#12 = #dscsvw06#6;
     |FINSI;

     #dscsvw06#22 = #canempre#30;

     nApun = nApun + 1;

     #dscsvw06#4 = nApun;
     nAsiento    = #dscsvw06#25;

     |GRABA 020#dscsvw06.a;
     |LIBERA #dscsvw06;
|FPRC;

|DEFBUCLE dscsvw06Cheq;
     #dscsvw06#3;
     ;
     INICIO;
     FINAL;
     be;
     NULL, dscsvw06Cheq;
|FIN;

|PROCESO dscsvw06Gra;
     #camaasil#25 = 0;

     |PARA nCampo = 1;  |SI nCampo [ 31;  |HACIENDO nCampo = nCampo + 1;
           nDCampo = nCampo - 1;
           #camaasil#nDCampo# = #dscsvw06#nCampo#;
     |SIGUE;

     |GRABA 020#camaasil.n;
     |LIBERA #camaasil;

     #camaasic#0 = #camaasil#0;
     #camaasic#1 = #camaasil#1;
     #camaasic#2 = #camaasil#2;
     |LEE 000#camaasic.=;
     |SI FSalida = 0;  |ACABA;  |FINSI;

     |DDEFECTO #camaasic;
     #camaasic#0  = #camaasil#0;
     #camaasic#1  = #camaasil#1;
     #camaasic#2  = #camaasil#2;
     #camaasic#17 = #dscsvw06#36;

     |GRABA 020#camaasic.n;
     |LIBERA #camaasic;
|FPRC;

|DEFBUCLE dscsvw06Gra;
     #dscsvw06#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dscsvw06Gra;
|FIN;

|PROCESO CreaAsientos;
     aAbre = #dscsvz16#1;  |QBF aAbre;
     |SI aAbre = "";  |ACABA;  |FINSI;

     aFicTmp = "dscsvw06" + Usuario;  |NOME_DAT #dscsvw06#aFicTmp#;
     aFicTmp = "dscsvw10" + Usuario;  |NOME_DAT #dscsvw10#aFicTmp#;

     |ABRE #dscsvw06;  |CIERRA #dscsvw06;  |DELINDEX #dscsvw06;

     nTotCmp  = 7;

     |ABRE #dscsvw06;
     aTabla = "asientos";
     |HAZ LeeCSV;
     |CIERRA #dscsvw06;

     nDocu = 0;
     |SELECT #3#camaasic;
     |LEE 000#camaasic.u;
     |SI FSalida = 0;
         nDocu = #camaasic#2;
     |FINSI;

     nError = 0;
     |BUCLE dscsvw06Cheq;
     |SI nChequeo = 1;
         |CONFI "0000SRegistros de asientos preparados para importar. Quiere consultarlos antes?";

         |SI FSalida = 0;
             |ABRE #dscsvw06;
             |CONSULTA_CLAVE #1#dscsvw06;
             |CIERRA #dscsvw06;
         |FINSI;
     |FINSI;

     |BUCLE dscsvw06Gra;
|FPRC;

|PROCESO RecalculosFinales;
     aAlfa = "*:contagen/careccab canempr1 " + (("00000" + #48#2) % -105) + #48#3 + ";" + "000";
     |SUB_EJECUTA aAlfa;

     aAlfa = "*:contagen/caz00023 canempr1 " + (("00000" + #48#2) % -105) + #48#3 + ";" + "CREA";
     |SUB_EJECUTA aAlfa;

     |MENAV "0000Importaci¢n realizada. A continuaci¢n se van a realizar una serie de chequeos";

     |MENAV "0000Chequeo general de asientos";
     aAlfa = "*:contagen/cacheapu canempr1 " + (("00000" + #48#2) % -105) + #48#3 + ";" + "CHEQUEO";
     |SUB_EJECUTA aAlfa;

     |MENAV "0000Asistente de balances";
     aAlfa = "*:contagen/caut0009 canempr1 " + (("00000" + #48#2) % -105) + #48#3 + ";" + "CHEQUEO";
     |SUB_EJECUTA aAlfa;

     |MENAV "0000Recuperaci¢n de saldos";
     aAlfa = "*:contagen/carecupe canempr1 " + (("00000" + #48#2) % -105) + #48#3 + ";" + "CHEQUEO";
     |SUB_EJECUTA aAlfa;

     |MENAV "0000Proceso terminado.";
|FPRC;

|PROCESO Importacion;
     |HAZ CreaCuentas;
     |HAZ CreaAsientos;
     |HAZ RecalculosFinales;
|FPRC;

|PROCESO Chequeo;
     nError = 0;

     aAlfa = #dscsvz16#0;  |QBF aAlfa;
     |SI aAlfa = ""; nError = 1;  |FINSI;

     aAlfa = #dscsvz16#1;  |QBF aAlfa;
     |SI aAlfa = ""; nError = 1;  |FINSI;

     |SI nError = 1;
         |MENAV "0000Faltan ficheros a informar";
     |FINSI;
|FPRC;

|PROCESO Browse;
     aFicBrowse = "F*.csv";
     |BROWSE aFicBrowse;

     aAlfa      = aFicBrowse;   |QBF aAlfa;
     aFicBrowse = "";
     aLong      = aAlfa % 0;
     nLong      = aLong;
     |PARA nCarac = 2;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos  = (nCarac * 100) + 1;
           aFicBrowse = aFicBrowse + (aAlfa % nPos);
     |SIGUE;

     |SI aFicBrowse = "";
         |ACABA;
     |FINSI;

     aAlfa = (aFicBrowse % -103) < "";
     |SI aAlfa ! "csv";
         |MENAV "0000Tipo de archivo incorrecto, introduzca un csv";
          aFicBrowse = "";
     |FINSI;
|FPRC;

|PROCESO Ubica;
     nFSalida = FSalida;

     |HAZ Browse;

     |SI aFicBrowse = "";  |ACABA;  |FINSI;

     |SI nFSalida = nBtnCar1;
         aOrigen  = aFicBrowse;
         aDestino = aPathTmp + "cuentas.csv";

         |SI eaIP = "";
             |COPIA_FICHERO aOrigen, aDestino;
         |SINO;
             |RECIBE_FICHERO aOrigen, aDestino;
         |FINSI;

         #dscsvz16#0 = aDestino;  |PINTA #dscsvz16#0;
     |FINSI;

     |SI nFSalida = nBtnCar2;
         aOrigen  = aFicBrowse;
         aDestino = aPathTmp + "diario.csv";

         |SI eaIP = "";
             |COPIA_FICHERO aOrigen, aDestino;
         |SINO;
             |RECIBE_FICHERO aOrigen, aDestino;
         |FINSI;

         #dscsvz16#1 = aDestino;  |PINTA #dscsvz16#1;
     |FINSI;
|FPRC;

|PROCESO Cancelar;
     |PTEC 807;
|FPRC;

|PROGRAMA;
     |LEE 000#camaasil.p;
     |SI FSalida = 0;
         |CONFI "0000NYa existe movimientos en esta empresa/periodo. Continuamos con la importaci¢n?";
         |SI FSalida ! 0;  |ACABA;  |FINSI;
     |FINSI;

     aTextoVentana = "Importaci¢n CSV ";
     |VENTANA 0501, 3599, -1, 16, aTextoVentana;
     n0Vent = FSalida;

     |PINPA #0#dscsvz16;  nPanta = FSalida;
     |PINDA #0#dscsvz16;
     |PTEC 802;  |PAUSA;

     aModAsiento = "NO";

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}", 1185, 1187, Ubica; nBtnCar1 = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}", 1485, 1487, Ubica; nBtnCar2 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{207}}Cancelar", 3570, 3582, Cancelar;
     nBtnCan = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::{{230}}Continuar";
          |LEETECLA aTecla;

          |SI aTecla = aEsc1;  |O  aTecla = aRetu;
              nError = 0;
              |HAZ Chequeo;
              |SI nError = 0;  |SAL;  |FINSI;
          |FINSI;

          |SI aTecla = aEsc2; |SAL;  |FINSI;
     |SIGUE;

     |SI aTecla = aEsc2;
         |MENAV "0000Proceso cancelado.";
         |FINVENTANA n0Vent;
         |ACABA;
     |FINSI;

     |FIN_CONTROL_WINDOWS nBtnCar1;
     |FIN_CONTROL_WINDOWS nBtnCar2;

     |HAZ Importacion;

     |FINVENTANA n0Vent;

     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
|FPRO;

|PROCESO BorraDocs;
     aAlfa = aPathTmp + "cuentas.csv";       |FBORRA aAlfa;
     aAlfa = aPathTmp + "diario.csv";        |FBORRA aAlfa;
|FPRC;

|PROCESO T80;  |TIPO 80;
     nChequeo = 0;
     eaIP     = "";  |IP_REMOTO eaIP;

     |HAZ LeeUnidadBasico;

     aPathLocal = eaUnidadBasico + "\documentos";   |RMKDIR aPathLocal;
     aPathLocal = aPathLocal + "\tmp";              |RMKDIR aPathLocal;
     aPathLocal = aPathLocal + "\";

     |DBASE aBaseConta;
     aPathTmp   = aBaseConta + "tmp";           |MKDIR aPathTmp;
     aPathTmp   = aPathTmp   + "/" + Usuario;   |MKDIR aPathTmp;
     aPathTmp   = aPathTmp   + "/";

     aPathContagen = eaUnidadBasico + "\documentos";   |RMKDIR aPathContagen;
     aPathContagen = aPathContagen + "\contagen\";     |RMKDIR aPathContagen;

     |HAZ BorraDocs;

     |ABRET #cacuenta;

     |HAZ inicio;

     |ABRET #caclipro;
     |ABRET #camaasic;
     |ABRET #camaasil;
     |ABRET #dsmom030;

     |PULSATECLA;
     |PULSATECLA;
|FPRC;

|PROCESO T90;  |TIPO 90;
     |HAZ BorraDocs;

     |CIERRAT #cacuenta;
     |CIERRAT #caclipro;
     |CIERRAT #camaasic;
     |CIERRAT #camaasil;
     |CIERRAT #dsmom030;

     aFicTmp = "dscsvw06" + Usuario;  |NOME_DAT #dscsvw06#aFicTmp#;
     aFicTmp = "dscsvw07" + Usuario;  |NOME_DAT #dscsvw07#aFicTmp#;
     aFicTmp = "dscsvw10" + Usuario;  |NOME_DAT #dscsvw10#aFicTmp#;

     |ABRE #dscsvw06;  |CIERRA #dscsvw06;  |DELINDEX #dscsvw06;
     |ABRE #dscsvw07;  |CIERRA #dscsvw07;  |DELINDEX #dscsvw07;
     |ABRE #dscsvw10;  |CIERRA #dscsvw10;  |DELINDEX #dscsvw10;

     |PULSATECLA;
     |PULSATECLA;
|FPRC;
