|FICHEROS;
   catbaz02;
   catbam01;
   catbam02;
   catbam03;
   catbam04;

   cacuenta,MANTE;
   caivaasi;
   camaasic;
   camaasil;

   catbaw01;
   catbaw02;
   dscsvw10;

   cacuebal #15;
   ca8m0002 #422;
   ca8cumay #410;
|FIN;

|VARIABLES;
    &entrada    = 1;

    aParam      = "";
    nSw         = 0;
    nEstado     = 0;

    aFichero    = "";
    aAlfa       = "";
    aAlfa1      = "";
    aAlfa2      = "";
    aAlfa3      = "";
    aFecha      = "";
    aDCampo19   = "";
    aHCampo19   = "";
    nOpera      = 0;

    aVer1       = "";
    aVer2       = "";
 {1}aConte      = "";
    aBaseConta  = "";
    aPathLocal  = "";
    aPathTmp    = "";
    aPathContagen = "";
    aFicBrowse  = "";
    aProg       = "";

    aParam1     = "";
    aParam2     = "";
    aParam3     = "";
    nInkey      = 0;
    aError      = "";
    nError      = 0;
    nBarra      = 0;
    aLong       = "";
    nLong       = 0;
    nCarac      = 0;
    nPos        = 0;
    aCarac      = "";
    aLongVar    = "";
    nLongVar    = 0;
    nCarVar     = 0;
    nPosVar     = 0;
    aCarVar     = "";
    aC10        = "";
    aC13        = "";
    aLee        = "";
    aVar        = "";

    nCampo      = 0;
    SwError     = 0;
    nSwHay      = 0;
    nSwHayD     = 0;
    nSwHayP     = 0;
    nSwHayS     = 0;
    nSaldo      = 0;
    fFechaI     = @;
    fFechaF     = @;
    nDias       = 0;
    nOrden      = 0;
    fUltima     = @;
    nUltSaldo   = 0;
    nPasa       = 0;
    nOk         = 0;
    nExiste     = 0;

    aAbre       = "";
    nTotCmp     = 0;
    aTabla      = "";
    nCmp        = 0;
    nReg        = 0;
    nHandle     = 0;
    nTCampos    = 0;
    nTValor     = 0;
    nPorce      = 0;
    nDia        = 0;
    nMes        = 0;
    nAny        = 0;
    nNume1      = 0;
    nNume2      = 0;
    nNegativo   = 0;
    aColumna    = "";
    aComilla    = "";
    aQueQuiero  = "";
    aTipoCampo  = "";
    nLinea      = 0;
    aCuenta     = "";
    aContra     = "";
    aSigno      = "";
    nImporte    = 0;
    nAlguno     = 0;

   &eaUnidadBasico = "";
   &eaOrigen       = "";
   &eaDestino      = "";
   &eaIP           = "";
   &eftbaz3        = @;
   &entbaz4        = 0;
   &entbaz5        = 0;
   &entbaz6        = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE defec1_i;

|| ////////////////////////////////////////////////////////////////////////
|PROCESO Conversion;
     nError  = 0;

     aAlfa = aPathLocal + "*.csv";
     |R_PDIR aAlfa;
     |PARA;  |SI FSalida = 0;  |HACIENDO;
          |RFBORRA aAlfa;

          |R_SDIR aAlfa;
     |SIGUE;

     aParam2 = aPathLocal + "ficcsv.txt";  |RFBORRA aParam2;

     aVer1   = "";
     aVer2   = "";
     aConte  = aBaseConta + "plugin/dsofficetxt.exe";
     |ESPECIFICA "MD5", aConte;
     aVer1 = FSalida;

     aConte  = aPathLocal + "dsofficetxt.exe";
     |ESPECIFICA "REMOTOMD5", aConte;
     aVer2 = FSalida;

     |SI aVer1 ! aVer2;
         eaOrigen  = aBaseConta + "plugin/dsofficetxt.exe";
         eaDestino = aPathLocal + "dsofficetxt.exe";

         |SI eaIP = "";
             |COPIA_FICHERO eaOrigen, eaDestino;
         |SINO;
             |ENVIA_FICHERO eaOrigen, eaDestino;
         |FINSI;

         aAlfa = aPathLocal + "dsofficetxt.exe";
         |XABRE "EC", aAlfa, 0;
         |SI FSalida < 0;
             |MENAV "0000No encuentro el conversor a CSV, por favor consulte con su administrador informatico.";
             aFicBrowse = "";
             nError     = 1;
             |ACABA;
         |FINSI;
     |FINSI;

     aProg   = aPathLocal + "dsofficetxt.exe";
     aParam1 = aFicBrowse;
     aParam2 = aPathLocal + "ficcsv.csv";
     aParam3 = "";

     aAlfa  = "cmd " + &34 + "/c START /WAIT " + aProg + " " + aParam1 + " " + aParam2 + aParam3 + &34;
     |RSYSTEM aAlfa;

     |XABRE "EC", aParam2, 0;
     |SI FSalida < 0;
         aFicBrowse = "";
         |MENAV "0000No se ha podido convertir a CSV el fichero seleccionado.";
         nError     = 1;
         |ACABA;
     |FINSI;

     aFicBrowse = aParam2;
|FPRC;

|PROCESO MiniBrowse;
     aFicBrowse = "F*.*";
     |BROWSE aFicBrowse;

     aAlfa      = aFicBrowse;   |QBF aAlfa;
     aFicBrowse = "";
     aLong      = aAlfa % 0;
     nLong      = aLong;
     |PARA nCarac = 2;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos  = (nCarac * 100) + 1;
           aFicBrowse = aFicBrowse + (aAlfa % nPos);
     |SIGUE;
|FPRC;

|PROCESO Browse;
     |HAZ MiniBrowse;

     |SI aFicBrowse = "";
         |ACABA;
     |FINSI;

     |HAZ Conversion;
|FPRC;

|CALCULO PonFichero; |TIPO 0;
     |SI FSalida = 10;
         #catbaz02#2 = "";
         |HAZ Browse;
         |SI aFicBrowse ! "";
             eaOrigen  = aFicBrowse;
             eaDestino = aPathTmp + "movbancos.csv";

             |SI eaIP = "";
                 |COPIA_FICHERO eaOrigen, eaDestino;
             |SINO;
                 |RECIBE_FICHERO eaOrigen, eaDestino;
             |FINSI;

             #catbaz02#2 = eaDestino;  |PINTA #catbaz02#2;
         |FINSI;
     |FINSI;

     aAlfa = #catbaz02#2; |QBF aAlfa;
     |SI aAlfa = "";
         |MENAV "0000Error. Introduzca Fichero a Importar";
         |ERROR;
     |FINSI;
|FCAL;

|PROCESO Uno1;
   #catbam01#0 = doce;
|FPRC;

|PROCESO Dos1;
   #catbam01#0 = "zzzzzzzzzzzz";
|FPRC;

|CALCULO Tipo6C0;  |TIPO 6;
  nInkey = FSalida;

  |C_M #catbaz02#Campo#, 0, aAlfa;
  |SI aAlfa = ""; |ACABA; |FINSI;

  |ABRE #catbam01;
  #catbam01#0 = doce;
  |LEE 040#catbam01.=;
  |SI FSalida ! 0;
      |DDEFECTO #catbam01;
      |GRABA 020#catbam01.n;
  |FINSI;
  |CIERRA #catbam01;

  |SI nInkey = 10;
      |PUSHV 0501 2380;
      |ABRE #catbam01;
      #catbam01#0 = #catbaz02#0;
      |CONSULTA_F_CLAVE #1#catbam01, Uno1, Dos1;
      |CIERRA #catbam01;
      |POPV;
      #catbaz02#0 = #catbam01#0;
  |FINSI;

  eaCuenta = #catbaz02#0; |QBF eaCuenta;
  |SI eaCuenta = "";
      |MENAV "    Cuenta Erronea ";
      |ERROR;
      |ACABA;
  |FINSI;

  salidas  = nInkey;
  |HAZ FiltraPuntos;
  #catbaz02#0 = eaCuenta;
  |PINTA #catbaz02#0;
|FCAL;

|CALCULO Tipo0C0;  |TIPO 0;
   emCta    = 1;
   eaCuenta = #catbaz02#0;
   |HAZ SacaNivel;
   |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea
   |HAZ LeeCuenta;
   |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea

   |ABRE #catbam03;
   #catbam03#0 = #catbaz02#0;
   #catbam03#2 = 1;
   |LEE 041#catbam03.];
   |SI FSalida = 0; |Y #catbam03#0 = #catbaz02#0;
       |CONFI "0000SHay Apuntes introducidos pendientes de Actualizar. Incorporar ";
       |SI FSalida ! 0;
           |CIERRA #catbam03;
           |ERROR;
           |ACABA;
       |FINSI;
   |FINSI;
   |CIERRA #catbam03;

   |ABRE #catbam01;
   #catbam01#0 = #catbaz02#0;
   |LEE 020#catbam01.=;
   |SI FSalida ! 0;
       |MENAV "0000Error no existe la cuenta en Saldo Anterior";
       |ERROR;
   |SINO;
       #catbaz02#3 = #catbam01#2; |PINTA #catbaz02#3;
       #catbaz02#4 = #catbam01#3; |PINTA #catbaz02#4;
   |FINSI;
   |CIERRA #catbam01;
|FCAL;

|PROCESO Tipo12_z; |TIPO 12;
   || .... Para que no pregunte conforme S/N
|FPRC;


|| //////////////////////////////////////////////////////
|PROCESO CalculaFecha;
     |QBT aFecha;
     aLongVar = aFecha % 0;
     nLongVar = aLongVar;
     nBarra   = 0;
     aAlfa1   = "";
     aAlfa2   = "";
     aAlfa3   = "";
     |PARA nCarVar = 1;  |SI nCarVar [ nLongVar;  |HACIENDO nCarVar = nCarVar + 1;
           nPosVar = (nCarVar * 100) + 1;
           aCarVar = aFecha % nPosVar;

           |SI  aCarVar = "0"; |O aCarVar = "1"; |O aCarVar = "2";
             |O aCarVar = "3"; |O aCarVar = "4"; |O aCarVar = "5";
             |O aCarVar = "6"; |O aCarVar = "7"; |O aCarVar = "8";
             |O aCarVar = "9";
                |SI nBarra = 0;  aAlfa1 = aAlfa1 + aCarVar;  |FINSI;
                |SI nBarra = 1;  aAlfa2 = aAlfa2 + aCarVar;  |FINSI;
                |SI nBarra = 2;  aAlfa3 = aAlfa3 + aCarVar;  |FINSI;
           |SINO;
                |SI aCarVar = ".";  |O aCarVar = "/";  |O aCarVar = "\";
                   |O aCarVar = "-";
                    nBarra = nBarra + 1;
               |FINSI;
           |FINSI;
      |SIGUE;

      |QBF aAlfa1;
      |QBF aAlfa2;
      |QBF aAlfa3;

      nDia = aAlfa1;
      nMes = aAlfa2;
      nAny = aAlfa3;

      |SI nAny < 1000;
          nAny = 2000 + nAny;
      |FINSI;

      |SI nDia = 0;  nDia = 1;            |FINSI;
      |SI nMes = 0;  nMes = 1;            |FINSI;
      |SI nAny = 0;  nAny = enEjercicio;  |FINSI;

      aFecha = (("00" + nDia) % -102);
      aFecha = aFecha + "." + (("00" + nMes) % -102);
      aFecha = aFecha + "." + (("0000" + nAny) % -104);
|FPRC;

|PROCESO QuitaPuntos;
     |QBT aAlfa;
     aLongVar = aAlfa % 0;
     nLongVar = aLongVar;
     aAlfa1   = "";
     |PARA nCarVar = 1;  |SI nCarVar [ nLongVar;  |HACIENDO nCarVar = nCarVar + 1;
           nPosVar = (nCarVar * 100) + 1;
           aCarVar = aAlfa % nPosVar;

           |SI  aCarVar ! ".";
                aAlfa1 = aAlfa1 + aCarVar;
           |FINSI;
      |SIGUE;

      aAlfa = aAlfa1;
|FPRC;

|PROCESO QuitaComillas;
     aLongVar = aAlfa % 0;
     nLongVar = aLongVar;

     aAlfa1   = "";
     aComilla = &34;
     |PARA nCarVar = 1;  |SI nCarVar [ nLongVar;  |HACIENDO nCarVar = nCarVar + 1;
           nPosVar = (nCarVar * 100) + 1;
           aCarVar = aAlfa % nPosVar;

           |SI aCarVar ! aComilla;
                aAlfa1 = aAlfa1 + aCarVar;
           |FINSI;
     |SIGUE;

     aAlfa = aAlfa1;
|FPRC;

|| //////////////////////////////////////////////////////
|PROCESO dscsvw10Campos;
     nTCampos  = nTCampos + 1;
|FPRC;

|DEFBUCLE dscsvw10Campos;
     #dscsvw10#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dscsvw10Campos;
|FIN;

|PROCESO CalculaColumna;
   nNume1   = nCampo + 64;
   aColumna = &nNume1;
   nOk = 0;
   nNume1 = -1;
   |SI #catbam01#22 = aColumna; nNume1 = 1; |FINSI;
   |SI #catbam01#23 = aColumna; nNume1 = 3; |FINSI;
   |SI #catbam01#24 = aColumna; nNume1 = 8; |FINSI;
   |SI #catbam01#25 = aColumna; nNume1 = 2; |FINSI;
   |SI #catbam01#26 = aColumna; nNume1 = 9; |FINSI;
   |SI #catbam01#27 = aColumna; nNume1 = 7; |FINSI;
   |SI #catbam01#31 = aColumna; nNume1 = 10;|FINSI;
|FPRC;

|PROCESO dscsvw10Carga;
    nCampo = #dscsvw10#0;
    aAlfa  = #dscsvw10#1;

    |HAZ CalculaColumna;
    |SI nNume1 = -1; |ACABA; |FINSI;

    aQueQuiero = "|C" + (("000" + nNume1) % -103) + "TIPO";
    aTipoCampo = #catbaw01#aQueQuiero#;

    |SI aTipoCampo = "F";
        aFecha = aAlfa; |HAZ CalculaFecha; aAlfa = aFecha;
    |FINSI;
    |SI aTipoCampo = "N"; |HAZ QuitaPuntos; |FINSI;

    #catbaw01#0 = nReg;
    #catbaw01#nNume1# = aAlfa;
|FPRC;

|DEFBUCLE dscsvw10Carga;
     #dscsvw10#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dscsvw10Carga;
|FIN;

|PROCESO GrabaDatos;
     |CIERRA #dscsvw10;
     nTCampos = 0;
     |BUCLE dscsvw10Campos;
     |SI nTotCmp = 0; nTotCmp = nTCampos; |FINSI;

     nTValor  = nTotCmp;
     |SI nTCampos < nTValor;
         |DELINDEX #dscsvw10;
         |ACABA;
     |FINSI;

     nPorce = ((nTValor * 100) / nTCampos) ! 0;
     |SI nPorce < 70;
         |DELINDEX #dscsvw10;
         |ACABA;
     |FINSI;

     nReg = nReg + 1;
     |SI nReg < #catbam01#20;
         |DELINDEX #dscsvw10;
         |ACABA;
     |FINSI;

     |DDEFECTO #catbaw01;
     |BUCLE dscsvw10Carga;

     |GRABA 020#catbaw01.n;
     |LIBERA #catbaw01;

     |DELINDEX #dscsvw10;

     |SI nReg = #catbam01#20;
         |HAZ PrimerChequeo;
         fFechaI = #catbaw01#1;
     |FINSI;
     fFechaF = #catbaw01#1;
|FPRC;

|PROCESO Grabaw10;
     #dscsvw10#0 = nCmp;
     #dscsvw10#1 = aVar;
     |GRABA 020#dscsvw10.n;
     |LIBERA #dscsvw10;
|FPRC;

|PROCESO SacaDatos;
     aLong  = aLee % 0;
     nLong  = aLong;
     aC10   = &10;
     aC13   = &13;
     |PARA nCarac = 1;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos   = (nCarac * 100) + 1;
           aCarac = aLee % nPos;

           |SI aCarac = aC13;
               aCarac = ";";
               nSw    = 1;
           |FINSI;

           |SI aCarac = ";";
               nCmp   = nCmp + 1;
               |HAZ Grabaw10;

               aVar   = "";
               aCarac = "";
           |FINSI;

           |SI aCarac = aC10;
               aCarac = "";
           |FINSI;

           |SI nSw = 1;  |Y nCmp ! 0;
               |HAZ GrabaDatos;

               |ABRE #dscsvw10;

               nCmp = 0;
               nSw  = 0;
           |FINSI;

           aVar = aVar + aCarac;
      |SIGUE;

|FPRC;

|PROCESO LeeCSV;
     aAlfa = "Procesando registros del fichero " + aTabla;
     |ATRI I; |PINTA 2105, aAlfa; |ATRI 0;

     nSw        = 0;
     |ABRE #dscsvw10;

     nCmp  = 0;
     nReg  = 0;
     |XABRE "B", aAbre, nHandle;
     |PARA;  |SI;  |HACIENDO;
          |XLEE nHandle, 255, aLee;
          |SI FSalida [ 0;  |SAL;  |FINSI;

          |HAZ SacaDatos;
          |SI SwError ! 0; |SAL; |FINSI;
     |SIGUE;
     |XCIERRA nHandle;

     |LEE 000#dscsvw10.p;
     |SI FSalida = 0;
         nCmp   = nCmp + 1;
         |HAZ Grabaw10;
         |HAZ GrabaDatos;
     |FINSI;

     |ABRE #dscsvw10; |CIERRA #dscsvw10; |DELINDEX #dscsvw10;
|FPRC;

|| //////////////////////////////////////////////////////
|PROCESO Errores;
     nOk = 1;

     |SI SwError = 3;
         aError = "0000Formato de fichero incorrecto. Revise configuracion";
     |FINSI;

     |SI SwError = 1;
         aError = "0000No hay datos para Importar";
     |FINSI;

     |SI SwError = 2;
         |SI nError = nSwHay;
             |SI nSwHayP = 1; |Y nSwHayD = 0;
                 aError = "Movimientos fuera del periodo contable. ";
             |FINSI;
             |SI nSwHayP = 0; |Y nSwHayD = 1;
                 aError = "Movimientos anteriores a la ultima fecha recogida";
             |FINSI;
             |SI nSwHayP = 1; |Y nSwHayD = 1;
                 aError = "Mov. fuera del periodo y anteriores a la ultima fecha recogida";
             |FINSI;
             aError = "0000" + aError;
         |SINO;
             nOk = 0;
             |SI nSwHayP = 1;
                 aError = "Hay apuntes fuera del ejercicio";
             |FINSI;
             |SI nSwHayD = 1;
                 aError = "Hay mov. ya recogidos anteriormente";
             |FINSI;

             |SI nSwHayD = 0; |Y nSwHayP = 0; |Y  nSwHayS = 1;
                 nOk = -1;
             |FINSI;
             aError = "0000S" + aError + ". Continuar";
         |FINSI;
     |FINSI;

     |SI nOk = 1;
         |MENAV aError;
     |SINO;
         |SI nOk = 0;
             nOk = 1;
             |CONFI aError;
             |SI FSalida = 0;
                 nOk = 0;
                 |MENAV "0000Los movimientos erróreos no se pasarán";
             |FINSI;
         |SINO;
             nOk = 0;
         |FINSI;
     |FINSI;
     |SI nSwHayS = 1; |Y nOk = 0;
         |MENAV "0000Saldo teórico de la cuenta incorrecto. Revise los movimentos";
     |FINSI;
|FPRC;

|PROCESO PrimerChequeo;
   |SI #catbaw01#1 [ "01.01.0000"; |O #catbaw01#1 > "31.12.2099";
       SwError = 3;
   |FINSI;
   |SI #catbam01#21 = "S";
       |SI #catbaw01#3 = 0; SwError = 3; |FINSI;
   |SINO;
       |SI #catbaw01#8 = 0; |Y #catbaw01#3 = 0; SwError = 3; |FINSI;
   |FINSI;
|FPRC;

|PROCESO catbaw01Cheq;
  nSwHay = nSwHay + 1;
  |SI #catbaw01#1 < fec1; |O #catbaw01#1 > fec2;
      nSwHayP = 1;
      nError = nError + 1;
  |SINO;
      |SI #catbaw01#1 > #catbam01#2;
          nPasa  = 1;
          nSaldo = nSaldo + #catbaw01#3;
          |SI #catbam01#21 = "N";
              nSaldo = nSaldo - #catbaw01#8;
          |FINSI;
          |SI nOrden = 0;
              |SI fUltima [ #catbaw01#1;
                  fUltima   = #catbaw01#1;
                  nUltSaldo = #catbaw01#9;
              |FINSI;
          |SINO;
              |SI fUltima < #catbaw01#1;
                  fUltima   = #catbaw01#1;
                  nUltSaldo = #catbaw01#9;
              |FINSI;
          |FINSI;
      |SINO;
          nSwHayD = 1;
          nError = nError + 1;
      |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE catbaw01Cheq;
   #catbaw01#2;
   ;
   "01.01.0000", 0000000001;
   "31.12.2099", 9999999999;
   ;
   NULL, catbaw01Cheq;
|FIN;

|PROCESO PasoFichero;
     aAbre = #catbaz02#2;  |QBF aAbre;
     |SI aAbre = "";  |ACABA;  |FINSI;

     nTotCmp    = 0;   || para calcular segun el primer pase

     |ABRE #catbaw01;
     aTabla = "movimientos";
     |HAZ LeeCSV;
     |CIERRA #catbaw01;

     nOrden = 0;
     |SI fFechaI > fFechaF; nOrden = 1; |FINSI;

     nSaldo    = 0;
     fUltima   = "01.01.0000";
     nUltSaldo = 0;
     |SI SwError = 0;
         |BUCLE catbaw01Cheq;
         |SI nSwHay = 0;
             SwError = 1;
         |SINO;
             |SI nSwHayP ! 0; |O nSwHayD ! 0; SwError = 2; |FINSI;
         |FINSI;
         |SI nPasa = 1; |Y #catbam01#26 ! " ";
             nSaldo = nSaldo + #catbam01#3;
             |SI nSaldo ! nUltSaldo;
                 nSwHayS = 1;
                 SwError = 2;
             |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|| //////////////////////////////////////////////////////
|PROCESO DarAltaw02;
   #catbaw02#0 = #catbam03#20;
   #catbaw02#1 = #catbam03#21;
   #catbaw02#2 = #catbam03#22;
   #catbaw02#3 = #catbam03#23;
   |LEE 041#catbaw02.=;
   |SI FSalida ! 0;
       #catbaw02#0 = #catbam03#20;
       #catbaw02#1 = #catbam03#21;
       #catbaw02#2 = #catbam03#22;
       #catbaw02#3 = #catbam03#23;
       |GRABA 020#catbaw02.c;
   |FINSI;
   #catbaw02#4 = #catbam03#0;
   #catbaw02#5 = #catbam03#2;
   #catbaw02#6 = 0;

   |GRABA 020#catbaw02.a;
   nAlguno = 1;
|FPRC;

|PROCESO Leerw02;
   nExiste = 0;
   |DDEFECTO #catbaw02;
   #catbaw02#0 = #camaasil#0;
   #catbaw02#1 = #camaasil#1;
   #catbaw02#2 = #camaasil#2;
   #catbaw02#3 = #camaasil#3;
   |LEE 041#catbaw02.=;
   |SI FSalida = 0;
       nExiste = 1;
   |FINSI;
|FPRC;

|PROCESO LeoFacturas;
   |HAZ Leerw02;
   |SI nExiste = 0;
       nSw = 1;
       |DDEFECTO #camaasic;
       #camaasic#0 = #camaasil#0;
       #camaasic#1 = #camaasil#1;
       #camaasic#2 = #camaasil#2;
       |LEE 041#camaasic.=;
       |SI aSigno = "D";
           aContra = #camaasic#10;
       |SINO;
           aContra = #camaasic#8;
       |FINSI;
       |ERROR10;
   |FINSI;
|FPRC;

|DEFBUCLE LeoFacturas;
   #camaasil#3;
   8, 9, 18;
   eaCuenta, fFechaI, 000001, 0001, aSigno, nImporte, " ";
   eaCuenta, fFechaI, 999999, 9999, aSigno, nImporte, " ";
   e;
   NULL, LeoFacturas;
|FIN;

|PROCESO BuscaExiste;
  |SI nOpera = 1;
      |HAZ DarAltaw02;
  |FINSI;

  |SI nOpera = 0;
      nSw      = 0;
      nNume1   = nDias * nNegativo;
      nNume2   = #catbam03#3;
      nNume2   = nNume2 + nNume1;
      fFechaI  = nNume2;
      aSigno   = #catbam03#6;
      nImporte = #catbam03#7;
      eaCuenta = #catbam03#0;
      |ABRE #camaasic;
      |BUCLE LeoFacturas;
      |SI nSw = 1;
          #catbam03#8  = aContra;
          #catbam03#19 = "X";
          #catbam03#20 = #camaasil#0;
          #catbam03#21 = #camaasil#1;
          #catbam03#22 = #camaasil#2;
          #catbam03#23 = #camaasil#3;
          #catbam03#24 = "CONTABILIZADO";
          |GRABA 020#catbam03.a;
          |HAZ DarAltaw02;
      |SINO;
          nSwHay = nSwHay + 1;
      |FINSI;
      |CIERRA #camaasic;
  |FINSI;
  |LIBERA #catbam03;
|FPRC;

|DEFBUCLE BuscaExiste;
  #catbam03#1;
  19;
  #catbaz02#0, 00001, aDCampo19;
  #catbaz02#0, 99999, aHCampo19;
  be;
  NULL, BuscaExiste;
|FIN;

|| //////////////////////////////////////////////////////
|PROCESO Actualiza;

    |DDEFECTO #cacuenta;
    #cacuenta#0 = eaCuenta;
    |LEE 141#cacuenta.=;
    |SI FSalida ! 0;
        #cacuenta#0 = eaCuenta;
        #cacuenta#1 = enNivel;
        #cacuenta#3 = "S";
        |HAZ defect1;
        mascara = #cacuenta#0; |QBF mascara;
        mascara = mascara + "zzzzzzzzzzzz";
        mascara = mascara % 112;
        #cacuenta#54 = mascara;
        #cacuenta#55 = #cacuenta#1;

        |GRABA 020#cacuenta.b;

        |PUSHV 0501 2480;
        |PINPA #0#cacuenta;
        |PINDA #0#cacuenta;
        |ENDATOS #1#cacuenta;
        |SI FSalida = 0;
            |GRABA 020#cacuenta.a;
        |SINO;
            |BORRA 020#cacuenta.a;
        |FINSI;
        |POPV;
    |FINSI;
    |LIBERA #cacuenta;
|FPRC;

|PROCESO catbaw01Paso;

  nOk = 0;
  |SI #catbaw01#1 < fec1; |O #catbaw01#1 > fec2;
      nOk = 1;
  |FINSI;
  |SI #catbaw01#1 [ #catbam01#2;
      nOk = 1;
  |FINSI;

  |SI nOk = 0;
      nSwHay = nSwHay + 1;
      nLinea = nLinea + 1;

      |DDEFECTO #catbam03;
      #catbam03#0 = #catbam02#0;
      #catbam03#1 = enNivel;
      #catbam03#2 = nLinea;
      #catbam03#3 = #catbaw01#1;

      aSigno = "D";
      |SI #catbam01#21 = "S";
          nImporte = #catbaw01#3;
          |SI nImporte < 0;
              nImporte = - nImporte;
              aSigno = "H";
          |FINSI;
      |SINO;
          |SI #catbaw01#3 ! 0; nImporte = #catbaw01#3; aSigno = "D"; |FINSI;
          |SI #catbaw01#8 ! 0; nImporte = #catbaw01#8; aSigno = "H"; |FINSI;
      |FINSI;

      |SI aSigno = "D";
          #catbam03#4 = #catbam01#10;
          aAlfa = #catbam01#12; |QBF aAlfa;
          aContra = #catbam01#14;
      |SINO;
          #catbam03#4 = #catbam01#15;
          aAlfa = #catbam01#17; |QBF aAlfa;
          aContra = #catbam01#19;
      |FINSI;
      |SI #catbaw01#10 ! 0; #catbam03#4 = #catbaw01#10; |FINSI;

      aAlfa = aAlfa + #catbaw01#2;
      #catbam03#5 = aAlfa;
      #catbam03#6 = aSigno;
      #catbam03#7 = nImporte;
      aAlfa = #catbaw01#7; |QBF aAlfa;
      |SI aAlfa = ""; aAlfa = aContra; |FINSI;
      #catbam03#8  = aAlfa;
      #catbam03#9  = enNivel;
      #catbam03#10 = #catbam03#4;
      #catbam03#11 = #catbam03#5;
      #catbam03#12 = #catbam01#6;
      #catbam03#13 = #catbam01#7;
      aAlfa = #catbam03#8; |QBF aAlfa;
      |SI aAlfa ! "";
         #catbam03#24 = #catbam03#8;
      |SINO;
         #catbam03#24 = "PENDIENTE";
      |FINSI;
      |GRABA 040#catbam03.n;

      |SI #catbam03#6 = "D";
          #catbam02#5 = #catbam02#5 + #catbam03#7;
          nSaldo = nSaldo + #catbam03#7;
      |SINO;
          #catbam02#6 = #catbam02#6 + #catbam03#7;
          nSaldo = nSaldo - #catbam03#7;
      |FINSI;
      #catbam02#7 = #catbam02#4 + #catbam02#5 - #catbam02#6;

      |SI #catbam02#3 < #catbam03#3;
          #catbam02#3 = #catbam03#3;
      |FINSI;

      eaCuenta = #catbam03#8;
      |HAZ Actualiza;

      |SI nOrden = 0;
          |SI fUltima [ #catbaw01#1;
              fUltima   = #catbaw01#1;
              nUltSaldo = #catbaw01#9;
          |FINSI;
      |SINO;
          |SI fUltima < #catbaw01#1;
              fUltima   = #catbaw01#1;
              nUltSaldo = #catbaw01#9;
          |FINSI;
      |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE PasaAuxiliar;
   #catbaw01#1;
   ;
   0000000001;
   9999999999;
   ;
   NULL, catbaw01Paso;
|FIN;

|PROCESO PasoDatos;
     |CONFI "0000NRegistros de movimientos preparados para importar. Quiere consultarlos antes?";
     |SI FSalida = 0;
         |ABRE #catbaw01;
         |C_V #catbaw01#4, 1, "N";
         |C_V #catbaw01#5, 1, "N";
         |C_V #catbaw01#6, 1, "N";
         |C_V #catbaw01#7, 1, "N";
         |SI #catbam01#27 ! " "; |C_V #catbaw01#7, 1, "S"; |FINSI;
         |SI #catbam01#24 ! " "; |C_V #catbaw01#8, 1, "S"; |FINSI;
         |SI #catbam01#26 ! " "; |C_V #catbaw01#9, 1, "S"; |FINSI;
         |SI #catbam01#31 ! " "; |C_V #catbaw01#10, 1, "S"; |FINSI;
         |CONSULTA_CLAVE #1#catbaw01;
         |CIERRA #catbaw01;
     |FINSI;

     eaCuenta = #catbaz02#0;
     |HAZ SacaNivel;

     aAlfa = "Pasando registro a entrada de movimientos                                  ";
     |ATRI I; |PINTA 2105, aAlfa; |ATRI 0;

|| ... Abre cabacera de apuntes a introducir catbam02;
     |SI aParam ! "recoge";
         |ABRE #catbam02;
         |DDEFECTO #catbam02;
         #catbam02#0 = #catbaz02#0;
         |LEE 141#catbam02.=;
         |SI FSalida ! 0;
             |DDEFECTO #catbam02;
             #catbam02#0 = #catbaz02#0;
             #catbam02#1 = enNivel;
             #catbam02#2 = #catbaz02#1;
             #catbam02#3 = #catbaz02#3;
             #catbam02#4 = #catbaz02#4;
             |GRABA 020#catbam02.b;
         |FINSI;
     |SINO;
         |DDEFECTO #catbam02;
         #catbam02#0 = #catbaz02#0;
         #catbam02#1 = enNivel;
         #catbam02#2 = #catbaz02#1;
         #catbam02#3 = #catbaz02#3;
         #catbam02#4 = #catbaz02#4;
     |FINSI;

     nUltSaldo = 0;
     nSaldo  = 0;
     fUltima = #catbam01#2;
     nSwHay  = 0;
     nLinea  = 0;
     |ABRE #catbam03;
     #catbam03#0 = #catbam02#0;
     #catbam03#2 = 1;
     |LEE 041#catbam03.];
     |SI FSalida = 0; |Y #catbam03#0 = #catbam02#0;
         nLinea = #catbam03#2;
     |FINSI;

     |ABRE #cacuenta;
     |BUCLE PasaAuxiliar;
     |CIERRA #cacuenta;
     |CIERRA #catbam03;

     #catbam02#3 = fUltima;
     |SI aParam ! "recoge";
         |GRABA 020#catbam02.a;
         |LIBERA #catbam02;
         |CIERRA #catbam02;
     |SINO;
         eftbaz3 = #catbam02#3;
         entbaz4 = #catbam02#4;
         entbaz5 = #catbam02#5;
         entbaz6 = #catbam02#6;
     |FINSI;

     aAlfa = nSwHay;
     aAlfa = "0000" + aAlfa + " Apuntes pasados a la Introduccion por Cuentas";
     |MENAV aAlfa;

     #catbam01#28 = #catbam01#2;
     #catbam01#29 = #catbam01#3;
     #catbam01#2  = fUltima;
     #catbam01#3  = #catbam01#3 + nSaldo;
     |GRABA 020#catbam01.a;
|FPRC;

|PROCESO HazOperacion;
|| ... Abre saldo anterior  catbam01
   |ABRE #catbam01;
   #catbam01#0 = #catbaz02#0;
   |LEE 141#catbam01.=;
   |SI FSalida ! 0;
       |DDEFECTO #catbam01;
       #catbam01#0 = #catbaz02#0;
       #catbam01#2 = #catbaz02#1;
       |GRABA 020#catbam01.b;
   |FINSI;

   aFichero = "catbaw01" + Usuario;  |NOME_DAT #catbaw01#aFichero#;
   aFichero = "catbaw02" + Usuario;  |NOME_DAT #catbaw02#aFichero#;
   aFichero = "dscsvw10" + Usuario;  |NOME_DAT #dscsvw10#aFichero#;

   |ABRE #catbaw01;
   |CIERRA #catbaw01;
   |DELINDEX #catbaw01;

   |SI aParam = "";
       |ABRE #catbaw02;
       |CIERRA #catbaw02;
       |DELINDEX #catbaw02;
   |FINSI;

   nOk     = 0;
   nPasa   = 0;
   SwError = 0;
   nSwHay  = 0;
   nSwHayD = 0;
   nSwHayP = 0;
   nSwHayS = 0;
   |SI aParam ! "busca";
       |HAZ PasoFichero;
       |SI SwError ! 0;
           |HAZ Errores;
       |FINSI;
       |SI nOk = 0;
           |HAZ PasoDatos;
       |FINSI;
   |FINSI;

   |LIBERA #catbam01;
   |CIERRA #catbam01;
   |DELINDEX #catbaw01;

   nAlguno = 0;
   |ABRE #catbaw02;
   aDCampo19 = "X";
   aHCampo19 = "X"; nOpera = 1; |BUCLE BuscaExiste;

   nSwHay = 0;
   nDias  = 0;
   aDCampo19 = " ";
   aHCampo19 = " "; nOpera = 0; |BUCLE BuscaExiste;
   |SI nSwHay ! 0;
       nNegativo = -1;
       |PARA nDias = 1; |SI nDias [ #catbam01#9; |HACIENDO nDias = nDias + 1;
             nSwHay = 0;
             |BUCLE BuscaExiste;
             |SI nSwHay = 0; |SAL; |FINSI;
       |SIGUE;
   |FINSI;
   |SI nSwHay ! 0;
       nNegativo = 1;
       |PARA nDias = 1; |SI nDias [ #catbam01#9; |HACIENDO nDias = nDias + 1;
             nSwHay = 0;
             |BUCLE BuscaExiste;
             |SI nSwHay = 0; |SAL; |FINSI;
       |SIGUE;
   |FINSI;
   |SI nAlguno ! 0;
       |MENAV "0000Existen movimientos ya contabilizados. Se dejan no modificables";
   |FINSI;
|FPRC;

|PROCESO LosPaths;
     eaIP     = "";
     |IP_REMOTO eaIP;

     |HAZ LeeUnidadBasico;

     aPathLocal = eaUnidadBasico + "\documentos";   |RMKDIR aPathLocal;
     aPathLocal = aPathLocal + "\tmp";              |RMKDIR aPathLocal;
     aPathLocal = aPathLocal + "\";

     |DBASE aBaseConta;
     aPathTmp   = aBaseConta + "tmp";          |MKDIR aPathTmp;
     aPathTmp   = aPathTmp  + "/" + Usuario;   |MKDIR aPathTmp;
     aPathTmp   = aPathTmp  + "/";

     aPathContagen = eaUnidadBasico + "\documentos";   |RMKDIR aPathContagen;
     aPathContagen = aPathContagen + "\contagen\";     |RMKDIR aPathContagen;

     aAlfa = aPathTmp + "movbancos.csv";       |FBORRA aAlfa;
|FPRC;

|PROGRAMA;

   aParam = PARAMETRO;
   |QBF aParam;

   |CLS;
   |CABEZA "Importacion extractos bancarios";
   |PINPA #0#catbaz02;

   |SI aParam = "";
       |HAZ inicio;
   |FINSI;

   |HAZ LosPaths;

   |ABRE #caivaasi;
   |DDEFECTO #caivaasi;
   |LEE 041#caivaasi.p;
   |CIERRA #caivaasi;

   |SI aParam = "recoge";
       #catbaz02#0 = eaCuenta;
       #catbaz02#1 = eaNomCta;
       |C_M #catbaz02#0, 1, "N";
   |FINSI;

   |PINDA #0#catbaz02;
   |ENDATOS #1#catbaz02;
   |SI FSalida = 0;
       |SI #catbaz02#5 = "SI";
           |HAZ HazOperacion;
       |FINSI;
   |FINSI;

   aAlfa = aPathTmp + "movbancos.csv"; |FBORRA aAlfa;

   |SI aParam = "";
       |DELINDEX #catbaw02;
   |FINSI;
|FPRO;
