|FICHEROS;

  anz00005 #00;   || Pantalla
  anm00009 #01;   || Lineas
  anm00008 #02;   || Empresas Analitica
  canempre #30;

  anm00016 #16;  || Empresas analiticas
|FIN;

|VARIABLES;
  &r_emp = 0;
  &r_per = 0;
  aAlfa1 = "";
  aAlfa2 = "";
  aAlfa3 = "";
  modo = 0;
  nNume1 = 0;
  nume2 = 0;
  swalta = 0;
  dire = "";
  nEjerActual = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoan_i;
|INCLUYE copian_i;

|| ************************************************************************

|CALCULO InicioAnz5; |TIPO 8;
 |HAZ InicioAna;
 |SI  enSwAna = 0; |ACABA;  |FINSI;

  aMonedaC = eaMoneda;
  |DBASE dir_fich;
  dir_fich = dir_fich + "fich/";
  aAlfa3 = ("000000" + #30#0) % -106;
  fich_copi = dir_fich + aAlfa3 + "/";
|FCAL;

|CALCULO defectos; |TIPO 7;
 #0#2 = enPeriodo - 1; |PINTA #0#2;
 #0#3 = enPeriodo;     |PINTA #0#3;
|FCAL;

|CALCULO periodo; |TIPO 7;
 #0#3 = #0#2 + 1;
 |PINTA #0#3;
|FCAL;

|CALCULO empresah; |TIPO 0;
 |SI FSalida = 2; |ACABA; |FINSI;
 |SI #0#1 < #0#0;
     |MENAV "    ERROR.! Empresa Hasta Menor que Empresa Desde.";
     |ERROR;
 |FINSI;
|FCAL;

|| ***********************************************************************
|| ***********************************************************************
|PROCESO altaempr;

 |SI eaAnNiv = "E";
     |ABRE #16;
     #16#0 = #30#2;
     |LEE 001#16=;
     |SI FSalida ! 0;
         |CIERRA #16;
         |ACABA;
     |FINSI;
     |CIERRA #16;
 |FINSI;


 |ATRI I; |PINTA 2221,      " Creando Empresa Contable:             "; |ATRI 0;
 |PINTA 2248, #30#2;
 |PINTA 2255, #30#3;
 |DBASE dir_fich;
 dir_fich = dir_fich + "fich/";
 aAlfa1 = ("000000" + #30#0) % -106;
 aAlfa3 = dir_fich + aAlfa1;
 fich_copi = dir_fich + aAlfa3 + "/";

 #30#3  = #0#3;
 #30#26 = #30#26 + 1; |SI #30#26 = 100; #30#26 = 0; |FINSI;
 nEjerActual = #30#26;

 || .................. clave
 nNume1 = ((#30#2 * 10) + #30#3);
 aAlfa1 = nNume1;
 aAlfa1 = ( "0000000" + aAlfa1 ) % -106;
 #30#0 = aAlfa1;

 |HAZ CambiaNombre;

 |GRABA 020#30n;
 enEmpresa = #30#2;
 enPeriodo = #30#3;
 swOperacion = 3;
 eaMoneda = aMonedaC;
 |SUB_EJECUTA "camoneda";
|| ...............................  mkdirector;

  |DBASE dir_fich;
  dir_fich = dir_fich + "fich/";
  aAlfa1 = ("000000" + #30#0) % -106;
  aAlfa3 = dir_fich + aAlfa1;
  |MKDIR aAlfa3;
  fich_alta = aAlfa3 + "/";
  dirfich0 = fich_alta;

 |PUSHV 2301 2480;

  eaCopiar = "M";
  |HAZ copia_maes;
  |HAZ acerar4;
  |HAZ pone0_maes;
  |HAZ ActDefecto;
  |HAZ NoTesteo;
 |POPV;
|FPRC;

|DEFBUCLE grabar_emp;
 #30#1;
 ;
 #0#0, #0#2;
 #0#1, #0#2;
 e;
 NULL, altaempr;
|FIN;

|| ***********************************
|PROCESO borr_emp2;

 |SI eaAnNiv = "E";
     |ABRE #16;
     #16#0 = #30#2;
     |LEE 001#16=;
     |SI FSalida ! 0;
         |CIERRA #16;
         |ACABA;
     |FINSI;
     |CIERRA #16;
 |FINSI;


 |ATRI I; |PINTA 2221, " Borrando Empresa Contable:             "; |ATRI 0;
 |PINTA 2249, #30#2;
 |PINTA 2256, #30#3;
  |DBASE dir_fich;
  dir_fich = dir_fich + "fich/";
  aAlfa1 = ("000000" + #30#0) % -106;
  aAlfa3 = dir_fich + aAlfa1;

  dire = "";
  |DIRECTORIO dire;
  aAlfa1 = dire + "bin/agirm -r " + aAlfa3;
  |SYSTEM aAlfa1;

  |BORRA 020#30a;
  |LIBERA #30;
|FPRC;

|DEFBUCLE borrar_emp;
 #30#1;
 ;
 #0#0, #0#3;
 #0#1, #0#3;
 e;
 NULL, borr_emp2;
|FIN;

|PROCESO crea_empresas;
 |BUCLE borrar_emp;
 |BUCLE grabar_emp;
|FPRC;

|| *************************************************************************
|PROCESO dup_lineas;
 #1#1 = #0#3;
 |GRABA 020#1n;
 |LIBERA #1;
|FPRC;

|DEFBUCLE grabalin;
 #1#1;
 ;
 #2#0, #2#1, 001;
 #2#0, #2#1, 999;
 e;
 NULL, dup_lineas;
|FIN;

|PROCESO dup_emp;
 |ATRI I; |PINTA 2221," Creando Empresa Analitica:             "; |ATRI 0;
 |PINTA 2249, #2#0;
 |PINTA 2256, #2#1;
 |BUCLE grabalin;
 #2#1 = #0#3;
 #2#2 = #2#2 + 1; |SI #2#2 = 100; #2#2 = 0; |FINSI;
 #2#5 = "01.01.0000";
 |GRABA 020#2n;
 |LIBERA #2;
|FPRC;

|DEFBUCLE grabar;
 #2#1;
 ;
 #0#0, #0#2;
 #0#1, #0#2;
 e;
 NULL, dup_emp;
|FIN;

|| ----------------------------------------------------------------------
|PROCESO borr_emp;
 |ATRI I; |PINTA 2221, " Borrando Empresa Analitica:            "; |ATRI 0;
 |PINTA 2250, #2#0;
 |PINTA 2257, #2#1;

 |BUCLELIN 1NULL#2;
 |BORRA 020#2a;
 |LIBERA #2;
|FPRC;

|DEFBUCLE borrar;
 #2#1;
 ;
 #0#0, #0#3;
 #0#1, #0#3;
 e;
 NULL, borr_emp;
|FIN;

|| ***********************************************************************
|CALCULO duplicar; |TIPO 3;

 |D_CUADRO 2120, 2361;
 |PINTA 2221, "                                        ";
 |BUCLE borrar;
 |BUCLE grabar;

 |SI #0#4 = "S";
     |HAZ crea_empresas;
 |FINSI;
|FCAL;
