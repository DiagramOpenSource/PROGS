|FICHEROS;
     caz00100 #100;

     cacieex6 #6;
     cacieex8 #8;
|FIN;

|VARIABLES;
      aAlfa      = "";
      aFichero   = "";
      aParametro = "";
      &enPlan    = 0;
      nSw6       = 0;
      nSw8       = 0;
|FIN;

|INCLUYE dscont_i;

|PROCESO Baja100;
     |SI FSalida = "POSICION";
         #100#0 = "PLAN CONTABLE DE 2008";
     |SINO;
         #100#0 = "";
     |FINSI;
|FPRC;

|PROCESO Alta100;
     #100#0 = "z" * 40;
|FPRC;

|PROCESO Tipo0;
     aAlfa   = #100#0;  |QBF aAlfa;
     aAlfa   = aAlfa % -104;
     enPlan = aAlfa;
     enEjercicio = 0;

     |SI enPlan = 2008;
         ||  eaPlanillaX = "caticuen";  |HAZ PlanDePlanillas;
     |FINSI;

     aParametro = PARAMETRO;
     |SI aParametro = "caconiso";
         |SI enPlan = 2015; aParametro = "caconi15"; |FINSI;
         |SI enPlan = 2008; aParametro = "caconi08"; |FINSI;
         |SI enPlan = 1990; aParametro = "caconiso"; |FINSI;
     |FINSI;

     |SUB_EJECUTA_NP aParametro;
|FPRC;


|PROCESO VerElCierre;
     aAlfa = ""; nSw6 = 0; nSw8 = 0;

     |ABRE #6;
     #6#22 = eaNifEmp;
     |LEE 001#6=;
     |SI FSalida = 0; nSw6 = 1; |FINSI;
     |CIERRA #6;

     |ABRE #8;
     #8#22 = eaNifEmp;
     |LEE 001#8=;
     |SI FSalida = 0; nSw8 = 1; |FINSI;
     |CIERRA #8;

     |SI nSw6 = 1; |Y aParametro = "cacieex2C";
         aAlfa = "    Existen datos del asiento en la Entrada Normal.";
     |FINSI;
     |SI nSw8 = 1; |Y aParametro = "cacieex2";
         aAlfa = "    Existen datos del asiento en la Entrada de Cooperativas.";
     |FINSI;

     |SI aAlfa ! "";
         aAlfa = aAlfa + " Acceda por esa opcion.";
         |MENAV aAlfa;
         aParametro = "";
     |FINSI;
|FPRC;

|PROGRAMA;

     aParametro = PARAMETRO;
     |SI aParametro = "cacieex2"; |O aParametro = "cacieex2C";
         |HAZ inicio;
         |SI enEjercicio ] 2008;
             |HAZ VerElCierre;
             |SI enEjercicio ] 2015;
                 |SI aParametro = "cacieex2";  |SUB_EJECUTA "cacieexa"; |FINSI;
             |SINO;
                 |SI aParametro = "cacieex2";  |SUB_EJECUTA "cacieex6"; |FINSI;
                 |SI aParametro = "cacieex2C"; |SUB_EJECUTA "cacieex8"; |FINSI;
             |FINSI;
         |SINO;
             |SI aParametro = "cacieex2";
                 |SUB_EJECUTA "cacieex2";
             |SINO;
                 |MENAV "    Proceso a partir del Ejercicio 2008";
             |FINSI;
         |FINSI;
         |ACABA;
     |FINSI;

     aFichero = ("CC" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #100 aFichero;

     |ABRE #100;  |CIERRA #100;  |DELINDEX #100;

     |ABRE #100;
     |LEE 000#100p;
     |SI FSalida ! 0;
         #100#0 = "PLAN CONTABLE DE 1990";  |GRABA 020#100n;  |LIBERA #100;
         #100#0 = "PLAN CONTABLE DE 2008";  |GRABA 020#100n;  |LIBERA #100;
         #100#0 = "PLAN CONTABLE DE 2015";  |GRABA 020#100n;  |LIBERA #100;
     |FINSI;

     |CLS;
     |PINPA #0#100;
     |ENTLINEAL #1#100, -1, 4, 13, 1, Baja100, Alta100;

     |CIERRA #100;  |DELINDEX #100;
|FPRO;
