|FICHEROS;
   guey0001 #0;

   camaasil #1;
   camaniva #2;
   cafantas #3;
   cacuenta #4;
   caivaasi #200;

   dscam042@ #142;     || Control C
   dscam043@ #143;     || Control L
   dscam008@ #108;     || Recibos Compra

   canempre #30;

   guew0001 #901;
   guew0002 #902;
   guew0003 #903;

   caselem1 #998;
|FIN;


|VARIABLES;

  &nSwAlgun = 0;
  Primero   = 0;
  informe   = "";
  inform1   = "guey1001";
  inform2   = "guey0001";

  aAlfa1    = "";
  aPathCart = "";
  aDef08    = "";
  aDef43    = "";
  aDef42    = "";
  nErr      = 0;
  aPathDef  = "";
  aPathFic  = "";
  aPathFicE = "";
  aFichero  = "";
  fDFecha   = @;
  fHFecha   = @;
  aTipo     = "";
  &nLinea2   = 0;

  &SwLin    = 0;
  &SwCuenta = 0;
  &enTot1   = 0;
  &enTot2   = 0;
  &enTot3   = 0;
  &enTot4   = 0;

  aTitCta  = "";
  CuentaAnt = "";
  nImp1     = 0;
  nImp2     = 0;
  nImp3     = 0;
  nImp4     = 0;
  nLibro    = 0;
  aSerie    = "";
  nFactura  = 0;
  nNumRec   = 0;
  nPendiente = 0;
  nPagado   = 0;
  nTRecibo  = 0;
  aEstado   = "";
  nTotal    = 0;
  aReferencia = "";
  fEmision  = @;
  fVto      = @;
|FIN;

|INCLUYE dscont_i;

|| *****************************************************************
|CALCULO ElTipo8; |TIPO 8;

 |HAZ inicio;

 |ABRE #200;
 |LEE 001#200p;
 |SI FSalida ! 0;
     |DDEFECTO #200;
 |FINSI;
 |CIERRA #200;

 aAlfa1 = #200#44;
 |C_M #0#9, 1,aAlfa1;
 |C_M #0#10,1,aAlfa1;

 nErr = 0;
 aPathCart = #200#56; |QBF aPathCart;
 |SI aPathCart = "";
     |MENAV "    Error. No tiene informado el Path de la Cartera ";
     |PTEC 807;
 |SINO;
     |HAZ CargaDef;
     |SI nErr ! 0;
         |MENAV "    Error al Cargar ficheros de Cartera ";
         |PTEC 807;
     |FINSI;
 |FINSI;
 |HAZ eParaModelos;
|FCAL;

|CALCULO fechad; |TIPO 0;
  fDFecha = fec1;
  |SI enSwSelFc = 1; fDFecha = "01.01.2000"; |FINSI;
  |SI #0#6 < fec1; |O #0#6 > fec2;
      |MENAV "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
      |ERROR;
  |FINSI;
|FCAL;

|CALCULO fechah; |TIPO 0;
   fDFecha = fec1;
   |SI enSwSelFc = 1; fDFecha = "01.01.2000"; |FINSI;
   |SI #0#7 < fDFecha; |O #0#7 > fec2; |O #0#7 < #0#6;
      |MENAV "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
      |ERROR;
   |FINSI;
|FCAL;

|CALCULO cuenta; |TIPO 0;
     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;
|FCAL;

|CALCULO defectos; |TIPO 7;
  #0#6 = fec1;    |PINTA #0#6;
  #0#7 = fec2;    |PINTA #0#7;
  #0#2 = "40.";   |PINTA #0#2;
  #0#3 = "4199."; |PINTA #0#3;
  #0#0 = #200#29; |PINTA #0#0;
  #0#1 = #200#29; |PINTA #0#1;
|FCAL;

|PROCESO nodepar; |TIPO 7;
   |CAMPO_MODIFICA #0#13, 1, eaDepar;
   |CAMPO_MODIFICA #0#14, 1, eaDepar;
|FPRC;

|| ******************************************************************
|PROCESO CargaDef;
     nErr = 1;

     aPathDef = aPathCart + "def/";
     aPathFic = aPathCart + "fich/";

     aDef42 = aPathDef + "dscam042";
     |CARGA_DEF aDef42, dscam042@;
     |SI FSalida ! 0;
          aDef42 = ""; |HAZ DescargaDef;
          |ACABA;
     |FINSI;

     aDef43 = aPathDef + "dscam043";
     |CARGA_DEF aDef43, dscam043@;
     |SI FSalida ! 0;
          aDef43 = ""; |HAZ DescargaDef;
          |ACABA;
     |FINSI;

     aDef08 = aPathDef + "dscam008";
     |CARGA_DEF aDef08, dscam008@;
     |SI FSalida ! 0;
          aDef08 = ""; |HAZ DescargaDef;
          |ACABA;
     |FINSI;

     |PATH_DAT #142 aPathFic;
     |PATH_DAT #143 aPathFic;

     nErr = 0;
|FPRC;

|PROCESO DescargaDef; |TIPO 9;
     |SI aDef08 ! ""; |DESCARGA_DEF #dscam008@; aDef08 = ""; |FINSI;
     |SI aDef43 ! ""; |DESCARGA_DEF #dscam043@; aDef43 = ""; |FINSI;
     |SI aDef42 ! ""; |DESCARGA_DEF #dscam042@; aDef42 = ""; |FINSI;
|FPRC;

|| ******************************************************************
|| ******************************************************************
|| ******************************************************************
|| ... Grabar Auxiliares
|PROCESO LaCuenta;
 |ABRE #4;
 |DDEFECTO #4;
 #4#0 = CuentaAnt;
 |LEE 041#4=;
 |SI FSalida = 0;  aTitCta = #4#2; |FINSI;
 |CIERRA #4;
|FPRC;

|PROCESO GrabaAux901;

 nImp4  = 0;
 #901#0 = CuentaAnt;
 |LEE 001#901=;
 |SI FSalida ! 0;
     |DDEFECTO #901;
     |HAZ LaCuenta;
     #901#0 = CuentaAnt;
     #901#1 = aTitCta;
     #901#2 = 0;
     #901#3 = 0;
     #901#4 = 0;
     #901#5 = -#4#53;
     nImp4  = #901#5;
     |GRABA 020#901c;
 |FINSI;

 #901#2 = #901#2 + nImp1;
 #901#3 = #901#3 + nImp2;
 #901#4 = #901#4 + nImp3;
 |GRABA 020#901a;
 nSwAlgun = 1;

 #3#0 = #3#0 + nImp1;
 #3#1 = #3#1 + nImp2;
 #3#2 = #3#2 + nImp3;
 #3#3 = #3#3 + nImp4;
|FPRC;

|PROCESO GrabaAux902;
    |DDEFECTO #902;
    #902#0 = CuentaAnt;
    #902#1 = nLibro;
    #902#2 = aSerie;
    #902#3 = nFactura;
    |LEE 001#902=;
    |SI FSalida = 0;
        |ACABA;         || Ya Existe
    |FINSI;

    #902#0 = CuentaAnt;
    #902#1 = nLibro;
    #902#2 = aSerie;
    #902#3 = nFactura;
    #902#4 = nTotal;
    #902#5 = fEmision
    #902#6 = aReferencia;
    |GRABA 020#902n;
|FPRC;

|PROCESO GrabaAux903;
    |DDEFECTO #903;
    #903#0 = CuentaAnt;
    #903#1 = nLibro;
    #903#2 = aSerie;
    #903#3 = nFactura;
    #903#4 = nNumRec;
    |LEE 001#903=;
    |SI FSalida = 0;
        |ACABA;         || Ya Existe
    |FINSI;

    #903#0 = CuentaAnt;
    #903#1 = nLibro;
    #903#2 = aSerie;
    #903#3 = nFactura;
    #903#4 = nNumRec;
    #903#5 = nPendiente;
    #903#6 = nPagado;
    #903#7 = nTRecibo;
    #903#8 = aEstado;
    #903#9 = fEmision;
    #903#10 = fVto;
    |GRABA 020#903n;

     nImp1  = nPendiente;
     nImp2  = 0;
     nImp3  = 0;
     |HAZ GrabaAux901;
|FPRC;

|| *************************************************************************
|| \\\\\\\\\\\\\\\\\\\\\\\\\ Recibos de Compras
|PROCESO dscam008;

    |SI #108#14 = "A"; |O #108#14 = "D"; |O #108#14 = "L";
        |ACABA;
    |FINSI;

    CuentaAnt = #108#5;
    aTitCta   = #108#6;
    nLibro    = #108#27;
    aSerie    = #108#0;
    nFactura  = #108#1;
    nNumRec   = #108#2;
    nPendiente = #108#58;  ||Pendiente
    nPagado    = #108#57;  ||Pagado
    nTRecibo   = #108#56;  ||Recibo
    aEstado    = #108#14;  ||Estado
    fEmision   = #108#4;   ||Emision
    fVto       = #108#3;   ||Vto

    |HAZ GrabaAux903;
    |SI #0#11 = "E";
        nTotal = 0;
        aReferencia = "Desde Cartera";
        |HAZ GrabaAux902;
    |FINSI;
|FPRC;

|DEFBUCLE dscam008;
     #108#2004;
     4,5;
     #0#0, #0#9,  #0#4, 001, #0#6, #0#2;
     #0#1, #0#10, #0#5, 999, #0#7, #0#3;
     ;
     NULL, dscam008;
|FIN;
|| /////////////////////////////////

|PROCESO dscam043;

  |SI #0#0 > #143#6; |O #0#1 < #143#5;
      |ACABA;     ||Fuera Seleccion
  |FINSI;

  |SI #0#9 > #143#8; |O #0#10 < #143#7;
      |ACABA;     ||Fuera Seleccion
  |FINSI;

  aAlfa1 = #143#9;
  aAlfa1 = ("00000" + aAlfa1) % -105;
  aPathFicE = aPathFic + aAlfa1 + "/";

  |PATH_DAT #108 aPathFicE;

  |BUCLE dscam008;
|FPRC;

|DEFBUCLE dscam043;
     #143#1003;
     3;
     enEmpresa, enPeriodo, 001, aTipo;
     enEmpresa, enPeriodo, 999, aTipo;
     ;
     NULL, dscam043;
|FIN;

|PROCESO LeeCartera;

  |ABRE #142;
  #142#0 = enEmpresa;
  #142#1 = enPeriodo;
  |LEE 001#142=;
  |SI FSalida ! 0;
      |CIERRA #142;
      |ACABA;
      || ... No existe la empresa en la Cartera
  |FINSI;
  |CIERRA #142;

  aTipo = "C";
  |BUCLE dscam043;
|FPRC;

|| ------------------------------
|PROCESO LeeFacturas;

 CuentaAnt = #2#12;
 aTitCta   = #2#13;
 nImp1     = 0;
 nImp2     = 0;
 nImp3     = #2#21;

 |HAZ GrabaAux901;

 |SI #0#11 = "E";
     nLibro    = #2#0;
     aSerie    = #2#2;
     nFactura  = #2#3;
     fEmision  = #2#4;   ||Emision
     nTotal    = #2#21;
     aReferencia = #2#22;
     |HAZ GrabaAux902;
 |FINSI;
|FPRC;

|DEFBUCLE LeeFacturas;
   #2#4;
   2,3;
   #0#2, #0#6, #0#0, 0000000000, #0#9, #0#4;
   #0#3, #0#7, #0#1, 9999999999, #0#10,#0#5;
   ;
   NULL, LeeFacturas;
|FIN;

||................................

|PROCESO LeeAsientos;

 CuentaAnt = #1#4;
 nImp1     = 0;
 nImp3     = 0;

 |SI #1#8 = "H";
     nImp2 = #1#9;
 |SINO;
     nImp2 = - #1#9;
 |FINSI;
 |HAZ GrabaAux901;
|FPRC;

|DEFBUCLE LeeAsientos;
   #1#3;
   ;
   #0#2, fDFecha,      1, 0001;
   #0#3, fHFecha, 999999, 9999;
   ;
   NULL, LeeAsientos;
|FIN;
|| -----------------------------

|PROCESO HazTodo;

   fDFecha = fec1;  || Principio o desde pantalla ???
   |SI enSwSelFc = 1; fDFecha = "01.01.2000"; |FINSI;
   fHFecha = #0#7;

   |DELINDEX #901;
   |DELINDEX #902;
   |DELINDEX #903;

   #3#0 = 0;
   #3#1 = 0;
   #3#2 = 0;
   #3#3 = 0;

   nSwAlgun = 0;
   |ABRE #901;
   |ABRE #902;
   |ABRE #903;
   |BUCLE LeeAsientos;
   |SI #0#11 = "E"; |BUCLE LeeFacturas; |FINSI;
   |HAZ LeeCartera;

   |CIERRA #903;
   |CIERRA #902;
   |CIERRA #901;
   |SI nSwAlgun = 1;
       |HAZ Imprimir;
   |FINSI;
|FPRC;
||---------------------------------------------------------------------------
|PROCESO LeeAux903;
 nLinea2 = nLinea2 + 1;

 |PRINT;
 SwLin    = 2;
 nSwAlgun = 1;
 SwCuenta = 0;

 |SI nLinea2 = 1;  || Suma una vez
     enTot1 = enTot1 + #902#4;
     enTot3 = enTot3 + #902#4;
 |FINSI;

 enTot2 = enTot2 + #903#5;
 enTot4 = enTot4 + #903#5;
|FPRC;

|DEFBUCLE LeeAux903;
 #903#1;
 ;
 #902#0, #902#1, #902#2, #902#3, 001;
 #902#0, #902#1, #902#2, #902#3, 999;
 ;
 NULL, LeeAux903;
|FIN;

|PROCESO LeeAux902;
 nLinea2 = 0;
 SwLin   = 1;
 |BUCLE LeeAux903;
 |SI nLinea2 = 0;
     |PRINT; nSwAlgun = 1; SwCuenta = 0;
     enTot1 = enTot1 + #902#4;
     enTot3 = enTot3 + #902#4;
 |FINSI;
|FPRC;

|DEFBUCLE LeeAux902;
 #902#1;
 ;
 #901#0, 00, "     ", 000001;
 #901#0, 99, "zzzzz", 999999;
 ;
 NULL, LeeAux902;
|FIN;

|PROCESO LeeAuxiliar;

  |SI #0#11 = "R";
      |PRINT;
      nSwAlgun = 1;
      |ACABA;
  |FINSI;


  |SI Primero = 0;
      Primero = 1;
      CuentaAnt = #901#0;
      SwCuenta = 1;
      enTot1   = 0;
      enTot2   = 0;
  |FINSI;

  |SI CuentaAnt ! #901#0;
      SwLin = 4;
      |PRINT; nSwAlgun = 1;
      CuentaAnt = #901#02;
      SwCuenta = 1;
      enTot1   = 0;
      enTot2   = 0;
  |FINSI;

  SwLin   = 0;
  |BUCLE LeeAux902;
  SwLin   = 0;
|FPRC;

|DEFBUCLE LeeAuxiliar;
 #901#1;
 ;
 #0#2;
 #0#3;
 ;
 NULL, LeeAuxiliar;
|FIN;

|PROCESO Imprimir;

   enTot1 = 0; enTot2 = 0; enTot3 = 0; enTot4 = 0;
   Primero = 0;
   nSwAlgun = 0;
   |BUCLE LeeAuxiliar;
   |SI nSwAlgun = 1;
       |SI #0#11 = "E";
           SwLin = 4; |PRINT;
       |FINSI;
       |PIEINF;
   |FINSI;
|FPRC;

|| ************************************************
|CALCULO ElTipo3; |TIPO 3;

  aFichero = ("1zg" + Usuario) % 108;
  |NOME_DAT #901 aFichero;
  aFichero = ("2zg" + Usuario) % 108;
  |NOME_DAT #902 aFichero;
  aFichero = ("3zg" + Usuario) % 108;
  |NOME_DAT #903 aFichero;

      |IMPRE 0;
      |SI FSalida < 0; |ACABA; |FINSI;

      informe = inform1;
      |SI #0#11 = "E";  informe = inform2;  |FINSI;

      |INFOR informe;
      |SI FSalida < 0; |ACABA; |FINSI;

  |SI #48#27 = "S";
      |HAZ MultiEmpresa;
  |SINO;
      |DFICE dirfich0; |QBF dirfich0;
      |HAZ HazTodo;
  |FINSI;
  |FININF;
  |FINIMP;
|FCAL;

|| //////////////////////////////////////////////////////////////////
|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #2 dirfich0;
   |PATH_DAT #4 dirfich0;
   |PATH_DAT #200 dirfich0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 000001;
 999999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;
|| ************************************************************************
