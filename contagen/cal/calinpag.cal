|FICHEROS;
   calinpag #0;
   caclipro #2;

   caforpag #3;
   caforopc #4,MANTE;    || pantalla vencimiento opcional

   dscamre@ #5;
   &Puente@ #1;

   caivaasi #200;
   caintvto #110;

   catmpvto #900;

   Referen@ #1000;

   dscam008@ #1001;
   dscam042@ #1002;
   dscam043@ #1003;
   dscam002@ #1004;
|FIN;

|VARIABLES;
   &eaNombre = "";

   nCambiaVto = -1;

   &entrada = 1;

   long = 0;
   sw = 0;
   nPara1 = 0;
   nume1 = 0;
   nume2 = 0;
   nume3 = 0;
   nume4 = 0;
   aAlfa = "";
   alfa1 = "";
   alfa2 = "";
   alfa3 = "";
   alfa4 = "";

   &enPCAut = 0;
   &eaRef = "";
   &pesetas = 0;
   &cuenta = "";
   &digito = 0;
   &libro = 0;
   &orden = 0;
   &serie = "";
   &depart = "";
   &emision = "";
   &nConcep = 0;
   &aDesc = "";
   recibo = 0;
   fectra1 = @;
   fectra2 = @;
   fectra3 = @;
   sw_alguno = 0;
   ptasrec1 = 0;
   ptasrec2 = 0;
   xx = 0;
   nSwNuevos = 0;
   fFv = @;
   fFe = @;
   nIm = 0;
   nTR = 0;
   nBan = 0;
   nFlag = 0;
   nNume = 0;
   nDeforma = 0;

   nSwModo    = 0;
   nSwError   = 0;
   NumDoc     = 0;
   Comodin    = "";
   &eaPath    = "";
   &eaRetorno = "";
   &enModoEsc = 0;
   &enAux     = 0;

   nCalc = 0;
   nDia  = 0;
   nDia1 = 0;
   nDia2 = 0;
   nDia3 = 0;
   aAlfa1 = "";
   nIncre1 = "";
   aIncre1 = "";

   nNumVtos  = 0;
   nNumTempo = 0;
   nGraba    = 0;

    nCalcFec = 0;
    aAlfaFec = "";
    fDFecha = @;
    fHFecha = @;
    &swVarios = 0;

    aQueQuiero = "";
    aNumCampos = "";
    nNumCampos = 0;
|FIN;

|INCLUYE dscont_i;

|PROCESO GrabaAux;

    |DDEFECTO #0;
    #0#0 = #110#0;
    #0#1 = #110#1;
    #0#2 = #110#2;
    #0#3 = #110#3;
    |LEE 101#0=;
    |SI FSalida ! 0;
        |DDEFECTO #0;
        #0#0 = #110#0;
        #0#1 = #110#1;
        #0#2 = #110#2;
        #0#3 = #110#3;
        |GRABA 020#0b;
    |FINSI;

    #0#4  = #110#4;
    #0#5  = #110#5;
    #0#6  = #110#6;
    #0#7  = #110#7;
    #0#8  = #110#8;

    #0#9  = 0;
    #0#10 = 0;
    #0#11 = #110#9;

   #0#12 = 0;      || nro. remesa
   #0#13 = "N";    || emision documento
   #0#14 = #2#13;  || banco
   #0#15 = #2#14;  || direccion
   #0#16 = #2#15;  || cod.postal 1
   #0#17 = #2#16;  || cod.postal 2
||   #0#18 = #2#17;  || cta.cte.
||   #0#19 = #2#18;  || ccc
   #0#20 = #2#19;  || poblacion
   #0#21 = "";     || observaciones
   #0#22 = "N";    || liquidado NO
   #0#23 = 0;      || ptas. liquidadas
   #0#24 = "N";    || exportado NO
   #0#25 = 0;
   #0#26 = #110#10;  || Banco pago por defecto;

   #0#27 = nConcep;
   #0#28 = aDesc;

   #0#29 = #2#29; || Entidad
   #0#30 = #2#30; || Sucursal
   #0#31 = #2#31; || DC
   #0#32 = #2#32; || Cuenta

    sw_alguno = 1;
    |GRABA 020#0a;
    |LIBERA #0;

    |BORRA 020#110a;
    |LIBERA #110;
|FPRC;

|DEFBUCLE GrabaAux;
 #110#1;
 ;
 libro, serie, orden,   1;
 libro, serie, orden, 999;
 be;
 NULL, GrabaAux;
|FIN;

|PROCESO CargaAuxiliar;
 |ABRE #0;
 |BUCLE GrabaAux;
 |CIERRA #0;
|FPRC;

|| -------------------------

|PROCESO comprueba;
    |SI emision ! #calinpag#6; |Y enAux = 0;
       aAlfaFec = "01." + (("00" + #48#11) % -102) + "."; |QBF aAlfaFec;
       nCalcFec = #48#26;
       |SI nCalcFec > 80;
          nCalcFec = nCalcFec + 1900;
       |SINO;
          nCalcFec = nCalcFec + 2000;
       |FINSI;
       aAlfaFec = aAlfaFec + nCalcFec;
       fDFecha = aAlfaFec;

       nCalcFec = #48#12; nCalcFec = nCalcFec + 1;
       |SI nCalcFec > 12;
          nCalcFec = nCalcFec - 12;
          aAlfaFec = nCalcFec;
          nCalcFec = #48#26 + 1;
       |SINO;
          nCalcFec = #48#26;
       |FINSI;
       |SI #48#26 > 80;
          nCalcFec = nCalcFec + 1900;
       |SINO;
          nCalcFec = nCalcFec + 2000;
       |FINSI;
       aAlfaFec = "01." + (("00" + aAlfaFec) % -102) + "." + nCalcFec;
       fHFecha = aAlfaFec; nCalcFec = fHFecha; nCalcFec = nCalcFec - 1;
       fHFecha = nCalcFec;

       |SI #calinpag#6 < fDFecha; |O #calinpag#6 > fHFecha; |BORRA 020#calinpag.a; |FINSI;
       |ACABA;
    |FINSI;

   sw_alguno = 1;
   |SI #0#4 ! cuenta;  nSwNuevos = 1;  |FINSI;
   |HAZ carga_pro;
   |GRABA 020#0a;
   |LIBERA #0;
   nBan = #0#26;
|FPRC;

|DEFBUCLE calinpag0;
   #0#1;
   ;
   libro, serie, orden,    1;
   libro, serie, orden,  999;
   be;
   NULL, comprueba;
|FIN;

|PROCESO VuelcaRecs;
   |ABRE #caclipro;
   #2#23 = "P";
   #2#10 = #0#4; eaCuenta = #0#4; |HAZ SacaNivel;
   #2#11 = enNivel;
   |LEE 000#caclipro.=;
   |SI FSalida ! 0; |DDEFECTO #caclipro; |FINSI;
   |CIERRA #caclipro;

   |ABRE #caforpag;
   #caforpag#0 = #caclipro#20;
   |LEE 000#caforpag.=;
   |SI FSalida ! 0;
       |DDEFECTO #caforpag;
       |SI #caclipro#20 = 0;
           #caforpag#2  = 1;
       |FINSI;
   |FINSI;
   |CIERRA #caforpag;

   |DDEFECTO #Puente@;

   #Puente@#0  = #0#0;            || Libro
   #Puente@#1  = #0#2;            || Serie
   #Puente@#2  = #0#1;            || Factura
   #Puente@#3  = #0#3;            || Numero recibo
   #Puente@#4  = #0#7;            || Fecha vto.
   #Puente@#5  = #0#6;            || Fecha emision
   #Puente@#6  = #0#4;            || Cuenta Proveedor
||   #Puente@#7  = #2#1;            || Nombre Proveedor
   ||ABDON N§ tiquet 010028/00698
   |QBF eaNombre;
   |SI eaNombre ! "";
      #Puente@#7  = eaNombre;     || Nombre Proveedor
   |SINO;
      #Puente@#7  = #2#1;         || Nombre Proveedor
   |FINSI;
   #Puente@#8  = #0#14;           || Nombre Banco
   #Puente@#9  = #0#29;           || Entidad
   #Puente@#10 = #0#30;           || Sucursal
   #Puente@#11 = #0#31;           || DC
   #Puente@#12 = #0#32;           || N§ Cuenta
   #Puente@#13 = #0#11;           || Tipo de recibo
   #Puente@#14 = "";                     || Departamento
   #Puente@#15 = #0#8;            || Importe Recibo
   #Puente@#16 = "";                     || A Aceptar
   #Puente@#17 = "";                     || Aceptado
   #Puente@#18 = "";                     || Codigo Cliente (GESTION)
   #Puente@#19 = "";                     || Representante
   #Puente@#20 = 0;                      || Nombre Representante
   #Puente@#21 = "   ";                  || Zona
   #Puente@#22 = NumDoc;                 || N§ Documento
   #Puente@#23 = enEmpresa;              || Codigo Empresa
   #Puente@#24 = enPeriodo;              || Periodo Empresa
   || #Puente@#23 = #48#2;                  || Codigo Empresa
   || #Puente@#24 = #48#3;                  || Periodo Empresa
   #Puente@#25 = "";                     || Empresa gestion
   #Puente@#26 = "N";                    || Recibo a cuenta
   #Puente@#27 = "C";                    || Tipo de recibo
   |SI enModoEsc = 1; #Puente@#28 = "A"; |FINSI; || Tipo de Operacion
   |SI enModoEsc = 2; #Puente@#28 = "M"; |FINSI;
   |SI enModoEsc = 3; #Puente@#28 = "B"; |FINSI;
   |SI #48#28 = "P";
      #Puente@#29 = "ESP";               || Divisa
      #Puente@#30 = "PESETA";            || Nombre Divisa
      #Puente@#31 = 166.386;             || Cambio Divisa en EUROS
      #Puente@#32 = 166.386;             || Cambio M.Base en EUROS
   |SINO;
      #Puente@#29 = "EUR";               || Divisa
      #Puente@#30 = "EURO";              || Nombre Divisa
      #Puente@#31 = 1;                   || Cambio Divisa en EUROS
      #Puente@#32 = 1;                   || Cambio M.Base en EUROS
   |FINSI;
   #Puente@#33 = #0#26;
   #Puente@#37 = eaRef;
   #Puente@#40 = #caclipro#20;
   #Puente@#41 = #caforpag#1;

   |SI nNumCampos ] 44;
       #Puente@#44 = #2#37;           || Forma pago confirming
   |FINSI;
   |SI nNumCampos ] 49;
       #Puente@#48 = #caclipro#40;
       #Puente@#49 = #caclipro#41;
   |FINSI;

   nGraba = 0;
   |SI #Puente@#28 ! "B"; |Y #Puente@#15 = 0;
       nGraba = 1;
   |FINSI;

   |SI nGraba = 0;
       |GRABA 020#Puente@.n;
       NumDoc = NumDoc + 1;
   |FINSI;
|FPRC;

|DEFBUCLE VuelcaRecs;
  #0#1;
  ;
  libro,serie,orden,001;
  libro,serie,orden,999;
  ;
  NULL,VuelcaRecs;
|FIN;

|PROCESO RectificaVtos;
   #catmpvto#0 = #calinpag#0;
   #catmpvto#1 = #calinpag#2;
   #catmpvto#2 = #calinpag#1;
   #catmpvto#3 = #calinpag#3;
   |LEE 000#catmpvto.=;
   |SI FSalida = 0;
      #calinpag#7 = #catmpvto#4;
      |GRABA 020#calinpag.a; |LIBERA #calinpag;

      |BORRA 020#catmpvto.a;
   |FINSI;
|FPRC;

|DEFBUCLE RectificaVtos;
#calinpag#1;
;
libro,serie,orden,000;
libro,serie,orden,999;
be;
NULL, RectificaVtos;
|FIN;

|PROCESO CompruebaCuadrados;
   nCalc = (nCalc + #calinpag#8) ! 2;
|FPRC;

|DEFBUCLE CompruebaCuadrados;
#calinpag#1;
;
libro, serie, orden,   1;
libro, serie, orden, 999;
;
NULL, CompruebaCuadrados;
|FIN;

|PROCESO CuentaTempo;
   nNumTempo = nNumTempo  + 1;
|FPRC;

|DEFBUCLE CuentaTempo;
#catmpvto#1;
;
libro, serie, orden, 000;
libro, serie, orden, 999;
;
NULL, CuentaTempo;
|FIN;

|PROGRAMA;

   |SI swVarios = 1; |Y dirfich0 ! "";
      |PATH_DAT #0  dirfich0;
      |PATH_DAT #2  dirfich0;
      |PATH_DAT #3  dirfich0;
      |PATH_DAT #4  dirfich0;
      |PATH_DAT #110  dirfich0;
      |PATH_DAT #200  dirfich0;
      |PATH_DAT #900  dirfich0;
   |FINSI;

   NumDoc = 1;
   |ABRE #200;
   |LEE 000#200p;
   |SI FSalida ! 0;
      |DDEFECTO #200;
      |GRABA 020#200n;
      |LEE 000#200p;
   |FINSI;
   |CIERRA #200;

   Comodin = #200#56; |QBF Comodin;
   |SI Comodin = ""; |ACABA; |FINSI;

   eaPath = Comodin;
   |HAZ CreaPuente;
   |SI eaRetorno = "ERROR"; |ACABA; |FINSI;

   aQueQuiero = "|NC";
   aNumCampos = #Puente@#aQueQuiero#;
   nNumCampos = aNumCampos;
   nNumCampos = nNumCampos - 1;

   |HAZ proveedor;
   sw_alguno = 0;
   |SI enAux = 1;
       nSwNuevos = 1;
       |HAZ CargaAuxiliar;
       |SI sw_alguno = 0;
           |HAZ CierraPuente;
           |ACABA;
       |FINSI;
   |FINSI;

|ET OtraVez;
   sw_alguno = 0;
   |BUCLE calinpag0;
   |SI sw_alguno = 0;
       |SI enPCAut = 0;
           aAlfa = "tv" + Usuario; |NOME_DAT #catmpvto#aAlfa#;
           |ABRE #catmpvto;
           nSwNuevos = 1;
           |HAZ pide_pago;
           |HAZ f_pago;
           |SI #200#1 = "S";
               |SI nCambiaVto = -1;
                  nCambiaVto = 0;
                  nNumTempo = 0; |BUCLE CuentaTempo;
                  |SI nNumTempo = nNumVtos;
                     #catmpvto#0 = libro;
                     #catmpvto#1 = serie;
                     #catmpvto#2 = orden;
                     #catmpvto#3 = 0;
                     |LEE 000#catmpvto.];
                     |SI FSalida = 0; |Y #catmpvto#0 = libro; |Y #catmpvto#1 = serie; |Y #catmpvto#2 = orden;
                        aAlfa = "    N" + &191 + " Desea conservar las fechas de vencimiento anteriores ?";
                        |CONFI aAlfa;
                        |SI FSalida = 0; nCambiaVto = 1; |FINSI;
                     |FINSI;
                  |FINSI;
               |FINSI;

               |HAZ carga;
               |SI nCambiaVto = 1; |BUCLE RectificaVtos; |FINSI;

               |SI PARAMETRO ! "";
                   nSwNuevos = 0;
                   |BUCLE VuelcaRecs;
                   |HAZ CierraPuente;
                   |HAZ CreaPuente;
               |FINSI;
               |SI enAux = 0; |Y sw_alguno ! 0;
                   |ENTLINEAL #1#0, -4, 2, 21, 1, min, max;
               |FINSI;
           |FINSI;
       |FINSI;
   |SINO;
       ||SI PARAMETRO = "NUEVO"; nSwNuevos = 1; |FINSI;
       |SI enAux = 0;
            |ENTLINEAL #1#0, -4, 2, 21, 1, min, max;
       |FINSI;
   |FINSI;

/*
   nCalc = 0;                     ||borrado tique 3073_2463
   |BUCLE CompruebaCuadrados;
   |SI nCalc ! pesetas;
      |MENAV "    Los vencimientos no suman el total de la factura.";
      |VETE OtraVez;
   |FINSI;
*/

   NumDoc = 1;
   |SI nSwNuevos ! 0; |O enAux = 3;
         |BUCLE VuelcaRecs;
   |FINSI;
   |HAZ CierraPuente;
|FPRO;

|PROCESO min;
   #0#0 = libro;
   #0#1 = orden;
   #0#2 = serie;
   #0#3 = 1;
|FPRC;

|PROCESO max;
   #0#0 = libro;
   #0#1 = orden;
   #0#2 = serie;
   #0#3 = 999;
|FPRC;

|PROCESO carga;
   nSwModo = 0;

   |ABRE #0;
   recibo = 0;
   |HAZ importe;
   |SI #3#2 ! 0;            || numero de vencimientos
      fectra1 = emision;

      nIncre1   = #3#3;
      |HAZ CalIncre;
      recibo = recibo + 1;
      |HAZ graba;

      recibo = recibo + 1;
      |PARA nPara1 = recibo; |SI nPara1 [ #3#2; |HACIENDO nPara1 = nPara1 + 1;
            nIncre1 = #3#4;
            |HAZ CalIncre;
            recibo = nPara1;
            |HAZ graba;                     || incremento vencimientos
      |SIGUE;
   |SINO;
      |SI #3#0 = 0;
          fectra1 = emision; recibo = 1; #3#2 = 1;
          |HAZ graba;
      |FINSI;
   |FINSI;
   |CIERRA #0;
|FPRC;

|PROCESO importe;
   |SI #3#2 = 1;
      ptasrec1 = 0;
      ptasrec2 = pesetas;
   |SINO;
      ptasrec1 = (pesetas / #3#2) ! EURODB;   || todos vto.menos ultimo
      nume2 = ptasrec1 * (#3#2 - 1);
      ptasrec2 = pesetas - nume2;               || ultimo vto.
   |FINSI;
|FPRC;

|PROCESO graba;
   #0#0 = libro;
   #0#1 = orden;
   #0#2 = serie;
   #0#3 = recibo;
   #0#4 = cuenta;
   #0#5 = digito;
   #0#6 = emision; || emision
   |HAZ DiasFijos;
   #0#7 = fectra1; || vto.
   |SI recibo ! #3#2;
      #0#8 = ptasrec1;  || anteriores al ultimo
   |SINO;
      #0#8 = ptasrec2;  || utlimo recibo
   |FINSI;
   #0#9 = 0;       || gastos negociacion
   #0#10 = 0;      || gastos impagados
   #0#11 = #3#5;  || tipo recibo
   #0#12 = 0;      || nro. remesa
   #0#13 = "N";    || emision documento
   #0#14 = #2#13;  || banco
   #0#15 = #2#14;  || direccion
   #0#16 = #2#15;  || cod.postal 1
   #0#17 = #2#16;  || cod.postal 2
||   #0#18 = #2#17;  || cta.cte.
||   #0#19 = #2#18;  || ccc
   #0#20 = #2#19;  || poblacion
   #0#21 = "";     || observaciones
   #0#22 = "N";    || liquidado NO
   #0#23 = 0;      || ptas. liquidadas
   #0#24 = "N";    || exportado NO
   #0#25 = 0;
   #0#26 = #2#28;  || Banco pago por defecto;

   #0#27 = nConcep;
   #0#28 = aDesc;

   #0#29 = #2#29; || Entidad
   #0#30 = #2#30; || Sucursal
   #0#31 = #2#31; || DC
   #0#32 = #2#32; || Cuenta
   |SI #0#8 ! 0;
      |HAZ MiraSiCertContr;
      |GRABA 020#0n;
      sw_alguno = 1;
   |FINSI;
|FPRC;

|PROCESO ayudita; |TIPO 0;
   xx = (FEntrada / 100) ! 0;
   |SI xx = 1;
      #0#6 = emision;            || emision
      nIncre1 = #3#4;
      |HAZ CalIncre;  || cuenta un vencimiento mas
      |HAZ DiasFijos;
      #0#7 = fectra1;            || vto.
      #0#26 = nBan;
      |PINTA #0#6;
      |PINTA #0#7;
      |PINTA #0#26;
   |FINSI;
|FPRC;

|PROCESO manuales; |TIPO 2;
   xx = (FEntrada / 100) ! 0;
   |SI xx = 1;
      |HAZ carga_pro;
      nSwNuevos = 1;
   |FINSI;
    |SI xx = 2;
       nFlag = 0 +. 1;
       |SI nFlag < 0;
          fFv = #0#7;
          fFe = #0#6;
          nIm = #0#8;
          nTR = #0#11;
          nBan = #0#26;
       |SINO;
          |SI nSwNuevos = 0;
             |SI fFv ! #0#7; |O nIm ! #0#8; |O nTR ! #0#11; |O fFe ! #0#6; |O nBan ! #0#26;
                nSwNuevos = 1;
             |SINO;
                nSwNuevos = 0;
             |FINSI;
          |FINSI;
       |FINSI;
    |FINSI;
    |SI xx = 3;
       nNume = enModoEsc; enModoEsc = 3; |HAZ VuelcaRecs; enModoEsc = nNume;
       |HAZ CierraPuente; |HAZ CreaPuente;
    |FINSI;
|FPRC;

|PROCESO MiroFVto;
 |SI #0#6 > #0#7;
     |SI #200#162 = "S";
         |CONFI "    NFecha de Emision menor que Fecha de Vencimiento. Continuar ?";
         |SI FSalida ! 0;
             |ERROR;
         |FINSI;
     |FINSI;
     |SI #200#162 = "X";
         |MENAV "    Fecha de Emision menor que Fecha de Vencimiento.";
         |ERROR;
     |FINSI;
 |FINSI;

 nSwModo = 0;
 #calinpag#4 = cuenta;
 |HAZ MiraSiCertContr;
|FPRC;


|PROCESO nocero; |TIPO 0;
  |SI FSalida = 2; |ACABA; |FINSI;
  |SI #calinpag#Campo# = 0;
      |MENAV "    Error! Importe igual a 0";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO carga_pro;
   #0#4 = cuenta;
   #0#5 = digito;
   #0#14 = #2#13;  || banco
   #0#15 = #2#14;  || direccion
   #0#16 = #2#15;  || cod.postal 1
   #0#17 = #2#16;  || cod.postal 2
   #0#20 = #2#19;  || poblacion
   #0#29 = #2#29; || Entidad
   #0#30 = #2#30; || Sucursal
   #0#31 = #2#31; || DC
   #0#32 = #2#32; || Cuenta

   fectra1 = #0#7; || guarda ultima fecha de vto.
|FPRC;
____________________________________/ LECTURAS
|PROCESO proveedor;
   |DDEFECTO #2;
   |ABRE #2;
   #2#23 = "P";
   #2#10 = cuenta; || cuenta
   #2#11 = digito; || digito
   |LEE 000#2=;
   |CIERRA #2;
|FPRC;

|PROCESO f_pago;
   nNumVtos = 1;

   |ABRE #3;
   |DDEFECTO #3;
   #3#0 = #4#0;
   |LEE 001#3=;
   |SI FSalida ! 0;
      |SI #3#0 ! 0;
         |MENAV "    ATENCION!!! Codigo de Vencimiento inexistente ...";
      |FINSI;
   |SINO;
      nNumVtos = #3#2;
   |FINSI;

   |CIERRA #3;
|FPRC;

|PROCESO pide_pago;
   #4#0 = #2#20; |HAZ f_pago;
   |SI #4#0 ! 0; |Y PARAMETRO = "";

      |PUSHV 05012380;
      ||DDEFECTO #4;
      |PINPA #0#4;
      |PINDA #0#4;
      |ATRI I; |PINTA 1332, #3#1; |ATRI 0;
      |ENDATOS #1#4;
      |SI FSalida ! 0; |O #4#1 = "N"; #4#0 = 0; |FINSI;
      |POPV;
   |FINSI;
   |SI #4#0 = 0; #4#0 = nDeforma; |FINSI;
|FPRC;

|PROCESO CalIncre;
 aIncre1 = "";
 nume1   =  (nIncre1 / 30) - ((nIncre1 / 30) ! 0);
 |SI nume1 = 0;

     nume1 = nIncre1 / 30;
     alfa1 = nume1;
     alfa1 = ("000" + alfa1) % -103;
     aIncre1 = "0." + alfa1 + "0000";

     fectra1 = fectra1 + aIncre1;
 |SINO;
     fectra1 = fectra1 + nIncre1;
 |FINSI;

|FPRC;

|PROCESO DiasFijos;

     |SI #2#34 = 0; |Y #2#35 = 0; |Y #2#36 = 0;
         |ACABA;
     |FINSI;

     nDia1 = 0; nDia2 = 0; nDia3 = 0;
     |SI #2#34 ! 0; nDia1 = #2#34; |FINSI;
     |SI #2#35 ! 0;
         |SI nDia1 > #2#35;
             nDia2 = nDia1;
             nDia1 = #2#35;
         |SINO;
             nDia2 = #2#35;
         |FINSI;
     |FINSI;
     |SI #2#36 ! 0;
         |SI nDia2 > #2#36;
             nDia3 = nDia2;
             nDia2 = #2#36;
         |SINO;
             nDia3 = #2#36;
         |FINSI;
         |SI nDia1 > #2#36;
             nDia2 = nDia1;
             nDia1 = #2#36;
         |FINSI;
     |FINSI;

     aAlfa = fectra1; aAlfa = aAlfa % 0102; nCalc = aAlfa;
     nDia  = nCalc;
     nCalc = 0;
     |SI nDia [ nDia3; |Y nDia3 ! 0;
         nCalc = nDia3;
     |FINSI;
     |SI nDia [ nDia2; |Y nDia2 ! 0;
         nCalc = nDia2;
     |FINSI;
     |SI nDia [ nDia1; |Y nDia1 ! 0;
         nCalc = nDia1;
     |FINSI;
     |SI nCalc = 0; nCalc = nDia1; |FINSI;

     aAlfa = fectra1; |QBF aAlfa;
     aAlfa = (("00" + nCalc) % -102) + (aAlfa % 0308);

     |SI fectra1 > aAlfa;
         fectra2 = aAlfa;
         fectra2 = fectra2 + "0.001000";  || pasa al mes siguiente
         aAlfa   = fectra2;
     |FINSI;

|| ... Problema de febrero
     alfa1   = aAlfa % 402; nume1 = alfa1; ||mes
     alfa2   = aAlfa % 102; nume2 = alfa2; ||dia

     |SI nume1 = 2; |Y nume2 ] 29;
         nCalc = 28;
         alfa3 = aAlfa % -104; nume3 = alfa3; ||any
         nume4 = (nume3 / 4) - ((nume3 / 4) ! 0);
         |SI nume4 = 0; nCalc = 29; |FINSI;
         aAlfa = (("00" + nCalc) % -102) + (aAlfa % 0308);
     |FINSI;
     |SI nume2 = 31;
         |SI nume1 = 4; |O nume1 = 6; |O nume1 = 9; |O nume1 = 11;
             aAlfa = "30" + (aAlfa % 0308);
         |FINSI;
    |FINSI;

    fectra1 = aAlfa;
|FPRC;

|PROCESO MiraSiCertContr;
   nSwError = 0;
   |DBASS aAlfa; |QBF aAlfa;
   aAlfa = aAlfa + "basica/def/tadm0001.mas";
   |CARGA_DEF aAlfa, Referen@;
   |SI FSalida = 0;
      |DBASS aAlfa; |QBF aAlfa;
      aAlfa = aAlfa + "basica/fich/"; |PATH_DAT #Referen@#aAlfa#;
      |ABRE #Referen@; |ABRE #caclipro;
      #caclipro#23 = "P";
      #caclipro#10 = #calinpag#4;
      |LEE 000#caclipro.=;
      |SI FSalida = 0;
         #Referen@#0 = #48#2;
         #Referen@#1 = #caclipro#9;
         |LEE 000#Referen@.=;
         |SI FSalida = 0;
            |SI #Referen@#3 = "S";
               |SI #Referen@#4 < #calinpag#7;
                  |SI nSwModo = 0;
                     aAlfa = "    NEl vencimiento supera el certificado. " + &191 + " Consultar registro ? ";
                     |CONFI aAlfa;
                     |SI FSalida = 0;
                        aAlfa = "|:basica/tadz0001.tab;" + (("00000" + #48#2) % -105) + "," + #48#8; |QBF aAlfa;
                        aAlfa = aAlfa + "," + #48#1; |QBF aAlfa;
                        aAlfa = aAlfa + "|" + #caclipro#9; |QBF aAlfa;
                        |SUB_EJECUTA aAlfa;
                     |FINSI;
                     nSwModo = 1;
                  |FINSI;
                  nSwError = 1;
               |FINSI;
            |FINSI;
         |FINSI;
      |FINSI;
      |CIERRA #caclipro;
      |CIERRA #Referen@; |DESCARGA_DEF #Referen@;
   |FINSI;
|FPRC;
