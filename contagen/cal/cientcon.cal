|FICHEROS;
   cientasc #0;
   cientasl #1;
   cacuenta #2;
   cisalant #3;
   cientcon #4;
   :/modelos/def/dsmom030 #5;
   caclipro #18;
|FIN;

|VARIABLES;
   &entrada = 1;
   modo = 0;

   signo = "";
   elimporte = 0;
   &total = 0;
   &sumal = 0;
   &difer = 0;
   &fecapu = @;
   &sigapu = "";
   &descrira = "";
   &fecant = @;
   &coant = 0;
   &deant = 0;
   descri = "";
   nume1 = 0;
   swalgun = 0;
   swborra = 0;
   SwSoloUno = 0;

   aAlfa1 = "";
   nAlta  = 0;
   &snFiltrA = "";
|FIN;

|INCLUYE dscont_i;

|| ***********************
|PROCESO comprueba_alta2;
   |CIERRAT #cacuenta;
   ||SALVA_DATOS #cacuenta;
   |NOME_DAT #cacuenta #"cacuenta"#;
   |ABRET #cacuenta;
   |LEE 000#cacuenta.=;
   |SI FSalida ! 0;
        ||REPON_DATOS #cacuenta;
        |PDEFECTO #cacuenta, 9, 54;
        |PDEFECTO #cacuenta, 58, 59;
        |GRABA 020#cacuenta.n;
   |FINSI;
   |CIERRAT #cacuenta;
   |NOME_DAT #cacuenta #cactaesp#;
   |ABRET #cacuenta;
|FPRC;

|RUTINA pintaimportes;
   |PINTA 0924,"             ";
   |PINTA 0949,"             ";
   |ATRI I;
   |PINTA 0924,sumal;
   |PINTA 0949,difer;
   |ATRI 0;
|FRUT;

|PROCESO laslineas;  |TIPO 2;
   sumal = sumal +. #4#8;
   difer = total - sumal;
   |HAZ pintaimportes;
   |HAZ actualiza_cta;
|FPRC;

|PROCESO lee_clienC;
     |ABRE #18;
     #18#23 = "C";
     #18#10 = eaCuenta;
     |LEE 041#18=;
     |SI FSalida = 0;
         nAlta = 1;
     |SINO;
         |DDEFECTO #18;
         #18#23 = "P";
         #18#10 = eaCuenta;
         |LEE 041#18=;
         |SI FSalida = 0;
             nAlta = 1;
         |FINSI;
     |FINSI;
     |CIERRA #18;
|FPRC;

|PROCESO Para347C;
     nAlta = 0;
     |SI eaCP347 = "S"; |Y #dsmom030#7 = "S";
         eaCuenta = #4#4;
         |HAZ lee_clienC;
     |FINSI;

     |SI nAlta = 0;
         #4#9  = 0;
         #4#10 = 0;
         |C_V #4#9,1, "N";
         |C_M #4#9,1, "N";
     |SINO;
         |C_V #4#9,1, "S";
         |C_M #4#9,1, "S";
         |SI #4#9 = 0;
             #4#9 = enEjercicio;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO AntesPara347C; |TIPO 7;
     |C_M #4#9,0,aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     |PUSHV 1153 1480;
     |PINTA 1155, "                       ";
     |PINTA 1255, "                       ";
     |PINTA 1355, "                       ";
     |PINTA 1455, "                       ";
     |D_CUADRO 1155,1478;
     |ATRI R; |PINTA 1256, " Ejercicio Operacion "; |ATRI 0;
     |PINTA #4#9;

|FPRC;

|PROCESO TrasPara347C; |TIPO 0;
     |C_M #4#9,0,aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     |SI #4#9 < 2000; |ERROR; |ACABA; |FINSI;
     |POPV;
     |C_V #4#9,1, "N";
     |C_M #4#9,1, "N";
|FPRC;

|PROCESO elimporte; |TIPO 0;
   |SI #cientcon#8 = 0;
      |MENAV  "     Debe Introducir un Importe";
      |ERROR;
   |SINO;
      |HAZ Para347C;
   |FINSI;
|FPRC;

|PROCESO tipo11;  |TIPO 11;
   |SI FSalida ! 6;  |Y FSalida ! 7;  |Y FSalida ! 1;
       |ACABA;
   |FINSI;

   difer = difer ! EURODB;
   |SI difer ! 0;
      |MENAV "     ATENCION!  NO Esta Cuadrado el Asiento";
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO tipo3;  |TIPO 3;
   |SI #cientcon#8 = 0;
      |MENAV "     ATENCION!  El importe NO puede ser CERO";
      |ERROR;
   |SINO;
       enFilConc = 11;
       eaCuenta   = #4#4;
       enConcepto = #4#6;
       |HAZ FiltroConcepto;
       |SI eswLect ! 0;
           |ERROR;
           |ACABA;
       |FINSI;
       coant    = #cientcon#6;
       descrira = #cientcon#7;
   |FINSI;
|FPRC;


|PROCESO eldigito;  |TIPO 6;
     eaCuenta = #cientcon#Campo#;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #cientcon#Campo# = eaCuenta;
     |PINTA #cientcon#Campo#;

     eaCuenta = #cientcon#Campo#;
     |QBF eaCuenta;
     |SI eaCuenta = "";
         |MENAV "    introduzca cuenta";
         |ERROR;
         |ACABA;
     |FINSI;

     emCta = 1;
     |HAZ SacaNivel;
     emCta = 0;
     |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea
     #cientcon#5 = enNivel;
|FPRC;

|PROCESO sacacuenta; |TIPO 0;
   |SI #cacuenta#3 ! "S";
      |MENAV "     LA CUENTA NO ES DE PASE DIRECTO";
      |ERROR;
      |ACABA;
   |FINSI;

   aAlfa1 = #cisalant#12;
   |SI aAlfa1 = "N";
       enFilConc = 11;
       eaCuenta   = #4#4;
       enConcepto = #4#6;
       |HAZ FiltroConcepto;
       |SI eswLect ! 0;
           |ERROR;
           |ACABA;
       |FINSI;
   |FINSI;

   |ATRI I;
   |PINTA 2203, #cacuenta#2;
   |PINTA 2240, #cacuenta#51;
   |PINTA 2254, #cacuenta#52;
   |PINTA 2268, #cacuenta#53;
   |ATRI 0;
   |HAZ comprueba_alta;
|FPRC;

|PROCESO Antescon; |TIPO 7;
  modo = (FEntrada / 100) ! 0;
  |SI modo = 1;
      #cientcon#6 = coant;    |PINTA #cientcon#6;
      #cientcon#7 = descrira; |PINTA #cientcon#7;
  |FINSI;
  aAlfa1 = #cisalant#12; |C_M #cientcon#6, 1, aAlfa1;
  aAlfa1 = #cisalant#13; |C_M #cientcon#7, 1, aAlfa1;
|FPRC;

|CALCULO Desdecon; |TIPO 0;  || desde Campos conceptos
   modo = ((FEntrada / 100) ! 0);
   enFilConc = 1;
   eaCuenta   = #cientcon#4;
   enConcepto = #cientcon#6;
   |HAZ FiltroConcepto;  |SI eswLect ! 0; |ACABA; |FINSI;
   |SI modo = 1;
       |SI #cientcon#6 = coant;
           #cientcon#7  = descrira;
       |SINO;
           #cientcon#7  = #dsmom030#1;
       |FINSI;
   |SINO;
      |SI #cientcon#6 ! coant;
          #cientcon#7 = #dsmom030#1;
      |FINSI;
   |FINSI;
   |PINTA #cientcon#7;
|FCAL;

|PROCESO Elptec; |TIPO 7;
   |C_M #cientcon#Campo#, 0, aAlfa1;
   |SI aAlfa1 = "S"; |Y eaSobreEs ! "S";
      |PTEC 816;
   |FINSI;
|FPRC;

|PROCESO duplica; |TIPO 0;
   |SI FSalida = 10;
       #cientcon#7 = descrira;
       |PINTA #cientcon#7;
   |FINSI;
|FPRC;

|CALCULO entrando; |TIPO 13;
  |SI SwSoloUno = 0;
      |CIERRAT #cacuenta;
      |NOME_DAT #cacuenta #cactaesp#;
      |ABRET #cacuenta;
       SwSoloUno = 1;
  |FINSI;

   #cacuenta#0 = #cientcon#4;
   #cacuenta#1 = #cientcon#5;
   |LEE 001#cacuenta.=;
   |SI FSalida ! 0;  |DDEFECTO #cacuenta;  |FINSI;
   |ATRI I;
   |PINTA 2203, #cacuenta#2;
   |PINTA 2240, #cacuenta#51;
   |PINTA 2254, #cacuenta#52;
   |PINTA 2268, #cacuenta#53;
   |ATRI 0;

   |HAZ pintaimportes;
|FCAL;

|PROCESO actualiza_cta;
  elimporte = 0 +. #cientcon#8;
  |SI sigapu  = "D";  signo = "H";  |SINO;  signo = "D";  |FINSI;
  MasMenos     = elimporte;
  DebeHaber    = signo;
  aCUENTA      = #cientcon#4;
  nNIVELCUENTA = #cientcon#5;
  nREGISTRO    = -1;
  nDIARIO      = 01;     ||No tiene diario asignado
  aFECHA       = fecapu;
  nDOCUMENTO   = -1;
  |HAZ ActCuentM;          || cuenta de pase Directo S
|FPRC;
