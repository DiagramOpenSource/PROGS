|FICHEROS;
   cavaexpl #0;
   cacuenta #1;
   caparexp #2;
   cabaexp0 #3;
   cabaexp1 #4;
   caexistc #6;
   caexistl #7;
   caconimp #8;
   canempre #30;
|FIN;

|VARIABLES;
   &pathbal = "";
   &balance = 1;
   &entrada = 0;
   varcamp = 0;
   mes = 0;
   campito = 0;
   nivel = 0;
   sw_pasa = 0;
   linea = 0;
   FEstado = 0;
   posici = 0;
   x = 0;
   perdesde = 0;
   &eldesde = 0;
   perhasta = 0;
   &elhasta = 0;
   debe1 = 0;
   debe2 = 0;
   debe3 = 0;
   debe4 = 0;
   debp1 = 0;
   debp2 = 0;
   debp3 = 0;
   debp4 = 0;
   sw_d1 = 0;
   sw_d2 = 0;
   sw_d3 = 0;
   sw_d4 = 0;
   haber1 = 0;
   haber2 = 0;
   haber3 = 0;
   habep1 = 0;
   habep2 = 0;
   habep3 = 0;
   sw_h1 = 0;
   sw_h2 = 0;
   sw_h3 = 0;
   sw = 0;
   bloque = 0;
   agrupaci = 0;
   epigrafe = 0;
   partida = 0;
   bloque1 = 0;
   H = 0;
   sw_epi = 0;
   sw_d21 = 0;
   sw_d31 = 0;
   sw_h21 = 0;
   sw_h31 = 0;
   sw_h32 = 0;
   punto_h1 = 0;
   punto_d1 = 0;
   punto_h2 = 0;
   punto_d2 = 0;
   punto_h3 = 0;
   punto_d3 = 0;
   punto_h4 = 0;
   punto_d4 = 0;
   punto_h5 = 0;
   punto_d5 = 0;
   puntp_h1 = 0;
   puntp_d1 = 0;
   puntp_h2 = 0;
   puntp_d2 = 0;
   puntp_h3 = 0;
   puntp_d3 = 0;
   puntp_h4 = 0;
   puntp_d4 = 0;
   puntp_h5 = 0;
   puntp_d5 = 0;
   xxxxfiac = "  ";          || Variables para no modificable
   xxaponer = "  ";
   xrelleno = "                              ";
   letrames = "";
   informe  = "";
   d = "D";
   h = "H";
   tipo = "";
   tipo1 = "";
   &sw_inf = 0;
   &cantidad = 0;
   &per_can = 0;
   &variable = "";
   &canti = 0;
   &per_canti = 0;
   &doh = "";
   mes_fin = 0;
   descar = 0;
   traraf = 0;
   factor = 0;
   ingresos = "INGRESOS DE LA EXPLOTACION";
   gastos = "GASTOS DE LA EXPLOTACION";
   &varit = "";
   nivelet = 0;
   c_cta = "";
   c_dig = 0;
   c_swt = 0;

   para1 = 0;
   para2 = 0;
   para3 = 0;
   para4 = 0;
   nume1 = 0;
   nume2 = 0;
   nume3 = 0;
   nume4 = 0;
   alfa1 = "";
   alfa2 = "";
   alfa3 = "";
   alfa4 = "";
   ult_cuent = "";
   ult_nivel = 0;
   nue_cuent = "";
   nue_nivel = 0;
   act_cuent = "";
   act_nivel = 0;
   carac = 0;
   guarda_can = 0;
   guarda_per = 0;
   &d_mes = 0;
   &h_mes = 0;
   &estru = 0;
   &r_eje = 0;
   &r_nom = "";
   &r_per = 0;
   &r_emp = 0;
   &aparam = "";
   destot = 0;
   desper = 0;
   aNomCta = "";
   aNomCta1 = "";
|FIN;

|INCLUYE dscont_i;
|INCLUYE digito_i;


|PROCESO antesque;
   aNomCta = "cacuenta";
   aparam = PARAMETRO; |SI aparam = " N"; aparam = ""; |FINSI;
   |QBF aparam;
   |SI aparam = "fech";
       aNomCta = "cabalano";
   |FINSI;
   |SI aparam = "Analitica";
       aNomCta = eaUsuAna;
   |FINSI;
   |NOME_DAT #1 aNomCta;
|FPRC;
____________________________________/ CALCULOS DE PANTALLA

|CALCULO AnaT13; |TIPO 13;
 aparam = PARAMETRO; |SI aparam = " N"; aparam = ""; |FINSI;
 |SI aparam = "Analitica";
     |ATRI P;
     |PINTA 0572, "ANALITICO";
     |ATRI 0;
     #30#1 = eaNomAna;
 |FINSI;
|FCAL;

|CALCULO inicio_b; |TIPO 8;
  |HAZ inicial;
  |SI swerror = 2; |PTEC 807; |ACABA; |FINSI;
  |SI swerror = 3; |PTEC 831; |PTEC 807; |ACABA; |FINSI;
  |HAZ DirAplicSt;
|FCAL;

|CALCULO estruc7; |TIPO 7;
 #0#6 = 1;
 |SI Haybasica = 1; |Y eaDepar = "S";
     #0#6 = 0;
 |FINSI;
 |PINTA #0#6;
|FCAL

|CALCULO pintames; |TIPO 0;
   varcamp = Campo + 2;                || Coge el campo del nombre del mes
   |SI #0Campo ! 0;|Y #0Campo ! 13;
      mes = #canempre#11 + (#0Campo - 1);   || Operacion para saber que mes es
   |SINO;
      mes = #0Campo;
   |FINSI;
   |HAZ mesletra;                      || Recoge el mes en letra
   #0varcamp = letrames;
   |PINTA #0varcamp;                   || Pinta el nombre del mes

   |SI Campo = 1;
       |SI #0#0 > #0#1;
           |MENAV "    Error de Entrada ...";
           |ERROR;
       |FINSI;                         || Si el mes hasta es menor al desde
   |FINSI;
|FCAL;

|CALCULO pintames2; |TIPO 0;
   |SI #0Campo ! 0;|Y #0Campo ! 13;
      mes = #canempre#11 + (#0Campo - 1);   || Operacion para saber que mes es
   |SINO;
      mes = #0Campo;
   |FINSI;
   |HAZ mesletra;
   |ATRI I;
   |SI Campo =  7;  |PINTA 1727, letrames;  |FINSI;
   |SI Campo =  8;  |PINTA 1827, letrames;  |FINSI;
   |SI Campo = 24;  |PINTA 1750, letrames;  |FINSI;
   |SI Campo = 25;  |PINTA 1850, letrames;  |FINSI;
   |ATRI 0;
|FCAL;

|PROCESO mesletra;
   |SI mes = 0;  letrames = "Apertura  "; |FINSI;
   |SI mes = 1;  letrames = "Enero     "; |FINSI;
   |SI mes = 2;  letrames = "Febrero   "; |FINSI;
   |SI mes = 3;  letrames = "Marzo     "; |FINSI;
   |SI mes = 4;  letrames = "Abril     "; |FINSI;
   |SI mes = 5;  letrames = "Mayo      "; |FINSI;
   |SI mes = 6;  letrames = "Junio     "; |FINSI;
   |SI mes = 7;  letrames = "Julio     "; |FINSI;
   |SI mes = 8;  letrames = "Agosto    "; |FINSI;
   |SI mes = 9;  letrames = "Septiembre"; |FINSI;
   |SI mes = 10; letrames = "Octubre   "; |FINSI;
   |SI mes = 11; letrames = "Noviembre "; |FINSI;
   |SI mes = 12; letrames = "Diciembre "; |FINSI;
   |SI mes = 13; letrames = "Cierre    "; |FINSI;
|FPRC;


|CALCULO estruct; |TIPO 0;
   |SI #0#6 ! 0;
      |ABRE #6;
      #caexistc#0 = #0#6;
      |LEE 040#6=;
      |SI FSalida ! 0;
         |MENAV "    No existe la estructura de descarga.";
         |CIERRA #6;
         |ERROR;
         |ACABA;
      |FINSI;
      |CIERRA #6;
   |FINSI;
|FCAL;

|PROCESO c_des; |TIPO 0;
   |SI #0#13 < #0#0;
      |MENAV "    Periodo no comprendido en Acumulados ...";
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO c_has; |TIPO 0;
   |SI #0#14 < #0#13;|O #0#14 > #0#1;
      |MENAV "    Periodo no comprendido en Acumulados ...";
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO numnivel; |TIPO 0;
   |SI #0Campo > #canempre#13;
       |MENAV "    El numero de niveles no coincide con el definido en la Empresa ...";
       |ERROR;
   |FINSI;
   nivelet = 0;
|FPRC;

|PROCESO nivelet; |TIPO 7;
   |SI Campo=19;|Y #0#17 < 2; #0Campo = 0; |PINTA #0Campo; |HAZ nomodifi; |FINSI;
   |SI Campo=20;|Y #0#17 < 3; #0Campo = 0; |PINTA #0Campo; |HAZ nomodifi; |FINSI;
   |SI Campo=21;|Y #0#17 < 4; #0Campo = 0; |PINTA #0Campo; |HAZ nomodifi; |FINSI;
   |SI Campo=22;|Y #0#17 < 5; #0Campo = 0; |PINTA #0Campo; |HAZ nomodifi; |FINSI;
   |SI Campo=23;|Y #0#17 < 6; #0Campo = 0; |PINTA #0Campo; |HAZ nomodifi; |FINSI;
|FPRC;

|PROCESO nomodifi;
   xxaponer = Anono;
   xxxxfiac = Control % A1001;
   Control = Control + xxxxfiac;
   Control = Control + xrelleno;
   Control = Control % A120;
   Control = Control + xxaponer;
|FPRC;

|PROCESO nivelrep; |TIPO 0;

   |SI nivelet = 0;    nivelet = #0#18;     |FINSI;
   
   |SI Campo = 19;
      |SI #0#19 = #0#18;|Y #0#19 ! 0;    || Si hay dos niveles iguales error
         |MENAV "    Nivel repetido ...";
         |ERROR;
         |ACABA;
      |SINO;
         |SI #0#19 > #0#18;  || Busca el nivel mas alto
            nivelet = #0#19;
         |SINO;
            nivelet = #0#18;
         |FINSI;
      |FINSI;
   |FINSI;
   
   |SI Campo = 20; |Y #0#20 ! 0;
      |SI #0#20 = #0#18; |O #0#20 = #0#19; || Si hay dos niveles iguales error
         |MENAV "    Nivel repetido ...";
         |ERROR;
         |ACABA;
      |FINSI;
      
      |SI #0#20 > nivelet;  || Busca el nivel mas alto
         nivelet = #0#20;
      |FINSI;
   |FINSI;
   
   |SI Campo = 21; |Y #0#21 ! 0;      || Si hay dos niveles iguales error
      |SI #0#21 = #0#18; |O #0#21 = #0#19; |O #0#21 = #0#20;
         |MENAV "    Nivel repetido ...";
         |ERROR;
         |ACABA;
      |FINSI;
      
      |SI #0#21 > nivelet;  || Busca el nivel mas alto
         nivelet = #0#21;
      |FINSI;
   |FINSI;
   
   |SI Campo = 22; |Y #0#22 ! 0;     || Si hay dos niveles iguales error
      |SI #0#22 = #0#18; |O #0#22 = #0#19; |O #0#22 = #0#20; |O #0#22 = #0#21;
         |MENAV "    Nivel repetido ...";
         |ERROR;
         |ACABA;
      |FINSI;
      
      |SI #0#22 > nivelet;  || Busca el nivel mas alto
         nivelet = #0#22;
      |FINSI;
   |FINSI;
   
   |SI Campo = 23; |Y #0#23 ! 0;    || Si hay dos niveles iguales error
      |SI #0#23 = #0#18; |O #0#23 = #0#19; |O #0#23 = #0#20; |O #0#23 = #0#21; |O #0#23 = #0#22;
         |MENAV "    Nivel repetido ...";
         |ERROR;
         |ACABA;
      |FINSI;
      
      |SI #0#23 > nivelet;  || Busca el nivel mas alto
         nivelet = #0#23;
      |FINSI;
   |FINSI;
|FPRC;
____________________________________/ BUSQUEDA DE EXISTENCIAS EN TEMPORAL
|PROCESO miracuen2;
   alfa1 = #cacuenta#0;    |QBF alfa1;
   alfa2 = alfa1 % 0;    nume2 = alfa2; nume2 = nume2 + 100;
   alfa3 = #cabaexp1#6 % nume2;
   alfa3 = (alfa3 + "            ") % 112;
   |SI alfa3 = #cacuenta#0;|Y #cabaexp1#13 = #cabaexp0#0;|Y #cabaexp1#14 = #cabaexp0#1;|Y #cabaexp1#15 = #cabaexp0#2;|Y #cabaexp1#16 = #cabaexp0#3;|Y #cabaexp1#17 = #cabaexp0#4;
      destot = (#cabaexp1#4 - #cabaexp1#5);
      |SI #cacuenta#5 = "H"; destot = -destot; |FINSI;
      cantidad = cantidad + destot;
      sw_pasa = 1;
   |FINSI;
   
   alfa1 = #cacuenta#0;    |QBF alfa1;
   alfa2 = alfa1 % 0;    nume2 = alfa2; nume2 = nume2 + 100;
   alfa3 = #cabaexp1#11 % nume2;
   alfa3 = (alfa3 + "            ") % 112;
   |SI alfa3 = #cacuenta#0;|Y #cabaexp1#18 = #cabaexp0#0;|Y #cabaexp1#19 = #cabaexp0#1;|Y #cabaexp1#20 = #cabaexp0#2;|Y #cabaexp1#21 = #cabaexp0#3;|Y #cabaexp1#22 = #cabaexp0#4;
      desper = (#cabaexp1#9 - #cabaexp1#10);
      |SI #cacuenta#5 = "H"; desper = -desper; |FINSI;
      per_can = per_can + desper;
      sw_pasa = 1;
   |FINSI;
|FPRC;

|DEFBUCLE busca_exist2;
   #4#1;
   ;
   #0#6, 001;
   #0#6, 999;
   ;
   NULL, miracuen2;
|FIN;

|PROCESO miracuen;
   |SI #cabaexp1#6 = #cacuenta#0; |Y #cabaexp1#7 = #cacuenta#1;
      destot = (#cabaexp1#4 - #cabaexp1#5);
      |SI #cacuenta#5 = "H"; destot = -destot; |FINSI;
      cantidad = cantidad + destot;
      sw_pasa = 1;
   |FINSI;
   |SI #cabaexp1#11 = #cacuenta#0; |Y #cabaexp1#12 = #cacuenta#1;
      desper = (#cabaexp1#9 -#cabaexp1#10);
      |SI #cacuenta#5 = "H"; desper = -desper; |FINSI;
      per_can = per_can + desper;
      sw_pasa = 1;
   |FINSI;
|FPRC;

|DEFBUCLE busca_exist;
   #4#1;
   ;
   #0#6, 001;
   #0#6, 999;
   ;
   NULL, miracuen;
|FIN;

|PROCESO leexplot;  || Lee el fichero de explotacion
   |DDEFECTO #2;
   #caparexp#0 = #cabaexp0#1;    || bloque
   #caparexp#1 = #cabaexp0#0;    || tipo
   #caparexp#2 = #cabaexp0#2;    || agrupacion
   #caparexp#4 = #cabaexp0#3;    || epigrafe
   #caparexp#6 = #cabaexp0#4;    || partida
   |LEE 000#2=;
   
   |SI #cabaexp0#0 = "D";     #caparexp#3 = gastos;     |FINSI;
   |SI #cabaexp0#0 = "H";     #caparexp#3 = ingresos;   |FINSI;
|FPRC;
____________________________________/
|PROCESO resulta;    || Calculamos cada cuenta
   |SI #cabaexp0#0 = "D"; |Y #cabaexp0#1 = 1;
      |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
         #cabaexp0#8 = #cabaexp0#8   + #cacuenta#x#;
         debe1 = debe1 + #cacuenta#x#;
         |SI x ] perdesde;|Y x [ perhasta;
            #cabaexp0#10 = #cabaexp0#10 + #cacuenta#x#;
            debp1 = debp1 + #cacuenta#x#;
         |FINSI;
      |SIGUE;
      sw_d1 = 1;
   |FINSI;
   
   |SI #cabaexp0#0 = "D"; |Y #cabaexp0#1 = 2;
      |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
         #cabaexp0#8 = #cabaexp0#8   + #cacuenta#x#;
         debe2 = debe2 + #cacuenta#x#;
         |SI x ] perdesde;|Y x [ perhasta;
            #cabaexp0#10 = #cabaexp0#10 + #cacuenta#x#;
            debp2 = debp2 + #cacuenta#x#;
         |FINSI;
      |SIGUE;
      sw_d2 = 1;
   |FINSI;
   
   |SI #cabaexp0#0 = "D"; |Y #cabaexp0#1 = 3;
      |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
         #cabaexp0#8 = #cabaexp0#8   + #cacuenta#x#;
         debe3 = debe3 + #cacuenta#x#;
         |SI x ] perdesde;|Y x [ perhasta;
            #cabaexp0#10 = #cabaexp0#10 + #cacuenta#x#;
            debp3 = debp3 + #cacuenta#x#;
         |FINSI;
      |SIGUE;
      sw_d3 = 1;
   |FINSI;
   
   |SI #cabaexp0#0 = "D"; |Y #cabaexp0#1 = 4;
      |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
         #cabaexp0#8 = #cabaexp0#8   + #cacuenta#x#;
         debe4 = debe4 + #cacuenta#x#;
         |SI x ] perdesde;|Y x [ perhasta;
            #cabaexp0#10 = #cabaexp0#10 + #cacuenta#x#;
            debp4 = debp4 + #cacuenta#x#;
         |FINSI;
      |SIGUE;
      sw_d4 = 0;
   |FINSI;
   
   |SI #cabaexp0#0 = "H"; |Y #cabaexp0#1 = 1;
      |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
         #cabaexp0#8   = #cabaexp0#8   + (#cacuenta#x# * -1);
         haber1 = haber1 + (#cacuenta#x# * -1);
         |SI x ] perdesde;|Y x [ perhasta;
            #cabaexp0#10 = #cabaexp0#10 + (#cacuenta#x# * -1);
            habep1 = habep1 + (#cacuenta#x# * -1);
         |FINSI;
      |SIGUE;
      sw_h1 = 1;
   |FINSI;
   
   |SI #cabaexp0#0 = "H"; |Y #cabaexp0#1 = 2;
      |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
         #cabaexp0#8   = #cabaexp0#8   + (#cacuenta#x# * -1);
         haber2 = haber2 + (#cacuenta#x# * -1);
         |SI x ] perdesde;|Y x [ perhasta;
            #cabaexp0#10 = #cabaexp0#10 + (#cacuenta#x# * -1);
            habep2 = habep2 + (#cacuenta#x# * -1);
         |FINSI;
      |SIGUE;
      sw_h2 = 1;
   |FINSI;
   
   |SI #cabaexp0#0 = "H"; |Y #cabaexp0#1 = 3;
      |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
         #cabaexp0#8   = #cabaexp0#8   + (#cacuenta#x# * -1);
         haber3 = haber3 + (#cacuenta#x# * -1);
         |SI x ] perdesde;|Y x [ perhasta;
            #cabaexp0#10 = #cabaexp0#10 + (#cacuenta#x# * -1);
            habep3 = habep3 + (#cacuenta#x# * -1);
         |FINSI;
      |SIGUE;
      sw_h3 = 1;
   |FINSI;
|FPRC;

|PROCESO grabatra;
   #cabaexp0#0 = #cacuenta#5;
   #cabaexp0#1 = #cacuenta#4;
   #cabaexp0#2 = #cacuenta#6;
   #cabaexp0#3 = #cacuenta#7;
   #cabaexp0#4 = #cacuenta#8;
   |LEE 101#3=;
   FEstado = FSalida;
   |SI FEstado ! 0;
      |DDEFECTO #3;
      #cabaexp0#0 = #cacuenta#5;
      #cabaexp0#1 = #cacuenta#4;
      #cabaexp0#2 = #cacuenta#6;
      #cabaexp0#3 = #cacuenta#7;
      #cabaexp0#4 = #cacuenta#8;
   |FINSI;
   |HAZ leexplot;
   #cabaexp0#5 = #caparexp#3;
   #cabaexp0#6 = #caparexp#5;
   #cabaexp0#7 = #caparexp#7;
   sw_pasa = 0;
   |BUCLE busca_exist;
   |SI sw_pasa = 0;
      |HAZ resulta;
      #cabaexp0#9 = 0;
      |SI #cabaexp0#8 ! 0;|O #cabaexp0#10 ! 0;
         |SI FEstado = 0; |GRABA 020#3a; |FINSI;
         |SI FEstado ! 0; |GRABA 020#3n; |FINSI;
      |FINSI;
   |FINSI;
   |LIBERA #3;
|FPRC;

|PROCESO calculit;
   |HAZ digitos2;
   |SI d_error ! 0;  |ACABA;  |FINSI;
   
   |SI #0#4 = "S";
      |SI #cacuenta#55 [ nivelet;
         |SI #cacuenta#3 = "S";
            |HAZ grabatra;
         |SINO;
            |SI #cacuenta#55 = nivelet; |HAZ grabatra; |FINSI;
         |FINSI;
      |FINSI;
   |SINO;
      |SI #cacuenta#3 = "S"; |HAZ grabatra; |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE todas_ctas;
   #1#4;
   ;
   d, 0, 01, 00, 00;
   h, 4, 99, 99, 99;
   ;
   NULL, calculit;
|FIN;
____________________________________/ IMPRESION RESULTADOS
|PROCESO bloqued1;
   |SI sw_d1 = 1; |O sw_h1 = 1; |HAZ tipod1;  |FINSI;      || I
|FPRC;

|PROCESO bloqued2;
   |SI sw_d2 = 1; |O sw_h2 = 1; |HAZ tipod2;  |FINSI;      || II
|FPRC;

|PROCESO bloqued3;
   |SI sw_d21 = 0;              |HAZ tipod21; |FINSI;      || III
|FPRC;

|PROCESO bloqued4;
   |SI sw_d3 = 1; |O sw_h3 = 1; |HAZ tipod3;  |FINSI;      || IV
|FPRC;

|PROCESO bloqued5;
   |SI sw_d31 = 0;              |HAZ tipod31; |FINSI;      || V
|FPRC;

|PROCESO bloqued6;
   |SI sw_d4 = 0;               |HAZ tipod4;  |FINSI;      || VI
|FPRC;

|PROCESO bloqueh1;
   |SI sw_d1 = 1; |O sw_h1 = 1; |HAZ tipoh1;  |FINSI;      || I
|FPRC;

|PROCESO bloqueh2;
   |SI sw_d2 = 1; |O sw_h2 = 1; |HAZ tipoh2;  |FINSI;      || II
|FPRC;

|PROCESO bloqueh3;
   |SI sw_h21 = 0;              |HAZ tipoh21; |FINSI;      || III
|FPRC;

|PROCESO bloqueh4;
   |SI sw_d3 = 1; |O sw_h3 = 1; |HAZ tipoh3;  |FINSI;      || IV
|FPRC;

|PROCESO bloqueh5;
   |SI sw_h31 = 0;              |HAZ tipoh31; |FINSI;      || V
|FPRC;

|PROCESO bloqueh6;
   |SI sw_h32 = 0;              |HAZ tipoh32; |FINSI;      || VI
|FPRC;

|PROCESO tipod1;
   variable = "  I. BENEFICIOS DE EXPLOTACION ..........................";
   canti = haber1 - debe1;
   per_canti = habep1 - debp1;
   |SI canti < 0;
      punto_h1 = canti * -1;
      puntp_h1 = per_canti * -1;
      canti = 0;
      per_canti = 0;
   |SINO;
      sw_inf = 5; |PRINT;
      sw_inf = 1; |PRINT;
      sw_inf = 5; |PRINT;
      sw_d1 = 0;
      sw_h1 = 0;
      punto_d1 = canti;
      puntp_d1 = per_canti;
   |FINSI;
|FPRC;

|PROCESO tipoh1;
   variable = "  I. PERDIDAS DE EXPLOTACION ............................";
   canti = debe1 - haber1;
   per_canti = debp1 - habep1;
   |SI canti [ 0;
      punto_d1 = canti * -1;
      puntp_d1 = per_canti * -1;
      canti = 0;
      per_canti = 0;
   |SINO;
      sw_inf = 5; |PRINT;
      sw_inf = 1; |PRINT;
      sw_inf = 5; |PRINT;
      sw_d1 = 0;
      sw_h1 = 0;
      punto_h1 = canti;
      puntp_h1 = per_canti;
   |FINSI;
|FPRC;

|PROCESO tipod2;
   variable = " II. RESULTADOS FINANCIEROS POSITIVOS ...................";
   canti = haber2 - debe2;
   per_canti = habep2 - debp2;
   |SI canti < 0;
      punto_h2 = canti * -1;
      puntp_h2 = per_canti * -1;
      canti = 0;
      per_canti = 0;
   |SINO;
      sw_inf = 5; |PRINT;
      sw_inf = 1; |PRINT;
      sw_inf = 5; |PRINT;
      sw_d2 = 0;
      sw_h2 = 0;
      punto_d2 = canti;
      puntp_d2 = per_canti;
   |FINSI;
|FPRC;

|PROCESO tipod21;
   variable = "III. BENEFICIOS DE LAS ACTIV. ORDINARIAS ...........";
   canti = punto_d1 + punto_d2 - punto_h1 - punto_h2;
   per_canti = puntp_d1 + puntp_d2 - puntp_h1 - puntp_h2;
   |SI canti < 0;
      punto_h3 = canti * -1;
      puntp_h3 = per_canti * -1;
      canti = 0;
      per_canti = 0;
   |SINO;
      sw_inf = 5; |PRINT;
      sw_inf = 1; |PRINT;
      sw_inf = 5; |PRINT;
      sw_d21 = 1;
      punto_d3 = canti;
      puntp_d3 = per_canti;
   |FINSI;
|FPRC;

|PROCESO tipoh2;
   variable = " II. RESULTADOS FINANCIEROS NEGATIVOS ...................";
   canti = debe2 - haber2;
   per_canti = debp2 - habep2;
   |SI canti [ 0;
      punto_d2 = canti * -1;
      puntp_d2 = per_canti * -1;
      canti = 0;
      per_canti = 0;
   |SINO;
      sw_inf = 5; |PRINT;
      sw_inf = 1; |PRINT;
      sw_inf = 5; |PRINT;
      sw_d2 = 0;
      sw_h2 = 0;
      punto_h2 = canti;
      puntp_h2 = per_canti;
   |FINSI;
|FPRC;

|PROCESO tipoh21;
   variable = "III. PERDIDAS DE LAS ACTIVIDADES ORDINARIAS .............";
   canti = punto_h1 + punto_h2 - punto_d1 - punto_d2;
   per_canti = puntp_h1 + puntp_h2 - puntp_d1 - puntp_d2;
   |SI canti [ 0;
      punto_d3 = canti * -1;
      puntp_d3 = per_canti * -1;
      canti = 0;
      per_canti = 0;
   |SINO;
      sw_inf = 5; |PRINT;
      sw_inf = 1; |PRINT;
      sw_inf = 5; |PRINT;
      sw_h21 = 1;
      punto_h3 = canti;
      puntp_h3 = per_canti;
   |FINSI;
|FPRC;

|PROCESO tipod3;
   variable = " IV. RESULTADOS EXTRAORDINARIOS POSITIVOS ...............";
   canti = haber3 - debe3;
   per_canti = habep3 - debp3;
   |SI canti < 0;
      punto_h4 = canti * -1;
      puntp_h4 = per_canti * -1;
      canti = 0;
      per_canti = 0;
   |SINO;
      sw_inf = 5; |PRINT;
      sw_inf = 1; |PRINT;
      sw_inf = 5; |PRINT;
      sw_d3 = 0;
      sw_h3 = 0;
      punto_d4 = canti;
      puntp_d4 = per_canti;
   |FINSI;
|FPRC;

|PROCESO tipod31;
   variable = "  V. BENEFICIO ANTES DE IMPUESTOS .......................";
   canti = punto_d3 + punto_d4 - punto_h3 - punto_h4;
   per_canti = puntp_d3 + puntp_d4 - puntp_h3 - puntp_h4;
   |SI canti < 0;
      punto_h5 = canti * -1;
      puntp_h5 = per_canti * -1;
      canti = 0;
      per_canti = 0;
   |SINO;
      sw_inf = 5; |PRINT;
      sw_inf = 1; |PRINT;
      sw_inf = 5; |PRINT;
      sw_d31 = 1;
      punto_d5 = canti;
      puntp_d5 = per_canti;
   |FINSI;
|FPRC;

|PROCESO tipoh3;
   variable = " IV. RESULTADOS EXTRAORDINARIOS NEGATIVOS ...............";
   canti = debe3 - haber3;
   per_canti = debp3 - habep3;
   |SI canti [ 0;
      punto_d5 = canti * -1;
      puntp_d5 = per_canti * -1;
      canti = 0;
      per_canti = 0;
   |SINO;
      sw_inf = 5; |PRINT;
      sw_inf = 1; |PRINT;
      sw_inf = 5; |PRINT;
      sw_d3 = 0;
      sw_h3 = 0;
      punto_h4 = canti;
      puntp_h4 = per_canti;
   |FINSI;
|FPRC;

|PROCESO tipoh31;
   variable = "  V. PERDIDAS ANTES DE IMPUESTOS ........................";
   canti = punto_h3 + punto_h4 - punto_d3 - punto_d4;
   per_canti = puntp_h3 + puntp_h4 - puntp_d3 - puntp_d4;
   |SI canti [ 0;
      punto_d5 = canti * -1;
      puntp_d5 = per_canti * -1;
      canti = 0;
      per_canti = 0;
   |SINO;
      sw_inf = 5; |PRINT;
      sw_inf = 1; |PRINT;
      sw_inf = 5; |PRINT;
      sw_h31 = 1;
      punto_h5 = canti;
      puntp_h5 = per_canti;
   |FINSI;
|FPRC;

|PROCESO tipoh32;
   variable = " VI. RESULTADO DEL EJERCICO (PERDIDAS) ..................";
   canti = punto_h5;
   per_canti = puntp_h5;
   |SI punto_h5 > 0;   canti = canti + debe4;           |FINSI;
   |SI puntp_h5 > 0;   per_canti = per_canti + debp4;   |FINSI;
   |SI canti [ 0;
      canti = 0;
      per_canti = 0;
   |SINO;
      sw_inf = 5; |PRINT;
      sw_inf = 1; |PRINT;
      sw_inf = 5; |PRINT;
      sw_h32 = 1;
   |FINSI;
|FPRC;

|PROCESO tipod4;
   variable = " VI. RESULTADO DEL EJERCICIO (BENEFICIOS) ...............";
   canti = punto_d5;
   per_canti = puntp_d5;
   |SI debe4 > 0; canti = canti - debe4;   |FINSI;
   |SI debp4 > 0; per_canti = per_canti - debp4;   |FINSI;
   |SI canti [ 0;
      canti = 0;
      per_canti = 0;
   |SINO;
      sw_inf = 5; |PRINT;
      sw_inf = 1; |PRINT;
      sw_inf = 5; |PRINT;
      sw_d4 = 1;
   |FINSI;
|FPRC;
____________________________________/ IMPRESION
|PROCESO recuenta2;
   alfa1 = #cacuenta#0;   |QBF alfa1;
   alfa2 = alfa1 % 0;   nume2 = alfa2; nume2 = nume2 + 100;
   |PARA; |SI FSalida = 0; |HACIENDO;
      |LEE 000#1s;
      alfa2 = #cacuenta#0 % nume2;
      |SI alfa1 = alfa2;|Y FSalida = 0;
         |SI #cabaexp0#0 = #cacuenta#5;|Y #cabaexp0#1 = #cacuenta#4;|Y #cabaexp0#2 = #cacuenta#6;
            |Y #cabaexp0#3 = #cacuenta#7;|Y #cabaexp0#4 = #cacuenta#8;|Y #cacuenta#3 = "S";
            |HAZ recuenta;
         |FINSI;
      |SINO;
         |DDEFECTO #1;
         #cacuenta#0 = act_cuent;
         #cacuenta#1 = act_nivel;          || lee la cuenta manual
         |LEE 020#1=;
         FSalida = 111;
      |FINSI;
   |SIGUE;
|FPRC;

|PROCESO nivel_manual2;
   guarda_can = cantidad;
   guarda_per = per_can;
   cantidad = 0;
   per_can = 0;
   
   |DDEFECTO #1;
   #cacuenta#0 = act_cuent;
   #cacuenta#1 = act_nivel;          || lee la cuenta manual
   |LEE 000#1=;
   |SI FSalida = 0;
      |BUCLE busca_exist2;
      |SI sw_pasa ! 1;
         |HAZ recuenta2;
      |FINSI;
      |SI cantidad ! 0;
         sw_inf = 6;
         |PRINT;
      |FINSI;
   |FINSI;
   |DDEFECTO #1;
   #cacuenta#0 = nue_cuent;
   #cacuenta#1 = nue_nivel;          || situa de nuevo el puntero
   |LEE 000#1=;
   
   cantidad = guarda_can;
   per_can = guarda_per;
|FPRC;

|PROCESO nivel_manual;
   nume1 = ult_nivel - 1;
   |PARA act_nivel = nume1; |SI act_nivel ] 1; |HACIENDO act_nivel = act_nivel - 1;
      |SI act_nivel = #0#18;|O act_nivel = #0#19;|O act_nivel = #0#20;
         |O act_nivel = #0#21;|O act_nivel = #0#22;
         |O act_nivel = #0#23;
         carac = 0;
         |SI act_nivel = 6;   carac = dig9;  |FINSI;
         |SI act_nivel = 5;   carac = dig8;  |FINSI;
         |SI act_nivel = 4;   carac = dig7;  |FINSI;
         |SI act_nivel = 3;   carac = dig6;  |FINSI;
         |SI act_nivel = 2;   carac = dig5;  |FINSI;
         |SI act_nivel = 1;   carac = dig4;  |FINSI;
         |SI carac ! 0;
            nume1 = carac + 100;
            alfa1 = ult_cuent % nume1;
            alfa2 = nue_cuent % nume1;
            |SI alfa1 ! alfa2;
               act_cuent = alfa1;
               |HAZ nivel_manual2;
            |FINSI;
         |FINSI;
      |FINSI;
   |SIGUE;
|FPRC;

|PROCESO recuenta;
   |SI #cabaexp0#0 =  "D";    factor = 1;    |FINSI;
   |SI #cabaexp0#0 =  "H";    factor = -1;   |FINSI;
   
   |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
      cantidad = cantidad + (#cacuenta#x# * factor);
      |SI x ] perdesde;|Y x [ perhasta;
         per_can = per_can + (#cacuenta#x# * factor);
      |FINSI;
   |SIGUE;
|FPRC;

|PROCESO cuentas;
   |HAZ digitos2;
   |SI d_error ! 0;  |ACABA;  |FINSI;
   nue_cuent = #cacuenta#0;
   nue_nivel = #cacuenta#1;
   
   sw_pasa = 0;
   cantidad = 0;   per_can = 0;
   sw_inf = 6;
   |SI #cacuenta#55 = #0#18; |O #cacuenta#55 = #0#19; |O #cacuenta#55 = #0#20;
      |O #cacuenta#55 = #0#21; |O #cacuenta#55 = #0#22; |O #cacuenta#55 = #0#23;
      |BUCLE busca_exist;
      |SI sw_pasa ! 1;
         |SI #cacuenta#3 = "S";
            |HAZ recuenta;
         |SINO;
            act_cuent = #cacuenta#0;
            act_nivel = #cacuenta#1;
            |HAZ recuenta2;
         |FINSI;
      |FINSI;
      |SI cantidad ! 0;|O per_can ! 0;
         |HAZ nivel_manual;
         |PRINT;
         ult_cuent = #cacuenta#0;
         ult_nivel = #cacuenta#1;
      |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE lee_ctas;
   #1#4;
   ;
   #3#0, #3#1, #3#2, #3#3, #3#4;
   #3#0, #3#1, #3#2, #3#3, #3#4;
   e;
   NULL, NULL, NULL, NULL, NULL, cuentas;
   #1#54, #1#55;
|FIN;

|PROCESO rotura;
   |SI #0#4 = "S";
      #cacuenta#54 = "          ";
      #cacuenta#55 = 0;
      
      |BUCLE lee_ctas;
   |FINSI;
   
   |SI ult_nivel ! 1;
      nue_cuent = "";
      nue_nivel = 0;
      |ABRE #1;
      |HAZ nivel_manual;
      |CIERRA #1;
   |FINSI;
   ult_cuent = "";
   ult_nivel = 0;
   
   alfa1 = #cabaexp0#7;   |QBF alfa1;
   |SI alfa1 = "";
      #cabaexp0#7 = ">Total Partida: "+#cabaexp0#1+"."+#cabaexp0#0+"."+#cabaexp0#2+"."+#cabaexp0#3+"."+#cabaexp0#4;
   |FINSI;
   sw_inf = 4;     |PRINT;        || partida
   sw_inf = 5;     |PRINT;        || linea en blanco
|FPRC;

|PROCESO epigraf;
   |SI epigrafe ! #cabaexp0#3;
      sw_epi = 0;           || rompe
   |FINSI;
   |HAZ rotura;
   epigrafe = #cabaexp0#3;
|FPRC;

|PROCESO impresio;
   |SI sw = 0;
      bloque = #cabaexp0#1;
      tipo = #cabaexp0#0;
      agrupaci = #cabaexp0#2;
      epigrafe = #cabaexp0#3;
      partida = #cabaexp0#4;
      |SI #cabaexp0#0 = "D"; doh = gastos;   |FINSI;
      |SI #cabaexp0#0 = "H"; doh = ingresos; |FINSI;
      sw_inf = 0;
      |PRINT;
      sw = 1;
   |FINSI;
   
   |SI bloque = #cabaexp0#1; |Y tipo = #cabaexp0#0;
      |HAZ epigraf;
   |SINO;
      |SI tipo ! #cabaexp0#0;                    || ruptura por tipo
         varit = gastos;
         sw_inf = 10; |PRINT;        || imprime total tipo
         |PIEINF;
         doh = gastos;
         sw_inf = 0;  |PRINT;        || imprime el titulo
      |FINSI;
      |HAZ epigraf;
      bloque = #cabaexp0#1;
      tipo = #cabaexp0#0;
   |FINSI;
|FPRC;

|DEFBUCLE imprimiendo;
   #3#1;
   ;
   d, 0, 01, 00, 00;
   h, 4, 99, 99, 99;
   ;
   NULL, impresio;
|FIN;

|PROCESO final;     || para cuando acaba el defbucle
   |SI tipo = "D";     varit = gastos;     |FINSI;
   |SI tipo = "H";     varit = ingresos;   |FINSI;
   sw_inf = 10;    |PRINT;                       || imprime total
   
   |HAZ bloqued1;      |HAZ bloqueh1;
   |HAZ bloqued2;      |HAZ bloqueh2;
   |HAZ bloqued3;      |HAZ bloqueh3;
   |HAZ bloqued4;      |HAZ bloqueh4;
   |HAZ bloqued5;      |HAZ bloqueh5;
   |HAZ bloqued6;      |HAZ bloqueh6;
|FPRC;
____________________________________/ CALCULO DE EXISTENCIAS
|PROCESO sumasal;    || Calculamos cada cuenta
   |SI #cacuenta#5 = "D"; |Y #cacuenta#4 = 1;
      debe1 = debe1 + destot;
      debp1 = debp1 + desper;
   |FINSI;
   
   |SI #cacuenta#5 = "D"; |Y #cacuenta#4 = 2;
      debe2 = debe2 + destot;
      debp2 = debp2 + desper;
   |FINSI;
   
   |SI #cacuenta#5 = "D"; |Y #cacuenta#4 = 3;
      debe3 = debe3 + destot;
      debp3 = debp3 + desper;
   |FINSI;
   
   |SI #cacuenta#5 = "D"; |Y #cacuenta#4 = 4;
      debe4 = debe4 + destot;
      debp4 = debp4 + desper;
   |FINSI;
   
   |SI #cacuenta#5 = "H"; |Y #cacuenta#4 = 1;
      haber1 = haber1 + destot;
      habep1 = habep1 + desper;
   |FINSI;
   
   |SI #cacuenta#5 = "H"; |Y #cacuenta#4 = 2;
      haber2 = haber2 + destot;
      habep2 = habep2 + desper;
   |FINSI;
   
   |SI #cacuenta#5 = "H"; |Y #cacuenta#4 = 3;
      haber3 = haber3 + destot;
      habep3 = habep3 + desper;
   |FINSI;
|FPRC;

|PROCESO grabatr1;
   #cacuenta#0 = c_cta;
   #cacuenta#1 = c_dig;
   |LEE 000#1=;
   |SI FSalida = 0;
      #cabaexp0#0 = #cacuenta#5;
      #cabaexp0#1 = #cacuenta#4;
      #cabaexp0#2 = #cacuenta#6;
      #cabaexp0#3 = #cacuenta#7;
      #cabaexp0#4 = #cacuenta#8;
      |LEE 101#3=;
      FEstado = FSalida;
      |SI FEstado ! 0;
         |DDEFECTO #3;
         #cabaexp0#0 = #cacuenta#5;
         #cabaexp0#1 = #cacuenta#4;
         #cabaexp0#2 = #cacuenta#6;
         #cabaexp0#3 = #cacuenta#7;
         #cabaexp0#4 = #cacuenta#8;
      |FINSI;
      |HAZ leexplot;
      #cabaexp0#5 = #caparexp#3;
      #cabaexp0#6 = #caparexp#5;
      #cabaexp0#7 = #caparexp#7;
      destot = (#cabaexp1#4 - #cabaexp1#5); |SI #cacuenta#5 = "H"; destot = -destot; |FINSI;
      desper = (#cabaexp1#9 -#cabaexp1#10); |SI #cacuenta#5 = "H"; desper = -desper; |FINSI;
      |SI c_swt = 1; #cabaexp0#8  = #cabaexp0#8  + destot; |FINSI;
      |SI c_swt = 2; #cabaexp0#10 = #cabaexp0#10 + desper; |FINSI;
      #cabaexp0#9 = 1;
      |SI #cabaexp0#8 ! 0;|O #cabaexp0#10 ! 0;
         |SI FEstado = 0; |GRABA 020#3a;     |FINSI;
         |SI FEstado ! 0; |GRABA 020#3n;     |FINSI;
      |FINSI;
      |LIBERA #3;
      |SI c_swt = 1; |HAZ sumasal;  |FINSI;    || solo una vez
   |FINSI;
|FPRC;

|PROCESO descarg;
   |HAZ digitos2;
   |SI d_error ! 0;  |ACABA;  |FINSI;
   linea = linea + 1;
   #cabaexp1#0 = linea;
   #cabaexp1#8 = #0#6;
   |LEE 000#4=;
   |SI FSalida = 0;  |BORRA 020#4a;  |FINSI;
   |DDEFECTO #4;
   #cabaexp1#0 = linea;
   #cabaexp1#8 = #0#6;
   #cabaexp1#1 = #cacuenta#0;
   #cabaexp1#2 = #cacuenta#1;
   #cabaexp1#3 = #cacuenta#2;

   mes_fin = #0#7 + 11;
   #cabaexp1#4 = #caexistl#mes_fin#;                   || iniciales (acum)
   mes_fin = #0#8 + 11;
   #cabaexp1#5 = #caexistl#mes_fin#;                   || finales (acum)
   descar = #cabaexp1#4 - #cabaexp1#5; ||iniciales - finales
   |SI descar ] 0;
      #cabaexp1#6 = #caexistl#5;     || cuenta descarga D
      #cabaexp1#7 = #caexistl#6;     || digito descarga D
   |SINO;
      #cabaexp1#6 = #caexistl#8;     || cuenta descarga H
      #cabaexp1#7 = #caexistl#9;     || digito descarga H
   |FINSI;

   eaCuenta = #cabaexp1#6;
   |HAZ SacaNivel;
   #cabaexp1#7 = enNivel;

   |DDEFECTO #1;
   #cacuenta#0 = #cabaexp1#6;
   #cacuenta#1 = #cabaexp1#7;
   |LEE 000#1=;
   #cabaexp1#13 = #cacuenta#5;    || tipo
   #cabaexp1#14 = #cacuenta#4;    || bloque
   #cabaexp1#15 = #cacuenta#6;    || agrupacion
   #cabaexp1#16 = #cacuenta#7;    || epigrafe
   #cabaexp1#17 = #cacuenta#8;    || partida

   mes_fin = #0#24 + 11;
   #cabaexp1#9 = #caexistl#mes_fin#;                   || iniciales (per)
   mes_fin = #0#25 + 11;
   #cabaexp1#10 = #caexistl#mes_fin#;                  || finales (per)
   descar = #cabaexp1#9 - #cabaexp1#10;
   |SI descar ] 0;
      #cabaexp1#11 = #caexistl#5;     || cuenta descarga D
      #cabaexp1#12 = #caexistl#6;     || digito descarga D
   |SINO;
      #cabaexp1#11 = #caexistl#8;     || cuenta descarga H
      #cabaexp1#12 = #caexistl#9;     || digito descarga H
   |FINSI;

   eaCuenta = #cabaexp1#11;
   |HAZ SacaNivel;
   #cabaexp1#12 = enNivel;

   |DDEFECTO #1;
   #cacuenta#0 = #cabaexp1#11;
   #cacuenta#1 = #cabaexp1#12;
   |LEE 000#1=;
   #cabaexp1#18 = #cacuenta#5;    || tipo
   #cabaexp1#19 = #cacuenta#4;    || bloque
   #cabaexp1#20 = #cacuenta#6;    || agrupacion
   #cabaexp1#21 = #cacuenta#7;    || epigrafe
   #cabaexp1#22 = #cacuenta#8;    || partida

   |SI nivelet < #cabaexp1#7;
      |SI nivelet = 1;  traraf = 100 + dig4;  |FINSI;
      |SI nivelet = 2;  traraf = 100 + dig5;  |FINSI;
      |SI nivelet = 3;  traraf = 100 + dig6;  |FINSI; || trata cta. descarga ac.
      |SI nivelet = 4;  traraf = 100 + dig7;  |FINSI;
      |SI nivelet = 5;  traraf = 100 + dig8;  |FINSI;
      #cabaexp1#6 = #cabaexp1#6 % traraf;
      #cabaexp1#7 = nivelet;
      #cabaexp1#6 = #cabaexp1#6 + "              ";
      #cabaexp1#6 = #cabaexp1#6 % 112;
   |FINSI;

   |SI nivelet < #cabaexp1#12;
      |SI nivelet = 1;  traraf = 100 + dig4;  |FINSI;
      |SI nivelet = 2;  traraf = 100 + dig5;  |FINSI;
      |SI nivelet = 3;  traraf = 100 + dig6;  |FINSI; || trata cta. descarga per.
      |SI nivelet = 4;  traraf = 100 + dig7;  |FINSI;
      |SI nivelet = 5;  traraf = 100 + dig8;  |FINSI;
      #cabaexp1#11 = #cabaexp1#11 % traraf;
      #cabaexp1#12 = nivelet;
      #cabaexp1#11 = #cabaexp1#11 + "              ";
      #cabaexp1#11 = #cabaexp1#11 % 112;
   |FINSI;

   |GRABA 020#4n;

   c_cta = #cabaexp1#6;    c_dig = #cabaexp1#7;   c_swt = 1;   || graba acum.
   |HAZ grabatr1;
   c_cta = #cabaexp1#11;   c_dig = #cabaexp1#12;  c_swt = 2;   || graba per.
   |HAZ grabatr1;
|FPRC;

|PROCESO lineades;
   |ABRE #1;
   #cacuenta#0 = #caexistl#2;
   #cacuenta#1 = #caexistl#3;
   |LEE 000#1=;
   |SI FSalida = 0;
      |HAZ descarg;
   |FINSI;
   |CIERRA #1;
|FPRC;

|PROCESO existen;
   |ABRE #6;
   posici = 100;
   linea = 0;
   #caexistc#0 = #0#6;
   |LEE 000#6=;
   |BUCLELIN 2lineades#6;
   |CIERRA #6;
|FPRC;

|PROCESO punteros;
   |SI #0#0 = 0;  eldesde = 11; |FINSI;
   |SI #0#0 = 1;  eldesde = 14; |FINSI;
   |SI #0#0 = 2;  eldesde = 17; |FINSI;
   |SI #0#0 = 3;  eldesde = 20; |FINSI;
   |SI #0#0 = 4;  eldesde = 23; |FINSI;
   |SI #0#0 = 5;  eldesde = 26; |FINSI;
   |SI #0#0 = 6;  eldesde = 29; |FINSI;
   |SI #0#0 = 7;  eldesde = 32; |FINSI;
   |SI #0#0 = 8;  eldesde = 35; |FINSI;
   |SI #0#0 = 9;  eldesde = 38; |FINSI;
   |SI #0#0 = 10; eldesde = 41; |FINSI;
   |SI #0#0 = 11; eldesde = 44; |FINSI;
   |SI #0#0 = 12; eldesde = 47; |FINSI;
   |SI #0#0 = 13; eldesde = 50; |FINSI;
   
   |SI #0#1 = 0;  elhasta = 11; |FINSI;
   |SI #0#1 = 1;  elhasta = 14; |FINSI;
   |SI #0#1 = 2;  elhasta = 17; |FINSI;
   |SI #0#1 = 3;  elhasta = 20; |FINSI;
   |SI #0#1 = 4;  elhasta = 23; |FINSI;
   |SI #0#1 = 5;  elhasta = 26; |FINSI;
   |SI #0#1 = 6;  elhasta = 29; |FINSI;
   |SI #0#1 = 7;  elhasta = 32; |FINSI;
   |SI #0#1 = 8;  elhasta = 35; |FINSI;
   |SI #0#1 = 9;  elhasta = 38; |FINSI;
   |SI #0#1 = 10; elhasta = 41; |FINSI;
   |SI #0#1 = 11; elhasta = 44; |FINSI;
   |SI #0#1 = 12; elhasta = 47; |FINSI;
   |SI #0#1 = 13; elhasta = 50; |FINSI;
   
   |SI #0#13 = 0;  perdesde = 11; |FINSI;
   |SI #0#13 = 1;  perdesde = 14; |FINSI;
   |SI #0#13 = 2;  perdesde = 17; |FINSI;
   |SI #0#13 = 3;  perdesde = 20; |FINSI;
   |SI #0#13 = 4;  perdesde = 23; |FINSI;
   |SI #0#13 = 5;  perdesde = 26; |FINSI;
   |SI #0#13 = 6;  perdesde = 29; |FINSI;
   |SI #0#13 = 7;  perdesde = 32; |FINSI;
   |SI #0#13 = 8;  perdesde = 35; |FINSI;
   |SI #0#13 = 9;  perdesde = 38; |FINSI;
   |SI #0#13 = 10; perdesde = 41; |FINSI;
   |SI #0#13 = 11; perdesde = 44; |FINSI;
   |SI #0#13 = 12; perdesde = 47; |FINSI;
   |SI #0#13 = 13; perdesde = 50; |FINSI;
   
   |SI #0#14 = 0;  perhasta = 11; |FINSI;
   |SI #0#14 = 1;  perhasta = 14; |FINSI;
   |SI #0#14 = 2;  perhasta = 17; |FINSI;
   |SI #0#14 = 3;  perhasta = 20; |FINSI;
   |SI #0#14 = 4;  perhasta = 23; |FINSI;
   |SI #0#14 = 5;  perhasta = 26; |FINSI;
   |SI #0#14 = 6;  perhasta = 29; |FINSI;
   |SI #0#14 = 7;  perhasta = 32; |FINSI;
   |SI #0#14 = 8;  perhasta = 35; |FINSI;
   |SI #0#14 = 9;  perhasta = 38; |FINSI;
   |SI #0#14 = 10; perhasta = 41; |FINSI;
   |SI #0#14 = 11; perhasta = 44; |FINSI;
   |SI #0#14 = 12; perhasta = 47; |FINSI;
   |SI #0#14 = 13; perhasta = 50; |FINSI;
|FPRC;

|PROCESO conexi; |TIPO 0;
   enTExi = 1;
   d_mes = #0#7;
   h_mes = #0#8;
   estru = #0#6;
   r_emp = empre;
   r_per = ejerc;
   r_eje = #canempre#26;
   r_nom = "ACUMULADOS " + #canempre#1;
   |SUB_EJECUTA "caexicon.run";
   |SI #0#7 ! #0#24;  |O #0#8 ! #0#25;
      d_mes = #0#24;
      h_mes = #0#25;
      estru = #0#6;
      r_emp = empre;
      r_per = ejerc;
      r_eje = #canempre#26;
      r_nom = "PERIODO " + #canempre#1;
      |SUB_EJECUTA "caexicon.run";
   |FINSI;
|FPRC;

|PROCESO let_nivel;
   |SI nivelet = 0;
      nivelet = #0#18;
   |FINSI;
   |PARA x = 23;  |SI x ] 18;  |HACIENDO x = x - 1;
      |SI nivelet < #0x;
         nivelet = #0x;
      |FINSI;
   |SIGUE;
|FPRC;

____________________________________/ CALCULO PRINCIPAL
|PROCESO impre; |TIPO 3;

   |HAZ FecApeCie;
   |SI enMAper ! dig1;
       |SI #0#1  < enMAper; |Y #0#1 ! 0; |ACABA; |FINSI;
       |SI #0#14 < enMAper; |Y #0#14 ! 0;
           #0#14 = enMAper;
       |FINSI;
   |FINSI;

   |HAZ let_nivel;
   |HAZ antesque;
   |SI #0#26 = "S"; |HAZ conexi; |FINSI;

   enSwOper = 0;
   |SUB_EJECUTA "carecsig.run";

   |ABRE #8;
   |DDEFECTO #8;
   #8#0 = 1;
   |LEE 000#8=;
   |CIERRA #8;
   |SI #caconimp#2 = "S";  |Y #caconimp#3 = "N";  |Y aparam = "";
      |DFICE pathbal;
      balance = 1;
      |SI #caconimp#10 = "S";
          enSwOper = 2;
          |SUB_EJECUTA "carecsig";
          enSwOper = 0;
          |SI enECuadre = 1; |ACABA; |FINSI;
          |NOME_DAT #1 eaNomFCta;
      |SINO;
          |SUB_EJECUTA "careccue";
      |FINSI;
   |FINSI;

   |DELINDEX #4;
   |DELINDEX #3;
   sw_d1 = 0; sw_d2 = 0; sw_d3 = 0; sw_d4 = 0;
   sw_h1 = 0; sw_h2 = 0; sw_h3 = 0;
   |IMPRE 0;
   |SI FSalida ! 0;  |ACABA;  |FINSI;
   |HAZ los_informes;
   informe = que_i1;
   |INFOR informe;
   |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;
   sw = 0;
   |HAZ punteros;        || calculo para saber los punteros de los saldos

   || ---------------------------------------------------------------------
   |ABRE #2;             || partidas de balance
   |ABRE #3;             || temporal de cuentas
   |ABRE #4;             || temporal de existencias
   |HAZ existen;         || calcula existencias
   |BUCLE todas_ctas;    || calcula resto de cuentas
   |CIERRA #2;
   |CIERRA #3;
   |ABRET #1;
   |BUCLE imprimiendo;   || lectura e impresion
   |HAZ final;
   |CIERRAT #1;
   |PIEINF;
   |FININF;
   |CIERRA #4;
   |FINIMP;
   |DELINDEX #3;
   |DELINDEX #4;
|FPRC;
