|FICHEROS;
   caestr67 #0;       || Informe Estadistica 6 y 7
   cacuenta #1;       || Plan contable
   caestr61 #2;       || Fichero fantasma de impresion por pantalla
   caconimp #8;
   caratios #103;
   caformul #104;

   canempre #30;
   caselem1 #998;
|FIN;

|VARIABLES;
   &pathbal = "";
   &balance = 1;
   &entrada = 1;
   lin = 0;
   sw_error = 0;
   x = 0;
   y = 0;
   z = 0;
   nivelact = 0;
   informe = "";
   mes = 0;                  || Variable para saber el mes seleccionado
   letrames = "";            || Variable para poner el mes en letra

   &lmes1 = "";
   &lmes2 = "";
   &lmes3 = "";
   t1 = 0;
   t2 = 0;
   t3 = 0;

   error2 = "0000No existe informe";
   error3 = "0000El numero de nivel no coincide con el numero de nivel de la empresa";

   sw = 0;
   rellenos = "";
   swsuma = 0;
   swprint = 0;
   swmov = 0;
   &i_mes1 = 0;
   &i_mes2 = 0;
   &i_mes3 = 0;
   &i_trim = 0;
   &i_trimt= 0;
   &t_mes1 = 0;
   &t_mes2 = 0;
   &t_mes3 = 0;
   &t_trim = 0;
   &t_trimt= 0;
   &tan100 = 0;
   &tan102 = 0;

   &swlinea = 0;      || Pone o no linea en blanco en informe
   &swcomen = 0;
   {-1}menu  = "";           || Variables menu impresora o pantalla
   menu1 = "2400";
   menu2 = "1";
   menu3 = "Elija opcion: ";
   menu4 = "PI";
   menu5 = " Pantalla ";
   menu6 = " Impresora ";
   men2  = "     Longitud de Cuenta Fuera de Nivel !";
   long = "  ";
   cta  = 0;
   num   = 0;
   swdig = 0;
   digito = 0;
   menor = 0;
   para1 = 0;
   nume1 = 0;
   nume2 = 0;
   nume3 = 0;
   nume4 = 0;
   alfa1 = "";
   alfa2 = "";
   alfa3 = "";
   alfa4 = "";
   dos_ant = "xx";
   &pagina = 0;
   sw_pagina = 0;
   &cmcta = "";
   &cmnom = "";
   &cmsal  = 0;
   &cmsal2 = 0;
   &deac = "";
   &titulo = "";
   titul2 = "";
   dif = 0;
   &aparam = "";

   Comodin  = "";
   Comodin1 = "";

   &r_emp    = 0;
   &r_per    = 0;

   nHasMes   = 0;
   aNomCta   = "";
|FIN;

|INCLUYE dscont_i;
|INCLUYE digito_i;
|INCLUYE calrat_i;

|PROCESO antesque;
   aNomCta = "cacuenta";
   aparam = PARAMETRO;
   |QBF aparam; |SI aparam = " N"; aparam = ""; |FINSI;
   |SI aparam = "fech";
       aNomCta = "cabalano";
   |FINSI;
   |NOME_DAT #1 aNomCta;
|FPRC;

|PROCESO inicio_y; |TIPO 8;
  |HAZ inicial;
  |SI swerror = 2; |PTEC 807; |ACABA; |FINSI;
  |SI swerror = 3; |PTEC 831; |PTEC 807; |ACABA; |FINSI;
|FPRC;
|| ***********************************************************************
||                       CALCULOS DE PANTALLA
|| ***********************************************************************

|PROCESO &c_pagina;
   |SI sw_pagina = 0;
      pagina = 1;
      sw_pagina = 1;
   |SINO;
      pagina = pagina + 1;
   |FINSI;
|FPRC;

|PROCESO mayor;
   alfa1 = #caestr67#Campo# > " ";
   #caestr67#Campo# = alfa1;
   |PINTA #caestr67#Campo#;
   |SI Campo = 20;
      |SI #caestr67#Campo# = "C";
         |C_M #caestr67#8, 1,"S";
         |C_M #caestr67#21,1,"N";
         #caestr67#21 = 0; |PINTA #caestr67#21;
         |PINTA 1521, "                                    ";
      |SINO;
         |C_M #caestr67#8, 1,"N";
         |C_M #caestr67#21,1,"S";
         #caestr67#8 = "            "; |PINTA #caestr67#8;
         |PINTA 1429, "                                    ";
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO cuenta; |TIPO 6;
   |SI #caestr67#20 = "R"; |ACABA; |FINSI;

   eaCuenta = #caestr67#Campo#;
   salidas  = FSalida;
   |HAZ FiltraPuntos;
   #caestr67#Campo# = eaCuenta;
   |PINTA #caestr67#Campo#;

   |SI #caestr67#Campo# = "            ";  |ERROR;  |ACABA;  |FINSI;
   |SI Campo = 8;
      digito = 9;
   |SINO;
      digito = Campo + 14;
   |FINSI;
   cta  = #caestr67#Campo#;
   long = cta % 0;
   swdig = 0;
   |SI long = #canempre#14; swdig = 1; #caestr67#digito# = 1; |FINSI;
   |SI long = #canempre#15; swdig = 1; #caestr67#digito# = 2; |FINSI;
   |SI long = #canempre#16; swdig = 1; #caestr67#digito# = 3; |FINSI;
   |SI long = #canempre#17; swdig = 1; #caestr67#digito# = 4; |FINSI;
   |SI long = #canempre#18; swdig = 1; #caestr67#digito# = 5; |FINSI;
   |SI long = #canempre#19; swdig = 1; #caestr67#digito# = 6; |FINSI;
   |SI swdig = 0;
      |MENAV men2;
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO nomcuenta; || leer la cuenta a comparar.
   |SI #caestr67#20 = "R", |ACABA; |FINSI;
   |SI FSalida = 2; |ACABA; |FINSI;
   sw_error = 1;
   |ABRE #1;
   |DDEFECTO #1;
   #cacuenta#0 = #caestr67#8;
   #cacuenta#1 = #caestr67#9;
   |LEE 001#1=;
   |PINTA 1429, #cacuenta#2;
   |CIERRA #1;
|FPRC;

|PROCESO mesconta; |TIPO 0;
   x = Campo + 1;                || Coge el campo del nombre del mes
   mes = #caestr67#Campo#;
   |HAZ mesletra;                      || Recoge el mes en letra
   #caestr67#x# = letrames;
   |PINTA #caestr67#x#;                   || Pinta el nombre del mes
|FPRC;

|PROCESO mesletra;
   |SI mes = 1;  letrames = "Enero a Marzo       "; |FINSI;
   |SI mes = 2;  letrames = "Abril a Junio       "; |FINSI;
   |SI mes = 3;  letrames = "Julio a Septiembre  "; |FINSI;
   |SI mes = 4;  letrames = "Octubre a Diciembre "; |FINSI;
|FPRC;

|PROCESO numnivel;|TIPO 0;
   |SI #caestr67#7 > #canempre#13;
      |MENAV error3;
      |ERROR;
      |ACABA;
   |FINSI;
|FPRC;

|PROCESO leeratio; || Lee el ratio
   |SI FSalida = 2; |ACABA; |FINSI;
   |SI #caestr67#20 = "C"; |ACABA; |FINSI;
   |ABRE #103;
   #caratios#0 = #caestr67#21;
   |LEE 001#103=;
   |SI FSalida = 0;
      |PINTA 1521, #caratios#1;
   |SINO;
      |MENAV "    Atencion!. Ratio Inexistente !!!";
      |ERROR;
   |FINSI;
   |CIERRA #103;
|FPRC;

|| **********************************************************************
||                       CALCULOS DE IMPRESION
|| **********************************************************************

|PROCESO condicion;      || Calculo condiciones de impresion
   |HAZ digitos2;
   |SI d_error ! 0;  |ACABA;  |FINSI;
   swprint = 0;         || Variable para saber si imprimimos o no
   swsuma = 0;          || Variable para saber si sumamos o no

   |SI #cacuenta#55 = #caestr67#7; swprint = 1; swsuma = 1; |FINSI;

   |SI #cacuenta#3 = "S";|Y #cacuenta#55 [ #caestr67#7;    || Si el nivel no esta seleccionado
      swsuma = 1;                    || pero tiene en el campo pase una S
      swprint = 1;                   || entonces se imprime y se suma.
   |FINSI;

   |SI swprint = 1;    || Si se puede imprimir
      |SI sw = 0;
         nivelact = #cacuenta#55;     || Nivel actual
         sw = 1;
      |FINSI;
      |HAZ suma;
      |SI swmov = 0; |Y #caestr67#6 = "N"; || cuentas sin mov.
         |ACABA;
      |FINSI;
      |HAZ tanto100;
      |SI menu2 = 1; |HAZ operapanta; |FINSI; || Si es por pantalla
      |SI menu2 = 2; |HAZ operaimpre; |FINSI; || Si es por impresora
   |FINSI;
|FPRC;

|PROCESO operapanta;   || Operaciones por pantalla;
   |SI lin > 1100;
      |HAZ cambiapag;  || Cambia de pagina
      |SI sw_error = 1; |ACABA; |FINSI;
   |FINSI;

   #caestr61#0 = #cacuenta#0;     || Cuenta
   #caestr61#1 = #cacuenta#2;     || Descripcion
   #caestr61#2 = i_trim;   || debe
   #caestr61#3 = i_trimt;   || debe
   #caestr61#8 = tan100;
   #caestr61#9 = tan102;
   |SI swsuma = 1;    || Si se puede sumar los totales
      #caestr61#4 = #caestr61#4 + #caestr61#2;         || Suma el total del debe
      #caestr61#5 = #caestr61#5 + #caestr61#3;         || Suma el total del debe
   |FINSI;
   FSalida = 0;
   |SI 0 [ FSalida;    || Pinta linea por pantalla
      |PINTA #caestr61#4; |PINTA #caestr61#5;  || Pinta los totales
      Pos = lin;
      |PINDA #0#2;         || Pinta las lineas
      Pos = -1;
      lin = lin + 100;     || Posicionar una linea mas abajo
   |FINSI;
|FPRC;

|PROCESO cambiapag;
   sw_error = 0;
   lin = 100;
   |ATRI I;
   Comodin = " Empresa: " + #30#2 + "  Periodo: " + #30#3 + "  Nombre: " + #30#1;
   |PINTA 0402,Comodin;
   |ATRI N;
   |PAUSA;
   |SI 1 = FSalida;|O 7 = FSalida;
      |ERROR10;    || Rompe el bucle y acaba
      sw_error = 1;
   |SINO;
      |PINPA #0#2;
      |ATRI R; |PINTA 0602, titul2; |ATRI 0;
      #caestr61#6 = #caestr61#4; #caestr61#7 = #caestr61#5;  || Coge los valores
      |PINTA #caestr61#6; |PINTA #caestr61#7;  || Pinta la suma anterior
      |PINTA #caestr61#10; |PINTA #caestr61#11; |PINTA #caestr61#12; |PINTA #caestr61#13;  || Cuenta
      FSalida = 0;
   |FINSI;
|FPRC;

|PROCESO operaimpre;     || impresora

   |SI nivelact ! #cacuenta#55;         || cambio de nivel, linea en blanco
      swlinea = 1;
      nivelact = #cacuenta#55;
   |SINO;
      swlinea = 0;
   |FINSI;

   |PRINT;
   swcomen = 1
   |SI swsuma = 1;     || acumula totales
      t_mes1 = t_mes1 + i_mes1;
      t_mes2 = t_mes2 + i_mes2;
      t_mes3 = t_mes3 + i_mes3;
      t_trim = t_trim + i_trim;
      t_trimt = t_trimt + i_trimt;
   |FINSI;
|FPRC;


|DEFBUCLE caestr670;      || plan contable (3ra. clave)
   #1#3;
   ;
   #0#0, 0;
   rellenos, 6;
   e;
   NULL, condicion;
|FIN;

|PROCESO impre;
   informe = "caestr67";
   |INFOR informe;
   |SI FSalida = 0;
      |BUCLE caestr670;
      |PIEINF;
      |FININF;
   |SINO;
      |MENAV error2;
   |FINSI;
|FPRC;

|PROCESO panta;
   lin = 100;
   |PINPA #0#2;       || Pinta la pantalla del informe
   |ATRI R; |PINTA 0602, titul2; |ATRI 0;
   |DDEFECTO #2;
   #caestr61#10 = cmcta;
   #caestr61#11 = cmnom;
   #caestr61#12 = cmsal;
   #caestr61#13 = cmsal2;
   |PINTA #caestr61#10; |PINTA #caestr61#11; |PINTA #caestr61#12; |PINTA #caestr61#13;  || Cuenta

   |BUCLE caestr670;
   |SI sw = 1; |Y sw_error = 0;
      |ATRI I;
      Comodin = " Empresa: " + #30#2 + "  Periodo: " + #30#3 + "  Nombre: " + #30#1;
      |PINTA 0402,Comodin;
      |ATRI N;
      |PAUSA;
   |FINSI;
|FPRC;

|PROCESO suma;

   i_mes1 = 0; i_mes2 = 0; i_mes3 = 0;
   |SI #caestr67#17 = "S"; i_mes1 = - #cacuenta#t1#; |FINSI;
   |SI #caestr67#18 = "S"; i_mes2 = - #cacuenta#t2#; |FINSI;
   |SI #caestr67#19 = "S"; i_mes3 = - #cacuenta#t3#; |FINSI;
   i_trim = i_mes1 + i_mes2 + i_mes3;
   i_trimt = 0;
   swmov = 0;
   |PARA x = 9; |SI x [ t3; |HACIENDO x = x + 3;
      y = x + 1;
      z = x + 2;
      |SI z = t1; |Y #caestr67#17 = "N";
         |VETE nosuma;
      |FINSI;
      |SI z = t2; |Y #caestr67#18 = "N";
         |VETE nosuma;
      |FINSI;
      |SI z = t3; |Y #caestr67#19 = "N";
         |VETE nosuma;
      |FINSI;

      |SI #cacuenta#x# ! 0; |O #cacuenta#y# ! 0;  swmov = 1; |FINSI;
      i_trimt = i_trimt - #cacuenta#z#;

      |ET nosuma;
   |SIGUE;
   |SI #caestr67#4 = "S"; |Y eaMoneda = "P";
      i_mes1 = i_mes1 / 1000; i_mes1 = i_mes1 ! 0;
      i_mes2 = i_mes2 / 1000; i_mes2 = i_mes2 ! 0;
      i_mes3 = i_mes3 / 1000; i_mes3 = i_mes3 ! 0;
      i_trim = i_trim / 1000; i_trim = i_trim ! 0;
      i_trimt = i_trimt / 1000; i_trimt = i_trimt ! 0;
   |FINSI;
|FPRC;

|PROCESO tanto100;
   tan100 = 0;
   tan102 = 0;
   |SI i_trim > 0; dif = i_trim;   |FINSI;
   |SI i_trim < 0; dif = - i_trim; |FINSI;
   |SI cmsal ! 0;
      |SI i_trim ! 0;
         tan100 = (dif * 100) / cmsal;
      |FINSI;
   |FINSI;
   |SI i_trimt > 0; dif = i_trimt;   |FINSI;
   |SI i_trimt < 0; dif = - i_trimt; |FINSI;
   |SI cmsal2 ! 0;
      |SI i_trimt ! 0;
         tan102 = (dif * 100) / cmsal2;
      |FINSI;
   |FINSI;

   |SI tan100 < 0; tan100 = - tan100; |FINSI;
   |SI tan102 < 0; tan102 = - tan102; |FINSI;
|FPRC;

|| ***********************************************************************
||                       CALCULO ENTRADA DE DATOS
|| ***********************************************************************

|PROCESO rellena;
   rellenos = "zzzzzzzzzzzz";
   menor = #caestr67#7;
   |SI menor = 1;  nume1 = dig4;  |FINSI;
   |SI menor = 2;  nume1 = dig5;  |FINSI;   || construye el hasta segun
   |SI menor = 3;  nume1 = dig6;  |FINSI;   ||/el nivel seleccionado
   |SI menor = 4;  nume1 = dig7;  |FINSI;
   |SI menor = 5;  nume1 = dig8;  |FINSI;
   |SI menor = 6;  nume1 = dig9;  |FINSI;
   nume1 = 100 + nume1;
   rellenos = #caestr67#1 % nume1;
   rellenos = rellenos + "zzzzzzzzzzzz";
   rellenos = rellenos % 112;
|FPRC;

|PROCESO lecturas; || Leer el saldo de la cuenta o el ratio
   |SI #caestr67#20 = "C"; |HAZ lectura_cta; |FINSI;
   |SI #caestr67#20 = "R"; |HAZ lectura_ratio; |FINSI;
|FPRC;

|PROCESO lectura_cta; || leer la cuenta a comparar.
   titulo = "CUENTA";
   titul2 = "CTA:";
   sw_error = 1;
   |ABRE #1;
   #cacuenta#0 = #caestr67#8;
   #cacuenta#1 = #caestr67#9;
   |LEE 001#1=;
   |SI FSalida ! 0; |CIERRA #1; |ACABA; |FINSI;
   sw_error = 0;
   cmcta = #cacuenta#0;
   cmnom = #cacuenta#2;
   cmsal = 0;
   cmsal2 = 0;
   |HAZ suma;
   |SI i_trim ] 0; cmsal =   i_trim; cmsal2 =   i_trimt; deac = "ACREEDOR";  |FINSI;
   |SI i_trim < 0; cmsal = - i_trim; cmsal2 = - i_trimt; deac = "DEUDOR";    |FINSI;
   |CIERRA #1;
|FPRC;

|PROCESO lectura_ratio; || leer el ratio a comparar.
   titulo = "RATIO ";
   titul2 = "RAT:";
   sw_error = 1;
   |ABRE #103;
   #caratios#0 = #caestr67#21;
   |LEE 001#103=;
   |SI FSalida = 0;
      sw_error = 0;
      deac = "RATIO";
      cmcta = "0000" + #caestr67#21;
      cmcta = cmcta % -104;
      cmnom = #caratios#1;
      |HAZ calculo_ratio;
      |SI error = 0;
         |SI  #caestr67#2 = 1; cmsal = #caratios#5;            cmsal2 = #caratios#5;  |FINSI;
         |SI  #caestr67#2 = 2; cmsal = #caratios#8  - #caratios#5;  cmsal2 = #caratios#8;  |FINSI;
         |SI  #caestr67#2 = 3; cmsal = #caratios#11 - #caratios#8;  cmsal2 = #caratios#11; |FINSI;
         |SI  #caestr67#2 = 4; cmsal = #caratios#14 - #caratios#11; cmsal2 = #caratios#14; |FINSI;
         |SI #caestr67#4 = "S"; |Y eaMoneda = "P";
            cmsal  = cmsal  / 1000; cmsal  = cmsal  ! 0;
            cmsal2 = cmsal2 / 1000; cmsal2 = cmsal2 ! 0;
         |FINSI;
      |SINO;
         sw_error = 1;
      |FINSI;
   |FINSI;
   |CIERRA #103;
|FPRC;

|PROCESO litmes;
   |SI #caestr67#2 = 1;
      lmes1 = "     ENERO";
      lmes2 = "   FEBRERO";
      lmes3 = "     MARZO";
      t1 = 14;
      t2 = 17;
      t3 = 20;
   |FINSI;
   |SI #caestr67#2 = 2;
      lmes1 = "     ABRIL";
      lmes2 = "      MAYO";
      lmes3 = "     JUNIO";
      t1 = 23;
      t2 = 26;
      t3 = 29;
   |FINSI;
   |SI #caestr67#2 = 3;
      lmes1 = "     JULIO";
      lmes2 = "    AGOSTO";
      lmes3 = "SEPTIEMBRE";
      t1 = 32;
      t2 = 35;
      t3 = 38;
   |FINSI;
   |SI #caestr67#2 = 4;
      lmes1 = "   OCTUBRE";
      lmes2 = " NOVIEMBRE";
      lmes3 = " DICIEMBRE";
      t1 = 41;
      t2 = 44;
      t3 = 47;
   |FINSI;
   |SI #caestr67#17 = "N"; lmes1 = "          "; |FINSI;
   |SI #caestr67#18 = "N"; lmes2 = "          "; |FINSI;
   |SI #caestr67#19 = "N"; lmes3 = "          "; |FINSI;
|FPRC;

|PROCESO HazTodo;

   |HAZ FecApeCie;
   |SI enMAper ! dig1;
       nHasMes = 3; |SI #0#2 = 2; nHasMes = 6;  |FINSI;
                    |SI #0#2 = 3; nHasMes = 9;  |FINSI;
                    |SI #0#2 = 4; nHasMes = 12; |FINSI;
       |SI nHasMes < enMAper; |Y nHasMes ! 0;
           |ACABA;
       |FINSI;
   |FINSI;

   |CIERRAT #1;
   |HAZ antesque;
   |SI aparam = "";
       enSwOper = 1;
       r_emp = empre;
       r_per = ejerc;
       |SUB_EJECUTA "carecsig";
       enSwOper = 0;
   |FINSI;

   |ABRE #8;
   |DDEFECTO #8;
   #8#0 = 1;
   |LEE 000#8=;
   |CIERRA #8;
   |SI #8#2 = "S";  |Y #8#3 = "N";  |Y aparam = "";
       pathbal = dirfich0;
       balance = 1;
       |SI #8#10 = "S";
           enSwOper = 2;
           |SUB_EJECUTA "carecsig";
           enSwOper = 0;
           |SI enECuadre = 1; |ACABA; |FINSI;
           |NOME_DAT #1 eaNomFCta;
       |SINO;
           |SUB_EJECUTA "careccue";
       |FINSI;
   |FINSI;

   sw_pagina = 0;
   swcomen   = 0
   t_mes1 = 0;
   t_mes2 = 0;
   t_mes3 = 0;
   t_trim = 0;
   t_trimt= 0;
   |HAZ rellena;
   |HAZ lecturas;
   |SI sw_error = 1; |ACABA; |FINSI;

   |SI menu2 = 1; |HAZ panta; |FINSI;  || Por pantalla
   |SI menu2 = 2; |HAZ impre; |FINSI;  || Por impresora
|FPRC;


|PROCESO imprimir; |TIPO 3;
   |HAZ litmes;

   |MENU menu;
   |SI FSalida <0;          || Se ha elegido opcion
       |ACABA;
   |FINSI;
   menu2 = FSalida;
   |SI menu2 = 2;                      || Por impresora
       |IMPRE 0;
       |SI FSalida ! 0;
           |MENAV "    Proceso abortado ...";
           |ACABA;
       |FINSI;
   |FINSI;

   |SI #48#27 = "S";
       |HAZ MultiEmpresa;
   |SINO;
       |DFICE dirfich0; |QBF dirfich0;
       |HAZ HazTodo;
   |FINSI;

   |SI menu2 = 2; |FINIMP; |FINSI;
|FPRC;

|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   nYaLoHeDicho = 0;
   |PATH_DAT #1 dirfich0;
   |PATH_DAT #8 dirfich0;
   |PATH_DAT #103 dirfich0;
   |PATH_DAT #104 dirfich0;

   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 001;
 999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;
