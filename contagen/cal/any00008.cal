|FICHEROS;
   any00008 #0;       || Informe comprobacion suma y saldos

   anm00001 #2;
   anm00005 #4;
   anm00006 #5;
   cacuenta #10;
   anm00017 #17;
   anm00018 #18;
   cawdatos #900;
|FIN;

|VARIABLES;

   aParam  = "";
   nInkey  = 0;
   informe = "";             || Variable informe
   inform1 = "any00008";     || Variable informe
   inform2 = "any10008";     || Variable informe
   inform3 = "any20008";     || Variable informe

   alfa1   = "";
   error2  = "0000No existe informe";
   mensa3  = "    CUENTA ANALITICA ERRONEA !!!";

   selec  = 0;
   swsel  = 0;
   nSw    = 0;
   aCta   = "";
   aCtaC  = "";
   incta  = "";
   ficta  = "";
   aDCuenta = "";
   aHCuenta = "";
   &nSwLinea = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoan_i;

|| ***********************************************************************
||                       CALCULOS DE PANTALLA
|| ***********************************************************************
|CALCULO leeagrupa; |TIPO 0;
 |ABRE #17;
 #17#0 = #0Campo;
 |LEE 001#17=;
 |SI FSalida = 0;
     |ATRI P; |PINTA 0965, "Desgl: ";
     |PINTA 0972, #17#2;
 |FINSI;
 |C_M #0#7, 1, "S";
 |SI #17#2 = "N";
     |C_M #0#7, 1, "N"; #0#7 = "C"; |PINTA #0#7;
 |FINSI;
|FCAL;

|CALCULO LeeSelec; |TIPO 6;
 |SI #0Campo = 0;
     #0#11 = "SIN SELECTOR DE CUENTAS";
     |C_M #0#12, 1, "S";
     |C_M #0#13, 1, "S";
 |SINO;
     |C_M #0#12, 1, "N"; #0#12 = "            "; |PINTA #0#12;
     |C_M #0#13, 1, "N"; #0#13 = "zzzzzzzzzzzz"; |PINTA #0#13;
     |ABRE #4;
     #4#0 = #0Campo;
     |LEE 001#4=;
     |SI FSalida ! 0;
         #4#1 = "ERROR SELECTOR NO EXISTE";
         |ERROR;
     |FINSI;
     #0#11 = #4#1;
     |CIERRA #4;
 |FINSI;
 |PINTA #0#11;
|FCAL;

|CALCULO cuenta; |TIPO 0;
   nInkey = FSalida;
   |SI nInkey = 2;   |ACABA; |FINSI;

   |C_M #0Campo,0,alfa1;
   |SI alfa1 = "N"; |ACABA; |FINSI;

   eaCuenta = #0Campo;
   salidas  = FSalida;
   |HAZ FiltraPuntos;
   #0Campo = eaCuenta;
   |PINTA #0Campo;
|FCAL;

|CALCULO anacta; |TIPO 6;
  salidas = FSalida;
  |HAZ FPuntosAna;
  |SI Campo = 1;
      |SI #0#0 > #0#1;
          |MENAV mensa3;
          |ERROR;
      |FINSI;
  |FINSI;
|FCAL;

|| **********************************************************************
||                       CALCULOS DE IMPRESION
|| **********************************************************************
|PROCESO ver_selec;
 |SI eaCuenta ] #5#2; |Y eaCuenta [ #5#3;
     swsel = 1;
     |ERROR10;
 |FINSI;
|FPRC;

|DEFBUCLE seleccion;
 #5#1;
 ;
 selec, 000;
 selec, 999;
 e;
 NULL, ver_selec;
|FIN;

|| ***********************************************************************
|PROCESO LasLecturas;
 |SI #0#7 = "C";
     |HAZ LeeCble;
 |SINO;
     |HAZ LeeAna;
 |FINSI;

 #0#14 = 0;
 #0#15 = 0;
 #0#16 = 0;
 nSwLinea = 1;
|FPRC;

|PROCESO ElCorte;
  nSwLinea = 2;
  |SI #17#2 = "S";
      |PRINT;
  |FINSI;
  #0#14 = 0;
  #0#15 = 0;
  #0#16 = 0;
|FPRC;

|PROCESO LeeAna;
  |ABRE #2;
  |DDEFECTO #2;
  #2#0 = aCta;
  |LEE 001#2=;
  |CIERRA #2;
|FPRC;

|PROCESO LeeCble;
  |ABRE #10;
  |DDEFECTO #10;
  #10#0 = aCtaC;
  |LEE 001#10=;
  |CIERRA #10;
|FPRC;

|| ***********************************************************************
|PROCESO Imprimir;

 eaCuenta = #18#20;
 |SI selec ! 0;
     swsel = 0;
     |BUCLE seleccion;
     |SI swsel = 0; |ACABA; |FINSI;
 |FINSI;

 |SI nSw = 0;
     aCtaC = #18#20;
     aCta  = #18#0;
     |HAZ LasLecturas;
     nSw = 1;
 |FINSI;

 |SI #0#7 = "C"; |Y aCtaC ! #18#20;
     |HAZ ElCorte;
     aCtaC = #18#20;
     |HAZ LasLecturas;
 |FINSI;

 |SI #0#7 = "A"; |Y aCta ! #18#0;
     |HAZ ElCorte;
     aCta = #18#0;
     |HAZ LasLecturas;
 |FINSI;

 |SI #0#7 = "C"; aCta  = #18#0; |HAZ LeeAna;   |FINSI;
 |SI #0#7 = "A"; aCtaC = #18#20; |HAZ LeeCble; |FINSI;

 |PRINT;
 nSwLinea = 0;

 |SI #18#2 = "D"; #0#14 = #0#14 + #18#16; #0#17 = #0#17 + #18#16; |FINSI;
 |SI #18#2 = "H"; #0#15 = #0#15 + #18#16; #0#18 = #0#18 + #18#16; |FINSI;
 #0#16 = #0#14 - #0#15;
 #0#19 = #0#17 - #0#18;
|FPRC;

|DEFBUCLE any00008A;
 #18#2;
 ;
 #0#9, incta, aDCuenta;
 #0#9, ficta, aHCuenta;
 e;
 NULL, Imprimir;
|FIN;

|DEFBUCLE any00008C;
 #18#1;
 ;
 #0#9, aDCuenta, incta;
 #0#9, aHCuenta, ficta;
 e;
 NULL, Imprimir;
|FIN;

|| ************************************************************
|PROCESO impre;       || Impresion por impresora;

   NumMes = dig1 + (#0#2 - 1);
   |SI NumMes > 12; NumMes = NumMes - 12; |FINSI;
   |SI #0#2 = 13; NumMes = 13; |FINSI;
   |HAZ NombreMes;
   #0#3 = NombreMes;

   NumMes = dig1 + (#0#4 - 1);
   |SI NumMes > 12; NumMes = NumMes - 12; |FINSI;
   |SI #0#4 = 13; NumMes = 13; |FINSI;
   |HAZ NombreMes;
   #0#5 = NombreMes;

   |HAZ FecApeCie;
   enM1 = #0#2; enM2 = #0#4;
   |HAZ LaFechaEmp;

   incta = #0#0;
   ficta = #0#1;

   |SI #0#7 = "C";
       informe = inform1;
       |SI #17#2 = "N"; informe = inform3; |FINSI;
   |FINSI;
   |SI #0#7 = "A"; informe = inform2; |FINSI;

   |IMPRE 0;                                 || Abrimos impresora
   |SI FSalida ! 0; |ACABA; |FINSI;

   |INFOR informe;                    || Abrimos informe
   |SI FSalida ! 0;
       |MENAV error2;
       |FINIMP;
       |ACABA;
   |FINSI;

   selec = #0#10;
   aDCuenta = "            ";
   aHCuenta = "zzzzzzzzzzzz";
   |SI selec = 0; aDCuenta = #0#12; aHCuenta = #0#13; |FINSI;

   nSw  = 0;
   aCta  = "";
   aCtaC = "";
   |SI #0#7 = "C";
       |BUCLE any00008C;
   |SINO;
       |BUCLE any00008A;
   |FINSI;
   |SI nSw = 1;
       |HAZ ElCorte;
       |PIEINF;
   |FINSI;
   |FININF;
   |FINIMP;

   #0#17 = 0;
   #0#18 = 0;
   #0#19 = 0;
|FPRC;

|| *********************************************************************
||                             COMIENZO
|| *********************************************************************
|PROGRAMA;
  |CLS;
  |CABEZA "Comprobacion Fichas Presupuestos Analiticos";

  aParam = PARAMETRO; |QBF aParam;

  |SI aParam = "";
      |HAZ inicio;
      |HAZ Analitica;
      |SI  enSwAna = 0;
           alfa1 = "    CONTABILIDAD NO ANALITICA ";
           |SI eaAnNiv = "E";
               alfa1 = "    EMPRESA NO ANALITICA ";
           |FINSI;
           |MENAV alfa1;
           |ACABA;
      |FINSI;
  |FINSI;

  FSalida = 1;
  |PARA; |SI 0 < FSalida; |HACIENDO;
      |PINPA #0#0;
      |PINDA #0#0;
      |ENDATOS #1#0;   || Seleccion
      |SI 0 [ FSalida;
         |HAZ impre;
         FSalida = 1;
      |FINSI;
   |SIGUE;
|FPRO;
