|FICHEROS;
   carecuad #0;
   camaasic #1;
   camaasil #2;
   catrasi1 #3;
   catrasi2 #4;
   caconasi #5;
   camaniva #15;

   calicont #31;  || Contrapartidas
   camanefe #347; || Cobros/Pagos 347
|FIN;

|VARIABLES;
   alfa1 = "";
   alfa2 = "";
   alfa3 = "";
   feini = @;
   fefin = @;

   Estado5 = 0;
   reg_leidos = 0;
   reg_grabados = 0;
   a_doc = 0;
   a_reg = 0;
   temporal1 = "";
   temporal2 = "";

   x = 0;
   c_doc = 0;
   sw    = 0;

   Linea = 0;
   sw_error = 0;
   diario = 0;
   fecha = @;
   docum = 0;
   debe = 0;
   haber = 0;
   &antdia = 0;
   &antfec = @;
   &antdoc = 0;
   &numero = 0;
   &error = "";

   traba1 = "";
   traba2 = "";
   traba3 = "";
   traba4 = "";
   traba31 = "";

   VAlfa1 = "";
   VAlfa2 = "";
   VAlfa3 = "";
   VAlfa11 = "";
   VAlfa21 = "";
   VAlfa31 = "";
   &enEmbDia  = 0;
   &enNuevo0  = 0;
   &efNuevo1  = @;
   &enEmbDoc  = 0;
   &enNuevo2  = 0;
   &enEmbLin  = 0;
   &enNuevo3  = 0;

   aPassword = "";
|FIN;

|INCLUYE dscont_i;

|| ************************************************************************
|PROCESO ivas;
   #catrasi2#0 = #catrasi1#12;
   #catrasi2#1 = #catrasi1#13;
   #catrasi2#2 = #catrasi1#14;
   #catrasi2#3 = #catrasi1#15;
   |LEE 101#4=;
   |SI FSalida = 0; |ACABA; |FINSI;
   #catrasi2#0 = #catrasi1#12;
   #catrasi2#1 = #catrasi1#13;
   #catrasi2#2 = #catrasi1#14;
   #catrasi2#3 = #catrasi1#15;
   #catrasi2#4 = #camaasil#0;
   #catrasi2#5 = #camaasil#1;
   #catrasi2#6 = #camaasil#2;
   #catrasi2#7 = #catrasi1#0;
   #catrasi2#8 = #catrasi1#1;
   #catrasi2#9 = #catrasi1#2;
   #catrasi2#10 = #catrasi1#24;
   |GRABA 020#4n;
   |LIBERA #4;
|FPRC;

|PROCESO impresora;
   |SI sw_error = 0;
      |IMPRE 0;
      |SI FSalida ! 0;  |ACABA;  |FINSI;
      |INFOR "cadescua";
      |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;
      sw_error = 1;
   |FINSI;
   antdia = diario; antfec = fecha; antdoc = docum;
   numero = numero + 1;
   |PRINT;
|FPRC;

|PROCESO haycambio;
   a_doc = a_doc + 1;
   #carecuad#3 = a_doc;
   ||PINTA #0#3;
   |SI debe ! haber;
      error = "Descuadre.";
      |HAZ impresora;
   |FINSI;
   |HAZ nuevoAsi;
|FPRC;

|PROCESO grabaCab;
   |DDEFECTO #1;
   #camaasic#0 = diario;
   #camaasic#1 = fecha;
   #camaasic#2 = c_doc;
   |LEE 101#1=;
   |SI FSalida ! 0;
      #camaasic#0 = diario;
      #camaasic#1 = fecha;
      #camaasic#2 = c_doc;
      #camaasic#3 = c_doc;
      |GRABA 020#1b;
      #carecuad#4 = c_doc;
      |PINTA #carecuad#1;
      |PINTA #carecuad#2;
      |PINTA #carecuad#3;
      |PINTA #carecuad#4;
   |FINSI;
   |SI #catrasi1#8 = "D";
      #camaasic#4 = #camaasic#4 + #catrasi1#9;
      |SI #camaasic#09 = 0; #camaasic#08 = #catrasi1#4; #camaasic#09 = #catrasi1#5; |FINSI;      || contrp.HABER
   |SINO;
      #camaasic#5 = #camaasic#5 + #catrasi1#9;
      |SI #camaasic#11 = 0; #camaasic#10 = #catrasi1#4; #camaasic#11 = #catrasi1#5; |FINSI;      || contrp.DEBE
   |FINSI;
   #camaasic#6 = #camaasic#4 - #camaasic#5;
   #camaasic#12 = #catrasi1#24;
   |SI #catrasi1#12 = "R";
       #camaasic#13 = 1;
   |FINSI;
   |SI #catrasi1#12 = "S";
       #camaasic#13 = 2;
   |FINSI;
   |SI #catrasi1#12 = "N";
       #camaasic#13 = 3;
   |FINSI;
   #camaasic#15 = #catrasi1#25;
   #camaasic#16 = #catrasi1#27;
   |GRABA 020#1a;
   |LIBERA #1;
|FPRC;

|PROCESO grabaLinea;
   Linea = Linea + #0#5;  || la suma 10;

   |SI diario = 0; |O diario = 99; Linea = #camaasil#3; |FINSI;

   |DDEFECTO #3;
   #catrasi1#0 = diario;
   #catrasi1#1 = fecha;
   #catrasi1#2 = c_doc;
   #catrasi1#3 = Linea;
   |LEE 101#3=;
   |SI FSalida = 0; |LIBERA #3; |ACABA; |FINSI;
   #catrasi1#0 = diario;
   #catrasi1#1 = fecha;
   #catrasi1#2 = c_doc;
   #catrasi1#3 = Linea;
   |PARA x = 4; |SI x [ 30; |HACIENDO x = x + 1;
      #catrasi1#x# = #camaasil#x#;
   |SIGUE;
   #catrasi1#24 = c_doc;   ||Contamos tambien los registros
   |GRABA 020#3n;
   |SI FSalida = 0;
      reg_grabados = reg_grabados + 1;
      #carecuad#2 = reg_grabados;
      ||PINTA #0#2;
   |FINSI;
   |LIBERA #3;
   |SI #catrasi1#12 = "R"; |O #catrasi1#12 = "S"; |O #catrasi1#12 = "N"; |HAZ ivas; |FINSI;
|FPRC;

|PROCESO grabaContra;
   #calicont#0 = #camaasil#0;
   #calicont#1 = #camaasil#1;
   #calicont#2 = #camaasil#2;
   #calicont#3 = #camaasil#3;
   |LEE 001#31=;
   |SI FSalida ! 0;
      |ACABA;
   |FINSI;
   |GRABAENDEF VAlfa2, VAlfa3, 0,#3#0, 1,#3#1, 2,#3#2, 3,#3#3, 4,#31#4, 5,#31#5, 6,#3#24;
|FPRC;

|PROCESO grabaCobPag347;
   #camanefe#0 = #camaasil#0;
   #camanefe#1 = #camaasil#1;
   #camanefe#2 = #camaasil#2;
   #camanefe#3 = #camaasil#3;
   |LEE 001#347=;
   |SI FSalida = 0;
       |GRABAENDEF VAlfa21, VAlfa31, 0,#3#0, 1,#3#1, 2,#3#2, 3,#3#3, 4,#347#4, 5,#347#5, 6,#3#24;
   |FINSI;
|FPRC;

|PROCESO grabaEmbargo;
    |SI HayAcceso = 1;
        |SI eaTEmbargo = "P"; |O eaTEmbargo = "A";
            |SI enCEmbargo ! 0; |Y enCEmbargo = #camaasil#6;
                enEmbDia = #camaasil#0; enNuevo0 = #catrasi1#0;
                efFechaE = #camaasil#1; efNuevo1 = #catrasi1#1;
                enEmbDoc = #camaasil#2; enNuevo2 = #catrasi1#2;
                enEmbLin = #camaasil#3; enNuevo3 = #catrasi1#3;
                |HAZ eCambioApteEmb;
            |FINSI;
        |FINSI;
    |FINSI;
|FPRC;

|PROCESO nuevoAsi;
   c_doc = c_doc + 1;
   Linea = 1 - #0#5;  ||  -9;
   debe = 0;
   haber = 0;
   diario = #camaasil#0;
   fecha  = #camaasil#1;
   docum  = #camaasil#2;
|FPRC;

|PROCESO actuali;
   reg_leidos = reg_leidos + 1;
   #carecuad#1 = reg_leidos; ||PINTA #0#1;

   |SI sw = 0;
      |HAZ nuevoAsi;
      sw = 1;
      a_doc= 1; #carecuad#3 = a_doc;
      ||PINTA #0#3;
   |FINSI;

   |SI diario ! #camaasil#0; |O fecha ! #camaasil#1; |O docum ! #camaasil#2;
      |HAZ haycambio;
   |SINO;
      |SI diario ! 0; |Y diario ! 99;
         |SI Linea > 1;
            |SI debe = haber; |HAZ nuevoAsi; |FINSI;
         |FINSI;
      |FINSI;
   |FINSI;

   |SI #camaasil#8 = "D";
      debe = debe + #camaasil#9;
   |SINO;
      haber = haber + #camaasil#9;
   |FINSI;

   |HAZ grabaLinea;
   |HAZ grabaEmbargo;
   |HAZ grabaContra;
   |HAZ grabaCobPag347;
   |HAZ grabaCab;
|FPRC;

|DEFBUCLE carecuad0;
   #2#1;
   ;
   00, feini, 000000,    0;
   99, fefin, 999999, 9999;
   be;
   NULL, NULL, NULL, NULL, NULL, actuali;
   #2#1, #2#0, #2#2;
|FIN;

|| ************************************************************************
||                EMPIEZA EL PROGRAMA CON TIPO 3
|| ************************************************************************

|PROCESO acuadrar; |TIPO 3;
   |SI #carecuad#0 = "N";  |ACABA; |FINSI;

   |HAZ Analitica;
   |HAZ DirAplicSt;
   |HAZ eLeeParametro;

   |NOPARA;

   |ABRE #1;
   |CIERRA #1;
   |DELINDEX #1;

   |HAZ nom_usu;
   alfa1 = "o" + nom_tmp;
   alfa2 = "p" + nom_tmp;
   traba1 = alfa1;
   |NOME_DAT #catrasi1 #alfa1#;
   |NOME_DAT #catrasi2 #alfa2#;

   |ABRE #3;
   |CIERRA #3;
   |DELINDEX #3;
   |ABRE #4;
   |CIERRA #4;
   |DELINDEX #4;

   || *************************************************
   traba3 = "q" + nom_tmp;
   |DFICE dir_fich;
   |DDEF VAlfa1; |QBF VAlfa1;
   VAlfa2 = VAlfa1 + "calicont";
   VAlfa3 = dir_fich + traba3;
   || ***************************************************
   traba31 = "r" + nom_tmp;
   |DFICE dir_fich;
   |DDEF VAlfa11; |QBF VAlfa11;
   VAlfa21 = VAlfa11 + "camanefe";
   VAlfa31 = dir_fich + traba31;

   |BLIN 24;
   |PINTA 2410,"Reconstruyendo Diario ... ";

   |ABRE #1;
   |ABRE #3;
   |ABRE #4;
   |ABRE #31;
   |ABRE #347;

   c_doc = 0;
   feini = "01.01.1900"
   fefin = "31.12.2099"

   |BUCLE carecuad0;

   |SI sw ! 0;
      |SI debe ! haber;
         error = "Descuadre.";
         |HAZ impresora;
      |FINSI;
   |FINSI;
   |SI sw_error = 1;
      |PIEINF;
      |FININF;
      |FINIMP;
   |FINSI;

   |CIERRA #347;
   |CIERRA #31;
   |CIERRA #4;
   |CIERRA #3;
   |CIERRA #2;
   |CIERRA #1;

   |HAZ final;
   |SIPARA;
|FPRC;

|| ***************************** PROCESOS FINALES

|PROCESO modi_iva;
    |SELECT #3#camaniva;
    #camaniva#0 = #catrasi2#1;
    #camaniva#2 = #catrasi2#3;
    #camaniva#3 = #catrasi2#2;
    |LEE 101#camaniva.=;
    |SI FSalida ! 0; |ACABA; |FINSI;
    #camaniva#7 = #catrasi2#7;
    #camaniva#8 = #catrasi2#8;
    #camaniva#9 = #catrasi2#9;
    |GRABA 020#camaniva.a;
    |LIBERA #camaniva;
    |SELECT #1#camaniva;
|FPRC;

|DEFBUCLE catrasi2;
   #4#1;
   ;
   " ", 00, 000000, "     ";
   "z", 99, 999999, "zzzzz";
   be;
   NULL, modi_iva;
|FIN;

|PROCESO final;
   |SI reg_leidos ! reg_grabados;
      |CUADRO 1003 1477;
      |ATRI R;
      |PINTA 1104, " HAY UN PROBLEMA EN EL FICHERO DE APUNTES, EL PROCESO NO PUEDE CONTINUAR ";
      |PINTA 1204, "  LANCE UNA INDEXACION DE FICHEROS (OPCIONES AVANZADAS/CONTROL INDICES)  ";
      |PINTA 1304, "                E INTENTE EJECUTAR DE NUEVO ESTE PROCESO.                ";
      |ATRI 0;
      |PAUSA;
      |ACABA;
   |FINSI;

   |DFICE dir_fich;       || Copia el fichero catrasi1 al camaasil
   traba2 = "camaasil";
   |HAZ copiando;
   |DELINDEX #3;

   |DFICE dir_fich;       || Copia el fichero de contrapartidas
   traba1 = traba3;       || con el creado por GRABAENDEF
   traba2 = "calicont";
   |HAZ copiando;

   traba4 = VAlfa3 + ".dat";   ||borra el fichero contrap. de GRABAENDEF
   |FBORRA traba4;
   traba4 = VAlfa3 + ".ixx";
   |FBORRA traba4;
   traba4 = VAlfa3 + ".ddx";
   |FBORRA traba4;

   |DFICE dir_fich;        || Copia el fichero de cobros y pagos
   traba1 = traba31;       || con el creado por GRABAENDEF
   traba2 = "camanefe";
   |HAZ copiando;

   traba4 = VAlfa31 + ".dat";
   |FBORRA traba4;
   traba4 = VAlfa31 + ".ixx";
   |FBORRA traba4;
   traba4 = VAlfa31 + ".ddx";
   |FBORRA traba4;

   || ... Actualiza el numero de Documento y el numero de Registros.
   |ABRE #5;
   |LEE 101#5p;
   Estado5 = FSalida;
   #caconasi#1 = c_doc;
   #caconasi#2 = c_doc;
   |SI Estado5 ! 0; |GRABA 020#5n; |FINSI;
   |SI Estado5 = 0; |GRABA 020#5a; |FINSI;
   |LIBERA #5;
   |CIERRA #5;

   || ... Proceso de IVAS
   |BLIN 24;
   |PINTA 2410,"Relacionando Fichero de Facturas ... ";
   |ABRE #camaniva;
   |BUCLE  catrasi2;
   |CIERRA #camaniva;
   |DELINDEX #4;
|FPRC;

|PROCESO copiando;
|| traba = " COPIANDO FICHERO: " + traba1 + "        ";
|| |PINTA 2211, traba;
   temporal1 = dir_fich + "/" + traba1 + ".ixx";
   temporal2 = dir_fich + "/" + traba2 + ".ixx";
   |COPIA_FICHERO temporal1,temporal2;
   |SI FSalida ! 0;  |ACABA;  |FINSI;
   temporal1 = dir_fich + "/" + traba1 + ".dat";
   temporal2 = dir_fich + "/" + traba2 + ".dat";
   |COPIA_FICHERO temporal1,temporal2;
   |SI FSalida ! 0;
      temporal2 = dir_fich + traba2 + ".ixx";
      |FBORRA temporal2;
      |ACABA;
   |FINSI;
   temporal1 = dir_fich + "/" + traba1 + ".ddx";
   temporal2 = dir_fich + "/" + traba2 + ".ddx";
   |COPIA_FICHERO temporal1,temporal2;
|FPRC;

|PROGRAMA;
     |CUADRO 0501 2380;
     |BLANCO 0602 2279;
     |PINTA 1320, "Introduzca Password : ";
     E_inf = "";
     E_sup = "20";
     aPassword = 1342 ? -1;
     |SI aPassword ! "<MYPASWD>     ";
         |MENAV "     Permiso denegado.";
         |ACABA;
     |FINSI;

     |CLS;
     |ATRI R;
     |PINTA 0911, "                                                    ";
     |PINTA 1011, " ESTE PROCESO  MODIFICA  EL NUMERO  DE DOCUMENTO DE ";
     |PINTA 1111, " LOS  ASIENTOS CONTABLES  Y  PIERDE LA RELACION QUE ";
     |PINTA 1211, " PUEDAN TENER ESTOS ASIENTOS CON OTRAS APLICACIONES ";
     |PINTA 1311, " (cartera, minutacion, gestion, laboral ...)        ";
     |PINTA 1411, "                                                    ";
     |PINTA 1511, "                                                    ";
     |PINTA 1611, "                                                    ";
     |ATRI 0;
     |CUADRO 0910 1663;
     |ATRI I;
     |PINTA 1511, " ESTA SEGURO (S/N):                                 ";

     alfa1 = "N"; |PINTA 1531, alfa1;
     E_inf = "SNsn";
     E_sup = 1;
     alfa1 = 1531 ? 1; alfa1 = alfa1 > " "; |PINTA 1531, alfa1;

     |ATRI 0;
     |SI alfa1 = "S";
         |MANTE #0;
     |FINSI;
|FPRO;
