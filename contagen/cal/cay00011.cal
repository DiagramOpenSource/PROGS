|FICHEROS;
     cay00011 #0;
     cacuenta #5;
     canempre #30;

     :/basica/def/agifigen #100;
     :/basica/def/agim0019 #119;

     :/contasoc/def/sodacoli #2;
     :/contasoc/def/maacceso #20;
     :/contasoc/def/maempr01 #21;
     :/contasoc/def/cubal801 #801;
     :/contasoc/def/cubal802 #802;

     ca8ctrle #400;
     ca8comay #401;
     ca8m0000 #420;

     caw00028 #999;
|FIN;

|VARIABLES;
     &entrada  = 0;
     x         = 0;
     nCampo    = 0;
     nSwTipo   = 0;
     nPin1     = 0;
     aParam    = "";
     nHay      = 0;
     aFichero  = "";
     nEmpresa  = 0;
     nDEmpresa = 0;
     nHEmpresa = 0;
     aAlfa1    = "";
     aAlfa2    = "";
     nPara1    = 0;
     nNume1    = 0;
     nNume2    = 0;
     nEjer     = 0;
     nEjerC    = 0;
     nEjer1    = 0;
     nEjer2    = 0;
     nEjerC1   = 0;
     nEjerC2   = 0;
     aPath     = "";

     &enCam0 = 0;
     &enCam1 = 0;
     &enCam2 = 0;
     &enCam6 = 0;
     &enCam7 = 0;
     &enCam8 = 0;

     nSocie   = 0;
     nConta   = 0;
     &nBalNum = 0;
     &nCtaNum = 0;
     &enTBal  = 0;
     &enTCta  = 0;

     &enActivo = 0;
     &enPasivo = 0;
     &enCifra  = 0;
     nTrabaja  = 0;

     &anyo    = 0;
     &eaFich1 = "";
     &eaFich2 = "";
     &eaFich3 = "";
     &eaICtas = "";

     aTBal = "";
     aTCta = "";
     nSw1  = 0;
     nSw2  = 0;
     aEstado = "";

  CodigoEmpresa  = 0;
  PeriodoEmpresa = 0;
  &enEmpresaPINET = 0;
  &enErrorPINET   = 0;
|FIN;

|INCLUYE dscont_i;

|| *************************************************************************
||                        CALCULOS DE PANTALLA
|| *************************************************************************

|CALCULO DesdeDonde;
   |C_M #0#41, 1, "S";
   |SI #0#0 = "C";
       |C_M #0#41, 1, "N";
   |FINSI;
|FCAL;

|PROCESO Mayor;
     |SI #0Campo = "N";
         |PARA x = 5;  |SI x [ 24;  |HACIENDO x = x + 1;
               |C_M #0x,1,"S";
         |SIGUE;
         |C_M #0#3,1,"N"; #0#3 = 0; |PINTA #0#3;
         |C_M #0#4,1,"N"; #0#4 = 0; |PINTA #0#4;
     |SINO;
         |PARA x = 5;  |SI x [ 24;  |HACIENDO x = x + 1;
               |C_M #0x,1,"N"; #0x = 0; |PINTA #0x;
         |SIGUE;
         |C_M #0#3,1,"S";
         |C_M #0#4,1,"S";
     |FINSI;
|FPRC;

|CALCULO SelTipo0;
     |SI Haybasica = 0; |ACABA; |FINSI;
     |SI #0#38 = "N";
         #0#26 = 0;   |PINTA #0#26;  |C_M #0#26, 1, "N";
         #0#27 = 99;  |PINTA #0#27;  |C_M #0#27, 1, "N";
         |PARA nCampo = 28;  |SI nCampo [ 37;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |C_M #0#26, 1, "S";
         |C_M #0#27, 1, "S";
         |PARA nCampo = 28;  |SI nCampo [ 37;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO SelTipo1;
     |SI Haybasica = 0; |ACABA; |FINSI;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 37;  |HACIENDO nCampo = nCampo + 1;
               #0Campo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 37;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|PROCESO DesHas;
     |SI #0#4 < #0#3;
         |MENAV "      Error en Introduccion";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO SiModi; |TIPO 7;
     |PARA x = Campo + 1;  |SI x [ 24;  |HACIENDO x = x + 1;
           |C_M #0x,1,"S";
     |SIGUE;
|FPRC;

|PROCESO NoModi;
     |PARA x = Campo + 1;  |SI x [ 24;  |HACIENDO x = x + 1;
           |C_M #0x,1,"N"; #0x = 0; |PINTA #0x;
     |SIGUE;
|FPRC;

|PROCESO EsBuena; || desde campo de empresas seleccion
     |SI FSalida = 2; |ACABA; |FINSI;
     |SI #0Campo = 0; |HAZ NoModi;  |ACABA;  |FINSI;

     |SI #0#0 = "C";
         |ABRE #30;
         #30#2 = #0Campo;
         #30#3 = 0;
         |LEE 011#30];
         |SI FSalida ! 0;  |O #30#2 ! #0Campo;
             |MENAV "     NO Existe la Empresa Contable";
             |ERROR;
         |FINSI;
         |CIERRA #30;
     |FINSI;

     |SI #0#0 = "S"; |Y HaySociedad = 1;
         |ABRE #20;
         #20#2 = #0Campo;
         #20#3 = #0#1;
         |LEE 011#20=;
         |SI FSalida ! 0;
             |MENAV "     NO Existe la Empresa en CCAA";
             |ERROR;
         |FINSI;
         |CIERRA #20;
     |FINSI;
|FPRC;

|| **********************************************************************
|PROCESO ImprAuxiliar;

  #999#29 = 2;  #999#30 = "PGC PYMES";
  #999#31 = 3;  #999#32 = "PYMES";
  #999#33 = 3;  #999#34 = "PYMES";

  nSw1 = 0;
  |SI #999#13 ] #0#42; nSw1 = nSw1 + 1; |FINSI;
  |SI #999#14 ] #0#43; nSw1 = nSw1 + 1; |FINSI;
  |SI #999#15 ] #0#44; nSw1 = nSw1 + 1; |FINSI;

  nSw2 = 0;
  |SI #999#26 ] #0#42; nSw2 = nSw2 + 1; |FINSI;
  |SI #999#27 ] #0#43; nSw2 = nSw2 + 1; |FINSI;
  |SI #999#28 ] #0#44; nSw2 = nSw2 + 1; |FINSI;

  |SI nSw1 ] 2; |Y nSw2 ] 2;

      #999#29 = 1;  #999#30 = "PGC NORMAL";
      #999#31 = 1;  #999#32 = "NORMAL";
      #999#33 = 2;  #999#34 = "ABREVIADO";

      |SI #0#1 ] 2013;
          #999#31 = 2;  #999#32 = "ABREVIADO";

          nSw1 = 0;
          |SI #999#13 > #0#45; nSw1 = nSw1 + 1; |FINSI;
          |SI #999#14 > #0#46; nSw1 = nSw1 + 1; |FINSI;
          |SI #999#15 > #0#47; nSw1 = nSw1 + 1; |FINSI;

          nSw2 = 0;
          |SI #999#26 > #0#45; nSw2 = nSw2 + 1; |FINSI;
          |SI #999#27 > #0#46; nSw2 = nSw2 + 1; |FINSI;
          |SI #999#28 > #0#47; nSw2 = nSw2 + 1; |FINSI;

          |SI nSw1 ] 2; |Y nSw2 ] 2;
              #999#31 = 1;  #999#32 = "NORMAL";
          |FINSI;
      |FINSI;

      nSw1 = 0;
      |SI #999#13 ] #0#48; nSw1 = nSw1 + 1; |FINSI;
      |SI #999#14 ] #0#49; nSw1 = nSw1 + 1; |FINSI;
      |SI #999#15 ] #0#50; nSw1 = nSw1 + 1; |FINSI;

      nSw2 = 0;
      |SI #999#26 ] #0#48; nSw2 = nSw2 + 1; |FINSI;
      |SI #999#27 ] #0#49; nSw2 = nSw2 + 1; |FINSI;
      |SI #999#28 ] #0#50; nSw2 = nSw2 + 1; |FINSI;

      |SI nSw1 ] 2; |Y nSw2 ] 2;
          #999#33 = 1;  #999#34 = "NORMAL";
      |FINSI;
  |FINSI;

  |PRINT;
|FPRC;

|DEFBUCLE ImprAuxiliar;
  #999#1;
  ;
  00001;
  99999;
  ;
  NULL, ImprAuxiliar;
|FIN;

|PROCESO Imprimir;
     |IMPRE 0;
     |SI FSalida ! 0;
         |MENAV "    Error en Impresora. Proceso interrupido.";
         |ACABA;
     |FINSI;

     |INFOR "cay00011";
     |SI FSalida ! 0;
         |MENAV "    No existe Informe";
         |FINIMP;
         |ACABA;
     |FINSI;

     |BUCLE ImprAuxiliar;
     |PIEINF;
     |FININF;
     |FINIMP;
|FPRC;
|| **********************************************************************

|PROCESO GrabaAuxiliar;
   #999#0 = nEmpresa;
   |LEE 041#999=;
   |SI FSalida ! 0;
       |DDEFECTO #999;
       #999#0  = nEmpresa;
       #999#1  = #100#1;
       #999#2  = #100#13;
       #999#3  = nEjer1;
       #999#16 = nEjer2;
       |GRABA 020#999c;
   |FINSI;
   nNume2 = 4; |SI nNume1 = 2; nNume2 = 17; |FINSI;

                        #999nNume2 = eNum2008;
   nNume2 = nNume2 + 1; #999nNume2 = #401#3;
   nNume2 = nNume2 + 1; #999nNume2 = #401#2;
   nNume2 = nNume2 + 1; #999nNume2 = nBalNum;;
   nNume2 = nNume2 + 1; #999nNume2 = enTBal;
   nNume2 = nNume2 + 1; #999nNume2 = aTBal;
   nNume2 = nNume2 + 1; #999nNume2 = nCtaNum;
   nNume2 = nNume2 + 1; #999nNume2 = enTCta;
   nNume2 = nNume2 + 1; #999nNume2 = aTCta;
   nNume2 = nNume2 + 1; #999nNume2 = enActivo;
   nNume2 = nNume2 + 1; #999nNume2 = enCifra;
   nNume2 = nNume2 + 1; #999nNume2 = nTrabaja;

   |GRABA 020#999a;
   |LIBERA #999;
   nHay = 1;
|FPRC;
||////////////////////////////////////////////////////////////////////

|PROCESO LeeBalCta;
       |DDEFECTO #420;
       #420#0 = nBalNum;
       |LEE 001#420=;
       enTBal = #420#2;
       |SI #420#2 = 1; aTBal = "Normal"; |FINSI;
       |SI #420#2 = 2; aTBal = "Abreviado"; |FINSI;
       |SI #420#2 = 3; aTBal = "PYME"; |FINSI;

       |DDEFECTO #420;
       #420#0 = nCtaNum;
       |LEE 001#420=;
       enTCta = #420#2;
       |SI #420#2 = 1; aTCta = "Normal"; |FINSI;
       |SI #420#2 = 2; aTCta = "Abreviado"; |FINSI;
       |SI #420#2 = 3; aTCta = "PYME"; |FINSI;
|FPRC;

|PROCESO LeoPlan;
      |DDEFECTO #401;
      #401#0 = eNum2008;
      |LEE 041#401=;
|FPRC;

|PROCESO LeeEmpresaConta;
     nConta = 0;
     |ABRE #30;
     |SELECT #4#30;
     |DDEFECTO #30;
     #30#2  = nEmpresa;
     #30#26 = nEjerC;
     |LEE 040#30=;
     |SI FSalida = 0;
         nConta = 1;       || hay contabilidad
     |FINSI;
     |SELECT #1#30;
     |CIERRA #30;

     |SI nConta = 1;
         cod1 = #30#2;
         cod2 = #30#3;
         emEmp = 1;  || Sin mensaje
         |HAZ leelo;
         |SI swerror ! 0;  nConta = 0; |FINSI;
     |FINSI;
     |SI nConta = 1;
         aAlfa1 = ("00000" + enEmpresa) % -105;
         aAlfa2=  ("0"     + enPeriodo) % -101;
         dirfich0 =  aPContag + "fich/" + aAlfa1 + aAlfa2 + "/";
         |HAZ LeoPlan;
     |FINSI;
|FPRC;

|PROCESO LaFicha;
     #20#0 = nEmpresa;
     #20#1 = nEjer;
     |LEE 041#20=;
     |SI FSalida = 0;
         nSocie = 1;
         |DDEFECTO #21;
         #21#0 = #20#0;
         #21#1 = #20#1;
         |LEE 041#21=;
         nTrabaja = #21#75 + #21#76;

         |SI #0#0 = "S";
             nBalNum = #21#101;
             nCtaNum = #21#102;
             |HAZ LeeBalCta;
         |FINSI;
     |FINSI;
|FPRC;

|| ////////////////////////////////////////////////////////
|PROCESO LeeLasCtas;
   enCifra = enCifra + (#5#53 - #5#50);
|FPRC;

|DEFBUCLE LeeLasCtas;
   #5#1;
   3;
   "70          ", "S";
   "70zzzzzzzzzz", "S";
   e;
   NULL, LeeLasCtas;
|FIN;

|PROCESO Contabilidad;

     enEmpresa   = nEmpresa;
     enEjercicio = nEjer;

     enActivo = 0;  enPasivo = 0;
     enCam2   = -1;       || numero de balance
     |SUB_EJECUTA "ca8ys001;Informe";
     enActivo = enActivo ! EURODB;
     enPasivo = enPasivo ! EURODB;

     |HAZ LeeBalCta;

     |PATH_DAT #5 dirfich0;
     enCifra = 0;
     |BUCLE LeeLasCtas;
     |SI enCifra ! 0;
         enCifra = - enCifra;
     |FINSI;
     |HAZ GrabaAuxiliar;
|FPRC;
|| ////////////////////////////////////////////////////////
|PROCESO Sociedad;
     enEmpresa   = nEmpresa;
     enEjercicio = nEjer;
     enActivo    = 0;
     enPasivo    = 0;
     enCifra     = 0;

     |SI #0#41 = "E";
         eaFich1 = "1s" + Usuario;
         eaFich2 = "1c" + Usuario;
         eaFich3 = "1d" + Usuario;
     |SINO;
         eaFich1 = "cubal801";
         eaFich2 = "cubal802";
     |FINSI;
     |NOME_DAT #801 eaFich1;
     |NOME_DAT #802 eaFich2;

     |SI #0#41 = "E";
         |DELINDEX #801;
         |DELINDEX #802;
         anyo    = nEjer;
         |SUB_EJECUTA ":contasoc/cubal800;BORRADOR";
     |FINSI;

     |ABRE #801;
     |SELECT #3#801;
     |DDEFECTO #801;
     #801#0  = nEmpresa;
     #801#1  = nEjer;
     #801#16 = "010000";
     |LEE 141#801=;
     |SI FSalida = 0;
         enActivo = #801#7;
     |FINSI;
     |LIBERA #801;
     |SELECT #1#801;
     |CIERRA #801;

     |ABRE #802;
     |SELECT #3#802;
     |DDEFECTO #802;
     #802#0  = nEmpresa;
     #802#1  = nEjer;
     #802#16 = "040100";
     |LEE 141#802=;
     |SI FSalida = 0;
         enCifra = #802#7;
     |FINSI;
     |LIBERA #802;
     |SELECT #1#802;
     |CIERRA #802;

     |HAZ LeeBalCta;
     |HAZ GrabaAuxiliar;
|FPRC;

|| ////////////////////////////////////////////////////////

|PROCESO LaEmpresa;

    enEmpresaPINET = #agifigen#0;
    |HAZ RestriccionPINET;
    |SI enErrorPINET = 1;  |ACABA;  |FINSI;

    nSwTipo = 1;
    nPin1 = #agifigen#87;
    |SI #0#38 = "S";
        |SI nPin1 ] #0#26; |Y nPin1 [ #0#27;
            nSwTipo = 0;
        |FINSI;
    |SINO;
        |SI nPin1 = #0#28;
            nSwTipo = 0;
        |SINO;
            |PARA nCampo = 29;  |SI nCampo [ 37;  |HACIENDO nCampo = nCampo + 1;
                  |SI nPin1 = #0nCampo; |Y nPin1 ! 0;
                      nSwTipo = 0;
                      |SAL;
                  |FINSI;
            |SIGUE;
        |FINSI;
    |FINSI;
    |SI nSwTipo = 1; |ACABA; |FINSI;

    aEstado = "A";
    |SI #100#94 > "01.01.0000"; |Y #100#94 [ #0#39;
        aEstado = "I";
    |FINSI;
    |SI aEstado ! #0#25; |Y #0#25 ! "T";
        |ACABA;
    |FINSI;

    nEmpresa = #100#0;

    |PARA nNume1 = 1; |SI nNume1 [ 2; |HACIENDO nNume1 = nNume1 + 1;

          |SI nNume1 = 1; nEjer = nEjer1; nEjerC = nEjerC1; |FINSI;
          |SI nNume1 = 2; nEjer = nEjer2; nEjerC = nEjerC2; |FINSI;

          nSocie   = 0;
          eNum2008 = -1;
          nTrabaja = 0;
          |HAZ LeeEmpresaConta;
          |SI HaySociedad = 1; |HAZ LaFicha; |FINSI;

          |SI #0#0 = "C";
              |SI nConta = 1;
                  |HAZ Contabilidad;
              |FINSI;
          |SINO;
              |SI nSocie = 1;
                  |HAZ Sociedad;
              |FINSI;
          |FINSI;

          |PULSATECLA;
    |SIGUE;
|FPRC;

|DEFBUCLE agifigen;
     #100#1;
     ;
     nDEmpresa;
     nHEmpresa;
     e;
     NULL, LaEmpresa;
|FIN;

|PROCESO Tipo3; |TIPO 3;

     enCam0 = 0;      || desde el mes
     enCam1 = 12;     || hasta el mes
     enCam6 = 0;      || estructura la suma de todas
     enCam7 = 0;      || Mes inicial existencias
     enCam8 = 12;     || Mes final existencias

     nEjer1 = #0#1;
     nEjer2 = #0#1 - 1;

     aAlfa1 = nEjer1; aAlfa1 = aAlfa1 % -102; nEjerC1 = aAlfa1;
     aAlfa1 = nEjer2; aAlfa1 = aAlfa1 % -102; nEjerC2 = aAlfa1;

     aFichero = ("1v" + Usuario) % 108;
     |NOME_DAT #999 aFichero; |DELINDEX #999;

     |SI #0#1 ] 2016;
         #0#42 = 4000000;
         #0#43 = 8000000;
         #0#48 = 11400000;
         #0#49 = 22800000;
     |FINSI;
     nHay = 0;
     |ABRE #999;
     |ABRE #401;
     |ABRE #420;
     |SI HaySociedad = 1;  |ABRE #20;  |ABRE #21; |FINSI;

     |SI #0#2 = "S";
         nDEmpresa = #0#3;
         nHEmpresa = #0#4;
         |BUCLE agifigen;
     |SINO;
         |PARA nPara1 = 5; |SI nPara1 [ 24; |HACIENDO nPara1 = nPara1 + 1;
               |SI #0nPara1 ! 0;
                   nDEmpresa = #0nPara1;
                   nHEmpresa = #0nPara1;
                   |BUCLE agifigen;
               |FINSI;
         |SIGUE;
     |FINSI;
     |SI HaySociedad = 1;  |CIERRA #21;  |CIERRA #20;  |FINSI;
     |CIERRA #420;
     |CIERRA #401;
     |CIERRA #999;

     |SI nHay = 1;
         |HAZ Imprimir;
     |SINO;
         |MENAV "    Seleccion vacia";
     |FINSI;

     |DELINDEX #999;
|FPRC;


|PROGRAMA;

     |HAZ inicio;
     |HAZ DirAplicSt;
     CodigoEmpresa  = enEmpresa;
     PeriodoEmpresa = enPeriodo;

     |CLS;
     |DDEFECTO #0;
     |PINPA #0#0;
     aParam = PARAMETRO; |QBF aParam;
     |SI HaySociedad = 0;             || si no hay impuesto solo desde conta
         |SI aParam ! "C"; aParam = "C"; |FINSI;
     |SINO;
         aPath = aPContas + "fich/";
         |PATH_DAT #2  aPath;
         |PATH_DAT #20 aPath;
         |PATH_DAT #21 aPath;
         |PATH_DAT #801 aPath;
         |PATH_DAT #802 aPath;
     |FINSI;

     |SI aParam ! "";
         |C_V #0#0, 1, "N";
         |C_M #0#0, 1, "N";
         #0#0 = aParam;
         |BLIN 5;
         |ATRI R;
         |PINTA 0610, "Informe Empresas con detalle PGC y Balances obligados a confeccionar";
         |ATRI 0;
         |SI aParam = "C";
             |C_V #0#41,1,"N";
             |C_M #0#41,1,"N";
             |PINTA 0834, "                                   ";
         |FINSI;
     |FINSI;
     #0#1 = enEjercicio;

     |PINDA #0#0;
     |ENDATOS #1#0;
|FPRO;
