|FICHEROS;
 caut0007 #0;
 cacuenta #1;
 caclipro #2;

 cam00020 #20,MANTE;
 canempre #30;
 &regisnif@ #709;
:/basica/def/agifigen #100;
 caselemz #911;
 caselem1 #998;
|FIN;

|VARIABLES;
   &entrada   = 1;
   &r_emp     = 0;
   &r_per     = 0;
   aAlfa1     = "";
   aAlfa2     = "";
   nNume1     = 0;
   nNume2     = 0;
   nPara1     = 0;
   nCampo     = 0;
   &eaVarDni  = "";
   &enCalcCif = 0;
   nTecla     = 0;
   nSiLanza   = 0;
   aUltPr     = "";
   aUltCl     = "";
   nUltPr     = 0;
   nUltCl     = 0;

     nDEmpresa  = 0;
     nHEmpresa  = 0;
     aFichero   = "";
     Linea      = 0;
     aBass      = "";
     aDef       = "";
     nSwNoDir   = 0;
     nSwTipo    = 0;
     nSwEjer    = 0;
     nLaEmpresa = 0;
     nPin1      = 0;
     nEjer      = 0;

     &eaOrigen  = "";
     &eaDestino = "";

     aEstEmp    = "";
     aSelTipo   = "";
     nDTip      = 0;
     nHTip      = 0;
     nTip1      = 0;
     nTip2      = 0;
     nTip3      = 0;
     nTip4      = 0;
     nTip5      = 0;
     nTip6      = 0;
     nTip7      = 0;
     nTip8      = 0;
     nTip9      = 0;
     nTip0      = 0;
     aQueQuiero = "";
     aNumCampos = "";
     nNumCampos = 0;

     aDni = "";
     nIVA = 0;
|FIN;

|INCLUYE dscont_i;

|PROCESO Consulta; |TIPO 0;
     |C_M #0Campo,0,aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     |SI FSalida ! 9;  |ACABA;  |FINSI;

     |SUB_EJECUTA_NP "caconemp";
     |ERROR;
     #0Campo = r_emp; |PTEC 802;
|FPRC;

|CALCULO AntesSel; |TIPO 7;
 aAlfa1 = "S";
 |SI #0#27 ! 0;
     aAlfa1 = "N";
     #0#22 = "S";
     #0#23 = 1;
     #0#24 = 99999;
     |PDEFECTO #0, 1, 21;
 |FINSI;
 |PARA nCampo = 1; |SI nCampo [ 24; |HACIENDO nCampo = nCampo + 1;
       |SI aAlfa1 = "N"; |PINTA #0nCampo; |FINSI;
       |SI nCampo ! 21; |C_M #0nCampo,1,aAlfa1; |FINSI;
 |SIGUE;
|FCAL;

|CALCULO LeePlanilla; |TIPO 6;
 |ABRE #911;
 #911#0 = #0#27;
 |LEE 001#911=;
 |SI FSalida ! 0;
     |SI #0#27 = 0;
         |DDEFECTO #911;
         #911#0 = 0;
         #911#1 = "Empresa Actual";
         |GRABA 020#911n;
     |FINSI;
 |FINSI;
 |CIERRA #911;
 |SI #0#27 ! 0;
     #0#25 = #911#45; |PINTA #911#45;
 |FINSI;
|FCAL;

|CALCULO Parrilla; |TIPO 0;
     |C_M #0#22, 0, aAlfa1;
     |SI aAlfa1 = "N";  |ACABA;  |FINSI;

     |SI #0#22 = "N";
         #0#23 = 0;  |PINTA #0#23;  |C_M #0#23, 1, "N";
         #0#24 = 0;  |PINTA #0#24;  |C_M #0#24, 1, "N";
         |PARA nCampo = 1;  |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |C_M #0#23, 1, "S"; |C_M #0#24, 1, "S";
         |PDEFECTO #0,1,21;
         |PARA nCampo = 1;  |SI nCampo [ 21;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO AntesEmpresa; |TIPO 7;
     |C_M #0Campo, 0, aAlfa1;
     |SI aAlfa1 = "N";  |ACABA;  |FINSI;

     #0#21 = ""; |PINTA #0#21;
     |SI #0Campo = 0; |ACABA; |FINSI;
     |ABRE #30;
     #30#2 = #0Campo;
     #30#3 = 0;
     |LEE 011#30];
     |SI FSalida = 0;  |Y #30#2 = #0Campo;
         #0#21 = #30#1; |PINTA #0#21;
     |FINSI;
     |CIERRA #30;
|FCAL;

|CALCULO NoMasEmpresas; |TIPO 0;
     |C_M #0Campo, 0, aAlfa1;
     |SI aAlfa1 = "N";  |ACABA;  |FINSI;

     |SI FSalida = 2;   |ACABA;  |FINSI;
     |SI FSalida = 9;   |HAZ Consulta;  |FINSI;

     |SI #0Campo = 0;
         #0#21 = ""; |PINTA #0#21;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0; |C_M #0nCampo, 1, "N"; |PINTA #0nCampo;
         |SIGUE;
         |ACABA;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;

     |ABRE #30;
     #30#2 = #0Campo;
     #30#3 = 0;
     |LEE 011#30];
     |SI FSalida ! 0;  |O #30#2 ! #0Campo;
         |MENAV "     NO Existe la Empresa Contable";
         |ERROR;
     |SINO;
         #0#21 = #30#1; |PINTA #0#21;
     |FINSI;
     |CIERRA #30;
|FCAL;

||**************************** Procesos cam00020
|CALCULO Tipo11m20; |TIPO 11;
 nTecla = FSalida;
 |SI nTecla = 10;
     |HAZ elCalDNI;
     |ERROR;
 |FINSI;
|FCAL;

|PROCESO elCalDNI;
      enCalcCif = 1;
      eaVarDni  = #20#0;
      |HAZ CalculoDNI;
      #20#0 = eaVarDni;
      |PINTA #20#0;
|FPRC;

|CALCULO elDNI; |TIPO 6;
  |SI FSalida = 10;
      |HAZ elCalDNI;
      |ERROR;
      |ACABA;
  |FINSI;

  eaVarDni  = #20#0;
  enCalcCif = 0;
  |HAZ CalculoDNI;
  eaVarDni = (eaVarDni + "                ") % 112;
  |SI #20#0 ! eaVarDni;
      |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
      |SI FSalida = 0;
          #20#0 = eaVarDni;  |PINTA #20#0;
          |ERROR;
          |ACABA;
      |FINSI;
  |FINSI;
|FCAL;

|CALCULO elDNI0; |TIPO 0;
  eaCodNif = #20#0;
  |HAZ LeeNifs;
  |SI ewNif = 0;
      #20#1 = #709#1; |PINTA #20#1;
  |FINSI;
|FCAL;

|CALCULO cuenta; |TIPO 6;
   |C_M #0Campo,0,aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   nTecla = FSalida;
   |SI nTecla = 15; |O nTecla = 16; |ACABA; |FINSI;

   eaCuenta = #20Campo;
   salidas  = FSalida;
   |HAZ FiltraPuntos;
   #20Campo = eaCuenta;
   |PINTA #20Campo;
|FCAL;

|CALCULO cuefin; |TIPO 0;
   |C_M #0Campo,0,aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   |SI nTecla = 15; |O nTecla = 16;
       |SI Campo = 2;
           #20Campo = aUltPr; |PINTA #20Campo;
           |SI nTecla = 16;
               #20#3 = nUltPr; |PINTA #20#3;
               #20#4 = aUltCl; |PINTA #20#4;
               #20#5 = nUltCl; |PINTA #20#5;
           |FINSI;
       |SINO;
           #20Campo = aUltCl; |PINTA #20Campo;
           |SI nTecla = 16;
               #20#5 = nUltCl; |PINTA #20#5;
           |FINSI;
       |FINSI;
   |FINSI;
   emCta    = 1;
   eaCuenta = #20Campo;
   |HAZ SacaNivel;
   |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea

   |HAZ LeeCuenta;
   |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea
   nNume1 = 6; |SI Campo = 4; nNume1 = 7; |FINSI;
   #20nNume1 = #cacuenta#2; |PINTA #20nNume1;
|FCAL;

|CALCULO TrIVA; |TIPO 0;
   nTecla = FSalida;
   |SI nTecla = 2; |ACABA; |FINSI;

   |C_M #0Campo,0,aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   aTipoIVA = "S";
   |SI Campo = 5; aTipoIVA = "R"; |FINSI;

   |SI nTecla = 10;
      efFechaIVA = ~; enLineaIVA = 0; enIvaTot = 1; |HAZ SelecTipoIVA; enIvaTot = 0;
      #20Campo = enPorcIVA; |PINTA #20Campo;
   |FINSI;

   |SI nTecla = 15; |O nTecla = 16;
       |SI Campo = 3;
           #20#3 = nUltPr; |PINTA #20#3;
           |SI nTecla = 16;
               #20#4 = aUltCl; |PINTA #20#4;
               #20#5 = nUltCl; |PINTA #20#5;
           |FINSI;
       |SINO;
           #20#5 = nUltCl; |PINTA #20#5;
       |FINSI;
   |FINSI;

   |SI #20Campo = 0;
       |SI aTipoIVA = "S";
           #20#8  = 0; |PINTA #20#8;
           #20#9  = 0; |PINTA #20#9;
       |FINSI;
       |SI aTipoIVA = "R";
           #20#10 = 0; |PINTA #20#10;
           #20#11 = 0; |PINTA #20#11;
       |FINSI;
       |ACABA;
   |FINSI;

   efFechaIVA = "31.12.2999"; enLineaIVA = #20Campo; |HAZ SacaIVA;
   |SI enLineaIVA < 0;
      |MENAV "    Control de cuentas no definido";
      |ERROR; |ACABA;
   |FINSI;

   |SI aTipoIVA = "S";
       #20#8 = enPorcIVA; |PINTA #20#8;
       #20#9 = enPorcREC; |PINTA #20#9;
   |SINO;
       #20#10 = enPorcIVA; |PINTA #20#10;
       #20#11 = enPorcREC; |PINTA #20#11;
   |FINSI;
|FCAL;

|CALCULO Tipo3m20; |TIPO 3;
   aUltPr = #20#2; aUltCl = #20#4;
   nUltPr = #20#3; nUltCl = #20#5;
|FCAL;

|PROCESO ClaveAlta0;
 #20#0 = "            ";
|FPRC;

|PROCESO ClaveBaja0;
 #20#0 = "zzzzzzzzzzzz";
|FPRC;

|PROCESO Tipo12u7; |TIPO 12;
 |SI #0#28 ! "A";
     |SI #0#28 = "C";
         |C_M #20#3, 1, "N";
         |C_M #20#5, 1, "N";
     |FINSI;
     |SI #0#28 = "I";
         |C_M #20#2, 1, "N";
         |C_M #20#4, 1, "N";
     |FINSI;
 |FINSI;

 enEjercicio = #0#26;
 |ENTLINEAL #1#20, -1, 2, 19, 0, ClaveAlta0, ClaveBaja0;

 |CONFI "    SRealizar ahora el Proceso ";
 |SI FSalida = 0;
     |HAZ Realiza;
 |FINSI;
|FPRC;

|| **************************************************************************
|| ********************* PROCESO PRINCIPAL
|PROCESO LeoFichas;
 aDni = #2#9; |QBF aDni;
 |SI aDni ! "";
     #20#0 = aDni;
     |LEE 001#20=;
     |SI FSalida = 0;
         eaCuenta = #20#2; nIVA = #20#3;
         |SI #2#23 = "C";
             eaCuenta = #20#4; nIVA = #20#5;
         |FINSI;
         |SI #0#28 ! "C"; #2#42 = nIVA; |FINSI; ||cambio todo o iva
         |SI #0#28 ! "I";                       ||cambio todo o contrapartida
             #2#21 = eaCuenta; |HAZ SacaNivel;
             #2#22 = enNivel;
         |FINSI;
         |GRABA 020#2a;
         |PINTA 2020, #2#10; |PINTA 2057, #2#23;
         |PINTA 2120, #2#1;
         |PINTA 2220, #2#9;  |PINTA 2245, #2#21; |PINTA 2264, #2#12;
     |FINSI;
 |FINSI;
 |LIBERA #2;
|FPRC;

|DEFBUCLE LeoFichas;
 #2#1;
 ;
 " ", "            ";
 "z", "zzzzzzzzzzzz";
 be;
 NULL, LeoFichas;
|FIN;

|PROCESO CopioaEmp;
    cod1 = #998#1;
    cod2 = #998#2;
    emEmp = 1;  || Sin mensaje
    |HAZ leelo;
    |SI swerror ! 0;  |ACABA;   |FINSI;

    eaDestino = #998#3;|QBF eaDestino;
    |PATH_DAT #1 eaDestino;
    |PATH_DAT #2 eaDestino;

    |PINTA 1920, #30#2;
    |PINTA 1927, #30#1;
    |PINTA 2010, " Cuenta :                Cliente / Proveedor :             ";
    |PINTA 2110, "                                                           ";
    |PINTA 2210, "                            Contr:               IVA:      ";

    |ABRE #20;
    |BUCLE LeoFichas;
    |CIERRA #20;
|FPRC;

|DEFBUCLE CopioaEmp;
 #998#1;
 ;
 000001;
 999999;
 ;
 NULL, CopioaEmp;
|FIN;

|| ********************* Procesos de seleccion
|PROCESO CalDirectorio;
  |DBASE dir_fich; |QBF dir_fich;
  dir_fich = dir_fich + "fich/";
  aAlfa1 = #998#1; |QBF aAlfa1;
  aAlfa2 = #998#2; |QBF aAlfa2;
  aAlfa1 = "00000" + aAlfa1;   aAlfa1 = aAlfa1 % -105;
  aAlfa2 = "0"     + aAlfa2;   aAlfa2 = aAlfa2 % -101;
  #998#3 = dir_fich + aAlfa1 + aAlfa2 + "/";

  nSwNoDir = 0;
  aAlfa1 = #998#3;   |QBF aAlfa1;
  |MKDIR aAlfa1;
  |SI FSalida = 0;
       nSwNoDir = 1;
       |RMDIR aAlfa1;
  |FINSI;
|FPRC;

|PROCESO ElTipoEmpresa;
    nSwTipo = 1;
    |SI Haybasica = 0; nSwTipo = 0; |ACABA; |FINSI;

    |ABRE #agifigen;
    #agifigen#0 = nLaEmpresa; || Empresa
    |LEE 041#agifigen.=;
    |SI FSalida ! 0;
        |DDEFECTO #agifigen;
        #agifigen#87 = 1;
    |FINSI;
    |CIERRA #agifigen;
    nPin1 = #agifigen#87;

    |SI aSelTipo = "S";
        |SI nPin1 ] nDTip; |Y nPin1 [ nHTip;
            nSwTipo = 0;
        |FINSI;
    |SINO;
        |SI nPin1 = nTip1; nSwTipo = 0; |FINSI;
        |SI nPin1 = nTip2; nSwTipo = 0; |FINSI;
        |SI nPin1 = nTip3; nSwTipo = 0; |FINSI;
        |SI nPin1 = nTip4; nSwTipo = 0; |FINSI;
        |SI nPin1 = nTip5; nSwTipo = 0; |FINSI;
        |SI nPin1 = nTip6; nSwTipo = 0; |FINSI;
        |SI nPin1 = nTip7; nSwTipo = 0; |FINSI;
        |SI nPin1 = nTip8; nSwTipo = 0; |FINSI;
        |SI nPin1 = nTip9; nSwTipo = 0; |FINSI;
        |SI nPin1 = nTip0; nSwTipo = 0; |FINSI;
    |FINSI;
|FPRC;

|PROCESO SelEjercicio;
  nSwEjer   = 0;

  |SI #0#25 = "T"; |ACABA; |FINSI;

  |SI #30#26 > 80;
       nEjer = #30#26 + 1900;
  |SINO;
       nEjer = #30#26 + 2000;
  |FINSI;

  |SI #0#25 = "="; |Y nEjer = #0#26; |ACABA; |FINSI;
  |SI #0#25 = "<"; |Y nEjer < #0#26; |ACABA; |FINSI;
  |SI #0#25 = ">"; |Y nEjer > #0#26; |ACABA; |FINSI;
  |SI #0#25 = "["; |Y nEjer [ #0#26; |ACABA; |FINSI;
  |SI #0#25 = "]"; |Y nEjer ] #0#26; |ACABA; |FINSI;

  nSwEjer = 1;
|FPRC;

|| ******************************************************************
|PROCESO Rellena;

   |SI aEstEmp ! "T";
       |SI #30#21 ! aEstEmp;  |ACABA; |FINSI;
   |FINSI;

   nLaEmpresa = #30#2;
   |HAZ ElTipoEmpresa;
   |SI nSwTipo = 1; |ACABA; |FINSI;

   |HAZ SelEjercicio;
   |SI nSwEjer = 1; |ACABA; |FINSI;

   |DDEFECTO #998;
   #998#0 = Linea;
   #998#1 = #30#2;
   #998#2 = #30#3;
   |HAZ CalDirectorio;
   |SI nSwNoDir = 0;
       |GRABA 020#998n;
       Linea = Linea + 1;
   |FINSI;
|FPRC;

|DEFBUCLE Rellena;
 #30#1;
 ;
 nDEmpresa, 0;
 nHEmpresa, 9;
 ;
 NULL, Rellena;
|FIN;

|PROCESO PorPlanilla;
 |ABRE #911;
 #911#0 = #0#27;
 |LEE 001#911=;
 |SI FSalida ! 0;
     |CIERRA #911;
     |ACABA;
 |FINSI;
 |CIERRA #911;

 aEstEmp  = #911#46;
 aSelTipo = #911#47;
 nDTip    = #911#48;
 nHTip    = #911#49;
 nTip1    = #911#50;
 nTip2    = #911#51;
 nTip3    = #911#52;
 nTip4    = #911#53;
 nTip5    = #911#54;
 nTip6    = #911#55;
 nTip7    = #911#56;
 nTip8    = #911#57;
 nTip9    = #911#58;
 nTip0    = #911#59;

 |SI #911#2 = "S";
     nDEmpresa = #911#3;
     nHEmpresa = #911#4;
     |BUCLE Rellena;
 |SINO;
     |PARA nPara1 = 5; |SI nPara1 [ 24; |HACIENDO nPara1 = nPara1 + 1;
           |SI #911nPara1 ! 0;
               nDEmpresa = #911nPara1;
               nHEmpresa = #911nPara1;
               |BUCLE Rellena;
           |FINSI;
     |SIGUE;
  |FINSI;
|FPRC;

|PROCESO Realiza;

  |DBASE aBass;  |QBT aBass;
  aBass  = aBass + "fich/";

  aFichero = ("u7" + Usuario) % 108;
  |NOME_DAT #998 aFichero;
  |DELINDEX #998;

  Linea = 1;
  |ABRE #998;
  |SI #0#27 ! 0;
      |HAZ PorPlanilla;
  |SINO;
       aEstEmp    = "T";
       aSelTipo   = "S";
       nDTip = 0; nHTip = 99;
       |SI #0#22 = "S";
            nDEmpresa = #0#23;
            nHEmpresa = #0#24;
            |BUCLE Rellena;
       |SINO;
           |PARA nPara1 = 1; |SI nPara1 [ 20; |HACIENDO nPara1 = nPara1 + 1;
                 |SI #0nPara1 ! 0;
                     nDEmpresa = #0nPara1;
                     nHEmpresa = #0nPara1;
                     |BUCLE Rellena;
                 |FINSI;
           |SIGUE;
       |FINSI;
  |FINSI;
  |CIERRA #998;

  nSiLanza = 0;
  |SI Linea = 1;
      |MENAV "    Seleccion Vacia";
  |SINO;

      |PUSHV 1809 2369;
      |PINTA 1910, " Empresa:                                                    ";
      |PINTA 2010, " Cuenta :                Cliente / Proveedor :              ";
      |PINTA 2110, "                                                            ";
      |PINTA 2210, "                            Contr:               IVA:       ";
      |CUADRO 1809 2369;

      |BUCLE CopioaEmp;
      |POPV;

      |CONFI "    SProceso Realizado. Desea conservar la Planilla ";
      |SI FSalida ! 0;
          nSiLanza = 1;
      |FINSI;
   |FINSI;
|FPRC;
|| **************************************************************************

|PROGRAMA;

   |HAZ inicio;
   |HAZ DirAplicSt;

   |CLS;
   |CABEZA "Informar Contrapartidas y Grupos IVA";

   |PINPA #0#0;
   |ABRE #0;
   |DDEFECTO #0;
   |LEE 101#0p;
   |SI FSalida ! 0;
      |GRABA 020#0b;
   |FINSI;
   #0#21 = "";
   #0#26 = enEjercicio;
   |PINDA #0#0;

   |ENTLINEAL #1#20, -1, 7, 19, 0, ClaveAlta0, ClaveBaja0;
   aUltPr = #20#2; aUltCl = #20#4;
   nUltPr = #20#3; nUltCl = #20#5;

   |ENDATOS #1#0;
   |SI FSalida = 0;
       |SI nSiLanza = 1;
           |DDEFECTO #0;
           |DELINDEX #20;
       |FINSI;
       |GRABA 020#0a;
   |FINSI;

   |LIBERA #0;
   |CIERRA #0;
|FPRO;
