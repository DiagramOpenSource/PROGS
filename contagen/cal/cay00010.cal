|FICHEROS;
   cay00010 #0;

   :/modelos/def/dsmom030 #03;        || conceptos
   :/integral/def/dsam0001 #40;
   camaasil #2;
   n43saldo #15;

   caw00027 #98;

   canempre #30;
   caselem1 #998;

   camaneof #10;

   catesfin #170;
|FIN;

|VARIABLES;
    &entrada = 1;
    informe  = "";
    inform1  = "cay00010";
    inform2  = "cayn0010";
    aBlancs  = "";
    aAlfa    = "";
    aAlfa1   = "";
    aAlfa2   = "";
    aNum1    = "";
    nNume0   = 0;
    nNume1   = 0;
    nNume2   = 0;
    nNume3   = 0;
    nTipo    = 0;

    aDCuenta = "";
    aHCuenta = "";
    nDConcepto = 0;
    nHConcepto = 0;
    fDFecha    = @;
    fHFecha    = @;
    nSw        = 0;
    nSwHay     = 0;
    nSwAlgun   = 0;
    nEstado    = 0;
    aFichero   = "";
    &nSwLinea  = 0;

    CodigoEmpresa  = 0;
    PeriodoEmpresa = 0;
    dirfichOri     = "";
    nPrint         = 0;

    nDiario        = 0;
    fFecha         = @;
    nDocumento     = 0;
    nApte          = 0;
    nConcepto      = 0;
    aDescripcion   = "";
    aSigno         = "";
    nImporte       = 0;
    aNomBanco      = "";
    nSwP           = 0;
|FIN;

|INCLUYE dscont_i;

|CALCULO defecto; |TIPO 7;
   #0#12 = fec1; |PINTA #0#12;
   #0#13 = fec2; |PINTA #0#13;
|FCAL;

|CALCULO cuenta;|TIPO 6;
   |C_M #0Campo, 0, aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   eaCuenta = #0Campo;
   salidas  = FSalida;
   |HAZ FiltraPuntos;
   #0Campo = eaCuenta; |PINTA #0Campo;
|FCAL;

|CALCULO SiDatos; |TIPO 0;
   aAlfa1 = #0Campo;

   nNume1 = Campo + 1; |C_M #0nNume1, 1, aAlfa1;
   |SI aAlfa1 = "N"; #0nNume1 = ""; |PINTA #0nNume1; |FINSI;

   nNume1 = Campo + 2; |C_M #0nNume1, 1, aAlfa1;
   |SI aAlfa1 = "N"; #0nNume1 = ""; |PINTA #0nNume1; |FINSI;
|FCAL;

|CALCULO HastaFecha;
   |SI #0#13 < #0#12; |Y #0#35 = "P";
       |MENAV "    Fecha Erronea";
       |ERROR;
   |FINSI;
|FCAL;

|CALCULO concepto; |TIPO 6;
   |SI #0#14 = 0;
       #0#15 = "Todos los conceptos";
   |SINO;
       |ABRE #3;
       #3#0 = #0#14;
       |LEE 041#3=;
       |SI FSalida ! 0;
           #0#15 = "";
           |MENAV "    No existe Concepto";
           |ERROR;
       |SINO;
           #0#15 = #3#1;
       |FINSI;
       |CIERRA #3;
   |FINSI;
   |PINTA #0#15;
|FCAL;

|CALCULO fechaUlt;
   |C_M #0#12, 1, "S";
   |SI #0#35 = "U";
       |C_M #0#12, 1, "N";
       #0#12 = fec1; |PINTA #0#12;
   |FINSI;
|FCAL;

|| *************************************************************************
||                       PROCESOS DEL EXTRACTO POR PANTALLA
|| *************************************************************************
|PROCESO NombreBanco;
   aNomBanco = "";
   |ABRE #10;
   #10#0 = #15#2;
   #10#1 = #15#3;
   |LEE 041#10=;
   |SI FSalida = 0;
       aNomBanco = #10#3;
   |FINSI;
   |CIERRA #10;
|FPRC;

|PROCESO GrabaAux;
       |DDEFECTO #98;
       #98#0  = #15#2;
       #98#1  = #15#3;
       #98#2  = #15#4;
       #98#3  = #15#5;
       #98#7  = enEmpresa;
       #98#14 = nDiario;
       #98#15 = fFecha;
       #98#16 = nDocumento;
       #98#17 = nApte;
       |LEE 041#98=;
       |SI FSalida ! 0;
           #98#0  = #15#2;
           #98#1  = #15#3;
           #98#2  = #15#4;
           #98#3  = #15#5;
           #98#7  = enEmpresa;
           #98#14 = nDiario;
           #98#15 = fFecha;
           #98#16 = nDocumento;
           #98#17 = nApte;
           |GRABA 020#98c;
       |FINSI;
       #98#4  = #15#6;
       #98#5  = #15#8;
       #98#6  = #15#9;
       #98#8  = eaNifEmp;
       #98#9  = eaNombreEmp;
       #98#10 = #15#0;
       #98#11 = fDFecha;
       #98#12 = fHFecha;
       #98#13 = aNomBanco;
       #98#18 = nConcepto;
       #98#19 = aDescripcion
       #98#20 = aSigno;
       #98#21 = nImporte;
       |GRABA 020#98a;
       |LIBERA #98;
       nSwAlgun = 1;
|FPRC;

|PROCESO PasoAuxW027;
       nSwHay = 1;
 |SI #0#14 ! 0;
       nDiario      = #2#0;
       fFecha       = #2#1;
       nDocumento   = #2#2;
       nApte        = #2#3;
       nConcepto    = #2#6;
       aDescripcion = #2#7;
       aSigno       = #2#8;
       nImporte     = #2#9;
       |HAZ GrabaAux;
 |FINSI;

       |SI fDFecha = "01.01.0000"; |O fDFecha > #2#2;
           fDFecha = #2#2;
       |FINSI;
       |SI fHFecha = "01.01.0000"; |O fHFecha < #2#2;
           fHFecha = #2#2;
       |FINSI;
|FPRC;

|DEFBUCLE PasoAuxW027;
     #2#3;
     6;
     #15#6, fDFecha, 000000, 0001, nDConcepto;
     #15#6, fHFecha, 999999, 9999, nHConcepto;
     e;
     NULL, PasoAuxW027;
|FIN;

|PROCESO PasoAuxiliar;
    |DDEFECTO #170;
    #170#0 = #15#6;
    |LEE 041#170=;
    |SI FSalida ! 0; |Y #0#34 ! "T";
        |ACABA;
    |FINSI;
    |SI #0#34 ! "T"; |Y #170#5 ! #0#34;
        |ACABA;
    |FINSI;

    fDFecha = #0#12;
    |SI #0#35 = "U";  fDFecha = #170#4; |FINSI;
    fHFecha = #0#13;
    |HAZ NombreBanco;

    |SI #0#14 ! 0;
        nSwHay = 0;
        |BUCLE PasoAuxW027;
    |FINSI;
    |SI #0#14 = 0; ||Y nSwHay = 1;
        nDiario     = 0;
        fFecha     = "01.01.2000";
        nDocumento = 0;
        nApte      = 1;
        |HAZ GrabaAux;
    |FINSI;
|FPRC;

|DEFBUCLE PasoAuxiliar;
     #15#2;
     8;
     aDCuenta, nTipo;
     aHCuenta, nTipo;
     ;
     NULL, PasoAuxiliar;
|FIN;

|PROCESO HazAuxiliar;

    |ABRE #98;
    |ABRE #170;
    |SI #0#0 = "S";
        aDCuenta = #0#1; aHCuenta = #0#2;
        nTipo    = 1;
        |BUCLE PasoAuxiliar;
    |FINSI;

    |SI #0#3 = "S";
        aDCuenta = #0#4; aHCuenta = #0#5;
        nTipo    = 2;
        |BUCLE PasoAuxiliar;
    |FINSI;

    |SI #0#6 = "S";
        aDCuenta = #0#7; aHCuenta = #0#8;
        nTipo    = 3;
        |BUCLE PasoAuxiliar;
    |FINSI;

    |SI #0#9 = "S";
        aDCuenta = #0#10; aHCuenta = #0#11;
        nTipo    = 4;
        |BUCLE PasoAuxiliar;
    |FINSI;

    |CIERRA #170;
    |CIERRA #98;
|FPRC;

|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   |SI #0#35 = "P";
       |SI #0#12 > fec2; |O #0#13 < fec1;
           |ACABA;  || Fuera de la seleccion
       |FINSI;
   |SINO;
       |SI #0#13 < fec1;
           |ACABA;  || Fuera de la seleccion
       |FINSI;
   |FINSI;

    dirfich0 = #caselem1#3; |QBF dirfich0;

    |PATH_DAT #2   dirfich0;
    |PATH_DAT #15  dirfich0;
    |PATH_DAT #170 dirfich0;
    |HAZ HazAuxiliar;
|FPRC;

|DEFBUCLE MultiEmp0;
    #caselem1#1;
    ;
    000001;
    999999;
    ;
    NULL,MultiEmp1;
|FIN;
|| //////////////////////////////////////////////////////////

|PROCESO LeeAuxiliar27;
   |SI nSw = 0;
       #0#29 = #98#0;
       #0#30 = #98#1;
       #0#31 = #98#13;
       #0#32 = #98#5;
       #0#33 = #98#6;
       nSw   = 1;
   |FINSI;
   |SI #0#29 ! #98#0; |O #0#30 ! #98#1;
       |SI nSwP = 0; nSwLinea = 2; |PRINT; |FINSI;
       nSwLinea = 0;
       #0#32 = #98#5;
       #0#33 = #98#6;
       |PIEINF;
       #0#29 = #98#0;
       #0#30 = #98#1;
       #0#31 = #98#13;
   |FINSI;
   |SI #0#32 ! #98#5;
       |SI nSwP = 0; nSwLinea = 2; |PRINT; |FINSI;
        nSwLinea = 0;
        #0#32 = #98#5;
        #0#33 = #98#6;
   |FINSI;
   |PRINT;
   nSwLinea = 1;
   nPrint   = 1;
|FPRC;

|DEFBUCLE LeeAuxiliar27;
    #98#2;
    ;
    "    ", "    ", 0, 00001, "          ", "01.01.0000";
    "zzzz", "zzzz", 9, 99999, "zzzzzzzzzz", "31.12.2999";
    ;
    NULL, LeeAuxiliar27;
|FIN;

|PROCESO Imprime;
     |IMPRE 0;
     |SI FSalida ! 0; |ACABA; |FINSI;

     informe = inform1;
     |SI #0#14 ! 0; informe = inform2;  |FINSI;
     aAlfa1 = Impresora;
     aAlfa1 = aAlfa1 % 113;
     |SI aAlfa1 = "CrystalReport";
         informe = "cc" + (informe % -106);
         nSwP = 1;
     |FINSI;

     |INFOR informe;
     |SI FSalida ! 0;
         |MENAV "    No existe Informe";
         |FINIMP;
         |ACABA;
     |FINSI;

     nSw = 0;
     |BUCLE LeeAuxiliar27;
     |SI nSw = 1;
         |PIEINF;
     |FINSI;
     |FININF;
     |FINIMP;
|FPRC;

|PROCESO Tipo3;
 || ... Campos de la Direccion
      aAlfa = #0#18; |QBF aAlfa;     || ... Direccion
      aNum1 = aAlfa % 0; nNume1 = aNum1;

      aAlfa = #0#20;   |QBF aAlfa;
      |SI aAlfa ! ""; aAlfa = "("+aAlfa+")"; |FINSI;
      #0#24 = aAlfa; aAlfa = #0#19;   |QBF aAlfa;
      #0#24 = aAlfa + " " + #0#24;

      aAlfa = #0#24; |QBF aAlfa;     || ... Poblacion
      aNum1 = aAlfa % 0; nNume2 = aNum1;

      aAlfa = #0#21; |QBF aAlfa;      || ... Telefono
      aAlfa = "Tfno.: " + aAlfa;
      aNum1 = aAlfa % 0; nNume3 = aNum1;

      nNume0 = nNume1; |SI nNume0 < nNume2; nNume0 = nNume2; |FINSI;
                       |SI nNume0 < nNume3; nNume0 = nNume3; |FINSI;
      |SI nNume0  < 40;
          nNume0  = (40 - nNume0);
          aBlancs = " " * nNume0;
          #0#25  = aBlancs + #0#18;
          #0#24  = aBlancs + #0#24;
          #0#26  = aBlancs + "Tfno.: " + #0#21;
          #0#27  = aBlancs + "       " + #0#22;
      |FINSI;
 ||.....................................................................
      CodigoEmpresa    = enEmpresa;
      PeriodoEmpresa   = enPeriodo;
      |DFICE dirfichOri;
      dirfich0 = dirfichOri;

      nDConcepto = #0#14; nHConcepto = #0#14;
      |SI #0#14 = 0; nDConcepto = 001; nHConcepto = 999; |FINSI;

      aFichero = "8v" + Usuario; |NOME_DAT #98  aFichero; |DELINDEX #98;

     nSwAlgun = 0;
     |BUCLE MultiEmp0;
     |SI nSwAlgun = 0;
         |MENAV "    Seleccion vacia";
     |SINO;
         |HAZ Imprime;
     |FINSI;
|FPRC;

|| *******************************************************************
|PROCESO LeeDespacho;
 |SI HayAcceso = 0; |ACABA; |FINSI;
 |ABRE #40;
 |LEE 101#40p;
 |SI FSalida = 0;
     #0#16 = #40#1;
     #0#17 = #40#2;
     #0#18 = #40#3;
     #0#19 = #40#4;
     #0#20 = #40#5;
     #0#21 = #40#6;
     #0#22 = #40#7;
 |FINSI;
 |CIERRA #40;
|FPRC;

|| *******************************************************************

|PROGRAMA;
     |HAZ inicio;
     CodigoEmpresa    = enEmpresa;
     PeriodoEmpresa   = enPeriodo;

     |CLS;
     |CABEZA "Carta Extractos Bancos";

     |SUB_EJECUTA_NP "caselemp";
     |NOME_DAT #998 eaFichsel;

     |ABRE #caselem1;
     |LEE 000#caselem1.p;
     |SI FSalida ! 0;
         |MENAV "    Seleccion vacia";
         |CIERRA #caselem1;
         |ACABA;
     |FINSI;

     cod1 = CodigoEmpresa;
     cod2 = PeriodoEmpresa;
     emEmp = 1;  || Sin mensaje
     |HAZ leelo;
     |HAZ DirAplicSt;

     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0;
         |DDEFECTO #0;
         |GRABA 020#0b;
     |FINSI;
     |HAZ LeeDespacho;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
         |HAZ Tipo3;
         |PDEFECTO #0, 28, 34;
         |GRABA 020#0a;
     |FINSI;
     |LIBERA #0;
     |CIERRA #0;
     |DELINDEX #998;
|FPRO;
