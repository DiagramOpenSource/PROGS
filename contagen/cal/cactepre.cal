|FICHEROS;
    cactepre #0;

    camaasic #02;
    camaasil #03;
    cacuenta #05;

    camandia #10;
    cacuebal #11;
    caconasi #20;

    canempre #30;
    imagen@  #330;

    caivaasi #200;
    cadeflab #210;

    camaniva #204;
    caivalin #205;
    calibros #206;

    :/basica/def/agifigen #100;
    :/basica/def/socm0003 #103;
    :/basica/def/socm0004 #104;
    socm004@  #199;
|FIN;

|VARIABLES;
     &entrada    = 1;
     x           = 0;
     nCampo      = 0;
     nSwTipo     = 0;
     nPin1       = 0;
     nHay        = 0;
     aFichero    = "";
     nDEmpresa   = 0;
     nHEmpresa   = 0;
     fDFecha     = @;
     fHFecha     = @;
     nDPrestamo  = 0;
     nHPrestamo  = 0;
     aAlfa1      = "";
     aAlfa2      = "";
     aAlfa3      = "";
     fFecha      = @;
     nPara1      = 0;
     nPara2      = 0;
     nNume1      = 0;
     nNume2      = 0;
     nNume3      = 0;
     nEjer       = 0;
     aPath       = "";
     nSwConta    = 0;
     nSw         = 0;
     nSwL        = 0;
     nSwR        = 0;
     nSwOp       = 0;
     nSwOpL      = 0;
     nSwOpR      = 0;
     nSwRegula   = 0;
     nImpRegul   = 0;
     nAlgun      = 0;
     fFecha1     = @;
     fFecha2     = @;
     &nEmpresa   = 0;
     &nConta      = 0;
     &fRegistro   = @;
     &fFecConta   = @;
     &nDiario     = 0;
     &nDocumento  = 0;
     &nImporte    = 0;
     &nPeriodo    = 0;
     &swIVA       = 0;
     &eaModif     = "";
     &nApunte     = 0;
     &nConcepto   = 0;
     &aDescrip    = "";
     &aCampo19    = "";
     &nCampo20    = 0;
     &nDebe       = 0;
     &nHaber      = 0;
     &aSigno      = "";
     &eaNomSocio  = "";
     &eaNifSocio  = "";
     &nBase       = 0;
     &nLibro      = 0;
     &nTipIRPF    = 0;

     aDirInicio  = "";
     aMas        = "";
     swImpresora = 0;
     aParam      = "";
     f1          = @;
     f2          = @;
     aEstado     = "";
     nError      = 0;

     aDCampo5    = "";
     aHCampo5    = "";
     aDCampo13   = "";
     aHCampo13   = "";

     &eaTituloL  = "";
     &men        = "";
     informe     = "cacte999";
     &eaNomFich  = "";

  CodigoEmpresa  = 0;
  PeriodoEmpresa = 0;
 &enEmpresaPINET = 0;
 &enErrorPINET   = 0;
|FIN;

|INCLUYE dscont_i;

|| *************************************************************************
||                        CALCULOS DE PANTALLA
|| *************************************************************************
|PROCESO Mayor;
     |C_M #0Campo, 0, aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     |SI #0Campo = "N";
         |PARA x = 5;  |SI x [ 24;  |HACIENDO x = x + 1;
               |C_M #0x,1,"S";
         |SIGUE;
         |C_M #0#3,1,"N"; #0#3 = 0; |PINTA #0#3;
         |C_M #0#4,1,"N"; #0#4 = 0; |PINTA #0#4;
     |SINO;
         |PARA x = 5;  |SI x [ 24;  |HACIENDO x = x + 1;
               |C_M #0x,1,"N"; #0x = 0; |PINTA #0x;
         |SIGUE;
         |C_M #0#3,1,"S";
         |C_M #0#4,1,"S";
     |FINSI;
|FPRC;

|CALCULO SelTipo0;
     |C_M #0Campo, 0, aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     |SI #0#38 = "N";
         #0#26 = 0;   |PINTA #0#26;  |C_M #0#26, 1, "N";
         #0#27 = 99;  |PINTA #0#27;  |C_M #0#27, 1, "N";
         |PARA nCampo = 28;  |SI nCampo [ 37;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |C_M #0#26, 1, "S";
         |C_M #0#27, 1, "S";
         |PARA nCampo = 28;  |SI nCampo [ 37;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO SelTipo1;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 37;  |HACIENDO nCampo = nCampo + 1;
               #0Campo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 37;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|PROCESO DesHas;
     |SI #0#4 < #0#3;
         |MENAV "      Error en Introduccion";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO SiModi; |TIPO 7;
     |PARA x = Campo + 1;  |SI x [ 24;  |HACIENDO x = x + 1;
           |C_M #0x,1,"S";
     |SIGUE;
|FPRC;

|PROCESO NoModi;
     |PARA x = Campo + 1;  |SI x [ 24;  |HACIENDO x = x + 1;
           |C_M #0x,1,"N"; #0x = 0; |PINTA #0x;
     |SIGUE;
|FPRC;

|PROCESO EsBuena; || desde campo de empresas seleccion
     |SI FSalida = 2; |ACABA; |FINSI;
     |SI #0Campo = 0; |HAZ NoModi;  |ACABA;  |FINSI;

     |ABRE #30;
     #30#2 = #0Campo;
     #30#3 = 0;
     |LEE 011#30];
     |SI FSalida ! 0;  |O #30#2 ! #0Campo;
         |MENAV "     NO Existe la Empresa Contable";
         |ERROR;
     |FINSI;
     |CIERRA #30;
|FPRC;

|CALCULO Fecha;
   |SI #0#41 > #0#42;
       |MENAV "    Error en la Introduccion";
       |ERROR;
   |FINSI;
|FCAL;

|CALCULO TipoPres;
     aAlfa1 = "S";
     |SI #0#52 = "E"; aAlfa1 = "N"; |FINSI;

     |C_M #0#48,1,aAlfa1;
     |C_M #0#49,1,aAlfa1;
     |C_M #0#50,1,aAlfa1;
|FCAL;

|CALCULO LaFecha; |TIPO 0;
   |C_M #0Campo,0,aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   aAlfa1 = "S";
   |SI #0#44 = "F"; aAlfa1 = "N"; |FINSI;
   |C_M #0#45,1,aAlfa1;
|FCAL;

|CALCULO LafechaP; |TIPO 0;
   |C_M #0Campo,0,aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   |SI aParam = "C";
       |SI #0Campo < fec1; |O #0Campo > fec2;
           |MENAV "    Fecha Erronea";
           |ERROR;
       |FINSI;
   |FINSI;
|FCAL;

|CALCULO TrasConcepto;
   |C_M #0Campo,0,aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   |SI FSalida = 2; |ACABA; |FINSI;

   |SI Campo = 49;
       enLibIVA   = #0#48;
       enFilConc  = 3;        || Concepto de IRPF
       enConcepto = #0Campo;
       |HAZ FiltroConcepto;  |SI eswLect ! 0; |ACABA; |FINSI;
   |FINSI;
|FCAL;

|CALCULO ElPtec; |TIPO 7;
    |PTEC 816;
|FCAL;

|CALCULO TrasLibro; |TIPO 6;
     |C_M #0Campo,0,aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     |ABRE #206;
     #206#0 = #0Campo;
     |LEE 041#206=;
     |SI FSalida ! 0;
         |MENAV "    No existe Libro";
         |ERROR;
     |SINO;
         |SI #206#2 ! "N";
             |MENAV "    ATENCION!!! Libro de I.V.A. ";
             |ERROR;
         |FINSI;
     |FINSI;
     |CIERRA #206;
|FCAL;

|| **********************************************************************
|| **********************************************************************
|PROCESO Contabiliza;

   |HAZ Multi_contador;
   nDiario    = #0#43;
   nLibro     = #0#48;
   nDocumento = nDOCUMENTO;

   enTipoAsto = 0;
   aTipoIVA   = "";
   nTipIRPF   = 0;
   nBase      = 0;
   fRegistro  = #104#3;
   |SI #103#5 = "A";
       enTipoAsto = 6;
       aTipoIVA = "N";
       nTipIRPF = #103#14;
   |FINSI;

   nDebe     = 0;
   nHaber    = 0;
   swIVA     = 0;

   nApunte   = 1;
   eaCuenta  = #103#16;   || Corto Plazo
   nImporte  = #104#4;    || Capital
   |SI #103#5 = "A";
       aSigno    = "D";
       nConcepto = #0#46;
       aDescrip  = #0#47;
       aCampo19  = "B";
       nCampo20  = 1;
       nBase    = #104#4;
   |FINSI;
   |SI #103#5 = "D";
       aSigno = "H";
       nConcepto = #0#54;
       aDescrip  = #0#55;
       aCampo19  = "";
       nCampo20  = 0;
   |FINSI;
   |HAZ Linea;

   |SI #104#5 ! 0;
       nApunte  = nApunte + 10;
       eaCuenta = #103#17;   || Intereses
       nImporte = #104#5;    ||
       |SI #103#5 = "A";
           aSigno    = "D";
           nConcepto = #0#46;
           aDescrip  = #0#47;
           aCampo19  = "B";
           nCampo20  = 2;
           nBase     = #104#5;
       |FINSI;
       |SI #103#5 = "D";
           aSigno = "H";
           nConcepto = #0#54;
           aDescrip  = #0#55;
           aCampo19  = "";
           nCampo20  = 0;
       |FINSI;
       |HAZ Linea;
   |FINSI;

   |SI #104#7 ! 0;
       nApunte  = nApunte + 10;
       eaCuenta = #103#19;   || IRPF
       nImporte = #104#7;    ||
       |SI #103#5 = "A";
           aSigno    = "H";
           nConcepto = #0#49;
           aDescrip  = #0#50;
           aCampo19  = "P";
           nCampo20  = 3;
           nBase     = #104#5;
       |FINSI;
       |SI #103#5 = "D";
           aSigno = "D";
           nConcepto = #0#54;
           aDescrip  = #0#55;
           aCampo19  = "";
           nCampo20  = 0;
       |FINSI;
       |HAZ Linea;
   |FINSI;

   nApunte  = nApunte + 10;
   eaCuenta = #103#18;   || Cargo / Abono
   aNOMCUENTA = eaNomSocio;
   nImporte = nDebe - nHaber;
   |SI #103#5 = "A";
       aSigno    = "H";
       nConcepto = #0#46;
       aDescrip  = #0#47;
       aCampo19  = "F";
       nCampo20  = 0;
   |FINSI;
   |SI #103#5 = "D";
       aSigno = "D";
       nImporte = - nImporte;
       nConcepto = #0#54;
       aDescrip  = #0#55;
       aCampo19  = "";
       nCampo20  = 0;
   |FINSI;
   |HAZ Linea;
   aNOMCUENTA = "";

   |SI nSwRegula = 1; |Y nImpRegul ! 0;
       nApunte  = nApunte + 10;
       eaCuenta = #103#15;   || Cargo / Abono
       nImporte = nImpRegul;
       nConcepto = #0#56;
       aDescrip  = #0#57;
       |SI #103#5 = "A";
           aSigno    = "D";
           aCampo19  = "X";
           nCampo20  = 0;
       |FINSI;
       |SI #103#5 = "D";
           aSigno = "H";
           aCampo19  = "";
           nCampo20  = 0;
       |FINSI;
       |HAZ Linea;

       nApunte  = nApunte + 10;
       eaCuenta = #103#16;   || Cargo / Abono
       nImporte = nImpRegul;
       nConcepto = #0#56;
       aDescrip  = #0#57;
       |SI #103#5 = "A";
           aSigno    = "H";
           aCampo19  = "X";
           nCampo20  = 0;
       |FINSI;
       |SI #103#5 = "D";
           aSigno = "D";
           aCampo19  = "";
           nCampo20  = 0;
       |FINSI;
       |HAZ Linea;
   |FINSI;

   nSwConta = 1;
|FPRC;
|| ///////////////////////////////////////////
|PROCESO LeeEspejo;
   nSwR    = 1;
   |SI nSwOpR = 0;
       |ERROR10;
       |ACABA;
   |FINSI;

   |SI nSwOpR = 1;
       nImpRegul = nImpRegul + #199#4;
   |FINSI;
|FPRC;

|DEFBUCLE LeeEspejo;
   #199#2003;
   ;
   #103#0, #103#1, fFecha1;
   #103#0, #103#1, fFecha2;
   ;
   NULL, LeeEspejo;
|FIN;

|PROCESO VerUltimo;  || Miro si es el ultimo regcibo del Ejercicio
   fFecha1 = #104#3;
   fFecha1 = fFecha1 + "01.00.0000";
   aAlfa1  = #104#3;
   aAlfa1  = aAlfa1 % -104;
   aAlfa1  = "31.12." + aAlfa1;
   fFecha2 = aAlfa1;
   nSwOpR = 0;
   nSwR    = 0;
   |BUCLE LeeEspejo;
   |SI nSwR = 0; nSwRegula = 1; |FINSI;
|FPRC;

|PROCESO BuscaImporte;

   nSwRegula = 1;
   nImpRegul = 0;
   aAlfa1    = #104#3; aAlfa1 = aAlfa1 % -104;
   nNume1    = aAlfa1; nNume1 = nNume1 + 1;
   |SI #103#13 = "A";
       aAlfa1 = nNume1; aAlfa1 = "01.01." + aAlfa1; fFecha1 = aAlfa1;
       aAlfa1 = nNume1; aAlfa1 = "31.12." + aAlfa1; fFecha2 = aAlfa1;
   |SINO;
       aAlfa1 = nNume1;
       aAlfa2 = #104#3; aAlfa2 = aAlfa2 % 105;

       |SI aAlfa2 = "29.02"; |O aAlfa2 = "28.02";
           |SI nNume1 = 2012; |O nNume1 = 2016; |O nNume1 = 2020; |O nNume1 = 2024;
                 |O nNume1 = 2028; |O nNume1 = 2032; |O nNume1 = 2036;
               aAlfa2 = "28.02." + aAlfa1; fFecha1 = aAlfa2;
               aAlfa2 = "29.02." + aAlfa1; fFecha2 = aAlfa2;
           |SINO;
               aAlfa2 = "28.02." + aAlfa1; fFecha1 = aAlfa2;
               aAlfa2 = "28.02." + aAlfa1; fFecha2 = aAlfa2;
           |FINSI;
       |SINO;
            aAlfa2 = aAlfa2 + "." + aAlfa1;
            fFecha1 = aAlfa2;
            fFecha2 = aAlfa2;
       |FINSI;

   |FINSI;
   nSwR   = 0;
   nSwOpR = 1;
   |BUCLE LeeEspejo;
   |SI nImpRegul = 0;
       nSwRegula = 0;
   |FINSI;
|FPRC;

|PROCESO PrestamosLineas;
   nSwL = 1;
   |SI nSwOpL = 0;
       |ERROR10;
   |FINSI;

   |SI nSwOpL = 1;

       |SI #0#44 = "F";  fFecConta = #104#3;  |FINSI;
       |SI fFecConta [ fec3;
           nError = 4; |HAZ GrabaErrores;
           |LIBERA #104;
           |ACABA;
       |FINSI;

       nSwRegula = 0;
       nImpRegul = 0;
       |SI #103#13 = "P";  nSwRegula = 1;  |FINSI;
       |SI #103#13 = "A";  |HAZ VerUltimo; |FINSI;
       |SI nSwRegula = 1;
           |HAZ BuscaImporte;
       |FINSI;

       |HAZ Contabiliza;
       #104#10 = "S";
       #104#11 = nDiario;
       #104#12 = fFecConta;
       #104#13 = nDocumento;
       #104#14 = nPeriodo;
       |GRABA 020#104a;
   |FINSI;
   |LIBERA #104;
|FPRC;

|DEFBUCLE PrestamosLineas;
   #104#2;
   10;
   #103#0, #103#1, fDFecha, "N";
   #103#0, #103#1, fHFecha, "N";
   be;
   NULL, PrestamosLineas;
|FIN;

|PROCESO PrestamosCab;

   nSwOpL = 0;
   nSwL   = 0;
   |BUCLE PrestamosLineas;
   |SI nSwL = 0; |ACABA; |FINSI;

   nSw = 1;
   |SI nSwOp  = 0;
       |ERROR10;
   |FINSI;

   |SI nSwOp = 1;

|| ... Chequeo de los datos para el Asiento
       |SI #103#16 = doce;    ||Cuenta C/P
           nError = 2; |HAZ GrabaErrores;
           |LIBERA #103;
           |ACABA;
       |FINSI;
       |SI #103#17 = doce;   ||Cuenta Intereses
           nError = 12; |HAZ GrabaErrores;
           |LIBERA #103;
           |ACABA;
       |FINSI;
       |SI #103#18 = doce;   ||Cuenta Cargo/Abono
           nError = 22; |HAZ GrabaErrores;
           |LIBERA #103;
           |ACABA;
       |FINSI;
       |SI #103#15 = doce; |Y #103#13 ! "N";  ||Cuenta L/P
           nError = 32; |HAZ GrabaErrores;
           |LIBERA #103;
           |ACABA;
       |FINSI;
       |SI #103#19 = doce; |Y #103#14 ! 0;   ||Cuenta IRPF
           nError = 42; |HAZ GrabaErrores;
           |LIBERA #103;
           |ACABA;
       |FINSI;

       eaNomSocio = #103#4;
       eaNifSocio = #103#3;
       nSwL   = 0;
       nSwOpL = 1;
       |BUCLE PrestamosLineas;
   |FINSI;
   |LIBERA #103;
|FPRC;

|DEFBUCLE PrestamosCab;
   #103#1;
   5;
   nEmpresa, #0#39, aDCampo5;
   nEmpresa, #0#40, aHCampo5;
   be;
   NULL, PrestamosCab;
|FIN;

|| /////////////////////////////////////////////////////
|PROCESO PorEmpContable;
   #30#11 = #330#11;
   #30#12 = #330#12;
   #30#26 = #330#26;
   |HAZ CalLaFecha;
   |SI fec2 < #0#41; |ACABA; |FINSI;
   |SI fec1 > #0#42; |ACABA; |FINSI;

   nPeriodo = #330#3;
   nConta = 1;
   |HAZ SituaEmpresa;
   |SI nConta = 1;
       fDFecha = fec1; |SI fDFecha < #0#41; fDFecha = #0#41; |FINSI;
       fHFecha = fec2; |SI fHFecha > #0#42; fHFecha = #0#42; |FINSI;

       nSwOp    = 1;
       nSwConta = 0;
       |ABRE #2;
       |ABRE #3;
       |ABRE #204;
       |ABRE #205;
       |BUCLE PrestamosCab;
       |CIERRA #205;
       |CIERRA #204;
       |CIERRA #3;
       |CIERRA #2;
       nAlgun = 1;
   |FINSI;
|FPRC;

|DEFBUCLE PorEmpContables;
   #330#1002;
   ;
   nEmpresa, 0;
   nEmpresa, 9;
   e;
   NULL, PorEmpContable;
|FIN;
|| ///////////////////////////////////////////////////////////

|PROCESO GrabaErrores;
  eaTituloL = "PRESTAMOS DE SOCIOS";
  |SI swImpresora = 0;
      swImpresora = 1;
      |IMPRE 0;
      |SI FSalida ! 0;
          |MENAV "    Error en la Impresora";
          swImpresora = 2;
          |ACABA;
      |FINSI;
      |INFOR informe;
      |SI FSalida ! 0;
          swImpresora = 3;
          |MENAV "     Error en el Informe";
          |FINIMP;
          |ACABA;
      |FINSI;
 |FINSI;
 |SI swImpresora = 1;
     |SI nError = 1;  men = "No existe Ejercicio " + enEjercicio; |FINSI;
     |SI nError = 2;
         men = "Cuenta Corto/Plazo en Blanco en Prestamo " + #103#1 + " " + #103#2;
     |FINSI;
     |SI nError = 12;
         men = "Cuenta Intereses en Blanco en Prestamo " + #103#1 + " " + #103#2;
     |FINSI;
     |SI nError = 22;
         men = "Cuenta Cargo/Abono en Blanco en Prestamo " + #103#1 + " " + #103#2;
     |FINSI;
     |SI nError = 32;
         men = "Cuenta Largo/Plazo en Blanco en Prestamo " + #103#1 + " " + #103#2;
     |FINSI;
     |SI nError = 42;
         men = "Cuenta IRPF en Blanco en Prestamo " + #103#1 + " " + #103#2;
     |FINSI;

     |SI nError = 3; men = "Fecha de Bloqueo " + fec3; |FINSI;

     |SI nError = 4;
         men = "Fecha de Bloqueo " + fec3 + " y Prestamo " + #104#1 + " fecha " + #104#3;
     |FINSI;
     |PRINT;
 |FINSI;
|FPRC;

|| ///////////////////////////////////////////////////////////

|PROCESO LaEmpresa;

    nSwTipo = 1;
    nPin1 = #agifigen#87;
    |SI #0#38 = "S";
        |SI nPin1 ] #0#26; |Y nPin1 [ #0#27;
            nSwTipo = 0;
        |FINSI;
    |SINO;
        |SI nPin1 = #0#28;
            nSwTipo = 0;
        |SINO;
            |PARA nCampo = 29;  |SI nCampo [ 37;  |HACIENDO nCampo = nCampo + 1;
                  |SI nPin1 = #0nCampo; |Y nPin1 ! 0;
                      nSwTipo = 0;
                      |SAL;
                  |FINSI;
            |SIGUE;
        |FINSI;
    |FINSI;
    |SI nSwTipo = 1; |ACABA; |FINSI;

    aEstado = "A";
    |SI #100#94 > "01.01.0000"; |Y #100#94 [ #0#39;
        aEstado = "I";
    |FINSI;
    |SI aEstado ! #0#25; |Y #0#25 ! "T";
        |ACABA;
    |FINSI;

|| ===============================================================
    nEmpresa = #100#0;
    fDFecha  = #0#41;
    fHFecha  = #0#42;
    nSw   = 0;
    nSwOp = 0;
    |BUCLE PrestamosCab;
    |SI nSw = 1;          || si que hay lineas de prestamos
        nHay = 1;
        |SI #0#44 = "P";      || Solo un periodo
            fDFecha   = #0#41;
            fHFecha   = #0#42;
            fFecConta = #0#45;
            |HAZ BuscoPeriodo;
            |SI nConta = 0;
                nError = 1; |HAZ GrabaErrores;
                |ACABA;
            |FINSI;
            |SI fFecConta [ fec3;
                nError = 3; |HAZ GrabaErrores;
                |ACABA;
            |FINSI;
            nSwOp    = 1;
            nSwConta = 0;
            |ABRE #2;
            |ABRE #3;
            |ABRE #204;
            |ABRE #205;
            |BUCLE PrestamosCab;
            |CIERRA #205;
            |CIERRA #204;
            |CIERRA #3;
            |CIERRA #2;
        |SINO;
            nAlgun = 0;
            |BUCLE PorEmpContables;
            |SI nAlgun = 0;
                nError = 1; |HAZ GrabaErrores;
            |FINSI;
        |FINSI;
    |FINSI;
|FPRC;

|DEFBUCLE agifigen;
     #100#1;
     ;
     nDEmpresa;
     nHEmpresa;
     e;
     NULL, LaEmpresa;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     aDCampo5 = " "; aHCampo5 = "Z";
     |SI #0#52 = "E"; aDCampo5 = "D"; aHCampo5 = "D"; |FINSI;
     |SI #0#52 = "S"; aDCampo5 = "A"; aHCampo5 = "A"; |FINSI;

     eaModif = #0#51;
     swImpresora = 0;
     nHay = 0;
     |SI #0#2 = "S";
         nDEmpresa = #0#3;
         nHEmpresa = #0#4;
         |BUCLE agifigen;
     |SINO;
         |PARA nPara1 = 5; |SI nPara1 [ 24; |HACIENDO nPara1 = nPara1 + 1;
               |SI #0nPara1 ! 0;
                   nDEmpresa = #0nPara1;
                   nHEmpresa = #0nPara1;
                   |BUCLE agifigen;
               |FINSI;
         |SIGUE;
     |FINSI;

     |SI nHay = 0;
         |MENAV "    Seleccion vacia";
     |SINO;
         |SI swImpresora = 1;
             |PIEINF;
             |FININF;
             |FINIMP;
         |FINSI;
     |FINSI;
|FPRC;

|PROGRAMA;
     aParam = PARAMETRO;

     |HAZ inicio;
     |HAZ DirAplicSt;
     CodigoEmpresa  = enEmpresa;
     PeriodoEmpresa = enPeriodo;

     aMas  = aPGenral + "def/socm0004";
     |CARGA_DEF aMas, socm004@;
     |SI FSalida ! 0;
         |MENAV "    Error Cargado Fichero de Basica. Proceso Interrumpido";
         |ACABA;
     |FINSI;

     aPath = aPGenral + "fich/";
     |PATH_DAT #103 aPath;
     |PATH_DAT #104 aPath;
     |PATH_DAT #199 aPath;

     fFecha   = ~;
     aAlfa1   = fFecha;
     aAlfa1   = aAlfa1 % -104;
     nEjer    = aAlfa1;
     aAlfa1   = "01.01." + nEjer; f1 = aAlfa1;
     aAlfa2   = "31.12." + nEjer; f2 = aAlfa2;

     |DBASE aDirInicio;
     aMas  = aDirInicio + "def/canempre";
     |CARGA_DEF aMas, imagen@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #socm004@;
         |MENAV "    Error Cargado Fichero de Contabilidad. Proceso Interrumpido";
         |ACABA;
     |FINSI;

     |CLS;
     |ABRE #0;
     #0#0 = 0;
     |LEE 141#0=;
     |SI FSalida ! 0;
         |DDEFECTO #0;
         #0#0 = 0;
         #0#1 = nEjer;
         |SI nEmpresa ! 0;
             #0#3 = nEmpresa;
             #0#4 = nEmpresa;
         |FINSI;
         #0#41 = f1;
         #0#42 = f2;
         |GRABA 020#0b;
     |FINSI;

     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
         |GRABA 020#0a;
     |FINSI;
     |LIBERA #0;
     |CIERRA #0;

     |DESCARGA_DEF #socm004@;
     |DESCARGA_DEF #imagen@;
|FPRO;
