|FICHEROS;
   camoauxz #0;
   camoauxc #1;
   camoauxl #2;
   cacuenta #3;

   inventc@ #11;
   inventl@ #12;

   canempre #30;
|FIN;

|VARIABLES;
  aCampo2 = "";
  nCampo3 = 0;
  nPara1  = 0;
  swSi    = 0;
  &r_emp  = 0;
  &r_per  = 0;
  &r_eje  = 0;
  &r_nom  ="";
  &aEstru  = "";
  &swUno  = 0;
  aAlfa1 = "";
  aAlfa2 = "";
  aAlfa3 = "";
  aAlfa4 = "";
  aPath  = "";
  nNume1 = 0;
  nInkey = 0;

  &enDesglose = 0;
|FIN;

|INCLUYE dscont_i;

||---------------------------------------------
|CALCULO LosDefectos; |TIPO 7;
 |SI swUno ! 0;
     |C_M #0#0, 1, "N";
     |C_M #0#7, 1, "N";
     #0#7 = aEstru;
 |FINSI;
 #0#0 = enEmpresa;
 |SI swUno ! 2;
     #0#1 = enPeriodo - 1; |SI #0#1 < 0; #0#1 = 9; |FINSI;
     #0#2 = #30#26 - 1;    |SI #0#2 < 0; #0#2 = 99; |FINSI;
 |SINO;
     #0#1 = enPeriodo;
     #0#2 = #30#26;
 |FINSI;
 #0#3 = eaNombreEmp;
 |PINTA #0#0;
 |PINTA #0#1;
 |PINTA #0#2;
 |PINTA #0#3;
 |PINTA #0#7;
|FCAL;

|CALCULO esbuena;  |TIPO 0;
  |SI FSalida = 9;
      r_emp = 0;
      |SUB_EJECUTA "caconemp";
      |ERROR;
      #0#0 = r_emp;
      #0#1 = r_per;
      |PTEC 802;
      |PTEC 802;
      |ACABA;
  |FINSI;
  |ABRE #30;
  #30#2 = #0#0;
  #30#3 = 0;
  |LEE 011#30];
  |SI FSalida ! 0;  |O #30#2 ! #0#0;
      |MENAV "     NO Existe la empresa Contable";
      |ERROR;
  |FINSI;
  |CIERRA #30;
|FCAL;

|CALCULO esbuena1; |TIPO 0;
  |SI FSalida = 2; |ACABA; |FINSI;
  |SALVA_DATOS #30;
  |ABRE #30;
  #30#2 = #0#0;
  #30#3 = #0#1;
  |LEE 011#30=;
  |SI FSalida ! 0;
      |MENAV "     NO Existe Periodo en la empresa Contable";
      |CIERRA #30;
      |ERROR;
      |ACABA;
  |FINSI;
  |CIERRA #30;
  #0#2 = #30#26; |PINTA #0#2;
  #0#3 = #30#1;  |PINTA #0#3;
  aMonedaC = #30#28;
  |REPON_DATOS #30;
|FCAL;

|CALCULO estaono; |TIPO 0;

  |DBASE aAlfa1; |QBF aAlfa1;
  aAlfa2 = aAlfa1 + "def/camoauxc.mas";
  |CARGA_DEF aAlfa2, inventc@;
  |SI FSalida = 0;
      aAlfa3 = #0#0; |QBF aAlfa3; aAlfa3 = ("00000" + aAlfa3) % -105;
      aAlfa4 = #0#1; |QBF aAlfa4; aAlfa4 = ("0"     + aAlfa4) % -101;
      aPath  = aAlfa1 + "fich/" + aAlfa3 + aAlfa4 + "/";
      |PATH_DAT #11 aPath;

      |ABRE #11;
      #11#6 = #0#4;
      #11#0 = doce;
      |LEE 040#11];
      |SI FSalida ! 0; |O #11#6 ! #0#4;
          |MENAV "      No existe Inventario ORIGEN";
          |ERROR;
      |FINSI;
      |CIERRA #11;
      |DESCARGA_DEF #inventc@;
  |FINSI;
|FCAL;

|CALCULO cuenta; |TIPO 6;
     eaCuenta = #camoauxz#Campo#;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #camoauxz#Campo# = eaCuenta;
     |PINTA #camoauxz#Campo#;
|FCAL;

|CALCULO LaConsulta; |TIPO 0;
 nInkey = FSalida;
 |SI nInkey = 10;
    eOpDep = 1; || 1 = Consulta pantalla
    enActividad = #0#7;
    eaCodDep    = #0#7;
    |HAZ Actividad;
 |FINSI;
|FCAL;

|CALCULO noestaW; |TIPO 0;
   |SI #0#0 = enEmpresa; |Y #0#0 = enPeriodo;
       |SI #0#4 = #0#7;
           |MENAV "    ERROR. Esta introducciendo el mismo Codigo de Inventario";
           |ERROR;
           |ACABA;
       |FINSI;
   |FINSI;

   |ABRE #1;
   #1#6 = #0#7;
   #1#0 = doce;
   |LEE 040#1];
   |SI FSalida = 0; |Y #1#6 = #0#7;
      |CONFI  "    NEl Inventario YA Existe. Continuar S/N ";
      |SI FSalida ! 0;
          |ERROR;
          |CIERRA #1;
          |ACABA;
      |FINSI;
   |FINSI;
   |CIERRA #1;

   |SI eaDepar = "N"; |ACABA; |FINSI;

   eOpDep = 0; || 1 = Consulta pantalla
   enActividad = #0#7;
   eaCodDep    = #0#7;
   |HAZ Actividad;
   |SI eswLect = 0;
       #0#8 = eaNombreAct;  |PINTA #0#8;
   |SINO;
      |MENAV "    NO EXISTE ACTIVIDAD ";
      |ERROR;
  |FINSI;
|FCAL;

|| *************************************************
|PROCESO CopioLineas1;
  #2#0 = #1#0;
  #2#1 = #1#1;
  #2#2 = #12#2;
  #2#3 = #12#3;
  #2#4 = #12#4;
  impMoneda = #12#5;
  |SI aMonedaC ! aMonedaA;
      |HAZ ElCambioMoneda;
  |FINSI;
  #2#5 = impMoneda;
  #2#6 = (#2#4 * #2#5) ! EURODB;
  #2#7 = #1#6;
  #1#3 = #1#3 + #2#6;
  |GRABA 040#2n;
|FPRC;

|DEFBUCLE CopioLineas;
 #12#1003;
 ;
 #11#6, #11#0, 001;
 #11#6, #11#0, 999;
 be;
 NULL, CopioLineas1;
|FIN;


|PROCESO LosCopio1;

 emCta    = 0;
 #1#0     = #11#0;
 eaCuenta = #1#0; |HAZ SacaNivel;
 |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea, no la paso
 #1#1 = enNivel;
 |HAZ LeeCuenta;
 |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea, no la paso

 #1#2 = #3#2;
 #1#3 = 0;
 #1#4 = #3#53;
 #1#6 = #0#7;

 |BUCLE CopioLineas;

 #1#5 = #1#4 - #1#3;
 |GRABA 020#1n;
|FPRC;

|DEFBUCLE LosCopio;
 #11#1002;
 ;
 #0#4, #0#5;
 #0#4, #0#6;
 e;
 NULL, LosCopio1;
|FIN;

|| ////////////////////////////////////////////////////////

|PROCESO BorroLineas1;
 |BORRA 020#2a;
 |LIBERA #2;
|FPRC;

|DEFBUCLE BorroLineas;
 #2#1;
 ;
 #1#6, #1#0, 001;
 #1#6, #1#0, 999;
 be;
 NULL, BorroLineas1;
|FIN;

|PROCESO LosBorro1;
 |BUCLE BorroLineas;

 |BORRA 020#1a;
 |LIBERA #1;
|FPRC;

|DEFBUCLE LosBorro;
 #1#1;
 ;
 #0#7, #0#5;
 #0#7, #0#6;
 be;
 NULL, LosBorro1;
|FIN;



|CALCULO pasalo; |TIPO 3;

  |DBASE aAlfa1; |QBF aAlfa1;
  aAlfa3 = #0#0; |QBF aAlfa3; aAlfa3 = ("00000" + aAlfa3) % -105;
  aAlfa4 = #0#1; |QBF aAlfa4; aAlfa4 = ("0"     + aAlfa4) % -101;
  aPath  = aAlfa1 + "fich/" + aAlfa3 + aAlfa4 + "/";

  aAlfa2 = aAlfa1 + "def/camoauxc.mas";
  |CARGA_DEF aAlfa2, inventc@;
  |SI FSalida = 0;
      aAlfa2 = aAlfa1 + "def/camoauxl.mas";
      |CARGA_DEF aAlfa2, inventl@;
      |SI FSalida = 0;

          |PATH_DAT #11 aPath;
          |PATH_DAT #12 aPath;

          |BUCLE LosBorro;

          |ABRE #1;
          |ABRE #2;
          |BUCLE LosCopio;
          |CIERRA #2;
          |CIERRA #1;

          |DESCARGA_DEF #inventl@;
      |FINSI;
      |DESCARGA_DEF #inventc@;
  |FINSI;
|FCAL;

|PROGRAMA;
 |HAZ inicio;
 aMonedaA = eaMoneda;
 |HAZ DirAplicSt;

 |SI swUno = 0;
     |CLS;
     |MANTE #0;
 |SINO;
     |PINPA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
         |HAZ pasalo;
     |FINSI;
 |FINSI;
|FPRO;


|| ..........................................................................
