|FICHEROS;
   caivaasi;
   caivaaxx,MANTE;
   caivaax1,MANTE;

   :/modelos/def/dsmom030;

   cafantas;

   cacuenta #4;
   calibros #10;
   caivases #11;
|FIN;

|VARIABLES;
   &entrada  = 1;
   &entrada1 = 1;

   aAlfa  = "";
   alfa1  = "";
   alfa2  = "";
   alfa3  = "";
   nNume1  = 0;
   nPara1  = 0;

   ConcepAnt = 0;
   nInkey = 0;

   nSwP = 0;
   swS = 0;
   swF = 0;
   swC = 0;
   swN = 0;
   swR = 0;
   swT = 0;

   aSubAnt = "";

   aParam      = "";
   ModoEntrada = 0;
   aNumCampos  = "";
   nNumCampos  = 0;
   aQueQuiero  = "";
   aIDCampo    = "";
|FIN;

|INCLUYE dscont_i;
|INCLUYE modulo_i;

|| ==================== Calculos Pantalla
|CALCULO libro; |TIPO 0;
   |DDEFECTO #10;
   |ABRE #10;
   #10#0 = #caivaax1#Campo#;
   |LEE 000#10=;
   |CIERRA #10;

   |SI Campo = 3;
       |SI #10#2 ! "R";
           |MENAV "    ATENCION!!! Libro de I.V.A. Soportado ...";
           |ERROR;
       |FINSI;
   |SINO;
       |SI #10#2 ! "S";
           |MENAV "    ATENCION!!! Libro de I.V.A. Repercutido ...";
           |ERROR;
       |FINSI;
   |FINSI;
|FCAL;

|CALCULO ConcepAnt; |TIPO 7;
   ConcepAnt = #caivaax1#Campo#;
   |SI Campo = 36;
       #caivaax1#36 = #caivaax1#28; |PINTA #caivaax1#36;
   |FINSI;
   |SI Campo = 13;
       #caivaax1#13 = #caivaax1#5; |PINTA #caivaax1#13;
   |FINSI;
|FCAL;

|CALCULO concepto;  |TIPO 0; || Filtros Conceptos
  nInkey = FSalida;
  |C_M #caivaax1#Campo#,0,alfa1;
  |SI alfa1 = "N"; |ACABA; |FINSI;

  |ABRE #dsmom030;
  #dsmom030#0 = #caivaax1#Campo#;
  |LEE 041#dsmom030.=;
  |SI FSalida ! 0;  |DDEFECTO #dsmom030;  |FINSI;
  |CIERRA #dsmom030;

  |SI #dsmom030#4 = "N";
      |SI #caivaasi#95 ! "X";
          |MENAV "    Error. Este Concepto no es de IVA";
      |FINSI;
      |SI #caivaasi#95 = "S";
          |ERROR;
      |FINSI;
      |ACABA;
  |FINSI;

  |SI Campo = 5; |O Campo = 13;   || Iva Repercutido
      |SI #caivaax1#3 ! #dsmom030#12;
          |SI #caivaasi#95 ! "X";
              |MENAV "    Error. Este Concepto no pertenece al Libro de IVA Repercutido";
          |FINSI;
          |SI #caivaasi#95 = "S";
              |ERROR;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;
  |SI Campo = 28; |O Campo = 36;   || Iva Soportado
      |SI #caivaax1#26 ! #dsmom030#14;
          |SI #caivaasi#95 ! "X";
              |MENAV "    Error. Este Concepto no pertenece al Libro de IVA Soportado";
          |FINSI;
          |SI #caivaasi#95 = "S";
              |ERROR;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;
|FCAL;

|CALCULO recoge; |TIPO 7;
   |C_M #caivaax1#Campo#,0,alfa1;
   |SI alfa1 = "N"; |ACABA; |FINSI;

   nNume1 = Campo - 2;
   |SI #caivaax1#nNume1# ! ConcepAnt;
       |ABRE #dsmom030;
       #dsmom030#0 = #caivaax1#nNume1#;
       |LEE 041#dsmom030.=;
       |SI FSalida ! 0;  |DDEFECTO #dsmom030;  |FINSI;
       #caivaax1#Campo# = #dsmom030#1;
       |PINTA #caivaax1#Campo#;
       |CIERRA #dsmom030;
       |PTEC 816;
   |FINSI;
|FCAL;

|CALCULO cuenta; |TIPO 6;
  |C_M #caivaax1#Campo#,0,alfa1;
  |SI alfa1 = "N"; |ACABA; |FINSI;

  eaCuenta = #caivaax1#Campo#;
  salidas  = FSalida;
  |HAZ FiltraPuntos;
  #caivaax1#Campo# = eaCuenta;
  |PINTA #caivaax1#Campo#;

  emCta = 1;
  eaCuenta = #caivaax1#Campo#;
  |HAZ SacaNivel;
|FCAL;

|CALCULO AntSerie; |TIPO 7;
 |C_M #caivaax1#Campo#,0,alfa1;
 |SI alfa1 = "N"; |ACABA; |FINSI;
  #caivaax1#75 = #caivaax1#63; |PINTA #caivaax1#75;
  #caivaax1#76 = #caivaax1#64; |PINTA #caivaax1#76;
|FCAL;

|CALCULO Serie; |TIPO 0;
   |DDEFECTO #11;
   |ABRE #11;
   #11#0 = #caivaax1#Campo#;
   |SI Campo = 65; #11#1 = "S"; |SINO; #11#1 = "R"; |FINSI;
   |LEE 000#11=;
   |SI FSalida ! 0;
       |DDEFECTO #11;
       #11#0 = #caivaax1#Campo#;
       |SI Campo = 65; #11#1 = "S"; |SINO; #11#1 = "R"; |FINSI;
       |GRABA 020#11n;
   |FINSI;
   |CIERRA #11;
|FCAL;

|CALCULO Quepongo7; |TIPO 7;
  nSwP = 0;
  |HAZ EsComersan;
  |SI FSalida = 0;  nSwP = 1; |FINSI;
  |SI Campo = 35; alfa1 = "    A¤adir en conceptos Astos Cl/Pr: S-erie,F-actura, C-Cta Ctble.,N-ombre,R-ef."; |FINSI;
  |SI Campo = 64; alfa1 = "    A¤adir en concepto Asto de IRPF: S-erie,F-actura, C-Cta Ctble.,N-ombre,R-ef."; |FINSI;
  |SI Campo = 74; alfa1 = "    Astos Base/IVA: S-erie,F-actura, C-Cta Ctble.,N-ombre,R-ef., =Igual Cl/Pr ";   |FINSI;
  |SI nSwP = 1;
      |SI Campo = 35; alfa1 = "    Astos Cl/Pr: S-erie,F-actura, C-Cta Ctble.,N-ombre,R-ef.,P-Ref Prov./Ref"; |FINSI;
      |SI Campo = 64; alfa1 = "    Asto de IRPF: S-erie,F-actura, C-Cta Ctble.,N-ombre,R-ef.,P-Ref Prov./Ref"; |FINSI;
      |SI Campo = 74; alfa1 = "    Astos Base/IVA: S-erie,F-Fra., C-Cta,N-ombre,R-ef.,P-Ref Prv, =Igual Cl/Pr";|FINSI;
  |FINSI;
  |MENSA alfa1;
|FCAL;

|CALCULO Quepongo; |TIPO 0;

 alfa1 = #caivaax1#Campo#; |QBF alfa1;
 |SI alfa1 = ""; |ACABA; |FINSI;

 swS = 0; swF = 0;
 swC = 0; swN = 0;
 swR = 0;

 alfa1 = #caivaax1#Campo# % 101;
 alfa2 = #caivaax1#Campo# % 201;
 alfa3 = #caivaax1#Campo# % 301;

 |SI Campo = 74; |Y #caivaax1#Campo# = "=  ";
     |ACABA;
 |FINSI;

 |SI alfa1 ! " ";
     swT = 0;
     |SI alfa1 = "S"; swS = 1; swT = 1; |FINSI;
     |SI alfa1 = "F"; swF = 1; swT = 1; |FINSI;
     |SI alfa1 = "C"; swC = 1; swT = 1; |FINSI;
     |SI alfa1 = "N"; swN = 1; swT = 1; |FINSI;
     |SI alfa1 = "R"; swR = 1; swT = 1; |FINSI;
     |SI alfa1 = "P"; |Y nSwP = 1;
         |SI Campo = 35; |O Campo = 64; |O Campo = 74;
             swR = 1; swT = 1;
         |FINSI;
     |FINSI;
     |SI swT ! 1; |ERROR; |ACABA; |FINSI;
 |FINSI;
 |SI alfa2 ! " ";
     swT = 0;
     |SI alfa2 = "S"; |Y swS = 0; swS = 1; swT = 1; |FINSI;
     |SI alfa2 = "F"; |Y swF = 0; swF = 1; swT = 1; |FINSI;
     |SI alfa2 = "C"; |Y swC = 0; swC = 1; swT = 1; |FINSI;
     |SI alfa2 = "N"; |Y swN = 0; swN = 1; swT = 1; |FINSI;
     |SI alfa2 = "R"; |Y swR = 0; swR = 1; swT = 1; |FINSI;
     |SI alfa2 = "P"; |Y nSwP = 1;
         |SI Campo = 35; |O Campo = 64; |O Campo = 74;
             swR = 1; swT = 1;
         |FINSI;
     |FINSI;
     |SI swT ! 1; |ERROR; |ACABA; |FINSI;
 |FINSI;
 |SI alfa3 ! " ";
     swT = 0;
     |SI alfa3 = "S"; |Y swS = 0; swS = 1; swT = 1; |FINSI;
     |SI alfa3 = "F"; |Y swF = 0; swF = 1; swT = 1; |FINSI;
     |SI alfa3 = "C"; |Y swC = 0; swC = 1; swT = 1; |FINSI;
     |SI alfa3 = "N"; |Y swN = 0; swN = 1; swT = 1; |FINSI;
     |SI alfa3 = "R"; |Y swR = 0; swR = 1; swT = 1; |FINSI;
     |SI alfa3 = "P"; |Y nSwP = 1;
         |SI Campo = 35; |O Campo = 64; |O Campo = 74;
             swR = 1; swT = 1;
         |FINSI;
     |FINSI;
     |SI swT ! 1; |ERROR; |ACABA; |FINSI;
 |FINSI;

 |SI Campo = 35;
     |SI #caivaax1#64 = "   ";
         #caivaax1#64 = #caivaax1#35; |PINTA #caivaax1#64;
     |FINSI;
     |SI #caivaax1#74 = "   ";
         #caivaax1#74 = #caivaax1#35; |PINTA #caivaax1#74;
     |FINSI;
 |FINSI;
|FCAL;

|CALCULO serie0; |TIPO 0;
  alfa1 = #caivaasi#44;
  |C_M #caivaax1#42,1,alfa1;
  |C_M #caivaax1#43,1,alfa1;
  |C_M #caivaax1#44,1,alfa1;
  |C_M #caivaax1#45,1,alfa1;
  |SI alfa1 = "N";
      |PDEFECTO #caivaax1,42,46;
      |PINDA #0#caivaax1;
  |FINSI;
|FCAL;

|CALCULO no_subde; |TIPO 7;
      |C_M #caivaax1#46, 1, eaSubde;
      |C_M #caivaax1#47, 1, eaSubde;
|FCAL;

|CALCULO Tras104; |TIPO 0;
 alfa1 = #caivaax1#67;
 |C_M #caivaax1#53,1,alfa1;
 |C_M #caivaax1#54,1,alfa1;
 |C_M #caivaax1#69,1,alfa1;
 |C_M #caivaax1#70,1,alfa1;
 |C_M #caivaax1#76,1,alfa1;
 |SI alfa1 = "N";
     #caivaax1#53 = 1;    |PINTA #caivaax1#53;
     #caivaax1#54 = 999;  |PINTA #caivaax1#54;
     #caivaax1#69 = 0;    |PINTA #caivaax1#69;
     #caivaax1#70 = "N";  |PINTA #caivaax1#70;
     #caivaax1#76 = "N";  |PINTA #caivaax1#76;
 |FINSI;
|FCAL;

|CALCULO Tras105; |TIPO 0;
 alfa1 = #caivaax1#68;
 |C_M #caivaax1#55, 1,alfa1;
 |C_M #caivaax1#56, 1,alfa1;
 |C_M #caivaax1#71, 1,alfa1;
 |C_M #caivaax1#72, 1,alfa1;
 |C_M #caivaax1#77,1,alfa1;
 |SI alfa1 = "N";
     #caivaax1#55 = 1;    |PINTA #caivaax1#55;
     #caivaax1#56 = 999;  |PINTA #caivaax1#56;
     #caivaax1#71 = 0;    |PINTA #caivaax1#71;
     #caivaax1#72 = "N";  |PINTA #caivaax1#72;
     #caivaax1#77 = "N";  |PINTA #caivaax1#77;
 |FINSI;
|FCAL;

|CALCULO Antes89; |TIPO 7;
 alfa1 = "S";
 |SI #caivaax1#67 = "N"; |Y #caivaax1#67 = "N";
     alfa1 = "N";
 |FINSI;
 |C_M #caivaax1#59, 1,alfa1;
 |C_M #caivaax1#60, 1,alfa1;
 |C_M #caivaax1#61, 1,alfa1;
|FCAL;

|| ////////////////////////////////////////////////////////////////
|CALCULO ParaEntrar; |TIPO 0;
  ModoEntrada = (FEntrada / 100) ! 0;

  aQueQuiero = "|NC";
  aNumCampos = #caivaaxx#aQueQuiero#;
  nNumCampos = aNumCampos;
  nNumCampos = nNumCampos - 1;

  |DDEFECTO #caivaax1;
  |PARA nPara1 = 2; |SI nPara1 [ nNumCampos; |HACIENDO nPara1 = nPara1 + 1;
         |SI ModoEntrada = 1;
             aAlfa = ("000" + nPara1) % -103;
             aQueQuiero = "|C" + aAlfa + "IDCAMPO";
             aIDCampo = #caivaaxx#aQueQuiero#;
             |QBF aIDCampo;
             |SI aIDCampo ! "";
                 aAlfa  = aIDCampo % 2;
                 nNume1 = aAlfa;
                 #caivaax1#nPara1# = #caivaasi#nNume1#;
             |FINSI;
         |SINO;
             #caivaax1#nPara1# = #caivaaxx#nPara1#;
         |FINSI;
  |SIGUE;

  #caivaax1#0  = #caivaaxx#0;
  #caivaax1#1  = #caivaaxx#1;
  #caivaax1#17 = (("00" + #caivaaxx#0) % -102);

  |PINPA #0#caivaax1;
  |PINDA #0#caivaax1;
  |SI ModoEntrada = 1; |ENDATOS #1#caivaax1; |FINSI;
  |SI ModoEntrada = 2; |ENDATOS #2#caivaax1; |FINSI;
  |SI FSalida = 0;
      |PARA nPara1 = 0; |SI nPara1 [ nNumCampos; |HACIENDO nPara1 = nPara1 + 1;
            #caivaaxx#nPara1# = #caivaax1#nPara1#;
      |SIGUE;
      |PINDA #0#caivaaxx;
  |SINO;
      |ERROR;
  |FINSI;
|FCAL;

|| ///////////////////////////////////////////////////////////
|PROCESO PintaFecha;
    alfa1 = &205; alfa1 = alfa1 * 44;
    |PINTA 0636, alfa1;
    #cafantas#10 = efActIni;
    #cafantas#11 = efActFin;
    |SI efActIni > "01.01.1900";
        |ATRI S; |PINTA 2406, "Fecha Inicio: "; |ATRI 0;
        |PINTA 2420, #cafantas#10;
    |FINSI;
    |SI efActFin > "01.01.1900";
        |ATRI S; |PINTA 2444, "Fecha Fin: "; |ATRI 0;
                 |PINTA 2455, #cafantas#11;
    |FINSI;
|FPRC;

|PROCESO PintaActividad;
       enActividad = #caivaaxx#0;
       eaCodDep    = #caivaaxx#0; |QBF eaCodDep;
       eaCodDep    = ("00" + eaCodDep) % -102;
       |HAZ Actividad;
       |SI eswLect = 0;
           #caivaaxx#0 = eaCodDep;    |PINTA #caivaaxx#0;
           #caivaaxx#1 = eaNombreAct; |PINTA #caivaaxx#1;
           |HAZ PintaFecha;
       |FINSI;
|FPRC;

|CALCULO Depar11; |TIPO 11;
  nInkey = FSalida;
  eOpDep = 0; || 1 = Consulta pantalla
  |SI nInkey = 10;
       eOpDep = 1;
       |HAZ PintaActividad;
       nInkey = 0;
       |ERROR;
  |FINSI;
|FCAL;

|CALCULO AntesDep; |TIPO 7;
   |SI eaDepar = "S";
      |SI Haybasica = 1;
          |MENSA "    <F2> Consultar Actividades.";
      |SINO;
          |MENSA "    <F2> Consultar Departamentos.";
      |FINSI;
   |FINSI;

   ModoEntrada = (FEntrada / 100) ! 0;
   |BLIN 24;
   |SI ModoEntrada > 1;
       eOpDep = 0;
       |HAZ PintaActividad;
   |FINSI;
|FCAL;

|CALCULO TrasDep; |TIPO 0;

  ModoEntrada = (FEntrada / 100) ! 0;

  eOpDep = 0; || 1 = Consulta pantalla
  |HAZ PintaActividad;
  |SI eswLect = 1;
      |MENAV "    Error.! No Existe Actividad / Departamento";
      |SI ModoEntrada = 1;
          |ERROR;
      |FINSI;
  |SINO;
      |SI efActFin > "01.01.1900"; |Y #caivaasi#119 = "S";
          alfa1 = "    NAtencion!!!. Actividad con Fecha de BAJA " + efActFin + ", Desea introducir Facturas ? ";
          |CONFI alfa1;
          |SI FSalida ! 0;
              |ERROR;
          |FINSI;
      |FINSI;
  |FINSI;
  #caivaaxx#17 = eaCodDep;
|FCAL;

|PROCESO inf0;
  #caivaaxx#0 = 01;
|FPRC;

|PROCESO sup0;
  #caivaaxx#0 = 99;
|FPRC;

|PROGRAMA;
   aParam = PARAMETRO; |QBF aParam;
   |SI aParam = "";
       |HAZ inicio;
       |HAZ DirAplicSt;
   |FINSI;
   |HAZ Analitica;

   |ABRE #caivaasi;
   |DDEFECTO #caivaasi;
   |LEE 001#caivaasi.p;
   |CIERRA #caivaasi;

   |SI #caivaasi#187 = "N";
       |MENAV "0000Parametros de Entrada de Facturas por Departamento NO activado";
       |ACABA;
   |FINSI;

   aSubAnt = #caivaasi#67;
   |C_M #caivaaxx#46,1,eaSubde;
   |C_M #caivaaxx#47,1,eaSubde;

   |CLS;
   |CABEZA "Def. Entrada Facturas por Actividades";

   |ENTLINEAL #1#caivaaxx, -1, 2, 22, 1, inf0, sup0;
|FPRO;
