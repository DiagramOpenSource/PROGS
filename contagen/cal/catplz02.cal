|FICHEROS;
   catplz02 #0;

   :/modelos/def/dsmom030 #330;

   caivaasi #200;
   camaasic #1;
   camaasil #2;

   caclipro #4;
   cacuenta #5;

   caivases #13;
   calibros #10;
   caivalin #11;
   camaniva #12;

   catplw01 #101;
   catplw02 #102;
   catplw03 #103;
   catplw04 #104;

   &sgpaises@ #718;
|FIN;

|VARIABLES;
   &entrada = 1;
   nInkey    = 0;
   aAlfa     = "";
   aAlfa1    = "";
   aAlfa2    = "";
   aAlfa3    = "";
   aAlfa4    = "";
   aTipo     = "";
   nNume1    = 0;
   nNume2    = 0;
   nNume3    = 0;
   nNume4    = 0;
   nPara1    = 0;
   nModo     = 0;
   aParam    = "";
   aLong     = "";
   nLong     = 0;

   &eaUnidadBasico = "";
   &eaOrigen  = "";
   &eaDestino = "";

   Comodin    = "";
   &Local      = 0;

   &enTFich   = 0;
   &enLlegoa9 = 0;
   &enNum9    = 0;
   &eaPath    = "";
   &eaPathExc = "";
   &eaFichero = "";

   &nLeer   = 0;
   nSw      = 0;
   nSal     = 0;
   nHay     = 0;
   aHoja    = "";
   nSwHay   = 0;
   nLinea   = 0;
   aCol     = "";
   aExcel   = "";
   nXls     = 0;
   aExt     = "";

   aFichero = "";
   fFecUlt   = @;

   &efFecha  = @;
   aQueQuiero = "";
   aNumCampos = "";
   nNumCampos = 0;
   aIDCampo   = "";
   aTemporal  = "";
   nExiste    = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoiv_i;

|CALCULO PonBarra; |TIPO 0;
     nInkey = FSalida;

     |SI nInkey = 10;
        aAlfa1 = "D c:\";
        |BROWSE aAlfa1;
        aAlfa1 = aAlfa1 % 280;
        #0Campo = aAlfa1;
        |PINTA #0Campo
     |FINSI;

     aAlfa1 = #0Campo;
     |QBF aAlfa1;
     aAlfa2 = aAlfa1 % -101;
     |SI aAlfa2 ! "/";|Y aAlfa2 ! "\";
          #0Campo = aAlfa1 + "\";
          |PINTA #0Campo;
     |FINSI;
|FCAL;

|CALCULO PonFichero; |TIPO 0;
     aAlfa1 = #0Campo;
     |QBF aAlfa1;
     |SI aAlfa1 = "";
         |MENAV "0000Introduzca el Nombre del Fichero";
         |ERROR;
         |ACABA;
     |FINSI;

      aAlfa2 = aAlfa1 - ".";
      |SI FSalida ! 0;
         |MENAV "0000Introduzca el Nombre del Fichero SIN extension";
         |ERROR;
      |FINSI;
|FCAL;

|PROCESO Tipo12_z; |TIPO 12;
   || .... Para que no pregunte conforme S/N
|FPRC;

|| //////////////////////////////////////////////////////

|PROCESO CreaCarp;
   |SI Local = 0;
       |RMKDIR aAlfa1;
   |SINO;
       |MKDIR aAlfa1;
   |FINSI;
|FPRC;

|PROCESO CopiaVacio;
   |DBASE aAlfa; |QBF aAlfa;
   aAlfa = aAlfa + "vplusgestion.xls";

   aAlfa1 = eaUnidadBasico + "\DOCUMENTOS";  |HAZ CreaCarp;
   aAlfa1 = aAlfa1 + "\tmp";                 |HAZ CreaCarp;
   aAlfa1 = aAlfa1 + "\" +  (("00000" + enEmpresa) % -105) + enPeriodo;
                                             |HAZ CreaCarp;

   aTemporal = aAlfa1 + "\vacia.xls";
   |SI Local = 0;
      |ENVIA_FICHERO aAlfa, aTemporal;
   |SINO;
      |COPIA_FICHERO aAlfa, aTemporal;
   |FINSI;
|FPRC;
|| ********************************************************************
|PROCESO GrabaAuxiliar;
     nLinea = nLinea + 1;
     #101#0 = nLinea;
     |GRABA 020#101n;
     nSwHay = 1;
|FPRC;


|PROCESO Una;
     ||| aCol contiene la columna, "A", "B" y nXls la fila

     aAlfa = aCol + nXls + "," + aAlfa;
     |ALFACALLDLL "agixl97.dll", "poke_dato", aAlfa;

     nSwHay = 1;
|FPRC;

|PROCESO Estructura1;

     |PARA nPara1 = 1; |SI nPara1 [ nNumCampos; |HACIENDO nPara1 = nPara1 + 1;
           aAlfa1 = ("000" + nPara1) % -103;
           aQueQuiero = "|C" + aAlfa1 + "IDCAMPO";

           aIDCampo = #101#aQueQuiero#; |QBF aIDCampo;
           aCol = aIDCampo;

           aQueQuiero = "|C" + aAlfa1 + "TIPO";
           aTipo = #101#aQueQuiero#;
           aAlfa = #101nPara1;
           |HAZ Una;
     |SIGUE;

|FPRC;

|PROCESO CargaHoja;

     nXls = #101#0;
     nXls = nXls + 1;

     |HAZ Estructura1;
|FPRC;

|DEFBUCLE LeeAuxiliar101;
     #101#1;
     ;
     0000001;
     9999999;
     e;
     NULL, CargaHoja;
|FIN;

|DEFBUCLE LeeAuxiliar102;
     #102#1;
     ;
     0000001;
     9999999;
     ;
     NULL, CargaHoja;
|FIN;

|DEFBUCLE LeeAuxiliar103;
     #103#1;
     ;
     0000001;
     9999999;
     ;
     NULL, CargaHoja;
|FIN;

|DEFBUCLE LeeAuxiliar104;
     #104#1;
     ;
     0000001;
     9999999;
     ;
     NULL, CargaHoja;
|FIN;

|PROCESO excelHoja;
     |ALFACALLDLL "agixl97.dll","selecciona_hoja",aHoja;
     |SI FSalida = 0;

         aQueQuiero = "|NC";
         |SI enTFich = 1;  aNumCampos = #101#aQueQuiero#;  |FINSI;
         nNumCampos = aNumCampos;
         nNumCampos = nNumCampos - 1;

         nSw = 1;
         |BUCLE LeeAuxiliar101;
     |FINSI;
|FPRC;

|PROCESO excel;
     aExcel = eaPathExc + eaFichero;

     aAlfa = "Pasando datos a la Hoja Excel " + eaFichero;
     |PINTA 2107, aAlfa;

     |ALFACALLDLL "agixl97.dll", "carga_book", aTemporal;

     aHoja = #0#8; |QBF aHoja;
     |SI aHoja ! "";
         enTFich = 1; || Clientes/Proveedores
         |HAZ excelHoja;
     |FINSI;

     |RFBORRA aExcel;
     |ALFACALLDLL "agixl97.dll", "fichero_destino", aExcel;
     |ALFACALLDLL "agixl97.dll", "fin_book", "salva";
|FPRC;

|PROCESO PasoaIndenxado;
    |CARGADLL "agixl97.dll";
    |SI FSalida < 0;
        |MENAV "0000No se puede usar agixl97.dll";
    |SINO;
        |HAZ excel;
        |DESCARGADLL "agixl97.dll";
    |FINSI;
|FPRC;

|PROCESO caclipro;
   |DDEFECTO #101;
   #101#1  = eaNombreEmp;
   #101#2  = #4#10;
   #101#3  = #4#1;
   #101#4  = #4#9;
   #101#5  = #4#0;
   |SI #4#24 = "   "; #4#24 = "ES "; |FINSI;

   #101#6  = #4#24;
   #101#7  = #4#9;
   #101#8  = #4#2;
   |SI #4#3 ! 0; |Y #4#4 ! 0;
       aAlfa1  = ("00"  + #4#3) % -102;
       aAlfa2  = ("000" + #4#4) % -103;
       aAlfa1  = aAlfa1 + aAlfa2;
       #101#9  = aAlfa1;
   |FINSI;
   #101#10 = #4#6;
   #101#11 = #4#5;

   eaCodPais = #4#24;
   |HAZ LeePais;
   #101#12 = #718#1;

   #101#13 = #4#7;
   #101#14 = #4#51;
   #101#15 = #4#21;
   #101#17 = #4#42;
   #101#18 = #4#52;
   #101#19 = #4#20;
   |HAZ GrabaAuxiliar;
   nExiste = 1;
|FPRC;

|DEFBUCLE caclipro;
   #4#1;
   ;
   " ", #5#0;
   "z", #5#0;
   e;
   NULL, caclipro;
|FIN;

|PROCESO PasarCta;
   aAlfa = #5#0; |QBF aAlfa;
   |SI aAlfa = ""; |ACABA; |FINSI;

   nExiste = 0;
   |BUCLE caclipro;
   |SI nExiste = 0;
       |DDEFECTO #101;
       #101#1  = eaNombreEmp;
       #101#2  = #5#0;
       #101#3  = #5#2;
       |HAZ GrabaAuxiliar;
   |FINSI;
|FPRC;

|DEFBUCLE cacuenta;
  #5#1;
  3;
  "             ", "S";
  "zzzzzzzzzzzzz", "S";
  ;
  NULL, PasarCta;
|FIN;

|PROCESO CargoIndenxado;
    |SI #0#4 = "S";
        nLinea  = 0;
        enTFich = 1;
        |ABRE #101;
        |BUCLE cacuenta;
        |CIERRA #101;
    |FINSI;
|FPRC;

|PROCESO HazOperacion;
   eaPath = #0#2; |QBF eaPath;
   |SI eaPath = "";
       |MENAV "    Error. Path de ficheros erroneo";
       |ACABA;
   |FINSI;

   Comodin = "";
   |IP_REMOTO Comodin;
   |QBF Comodin;
   |SI Comodin = "";
       Local = 1;
   |SINO;
       Local = 0;
   |FINSI;

   aFichero = ("1h" + Usuario) % 108;
   |NOME_DAT #101 aFichero; |DELINDEX #101;

   nSwHay = 0;
   eaDestino = eaPath + #0#3; |QBF eaDestino;
   eaDestino = eaDestino + ".xls";
   nLeer    = 1;
   eaFichero = #0#3; |QBF eaFichero;
   eaFichero = eaFichero + ".xls";
   eaPathExc = eaPath;

   |HAZ CopiaVacio;

   |HAZ CargoIndenxado;

   |SI nSwHay = 1;
       nSwHay = 0;
       |HAZ PasoaIndenxado;
       |SI nSwHay = 1;
           aAlfa = "0000Creado el Fichero " + eaFichero + " en la Carpeta " + eaPath;
           |MENAV aAlfa;
       |FINSI;
   |FINSI;

   |DELINDEX #101;
|FPRC;

|PROGRAMA;
   |CLS;
   |CABEZA "Exportacion PlusGestiona";

   |HAZ inicio;
   |HAZ DirAplicSt;
   |HAZ LeeUnidadBasico;

   aTipoIVA = "R";
   |HAZ eLeeParametro;

   |ABRE #0;
   #0#0 = "A";
   |LEE 141#0=;
   |SI FSalida ! 0;
       |DDEFECTO #0;
       |GRABA 020#0b;
   |FINSI;

   |PINPA #0#0;
   |PINDA #0#0;
   |ENDATOS #1#0;
   |SI FSalida = 0;
       |SI #0#1 = "SI";
           |HAZ HazOperacion;
       |FINSI;
       #0#1 = "NO";
       |GRABA 020#0a;
   |FINSI;
   |LIBERA #0;
   |CIERRA #0;
|FPRO;
