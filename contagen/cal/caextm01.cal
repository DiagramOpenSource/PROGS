|FICHEROS;
   caextm01 #0;
   caextm02 #1;
   caexttip #10;

   camaasic #2;
   camaasil #3;
   cacuenta #5;

   caextrp4 #999;

   caextmw1 #900,MANTE;

   caextm02@ #901;
   caextm01@ #910;
|FIN;

|VARIABLES;
     &entrada = 1;
     nModo    = 0;
     inkey    = 0;
     inkeyc   = 0;
     totallin = 0;
     aAlfa    = "";
     aAlfa1   = "";
     aAlfa2   = "";
     aAlfa3   = "";
     aAlfa4   = "";
     nNume1   = 0;
     nNumero  = 0;
     nError   = 0;
     aFichero  = "";
     aMas     = "";
     aDef     = "";
     nEst900  = 0;
     nDTipo   = 0;
     nHTipo   = 0;
     nHay     = 0;

     nBoton2  = 0;
     nBoton3  = 0;
     nBoton4  = 0;
     nBoton5  = 0;
     nBoton6  = 0;
     nBoton7  = 0;
     nSwBoton2 = 0;

     aPath          = "";
     aPathFich      = "";
     aPathEste      = "";
     aPathOtros     = "";
     nVariable      = 0;
     &enDentro      = 0;
     &eaNombreList  = "";
     &enCuadroIni   = 0;
     &enCuadroFin   = 0;
     &enCampoAncho  = 0;
     &enModoEdita   = 0;
     &enTotalVaria  = 0;
{101}&epPuntEdita   = "";
|FIN;

|INCLUYE dscont_i;

||  ***********************************************************

|| -------------------------------------------------------------------------
|PROCESO lee_asiento1;             || CARGA LA PANTALLA DEL ASIENTO COMPLETO
   |HAZ lacuenta;
   |DDEFECTO #999;
   #999#00 = #3#3;        || apunte
   #999#01 = #3#16;       || cuenta DEBE
   #999#02 = #3#17;       || digito DEBE
   #999#03 = #3#10;       || cuenta HABER
   #999#04 = #3#11;       || digito HABER
   #999#05 = #3#6;        || concepto
   #999#06 = #3#7;        || descripcion
   #999#07 = #3#21;       || departamento
   #999#08 = #3#9;        || importe
   #999#09 = #5#2;        || descripcion
   #999#10 = #5#51;       || debe
   #999#11 = #5#52;       || haber
   #999#12 = #5#53;       || saldo
   #999#13 = #3#0;        || diario
   #999#14 = #3#1;        || fecha
   #999#15 = #3#2;        || documento
   #999#16 = #3#28;       || subdepartamento
   |GRABA 020#999n;
   totallin = totallin + 1;
|FPRC;

|DEFBUCLE lee_asiento;
   #3#1;
   ;
   #0#3, #0#4, #0#5,    1;
   #0#3, #0#4, #0#5, 9999;
   e;
   NULL, lee_asiento1;
|FIN;

|| ------------------------------------------
|RUTINA mini;
   #999#0 = 1;
|FRUT;

|RUTINA maxi;
   #999#0 = 9999;
|FRUT;

|PROCESO VerTodoElAsto;
   aPathOtros = aPathEste;
   |SI #0#12 ! -1;
       aAlfa1 = ("00000" + enEmpresa) % -105;
       aAlfa2 = #0#12;
       aAlfa1 = aAlfa1 + aAlfa2;
       aPathOtros = aPathFich + aAlfa1 + "/";
   |FINSI;

   |PATH_DAT #2 aPathOtros;
   |PATH_DAT #3 aPathOtros;
   |PATH_DAT #5 aPathOtros;

   |DELINDEX #999;
   |ABRE #999;
   totallin = 0;
   |BUCLE lee_asiento;
   |CIERRA #999;
   |SI totallin ! 0;
      |PUSHV 07012380;
      |ENTLINEAL #1#999, -1, 4, 20, 1, mini, maxi;
      |POPV;
   |SINO;
      |MENAV "    No existe Asiento Relacionado ...";
   |FINSI;
   |DELINDEX #999;

   |SI #0#12 ! -1;
      |PATH_DAT #2 aPathEste;
      |PATH_DAT #3 aPathEste;
      |PATH_DAT #5 aPathEste;
   |FINSI;
|FPRC;

|PROCESO lacuenta;
   |ABRE #5;
   #5#0 = #3#4;
   |LEE 001#5=;
   |SI FSalida ! 0;
       |DDEFECTO #5;
       #5#2 = "<CUENTA INEXISTENTE>";
   |FINSI;
   |CIERRA #5;
|FPRC;

|PROCESO ParaConsulta;
   nModo = (FEntrada / 100) ! 0;

   |SI inkey  = 10;
      |PUSHV 0501 2380;
      |ABRE #2;
      |PINPA #0#2;
      #2#0 = #0#3;
      #2#1 = #0#4;
      #2#2 = #0#5;
      |PTEC 802;
      |PTEC 802;
      |CONSULTA #0#2;
      inkeyc = FSalida;
      |CIERRA #2;
      |POPV;

      |SI inkeyc = 0;
         #0#3 = #2#0; |PINTA #0#3;
         #0#4 = #2#1; |PINTA #0#4;
         #0#5 = #2#2; |PINTA #0#5;
      |SINO;
         |ERROR;
      |FINSI;
   |FINSI;
|FPRC;

|CALCULO fecha; |TIPO 0;

   |C_M #0Campo,0,aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   inkey = FSalida;
   |SI inkey = 10;
       |HAZ ParaConsulta;
   |FINSI;

   |SI #0#4 > fec2;|O #0#4 < fec1;
      |MENAV "    Fecha fuera de Periodo ...";
      |ERROR;
   |FINSI;
|FCAL;

|CALCULO diario;
   |C_M #0Campo,0,aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   inkey = FSalida;
   |SI inkey = 10;
       |HAZ ParaConsulta;
   |FINSI;
|FCAL;

|CALCULO documento;
   |C_M #0Campo,0,aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   inkey = FSalida;
   |SI inkey = 10;
       |HAZ ParaConsulta;
   |FINSI;
   |SI inkey = 2; |ACABA; |FINSI;

   |ABRE #2;
   #2#0 = #0#3;
   #2#1 = #0#4;
   #2#2 = #0#5;
   |LEE 041#2=;
   |SI FSalida ! 0;
       |MENAV "0000Error no existe Asiento Contable.";
       |ERROR;
   |SINO;
       |ABRE #3;
       #3#0 = #2#0;
       #3#1 = #2#1;
       #3#2 = #2#2;
       #3#3 = 1;
       |LEE 041#3];
       |SI FSalida = 0; |Y #3#0 = #2#0; |Y #3#1 = #2#1; |Y #3#2 = #3#2;
           #0#10 = #3#6;
           #0#11 = #3#7;
       |FINSI;
       |CIERRA #3;
       #0#12 = enPeriodo;
   |FINSI;
   |CIERRA #2;
   |PINTA #0#10;
   |PINTA #0#11;
|FCAL;

|CALCULO cuenta; |TIPO 6;
   |C_M #0Campo,0,aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   eaCuenta = #0Campo;
   salidas  = FSalida;
   |HAZ FiltraPuntos;
   #0Campo = eaCuenta;
   |PINTA #0Campo;

   eaCuenta = #0Campo;
   |HAZ SacaNivel;
|FCAL;

|CALCULO cuefin; |TIPO 0;                    || desde campo 'cuenta'
   |C_M #0Campo,0,aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   aAlfa1 = #0Campo; |QBF aAlfa1;
   |SI aAlfa1 = "";
       |MENAV "0000Introduzca una Cuenta Contable";
       |ERROR;
   |FINSI;

   #0#7 = #5#2; |PINTA #0#7;
|FCAL;

|PROCESO BorroLineas;
   |BORRA 020#1a;
   |LIBERA #1;
|FPRC;

|DEFBUCLE BorroLineas;
   #1#1;
   ;
   #0#13, #0#0, 0001;
   #0#13, #0#0, 9999;
   be;
   NULL, BorroLineas;
|FIN;

|CALCULO Tipo1; |TIPO 1;
   nModo = (FEntrada / 100) ! 0;

   |SI nModo = 3;
       |BUCLE BorroLineas;
   |FINSI;
|FCAL;
|| /////////////////////////////////////////////////////////////////////////
|PROCESO RecogeDelFichero;
     #1#3 = #0#13;
     #1#0 = #0#0;
     #1#1 = 1;
     |LEE 040#1=;
     |SI FSalida ! 0;
         |PARA nVariable = 1;  |SI nVariable [ 100;  |HACIENDO nVariable = nVariable + 1;
               #1#3 = #0#13;
               #1#0 = #0#0;
               #1#1 = nVariable;
               |LEE 040#1=;
               |SI FSalida = 0;
                   #1#3 = #0#13;
                   #1#0 = #0#0;
                   #1#1 = nVariable;
                   |GRABA 020#1n;
                   |LIBERA #1;
               |FINSI;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO AltaLinea;
   #1#3 = #0#13;
   #1#0 = #0#0;
   #1#1 = 0001;
|FPRC;

|PROCESO BajaLinea;
   #1#3 = #0#13;
   #1#0 = #0#0;
   #1#1 = 9999
|FPRC;

|CALCULO Tipo3; |TIPO 3;

   enTotalVaria  = 99;
   eaNombreList  = "";
   enCuadroIni   = 1425;
   enCuadroFin   = 2276;
   enCampoAncho  = 50;
   enModoEdita   = 0;

   |ABRE #1;
   |HAZ RecogeDelFichero;
   |PARA nVariable = 1;  |SI nVariable [ 100;  |HACIENDO nVariable = nVariable + 1;
         IepPuntEdita  = epPuntEdita1 <-;
         IepPuntEdita = IepPuntEdita + (nVariable - 1);

         #1#3 = #0#13;
         #1#0 = #0#0;
         #1#1 = nVariable;
         |LEE 101#1=;
         |SI FSalida ! 0;
             epPuntEdita  = "";
         |SINO;
             epPuntEdita  = #1#2;
             |BORRA 020#1a;
         |FINSI;
         |LIBERA #1;
     |SIGUE;

     |PUSHV  0501 2380;
     |CUADRO 1224 2379;
     |BLANCO 1325 2278;
     |SI #0#8 ! "X";
         |ATRI S;  |PINTA 1326, "Introduzca el Texto ";  |ATRI 0;
     |SINO;
         |ATRI P;  |PINTA 1326, "SOLO CONSULTA. PARA MODIFICAR EN EJERCICIO POSTERIOR"; |ATRI 0;
     |FINSI;

     |HAZ Edita;
     |POPV;

 |SI #0#8 ! "X";
     |PARA nVariable = 1;  |SI nVariable [ 100;  |HACIENDO nVariable = nVariable + 1;
           IepPuntEdita = epPuntEdita1 <-;
           IepPuntEdita = IepPuntEdita + (nVariable - 1);

           |DDEFECTO #1;
           #1#3 = #0#13;
           #1#0 = #0#0;
           #1#1 = nVariable;
           |LEE 101#1=;
           |SI FSalida ! 0;  |GRABA 020#1b;  |FINSI;
           #1#2 = epPuntEdita;
           |GRABA 020#1a;
           |LIBERA #1;
     |SIGUE;

     |PARA nVariable = 100;  |SI nVariable ] 1;  |HACIENDO nVariable = nVariable - 1;
           #1#3 = #0#13;
           #1#0 = #0#0;
           #1#1 = nVariable;
           |LEE 101#1=;
           aAlfa = #1#2;  |QBF aAlfa;

           |LIBERA #1;

           |SI aAlfa ! "";  |SAL;  |FINSI;

           |BORRA 020#1a;
     |SIGUE;
 |FINSI;

 |CIERRA #1;
 |ENTLINEAL #1#1, -3, 7, 22, 0, AltaLinea, BajaLinea;

|FCAL;

|CALCULO Lafecha; |TIPO 7;
   |SI nEst900 = 1;
       #0#13 = enEmpresa;
       #0#0  = #910#0;
       |ENTLINEAL #1#1, -3, 7, 22, 0, AltaLinea, BajaLinea;
   |FINSI;
|FCAL;

|CALCULO ElTipo; |TIPO 0;
   nModo = (FEntrada / 100) ! 0;
   nError = 0;

   |ABRE #10;
   #10#0 = #0#1;
   |LEE 041#10=;
   |SI FSalida ! 0;
       nError = 1;
   |FINSI;
   |CIERRA #10;

   |SI nError = 0;
       |PINTA 0828, #10#1;
       |SI nModo = 1;
           #0#2 = #10#2; |PINTA #0#2;
       |FINSI;
   |FINSI;

   |SI #0#2 = 1; aAlfa1 = "General"; |FINSI;
   |SI #0#2 = 2; aAlfa1 = "Cuenta "; |FINSI;
   |SI #0#2 = 3; aAlfa1 = "Asiento"; |FINSI;

   |ATRI P;
   |PINTA 0862, aAlfa1;
   |ATRI 0;

   |SI #10#3 = "S"; aAlfa1 = "Publico   "; |FINSI;
   |SI #10#3 = "N"; aAlfa1 = "NO Publico"; |FINSI;

   |ATRI P;
   |PINTA 0870, aAlfa1;
   |ATRI 0;

   aAlfa1 = "N";
   aAlfa2 = "N";
   |SI nModo = 1;

      |SI #0#2 = 1;
          #0#3 = 0;            |PINTA #0#3;
          #0#4 = "01.01.0000"; |PINTA #0#4;
          #0#5 = 0;            |PINTA #0#5;
          #0#6 = "";           |PINTA #0#6;
          #0#7 = "";           |PINTA #0#7;
          #0#10 = 000;         |PINTA #0#10;
          #0#11 = "";          |PINTA #0#11;
          #0#12 = -1;
      |FINSI;

      |SI #0#2 = 2;
          aAlfa1 = "S";
          #0#3  = 0;            |PINTA #0#3;
          #0#4  = "01.01.0000"; |PINTA #0#4;
          #0#5  = 0;            |PINTA #0#5;
          #0#10 = 000; |PINTA #0#10;
          #0#11 = "";  |PINTA #0#11;
          #0#12 = -1;
      |FINSI;

      |SI #0#2 = 3;
          aAlfa2 = "S";
          |SI #0#3 = 0;            #0#3 = 1; |PINTA #0#3; |FINSI;
          |SI #0#4 = "01.01.0000"; #0#4 = ~; |PINTA #0#4; |FINSI;
          |SI #0#5 = 0;            #0#5 = 1; |PINTA #0#5; |FINSI;
          #0#6 = ""; |PINTA #0#6;
          #0#7 = ""; |PINTA #0#7;
          #0#12 = enPeriodo;
      |FINSI;
   |FINSI;
   |C_M #0#3, 1, aAlfa2;
   |C_M #0#4, 1, aAlfa2;
   |C_M #0#5, 1, aAlfa2;
   |C_M #0#6, 1, aAlfa1;

   |SI #0#2 = 1;
       |PINTA 0923, "Asiento:";
       |PINTA 1123, "Cuenta :";
   |FINSI;
   |SI #0#2 = 2;
       |PINTA 0923, "Asiento:";
       |ATRI S; |PINTA 1123, "Cuenta :"; |ATRI 0;
   |FINSI;
   |SI #0#2 = 3;
       |ATRI S; |PINTA 0923, "Asiento:"; |ATRI 0;
                |PINTA 1123, "Cuenta :";
   |FINSI;
|FCAL;

|CALCULO SegunTipo;

   nModo  = (FEntrada / 100) ! 0;
   |SI nModo = 1;
       aAlfa1 = "S";
       aAlfa2 = "S";
       aAlfa3 = "S";
       aAlfa4 = "S";
   |SINO;
       aAlfa1 = "N";
       aAlfa2 = "N";
       aAlfa3 = "N";
       aAlfa4 = "S";
       |SI #0#8 = "X"; aAlfa4 = "N"; |FINSI;
   |FINSI;
   |C_M #0#1, 1, aAlfa3;
   |C_M #0#3, 1, aAlfa2;
   |C_M #0#4, 1, aAlfa2;
   |C_M #0#5, 1, aAlfa2;
   |C_M #0#6, 1, aAlfa1;
   |C_M #0#8, 1, aAlfa4;
   |C_M #0#9, 1, aAlfa4;

   |PINTA 0828, "                             ";
   |PINTA 0862, "                 ";
   |PINTA 0923, "Asiento:";
   |PINTA 1123, "Cuenta :";
   |SI nModo ! 1;
       |HAZ ElTipo;
   |FINSI;
|FCAL;

|| //////////////////////////////////////////////////////////////////////
|PROCESO ParaBuscar;
   nHay = 1;
   |ERROR10;
|FPRC;

|DEFBUCLE ParaBuscar;
   #900#1;
   2;
   enEmpresa, 0001, nDTipo;
   enEmpresa, 9999, nHTipo;
   ;
   NULL, ParaBuscar;
|FIN;

|PROCESO BuscaAnotaciones
  nHay = 0;
  |BUCLE ParaBuscar;
  |SI nHay = 0;
      |MENAV "0000No existen Anotaciones Extracontable";
  |SINO;
      |SI inkey = 11;
          |ATRI S; |PINTA 0627, aAlfa1; |ATRI 0;

      |SINO;

          |PINPA #0#900;
          |ATRI R; |PINTA 0602, aAlfa2; |ATRI 0;
          |C_P #900#1, 1, 0644;
          |C_P #900#9, 1, 0666;

          aAlfa1 = "S";
          aAlfa2 = "N";
          aAlfa3 = "N";
          |SI inkey = 12;
              aAlfa1 = "N";
              aAlfa2 = "N";
              aAlfa3 = "S";
              |PINTA 0529, "                                                    ";
              |PINTA 0629, "                                                    ";
              |PINTA 0729, "Â";
              |PINTA 0780, "¿";
              |ATRI R; |PINTA 0809, " C.Tipo  Fecha Anot."; |ATRI 0;
              |C_P #900#1, 1, 0911;
              |C_P #900#9, 1, 0917;
          |FINSI;
          |SI inkey = 13;
              aAlfa1 = "N";
              aAlfa2 = "S";
              |ATRI R; |PINTA 0809, " Cuenta Contable    "; |ATRI 0;
          |FINSI;
          |C_M #900#0, 1, aAlfa3;
          |C_V #900#3, 1, aAlfa1;
          |C_V #900#4, 1, aAlfa1;
          |C_V #900#5, 1, aAlfa1;
          |C_V #900#6, 1, aAlfa2;
          |C_L #900#1, 1, aAlfa3;
          |C_L #900#9, 1, aAlfa3;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO ClaveAlta;
   #0#13 = enEmpresa;
   #0#0  = 0001;
   #0#9  = "01.01.0000";
|FPRC;

|PROCESO ClaveBaja;
   #0#13 = enEmpresa;
   #0#0  = 9999;
   #0#9  = "31.12.2099";
|FPRC;

|PROCESO Cl900Alta;
   #900#13 = enEmpresa;
   #900#0  = 0001;
   #900#9  = "01.01.0000";
|FPRC;

|PROCESO Cl900Baja;
   #900#13 = enEmpresa;
   #900#0  = 9999;
   #900#9  = "31.12.2099";
|FPRC;

|PROCESO Cl900Alta2;
   #900#13 = enEmpresa;
   #900#0  = 0001;
   #900#2  = nDTipo;
   #900#3  = 00;
   #900#4  = "01.01.1900";
   #900#5  = 000001;
   #900#9  = "01.01.1900";
   |SI FSalida = "POSICION";
       #900#0 = #0#0;
       #900#3 = #0#3;
       #900#4 = #0#4;
       #900#5 = #0#5;
   |FINSI;
|FPRC;

|PROCESO Cl900Baja2;
   #900#13 = enEmpresa;
   #900#0  = 9999;
   #900#2  = nHTipo;
   #900#3  = 99;
   #900#4  = "31.12.2099";
   #900#5  = 999999;
   #900#9  = "31.12.2099";
|FPRC;

|PROCESO Cl900Alta3;
   #900#13 = enEmpresa;
   #900#0  = 0001;
   #900#2  = nDTipo;
   #900#6  = "            ";
   #900#9  = "01.01.1900";
   |SI FSalida = "POSICION";
       #900#0 = #0#0;
       #900#6 = #0#6;
   |FINSI;
|FPRC;

|PROCESO Cl900Baja3;
   #900#13 = enEmpresa;
   #900#0  = 9999;
   #900#2  = nHTipo;
   #900#6  = "zzzzzzzzzzzz";
   #900#9  = "31.12.2099";
|FPRC;

|PROCESO Cl910Alta4;
   #910#13 = enEmpresa;
   #910#9  = "01.01.0000";
   #910#0  = 0001;
   |SI FSalida = "POSICION";
       #910#9 = #0#9;
       #910#0 = #0#0;
   |FINSI;
|FPRC;

|PROCESO Cl910Baja4;
   #910#13 = enEmpresa;
   #910#9  = "31.12.2099";
   #910#0  = 9999;
|FPRC;

|CALCULO Tipo7C00; |TIPO 7;

   |HAZ SegunTipo;

   |CONTROL_SIMPLE 0, "LBOTON,F2 Consulta Asiento ", 0525, 0538, Boton2;
   nBoton2 = FSalida;

   |CONTROL_SIMPLE 0, "LBOTON,F7 Cambiar Estado ", 0625, 0638, Boton7;
   nBoton7 = FSalida;

   |CONTROL_SIMPLE 0, "LBOTON,F3 Cons.por Fecha Anotacion ", 0540, 0559, Boton3;
   nBoton3 = FSalida;

   |CONTROL_SIMPLE 0, "LBOTON,F4  Anotaciones GENERAL ", 0561, 0580, Boton4;
   nBoton4 = FSalida;

   |CONTROL_SIMPLE 0, "LBOTON,F5  Anotaciones CUENTAS ", 0640, 0659, Boton5;
   nBoton5 = FSalida;

   |CONTROL_SIMPLE 0, "LBOTON,F6  Anotaciones ASIENTO ", 0661, 0680, Boton6;
   nBoton6 = FSalida;

   |ESTADO_CONTROL nBoton2,  "DISABLE";  nSwBoton2  = 0;

   |ESTADO_CONTROL nBoton3,  "ENABLE";
   |ESTADO_CONTROL nBoton4,  "ENABLE";
   |ESTADO_CONTROL nBoton5,  "ENABLE";
   |ESTADO_CONTROL nBoton6,  "ENABLE";
   |ESTADO_CONTROL nBoton7,  "ENABLE";

   |SI nModo > 1; |Y #0#2 = 3;
       |ESTADO_CONTROL nBoton2,  "ENABLE";  nSwBoton2  = 1;
   |FINSI;

   |ENTLINEAL #1#1, -3, 7, 22, 0, AltaLinea, BajaLinea;
|FCAL;

|PROCESO Tipo5w1;  |TIPO 5;
       |LEE 040#900p;
       #900#8 = "R";
       |LINEAS_BI_ATRI #900#8, S, N, -1;
|FPRC;

|PROCESO Tipo7w1; |TIPO 7;
   nNume1 = #0#0;
   #0#0   = #900#0;
   #0#13  = #900#13;
   |C_V #1#1, 1, "N";
   |C_P #1#2, 1, 0930;
   |ENTLINEAL #1#1, -3, 7, 21, 0, AltaLinea, BajaLinea;
   |C_P #1#2, 1, 1329;
   |C_V #1#1, 1, "S";
   #0#0   = nNume1;
   #0#13  = enEmpresa;

   |SI #900#2 = 2;
       |PINTA 2302, #900#7;
   |FINSI;
   |SI #900#2 = 3;
       |PINTA 2302, #900#10;
       |PINTA 2306, #900#11;
   |FINSI;
|FPRC;

|PROCESO Tipo11w1; |TIPO 11;
   |SI FSalida ] 10; |Y FSalida [ 12; |Y #900#8 ! "X";
       aAlfa = "C";
       |SI FSalida = 10; aAlfa = "R"; |FINSI;
       |SI FSalida = 11; aAlfa = "P"; |FINSI;

       |LEE 141#900=;
       #900#8 = aAlfa;
       |GRABA 020#900a;
       |LIBERA #900;
       |PONCONTROL 23, "SI";

       |SI #900#2 = 3; |PTEC 806; |FINSI;
   |FINSI;
|FPRC;

|CALCULO Tipo11; |TIPO 11;
  nEst900 = 0;
  |HAZ QuitaBoton;

  inkey = FSalida;
  nModo = (FEntrada / 100) ! 0;
  |SI inkey = 10; |Y nSwBoton2  = 1;
      |HAZ VerTodoElAsto;
      |ERROR;
  |FINSI;

  |SI inkey = 15; |Y nModo ! 1;
      |LEE 141#0=;
       |SI #0#8 = "P"; aAlfa = "R"; |FINSI;
       |SI #0#8 = "R"; aAlfa = "C"; |FINSI;
       |SI #0#8 = "C"; aAlfa = "P"; |FINSI;

       #0#8 = aAlfa; |PINTA #0#8;
       |GRABA 020#0a;
       |LIBERA #0;

       |ERROR;
       |PTEC 808;
       |PTEC 809;
  |FINSI;

  |SI inkey ] 11; |Y inkey [ 14;
      ||... falta seleccion

      |PUSHV 0501 2380;

      nNumero = #0#0;
      nEst900 = 1;
      nDTipo  = 1;
      nHTipo  = 3;
      |SI inkey = 11; aAlfa2 = " por Fecha Anotacion"; |FINSI;
      |SI inkey = 12; aAlfa2 = " General            "; |FINSI;
      |SI inkey = 13; aAlfa2 = " Cuentas Contables  "; |FINSI;
      |SI inkey = 14; aAlfa2 = " Asientos Contables "; |FINSI;
      aAlfa1 =  "Consulta Anotaciones" + aAlfa2;

      |SI inkey = 11;   || Ordenado por Introduccion

          |HAZ BuscaAnotaciones;
          |SI nHay ! 0;

              |C_M #910#0,1,"N";
              |ENTLINEAL #4#910, -3, 5, 22, 0, Cl910Alta4, Cl910Baja4;
              |C_M #910#0,1,"S";

              nNumero = #910#0;
          |FINSI;
      |FINSI;

      |SI inkey = 12;
          nDTipo = 1;
          nHTipo = 1;
          |HAZ BuscaAnotaciones;
          |SI nHay ! 0;
              |ENTLINEAL #3#900, -5, 4, 21, 0, Cl900Alta3, Cl900Baja3;
              nNumero = #900#0;
          |FINSI;
      |FINSI;

      |SI inkey = 13;
          nDTipo = 2;
          nHTipo = 2;
          |HAZ BuscaAnotaciones;
          |SI nHay ! 0;
              |ENTLINEAL #3#900, -3, 4, 21, 0, Cl900Alta3, Cl900Baja3;
              nNumero = #900#0;
          |FINSI;
      |FINSI;
      |SI inkey = 14;
          nDTipo = 3;
          nHTipo = 3;
          |HAZ BuscaAnotaciones;
          |SI nHay ! 0;
              |ENTLINEAL #2#900, -4, 4, 21, 0, Cl900Alta2, Cl900Baja2;
              nNumero = #900#0;
          |FINSI;
      |FINSI;

      #0#0  = nNumero;
      #0#13 = enEmpresa;
      |LEE 041#0=;

      |PONCONTROL 23, "SI";
      |POPV;
  |FINSI;

|FCAL;

|CALCULO Tipo0; |TIPO 0;
   |HAZ QuitaBoton;
|FCAL;

|PROCESO QuitaBoton;
   |FIN_CONTROL_WINDOWS nBoton2;
   |FIN_CONTROL_WINDOWS nBoton7;
   |FIN_CONTROL_WINDOWS nBoton3;
   |FIN_CONTROL_WINDOWS nBoton4;
   |FIN_CONTROL_WINDOWS nBoton5;
   |FIN_CONTROL_WINDOWS nBoton6;
|FPRC;

|PROCESO Boton2;
  |PTEC 824;
|FPRC;

|PROCESO Boton3;
  |PTEC 825;
|FPRC;

|PROCESO Boton4;
  |PTEC 826;
|FPRC;

|PROCESO Boton5;
  |PTEC 827;
|FPRC;

|PROCESO Boton6;
  |PTEC 828;
|FPRC;

|PROCESO Boton7;
  |PTEC 829;
|FPRC;

|PROGRAMA;

   |HAZ inicio;

   |DFICB aPathFich;
   |DFICE aPathEste;

   |HAZ eVerNuevaAnotacion;

   aPath = aPathFich + "000000/";
   |PATH_DAT #900 aPath;

   aFichero = ("9y" + Usuario) % -108;
   |NOME_DAT #999 aFichero;

   |DBASE aMas;
   aDef  = aMas + "def/caextm01";
   |CARGA_DEF aDef, caextm01@;
   |SI FSalida ! 0;
       |MENAV "    Error Cargado Fichero de Contagen. Proceso Interrumpido";
       |ACABA;
   |SINO;
       aDef  = aMas + "def/caextm02";
       |CARGA_DEF aDef, caextm02@;
       |SI FSalida ! 0;
           |MENAV "    Error Cargado Fichero de Contagen. Proceso Interrumpido";
           |DESCARGA_DEF #caextm01@;
           |ACABA;
       |FINSI;
   |FINSI;

   |CLS;
   |CABEZA "Anotaciones Extracontables";

   |PINPA #0#0;

   |ENTLINEAL #1#0, -2, 2, 22, 0, ClaveAlta, ClaveBaja;

   |DESCARGA_DEF #caextm02@;
   |DESCARGA_DEF #caextm01@;
|FPRO;
