|FICHEROS;
   caarm001 #0,MANTE;
   caarm002 #1,MANTE;

   cacuenta #2;
   caarz001 #10;
   caclipro #12;
   cafantas #98;

   :/basica/def/agicen14 #114;
   :/basica/def/agicen17 #117;
   :/modelos/def/dsmom030 #130;

   caivaasi;
   calibros;

   canempre #30;

   activid@ #960;
   &espejo1@ #900;
|FIN;

|VARIABLES;
   &entrada  = 1;
   aAlfa     = "";
   aAlfa1    = "";
   aAlfa2    = "";
   aAlfa3    = "";
   nNume     = 0;
   nNume1    = 0;
   nNume2    = 0;
   fFecha    = @;
   aDCtaA    = "";
   aDCtaC    = "";
   aDCtaI    = "";
   aDCtaR    = "";

   nInkey     = 0;
   esalta    = 0;
   esaltaL   = 0;
   nSwSal    = 0;

   nAny1     = 0;
   nAny2     = 0;
   nMes1     = 0;
   nMes2     = 0;
   nMeses    = 0;

   fFecUlt     = @;
   nLinea      = 0;

   &nConta     = 0;
   &fFecConta  = @;
   &nEmpresa   = 0;
   &nPeriodo   = 0;

   nUltLin     = 0;

   &aFich1   = "";
   aFich2    = "";
   nYaCrea   = 0;
   aPositivo = "";
   aObligado = "";
   nConIRPF  = 0;
   nConIRPF1 = 0;
   aModIRPF1 = "";
   nSwModif  = 0;
   &aCPartida  = "";
   nCTiva     = 0;

   aModi      = "";
   nSwObli    = 0;

   nDLinea   = 0;
   nHLinea   = 0;
   fDFecha   = @;
   fHFecha   = @;

   &efDFecha = @;
   &efHFecha = @;
   &enContra = 0;
   &enConLin = 0;
   &elDiario = 0;
   &eSNBorro = "";
   nSiHago   = 0;

   &eaCampo4 = "";
   &efCampo5 = @;
   &enCampo6 = 0;
   &efCampo7 = @;
   &eaCampo8 = "";
   &eaCampo10 = "";
   &eaCampo11 = "";
   &eaCampo12 = "";
   &enCampo13 = 0;
   &eaCampo14 = "";
   &eaCampo15 = "";
   &eaCampo16 = "";
   &eaCampo17 = "";
   &eaCampo18 = "";
   &enCampo20 = 0;
   &eaCampo21 = "";
   &enCampo23 = 0;
   &eaCampo24 = "";
   &eaCampo25 = "";
   &eaCampo26 = "";
   &efCampo27 = @;
   &enCampo31 = 0;
   &enCampo32 = 0;
   &enCampo33 = 0;
   &enCampo34 = 0;
   &enCampo35 = 0;
   &eaCampo36 = "";
   &efLCamp3  = @;
   &enLCamp4  = 0;
   &enLCamp5  = 0;
   &enLCamp6  = 0;
   &enLCamp7  = 0;
   &enLCamp8  = 0;
   &enLCamp9  = 0;

   nSwCal     = 0;
   &enOpera   = 0;
   &eaModif   = "";

   nTrabajo   = 0;
   &enNoExis   = 0;
   &elDocumen  = 0;
   &NumDoc     = 0;
   &laFactura  = 0;
   &enContrato = 0;
   &eaActContra   = "";
   &eaSubContra   = "";
   &enDesBasica   = 0;
   &enDesContrato = 0;
   &enDesModelo   = 0;
   &enSwBUENO     = 0;
   nPulsoF9       = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoan_i;
|INCLUYE dscoiv_i;

**************************************************************************
|RUTINA ClaveBajaL;

     #1#0  = #0#0;
     #1#1  = #0#1;
     |SI FSalida = "POSICION";
         #1#2  = nUltLin - 1;
         |SI #1#2 < 1; #1#2 = 1; |FINSI;
     |SINO;
         #1#2  = 001;
     |FINSI;
|FRUT;

|RUTINA ClaveAltaL;
     #1#0  = #0#0;
     #1#1  = #0#1;
     #1#2  = 999;
|FRUT;

**************************************************************************
 LLAMADAS DESDE Cabecera
**************************************************************************
|CALCULO AntesAna; |TIPO 7;
 esalta = (FEntrada / 100) ! 0;
 |SI enSwAna = 1;
     |SI #0#4 = "I"; nNume1 = 14; |FINSI;
     |SI #0#4 = "G"; nNume1 = 16; |FINSI;
     eaCuenta = #0nNume1;
     |HAZ LeeTablaAna;
     |SI swRelac = 0;
         |SI esalta = 1; #0Campo = eaCtaAna; |FINSI;
         |C_M #0Campo,1,eaAnaMod;
     |SINO;
         |SI esalta = 1; #0Campo = ""; |FINSI;
         |SI eaPAna = "S"; |Y #0Campo = "       ";
             |C_M #0Campo,1,"N";
         |SINO;
             |C_M #0Campo,1,"S";
         |FINSI;
     |FINSI;
     |PINTA #0Campo;
     eaCtaAna = #0Campo;
     |HAZ eLimCtaAna7;
     |SI enBuenaAna ! 0; |C_M #0Campo,1,"S"; |FINSI;
 |FINSI;
|FCAL;

|CALCULO anacta; |TIPO 0;
   nInkey = FSalida;
   |SI nInkey = 2;  |ACABA; |FINSI;
   |SI enSwAna = 0; |ACABA; |FINSI;

   salidas = FSalida; |HAZ FPuntosAna;

   eOpAna = 0; || 1 = Consulta pantalla
   |SI nInkey = 10; eOpAna = 1;  |FINSI;
   eaCtaAna = #0Campo;
   |HAZ LeeCtaAna;
   |SI eswLect = 1;
        |MENAV "    Error.! No Existe Cuenta Analitica";
        |ERROR;
        |ACABA;
   |SINO;
        #0Campo = eaCtaAna;
        |PINTA #0Campo;
  |FINSI;

|| ...  Comprobar validez de la cuenta analitica segun limites definidos
   |HAZ eLimCtaAna;
   |SI enBuenaAna ! 0;
       |MENAV eaBuenaAna;
       |ERROR;
  |FINSI;
|| ---------------------------------------------------------
|FCAL;

|PROCESO BorraLineas;
 |SI nTrabajo = 1; |Y #1#10 = "S";
     |HAZ BajaAsto;
 |FINSI;
 |BORRA 140#1a;
 |LIBERA #1;
|FPRC;

|DEFBUCLE caarm002;
  #1#1;
  3;
  #0#0, #0#1, nDLinea, fDFecha;
  #0#0, #0#1, nHLinea, fHFecha;
  be;
  NULL, BorraLineas;
|FIN;

|CALCULO Tipo2_1;  |TIPO 2;
 esalta  = (FEntrada / 100) ! 0;
 |SI esalta ! 3;  |ACABA;  |FINSI;

 nLinea = 999;
 |HAZ ParaVerUltima;

 |SI nConta ! 0;
     aAlfa3 = "0000NConfirmar la baja del Contrato, tiene recibos contabilizados.";
 |SINO;
     aAlfa3 =  "0000SConfirma la baja del Contrato.";
 |FINSI;

 nTrabajo = 0;
 |CONFI aAlfa3;
 |SI FSalida ! 0;
     |ERROR;
 |SINO;
     |SI nConta ! 0;
         |CONFI "0000NBorrar los Asientos de los Recibos del Contrato";
         |SI FSalida = 0;
             nTrabajo = 1;
             |CONFI "0000NEsta Seguro de Continuar con la Baja";
             |SI FSalida ! 0;
                 |ERROR;
                 |ACABA;
             |FINSI;
         |FINSI;
     |FINSI;
     fDFecha = "01.01.0000";
     fHFecha = "31.12.2999";
     nDLinea = 000;
     nHLinea = 999;
     |BUCLE caarm002;
 |FINSI;
|FCAL;

|CALCULO PresentaLinea;  |TIPO 7;
    enEmpresa = #0#0;
    nSwSal    = 1;
    |HAZ ParaVerUltima;
    |ENTLINEAL #1#1, -3, 7, 22, 0, ClaveBajaL, ClaveAltaL;
    enConcepto = #0#31;
    |HAZ PintaConcepto;
|FCAL;

|PROCESO AntesDescripcion; |TIPO 7;

   |C_M #0#11, 1, "S";
   |C_M #0#12, 1, "S";

   esalta = (FEntrada / 100) ! 0;
   |SI enDesBasica = 1;
       |SI esalta = 1;
           #0#11 = eaActContra; |PINTA #0#11;
       |FINSI;
       |SI #0#12 = "   ";
           #0#12 = eaSubContra; |PINTA #0#12;
       |FINSI;
       |SI enContrato = 0;
           |C_M #0#11, 1, "N";
           |C_M #0#12, 1, "N";
       |FINSI;
   |FINSI;
|FPRC;

|CALCULO AntesDepar; |TIPO 7;
   |SI Haybasica = 1;
       |MENSA "    <F2> Consultar Actividades";
   |SINO;
       |MENSA "    <F2> Consultar Departamentos";
   |FINSI;
|FCAL;

|CALCULO TrasDepar; |TIPO 0;
   |SI eaDepar = "N"; |ACABA; |FINSI;
   eOpDep = 0; || 1 = Consulta pantalla
   |SI FSalida = 10; eOpDep = 1;  |FINSI;
   enEmpresa   = #0#0;
   enActividad = #0#11;
   eaCodDep    = #0#11;
   |HAZ Actividad;
   |SI eswLect = 1;
       |MENAV "    Error.! No Existe Actividad / Departamento";
       |ERROR;
   |SINO;
       #0#11 = eaCodDep;    |PINTA #0#11;
       #0#24 = eaNombreAct; |PINTA #0#24;
       |SI efActFin > "01.01.1900"; |Y snFilBa = "S";
           aAlfa = "    NAtencion!!!. Actividad con Fecha de BAJA " + efActFin + ", Desea introducir Facturas ? ";
           |CONFI aAlfa;
           |SI FSalida ! 0;
               |ERROR;
               |ACABA;
           |FINSI;
       |FINSI;
       |SI eaEsCom = "S";
           |CONFI "    NAtencion!!!. Actividad Comunera, ¨ Desea introducir Movimientos ? ";
           |SI FSalida ! 0;
               |ERROR;
               |ACABA;
           |FINSI;
       |FINSI;

       |SI enTipoTribut = 6;  |O enTipoTribut = 7;
           |O enTipoTribut = 11; |O enTipoTribut = 12;
              |O enTipoTribut = 16; |O enTipoTribut = 17;
           |MENAV "0000Esta Actividad es Agricola.  ";
           || |ERROR;
       |FINSI;
   |FINSI;
|FCAL;

|CALCULO AntesSubde; |TIPO 7;
   |SI enDesBasica = 0;
       |MENSA "    Subdepartamento: Referencias Catastrales <F2> Consultar <F3> Modificar";
   |SINO;
       |MENSA "    Subdepartamento: Referencias Catastrales <F2> Consultar";
   |FINSI;
|FCAL;

|CALCULO TrasSubde; |TIPO 0;

   eOpSub = 0; || 1 = Consulta pantalla
   |SI FSalida = 10; eOpSub = 1;  |FINSI;
   |SI enDesBasica = 0;
       |SI FSalida = 11; eOpSub = 2;  |FINSI;
       enDesContrato = 1;
   |FINSI;

   enConcepto  = 1;
   eaCodSubdep = #0Campo;
   enQue = 1;
   |HAZ SubActividad;
   enQue = 0;
   |SI eswLect = 1;
       |MENAV "    Error.! No Existe Subdepartamento";
       |ERROR;
   |SINO;
       #0#12 = eaCodSubdep; |PINTA #0#12;
       #0#25 = eaNomSubdep; |PINTA #0#25;
   |FINSI;
   enDesContrato = 0;
|FCAL;

|CALCULO AntesTipo; |TIPO 7;
   esalta = (FEntrada / 100) ! 0;
   aAlfa = "S";
   |SI esalta ! 1;  aAlfa = "N";  |FINSI;
   |C_M #0#4, 1, aAlfa;
|FCAL;

|PROCESO ParaVerCon;
       enLibIVA  = #0#35;
       enFilConc = 2;          || Conceptos de IVA
       |HAZ CreaConcepto;
       aFich1    = eaFiCon;
       enFilConc = 3;          || Conceptos de IRPF
       |HAZ CreaConcepto;
       aFich2 = eaFiCon;
       nYaCrea = 1;
|FPRC;

|CALCULO elTipo0; |TIPO 0;
   nSwModif = 0;
   |SI Contenido ! #0#4; nSwModif = 1; |FINSI;

   #0#28 = "";
   |SI #0#4 = "I";
       #0#28 = "INGRESO";
       aDCtaC = "43";
       aDCtaA = "7";
       aDCtaI = "477";
       aDCtaR = "473";
       aTipoIVA = "R";
   |FINSI;
   |SI #0#4 = "G";
       #0#28 = "GASTO";
       aDCtaA = "41";
       aDCtaC = "6";
       aDCtaI = "472";
       aDCtaR = "";
       aTipoIVA = "S";
   |FINSI;
   |PINTA #0#28;

   enAParam = #0#11;
   |HAZ LeeDefIVA;
   |SI #0#4 = "I"; dConc1 = 704; |FINSI;
   |SI #0#4 = "G"; dConc1 = 602; |FINSI;
   dConc2 = dConc1;

   |SI esalta = 1;
       #0#35 = dLibro; |PINTA #0#35;
       aAlfa = #0#14; |QBF aAlfa; |SI aAlfa = ""; |O nSwModif = 1; #0#14 = aDCtaA; |PINTA #0#14; |FINSI;
       aAlfa = #0#16; |QBF aAlfa; |SI aAlfa = ""; |O nSwModif = 1; #0#16 = aDCtaC; |PINTA #0#16; |FINSI;
       aAlfa = #0#18; |QBF aAlfa; |SI aAlfa = ""; |O nSwModif = 1; #0#18 = aDCtaI; |PINTA #0#18; |FINSI;
       aAlfa = #0#21; |QBF aAlfa; |SI aAlfa = ""; |O nSwModif = 1; #0#21 = aDCtaR; |PINTA #0#21; |FINSI;
       |SI #0#31 [ 1; |O nSwModif = 1; #0#31 = dConc1;  |PINTA #0#31; |FINSI;
       |SI #0#32 [ 1; |O nSwModif = 1; #0#32 = dConc1;  |PINTA #0#32; |FINSI;
       |SI #0#33 [ 1; |O nSwModif = 1; #0#33 = dConc2;  |PINTA #0#33; |FINSI;
       |SI #0#34 [ 1; |O nSwModif = 1;
           #0#34 = nDCIrpf; |PINTA #0#34;
           #0#23 = dTpIrpf; |PINTA #0#23;
           |HAZ ConDefecto;
       |FINSI;

       #0#36 = dCoPag; |PINTA #0#36;
       |SI #0#20 = 0; |O nSwModif = 1;
           efFechaIVA = #0#5;
           enLineaIVA = dTiva;
           |HAZ SacaIVA;
           #0#20 = enPorcIVA; |PINTA #0#20;
       |FINSI;
   |FINSI;

   |SI nYaCrea = 0; |O nSwModif = 1;
       |HAZ ParaVerCon;
   |FINSI;
|FCAL;

|CALCULO libro; |TIPO 0;

   nSwModif = 0;
   |SI #0Campo ! Contenido;  nSwModif = 1; |FINSI;

   |DDEFECTO #calibros;
   |ABRE #calibros;
   #calibros#0 = #0Campo;
   |LEE 000#calibros.=;
   |CIERRA #calibros;

   |SI #0#4 = "I";
       |SI #calibros#2 ! "R";
           |MENAV "    ATENCION!!! Libro de I.V.A. Soportado ...";
           |ERROR;
       |FINSI;
   |SINO;
       |SI #calibros#2 ! "S";
           |MENAV "    ATENCION!!! Libro de I.V.A. Repercutido ...";
           |ERROR;
       |FINSI;
   |FINSI;

   |SI nYaCrea = 0; |O nSwModif = 1;
       |HAZ ParaVerCon;
   |FINSI;
|FCAL;

|CALCULO Elsmesos; |TIPO 0;
  aAlfa = Contenido;
  |SI aAlfa ! #0#6; |Y #0#6 ! 0;
      nNume   = 0 + (#0#6/1000);
      fFecha  = #0#5 + nNume;
      nNume   = fFecha;
      nNume   = nNume - 1;
      fFecha  = nNume;
      #0#7    = fFecha; |PINTA #0#7;
  |FINSI;
|FCAL;

|CALCULO FecFPago; |TIPO 0;
  aAlfa = Contenido;

  |SI #0#7 > "01.01.0000";

      |SI #0#7 < #0#5;
          |MENAV "0000Error. Fecha final del contrato inferior a fecha inicial";
          |ERROR;
          |ACABA;
      |FINSI;

      |SI #0#7 ! aAlfa;
          fFecha = #0#7;
||          nNume = fFecha;
||          nNume  = nNume + 1;
||          fFecha = nNume;

          nMeses = 0;
          aAlfa  = fFecha; aAlfa1 = aAlfa % -104; aAlfa2 = aAlfa % 0402;
                         nAny1  = aAlfa1;       nMes1  = aAlfa2;
          aAlfa  = #0#5; aAlfa1 = aAlfa % -104; aAlfa2 = aAlfa % 0402;
                         nAny2  = aAlfa1;       nMes2  = aAlfa2;
          |SI nAny1 ! nAny2;
              nMeses = 12 * (nAny1 - nAny2 - 1);
              nMeses = nMeses + nMes1 + (12 - nMes2);
          |SINO;
              nMeses = nMes1 - nMes2;
          |FINSI;
          #0#6 = nMeses; |PINTA #0#6;
      |FINSI;
  |FINSI;
|FCAL;

|CALCULO Elperiodo; |TIPO 0;
   #0#29 = "";
   |SI #0#8 = "M"; #0#29 = "MENSUAL   "; |FINSI;
   |SI #0#8 = "T"; #0#29 = "TRIMESTRAL"; |FINSI;
   |SI #0#8 = "S"; #0#29 = "SEMESTRAL "; |FINSI;
   |SI #0#8 = "A"; #0#29 = "ANUAL     "; |FINSI;
   |PINTA #0#29;
|FCAL;

|CALCULO Antescuenta; |TIPO 7;
   aAlfa = #0Campo; |QBF aAlfa;
   aAlfa = aAlfa % 0;
   nNume1 = aAlfa;
   |SI nNume1 > 0; |Y nNume1 < 4;
       |PTEC 816;
   |FINSI;
   |SI Campo = 14; |O Campo = 16;
       |SI Campo = 14; aAlfa1 = "0000Cuenta de Abono (Cuenta al haber)"; |FINSI;
       |SI Campo = 16; aAlfa1 = "0000Cuenta de Cargo (Cuenta al debe)";  |FINSI;
       aAlfa2 = ". Ctrl-F2 Utilidades Cuentas";
       aAlfa3 = ". Ctrl-F2 Util.Ctas, <F12> Introd. por NIF ";
       |SI #0#4 = "G";
           aAlfa3 = ". Ctrl-F2 Utilidades Cuentas";
           aAlfa2 = ". Ctrl-F2 Util.Ctas, <F12> Introd. por NIF ";
       |FINSI;
       aAlfa2 = aAlfa1 + aAlfa2;
       aAlfa3 = aAlfa1 + aAlfa3;

       |SI Campo = 14;
           aAlfa1 = aAlfa2;
       |SINO;
           aAlfa1 = aAlfa3;
       |FINSI;
       |MENSA aAlfa1;
   |FINSI;
|FCAL;

|CALCULO cuenta1; |TIPO 6;
   emCta = 1;        || Dar mensajes de error

   |SI FSalida = 9;
       nPulsoF9 = 1;
       |SUB_EJECUTA_NP "cacugen1";
       |SI enSwBUENO = 0;
           #0Campo = eaCuenta;
           |PINTA #0Campo;
       |SINO;
           nPulsoF9 = 0;
           |ERROR;
           |ACABA;
      |FINSI;
   |FINSI;

   eaCuenta = #0Campo;
   salidas  = FSalida;
   |HAZ FiltraPuntos;
   #0Campo = eaCuenta;
   |PINTA #0Campo;

   |HAZ SacaNivel;
   FSalida = salidas;
|FCAL;

|CALCULO refresca1; |TIPO 0;
   nSwModif = 0;
   aAlfa    = Contenido;

   emCta = 1;        || Dar mensajes de error
   eaCuenta = #0Campo;
   |HAZ LeeCuenta;
   |SI eswCta ! 0; |ACABA; |FINSI;

   nNume1   = Campo + 1;
   #0nNume1 = #cacuenta#2; |PINTA #0nNume1;

   |SI Campo = 14; |O Campo = 16;
       aAlfa1 = #0Campo; |QBF aAlfa1;
       |SI aAlfa1 = "";
           |MENAV "0000Introduzca una cuenta";
           |ERROR;
           |ACABA;
       |FINSI;
   |FINSI;

   |SI aAlfa ! #0Campo;
       nSwModif = 1;
       |SI #0#4 = "I";
           |SI Campo = 14; aAlfa = aDCtaA; aAlfa1 = #0Campo % 101; |FINSI;
           |SI Campo = 16; aAlfa = aDCtaC; aAlfa1 = #0Campo % 102; |FINSI;
       |SINO;
           |SI Campo = 14; aAlfa = aDCtaA; aAlfa1 = #0Campo % 102; |FINSI;
           |SI Campo = 16; aAlfa = aDCtaC; aAlfa1 = #0Campo % 101; |FINSI;
       |FINSI;

       |SI Campo = 18; aAlfa = aDCtaI; aAlfa1 = #0Campo % 103; |FINSI;
       |SI Campo = 21; aAlfa = aDCtaR; aAlfa1 = #0Campo % 103; |FINSI;
       |SI aAlfa ! aAlfa1; |Y aAlfa1 > "   ";
           |CONFI "0000NEs correcta esta Cuenta?";
           |SI FSalida ! 0; |ERROR; |FINSI;
       |FINSI;
   |FINSI;
   |SI aTipoIVA = "S"; |Y Campo = 14; nNume1 = 16; |HAZ DatosCaclipro; |FINSI;
   |SI aTipoIVA = "R"; |Y Campo = 16; nNume1 = 14; |HAZ DatosCaclipro; |FINSI;
|FCAL;

|CALCULO Tipo7C34; |TIPO 7;
    aAlfa = "S";
    |SI #0#21 = doce;
        aAlfa   = "N";
        #0Campo = 0; |PINTA #0Campo;
    |FINSI;
    |C_M #0Campo, 1, aAlfa;
    enConcepto = #0Campo;
    |SI enConcepto ! 0; |HAZ PintaConcepto; |FINSI;
|FCAL;

|PROCESO LaCtaIRPF;
    |ABRE #dsmom030;
    #dsmom030#0 = #0#34;
    |LEE 041#dsmom030.=;
    |SI FSalida = 0;
        nNume2 = 22; |SI #0#4 = "I"; nNume2 = 20; |FINSI;
        aAlfa = #dsmom030#nNume2#; |QBF aAlfa;
        |SI aAlfa ! ""; #0#21 = aAlfa; |PINTA #0#21; |FINSI;
        #0#23 = #dsmom030#93; |PINTA #0#23;
    |FINSI;
    |CIERRA #dsmom030;
|FPRC;

|PROCESO DatosCaclipro;
   aCPartida = "";
   nConIRPF  = 0;
   nCTiva    = 0;
   |ABRE #caclipro;
   |SI aTipoIVA = "R";
       #caclipro#23 = "C";
   |SINO;
       #caclipro#23 = "P";
   |FINSI;
   #caclipro#10 = #0Campo;
   |LEE 000#caclipro.=;
   |SI FSalida = 0;
       nCTiva       = #caclipro#42;
       aCPartida    = #caclipro#21; |QBF aCPartida;
       |SI snReten ! "N";
           nConIRPF  = #caclipro#52;
       |FINSI;
   |FINSI;
   |CIERRA #caclipro;
   |SI nSwModif = 1;
       |SI nConIRPF ! 0;
           #0#34 = nConIRPF; |PINTA #0#34;
           |HAZ LaCtaIRPF;
       |FINSI;
       |SI aCPartida ! ""; #0nNume1 = aCPartida; |PINTA #0nNume1; |FINSI;
   |FINSI;
|FPRC;

|PROCESO ConDefecto;
   |ABRE #dsmom030;
   #dsmom030#0 = #0#31;
   |LEE 041#dsmom030.=;
   |SI FSalida = 0;
       |SI #dsmom030#98 ! 0;
           #0#34 = #dsmom030#98; |PINTA #0#34;
           #dsmom030#0 = #0#34;
           |LEE 041#dsmom030.=;
           |SI FSalida = 0; |Y #dsmom030#93 ! 0;
               #0#23 = #dsmom030#93; |PINTA #0#23;
           |FINSI;
       |FINSI;
   |FINSI;
   |CIERRA #dsmom030;
|FPRC;

|PROCESO PintaConcepto;
   aAlfa = "";
   |ABRE #dsmom030;
   #dsmom030#0 = enConcepto;
   |LEE 041#dsmom030.=;
   |SI FSalida = 0;
       aAlfa = #dsmom030#1;
   |FINSI;
   |CIERRA #dsmom030;
   |ATRI S; |PINTA 2402, aAlfa; |ATRI 0;
|FPRC;

|PROCESO AntesConcepto; |TIPO 7;
   enConcepto = #0Campo;
   |SI enConcepto > 0;
       |HAZ PintaConcepto;
   |FINSI;
|FPRC;

|CALCULO MiraConcepto;  |TIPO 0;  ||Concepto Abono y Cargo

   |SI FSalida = 2; |ACABA; |FINSI;

   nSwModif = 0;
   |SI Contenido ! #0Campo; nSwModif = 1; |FINSI;

   |SI FSalida = 10;
       eaFiCon    = aFich1;
       enConcepto = #0Campo;
       |HAZ VerConcepto;
       #0Campo = enConcepto; |PINTA #0Campo;
   |FINSI;

   enLibIVA  = #0#35;
   enFilConc = 2;          || Concepto de IVA
   enConcepto = #0Campo;
   |HAZ FiltroConcepto;  |SI eswLect ! 0; |ACABA; |FINSI;

   |SI eaDepar = "S";
       |SI Haybasica = 1;
           enFilRegIVA = 1;
           |HAZ FilRegimenIVA;
           |SI eswLect ! 0; |ACABA; |FINSI;
       |FINSI;
   |FINSI;

   aPositivo = #dsmom030#96;
   aObligado = #dsmom030#97;
   nConIRPF1 = #dsmom030#98;
   aModIRPF1 = #dsmom030#99;
   |SI snReten = "N";
       aObligado = " ";
       nConIRPF1 = 0;
       aModIRPF1 = " ";
   |FINSI;
   || si en el Caivaasi se informa que no hay retenciones
   || no hace caso al control del concepto.

   |C_M #0#34, 1, snReten;
   |SI aObligado = "S";
       |SI nConIRPF ! 0;
           #0#34 = nConIRPF;
       |SINO;
           #0#34 = nConIRPF1;
       |FINSI;
       |PINTA #0#34;
       |SI aModIRPF1 = "N"; |C_M #0#34, 1, "N"; |FINSI;
   |FINSI;

   |SI aObligado = "N"; |Y snFiltr = "S";
       #0#34 = 0; |PINTA #0#34;
       |C_M #0#34, 1, "N";
   |FINSI;

   |SI nSwModif = 1;
       |SI Campo = 31; #0#32 = #0#31; |PINTA #0#32;
                       #0#33 = #0#31; |PINTA #0#33; |FINSI;
       |SI Campo = 32; #0#33 = #0#32; |PINTA #0#33; |FINSI;
   |FINSI;

   |HAZ PintaConcepto;
|FCAL;

|PROCESO FiltroObligado;
   |C_M #0Campo,0,aAlfa;
   |SI aAlfa = "N"; |ACABA; |FINSI;

   aAlfa2 = "concepto";
   aAlfa1 = "sin Retencion";
   |SI aObligado = "S"; aAlfa1 = "con Retencion Obligada"; |FINSI;

   nSwObli = 0;
   |SI snFiltr ! "X";
       |SI snFiltr = "S";
           aAlfa = "    Factura " + aAlfa1;
           |SI aObligado = "S"; aAlfa = aAlfa  + ", introduzca " + aAlfa2; |FINSI;
           |MENAV aAlfa;
           nSwObli = 1;
       |SINO;
           aAlfa = "    NFactura "+ aAlfa1 + ", continuar";
           |CONFI aAlfa;
           |SI FSalida ! 0;
               nSwObli = 1;
           |FINSI;
       |FINSI;
   |FINSI;

   |SI nSwObli = 1;
           #0#34 = nConIRPF1;
           |SI nConIRPF ! 0; #0#34 = nConIRPF; |FINSI;
           |SI aObligado = "N"; #0#34 = 0; |FINSI;
           |PINTA #0#34;
       |ERROR;
   |FINSI;
|FPRC;

|CALCULO TrasConcepto2;  |TIPO 0;     ||Concepto IRPF
   |SI FSalida = 2; |ACABA; |FINSI;

   |C_M #0Campo,0,aModi;
   |SI aModi = "N"; |ACABA; |FINSI;

   nSwModif = 0;
   |SI Contenido ! #0Campo; nSwModif = 1; |FINSI;

   |SI FSalida = 10;
       eaFiCon = aFich2;
       enConcepto = #0#34;
       |HAZ VerConcepto;
       #0#34 = enConcepto; |PINTA #0#34;
   |FINSI;

   |SI #0#34 ! 0; |Y aObligado = "N";
       |HAZ FiltroObligado;
       |SI nSwObli = 1; |ACABA; |FINSI;
   |FINSI;

   |SI #0#34 = 0;
       |SI aObligado = "S";
           |HAZ FiltroObligado;
           |SI nSwObli = 1; |ACABA; |FINSI;
       |FINSI;
       |ACABA;
   |FINSI;

   |SI #0#34 < nDCIrpf; |O #0#34 > nHCIrpf;
       |SI snFiltr ! "X";
           |MENAV "    Error, Concepto Contable no Correcto ";
       |FINSI;
       |SI snFiltr = "S";
           |ERROR;
           |ACABA;
       |FINSI;
   |FINSI;

   enLibIVA  = #0#35;
   enFilConc = 3;        || Concepto de IRPF
   enConcepto = #0#34;
   |HAZ FiltroConcepto;  |SI eswLect ! 0; |ACABA; |FINSI;

   |SI nSwModif = 1; |Y #dsmom030#93 ! 0;
       #0#23 = #dsmom030#93; |PINTA #0#23;
   |FINSI;
   |HAZ PintaConcepto;
|FCAL;

|CALCULO Tipo7CX; |TIPO 7;
    aAlfa = "S";
    nNume1 = Campo - 2;
    |SI #0nNume1 = doce;
        aAlfa   = "N";
        #0Campo = 0; |PINTA #0Campo;
    |FINSI;
    |C_M #0Campo, 1, aAlfa;
|FCAL;

|CALCULO fecactiva; |TIPO 7;
 aAlfa = "S";
 |SI #0#10 = "A";
     aAlfa = "N";
     #0#27 = "01.01.0000";
 |SINO;
     |SI #0#27 < "01.01.1900";
         #0#27 = ~;
     |FINSI;
 |FINSI;
 |PINTA #0#27;
 |C_M #0#27,1,aAlfa;
|FCAL;

|CALCULO fecactiva0; |TIPO 0;
  |SI #0#10 = "I";
      |SI #0#27 < "01.01.1900";
          |MENAV "    Introduzca fecha a partir de la que el contrato esta inactivo";
          |ERROR;
      |FINSI;
  |FINSI;
|FCAL;

|CALCULO AntesCobroPago;  |TIPO 7;
  aAlfa1 = "0000<P> Asto pago automatico,<T> Asto traspaso, <N> Solo hara Asto del Recibo";
  |SI #0#4 = "I";
      aAlfa1 = "0000<P> Asto cobro automatico,<T> Asto traspaso, <N> Solo hara Asto del Recibo";
  |FINSI;
  |MENSA aAlfa1;
|FCAL;

|CALCULO elTipo11; |TIPO 11;
  nInkey = FSalida;
  esalta = (FEntrada / 100) ! 0;
  |SI nInkey = 10;
      |BLIN 4;
      |SI esalta = 2;
          nLinea  = 999;
          |HAZ AltaRecibos;
          |ERROR;
      |FINSI;
  |FINSI;
  |SI nInkey = 17;
      |BLIN 4;
      |SI esalta = 2;
          nLinea  = 999;
          |HAZ BajaRecibos;
          |ERROR;
      |FINSI;
  |FINSI;
|FCAL;

|CALCULO elTipo3; |TIPO 3;
    enEmpresa = #0#0;
    esalta   = (FEntrada / 100) ! 0;
    |SI esalta = 1;
        nLinea  = 999;
        |HAZ AltaRecibos;
    |FINSI;
    nSwSal    = 0;
    |ENTLINEAL #1#1, -3, 1, 22, 0, ClaveBajaL, ClaveAltaL;
    nYaCrea   = 0;
|FCAL;

|| ***************************************************************************
||   Calculos de las Lineas de Contratos
|| ***************************************************************************
|PROCESO VerEspejo;
   |SI fFecUlt < #900#3; |Y #900#2 < nLinea;
       fFecUlt = #900#3;
   |FINSI;
   |SI #900#10 = "S";
       nConta = nConta + 1;
   |FINSI;
   nUltLin = #900#2;
|FPRC;

|DEFBUCLE VerUltima;
   #900#1003;
   ;
   #0#0, #0#1, 001;
   #0#0, #0#1, 999;
   ;
   NULL, VerEspejo;
|FIN;

|PROCESO PedirDatos;
       nSiHago = 0;
       |PUSHV 1220 1860;
       |BLANCO 1220 1860;
       |CUADRO 1220 1860;

       |ATRI R; |PINTA 1321, " Generar Recibos                       "; |ATRI 0;
       |PINTA 1422, "De Fecha:           ";
       |PINTA 1522, " A Fecha:           ";
       |PINTA 1622, "Modificar los Recibos ya existentes ";

       |C_P #cafantas#10,1,1432; |PINTA 1432, #cafantas#10;
       |C_P #cafantas#11,1,1532; |PINTA 1532, #cafantas#11;
       |C_P #cafantas#15,1,1658; |PINTA 1658, #cafantas#15;

|ET Primero;
       |ENCAMPO #10#cafantas;
       |SI FSalida = 7;  nSiHago = 1;  |ACABA; |FINSI;
       |SI FSalida = 1;  |VETE Salir; |FINSI;

|ET Segundo;
       |ENCAMPO #11#cafantas;
       |SI FSalida = 7;  nSiHago = 1;  |ACABA; |FINSI;
       |SI FSalida = 1;  |VETE Salir;   |FINSI;
       |SI FSalida = 2;  |VETE Primero; |FINSI;

       |SI nUltLin ! 0;
           |ENCAMPO #15#cafantas;
           |SI FSalida = 7;  nSiHago = 1;  |ACABA; |FINSI;
           |SI FSalida = 2;  |VETE Segundo; |FINSI;
       |FINSI;

|ET Salir;
       nSwCal = 0;
       |SI #0#10 = "I";
           |SI #0#27 ! "01.01.0000"; |Y #0#27 < #cafantas#10;
               |MENAV "0000Contrato Inactivo en las Fechas indicadas";
               nSwCal = 1;
            |FINSI;
       |SINO;
            |SI #0#5 ! "01.01.0000"; |Y #0#5 > #cafantas#11;
                |MENAV "0000Fecha Inicio de Contrato superior a las fechas indicadas";
                nSwCal = 1;
            |SINO;
                |SI #0#7 ! "01.01.0000"; |Y #0#7 < #cafantas#10;
                   |MENAV "0000Contrato finalizado en las fechas indicadas";
                   nSwCal = 1;
                |FINSI;
            |FINSI;
       |FINSI;
       |SI nSwCal = 1;
           |CONFI "0000NVolver a Seleccionar Fechas";
           |SI FSalida = 0;
               |VETE Primero;
           |SINO;
               nSiHago = 1;
               |ACABA;
           |FINSI;
       |FINSI;

       |CONFI "0000SRealizar Proceso";
       |SI FSalida ! 0;
           nSiHago = 1;
       |FINSI;
|FPRC;

|PROCESO PedirDatos2;
   nSiHago = 0;
   |PUSHV 1220 1760;
   |BLANCO 1220 1760;
   |CUADRO 1220 1760;

   #cafantas#20 = "N";

   |ATRI R; |PINTA 1321, " Contabilizar Recibo                   "; |ATRI 0;

   |PINTA 1422, " Dejar Asientos Modificables: "; |C_P #cafantas#15,1,1452; |PINTA 1452, #cafantas#15;
   |PINTA 1522, " Diario Contable ...........: "; |C_P #cafantas#22,1,1552; |PINTA 1552, #cafantas#22;
   |PINTA 1622, " Contabilizar este Recibo ..: "; |C_P #cafantas#20,1,1652; |PINTA 1652, #cafantas#20;

|ET Primero2;
   |ENCAMPO #15#cafantas;
   |SI FSalida = 7;  nSiHago = 1;  |ACABA; |FINSI;
   |SI FSalida = 1;  |VETE Salir2; |FINSI;

|ET Segundo2;
   |ENCAMPO #22#cafantas;
   |SI FSalida = 7;  nSiHago = 1;  |ACABA; |FINSI;
   |SI FSalida = 1;  |VETE Salir2;   |FINSI;
   |SI FSalida = 2;  |VETE Primero2; |FINSI;

   |ENCAMPO #20#cafantas;
   |SI FSalida = 7;  nSiHago = 1;  |ACABA; |FINSI;
   |SI FSalida = 2;  |VETE Segundo2; |FINSI;

|ET Salir2;
   |SI #cafantas#20 = "N";
       nSiHago = 1;
   |FINSI;
   eaModif  = #cafantas#15;
   elDiario = #cafantas#22;
|FPRC;

|PROCESO PedirDatos3;
       nSiHago = 0;
       |PUSHV 1220 1860;
       |BLANCO 1220 1860;
       |CUADRO 1220 1860;

       |ATRI R; |PINTA 1321, " Dar de Baja Recibos                   "; |ATRI 0;
       |PINTA 1422, "De Fecha:           ";
       |PINTA 1522, " A Fecha:           ";
       |PINTA 1622, "Borrar los Asientos de los Recibos: ";

       |C_P #cafantas#10,1,1432; |PINTA 1432, #cafantas#10;
       |C_P #cafantas#11,1,1532; |PINTA 1532, #cafantas#11;
       |C_P #cafantas#15,1,1658; |PINTA 1658, #cafantas#15;

|ET Primero3;
       |ENCAMPO #10#cafantas;
       |SI FSalida = 7;  nSiHago = 1;  |ACABA; |FINSI;
       |SI FSalida = 1;  |VETE Salir3; |FINSI;

|ET Segundo3;
       |ENCAMPO #11#cafantas;
       |SI FSalida = 7;  nSiHago = 1;    |ACABA; |FINSI;
       |SI FSalida = 1;  |VETE Salir3;   |FINSI;
       |SI FSalida = 2;  |VETE Primero3; |FINSI;

       |ENCAMPO #15#cafantas;
       |SI FSalida = 7;  nSiHago = 1;  |ACABA; |FINSI;
       |SI FSalida = 2;  |VETE Segundo3; |FINSI;

|ET Salir3;
       |CONFI "0000NRealizar Proceso";
       |SI FSalida ! 0;
           nSiHago = 1;
       |FINSI;
|FPRC;

|PROCESO AltaRecibos;
   aAlfa = "0000NDesea realizar el Calculo de los Recibos";
   |SI esalta = 1;
       aAlfa = "0000SDesea realizar el Calculo de los Recibos";
   |FINSI;
   |CONFI aAlfa;
   |SI FSalida = 0;

       |HAZ ParaVerUltima;
       |HAZ CalNuevaFecha;

       |SI #0#7 [ fFecha; |Y #0#7 > "01.01.0000";
           |MENAV "0000Calculados todos los Recibos de Este Contrato";
           |CONFI "0000NDesea Recalcularlos";
           |SI FSalida ! 0;
               |ACABA;
           |FINSI;

           nNume = 0 - (11/1000);
           fFecha  = fFecha + nNume;
           #cafantas#10 = fFecha;
           #cafantas#11 = #0#7;
           #cafantas#15 = "S";

       |SINO;

           #cafantas#10 = fFecha;
           nNume = 0 + (11/1000);
           fFecha  = fFecha + nNume;
           |SI fFecha > #0#7; |Y #0#7 > "01.01.0000";
               nNume  = #0#7;
               nNume  = nNume + 1;
               fFecha = nNume;
           |FINSI;
           #cafantas#11 = fFecha;
           #cafantas#15 = "N";

       |FINSI;

       |HAZ PedirDatos;
       |SI nSiHago = 0;
           efDFecha  = #cafantas#10;
           efHFecha  = #cafantas#11;
           enContra  = #0#1;
           eSNBorro  = #cafantas#15;
           |HAZ RecojoDatos;
           |SUB_EJECUTA_NP "caarz003;generar";
       |FINSI;
       |POPV;
       |PONCONTROL 23, "SI";
   |FINSI;
|FPRC;

|PROCESO BajaRecibos;
  nLinea = 999;
  |HAZ ParaVerUltima;
  |SI nUltLin = 0; |ACABA; |FINSI;

  |SI nConta ! 0;
      aAlfa3 = "0000NDesea dar de baja los Recibos, hay recibos contabilizados.";
  |SINO;
      aAlfa3 =  "0000SDesea dar de baja los Recibos";
  |FINSI;

  nTrabajo = 0;
  |CONFI aAlfa3;
  |SI FSalida ! 0;

      |ACABA;

  |SINO;

      |HAZ PedirDatos3;
      |SI nSiHago = 0;
          |SI #cafantas#15 = "S"; nTrabajo = 1; |FINSI;

          fDFecha = #cafantas#10;
          fHFecha = #cafantas#11;
          nDLinea = 001;
          nHLinea = 999;
          |BUCLE caarm002;
      |FINSI;
      |POPV;
      |PONCONTROL 23, "SI";
   |FINSI;
|FPRC;

|PROCESO RecojoDatos;
   eaCampo4  = #0#4;
   efCampo5  = #0#5;
   enCampo6  = #0#6;
   efCampo7  = #0#7;
   eaCampo8  = #0#8;
   eaCampo10 = #0#10;
   eaCampo11 = #0#11;
   eaCampo12 = #0#12;
   enCampo13 = #0#13;
   eaCampo14 = #0#14;
   eaCampo15 = #0#15;
   eaCampo16 = #0#16;
   eaCampo17 = #0#17;
   eaCampo18 = #0#18;
   enCampo20 = #0#20;
   eaCampo21 = #0#21;
   enCampo23 = #0#23;
   eaCampo24 = #0#24;
   eaCampo25 = #0#25;
   eaCampo26 = #0#26;
   efCampo27 = #0#27;
   enCampo31 = #0#31;
   enCampo32 = #0#32;
   enCampo33 = #0#33;
   enCampo34 = #0#34;
   enCampo35 = #0#35;
   eaCampo36 = #0#36;
|FPRC;

|| ********************************************************************
|PROCESO BajaAsto;
   nEmpresa   = #1#0;
   enPeriodo  = #1#11;
   enLibro    = #1#12;
   NumDoc     = #1#13;
   enOpera    = 3;
   |HAZ Consultas;
|FPRC;

|PROCESO ModiAsto;
   nEmpresa   = #1#0;
   enPeriodo  = #1#11;
   enLibro    = #1#12;
   NumDoc     = #1#13;
   enOpera    = 2;
   |HAZ Consultas;

   |SI enNoExis = 1;
       enEmpresa = #1#0;
       efDFecha  = #1#3;
       efHFecha  = #1#3;
       enContra  = #1#1;
       enConLin  = #1#2;
       |HAZ RecojoDatos;
       efLCamp3  = #1#3;
       enLCamp4  = #1#4;
       enLCamp5  = #1#5;
       enLCamp6  = #1#6;
       enLCamp7  = #1#7;
       enLCamp8  = #1#8;
       enLCamp9  = #1#9;
       |SUB_EJECUTA_NP "caarz003;contamodi";
   |FINSI;
   |PONCONTROL 23, "SI";
|FPRC;

|PROCESO Contabilizar;
   nConta    = 0;
   nEmpresa  = #1#0;
   fFecConta = #1#3;
   |HAZ BuscoPeriodo;
   |SI nConta = 0;
       |MENAV "0000No existe Ejercicio Contable";
       |ACABA;
   |FINSI;

   |HAZ PedirDatos2;
   |SI nSiHago = 0;
       enEmpresa = #1#0;
       enPeriodo = nPeriodo;
       efDFecha  = #1#3;
       efHFecha  = #1#3;
       enContra  = #1#1;
       enConLin  = #1#2;
       |HAZ RecojoDatos;
       |SUB_EJECUTA_NP "caarz003;contanuevo";
   |FINSI;
   |POPV;
   |PONCONTROL 23, "SI";
|FPRC;

|PROCESO BorrarSoloAsto;
   nConta    = 0;
   nEmpresa  = #1#0;
   fFecConta = #1#3;
   |HAZ BuscoPeriodo;
   |SI nConta = 0;
       |MENAV "0000No existe Ejercicio Contable";
       |ACABA;
   |FINSI;

   |CONFI "0000SEstas seguro de Borrar el Asiento del Recibo";
   |SI FSalida = 0;

       |LEE 141#1=;

       |HAZ BajaAsto;
       #1#10 = "N";
       #1#11 = -1;
       #1#12 = 0;
       #1#13 = 0;
       #1#14 = "";
       |GRABA 020#1a;
       |LIBERA #1;

       |PONCONTROL 23, "SI";
   |FINSI;
|FPRC;
|| ********************************************************************
|CALCULO Tipo11_L; |TIPO 11;
  nInkey = FSalida;
  esaltaL = (FEntrada / 100) ! 0;
  |SI nInkey = 10;
      |BLIN 4;
      nLinea = 999;
      |HAZ AltaRecibos;
      |ERROR;
  |FINSI;

  |SI nInkey = 11;
      |BLIN 4;
      |SI esaltaL ! 1; |Y #1#10 ! "S";
          |HAZ Contabilizar;
          |SI nConta = 1;
              |PTEC 803;
          |SINO;
              |ERROR;
          |FINSI;
      |SINO;
          |ERROR;
      |FINSI;
  |FINSI;

  |SI nInkey = 12; |O nInkey = 13;
      |BLIN 4;
      |SI esaltaL = 2; |Y #1#10 = "S"; |Y #1#11 ! -1;
          enPeriodo  = #1#11;
          enLibro    = #1#12;
          NumDoc     = #1#13;
          enOpera    = nInkey;
          |HAZ Consultas;
      |FINSI;
      |ERROR;
  |FINSI;

  |SI nInkey = 14;
      |BLIN 4;
      |SI esaltaL ! 1; |Y #1#10 = "S";
          |HAZ BorrarSoloAsto;
          |SI nConta = 1;
              |PTEC 803;
          |SINO;
              |ERROR;
          |FINSI;
      |SINO;
          |ERROR;
      |FINSI;
  |FINSI;

  |SI nInkey = 17;
      |BLIN 4;
      nLinea  = 999;
      |HAZ BajaRecibos;
      |ERROR;
  |FINSI;
|FCAL;

|CALCULO SalgoYa;  |TIPO 7;
  |SI nSwSal = 1;  |PTEC 807;  |FINSI;
|FCAL;

|CALCULO losCalculos; |TIPO 0;
  |SI #1Campo = Contenido; |ACABA; |FINSI;

  |HAZ losCalculos1;
|FCAL;

|CALCULO losCalculos1; |TIPO 0;
  |SI Campo = 4; |O Campo = 5;
      #1#6 = ((#1#4 * #1#5) / 100) ! 2; |PINTA #1#6;
  |FINSI;
  |SI Campo = 4; |O Campo = 7;
      #1#8 = ((#1#4 * #1#7) / 100) ! 2; |PINTA #1#8;
  |FINSI;
  #1#9 = #1#4 + #1#6 - #1#8;        |PINTA #1#9;
|FCAL;

|CALCULO Tipo7_L;  |TIPO 7;
  esaltaL = (FEntrada / 100) ! 0;

  aAlfa = "0000<F2> Generar nuevos Recibos, <F9> Baja Recibos";
  |SI esaltaL ! 1;
      |SI #1#10 = "S";
          aAlfa = "0000F2-Generar Recibos, F4-Cons. Fra, F5-Cons. Asto, F6-Baja Asto, F9-Baja Recibos";
      |SINO;
          aAlfa = "0000<F2> Generar nuevos Recibos, <F3> Contabilizar Recibo, <F9> Baja Recibos";
      |FINSI;
  |FINSI;
  |MENSA aAlfa;

  aAlfa = #1#14; |QBF aAlfa;
  |SI aAlfa = "";
      enConcepto = #0#31;
      |HAZ PintaConcepto;
  |SINO;
      |ATRI S; |PINTA 2402, aAlfa; |ATRI 0;
  |FINSI;
|FCAL;

|CALCULO Tipo0_L; |TIPO 0;
  |BLIN 4;
  nLinea  = #1#2;
  |HAZ ParaVerUltima;

  esaltaL = (FEntrada / 100) ! 0;
  |SI esaltaL = 1;
      |SI #0#30 = "B";
          #1#4  = #0#13;
      |SINO;
          #1#4  = (#0#13 / (1 + #0#20/100 - #0#23/100)) ! 2;
      |FINSI;
                          |PINTA #1#4;
      #1#5  = #0#20;      |PINTA #1#5;
      #1#7  = #0#23;      |PINTA #1#7;

      Campo = 4;
      |HAZ losCalculos1;
      Campo = 2;
      |SI #1#2 = 1;
          #1#3 = #0#5;
      |SINO;
          |HAZ CalNuevaFecha;
          #1#3 = fFecha;
      |FINSI;
      |PINTA #1#3;
  |FINSI;
|FCAL;

|CALCULO Tipo3_L;  |TIPO 3;
   esaltaL = (FEntrada / 100) ! 0;
   |SI esaltaL = 2; |Y #1#10 = "S";
       |SI nTrabajo = 1;
           |HAZ ModiAsto;
       |SINO;
           #1#10 = "N";
       |FINSI;
   |FINSI;
|FCAL;

|CALCULO Tipo1_L;  |TIPO 1;

   |SI esalta = 3; |ACABA; |FINSI;

   nTrabajo  = 0;
   esaltaL = (FEntrada / 100) ! 0;

   |SI #1#10 = "S";
       |AVISO;
       |CONFI "2400N** Linea Contabilizada **. Continuar (S/N) ";
       |SI FSalida ! 0;
           |ERROR;
       |SINO;
           aAlfa = "0000NBorrar";
           |SI esaltaL = 2; aAlfa = "0000SModificar"; |FINSI;
           aAlfa = aAlfa + " el Asiento de este Recibo ";
           |CONFI aAlfa;
           |SI FSalida = 0;
               nTrabajo = 1;
               |SI esaltaL = 3;
                   |CONFI "0000NEsta Seguro de Continuar con la Baja";
                   |SI FSalida ! 0;
                       |ERROR;
                   |SINO;
                       |HAZ BajaAsto;
                   |FINSI;
               |FINSI;
           |FINSI;
        |FINSI;
    |FINSI;
|FCAL;

|CALCULO Fecha_L;
   |SI #1#3 > #0#7; |Y #0#7 > "01.01.0000";
       |MENAV "0000Error. Fecha mayor a la fecha final Contrato";
       |ERROR;
   |SINO;
       |SI #1#3 < #0#5;
           |MENAV "0000Error. Fecha inferior a la fecha inicio Contrato";
           |ERROR;
       |SINO;
           |SI #1#3 < fFecUlt;
               |MENAV "0000Error. Fecha inferior a la fecha del ultimo Movimiento Introducido";
               |ERROR;
           |FINSI;
       |FINSI;
   |FINSI;
|FCAL;

|PROCESO CalNuevaFecha;
   |SI nUltLin = 0;
       fFecha = fFecUlt;
   |SINO;
       nMeses = 1; |SI #0#8 = "T"; nMeses = 3;  |FINSI;
                   |SI #0#8 = "S"; nMeses = 6;  |FINSI;
                   |SI #0#8 = "A"; nMeses = 12; |FINSI;
       nNume   = 0 + (nMeses/1000);
       fFecha  = fFecUlt + nNume;
   |FINSI;
   |SI fFecha > #0#7; |Y #0#7 > "01.01.0000";
       nNume  = #0#7;
       nNume  = nNume + 1;
       fFecha = nNume;
   |FINSI;
|FPRC;

|PROCESO ParaVerUltima;
  fFecUlt = #0#5;
  nConta  = 0;
  nUltLin = 0;
  |BUCLE VerUltima;
|FPRC;
