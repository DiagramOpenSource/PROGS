|INCLUYE dscont_i;
|FICHEROS;
   caestadi #0;
   canempre #30;
   camaasil #3;
   caexistl #4;
|FIN;

|VARIABLES;
   mens1 = "    Codigo de Empresa Inexistente !!";
   mens2 = "    Ejercicios No Definidos !!!!";
   infor = "caestadi";
   direc1 = "";
   direc2 = "";
   varcampo = 0;
   linea1 = "ctlipun";
   linea2 = "";
   codig1 = "";
   codig2 = "";
   ||empre = 0;  || indices
   nombr = 0;  || de
   exist = 0;  || variables
   perio = 0;
   fecalf = "";
   mes = 0;
   grupoalf = "";
   swgrupo = 0;
   &g = "";          || codigo
   &n = "";          || nombre
   &t = 0;          || explotacion
   &v = 0;          || existencias finales
   x = 10;
   y = 47;
   xy = 0;
   &anyo = "";
   dif_exi = 0;
   ini = 0;
   &fin = 0;
   d_mes = 0;
   h_mes = 0;
   nume1 = 0;
   nume2 = 0;
   fecdes = @;
   fechas = @;
   alfa1 = "";
   &r_emp = 0;
   &r_per = 0;
|FIN;

|PROCESO consulta;
   |SI FSalida ! 9;  |ACABA;  |FINSI;
   r_emp = 0;
   |SUB_EJECUTA "caconemp.run";
   |ERROR;
   |SI r_emp = 0; |ACABA; |FINSI;
   |SI Campo = 64; |O Campo = 65;
      #caestadi#Campo# = r_emp;
      |PTEC 802;
   |FINSI;
   |SI Campo [ 63;
      #caestadi#Campo# = r_emp;
      varcampo = Campo + 20;
      #caestadi#varcampo# = r_per;
      |PTEC 802;
      |PTEC 802;
   |FINSI;
|FPRC;

|PROCESO mayor;
   #caestadi#Campo# = #caestadi#Campo# > #caestadi#Campo#;
   |PINTA #caestadi#Campo#;
|FPRC;

|PROCESO salta1; |TIPO 7;
   varcampo = Campo - 1;
   |SI #caestadi#varcampo# = 0;
      |PARA x = Campo;  |SI x [ 19;  |HACIENDO x = x + 1;
         |CAMPO_MODIFICA #caestadi#x#, 1,"N";
      |SIGUE;
   |SINO;
      |CAMPO_MODIFICA #caestadi#Campo#, 1,"S";
   |FINSI;
|FPRC;

|PROCESO salta2; |TIPO 7;
   varcampo = Campo - 20;
   |SI #caestadi#varcampo# = 0;
      |PARA x = Campo; |SI x [ 39;  |HACIENDO x = x + 1;
         |CAMPO_MODIFICA #caestadi#x#, 1,"N";
      |SIGUE;
   |SINO;
      |CAMPO_MODIFICA #caestadi#Campo#, 1,"S";
   |FINSI;
|FPRC;

|PROCESO salta3; |TIPO 7;
   |SI #caestadi#63 = "N";
      |CAMPO_MODIFICA #caestadi#Campo#, 1,"S";
   |SINO;
      |CAMPO_MODIFICA #caestadi#Campo#, 1,"N";
   |FINSI;
|FPRC;

|PROCESO nomodif; |TIPO 7;
   |SI #caestadi#63 = "S";
      |CAMPO_MODIFICA #caestadi#Campo#, 1,"S";
   |SINO;
      |CAMPO_MODIFICA #caestadi#Campo#, 1,"N";
   |FINSI;
|FPRC;

|PROCESO comproba; |TIPO 0;
   nume1 = Campo - 20;
   |SI #caestadi#nume1# ! 0;|Y FSalida  ! 2;
      |ABRE #30;
      #canempre#2 = #caestadi#nume1#;
      #canempre#3 = #caestadi#Campo#;
      |LEE 040#30=;
      |SI FSalida ! 0;
         |MENAV mens1;
         |ERROR;
      |FINSI;
      |CIERRA #30;
   |FINSI;
|FPRC;

|PROCESO acumuline;
   |HAZ mesleido;
   nume2 = mes + 67;
   #caestadi#nume2# = #caestadi#nume2# + 1;
   |SI d_mes [ mes; |Y h_mes ] mes;
      |HAZ grupo;
      |SI swgrupo = 0;
         |SI #camaasil#8 = "D";
            t = t - #camaasil#9;
         |SINO;
            t = t + #camaasil#9;
         |FINSI;
      |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE leelineas;
   #3#1;
   ;
   00,fecdes,000000,0000;
   99,fechas,999999,9999;
   be;
   NULL,acumuline;
|FIN;

|PROCESO empresas;
   |DFICB direc2;                 || carga el directorio raiz de empresas
   codig1 = #canempre#2;
   codig2 = "00000" + codig1;
   codig2 = codig2 % -105;
   direc2 = direc2 + codig2 + #canempre#3;
   direc2 = direc2 + "/";     || construye directorio de la empresa especifica
   |HAZ lee_existen;
   |PATH_DAT #camaasil #direc2#;           || se situa en directorio de empresa
   |ATRI I; |ATRI P;
   |PINTA 2330, codig2;
   |ATRI 0;
   fecdes = "01.01.0000";
   fechas = "31.12.3000";
   |BUCLE leelineas;
   n = #canempre#1;         || nombre
   g = #canempre#2;         || cod.empresa
   anyo = #canempre#26;     || periodo
   v = dif_exi;
   t = t + v;
   |PRINT;
   |HAZ limpieza;
|FPRC;

|DEFBUCLE desdehas;
   #30#1;
   ;
   #0#64,#0#67;
   #0#65,#0#67;
   be;
   NULL,empresas;
|FIN;

|| *************************************************************************

|PROCESO meollo; |TIPO 3;      || las empresas van desde 0 a 19
   |IMPRE 0;
   |SI FSalida ! 0;  |ACABA;  |FINSI;
   |INFOR infor;
   |SI FSalida ! 0;  |FINIMP; |ACABA;  |FINSI;
   |SI #caestadi#40 = 0;   d_mes = 1;   |SINO;  d_mes = #caestadi#40;  |FINSI;
   |SI #caestadi#41 = 13;  h_mes = 12;  |SINO;  h_mes = #caestadi#41;  |FINSI;
   |ABRE  #30;
   |SI #caestadi#63 = "N";
      |HAZ desfijos;
   |SINO;
      |BUCLE desdehas;
   |FINSI;
   |CIERRA #30;
   |PIEINF;
   |FININF;
   |FINIMP;
|FPRC;

|PROCESO desfijos;
   |PARA empre = 0; |SI #caestadi#empre# ! 0; |Y empre [ 19;  |HACIENDO empre = empre + 1;
      nombr = empre + 43;        || campo del nombre de la empresa
      perio = empre + 20;        || campo periodo
      |DDEFECTO #30;
      #canempre#2 = #caestadi#empre#;
      #canempre#3 = #caestadi#perio#;
      |LEE 040#30=;
      |DFICB direc2;                 || carga el directorio raiz de empresas
      codig1 = #caestadi#empre#;
      codig2 = "00000" + codig1;
      codig2 = codig2 % -105;
      direc2 = direc2 + codig2 + #caestadi#perio#;
      direc2 = direc2 + "/";     || construye directorio de la empresa especifica
      |HAZ lee_existen;
      |ATRI I; |ATRI P;
      |PINTA 2330, codig2;
      |ATRI 0;
      |PATH_DAT #camaasil #direc2#;           || se situa en directorio de empresa
      fecdes = "01.01.0000";
      fechas = "31.12.3000";
      |BUCLE leelineas;
      g = #canempre#2;        || codigo
      n = #canempre#1;        || nombre
      anyo = #canempre#26;    || periodo
      v = dif_exi;
      t = t + v;
      |PRINT;
      |ET otraempre;
      |HAZ limpieza;
   |SIGUE;
|FPRC;

|PROCESO mesleido;
   fecalf = #camaasil#1;
   fecalf = fecalf % -107;
   fecalf = fecalf % 102;
   mes = fecalf;
|FPRC;


|PROCESO grupo;
   swgrupo = 0;
   grupoalf = #camaasil#4 % 101;
   |SI grupoalf ! 7; |Y grupoalf ! 6; |Y grupoalf ! 3;
      swgrupo = 1;
   |SINO;
      |SI grupoalf = 3; |Y dif_exi = 0;
         swgrupo = 1;
      |FINSI;
   |FINSI;
   |SI #camaasil#0 = 0;|O #camaasil#0 = 99;
      swgrupo = 1;
   |FINSI;
|FPRC;

|PROCESO limpieza;
   #caestadi#68 = 0;  #caestadi#69 = 0;  #caestadi#70 = 0;  #caestadi#71 = 0;  #caestadi#72 = 0;  #caestadi#73 = 0;
   #caestadi#74 = 0;  #caestadi#75 = 0;  #caestadi#76 = 0;  #caestadi#77 = 0;  #caestadi#78 = 0;  #caestadi#79= 0;
   t = 0;
|FPRC;

|PROCESO lee_ini1;
   nume1 = #caestadi#40 + 11; nume2 = #caestadi#41 + 11
   ini = ini + #caexistl#nume1#;
   fin = fin + #caexistl#nume2#;
|FPRC;

|DEFBUCLE lee_ini;
   #4#1;
   ;
   #0#80, 0;
   #0#80, 99;
   e;
   NULL, lee_ini1;
|FIN;

|PROCESO lee_existen;
   ini = 0;   fin = 0;
   |PATH_DAT #caexistl #direc2#;
   |BUCLE lee_ini;
   dif_exi = fin - ini;
|FPRC;
