|FICHEROS;
   caz00019 #0;

   camaasic #10;
   camaasil #11;
   camandia #12;
   canempre #30;
|FIN;

|VARIABLES;
   nSalir     = 0;

   aClave     = "";
   aPassword  = "";
   nSwNO      = 0;
   fFecDef    = @;

   nNumAstos  = 0;
   aNumAstos  = "";
   primero    = "";
   temporal1  = "";
   aAlfa1     = "";
|FIN;

|INCLUYE dscont_i;
|INCLUYE impdat_i;
|| *******************************************************************

|CALCULO AntesFecha; |TIPO 7;
 |SI #0#11 > "01.01.0000";
     nSalir = 0;
     |ET Clave;
     |HAZ MiraClave;
     |SI nSwNO = 1;
         nSalir = nSalir + 1;
         |SI nSalir ] 3;
             |PTEC 807;
         |SINO;
             |VETE Clave;
         |FINSI;
     |FINSI;
 |FINSI;
 #0#12 = fFecDef;
|FCAL;

|CALCULO DespuesFecha; |TIPO 0;
     aPassword = "";
     |BLANCO 2302 2379;
     |PINTA 2324, "Entre Nueva CLAVE: ";
     E_inf = "";
     E_sup = "10";
     aPassword = 2343 ? -1;
     |BLANCO 2302 2379;
     #0#13 = aPassword;
|FCAL;

|PROCESO MiraClave;
     nSwNO = 0;
     aPassword = "";
     |BLANCO 2302 2379;
     |PINTA 2327, "Entre la CLAVE: ";
     E_inf = "";
     E_sup = "10";
     aPassword = 2343 ? -1;
     |QBF aPassword;
     |SI aPassword ! aClave;
         |MENAV "     Clave incorrecta.";
         nSwNO = 1;
     |FINSI;
     |BLANCO 2302 2379;
|FPRC;

|| ::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::1

|CALCULO VoyaBloquear; |TIPO 3;
  |ABRE #30;
  #30#0 = #0#0;
  #30#1 = #0#1;
  |LEE 101#30=;
  |SI FSalida = 0;
      #30#34 = #0#12;
      #30#35 = #0#13;
      |GRABA 020#30a;
  |FINSI;
  |LIBERA #30;
  |CIERRA #30;

  |SI #30#34 < fec2; |O #30#34 = "01.01.0000";
      |SI nHay01 = 1;
          |CONFI "    SBorrar la Entrada de Datos Contables afectos al IS";
          |SI FSalida = 0;
              |HAZ BSodacoca;
          |FINSI;
      |FINSI;
  |FINSI;
|FCAL;

||*********************************************************************
|PROCESO ElUltimo;
  #10#0 = #12#0;
  #10#1 = "31.12.2999";
  #10#2 = 999999;
  |LEE 010#10];
  |SI FSalida = 0;
      |LEE 040#10a;
  |SINO;
      |LEE 040#10u;
  |FINSI;

  |SI #10#1 > #0#8;
      #0#7 = #10#0;
      #0#8 = #10#1;
      #0#9 = #10#2;
  |FINSI;
|FPRC;

|DEFBUCLE LosDiarios;
 #12#1;
 ;
 00;
 99;
 e;
 NULL, ElUltimo;
|FIN;

|PROCESO LosDatos;

 |DDEFECTO #0;
 #0#0 = enEmpresa;
 #0#1 = enPeriodo;
 #0#2 = eaNombreEmp;
 #0#3 = enEjercicio;
 #0#4 = fec1;
 #0#5 = fec2;

 nNumAstos = 0;
 #0#6 = 00;
 #0#7 = "01.01.0000";
 #0#8 = 0;
 |PATH_DAT #10 dirfich0;
 |PATH_DAT #11 dirfich0;
 |PATH_DAT #12 dirfich0;

  nNumAstos = 0;
  primero   = "camaasic";
  temporal1 = dirfich0;
  |LEEDEDEF primero, temporal1;
  |SI FSalida ] 0;
      temporal1 = FSalida % 375;
      aNumAstos = (A\temporal1 % 1615);
      nNumAstos = aNumAstos;
  |FINSI;
  #0#6 = nNumAstos;

  |ABRE #10;
  |BUCLE LosDiarios;
  |CIERRA #10;
  |ABRE #11;
  #11#0 = #0#7;
  #11#1 = #0#8;
  #11#2 = #0#9;
  #11#3 = 1;
  |LEE 001#11];
  |SI FSalida = 0; |Y #11#0 = #0#7; |Y #11#1 = #0#8; |Y #11#2 = #0#9;
      #0#10 = #11#7;
  |FINSI;
  |CIERRA #11;

  aClave = #30#35; |QBF aClave;
  #0#11 = fec3;
  |SI #0#11 > "01.01.0000";
      fFecDef = #0#11;
  |SINO;
      fFecDef = #0#8;
  |FINSI;
|FPRC;

|| -----------------------------------------------------------------------
|PROCESO PantaImp;
    aTexImp = "- " + aTexImp + "                                                   ";
    aTexImp = aTexImp % 0151;
    |ATRI R;
    |PINTA 1506, " ATENCION datos en: ";
    |SI nHay01 = 1;
        |PINTA 1526,"- Entrada General de Datos Contables afectos al IS ";
    |FINSI;
    |SI nHay04 = 1;
        |SI nHay01 = 0; |PINTA 1526, aTexImp; |FINSI;
        |SI nHay01 = 1;
            |PINTA 1606, "                    ";
            |PINTA 1626, aTexImp;
        |FINSI;
    |FINSI;
    |ATRI r;
|FPRC;
|| -----------------------------------------------------------------------

|PROGRAMA;
 |CLS;
 |CABEZA "Bloquear Datos Contables";

 |HAZ LeoLaEmpresa;
 |HAZ DirAplicSt;

 |DDEFECTO #0;
 |PINPA #0#0;
 |DFICE dirfich0;
 |HAZ LosDatos;
 |SI aPContas ! "";
     nEmpImp = enEmpresa;
     nEjeImp = enEjercicio;
     |HAZ MiroImpuesto;
     |SI nHay01 = 1; |O nHay04 = 1;
         |HAZ PantaImp;
     |FINSI;
 |FINSI;

 |PINDA #0#0;

 |ENDATOS #1#0;                  || Seleccion informe
|FPRO;

|PROCESO LeoLaEmpresa;
   swerror = 0;
   |QBF fich_alta;
   |SI fich_alta ! "";
       empresa = fich_alta;
   |SINO;
       |DFICE empresa;
   |FINSI;
   aAlfa1 = empresa  % -107;
   aAlfa1 = aAlfa1 %  106;

   clave1 = aAlfa1 %  105; cod1 = clave1;
   clave2 = aAlfa1 % -101; cod2 = clave2;
   fec1 = "01.01.0000";
   fec2 = "01.01.0000";
   fec3 = "01.01.0000";

   |ABRE #30;
   |DDEFECTO #30;
   #30#2 = cod1;
   #30#3 = cod2;
   |LEE 000#30=;
   |SI FSalida ! 0;
       swerror = 1;
       |SI cod1 ! 0; |Y cod2 ! 0;
          |MENAV "    No existe Empresa !!!";
       |FINSI;
       |CIERRA #30;
       |ACABA;
   |FINSI;

   enEmpresa   = #30#2;
   enPeriodo   = #30#3;
   eaNombreEmp = #30#1;
   eaNifEmp    = #30#8;
   |CIERRA #30;

   cod2 = #30#26;
   tra1 = 100 + cod2;
   clave2 = tra1 % -102;
   tra1 = 100 + #30#11;
   mes1 = tra1 % -102;
   |SI cod2 < 80;
      work1 = "01." + mes1 + ".20" + clave2;
      enEjercicio = 2000 + #30#26;
   |SINO;
      work1 = "01." + mes1 + ".19" + clave2;
      enEjercicio = 1900 + #30#26;
   |FINSI;
   fec1 = work1;
   tra1 = 100 + #30#12;
   mes1 = tra1 % -102;
   |SI #30#11 > #30#12;
      cod2 = cod2 + 1;
      |SI cod2 > 99; cod2 = cod2 - 100;    |FINSI;
   |FINSI;
   dia1 = "31";
   |SI #30#12 = 4; |O #30#12 = 6; |O #30#12 = 9; |O #30#12 = 11;
      dia1 = "30";
   |FINSI;
   |SI #30#12 = 2;
      dia1 = "28";
      |SI cod2 = 88; |O cod2 = 92; |O cod2 = 96; |O cod2 = 0; |O cod2 = 4;
          |O cod2 = 8; |O cod2 = 12; |O cod2 = 16; |O cod2 = 20;
         dia1 = "29";
      |FINSI;
   |FINSI;
   tra1 = 100 + cod2;
   clave2 = tra1 % -102;
   |SI cod2 < 80;
      work1 = dia1 + "." + mes1 + ".20" + clave2;
   |SINO;
      work1 = dia1 + "." + mes1 + ".19" + clave2;
   |FINSI;
   fec2 = work1;
   fec3 = #30#34;
|FPRC;
