|FICHEROS;
   luxeg017 #0;

   luxeg008 #10;
   luxeg007 #11;
   luxeg002 #12;
   luxeg003 #13;
   luxeg001 #14;
   luxeg016 #15;
   luxeg004 #16;

   cajasald #21;
   camaniva #22;
   caconasi #23;
   caivases #24;
   caivalin #25;

   caenlace #100;
|FIN;

|INCLUYE dscont_i;

|VARIABLES;
   nNivel    = 0;
   nCalc     = 0;
   nCont     = 0;
   nLinea    = 0;
   nAnyo     = 0;
   Documento = 0;
   nTipoIVA  = 0;
   nNumFact  = 0;
   nNumLineaIVA = 0;
   nNumAcumIVA  = 0;
   nSwHay    = 0;

   aAlfa  = "";
   aCont  = "";

   fFechaCont = @;
|FIN;

|PROCESO ContadorFra;
      |ABRE #caivases;
      #caivases#0 = #luxeg002#14;
      #caivases#1 = "S";
      |LEE 101#caivases.=;
      |SI FSalida ! 0;
         |DDEFECTO #caivases;
         #caivases#0 = #luxeg002#14;
         #caivases#1 = "S";
         |GRABA 020#caivases.b;
      |FINSI;
      nNumFact = #caivases#3;
      #caivases#3 = #caivases#3 + #caivases#4;
      |GRABA 020#caivases.a; |LIBERA #caivases;
      |CIERRA #caivases;

      |ABRE #camaniva; |SELECT #3#camaniva;
      #camaniva#0 = #luxeg002#13;
      #camaniva#2 = #luxeg002#14;
      #camaniva#3 = nNumFact;
      |LEE 000#camaniva.=;
      |SI FSalida = 0;
         #camaniva#0 = #luxeg002#13;
         #camaniva#2 = #luxeg002#14;
         #camaniva#3 = nNumFact;
         |LEE 000#camaniva.];
         |PARA; |SI FSalida = 0; |Y #camaniva#0 = #luxeg002#13; |Y #camaniva#2 = #luxeg002#14; |HACIENDO;
            nNumFact = #camaniva#3 + 1;
            |LEE 000#camaniva.s;
         |SIGUE;
      |FINSI;
      |SELECT #1#camaniva; |CIERRA #camaniva;
|FPRC;

|PROCESO MiraNivel;
   nNivel = 0;
   aAlfa = #caenlace#4; |QBF aAlfa;
   aAlfa = aAlfa % 0; nCalc = aAlfa;

   |PARA nCont = 14; |SI nCont [ 19; |HACIENDO nCont = nCont + 1;
      |SI #48nCont = nCalc; nNivel = nCont - 13; |SAL; |FINSI;
   |SIGUE;
|FPRC;

|PROCESO ApuntesLineas;
   || Cada linea lo que hace es una base del registro de iva soportado

   #luxeg001#0 = #luxeg008#4;
   |LEE 000#luxeg001.=;
   |SI FSalida ! 0; |DDEFECTO #luxeg001; |FINSI;

   #luxeg003#0 = #luxeg008#2;
   |LEE 000#luxeg003.=;
   |SI FSalida ! 0;
      |DDEFECTO #luxeg003;
      #luxeg003#3 = " ";
   |FINSI;

   nTipoIVA = #luxeg001#2;
   aAlfa = #luxeg007#1; |QBF aAlfa; aAlfa = aAlfa % -104;
   nAnyo = aAlfa;

   aTipoIVA = "S";
   enEjercicio = nAnyo;
   |SI #luxeg017#6 ! "01.01.0000";
      efFechaIVA = #luxeg017#6;
   |SINO;
      efFechaIVA = #luxeg007#2;
   |FINSI;
   enLineaIVA = 0;
   enPorcIVA = nTipoIVA; enPorcREC = 0;
   |HAZ BuscaTipoIVA;
   |SI enLineaIVA < 0; enLineaIVA = 0; |FINSI;

   nNumLineaIVA = 0;
   || Creo la parte del asiento de la factura de soportado .....
   |DDEFECTO #caenlace;
   #caenlace#0  = #luxeg002#2;
   #caenlace#1  = fFechaCont;
   #caenlace#2  = Documento;
   #caenlace#3  = 1;
   #caenlace#37 = #48#2;
   #caenlace#38 = #48#3;
   |LEE 000#caenlace.];
   |PARA; |SI FSalida = 0; |Y #caenlace#0 = #luxeg002#2; |Y #caenlace#1 = fFechaCont;
    |Y #caenlace#2 = Documento; |Y #caenlace#37  = #48#2; |Y #caenlace#38  = #48#3; |HACIENDO;
      |SI #caenlace#26 = "I";
         |SI eaCtaIVA = #caenlace#4;
            nNumLineaIVA = #caenlace#3;
         |FINSI;
      |FINSI;
      |LEE 000#caenlace.s;
   |SIGUE;

   |SI nNumLineaIVA = 0;
      |DDEFECTO #caenlace;
      #caenlace#0 = #luxeg002#2;
      #caenlace#1 = fFechaCont;
      #caenlace#2 = Documento;
      #caenlace#3 = nLinea; nLinea = nLinea + 10;
      #caenlace#4 = eaCtaIVA;
      |HAZ MiraNivel;
      #caenlace#5 = nNivel;
      #caenlace#6 = #luxeg002#5;
      aAlfa = #luxeg002#6; |QBF aAlfa;
      |SI aAlfa = "";
         #caenlace#7 = "Factura liquidacion";
      |SINO;
         #caenlace#7 = #luxeg002#6;
      |FINSI;
      aAlfa = #caenlace#7; |QBF aAlfa;
      aAlfa = aAlfa + " parte n§ " + #luxeg007#0;
      #caenlace#7 = aAlfa;
      #caenlace#8   = "D";
      #caenlace#10  = "S";
      #caenlace#11  = #luxeg002#13;
      #caenlace#12  = #luxeg002#14;
      #caenlace#13  = nNumFact;
      #caenlace#15  = #cajasald#6;
      #caenlace#26  = "I";
      #caenlace#27  = 0;
      |SI #luxeg017#6 ! "01.01.0000";
         #caenlace#29  = #luxeg017#6;
      |SINO;
         #caenlace#29  = #luxeg007#2;
      |FINSI;
      #caenlace#37  = #48#2;
      #caenlace#38  = #48#3;
      #caenlace#51  = #luxeg007#2;
      |GRABA 020#caenlace.c;
   |SINO;
      #caenlace#0 = #luxeg002#2;
      #caenlace#1 = fFechaCont;
      #caenlace#2 = Documento;
      #caenlace#3 = nNumLineaIVA;
      |LEE 000#caenlace.=;
   |FINSI;
   #caenlace#9 = #caenlace#9 + (#luxeg008#6 ! 2);
   |GRABA 020#caenlace.a;

   |DDEFECTO #caenlace;
   #caenlace#0 = #luxeg002#2;
   #caenlace#1 = fFechaCont;
   #caenlace#2 = Documento;
   #caenlace#3 = nLinea; nLinea = nLinea + 10;
   #caenlace#4 = #luxeg008#11;
   |HAZ MiraNivel;
   #caenlace#5 = nNivel;
   #caenlace#6 = #luxeg002#11;
   aAlfa = #luxeg002#12; |QBF aAlfa;
   |SI aAlfa = "";
      #caenlace#7 = #luxeg008#3;
   |SINO;
      #caenlace#7 = #luxeg002#12;
   |FINSI;
   aAlfa = #caenlace#7; |QBF aAlfa;
   aAlfa = aAlfa + " parte n§ " + #luxeg007#0;
   #caenlace#7 = aAlfa;
   #caenlace#8   = "D";
   #caenlace#9   = #luxeg008#5;
   #caenlace#10  = "S";
   #caenlace#11  = #luxeg002#13;
   #caenlace#12  = #luxeg002#14;
   #caenlace#13  = nNumFact;
   #caenlace#15  = #cajasald#6;
   #caenlace#26  = "B";
   #caenlace#27  = nNumAcumIVA; nNumAcumIVA = nNumAcumIVA + 1;
   #caenlace#28  = nTipoIVA;
   |SI #luxeg017#6 ! "01.01.0000";
      #caenlace#29  = #luxeg017#6;
   |SINO;
      #caenlace#29  = #luxeg007#2;
   |FINSI;
   #caenlace#37  = #48#2;
   #caenlace#38  = #48#3;
   #caenlace#51  = #luxeg007#2;
   |GRABA 020#caenlace.n;
|FPRC;

|DEFBUCLE ApuntesLineas;
#luxeg008#1;
;
#luxeg007#0, 001;
#luxeg007#0, 999;
;
NULL, ApuntesLineas;
|FIN;

|PROCESO ElimLinsRegIVA;
   |BORRA 020#caivalin.a;
|FPRC;

|DEFBUCLE ElimLinsRegIVA
#caivalin#1;
;
#camaniva#0, #camaniva#1, 01;
#camaniva#0, #camaniva#1, 99;
be;
NULL, ElimLinsRegIVA;
|FIN;

|PROCESO EliminaRegistroIVA;
   |ABRE #camaniva; |SELECT #2#camaniva;
   #camaniva#7 = #luxeg016#2;
   #camaniva#8 = fFechaCont;
   #camaniva#9 = #luxeg016#4;
   |LEE 101#camaniva.=;
   |SI FSalida = 0;
      |BUCLE ElimLinsRegIVA
      |BORRA 020#camaniva.a; |LIBERA #camaniva;
   |FINSI;
   |SELECT #1#camaniva; |CIERRA #camaniva;
|FPRC;

|PROCESO Contabiliza;
   #luxeg016#0 = #luxeg007#0;
   #luxeg016#5 = 1;
   #luxeg016#1 = 1;
   |LEE 000#luxeg016.];
   |SI FSalida = 0; |Y #luxeg016#0 = #luxeg007#0; |Y #luxeg016#5 = 1;
      aCont = "S";
   |SINO;
      aCont = "N";
   |FINSI;

   |SI #luxeg017#4 > aCont; |O aCont > #luxeg017#5; |ACABA; |FINSI;
   |SI #luxeg017#4 > #luxeg007#20; |O #luxeg007#20 > #luxeg017#5; |ACABA; |FINSI;

   nSwHay    = 1;

   #luxeg004#0 = #luxeg007#4;
   |LEE 000#luxeg004.=;
   |SI FSalida ! 0; |DDEFECTO #luxeg004; |FINSI;

   |ABRE #cajasald;
   |LEE 000#cajasald.p;
   |SI FSalida ! 0; |DDEFECTO #cajasald; |FINSI;
   |CIERRA #cajasald;

   || Creacion del fichero puente con las facturas por cada linea
   || Lo primero es crear el historico de enlaces ...

   #luxeg016#0 = #luxeg007#0;
   #luxeg016#5 = 1;
   #luxeg016#1 = 1;
   |LEE 101#luxeg016.=;
   |SI FSalida ! 0;
      |ABRE #caconasi;
      |LEE 101#caconasi.p;
      |SI FSalida ! 0;
         |DDEFECTO #caconasi;
         |GRABA 020#caconasi.b;
      |FINSI;
      #caconasi#1 = #caconasi#1 + 1;
      Documento = #caconasi#1;
      |GRABA 020#caconasi.a;
      |LIBERA #caconasi; |CIERRA #caconasi;

      |DDEFECTO #luxeg016;
      #luxeg016#0 = #luxeg007#0;
      #luxeg016#5 = 1;
      #luxeg016#1 = 1;
      |GRABA 020#luxeg016.b;
      #luxeg016#2 = #luxeg002#2;
      |SI #luxeg017#6 ! "01.01.0000";
         #luxeg016#3 = #luxeg017#6;
      |SINO;
         #luxeg016#3 = #luxeg007#2;
      |FINSI;
      #luxeg016#4 = Documento;
      |GRABA 020#luxeg016.a; |LIBERA #luxeg016;
      fFechaCont = #luxeg016#3;

      |HAZ ContadorFra;
   |SINO;
      nNumFact = -1;
      |ABRE #camaniva; |SELECT #2#camaniva;
      #camaniva#7 = #luxeg016#2;
      #camaniva#8 = #luxeg016#3;
      #camaniva#9 = #luxeg016#4;
      |LEE 000#camaniva.=;
      |SI FSalida = 0;
         nNumFact = #camaniva#3;
      |FINSI;
      |SELECT #1#camaniva; |CIERRA #camaniva;

      |SI nNumFact < 0; |HAZ ContadorFra; |FINSI;

      fFechaCont = #luxeg016#3;
      Documento  = #luxeg016#4;
      |SI #luxeg017#6 ! "01.01.0000"; |Y fFechaCont ! #luxeg017#6;
         #luxeg016#3 = #luxeg017#6;
         |GRABA 020#luxeg016.a; |LIBERA #luxeg016;
      |FINSI;
   |FINSI;

   nLinea = 1;
   || Creo la parte del asiento de la factura de soportado ...
   ||    ... y lo primero me aseguro de que el parte no existe en
   ||    contabilidad ... para esto si la fecha no ha cambiado o es posterior
   ||    uso el mecanismo del enlace de incluir la linea para que de de baja
   ||    la anterior. Pero si la nueva fecha es anterior, tengo que eliminar
   ||    el asiento a priori, ya que si no el enlace comenzar  a escribir el
   ||    asiento en la nueva fecha antes de eliminar el anterior, con lo cual
   ||    escribir  en el registro de IVA antes de haber eliminado lo escrito
   ||    anteriormente.

   |SI fFechaCont > #luxeg017#6;
      |HAZ EliminaRegistroIVA;
   |FINSI;

   |DDEFECTO #caenlace;
   #caenlace#0  = #luxeg002#2;
   #caenlace#1  = fFechaCont;
   #caenlace#2  = Documento;
   #caenlace#3  = 0;
   #caenlace#25 = "S";
   #caenlace#37 = #48#2;
   #caenlace#38 = #48#3;
   |GRABA 020#caenlace.n;

   |SI #luxeg017#6 ! "01.01.0000"; |Y fFechaCont ! #luxeg017#6;
      fFechaCont = #luxeg017#6;
   |FINSI;

   |DDEFECTO #caenlace;
   #caenlace#0 = #luxeg002#2;
   #caenlace#1 = fFechaCont;
   #caenlace#2 = Documento;
   #caenlace#3 = nLinea; nLinea = nLinea + 10;
   #caenlace#4 = #luxeg002#1;
   |HAZ MiraNivel;
   #caenlace#5 = nNivel;
   #caenlace#6 = #luxeg002#3;
   aAlfa = #luxeg002#4; |QBF aAlfa;
   |SI aAlfa = "";
      #caenlace#7 = "Factura liquidacion";
   |SINO;
      #caenlace#7 = #luxeg002#4;
   |FINSI;
   aAlfa = #caenlace#7; |QBF aAlfa;
   aAlfa = aAlfa + " parte n§ " + #luxeg007#0;
   #caenlace#7 = aAlfa;
   #caenlace#8   = "H";
   #caenlace#9   = #luxeg007#7;
   #caenlace#10  = "S";
   #caenlace#11  = #luxeg002#13;
   #caenlace#12  = #luxeg002#14;

   #caenlace#13  = nNumFact;
   #caenlace#15  = #cajasald#6;
   #caenlace#26  = "F";
   #caenlace#27  = 0;
   |SI #luxeg017#6 ! "01.01.0000";
      #caenlace#29  = #luxeg017#6;
   |SINO;
      #caenlace#29  = #luxeg007#2;
   |FINSI;
   #caenlace#37  = #48#2;
   #caenlace#38  = #48#3;
   #caenlace#51  = #luxeg007#2;
   |GRABA 020#caenlace.n;

   nNumAcumIVA  = 1; |BUCLE ApuntesLineas

   |DDEFECTO #caenlace;
   #caenlace#0 = #luxeg002#2;
   #caenlace#1 = fFechaCont;
   #caenlace#2 = Documento;
   #caenlace#3 = nLinea; nLinea = nLinea + 10;
   #caenlace#4 = #luxeg002#1;
   |HAZ MiraNivel;
   #caenlace#5 = nNivel;
   #caenlace#6 = #luxeg002#7;
   aAlfa = #luxeg002#8; |QBF aAlfa;
   |SI aAlfa = "";
      #caenlace#7 = "Pago ";
   |SINO;
      #caenlace#7 = #luxeg002#8;
   |FINSI;
   aAlfa = #caenlace#7; |QBF aAlfa;
   aAlfa = aAlfa + " parte n§ " + #luxeg007#0;
   #caenlace#7 = aAlfa;
   #caenlace#8   = "D";
   #caenlace#9   = #luxeg007#7;
   #caenlace#15  = #cajasald#6;
   #caenlace#26  = "X";
   #caenlace#37  = #48#2;
   #caenlace#38  = #48#3;
   |GRABA 020#caenlace.n;

   |DDEFECTO #caenlace;
   #caenlace#0 = #luxeg002#2;
   #caenlace#1 = fFechaCont;
   #caenlace#2 = Documento;
   #caenlace#3 = nLinea; nLinea = nLinea + 10;
   #caenlace#4 = #luxeg004#8;
   |HAZ MiraNivel;
   #caenlace#5 = nNivel;
   #caenlace#6 = #luxeg002#9;
   aAlfa = #luxeg002#10; |QBF aAlfa;
   |SI aAlfa = "";
      #caenlace#7 = "Pago ";
   |SINO;
      #caenlace#7 = #luxeg002#10;
   |FINSI;
   aAlfa = #caenlace#7; |QBF aAlfa;
   aAlfa = aAlfa + " parte n§ " + #luxeg007#0;
   #caenlace#7 = aAlfa;
   #caenlace#8   = "H";
   #caenlace#9   = #luxeg007#7;
   #caenlace#15  = #cajasald#6;
   #caenlace#26  = "X";
   #caenlace#37  = #48#2;
   #caenlace#38  = #48#3;
   |GRABA 020#caenlace.n;

   |SI #luxeg007#26 ! #luxeg002#16;  || Asiento de liquidacion
      |SI #luxeg007#8 ! 0;
         #luxeg016#0 = #luxeg007#0;
         #luxeg016#5 = 2; || Segundo asiento del 'paquete'
         #luxeg016#1 = 1;
         |LEE 101#luxeg016.=;
         |SI FSalida ! 0;
            fFechaCont = #luxeg007#2;
            |ABRE #caconasi;
            |LEE 101#caconasi.p;
            |SI FSalida ! 0;
               |DDEFECTO #caconasi;
               |GRABA 020#caconasi.b;
            |FINSI;
            #caconasi#1 = #caconasi#1 + 1;
            Documento = #caconasi#1;
            |GRABA 020#caconasi.a;
            |LIBERA #caconasi; |CIERRA #caconasi;

            |DDEFECTO #luxeg016;
            #luxeg016#0 = #luxeg007#0;
            #luxeg016#1 = 1;
            #luxeg016#2 = #luxeg002#2;
            #luxeg016#3 = fFechaCont;
            #luxeg016#4 = Documento;
            #luxeg016#5 = 2;
            |GRABA 020#luxeg016.c;
         |SINO;
            fFechaCont = #luxeg016#3;
            Documento  = #luxeg016#4;
         |FINSI;

         nLinea = 1;
         |DDEFECTO #caenlace;
         #caenlace#0 = #luxeg002#2;
         #caenlace#1 = fFechaCont;
         #caenlace#2 = Documento;
         #caenlace#3 = nLinea; nLinea = nLinea + 10;
         #caenlace#4 = #luxeg007#26;
         |HAZ MiraNivel;
         #caenlace#5 = nNivel;
         #caenlace#6 = #luxeg002#3;
         aAlfa = #luxeg002#4; |QBF aAlfa;
         |SI aAlfa = "";
            #caenlace#7 = "Factura liquidacion";
         |SINO;
            #caenlace#7 = #luxeg002#4;
         |FINSI;
         aAlfa = #caenlace#7; |QBF aAlfa;
         aAlfa = aAlfa + " parte n§ " + #luxeg007#0;
         #caenlace#7 = aAlfa;
         #caenlace#9   = #luxeg007#8;
         |SI #luxeg007#8 < 0;
            #caenlace#8   = "D";
            #caenlace#9   = -#caenlace#9;
         |SINO;
            #caenlace#8   = "H";
         |FINSI;

         #caenlace#15  = #cajasald#6;
         #caenlace#37  = #48#2;
         #caenlace#38  = #48#3;
         |GRABA 020#caenlace.n;

         |DDEFECTO #caenlace;
         #caenlace#0 = #luxeg002#2;
         #caenlace#1 = fFechaCont;
         #caenlace#2 = Documento;
         #caenlace#3 = nLinea; nLinea = nLinea + 10;
         #caenlace#4 = #luxeg004#8;
         |HAZ MiraNivel;
         #caenlace#5 = nNivel;
         #caenlace#6 = #luxeg002#7;
         aAlfa = #luxeg002#8; |QBF aAlfa;
         |SI aAlfa = "";
            #caenlace#7 = "Pago ";
         |SINO;
            #caenlace#7 = #luxeg002#8;
         |FINSI;
         aAlfa = #caenlace#7; |QBF aAlfa;
         aAlfa = aAlfa + " parte n§ " + #luxeg007#0;
         #caenlace#7 = aAlfa;
         #caenlace#9   = #luxeg007#8;
         |SI #luxeg007#8 < 0;
            #caenlace#8   = "H";
            #caenlace#9   = -#caenlace#9;
         |SINO;
            #caenlace#8   = "D";
         |FINSI;
         #caenlace#15  = #cajasald#6;
         #caenlace#37  = #48#2;
         #caenlace#38  = #48#3;
         |GRABA 020#caenlace.n;
      |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE Hojas;
#luxeg007#1;
2;
#luxeg017#0, #luxeg017#2;
#luxeg017#1, #luxeg017#3;
;
NULL, Contabiliza;
|FIN;

|PROGRAMA;
   |CLS;
   |CABEZA "Enlace masivo de liquidaciones de hojas de gasto";
   |PINPA #0#0; |PINDA #0#0;
   |ENDATOS #1#0;
   |SI FSalida = 0;
      aAlfa = "pu" + Usuario; |NOME_DAT #caenlace#aAlfa#; |DELINDEX #caenlace;
      |ABRE #caenlace;

      |ABRE #luxeg016; |ABRE #luxeg004;
      |ABRE #luxeg003; |ABRE #luxeg001;
      |ABRE #luxeg002;

      |LEE 000#luxeg002.p;
      |SI FSalida ! 0; |DDEFECTO #luxeg002; |FINSI;

      nSwHay = 0; |BUCLE Hojas;

      |CIERRA #caenlace;

      |SI nSwHay ! 0;
         |DFICE aAlfa; |QBF aAlfa;
         aAlfa = "dsapunte.tab;" + aAlfa + ",pu" + Usuario;
         |SUB_EJECUTA aAlfa;
      |SINO;
         |MENAV "    Seleccion vacia";
      |FINSI;

      |CIERRA #luxeg002;
      |CIERRA #luxeg001; |CIERRA #luxeg003;
      |CIERRA #luxeg016; |CIERRA #luxeg004;
   |FINSI;
|FPRO;
