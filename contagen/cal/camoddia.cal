|FICHEROS;
   camoddia #0;    || def entrada
   camandia #10;

   camaniva #100;
   caivaasi;
   dscam043@ #102;
   dscam00x@ #103;
|FIN;

|VARIABLES;
   &entrada = 1;
   importe = 0;
   men1  = "     NO EXISTE EMPRESA !";
   men2  = "     Longitud de Cuenta Fuera de Nivel !";
   men3  = "     ERROR el asiento ya existe";
   men4  = "     ERROR el asiento NO existe";
   opera = "";
   fecalf = "";
   mensa1 = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
   mensa2 = "    ERROR FECHA INFERIOR O IGUAL AL BLOQUEO DE DATOS";
   &modcon = 0;
   &modifi = 0;
   mesi1 = "";
   mesi2 = "";
   traba1 = "";
   &no_mensaje = 1;
   si_sigue = 0;
   &sw_ivamerr = 0;
   alfa1 = "";
   alfa2 = "";
   i_perio = 0;
   trim1 = @;
   trim2 = @;
   trim3 = @;
   sw_exi = 0;

   aAlfa1 = "";
   nEmpr  = 0;
   &enSwErrIVA = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE moddia_i;

|PROCESO RevisaAsto;

   |ABRE #caivaasi;
   |LEE 000#caivaasi.p;
   |SI FSalida ! 0; |DDEFECTO #caivaasi; |FINSI;
   |CIERRA #caivaasi;
   |SI #caivaasi#1 ! "S"; |ACABA; |FINSI;

   enSwErrIVA = 0;
   || Primero miro si tiene registro de IVA ...
   |ABRE #camaniva;
   |SELECT #2#camaniva;
   #camaniva#7 = #camoddia#0;
   #camaniva#8 = #camoddia#1;
   #camaniva#9 = #camoddia#2;
   |LEE 000#camaniva.=;
   |SI FSalida = 0;
      || ... y si lo tiene, compruebo los vencimientos ...
      aAlfa = #caivaasi#56; |QBF aAlfa;
      aAlfa1 = aAlfa % 0201; |SI aAlfa1 = ":"; aAlfa = aAlfa % 0399; |FINSI;
      aAlfa1 = aAlfa + "def/dscam043.mas";
      |CARGA_DEF aAlfa1, dscam043@;
      |SI FSalida = 0;
         aAlfa1 = aAlfa + "fich/"; |PATH_DAT #dscam043@#aAlfa1#;
         |SI #camaniva#36 = "R"; aAlfa1 = aAlfa + "def/dscam003.mas"; |FINSI;
         |SI #camaniva#36 = "S"; aAlfa1 = aAlfa + "def/dscam008.mas"; |FINSI;
         |CARGA_DEF aAlfa1, dscam00x@;
         nEmpr = -1;
         |ABRE #dscam043@;
         #dscam043@#0 = #48#2;
         #dscam043@#1 = #48#3;
         #dscam043@#2 = 1;
         |LEE 000#dscam043@.];
         |PARA; |SI FSalida = 0; |Y #dscam043@#0 = #48#2; |Y #dscam043@#1 = #48#3; |HACIENDO;
            |SI #camaniva#36 = "S"; |Y #dscam043@#3 = "C";
               |SI #camaniva#0 ] #dscam043@#5; |Y #camaniva#2 [ #dscam043@#6;
               |Y  #camaniva#2 ] #dscam043@#7; |Y #camaniva#2 [ #dscam043@#8;
                  nEmpr = #dscam043@#9; |SAL;
               |FINSI;
            |FINSI;
            |SI #camaniva#36 = "R"; |Y #dscam043@#3 = "V";
               |SI #camaniva#0 ] #dscam043@#5; |Y #camaniva#2 [ #dscam043@#6;
               |Y  #camaniva#2 ] #dscam043@#7; |Y #camaniva#2 [ #dscam043@#8;
                  nEmpr = #dscam043@#9; |SAL;
               |FINSI;
            |FINSI;
            |LEE 000#dscam043@.s;
         |SIGUE;
         |SI nEmpr ] 0;
            aAlfa = #caivaasi#56; |QBF aAlfa;
            aAlfa1 = aAlfa + "fich/" + (("00000" + nEmpr) % -105) + "/";
            |PATH_DAT #dscam00x@#aAlfa1#;
            |ABRE #dscam00x@;
            |SI #camaniva#36 = "R"; |SELECT #9#dscam00x@; |FINSI;
            |SI #camaniva#36 = "S"; |SELECT #2#dscam00x@; |FINSI;
            #dscam00x@#27 = #camaniva#0;
            #dscam00x@#0  = #camaniva#2;
            #dscam00x@#1  = #camaniva#3;
            #dscam00x@#2  = 1;
            |LEE 000#dscam00x@.];
            |PARA; |SI FSalida = 0; |Y #dscam00x@#27 = #camaniva#0;  |Y #dscam00x@#0  = #camaniva#2;
             |Y #dscam00x@#1  = #camaniva#3; |HACIENDO;
               |SI #dscam00x@#4  = #camaniva#4;
                        |Y #dscam00x@#44 = #dscam043@#0;
                        |Y #dscam00x@#45 = #dscam043@#1;
                  |SI #dscam00x@#14 ! "P"; enSwErrIVA = -1; |SAL; |FINSI;
               |FINSI;
               |LEE 000#dscam00x@.s;
            |SIGUE;
            |CIERRA #dscam00x@;
         |FINSI;
         |DESCARGA_DEF #dscam00x@;
         |CIERRA #dscam043@; |DESCARGA_DEF #dscam043@;
      |FINSI;
   |FINSI;

   |SI enSwErrIVA < 0;
      |MENAV "    Asiento de iva con vencimientos no pendientes. No se cambiara el registro de IVA";
      #camoddia#8 = "N"; |PINTA #camoddia#8;
      |C_M #camoddia#8, 1, "N";
   |SINO;
      |C_M #camoddia#8, 1, "S";
   |FINSI;
|FPRC;

|PROGRAMA;

   |HAZ inicio;
   |HAZ Analitica;
   |HAZ DirAplicSt;
   |HAZ eLeeParametro;

   |CLS;
   |CABEZA "Modificacion del Asientos";
   |PINPA #0#0;
   |PINDA #0#0;
   FSalida = 1;
   |PARA; |SI FSalida > 0; |HACIENDO;
      |ENDATOS #1#0;
      |SI FSalida < 0; |SAL; |FINSI;

      |ABRE #1;
      |ABRE #2;
      #1#0 = #0#0;
      #1#1 = #0#1;
      #1#2 = #0#2;
      |LEE 001#1=;
      |SI FSalida ! 0;
         |MENAV men4;
         #0#6 = #1#4; |PINTA #0#6;
         #0#7 = #1#5; |PINTA #0#7;
      |SINO;
         #1#0 = #0#3;
         #1#1 = #0#4;
         #1#2 = #0#5;
         |LEE 001#1=;
         |SI FSalida = 0;
            |MENAV men3;
         |SINO;
            |HAZ cambio;
            |HAZ los_ivas;
            |PINPA #0#0;
            |PINDA #0#0;
            |HAZ borrar;
            |SI #1#13 > 102; |Y enSwDejar = 1;
                ant_dia = #0#3; vie_dia = #0#0;
                ant_fec = #0#4; vie_fec = #0#1;
                ant_doc = #0#5; vie_doc = #0#2;
                |HAZ eGrabaModificar;
            |FINSI;
         |FINSI;
      |FINSI;
      |CIERRA #1;
      |CIERRA #2;
      FSalida = 1;
   |SIGUE;
|FPRO;

|PROCESO diario; |TIPO 0;
   #0#3 = #0#0;
   |PINTA #0#3;
|FPRC;

|PROCESO fevie; |TIPO 0;
   |SI #0#1 ] fec1; |Y #0#1 [ fec2;
       #0#4 = #0#1;
       |PINTA #0#4;
   |FINSI;
|FPRC;

|PROCESO docume; |TIPO 0;
   |SI FSalida = 2; |ACABA; |FINSI;
   |SI si_sigue = 0; |ERROR; |ACABA; |FINSI;
   |SI #0#2 ! 0;
       #0#5 = #0#2; |PINTA #0#5;
   |FINSI;
   |SI #0#9 ! 0;
       #0#10 = #0#9; |PINTA #0#10;
   |FINSI;
|FPRC;
