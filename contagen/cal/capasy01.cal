|FICHEROS;
  capasy01 #0;
  cay00005 #10;

  camaasil #1;
  camaasic #2;
  cacuenta #3;
  camasil@ #11;
  camasic@ #12;

  caw00012 #912;
  caw00013 #913;

  :/basica/def/dspase00 #100;

  canempre #30;
  canempr@ #330;
  camoned@ #331;

  cam00010 #110;
  cam00012 #112;
|FIN;

|VARIABLES;
 informe = "";
 inform1 = "cay10005";
 inform2 = "cay20005";

 nSwPaso = 0;
 aAlfa1  = "";
 aAlfa2  = "";
 aAlfa3  = "";
 nPara1  = 0;
 nNume1  = 0;
 nNume2  = 0;
 nNume3  = 0;
 nNume4  = 0;
 &r_emp  = 0;
 &r_per  = 0;

 aFichero = "";
 aDef11   = "";
 aDef12   = "";
 aDef330  = "";
 aDef331  = "";

 aDirInicio = "";
 aDirBase   = "";
 aPathDef   = "";
 aPathFic1  = "";
 aPathFic0  = "";
 aPathFic00 = "";
 aDFicEm1   = "";
 aDFicEm2   = "";
 nSwError   = 0;
 aMensa     = "";
 aQueQuiero = "";
 aNumCampos = "";
 nNumCampos = 0;

 nConta0    = 0;
 nConta1    = 0;
 nConta2    = 0;

 nSwBusco   = 0;
 fFecB      = @;
 nImpDB     = 0;
 nImpHB     = 0;

 nSwELin    = 0;
 nSwELin2   = 0;
 nSwW13     = 0;
 nW13Cam0   = 0;
 nW13Cam1   = 0;
 nW13Cam2   = 0;
 fW13Cam3   = @;
 nW13Cam4   = 0;
 nW13Cam5   = 0;
 fW13Cam6   = @;
 nW13Cam7   = 0;
 nW13Cam8   = 0;
 nW13Cam9   = 0;
 aW13Cam10  = "";
 aDescrip   = "";
 nSwAlgun = 0;

 &nSwInf   = 0;
 fLaFec    = @;

 nInkey     = 0;
 nDEmp      = 0;
 nHEmp      = 0;

 nTipoDif   = 0;
 Comodin    = "";
 Comodin1   = "";

 &eaMonedaV  = "";
 &enEmpresaV = 0;
 &enPeriodoV = 0;
&eSwCambio = 0;
 nDiario   = 0;
 nDebe1    = 0;
 nDebe2    = 0;
 nHaber1   = 0;
 nHaber2   = 0;
 aParam    = "";
 &eaPreg22_5 = "";
|FIN;

|INCLUYE dscont_i;

|| **********************************************************************
|| ***** Calculos de Pantalla
|| **********************************************************************
|CALCULO defectos; |TIPO 7;
   |SI nSwPaso = 0;
       #0#0 = enEmpresa;     |PINTA #0#0;
       #0#1 = enPeriodo;     |PINTA #0#1;
       #0#2 = eaNombreEmp;   |PINTA #0#2;
       #0#3 = #30#26;        |PINTA #0#3;
       #0#4 = eaMoneda;

       #0#5 = #0#0; |PINTA #0#5;
       #0#6 = #0#1; |PINTA #0#6;
       #0#7 = #0#2; |PINTA #0#7;
       #0#8 = #0#3; |PINTA #0#8;
       #0#9 = #0#4;

       #0#12 = fec1; |PINTA #0#12;
       #0#13 = fec2; |PINTA #0#13;
       nSwPaso = 1;
   |FINSI;
|FCAL;

|CALCULO esbuena;

  |SI FSalida = 9;
      r_emp = #0Campo;
      |SUB_EJECUTA_NP "caconemp";
      |ERROR;
      nNume1   = Campo + 1;
      #0Campo  = r_emp;
      #0nNume1 = r_per;
      |PTEC 802;
      |PTEC 802;
      |ACABA;
  |FINSI;

  |SI Campo = 0; |Y #0Campo = enEmpresa; |ACABA; |FINSI;

  |ABRE #30;
  #30#2 = #0Campo;
  #30#3 = 0;
  |LEE 011#30];
  |SI FSalida ! 0;  |O #30#2 ! #0Campo;
      |MENAV "     NO Existe la Empresa Contable";
      |ERROR;
  |SINO;
      nNume1   = Campo + 2;
      #0nNume1 = #30#1; |PINTA #0nNume1;
  |FINSI;
  |CIERRA #30;

  |SI Campo = 0;
      |SI #30#26 > 80;
          enEjercicio = 1900 + #30#26;
      |SINO;
          enEjercicio = 2000 + #30#26;
      |FINSI;
      aAlfa1 = enEjercicio;
      aAlfa2 = #0#12 % 106;
      aAlfa3 = aAlfa2 + aAlfa1; #0#12 = aAlfa3; |PINTA #0#12;
      aAlfa2 = #0#13 % 106;
      aAlfa3 = aAlfa2 + aAlfa1; #0#13 = aAlfa3; |PINTA #0#13;
  |FINSI;
|FCAL;

|CALCULO esbuena1;
  |SI FSalida = 2; |ACABA; |FINSI;

  nNume1 = Campo - 1;
  nNume2 = Campo + 1;
  nNume3 = Campo + 2;
  nNume4 = Campo + 3;
  |SI Campo = 1; |Y #0Campo = enPeriodo; |Y #0nNume1 = enEmpresa;
      |ACABA;
  |FINSI;

  |ABRE #30;
  #30#2 = #0nNume1;
  #30#3 = #0Campo;
  |LEE 011#30=;
  |SI FSalida ! 0;
      |MENAV "     NO Existe Periodo en la Empresa Contable";
      |CIERRA #30;
      |ERROR;
      |ACABA;
  |FINSI;

  #0nNume2 = #30#1;   |PINTA #0nNume2;
  #0nNume3 = #30#26;  |PINTA #0nNume3;
  #0nNume4 = #30#28;
  |CIERRA #30;
|FCAL;

|CALCULO Defecto; |TIPO 7;
    |DBASS aDirInicio; |QBF aDirInicio;
    aDirInicio = #100#0; |QBF aDirInicio;
    aDirBase = aDirInicio + "agicont/";
    #0Campo = aDirBase;
    |PINTA #0Campo;
    |HAZ DesCargaDef;
|FCAL;

|CALCULO PonBarra; |TIPO 0;
     aAlfa1 = #0Campo;
     |QBF aAlfa1;
     aAlfa2 = aAlfa1 % -101;
     |SI aAlfa2 ! "/";|Y aAlfa2 ! "\";
          #0Campo = aAlfa1 + "/";
          |PINTA #0Campo;
     |FINSI;
     |HAZ CargaEmpresa;
     |SI nSwError = 1;
         |MENAV aMensa;
         |ERROR;
     |FINSI;
|FCAL;

|RUTINA infA2;
   #330#2 = nDEmp;
   #330#3 = 0;
|FRUT;

|RUTINA supA2;
   #330#2 = nHEmp;
   #330#3 = 9;
|FRUT;


|CALCULO LeeEmpresa; |TIPO 0;
  nInkey = FSalida;

  aDirBase = #0#17; |QBF aDirBase;
  aPathFic0  = aDirBase + "fich/";
  |PATH_DAT #330 aPathFic0;

  |SI nInkey = 10;
     nDEmp = 0; nHEmp = 99999;
     |ABRE #330;
     |CONSULTA_F_CLAVE #1#330,infA2,supA2;
     #0#5 = #330#2; |PINTA #0#5;
     #0#6 = #330#3; |PINTA #0#6;
     |CIERRA #330;
     |PTEC 802;
     |ACABA;
  |FINSI;

  |ABRE #330;
  #330#2 = #0#5;
  #330#3 = 0;
  |LEE 011#330];
  |SI FSalida ! 0;  |O #330#2 ! #0#5;
      |MENAV "     NO Existe la empresa Contable";
      |ERROR;
  |SINO;
      #0#7 = #330#1; |PINTA #0#7;
  |FINSI;
  |CIERRA #330;
|FCAL;

|CALCULO LeeEmpresaP; |TIPO 0;
  nInkey = FSalida;
  |SI nInkey = 2; |ACABA; |FINSI;

  aDirBase = #0#17; |QBF aDirBase;
  aPathFic0  = aDirBase + "fich/";
  aPathFic00 = aDirBase + "fich/000000/";
  |PATH_DAT #330 aPathFic0;
  |PATH_DAT #331 aPathFic00;

  |SI nInkey = 10;
     nDEmp = #0#5; nHEmp = #0#5;
     |ABRE #330;
     |CONSULTA_F_CLAVE #1#330,infA2,supA2;
     #0#6 = #330#3; |PINTA #0#6;
     |CIERRA #330;
  |FINSI;

  aMonedaC = "E";
  |ABRE #330;
  #330#2 = #0#5;
  #330#3 = #0#6;
  |LEE 011#330=;
  |SI FSalida ! 0;
      |MENAV "     NO Existe Periodo en la Empresa Contable";
      |ERROR;
  |SINO;
      #0#7   = #330#1;  |PINTA #0#7;
      #0#8   = #330#26; |PINTA #0#8;
      #0#9 = "P";
      |SI aDef331 ! "";
          |ABRE #331;
          #331#0 = #0#5;
          #331#1 = #0#6;
          |LEE 000#331=;
          |SI FSalida = 0; #0#9 = #331#2; |FINSI;
          |CIERRA #331;
      |FINSI;
      |SI #0#3 ! #0#8;
          |MENAV "    Error. Las Empresas no pertenecen al mismo Ejercicio Contable ";
          |ERROR;
     |FINSI;
  |FINSI;
  |CIERRA #330;
|FCAL;

|CALCULO dia_h; |TIPO 0;
 |SI #0#10 > #0#11; |ERROR; |FINSI;
|FCAL;

|CALCULO fech_h; |TIPO 0;
 |SI #0#12 > #0#13; |ERROR; |FINSI;
|FCAL;

|CALCULO quetitol; |TIPO 0;
 |SI #0#14 = "S";
     #0#15 = "INFORME DE ASIENTOS DUPLICADOS ";
 |SINO;
     #0#15 = "INFORME DE ASIENTOS NO DUPLICADOS ";
 |FINSI;
 |PINTA #0#15;
|FCAL;

|| *************************************************************************
|PROCESO DesCargaDef;
     |SI aDef11  ! "";   |DESCARGA_DEF #camasil@; aDef11  = ""; |FINSI;
     |SI aDef12  ! "";   |DESCARGA_DEF #camasic@; aDef12  = ""; |FINSI;
     |SI aDef330 ! "";   |DESCARGA_DEF #canempr@; aDef330 = ""; |FINSI;
     |SI aDef331 ! "";   |DESCARGA_DEF #camoned@; aDef331 = ""; |FINSI;
|FPRC;
|| *************************************************************************
|PROCESO CargaEmpresa;

     nSwError = 0;
     aMensa   = "";

     aDirBase = #0#17; |QBF aDirBase;
     aPathDef = aDirBase + "def/";

     aDef330 = aPathDef + "canempre";
     |CARGA_DEF aDef330, canempr@;
     |SI FSalida ! 0;
         aMensa   = "0000Error en:" + aDef330;
         aDef330    = "";
         nSwError = 1;
         |ACABA;
     |FINSI;

|| ... Averiguar la Version de Contabilidad
|| .......................................................
     aQueQuiero = "|NC";
     aNumCampos = #canempr@#aQueQuiero#;
     nNumCampos = aNumCampos;

     aDef331 = aPathDef + "camoneda";
     |CARGA_DEF aDef331, camoned@;
     |SI FSalida ! 0;
         aMensa   = "0000Error en:" + aDef331;
         aDef331   = "";
     |FINSI;
|FPRC;
|| *************************************************************************

|PROCESO GrabaW13;
 |DDEFECTO #913;
 #913#0 = nW13Cam0;
 #913#1 = nW13Cam1;
 #913#2 = nW13Cam2;
 #913#3 = fW13Cam3;
 #913#4 = nW13Cam4;
 #913#5 = nW13Cam5;
 #913#6 = fW13Cam6;
 #913#7 = nW13Cam7;
 #913#8 = nW13Cam8;
 #913#9 = nW13Cam9;
 #913#10 = aW13Cam10;
 |GRABA 000#913n;
|FPRC;

|PROCESO LeeW13;
 nSwW13 = 0;
 #913#0 = nW13Cam0;
 #913#1 = nW13Cam1;
 #913#2 = nW13Cam2;
 #913#3 = fW13Cam3;
 #913#4 = nW13Cam4;
 |LEE 000#913=;
 |SI FSalida = 0;
     nSwW13 = 1;
 |FINSI;
|FPRC;

|PROCESO GrabaW12_1;  || Graba Auxiliar 12 para Empresa 1
 |DDEFECTO #912;
 #912#0 = fFecB;
 nConta1 = nConta1 + 1;
 #912#1  = nConta1;
 #912#2  = #2#0;
 #912#3  = #2#1;
 #912#4  = #2#2;
 #912#5  = 1;
 #912#10 = #2#4;
 #912#11 = #2#5;
 #912#12 = aDescrip;

 |GRABA 000#912n;
|FPRC;

|| //////////////////
|PROCESO BuscoC;
 nConta2 = #912#1;
 |ERROR10;
|FPRC;

|DEFBUCLE BuscoC;
 #912#2;
 9;
 000001,fFecB, 0;
 999999,fFecB, 0;
 ;
 NULL, BuscoC;
|FIN;

|PROCESO GrabaW12_2;  || Graba Auxiliar 12 para Empresa 1
 nConta2 = 0;
 |BUCLE BuscoC;
 |SI nConta2 = 0;
     nConta1 = nConta1 + 1;
     nConta2 = nConta1;
 |FINSI;

 |ABRE #912;
 |DDEFECTO #912;
 #912#0 = fFecB;
 #912#1 = nConta2;
 |LEE 001#912=;
 |SI FSalida ! 0;
     |DDEFECTO #912;
     #912#0 = fFecB;
     #912#1 = nConta2;
     |GRABA 020#912c;
 |FINSI;

 #912#6  = #12#0;
 #912#7  = #12#1;
 #912#8  = #12#2;
 #912#9  = 1;
 #912#13 = #12#4;
 #912#14 = #12#5;
 #912#15 = aDescrip;
 |GRABA 000#912a;

 |CIERRA #912;
|FPRC;

|| *************************************************************************
|PROCESO CreaAuxi2;
 nW13Cam0  = #0#5;
 nW13Cam1  = #0#6;
 nW13Cam2  = #12#0;
 fW13Cam3  = #12#1;
 nW13Cam4  = #12#2;
 |HAZ LeeW13;
 |SI nSwW13 = 0;

    |ATRI R;
    |PINTA 2353, #12#0;
    |PINTA 2356, #12#1;
    |PINTA 2367, #12#2;
    |ATRI 0;

    fFecB  = #12#1;  || Fecha a Buscar
    nImpDB = #12#4;  || Importe Debe a Buscar
    nImpHB = #12#5;  || Importe Haber a Buscar
    aDescrip = "";
    |ABRE #11;
    #11#0 = #12#0;
    #11#1 = #12#1;
    #11#2 = #12#2;
    #11#3 = 0;
    |LEE 001#11];
    |SI FSalida = 0; |Y #11#0 = #12#0; |Y #11#1 = #12#1; |Y #11#2 = #12#2;
        aDescrip = #11#7;
    |FINSI;
    |CIERRA #11;

    |HAZ GrabaW12_2;  || Graba Auxiliar 12 para Empresa 1
 |FINSI;
 nDiario = #12#0;
 nDebe1  = 0;     nHaber1 = 0;
 nDebe2  = #12#4; nHaber2 = #12#5;
 |HAZ GrRegistro_110;
|FPRC;

|DEFBUCLE CreaAuxi2;
 #12#1003;
 ;
 #0#10, #0#12, 000001;
 #0#11, #0#13, 999999;
 e;
 NULL, CreaAuxi2;
|FIN;

|| *************************************************************************
|PROCESO LasLSegunda;
 |SI eSwCambio = 0;
     |SI #1#4 = #11#4; |Y #1#7 = #11#7; |Y #1#8 = #11#8; |Y #1#9 = #11#9;
         nSwELin2 = 1;
         |ERROR10;
     |FINSI;
 |SINO;
     |SI #1#7 = #11#7; |Y #1#8 = #11#8; |Y #1#9 = #11#9;
         nSwELin2 = 1;
         |ERROR10;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE LasLSegunda;
 #11#1004;
 ;
 #12#0, #12#1, #12#2, 0001;
 #12#0, #12#1, #12#2, 9999;
 e;
 NULL, LasLSegunda;
|FIN;


|PROCESO LasLineas;  || Por cada Linea Comparo el otro asiento

 nSwELin2 = 0;
 |BUCLE LasLSegunda;
 |SI nSwELin2 = 0;
     nSwELin = 1;
     |ERROR10;
 |SINO;
     nSwELin = 0;
 |FINSI;
|FPRC;

|DEFBUCLE LasLineas;
 #1#1;
 ;
 #2#0, #2#1, #2#2, 0001;
 #2#0, #2#1, #2#2, 9999;
 e;
 NULL, LasLineas;
|FIN;

|PROCESO LeeSegunda;
 || ... Encontrada una cabecera Igual
 nW13Cam0  = #0#5;
 nW13Cam1  = #0#6;
 nW13Cam2  = #12#0;
 fW13Cam3  = #12#1;
 nW13Cam4  = #12#2;
 nW13Cam5  = #2#0;
 fW13Cam6  = #2#1;
 nW13Cam7  = #2#2;
 nW13Cam8  = nImpDB;
 nW13Cam9  = nImpHB;
 aW13Cam10 = aDescrip;
 |HAZ LeeW13;
 |SI nSwW13 = 0;
     nSwELin = 1;
     |BUCLE LasLineas;
     |SI nSwELin = 0;
         nSwBusco  = 1;
         |ERROR10;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE LeeSegunda;
 #12#1003;
 4,5;
 00, fFecB, 000001, nImpDB, nImpHB;
 99, fFecB, 999999, nImpDB, nImpHB;
 ;
 NULL, LeeSegunda;
|FIN;

|PROCESO CreaAuxi1;
 nSwBusco = 0;

 |PINTA 2353, #2#0;
 |PINTA 2356, #2#1;
 |PINTA 2367, #2#2;

 nDiario = #2#0;  || Diario
 fFecB   = #2#1;  || Fecha a Buscar
 nImpDB  = #2#4;  || Importe Debe a Buscar
 nImpHB  = #2#5;  || Importe Haber a Buscar
 aDescrip = "";
 |ABRE #1;
 #1#0 = #2#0;
 #1#1 = #2#1;
 #1#2 = #2#2;
 #1#3 = 0;
 |LEE 001#1];
 |SI FSalida = 0; |Y #1#0 = #2#0; |Y #1#1 = #2#1; |Y #1#2 = #2#2;
     aDescrip = #1#7;
 |FINSI;
 |CIERRA #1;

 |BUCLE LeeSegunda;
 |SI nSwBusco = 1;
     nConta2 = 1;
     |HAZ GrabaW13;
 |SINO;
     |SI #0#14 = "N";
         |HAZ GrabaW12_1;
     |FINSI;
 |FINSI;

 nDiario = #2#0;
 nDebe1  = #2#4; nHaber1 = #2#5;
 nDebe2  = 0;    nHaber2 = 0;
 |HAZ GrRegistro_110;
|FPRC;

|DEFBUCLE CreaAuxi1;
 #2#1;
 ;
 #0#10, #0#12, 000001;
 #0#11, #0#13, 999999;
 e;
 NULL, CreaAuxi1;
|FIN;

|| *************************************************************************
|PROCESO Repe;

 |SI nSwAlgun = 0;
     fLaFec = #912#0;
     nSwAlgun = 1;
 |FINSI;

 |SI fLaFec ! #912#0;
     nSwInf = 1;
     fLaFec = #912#0;
 |FINSI;

 |SI #0#14 = "N"; |Y #0#18 = "S";
     |HAZ GrabaRegistro;
 |FINSI;

 |SI aParam = "";  |PRINT; |FINSI;
 nSwAlgun = 1;
 nSwInf   = 0;
|FPRC;

|DEFBUCLE SiRepe;
 #913#1;
 ;
 INICIO;
 FINAL;
 ;
 NULL, Repe;
|FIN;

|DEFBUCLE NoRepe;
 #912#1;
 ;
 INICIO;
 FINAL;
 ;
 NULL, Repe;
|FIN;

|| *******************************************************************
|| -----Crear Registro de Diferencias
|PROCESO Borra112;
 |BORRA 020#112a;
 |LIBERA #112;
|FPRC;

|DEFBUCLE Borra112;
 #112#1;
 ;
 #0#0, #0#1, #0#10, 000001;
 #0#0, #0#1, #0#11, 999999;
 be;
 NULL, Borra112;
|FIN;

|PROCESO Borra110;
 |BORRA 020#110a;
 |LIBERA #110;
|FPRC;

|DEFBUCLE Borra110;
 #110#1;
 ;
 #0#0, #0#1, nTipoDif, #0#10;
 #0#0, #0#1, nTipoDif, #0#11;
 be;
 NULL, Borra110;
|FIN;

|PROCESO GrRegistro_110;
 |ABRE #110;
 #110#0  = #0#0;
 #110#1  = #0#1;
 #110#11 = nTipoDif;
 #110#13 = nDiario;
 |LEE 001#110=;
 |SI FSalida ! 0;
     |DDEFECTO #110;
     |PARA nPara1 = 0; |SI nPara1 [ 9; |HACIENDO nPara1 = nPara1 + 1;
           #110nPara1 = #0nPara1;
     |SIGUE;
     #110#10 = #0#17;
     #110#11 = nTipoDif;
     #110#12 = "Asientos";
     #110#13 = nDiario;
     |GRABA 020#110b;
 |FINSI;

 #110#14 = #110#14 + nDebe1;
 #110#15 = #110#15 + nHaber1;
 #110#19 = #110#19 + nDebe2;
 #110#20 = #110#20 + nHaber2;
 |GRABA 020#110a;
 |LIBERA #110;
 |CIERRA #110;
|FPRC;

|PROCESO GrabaRegistro;

 #110#0  = #0#0;
 #110#1  = #0#1;
 #110#11 = nTipoDif;
 #110#13 = #912#2;
 |LEE 001#110=;
 |SI FSalida ! 0;
     |ACABA;
 |FINSI;

 #110#24 = "X";
 |GRABA 020#110a;
 |LIBERA #110;

 |DDEFECTO #112;
 nConta0 = nConta0 + 1;
 |PARA nPara1 = 0; |SI nPara1 [ 15; |HACIENDO nPara1 = nPara1 + 1;
       #112nPara1 = #912nPara1;
 |SIGUE;
 #112#1 = nConta0;
 #112#16 = #110#0;
 #112#17 = #110#1;
 #112#18 = #110#13;
 |GRABA 000#112n;
|FPRC;

|| ********************************************************************

|PROCESO Imprimir;
 |PARA nNume1 = 0; |SI nNume1 [ 16; |HACIENDO nNume1 = nNume1 + 1;
       #10nNume1 = #0nNume1;
 |SIGUE;

 |BLIN 23;
 |ATRI R; |PINTA 2304, " Imprimiendo ...."; |ATRI 0;

 nConta0 = 0;
 nSwAlgun = 0;
 |SI #0#14 = "N";
     |ABRE #110;
     |ABRE #112;
     |BUCLE NoRepe;
     |CIERRA #112;
     |CIERRA #110;
 |SINO;
     |BUCLE SiRepe;
 |FINSI;

 |SI nSwAlgun = 1; |Y aParam = "";
     |PIEINF;
 |FINSI;

|FPRC;


|| *************************************************************************
|CALCULO ElInforme; |TIPO 3;

  |DBASE aDirBase; |QBF aDirBase;
  aPathFic0 = aDirBase + "fich/";  ||ficheros de esta aplicacion

  aDirBase  = #0#17; |QBF aDirBase;
  aPathDef  = aDirBase + "def/";
  aPathFic1 = aDirBase + "fich/";


  nSwError = 0;
  aDef11 = aPathDef + "camaasil";
  |CARGA_DEF aDef11, camasil@;
  |SI FSalida ! 0;
      aMensa = "0000Error en:" + aDef11;
      aDef11 = "";
      nSwError = 1;
  |SINO;
      aDef12 = aPathDef + "camaasic";
      |CARGA_DEF aDef12, camasic@;
      |SI FSalida ! 0;
          aMensa = "0000Error en:" + aDef12;
          aDef12 = "";
          nSwError = 1;
      |FINSI;
  |FINSI;
  |SI nSwError = 1;
      aMensa = aMensa + ". Proceso Abortado";
      |MENAV aMensa;
      |HAZ DesCargaDef;
      |ACABA;
  |FINSI;

  aAlfa1 = #0#0; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
  aAlfa2 = #0#1; |QBF aAlfa2; aAlfa2 = ("0" + aAlfa2) % -101;
  aDFicEm1 = aPathFic0 + aAlfa1 + aAlfa2 + "/";

  aAlfa1 = #0#5; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
  aAlfa2 = #0#6; |QBF aAlfa2; aAlfa2 = ("0" + aAlfa2) % -101;
  aDFicEm2 = aPathFic1 + aAlfa1 + aAlfa2 + "/";

  |PATH_DAT #1 aDFicEm1;
  |PATH_DAT #2 aDFicEm1;
  |PATH_DAT #3 aDFicEm1;

  |PATH_DAT #11 aDFicEm2;
  |PATH_DAT #12 aDFicEm2;

  |SI aParam = "";
     |IMPRE 0;
     |SI FSalida ! 0;
         |HAZ DesCargaDef;
         |ACABA;
     |FINSI;
  |FINSI;

  informe = inform1;
  |SI #0#14 = "S";
      informe = inform2;
  |FINSI;

  |SI aParam = "";
      |INFOR informe;
      |SI FSalida ! 0;
          |HAZ DesCargaDef;
          |FINIMP;
          |ACABA;
      |FINSI;
  |FINSI;

  |SI #0#3 > 80;
      enEjercicio = 1900 + #0#3;
  |SINO;
      enEjercicio = 2000 + #0#3;
  |FINSI;

  nTipoDif = 1;
  |SI #0#18 = "S";   || Crear el Registro de Diferencias
      |BUCLE Borra110;
      |BUCLE Borra112;
  |FINSI;

  aFichero = ("12w" + Usuario) % 108;
  |NOME_DAT #912 aFichero;
  |DELINDEX #912;

  aFichero = ("13w" + Usuario) % 108;
  |NOME_DAT #913 aFichero;
  |DELINDEX #913;

  |ATRI R; |PINTA 2304, " Creando temporal. Espere ... "; |ATRI 0;
  |ATRI I; |PINTA 2334, "Asiento Empresa 1: "; |ATRI 0;

  nConta1 = 0;
  nConta2 = 0;
  |ABRE #912;
  |ABRE #913;

  |ABRE #11;
  |ABRE #12;
  |BUCLE CreaAuxi1;   || Lee Empresa Uno
  |CIERRA #12;
  |CIERRA #11;

  |CIERRA #912;
  |SI #0#14 = "N";
      |ATRI I; |PINTA 2334, "Asiento Empresa 2: "; |ATRI 0;
      |ABRE #1;
      |ABRE #2;
      |BUCLE CreaAuxi2;   || Lee Empresa Dos
      |CIERRA #2;
      |CIERRA #1;
  |FINSI;
  |CIERRA #913;

  |SI nConta1 ! 0; |O nConta2 ! 0;
      |HAZ Imprimir;
  |FINSI;

  |SI aParam = "";
      |FININF;
      |FINIMP;
  |FINSI;

  |DELINDEX #912;
  |DELINDEX #913;
  |HAZ DesCargaDef;
|FCAL;

|| ***********************************************************************

|PROGRAMA;
     |DBASS aDirInicio; |QBF aDirInicio;
     aDirBase = aDirInicio + "basica/fich/";
     |PATH_DAT #100 aDirBase;
     |ABRE #100;
     |LEE 040#100p;
     |SI FSalida ! 0;
         |DDEFECTO #100;
         #100#0 = aDirInicio;
     |FINSI;
     |CIERRA #100;

   Comodin = PARAMETRO; |QBF Comodin;
   |SI Comodin ! "";
       aParam = Comodin; |SI eaPreg22_5 = "S"; aParam = ""; |FINSI;
       cod1 = enEmpresa;
       cod2 = enPeriodo;
       emEmp = 1;  || Sin mensaje
       |HAZ leelo;
       |SI swerror ! 0;  |ACABA;   |FINSI;

       Comodin1 = Comodin % -101;
       |SI Comodin1 ! "/";
           Comodin = Comodin + "/agicont/";
       |SINO;
           Comodin = Comodin + "agicont/";
       |FINSI;

       |DDEFECTO #0;
       #0#17 = Comodin;

       #0#0 = enEmpresa;
       #0#1 = enPeriodo;
       #0#2 = eaNombreEmp;
       #0#3 = #30#26;
       #0#4 = eaMoneda;

       #0#5 = enEmpresaV;
       #0#6 = enPeriodoV;
       #0#7 = eaNombreEmp;
       #0#8 = #30#26;
       #0#9 = eaMonedaV;
       #0#12 = fec1;
       #0#13 = fec2;
       #0#15 = "INFORME DE ASIENTOS NO DUPLICADOS ";
       |HAZ ElInforme;
    |SINO;
        |CLS;
        |MANTE #0;
    |FINSI;
|FPRO;
|| **************************************************************************
