|FICHEROS;
  caz00030 #0;

  cam00014 #14,MANTE;
  cam00015 #15,MANTE;

  caz0030@ #930;
|FIN;

|VARIABLES;
 nDEmpresa = 0;
 nHEmpresa = 0;
 nDPeriodo = 0;
 nHPeriodo = 0;
 nSwGraba  = 0;

 nInkey   = 0;
 nSwSal   = 0;
 aFichero = "";
 aAlfUlt  = "";
 aAlfa   = "";
 aAlfa1   = "";
 aAlfa2   = "";
 nModoEntrada = 0;

 aTipo = "";
 nSwEsta = 0;

 nSwSOLO = 0;
 aParam  = "";
 nVale   = 0;
 inform1 = "caz00030";
 inform2 = "caz10030";
 informe = "";
 aDef    = "";
 aMas    = "";

 &nSwLinea = 0;
 &aTexto   = "";
 &nSwTit   = 0;

 &aCamp1 = "";
 &nCamp2 = 0;
 &nCamp3 = 0;
 &aCamp4 = "";
 nHay    = 0;

 nDPer   = 0;
 nHPer   = 0;
 aDPer   = "";
 aHPer   = "";
|FIN;

|INCLUYE dscont_i;

|| /////////////////////// PROCESOS DE TRABAJO \\\\\\\\\\\\\\\\\\\\
|| ******************************************** Calculos Pantalla
|CALCULO wTipo12; |TIPO 12;
|FCAL;

|RUTINA ClaveBajaAxC;
 #14#0 = #0#0;
 #14#1 = #0#1;
 #14#11 = 0;
 #14#13 = 00;
|FRUT;

|RUTINA ClaveAltaAxC;
 #14#0  = #0#0;
 #14#1  = #0#1;
 #14#11 = 9;
 #14#13 = 99;
|FRUT;

|PROCESO wTipo3; |TIPO 11;
 nInkey = FSalida;
 |SI nInkey = 10;
     cod1 = #0#0;
     cod2 = #0#1;
     emEmp = 1;  || Sin mensaje
     |HAZ leelo;
     |SI swerror ! 0;
         |MENAV "    Error, ya no existe la Empresa Contable";
         |ACABA;
     |FINSI;

     |DFICB aAlfa;
     aAlfa1 = #0#0; aAlfa1 = ("00000"  + aAlfa1) % -105;
     aAlfa2 = #0#1; aAlfa2 = ("0"  + aAlfa2) % -101;
     dirfich0 = aAlfa + aAlfa1 + aAlfa2 + "/";

     nSwSal = 0;
     |ENTLINEAL #1#14, -4, 4, 22, 0, ClaveBajaAxC, ClaveAltaAxC;
     |ERROR;
 |FINSI;
 |SI nInkey = 12;
     |HAZ wTipo1;
 |FINSI;

 |SI nInkey = 13; |O nInkey = 14;
     |HAZ wTipo4;
     |ERROR;
 |FINSI;

|FPRC

|CALCULO PresentaLinea;  |TIPO 7;
 |ENTLINEAL #1#14, -4, 7, 22, 0, ClaveBajaAxC, ClaveAltaAxC;
|FCAL;

|| /////////////////////////////////////////////////////
|PROCESO Borrolin15;
 |BORRA 020#15a;
 |LIBERA #15;
|FPRC;

|DEFBUCLE borrarL15;
 #15#1;
 ;
 #14#0, #14#1, #14#11, #14#13, 00001;
 #14#0, #14#1, #14#11, #14#13, 99999;
 be;
 NULL, Borrolin15;
|FIN;

|PROCESO BorraElemento;
 |BUCLE borrarL15;

 |BORRA 020#14a;
 |LIBERA #14;
|FPRC;

|DEFBUCLE borrarElementos;
 #14#1;
 ;
 #0#0, #0#1, 0, 00;
 #0#0, #0#1, 9, 99;
 be;
 NULL, BorraElemento;
|FIN;

|CALCULO wTipo1;
     |CONFI "    NEsta Seguro de Borrar los Registros de esta Empresa ";
     |SI FSalida ! 0;
         |ERROR;
         |ACABA;
     |FINSI;
     |BUCLE borrarElementos;
|FCAL;

|| *****************************************************************
|| **************************************************************************
|PROCESO Leo15;
 nSwLinea=15;
 |PRINT;
 nSwTit = 1;
 nHay   = 1;
|FPRC;

|DEFBUCLE Leo15;
 #15#1;
 ;
 #14#0, #14#1, #14#11, #14#13, 00001;
 #14#0, #14#1, #14#11, #14#13, 99999;
 e;
 NULL, Leo15;
|FIN;

|PROCESO Leo14;
 nSwLinea=14;
 |PRINT;
 |SI nInkey = 13; |ACABA; |FINSI;

 nHay   = 0;
 nSwTit = 0;
 |BUCLE Leo15;
 |SI nHay = 1; nSwLinea = 1; |PRINT; |FINSI;
|FPRC;

|DEFBUCLE Leo14;
 #14#1;
 ;
 nDEmpresa, nDPeriodo, 0, 00;
 nHEmpresa, nHPeriodo, 9, 99;
 e;
 NULL, Leo14;
|FIN;

|PROCESO LeoEspejo;
 aCamp1 = #930#0; |QBF aCamp1;
 aCamp1 = ("00000" + aCamp1) % -105;
 nCamp2 = #930#1;
 nCamp3 = #930#2;
 aCamp4 = #930#3;
 nDEmpresa = #930#0; nDPeriodo = #930#1;
 nHEmpresa = #930#0; nHPeriodo = #930#1;
 nSwLinea = 0;
 |PRINT;
 |BUCLE Leo14;
|FPRC;

|DEFBUCLE LeoEspejo;
 #930#1002;
 ;
 00001, nDPer;
 99999, nHPer;
 ;
 NULL, LeoEspejo;
|FIN;

|PROCESO wTipo4;
  ||.... Imprimir relacion
  |SI nInkey = 13;

      aTexto = "RELACION DE EMPRESAS DE REO CON REGISTRO DE PASE";
      |SI nSwSOLO = 1; aTexto = "RELACION DE EMPRESAS DE REO CON DIREFRENCIAS EN REGISTROS DE PASE"; |FINSI;
      informe = inform1;

      |PUSHV  0511 0934;
      |CUADRO 0511 0934;
      |ATRI R; |PINTA 0612, " Selecciones Periodos "; |ATRI 0;
      |PINTA 0712, " Desde Periodo:       ";
      |PINTA 0812, " Hasta Periodo;       ";
      |PINTA 0728, #0#1;
      |PINTA 0828, #0#1;
      E_sup  = 1;
      E_inf  = "0123456789";
      aDPer  = #0#1; aDPer  = 0728 ? 0;
      aHPer  = #0#1; aHPer  = 0828 ? 0;
      |POPV;
  |SINO;
      aTexto = "REGISTROS DE DIFERENCIAS DE LA EMPRESA DE REO";
      informe = inform2;
  |FINSI;

  |IMPRE 0;
  |SI FSalida ! 0; |ACABA; |FINSI;
  |INFOR informe;
  |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

  |SI nInkey = 14;
      #930#0 = #0#0; #930#1 = #0#1;
      #930#2 = #0#2; #930#3 = #0#3;
      |HAZ LeoEspejo;
  |SINO;
       nDPer = aDPer; nHPer = aHPer;
      |BUCLE LeoEspejo;
  |FINSI;
  |PIEINF;
  |FININF;
  |FINIMP;
|FPRC;
|| **************************************************************************

|| *****************************************************************
|PROCESO GrabaTempo;
     nVale = 0;
     |SI nSwSOLO = 1; |Y #14#19 = " "; nVale = 1; |FINSI;
     |SI nVale = 0;
         #0#0 = #14#0;
         #0#1 = #14#1;
         |LEE 001#0=;
         |SI FSalida ! 0;
             #0#0 = #14#0;
             #0#1 = #14#1;
             |SI #14#3 < 80;
                 #0#2 = 2000 + #14#3;
             |SINO;
                 #0#2 = 1900 + #14#3;
             |FINSI;
             #0#3 = #14#2;
             |GRABA 020#0n;
             nSwGraba = 1;
         |FINSI;

         |SI #14#0 = enEmpresa; |Y #14#1 = enPeriodo;
             nSwEsta = 1;
         |FINSI;
    |FINSI;
|FPRC;

|DEFBUCLE cam00014;
    #14#1;
    ;
    nDEmpresa, nDPeriodo, 0, 00;
    nHEmpresa, nHPeriodo, 9, 99;
    ;
    NULL, GrabaTempo;
|FIN;

|| ----------------------------------------------------------------------
|RUTINA ClaveBajaC;
  |SI FSalida = "POSICION"; |Y nSwEsta = 1;
      #0#0 = enEmpresa;
      #0#1 = enPeriodo;
  |SINO;
      #0#0 = 1;
      #0#1 = 0;
  |FINSI;
|FRUT;

|RUTINA ClaveAltaC;
     #0#0 = 99999;
     #0#1 = 9;
|FRUT;

|PROGRAMA;
 nSwSOLO = 0;
 aParam = PARAMETRO; |QBF aParam;
 |SI aParam = "error"; nSwSOLO = 1; |FINSI;
 |HAZ inicio;

 aFichero = ("0z" + Usuario) % 108;
 |NOME_DAT #0 aFichero;
 |ABRE #0;  |CIERRA #0;  |DELINDEX #0;

 |DBASE aMas;
 aDef = aMas + "def/caz00030";
 |CARGA_DEF aDef, caz0030@;
 |SI FSalida ! 0;
     |ACABA;
 |FINSI;
 |NOME_DAT #930 aFichero;

 nSwEsta  = 0;
 nSwGraba = 0;
 |ABRE #0;
 nDEmpresa = 00000; nDPeriodo = 0;
 nHEmpresa = 99999; nHPeriodo = 9;
 |BUCLE cam00014;
 |CIERRA #0;


 |CLS;
 |CABEZA "Registro de Pase de REO";
 |PINPA #0#0;
 |SI nSwSOLO = 1;
     aAlfa1 = "Registro de Diferencias en el Pase a Contabilidad desde R.E.O. ";
 |SINO;
     aAlfa1 = "Registro del Pase de Empresas a Contabilidad desde R.E.O.      ";
 |FINSI;
 |ATRI S; |PINTA 0502, aAlfa1; |ATRI 0;
 || **************************************************  Tipo3;
 |ENTLINEAL #1#0, -1, 4, 22, 0, ClaveBajaC, ClaveAltaC;

 |DESCARGA_DEF #caz0030@;
|FPRO;
