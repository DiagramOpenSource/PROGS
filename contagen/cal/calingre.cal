|FICHEROS;
   calingre #0;
   camaasil #1;
   cacuenta #3;
   caconlib #6;
   caplctal #7;

   camaniva #20;

   canempre #30;
   caselem1 #998;
|FIN;

|VARIABLES;
   &entrada = 1;
   men1   = "    NO EXISTE EMPRESA";
   men2   = "    NO existe el Codigo de Columnas de Libros ";
   mensa1 = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
   inform1 = "calingre";
   inform2 = "calingrh";
   informe = "";
   tipo = "I";
   signo = "";
   importe = 0;
   ult_doc = 0;
   x = 0;
   y = 0;
   z = 0;
   p = 0;
   m ="";
   pn= 0;
   alfa1 = "";
   alfa2 = "";
   alfa3 = "";

   des_doc = 0;
   has_doc = 0;
   des_dia = 0;
   has_dia = 0;
   des_fec = @;
   has_fec = @;
   des_cue = "";
   has_cue = "";
   &lin1 = 0
   &lin2 = 0
   &lin3 = 0
   &lin4 = 0
   &lin5 = 0
   &lin6 = 0
   &lin7 = 0
   &lin8 = 0
   &lin9 = 0

   sw_col = 0;
   cuenta = "";

   &enEjer = 0;
   &regis = 0;
   &sw = 0;
   &swlin = 0;
   &hoja = 0;
   &titulo = "* VENTAS E INGRESOS *";
   &vari = "REGIS.";
   &fech = "";
   &p1 = "";
   &p2 = "";
   &p3 = "";
   &p4 = "";
   &p5 = "";
   &p6 = "";
   &p7 = "";
   &p8 = "";
   &p9 = "";
   &p10 = 0;
   &pt1 = "";
   &pt2 = "";
   &pt3 = "";
   &pt4 = "";
   &pt5 = "";
   &pt6 = "";
   &pt7 = "";
   &pt8 = "";
   &pt9 = "";
   &pt10 = 0;
   &hor = "";
   &asi = "";
   &aii = "";
   &tea = "";
   &tei = "";
   &cru = "";
   &teb = "";
   &ted = "";
   &vert= "";
   &asd = "";
   &aid = "";

  || Cont = 0;
   aAlfa1 = "";
   aAlfa2 = "";
   aAlfa3 = "";
   aAlfa4 = "";
   nume1  = 0;

   nDDiario = 0;
   nHDiario = 0;
   fDFecha  = @;
   fHFecha  = @;
   nDDocum  = 0;
   nHDocum  = 0;
   nCampo7  = 0;
   aCampo8  = "";
   aCampo9  = "";
   aCampo10 = "";
   aCampo11 = "";
   fCampo12 = @;
   nCampo13 = 0;
   nCampo14 = 0;
   nCampo15 = 0;
   aCampo16 = "";
|FIN;

|INCLUYE dscont_i;
____________________________________/ CALCULOS DE PANTALLA

|PROCESO sacaimpo;
   |SI #0#16 = "S";
      |PARA x = 18; |SI x [ 27; |HACIENDO x = x + 1;
         |CAMPO_MODIFICA #0x,1,"N";
         #0x = 0; |PINTA #0x;
      |SIGUE;
   |SINO;
      |PARA x = 18; |SI x [ 26; |HACIENDO x = x + 1;
         |CAMPO_MODIFICA #0x,1,"S";
      |SIGUE;
   |FINSI;
|FPRC;

|PROCESO sumaimpo;
   #0#27 = 0;
   |PARA x = 18; |SI x [ 26; |HACIENDO x = x + 1;
      #0#27 = #0#27 + #0x;
   |SIGUE;
   |PINTA #0#27;
|FPRC;

|PROCESO fecha;
   |SI #0Campo < fec1; |O #0Campo > fec2;
      |MENAV mensa1;
      |ERROR;
      |ACABA;
   |FINSI;
   |SI Campo = 4;
      |SI #0#4 < #0#3; |ERROR; |FINSI;
   |FINSI;
|FPRC;

|PROCESO leeconlib; |TIPO 0;
   |SI FSalida = 2; |ACABA; |FINSI;
   |SI #0Campo = 0; |ERROR; |ACABA; |FINSI;
   |ABRE #6;
   #6#0 = #0Campo;
   |LEE 001#6=;
   |SI FSalida ! 0;
      |MENAV men2;
      |ERROR;
   |SINO;
      |ATRI I;
      |PINTA 1932, #6#64;
      |ATRI 0;
   |FINSI;
   |CIERRA #6;
|FPRC;

|PROCESO impsemi; |TIPO 0;
   |SI #0#9 = "S";
      |CAMPO_MODIFICA #0#10,1,"N";
      |CAMPO_MODIFICA #0#11,1,"N";
   |SINO;
      |CAMPO_MODIFICA #0#10,1,"S";
      |CAMPO_MODIFICA #0#11,1,"S";
   |FINSI;
|FPRC;

|| **************************************************************************

|PROCESO vercol1;

   |SI #caplctal#7 = "*"; |ACABA; |FINSI;
   alfa1 = #caplctal#2;
   |QBF alfa1;
   alfa2 = alfa1 % 0;
   p     = alfa2;
   p     = p + 100;
   alfa3 = cuenta % p;
   |SI alfa1 = alfa3;
      |SI #caplctal#7 = "0"; |Y #camaasil#8 = "H"; |ACABA; |FINSI;
      |SI #caplctal#7 = "1"; |Y #camaasil#8 = "D"; |ACABA; |FINSI;
      sw_col = #caplctal#6;
      signo  = #caplctal#7;
      |SI signo = "0"; signo = "D"; |FINSI;
      |SI signo = "1"; signo = "H"; |FINSI;
      |ERROR10;
   |FINSI;
|FPRC;

|DEFBUCLE vercol0;
   #7#2;
   ;
   #0#7, tipo, des_cue;
   #0#7, tipo, has_cue;
   be;
   NULL,vercol1;
|FIN;

|PROCESO vercolumna;
   cuenta  = #camaasil#4;
   has_cue = cuenta;
   alfa1   = cuenta % 101;
   alfa1   = alfa1 + doce;
   alfa1   = alfa1 % 112;
   des_cue = alfa1;
   sw_col  = 0;
   signo   = "";
   |BUCLE vercol0;
|FPRC;

|| ****************************************
|PROCESO xcontador;
   |SI #0#14 = 0;
       regis = #camaasil#2;
   |SINO;
       regis = regis + 1;
   |FINSI;
   ult_doc = #camaasil#2;
|FPRC;

|PROCESO leecuen;
 |DDEFECTO #cacuenta;

 |SI #0#8 = 1;
     #cacuenta#0 = #1#4;
     |LEE 001#cacuenta.=;
 |FINSI;
 |SI #0#8 = 2;
     |ABRE #camaniva;
     |SELECT #3#camaniva;
     #camaniva#0  = #camaasil#13;
     #camaniva#2  = #camaasil#15;
     #camaniva#3  = #camaasil#14;
     |LEE 001#camaniva.=;
     |SI FSalida = 0;
         #cacuenta#0 = #camaniva#12;
         |LEE 001#cacuenta.=;
     |SINO;
        #cacuenta#0 = #1#4;
        |LEE 001#cacuenta.=;
     |FINSI;
     |CIERRA #camaniva;
 |FINSI;
|FPRC;

|PROCESO borrar;
   lin1 = 0;
   lin2 = 0;
   lin3 = 0;
   lin4 = 0;
   lin5 = 0;
   lin6 = 0;
   lin7 = 0;
   lin8 = 0;
   lin9 = 0;
   p10 = 0;
|FPRC;

|PROCESO implin;
   pn = lin1; |HAZ cambi_nu; p1 = m;
   pn = lin2; |HAZ cambi_nu; p2 = m;
   pn = lin3; |HAZ cambi_nu; p3 = m;
   pn = lin4; |HAZ cambi_nu; p4 = m;
   pn = lin5; |HAZ cambi_nu; p5 = m;
   pn = lin6; |HAZ cambi_nu; p6 = m;
   pn = lin7; |HAZ cambi_nu; p7 = m;
   pn = lin8; |HAZ cambi_nu; p8 = m;
   pn = lin9; |HAZ cambi_nu; p9 = m;
|FPRC;

|PROCESO totales;
   pt10 = #0#27;
   pn = #0#18; |HAZ cambi_nu; pt1 = m;
   pn = #0#19; |HAZ cambi_nu; pt2 = m;
   pn = #0#20; |HAZ cambi_nu; pt3 = m;
   pn = #0#21; |HAZ cambi_nu; pt4 = m;
   pn = #0#22; |HAZ cambi_nu; pt5 = m;
   pn = #0#23; |HAZ cambi_nu; pt6 = m;
   pn = #0#24; |HAZ cambi_nu; pt7 = m;
   pn = #0#25; |HAZ cambi_nu; pt8 = m;
   pn = #0#26; |HAZ cambi_nu; pt9 = m;
|FPRC;

|| ****************************************
|PROCESO imprimir;
   |HAZ vercolumna;
   |SI sw_col =  0; |ACABA; |FINSI;

   |HAZ borrar;
   |HAZ leecuen;
   |HAZ xcontador;
   alfa1 = #camaasil#1;
   alfa1 = alfa1 % 105;
   alfa2 = alfa1 % 102;
   alfa3 = alfa1 % -102;
   fech = alfa2 + alfa3;
   importe = #camaasil#9;
   |SI signo ! #camaasil#8; |O #caplctal#7 = "1";
       importe = - importe;
   |FINSI;
   |SI sw_col = 1;  lin1 = importe; |FINSI;
   |SI sw_col = 2;  lin2 = importe; |FINSI;
   |SI sw_col = 3;  lin3 = importe; |FINSI;
   |SI sw_col = 4;  lin4 = importe; |FINSI;
   |SI sw_col = 5;  lin5 = importe; |FINSI;
   |SI sw_col = 6;  lin6 = importe; |FINSI;
   |SI sw_col = 7;  lin7 = importe; |FINSI;
   |SI sw_col = 8;  lin8 = importe; |FINSI;
   |SI sw_col = 9;  lin9 = importe; |FINSI;
   p10 = importe;
   |HAZ implin;
   swlin = 1;
   |PRINT;
   sw=1;
   p = sw_col + 17;
   #0p   = #0p   + importe;
   #0#27 = #0#27 + importe;
   |HAZ totales;
|FPRC;

|DEFBUCLE calingre3;
   #1#1;
   ;
   des_dia, des_fec, des_doc, 0001;
   has_dia, has_fec, has_doc, 9999;
   e;
   NULL,NULL,NULL,NULL,NULL,imprimir;
   #1#1,#1#0,#1#2;
|FIN;


|DEFBUCLE calingre2;
   #1#1;
   ;
   des_dia, des_fec, des_doc, 0001;
   has_dia, has_fec, has_doc, 9999;
   e;
   NULL,NULL,NULL,NULL,NULL,imprimir;
   #1#2, #1#0, #1#1;
|FIN;

|DEFBUCLE calingre1;
   #1#1;
   ;
   des_dia, des_fec, des_doc, 0001;
   has_dia, has_fec, has_doc, 9999;
   e;
   NULL,imprimir;
|FIN;

|| ****************************************
|PROCESO anterior;

   |HAZ vercolumna;
   |SI sw_col ! 0;
       p = sw_col + 17;
       importe = #camaasil#9;
       |SI signo ! #camaasil#8; |O #caplctal#7 = "1";
           importe = - importe;
       |FINSI;
       #0p   = #0p + importe;
       #0#27 = #0#27 + importe;
       sw=1;
       |HAZ totales;
       || ... |HAZ xcontador; NO
   |FINSI;
|FPRC;

|DEFBUCLE calingre0;
   #1#1;
   ;
   des_dia, des_fec, des_doc, 0001;
   has_dia, has_fec, has_doc, 9999;
   be;
   NULL,anterior;
|FIN;

|| ******************** Calculo Imprimir
|PROCESO impre;

   |SI #0#28 = "N";
       informe = inform1;
   |SINO;
       informe = inform2;
   |FINSI;

   |INFOR informe;
   |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

   |ABRE #7;

   |SI #0#16 = "S"; || sumas anteriores
      des_dia = #0#1; || los diarios que pide 00;
      has_dia = #0#2; ||                      99;
      des_fec = fec1;
      x = #0#3;
      x = x - 1;
      has_fec = x;
      des_doc = 000001;
      has_doc = 999999; || manda la fecha, #0#5 - 1;
      |BUCLE calingre0;
   |FINSI;

   des_dia = #0#1;
   has_dia = #0#2;
   des_fec = #0#3;
   has_fec = #0#4;
   des_doc = #0#5;
   has_doc = #0#6;

   |ABRE #3;
   |SI #0#15 = 1; |BUCLE calingre1; |FINSI;
   |SI #0#15 = 2; |BUCLE calingre2; |FINSI;
   |SI #0#15 = 3; |BUCLE calingre3; |FINSI;

   |CIERRA #3;
   |CIERRA #7;
   |SI swlin = 1; |PIEINF; |FINSI;
   |FININF;
|FPRC;

|PROCESO variaimpr;
   |SI #0#9 = "S";
      hor        = "³";
      asi        = "Ú";
      aii        = "À";
      tea        = "Â";
      tei        = "Ã";
      cru        = "Å";
      teb        = "Á";
      ted        = "´";
      vert       = "ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ";
      asd        = "¿";
      aid        = "Ù";
   |SINO;
      hor        = #0#11;
      asi        = #0#11;
      aii        = #0#11;
      tea        = #0#11;
      tei        = #0#11;
      cru        = #0#11;
      teb        = #0#11;
      ted        = #0#11;
      vert       = #0#10 * 25;
      asd        = #0#11;
      aid        = #0#11;
   |FINSI;
|FPRC;

|PROCESO cambi_nu;

 aAlfa2 = "           ";
 |SI #0#28 = "S"; aAlfa2 = "         "; |FINSI;

 |SI #0#9 = "S"; #0#11 = "³" ; |FINSI;

 m = #0#11 + aAlfa2 + #0#11;

 aAlfa1 = pn; |QBF aAlfa1;

 |SI aAlfa1 ! 0; |Y aAlfa1 ! "-0";
     |SI EURODB = 2;
         aAlfa3 = aAlfa1 % -103;
         aAlfa4 = aAlfa3 % 101;
         |SI aAlfa4 ! ".";
             aAlfa4 = aAlfa3 % 201;
             |SI aAlfa4 = ".";
                 aAlfa1 = aAlfa1 + "0";
             |SINO;
                 aAlfa1 = aAlfa1 + ".00";
             |FINSI;
         |FINSI;
     |FINSI;

     aAlfa3 = aAlfa1 % 0; nume1 = aAlfa3;

     |SI #0#28 = "N";
         |SI nume1 [ 11;
             aAlfa1 = aAlfa2 + aAlfa1;
             aAlfa1 = aAlfa1 % -111;
             m = #0#11 + aAlfa1 + #0#11;
         |SINO;
             aAlfa1 = aAlfa2 + aAlfa1;
             aAlfa1 = aAlfa1 % -112;
             m = aAlfa1 + #0#11;
         |FINSI;
     |SINO;
          |SI nume1 [ 9;
              aAlfa1 = aAlfa2 + aAlfa1;
              aAlfa1 = aAlfa1 % -109;
              m = #0#11 + aAlfa1 + #0#11;
          |SINO;
              aAlfa1 = aAlfa2 + aAlfa1;
              aAlfa1 = aAlfa1 % -110;
              m = aAlfa1 + #0#11;
          |FINSI;
     |FINSI;

  |FINSI;
|FPRC;


|PROCESO HazTodo;

   enEjer = enEjercicio;
   sw    = 0;
   swlin = 0;
   hoja  = #0#13;
   regis = #0#14 - 1;
   vari = "REGIS."; |SI #0#14 = 0; vari = "DOCUM.";|FINSI;

   |PARA x = 18; |SI x [ 27; |HACIENDO x = x + 1;
         |SI #0#16 = "S";
             #0x = 0;
         |SINO;
             |SI #0x ! 0; sw = 1; |FINSI;
         |FINSI;
   |SIGUE;

   |HAZ totales;
   |HAZ variaimpr;
   |HAZ impre;

   |SI swlin ! 0;
       |SI #0#4 ! fec2;
           #0#3 = #0#4 + 1;
       |SINO;
           #0#3 = fec2;
       |FINSI;
       #0#4 = fec2;
       #0#5 = ult_doc + 1;
       #0#6 = 999999;
       |SI #0#5 > #0#6; #0#5 = #0#6; |FINSI;
       #0#13 = hoja;
       |SI #0#14 ! 0; #0#14 = regis + 1; |FINSI;
       #0#16 = "N";
       |GRABA 011#0a;
   |FINSI;

   |LIBERA #0;
   |CIERRA #0;
|FPRC;


|| **************************************************************************
|PROGRAMA;

   |HAZ inicio;
   |CLS;
   |CABEZA "Libro de Ingresos";

   |ABRE #0;
   #0#0 = 1;
   |LEE 111#0=;
   |SI FSalida ! 0;
      |DDEFECTO #0;
      #0#3 = fec1;
      #0#4 = fec2;
      |GRABA 011#0c;
   |FINSI;
   |LIBERA #0;

   |PINPA #0#0;
   #0#0 = 1;
   |LEE 111#0=;
   #0#12 = ~;

   |PINDA #0#0;
   |ENDATOS #1#0;
   |SI FSalida ! 0;  |LIBERA #0; |CIERRA #0;  |ACABA;  |FINSI;

   |HAZ GuardaPantalla;

   |IMPRE 0;
   |SI FSalida ! 0; |ACABA; |FINSI;

   |SI #0#28 = "N";
       |MENAV "0000 Recuerde de seleccionar el papel de la impresora en Horizontal";
   |FINSI;

   |SI #48#27 = "S";
       |HAZ MultiEmpresa;
   |SINO;
       |DFICE dirfich0;
       |HAZ HazTodo;
   |FINSI;
   |FINIMP;
|FPRO;

|| ***********************************************************************

|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #0 dirfich0;
   |PATH_DAT #1 dirfich0;
   |PATH_DAT #3 dirfich0;
   |PATH_DAT #6 dirfich0;
   |PATH_DAT #7 dirfich0;
   |PATH_DAT #20 dirfich0;

   |HAZ LeePantalla;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 001;
 999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
   |GRABA 020#0a;
   |LIBERA #0;
   |CIERRA #0;

   |SUB_EJECUTA "caselemp";
   |NOME_DAT #998 eaFichsel;
   |BUCLE MultiEmp0;
   |DELINDEX #998;
|FPRC;

|PROCESO GuardaPantalla;
 nDDiario = #0#1;
 nHDiario = #0#2;
 fDFecha  = #0#3;
 fHFecha  = #0#4;
 nDDocum  = #0#5;
 nHDocum  = #0#6;

 nCampo7  = #0#7;
 aCampo8  = #0#8;
 aCampo9  = #0#9;
 aCampo10 = #0#10;
 aCampo11 = #0#11;
 fCampo12 = #0#12;

 nCampo13 = #0#13;
 nCampo14 = #0#14;

 nCampo15 = #0#15;

 aCampo16 = #0#16;

|FPRC;

|PROCESO LeePantalla;

 |ABRE #0;
 #0#0 = 1;
 |LEE 111#0=;
 |SI FSalida ! 0;
     |DDEFECTO #0;
     |GRABA 011#0b;
 |FINSI;

 #0#1 = nDDiario;
 #0#2 = nHDiario;
 #0#3 = fDFecha;
 #0#4 = fHFecha;
 #0#5 = nDDocum;
 #0#6 = nHDocum;

 #0#7  = nCampo7;
 #0#8  = aCampo8;
 #0#9  = aCampo9;
 #0#10 = aCampo10;
 #0#11 = aCampo11;
 #0#12 = fCampo12;

 |SI nCampo13 = 0; #0#13 = 0; |FINSI;
 |SI nCampo14 = 0; #0#14 = 0; |FINSI;

 nCampo15 = #0#15;

 #0#16 = aCampo16;
 |SI aCampo16 = "S";
     #0#18 = 0; #0#19 = 0; #0#20 = 0; #0#21 = 0; #0#22 = 0;
     #0#23 = 0; #0#24 = 0; #0#25 = 0; #0#26 = 0; #0#27 = 0;
 |FINSI;
|FPRC;
