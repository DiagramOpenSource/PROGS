|FICHEROS;
   n43desco #0;

   camaasil #1;
   zdescili #2,MANTE;

   cacuenta #10;
   n43histo #20;
   n43datos #21;
|FIN;

|VARIABLES;
    &entrada = 1;
    vacio    = "            ";
    feini    = @;
    fefin    = @;
    fichero  = "";
    men2      = "     Longitud de Cuenta Fuera de Nivel !";

    conta     = 0;
    tipo      = 0;
    sw        = 0;
    error     = 0;
    &contador = 0;
|FIN;

|INCLUYE dscont_i;

|PROCESO cuenta;|TIPO 6;

   |SI #0Campo ! vacio;
       eaCuenta = #0Campo;
       salidas  = FSalida;
       |HAZ FiltraPuntos;
       #0Campo = eaCuenta; |PINTA #0Campo;
   |SINO;
        |ABRE #21;
        |LEE 040#21u;
        |SI FSalida = 0;
            #0Campo = #21#1; |PINTA #0Campo;
        |SINO;
            |CIERRA #21;
            |MENAV "    No existen Datos";
            |ERROR;
            |ACABA;
        |FINSI;
        |CIERRA #21;
   |FINSI;

   eaCuenta = #0#0;
   |HAZ SacaNivel; #0#1 = enNivel;
   |SI eswCta = 1;
       |MENAV men2;
       |ERROR;
   |FINSI;
|FPRC;

|PROCESO cuefin; |TIPO 0;
   error = 0;
   |ABRE #10;
   #10#0 = #00;
   |LEE 040#10=;
   |SI FSalida ! 0;
       |CIERRA #10;
        error = 1;
       |MENAV "    No existe Cuenta";
       |ERROR;
       |ACABA;
  |FINSI;
  |SI #10#3 = "N";
      error = 1;
      |CIERRA #10;
      |MENAV "      La cuenta no tiene pase DIRECTO !";
      |ERROR;
      |ACABA;
  |FINSI;
  #0#4  = #10#2;  |PINTA #0#4;
  #0#7  = #10#51; |PINTA #0#7;
  #0#11 = #10#52; |PINTA #0#11;
  #0#5  = #10#53; |PINTA #0#5;
  |CIERRA #10;
|FPRC;

|| ***********************************************************************
|PROCESO PintaCamps;
 #0#3 = #0#9  - #0#13;
 #0#6 = #0#10 - #0#14;
 |PINTA #0#9;  |PINTA #0#13; |PINTA #0#3;
 |PINTA #0#10; |PINTA #0#14; |PINTA #0#6;
|FPRC;


||... Procesos de Zdescili
|PROCESO pintali1; |TIPO 0;
   |CAMPO_ATRIBUTO #2#0, R, 0, 0; |PINTA #2#0;
   |CAMPO_ATRIBUTO #2#2, R, 0, 0; |PINTA #2#2;
   |CAMPO_ATRIBUTO #2#3, R, 0, 0; |PINTA #2#3;
   |CAMPO_ATRIBUTO #2#5, R, 0, 0; |PINTA #2#5;
   |CAMPO_ATRIBUTO #2#6, R, 0, 0; |PINTA #2#6;
   |CAMPO_ATRIBUTO #2#7, R, 0, 0; |PINTA #2#7;
   |CAMPO_ATRIBUTO #2#8, R, 0, 0; |PINTA #2#8;
   |HAZ pidetecl;
|FPRC;

|PROCESO pidetecl; |TIPO 0;
   |SI FSalida ! 0;  |ACABA;  |FINSI;
   |LEE 101#0=;
   |SI #2#8 = "*";
       #2#8 = " ";
       #0#9  = #0#9  + #2#6;
       #0#13 = #0#13 + #2#7;

       #0#10 = #0#10 + #2#6;
       #0#14 = #0#14 + #2#7;
   |SINO;
       #2#8 = "*";
       #0#9  = #0#9  - #2#6;
       #0#13 = #0#13 - #2#7;

       #0#10 = #0#10 - #2#6;
       #0#14 = #0#14 - #2#7;
   |FINSI;
   |PINTA #2#8;
   |GRABA 020#2a;
   |LIBERA #2;
   |HAZ PintaCamps;
|FPRC;

|PROCESO pintali2; |TIPO 0;
   |CAMPO_ATRIBUTO #2#0, D, 0, 0;
   |CAMPO_ATRIBUTO #2#2, D, 0, 0;
   |CAMPO_ATRIBUTO #2#3, D, 0, 0;
   |CAMPO_ATRIBUTO #2#5, D, 0, 0;
   |CAMPO_ATRIBUTO #2#6, D, 0, 0;
   |CAMPO_ATRIBUTO #2#7, D, 0, 0;
   |CAMPO_ATRIBUTO #2#8, D, 0, 0;
   |PINTA #2#0;
   |PINTA #2#2;
   |PINTA #2#3;
   |PINTA #2#5;
   |PINTA #2#6;
   |PINTA #2#7;
   |PINTA #2#8;
|FPRC;
|| ********************************************************************
|PROCESO calcuant;
  |SI #1#18 = " ";              || Debe, Haber y saldo Pendiente Conciliar
      |SI #1#8 = "D";
          #0#10 = #0#10 + #1#9;
      |SINO;
          #0#14 = #0#14 + #1#9;
      |FINSI;
      |ACABA;
  |FINSI;

  || Apuntes Conciliados

  |SI #1#8 = "D";              || Debe, haber y saldo Conciliado
      #0#8  = #0#8 + #1#9;
  |SINO;
      #0#12 = #0#12 + #1#9;
  |FINSI;
  #0#2 = #0#8 - #0#12;

  contador = contador + 1;
  #2#0 = contador;
  #2#1 = #1#0;
  #2#2 = #1#1;
  #2#3 = #1#2;
  #2#4 = #1#3;
  #2#5 = #1#7;
  |SI #1#8 = "D";
      #2#6 = #1#9;
      #2#7 = 0;
  |SINO;
      #2#7 = #1#9;
      #2#6 = 0;
  |FINSI;
  #2#8 = "*";
  |GRABA 020#2n;
|FPRC;

|DEFBUCLE camaasil0;
   #1#3;
   ;
   #0#0, feini, 000000, 0001;
   #0#0, fefin, 999999, 9999;
   e;
   NULL, calcuant;
|FIN;
|| /////////////////////////////////////////////////

|PROCESO cero;
     #21#0 = #20#1;
     |LEE 040#21=;
     |SI FSalida = 0;
         #21#9 = "N";
         #21#11 = "N";
         |GRABA 020#21a;
     |FINSI;
|FPRC;

|PROCESO lehis_tipo1;
   #21#0 = #20#1;
   |LEE 040#21=;                  || Marca los registros de entradas
   |SI FSalida = 0;
       #21#9  = "N";
       #21#11 = "N";
       |GRABA 020#21a;
   |FINSI;

   #1#0 = #20#18;
   #1#1 = #20#12;
   #1#2 = #20#13;
   #1#3 = #20#19;
   |LEE 040#1=;
   |SI FSalida = 0;
       #1#18 = " ";
       |GRABA 020#1a;
   |FINSI;
   |BORRA 020#20a;
   |LIBERA #20;
|FPRC;

|DEFBUCLE lehis_tipo1;
    #20#2;
    ;
    conta;
    conta;
    be;
    NULL, lehis_tipo1;
|FIN;

|PROCESO lehis2;
    #21#0 = #20#1;
    |LEE 040#21=;                  || Marca los registros de entradas
    |SI FSalida = 0;
        #21#9  = "N";
        #21#11 = "N";
        |GRABA 020#21a;
    |FINSI;

    #1#0 = #20#18;
    #1#1 = #20#12;
    #1#2 = #20#13;
    #1#3 = #20#19;
    |LEE 040#1=;
    |SI FSalida = 0;
        #1#18 = " ";
        |GRABA 020#1a;
    |FINSI;
    |BORRA 020#20a;
|FPRC;

|DEFBUCLE lehis_tipo2;
    #20#3;
    ;
    #1#4,#1#5,#1#1,#1#2;
    #1#4,#1#5,#1#1,#1#2;
    ;
    NULL, lehis2;
|FIN;

|PROCESO borrahis;
     |SELECT #3#20;
     #20#10  = #1#4; ||Cuenta Apunte;
     #20#11  = #1#5; ||Digito;
     #20#12  = #1#1; ||Fecha Apunte;
     #20#13  = #1#2; ||Documento Apunte;
     |LEE 040#20=;  ||Lee Fichero Consolidaciones;
     |SI FSalida = 0;
         conta = #20#1;
         tipo  = #20#20;
         |SI tipo = 0;
             |HAZ cero;
             |BORRA 020#20a;
         |FINSI;
         |SI tipo = 1;
             |BUCLE lehis_tipo1;
         |FINSI;
         |SI tipo = 2;
             |BUCLE lehis_tipo2;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO grabaapun;
    #1#0 = #2#1;
    #1#1 = #2#2;
    #1#2 = #2#3;
    #1#3 = #2#4;
    |LEE 101#1=;
    |SI FSalida = 0;
        #1#18 = " ";
        |GRABA 020#1a;
        |HAZ borrahis;
    |FINSI;
    |LIBERA #1;
|FPRC;

|DEFBUCLE zconcili;
    #2#1;
    8;
    0001, " ";
    9999, " ";
    e;
    NULL, grabaapun;
|FIN;

|| *********************************************************************
|RUTINA inferior;
   #2#0 = 1;
|FRUT;

|RUTINA superior;
   #2#0 = contador;
|FRUT;

|PROCESO concilia; |TIPO 3;
 |SI error = 1; |ACABA; |FINSI;

 fichero = ("y" + Usuario) % 108;
 |NOME_DAT #2 fichero;
 |DELINDEX #2;

 |MENSA "2400 Procesando ......";

 |ABRE #2;
 contador = 0;
 feini = "01.01.1900";
 fefin = "31.12.2999";
 |BUCLE camaasil0;
 |CIERRA #2;
 |BLIN 24;

 |PINTA #0#8;  |PINTA #0#12; |PINTA #0#2;
 #0#9 = 0; #0#13 = 0; #0#3 = 0;
 |HAZ PintaCamps;

 |SI contador = 0;
     |MENAV "      No hay nada para Desconciliar !!!";
     |DELINDEX #2;
     |ACABA;
 |FINSI;

 |ENTLINEAL #1#2, -1, 4, 22, 0, inferior, superior;
 |CONFI "2400SActualizar Desconciliacion: ";
 |SI FSalida = 0;
    |ABRE #1;
    |ABRET #20;
    |ABRE  #21;
    |BUCLE zconcili;
    |CIERRA  #21;
    |CIERRAT #20;
    |CIERRA  #1;
 |FINSI;
 |DELINDEX #2;
|FPRC;
