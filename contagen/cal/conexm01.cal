|FICHEROS;
 conexm02 #0;
 conexm01 #1;

 cacuenta #100;
 conexw01 #101;
|FIN;

|VARIABLES;
 alfa1 = "";
 x = 0;
 doce = "            ";
 cta  = 0;
 cta1 = "";
 long = "";
 sw = 0;


    p_lugar = 0;
    p_cuan3 = 0;
    p_cuan4 = 0;
    p_cuan5 = 0;
    p_alfa1 = "";
    p_alfa2 = "";
    p_alfa3 = "";
    p_alfa4 = "";
    p_alfa5 = "";
    p_nume1 = 0;
    p_nume2 = 0;
    p_ceros = "";
    p_error = 0;
    p_mensa = "    LONGITUD DE CUENTA FUERA DE NIVEL!";
    desdec1 = "";
    hastac1 = "";
    varia_cod = "";
    sw_nom = 0;
    cuen11 = "";
    cuen21 = "";
    cuen31 = "";
    num_campo = 0;

    salidas = 0;
    dig1    = 0;
    dig2    = 0;
    dig3    = 0;
    dig4    = 0;
    dig5    = 0;
    dig6    = 0;
    dig7    = 0;
    dig8    = 0;
    dig9    = 0;
|FIN;

|CALCULO cuenta; |TIPO 0;
  |SI FSalida = 2; |ACABA; |FINSI;
  p_alfa1 = #0Campo;
  salidas = FSalida;
  |HAZ punto;
  |SI p_error ! 0;    |ERROR; |ACABA; |FINSI;
  |SI #0Campo = doce; |ERROR; |ACABA; |FINSI;

   x = Campo + 2;

  alfa1 = #0Campo % 101;
  |SI alfa1 = "0";
      cta1 = #0Campo;
      |QBF cta1;
      long = cta1 % 0;
  |SINO;
      cta  = #0Campo;
      long = cta % 0;
  |FINSI;
   sw = 0;
   |SI long = dig4; sw = 1; #0x = 1; |FINSI;
   |SI long = dig5; sw = 1; #0x = 2; |FINSI;
   |SI long = dig6; sw = 1; #0x = 3; |FINSI;
   |SI long = dig7; sw = 1; #0x = 4; |FINSI;
   |SI long = dig8; sw = 1; #0x = 5; |FINSI;
   |SI long = dig9; sw = 1; #0x = 6; |FINSI;
   |SI sw = 0;
       |MENAV "    Longitud de Cuenta fuera de Nivel ...";
       |ERROR;
       |ACABA;
   |FINSI;
|FCAL;

|PROCESO cuenfin; |TIPO 0;
 |HAZ cuenta;
 |SI sw = 0; |ACABA; |FINSI;
 |SI #0#3 < #0#2;
     |MENAV "    Cuenta Erronea. !";
     |ERROR;
 |FINSI;
|FPRC;

/****************************************************************************/
/****************************************************************************/
/****************************************************************************/
/****************************************************************************/

|CALCULO punto;               || desde calculo 'cuenta'
  |SI salidas = 18;
      num_campo = Campo;
      |HAZ cuent1;
      |ACABA;
  |FINSI;
  p_error = 0;
  p_lugar = 0;
  p_cuan3 = 0;
  p_cuan4 = 0;
  p_alfa3 = "";
  p_alfa4 = "";
  p_alfa4 = p_alfa1 > " ";
  p_alfa1 = p_alfa4;
  p_alfa4 = "";
  |PARA p_nume1 = 1; |SI p_nume1 [ 12; |HACIENDO p_nume1 = p_nume1 + 1;
        p_nume2 = (p_nume1 * 100) + 1;
        p_alfa2 = p_alfa1 % p_nume2;
        |SI p_alfa2 ! " ";
            |SI p_alfa2 = ".";
                p_lugar = p_nume1;
            |SINO;
                |SI p_lugar=0; p_alfa3=p_alfa3+p_alfa2; p_cuan3=p_cuan3+1; |FINSI;
                |SI p_lugar!0; p_alfa4=p_alfa4+p_alfa2; p_cuan4=p_cuan4+1; |FINSI;
            |FINSI;
        |FINSI;
  |SIGUE;
  |SI p_lugar ! 0;
      |SI dig3 = 1;  p_cuan5 = dig4;  |FINSI;
      |SI dig3 = 2;  p_cuan5 = dig5;  |FINSI;
      |SI dig3 = 3;  p_cuan5 = dig6;  |FINSI;
      |SI dig3 = 4;  p_cuan5 = dig7;  |FINSI;
      |SI dig3 = 5;  p_cuan5 = dig8;  |FINSI;
      |SI dig3 = 6;  p_cuan5 = dig9;  |FINSI;
      p_nume1 = p_cuan5 - (p_cuan3 + p_cuan4);
      |SI p_nume1 > 0;
          cuen11 = p_alfa4 % 101;
          p_ceros = "0" * p_nume1;
          p_alfa5 = p_alfa3 + p_ceros + p_alfa4;
          cuen11 = p_alfa4 % 101;
          |SI cuen11 = "U";  |O cuen11 = "A";
              cuen31 = cuen11;
              |HAZ ultialta;
          |FINSI;
          #0Campo = p_alfa5;
          |PINTA #0Campo;
      |SINO;
          |MENAV p_mensa;
          p_error = 1;
      |FINSI;
  |SINO;
      p_alfa5 = p_alfa1;
  |FINSI;
|FCAL;

|PROCESO busca_cuen1;
     cuen31 = #cacuenta#2;
     cuen31 = cuen31 > cuen31;
     cuen21 = cuen31 - cuen11;
     |SI cuen21 ! cuen31; |Y #cacuenta#3 = "S";
          #conexw01#1 = #cacuenta#2;
          #conexw01#2 = #cacuenta#0;
          |PINTA #conexw01#1;
          |PINTA #conexw01#2;
          |PAUSA;
          |SI FSalida = 1;  sw_nom = 1;  |ERROR10;  |FINSI; ||Esc
          |SI FSalida = 7;  |ERROR10;  |FINSI;    ||Ctrl+C
     |FINSI;
|FPRC;

|DEFBUCLE consulta11;
#cacuenta#1;
;
desdec1;
hastac1;
e;
NULL , busca_cuen1;
|FIN;

|PROCESO cuent1; |TIPO 0;
          sw_nom = 0;
          |PUSHV 0501 2380;
          |DDEFECTO #conexw01;
          |PINPA #0#conexw01;
          |ENDATOS #1#conexw01;
          |SI FSalida = 0;
               |SI #conexw01#0 ] 1; |Y #conexw01#0 [ 7;
                    desdec1 = #conexw01#0 + "           ";
                    hastac1 = #conexw01#0 + "99999999999";
               |SINO;
                    |SI #conexw01#0 = "C";
                         desdec1 = "4300        ";
                         hastac1 = "439999999999";
                    |FINSI;
                    |SI #conexw01#0 = "P";
                         desdec1 = "4000        ";
                         hastac1 = "419999999999";
                    |FINSI;
                    |SI #conexw01#0 = "T";
                         desdec1 = "            ";
                         hastac1 = "zzzzzzzzzzzz";
                    |FINSI;
               |FINSI;
               cuen11 = #conexw01#1; |QBF cuen11;
               cuen11 = cuen11 > cuen11;
               |MENSA "0000Pulse <Return> Para Seguir, <Esc> Para Aceptar, <Ctrl+C> Para Abandonar";
               |BUCLE consulta11;
          |FINSI;
          |POPV;
          |BLIN 4;
          |SI sw_nom = 1;
               varia_cod = #cacuenta#0;
               |QBF varia_cod;
               #0num_campo = varia_cod;
          |SINO;
               #0num_campo = p_alfa1;
          |FINSI;
          |PINTA #0num_campo;
          Campo = num_campo;
          |ERROR;
|FPRC;

|PROCESO ultialta;
  |ABRE #cacuenta;
  p_nume2 = p_alfa3;
  p_nume2 = p_nume2 + 1;
  cuen11 = p_nume2;
  #cacuenta#0 = cuen11;
  #cacuenta#1 = "0";
  |LEE 021#cacuenta.];
  |SI FSalida ! 0;
      |LEE 021#cacuenta.u;
  |SINO;
      |LEE 021#cacuenta.a;
      |SI FSalida ! 0;
          |LEE 021#cacuenta.p;
      |FINSI;
  |FINSI;
  cuen21 = p_alfa3 % 0;
  p_nume2 = cuen21;
  p_nume2 = p_nume2 + 100;
  cuen11 = p_nume2;
  cuen21 = #cacuenta#0 % cuen11;
  |SI cuen21 = p_alfa3;
      cuen21 = #cacuenta#0;
      p_nume2 = cuen21;
      |SI cuen31 = "A";
          p_nume2 = p_nume2 + 1;
      |FINSI;
      p_alfa5 = p_nume2;
  |SINO;
      p_ceros = "0" * p_nume1;
      p_alfa5 = p_alfa3 + p_ceros + "1";
  |FINSI;
  |DDEFECTO #cacuenta;
  |CIERRA #cacuenta;
|FPRC;
