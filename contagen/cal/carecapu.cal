|FICHEROS;
   carecapu #0;

   camaniva #1;
   caivalin #2;

   camaasic #3;
   camaasil #4;
   camandia #10;

   casieaca #111;
   caw00017 #917;
   caivaasi;
   camanref #1002;
|FIN;

|VARIABLES;
   aParam  = "";
   Comodin = "";
   aLong   = "";
   nLong   = 0;

   Busca        = 0;
   NumLinea     = 0;
   SwError      = 0;
   aTipo        = "";
   NumApunte    = 0;
   NumApunteAnt = 0;

   nDi1 = 0;
   fFe1 = @;
   nDo1 = 0;
   nRe1 = 0;
   nAs1 = 0;
   nEstadoA = 0;
   nSwBueno = 0;
   nEstadoF = 0;

   nSwHay17= 0;
   &en0W17 = 0;
   &ef1W17 = @;
   &en2W17 = 0;

   nPara1  = 0;
   importe = 0;
   import1 = 0;
   import2 = 0;
   aSigno = "";
   aSigAn = "";
    alfa1 = "";
    alfa2 = "";
    aTipoDoc = "";
    aAlfa  = "";
    aAlfa1 = "";
    aAlfa2 = "";
    aAlfa3 = "";
    aAlfa4 = "";
    aAlfa5 = "";
    aAlfa7 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
   aCtaDebe  = "";
   aCtaHaber = "";
   aAbo        = "";
   nSwIntra = 0;
   Modo     = 1;
   nImporResta = 0;
   enPred = 0;
   nSi    = 0;
   aFichero = "";

    aDCon1 = "";
    aDCon2 = "";
    nDCon0 = 0;
    nDCon1 = 0;

    aMulDep = "";
    aDep    = "";
    aSub    = "";
    nReserva = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoiv_i;
|INCLUYE modulo_i;

|| ************************************* Calculos Pantalla
|CALCULO Fechas; |TIPO 7;
   #0#7 = fec1; |PINTA #0#7;
   #0#8 = fec2; |PINTA #0#8;
|FCAL;

|CALCULO TomaDocumento; |TIPO 7;
   |C_M #0#10,0,aAlfa1;
   |HAZ contador;
   #0#11 = nDOCUMENTO;
   #0#12 = nREGISTRO;
   |SI aAlfa1 = "S"; |PINTA #0#11; |FINSI;
|FCAL;

|CALCULO ElDiario; |TIPO 6;

 |C_M #0#10,0,aAlfa1;
 |ABRE #10;
 #10#0 = #0#10;
 |LEE 001#10=;
 |SI FSalida ! 0;
     |SI aAlfa1 = "N";
         #10#0 = #0#10;
         |GRABA 020#10n;
     |FINSI;
 |SINO;
     |SI aAlfa1 ! "N";
         |PINTA 1827, #10#1;
     |FINSI;
 |FINSI;
 |CIERRA #10;
|FCAL;

||********************************************************
||********************************************************
||********************************************************
|PROCESO Actualiza;

   |SI #4#8 = "D";
       #3#4 = #3#4 + importe;
   |SINO;
       #3#5 = #3#5 + importe;
   |FINSI;
   #3#6 = #3#4 - #3#5;
   |SI #3#9 = 0; |Y aCtaDebe ! "";
       eaCuenta = aCtaDebe; |HAZ SacaNivel;
       #3#8 = aCtaDebe;
       #3#9 = enNivel;
   |FINSI;
   |SI #3#11 = 0; |Y aCtaHaber ! "";
       eaCuenta = aCtaHaber; |HAZ SacaNivel;
       #3#10 = aCtaHaber;
       #3#11 = enNivel;
   |FINSI;

   |HAZ ActuaCta;
|FPRC;

|PROCESO ActuaCta;
   MasMenos     = importe;
   DebeHaber    = #4#8;
   aCUENTA      = #4#4;
   nREGISTRO    = #4#24;
   nDIARIO      = #4#0;
   aFECHA       = #4#1;
   nDOCUMENTO   = #4#2;
   nNIVELCUENTA = #4#5;
   |SI #4#19 ! "X";
       |HAZ ActDiario;
       |HAZ ActCuenta;
       |HAZ ActTesteo;
   |FINSI;
|FPRC;

|PROCESO CambioCuenta;
    efFechaIVA = #camaniva#4;
    enLineaIVA = #caivalin#16;
    |HAZ SacaIVA;

    #caivalin#13 = eaCtaIVA;
    #caivalin#14 = eaCtaREC;
|FPRC;

|PROCESO BuscaLinea;
   NumLinea = #4#3; SwError = 0;
   |ERROR10;
|FPRC;

|DEFBUCLE BuscaLinea;
  #4#1;
  4,19,29;
  #3#0,#3#1,#3#2,0001,eaCuenta,aTipo,#2#28;
  #3#0,#3#1,#3#2,9999,eaCuenta,aTipo,#2#28;
  ;
  NULL,BuscaLinea;
|FIN;

|PROCESO CreaLineas;

   aDep = #caivalin#30; |QBF aDep;
   |SI aMulDep ! "S"; |O aDep = "";
       #caivalin#30 = #camaniva#10;
       aSub = #caivalin#31; |QBF aSub;
       |SI aMulDep ! "S"; |O aSub = ""; #caivalin#31 = #camaniva#11; |FINSI;
   |FINSI;
   aDep = #caivalin#30;
   aSub = #caivalin#31;

   |SI aTipoIVA = "N";
       |SI aTipo = "B"; |Y #2#6  = 0; |ACABA; |FINSI;
       |SI aTipo = "D"; |Y #2#11 = 0; |ACABA; |FINSI;
       |SI aTipo = "P"; |Y #2#22 = 0; |ACABA; |FINSI;
   |FINSI;

   |SI aTipo = "I"; importe = #2#8;  |FINSI;
   |SI aTipo = "R"; importe = #2#10; |FINSI;
   |SI aTipo = "B";
       importe = #2#6;
       |SI aTipoIVA = "N";
           |SI #2#6 = 0; |Y #2#20 ! 0;
               importe = #2#20;
           |FINSI;
      |FINSI;
   |FINSI;
   |SI aTipo = "D"; importe = #2#11; |FINSI;
   |SI aTipo = "P";
       importe = #2#22;
       |SI aTipoIVA = "N";
||           |SI #2#22 = 0; importe = #2#6; |FINSI;
       |FINSI;
   |FINSI;
   |SI importe = 0;   |ACABA; |FINSI;

   |SI #2#25 = "S"; |Y nReserva = 0; |Y #camaniva#42 ! "I";
       nSwIntra = 1;
       |SI aTipo = "I"; |O aTipo = "R";

           |SI snAbono = "S";
               |SI #2#8  < 0; #2#8  = - #2#8; |FINSI;
               |SI #2#10 < 0; #2#10 = - #2#10; |FINSI;
           |FINSI;

           |SI aTipo = "I"; nImporResta = nImporResta + #2#8; |FINSI;
           |SI aTipo = "R"; nImporResta = nImporResta + #2#10; |FINSI;

           |SI #caivaasi#150 ! "S"; |ACABA; |FINSI;
       |FINSI;
   |FINSI;
   |SI #camaniva#42 = "I"; |Y nReserva = 0;
       |SI #2#25 = "S"; nSwIntra = 1; |FINSI;
       |SI aTipo = "I"; |O aTipo = "R";
           |SI snAbono = "S";
               |SI #2#8  < 0; #2#8  = - #2#8; |FINSI;
               |SI #2#10 < 0; #2#10 = - #2#10; |FINSI;
           |FINSI;
           |SI aTipo = "I"; nImporResta = nImporResta + #2#8; |FINSI;
           |SI aTipo = "R"; nImporResta = nImporResta + #2#10; |FINSI;

           |SI #caivaasi#151 ! "S"; |ACABA; |FINSI;
       |FINSI;
   |FINSI;

   Busca = 0;
   |SI aTipo = "I";
       |SI #camaniva#31 = "N"; Busca = 1; |FINSI;
   |FINSI;
   |SI aTipo = "R";
       |SI #camaniva#31 = "N"; Busca = 1; |FINSI;
   |FINSI;
   |SI aTipo = "D";
      |SI #camaniva#34 = "N"; Busca = 1; |FINSI;
   |FINSI;
   |SI aTipo = "P";
      |SI #camaniva#33 = "N"; Busca = 1; |FINSI;
   |FINSI;

   |SI nReserva = 1; |HAZ CambioCuenta; |FINSI;

   |SI Busca = 1;
      |SI aTipoIVA ! "N";
          |SI aTipo = "I"; eaCuenta = #2#13; |FINSI;
          |SI aTipo = "R"; eaCuenta = #2#14; |FINSI;
          |SI aTipo = "D"; eaCuenta = #2#15; |FINSI;
          |SI aTipo = "P"; eaCuenta = #2#23; |FINSI;
      |SINO;
          eaCuenta = #2#23;
      |FINSI;
      SwError = 1; |BUCLE BuscaLinea;
      |SI SwError = 0;
          NumApunteAnt = NumApunte;
          NumApunte    = NumLinea;
      |FINSI;
   |FINSI;

   aSigAn = "";
   |SI aTipoIVA = "R";      || ... Iva Repercutido
       |SI aTipo = "D"; |O aTipo = "P";
            aSigno = "D";
       |SINO;
            aSigno = "H";
       |FINSI;
   |FINSI;
   |SI aTipoIVA = "S";       || ... Iva Soportado
       |SI aTipo = "D"; |O aTipo = "P";
           aSigno = "H";
       |SINO;
           aSigno = "D";
       |FINSI;
   |FINSI;
   |SI aTipoIVA = "N";        || ... No IVA
       |SI aTipo = "B";
           aSigno = "D";
       |SINO;
           aSigno = "H";
       |FINSI;
   |FINSI;

   #4#0 = #3#0;
   #4#1 = #3#1;
   #4#2 = #3#2;
   #4#3 = NumApunte;
   |LEE 010#4=;
   |SI FSalida ! 0;
      |DDEFECTO #4;
       #4#0 = #3#0;
       #4#1 = #3#1;
       #4#2 = #3#2;
       #4#3 = NumApunte;
       |SI aTipoIVA ! "N";
          |SI aTipo = "I"; #4#4 = #2#13; |FINSI;
          |SI aTipo = "R"; #4#4 = #2#14; |FINSI;
          |SI aTipo = "B"; #4#4 = #2#12; |FINSI;
          |SI aTipo = "D"; #4#4 = #2#15; |FINSI;
          |SI aTipo = "P"; #4#4 = #2#23; |FINSI;
       |SINO;
           #4#4 = #2#23;
       |FINSI;
       eaCuenta = #4#4; |HAZ SacaNivel;
       #4#5 = enNivel;

       |SI aTipo = "I"; |O aTipo = "R";
           #4#6 = #1#29;
           #4#7 = #1#30;
       |SINO;
           |SI aTipo = "P";
               |SI aTipoIVA ! "N";
                   #4#6 = #2#18;
                   #4#7 = #2#19;
               |SINO;
                   #4#6 = #2#3;
                   #4#7 = #2#4;
               |FINSI;
           |SINO;
               #4#6 = #2#3;
               #4#7 = #2#4;
           |FINSI;
       |FINSI;
       #4#8  = aSigno;
       #4#19 = aTipo;
       #4#20 = #2#2;
       #4#22 = 0;
       #4#23 = 0;
       #4#24 = nREGISTRO;
       |GRABA 020#camaasil.b;
   |SINO;
       aSigAn = #4#8;
       #4#20  = 0;
   |FINSI;

   |HAZ adapta3;
   #4#9 = #4#9 + importe;

   |HAZ adapta1;
   |HAZ adapta2;
   |GRABA 020#camaasil.a;
   |LIBERA #camaasil;
   |HAZ Actualiza;
   NumApunte = NumApunte + 10;

   |SI NumApunteAnt ! 0;
       NumApunte = NumApunteAnt;
       NumApunteAnt = 0;
   |FINSI;

   |LIBERA #2;
|FPRC;

|DEFBUCLE CreaLineas;
 #2#1;
 ;
 #1#0,#1#1,01;
 #1#0,#1#1,99;
 be;
 NULL,CreaLineas;
|FIN;
|| -----------------------------------------------------------------

|PROCESO LasLineas;
 nSwBueno = 1;

 |SI #0#9 ! "N";
     |SI #4#19 = "X"; ||modificacion
         || no actualizo datos
     |SINO;
         importe = 0 - #4#9;
         |HAZ ActuaCta;
     |FINSI;

     |BORRA 020#4a;

 |FINSI;
 |LIBERA #4;
|FPRC;

|DEFBUCLE LasLineas;
 #4#1;
 12,13,15,14;
 #3#0, #3#1, #3#2, 0001, #1#36, #1#0, #1#2, #1#3;
 #3#0, #3#1, #3#2, 9999, #1#36, #1#0, #1#2, #1#3;
 be;
 NULL, LasLineas;
|FIN;

|| .... Proceso las lineas X del Asiento
|PROCESO LeeLineas;
 |PARA nPara1 = 0; |SI nPara1 [ 30; |HACIENDO nPara1 = nPara1 + 1;
       #917nPara1 = #4nPara1;
 |SIGUE;
 |GRABA 020#917n;
 nSwHay17= 1;
 en0W17 = #917#0;
 ef1W17 = #917#1;
 en2W17 = #917#2;
|FPRC;

|DEFBUCLE LeeLineas;
 #4#1;
 19;
 #1#7,#1#8,#1#9,0000,"X";
 #1#7,#1#8,#1#9,9999,"X";
 ;
 NULL,LeeLineas;
|FIN;

|PROCESO ElAuxW17;
  |ABRE #917;
  en0W17 = #1#7;
  ef1W17 = #1#8;
  en2W17 = #1#9;
  |BUCLE LeeLineas;
  |CIERRA #917;
|FPRC;
|| //////////////////////////////////////////////////

|PROCESO MiroDocUlt;
   |SELECT #3#3;
   #3#2 = #0#11;
   |LEE 010#3=;
   |SI FSalida = 0;
       |LEE 010#3u;
       |SI FSalida ! 0;
           #0#11 = 1;
       |SINO;
           #0#11 = #3#2 + 1;
       |FINSI;
   |FINSI;
   |SELECT #1#3;
|FPRC;

|PROCESO Facturas;
   enAParam = #camaniva#10;
   |HAZ eActivParam;

   |SI #1#8 [ fec3;
       |MENAV "    Asiento con fecha Inferior o igual al Bloqueo. No se actualiza";
       |ACABA;
   |FINSI;

   |SI #1#9 < 0; |ACABA; |FINSI; ||factura especial

   |DELINDEX #917;
   nSwHay17 = 0;
   enPred   = 0;
   nDi1 = #1#7;
   fFe1 = #1#8;
   nDo1 = #1#9;
   nAs1 = #1#9;
   nRe1 = #0#12;
   aTipoIVA = #1#36;

   snAbono = "N";
   |SI #1#36 = "R";  snAbono = #caivaasi#93; |FINSI;
   |SI #1#36 = "S";  snAbono = #caivaasi#94; |FINSI;

   efFechaIVA = #camaniva#4; enLineaIVA = 1;
   |HAZ SacaIVA;

   |HAZ MiroDocUlt;

   nEstadoF = 0;
   |DDEFECTO #3;
   #3#0 = #1#7;
   #3#1 = #1#8;
   #3#2 = #1#9;
   |LEE 101#3=;
   nEstadoA = FSalida;
   |SI nEstadoA = 0;
       |HAZ ElAuxW17;
       enPred   = #3#7;
       nAs1     = #3#3;
       nRe1     = #3#12;
       nSwBueno = 0;
       |BUCLE LasLineas;
       |SI nSwBueno = 1;
           |SI #0#9 = "N";  |LIBERA #3; |ACABA;  |FINSI;
       |SINO;
           || ... Existe pero ya esta ocupado
           nDi1 = #0#10;
           nDo1 = #0#11; #0#11 = #0#11 + 1;
           nRe1 = #0#12; #0#12 = #0#12 + 1;
           nAs1 = nDo1;
           nEstadoA = -1;
           nEstadoF = 1;
       |FINSI;
  |SINO;
       |SI #0#9 = "S"; |LIBERA #3; |ACABA; |FINSI;
  |FINSI;
  |LIBERA #3;

  |SI enPred ! 0;
      |ABRE #111;
      |DDEFECTO #111;
      #111#0 = enPred;
      |LEE 001#111=;
      |CIERRA #111;
  |FINSI;

   nDIARIO    = nDi1;
   aFECHA     = fFe1;
   nDOCUMENTO = nDo1;
   nREGISTRO  = nRe1;

   || Creo el asiento
   |DDEFECTO #3;
   #3#0 = nDIARIO;
   #3#1 = aFECHA;
   #3#2 = nDOCUMENTO;
   |LEE 101#3=;
   nEstadoA = FSalida;
   #3#3 = nAs1;
   #3#4 = 0;
   #3#5 = 0;
   #3#6 = 0;
   #3#7 = enPred;
   #3#12 = nREGISTRO;
   |SI #1#36 = "R"; #3#13 = 1; |FINSI;
   |SI #1#36 = "S"; #3#13 = 2; |FINSI;
   |SI #1#36 = "N"; #3#13 = 3; |FINSI;
   #3#17 = #1#22;

   |SI nEstadoA ! 0; |GRABA 020#3b; |FINSI;


   ||  ............... Creo la linea del Total Factura
   NumApunte = 1;

   |DDEFECTO #4;
   #4#0 = #3#0;
   #4#1 = #3#1;
   #4#2 = #3#2;
   #4#3 = NumApunte;    NumApunte = NumApunte + 10;

   #4#4 = #1#12;  || Titular Factura
   eaCuenta = #4#4; |HAZ SacaNivel;
   #4#5 = enNivel;
   #4#6 = #1#27;
   #4#7 = #1#28;

   nSwIntra    = 0;
   nImporResta = 0;
   aSigAn   = "";
   aTipoIVA = #1#36;
   |SI aTipoIVA = "R";
       aSigno  = "D";
   |SINO;
       aSigno = "H";
   |FINSI;

   #4#8 = aSigno;
   importe = #1#21;
   |HAZ adapta3;

   #4#9  = importe;
   #4#19 = "F";
   #4#20 = 0;
   #4#21 = #1#10;
   #4#24 = #3#12;
   #4#26 = #1#22;
   Comodin = Usuario % 1099;
   #4#27 = Comodin;
   #4#28 = #1#11;

   aDep = #1#10;
   aSub = #1#11;

   |HAZ adapta1;
   |HAZ adapta2;

   |GRABA 020#4n;
   |HAZ Actualiza;

    nReserva = 0;
    |SI aTipoIVA ! "N";
        aTipo = "I"; |BUCLE CreaLineas;        || Creo las lineas del IVA
        aTipo = "R"; |BUCLE CreaLineas;        || Creo las lineas del Recargo
        |SI nSwIntra = 1; |Y #caivaasi#150 = "S"; |Y #camaniva#42 ! "I";
            |SI aTipoIVA = "R"; aTipoIVA = "S"; |SINO; aTipoIVA = "R"; |FINSI;
                nReserva = 1;
                aTipo = "I"; |BUCLE CreaLineas;        || Creo las lineas del IVA
                aTipo = "R"; |BUCLE CreaLineas;        || Creo las lineas del Recargo
            |SI aTipoIVA = "R"; aTipoIVA = "S"; |SINO; aTipoIVA = "R"; |FINSI;
        |SINO;
            |SI #camaniva#42 = "I"; |Y #caivaasi#151 = "S";
                |SI aTipoIVA = "R"; aTipoIVA = "S"; |SINO; aTipoIVA = "R"; |FINSI;
                    nReserva = 1;
                    aTipo = "I"; |BUCLE CreaLineas;        || Creo las lineas del IVA
                    aTipo = "R"; |BUCLE CreaLineas;        || Creo las lineas del Recargo
                |SI aTipoIVA = "R"; aTipoIVA = "S"; |SINO; aTipoIVA = "R"; |FINSI;
            |FINSI;
        |FINSI;
    |FINSI;
    nReserva = 0;
    aTipo = "B"; |BUCLE CreaLineas;        || Creo las lineas de las bases
    aTipo = "D"; |BUCLE CreaLineas;        || Creo las lineas de los descuentos
    aTipo = "P"; |BUCLE CreaLineas;        || Creo las lineas del IRPF

|SI nImporResta ! 0;
    #4#0 = #3#0;
    #4#1 = #3#1;
    #4#2 = #3#2;
    #4#3 = 1;
    |LEE 010#camaasil.=;
    #4#9 = #4#9 - nImporResta;
    |GRABA 020#camaasil.a;
    |LIBERA #camaasil;

    importe = -nImporResta;
    |HAZ Actualiza;
|FINSI;

   |HAZ RecogeW17;

   |GRABA 020#3a;
   |LIBERA #3;

   |SI nEstadoF = 1;
       #1#7 = nDi1;
       #1#8 = fFe1;
       #1#9 = nDo1;
       |GRABA 020#1a;
   |FINSI;
   |LIBERA #1;

   |HAZ NoAsiento0;

|FPRC;

|DEFBUCLE Facturas;
 #1#1;
 2,3,4,36;
 #0#1, 0000000001, #0#5, #0#3, #0#7, #0#0;
 #0#2, 9999999999, #0#6, #0#4, #0#8, #0#0;
 be;
 NULL,Facturas;
|FIN;


|| ************************************************************************
|CALCULO Recupera; |TIPO 3;

   aMulDep = #caivaasi#132;

   aFichero = ("17w" + Usuario) % 108;
   |NOME_DAT #917 aFichero;

   |ABRE #3;
   |ABRE #4;
   |BUCLE Facturas;
   |CIERRA #4;
   |CIERRA #3;

|FCAL;


|PROGRAMA;
  aParam = PARAMETRO; |QBF aParam;
  |HAZ inicio;
  |CLS;
  |CABEZA "Recup.Apuntes de IVA";

  |SI aParam = "";
      |MANTE #0;
  |SINO;
      |PINPA #0#0;
      |PINTA 0603, "                                              ";
      |ATRI R; |PINTA 0603, "Reconstruir Asientos de Ventas Englobadas"; |ATRI 0;
      |PINTA 0905, "                                         ";

      #0#0 = "R"; |C_M #0#0,1,"N"; |C_V #0#0,1,"N";
      #0#9 = "S"; |C_M #0#9,1,"N";
      |C_M #0#10,1,"N"; |C_V #0#10,1,"N";
      |C_M #0#11,1,"N"; |C_V #0#11,1,"N";
      |BLIN 17;
      |BLIN 18;
      |BLIN 19;
      |BLIN 20;
      |CUADRO 1503 1763;
      |PINDA #0#0;
      |ENDATOS #1#0;
  |FINSI;
|FPRO;

|PROCESO adapta1;
  #4#12 = aTipoIVA;
  #4#13 = #1#0;
  #4#14 = #1#3;
  #4#15 = #1#2;
  #4#21 = aDep;
  #4#28 = aSub;

  #4#26 = #1#22;  ||referencia
  |SI #4#4  = #2#12;
      #4#29 = #2#28;  ||Cuenta Analitica
  |FINSI;
  #4#30 = #1#4;   ||fecha linea

  Comodin = Usuario % 0810;
  #4#27 = Comodin;

  |SI #4#8 = "D";
      #4#16 = #4#4;
      #4#17 = #4#5;
      #4#10 = "";
      #4#11 = 0;
  |SINO;
      #4#16 = "";
      #4#17 = 0;
      #4#10 = #4#4;
      #4#11 = #4#5;
  |FINSI;

  |SI #4#19 = "F"; |O #4#19 = "B";
      |SI #4#8 = "D";
          |SI aCtaDebe = ""; aCtaDebe = #4#4; |FINSI;
      |FINSI;
      |SI #4#8 = "H";
          |SI aCtaHaber = ""; aCtaHaber = #4#4; |FINSI;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO adapta2;  ||El concepto
   |SI #4#12 ! "C"; |Y #4#12 ! "P";
       |SI #caivaasi#131 = "S"; |ACABA; |FINSI;
       |SI #4#19 ! "P";
           aAlfa5 = "";
           aAlfa1 = #4#7;  |QBF aAlfa1;  || Descripcion
           aAlfa2 = #caivaasi#45;
           |SI #4#19 = "B"; |O #4#19 = "I"; |O #4#19 = "R";
               |O #4#19 = "b"; |O #4#19 = "i"; |O #4#19 = "r";
               |SI #caivaasi#130 ! "=  ";
                   aAlfa2 = #caivaasi#130;
               |FINSI;
           |FINSI;

           |QBF aAlfa2; |SI aAlfa2 = ""; |ACABA; |FINSI;
           aAlfa3 = aAlfa2 % 0;
           nDCon0 = aAlfa3;
           |PARA nDCon1 = 1; |SI nDCon1 [ nDCon0; |HACIENDO nDCon1 = nDCon1 + 1;
                 aAlfa3 = nDCon1;
                 aAlfa3 = aAlfa3 + "01";
                 aDCon2 = aAlfa2 % aAlfa3;
                 |HAZ adapta2_c;
                 aAlfa5 = aAlfa5 + aDCon1;
           |SIGUE;

           #4#7 = aAlfa1 + aAlfa5;
       |SINO;

           aAlfa5 = "";
           aAlfa1 = #4#7;  |QBF aAlfa1;  || Descripcion
           aAlfa2 = #caivaasi#96; |QBF aAlfa2;  || lo que a¤ade


           |QBF aAlfa2; |SI aAlfa2 = ""; |ACABA; |FINSI;
           aAlfa3 = aAlfa2 % 0;
           nDCon0 = aAlfa3;
           |PARA nDCon1 = 1; |SI nDCon1 [ nDCon0; |HACIENDO nDCon1 = nDCon1 + 1;
                 aAlfa3 = nDCon1;
                 aAlfa3 = aAlfa3 + "01";
                 aDCon2 = aAlfa2 % aAlfa3;
                 |HAZ adapta2_c;
                 aAlfa5 = aAlfa5 + aDCon1;
           |SIGUE;

           #4#7 = aAlfa1 + aAlfa5;
       |FINSI;
   |FINSI;
|FPRC;

|PROCESO adapta2_c;  ||El concepto

    aDCon1 = "";
    |SI aDCon2 = "S";
         aDCon1 = #4#15; |QBF aDCon1;
         |SI aDCon1 ! ""; aDCon1 = aDCon1 + aSepSer; |QBF aDCon1; |FINSI;
     |FINSI;                                         || Serie
     |SI aDCon2 = "F";
         aDCon1 = #4#14; |QBF aDCon1;
         |SI aNumIzq = "S";
             aDCon1 = ("000000" + aDCon1) % -106;
         |FINSI;
     |FINSI;                                         || Factura

     |SI aDCon2 = "C";
         aDCon1 = #1#12; |QBF aDCon1;
     |FINSI;                                         || Cuenta Cliente

     |SI aDCon2 = "N";
         aDCon1 = #1#13; |QBF aDCon1;
     |FINSI;                                         || Nombre Cliente

     |SI aDCon2 = "R";
         |SI #1#22 ! "                    ";
             aDCon1 = #1#22; |QBF aDCon1;
         |FINSI;
     |FINSI;                                         || Referencia

     |SI aDCon2 = "P";
         aDCon1 = #1#22;
         |SI #1#36 = "S";
             |HAZ EsComersan;
             |SI FSalida = 0;
                 |ABRE #1002;
                 |DDEFECTO  #1002;
                 #1002#0 = #1#0;
                 #1002#1 = #1#1;
                 |LEE 000#1002=;
                 aDCon1 = #1002#2;
                 |CIERRA #1002;
             |FINSI;
         |FINSI;
         |QBF aDCon1;
     |FINSI;                                         || Referencia Comersan



     |SI aDCon1 ! "";
         |SI aDCon2 = "F";
             aAlfa7 = aAlfa2 - "S";
             |SI aAlfa7 ! aAlfa2; |Y #4#15 > "     ";
                 |ACABA;
             |FINSI;
         |FINSI;
         aDCon1 = " " + aDCon1;
     |FINSI;
|FPRC;

|PROCESO adapta3;       || cambios de signos en ABONOS
   aAbo = snAbono;
   |SI enPred ! 0;
       |SI #111#9 = 1; snAbono = "S"; |FINSI;
       |SI #111#9 = 0; snAbono = "N"; |FINSI;
   |FINSI;

   |SI snAbono ! "S";   || ...  No cambiar signo en Abonos Salir
       snAbono = aAbo;
       |ACABA;
   |FINSI;

   |SI aSigAn = "";     || Es la primera linea de este
       |SI importe < 0;
           |HAZ CambiaSigno;
       |FINSI;
       snAbono = aAbo;
       |ACABA;
   |FINSI;

|| ----------------------------------------------------------

   |SI aSigno = aSigAn;      ||Por ahora es signo sin abono

       import2 = #4#9 + importe;
       |SI import2 ] 0;
           snAbono = aAbo;
           |ACABA;           ||Queda positivo
       |SINO;

           import1 = #4#9;   ||Queda negativo
           import2 = importe;
           importe = -#4#9;   ||Desactualiza
           |HAZ Actualiza;

           #4#9 = 0;
           importe = import1 + import2;
           |HAZ CambiaSigno;
      |FINSI;

   |SINO;

       || ............ Ya era Abono
       |SI importe < 0;   || Es negativo ahora tambien
           importe = -importe;
           snAbono = aAbo;
           |ACABA;
       |FINSI;


      import1 = #4#9;   ||Queda negativo
      import2 = importe;
      importe = - #4#9;   ||Desactualiza
      |HAZ Actualiza;

      importe = import2 - #4#9;
      #4#8 = aSigno;
      #4#9 = 0;
      |SI importe ] 0;
           snAbono = aAbo;
           |ACABA;           ||Queda positivo
       |SINO;
           |HAZ CambiaSigno;
      |FINSI;

   |FINSI;
   snAbono = aAbo;
|FPRC;

|PROCESO CambiaSigno;
 |SI #4#8 = "D";
     #4#8 = "H";
 |SINO;
     #4#8 = "D";
 |FINSI;
 importe = -importe;
|FPRC;


|PROCESO LeeApuntea0;
 |SI #4#9 = 0;
     |BORRA 020#camaasil.a;
 |SINO;
     nSi = 1;
     |ERROR10;
 |FINSI;
 |LIBERA #camaasil;
|FPRC;

|DEFBUCLE LeeApuntea0;
 #4#1;
 ;
 #1#7,#1#8,#1#9,0001;
 #1#7,#1#8,#1#9,9999;
 be;
 NULL, LeeApuntea0;
|FIN;

|PROCESO NoAsiento0;
 nSi = 0;
 |BUCLE LeeApuntea0;
 |SI nSi = 0;
     #3#0 = #1#7;
     #3#1 = #1#8;
     #3#2 = #1#9;
     |LEE 101#3=;
     |SI FSalida = 0;
         |BORRA 020#3a;
     |FINSI;
     |LIBERA #3;
     #1#9 = 0;
 |FINSI;
|FPRC;

|| ***********************************************************************
|PROCESO Recojo17;
  |DDEFECTO #camaasil;
  #4#0 = #3#0;
  #4#1 = #3#1;
  #4#2 = #3#2;
  #4#3 = NumApunte;
  |PARA nPara1 = 4; |SI nPara1 [ 30; |HACIENDO nPara1 = nPara1 + 1;
       #4nPara1 = #917nPara1;
  |SIGUE;
  #4#12 = aTipoIVA;
  #4#13 = #1#0;
  #4#14 = #1#3;
  #4#15 = #1#2;
  #4#19 = "X";
  #4#20 = 0;
  #4#24 = nREGISTRO;
  #4#26 = #1#22;
  Comodin = Usuario % 1099;
  #4#27 = Comodin;

  |SI #4#9 ! 0;
      |GRABA 020#camaasil.n;
      importe = #4#9;
      |HAZ Actualiza;
      NumApunte = NumApunte + 10;
  |FINSI;
|FPRC;

|DEFBUCLE Recojo17;
  #917#1;
  ;
  INICIO;
  FINAL;
  ;
  NULL, Recojo17;
|FIN;

|PROCESO RecogeW17;
  |BUCLE Recojo17;
|FPRC;
