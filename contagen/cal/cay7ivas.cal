|FICHEROS;
   cay7ivas #0;
   camaniva #1;    || iva
   cacuenta #4;    || Cuentas
   caclipro #18;   || Clientes y Proveedores

   canempre #30;
   caselem1 #998;

   &regisnif@ #709;
   :/basica/def/agim0003  #710;
   :/integral/def/dsam0103 #711;

   caw00035 #910; || Auxiliar Cuentas
   caw00034 #999; || auxiliar Cuentas/Nombre/NIF
|FIN;

|VARIABLES;
   &entrada = 1;

   &eaNom    = "";
   &aNombre  = "";
   &aNomPlan = "";
   &aNif     = "";

   aAlfa1   = "";
   aAlfa2   = "";
   nNume1   = 0;
   aPath    = "";

   nEstado = 0;
   DesdeTipo = "";
   HastaTipo = "";
   informe   = "cay7ivas";

   aNomFra   = "";
   aNifFra   = "";
   nRegistro = 0;
   nRegiPlan = 0;
   nSwError  = 0;
   nSwPr     = 0;
   nSw       = 0;

   &aCta      = "";
   &enSwLinea = 0;
   &eaError   = "";
   &eaErrorl  = "";

   nCampo     = 0;
   nSwPrint   = 0;
   nSwOp      = 0;
   aFichero   = "";
   aDef       = "";
   fDFecha    = @;
|FIN;

|INCLUYE dscont_i;

|PROCESO Tipo8; |TIPO 8;
   |HAZ inicio;
   ewNif = 0;
   enSwDeDonde = -1;
   |HAZ DirAplicSt;
   |SI HayAcceso = 1;
       enSwDeDonde = 1;
       |REFERENCIA_DEF regisnif@, #711;
   |SINO;
       |SI Haybasica = 1;
           enSwDeDonde = 0;
           |REFERENCIA_DEF regisnif@, #710;
       |FINSI;
   |FINSI;
   |SI enSwDeDonde = -1; ewNif = 1; |FINSI; || no existe Basica ni Integrada
|FPRC;
____________________________________/ CALCULOS DESDE PANTALLA

|CALCULO def_fec; |TIPO 7;
   |HAZ eParaModelos;
   fDFecha = fec1;
   |SI enSwSelFc = 1; fDFecha = "01.01.2000"; |FINSI;
   #0#3 = fDFecha; |PINTA #0#3;
   #0#4 = fec2;    |PINTA #0#4;
|FCAL;

|CALCULO cuenta;|TIPO 6;
     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta; |PINTA #0Campo;
|FCAL;

|CALCULO Simodif; |TIPO 0;
 aAlfa1 = #0#17;
 |C_M #0#21,1,aAlfa1;
|FCAL;
|| **************************************************************************

|PROCESO Igualdad;
  |SI ewNif = 1;  |ACABA; |FINSI;
  |SI #0#21 = "N"; |ACABA; |FINSI;

  |ABRE #709;
  |DDEFECTO #709;
  #709#0 = aNifFra;
  |LEE 141#709=;
  |SI FSalida = 0;
      aAlfa1 = #709#9; |QBF aAlfa1;
      |SI aAlfa1 = "";
          #18#2 = #709#2;
      |SINO;
          #18#2 = aAlfa1 + " " + #709#2;
      |FINSI;
      aAlfa1 = #18#2; |QBF aAlfa1;
      #18#2 = aAlfa1 + ", " + #709#3;
      aAlfa1 = #18#2; |QBF aAlfa1;
      #18#2 = aAlfa1 + " " + #709#10;
      aAlfa1 = #18#2; |QBF aAlfa1;
      #18#2 = aAlfa1 + " " + #709#11;

      #18#3 = #709#4;
      #18#4 = #709#5;
      #18#5 = #709#7;
      #18#6 = #709#6;
      #18#7 = #709#8;
      #18#24 = #709#12;
  |FINSI;
  |CIERRA #709;
|FPRC;

|PROCESO Modifico;
   |ABRE #18;
   nEstado = 0;
   #18#23 = "C";
   #18#10 = eaCuenta;
   #18#11 = enNivel;
   |LEE 001#18=;
   |SI FSalida ! 0;
       #18#23 = "P";
       #18#10 = eaCuenta;
       #18#11 = enNivel;
       |LEE 001#18=;
       |SI FSalida ! 0;
           nEstado = 1;
       |FINSI;
   |FINSI;
   |SI nEstado = 0;
       #18#1 = aNomFra;
       #18#9 = aNifFra;
       |HAZ Igualdad;
       |GRABA 020#18a;
   |FINSI;
   |CIERRA #18;

   |ABRE #4;
   #4#0 = eaCuenta;
   #4#1 = enNivel;
   |LEE 001#4=;
   |SI FSalida = 0;
       #4#2 = aNomFra;
       |GRABA 020#4a;
   |FINSI;
   |CIERRA #4;
|FPRC;

|PROCESO Leyendo;
   eaCuenta = aCta;
   |HAZ SacaNivel;

   aNomPlan  = "";
   aNombre   = "";
   aNif      = "";
   nRegistro = 0;
   nRegiPlan = 0;
   |ABRE #18;
   #18#23 = "C";
   #18#10 = eaCuenta;
   #18#11 = enNivel;
   |LEE 001#18=;
   |SI FSalida = 0;
       aNombre = #18#1;        || nombre del cliente
       aNif    = #18#9;
       nRegistro = 1;
   |SINO;
       #18#23 = "P";
       #18#10 = eaCuenta;
       #18#11 = enNivel;
       |LEE 001#18=;
       |SI FSalida = 0;
           aNombre = #18#1;        || nombre del proveedor
           aNif    = #18#9;
           nRegistro = 1;
       |FINSI;
   |FINSI;
   |CIERRA #18;

   |ABRE #4;
   #4#0 = eaCuenta;
   #4#1 = enNivel;
   |LEE 001#4=;
   |SI FSalida = 0;
       aNomPlan  = #4#2;         || nombre de la cuenta
       nRegiPlan = 1;
   |FINSI;
   |CIERRA #4;

   |QBF aNif;
   |QBF aNombre;
   |QBF aNomPlan;

   eaError  = "";
   nSwError = 0;
   enSwLinea = 0;
   |SI aNomPlan ! aNomFra; |O nRegiPlan = 0;
       nSwError = 1;
       eaNom = aNomPlan;
       eaError = "No existe en Plan Contable";
       |SI nRegiPlan = 1;
           eaError = "Nombre diferente, en Plan Contable y en Registro de IVA";
       |FINSI;
       |PRINT; nSwPrint = 1;
       enSwLinea = 1;
   |FINSI;
   |SI aNombre ! aNomFra; |O aNif ! aNifFra; |O nRegistro = 0;
       nSwError = 1;
       eaNom = aNombre;
       eaError = "No existe en la Ficha de Clientes / Proveedores";
       |SI nRegistro ! 0;
           eaError = "Datos distintos, en Ficha Cliente - Proveedor  y en Registro de IVA"
       |FINSI;
       |PRINT; nSwPrint = 1;
       enSwLinea = 1;
   |FINSI;
|FPRC;
|| **************************************************************************
|| **************************************************************************

|PROCESO lee_aux2;

  |SI nSwOp = 0;
      aAlfa1 = #999#1; |QBF aAlfa1;
      |SI aNifFra ! aAlfa1;
          nSwPr = 1;
          |ERROR10;
          |ACABA;
      |FINSI;
  |FINSI;

  |SI nSwOp = 1;
      eaError = "Diferentes Nombre en Registros de IVA";
      |ABRE #1;
      #1#0 = #999#2;
      #1#1 = #999#3;
      |LEE 001#1=;
      |SI FSalida = 0;
          |SI enSwLinea = 0;  enSwLinea = 2; |FINSI;
          |SI enSwLinea = 1;  enSwLinea = 3; |FINSI;
          |PRINT;
          nSwPrint = 1; enSwLinea = 4;
      |FINSI;
      |CIERRA #1;
  |FINSI;
|FPRC;

|DEFBUCLE lee_aux2;
 #999#1;
 ;
 #910#0, "               ", 00, 0000000001;
 #910#0, "zzzzzzzzzzzzzzz", 99, 9999999999;
 ;
 NULL, lee_aux2;
|FIN;

|PROCESO lee_auxiliar;

 #0#18 = #910#0;
 #0#19 = #910#2;
 #0#20 = #910#1;


 enSwLinea = 0;
 aCta      = #910#0;
 aNomFra   = #910#1; |QBF aNomFra;
 aNifFra   = #910#2; |QBF aNifFra;
 |HAZ Leyendo;

 nSwPr = 0;
 nSwOp = 0;
 eaError  = "";
 |BUCLE lee_aux2;
 |SI nSwPr = 1;
     nSwOp = 1;
     |BUCLE lee_aux2;
 |SINO;
     |SI #0#17 = "S"; |Y nSwError = 1;
         |HAZ Modifico;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE lee_auxiliar;
 #910#1;
 ;
 "            ";
 "zzzzzzzzzzzz";
 ;
 NULL, lee_auxiliar;
|FIN;
|| //////////////////////////

|PROCESO GrabaAuxiliar;

 #999#0 = #1#12;  || cuenta contable
 #999#1 = #1#14;  || nif
 #999#2 = #1#0;   || libro
 #999#3 = #1#1;   || documento
 #999#4 = #1#13;  || nombre
 |GRABA 000#999n;
 nSw    = 1;

 #910#0 = #1#12;
 |LEE 001#910=;
 |SI FSalida ! 0;
     |DDEFECTO #910;
     #910#0 = #1#12;
     #910#1 = #1#13;
     #910#2 = #1#14;
     |GRABA 020#910n;
 |FINSI;
|FPRC;

|PROCESO lee_iva1;

   |PARA nCampo = 9; |SI nCampo [ 16; |HACIENDO nCampo = nCampo + 1;
         |SI #1#12 = #0nCampo;
              |ACABA;
         |FINSI;
   |SIGUE;

   |HAZ GrabaAuxiliar;
|FPRC;

|DEFBUCLE lee_iva;
   #1#4;
   36;
   #0#5, #0#3, #0#1, 0000000001, DesdeTipo;
   #0#6, #0#4, #0#2, 9999999999, HastaTipo;
   e;
   NULL, lee_iva1;
|FIN;
|| **************************************************************************

|PROCESO HazTodo;
       |DELINDEX #910;
       |DELINDEX #999;

       nSw      = 0;
       |ABRE #910;
       |ABRE #999;
       |BUCLE lee_iva;
       |CIERRA #999;
       |CIERRA #910;

       |SI nSw = 0;  |ACABA;  |FINSI;

       nSwPrint = 0;
       nSw      = 0;
       |BUCLE lee_auxiliar;

       |SI nSwPrint = 1;
           |PIEINF;
       |FINSI;
|FPRC;

|CALCULO tipo3; |TIPO 3;

   DesdeTipo = #0#0; HastaTipo = #0#0;
   |SI #0#0 = "A"; DesdeTipo = "R"; HastaTipo = "S"; |FINSI;

   |IMPRE 0;
   |SI FSalida ! 0;  |ACABA; |FINSI;

   |INFOR informe;
   |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

   aFichero = ("xy" + Usuario) % 108;
   |NOME_DAT #910 aFichero;
   aFichero = ("xx" + Usuario) % 108;
   |NOME_DAT #999 aFichero;

   |SI #48#27 = "S";
       |HAZ MultiEmpresa;
   |SINO;
       |DFICE dirfich0; |QBF dirfich0;
       |HAZ HazTodo;
   |FINSI;

   |FININF;
   |FINIMP;
   |DELINDEX #910;
   |DELINDEX #999;

|FCAL;

|| **************************************************************************

|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #4 dirfich0;
   |PATH_DAT #18 dirfich0;

   |ATRI R; |PINTA 2309, "Empresa: ";      |ATRI 0;
   |PINTA 2318, #30#2; |PINTA 2324, #30#1; |ATRI 0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 001;
 999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;
