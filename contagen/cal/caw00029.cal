|FICHEROS;
     caw00029 #999,MANTE;

     camw0006 #6;
     canempre #30;
|FIN;

|VARIABLES;
     nBoton1   = 0;
     nBoton2   = 0;
     nBoton3   = 0;
     nBoton4   = 0;
     nBoton5   = 0;
     nBoton6   = 0;
     aAlfa     = "";
     nCampo    = 0;
     &enOperar = 0;
     nError    = 0;
     nCampo14  = 0;
     nCampo13  = 0;
|FIN;

|INCLUYE dscont_i;

|PROCESO Tipo5Fw29;  |TIPO 5;
     |SI enOperar = 1;
         |LEE 040#caw00029.p;
         #caw00029#9 = "NO";
         |LINEAS_BI_ATRI #caw00029#9, D, S, -1;
     |FINSI;
|FPRC;

|PROCESO PonActivado;
     #caw00029#nCampo# = aAlfa;
     |GRABA 020#caw00029.a;
     |LIBERA #caw00029;
|FPRC;

|DEFBUCLE caw00029P;
     #caw00029#1;
     ;
     0000, 00001, 0;
     9999, 99999, 9;
     be;
     NULL, PonActivado;
|FIN;

|PROCESO LeoEmpresa;
     nError = 0;
     |ABRE #30;
     #30#2 = #999#14;
     #30#3 = #999#13;
     |LEE 041#30=;
     |SI FSalida = 0;
         nError = 1;
         |SI #999#14 ! #999#1;
             aAlfa = "0000NYa existe la Empresa Destino";
         |SINO;
             |SI #999#13 ! #999#2; |O #999#6 ! #30#26;
                 aAlfa ="0000NPeriodo Contable Ya Existente, Ejercicio: " + #30#26;
             |SINO;
                 aAlfa = "0000SEmpresa y Ejercicio Ya existe";
             |FINSI;
         |FINSI;
     |FINSI;
     |CIERRA #30;
     |SI nError = 1;
         aAlfa = aAlfa + " .Continuar";
         |CONFI aAlfa;
         |SI FSalida = 0;
             #999#9 = "SI";
             nError = 0;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo11Fw29;  |TIPO 11;
     |SI FSalida = 10;
         aAlfa  = "S";
         nCampo = 8;
         |BUCLE caw00029P;
         |PONCONTROL  23, "SI";
         |PTEC 812;
     |FINSI;

     |SI FSalida = 11;
         aAlfa = "N";
         nCampo = 8;
         |BUCLE caw00029P;
         |PONCONTROL  23, "SI";
         |PTEC 812;
     |FINSI;

     |SI FSalida = 12;
         |LEE 101#caw00029.=;

         |SI #caw00029#8 = "N";
             #caw00029#8 = "S";
         |SINO;
             #caw00029#8 = "N";
         |FINSI;
         |GRABA 020#caw00029.a;
         |LIBERA #caw00029;
         |PINTA #caw00029#8;
         |ERROR;
     |FINSI;

     |SI FSalida = 13;
         aAlfa = "S";
         |SI #caw00029#7 = "S"; aAlfa = "N"; |FINSI;
         nCampo = 7;
         |BUCLE caw00029P;
         |PONCONTROL  23, "SI";
         |PTEC 812;
     |FINSI;

     |SI FSalida = 14; |Y enOperar = 1;
         |LEE 101#caw00029.=;

         nCampo14 = #999#14;
         nCampo13 = #999#13;

     |ET NumEmpresa;
         |ENCAMPO #14#999;
         |SI FSalida = 1; |LIBERA #caw00029; |ACABA; |FINSI;

         |ENCAMPO #13#999;
         |SI FSalida = 2; |VETE NumEmpresa; |FINSI;
         |SI FSalida = 1; |LIBERA #caw00029; |ACABA; |FINSI;

         |HAZ LeoEmpresa;
         |SI nError = 1;
             #999#14 = nCampo14; |PINTA #999#14;
             #999#13 = nCampo13; |PINTA #999#13;
             |VETE NumEmpresa;
         |FINSI;

         |GRABA 020#caw00029.a;
         |LIBERA #caw00029;
         |PINTA #caw00029#8;
         |ERROR;
         || pedir nuevo periodo y empresa
     |FINSI;

     |SI FSalida = 15; |Y enOperar = 1;
         |ABRE #6;
         |DDEFECTO #6;
         #6#2  = #999#1;
         #6#26 = #999#6;
         |LEE 041#6=;
         |PUSHV 0501 2380;
         |PINPA #0#6;
         |PINDA #0#6;
         |PAUSA;
         |POPV;
         |CIERRA #6;
     |FINSI;
|FPRC;

|PROCESO Boton141;
     |PTEC 824;
|FPRC;

|PROCESO Boton142;
     |PTEC 825;
|FPRC;

|PROCESO Boton143;
     |PTEC 826;
|FPRC;

|PROCESO Boton144;
     |PTEC 827;
|FPRC;

|PROCESO Boton145;
     |PTEC 828;
|FPRC;

|PROCESO Boton146;
     |PTEC 829;
|FPRC;

|PROCESO PonBotonW29;

     |SI enOperar = 0;
         |CONTROL_SIMPLE 0, "BOTON,F2 Archivar Todas", 0803, 0818, Boton141;
         nBoton1 = FSalida;

         |CONTROL_SIMPLE 0, "BOTON,F3 No Archivar Todas", 0820, 0837, Boton142;
         nBoton2 = FSalida;

         |CONTROL_SIMPLE 0, "BOTON,F4 Mod. Archivar Linea", 0839, 0854, Boton143;
         nBoton3 = FSalida;

         |CONTROL_SIMPLE 0, "BOTON,F5 Cambiar Borrar (S/N)", 0856, 0874, Boton144;
         nBoton4 = FSalida;
     |FINSI;

     |SI enOperar = 1;
         |CONTROL_SIMPLE 0, "BOTON,F2 Recupera Todas", 0803, 0817, Boton141;
         nBoton1 = FSalida;

         |CONTROL_SIMPLE 0, "BOTON,F3 No Recupera Todas", 0818, 0832, Boton142;
         nBoton2 = FSalida;

         |CONTROL_SIMPLE 0, "BOTON,F4 Mod.Recupera Linea", 0833, 0849, Boton143;
         nBoton3 = FSalida;

         |CONTROL_SIMPLE 0, "BOTON,F5 Cambia Borra (S/N)", 0850, 0864, Boton144;
         nBoton4 = FSalida;

         |CONTROL_SIMPLE 0, "BOTON,F6 Empresa + Periodo", 0865, 0879, Boton145;
         nBoton5 = FSalida;

         |CONTROL_SIMPLE 0, "BOTON,F7 Ver Datos Archivo", 0965, 0979, Boton146;
         nBoton6 = FSalida;
    |FINSI;
|FPRC;

|PROCESO QuitaBotonW29;

     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;
     |FIN_CONTROL_WINDOWS nBoton4;
     |SI enOperar = 1;
         |FIN_CONTROL_WINDOWS nBoton5;
         |FIN_CONTROL_WINDOWS nBoton6;
     |FINSI;

     |PULSATECLA;
|FPRC;
