|FICHEROS;
     gadz0001 #1;

     caclipro #5;
     cacuenta #6;

     camaniva;
     caivalin;
     ca8cumay;
     ca8m0002;
     cacuebal;
     calibros;
     caivaasi;
     caivases;
     caintiva #7;
     cainliva #8;

     :/modelos/def/dsmom030 #130;
     :/dsarchi/def/dpntm100 #850;
     :/dsarchi/def/dpntm101 #851;
     :/dsarchi/def/dsarm005 #805;

     &regisnif@ #709;

     gadm0004 #1004;
|FIN;

|VARIABLES;
     nConceIRPF = 0;
     aDesIRPF = "";

     nBotonSalir     = -1;
     nBotonCancelar  = -1;
     nBotonAceptar   = -1;
     nBotonContinuar = -1;
     nContinuar      = -1;

     aQueBoton       = "";
     aID             = "";

      aMensa   = "";
      mensa1   = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
      mensa2   = "    ERROR FECHA INFERIOR O IGUAL AL BLOQUEO DE DATOS";
      &enSWAut = 0;
      &enLiAut = 0;
      &enOrAut = 0;
      &eaSeAut = "";
      nInkey   = 0;
      aBlancs  = "";
      aAlfa1   = "";
      aAlfa2   = "";
      aAlfa3   = "";
      aAlfa5   = "";
      aAlfa7   = "";
      mesdes   = "";
      meshas   = "";

      xx       = 0;
      aDCon1   = "";
      aDCon2   = "";
      nDCon0   = 0;
      nDCon1   = 0;

      &eaVarDni  = "";
      &enCalcCif = 0;

      swNuevo  = 0;
      swRepite = 0;

      &enQSerie = 0;  || 0 Sin serie,  1 con serie
      &entrada  = 1;
      &ct1_am   = "";
      &ct2_am   = 0;
      &fec_am   = @;
      &imp_am   = 0;
      &iva_am   = 0;
      &tiva_am  = 0;
      &c_am     = "";
      &prv_am   = "";
      &c_conp   = 0;
      &nLib_am  = 0;
      &aSer_am  = "";
      &nFra_am  = 0;
      &dni_am   = "";
      &clau_am  = "";
      &igic_am  = "";
      &nDoc_am  = 0;
      &nBas_am  = 0;
      &aElemento = "";
      &nNumero  = 0;

      a_param   = "";
      fFecha1   = @;
      alfa1     = "";
      alfa2     = "";
      alfa3     = "";
      alfa4     = "";
      alfa5     = "";
      alfa6     = "";
      nNume1    = 0;
      nNume2    = 0;
      nNume3    = 0;
      nNume4    = 0;
      nNume5    = 0;
      nNume6    = 0;
      muestra   = "";
      nSwLaC    = 0;
      asteris   = 0;
      longi     = 0;

      Comodin   = "";
      Comodin1  = "";
      ComodNum  = 0;
      nPara1    = 0;
      nActualizado = 0;
      nRegPpal     = 0;
      nConta       = 0;

      ModoEntrada = 0;
      factura     = 0;
      NumFactura  = 0;
      NumBase     = 0;
      SerieAnt    = "  XX";
      FacturaAnt  = 0;
      nFacCon     = 0;
      FechaAnt    = "";
      CuentaAnt   = "";
      DigitoAnt   = 0;
      PerioAnt    = 0;
      ClienAnt    = "";
      CifAnt      = "";
      ConceptoAnt = 0;
      DescAnt     = "";
      DeparAnt    = "";
      NDeparAnt   = "";
      SubdeAnt    = "";
      NSubdeAnt   = "";
      ReferAnt    = "";
      aAntesDes0 = "";
      aAntesDes1 = "";
      nAnterior  = 0;
      nAnteriorIRPF = 0;
      nCamp10     = 0;
      nCamp11     = 0;
      nCamp12     = 0;
      nCamp13     = 0;
      nCamp14     = 0;
      nCamp15     = 0;
      nCamp16     = 0;
      aCamp17     = "";
      nCamp18     = 0;
      aCamp19     = "";
      aCamp28     = "";
      nCamp29     = 0;
      aCamp32     = "";
      nCamp34     = 0;
      nCamp35     = 0;
      aCamp36     = "";
      nCamp37     = 0;
      aCamp39     = "";
      aCamp40     = "";
      nCamp41     = 0;
      aCamp42     = "";
      nCamp43     = 0;
      aCamp44     = "";
      aCamp49     = "";
      nCamp51     = 0;
      aCamp52     = "";
      aCamp54     = "";
      aCamp55     = "";
      aCamp56     = "";
      aCamp57     = "";
      aCamp58     = "";
      nCamp59     = 0;
      nCamp60     = 0;
      fCamp61     = @;
      aCamp62     = "";
      aCamp63     = "";
      aCamp64     = "";
      aCamp68     = "";
      aTexto      = "";
      aFraPrv     = "";

      sw     = 0;
      SwMovs = 0;
      Serie  = "";
      nTecla  = 0;

      BaseAnt = 0;
      Calc = 0;
      swpase = 0;

      aFich1 = "";
      aFich2 = "";
      nYaCrea = 0;
      swmalo  = 0;
      swpcta = 0;
      &enModAmor = 0;

      &enSwActual = 0;
      &en0W17   = 0;
      &ef1W17   = @;
      &en2W17   = 0;
      &nSwHay17 = 0;
      &eaFitW17 = "";
      &coant      = 0;
      &deant      = "";
      &dpant      = "";
      &sdant      = "";
      &refant     = "";
      fechaW17    = @;

      aMas       = "";
      aDirInicio = "";
      nEstaLin   = 0;
      nEstado    = 0;

      aImpriSN   = "";
      nSwImp     = 0;
      aCPartida  = "";
      nCTiva     = 0;
      swclipro   = 0;

      &libro     = 0;
      &serie     = "";
      &orden     = 0;
      &depart    = "";
      &pesetas   = 0;
      &cuenta    = "";
      &digito     = 0;
      &emision   = "";
      &nConcep   = 0;
      &aDesc     = "";
      &enModoEsc = 0;
      &enLinea   = 0;
      &enCyP     = "";
      &eaRef     = "";
      aCta        = "";
      aAlfa       = "";
      &enSwBUENO = 0;

      nError = 0;
      &enDiAna = 0;
      &efFeAna = @;
      &enDoAna = 0;
      &enApAna = 0;
      &eaGrAna = "";
      &enImAna = 0;
      &enQueAna = 0;
      &nSwGrupo = 0;
      &eaFitnW12 = "";
      &eaFitnW13 = "";

      &r_entra = "";
      &r_alfa1 = "";

      nNumLin  = 0;
      aSerTot  = "";
      nFacTot  = 0;
      nLinTot  = 0;

      &ext1 = "";
      &ext2 = "";

      swAmor   = 0;
      swInve   = 0;
      nIva0    = 0;
      nPrimLin = 0;
      nMasLin  = 0;
      nSwOp    = 0;
      nswcambio = 0;
      nNoSalta  = 0;
      nSwIntra  = 0;
      nCampo    = 0;
      nPanta1   = 0;


      &enPerIntra = 0;
      &enIn03   = 0;

     {20}aPopup        = "";
        nID            = 0;
      swAmorC   = 0;

      aEjecuta          = "";
      aAbre             = "";
      aPathDefDSARCHI   = "";
      aPathFichGAD      = "";
      aPathFichDESPANET = "";
      aPathDSARCHI      = "";
      nDSARCHI          = 0;
      nHandle           = 0;
      nBoton1           = 0;
      nBoton2           = 0;
      nEstadoBoton1     = 0;
      nEstadoBoton2     = 0;
      nForma            = 0;
      aValor            = "";
      aSerieArc         = "";
      nFactuArc         = 0;

      nPulsoF9 = 0;
      aPCorreo = "";

      &eaTip349   = "";
      &eaFichCab  = "";
      &eaFichLin  = "";
      aGuarda     = "";
|FIN;

|VARIABLES;
         nVent      = 0;
         nVentTrab  = 0;

     {-1}aVent      = "";
         aVent1     = 0;
         aVent2     = 0;
         aVent3     = 0;
         aVent4     = 0;
         aVent5     = "";
|FIN;

|INCLUYE dscoiv_i;
|INCLUYE dscont_i;
|INCLUYE dscoan_i;
|INCLUYE defec1_i;
|INCLUYE modulo_i;
|INCLUYE cla340_i;

|PROCESO Tipo15Fm5;
|FPRC;

|PROCESO Tipo16Fm5;
|FPRC;

|PROCESO Tipo17Fm5;
|FPRC;


|PROCESO CuentaCont;
     |SI enQSerie = 0;
         |ABRE #calibros;
         #calibros#0 = dLibro;
         |LEE 101#calibros.=;
         |SI FSalida = 0;
             NumFactura  = #calibros#4;
             #calibros#4 = #calibros#4 + #calibros#3;
             |GRABA 020#calibros.a;
         |FINSI;
         |LIBERA #calibros;
         |CIERRA #calibros;
     |SINO;
         |ABRE #caivases;
         #caivases#0 = #1#26;
         #caivases#1 = aTipoIVA;
         |LEE 101#caivases.=;
         |SI FSalida = 0;
             NumFactura  = #caivases#3;
             #caivases#3 = #caivases#3 + #caivases#4;
             |GRABA 020#caivases.a;
        |FINSI;
        |LIBERA #caivases;
        |CIERRA #caivases;
     |FINSI;

     #gadz0001#2 = NumFactura;

     |SI nYaCrea = 0;
         enFilConc  = 2;
         |HAZ CreaConcepto;
         aFich1     = eaFiCon;
         enFilConc  = 3;          || Conceptos de IRPF
         |HAZ CreaConcepto;
         aFich2     = eaFiCon;

         nYaCrea    = 1;
     |FINSI;
|FPRC;

|PROCESO DescuentaCont;
     |SI enQSerie = 0;
         |ABRE #calibros;
         #calibros#0 = dLibro;
         |LEE 101#calibros.=;
         |SI FSalida = 0;
             NumFactura = NumFactura + #calibros#3;
             |SI NumFactura = #calibros#4;
                 #calibros#4 = #calibros#4 - #calibros#3;
                 |GRABA 020#calibros.a;
             |FINSI;
         |FINSI;
         |LIBERA #calibros;
         |CIERRA #calibros;
     |SINO;
         |ABRE #caivases;
         #caivases#0 = #1#26;
         #caivases#1 = aTipoIVA;
         |LEE 101#caivases.=;
         |SI FSalida = 0;
             NumFactura = NumFactura + #caivases#4;
             |SI #caivases#3 = NumFactura;
                 #caivases#3 = #caivases#3 - #caivases#4;
                 |GRABA 020#caivases.a;
             |FINSI;
         |FINSI;
         |LIBERA #caivases;
         |CIERRA #caivases;
     |FINSI;
|FPRC;

|PROCESO CreaVtos;
     enModoEsc = 1;
     |SI #1#49 = "S";
         |ACABA;
     |FINSI;

     enLinea = #1#1;
     libro   = #1#0;
     serie   = #1#26;
     orden   = #1#2;
     emision = #1#4;
     |SI #1#54 = "N";
         pesetas = #1#29;
     |SINO;
         pesetas = #1#12;
     |FINSI;
     cuenta = #1#5; eaCuenta = cuenta; |HAZ SacaNivel;
     digito = enNivel;
     enCyP  = #1#49;
     eaRef  = #1#53;
     |HAZ EsComersan;
     |SI FSalida = 0; |Y aTipoIVA = "S";
         aFraPrv = #1#72; |QBF aFraPrv;
         |SI aFraPrv ! "";  eaRef = aFraPrv; |FINSI;
     |FINSI;
     |SUB_EJECUTA_NP "caintvto";
|FPRC;

|PROCESO ActCliPro;
     |ABRE #caclipro;
     |SI aTipoIVA = "R";
         #caclipro#23 = "C";
     |SINO;
         #caclipro#23 = "P";
     |FINSI;
     #caclipro#10 = #1#5;
     #caclipro#11 = #1#6;
     |LEE 101#caclipro.=;
     |SI FSalida = 0;
         |SI aCPartida = ""; |Y #caivaasi#128 = "S";
             #caclipro#21 = #1#17;
         |FINSI;
         |SI nCTiva = 0; |Y #caivaasi#129 = "S";
             #caclipro#42 = #1#10;
         |FINSI;
         |GRABA 020#caclipro.a;
     |FINSI;
     |LIBERA #caclipro;
     |CIERRA #caclipro;
|FPRC;

|PROCESO Tipo12gadz1;  |TIPO 12;
     nActualizado = 0;

     |SI FSalida = 7;
         NumFactura = #1#2;
         |HAZ DescuentaCont;

         |ACABA;
     |FINSI;

     |SI nContinuar ! 1;
         |ERROR;
         |ACABA;
     |FINSI;

     nContinuar = 0;

     #1#65 = #1#5;
     #1#66 = #1#17;
     #1#67 = #1#29;

     swpcta = 0;
     swpase = 0;
     swmalo = 0;
     swRepite = 0;

     |SI #1#2  = 0;
         swmalo = 1;
         alfa1 = "    Error. Registro no valido. No Existe N£mero de Factura";
     |FINSI;

     |SI #1#17 = doce;
         swmalo = 1;
         alfa1 = "    Error. Registro no valido. No Existe Cuenta Contrapartida";
     |FINSI;

     |SI #1#5 = doce;
         swmalo = 1;
         alfa1 = "    Error. Registro no valido. No Existe Cuenta Cliente/Proveeedor";
     |FINSI;

     |SI #1#4 < fec1; |O #1#4 > fec2;
         swmalo = 1;
         alfa1 = "    Error. Registro no valido. Fecha Incorrecta";
     |FINSI;

     |SI aElCero = "N"; |Y #1#12 = 0; |Y  #1#14 = 0;
          |Y #1#16 = 0; |Y #1#29 = 0; |Y  #1#35 = 0;
          swmalo = 1;
          alfa1 = "    Error. Factura de importe igual a 0";
     |FINSI;

     |SI swmalo = 1;
         |MENAV alfa1;
         |ERROR;
         |ACABA;
     |FINSI;

     |SI swclipro = 1;
         |ABRE #caivaasi;
         |DDEFECTO #caivaasi;
         |LEE 000#caivaasi.p;
         |CIERRA #caivaasi;
         |SI #caivaasi#128 = "S"; |O #caivaasi#129 = "S";
             |HAZ ActCliPro;
         |FINSI;
     |FINSI;

     swclipro = 0;

     |SI #caivaasi#1 = "S";
         |HAZ CreaVtos;
     |FINSI;

     nPrimLin   = 0;
     nMasLin    = 0;
     nswcambio  = 0;

     eaFichCab  = ("ca" + Usuario + "zzzzzzzz") % 108;
     eaFichLin  = ("li" + Usuario + "zzzzzzzz") % 108;

     |NOME_DAT #7 eaFichCab;
     |NOME_DAT #8 eaFichLin;

     |ABRE #7;  |CIERRA #7;  |DELINDEX #7;
     |ABRE #8;  |CIERRA #8;  |DELINDEX #8;

     |ABRE #7;
     #7#0 = #1#0;
     |GRABA 020#7n;
     |LIBERA #7;
     |CIERRA #7;

     |ABRE #8;
     |PARA nPara1 = 0; |SI nPara1 [ 72; |HACIENDO nPara1 = nPara1 + 1;
           #8nPara1 = #1nPara1;
     |SIGUE;
     |GRABA 020#8n;
     |LIBERA #8;
     |CIERRA #8;

     enLibro    = #1#0;
     |SUB_EJECUTA "caivaact;GAD";

     |ABRE #7;  |CIERRA #7;  |DELINDEX #7;
     |ABRE #8;  |CIERRA #8;  |DELINDEX #8;

     nActualizado = 1;
|FPRC;

|PROCESO PintaCobro;
    |SI #gadz0001#49 ! "N";
        |C_V #gadz0001#40,1,"S";
    |SINO;
        |C_V #gadz0001#40,1,"N";
    |FINSI;

    |PINTA #gadz0001#40;
|FPRC;

|PROCESO Bajacaivases;
     #caivases#0 = "";
     #caivases#1 = #1#50;
|FPRC;

|PROCESO Altacaivases;
     #caivases#0 = "zzzzz";
     #caivases#1 = #1#50;
|FPRC;

|PROCESO Tipo7C26;  |TIPO 7;
     |SI nBotonSalir = -1;
         aQueBoton     = "|X000";
         aID           = #gadz0001#aQueBoton#;
         nBotonAceptar = aID;
         |ESTADO_CONTROL nBotonAceptar, "HIDE";

         aQueBoton      = "|X001";
         aID            = #gadz0001#aQueBoton#;
         nBotonCancelar = aID;
         |ESTADO_CONTROL nBotonCancelar, "HIDE";

         |CONTROL_SIMPLE 0, "BOTON,{{230}}Continuar", 2588, 2598, Continuar;
         nBotonContinuar = FSalida;

         |CONTROL_SIMPLE 0, "BOTON,{{197}}Salir", 2576, 2586, Salir;
         nBotonSalir = FSalida;

         |PTEC 802;
     |FINSI;
|FPRC;

|PROCESO Tipo66C26;  |TIPO 66;
     |ABRE #caivases;
     #caivases#0 = #1#26;
     #caivases#1 = #1#50;
     |LEE 000#caivases.];

     |CONSULTA_F_CLAVE #1#caivases, Bajacaivases, Altacaivases;
     |SI FSalida = 0;
         #1#26 = #caivases#0;   |PINTA #1#26;
     |FINSI;
     |CIERRA #caivases;
|FPRC;

|PROCESO Tipo0C26; |TIPO 0;
     |SI Contenido = #1Campo;  |Y #1#2 ! 0;  |ACABA;  |FINSI;

     |HAZ CuentaCont;

     #gadz0001#2 = NumFactura;

     |PINTA #gadz0001#2;

     enFilConc  = 2;
     enConcepto = #1#7;
     eaReperc   = " ";
     |HAZ FiltroConcepto;
     Campo = 8;  |HAZ PonloAlFinal;
|FPRC;

|PROCESO Tipo0C2;  |TIPO 0;
    |SI #gadz0001#2 ! NumFactura;
        |HAZ DescuentaCont;
    |FINSI;

    |HAZ PintaCobro;
|FPRC;

|PROCESO Tipo0C4; |TIPO 0;
||     |C_M #1#4, 0, aAlfa;
||     |SI aAlfa = "N";   |ACABA;  |FINSI;

     |SI #1#4 < fec1; |O #1#4 > fec2;
         |MENAV mensa1;
         |ERROR;
         |ACABA;
     |SINO;
         |SI #1#4 [ fec3;
             |MENAV mensa2;
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;

     Comodin = #1#4; |QBF Comodin;
     Comodin = Comodin % 0402;
     |SI enEjercicio [ 2009;
         |SI #caivaasi#46 = "T";
             |SI Comodin ] "01"; |Y Comodin [ "03"; Comodin = "01"; |FINSI;
             |SI Comodin ] "04"; |Y Comodin [ "06"; Comodin = "02"; |FINSI;
             |SI Comodin ] "07"; |Y Comodin [ "09"; Comodin = "03"; |FINSI;
             |SI Comodin ] "10"; |Y Comodin [ "12"; Comodin = "04"; |FINSI;
         |SINO;
             |SI #caivaasi#46 = "A"; Comodin = "00"; |FINSI;
         |FINSI;
     |FINSI;

     ComodNum = Comodin;
     #1#31 = ComodNum; |PINTA #1#31;

     fFecha1 = Contenido;
     |SI fFecha1 ! #1#4;  nswcambio = 1; |FINSI;
|FPRC;

|PROCESO Tipo66C7;  |TIPO 66;
     eaFiCon = aFich1;
     enConcepto = #1#7;
     |HAZ VerConcepto;
     #1#7 = enConcepto;
     |PINTA #1#7;
|FPRC;

|PROCESO Tipo0C7;  |TIPO 0
     enLibIVA   = #1#0;
     enFilConc  = 2;
     enConcepto = #1#7;
     eaReperc   = " ";
     |HAZ FiltroConcepto;
     |SI eswLect ! 0; |ACABA; |FINSI;

     |C_M #1#45,0,aAlfa1;
     |SI aAlfa1 = "N";   ||no modifica Actividad
         enActividad = #1#45;
         |SI Haybasica = 1;
             enFilRegIVA = 0;
             enConcepto  = #1#7;
             |HAZ FilRegimenIVA;
             |SI eswLect ! 0; |ACABA; |FINSI;
         |FINSI;
     |FINSI;

     |SI #1#7 = dConc1;
         #dsmom030#1 = dDesc1;
     |FINSI;

     #1#8 = #dsmom030#1; |PINTA #1#8;

     nSwIntra = 1;
     |SI #1#54 = "N";
         nSwImp = 0;
         nSwIntra = 0;
     |SINO;
         |SI #dsmom030#26 = 5; eaTip349 = "AR";  |FINSI;
         |SI #dsmom030#26 = 6; eaTip349 = "IR";  |FINSI;
         |SI #dsmom030#30 = 2; eaTip349 = "AR";  |FINSI;
         |SI #dsmom030#30 = 3; eaTip349 = "IR";  |FINSI;
         |SI #dsmom030#34 = 9; eaTip349 = "ER";  |FINSI;
         |SI #dsmom030#34 = 4; eaTip349 = "SR";  |FINSI;
     |FINSI;

     #1#17 = eaCtaIVA; |PINTA #1#17;
     emCta    = 0;
     eaCuenta = #1#17; |HAZ SacaNivel; #1#18 = enNivel;
     |HAZ LeeCuenta;
     |SI eswCta ! 0;
         |C_M #1#17,1,"S";
     |SINO;
         #1#28 = #cacuenta#2; |PINTA #1#28;
     |FINSI;

/*
     |C_M #1#10, 1,snTiva
     |SI eaReperc = "N";
         #1#10 = enNumExe; |PINTA #1#10;
         |C_M #1#10, 1,"N";
     |FINSI;

     swAmorC = 0;
     |C_M #1#32, 1, snInver;
     |SI aTipoIVA = "S";
         |SI #dsmom030#28 = 2; |O #dsmom030#28 = 4; |O #dsmom030#28 = 6;
             #1#32 = "S"; |PINTA #1#32; swAmorC = 1;
             |C_M #1#32, 1, "N";
         |SINO;
             |SI #dsmom030#32 = 3; swAmorC = 2; |FINSI;
             #1#32 = "N"; |PINTA #1#32;
         |FINSI;
     |SINO;
         |SI #dsmom030#34 = 14;
             #1#32 = "S"; |PINTA #1#32;
         |FINSI;
     |FINSI;

*/

     #1#32 = "N";

     aClau340 = #1#62;
     nNumFras = #1#60;
     |HAZ ClauDefecto;
     #1#62 = aClau340;
     |PINTA #1#62;
|FPRC;

|PROCESO Tipo0C10; |TIPO 0;
     |SI FSalida = 2; |ACABA; |FINSI;

     |SI FSalida = 10;
        efFechaIVA = #1#4;  |HAZ SelecTipoIVA;
        #1#10 = enLineaIVA; |PINTA #1#10;
        |ERROR;
        |ACABA;
     |FINSI;

     efFechaIVA = #1#4; enLineaIVA = #1#10;
     |HAZ SacaIVA;

     #1#20 = eaCtaIVA;
     #1#22 = eaCtaREC;
     #1#24 = eaCtaDTO;

     |SI #1#19 = "N";
         |SI dCtaNoD ! "";
             #1#20 = dCtaNoD;
             #1#22 = dCtaNoD;
         |SINO;
             #1#20 = #1#17;
             |SI #1#15 ! 0; #1#20 = #1#17; |FINSI;
         |FINSI;
     |FINSI;

     |SI #1#19 = "I";
         #1#20 = "";
         #1#22 = 0;
     |FINSI;

     emCta = 0;
     eaCuenta = #1#20; |HAZ SacaNivel; #1#21 = enNivel;
     eaCuenta = #1#22; |HAZ SacaNivel; #1#23 = enNivel;
     eaCuenta = #1#24; |HAZ SacaNivel; #1#25 = enNivel;
|FPRC;

|PROCESO ImprContra;
     |QBF aCPartida;
     |SI aCPartida ! "";
         #1#17 = aCPartida;
         |PINTA #1#17;
         emCta    = 0;
         eaCuenta = #1#17;
         |HAZ SacaNivel;
         #1#18 = enNivel;
         |HAZ LeeCuenta;
     |FINSI;

     |SI eswCta ! 0; |O aCPartida = "";
         #1#17 = eaCtaIVA;
         |PINTA #1#17;
         emCta    = 0;
         eaCuenta = #1#17;
         |HAZ SacaNivel;
         #1#18 = enNivel;
         |HAZ LeeCuenta;
     |FINSI;

     |SI eswCta ! 0;
         |C_M #1#17,1,"S";
     |SINO;
         #1#28 = #cacuenta#2; |PINTA #1#28;
     |FINSI;

     |SI nCTiva ! 0;
         |SI #1#10 ! nCTiva;
             #1#10 = nCTiva; |PINTA #1#10;
             nNoSalta = 1; |HAZ Tipo0C10; nNoSalta = 0;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO LeeLaContra;
     |ABRE #caclipro;
     |SI aTipoIVA = "R";
         #caclipro#23 = "C";
     |SINO;
         #caclipro#23 = "P";
     |FINSI;
     aCPartida = "";
     nCTiva    = 0;
     swclipro  = 0;
     #caclipro#10 = #1#5;
     #caclipro#11 = #1#6;
     |LEE 000#caclipro.=;
     |SI FSalida = 0;
         aCPartida = #caclipro#21; |QBF aCPartida;
         nCTiva    = #caclipro#42;
         |SI aCPartida = ""; |O nCTiva = 0; swclipro = 1; |FINSI;
     |FINSI;
     |CIERRA #caclipro;
     |HAZ ImprContra;
|FPRC;

|PROCESO Tipo6C5; |TIPO 6;
     |SI Campo = 5; swRepite = 0; |FINSI;

     |C_M #1#Campo#, 0, aAlfa;
     |SI aAlfa = "N"; |ACABA; |FINSI;

     |SI FSalida = 10; || ... extractos
         ext1 = #1#5;          || cuenta
         ext2 = #1#6;          || digito
         |SUB_EJECUTA_NP "caextrap";
         |ERROR;
         |ACABA;
     |FINSI;

     |SI Campo = 40; |Y #1#49 = "N"; |ACABA; |FINSI;

     eaCuenta = #1#Campo#;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #1#Campo# = eaCuenta;
     |PINTA #1#Campo#;

     |SI #1#Campo# = "            ";
         |MENAV "0000Introduzca una cuenta";
         |ERROR;
     |FINSI;
|FPRC;

|CALCULO CuentaCobro;
    |SI #1#49 ! "N";
        #1#41 = #cacuenta#1;
        #1#42 = #cacuenta#2;
        |SI #1#49 = "T";
            #1#43 = dConCEf;
            #1#44 = dDesCEf;
        |SINO;
            #1#43 = dConCar;
            #1#44 = dDesCar;
       |FINSI;
   |FINSI;
|FCAL;

|PROCESO Tipo0C5; |TIPO 0;
     aGuarda = eaCtaIVA;
     |C_M #1#Campo#, 0, aAlfa;
     |SI aAlfa = "N";
         |SI Campo = 5;
             |SI #1#3 > 01;
                 |HAZ LeeLaContra;
             |FINSI;
         |FINSI;

         |SI Campo ! 40;  |ACABA;  |FINSI;
     |FINSI;

     |SI Contenido ! #1#Campo#; nswcambio = 1; |FINSI;

     |SI swRepite = 1; |ACABA; |FINSI;

     emCta    = 1;
     eaCuenta = #1#Campo#;
     |HAZ SacaNivel;
     |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea
     nNume1 = Campo + 1;
     #1#nNume1# = enNivel;

     |HAZ LeeCuenta;
     |SI eswCta ! 0; |ACABA; |FINSI;

     enConcepto = #1#7;
     |SI Campo = 5;    || Leer Proveedor/Cliente
         |HAZ FiltraCtaCP;
         |SI eswLect = 0;
             |ABRE #caclipro;
             |SI aTipoIVA = "R";
                 #caclipro#23 = "C";
             |SINO;
                 #caclipro#23 = "P";
             |FINSI;
             aCPartida = "";
             nCTiva    = 0;
             swclipro  = 0;
             #caclipro#10 = #1#5;
             #caclipro#11 = #1#6;
             |LEE 000#caclipro.=;
             |SI FSalida = 0;
                 #1#27 = #caclipro#1;
                 #1#30 = #caclipro#9;
                 aCPartida = #caclipro#21; |QBF aCPartida;
                 nCTiva    = #caclipro#42;
                 |SI aCPartida = ""; |O nCTiva = 0; swclipro = 1; |FINSI;
             |SINO;
                 #1#27 = #cacuenta#2;
                 #1#30 = "";
                 |C_M #1#30,1,"S";  || No tengo el nif.
             |FINSI;
             |CIERRA #caclipro;

             |PINTA #1#27;
             |PINTA #1#30;
             |HAZ ImprContra;
         |FINSI;
     |SINO;
         |SI Campo = 17;
             |HAZ FiltraCtaIG;
             #1#28 = #cacuenta#2; |PINTA #1#28;
         |SINO;
             |HAZ CuentaCobro;
         |FINSI;
     |FINSI;
     eaCtaIVA = aGuarda;
|FPRC;

|PROCESO Tipo0C45; |TIPO 0;
     |SI eaDepar = "N"; |ACABA; |FINSI;
     |SI FSalida = 2;   |ACABA; |FINSI;

     eOpDep      = 0;                            || 1 = Consulta pantalla
     enActividad = #1#45;
     eaCodDep    = #1#45;
     |HAZ Actividad;
     |SI eswLect = 1;
         |MENAV "    Error.! No Existe Actividad / Departamento";
         |C_M #1#Campo#, 1, "S";
         |ERROR;
     |SINO;
         alfa1 = enActividad;
         |SI Haybasica = 1;
             enFilRegIVA = 1;
             enConcepto  = #1#7;
             |HAZ FilRegimenIVA;
             alfa1 = ("00" + alfa1) % A02;
         |FINSI;

         #1#45 = eaCodDep;     |PINTA #1#45;
         #1#46 = eaNombreAct;  |PINTA #1#46;
         |SI efActFin > "01.01.1900"; |Y snFilBa = "S";
             aAlfa1 = "    NAtencion!!!. Actividad con Fecha de BAJA " + efActFin + ", Desea introducir Facturas ? ";
             |CONFI aAlfa1;
             |SI FSalida ! 0;
                 |ERROR;
                 |ACABA;
             |FINSI;
         |FINSI;
         |SI eaEsCom = "S";
             |CONFI "    NAtencion!!!. Actividad Comunera, ¨ Desea introducir Facturas ? ";
             |SI FSalida ! 0;
                 |ERROR;
                 |ACABA;
             |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo0C47; |TIPO 0;
     |SI eaSubde = "N"; |ACABA; |FINSI;

     enConcepto  = #1#7;
     eaCodSubdep = #1#47;
     enQue = 1;
     |HAZ SubActividad;
     enQue = 0;
     |SI eswLect = 1;
         |MENAV "    Error.! No Existe SubDepartamento";
         |ERROR;
     |SINO;
         #1#47 = eaCodSubdep; |PINTA #1#47;
         #1#48 = eaNomSubdep; ||  |PINTA #1#48;
         |HAZ pSubdepar;
     |FINSI;
|FPRC;

|PROCESO CalCuenta;
     asteris = 0;
     muestra = dCtaCEf; |QBF muestra;
     alfa1   = dCtaCEf; |QBF alfa1;
     alfa2   = alfa1 % 0;
     longi   = alfa2;            || 'longi' sirve despues (no tocar)
     nNume3  = (longi * 100) + 1;
     alfa2   = alfa1 % nNume3;
     |SI alfa2 = "*";
         nNume3    = 99 + longi;
         muestra   = alfa1 % nNume3;
         asteris   = 1;
     |FINSI;

     nSwLaC = 0;
     |PARA nNume4 = 1; |SI nNume4 [ longi; |HACIENDO nNume4 = nNume4 + 1;
           nNume1 = (nNume4 * 100) + 1;
           alfa3  = alfa1 % nNume1;
           |SI alfa3 = "C";  nSwLaC = 1; |FINSI;
     |SIGUE;

     |SI nSwLaC = 1;
         alfa5 = muestra % 0;
         nNume5 = alfa5;
         alfa5 = "";
         |PARA nPara1 = 1; |SI nPara1 [ nNume5; |HACIENDO nPara1 = nPara1 + 1;
               nNume6 = (nPara1 * 100) + 1;
               alfa6  = muestra % nNume6;
               |SI alfa6 = "C";
                   alfa6 = #1#5 % nNume6;
               |FINSI;
               alfa5 = alfa5 + alfa6;
        |SIGUE;

        muestra = alfa5;
    |FINSI;

     nNume2 = MaximoDigitos - (longi - 1);
     |SI asteris = 1;
         nNume2 = (100 * longi) + nNume2;
         alfa1 = #1#5 % nNume2;
         muestra = muestra + alfa1;             || si es "*"
     |FINSI;

     #1#40 = muestra;
|FPRC;

|PROCESO Tipo0C49;  |TIPO 0;
    |SI FSalida = 2; |ACABA; |FINSI;

     alfa1 = Contenido;
     |SI alfa1 ! #1#49; |Y #1#49 = "T";
         #1#40 = "";
     |FINSI;
     |HAZ PintaCobro;

     |SI #1#49 = "N";
         #1#40 = ""; #1#41 = 0;
         #1#42 = ""; #1#43 = 1; #1#44 = "";
         |ACABA;
     |FINSI;

     alfa1 = "S";
     |SI #1#49 ! "T"; alfa1 = "N"; |FINSI;
     |SI #1#49 = "T"; alfa1 = snCtaEf; |FINSI;
     |SI #1#49 = "P"; alfa1 = snCtaCa; |FINSI;

     |C_M #1#40, 1, alfa1;

     |SI #1#49 ! "T";
         |SI #1#40 = doce; #1#40 = dCtaCar; |FINSI;
         |SI dCtaCar = ""; |Y snCtaCa = "N";
             |MENAV "    Error, NO esta INFORMADA LA CUENTA DE COBRO/PAGO. No puede crear Asiento. ";
             #1#49 = "N"; |PINTA #1#49;
             |HAZ PintaCobro;
             |ERROR;
             |ACABA;
         |FINSI;
     |SINO;
         |SI #1#40 = doce;
             |HAZ CalCuenta;
         |SINO;
             |C_M #1#Campo#,0,alfa1;
             |SI alfa1 = "S"; alfa1 = snCtaEf; |FINSI;
             |C_M #1#40,1,alfa1;
         |FINSI;
     |FINSI;
     |PINTA #1#40;
|FPRC;

|PROCESO Tipo66C51;  |TIPO 66;
     eaFiCon    = aFich2;
     enConcepto = #1#51;
     |HAZ VerConcepto;
     #1#51 = enConcepto;
     |PINTA #1#51;
|FPRC;

|PROCESO Tipo0C51;  |TIPO 0;
     |C_M #1#51, 0, aAlfa;
     |SI aAlfa = "N";  |ACABA;  |FINSI;

     |SI #1#51 = 0;
         |ERROR;
     |FINSI;

     |SI #1#51 < nDCIrpf; |O #1#51 > nHCIrpf;
         |SI snFiltr ! "X";
             |MENAV "    Error, Concepto Contable no Correcto ";
         |FINSI;
         |SI snFiltr = "S";
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;

     enLibIVA  = #1#0;
     enFilConc = 3;        || Concepto de IRPF
     enConcepto = #1#51;
     |HAZ FiltroConcepto;  |SI eswLect ! 0; |ACABA; |FINSI;

     |SI snFiMod = "S";
         enPanMod = 0;
         eaFecMod = #1#4;
         enPerMod = #1#31;
         |HAZ ConsulPlan;
         |SI enExiMod = 0; |Y snSeMod = "N";
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;

     |SI #1#51 ! nAnteriorIRPF;
         #1#52 = #dsmom030#1; |PINTA #1#52;
     |FINSI;

     #1#36 = eaCtaIVA;
     emCta    = 0;
     eaCuenta = #1#36; |HAZ SacaNivel; #1#37 = enNivel;

     |SI #dsmom030#38 = 180; |Y aTipoIVA = "S";
         |SI #1#62 ! "R"; |Y #1#62 ! "D";
             |CONFI "    SFra con Retencion Mod.180 y Clave 340 diferente a <R>. Modificarla ?";
             |SI FSalida = 0;
                 #1#62 = "R"; |PINTA #1#62;
             |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO adapta2_c;  ||El concepto
     aDCon1 = "";
     |SI aDCon2 = "S";
         aDCon1 = #gadz0001#26; |QBF aDCon1;
         |SI aDCon1 ! ""; aDCon1 = aDCon1 + aSepSer; |QBF aDCon1; |FINSI;
     |FINSI;                                         || Serie
     |SI aDCon2 = "F";
         aDCon1 = #gadz0001#2; |QBF aDCon1;
         |SI aNumIzq = "S";
             aDCon1 = ("000000" + aDCon1) % -106;
         |FINSI;
     |FINSI;                                         || Factura
     |SI aDCon2 = "C";
         aDCon1 = #gadz0001#5; |QBF aDCon1;
     |FINSI;                                         || Cuenta Cliente
     |SI aDCon2 = "N";
         aDCon1 = #gadz0001#27; |QBF aDCon1;
     |FINSI;                                         || Nombre Cliente
     |SI aDCon2 = "R";
         |SI #gadz0001#53 ! "                    ";
             aDCon1 = #gadz0001#53; |QBF aDCon1;
         |FINSI;
     |FINSI;                                         || Referencia
     |SI aDCon2 = "P";
         aDCon1 = #gadz0001#53;
         |SI aTipoIVA = "S";
             |HAZ EsComersan;
             |SI FSalida = 0; aDCon1 = #gadz0001#72;  |FINSI;
         |FINSI;
         |QBF aDCon1;
     |FINSI;                                         || Referencia Comersan

     |SI aDCon1 ! "";
         |SI aDCon2 = "F";
             aAlfa7 = aAlfa2 - "S";
             |SI aAlfa7 ! aAlfa2; |Y #gadz0001#26 > "     ";
                 |ACABA;
             |FINSI;
         |FINSI;
         aDCon1 = " " + aDCon1;
     |FINSI;
|FPRC;

|PROCESO adaptaC2;  ||El concepto en IVA
     aAlfa5 = "";
     aAlfa1 = #dsmom030#1;  |QBF aAlfa1;  || Descripcion
     |SI xx = 1; aAlfa2 = #caivaasi#45;  |FINSI;  ||Cliente
     |SI xx = 2; aAlfa2 = #caivaasi#130; |FINSI;  ||IVA
     |SI xx = 3; aAlfa2 = #caivaasi#96;  |FINSI;  ||IRPF
     |SI aAlfa2 = "=  "; aAlfa2 = #caivaasi#45; |FINSI;
     |QBF aAlfa2;  || lo que a¤ade

     aAlfa3 = aAlfa2 % 0;
     nDCon0 = aAlfa3;
     |PARA nDCon1 = 1; |SI nDCon1 [ nDCon0; |HACIENDO nDCon1 = nDCon1 + 1;
           aAlfa3 = nDCon1;
           aAlfa3 = aAlfa3 + "01";
           aDCon2 = aAlfa2 % aAlfa3;
           |HAZ adapta2_c;
           aAlfa5 = aAlfa5 + aDCon1;
     |SIGUE;
     aAlfa1  = aAlfa1 + aAlfa5;
|FPRC;


|PROCESO LosConceptos;
   |SI #caivaasi#131 = "S"; ||  ... Siempre por si modifica alguna cosa
       |SI nCampo ! 8; |Y nCampo ! 52; nCampo = 8; |FINSI;
       xx = 1; |SI nCampo = 52; xx = 3; |FINSI;
       |HAZ adaptaC2;
       #1nCampo = aAlfa1;
       |PINTA #1nCampo;
   |FINSI;
|FPRC;

|PROCESO PonloAlFinal;  |TIPO 7;
     |SI Campo !  52; |O #1#51 ! 0;
         nCampo = Campo;
         |HAZ LosConceptos;
         nCampo = 0;
     |FINSI;
|FPRC;

|PROCESO Continuar;
     nContinuar = 1;
     |PTEC 806;
|FPRC;

|PROCESO Salir;
     |PTEC 807;
|FPRC;

|PROCESO ActualizaNIF;
     |SI nCampo ! 4;
         aAlfa = #709nCampo;  |QBF aAlfa;
         |SI aAlfa = "";  |O aAlfa = "0";
             #709nCampo = aValor;
         |FINSI;
     |SINO;
         |SI #709#4 = 0;  |Y #709#5 = 0;
             aAlfa1 = aValor % 102;
             aAlfa2 = aValor % 303;
             #709#4 = aAlfa1;
             #709#5 = aAlfa2;

             enCodProv = #709#4;  |HAZ LeeProvincia;
             #709#7 = eaProvincia;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO CreaNIF;
     |HAZ LeeNifs;

     |ABRE #709;
     #709#0 = #1#30;
     |LEE 101#709=;
     |SI FSalida ! 0;
         |DDEFECTO #709;
         #709#0 = #1#30;
         |GRABA 020#709b;
     |FINSI;

     |SI aTipoIVA = "R";
         nCampo = 1;   aValor = #850#4;  |HAZ ActualizaNIF;
         nCampo = 12;  aValor = #850#5;  |HAZ ActualizaNIF;
     |FINSI;

     |SI aTipoIVA = "S";
         nCampo = 1;   aValor = #851#4;   |HAZ ActualizaNIF;
         nCampo = 12;  aValor = #851#5;   |HAZ ActualizaNIF;
         nCampo = 2;   aValor = #851#51;  |HAZ ActualizaNIF;
         nCampo = 6;   aValor = #851#53;  |HAZ ActualizaNIF;
         nCampo = 8;   aValor = #851#54;  |HAZ ActualizaNIF;
         nCampo = 17;  aValor = #851#55;  |HAZ ActualizaNIF;
         nCampo = 4;   aValor = #851#52;  |HAZ ActualizaNIF;
     |FINSI;

     |GRABA 020#709a;
     |LIBERA #709;
     |CIERRA #709;
|FPRC;

|PROCESO CreaCuenta;
     |VENTANA 0501, 2599, -1, 16, "No existe la cuenta contable correspondiente a este CIF/NIF. Elija con INTRO la cuenta a crear.";
     nVentTrab = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentTrab;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

||     |MENAV "0000No existe el NIF en las cuentas contables. Elija una cuenta a crear";

     |PARA;  |SI;  |HACIENDO;
          eaCodNif  = #1#30; |QBF eaCodNif;
          eaCuenta  = "F*+";
          eaNomCta  = #709#1;
          |HAZ FiltraPuntos;
          |SI eaCuenta ! "";  |SAL;  |FINSI;

          |MENAV "0000Por favor seleccione una cuenta.";
     |SIGUE;

     |ABRE #6;
     |DDEFECTO #6;

     enNivel = dig3;
     #6#0    = eaCuenta;
     #6#1    = enNivel;
     #6#3    = "S";

     |SI aTipoIVA = "R";  #6#2 = #850#4;  |FINSI;
     |SI aTipoIVA = "S";  #6#2 = #851#4;  |FINSI;

     nMensa   = 1;
     nMensa9  = 1;
     |HAZ defect1;
     #6#56   = mesdes;
     #6#57   = meshas;

     |GRABA 020#6n;
     |LIBERA #6;
     |CIERRA #6;

     |DDEFECTO #5;
     |ABRE #5;
     |SELECT #1#5;

     |SI aTipoIVA = "R";  #5#23 = "C";  |FINSI;
     |SI aTipoIVA = "S";  #5#23 = "P";  |FINSI;

     #5#10 = #6#0;
     #5#0  = #5#10;
     #5#9 = #709#0;
     #5#1 = #709#1;
     Comodin = #709#9; |QBF Comodin;
     |SI Comodin = "";
         #5#2 = #709#2;
     |SINO;
         #5#2 = Comodin + " " + #709#2;
     |FINSI;
     Comodin = #5#2; |QBF Comodin;
     #5#2    = Comodin + ", " + #709#3;
     Comodin = #5#2; |QBF Comodin;
     #5#2    = Comodin + " " + #709#10;
     Comodin = #5#2; |QBF Comodin;
     #5#2    = Comodin + " " + #709#11;

     #5#3    = #709#4;
     #5#4    = #709#5;
     #5#5    = #709#7;
     #5#6    = #709#6;
     #5#7    = #709#8;
     #5#8    = #709#17;
     #5#24   = #709#12;

     #5#44 = #5#2;
     #5#45 = #5#3;
     #5#46 = #5#4;
     #5#47 = #5#5;
     #5#48 = #5#6;
     #5#27 = aPCorreo;
     #5#49 = #5#27
     #5#50 = #5#25;

     |GRABA 020#5n;
     |LIBERA #5;
     |CIERRA #5;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentTrab;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVentTrab;

     |MENAV "0000Creada nueva cuenta contable, revise la cuenta";
|FPRC;

|PROCESO ModLosDefectos;
     |HAZ LeeDefIVA;

     #1#50 = aTipoIVA;

     |C_M #1#26, 1,snSerie;  || Modificables S/N
     |C_M #1#7,  1,snConc1;
     |C_M #1#8,  1,snDesc1;
     ||   |C_M #1#10, 1,snTiva;   ||Quitado hasta nueva orden
     |C_M #1#32, 1,snInver;
     |C_M #1#19, 1,snDeduc;

     |SI #1#34 = 0;  |Y #1#35 = 0;
         snReten = "N";
         snDIrpf = "N";

         #1#55   = "N";
         #1#56   = "N";
     |FINSI;

     |C_M #1#51, 1,snReten;
     |C_M #1#55, 1,snReten;
     |C_M #1#56, 1,snReten;
     |C_M #1#52, 1,snDIrpf;

     |C_M #1#59, 1,snBIrpf;
     |SI snReten = "N"; #1#51 = 0; |FINSI;

     alfa1 = #caivaasi#20;        ||Departamento
     |SI eaDepar = "N"; alfa1 = "N"; |FINSI;
     |C_M #1#45, 1, alfa1;
     alfa1 = #caivaasi#68;        ||Subdepartamento
     |SI eaSubde = "N"; alfa1 = "N"; |FINSI;
     |C_M #1#47, 1, alfa1;

     alfa1 = #caivaasi#28;        ||Periodo Modifica o Calcular
     |C_M #1#31, 1, alfa1;

     alfa1 = #caivaasi#70;        ||Cuenta Base
     |C_M #1#17,1, alfa1;

     #1#49 = dCoPag;
     |C_M #1#49,1,snCoPa;

     |SI enNumAct > 1; |C_M #1#57, 1,"S"; |FINSI;

     |HAZ PintaCobro;
|FPRC;

|PROCESO CargaDatosRecibidas;
     |ABRE #gadm0004;
     |DDEFECTO #gadm0004;
     |LEE 000#gadm0004.p;
     nConceIRPF = #gadm0004#1;
     aDesIRPF = #gadm0004#2;
     |CIERRA #gadm0004;

     nError = 0;

     |DBASS aPathDSARCHI;   |QBF aPathDSARCHI;
     aPathFichGAD = aPathDSARCHI + "dsarchi/fich/";
     |PATH_DAT #805 aPathFichGAD;

     aPathFichDESPANET = aPathDSARCHI + "despanet/" + (("00000" + #48#2) % -105) + "/fich/";
     |PATH_DAT #851 aPathFichDESPANET;

     |ABRE #851;
     #851#0 = PARAMETRO;
     |LEE 000#851=;
     |CIERRA #851;

     |DDEFECTO #gadz0001;
     #gadz0001#4  = #851#15;
     #gadz0001#7  = 600;
     #gadz0001#12 = #851#21;
     #gadz0001#13 = #851#22;
     #gadz0001#14 = #851#23;
     #gadz0001#15 = #851#24;
     #gadz0001#16 = #851#25;
     #gadz0001#27 = #851#4;
     #gadz0001#29 = #851#28;
     #gadz0001#30 = #851#2;
     #gadz0001#32 = #851#18;
     #gadz0001#45 = ("00" + #851#1) % -102;
     #gadz0001#49 = dCoPag;
     #gadz0001#53 = #851#13;
     #gadz0001#60 = #851#9;
     #gadz0001#61 = #851#16;
     #gadz0001#62 = #851#8;
     #gadz0001#63 = #851#11;
     #gadz0001#64 = #851#12;
     #gadz0001#67 = #851#28;
     #gadz0001#68 = #851#14;
     aPCorreo     = #851#56;

     |SI #851#8 = "P";
         #gadz0001#54 = "S";
     |FINSI;

     |SI #851#26 ! 0;
         #gadz0001#39 = "B";
         #gadz0001#59 = #gadz0001#12;
         #gadz0001#34 = #851#26;
         #gadz0001#35 = #851#27;
         #gadz0001#51 = nConceIRPF;
         #gadz0001#52 = aDesIRPF;
         |MENAV "0000Factura con retencion, revise el concepto.";
     |FINSI;

     efFechaIVA = #1#4;
     enLineaIVA = 0;
     enPorcIVA  = #1#13;
     enPorcREC  = #1#15;
     aTipoIVA   = "S";
     |HAZ BuscaTipoIVA;
     #1#10 = enLineaIVA;

     |HAZ CreaNIF;
     #1#69 = #851#0;

     |ABRE #5;
     |SELECT #4#5;
     #5#9 = #1#30;
     |LEE 000#5];
     |PARA;  |SI FSalida = 0;  |HACIENDO;
          |SI #5#9 ! #1#30;  |SAL;  |FINSI;

          |SI #5#23 = "P";
              #1#5 = #5#10;
              |SAL;
          |FINSI;

          |LEE 000#5s;
     |SIGUE;

     |SELECT #1#5;
     |CIERRA #5;

     swpcta        = 1;
     aAlfa         = #1#5;   |QBF aAlfa;
     |SI aAlfa = "";
         |HAZ CreaCuenta;
         #1#5 = #5#10;
     |FINSI;

     |HAZ ModLosDefectos;

     #1#3 = 1;

     |SI #851#44 = "N";
         |ABRE #805;
         #805#0 = #851#50;
         |LEE 000#805=;
         |SI FSalida ! 0;  |DDEFECTO #805;  |FINSI;
         |CIERRA #805;

         |SI #805#73 = 0;
             |MENAV "0000Factura con varios tipos de IVA. Seleccione la primera factura";
             nError = 1;
             |ACABA;
         |FINSI;

         |ABRE #camaniva;
         #camaniva#0 = dLibro;
         #camaniva#1 = #805#73;
         |LEE 000#camaniva.=;
         |SI FSalida = 0;
             #1#26 = #camaniva#2;
             #1#2  = #camaniva#3;
         |FINSI;
         |CIERRA #camaniva;

         |ABRE #caivalin;
         #caivalin#0 = #camaniva#0;
         #caivalin#1 = #camaniva#1;
         #caivalin#2 = 99;
         |LEE 000#caivalin.];
         |SI FSalida = 0;
             |LEE 000#caivalin.a;
         |SINO;
             |LEE 000#caivalin.u;
         |FINSI;

         #1#3 = 2;
         |SI FSalida = 0;  |Y  #caivalin#0 = #camaniva#0;  |Y  #caivalin#1 = #camaniva#1;
             #1#3 = #caivalin#2 + 1;
         |FINSI;
         |CIERRA #caivalin;

         |C_M #1#2,  1, "N";
         |C_M #1#26, 1, "N";
     |FINSI;

     #gadz0001#0  = dLibro;
     enLibIVA     = dLibro;

     |SI #gadz0001#53 = "                    ";
         |HAZ EsComersan;
         |SI FSalida = 0; |Y aTipoIVA = "S";
             |SI eaRefer = "N"; enLgRef = #caivaasi#117; |FINSI;
              #gadz0001#53 = #gadz0001#26 + "-." + #gadz0001#2;
              r_entra = entrada;
              r_alfa1 = #gadz0001#53;
              |HAZ r1_punto;
              #gadz0001#53 = r_alfa1;
         |FINSI;
     |FINSI;

     |HAZ Tipo0C4;
     |HAZ Tipo0C7;
     |HAZ Tipo0C10;
     |HAZ Tipo0C45;
     |HAZ Tipo0C49;

     Campo = 8;  |HAZ PonloAlFinal;
|FPRC;

|PROCESO EntradaDeDatos;
     |SI nConta = 0;
         aAlfa = "Validaci¢n de datos contables";
     |SINO;
         aAlfa = "Validaci¢n de datos contables base: " + nConta;
     |FINSI;

     |VENTANA 0501, 2599, -1, 16, aAlfa;
     nVent  = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |HAZ CargaDatosRecibidas;
     |SI nError ! 0;  |ACABA;  |FINSI;

     |PINPA #0#gadz0001; nPanta1 = FSalida;

     |SI nConta ! 0;
     |FINSI;

     |C_V #1#72, 1, "N";
     |C_M #1#72, 1, "N";

     |HAZ EsComersan;
     |SI FSalida = 0; |Y aTipoIVA = "S";
         |C_V #1#72, 1, "S";
         |C_M #1#72, 1, "S";
         |CONTROL_SIMPLE nPanta1, "LABEL,{{7}}N§.factura proveedor", 2178, 2198, NULL;
     |FINSI;

     |PINDA #0#gadz0001;

     |ENDATOS #1#gadz0001;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVent;
|FPRC;

|PROGRAMA;
     |HAZ inicio;

     |HAZ EntradaDeDatos;

     |SI nActualizado = 0;
         |PULSATECLA;
         |PULSATECLA;
         |PULSATECLA;
         |ACABA;
     |FINSI;

     nRegPpal = #851#0;

     |ABRE #851;
     |SELECT #4#851;
     #851#45 = nRegPpal;
     #851#0  = 1;
     |LEE 000#851];

     nConta = 2;
     |PARA;  |SI FSalida = 0;  |HACIENDO;
          |SI #851#45 ! nRegPpal;  |SAL;  |FINSI;

          |SI #851#29 = "N";
              aAlfa  = "0000Factura con varias bases. Se va a proceder a contabilizar la base " + nConta;
              |MENAV aAlfa;
              nBotonSalir = -1;

              PARAMETRO = #851#0;
              |SELECT #1#851;
              |HAZ EntradaDeDatos;
          |FINSI;

          |SELECT #4#851;
          |SI nActualizado = 1;
              nConta = nConta + 1;

              #851#45 = nRegPpal;
              #851#0  = 1;
              |LEE 000#851];
          |SINO;
              |MENAV "0000La factura se tiene que contabilizar.";
              |LEE 000#851=;
          |FINSI;
     |SIGUE;

     |CIERRA #851;

     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
|FPRO;
