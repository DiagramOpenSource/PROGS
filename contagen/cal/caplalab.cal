|FICHEROS;
   caplalab #0;

   camaniva #1;
   caivalin #2;
   caclipro #3;
   caivaasi #200;

   caplala1 #4,MANTE;
   caplala2 #5,MANTE;
   caplala3 #10,MANTE;

   camaasic #6;
   camaasil #7;
   :/modelos/def/dsmom030 #9;
   :/modelos/def/dsmom107 #107;

   cadeflab #210;
   cadeflac #211;
   cadeflax #212;
   cadeflay #213;
   cadeflaz #214;
   anm00007 #207;

   cadefxab #230;
   cadefxax #232;
   cadefxaz #234;

   cafantas #13;
   &regisnif@ #709;
|FIN;

|VARIABLES;
   aDCtaAna    = "";
   aHCtaAna    = "";
   alfa1       = "";
   &UltPeriodo = "";
   &UltFecha   = "";

   Desglose    = 1;
   aParam      = "";
   FEstado     = 0;
   Cont        = 0;
   Calc        = 0;
   Calc1       = 0;
   nNumCmps    = 0;
   Comodin     = "";
   Comodin1    = "";
   aLong       = "";
   nLong       = 0;
   SwEscrita   = 0;

   Mes         = 0;
   DiasMes     = 0;
   FechaHoy    = @;
   NumApunte   = 0;
   NumApunteAnt = 0;
   NumLinea    = 0;

   SwError     = 0;
   LinBase     = 0;
   NumRegistro = 0;
   nLibro      = 0;

   &ModoEntrada = 0;
   Cpto         = 0;
   Desc         = "";
   Base         = 0;
   Porc         = 0;
   Cuota        = 0;
   aTipo        = "";

   &eaVarDni   = "";
   &enCalcCif  = 0;
   &NombreCif  = "";

   nSwHay      = 0;
   aCtaAreaSec = "";
   aAnaAreaSec = "";
   aCtaCentro = "";
   aAnaCentro = "";
   aEspecie   = "";
   aRepercute = "";

   nSwPrimer = 0;
   &enSwPasa = 0;
   &eaIncide = "";
   &eaExiMod = "";

   nPara1  = 0;
   nNume1  = 0;
   nNume2  = 0;
   nNume3  = 0;
   aAlfa   = "";
   aAlfa1  = "";
   aAlfa2  = "";
   aAlfa3  = "";
   aAlfCod = "";
   aAlfCon = "";
   aAlfTro = "";

   eaConcepto = "";
   nInkey = 0;

   aCtaCentroY = "";
   aCtaCentroZ = "";
   &enSinCentro = 0;
   nSinEste  = 0;
   nSwCentro = 0;
   aPath     = "";

   nCta9  = 0;
   nCta21 = 0;
   nCta44 = 0;
   nCta48 = 0;
   nCta37 = 0;
   nCta13 = 0;
   nCta17 = 0;
   nCta25 = 0;
   nCtaRe = 0;

   nConce   = 0;
   &nsw30   = 0;
   &aAlfa30 = "";
   nNumNuevo = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoan_i;
|INCLUYE dscoiv_i;
|INCLUYE dscola_i;

|| ********************************************* Calculos PANTALLA
|PROCESO PintaFecha0;
    alfa1 = &205; alfa1 = alfa1 * 40;
    |PINTA 0526, alfa1;
    #cafantas#10 = efActIni;
    #cafantas#11 = efActFin;
    |SI efActIni > "01.01.1900";
        |ATRI S; |PINTA 0526, "F.Inicio: "; |ATRI 0;
        |PINTA 0536, #cafantas#10;
    |FINSI;
    |SI efActFin > "01.01.1900";
        |ATRI S; |PINTA 0547, "F.Fin: "; |ATRI 0;
                 |PINTA 0554, #cafantas#11;
    |FINSI;
|FPRC;

|CALCULO Eltip13; |TIPO 13;
    alfa1 = &205; alfa1 = alfa1 * 40;
    |PINTA 0526, alfa1;
    ModoEntrada = (FEntrada / 100) ! 0;
    |SI ModoEntrada  [ 1; |ACABA; |FINSI;
    |SI eaDepar = "S";
       eOpDep = 0;
       enActividad = #0#27;
       eaCodDep    = #0#27;
       |HAZ Actividad;
       |HAZ PintaFecha0;
   |FINSI;
|FCAL;

|CALCULO LetraDni; |TIPO 6;
   |SI FSalida = 10;
       enCalcCif = 1;
       eaVarDni  = #0#1;
       |HAZ CalculoDNI;
       #0#1 = eaVarDni;
       |PINTA #0#1;
       |ERROR;
   |FINSI;

     eaVarDni  = #0Campo;
     enCalcCif = 0;
     |HAZ CalculoDNI;
     eaVarDni = (eaVarDni + "             ") % 112;
     |SI #0Campo ! eaVarDni;
         |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
         |SI FSalida = 0;
             #0Campo = eaVarDni;  |PINTA #0Campo;
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;
|FCAL;

|CALCULO TrasDni; |TIPO 0;

   |SELECT #4#3;
   #3#9 = #0#1;
   |LEE 010#3=;
   |SI FSalida = 0;
      NombreCif = #3#1;
   |SINO;
      eaCodNif  = #0#1;
      |HAZ LeeNifs;
      NombreCif = #709#1;
   |FINSI;
   |SELECT #1#3;

   efRNomina  = #caplalab#2;
   enRLaboral = #caplalab#55;
   eaTrans    = #caplalab#56;
   |SI HayLaboral = 1;
       eaCodNif = #0#1;
       |HAZ eBuscaRLaboral;
   |FINSI;

   ModoEntrada = (FEntrada / 100) ! 0;
   Comodin = "|NC";
   nNumCmps = #caplalab#Comodin#;
   nNumCmps = nNumCmps - 1;

   |PUSHV 0501 2380;

   |DDEFECTO #4;
   #0#26 = NombreCif;
   |PARA Cont = 0; |SI Cont [ nNumCmps; |HACIENDO Cont = Cont + 1;
         #4Cont = #0Cont;
   |SIGUE;
   |PINPA #0#4;
   |PINDA #0#4;

   |HAZ LeeDefLab;
   |SI ModoEntrada = 1;
       #4#55 = enRLaboral;
       #4#56 = eaTrans;
       |HAZ TomaDefectos;
       |ENDATOS #1#4;
   |FINSI;
   |SI ModoEntrada = 2;
       |HAZ TomaDefModif;
       |ENDATOS #2#4;
   |FINSI;
   |SI FSalida = 0;
       |PARA Cont = 0; |SI Cont [ nNumCmps; |HACIENDO Cont = Cont + 1;
             #0Cont = #4Cont;
       |SIGUE;
       |PINDA #0#0;
       UltPeriodo = #4#3;
       UltFecha   = #4#2;
   |SINO;
       |ERROR;
   |FINSI;
   |POPV;
|FCAL;

|| ////////////////////////////////

|PROCESO Borra03;
   |BORRA 020#10a;
|FPRC;

|DEFBUCLE Borra03;
 #10#1;
 ;
 #0#0,#0#1,001;
 #0#0,#0#1,999;
 ;
 NULL,Borra03;
|FIN;

|PROCESO Borra02;
   |BORRA 020#5a;
|FPRC;

|DEFBUCLE Borra02;
 #5#1;
 ;
 #0#0,001;
 #0#0,999;
 ;
 NULL,Borra02;
|FIN;

|CALCULO Baja; |TIPO 1;
   ModoEntrada = (FEntrada / 100) ! 0;

   |SI ModoEntrada = 3;
       |BUCLE Borra02;
       |BUCLE Borra03;
   |FINSI;
|FCAL;

|| /////////////////////////////////////
|PROCESO LeeDefLab;
   |ABRE #107;
   |LEE 000#107p;
   |CIERRA #107;

   |ABRE #210;
   |LEE 001#210p;
   |SI FSalida ! 0;
       |DDEFECTO #210;
       |GRABA 020#210n;
   |SINO;
       |SI #210#111 = doce;
           #210#111 = #210#16;
           #210#112 = #210#17;
           #210#113 = #210#18;
           |SI nsw30 = 0;
               #210#112 = #210#17;
               #210#113 = #210#18;
           |SINO;
               #210#112 = 463;
               #210#113 = aAlfa30;
           |FINSI;
           #210#114 = #210#39;
           #210#115 = #210#40;
           #210#116 = #210#41;
           |GRABA 020#210a;
       |FINSI;
   |FINSI;
   |CIERRA #210;

   |ABRE #200;
   |LEE 000#200p;
   |CIERRA #200;

   snFiMod = #200#111;
   snSeMod = #200#112;
   snFilBa = #200#119;
   eaPaDOM1 = #200#114;  ||documento automatico
   eaPaDOM2 = #200#133;  ||permitir repetir diferentes diarios
   eaPaDOM3 = #200#134;  ||permitir ultimo + 1
   eaPaCTA1 = #200#135;  ||permitir alta no estructuras 2008
   eaPaCTA2 = #200#136;  ||Aviso 1990
   eaPaNif1 = #200#152;
   eaPaNif2 = #200#153;
   eaABanco = #200#161;
   eaObClPr = #200#186;

   enSinCentro = 0;
   |SI #210#60 ! 0; |O #210#61 ! "N";
       |ABRE #230;
       |LEE 001#230p;
       |SI FSalida = 0; enSinCentro = 1; |FINSI;
       |CIERRA #230;
   |FINSI;
|FPRC;

|PROCESO TomaDefectos;

   #4#7  = #210#2;  || Concepto Total Devengado
   #4#8  = #210#3;  || Descripcion Total Devengado
   #4#9  = #210#1;  || Cuenta Total Devengado

   #4#11 = #210#5;  || Concepto Aportacion S.S.
   #4#12 = #210#6;  || Descripcion Aportacion S.S.
   #4#13 = #210#4;  || Cuenta Aportacion S.S.

   #4#15 = #210#8;  || Concepto Anticipos
   #4#16 = #210#9;  || Descripcion Anticipos
   #4#17 = #210#7;  || Cuenta Anticipos

   #4#19 = #210#11; || Concepto En especie 908
   #4#20 = #210#12; || Descr. En especie
   #4#21 = #210#10; || Cuenta En especie

   #4#19 = #210#11; || Concepto En especie 908
   #4#20 = #210#12; || Descr. En especie
   #4#21 = #210#10; || Cuenta En especie

   #4#42 = #210#64; || Concepto En especie 922
   #4#43 = #210#65; || Descr. En especie
   #4#44 = #210#63; || Cuenta En especie

   #4#46 = #210#70; || Concepto En especie 923
   #4#47 = #210#71; || Descr. En especie
   #4#48 = #210#69; || Cuenta En especie

   #4#23 = #210#14; || Concepto Liquido
   #4#24 = #210#15; || Descr. Liquido
   #4#25 = #210#13; || Cuenta Liquido

   #4#35 = #210#50; || Concepto Otras deducciones
   #4#36 = #210#51; || Descr. Otras deducciones
   #4#37 = #210#49; || Cuenta Otras Deducciones

   #4#32 = #210#19; || Libro
   #4#33 = #210#54; || Diario

   #4#31 = #210#21; || Descripcion general

   #4#27 = #210#22; || Departamento
   #4#28 = #210#23; || Nombre Departamento
   #4#29 = #210#57; || subdepartamento
   #4#30 = #210#58; || Nombre suddepartamento
   #4#40 = #210#60; || Centro de Trabajo

   #4#3  = UltPeriodo; |PINTA #4#3;
   #4#50 = #210#102;
   #4#51 = #210#104;

   |PINDA #0#4;

   |HAZ TomaDefModif;
|FPRC;

|PROCESO TomaDefModif;

   |SI eaDepar = "N"; #210#52 = "N"; |FINSI;
   |SI eaSubde = "N"; #210#59 = "N"; |FINSI;

   Comodin = #210#24; |C_M #4#9 ,1,Comodin;
   Comodin = #210#25; |C_M #4#7 ,1,Comodin;
   Comodin = #210#26; |C_M #4#8 ,1,Comodin;

   Comodin = #210#27; |C_M #4#13,1,Comodin;
   Comodin = #210#28; |C_M #4#11,1,Comodin;
   Comodin = #210#29; |C_M #4#12,1,Comodin;

   Comodin = #210#30; |C_M #4#17,1,Comodin;
   Comodin = #210#31; |C_M #4#15,1,Comodin;
   Comodin = #210#32; |C_M #4#16,1,Comodin;

   Comodin = #210#33; |C_M #4#21,1,Comodin;
   Comodin = #210#34; |C_M #4#19,1,Comodin;
   Comodin = #210#35; |C_M #4#20,1,Comodin;

   Comodin = #210#36; |C_M #4#25,1,Comodin;
   Comodin = #210#37; |C_M #4#23,1,Comodin;
   Comodin = #210#38; |C_M #4#24,1,Comodin;

   Comodin = #210#46; |C_M #4#35,1,Comodin;
   Comodin = #210#47; |C_M #4#36,1,Comodin;
   Comodin = #210#48; |C_M #4#37,1,Comodin;

   Comodin = #210#61; |C_M #4#40,1,Comodin;
   Comodin = #210#43; |C_M #4#32,1,Comodin;
   Comodin = #210#56; |C_M #4#33,1,Comodin;
   Comodin = #210#53; |C_M #4#26,1,Comodin;
   Comodin = #210#52; |C_M #4#27,1,Comodin;
   Comodin = #210#59; |C_M #4#29,1,Comodin;

   Comodin = #210#67; |C_M #4#42,1,Comodin;
   Comodin = #210#68; |C_M #4#43,1,Comodin;
   Comodin = #210#66; |C_M #4#44,1,Comodin;

   Comodin = #210#73; |C_M #4#46,1,Comodin;
   Comodin = #210#74; |C_M #4#47,1,Comodin;
   Comodin = #210#72; |C_M #4#48,1,Comodin;

   Comodin = #210#103;
   |SI Comodin = "X";
       Comodin = "N";
       #4#50 = ""; |PINTA #4#50;
       #4#51 = ""; |PINTA #4#51;
  |FINSI;
  |C_M #4#50,1,Comodin;
  |C_M #4#51,1,Comodin;
|FPRC;

|| ************************************************************************

|PROCESO Actualiza;
   |SI #camaasil#8 = "D";
      #camaasic#4 = #camaasic#4 + #camaasil#9;
   |SINO;
      #camaasic#5 = #camaasic#5 + #camaasil#9;
   |FINSI;
   #camaasic#6  = #camaasic#4 - #camaasic#5;
   #camaasic#13 = 3;
   |SI aParam ! ""; |Y #210#110 = "N"; |Y #107#29 = "S";
       #camaasic#13 = 103;
   |FINSI;
   |GRABA 020#camaasic.a;
   |LIBERA #camaasic;

   MasMenos     = #camaasil#9;
   DebeHaber    = #camaasil#8;
   aCUENTA      = #camaasil#4;
   nREGISTRO    = #camaasil#24;
   nDIARIO      = #camaasil#0;
   aFECHA       = #camaasil#1;
   nDOCUMENTO   = #camaasil#2;
   nNIVELCUENTA = #camaasil#5;
   nPIDECUENTA  = 1;
   |HAZ ActDiario; |HAZ ActCuenta; |HAZ ActTesteo;
|FPRC;

|PROCESO BuscaLinea;
   NumLinea = #camaasil#3; SwError = 0;
   |ERROR10;
|FPRC;

|DEFBUCLE BuscaLinea;
 #camaasil#1;
 4,6,19,29,21,28;
 #6#0,#6#1,#6#2,0001,eaCuenta,nConce,aTipo, aDCtaAna, #caivalin#30, #caivalin#31;
 #6#0,#6#1,#6#2,9999,eaCuenta,nConce,aTipo, aHCtaAna, #caivalin#30, #caivalin#31;
 e;
 NULL,BuscaLinea;
|FIN;

|PROCESO CreaLineas;

   |SI aTipo = "B"; |Y #caivalin#6  = 0; |ACABA; |FINSI;
   |SI aTipo = "D"; |Y #caivalin#11 = 0; |ACABA; |FINSI;
   |SI aTipo = "P"; |Y #caivalin#22 = 0; |ACABA; |FINSI;

   aDCtaAna = "       ";
   aHCtaAna = "zzzzzzz";
   |SI enSwAna = 1; |Y aTipo = "B";
       aDCtaAna = #caivalin#28;
       aHCtaAna = #caivalin#28;
   |FINSI;

   nConce   = #caivalin#3;
   eaCuenta = #caivalin#23;
   aNOMCUENTA = "";
   |SI nCta9  = 1; |Y eaCuenta = #caplalab#9;  aNOMCUENTA = #0#26; |FINSI;
   |SI nCta21 = 1; |Y eaCuenta = #caplalab#21; aNOMCUENTA = #0#26; |FINSI;
   |SI nCta44 = 1; |Y eaCuenta = #caplalab#44; aNOMCUENTA = #0#26; |FINSI;
   |SI nCta48 = 1; |Y eaCuenta = #caplalab#48; aNOMCUENTA = #0#26; |FINSI;
   |SI nCta13 = 1; |Y eaCuenta = #caplalab#13; aNOMCUENTA = #0#26; |FINSI;
   |SI nCta37 = 1; |Y eaCuenta = #caplalab#37; aNOMCUENTA = #0#26; |FINSI;
   |SI nCta17 = 1; |Y eaCuenta = #caplalab#17; aNOMCUENTA = #0#26; |FINSI;
   |SI nCtaRe = 1; |Y aTipo = "P"; aNOMCUENTA = #0#26; |FINSI;

   |SI aTipo ! "P"; |O Desglose = 0;
      SwError = 1; |BUCLE BuscaLinea;
      |SI SwError = 0;
         NumApunteAnt = NumApunte;
         NumApunte = NumLinea;
      |FINSI;
   |FINSI;

   #camaasil#0 = #camaasic#0;
   #camaasil#1 = #camaasic#1;
   #camaasil#2 = #camaasic#2;
   #camaasil#3 = NumApunte;
   |LEE 101#camaasil.=;
   |SI FSalida = 0;
      |SI aTipo = "B";
         #camaasil#29 = #caivalin#28;
         #camaasil#9  = #camaasil#9 + #caivalin#6;
         #camaasil#20 = 0;
      |FINSI;
      |SI aTipo = "D";
         #camaasil#9 = #camaasil#9 + #caivalin#11;
         #camaasil#20 = 0;
      |FINSI;
      |SI aTipo = "P";
         |SI #caivalin#22 ! 0;
            #camaasil#9 = #camaasil#9 + #caivalin#22;
         |SINO;
            #camaasil#9 = #camaasil#9 + #caivalin#6;
         |FINSI;
         #camaasil#20 = 0;
      |FINSI;

      |SI Desglose = 1;
         |SI #camaasil#9 ! 0;
            |GRABA 020#camaasil.a; |LIBERA #camaasil; |LEE 000#camaasil.=;
            |SI aTipo = "B"; #camaasil#9 = #caivalin#6;  |FINSI;
            |SI aTipo = "D"; #camaasil#9 = #caivalin#11; |FINSI;
            |SI aTipo = "P";
               |SI #caivalin#22 = 0;
                  #camaasil#9 = #caivalin#6;
               |SINO;
                  #camaasil#9 = #caivalin#22;
               |FINSI;
            |FINSI;
            |HAZ Actualiza;
         |FINSI;
      |SINO;
         |GRABA 020#camaasil.a; |LIBERA #camaasil; |LEE 000#camaasil.=;
         |SI aTipo = "B"; #camaasil#9 = #caivalin#6;  |FINSI;
         |SI aTipo = "D"; #camaasil#9 = #caivalin#11; |FINSI;
         |SI aTipo = "P";
            |SI #caivalin#22 = 0;
               #camaasil#9 = #caivalin#6;
            |SINO;
               #camaasil#9 = #caivalin#22;
            |FINSI;
         |FINSI;
         |HAZ Actualiza;
      |FINSI;
   |SINO;
      |DDEFECTO #camaasil;
      #camaasil#0 = #camaasic#0;
      #camaasil#1 = #camaasic#1;
      #camaasil#2 = #camaasic#2;
      #camaasil#3 = NumApunte;
      #camaasil#4 = #caivalin#23;
      eaCuenta = #camaasil#4; |HAZ SacaNivel;
      #camaasil#5 = enNivel;
      #camaasil#6 = #caivalin#3;
      #camaasil#7 = #caivalin#4;

      |SI aTipo = "D";
         #camaasil#8 = "H";
         #camaasil#16 = "";          #camaasil#17 = 0;
         #camaasil#10 = #camaasil#4; #camaasil#11 = #camaasil#5;
      |SINO;
         #camaasil#8 = "D";
         #camaasil#10 = "";          #camaasil#11 = 0;
         #camaasil#16 = #camaasil#4; #camaasil#17 = #camaasil#5;
      |FINSI;

      |SI aTipo = "P";
         |SI #caivalin#22 = 0;
            #camaasil#8 = "D";
            #camaasil#16 = #camaasil#4; #camaasil#17 = #camaasil#5;
            #camaasil#10 = "";          #camaasil#11 = 0;
         |SINO;
            #camaasil#8 = "H";
            #camaasil#10 = #camaasil#4; #camaasil#11 = #camaasil#5;
            #camaasil#16 = "";          #camaasil#17 = 0;
         |FINSI;
      |FINSI;
      |SI aTipo = "B";
         #camaasil#29 = #caivalin#28;
      |FINSI;

      |SI Desglose = 1;
         #camaasil#12 = "N";
         #camaasil#13 = #camaniva#0;
         #camaasil#14 = #camaniva#3;
         #camaasil#15 = #camaniva#2;
         #camaasil#20 = #caivalin#2;
      |FINSI;
      #camaasil#19 = aTipo;
      #camaasil#21 = #camaniva#10;
      #camaasil#22 = 0;
      #camaasil#23 = "N";
      #camaasil#24 = nREGISTRO;
      Comodin = Usuario % 1099;
      #camaasil#27 = Comodin;
      #camaasil#28 = #camaniva#11;

      |SI aTipo = "B"; #camaasil#9 = #caivalin#6;  |FINSI;
      |SI aTipo = "D"; #camaasil#9 = #caivalin#11; |FINSI;
      |SI aTipo = "P";
         |SI #caivalin#22 ! 0;
            #camaasil#9 = #caivalin#22;
         |SINO;
            #camaasil#9 = #caivalin#6;
         |FINSI;
      |FINSI;

      |SI Desglose = 1;
         |SI #camaasil#9 ! 0;
            |GRABA 020#camaasil.n; |LEE 000#camaasil.=;
            |HAZ Actualiza;
            NumApunte = NumApunte + 10;
         |FINSI;
      |SINO;
         |GRABA 020#camaasil.n; |LEE 000#camaasil.=;
         |HAZ Actualiza;
         NumApunte = NumApunte + 10;
      |FINSI;
   |FINSI;

   |SI NumApunteAnt ! 0;
      NumApunte = NumApunteAnt;
      NumApunteAnt = 0;
   |FINSI;

   |LIBERA #caivalin;
|FPRC;

|DEFBUCLE CreaLineas;
   #caivalin#1;
   ;
   #camaniva#0,#camaniva#1,01;
   #camaniva#0,#camaniva#1,99;
   be;
   NULL,CreaLineas;
|FIN;

|PROCESO LeoLiquido;
   nNumNuevo = #camaasil#3;
   |ERROR10;
|FPRC;

|DEFBUCLE LeoLiquido;
   #camaasil#1;
   4,6,21,28;
   #6#0, #6#1, #6#2, 0001, #1#12, #1#27, #1#10, #1#11;
   #6#0, #6#1, #6#2, 9999, #1#12, #1#27, #1#10, #1#11;
   e;
   NULL, LeoLiquido;
|FIN;
|PROCESO CreaAsiento;
   |SI nDIARIO ! #0#33; |O aFECHA ! #0#2;
       nDOCUMENTO = -1;
   |FINSI;

   nDIARIO = #0#33;
   aFECHA  = #0#2;
   |SI nDOCUMENTO = -1; |O Desglose = 1;
      |HAZ NuevoDoc;

      |DDEFECTO #camaasic;
      #camaasic#0 = nDIARIO;
      #camaasic#1 = aFECHA;
      #camaasic#2 = nDOCUMENTO;
      #camaasic#3 = nDOCUMENTO;
      #camaasic#12 = nREGISTRO;
      |GRABA 020#camaasic.b;
      NumApunte = 1;
   |FINSI;

   #camaniva#7 = nDIARIO;
   #camaniva#8 = aFECHA;
   #camaniva#9 = nDOCUMENTO;
   |GRABA 020#camaniva.a;
   |LIBERA #camaniva;

   || Creo la linea del Liquido
   |DDEFECTO #camaasil;
   #camaasil#0 = #camaasic#0;
   #camaasil#1 = #camaasic#1;
   #camaasil#2 = #camaasic#2;
   #camaasil#3 = NumApunte;
   #camaasil#4 = #camaniva#12;

   aNOMCUENTA = ""; |SI nCta25 = 1; aNOMCUENTA = #0#26; |FINSI;

   |SI Desglose = 0;
      |SELECT #3#camaasil;
      #camaasil#3 = 1;
      |LEE 101#camaasil.];
      |SI FSalida ! 0; |O #camaasil#4 ! #camaniva#12; |O #camaasil#0 ! #camaasic#0;
           |O #camaasil#1 ! #camaasic#1; |O #camaasil#2 ! #camaasic#2;
             |O #camaasil#21 ! #camaniva#10; |O #camaasil#28 ! #camaniva#11;
               |O #camaasil#6 ! #camaniva#27;

         |CIERRA #camaasil;
         nNumNuevo = 0;
         |BUCLE LeoLiquido;
         |ABRE #camaasil;
         |SI nNumNuevo = 0;
             |SELECT #1#camaasil;
             |DDEFECTO #camaasil;
             #camaasil#0 = #camaasic#0;
             #camaasil#1 = #camaasic#1;
             #camaasil#2 = #camaasic#2;
             #camaasil#3 = NumApunte;
             #camaasil#4 = #camaniva#12;
             |GRABA 020#camaasil.b;
         |SINO;
             |DDEFECTO #camaasil;
             |SELECT #1#camaasil;
             #camaasil#0 = #camaasic#0;
             #camaasil#1 = #camaasic#1;
             #camaasil#2 = #camaasic#2;
             #camaasil#3 = nNumNuevo;
             |LEE 101#camaasil.=;
         |FINSI;
      |FINSI;
   |SINO;
      |GRABA 020#camaasil.b;
   |FINSI;
   NumApunte = NumApunte + 10;
   eaCuenta = #camaniva#12; |HAZ SacaNivel;
   #camaasil#5 = enNivel;
   #camaasil#6 = #camaniva#27;
   #camaasil#7 = #camaniva#28;
   #camaasil#8 = "H";
   #camaasil#10 = #camaasil#4; #camaasil#11 = #camaasil#5;
   #camaasil#16 = "";          #camaasil#17 = 0;
   |SI Desglose = 0;
      #camaasil#9 = -#camaasil#9; |HAZ Actualiza; #camaasil#9 = -#camaasil#9;
      #camaasil#9  = #camaasil#9 + #camaniva#21;
   |SINO;
      #camaasil#9  = #camaniva#21;
   |FINSI;
   #camaasil#12 = aTipoIVA;
   |SI Desglose = 1;
      #camaasil#12 = aTipoIVA;
      #camaasil#13 = #camaniva#0;
      #camaasil#14 = #camaniva#3;
      #camaasil#15 = #camaniva#2;
      #camaasil#19 = "F"; #camaasil#20 = 0;
   |FINSI;
   #camaasil#21 = #camaniva#10;
   #camaasil#22 = 0;
   #camaasil#23 = "N";
   #camaasil#24 = nREGISTRO;
   Comodin = Usuario % 1099;
   #camaasil#27 = Comodin;
   #camaasil#28 = #camaniva#11;

   |SI #camaasil#9 ! 0; |O Desglose = 0;
       |GRABA 020#camaasil.a;
   |SINO;
       |BORRA 020#camaasil.a;
   |FINSI;
   |LIBERA #camaasil;

   |SI Desglose = 0; |SELECT #1#camaasil; |FINSI;

   |HAZ Actualiza;

   #camaasil#0 = #camaasic#0;
   #camaasil#1 = #camaasic#1;
   #camaasil#2 = #camaasic#2;
   #camaasil#3 = 1;
   |LEE 000#camaasil.=;
   |SI FSalida = 0; |Y #camaasil#0 = #camaasic#0; |Y #camaasil#1 = #camaasic#1;
    |Y #camaasil#2 = #camaasic#2;
       |PARA; |SI FSalida = 0; |Y #camaasil#0 = #camaasic#0; |Y #camaasil#1 = #camaasic#1;
               |Y #camaasil#2 = #camaasic#2; |HACIENDO;
          NumApunte = #camaasil#3 + 10;
          |LEE 000#camaasil.s;
       |SIGUE;
   |SINO;
      NumApunte = 1;
   |FINSI;
   aTipo = "B"; |BUCLE CreaLineas;        || Creo las lineas de las bases
   aTipo = "D"; |BUCLE CreaLineas;        || Creo las lineas de los descuentos
   aTipo = "P"; |BUCLE CreaLineas;        || Creo las lineas del IRPF

   |LIBERA #camaasic;
|FPRC;

|PROCESO NuevoCtrl;

  |SI #caplalab#52 ! 0; |Y #caplalab#49 ! 0;
      enLabC0 = #caplalab#52;  || Empresa
      enLabC1 = #caplalab#49;  || Trabajador
      aAlfa1  = #caplalab#3;
      aAlfa1  = aAlfa1 % -102;
      enLabC2 = aAlfa1;
      enLabC3 = #caplalab#53;
      enLabC4 = #caplalab#57;
      eaLabC5 = #caplalab#54;

      |HAZ eCtrlPaseT;
  |FINSI;
|FPRC;


|PROCESO LineaReg;
   |SI aEspecie = "D"; aEspecie = "N"; |FINSI;
   |SI aEspecie = "E"; aEspecie = "S"; |FINSI;

   |DDEFECTO #caivalin;

   #caivalin#0  = #camaniva#0;
   #caivalin#1  = #camaniva#1;
   #caivalin#2  = LinBase;
   #caivalin#5  = "S";
   |SI aTipo = "B";
      #caivalin#3  = Cpto;
      #caivalin#4  = Desc;
      #caivalin#6  = Base;
      #caivalin#28 = aAnaCentro;
   |FINSI;
   |SI aTipo = "P";
      #caivalin#3  = Cpto;
      #caivalin#4  = Desc;
      #caivalin#18  = Cpto;
      #caivalin#19  = Desc;
      #caivalin#20  = Base;
      #caivalin#21  = Porc;
      #caivalin#22  = Cuota;
   |FINSI;
   |SI aTipo = "D";
      #caivalin#3  = Cpto;
      #caivalin#4  = Desc;
      #caivalin#11 = Base;
   |FINSI;

   #caivalin#26  = aEspecie;    || En especie
   #caivalin#27  = aRepercute;  || repercute
   #caivalin#23  = eaCuenta;

   #caivalin#30 = #camaniva#10;
   #caivalin#31 = #camaniva#11;

   |SI #caivalin#6 ! 0; |O #caivalin#20 ! 0; |O #caivalin#11 ! 0;
      |SI aTipo = "B"; #camaniva#19 = #camaniva#19 + #caivalin#6;  |FINSI;
      |SI aTipo = "P"; #camaniva#20 = #camaniva#20 + #caivalin#22; |FINSI;
      |SI aTipo = "D"; #camaniva#26 = #camaniva#26 + #caivalin#11; |FINSI;
      #camaniva#21 = #camaniva#19 + #camaniva#23 - (#camaniva#20 + #camaniva#26);
      |GRABA 020#caivalin.n; LinBase = LinBase + 1;
   |FINSI;
|FPRC;

|PROCESO LineasRet;
   eaCuenta = #caplala2#9; Cpto = #caplala2#2; Desc  = #caplala2#3;
   Base     = #caplala2#6; Porc = #caplala2#7; Cuota = #caplala2#8;
   aEspecie   = #caplala2#4;
   aRepercute = #caplala2#5;

   |SI #210#80 ! " ";
       |SI #210#81 = "S"; |O #210#91 = "S";
           eaConcepto = Desc; |HAZ MontaConcep; Desc = eaConcepto;
       |FINSI;
   |FINSI;
   |SI #210#82 = "S"; |Y #caplalab#49 ! 0;
       |SI #210#83 = "S"; |O #210#100 = "S";
           |HAZ MontaCta;
           nCtaRe = 1;
       |FINSI;
   |FINSI;
   |HAZ LineaReg;
|FPRC;

|DEFBUCLE LineasRet;
 #caplala2#1;
 ;
 #caplalab#0,001;
 #caplalab#0,999;
 ;
 NULL,LineasRet;
|FIN;

|PROCESO EscribeDesglose;
   aAnaCentro = "";
   |SI #caplala3#8 = "H";
       aTipo = "D";
   |SINO;
       aTipo = "B";
   |FINSI;
   eaCuenta = #caplala3#6;

   |SI #210#82 = "S"; |Y #caplalab#49 ! 0;
       |SI #210#83 = "S"; |O #210#93 = "S";
           |HAZ MontaCta; #caplala3#6 = eaCuenta;
       |FINSI;
   |FINSI;

   |SI enSwAna = 1; |Y aTipo = "B"; |HAZ BuscoAna; |FINSI;
   aEspecie   = #caplala3#9;
   aRepercute = #caplala3#10;

   aAlfa1 = #caplalab#31 % 101;
   Cpto   = #caplala3#4;
   Desc   = #caplala3#5; |QBF Desc;
   |SI aAlfa1 ! " ";
       Desc  = Desc + " " + #caplalab#31;
   |SINO;
       Desc  = Desc + #caplalab#31;
   |FINSI;
   |QBF Desc;
   |SI #210#80 ! " ";
       |SI #210#81 = "S"; |O #210#84 = "S";
           eaConcepto = Desc; |HAZ MontaConcep; Desc = eaConcepto;
       |FINSI;
   |FINSI;

   Base  = #caplala3#7;
   |HAZ LineaReg;
   SwEscrita = 1;
|FPRC;

|DEFBUCLE EscribeDesglose;
 #caplala3#1;
 ;
 #caplalab#0,#0#1,001;
 #caplalab#0,#0#1,999;
 ;
 NULL,EscribeDesglose;
|FIN;

|PROCESO MontaCta;
  eaCuenta = eaCuenta % aAlfTro;
  eaCuenta = eaCuenta + aAlfCod;
|FPRC;

|PROCESO TMontaCta;
 |SI #cadeflab#83 = "S"; |O #cadeflab#93 ! "N";
     eaCuenta = #caplalab#9; |HAZ MontaCta; #caplalab#9 = eaCuenta;
     nCta9    = 1;
 |FINSI;
 |SI #cadeflab#83 = "S"; |O #cadeflab#94 ! "N";
     eaCuenta = #caplalab#21; |HAZ MontaCta; #caplalab#21 = eaCuenta;
     nCta21    = 1;
 |FINSI;
 |SI #cadeflab#83 = "S"; |O #cadeflab#95 ! "N";
     eaCuenta = #caplalab#44; |HAZ MontaCta; #caplalab#44 = eaCuenta;
     nCta44    = 1;
 |FINSI;
 |SI #cadeflab#83 = "S"; |O #cadeflab#96 ! "N";
     eaCuenta = #caplalab#48; |HAZ MontaCta; #caplalab#48 = eaCuenta;
     nCta48    = 1;
 |FINSI;
 |SI #cadeflab#83 = "S"; |O #cadeflab#97 ! "N";
     eaCuenta = #caplalab#13; |HAZ MontaCta; #caplalab#13 = eaCuenta;
     nCta13    = 1;
 |FINSI;
 |SI #cadeflab#83 = "S"; |O #cadeflab#98 ! "N";
     eaCuenta = #caplalab#37; |HAZ MontaCta; #caplalab#37 = eaCuenta;
     nCta37   = 1;
 |FINSI;
 |SI #cadeflab#83 = "S"; |O #cadeflab#99 ! "N";
     eaCuenta = #caplalab#17; |HAZ MontaCta; #caplalab#17 = eaCuenta;
     nCta17    = 1;
 |FINSI;

 || ... Faltan cuentas de IRPF

 |SI #cadeflab#83 = "S"; |O #cadeflab#101 ! "N";
     eaCuenta = #caplalab#25; |HAZ MontaCta; #caplalab#25 = eaCuenta;
     nCta25 = 1;
 |FINSI;
|FPRC;

|PROCESO CamCtas;
 aAlfCod = #caplalab#49;
 nNume2 = 4; nNume3 = 12;
 nNume1 = dig3 - 1;
 |SI nNume1 = 1; nNume2 = dig4; nNume3 = dig5; |FINSI;
 |SI nNume1 = 2; nNume2 = dig5; nNume3 = dig6; |FINSI;
 |SI nNume1 = 3; nNume2 = dig6; nNume3 = dig7; |FINSI;
 |SI nNume1 = 4; nNume2 = dig7; nNume3 = dig8; |FINSI;
 |SI nNume1 = 5; nNume2 = dig8; nNume3 = dig9; |FINSI;

 nNume3 = nNume3 - nNume2; aAlfa2 = "0" * nNume3;
 nNume3 = 100 + nNume3; nNume3 = - nNume3;
 aAlfa3 = nNume3;
 aAlfCod = (aAlfa2 + aAlfCod) % aAlfa3;

 nNume2 = 100+ nNume2;  aAlfTro = nNume2;
 |HAZ TMontaCta;
|FPRC;

|PROCESO CamCtasDni;

 aAlfCod = #caplalab#1; |QBF aAlfCod;
 aAlfa   = aAlfCod % 0;
 nNume1  = aAlfa;
 aAlfa1  = aAlfCod % 101;
 |SI aAlfa1 < "0"; |O aAlfa1 > "9"; aAlfa1 = "0"; |FINSI;
 |PARA nPara1 = 2; |SI nPara1 [ nNume1; |HACIENDO nPara1 = nPara1 + 1;
       nNume2 = (nPara1 * 100) + 01;
       aAlfa2 = aAlfCod % nNume2;
       |SI aAlfa2 ] "0"; |Y aAlfa2 [ "9";
           aAlfa1 = aAlfa1 + aAlfa2;
       |FINSI;
 |SIGUE;
 aAlfCod = aAlfa1;

 nNume2 = 4; nNume3 = 12;
 nNume1 = dig3 - 1;
 |SI nNume1 = 1; nNume2 = dig4; nNume3 = dig5; |FINSI;
 |SI nNume1 = 2; nNume2 = dig5; nNume3 = dig6; |FINSI;
 |SI nNume1 = 3; nNume2 = dig6; nNume3 = dig7; |FINSI;
 |SI nNume1 = 4; nNume2 = dig7; nNume3 = dig8; |FINSI;
 |SI nNume1 = 5; nNume2 = dig8; nNume3 = dig9; |FINSI;

 nNume3 = nNume3 - nNume2; aAlfa2 = "0" * nNume3;
 nNume3 = 100 + nNume3; nNume3 = - nNume3;
 aAlfa3 = nNume3;
 aAlfCod = (aAlfa2 + aAlfCod) % aAlfa3;

 nNume2 = 100+ nNume2;  aAlfTro = nNume2;
 |HAZ TMontaCta;
|FPRC;

|PROCESO MontaConcep;
  |QBF eaConcepto;
  eaConcepto = eaConcepto + " " + aAlfCon;
|FPRC;

|PROCESO CamConcep;
 aAlfCon = "";
 |SI #210#80 = "N"; aAlfCon = #caplalab#26; |FINSI;
 |SI #210#80 = "C"; |Y #caplalab#49 ! 0; aAlfCon = #caplalab#49; |FINSI;
 |SI #210#80 = "D"; aAlfCon = #caplalab#1; |FINSI;
 |QBF aAlfCon;
 |SI aAlfCon = ""; |ACABA; |FINSI;

 |SI #cadeflab#81 = "S"; |O #cadeflab#84 = "S";
     eaConcepto = #caplalab#8; |HAZ MontaConcep; #caplalab#8 = eaConcepto;
 |FINSI;

 |SI #cadeflab#81 = "S"; |O #cadeflab#85 = "S";
     eaConcepto = #caplalab#20; |HAZ MontaConcep; #caplalab#20 = eaConcepto;
 |FINSI;

 |SI #cadeflab#81 = "S"; |O #cadeflab#86 = "S";
     eaConcepto = #caplalab#43; |HAZ MontaConcep; #caplalab#43 = eaConcepto;
 |FINSI;

 |SI #cadeflab#81 = "S"; |O #cadeflab#87 = "S";
     eaConcepto = #caplalab#47; |HAZ MontaConcep; #caplalab#47 = eaConcepto;
 |FINSI;

 |SI #cadeflab#81 = "S"; |O #cadeflab#88 = "S";
     eaConcepto = #caplalab#12; |HAZ MontaConcep; #caplalab#12 = eaConcepto;
 |FINSI;

 |SI #cadeflab#81 = "S"; |O #cadeflab#89 = "S";
     eaConcepto = #caplalab#36; |HAZ MontaConcep; #caplalab#36 = eaConcepto;
 |FINSI;

 |SI #cadeflab#81 = "S"; |O #cadeflab#90 = "S";
     eaConcepto = #caplalab#16; |HAZ MontaConcep; #caplalab#16 = eaConcepto;
 |FINSI;

 || ... Faltan cuentas de IRPF
 |SI #cadeflab#81 = "S"; |O #cadeflab#92 = "S";
     eaConcepto = #caplalab#24; |HAZ MontaConcep; #caplalab#24 = eaConcepto;
 |FINSI;

|FPRC;

|PROCESO Volcado;
   |SI aParam ! "";
       |SI nSwPrimer = 0;
           enConcepto = 0;
           eaFecMod   = #caplalab#2;
           enPanMod   = 1;   || Sin aviso de pantalla
           |HAZ ConsulPlan;
           eaExiMod = "SI";
           |SI enExiMod = 0;
               eaIncide = "No existe Modelo 110 en Planing ";
               eaExiMod = "NO";
               |SI snSeMod = "N";
                   eaIncide = "No pasa Nominas.No existe Mod.110 Planing";
                   |ERROR10;
                   |LIBERA #caplalab;
                   |ACABA;
               |FINSI;
               |SI snFiMod = "N"; eaIncide = ""; |FINSI;
           |FINSI;
       |FINSI;
       nSwPrimer = 1;
   |FINSI;

   |SI #0#2 [ fec3;
       eaIncide = "Empresa con Fecha Bloqueada";
       |ACABA;
   |FINSI;

   |SELECT #4#3;
   #3#9 = #0#1;
   |LEE 010#3.=;
   |SELECT #1#3;

   nLibro = #caplalab#32;
   #camaniva#0 = nLibro;
   #camaniva#1 = 9999999999;
   |LEE 010#camaniva.];
   |SI FSalida = 0;
       |LEE 040#camaniva.a;
   |SINO;
       |LEE 040#camaniva.u;
   |FINSI;

   NumRegistro = 1;
   |SI FSalida = 0; |Y #camaniva#0 = nLibro;
       NumRegistro = #camaniva#1 + 1;
   |FINSI;

   nCta9  = 0;
   nCta21 = 0;
   nCta44 = 0;
   nCta48 = 0;
   nCta37 = 0;
   nCta13 = 0;
   nCta17 = 0;
   nCta25 = 0;
   nCtaRe = 0;
   |SI #210#82 = "S"; |Y #caplalab#49 ! 0;    |HAZ CamCtas;    |FINSI;
   |SI #210#82 = "D"; |Y #caplalab#1  > doce; |HAZ CamCtasDni; |FINSI;

   |SI #210#80 ! " ";
       |HAZ CamConcep;
   |FINSI;

   |DDEFECTO #camaniva;
   #camaniva#0  = #caplalab#32;
   #camaniva#1  = NumRegistro;
   #camaniva#4  = #caplalab#2;
   Comodin = #camaniva#4 % 0402; |QBF Comodin;
   #camaniva#5 = Comodin;                            || Periodicidad
   |SI enEjercicio [ 2009;
       |SI #200#149 = "T";
           |SI Comodin ] "01"; |Y Comodin [ "03"; #camaniva#5 = "01"; |FINSI;
           |SI Comodin ] "04"; |Y Comodin [ "06"; #camaniva#5 = "02"; |FINSI;
           |SI Comodin ] "07"; |Y Comodin [ "09"; #camaniva#5 = "03"; |FINSI;
           |SI Comodin ] "10"; |Y Comodin [ "12"; #camaniva#5 = "04"; |FINSI;
       |SINO;
           #camaniva#5 = Comodin;                            || Periodicidad
       |FINSI;
   |FINSI;
   #camaniva#10 = #caplalab#27;
   #camaniva#11 = #caplalab#29;
   #camaniva#12 = #caplalab#25;
   #camaniva#13 = #caplalab#26;
   #camaniva#14 = #0#1;
   #camaniva#24 = #caplalab#28;
   #camaniva#25 = #caplalab#30;
   #camaniva#27 = #caplalab#23;
   #camaniva#28 = #caplalab#24;
   #camaniva#35 = "N";
   #camaniva#36 = "N";
   #camaniva#46 = #caplalab#55;
   #camaniva#47 = #caplalab#56;

   |GRABA 020#camaniva.b;

   || Escribo la linea del Total Devengado
   aRepercute = "N";

   SwEscrita = 0;
   LinBase = 1;
   aAnaCentro = "";

   |SI #210#42 = "C";
       enCentro = #caplalab#40;
       |HAZ CtaAnaCen1;
       #caplalab#9 = aCtaCentro;
       nCta9 = 0;
   |FINSI;
   |SI #210#42 = "A";
       eaArea    = #caplalab#50;
       eaSeccion = #caplalab#51;
       |HAZ CtaAnaCen2;
       #caplalab#9 = aCtaAreaSec;
       nCta9 = 0;
   |FINSI;
   |SI #210#42 = "S"; |BUCLE EscribeDesglose; |FINSI;

   |SI SwEscrita = 0;
       aTipo = "B";         eaCuenta = #caplalab#9;
       Cpto  = #caplalab#7; Desc     = #caplalab#8;
       Base  = #caplalab#6;
       eaCuenta = #caplalab#9;
       aEspecie = "N";

       |SI enSwAna = 1; |HAZ BuscoAna; |FINSI;

       |HAZ LineaReg;
   |FINSI;

   aEspecie   = "N";
   aRepercute = "N";
   aAnaCentro = "";
   || Escribo la linea de aportaciones S.S.
   aTipo    = "D";
   eaCuenta  = #0#13;
   |SI #210#106 = "C";
       enCentro = #0#40;
       |HAZ CtaAnaCenY;
       #0#13 = aCtaCentroY;
   |FINSI;
   |SI #210#106 = "A";
       eaArea    = #0#50;
       eaSeccion = #0#51;
       |HAZ CtaAnaCenZ;
       #0#13 = aCtaCentroZ;
   |FINSI;
   aAlfa1 = #0#13; |QBF aAlfa1; |SI aAlfa1 = ""; #0#13 = eaCuenta; |FINSI;

   eaCuenta = #0#13;
   Cpto     = #0#11;
   Desc     = #0#12;
   Base     = #0#10;
   |HAZ LineaReg;

   || Escribo la linea de anticipos
   eaCuenta = #0#17;
   Cpto     = #0#15;
   Desc     = #0#16;
   Base     = #0#14;
   |HAZ LineaReg;

   || Escribo la linea de otras deducciones
   eaCuenta = #caplalab#37;
   Cpto     = #0#35;
   Desc     = #0#36;
   Base     = #0#34;
   |HAZ LineaReg;

   || Escribo la linea de total de productos recibidos en especie
   aEspecie   = "S";
   |SI #0#18 ! 0;
      eaCuenta   = #0#21;
      Cpto       = #0#19;
      Desc       = #0#20;
      Base       = #0#18;
      aRepercute = #cadeflab#75;
      |HAZ LineaReg;
   |FINSI;
   |SI #0#41 ! 0;
      eaCuenta = #0#44;
      Cpto     = #0#42;
      Desc     = #0#43;
      Base     = #0#41;
      aRepercute = #cadeflab#76;
      |HAZ LineaReg;
   |FINSI;
   |SI #0#45 ! 0;
      eaCuenta = #0#48;
      Cpto     = #0#46;
      Desc     = #0#47;
      Base     = #0#45;
      aRepercute = #cadeflab#77;
      |HAZ LineaReg;
   |FINSI;
   aEspecie   = "N";

   || Escribo las bases de irpf

   aTipo = "P";
   |BUCLE LineasRet;
   |SI LinBase = 1;  || no existe linea de factura;
       |BORRA 040#camaniva.a;
       |LIBERA #camaniva;
   |SINO;
       |HAZ CreaAsiento;
       |SI HayLaboral = 1; |HAZ NuevoCtrl; |FINSI;
   |FINSI;

   #caplala1#0 = #0#0;
   |LEE 101#caplala1.=;
   |SI FSalida = 0;
       |BORRA 020#caplala1.a;
       |LIBERA #caplala1;
  |FINSI;

  |BUCLE Borra02;
  |BUCLE Borra03;
  |BORRA 020#caplalab.a;
  |LIBERA #caplalab;
  enSwPasa = 1;
|FPRC;

|DEFBUCLE Volcado;
 #0#1;
 ;
 INICIO;
 FINAL;
 be;
 NULL,Volcado;
|FIN;

|PROCESO VolcadoCentro;

  |SI nSwCentro ! #0#40;
      nSwCentro = #0#40;
      nSinEste = 0;
      |ABRE #230;
      #230#0 = #0#40;
      |LEE 001#230=;
      |SI FSalida = 0;
          nSinEste = 1;
          |PARA nNume1 = 1; |SI nNume1 [ 107; |HACIENDO nNume1 = nNume1 + 1;
                |SI nNume1 ! 78; |Y nNume1 ! 79;
                    #210nNume1 = #230nNume1;
                |FINSI;
          |SIGUE;
          #210#60 = #230#0;
      |SINO;
          |ABRE #210;
          |LEE 001#210p;
          |CIERRA #210;
      |FINSI;
  |FINSI;

  |HAZ Volcado;
|FPRC;

|DEFBUCLE VolcadoCentro;
 #0#2;
 ;
 00, 0001;
 99, 9999;
 be;
 NULL,VolcadoCentro;
|FIN;

|| ///////////////////////////////////////////////////
|PROCESO BorroLoPasado;
  #caplala1#0 = #0#0;
  |LEE 101#caplala1.=;
  |SI FSalida = 0;
       |BORRA 020#caplala1.a;
       |LIBERA #caplala1;
  |FINSI;
  |BUCLE Borra02;
  |BUCLE Borra03;
  |BORRA 020#caplalab.a;
  |LIBERA #caplalab;
|FPRC;

|DEFBUCLE BorroLoPasado;
 #0#1;
 ;
 INICIO;
 FINAL;
 be;
 NULL,BorroLoPasado;
|FIN;
|| ///////////////////////////////////////////////////
|PROCESO BorraCab;
   #camaasic#0 = nDIARIO;
   #camaasic#1 = aFECHA;
   #camaasic#2 = nDOCUMENTO;
   |LEE 001#camaasic.=;
   |SI FSalida = 0;
       |BORRA 020#camaasic.a;
   |FINSI;
|FPRC;

|PROCESO BorraCeros;
   |SI #camaasil#9 = 0;
       |BORRA 020#camaasil.a;
   |SINO;
       nSwHay = 1;
   |FINSI;
   |LIBERA #camaasil;
|FPRC;

|DEFBUCLE BorraCeros;
 #camaasil#1;
 ;
 nDIARIO,aFECHA,nDOCUMENTO,0001;
 nDIARIO,aFECHA,nDOCUMENTO,9999;
 be;
 NULL,BorraCeros;
|FIN;
|| /////////////////////////////

|PROCESO MiraLineas;
   SwError = 1;
|FPRC;

|DEFBUCLE MiraLineas;
 #0#1;
 ;
 INICIO;
 FINAL;
 ;
 NULL,MiraLineas;
|FIN;

||**************************************************************************
|RUTINA inf;
   #0#0 = 0001;
|FRUT;

|RUTINA sup;
   #0#0 = 9999;
|FRUT;

|PROGRAMA;

   |HAZ ePatronM037;

   |ABRE #dsmom030;
   #dsmom030#0 = 463;
   |LEE 041#dsmom030.=;
   |SI FSalida = 0; #dsmom030#42 = "A"; |Y #dsmom030#43 = "02";
       aAlfa30 = #dsmom030#1;
       nsw30   = 1;
   |FINSI;

   aParam = PARAMETRO; |QBF aParam;
   |SI aParam ! "";
       aParam = aParam % 101;
       |QBF aParam;
   |FINSI;
   enSiBloq = 1;
   |SI aParam = "";
      enSiBloq = 0;
      |CLS;
      |CABEZA "Entrada datos laboral";
   |FINSI;

   |HAZ inicial; |SI swerror = 2; |ACABA; |FINSI;
   |HAZ DirAplicSt;
   aPath = aPModelo + "fich/";
   |PATH_DAT #107 aPath;

   |HAZ Analitica; || Miro si es analitica

   |SI aParam = "";
       |SI snFiMod = "S";
           enPerMod = -1;
           enPanMod = 0;
           |HAZ ConsulPlan;
           |SI enExiMod = 0; |Y snSeMod = "N";
               |ABRE #0;
               |LEE 041#0p;
               |SI FSalida ! 0;
                   |CIERRA #0;
                   |ACABA;
               |FINSI;
               |CIERRA #0;
           |FINSI;
       |FINSI;
   |FINSI;

   |ABRE #3;
   |SI aParam = "";
       |PINPA #0#0;
       |ENTLINEAL #1#0, -1, 1, 22, 0, inf, sup;
       SwError = 0;
       |BUCLE MiraLineas;
   |SINO;
        SwError = 1;
   |FINSI;

   |ABRE #0;
   |ABRE #4;
   |ABRE #5;
   |ABRE #10;

   |SI aParam = "";
       |SI SwError = 1;
           |CONFI "    S¨Desea hacer el volcado de los registros?";
            FEstado = FSalida;
       |FINSI;
   |SINO;
        FEstado = 0;
   |FINSI;

   |HAZ LeeDefLab;

   |SI FEstado = 0; |Y SwError = 1;
       |ABRE #camaniva;
       |ABRE #caivalin;
       |ABRE #camaasic;
       |ABRE #camaasil;
       nDOCUMENTO = -1;
       nDIARIO    = -1;
       aFECHA     = "";

       aAlfa1 = #210#78;
       aAlfa2 = "    "+ aAlfa1 + "¨Desea generar un Solo Asiento?";

       |SI aParam = ""; |Y #210#79 = "S";
           |CONFI aAlfa2;
           nInkey = FSalida;
       |SINO;
           |SI #210#78 = "S";
               nInkey = 0;
           |SINO;
               nInkey = 1;
           |FINSI;
       |FINSI;

       FSalida = nInkey;

       |SI FSalida = 0;
           Desglose = 0;  || Un solo asiento
       |SINO;
           Desglose = 1;  || Asiento por cada nomina
       |FINSI;

       enSwPasa  = 0;
       eaIncide  = "";
       eaExiMod  = "";
       nSwPrimer = 0;
       nSinEste  = 0;
       |SI enSinCentro = 0;
           |BUCLE Volcado;
       |SINO;
           nSwCentro = -1;
           |BUCLE VolcadoCentro;
       |FINSI;
       |SI enSwPasa = 0; |Y eaIncide = "";
           eaIncide = "No existe Nominas para contabilizar";
       |FINSI;
       |SI enSwPasa = 1; |Y eaIncide = "";
           eaIncide = "Realizado el Asiento de Nominas";
       |FINSI;
       |SI enExiMod = 0; |Y snSeMod = "N"; |Y aParam ! "";
         ||  |BUCLE BorroLoPasado;
       |FINSI;

       |CIERRA #camaasil;
       |SI Desglose = 0;
           nSwHay = 0;
           |BUCLE BorraCeros;
           |SI nSwHay = 0;
               |HAZ BorraCab;
           |FINSI;
       |FINSI;
       |CIERRA #camaasic;
       |CIERRA #caivalin;
       |CIERRA #camaniva;
   |FINSI;

   |CIERRA #10;
   |CIERRA #5;
   |CIERRA #4;
   |CIERRA #0;
   |CIERRA #3;
   fich_alta = "";
|FPRO;

|| ************** Procesos de Analitica
|PROCESO BuscoAna;
 aAnaCentro = "";
 |SI #210#42 = "C";
     enCentro = #0#40;
     |HAZ CtaAnaCen;
 |SINO;
     |SI #210#42 = "A";
         eaArea    = #0#50;
         eaSeccion = #0#51;
         |HAZ CtaAnaCen0;
     |SINO;
         |SI #210#42 = "N"; |Y nSinEste = 1;
             enCentro = #0#40;
             |HAZ CtaAnaCen;
         |SINO;
             |HAZ LeeTabla;
         |FINSI;
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO CtaAnaCen;
      |ABRE #211;
      |DDEFECTO #211;
      #211#0 = enCentro;
      |LEE 001#211=;
      |SI FSalida = 0;
          |SI nSinEste = 0; aCtaCentro = #211#2; |FINSI;
          aAnaCentro = #211#4;
      |SINO;
          aCtaCentro = eaCuenta;
          |HAZ LeeTabla;
      |FINSI;
      |CIERRA #211;
|FPRC;

|PROCESO CtaAnaCenY;
      aCtaCentroY = "";
      |ABRE #213;
      |DDEFECTO #213;
      #213#0 = enCentro;
      |LEE 001#213=;
      |SI FSalida = 0;
          aCtaCentroY = #213#2;
      |FINSI;
      |CIERRA #213;
|FPRC;

|PROCESO CtaAnaCenZ;
  |SI nSinEste = 0;
      aCtaCentroZ = "";
      |ABRE #214;
      |DDEFECTO #214;
      #214#0 = eaArea;
      #214#1 = eaSeccion;
      |LEE 001#214=;
      |SI FSalida = 0;
          aCtaCentroZ = #214#3;
      |FINSI;
      |CIERRA #214;
  |SINO;
      aCtaCentroZ = "";
      |ABRE #234;
      |DDEFECTO #234;
      #234#0  = eaArea;
      #234#1  = eaSeccion;
      #234#10 = nSwCentro;
      |LEE 001#234=;
      |SI FSalida = 0;
          aCtaCentroZ = #234#3;
      |FINSI;
      |CIERRA #234;
  |FINSI;
|FPRC;

|PROCESO CtaAnaCen0;
   |SI nSinEste = 0;
      |ABRE #212;
      |DDEFECTO #212;
      #212#0 = eaArea;
      #212#1 = eaSeccion;
      |LEE 001#212=;
      |SI FSalida = 0;
          aCtaAreaSec = #212#3;
          aCtaCentro  = #212#3;
          aAnaCentro  = #212#5;
      |SINO;
          aCtaAreaSec = eaCuenta;
          aCtaCentro  = eaCuenta;
          |HAZ LeeTabla;
      |FINSI;
      |CIERRA #212;
   |SINO;
      |ABRE #232;
      |DDEFECTO #232;
      #232#0 = eaArea;
      #232#1 = eaSeccion;
      #232#6 = nSwCentro;
      |LEE 001#232=;
      |SI FSalida = 0;
          aCtaAreaSec = #232#3;
          aCtaCentro  = #232#3;
          aAnaCentro  = #232#5;
      |SINO;
          aCtaAreaSec = eaCuenta;
          aCtaCentro  = eaCuenta;
          |HAZ LeeTabla;
      |FINSI;
      |CIERRA #232;
   |FINSI;
|FPRC;

|PROCESO LeeTabla;
      |ABRE #207;
      |DDEFECTO #207;
      #207#0 = aCtaCentro;
      |LEE 001#207=;
      |SI FSalida = 0;
           aAnaCentro = #207#3;
      |FINSI;
      |CIERRA #207;
|FPRC;

|PROCESO CtaAnaCen1;
      |ABRE #211;
      |DDEFECTO #211;
      #211#0 = enCentro;
      |LEE 001#211=;
      |SI FSalida = 0;
          aCtaCentro = #211#2;
      |SINO;
          aCtaCentro = #0#9;
      |FINSI;
      |CIERRA #211;
|FPRC;

|PROCESO CtaAnaCen2;
   |SI nSinEste = 0;
      |ABRE #212;
      |DDEFECTO #212;
      #212#0 = eaArea;
      #212#1 = eaSeccion;
      |LEE 001#212=;
      |SI FSalida = 0;
          aCtaAreaSec = #212#3;
      |SINO;
          aCtaAreaSec = #0#9;
      |FINSI;
      |CIERRA #212;
   |SINO;
      |ABRE #232;
      |DDEFECTO #232;
      #232#0 = eaArea;
      #232#1 = eaSeccion;
      #232#6 = nSwCentro;
      |LEE 001#232=;
      |SI FSalida = 0;
          aCtaAreaSec = #232#3;
      |SINO;
          aCtaAreaSec = #0#9;
      |FINSI;
      |CIERRA #232;
   |FINSI;
|FPRC;
