|FICHEROS;
   cafinanc #0,MANTE;
   cafinanl #1,MANTE;
   cacuenta #2;
   cafinant #9;
   caz00009 #10;

   :/modelos/def/dsmom042 #42;
   :/modelos/def/dsmom041 #41;
   :/modelos/def/dsmom040 #40;

   activid@ #960;
|FIN;

|VARIABLES;
   &entrada  = 1;
   alfa1     = "";
   alfa2     = "";
   alfa3     = "";
   alfa4     = "";
   nNume     = 0;
   fFecha    = @;
   nume1     = 0;
   nume2     = 0;
   nume3     = 0;
   nume4     = 0;
   nInkey     = 0;
   esalta    = 0;
   esaltaL   = 0;
   nSwSal    = 0;
   nSwRecal  = 0;
   &sw009    = 1;
   nTipIVA   = 0;
   nImpPend  = 0;
   nImpCanvi = 0;

  &aLaMonEmp = "";
  &aMonMod   = "";

  aElem       = "";
  nElem       = 0;
  nEstado     = 0;
  nSwError    = 0;

   &eaCam2     = "";
   &eaElemento = "";
   &enElemeNum = 0;
   &eaCam4     = "";
   &eaCam5     = "";
   &eaCam8     = "";
   &enCam10    = 0;
   &efCam27    = @;
   &eaCam33    = "";
   &eaCtana    = "";
   &enOperFin  = 0;
   &eaDElem    = "";
   &enDNEle    = "";
   &enDeAqui   = 0;
   &snFilBa    = "";
   &enCam43    = 0;
   &enCam46    = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoan_i;
|INCLUYE numamo_i;

**************************************************************************
|RUTINA ClaveBajaL;
     #1#0  = #0#0;
     #1#1  = #0#1;
     #1#2  = 001;
|FRUT;

|RUTINA ClaveAltaL;
     #1#0  = #0#0;
     #1#1  = #0#1;
     #1#2  = 999;
|FRUT;

**************************************************************************
 LLAMADAS DESDE Cabecera
**************************************************************************
|CALCULO AntesAna; |TIPO 7;
 esalta = (FEntrada / 100) ! 0;
 |SI enSwAna = 1;
     |SI Campo = 47; nume1 = 35; |FINSI;
     |SI Campo > 47; |Y Campo < 52; nume1 = Campo - 41; |FINSI;
     |SI Campo = 52; nume1 = 41; |FINSI;
     |SI Campo = 53; nume1 = 11; |FINSI;
     eaCuenta = #0nume1;
     |HAZ LeeTablaAna;
     |SI swRelac = 0;
         |SI esalta = 1; #0Campo = eaCtaAna; |FINSI;
         |C_M #0Campo,1,eaAnaMod;
     |SINO;
         |SI esalta = 1; #0Campo = ""; |FINSI;
         |SI eaPAna = "S"; |Y #0Campo = "       ";
             |C_M #0Campo,1,"N";
         |SINO;
             |C_M #0Campo,1,"S";
         |FINSI;
     |FINSI;
     |PINTA #0Campo;
     eaCtaAna = #0Campo;
     |HAZ eLimCtaAna7;
     |SI enBuenaAna ! 0; |C_M #0Campo,1,"S"; |FINSI;
 |FINSI;
|FCAL;

|CALCULO anacta; |TIPO 0;
   nInkey = FSalida;
   |SI nInkey = 2;  |ACABA; |FINSI;
   |SI enSwAna = 0; |ACABA; |FINSI;

   salidas = FSalida; |HAZ FPuntosAna;

   eOpAna = 0; || 1 = Consulta pantalla
   |SI nInkey = 10; eOpAna = 1;  |FINSI;
   eaCtaAna = #0Campo;
   |HAZ LeeCtaAna;
   |SI eswLect = 1;
        |MENAV "    Error.! No Existe Cuenta Analitica";
        |ERROR;
        |ACABA;
   |SINO;
       #0Campo = eaCtaAna;
       |PINTA #0Campo;
  |FINSI;

|| ...  Comprobar validez de la cuenta analitica segun limites definidos
   |HAZ eLimCtaAna;
   |SI enBuenaAna ! 0;
       |MENAV eaBuenaAna;
       |ERROR;
  |FINSI;
|| ---------------------------------------------------------
|FCAL;

|PROCESO BorraLineas;
 |BORRA 140#1a;
 |LIBERA #1;
|FPRC;

|DEFBUCLE cafinanl;
  #1#1;
  ;
  #0#0, #0#1, 000;
  #0#0, #0#1, 999;
  be;
  NULL, BorraLineas;
|FIN;

|CALCULO laMoneda; |TIPO 13;
|FCAL;

|CALCULO Tipo2_1;  |TIPO 2;
 esalta  = (FEntrada / 100) ! 0;
 |SI esalta ! 3;  |ACABA;  |FINSI;

 |BUCLE cafinanl;
 |SI Haymodelo = 1; |Y #0#44 > "        ";
     eaElemento = #0#44;
     enElemeNum = #0#45;
     |HAZ BajaElemento;
 |FINSI;
|FCAL;

|CALCULO elTipo3; |TIPO 3;
    |HAZ laMoneda;
    esalta  = (FEntrada / 100) ! 0;
    #0#0  = #10#1;
    #0#32 = #10#2;
    enEmpresa = #0#0;
    nSwSal   = 0;
    nSwRecal = 0;
    |ENTLINEAL #1#1, -3, 2, 22, 0, ClaveBajaL, ClaveAltaL;
    |SI nSwRecal = 1;
        |HAZ Recalcular;
        nSwRecal = 0;
    |FINSI;
|FCAL;

|CALCULO PresentaLinea;  |TIPO 7;
    |HAZ LeeTipo;
    enEmpresa = #0#0;
    nSwSal = 1;
    |ENTLINEAL #1#1, -3, 7, 22, 1, ClaveBajaL, ClaveAltaL;
|FCAL;

|CALCULO AntesDepar; |TIPO 7;
   |SI eaDepar = "N"; |ACABA; |FINSI;
   |SI Haybasica = 1;
       |MENSA "    <F2> Consultar Actividades";
   |SINO;
       |MENSA "    <F2> Consultar Departamentos";
   |FINSI;
|FCAL;

|CALCULO TrasDepar; |TIPO 0;
   |SI eaDepar = "N"; |ACABA; |FINSI;
   eOpDep = 0; || 1 = Consulta pantalla
   |SI FSalida = 10; eOpDep = 1;  |FINSI;
   enEmpresa   = #0#0;
   enActividad = #0#33;
   eaCodDep    = #0#33;
   |HAZ Actividad;
   |SI eswLect = 1;
       |MENAV "    Error.! No Existe Actividad / Departamento";
       |ERROR;
   |SINO;
       #0#33 = eaCodDep;    |PINTA #0#10;
       #0#34 = eaNombreAct; |PINTA #0#34;
       |SI efActFin > "01.01.1900"; |Y snFilBa = "S";
           alfa1 = "    NAtencion!!!. Actividad con Fecha de BAJA " + efActFin + ", Desea introducir Facturas ? ";
           |CONFI alfa1;
           |SI FSalida ! 0;
               |ERROR;
               |ACABA;
           |FINSI;
       |FINSI;
       |SI eaEsCom = "S";
           |CONFI "    NAtencion!!!. Actividad Comunera, ¨ Desea introducir Movimientos ? ";
           |SI FSalida ! 0;
               |ERROR;
               |ACABA;
           |FINSI;
       |FINSI;
   |FINSI;
|FCAL;

|CALCULO LeeTipo; |TIPO 0;
 |ABRE #9;
 alfa1 = " " * 27;
 #9#0 = #0#3;
 |LEE 001#9=;
 |SI FSalida = 0;
     alfa1 = #9#1 % 127;
 |FINSI;
 |ATRI P; |PINTA 0652, alfa1; |ATRI 0;
 |CIERRA #9;
|FCAL;

|CALCULO elTipo0; |TIPO 0;
 nInkey = FSalida;
 esalta = (FEntrada / 100) ! 0;
 |SI esalta = 1; #0#40 = aMonMod; |FINSI;

 |SI nInkey = 11;  |HAZ VerElemento; |ERROR; |FINSI;
|FCAL;

|CALCULO elTipo11; |TIPO 11;
 nInkey = FSalida;
 |SI nInkey = 11;  |HAZ VerElemento; |ERROR; |FINSI;
|FCAL;


|CALCULO FecFPago; |TIPO 7;
 |SI #0Campo [ "01.01.1900";
     nNume = 0 + (#0#6/1000);
     fFecha = #0#5 + nNume;
     #0Campo = fFecha; |PINTA #0Campo;
 |FINSI;
|FCAL;

|CALCULO cuenta1; |TIPO 6;
   emCta = 1;        || Dar mensajes de error

   eaCuenta = #0Campo;
   salidas  = FSalida;
   |HAZ FiltraPuntos;
   #0Campo = eaCuenta;
   |PINTA #0Campo;

   |HAZ SacaNivel;
   FSalida = salidas;
|FCAL;

|CALCULO refresca1; |TIPO 0;
   emCta = 1;        || Dar mensajes de error
   eaCuenta = #0Campo;
   |HAZ LeeCuenta;
   |SI eswCta = 0;
       nume1 = Campo + 5;
       |SI Campo = 35; nume1 = 36; |FINSI;
       |SI Campo = 41; nume1 = 42; |FINSI;
       #0nume1 = #cacuenta#2; |PINTA #0nume1;
   |FINSI;
|FCAL;

|CALCULO fecactiva; |TIPO 7;
 alfa1 = "S";
 |SI #0#54 = "A";
     alfa1 = "N";
     #0#55 = "01.01.0000";
 |SINO;
     |SI #0#55 < "01.01.1900";
         #0#55 = ~;
     |FINSI;
 |FINSI;
 |PINTA #0#55;
 |C_M #0#55,1,alfa1;
|FCAL;

|CALCULO fecactiva0; |TIPO 0;
 |SI #0#54 = "I";
     |SI #0#55 < "01.01.1900";
         |MENAV "    Introduzca fecha a partir de la que la operacion esta inactiva";
         |ERROR;
     |FINSI;
 |FINSI;
|FCAL;
|| *******************************************************************
|PROCESO elRecalculo0;
 #1#6  = #1#4 + #1#5;
 #1#8  = (#1#6 % #1#7) ! EURODB;
 #1#9  = #1#6 + #1#8;
 #0#39 = #0#39 + #1#4;
 #1#10 = #0#24 - #0#39;
 |GRABA 020#1a;
 |LIBERA #1;
|FPRC;

|DEFBUCLE elRecalculo;
  #1#1;
  ;
  #0#0, #0#1, 000;
  #0#0, #0#1, 999;
  be;
  NULL, elRecalculo0;
|FIN;

|PROCESO Recalcular;
  #0#39 = 0;
  |BUCLE elRecalculo;
|FPRC;

|CALCULO Recalculo; |TIPO 0;

 esalta = (FEntrada / 100) ! 0;
 |SI esalta = 1; #0#40 = aMonMod; |FINSI;

 nInkey = FSalida;
 |SI nInkey = 10;
     |HAZ Recalcular;
     |ERROR;
 |FINSI;
 |SI nInkey = 11;
     |HAZ VerElemento;
     |ERROR;
 |FINSI;
|FCAL;


|CALCULO ElElem7; |TIPO 7;
 |SI Haymodelo = 0;
     |C_M #0#44,1,"N"; #0#44 = ""; |PINTA #0#44;
     |C_M #0#45,1,"N"; #0#45 = 0;  |PINTA #0#45;
 |FINSI;
 aElem = #0#44;
 nElem = #0#45;
|FCAL;

|CALCULO ElElem6; |TIPO 6;
  nInkey = FSalida;
  |C_M #0#44,0,alfa1;
  |SI alfa1 = "N"; |ACABA; |FINSI;

   x_entra = entrada;
   x_alfa1 = #0Campo;
   |HAZ x_punto;
   #0Campo = x_alfa1;
   |PINTA #0Campo;
|FCAL;


|PROCESO VerElemento;
      |SI #0#44 [ "        "; |ACABA; |FINSI;
      enEmpresa = #0#0;
      eaDElem   = #0#44;
      enDNEle   = #0#45;
      enDeAqui  = 1;
      |SUB_EJECUTA_NP ":modelos/dsmoz036";
|FPRC;

|RUTINA infA2;
   #41#0 = enEmpresa;
   #41#2 = "        ";
   #41#3 = 00;
|FRUT;

|RUTINA supA2;
   #41#0 = enEmpresa;
   #41#2 = "zzzzzzzz";
   #41#3 = 99;
|FRUT;

|CALCULO ElElem0; |TIPO 0;
  |C_M #0Campo,0,alfa1;
  |SI alfa1 = "N"; |ACABA; |FINSI;

  |SI nInkey  = 10;
      |ABRE #41;
      #41#2 = #0#44;
      #41#3 = #0#45;
      |CONSULTA_F_CLAVE #1#41,infA2,supA2;
      #0#44 = #41#2; |PINTA #0#44;
      #0#45 = #41#3; |PINTA #0#45;
      |CIERRA #41;
  |FINSI;

  |SI nInkey  = 11;
      |HAZ VerElemento;
      |ERROR;
      |ACABA;
  |FINSI;

  |SI #0#44 ! aElem; |O #0#45 ! nElem;
      |SI aElem ! "        ";
          |CONFI "    NDesea Cambiar el Elemento de Inmovilizado ";
          |SI FSalida ! 0;
              #0#44 = aElem; |PINTA #0#44;
              #0#45 = nElem; |PINTA #0#45;
              |ERROR;
              |ACABA;
          |FINSI;
      |FINSI;
  |FINSI;


  alfa1 = #0#44; |QBF alfa1;
  |SI alfa1 = "";
      #0#45 = 0;
      |PINTA #0#45;
      |PTEC 802;
      |ACABA;
  |FINSI;

  |SI #0#44 ! aElem;
      |ABRE #41;
      #41#0 = enEmpresa;
      #41#2 = #0#44;
      #41#3 = 00;
      |LEE 011#41];
      |SI FSalida ! 0; |O #41#0 ! enEmpresa; |O #41#2 ! #0#44;
          #0#45 = 0;
          |PINTA #0#45;
          |PTEC 802;
      |FINSI;
      |CIERRA #41;
   |FINSI;
|FCAL;

|CALCULO ElNumElem0; |TIPO 0;

  |SI FSalida = 2; |ACABA; |FINSI;
  |C_M #0Campo,0,alfa1;
  |SI alfa1 = "N"; |ACABA; |FINSI;

  alfa1 = #0#44; |QBF alfa1; |SI alfa1 = ""; |ACABA; |FINSI;

  |SI #0#45 ! nElem;
      |SI #0#44 = aElem;
          |CONFI "    NDesea Cambiar el Elemento de Inmovilizado ";
          |SI FSalida ! 0;
              #0#45 = nElem; |PINTA #0#45;
              |ERROR;
              |ACABA;
          |FINSI;
      |FINSI;
  |FINSI;

  |ABRE #41;
  #41#0 = enEmpresa;
  #41#2 = #0#44;
  #41#3 = #0#45;
  |LEE 041#41=;
  nEstado = FSalida;
  |CIERRA #41;
  |SI nEstado ! 0;
      |CONFI "    SEl Elemento de Inmovilizado no Existe, Desea darlo de Alta S/N";
      |SI FSalida = 0;
          |HAZ AltaElemento;
      |SINO;
          |ERROR;
          |ACABA;
      |FINSI;
  |FINSI;
  nSwError = 0;
  |HAZ ModiElemento;
  |SI nSwError = 1;
      |ERROR;
  |FINSI;
|FCAL;

|| ***************************************************************************
|| ***************************************************************************
||   Calculos de las Lineas de Operaciones Financieras
|| ***************************************************************************
|| ***************************************************************************
|CALCULO SalgoYa;  |TIPO 7;
  |SI nSwSal = 1;  |PTEC 807;  |FINSI;
|FCAL;

|CALCULO Tipo3_L; |TIPO 3;
  nTipIVA = #1#7;

  esaltaL = (FEntrada / 100) ! 0;
  |SI esaltaL = 2;
      |SI nImpCanvi ! #1#10;
          nSwRecal = 1;
      |FINSI;
  |FINSI;

|FCAL;

|CALCULO losCalculos; |TIPO 0;
 |SI Campo = 4; |Y #1#4 = Contenido; |ACABA; |FINSI;
 |SI Campo = 5; |Y #1#5 = Contenido; |ACABA; |FINSI;
 |SI Campo = 7; |Y #1#7 = Contenido; |ACABA; |FINSI;

 #1#6 =  #1#4 + #1#5;             |PINTA #1#6;
 #1#8 = (#1#6 % #1#7) ! EURODB;   |PINTA #1#8;
 |HAZ losCalculos1;
|FCAL;

|CALCULO losCalculos1; |TIPO 0;
 #1#9 =  #1#6 + #1#8;             |PINTA #1#9;
 #1#10 = #1#10 - #1#4;            |PINTA #1#10;
 #1#10 = nImpPend;
|FCAL;

|CALCULO Tipo2_L;  |TIPO 2;
 #0#39 = #0#39 +. #1#4;
 #1#10 = #1#10 -. #1#4;
 nImpPend = #1#10;

 esaltaL = (FEntrada / 100) ! 0;
 |SI esaltaL = 3; nSwRecal = 1; |FINSI;
|FCAL;

|CALCULO Tipo0_L; |TIPO 0;
  esaltaL = (FEntrada / 100) ! 0;
  |SI esaltaL = 1;
      #1#7  = nTipIVA;                     |PINTA #1#7;
      #1#10 = #0#24 - #0#39; |PINTA #1#10;
  |SINO;
      nImpCanvi = #1#10;
  |FINSI;
  nImpPend = #1#10;
|FCAL;

|CALCULO Tipo1_L;  |TIPO 1;
   |SI esalta = 3; |ACABA; |FINSI;

   esaltaL = (FEntrada / 100) ! 0;
   |SI #1#11 = "S";
      |AVISO;
      |CONFI "2400N** Linea Contabilizada **. Continuar (S/N) ";
      |SI FSalida ! 0;
         |ERROR;
      |FINSI;
   |FINSI;
|FCAL;

||****** Procesos de Amortizaciones
|PROCESO BajaElemento;
 alfa1 = eaElemento; |QBF alfa1;
 |SI alfa1 = ""; |ACABA; |FINSI;

 |ABRE #41;
 #41#0 = enEmpresa;
 #41#2 = eaElemento;
 #41#3 = enElemeNum;
 |LEE 141#41=;
 |SI FSalida = 0;
     |SI #41#36 = #0#1;
         #41#36 = 0;
         #41#37 = "01.01.0000";
         #41#38 = 0;
         |GRABA 020#41a;
     |FINSI;
 |FINSI;
 |LIBERA #41;
 |CIERRA #41;
|FPRC;

|PROCESO AltaElemento;
   eaCam2     = #0#33; || Actividad
   eaElemento = #0#44; || Elemento
   enElemeNum = #0#45; || Numero
   eaCam4     = #0#4;  || Descripcion
   eaCam5     = #0#9;  || Cuenta Contable
   eaCam8     = #0#36; || Proveedor de Compra
   enCam10    = #0#24; || Importe Nominal
   enCam43    = #0#29; || IVA
   efCam27    = #0#5;  || Fecha Compra
   eaCam33    = #0#34; || Nombre Actividad
   eaCtana    = "";
   enOperFin  = #0#1;
   enCam46    = ( #0#29 * 100 / #0#24) ! 0;

   |SUB_EJECUTA_NP "camocab4";
|FPRC;

|PROCESO ModiElemento;
 nSwError = 0;

 |ABRE #41;
 #41#0 = #0#0;
 #41#2 = #0#44;
 #41#3 = #0#45;
 |LEE 141#41=;
 |SI FSalida ! 0;
     nSwError = 1;
     |CIERRA #41;
     |ACABA;
 |FINSI;

 |SI #41#36 ! #0#1; |Y #41#36 ! 0;
     |MENAV "    Error. Este Elemento de Inmovilizado esta Asignado a otra Op.Financiera";
      nSwError = 1;
      |CIERRA #41;
      |ACABA;
 |FINSI;
 #41#36 = #0#1;
 #41#37 = #0#46;
 |GRABA 020#41a;
 |LIBERA #41;
 |CIERRA #41;
 |SI aElem ! #0#44; |O nElem ! #0#45;
     eaElemento = aElem;
     enElemeNum = nElem;
     |HAZ BajaElemento;
 |FINSI;
|FPRC;
