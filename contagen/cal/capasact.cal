|| cambiar: caexistc/caexistl (codigo = actividad/departamento)
||          camoauxc/camoauxl (nuevo codigo = actividad/departamento)
||          cavenaut          (nuevo codigo = actividad/departamento)

|FICHEROS;
 camansde #0;
 camandep #20;

 caexistc #1;
 caexistl #2;
 caexlin@ #12;

 camoauxc #3;
 camoauxl #4;
 camoxca@ #13;
 camoxli@ #14;

 cavenaut #5;
 cavenau@ #15;

 :/basica/def/agicen14 #114;
|FIN;

|VARIABLES;

 aDirFich = "";
 VAlfa1   = "",
 aMas     = "";

 aAlfa1 = "";
 aAlfa2 = "";
 nNume1 = 0;
 nNume2 = 0;
 nNume4 = 0;
 nPara1 = 0;
 nPara2 = 0;

 SwCambia = 0;
 &aElDepar = "";

 nCampo1 = 0;
 aCampo1 = "";
 sw      = 0;
 &r_emp  = 0;
 &r_per  = 0;

|FIN;

|INCLUYE dscont_i;

|| **********************************************************************
|PROCESO LeeVentas;

 |SI #5#95 = aElDepar; |ACABA; |FINSI;

 |PARA nPara1 = 0; |SI nPara1 [ 95; |HACIENDO nPara1 = nPara1 + 1;
       #15nPara1 = #5nPara1;
 |SIGUE;

 #15#95 = aElDepar;
 |GRABA 020#15n;

 |BORRA 020#5a;
 |LIBERA #5;
|FPRC;

|DEFBUCLE LeeVentas;
 #5#1;
 ;
 00001, 1, "  ";
 99999, 9, "zz";
 be;
 NULL, LeeVentas;
|FIN;


|PROCESO LasVentas;

 |ABRE #5;
 #5#0  = 0;
 #5#1  = 1;
 #5#95 = "  ";
 |LEE 040#5];
 |SI FSalida ! 0;
     |CIERRA #5;
     |ACABA;            || No hay ninguno
 |FINSI;
 |CIERRA #5;
 |SI #5#95 = aElDepar; |ACABA; |FINSI;

 aMas = VAlfa1 + "def/cavenaut.mas";
 |CARGA_DEF aMas, cavenau@;
 |SI FSalida = 0;
     |PATH_DAT #15 aDirFich;
     |ABRE #15;
     |BUCLE LeeVentas;
     |CIERRA #15;
     |DESCARGA_DEF #cavenau@;
 |FINSI;
|FPRC;

|| **********************************************************************
|PROCESO LeeMoauxli;
 #14#0 = #4#0; #14#1 = #4#1;
 #14#2 = #4#2; #14#3 = #4#3;
 #14#4 = #4#4; #14#5 = #4#5;
 #14#6 = #4#6;
 #14#7 = aElDepar;
 |GRABA 020#14n;

 |BORRA 020#4a;
 |LIBERA #4;
|FPRC;

|DEFBUCLE LeeMoauxli;
 #4#1;
 ;
 #3#6, #3#0, 001;
 #3#6, #3#0, 999;
 be;
 NULL, LeeMoauxli;
|FIN;

|PROCESO LeeMoauxca;

 |SI #3#6 = aElDepar; |ACABA; |FINSI;

 |ABRE #14;
 |BUCLE LeeMoauxli;
 |CIERRA #14;

 #13#0 = #3#0; #13#1 = #3#1;
 #13#2 = #3#2; #13#3 = #3#3;
 #13#4 = #3#4; #13#5 = #3#5;
 #13#6 = aElDepar;
 |GRABA 020#13n;

 |BORRA 020#3a;
 |LIBERA #3;
|FPRC;

|DEFBUCLE LeeMoauxca;
 #3#1;
 ;
 "  ", "            ";
 "zz", "zzzzzzzzzzzz";
 be;
 NULL, LeeMoauxca;
|FIN;


|PROCESO ElInventario;

 |ABRE #3;
 #3#6 = "  ";
 #3#0 = "            ";
 |LEE 040#3];
 |SI FSalida ! 0;
     |CIERRA #3;
     |ACABA;            || No hay ninguno
 |FINSI;
 |CIERRA #3;

 aMas = VAlfa1 + "def/camoauxc.mas";
 |CARGA_DEF aMas, camoxca@;
 |SI FSalida = 0;
     aMas = VAlfa1 + "def/camoauxl.mas";
     |CARGA_DEF aMas, camoxli@;
     |SI FSalida = 0;
        |PATH_DAT #13  aDirFich;
        |PATH_DAT #14  aDirFich;

        |ABRE #13;
        |BUCLE LeeMoauxca;
        |CIERRA #13;

        |DESCARGA_DEF #camoxli@;
     |FINSI;
     |DESCARGA_DEF #camoxca@;
 |FINSI;
|FPRC;


|| **************************************************************************
|PROCESO LasLineas;
 |PARA nPara1 = 1; |SI nPara1 [ 24; |HACIENDO nPara1 = nPara1 + 1;
       #12nPara1 = #2nPara1;
 |SIGUE;
 #12#0 = aElDepar;
 |GRABA 020#12n;

 |BORRA 020#2a;
 |LIBERA #2;
|FPRC;

|DEFBUCLE LasLineas;
 #2#1;
 ;
 nCampo1, 00;
 nCampo1, 99;
 be;
 NULL, LasLineas;
|FIN;

|PROCESO ElCambio;
 |ABRE #1;
 #1#0 = nCampo1;
 |LEE 140#1=;
 |SI FSalida = 0;

     |SALVA_DATOS #1;
     |BORRA 020#1a;

     |REPON_DATOS #1;
     #1#0 = aElDepar;
     #1#1 = eaNombreAct;
     |GRABA 000#1n;

 |FINSI;
 |LIBERA #1;
 |CIERRA #1;

 aMas = VAlfa1 + "def/caexistl.mas";
 |CARGA_DEF aMas, caexlin@;
 |SI FSalida ! 0;
     |MENAV "    Error Cargado Fichero de Existencias";
     |ACABA;
 |FINSI;
 |PATH_DAT #12  aDirFich;

 |ABRE #12;
 |BUCLE LasLineas;
 |CIERRA #12;

 |DESCARGA_DEF #caexlin@;
|FPRC;


|PROCESO LeeExisCab;
 |SI sw = 1;
     sw = 2;
     |ERROR10;  ||Hay mas de una estructura
 |FINSI;
 sw = 1;
 nCampo1 = #1#0;
 aCampo1 = #1#0; |QBF aCampo1;
 aCampo1 = ("00" + aCampo1) % -102;
|FPRC;

|DEFBUCLE LeeExisCab;
 #1#1;
 ;
 00;
 99;
 e;
 NULL, LeeExisCab;
|FIN;

|PROCESO LasExistencias;
|| .................................... Existencias
   sw = 0;
   nCampo1 = 0;
   aCampo1 = "";
   |BUCLE LeeExisCab;
   |SI sw > 0;
       |SI sw = 1; |Y Haybasica = 1;
           nNume4 = aElDepar;
           |SI nCampo1 ! nNume4;    ||Cambiamos el codigo por la Actividad
               |HAZ ElCambio;
           |FINSI;
       |FINSI;
       |SI Haybasica = 1;
           fich_alta = aDirFich;
           |SUB_EJECUTA "caz00006";
       |FINSI;
       r_emp= enEmpresa;
       r_per= enPeriodo;
       |HAZ ExisGeneral;   ||Crea el general.
  |FINSI;
|FPRC;


|| **************************************************************************
|PROGRAMA;
   |DBASE VAlfa1; |QBF VAlfa1;
   aAlfa1 = ("00000" + enEmpresa) % -105;
   aAlfa2 = ("0" + enPeriodo)     % -101;

   aDirFich = VAlfa1 + "fich/"+ aAlfa1 + aAlfa2 + "/";
   |PATH_DAT #1  aDirFich;
   |PATH_DAT #2  aDirFich;
   |PATH_DAT #3  aDirFich;
   |PATH_DAT #4  aDirFich;
   |PATH_DAT #5  aDirFich;

   cod1 = enEmpresa;
   cod2 = enPeriodo;
   emEmp = 1;
   |HAZ leelo;
   |SI swerror = 1; |ACABA; |FINSI;

   aElDepar    = "01";
   eaNombreAct = "";
   |HAZ DirAplicSt;
   |SI Haybasica = 1;
       || Lee Actividad
       |ABRE #agicen14;
       #agicen14#0 = enEmpresa;
       #agicen14#1 = 00;
       |LEE 041#agicen14.];
       |SI FSalida = 0; |Y #agicen14#0 = enEmpresa;
           aAlfa1 = #agicen14#1; |QBF aAlfa1;
           aAlfa1 = ("00" + aAlfa1) % -102;
           aElDepar = aAlfa1;
           eaNombreAct  = #agicen14#4;
       |FINSI;
       |CIERRA #agicen14;
   |SINO;
       |SI eaDepar = "S";
           |ABRE #20;
           #20#0 = enEmpresa;
           #20#1 = "  ";
           |LEE 001#20];
           |SI FSalida ! 0; |O #20#0 ! enEmpresa;
               #20#0 = enEmpresa;
               #20#1 = aElDepar;
               #20#2 = "";
               |GRABA 020#20n;
           |FINSI;
           aElDepar    = #20#1;
           eaNombreAct = #20#2;
           |CIERRA #20;
       |FINSI;
   |FINSI;

   || ... Procesos para los ficheros que tiene como clave el Departamento

   |HAZ LasExistencias;

   |HAZ ElInventario;

   |HAZ LasVentas;

|FPRO;
