|FICHEROS;
   cachecta #0;
   cacuenta #1;
   caclipro #2;

   imagen@  #900;
   canempre #30;
   caselem1 #998;
|FIN;

|VARIABLES;
   &entrada = 1;

   informe = "cachecta";
   aDCta   = "            ";
   aHCta   = "zzzzzzzzzzzz";
   aDPase  = "";
   aHPase  = "";

   aAlfa1    = "";
   aAlfa2    = "";
   aBlanc    = "";
   aZetes    = "";

   nSwImp    = 0;
   &enSwP    = 0;
   aNom      = "";
   aAnterior = "";

   aFichero  = "";
   aMas      = "";
   &fFecha   = @;
   &eaNif    = "";
|FIN;

|INCLUYE dscont_i;

|CALCULO cuenta; |TIPO 6;
     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;
|FCAL;

|| *************************************************************************
|PROCESO LeeLaFicha;
 eaNif = "";
 |ABRE #1;
 |DDEFECTO #1;
 #1#0 = eaCuenta;
 |LEE 001#1=;
 |CIERRA #1;
 |ABRE #2;
 |DDEFECTO #2;
 #2#23 = "C";
 #2#10 = eaCuenta;
 |LEE 001#2=;
 |SI FSalida = 0;
     eaNif = #2#9;
 |SINO;
     #2#23 = "P";
     #2#10 = eaCuenta;
     |LEE 001#2=;
     |SI FSalida = 0;
         eaNif = #2#9;
     |FINSI;
 |FINSI;
 |CIERRA #2;
|FPRC;

|PROCESO Imprime;

 |SI aNom = #900#2; |Y aNom ! "";
     eaCuenta = aAnterior;|HAZ LeeLaFicha;
     |SI enSwP = 0;
         |PRINT;
     |FINSI;
     eaCuenta = #900#0;|HAZ LeeLaFicha;
     enSwP  = 1;
     nSwImp = 1;
     |PRINT;
 |SINO;
     enSwP  = 0;
 |FINSI;

 aAnterior = #900#0;
 aNom      = #900#2;
|FPRC;

|DEFBUCLE LeeAuxiliar;
 #900#2001;
 ;
 aBlanc;
 aZetes;
 ;
 NULL, Imprime;
|FIN;

|PROCESO buscando;
  aAlfa1 = #1#0; |QBF aAlfa1;
  |SI aAlfa1 = ""; |ACABA;  |FINSI;

  |DDEFECTO #900;
  #900#0 = #1#0;
  #900#1 = #1#1;
  #900#3 = #1#3;
  aAlfa1 = #1#2;
  aAlfa1 = aAlfa1 > " ";
  |QBT aAlfa1;
  |SI #0#2 ! "S";
      aAlfa2 = #1#1;
      aAlfa1 = (aAlfa1 + aBlanc) % 135;
      aAlfa1 = aAlfa1 + aAlfa2;
  |FINSI;
  #900#2 = aAlfa1;
  |GRABA 000#900n;
|FPRC;

|DEFBUCLE cachecta;
   #1#1;
   3;
   aDCta, aDPase;
   aHCta, aHPase;
   e;
   NULL, buscando;
|FIN;

|PROCESO HazTodo;

   |DELINDEX #900;

   |ABRE #900;
   |BUCLE cachecta;
   |CIERRA #900;

   nSwImp = 0;
   enSwP  = 0;
   aNom   = "";
   |BUCLE LeeAuxiliar;
   |SI nSwImp = 1;
       |PIEINF;
   |FINSI;
|FPRC;

|| ----------------------------------------------------------------
|PROCESO ElChequeo; |TIPO 3;

    fFecha  = ~;
    aBlanc  = " " * 36;
    aZetes  = "z" * 36;
    aDCta = #0#0;
    aHCta = #0#1; |QBF aHCta;
    aHCta = (aHCta + "zzzzzzzzzzzz") % 112;
    aDPase = " ";
    aHPase = "z";
    |SI #0#2 ! "T";
        aDPase = #0#2; aHPase = #0#2;
    |FINSI;
    |DBASE aMas; |QBF aMas;
    aMas = aMas + "/def/cacuenta";
    |CARGA_DEF aMas, imagen@;
    |SI FSalida ! 0;
        |MENAV "    Error en Instalacion de Contabilidad";
        |ACABA;
    |FINSI;

    aFichero = ("wc" + Usuario) % 108;
    |NOME_DAT #900 aFichero;
    |ABRE #900; |CIERRA #900;

    |IMPRE 0;
    |SI FSalida ! 0;  |ACABA;  |FINSI;
    |INFOR informe;
    |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

    |SI #48#27 = "S";
        |HAZ MultiEmpresa;
    |SINO;
        |DFICE dirfich0; |QBF dirfich0;
        |HAZ HazTodo;
    |FINSI;
    |FININF;
    |FINIMP;

    |DELINDEX #900;
    |DESCARGA_DEF #imagen@;
|FPRC;

|| **********************************************************************
|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #2 dirfich0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 000001;
 999999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;
