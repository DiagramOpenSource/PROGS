|FICHEROS;
   cazsalpr #0;    || Informe comprobacion saldos
   cacuentm #1;    || temporal cuentas presupuestos
   cacuenta #10;   || maestro cuentas
   cacuepre #11;   || fichas presupuestarias
   canempre #30;
|FIN;

|VARIABLES;
   &entrada = 1;
   x = 0;
   men1 = "     NO EXISTE EMPRESA !";
   mes = 0;                  || Variable para saber el mes seleccionado
   letrames = "";            || Variable para poner el mes en letra
   varcamp = 0;              || Variable para posicionarse en un campo
   saldo = 0;         || Resta entre el debe y el haber;
   rellenos = "";     || Variable para rellenar el hasta de la cuenta en zetas
   pepe1    = 0;
   pepe2    = 0;
   runnom   = "caybalpr";
   aAlfa    = "";
   informe  = "";
   aAlfa1   = "";
   nCalc    = 0;
   nCalc1   = 0;

   &i_salcuep = 0;
   &i_salprep = 0;
   &i_salcuee = 0;
   &i_salpree = 0;
   &i_salprea = 0;
   &dife1     = 0;
   &dife2     = 0;
   &porce1    = 0;
   &porce2    = 0;

   &p1 = 0;
   &p2 = 0;
   &p3 = 0;
   &e1 = 0;
   &e2 = 0;
   &e3 = 0;
   &a1 = 0;
   &rp = 0;
   &re = 0;
   menor = 0;
   para1 = 0;
   &aparam = "";
   aFichero = "";
   nSw      = 0;
   &eaTexto = "";
   nume1 = 0;
   nume2 = 0;
   des_pre = " ";
   has_pre = "z";
   t_agrupa = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE digito_i;


|PROCESO presup2;    || si no esta como ficha presupuestaria no sale SI

   |DDEFECTO #11;
   #cacuepre#19 = t_agrupa;
   #cacuepre#0 = #cacuenta#0;
   #cacuepre#1 = #cacuenta#1;
   |LEE 001#11=;

   |PARA nume1 = 0; |SI nume1 [ 58; |HACIENDO nume1 = nume1 + 1;
         #cacuentm#nume1# = #cacuenta#nume1#;
   |SIGUE;

   |PARA nume1 = 59; |SI nume1 [ 70; |HACIENDO nume1 = nume1 + 1;
         nume2 = nume1 - 55;
         #cacuentm#nume1# = #cacuepre#nume2#;
   |SIGUE;
   #cacuentm#71 = #cacuepre#02;  || signo

   |GRABA 020#1n;
|FPRC;

|DEFBUCLE bu_pres2;   || balances situacion y explotacion
   #10#4;
   ;
   des_pre, 0,  0,  0,  0;
   has_pre, 4, 99, 99, 99;
   e;
   NULL, presup2;
|FIN;

|PROCESO presup0; |TIPO 0;

      |HAZ pre_aviso;
      aFichero = ("9p" + Usuario) % 108;
      |NOME_DAT #1 aFichero;

      |DELINDEX #1;
      |ABRE #1;
      |ABRE #11;
      |BUCLE bu_pres2;
      |CIERRA #1;
      |CIERRA #11;
      |POPV;
|FPRC;

|PROCESO pre_aviso;
   |PUSHV 11181760;
   |ATRI P;
   |CUADRO 11 18 15 60;
   |ATRI R;
   |CUADRO 12 19 14 59;
   |ATRI 0;
   |ATRI R;
   |ATRI I;
   |PINTA 1320, " Procesando Fichas Presupuestarias ... ";
   |ATRI 0;
|FPRC;

|PROCESO antesque;
   aparam = PARAMETRO;
   |QBF aparam;
   |SI aparam = "fech";
      |NOME_DAT #10 "cabalano";
      |NOME_DAT #11 "cabalpre";
   |FINSI;
|FPRC;

|| ***********************************************************************
||                       CALCULOS DE PANTALLA
|| ***********************************************************************

|PROCESO cuenta; |TIPO 0;
     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;
|FPRC;

|PROCESO mesconta; |TIPO 0;
   varcamp = Campo + 1;                || Coge el campo del nobre del mes
   mes = #canempre#11 + (#0Campo - 1);       || Operacion para seber que mes es
   |SI mes > 12; mes = mes - 12; |FINSI;
   |HAZ mesletra;                      || Recoge el mes en letra
   #0varcamp = letrames;
   |PINTA #0varcamp;                   || Pinta el nombre del mes

   |SI Campo = 4; |Y #0#2 > #0#4;
       |ERROR;
   |FINSI;
|FPRC;

|PROCESO mesletra;
   |SI mes = 0;  letrames = "Apertura  "; |FINSI;
   |SI mes = 1;  letrames = "Enero     "; |FINSI;
   |SI mes = 2;  letrames = "Febrero   "; |FINSI;
   |SI mes = 3;  letrames = "Marzo     "; |FINSI;
   |SI mes = 4;  letrames = "Abril     "; |FINSI;
   |SI mes = 5;  letrames = "Mayo      "; |FINSI;
   |SI mes = 6;  letrames = "Junio     "; |FINSI;
   |SI mes = 7;  letrames = "Julio     "; |FINSI;
   |SI mes = 8;  letrames = "Agosto    "; |FINSI;
   |SI mes = 9;  letrames = "Septiembre"; |FINSI;
   |SI mes = 10; letrames = "Octubre   "; |FINSI;
   |SI mes = 11; letrames = "Noviembre "; |FINSI;
   |SI mes = 12; letrames = "Diciembre "; |FINSI;
   |SI mes = 13; letrames = "Cierre    "; |FINSI;
|FPRC;

|PROCESO LeeAgrupa;
    |SI t_agrupa ! #0#21; |O nSw = 0;
        #0#22 = "M";
        |ABRE #11;
        #11#19 = #0#21;
        #11#0  = doce;
        #11#1  = 0;
        |LEE 041#11];
        |SI FSalida = 0; |Y #11#19 = #0#21;
            #0#22 = #11#3;
        |FINSI;
        |CIERRA #11;
        |PINTA #0#22;
        nSw = 1;
        t_agrupa = #0#21;
    |FINSI;
|FPRC;

|| **********************************************************************
||                       CALCULOS DE IMPRESION
|| **********************************************************************

|PROCESO &totales;            || calcula los totales del informe
   p1 = p1 + i_salcuep;
   p2 = p2 + i_salprep;
   p3 = p3 + dife1;
   e1 = e1 + i_salcuee;
   e2 = e2 + i_salpree;
   e3 = e3 + dife2;
   a1 = a1 + i_salprea;
   rp = ((p1 * 100) / p2) ! 2;
   re = ((e1 * 100) / e2) ! 2;
|FPRC;

|PROCESO condicion;    || Calculo condiciones de impresion
   |HAZ digitos2;
   |SI d_error ! 0;  |ACABA;  |FINSI;

   pepe1     = 0;
   pepe2     = 0;
   i_salcuep = 0;       || Pone a cero el total saldo periodo cuentas
   i_salprep = 0;       || Pone a cero el total saldo periodo presupuesto
   i_salcuee = 0;       || Pone a cero el total saldo ejercicio cuentas
   i_salpree = 0;       || Pone a cero el total saldo ejercicio presupuesto
   i_salprea = 0;

   varcamp =  (#0#2 * 3) + 9;
   || ************ Bucle del debe y haber periodo *************

   |PARA x = #0#2; |SI x [ #0#4; |HACIENDO x = x + 1;
         |SI x = #0#4; i_salcuep = #cacuentm#varcamp#; |FINSI;
         pepe1   = pepe1 + #cacuentm#varcamp#;
         varcamp = varcamp + 1;          || Se situa en el campo del haber

         |SI x = #0#4; i_salcuep = i_salcuep - #cacuentm#varcamp#; |FINSI;
         pepe2     = pepe2 + #cacuentm#varcamp#;
         i_salcuee = pepe1 - pepe2;

         varcamp   = varcamp + 2;        || Se situa en el campo del debe
   |SIGUE;

   |SI #0#22 = "M";
       |PARA x = #0#2; |SI x [ #0#4; |HACIENDO x = x + 1;
          |SI x ! 0;
             varcamp = x + 58;
             i_salpree = i_salpree + #cacuentm#varcamp#;  || suma presupuestado
             |SI x = #0#4; i_salprep = #cacuentm#varcamp#; |FINSI;
          |FINSI;
       |SIGUE;
   |SINO;
       |PARA x = 59; |SI x [ 70; |HACIENDO x = x + 1;
             i_salpree = i_salpree + #cacuentm#x#;  || suma presupuestado
             |SI x = 70; i_salprep = #cacuentm#x#; |FINSI;
       |SIGUE;
   |FINSI;

   |SI #cacuentm#71 = "H"; i_salprep = -i_salprep; i_salpree = -i_salpree; |FINSI;

   |SI i_salprep = 0; |Y i_salpree = 0; |Y #0#6 = "N"; |ACABA; |FINSI;

   |PARA x = 59; |SI x [ 70; |HACIENDO x = x + 1;
      i_salprea = i_salprea + #cacuentm#x#;
   |SIGUE;
   |SI #cacuentm#71 = "H"; i_salprea = -i_salprea; |FINSI;

   dife1 = i_salcuep - i_salprep;
   dife2 = i_salcuee - i_salpree;

   porce1 = ((i_salcuep * 100) / i_salprep) ! 2;
   porce2 = ((i_salcuee * 100) / i_salpree) ! 2;

   |PRINT;
|FPRC;

|DEFBUCLE caybalpr0;     || Bucle del plan contable (descendente)
   #1#3;                   || Lee por la tercera clave
   ;
   #0#0,     1;
   rellenos, 6;
   e;
   NULL, condicion;
|FIN;

|DEFBUCLE caybalpr1;     || Bucle del plan contable (ascendente)
   #1#1;                    || Lee por la primera clave
   ;
   #0#0, 1;
   #0#1, 6;
   e;
   NULL, condicion;
|FIN;

|PROCESO rellena;
   rellenos = "zzzzzzzzzzzz";
   menor = -1;

   |PARA para1 = 9; |SI para1 [ 14; |HACIENDO para1 = para1 + 1;
        |SI #0para1 ! 0;
         |SI menor = -1;       menor = #0para1;  |FINSI;
         |SI menor > #0para1;  menor = #0para1;  |FINSI;
      |FINSI;                   || saca el nivel elegido mas peque¤o
   |SIGUE;

   |SI menor ! 0;
      |SI menor = 1;  nume1 = dig4;  |FINSI;
      |SI menor = 2;  nume1 = dig5;  |FINSI;   || construye el hasta segun
      |SI menor = 3;  nume1 = dig6;  |FINSI;   ||/el nivel mas bajo elegido
      |SI menor = 4;  nume1 = dig7;  |FINSI;
      |SI menor = 5;  nume1 = dig8;  |FINSI;
      |SI menor = 6;  nume1 = dig9;  |FINSI;
      nume1 = 100 + nume1;
      rellenos = #0#1 % nume1;
      rellenos = rellenos + "zzzzzzzzzzzz";
      rellenos = rellenos % 112;
   |FINSI;
|FPRC;

|PROCESO impre;       || Impresion por impresora;

   |HAZ rellena;
   eaTexto = "Presupuesto Mensual";
   |SI #0#22 = "A";
       eaTexto = "Presupuesto Anual";
   |FINSI;

   Impresora = "Crystal Reports";
   |IMPRE 0;                                 || Abrimos impresora
   |SI FSalida ! 0; |ACABA; |FINSI;

   aAlfa = Impresora;
   aAlfa = aAlfa % 113;
   |SI aAlfa = "CrystalReport";
       informe = "cczsalpr";
   |SINO;
       informe = "cazsalpr";
   |FINSI;
   |INFOR informe;
   |SI FSalida ! 0;
       |MENAV "0000No Existe Informe";
       |FINIMP;
       |ACABA;
   |FINSI;

   t_agrupa = #0#21;
   |HAZ presup0;

   p1 = 0;   p2 = 0;   p3 = 0;
   e1 = 0;   e2 = 0;   e3 = 0;
   a1 = 0;

   |SI #0#16 = "D";
      |BUCLE caybalpr0;         || descendente
   |SINO;
      |BUCLE caybalpr1;         || ascendente
   |FINSI;

   |PIEINF;
   |FININF;
   |FINIMP;
|FPRC;

|| *********************************************************************
||                             COMIENZO
|| *********************************************************************

|PROGRAMA;
   |HAZ antesque;
   |CLS;
   |CABEZA "Balance de Saldos";
   |HAZ inicio;
   |DDEFECTO #0;

   aAlfa = PARAMETRO; |QBF aAlfa;
   aAlfa = aAlfa - "FICHA:";
   |SI FSalida ! 0;
      aAlfa = aAlfa - ","; nCalc = FSalida;
      nCalc1 = 99 + ((nCalc / 100) ! 0);
      aAlfa1 = aAlfa % nCalc1; #0#21 = aAlfa1;
      nCalc1 = nCalc + 98;
      aAlfa1 = aAlfa % nCalc1; #0#0 = aAlfa1; #0#1 = aAlfa1;
   |FINSI;

   FSalida = 1;
   |PARA; |SI 0 < FSalida; |HACIENDO;
      |PINPA #0#0;
      |PINDA #0#0;
      |ENDATOS #1#0;   || Seleccion
      |SI 0 [ FSalida;
         |HAZ impre;
         FSalida = 1;
      |FINSI;
   |SIGUE;
   |DELINDEX #1;  || se carga el temporal
|FPRO;
