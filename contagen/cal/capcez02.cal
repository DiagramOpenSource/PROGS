|FICHEROS;
   capcez02 #0;
   capcem01 #10;
   caclipro #11;
   cacuenta #12;
   cacuebal #13;

   caw00102 #100,MANTE;
   caw00103 #101,MANTE;

   canempre #200;
   ca8cumay #410;
   ca8m0002 #422;
|FIN;

|VARIABLES;
   aNif = "";
   &eaTempo = "";
   aFicDes = "";
   aFicOrg = "";
   aVer1 = "";
   aVer2 = "";
   aBase = "";
   {1}aLocal = "";
   {1}aConte = "";
   aExe = "";
   aLon = "";
   nPosi = 0;
   aDato = "";
   aCara = "";
   nCara = 0;
   i = 0;
   nSwLeeName = 0;
   nSw       = 0;
   nSwLeeCli = 0;
   nSwLeeTip = 0;
   nSwLeePAdd = 0;
   nSwLeeAdId = 0;
   nSwLeeTDef = 0;
   nHandle   = 0;

   aIP      = "";
   aOrigen  = "";
   aDestino = "";

   aReg    = "";
   aRegSob = "";

   aCtaCble = "";

   aAlfa   = "";
   aAlfa1  = "";
   aAlfa2  = "";
   aAlfa3  = "";
   aTag    = "";
   aIniTag = "";
   aFinTag = "";
   aValor  = "";
   aCRLF   = "";
   aCR     = "";
   aLF     = "";
   aComillas = "";

   nPos     = 0;
   nCalc    = 0;
   nCalc1   = 0;
   nCalc2   = 0;
   nSwLen   = 0;
   nNumBase = 0;

   nContLinBlanco = 10;
   nContSpaces    = 0;
   nContRegs      = 50;

   aLong   = "";
   nLong   = 0;
   nCont   = 0;
   nCont1  = 0;
   nNivel  = 0;
   aCuenta = "";

   nSwExiste = 0;
|FIN;

|PROCESO LimpiaNif1;
   || Quitamos los espacios y los caracteres extra¤os ...
   |QBT aAlfa;
   aAlfa = aAlfa - "-"; |PARA; |SI FSalida ! 0; |HACIENDO; aAlfa = aAlfa - "-"; |SIGUE;
   aAlfa = aAlfa - ":"; |PARA; |SI FSalida ! 0; |HACIENDO; aAlfa = aAlfa - ":"; |SIGUE;
   aAlfa = aAlfa - ";"; |PARA; |SI FSalida ! 0; |HACIENDO; aAlfa = aAlfa - ";"; |SIGUE;
   aAlfa = aAlfa - "_"; |PARA; |SI FSalida ! 0; |HACIENDO; aAlfa = aAlfa - "_"; |SIGUE;

   aAlfa1 = aAlfa % 0102;
   aAlfa = aAlfa % 0399;
|FPRC;

|PROCESO BuscaCuenta;
   nSwExiste = 0;

   aAlfa = #caw00103#3; |HAZ LimpiaNif1;

   #caclipro#9 = aAlfa;
   |LEE 000#caclipro.=;
   |SI FSalida = 0;
      aAlfa = #caclipro#24; aAlfa = aAlfa % 0102;
      |SI aAlfa = aAlfa1; nSwExiste = 1; |FINSI;
   |FINSI;

   |SI nSwExiste = 1;
      #caw00103#2 = #caclipro#10; |PINTA #caw00103#2;
   |SINO;
      |ABRE #capcem01;
      #capcem01#0 = #caw00103#0;
      |LEE 000#capcem01.=;
      |SI FSalida ! 0; |DDEFECTO #capcem01; |FINSI;
      |CIERRA #capcem01;

      |ABRE #caclipro;
      #caclipro#23 = "C";
      #caclipro#10 = #caw00103#2; aCuenta = #caclipro#10; |QBF aCuenta;
      |HAZ BuscaNivel;
      #caclipro#11 = nNivel;
      |LEE 101#caclipro.=;
      |SI FSalida = 0;
         aAlfa = #caclipro#9; |QBF aAlfa;
         |SI aAlfa = "";
            aAlfa = #caw00103#3; |HAZ LimpiaNif1;
            #caclipro#9 = aAlfa;
         |FINSI;
         |GRABA 020#caclipro.a; |LIBERA #caclipro;
      |FINSI;
      |CIERRA #caclipro;
   |FINSI;
|FPRC;

|PROCESO MiraFuncion; |TIPO 11;
   |SI FSalida = 10;
      |SI #caw00103#4 = 1;
         |MENAV "    Este cliente ya tiene ficha de cliente/proveedor y cuenta contable";
      |SINO;
         aAlfa = "    " + &191 + " Crear las fichas de cliente/proveedor y cuenta contable ?";
         |CONFI aAlfa;
         |SI FSalida = 0;
            |DDEFECTO #caclipro;
            #caclipro#23 = "C";
            #caclipro#10 = #caw00103#2;
            aAlfa = #caclipro#10; |QBF aAlfa; |HAZ BuscaNivel;
            #caclipro#11 = nNivel;
            aAlfa = #caw00103#3; |HAZ LimpiaNif1;
            #caclipro#9  = aAlfa;
            #caclipro#24 = aAlfa1;
            #caclipro#1  = #caw00103#1;
            |GRABA 020#caclipro.c;

            aCuenta = #caw00103#2; |QBF aCuenta;
            |ABRE #cacuebal;

            |DDEFECTO #cacuenta;
            |PARA nCont = 6; |SI nCont ] 1; |HACIENDO nCont = nCont - 1;
               nCalc = 13 + nCont;
               |SI #canempre#nCalc# ! 0;
                  nCalc = 100 + #canempre#nCalc#;
                  aAlfa = aCuenta % nCalc;
                  #cacuenta#0 = aAlfa;
                  #cacuenta#1 = nCont;
                  |LEE 000#cacuenta.=;
                  |SI FSalida = 0;
                     #cacuebal#0 = aAlfa
                     |LEE 000#cacuebal.=;
                     |SI FSalida = 0;
                        #cacuenta#4 = #cacuebal#1;
                        #cacuenta#5 = #cacuebal#2;
                        #cacuenta#6 = #cacuebal#3;
                        #cacuenta#7 = #cacuebal#4;
                        #cacuenta#8 = #cacuebal#5;
                        |SAL;
                     |FINSI;
                  |FINSI;
               |FINSI;
            |SIGUE;
            |CIERRA #cacuebal;

            |PARA nCont1 = 9; |SI nCont1 [ 53; |HACIENDO nCont1 = nCont1 + 1;
               #cacuenta#nCont1# = 0;
            |SIGUE;

            #cacuenta#0 = aCuenta;
            #cacuenta#1 = nNivel;
            #cacuenta#2 = #caw00103#1;
            #cacuenta#3 = "S";
            |GRABA 020#cacuenta.c;
         |FINSI;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO BuscaNivel;
   aLong = aAlfa % 0; nLong = aLong;
   nNivel = 0;
   |SI nLong > 0;
      |SI nLong = #48#14; nNivel = 1; |FINSI;
      |SI nLong = #48#15; nNivel = 2; |FINSI;
      |SI nLong = #48#16; nNivel = 3; |FINSI;
      |SI nLong = #48#17; nNivel = 4; |FINSI;
      |SI nLong = #48#18; nNivel = 5; |FINSI;
      |SI nLong = #48#19; nNivel = 6; |FINSI;
   |FINSI;
|FPRC;

|PROCESO BuscaValor;
   aIniTag = "<" + aTag;
   aFinTag = "</" + aTag + ">";

   aAlfa1 = aIniTag + ">";
   aAlfa1 = aReg - aAlfa1;
   |SI FSalida ! 0; aIniTag = aIniTag + ">"; |FINSI;

   aIniTag = "<" + aTag;


   aAlfa1 = aReg - aIniTag; nCalc = ((FSalida / 100) ! 0) + 1;
   |SI FSalida ! 0;
      aAlfa1 = aAlfa1 - aFinTag; nCalc1 = ((FSalida / 100) ! 0);
      nCalc = (nCalc * 100) + (nCalc1 - nCalc);
      aValor = aAlfa1 % nCalc;
      aValor = aValor - ">";
      |SI FSalida ! 0;
         nCalc = FSalida + 98;
         aValor = aValor % nCalc;
      |FINSI;
      FSalida = 0;
   |SINO;
      aValor = ""; nCalc = -1; FSalida = nCalc;
   |FINSI;
|FPRC;

|PROCESO Interpreta;
   aAlfa1 = aReg - "</Company>";
   |SI FSalida ! 0;     || Hay que eliminarlo porque tiene un Tag 'Id' como el
      nCalc1 = FSalida; ||   del codigo de cliente
      aAlfa1 = aAlfa1 - "<Company";
      |SI FSalida ! 0;
         nCalc2 = FSalida; nCalc1 = nCalc1 - 800;
      |SINO;
         nCalc2 = 0108;
      |FINSI;
      nCalc1 = (nCalc1 / 100) ! 0; nCalc2 = (nCalc2 / 100) ! 0;
      nCalc1 = (nCalc2 * 100) + (nCalc1 - nCalc2);
      aAlfa1 = aAlfa1 % nCalc1;
      aAlfa1 = "<Company" + aAlfa1 + "</Company>";
      aReg = aReg - aAlfa1;
   |FINSI;

   aAlfa1 = aReg - "</Customer>";
   |SI FSalida ! 0;
      nSwLeeCli = 0; nSwLeeTip = 0;
      nSwLeePAdd = 0;
      |GRABA 020#capcem01.a;
   |FINSI;

   aAlfa1 = aReg - "<Customer ";
   |SI FSalida ! 0;
      nSwLeeCli = 1;
   |FINSI;

   aAlfa1 = aReg - "<PostalAddress>";
   |SI FSalida ! 0; nSwLeePAdd = 1; |FINSI;
   aAlfa1 = aReg - "</PostalAddress>";
   |SI FSalida ! 0; nSwLeePAdd = 0;  nSwLeeAdId = 0; |FINSI;

   aAlfa1 = aReg - "<TaxDefinition>";
   |SI FSalida ! 0; nSwLeeTDef = 1;|FINSI;

   aAlfa1 = aReg - "</TaxDefinition>";
   |SI FSalida ! 0; nSwLeeTDef = 0; nSwLeeName = 1; |FINSI;

   |SI nSwLeeCli = 0; |ACABA; |FINSI;

   aTag = "Id"; |HAZ BuscaValor;
   |SI FSalida = 0; |Y nSwLeeCli = 1;
      aCtaCble = "";
      aNif = "";
      #capcem01#0 = aValor;
      |LEE 000#capcem01.=;
      |SI FSalida = 0;
         |BORRA 020#capcem01.a;
         aCtaCble = #capcem01#10;
         aNif = #capcem01#8;
      |FINSI;

      |DDEFECTO #capcem01;
      #capcem01#0 = aValor;
      #capcem01#10 = aCtaCble;
      #capcem01#8 = aNif;
      |GRABA 020#capcem01.c;

      aAlfa1 = "    Importando cliente " + aValor; |QBF aAlfa1;
      aAlfa1 = (aAlfa1 + (" " * 50)) % 0150;
      |PINTA 2301, aAlfa1;
      nContRegs = nContRegs -1;
      |SI nContRegs = 0; |PULSATECLA; nContRegs = 50; |FINSI;
   |FINSI;

   aTag = "Type"; |HAZ BuscaValor;
   |SI FSalida = 0;
      |SI aValor = "private customer"; nSwLeeTip = 1; |FINSI;
      |SI aValor = "business customer"; nSwLeeTip = 2; |FINSI;
   |FINSI;

   |SI nSwLeePAdd = 1;
      aTag = "AddressId"; |HAZ BuscaValor;
      |SI aValor = "0";
         nSwLeeAdId = 1;
      |SINO;
         nSwLeeAdId = 0;
      |FINSI;
   |FINSI;

   |SI nSwLeeTip = 1;
      aTag = "FirstName"; |HAZ BuscaValor;
      |SI FSalida = 0;
         aAlfa = aValor; |HAZ QuitaRarosImp; aValor = aAlfa;
         aAlfa = #capcem01#1; |QBF aAlfa;
         aAlfa = aAlfa + aValor; |QBF aAlfa;
         #capcem01#1 = aAlfa;
      |FINSI;

      aTag = "LastName"; |HAZ BuscaValor;
      |SI FSalida = 0;
         aAlfa = aValor; |HAZ QuitaRarosImp; aValor = aAlfa;
         aAlfa = #capcem01#1; |QBF aAlfa;
         aAlfa = aAlfa + " " + aValor; |QBF aAlfa;
         #capcem01#1 = aAlfa;
      |FINSI;

      |SI nSwLeePAdd = 1;
         aAlfa = #capcem01#2; |QBF aAlfa;
         |SI aAlfa = "";
            aTag = "Street"; |HAZ BuscaValor;
            |SI FSalida = 0;
                 aAlfa = aValor; |HAZ QuitaRarosImp; aValor = aAlfa;
                 #capcem01#2 = aValor;
            |FINSI;
            aTag = "Zip"; |HAZ BuscaValor;
            |SI FSalida = 0;
                 aAlfa = aValor; |HAZ QuitaRarosImp; aValor = aAlfa;
                 #capcem01#3 = aValor;
            |FINSI;
            aTag = "City"; |HAZ BuscaValor;
            |SI FSalida = 0;
               aValor = aValor - "(";
               |SI FSalida ! 0;
                  nCalc = ((FSalida / 100) ! 0) + 99;
                  aAlfa = aValor % nCalc;
                  |HAZ QuitaRarosImp;
                  #capcem01#4 = aAlfa;
                  nCalc = ((nCalc - 99) * 100) + 99;
                  aAlfa = aValor % nCalc; aAlfa = aAlfa - ")";
                  |HAZ QuitaRarosImp;
                  #capcem01#5 = aAlfa;
               |SINO;
                  aAlfa = aValor; |HAZ QuitaRarosImp; aValor = aAlfa;
                  #capcem01#4 = aValor;
               |FINSI;
            |FINSI;
            aTag = "Country"; |HAZ BuscaValor;
            |SI FSalida = 0; #capcem01#6 = aValor; |FINSI;
         |FINSI;
      |FINSI;
   |FINSI;

   |SI nSwLeeTip = 2;
      aTag = "Name1"; |HAZ BuscaValor;
      |SI FSalida = 0;
          aAlfa = aValor; |HAZ QuitaRarosImp; aValor = aAlfa;
          #capcem01#1 = aValor;
      |FINSI;

      aTag = "VATRegNo"; |HAZ BuscaValor;
      |SI FSalida = 0;
          |SI aNif ! ""; aValor = aNif; |FINSI;
          #capcem01#8 = aValor;
      |FINSI;

      |SI nSwLeePAdd = 1;
         aTag = "AddressId"; |HAZ BuscaValor;
         |SI aValor = "0";
            nSwLeeAdId = 1;
         |SINO;
            nSwLeeAdId = 0;
         |FINSI;
      |FINSI;

      |SI nSwLeeAdId = 1;
         aTag = "Street"; |HAZ BuscaValor;
         |SI FSalida = 0;
              aAlfa = aValor; |HAZ QuitaRarosImp; aValor = aAlfa;
              #capcem01#2 = aValor;
         |FINSI;

         aTag = "Zip"; |HAZ BuscaValor;
         |SI FSalida = 0; #capcem01#3 = aValor; |FINSI;

         aTag = "City"; |HAZ BuscaValor;
         |SI FSalida = 0;
            aValor = aValor - "(";
            |SI FSalida ! 0;
               nCalc = ((FSalida / 100) ! 0) + 99;
               aAlfa = aValor % nCalc;
               #capcem01#4 = aAlfa;
               nCalc = ((nCalc - 99) * 100) + 99;
               aAlfa = aValor % nCalc; aAlfa = aAlfa - ")";
               |HAZ QuitaRarosImp;
               #capcem01#5 = aAlfa;
            |SINO;
               aAlfa = aValor; |HAZ QuitaRarosImp; aValor = aAlfa;
               #capcem01#4 = aValor;
            |FINSI;
         |FINSI;

         aTag = "Country"; |HAZ BuscaValor;
         |SI FSalida = 0; #capcem01#6 = aValor; |FINSI;
      |FINSI;
   |FINSI;

   aTag = "Currency"; |HAZ BuscaValor;
   |SI FSalida = 0; #capcem01#7 = aValor; |FINSI;

  ||SI nSwLeeTDef = 1;
  |SI nSwLeeName = 1;
     nSwLeeName = 0;
      aTag = "Name"; |HAZ BuscaValor;
      |SI FSalida = 0;
         #capcem01#9 = aValor;
         |QBF aValor;
         |SI aValor = "Drittland";    #capcem01#11 = 715; |FINSI;
         |SI aValor = "Inland";       #capcem01#11 = 700; |FINSI;
         |SI aValor = "EU";           #capcem01#11 = 713; |FINSI;
         |SI aValor = "EU-ohneUstID"; #capcem01#11 = 700; |FINSI;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO RecortaLinea;
   aReg = aReg - aCRLF;
   |PARA; |SI FSalida ! 0; |HACIENDO; aReg = aReg - aCRLF; |SIGUE;
   aReg = aReg - aCR;
   |PARA; |SI FSalida ! 0; |HACIENDO; aReg = aReg - aCR; |SIGUE;
   aReg = aReg - aLF;
   |PARA; |SI FSalida ! 0; |HACIENDO; aReg = aReg - aLF; |SIGUE;

   aAlfa1 = aReg; nPos = 0;
   aAlfa2 = aAlfa1 % -101; aAlfa3 = "";
   |PARA nCalc1 = 2; |SI; |HACIENDO nCalc1 = nCalc1 + 1;
      nCalc = -((nCalc1 * 100) + 1);
      aAlfa2 = aAlfa1 % nCalc; aAlfa3 = aAlfa2 + aAlfa3; aAlfa3 = aAlfa3 % 0102;
      |SI aAlfa2 = ">"; nPos = nCalc1; |FINSI;
      |SI aAlfa3 = "</";
         |SI nPos ! 0; nCalc1 = nPos; |SAL; |FINSI;
      |FINSI;
   |SIGUE;
   nCalc1 = nCalc1 - 1;
   aAlfa = aReg % 0; nCalc = aAlfa; nCalc = nCalc - nCalc1;

   aAlfa = aReg; aAlfa2 = "";
   |PARA; |SI nCalc > 99; |HACIENDO;
      aAlfa1 = aAlfa % 0199; aAlfa2 = aAlfa2 + aAlfa1; aAlfa = aAlfa - aAlfa1;
      nCalc = nCalc - 99;
   |SIGUE;
   nCalc = nCalc + 100;
   aAlfa2 = aAlfa2 + (aAlfa % nCalc);
   aRegSob = aReg - aAlfa2; aReg = aAlfa2;

   aAlfa = aRegSob % 0; nSwLen = aAlfa;
   nSwLen = 220 - nSwLen;
|FPRC;

|PROCESO LeeDatos;
   aAlfa = aReg % 0; nCalc = aAlfa; nSwLen = 220 - nCalc;
   |PARA; |SI nCalc < 220; |HACIENDO;
      FSalida = 0; nContLinBlanco = 10;
      |PARA; |SI FSalida = 0; |HACIENDO;
         |XLEE nHandle, nSwLen, aAlfa;
         |SI FSalida = 0; nContLinBlanco = nContLinBlanco - 1; |FINSI;
         |SI nContLinBlanco = 0; |SAL; |FINSI;
      |SIGUE;
      aReg = aReg + aAlfa;
      |SI nContLinBlanco = 0; |SAL; |FINSI;

      |HAZ FiltraBlancos;
      aAlfa = aReg % 0; nCalc = aAlfa; nSwLen = 220 - nCalc;
   |SIGUE;
|FPRC;

|PROCESO FiltraBlancos;
   nSw = 0;
   aAlfa = aReg; aAlfa2 = "";
   |PARA; |SI aAlfa ! ""; |HACIENDO;
      aAlfa1 = aAlfa % 0101;
      aAlfa = aAlfa - aAlfa1;
      |SI aAlfa1 = "<"; |Y nSw = 0; nSw = 1; |FINSI;
      |SI aAlfa1 = aComillas; |Y nSw = 0; nSw = 1; |FINSI;
      |SI aAlfa1 = " "; |Y nSw = 1;
         nSw = 2; nContSpaces = 0;
      |FINSI;
      |SI nSw = 2;
         |SI aAlfa1 = " ";
            nContSpaces = nContSpaces + 1;
            |SI nContSpaces > 3; aAlfa1 = ""; |FINSI;
         |SINO;
            nSw = 0;
         |FINSI;
      |FINSI;
      aAlfa2 = aAlfa2 + aAlfa1;
   |SIGUE;
   aReg = aAlfa2;
|FPRC;

|PROCESO RevisaResultados;
   aAlfa = #capcem01#10; |QBF aAlfa;
   |SI aAlfa = "";
      aAlfa = #capcem01#8; |QBF aAlfa;
      |SI aAlfa ! "";
         aAlfa = #capcem01#8; |HAZ LimpiaNif1;
         #caclipro#9 = aAlfa;
         |LEE 000#caclipro.=;
         |SI FSalida = 0;
            aAlfa = #caclipro#24; aAlfa = aAlfa % 0102;
            |SI aAlfa = aAlfa1;
               #capcem01#10 = #caclipro#10;
               |GRABA 020#capcem01.a; |ACABA;
            |FINSI;
         |FINSI;
      |FINSI;

      #caw00103#0 = #capcem01#0;
      |LEE 000#caw00103.=;
      |SI FSalida ! 0;
         |DDEFECTO #caw00103;
         #caw00103#0 = #capcem01#0;
         #caw00103#1 = #capcem01#1;
         #caw00103#2 = #capcem01#10;
         #caw00103#3 = #capcem01#8;

         aAlfa = #capcem01#8; |HAZ LimpiaNif1;
         #caclipro#9 = aAlfa;
         |LEE 000#caclipro.=;
         |SI FSalida = 0;
            aAlfa = #caclipro#24; aAlfa = aAlfa % 0102;
            |SI aAlfa = aAlfa1
               #caw00103#4 = 1;
            |SINO;
               #caw00103#4 = 2;
            |FINSI;
         |SINO;
            #caw00103#4 = 2;
         |FINSI;

         |GRABA 020#caw00103.c;
      |FINSI;
   |FINSI;

   aAlfa = #capcem01#8; |QBF aAlfa;
   |SI aAlfa = "";
      #caw00103#0 = #capcem01#0;
      |LEE 000#caw00103.=;
      |SI FSalida ! 0;
         |DDEFECTO #caw00103;
         #caw00103#0 = #capcem01#0;
         #caw00103#1 = #capcem01#1;
         #caw00103#2 = #capcem01#10;
         #caw00103#3 = #capcem01#8;

         aAlfa = #capcem01#8; |HAZ LimpiaNif1;
         #caclipro#9 = aAlfa;
         |LEE 000#caclipro.=;
         |SI FSalida = 0;
            aAlfa = #caclipro#24; aAlfa = aAlfa % 0102;
            |SI aAlfa = aAlfa1
               #caw00103#4 = 1;
            |SINO;
               #caw00103#4 = 2;
            |FINSI;
         |SINO;
            #caw00103#4 = 2;
         |FINSI;

         |GRABA 020#caw00103.c;
      |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE RevisaResultados;
#capcem01#1;
;
INICIO;
FINAL;
;
NULL, RevisaResultados;
|FIN;

|RUTINA inf;
   #caw00103#0 = 000000;
|FRUT;

|RUTINA sup;
   #caw00103#0 = 999999;
|FRUT;

|PROCESO VuelcaCambios;
   #capcem01#0 = #caw00103#0;
   |LEE 101#capcem01.=;
   |SI FSalida = 0;
      #capcem01#8  = #caw00103#3;
      #capcem01#10 = #caw00103#2;
      |GRABA 020#capcem01.a; |LIBERA #capcem01;
   |FINSI;
|FPRC;

|DEFBUCLE VuelcaCambios;
#caw00103#1;
;
INICIO;
FINAL;
;
NULL, VuelcaCambios;
|FIN;

|PROGRAMA;

   |ABRE #capcez02;
   |LEE 000#capcez02.p;
   |SI FSalida ! 0; |DDEFECTO #capcez02; |GRABA 020#capcez02.b; |FINSI;
   #caw00102#0 = #capcez02#0;

   aCRLF     = &13 + &10;
   aCR       = &13;
   aLF       = &10;
   aComillas = &34;

   |PUSHV 0501 2380;

   |PINPA #0#caw00102; |PINDA #0#caw00102;
|ET VuelveAPedir;
   |ENDATOS #1#caw00102;
   |SI FSalida = 0;
      aAlfa = #caw00102#0; |QBF aAlfa;
      |SI aAlfa = "";
         |MENAV "    Proceso cancelado"; |POPV; |ACABA;
      |FINSI;
      #capcez02#0 = #caw00102#0;

      |XABRE "E", aAlfa, nHandle;
      |SI FSalida < 0;
         |XABRE "CE", aAlfa, nHandle;
         |SI FSalida < 0;
            |MENAV "    Fichero inexistente";
            |VETE VuelveAPedir;
         |SINO;
            || Lo copio a una ubicacion del servidor
            aAlfa = #caw00102#0; |QBF aAlfa;
            aAlfa1 = aAlfa % -101; aAlfa = aAlfa % -299;
            |PARA; |SI aAlfa1 ! "/"; |Y aAlfa1 ! "\"; |HACIENDO;
               aAlfa2 = aAlfa1 + aAlfa2;
               aAlfa1 = aAlfa % -101; aAlfa = aAlfa % -299;
            |SIGUE;

            |DBASE aAlfa; |QBF aAlfa;
            aAlfa = aAlfa + "tmp/"; aDestino = aAlfa + aAlfa2;
            aOrigen = #caw00102#0; |QBF aOrigen;
            aIP = ""; |IP_REMOTO aIP;
            |SI aIP = "";
               |COPIA_FICHERO aOrigen, aDestino;
            |SINO;
               |RECIBE_FICHERO aOrigen, aDestino;
            |FINSI;
            #caw00102#0 = aDestino; |PINTA #caw00102#0;
         |FINSI;
      |FINSI;

      |GRABA 020#capcez02.a; |LIBERA #capcez02;

      |ABRE #capcem01;
      |ABRE #caclipro; |SELECT #4#caclipro;
      |ABRE #cacuenta;

      nSw = 0; nSwLen = 220;
      aAlfa = #caw00102#0; |QBF aAlfa;
      |HAZ Utf82Ansi;
      |XABRE "B", aFicDes, nHandle;
      |SI FSalida ] 0;
         |PARA; |SI nContLinBlanco > 0; |HACIENDO;
            |HAZ LeeDatos; |HAZ RecortaLinea;
            |HAZ Interpreta;

            aAlfa = aRegSob % 0; nCalc = aAlfa;
            |SI nCalc > 170;
               aReg = aRegSob; aRegSob = ""; |HAZ Interpreta;
               nSwLen = 220;
            |FINSI;

            aReg = aRegSob; aRegSob = "";
         |SIGUE;
         aReg = aRegSob; aRegSob = ""; |HAZ Interpreta;
         |XCIERRA nHandle;

         aAlfa = "tcl" + Usuario; |NOME_DAT #caw00103#aAlfa#;
         eaTempo = aAlfa;
         |DELINDEX #caw00103; |ABRE #caw00103;
         |BUCLE RevisaResultados;

         |PINPA #0#caw00103;
         |ENTLINEAL #1#caw00103, -1, 2, 22, 0, inf, sup;

         aAlfa = "     " + &191 + " Continuar con los cambios ?";
         |CONFI aAlfa;
         |SI FSalida = 0;
            |BUCLE VuelcaCambios;
         |FINSI;
         |CIERRA #caw00103; |DELINDEX #caw00103;
      |FINSI;
      |CIERRA #capcem01;
      |SELECT #1#caclipro; |CIERRA #caclipro;
      |CIERRA #cacuenta;
      |FBORRA aFicDes;
   |SINO;
      |MENAV "    Proceso cancelado";
   |FINSI;
   |POPV;

   |CIERRA #capcez02;
|FPRO;

|PROCESO QuitaRarosImp;
     |QBF aAlfa;
     aLon = aAlfa % A0;
     aDato = "";
     |PARA i = 0; |SI i [ aLon; |HACIENDO i = i + 1;
          nPosi = (i + 1) * 100 + 1;
          aCara = aAlfa % nPosi;
          nCara = &aCara;
          |SI nCara = 241; aCara = "¤"; |FINSI;
          |SI nCara = 209; aCara = "¥"; |FINSI;
          |SI nCara = 208; aCara = "¥"; |FINSI; ||no se porque
          |SI nCara = 225; aCara = " "; |FINSI;
          |SI nCara = 233; aCara = "‚"; |FINSI;
          |SI nCara = 237; aCara = "¡"; |FINSI;
          |SI nCara = 243; aCara = "¢"; |FINSI;
          |SI nCara = 250; aCara = "£"; |FINSI;
          |SI nCara = 193; aCara = "A"; |FINSI;
          |SI nCara = 201; aCara = "E"; |FINSI;
          |SI nCara = 205; aCara = "I"; |FINSI;
          |SI nCara = 211; aCara = "O"; |FINSI;
          |SI nCara = 218; aCara = "U"; |FINSI;
          |SI nCara = 186; aCara = "§"; |FINSI;
          |SI nCara = 170; aCara = "¦"; |FINSI;
          |SI nCara = 176; aCara = "°"; |FINSI;     ||Euro
          aDato = aDato + aCara;
     |SIGUE;
     aAlfa = aDato;
|FPRC;

|PROCESO Utf82Ansi;
     aFicOrg = aAlfa;

     aIP = "";
     |IP_REMOTO aIP;
     |DBASE aBase;
     |ESPECIFICA "RBASS", aLocal;
     aOrigen = aBase + "plugin/ansitoutf8.exe"
     aDestino = aLocal + "/bin/ansitoutf8.exe";
     aConte = aOrigen;
     |ESPECIFICA "MD5", aConte;
     aVer1 = FSalida;
     aConte = aDestino;
     |ESPECIFICA "REMOTOMD5", aConte;
     aVer2 = FSalida;
     |SI aVer1 ! aVer2;
          |SI aIP ! "";
               |ENVIA_FICHERO aOrigen, aDestino;
          |SINO;
               |COPIA_FICHERO aOrigen, aDestino;
          |FINSI;
     |FINSI;

     aFicDes = aLocal + Usuario + ".des";
     |SI aIP = "";
          |COPIA_FICHERO aFicOrg, aFicDes;
     |SINO;
          |ENVIA_FICHERO aFicOrg, aFicDes;
     |FINSI;

     aFicOrg = aLocal + Usuario + ".org";

     aAlfa = aDestino + " 1 " + aFicDes + " " + aFicOrg;
     aLon = aAlfa % A0;
     aExe = "";
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPosi = i * 100 + 1;
          aCara = aAlfa % nPosi;
          |SI aCara = "/"; aCara = "\"; |FINSI;
          aExe = aExe + aCara;
     |SIGUE;

     aExe = "cmd " + &34 + "/c START /MIN /WAIT " + aExe + &34;
     |RSYSTEM aExe;
     |RFBORRA aFicDes;

     |DFICE aFicDes; aFicDes = aFicDes + Usuario;
     |SI aIP = "";
          |COPIA_FICHERO aFicOrg, aFicDes;
     |SINO;
          |RECIBE_FICHERO aFicOrg, aFicDes;
     |FINSI;
     |RFBORRA aFicOrg;
|FPRC;
