|FICHEROS;
   cadiasim #0;

   camaasic #1;
   camaasil #2;
   cacuenta #5;
   caconimp #8;

   caparsim #10;
   caparsil #11;

   canempre #30;
   caselem1 #998;

   caw00020 #99;
   cawdatos #900;
|FIN;

|VARIABLES;
   &entrada = 1;
   mensa1 = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";

   aAlfa1    = "";
   aAlfa2    = "";
   aAlfa3    = "";
   nNume1    = 0;
   nNume2    = 0;
   nNume3    = 0;
   nPara1    = 0;
   nSwAlgun  = 0;
   nSwOpera  = 0;
   nDdepar   = 0;
   nHdepar   = 0;
   aFichero  = "";
   nContar   = 0;

   aTipo     = "";
   nCampo    = 0;
   nCamTot   = 0;
   nSwSI     = 0;
   nSigno    = 0;
   nColumna  = 0;
   aCta      = "";
   aDescrip  = "";

   nTImpr       = 0;
   nEmpresas    = 0;
   aAlfa        = "";
   nAscci       = 0;
   aLetra       = "";
   aDocumento   = "";
   aOrigen      = "";
   aDestino     = "";
   aIPRemoto    = "";
|FIN;

|INCLUYE dscont_i;

|| *********************************************************************
|CALCULO fechad;
 |SI #0#4 < fec1; |O #0#4 > fec2;
     |MENAV mensa1;
     |ERROR;
 |FINSI;
|FCAL;

|CALCULO fechah;
 |SI #0#5 < fec1; |O #0#5 > fec2; |O #0#5 < #0#4;
     |MENAV mensa1;
     |ERROR;
 |FINSI;
|FCAL;

|CALCULO nodepar; |TIPO 7;
   |C_M #0#11, 1, eaDepar;
   |C_M #0#12, 1, eaDepar;
|FCAL;

|CALCULO nosubde; |TIPO 7;
   |C_M #0#13, 1, eaSubde;
   |C_M #0#14, 1, eaSubde;
|FCAL;

|CALCULO PorPI; |TIPO 7;
   |C_M #0Campo,1,"S";
   |SI #0#16 = "R";
       #0Campo = "I"; |PINTA #0Campo;
       |C_M #0Campo,1,"N";
   |FINSI;
|FCAL;

|PROCESO DatosEmp; |TIPO 7;
 |ABRE #8;
 |DDEFECTO #8;
 #8#0 = 1;
 |LEE 000#8=;
 |CIERRA #8;
 #0#15 = #8#7; |PINTA #0#15;
 aAlfa1 = #8#8; |C_M #0#15,1,aAlfa1;
|FPRC;
|| ----------------------------------------------------------------------
|PROCESO CargaVariables;
     nEmpresas  = nEmpresas  + 1;

     aAlfa     = #99#80;
     aAlfa     = "A" + nEmpresas + "," + aAlfa;
     |ALFACALLDLL "agixl97.dll","poke_dato",aAlfa;

     aAlfa     = "B" + nEmpresas + "," + #99#4;
     |ALFACALLDLL "agixl97.dll","poke_dato",aAlfa;

     nAscci = 66;
     |PARA nPara1 = 5; |SI nPara1 [ 74; |HACIENDO nPara1 = nPara1 + 1;

           nAscci = nAscci + 1;
           |SI nAscci < 91;
               aLetra = &nAscci;
           |SINO;
               nNume1 = nAscci - 26;
               aLetra = "A" + &nNume1;
           |FINSI;
           |SI #99nPara1 ! 0;
               aAlfa     = aLetra + nEmpresas + "," + #99nPara1;
               |ALFACALLDLL "agixl97.dll","poke_dato",aAlfa;
           |FINSI;
           |SI nPara1 = 20; nPara1 = 24; |FINSI;
           |SI nPara1 = 36; nPara1 = 44; |FINSI;
           |SI nPara1 = 54; nPara1 = 59; |FINSI;
           |SI nPara1 = 63; nPara1 = 74; |FINSI;
   |SIGUE;
|FPRC;

|PROCESO PieDocumento;
 |DDEFECTO #99;
 #99#0  = enEmpresa;
 #99#1  = enEjercicio;
 #99#2  = nContar + 1;
 #99#3  = "00.00.0000";
 #99#4  = "TOTAL ........................";
 #99#75 = 0;
 |PARA nPara1 = 18; |SI nPara1 [ 91; |HACIENDO nPara1 = nPara1 + 1;
       nNume1 = nPara1 - 13;
       |SI nPara1 ] 88; nNume1 = nNume1 + 1; |FINSI;
       #99nNume1 = #0nPara1;
 |SIGUE;
 #99#80 = "     ";
 |HAZ CargaVariables;
|FPRC;

|PROCESO LeeAuxiliar;
 |SI nTImpr = 0;
     |PRINT;
 |SINO;
     |HAZ CargaVariables;
 |FINSI;
 |PARA nPara1 = 18; |SI nPara1 [ 91; |HACIENDO nPara1 = nPara1 + 1;
       nNume1 = nPara1 - 13;
       |SI nPara1 ] 88; nNume1 = nNume1 + 1; |FINSI;
       #0nPara1 = #0nPara1 + #99nNume1;
 |SIGUE;
|FPRC;

|DEFBUCLE LeeAuxiliar;
 #99#1;
 ;
 enEmpresa, enEjercicio, 0000000001, 00000;
 enEmpresa, enEjercicio, 9999999999, 00000;
 ;
 NULL, LeeAuxiliar;
|FIN;

|PROCESO CopiaFichero;
     aAlfa = "C:\MODELOS";                   |RMKDIR aAlfa;
     aAlfa = "C:\MODELOS\DOCUMENTOS";        |RMKDIR aAlfa;

     aOrigen   = aBase + "basica/word/" + aDocumento;
     aDestino  = "C:/MODELOS/DOCUMENTOS/" + aDocumento;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |SI aIPRemoto ! "";
         |ENVIA_FICHERO aOrigen, aDestino;
     |SINO;
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;
|FPRC;

|PROCESO AsignaCabecera;

     aAlfa = "A1," + #0#9; || Titulo
     |ALFACALLDLL "agixl97.dll","poke_dato",aAlfa;

|FPRC;

|PROCESO EmiteDocumento;
     |HAZ CopiaFichero;

     |CARGADLL "agixl97.dll";
     |SI FSalida < 0;  |ACABA;  |FINSI;

     aAlfa = "C:\MODELOS\DOCUMENTOS\" + aDocumento;
     |ALFACALLDLL "agixl97.dll", "carga_book", aAlfa;
     |ALFACALLDLL "agixl97.dll", "selecciona_hoja", "Intro";

     |HAZ AsignaCabecera;

     |MENSA "      Cargando Campos al Documento, espere un Momento.";

     nEmpresas = 6;
     |BUCLE LeeAuxiliar;
     |HAZ PieDocumento;

     |MENSA "      Imprimiendo el Documento, espere un Momento.";

     |SI #0#17 = "I";
         |ALFACALLDLL "agixl97.dll","fin_book", "imprime";
     |SINO;
         |ALFACALLDLL "agixl97.dll","fin_book", "";
     |FINSI;

     |DESCARGADLL "agixl97.dll";

     |RFBORRA aDestino;
|FPRC;

|| ----------------------------------------------------------------------
|PROCESO BuscaCta;
 nSwSI = 0;

 |ABRE #10;
 |DDEFECTO #10;

 |ABRE #11;
 |SELECT #2#11;
 |DDEFECTO #11;
 #11#6 = aCta;
 |LEE 001#11=;
 |SI FSalida = 0;
     #10#0 = #11#0;
     #10#1 = #11#1;
     #10#2 = #11#2;
     #10#3 = #11#3;
     #10#4 = #11#4;
     |LEE 001#10=;
     |SI FSalida = 0;
         nSwSI  = 1;
     |FINSI;
 |FINSI;
 |SELECT #1#11;
 |CIERRA #11;
 |CIERRA #10;
|FPRC;

|PROCESO CalculaCampo;
 |DDEFECTO #5;
 #5#0 = eaCuenta;
 |LEE 001#5=;

 aTipo    = #5#5;
 nSigno   = 1;
 nColumna = 0;
 nCampo   = 0;
 nCamTot  = 0;

 aAlfa1 = eaCuenta; |QBF aAlfa1;
 aAlfa2 = aAlfa1 % 0;
 nNume1 = aAlfa2;
 |PARA nPara1 = nNume1; |SI nPara1 ] 1; |HACIENDO nPara1 = nPara1 - 1;

       nNume2 = 100 + nPara1;
       aAlfa2 = nNume2;
       aCta   = eaCuenta % aAlfa2;
       |HAZ BuscaCta;
       |SI nSwSI = 1;
           |SAL;
       |FINSI;
 |SIGUE;

 |SI nSwSI = 0;
     aAlfa1 = "    Error!!!. La Cuenta " + eaCuenta + " no esta definida en Diario Simplificado";
     |MENAV aAlfa1;
     |ACABA;
 |FINSI;

  nColumna = #10#8;
  aTipo    = #10#1;
  nSigno   = 1;
  nCampo   = 0;

 |SI aTipo = "A";  ||Activo  de 5 a 20 (24)
     nCampo = 4 + nColumna;
     |SI nCampo > 20; nCampo = 4; |FINSI;  ||por ahora
     |SI #2#8 = "H"; nSigno = -1; |FINSI;
     nCamTot = 76;
 |FINSI;

 |SI aTipo = "P";  ||Pasivo  de 25 a 36 (44)
     nCampo = 24 + nColumna;
     |SI nCampo > 36; nCampo = 25; |FINSI;  ||por ahora
     |SI #2#8 = "D"; nSigno = -1; |FINSI;
     nCamTot = 77;
 |FINSI;

 |SI aTipo = "D";  ||Debe  de 45 a 54 (59)
     nCampo = 44 + nColumna;
     |SI nCampo > 54; nCampo = 45; |FINSI;  ||por ahora
     |SI #2#8 = "H"; nSigno = -1; |FINSI;
     nCamTot = 78;
 |FINSI;

 |SI aTipo = "H";  ||Haber  de 60 a 74
     nCampo = 59 + nColumna;
     |SI nCampo > 63; nCampo = 60; |FINSI;  ||por ahora
     |SI #2#8 = "D"; nSigno = -1; |FINSI;
     nCamTot = 79;
 |FINSI;
|FPRC;


|PROCESO LeeLineas;

  |SI Haybasica = 1;
      nDepar  = #2#21; |SI nDepar = 0;  nDepar = 1;   |FINSI;
      nDdepar = #0#11;
      nHdepar = #0#12;
  |FINSI;

  |SI nSwOpera = 0;

     |SI Haybasica = 1;
         |SI nDepar < nDdepar; |O nDepar > nHdepar; |ACABA; |FINSI;
     |SINO;
         |SI #2#21 < #0#11;  |O #2#21 > #0#12;  |ACABA;  |FINSI;
     |FINSI;
     |SI #2#28 < #0#13; |O #2#28 > #0#14; |ACABA; |FINSI;

     nSwAlgun = 1;
     |ERROR10;
     |ACABA;
  |FINSI;

  |DDEFECTO #99;
  #99#0   = enEmpresa;
  #99#1   = enEjercicio;
  #99#2   = nContar;
  #99#75  = #2#3;
  |LEE 101#99=;
  |SI FSalida ! 0;
      |DDEFECTO #99;
      #99#0   = enEmpresa;
      #99#1   = enEjercicio;
      #99#2   = nContar;
      #99#75  = #2#3;
      |GRABA 020#99b;
  |FINSI;

  eaCuenta = #2#4;
  |HAZ CalculaCampo;
  |SI nCampo ! 0;
      #99nCampo  = #99nCampo  + (nSigno * #2#9);
      #99nCamTot = #99nCamTot + (nSigno * #2#9);
  |SINO;
      #5#2 = "NO DEFINIDA DIARIO SIMPLIFICADO";
  |FINSI;
  aAlfa1 = eaCuenta; |QBF aAlfa1;
  aAlfa1 = aAlfa1 + " " + #5#2;
  #99#4 = aAlfa1;
  #99#80 = #2#1;

  |GRABA 020#99a;
  |LIBERA #99;

  aAlfa1 = aDescrip; |QBF aAlfa1;
  |SI aAlfa1 = "";  aDescrip = #2#7; |FINSI;

   #99#0   = enEmpresa;
   #99#1   = enEjercicio;
   #99#2   = nContar;
   #99#75  = 0;
   |LEE 101#99=;
   |SI FSalida ! 0;
      |DDEFECTO #99;
      #99#0   = enEmpresa;
      #99#1   = enEjercicio;
      #99#2   = nContar;
      #99#3   = #1#1; || Fecha
      #99#75  = 0;
      #99#80  = #2#1;
      |GRABA 020#99b;
   |FINSI;
   #99#4  = aDescrip;
   |SI nCampo ! 0;
      #99nCampo  = #99nCampo  + (nSigno * #2#9);
      #99nCamTot = #99nCamTot + (nSigno * #2#9);
   |FINSI;
   |GRABA 020#99a;
   |LIBERA #99;
|FPRC;

|DEFBUCLE LeeLineas;
 #2#1;
 ;
 #1#0, #1#1, #1#2, 0001;
 #1#0, #1#1, #1#2, 9999;
 e;
 NULL, LeeLineas;
|FIN;

|PROCESO bucleimp;

   nSwAlgun = 0;
   nSwOpera = 0;
   |BUCLE LeeLineas;
   |SI nSwAlgun = 0;  |ACABA;  |FINSI;

   nContar  = nContar + 1;
   aDescrip = "";
   |DDEFECTO #99;
   #99#0   = enEmpresa;
   #99#1   = enEjercicio;
   #99#2   = nContar;
   #99#75  = 0;
   |LEE 101#99=;
   |SI FSalida ! 0;
      |DDEFECTO #99;
      #99#0   = enEmpresa;
      #99#1   = enEjercicio;
      #99#2   = nContar;
      #99#3   = #1#1; || Fecha
      #99#75  = 0;
      #99#80  = #2#1;
      |GRABA 020#99b;
   |FINSI;
   |LIBERA #99;

   nSwOpera = 1;
   |ABRE #5;
   |BUCLE LeeLineas;
   |CIERRA #5;
|FPRC;

|DEFBUCLE cadiasim1;
   #1#2;
   ;
   #0#0, #0#4, #0#7, #0#2;
   #0#1, #0#5, #0#8, #0#3;
   e;
   NULL, bucleimp;
|FIN;

|DEFBUCLE cadiasim2;
   #1#1;
   3;
   #0#7, #0#4, #0#2, #0#0;
   #0#8, #0#5, #0#3, #0#1;
   e;
   NULL, bucleimp;
|FIN;

|DEFBUCLE cadiasim3;
   #1#3;
   3,1,0;
   #0#2,  #0#0, #0#4, #0#7;
   #0#3,  #0#1, #0#5, #0#8;
   e;
   NULL, bucleimp;
|FIN;

|RUTINA inf99;
 #99#0  = enEmpresa;
 #99#1  = enEjercicio;
 #99#2  = 1;
 #99#75 = 0;
|FRUT;

|RUTINA sup99;
 #99#0 = enEmpresa;
 #99#1 = enEjercicio;
 #99#2 = 9999999999;
 #99#75 = 99999;
|FRUT;


|PROCESO HazTodo;

   nContar = 0;
   |ABRE #99;
   |SI #0#10 = 1; |BUCLE cadiasim1; |FINSI;
   |SI #0#10 = 2; |BUCLE cadiasim2; |FINSI;
   |SI #0#10 = 3; |BUCLE cadiasim3; |FINSI;
   |CIERRA #99;

   |SI nContar ! 0;
       |SI #0#15 = "S";
           eaIa = #30#21;
           |HAZ LeeDatosEmpresa;
       |FINSI;

       |PDEFECTO #0,18, 88;
       |SI nTImpr = 0;
           |BUCLE LeeAuxiliar;
           |PIEINF;
       |SINO;
           |HAZ EmiteDocumento;
       |FINSI;
   |FINSI;

|FPRC;

|PROCESO LaImpresion;

  |HAZ DirAplicSt;
  |SI Haybasica = 1; |Y #0#12 = "zz"; #0#12 = "99"; |FINSI;

  aFichero = ("0w" + Usuario) % 108;
  |NOME_DAT #99 aFichero;
  |ABRE #99; |CIERRA #99; |DELINDEX #99;

|| .... Llamar hoja Excel o impresora.
  |SI Haybasica = 0; |O #0#16 = "R";
      |IMPRE 0;
      |SI FSalida ! 0;|ACABA; |FINSI;
      |INFOR "cadiasim";
      |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;
      nTImpr = 0;
  |SINO;
      |DBASS aBase;   |QBF aBase;
      |CARGADLL "agixl97.dll";
      |SI FSalida < 0;
          |MENAV "0000No tiene instalada el agixl97.dll, Consulte a su Distribuidor";
          |ACABA;
      |FINSI;
      |DESCARGADLL "agixl97.dll";

      |SUB_EJECUTA_NP ":basica/wordm000;CREALOS";

      aDocumento = "exce0004.xls";
      nTImpr = 1;
  |FINSI;


   |SI #48#27 = "S"; |Y #0#16 = "R";
       |HAZ MultiEmpresa;
   |SINO;
      |DFICE dirfich0; |QBF dirfich0;
      |HAZ HazTodo;
  |FINSI;

  || Cerrar hoja excel
  |SI nTImpr = 0;
      |FININF;
      |FINIMP;
  |FINSI;

  |DELINDEX #99;
|FPRC;

|PROGRAMA;
   || si en "PARAMETRO" viene "WEB" sale al menu
   |CLS;
   |CABEZA "Libro Diario Simplificado";
   |HAZ inicio;

   |DDEFECTO #0;
   #0#4 = fec1;
   #0#5 = fec2;
   FSalida = 1;
   |PARA; |SI FSalida = 1; |HACIENDO;
          |PINPA #0#0;
          |PINDA #0#0;                    || Pinta los datos por defecto
          |ENDATOS #1#0;                  || Seleccion informe
          |SI FSalida ! 0;  |SAL;  |FINSI;
          |HAZ LaImpresion;
          |SI PARAMETRO ! "WEB";        || Controlamos que salga al menu
               FSalida = 1;             || Vuelve al bucle
          |SINO;
               FSalida = 0;
          |FINSI;
   |SIGUE;
|FPRO;
||********************************************************************

|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #2 dirfich0;
   |PATH_DAT #5 dirfich0;

   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 001;
 999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;
