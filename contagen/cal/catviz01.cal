|FICHEROS;
   catviz01;

   :/modelos/def/dsmom030;

   caivaasi;
   catvim01;
   catvim02;
   caivases;

   catviw01;
   catviw02;
   dscsvw10;

   calibros;
   caintiva;
   cainliva;

   caclipro;
   cacuenta;
   cacuebal;
   caivases;

   catplw06;

   &regisnif@ #709;
   :/basica/def/agim0003  #710;
   :/integral/def/dsam0103 #711;

   &poblaci@  #703;
   &sgpaises@ #718;

   ca8cumay #410;
   ca8m0002 #422;
|FIN;

|VARIABLES;
   &entrada = 1;

   nInkey    = 0;
   aAlfa     = "";
   aAlfa1    = "";
   aAlfa2    = "";
   aAlfa3    = "";
   aAlfa4    = "";
   alfa1     = "";
   alfa2     = "";
   alfa3     = "";
   alfa4     = "";
   nSwNivel  = 0;
   nNume1    = 0;
   nNume2    = 0;
   nNumBase  = 0;
   nPara1    = 0;
   aLong     = "";
   nLong     = 0;
   aLee      = "";
   aVar      = "";
   aCarac    = "";
   nHandle   = 0;
   nSw       = 0;
   nSwHay    = 0;

   nLinFic    = 0;
   aLinea     = "";
   aRuta      = "";
   aAbre      = "";
   aBorra     = "";
   aHora      = "";
   aUsuario   = "";
   aUsuari1   = "";

   nSumaAsto   = 0;
   nSumaFras   = 0;
   nSumaCtas   = 0;
   nTotalBase  = 0;
   nTotalCuota = 0;
   nTotalFras  = 0;
   nCuotaIVA   = 0;
   nTotal      = 0;

   aQueQuiero = "";
   aNumCampos = "";
   nNumCampos = 0;

   nError      = 0;
   aFichero    = "";
   aFicBrowse  = "";
   aVer1       = "";
   aVer2       = "";
   {1}aConte   = "";
   aOrigen     = "";
   aDestino    = "";
   aPathLocal  = "";
   aPathTmp    = "";
   aPathContagen = "";
   aBaseConta  = "";
   aParam1     = "";
   aParam2     = "";
   aParam3     = "";
   aProg       = "";
   aDocRemoto  = "";
   nCarac     = 0;
   nPos       = 0;
   aCaracter  = "";
   aDocServidor = "";
   {1}aContiene  = "";

   nSalir   = 0;
   nReg     = 0;
   nCmp     = 0;
   nTCampos = 0;
   nTotCmp  = 0;
   aC10     = "";
   aC13     = "";
   SwDefine = 0;
   nTValor  = 0;
   nPorce   = 0;
   nCampo   = 0;

   aSerie    = "";
   aConcepto = "";
   nLibro    = 0;
   nRegistro = 0;
   aNombre   = "";
   aNif      = "";
   aContra   = "";

   &eaUnidadBasico = "";
   &eaIP           = "";
   &eaNomOri       = "";
|FIN;

|INCLUYE dscont_i;
|INCLUYE defec1_i;
|INCLUYE dscoiv_i;
|INCLUYE cla340_i;

|| ///////////////////////////////////  Fichero Nuevo de Historico
|PROCESO VerEste;
     #catvim01#0 = eaNomOri;
     aAlfa  = #catvim01#0;

     nLinFic = 0;
     #catvim01#0 = aAlfa;
     #catvim01#1 = 99;
     |LEE 041#catvim01.];
     |SI FSalida ! 0;
         |LEE 041#catvim01.u;
     |SINO;
         |LEE 041#catvim01.a;
     |FINSI;
     |SI FSalida = 0; |Y #catvim01#0 = aAlfa;
         nLinFic = #catvim01#1;
     |FINSI;
|FPRC;

|PROCESO CrearHist111;
      aUsuario = Usuario % 108;
      aUsuari1 = Usuario % 840;
      |ABRE #catvim01;
      |HAZ VerEste;

      |DDEFECTO #catvim01;
      #catvim01#0 = eaNomOri;
      #catvim01#1 = nLinFic + 1;
      #catvim01#2 = #catviz01#3;
      #catvim01#7  = ~;
      |HORA aHora;
      #catvim01#8  = aHora;
      #catvim01#9  = aUsuario;
      #catvim01#10 = aUsuari1;
      #catvim01#11 = enEjercicio;
      #catvim01#12 = enEmpresa;
      #catvim01#13 = enPeriodo;

      #catvim01#14 = nSumaAsto;
      #catvim01#15 = nSumaFras;
      #catvim01#16 = nTotalBase;
      #catvim01#17 = nTotalCuota;
      #catvim01#19 = nTotalFras;

      |GRABA 020#catvim01.n;
      |LIBERA #catvim01;
      |CIERRA #catvim01;
/*
      |PARA nPara1 = 0; |SI nPara1 [ nNumCampos; |HACIENDO nPara1 = nPara1 + 1;
            #catplw06#nPara1# = #catvim01#nPara1#;
      |SIGUE;
      |PINPA #0#catplw06;
      |PINDA #0#catplw06;
      |PAUSA;
*/
|FPRC;

|PROCESO ClaveBaja111;
     #catvim01#0 = "                                        ";
     #catvim01#1 = 01;
     #catvim01#7 = "01.01.1900";
     #catvim01#8 = "     ";
|FPRC;

|PROCESO ClaveAlta111;
     #catvim01#0 = "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz";
     #catvim01#1 = 99;
     #catvim01#7 = "31.12.2099";
     #catvim01#8 = "zzzzz";
|FPRC;

|| ////////////////////////////////////////////////////////////////////////

|PROCESO Browse;
     aFicBrowse = "F*.*";
     |BROWSE aFicBrowse;

     aAlfa      = aFicBrowse;   |QBF aAlfa;
     aFicBrowse = "";
     aLong      = aAlfa % 0;
     nLong      = aLong;
     |PARA nCarac = 2;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos  = (nCarac * 100) + 1;
           aFicBrowse = aFicBrowse + (aAlfa % nPos);
     |SIGUE;

     aAlfa = aFicBrowse;
     aRuta = "";
     aLong = aAlfa % 0;
     nLong = aLong;
     |PARA nCarac = 1;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos       = (nCarac * 100) + 1;
           aCaracter  = aAlfa % nPos;
           aRuta      = aRuta    + aCaracter;
           eaNomOri   = eaNomOri + aCaracter;

           |SI aCaracter = "/";  |O aCaracter = "\";
               eaNomOri = "";
           |FINSI;
     |SIGUE;
|FPRC;

|CALCULO PonFichero; |TIPO 0;
     nInkey = FSalida;

     |SI nInkey = 11;
         |ABRE #catvim01;
         #catvim01#0 = #catviz01#3;
         |CONSULTA_F_CLAVE #2#catvim01, ClaveBaja111, ClaveAlta111;
         |CIERRA #catvim01;
         |ERROR;
         |ACABA;
     |FINSI;

     |SI nInkey = 10;
         |HAZ Browse;
         #catviz01#Campo# = aFicBrowse; || aRuta;
     |FINSI;
     |PINTA #catviz01#Campo#;

     aAlfa3 = #catviz01#Campo#; |QBF aAlfa3;
     |SI aAlfa3 = "";
         |MENAV "0000Error. Introduzca Fichero a Importar";
         |ERROR;
         |ACABA;
     |FINSI;
     aAlfa1 = #catviz01#Campo#; |QBT aAlfa1;
     |SI aAlfa1 ! aAlfa3;
         |MENAV "0000Error. El nombre del Fichero no puede contener espacios en Blanco";
         |ERROR;
         |ACABA;
     |FINSI;

     |ABRE #catvim01;
     |HAZ VerEste;
     |CIERRA #catvim01;
/*
     |SI nLinFic ! 0;
         aAlfa = "0000NFichero Ya importado con Fecha " + #catvim01#7;
         aAlfa = aAlfa + " .Desea volverlo a Importar";
         |CONFI aAlfa;
         |SI FSalida ! 0;
             #catviz01#Campo# = ""; |PINTA #catviz01#Campo#;
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;
*/
|FCAL;

|CALCULO TrasLibro; |TIPO 6;
   |C_M #catviz01#Campo#, 0, aAlfa;
   |SI aAlfa = "N"; |ACABA; |FINSI;

   |ABRE #calibros;
   #calibros#0 = #catviz01#Campo#;
   |LEE 010#calibros.=;
   |SI FSalida ! 0;
      |MENAV "     No existe el libro";
      |CIERRA #calibros;
      |ERROR;
      |ACABA;
   |FINSI;
   |CIERRA #calibros;
   |SI Campo = 7; |Y #calibros#2 ! "R";
       |MENAV "     Libro de IVA No Correcto. No es de Repercutido";
       |ERROR;
       |ACABA;
   |FINSI;
   |SI Campo = 8; |Y #calibros#2 ! "S";
       |MENAV "     Libro de IVA No Correcto. No es de Soportado";
       |ERROR;
       |ACABA;
   |FINSI;
|FCAL;

|CALCULO TrasConcepto;  |TIPO 0;
   |C_M #catviz01#Campo#, 0, aAlfa;
   |SI aAlfa = "N"; |ACABA; |FINSI;

   enLibIVA = #catviz01#7;
   aTipoIVA = "R";
   |SI Campo = 10;
       enLibIVA = #catviz01#8;
       aTipoIVA = "S";
   |FINSI;

   enFilConc = 2;          || Concepto de IVA
   eaCuenta = " ";
   eaReperc = " ";
   enConcepto = #catviz01#Campo#;
   |HAZ FiltroConcepto;  |SI eswLect ! 0; |ACABA; |FINSI;
   nNume1 = Campo + 2;
   #catviz01#nNume1# = #dsmom030#1; |PINTA #catviz01#nNume1#;
|FCAL;

|PROCESO Tipo12_z; |TIPO 12;
   || .... Para que no pregunte conforme S/N
|FPRC;

|| ////////////////////////////////////////////////////////////////////////
|PROCESO nuSacaNivel;

 |QBF eaCuenta;
 alfa1  = eaCuenta % 0;
 nNume1 = alfa1;

 |SI nNume1 < MaximoDigitos;
     nNume2 = MaximoDigitos - nNume1;
     alfa4 = "0" * nNume2;

     nSwNivel = 0;
     aAlfa = eaCuenta % 103;
     |SI aAlfa = "472"; |O aAlfa = "477";
        nSwNivel = 1;
     |SINO;
        |SI nNume1 [ 6;
            |SI aAlfa = "400"; |O aAlfa = "410"; |O aAlfa = "430";
                nSwNivel = 1;
            |FINSI;
        |FINSI;
     |FINSI;

     |SI nSwNivel = 0;
         alfa1 = eaCuenta % 104;
         alfa2 = eaCuenta % 508;
     |SINO;
         alfa1 = eaCuenta % 103;
         alfa2 = eaCuenta % 409;
     |FINSI;
     |QBF alfa2;

     alfa3 = alfa1 + alfa4 + alfa2;
     eaCuenta = alfa3;

 |FINSI;

 |HAZ SacaNivel;
|FPRC;
|| ////////////////////////////////////////////////////////////////////////
|PROCESO Conversion;
     nError  = 0;

     aAlfa = aPathLocal + "*.csv";
     |R_PDIR aAlfa;
     |PARA;  |SI FSalida = 0;  |HACIENDO;
          |RFBORRA aAlfa;

          |R_SDIR aAlfa;
     |SIGUE;

     aParam2 = aPathLocal + "iva.txt";  |RFBORRA aParam2;

     aVer1   = "";
     aVer2   = "";
     aConte  = aBaseConta + "plugin/dsofficetxt.exe";
     |ESPECIFICA "MD5", aConte;
     aVer1 = FSalida;

     aConte  = aPathLocal + "dsofficetxt.exe";
     |ESPECIFICA "REMOTOMD5", aConte;
     aVer2 = FSalida;

     |SI aVer1 ! aVer2;
         aOrigen  = aBaseConta + "plugin/dsofficetxt.exe";
         aDestino = aPathLocal + "dsofficetxt.exe";

         |SI eaIP = "";
             |COPIA_FICHERO aOrigen, aDestino;
         |SINO;
             |ENVIA_FICHERO aOrigen, aDestino;
         |FINSI;

         aAlfa = aPathLocal + "dsofficetxt.exe";
         |XABRE "EC", aAlfa, 0;
         |SI FSalida < 0;
             |MENAV "0000No encuentro el conversor a CSV, por favor consulte con su administrador informatico.";
             nError     = 1;
             aFicBrowse = "";
             |ACABA;
         |FINSI;
     |FINSI;

     aProg    = aPathLocal + "dsofficetxt.exe";
     aParam1  = aFicBrowse;
     aParam2  = aPathLocal + "iva.csv";
     aParam3  = "";

     aAlfa  = "cmd " + &34 + "/c START /WAIT " + aProg + " " + aParam1 + " " + aParam2 + aParam3 + &34;
     |RSYSTEM aAlfa;

     |XABRE "EC", aParam2, 0;
     |SI FSalida < 0;
         nError     = 1;
         aFicBrowse = "";
         |MENAV "0000No se ha podido convertir a CSV el fichero seleccionado.";
         |ACABA;
     |FINSI;

     aFicBrowse = aParam2;
|FPRC;

|| //////////////////////////////////////////////////////////////////////
|PROCESO Limpio_viw2;
   eaCuenta = #catviw02#11; |HAZ nuSacaNivel; #catviw02#11 = eaCuenta;
   eaCuenta = #catviw02#13; |HAZ nuSacaNivel; #catviw02#13 = eaCuenta;
   |PARA nPara1 = 17; |SI nPara1 [ 36; |HACIENDO nPara1 = nPara1 + 1;
         aAlfa = #catviw02#nPara1#; |QBF aAlfa;
         |SI aAlfa ! "";
             eaCuenta = #catviw02#nPara1#; |HAZ nuSacaNivel;
             #catviw02#nPara1# = eaCuenta;
         |FINSI;
   |SIGUE;
|FPRC;

|PROCESO FicViw02;
    nCampo = #dscsvw10#0;
    aAlfa  = #dscsvw10#1; |QBT aAlfa;
    aAlfa  = aAlfa > " ";
    aAlfa1 = aAlfa % 105;
    aAlfa2 = aAlfa % 102;
    aAlfa3 = aAlfa % 101;

    |SI aAlfa = ""; |ACABA; |FINSI;
    aAlfa4 = aAlfa - "CLIENTE";
    nNume2 = FSalida;

    nNume1 = 0;
    |SI aAlfa  = "FDEPAGO";                        nNume1 = 6;  |FINSI;
    |SI aAlfa  = "TCLIENTE";                       nNume1 = 7;  |FINSI;
    |SI nNume2 ! 0; |Y aAlfa ! "TCLIENTE";         nNume1 = 8;  |FINSI;
    |SI aAlfa  = "N§CLIENTE"; |O aAlfa = "CODIGO"; nNume1 = 8;  |FINSI;
    |SI aAlfa  = "NOMBRE";                         nNume1 = 9;  |FINSI;
    |SI aAlfa2 = "47";                             nNume1 = 10; |FINSI;
    |SI aAlfa2 = "55";                             nNume1 = 12; |FINSI;
    |SI aAlfa1 = "TOTAL";                          nNume1 = 14; |FINSI;

    |SI nNume1 ! 0;
        #catviw02#nNume1# = nCampo;
        |SI nNume1 = 10; |O nNume1 = 12;
            nNume1 = nNume1 + 1;
            #catviw02#nNume1# = aAlfa;
        |FINSI;
    |SINO;
        |SI aAlfa3 = "7"; |O aAlfa3 = "6";
            nNumBase = nNumBase + 1;
            nNume1   = nNumBase + 16;
            #catviw02#nNume1# = aAlfa;
            |SI nNumBase = 1;
                #catviw02#15 = nCampo;
            |FINSI;
            #catviw02#16 = nCampo;
            |SI aAlfa3 = "7"; #catviz01#13 = "R"; |FINSI;
            |SI aAlfa3 = "6"; #catviz01#13 = "S"; |FINSI;
        |FINSI;
    |FINSI;
|FPRC;

|PROCESO dscsvw10Carga;
     |SI SwDefine = 0;
         |HAZ FicViw02;
         |ACABA;
     |FINSI;

     nCampo = #dscsvw10#0;
     nNume1 = nCampo;
     |SI nCampo > 5;
         nNume1 = -1;
         |SI nCampo = #catviw02#6;  nNume1 = 6; |FINSI;
         |SI nCampo = #catviw02#7;  nNume1 = 7; |FINSI;
         |SI nCampo = #catviw02#8;  nNume1 = 8; |FINSI;
         |SI nCampo = #catviw02#9;  nNume1 = 9; |FINSI;
         |SI nCampo = #catviw02#10; nNume1 = 10;|FINSI;
         |SI nCampo = #catviw02#12; nNume1 = 11;|FINSI;
         |SI nCampo = #catviw02#14; nNume1 = 12;|FINSI;
         |SI nCampo ] #catviw02#15; |Y nCampo [ #catviw02#16;
             nNume1 = nCampo - #catviw02#15 + 1;
             nNume1 = nNume1 + 12;
         |FINSI;
     |FINSI;
     |SI nNume1 ! -1;
         #catviw01#nNume1# = #dscsvw10#1;
     |FINSI;
|FPRC;

|DEFBUCLE dscsvw10Carga;
     #dscsvw10#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dscsvw10Carga;
|FIN;

|PROCESO dscsvw10Campos;
     nTCampos  = nTCampos + 1;
|FPRC;

|DEFBUCLE dscsvw10Campos;
     #dscsvw10#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dscsvw10Campos;
|FIN;

|PROCESO GrabaDatos;
     |CIERRA #dscsvw10;
     nTCampos = 0;
     |BUCLE dscsvw10Campos;

     nError   = 0;
     nTValor  = nTotCmp;
     |SI nTCampos < nTValor;
         |DELINDEX #dscsvw10;
         |ACABA;
     |FINSI;

     nPorce = ((nTValor * 100) / nTCampos) ! 0;
     |SI nPorce < 70;
         |DELINDEX #dscsvw10;
         |ACABA;
     |FINSI;

     nReg = nReg + 1;
     |SI nReg = 1;
         |DELINDEX #dscsvw10;
         |ACABA;
     |FINSI;

     |SI SwDefine = 0;
         nNumBase  = 0;
         |DDEFECTO #catviw02;
     |FINSI;

     |DDEFECTO #catviw01;
     |BUCLE dscsvw10Carga;
     |SI SwDefine = 0;
         |HAZ Limpio_viw2;
         SwDefine = 1;
     |SINO;
         #catviw01#0 = nReg;
         |GRABA 020#catviw01.n;
     |FINSI;
     |LIBERA #catviw01;

     |DELINDEX #dscsvw10;
     |PULSATECLA;
|FPRC;

|PROCESO Grabaw10;
     #dscsvw10#0 = nCmp;
     #dscsvw10#1 = aVar;
     |GRABA 020#dscsvw10.n;
     |LIBERA #dscsvw10;
|FPRC;

|PROCESO SacaDatos;
     aLong  = aLee % 0;
     nLong  = aLong;
     aC10   = &10;
     aC13   = &13;
     |PARA nCarac = 1;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos   = (nCarac * 100) + 1;
           aCarac = aLee % nPos;

           |SI aCarac = aC13;
               aCarac = ";";
           |FINSI;

           |SI aCarac = ";";
               nCmp   = nCmp + 1;
               |HAZ Grabaw10;

               aVar   = "";
               aCarac = "";
           |FINSI;

           |SI aCarac = aC10;
               aCarac = "";
           |FINSI;

           |SI nCmp = nTotCmp;  |Y nCmp ! 0;
               |HAZ GrabaDatos;

               |ABRE #dscsvw10;

               nCmp = 0;
           |FINSI;

           aVar = aVar + aCarac;
      |SIGUE;
|FPRC;

|PROCESO LeeCSV;
     aAlfa = "Procesando registros del fichero de Facturas IVA";
     |PINTA 2205, aAlfa;

     |ABRE #dscsvw10;
     SwDefine = 0;
     nCmp     = 0;
     nReg     = 0;
     |XABRE "B", aAbre, nHandle;
     |PARA;  |SI;  |HACIENDO;
          |XLEE nHandle, 255, aLee;
          |SI FSalida [ 0;  |SAL;  |FINSI;

          |HAZ SacaDatos;
     |SIGUE;
     |XCIERRA nHandle;

     |LEE 000#dscsvw10.p;
     |SI FSalida = 0;
         nCmp   = nCmp + 1;
         |HAZ Grabaw10;
         |HAZ GrabaDatos;
     |FINSI;
|FPRC;

|PROCESO SacaDatosEspecial;
     aLong  = aLee % 0;
     nLong  = aLong;
     aC10   = &10;
     aC13   = &13;
     |PARA nCarac = 1;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos   = (nCarac * 100) + 1;
           aCarac = aLee % nPos;

           |SI aCarac = aC13;
               nTotCmp  = nTotCmp + 1;
               nSalir = 1;
               |SAL;
           |FINSI;

           |SI aCarac = ";";
               nTotCmp  = nTotCmp + 1;
           |FINSI;
      |SIGUE;
|FPRC;

|PROCESO LeeCSVPrimero;
     nSalir  = 0;
     nTotCmp = 0;
     |XABRE "B", aAbre, nHandle;
     |PARA;  |SI;  |HACIENDO;
          |XLEE nHandle, 255, aLee;
          |SI FSalida [ 0;  |SAL;  |FINSI;

          |HAZ SacaDatosEspecial;
          |SI nSalir = 1; |SAL; |FINSI;
     |SIGUE;
     |XCIERRA nHandle;
|FPRC;

|PROCESO excel;
     |HAZ Conversion;

     aOrigen  = aFicBrowse;
     aDestino = aPathTmp + "iva.csv";
     |SI eaIP = "";
        |COPIA_FICHERO aOrigen, aDestino;
     |SINO;
        |RECIBE_FICHERO aOrigen, aDestino;
     |FINSI;
     aFicBrowse = aDestino;

     aAbre = aFicBrowse;  |QBF aAbre;
     |SI aAbre = ""; nError = 1;  |ACABA;  |FINSI;

     aFichero = "catviw01" + Usuario;  |NOME_DAT #catviw01#aFichero#;
     aFichero = "dscsvw10" + Usuario;  |NOME_DAT #dscsvw10#aFichero#;

     |ABRE #catviw01;  |CIERRA #catviw01;  |DELINDEX #catviw01;
     |DDEFECTO #catviw02;

     |HAZ LeeCSVPrimero;

     |ABRE #catviw01;
     |HAZ LeeCSV;

|| .. ....... . . .          |CONSULTA_CLAVE #1#catviw01;
     |CIERRA #catviw01;

     |SI nReg = 0;
         |MENAV "0000No Hay datos para Pasar";
         nError = 1;
     |FINSI;
|FPRC;
|| *********************************************************************
|PROCESO LeoRegistro;
   nRegistro = 0;
   |ABRE #cainliva;
   #cainliva#0 = nLibro;
   #cainliva#1 = 999;
   |LEE 041#cainliva.];
   |SI FSalida = 0;
       |LEE 041#cainliva.a;
   |SINO;
       |LEE 041#cainliva.u;
   |FINSI;
   |SI FSalida = 0; |Y #cainliva#0 = nLibro;
       nRegistro = #cainliva#1;
   |FINSI;
   |CIERRA #cainliva;
|FPRC;

|PROCESO BorrarPrimero;
   |BORRA 020#cainliva.a;
   |LIBERA #cainliva;
|FPRC;

|DEFBUCLE BorrarPrimero;
   #cainliva#1;
   ;
   nLibro, 001;
   nLibro, 999;
   be;
   NULL, BorrarPrimero;
|FIN;

|PROCESO LaComprobacion;

   aTipoIVA = #catviz01#13;
   |SI aTipoIVA = "R";
       nLibro = #catviz01#7; aAlfa = "Venta";
       enConcepto = #catviz01#9;
       aConcepto  = #catviz01#11;
       aSerie     = #catviz01#14;
   |SINO;
       nLibro = #catviz01#8; aAlfa = "Compra";
       enConcepto = #catviz01#10;
       aConcepto  = #catviz01#12;
       aSerie     = #catviz01#15;
   |FINSI;

   nError = 0;
   |HAZ LeoRegistro;
   |SI nRegistro > 0;
       aAlfa = "0000SExisten Datos en la Introduccion de Facturas de " + aAlfa + ". Continuar ";
       |CONFI aAlfa;
       |SI FSalida ! 0;
           nError = 1;
           |ACABA;
       |FINSI;
       aAlfa = "0000NBorramos Datos Actuales";
       |CONFI aAlfa;
       |SI FSalida = 0;
           |BUCLE BorrarPrimero;
           |ABRE #caintiva;
           #caintiva#0 = nLibro;
           |LEE 141#caintiva.=;
           |SI FSalida = 0;
               |BORRA 040#caintiva.a;
           |FINSI;
           |LIBERA #caintiva;
           |CIERRA #caintiva;
           nRegistro = 0;
       |FINSI;
   |FINSI;
|FPRC;
|| ///////////////////////////////////////////////////////////////////////
|PROCESO AltaCuenta;
    |DDEFECTO #cacuenta;
    #cacuenta#0 = eaCuenta;
    #cacuenta#1 = enNivel;
    #cacuenta#2 = #catviw01#9;
    #cacuenta#3 = "S";
    mascara = #cacuenta#0;
    |QBF mascara;
    mascara = mascara +"zzzzzzzzzzzz";
    mascara = mascara % 112;
    #cacuenta#54 = mascara;
    #cacuenta#55 = #cacuenta#1;
    NumMes = dig1; |HAZ NombreMes; #cacuenta#56 = NombreMes;
    NumMes = dig1 + 11; |SI NumMes > 12; NumMes = NumMes - 12; |FINSI;
    |HAZ NombreMes; #cacuenta#57 = NombreMes;
    |HAZ defect1;
    |GRABA 000#cacuenta.n;
|FPRC;

|PROCESO CamposCliente;
  aNombre = "";
  aNif    = "";
  |DDEFECTO #caclipro;
  |SI aTipoIVA = "R";
      #caclipro#23 = "C";
  |SINO;
      #caclipro#23 = "P";
  |FINSI;
  #caclipro#10 = eaCuenta;
  |LEE 041#caclipro.=;
  |SI FSalida = 0;
      aNombre = #caclipro#1;
      aNif    = #caclipro#9;
  |SINO;
      #cacuenta#0 = eaCuenta;
      |LEE 041#cacuenta.=;
      |SI FSalida = 0;
          aNombre = #cacuenta#2;
      |SINO;
          |HAZ AltaCuenta;
      |FINSI;
  |FINSI;
  aAlfa = #catviw01#9; |QBF aAlfa;
  |SI aAlfa ! "";
      aNombre = aAlfa;
  |FINSI;
  #cainliva#27 = aNombre;
  #cainliva#30 = aNif;
|FPRC;

|PROCESO BuscaContra;
  eaCuenta = aContra;
  |HAZ nuSacaNivel;
  aContra  = eaCuenta;

  enLineaIVA = 0;
  |DDEFECTO #catvim02;
  #catvim02#0 = aContra;
  |LEE 041#catvim02.=;
  |SI FSalida = 0;
      enLineaIVA = #catvim02#2;
  |FINSI;

  #cainliva#17 = aContra;
  #cainliva#18 = enNivel;
  #cainliva#28 = #catvim02#1;
  #cainliva#66 = aContra;

  enPorcIVA = 0;
  enPorcREC = 0;
  efFechaIVA = #cainliva#4;
  |HAZ SacaIVA;
  #cainliva#10 = enLineaIVA;
  #cainliva#13 = enPorcIVA;
  #cainliva#15 = enPorcREC;

|FPRC;

|PROCESO PasoFactura;
  nRegistro = nRegistro + 1;
  nNumBase  = nNumBase  + 1;
  #cainliva#1 = nRegistro;
  #cainliva#3 = nNumBase;

  nNume1 = nPara1 - 12;  || numero de base 1, 2, 3 ...
  nNume1 = nNume1 + 16;
  aContra = #catviw02#nNume1#;
  |HAZ BuscaContra;
  #cainliva#12 = #catviw01#nPara1#;
  |SI #cainliva#13 ! 0;
      #cainliva#14 = (#cainliva#12 % #cainliva#13) ! 2;
      #cainliva#20 = #catviw02#11;
      eaCuenta = #cainliva#20; |HAZ SacaNivel;
      #cainliva#21 = enNivel;
  |SINO;
      #cainliva#14 = 0;
      #cainliva#20 = "";
      #cainliva#21 = 0;
  |FINSI;
  #cainliva#29 = #cainliva#12 + #cainliva#14;
  #cainliva#67 = #cainliva#29;

  nCuotaIVA   = nCuotaIVA   + #cainliva#14;
  nTotal      = nTotal      + #cainliva#29;
  nSumaAsto   = nSumaAsto   + 1;
  nTotalBase  = nTotalBase  + #cainliva#12;
  nTotalCuota = nTotalCuota + #cainliva#14;
  nTotalFras  = nTotalFras  + #cainliva#29;

  |GRABA 020#cainliva.n;
  nSw = 1;
|FPRC;

|PROCESO LeoCatviw01;
  |DDEFECTO #cainliva;
  #cainliva#0 = nLibro;
  #cainliva#2 = #catviw01#3;
  #cainliva#4 = #catviw01#5;
  eaCuenta    = #catviw01#8; |HAZ nuSacaNivel;
  #cainliva#5  = eaCuenta;
  #cainliva#65 = eaCuenta;
  |HAZ CamposCliente;
  #cainliva#6 = enNivel;
  #cainliva#7 = enConcepto;
  #cainliva#8 = aConcepto;
  aAlfa = #cainliva#4;
  aAlfa = aAlfa % 0402;
  #cainliva#31 = aAlfa;
  #cainliva#38 = aTipoIVA;
  #cainliva#26 = aSerie;
  #cainliva#45 = #caivaasi#19;
  #cainliva#47 = #caivaasi#67;
  #cainliva#49 = "N";
  #cainliva#50 = aTipoIVA;
  #cainliva#53 = #catviw01#4;

  nNumBase  = 0;
  nCuotaIVA = 0;
  nTotal    = 0;
  |PARA nPara1 = 13; |SI nPara1 [ 32; |HACIENDO nPara1 = nPara1 + 1;
        |SI #catviw01#nPara1# ! 0;
            |HAZ PasoFactura;
        |FINSI;
  |SIGUE;
  nTotalFras = nTotalFras + 1;
|FPRC;

|DEFBUCLE LeoCatviw01;
  #catviw01#1;
  ;
  0000001;
  9999999;
  ;
  NULL, LeoCatviw01;
|FIN;

|PROCESO PasoIva;
   nSumaAsto   = 0;
   nSumaFras   = 0;
   nTotalBase  = 0;
   nTotalCuota = 0;
   nTotalFras  = 0;

   |HAZ LaComprobacion;
   |SI nError = 1; |ACABA; |FINSI;

   |ABRE #calibros;
   |DDEFECTO #calibros;
   #calibros#0 = nLibro;
   |LEE 010#calibros.=;
   |CIERRA #calibros;

   |ABRE #caintiva;
   |DDEFECTO #caintiva;
   #caintiva#0 = nLibro;
   |LEE 141#caintiva.=;
   |SI FSalida ! 0;
       #caintiva#0 = nLibro;
       #caintiva#1 = #calibros#1;
       |GRABA 020#caintiva.n;
   |FINSI;
   |LIBERA #caintiva;
   |CIERRA #caintiva;

   eDCl340 = #calibros#6;
   nIvaInc = 1;
   |HAZ LeeDefIVA;
   nIvaInc = 0;

   |ABRE #cainliva;
   |ABRE #cacuenta;
   |ABRE #caclipro;
   |ABRE #catvim02;
   |ABRE #dsmom030;

   |BUCLE LeoCatviw01;
   |CIERRA #dsmom030;
   |CIERRA #catvim02;
   |CIERRA #caclipro;
   |CIERRA #cacuenta;
   |CIERRA #cainliva;
|FPRC;

|PROCESO HazOperacion;
   aFicBrowse = #catviz01#3; |QBF aFicBrowse;
   |HAZ excel;
   |SI nError = 1; |ACABA; |FINSI;

   nSw    = 0;
   |HAZ PasoIva;
   |SI nSw = 1;
       |MENAV "0000Datos Pasados a Introduccion de Facturas";
       |HAZ CrearHist111;
   |FINSI;
   |DELINDEX #catviw01;
|FPRC;

|PROGRAMA;

   |CLS;
   |CABEZA "Importacion Vigoce";

   ewNif = 0;
   enSwDeDonde = -1;
   |HAZ DirAplicSt;
   |SI HayAcceso = 1;
       enSwDeDonde = 1;
       |REFERENCIA_DEF regisnif@, #711;
   |SINO;
       |SI Haybasica = 1;
           enSwDeDonde = 0;
           |REFERENCIA_DEF regisnif@, #710;
       |FINSI;
   |FINSI;
   |SI enSwDeDonde = -1; ewNif = 1; |FINSI; || no existe Basica ni Integrada

   |ABRE #caivaasi;
   |DDEFECTO #caivaasi;
   |LEE 041#caivaasi.p;
   |CIERRA #caivaasi;

   aQueQuiero = "|NC";
   aNumCampos = #catvim01#aQueQuiero#;
   nNumCampos = aNumCampos;
   nNumCampos = nNumCampos - 1;

   |ABRE #catviz01;
   #catviz01#0 = "A";
   |LEE 141#catviz01.=;
   |SI FSalida ! 0;
       |DDEFECTO #catviz01;
       #catviz01#7  = #caivaasi#3;
       #catviz01#8  = #caivaasi#29;
       #catviz01#9  = #caivaasi#5;
       #catviz01#10 = #caivaasi#31;
       #catviz01#14 = #caivaasi#63;
       #catviz01#15 = #caivaasi#65;
       |GRABA 020#catviz01.b;
   |FINSI;

   |PINPA #0#catviz01;
   |PINDA #0#catviz01;
   |ENDATOS #1#catviz01;
   |SI FSalida = 0;
       |SI #catviz01#1 = "SI";
           |HAZ HazOperacion;
       |FINSI;
       #catviz01#1 = "NO";
       #catviz01#3 = "";
       |GRABA 020#catviz01.a;
   |FINSI;
   |LIBERA #catviz01;
   |CIERRA #catviz01;
|FPRO;

|PROCESO Tipo80;  |TIPO 80;
     eaIP = "";  |IP_REMOTO eaIP;

     |HAZ LeeUnidadBasico;

     aPathLocal = eaUnidadBasico + "\documentos";   |RMKDIR aPathLocal;
     aPathLocal = aPathLocal + "\tmp";              |RMKDIR aPathLocal;
     aPathLocal = aPathLocal + "\";

     |DBASE aBaseConta;
     aPathTmp   = aBaseConta + "tmp";           |MKDIR aPathTmp;
     aPathTmp   = aPathTmp   + "/" + Usuario;   |MKDIR aPathTmp;
     aPathTmp   = aPathTmp   + "/";

     aPathContagen = eaUnidadBasico + "\documentos";   |RMKDIR aPathContagen;
     aPathContagen = aPathContagen + "\contagen\";     |RMKDIR aPathContagen;

     aAlfa = aPathTmp + "iva.csv"; |FBORRA aAlfa;

     |HAZ inicio;

     |PULSATECLA;
     |PULSATECLA;
|FPRC;

|PROCESO Tipo90;  |TIPO 90;
     aAlfa = aPathTmp + "iva.csv";           |FBORRA aAlfa;

     |PULSATECLA;
     |PULSATECLA;
|FPRC;
