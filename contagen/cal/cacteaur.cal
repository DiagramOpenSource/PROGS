|FICHEROS;
   cacteaur #00;
   camaasic #02;
   camaasil #03;
   cacuenta #05;
   camautoc #06;
   camautol #07;
   camandia #10;

   camaniva #11;
   caivalin #12;
   calibros #13;
   caconasi #20;

   canempre #30;
   caselem1 #998;
   catrasi1 #999;

   caivaasi #200;
   cadeflab #210;

   imagen@  #901;
|FIN;

|VARIABLES;
   &entrada = 1;
   mensa1 = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
   mensa2 = "    ERROR FECHA INFERIOR O IGUAL AL BLOQUEO DE DATOS";

   aAlfa1 = "";
   aAlfa2 = "";
   aAlfa3 = "";
   aAlfa4 = "";
   nNume1 = 0;
   nNume2 = 0;
   nNume3 = 0;
   nNume4 = 0;
   fFech1 = @;
   fFech2 = @;
   fFechaAsto = @;
   nPara1 = 0;

   diar = 0;
   fech = @;
   asie = 0;
   apun = 0;
   cuen = "";
   digi = 0;
   sign = "";
   ptas = 0;
   conc = 0;
   desc = "";

   sw = 0;
   estado = 0;

   dpar = "";
   subd = "";
   swpr = 0;
   documen = 0;
   escon = 0;
   inkey = 0;
   swsel = 0;
   swContar = 0;

   nLibro = 0;
   nPerio = 0;
   NumDoc = 0;
   NumFactura = 0;
   eaTitCuenta = "";
   aSerie = "";
   &enQSerie = 0;
   aParam  = "";
   nParam  = 0;
   SwAsiento = 0;

   &antdia  = 0;  ||Variables para el Informe de posibles errores
   &antfec  = @;
   &antdoc  = 0;
   &antapu  = 0;
   &lerror  = 0;
   &men     = "";
   informe = "cacheapu";
   swImpresora = 0;

   aFichero = "";
   aElDepar = "";
   aElSubDepar = "";
   aDDepar  = "";
   aHDepar  = "";
   nTIva    = 0;
   nTRec    = 0;
   nOper    = 0;
   nSwFac   = 0;

   aDirInicio = "";
   aMas       = "";
   aDef901    = "";
   nGuarda    = 0;
   nSwEn      = 0;
   nSwImagen  = 0;
   nEstado    = 0;

   nIncrApte  = 0;
   aElSigno   = "";
   fDFecha    = @;
   aLaCtaAna  = "";
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoan_i;

|| ************************************************************************
|| *********** CALCULOS PANTALLA ******************************************
|| ************************************************************************
|CALCULO fTipo8; |TIPO 8;
  aParam = PARAMETRO; |QBT aParam;
  nParam = aParam;
  |HAZ inicio;
  |DDEFECTO #200;
  |ABRE #200;
  |LEE 010#200p;
  |CIERRA #200;
  aAlfa1 = #200#44;
  |C_M #0#4,1,aAlfa1; |C_M #0#5,1,aAlfa1;
  eaPaCTA1 = #200#135;  ||permitir alta no estructuras 2008
  eaPaCTA2 = #200#136;  ||Aviso 1990
  eaPaNif1 = #200#152;
  eaPaNif2 = #200#153;
  eaABanco = #200#161;
  eaObClPr = #200#186;

  |DDEFECTO #210;
  |ABRE #210;
  |LEE 010#210p;
  |CIERRA #210;

  |HAZ eParaModelos;
|FCAL;

|CALCULO elTipo7; |TIPO 7;
 fDFecha = fec1;
 |SI enSwSelFc = 1; fDFecha = "01.01.2000"; |FINSI;
 #0#8 = fDFecha; |PINTA #0#8;
 #0#9 = fec2;    |PINTA #0#9;
 |SI nParam ! 0;
     #0#0 = nParam;
     |HAZ LeeAsto;
     |SI SwAsiento = 0;
         |C_M #0#0,1,"N"; |PINTA #0#0;
         |PINTA #0#1;
         |PINTA #0#2;
         |PINTA #0#3;
         |PINTA #0#12;
     |FINSI;
 |FINSI;
|FCAL;

|CALCULO no_dep; |TIPO 7;
   |C_M #0#10, 1, eaDepar;
   |C_M #0#11, 1, eaDepar;
|FCAL;

|PROCESO LeeAsto;
 SwAsiento = 0;
 |ABRE #6;
 #6#0 = #0#0;
 |LEE 001#6=;
 |SI FSalida ! 0;
     SwAsiento = 1;
 |SINO;
    |SI #6#24 = "N";
        SwAsiento = 2;
    |SINO;
        #0#1  = #6#1;
        #0#2  = #6#11;
        #0#12 = #6#10;
        #0#21 = #6#25;
        |SI #6#25 = "R"; #0#3 = #200#3;  aAlfa1 = #200#4; |FINSI;
        |SI #6#25 = "S"; #0#3 = #200#29; aAlfa1 = #200#30; |FINSI;
        |SI #6#25 = "N"; #0#3 = #210#19; aAlfa1 = #210#43; |FINSI;
        |C_M #0#3,1,aAlfa1;
    |FINSI;
 |FINSI;
 |CIERRA #6;
|FPRC;

|CALCULO LeeAsiento; |TIPO 0;
 |C_M #0#0,0,aAlfa1;
 |SI aAlfa1 = "N"; |ACABA; |FINSI;

 |HAZ LeeAsto;
 |SI SwAsiento ! 0;
     |SI SwAsiento = 1;
         |MENAV "    Error, No Existe Asiento Fijo ";
     |SINO;
         |MENAV "    Error, este Asiento Fijo no esta relacionado con Registros de IVA/IRPF";
     |FINSI;
     |ERROR;
 |SINO;
     |PINTA #0#1;
     |PINTA #0#2;
     |PINTA #0#3;
     |PINTA #0#12;
 |FINSI;
|FCAL;

|CALCULO limite; |TIPO 0;
   fDFecha = fec1;
   |SI enSwSelFc = 1; |Y Campo ! 13;  fDFecha = "01.01.2000"; |FINSI;

   |SI #0Campo< fDFecha; |O #0Campo > fec2;
      |MENAV mensa1;
      |ERROR;
      |ACABA;
   |FINSI;

   |SI Campo = 13; |Y #0Campo [ fec3;
       |MENAV mensa2;
       |ERROR;
       |ACABA;
   |FINSI;

   |SI Campo = 9; |Y #0#8 > #0#9;
       |MENAV "    ATENCION!!! Fecha hasta inferior";
       |ERROR;
   |FINSI;
|FCAL;

|CALCULO ElLibro; |TIPO 6;
   |ABRE #13;
   #13#0 = #0#3;
   |LEE 010#13=;
   |SI FSalida ! 0;
      |MENAV "     No existe el libro";
      |CIERRA #13;
      |C_M #0#3,1,"S";
      |ERROR;
      |ACABA;
   |FINSI;
   |CIERRA #13;

   |SI #0#21 ! #13#2;
       |MENAV "     Libro de IVA/IRPF No Correcto";
       |C_M #0#3,1,"S";
       |ERROR;
   |FINSI;
|FCAL;

|| ///////////////////////////////////////////////////////////////////////
|| ///////////////////////////////////////////////////////////////////////
|PROCESO CalImporte;
  || //// Importes de Cabecera \\\\\\\\\\
  |SI #7#10 = 1;  ptas = ptas + #11#19; |FINSI;
  |SI #7#10 = 2;  ptas = ptas + #11#20; |FINSI;
  |SI #7#10 = 3;  ptas = ptas + #11#23; |FINSI;
  |SI #7#10 = 4;  ptas = ptas + #11#26; |FINSI;
  |SI #7#10 = 5;  ptas = ptas + #11#21; |FINSI;
  |SI #7#10 = 6;  ptas = ptas + nTIva;  |FINSI;
  |SI #7#10 = 7;  ptas = ptas + nTRec;  |FINSI;
  || //// Importes de Lineas \\\\\\\\\\
  |SI #7#10 = 11; ptas = ptas + #12#6;  |FINSI;
  |SI #7#10 = 12; ptas = ptas + #12#20; |FINSI;
  |SI #7#10 = 13; ptas = ptas + #12#22; |FINSI;
  |SI #7#10 = 14; ptas = ptas + #12#8;  |FINSI;
  |SI #7#10 = 15; ptas = ptas + #12#10; |FINSI;
  |SI #7#10 = 16; ptas = ptas + #12#11; |FINSI;
|FPRC;

|PROCESO leecta;
   sign = "D";
   ptas = 0;
   |ABRE #5;
   #5#0 = cuen;
   |LEE 001#5=;
   |SI FSalida ! 0;
       |DDEFECTO #5;
   |FINSI;
   |SI #5#53 < 0; sign = "D"; ptas = - #5#53; |FINSI;
   |SI #5#53 ] 0; sign = "H"; ptas =   #5#53; |FINSI;
   |CIERRA #5;
|FPRC;

|PROCESO DatosLineas;
  eaCuenta = cuen; |HAZ SacaNivel;
  digi     = enNivel;
  |SI enSwAna = 1;
      aAlfa1 = #7#14; |QBF aAlfa1;
      |SI aAlfa1 = "*TABLA";
          |HAZ LeeTablaAna;
      |SINO;
          |SI aAlfa1 ! "*REGIS";
              eaCtaAna = #7#14;
          |SINO;
              |SI eaCtaAna = "";
                  eaCtaAna = aLaCtaAna;
              |FINSI;
          |FINSI;
      |FINSI;
  |FINSI;

  aElSigno = #7#15;
  |SI #7#7 = "S";    ||saldo de la cuenta
      |HAZ leecta;
  |SINO;
      |SI #7#7 = "C";  || Cuadre de Asiento
          |SI #0#20 ] 0; sign = "H"; ptas = #0#20;   |FINSI;
          |SI #0#20 < 0; sign = "D"; ptas = - #0#20; |FINSI;
      |SINO;
          sign = #7#7;     ||debe o haber
          |SI #7#10 = 0;   ||Importe del asiento Fijo
              ptas = #7#8;
          |SINO;
              |HAZ CalImporte;
          |FINSI;
      |FINSI;
  |FINSI;

  |SI #7#7 = "S"; |O #7#7 = "C";
      |SI aElSigno = "+"; |Y sign = "H"; ptas = 0; |FINSI;
      |SI aElSigno = "-"; |Y sign = "D"; ptas = 0; |FINSI;
  |FINSI;
|FPRC;
|| /////////////////////////////////////////////////////////////////////

|PROCESO GrabaLinea;
  |HAZ DatosLineas;
  |SI ptas ! 0;
      apun  = apun +  nIncrApte;
      |HAZ lineas;
  |FINSI;
|FPRC;

|PROCESO LeoIvaLin;
  |SI nOper = 4;
      |SI aLaCtaAna = ""; aLaCtaAna = #12#28; |QBF aLaCtaAna; |FINSI;
      |ACABA;
  |FINSI;
  |SI nOper = 2;
      nTIva = nTIva + #12#8;
      nTRec = nTRec + #12#10;
      |ACABA;
  |FINSI;
  |SI nOper = 3;
      |HAZ CalImporte;
      |ACABA;
  |FINSI;

|| ... nOper = 0 y nOper = 1
  aAlfa1 = cuen; |QBF aAlfa1;
  aAlfa2 = aAlfa1 % 101;
  |SI aAlfa2 = "C";
      aAlfa3 = aAlfa1 % 201; nNume1 = aAlfa3
      |SI nNume1 = 1;
          cuen = #11#12;
      |SINO;
          |SI #0#21 ! "N";
              |SI nNume1 = 2; aAlfa1 = #12#12; eaCtaAna = #12#28; |FINSI;
              |SI nNume1 = 3; aAlfa1 = #12#13; |FINSI;
              |SI nNume1 = 4; aAlfa1 = #12#14; |FINSI;
              |SI nNume1 = 5; aAlfa1 = #12#23; |FINSI;
              |SI nNume1 = 6; aAlfa1 = #12#15; |FINSI;
          |SINO;
              aAlfa1 = #12#23; eaCtaAna = #12#28;
          |FINSI;
      |FINSI;
  |FINSI;

  |QBF aAlfa1;
  |SI aAlfa1 ! "";
      cuen = aAlfa1;
      |SI nOper = 0;    ||Esta buscando la Cuenta
          |ERROR10;
          |ACABA;
      |SINO;
          |HAZ GrabaLinea;
      |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE LeoIvaLin;
 #12#1;
 ;
 #11#0, #11#1, 01;
 #11#0, #11#1, 99;
 e;
 NULL, LeoIvaLin;
|FIN;

|PROCESO leelineas;

  eaCtaAna = "";
  ptas     = 0;
  cuen     = #7#2;
  conc     = #7#4;
  aAlfa2   = #7#5; |QBF aAlfa2;
  aAlfa1   = #0#14; aAlfa1 = " " + aAlfa1;
  desc     = aAlfa2 + aAlfa1;
  dpar     = #11#10; |SI #6#22 = "N"; dpar = #7#6; |FINSI;
                     |SI #6#22 = "R"; dpar = aElDepar; |FINSI;
  subd     = #11#11; |SI #6#23 = "N"; subd = #7#9; |FINSI;
                     |SI #6#23 = "R"; subd = aElSubDepar; |FINSI;

  nTIva = 0;
  nTRec = 0;
  |SI #7#10 = 6; |O #7#10 = 7;
      nOper = 2;
      |BUCLE LeoIvaLin;
  |FINSI;

  aAlfa1 = cuen; |QBF aAlfa1;
  aAlfa2 = aAlfa1 % 101;

  nOper = 0;
  |SI #7#10 = 0;
      |SI #7#7 = "S"; |Y aAlfa2 = "C"; |Y aAlfa1 ! "C1";
          nOper = 1;
      |FINSI;
  |SINO;
      |SI #7#10 > 10;
          nOper = 1;
      |FINSI;
  |FINSI;

  |SI nOper = 0;    || Un solo apunte
      aAlfa1 = cuen; |QBF aAlfa1;
      aAlfa2 = aAlfa1 % 101;
      |SI aAlfa2 = "C";
          aAlfa3 = aAlfa1 % 201; nNume1 = aAlfa3
          |SI nNume1 = 1;
              cuen = #11#12;
          |SINO;
              |BUCLE LeoIvaLin;
          |FINSI;
      |FINSI;
      |HAZ GrabaLinea;
  |SINO;
      |BUCLE LeoIvaLin;
  |FINSI;
|FPRC;

|PROCESO contab0;
  #0#16 = #0#13;
  #0#17 = documen;
  #0#18 = 0;
  #0#19 = 0;
  #0#20 = 0;

  |PINTA 2211, " Pasando Registro :                             ";
  |PINTA 2231, #11#2;
  |PINTA 2237, #11#3;

  |ABRE #999;
  |DDEFECTO #999;
  apun = 1 -  nIncrApte;
  sw = 0;
  |BUCLELIN 2leelineas#6;  || Lee las Lineas del asiento fijo
  |CIERRA #999;
  |SI sw = 1;  |HAZ pasa_asto; |FINSI;
  |DELINDEX #999;
|FPRC;

|PROCESO contab1;
  aElSubDepar = #11#11;
  aElDepar    = #11#10;
  |SI enSwAna = 1;
      nOper = 4;
      aLaCtaAna = "";
      |BUCLE LeoIvaLin;
  |FINSI;

  |SI Haybasica = 1;
      |QBF aElDepar;
      aElDepar = ("00" + aElDepar) % -102
  |FINSI;
  |SI aElDepar < aDDepar; |O aElDepar > aHDepar;  |ACABA; |FINSI;
  |HAZ contab0;
|FPRC;

|DEFBUCLE LeeCabRegis;  || Lee Facturas
   #11#3;
   4;
   #0#3, #0#4, #0#6, #0#8;
   #0#3, #0#5, #0#7, #0#9;
   e;
   NULL, contab1;
|FIN;

|| **************************************************************************
|| **************************************************************************
|PROCESO LeeFacturas;
  aElSubDepar = #11#11;
  aElDepar    = #11#10;
  |SI Haybasica = 1;
      |QBF aElDepar;
      aElDepar = ("00" + aElDepar) % -102
  |FINSI;
  |SI aElDepar < aDDepar; |O aElDepar > aHDepar;  |ACABA; |FINSI;

  |PINTA 2211, " Pasando Registro :                             ";
  |PINTA 2231, #11#2;
  |PINTA 2237, #11#3;

  nTIva = 0; nTRec = 0;
  |SI #7#10 = 6; |O #7#10 = 7;
      nOper = 2;
      |BUCLE LeoIvaLin;
  |FINSI;

  |SI nSwFac = 1;
      cuen  = #11#12;
      nOper = 0;
      |HAZ GrabaLinea;
      |ACABA;
  |FINSI;

  |SI nSwFac = 2;
       aAlfa1 = cuen; |QBF aAlfa1;
       aAlfa2 = aAlfa1 % 101;
       nOper  = 1;
       |BUCLE LeoIvaLin;
       |ACABA;
  |FINSI;

  |SI #7#10 < 10;
      |HAZ CalImporte;
  |SINO;
      nOper = 3;
      |BUCLE LeoIvaLin;
  |FINSI;

|FPRC;

|DEFBUCLE LeeFacturas;
   #11#3;
   4;
   #0#3, #0#4, #0#6, #0#8;
   #0#3, #0#5, #0#7, #0#9;
   e;
   NULL, LeeFacturas;
|FIN;

|PROCESO CalImporteT;
  nSwFac = 0;
  ptas   = 0;
  eaCuenta = cuen; |HAZ SacaNivel;
  digi     = enNivel;
  aElSigno = #7#15;

  |SI #7#7 = "S";    ||saldo de la cuenta
      |HAZ leecta;
  |SINO;
      |SI #7#7 = "C";  || Cuadre de Asiento
          |SI #0#20 ] 0; sign = "H"; ptas = #0#20;   |FINSI;
          |SI #0#20 < 0; sign = "D"; ptas = - #0#20; |FINSI;
      |SINO;
          sign = #7#7;     ||debe o haber
          |SI #7#10 = 0;   ||Importe del asiento Fijo
              ptas = #7#8;
          |SINO;
              |BUCLE LeeFacturas;
          |FINSI;
      |FINSI;
  |FINSI;

  |SI #7#7 = "S"; |O #7#7 = "C";
      |SI aElSigno = "+"; |Y sign = "H"; ptas = 0; |FINSI;
      |SI aElSigno = "-"; |Y sign = "D"; ptas = 0; |FINSI;
  |FINSI;

  |SI enSwAna = 1;
      aAlfa1 = #7#14; |QBF aAlfa1;
      |SI aAlfa1 = "*TABLA";
          |HAZ LeeTablaAna;
      |SINO;
          |SI aAlfa1 ! "*REGIS";
              eaCtaAna = #7#14;
          |FINSI;
      |FINSI;
  |FINSI;

  |SI ptas ! 0;
      apun  = apun + nIncrApte;
      |HAZ lineas;
  |FINSI;
|FPRC;

|PROCESO leelineasT;
  eaCtaAna = "";
  cuen     = #7#2;
  conc     = #7#4;
  aAlfa2   = #7#5; |QBF aAlfa2;
  aAlfa1   = #0#14; aAlfa1 = " " + aAlfa1;
  desc     = aAlfa2 + aAlfa1;
  dpar     = eaDfDepC; |SI #6#22 = "N"; dpar = #7#6; |FINSI;
  subd     = eaDfSubC; |SI #6#23 = "N"; subd = #7#9; |FINSI;

  aAlfa1 = cuen; |QBF aAlfa1;
  aAlfa2 = aAlfa1 % 101;
  ptas  = 0;
  |SI aAlfa2 ! "C";
      nSwImagen = 0;        ||no busco igual
      |HAZ CalImporteT;
  |SINO;
      |SI aAlfa1 = "C1";
          nSwFac = 1;
          nSwImagen = 1;
          |BUCLE LeeFacturas;
      |SINO;
          nSwFac = 2;
          nSwImagen = 1;
          |BUCLE LeeFacturas;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO UnTotal;

  #0#16 = #0#13;
  #0#17 = documen;
  #0#18 = 0;
  #0#19 = 0;
  #0#20 = 0;

  |ABRE #999;
  |DDEFECTO #999;
  apun =  1 - nIncrApte;
  sw = 0;
  |BUCLELIN 2leelineasT#6;  || Lee las Lineas del asiento fijo
  |CIERRA #999;
  |SI sw = 1;  |HAZ pasa_asto; |FINSI;
  |DELINDEX #999;
|FPRC;

|| **************************************************************************
|PROCESO HazTodo;
   |HAZ Analitica;
   |ABRE #999;  |CIERRA #999;  |DELINDEX #999;

   aMonedaA =  eaMoneda;

   |HAZ contador;
   documen = nDOCUMENTO;

   || ////  Lee el Asiento Fijo
   |ABRE #6;
   #6#0 = #0#0;  || Asiento Fijo
   |LEE 001#6=;
   |SI FSalida ! 0;
       |CIERRA #6;  ||En esta empresa no esta el asiento elegido
       |ACABA;
   |FINSI;
   |SI #6#24 = "N";
       |CIERRA #6;  || NO esta relacionado con IVA/IRPF
       |ACABA;
   |FINSI;
   nIncrApte = #6#26; |SI nIncrApte = 0; nIncrApte = 10; |FINSI;
   nSwImagen = 0;
   lerror = 0;
   |SI #0#15 = "S";
       |BUCLE LeeCabRegis;
   |SINO;
       |HAZ UnTotal;
   |FINSI;

   |SI lerror ! 0;
       |SI swImpresora = 1;
           |PIEINF;
       |SINO;
           aAlfa1 = enEmpresa; aAlfa1 = ("00000" + aAlfa1) % -105;
           aAlfa1 = "    Errores en Asientos de la Empresa " + aAlfa1;
           |MENAV aAlfa1;
       |FINSI;
   |FINSI;

   |CIERRA #6;
|FPRC;


|CALCULO proceso; |TIPO 3;


   |SI Haybasica = 1;
       |SI #0#11 = "zz"; #0#11 = "99"; |FINSI;
   |FINSI;
   aDDepar = #0#10;
   aHDepar = #0#11;

   aFichero = ("r" + Usuario) % 108;
   |NOME_DAT #999 aFichero;

   |DBASE aDirInicio;
   aMas  = aDirInicio + "def/catrasi1";
   aDef901 = aMas;
   |CARGA_DEF aMas, imagen@;
   |SI FSalida ! 0;
       aDef901 = "";
       |MENAV "    Error Cargado Fichero de Contabilidad. Proceso Interrumpido";
       |ACABA;
   |FINSI;
   |NOME_DAT #901 aFichero;


   |SI #48#27 = "S";
       |HAZ MultiEmpresa;
   |SINO;
       |DFICE dirfich0; |QBF dirfich0;
       |HAZ HazTodo;
   |FINSI;
   |SI swImpresora = 1;
       |FININF;
       |FINIMP;
   |FINSI;

   |SI aDef901 ! ""; |DESCARGA_DEF #901; aDef901 = ""; |FINSI;
|FCAL;

|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   |SI #0#13 [ fec3; |ACABA; |FINSI; || en esta empresa no se puede crear asientos
   |SI #0#13 < fec1; |O #0#13 > fec2;
       |ACABA;    || no contabiliza en esta Empresa
   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #2 dirfich0;
   |PATH_DAT #3 dirfich0;
   |PATH_DAT #5 dirfich0;
   |PATH_DAT #6 dirfich0;
   |PATH_DAT #7 dirfich0;
   |PATH_DAT #10 dirfich0;
   |PATH_DAT #11 dirfich0;
   |PATH_DAT #12 dirfich0;
   |PATH_DAT #13 dirfich0;
   |PATH_DAT #20 dirfich0;
   |PATH_DAT #200 dirfich0;
   |PATH_DAT #210 dirfich0;

  |DDEFECTO #200;
  |ABRE #200;
  |LEE 010#200p;
  |CIERRA #200;
  |DDEFECTO #210;
  |ABRE #210;
  |LEE 010#210p;
  |CIERRA #210;

   |PINTA 1910, "                                                             ";
   |PINTA 2010, "                                                             ";
   |PINTA 2110, "                                                             ";
   |PINTA 2210, "                                                             ";
   |PINTA 2310, "                                                             ";
   |CUADRO 1910 2370;
   |ATRI N;
   |PINTA 2011, " EMPRESA: ";
   |ATRI P; |PINTA 2021, #30#2;
   |PINTA 2027, #30#1; |ATRI 0;

   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 000001;
 999999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;

|| ************************************************************************
|| ************************************************************************
|| ************************************************************************
|PROCESO BuscoImagen;
  nSwEn = 1;
  apun  = #901#3;
|FPRC;

|DEFBUCLE BuscoImagen;
 #901#3004;
 8;
 cuen, #0#16, #0#17, 0001, sign;
 cuen, #0#16, #0#17, 9999, sign;
 ;
 NULL, BuscoImagen;
|FIN;

|PROCESO lineas;    || graba apunte

   nGuarda = apun;
   |SI nSwImagen = 1;
       nSwEn = 0;
       |BUCLE BuscoImagen;
       |SI nSwEn = 1;
           nGuarda = nGuarda - 10;
       |FINSI;
   |FINSI;

   |DDEFECTO #999;
   #999#00 = #0#12;
   #999#01 = #0#16;
   #999#02 = #0#17;
   #999#03 = apun;   || apunte
   |LEE 001#999=;
   nEstado = FSalida;
   #999#00 = #0#12;
   #999#01 = #0#16;
   #999#02 = #0#17;
   #999#03 = apun;   || apunte
   #999#04 = cuen;   || cuenta
   #999#05 = digi;   || digito
   #999#06 = conc;   || concepto
   #999#07 = desc;   || descripcion
   #999#08 = sign;   || signo
   #999#09 = #999#09 + ptas;   || importe
   |SI #999#08 = "D";
       #999#16 = #999#4;
       #999#17 = #999#5;
       #999#10 = "";
       #999#11 = 0;
   |SINO;
       #999#16 = "";
       #999#17 = 0;
       #999#10 = #999#4;
       #999#11 = #999#5;
   |FINSI;
   #999#21 = dpar;
   #999#28 = subd;
   #999#29 = eaCtaAna;
   |SI nEstado ! 0; |GRABA 020#999n; |FINSI;
   |SI nEstado = 0; |GRABA 020#999a; |FINSI;
   sw   = 1;
   |SI sign = "D"; #0#18 = #0#18 + ptas; |FINSI;
   |SI sign = "H"; #0#19 = #0#19 + ptas; |FINSI;
   #0#20 = #0#18 - #0#19;
   ptas = 0;
   apun = nGuarda;
|FPRC;

|PROCESO pasa_asto1;    || graba apunte
   |DDEFECTO #3;
   |PARA nPara1 = 0; |SI nPara1 [ 30; |HACIENDO nPara1 = nPara1 + 1;
         #3nPara1 = #999nPara1;
   |SIGUE;
   #3#2  = nDOCUMENTO;
   #3#24 = nREGISTRO;
   |GRABA 020#3n;
   |HAZ cabecera;           || cabecera apunte
   MasMenos     = #3#9;
   DebeHaber    = #3#8;
   aCUENTA      = #3#4;
   nREGISTRO    = #3#24;
   nDIARIO      = #3#0;
   aFECHA       = #3#1;
   nDOCUMENTO   = #3#2;
   nNIVELCUENTA = #3#5;
   |HAZ ActDiario;          || diario
   |HAZ ActCuenta;          || cuenta
   |HAZ ActTesteo;          || cuenta

   |HAZ HazElChequeo;
|FPRC;

|DEFBUCLE pasa_asto0;
   #999#1;
   ;
   00, "01.01.1900", 000001, 0001;
   99, "31.12.2090", 999999, 9999;
   e;
   NULL, pasa_asto1;
|FIN;

|PROCESO pasa_asto;
   |HAZ Multi_contador;
   |ABRE #2;
   |ABRE #3;
   |BUCLE pasa_asto0;
   |CIERRA #2;
   |CIERRA #3;

   documen = #3#2 + 1;
   aAlfa1  = #3#0; aAlfa1 = ("00" + aAlfa1) % -102;
   #6#7    = aAlfa1;
   #6#8    = #3#1;
   aAlfa1  = #3#2; aAlfa1 = ("000000" + aAlfa1) % -106;
   #6#9    = aAlfa1;
   |GRABA 020#6a;
|FPRC;

|PROCESO cabecera;       || actualiza cabeceras
   |DDEFECTO #2;
   #2#00 = #3#0; ||  diario
   #2#01 = #3#1; ||  fecha
   #2#02 = #3#2; ||  documento
   #2#03 = #3#2; ||  asiento
   |LEE 101#2=;
   |SI FSalida ! 0;
       |DDEFECTO #2;
       #2#00 = #3#0; ||  diario
       #2#01 = #3#1; ||  fecha
       #2#02 = #3#2; ||  documento
       #2#03 = #3#2; ||  asiento
       |GRABA 020#2b;
   |FINSI;
   |SI #3#08 = "D";
       #2#04 = #2#04 + #3#09;
       |SI #2#09 = 0; #2#08 = #3#4; #2#09 = #3#5; |FINSI;  || contrp.HABER
   |SINO;
       #2#05 = #2#05 + #3#09;
       |SI #2#11 = 0; #2#10 = #3#4; #2#11 = #3#5; |FINSI;  || contrp.DEBE
   |FINSI;
   #2#6  = #2#4 - #2#5;      || saldo
   #2#12 = #3#24;
   #2#13 = 0;
   |GRABA 020#2a;
   |LIBERA #2;
|FPRC;

|| ************************* Proceso para sacar posibles errores

|PROCESO HazElChequeo;
  eaCuenta = #3#4; |QBF eaCuenta;

  |SI eaCuenta = "";
      men = "CUENTA CONTABLE EN BLANCO";
      |HAZ HazPrint;
  |SINO;
      |HAZ SacaNivel;
      |SI enNivel = 0;
          men = "CUENTA CONTABLE FUERA DE NIVEL";
          |HAZ HazPrint;
      |FINSI;
  |FINSI;
  |ABRE #5;
  #5#0 = #3#4;
  #5#1 = #3#5;
  |LEE 001#5=;
  |SI FSalida ! 0;
      men = "NO EXISTE CUENTA CONTABLE";
      |HAZ HazPrint;
  |SINO;
      |SI #5#3 = "N";
         men = "CUENTA CONTABLE SIN ASIENTO DIRECTO";
         |HAZ HazPrint;
      |FINSI;
  |FINSI;
  |CIERRA #cacuenta;

  |SI #3#9 = 0;                   || Importe Cero
      men = "IMPORTE IGUAL A CERO";
      |HAZ HazPrint;
  |FINSI;

  |SI #3#6 = 0;                   || Concepto
      men = "CONCEPTO AUTOMATICO IGUAL A CERO";
      |HAZ HazPrint;
  |FINSI;
|FPRC;

|PROCESO HazPrint;
  |SI swImpresora = 0;
      swImpresora = 1;
      |IMPRE 0;
      |SI FSalida ! 0;  swImpresora = 2; |ACABA; |FINSI;
      |INFOR informe;
      |SI FSalida ! 0;  swImpresora = 3; |FINIMP;  |ACABA;  |FINSI;
 |FINSI;
 |SI swImpresora = 1;
     antdia  = #3#0;
     antfec  = #3#1;
     antdoc  = #3#2;
     antapu  = #3#3;
     |PRINT;
 |FINSI;
 lerror = lerror + 1;
|FPRC;
