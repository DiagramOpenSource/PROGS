|| ... Rutina de Acceso a Analitica
|FICHEROS;
   :/contagen/def/anm00000 #600;
   :/contagen/def/anm00008 #610;
   :/contagen/def/anm00016 #616;
   :/contagen/def/canempre #30;
|FIN;

|VARIABLES;
  nNume1 = 0;
  aAlfa1 = "";
  aAlfa2 = "";
  aAlfa3 = "";
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoan_i;

|PROCESO Analitica;
   |ABRE #600;
   |DDEFECTO #600;
   |LEE 101#600p;
   |SI FSalida ! 0;
       |GRABA 020#600b;
   |FINSI;

   enSwAna = 0;  || 0 = Sin Analitica / 1 = Contabilidad Analitica
   |SI #600#1 = "S";
       MaxAnalitica = 7;
       nAdig0 = #600#2;
       nAdig1 = #600#3;
       nAdig2 = #600#4;
       nAdig3 = #600#5;
       nAdig4 = #600#6;
       nAdig5 = #600#7;
       eaPAna = #600#9;
       eaGAna = #600#10;
       eaOAna = #600#13;
       eaUAna = #600#14;
       enSwAna = 1;
   |FINSI;
   eaRefer = #600#8;
   eaLnRef = #600#15;
   enLgRef = #600#12;
   eaAnNiv = #600#11;
   |CIERRA #600;

   |SI eaAnNiv = "E"; |Y enEmpresa ! 0;
       |ABRE #616;
       #616#0 = enEmpresa;
       |LEE 001#616=;
       |SI FSalida ! 0;
           enSwAna = 0;
       |SINO;
            MaxAnalitica = 7;
            nAdig0 = #616#2;
            nAdig1 = #616#3;
            nAdig2 = #616#4;
            nAdig3 = #616#5;
            nAdig4 = #616#6;
            nAdig5 = #616#7;
            eaPAna = #616#9;
            eaGAna = #616#10;
            eaOAna = #616#14;
            enSwAna = 1;
            eaRefer = #616#8;
            enLgRef = #616#13;
       |FINSI;
       |CIERRA #616;
   |FINSI;
|FPRC;

|CALCULO InicioAna; |TIPO 8;
  |HAZ inicio;
  |HAZ Analitica;
  |SI  enSwAna = 0;
       aAlfa1 = "    CONTABILIDAD NO ANALITICA ";
       |SI eaAnNiv = "E";
           aAlfa1 = "    EMPRESA NO ANALITICA ";
       |FINSI;
       |MENAV aAlfa1;
       |PTEC 807;
  |FINSI;
|FCAL;

|PROCESO CambiaNombre;
     |SI #anm00008#2 ] 80;
         nNume1 = 1900 + #anm00008#2;
     |SINO;
         nNume1 = 2000 + #anm00008#2;
     |FINSI;
     aAlfa1 = #anm00008#3;  |QBF aAlfa1;
     aAlfa2 = #anm00008#4;  |QBF aAlfa2;
     #30#29 = "[" + (("00000" + #anm00008#0) % -105) + "/" + nNume1 + "] " + aAlfa1 + " " + aAlfa2;
|FPRC;


|PROCESO AnaAltaEmp;
   |SI eaAnNiv ! "E"; |ACABA; |FINSI;

       |ABRE #616;
       #616#0 = #30#2;
       |LEE 001#616=;
       |SI FSalida ! 0;
            #616#0 = #30#2;
            #616#1 = "S";
            #616#2 = nAdig0;
            #616#3 = nAdig1;
            #616#4 = nAdig2;
            #616#5 = nAdig3;
            #616#6 = nAdig4;
            #616#7 = nAdig5;
            #616#8 = eaRefer;
            #616#9 = eaPAna;
            #616#10 = eaGAna;
            #616#11 = "E";
            #616#12 = #30#1;
            #616#13 = enLgRef;
            |GRABA 020#616n;
       |FINSI;
       |CIERRA #616;
|FPRC;
