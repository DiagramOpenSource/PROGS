|FICHEROS;
 capasz02 #0;

 camw0001 #1;
 camw0002 #2;
 camw0003 #3;
 camw0004 #4;

 camaniva #11;
 caivalin #12;
 camaasil #13;

 :/basica/def/agifigen #100;
 canempre #30;
 caselem1 #998;
|FIN;

|VARIABLES;

 nSwAlgun = 0;
 aAlfa1 = "";
 aAlfa2 = "";
 aAlfa3 = "";
 aAlfa4 = "";
 nNume1 = 0;
 nNume2 = 0;
 nNume3 = 0;
 nNume4 = 0;

 nPara1 = 0;
 nPara2 = 0;
 nCampo = 0;
 nSwTipo  = 0;
 aDescrip = "";
 aTipoRS  = "";
 nTipoNum = 0;
 nPrimer  = 0;
 nPrimer1 = 0;
 nUltim   = 0;
 nDTipoNum = 0;
 nHTipoNum = 0;
 nDPeriodo = 0;
 nHPeriodo = 0;

 nLaEmpresa = 0;
 nSwTipoE   = 0;
 nSwNoDir   = 0;
 nPin1      = 0;

 &nConcepto  = 0;
 &aDescripcion ="";
 nNuevoCon  = 0;
 nSwCambio  = 0;
 nSelCab1   = 0;
 nSelCab2   = 0;
 nDEmpresa  = 0;
 nHEmpresa  = 0;

 nSwEnLinea = 0;
 nLinea     = 0;
 aFichero   = "";

 aAcum = "";
 nDAcum = 0;
 nHAcum = 0;

 informe = "capasz02";

 nImporte = 0;

|FIN;

|INCLUYE dscont_i;

|| ******************************************************************
|| **** PROCESOS DE SELECCION CAMBIO ********************************
|| ******************************************************************
|CALCULO Parrilla; |TIPO 0;
     |SI #0#2 = 0;
         #0#2 = 0;  |PINTA #0#2;
         #0#3 = 0;  |PINTA #0#3;  |C_M #0#3, 1, "N";
         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |SI enEmpresa = 0;
             |C_M #0#2, 1, "S";
             |C_M #0#3, 1, "S";
         |FINSI;

         |PARA nCampo = 4;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas; |TIPO 0;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 23;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO SelTipo0; |TIPO 0;
 |SI #0#37 = "N";
     #0#25 = 0;   |PINTA #0#25;  |C_M #0#25, 1, "N";
     #0#26 = 99;  |PINTA #0#26;  |C_M #0#26, 1, "N";
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |SINO;
     |C_M #0#25, 1, "S";
     |C_M #0#26, 1, "S";
     |PARA nCampo = 27;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "N";
           #0nCampo = 0;  |PINTA #0nCampo;
     |SIGUE;
 |FINSI;
|FCAL;

|CALCULO SelTipo1;
 |SI #0Campo = 0;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           #0nCampo = 0;  |C_M #0nCampo, 1, "N";
     |SIGUE;
 |SINO;
     |PARA nCampo = Campo + 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           |C_M #0nCampo, 1, "S";
     |SIGUE;
 |FINSI;
|FCAL;

|| ***********************************************************************
|| ***********************************************************************
|| ***********************************************************************
|PROCESO CambiaApte;
 #13#6 = nNuevoCon;
 |GRABA 020#13a;
 |LIBERA #13;
|FPRC;

|DEFBUCLE CambiaApte;
 #13#1;
 19,20;
 #11#7, #11#8, #11#9, 0001, aAcum, nDAcum;
 #11#7, #11#8, #11#9, 9999, aAcum, nHAcum;
 be;
 NULL, CambiaApte;
|FIN;

|| ********************************************************************
|PROCESO camw0002;
 nNuevoCon    = #2#3;
 aDescripcion = #2#4;
|FPRC;

|DEFBUCLE camw0002;
 #2#1;
 2;
 #1#0,01, #12#21;
 #1#0,99, #12#21;
 ;
 NULL, camw0002;
|FIN;

|PROCESO ModIRPF;
 |PINTA 2417, #12#0;
 |PINTA 2420, #12#1;
 |PINTA 2432, #12#2;

 #11#0 = #12#0;
 #11#1 = #12#1;
 |LEE 001#11=;
 |SI FSalida ! 0;
     |ACABA;
 |FINSI;
 |SI #11#36 ! "S";  |ACABA; |FINSI;

 nConcepto = #12#18;
 nNuevoCon = #1#1;
 aDescripcion = #1#10;
 |BUCLE camw0002;

 |SI nNuevoCon ! nConcepto;

     #12#18 = nNuevoCon;
     |GRABA 020#12a;
     |LIBERA #12;

     aAcum = "P";
     nDAcum = #12#2;
     nHAcum = #12#2;
     |BUCLE CambiaApte;

     |PRINT;
     nSwAlgun = 1;
 |FINSI;
|FPRC;

|DEFBUCLE leeIRPF;
 #12#1;
 21;
 00, 0000000001, 01, 1;
 99, 9999999999, 99, 999.99;
 be;
 NULL, ModIRPF;
|FIN;

|PROCESO HazTodo1;
   eaMoneda = "E"; |HAZ Monedas;

   |PINTA 2404, "En Proceso: ";
   |ABRE #11;
   |BUCLE leeIRPF;
   |CIERRA #11;
|FPRC;


|PROCESO MultiEmp11;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   aMonedaC = eaMoneda;
   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #11 dirfich0;
   |PATH_DAT #12 dirfich0;
   |PATH_DAT #13 dirfich0;

   |ATRI R; |PINTA 2304, "Empresa: ";      |ATRI 0;
   |PINTA 2313, #30#2; |PINTA 2319, #30#1; |ATRI 0;
   nSwAlgun = 0;
   |HAZ HazTodo1;
   |SI nSwAlgun = 1;
       |PIEINF;
   |FINSI;
|FPRC;

|DEFBUCLE MultiEmp01;
 #caselem1#1;
 ;
 000001;
 999999;
 ;
 NULL,MultiEmp11;
|FIN;

|PROCESO CambioIRPF;

 |INFOR informe;
 |SI FSalida ! 0; |ACABA; |FINSI;

 |DDEFECTO #3;
 #3#3 = "CAMBIO DE REGISTROS DE IRPF";
 #3#4 = "SOPORTADO";
 |PARA nPara1 = 5; |SI nPara1 [ 40; |HACIENDO nPara1 = nPara1 + 1;
       nPara2 = nPara1 - 3;
       #3nPara1 = #0nPara2;
 |SIGUE;

 |PINTA 2204, #3#3;
 |HAZ SelEmpresa;
 |SI nLinea = 1;  |ACABA; |FINSI;  || no hay ninguna empresa

 |BUCLE MultiEmp01;

 |FININF;
|FPRC;


|| ......................................................................

|CALCULO ElCambio; |TIPO 3;

 aMonedaA = "E";  ||Importe en Euros
 |HAZ DirAplicSt;

 aFichero = ("3w" + Usuario) % 108;
 |NOME_DAT #998 aFichero;

|| ... ... ... ... Lee control de Definicion
 |ABRE #1;
 |DDEFECTO #1;
 |LEE 000#1p;
 |SI FSalida ! 0;
     |MENAV "    NO EXISTE DEFINICION CAMBIO ... PROCESO ABORTADO";
     |CIERRA #1;
     |ACABA;
 |FINSI;
 |CIERRA #1;

 nDPeriodo = #0#0;
 nHPeriodo = #0#1;

 |IMPRE 0;
 |SI FSalida ! 0; |ACABA; |FINSI;

 |HAZ CambioIRPF;

 |DELINDEX #998;
 |FINIMP;
|FCAL;

|| *************************************************************************
|| PROCESO PARA SELECCIONAR EMPRESA
|| *************************************************************************
|PROCESO CalDirectorio;

  |DBASS dir_fich; |QBF dir_fich;
  dir_fich = dir_fich + "contagen/fich/";
  aAlfa1 = #998#1; |QBF aAlfa1;
  aAlfa2 = #998#2; |QBF aAlfa2;
  aAlfa1 = "00000" + aAlfa1;   aAlfa1 = aAlfa1 % -105;
  aAlfa2 = "0"     + aAlfa2;   aAlfa2 = aAlfa2 % -101;
  #998#3 = dir_fich + aAlfa1 + aAlfa2 + "/";

  nSwNoDir = 0;
  aAlfa1 = #998#3;   |QBF aAlfa1;
  |MKDIR aAlfa1;
  |SI FSalida = 0;
       nSwNoDir = 1;
       |RMDIR aAlfa1;
  |FINSI;
|FPRC;

|PROCESO ElTipoEmpresa;
    nSwTipoE = 1;
    |SI Haybasica = 0; nSwTipoE = 0; |ACABA; |FINSI;
    |ABRE #agifigen;
    #agifigen#0 = nLaEmpresa; || Empresa
    |LEE 041#agifigen.=;
    |SI FSalida ! 0;
        |DDEFECTO #agifigen;
        #agifigen#87 = 1;
    |FINSI;
    |CIERRA #agifigen;
    nPin1 = #agifigen#87;

    |SI #3#40 = "S";
        |SI nPin1 ] #3#28; |Y nPin1 [ #3#29;
            nSwTipoE = 0;
        |FINSI;
    |SINO;
        |SI nPin1 = #3#30;
            nSwTipoE = 0;
        |SINO;
            |PARA nCampo = 31;  |SI nCampo [ 39;  |HACIENDO nCampo = nCampo + 1;
                  |SI nPin1 = #3nCampo; |Y nPin1 ! 0;
                      nSwTipoE = 0;
                      |SAL;
                  |FINSI;
            |SIGUE;
        |FINSI;
    |FINSI;
|FPRC;

|PROCESO Rellena;

   |SI #3#27 = "N"; |Y #canempre#21 = "I"; |ACABA; |FINSI;

   nLaEmpresa  = #canempre#2;
   |HAZ ElTipoEmpresa;
   |SI nSwTipoE = 1; |ACABA; |FINSI;

   |DDEFECTO #998;
   #998#0 = nLinea;
   nLinea = nLinea + 1;
   #998#1 = #canempre#2;
   #998#2 = #canempre#3;
   |HAZ CalDirectorio;
   |SI nSwNoDir = 0;
       |GRABA 020#998n;
   |FINSI;
|FPRC;

|DEFBUCLE Rellena;
 #canempre#1;
 ;
 nDEmpresa,nDPeriodo;
 nHEmpresa,nHPeriodo;
 ;
 NULL,Rellena;
|FIN;


|PROCESO SelEmpresa;
 |DELINDEX #998;

 |ABRE #998;
 nLinea = 1;

 |SI #3#5 ! 0;
     nDEmpresa = #3#5;
     nHEmpresa = #3#6;
     |BUCLE Rellena;
 |SINO;
     |PARA nPara2 = 7; |SI nPara2 [ 26; |HACIENDO nPara2 = nPara2 + 1;
          nDEmpresa = #3nPara2;
          nHEmpresa = #3nPara2;
          |BUCLE Rellena;
     |SIGUE;
 |FINSI;
 |CIERRA #998;
|FPRC;
