|FICHEROS;
   caz00101 #0;
   ca8accaa;

   espejo1@;
   espejo2@;

   ca8m0000 #420;
   ca8w0001 #50,MANTE;
|FIN;

|VARIABLES;
 aAlfa        = "";
 aParam       = "";
 &nBalNum     = 0;
 &enOPlan     = 0;
 &enDPlan     = 0;
 &HaySociedad = 0;
 &aPContas    = "";
 aBase        = "";
 aDef         = "";
 aFichero     = "";
 nTipo        = 0;
 aQueQuiero   = "";
 aNumCampos   = "";
 nNumCampos   = 0;
 nCampo       = 0;
 nCampoNum    = 0;
 nPara1       = 0;
 nDesDonde    = 0;
 aLosDefs     = "";
 aElProgr     = "";
 nCanal       = 0;
 aPassword    = "";
 nDesdeNum    = 0;
 nHastaNum    = 0;
 nONum        = 0;
 nDNum        = 0;
 nOpera       = 0;
 &wExCa8M0    = 0;
 &wPatOfi     = 0;
 &wTipoPat    = 0;

 &enONume     = 0;
 &enDNume     = 0;
 &enOp101     = 0;
|FIN;

|PROCESO LeoPlan;
 |ABRE #ca8accaa;
 #ca8accaa#0 = #0Campo;
 |LEE 041#ca8accaa.=;
 |SI FSalida ! 0; |Y Campo = 0;
     |MENAV "0000Error. No existe Ejercicio Origen";
     |ERROR;
 |SINO;
     |SI FSalida = 0; |Y Campo = 1;
         |CONFI "0000NError. Ya existe Ejercicio Destino. Continuar";
         |SI FSalida ! 0;
             |ERROR;
         |SINO;
             |SI #0Campo = 2008; |O #0Campo = 2015; |O #0Campo = 2016;
                |O #0Campo = 2018;
                 |MENAV "0000Se duplicaran Estructuras y se instalaran las Oficiales del Ejercicio";
                 |CONFI "0000NContinuar";
                 |SI FSalida ! 0;
                     |ERROR;
                 |FINSI;
              |FINSI;
         |FINSI;
     |FINSI;
 |FINSI;
 |CIERRA #ca8accaa;
|FPRC;

||  *****************************************************

|PROCESO  CargaFichero;
  |SI nDesDonde = 0;
      aDef = aBase + "contagen/";
  |SINO;
      aDef = aBase + "contasoc/";
  |FINSI;
  aDef = aDef + "def/" + aFichero;
  |CARGA_DEF aDef, espejo1@;
  |SI FSalida ! 0;
      aDef = "";
      |ACABA;
  |SINO;
      |CARGA_DEF aDef, espejo2@;
  |FINSI;

  aQueQuiero = "|NC";
  aNumCampos = #espejo1@#aQueQuiero#;
  nNumCampos = aNumCampos;
  nNumCampos = nNumCampos - 1;
|FPRC;

|PROCESO  DesCargaFichero;
  |SI aDef ! "";
      |DESCARGA_DEF #espejo1@;
      |DESCARGA_DEF #espejo2@;
      aDef = "";
  |FINSI;
|FPRC;

||  *****************************************************
|PROCESO LeeDefinicion;
 |C_M #50Campo, 0, aAlfa;
 |SI aAlfa = "N"; |ACABA; |FINSI;

 |SI Campo = 1; |Y #50#1 = #50#0;
     |MENAV "    Error, origen y destino igual";
     |ERROR;
     |ACABA;
 |FINSI;

 |SI Campo = 1;
     enDPlan  = #0#1;
     nBalNum  = #50#1;
     wTipoPat = 1;
     |HAZ eVerEsPatron;
     |SI wPatOfi = 1;
          |CONFI "    NCodigo de Patron Oficial. Continuar S/N";
          |SI FSalida ! 0;
              |ERROR;
              |ACABA;
          |FINSI;
     |FINSI;
 |SINO;
     |SI aParam = "borrar";
         enDPlan  = #0#0;
         nBalNum  = #50#0;
         wTipoPat = 1;
         |HAZ eVerEsPatron;
         |SI wPatOfi = 1;
              |MENAV "    Codigo de Patron Oficial. No se puede Borrar";
              |ERROR;
              |ACABA;
         |FINSI;
     |FINSI;
 |FINSI;

 |ABRE #420;
 #420#0 = #50Campo;
 |LEE 101#420=;
 |SI FSalida = 0;
     |SI Campo = 0; #50#2 = #420#1; |PINTA #50#2; |FINSI;
     |SI Campo = 1;
         #50#3 = #420#1; |PINTA #50#3;
         |CONFI "    NYa existe Definicion, Continuar ";
         |SI FSalida ! 0;
             |ERROR;
         |FINSI;
     |FINSI;
 |SINO;
     |SI Campo = 1; #50#3 = #50#2; |PINTA #50#3; |FINSI;
     |SI Campo = 0;
         |MENAV "    No existe definicion Origen ";
         |ERROR;
     |FINSI;
 |FINSI;
 |LIBERA #420;
 |CIERRA #420;
|FPRC;

|CALCULO CopiarNum1;
 |PINPA #0#50;
 aAlfa = enDPlan;
 aAlfa = "Definicion Balances Ejercicio " + aAlfa;
 #50#4 = aAlfa;
 |PINDA #0#50;
 |ENDATOS #1#50;
 |SI FSalida ! 0;
     #0#2 = "NO";
     |ACABA;
 |FINSI;

 nBalNum = #50#0;
 |HAZ eExisteCa8m0000;
 |SI wExCa8M0 = 0;
     |MENAV "    Copia cancelada. No existe Tabla Origen ...";
      #0#2 = "NO";
     |ACABA;
 |FINSI;

 nBalNum = #50#1;
 |HAZ eExisteCa8m0000;
 |SI wExCa8M0 = 1;
     |AVISO;
     |CONFI "    NRegrabar Tabla Destino YA EXISTENTE (S/N): ";
     |SI FSalida ! 0;
         |MENAV "    Copia cancelada ...";
         #0#2 = "NO";
         |ACABA;
     |FINSI;
 |FINSI;

 |ABRE #420;
 #420#0 = #50#1;
 |LEE 101#420=;
 |SI FSalida ! 0;
     #420#1 = #50#3;
     |GRABA 020#420n;
 |SINO;
     #420#1 = #50#3;
     |GRABA 020#420a;
 |FINSI;
 |LIBERA #420;
 |CIERRA #420;

 nONum = #50#0;
 nDNum = #50#1;
|FCAL;

||  *****************************************************
|CALCULO BorrarNum1;
 |C_V #50#1, 1, "N"; |C_V #50#3, 1, "N";
 |C_M #50#1, 1, "N";
 |PINPA #0#50;
 aAlfa = enDPlan;
 aAlfa = "Definicion Balances Ejercicio " + aAlfa;
 #50#4 = aAlfa;
 |ATRI R; |PINTA 0911, "Borrar"; |ATRI 0;
 |ATRI I; |PINTA 1111, "Estructura"; |ATRI 0;
          |PINTA 1211, "          ";
 |PINDA #0#50;
 |ENDATOS #1#50;
 |SI FSalida ! 0;
      #0#2 = "NO";
      |ACABA;
 |FINSI;

 nBalNum = #50#0;
 |HAZ eExisteCa8m0000;
 |SI wExCa8M0 = 0;
     |MENAV "    No existe la Estructura ...";
     #0#2 = "NO";
     |ACABA;
 |FINSI;

 #50#1 = #50#0;

 nONum = #50#0;
 nDNum = #50#1;
|FCAL;
||  *****************************************************

|PROCESO Especial;
  |SI aFichero = "ca8m0000";
      nBalNum = #espejo1@#0;
      |HAZ eBorraCa8m0000;
  |SINO;
      nBalNum = #espejo1@#0;
      |HAZ eBorraCa8comay;
  |FINSI;
|FPRC;

|DEFBUCLE TipoEspecial;
  #espejo1@#1001;
  ;
  02;
  99;
  ;
  NULL, Especial;
|FIN;

|PROCESO Duplicar;

  |SI nOpera = 0;
      |BORRA 020#espejo1@.a;
  |FINSI;

  |SI nOpera = 1;
      |PARA nPara1 = 0; |SI nPara1 [ nNumCampos; |HACIENDO nPara1 = nPara1 + 1;
            #espejo2@#nPara1# = #espejo1@#nPara1#;
      |SIGUE;

      #espejo2@#nCampo# = enDPlan;
      |SI aParam = "copiar";
          |SI aFichero = "ca8cumay";
              #espejo2@#10 = nDNum;
          |SINO;
              #espejo2@#0  = nDNum;
          |FINSI;
      |FINSI;
      |GRABA 040#espejo2@.n;
  |FINSI;

  |LIBERA #espejo1@;
|FPRC;

|DEFBUCLE Tipo6;
  #espejo1@#1003;
  ;
  enOPlan, nDesdeNum, 01;
  enOPlan, nHastaNum, 13;
  be;
  NULL, Duplicar;
|FIN;

|DEFBUCLE Tipo5;
  #espejo1@#1005;
  ;
  enOPlan, nDesdeNum, 001, 1,  001;
  enOPlan, nHastaNum, 999, 13, 999;
  be;
  NULL, Duplicar;
|FIN;

|DEFBUCLE Tipo4;
  #espejo1@#1003;
  ;
  enOPlan, nDesdeNum, 001;
  enOPlan, nHastaNum, 999;
  be;
  NULL, Duplicar;
|FIN;

|DEFBUCLE Tipo3;
  #espejo1@#1011;
  ;
  enOPlan, nDesdeNum, 0, 0, " ", 00, 0, 00, 00, 0, 001;
  enOPlan, nHastaNum, 9, 9, "z", 99, 9, 99, 99, 9, 999;
  be;
  NULL, Duplicar;
|FIN;

|DEFBUCLE Tipo2;
  #espejo1@#1010;
  ;
  enOPlan, nDesdeNum, 0, 0, " ", 00, 0, 00, 00, 0;
  enOPlan, nHastaNum, 9, 9, "z", 99, 9, 99, 99, 9;
  be;
  NULL, Duplicar;
|FIN;

|DEFBUCLE Tipo1;
  #espejo1@#1003;
  ;
  enOPlan, nDesdeNum, "     ";
  enOPlan, nHastaNum, "zzzzz";
  be;
  NULL, Duplicar;
|FIN;

|PROCESO DuplicaDatos;
  |HAZ CargaFichero;
  |SI aDef = ""; |ACABA; |FINSI;

  nOpera       = 1;
  nDesdeNum    = 00;
  nHastaNum    = 99;

  |SI aParam = "baja";
      nOpera    = 0;
  |FINSI;
  |SI aParam = "copiar"; |O aParam = "borrar";
      nOpera    = 0;
      nDesdeNum = nDNum;
      nHastaNum = nDNum;
  |FINSI;

  |ABRE #espejo2@;
  |SI nTipo = 1; |BUCLE Tipo1; |FINSI;
  |SI nTipo = 2; |BUCLE Tipo2; |FINSI;
  |SI nTipo = 3; |BUCLE Tipo3; |FINSI;
  |SI nTipo = 4; |BUCLE Tipo4; |FINSI;
  |SI nTipo = 5; |BUCLE Tipo5; |FINSI;
  |SI nTipo = 6; |BUCLE Tipo6; |FINSI;
  |CIERRA #espejo2@;

  |SI aParam = "copiar";
      nOpera    = 1;
      nDesdeNum = nONum;
      nHastaNum = nONum;

      |ABRE #espejo2@;
      |SI nTipo = 1; |BUCLE Tipo1; |FINSI;
      |SI nTipo = 2; |BUCLE Tipo2; |FINSI;
      |SI nTipo = 3; |BUCLE Tipo3; |FINSI;
      |SI nTipo = 4; |BUCLE Tipo4; |FINSI;
      |SI nTipo = 5; |BUCLE Tipo5; |FINSI;
      |SI nTipo = 6; |BUCLE Tipo6; |FINSI;
      |CIERRA #espejo2@;
  |FINSI;

  |HAZ DesCargaFichero;
|FPRC;

|PROCESO Tipo12; |TIPO 12;

 |SI #0#2 = "NO"; enDPlan = 0; |ACABA; |FINSI;

 enOPlan = #0#0;
 enDPlan = #0#1;

 |ABRE #ca8accaa;
 #ca8accaa#0 = enDPlan;
 |LEE 041#ca8accaa.=;
 |SI FSalida ! 0;
     #ca8accaa#0 = enDPlan;
     aAlfa = "Cuentas Anuales Ejercicio " + enDPlan;
     #ca8accaa#1 = aAlfa;
     |GRABA 040#ca8accaa.n;
 |SINO;
     |SI aParam = "baja";
         |SI enDPlan ! 2008; |Y enDPlan ! 2015; |Y enDPlan ! 2016;
            |Y enDPlan ! 2018;
             |BORRA 040#ca8accaa.a;
         |FINSI;
     |FINSI;
 |FINSI;
 |CIERRA #ca8accaa;

 nDesDonde = 0;  || desde contagen

 |SI aParam ! "copiar"; |Y aParam ! "borrar";
     aFichero = "ca8cumay"; nTipo = 1; nCampo = 12; |HAZ DuplicaDatos;
     aFichero = "ca8m0010"; nTipo = 1; nCampo = 5;  |HAZ DuplicaDatos;
 |SINO;
     |SI enOp101 = 2; |O enOp101 = 3;   || plan contable
         aFichero = "ca8cumay"; nTipo = 1; nCampo = 12; |HAZ DuplicaDatos;
         aFichero = "ca8m0010"; nTipo = 1; nCampo = 5;  |HAZ DuplicaDatos;
     |FINSI;
 |FINSI;

 |SI enOp101 ! 2;
     aFichero = "ca8m0001"; nTipo = 2; nCampo = 14; |HAZ DuplicaDatos;
     aFichero = "ca8m0002"; nTipo = 3; nCampo = 14; |HAZ DuplicaDatos;
     aFichero = "ca8m0021"; nTipo = 2; nCampo = 14; |HAZ DuplicaDatos;
     aFichero = "ca8m0022"; nTipo = 3; nCampo = 14; |HAZ DuplicaDatos;

     |SI HaySociedad = 0;
         nDesDonde = 0;  || desde contagen
         aFichero = "ca8m0006"; nTipo = 4; nCampo = 34; |HAZ DuplicaDatos;
         aFichero = "ca8m0007"; nTipo = 5; nCampo = 7;  |HAZ DuplicaDatos;
         aFichero = "ca8mtcol"; nTipo = 6; nCampo = 5;  |HAZ DuplicaDatos;
         aFichero = "ca8mtlin"; nTipo = 4; nCampo = 8;  |HAZ DuplicaDatos;
     |SINO;
         nDesDonde = 1;  || desde sociedad
         aFichero = "soda8m02"; nTipo = 4; nCampo = 34; |HAZ DuplicaDatos;
         aFichero = "soda8m03"; nTipo = 5; nCampo = 7;  |HAZ DuplicaDatos;
         aFichero = "soda8tco"; nTipo = 6; nCampo = 5;  |HAZ DuplicaDatos;
         aFichero = "soda8tli"; nTipo = 4; nCampo = 8;  |HAZ DuplicaDatos;
         aFichero = "soda8m22"; nTipo = 4; nCampo = 34; |HAZ DuplicaDatos;
         aFichero = "soda8m23"; nTipo = 5; nCampo = 7;  |HAZ DuplicaDatos;
     |FINSI;
  |FINSI;

  |SI aParam = "baja"; |O aParam = "borrar";
      |SI enOp101 ! 2;
          nDesDonde = 0;  || desde contagen
          aFichero = "ca8m0000";
          |HAZ CargaFichero;
          |SI aDef = ""; |ACABA; |FINSI;
          |BUCLE TipoEspecial;
          |HAZ DesCargaFichero;
      |FINSI;
      |SI enOp101 ! 1;    ||baja o borrar de ca8comay;
          nDesDonde = 0;  || desde contagen
          aFichero = "ca8comay";
          |HAZ CargaFichero;
          |SI aDef = ""; |ACABA; |FINSI;
          |BUCLE TipoEspecial;
          |HAZ DesCargaFichero;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO DirContasoc;
  |DBASS aBase; |QBT aBase;
  aLosDefs = aBase + "contasoc/def/";
  aElProgr = aLosDefs + "isacceso.mas";
  |XABRE "E", aElProgr, nCanal;
  |SI FSalida ] 0;
      HaySociedad = 1;
  |SINO;
      HaySociedad = 0;
  |FINSI;
  |XCIERRA nCanal;
  aPContas = aBase + "contasoc/";
|FPRC;

|PROGRAMA;

 |HAZ DirContasoc;

 aParam = PARAMETRO; |QBF aParam;
 |SI aParam = "primero";
     #0#0 = enOPlan;
     #0#1 = enDPlan;
     #0#2 = "SI";
     |HAZ Tipo12;
     |ACABA;
 |FINSI;

 |SI enOp101 = 0;
     |CUADRO 2013 2263;
     |PINTA 2115, "Introduzca Password : ";
      E_inf = "";
      E_sup = "20";
      aPassword = 2137 ? -1;
      |SI aPassword ! "<MYPASWD>     ";
          |MENAV "     Permiso denegado.";
          enDPlan = 0;
          |ACABA;
      |FINSI;
      |BLANCO 2013 2263;
 |FINSI;

 |SI aParam = "copiar"; |O aParam = "borrar"; |O aParam = "baja";
     #0#0 = enDPlan;
     #0#1 = enDPlan;
     #0#2 = "SI";
     |SI enOp101 = 0;
         |SI aParam = "copiar"; |HAZ CopiarNum1; |FINSI;
         |SI aParam = "borrar"; |HAZ BorrarNum1; |FINSI;
     |SINO;
          nONum = enONume;
          nDNum = enDNume;
     |FINSI;
     |HAZ Tipo12;
 |SINO;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida ! 0;
         enDPlan = 0;
     |FINSI;
 |FINSI;
|FPRO;
