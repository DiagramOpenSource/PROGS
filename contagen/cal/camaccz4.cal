|FICHEROS;
   camaccz4 #0;

   caconimp #8;
   camaniva #11;
   caivalin #12;

   caivaasi #200;

   camacc01 #501;
   camacc02 #502;
   camacc03 #503;

   cawdatos #900;
   camaccw1 #991;
   camaccw4 #999;
   canempre #30;

   camaccz3;
|FIN;

|VARIABLES;
    aParam     = "";
    aParam1    = "";

    aAlfa1   = "";
    aAlfa2   = "";
    nNume1   = 0;
    nNume2   = 0;
    nPara1   = 0;
    aFichero = "";

    nCampo   = 0;

    nSwAlgun = 0;
    nSwSel   = 0;
    nSw      = 0;

    &aCif      = "";
    &enSwLinea = 0;
    &enTotal   = 0;
    &enTBases  = 0;
    &enTCuotas = 0;
    &eaTexto   = "";
    nOp        = 0;
    nError     = 0;
    &eaError   = "";
    nImpre     = 0;
    informe    = "";
    nSwPrint   = 0;

    nLibro     = 0;
    fFechaFin  = @;
    aDescrip   = "";
    nLinea     = 0;
    nLineaUlt  = 0;
    nNumBase   = 0;
    nEjerCobro = 0;
    nSuma1     = 0;
    nSuma2     = 0;
    nSuma3     = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoiv_i;

|| ***************** Calculos de Pantalla
|CALCULO Primero;
  |HAZ inicio;

  |ABRE #200;
  |LEE 000#200p;
  |SI FSalida ! 0;
      |DDEFECTO #200;
      |GRABA 040#200n;
  |FINSI;
  |CIERRA #200;
  aAlfa1  = #200#44;
|FCAL;

|CALCULO HagoInf;
   aAlfa1 = #0#4;
   |C_M #0#5, 1, aAlfa1;
   |C_M #0#6, 1, aAlfa1;
|FCAL;

|| *************************************************************************
|PROCESO LeoMacc03;
     nNumBase = #503#3;
     #caivalin#0 = #503#0;
     #caivalin#1 = #503#1;
     #caivalin#2 = nNumBase;
     |LEE 141#caivalin.=;
     |SI FSalida ! 0;
         |BORRA 020#503a;
         |LIBERA #503;
         |ACABA;
     |SINO;
         |SI #503#3 = nLineaUlt;
             #503#4 = #503#4 + (#502#10 - nSuma1);
             #503#5 = #503#5 + (#502#11 - nSuma2);
             #503#6 = #503#6 + (#502#12 - nSuma3);
             |GRABA 020#503a;
             || hacer cuadre
         |FINSI;
     |FINSI;
     |LIBERA #503;
|FPRC;

|DEFBUCLE LeoMacc03;
    #503#1;
    ;
    #502#17, #502#0, #502#1, #502#2, 01;
    #502#17, #502#0, #502#1, #502#2, 99;
    be;
    NULL, LeoMacc03;
|FIN;

|PROCESO LeoLineaIVA;
     nNumBase = #caivalin#2;
     |DDEFECTO #503;
     #503#0 = #502#0;
     #503#1 = #502#1;
     #503#2 = #502#2;
     #503#3 = nNumBase;
     #503#7 = #502#17;
     |LEE 141#503=;
     |SI FSalida ! 0;
         |DDEFECTO #503;
         #503#0 = #502#0;
         #503#1 = #502#1;
         #503#2 = #502#2;
         #503#3 = nNumBase;
         #503#7 = #502#17;
         |GRABA 020#503b;
     |FINSI;

|| .. asi el calcul
     #503#4 = ((#caivalin#6 + #caivalin#8 - #caivalin#22) % #502#13) ! 2;
     #503#5 = (#caivalin#6 % #502#13) ! 2;
     #503#6 = (#caivalin#8 % #502#13) ! 2;

     nSuma1 = (nSuma1 + #503#4) ! 2;
     nSuma2 = (nSuma2 + #503#5) ! 2;
     nSuma3 = (nSuma3 + #503#6) ! 2;
     nLineaUlt = #caivalin#2;

     |GRABA 020#503a;
     |LIBERA #503;
|FPRC;

|DEFBUCLE LeoLineaIVA;
     #caivalin#1;
     ;
     #999#0, #999#1, 01;
     #999#0, #999#1, 99;
     e;
     NULL, LeoLineaIVA;
|FIN;

|PROCESO OpLiquidar;

   |SI #501#5 = "S"; |ACABA; |FINSI; || esta toda pagada/cobrada
   |SI #501#6 = #999#8; |Y #501#7 = #999#9;
       |ACABA;
   |FINSI;

   |ABRE #502;
   #502#17 = #501#17;
   #502#0  = #501#0;
   #502#1  = #501#1;
   #502#2  = 99;
   |LEE 041#502];
   |SI FSalida = 0;
       |LEE 041#502a;
   |SINO;
       |LEE 041#502u;
   |FINSI;

   nLinea = 1;
   |SI FSalida = 0; |Y #502#17 = #501#17; |Y #502#0 = #501#0; |Y #502#1 = #501#1;
       nLinea = #502#2 + 1;
   |FINSI;

   |DDEFECTO #502;
   #502#17 = #501#17;
   #502#0  = #501#0;
   #502#1  = #501#1;
   #502#2  = nLinea;
   #502#3  = 99;       ||diario de cierre
   #502#4  = fFechaFin;
   #502#5  = 0;
   #502#6  = 0;
   #502#7  = 12;
   #502#10 = #999#10 - #501#12;  || Total Cobrado
   #502#11 = #999#8 - #501#6;
   #502#12 = #999#9 - #501#7;
   #502#13 = 100 - #501#16;
   #502#14 = "O";
   #502#15 = aDescrip;
   |GRABA 040#502n;

   nLinea = #502#2;
   nSuma1 = 0; nSuma2 = 0; nSuma3 = 0; nLineaUlt = 0;
   |ABRE #503; |BUCLE LeoLineaIVA; |CIERRA #503;
   |ABRE #caivalin; |BUCLE LeoMacc03; |CIERRA #caivalin;

   |CIERRA #502;


   #501#5 = "S";
   #501#6  = #999#8;
   #501#7  = #999#9;
   #501#12 = #999#10;
   #501#16 = 100;
   |GRABA 020#501a;
   |LIBERA #501;

|| .. Para Imprimir
   |SI nLibro ! 0; |Y nLibro ! #501#0;
       |SI nSwPrint = 1; |Y #0#4 = "S";
           |PIEINF;
       |FINSI;
        nSwPrint = 0;
   |FINSI;

   |SI #501#13 = "R";
       eaTexto = "Cobrados";
   |SINO;
       eaTexto = "Pagados";
   |FINSI;

    nLibro = #501#0;
    |SI #0#4 = "S";
        |PRINT;
         nSwPrint = 1;
    |FINSI;
    nSwAlgun = 1;
|FPRC;


|| *************************************************************************
|PROCESO GrabaAuxiliar;
     #991#0  = #501#0;
     #991#1  = #501#1;
     #991#26 = #501#17;
     |LEE 041#991=;
     |SI FSalida ! 0;
         |DDEFECTO #991;
         #991#0  = #501#0;
         #991#1  = #501#1;
         #991#26 = #501#17;
         |GRABA 020#991c;
     |FINSI;

     |SI nSwSel ! 2;
         |PARA nPara1 = 2; |SI nPara1 [ 27; |HACIENDO nPara1 = nPara1 + 1;
               #991nPara1 = #999nPara1;
         |SIGUE;
     |FINSI;
     #991#11 = eaError;
     |GRABA 020#991a;
     |LIBERA #991;
      nError = 1;
|FPRC;

|| //////////////////////////////
|PROCESO Facturas;

   aTipoIVA = #11#36;

   enSwCaja = 0;
   |SI enEjercicio ] 2014;
      |SI #camaniva#42 = "Z"; |O #camaniva#42 = "1"; |O #camaniva#42 = "2";
          |O #camaniva#42 = "3"; |O #camaniva#42 = "4"; |O #camaniva#42 = "5";
          |O #camaniva#42 = "6"; |O #camaniva#42 = "7"; |O #camaniva#42 = "8";
             enSwCaja = 1;
      |FINSI;
   |FINSI;
   |SI enSwCaja = 0; nSwSel = 0; |ACABA; |FINSI;

   nSwSel = 1;

     |DDEFECTO #999;
     #999#0  = #camaniva#0;
     #999#1  = #camaniva#1;
     #999#2  = #camaniva#03;
     #999#3  = #camaniva#02;
     #999#4  = #camaniva#04;
     #999#5  = #camaniva#12;
     #999#6  = #camaniva#27;
     #999#7  = #camaniva#28;
     #999#8  = #camaniva#19;
     #999#9  = #camaniva#23;
     #999#10 = #camaniva#21;
     #999#11 = #camaniva#13;
     #999#12 = #camaniva#14;
     #999#13 = aTipoIVA;
     #999#14 = "N";
     #999#15 = #camaniva#04;
     #999#17 = #camaniva#5;
     #999#21 = #camaniva#7;
     #999#22 = #camaniva#8;
     #999#23 = #camaniva#9;

     #999#16 = #501#12;
     #999#17 = #501#8;
     #999#18 = #501#16;
     #999#19 = #501#6;
     #999#20 = #501#7;

     #999#26 = #501#17;
     #999#27 = #501#18;
|FPRC;

|PROCESO RegCajaOrigen;
   |CIERRA #11;
   |CIERRA #12; ePerioCaja = #501#18; |HAZ ePathCaja;

   |PATH_DAT #11 ePathCaja;
   |PATH_DAT #12 ePathCaja;

   |ABRE #11;
   nSwSel = 0;
   #11#0 = #501#0;
   #11#1 = #501#1;
   |LEE 041#11=;
   |SI FSalida = 0;
       |HAZ Facturas;
   |SINO;
       nSwSel = 2;
       eaError = "No existe Factura";
       |DDEFECTO #999;
   |FINSI;
   |SI nSwSel = 1;
       |HAZ OpLiquidar;
   |FINSI;
   |SI nSwSel ] 2;
       |HAZ GrabaAuxiliar;
   |FINSI;
   |CIERRA #11;
|FPRC;

|DEFBUCLE RegCajaOrigen;
   #501#1;
   ;
   nEjerCobro, 01, 0000000001;
   nEjerCobro, 99, 9999999999;
   e;
   NULL, RegCajaOrigen;
|FIN;

|| ******************************************************
|PROCESO MiraErrores;
   |PRINT;
   nSwPrint = 1;
|FPRC;

|DEFBUCLE MiraErrores;
   #999#1;
   ;
   INICIO;
   FINAL;
   ;
   MiraErrores;
|FIN;

|| ******************************************************
|CALCULO impre; |TIPO 3;

   |SI #0#7 = "NO"; |ACABA; |FINSI;

   aAlfa1 = enEjercicio;
   aAlfa1 = "31.12." + aAlfa1;
   fFechaFin = aAlfa1;
   aDescrip  = "DEVENGO A " + fFechaFin;

   nEjerCobro = enEjercicio - 1;

   aFichero = "y" + Usuario; |NOME_DAT #991 aFichero; |DELINDEX #991;

   nSwAlgun = 0;

   |ABRE #8;
   |DDEFECTO #8;
   |LEE 001#8=;
   |CIERRA #8;
   enDatos = 0; |SI #8#7 = "S"; enDatos=1; |FINSI;
   eaIa = #canempre#21;
   |HAZ LeeDatosEmpresa;

   |SI #0#4 = "S";
        |IMPRE 0;
        |SI FSalida ! 0;
            |MENAV "    Error en Impresora. Proceso abortado";
            |ACABA;
        |FINSI;
        nImpre = 1;

        |INFOR "camaccz4";
        |SI FSalida ! 0;
            |MENAV "    Error en Informe. Proceso abortado";
            |FINIMP;
            |ACABA;
        |FINSI;
    |FINSI;

    |ABRE #991;
    nLibro   = 0;
    nError   = 0;
    nSwPrint = 0;
    |BUCLE RegCajaOrigen;
    |SI nImpre = 1;
        |SI nSwPrint = 1;  |PIEINF;  |FINSI;
        |FININF;
        nSwPrint = 0;
    |FINSI;

    |SI nSwAlgun = 0; |Y aParam = "";
        aAlfa1 = "0000No hay Facturas RECC para liquidar";
        |MENAV aAlfa1;
    |FINSI;

    |CIERRA #991;

    |SI nError ! 0;
        |SI nImpre = 0;
            |IMPRE 0;
            |SI FSalida ! 0;
                |MENAV "    Error en impresora. Proceso abortado";
                |VETE Salir;
            |FINSI;
            nImpre = 1;
        |FINSI;
        #camaccz3#21 = #camaccz4#5;
        #camaccz3#22 = #camaccz4#6;

        |INFOR "camaccy3";
        |SI FSalida ! 0;
            |MENAV "    Error en informe. Proceso abortado";
            |VETE Salir;
        |FINSI;

        nSwPrint = 0;
        |BUCLE MiraErrores;
        |PIEINF;
        |FININF;
    |FINSI;

|ET Salir;
    |SI nImpre = 1;
        |FINIMP;
        nImpre = 0;
    |FINSI;
|FCAL;

|PROGRAMA;
   aParam = PARAMETRO; |QBF aParam;

   |HAZ Primero;
   |SI enEjercicio < 2015;
       |MENAV "    Fras en Regimen Especial de Cr. de Caja a partir del Ejercicio 2015";
       |ACABA;
   |FINSI;

   |DDEFECTO #0;
   #0#0 = enEmpresa;
   #0#1 = eaNombreEmp;
   #0#2 = enPeriodo;
   #0#3 = enEjercicio;

   |SI aParam = "";
       |SI #0#3 < 2014;
           aAlfa2 = "0000Error Empresa con Ejercicio inferior a 2015";
           |MENAV aAlfa2;
           |ACABA;
       |FINSI;

       |CLS;
       |PINPA #0#0;
       |PINDA #0#0;

       |ENDATOS #1#0;
   |FINSI;

   |SI aParam ! "";
       #0#4  = "N";
       #0#7  = "SI";
       |HAZ impre;
   |FINSI;

|FPRO;
