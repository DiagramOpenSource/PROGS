|FICHEROS;
  caz00024 #0;

  canempre #30;

  camaasic #31;
  camaasil #32;
  camandia #33;
  cacuenta #34;
  calibros #35;
  camaniva #36;
  caivalin #37;
  caclipro #38;
  caexistl #39;
  caexistc #40;
  caivam03 #41; || nueva  datos de las intracomunitarias
  camanefe #42; || nueva  los datos de cobros y pagos

  amaasic@ #51;
  amaasil@ #52;
  amandia@ #53;
  acuenta@ #54;
  alibros@ #55;
  amaniva@ #56;
  aivalin@ #57;
  aclipro@ #58;
  aexistl@ #59;
  aexistc@ #60;
  aivam03@ #61; || nueva
  amanefe@ #62; || nueva  los datos de cobros y pagos

|FIN;

|VARIABLES;

 &enSwOpPar = 0; || 0 = Montar Ficheros, 1= Borrar calculo
 &enModelo = 0;
 nEjer     = 0;
 aDia      = "";
 fFecha1   = @;
 fFecha2   = @;

 nNume1   = 0;
 nNume2   = 0;
 nNume3   = 0;
 aAlfa1   = "";
 aAlfa2   = "";
 aAlfa3   = "";
 nSwUno   = 0;
 nMasAlto = 0;
 aUsu     = "";
 aDTip    = "";
 aHTip    = "";

 &enSwError  = 0;
 &aPathConta = "";

 &eaDirOrigen  = "";
 &eaDirDestino = "";

 aEmpresa  = "";
 aEjer     = "";
 nCalC     = 0;
 mascara   = "";
 nLong0    = 0;
 nLong1    = 0;
 nLong2    = 0;
 nLong3    = 0;
 nLong4    = 0;
 nLong5    = 0;
 nLong6    = 0;

 nLibroD   = 0;
 nLExi     = 0;
 nSwLExi   = 0;
 nIncLExi  = 0;
 nSwBueno  = 0;
 nNumDoc   = 0;
 nDoc      = 0;

 nPeriodo  = 0;
 aPeriodo  = "";

 aTitulo   = "";
 Diario    = 0;
 aDire     = "";
 traba     = "";
 traba1    = "";
 temporal1 = "";
 temporal2 = "";
 nError    = 0;
 aDef51    = "";
 aDef52    = "";
 aDef53    = "";
 aDef54    = "";
 aDef55    = "";
 aDef56    = "";
 aDef57    = "";
 aDef58    = "";
 aDef59    = "";
 aDef60    = "";
 aDef61    = "";
 aDef62    = "";
|FIN;

|INCLUYE dscont_i;

|| /////////////////////// PROCESOS DE TRABAJO \\\\\\\\\\\\\\\\\\\\
|PROCESO LeeEmpresa;

 |SI #30#26 < 80;
     nEjer = 2000 + #30#26;
 |SINO;
     nEjer = 1900 + #30#26;
 |FINSI;
 aAlfa1 = #30#11; aAlfa1 = ("00" + aAlfa1) % -102; aAlfa2 = nEjer;
 aAlfa1 = "01." + aAlfa1 + "." + aAlfa2; fFecha1 = aAlfa1;

 nNume1 = nEjer;
 |SI #30#11 > #30#12;  nNume1 = nEjer + 1;  |FINSI;
 aAlfa1 = #30#12; aAlfa1 = ("00" + aAlfa1) % -102; aAlfa2 = nNume1;

 aDia = "31";
 |SI #30#12 = 4; |O #30#12 = 6; |O #30#12 = 9; |O #30#12 = 11;
     aDia = "30";
 |FINSI;
 |SI #30#12 = 2;
     aDia   = "28";
     nNume2 = nNume1 / 4; nNume3 = (nNume1 / 4) ! 0;
     |SI nNume2 = nNume3;  aDia = "29"; |FINSI;
  |FINSI;
  aAlfa1 = aDia + "." + aAlfa1 + "." + aAlfa2; fFecha2 = aAlfa1;

  |SI #0#2 [ fFecha2; |Y #0#3 ] fFecha1;
      nNume1 = #30#3 + 6;
      #0nNume1 = 1;
      nSwUno   = 1;

      |SI nEjer ] nMasAlto;
          nMasAlto = nEjer;
          #0#4 = #30#1;
          #0#16 = #30#13;
          #0#17 = #30#14;
          #0#18 = #30#15;
          #0#19 = #30#16;
          #0#20 = #30#17;
          #0#21 = #30#18;
          #0#22 = #30#19;
      |FINSI;

      |SI enSwOpPar ! 0;
          |HAZ HazTodoDos;
      |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE LeeEmpresa;
 #30#1;
 ;
 enEmpresa, 0;
 enEmpresa, 9;
 e;
 NULL, LeeEmpresa;
|FIN;

|PROGRAMA;

    |DDEFECTO #0;
    nMasAlto = 0;
    #0#0 = enEmpresa;
    #0#1 = enEjercicio;
    aEjer  = enEjercicio; |QBF aEjer;
    aAlfa1 = "01.01."+ aEjer; #0#2 = aAlfa1;
    aAlfa1 = "31.12."+ aEjer; #0#3 = aAlfa1;

    aEmpresa = enEmpresa; |QBF aEmpresa;
    aEmpresa = ("00000" + aEmpresa) % -105;

    |DBASE aBase; |QBF aBase;

    |SI enSwOpPar = 0;
        aMonedaA   = "E";
        enSwError  = 0;
        aPathConta = "";

        nSwUno = 0;
        |BUCLE LeeEmpresa;
        |SI nSwUno = 0;
            enSwError = 1;
            |ACABA;
        |FINSI;

        eaDirDestino = aBase + "fich/" + aEmpresa + aEjer;
        |MKDIR eaDirDestino;
        eaDirDestino = eaDirDestino + "/";
        aPathConta = eaDirDestino;

        |HAZ CargaDefs;
        |SI nError = 1;
            |HAZ DesCargaDefs;
            |ACABA;
        |FINSI;

       |HAZ Acerar;
       |HAZ HazTodo;
    |FINSI;

    |SI enSwOpPar ! 0;
        aAlfa1 = aPathConta % - 0110;
        aAlfa2 = aAlfa1 % 105;
        aAlfa3 = aAlfa1 % 604;
        |SI aAlfa3 ! enEjercicio; |O aAlfa2 ! enEmpresa;
            |ACABA;
        |FINSI;

        eaDirDestino = aPathConta;
        |HAZ CargaDefs;
        |SI nError = 1;
            |HAZ DesCargaDefs;
            |ACABA;
        |FINSI;
        |HAZ Acerar;

        nSwUno = 0;
        |BUCLE LeeEmpresa;
        |HAZ BorrarDirectorio;
    |FINSI;

    |HAZ DesCargaDefs;

|FPRO;

|| ************************************************************************
|PROCESO BorrarDirectorio;
   aDire = "";
   |DIRECTORIO aDire;
   aAlfa1 = aDire + "bin/agirm -r " + eaDirDestino;
   |SYSTEM aAlfa1;
|FPRC;

|PROCESO CargaDefs;
    nError = 0;

    aDef51 = aBase + "def/camaasic.mas";
    |CARGA_DEF aDef51, amaasic@;
    |SI FSalida ! 0; nError = 1; aDef51 = ""; |ACABA; |FINSI;

    aDef52 = aBase + "def/camaasil.mas";
    |CARGA_DEF aDef52, amaasil@;
    |SI FSalida ! 0; nError = 1; aDef52 = ""; |ACABA; |FINSI;

    aDef53 = aBase + "def/camandia.mas";
    |CARGA_DEF aDef53, amandia@;
    |SI FSalida ! 0; nError = 1; aDef53 = ""; |ACABA; |FINSI;

    aDef54 = aBase + "def/cacuenta.mas";
    |CARGA_DEF aDef54, acuenta@;
    |SI FSalida ! 0; nError = 1; aDef54 = ""; |ACABA; |FINSI;

    aDef55 = aBase + "def/calibros.mas";
    |CARGA_DEF aDef55, alibros@;
    |SI FSalida ! 0; nError = 1; aDef55 = ""; |ACABA; |FINSI;

    aDef56 = aBase + "def/camaniva.mas";
    |CARGA_DEF aDef56, amaniva@;
    |SI FSalida ! 0; nError = 1; aDef56 = ""; |ACABA; |FINSI;

    aDef57 = aBase + "def/caivalin.mas";
    |CARGA_DEF aDef57, aivalin@;
    |SI FSalida ! 0; nError = 1; aDef57 = ""; |ACABA; |FINSI;

    aDef58 = aBase + "def/caclipro.mas";
    |CARGA_DEF aDef58, aclipro@;
    |SI FSalida ! 0; nError = 1; aDef58 = ""; |ACABA; |FINSI;

    aDef59 = aBase + "def/caexistl.mas";
    |CARGA_DEF aDef59, aexistl@;
    |SI FSalida ! 0; nError = 1; aDef59 = ""; |ACABA; |FINSI;

    aDef60 = aBase + "def/caexistc.mas";
    |CARGA_DEF aDef60, aexistc@;
    |SI FSalida ! 0; nError = 1; aDef60 = ""; |ACABA; |FINSI;

    aDef61 = aBase + "def/caivam03.mas";
    |CARGA_DEF aDef61, aivam03@;
    |SI FSalida ! 0; nError = 1; aDef61 = ""; |ACABA; |FINSI;

    aDef62 = aBase + "def/camanefe.mas";
    |CARGA_DEF aDef62, amanefe@;
    |SI FSalida ! 0; nError = 1; aDef62 = ""; |ACABA; |FINSI;
|FPRC;

|PROCESO DesCargaDefs;
    |SI aDef62 ! ""; |DESCARGA_DEF #amanefe@; aDef62 = ""; |FINSI;
    |SI aDef61 ! ""; |DESCARGA_DEF #aivam03@; aDef61 = ""; |FINSI;
    |SI aDef60 ! ""; |DESCARGA_DEF #aexistc@; aDef60 = ""; |FINSI;
    |SI aDef59 ! ""; |DESCARGA_DEF #aexistl@; aDef59 = ""; |FINSI;
    |SI aDef58 ! ""; |DESCARGA_DEF #aclipro@; aDef58 = ""; |FINSI;
    |SI aDef57 ! ""; |DESCARGA_DEF #aivalin@; aDef57 = ""; |FINSI;
    |SI aDef56 ! ""; |DESCARGA_DEF #amaniva@; aDef56 = ""; |FINSI;
    |SI aDef55 ! ""; |DESCARGA_DEF #alibros@; aDef55 = ""; |FINSI;
    |SI aDef54 ! ""; |DESCARGA_DEF #acuenta@; aDef54 = ""; |FINSI;
    |SI aDef53 ! ""; |DESCARGA_DEF #amandia@; aDef53 = ""; |FINSI;
    |SI aDef52 ! ""; |DESCARGA_DEF #amaasil@; aDef52 = ""; |FINSI;
    |SI aDef51 ! ""; |DESCARGA_DEF #amaasic@; aDef51 = ""; |FINSI;
|FPRC;

|| ********************************************************************
|| ********************************************************************
|PROCESO CopiaCliPro;
   |DDEFECTO #caclipro;
   aAlfa1 = "|NC";

   nCalC  = #caclipro#aAlfa1#;
   |PARA nNume1 = 0; |SI nNume1 < nCalC; |HACIENDO nNume1 = nNume1 + 1;
         #38nNume1 = #58nNume1;
   |SIGUE;

   eaCuenta = #38#10; |HAZ SacaNivel; #38#11 = enNivel;
   eaCuenta = #38#21; |HAZ SacaNivel; #38#22 = enNivel;
   |GRABA 040#38n;
|FPRC;

|DEFBUCLE CliPro;
   #58#1002;
   ;
   " ","            ";
   "z","zzzzzzzzzzzz";
   e;
   NULL,CopiaCliPro;
|FIN;

|PROCESO Cuentas;
  |SI #54#0 = "            "; |ACABA; |FINSI;
  eaCuenta = #54#0;

  #34#0 = eaCuenta;
  |LEE 040#34=;
  |SI FSalida ! 0;
      |DDEFECTO #34;
       #34#0 = eaCuenta;
       |HAZ SacaNivel;
       #34#1 = enNivel;
       #34#2 = #54#2; #34#3 = #54#3;
       #34#4 = #54#4; #34#5 = #54#5;
       #34#6 = #54#6; #34#7 = #54#7;
       #34#8 = #54#8;
       mascara = #34#0; |QBF mascara;
       mascara = (mascara +"zzzzzzzzzzzz") % 112;
       #34#54  = mascara;
       #34#55  = #34#1;

       |SI enNivel ! 0;
           |GRABA 040#34n;
       |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE Cuentas;
 #54#1001;
 ;
 "            ";
 "zzzzzzzzzzzz";
 e;
 NULL,Cuentas;
|FIN;

|| *************************************************************************
|| *************************************************************************
|| ..................................... Existencias
|PROCESO BuscoLinea0;
 nLExi = #39#1;
 |SI #39#2 = #59#2;
     nSwLExi = 1;
     |ERROR10;
 |FINSI;
|FPRC;

|DEFBUCLE BuscoLinea;
 #39#1;
 ;
 #40#0, 00;
 #40#0, 99;
 e;
 NULL, BuscoLinea0;
|FIN;

|PROCESO LineaExist0;
 eaCuenta = #59#2; |HAZ SacaNivel; #59#3 = enNivel;
 eaCuenta = #59#5; |HAZ SacaNivel; #59#6 = enNivel;
 eaCuenta = #59#8; |HAZ SacaNivel; #59#9 = enNivel;

 |SI aMonedaC ! aMonedaA;
     |PARA nNume1 = 11; |SI nNume1 [ 24; |HACIENDO nNume1 = nNume1 + 1;
           impMoneda = #59nNume1; |HAZ ElCambioMoneda; #59nNume1 = impMoneda;
     |SIGUE;
 |FINSI;

 nLExi = #59#1;
 |SI nIncLExi = 0;
    nSwLExi = 0;
    |BUCLE BuscoLinea;
    |SI nSwLExi = 0;
        nLExi = nLExi + 1;
    |FINSI;
 |FINSI;

 |ABRE #39;
 #39#0 = #59#0;
 #39#1 = nLExi;
 |LEE 040#39=;
 |SI FSalida ! 0;
     |DDEFECTO #39;
     #39#0 = #59#0;
     #39#1 = nLExi;
     #39#2 = #59#2; #39#3 = #59#3; #39#4 = #59#4;
     #39#5 = #59#5; #39#6 = #59#6; #39#7 = #59#7;
     #39#8 = #59#8; #39#9 = #59#9; #39#10= #59#10;
     |GRABA 020#39b;
 |FINSI;

 |PARA nNume1 = 11; |SI nNume1 [ 24; |HACIENDO nNume1 = nNume1 + 1;
       #39nNume1 = #39nNume1 + #59nNume1;
 |SIGUE;
 |GRABA 040#39a;
 |LIBERA #39;
 |CIERRA #39;

|FPRC;

|DEFBUCLE LineaExist;
 #59#1002;
 ;
 #60#0, 00;
 #60#0, 99;
 e;
 NULL, LineaExist0;
|FIN;

|PROCESO CopiaExist;
 nIncLExi = 0;
 #40#0 = #60#0;
 |LEE 141#40=;
 |SI FSalida ! 0;
     nIncLExi = -1;
     #40#0 = #60#0;
     #40#1 = #60#1;
     eaCuenta = #60#2; |HAZ SacaNivel; #40#3 = enNivel;
     |GRABA 020#40b;
 |FINSI;
 |BUCLE LineaExist;
 |LIBERA #40;
|FPRC;

|DEFBUCLE LasExistencias;
 #60#1001;
 ;
 00;
 99;
 e;
 NULL,CopiaExist;
|FIN;

|| ..................................... IVA/IRPF
|PROCESO CopiaLinIVA;
   |DDEFECTO #37;

   aAlfa1 = "|NC";
   nCalC  = #caivalin#aAlfa1#;
   |PARA nNume1 = 0; |SI nNume1 < nCalC; |HACIENDO nNume1 = nNume1 + 1;
         #37nNume1 = #57nNume1;
   |SIGUE;
   #37#0  = #36#0;    || Libro
   #37#1  = #36#1;    || Documento

   |SI aMonedaC ! aMonedaA;
       impMoneda = #37#6;  |HAZ ElCambioMoneda; #37#6 = impMoneda;
       impMoneda = #37#8;  |HAZ ElCambioMoneda; #37#8 = impMoneda;
       impMoneda = #37#10; |HAZ ElCambioMoneda; #37#10 = impMoneda;
       impMoneda = #37#11; |HAZ ElCambioMoneda; #37#11 = impMoneda;
       impMoneda = #37#20; |HAZ ElCambioMoneda; #37#20 = impMoneda;
       impMoneda = #37#22; |HAZ ElCambioMoneda; #37#22 = impMoneda;
   |FINSI;

   |GRABA 040#37n;
|FPRC;

|DEFBUCLE BuscaLinIVA;
 #57#1003;
 ;
 #56#0, #56#1, 01;
 #56#0, #56#1, 99;
 e;
 NULL, CopiaLinIVA;
|FIN;

|PROCESO CopiaFacturas;
/*
   nSwBueno = 0;
   |SI #56#4 ] #0#2; |Y #56#4 [ #0#3;
       nSwBueno = 1;
   |SINO;
       |SI #56#8 ] #0#2; |Y #56#8 [ #0#3;
           nSwBueno = 1;
       |FINSI;
   |FINSI;
   |SI nSwBueno = 0; |ACABA; |FINSI;
*/

   nLibroD = #56#0;
   |HAZ GrabaLibro;

   #36#0 = nLibroD;
   #36#1 = nNumDoc;

   aAlfa1 = "|NC";
   nCalC  = #camaniva#aAlfa1#;
   |PARA nNume1 = 2; |SI nNume1 < nCalC; |HACIENDO nNume1 = nNume1 + 1;
         #36nNume1 = #56nNume1;
   |SIGUE;

   |SI aMonedaC ! aMonedaA;
      impMoneda = #36#15; |HAZ ElCambioMoneda; #36#15 = impMoneda;
      impMoneda = #36#17; |HAZ ElCambioMoneda; #36#17 = impMoneda;
      impMoneda = #36#19; |HAZ ElCambioMoneda; #36#19 = impMoneda;
      impMoneda = #36#20; |HAZ ElCambioMoneda; #36#20 = impMoneda;
      impMoneda = #36#21; |HAZ ElCambioMoneda; #36#21 = impMoneda;
      impMoneda = #36#23; |HAZ ElCambioMoneda; #36#23 = impMoneda;
      impMoneda = #36#26; |HAZ ElCambioMoneda; #36#26 = impMoneda;
   |FINSI;

   |GRABA 040#36n;

   |BUCLE BuscaLinIVA;

   #61#0 = #56#0;
   #61#1 = #56#1;
   |LEE 001#61=;
   |SI FSalida = 0;
       #41#0 = nLibroD;
       #41#1 = nNumDoc;

       aAlfa1 = "|NC";
       nCalC  = #caivam03#aAlfa1#;
       |PARA nNume1 = 2; |SI nNume1 < nCalC; |HACIENDO nNume1 = nNume1 + 1;
             #41nNume1 = #61nNume1;
       |SIGUE;

      |SI aMonedaC ! aMonedaA;
          impMoneda = #41#6;  |HAZ ElCambioMoneda; #41#6  = impMoneda;
          impMoneda = #41#9;  |HAZ ElCambioMoneda; #41#9  = impMoneda;
          impMoneda = #41#11; |HAZ ElCambioMoneda; #41#11 = impMoneda;
          impMoneda = #41#12; |HAZ ElCambioMoneda; #41#12 = impMoneda;
          impMoneda = #41#14; |HAZ ElCambioMoneda; #41#14 = impMoneda;
          impMoneda = #41#15; |HAZ ElCambioMoneda; #41#15 = impMoneda;
          impMoneda = #41#17; |HAZ ElCambioMoneda; #41#17 = impMoneda;
          impMoneda = #41#18; |HAZ ElCambioMoneda; #41#18 = impMoneda;
          impMoneda = #41#20; |HAZ ElCambioMoneda; #41#20 = impMoneda;
          impMoneda = #41#21; |HAZ ElCambioMoneda; #41#21 = impMoneda;
          impMoneda = #41#23; |HAZ ElCambioMoneda; #41#23 = impMoneda;
          impMoneda = #41#24; |HAZ ElCambioMoneda; #41#24 = impMoneda;
          impMoneda = #41#26; |HAZ ElCambioMoneda; #41#26 = impMoneda;
          impMoneda = #41#27; |HAZ ElCambioMoneda; #41#27 = impMoneda;
          impMoneda = #41#29; |HAZ ElCambioMoneda; #41#29 = impMoneda;
      |FINSI;

      |GRABA 040#41n;
   |FINSI;
|FPRC;

|DEFBUCLE LasFacturas;
 #56#2003;
 ;
 00, #0#2, 000001;
 99, #0#3, 999999;
 e;
 NULL,CopiaFacturas;
|FIN;

|| ------------------------------------------------------------------------
|PROCESO CopiaCobros;
   |DDEFECTO #42;

   aAlfa1 = "|NC";
   nCalC  = #camanefe#aAlfa1#;
   |PARA nNume1 = 0; |SI nNume1 < nCalC; |HACIENDO nNume1 = nNume1 + 1;
         #42nNume1 = #62nNume1;
   |SIGUE;

   #42#0  = #31#0;    ||Diario
   #42#2  = #31#2;    ||Documento
   |GRABA 040#42n;
|FPRC;

|DEFBUCLE BuscaCobros;
 #62#1004;
 ;
 #51#0, #51#1, #51#2, 0001;
 #51#0, #51#1, #51#2, 9999;
 e;
 NULL, CopiaCobros;
|FIN;

|PROCESO CopiaLineas;
   |DDEFECTO #32;

   aAlfa1 = "|NC";
   nCalC  = #camaasil#aAlfa1#;
   |PARA nNume1 = 0; |SI nNume1 < nCalC; |HACIENDO nNume1 = nNume1 + 1;
         #32nNume1 = #52nNume1;
   |SIGUE;

   #32#0  = #31#0;    ||Diario
   #32#2  = #31#2;    ||Documento
   #32#24 = #31#12;   ||Registro
   |SI aMonedaC ! aMonedaA;
       impMoneda = #32#9; |HAZ ElCambioMoneda; #32#9 = impMoneda;
   |FINSI;

   eaCuenta = #32#4;  |HAZ SacaNivel; #32#5  = enNivel;
   eaCuenta = #32#10; |HAZ SacaNivel; #32#11 = enNivel;
   eaCuenta = #32#16; |HAZ SacaNivel; #32#17 = enNivel;

   |GRABA 040#32n;

   |HAZ ActualizaLin;
|FPRC;

|DEFBUCLE BuscaLineas;
 #52#1004;
 ;
 #51#0, #51#1, #51#2, 0001;
 #51#0, #51#1, #51#2, 9999;
 e;
 NULL, CopiaLineas;
|FIN;

|PROCESO CopiaAsientos;

   Diario = #51#0;
   |HAZ ElDiario;

   #31#0 = Diario;
   #31#1 = #51#1;
   #31#2 = #51#2;
   nDoc = #31#2;
   |LEE 040#31=;
   |SI FSalida = 0;
       |ACABA;
   |FINSI;

   aAlfa1 = "|NC";
   nCalC  = #camaasic#aAlfa1#;
   |PARA nNume1 = 0; |SI nNume1 < nCalC; |HACIENDO nNume1 = nNume1 + 1;
         #31nNume1 = #51nNume1;
   |SIGUE;
   #31#0 = Diario;
   #31#2 = nDoc;

   |SI aMonedaC ! aMonedaA;
      impMoneda = #31#4; |HAZ ElCambioMoneda; #31#4 = impMoneda;
      impMoneda = #31#5; |HAZ ElCambioMoneda; #31#5 = impMoneda;
      impMoneda = #31#6; |HAZ ElCambioMoneda; #31#6 = impMoneda;
   |FINSI;

   eaCuenta = #31#8;  |HAZ SacaNivel; #31#9  = enNivel;
   eaCuenta = #31#10; |HAZ SacaNivel; #31#11 = enNivel;

   |HAZ NuevoRegistro;
   #31#12 = nREGISTRO;
   |GRABA 040#31n;

   |BUCLE BuscaLineas;
   |BUCLE BuscaCobros;
|FPRC;

|DEFBUCLE LosAsientos;
 #51#1003;
 ;
 00, #0#2, 000001;
 99, #0#3, 999999;
 e;
 NULL,CopiaAsientos;
|FIN;

|| *********************************************************************
|PROCESO CopioOtros;
 || Ficheros Maestros
    || Estructura Balance
    traba1 = "caagrupa";  |HAZ copiando;    ||  1 agrupaciones
    traba1 = "caepigra";  |HAZ copiando;    ||  2 epigrafes
    traba1 = "caparbal";  |HAZ copiando;    ||  3 partidas de balance
    traba1 = "caparexp";  |HAZ copiando;    ||  4 partidas de explotacion
    traba1 = "cacuebal";  |HAZ copiando;    ||  5 cuentas balance
    traba1 = "cacuesig";  |HAZ copiando;    ||  6 cuentas balance segun Signo
    traba1 = "caconimp";  |HAZ copiando;    || 22 Tipo de impresos
    traba1 = "caivaasi";  |HAZ copiando;    || 23 Control de IVA
    traba1 = "caivaaxx";  |HAZ copiando;    ||    Control de IVA por Act.
    traba1 = "caclipro";  |HAZ copiando;    ||    Clientes y Proveedores
    traba1 = "camandia";  |HAZ copiando;    || 12 diarios

    traba1 = "cadeflab";  |HAZ copiando;    || 26 Defectos Laboral
    traba1 = "cadefxab";  |HAZ copiando;    || 26 Defectos Laboral
    traba1 = "cadeflal";  |HAZ copiando;    || 27 Defectos Laboral Lineas
    traba1 = "cadefxal";  |HAZ copiando;    ||
    traba1 = "cadeflac";  |HAZ copiando;    || 28 Defectos Laboral Centros
    traba1 = "cadeflax";  |HAZ copiando;    || --
    traba1 = "cadefxax";  |HAZ copiando;    || --
    traba1 = "cadeflay";  |HAZ copiando;    || --
    traba1 = "cadeflaz";  |HAZ copiando;    || --
    traba1 = "cadefxaz";  |HAZ copiando;    || --
|FPRC;

|PROCESO copiando;
  temporal1 = eaDirOrigen + traba1 + ".ixx";
  temporal2 = eaDirDestino + traba1 + ".ixx";
  |COPIA_FICHERO temporal1,temporal2;
  |SI FSalida ! 0;  |ACABA;  |FINSI;
  temporal1 = eaDirOrigen + traba1 + ".dat";
  temporal2 = eaDirDestino + traba1 + ".dat";
  |COPIA_FICHERO temporal1,temporal2;
  |SI FSalida ! 0;
      temporal2 = eaDirDestino + traba1 + ".ixx";
      |FBORRA temporal2;
      |ACABA;
  |FINSI;
  temporal1 = eaDirOrigen + traba1 + ".ddx";
  temporal2 = eaDirDestino + traba1 + ".ddx";
  |COPIA_FICHERO temporal1,temporal2;
|FPRC;
|| *********************************************************************

|PROCESO ProcesaEmpresa;

   cod1 = enEmpresa;
   cod2 = nPeriodo;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;
   aMonedaC = eaMoneda;

   aPeriodo = nPeriodo; |QBF aPeriodo;
   eaDirOrigen = aBase + "fich/" + aEmpresa + aPeriodo + "/";

   |PATH_DAT #51 eaDirOrigen;
   |PATH_DAT #52 eaDirOrigen;
   |PATH_DAT #53 eaDirOrigen;
   |PATH_DAT #54 eaDirOrigen;
   |PATH_DAT #55 eaDirOrigen;
   |PATH_DAT #56 eaDirOrigen;
   |PATH_DAT #57 eaDirOrigen;
   |PATH_DAT #58 eaDirOrigen;
   |PATH_DAT #59 eaDirOrigen;
   |PATH_DAT #60 eaDirOrigen;
   |PATH_DAT #61 eaDirOrigen;
   |PATH_DAT #62 eaDirOrigen;

   nLong0 = #30#13; dig3 = #0#16;
   nLong1 = #30#14; dig4 = #0#17;      || 1 nivel
   nLong2 = #30#15; dig5 = #0#18;      || 2 nivel
   nLong3 = #30#16; dig6 = #0#19;      || 3 nivel
   nLong4 = #30#17; dig7 = #0#20;      || 4 nivel
   nLong5 = #30#18; dig8 = #0#21;      || 5 nivel
   nLong6 = #30#19; dig9 = #0#22;      || 6 nivel

   |SI nSwUno = 0;
       |HAZ CopioOtros;
   |FINSI;

   |ABRE #34;
   aTitulo = "Tratando Plan Contable ";
   |BUCLE Cuentas;
   |CIERRA #34;

   |SI  nSwUno = 1;
       |ABRE #38;
       aTitulo = "Tratando Clientes / Proveedores ";
       |BUCLE CliPro;
       |CIERRA #38;
   |FINSI;
|| ................... Pase de Asientos
    |ABRE #31;
    |ABRE #32;
    |ABRE #42;
    aTitulo = "Tratando Apuntes Contables ";
    |BUCLE LosAsientos;
    |CIERRA #42;
    |CIERRA #32;
    |CIERRA #31;

    |ABRE #36;
    |ABRE #37;
    |ABRE #41;
    |ABRE #61;
    aTitulo = "Tratando Registros IVA/IRPF ";
    |BUCLE LasFacturas;
    |CIERRA #61;
    |CIERRA #41;
    |CIERRA #37;
    |CIERRA #36;

    aTitulo = "Tratando Existencias ";
    |ABRE #40;
    |BUCLE LasExistencias;
    |CIERRA #40;

    nSwUno = 1;
|FPRC;

|PROCESO HazTodo;
  nSwUno   = 0;
  |PARA nPeriodo = 0; |SI nPeriodo [ 9; |HACIENDO nPeriodo = nPeriodo + 1;

        nNume1 = nPeriodo + 6;
        |SI #0nNume1 = 1;
            |HAZ ProcesaEmpresa;
        |FINSI;
  |SIGUE;

|FPRC;

|| ... Rutinas Varias .......................................................
|PROCESO Acerar;
  |PATH_DAT #31 eaDirDestino;
  |PATH_DAT #32 eaDirDestino;
  |PATH_DAT #33 eaDirDestino;
  |PATH_DAT #34 eaDirDestino;
  |PATH_DAT #35 eaDirDestino;
  |PATH_DAT #36 eaDirDestino;
  |PATH_DAT #37 eaDirDestino;
  |PATH_DAT #38 eaDirDestino;
  |PATH_DAT #39 eaDirDestino;
  |PATH_DAT #40 eaDirDestino;
  |PATH_DAT #41 eaDirDestino;
  |PATH_DAT #42 eaDirDestino;

  |SI enSwOpPar = 0;
   |DELINDEX #camaasic;
   |DELINDEX #camaasil;
   |DELINDEX #camandia;
   |DELINDEX #camaniva;
   |DELINDEX #caivalin;
   |DELINDEX #caexistc;
   |DELINDEX #caexistl;
   |DELINDEX #cacuenta;
   |DELINDEX #caivam03;
   |DELINDEX #camanefe;
 |FINSI;
|FPRC;


|PROCESO GrabaLibro;
 |ABRE #35;
 #35#0 = nLibroD;
 |LEE 040#35=;
 |SI FSalida ! 0;
     |DDEFECTO #35;
     #35#0 = nLibroD;
     |ABRE #55;
     #55#0 = #56#0;
     |LEE 040#55=;
     |SI FSalida = 0;
         #35#1 = #55#1;
         #35#2 = #55#2;
         #35#3 = #55#3;
     |SINO;
         #35#1 = "LIBRO SIN DEFINIR";
         #35#2 = #56#36;
         #35#3 = 1;
    |FINSI;
    |CIERRA #55;
    |GRABA 020#35n;
 |FINSI;
 |CIERRA #35;

 #36#0 = nLibroD;
 #36#1 = 9999999999;
 |LEE 010#36];
 |SI FSalida = 0;
     |LEE 040#36a;
 |SINO;
     |LEE 040#36u;
 |FINSI;
 nNumDoc = 1;
 |SI FSalida = 0; |Y #36#0 = nLibroD;
     nNumDoc = #36#1 + 1;
 |FINSI;
|FPRC;

|PROCESO ElDiario;
 |ABRE #33;
 #33#0 = Diario;
 |LEE 040#33=;
 |SI FSalida ! 0;
     |ABRE #53;
     #53#0 = #51#0;
     |LEE 040#53=;
     |SI FSalida ! 0;
         |DDEFECTO #53;
     |FINSI;
     |CIERRA #53;
     |DDEFECTO #33;
     #33#0 = Diario;
     #33#1 = #53#1;
     |GRABA 020#33n;
 |FINSI;
 |CIERRA #33;
|FPRC;

|PROCESO ActualizaLin;
       dirfich0 = eaDirDestino;
       MasMenos     = #32#9;
       DebeHaber    = #32#8;
       aCUENTA      = #32#4;
       nREGISTRO    = #32#24;
       nDIARIO      = #32#0;
       aFECHA       = #32#1;
       nDOCUMENTO   = #32#2;
       nNIVELCUENTA = #32#5;
       |HAZ ActDiario;          || diario
       |HAZ ActCuenta;          || cuenta
       |HAZ ActTesteo;          || cuenta
|FPRC;
|| **************************************************************************
|PROCESO LasFacturasDos;

   |SELECT #3#56;
   |DDEFECTO #56;
   #56#0 = #36#0;
   #56#2 = #36#2;
   #56#3 = #36#3;
   |LEE 001#56=;
   |SI FSalida = 0; |Y #56#4 = #36#4;
       #56#40 = #36#40;
       |GRABA 020#56a;
   |FINSI;
   |LIBERA #56;
   |SELECT #1#56;
|FPRC;

|DEFBUCLE LasFacturasDos;
 #36#1;
 8,36;
 01, 0000000001, fFecha1, aDTip;
 99, 9999999999, fFecha2, aHTip;
 e;
 NULL,LasFacturasDos;
|FIN;

|PROCESO HazTodoDos;

   nPeriodo = #30#3;
   aPeriodo = nPeriodo; |QBF aPeriodo;
   eaDirOrigen = aBase + "fich/" + aEmpresa + aPeriodo + "/";

   |PATH_DAT #51 eaDirOrigen;
   |PATH_DAT #52 eaDirOrigen;
   |PATH_DAT #53 eaDirOrigen;
   |PATH_DAT #54 eaDirOrigen;
   |PATH_DAT #55 eaDirOrigen;
   |PATH_DAT #56 eaDirOrigen;
   |PATH_DAT #57 eaDirOrigen;
   |PATH_DAT #58 eaDirOrigen;
   |PATH_DAT #59 eaDirOrigen;
   |PATH_DAT #60 eaDirOrigen;
   |PATH_DAT #61 eaDirOrigen;
   |PATH_DAT #62 eaDirOrigen;

   nLong0 = #30#13; dig3 = #0#16;
   nLong1 = #30#14; dig4 = #0#17;      || 1 nivel
   nLong2 = #30#15; dig5 = #0#18;      || 2 nivel
   nLong3 = #30#16; dig6 = #0#19;      || 3 nivel
   nLong4 = #30#17; dig7 = #0#20;      || 4 nivel
   nLong5 = #30#18; dig8 = #0#21;      || 5 nivel
   nLong6 = #30#19; dig9 = #0#22;      || 6 nivel

  |SI enSwOpPar = 1;  || modificacion de facturas
      |ABRE #56;
      aTitulo = "Tratando Registros IVA/IRPF ";
      aDTip = "R";
      aHTip = "S";
      |BUCLE LasFacturasDos;
      |CIERRA #56;
  |FINSI;
  |SI enSwOpPar = 2;  || modificacion de facturas
  |FINSI;
|FPRC;
