|FICHEROS;
   cacuedep #0;
   cacuedlp #1;
   cafantas #2;

   :/contagen/def/cacuebal #5;    || Cuentas de balance
   :/contagen/def/caparbal #6;    || partidas balances
   :/contagen/def/caparexp #7;    || partidas explotacion

   canempre #30;
   caselem1 #998;
|FIN;

|VARIABLES;
   &entrada = 1;
   &eaTex1 = "";
   &eaTex2 = "";
   &eaTex3 = "";
   aDTit   = "";
   aHTit   = "";
   aDCod   = "";
   aHCod   = "";
   nCampo  = 0;
   nSwSaldo = 0;

   &eSwSelec  = 0;
   &eaDDepar  = "";
   &eaHDepar  = "";
   &efDFecha  = @;
   &efHFecha  = @;
   &eaNomFDep = "";  ||Nombre del Fichero Creado
|FIN;

|INCLUYE dscont_i;

|| **************************** PROCESOS PANTALLA
|CALCULO cuenta; |TIPO 0;
     eaCuenta = #cacuedlp#Campo#;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #cacuedlp#Campo# = eaCuenta;
     |PINTA #cacuedlp#Campo#;
|FCAL;

|| ........................
|PROCESO ImprimeTempo;

   nSwSaldo = 0;
   |PARA nCampo = 0;  |SI nCampo [ 53;  |HACIENDO nCampo = nCampo + 1;
         |SI #0nCampo ! 0;
              nSwSaldo = 1;
              |SAL;
         |FINSI;
   |SIGUE;
   |SI #1#5 = "N"; |Y nSwSaldo = 0;
       |ACABA;
   |FINSI;

   |HAZ Lecturas;
   |PRINT;
   |PIEINF;
|FPRC;

|DEFBUCLE cacuenta_D;
   #0#2;
   ;
   eaDDepar, aDCod;
   eaHDepar, aHCod;
   ;
   NULL, ImprimeTempo;
|FIN;

|DEFBUCLE cacuenta_C;
   #0#1;
   ;
   aDCod, eaDDepar;
   aHCod, eaHDepar;
   ;
   NULL, ImprimeTempo;
|FIN;

|PROCESO HazTodo;
  eSwSelec  = 2;
  efDFecha  = fec1;
  efHFecha  = fec2;
  |ATRI I;
  |PINTA 2302, "Procesando Cuentas por Departamentos ... Espere ";
  |ATRI 0;
  |SUB_EJECUTA_NP "cacuedep";
  |PINTA 2302, "                                                ";
  |NOME_DAT #0 eaNomFDep;
  |PATH_DAT #0 dirfich0;

  |SI #1#4 = 1; |BUCLE cacuenta_C; |FINSI;
  |SI #1#4 = 2; |BUCLE cacuenta_D; |FINSI;
|FPRC;

||*************************************************************************

|CALCULO impre;|TIPO 3;

   aDCod = #1#0;     aHCod = #1#1;
   eaDDepar  = #1#2; eaHDepar  = #1#3;
   aDTit = " " * 36; aHTit = "z" * 36;

   |IMPRE 0;
   |SI FSalida ! 0;  |ACABA;  |FINSI;

   |INFOR "cacuedep";
   |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;


   |SI #48#27 = "S";
       |HAZ MultiEmpresa;
   |SINO;
       |DFICE dirfich0; |QBF dirfich0;
       |HAZ HazTodo;
   |FINSI;
   |FININF;
   |FINIMP;
|FCAL;
||*************************************************************************

|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #5 dirfich0;
   |PATH_DAT #6 dirfich0;
   |PATH_DAT #7 dirfich0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 000001;
 999999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;

||*************************************************************************
|PROCESO Lecturas;

 eaTex1 = "";  eaTex2 = "";  eaTex3 = "";
 |SI #0#4 = 0;
     |ABRE #6;
     #caparbal#0 = #0#5;     || tipo
     #caparbal#1 = #0#6;     || agrupacion
     #caparbal#3 = #0#7;     || epigrafe
     #caparbal#5 = #0#8;     || partida
     |LEE 001#6=;
     |SI FSalida ! 0;  |DDEFECTO #6;  |FINSI;
     eaTex1 = #6#2;
     eaTex2 = #6#4;
     eaTex3 = #6#6;
     |CIERRA #6;
  |SINO;
     |ABRE #7;
     #caparexp#0 = #0#4;     || bloque
     #caparexp#1 = #0#5;     || tipo
     #caparexp#2 = #0#6;     || agrupacion
     #caparexp#4 = #0#7;     || epigrafe
     #caparexp#6 = #0#8;     || partida
     |LEE 001#7=;
     |SI FSalida ! 0;  |DDEFECTO #7;  |FINSI;
     eaTex1 = #7#3;
     eaTex2 = #7#5;
     eaTex3 = #7#7;
     |CIERRA #7;
  |FINSI;

  |SI eaDepar = "N"; |ACABA; |FINSI;

   #2#8  = "";
   #2#10 = "01.01.0000";
   #2#11 = "01.01.0000";
   eOpDep = 0;
   enActividad = #0#57;
   eaCodDep    = #0#57;
   |HAZ Actividad;
   |SI eswLect = 0;
       #2#8  = eaNombreAct;
       #2#10 = efActIni;
       #2#11 = efActFin;
   |FINSI;
   #0#57 = eaCodDep;
|FPRC;
