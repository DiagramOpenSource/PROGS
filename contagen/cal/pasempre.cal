|FICHEROS;
   pasempre #0;
   camaasil #1;
   camaniva #2;
   caivalin #3;
   camaasic #4;
   cacuenta #5;
   casiim01 #6;
   casiim02 #7;
   camandia #10;
   caivam03 #223;

   amaasil@ #11;
   amaniva@ #12;
   aivalin@ #13;
   amaasic@ #14;
   asiim01@ #16;
   asiim02@ #17;
   aivam03@ #113;

   canempre #30;
|FIN;

|VARIABLES;
   nDef11     = 0;
   nDef12     = 0;
   nDef13     = 0;
   nDef14     = 0;
   nDef16     = 0;
   nDef17     = 0;
   nDef113    = 0;
   nSwAlguno  = 0;
   aAlfa1     = "";
   aAlfa2     = "";
   nNume1     = 0;
   nNume2     = 0;
   nCalC      = 0;
   nNumRegFac = 0;
   nPara1     = 0;
   nError     = 0;
   aH60       = "";

   &eaDirDestino = "";
   &eaRenumera   = "";
   &enDnDIARIO    = 0;
   &enHnDIARIO    = 0;
   &efDFechas    = @;
   &efHFechas    = @;
   &enDDocume    = 0;
   &enHDocume    = 0;
   &enBorraAu    = 1;
   &swVarios     = 0;
   &r_emp = 0;
   &r_per = 0;
|FIN;

|INCLUYE dscont_i;

|| *****************************************************
|CALCULO consulta;
  |SI FSalida ! 9;  |ACABA;  |FINSI;
  r_emp = 0;
  |SUB_EJECUTA_NP "caconemp";
  |ERROR;
  #0#6 = r_emp;
  #0#7 = r_per;
  |PTEC 802;
  |PTEC 802;
|FCAL;

|CALCULO esbuena;
  |SI FSalida = 9;  |ACABA;  |FINSI;
  |ABRE #30;
  #30#2 = #0#6;
  #30#3 = 0;
  |LEE 011#30];
  |SI FSalida ! 0;  |O #30#2 ! #0#6;
      |MENAV "     NO Existe la Empresa Contable";
      |ERROR;
  |FINSI;
  |CIERRA #30;
|FCAL;

|CALCULO esbuena1;
  |SI FSalida = 2; |ACABA; |FINSI;

  |SI #0#6 = enEmpresa; |Y #0#7 = enPeriodo;
      |MENAV "    Error, ha introducido la misma Empresa ";
      |ERROR;
      |ACABA;
  |FINSI;
  |ABRE #30;
  #30#2 = #0#6;
  #30#3 = #0#7;
  |LEE 011#30=;
  |SI FSalida ! 0;
      |MENAV "     NO Existe Periodo en la empresa Contable";
      |CIERRA #30;
      |ERROR;
      |ACABA;
  |FINSI;
  #0#8 = #30#1;
  |CIERRA #30;

  |PINTA #0#8;
|FCAL;

|CALCULO Defectos;
   #0#2 = fec1;  |PINTA #0#2;
   #0#3 = fec2;  |PINTA #0#3;
|FCAL;

|CALCULO Fecha; |TIPO 0;   || Desde/hasta fecha
   |SI #0Campo < fec1; |O #0Campo > fec2;
      |MENAV "        ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
      |ERROR;
   |FINSI;
|FCAL;

|CALCULO DiarioHasta; | TIPO 0;   ||  hasta diario
   |SI #0#1 < #0#0;  |ERROR;  |FINSI;
|FCAL;

|CALCULO FechaHasta; | TIPO 0;   ||  hasta fecha
   |SI #0#3 < #0#2;  |ERROR;  |FINSI;
|FCAL;

|CALCULO DocumentoHasta; | TIPO 0;   ||  hasta documento
   |SI #0#5 < #0#4;  |ERROR;  |FINSI;
|FCAL;

||*************************************************************************
|PROCESO Lcasiim02;
   aAlfa1 = "|NC";
   nCalC = #casiim02#aAlfa1#;
   |PARA nNume1 = 0; |SI nNume1 < nCalC; |HACIENDO nNume1 = nNume1 + 1;
         #asiim02@#nNume1# = #casiim02#nNume1#;
   |SIGUE;

   #asiim02@#2 = #amaniva@#1;

   |GRABA 020#asiim02@.n;
   |LIBERA #casiim02;
|FPRC;

|DEFBUCLE Lcasiim02;
   #casiim02#1;
   ;
   #casiim01#0, #casiim01#1, #casiim01#2, "";
   #casiim01#0, #casiim01#1, #casiim01#2, aH60;
   be;
   NULL, Lcasiim02;
|FIN;

|PROCESO Lineas;
   aAlfa1 = "|NC";
   nCalC = #caivalin#aAlfa1#;
   |PARA nNume1 = 0; |SI nNume1 < nCalC; |HACIENDO nNume1 = nNume1 + 1;
         #aivalin@#nNume1# = #caivalin#nNume1#;
   |SIGUE;

   #aivalin@#1 = #amaniva@#1;
   |GRABA 020#aivalin@.n;
   |LIBERA #caivalin;
|FPRC;

|DEFBUCLE Lcaivalin;
   #caivalin#1;
   ;
   #camaniva#0,#camaniva#1,01;
   #camaniva#0,#camaniva#1,99;
   be;
   NULL,Lineas;
|FIN;


|PROCESO PasaRegs;
   nNumRegFac = #camaniva#1;
   #amaniva@#0 = #camaniva#0;
   #amaniva@#1 = #camaniva#1;
   |LEE 040#amaniva@.=;
   |SI FSalida = 0;
       |ACABA;
   |FINSI;

   aAlfa1 = "|NC";
   nCalC  = #camaniva#aAlfa1#;
   |PARA nNume1 = 0; |SI nNume1 < nCalC; |HACIENDO nNume1 = nNume1 + 1;
         #amaniva@#nNume1# = #camaniva#nNume1#;
   |SIGUE;
   #amaniva@#1 = nNumRegFac;
   |GRABA 020#amaniva@.n;

   |SI #camaniva#36 ! "N";
       |ABRE #223;
       #223#0 = #camaniva#0;
       #223#1 = #camaniva#1;
       |LEE 001#223=;
       |SI FSalida = 0;
           #113#0 = #amaniva@#0;
           #113#1 = #amaniva@#1;
           |LEE 101#113=;
           |SI FSalida ! 0;
               #113#0 = #amaniva@#0;
               #113#1 = #amaniva@#1;
               |GRABA 020#113b;
           |FINSI;
           #113#2 = #amaniva@#2;
           #113#3 = #amaniva@#3;
           |PARA nPara1 = 4; |SI nPara1 [ 29; |HACIENDO nPara1 = nPara1 + 1;
                 #113nPara1 = #223nPara1;
           |SIGUE;
           |GRABA 020#113a;
           |LIBERA #113;
       |FINSI;
       |CIERRA #223;
  |FINSI;

   |BUCLE Lcaivalin;

   #casiim01#0 = "camaniva";
   #casiim01#1 = #camaniva#0;
   #casiim01#2 = #camaniva#1;
   |LEE 041#casiim01.=;
   |SI FSalida = 0;
       aAlfa1 = "|NC";
       nCalC = #casiim01#aAlfa1#;
       |PARA nNume1 = 0; |SI nNume1 < nCalC; |HACIENDO nNume1 = nNume1 + 1;
             #asiim01@#nNume1# = #casiim01#nNume1#;
       |SIGUE;
       #asiim01@#2 = #amaniva@#1;
       |GRABA 020#asiim01@.n;

       |BUCLE Lcasiim02;
   |FINSI;

   nSwAlguno = 1;
   |LIBERA #camaniva;
|FPRC;

|DEFBUCLE camaniva;
   #camaniva#2;
   ;
   #0#0, #0#2, #0#4;
   #0#1, #0#3, #0#5;
   ;
   NULL, PasaRegs;
|FIN;

|| ----------------------------------
|PROCESO PasaLineas;
   aAlfa1 = "|NC";
   nCalC = #camaasil#aAlfa1#;
   |PARA nNume1 = 0; |SI nNume1 < nCalC; |HACIENDO nNume1 = nNume1 + 1;
         #amaasil@#nNume1# = #camaasil#nNume1#;
   |SIGUE;
   |GRABA 020#amaasil@.n;

   |HAZ LaActualizacion;
|FPRC;

|DEFBUCLE Lcamaasil;
   #camaasil#1;
   ;
   #camaasic#0, #camaasic#1, #camaasic#2, 0001;
   #camaasic#0, #camaasic#1, #camaasic#2, 9999;
   ;
   NULL, PasaLineas;
|FIN;

|PROCESO PasaMovs;

   #amaasic@#0 = #camaasic#0;
   #amaasic@#1 = #camaasic#1;
   #amaasic@#2 = #camaasic#2;
   |LEE 040#amaasic@.=;
   |SI FSalida = 0;
       |ACABA;
   |FINSI;                           ||Ya existe y sale

   aAlfa1 = "|NC";
   nCalC  = #camaasic#aAlfa1#;
   |PARA nNume1 = 0; |SI nNume1 < nCalC; |HACIENDO nNume1 = nNume1 + 1;
         #amaasic@#nNume1# = #camaasic#nNume1#;
   |SIGUE;
   |GRABA 020#amaasic@.n;

   |BUCLE Lcamaasil;
   nSwAlguno = 1;
   |LIBERA #camaasic;
|FPRC;

|DEFBUCLE camaasic;
   #camaasic#1;
   ;
   #0#0, #0#2, #0#4;
   #0#1, #0#3, #0#5;
   be;
   NULL, PasaMovs;
|FIN;


|PROCESO HazTodo;
  |SI #0#11 = "S";     ||Borrar antes de Seguir
        enBorraAu  = 2;
        swVarios   = 1;
        dirfich0   = eaDirDestino;
        enDnDIARIO = #0#0;
        enHnDIARIO = #0#1;
        efDFechas  = #0#2;
        efHFechas  = #0#3;
        enDDocume  = #0#4;
        enHDocume  = #0#5;
        |SUB_EJECUTA_NP "caborasi";
        enBorraAu  = 1;
        swVarios   = 0;
  |FINSI;

 |PATH_DAT #5  eaDirDestino;
 |PATH_DAT #10 eaDirDestino;
 |PATH_DAT #11 eaDirDestino;
 |PATH_DAT #12 eaDirDestino;
 |PATH_DAT #13 eaDirDestino;
 |PATH_DAT #14 eaDirDestino;
 |PATH_DAT #16 eaDirDestino;
 |PATH_DAT #17 eaDirDestino;
 |PATH_DAT #113 eaDirDestino;

 |ABRE #amaasil@;
 |ABRE #amaasic@;
 |BUCLE camaasic;
 |CIERRA #amaasic@;
 |CIERRA #amaasil@;

 |ABRE #casiim01;
 |ABRE #amaniva@;
 |ABRE #aivalin@;
 |ABRE #asiim01@;
 |ABRE #asiim02@;
 |ABRE #aivam03@;

 |BUCLE camaniva;

 |CIERRA #aivam03@;
 |CIERRA #asiim01@;
 |CIERRA #asiim02@;
 |CIERRA #aivalin@;
 |CIERRA #amaniva@;
 |CIERRA #casiim01;
|FPRC;

|| **************************************************************************
|PROCESO Descargar;
   |SI nDef113 = 1; |DESCARGA_DEF #aivam03@; nDef113 = 0; |FINSI;
   |SI nDef17  = 1; |DESCARGA_DEF #asiim02@; nDef17  = 0; |FINSI;
   |SI nDef16  = 1; |DESCARGA_DEF #asiim01@; nDef16  = 0; |FINSI;
   |SI nDef14  = 1; |DESCARGA_DEF #amaasic@; nDef14  = 0; |FINSI;
   |SI nDef13  = 1; |DESCARGA_DEF #aivalin@; nDef13  = 0; |FINSI;
   |SI nDef12  = 1; |DESCARGA_DEF #amaniva@; nDef12  = 0; |FINSI;
   |SI nDef11  = 1; |DESCARGA_DEF #amaasil@; nDef11  = 0; |FINSI;
|FPRC;

|CALCULO Tipo3;  |TIPO 3;
   |HAZ eLeeParametro;
   eaRenumera = #0#9;
   nSwAlguno = 0;
   aH60      = " " * 60;

   |DBASE eaDirDestino; |QBT eaDirDestino;
   aAlfa1 = eaDirDestino;
   aAlfa2 = (("00000" + #0#6) % -105) + (("0" + #0#7) % -101) + "/";
   eaDirDestino = eaDirDestino + "fich/" + aAlfa2;

   nError = 0;

   nDef11 = 1;
   aAlfa2 = aAlfa1 + "def/camaasil.mas";
   |CARGA_DEF aAlfa2, amaasil@;
   |SI FSalida ! 0;
       nDef11 = 0;
       nError = 1;
   |FINSI;

   |SI nError = 0;
       nDef12 = 1;
       aAlfa2 = aAlfa1 + "def/camaniva.mas";
       |CARGA_DEF aAlfa2, amaniva@;
       |SI FSalida ! 0;
           nDef12 = 0;
           nError = 1;
       |FINSI;
   |FINSI;

   |SI nError = 0;
       nDef13 = 1;
       aAlfa2 = aAlfa1 + "def/caivalin.mas";
       |CARGA_DEF aAlfa2, aivalin@;
       |SI FSalida ! 0;
           nDef13 = 0;
           nError = 1;
       |FINSI;
   |FINSI;

   |SI nError = 0;
       nDef14 = 1;
       aAlfa2 = aAlfa1 + "def/camaasic.mas";
       |CARGA_DEF aAlfa2, amaasic@;
       |SI FSalida ! 0;
           nDef14 = 0;
           nError = 1;
       |FINSI;
   |FINSI;

   |SI nError = 0;
       nDef16 = 1;
       aAlfa2 = aAlfa1 + "def/casiim01.mas";
       |CARGA_DEF aAlfa2, asiim01@;
       |SI FSalida ! 0;
           nDef16 = 0;
           nError = 1;
       |FINSI;
   |FINSI;

   |SI nError = 0;
       nDef17 = 1;
       aAlfa2 = aAlfa1 + "def/casiim02.mas";
       |CARGA_DEF aAlfa2, asiim02@;
       |SI FSalida ! 0;
           nDef17 = 0;
           nError = 1;
       |FINSI;
   |FINSI;

   |SI nError = 0;
       nDef113 = 1;
       aAlfa2 = aAlfa1 + "def/caivam03.mas";
       |CARGA_DEF aAlfa2, aivam03@;
       |SI FSalida ! 0;
           nDef113 = 0;
           nError = 1;
       |FINSI;
   |FINSI;

   |SI nError = 1;
       |MENAV "0000Error Cargando Ficheros de Contabilidad";
   |SINO;
       |HAZ HazTodo;
   |FINSI;

   |HAZ Descargar;

   |SI #0#10 = "M";
       enDnDIARIO = #0#0;
       enHnDIARIO = #0#1;
       efDFechas = #0#2;
       efHFechas = #0#3;
       enDDocume = #0#4;
       enHDocume = #0#5;
       |SI nSwAlguno = 1;
           |SUB_EJECUTA_NP "caborasi";
       |FINSI;
   |FINSI;
|FCAL;

|PROCESO LaActualizacion;
   MasMenos     = #camaasil#9;
   DebeHaber    = #camaasil#8;
   aCUENTA      = #camaasil#4;
   nREGISTRO    = #camaasil#24;
   nDIARIO      = #camaasil#0;
   aFECHA       = #camaasil#1;
   nDOCUMENTO   = #camaasil#2;
   nNIVELCUENTA = #camaasil#5;
   |HAZ ActDiario;          || diario
   |HAZ ActCuenta;          || cuenta
   |HAZ ActTesteo;          || Para recalculo en informes
|FPRC;
