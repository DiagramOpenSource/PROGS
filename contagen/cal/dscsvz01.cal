|FICHEROS;
     dscsvm01;
     dscsvm02;
     dscsvm03;
     dscsvm04;
     dscsvm05;

     dscsvw01;
     dscsvw02;
     dscsvw03,MANTE;
|FIN;

|VARIABLES;
     nBtnInfo = -1;
     &eaNombrePDF = "";

     n0Vent          = -1;
     n1Vent          = -1;
     nGrid1          = -1;
     nGrid3          = -1;
     nTree           = -1;
     nBtnBor         = -1;
     nBtnAut         = -1;
     nBtnNCo         = -1;
     nBtnBCo         = -1;
     nBtnCar1        = -1;
     nBtnCar2        = -1;

     nRango          = 0;
     nHandle         = 0;
     nIde            = 0;
     nCampo          = 0;
     nPanta          = 0;
     nLong           = 0;
     nCarac          = 0;
     nPos            = 0;
     nFSalida        = 0;
     nColum          = 0;
     nNume1          = 0;
     nNume2          = 0;
     nRam1           = 0;
     nRam2           = 0;
     nPide           = 0;
     nFila           = 0;
     nCargar         = 0;
     nPosi           = 0;

     aAlfa           = "";
     aEsc1           = "";
     aEsc2           = "";
     aRetu           = "";
     aTecla          = "";
     aBaseConta      = "";
     aPathLocal      = "";
     aPathTmp        = "";
     aPathCSV        = "";
     aFicTree        = "";
     aAbre           = "";
     aTabla          = "";
     aTablaTree      = "";
     aFilaTree       = "";
     aDescri         = "";
     aRuta           = "";
     aLong           = "";
     aCadena         = "";
     aOrigen         = "";
     aDestino        = "";
     aDTabla         = "";
     aHTabla         = "";
     aBuscar         = "";
     aRefresca       = "";
     aCarac          = "";
     aContenido      = "";
     a100            = "";

     {-1}sTree       = 0;
         sT_nID      = 0;
         sT_nOper    = 0;
         sT_aData    = "";

     {-1}aVent       = "";
         aVent1      = 0;
         aVent2      = 0;
         aVent3      = 0;
         aVent4      = 0;
         aVent5      = "";

     &eaUnidadBasico = "";
     &eaIP           = "";
|FIN;

|PROCESO dscsvm05Bor;
     |BORRA 020#dscsvm05.a;
     |LIBERA #dscsvm05;

     |PULSATECLA;
|FPRC;

|DEFBUCLE dscsvm05Bor;
     #dscsvm05#1;
     ;
     #dscsvm04#0, #dscsvm04#1, #dscsvm04#2, #dscsvm04#3, #dscsvm04#4,   0;
     #dscsvm04#0, #dscsvm04#1, #dscsvm04#2, #dscsvm04#3, #dscsvm04#4, 999;
     be;
     NULL, dscsvm05Bor;
|FIN;

|PROCESO dscsvm04Bor;
     |BORRA 020#dscsvm04.a;
     |LIBERA #dscsvm04;

     |BUCLE dscsvm05Bor;

     |PULSATECLA;
|FPRC;

|DEFBUCLE dscsvm04Bor;
     #dscsvm04#1;
     ;
     #dscsvm03#0, #dscsvm03#1, #dscsvm03#2, #dscsvm03#3, "";
     #dscsvm03#0, #dscsvm03#1, #dscsvm03#2, #dscsvm03#3, a100;
     be;
     NULL, dscsvm04Bor;
|FIN;

|PROCESO dscsvm03Bor;
     |BORRA 020#dscsvm03.a;
     |LIBERA #dscsvm03;

     |BUCLE dscsvm04Bor;
     |PULSATECLA;
|FPRC;

|DEFBUCLE dscsvm03Bor;
     #dscsvm03#1;
     ;
     #dscsvm02#0, #dscsvm02#1, #dscsvm02#2, "  ";
     #dscsvm02#0, #dscsvm02#1, #dscsvm02#2, "ZZ";
     be;
     NULL, dscsvm03Bor;
|FIN;

|PROCESO dscsvm02Bor;
     |BORRA 020#dscsvm02.a;
     |LIBERA #dscsvm02;

     |BUCLE dscsvm03Bor;

     |PULSATECLA;
|FPRC;

|DEFBUCLE dscsvm02Bor;
     #dscsvm02#1;
     ;
     #dscsvm01#0, aDTabla,  0;
     #dscsvm01#0, aHTabla, 99;
     be;
     NULL, dscsvm02Bor;
|FIN;

|PROCESO UbicaImporta;
     aRuta    = "F*.csv";
     |BROWSE aRuta;

     aAlfa  = aRuta;   |QBF aAlfa;
     aRuta = "";
     aLong  = aAlfa % 0;
     nLong  = aLong;
     |PARA nCarac = 2;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos  = (nCarac * 100) + 1;
           aRuta = aRuta + (aAlfa % nPos);
     |SIGUE;

     |SI aRuta = "";
         |ACABA;
     |FINSI;

     aAlfa = (aRuta % -103) < "";
     |SI aAlfa ! "csv";
         |MENAV "0000Fichero no v lido. Debe tener extensi¢n CSV";
         |ACABA;
     |FINSI;

     |SI nFSalida = nBtnCar1;
         aOrigen  = aRuta;
         aDestino = aPathTmp + "cacuenta" + (("00000" + #dscsvm01#0) % -105) + ".csv";

         |SI eaIP = "";
             |COPIA_FICHERO aOrigen, aDestino;
         |SINO;
             |RECIBE_FICHERO aOrigen, aDestino;
         |FINSI;

         #dscsvw01#0 = aDestino;  |PINTA #dscsvw01#0;
     |FINSI;

     |SI nFSalida = nBtnCar2;
         aOrigen  = aRuta;
         aDestino = aPathTmp + "camaasil" + (("00000" + #dscsvm01#0) % -105) + ".csv";

         |SI eaIP = "";
             |COPIA_FICHERO aOrigen, aDestino;
         |SINO;
             |RECIBE_FICHERO aOrigen, aDestino;
         |FINSI;

         #dscsvw01#1 = aDestino;  |PINTA #dscsvw01#1;
     |FINSI;
|FPRC;

|PROCESO Ubica;
     nFSalida = FSalida;

     |SI aContenido = "IMPORTACIO";
         |HAZ UbicaImporta;
         |ACABA;
     |FINSI;

     |SI nFSalida = nBtnCar1;
         aAlfa = aPathCSV + "cacuenta" + (("00000" + #dscsvm01#0) % -105) + ".csv";
         |XABRE "E", aAlfa, 0;
         |SI FSalida ] 0;
             |CONFI "0000NHay una configuraci¢n definida para este CSV. La desactivamos?";
             |SI FSalida ! 0;
                 |MENAV "0000Operaci¢n cancelada.";
                 |ACABA;
             |FINSI;

             |FBORRA aAlfa;
             #dscsvw01#0 = "";  |PINTA #dscsvw01#0;

             aDTabla = "cacuenta";
             aHTabla = "cacuenta";
             |BUCLE dscsvm02Bor;
         |FINSI;
     |FINSI;

     |SI nFSalida = nBtnCar2;
         aAlfa = aPathCSV + "camaasil" + (("00000" + #dscsvm01#0) % -105) + ".csv";
         |XABRE "E", aAlfa, 0;
         |SI FSalida ] 0;
             |CONFI "0000NHay una configuraci¢n definida para este CSV. La desactivamos?";
             |SI FSalida ! 0;
                 |MENAV "0000Operaci¢n cancelada.";
                 |ACABA;
             |FINSI;

             |FBORRA aAlfa;
             #dscsvw01#1 = "";  |PINTA #dscsvw01#1;

             aDTabla = "camaasil";
             aHTabla = "camaasil";
             |BUCLE dscsvm02Bor;
         |FINSI;
     |FINSI;

     aRuta    = "F*.csv";
     |BROWSE aRuta;

     aAlfa  = aRuta;   |QBF aAlfa;
     aRuta = "";
     aLong  = aAlfa % 0;
     nLong  = aLong;
     |PARA nCarac = 2;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos  = (nCarac * 100) + 1;
           aRuta = aRuta + (aAlfa % nPos);
     |SIGUE;

     |SI aRuta = "";
         |ACABA;
     |FINSI;

     aAlfa = (aRuta % -103) < "";
     |SI aAlfa ! "csv";
         |MENAV "0000Fichero no v lido. Debe tener extensi¢n CSV";
         |ACABA;
     |FINSI;

     |SI nFSalida = nBtnCar1;
         aOrigen  = aRuta;
         aDestino = aPathCSV + "cacuenta" + (("00000" + #dscsvm01#0) % -105) + ".csv";

         |SI eaIP = "";
             |COPIA_FICHERO aOrigen, aDestino;
         |SINO;
             |RECIBE_FICHERO aOrigen, aDestino;
         |FINSI;

         #dscsvw01#0 = aDestino;  |PINTA #dscsvw01#0;
     |FINSI;

     |SI nFSalida = nBtnCar2;
         aOrigen  = aRuta;
         aDestino = aPathCSV + "camaasil" + (("00000" + #dscsvm01#0) % -105) + ".csv";

         |SI eaIP = "";
             |COPIA_FICHERO aOrigen, aDestino;
         |SINO;
             |RECIBE_FICHERO aOrigen, aDestino;
         |FINSI;

         #dscsvw01#1 = aDestino;  |PINTA #dscsvw01#1;
     |FINSI;
|FPRC;

|PROCESO VentImporta;
     |VENTANA 0501, 0540, -1, 16, "Procesando datos, espere por favor";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     |HAZ Importacion;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;
|FPRC;

|PROCESO Instrucciones;
     |HAZ LeeUnidadBasico;
     eaNombrePDF = "dscsvz01";
     |HAZ MuestraPDF;
|FPRC;

|PROCESO Importar;
     |VENTANA 0501, 3799, -1, 16, "Introduzca la ubicaci¢n de los CSV";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     |DDEFECTO #dscsvw01;

     #dscsvw01#2 = #dscsvm01#1;

     |PINPA #0#dscsvw01;  nPanta = FSalida;
     |PINDA #0#dscsvw01;


     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}", 1085, 1087, Ubica; nBtnCar1 = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}", 1385, 1387, Ubica; nBtnCar2 = FSalida;

     |DDEFECTO #dscsvw01;
     FSalida = "::{{230}}Continuar";
     |LEETECLA aTecla;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;

     aAlfa = #dscsvw01#0 + #dscsvw01#1;  |QBF aAlfa;
     |SI aAlfa ! "";
         |CONFI "0000SVamos a proceder a la importaci¢n de los datos. Est  seguro";
         |SI FSalida = 0;
             |HAZ VentImporta;
         |SINO;
             |MENAV "0000Proceso cancelado.";
         |FINSI;
     |SINO;
         |MENAV "0000Proceso cancelado.";
     |FINSI;
|FPRC;

|PROCESO BtnNCo;
     |VENTANA 0501, 1165, -1, 16, "Nueva columna";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     |DDEFECTO #dscsvw03;

     |PINPA #0#dscsvw03;  nPanta = FSalida;
     |PINDA #0#dscsvw03;

     |ENDATOS #1#dscsvw03;
     |SI FSalida = 0;
         |DDEFECTO #dscsvm03;
         #dscsvm03#0 = #dscsvm02#0;
         #dscsvm03#1 = #dscsvm02#1;
         #dscsvm03#2 = #dscsvm02#2;
         #dscsvm03#3 = #dscsvw03#0;
         #dscsvm03#4 = #dscsvw03#1;
         #dscsvm03#5 = #dscsvw03#2;

         |GRABA 020#dscsvm03.n;
         |LIBERA #dscsvm03;

         |REFRESCACONTROL nGrid3;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;

     |FOCOTECLADO nGrid3;
|FPRC;

|PROCESO BtnBCo;
     |CONFI "0000NSeguro que quiere borrar la columna";
     |SI FSalida ! 0;
         |FOCOTECLADO nGrid3;
         |ACABA;
     |FINSI;

     |LEE 101#dscsvm03.=;
     |SI FSalida = 0;
         |HAZ dscsvm03Bor;

         |REFRESCACONTROL nGrid3;
         |FOCOTECLADO nGrid3;
     |FINSI;
|FPRC;

|PROCESO BtnAut;
     |VENTANA 0501, 1565, -1, 16, "Carga autom tica de columnas";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     |PINPA #0#dscsvw02;  nPanta = FSalida;
     |PINDA #0#dscsvw02;

     |ENDATOS #1#dscsvw02;
     |SI FSalida = 0;
         |HAZ CargaAut;
         |REFRESCACONTROL nGrid3;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;

     |FOCOTECLADO nGrid3;
|FPRC;

|PROCESO Baja3;
     #dscsvm03#0 = #dscsvm01#0;
     #dscsvm03#1 = aTablaTree;
     #dscsvm03#2 = aFilaTree;
     #dscsvm03#3 = "";
|FPRC;

|PROCESO Alta3;
     #dscsvm03#0 = #dscsvm01#0;
     #dscsvm03#1 = aTablaTree;
     #dscsvm03#2 = aFilaTree;
     #dscsvm03#3 = "ZZ";
|FPRC;

|PROCESO EvntTree;
     |SI FEntrada = 0;
         aAlfa      = Contenido;  |QBF aAlfa;
         aBuscar    = "";
         aLong      = aAlfa % 0;
         nLong      = aLong;
         nCargar    = 0;
         |PARA nPosi = 1;  |SI nPosi [ nLong;  |HACIENDO nPosi = nPosi + 1;
               nPos = (nPosi * 100) + 1;
               aCarac = aAlfa % nPos;

               |SI nCargar = 1;
                   aBuscar = aBuscar + aCarac;
               |FINSI;

               |SI aCarac = ":";  nCargar = 1; |FINSI;
         |SIGUE;

         aContenido = aBuscar;  |QBF aContenido;


         aTablaTree = aContenido % 108;
         aFilaTree  = aContenido % 902;

         |ESTADO_CONTROL nBtnAut, "DISABLE";
         |ESTADO_CONTROL nBtnNCo, "DISABLE";
         |ESTADO_CONTROL nBtnBCo, "DISABLE";

         #dscsvm02#0 = #dscsvm01#0;
         #dscsvm02#1 = aTablaTree;
         #dscsvm02#2 = aFilaTree;
         |LEE 000#dscsvm02.=;
         |SI FSalida = 0;
             |ESTADO_CONTROL nBtnNCo, "ENABLE";

             |SI #dscsvm02#3 = "S";
                 |ESTADO_CONTROL nBtnAut, "ENABLE";
             |FINSI;
         |FINSI;

         |REFRESCACONTROL nGrid3;

         |ACABA;
     |FINSI;

     |SI aContenido = "";  |ACABA;  |FINSI;

     |SI aContenido = "IMPORTACIO";
         |HAZ Importar;
         |ACABA;
     |FINSI;

     aTablaTree = Contenido % -308;
     aFilaTree  = Contenido % -102;

     |SI aTablaTree = "        ";  |O aTablaTree = "";
         |ACABA;
     |FINSI;

     |SI aFilaTree ! "NU";
         |SI #dscsvm02#3 = "N";
             |CONFI "0000NQuiere borrar la fila seleccionada";
             |SI FSalida ! 0;  |ACABA;  |FINSI;

             #dscsvm02#0 = #dscsvm01#0;
             #dscsvm02#1 = aTablaTree;
             #dscsvm02#2 = aFilaTree;
             |LEE 101#dscsvm02.=;
             |SI FSalida = 0;
                 |HAZ dscsvm02Bor;

                 aBuscar = aTablaTree + "NU";
             |FINSI;
         |FINSI;
     |FINSI;

     |SI aFilaTree = "NU";
         |CONFI "0000NQuiere crear una nueva fila de datos";
         |SI FSalida ! 0;  |ACABA;  |FINSI;

         nFila = 1;
         |CONFI "0000NLos datos de la nueva fila se encuentran antes que los datos de la fila principal";
         |SI FSalida ! 0;
             nFila = 51;
         |FINSI;

         |PARA nCampo = nFila;  |SI nCampo [ 99;  |HACIENDO nCampo = nCampo + 1;
               #dscsvm02#0 = #dscsvm01#0;
               #dscsvm02#1 = aTablaTree;
               #dscsvm02#2 = nCampo;
               |LEE 000#dscsvm02.=;
               |SI FSalida ! 0;
                   |DDEFECTO #dscsvm02;
                   #dscsvm02#0 = #dscsvm01#0;
                   #dscsvm02#1 = aTablaTree;
                   #dscsvm02#2 = nCampo;
                   #dscsvm02#3 = "N";
                   |GRABA 020#dscsvm02.n;
                   |LIBERA #dscsvm02;
                   |SAL;
               |FINSI;
         |SIGUE;
     |FINSI;

     |HAZ CreaArbol;
|FPRC;

|PROCESO CargaArbol;
     aAlfa = aAlfa + &13 + &10;
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO dscsvm02Ar;
     |SI #dscsvm02#1 ! aTabla;
         |SI aTabla ! "";
             nRam2 = nRam2 + 1;
             aAlfa = ":Nueva fila {{";
             aAlfa = aAlfa + aTabla + "NU}}";
             aAlfa = nRam1 + "," + nRam2 + aAlfa;
             |HAZ CargaArbol;
         |FINSI;

         nRam1 = nRam1 + 1;
         nRam2 = 0;

         |SI #dscsvm02#1 = "cacuenta";
             aAlfa = ":Cuentas contables";
         |FINSI;

         |SI #dscsvm02#1 = "camaasil";
             aAlfa   = ":Movimientos contables";
         |FINSI;

         aAlfa = nRam1 + aAlfa;
         |HAZ CargaArbol;
     |FINSI;

     aAlfa = "";
     |SI #dscsvm02#3 = "S";
         aAlfa = "principal";
     |FINSI;

     nRam2 = nRam2 + 1;
     aAlfa = nRam1 + "," + nRam2 + ":Fila " + aAlfa;
     aAlfa = aAlfa + "{{"
     aAlfa = aAlfa + #dscsvm02#1 + (("00" + #dscsvm02#2) % -102) + "}}";
     |HAZ CargaArbol;

     aTabla = #dscsvm02#1;
|FPRC;

|DEFBUCLE dscsvm02Ar;
     #dscsvm02#1;
     ;
     #dscsvm01#0, "        ",  1;
     #dscsvm01#0, "zzzzzzzz", 99;
     ;
     NULL, dscsvm02Ar;
|FIN;

|PROCESO CreaArbol;
     |SI nTree ! -1;
         |DESTRUYECONTROL nTree;
     |FINSI;

     |HAZ LeeUnidadBasico;

     aPathLocal = eaUnidadBasico + "\documentos";   |RMKDIR aPathLocal;
     aPathLocal = aPathLocal + "\tmp";              |RMKDIR aPathLocal;
     aPathLocal = aPathLocal + "\";

     aFicTree = "dscsvz0101" + Usuario + ".txt";

     aAbre = aPathLocal + aFicTree;
     |XABRE "WBC", aAbre, nHandle;

     nRam1  = 0;
     nRam2  = 0;
     aTabla = "";
     |BUCLE dscsvm02Ar;

     |SI aTabla ! "";
         nRam2 = nRam2 + 1;
         aAlfa = ":Nueva fila {{";
         aAlfa = aAlfa + aTabla + "NU}}";
         aAlfa = nRam1 + "," + nRam2 + aAlfa;
         |HAZ CargaArbol;
     |FINSI;

     nRam1 = nRam1 + 1;
     aAlfa = nRam1 + ":--------------------";
     |HAZ CargaArbol;

     nRam1 = nRam1 + 1;
     aAlfa = nRam1 + ":Realizar importaci¢n {{IMPORTACIO}}";
     |HAZ CargaArbol;

     |XCIERRA nHandle;

     aAbre = aPathLocal + aFicTree;
     aAlfa = "TREECTRL,Tablas {{" + aAbre + "}}";
     |CONTROL_SIMPLE 0, aAlfa, 2002, 3430, EvntTree;
     nTree = FSalida;

     |SI aBuscar ! "";
         sT_nID   = nTree;
         sT_nOper = 3;
         sT_aData = "[" + aBuscar;
         |ESPECIFICA "COMMANDCONTROL", sTree;
         |SI FSalida ] 0;
             aRefresca = ":" + FSalida;

             sT_nOper = 5;
             sT_aData = aRefresca;
             |ESPECIFICA "COMMANDCONTROL", sTree;
             |SI FSalida ] 0;
                 aRefresca = ":" + FSalida;
             |FINSI;
         |SINO;
             aRefresca = "1:";
         |FINSI;
     |FINSI;

     sT_nID   = nTree;
     sT_nOper = 2;
     |SI aRefresca = "";
         sT_aData = "1:";
     |SINO;
         sT_aData = aRefresca;
     |FINSI;
     |ESPECIFICA "COMMANDCONTROL", sTree;
     |ESPECIFICA "COMMANDCONTROL", sTree;

     |RFBORRA aAbre;

     |REFRESCACONTROL nGrid3;
|FPRC;

|PROCESO GrabaCSV02;
     aAlfa = #dscsvw01#0;  |QBF aAlfa;
     |SI aAlfa ! "";
         #dscsvm02#0 = #dscsvm01#0;
         #dscsvm02#1 = "cacuenta";
         #dscsvm02#2 = 50;
         |LEE 101#dscsvm02.=;
         |SI FSalida ! 0;
             #dscsvm02#0 = #dscsvm01#0;
             #dscsvm02#1 = "cacuenta";
             #dscsvm02#2 = 50;
             #dscsvm02#3 = "S";
             |GRABA 020#dscsvm02.n;
         |FINSI;
         |LIBERA #dscsvm02;
     |FINSI;

     aAlfa = #dscsvw01#1;  |QBF aAlfa;
     |SI aAlfa ! "";
         #dscsvm02#0 = #dscsvm01#0;
         #dscsvm02#1 = "camaasil";
         #dscsvm02#2 = 50;
         |LEE 101#dscsvm02.=;
         |SI FSalida ! 0;
             #dscsvm02#0 = #dscsvm01#0;
             #dscsvm02#1 = "camaasil";
             #dscsvm02#2 = 50;
             #dscsvm02#3 = "S";
             |GRABA 020#dscsvm02.n;
         |FINSI;
         |LIBERA #dscsvm02;
     |FINSI;
|FPRC;

|PROCESO PideDescri;
     |VENTANA 0501, 3799, -1, 16, "Datos importaci¢n en CSV";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     |PINPA #0#dscsvw01;  nPanta = FSalida;
     |PINDA #0#dscsvw01;

     aContenido = "";

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}", 1085, 1087, Ubica; nBtnCar1 = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}", 1385, 1387, Ubica; nBtnCar2 = FSalida;

     |SI nPide = 1;
         |ENDATOS #1#dscsvw01;
         |SI FSalida = 0;
             #dscsvm01#1 = #dscsvw01#2;
             #dscsvm01#2 = #dscsvw01#0;
             #dscsvm01#3 = #dscsvw01#1;
             |GRABA 020#dscsvm01.a;
             |LIBERA #dscsvm01;

             |HAZ GrabaCSV02;
         |SINO;
             |DDEFECTO #dscsvw01;
         |FINSI;
     |SINO;
         FSalida = "::{{230}}Continuar";
         |LEETECLA aTecla;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;
|FPRC;

|PROCESO Evnt1;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#dscsvm01.=;

         |ESTADO_CONTROL nBtnBor, "ENABLE";

         |HAZ CreaArbol;
     |FINSI;

     |SI FSalida = 4;
         |LEE 101#dscsvm01.=;
         |SI FSalida = 0;
             #dscsvw01#0 = #dscsvm01#2;
             #dscsvw01#1 = #dscsvm01#3;
             #dscsvw01#2 = #dscsvm01#1;
             nPide = 1;  |HAZ PideDescri;

             |REFRESCACONTROL nGrid1;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Evnt3;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#dscsvm03.=;

         |SI #dscsvm02#3 = "S";
             |ESTADO_CONTROL nBtnAut, "DISABLE";
         |FINSI;

         |ESTADO_CONTROL nBtnBCo, "ENABLE";
     |FINSI;

     |SI FSalida = 4;
         |LEE 101#dscsvm03.=;
         |SI FSalida = 0;
             |HAZ EntraM04;

             |REFRESCACONTROL nGrid3;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO BtnNue;
     nIde = 1;
     |LEE 000#dscsvm01.u;
     |SI FSalida = 0;
         nIde = #dscsvm01#0 + 1;
     |FINSI;

     |DDEFECTO #dscsvw01;
     |DDEFECTO #dscsvm01;
     #dscsvm01#0 = nIde;
     |GRABA 020#dscsvm01.b;

     nPide   = 1;  |HAZ PideDescri;
     aDescri = #dscsvw01#0 + #dscsvw01#1;  |QBF aDescri;
     |SI aDescri = "";
         |MENAV "0000Proceso cancelado, introduzca el modelo de CSV a importar";

         |BORRA 020#dscsvm01.a;

         |FOCOTECLADO nGrid1;
         |ACABA;
     |FINSI;

     |HAZ GrabaCSV02;

     |REFRESCACONTROL nGrid1;
     |FOCOTECLADO nGrid1;
|FPRC;

|PROCESO BtnBor;
     |LEE 000#dscsvm01.=;
     |SI FSalida ! 0;
         |FOCOTECLADO nGrid1;
         |ACABA;
     |FINSI;

     |CONFI "0000NSeguro que quiere borrar la importaci¢n seleccionada";
     |SI FSalida ! 0;
         |FOCOTECLADO nGrid1;
         |ACABA;
     |FINSI;

     |LEE 101#dscsvm01.=;

     aDTabla = "";
     aHTabla = "z" * 8;
     |BUCLE dscsvm02Bor;

     |BORRA 020#dscsvm01.a;
     |LIBERA #dscsvm01;

     aAlfa = aPathCSV + "cacuenta" + (("00000" + #dscsvm01#0) % -105) + ".csv";
     |FBORRA aAlfa;

     aAlfa = aPathCSV + "camaasil" + (("00000" + #dscsvm01#0) % -105) + ".csv";
     |FBORRA aAlfa;

     |HAZ CreaArbol;

     |ESTADO_CONTROL nBtnBor, "DISABLE";
     |REFRESCACONTROL nGrid3;

     |REFRESCACONTROL nGrid1;
     |FOCOTECLADO nGrid1;
|FPRC;

|PROGRAMA;
     eaIP = "";  |IP_REMOTO eaIP;

     |DBASE aBaseConta;  |QBF aBaseConta;

     |HAZ LeeUnidadBasico;

     aPathLocal = eaUnidadBasico + "\documentos";   |RMKDIR aPathLocal;
     aPathLocal = aPathLocal + "\tmp";              |RMKDIR aPathLocal;
     aPathLocal = aPathLocal + "\";

     aPathCSV   = aBaseConta + "importacsv";  |MKDIR aPathCSV;
     aPathCSV   = aPathCSV   + "/";

     aPathTmp   = aBaseConta + "tmp";           |MKDIR aPathTmp;
     aPathTmp   = aPathTmp   + "/" + Usuario;   |MKDIR aPathTmp;
     aPathTmp   = aPathTmp   + "/";

     |VENTANA 0501, 3799, -1, 16, "Importaci¢n de datos ficheros CSV";
     n0Vent = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{137}}Instrucciones", 0585, 0599, Instrucciones;
     nBtnInfo = FSalida;

     |CONTROL_SIMPLE 0, "LABEL,{{15}}Importaciones", 0502, 0520, NULL;
     |CONTROL_SIMPLE 0, "LABEL,{{11}}(Doble click entra en la configuraci¢n)", 0522, 0584, NULL;
     |CONTROL_SIMPLE 0, "LABEL,{{11}}(Doble click entra en la configuraci¢n)", 1957, 1998, NULL;
     |CONTROL_SIMPLE 0, "LABEL,{{15}}Tablas/Filas", 1902, 1920, NULL;
     |CONTROL_SIMPLE 0, "LABEL,{{15}}Tabla/Fila/Columnas", 1934, 1955, NULL;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Nueva importaci¢n", 1780, 1798, BtnNue;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Borrar importaci¢n", 1760, 1778, BtnBor;
     nBtnBor = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Carga autom tica", 3580, 3598, BtnAut;
     nBtnAut = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Nueva columna", 3534, 3550, BtnNCo;
     nBtnNCo = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Borrar columna", 3552, 3568, BtnBCo;
     nBtnBCo = FSalida;

     |ESTADO_CONTROL nBtnBor, "DISABLE";
     |ESTADO_CONTROL nBtnAut, "DISABLE";
     |ESTADO_CONTROL nBtnNCo, "DISABLE";
     |ESTADO_CONTROL nBtnBCo, "DISABLE";

     nRango   = 2 + 4 + 8 + 16 + 32 + 128;
     |LINEAL_SIMPLE #1#dscsvm01, nRango, 0602, 1698, NULL, NULL, Evnt1;
     nGrid1 = FSalida;

     |LINEAL_SIMPLE #1#dscsvm03, nRango, 2034, 3498, Baja3, Alta3, Evnt3;
     nGrid3 = FSalida;

     |SI nTree = -1;
         |HAZ CreaArbol;
     |FINSI;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::{{001}}Salir," + nGrid1;
          |LEETECLA aTecla;
          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
          |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FINVENTANA n0Vent;

     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
|FPRO;

|PROCESO Tipo80;  |TIPO 80;
     a100 = "z" * 100;

     |ABRET #dscsvm01;
     |ABRET #dscsvm02;
     |ABRET #dscsvm03;
     |ABRET #dscsvm04;
     |ABRET #dscsvm05;
|FPRC;

|PROCESO Tipo90;  |TIPO 90;
     |CIERRAT #dscsvm01;
     |CIERRAT #dscsvm02;
     |CIERRAT #dscsvm03;
     |CIERRAT #dscsvm04;
     |CIERRAT #dscsvm05;

     |PULSATECLA;
     |PULSATECLA;
|FPRC;
