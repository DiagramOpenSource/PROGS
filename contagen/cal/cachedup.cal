|FICHEROS;
   cachedup #0;
   catraiva #4;    || iva temporal
   camaniva #5;    || iva
|FIN;

|VARIABLES;
   informe = "";
   alfa1 = "";
   alfa2 = "";
   alfa3 = "";
   nume1 = 0;
   nume2 = 0;
   nume3 = 0;
   elsw1 = 0;
   mensa1 = "    ERROR!. FECHA INCORRECTA ";
   mensa2 = "    ERROR!. LIBRO INCORRECTO ";
   swalgun = 0;
   fDFecha = @;

   ccc        = "";
   c_alfa1    = "";
   c_alfa2    = "";
   c_nume1    = 0;
   c_nume2    = 0;
   c_nume3    = 0;

   ceros    = "";
   espacios = "";
   blancos  = "";
   cetas    = "" ;

   concepto = "";
   camcla1  = "";
   camcla2  = "";
   &swlin   = 0;
   &tipo    = "";
   &descrip = "";

   sw       = 0;

   &serie   = "";
   &numero  = 0;
   &antnume = 0;
   &antser  = "";
   &texterr = "";
   &primero = 0;
   &ultimo  = 0;
   &impnum  = "";
|FIN;

|INCLUYE dscont_i;

*****************************************************************************
CALCULOS DESDE CAMPOS
*****************************************************************************
|PROCESO Tipo8; |TIPO 8;
     |HAZ inicio;
     |HAZ eParaModelos;
|FPRC;

|PROCESO mayor;
   |SI Campo = 0;
      |C_M #cachedup#11,1,"S";
      |C_M #cachedup#13,1,"S"; |C_M #cachedup#14,1,"S";
      |SI #0Campo = "S";
         |C_M #cachedup#11,1,"N";
         |C_M #cachedup#13,1,"N"; |C_M #cachedup#14,1,"N";
         #cachedup#11 = "D"; |PINTA #cachedup#11;
         #cachedup#13 = "          "; |PINTA #cachedup#13;
         #cachedup#14 = "zzzzzzzzzz"; |PINTA #cachedup#14;
      |FINSI;
   |SINO;
      |C_M #0Campo,0,alfa1;
      |SI alfa1 = "N"; |ACABA; |FINSI;
      |C_M #cachedup#11,1,"S";
      |C_M #cachedup#13,1,"S"; |C_M #cachedup#14,1,"S";
      |SI Campo = 11;
         |SI #0Campo ! "S";
            |C_M #cachedup#13,1,"N"; |C_M #cachedup#14,1,"N";
            #cachedup#13 = "          "; |PINTA #cachedup#13;
            #cachedup#14 = "zzzzzzzzzz"; |PINTA #cachedup#14;
         |FINSI;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO defectos; |TIPO 7;
   #cachedup#3 = fec1; |PINTA #cachedup#3;
   #cachedup#4 = fec2; |PINTA #cachedup#4;
|FPRC;

|PROCESO fechad;
   fDFecha = fec1;
   |SI enSwSelFc = 1; fDFecha = "01.01.0000"; |FINSI;
   |SI #0Campo < fDFecha; |O #0Campo > fec2;
      |MENAV mensa1;
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO fechah;
   fDFecha = fec1;
   |SI enSwSelFc = 1; fDFecha = "01.01.0000"; |FINSI;
   |SI #0Campo < fDFecha; |O #0Campo > fec2; |O #0Campo < #cachedup#3;
      |MENAV mensa1;
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO libroh; |TIPO 0;
   |SI #cachedup#2 < #cachedup#1;
      |MENAV mensa2;
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO cam_pos_2; |TIPO 0;
   nume1 = #0Campo;
   |C_M #cachedup#8, 1, "S";
   |C_M #cachedup#9, 1, "S";
   |SI nume1 = 0;
      |C_M #cachedup#8,1,"N"; #cachedup#8 = 0; |PINTA #cachedup#8;
      |C_M #cachedup#9,1,"N";
      ||    |C_M #0#10,1,"N"; #0#10 = 1; |PINTA #0#10;
   |FINSI;
|FPRC;

|PROCESO quelong; |TIPO 0;
   |C_M #0Campo,0,alfa1;
   |SI alfa1 = "N"; |ACABA; |FINSI;
   nume1 = Campo - 1;
   nume2 = #cachedup#nume1# + #0Campo - 1;
   |SI nume2 > 30;
      |AVISO;
      |ERROR;
   |FINSI;
|FPRC;
*****************************************************************************
CALCULOS DE IMPRESION
*****************************************************************************
|PROCESO poncampos_s;
   #4#0  = #camaniva#12;     || Cuenta Proveedor
   #4#1  = camcla1;          || Campo orden 1
   #4#2  = camcla2;          || Campo orden 2
   #4#3  = " ";              || Error
   #4#4  = #camaniva#0;             || Libro
   #4#5  = #camaniva#3;             || Factura
   #4#6  = #camaniva#2;             || Serie
   #4#7  = #camaniva#4;             || Fecha
   #4#8  = #camaniva#12;             || Cuenta Contable
   #4#9  = #camaniva#13;            || Titulo Cuenta
   #4#10 = #camaniva#28;             || Concepto
   #4#11 = #camaniva#21;            || Total Factura
|FPRC;

|PROCESO tmp_sopor1;

   |SI #cachedup#11 ! "S";
      |PINTA 2241, "            ";
      |PINTA 2241, #camaniva#0;
      |PINTA 2244, #camaniva#3;
      |PINTA 2251, #camaniva#2;
   |FINSI;

   concepto = #camaniva#28;
   |HAZ montacamcla;
   |DDEFECTO #4;

   #4#0 = #camaniva#12;
   #4#1 = camcla1;
   #4#2 = camcla2;
   |LEE 101#4=;
   |SI FSalida = 0;
      |SI #cachedup#11 ! "S";
         |SI #4#3 ! "X";
            swlin = 1;
            |PRINT;
         |FINSI;
         |HAZ poncampos_s;
         #4#3 = "X";
         |GRABA 020#4a;
         swlin = 0;
         |PRINT;
         swalgun = 1;
      |FINSI;
   |SINO;
      |HAZ poncampos_s;
      |GRABA 020#4n;
   |FINSI;
   |LIBERA #4;
|FPRC;

|DEFBUCLE tmp_sopor;
   #camaniva#1;
   4,36;
   #0#1, 0000000001, #cachedup#3, "S";
   #0#2, 9999999999, #cachedup#4, "S";
   e;
   NULL, NULL, NULL, NULL, NULL, tmp_sopor1;
   #camaniva#4, #camaniva#6, #camaniva#0, #camaniva#2, #camaniva#28;
|FIN;

|PROCESO poncampos_r;
   #4#0  = "            ";   || Cuenta Proveedor
   #4#1  = camcla1;           || Campo orden 1
   #4#2  = camcla2;           || Campo orden 2
   #4#3  = " ";              || Error
   #4#4  = #camaniva#0;             || Libro
   #4#5  = #camaniva#3;             || Factura
   #4#6  = #camaniva#2;             || Serie
   #4#7  = #camaniva#4;             || Fecha
   #4#8  = #camaniva#12;            || Cuenta Contable
   #4#9  = #camaniva#13;            || Titulo Cuenta
   #4#10 = #camaniva#28;            || Concepto
   #4#11 = #camaniva#21;            || Total Factura
|FPRC;

|PROCESO tmp_reper1;

   |SI #cachedup#11 ! "S";
      |PINTA 2241, "            ";
      |PINTA 2241, #camaniva#0;
      |PINTA 2244, #camaniva#3;
      |PINTA 2251, #camaniva#2;
   |FINSI;

   concepto = #camaniva#28;
   |HAZ montacamcla;
   |DDEFECTO #4;
   #4#0 = "            ";
   #4#1 = camcla1;
   #4#2 = camcla2;
   |LEE 101#4=;
   |SI FSalida = 0;
      |SI #cachedup#11 ! "S";
         |SI #4#3 ! "X";
            swlin = 1;
            |PRINT;
         |FINSI;
         |HAZ poncampos_r;
         #4#3 = "X";
         |GRABA 020#4a;
         swlin = 0;
         |PRINT;
         swalgun = 1;
      |FINSI;
   |SINO;
      |HAZ poncampos_r;
      |GRABA 020#4n;
   |FINSI;
   |LIBERA #4;
|FPRC;

|DEFBUCLE tmp_reper;
   #camaniva#1;
   4,36;
   #0#1, 0000000001, #cachedup#3, "R";
   #0#2, 9999999999, #cachedup#4, "R";
   e;
   NULL, NULL, NULL, NULL, NULL, tmp_reper1;
   #camaniva#6, #camaniva#0, #camaniva#2, #camaniva#28;
|FIN;

|PROCESO saltos;
   |SI #cachedup#10 = 1;
      alfa1  = #4#1; numero = alfa1;
      serie  = #4#2;
   |SINO;
      alfa1  = #4#2; numero = alfa1;
      serie  = #4#1;
   |FINSI;

   |PINTA 2241, "            ";
   |PINTA 2241, #4#4;
   |PINTA 2244, #4#5;
   |PINTA 2251, #4#6;

   |SI numero < #cachedup#13; |ACABA; |FINSI;

   |SI sw = 0;
      antser = serie;
      antnume = numero;
      primero = numero;
      |SI primero > #cachedup#13;
         impnum = primero;
         texterr = impnum + " ES EL PRIMER NUMERO DE FACTURA ";
         |PRINT;
         swalgun = 1;
      |FINSI;
      sw = 1;
   |FINSI;

   |SI antser ! serie;
      |SI ultimo < #cachedup#14;
         impnum = ultimo;
         texterr = impnum + " ES EL ULTIMO NUMERO DE FACTURA ";
         |PRINT;
         swalgun = 1;
      |FINSI;
      antser = serie;
      antnume = numero;
      primero = numero;
      |SI primero > #cachedup#13;
         impnum = primero;
         texterr = impnum + " ES EL PRIMER NUMERO DE FACTURA ";
         |PRINT;
         swalgun = 1;
      |FINSI;
   |FINSI;

   ultimo = numero;
   |SI ultimo > #cachedup#14; |ACABA; |FINSI;

   nume1 = numero - antnume;
   |SI nume1 > 1;
      alfa1 = antnume;
      alfa2 = numero;
      impnum = "";
      texterr = "SALTA DEL NUMERO " + alfa1 + " AL NUMERO " + alfa2;
      |PRINT;
      swalgun = 1;
   |FINSI;
   antnume = numero;
|FPRC;

|DEFBUCLE comsaltos;
   #4#1;
   ;
   "            ", blancos, blancos;
   "zzzzzzzzzzzz", cetas,   cetas;
   e;
   NULL, saltos;
|FIN;

|| ************************************************************************
|| ****************** programa por tipo 3 *********************************
|| ************************************************************************
|PROCESO chequea; |TIPO 3;

   |IMPRE 0;
   |SI FSalida ! 0;  |ACABA;  |FINSI;

   |SI #cachedup#11 ! "S";
      informe = "cachedup";  || Duplicados
   |SINO;
      informe = "cachesal";  || Saltos
   |FINSI;

   |INFOR informe;
   |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

   tipo = #cachedup#0;
   |SI tipo = "R"; descrip = "REPERCUTIDO"; |FINSI;
   |SI tipo = "S"; descrip = "SOPORTADO  "; |FINSI;

   |HAZ nom_usu;
   alfa1 = "f" + nom_tmp;
   |NOME_DAT #catraiva #alfa1#;
   |ABRE #4;
   |CIERRA #4;
   |DELINDEX #4;

   |ABRE #4;
   |ATRI P;  |PINTA 2241, "<TEMPORAL>";  |ATRI 0;

   swalgun = 0;
   |SI tipo = "R";  |BUCLE tmp_reper;  |FINSI;
   |SI tipo = "S";  |BUCLE tmp_sopor;  |FINSI;

   |CIERRA #4;

   |SI #cachedup#11 = "S";   || Para Controlar Saltos
      sw = 0;
      blancos = " " * 30;
      cetas   = "z" * 30;
      |BUCLE comsaltos;
      |SI sw = 1; |Y ultimo < #cachedup#14;
         impnum = ultimo;
         texterr = impnum + " ES EL ULTIMO NUMERO DE FACTURA ";
         |PRINT;
         swalgun = 1;
      |FINSI;
   |FINSI;

   |SI swalgun = 1;
      |PIEINF;
   |FINSI;
   |FININF;
   |FINIMP;

   |DELINDEX #4;
|FPRC;

|PROCESO montacamcla;
   espacios = " " * 30;

   || ................................... Campo 1
   nume1 = (#cachedup#5 * 100) + #cachedup#6;
   alfa1 = concepto % nume1;
   ccc = alfa1; |HAZ quitalgo; alfa1 = ccc;
   nume2 = alfa1;
   alfa1 = nume2; |QBF alfa1;

   ceros = "0" * #cachedup#6;
   nume1 = 100 + #cachedup#6; nume1 = - nume1;
   alfa1 = (ceros + alfa1) % nume1;
   alfa1 = (alfa1 + espacios) % 130;

   || ................................... Campo 2
   |SI #cachedup#7 = 0;
      alfa2 = espacios;
   |SINO;
      nume1 = (#cachedup#7 * 100) + #cachedup#8;
      alfa2 = concepto % nume1;
      |SI #cachedup#9 = "N";
         ccc = alfa2; |HAZ quitalgo; alfa2 = ccc;
         nume2 = alfa2;
         alfa2 = nume2;
      |FINSI;
      |QBF alfa2;

      ceros   = "0" * #cachedup#8;
      blancos = " " * #cachedup#8;
      nume1   = 100 + #cachedup#8;
      |SI #cachedup#9 = "A";
         alfa2 = (alfa2 + blancos) % nume1;
      |SINO;
         nume1 = -nume1;
         alfa2 = (ceros + alfa2) % nume1;
      |FINSI;
      alfa2 = (alfa2 + espacios) % 130;
   |FINSI;

   |SI #cachedup#10 = 1;
      camcla1 = alfa1;
      camcla2 = alfa2;
   |SINO;
      camcla1 = alfa2;
      camcla2 = alfa1;
   |FINSI;
|FPRC;

|PROCESO quitalgo;
   |QBT ccc;
   c_alfa1 = ccc % 0;
   c_nume1 = c_alfa1;
   c_alfa1 = "";
   c_alfa2 = "";
   |PARA c_nume2 = 1; |SI c_nume2 [ c_nume1; |HACIENDO c_nume2 = c_nume2 + 1;
      c_nume3 = c_nume2 * 100 + 1;
      c_alfa2 = ccc % c_nume3;
      |SI c_alfa2 ] "0"; |Y c_alfa2 [ "9";
         c_alfa1 = c_alfa1 + c_alfa2;
      |FINSI;
   |SIGUE;
   ccc = c_alfa1;
|FPRC;
