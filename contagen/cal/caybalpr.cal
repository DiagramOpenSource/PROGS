|FICHEROS;
   caybalpr #0;       || Informe comprobacion suma y saldos
   cacuentm #1;       || temporal cuentas presupuestos
   cacuenta #10;      || maestro cuentas
   cacuepre #11;      || fichas presupuestarias
   canempre #30;
|FIN;

|VARIABLES;
   &entrada = 1;
   nSw = 1;
   x = 0;
   men1 = "     NO EXISTE EMPRESA !";
   informe = "";             || Variable informe
   inform1 = "caybalpr";     || Variable informe
   inform2 = "caybalp2";     || Variable informe
   inform3 = "caybalp1";     || Variable informe
   mes = 0;                  || Variable para saber el mes seleccionado
   letrames = "";            || Variable para poner el mes en letra
   varcamp = 0;              || Variable para posicionarse en un campo
   xxxxfiac = "  ";          || Variables para no modificable
   xxaponer = "  ";
   xrelleno = "                              ";
   error1 = "0000Nivel repetido";
   error2 = "0000No existe informe";
   error3 = "0000El numero de nivel no coincide con el numero de nivel de la empresa";
   swprint = 0;       || Variable para saber si imprimimos o no
   swsuma = 0;        || Variable para saber si sumamos o no
   nivel = 0;         || Variable que posee el nivel mas alto
   saldo = 0;         || Resta entre el debe y el haber;
   sw = 0;            || sw para nivel anterior
   nivelact = 0;      || Nivel actual
   rellenos = "";     || Variable para rellenar el hasta de la cuenta en zetas
   &i_debe = 0;       || Total del debe por registro
   &i_haber = 0;      || Total del haber por registro
   &i_acumdebe = 0;   || Total acumulado debe por registro
   &i_acumhaber = 0;  || Total acumulado haber por registro
   &i_deudor = 0;     || Total saldo deudor por registro
   &i_acreedor = 0;   || Total saldo acreedor por registro
   &t_debe = 0;       || Total del debe del informe
   &t_haber = 0;      || Total del haber del informe;
   &t_acumdebe = 0;   || Total del acumulado debe del informe
   &t_acumhaber = 0;  || Total del acumulado haber del informe
   &t_deudor = 0;     || Total saldo deudor por informe
   &t_acreedor = 0;   || Total saldo acreedor por informe
   &swlinea = 0;      || Pone o no linea en blanco en informe
   &swcomen = 0;

   &i_presu = 0;
   &i_acumpresu = 0;
   &i_desvpresu = 0;
   &t_presu = 0;
   &t_acumpresu = 0;
   &t_desvpresu = 0;
   runnom = "caybalpr";
   menor = 0;
   para1 = 0;
   &aparam = "";
   {-1}form  = "";           || Variables menu formato <1> o <2>
   form1 = "2400";
   form2 = "1";
   form3 = "Elija formato: ";
   form4 = "123";
   form5 = " < 1 > ";
   form6 = " < 2 > ";
   form7 = " < 3 > ";
   op_form = 0;

   nume1 = 0;
   nume2 = 0;
   des_pre = " ";
   has_pre = "z";
   t_agrupa = 0;
   aFichero = "";
   &eaTexto = "";
|FIN;

|INCLUYE dscont_i;
|INCLUYE digito_i;

|PROCESO presup2;    || si no esta como ficha presupuestaria no sale SI

   |DDEFECTO #11;
   #cacuepre#19 = t_agrupa;
   #cacuepre#0 = #cacuenta#0;
   #cacuepre#1 = #cacuenta#1;
   |LEE 001#11=;

   |PARA nume1 = 0; |SI nume1 [ 58; |HACIENDO nume1 = nume1 + 1;
         #cacuentm#nume1# = #cacuenta#nume1#;
   |SIGUE;

   |PARA nume1 = 59; |SI nume1 [ 70; |HACIENDO nume1 = nume1 + 1;
         nume2 = nume1 - 55;
         #cacuentm#nume1# = #cacuepre#nume2#;
   |SIGUE;

   #cacuentm#71 = #cacuepre#02;  || signo

   |GRABA 020#1n;
|FPRC;

|DEFBUCLE bu_pres2;   || balances situacion y explotacion
   #10#4;
   ;
   des_pre, 0,  0,  0,  0;
   has_pre, 4, 99, 99, 99;
   e;
   NULL, presup2;
|FIN;

|PROCESO presup0; |TIPO 0;

      |HAZ pre_aviso;
      aFichero = ("9p" + Usuario) % 108;
      |NOME_DAT #1 aFichero;

      |DELINDEX #1;
      |ABRE #1;
      |ABRE #11;
      |BUCLE bu_pres2;
      |CIERRA #1;
      |CIERRA #11;

      |POPV;
|FPRC;

|PROCESO pre_aviso;
   |PUSHV 11181760;
   |ATRI P;
   |CUADRO 11 18 15 60;
   |ATRI R;
   |CUADRO 12 19 14 59;
   |ATRI 0;
   |ATRI R;
   |ATRI I;
   |PINTA 1320, " Procesando Fichas Presupuestarias ... ";
   |ATRI 0;
|FPRC;

|PROCESO antesque;
   aparam = PARAMETRO;
   |QBF aparam;
   |SI aparam = "fech";
      |NOME_DAT #10 "cabalano";
      |NOME_DAT #11 "cabalpre";
   |FINSI;
|FPRC;
|| ***********************************************************************
||                       CALCULOS DE PANTALLA
|| ***********************************************************************

|PROCESO cuenta; |TIPO 0;
     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;
|FPRC;

|PROCESO mesconta; |TIPO 0;
   varcamp = Campo + 1;                || Coge el campo del nobre del mes
   mes = #canempre#11 + (#0Campo - 1);       || Operacion para seber que mes es
   |SI mes > 12; mes = mes - 12; |FINSI;
   |SI #0Campo = 13; mes = 13; |FINSI;

   |HAZ mesletra;                      || Recoge el mes en letra
   #0varcamp = letrames;
   |PINTA #0varcamp;                   || Pinta el nombre del mes

   |SI Campo = 4; |Y  #0#2 > #0#4;
       |ERROR;
   |FINSI;
|FPRC;

|PROCESO mesletra;
   |SI mes = 0;  letrames = "Apertura  "; |FINSI;
   |SI mes = 1;  letrames = "Enero     "; |FINSI;
   |SI mes = 2;  letrames = "Febrero   "; |FINSI;
   |SI mes = 3;  letrames = "Marzo     "; |FINSI;
   |SI mes = 4;  letrames = "Abril     "; |FINSI;
   |SI mes = 5;  letrames = "Mayo      "; |FINSI;
   |SI mes = 6;  letrames = "Junio     "; |FINSI;
   |SI mes = 7;  letrames = "Julio     "; |FINSI;
   |SI mes = 8;  letrames = "Agosto    "; |FINSI;
   |SI mes = 9;  letrames = "Septiembre"; |FINSI;
   |SI mes = 10; letrames = "Octubre   "; |FINSI;
   |SI mes = 11; letrames = "Noviembre "; |FINSI;
   |SI mes = 12; letrames = "Diciembre "; |FINSI;
   |SI mes = 13; letrames = "Cierre    "; |FINSI;
|FPRC;

|PROCESO numnivel;|TIPO 0;
   |SI #0#8 > dig3;
       |MENAV error3;
       |ERROR;
   |FINSI;
|FPRC;

|PROCESO nivel;|TIPO 7;  || Calculo para hacer no modificable
   |SI Campo = 10; |Y #0#8 < 2; || Si solo hemos seleccionado
       #0Campo = 0;
       |PINTA #0Campo;
       |C_M #0Campo,1,"N";              || un solo nivel;
   |FINSI;
   |SI Campo = 11; |Y #0#8 < 3; || Si solo hemos seleccionado
       #0Campo = 0;
       |PINTA #0Campo;
       |C_M #0Campo,1,"N";              || dos niveles;
   |FINSI;
   |SI Campo = 12; |Y #0#8 < 4; || Si solo hemos seleccionado
       #0Campo = 0;
       |PINTA #0Campo;
       |C_M #0Campo,1,"N";              || tres niveles;
   |FINSI;
   |SI Campo = 13; |Y #0#8 < 5; || Si solo hemos seleccionado
       #0Campo = 0;
       |PINTA #0Campo;
       |C_M #0Campo,1,"N";              || cuatro niveles;
   |FINSI;
   |SI Campo = 14; |Y #0#8 < 6; || Si solo hemos seleccionado
       #0Campo = 0;
       |PINTA #0Campo;
       |C_M #0Campo,1,"N";              || cinco niveles;
   |FINSI;
|FPRC;

|PROCESO nomodifi;             || RUTINA DE CONVERSION A NO MODIFICABLES
|| VARIABLES: A_
   xxaponer = Anono;
   xxxxfiac = Control % A1001;
   Control = Control + xxxxfiac;
   Control = Control + xrelleno;
   Control = Control % A120;
   Control = Control + xxaponer;
|FPRC;

|PROCESO nivelrep;|TIPO 0;  || Para comprobar que no repetimos los niveles

   |SI nivel = 0;        || Buscamo el nivel mas alto;
       nivel = #0#9;
   |FINSI;

   |SI Campo = 10;
       |SI #0#10 = #0#9;|Y #0#10 ! 0;    || Si hay dos niveles iguales error
          |MENAV error1;
          |ERROR;
          |ACABA;
      |SINO;
         |SI #0#10 > #0#9;  || Busca el nivel mas alto
             nivel = #0#10;
         |SINO;
             nivel = #0#9;
         |FINSI;
      |FINSI;
   |FINSI;

   |SI Campo = 11; |Y #0#11 ! 0;
       |SI #0#11 = #0#9; |O #0#11 = #0#10; || Si hay dos niveles iguales error
           |MENAV error1;
           |ERROR;
           |ACABA;
      |FINSI;
      |SI #0#11 > nivel;  || Busca el nivel mas alto
          nivel = #0#11;
      |FINSI;
   |FINSI;

   |SI Campo = 12; |Y #0#12 ! 0;      || Si hay dos niveles iguales error
       |SI #0#12 = #0#9; |O #0#12 = #0#10; |O #0#12 = #0#11;
           |MENAV error1;
           |ERROR;
           |ACABA;
       |FINSI;
       |SI #0#12 > nivel;  || Busca el nivel mas alto
           nivel = #0#12;
       |FINSI;
   |FINSI;

   |SI Campo = 13; |Y #0#13 ! 0;     || Si hay dos niveles iguales error
       |SI #0#13 = #0#9; |O #0#13 = #0#10; |O #0#13 = #0#11; |O #0#13 = #0#12;
           |MENAV error1;
           |ERROR;
           |ACABA;
       |FINSI;
       |SI #0#13 > nivel;  || Busca el nivel mas alto
           nivel = #0#13;
       |FINSI;
   |FINSI;

   |SI Campo = 14; |Y #0#14 ! 0;    || Si hay dos niveles iguales error
       |SI #0#14 = #0#9; |O #0#14 = #0#10; |O #0#14 = #0#11; |O #0#14 = #0#12; |O #0#14 = #0#13;
          |MENAV error1;
          |ERROR;
          |ACABA;
       |FINSI;
       |SI #0#14 > nivel;  || Busca el nivel mas alto
           nivel = #0#14;
       |FINSI;
   |FINSI;
|FPRC;

|PROCESO LeeAgrupa;
    |SI t_agrupa ! #0#20; |O nSw = 0;
        #0#21 = "M";
        |ABRE #11;
        #11#19 = #0#20;
        #11#0  = doce;
        #11#1  = 0;
        |LEE 041#11];
        |SI FSalida = 0; |Y #11#19 = #0#20;
            #0#21 = #11#3;
        |FINSI;
        |CIERRA #11;
        |PINTA #0#21;
        nSw = 1;
        t_agrupa = #0#20;
    |FINSI;
|FPRC;

|| **********************************************************************
||                       CALCULOS DE IMPRESION
|| **********************************************************************

|PROCESO condicion;    || Calculo condiciones de impresion

   |HAZ digitos2;
   |SI d_error ! 0;  |ACABA;  |FINSI;
   swprint = 0;           || Variable para saber si imprimimos o no
   swsuma = 0;            || Variable para saber si sumamos o no
   i_debe = 0;            || Pone a cero el total debe del registro
   i_haber = 0;           || Pone a cero el total haber del registro
   i_presu = 0;
   i_acumdebe = 0;        || Pone a cero el total acumulado debe del registro
   i_acumhaber = 0;       || Pone a cero el total acumulado haber del registro
   i_acumpresu = 0;

   |SI sw = 0;
      nivelact = #cacuentm#55;  || Nivel actual
      sw = 1;
   |FINSI;

   |SI #cacuentm#55 ! 0;     || Condicion para saber si el nivel esta seleccionado
      |SI #cacuentm#55 = #0#9;|O #cacuentm#55 = #0#10;|O #cacuentm#55 = #0#11; || Compara 3 niveles
         swprint = 1;
      |SINO;
         |SI #cacuentm#55 = #0#12;|O #cacuentm#55 = #0#13;|O #cacuentm#55 = #0#14; || Compara 3 niveles
            swprint = 1;
         |SINO;
            swprint = 0;
         |FINSI;
      |FINSI;
   |FINSI;

   |SI swprint = 1; |Y #cacuentm#55 = nivel;  || Si es el nivel mas alto entoces
      swsuma = 1;                    || se suma.
   |FINSI;

   |SI #cacuentm#3 = "S"; |Y #cacuentm#55 [ nivel;   || Si el nivel no esta seleccionado
      swsuma = 1;                    || pero tiene en el campo pase una S
      swprint = 1;                   || entonces se imprime y se suma.
   |FINSI;

   |SI swprint = 1;    || Si se puede imprimir
      varcamp = 9 + (#0#2 * 3)
      || ************ Bucle del debe y haber periodo *************

      |PARA x = #0#2; |SI x [ #0#4; |HACIENDO x = x + 1;
         i_debe = i_debe + #cacuentm#varcamp#;    || Suma el debe
         varcamp = varcamp + 1;          || Se situa en  el campo del haber
         i_haber = i_haber + #cacuentm#varcamp#;  || Suma el haber
         varcamp = varcamp + 2;          || Se situa en el campo debe
      |SIGUE;

      |SI #0#21 = "M";
          |PARA x = #0#2; |SI x [ #0#4; |HACIENDO x = x + 1;
                |SI x ! 0;
                    varcamp = x + 58;
                    i_presu = i_presu + #cacuentm#varcamp#;
                |FINSI;
          |SIGUE;
      |SINO;
          |PARA x = 59; |SI x [ 70; |HACIENDO x = x + 1;
               i_presu = i_presu + #cacuentm#x#;
          |SIGUE;
      |FINSI;

      nume1 = 0;
      || .... modificado para compara con el saldo
      |SI #cacuentm#71 = "D";   nume1 = i_debe - i_haber;  |FINSI;
      |SI #cacuentm#71 = "H";   nume1 = i_haber - i_debe;  |FINSI;

      i_desvpresu = ((nume1 * 100) / i_presu) ! 2;

      || ************ Bucle del debe y haber acumulado *************

      varcamp = 9;
      |PARA x = 0; |SI x [ #0#4; |HACIENDO x = x + 1;  || Bucle de campos
         i_acumdebe = i_acumdebe + #cacuentm#varcamp#;   || Suma el acumulado debe
         varcamp = varcamp + 1;                 || Se situa en  el campo del haber
         i_acumhaber = i_acumhaber + #cacuentm#varcamp#; || Suma el acumulado haber
         varcamp = varcamp + 2;          || Se situa en el campo debe
      |SIGUE;

      |SI #0#21 = "M";
          varcamp = #0#4 + 58;
      |SINO;
          varcamp = 70;
      |FINSI;
      |PARA x = 59; |SI x [ varcamp;  |HACIENDO x = x + 1;
         i_acumpresu = i_acumpresu + #cacuentm#x#;
      |SIGUE;

      nume1 = 0;
      |SI #cacuentm#71 = "D";   nume1 = i_acumdebe - i_acumhaber; |FINSI;
      |SI #cacuentm#71 = "H";   nume1 = i_acumhaber - i_acumdebe; |FINSI;
      t_desvpresu = ((nume1 * 100)/ i_acumpresu) ! 2;

      saldo = i_acumdebe - i_acumhaber;       || Coge la diferencia
      |SI saldo > 0; i_deudor = saldo; i_acreedor = 0;            |FINSI;
      |SI saldo < 0; i_deudor = 0;     i_acreedor = saldo * (-1); |FINSI;
      |SI saldo = 0; i_deudor = 0;     i_acreedor = 0;            |FINSI;

      |SI i_acumdebe = 0;|Y i_acumhaber = 0;|Y i_acumpresu = 0;|Y #0#6 = "N";
         |ACABA;
      |FINSI;

      |SI nivelact ! #cacuentm#55;    || Si el nivel actual es distinto al
         swlinea = 1;                 || anterior pone una linea en blanco
         nivelact = #cacuentm#55;     || Actualiza el nivel
      |SINO;
         swlinea = 0;          || Si no no pone linea en blanco
      |FINSI;
      |PRINT;

      |SI swsuma = 1;    || Si se puede sumar los totales
         t_debe      = t_debe      + i_debe;        || Suma el total del debe
         t_haber     = t_haber     + i_haber;       || Suma el total del haber
         t_presu     = t_presu     + i_presu;
         t_acumdebe  = t_acumdebe  + i_acumdebe;    || Suma el total acumulado debe
         t_acumhaber = t_acumhaber + i_acumhaber;   || Suma el total acumulado haber
         t_acumpresu = t_acumpresu + i_acumpresu;
         t_deudor    = t_deudor    + i_deudor;      || Suma total saldo deudor
         t_acreedor  = t_acreedor  + i_acreedor;    || Suma total saldo acreedor
      |FINSI;

      swcomen = 1;        || Para imprimir el saldo anterior
   |FINSI;
|FPRC;

|DEFBUCLE caybalpr0;      || Bucle del plan contable
   #1#3;           || Lee por la tercera clave
   ;
   #0#0,1;
   rellenos,6;
   e;
   NULL,condicion;
|FIN;

|DEFBUCLE caybalpr1;      || Bucle del plan contable
   #1#1;           || Lee por la primera clave
   ;
   #0#0,1;
   #0#1,6;
   e;
   NULL,condicion;
|FIN;

|PROCESO rellena;
   rellenos = "zzzzzzzzzzzz";
   menor = -1;

   |PARA para1 = 9; |SI para1 [ 14; |HACIENDO para1 = para1 + 1;
      |SI #0para1 ! 0;
         |SI menor = -1;       menor = #0para1;  |FINSI;
         |SI menor > #0para1;  menor = #0para1;  |FINSI;
      |FINSI;                   || saca el nivel elegido mas peque¤o
   |SIGUE;

   |SI menor ! 0;
      |SI menor = 1;  nume1 = dig4;  |FINSI;
      |SI menor = 2;  nume1 = dig5;  |FINSI;   || construye el hasta segun
      |SI menor = 3;  nume1 = dig6;  |FINSI;   ||/el nivel mas bajo elegido
      |SI menor = 4;  nume1 = dig7;  |FINSI;
      |SI menor = 5;  nume1 = dig8;  |FINSI;
      |SI menor = 6;  nume1 = dig9;  |FINSI;
      nume1 = 100 + nume1;
      rellenos = #0#1 % nume1;
      rellenos = rellenos + "zzzzzzzzzzzz";
      rellenos = rellenos % 112;
   |FINSI;
|FPRC;

|PROCESO impre;       || Impresion por impresora;
   |HAZ FecApeCie;
   |SI enMAper ! dig1;
       |SI #0#4 < enMAper; |Y #0#4 ! 0;
           |ACABA;
       |FINSI;
   |FINSI;

   eaTexto = "Presupuesto Mensual";
   |SI #0#21 = "A";
       eaTexto = "Presupuesto Anual";
   |FINSI;

   |HAZ rellena;

   |MENU form;
   op_form = FSalida;
   |SI op_form [ 0; |ACABA; |FINSI;

   |SI op_form = 1; informe = inform1; |FINSI;
   |SI op_form = 2; informe = inform2; |FINSI;
   |SI op_form = 3; informe = inform3; |FINSI;

   |IMPRE 0;                                 || Abrimos impresora
   |SI FSalida = 0;

      t_agrupa = #0#20;
      |HAZ presup0;

      |INFOR informe;                    || Abrimos informe
      |SI FSalida = 0;
          |SI #0#16 = "D";
              |BUCLE caybalpr0;
          |SINO;
              |BUCLE caybalpr1;
          |FINSI;
          |PIEINF;
          |FININF;
      |SINO;
          |MENAV error2;
      |FINSI;

      |FINIMP;
   |FINSI;
|FPRC;

|PROCESO borrar;
   nivel = 0;
   sw = 0;
   swcomen = 0;
   i_debe = 0;
   i_haber = 0;
   i_presu = 0;
   i_acumdebe = 0;
   i_acumhaber = 0;
   i_acumpresu = 0;
   i_deudor = 0;
   i_acreedor = 0;
   t_debe = 0;
   t_haber = 0;
   t_presu = 0;
   t_acumdebe = 0;
   t_acumhaber = 0;
   t_acumpresu = 0;
   t_deudor = 0;
   t_acreedor = 0;
|FPRC;

|| *********************************************************************
||                             COMIENZO
|| *********************************************************************

|PROGRAMA;

   |HAZ antesque;
   |CLS;
   |CABEZA "Balance Sumas y Saldos";
   |HAZ inicio;

   |DDEFECTO #0;
   FSalida = 1;
   |PARA; |SI 0 < FSalida; |HACIENDO;
      |PINPA #0#0;
      |PINDA #0#0;
      |ENDATOS #1#0;   || Seleccion
      |SI 0 [ FSalida;
         |HAZ impre;
         |HAZ borrar;
         FSalida = 1;
      |FINSI;
   |SIGUE;
   |DELINDEX #1;  || se carga el temporal
|FPRO;
