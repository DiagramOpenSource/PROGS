|FICHEROS;
   catqz002 #0;

   catqm001 #10;
   catqw002 #100;

   caivases #4;
   calibros #5;

   caivalin #11;
   camaniva #12;

   caivaasi;
   :/modelos/def/dsmom030 #130;

   &regisnif@ #709;
   :/basica/def/agim0003  #710;
   :/integral/def/dsam0103 #711;

   &poblaci@  #703;
   &sgpaises@ #718;

   caivatm4;
|FIN;

|VARIABLES;
   &entrada = 1;
   fFecha   = @;
   aAlfa    = "";
   aAlfa1   = "";
   aAlfa2   = "";
   aAlfa3   = "";
   nNume1   = 0;
   nDebe    = 0;
   nHaber   = 0;

   mensa1   = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
   mensa2   = "    ERROR FECHA INFERIOR O IGUAL AL BLOQUEO DE DATOS";
   aFichero = "";
   nConte   = 0;
   nError   = 0;

   nSwHay   = 0;

   fFecUlt  = @;
   aSigAn   = "";
   aDeMas   = "";

   Comodin  = "";
   &nLeer   = 0;
   nSw      = 0;
   nSal     = 0;
   nHay     = 0;

   &enTFich   = 0;
   &Local     = 0;
   &enLlegoa9 = 0;
   &enNum9    = 0;
   &eaPath    = "";
   &eaPathExc = "";
   &eaFichero = "";

   &enError    = 0;

   &enImporte  = 0;
   &eaDescrip  = "";
   &eaSigno    = "";
   &eaReferen  = "";

   &enQSerie   = 0;
   &eaUnidadBasico = "";
   &eaOrigen  = "";
   &eaDestino = "";

   nLinea     = 0;
   aCol       = "";
   aExcel     = "";
   nXls       = 0;
   aExt       = "";

   nLibro     = 0;
   nIncre     = 0;
   nSumaDoc   = 0;
   aSerie     = "";
   nElDoc     = 0;
   nDocumento = 0;
   nNumFra    = 0;
   nMFactu    = 0;
   nFactu     = 0;
   aContra    = "";
   nPeriodo   = 0;

   &eaVarDni  = "";
   &enCalcCif = 0;
   nSwBueno   = 0;
   &enAParam  = 0;

   &eaNomFich = "";
   &enAlguno  = 0;
   &enSiiLib  = 0;
   &enSiiDoc  = 0;
|FIN;

|INCLUYE dscont_i;

|| ************************************************************************

|CALCULO fecha;
   |SI #0Campo < fec1; |O #0Campo > fec2;
      |MENAV mensa1;
      |ERROR;
      |ACABA;
   |FINSI;

   |SI Campo = 4;  |Y #0#3 > #0#4;
      |MENAV "     Atencion  fecha Erronea";
      |ERROR;
   |FINSI;
|FCAL;

|CALCULO LeeLibro; |TIPO 0;
   |DDEFECTO #5;
   |ABRE #5;
   #5#0 = #0Campo;
   |LEE 000#5=;
   |CIERRA #5;

   |SI #5#2 ! "R";
       |MENAV "    ATENCION!!! El Libro de I.V.A. no es de Repercutido ...";
       |ERROR;
   |FINSI;
|FCAL;

|CALCULO Ehque; |TIPO 7;
 |SI enQSerie = 0;
     |C_M #0#7,1,"N"; #0#7 = ""; |PINTA #0#7;
 |SINO;
     |C_M #0#7,1,"S";
 |FINSI;
|FCAL;

|CALCULO BuscaNum; |TIPO 7;
   |SI enQSerie = 0;
       |ABRE #5;
       #5#0 = #0#6;
       |LEE 001#5=;
       |SI FSalida = 0;
           #0#8 = #5#4; |PINTA #0#8;
           nIncre = #5#3;
       |FINSI;
       |CIERRA #5;
   |SINO;
       |ABRE #4;
       #4#0 = #0#7;
       #4#1 = "R";
       |LEE 001#4=;
       |SI FSalida = 0;
           #0#8 = #4#3; |PINTA #0#8;
           nIncre = #4#4;
       |FINSI;
       |CIERRA #4;
   |FINSI;
|FCAL;

|| ********************************************************************
|PROCESO GrabaAuxiliar;

     |SI #100#1 < #0#9; |O #100#1 > #0#10; |ACABA; |FINSI;
     |SI #100#2 < #0#3; |O #100#2 > #0#4;  |ACABA; |FINSI;

     nLinea = nLinea + 1;
     #100#0 = nLinea;
     |GRABA 020#100n;
     |SI #100#2 > fFecUlt; fFecUlt = #100#2; |FINSI;
     nSwHay = 1;
|FPRC;

|PROCESO Una;
     ||| aCol contiene la columna, "A", "B" y nXls la fila

     aAlfa = aCol + nXls;
     |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa;
     |SI FSalida ! 0;
         nSal = 1;
     |SINO;
         |QBF aAlfa;
         |SI aAlfa ! ""; nHay = 1; |FINSI;
     |FINSI;

     |SI aCol = "B";
         nSwBueno = 0;
         aAlfa1 = aAlfa % 102;
         aAlfa2 = aAlfa % 403;  aAlfa2 = aAlfa2 < "";
         aAlfa3 = aAlfa % -102; aAlfa3 = "20" + aAlfa3;
         |SI aAlfa2 = "ene"; aAlfa2 = "01"; nSwBueno = 1; |FINSI;
         |SI aAlfa2 = "feb"; aAlfa2 = "02"; nSwBueno = 1; |FINSI;
         |SI aAlfa2 = "mar"; aAlfa2 = "03"; nSwBueno = 1; |FINSI;
         |SI aAlfa2 = "abr"; |O aAlfa2 = "apr"; aAlfa2 = "04"; nSwBueno = 1; |FINSI;
         |SI aAlfa2 = "may"; aAlfa2 = "05"; nSwBueno = 1; |FINSI;
         |SI aAlfa2 = "jun"; aAlfa2 = "06"; nSwBueno = 1; |FINSI;
         |SI aAlfa2 = "jul"; aAlfa2 = "07"; nSwBueno = 1; |FINSI;
         |SI aAlfa2 = "ago"; aAlfa2 = "08"; nSwBueno = 1; |FINSI;
         |SI aAlfa2 = "sep"; aAlfa2 = "09"; nSwBueno = 1; |FINSI;
         |SI aAlfa2 = "oct"; aAlfa2 = "10"; nSwBueno = 1; |FINSI;
         |SI aAlfa2 = "nov"; aAlfa2 = "11"; nSwBueno = 1; |FINSI;
         |SI aAlfa2 = "dic"; aAlfa2 = "12"; nSwBueno = 1; |FINSI;
         |SI nSwBueno = 1;
             aAlfa = aAlfa1 + "/" + aAlfa2 + "/" + aAlfa3;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO CargaHoja;
     nLinea = 0;

     |PARA nXls = 2; |SI nXls < 65536; |HACIENDO nXls = nXls + 1;
           nSal = 0;
           nHay = 0;
           aCol = "A"; |HAZ Una; #100#1 = aAlfa;
           aCol = "B"; |HAZ Una; #100#2 = aAlfa;
           aCol = "C"; |HAZ Una; #100#3 = aAlfa;
           aCol = "D"; |HAZ Una; #100#4 = aAlfa;
           aCol = "E"; |HAZ Una; #100#5 = aAlfa;
           aCol = "F"; |HAZ Una; #100#6 = aAlfa;
           aCol = "G"; |HAZ Una; #100#7 = aAlfa;
           aCol = "H"; |HAZ Una; #100#8 = aAlfa;

           |SI nSal = 1; |O nHay = 0;
               |SAL;
           |SINO;
               |HAZ GrabaAuxiliar;
           |FINSI;
     |SIGUE;
|FPRC;

|PROCESO excel;
     aExcel = eaPathExc + eaFichero;

     |XABRE "CE", aExcel, 0;
     |SI FSalida < 0;
          aAlfa = "0000No existe " + aExcel;
          |MENAV aAlfa;
          |ACABA;
     |FINSI;

     aAlfa = "Pasando datos de la Hoja Excel " + eaFichero;
     |PINTA 2107, aAlfa;

     |ALFACALLDLL "agixl97.dll", "carga_book", aExcel;
     |ALFACALLDLL "agixl97.dll", "selecciona_hoja", eaCuenta;
     |SI FSalida = 0;
         |ABRE #100;
         |HAZ CargaHoja;
         |CIERRA #100;
     |FINSI;
     |ALFACALLDLL "agixl97.dll", "fin_book", "salva";
|FPRC;

|PROCESO PasoaIndenxado;

    |CARGADLL "agixl97.dll";
    |SI FSalida < 0;
        |MENAV "0000No se puede usar agixl97.dll";
    |SINO;
        |HAZ excel;
        |DESCARGADLL "agixl97.dll";
    |FINSI;
|FPRC;

|PROCESO VerDirectorio;
      nSw      = 0;
      eaOrigen = eaPath + "libroiva." + aExt;
      |_PDIR eaOrigen;
      |PARA; |SI FSalida = 0; |HACIENDO;
             nLeer    = 1;
             nSw      = 1;
             eaFichero = eaOrigen - eaPath;
                 eaPathExc = eaPath;
                 |HAZ CopioAntes;
                 |HAZ PasoaIndenxado;
             |_SDIR eaOrigen;
       |SIGUE;

       |SI nSw = 0;
           eaOrigen = eaPath + "libroiva." + aExt;
           |R_PDIR eaOrigen;
           |PARA; |SI FSalida = 0; |HACIENDO;
                  nLeer  = 2;
                  nSw    = 1;
                  eaFichero = eaOrigen - eaPath;
                      eaPathExc = eaPath;
                      |HAZ CopioAntes;
                      |HAZ PasoaIndenxado;
                  |R_SDIR eaOrigen;
            |SIGUE;
       |FINSI;
|FPRC;

|| ********************************************************************
|| ********************************************************************
|PROCESO Anyade;
    aAlfa = eaDescrip; |QBF aAlfa;

    |SI aAlfa ! ""; aAlfa = aAlfa + " "; |FINSI;

    aAlfa1 = #12#2; |QBF aAlfa1;
    |SI aAlfa1 ! "";
        aAlfa1 = aAlfa1 + #caivaasi#97; |QBF aAlfa1;
        aAlfa  = aAlfa + aAlfa1;
    |FINSI;

    aAlfa1 = #12#3; |QBF aAlfa1;
    |SI #caivaasi#98 = "S";
        aAlfa1 = ("000000" + aAlfa1) % -106;
    |FINSI;
    eaDescrip = aAlfa + aAlfa1;
|FPRC;

|| **************************************

|PROCESO BuscaNumero;
   nLibro = #0#6;
   aSerie = #0#7;
   nFactu = #0#8;

   |ABRE #12;
   #12#0 = nLibro;
   #12#1 = 9999999999;
   |LEE 010#12];
   |SI FSalida = 0;
       |LEE 040#12a;
   |SINO;
       |LEE 040#12u;
   |FINSI;
   nElDoc = 1;
   |SI FSalida = 0; |Y #12#0 = nLibro;
       nElDoc = #12#1 + 1;
   |FINSI;
   |CIERRA #12;

   |ABRE #130;
   |DDEFECTO #130;
   #130#0 = #10#27;
   |LEE 041#130=;
   |CIERRA #130;
   aContra = #130#20; |QBF aContra;
   |SI aContra = "";
       aContra = "700000000000";
       |SI MaximoDigitos ! 12;
           nNume1 = 100 + MaximoDigitos;
           aContra = aContra % nNume1;
       |FINSI;
   |FINSI;
|FPRC;

|PROCESO Departamento;
   eOpDep = 0;
   enActividad = #10#25;
   eaCodDep    = #10#25;
   |HAZ Actividad;
   eOpSub = 0;
   enConcepto  = #10#27;
   eaCodSubdep = #10#26;
   |HAZ OtroSubActividad;

   enAParam = #10#25;
   |HAZ eActivParam;
|FPRC;

|PROCESO Periodo;
  aAlfa1 = #12#4;
  aAlfa1 = aAlfa1 % 402;
  nNume1 = aAlfa1;
  nPeriodo = nNume1;
  |SI #caivaasi#46 = "A"; nPeriodo = 0; |FINSI;
  |SI #caivaasi#46 = "T"; |Y enEjercicio [ 2009;
      nPeriodo = 1;
      |SI nNume1 ] 4;  nPeriodo = 2; |FINSI;
      |SI nNume1 ] 7;  nPeriodo = 3; |FINSI;
      |SI nNume1 ] 10; nPeriodo = 4; |FINSI;
  |FINSI;
|FPRC;

|PROCESO Cliente;
    eaCodNif = #12#14; |QBF eaCodNif;
    |SI eaCodNif = ""; |ACABA; |FINSI;

    #709#0 = eaCodNif;
    |LEE 141#709=;
    |SI FSalida  ! 0;
        |DDEFECTO #709;

        #709#0  = eaCodNif;   || Nif
        #709#1  = #12#13;       || Nombre y Apellidos
        aAlfa1  = #100#5; |QBF aAlfa1;
        |SI aAlfa1 ! ""; aAlfa1 = ("00000" + aAlfa1) % -105; |FINSI;
        aAlfa2  = aAlfa1 % 102; #709#4  = aAlfa2;
        aAlfa2  = aAlfa1 % 303; #709#5  = aAlfa2;

        |SI #709#6 ! 99; #709#12 = "ES "; |FINSI;

        eaCodPais = #709#12;
        |HAZ LeePais;
        #709#13 = #718#1;     || Nombre Pais

        enCodProv = #709#4;
        enCodPobl = #709#5;
        enSwMensa = 1;
        |HAZ LeeLocalidad;
        enSwMensa = 0;
        #709#6 = eaPoblacion;
        #709#7 = eaProvincia;
        |SI enSwDeDonde = 1;
            #709#14 = #703#4;
            #709#15 = #703#5;
        |FINSI;
        |GRABA 020#709n;
    |FINSI;
    |LIBERA #709;
|FPRC;

|PROCESO BuscaIVA;
  nNume1 = ((#100#7 * 100) / #100#6 ) ! 0;
  |SI nNume1 ! 0; |Y nNume1 ! 4; |Y nNume1 ! 10; |Y nNume1 ! 21;
      |SI nNume1 > 11;                nNume1 = 21; |FINSI;
      |SI nNume1 > 5; |Y nNume1 < 11; nNume1 = 10; |FINSI;
      |SI nNume1 [ 5;                 nNume1 = 4;  |FINSI;
  |FINSI;
  #11#7 = nNume1;
  efFechaIVA = #12#4;  enLineaIVA = 0;
  enPorcIVA  = #11#7;  enPorcREC = #11#9;
  eaCtaIVA   = #11#13; eaCtaREC  = #11#14;
  |HAZ BuscaTipoIVA;
  #11#16 = enLineaIVA;
  #11#13 = eaCtaIVA;
  #11#14 = eaCtaREC;
|FPRC;

|PROCESO BorroLineas;
   |BORRA 020#11a;
   |LIBERA #11;
|FPRC;

|DEFBUCLE BorroLineas;
  #11#1;
  ;
  #12#0, #12#1, 01;
  #12#0, #12#1, 99;
  be;
  NULL, BorroLineas;
|FIN;

|PROCESO LeeAuxiliar;

   nSumaDoc   = 0;
   nDocumento = nElDoc;
   |SI #0#8 = 0;
       nNumFra = #100#1;
   |SINO;
       nNumFra = nFactu;
   |FINSI;

   |SELECT #3#12;
   #12#0 = nLibro;
   #12#2 = aSerie;
   #12#3 = nNumFra;
   |LEE 041#12=;
   |SI FSalida = 0;
       |SI #0#11 = "N"; |SELECT #1#12; |ACABA;            |FINSI;
       |SI #0#11 = "S"; nDocumento = #12#1; nSumaDoc = 1; |FINSI;
   |FINSI;

   |SELECT #1#12;
   |DDEFECTO #12;
   #12#0 = nLibro;
   #12#1 = nDocumento;
   |LEE 141#12=;
   |SI FSalida ! 0;
       #12#0 = nLibro;
       #12#1 = nDocumento;
       |GRABA 020#12b;
   |FINSI;

   |PINTA 2107, "                                                         ";
   aAlfa = "Creando Factura " + #100#1 + " a Fecha " + #100#2;
   |PINTA 2107, aAlfa;

   |DDEFECTO #12;
   #12#0 = nLibro;
   #12#1 = nDocumento;

   #12#2   = aSerie;
   #12#3   = nNumFra;
   #12#4   = #100#2;
   |HAZ Periodo;
   #12#5  = nPeriodo;
   #12#7  = 01;
   #12#8  = #12#4;
   #12#9  = -1;          || Cuidado
   #12#10 = #10#25;
   #12#11 = #10#26;
   #12#12 = #10#32;
   #12#13 = #100#4;
   #12#14 = #100#3;
   #12#24 = eaNombreAct;
   #12#25 = eaNomSubdep;

   #12#27 = #10#27;
   eaDescrip = #10#28;
   |SI #10#29 = "S"; |HAZ Anyade; |FINSI;
   #12#28 = eaDescrip;
   #12#29 = #12#27;
   #12#30 = #12#28;
   #12#36 = aTipoIVA;
   |HAZ Cliente;

   |BUCLE BorroLineas;

   |ABRE #11;
   |DDEFECTO #11;
   #11#0 = #12#0;
   #11#1 = #12#1;
   #11#2 = 1;
   |LEE 141#11=;
   |SI FSalida ! 0;
       #11#0 = #12#0;
       #11#1 = #12#1;
       #11#2 = 1;
       |GRABA 020#11b;
   |FINSI;

   #11#3 = #12#27;
   #11#4 = #12#28;

   #11#6 = #100#6;
   #11#8 = #100#7;
   #11#12 = aContra;
   #11#16 = 0;
   |HAZ  BuscaIVA;
   #11#30 = #12#10;
   #11#31 = #12#11;
   |GRABA 020#11a;
   |LIBERA #11;

   #12#19 = #12#19 + #11#6;
   #12#15 = #12#15 + #11#6;
   #12#17 = #12#15 %  #12#16;
   #12#20 = #12#20 + #11#22;
   #12#23 = #12#23 + (#11#8 + #11#10);
   #12#26 = #12#26 + #11#11;
   #12#21 = #12#19 + #12#23 - #12#26 - #12#20;

   |GRABA 020#12a;
   |LIBERA #12;
   |CIERRA #11;

   enSiiLib = #12#0;
   enSiiDoc = #12#1;
   |HAZ AuxIvatm4;

   |SI nMFactu < #12#3; nMFactu = #12#3; |FINSI;

   |SI nSumaDoc = 0; nElDoc = nElDoc + 1;  |FINSI;
   |SI #0#8 ! 0; nFactu = nFactu + nIncre; |FINSI;
   nSw = 1;

|FPRC;

|DEFBUCLE LeeAuxiliar;
   #100#1;
   ;
   0000001;
   9999999;
   ;
   NULL, LeeAuxiliar;
|FIN;

|| ********************************************************************
|PROCESO ActIVA;
   |SI #caivaasi#44 = "N";
       |ABRE #5;
       #5#0 = nLibro;
       |LEE 001#5=;
       |SI FSalida = 0;
           |SI nMFactu ] #5#4;
               #5#4 = nMFactu + #5#3;
           |FINSI;
           |GRABA 020#5a;
           |LIBERA #5;
       |FINSI;
       |CIERRA #5;
   |SINO;
       |ABRE #4;
       #4#0 = aSerie;
       #4#1 = aTipoIVA;
       |LEE 001#4=;
       |SI FSalida = 0;
           |SI nMFactu ] #4#3;
               #4#3 = nMFactu + #4#4;
           |FINSI;
           |GRABA 020#4a;
           |LIBERA #4;
       |FINSI;
       |CIERRA #4;
   |FINSI;
|FPRC;
|| ********************************************************************
|PROCESO elpase;

   |ABRE #10;
   |DDEFECTO #10;
   #10#0 = #0#1;
   |LEE 041#10=;
   |CIERRA #10;

   enTFich = 2;  ||fichero de recaudacion

   eaPath = #10#23; |QBF eaPath;
   |SI eaPath = "";
       |MENAV "    Error. Path de ficheros erroneo";
       |ACABA;
   |FINSI;

   Comodin = "";
   |IP_REMOTO Comodin;
   |QBF Comodin;
   |SI Comodin = "";
       Local = 1;
   |SINO;
       Local = 0;
   |FINSI;

   aFichero = "2h" + Usuario;
   |NOME_DAT #100 aFichero;
   |DELINDEX #100;

   enAlguno = 0;
   eaNomFich  = ("0p" + Usuario) % 108;
   |NOME_DAT #caivatm4#eaNomFich#;
   |DELINDEX #caivatm4;

   nSwHay = 0;
   aExt = "xls";  |HAZ VerDirectorio;
   aExt = "xlsx"; |HAZ VerDirectorio;

   |SI nSwHay = 1;
       |HAZ BuscaNumero;
       |HAZ Departamento;
       nSw    = 0;

       nMFactu = 0;
       |ABRE #12;
       |ABRE #709;
       |BUCLE LeeAuxiliar;
       |SI nSw = 1;
           |HAZ ActIVA;
       |SINO;
           |MENAV "    No hay facturas para importar";
       |FINSI;
       |CIERRA #709;
       |CIERRA #12;
       |SI enAlguno = 1;
           |SUB_EJECUTA_NP "caut0018;PASE";
       |FINSI;
   |SINO;
       |MENAV "    No existe Fichero para Importar";
   |FINSI;
|FPRC;

|PROGRAMA;

   |HAZ inicio;

   ewNif = 0;
   |HAZ DirAplicSt;
   enSwDeDonde = -1;
   |SI HayAcceso = 1;
       enSwDeDonde = 1;
       |REFERENCIA_DEF regisnif@, #711;
   |SINO;
       |SI Haybasica = 1;
           enSwDeDonde = 0;
           |REFERENCIA_DEF regisnif@, #710;
       |FINSI;
   |FINSI;
   |SI enSwDeDonde = -1; ewNif = 1; |FINSI; || no existe Basica ni Integrada

   aTipoIVA = "R";
   |HAZ eLeeParametro;
   enQSerie = 0;
   |SI #caivaasi#44 = "S"; enQSerie = 1;|FINSI;
   |HAZ LeeUnidadBasico;

   |CLS;
   |PINPA #0#0;
   |CABEZA "Pase a Libro de Facturas Emitidas";

   |ABRE #0;
   #0#0 = "A";
   |LEE 141#0=;
   |SI FSalida ! 0;
       |DDEFECTO #0;
       #0#3 = fec1;
       #0#4 = fec2;
       |GRABA 020#0b;
   |FINSI;
   |HAZ BuscaNum;

   |PINDA #0#0;
   |ENDATOS #2#0;
   |SI FSalida = 0;
       nNume1  = #0#3; nNume1 = nNume1 - 1;
       fFecUlt = nNume1;
       |HAZ elpase;
       nNume1  = fFecUlt; nNume1 = nNume1 + 1;
       #0#3    = nNume1;
       #0#4    = fec2;
       |SI #0#3 > #0#4; #0#3 = #0#4; |FINSI;

       |GRABA 021#0a;
   |FINSI;
   |LIBERA #0;
   |CIERRA #0;
|FPRO;
