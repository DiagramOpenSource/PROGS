|FICHEROS;
   camautin #0;
   camautoc #1;
   camautol #2;
   cacuenta #3;
   camautti #4;

   canempre #30;
   caselem1 #998;
|FIN;

|VARIABLES;
   aAlfa1 = "";
   aAlfa2 = "";
   aAlfa3 = "";
   nNume1 = 0;
   nNume2 = 0;
   nNume3 = 0;
   tip1  = "";
   tip2  = "";
   tip3  = "";
   tip4  = "";
   sw    = 0;

   swlin  = 0;
   &swinf = 0;
   &tex   = "";
   &tex2  = "";
   &tex3  = "";
   swTipCta = 0;

   nDMod = 0;
   nHMod = 0;
|FIN;

|INCLUYE dscont_i;

|CALCULO AntesFinan; |TIPO 7;
 |C_M #0#5,1,"S";
 |SI #0#6 > 0; |O #0#8 = "S";
     |C_M #0#5,1, "N"; #0#5 = "N"; |PINTA #0#5;
 |FINSI;
|FCAL;

|CALCULO AntesReg; |TIPO 7;
 |C_M #0#8,1,"S";
 |SI #0#6 > 0;
     |C_M #0#8,1, "N"; #0#8 = "N"; |PINTA #0#8;
 |FINSI;
|FCAL;

|PROCESO NomCta;
  aAlfa1 = #2#2; |QBF aAlfa1;
  aAlfa2 = aAlfa1 % 101;
  |SI aAlfa2 ! "C"; |ACABA; |FINSI;
  aAlfa3 = aAlfa1 % 201; nNume1 = aAlfa3

  |SI #1#12 = "S";
      |SI nNume1 = 1; aAlfa1 = "Cuenta Proveedor de Compra del Bien "; |FINSI;
      |SI nNume1 = 2; aAlfa1 = "Cta Prov.Inmovilizado a Largo Plazo "; |FINSI;
      |SI nNume1 = 3; aAlfa1 = "Cta Prov.Inmovilizado a Corto Plazo "; |FINSI;
      |SI nNume1 = 4; aAlfa1 = "Cuenta Cargo de Inmovilizado        "; |FINSI;
      |SI nNume1 = 5; aAlfa1 = "Cuenta Cargo de Intereses Diferidos "; |FINSI;
      |SI nNume1 = 6; aAlfa1 = "Cta Cargo de IVA Soportado Diferido "; |FINSI;
      |SI nNume1 = 7; aAlfa1 = "Cuenta Gastos de Intereses          "; |FINSI;
  |SINO;
      |SI nNume1 = 1;
          |SI #1#25 = "R"; aAlfa1 = "Cuenta Cliente   "; |FINSI;
          |SI #1#25 = "S"; aAlfa1 = "Cuenta Proveedor "; |FINSI;
          |SI #1#25 = "N"; aAlfa1 = "Cuenta Titular   "; |FINSI;
      |FINSI;
      |SI nNume1 = 2; aAlfa1 = "Cuenta Contrapartida "; |FINSI;
      |SI nNume1 = 3; aAlfa1 = "Cuenta I.V.A.        "; |FINSI;
      |SI nNume1 = 4; aAlfa1 = "Cuenta Recargo       "; |FINSI;
      |SI nNume1 = 5; aAlfa1 = "Cuenta I.R.P.F.      "; |FINSI;
      |SI nNume1 = 6; aAlfa1 = "Cuenta Descuentos    "; |FINSI;
  |FINSI;
  #3#2 =  aAlfa1;
|FPRC;

|PROCESO imprime2;
  swTipCta = 0;
  aAlfa1 = "                                    ";
  |SI #1#12 = "S"; |O #1#24 = "S";
      aAlfa1 = #2#2; |QBF aAlfa1;
      aAlfa2 = aAlfa1 % 101;
      |SI aAlfa2 = "C"; swTipCta = 1; |FINSI;
  |FINSI;
  |SI swTipCta = 0;
      #3#0 = #2#2;
      |LEE 001#3=;
      |SI FSalida ! 0;
         #3#2 = "NO EXISTE CUENTA";
      |FINSI;
  |SINO;
      |HAZ NomCta;
  |FINSI;

   tex2 = "";
   |SI #camautol#7 = "D"; tex2 = "al DEBE     "; |FINSI;
   |SI #camautol#7 = "H"; tex2 = "al HABER    "; |FINSI;
   |SI #camautol#7 = "S"; tex2 = "segun SALDO "; |FINSI;
   |SI #camautol#7 = "C"; tex2 = "CUADRE ASTO."; |FINSI;

   swinf = 1;
   |PRINT;
   swlin = 1;
|FPRC;

|PROCESO imprime1;
   #camautti#0 = #camautoc#11;
   |LEE 001#4=;
   |SI FSalida ! 0; |DDEFECTO #4; |FINSI;

   tex = "";
   |SI #1#12 = "S"; tex = "Operacion Financiera"; |FINSI;
   |SI #1#21 ! 0;   tex = "Modelo Oficial "; |FINSI;
   |SI #1#24 = "S";
       tex = "Datos de Registros ";
       |SI #1#25 = "R"; tex = tex + " de IVA Repercutido"; |FINSI;
       |SI #1#25 = "S"; tex = tex + " de IVA Soportado"; |FINSI;
       |SI #1#25 = "N"; tex = tex + " de I.R.P.F."; |FINSI;
   |FINSI;
   swinf = 0;
   |PRINT;
   swlin = 0;
   |BUCLELIN 2imprime2#1;
   |SI swlin = 1;
      swinf = 2;
      |PRINT;
   |FINSI;
   sw = 1;
|FPRC;

|DEFBUCLE lectura1;
   #1#1;
   11, 12, 21, 24;
   #0#0, #0#3, tip1, nDMod, tip3;
   #0#1, #0#4, tip2, nHMod, tip4;
   e;
   NULL, imprime1;
|FIN;

|CALCULO impresion; |TIPO 3;
   nDMod = #0#6;
   nHMod = #0#7;
   |IMPRE 0;
   |SI FSalida ! 0; |ACABA; |FINSI;

   |SI #48#27 = "S";
       |HAZ MultiEmpresa;
   |SINO;
       |DFICE dirfich0; |QBF dirfich0;
       |HAZ HazTodo;
   |FINSI;
   |FINIMP;
|FCAL;

|PROCESO HazTodo;

   |HAZ Analitica;
   |INFOR "camautin";
   |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

   tip1 = #0#5; tip2 = #0#5;
   |SI #0#5 = "T"; tip1 = " "; tip2 = "z"; |FINSI;
   tip3 = #0#8; tip4 = #0#8;
   |SI #0#8 = "T"; tip3 = " "; tip4 = "z"; |FINSI;

   |ABRE #3;
   |ABRE #4;
   sw = 0;
   |BUCLE lectura1;
   |CIERRA #3;
   |CIERRA #4;

   |SI sw = 1;
      |PIEINF;
   |FINSI;
   |FININF;
|FPRC;

|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #2 dirfich0;
   |PATH_DAT #3 dirfich0;
   |PATH_DAT #4 dirfich0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 001;
 999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;
