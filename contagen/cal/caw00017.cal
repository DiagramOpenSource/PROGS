|FICHEROS;
   caw00017 #00;        || apuntes lins. extras

   cafantas #02;        || def para 'PINTA' de acumulados diario
   :/modelos/def/dsmom030 #03;        || conceptos
   cacuenta #04;        || cuentas
   camandia #06;        || diarios
   calicont #31;        || Contrapartidas
|FIN;

|VARIABLES;
  &enSwActual = 0;
  &nSwHay17 = 0;
  &en0W17   = 0;
  &ef1W17   = @;
  &en2W17   = 0;
  enSaldo  = 0;
  enDebe   = 0;
  enHaber  = 0;
  &eaFitW17 = "";
  &p_cen      = 0;
  ModoEntrada = 0;
  nInkey      = 0;

  &coant      = 0;
  &deant      = "";
  &dhant      = "";
  &imant      = 0;
  &dpant      = "";
  &sdant      = "";
  &refant     = "";
  cuant       = "";
  diant       = 0;
  trant       = "";
  drant       = 0;

  alfa1      = "";
  alfa2      = "";
  alfa3      = "";
  alfa4      = "";
  nume1      = 0;
  nume2      = 0;
  nume3      = 0;
  nume4      = 0;

  swCambiar = 0;
   cta_t = 0;
   dig_t = 0;
   &ext1 = "";
   &ext2 = "";
   &ext3 = "";
   &ext4 = "";
   &ext5 = "";

   &enDiarioDentro    = 0;
   &efFechaDentro     = @;
   &enDocumentoDentro = 0;
   &r_entra = "";
   &r_alfa1 = "";
   &avisos = "";
   ctdeant = "";
   dideant = 0;
   cthaant = "";
   dihaant = 0;
   sumoresto = 0;
   aponer = "  ";
   ctpre   = "";
   dipre   = 0;
   sit1 = "";
   sit2 = "";
   sit3 = "";
   sit4 = "";
   sit5 = 0;
   sit6 = 0;
   sit7 = "";
   programa = "";

   fiac = "  ";
   guarda = "  ";
   relleno = "                                       ";
   aBlancs = "";
   &entrada = 1;
   &snFilBa = "";
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoan_i;

|PROCESO refresca; |TIPO 2;        || calculos de (des)actualizacion
   ModoEntrada = (FEntrada / 100) ! 0;

   |SI p_cen = 0;
       |HAZ a_asiento;     || acumulados asiento

       MasMenos     = 0 +. #0#9;
       DebeHaber    = #0#8;
       aCUENTA      = #0#4;
       nREGISTRO    = #0#24;
       nDIARIO      = #0#0;
       aFECHA       = #0#1;
       nDOCUMENTO   = #0#2;
       nNIVELCUENTA = #0#5;
|SI enSwActual = 0;
       |HAZ ActDiario;          || diario
       |HAZ ActCuenta;          || cuenta
       |HAZ ActTesteo;          || cuenta
|FINSI;
       |HAZ pinta_cta;     || pinta los datos de la cuenta
       |HAZ PintaTotal;
   |FINSI;
|FPRC;

|PROCESO saalta; |TIPO 0;               || desde campo 'linea'

   ModoEntrada = (FEntrada / 100) ! 0;
   nInkey = FSalida;
   |SI ModoEntrada = 1; |Y #0#3 ! 1;
       |SI coant ! 0;
           #0#6 = coant;
           #0#8 = dhant;
       |FINSI;
       |PINTA #0#6;
   |FINSI;

   |SI ModoEntrada = 1;
       #0#21 = dpant; |PINTA #0#21;
       #0#28 = sdant; |PINTA #0#28;
       #0#26 = refant;
   |FINSI;
|FPRC;

|PROCESO ElCambio;
   swCambiar = 0;

   |SI #0#8 = "D";
       #0#8 = "H";
       #0#10 = #0#16;
       #0#11 = #0#17;
       #0#16 = "";
       #0#17 = 0;
   |SINO;
       #0#8 = "D";
       #0#16 = #0#10;
       #0#17 = #0#11;
       #0#10 = "";
       #0#11 = 0;
   |FINSI;
   |PINTA #0#16;
   |PINTA #0#10;
   |PTEC 806;
|FPRC;

|PROCESO diez;
   ModoEntrada = (FEntrada / 100) ! 0;
   |SI ModoEntrada = 1; |Y FSalida ! 1; |Y FSalida ! 7; |Y #0#3 ! 1;
       #0#3 = #0#3 + 9;
       |PINTA #0#3;
   |FINSI;
|FPRC;

|PROCESO entmod; |TIPO 1;
   coant = #0#6;
   deant = #0#7;
   ModoEntrada = (FEntrada / 100) ! 0;
   |SI ModoEntrada = 3;
       |ABRE #31;
       #calicont#0 = #0#0;
       #calicont#1 = #0#1;
       #calicont#2 = #0#2;
       #calicont#3 = #0#3;
       |LEE 101#31=;
       |SI FSalida = 0;
          |BORRA 020#31a;
       |FINSI;
       |LIBERA #31;
       |CIERRA #31;
   |FINSI;
|FPRC;


|PROCESO guarda; |TIPO 3;
   #0#0 = en0W17;
   #0#1 = ef1W17;
   #0#2 = en2W17;

   ModoEntrada = (FEntrada / 100) ! 0;

   alfa1 = #0#16;   |QBF alfa1;
   alfa2 = #0#10;   |QBF alfa2;
   |SI alfa1 = "";  |Y alfa2 = "";    ||al menos una cuenta cargada;
       avisos = "ATENCION!!! Apunte sin cuentas ...";
       |HAZ aviso5;
       |PAUSA;
       |HAZ aviso9;
       |ERROR;
       |ACABA;
   |FINSI;
   |SI #0#9 = 0;                       || importe distinto de cero
       avisos = "ATENCION!!! Importe cero ...";
       |HAZ aviso5;
       |PAUSA;
       |HAZ aviso9;
       |ERROR;
       |ACABA;
   |FINSI;
   |SI alfa1 ! "";
       ctdeant = #0#16;
       dideant = #0#17;
   |FINSI;
   |SI alfa2 ! "";
       cthaant = #0#10;
       dihaant = #0#11;
   |FINSI;
   cuant = #0#16;
   diant = #0#17;
   coant = #0#6;
   deant = #0#7;
   dhant = #0#8;
   imant = #0#9;
   trant = #0#10;
   drant = #0#11;
   dpant = #0#21;
   sdant = #0#28;
   refant = #0#26;

|| =========================================================================
   |SI #0#16 ! doce; |Y #0#10 ! doce;        || provoca otro apunte
||       |MENAV "    Error. Mas de una Cuenta ";
||       |ERROR;
||       |ACABA;

      #0#4 = #0#16;         || cuenta DEBE
      #0#5 = #0#17;         || digito DEBE
      #0#8 = "D";           || signo DEBE
      #0#10 = "";           || vacia cuenta HABER
      #0#11 = 0;            || vacia digito HABER
      |HAZ refresca;

      |GRABA 020#0a;        || graba el actual
      |LIBERA #0;

      |SI #0#3 < 9990;
          #0#3 = #0#3 + 10;     || aumenta el apunte
      |SINO;
          #0#3 = #0#3 + 1;      || aumenta el apunte
      |FINSI;

      #0#4  = trant;        || cuenta HABER
      #0#5  = drant;        || digito HABER
      #0#16 = "";           || vacia cuenta DEBE
      #0#17 = 0;            || vacia digito DEBE
      #0#6 = coant;         || concepto
      #0#7 = deant;         || descripcion
      #0#8 = "H";           || signo
      #0#9 = imant;         || importe
      #0#10 = trant;        || cuenta HABER
      #0#11 = drant;        || digito HABER
      #0#21 = dpant;        || departamento
      #0#28 = sdant;        || subdepartamento
      #0#26 = refant;        || referencia anterior

     |GRABA 020#0n;        || graba el provocado

     |PONCONTROL 23, "SI";

     |LEE 020#0=;
     aponer = "kksi";
     |HAZ poncontr;
     Pos = -1;
     |PTEC 809;
     |PTEC 809;
     |PTEC 808;
     |PTEC 808;

   |FINSI;
|| =========================================================================

   |SI sumoresto = 1;
       aponer = "kksi";
       |HAZ poncontr;
       Pos = -1;
       sumoresto = 0;
       |PTEC 809;
       |PTEC 809;
   |FINSI;
   ctpre = #0#4;
   dipre = #0#5;
|FPRC;

|PROCESO funcion1; |TIPO 11;                  || desde campo 'linea'
  nInkey = FSalida;

  |SI nInkey = 1; |O nInkey = 7;
      |SI enSaldo ! 0;
          alfa1 = "    NAVISO !!! Asiento Descuadrado. Desea Salir S/N ";
          |CONFI alfa1;
          |SI FSalida ! 0;
              |ERROR;
          |FINSI;
          nInkey = 0;
          |ACABA;
      |FINSI;
  |FINSI;

  ModoEntrada = (FEntrada / 100) ! 0;
  |SI ModoEntrada ! 1;
      |HAZ funcion2;
  |FINSI;
|FPRC;

|PROCESO funcion2;                           || desde calculo 'funcion1'
   |SI nInkey ] 0;  |Y nInkey [ 8;
       |ACABA;
   |FINSI;

   |SI nInkey ! 9;  |Y nInkey ! 10; |Y nInkey ! 11;
    |Y nInkey ! 12; |Y nInkey ! 15; |Y nInkey ! 17;
      |AVISO;
      |ERROR;
      |ACABA;
   |FINSI;

   |SI nInkey = 9;            || extractos
       ext1 = #0#4;          || cuenta
       ext2 = #0#5;          || digito
       programa = "caextrap";
       |HAZ externos;
       |ERROR;
       |ACABA;
   |FINSI;

   |SI nInkey = 10;               || diario de trabajo
       programa = "caditrab";
       |HAZ externos;
       |ERROR;
       |ACABA;
   |FINSI;

   |SI nInkey = 11;               || anotaciones extracontables
       ext1 = #0#0;          || diario
       ext2 = #0#1;          || fecha
       ext3 = #0#2;          || documento
       programa = "caextra0.run";

       enDiarioEx = #0#0;
       efFechaEx  = #0#1;
       enDocumEx  = #0#2;
       eaLaCtaEx  = #0#4;
       eaCtaDeEx  = #cacuenta#2;
       enConNuEx  = #0#6;
       eaConDeEx  = #0#7;
       enTipoEx   = 0;
       programa = "caextmz1";
       |HAZ externos;
       |ERROR;
       |ACABA;
   |FINSI;

   |SI nInkey = 12;                  || Busqueda de Apuntes
||        programa = "cabusapu";
||       |HAZ externos;
||       |ERROR;
||       |ACABA;
   |FINSI;

   |SI nInkey = 15;
       |ERROR;
       |PTEC 802;
       |PTEC 802;
       swCambiar = 1;
   |FINSI;
|FPRC;

|| ======================================================================
|PROCESO cuefin; |TIPO 0;                    || desde campo 'cuenta'
   |HAZ signo;
   |SI nInkey = 16;
       |PTEC 807;
       |ERROR;
   |SINO;
       cta_t = Campo;
       dig_t = cta_t + 1;
       |HAZ cuefin_t;
       |HAZ funcion2;
   |FINSI;
|FPRC;

|PROCESO otravez;
   emCta = 0;
   cta_t = Campo - 1;
   dig_t = Campo;
   eaCuenta = #0cta_t;
   |HAZ SacaNivel;
   #0dig_t = enNivel;
|FPRC;

|PROCESO cuefin_t;                      || desde calculo 'cuefin'
   |ABRET #4;
   #cacuenta#0 = #0cta_t;
   #cacuenta#1 = #0dig_t;
   |LEE 001#4=;
   |SI FSalida ! 0;
      |ERROR;
      |CIERRAT #4;
      |ACABA;
   |FINSI;
   |CIERRAT #4;
   |SI #cacuenta#3 = "N"; |Y #cacuenta#0 ! doce;
      |MENAV "    Error. Cuenta sin Pase Directo";
      |ERROR;
      |ACABA;
   |FINSI;
   |HAZ pinta_cta;
|FPRC;

|PROCESO cuenta; |TIPO 6;                    || desde campo 'cuenta'
   nInkey = FSalida;
   |SI nInkey = 15;
      |SI Campo = 10;
         #0#10 = cthaant;
         #0#11 = dihaant;
      |FINSI;
      |SI Campo = 16;
         #0#16 = ctdeant;
         #0#17 = dideant;
      |FINSI;
      |PINTA #0Campo;
      nInkey = 0;
   |FINSI;

   eaCuenta = #0Campo;
   salidas  = FSalida;
   |HAZ FiltraPuntos;
   #0Campo = eaCuenta;
   |PINTA #0Campo;

   cta_t = Campo;
   dig_t = cta_t + 1;
   |HAZ blanco;

   emCta = 1;
   eaCuenta = #0cta_t;
   |HAZ SacaNivel;
   #0dig_t = enNivel;
|FPRC;

|PROCESO blanco;              || desde calculo 'cuenta'
   alfa1 = #0cta_t;   |QBF alfa1;
   |SI alfa1 = "";
      |ABRET #4;
      #cacuenta#0 = #0cta_t;
      #cacuenta#1 = #0dig_t;
      |LEE 001#4=;
      |SI FSalida ! 0;
         |DDEFECTO #4;
         #cacuenta#3 = "S";       || pase directo si
         |GRABA 040#4n;
      |FINSI;
      #cacuenta#3 = "S";       || pase directo si
      |GRABA 040#4a;
      |CIERRAT #4;
   |FINSI;                  || graba automaticamente las cuentas en blanco
|FPRC;

|PROCESO concepto; |TIPO 0;             || desde campo 'concepto'
   nInkey = FSalida;

   |SI nInkey = 10;
       enConcepto = #0#6;
       |HAZ VerConcepto30;
       #0#6 = enConcepto; |PINTA #0#6;
   |FINSI;

   enFilConc = 1;
   ModoEntrada = (FEntrada / 100) ! 0;
   |SI #0#8 = "D"; |O #0#8 = "x";
       eaCuenta   = #0#16;
       enConcepto  = #0#6;
       |HAZ FiltroConcepto;  |SI eswLect ! 0; |ACABA; |FINSI;
   |FINSI;
   |SI #0#8 = "H"; |O #0#8 = "x";
       eaCuenta   = #0#10;
       enConcepto  = #0#6;
       |HAZ FiltroConcepto;  |SI eswLect ! 0; |ACABA; |FINSI;
   |FINSI;
   |SI ModoEntrada = 1;
       #0#7 = #dsmom030#1;
   |SINO;
       |SI #0#6 = coant;
           #0#7 = deant;
      |SINO;
          #0#7 = #dsmom030#1;
      |FINSI;
   |FINSI;
   |PINTA #0#7;
|FPRC;

|PROCESO descrip; |TIPO 0;        || desde campo 'descripcion'
   nInkey = FSalida;
   |SI FSalida = 15;
      #0#7 = deant;
      |PINTA #0#7;
   |FINSI;
|FPRC;

|PROCESO nocero; |TIPO 0;          || desde campo 'importe'
   nInkey = FSalida;
   swCambiar = 0;

   |HAZ aviso9;
   |SI #0Campo = 0;|Y nInkey ! 11;
       |ERROR;
       |ACABA;
   |SINO;
      |SI nInkey = 11;
         |SI #0#8 = "D";  #0#9 = enSaldo * -1;  |FINSI;
         |SI #0#8 = "H";  #0#9 = enSaldo;       |FINSI;
         |PINTA #0#9;
         |ERROR;
         |PTEC 802;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO signo;                    || desde calculo 'c_importe' y 'cuefin'
   |SI #0#16 ! doce;
       #0#8 = "D";
       #0#4 = #0#16;
       #0#5 = #0#17;
   |FINSI;
   |SI #0#10 ! doce;
       #0#8 = "H";
       #0#4 = #0#10;
       #0#5 = #0#11;
   |FINSI;
   |SI #0#16 ! doce;|Y #0#10 ! doce;
       #0#8 = "x";
       #0#4 = "";
       #0#5 = 0;
   |FINSI;
|FPRC;

****************************************************************************
RUTINAS VARIAS
****************************************************************************
|PROCESO a_asiento;                || calculo 'refresca'
   |SI #0#8 = "D";|O #0#8 = "x";  enDebe = enDebe +. #0#9;  |FINSI;
   |SI #0#8 = "H";|O #0#8 = "x";  enHaber = enHaber +. #0#9;  |FINSI;
   enSaldo = enDebe - enHaber;
|FPRC;

|PROCESO pinta_cta; |TIPO 13;      || desde 'refresca', 'cuefin_t', CONSULTA

   |SI #0#16 ! doce;
       cta_t = 16;
       |HAZ pinta_cta_t;
   |SINO;
      |SI #0#10 ! doce;
         cta_t = 10;
         |HAZ pinta_cta_t;
      |SINO;
         |DDEFECTO #4;
      |FINSI;
   |FINSI;
   |ATRI I;
   |PINTA 2102, #cacuenta#2;
   |PINTA 2139, #cacuenta#51;
   |PINTA 2153, #cacuenta#52;
   |PINTA 2167, #cacuenta#53;
   |ATRI 0;

   aBlancs = " " * 78;
   |PINTA 2202, aBlancs; |PINTA 2302, aBlancs;

   ModoEntrada = (FEntrada / 100) ! 0; |SI ModoEntrada = 1; |ACABA; |FINSI;
   |SI eaDepar = "S";
        eOpDep = 0;
        enActividad = #0#21;
        eaCodDep    = #0#21;
        |HAZ Actividad;
        |HAZ pDepartamento;
   |FINSI;
   |SI eaSubde = "S";
       eOpSub = 0;
       enConcepto  = #0#6;
       eaCodSubdep = #0#28;
       |HAZ SubActividad;
       |HAZ pSubdepar;
   |FINSI;

   |HAZ PintaTotal;
|FPRC;

|PROCESO PintaTotal;
    #cafantas#12 = enDebe;
    #cafantas#13 = enHaber;
    #cafantas#14 = enDebe - enHaber;
    enSaldo     = (enDebe - enHaber) ! EURODB;
    |ATRI P;
    |PINTA 1328, #cafantas#12;
    |PINTA 1348, #cafantas#13;
    |PINTA 1368, #cafantas#14;
    |ATRI 0;
|FPRC;

|PROCESO pinta_cta_t;              || rutina 'pinta_cta'
   dig_t = cta_t + 1;
   |ABRET #4;
   #cacuenta#0 = #0cta_t;
   #cacuenta#1 = #0dig_t;
   |LEE 001#4=;
   |SI FSalida ! 0;  |DDEFECTO #4;  |FINSI;
   |CIERRAT #4;
|FPRC;

|PROCESO poncontr;                 || calculo 'guarda'
   fiac = Control % A109;
   fiac = fiac + relleno;
   guarda = fiac % A109;
   fiac = Control % A1001;
   Control = Control + fiac;
   Control = Control + relleno;
   Control = Control % A120;
   Control = Control + aponer;
|FPRC;

|PROCESO externos;                      || desde calculo 'cuefin'
   enDiarioDentro    = #0#0;
   efFechaDentro     = #0#1;
   enDocumentoDentro = #0#2;

   |ATRI P;
   |MENSA "    Cargando Opcion ...";
   |ATRI 0;
   sit1 = #0#24;
   sit2 = #0#1;         || fecha
   sit3 = #0#2;         || documento        / las guarda para la vuelta
   sit4 = #0#3;         || apunte
   sit5 = enDebe;         || debe asiento
   sit6 = enHaber;         || haber asiento
   sit7 = #0#26;

   |LIBERA #0;
   |CIERRAT #0;        || cierra lins.
   |SUB_EJECUTA_NP programa;

   |ABRET #0;          || abre lins.
   #0#24 = sit1;
   #0#3 = sit4;         || apunte
   |LEE 101#0=;        || lee el apunte
   enDebe = sit5;         || debe asiento
   enHaber = sit6;         || haber asiento
   enSaldo = enDebe - enHaber;  || saldo asiento
   #0#26 = sit7;
|FPRC;

****************************************************************************
CALCULO PARA ASIENTOS PREDEFINIDOS
****************************************************************************
||........... ATENCION IVA 09.01.2001
|PROCESO c_empieza;      || desde 'c_apunte'
|FPRC;

|PROCESO lee_predef;
|FPRC;

|PROCESO c_apunte; |TIPO 7;             || desde campo 'linea'

   ModoEntrada = (FEntrada / 100) ! 0;
   |SI ModoEntrada = 1;
       |MENSA "    F12-Extracto;F2-Diario;F3-Anotaciones;";
   |SINO;
       |MENSA "    F12-Extracto;F2-Diario;F3-Anotaciones;F7-Cambio D/H;";
   |FINSI;

   |HAZ diez;
|FPRC;

|PROCESO c_cuenta; |TIPO 7;
  |SI swCambiar = 1; |HAZ ElCambio; |ACABA; |FINSI;

   ModoEntrada = (FEntrada / 100) ! 0;
   |CAMPO_MODIFICA #0#10, 1, "S";
   |CAMPO_MODIFICA #0#16, 1, "S";
   |SI ModoEntrada ! 1;
       |SI #0#10 ! doce; |O #0#16 ! doce;
           |SI Campo=10; |Y #0#16!doce; |CAMPO_MODIFICA #0#10,1,"N"; |FINSI;
           |SI Campo=16; |Y #0#10!doce; |CAMPO_MODIFICA #0#16,1,"N"; |FINSI;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO c_concepto; |TIPO 7;
|FPRC;

|PROCESO c_descrip; |TIPO 7;
   |SI eaSobreEs ! "S";
       |PTEC 816;
   |FINSI;
|FPRC;

|PROCESO c_departa; |TIPO 7;
   |CAMPO_MODIFICA #0#21, 1, eaDepar;
   |SI eaDepar  = "S";
      |SI Haybasica = 1;
          |MENSA "    <F2> Consultar Actividades";
      |SINO;
          |MENSA "    <F2> Consultar Departamentos";
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO c_subdepar; |TIPO 7;
   |CAMPO_MODIFICA #0#28, 1, eaSubde;

   |SI eaSubde  = "S";
      |SI Haybasica = 1;
          |SI enArrend ! 2;
              |MENSA "    Subdepartamento. <F2> Consultar Referencias Catastrales";
          |SINO;
              |MENSA "    Subdepartamento. <F2> Consultar Cultivos";
          |FINSI;
      |FINSI;
   |FINSI;
|FPRC;

|| ================= desde  Aqui
|PROCESO pActividad; |TIPO 0;
   |SI eaDepar = "N"; |ACABA; |FINSI;

   aBlancs = " "* 78; |PINTA 2202, aBlancs;

   eOpDep = 0; || 1 = Consulta pantalla
   |SI FSalida = 10;  eOpDep = 1; |FINSI;
   enActividad = #0#21;
   eaCodDep    = #0#21;
   |HAZ Actividad;
   |SI eaCodDep = ""; eswLect = 0; |FINSI;
   |SI eswLect = 1;
       |MENAV "    Error.! No Existe Actividad / Departamento";
       |ERROR;
   |SINO;
       #0#21 = eaCodDep; |PINTA #0#21;
       |HAZ pDepartamento;
       |SI efActFin > "01.01.1900"; |Y snFilBa ! "N";
           alfa1 = "    NAtencion!!!. Actividad con Fecha de BAJA " + efActFin + ", Desea introducir Facturas ? ";
           |CONFI alfa1;
           |SI FSalida ! 0;
              |ERROR;
              |ACABA;
           |FINSI;
       |FINSI;
       |SI eaEsCom = "S";
           |CONFI "    NAtencion!!!. Actividad Comunera, ¨ Desea introducir Movimientos ? ";
           |SI FSalida ! 0;
               |ERROR;
           |FINSI;
       |FINSI;
   |FINSI;
|FPRC;

|PROCESO pSubActividad; |TIPO 0;
   |SI eaSubde = "N"; |ACABA; |FINSI;

   eOpSub = 0; || 1 = Consulta pantalla
   |SI FSalida = 10; eOpSub = 1; |FINSI;

   aBlancs = " " * 78; |PINTA 2302, aBlancs;

   enConcepto  = #0#6;
   eaCodSubdep = #0#28;
   enQue = 1;
   |HAZ SubActividad;
   enQue = 0;
   |SI eswLect = 1;
       |MENAV "    Error.! No Existe SubDepartamento";
       |ERROR;
   |SINO;
       #0#28 = eaCodSubdep; |PINTA #0#28;
       |HAZ pSubdepar;
   |FINSI;
|FPRC;

|| ================= Hasta aqui

|PROCESO c_importe; |TIPO 7;
   |HAZ signo;
   ModoEntrada = (FEntrada / 100) ! 0;
   |SI ModoEntrada = 1;
      #0#9 = imant;
      |PINTA #0#9;
   |FINSI;
|FPRC;

|PROCESO c_ref; |TIPO 7;
  ModoEntrada = (FEntrada / 100) ! 0;
  |SI ModoEntrada = 1;
      #0#26 = refant;
      |PINTA #0#26;
  |FINSI;
  |C_M #0Campo,0,alfa1; |SI alfa1 = "N"; |ACABA; |FINSI;
|FPRC;

|PROCESO p_ref; |TIPO 0;
  |C_M #0Campo,0,alfa1;
  |SI alfa1 = "S";
      |SI nInkey = 15;
          #0#26 = refant;
          |PINTA #0#26;
          refant = #0Campo;
          |ACABA;
      |FINSI;
      r_entra = entrada;
      r_alfa1 = #0Campo;
      |HAZ r1_punto;
      #0Campo = r_alfa1;
      |PINTA #0Campo;
  |FINSI;
  refant = #0Campo;
|FPRC;

|| ********************************************************************
|| ********************************************************************

|RUTINA InfW17;
 #0#0 = en0W17;
 #0#1 = ef1W17;
 #0#2 = en2W17;
 #0#3 = 0001;
|FRUT;

|RUTINA SupW17;
 #0#0 = en0W17;
 #0#1 = ef1W17;
 #0#2 = en2W17;
 #0#3 = 9999;
|FRUT;

|PROCESO LosDeAntes;
   cuant = #0#16;
   diant = #0#17;
   coant = #0#6;
   deant = #0#7;
   dhant = #0#8;
   imant = #0#9;
   trant = #0#10;
   drant = #0#11;
   dpant = #0#21;
   sdant = #0#28;
   refant = #0#26;
   |SI #0#8 = "D"; enDebe  = enDebe + #0#9; |FINSI;
   |SI #0#8 = "H"; enHaber = enHaber+ #0#9; |FINSI;
|FPRC;

|DEFBUCLE LosDeAntes;
 #0#1;
 ;
 en0W17, ef1W17, en2W17, 0000;
 en0W17, ef1W17, en2W17, 9999;
 ;
 NULL, LosDeAntes;
|FIN;


|PROGRAMA;
||  |HAZ inicio;


 |NOME_DAT #0 eaFitW17;
 enDebe = 0;
 enHaber= 0;
 |BUCLE LosDeAntes;
 enSaldo = enDebe - enHaber;
 |PINPA #0#0;
 |ENTLINEAL #1#0, -4, 2, 19, 0, InfW17, SupW17;
 nSwHay17 = 1;
|FPRO;


|PROCESO aviso5;         || desde calculo 'c_importe' (camaasil)
   |PUSHV 09101171;
   |HAZ color_f;
   |CUADRO 09 10 11 71;
   ||            123456789012345678901234567890123456789012345678901234567890
   |HAZ color_c;

   |QBF avisos;
   alfa1 = avisos % 0;  nume1 = alfa1;
   nume2 = ((60 - nume1) / 2) ! 0;     nume1 = 60 - nume1 - nume2;
   avisos = (" " * nume1) + avisos + (" " * nume2);        || lo centra

   |PINTA 1011, avisos;
   |ATRI 0;
   |AVISO;
|FPRC;

|PROCESO aviso9;         || desde calculo 'descuadre', 'modbaj'
   |ATRI 0;
   |POPV;
|FPRC;

|PROCESO color_c;        || activa el color caracter en avisos
   |COLOR 14, 0;
   |ATRI 0;
|FPRC;

|PROCESO color_f;        || activa el color graficos en avisos
   |COLOR 4, 0;
   |ATRI 0;
|FPRC;
