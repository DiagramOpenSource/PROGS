|FICHEROS;
   caz00007 #0;

   camaasil #1;    || apuntes lins
   camaasic #3;
   anm00001 #2;

   camaniva #10;
   caivalin #11;
   caivaasi #200;

   caw00015 #900,MANTE;
   canempre #30;
|FIN;

|VARIABLES;

   aFichero  = "";
   aAlfa1    = "";
   aAlfa2    = "";
   sw        = 0;
   swc       = 0;
   nElDoc    = 0;

   mensa1 = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";

   enQSerie = 0;
   tipoRS   = "";
   aDTipoRS = "";
   aHTipoRS = "";
   para1 = 0;
   nume1 = 0;
   nume2 = 0;
   nume3 = 0;
   aTitCuenta = "";
   enBorraAu = 0;

   nSwGraba = 0;
   nDLinea  = 0;
   nHLinea  = 0;
   &snFiltr = "";
   &snFilAc = "";
   &snFilBa = "";
|FIN;

|INCLUYE dscont_i;

____________________________________/ CALCULOS DESDE PANTALLA

|CALCULO eInic8; |TIPO 8;
 |HAZ inicio;
 |HAZ DirAplicSt;
 |HAZ Analitica;
 |ABRE #200;
 |DDEFECTO #200;
 |LEE 010#200p;
 enQSerie = 0;
 |SI #200#44 = "S"; enQSerie = 1;|FINSI;
 snFilBa = #200#119;
 |CIERRA #200;
|FCAL;

|CALCULO def_fec; |TIPO 7;
   |SI sw = 0;
      #0#2 = fec1;    |PINTA #0#2;
      #0#3 = fec2;    |PINTA #0#3;
      sw = 1;
   |FINSI;
|FCAL;

|CALCULO fecha; | TIPO 0;   || Desde/hasta fecha
   |SI #0Campo < fec1; |O #0Campo > fec2;
      |MENAV mensa1;
      |ERROR;
   |FINSI;
|FCAL;

|CALCULO diarioh; | TIPO 0;   ||  hasta diario
   |SI #0#1 < #0#0;  |ERROR;  |FINSI;
|FCAL;

|CALCULO fechah; | TIPO 0;   ||  hasta fecha
   |SI #0#3 < #0#2;  |ERROR;  |FINSI;
|FCAL;

|CALCULO documh; | TIPO 0;   ||  hasta documento
   |SI #0#5 < #0#4;  |ERROR;  |FINSI;
|FCAL;

|CALCULO Ehque; |TIPO 7;
 |SI enQSerie = 0;
     |C_M #0#13,1,"N"; #0#13 = "";      |PINTA #0#13;
     |C_M #0#14,1,"N"; #0#14 = "zzzzz"; |PINTA #0#14;
 |SINO;
     |C_M #0#13,1,"S";
     |C_M #0#14,1,"S";
 |FINSI;
|FCAL;

|| *************************************************************************
|| *************************************************************************
|| ///////////////////////////////////////////////////////////
|| ...... .... ..... ..... Grabar por factura
|PROCESO ComprueboI;
 nSwGraba = 0;
 |SI enSwAna = 1; |ACABA; |FINSI;

 |SELECT #3#900;
 #900#13 = #10#0;
 #900#16 = #10#1;
 |LEE 001#900=;
 |SI FSalida = 0;
     nSwGraba = 1;
 |FINSI;
 |SELECT #1#900;
|FPRC;

|| ...... .... ..... ..... Grabar por factura
|PROCESO GrabaAuxFra1;

       |DDEFECTO #900;
       |HAZ ComprueboI;
       |SI nSwGraba = 1; |ACABA; |FINSI;

       nElDoc = nElDoc + 1;
       #900#0 = nElDoc;
       #900#1 = #10#7;
       #900#2 = #10#8;
       #900#3 = #10#9;
       #900#4 = 0000;
       aAlfa1 = #10#8;
       #900#5 = aAlfa1 % 105;

       aAlfa1 = #11#30; |QBF aAlfa1; |SI aAlfa1 = ""; #11#30 = #10#10; |FINSI;
       #900#6 = #11#30;  || Departamento

       aAlfa1 = #11#31; |QBF aAlfa1; |SI aAlfa1 = ""; #11#31 = #10#11; |FINSI;
       #900#7 = #11#31;  || subdepartamento

       #900#8 = #11#28;  || Cuenta Analitica
       #900#23 = #10#22;
       #900#9 = "S";

       #900#10 = #10#28;
       #900#11 = #10#27;
       #900#12 = #10#36;
       #900#13 = #10#0;
       #900#14 = #10#3;
       #900#15 = #10#2;
       #900#16 = #10#1;
       #900#17 = "B";
       #900#18 = #11#02;

       #900#24 = #11#30;  || Departamento
       #900#25 = #11#31;  || subdepartamento

       #900#26 = #11#28;  || Cuenta Analitica
       #900#27 = #10#22;  || Referencia

       #900#28 = #11#12;

       |GRABA 020#900n;
       swc = 1;
|FPRC;

|DEFBUCLE LasLineas;
   #11#1;
   ;
   #10#0, #10#1, 01;
   #10#0, #10#1, 99;
   e;
   NULL, GrabaAuxFra1;
|FIN;

|PROCESO GrabaAuxFra;
  |BUCLE LasLineas;
|FPRC;

|DEFBUCLE lee_facturas;
   #10#3;
   7,8,9,36;
   #0#7, #0#13, #0#9,  #0#0, #0#2, #0#4, aDTipoRS;
   #0#8, #0#14, #0#10, #0#1, #0#3, #0#5, aHTipoRS;
   e;
   NULL, GrabaAuxFra;
|FIN;

|| ***********************************************************************
|PROCESO Compruebo;
 nSwGraba = 0;
 |SI enSwAna = 1; |ACABA; |FINSI;

 |SELECT #2#900;
 #900#1 = #1#0;
 #900#2 = #1#1;
 #900#3 = #1#2;
 |LEE 001#900=;
 |SI FSalida = 0;
     nSwGraba = 1;
 |FINSI;
 |SELECT #1#900;
|FPRC;

|| ...... .... ..... ..... Grabar por Asiento
|PROCESO GrabaAuxAsto;

       |DDEFECTO #900;

       |HAZ Compruebo;
       |SI nSwGraba = 1; |ACABA; |FINSI;

       nElDoc = nElDoc + 1;
       #900#0 = nElDoc;
       #900#1 = #1#0;
       #900#2 = #1#1;
       #900#3 = #1#2;
       #900#4 = #1#3;
       aAlfa1 = #1#1;
       #900#5 = aAlfa1 % 105;

       #900#6 = #1#21;  || Departamento
       #900#7 = #1#28;  || subdepartamento
       #900#8 = #1#29;  || Cuenta Analitica
       #900#23 = #1#26;

       #900#9 = "N";
       |SI #1#19 = "F"; |O #1#19 = " "; #900#9 = "S"; |FINSI;
       |SI #1#19 = "B"; |Y enSwAna = 1; #900#9 = "S"; |FINSI;

       #900#10 = #1#7;
       #900#11 = #1#6;
       #900#12 = #1#12;
       #900#13 = #1#13;
       #900#14 = #1#14;
       #900#15 = #1#15;
       #10#0 = #1#13;
       #10#2 = #1#15;
       #10#3 = #1#14;
       |LEE 001#10=;
       |SI FSalida ! 0;
           #900#10 = "ERROR, NO EXISTE LA FACTURA";
           #900#16 = 0;
       |SINO;
           #900#16 = #10#1;
       |FINSI;
       #900#17 = #1#20;
       #900#18 = #1#19;

       #900#19 = #1#21;  || Departamento
       #900#20 = #1#28;  || subdepartamento
       #900#21 = #1#29;  || Cuenta Analitica
       #900#22 = #1#26;  || Referencia
       #900#28 = #1#4;

       |GRABA 020#900n;
       swc = 1;
|FPRC;

|DEFBUCLE lee_diario;
   #1#1;
   12,13,14,15;
   #0#0, #0#2, #0#4,    1, aDTipoRS, #0#7, #0#9,  #0#13;
   #0#1, #0#3, #0#5, 9999, aHTipoRS, #0#8, #0#10, #0#14;
   e;
   NULL, GrabaAuxAsto;
|FIN;

|| ***********************************************************************
|| ***********************************************************************
|PROCESO GrabaCabecera;
 |ABRE #3;
 #3#0 = #1#0;
 #3#1 = #1#1;
 #3#2 = #1#2;
 |LEE 101#3=;
 |SI FSalida = 0;
     |SI #3#12 ! #1#26;
         #3#12 = #1#26;
         |GRABA 020#3a;
     |FINSI;
 |FINSI;
 |LIBERA #3;
 |CIERRA #3;
|FPRC;

|PROCESO Apunte;
 nSwGraba = 0;

 |SI #1#21 ! #900#6; |Y eaDepar  = "S";
     #1#21 = #900#6;
     nSwGraba = 1;
 |FINSI;
 |SI #1#28 ! #900#7; |Y eaSubde  = "S";
     #1#28 = #900#7;
     nSwGraba = 1;
 |FINSI;

 |SI #1#26 ! #900#23;
     #1#26 = #900#23;
     |HAZ GrabaCabecera;
     nSwGraba = 1;
 |FINSI;

 |SI enSwAna = 1; |Y #900#17 = #1#20; |Y #900#18 = #1#19;
     |SI #1#29 ! #900#8;
         #1#29 = #900#8;
         nSwGraba = 1;
     |FINSI;
 |FINSI;
 |SI nSwGraba = 1; |GRABA 020#1a; |FINSI;
 |LIBERA #1;
|FPRC;

|DEFBUCLE Apunte;
 #1#1;
 ;
 #900#1,#900#2,#900#3, nDLinea;
 #900#1,#900#2,#900#3, nHLinea;
 be;
 NULL, Apunte;
|FIN;

|PROCESO CambiaAsto;
 |SI enSwAna = 0;
     nDLinea = 1;
     nHLinea = 9999;
 |SINO;
     |SI #900#4 ! 0;
         nDLinea = #900#4;
         nHLinea = #900#4;
     |FINSI;
 |FINSI;
 |BUCLE Apunte;
|FPRC;

|PROCESO CambiaIVA;
 #10#0 = #900#13;
 #10#1 = #900#16;
 |LEE 101#10=;
 |SI FSalida ! 0;
     |LIBERA #10;
     |ACABA;
 |FINSI;

 nSwGraba = 0;
 |SI #10#10 ! #900#6; |Y eaDepar  = "S";
     #10#10 = #900#6;
     eOpDep = 0;
     enActividad = #900#6;
     eaCodDep    = #900#6;
     |HAZ Actividad;
     #10#24 = eaNombreAct;
     nSwGraba = 1;
 |FINSI;
 |SI #10#11 ! #900#7; |Y eaSubde  = "S";
     #10#11 = #900#7;
     eOpSub = 0;
     enConcepto  = #900#7;
     eaCodSubdep = #900#7;
     |HAZ SubActividad;
     #10#25 = eaNomSubdep;
     nSwGraba = 1;
 |FINSI;

 |SI #10#22 ! #900#23;
     #10#22 = #900#23;
     nSwGraba = 1;
 |FINSI;

 |SI nSwGraba = 1; |GRABA 020#10a; |FINSI;
 |LIBERA #10;

 |SI enSwAna = 1;
     #11#0 = #10#0;
     #11#1 = #10#1;
     #11#2 = #900#17;
     |LEE 101#11=;
     |SI FSalida ! 0;
         |LIBERA #11;
         |ACABA;
     |FINSI;
     |SI #11#28 ! #900#8;
         #11#28 = #900#8;
         |GRABA 020#11a;
     |FINSI;
     |LIBERA #11;
 |FINSI;
|FPRC;


|PROCESO leeElAuxi;

 |SI #900#6 ! #900#19; |O #900#7 ! #900#20;
     |O #900#8 ! #900#21; |O #900#23 ! #900#22;
        |HAZ CambiaAsto;
 |FINSI;

 |SI #900#6 ! #900#24; |O #900#7 ! #900#25;
     |O #900#8 ! #900#26; |O #900#23 ! #900#27;
        |HAZ CambiaIVA;
 |FINSI;
|FPRC;

|DEFBUCLE leeElAuxi;
 #900#1;
 9;
 0000001, "S";
 9999999, "S";
 ;
 NULL, leeElAuxi;
|FIN;

|PROCESO actualiza;

 |ABRE #10;
 |ABRE #11;
 |BUCLE leeElAuxi;
 |CIERRA #11;
 |CIERRA #10;

|FPRC;

|| ***********************************************************************
|| *********************** RECUPERACION CON TIPO 3 ***********************
|| ***********************************************************************
|RUTINA ClaveAlta900;
 #900#0 = 1;
|FRUT;

|RUTINA ClaveBaja900;
 #900#0 = 99999;
|FRUT;

|CALCULO recupera; |TIPO 3;

   tipoRS= #0#6;
   aDTipoRS = tipoRS; aHTipoRS = tipoRS;
   |SI tipoRS = "T";
       aDTipoRS = "N"; aHTipoRS = "S";
   |FINSI;

   aFichero = ("15w" + Usuario) % 108;
   |NOME_DAT #900 aFichero;
   |ABRE #900; |CIERRA #900; |DELINDEX #900;

   swc   = 0;
   nElDoc = 0;
   |ABRE #900;
   |ATRI I; |PINTA 1964, " <TEMPORAL> "; |ATRI 0;

   |SI #0#11 = "A";
       |ABRE #10;
       |SELECT #3#10;
       |BUCLE lee_diario;
       |SELECT #1#10;
       |CIERRA #10;
   |SINO;
       |ABRE #1;
       |BUCLE lee_facturas;
       |CIERRA #1;
   |FINSI;
   |PINTA 1964, "            ";
   |CIERRA #900;

   |SI swc ! 0;
       |SI enBorraAu = 0;
           |SI enSwAna = 1;
               |PINPA #0#900;
           |SINO;
               |PINPA #1#900;
               |C_V #900#8,1,"N";
               |C_M #900#8,1,"N";
               |C_P #900#9,1,1370;
           |FINSI;
           |ATRI S; |PINTA 0706, "Empresa: "; |ATRI 0;
           |PINTA 0715, #canempre#2;
           |PINTA 0721, #canempre#1;
           |ENTLINEAL #1#900, -1, 2, 22, 0, ClaveAlta900, ClaveBaja900;
           |CONFI "2400SDesea modificar los Asientos/Facturas Seleccionados ";
           |SI FSalida ! 0; swc = 0; |FINSI;
     |FINSI;
  |FINSI;

  |SI swc ! 0;
      |HAZ actualiza;
  |FINSI;
  |DELINDEX #900;
|FCAL;
