|FICHEROS;
     pacondis;
     cacuenta;
     caclipro;
     camaasil;
     camaasic;
     camaniva;
     caivalin;
     calincob;
     calinpag;
     caivases;
     caivaasi;
     &Puente@;
     cacuebal;
     ca8m0002;
     ca8cumay;
     :/modelos/def/dsmom030;
     :/basica/def/agim0003;
     :/basica/def/agim0016;
     :/integral/def/dsam0103;
     :/integral/def/dsam0104;
     pacondit;
     regisnif@;
     paises@;

     caw00011;
|FIN;

|INCLUYE dscont_i;
|INCLUYE defec1_i;

|VARIABLES;
     nLinea    = 0;
     nSwMal    = 0;
     nLibro    = 0;
     nNume     = 0;
     nConce    = 0;
     Comodin   = "";
     Comodin1  = "";
     nPos      = 0;
     aSerie    = "";
     aCliPrv   = "";
     aPath     = "";
     aAlfa     = "";
     aAlfa1    = "";
     aAlfa2    = "";
     aDes      = "";
     aIP       = "";
     nHandle   = 0;
     aDato1    = "";
     aDato2    = "";
     aDato3    = "";
     aDato4    = "";
     aFic      = "";
     nLin      = 0;
     aCuenta   = "";
     aDesglose = "";
     aDescrip  = "";
     aRazon    = "";
     aDirec    = "";
     aPobla    = "";
     aCp       = "";
     aProv     = "";
     aTlf      = "";
     aFax      = "";
     aDNI      = "";
     aRegimen  = "";
     aReten    = "";
     aCodBan   = "";
     aCodPais  = "";
     aIban     = "";
     aBic      = "";
     aFecha    = "";
     aDoc      = "";
     aDepar    = "";
     aNumero   = "";
     aImporte  = "";
     aDescri   = "";
     aTipo     = "";
     aReservado = "";
     alfa1     = "";
     alfa2     = "";
     alfa3     = "";
     alfa4     = "";
     nNume1    = 0;
     nNume2    = 0;
     aLonCta   = "";
     aLon      = "";
     nSw       = 0;
     nImporte  = 0;
     aNombre   = "";
     aAsiento  = "";
     aRegIVA   = "";
     aFechaFac = "";
     aFechaCon = "";
     aNumFac   = "";
     aSopRep   = "";
     aBase1    = "";
     aPorIVA1  = "";
     aCuota1   = "";
     aRecargo1 = "";
     aCuotaRec1 = "";
     aBase2    = "";
     aPorIVA2  = "";
     aCuota2   = "";
     aRecargo2 = "";
     aCuotaRec2 = "";
     aBase3    = "";
     aPorIVA3  = "";
     aCuota3   = "";
     aRecargo3 = "";
     aCuotaRec3 = "";
     aIRPF     = "";
     aCuotaIRPF =  "";
     nBase1    = 0;
     nPorIVA1  = 0;
     nCuota1   = 0;
     nRecargo1 = 0;
     nCuotaRec1 = 0;
     nBase2    = 0;
     nPorIVA2  = 0;
     nCuota2   = 0;
     nRecargo2 = 0;
     nCuotaRec2 = 0;
     nBase3    = 0;
     nPorIVA3  = 0;
     nCuota3   = 0;
     nRecargo3 = 0;
     nCuotaRec3 = 0;
     nIRPF     = 0;
     nCuotaIRPF = 0;
     aFechaVTO = "";
     aCta      = "";
     aCtaTot   = "";
     aCtaBas   = "";
     aCtaIva   = "";
     nSwError  = 0;
     nParaCon  = 0;
     nCampo90  = 0;
     nEstado   = 0;
     nSwBal    = 0;
     nError    = 0;
     nPas      = 0;

     fFecha    = @;
     aCtaBasR  = "";
     aCtaIvaR  = "";
     n1Vent    = 0;

     {-1}aVent       = "";
         aVent1      = 0;
         aVent2      = 0;
         aVent3      = 0;
         aVent4      = 0;
         aVent5      = "";

     &libro = 0;
     &serie = "";
     &orden = 0;
     &depart = "";
     &pesetas = 0;
     &cuenta = "";
     &emision = "";
     &nConcep = 0;
     &aDesc   = "";
     &digito = 0;
     &enModoEsc = 0;
     &eaPath = "";
     &eaRetorno = "";
     &numdocum = 0;
     &enDiarioNivel = -1;
     &eaRef = "";
     &eaNombre = "";
     &enAux = 0;
     &pathbal = "";
     &balance = 1;
     &eaFicDis = "";
     &eaPathLocalDK = "";
|FIN;

|PROCESO GrabaError;
    nLinea = nLinea + 1;
    #caw00011#0 = nLinea;
    |SI nSwError = 1;
        |SI nSwMal = 0;
            aAlfa = "Existen Cuentas Fuera de Nivel en el PLAN CONTABLE";
        |SINO;
           aAlfa = "Cuenta fuera de Nivel: " + aCuenta + " " + aDescrip;
        |FINSI;
    |FINSI;
    |SI nSwError = 2;
        |SI nError = 2;
            aAlfa = "Existen Cuentas Fuera de Nivel en el HISTORICO DE IVA";
        |FINSI;
        |SI nError = 1;
            aAlfa = "Fuera del Ejercicio Registro de IVA: "+ aRegIVA + " a Fecha " + fFecha;
        |FINSI;
    |FINSI;
    |SI nSwError = 3;
        |SI nError = 2;
            aAlfa = "Existen Cuentas Fuera de Nivel en el DIARIO";
        |FINSI;
        |SI nError = 1;
            aAlfa = "Fuera del Ejercicio en el Diario: "+ aAsiento + " a Fecha " + fFecha;
        |FINSI;
    |FINSI;
    |SI nSwError = 4;
        |SI nError = 2;
            aAlfa = "Existen Cuentas Fuera de Nivel en los COBROS";
        |FINSI;
        |SI nError = 1;
            aAlfa = "Fuera del Ejercicio en los Cobros";
        |FINSI;
    |FINSI;
    #caw00011#1 = aAlfa;
    |GRABA 040#caw00011.n;
|FPRC;

|PROCESO SacaSerie;
     aAlfa = aNumFac - "-";
     |SI FSalida > 0;
          nPos = ((FSalida - 1) / 100) + 1;
          aAlfa = aNumFac;
          aNumFac = aNumFac % nPos;
          nPos = nPos + 98;
          aSerie = aAlfa % nPos;
     |SINO;
          aSerie = "";
     |FINSI;
|FPRC;

|PROCESO GrabaRecibos;
     |SI #pacondit#4 = "S";
          #camaniva#0 = #pacondit#0;
          #camaniva#1 = #pacondit#1;
          |LEE 020#camaniva.=;

          numdocum = #camaniva#1;
          libro = #camaniva#0;
          serie = #camaniva#2;
          orden = #camaniva#3;
          emision = #camaniva#4;
          pesetas = #camaniva#21 - #camaniva#23;
          enDiarioNivel = #camaniva#7;
          cuenta = #camaniva#12;
          eaCuenta = cuenta; |HAZ SacaNivel;
          digito = enNivel;
          depart = #camaniva#10;
          eaRef  = #camaniva#22;
          enModoEsc = 2;
          |SI #camaniva#36 = "R"; aAlfa = "calincob.tab"; |FINSI;
          |SI #camaniva#36 = "S"; aAlfa = "calinpag.tab"; |FINSI;
          eaNombre = #camaniva#13;

          enAux = 3; |SUB_EJECUTA_NP aAlfa;
     |FINSI;
|FPRC;

|DEFBUCLE GrabaRecibos;
     #pacondit#1;
     ;
     01, 0000000001;
     99, 9999999999;
     ;
     NULL, GrabaRecibos;
|FIN;

|PROCESO BorraRecibos;
     Comodin = #caivaasi#56; |QBF Comodin;
     |SI Comodin = ""; |ACABA; |FINSI;

     eaPath = Comodin;
     |HAZ CreaPuente;
     |SI eaRetorno = "ERROR"; |ACABA; |FINSI;

     #camaniva#0 = #pacondit#0;
     #camaniva#1 = #pacondit#1;
     |LEE 020#camaniva.=;

     |DDEFECTO #Puente@;
     #Puente@#0 = #camaniva#0;
     #Puente@#1 = #camaniva#2;
     #Puente@#2 = #camaniva#3;
     #Puente@#3 = 0;
     #Puente@#5 = #camaniva#4;
     |DFICE Comodin; |QBF Comodin;
     Comodin = Comodin % -107; Comodin = Comodin % 106;
     Comodin1 = Comodin % 0105; #Puente@#23 = Comodin1;
     Comodin1 = Comodin % -101; #Puente@#24 = Comodin1;
     |SI #camaniva#36 = "R"; #Puente@#27 = "V"; |FINSI;
     |SI #camaniva#36 = "S"; #Puente@#27 = "C"; |FINSI;
     #Puente@#28 = "B";
     |GRABA 020#Puente@.n;
     |HAZ CierraPuente;
|FPRC;

|DEFBUCLE BorraRecibos;
     #pacondit#1;
     ;
     01, 0000000001;
     99, 9999999999;
     ;
     NULL, BorraRecibos;
|FIN;

|PROCESO nuSacaNivel;
     |QBF eaCuenta;
     alfa1 = eaCuenta % 0;
     nNume1 = alfa1;
     |SI nNume1 < MaximoDigitos;
          nNume2 = MaximoDigitos - nNume1;
          alfa4 = "0" * nNume2;

          alfa1 = eaCuenta % 104;
          alfa2 = eaCuenta % 505;
          alfa3 = alfa1 + alfa4 + alfa2;
          eaCuenta = alfa3;
     |FINSI;
     |HAZ SacaNivel;
|FPRC;

|PROCESO GrabaCobro;
     aCuenta   = aDato1 % 0112
     aAsiento  = aDato1 % 1306
     aFechaVTO = aDato1 % 1908; aFechaVTO = (aFechaVTO % 102)+"."+(aFechaVTO%302)+"."+(aFechaVTO%504);
     fFecha    = aFechaVTO;

     |SI fFecha < fec1;  |O fFecha > fec2;
         |HAZ GrabaError;
         nError = 1;
         |ACABA;
     |FINSI;

     aDescri = aDato1 % 2730
     aDoc = aDato1 % 5710
     aCta = aDato1 % 6712
     aTipo = aDato1 % 7901
     aImporte = aDato1 % 8012; nImporte = aImporte / 100;

     |QBF aCuenta
     aLon = aCuenta % A0;
     eaCuenta = aCuenta;
     |HAZ nuSacaNivel;

     |SI enNivel = 0;    nError = 2;  |FINSI;
     |SI aLon ! aLonCta; nError = 2;  |FINSI;
     |SI nError = 2;
         |HAZ GrabaError;
         |ACABA;
     |FINSI;

     |SELECT #2#pacondit;
     #pacondit#3 = aAsiento;
     |LEE 000#pacondit.=;
     |SI FSalida ! 0;
          aAlfa = "0000No se encuentra el registro de iva, asiento: [" + aAsiento + "]...";
          |MENAV aAlfa;
     |SINO;
          #camaniva#0 = #pacondit#0;
          #camaniva#1 = #pacondit#1;
          |LEE 000#camaniva.=;
          |SI FSalida ! 0;
               aAlfa = "0000No se encuentra el registro de iva: [" + #camaniva#0 + "/" + #camaniva#1 + "]...";
               |MENAV aAlfa;
          |SINO;
               |SI aTipo = "C";
                    |DDEFECTO #calincob;
                    #calincob#0 = #camaniva#0;
                    #calincob#1 = aDoc;
                    #calincob#2 = #camaniva#2;
                    #calincob#3 = 1;
                    |LEE 101#calincob.=;
                    |SI FSalida ! 0; |GRABA 020#calincob.b; |FINSI;
                    #calincob#4 = aCuenta;
                    #calincob#5 = enNivel;
                    #calincob#6 = #camaniva#4;
                    #calincob#7 = aFechaVTO;
                    #calincob#14 = aFechaVTO;
                    #calincob#8 = aImporte;
                    #calincob#26 = aImporte;
                    |GRABA 020#calincob.a;
                    |LIBERA #calincob;
               |SINO;
                    |DDEFECTO #calinpag;
                    #calinpag#0 = #camaniva#0;
                    #calinpag#1 = aDoc;
                    #calinpag#2 = #camaniva#2;
                    #calinpag#3 = 1;
                    |LEE 101#calinpag.=;
                    |SI FSalida ! 0; |GRABA 020#calinpag.b; |FINSI;
                    #calinpag#4 = aCuenta;
                    #calinpag#5 = enNivel;
                    #calinpag#6 = #camaniva#4;
                    #calinpag#7 = aFechaVTO;
                    #calinpag#9 = aImporte;
                    |GRABA 020#calinpag.a;
                    |LIBERA #calinpag;
               |FINSI;
               #pacondit#4 = "S";
               |GRABA 020#pacondit.a;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Pendientes; || Registro < 250
     nLin   = 0;
     nError = 0;
     |PARA; |SI; |HACIENDO;
          nLin = nLin + 1;
          aAlfa = aFic + ":" + nLin + "      ";
          |PINTA 2107, aAlfa;
          |XLEE nHandle, 250, aDato1;
          |SI FSalida < 0; |SAL; |FINSI;

          |HAZ GrabaCobro;

          |SI nError = 2;  |SAL;  |FINSI;
     |SIGUE;

     |SI nError = 2;
         aAlfa = "0000" + aFic + " .No corresponden los Niveles; proceso interrumpido ...";
         |MENAV aAlfa;
     |FINSI;

     |SI nError = 1;
         aAlfa = "0000" + aFic + ". Tiene registros fuera del periodo contable";
         |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO ConceptoIRPF;
     |SI Haymodelo = 0; |ACABA; |FINSI;

     |DDEFECTO #dsmom030;
     aAlfa = aTipoIVA + aSopRep;
     |SI aAlfa = "AS";
          #dsmom030#0 = 401;
     |SINO;
          #dsmom030#0 = 410;
     |FINSI;

     #caivalin#18 = #dsmom030#0;

     |LEE 000#dsmom030.=;
     |SI FSalida = 0;
          #caivalin#19 = #dsmom030#1;
     |FINSI;
|FPRC;

|PROCESO Concepto;
     |SI Haymodelo = 0; |ACABA; |FINSI;

     |DDEFECTO #dsmom030;
     aAlfa = aTipoIVA + aSopRep;
     |SI aAlfa = "DS"; #dsmom030#0 = 600; |FINSI;
     |SI aAlfa = "DR"; #dsmom030#0 = 700; |FINSI;
     |SI aAlfa = "NS"; #dsmom030#0 = 600; |FINSI;
     |SI aAlfa = "NR"; #dsmom030#0 = 700; |FINSI;
     |SI aAlfa = "MS"; #dsmom030#0 = 604; |FINSI;
     |SI aAlfa = "MR"; #dsmom030#0 = 712; |FINSI;
     |SI aAlfa = "IS"; #dsmom030#0 = 607; |FINSI;
     |SI aAlfa = "IR"; #dsmom030#0 = 713; |FINSI;
     |SI aAlfa = "PS"; #dsmom030#0 = 605; |FINSI;
     |SI aAlfa = "PR"; #dsmom030#0 = 715; |FINSI;
     |SI aAlfa = "ES"; #dsmom030#0 = 600; |FINSI;
     |SI aAlfa = "ER"; #dsmom030#0 = 700; |FINSI;
     |SI aAlfa = "AS"; #dsmom030#0 = 602; |FINSI;
     |SI aAlfa = "AR"; #dsmom030#0 = 704; |FINSI;

     #camaniva#27 = #dsmom030#0;
     #camaniva#29 = #dsmom030#0;

     |LEE 000#dsmom030.=;
     |SI FSalida = 0;
          #camaniva#28 = #dsmom030#1;
          #camaniva#30 = #dsmom030#1;
     |FINSI;
|FPRC;

|PROCESO RecalHistoIVA;
    #camaniva#19 = #camaniva#19 + #caivalin#6;   ||Total Bases
    #camaniva#15 = #camaniva#15 + #caivalin#6;
    #camaniva#17 = #camaniva#15 % #camaniva#16;
    #camaniva#20 = #camaniva#20 + #caivalin#22;  ||Total Irpf
    #camaniva#23 = #camaniva#23 + (#caivalin#8 + #caivalin#10);
    #camaniva#26 = #camaniva#26 + #caivalin#11;  ||Total Descuento
    #camaniva#21 = #camaniva#19 + #camaniva#23 - #camaniva#26 - #camaniva#20;
    aAlfa = #camaniva#4 % 402;
    #camaniva#5 = aAlfa;
|FPRC;

|PROCESO GrabaIVA;
     aCuenta = aDato1 % 0112;
     aRazon  = aDato1 % 1340;
     aNombre = aDato1 % 5330;
     aDirec  = aDato1 % 8330;
     aPobla  = aDato1 % 11330;
     aCp     = aDato1 % 14305; |QBF aCp; aCp = ("00000" + aCp) % -105;
     aProv   = aDato1 % 14820;
     aTlf    = aDato1 % 16820;
     aFax    = aDato1 % 18815;
     aDNI    = aDato1 % 20315;
     aAsiento = aDato1 % 21806;
     aRegIVA = aDato1 % 22406;
     aFechaFac = aDato1 % 23008;
     aFechaFac = (aFechaFac % 102) + "." + (aFechaFac%302) + "." + (aFechaFac % 504);
     aFechaCon = aDato1 % 23808;
     aFechaCon = (aFechaCon % 102) + "." + (aFechaCon%302) + "." + (aFechaCon % 504);
     fFecha    = aFechaFac;

     |SI fFecha < fec1;  |O fFecha > fec2;
         nError = 1;
         |HAZ GrabaError;
         |ACABA;
     |FINSI;

     aNumFac = aDato2 % 110;
     aTipoIVA = aDato2 % 1101;
     aSopRep = aDato2 % 1201;

     aBase1 = aDato2 % 1312; nBase1 = aBase1 / 100;
     aPorIVA1 = aDato2 % 2502;
     aCuota1 = aDato2 % 2712; nCuota1 = aCuota1 / 100;
     aRecargo1 = aDato2 % 3902; nRecargo1 = aRecargo1 / 10;
     aCuotaRec1 = aDato2 % 4110; nCuotaRec1 = aCuotaRec1 / 100;

     aBase2 = aDato2 % 5112;  nBase2 = aBase2 / 100;
     aPorIVA2 = aDato2 % 6302;
     aCuota2 = aDato2 % 6512; nCuota2 = aCuota2 / 100;
     aRecargo2 = aDato2 % 7702; nRecargo2 = aRecargo2 / 10;
     aCuotaRec2 = aDato2 % 7910; nCuotaRec2 = aCuotaRec2 / 100;

     aBase3 = aDato2 % 8912; nBase3 = aBase3 / 100;
     aPorIVA3 = aDato2 % 10102;
     aCuota3 = aDato2 % 10312; nCuota3 = aCuota3 / 100;
     aRecargo3 = aDato2 % 11502; nRecargo3 = aRecargo3 / 10;
     aCuotaRec3 = aDato2 % 11710; nCuotaRec3 = aCuotaRec3 / 100;

     aIRPF = aDato2 % 12702;
     aCuotaIRPF = aDato2 % 12912;
     aCodPais= aDato2 % 14102;

     |QBF aCuenta
     aLon = aCuenta % A0;
     eaCuenta = aCuenta;
     |HAZ nuSacaNivel;

     |SI enNivel = 0;    nError = 2;  |FINSI;
     |SI aLon ! aLonCta; nError = 2;  |FINSI;
     |SI nError = 2;
         |HAZ GrabaError;
         |ACABA;
     |FINSI;

     |SI aSopRep = "R";
         nLibro = #pacondis#1;
         aSerie = #pacondis#2;
     |SINO;
         nLibro = #pacondis#3;
         aSerie = #pacondis#4;
     |FINSI;

     |SELECT #2#camaniva;
     #camaniva#7 = #pacondis#0;
     #camaniva#8 = aFechaCon;
     #camaniva#9 = aAsiento;
     |LEE 101#camaniva.=;
     |SI FSalida = 0;
         |BORRA 020#camaniva.a;
         |BUCLELIN 1NULL#camaniva;
         |LIBERA #camaniva;
     |FINSI;

     |SELECT #1#camaniva;
     #camaniva#0 = nLibro;
     #camaniva#1 = 9999999999;
     |LEE 101#camaniva.];
     |SI FSalida = 0;
         |LEE 000#camaniva.a;
     |SINO;
         |LEE 000#camaniva.u;
     |FINSI;
     |SI FSalida = 0; |Y nLibro = #camaniva#0;
         nNume = #camaniva#1 + 1;
     |SINO;
         nNume = 1;
     |FINSI;

     |HAZ SacaSerie;

     |DDEFECTO #camaniva;
     #camaniva#0 = nLibro;
     #camaniva#1 = nNume;
     #camaniva#2 = aSerie;
     #camaniva#3 = aNumFac;
     #camaniva#4 = aFechaFac;
     #camaniva#7 = #pacondis#0;
     #camaniva#8 = aFechaCon;
     #camaniva#9 = aAsiento;
     #camaniva#12 = aCuenta;
     #camaniva#13 = aRazon;
     #camaniva#14 = aDNI;
     #camaniva#36 = aSopRep;
     |HAZ Concepto;
     aDes = #camaniva#28; |QBF aDes;
     aAlfa = #camaniva#2; |QBF aAlfa;
     |SI aAlfa = "";
          aDes = aDes + " " + #camaniva#3;
     |SINO;
          aDes = aDes + " " + #camaniva#2; |QBF aDes;
          aDes = aDes + "-" + #camaniva#3;
     |FINSI;
     #camaniva#28 = aDes;
     #camaniva#30 = aDes;

     #camaniva#10 = eaDfDepC;
     #camaniva#11 = eaDfSubC;
     #camaniva#24 = eaDfDepN;
     #camaniva#25 = eaDfSubN;

     |GRABA 020#camaniva.b;

     #pacondit#0 = #camaniva#0;
     #pacondit#1 = #camaniva#1;
     #pacondit#2 = #camaniva#27;
     #pacondit#3 = #camaniva#9;
     |GRABA 020#pacondit.n;

     nLin = 0;
     |SI nBase1 ! 0;
          nLin = nLin + 1;
          |DDEFECTO #caivalin;
          #caivalin#0 = #camaniva#0;
          #caivalin#1 = #camaniva#1;
          #caivalin#2 = nLin;
          #caivalin#3 = #camaniva#27;
          #caivalin#4 = aDes;
          #caivalin#6 = nBase1;
          #caivalin#7 = aPorIVA1;
          #caivalin#8 = nCuota1;
          #caivalin#9 = nRecargo1;
          #caivalin#10 = nCuotaRec1;
          |SI nLin = 1; |Y nCuotaIRPF ! 0;
               #caivalin#21 = aIRPF;
               #caivalin#22 = nCuotaIRPF;
               aAlfa = aTipoIVA + aSopRep;
               |HAZ ConceptoIRPF;
          |FINSI;
          |GRABA 020#caivalin.n;
     |FINSI;

     |SI nBase2 ! 0;
          nLin = nLin + 1;
          |DDEFECTO #caivalin;
          #caivalin#0 = #camaniva#0;
          #caivalin#1 = #camaniva#1;
          #caivalin#2 = nLin;
          #caivalin#3 = #camaniva#27;
          #caivalin#4 = aDes;
          #caivalin#6 = nBase2;
          #caivalin#7 = aPorIVA2;
          #caivalin#8 = nCuota2;
          #caivalin#9 = nRecargo2;
          #caivalin#10 = nCuotaRec2;
          |SI nLin = 1; |Y nCuotaIRPF ! 0;
               #caivalin#21 = aIRPF;
               #caivalin#22 = nCuotaIRPF;
               |HAZ ConceptoIRPF;
          |FINSI;
          |GRABA 020#caivalin.n;
     |FINSI;

     |SI nBase3 ! 0;
          nLin = nLin + 1;
          |DDEFECTO #caivalin;
          #caivalin#0 = #camaniva#0;
          #caivalin#1 = #camaniva#1;
          #caivalin#2 = nLin;
          #caivalin#3 = #camaniva#27;
          #caivalin#4 = aDes;
          #caivalin#6 = nBase3;
          #caivalin#7 = aPorIVA3;
          #caivalin#8 = nCuota3;
          #caivalin#9 = nRecargo3;
          #caivalin#10 = nCuotaRec3;
          |SI nLin = 1; |Y nCuotaIRPF ! 0;
               #caivalin#21 = aIRPF;
               #caivalin#22 = nCuotaIRPF;
               |HAZ ConceptoIRPF;
          |FINSI;
          |GRABA 020#caivalin.n;
     |FINSI;
     |BUCLELIN 0RecalHistoIVA#camaniva;
     |GRABA 020#camaniva.a;
     |LIBERA #camaniva;
|FPRC;

|PROCESO camaasilCheq;
     |SI nPas = 0;
         |SI aCtaTot = "";
             aAlfa = #camaasil#4 % 102;
             |SI aAlfa = "40";  |O aAlfa = "41";
              |O aAlfa = "43";  |O aAlfa = "44";
                 aCtaTot = #camaasil#4;
             |FINSI;
         |FINSI;

         |SI aCtaBas = "";
             aAlfa = #camaasil#4 % 101;
             |SI aAlfa = "7"; |O aAlfa = "6";
                 |SI #caivalin#6 = #camaasil#9;
                     aCtaBas = #camaasil#4;  |QBF aCtaBas;
                     aCtaBasR = "";
                 |SINO;
                     aCtaBasR = #camaasil#4;
                 |FINSI;
             |FINSI;
         |FINSI;

         |SI aCtaIva = "";
             aAlfa = #camaasil#4 % 103;
             |SI aAlfa = "472";  |O aAlfa = "477";
                  |SI #caivalin#8 = #camaasil#9;
                     aCtaIva = #camaasil#4;  |QBF aCtaIva;
                     aCtaIvaR = "";
                 |SINO;
                     aCtaIvaR = #camaasil#4;
                 |FINSI;
             |FINSI;
         |FINSI;
     |FINSI;

     |SI nPas = 1;
         #camaasil#12 = #camaniva#36;
         #camaasil#13 = #camaniva#0;
         #camaasil#14 = #camaniva#3;
         #camaasil#15 = #camaniva#2;
         #camaasil#20 = #caivalin#2;

         eaCuenta = #camaasil#4; |QBF eaCuenta;
                                     #camaasil#19 = "P";
         |SI aCtaBas = eaCuenta;     #camaasil#19 = "B";  |FINSI;
         |SI aCtaIva = eaCuenta;     #camaasil#19 = "I";  |FINSI;
         |SI aCtaTot = eaCuenta;     #camaasil#19 = "F";  #camaasil#20 = 0;  |FINSI;

         |SI #camaasil#19 = "B"; |Y aCtaBasR ! "";
              #camaasil#20 = 0;
         |FINSI;
          |SI #camaasil#19 = "I"; |Y aCtaIvaR ! "";
               #camaasil#20 = 0;
         |FINSI;

         |GRABA 020#camaasil.a;
         |LIBERA #camaasil;
     |FINSI;
|FPRC;

|DEFBUCLE camaasilCheq;
     #camaasil#1;
     ;
     #camaasic#0, #camaasic#1, #camaasic#2, 0001;
     #camaasic#0, #camaasic#1, #camaasic#2, 9999;
     be;
     NULL, camaasilCheq;
|FIN;

|PROCESO caivalinCheq;

     |SI #caivalin#12 ! doce; |ACABA; |FINSI;

     #camaniva#0 = #caivalin#0;
     #camaniva#1 = #caivalin#1;
     |LEE 000#camaniva.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     #camaasic#0 = #camaniva#7;
     #camaasic#1 = #camaniva#8;
     #camaasic#2 = #camaniva#9;
     |LEE 141#camaasic.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aCtaTot = "";
     aCtaBas = ""; aCtaBasR = "";
     aCtaIva = ""; aCtaIvaR = "";
     nPas = 0;  |BUCLE camaasilCheq;

||     |SI aCtaTot ! "";  |Y aCtaBas = "";  |Y  aCtaIva = "";

      |SI aCtaBas = ""; aCtaBas = aCtaBasR; |FINSI;
      |SI aCtaIva = ""; aCtaIva = aCtaIvaR; |FINSI;
       nPas = 1;  |BUCLE camaasilCheq;
||     |FINSI;


     #caivalin#12 = aCtaBas;
     #caivalin#13 = aCtaIva;

     |GRABA 020#caivalin.a;
     |LIBERA #caivalin;

     |SI #camaniva#36 = "R"; #camaasic#13 = 1; |FINSI;
     |SI #camaniva#36 = "S"; #camaasic#13 = 2; |FINSI;
     |SI #camaniva#36 = "N"; #camaasic#13 = 3; |FINSI;

     |GRABA 020#camaasic.a;
     |LIBERA #camaasic;
|FPRC;

|DEFBUCLE caivalinCheq;
     #caivalin#1;
     ;
     #pacondit#0, #pacondit#1, 01;
     #pacondit#0, #pacondit#1, 99;
     ;
     NULL, caivalinCheq;
|FIN;

|PROCESO paconditCheq;
    |BUCLE caivalinCheq;
|FPRC;

|DEFBUCLE paconditCheq;
     #pacondit#1;
     ;
     00, 0000000001;
     99, 9999999999;
     ;
     NULL, paconditCheq;
|FIN;

|PROCESO HistoIVA;   || Registro de 389
     nLin   = 0;
     nError = 0;
     nSwMal = 0;
     |PARA; |SI; |HACIENDO;
          nLin = nLin + 1;
          aAlfa = aFic + ":" + nLin + "      ";
          |PINTA 2107, aAlfa;
          |XLEE nHandle, 245, aDato1;
          |SI FSalida = 0; |SAL; |FINSI;
          |XLEE nHandle, 144, aDato2;

          |XLEE nHandle, 140, aDato3;  || modificado 5213 - 146
          |HAZ GrabaIVA;

          |SI nError = 2;  |SAL;  |FINSI;
     |SIGUE;

     |SI nError = 2;
         aAlfa = "0000" + aFic + " .No corresponden los Niveles; proceso interrumpido ...";
         |MENAV aAlfa;
         |ACABA;
     |FINSI;

     |SI nError = 1;
         aAlfa = "0000" + aFic + ". Tiene registros fuera del periodo contable";
         |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO GrabaApun;
     aCuenta    = aDato1 % 0112
     aFecha     = aDato1 %  1308;
     aFecha     = (aFecha % 102)+ "." + (aFecha % 302) + "." + (aFecha % 504);
     fFecha     = aFecha;
     |SI fFecha < fec1;  |O fFecha > fec2;
         nError = 1;
         |HAZ GrabaError;
         |ACABA;
     |FINSI;

     aDoc       = aDato1 %  2108
     aDepar     = aDato1 % 2908
     aNumero    = aDato1 % 3706
     aImporte   = aDato1 % 4312; nImporte = aImporte / 100;
     aDescri    = aDato1 % 5530
     aTipo      = aDato1 % 8501
     aReservado = aDato1 % 8601

     |QBF aCuenta
     aLon = aCuenta % A0;
     eaCuenta = aCuenta;
     |HAZ nuSacaNivel;
     |SI enNivel = 0;    nError = 2;  |FINSI;
     |SI aLon ! aLonCta; nError = 2;  |FINSI;
     |SI nError = 2;
         |HAZ GrabaError;
         |ACABA;
     |FINSI;

     |SI nSw = 1;
         #camaasic#0 = #pacondis#0;
         #camaasic#1 = aFecha;
         #camaasic#2 = aNumero;
         |LEE 101#camaasic.=;
         |SI FSalida = 0;
             |BUCLELIN 1NULL#camaasic;
             |BORRA 020#camaasic.a;
             |LIBERA #camaasic;
         |FINSI;

         |HAZ GrabaAux9;
     |SINO;
         |SELECT #2#pacondit;
         #pacondit#3 = aNumero;
         |LEE 000#pacondit.=;
         nConce = 1;
         |SI FSalida = 0;
             nConce = #pacondit#2;
         |FINSI;

         |SELECT #1#pacondit;

         |DDEFECTO #camaasic;
         #camaasic#0 = #pacondis#0;
         #camaasic#1 = aFecha;
         #camaasic#2 = aNumero;
         |LEE 101#camaasic.=;
         |SI FSalida ! 0; |GRABA 020#camaasic.b; |FINSI;

         #camaasil#0 = #pacondis#0;
         #camaasil#1 = aFecha;
         #camaasil#2 = aNumero;
         #camaasil#3 = 9999;
         |LEE 000#camaasil.];
         |SI FSalida = 0;
             |LEE 000#camaasil.a;
         |SINO;
             |LEE 000#camaasil.u;
         |FINSI;

         nLin = 1;
         |SI FSalida = 0; |Y #camaasil#0 = #pacondis#0; |Y #camaasil#1 = aFecha; |Y #camaasil#2 = aNumero;
             nLin = #camaasil#3 + 1;
         |FINSI;

         |DDEFECTO #camaasil;
         #camaasil#0 = #pacondis#0;
         #camaasil#1 = aFecha;
         #camaasil#2 = aNumero;
         #camaasil#3 = nLin;
         #camaasil#4 = aCuenta;
         #camaasil#5 = enNivel;
         #camaasil#6 = nConce;
         #camaasil#7 = aDescri;
         #camaasil#8 = aTipo;
         #camaasil#9 = nImporte;
         #camaasil#21 = eaDfDepC;
         #camaasil#28 = eaDfSubC;
         |SI #camaasil#8 = "H";
             #camaasil#10 = #camaasil#4;
         |SINO;
             #camaasil#16 = #camaasil#4;
         |FINSI;
         |GRABA 020#camaasil.n;
         nSwBal = 1;

         #camaasic#3 = #camaasic#2;
         |SI #camaasil#8 = "D"; #camaasic#4 = #camaasic#4 + #camaasil#9;  |FINSI;
         |SI #camaasil#8 = "H"; #camaasic#5 = #camaasic#5 + #camaasil#9;  |FINSI;
         #camaasic#6 = #camaasic#4 - #camaasic#5;
         |GRABA 020#camaasic.a;
         |LIBERA #camaasic;
     |FINSI;
|FPRC;

|PROCESO LinApunte; || Registro < 250
     nLin   = 0;
     nError = 0;
     |PARA; |SI; |HACIENDO;
          nLin = nLin + 1;
          aAlfa = aFic + ":" + nLin + "      ";
          |PINTA 2107, aAlfa;
          |XLEE nHandle, 250, aDato1;
          |SI FSalida < 0; |SAL; |FINSI;

          |HAZ GrabaApun;

          |SI nError = 2;  |SAL;  |FINSI;
     |SIGUE;

     |SI nError = 2;
         aAlfa = "0000" + aFic + ". No corresponden los Niveles; proceso interrumpido ...";
         |MENAV aAlfa;
         |ACABA;
     |FINSI;

     |SI nError = 1;
         aAlfa = "0000" + aFic + ". Tiene registros fuera del periodo contable";
         |MENAV aAlfa;
     |FINSI;

     |SI nSw = 2;
        |ABRE #camaasic;  |SELECT #1#camaasic;
        |ABRE #camaniva;  |SELECT #1#camaniva;
        |CIERRA #pacondit;
        |BUCLE paconditCheq;
        |ABRE #pacondit;
        |CIERRA #camaasic;
        |CIERRA #camaniva;
     |FINSI;
|FPRC;

|PROCESO RegNif;
     |SI enSwDeDonde ] 0;
          |DDEFECTO #paises@;
          #paises@#0 = aCodPais;
          |LEE 000#paises@.=;

          |DDEFECTO #regisnif@;
          #regisnif@#0 = aDNI;
          |LEE 101#regisnif@.=;
          |SI FSalida ! 0; |GRABA 020#regisnif@.b; |FINSI;
          #regisnif@#1 = #caclipro#1;
          #regisnif@#2 = #caclipro#2;
          #regisnif@#4 = #caclipro#3;
          #regisnif@#5 = #caclipro#4;
          #regisnif@#6 = #caclipro#6;
          #regisnif@#7 = #caclipro#5;
          #regisnif@#8 = #caclipro#7;
          #regisnif@#17 = #caclipro#8;
          #regisnif@#12 = aCodPais;
          #regisnif@#13 = #paises@#1;
          |GRABA 020#regisnif@.a;
          |LIBERA #regisnif@;
     |FINSI;
|FPRC;

|PROCESO Cuentas;   || Registro de 284
     nError = 0;
     nSwMal = 0;
     nLin   = 0;
     |PARA; |SI; |HACIENDO;
          nLin = nLin + 1;
          aAlfa = aFic + ":" + nLin + "      ";

          |PINTA 2107, aAlfa;
          |XLEE nHandle, 197, aDato1;
          |SI FSalida = 0; |SAL; |FINSI;

          |XLEE nHandle, 87, aDato2;

             || |SI #pacondis#8 = 1;
          |XLEE nHandle, 135, aDato3;
             || |FINSI;

          aCuenta   = aDato1 % 112;
          aDesglose = aDato1 % 1301;
          aDescrip  = aDato1 % 1424;
          aRazon    = aDato1 % 3840;
          aDirec    = aDato1 % 7830;
          aPobla    = aDato1 % 10830;
          aCp       = aDato1 % 13805; |QBF aCp; aCp = ("00000" + aCp) % -105;
          aProv     = aDato1 % 14320;
          aTlf      = aDato1 % 16320;
          aFax      = aDato1 % 18315;

          aDNI      = aDato2 % 115;
          aRegimen  = aDato2 % 1601;
          aReten    = aDato2 % 1702;
          aCodBan   = aDato2 % 1920;
          aCodPais  = aDato2 % 3902;
          aIban     = aDato2 % 4134;
          aBic      = aDato2 % 7511;

          |QBF aCuenta
          aLon = aCuenta % A0;
          eaCuenta = aCuenta;
          |HAZ nuSacaNivel;
          |SI enNivel ! 0; |Y aLon = aLonCta;
               aAlfa = aCuenta % 104;
               |SI aAlfa = "4300";
                    aCliPrv = "C";
               |SINO;
                    aCliPrv = "P";
               |FINSI;
               |DDEFECTO #cacuenta;
               #cacuenta#0 = aCuenta;
               |LEE 101#cacuenta.=;
               |SI FSalida ! 0; |GRABA 020#cacuenta.b; |FINSI;
               #cacuenta#1 = enNivel;
               #cacuenta#2 = aRazon;
               #cacuenta#3 = "S";
               #cacuenta#54 = #cacuenta#0;
               #cacuenta#55 = #cacuenta#1;
               |GRABA 020#cacuenta.a;
               |LIBERA #cacuenta;

               |DDEFECTO #caclipro;
               #caclipro#23 = aCliPrv;
               #caclipro#10 = aCuenta;
               |LEE 101#caclipro.=;
               |SI FSalida ! 0; |GRABA 020#caclipro.b; |FINSI;
               #caclipro#1 = aRazon;
               #caclipro#2 = aDirec;
               aAlfa = aCp %  102; #caclipro#3 = aAlfa;
               aAlfa = aCp % -103; #caclipro#4 = aAlfa;
               #caclipro#5 = aProv;
               #caclipro#6 = aPobla;
               #caclipro#7 = aTlf;
               #caclipro#8 = aFax;
               #caclipro#9 = aDNI;
               aAlfa = aCodBan % 104; #caclipro#29 = aAlfa;
               aAlfa = aCodBan % 504; #caclipro#30 = aAlfa;
               aAlfa = aCodBan % 902; #caclipro#31 = aAlfa;
               aAlfa = aCodBan % 1110; #caclipro#32 = aAlfa;
               |GRABA 020#caclipro.a;
               |LIBERA #caclipro;
               |HAZ RegNif;
          |SINO;
               |SI nSwMal = 0;
                   |HAZ GrabaError;
                   aAlfa = "0000S" + aFic + "S. No corresponden los Niveles; Continuar ";
                   |CONFI aAlfa;
                   |SI FSalida !  0;
                       nError = 2;
                       |SAL;
                   |SINO;
                       nSwMal = 1;
                   |FINSI;
               |FINSI;
               |SI nSwMal = 1;
                   |HAZ GrabaError;
               |FINSI;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Abre;
     aAlfa = aPath + aFic;
     |DFICE aDes;
     aDes = aDes + Usuario;
     |SI aIP ! "";
          |RECIBE_FICHERO aAlfa, aDes;
          |SI FSalida = 0;
               |XABRE aTipo, aDes, nHandle;
          |FINSI;
     |SINO;
          |XABRE aTipo, aAlfa, nHandle;
     |FINSI;
     |SI FSalida < 0;
          aAlfa = "0000Error al abrir:" + aFic;
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO Cierra;
     |XCIERRA nHandle;
     |SI aIP ! ""; |FBORRA aDes; |FINSI;
|FPRC,

|PROCESO BorraFic;
     aAlfa = aPath + aFic;
     |RFBORRA aAlfa;
|FPRC;

|PROCESO GrabaAux9;
     |SI Haymodelo = 0; |ACABA; |FINSI;

     nCampo90 = -1;
     |SELECT #2#pacondit;
     #pacondit#3 = aNumero;
     |LEE 000#pacondit.=;
     nEstado = FSalida;
     |SI nEstado = 0;
         |SI #pacondit#0 ! 00;
             |SELECT #1#pacondit;
             |ACABA;
         |SINO;
             nCampo90 = #pacondit#1;
         |FINSI;
     |FINSI;
     |SELECT #1#pacondit;

     nConce = 1;
     aAlfa  = aCuenta % 103;
     |SI aAlfa = "570"; |O aAlfa = "572";
         aAlfa1 = aDescri > " ";
         aAlfa2 = aAlfa1 - "COBRO";
         |SI FSalida ! 0;
             |SI aAlfa = "570"; nConce = 060; |FINSI;
             |SI aAlfa = "572"; nConce = 062; |FINSI;
         |SINO;
             aAlfa2 = aAlfa1 - "PAGO";
             |SI FSalida ! 0;
                 |SI aAlfa = "570"; nConce = 061; |FINSI;
                 |SI aAlfa = "572"; nConce = 063; |FINSI;
             |FINSI;
         |FINSI;
     |FINSI;

     |SI nConce ! 1;
         #pacondit#0 = 00;
         #pacondit#1 = nCampo90;
         |SI nCampo90 = -1;
             nParaCon = nParaCon + 1;
             #pacondit#1 = nParaCon;
         |FINSI;
         #pacondit#2 = nConce;
         #pacondit#3 = aNumero;
         #pacondit#4 = "";
         |SI nEstado ! 0; |GRABA 040#pacondit.n; |FINSI;
         |SI nEstado = 0; |GRABA 040#pacondit.a; |FINSI;
     |FINSI;
|FPRC;

|PROCESO Max965;
     #caw00011#0 = 0;
|FPRC;

|PROCESO Min965;
     #caw00011#0 = 999999;
|FPRC;

|PROCESO T3; |TIPO 3;
     nSwBal = 0;
     aAlfa  = "ti" + Usuario;
     |NOME_DAT #pacondit#aAlfa#;
     |DELINDEX #pacondit;
     aAlfa  = "1e" + Usuario;
     |NOME_DAT #caw00011#aAlfa#;
     |DELINDEX #caw00011;

     |ABRE #pacondit;
     aPath = #pacondis#5; |QBF aPath;
     aAlfa = aPath % -101;
     |SI aAlfa ! "/"; |Y aAlfa ! "\"; aPath = aPath + "/"; |FINSI;

     aIP = "";
     |IP_REMOTO aIP;

     nSwMal = 0;
     nLinea = 0;
     |ABRE #caw00011;

     nSwError = 1;
     aTipo    = "B";
     aFic     = "PLACO000.ASC";
     |HAZ Abre;
     |SI FSalida ] 0;
          |ABRE #cacuenta;
          |ABRE #caclipro;
          |HAZ Cuentas;
          |CIERRA #caclipro;
          |CIERRA #cacuenta;
          |HAZ Cierra;
     |FINSI;

     nSwError = 2;
     aTipo    = "B";
     aFic     = "LBIVA000.ASC";
     |HAZ Abre;
     |SI FSalida ] 0;
          |ABRE #camaniva;
          |ABRE #caivalin;
          |SI Haymodelo = 1; |ABRE #dsmom030; |FINSI;
          |HAZ HistoIVA;
          |SI Haymodelo = 1; |CIERRA #dsmom030; |FINSI;
          |CIERRA #caivalin;
          |CIERRA #camaniva;
          |HAZ Cierra;
     |FINSI;

     nSwError = 3;
     aTipo    = "";
     aFic     = "APUNT000.ASC";
     |HAZ Abre;
     |SI FSalida ] 0;
          |ABRE #camaasil;
          |ABRE #camaasic;
          nSw = 1; |HAZ LinApunte;
          |CIERRA #camaasic;
          |CIERRA #camaasil;
          |HAZ Cierra;

          |HAZ Abre;
          |SI FSalida ] 0;
               |ABRE #camaasil;
               |ABRE #camaasic;
               nSw = 2; |HAZ LinApunte;
               |CIERRA #camaasic;
               |CIERRA #camaasil;
               |HAZ Cierra;
          |FINSI;
     |FINSI;

     |ABRE #camaniva;
     |BUCLE BorraRecibos;

     aTipo = "";
     aFic = "PREVI000.ASC";
     nSwError = 4;
     |HAZ Abre;
     |SI FSalida ] 0;
          |ABRE #calincob;
          |ABRE #calinpag;
          |HAZ Pendientes;
          |CIERRA #calinpag;
          |CIERRA #calincob;
          |HAZ Cierra;
     |FINSI;

     |BUCLE GrabaRecibos;
     |CIERRA #camaniva;


     |SI #pacondis#7 = "S";
         aFic = "PLACO000.ASC";  |HAZ BorraFic;
         aFic = "LBIVA000.ASC";  |HAZ BorraFic;
         aFic = "APUNT000.ASC";  |HAZ BorraFic;
         aFic = "PREVI000.ASC";  |HAZ BorraFic;
     |FINSI;

     |SI nSwBal = 1;
         |DFICE pathbal;
         |MENAV "0000Se ha actualizado el Diario, es conveniente Realizar un Recalculo de Saldos";
         |CONFI "2410SDesea Realizarlo ahora ";
         |SI FSalida = 0;
             balance = 2;
             |SUB_EJECUTA "careccue";
         |FINSI;
     |FINSI;

     |CIERRA #pacondit;
     |CIERRA #caw00011;
     |SI nLinea ! 0;
         |CONFI "0000SHay Errores. Desea consultarlos";
         |SI FSalida = 0;
             |PUSHV 0501 2380;
             |PINPA #0#caw00011;
             |ATRI S; |PINTA 0603, " ERRORES EN LA IMPORTACION "; |ATRI 0;
             |ENTLINEAL #1#caw00011, -1, 5, 22, 0, Max965, Min965;
             |POPV;
         |FINSI;
     |FINSI;

     |DELINDEX #pacondit;
     |DELINDEX #caw00011;
|FPRC;

|PROCESO Browser; |TIPO 0;
     |SI FSalida ! 10; |ACABA; |FINSI;

     aPath = "D*.*";
     |BROWSE aPath;

     aPath = aPath % 2; |QBF aPath;
     |SI aPath = "D"; aPath = ""; |FINSI;

     |QBF aPath;
     |SI aPath = "";
          |ERROR;
     |SINO;
          #pacondis#5 = aPath;
          |PINTA #pacondis#5;
     |FINSI;
|FPRC;

|PROCESO Serie66; |TIPO 66;
     |ABRE #caivases;
     #caivases#0 = #pacondis#Campo#;
     |SI Campo = 2;
          #caivases#1 = "R";
     |SINO;
          #caivases#1 = "S";
     |FINSI;
     |LEE 000#caivases.];
     |CONSULTA_CLAVE #1#caivases;
     |SI FSalida = 0;
          #pacondis#Campo# = #caivases#0; |PINTA #pacondis#Campo#;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #caivases;
|FPRC;

|PROCESO Serie0; |TIPO 0;
     |ABRE #caivases;
     #caivases#0 = #pacondis#Campo#;
     |SI Campo = 2;
          #caivases#1 = "R";
     |SINO;
          #caivases#1 = "S";
     |FINSI;
     |LEE 000#caivases.=;
     |SI FSalida ! 0;
          |MENAV "0000No existe serie...";
          |ERROR;
     |FINSI;
     |CIERRA #caivases;
|FPRC,

|PROCESO T80DK;  |TIPO 80;
     |SI #48#13 = 1; aLonCta = #48#14; |FINSI;
     |SI #48#13 = 2; aLonCta = #48#15; |FINSI;
     |SI #48#13 = 3; aLonCta = #48#16; |FINSI;
     |SI #48#13 = 4; aLonCta = #48#17; |FINSI;
     |SI #48#13 = 5; aLonCta = #48#18; |FINSI;
     |SI #48#13 = 6; aLonCta = #48#19; |FINSI;
     |HAZ inicio;

     |ABRE #caivaasi;
     |LEE 000#caivaasi.p;
     |CIERRA #caivaasi;

     ewNif = 0;
     enSwDeDonde = -1;
     |HAZ DirAplicSt;
     |SI HayAcceso = 1;
          enSwDeDonde = 1;
          |REFERENCIA_DEF regisnif@, #dsam0103;
          |REFERENCIA_DEF paises@, #dsam0104;
     |SINO;
          |SI Haybasica = 1;
               enSwDeDonde = 0;
               |REFERENCIA_DEF regisnif@, #agim0003;
               |REFERENCIA_DEF paises@, #agim0016;
          |FINSI;
     |FINSI;

     |SI enSwDeDonde ] 0;
          |ABRE #regisnif@;
          |ABRE #paises@;
     |FINSI;

     |ABRE #pacondis;
     |LEE 101#pacondis.p;
     |SI FSalida ! 0; |GRABA 020#pacondis.b; |FINSI;
|FPRC;

|PROCESO T90DK;  |TIPO 90;
     |GRABA 020#pacondis.a;
     |CIERRA #pacondis;

     |SI enSwDeDonde ] 0;
          |CIERRA #paises@;
          |CIERRA #regisnif@;
     |FINSI;
|FPRC;

|| PROCESOS DEL GADZ0100;

|PROCESO Placo000GADZ0100;
     |HAZ T80DK;

     |VENTANA 0501, 0540, -1, 16, "Grabando informaci¢n ...";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     |XABRE "B", eaFicDis, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     |ABRE #cacuenta;
     |ABRE #caclipro;
     |HAZ Cuentas;
     |CIERRA #caclipro;
     |CIERRA #cacuenta;

     |XCIERRA nHandle;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;
|FPRC;

|PROCESO Apunt000GADZ0100;
     |HAZ T80DK;

     |VENTANA 0501, 0540, -1, 16, "Grabando informaci¢n ...";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     nSwBal = 0;
     aAlfa  = "ti" + Usuario;
     |NOME_DAT #pacondit#aAlfa#;
     |ABRE #pacondit;  |CIERRA #pacondit;  |DELINDEX #pacondit;

     |ABRE #pacondit;
     aPath = eaPathLocalDK; |QBF aPath;
     aAlfa = aPath % -101;
     |SI aAlfa ! "/"; |Y aAlfa ! "\";
         aPath = aPath + "/";
     |FINSI;

     |XABRE "", eaFicDis, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     |ABRE #camaasil;
     |ABRE #camaasic;
     nSw = 1; |HAZ LinApunte;
     |CIERRA #camaasic;
     |CIERRA #camaasil;
     |XCIERRA nHandle;

     |XABRE "", eaFicDis, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     |ABRE #camaasil;
     |ABRE #camaasic;
     nSw = 2; |HAZ LinApunte;
     |CIERRA #camaasic;
     |CIERRA #camaasil;
     |XCIERRA nHandle;
     |CIERRA #pacondit;  |DELINDEX #pacondit;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;
|FPRC;

|PROCESO Previ000GADZ0100;
     |HAZ T80DK;

     |VENTANA 0501, 0540, -1, 16, "Grabando informaci¢n ...";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     |XABRE "", eaFicDis, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     |XCIERRA nHandle;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;
|FPRC;
