|FICHEROS;
   cacambix;
   cacambiy;

   cacambiv;
|FIN;

|VARIABLES;
   &eaDirFich = "";
   aParam   = "";
   nInkey   = 0;
   alfa1    = "";
   alfa2    = "";
   alfa3    = "";
   nume1    = 0;
   nume2    = 0;
   elsw1    = 0;
   cuenta   = "";
   nSwGraba = 0;
   nSalir   = 0;
   nNumero  = 0;
   nPara1   = 0;
   nSw      = 0;
   nOpera   = 0;
|FIN;

|INCLUYE dscont_i;

|PROCESO lee_revision1;
   nSwGraba = 0;
   |SI #cacambiy#2 = "S";
       cuenta = #cacambix#2;   |QBF cuenta;
       |SI #cacambiy#3 < #cacambiy#5;
           nume1 = 99 + #cacambiy#3;
       |SINO;
           nume1 = 99 + #cacambiy#5;
       |FINSI;
       alfa1 = cuenta % nume1;        || parte izda.
       nume1 = (#cacambiy#3 * 100) + #cacambiy#4;
       alfa3 = cuenta % nume1;        || parte a desplazar
       nume1 = #cacambiy#5 - #cacambiy#3;
       |SI nume1 < 0; nume1 = 0; |FINSI;
       alfa2 = &32 * nume1;           || relleno de espacios
       cuenta = alfa1 + alfa2 + alfa3;

       #cacambix#2 = cuenta;
       nSwGraba = 1;
   |FINSI;
   |SI #cacambiy#6 = "S";
       cuenta = #cacambix#2;   |QBF cuenta;
       alfa1 = cuenta % 0;
       nume2 = alfa1;

       nume1 = 99 + #cacambiy#7;
       alfa1 = cuenta % nume1;         || parte izda.
       nume1 = #cacambiy#8;
       alfa2 = "0" * nume1;            || relleno de ceros
       nume1 = -((nume2 - (#cacambiy#7 + #cacambiy#8 - 1)) + 100);
       alfa3 = cuenta % nume1;         || parte dcha.
       cuenta = alfa1 + alfa2 + alfa3;

       #cacambix#2 = cuenta;
       nSwGraba = 1;
   |FINSI;
   |SI nSwGraba = 1;
       |GRABA 020#cacambix.a;
   |FINSI;
   |LIBERA #cacambix;
|FPRC;

|DEFBUCLE lee_revision;
   #cacambix#1;
   4;
   #cacambiy#0, 0,  10001;
   #cacambiy#1, 9, 999999;
   be;
   NULL, lee_revision1;
|FIN;

|PROCESO funcion;
   |CIERRAT #cacambix;
   |BUCLE lee_revision;
   |ABRET #cacambix;
|FPRC;

|PROCESO lee_Recuperar;
   |SI nOpera = 1;
       |SI #cacambix#2 ! #cacambix#6;
           nSw = 1;
           |ERROR10;
       |FINSI;
       |ACABA;
   |FINSI;

   alfa1 = #cacambix#6; |QBF alfa1;
   |SI alfa1 ! "";
       #cacambix#2 = #cacambix#6;
       |GRABA 020#cacambix.a;
   |FINSI;
   |LIBERA #cacambix;
|FPRC;

|DEFBUCLE lee_Recuperar;
   #cacambix#1;
   4;
   #cacambiy#0, 0,  10001;
   #cacambiy#1, 9, 999999;
   be;
   NULL, lee_Recuperar;
|FIN;

|PROCESO funcion_R;

   |CIERRAT #cacambix;
   |BUCLE lee_Recuperar;
   |ABRET #cacambix;

|FPRC;

|PROCESO tipo13; |TIPO 13;
   nInkey = FSalida;
   |ATRI R;
   |MENSA "    F12-Modif.Ctas Destino por Ampliacion Digitos. F2-Recuperar Cambio Original";
   |ATRI 0;
   |HAZ tipo11;
|FPRC;

|PROCESO tipo11;

   |SI nInkey = 9;

       |PUSHV 05012380;
       |PINPA #0#cacambiy;
       |PINDA #0#cacambiy;

       |ENDATOS #1#cacambiy;
       |SI FSalida = 0;|Y #cacambiy#9 = "SI";
           elsw1 = 0;
           |SI #cacambiy#2 = "S"; |O #cacambiy#6 = "S";
               |ATRI P; |MENSA "    Modificando ..."; |ATRI 0;
               |HAZ funcion;
               |BLIN 4;
               elsw1 = 1;
               |HAZ GraboAuxiliar;
           |FINSI;
       |FINSI;
       |POPV;
       |ERROR;
       |SI elsw1 = 1;  |PTEC 806; |FINSI;
   |FINSI;

   |SI nInkey = 10;
       elsw1 = 1;
       |ATRI P; |MENSA "    Recuperando Primeros Datos ..."; |ATRI 0;
       |HAZ funcion_R;
       |BLIN 4;
       |ERROR;
       |PTEC 806;
   |FINSI;
|FPRC;

|PROCESO min;
   #cacambix#4 = 10001;
|FPRC;

|PROCESO max;
   #cacambix#4 = nNumero;
|FPRC;

|| ... Nuevo para tener la pantalla grabada
|PROCESO GraboAuxiliar;
   |ABRE #cacambiv;
   #cacambiv#9 = enEmpresa;
   |LEE 141#cacambiv.=;
   |SI FSalida ! 0;
       |DDEFECTO #cacambiv;
       #cacambiv#9 = enEmpresa;
       |GRABA 020#cacambiv.b;
   |FINSI;

   |PARA nPara1 = 0; |SI nPara1 [ 8; |HACIENDO nPara1 = nPara1 + 1;
         #cacambiv#nPara1# = #cacambiy#nPara1#;
   |SIGUE;
   |GRABA 020#cacambiv.a;
   |LIBERA #cacambiv;
   |CIERRA #cacambiv;
|FPRC;

|PROCESO LeoAuxiliar;
   |ABRE #cacambiv;
   #cacambiv#9 = enEmpresa;
   |LEE 041#cacambiv.=;
   |SI FSalida ! 0;
       |DDEFECTO #cacambiv;
       #cacambiv#9 = enEmpresa;
       |GRABA 020#cacambiv.n;
   |FINSI;

   |PARA nPara1 = 0; |SI nPara1 [ 8; |HACIENDO nPara1 = nPara1 + 1;
         #cacambiy#nPara1# = #cacambiv#nPara1#;
   |SIGUE;
   #cacambiy#9 = "NO";
   |SI aParam = "AUTO"; #cacambiy#9 = "SI"; |FINSI;
   |CIERRA #cacambiv;
|FPRC;

|PROGRAMA;
   aParam = PARAMETRO; |QBF aParam;

   |SI aParam ! "AUTO";
       |CLS;
       |CABEZA "Revision Cambio";
       |PINPA #0#cacambix;
   |FINSI;

   |SI aParam = ""; |O aParam = "AUTO";
       |HAZ inicio;
   |SINO;
       |PATH_DAT #cacambix#eaDirFich#;
       |PATH_DAT #cacambiy#eaDirFich#;

       alfa1 = "Cambio en la Empresa del Ejercicio " + enEjercicio;
       |ATRI P; |PINTA 0602, alfa1; |ATRI 0;
   |FINSI;

   nNumero = -1;
   |ABRE #cacambix;
   |SELECT #3#cacambix;
   |LEE 000#cacambix.u;
   |SI FSalida = 0;|Y #cacambix#4 ] 10001;
        nNumero = #cacambix#4;
   |FINSI;
   |CIERRA #cacambix;

   |SI nNumero = -1;
       |SI aParam ! "AUTO"; |MENAV "    Preparacion Nula ..."; |FINSI;
       |ACABA;
   |FINSI;

   nSw = 0;
   |SI aParam ! "AUTO"
       nOpera = 1; |BUCLE lee_Recuperar; nOpera = 0;
   |FINSI;

   |SI aParam = "AUTO"; |O nSw = 0;
       |HAZ LeoAuxiliar;
   |FINSI;

   |SI aParam ! "AUTO";
       nSalir = 0;
       |PARA; |SI nSalir = 0; |HACIENDO;
              elsw1 = 0;
              |ENTLINEAL #3#cacambix, -1, 5, 22, 0, min, max;
              |SI elsw1 = 0;
                  |CONFI "    STabla de Cambios Correcta S/N";
                  |SI FSalida = 0;
                      nSalir = 1;
                  |FINSI;
              |FINSI;
       |SIGUE;
   |SINO;
       |BUCLE lee_Recuperar;
       |BUCLE lee_revision;
   |FINSI;
|FPRO;
