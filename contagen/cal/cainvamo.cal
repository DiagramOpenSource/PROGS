|FICHEROS;
   cainvamo #00;
   cacuenta #01;
   caparbal #02;
   cabaexp0 #03;
   cabaexp2 #05;

   caexistc #06;
   caexistl #07;
   :/modelos/def/dsmom041 #10;

   camoauxc #11;
   camoauxl #12;
   cauxinva #20;   ||Temporal listados inventario
   cauxinv2 #21;   ||Temporal listados inventario 2 Cabecera
   canempre #30;

    ca8wb001 #63;         || temporal Totales Texto
    cabaexp1 #64;         || temporal existencias
    ca8wb002 #65;         || temporal Cuentas
    ca8wb003 #62;         || temporal Cuentas
    cuentas@ #901;
    ca8comay #401;        || Control de Plan
    ca8cumay #410;        || Control de Balances
    ca8m0000 #420;        || Descripcion Numero de Balance
    ca8m0001 #421;        || Def Balance Textos
    ca8m0002 #422;        || Def Balance Linea Cuentas
    ca8m0011 #431;        || Reparto 8 y 9 cabera
    ca8m0012 #432;        || Reparto 8 y 9 lineas
    ca8m0003 #403;        || Igualdad
|FIN;

|VARIABLES;
   &entrada = 1;
   alfa1 = "";
   sw_pasa = 0;
   linea = 0;
   FEstado = 0;

   saldo  = 0;
   activo = 0;
   pasivo = 0;
   explo  = 0;
   debe   = 0;
   debe1  = 0;
   debe2  = 0;
   debe3  = 0;
   debe4  = 0;
   haber  = 0;
   haber1 = 0;
   haber2 = 0;
   haber3 = 0;
   haber4 = 0;

   x = 0;
   y = 0;
   sw = 0;
   agrupaci = 0;
   subagrup = 0;
   epigrafe = 0;
   partida = 0;
   sw_epi = 0;
   informe  = "cauxinva";
   d = "A";
   h = "P";
   tipo = "";
   cue_perdi = "";
   dig_perdi = 0;
   act_cuent = "";
   act_nivel = 0;
   sw_error  = 0;
   swNOAmor = 0;

   &eldesde = 0;
   &elhasta = 0;

   &sw_inf = 0;
   &cantidad = 0;
   &variable = "";

   &canti = 0;
   &doh = "";

   des_exi = 0;
   has_exi = 0;
   des_ele = "";
   has_ele = "";
   swt_amort = 0;

   descar = 0;
   import = 0;

   &elem  = "";
   &desel = "";
   &can   = 0;

   &aux_letra = 0;
   &aux_desc  = "";
   &aux_imp   = 0;
   mensaje   = "";

   sw_exp = 0;
   &r_emp = 0;
   &r_per = 0;
   &r_eje = 0;
   &r_nom = "";
   &d_mes = 0;
   &h_mes = 0;
   &estru = 0;

   aFichero = "";
   nNume1   = 0;
   nCampo   = 0;
   nHay     = 0;
   nBalance = 0;
   nBalNum  = 0;

   nImpoOri = 0;
   nImporte = 0;
   nLong    = 0;
   nLongT   = 0;
   aTitulo  = "";
   aMas     = "";
   aDef     = "";
   aAPR     = "";
   aCuenta  = "";
   aAlfa1   = "";
   aAlfa2   = "";

   acumula = 0;
   e1 = 0;
   e2 = 0;
   e3 = 0;
   e4 = 0;
   nCampo2 = 0;
   aCampo3 = "";
   nCampo4 = 0;
   nCampo5 = 0;
   nCampo6 = 0;
   nCampo7 = 0;
   nCampo8 = 0;
   nSI      = 0;
   aCtaM    = "";
   aDCta    = "";
   aHCta    = "";
   nNivel   = 0;
   nivel    = 0;
   nCam1    = 0;
   nCam2    = 0;
   &pathbal = "";
   &balance = 1;
   mes_fin  = 0;
   tracan5  = 0;
   aSigno   = "";
   descn    = 0;
   swexis   = 0;
   nDCampo5 = 0;
   nDCampo6 = 0;
   nDCampo7 = 0;
   nDCampo8 = 0;
   nHCampo5 = 0;
   nHCampo6 = 0;
   nHCampo7 = 0;
   nHCampo8 = 0;
   nOpera   = 0;
   nImprimir = 0;

   aPath08   = "";
   dirfichO  = "";
   nEjer     = 0;
   nPeriodo  = 0;
   aCta08    = 0;
   aCta90    = 0;
   nEjerCam  = 0;
   nSwEjer   = 0;
   nPara1    = 0;
|FIN;

|INCLUYE dscont_i;

|PROCESO Elinicio; |TIPO 8;
 swNOAmor = 0;
 |HAZ inicial;
 |SI swerror = 2; |PTEC 807; |ACABA; |FINSI;
 |SI swerror = 3; |PTEC 831; |PTEC 807; |ACABA; |FINSI;

 |HAZ DirAplicSt;
 |SI Haymodelo ! 0;
     alfa1 = aPModelo + "fich/";
     |PATH_DAT #dsmom041 #alfa1#;
     swNOAmor = 1;
 |FINSI;
 aPath08 = "";
 |DFICE dirfichO; |QBF dirfichO;
 |SI enEjercicio = 2008; aPath08 = dirfichO; |FINSI;
 nEjer    = enEjercicio;
 nPeriodo = enPeriodo;
 |SI aPath08 = "";
    |SALVA_DATOS #30;
    |ABRE #30;
    |SELECT #4#30;
    #30#2 = enEmpresa;
    #30#26 = 8;
    |LEE 001#30=;
    |SI FSalida = 0;
        aAlfa1 = #30#2; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
        aAlfa2 = #30#3; |QBF aAlfa2; aAlfa2 = ("0"     + aAlfa2) % -101;
        |DFICB aPath08;
        aPath08 = aPath08 + aAlfa1 + aAlfa2 + "/";
    |FINSI;
    |SELECT #1#30;
    |CIERRA #30;
    |REPON_DATOS #30;
 |FINSI;
|FPRC;

|| ========================================================================
||                        CALCULOS PANTALLA
|| ========================================================================
|CALCULO estruc7; |TIPO 7;
 #0#6 = 1;
 |SI Haybasica = 1; |Y eaDepar = "S";
     #0#6 = 0;
 |FINSI;
 |PINTA #0#6;
|FCAL

|CALCULO amort; |TIPO 7;
 |C_M #0#13,1,"N";
 |SI swNOAmor = 1; |C_M #0#13,1,"S"; |FINSI;
|FCAL;

|PROCESO descri; |TIPO 6;
 |C_M #0Campo, 0, alfa1;
 |SI alfa1 = "N"; |ACABA; |FINSI;

 |ABRE #420;
 |DDEFECTO #420;
 #420#0 = #0Campo;
 |LEE 001#420=;
 |SI FSalida ! 0;
     |MENAV "    Error. No existe Numero de Balance";
     |ERROR;
 |FINSI;
 |CIERRA #420;
 nCampo = Campo + 1;
 #0nCampo = #420#1; |PINTA #0nCampo;
|FPRC;

|PROCESO pintames; |TIPO 0;
   x = Campo + 2;                || Coge el campo del nombre del mes
   |SI #0Campo ! 0; |Y #0Campo ! 13;
      NumMes = #canempre#11 + (#0Campo - 1);   || Operacion para saber que mes es
      |SI NumMes > 12; NumMes = NumMes - 12; |FINSI;
   |SINO;
      NumMes = #0Campo;
   |FINSI;
   |HAZ NombreMes;                          || Recoge el mes en letra
   #0x = NombreMes;
   |PINTA #0x;                       || Pinta el nombre del mes
   |SI Campo = 1; |Y #0#0 > #0#1;
      |MENAV "    Error de Entrada ...";
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO pintames2; |TIPO 0;
   |SI #0Campo ! 0; |Y #0Campo ! 13;
      NumMes = #canempre#11 + (#0Campo - 1);   || Operacion para saber que mes es
      |SI NumMes > 12; NumMes = NumMes - 12; |FINSI;
   |SINO;
      NumMes = #0Campo;
   |FINSI;
   |HAZ NombreMes;
   |ATRI I;
   |SI Campo =  9;  |PINTA 1027, NombreMes;  |FINSI;
   |SI Campo = 10;  |PINTA 1061, NombreMes;  |FINSI;
   |ATRI 0;
   |SI Campo = 10; |Y #0#9 > #0#10;
      |MENAV "    Error de Entrada ...";
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO estruct; |TIPO 0;
   |SI #0#6 = 0; |ACABA; |FINSI;
   |ABRE #6;
   #caexistc#0 = #0#6;
   |LEE 001#6=;
   |SI FSalida ! 0;
      |MENAV "    No existe la estructura de descarga.";
      |CIERRA #6;
      |ERROR;
      |ACABA;
   |SINO;
      cue_perdi = #caexistc#2;
      dig_perdi = #caexistc#3;
   |FINSI;
   |CIERRA #6;
|FPRC;

|CALCULO QueHago2;
 |SI #0#15 = 1;
     alfa1 = "S";
 |SINO;
     alfa1 = "N";
 |FINSI;
 |C_M #0#16,  1, alfa1;
|FCAL;

|PROCESO conexi;
   enTExi = 1;
   d_mes = #0#9;
   h_mes = #0#10;
   estru = #0#6;
   r_emp = empre;
   r_per = ejerc;
   r_eje = #canempre#26;
   r_nom = #canempre#1;
   |SUB_EJECUTA "caexicon.run";
|FPRC;

|PROCESO MiroSiHay;
  |SI nHay = -1; nHay = 0; |FINSI;
  nNume1 = 0;
  |PARA nPara1 = 9; |SI nPara1 [ 47; |HACIENDO nPara1 = nPara1 + 1;
        |SI #1nPara1 ! 0; nNume1 = 1; |SAL; |FINSI;
  |SIGUE;
  |SI nNume1 ! 0; nHay = 1; |ERROR10; |FINSI;
|FPRC;

|DEFBUCLE MiroSiHay;
   #1#1;
   3;
   "8           ", "S";
   "999999999999", "S";
   ;
   NULL, MiroSiHay;
|FIN;


|PROCESO conexi8y9;
  nHay = -1;
  |BUCLE MiroSiHay;
  |SI nHay = 1;
      d_mes = #0#0;
      h_mes = #0#1;
      |SUB_EJECUTA "ca8z0011;BALANCEN";
  |FINSI;
|FPRC;
|| ========================================================================
||                     CALCULOS de FICHEROS AUXILIARES
|| ========================================================================
|PROCESO grabatra;

   |SI #cacuenta#5 = "D"; |O #cacuenta#5 = "H"; |ACABA; |FINSI;

   || ... Graba temporal de Agrupaciones Exp0;
   |SI saldo = 0; |ACABA; |FINSI;
   #cabaexp0#0 = #cacuenta#5;
   #cabaexp0#1 = #cacuenta#4;
   #cabaexp0#2 = #cacuenta#6;
   #cabaexp0#3 = #cacuenta#7;
   #cabaexp0#4 = #cacuenta#8;
   |LEE 101#3=;
   |SI FSalida ! 0;
      |DDEFECTO #3;
      #cabaexp0#0 = #cacuenta#5;
      #cabaexp0#1 = #cacuenta#4;
      #cabaexp0#2 = #cacuenta#6;
      #cabaexp0#3 = #cacuenta#7;
      #cabaexp0#4 = #cacuenta#8;
      |HAZ leexplot;
      #cabaexp0#5 = #caparbal#2;
      #cabaexp0#6 = #caparbal#4;
      #cabaexp0#7 = #caparbal#6;
      |GRABA 020#3c;
   |FINSI;
   #cabaexp0#8 = #cabaexp0#8 + saldo;
   |GRABA 020#3a;
   |LIBERA #3;

   || ... Graba Auxiliar de Cuentas.
   #cabaexp2#0 = #cacuenta#5;
   #cabaexp2#1 = #cacuenta#4;
   #cabaexp2#2 = #cacuenta#6;
   #cabaexp2#3 = #cacuenta#7;
   #cabaexp2#4 = #cacuenta#8;
   #cabaexp2#5 = #cacuenta#0;
   #cabaexp2#6 = #cacuenta#1;
   #cabaexp2#7 = #cacuenta#2;
   |LEE 000#5=;
   |SI FSalida ! 0;
      |DDEFECTO #5;
      #cabaexp2#0 = #cacuenta#5;
      #cabaexp2#1 = #cacuenta#4;
      #cabaexp2#2 = #cacuenta#6;
      #cabaexp2#3 = #cacuenta#7;
      #cabaexp2#4 = #cacuenta#8;
      #cabaexp2#5 = #cacuenta#0;
      #cabaexp2#6 = #cacuenta#1;
      #cabaexp2#7 = #cacuenta#2;
      |HAZ leexplot;
      #cabaexp2#8 = #caparbal#2;
      #cabaexp2#9 = #caparbal#4;
      #cabaexp2#10 = #caparbal#6;
      #cabaexp2#11 = 0;
      |GRABA 020#5c;
   |FINSI;

   #cabaexp2#11 = #cabaexp2#11 + saldo;
   |GRABA 020#5a;
   |LIBERA #5;


   || ....... compara acumulados amortizaciones;
   |HAZ amortiza;    || lee la cuenta en las fichas de amortizacion
   |SI swt_amort ! 0;
      |SI saldo ! import;
         |SI swt_amort = 1; mensaje = "0000El saldo de la Cuenta:"+#cacuenta#0+" No coincide con lo Amortizado"; |FINSI;
         |SI swt_amort = 2; mensaje = "0000El saldo de la Cuenta:"+#cacuenta#0+" No coincide con el Desglose"; |FINSI;
         |MENAV mensaje;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO exis_expl;
   descar = #caexistl#des_exi# - #caexistl#has_exi#; || iniciales - finales
   |SI descar = 0; |ACABA; |FINSI;
   |SI descar ] 0;
      act_cuent = #caexistl#5;     || cuenta descarga D
      act_nivel = #caexistl#6;     || digito descarga D
   |SINO;
      act_cuent = #caexistl#8;     || cuenta descarga H
      act_nivel = #caexistl#9;     || digito descarga H
   |FINSI;

   |SI act_cuent = #cacuenta#0; |Y act_nivel = #cacuenta#1;
      sw_exp = 1;
      |SI descar ] 0;
         debe = debe + descar;
      |SINO;
         haber = haber - descar;
      |FINSI;
      saldo = debe - haber;
   |FINSI;
|FPRC;

|DEFBUCLE exis2;
   #7#1;
   ;
   #0#6, 01;
   #0#6, 99;
   e;
   NULL, exis_expl;
|FIN;

|PROCESO exis_3000;
   sw_exp  = 1;
   saldo   = saldo + #caexistl#has_exi#;
|FPRC;

|DEFBUCLE exis1;
   #7#2;
   ;
   #0#6, #1#0;
   #0#6, #1#0;
   e;
   NULL,exis_3000;
|FIN;

|PROCESO calcular;
   saldo   = 0;
   debe    = 0;
   haber   = 0;
   sw_exp  = 0;
   alfa1   = #cacuenta#0 % 101;
   |SI alfa1 = "3";                 |BUCLE exis1; |FINSI;
   |SI alfa1 = "6"; |O alfa1 = "7"; |BUCLE exis2; |FINSI;

   |SI sw_exp = 1; |ACABA; |FINSI;

   |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
      y = x + 1;
      debe  = debe  + #cacuenta#x#;
      haber = haber + #cacuenta#y#;
      saldo = debe - haber;
   |SIGUE;
|FPRC;

|PROCESO grabar;
   |SI #cacuenta#0 ! cue_perdi; || no es perdidas y ganancias
      |HAZ calcular;
      saldo = saldo ! EURODB;
      |SI saldo ! 0;
         |SI #cacuenta#5 = "H"; |O #cacuenta#5 = "P";
            saldo = - saldo;
         |FINSI;
         |HAZ grabatra;
         |HAZ sumasaldo;
      |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE cainvamo0;
   #1#1;
   3;
   "            ", "S";
   "zzzzzzzzzzzz", "S";
   e;
   NULL, grabar;
|FIN;

|| ................................................

|PROCESO sumasaldo;    || Calculamos cada cuenta
   |SI #cacuenta#5 = "A"; activo = activo + saldo; |FINSI;
   |SI #cacuenta#5 = "P"; pasivo = pasivo + saldo; |FINSI;
   |SI #cacuenta#5 = "D";
      |SI #cacuenta#4 = 1;
         debe1 = debe1 + saldo;
      |FINSI;
      |SI #cacuenta#4 = 2;
         debe2 = debe2 + saldo;
      |FINSI;
      |SI #cacuenta#4 = 3;
         debe3 = debe3 + saldo;
      |FINSI;
      |SI #cacuenta#4 = 4;
         debe4 = debe4 + saldo;
      |FINSI;
   |FINSI;
   |SI #cacuenta#5 = "H";
      |SI #cacuenta#4 = 1;
         haber1 = haber1 + saldo;
      |FINSI;
      |SI #cacuenta#4 = 2;
         haber2 = haber2 + saldo;
      |FINSI;
      |SI #cacuenta#4 = 3;
         haber3 = haber3 + saldo;
      |FINSI;
      |SI #cacuenta#4 = 4;
         haber4 = haber4 + saldo;
      |FINSI;
   |FINSI;
|FPRC;

|| ..................................

|PROCESO gr_final; || Grabar totales
   debe = 0;
   haber = 0;
   #cacuenta#0 = cue_perdi;
   #cacuenta#1 = dig_perdi;
   |LEE 001#1=;
   |SI FSalida ! 0;
      sw_error = 1;
      |MENAV "    ATENCION !!!. NO existe la cuenta de Perdidas y Ganancias ";
      |ACABA;
   |FINSI;
   |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
      y = x + 1;
      debe  = debe  + #cacuenta#x#;
      haber = haber + #cacuenta#y#;
      saldo = debe - haber;
   |SIGUE;
   debe  = debe1  + debe2  + debe3  + debe4;
   haber = haber1 + haber2 + haber3 + haber4;
   saldo = saldo + ( debe - haber);
   |SI #cacuenta#5 = "P";
      saldo = - saldo;
   |FINSI;
   |HAZ grabatra; || ....................... Graba la cuenta 129. como otra mas.
   |SI #cacuenta#5 = "A"; activo = activo + saldo; |FINSI;
   |SI #cacuenta#5 = "P"; pasivo = pasivo + saldo; |FINSI;
|FPRC;

|| .......................................
|PROCESO leexplot; || lee t¡tulos balance
   #caparbal#0 = #cabaexp0#0;
   #caparbal#1 = #cabaexp0#2;
   #caparbal#3 = #cabaexp0#3;
   #caparbal#5 = #cabaexp0#4;
   |LEE 040#2=; |SI FSalida ! 0; |DDEFECTO #2; |FINSI;
|FPRC;

|| ***************************************************************************
|| *********************  CALCULO DE IMPRESION *******************************
|| ***************************************************************************
|PROCESO mira_aux;
   |SI swt_amort = 0;
      sw_inf = 10;
      aux_letra = 2;
      aux_desc = "Descripcion                                    Importe";
      swt_amort = 2;
      aux_imp = 0;
      |HAZ graba_aux;
      |SI #0#11 = "S"; |PRINT; |FINSI;
   |FINSI;
   can   = #camoauxl#6;
   desel = #camoauxl#3;
   elem  = "";
   aux_letra = 5;
   aux_desc  = elem + desel;
   aux_imp   = #camoauxl#6;
   |HAZ graba_aux;
   |SI #0#11 = "S"; |PRINT; |FINSI;
|FPRC;

|PROCESO imp_amort;

   |SI #dsmom041#27 > "01.01.0000"; |Y #dsmom041#27 > fec2;
       |ACABA;
   |FINSI;

   aAlfa1 = #dsmom041#27;
   aAlfa1 = aAlfa1 % -104;
   nNume1 = aAlfa1;
   |SI nSwEjer ! 0;
       |SI nSwEjer = 1;
           |SI nNume1 < 2008; |ACABA; |FINSI;
       |SINO;
           |SI nNume1 ] 2008; |ACABA; |FINSI;
       |FINSI;
   |FINSI;

   |SI enConverModelo ! 0;
       #dsmom041#10 = (#dsmom041#10 * enConverModelo ) ! enRedonModelo;
   |FINSI;

   |SI swt_amort = 0;
       swt_amort = 1;
       sw_inf = 10;
       aux_letra = 2;
       aux_desc = "Descripcion                                    Importe";
       aux_imp = 0;
       |HAZ graba_aux;
       |SI #0#11 = "S"; |PRINT; |FINSI;
   |FINSI;
   sw_inf = 9;
   can   = #dsmom041#10;
   elem  = #dsmom041#2;
   desel = #dsmom041#4;
   aux_letra = 5;
   aux_desc  = elem + "  " + desel;
   aux_imp   = #dsmom041#10 ! EURODB;
   |HAZ graba_aux;
   |SI #0#11 = "S"; |PRINT; |FINSI;
|FPRC;

|DEFBUCLE amort1;
   #dsmom041#4;
   25;
   enEmpresa, aCuenta, des_ele;
   enEmpresa, aCuenta, has_ele;
   e;
   NULL, imp_amort;
|FIN;

|PROCESO MiraAux10;
   |BUCLELIN 2mira_aux#11;
|FPRC;

|DEFBUCLE MiraAux10;
  #camoauxc#1;
  ;
  "  ", #cabaexp2#5;
  "zz", #cabaexp2#5;
  e;
  NULL, MiraAux10;
|FIN;

|PROCESO busca_amort;
   nEjerCam  = enEjercicio;
   eaCuenta  = #5#5;
   |HAZ CamCuenta;
   swt_amort = 0;
   import    = 0;
   |SI swNOAmor = 1;
       |SI aCta08 ! aCta90;
           nSwEjer = 1; aCuenta = aCta08; |BUCLE amort1;
           nSwEjer = 2; aCuenta = aCta90; |BUCLE amort1;
       |SINO;
           nSwEjer = 0; aCuenta = #5#5; |BUCLE amort1;
       |FINSI;
   |FINSI;

   |SI swt_amort = 0;
       |SI #cainvamo#6 ! 0;
           #camoauxc#0 = #cabaexp2#5;
           aAlfa1 = ("00" + #cainvamo#6) % -102;
           #camoauxc#6 = aAlfa1;
           |LEE 001#11=;
           |SI FSalida = 0;
               |BUCLELIN 2mira_aux#11;
           |FINSI;
       |SINO;
           |CIERRA #camoauxc;
           |BUCLE MiraAux10;
           |ABRE #camoauxc;
       |FINSI;
   |FINSI;
|FPRC;

|PROCESO cuentas;
   |HAZ busca_amort;

   |SI #cabaexp2#11 ! 0;
      cantidad = #cabaexp2#11;
      sw_inf = 6;
      aux_letra = 3;
      aux_desc = #cabaexp2#5 + #cabaexp2#7;
      aux_imp = cantidad;
      |HAZ graba_aux;
      |SI #0#11 = "S"; |PRINT; |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE cainvamo2;
   #5#1;
   ;
   #3#0, #3#1, #3#2, #3#3, #3#4, "            ", 0;
   #3#0, #3#1, #3#2, #3#3, #3#4, "zzzzzzzzzzzz", 9;
   e;
   NULL, NULL, NULL, NULL, NULL, cuentas;
   #5#5, #5#6;
|FIN;

|| *****************************************************************

|PROCESO canv_tipo;
   |SI #0#15 = 2;
       sw_epi = 0;
       tipo     = #cabaexp0#0;
       agrupaci = #cabaexp0#2;
       epigrafe = #cabaexp0#3;
       partida  = 0;
   |SINO;
       tipo     = #63#3;  ||Activo o Pasivo
       agrupaci = #63#4;
       subagrup = #63#5;
       partida  = 0;
   |FINSI;

   |SI tipo = "A"; doh = "ACTIVO"; |FINSI;
   |SI tipo = "P"; doh = "PASIVO"; |FINSI;
   aux_letra = 0;    || Graba Cabecera ACTIVO/PASIVO
   aux_desc = doh;
   aux_imp = 0;
   |HAZ graba_aux;
|FPRC;

|| ----- IMPRESION
|PROCESO impresio;

   |SI sw = 0;
      |HAZ canv_tipo;
      |HAZ agrupacion;
      sw = 1;
   |FINSI;

   |SI tipo ! #cabaexp0#0;
      |HAZ final;
      |HAZ canv_tipo;
      |HAZ agrupacion;
   |FINSI;

   |HAZ epigraf;
|FPRC;

|PROCESO epigraf;
   |SI agrupaci ! #cabaexp0#2;
      |HAZ gr_blanco;
      |HAZ agrupacion;
      agrupaci = #cabaexp0#2;
   |FINSI;
   |HAZ rotura;
   sw_epi = 0;
   epigrafe = #cabaexp0#3;
|FPRC;

|PROCESO rotura;
   |SI epigrafe ! #cabaexp0#3; |O sw_epi = 1;
      |SI #cabaexp0#3 ! 0;
         sw_inf = 1;  || epigrafe  ... | sin importe

         aux_letra = 1;
         aux_desc = #cabaexp0#6;
         aux_imp = 0;
         |HAZ graba_aux;
         |SI #0#11 = "S"; |PRINT; |FINSI;
      |FINSI;
   |FINSI;
   |HAZ partida;                                       || partida
   |HAZ detalle;                                       || cuentas
|FPRC;

|PROCESO agrupacion;
   sw_epi = 1;                || fuerza la impresion del epigrafe
   |SI #cabaexp0#3 = 0;              || epigrafe=0
      sw_inf = 7;              || agrupacion (con importe)
      aux_letra = 9;
      aux_desc = #cabaexp0#5;
      aux_imp = #cabaexp0#8;
      |HAZ graba_aux;
      |SI #0#11 = "S"; |PRINT; |FINSI;
   |SINO;
      sw_inf = 0;            || agrupacion (sin importe)
      aux_letra = 9;
      aux_desc = #cabaexp0#5;
      aux_imp = 0;
      |HAZ graba_aux;
      |SI #0#11 = "S"; |PRINT; |FINSI;
   |FINSI;
|FPRC;

|PROCESO partida;        || imprime titulo de partidas
   |SI #cabaexp0#4 ! 0;
      sw_inf = 3;
      aux_letra = 1;
      aux_desc = #cabaexp0#7;
      aux_imp = #cabaexp0#8;
      |HAZ graba_aux;
      |SI #0#11 = "S"; |PRINT; |FINSI;
   |FINSI;
|FPRC;

|PROCESO detalle;        || imprime detalle de cuentas
   |BUCLE cainvamo2;
   aux_letra = 8;
   aux_desc = "...................................................";
   aux_imp = 0;
   |HAZ graba_aux;
   |HAZ gr_blanco;
|FPRC;

|PROCESO final;
   |SI tipo = "A";
      sw_inf = 6;
      variable = "T O T A L   A C T I V O ..............................:";
      canti = activo;
   |SINO;
      sw_inf = 6;
      variable = "T O T A L   P A S I V O ..............................:";
      canti = pasivo;
   |FINSI;

   aux_letra = 6;
   aux_desc = variable;
   aux_imp = canti;

   |HAZ graba_aux;
   aux_letra = 7;
   aux_desc = "";
   aux_imp = 0;
   |HAZ graba_aux;
   |SI #0#11 = "S";  |PIEINF; |FINSI;
|FPRC;

|DEFBUCLE cainvamo1;
   #3#1;
   ;
   d, 0, 00, 00, 00;
   h, 0, 99, 99, 99;
   e;
   NULL, impresio;
|FIN;
|| ////////////////////////////////////////
|PROCESO HaySaldoSI;
  |SI #65#13 ! 0;
      nHay = 1;
      |ERROR10;
  |FINSI;
|FPRC;

|DEFBUCLE HaySaldoSI;
 #65#1;
 ;
 #63#0, #63#1, #63#2, #63#3, #63#4, nDCampo5, nDCampo6, nDCampo7, nDCampo8, "            ";
 #63#0, #63#1, #63#2, #63#3, #63#4, nHCampo5, nHCampo6, nHCampo7, nHCampo8, "zzzzzzzzzzzz";
 e;
 NULL, HaySaldoSI;
|FIN;

|PROCESO HaySaldo;
   nDCampo5 = #63#5; nHCampo5 = #63#5;
   |SI #63#5 = 0;
       nDCampo5 = 0;
       nHCampo5 = 9;
   |FINSI;
   nDCampo6 = #63#6; nHCampo6 = #63#6;
   |SI #63#6 = 0;
       nDCampo6 = 00;
       nHCampo6 = 99;
   |FINSI;
   nDCampo7 = #63#7; nHCampo7 = #63#7;
   |SI #63#7 = 0;
       nDCampo7 = 00;
       nHCampo7 = 99;
   |FINSI;
   nDCampo8 = #63#8; nHCampo8 = #63#8;
   |SI #63#8 = 0;
       nDCampo8 = 0;
       nHCampo8 = 9;
   |FINSI;
   nHay = 0;
   |BUCLE HaySaldoSI;
|FPRC;

|PROCESO cuentas_n;
  #5#5     = #65#10;
  #5#6     = #65#11;
  |HAZ busca_amort;
  |SI #65#13 ! 0;
      cantidad  = #65#13;
      sw_inf    = 6;
      aux_letra = 3;
      aux_desc  = #65#10 + #65#11;
      aux_imp   = cantidad;
      |HAZ graba_aux;
      |SI #0#11 = "S"; |PRINT; |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE cabasitu2;
 #65#1;
 ;
 #63#0, #63#1, #63#2, #63#3, #63#4, #63#5, #63#6, #63#7, #63#8, "            ";
 #63#0, #63#1, #63#2, #63#3, #63#4, #63#5, #63#6, #63#7, #63#8, "zzzzzzzzzzzz";
 e;
 NULL, cuentas_n;
|FIN;

|PROCESO agrupacion_n;
  sw_epi = 1;                || fuerza la impresion del epigrafe
|FPRC;

|PROCESO impresio_n;

  |SI #63#9 ! "A"; nImprimir = 0; |FINSI;

  |SI #63#13 = 0; |Y #63#9 ! "A";
      nOpera = 0;
      |SI eaValeCero = "N"; nOpera = 1; |FINSI;
      |SI #63#0 = "A"; |Y activo ! 0; nOpera = 1; |FINSI;
      |SI #63#0 = "P"; |Y pasivo ! 0; nOpera = 1; |FINSI;
      |SI #63#0 = tipo;               nOpera = 1; |FINSI;
      |SI nOpera = 1;
          nHay = 0;
          |HAZ HaySaldo;
          |SI nHay = 0; |ACABA; |FINSI;
      |FINSI;
  |FINSI;

  |SI sw = 0;
      |HAZ canv_tipo;
      |HAZ agrupacion_n;
      sw = 1;
  |FINSI;

  |SI tipo ! #63#3;
      |HAZ final;
      |HAZ canv_tipo;
      |HAZ agrupacion;
  |FINSI;

  |SI agrupaci ! #63#4; |O subagrup ! #63#5;
      |HAZ gr_blanco;       || linea en blanco
      |HAZ agrupacion;
      agrupaci = #63#4;
      subagrup = #63#5;
  |FINSI;

  |SI #63#9 = "C"; |O #63#9 = "S"; |O #63#9 = "T";
      |SI #63#7 ! 0; sw_inf = 2; |FINSI;
      |SI #63#7 = 0; sw_inf = 0; |FINSI;
  |FINSI;
  |SI #63#9 = "A"; sw_inf = 7; |FINSI;
  |SI #63#9 ! "A"; |O nImprimir = 1;
      sw_inf = 3;
      aux_letra = 1;
      aux_desc = #63#10;
      aux_imp  = #63#13;
      |HAZ graba_aux;
      |SI #0#11 = "S"; |PRINT; |FINSI;
      nImprimir = 1;
  |FINSI;

  |SI #63#9 = "C";
      sw_inf=3;
      nOpera = 0; |BUCLE cabasitu2;
  |FINSI;
|FPRC;

|DEFBUCLE cabasitu1;
 #63#1;
 ;
 nBalNum, nBalance, 0, "A", 00, 0, 00, 00, 0;
 nBalNum, nBalance, 0, "P", 99, 9, 99, 99, 9;
 e;
 NULL,impresio_n;
|FIN;

|| ***************************************************************************
|| *********************  GRABACION DE TEMPORALES Y CALCULOS *****************
|| ***************************************************************************
|PROCESO MSigno_sLeo;
  #410#12 = enEjerBal;
  #410#0  = aCtaM;
  #410#10 = eNum2008;
  |LEE 001#410=;
  |SI FSalida = 0;
      nSI  = 1;
      aAPR = #410#3;
  |FINSI;
|FPRC;

|PROCESO MSigno08_s;
  nSI = 0;
  aCtaM = aCuenta % 105;
  |HAZ MSigno_sLeo;
  |SI nSI = 1; |ACABA; |FINSI;

  aCtaM = aCuenta % 104;
  |HAZ MSigno_sLeo;
  |SI nSI = 1; |ACABA; |FINSI;

  aCtaM = aCuenta % 103;
  |HAZ MSigno_sLeo;
  |SI nSI = 1; |ACABA; |FINSI;

  aCtaM = aCuenta % 102;
  |HAZ MSigno_sLeo;
|FPRC;

|| .............................................................
|PROCESO cargatra_3;  ||Carga Trabajo de Cuentas
  #63#0 = nBalNum;
  #63#1 = nBalance;
  #63#2 = nCampo2;
  #63#3 = aCampo3;
  #63#4 = nCampo4;
  #63#5 = nCampo5;
  #63#6 = nCampo6;
  #63#7 = nCampo7;
  #63#8 = nCampo8;
  |LEE 101#63=;
  |SI FSalida = 0; |Y #63#9 ! "A";
      #63acumula = #63acumula + nImporte;
      |GRABA 020#63a;
  |FINSI;
  |LIBERA #63;
|FPRC;

|PROCESO LosTotales;
  nCampo2 = #65#2;
  aCampo3 = #65#3;
  nCampo4 = #65#4;
  nCampo5 = #65#5;
  nCampo6 = #65#6;
  nCampo7 = #65#7;
  nCampo8 = #65#8;

  |HAZ cargatra_3;

  |SI nCampo8 ! 0;    ||Subpartida (A/P = 0)
      nCampo8 = 0;
      |HAZ cargatra_3;
  |FINSI;

  |SI nCampo7 ! 0;   ||Partida
      nCampo8 = 0;
      nCampo7 = 0;
      |HAZ cargatra_3;
  |FINSI;

  |SI nCampo6 ! 0;   ||Epigrafe
      nCampo8 = 0;
      nCampo7 = 0;
      nCampo6 = 0;
      |HAZ cargatra_3;
  |FINSI;

  |SI nCampo5 ! 0;  ||Subagrupacion
      nCampo8 = 0;
      nCampo7 = 0;
      nCampo6 = 0;
      nCampo5 = 0;
      |HAZ cargatra_3;
  |FINSI;

  |SI nCampo4 ! 0;  ||Agrupacion
      nCampo8 = 0;
      nCampo7 = 0;
      nCampo6 = 0;
      nCampo5 = 0;
      nCampo4 = 0;
      |HAZ cargatra_3;
  |FINSI;

  |SI aCampo3 = "A";
      activo = (activo + nImporte) ! EURODB;
  |SINO;
      pasivo = (pasivo + nImporte) ! EURODB;
  |FINSI;
|FPRC;

|PROCESO cargatra_5;  ||Carga Trabajo de Cuentas
  #65#0 = nBalNum;
  #65#1 = nBalance;
  #65#2 = #422#2;
  #65#3 = #422#3;
  #65#4 = #422#4;
  #65#5 = #422#5;
  #65#6 = #422#6;
  #65#7 = #422#7;
  #65#8 = #422#12;
  #65#9 = #422#8;
  #65#10 = aCuenta;
  |LEE 001#65=;
  |SI FSalida ! 0;
      |DDEFECTO #65;
      #65#0 = nBalNum;
      #65#1 = nBalance;
      #65#2 = #422#2;
      #65#3 = #422#3;
      #65#4 = #422#4;
      #65#5 = #422#5;
      #65#6 = #422#6;
      #65#7 = #422#7;
      #65#8 = #422#12;
      #65#9 = #422#8;
      #65#10 = aCuenta;
      |GRABA 020#65b;
  |FINSI;

  #65#11 = aTitulo;
  #65#12 = nLong;
  #65acumula = #65acumula + nImporte;
  |GRABA 020#65a;
  |LIBERA #65;

  |HAZ LosTotales;
|FPRC;

|PROCESO Busco;
  nImpoOri = nImporte;
  nSI = 1;
  |SI #422#11 ! " ";
      |SI #422#11 = "P"; |Y nImporte < 0; |ACABA; |FINSI;
      |SI #422#11 = "N"; |Y nImporte > 0; |ACABA; |FINSI;
  |FINSI;
  |SI #422#10 = "-"; nImporte = - nImporte; |FINSI;
  |HAZ cargatra_5;
  nImporte = nImpoOri;
|FPRC;

|DEFBUCLE Busco;
 #422#2;
 ;
 enEjerBal, nBalNum, nBalance, aDCta;
 enEjerBal, nBalNum, nBalance, aHCta;
 ;
 NULL, Busco;
|FIN;

|PROCESO CargaVarios;

  nSI = 0;
  aDCta = aCuenta % 105;
  aHCta = aCuenta % 105;
  |BUCLE Busco;
||  |SI nSI = 1; |ACABA; |FINSI;

  aDCta = aCuenta % 104;
  aHCta = aCuenta % 104;
  |BUCLE Busco;
||  |SI nSI = 1; |ACABA; |FINSI;

  aDCta = aCuenta % 103;
  aHCta = aCuenta % 103;
  |BUCLE Busco;
||  |SI nSI = 1; |ACABA; |FINSI;

  aDCta = aCuenta % 102;
  aHCta = aCuenta % 102;
  |BUCLE Busco;
|FPRC;

||......................................................
|PROCESO descarga;
  linea = linea + 1;
  #64#0 = linea;
  #64#8 = #0#6;
  |LEE 001#64=;
  |SI FSalida = 0;  |BORRA 020#64a;  |FINSI;

  aCuenta = #1#0;
  nNivel  = #1#1;
  aTitulo = #1#2;
  aAPR    = #1#5; |HAZ MSigno08_s;

  |DDEFECTO #64;
  #64#0 = linea;    || linea
  #64#8 = #0#6;     || estructura
  #64#1 = #1#0;     || cuenta
  #64#2 = #1#1;     || digito
  #64#3 = #1#2;     || descripcion

  mes_fin   = #0#9 + 11;  || mes iniciales
  #64e1     = #7mes_fin;    || exist. iniciales
  mes_fin   = #0#10 + 11;  || mes finales
  #64e2     = #7mes_fin;    || exist. finales
  tracan5   = #7mes_fin;

  descar = (#64e1 - #64e2) ! EURODB; || ... iniciales - finales
  |SI descar ] 0;
      #64e3 = #7#5;     || cuenta descarga D
      #64e4 = #7#6;     || digito descarga D
  |SINO;
      #64e3 = #7#8;     || cuenta descarga H
      #64e4 = #7#9;     || digito descarga H
  |FINSI;
  |GRABA 020#64n;

  nImporte = tracan5;
  |SI nImporte ! 0; |HAZ CargaVarios; |FINSI;
|FPRC;

|PROCESO lineades;
  #1#0 = #7#2;
  #1#1 = #7#3;
  |LEE 000#1=;
  |SI FSalida = 0;
      |HAZ descarga;
  |FINSI;
|FPRC;

|PROCESO pideexis;
  |ABRE #6;
  #6#0 = #0#6;
  |LEE 001#6=;
  cue_perdi = #6#2;
  dig_perdi = #6#3;
  |BUCLELIN 2lineades#6;
  |CIERRA #6;
|FPRC;

|PROCESO Grabaw3;
 |DDEFECTO #62;
 #62#0 = aCuenta;
 |LEE 001#62=;
 |SI FSalida ! 0;
     #62#0 = aCuenta;
     |GRABA 020#62c;
 |FINSI;
 |SI aSigno = "D";
     #62#1 = #62#1 + nImporte;
 |SINO;
     #62#2 = #62#2 + nImporte;
 |FINSI;
 |GRABA 020#62a;
|FPRC;

|PROCESO reparto_lin;
 aAlfa1 = #432#2; |QBF aAlfa1;
 |SI aAlfa1 ! "";
     aCuenta  = #432#2;
     aSigno   = #431#8;
     nImporte = #432#4;
     |HAZ Grabaw3;
 |FINSI;
|FPRC;

|DEFBUCLE reparto_lin;
 #432#1;
 ;
 #431#0, 01;
 #431#0, 99;
 e;
 NULL, reparto_lin;
|FIN;

|PROCESO reparto;
 aAlfa1 = #431#2; |QBF aAlfa1;
 |SI aAlfa1 ! "";
     aCuenta  = #431#2;
     nImporte = #431#4;
     aSigno   = #431#8;
     |HAZ Grabaw3;
 |SINO;
     |BUCLE reparto_lin;
 |FINSI;
|FPRC;

|DEFBUCLE reparto;
 #431#1;
 ;
 "            ";
 "zzzzzzzzzzzz";
 e;
 NULL, reparto;
|FIN;

|PROCESO pide8y9;
  |ABRE #62;
  |BUCLE reparto;
  |CIERRA #62;
|FPRC;

|| Explotacion
|PROCESO ver_exp;
 |SI #64e3 = #1#0; |Y #64e4 = #1#1;
     sw_exp = 1;
     descar = (#64e1-#64e2);
     |SI aAPR = "H"; descar = -descar; |FINSI;
     descn = descn + descar;
 |FINSI;
|FPRC;

|DEFBUCLE cabasitu9;
 #64#1;
 ;
 #0#6, 001;
 #0#6, 999;
 e;
 NULL, ver_exp;
|FIN;

|PROCESO calexpl01;
  aCuenta = #1#0;
  nNivel  = #1#1;
  aAPR    = #1#5; |HAZ MSigno08_s;
  |SI aAPR ! "D"; |Y aAPR ! "H"; |ACABA; |FINSI;  || No es explotacion

  sw_exp = 0;
  descn  = 0;
  |BUCLE cabasitu9;
  |SI sw_exp = 1
      |SI aAPR = "D";
          debe1 = debe1 + descn;
      |SINO;
          haber1 = haber1 + descn;
     |FINSI;
  |SINO;
     |SI aAPR = "D";
         |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
               debe1  = (debe1  + #1x) ! EURODB;
         |SIGUE;
     |SINO;
         |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
               haber1 = (haber1 - #1x) ! EURODB;
         |SIGUE;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE calexpl0;
 #1#1;
 3;
 "            ", "S";
 "zzzzzzzzzzzz", "S";
 e;
 NULL,calexpl01;
|FIN;

|PROCESO explota;
  debe1  = 0;
  haber1 = 0;

  |BUCLE calexpl0;

  canti = (haber1 - debe1) ! EURODB;
  explo = canti;

  |ABRE #1;
  |DDEFECTO #1;
  #1#0 = #6#2;
  #1#1 = #6#3;
  |LEE 000#1=;
  |CIERRA #1;
  aCuenta = cue_perdi;
  nNivel  = dig_perdi;
  aAPR    = #1#5; |HAZ MSigno08_s;
  aTitulo = #1#2;

  tracan5 = - canti;
  |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
        tracan5 = tracan5 + #1x;
  |SIGUE;
  nImporte = tracan5;  ||Auque sea 0, la graba
  |HAZ CargaVarios;
|FPRC;

|PROCESO grabatra_n; || grabo todas
   nImporte = 0;
   |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
         nImporte = nImporte + #1x;
   |SIGUE;

  |SI act_cuent = "1";
      #62#0 = #1#0;
      |LEE 001#62=;
      |SI FSalida = 0;
          nImporte = nImporte + #62#1 - #62#2; ||debe menos haber
      |FINSI;
  |FINSI;

  |SI nImporte ! 0;
    |SI #422#11 ! " ";
        |SI #422#11 = "P"; |Y nImporte < 0; |ACABA; |FINSI;
        |SI #422#11 = "N"; |Y nImporte > 0; |ACABA; |FINSI;
    |FINSI;
    |SI #422#10 = "-"; nImporte = - nImporte; |FINSI;
    |HAZ cargatra_5;
  |FINSI;
|FPRC;

|PROCESO miracuen;
  |SI #64#1 = #1#0; |Y #64#2 = #1#1;        || existencias
      swexis = 1;
      cantidad = cantidad + #64#5;
  |FINSI;
|FPRC;

|DEFBUCLE cabasitu3;
 #64#1;
 ;
 #0#6, 001;
 #0#6, 999;
 e;
 NULL, miracuen;
|FIN;

|PROCESO calculit;

  aCuenta = #1#0;
  nNivel  = #1#1;
  aTitulo = #1#2;
  aAPR    = #1#5; |HAZ MSigno08_s;
  |SI aAPR ! "A"; |Y aAPR ! "P"; |ACABA; |FINSI;

  aAlfa1 = #1#0; |QBF aAlfa1;
  aAlfa1 = aAlfa1 % 0;
  nLong  = aAlfa1;
  aCuenta = #1#0;
  nNivel  = #1#1;
  aTitulo = #1#2;
  nImporte = 0;

  |SI #1#0 = #6#2;  |ACABA;  |FINSI;  || es la cuenta de perdidas y ganancias
  act_cuent = #1#0 % 101;
  swexis = 0;
  |SI act_cuent = "3"; |BUCLE cabasitu3; |FINSI;
  |SI swexis = 0;
      |HAZ grabatra_n;
  |FINSI;
|FPRC;

|DEFBUCLE cabasitu0;
 #1#1;
 3;
 aDCta, "S";
 aHCta, "S";
 e;
 NULL,calculit;
|FIN;

|PROCESO calculit_4;
 aAlfa1 = #422#9; |QBF aAlfa1;
 aDCta = (aAlfa1 + "            ") % 112;
 aHCta = (aAlfa1 + "zzzzzzzzzzzz") % 112;
 |BUCLE cabasitu0;
|FPRC;

|DEFBUCLE cabasitu4;
 #422#1;
 ;
 enEjerBal, nBalNum, nBalance, 0, "A", 00, 0, 00, 00, 0, 001;
 enEjerBal, nBalNum, nBalance, 0, "P", 99, 9, 99, 99, 9, 999;
 e;
 NULL,calculit_4;
|FIN;

|PROCESO GrabaAuxiliar;
 |SI #421#3 = "A"; |O #421#3 = "P";
     |DDEFECTO #3;
     #63#0  = #421#0;
     #63#1  = #421#1;
     #63#2  = #421#2;
     #63#3  = #421#3;
     #63#4  = #421#4;
     #63#5  = #421#5;
     #63#6  = #421#6;
     #63#7  = #421#7;
     #63#8  = #421#10;
     #63#9  = #421#8;
     #63#10 = #421#9;
     #63#17 = #421#11;
     #63#18 = #421#12;
     |GRABA 000#63n;
 |FINSI;
 |LIBERA #421;
|FPRC;

|DEFBUCLE GrabaAuxiliar;
 #421#1;
 ;
 enEjerBal, nBalNum, nBalance, 0, "A", 00, 0, 00, 00, 0;
 enEjerBal, nBalNum, nBalance, 0, "P", 99, 9, 99, 99, 9;
 e;
 NULL, GrabaAuxiliar;
|FIN;

|PROCESO Nuevo2008;
         nBalance = 0;
         nBalNum  = #0#17;
         |ABRE #63;
         |BUCLE GrabaAuxiliar;
         |CIERRA #63;

         |DBASE aMas;
         aDef = aMas + "def/cacuenta";
         |CARGA_DEF aDef, cuentas@;
         |SI FSalida ! 0;
             |MENAV "    Error al Carga fichero de Cuentas";
             |ACABA;
         |FINSI;
         |PATH_DAT #901 dirfich0;

         eldesde = (#0#0 * 3) + 11;
         elhasta = (#0#1 * 3) + 11;
         acumula = 13;
         e1 = 4;
         e2 = 5;
         e3 = 6;
         e4 = 7;

         |ABRE #1;
         |ABRE #901;
         |ABRE #410;
         |ABRE #63;
         |ABRE #64;
         |ABRE #65;
         |HAZ pideexis;        || calcula existencias y graba
         |HAZ pide8y9;
         |CIERRA #1;

         |HAZ explota;         || calcula la explotacion
         |ABRE #62;
         |BUCLE cabasitu4;     || calcula resto de cuentas
         |CIERRA #62;
         |CIERRA #65;
         |CIERRA #64;
         |CIERRA #63;
         |CIERRA #410;
         |CIERRA #901;
         |DESCARGA_DEF #901;

         |ABRE #20;     || Crear Fichero
         |CIERRA #20;
         |DELINDEX #20; || -> Lineas balance
         |HAZ cabecera; || graba cabecera del informe
         |ABRE #20;
         #cauxinva#0 = 0;

         |ABRE #11;
          sw = 0;
         |BUCLE cabasitu1;
         |HAZ final;
         |SI #0#11 = "S";
            |FININF;
            |FINIMP;
         |FINSI;
         |CIERRA #11;
         |CIERRA #20;
|FPRC;
|| *************************************************************************
|PROCESO impre; |TIPO 3;

   |SI #0#14 = "S"; |HAZ conexi; |FINSI;
   |SI #0#15 = 1; |HAZ conexi8y9; |FINSI;

   |SI #0#11 = "S";   || Impresion Directa
      |IMPRE 0;
      |SI FSalida ! 0; |ACABA; |FINSI;
      |INFOR informe;
      |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;
   |FINSI;

   |HAZ LeeConversor;

   || ... borrar los ficheros auxiliares
   aFichero = ("7t" + Usuario) % 108;
   |ABRE #3;  |CIERRA #3; |DELINDEX #3; || -> Total epigrafes

   aFichero = ("7e" + Usuario) % 108;
   |ABRE #5;  |CIERRA #5; |DELINDEX #5; || -> Total por cuentas

   aFichero = ("8t" + Usuario) % 108;
   |NOME_DAT #63 aFichero;|DELINDEX #63;

   aFichero = ("8e" + Usuario) % 108;
   |NOME_DAT #64 aFichero;|DELINDEX #64;

   aFichero = ("8c" + Usuario) % 108;
   |NOME_DAT #65 aFichero;|DELINDEX #65;

   aFichero = ("8f" + Usuario) % 108;
   |NOME_DAT #62 aFichero;|DELINDEX #62;

   || ....................................

   eldesde = 9 + #0#0 * 3;
   elhasta = 9 + #0#1 * 3;
   des_exi =  #0#9  + 11;
   has_exi =  #0#10 + 11;
   activo = 0;
   pasivo = 0;
   explo  = 0;
   debe1  = 0;
   debe2  = 0;
   debe3  = 0;
   debe4  = 0;
   haber1 = 0;
   haber2 = 0;
   haber3 = 0;
   haber4 = 0;

   des_ele = "S";
   |SI #0#13 = "S"; des_ele = "N"; |FINSI;
   has_ele = "S";

   ||... Modificar Cuentas a balance segun saldo
   r_emp = enEmpresa;
   r_per = enPeriodo;
   eldesde = 11 + #0#0 * 3;
   elhasta = 11 + #0#1 * 3;
   |SUB_EJECUTA "carecsig";
   eldesde = 9 + #0#0 * 3;
   elhasta = 9 + #0#1 * 3;
   || ---------------------------------------------------------------------
   || ---------------------------------------------------------------------
   |SI #0#15 = 1;
       |HAZ Nuevo2008;
   |SINO;

      |ABRET #1;
      |ABRE #2;
      |ABRE #3;
      |ABRE #5;
      |ABRET #10;
      |ABRE #11;

      |BUCLE cainvamo0;      || Bucle de Calculo todas las cuentas
      |HAZ gr_final;         || Graba Totales

      |CIERRAT #1;
      |CIERRA #2;
      |CIERRA #3;
      |CIERRA #5;
      |CIERRAT #10;
      |CIERRA #11;
   || ************************************************************************
      |SI sw_error = 0;
         |ABRE #20;     || Crear Fichero
         |CIERRA #20;
         |DELINDEX #20; || -> Lineas balance
         |HAZ cabecera; || graba cabecera del informe
         |ABRE #20;
         #cauxinva#0 = 0;

         |ABRE #11;
         |BUCLE cainvamo1; || Imprimir y Grabar
         |HAZ final;
         |SI #0#11 = "S";
            |FININF;
            |FINIMP;
         |FINSI;
         |CIERRA #11;
         |CIERRA #20;
      |FINSI;

   |FINSI;

   |DELINDEX #3;
   |DELINDEX #5;
   |DELINDEX #62;
   |DELINDEX #63;
   |DELINDEX #64;
   |DELINDEX #65;
|FPRC;

|| **********************************************************************
|PROCESO ver_amort;

   |SI #dsmom041#27 > "01.01.0000"; |Y #dsmom041#27 > fec2;
       |ACABA;
   |FINSI;
   aAlfa1 = #dsmom041#27;
   aAlfa1 = aAlfa1 % -104;
   nNume1 = aAlfa1;
   |SI nSwEjer ! 0;
       |SI nSwEjer = 1;
           |SI nNume1 < 2008; |ACABA; |FINSI;
       |SINO;
           |SI nNume1 ] 2008; |ACABA; |FINSI;
       |FINSI;
   |FINSI;

   |SI enConverModelo ! 0;
       #dsmom041#10 = (#dsmom041#10 * enConverModelo ) ! enRedonModelo;
   |FINSI;
   swt_amort = 1;
   import    = (import + #dsmom041#10) ! EURODB;
|FPRC;

|DEFBUCLE amort;
   #dsmom041#4;
   25;
   enEmpresa, aCuenta, des_ele;
   enEmpresa, aCuenta, has_ele;
   e;
   NULL, ver_amort;
|FIN;

|PROCESO amortiza;  || Busca la cuenta en Amortizaciones y Desglose

   nEjerCam  = enEjercicio;
   eaCuenta  = #1#0;
   |HAZ CamCuenta;
   swt_amort = 0;
   import    = 0;

   |SI swNOAmor = 1;
       |SI aCta08 ! aCta90;
           nSwEjer = 1; aCuenta = aCta08; |BUCLE amort;
           nSwEjer = 2; aCuenta = aCta90; |BUCLE amort;
       |SINO;
           nSwEjer = 0; aCuenta = #5#5; |BUCLE amort;
       |FINSI;
       |SI swt_amort = 1; |ACABA; |FINSI;
   |FINSI;

   |DDEFECTO #11;
   #camoauxc#0 = #cacuenta#0;
   #camoauxc#1 = #cacuenta#1;
   |LEE 001#11=;
   |SI FSalida = 0;
      swt_amort = 2;
      import  = #camoauxc#3 ! EURODB;
   |FINSI;
|FPRC;

|| ********************** Ficheros Auxiliares del Balance Inventario
|PROCESO gr_blanco;
   #cauxinva#0 = #cauxinva#0 + 10;
   #cauxinva#1 = 4;
   #cauxinva#2 = "";
   #cauxinva#3 = 0;
   |GRABA 021#20n;
   |LIBERA #20;
   sw_inf = 4;
   |SI #0#11 = "S";
      |PRINT;
   |FINSI;
|FPRC;

|PROCESO graba_aux;
   #cauxinva#0 = #cauxinva#0 + 10;
   #cauxinva#1 = aux_letra;
   #cauxinva#2 = aux_desc;
   #cauxinva#3 = aux_imp;
   |GRABA 021#20n;
   |LIBERA #20;
|FPRC;

|PROCESO cabecera;
   |ABRE #21;
   |CIERRA #21;
   |DELINDEX #21; || -> Cabecera balance

   |ABRE #21;
   |DDEFECTO #21;
   #cauxinv2#0 = "A";
   #cauxinv2#1 = #0#7;
   #cauxinv2#2 = #0#8;
   #cauxinv2#3 = "";
   #cauxinv2#4 = " Empresa ..: " + #canempre#1 + "    Ejercicio: " + #canempre#26;
   #cauxinv2#5 = " Acumulados: " + #0#2 + "      -" + #0#3 + "                 Fecha ...: " + #0#12;
   #cauxinv2#6 = "";
   |GRABA 021#21n;
   |LIBERA #21;
   |CIERRA #21;
|FPRC;

|PROCESO CamCuenta;
   aCta08 = eaCuenta;
   aCta90 = eaCuenta;
   |SI aPath08 ! "";
       |PATH_DAT #403 aPath08;
       |ABRE #403;
       |DDEFECTO #403;
       |SI nEjerCam ] 2008;
           |SELECT #2#403;
           #403#2 = eaCuenta;
           |LEE 041#403];
           |SI FSalida = 0; |Y #403#2 = eaCuenta;
               aCta08 = #403#2;
               aCta90 = #403#0;
           |FINSI;
           |SELECT #1#403;
       |SINO;
           #403#0 = eaCuenta;
           |LEE 041#403=;
           |SI FSalida = 0;
               aCta08 = #403#2;
               aCta90 = #403#0;
           |FINSI;
       |FINSI;
       |CIERRA #403;
   |FINSI;
|FPRC;
