|FICHEROS;
  otp00016 #0;

  otp00006 #1;
  otp00007 #2;
  otp00008 #3;
  otp00009 #4;

  canempre #30;
  camaasic #31;
  camaasil #32;
  camaniva #36;
  caivalin #37;

  caw00016 #916;
|FIN;

|VARIABLES;
  nImpre = 0;
 nEstado  = 0;

 &eaDirOrigen  = "";

 nNume1   = 0;
 nNume2   = 0;
 aAlfa    = "";
 aAlfa1   = "";
 aAlfa2   = "";
 aAlfa3   = "";
 aBaseAse = "";
 aAseActual = "";

 nDia00   = 0;
 nDia99   = 0;
 ndDiario = 0;
 nhDiario = 0;
 nIncrDia = 0;
 nDiaDest = 0;

 Diario   = 0;
 aDeparta = "";
 ndConcep = 0;
 nhConcep = 0;
 ndLibro  = 0;
 nhLibro  = 0;
 nIncrLib = 0;
 nLibDest = 0;

 nLibroD  = 0;
 ndDepar  = 0;
 nhDepar  = 0;

 &enSwChequeo = 0;
 &nNumero  = 0;
 &aTexto   = "";
 &enSwInf  = 0;

 nContar1  = 0;
 nContar2  = 0;
 nSwAlguno = 0;
 aFichero = "";
 &lerror  = 0;
 informe    = "otp00016";
 nSDiario   = 0;
 fSFecha    = @;
 nSDocum    = 0;
 nSw        = 0;
 nSLibro    = 0;
 nSRegistro = 0;
 nSwError   = 0;

 &enSwProg   = 0;
 nEjer       = 0;
 aMes        = "";
 aDia        = "";
 fFechaI     = @;
 fFechaF     = @;
 nReg        = 0;
 nAlgunPer   = 0;
 nOtroChequeo = 0;
 nOpera       = 0;
 &enEmpCons   = 0;
 &enPerCons   = 0;
 &LaConsola   = 0;
|FIN;

|INCLUYE dscont_i;

|| /////////////////////// PROCESOS DE TRABAJO \\\\\\\\\\\\\\\\\\\\
|| *************************************************************************
|| *************************************************************************
|CALCULO y16Tipo12; |TIPO 12;
|FCAL;

|PROCESO CopiaFacturas;
   aTexto = "";
   |SI aDeparta ! "00";                         || No la copia
       |SI aDeparta ! #36#10;  |ACABA; |FINSI;
   |FINSI;

   |SI nLibDest = -1;
       nLibroD  = #36#0 + nIncrLib;
   |SINO;
       nLibroD = nLibDest;
   |FINSI;

   nReg = #36#1;
   |SI #1#11 = "S";
       |SELECT #3#916;
       #916#2 = 2;
       #916#7 = nLibroD;
       #916#8 = 9999999999;
       |LEE 010#916];
       |SI FSalida = 0;
           |LEE 040#916a;
       |SINO;
           |LEE 040#916u;
       |FINSI;
       nReg = 1;
       |SI FSalida = 0; |Y #916#2 = 2; |Y #916#7 = nLibroD;
           nReg = #916#8 + 1;
       |FINSI;
       |SELECT #1#916;
   |FINSI;
   #36#1 = nReg;

   |SI #2#23 ! "N";   ||Colocar la serie en la factura
       |SI #36#36 ! "N";
           |SI #36#36 = #2#23; |O #2#23 = "A";
               #36#2  = ("00000" + #2#1) % -105;
           |FINSI;
       |FINSI;
   |FINSI;

   |HAZ GrabaAux2;
|FPRC;

|DEFBUCLE LasFacturas;
 #36#1;
 27;
 ndLibro, 0000000001, ndConcep;
 nhLibro, 9999999999, nhConcep;
 e;
 NULL,CopiaFacturas;
|FIN;

|PROCESO Lotp00009p;
 ndLibro  = #4#3;
 nhLibro  = #4#3;
 nIncrLib = 0;
 nLibDest = #4#4;
 |BUCLE LasFacturas;
 |LIBERA #4;
|FPRC;

|DEFBUCLE Lotp00009;
 #4#1;
 ;
 #2#19, #2#0, #2#20, #2#1, 00;
 #2#19, #2#0, #2#20, #2#1, 99;
 be;
 NULL, Lotp00009p;
|FIN;

||  *******************************************************************
|| ------------------------------------------------------------------------
|| ------------------------------------------------------------
|PROCESO MiraAsientos;
   |DDEFECTO #916;

   nSwError = 0;
   aTexto   = "";

   |SI nSw = 1;
       |SI #32#0 = nSDiario; |Y #32#1 = fSFecha; |Y #32#2 = nSDocum;
           |ACABA;
       |FINSI;
   |FINSI;

   nSw = 1;
   nSDiario = #32#0;
   fSFecha  = #32#1;
   nSDocum  = #32#2;

   |SI #32#6 < ndConcep; |O #32#6 > nhConcep;   ||No lo copiara
       |ACABA;
   |FINSI;
   |SI aDeparta ! "00";
       |SI aDeparta ! #32#21; |ACABA; |FINSI;   ||No lo copiara
   |FINSI;

   #31#0 = #32#0;
   #31#1 = #32#1;
   #31#2 = #32#2;
   |LEE 001#31=;
   |SI FSalida ! 0;
       aTexto = "No existe Cabecera del Asiento ";
       nSwError = 1;
   |FINSI;

   |SI nDiaDest = -1;
       |SI #32#0 = 0; |O #32#0 = 99;
           Diario = #32#0;
           |SI #32#0 = 0;  nDia00 = nDia00 + 1; |FINSI;
           |SI #32#0 = 99; nDia99 = nDia99 + 1; |FINSI;
       |SINO;
           Diario = #32#0 + nIncrDia;
       |FINSI;
   |SINO;
       Diario = nDiaDest;
   |FINSI;

   |HAZ GrabaAux1;
   |SI nSwError = 1;
       enSwInf = 1;
       nNumero = nNumero + 1;
       |PRINT; enSwChequeo=1;
   |FINSI;
|FPRC;

|DEFBUCLE LosAsientos;
 #32#1;
 ;
 ndDiario, fec1, 000001, 0001;
 nhDiario, fec2, 999999, 9999;
 e;
 NULL,MiraAsientos;
|FIN;

|PROCESO Lotp00008p;
 ndDiario = #3#3;
 nhDiario = #3#3;
 nIncrDia = 0;
 nDiaDest = #3#4;
 aDeparta = #3#5;
 ndConcep = #3#6;
 nhConcep = #3#7;
 nSw = 0;
 |BUCLE LosAsientos;
 |LIBERA #3;
|FPRC;

|DEFBUCLE Lotp00008;
 #3#1;
 ;
 #2#19, #2#0, #2#20, #2#1, 00;
 #2#19, #2#0, #2#20, #2#1, 99;
 be;
 NULL, Lotp00008p;
|FIN;

|| *********************************************************************
|PROCESO CalFechas;

   |SI #canempre#26 > 80;
       nEjer = 1900 + #canempre#26;
   |SINO;
       nEjer = 2000 + #canempre#26;
   |FINSI;
   aAlfa1 = nEjer;

   aMes    = ( "00" + #canempre#11) % -102;
   aAlfa2  = "01." + aMes + "." + aAlfa1;
   fFechaI = aAlfa2;

   aMes    = ( "00" + #canempre#12) % -102;
   |SI #canempre#11 > #canempre#12;
       nEjer  = nEjer + 1;
       aAlfa1 = nEjer;
   |FINSI;

   aDia = "31";
   |SI #canempre#12 = 4; |O #canempre#12 = 6; |O #canempre#12 = 9; |O #canempre#12 = 11;
       aDia = "30";
   |FINSI;
   |SI #canempre#12 = 2;
       aDia = "28";
       |SI nEjer = 2000; |O nEjer = 2004; |O nEjer = 2008; |O nEjer = 2012
           |O nEjer = 2016; |O nEjer = 2020;
            aDia = "29";
       |FINSI;
   |FINSI;
   aAlfa2 = aDia + "." + aMes + "." + aAlfa1;
   fFechaF = aAlfa2;
|FPRC;

|PROCESO ProcesaEmpresa_V;

   |HAZ CalFechas;
   |SI fFechaI > fec2; |ACABA; |FINSI;
   |SI fFechaF < fec1; |ACABA; |FINSI;

   nContar1 = 0;  || Contador Asiento
   nContar2 = 0;  || Contador Facturas

   enSwInf = 0;

   aAlfa1 = aBaseAse + "ase" + (("0000" + #2#20) % -104) + "/contagen/fich/";
   aAlfa2 = #30#0; |QBF aAlfa2;
   aAlfa2 = ("000000" + #30#0) % -106;
   eaDirOrigen = aAlfa1 + aAlfa2 + "/";

   |SI nOpera = 1;
       enEmpCons = #30#2;
       enPerCons = #30#3;
       cod1 = #30#2;
       cod2 = #30#3;
       LaConsola = 1;
       |HAZ leelo;
       LaConsola = 0;
       |SUB_EJECUTA_NP "cacheapu;COTP";
       |SI lerror ! 0;
           enSwChequeo = 1;
       |FINSI;
       |ACABA;
   |FINSI;

   |PATH_DAT #31 eaDirOrigen;  || Asiento
   |PATH_DAT #32 eaDirOrigen;  || Lineas Asto
   |PATH_DAT #36 eaDirOrigen;  || Facturas
   |PATH_DAT #37 eaDirOrigen;  || Lineas Facturas

   |SI #1#2 ! 3;
       |ABRE #31;
       |SI #2#4 = "S";
           ndDiario = #2#5;
           nhDiario = #2#6;
           nIncrDia = #2#7;
           nDiaDest = -1;
           aDeparta = #2#12;
           ndConcep = #2#13;
           nhConcep = #2#14;
           nSw = 0;
           |BUCLE LosAsientos;
       |SINO;
           |BUCLE Lotp00008;
       |FINSI;
       |CIERRA #31;
   |FINSI;

|| .................. Pase de Facturas
   |SI #1#2 > 2;
       |ABRE #36;
       |ABRE #37;
       aDeparta = #2#12;
       ndConcep = #2#13;
       nhConcep = #2#14;
       |SI #2#8 = "S";
           ndLibro  = #2#9;
           nhLibro  = #2#10;
           nIncrLib = #2#11;
           nLibDest = -1;
           |BUCLE LasFacturas;
       |SINO;
           |BUCLE Lotp00009;
       |FINSI;
       |CIERRA #37;
       |CIERRA #36;
   |FINSI;

    nAlgunPer = 1;
|FPRC;

|DEFBUCLE LeePeriodos;
 #30#1;
 ;
 #2#1, 0;
 #2#1, 9;
 ;
 NULL, ProcesaEmpresa_V;
|FIN;

|PROCESO ProcesaEmpresa;
   aAlfa = aBaseAse + "ase" + (("0000" + #2#20) % -104) + "/contagen/fich/";
   |PATH_DAT #30 aAlfa;

   nAlgunPer = 0;
   |BUCLE LeePeriodos;
   |SI nAlgunPer = 0; |Y nOpera = 0;
       nNumero = nNumero + 1;
       aTexto = "No existe el ejercicio contable de la Empresa ";
       |PRINT; enSwChequeo=1;
   |FINSI;
|FPRC;

|DEFBUCLE Lotp00007;
 #2#1;
 ;
 #1#10, #1#0, 0000, 00001;
 #1#10, #1#0, 9999, 99999;
 e;
 NULL,ProcesaEmpresa;
|FIN;

||*******************************************************************
|PROCESO NuevoChequeo;
 nImpre = 1;
 |IMPRE 0;
 |SI FSalida = 0;
     |SI FSalida = 0;
         nOpera = 1; |BUCLE Lotp00007;
     |FINSI;
     |SI enSwChequeo = 1;
         |CONFI "0000NExiste errores en los Astos de las Empresas. Continuar";
         |SI FSalida ! 0;
             nImpre = 0;
             |FINIMP;
         |SINO;
             enSwChequeo = 0;
         |FINSI;
     |FINSI;
 |FINSI;
|FPRC;

||*******************************************************************
|PROCESO LeeFras;

 |SI nSw = 1;
     |SI nSLibro = #916#7; |Y nSRegistro = #916#8; |Y #1#11 = "N";
         #916#11 = "Registro de IVA Duplicado ";

         |PRINT; enSwChequeo=1;
         nNumero = nNumero + 1;
     |FINSI;
 |FINSI;
 nSLibro    = #916#7;
 nSRegistro = #916#8;
 nSw = 1;
|FPRC;

|DEFBUCLE LeeFras;
 #916#3;
 ;
 2, 00, 0000000001;
 2, 99, 9999999999;
 ;
 NULL, LeeFras;
|FIN;

|PROCESO LeeAstos;

 |SI nSw = 1;
     |SI nSDiario = #916#4; |Y fSFecha = #916#5; |Y nSDocum = #916#6; |Y #1#12 = "N";
         #916#11 = "Asiento Duplicado ";
         |PRINT; enSwChequeo=1;
         nNumero = nNumero + 1;
     |FINSI;
 |FINSI;
 nSDiario = #916#4;
 fSFecha  = #916#5;
 nSDocum  = #916#6;
 nSw = 1;
|FPRC;

|DEFBUCLE LeeAstos;
 #916#2;
 ;
 1,01,"01.01.0000", 000001;
 1,98,"31.12.2099", 999999;
 ;
 NULL, LeeAstos;
|FIN;

|PROCESO Auxiliares;
 enSwInf = 1;
 nSw = 0;
 |BUCLE LeeAstos;

 enSwInf = 2;

 nSw = 0;
 |BUCLE LeeFras;
|FPRC;

||*******************************************************************
|PROCESO GrabaAux1;
 |DDEFECTO #916;

 nContar1 = nContar1 + 1;
 #916#0 = #2#1;
 #916#1 = enAny;
 #916#2 = 1;
 #916#3 = nContar1;
 #916#4 = Diario;
 #916#5 = #32#1;
 #916#6 = #32#2;
 #916#11 = aTexto;
 #916#12 = #2#2;
 #916#13 = #32#0;
 |SI #32#0 = 0;  #916#6 = nDia00; |FINSI;
 |SI #32#0 = 99; #916#6 = nDia99; |FINSI;
 #916#15 = #32#2;
 |GRABA 001#916n;
 nSwAlguno = 1;
|FPRC;

|PROCESO GrabaAux2;
 |DDEFECTO #916;

 nContar2 = nContar2 + 1;
 #916#0 = #2#1;
 #916#1 = enAny;
 #916#2 = 2;
 #916#3 = nContar2;
 #916#7 = nLibroD;
 #916#8 = #36#1;
 #916#9 = #36#2;
 #916#10 = #36#3;
 #916#11 = aTexto;
 #916#12 = #2#2;
 #916#14 = #36#0;
 |GRABA 001#916n;
 nSwAlguno = 1;
|FPRC;

||*******************************************************************
|PROGRAMA;

 |SI enSwProg = 0; |HAZ inicio; |FINSI;

 |DBASS aAlfa; |QBF aAlfa;
 aAlfa2 = ""; aAlfa1 = ""; aAlfa = aAlfa % -299;
 |PARA; |SI aAlfa1 ! "/"; |Y aAlfa1 ! "\"; |HACIENDO;
    aAlfa2 = aAlfa1 + aAlfa2;
    aAlfa1 = aAlfa % -101; aAlfa = aAlfa % -299;
 |SIGUE;
 aAseActual = aAlfa2 - "ase";
 aBaseAse = aAlfa + "/";

 |PATH_DAT #otp00006#aBaseAse#; |PATH_DAT #otp00007#aBaseAse#;
 |PATH_DAT #otp00008#aBaseAse#; |PATH_DAT #otp00009#aBaseAse#;

 |ABRE #1;
 #1#0  = enEmpresa;
 #1#10 = aAseActual;
 |LEE 101#1=;
 |CIERRA #1;

 |SI enSwProg = 0; |Y #1#2 = 1;
     |MENAV "    No corresponde realizar el Chequeo ";
     |ACABA;
 |FINSI;

 #0#0  = enEmpresa;
 #0#1  = enPeriodo;
 #0#2  = #1#1;
 #0#3  = enEjercicio;

 nOtroChequeo = 0;
 |SI enSwProg = 0;
     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida ! 0; |O #0#4 = "N";
         |ACABA;
     |FINSI;
 |SINO;
     |CONFI "    SDesea Realizar primero un Chequeo General de Asientos de las Empresas";
     |SI FSalida = 0;
         nOtroChequeo = 1;
     |FINSI;
 |FINSI;

 enSwChequeo = 0;
 |SI nOtroChequeo = 1;
     |HAZ NuevoChequeo;
     |SI enSwChequeo = 1; |ACABA; |FINSI;
 |FINSI;

 aFichero = ("16w" + Usuario) % 108;
 |NOME_DAT #916 aFichero;
 |DELINDEX #916;

 |SI nImpre = 0;
     nImpre = 1;
     |IMPRE 0;
 |SINO;
     FSalida = 0;
 |FINSI;
 |SI FSalida = 0;

     |INFOR informe;
     |SI FSalida = 0;
         nOpera   = 0;
         nNumero  = 0;
         nSwAlguno = 0;
         |ABRE #916;
         |BUCLE Lotp00007;
         |CIERRA #916;
         |SI nSwAlguno ! 0;
             |HAZ Auxiliares;
         |FINSI;
         |SI enSwChequeo = 1; |PIEINF; |FINSI;
         |FININF;
         |DELINDEX #916;

     |FINSI;

     |SI nImpre = 1;
         |FINIMP;
         nImpre = 0;
     |FINSI;
 |FINSI;

 |SI enSwChequeo = 1; |Y enSwProg = 1;
     |CONFI "    NErrores Detectados. Desa continuar con la Fusion S/N?";
     |SI FSalida = 0;
         enSwChequeo = 0;
     |FINSI;
 |FINSI;
|FPRO;
