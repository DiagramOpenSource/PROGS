|FICHEROS;
     cay00002 #0;
     camaniva #102;
     caivalin #103;
     caw00050 #999;

     :/modelos/def/dsmom033 #33;
     :/basica/def/agicen14  #114;

     canempre #30;
     caselem1 #998;
     caivaasi #200;
|FIN;

|VARIABLES;
     aInforme   = "";
     aDirfich0  = "";

     &aCultiu   = "";
     &nCultiu   = 0;
     &enTot1    = 0;
     &enTot2    = 0;
     &enTot3    = 0;
     &enTot4    = 0;
     &enTot5    = 0;
     &enTot6    = 0;
     &enTot7    = 0;
     &aD1       = "";
     &aD2       = "";
     &aD3       = "";
     &aD4       = "";
     &aD5       = "";
     aNum1      = "";
     nNume1     = 0;
     nNum1      = 0;
     nNum2      = 0;
     nNum3      = 0;
     nSwPrint   = 0;
     nEjer      = 0;

     aFichero   = "";
     aAlfa      = "";
     aAlfa1     = "";
     aAlfa2     = "";
     aAlfa3     = "";
     fDFecha    = @;
     fHFecha    = @;
     nDPerio    = 0;
     nHPerio    = 0;
     SwAgricola = 0;
     SwAlgun    = 0;
     aVariable  = "";
     nCanal     = 0;
     aCampo0    = "";
     aCampo1    = "";
     aCampo2    = "";

     &eaTipo    = "";
     &eSwLinea  = 0;
     &eSwTotal  = 0;
     &eaInver   = "";
     aMulDep    = "";
     aCultivo   = "";
     &eaTex3    = "";
|FIN;

|INCLUYE dscont_i;

|CALCULO elinicio; |TIPO 8;
 |HAZ inicio;
 |HAZ DirAplicSt;
 |SI Haymodelo = 0;
     |MENAV "    Error.! No Existe Aplicacion MODELOS.";
     |PTEC 807;
 |SINO;
     |DBASS aBase; |QBT aBase;  || Directorio Base de las Aplaciones
     aFichero = aBase + "contagen/def/cultgana.ref";
     |XABRE "W", aFichero, nCanal;
      aVariable = ":/modelos/def/dsmom033" + &13 + &10;
     |XGRABA nCanal, aVariable;
     |XCIERRA nCanal;
 |FINSI;
|FCAL;

|| ************** Calculos de Pantalla
|CALCULO defecto; |TIPO 7;
 #0#0 = enEjercicio; |PINTA #0#0;
 aAlfa1 = #0#0;
 aAlfa2 = "01.01." + aAlfa1; #0#8 = aAlfa2; |PINTA #0#8;
 aAlfa2 = "31.12." + aAlfa1; #0#9 = aAlfa2; |PINTA #0#9;
|FCAL;

|CALCULO LaSeleccion; |TIPO 0;
  |SI #0Campo = "P";
      |C_M #0#6,1,"S"; |C_M #0#7,1,"S";
      |C_M #0#8,1,"N"; |C_M #0#9,1,"N";
      |HAZ defecto;
  |SINO;
      |C_M #0#6,1,"N"; |C_M #0#7,1,"N";
      |C_M #0#8,1,"S"; |C_M #0#9,1,"S";
      #0#6 = 00; #0#7 = 12;
      |PINTA #0#6; |PINTA #0#7;
  |FINSI;
|FCAL;

|| **************************************************************************
|PROCESO LeeLinea;

  aAlfa1 = #103#30; |QBF aAlfa1;
  |SI aAlfa1 = "";
      #103#30 = #102#10;
      aAlfa1 = #103#31; |QBF aAlfa1; |SI aAlfa1 = ""; #103#31 = #102#11; |FINSI;
  |FINSI;

  |SI aMulDep = "S";
||..... Seleccion de Actividad
      nDepar = #103#30;
      |SI nDepar = 0;  nDepar = 1;   |FINSI;
      |ABRE #114;
      #114#0 = enEmpresa;
      #114#1 = nDepar;
      |LEE 040#114=;
      |SI FSalida ! 0;  nDepar = 1;  |FINSI;
      |CIERRA #114;

      |SI nDepar < #0#3; |O nDepar > #0#4;  ||desde/hasta actividad
          |ACABA;
      |FINSI;

      |SI #114#9 ! 6; |Y #114#9 ! 7; |Y #114#9 ! 11; |Y #114#9 ! 12;
          |ACABA;                             || No es actividad Agricola
      |FINSI;
    || .....................................................................
    aCultivo = #103#31; |QBF aCultivo;
    |SI aCultivo = ""; |ACABA; |FINSI;

    #33#0 = aCultivo;   ||  Subdepartamento
    |LEE 040#33=;
    |SI FSalida ! 0;  |ACABA;  |FINSI;
 |FINSI;

|| .... Total Recargo
   #999#0 = aCultivo;  ||Cultivo
   #999#1 = "R";
   #999#2 = "D";
   #999#3 = #103#17;
   #999#4 = "R";
   #999#5 = #103#9;
   |LEE 101#999=;
   |SI FSalida ! 0;
       |DDEFECTO #999;
       #999#0 = aCultivo;  ||Cultivo
       #999#1 = "R";
       #999#2 = "D";
       #999#3 = #103#17;
       #999#4 = "R";
       #999#5 = #103#9;
       |GRABA 020#999b;
   |FINSI;
   #999#6 = #999#6 + #103#6;   ||Base Imponible
   #999#7 = #999#7 + #103#10;  ||Cuota recargo

   |GRABA 020#999a;
   |LIBERA #999;
   SwAlgun = 1;

   |SI #103#21 ! 0; |O #103#20 ! 0;
|| .... Total Retencion
       #999#0 = aCultivo;  ||Cultivo
       #999#1 = "R";
       #999#2 = "D";
       #999#3 = #103#17;
       #999#4 = "T";
       #999#5 = #103#21;
       |LEE 101#999=;
       |SI FSalida ! 0;
           |DDEFECTO #999;
           #999#0 = aCultivo;  ||Cultivo
           #999#1 = "R";
           #999#2 = "D";
           #999#3 = #103#17;
           #999#4 = "T";
           #999#5 = #103#21;
           |GRABA 020#999b;
       |FINSI;
       #999#6 = #999#6 + #103#20;   ||Base Imponible
       #999#7 = #999#7 + #103#22;   ||Retencion

       |GRABA 020#999a;
       |LIBERA #999;
  |FINSI;
|FPRC;

|DEFBUCLE caivalin;
     #103#1;
     ;
     #102#0, #102#1, 01;
     #102#0, #102#1, 99;
     ;
     NULL, LeeLinea;
|FIN;

|PROCESO LeeCabecera;
  |SI aMulDep ! "S";
  ||..... Seleccion de Actividad
      nDepar = #102#10;
      |SI nDepar = 0;  nDepar = 1;   |FINSI;
      |ABRE #114;
      #114#0 = enEmpresa;
      #114#1 = nDepar;
      |LEE 040#114=;
      |SI FSalida ! 0;  nDepar = 1;  |FINSI;
      |CIERRA #114;

      |SI nDepar < #0#3; |O nDepar > #0#4;  ||desde/hasta actividad
          |ACABA;
      |FINSI;

      |SI #114#9 ! 6; |Y #114#9 ! 7; |Y #114#9 ! 11; |Y #114#9 ! 12;
          |ACABA;                             || No es actividad Agricola
      |FINSI;
    || .....................................................................
    aCultivo = #102#11; |QBF aCultivo;
    |SI aCultivo = ""; |ACABA; |FINSI;

    #33#0 = aCultivo;   ||  Subdepartamento
    |LEE 040#33=;
    |SI FSalida ! 0;  |ACABA;  |FINSI;
  |FINSI;

  |BUCLE caivalin;
|FPRC;

|DEFBUCLE camaniva;
  #102#1;
  4,5,36;
  #0#10, 0000000000, fDFecha, nDPerio, "R";
  #0#11, 9999999999, fHFecha, nHPerio, "R";
  ;
  NULL, LeeCabecera;
|FIN;

|| -----------------------------------------------------------------------
|PROCESO ActAgric0;
 |SI #114#9 = 6; |O #114#9 = 7; |O #114#9 = 11; |O #114#9 = 12;
     SwAgricola = 1;
     |ERROR10;
 |FINSI;
|FPRC;

|DEFBUCLE ActAgric;
  #114#1;
  ;
  enEmpresa, 00;
  enEmpresa, 99;
  e;
  NULL, ActAgric0;
|FIN;

|| -----------------------------------------------------------------------
|PROCESO LeeCultivo;
 aD1 = ""; aD2 = "";
 aD3 = ""; aD4 = "";
 aD5 = "";
 enTot1 = 0; enTot2 = 0;
 enTot3 = 0; enTot4 = 0;
 enTot5 = 0; enTot6 = 0;
 enTot7 = 0;

 |ABRE #33;
 #33#0 = nCultiu;
 |LEE 040#33=;
 |SI FSalida ! 0;  |DDEFECTO #33; |FINSI;

 aCultiu = nCultiu; |QBF aCultiu;
 aCultiu = ("000" + aCultiu) % -103;

 aCampo0 = #33#1;
 |HAZ descripcion;
 aD1 = aCampo1; aD2 = aCampo2;
 aCampo0 = #33#4;
 |HAZ descripcion;
 aD3 = aCampo1; aD4 = aCampo2;
 aD5 = #33#2;
|FPRC;

|PROCESO Imprime_R;
  |SI nSwPrint = 0;
      nCultiu = #999#0;
      |HAZ LeeCultivo;
      eaTipo   = #999#4;
      eaInver  = #999#3;
      eSwLinea = 0;
      eSwTotal = 0;
      eaTex3   = "";
  |FINSI;

  |SI nCultiu ! #999#0;
      |SI #0#2 = 1;
          |SI eSwTotal = 1;
              nNume1 = #999#0;
              #999#0 = nCultiu;
              eSwTotal = 2; |PRINT;
              #999#0 = nNume1;
          |FINSI;
      |FINSI;
      |SI #0#2 = 2; |PRINT; |FINSI;

      nCultiu = #999#0;
      |HAZ LeeCultivo;
      eaTipo   = #999#4;
      eaInver  = #999#3;
      eSwLinea = 0;
      eSwTotal = 0;
      eaTex3   = "";
  |FINSI;

  |SI #0#2 = 1;
      |SI eaInver ! #999#3;
          eSwLinea = 0;
      |FINSI;
      |SI eaTipo ! #999#4;
          |SI eSwTotal = 1;
              eSwTotal = 2;
              |PRINT;
          |FINSI;
          eSwTotal = 0;
          eSwLinea = 0;
          eaTipo   = #999#4;
          eaInver  = #999#3;
          eaTex3   = "";
      |FINSI;

      |PRINT;
      eSwLinea = 1;
      eSwTotal = 1;
      |SI #999#7 ! 0;
          eaTex3 = "===============";
      |FINSI;
  |FINSI;

  |SI #999#4 = "R";
      enTot1 = enTot1 + #999#6;
      enTot2 = enTot2 + #999#7;
      |SI #999#3 = "N";
          enTot6 = enTot6 + #999#6;
      |SINO;
          enTot7 = enTot7 + #999#6;
      |FINSI;
  |SINO;
      enTot3 = enTot3 + #999#6;
      enTot4 = enTot4 + #999#7;
  |FINSI;
  enTot5 = enTot6 * #33#5;
  nSwPrint = 1;
|FPRC;

|DEFBUCLE cay00002;
     #999#2;
     ;
     000, " ", " ", " ", 0,   " ";
     999, "z", "z", "z", 999, "z";
     ;
     NULL, Imprime_R;
|FIN;

||.......................................
|PROCESO HazTodo;
    |ABRE #200;
    |DDEFECTO #200;
    |LEE 000#200p;
    |CIERRA #200;
    aMulDep = #200#132;
    |SI #0#3 [ 1; |Y #0#4 = 99;  aMulDep = "N"; |FINSI;

    enEmpresa   = #canempre#2;  ||Empresa
    eaNombreEmp = #canempre#1;
    SwAgricola = 0;
    |BUCLE ActAgric;
    |SI SwAgricola = 0; |ACABA; |FINSI;

    aFichero = ("50" + Usuario + "zzzzzzzz") % 108;
    |NOME_DAT #999 aFichero;
    |ABRE #999;  |CIERRA #999;  |DELINDEX #999;

     SwAlgun = 0;
     |ABRE #999;
     |ABRE #33;
     |BUCLE camaniva;
     |CIERRA #33;
     |CIERRA #999;

  |SI SwAlgun = 1;
      |SI #0#2 = 1;  aInforme = "cay10002"; |FINSI;
      |SI #0#2 = 2;  aInforme = "cay20002"; |FINSI;
      |INFOR aInforme;
      |SI FSalida = 0;
          nSwPrint = 0;
          |BUCLE cay00002;
          |SI nSwPrint = 1;
              |SI #0#2 = 1;
                 |SI eSwTotal = 1;
                     #999#0 = nCultiu;
                     eSwTotal = 2;|| |PRINT;
                  |FINSI;
              |FINSI;
              |SI #0#2 = 2; |PRINT; |FINSI;
              |PIEINF;
          |FINSI;
          |FININF;
       |FINSI;
  |FINSI;
  |DELINDEX #999;
|FPRC;

|| **************************************************************************
|PROCESO Tipo3;  |TIPO 3;
     aAlfa = #0#0;
     aAlfa = aAlfa % -102;
     nEjer = aAlfa;        || Ejercicio dos digitos

     fDFecha  = "01.01.1900";
     fHFecha  = "31.12.2090";
     nDPerio  = 0;
     nHPerio  = 99;
     |SI #0#5 = "F";
         fDFecha  = #0#8;
         fHFecha  = #0#9;
     |SINO;
         nDPerio  = #0#6;
         nHPerio  = #0#7;
     |FINSI;

     |IMPRE 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI #48#27 = "S";
         |HAZ MultiEmpresa;
     |SINO;
         |DFICE aDirfich0; |QBF aDirfich0;
         |HAZ HazTodo;
     |FINSI;
     |FINIMP;
|FPRC;

|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   aDirfich0   = #caselem1#3; |QBF aDirfich0;

   |ATRI S;
   |PINTA 2206, "Empresa : ";
   |PINTA 2216, #canempre#2;
   |PINTA 2222, #canempre#1;
   |ATRI s;

   |PATH_DAT #102 aDirfich0;
   |PATH_DAT #103 aDirfich0;
   |PATH_DAT #200 aDirfich0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 00001;
 99999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;

|PROCESO descripcion;
 aCampo1 = aCampo0;
 aCampo2 = "";
 |QBF aCampo0;
 aNum1 = aCampo0 % 0; nNume1 = aNum1;
 |SI nNume1 > 75;
     aAlfa1 = aCampo0 % 7601; |QBF aAlfa1;
     |SI aAlfa1 = "," |O aAlfa1 = "";
         aCampo1 = aCampo0 %  175;
         aCampo2 = aCampo0 % 7670;
     |SINO;
         nNum2 = 75
         |PARA nNum1 = 75; |SI nNum1 > 0; |HACIENDO nNum1 = nNum1 - 1;
               aAlfa3 = nNum1; |QBF aAlfa3;
               aAlfa3 = ("00" + aAlfa3) % -102
               aAlfa3 = aAlfa3 + "01";
               aAlfa1 = aCampo0 % aAlfa3; |QBF aAlfa1;
               |SI aAlfa1 = ","; |O aAlfa1 = "";
                   aAlfa3 = nNum1; |QBF aAlfa3;
                   aAlfa3 = ("00" + aAlfa3) % -102
                   aAlfa3 = "1" + aAlfa3;
                   aCampo1 = aCampo0 % aAlfa3;

                   nNum2  = nNum1 + 1;
                   aAlfa3 = nNum2; |QBF aAlfa3;
                   aAlfa3 = ("00" + aAlfa3) % -102
                   aAlfa3 = aAlfa3 + "75";
                   aCampo2 = aCampo0 % aAlfa3;
                   |SAL;
              |FINSI;
         |SIGUE;
     |FINSI;
 |FINSI;
|FPRC;
