|FICHEROS;
     camaasil #0;
     camaniva #1;
     calibros #2;
     tagz0001 #10; ||Este
     tagm0087@;
     tagm0088@;
|FIN;

|VARIABLES;
     {1}aLocal = "";
     nHandle = 0;
     aBat = "";
     aEmpGes = "00002";  || a pi¤on !!!
     aPinta = "";
     aCta = "";
     aAlfa = "";
     aTipo = "";
     aSerie = "";
     aLon = "";
     aCar = "";
     i = 0;
     aPath = "";
     aPathExe = "";
     nPos = 0;
     aExe = "";
     aParam = "";
     aAno = "";
     aPrv = "";
|FIN;

|PROCESO PinParam;
     |SI aPinta = "S";
          |PUSHV 0501 2380;
          |DDEFECTO #0;
          aLon = aExe % A0;
          #10#0 = aExe % 160; aExe = aExe % 61;
          |SI aLon > 60;
               #10#1 = aExe % 160; aExe = aExe % 61;
               |SI aLon > 120;
                    #10#2 = aExe % 160; aExe = aExe % 61;
                    |SI aLon > 180;
                         #10#3 = aExe % 160;
                    |FINSI;
               |FINSI;
          |FINSI;
          |PINPA #0#10;
          |PINDA #0#10;
          |PAUSA;
          |POPV;
     |FINSI;
|FPRC;

|PROCESO Serie;
     |DBASS aPath;
     aAlfa = aPath + "dscomer9/def/tagm0088.mas";
     |CARGA_DEF aAlfa, tagm0088@;
     |SI FSalida = 0;
          aAlfa = aPath + "dscomer9/fich/" + aEmpGes + "/";
          |PATH_DAT #tagm0088@#aAlfa#;

          |ABRE #tagm0088@;
          #tagm0088@#0 = aSerie;
          |LEE 000#tagm0088@.=;
          |CIERRA #tagm0088@;
          aSerie = ("00" + #tagm0088@#1) % -102;
     |SINO;
          |MENAV "0000Error al cargar 'tagm00880...";
     |FINSI;
|FPRC;

|PROCESO CambiaBarra;
     aAlfa = "";
     aLon = aPath % A0;
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aCar = aPath % nPos;
          |SI aCar = "/"; aCar = "\"; |FINSI;
          aAlfa = aAlfa + aCar;
     |SIGUE;
     aPath = aAlfa;
|FPRC;

|PROCESO Param;
     |DBASS aPath;
     aAlfa = aPath + "dscomer9/def/tagm0087.mas";
     |CARGA_DEF aAlfa, tagm0087@;
     |SI FSalida = 0;
          aAlfa = aPath + "dscomer9/fich/" + aEmpGes + "/";
          |PATH_DAT #tagm0087@#aAlfa#;

          |ABRE #tagm0087@;
          |LEE 000#tagm0087@.p;
          |CIERRA #tagm0087@;
          aPathExe = #tagm0087@#4; |QBF aPathExe;
          aPinta = #tagm0087@#7;

          aPath = aPathExe; |HAZ CambiaBarra; aPathExe = aPath;
     |SINO;
          |MENAV "0000Error al cargar 'tagm00880...";
     |FINSI;
|FPRC;

|PROCESO FacCom;
     |ABRE #1; |SELECT #2#1;
     #1#7 = #0#0;
     #1#8 = #0#1;
     #1#9 = #0#2;
     |LEE 000#1=;
     |CIERRA #1;
|FPRC;

|PROGRAMA;
     aTipo = PARAMETRO % 0103;
     |SI aTipo = "ASI";
          |ABRE #0;
          aAlfa = PARAMETRO % 0402; #0#0 = aAlfa;
          aAlfa = PARAMETRO % 0610; #0#1 = aAlfa;
          aAlfa = PARAMETRO % 1606; #0#2 = aAlfa;
          aAlfa = PARAMETRO % 2204; #0#3 = aAlfa;
          |LEE 000#0=;
          |SI FSalida = 0;
               aSerie = #0#15; |HAZ Serie;

               |SI #0#12 = "R";    ||Venta
                    aParam = "80/30" + aSerie + (("000000" + #0#14)%-106);
               |FINSI;
               |SI #0#12 = "S";    ||Compra
                    |HAZ FacCom;
                    aAno = #1#4 % 704;

                    aCta = #0#10; |QBF aCta;
                    |SI aCta = ""; aCta = #0#16; |FINSI;
                    |QBF aCta;
                    aPrv = aCta % -106;

                    aParam = "F/" + aAno + "/30" + aPrv + "/" + #0#26;
               |FINSI;

               |HAZ Param;
               aExe = aPathExe + "cobete.exe -R:" + &34 + aParam + &34;
               |HAZ PinParam;
               ||RSYSTEM aExe;
               |HAZ System;
          |FINSI;
          |CIERRA #0;
     |FINSI;
     |SI aTipo = "IVA";
          |ABRE #1;
          aAlfa = PARAMETRO % 102; #1#0 = aAlfa;
          aAlfa = PARAMETRO % 3; #1#1 = aAlfa;
          |LEE 000#1=;
          |SI FSalida = 0;
               |ABRE #2;
               #2#0 = #1#0;
               |LEE 000#2=;
               |CIERRA #2;

               aSerie = #1#2; |HAZ Serie;

               |SI #2#2 = "R";     ||Venta
                    aParam = "80/30" + aSerie + (("000000" + #1#3)%-106);
               |FINSI;
               |SI #2#2 = "S";     ||Compra
                    aAno = #1#4 % 704;

                    aCta = #1#12; |QBF aCta;
                    aPrv = aCta % -106;

                    aParam = "F/" + aAno + "/30" + aPrv + "/" + #1#22;
               |FINSI;

               |HAZ Param;
               aExe = aPathExe + "cobete.exe -R:" + &34 + aParam + &34;
               |HAZ PinParam;
               |HAZ System;
               ||RSYSTEM aExe;
          |FINSI;
          |CIERRA #1;
     |FINSI;
|FPRO;

|PROCESO System;
     |ESPECIFICA "RBASS", aLocal;
     aBat = aLocal;
     aBat = aBat + "tmp/";
     |RMKDIR aBat;
     aBat = aBat + Usuario + ".bat";

     |XABRE "WC", aBat, nHandle;
     |XGRABA nHandle, "@echo off";
     |XGRABA nHandle, aExe;
     |XGRABA nHandle, "exit";
     |XCIERRA nHandle;

||     aAlfa = "cmd " + &34 + "/c START /MIN /WAIT " + aBat + &34;
     aAlfa = "cmd " + &34 + "/c START /MIN " + aBat + &34;
     |RSYSTEM aAlfa;
|FPRC;
