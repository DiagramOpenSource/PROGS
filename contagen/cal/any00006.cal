|FICHEROS;
   any00006 #0;
   anm00001 #1;
   :/modelos/def/dsmom041 #41;
   :/modelos/def/dsmom042 #42;
   canempre #30;
   caselem1 #998;

   :/modelos/def/dsmow042 #999;       ||Temporal grabacion cuenta
|FIN;

|VARIABLES;
   informe = "any00006";
   &entrada = 1;
   &tipo = "AMORTIZACIONES";
   amo = "A";
   &impo = 0;
   &total = 0;
   &totcue = 0;
   &lintot = 0;
   &linnor = 0;
   &linant = 0;
   &ulti = "";
   &impoc = 0;
   &totalc = 0;
   &totcuec = 0;
   &nof = "";
   &noc = "";
   t1 = "NORMAL";
   t2 = "DEGRESIVA";
   t3 = "ACELERADA";
   t4 = "RESIDUAL";
   t5 = "PAGOS LEASING";
   t6 = "MANUAL";

   aFichero  = "";
   nCampo    = 0;
   nDEmpresa = 0;
   nHEmpresa = 0;
   nDActividad = 0;
   nHActividad = 0;
   swCuenta = 0;
   swPrimero = 0;
   nLaEmpresa = 0;
   swBueno = 0;

   aAlfa1 = "";
   aAlfa2 = "";
   nCanal = 0;
   aVariable = "";
|FIN;

|INCLUYE dscont_i;
|INCLUYE numamo_i;

|| ******************************************************************
|CALCULO numamo; |TIPO 0;
   x_alfa1 = #0Campo;
   |HAZ x_punto;
   #0Campo = x_alfa1;
   |PINTA #0Campo;
|FCAL;
|| ******************************************************************

|PROCESO graba_tmp;
   |SI #0#7 = "N";
       |SI #dsmom041#25 = "N"; |ACABA; |FINSI;
   |FINSI;

   #999#0 = #dsmom041#0;   || Empresa
   #999#1 = #dsmom041#1;   || Actividad
   #999#2 = #dsmom041#34;  || Cuenta Analitica
   #999#3 = #dsmom041#2;   || Elemento
   #999#4 = #dsmom041#3;   || Numero
   |GRABA 021#999n;
|FPRC;

|DEFBUCLE carga_tmp;
   #dsmom041#1;
   1, 34;
   enEmpresa,#0#2,#0#09, nDActividad, #0#0;
   enEmpresa,#0#3,#0#10, nHActividad, #0#1;
   e;
   NULL , graba_tmp;
|FIN;

|| .......................
|PROCESO imprelin;
 |SI #dsmom042#6 = "A";
      |SI #dsmom042#5 ] #0#4; |Y #dsmom042#5 [ #0#5;
          impo   = impo   + #dsmom042#7; impoc   = impoc   + #dsmom042#11;
          total  = total  + #dsmom042#7; totalc  = totalc  + #dsmom042#11;
          totcue = totcue + #dsmom042#7; totcuec = totcuec + #dsmom042#11;
      |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE LasLineas;
 #dsmom042#1;
 6;
 #dsmom041#0,#dsmom041#2,#dsmom041#3,001,"A";
 #dsmom041#0,#dsmom041#2,#dsmom041#3,999,"A";
 e;
 NULL, imprelin;
|FIN;

|PROCESO LaImpresion;
   #dsmom041#0 = #999#0;
   #dsmom041#1 = #999#1;
   #dsmom041#2 = #999#3;
   #dsmom041#3 = #999#4;
   |LEE 001#dsmom041.=;
   |SI FSalida ! 0;  |ACABA;  |FINSI;

   |SI swPrimero = 0;  ||Controla el primer pase
       swPrimero = 1;
       enEmpresa   = #999#0;
       eaNombreEmp = #dsmom041#32;
       ulti        = #999#2;       || Cuenta Analitica
       swCuenta    = 0;
   |FINSI;

   |SI enEmpresa ! #999#0; || Controla la Ruptura por Empresas
       |SI swCuenta = 1;
           lintot = 1;
           |PRINT;
           |PIEINF; || imprime el pie del informe
       |FINSI;
       enEmpresa   = #999#0;
       eaNombreEmp = #dsmom041#32;
       ulti        = #999#2;       || Cuenta Analitica
       swCuenta    = 0;
   |FINSI;

   |SI swCuenta =  0;   || Primera cuenta de esta Empresa
       lintot = 0;
       linnor = 1;
       linant = 0;
   |SINO;
        |SI #999#2 ! ulti;
            lintot = 1;
            |PRINT; || linea del informe
            totcue = 0; totcuec = 0;
            lintot = 0;
            linant = 0;
            linnor = 1;
        |SINO;
            lintot = 0;
            linnor = 0;
            linant = 1;
         |FINSI;
    |FINSI;

    ulti = #999#2;
    nof = t1; noc = t1;
    |SI #dsmom041#13 = "D"; nof = t2; |FINSI;
    |SI #dsmom041#13 = "A"; nof = t3; |FINSI;
    |SI #dsmom041#13 = "R"; nof = t4; |FINSI;
    |SI #dsmom041#13 = "F"; nof = t5; |FINSI;
    |SI #dsmom041#13 = "M"; nof = t6; |FINSI;

    |SI #dsmom041#19 = "D"; noc = t2; |FINSI;
    |SI #dsmom041#19 = "A"; noc = t3; |FINSI;
    |SI #dsmom041#19 = "R"; noc = t4; |FINSI;
    |SI #dsmom041#19 = "F"; noc = t5; |FINSI;
    |SI #dsmom041#19 = "M"; noc = t6; |FINSI;

    |BUCLE LasLineas; || lee las lineas de esta cabecera
    |SI impo ! 0; |O impoc ! 0;
        |PRINT;
         swCuenta = 1;
    |SINO;
         |SI #0#7 = "S";
             |PRINT;
             swCuenta = 1;
         |FINSI;
    |FINSI;
    lintot = 0;
    linnor = 0;
    linant = 0;
    impo = 0; impoc = 0;
|FPRC;

|DEFBUCLE dsmoy040;
   #999#1;
   ;
   nDEmpresa, #0#0, #0#2, #0#09;
   nHEmpresa, #0#1, #0#3, #0#10;
   ;
   NULL, LaImpresion;
|FIN;
|| //////////////////////////////////

|PROCESO  HazTodo;

      nDActividad = 0;
      nHActividad = 99;

      aFichero = ("40y" + Usuario) % 108;
      |NOME_DAT #999 aFichero;
      |ABRE #999;  |CIERRA #999;  |DELINDEX #999;

      nDEmpresa = enEmpresa;
      nHEmpresa = enEmpresa;
      nLaEmpresa = -1;
      swBueno = 0;
      |ABRE #999;
      |BUCLE carga_tmp;
      |CIERRA #999;

      swPrimero = 0;
      swCuenta = 0;
      |ABRE #41;
      |BUCLE dsmoy040;
      |SI swCuenta = 1;
         lintot = 1;
         |PRINT;
         |PIEINF; || imprime el pie del informe
      |FINSI;
      |CIERRA #41;

      |DELINDEX #999;
|FPRC;

|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3; |QBF dirfich0;

   |PATH_DAT #1 dirfich0;

   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 000001;
 999999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
   |SUB_EJECUTA "caselemp";
   |NOME_DAT #998 eaFichsel;
   |BUCLE MultiEmp0;
   |DELINDEX #998;
|FPRC;

|| **********************************************************************

|PROGRAMA;

  |CLS;
  |CABEZA "Inf. Ctas Analiticas en Bienes Amortizables";
  |HAZ inicio;
  |HAZ Analitica;
  |SI  enSwAna = 0;
       aAlfa1 = "    CONTABILIDAD NO ANALITICA ";
       |SI eaAnNiv = "E";
           aAlfa1 = "    EMPRESA NO ANALITICA ";
       |FINSI;
       |MENAV aAlfa1;
       |ACABA;
  |FINSI;

  |HAZ DirAplicSt;
  |SI Haymodelo = 0;
      |MENAV "    Error.! No Existe Aplicacion MODELOS.";
      |ACABA;
  |SINO;
      aAlfa1 = aPModelo + "fich/";
      |PATH_DAT #dsmom041 #aAlfa1#;
      |PATH_DAT #dsmom042 #aAlfa1#;

      aFichero = aPContag + "def/amorcabe.ref";
      |XABRE "W", aFichero, nCanal;
      aVariable = ":/modelos/def/dsmom041" + &13 + &10;
      |XGRABA nCanal, aVariable;
      |XCIERRA nCanal;

      aFichero = aPContag + "def/amorline.ref";
      |XABRE "W", aFichero, nCanal;
      aVariable = ":/modelos/def/dsmom042" + &13 + &10;
      |XGRABA nCanal, aVariable;
      |XCIERRA nCanal;
  |FINSI;

  |PINPA #0#0;
  |PINDA #0#0;
  |ENDATOS #1#0;

  |SI FSalida = 0;

      |IMPRE 0;
      |SI FSalida ! 0; |ACABA; |FINSI;

      |INFOR informe;
      |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

      |SI #48#27 = "S";
          |HAZ MultiEmpresa;
      |SINO;
          |DFICE dirfich0; |QBF dirfich0;
          |HAZ HazTodo;
      |FINSI;

      |FININF;
      |FINIMP;
  |FINSI;
|FPRO;
