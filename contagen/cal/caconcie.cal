|FICHEROS;
     caconcie #0; ||Este Def.
     camaasil #1; ||Asientos....
     caconcim #2; ||Tipos Conciliacion Avanzada.

     cacuenta #3; ||Plan contable...............
     :/modelos/def/dsmom030 #5; ||Conceptos.
     caconci2 #7; ||Tipos Conciliacion Avanzada. (Lineas).
     caconcil #8; ||Diferencias ??????.

     caconte1 #994; ||Auxiliar 1.
     caconte2 #996; ||Auxiliar 2.

     canempre #30;
     caselem0 #997;
     caselem1 #998;
|FIN;

|VARIABLES;
    nSumavi = 0;
    nImp   = 0;
    aDes4  = "";
    aDes3  = "";
    aDes5  = "";
    aDes44 = "";
    aDes33 = "";
    aDes55 = "";

    aAlfa  = "";
    nImpo  = 0;
    nNume  = 0;

    aFichero  = "";
    aDCuenta  = "";
    aHCuenta  = "";
    nSwAlgun  = 0;
    nQImporte = 0;
    aSigno    = "";

    aBlancs   = "";
    aAlfaE    = "";
    aPSerie   = "";
    aPNumer   = "";
    aAlfa1    = "";
    aAlfa2    = "";
    aAlfa3    = "";
    nNume1    = 0;
    nNume2    = 0;
    nNume3    = 0;

    fDFecha   = @;
    fHFecha   = @;
    nDDiario  = 0;
    nHDiario  = 99;
    aDDepar   = "";
    aHDepar   = "";
    mensa1    = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
    nNoGraba  = 0;

    nSw       = 0;
    nEmpMulti = 0;
    &eSwMul   = 0;
    &espe15   = "";
    &espe16   = 0;
    nSwOpera  = 0;
    nPerAct   = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE dsco08_i;

|CALCULO losdefectos; |TIPO 7;
 |SI #0#8 = "S";
     #0#4 = "01.01.2000";
 |SINO;
     #0#4 = fec1;
 |FINSI;
 |PINTA #0#4;
 #0#5 = fec2; |PINTA #0#5;
|FCAL;

|CALCULO fecha; |TIPO 0;
 |SI #0#8 = "N";
     |SI #0Campo < fec1; |O #0Campo > fec2;
         |MENAV mensa1;
         |ERROR;
         |ACABA;
     |FINSI;
 |FINSI;

 |SI Campo = 5; |Y #0#4 > #0#5;
     |MENAV "    Error fecha inferior a la fecha desde";
     |ERROR;
 |FINSI;
|FCAL;

|| ************************************************************************
|PROCESO LeeDiferencias;

     |SI #8#6 ! 0;
          nQImporte = #8#6;
          aSigno   = "D";
     |SINO;
          nQImporte = #8#7;
          aSigno   = "H";
     |FINSI;
     |SI #2#6 = "+";  |Y nQImporte  [ 0; |ACABA; |FINSI;
     |SI #2#6 = "-";  |Y nQImporte  ] 0; |ACABA; |FINSI;

     nSumavi = nSumavi + 1;
     #994#0 = 99;
     #994#1 = #8#4;
     #994#2 = #8#2;
     #994#3 = nSumavi;
     #994#4 = #8#0;
     eaCuenta = #8#0; |HAZ SacaNivel;
     #994#5 = enNivel;
     #994#6 = #2#4;
     #994#7 = #8#5;
     #994#8 = aSigno;
     #994#9 = nQImporte;
     |GRABA 020#994n;
     nSwAlgun = 1;
|FPRC;

|DEFBUCLE LeeDiferencias;
     #8#1;
     8,4;
     aDCuenta,   1, " ", fDFecha;
     aHCuenta, 999, " ", fHFecha;
     ;
     NULL,LeeDiferencias;
|FIN;

|PROCESO LeeAstos;

     |SI #2#6 = "+";  |Y #1#9 [ 0; |ACABA; |FINSI;
     |SI #2#6 = "-";  |Y #1#9 ] 0; |ACABA; |FINSI;

     #994#0 = #1#0;
     #994#1 = #1#1;
     #994#2 = #1#2;
     #994#3 = #1#3;
     #994#4 = #1#4;
     eaCuenta = #1#4; |HAZ SacaNivel;
     #994#5 = enNivel;
     #994#6 = #1#6;
     #994#7 = #1#7;
     #994#8 = #1#8;
     #994#9 = #1#9;
     |GRABA 020#994n;
     nSwAlgun = 1;
|FPRC;

|DEFBUCLE LeeAstos;
 #1#3;
 6,8,18,0,21;
 aDCuenta, fDFecha, 000000, 0001, #2#4, #2#5, " ", nDDiario, aDDepar;
 aHCuenta, fHFecha, 999999, 9999, #2#4, #2#5, " ", nHDiario, aHDepar;
 ;
 NULL, LeeAstos;
|FIN;

||_________________________________________________________________________

|PROCESO GrabaCon;
     #996#0  = #994#0;
     #996#1  = #994#1;
     #996#2  = #994#2;
     #996#3  = #994#3;
     #996#4  = #994#4;
     #996#5  = #994#5;
     #996#6  = #994#6;
     #996#7  = #994#7;
     #996#8  = #994#8;
     #996#9  = #994#9;
     #996#10 = nNume;
     |GRABA 020#996n;
|FPRC;

|PROCESO GrabaCon1;
     nNoGraba = 0;
     |SELECT #3#996;
     #996#0 = #1#0;
     #996#1 = #1#1;
     #996#2 = #1#2;
     #996#3 = #1#3;
     |LEE 041#996=;
     |SI FSalida = 0;
         nNoGraba = 1;
     |FINSI;
     |SELECT #1#996;

     |SI nNoGraba = 0;
         #996#0  = #1#0;
         #996#1  = #1#1;
         #996#2  = #1#2;
         #996#3  = #1#3;
         #996#4  = #1#4;
         eaCuenta = #1#4; |HAZ SacaNivel;
         #996#5 = enNivel;
         #996#6  = #1#6;
         #996#7  = #1#7;
         #996#8  = #1#8;
         #996#9  = #1#9;
         #996#10 = nNume;
         |GRABA 020#996n;
     |FINSI;
|FPRC;

|PROCESO LeeAstos1;
  |SI #994#0 = #1#0; |Y #994#1 = #1#1;
      |Y #994#2 = #1#2; |Y #994#3 = #1#3;
         |ACABA;  || es el mismo apunte
  |FINSI;

     |SI #7#4 = "+";  |Y #1#9 [ 0; |ACABA; |FINSI;
     |SI #7#4 = "-";  |Y #1#9 ] 0; |ACABA; |FINSI;

     |DDEFECTO #5;
     #5#0 = #1#6;
     |LEE 000#5=;

     aDes33 = #1#7; |QBF aDes33;
     aDes55 = #5#1; |QBF aDes55;
     aDes44 = aDes33 - aDes55;
     |SI aDes44 = aDes33;
         aDes44 = aDes33 - aDes5;
     |FINSI;
     |QBT aDes44;

     |SI #2#10 = "S";
         aAlfaE = aDes44; |HAZ Especial; aDes44 = aAlfaE;
     |FINSI;

     |SI aDes44 = aDes4;
           |HAZ GrabaCon1;
           |SI nNoGraba = 0;
               |SI #1#8 = "D";
                   nImpo = nImpo + #1#9;
               |FINSI;
               |SI #1#8 = "H";
                   nImpo = nImpo - #1#9;
               |FINSI;
               aAlfa = nImpo;
               |SI aAlfa = "-0"; |O aAlfa = "0"; nImpo = 0; |FINSI;
               |SI nImpo = 0; |ERROR10; |FINSI;
           |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE LeeAstos1;
 #1#3;
 6,8,18, 0, 21;
 #994#4, fDFecha, 000000, 0001, #7#2, #7#3, " ", nDDiario, aDDepar;
 #994#4, fHFecha, 999999, 9999, #7#2, #7#3, " ", nHDiario, aHDepar;
 ;
 NULL, LeeAstos1;
|FIN;

|PROCESO VariosCon;
   |BUCLE LeeAstos1;
|FPRC;

|DEFBUCLE VariosCon;
   #7#1;
   ;
   #2#0, 01;
   #2#0, 99;
   ;
   NULL, VariosCon;
|FIN;

||\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO DesGrabaCon;
    |BORRA 020#996a;
|FPRC;

|DEFBUCLE DesGrabaCon;
     #996#1;
     ;
     nNume, 00, "01.01.1900", 000001, 0001;
     nNume, 99, "31.12.2300", 999999, 9999;
     ;
     NULL, DesGrabaCon;
|FIN;

||////////////////////////////

|PROCESO LeeAux1;

  aBlancs = " " * 30;
  #5#0 = #994#6;
  |LEE 000#5=;
  |SI FSalida ! 0;  |ACABA; |FINSI;

  aDes3 = #994#7; |QBF aDes3;
  aDes5 = #5#1;   |QBF aDes5;
  aDes4 = aDes3 - aDes5;
  |SI aDes4 = aDes3;
      aDes4 = aDes3 - "N/FRA:";
      |SI aDes4 ! aDes3; aDes5 = "N/FRA:"; |FINSI;
  |FINSI;
  |QBT aDes4;

  |SI #2#10 = "S";
       aAlfaE = aDes4; |HAZ Especial; aDes4 = aAlfaE;
  |FINSI;

  |SI #994#8 = "D";
      nImpo = #994#9;
  |SINO;
      nImpo = -#994#9;
  |FINSI;
  nNume = nNume + 1;

  |ABRE #996;
  |HAZ GrabaCon;

  |BUCLE VariosCon;
  |CIERRA #996;

  |SI nImpo ! 0;
      |BUCLE DesGrabaCon;
  |SINO;
      nSwAlgun = 1;
  |FINSI;
|FPRC;

|DEFBUCLE LeeAux1;
  #994#2;
  ;
  "            ";
  "zzzzzzzzzzzz";
  ;
  NULL, LeeAux1;
|FIN;

|| --------------------------------------------------------------------
|PROCESO Concilia;
    #1#0 = #996#0;
    #1#1 = #996#1;
    #1#2 = #996#2;
    #1#3 = #996#3;
    |LEE 110#1=;
    |SI FSalida = 0;
         #1#18 = "û";
         |GRABA 020#1a;
    |SINO;
         #8#0 = #996#4;
         #8#1 = #996#5;
         #8#2 = #996#2;
         |LEE 101#8=;
         |SI FSalida = 0;
              #8#8 = "û";
              |GRABA 020#8a;
         |FINSI;
         |LIBERA #8;
     |FINSI;
     |LIBERA #1;
|FPRC;

|DEFBUCLE Concilia;
  #996#1;
  ;
  00000, 00, "01.01.1900", 000000, 0001;
  99999, 99, "31.12.2300", 999999, 9999;
  ;
  NULL, Concilia;
|FIN;

|| ..............................
|RUTINA inf;
     #996#10 = 0;
     #996#0  = 0;
     #996#1  = "01.01.1900";
     #996#2  = 0;
     #996#3  = 0;
|FRUT;

|RUTINA sup;
     #996#10 = 99999;
     #996#0  = 99;
     #996#1  = "31.12.2300";
     #996#2  = 999999;
     #996#3  = 9999;
|FRUT;

|| ********************************************************************
|CALCULO ejec; |TIPO 3;

     aFichero = ("1c" + Usuario) % 108;
     |NOME_DAT #994 aFichero;

     aFichero = ("2c" + Usuario) % 108;
     |NOME_DAT #996 aFichero;

     |DELINDEX #994;
     |DELINDEX #996;

     nDDiario = #0#2;
     nHDiario = #0#3;
     fDFecha  = #0#4;
     fHFecha  = #0#5;
     aDDepar  = #0#6;
     aHDepar  = #0#7;
 || .... .... .... ... ... ...
     |ABRE #2;
     #2#0 = #0#0;
     |LEE 001#2=;
     aDCuenta = (#2#2 + "                 ") % 112;
     aHCuenta = (#2#3 + "                 ") % 112;
     |CIERRA #2;

     |HAZ HazTodo;

     |SI nSwAlgun = 0;
         |MENAV "    No existen Apuntes para Conciliar ";
         |ACABA;
     |FINSI;

     nSwAlgun = 0;
     |ABRE #5;
     |BUCLE LeeAux1;
     |CIERRA #5;

     |SI nSwAlgun ! 0;

         |ENTLINEAL #1#996, -5 , 4 , 20 , 1 , inf , sup;
         |CONFI "2400SActualizar Conciliacion : ";
         |SI FSalida = 0;
              |ABRE #1;
              |ABRE #8;
              |BUCLE Concilia;
              |CIERRA #1;
              |CIERRA #8;
         |FINSI;
     |FINSI;

     |DELINDEX #994;
     |DELINDEX #996;
|FCAL;
|| *********************************************************************
|PROCESO HazTodo;
     |ABRE #994;
     nSwAlgun = 0;
     |BUCLE LeeAstos;

     nSumavi = 0;
     |BUCLE LeeDiferencias;
     |CIERRA #994;
|FPRC;
|| *********************************************************************
|PROCESO Especial;

 aPSerie = "";
 aPNumer = aAlfaE;

 aAlfa1 = aAlfaE;
 aAlfa1 = aAlfaE - #2#11;
 |SI FSalida ! 0;
     |SI FSalida = 101;
         aPSerie = "";
         aPNumer = aAlfaE % 230;
     |SINO;
         aAlfa1 = FSalida;
         aAlfa2 = aAlfa1 % 0;
         |SI aAlfa2 = 4;
             aAlfa2 = aAlfa1 % 102; ||Posicion de la Barra
         |SINO;
             aAlfa2 = aAlfa1 % 101; ||Posicion de la Barra
         |FINSI;
         nNume1 = aAlfa2;
         nNume2 = nNume1 - 1; aAlfa2 = nNume2; aAlfa2 = "1" + (("00" + aAlfa2) % -102);
         nNume3 = nNume1 + 1; aAlfa3 = nNume3; aAlfa3 = aAlfa3 + "30";

         aPSerie = aAlfaE % aAlfa2;
         aPNumer = aAlfaE % aAlfa3;
     |FINSI;
 |FINSI;
 |SI #2#12 = "S";
     nNume1 = aPNumer;
     aPNumer = nNume1;
 |FINSI;
 aAlfaE = aPSerie + aPNumer;
|FPRC;
