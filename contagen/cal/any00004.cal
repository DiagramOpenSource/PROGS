|FICHEROS;
    any00004 #0;

    camaasil #1;
    camaasic #2;
    cacuenta #3;
    anw00004 #4;
    caclipro #5;
    anm00003 #7;
    anm00013 #13;

    caconimp #8;
    ca8m0004 #404;

    canempre #30;
    anm0003@ #107;
    caselem1 #998;
|FIN;

|VARIABLES;
    &entrada = 1;
    txtot    = "Total cuenta";
    txsys    = "Suma y sigue";
    sw_con   = 0;
    x        = 0;
    sw       = 0;
    lin      = 0;
    cuenta   = "";
    cuen     = "";
    fecalf   = "";
    mensa1   = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
    traba1   = "";
    num      = 0;
    alfa1    = "";
    alfa2    = "";
    alfa3    = "";
    nume1    = 0;
    nume2    = 0;
    nume3    = 0;
    finic    = @;
    sw_p     = 0;
    cant1    = 0;
    cant2    = 0;
    cant3    = 0;
    cant4    = 0;
    swsel    = 0;
    incta    = "";
    ficta    = "";
    nCampo   = 0;
    informe  = "any00004";

    &debe    = 0;
    &haber   = 0;

    sw_prim  = 0;
    sw_print = 0;
     swtotal = 0;
    sw_pagina = 0;
    &pagina = 0;
    &sw_cab = 0;

     nSwGApte = 0;
     aMas     = "";
     aCta2Gru = "";
     dn_error2 = 0;

     nDia8      = 0;
     fFec8      = @;
     nDoc8      = 0;
     nIgual     = 0;
     nAsto8     = 0;
     nDiaAper   = 0;
     contra_ad  = "";
     contra_tad = "";
     contra_ah  = "";
     contra_tah = "";
     nSwAper    = 0;
     contra_d   = "";
     contra_td  = "";
     contra_h   = "";
     contra_th  = "";
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoan_i;
|INCLUYE digana_i;

|| *********************************************************************
||                             COMIENZO
|| *********************************************************************
|CALCULO &c_pagina;
  |SI sw_pagina = 0;
      pagina = #0#38;
      sw_pagina = 1;
  |SINO;
      pagina = pagina + 1;
  |FINSI;
  sw_cab = 1;
|FCAL;

|CALCULO seleccio2; |TIPO 0;
|SI #0#48 = "N";
    |C_M #0#49, 1, "S"; |C_M #0#50, 1, "S";
    |C_M #0#53, 1, "S"; |C_M #0#54, 1, "S";
    |C_M #0#55, 1, "S"; |C_M #0#56, 1, "S";
    |C_M #0#57, 1, "S"; |C_M #0#58, 1, "S";
    |C_M #0#0, 1, "N"; |C_M #0#1, 1, "N";
    #0#0 = "            "; |PINTA #0#0;
    #0#1 = "zzzzzzzzzzzz"; |PINTA #0#1;
|SINO;
    |C_M #0#0, 1, "S"; |C_M #0#1, 1, "S";
    |C_M #0#49, 1, "N"; |C_M #0#50, 1, "N";
    |C_M #0#51, 1, "N"; |C_M #0#52, 1, "N";
    |C_M #0#53, 1, "N"; |C_M #0#54, 1, "N";
    |C_M #0#55, 1, "N"; |C_M #0#56, 1, "N";
    |C_M #0#57, 1, "N"; |C_M #0#58, 1, "N";
|FINSI;
|FCAL;

|CALCULO cuenta; |TIPO 6;
     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;
|FCAL;

|CALCULO seleccio; |TIPO 0;
|SI #0#4 = "N";
    |PARA nume1 = 5; |SI nume1 [ 28; |HACIENDO nume1 = nume1 + 1;
          |CAMPO_MODIFICA #0nume1, 1, "S";
    |SIGUE;
    |CAMPO_MODIFICA #0#31, 1, "N";
    |CAMPO_MODIFICA #0#32, 1, "N";
    #0#31 = 1;       |PINTA #0#31;
    #0#32 = 999;     |PINTA #0#32;
|SINO;
    |PARA nume1 = 5; |SI nume1 [ 28; |HACIENDO nume1 = nume1 + 1;
          |CAMPO_MODIFICA #0nume1, 1, "N";
    |SIGUE;
    |CAMPO_MODIFICA #0#31, 1, "S";
    |CAMPO_MODIFICA #0#32, 1, "S";
|FINSI;
|FCAL;

|CALCULO fechad;
  |SI #0#2 < fec1; |O #0#2 > fec2;
      |MENAV mensa1;
      |ERROR;
  |FINSI;
|FCAL;

|CALCULO fechah;
  |SI #0#3 < fec1; |O #0#3 > fec2; |O #0#3 < #0#2;
      |MENAV mensa1;
      |ERROR;
  |FINSI;
|FCAL;

|CALCULO no_depar; |TIPO 7;
   |C_M #0#38, 1, eaDepar;
   |C_M #0#39, 1, eaDepar;
|FCAL;

|CALCULO nosubde; |TIPO 7;
   |C_M #0#29, 1, eaSubde;
   |C_M #0#30, 1, eaSubde;
|FCAL;

|| **************************************************************************
|PROCESO ParaApertura;
   nDia8  = -1;
   nAsto8 = 0;
   contra_ad  = ""; contra_tad = "";
   contra_ah  = ""; contra_tah = "";

   |SI enEjercicio = 2008;
       |ABRE #8;
       |DDEFECTO #8;
       #8#0 = 1;
       |LEE 001#8=;
       |CIERRA #8;

       |SI #8#11 = "S";
           |ABRE #404;
           |LEE 001#404p;
           |SI FSalida = 0;
               nDia8 = #404#10;
               fFec8 = #404#11;
               nDoc8 = #404#12;
           |FINSI;
           |CIERRA #404;
           |ABRE #2;
           #2#0 = nDia8;
           #2#1 = fFec8;
           #2#2 = nDoc8;
           |LEE 001#2=;
           |SI FSalida = 0;
               contra_ad  = #2#10; contra_tad = "";
               contra_ah  = #2#8;  contra_tah = "";
               |HAZ LeeContra;
           |FINSI;
           |CIERRA #2;
       |FINSI;
   |FINSI;
|FPRC;

|PROCESO LeeContra;
  |ABRE #3;
  |DDEFECTO #3;
  #3#0 = contra_ad;
  |LEE 000#3=;
  contra_tad = #3#2;

  |DDEFECTO #3;
  #3#0 = contra_ah;
  |LEE 000#3=;
  contra_tah = #3#2;

  |CIERRA #3;
|FPRC;

|| *******************************************************************
|PROGRAMA;
  |CLS;
  |CABEZA "Extractos Referencia por pantalla";
  |HAZ inicio;
  |HAZ Analitica;
  |SI  enSwAna = 0;
       alfa1 = "    CONTABILIDAD NO ANALITICA ";
       |SI eaAnNiv = "E";
           alfa1 = "    EMPRESA NO ANALITICA ";
       |FINSI;
       |MENAV alfa1;
       |ACABA;
 |FINSI;

  |DBASE aMas;
  aMas  = aMas + "def/anm00003";
  |CARGA_DEF aMas, anm0003@;
  |SI FSalida ! 0;
      |MENAV "    Error Cargado Fichero de Grupos Analiticos. Proceso Interrumpido";
      |ACABA;
  |FINSI;

  #0#2 = fec1; #0#3 = fec2;
  FSalida = 1;

|PARA;|SI FSalida = 1; |HACIENDO;
    |PINPA #0#0;
    |PINDA #0#0;                    || Pinta los datos por defecto
    |ENDATOS #1#0;                  || Seleccion informe
    |SI FSalida = 0;                || Si el conforme es igua a S
        |IMPRE 0;
        |SI FSalida ! 0;
            |MENAV "    Proceso abortado ... ";
             FSalida = 0;
        |SINO;
            |HAZ Imprimir;
            |FINIMP;
             FSalida = 1;
        |FINSI;
    |SINO;
         FSalida = 0;
    |FINSI;
|SIGUE;

 |DESCARGA_DEF #107;
|FPRO;

|PROCESO Imprimir;
  |INFOR informe;
  |SI FSalida ! 0; |ACABA; |FINSI;

  |SI #48#27 = "S";
      |HAZ MultiEmpresa;
  |SINO;
      |DFICE dirfich0; |QBF dirfich0;
      |HAZ HazTodo;
  |FINSI;
  |FININF;
|FPRC;

|| *************************************************************************
||                          MEOLLO
|| *************************************************************************
|PROCESO HazTodo;
  sw_pagina = 0;
  |HAZ borrar;
  sw_prim = 0;
  swtotal = 0;
  |HAZ ParaApertura;
  |HAZ impre;
  sw_cab = 0;
|FPRC;

|CALCULO ver_grupo2;
 |SI #107#2 ] incta; |Y #107#2 [ ficta;
     dn_cuent = #107#2;
     |HAZ dig_ana2;
     |SI dn_error  = 0;
         dn_error2 = 0;
     |FINSI;
 |FINSI;
|FCAL;

|DEFBUCLE selegrupo2;
 #107#1002;
 ;
 aCta2Gru, 000;
 aCta2Gru, 999;
 e;
 NULL, ver_grupo2;
|FIN;

|PROCESO ver_grupo;
 |SI #7#2 ] incta; |Y #7#2 [ ficta;
     dn_cuent = #7#2;
     |HAZ dig_ana2;
     |SI dn_error = 0;
         swsel = 1;
        |ERROR10;
     |FINSI;
 |SINO;
      alfa3 = #7#2 % 103;
      |SI alfa3 = "GRU";
         dn_error2 = 1;
         aCta2Gru  = #7#2;
         |BUCLE selegrupo2;
         |SI dn_error2 = 0;
             swsel = 1;
             |ERROR10;
         |FINSI;
      |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE selegrupo;
 #7#1;
 ;
 #1#29, 000;
 #1#29, 999;
 e;
 NULL, ver_grupo;
|FIN;

|PROCESO ver_grupo_apte;
 nSwGApte = 1;
 |SI #13#6 ] incta; |Y #13#6 [ ficta;
     dn_cuent = #13#6;
     |HAZ dig_ana2;
     |SI dn_error = 0;
         swsel = 1;
        |ERROR10;
     |FINSI;
 |SINO;
      alfa3 = #13#6 % 103;
      |SI alfa3 = "GRU";
         dn_error2 = 1;
         aCta2Gru  = #13#6;
         |BUCLE selegrupo2;
         |SI dn_error2 = 0;
             swsel = 1;
             |ERROR10;
         |FINSI;
      |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE selgruApte;
 #13#1;
 ;
 #1#0, #1#1, #1#2, #1#3, #1#29, 000;
 #1#0, #1#1, #1#2, #1#3, #1#29, 999;
 e;
 NULL, ver_grupo_apte;
|FIN;

|PROCESO cuenta_ana;
  swsel = 0;
  |SI #1#29 ] incta; |Y #1#29 [ ficta;
      dn_cuent = #1#29;
      |HAZ dig_ana2;
      |SI dn_error = 0; swsel = 1; |FINSI;
  |SINO;
      alfa3 = #1#29 % 103;
      |SI alfa3 = "GRU";
          nSwGApte = 0;
          |BUCLE selgruApte;
          |SI nSwGApte = 0;
              |BUCLE selegrupo;
          |FINSI;
      |FINSI;
  |FINSI;
|FPRC;

|CALCULO concepto;
  sw_con = 0;
  |PARA x = 5; |SI x [ 28; |HACIENDO x = x + 1;
        |SI #0x  = #1#6;
            sw_con = 1;
            |SAL;
       |FINSI;
  |SIGUE;
|FCAL;

|CALCULO leecuen;
 #4#0 = #30#1;
 #4#1 = cuenta;
 eaCuenta = #4#1; |HAZ SacaNivel;

 #3#0 = cuenta;
 |LEE 040#3=;
 |SI FSalida = 0;
     #4#2 = #3#2;
 |FINSI;
 sw_print = 0;

 #5#23 = "C";
 #5#10 = cuenta;
 #5#11 = enNivel;
 |LEE 040#5=;
 |SI FSalida = 0;
     #4#3 = #5#2;
     #4#4 = #5#7;
 |SINO;
     #5#23 = "P";
     #5#10 = cuenta;
     #5#11 = enNivel;
     |LEE 040#5=;
     |SI FSalida = 0;
         #4#3 = #5#2;
         #4#4 = #5#7;
     |FINSI;
 |FINSI;
|FCAL;

|PROCESO Apertura2008;
   |SALVA_DATOS #1;

   |ABRE #404;
   |DDEFECTO #404;
   #404#10 = nDia8;
   #404#11 = fFec8;
   #404#12 = nDoc8;
   #404#0  = #1#3;
   |LEE 041#404=;
   |SI FSalida ! 0; |O #404#8 ! #1#4; || no existe o no son iguales
       |CIERRA #404;
       |REPON_DATOS #1;
       |HAZ ParaImprimir;
       |ACABA;
   |FINSI;
   || .... Primer Asiento Apertura 2007
   nSwAper   = 1;
   contra_d  = contra_ad;
   contra_td = contra_tad;
   contra_h  = contra_ah;
   contra_th = contra_tah;
   #1#4 = #404#1;
   #1#5 = #404#7;
   |HAZ ParaImprimir;
   |SI #404#8 ! #404#1;
       nSwAper = 2;
       contra_d  = #404#8; contra_td = #404#2;
       contra_h  = #404#8; contra_th = #404#2;
       #1#4 = #404#1;   || 2§ asiento
       #1#5 = #404#7;
       #1#7 = #8#13;
       |SI #404#3 = "D";
           #1#8 = "H";
       |SINO;
           #1#8 = "D";
       |FINSI;
       #1#9 = #404#4 + #404#5;
       |HAZ ParaImprimir;

       contra_d  = #404#1; contra_td = #404#2;
       contra_h  = #404#1; contra_th = #404#2;
       #1#4 = #404#8;   || 2§ asiento
       #1#5 = #404#9;
       #1#7 = #8#13;
       #1#8 = #404#3;
       #1#9 = #404#4 + #404#5;
       |HAZ ParaImprimir;
   |FINSI;
   |CIERRA #404;
   |REPON_DATOS #1;
|FPRC;

|CALCULO buclepan;

  sw_con = 1;
  |SI #0#4 = "N";  |HAZ concepto;  |FINSI;
  |SI sw_con = 0;  |ACABA;  |FINSI;

  |HAZ cuenta_ana;
  |SI swsel = 0; |ACABA; |FINSI;

  |SI sw_prim  = 0;
      cuenta = #1#4;
      sw_prim  = 1;
      |HAZ leecuen;
  |FINSI;

  |SI cuenta ! #1#4;
      |SI sw_print = 1;
          |PIEINF;
      |FINSI;
      |HAZ borrar;
      cuenta = #1#4;
      |HAZ leecuen;
  |FINSI;

   nSwAper = 0;
   |SI nDia8 ! -1;
       |SI #1#0 = nDia8; |Y #1#1 = fFec8; |Y #1#2 = nDoc8;
           |HAZ Apertura2008;
           nDiaAper = 1;
           |ACABA;           ||este asiento se trata de forma diferente
       |FINSI;
   |FINSI;

   |HAZ ParaImprimir;
|FCAL;

|CALCULO ParaImprimir;
   #4#18 = #1#0;                         || Diario
   alfa1 = #1#1;
   #4#8 = (alfa1 % 102) + (alfa1 % 402); || Fecha
   #4#9 = #1#26;                         || Referencia
   #4#10 = #1#7;                         || Descripcion
   #4#11 = #1#8;                         || Signo
   #4#12 = #1#9;                         || Importe;
   #4#17 = #1#29;                        || Cuenta Analitica
   |SI #4#11 = "D";
       debe  = #1#9;
       haber = 0;
   |SINO;
       haber = #1#9;
       debe  = 0;
   |FINSI;

   |SI #1#1 ] #0#2; |Y #1#1 [ #0#3;
       |HAZ leecabecera;

       #4#5  = #4#14;
       #4#6  = #4#15;
       #4#7  = #4#16;
       #4#13 = #4#16;
       |PRINT;
       |HAZ suma_totales;
   |SINO;
       || .... suma saldos anteriores a la fecha de pantalla
       |SI #0#37 = "S";
           |HAZ suma_totales;
           #4#5  = #4#14;
           #4#6  = #4#15;
           #4#7  = #4#16;
           #4#13 = #4#16;
           sw_cab = 1;
       |FINSI;
   |FINSI;
|FCAL;

|PROCESO leecabecera;
  |DDEFECTO #2;
  #2#0 = #1#0;
  #2#1 = #1#1;
  #2#2 = #1#2;
  |LEE 001#2=;
  |SI nSwAper ! 0;
      |SI nSwAper = 1;
          nAsto8 = #2#3 - 1; |SI nAsto8 [ 0; nAsto8 = 1; |FINSI;
          #2#3 = nAsto8;
      |SINO;
          #2#3 = nAsto8 + 1;
      |FINSI;
  |FINSI;
  #4#19 = #2#3;
|FPRC;

|DEFBUCLE caextrpa0;
 #1#3;
 0, 6, 26, 21, 28;
 #0#0, finic, 000001, 0001, #0#35, #0#31, #0#33, #0#38, #0#29;
 #0#1, #0#3,  999999, 9999, #0#36, #0#32, #0#34, #0#39, #0#30;
 e;
 NULL,buclepan;
|FIN;


|CALCULO impre;
  sw = 0;

  |ABRE #2;
  |ABRE #3;
  |ABRE #5;
  finic = fec1; |SI #0#37 = "N"; finic = #0#2; |FINSI;
  incta = #0#43;
  ficta = #0#44;
  |SI #0#48 = "N";
       |PARA nCampo = 49; |SI nCampo [ 58; |HACIENDO nCampo = nCampo + 1;
             alfa1 = #0nCampo; |QBF alfa1;
             |SI alfa1 ! "";
                 #0#0 = #0nCampo;
                 #0#1 = #0nCampo;
                 |BUCLE caextrpa0;
             |FINSI;
       |SIGUE;
   |SINO;
       |BUCLE caextrpa0;
  |FINSI;
  |CIERRA #2;
  |CIERRA #3;
  |CIERRA #5;

  |SI sw_print = 1;  |PIEINF; |FINSI;
|FCAL;

|CALCULO borrar;
 |DDEFECTO #4;
 sw_print = 0;
 nDiaAper = 0;
|FCAL;

|CALCULO suma_totales;
 |SI #1#8 = "D";
     #4#14 = #4#14 + #1#9;
 |SINO;
     #4#15 = #4#15 + #1#9;
 |FINSI;
  #4#16 = #4#14 - #4#15;
 sw_print = 1;
|FCAL;

|| **********************************************************************
|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #2 dirfich0;
   |PATH_DAT #3 dirfich0;
   |PATH_DAT #5 dirfich0;
   |PATH_DAT #7 dirfich0;
   |PATH_DAT #13 dirfich0;
   |PATH_DAT #107 dirfich0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 000001;
 999999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;
