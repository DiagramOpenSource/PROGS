|FICHEROS;
   :/basica/def/agicen14 #114;
   :/basica/def/agifigen #118;
   :/basica/def/agim0019 #119;

   :/basica/def/agim0051 #151;
   :/basica/def/agim0002 #102;
   :/basica/def/agim0050 #150;

   camansde;
   :/basica/def/agicen17;
   :/modelos/def/dsmom033;
   maempr1@ #211;

   cawdatos #900;
   camaasic #110;
   cacreano #260;
|FIN;

|VARIABLES;
  aAlfa  = "";
  aAlfa1 = "";
  aAlfa2 = "";
  aAlfa3 = "";

  nNume1 = 0;
  nNume2 = 0;
  nNume3 = 0;

  nPara1 = 0;
  nMes      = 0;
  nElAny    = 0;
  nPin1 = 0;
  aPin1 = "";
  efPn10 = @;
  efPn11 = @;
  swExiste = 0;
  aLaFecha1 = "";
  aLaFecha2 = "";

  nSwAlgun     = 0;
  nImporte     = 0;
  nSwExi       = 0;
  &enTipSoc    = 0;
  &eaTipSoc    = "";
  &SwPeqDimen  = 0;
  fFecha       = @;
  nDiario      = 0;
  aMas         = "";
  aDirInicio   = "";
  &aparam      = "";
  &eNoLeo      = 0;
|FIN;

|INCLUYE dscont_i;

|| ------------------------------------------------
|PROCESO LaActDefecto;
    |ABRE #agicen14;
    swNoExis = 1;
    #agicen14#0 = #900#0; || Empresa
    #agicen14#1 = eaDfDepC;
    |LEE 041#agicen14.=;
    |SI FSalida = 0;
        swNoExis = 0;
        #900#5 = #agicen14#9;
        #900#6 = #agicen14#10;
        #900#7 = #agicen14#7;
        #900#8 = #agicen14#8;
        efPn10 = #agicen14#5;
        efPn11 = #agicen14#6;
        |SI enEjercicio ] 2009;
            #900#26 = #agicen14#20;  ||CNA
        |SINO;
            #900#26 = #agicen14#21;  ||CNA
        |FINSI;
    |FINSI;
    |CIERRA #agicen14;
|FPRC;

|PROCESO PrimeraAct;
    #900#5 = #agicen14#9;
    #900#6 = #agicen14#10;
    #900#7 = #agicen14#7;
    #900#8 = #agicen14#8;
    efPn10 = #agicen14#5;
    efPn11 = #agicen14#6;
    |SI enEjercicio ] 2009;
        #900#26 = #agicen14#20;  ||CNA
    |SINO;
        #900#26 = #agicen14#21;  ||CNA
    |FINSI;

    |SI #agicen14#6 [ "01.01.0000"; |O #agicen14#6 ] fFecha;
        |ERROR10;
    |FINSI;
|FPRC;

|DEFBUCLE PrimeraAct;
    #agicen14#1;
    ;
    #900#0, 01;
    #900#0, 99;
    ;
    NULL, PrimeraAct;
|FIN;

|PROCESO LeeDatosEmpresa;

  |DDEFECTO #900;
  #900#0 = enEmpresa;

  nPin1 = 0;
  aPin1 = "";
  efPn10 = "01.01.0000";
  efPn11 = "01.01.0000";
  fFecha = "31.12.2999";
  |SI enEjercicio ! 0;
      aAlfa1 = enEjercicio;
      aAlfa1 = "01.01."+ aAlfa1;
      fFecha = aAlfa1;
  |FINSI;

 || Lee Actividad
  swNoExis = 1;
  |SI eaDepar = "S"; |Y eaDfDepC > "  ";
      |HAZ LaActDefecto;
  |FINSI;
  |SI swNoExis = 1;
      |BUCLE PrimeraAct;
  |FINSI;

 || Lee Ficha Sociedades
    swExiste = 0;
    |ABRE #agim0019;
    #agim0019#0 = #900#0; || Empresa
    #agim0019#1 = 99;
    |LEE 041#agim0019.];
    |SI FSalida = 0;
        |LEE 040#agim0019.a;
    |SINO;
        |LEE 040#agim0019.u;
    |FINSI;
    |SI FSalida = 0; |Y #agim0019#0 = #900#0;
         swExiste = 1;
    |FINSI;

    |SI swExiste = 1;
        efPn10 = #agim0019#7;      || Fecha Activa
        efPn11 = #agim0019#55;     || Fecha Inactiva
        nPin1 = #agim0019#4;
        aPin1 = #agim0019#5;
    |FINSI;
    |CIERRA #agim0019;

    |SI eNoLeo = 0;
        |ABRE #agifigen;
        #agifigen#0 = #900#0; || Empresa
        |LEE 041#agifigen.=;
    |SINO;
        FSalida = 0;
    |FINSI;
    |SI FSalida = 0;
        efPn10  = #agifigen#93;
        efPn11  = #agifigen#94;
        nPin1   = #agifigen#87;
        aPin1   = #agifigen#88;
        #900#1 = #agifigen#13;  || Nif
        #900#2 = #agifigen#1;   || Nombre
    |FINSI;
    |SI eNoLeo = 0;
        |CIERRA #agifigen;
    |FINSI;
    |SI efPn10 [ "01.01.1900"; efPn10 = "01.01.0000"; |FINSI;
    |SI efPn11 [ "01.01.1900"; efPn11 = "01.01.0000"; |FINSI;
    |SI efPn11 > "01.01.1900"; eaIa = "I"; |FINSI;
    #900#3 = nPin1;
    #900#4 = aPin1;
    #900#9 = efPn10;
    #900#10 = efPn11;

    #900#11 = "ACTIVA";
    |SI eaIa = "I"; #900#11 = "INACTIVA"; |FINSI;
    #900#12 = enEjercicio;

    |HAZ FecApeCie;
    #900#13 = efAper;
    #900#14 = efCier;

    enM1f = enM1;
    enM2f = enM2;
    |HAZ LaFechaEmp;
    |SI aparam = "fech"; |O aparam = "Situacionfech";
        |SI dirfich0 ! "";
            |PATH_DAT #260 dirfich0;
        |FINSI;
        |ABRE #260;
        |DDEFECTO #260;
        |LEE 041#260p;
        |SI FSalida = 0;
            |SI #260#2 ] #900#13;  #900#13 = #260#2; |FINSI;
            |SI #260#3 [ #900#14;  #900#14 = #260#3; |FINSI;
            aAlfa = #900#13;  aAlfa = aAlfa % 402;  enM1f = aAlfa;
            aAlfa = #900#14;  aAlfa = aAlfa % 402;  enM2f = aAlfa;
            |SI enM1 = 0; |Y #260#0 = 00; |Y fec1 = #900#13;
                enM1f = 00;
            |FINSI;
            |SI enM2 = 0; |Y #260#0 = 00; |Y fec1 = #900#13;
                enM2f = 00;
            |FINSI;
        |FINSI;
        |CIERRA #260;
    |FINSI;

    |SI aparam = "Analitica"; |Y eaNomAna ! "";
        #900#2 = eaNomAna;
    |FINSI;

    |SI #900#13 > #900#14;
        #900#14 = #900#13;
    |FINSI;

    |SI nPin1 = 2;
        |HAZ ElTipoEmpSoc;
        #900#25 = eaTipSoc;
    |FINSI;
|FPRC;

|PROCESO LeeDatosActividad;

  || Lee Actividad en concreto.
    |ABRE #agicen14;
    swNoExis = 0;
    |SI enActividad ! 0;
        #agicen14#0 = #900#0; || Empresa
        #agicen14#1 = enActividad;
        |LEE 041#agicen14.=;
        |SI FSalida = 0;
            #900#15 = #agicen14#1;   ||Act.
            #900#16 = #agicen14#2;   ||Tipo Epig.
            #900#17 = #agicen14#3;   ||Epigrafe
            #900#18 = #agicen14#4;   ||Descripcion Actividad
            #900#19 = #agicen14#5;   ||Fecha Inicio
            #900#20 = #agicen14#6;   ||Fecha Fin
            #900#21 = #agicen14#11;  ||Sector actividad
            #900#22 = #agicen14#12;  ||Sector actividad
            #900#23 = #agicen14#16;  ||Tipo Actividad
            #900#24 = #agicen14#17;  ||Descripcion Tipo Actividad
            |SI enEjercicio ] 2009;
                #900#26 = #agicen14#20;  ||CNA
            |SINO;
                #900#26 = #agicen14#21;  ||CNA
            |FINSI;
       |SINO;
            |PDEFECTO #900,15,27;
            #900#28 = "";
            #900#29 = "";
            #900#30 = "";
            swNoExis = 1;
       |FINSI;
    |FINSI;

    |CIERRA #agicen14;
|FPRC;

|PROCESO LeeDatosSubdepar;

   eswLect = 0;
   |SALVA_DATOS #agicen14;

   |ABRE #agicen14;
   #agicen14#0 = #900#0; || Empresa
   #agicen14#1 = #900#15;
   |LEE 041#agicen14.=;
    enTipoTribut = #agicen14#9;
   |CIERRA #agicen14;
   |REPON_DATOS #agicen14;

   eaNomSubdep = "";
   eaNo1Subdep = "";

   |SI enSubde = 1;

       |ABRE #camansde;
       |DDEFECTO #camansde;
       #camansde#0 = #900#0;
       #camansde#1 = #900#15;
       #camansde#2 = eaCodSubdep;
       |LEE 040#camansde.=;
       eaNomSubdep = #camansde#3;
       eaNo1Subdep = #camansde#4;
       |CIERRA #camansde;

   |SINO;

       |SI enTipoTribut = 6;  |O enTipoTribut = 7;
        |O enTipoTribut = 11; |O enTipoTribut = 12;
        |O enTipoTribut = 16; |O enTipoTribut = 17;

           enCultivo = eaCodSubdep;
           |ABRE #dsmom033;
           |DDEFECTO #dsmom033;
           #dsmom033#0 = enCultivo;
           |LEE 001#dsmom033.=;
           eaNomSubdep = #dsmom033#1 % 140;
           eaNo1Subdep = #dsmom033#9;
       |SINO;
           |ABRE #agicen17;
           |DDEFECTO #agicen17;
           #agicen17#0 = #900#0;
           #agicen17#1 = #900#15;
           #agicen17#2 = eaCodSubdep;
           |LEE 040#agicen17.=;
           eaNomSubdep = #agicen17#3;
           eaNo1Subdep = #agicen17#5;
           |CIERRA #agicen17;
        |FINSI;
    |FINSI;

    #900#28 =  eaCodSubdep;
    #900#29 =  eaNomSubdep;
    #900#30 =  eaNo1Subdep;
|FPRC;

|PROCESO LaFechaEmp;
    |SI enM1 = 13;
        #900#13 = efCier;
    |SINO;
        |SI enM1 ] 1;
            nMes = enM1; |HAZ LaFecha;
            #900#13 = aLaFecha1;
        |FINSI;
    |FINSI;

    |SI enM2 = 0;
        #900#14 = efAper;
    |SINO;
        |SI enM2 [ 12;
            nMes = enM2; |HAZ LaFecha;
            #900#14 = aLaFecha2;
        |FINSI;
    |FINSI;
|FPRC;

|PROCESO LaFecha;

  nElAny = enEjercicio;
  |SI nMes ! 0; |Y nMes ! 13;
      NumMes = dig1 + (nMes - 1);
      |SI NumMes > 12; NumMes = NumMes - 12; |FINSI;
  |SINO;
      NumMes = nMes;
  |FINSI;
  |SI NumMes < dig1; nElAny = nElAny + 1; |FINSI;

  aAlfa1 = NumMes; |QBF aAlfa1;
  aAlfa1 = ("00" + aAlfa1) % -102;
  aAlfa2 = nElAny;

  aLaFecha1 = "01." + aAlfa1 + "." + aAlfa2;

  aAlfa3 = "31.";
  |SI NumMes = 4; |O NumMes = 6; |O NumMes = 9; |O NumMes = 11;
      aAlfa3 = "30.";
  |SINO;
      |SI NumMes = 2;
          aAlfa3 = "28.";
          nNume1 = nElAny / 4; nNume2 = (nElAny / 4) ! 0;
          |SI nNume1 = nNume2;
              aAlfa3 = "29.";
          |FINSI;
      |FINSI;
  |FINSI;
  aLaFecha2 = aAlfa3 + aAlfa1 + "." + aAlfa2;
|FPRC;

|| --------------------------------------------------------------

|PROCESO LeeTipSoc;

 nSwExi   = 0;
 enTipSoc = 0;
 eaTipSoc = "";

 |ABRE #151;
 #151#0 = enEmpresa;
 |SI enEjercicio ! -1;
     #151#1 = enEjercicio;
     |LEE 001#151=;
     |SI FSalida ! 0;
         nSwExi = 1;
     |SINO;
         enTipSoc = #151#2;
         eaTipSoc = #151#3;
     |FINSI;
 |SINO;
     #151#1 = 9999;
     |LEE 001#151];
     |SI FSalida = 0;
         |LEE 040#151a;
     |SINO;
         |LEE 040#151u;
     |FINSI;
     nSwExi = 1;
     |SI FSalida = 0; |Y #151#0 = enEmpresa;
         enTipSoc = #151#2;
         eaTipSoc = #151#3;
         nSwExi = 0;
     |FINSI;
 |FINSI;
 |CIERRA #151;
|FPRC;

|PROCESO LeeControl;
        |ABRE #150;
        #150#0 = enEjercicio;
        |LEE 001#150=;
        |SI FSalida ! 0;
            |DDEFECTO #150;
            #150#0 = enEjercicio;
            #150#4 = enEjercicio - 1;
            |GRABA 020#150n;
        |FINSI;
        |CIERRA #150;
|FPRC;

|PROCESO ElVolumen;
 nSwAlgun = 1;
 nImporte = nImporte + #102#13;
|FPRC;

|DEFBUCLE ElVolumen;
 #102#1;
 ;
 enEmpresa, 00, #150#4;
 enEmpresa, 99, #150#4;
 e;
 NULL, ElVolumen;
|FIN;


|PROCESO GrabaEmpresa;

        enTipSoc    = 0;
        eaTipSoc    = "";
        |HAZ LeeControl;

        nSwAlgun = 0;
        nImporte = 0;
        |BUCLE ElVolumen;
        |SI nSwAlgun = 1;
            |ABRE #151;
            #151#0 = enEmpresa;
            #151#1 = enEjercicio;
            |LEE 101#151=;
            |SI FSalida ! 0;
                |DDEFECTO #151;
                #151#0 = enEmpresa;
                #151#1 = enEjercicio;
                |GRABA 020#151b;
            |FINSI;

            |SI nImporte [ #150#1;
                #151#2 = 1;
            |SINO;
                |SI nImporte > #150#2; #151#2 = 2; |FINSI;
                |SI nImporte > #150#3; #151#2 = 3; |FINSI;
            |FINSI;
            |SI #151#2 = 1; #151#3 = "EMPRESA PYME";    |FINSI;
            |SI #151#2 = 2; #151#3 = "EMPRESA NO PYME"; |FINSI;
            |SI #151#2 = 3; #151#3 = "GRAN EMPRESA";    |FINSI;

            #151#4 = nImporte;
            #151#5 = eaNombreEmp;
            |GRABA 020#151a;
            |LIBERA #151;
            enTipSoc = #151#2;
            eaTipSoc = #151#3;
            |CIERRA #151;
        |FINSI;
|FPRC;

|PROCESO ElTipoEmpSoc;
     enCooperativa = -1;

     enTipSoc = 0;
     eaTipSoc = "";
     |HAZ LeeTipSoc;
     |SI nSwExi = 1;
         |SI enEjercicio = -1;
             fFecha = ~;
             aAlfa1 = fFecha % -104;
             enEjercicio = aAlfa1;
         |FINSI;
         |HAZ GrabaEmpresa;
     |FINSI;

     |SI HaySociedad = 1;
         enCooperativa = 0;
         SwPeqDimen    = 0;

         |DBASS aDirInicio;
         aMas  = aDirInicio + "contasoc/def/maempr11";
         |CARGA_DEF aMas, maempr1@;
         |SI FSalida = 0;
             |ABRE #211;
             #211#0 = enEmpresa;
             #211#1 = enEjercicio;
             #211#2 = 6;
             |LEE 040#211=;
             |SI FSalida = 0; SwPeqDimen = 1; |FINSI;

             #211#0 = enEmpresa;
             #211#1 = enEjercicio;
             #211#2 = 17;
             |LEE 000#211=;
             |SI FSalida = 0;  enCooperativa = 17;  |FINSI;

             #211#0 = enEmpresa;
             #211#1 = enEjercicio;
             #211#2 = 18;
             |LEE 000#211=;
             |SI FSalida = 0;  enCooperativa = 18;  |FINSI;

             #211#0 = enEmpresa;
             #211#1 = enEjercicio;
             #211#2 = 19;
             |LEE 000#211=;
             |SI FSalida = 0;  enCooperativa = 19;  |FINSI;

             |CIERRA #211;
             |SI SwPeqDimen = 1; |Y enTipSoc > 1; enTipSoc = 0; |FINSI;
             |DESCARGA_DEF #211;
         |FINSI;
     |FINSI;
|FPRC;


|PROCESO LeeDiario;
 fFecha = #110#1;
 |ERROR10;
|FPRC;

|DEFBUCLE LeeDiario;
 #110#1;
 ;
 nDiario, fec1, 000001;
 nDiario, fec2, 999999;
 ;
 NULL, LeeDiario;
|FIN;

|PROCESO FecApeCie;
  |SI dirfich0 ! "";
      |PATH_DAT #110 dirfich0;
  |FINSI;

  fFecha = fec1; nDiario = 0;  |BUCLE LeeDiario; efAper = fFecha;
  aAlfa1 = efAper; aAlfa1 = aAlfa1 % 402; enMAper = aAlfa1;

  fFecha = fec2; nDiario = 99; |BUCLE LeeDiario; efCier = fFecha;
  aAlfa1 = efCier; aAlfa1 = aAlfa1 % 402; enMCier = aAlfa1;

|FPRC;

|PROCESO CuentoAct;  || Mas de una Actividad
 |SI #114#5 > "01.01.0000";
     |SI #114#5 > fec2;  |ACABA;  |FINSI;
 |FINSI;
 |SI #114#6 > "01.01.0000";
     |SI #114#6 < fec1;  |ACABA;  |FINSI;
 |FINSI;
 enNumAct = enNumAct + 1;
|FPRC;

|DEFBUCLE CuentoAct;
 #114#1;
 ;
 enEmpresa, 01;
 enEmpresa, 99;
 ;
 NULL, CuentoAct;
|FIN;

|PROCESO CuentoActiv;
 enNumAct = 0;

 |HAZ DirAplicSt;
 |SI Haybasica = 1;
     |BUCLE CuentoAct;
 |FINSI;
|FPRC;
