|FICHEROS;
 caut0017;

 camaasil;
 camaniva #1;
 caivalin;

 caivaasi;
 camanads;

 caivatm4;
|FIN;

|VARIABLES;
 aAlfa1 = "";
 aAlfa2 = "";
 nNume1 = 0;
 nNume2 = 0;
 nPara1 = 0;
 nPara2 = 0;

 SwCambia   = 0;
 aElDepar   = "";
 aElSubde   = "";
 SwCambiaL  = 0;
 aElDeparL  = "";
 aElSubdeL  = "";

 &snFilBa   = "";
 &eaMulDep  = "";
 &eaNomFich = "";
 &enQSerie  = 0;

 aParam = "";
 nLinea = 0;
 nDAcum = 0;
 nHAcum = 0;
 nDesde = 0;
 nHasta = 0;
 nEmpresa = 0;
 nPeriodo = 0;
|FIN;

|INCLUYE dscont_i;

|| **********************************************************************
|CALCULO libroh; | TIPO 0;   ||  hasta diario
   |SI #caut0017#2 < #caut0017#1;  |ERROR;  |FINSI;
|FCAL;

|CALCULO hque; |TIPO 7;
 |SI enQSerie = 0;
     |C_M #caut0017#3,1,"N"; #caut0017#3 = "";      |PINTA #caut0017#3;
     |C_M #caut0017#4,1,"N"; #caut0017#4 = "zzzzz"; |PINTA #caut0017#4;
 |SINO;
     |C_M #caut0017#3,1,"S";
     |C_M #caut0017#4,1,"S";
 |FINSI;
|FCAL;

|CALCULO serieh; | TIPO 0;   ||  hasta documento
   |SI #caut0017#4 < #caut0017#3;  |ERROR;  |FINSI;
|FCAL;

|CALCULO facturah; | TIPO 0;   ||  hasta documento
   |SI #caut0017#6 < #caut0017#4;  |ERROR;  |FINSI;
|FCAL;

|CALCULO LaFecha; |TIPO 0;
 |SI #caut0017#Campo# < fec1; |O #caut0017#Campo#  > fec2;
     |MENAV "    Error, Fecha Incorrecta ";
     |ERROR;
     |ACABA;
 |FINSI;
 |SI Campo = 8; |Y #caut0017#7 > #caut0017#8;
     |MENAV "    Error, Fecha Incorrecta ";
     |ERROR;
 |FINSI;
|FCAL;

|CALCULO LaParri; |TIPO 0;
  |SI #caut0017#9 = "S";
      aAlfa1 = "S"; aAlfa2 = "N";
      #caut0017#12 = 0; |PINTA #caut0017#12;
      #caut0017#13 = 0; |PINTA #caut0017#13;
      #caut0017#14 = 0; |PINTA #caut0017#14;
      #caut0017#15 = 0; |PINTA #caut0017#15;
      #caut0017#16 = 0; |PINTA #caut0017#16;
  |SINO;
      aAlfa2 = "S"; aAlfa1 = "N";
      #caut0017#10 = 001; |PINTA #caut0017#10;
      #caut0017#11 = 999; |PINTA #caut0017#11;
  |FINSI;
  |C_M #caut0017#10,1,aAlfa1;
  |C_M #caut0017#11,1,aAlfa1;
  |C_M #caut0017#12,1,aAlfa2;
  |C_M #caut0017#13,1,aAlfa2;
  |C_M #caut0017#14,1,aAlfa2;
  |C_M #caut0017#15,1,aAlfa2;
  |C_M #caut0017#16,1,aAlfa2;
|FCAL;

|CALCULO TrasConfi; |TIPO 6;
  |C_M #caut0017#Campo#,0,aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

  |SI FSalida = 2; |ACABA; |FINSI;
  |SI #caut0017#Campo#0 = 0; |ACABA; |FINSI;

  |ABRE #camanads;
  #camanads#0 = #caut0017#Campo#;
  |LEE 041#camanads.=;
  |SI FSalida ! 0;
      |MENAV "0000Error. No existe numero de Configuracion";
      |ERROR;
  |FINSI;
  |CIERRA #camanads;
|FCAL;

|| **********************************************************************
|PROCESO LineasAsto0;
 #camaasil#21 = aElDeparL;
 #camaasil#28 = aElSubdeL;
 |GRABA 020#camaasil.a;
 |LIBERA #camaasil;
|FPRC;

|DEFBUCLE LineasAsto;
  #camaasil#1;
  12,13,14,15,20;
  #1#7, #1#8, #1#9, 0001, #1#36, #1#0, #1#3, #1#2, nDAcum;
  #1#7, #1#8, #1#9, 9999, #1#36, #1#0, #1#3, #1#2, nHAcum;
  be;
  NULL, LineasAsto0;
|FIN;

|| ////////////////////////////////////////////////////////////////////////
|PROCESO LinConfigura;

  |SI #camaniva#2 < #camanads#4; |ACABA; |FINSI;
  |SI #camaniva#2 > #camanads#5; |ACABA; |FINSI;

  |SI #caivalin#12 < #camanads#6; |ACABA; |FINSI;
  |SI #caivalin#12 > #camanads#7; |ACABA; |FINSI;

  |SI #camanads#8 ! "X"
      SwCambiaL = 1;
      aElDeparL = eaDfDepC;
      |SI #camanads#8 = "V";
          aElDeparL = #camanads#9;
      |FINSI;
  |FINSI;
  |SI #camanads#10 ! "X";
       SwCambiaL = 1;
       aElSubdeL = eaDfSubC;
       |SI #camanads#10 = "V";
           aElSubdeL = #camanads#11;
       |FINSI;
  |FINSI;

||   |SI #caut0017#9 = "S";  |ERROR10;  |FINSI;
|FPRC;

|DEFBUCLE LinConfigura;
  #camanads#1;
  1;
  nDesde, #camaniva#0;
  nHasta, #camaniva#0;
  e;
  NULL, LinConfigura;
|FIN;

|| ////////////////////////////////////////////////////////////////////////
|PROCESO LeeDepySubd;
    eOpDep = 0;
    enActividad = #camaniva#10;
    eaCodDep    = #camaniva#10;
    |HAZ Actividad;
    #camaniva#24 = eaNombreAct;

    eOpSub = 0;
    eaNomSubdep = "";
    enConcepto  = #camaniva#27;
    eaCodSubdep = #camaniva#11;
    |SI eaSubde = "S"; enQue = 1; |FINSI;
    |SI eaCodSubdep ! "   ";
        |HAZ SubActividad;
        enQue = 0;
    |FINSI;
    #camaniva#25 = eaNomSubdep;
|FPRC;

|| ////////////////////////////////////////////////////////////////////////
|PROCESO LineasIVA0;
 aAlfa1 = #caivalin#30; |QBF aAlfa1;
 |SI aAlfa1 = "";
     #caivalin#30  = #camaniva#10;
     aAlfa1 = #caivalin#31; |QBF aAlfa1; |SI aAlfa1 = ""; #caivalin#31 = #camaniva#11; |FINSI;
 |FINSI;

 |SI eaMulDep = "S"; |O nLinea = 0;  || multi o la primera

     SwCambiaL = 0;
     aElDeparL = #caivalin#30;
     aElSubdeL = #caivalin#31;

     |SI #caut0017#9 = "S";
         nDesde = #caut0017#10;
         nHasta = #caut0017#11;
         |BUCLE LinConfigura;
     |SINO;
         |ABRE #camanads;
         |PARA nPara1 = 12; |SI nPara1 [ 16; |HACIENDO nPara1 = nPara1 + 1;
               |SI #caut0017#nPara1# ! 0;
                   #camanads#0 = #caut0017#nPara1#;
                   |LEE 041#camanads.=;
                   |SI FSalida = 0; |Y #camanads#1 = #camaniva#0;
                       |HAZ LinConfigura;
                   |FINSI;
               |FINSI;
         |SIGUE;
         |CIERRA #camanads;
     |FINSI;
  |FINSI;

  |SI SwCambiaL = 1;
      #caivalin#30 = aElDeparL;
      #caivalin#31 = aElSubdeL;
      |GRABA 020#caivalin.a;
      |SI nLinea = 0;
          aElDepar = aElDeparL;
          aElSubde = aElSubdeL;
          SwCambia = 1;
      |FINSI;
      nDAcum = #caivalin#2;
      nHAcum = #caivalin#2;
      |BUCLE LineasAsto;
  |FINSI;
  |LIBERA #caivalin;
  nLinea = 1;
|FPRC;

|DEFBUCLE  LineasIVA;
  #caivalin#1;
  ;
  #camaniva#0, #camaniva#1, 01;
  #camaniva#0, #camaniva#1, 99;
  be;
  NULL, LineasIVA0;
|FIN;

|PROCESO CabecIVA0;

 SwCambia  = 0;
 aElDepar    = #camaniva#10;
 aElSubde    = #camaniva#11;

 nLinea    = 0;
 SwCambiaL = 0;
 |BUCLE LineasIVA;

 #camaniva#10 = aElDepar;
 #camaniva#11 = aElSubde;

 |SI SwCambia = 1;
     |HAZ LeeDepySubd;
     |GRABA 020#camaniva.a;

     aElDeparL = #camaniva#10;
     aElSubdeL = #camaniva#11;
     nDAcum = 00;
     nHAcum = 00;
     |BUCLE LineasAsto;
 |FINSI;
 |LIBERA #camaniva;
|FPRC;

|DEFBUCLE CabecIVA;
  #camaniva#3;
  4;
  #caut0017#1, #caut0017#3, #caut0017#5, #caut0017#7;
  #caut0017#2, #caut0017#4, #caut0017#6, #caut0017#8;
  be;
  NULL, CabecIVA0;
|FIN;

|PROCESO IniEmpresa;

    cod1 = #caivatm4#0;
    cod2 = #caivatm4#1;
    emEmp = 1;  || Sin mensaje
    |HAZ leelo;
    |SI swerror ! 0;  |ACABA;   |FINSI;

    |SI nEmpresa ! -1; |CIERRA #camaniva; |FINSI;

    |DBASE dirfich0; |QBF dirfich0;
    dirfich0 = dirfich0 + "fich/";
    aAlfa1   = ("00000" + #caivatm4#0) % -105;
    aAlfa2   = ("0"     + #caivatm4#1) % -101;
    dirfich0 = dirfich0 + aAlfa1 + aAlfa2 + "/";
    |QBF dirfich0;

    |PATH_DAT #camaasil#dirfich0#;
    |PATH_DAT #camaniva#dirfich0#;
    |PATH_DAT #caivalin#dirfich0#;
    |PATH_DAT #caivaasi#dirfich0#;
    |PATH_DAT #camanads#dirfich0#;

    |ABRE #caivaasi;
    |DDEFECTO #caivaasi;
    |LEE 001#caivaasi.p;
    |CIERRA #caivaasi;
    snFilBa  = #caivaasi#119;
    eaMulDep = #caivaasi#132;
    enQSerie = 0;
    |SI #caivaasi#44 = "S"; enQSerie = 1;|FINSI;
    #caut0017#7 = fec1;
    #caut0017#8 = fec2;

    |ABRE #camaniva;
|FPRC;

|PROCESO PorAuxiliar;
  |SI nEmpresa ! #caivatm4#0; |O nPeriodo ! #caivatm4#1;
      |HAZ IniEmpresa;
      |SI swerror ! 0;  |ACABA;   |FINSI;
      nEmpresa = #caivatm4#0;
      nPeriodo = #caivatm4#1;
  |FINSI;

  #camaniva#0 = #caivatm4#2;
  #camaniva#1 = #caivatm4#3;
  |LEE 141#camaniva.=;
  |SI FSalida = 0;
      |HAZ CabecIVA0;
  |SINO;
      |LIBERA #camaniva;
  |FINSI;
|FPRC;

|DEFBUCLE PorAuxiliar;
  #caivatm4#1;
  ;
  00001, 0, 01, 0000000001;
  99999, 9, 99, 9999999999;
  e;
  NULL, PorAuxiliar;
|FIN;

|| /////////////////////////////////////////////////////////////////////
|CALCULO ElCambio; |TIPO 12;

  |SI #caut0017#17 = "N";
      |ACABA;
  |FINSI;

  |SI aParam = "";
      |BUCLE CabecIVA;
  |SINO;
      |NOME_DAT #caivatm4#eaNomFich#;
      nEmpresa = -1;
      nPeriodo = -1;
      |BUCLE PorAuxiliar;
      |SI nEmpresa ! -1;
         |CIERRA #camaniva;
      |FINSI;
  |FINSI;
|FCAL;

|PROGRAMA;
   aParam = PARAMETRO;
   |QBF aParam;

 |SI aParam = "";
   |HAZ inicio;
   |SI eaDepar = "N"; |Y eaSubde = "N";
       |SI aParam = "";
          |CONFI "0000NEsta Empresa No tiene Departamento ni Subdepartamento. Desea Continuar";
          |SI FSalida ! 0;
              |ACABA;
          |SINO;
              eaDepar = "S";
              eaSubde = "S";
          |FINSI;
       |SINO;
          |ACABA;
       |FINSI;
   |FINSI;
 |FINSI;

   |HAZ DirAplicSt;

   |DDEFECTO #caut0017;

   |SI aParam = "";

       |ABRE #caivaasi;
       |DDEFECTO #caivaasi;
       |LEE 001#caivaasi.p;
       |CIERRA #caivaasi;
       snFilBa  = #caivaasi#119;
       eaMulDep = #caivaasi#132;
       enQSerie = 0;
       |SI #caivaasi#44 = "S"; enQSerie = 1;|FINSI;
       #caut0017#7 = fec1;
       #caut0017#8 = fec2;

       |CLS;
       |CABEZA "Asig.Auto Depar/Subdepar";
       |PINPA #0#caut0017;
       |PINDA #0#caut0017;
       |ENDATOS #1#caut0017;

   |SINO;

       #caut0017#17 = "S";
       |HAZ ElCambio;

   |FINSI;
|FPRO;
|| **************************************************************************
