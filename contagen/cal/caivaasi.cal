|FICHEROS;
   caivaasi #0;
   :/modelos/def/dsmom030 #1;
   caforpag #3;

   cacuenta #4;
   calibros #10;
   caivases #11;

   :/modelos/def/dsmom002;
   :/modelos/def/dsmom026;
|FIN;

|VARIABLES;
   &entrada  = 1;
   &entrada1 = 1;
   alfa1 = "";
   alfa2 = "";
   alfa3 = "";
   alfa4 = "";
   nume1 = 0;
   trara  = "";
   para1  = 0;
   long   = 0;
   nSwLaC = 0;
   nSwError = 0;

   ConcepAnt = 0;
   ModoEntrada = 0;
   inkey = 0;

   swS = 0;
   swF = 0;
   swC = 0;
   swN = 0;
   swR = 0;
   swT = 0;
   aDepAnt    = "";
   aSubAnt    = "";
   &enCambio  = 0;
   aTPerio    = "";
   aTPerio110 = "";
   nSi340     = 0;
   nSi303     = 0;
   nSi111     = 0;
   nSwP       = 0;
   nSwGrupo   = 0;

   nSwFin     = 0;
   nBoton5    = 0;
   nTecla     = 0;
   aF5        = "";
   nHePulsado = 0;

   nEmpresa   = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE modulo_i;

|| ==================== Calculos Pantalla
|CALCULO AntLibro; |TIPO 7;
  |SI ModoEntrada ! 1; |ACABA; |FINSI;
  #0#73 = #0#3; |PINTA #0#73;
  #0#74 = #0#4; |PINTA #0#74;
|FCAL;

|CALCULO libro; |TIPO 0;
   |DDEFECTO #10;
   |ABRE #10;
   #10#0 = #0Campo;
   |LEE 000#10=;
   |CIERRA #10;

   |SI Campo = 3;  |O Campo = 73;
       |SI #10#2 ! "R";
           |MENAV "    ATENCION!!! Libro de I.V.A. Soportado ...";
           |ERROR;
       |FINSI;
   |SINO;
       |SI #10#2 ! "S";
           |MENAV "    ATENCION!!! Libro de I.V.A. Repercutido ...";
           |ERROR;
       |FINSI;
   |FINSI;
|FCAL;

|CALCULO ConcepAnt; |TIPO 7;
   ConcepAnt = #0Campo;
   |SI Campo = 57;
       #0#57 = #0#31; |PINTA #0#31;
   |FINSI;
   |SI Campo = 13;
       #0#13 = #0#5; |PINTA #0#13;
   |FINSI;
   |SI Campo = 77; |Y ModoEntrada = 1;
       #0#77 = #0#5; |PINTA #0#77;
   |FINSI;
|FCAL;

|CALCULO concepto;  |TIPO 0; || Filtros Conceptos
  inkey = FSalida;
  |C_M #0Campo,0,alfa1;
  |SI alfa1 = "N"; |ACABA; |FINSI;

  |SI inkey = 15; |Y Campo = 77;
      #0#77 = #0#5; |PINTA #0#77;
      #0#78 = #0#6; |PINTA #0#78;
      #0#79 = #0#7; |PINTA #0#79;
      #0#80 = #0#8; |PINTA #0#80;
  |FINSI;

  |ABRE #dsmom030;
  #dsmom030#0 = #0Campo;
  |LEE 041#dsmom030.=;
  |SI FSalida ! 0;  |DDEFECTO #dsmom030;  |FINSI;
  |CIERRA #dsmom030;

  |SI Campo = 50; |O Campo = 54; |O Campo = 139; |O Campo = 143;  || Concepto Cobro o Pago
      |SI #dsmom030#4 = "S"; |O #dsmom030#5 = "S";
          |SI #0#95 ! "X";
              |MENAV "    Error. Concepto no Corresponde a Cobros/Pagos";
          |FINSI;
          |SI #0#95 = "S";
              |ERROR;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;

  |SI #dsmom030#4 = "N";
      |SI #0#95 ! "X";
          |MENAV "    Error. Este Concepto no es de IVA";
      |FINSI;
      |SI #0#95 = "S";
          |ERROR;
      |FINSI;
      |ACABA;
  |FINSI;

  |SI Campo = 5; |O Campo = 13;   || Iva Repercutido
      |SI #0#3 ! #dsmom030#12;
          |SI #0#95 ! "X";
              |MENAV "    Error. Este Concepto no pertenece al Libro de IVA Repercutido";
          |FINSI;
          |SI #0#95 = "S";
              |ERROR;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;
  |SI Campo = 31; |O Campo = 57;   || Iva Soportado
      |SI #0#29 ! #dsmom030#14;
          |SI #0#95 ! "X";
              |MENAV "    Error. Este Concepto no pertenece al Libro de IVA Soportado";
          |FINSI;
          |SI #0#95 = "S";
              |ERROR;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;
  |SI Campo = 77;   || Iva Repercutido Englobadas
      |SI #0#73 ! #dsmom030#12;
          |SI #0#95 ! "X";
              |MENAV "    Error. Este Concepto no pertenece al Libro de IVA Repercutido";
          |FINSI;
          |SI #0#95 = "S";
              |ERROR;
          |FINSI;
      |FINSI;
  |FINSI;
|FCAL;

|CALCULO recoge; |TIPO 7;
  |C_M #0Campo,0,alfa1;
  |SI alfa1 = "N"; |ACABA; |FINSI;

   |SI Campo = 51; |O Campo = 55; |O Campo = 140; |O Campo = 144;
       nume1 = Campo - 1;
   |SINO;
       nume1 = Campo - 2;
   |FINSI;
   |SI #0nume1 ! ConcepAnt;
       |ABRE #dsmom030;
       #dsmom030#0 = #0nume1;
       |LEE 041#dsmom030.=;
       |SI FSalida ! 0;  |DDEFECTO #dsmom030;  |FINSI;
       #0Campo = #dsmom030#1;
       |PINTA #0Campo;
       |CIERRA #dsmom030;
       |PTEC 816;
   |FINSI;
|FCAL;

|CALCULO nodepar; |TIPO 7;
  |CAMPO_MODIFICA #0#19, 1, eaDepar;
  |CAMPO_MODIFICA #0#20, 1, eaDepar;
  |CAMPO_MODIFICA #0#132, 1, "S";
  |SI eaDepar = "N"; |Y eaSubde = "N";
      |CAMPO_MODIFICA #0#132, 1, "N";
      #0#132 = "N"; |PINTA #0#132;
  |FINSI;
|FCAL;

|CALCULO DatSii; |TIPO 7;
   alfa1 = "S";
   |SI enEjercicio < 2017;
       alfa1 = "N";
       #caivaasi#188 = "N"; |PINTA #caivaasi#188;
       #caivaasi#190 = "N"; |PINTA #caivaasi#190;
   |FINSI;
   |C_M #caivaasi#188, 1, alfa1;
   |C_M #caivaasi#190, 1, alfa1;
|FCAL;

|CALCULO cuenta; |TIPO 6;
  |C_M #0Campo,0,alfa1;
  |SI alfa1 = "N"; |ACABA; |FINSI;

  eaCuenta = #0Campo;
  salidas  = FSalida;
  |HAZ FiltraPuntos;
  #0Campo = eaCuenta;
  |PINTA #0Campo;

  emCta = 1;
  eaCuenta = #0Campo;
  |HAZ SacaNivel;
  |SI Campo = 39; #0#40 = enNivel;|FINSI;
|FCAL;

|PROCESO inf4;
 #4#0 = "            ";
 #4#3 = "S";
|FPRC;

|PROCESO sup4;
 #4#0 = "zzzzzzzzzzzz";
 #4#3 = "S";
|FPRC;

|PROCESO cuenta_esp; |TIPO 6;
  inkey = FSalida;
  |C_M #0Campo,0,alfa1;
  |SI alfa1 = "N"; |ACABA; |FINSI;

  |SI inkey = 10;
      #4#0 = #0Campo;
      |ABRE #4;
      |CONSULTA_F_CLAVE #6#4, inf4,sup4;
      |CIERRA #4;
      #0Campo = #4#0; |PINTA #0Campo;
      |ERROR;
      |ACABA;
  |FINSI;
  alfa1 = #0Campo; |QBF alfa1;
  alfa2 = alfa1 % 0; long  = alfa2;

  alfa4 = "";
  nSwLaC = 0;
  nSwError = 0;
  |PARA para1 = 1; |SI para1 [ long; |HACIENDO para1 = para1 + 1;
        nume1 = (para1 * 100) + 1;
        alfa3 = alfa1 % nume1;

        |SI alfa3 = "c"; |O alfa3 = "C";  nSwLaC = 1; alfa3 = "C"; |FINSI;
        alfa4 = alfa4 + alfa3;

        |SI alfa3 = "*";
            |SAL;
        |SINO;
            |SI alfa3 < "0"; |O alfa3 > "9";
                |SI alfa3 ! "C"; |Y alfa3 ! ".";
                    nSwError = 1; |SAL;
                |FINSI;
            |FINSI;
        |FINSI;
  |SIGUE;
  |SI nSwError = 1;
      |MENAV "    Cuenta Mal Informada";
      |ERROR;
      |ACABA;
  |FINSI;
  #0Campo = alfa4; |PINTA #0Campo;

  nume1 = (long * 100) + 1;
  alfa2 = alfa4 % nume1;

  |SI alfa2 ! "*";
      || ......................... Calculo de Digito.
      eaCuenta = #0Campo;
      salidas  = FSalida;
      |HAZ FiltraPuntos;
      #0Campo = eaCuenta;
      |PINTA #0Campo;

      emCta = 1;
      eaCuenta = #0Campo;
      |HAZ SacaNivel;
      |SI eswCta = 0; |Y nSwLaC = 0;
          |HAZ LeeCuenta;
      |FINSI;
  |FINSI;
|FPRC;


|CALCULO MiraCartera0; |TIPO 0;
   alfa1 = #0Campo;
   |C_M #0#56,1,alfa1;
   |C_M #0#162,1,alfa1;
   |C_M #0#161,1,"S";
   |SI alfa1 = "N";
       #0#56   = "";  |PINTA #0#56;
       #0#162  = "N";
       |C_M #0#163,1,"N";
       #0#163  = "N";  |PINTA #0#163;
   |SINO;
       |C_M #0#161,1,"N";
       #0#161  = "N"; |PINTA #0#161
       |C_M #0#163,1,"S";
   |FINSI;
|FCAL;

|CALCULO MiraCartera1; |TIPO 0;
   |C_M #0Campo,0,alfa1;
   |SI alfa1 = "N"; |ACABA; |FINSI;

   |SI FSalida = 10;
      |DBASS alfa1; |QBF alfa1;
      alfa1 = alfa1 + "dscarter/";
      #0#56 = alfa1;
      |PINTA #0#56;
   |FINSI;

   alfa1 = #0#56; |QBF alfa1;
   |SI alfa1 = ""; |ERROR;  |FINSI;
|FCAL;

|CALCULO cobpag; |TIPO 0;
 ||  alfa1 = #0Campo;
 ||  #0#52 = #0#48;
|FCAL;


|CALCULO Quepongo7; |TIPO 7;
  nSwP = 0;
  |HAZ EsComersan;
  |SI FSalida = 0;  nSwP = 1; |FINSI;
  |SI Campo = 45;  alfa1 = "    A¤adir en conceptos Astos Cl/Pr: S-erie,F-actura, C-Cta Ctble.,N-ombre,R-ef."; |FINSI;
  |SI Campo = 96;  alfa1 = "    A¤adir en concepto Asto de IRPF: S-erie,F-actura, C-Cta Ctble.,N-ombre,R-ef."; |FINSI;
  |SI Campo = 130; alfa1 = "    Astos Base/IVA: S-erie,F-actura, C-Cta Ctble.,N-ombre,R-ef., =Igual Cl/Pr ";   |FINSI;
  |SI nSwP = 1;
      |SI Campo = 45;  alfa1 = "    Astos Cl/Pr: S-erie,F-actura, C-Cta Ctble.,N-ombre,R-ef.,P-Ref Prov./Ref"; |FINSI;
      |SI Campo = 96;  alfa1 = "    Asto de IRPF: S-erie,F-actura, C-Cta Ctble.,N-ombre,R-ef.,P-Ref Prov./Ref"; |FINSI;
      |SI Campo = 130; alfa1 = "    Astos Base/IVA: S-erie,F-Fra., C-Cta,N-ombre,R-ef.,P-Ref Prv, =Igual Cl/Pr";|FINSI;
  |FINSI;
  |MENSA alfa1;
|FCAL;

|CALCULO Quepongo; |TIPO 0;

 alfa1 = #0Campo; |QBF alfa1;
 |SI alfa1 = ""; |ACABA; |FINSI;

 swS = 0; swF = 0;
 swC = 0; swN = 0;
 swR = 0;

 alfa1 = #0Campo % 101;
 alfa2 = #0Campo % 201;
 alfa3 = #0Campo % 301;

 |SI Campo = 130; |Y #0Campo = "=  ";
     |ACABA;
 |FINSI;

 |SI alfa1 ! " ";
     swT = 0;
     |SI alfa1 = "S"; swS = 1; swT = 1; |FINSI;
     |SI alfa1 = "F"; swF = 1; swT = 1; |FINSI;
     |SI alfa1 = "C"; swC = 1; swT = 1; |FINSI;
     |SI alfa1 = "N"; swN = 1; swT = 1; |FINSI;
     |SI alfa1 = "R"; swR = 1; swT = 1; |FINSI;
     |SI alfa1 = "P"; |Y nSwP = 1;
         |SI Campo = 45; |O Campo = 96; |O Campo = 130;
             swR = 1; swT = 1;
         |FINSI;
     |FINSI;
     |SI swT ! 1; |ERROR; |ACABA; |FINSI;
 |FINSI;
 |SI alfa2 ! " ";
     swT = 0;
     |SI alfa2 = "S"; |Y swS = 0; swS = 1; swT = 1; |FINSI;
     |SI alfa2 = "F"; |Y swF = 0; swF = 1; swT = 1; |FINSI;
     |SI alfa2 = "C"; |Y swC = 0; swC = 1; swT = 1; |FINSI;
     |SI alfa2 = "N"; |Y swN = 0; swN = 1; swT = 1; |FINSI;
     |SI alfa2 = "R"; |Y swR = 0; swR = 1; swT = 1; |FINSI;
     |SI alfa2 = "P"; |Y nSwP = 1;
         |SI Campo = 45; |O Campo = 96; |O Campo = 130;
             swR = 1; swT = 1;
         |FINSI;
     |FINSI;
     |SI swT ! 1; |ERROR; |ACABA; |FINSI;
 |FINSI;
 |SI alfa3 ! " ";
     swT = 0;
     |SI alfa3 = "S"; |Y swS = 0; swS = 1; swT = 1; |FINSI;
     |SI alfa3 = "F"; |Y swF = 0; swF = 1; swT = 1; |FINSI;
     |SI alfa3 = "C"; |Y swC = 0; swC = 1; swT = 1; |FINSI;
     |SI alfa3 = "N"; |Y swN = 0; swN = 1; swT = 1; |FINSI;
     |SI alfa3 = "R"; |Y swR = 0; swR = 1; swT = 1; |FINSI;
     |SI alfa3 = "P"; |Y nSwP = 1;
         |SI Campo = 45; |O Campo = 96; |O Campo = 130;
             swR = 1; swT = 1;
         |FINSI;
     |FINSI;
     |SI swT ! 1; |ERROR; |ACABA; |FINSI;
 |FINSI;

 |SI Campo = 45;
     |SI #0#96 = "   ";
         #0#96 = #0#45; |PINTA #0#96;
     |FINSI;
     |SI #0#130 = "   ";
          #0#130 = #0#45; |PINTA #0#130;
     |FINSI;
 |FINSI;
|FCAL;

|CALCULO serie0; |TIPO 0;
  alfa1 = #0#44;
  |C_M #0#63,1,alfa1;
  |C_M #0#64,1,alfa1;
  |C_M #0#65,1,alfa1;
  |C_M #0#66,1,alfa1;
  |C_M #0#75,1,alfa1;
  |C_M #0#76,1,alfa1;
  |SI alfa1 = "N";
      |PDEFECTO #0,63,67;
      #0#75 = ""; #0#76 = "N";
      |PINDA #0#0;
  |FINSI;
|FCAL;

|CALCULO AntSerie; |TIPO 7;
 |C_M #0Campo,0,alfa1;
 |SI alfa1 = "N"; |ACABA; |FINSI;
  #0#75 = #0#63; |PINTA #0#75;
  #0#76 = #0#64; |PINTA #0#76;
|FCAL;

|CALCULO Serie; |TIPO 0;
   |DDEFECTO #11;
   |ABRE #11;
   #11#0 = #0Campo;
   |SI Campo = 65; #11#1 = "S"; |SINO; #11#1 = "R"; |FINSI;
   |LEE 000#11=;
   |SI FSalida ! 0;
       |DDEFECTO #11;
       #11#0 = #0Campo;
       |SI Campo = 65; #11#1 = "S"; |SINO; #11#1 = "R"; |FINSI;
       |GRABA 020#11n;
   |FINSI;
   |CIERRA #11;
|FCAL;

|CALCULO Ctrl340; |TIPO 7;
 alfa1 = "S";
 |SI nSi340 = 0;
     alfa1 = "N";
     #0#156 = "S"; |PINTA #0#156;
 |FINSI;
 |C_M #0#156,1,alfa1;
|FCAL;

|CALCULO Ctrl303; |TIPO 7;
 alfa1 = "S";
 |SI nSi303 = 0; |Y nSi111 = 0;
     alfa1 = "N";
     #0#167 = "S"; |PINTA #0#167;
 |FINSI;
 |C_M #0#167,1,alfa1;
|FCAL;

|CALCULO no_subde; |TIPO 7;
      |CAMPO_MODIFICA #0#67, 1, eaSubde;
      |CAMPO_MODIFICA #0#68, 1, eaSubde;
|FCAL;

|CALCULO eldefecto; |TIPO 7;
  |SI ModoEntrada ! 1; |ACABA; |FINSI;

  |SI EURODB = 2;
      #0Campo = (250000 / 166.386) ! EURODB;
  |SINO;
      #0Campo = 250000;
  |FINSI;
  |PINTA #0Campo;
|FCAL;

|CALCULO Antes97; |TIPO 7;
 |SI #0#44 = "N";
     |C_M #0#97,1,"N";
     #0#97 = ""; |PINTA #0#97;
 |SINO;
     |C_M #0#97,1,"S";
 |FINSI;
|FCAL;

|CALCULO QueFiltro; |TIPO 0;
 alfa1 = "S";
 |SI #0#95 ! "S"; alfa1 = "N"; |FINSI;
 |C_M #0#118,1, alfa1;
 |SI #0#95 ! "S"; #0#118 = "N"; |PINTA #0#118; |FINSI;
|FCAL;

|CALCULO Tras104; |TIPO 0;
 alfa1 = #0#104;
 |C_M #0#83,1,alfa1;
 |C_M #0#84,1,alfa1;
 |C_M #0#106,1,alfa1;
 |C_M #0#107,1,alfa1;
 |C_M #0#157,1,alfa1;
 |SI alfa1 = "N";
     #0#83 = 1;    |PINTA #0#83;
     #0#84 = 999;  |PINTA #0#84;
     #0#106 = 0;   |PINTA #0#106;
     #0#107 = "N"; |PINTA #0#107;
     #0#157 = "N"; |PINTA #0#157;
 |FINSI;
|FCAL;

|CALCULO Tras105; |TIPO 0;
 alfa1 = #0#105;
 |C_M #0#85, 1,alfa1;
 |C_M #0#86, 1,alfa1;
 |C_M #0#108,1,alfa1;
 |C_M #0#109,1,alfa1;
 |C_M #0#158,1,alfa1;
 |SI alfa1 = "N";
     #0#85 = 1;    |PINTA #0#85;
     #0#86 = 999;  |PINTA #0#86;
     #0#108 = 0;   |PINTA #0#108;
     #0#109 = "N"; |PINTA #0#109;
     #0#158 = "N"; |PINTA #0#158;
 |FINSI;
|FCAL;

|CALCULO Antes89; |TIPO 7;
 alfa1 = "S";
 |SI #0#104 = "N"; |Y #0#105 = "N";
     alfa1 = "N";
 |FINSI;
 |C_M #0#89, 1,alfa1;
 |C_M #0#90, 1,alfa1;
 |C_M #0#91, 1,alfa1;
|FCAL;

|CALCULO Antes111; |TIPO 7;
 |SI Haymodelo = 0;
      |C_M #0#111,1,"N"; #0#111 = "N"; |PINTA #0#111;
      |C_M #0#112,1,"N"; #0#112 = "S"; |PINTA #0#112;
 |SINO;
      |C_M #0#111,1,"S";
      |C_M #0#112,1,"S";
 |FINSI;
|FCAL;

|CALCULO Antes112; |TIPO 7;
 |SI Haymodelo = 1;
      |SI #0#111 = "N";
          |C_M #0#112,1,"N"; #0#112 = "S"; |PINTA #0#112;
      |SINO;
          |C_M #0#112,1,"S";
      |FINSI;
 |FINSI;
|FCAL;

|PROCESO cambiodep;
  |SI aDepAnt ! #0#19; |O aSubAnt ! #0#67;
           enCambio = 0; |SI aDepAnt ! #0#19; enCambio = 1; |FINSI;
           || enEmpresa = #canempre#2;
           || enPeriodo = #canempre#3;
           eaDfDepC  = #0#19;
           eaDfSubC  = #0#67;
           |SUB_EJECUTA_NP "caut0003;3";
  |FINSI;
|FPRC;

|PROCESO englobadas;
 alfa1 = #0Campo;
 |C_M #0#38, 1, alfa1;
 |C_M #0#39, 1, alfa1; |C_M #0#41, 1, alfa1;
 |C_M #0#42, 1, alfa1; |C_M #0#43, 1, alfa1;
 |C_M #0#17, 1, alfa1; |C_M #0#18, 1, alfa1;
 |C_M #0#73, 1, alfa1; |C_M #0#74, 1, alfa1;
 |C_M #0#75, 1, alfa1; |C_M #0#76, 1, alfa1;
 |C_M #0#77, 1, alfa1; |C_M #0#78, 1, alfa1;
 |C_M #0#79, 1, alfa1; |C_M #0#80, 1, alfa1;
|FPRC;


|CALCULO ladesv;
  alfa1 = #0Campo;
  |C_M #0#183,1,alfa1;
|FCAL;

|PROCESO ElRelleno; |TIPO 7;
 |C_M #0#116,0, alfa1;
 |SI alfa1 = "N"; |ACABA; |FINSI;

 |SI #0#116 = "S";
     |C_M #0#117,1, "S";
 |SINO;
     |C_M #0#117,1, "N";
 |FINSI;
|FPRC;

|PROCESO Esauto;
 alfa1 = #0#114;
 |SI #0#114 = "S";
     alfa1 = "N"; #0#133 = "S";
                  #0#134 = "S";
 |SINO;
     alfa1 = "S";
 |FINSI;
 |C_M #0#133, 1, alfa1;
 |C_M #0#134, 1, alfa1;
 |PINTA #0#133;
 |PINTA #0#134;
|FPRC;

|CALCULO ComoModelo;
 |SI Campo = 46;
     |SI #0#46 ! aTPerio; |Y aTPerio ! "";
         |CONFI "    SPeriodicidad diferente la informada a la del Modelo de IVA. Cambiarla ";
         |SI FSalida = 0;
             #0#46 = aTPerio; |PINTA #0#46;
         |FINSI;
     |FINSI;
     |SI ModoEntrada = 1;
         #0#149 = #0#46; |PINTA #0#149;
     |FINSI;
 |FINSI;
 |SI Campo = 149;
     |SI #0#149 ! aTPerio110; |Y aTPerio110 ! "";
         |CONFI "    SPeriodicidad diferente la informada a la del Modelo de IRPF. Cambiarla ";
         |SI FSalida = 0;
             #0#149 = aTPerio110; |PINTA #0#149;
         |FINSI;
     |FINSI;
 |FINSI;
|FCAL;

|PROCESO dsmom002;
  |SI #dsmom002#8 > "01.01.1900";
      |SI #dsmom002#8 > fec2;  |ACABA;  |FINSI;
  |FINSI;
  |SI #dsmom002#9 > "01.01.1900";
      |SI #dsmom002#9 < fec1;  |ACABA;  |FINSI;
  |FINSI;

  alfa1 = "";
  |SI #dsmom002#2 = 110;
       alfa1 = #dsmom002#7;
       |SI alfa1 ! ""; aTPerio110 = alfa1 % 101; |FINSI;
       nSi111 = 1;
  |FINSI;

  |SI nSwGrupo = 1; |ACABA; |FINSI;

  |SI #dsmom002#2 = 115; |O #dsmom002#2 = 123;
      nSi111 = 1;
  |FINSI;

  |SI #dsmom002#2 ] 300;  |Y #dsmom002#2 [ 370;
      alfa1 = "";
      |SI enEjercicio [ 2008;
          |SI #dsmom002#2 = 300; |O #dsmom002#2 = 320; |O #dsmom002#2 = 330; |O #dsmom002#2 = 332;
              alfa1 = #dsmom002#7;
          |FINSI;
      |SINO;
          |SI #dsmom002#2 = 303; |O #dsmom002#2 = 340; |O #dsmom002#2 = 322; |O #dsmom002#2 = 353;
              alfa1 = #dsmom002#7;
          |FINSI;
          |SI #dsmom002#2 = 303; nSi303 = 1; |FINSI;
          |SI #dsmom002#2 = 322; nSi303 = 1; |FINSI;
          |SI #dsmom002#2 = 353; nSi303 = 1; |FINSI;
          |SI #dsmom002#2 = 340; nSi340 = 1; |FINSI;
      |FINSI;
      |SI #dsmom002#2 = 310; |O #dsmom002#2 = 311; nSi303 = 1; alfa1 = #dsmom002#7; |FINSI;
      |SI #dsmom002#2 = 370;                  nSi303 = 1; alfa1 = #dsmom002#7; |FINSI;

      |SI alfa1 ! ""; aTPerio = alfa1 % 101; |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE dsmom002;
  #dsmom002#1;
  ;
  nEmpresa, 01, 110;
  nEmpresa, 99, 370;
  ;
  NULL, dsmom002;
|FIN;

|PROCESO LosGrupos;
  nSwGrupo   = 1;
  aTPerio110 = "";
  nEmpresa   = #dsmom026#0;
  |BUCLE dsmom002;
  |SI nSi111 = 1;
      |ERROR10;
  |FINSI;
|FPRC;

|DEFBUCLE LosGrupos;
  #dsmom026#2;
  ;
  enEmpresa, 00001;
  enEmpresa, 99999;
  e;
  NULL,LosGrupos;
|FIN;

|PROCESO VerMod303;
  nSwGrupo   = 0;
  aTPerio    = "";
  aTPerio110 = "";
  nEmpresa = enEmpresa;
  |BUCLE dsmom002;
  |SI nSi111 = 0; |BUCLE LosGrupos; |FINSI;
|FPRC;

|CALCULO losembargos;
  alfa1 = "S";
  |SI #0#165 = "N"; |O #0#165 = "F";
      alfa1 = "N";
      #0#166 = 0; |PINTA #0#166;
  |FINSI;
  |C_M #0#166, 1, alfa1;
|FCAL;

|CALCULO conceptoE;  |TIPO 0;
      inkey = FSalida;
      |C_M #0Campo,0,alfa1;
      |SI alfa1 = "N"; |ACABA; |FINSI;

      |SI #0Campo = 0; |ACABA; |FINSI;

      |ABRE #dsmom030;
      #dsmom030#0 = #0Campo;
      |LEE 041#dsmom030.=;
      |SI FSalida ! 0;
          |MENAV "    No existe Concepto.";
          |ERROR;
          |ACABA;
      |FINSI;
      |CIERRA #dsmom030;

      |SI #dsmom030#4 = "S"; |O #dsmom030#5 = "S";
          |SI #0#95 ! "X";
              |MENAV "    Error. Concepto no Corresponde a Cobros/Pagos";
          |FINSI;
          |SI #0#95 = "S";
              |ERROR;
          |FINSI;
      |FINSI;
|FCAL;

|CALCULO ClCaja; |TIPO 0;
      nume1 = Campo + 1;
      alfa1 = "S";
      |SI #0Campo = " ";
          alfa1 = "N";
          #0nume1 = "S"; |PINTA #0nume1;
      |FINSI;
      |C_M #0nume1,1,alfa1;
|FCAL;

|CALCULO conceptoCaja;  |TIPO 0;
      inkey = FSalida;
      |C_M #0Campo,0,alfa1;
      |SI alfa1 = "N"; |ACABA; |FINSI;

      |SI #0Campo = 0; |ACABA; |FINSI;

      |ABRE #dsmom030;
      #dsmom030#0 = #0Campo;
      |LEE 041#dsmom030.=;
      |SI FSalida ! 0;
          |MENAV "    No existe Concepto.";
          |ERROR;
          |ACABA;
      |FINSI;
      |CIERRA #dsmom030;

      nume1 = 0;
      alfa1 = "";
      |SI Campo = 173; |O Campo = 175;
          |SI #dsmom030#7 = "N";
              alfa1 = "    Error. Concepto no Corresponde a Caja en Efectivo";
               nume1 = 1;
          |FINSI;
      |SINO;
          |SI #dsmom030#4 = "S"; |O #dsmom030#5 = "S"; |O #dsmom030#8 = "S";
              alfa1 = "    Error. Concepto no Corresponde a Cobros/Pagos";
              nume1 = 1;
          |FINSI;
      |FINSI;
      |SI nume1 = 1;
          |SI #0#95 ! "X";
              |MENAV alfa1;
          |FINSI;
          |SI #0#95 = "S";
              |ERROR;
          |FINSI;
      |FINSI;
|FCAL;

|CALCULO CobroAM;
      alfa1 = "S";
      |SI #0Campo = "M"; alfa1 = "N"; |FINSI;
      nume1 = Campo - 4;  |C_M #0nume1, 1, alfa1;
      nume1 = nume1 + 1; |C_M #0nume1, 1, alfa1;
|FCAL;

|CALCULO ModParamA;
   |C_M #0#187, 0, alfa1;
   |SI alfa1 = "N"; |ACABA; |FINSI;

   |SI #0#187 = "N";
       |HAZ NoPulso;
   |SINO;
       |HAZ SiPulso;
   |FINSI;
|FCAL;

|PROGRAMA;
   |HAZ inicio;
   |CLS;
   |CABEZA "Control Introduccion de IVA";

   |HAZ DirAplicSt;
   |HAZ VerMod303;

   |PINPA #0#0;
   ModoEntrada = 2;
   |ABRE #0;
   |LEE 101#0p;
   |SI FSalida ! 0;
      #0#19 = eaDfDepC;
      #0#67 = eaDfSubC;
      |SI aTPerio ! ""; #0#46 = aTPerio; |FINSI;
      |GRABA 020#0b;
      ModoEntrada = 1;
   |FINSI;
   aDepAnt = #0#19;
   aSubAnt = #0#67;

   |HAZ Analitica;
   |SI enSwAna = 1;
       |C_M #0#116,1,"N"; #0#116 = eaRefer;
       |C_M #0#117,1,"N"; #0#117 = enLgRef;
   |SINO;
       |SI eaRefer = "S";
           || |C_M #0#117,1,"N"; #0#117 = enLgRef;
       |FINSI;
   |FINSI;

   |SI HayAcceso = 0;
       #0#165 = "N"; |C_M #0#165,1,"N";
       #0#166 = 000; |C_M #0#166,1,"N";
   |FINSI;
   |SI eaDepar = "N";
       #0#187 = "N"; |C_M #0#187, 1, "N";
   |FINSI;

   |SI #0#137 = " ";   || no tenia aun el cambio de parametros para
                       || separar cobros y pagos
       |SI #0#48 = "S"; #0#48 = "P"; |FINSI;
       |SI #0#52 = "S"; #0#52 = "P"; |FINSI;
       #0#137 = #0#103;
   |FINSI;

   eaPaCTA1 = #0#135;  ||permitir alta no estructuras 2008
   eaPaCTA2 = #0#136;  ||Aviso 1990
   eaPaNif1 = #0#152;
   eaPaNif2 = #0#153;
   eaABanco = #0#161;
   eaObClPr = #0#186;
   |PINDA #0#0;
   |HAZ BotonIVA;
   nHePulsado = 0;

   |ENDATOS #2#0;
   |SI FSalida = 0;
      |GRABA 020#0a;
      |LIBERA #0;
      |CIERRA #0;
      |HAZ cambiodep;
   |SINO;
      |LIBERA #0;
      |CIERRA #0;
   |FINSI;

   |HAZ FinBotonIVA;
   |SI nHePulsado = 1;
       |SUB_EJECUTA_NP "caivaaxx;Pulsa";
   |FINSI;
|FPRO;

|PROCESO Boton94IVA; |TIPO 7;
   |HAZ BotonIVA;
|FPRC;

|PROCESO BotonIVA;
   |SI eaDepar = "S"; |Y nSwFin = 0;
       |CONTROL_SIMPLE 0, "LBOTON,F5- Parametros Actividad", 0560, 0575, Boton5;
       nBoton5 = FSalida;
       nSwFin = 1;

       |SI #0#187 = "N";
           |HAZ NoPulso;
       |SINO;
           |HAZ SiPulso;
       |FINSI;
   |FINSI;
|FPRC;

|PROCESO NoPulso;
    |ESTADO_CONTROL nBoton5,  "DISABLE";
|FPRC;

|PROCESO SiPulso;
    |ESTADO_CONTROL nBoton5,  "ENABLE";
|FPRC;

|PROCESO Boton5;
     |PTEC 827;
|FPRC;

|PROCESO Boton7; |TIPO 0;
     |SI eaDepar = "S"; |Y nSwFin = 1;
         nTecla = FSalida;
         |SI #caivaasi#187 = "N"; |ACABA; |FINSI;

         |SI nTecla = 13; |Y nHePulsado = 0;
             nHePulsado = 1;
             |PTEC 806;
         |FINSI;
     |FINSI;
|FPRC;


|PROCESO FinBotonIVA;

     |SI Campo = 94;
         |SI FSalida = 2; |O FSalida = 4;
             |ACABA;
         |FINSI;
     |FINSI;

     |SI eaDepar = "S"; |Y nSwFin = 1;
         |FIN_CONTROL_WINDOWS nBoton5;
         nSwFin = 0;
     |FINSI;
|FPRC;
