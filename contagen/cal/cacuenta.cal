|| TENED EN CUENTA QUE SI SE MODIFICAN CAMPOS EN EL 'cacuenta.def'
|| HAY QUE HACER LO MISMO EN EL 'plancont.def'

|FICHEROS;
   cacuenta #0;    || cuentas
   cacuent2 #1;    || cuentas por digitos para el informe

   :/contagen/def/cacuebal #5;    || cuentas de balance
   :/contagen/def/caparbal #6;    || partidas balances
   :/contagen/def/caparexp #7;    || partidas explotacion
   :/contagen/def/caagrupa #8;    || agrupaciones
   :/contagen/def/caepigra #9;    || epigrafes
   :/contagen/def/ca8m0002 #422;  || Cuenta a Balance
   :/contagen/def/ca8cumay #410;  || Cuenta de mayor
   :/contagen/def/caclipro;

   :/contagen/def/camaasil #10;   || lineas
   :/contagen/def/cactrlal #11;   || control altas en otras empresas.
   :/contagen/def/canempre #30;

   cafanta1   #217,MANTE;
   &regisnif@ #709;

   trabajo@;
|FIN;

|VARIABLES;
   &entrada  = 0;
   nNuevo = 0;
   men1     = "     NO EXISTE EMPRESA !";
   men2     = "     LONGITUD DE CUENTA FUERA DE NIVEL !";
   men3     = "     CUENTA ERRONEA !!";
   error    = "     CLAVE EXISTENTE !";
   valida   = "     Conforme S/N.:";
   mascara  = "";
   digito2  = "  ";
   num      = 0;
   sw       = 0;
   mes      = "                ";
   me       = 0;
   cta      = "            ";
   ctanum   = 0;
   ctaalf   = " ";
   longalf  = " ";
   longnum  = " ";
   codcli   = 0;
   codpr    = 0;
   nompro   = "";
   nada     = "";
   &relacionCta = 0;
   cuen1 = "";
   cuen2 = "";
   cuen3 = "";
   desdec = "";
   hastac = "";

   &raf_oper = 0;

   &raf_clpr = "";
   &raf_cue = "";
   &raf_dig = 0;
   &raf_des = "";
   &raf_sw = 0;
   &tipoalta = "";
   &raf_pase = "";
   &raf_blo = 0;
   &raf_tipo = "";
   &raf_agru = 0;
   &raf_epi = 0;
   &raf_par = 0;
   &raf_empresa = 0;
   &raf_periodo = 0;
   &raf_ejercic = 0;

   mesdes = "";
   meshas = "";
   xx = 0;
   yy = 0;
   operac = 0;
   alfa1  = "";
   alfa2  = "";
   swaltas = 0;
   swraf = 0;
   &deCuenta = 0;
   existe = 0;
   Comodin = "";
   Tecla = 0;
   nEstado = 0;

   aCamp23 = "";
   aCamp10 = "";
   nCamp11 = 0;

   &eaTex1 = "";
   &eaTex2 = "";
   &eaTex3 = "";
   nume1   = 0;
   &nBanco = 0;

   aAlfa   = "";
   aAlfa4  = "";
   aAlfa5  = "";
   aDef    = "";
   aFic    = "";

   nEsOTP     = 0;
   nHandle    = 0;
   nEmpresa   = 0;
   nCebi      = 0;
   nEsCtaProv = 0;

   &enNoModDatosOTP = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE defect_i;

||  ***************************************
|CALCULO alentrar;  |TIPO 8;
   entrada = 0;
   |HAZ inicio;

   |PARA num = 1;|SI num [ 12;|HACIENDO num = num + 1;
      |SI #canempre#11 = num;
          |HAZ pinta;
          mesdes = mes;
          |SAL;
      |FINSI;
   |SIGUE;

   |PARA num = 12; |SI num ] 1; |HACIENDO num = num - 1;
      |SI #canempre#12 = num;
         |HAZ pinta;
         meshas = mes;
         |SAL;
      |FINSI;
   |SIGUE;
   raf_empresa = #canempre#2;
   raf_periodo = #canempre#3;
   raf_ejercic = #canempre#26;

   |ABRE #11;
   |DDEFECTO #11;
   |LEE 101#11p;
   |SI FSalida ! 0; swaltas = 1; |FINSI;
   |LIBERA #11;
   |CIERRA #11;
|FCAL;

||  ***************************************
|CALCULO bajano; |TIPO 1;
  yy = 0;
  operac = (FEntrada / 100) ! 0;
  |SI operac ! 3; |ACABA; |FINSI;

  |PARA xx = 9; |SI xx [ 53; |HACIENDO xx = xx + 1;
        |SI #cacuenta#xx# ! 0;
            yy = 1;
            |SAL;
        |FINSI;
  |SIGUE;
  |SI yy = 0; |Y #cacuenta#3 = "N"; |HAZ bclipro; |ACABA; |FINSI;
  |SI yy = 0;
     |ABRE #10;
     |SELECT #3#10;
     #camaasil#4 = #cacuenta#0;
     #camaasil#5 = #cacuenta#1;
     #camaasil#0 = 0;
     #camaasil#1 = "01.01.0000";
     #camaasil#2 = 0;
     |LEE 000#10];
     |SI FSalida = 0;
         |SI #camaasil#4 = #cacuenta#0;
             yy = 1;
         |FINSI;
     |FINSI;
     |SELECT #1#10;
     |CIERRA #10;
  |FINSI;

  |SI yy = 0; |HAZ bclipro; |ACABA; |FINSI;

  |AVISO;
  |CONFI "2400NCuenta con Movimientos. Se dispone a BORRARLA. 쭱sta seguro? ";
  |SI FSalida ! 0;
     |ERROR;
  |SINO;
     |HAZ bclipro;
  |FINSI;
|FCAL;

|PROCESO bclipro;
   alfa1 = #cacuenta#0; alfa1 = alfa1 % 101;

   |SI alfa1 = "6"; |O alfa1 = "7";  |ACABA;  |FINSI;
   |SI alfa1 = "8"; |O alfa1 = "9";  |ACABA;  |FINSI;

   |ABRE #caclipro;
   #caclipro#23 = "C";
   #caclipro#10 = #cacuenta#0;
   #caclipro#11 = #cacuenta#1;
   |LEE 101#caclipro.=;
   |SI FSalida = 0;
      |BORRA 020#caclipro.a;
   |SINO;
      #caclipro#23 = "P";
      #caclipro#10 = #cacuenta#0;
      #caclipro#11 = #cacuenta#1;
      |LEE 101#caclipro.=;
      |SI FSalida = 0;
         |BORRA 020#caclipro.a;
      |FINSI;
   |FINSI;
   |LIBERA #caclipro;
   |CIERRA #caclipro;
|FPRC;

|PROCESO filtro_cta; |TIPO 0;
   |SI FSalida = 2; |ACABA; |FINSI;

   |SI enEjercicio < 2008;
       |SI #0#5 = "I"; |O #0#5 = "G";
           |ERROR;
           |ACABA;
       |FINSI;
   |FINSI;

   |SI #cacuenta#4 = 0;
      |SI #cacuenta#5 = "D"; |O #cacuenta#5 = "H";
          |O #cacuenta#5 = "G"; |O #cacuenta#5 = "I";
         |ERROR;
      |FINSI;
   |SINO;
      |SI #cacuenta#4 = 6;
          |SI #cacuenta#5 = "D"; |O #cacuenta#5 = "H";
              |O #cacuenta#5 = "A"; |O #cacuenta#5 = "P";
             |ERROR;
          |FINSI;
      |SINO;
          |SI #cacuenta#5 = "A"; |O #cacuenta#5 = "P";
              |ERROR;
          |FINSI;
      |FINSI;
   |FINSI;
|FPRC;


|PROCESO pintas2;
   |ABRE #8;
   #caagrupa#0 = #cacuenta#5;    || tipo
   #caagrupa#1 = #cacuenta#6;    || agrupacion
   |LEE 001#8=;
   |SI FSalida ! 0;  |DDEFECTO #8;  |FINSI;
   |ATRI I;
   |SI entrada = 0; |PINTA 1321, #caagrupa#2; |FINSI;
   |ATRI 0;
   |CIERRA #8;

   |ABRE #9;
   #caepigra#0 = #cacuenta#5;    || tipo
   #caepigra#1 = #cacuenta#6;    || agrupacion
   #caepigra#3 = #cacuenta#7;    || epigrafe
   |LEE 001#9=;
   |SI FSalida ! 0;  |DDEFECTO #9;  |FINSI;
   |ATRI I;
   |SI enEjercicio < 2999; |Y entrada = 0; |PINTA 1421, #caepigra#4; |FINSI;
   |ATRI 0;
   |CIERRA #9;
|FPRC;


|PROCESO pintas; |TIPO 14;
  |SI enEjercicio ] 2999; |Y entrada = 0;
      |C_M #cacuenta#7,1,"N"; |C_V #cacuenta#7,1,"N";
      |C_M #cacuenta#8,1,"N"; |C_V #cacuenta#8,1,"N";
      alfa1 = "읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴켸";
      |PINTA 1401, alfa1;
      |BLIN 15;
      |BLIN 16;
  |FINSI;

   eaTex1 = "";
   eaTex2 = "";
   eaTex3 = "";
   |SI #cacuenta#4 = 0;
       |ABRE #6;
       #caparbal#0 = #cacuenta#5;     || tipo
       #caparbal#1 = #cacuenta#6;     || agrupacion
       #caparbal#3 = #cacuenta#7;     || epigrafe
       #caparbal#5 = #cacuenta#8;     || partida
       |LEE 001#6=;
       |SI FSalida ! 0;  |DDEFECTO #6;  |FINSI;
       |ATRI I;
       |SI enEjercicio < 2999; |Y entrada = 0; |PINTA 1521, #caparbal#6; |FINSI;
       |ATRI 0;
       |CIERRA #6;
       eaTex1 = #6#2;
       eaTex2 = #6#4;
       eaTex3 = #6#6;
   |SINO;
      |ABRE #7;
      #caparexp#0 = #cacuenta#4;     || bloque
      #caparexp#1 = #cacuenta#5;     || tipo
      #caparexp#2 = #cacuenta#6;     || agrupacion
      #caparexp#4 = #cacuenta#7;     || epigrafe
      #caparexp#6 = #cacuenta#8;     || partida
      |LEE 001#7=;
      |SI FSalida ! 0;  |DDEFECTO #7;  |FINSI;
      |ATRI I;
      |SI enEjercicio < 2999; |Y entrada = 0; |PINTA 1521, #caparexp#7; |FINSI;
      |ATRI 0;
      |CIERRA #7;
      eaTex1 = #7#3;
      eaTex2 = #7#5;
      eaTex3 = #7#7;
   |FINSI;
   |HAZ pintas2;
|FPRC;


|PROCESO altas;|TIPO 0;
   Tecla = FSalida;
   cta = #cacuenta#0;
   digito2 = cta % 102;
   alfa1   = cta % 103;
   alfa2   = cta % 104;
   |BLIN 4;

   |SI entrada = 2; |ACABA; |FINSI;

   |SI Tecla = 12;  |HAZ AltaNif; |ACABA; |FINSI;

   |SI Tecla ! 9; |Y eaObClPr ! "S"; |ACABA; |FINSI;

   |SI #cacuenta#3 ! "S"; |ACABA; |FINSI;

   raf_cue = #cacuenta#0;
   raf_dig = #cacuenta#1;
   raf_des = #cacuenta#2;
   raf_sw = 0;

   |SI eaABanco = "S";
       nBanco = -1;
       |SI digito2 = "57"; |O digito2 = "16"; |O digito2 = "17";
           |O digito2 = "51"; |O digito2 = "52";
             |O digito2 = "53"; |O digito2 = "54";

           |SI alfa1 ! "171"; |Y alfa1 ! "172"; |Y alfa1 ! "173";
               |Y alfa1 ! "174"; |Y alfa1 ! "175"; |Y alfa2 ! "5209";
                  |Y alfa1 ! "521"; |Y alfa1 ! "522"; |Y alfa1 ! "523";
                     |Y alfa1 ! "524"; |Y alfa1 ! "525"; |Y alfa1 ! "529";

               |SUB_EJECUTA_NP "n43saldo";
               |VETE salir;
           |FINSI;

       |FINSI;
   |FINSI;

   |SI digito2 = "43";  |O digito2 = "44";
       tipoalta = "C";
   |SINO;
      |SI digito2 = "40";  |O digito2 = "41";
        |O alfa1 = "173"; |O alfa1 = "174";
          |O alfa1 = "523"; |O alfa1 = "524";
            |O alfa1 = "551";
          tipoalta = "P";
      |SINO;
          tipoalta = "X";
          |SI Tecla ! 9; |ACABA; |FINSI;
      |FINSI;
   |FINSI;
   |SUB_EJECUTA_NP "caclipr1.tab";

   |ET salir;
   |SI raf_sw = 1;
       #cacuenta#2 = raf_des;
       |PINTA #cacuenta#2;
        raf_sw = 0;
   |SINO;
       |ERROR;
   |FINSI;
|FPRC;

|PROCESO MiraSiEsOTP;
     |DBASS aAlfa; |QBF aAlfa;
     aAlfa = aAlfa + "dspreven/def/pycm0070.mas";
     |XABRE "E", aAlfa, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     |CARGA_DEF aAlfa, trabajo@;
     |ABRE #trabajo@;
     |LEE 000#trabajo@.p;
     |SI FSalida = 0;
         nEsOTP = 1;

         |DBASS aAlfa; |QBF aAlfa;
         aAlfa = aAlfa - "ase1012";
         |SI FSalida ! 0;
             |SI #48#2 = 70102;
                 nEsOTP = 0;
             |FINSI;
         |FINSI;
     |FINSI;
     |CIERRA #trabajo@;
     |DESCARGA_DEF #trabajo@;
|FPRC;

|PROCESO MiraSiIncidencia;
     |MODULO aAlfa;
     |SI aAlfa = "cacugene";  |ACABA;  |FINSI;
     |SI aAlfa = "otp00017";  |ACABA;  |FINSI;

     |ABRE #caclipro;
     |SELECT #1#caclipro;
     #caclipro#23 = "C";
     #caclipro#10 = #cacuenta#0;
     |LEE 000#caclipro.=;
     |SI FSalida ! 0;
         |CIERRA #caclipro;
         |ACABA;
     |FINSI;
     |CIERRA #caclipro;

     |DBASS aBase; |QBF aBase;
     aDef = aBase + "dspreven/def/visalm00.mas";
     aFic = aBase + "dspreven/fich/";

     |CARGA_DEF aDef, trabajo@;
     |PATH_DAT #trabajo@#aFic#;
     |ABRE #trabajo@;
     |LEE 000#trabajo@.p;
     |PARA; |SI FSalida = 0; |HACIENDO;
            |SI #trabajo@#3 = "S"; |SAL; |FINSI;
            |LEE 000#trabajo@.s;
     |SIGUE;

     |SI #trabajo@#3 = "S";
         aFic = aFic + (("00000" + #trabajo@#0) % -105) + "/";
     |SINO;
         aFic = "";
     |FINSI;

     |CIERRA #trabajo@;
     |DESCARGA_DEF #trabajo@;

     |SI aFic = "";  |ACABA;  |FINSI;

     aDef = aBase + "dspreven/def/pycm0004.mas";
     |CARGA_DEF aDef, trabajo@;
     |PATH_DAT #trabajo@#aFic#;
     |ABRE #trabajo@;
     #trabajo@#0 = #caclipro#9;
     |LEE 000#trabajo@.=;
     |SI FSalida ! 0;
         |CIERRA #trabajo@;
         |DESCARGA_DEF #trabajo@;
         |ACABA;
     |FINSI;

     nEmpresa = #trabajo@#54;

     |CIERRA #trabajo@;
     |DESCARGA_DEF #trabajo@;

     aAlfa = aBase - "ase1012";
     |SI FSalida ! 0;  nCebi = 1;  |FINSI;

     aDef = aBase + "dspreven/def/dspvm002.mas";
     |CARGA_DEF aDef, trabajo@;
     |PATH_DAT #trabajo@#aFic#;
     |ABRE #trabajo@;
     |SELECT #2#trabajo@;

     #trabajo@#8 = nEmpresa;
     #trabajo@#0 = 0;
     |LEE 000#trabajo@.];
     |PARA; |SI FSalida = 0; |Y #trabajo@#8 = nEmpresa; |HACIENDO;
            |SI nCebi = 1;  |Y #trabajo@#204 = 70102;
                || NO HAGO NADA
            |SINO;
                |SI #trabajo@#1 = "A";
                    |SI #trabajo@#121 = "S"; |O #trabajo@#71 = "S";
                         aAlfa = "0000Cliente con INCIDENCIA numero " + #trabajo@#0;
                         aAlfa = aAlfa + " de BAJA o IMPAGO";
                         |MENAV aAlfa;
                         |SAL;
                    |FINSI;
                |FINSI;
            |FINSI;

            |LEE 000#trabajo@.s;
     |SIGUE;

     |CIERRA #trabajo@;
     |DESCARGA_DEF #trabajo@;
|FPRC;

|PROCESO nivel; |TIPO 0;
   operac = (FEntrada / 100) ! 0;
   |SI #cacuenta#0 = doce; |Y operac = 4;
       |ACABA;
   |FINSI;

   eaCuenta = #cacuenta#Campo#;
   salidas  = FSalida;
   |HAZ FiltraPuntos;
   #cacuenta#Campo# = eaCuenta;
   |SI entrada = 0; |PINTA #cacuenta#Campo#; |FINSI;

   nEsCtaProv = 0;
   aAlfa  = #cacuenta#0 % 103;
   |SI aAlfa = "400"; |O aAlfa = "410";
       aAlfa  = #cacuenta#0 % 104;
       |SI aAlfa ! "4009";  |Y aAlfa ! "4109";
           nEsCtaProv = 1;
       |FINSI;
   |FINSI;

   enNoModDatosOTP = 0;
   nEsOTP          = 0;
   |HAZ MiraSiEsOTP;
   |SI nEsOTP = 1;
       |SI nEsCtaProv = 1;
           |MODULO aAlfa;  |QBF aAlfa;
           |SI aAlfa = "cacuenta"; |O aAlfa = "cacugene";
               |SI aAlfa = "cacuenta";  |O aAlfa = "cacugene";
                   |MENAV "0000No se puede crear o modificar cuentas de proveedores desde aqui.";
                   |ERROR;
                   |ACABA;
               |FINSI;

               |SI aAlfa = "cacugene";
                    ||enNoModDatosOTP = 1;
               |FINSI;
           |SINO;
               |LEE 000#cacuenta.=;
               |SI FSalida ! 0;
                  |MENAV "0000No se puede crear cuentas de proveedores desde aqui.";
                  |ERROR;
                  |ACABA;
               |FINSI;
           |FINSI;
       |FINSI;

       |HAZ MiraSiIncidencia;
   |FINSI;

   |SI entrada = 1;  |Y #cacuenta#0 = doce;  |ACABA;  |FINSI;

   nMensa9 = 0;
   |SI entrada = 0; |HAZ eLeeParametro; |FINSI;
   |SI eaPaCTA2 = "N"; nMensa9 = 1; |FINSI;
   |HAZ defect;
   |HAZ saca0;
   |SI nNoBal8 = 1; |Y operac = 1; |Y eaPaCTA1 = "N";
       |ERROR;
       |ACABA;
   |FINSI;

   cta = #0#0;
   |QBF cta;
   nada = cta % A101;
   |SI nada ! "0";
       ctanum = cta;
       ctaalf = ctanum;
       longnum = ctaalf % A0;
   |SINO;
       ctanum = 1;
       ctaalf = cta;
       longnum = ctaalf % A0;
   |FINSI;
   longalf = cta    % A0;
   |SI longnum ! longalf;|O ctanum = 0;
       |MENAV men3;
       |PINTA #0#0;
       |ERROR;
       |ACABA;
   |FINSI;

   emCta = 1;
   eaCuenta = #cacuenta#0;
   |HAZ SacaNivel;
   |SI eswCta = 1;
      |ACABA;
   |FINSI;

   ||***** 1a Clave;
   ||***** nivel = nivel de la cta;
   #cacuenta#1 = enNivel;
   || la 3a clave esta formada por la cta + zzzz y el campo fantasma del nivel;
   mascara = #cacuenta#0;
   |QBF mascara;
   mascara = mascara +"zzzzzzzzzzzz";
   mascara = mascara % 112;
   #cacuenta#54 = mascara;
   #cacuenta#55 = enNivel;
   #cacuenta#56 = mesdes;
   #cacuenta#57 = meshas;
   |SI #cacuenta#1 = #canempre#13; |O #cacuenta#0 = doce;
       #cacuenta#3 = "S";
   |SINO;
       #cacuenta#3 = "N";
   |FINSI;
   |SI entrada = 0; |PINTA #cacuenta#3; |FINSI;

|FPRC;

|PROCESO eElAnterior; |TIPO 7;
 |SI entrada ! 2;
     |MENSA "    <F12> Alta/Consulta Ctas, <F2> Alta otras Empresas, <F4> Entrada CIF";
 |FINSI;
 operac = (FEntrada / 100) ! 0;
 |SI operac !  1; |ACABA; |FINSI;

 alfa1 = #cacuenta#0 % 101;
 |MODULO aAlfa;
 |SI aAlfa = "cacugene";
   |SI alfa1 ! "6"; |Y alfa1 ! "7";
       |PTEC 802;
       |PTEC 806;
       |ACABA;
   |FINSI;
 |FINSI;

 eaCuenta = #cacuenta#0;
 alfa1 = #cacuenta#0; |QBF alfa1;
 alfa1 = alfa1 % 0;
 nume1 = alfa1;
 eaNomCtaSup = "";
 |SI nume1 > 0; |Y nume1 [ 5; |Y enEjercicio ] 2008;
     |HAZ eLeeCtaMayor;
 |FINSI;

 |SI eaNomCtaSup = "";
     |HAZ LeeCtaSup;
 |FINSI;
 |SI eaNomCtaSup ! "";
    #cacuenta#Campo# = eaNomCtaSup;
 |FINSI;
 |SI entrada = 0; |PINTA #cacuenta#Campo#; |FINSI;
 |PTEC 816;
|FPRC;

|PROCESO pinta;
   |SI num =  1; mes = "Enero";     |ACABA;  |FINSI;
   |SI num = 12; mes = "Diciembre"; |ACABA;  |FINSI;
   |SI num =  2; mes = "Febrero";   |ACABA;  |FINSI;
   |SI num =  3; mes = "Marzo";     |ACABA;  |FINSI;
   |SI num =  4; mes = "Abril";     |ACABA;  |FINSI;
   |SI num =  5; mes = "Mayo";      |ACABA;  |FINSI;
   |SI num =  6; mes = "Junio";     |ACABA;  |FINSI;
   |SI num =  7; mes = "Julio";     |ACABA;  |FINSI;
   |SI num =  8; mes = "Agosto";    |ACABA;  |FINSI;
   |SI num =  9; mes = "Septiembre";|ACABA;  |FINSI;
   |SI num = 10; mes = "Octubre";   |ACABA;  |FINSI;
   |SI num = 11; mes = "Noviembre"; |ACABA;  |FINSI;
|FPRC;

|PROCESO saca0;
   |SI entrada = 0;
       |PINTA #cacuenta#4;
       |PINTA #cacuenta#5;
       |PINTA #cacuenta#6;
       |PINTA #cacuenta#7;
       |PINTA #cacuenta#8;

       |HAZ pintas;
       |HAZ pintas2;
   |FINSI;
|FPRC;


|PROCESO al_emp; |TIPO 30;

   raf_oper = (FEntrada / 100) ! 0;
   |HAZ eOtraAlta;
|FPRC;


|PROCESO cuenta_tipo4; |TIPO 4;
   |DDEFECTO #1;
   #1#61 = #0#11 + #0#14;
   #1#62 = #1#61 + #0#17;
   #1#63 = #1#62 + #0#20;
   #1#64 = #1#63 + #0#23;
   #1#65 = #1#64 + #0#26;
   #1#66 = #1#65 + #0#29;
   #1#67 = #1#66 + #0#32;
   #1#68 = #1#67 + #0#35;
   #1#69 = #1#68 + #0#38;
   #1#70 = #1#69 + #0#41;
   #1#71 = #1#70 + #0#44;
   #1#72 = #1#71 + #0#47;
   #1#73 = #1#72 + #0#50;

   |IMPRE 0;
   |SI FSalida = 0;
      |INFOR "cacuenta";
      |PRINT;
      |PIEINF;
      |FININF;
   |SINO;
      |MENAV "    Proceso abortado ...";
   |FINSI;
   |FINIMP;
|FPRC;

|| ********************** BuscoNIFS
|PROCESO AltaNif1;
 alfa1 = #cacuenta#0;  alfa1 = alfa1 % 104;
 alfa2 = #caclipro#10; alfa2 = alfa2 % 104;
 existe = 1;
 aCamp23 = #caclipro#23;
 aCamp10 = #caclipro#10;
 nCamp11 = #caclipro#11;
 |SI alfa1 = alfa2;
     existe = 2;
     |ERROR10;
 |FINSI;
|FPRC;

|DEFBUCLE AltaNif0;
 #caclipro#4;
 ;
 #cafanta1#0;
 #cafanta1#0;
 e;
 NULL, AltaNif1;
|FIN;

|PROCESO AltaNif;
 operac = (FEntrada / 100) ! 0;
 existe = 0;
 |ABRET #caclipro;
 |SELECT #1#caclipro;
 #caclipro#23 = "C";
 #caclipro#10 = #cacuenta#0;
 |LEE 001#caclipro.=;
 |SI FSalida ! 0;
     #caclipro#23 = "P";
     #caclipro#10 = #cacuenta#0;
     |LEE 101#caclipro.=;
     |SI FSalida ! 0; existe = 1;|FINSI;
 |FINSI;
 |CIERRAT #caclipro;

 |SI existe = 0;      || Ya existe el Cliente/Proveedor en caclipro
     deCuenta = 1;
     Tecla = 9;
     |ACABA;
 |FINSI;

 #cafanta1#0 = "";
 |PUSHV 0737 1163;
 |PINPA #0#cafanta1;
 |PINDA #0#cafanta1;
 |ENDATOS #1#cafanta1;
 |SI FSalida ! 0;
     |ERROR;
 |SINO;
     Comodin = #cafanta1#0; |QBF Comodin;
     |SI Comodin = "";
         |ERROR;
     |SINO;
         |PUSHV 0501 2380;
         existe = 0;
         |BUCLE AltaNif0;   || puede haber mas de uno
         |PINPA #0#caclipro;
         |SI existe = 2;       ||Existe YA con el Mismo Grupo de Cuentas
             |PINDA #0#caclipro;
             |MENAV "    Error.! Ya existe Cliente/Proveedor con otro numero de Cuenta";
             |ERROR;
             |POPV;
             |POPV;
             |ACABA;
         |FINSI;
         |ABRE #caclipro;
         |SI existe ! 0;
             #caclipro#23 = aCamp23;
             #caclipro#10 = aCamp10;
             #caclipro#11 = nCamp11;
             |LEE 001#caclipro.=;
         |SINO;
             |DDEFECTO #caclipro;
             |QBF eaNifEmp;
             |SI Comodin = eaNifEmp;
                 |CONFI "    NIntroducido NIF de la Empresa Actual. Continuar";
                 |SI FSalida ! 0;
                     |ERROR;
                     |POPV;
                     |POPV;
                     |ACABA;
                 |FINSI;
             |FINSI;
         |FINSI;
         digito2 = #cacuenta#0 % 102;
         alfa1   = #cacuenta#0 % 103;
         tipoalta = "C";
         |SI digito2 = "40";  |O digito2 = "41";
            |O alfa1 = "173"; |O alfa1 = "174";
              |O alfa1 = "523"; |O alfa1 = "524";
                |O alfa1 = "551";
             tipoalta = "P";
         |FINSI;
         #caclipro#23 = tipoalta;
         #caclipro#10 = #cacuenta#0;
         #caclipro#11 = #cacuenta#1;
         #caclipro#9  = #cafanta1#0;
         #caclipro#0  = #cacuenta#0;
         |SI existe = 0;
             eaCodNif = #cafanta1#0; |QBF eaCodNif;
             |HAZ LeeNifs;
             |SI ewNif = 0;
                 #caclipro#9 = #709#0;
                 #caclipro#1 = #709#1;
                 Comodin = #709#9; |QBF Comodin;
                 |SI Comodin = "";
                    #caclipro#2 = #709#2;
                 |SINO;
                    #caclipro#2 = Comodin + " " + #709#2;
                 |FINSI;
                 Comodin = #caclipro#2; |QBF Comodin;
                 #caclipro#2 = Comodin + ", " + #709#3;
                 Comodin = #caclipro#2; |QBF Comodin;
                 #caclipro#2 = Comodin + " " + #709#10;
                 Comodin = #caclipro#2; |QBF Comodin;
                 #caclipro#2 = Comodin + " " + #709#11;
                 #caclipro#3 = #709#4;
                 #caclipro#4 = #709#5;
                 #caclipro#5 = #709#7;
                 #caclipro#6 = #709#6;
                 #caclipro#7 = #709#8;
                 #caclipro#8 = #709#17;
                 #caclipro#24 = #709#12;

                 #caclipro#44 = #caclipro#2;
                 #caclipro#45 = #caclipro#3;
                 #caclipro#46 = #caclipro#4;
                 #caclipro#47 = #caclipro#5;
                 #caclipro#48 = #caclipro#6;
                 #caclipro#49 = #caclipro#27
                 #caclipro#50 = #caclipro#25;
             |FINSI;
         |FINSI;
         |GRABA 020#caclipro.b;  || nou
         |LIBERA #caclipro;
         |CIERRA #caclipro;
         tipoalta = tipoalta + "D";
         raf_cue = #cacuenta#0;
         raf_dig = #cacuenta#1;
         raf_sw = 0;
         |SUB_EJECUTA_NP "caclipr1.tab";
         |SI raf_sw = 1;
             #cacuenta#2 = raf_des; |PINTA #cacuenta#2;
         |FINSI;
         |POPV;
     |FINSI;
 |FINSI;
 |POPV;
 |PINTA #cacuenta#2;
|FPRC;
