|FICHEROS;
   any00007 #0;       || Informe comprobacion suma y saldos
   camaasil #1;
   anm00001 #2;
   anm00004 #3;
   anm00005 #4;
   anm00006 #5;
   cacuenta #10;
   anm00017 #17;
   anm00018 #18;
   anw00005 #6;         || trabajo Ctas Analitica
   anm00003 #7;         || Grupos Analiticas
   anm00013 #13;        || Grupos Analiticas  por apuntes
   anm0003@ #107;       || Espejo grupos analitica
   cawdatos #900;
|FIN;

|VARIABLES;
   aMas     = "";
   aFichero = "";
   &entrada = 1;
   potra = 0;
   x = 0;
   men1 = "     NO EXISTE EMPRESA !";
   informe = "";             || Variable informe
   inform1 = "any00007";     || Variable informe
   inform2 = "any20007";     || Variable informe
   inform3 = "any10007";     || Variable informe
   mes = 0;                  || Variable para saber el mes seleccionado
   letrames = "";            || Variable para poner el mes en letra
   varcamp = 0;              || Variable para posicionarse en un campo
   varcamp1 = 0;              || Variable para posicionarse en un campo

   mensa1 = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
   mensa2 = "    FECHA ERRONEA !!!";
   mensa3 = "    CUENTA ANALITICA ERRONEA !!!";
   error1 = "0000Nivel repetido";
   error2 = "0000No existe informe";
   error3 = "0000El n§ niveles no coincide con el n§ niveles Analitica de la Empresa";
   error4 = "0000Nivel de Rotura erroneo !";

   swprint = 0;       || Variable para saber si imprimimos o no
   swsuma = 0;        || Variable para saber si sumamos o no
   nivel = 0;         || Variable que posee el nivel mas alto
   saldo = 0;         || Resta entre el debe y el haber;
   sw = 0;            || sw para nivel anterior
   nivelact = 0;      || Nivel actual
   rellenos = "";     || Variable para rellenar el hasta de la cuenta en zetas
   &i_debe = 0;       || Total del debe por registro
   &i_haber = 0;      || Total del haber por registro
   &i_acumdebe = 0;   || Total acumulado debe por registro
   &i_acumhaber = 0;  || Total acumulado haber por registro
   &i_deudor = 0;     || Total saldo deudor por registro
   &i_acreedor = 0;   || Total saldo acreedor por registro
   &t_debe = 0;       || Total del debe del informe
   &t_haber = 0;      || Total del haber del informe;
   &t_acumdebe = 0;   || Total del acumulado debe del informe
   &t_acumhaber = 0;  || Total del acumulado haber del informe
   &t_deudor = 0;     || Total saldo deudor por informe
   &t_acreedor = 0;   || Total saldo acreedor por informe
   &swlinea = 0;      || Pone o no linea en blanco en informe
   &swcomen = 0;

   &i_presu = 0;
   &i_acumpresu = 0;
   &i_desvpresu = 0;
   &t_presu = 0;
   &t_acumpresu = 0;
   &t_desvpresu = 0;

   menor = 0;
   para1 = 0;
   aParam = "";
   {-1}form  = "";           || Variables menu formato <1> o <2>
   form1 = "2400";
   form2 = "1";
   form3 = "Elija formato: ";
   form4 = "123";
   form5 = " < 1 > ";
   form6 = " < 2 > ";
   form7 = " < 3 > ";
   op_form = 0;

   nume1 = 0;
   nume2 = 0;
   t_agrupa = 0;

   nSwGApte   = 0;
   aCta2Gru   = "";
   edn_error2 = 0;
   grupod2    = 0;
   acdebe2    = 0;
   grupoh2    = 0;
   achaber2   = 0;
   swgrupo2   = 0;
   ctaultima2 = "";
   nSwOp      = 0;
   aNuevoGrupo = "";

   ctanueva = "";
  ctaultima = "";
  grupoc    = "";
  grupod    = 0;
  grupoh    = 0;
  acdebe    = 0;
  achaber   = 0;
  swgrupo   = 0;
  idig      = 0;
  descri    = "";
  sw_con    = 0;
  alfa1     = "";
  alfa2     = "";
  alfa3     = "";

    incta = "";
    ficta = "";
    nrot = 0;
    nr1 = 0;
    nr2 = 0;
    nr3 = 0;
    nr4 = 0;
    selec = 0;
    swalgun = 0;
    swsel = 0;
    debe = 0;
    haber = 0;
    ctaana = "";
    digana = 0;
    anacta = "";

    desfec = @;
    hasfec = @;
    nInkey = 0;
    aDCuenta = "";
    aHCuenta = "";
    aDCtaAgr = "";
    aHCtaAgr = "";
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoan_i;
|INCLUYE digana_i;

|| ***********************************************************************
||                       CALCULOS DE PANTALLA
|| ***********************************************************************

|PROCESO Laagrup; |TIPO 7;
  |SI aParam ! ""; |Y aParam ! "todas";
      #0#20 = aParam;
      |PTEC 802;
  |FINSI;
|FPRC;

|CALCULO LeeSelec; |TIPO 6;
 |SI #0Campo = 0;
     #0#22 = "SIN SELECTOR DE CUENTAS";
     |C_M #0#23, 1, "S";
     |C_M #0#24, 1, "S";
 |SINO;
     |C_M #0#23, 1, "N"; #0#23 = "            "; |PINTA #0#23;
     |C_M #0#24, 1, "N"; #0#24 = "zzzzzzzzzzzz"; |PINTA #0#24;
     |ABRE #4;
     #4#0 = #0Campo;
     |LEE 001#4=;
     |SI FSalida ! 0;
         #4#1 = "ERROR SELECTOR NO EXISTE";
         |ERROR;
     |FINSI;
     #0#22 = #4#1;
     |CIERRA #4;
 |FINSI;
 |PINTA #0#22;
|FCAL;

|CALCULO cuenta; |TIPO 0;
   nInkey = FSalida;
   |SI nInkey = 2;   |ACABA; |FINSI;

   |C_M #0Campo,0,alfa1;
   |SI alfa1 = "N"; |ACABA; |FINSI;

   eaCuenta = #0Campo;
   salidas  = FSalida;
   |HAZ FiltraPuntos;
   #0Campo = eaCuenta;
   |PINTA #0Campo;
|FCAL;

|CALCULO anacta; |TIPO 6;
  salidas = FSalida;
  |HAZ FPuntosAna;
  |SI Campo = 1;
      |SI #0#0 > #0#1;
          |MENAV mensa3;
          |ERROR;
      |FINSI;
  |FINSI;
|FCAL;

|CALCULO rotura;|TIPO 0;
  |SI #0Campo ] nAdig0;
      |MENAV error3;
      |ERROR;
  |FINSI;
|FCAL;

|CALCULO nivel; |TIPO 7;
 |C_M #0Campo, 1,"S";
 |SI Campo = 9; |Y #0#8 < 1; || Si solo hemos seleccionado
     #0Campo = 0; |PINTA #0Campo;
     |C_M #0Campo, 1,"N";
 |FINSI;
 |SI Campo = 10; |Y #0#8 < 2; || Si solo hemos seleccionado
     #0Campo = 0; |PINTA #0Campo;
     |C_M #0Campo, 1,"N";
 |FINSI;
 |SI Campo = 11; |Y #0#8 < 3; || Si solo hemos seleccionado
     #0Campo = 0; |PINTA #0Campo;
     |C_M #0Campo, 1,"N";
 |FINSI;
 |SI Campo = 12; |Y #0#8 < 4; || Si solo hemos seleccionado
    #0Campo = 0; |PINTA #0Campo;
    |C_M #0Campo, 1,"N";
 |FINSI;
|FCAL;

|CALCULO nivelrep;|TIPO 0;  || Para comprobar que no repetimos los niveles
 |SI #0Campo ! 0; |Y #0Campo ] nAdig0;
      |MENAV error4;
      |ERROR;
      |ACABA;
 |FINSI;
 |SI nivel = 0;        || Buscamo el nivel mas bajo;
     nivel = #0#9;
 |FINSI;
 |SI Campo = 10;
    |SI #0#10 = #0#9; |Y #0#10 ! 0;    || Si hay dos niveles iguales error
      |MENAV error1;
      |ERROR;
      |ACABA;
    |SINO;
       |SI #0#10 < #0#9;  || Busca el nivel mas alto
           nivel = #0#10;
       |SINO;
           nivel = #0#9;
       |FINSI;
    |FINSI;
 |FINSI;
 |SI Campo = 11; |Y #0#11 ! 0;
    |SI #0#11 = #0#9; |O #0#11 = #0#10; || Si hay dos niveles iguales error
       |MENAV error1;
       |ERROR;
       |ACABA;
    |FINSI;
    |SI #0#11 < nivel;  || Busca el nivel mas alto
        nivel = #0#11;
    |FINSI;
 |FINSI;
 |SI Campo = 12; |Y #0#12 ! 0;      || Si hay dos niveles iguales error
    |SI #0#12 = #0#9; |O #0#12 = #0#10; |O #0#12 = #0#11;
       |MENAV error1;
       |ERROR;
       |ACABA;
    |FINSI;
    |SI #0#12 < nivel;  || Busca el nivel mas alto
        nivel = #0#12;
    |FINSI;
 |FINSI;
|FCAL;

|PROCESO mesconta; |TIPO 0;
   varcamp = Campo + 1;                || Coge el campo del nobre del mes
   mes = dig1 + (#0Campo - 1);       || Operacion para seber que mes es
   |SI mes > 12; mes = mes - 12; |FINSI;
   |SI #0Campo = 13; mes = 13; |FINSI;

   |HAZ mesletra;                      || Recoge el mes en letra
   #0varcamp = letrames;
   |PINTA #0varcamp;                   || Pinta el nombre del mes

   |SI Campo = 4; |Y  #0#2 > #0#4;
       |ERROR;
   |FINSI;
|FPRC;

|PROCESO mesletra;
   |SI mes = 0;  letrames = "Apertura  "; |FINSI;
   |SI mes = 1;  letrames = "Enero     "; |FINSI;
   |SI mes = 2;  letrames = "Febrero   "; |FINSI;
   |SI mes = 3;  letrames = "Marzo     "; |FINSI;
   |SI mes = 4;  letrames = "Abril     "; |FINSI;
   |SI mes = 5;  letrames = "Mayo      "; |FINSI;
   |SI mes = 6;  letrames = "Junio     "; |FINSI;
   |SI mes = 7;  letrames = "Julio     "; |FINSI;
   |SI mes = 8;  letrames = "Agosto    "; |FINSI;
   |SI mes = 9;  letrames = "Septiembre"; |FINSI;
   |SI mes = 10; letrames = "Octubre   "; |FINSI;
   |SI mes = 11; letrames = "Noviembre "; |FINSI;
   |SI mes = 12; letrames = "Diciembre "; |FINSI;
   |SI mes = 13; letrames = "Cierre    "; |FINSI;
|FPRC;


|| **********************************************************************
||                       CALCULOS DE IMPRESION
|| **********************************************************************

|PROCESO condicion;    || Calculo condiciones de impresion

   |SI #6#10 = "C";
       |DDEFECTO #2;
       #2#0 = #6#0;
       |LEE 001#2=;
   |SINO;
       |DDEFECTO #3;
       #3#0 = #6#0;
       #3#1 = #6#1;
       |LEE 001#3=;
       #2#1 = #3#2;
   |FINSI;

   swprint = 1;           || Variable para saber si imprimimos o no
   swsuma  = 0;            || Variable para saber si sumamos o no

   i_debe  = 0;            || Pone a cero el total debe del registro
   i_haber = 0;           || Pone a cero el total haber del registro
   i_presu = 0;
   i_acumdebe = 0;        || Pone a cero el total acumulado debe del registro
   i_acumhaber = 0;       || Pone a cero el total acumulado haber del registro
   i_acumpresu = 0;

   |SI sw = 0;
      nivelact = #6#1;  || Nivel actual
      sw = 1;
   |FINSI;

   |SI #6#10 = "C";  || Si es el nivel mas alto entoces
        swsuma = 1;                    || se suma.
   |FINSI;

   i_debe  = #6#2;
   i_haber = #6#3;

   varcamp  = 11 + #0#2; |SI #0#2 = 0;  varcamp  = 12; |FINSI;
                         |SI #0#2 = 13; varcamp  = 23; |FINSI;
   varcamp1 = 11 + #0#4; |SI #0#4 = 0;  varcamp1 = 12; |FINSI;
                         |SI #0#4 = 13; varcamp1 = 23; |FINSI;
   |PARA x = 12; |SI x [ varcamp1; |HACIENDO x = x + 1;
         |SI x ] varcamp; |Y x [ varcamp1;
             i_presu      = i_presu + #6x;     || Suma el debe
         |FINSI;
         i_acumpresu  =  i_acumpresu + #6x;
   |SIGUE;

   nume1 = 0;
   |SI #6#25 = "D";   nume1 = i_debe - i_haber;  |FINSI;
   |SI #6#25 = "H";   nume1 = i_haber - i_debe;  |FINSI;
   i_desvpresu = ((nume1 * 100) / i_presu) ! 2;

   || ************ Bucle del debe y haber acumulado *************

   i_acumdebe  = #6#5;   || Suma el acumulado debe
   i_acumhaber = #6#6;   || Suma el acumulado haber

   nume1 = 0;
   |SI #6#25 = "D";   nume1 = i_acumdebe - i_acumhaber; |FINSI;
   |SI #6#25 = "H";   nume1 = i_acumhaber - i_acumdebe; |FINSI;
   t_desvpresu = ((nume1 * 100)/ i_acumpresu) ! 2;

   saldo = i_acumdebe - i_acumhaber;       || Coge la diferencia
   |SI saldo > 0; i_deudor = saldo; i_acreedor = 0;            |FINSI;
   |SI saldo < 0; i_deudor = 0;     i_acreedor = saldo * (-1); |FINSI;
   |SI saldo = 0; i_deudor = 0;     i_acreedor = 0;            |FINSI;

   |SI i_acumdebe = 0;|Y i_acumhaber = 0;|Y i_acumpresu = 0;|Y #0#6 = "N";
        |ACABA;
   |FINSI;

   |PRINT;
   |SI swsuma = 1;    || Si se puede sumar los totales
       t_debe      = t_debe      + i_debe;        || Suma el total del debe
       t_haber     = t_haber     + i_haber;       || Suma el total del haber
       t_presu     = t_presu     + i_presu;
       t_acumdebe  = t_acumdebe  + i_acumdebe;    || Suma el total acumulado debe
       t_acumhaber = t_acumhaber + i_acumhaber;   || Suma el total acumulado haber
       t_acumpresu = t_acumpresu + i_acumpresu;
       t_deudor    = t_deudor    + i_deudor;      || Suma total saldo deudor
       t_acreedor  = t_acreedor  + i_acreedor;    || Suma total saldo acreedor
   |FINSI;

   swcomen = 1;        || Para imprimir el saldo anterior
|FPRC;


|DEFBUCLE any000070;      || Bucle del plan contable
   #6#1;                  || Lee por la primera clave
   ;
   #0#0, "            ";
   #0#1, "zzzzzzzzzzzz";
   e;
   NULL, condicion;
|FIN;

|| ***********************************************************************
|PROCESO ver_selec;
 |SI eaCuenta ] #5#2; |Y eaCuenta [ #5#3;
     swsel = 1;
     |ERROR10;
 |FINSI;
|FPRC;

|DEFBUCLE seleccion;
 #5#1;
 ;
 selec, 000;
 selec, 999;
 e;
 NULL, ver_selec;
|FIN;
|| *******************************************************************
|PROCESO graba3;
 |SI ctaana = #18#0;
     |ACABA;
 |FINSI;
 #6#0  = ctaana;
 #6#1  = digana;
 #6#24 = "";
 |LEE 101#6=;
 |SI FSalida ! 0;
     |DDEFECTO #6;
     #6#0 = ctaana;
     #6#1 = digana;
     #6#8 = anacta;
     #6#9 = digana;
     #6#24 = "";
     |GRABA 020#6c;
 |FINSI;
 |PARA nume1 = 12; |SI nume1 [ 23; |HACIENDO nume1 = nume1 + 1;
       nume2 = nume1-8;
       #6nume1 = #6nume1 + #18nume2;
 |SIGUE;
 #6#10 = "M";
 |GRABA 020#6a;
 |LIBERA #6;
|FPRC;

|PROCESO presup2;    || si no esta como ficha presupuestaria no sale SI

   eaCuenta = #18#20;  ||si no esta seleccionada no la recoge
   |SI selec ! 0; |Y #17#2 = "S";
       swsel = 0;
       |BUCLE seleccion;
       |SI swsel = 0; |ACABA; |FINSI;
   |FINSI;

   #6#0  = #18#0;
   #6#1  = nAdig0;
   #6#24 = "";
   |LEE 101#6=;
   |SI FSalida ! 0;
      |DDEFECTO #6;
      #6#0 = #18#0;
      #6#1 = nAdig0;
      #6#8 = #6#0;
      #6#9 = #6#1;
      #6#24 = "";
      |GRABA 020#6c;
   |FINSI;
   |PARA nume1 = 12; |SI nume1 [ 23; |HACIENDO nume1 = nume1 + 1;
         nume2 = nume1-8;
         #6nume1 = #6nume1 + #18nume2;
   |SIGUE;
   #6#10 = "C";
   #6#25 = #18#02;  || signo
   |GRABA 020#6a;
   |LIBERA #6;

   swalgun = 1;
   || ... Graba Auxiliar de Cuentas.
   |SI nrot = 0; |ACABA; |FINSI;
   |SI nr1 ! 0;
      digana = nr1;
      ctaana = #18#0;
      |HAZ creamayor;
      |HAZ graba3;
   |FINSI;
   |SI nr2 ! 0;
       digana = nr2;
       ctaana = #18#0;
       |HAZ creamayor;
       |HAZ graba3;
  |FINSI;
  |SI nr3 ! 0;
       digana = nr3;
       ctaana = #18#0;
       |HAZ creamayor;
       |HAZ graba3;
  |FINSI;
  |SI nr4 ! 0;
      digana = nr4;
      ctaana = #18#0;
      |HAZ creamayor;
      |HAZ graba3;
  |FINSI;
|FPRC;

|DEFBUCLE bu_pres2;   || balances situacion y explotacion
   #18#2;
   ;
   t_agrupa, "       ", aDCtaAgr;
   t_agrupa, "zzzzzzz", aHCtaAgr;
   e;
   NULL, presup2;
|FIN;

|| ***********************************************************************
|PROCESO ggrupos2;
 ctanueva = #107#2;
 debe  = 0;
 haber = 0;
 |SI grupod2 ! 0; debe  = (grupod2 * #107#4 / 100) ! EURODB; |FINSI;
 |SI grupoh2 ! 0; haber = (grupoh2 * #107#4 / 100) ! EURODB; |FINSI;
 |SI debe ! 0; |O haber ! 0;
     |SI ctanueva ] incta; |Y ctanueva [ ficta;
         |HAZ gragrup;
     |FINSI;
 |FINSI;
 swgrupo2   = 1;
 ctaultima2 = #107#2;
 acdebe2    = (acdebe2 + debe)   ! EURODB;
 achaber2   = (achaber2 + haber) ! EURODB;
|FPRC;

|PROCESO fingrupo2;
 |SI swgrupo2 = 0; |ACABA; |FINSI;
 |SI acdebe2 = grupod2; |Y achaber2 = grupoh2; |ACABA; |FINSI;
 ctanueva = ctaultima2;
 debe  = grupod2 - acdebe2;
 haber = grupoh2 - achaber2;
 |SI debe ! 0; |O haber ! 0;
     |SI ctanueva ] incta; |Y ctanueva [ ficta;
         |HAZ gragrup;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE leegrupo2;
 #107#1002;
 ;
 aCta2Gru, 000;
 aCta2Gru, 999;
 e;
 NULL, ggrupos2, NULL, NULL, fingrupo2;
|FIN;

|PROCESO Subgrupo;
 aCta2Gru = ctanueva;
 grupod2  = debe;  acdebe2  = 0;
 grupoh2  = haber; achaber2 = 0;
 swgrupo2  = 0;
 ctaultima2 = "";
 |BUCLE leegrupo2;
|FPRC;

|PROCESO TrataCtaGrupo;
 alfa3 = ctanueva % 103;
 |SI alfa3 ! "GRU";   ||Tratamiento normal de Cuenta Analitica
     |SI ctanueva ] incta; |Y ctanueva [ ficta;
         |HAZ gragrup;
     |FINSI;
 |SINO;
     |SI #0#16 = "N";   || Si quiere grabar grupos
         |SI ctanueva ] incta; |Y ctanueva [ ficta;
             |HAZ gragrup;
         |SINO;
             |HAZ Subgrupo;
         |FINSI;
     |SINO;
         |HAZ Subgrupo;
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO ggruposA;
 nSwGApte = 1;
 ctanueva = #13#6;
 |SI #1#8 = "D";
     debe = #13#9;
     haber = 0;
 |SINO;
     debe = 0;
     haber = #13#9;
 |FINSI;
 |HAZ TrataCtaGrupo;
|FPRC;

|DEFBUCLE leegrApte;
 #13#1;
 ;
 #1#0, #1#1, #1#2, #1#3, grupoc, 000;
 #1#0, #1#1, #1#2, #1#3, grupoc, 999;
 e;
 NULL, ggruposA;
|FIN;

|PROCESO ggrupos;
 ctanueva = #7#2;
 debe  = 0;
 haber = 0;
 |SI grupod ! 0; debe  = (grupod * #7#4 / 100) ! EURODB; |FINSI;
 |SI grupoh ! 0; haber = (grupoh * #7#4 / 100) ! EURODB; |FINSI;
 swgrupo   = 1;
 ctaultima = #7#2;
 acdebe    = (acdebe + debe)   ! EURODB;
 achaber   = (achaber + haber) ! EURODB;
 |SI debe ! 0; |O haber ! 0;
     |HAZ TrataCtaGrupo;
 |FINSI;
|FPRC;

|PROCESO fingrupo;
|| aqui
 |SI swgrupo = 0; |ACABA; |FINSI;
 |SI acdebe = grupod; |Y achaber = grupoh; |ACABA; |FINSI;
 ctanueva = ctaultima;
 debe  = grupod - acdebe;
 haber = grupoh - achaber;
 |SI debe ! 0; |O haber ! 0;
     |HAZ TrataCtaGrupo;
 |FINSI;
|FPRC;

|DEFBUCLE leegrupo;
 #7#1;
 ;
 grupoc, 000;
 grupoc, 999;
 e;
 NULL, ggrupos, NULL, NULL, fingrupo;
|FIN;

|PROCESO BuscoOtros;
     nSwGApte = 0;
     grupoc  = ctanueva;
     |BUCLE leegrApte;
     |SI nSwGApte = 0;
         ctanueva = #1#29;
         swgrupo  = 0;
         grupod   = debe;  acdebe  = 0;
         grupoh   = haber; achaber = 0;
         ctaultima = "";
         |BUCLE leegrupo;
     |FINSI;
|FPRC;

|PROCESO grabatra;  || ... Graba temporales
 |SI #1#9 = 0; |ACABA; |FINSI;
 |SI #1#8 = "D";
     debe = #1#9;
     haber = 0;
 |SINO;
     debe = 0;
     haber = #1#9;
 |FINSI;
 ctanueva = #1#29;
 alfa3 = ctanueva % 103;
 |SI alfa3 ! "GRU";   ||Tratamiento normal de Cuenta Analitica
     |HAZ gragrup;
 |SINO;
     |SI #0#16 = "N";   || Si quiere grabar grupos
         |SI ctanueva ] incta; |Y ctanueva [ ficta;
             |HAZ gragrup;
         |SINO;
             |HAZ BuscoOtros;
         |FINSI;
     |SINO;
         |HAZ BuscoOtros;
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO gragrup;
 || ... Graba temporal de Cuentas de Analitica
 |SI ctanueva [ "       "; |ACABA; |FINSI;
 dn_cuent = ctanueva;
 |HAZ dig_ana2;
 |SI dn_error !  0; |ACABA; |FINSI;
 #6#0 = ctanueva;
 #6#1 = nAdig0;
 #6#24 = "";
 |LEE 101#6=;
 |SI FSalida ! 0;
     |DDEFECTO #6;
     #6#0 = ctanueva;
     #6#1 = nAdig0;
     #6#8 = #6#0;
     #6#9 = #6#1;
     #6#24 = "";
     |GRABA 020#6c;
 |FINSI;
 |SI #1#1 ] desfec;
     #6#2 = #6#2 + debe;
     #6#3 = #6#3 + haber;
     #6#4 = #6#2 - #6#3;
 |FINSI;
 #6#5 = #6#5 + debe;
 #6#6 = #6#6 + haber;
 #6#7 = #6#5 - #6#6;
 #6#10 = "C";
 |GRABA 020#6a;
 |LIBERA #6;

 swalgun = 1;
 || ... Graba Auxiliar de Cuentas.
 |SI nrot = 0; |ACABA; |FINSI;
 |SI nr1 ! 0;
     digana = nr1;
     ctaana = ctanueva;
     |HAZ creamayor;
     |HAZ graba2;
 |FINSI;
 |SI nr2 ! 0;
     digana = nr2;
     ctaana = ctanueva;
     |HAZ creamayor;
     |HAZ graba2;
 |FINSI;
 |SI nr3 ! 0;
     digana = nr3;
     ctaana = ctanueva;
     |HAZ creamayor;
     |HAZ graba2;
 |FINSI;
 |SI nr4 ! 0;
     digana = nr4;
     ctaana = ctanueva;
     |HAZ creamayor;
     |HAZ graba2;
 |FINSI;
|FPRC;

|PROCESO graba2;
 |SI ctaana = ctanueva;
     |ACABA;
 |FINSI;

 #6#0 = ctaana;
 #6#1 = digana;
 #6#24 = "";
 |LEE 101#6=;
 |SI FSalida ! 0;
     |DDEFECTO #6;
     #6#0 = ctaana;
     #6#1 = digana;
     #6#8 = anacta;
     #6#9 = digana;
     #6#24 = "";
     |GRABA 020#6c;
 |FINSI;
 |SI #1#1 ] desfec;
     #6#2 = #6#2 + debe;
     #6#3 = #6#3 + haber;
     #6#4 = #6#2 - #6#3;
 |FINSI;
 #6#5 = #6#5 + debe;
 #6#6 = #6#6 + haber;
 #6#7 = #6#5 - #6#6;

 #6#10 = "M";
 |GRABA 020#6a;
 |LIBERA #6;
|FPRC;

|PROCESO creamayor;
    |SI digana = 1;  nume1 = nAdig1;  |FINSI;
    |SI digana = 2;  nume1 = nAdig2;  |FINSI;
    |SI digana = 3;  nume1 = nAdig3;  |FINSI;
    |SI digana = 4;  nume1 = nAdig4;  |FINSI;
    nume1 = 100 + nume1;
    alfa1 = ctaana % nume1;
    alfa2 = ctaana % nume1;
    alfa1 = alfa1 + "       ";
    alfa2 = alfa2 + "zzzzzzz";
    ctaana = alfa1 % 107;
    anacta = alfa2 % 107;
|FPRC;

|| -----------------------------------------------------------------
|| ---- Seleccion de apuntes.

|| ... Segundo nivel de grupos

|CALCULO ver_grupo2;
 |SI #107#2 ] incta; |Y #107#2 [ ficta;
     dn_cuent = #107#2;
     |HAZ dig_ana2;
     |SI dn_error = 0;
         edn_error2 = 0;
         |ERROR10;
     |FINSI;
 |FINSI;
|FCAL;

|DEFBUCLE selegrupo2;
 #107#1002;
 ;
 aCta2Gru, 000;
 aCta2Gru, 999;
 e;
 NULL, ver_grupo2;
|FIN;

|PROCESO ver_grupo_apte;
 nSwGApte = 1;
 |SI #13#6 ] incta; |Y #13#6 [ ficta;
     dn_cuent = #13#6;
     |HAZ dig_ana2;
     |SI dn_error = 0;
         swsel = 1;
        |ERROR10;
     |FINSI;
 |SINO;
     alfa1 = #13#6 % 103;
     |SI alfa1 = "GRU";
         edn_error2 = 1;
         aCta2Gru   = #13#6;
         |BUCLE selegrupo2;
         |SI edn_error2 = 0;
             swsel = 1;
             |ERROR10;
         |FINSI;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE selgruApte;
 #13#1;
 ;
 #1#0, #1#1, #1#2, #1#3, #1#29, 000;
 #1#0, #1#1, #1#2, #1#3, #1#29, 999;
 e;
 NULL, ver_grupo_apte;
|FIN;

|PROCESO ver_grupo;
 |SI #7#2 ] incta; |Y #7#2 [ ficta;
     dn_cuent = #7#2;
     |HAZ dig_ana2;
     |SI dn_error = 0;
         swsel = 1;
        |ERROR10;
     |FINSI;
 |SINO;
     alfa1 = #7#2 % 103;
     |SI alfa1 = "GRU";
         edn_error2 = 1;
         aCta2Gru   = #7#2;
         |BUCLE selegrupo2;
         |SI edn_error2 = 0;
             swsel = 1;
             |ERROR10;
         |FINSI;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE selegrupo;
 #7#1;
 ;
 #1#29, 000;
 #1#29, 999;
 e;
 NULL, ver_grupo;
|FIN;

|PROCESO ver_apunte;
 eaCuenta = #1#4;
 |SI selec ! 0;
     swsel = 0;
     |BUCLE seleccion;
     |SI swsel = 0; |ACABA; |FINSI;
 |FINSI;

 swsel = 0;           || Solo comprueba si esta dentro de la seleccion
 alfa3 = #1#29 % 103;
 |SI #1#29 ] incta; |Y #1#29 [ ficta;
     dn_cuent = #1#29;
     |HAZ dig_ana2;
     |SI dn_error = 0; swsel = 1; |FINSI;
 |SINO;
     |SI alfa3 = "GRU";
         nSwGApte = 0;
         |BUCLE selgruApte;
         |SI nSwGApte = 0;
             |BUCLE selegrupo;
         |FINSI;
     |FINSI;
 |FINSI;
 |SI swsel = 0; |ACABA; |FINSI;
 |PINTA 2321, " Creando temporal: ";
 |ATRI R;
 |PINTA 2340, #1#1;
 |ATRI 0;
 |HAZ grabatra;
|FPRC;

|DEFBUCLE cargatra;
 #1#1;
 4;
 00, fec1,   000001, 0001, aDCuenta;
 99, hasfec, 999999, 9999, aHCuenta;
 e;
 NULL, ver_apunte;
|FIN;

|PROCESO Pricargatra;
 ctanueva = #2#0;
 debe     = 0;
 haber    = 0;
 alfa1    = #2#0; |QBF alfa1;
 |ABRE #3;
 #3#0 = alfa1;
 |LEE 001#3=;
 |SI FSalida ! 0;
     |HAZ gragrup;
 |SINO;
     alfa2 = #3#0; |QBF alfa2;
     |SI alfa1 ! alfa2;
         |HAZ gragrup;
     |FINSI;
 |FINSI;
 |CIERRA #3;
|FPRC;

|DEFBUCLE Pricargatra;
 #2#1;
 ;
 #0#0;
 #0#1;
 e;
 NULL, Pricargatra;
|FIN;
|| ************************************************************

|PROCESO impre;       || Impresion por impresora;

   |HAZ FecApeCie;
   |SI enMAper ! dig1;
       |SI #0#4 < enMAper; |Y #0#4 ! 0;
           |ACABA;
       |FINSI;
   |FINSI;
   enM1 = #0#2; enM2 = #0#4;
   |HAZ LaFechaEmp;
   desfec = #900#13; hasfec = #900#14;

  incta = #0#0;
  ficta = #0#1;
   nrot = #0#8;
    nr1 = #0#9;
    nr2 = #0#10;
    nr3 = #0#11;
    nr4 = #0#12;

   |MENU form;
   op_form = FSalida;
   |SI op_form [ 0; |ACABA; |FINSI;

   |SI op_form = 1; informe = inform1; |FINSI;
   |SI op_form = 2; informe = inform2; |FINSI;
   |SI op_form = 3; informe = inform3; |FINSI;

   |IMPRE 0;                                 || Abrimos impresora
   |SI FSalida ! 0; |ACABA; |FINSI;

   |INFOR informe;                    || Abrimos informe
   |SI FSalida ! 0;
       |MENAV error2;
       |FINIMP;
       |ACABA;
   |FINSI;

   |DELINDEX #6;
   swalgun = 0;
   t_agrupa = #0#20;

   selec = #0#21;
   aDCuenta = "            ";
   aHCuenta = "zzzzzzzzzzzz";
   |SI selec = 0; aDCuenta = #0#23; aHCuenta = #0#24; |FINSI;
   aDCtaAgr = aDCuenta;
   aHCtaAgr = aHCuenta;
   |SI #17#2 = "N"; aDCtaAgr = ""; aHCtaAgr = "zzzzzzzzzzzz"; |FINSI;

   |ABRE #6;
   |BUCLE bu_pres2;

   |SI #0#6 = "S";
       nSwOp = 1;
       |BUCLE Pricargatra;
   |FINSI;
   nSwOp = 0;
   |BUCLE cargatra;
   |CIERRA #6;

   |SI swalgun = 0;
       |MENAV "    Seleccion Vacia ...";
   |SINO;
       |ABRE #2;
       |ABRE #3;
       |BUCLE any000070;
       |CIERRA #3;
       |CIERRA #2;
       |PIEINF;
   |FINSI;
   |FININF;
   |FINIMP;
|FPRC;

|PROCESO borrar;
   nivel = 0;
   sw = 0;
   swcomen = 0;
   i_debe = 0;
   i_haber = 0;
   i_presu = 0;
   i_acumdebe = 0;
   i_acumhaber = 0;
   i_acumpresu = 0;
   i_deudor = 0;
   i_acreedor = 0;
   t_debe = 0;
   t_haber = 0;
   t_presu = 0;
   t_acumdebe = 0;
   t_acumhaber = 0;
   t_acumpresu = 0;
   t_deudor = 0;
   t_acreedor = 0;
|FPRC;

|| *********************************************************************
||                             COMIENZO
|| *********************************************************************
|PROGRAMA;

  |CLS;
  |CABEZA "Balance Sumas y Saldos Presupuestos Ctas Analiticas";

  aParam = PARAMETRO; |QBF aParam;

  |SI aParam = "";
      |HAZ inicio;
      |HAZ Analitica;
      |SI  enSwAna = 0;
           alfa1 = "    CONTABILIDAD NO ANALITICA ";
           |SI eaAnNiv = "E";
               alfa1 = "    EMPRESA NO ANALITICA ";
           |FINSI;
           |MENAV alfa1;
           |ACABA;
      |FINSI;
   |FINSI;

   |C_M #0#8,1,"N";
   |C_M #0#9,1,"N";
   |C_M #0#10,1,"N";
   |C_M #0#11,1,"N";
   |C_M #0#12,1,"N";
   |SI nAdig0 > 1; |C_M #0#8,1,"S"; |FINSI;

   |DBASE aMas;
   aMas  = aMas + "def/anm00003";
   |CARGA_DEF aMas, anm0003@;
   |SI FSalida ! 0;
       |MENAV "    Error Cargado Fichero de Grupos Analiticos. Proceso Interrumpido";
       |ACABA;
   |FINSI;

   aFichero = ("y5" + Usuario) % 108;
   |NOME_DAT #6 aFichero;
   |DELINDEX #6;

   FSalida = 1;
   |PARA; |SI 0 < FSalida; |HACIENDO;
       |PINPA #0#0;
       |PINDA #0#0;
       |ENDATOS #1#0;   || Seleccion
       |SI 0 [ FSalida;
          |HAZ impre;
          |HAZ borrar;
          FSalida = 1;
       |FINSI;
       |DELINDEX #6;  || se carga el temporal
    |SIGUE;
    |DESCARGA_DEF #107;
|FPRO;

||*******************************************************************
