|FICHEROS;
   caz00038 #0;

   caw00037 #900,MANTE;
   calibros #1;

   camaniva #11;
   caivalin #12;
   caivaasi #200;

  :/modelos/def/dsmom030 #330;
  :/modelos/def/dsmom041 #41;
  :/modelos/def/dsmom042 #42;

   canempre #30;
   caselem1 #998;

   espejo@  #999;
|FIN;

|VARIABLES;
    nInkey   = 0;
    aAlfa1   = "";
    aAlfa2   = "";
    nNume1   = 0;
    nNume2   = 0;
    aFichero = "";
    informe  = "caw00037";

    nSwAlgun = 0;
    nSw      = 0;
    nCampo   = 0;
    aDTipo   = "";
    aHTipo   = "";
    &enSwLinea = 0;
    nError   = 0;
    &nC0     = "";
    &nC1     = "";
    &nC2     = "";
    &aC3     = "";
    &nC4     = "";
    &fC5     = @;
    &aC7     = "";
    &aC11    = "";
    &nC12    = 0;
    &aC17    = "";
    &aC18    = "";
    &aC19    = "";

   &ct1_am        = "";
   &ct2_am        = 0;
   &fec_am        = @;
   &imp_am        = 0;
   &iva_am        = 0;
   &tiva_am       = 0;
   &nLib_am       = 0;
   &aSer_am       = "";
   &nFra_am       = 0;
   &nDoc_am       = 0;
   &nBas_am       = 0;
   &c_ctana       = "";
   &c_conp        = 0;
   &c_am          = "";
   &prv_am        = "";
   &dni_am        = "";
   &clau_am       = "";
   &igic_am       = "";

   &nSwFra        = 0;
   &enSwEncuentro = 0;
   &aElemento     = "";
   &nNumero       = 0;
   &nLinea        = 0;
   &enModAmor     = 0;

   nEstado = 0;
   aExiste = "";
   aMulDep = "";
   nDdepar = 0;
   nHdepar = 0;
   nSwSel  = 0;
   &eaDElem    = "";
   &enDNEle    = "";
   &enDeAqui   = 0;

   aMas = "";
   aDef = "";
   aDeducible = "";
   fDFecha    = @;
|FIN;

|INCLUYE dscont_i;

|| ***************** Calculos de Pantalla
|CALCULO Primero; |TIPO 8;
  |HAZ inicio;

  |ABRE #200;
  |LEE 000#200p;
  |SI FSalida ! 0;
      |DDEFECTO #200;
      |GRABA 040#200n;
  |FINSI;
  |CIERRA #200;
  aAlfa1 = #200#44;
  |C_M #0#7,1,aAlfa1;
  |C_M #0#8,1,aAlfa1;
  |HAZ eParaModelos;
|FCAL;

|CALCULO defectos; |TIPO 7;
  #0#4 = fec1; |PINTA #0#4;
  #0#5 = fec2; |PINTA #0#5;
|FCAL;

|CALCULO fecha; |TIPO 0;
  fDFecha = fec1;
  |SI enSwSelFc = 1; fDFecha = "01.01.0000"; |FINSI;
  |SI #0Campo < fDFecha; |O #0Campo > fec2;
      |MENAV "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
      |ERROR;
      |ACABA;
  |FINSI;
  |SI Campo = 5; |Y #0#5 < #0#4;
      |MENAV "    FECHA ERRONEA ";
      |ERROR;
  |FINSI;
|FCAL;

|CALCULO nodepar; |TIPO 7;
   |C_M #0#9, 1, eaDepar;
   |C_M #0#10, 1, eaDepar;
|FCAL;

|CALCULO DesHasC; |TIPO 0;
     |SI #0#11 = "N";
         #0#12 = 001;  |PINTA #0#12;  |C_M #0#12, 1, "N";
         #0#13 = 999;  |PINTA #0#13;  |C_M #0#13, 1, "N";
         |PARA nCampo = 14;  |SI nCampo [ 25;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |C_M #0#12, 1, "S";
         |C_M #0#13, 1, "S";
         |PARA nCampo = 14;  |SI nCampo [ 25;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "N";
               #0nCampo = 0;  |PINTA #0nCampo;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasCon; |TIPO 0;
     |SI #0Campo = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 25;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0;  |C_M #0nCampo, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 25;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|| *************************************************************************
|PROCESO VerElemento;
      eaDElem   = #900#17;
      enDNEle   = #900#18;
      enDeAqui  = 1;
      |SUB_EJECUTA_NP ":modelos/dsmoz036";
|FPRC;

|PROCESO AltaElemento;
      |CONFI "2400SDesea crear el Bien Amortizable : ";
      |SI FSalida ! 0;  |ACABA; |FINSI;

      ct1_am  = #900#15; eaCuenta = #900#15; |HAZ SacaNivel;
      ct2_am  = enNivel;
      fec_am  = #900#5;
      imp_am  = #900#12;   ||pase a amortizacion solo la base fra.
      tiva_am = #900#13;   || % IVA
      iva_am  = #900#14;   || cuota
      |SI #900#24 ! "S";
          iva_am  = 0;
          tiva_am = 0;
          |SI #900#24 = "I";
              imp_am = #900#12 + #900#14;
          |FINSI;
      |FINSI;
      c_conp  = #900#6;
      c_am    = #900#7;  |QBF c_am;
      c_am    = c_am + " " + #900#20;
      prv_am  = #900#9;
      nLib_am = #900#0;
      aSer_am = #900#3;
      nFra_am = #900#4;
      c_ctana = #900#21;
      enModAmor = 1;
      nDoc_am = #900#1;
      nBas_am = #900#2;
      nSwFra  = 1; aElemento = "";
      dni_am  = #900#10;
      clau_am = #900#22;
      igic_am = #900#23;
      enActividad = #900#11;
      eaCodSubdep = #900#26;
      |SI imp_am ! 0; |SUB_EJECUTA_NP "camocab1.tab"; |FINSI;
      nSwFra  = 0;
      |SI aElemento ! "";
          #900#16 = "N";
          |ABRE #41;
          #41#0 = enEmpresa;
          #41#2 = aElemento;
          #41#3 = nNumero;
          |LEE 001#41=;
          |SI FSalida = 0; aExiste = "S"; |FINSI;
          |CIERRA #41;
          #900#16 = aExiste;
          #900#17 = #41#2;
          aAlfa1 = "";
          |SI aExiste = "S";
              aAlfa1 = #41#3; aAlfa1 = ("00" + aAlfa1) % -102;
          |FINSI;
          #900#18 = aAlfa1;
          #900#19 = #41#4;
      |FINSI;
|FPRC;

|| ******************************************************
|PROCESO ImprimeAux;
 nC0  = #999#0; nC0 = ("00" + nC0) % -102;
 nC1  = #999#1; nC1 = ("0000000000" + nC1) % -110;
 nC2  = #999#2; nC2 = ("00" + nC2) % -102;
 aC3  = #999#3;
 nC4  = #999#4; nC4 = ("000000" + nC4) % -106;
 fC5  = #999#5;
 aC7  = #999#7;
 aC11 = #999#11;
 nC12 = #999#12;
 aC17 = #999#17;
 aC18 = #999#18;
 aC19 = #999#19;
 |PRINT;
 nSw = 1;
|FPRC;

|DEFBUCLE ImprimeAux;
 #999#1003;
 ;
 00, 0000000001,00;
 99, 9999999999,99;
 ;
 NULL, ImprimeAux;
|FIN;

|PROCESO Imprimir;
  |IMPRE 0;
  |SI FSalida ! 0;
      |MENAV "    Proceso interrumpido";
      |ACABA;
  |FINSI;
  |INFOR informe;
  |SI FSalida ! 0;
      |MENAV "    Error. No existe informe";
      |FINIMP;
      |ACABA;
  |FINSI;

  nSw = 0;
  |BUCLE ImprimeAux;
  |SI nSw = 1;
      |PIEINF;
  |FINSI;
  |FININF;
  |FINIMP;
|FPRC;

|PROCESO Tipo5w037;  |TIPO 5;
       |LEE 040#900p;
       #900#16 = "S";
       |LINEAS_BI_ATRI #900#16, D, S, -1;
|FPRC;

|PROCESO Tipo7w037; |TIPO 7;
  |SI #900#16 = "S";
      |MENSA "    <F2> Consulta Elemento Amortizable";
  |SINO;
      |MENSA "    <F2> Alta Elemento Amortizable";
  |FINSI;
|FPRC;

|PROCESO Tipo11w037; |TIPO 11;

 nInkey = FSalida;
 |SI nInkey ! 10; |Y nInkey ! 11; |ACABA; |FINSI;

 |SI nInkey = 11;
     |HAZ Imprimir;
     |ACABA;
 |FINSI;

 |LEE 141#900=;

 |SI #900#16 = "S";
     |HAZ VerElemento;
     |ERROR;
 |SINO;
     |HAZ AltaElemento;

     |GRABA 000#900a;
 |FINSI;

 |LIBERA #900;
|FPRC;

|| *************************************************************************
|| **************************   MEOLLO   ***********************************
|| *************************************************************************
|PROCESO GrabaRegistro;
      |DDEFECTO #900;
      #900#0 = #12#0;
      #900#1 = #12#1;
      #900#2 = #12#2;
      |LEE 000#900=;
      nEstado = FSalida;
      #900#0  = #12#0;
      #900#1  = #12#1;
      #900#2  = #12#2;
      #900#3  = #11#2;
      #900#4  = #11#3;
      #900#5  = #11#4;
      #900#6  = #12#3;
      #900#7  = #12#4;
      #900#8  = #11#12;
      #900#9  = #11#13;
      #900#10 = #11#14;
      #900#11 = #12#30;
      #900#26 = #12#31;
      #900#12 = #12#6;
      #900#13 = #12#7;
      #900#14 = #12#8;
      #900#15 = #12#12;
      #900#16 = aExiste;
      #900#17 = #41#2;
      aAlfa1 = "";
      |SI aExiste = "S";
          aAlfa1 = #41#3;
          aAlfa1 = ("00" + aAlfa1) % -102;
      |FINSI;
      #900#18 = aAlfa1;
      #900#19 = #41#4;
      #900#20 = #11#22;
      #900#21 = #12#28;
      #900#22 = #11#42;
      #900#24 = #12#5;
      #900#25 = #900#12;
      |SI #12#5 ! "S"; |Y #11#36 = "S";
          |SI #12#12 = #12#13;
              #900#25 = #900#12 + #900#14;
              #900#24 = "I";
          |FINSI;
      |FINSI;

      |ABRE #calibros;
      |DDEFECTO #calibros;
      #calibros#0 = #11#0;
      |LEE 001#calibros.=;
      |CIERRA #calibros;
      #900#23 = #calibros#7;

      |SI #0#26 = "N"; |O aExiste ! "S";
          |SI nEstado = 0; |GRABA 020#900a; |FINSI;
          |SI nEstado ! 0; |GRABA 020#900n; |FINSI;
          nSwAlgun = 1;
      |FINSI;
|FPRC;

|PROCESO LinLibro0;

  aAlfa1 = #12#30; |QBF aAlfa1; |SI aAlfa1 = ""; #12#30 = #11#10; |FINSI;

  |SI aMulDep = "S";
     |SI Haybasica = 1;
         nDepar  = #12#30; |SI nDepar = 0;  nDepar = 1;   |FINSI;
         nDdepar = #0#9;
         nHdepar = #0#10;
         |SI nDepar < nDdepar; |O nDepar > nHdepar; |ACABA; |FINSI;
     |SINO;
         |SI #12#30 < #0#9;  |O #12#30 > #0#10;  |ACABA;  |FINSI;
     |FINSI;
 |FINSI;

 |SI #0#11 = "N";
     nSwSel = 0;
     |PARA nCampo = 14; |SI nCampo [ 25; |HACIENDO nCampo = nCampo + 1;
           |SI #0nCampo ! 0;
               |SI #0nCampo = #12#3;
                   nSwSel = 1;
                   |SAL;
               |FINSI;
           |FINSI;
     |SIGUE;
     |SI nSwSel = 0; |ACABA; |FINSI;
 |FINSI;

|| ... Graba Resumen  por Conceptos

  |DDEFECTO #41;
  aExiste = "N";
  nLib_am = #camaniva#0;
  aSer_am = #camaniva#2;
  nFra_am = #camaniva#3;
  |SUB_EJECUTA_NP "camocab5";
  |SI enSwEncuentro = 1;
      |ABRE #41;
      #41#0 = enEmpresa;
      #41#2 = aElemento;
      #41#3 = nNumero;
      |LEE 001#41=;
      |SI FSalida = 0; aExiste = "S"; |FINSI;
      |CIERRA #41;
   |FINSI;
   |HAZ GrabaRegistro;
|FPRC;

|DEFBUCLE LinLibro;
 #12#1;
 3,17;
 #11#0, #11#1, 01, #0#12, "S";
 #11#0, #11#1, 99, #0#13, "S";
 ;
 NULL, LinLibro0;
|FIN;

|PROCESO Facturas;
 |SI aMulDep ! "S";
     |SI Haybasica = 1;
         nDepar  = #11#10; |SI nDepar = 0;  nDepar = 1;   |FINSI;
         nDdepar = #0#9;
         nHdepar = #0#10;
         |SI nDepar < nDdepar; |O nDepar > nHdepar; |ACABA; |FINSI;
     |SINO;
         |SI #11#10 < #0#9;  |O #11#10 > #0#10;  |ACABA;  |FINSI;
     |FINSI;
 |FINSI;

 |BUCLE LinLibro;
|FPRC;

|DEFBUCLE Facturas;
 #11#3;
 4, 36;
 #0#0, #0#7, #0#2, #0#4, aDTipo;
 #0#1, #0#8, #0#3, #0#5, aHTipo;
 ;
 NULL, Facturas;
|FIN;

|| ******************************************************
|PROCESO AuxMin;
 #900#0 = 01;
 #900#1 = 0000000001;
 #900#2 = 01;
|FPRC;

|PROCESO AuxMax;
 #900#0 = 99;
 #900#1 = 9999999999;
 #900#2 = 99;
|FPRC;

|CALCULO HazTodo;

   aFichero = "x" + Usuario;

   |NOME_DAT #900 aFichero;
   |NOME_DAT #999 aFichero;
   |ABRE #900; |CIERRA #900; |DELINDEX #900;

   |ABRE #200;
   |DDEFECTO #200;
   |LEE 000#200p;
   |CIERRA #200;
   aMulDep = #200#132;

   |SI Haybasica = 0;  || si selecciona todos los departamentos no es necesario ver lineas
       |SI #0#9 = "  "; |Y #0#10 = "zz"; aMulDep = "N"; |FINSI;
   |SINO;
       |SI #0#9 [ 1;    |Y #0#10 = 99;   aMulDep = "N"; |FINSI;
   |FINSI;

   nSwAlgun = 0;
   aDTipo = "S";
   aHTipo = "S";
   |ABRE #900;
   |BUCLE Facturas;
   |CIERRA #900;

   |SI nSwAlgun = 1;
       |PINPA #0#900;
       |ENTLINEAL #1#900, -3, 4, 22, 0, AuxMin, AuxMax;
    |SINO;
       |MENAV "    No existe Facturas dentro de la seleccion informada";
    |FINSI;

   |DELINDEX #900;
|FCAL;

|CALCULO impre; |TIPO 3;

    |DBASE aMas;
    aDef = aMas + "def/caw00037";
    |CARGA_DEF aDef, espejo@;
    |SI FSalida ! 0;
        |MENAV "    Error cargando def caw00037";
        |ACABA;
    |FINSI;

   |HAZ DirAplicSt;
   |SI Haybasica = 1; |Y #0#10 = "zz"; #0#10 = "99"; |FINSI;

   |SI #0#11 = "N";
       #0#12 = 001;
       #0#13 = 999;
   |FINSI;

||   |SI aParam = "";
||       |SI #48#27 = "S";
||           |HAZ MultiEmpresa;
||       |SINO;
           |DFICE dirfich0; |QBF dirfich0;
           |HAZ HazTodo;
||       |FINSI;
||   |SINO;
||       |HAZ HazTodo;
||   |FINSI;

     |DESCARGA_DEF #999;
|FCAL;
