|INCLUYE dscont_i;

|FICHEROS;
   caportad #0;
   canempre #30;
|FIN;

|VARIABLES;
  directo = "";
  direct1 = "";
  esalta = 0;
  traba = "";
     letra = "";
     alfa1 = "";
     alfa2 = "";
     final = "";
     x = "";
     nume1 = 0;
     nume2 = 0;
     lugar = 0;
     nifnum = 0;
     resto = 0;

  pos1 = "";
  pos2 = "";
  aAntes = "";

  &eaVarDni  = "";
  &enCalcCif = 0;
|FIN;

|INCLUYE datpor_i;

|| .... Calculos desde los Campos

|CALCULO antesque;  |TIPO 7;
  |ABRE #canempre;
  #canempre#2 = cod1;
  #canempre#3 = cod2;
  |LEE 040#canempre.=;
  #caportad#0 = #canempre#2;
  #caportad#2 = #canempre#1;
  |PINTA #caportad#0;
  |PINTA #caportad#2;
  |CIERRA #canempre;
|FCAL;

|CALCULO despuesde; |TIPO 0;    ||Alta datos portadas
  esalta = (FEntrada / 100) ! 0;
  |SI esalta ! 1;  |ACABA;  |FINSI;

  |SI HayAcceso = 0; |Y Haybasica = 0; ||No existe ni acceso ni basica
      |HAZ desdecontab;                ||Lee de Canempre
  |SINO;
      enCodCli = #caportad#0;
      |HAZ LeePersona;
      |SI ewClien = 0;
          |HAZ desdebasica;
      |SINO;
          |HAZ desdecontab;
      |FINSI;
      |HAZ AltaNif;
  |FINSI;
  |PINDA #0#caportad;
|FCAL;

|CALCULO def_any;  |TIPO 7;
  #caportad#1 = #canempre#26;
  |PINTA #caportad#1;
|FCAL;

|CALCULO letradni;  |TIPO 6;
   |SI FSalida = 10;
      enCalcCif = 1;
      eaVarDni  = #caportad#Campo#;
      |HAZ CalculoDNI;
      #caportad#Campo# = eaVarDni;
      |PINTA #caportad#Campo#;
      |ERROR;
      |ACABA;
   |FINSI;

     eaVarDni  = #0Campo;
     enCalcCif = 0;
     |HAZ CalculoDNI;
     eaVarDni = (eaVarDni + "             ") % 112;
     |SI #0Campo ! eaVarDni;
         |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
         |SI FSalida = 0;
             #0Campo = eaVarDni;  |PINTA #0Campo;
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;
|FCAL;

|CALCULO ApoNifs; |TIPO 7;
 aAntes = #caportad#Campo#;
|FCAL;

|CALCULO PoNifs; |TIPO 0;
  |SI Campo = 13; |Y aAntes = #caportad#Campo#; |ACABA; |FINSI;

  traba = #caportad#Campo#;
  |QBF traba;
  |SI traba = "";  |ACABA;  |FINSI;

  |SI Haybasica = 0; |Y HayAcceso = 0; |ACABA; |FINSI;

  eaCodNif = #caportad#Campo#;
  |HAZ LeeNifs;
  |SI ewNif = 0;
      |HAZ igualdad;
  |FINSI;
|FCAL;

|PROCESO igualdad;
  |SI Campo = 13;
      #caportad#2 = #709#1;   || Apellidos y Nombre
      #caportad#4 = #709#2;   || Domicilio
      #caportad#5 = #709#3;   || Numero
      #caportad#9 =  #709#4;  || Cod. Provincia
      #caportad#10 = #709#5;  || Cod. Poblacion
      #caportad#11 = #709#6;  || Poblacion
      #caportad#12 = #709#7;  || Provincia
      #caportad#14 = #709#8;  || Telefono
      #caportad#3  = #709#9;  || Sg
      #caportad#6  = #709#16; || Piso
      #caportad#7  = #709#10; || Escalera
      #caportad#8  = #709#11; || Puerta
      #caportad#103 = #709#12;  || Codigo Pais
      #caportad#104 = #709#13;  || Nombre Pais
      #caportad#101 = #709#14;  || Codigo Comunidad
      #caportad#102 = #709#15;  || Nombre Comunidad
      |PINDA #0#caportad;
  |FINSI;

  |SI Campo = 38;
      #caportad#39 = #709#1;  || Apellidos y Nombre
      #caportad#40 = #709#9;  || Sg
      #caportad#41 = #709#2;  || Domicilio
      #caportad#42 = #709#3;  || Numero
      #caportad#43 = #709#16; || Escalera
      #caportad#44 = #709#10; || Piso
      #caportad#45 = #709#11; || Puerta
      #caportad#46 = #709#8;  || Telefono
      #caportad#47 = #709#4;  || Cod. Provincia
      #caportad#48 = #709#5;  || Cod. Poblacion
      #caportad#49 = #709#6;  || Poblacion
      #caportad#50 = #709#7;  || Provincia
      |PINDA #1#caportad;
  |FINSI;

  |SI Campo = 53; |O Campo = 58; |O Campo = 63;
      nume1 = Campo - 2;
      nume2 = Campo - 1;
      #caportad#nume1# = #709#1 % 0120;  || Apellidos y Nombre 1
      #caportad#nume2# = #709#1 % 2120;  || Apellidos y Nombre 2
      |PINTA #caportad#nume1#;
      |PINTA #caportad#nume2#;
  |FINSI;
|FPRC;

|CALCULO PoLocalidad; |TIPO 0;
 nume1 = Campo - 1;
 enCodPobl = #caportad#Campo#;
 enCodProv = #caportad#nume1#;
 |HAZ LeeLocalidad;
 |SI nume1 = 9;
     #caportad#12 = eaProvincia; |PINTA #caportad#12;
     #caportad#11 = eaPoblacion; |PINTA #caportad#11;
 |SINO;
     #caportad#50 = eaProvincia; |PINTA #caportad#50;
     #caportad#49 = eaPoblacion; |PINTA #caportad#49;
 |FINSI;
 |SI nume1 = 9; |Y HayAcceso = 1;
     #caportad#101 = #poblaci@#4; |PINTA #caportad#101;
     #caportad#102 = #poblaci@#5; |PINTA #caportad#102;
     #caportad#103 = #poblaci@#6; |PINTA #caportad#103;
     #caportad#104 = #poblaci@#7; |PINTA #caportad#104;
 |FINSI;
|FCAL;

|CALCULO PoHacienda; |TIPO 0;
  |SI HayAcceso = 0; |Y Haybasica = 0; |ACABA; |FINSI;

  enCodProv = #caportad#16;
  enCodPobl = #caportad#17;
  |HAZ LeeHacienda;
  #caportad#18 = #haciend@#2; |PINTA #caportad#18;
  #caportad#19 = #haciend@#3; |PINTA #caportad#19;
|FCAL;

|CALCULO PoComunidad; |TIPO 0;
  |SI HayAcceso = 0; |ACABA; |FINSI;

  enCodComu = #caportad#101;
  |HAZ LeeComunidad;
  #caportad#102 = #721#1; |PINTA #caportad#102;
|FCAL;

|CALCULO PoPais; |TIPO 0;
  |SI HayAcceso = 0; |Y Haybasica = 0; |ACABA; |FINSI;

  eaCodPais = #caportad#103;
  |HAZ LeePais;
  #caportad#104 = #sgpaises@#1; |PINTA #caportad#104;
|FCAL;

|CALCULO creaempre; |TIPO 0;
  pos1 = #caportad#9;
  pos2 = "000" + pos1;
  traba = pos2 % -102;
  pos1 = #caportad#10;
  pos2 = "0000" + pos1;
  pos1 = pos2 % -103;
  #caportad#78 = traba + pos1;
|FCAL;

|CALCULO creahacie; |TIPO 0;
  pos1 = #caportad#16;
  pos2 = "000" + pos1;
  traba = pos2 % -102;
  pos1 = #caportad#17;
  pos2 = "0000" + pos1;
  pos1 = pos2 % -103;
  #caportad#79 = traba + pos1;
|FCAL;

|CALCULO crearepre; |TIPO 0;
  pos1 = #caportad#47;
  pos2 = "000" + pos1;
  traba = pos2 % -102;
  pos1 = #caportad#48;
  pos2 = "0000" + pos1;
  pos1 = pos2 % -103;
  #caportad#80 = traba + pos1;
|FCAL;

|CALCULO calcu100; |TIPO 0;
  |SI #caportad#72 = 100;
      #caportad#74 = 0;
      #caportad#75 = 0;
      #caportad#76 = 0;
      #caportad#77 = 0;
      |PINTA #caportad#74;
      |PINTA #caportad#75;
      |PINTA #caportad#76;
      |PINTA #caportad#77;
      |CAMPO_MODIFICA #caportad#74, 1, "N";
      |CAMPO_MODIFICA #caportad#75, 1, "N";
      |CAMPO_MODIFICA #caportad#76, 1, "N";
      |CAMPO_MODIFICA #caportad#77, 1, "N";
  |SINO;
      |CAMPO_MODIFICA #caportad#74, 1, "S";
      |CAMPO_MODIFICA #caportad#75, 1, "S";
      |CAMPO_MODIFICA #caportad#76, 1, "S";
      |CAMPO_MODIFICA #caportad#77, 1, "S";
  |FINSI;
|FCAL;

|CALCULO cont_por; |TIPO 0;
  nume1 = #caportad#72 + #caportad#74 + #caportad#75 + #caportad#76 + #caportad#77;
  |SI nume1 > 100;
      #caportad#Campo# = 0;
      |MENAV "     ERROR! La suma de porcentajes superior a 100 %";
      |ERROR;
  |FINSI;
|FCAL;

|CALCULO hasta100; |TIPO 0;
  nume1 = #caportad#72 + #caportad#74 + #caportad#75 + #caportad#76 + #caportad#77;
  |SI nume1 < 100;
      #caportad#72 = 100 - #caportad#74 - #caportad#75 - #caportad#76 - #caportad#77;
      |PINTA #caportad#72;
      |MENAV "     ATENCION! Ajustado A 100% porcentaje comun";
  |FINSI;
|FCAL;
