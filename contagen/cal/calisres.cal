|FICHEROS;
   calisres #0;    || def entrada para informe
   caexistl #1;    || lineas de existencias
   cacuenta #2;    || Plan contable        --> #3
   caplctac #3;    || cabecera Planilla
   caplctal #4;    || Lineas Planilla      --> #7
   catralre #5;    || Trabajo
   caconlib #6;
   caportad #8;    || Portadas

   canempre #30;
   caselem1 #998;
|FIN;

|VARIABLES;
   sw_error = 0;
   aFichero = "";
   sw_def = 0;
   sw_exi = 0;
   puntos = ".........................";
   men1   = "    NO existe Informe ";
   men2   = "    NO existe el Codigo de Columnas de Libros ";
   alfa1  = "";
   alfa2  = "";
   alfa3  = "";
   alfa4  = "";
   xx = 0;
   yy = 0;
   lin = 0;
   &r_emp = 0;
   &r_per = 0;
   &r_eje = 0;
   &estru = 0;
   &d_mes = 0;
   &h_mes = 0;
   &r_nom = "";

   a_plan = 0;
   a_tipo = "";
   a_nume = 0;
   a_desc = "";
   a_imp1 = 0;
   a_imp2 = 0;
   a_imp3 = 0;
   a_imp4 = 0;
   a_imp5 = 0;

   exini1 = 0;
   exini2 = 0;
   exini3 = 0;
   exini4 = 0;
   exfin1 = 0;
   exfin2 = 0;
   exfin3 = 0;
   exfin4 = 0;

   informe = "calisres";

   y = "";
   x = 0;
   uno = "";
   dos = "";
   scta = 0;
   cc3 = 0;
   cc4 = 0;
   cc5 = 0;
   signo = "";
   sw = 0;
   tt1 = 0;
   tt2 = 0;
   tt3 = 0;
   tt4 = 0;
   tt5 = 0;
   ingt1 = 0;
   ingt2 = 0;
   ingt3 = 0;
   ingt4 = 0;
   ingt5 = 0;
   &swlinea = 0;
   &tot1 = 0;
   &tot2 = 0;
   &tot3 = 0;
   &tot4 = 0;
   &tot5 = 0;
   &subt = "";
   {-1}menu  = "";           || Variables menu impresora o pantalla
   menu1 = "2400";
   menu2 = "1";
   menu3 = " Elija opcion: ";
   menu4 = "PI";
   menu5 = " Pantalla ";
   menu6 = " Impresora ";

   Comodin  = "";
   Comodin1 = "";

   Empresa = 0;
   Periodo = 0;
|FIN;


|INCLUYE dscont_i;

***************************************************************************
CAMPO DE PANTALLA
***************************************************************************
|PROCESO inicio_y; |TIPO 8;
  |HAZ inicial;
  |SI swerror = 2; |PTEC 807; |ACABA; |FINSI;
  |SI swerror = 3; |PTEC 831; |PTEC 807; |ACABA; |FINSI;
|FPRC;

|PROCESO defectos; |TIPO 7;
   |SI sw_def = 1; |ACABA; |FINSI;
   |DDEFECTO #3;
   |ABRE #3;
   |LEE 000#3p;
   |SI FSalida = 0;
      #calisres#0 = #caplctac#0;
      |PINTA #calisres#0;
   |FINSI;
   |CIERRA #3;

   |DDEFECTO #8;
   |ABRE #8;
   #8#0 = #canempre#2;
   #8#1 = #canempre#26;
   |LEE 000#8=;
   |SI FSalida = 0;
       |SI #8#83 = "N";
           |CAMPO_MODIFICA #calisres#1, 1, "S";
           #calisres#1 = #8#88;
           |PINTA #calisres#1;
      |SINO;
           |CAMPO_MODIFICA #calisres#1, 1, "N";
      |FINSI;
      #calisres#4 = #8#86;
      |PINTA #calisres#4;
   |FINSI;
   |CIERRA #8;
   sw_def = 1;
|FPRC;

|PROCESO leeconlib; |TIPO 0;
   |SI FSalida = 2; |ACABA; |FINSI;
   |SI #calisres#Campo# = 0; |ERROR; |ACABA; |FINSI;
   |ABRE #6;
   #caconlib#0 = #calisres#Campo#;
   |LEE 001#6=;
   salidas = FSalida;
   |CIERRA #6;
   |SI salidas ! 0;
      |MENAV men2;
      |ERROR;
   |SINO;
      #calisres#5  =  #caconlib#1  + #caconlib#2;  #calisres#6  =  #caconlib#3  + #caconlib#4;
      #calisres#7  =  #caconlib#5  + #caconlib#6;  #calisres#8  =  #caconlib#7  + #caconlib#8;
      #calisres#9  =  #caconlib#9  + #caconlib#10; #calisres#10 =  #caconlib#11 + #caconlib#12;
      #calisres#11 =  #caconlib#13 + #caconlib#14; #calisres#12 =  #caconlib#15 + #caconlib#16;
      #calisres#13 =  #caconlib#17 + #caconlib#18; #calisres#14 =  #caconlib#19 + #caconlib#20;
      #calisres#15 =  #caconlib#21 + #caconlib#22; #calisres#16 =  #caconlib#23 + #caconlib#24;
      #calisres#17 =  #caconlib#25 + #caconlib#26; #calisres#18 =  #caconlib#27 + #caconlib#28;
      #calisres#19 =  #caconlib#29 + #caconlib#30; #calisres#20 =  #caconlib#31 + #caconlib#32;
      #calisres#21 =  #caconlib#33 + #caconlib#34; #calisres#22 =  #caconlib#35 + #caconlib#36;
      |PARA xx = 5; |SI xx [ 22; |HACIENDO xx = xx + 1;
         alfa1 = #calisres#xx#; |QBF alfa1;
         alfa1 = alfa1 + puntos;
         alfa1 = alfa1 % 125;
         #calisres#xx#  = alfa1;
         |PINTA #calisres#xx#;
      |SIGUE;
      |ATRI I;
      |PINTA 1469, #caconlib#55; |PINTA 1569, #caconlib#56;
      |PINTA 1669, #caconlib#57; |PINTA 1769, #caconlib#58;
      |PINTA 1869, #caconlib#59; |PINTA 1969, #caconlib#60;
      |PINTA 2069, #caconlib#61; |PINTA 2169, #caconlib#62;
      |PINTA 2269, #caconlib#63;
      |ATRI 0;
   |FINSI;
|FPRC;

|PROCESO existenc; |TIPO 0;
   |SI FSalida ! 9; |ACABA; |FINSI;
   |SI #8#83 ! "N";
      || ... |MENAV "    La Actividad de esta Empresa es Agricola ";
      |ACABA;
   |FINSI;
   |PARA x = 1; |SI x [ 4; |HACIENDO x = x + 1;
      d_mes = 0 + (x - 1) * 3;
      h_mes = 3 + (x - 1) * 3;
      estru = #calisres#1;
      r_emp = #canempre#2;
      r_per = #canempre#3;
      r_eje = #canempre#26;
      r_nom = #canempre#1;
      |QBF r_nom;
      r_nom = r_nom % A120;
      y = x;
      r_nom = r_nom + " - " + y + "§ trimestre";
      enTExi = 1;
      |SUB_EJECUTA "caexicon.run";
   |SIGUE;
|FPRC;

***************************************************************************

|PROCESO graba_reg;
   #catralre#0 = a_plan;
   #catralre#1 = a_tipo;
   #catralre#2 = a_nume;
   |LEE 101#5=;
   salidas = FSalida;
   |SI salidas ! 0;
      |DDEFECTO #5;
      #catralre#0 = a_plan;
      #catralre#1 = a_tipo;
      #catralre#2 = a_nume;
   |FINSI;
   #catralre#3 = a_desc;
   #catralre#4 = #catralre#4 + a_imp1;
   #catralre#5 = #catralre#5 + a_imp2;
   #catralre#6 = #catralre#6 + a_imp3;
   #catralre#7 = #catralre#7 + a_imp4;
   #catralre#8 = #catralre#8 + a_imp5;
   |SI salidas = 0; |GRABA 020#5a; |FINSI;
   |SI salidas ! 0; |GRABA 020#5n; |FINSI;
   |LIBERA #5;
|FPRC;

|| ===== Calculo de Existencias

|PROCESO sumaex;
   sw_exi = 1;
   exini1 = exini1 + #caexistl#11; exfin1 = exfin1 + #caexistl#14;
   exini2 = exini2 + #caexistl#14; exfin2 = exfin2 + #caexistl#17;
   exini3 = exini3 + #caexistl#17; exfin3 = exfin3 + #caexistl#20;
   exini4 = exini4 + #caexistl#20; exfin4 = exfin4 + #caexistl#23;
|FPRC;

|DEFBUCLE exist;
   #1#1;
   ;
   #0#1, 01;
   #0#1, 99;
   e;
   NULL, sumaex;
|FIN;

|PROCESO lee_exist;
   sw_exi = 0;
   exini1 = 0; exfin1 = 0;
   exini2 = 0; exfin2 = 0;
   exini3 = 0; exfin3 = 0;
   exini4 = 0; exfin4 = 0;
   |BUCLE exist;
   |SI sw_exi = 1;
      a_tipo = "C";
      a_nume = 0;
      a_desc = "Existencias Iniciales ...";
      a_imp1 = exini1;
      a_imp2 = exini2;
      a_imp3 = exini3;
      a_imp4 = exini4;
      a_imp5 = exini1;
      |HAZ graba_reg;  || Cambiar existencias Finales a Consumo 02.09.1997
      a_tipo = "C";
      a_nume = 10;
      a_desc = "Existencias Finales .....";
      a_imp1 = exfin1;
      a_imp2 = exfin2;
      a_imp3 = exfin3;
      a_imp4 = exfin4;
      a_imp5 = exfin4;
      |HAZ graba_reg;
   |FINSI;
|FPRC;
|| ==================================================

|PROCESO cal_ctas2;
   |PARA xx = 1; |SI xx [ 4; |HACIENDO xx = xx + 1;
      cc3 = 12 + (xx - 1) * 9;
      cc4 = 15 + (xx - 1) * 9;
      cc5 = 18 + (xx - 1) * 9;
      |SI signo = "H";
         cc3 = cc3 + 1;
         cc4 = cc4 + 1;
         cc5 = cc5 + 1;
      |FINSI;
      |SI signo = "S";
         cc3 = cc3 + 2;
         cc4 = cc4 + 2;
         cc5 = cc5 + 2;
      |FINSI;
      scta = #cacuenta#cc3# + #cacuenta#cc4# + #cacuenta#cc5#;
      |SI #caplctal#3 = "I"; |Y signo = "S";
         scta = - scta;
      |FINSI;
      |SI xx = 1; a_imp1 = a_imp1 + scta; |FINSI;
      |SI xx = 2; a_imp2 = a_imp2 + scta; |FINSI;
      |SI xx = 3; a_imp3 = a_imp3 + scta; |FINSI;
      |SI xx = 4; a_imp4 = a_imp4 + scta; |FINSI;
   |SIGUE;
|FPRC;

|DEFBUCLE lee_ctas;
   #2#1;
   3;
   #4#2, "S";
   dos,  "S";
   e;
   NULL, cal_ctas2;
|FIN;

|PROCESO cal_ctas1;
   a_imp1 = 0;
   a_imp2 = 0;
   a_imp3 = 0;
   a_imp4 = 0;
   a_imp5 = 0;
   uno = #caplctal#2;
   |QBF uno;
   dos = uno + "999999999999";
   dos = dos % A112;
   signo = #caplctal#4;
   |SI signo = "*"; signo = #caplctal#7; |FINSI;
   |SI signo = "*"; |ACABA; |FINSI;

   |BUCLE lee_ctas;

   a_imp5 = a_imp1 + a_imp2 + a_imp3 + a_imp4;
   a_tipo = #caplctal#3;
   |SI a_tipo = "I";
      a_nume = #caplctal#6;
      x = #caplctal#6 + 4;
      a_desc = #calisres#x#;
   |FINSI;
   |SI a_tipo = "G";
      a_nume = #caplctal#6;
      x = #caplctal#6 + 13;
      a_desc = #calisres#x#;
      x = #caplctal#6 + 54;
      a_tipo = #caconlib#x#;
   |FINSI;
   |SI a_tipo = "V";
      a_nume = 0;
      a_desc = "Incremento de patrimonio.";
      |SI a_imp5 < 0;
         a_nume = 1;
         a_desc = "Disminuciones patrimonio.";
      |FINSI;
   |FINSI;
   |SI a_tipo = "R";
      a_nume = 0;
      a_desc = "Retenciones practicadas..";
   |FINSI;
   |HAZ graba_reg;
|FPRC;

|DEFBUCLE calisres0;
   #4#1;
   ;
   #0#0, 001;
   #0#0, 999;
   e;
   NULL, cal_ctas1;
|FIN;
|| **************************************************************************

|PROCESO poncero;
   sw   = 0;
   tot1 = 0;
   tot2 = 0;
   tot3 = 0;
   tot4 = 0;
   tot5 = 0;
   subt = "";
|FPRC;

|PROCESO imprimir;
   |SI #catralre#4 = 0; |Y #catralre#5 = 0; |Y #catralre#6 = 0; |Y #catralre#7 = 0;
      |ACABA;
   |FINSI;
   |SI sw = 0;
      sw = 1;
      |SI a_tipo = "I"; |O a_tipo = "C"; |O a_tipo = "G";
         swlinea = 0;
         |SI menu2 = 1; |HAZ operapanta; |FINSI;
         |SI menu2 = 2; |PRINT; |FINSI;
      |FINSI;
   |FINSI;
   swlinea = 1;
   |SI menu2 = 1; |HAZ operapanta; |FINSI;
   |SI menu2 = 2; |PRINT; |FINSI;
   sw_def = 1;
   |SI #catralre#1 = "C"; |Y #catralre#2 = 10; || Si es existencias Finales Resta el total.
      tot1 = tot1 - #catralre#4;
      tot2 = tot2 - #catralre#5;
      tot3 = tot3 - #catralre#6;
      tot4 = tot4 - #catralre#7;
      tot5 = tot5 - #catralre#8;
   |SINO;
      tot1 = tot1 + #catralre#4;
      tot2 = tot2 + #catralre#5;
      tot3 = tot3 + #catralre#6;
      tot4 = tot4 + #catralre#7;
      tot5 = tot5 + #catralre#8;
   |FINSI;
|FPRC;

|DEFBUCLE calisres2;
   #5#1;
   ;
   #0#0, a_tipo, 00;
   #0#0, a_tipo, 99;
   e;
   NULL, imprimir;
|FIN;

|PROCESO calisres1;
|| ... Ingresos
   |HAZ poncero;
   subt = " INGRESOS ";
   a_tipo = "I";
   |BUCLE calisres2;
   |SI sw = 1;
      subt = "TOTAL INGRESOS .....................";
      swlinea = 2;
      |SI menu2 = 1; |HAZ operapanta; |FINSI;
      |SI menu2 = 2; |PRINT; |FINSI;
      tt1 = tt1 + tot1; ingt1 = tt1;
      tt2 = tt2 + tot2; ingt2 = tt2;
      tt3 = tt3 + tot3; ingt3 = tt3;
      tt4 = tt4 + tot4; ingt4 = tt4;
      tt5 = tt5 + tot5; ingt5 = tt5;
   |FINSI;
   || ... CONSUMO
   |HAZ poncero;
   subt = " CONSUMO ";
   a_tipo = "C";
   |BUCLE calisres2;
   |SI sw = 1;
      subt = "TOTAL CONSUMO ......................";
      swlinea = 2;
      |SI menu2 = 1; |HAZ operapanta; |FINSI;
      |SI menu2 = 2; |PRINT; |FINSI;
      tt1 = tt1 - tot1;
      tt2 = tt2 - tot2;
      tt3 = tt3 - tot3;
      tt4 = tt4 - tot4;
      tt5 = tt5 - tot5;
   |FINSI;
   || ... Gastos
   |HAZ poncero;
   subt = " GASTOS ";
   a_tipo = "G";
   |BUCLE calisres2;
   |SI sw = 1;
      subt = "TOTAL GASTOS .......................";
      swlinea = 2;
      |SI menu2 = 1; |HAZ operapanta; |FINSI;
      |SI menu2 = 2; |PRINT; |FINSI;
      tt1 = tt1 - tot1;
      tt2 = tt2 - tot2;
      tt3 = tt3 - tot3;
      tt4 = tt4 - tot4;
      tt5 = tt5 - tot5;
   |FINSI;
   || ... Totales A;
   |SI #calisres#4 ! 0;
      subt = "DIFERENCIA .........................";
      swlinea = 3;
      tot1 = tt1;
      tot2 = tt2;
      tot3 = tt3;
      tot4 = tt4;
      tot5 = tt5;
      |SI menu2 = 1; |HAZ operapanta; |FINSI;
      |SI menu2 = 2; |PRINT; |FINSI;
      tot1 = (tt1 % #calisres#4) ! 0;
      tot2 = (tt2 % #calisres#4) ! 0;
      tot3 = (tt3 % #calisres#4) ! 0;
      tot4 = (tt4 % #calisres#4) ! 0;
      tot5 = (tt5 % #calisres#4) ! 0;
      subt = "COEFICIENTE GASTOS " + #calisres#4 + " %.................";
      swlinea = 3;
      |SI menu2 = 1; |HAZ operapanta; |FINSI;
      |SI menu2 = 2; |PRINT; |FINSI;
      tt1 = tt1 - tot1;
      tt2 = tt2 - tot2;
      tt3 = tt3 - tot3;
      tt4 = tt4 - tot4;
      tt5 = tt5 - tot5;
   |FINSI;
   subt = "RENDIMIENTO NETO ORDINARIO .........";
   swlinea = 4;
   tot1 = tt1;
   tot2 = tt2;
   tot3 = tt3;
   tot4 = tt4;
   tot5 = tt5;
   |SI menu2 = 1; |HAZ operapanta; |FINSI;
   |SI menu2 = 2; |PRINT; |FINSI;
   || ... Inversion
   |HAZ poncero;
   a_tipo = "V";
   |BUCLE calisres2;
   |SI sw = 1;
      tt1 = tt1 + tot1;
      tt2 = tt2 + tot2;
      tt3 = tt3 + tot3;
      tt4 = tt4 + tot4;
      tt5 = tt5 + tot5;
      || ... Totales A;
      subt = "RENDIMIENTO NETO ...................";
      swlinea = 4;
      tot1 = tt1;
      tot2 = tt2;
      tot3 = tt3;
      tot4 = tt4;
      tot5 = tt5;
      |SI menu2 = 1; |HAZ operapanta; |FINSI;
      |SI menu2 = 2; |PRINT; |FINSI;
   |FINSI;
   || ... Retenciones
   |HAZ poncero;
   a_tipo = "R";
   |BUCLE calisres2;
|FPRC;

|| **************************************************************************
|PROCESO HazTodo;
  a_plan = #calisres#0;

  |ABRE #5;
  |HAZ lee_exist;

  |ABRET #2;
  |ABRET #4;
  |BUCLE calisres0;
  |CIERRAT #2;
  |CIERRAT #4;
  |CIERRA #5;
  |SI menu2 = 1; |HAZ panta; |FINSI;  || Por pantalla
  |SI menu2 = 2; |HAZ impre; |FINSI;  || Por impresora
  |DELINDEX #5;
|FPRC;

|PROCESO resumen; |TIPO 3;

   |MENU menu;
   |SI FSalida [ 0; |ACABA; |FINSI;

   menu2 = FSalida;
   |SI menu2 = 2;
       |IMPRE 0;
       |SI FSalida ! 0; |ACABA; |FINSI;
       |INFOR informe;
       |SI FSalida ! 0;
           |MENAV men1;
           |FINIMP;
           |ACABA;
       |FINSI;
  |FINSI;

  aFichero = ("b" + Usuario) % 108;
  |NOME_DAT #5 aFichero;

  |SI #48#27 = "S";
      |HAZ MultiEmpresa;
  |SINO;
      |DFICE dirfich0;
      |HAZ HazTodo;
  |FINSI;
  |SI menu2 = 2;
     |FININF;
     |FINIMP;
  |FINSI;
  |DELINDEX #5;
|FPRC;

|PROCESO impre;
   sw = 0;
   sw_def = 0;
   |HAZ calisres1;
   |SI sw_def = 1;
      |PIEINF;
   |FINSI;
|FPRC;

|PROCESO panta;

   alfa4  = &45 * 23 + &179 + &45 * 10 + &179 + &45 * 10 + &179 + &45 * 10;
   alfa4  = alfa4 + &179 + &45 * 10 + &179 + &45 * 12;lin = 100;

   |PINPA #0#5;       || Pinta la pantalla del informe
   |PINTA 0511, #canempre#1;
   |PINTA 0559, #canempre#26;
   |PINTA 0570, #calisres#2;
   sw = 0;
   sw_def = 0;
   |HAZ calisres1;
   |SI sw_def = 1;
      |PAUSA;
   |FINSI;
|FPRC;

|| **************************************************************************

|PROCESO operapanta;   || Operaciones por pantalla;
   |SI lin > 1400;
      |HAZ cambiapag;
      |SI sw_error = 1; |ACABA; |FINSI;
   |FINSI;
   |SI swlinea = 0; |O swlinea = 4;
      |SI lin > 1300;
         |HAZ cambiapag;
         |SI sw_error = 1; |ACABA; |FINSI;
      |FINSI;
   |FINSI;
   |SI swlinea = 0;
      yy = lin + 801
      |ATRI R;
      |PINTA yy, subt;
      |ATRI 0;
      lin = lin + 100;
   |FINSI;
   |SI swlinea = 1;
      Pos = lin;
      |PINDA #0#5;         || Pinta las lineas
      Pos = -1;
      lin = lin + 100;     || Posicionar una linea mas abajo
   |FINSI;
   |SI swlinea = 4;
      yy = lin + 801; |PINTA yy, alfa4;
      lin = lin + 100;
   |FINSI;
   |SI swlinea ] 2;
      subt = subt % 123;
      #catralre#4 = tot1; #catralre#5 = tot2;
      #catralre#6 = tot3; #catralre#7 = tot4;
      #catralre#8 = tot5;
      |ATRI N;
      yy = lin + 801; |PINTA yy, subt;
      yy = lin + 825; |PINTA yy, #catralre#4;
      yy = lin + 836; |PINTA yy, #catralre#5;
      yy = lin + 847; |PINTA yy, #catralre#6;
      yy = lin + 858; |PINTA yy, #catralre#7;
      yy = lin + 869; |PINTA yy, #catralre#8;
      |ATRI 0;
      lin = lin + 200;     || Posicionar una linea mas abajo
   |FINSI;
|FPRC;

|PROCESO cambiapag;
   sw_error = 0;
   lin = 100;
   |PAUSA;
   |SI 1 = FSalida;|O 7 = FSalida;
      |ERROR10;    || Rompe el bucle y acaba
      sw_error = 1;
   |SINO;
      |PINPA #0#5;
      |PINTA 0511, #canempre#1;
      |PINTA 0559, #canempre#26;
      |PINTA 0570, #calisres#2;
      FSalida = 0;
   |FINSI;
|FPRC;


|| ********************************************************************
|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #2 dirfich0;
   |PATH_DAT #3 dirfich0;
   |PATH_DAT #4 dirfich0;
   |PATH_DAT #6 dirfich0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 001;
 999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;
