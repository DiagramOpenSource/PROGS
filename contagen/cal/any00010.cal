|FICHEROS;
   any00010 #0;

   caivalin #1;
   camaniva #2;
   calibros #3;

   camaasil #4;
   caconimp #8;  || configuracion impresiones
   caivaasi #200;

   anm00001 #11;
   anm00003 #7;     || Grupos Analiticas
   anm00013 #13;    || Grupos Analiticas de Apuntes
   anm0003@ #107;   || Espejo grupos analitica
   anm0001@ #911;   || Espejo cuentas Analiticas

   :/modelos/def/dsmom030 #300;
   :/modelos/def/dsmom031 #331;

   canempre #30;
   caselem1 #998;

   anw00015 #915;
   caw00031 #901;
   caw00045 #903;

   cawdatos #900;
   &regisnif@ #709;
|FIN;

|VARIABLES;
   &entrada = 1;
   nEstado  = 0;
   informe  = "";

   inform1   = "cay3ivas";
   inform2   = "cay3ivan";
   inform3   = "cay3ivay";
   inform4   = "cay3ivax";

   &enSwPAna  = 0;
   &aCtaAna   = "";
   &aTitAna   = "";
   aCta915  = "";
   aCta2Gru  = "";
   edn_error2 = 0;

   aFichero = "";
   aParam  = "";
   aClave  = "";

   aAlfa0 = "";
   aAlfa1 = "";
   aAlfa2 = "";
   aAlfa3 = "";
   nNume1 = 0;
   nNume2 = 0;
   nNume3 = 0;
   nNume4 = 0;
   nPara1 = 0;
   nPara2 = 0;
   nPara3 = 0;

   aDTipo = "";
   aHTipo = "";

   fFecha     = @;
   fLaFecha   = @;
   nGraba     = 0;
   nLineaBase = 0;
   nLinea915  = 0;
   nDocumento = 0;
   nBase      = 0;
   nCuota     = 0;
   nRecargo   = 0;
   nBaseI     = 0;
   nCuotaI    = 0;
   nSwReten   = 0;
   nSwRecargo = 0;
   nTanto     = 0;
   nTanto1    = 0;
   nOpera     = 0;

   nOpMdep      = 0;
   nAlSi        = 0;
   nAlNo        = 0;
   nAlConcepto  = 0;

   swsel        = 0;
   nSwConcep    = 0;
   nSwGApte     = 0;

   aMas         = "";
   aPath        = "";

   aMulDep      = 0;
   nDdepar      = 0;
   nHdepar      = 0;
   fDFecha      = @;
   fHFecha      = @;
   nDLiqui      = 0;
   nHLiqui      = 0;

   &SwPrim      = 0;
   SwHayDatos   = 0;
   &nFactura    = 0;
   &nPagina     = 0;
   &SwLinea     = 0;

   &nLBase   = 0;
   &nLTipoI  = 0;
   &nLCuoI   = 0;
   &nLTipoR  = 0;
   &nLCuoR   = 0;

   &nLBasP   = 0;
   &nLTipP   = 0;
   &nLCuoP   = 0;

   &nPagDup   = 0;
   &nSwPagDup = 0;
   &efFechaEmi = @;

   &TotalBases  = 0;
   &TotalIvas   = 0;
   &TotalRecs   = 0;
   &TotalCuotas = 0;

&eaImpresora = "";
&eaPortada   = "";
&eaTituloL   = "";
&aDesTip     = "";
&eaClPro     = "";
&enSwSelCp   = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoan_i;
|INCLUYE caliva_i;

|| //////////////////// Calculos de Pantalla \\\\\\\\\\\\\\\\\\\\\\\\\\
|CALCULO Eano; |TIPO 7;
  #0#1 = enEjercicio; |PINTA #0#1;
  |SI #0#0 = "R";
      #0#13 = "LIBRO REGISTRO DE FACTURAS EMITIDAS ";
  |SINO;
      #0#13 = "LIBRO REGISTRO DE FACTURAS RECIBIDAS";
      |ATRI R; |PINTA 0538, "Recibidas"; |ATRI 0;
  |FINSI;

  |PINTA #0#13;
|FCAL;

|CALCULO losDefectos; |TIPO 0;
  |SI nEstado ! 0;
      #0#6 = fec1;
      |SI enSwSelFc = 1; #0#6 = "01.01.2000"; |FINSI;
      |PINTA #0#6;
      #0#7 = fec2; |PINTA #0#7;
  |FINSI;
|FCAL;

|CALCULO Cuadro; |TIPO 7;
   |PUSHV  0612 1770;
   |CUADRO 0612 1770;
   |ATRI R;
   |PINTA 0613, " ------------ FORMATO LIBRO FACTURAS ------------------- ";
   |PINTA 0713, " ( 1) Descr. Fra., Nombre y NIF, una linea               ";
   |PINTA 0813, " ( 2) Descr. Fra., Nombre y NIF, varias lineas           ";
   |PINTA 0913, " ( 3) Descr. Fra., Nombre y NIF, una linea s/recargo     ";
   |PINTA 1013, " ( 4) Descr. Fra., Nombre y NIF, varias lineas s/recargo ";
   |PINTA 1113, " ( 5) Cuota total IVA y Cuota Total Recargo              ";
   |PINTA 1213, " ( 6) Cuota total IVA, s/recargo                         ";
   |PINTA 1313, " ( 7) Nombre y NIF, una linea                            ";
   |PINTA 1413, " ( 8) Nombre y NIF, varias lineas con rayas              ";
   |PINTA 1513, " ( 9) Formato Oficial en Crystal Reports                 ";
   |PINTA 1613, " (10) Formato Oficial Crystal Reports agrupando Periodos ";
   |ATRI r;
|FCAL;

|CALCULO SalCuadro; |TIPO 0;
   |POPV;
|FCAL;

|CALCULO fecha; |TIPO 0;

 fDFecha = fec1;
 |SI enSwSelFc = 1; |Y #0#9 ! 2;  fDFecha = "01.01.0000"; |FINSI;

 |SI #0Campo < fDFecha; |O #0Campo > fec2;
     |MENAV "    Error. Fecha Incorrecta ";
     |ERROR;
 |SINO;
     |SI Campo = 7; |Y #0#6 > #0#7;
         |MENAV "    Error. Fecha Incorrecta ";
         |ERROR;
     |FINSI;
 |FINSI;
|FCAL;

|CALCULO DesdePeriodo; |TIPO 0;

  |SI #0Campo > 12; |Y #0Campo ! 99;
      |ERROR;
      |ACABA;
  |FINSI;

  |SI #0#19 = 99;
      aAlfa1 = "N";
      aAlfa2 = "S";
      #0#20 = 99; |PINTA #0#20;
  |SINO;
      aAlfa1 = #0#1;
      aAlfa2 = "01.01." + aAlfa1;
      aAlfa3 = "31.12." + aAlfa1;
      #0#6 = aAlfa2; |PINTA #0#6;
      #0#7 = aAlfa3; |PINTA #0#7;
      aAlfa1 = "S";
      aAlfa2 = "N";
      |SI #0#20 = 99;
          #0#20 = 12; |PINTA #0#20;
      |FINSI;
  |FINSI;
  |C_M #0#20,1,aAlfa1;
  |C_M #0#6,1,aAlfa2;
  |C_M #0#7,1,aAlfa2;
|FCAL;

|CALCULO HastaPeriodo; |TIPO 0;
  |C_M #0#20, 0, aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

  |SI #0Campo > 12;
      |ERROR;
      |ACABA;
  |FINSI;

  |SI #0#19 > #0#20;
      |ERROR;
  |FINSI;
|FCAL;

|CALCULO nodepar; |TIPO 7;
   |C_M #0#16, 1, eaDepar;
   |C_M #0#17, 1, eaDepar;
|FCAL;

|CALCULO ElOrden;
   |SI #0#9 = 2; |Y enSwSelFc = 1;
       |SI #0#6 < fec1;
           |MENAV "    Error en las fechas de Seleccion de Fecha Contable";
           |ERROR;
       |FINSI;
   |FINSI;
|FCAL;

||///////////////////////////////////////////////////////////////////
||///////////////////////////////////////////////////////////////////
|PROCESO CtrlConcepto;
     nSwConcep = 0;
     |SI enSwSelCp = 0; |ACABA; |FINSI;

     |DBASS aPath; |QBF aPath;
     aPath = aPath + "modelos/fich/";

     |PATH_DAT #331 aPath;
     |ABRE #331;
     #331#0 = enConcepto;
     #331#1 = #0#24;
     |LEE 041#331=;
     |SI FSalida ! 0;
         nSwConcep = 1;
     |FINSI;
     |CIERRA #331;
|FPRC;

||///////////////////////////////////////////////////////////////////
|| ... Grabo datos del auxiliar

|PROCESO LasVariables;

   aCta915 = aCtaAna;  |SI #0#23 = "N"; aCta915 = ""; |FINSI;
   nLinea915  = 1;
   nLibro     = #2#0;
   nDocumento = #2#1;
   |SI nGraba = 0;   || Solo la suma anterior
       nLibro     = 0;
       nDocumento = 0;
       nLinea915  = 0;
   |FINSI;

   |SI nTanto = -100;
       nCuota     = (nBase % #1#7) ! 2;
       nRecargo   = (nBase % #1#9) ! 2;
       nBaseI     = (#1#20 % #13#8) ! 2;
       nCuotaI    = (#1#22 % #13#8) ! 2;
       |SI nTanto1 ! -1; |Y nTanto1 ! 100;
           nBase      = (nBase    % nTanto1) ! 2;
           nCuota     = (nCuota   % nTanto1) ! 2;
           nRecargo   = (nRecargo % nTanto1) ! 2;
           nBaseI     = (nBaseI   % nTanto1) ! 2;
            nCuotaI    = (nCuotaI  % nTanto1) ! 2;
       |FINSI;
   |SINO;
       |SI nTanto = 100;
           nBase      = #1#6;
           nCuota     = #1#8;
           nRecargo   = #1#10;
           nBaseI     = #1#20;
           nCuotaI    = #1#22;
       |SINO;
           nBase      = (#1#6  % nTanto) ! 2;
           nCuota     = (#1#8  % nTanto) ! 2;
           nRecargo   = (#1#10 % nTanto) ! 2;
           nBaseI     = (#1#20 % nTanto) ! 2;
           nCuotaI    = (#1#22 % nTanto) ! 2;
           |SI nTanto1 ! -1; |Y nTanto1 ! 100;
               nBase      = (nBase    % nTanto1) ! 2;
               nCuota     = (nCuota   % nTanto1) ! 2;
               nRecargo   = (nRecargo % nTanto1) ! 2;
               nBaseI     = (nBaseI   % nTanto1) ! 2;
               nCuotaI    = (nCuotaI  % nTanto1) ! 2;
           |FINSI;
       |FINSI;
   |FINSI;
|FPRC;

|PROCESO LeeCtaAna;
   |ABRE #11;
   |DDEFECTO #11;
   #11#0 = #911#0;
   |LEE 040#11=;
   #911#1 = #11#1;
   |CIERRA #11;
|FPRC;

|PROCESO Grabaw915;

   |HAZ LasVariables;

   |DDEFECTO #915;
   #915#0 = nLibro;
   #915#1 = nDocumento;
   #915#2 = aCta915;
   #915#3 = nLinea915;
   |LEE 041#915=;
   |SI FSalida ! 0;
       |DDEFECTO #915;
       #915#0 = nLibro;
       #915#1 = nDocumento;
       #915#2 = aCta915;
       #915#3 = nLinea915;

       #915#4  = #2#2;   ||Serie
       #915#5  = #2#3;   ||Fra.
       #915#6  = fLaFecha; ||Fecha Fra  / Fecha Contable
       #915#7  = #2#8;   ||Fecha Contable
       #915#8  = #2#10;  ||departamento
       #915#9  = #2#11;  ||Subdepartamento
       #915#10 = #2#12;  ||Cuenta Contable
       #915#11 = #2#13;  ||Nombre
       #915#12 = #2#14;  ||NIF
       eaCodNif = #2#14;  ||El Dni
       |HAZ LeeNifs;
       |SI #709#12 ! "ES "; #915#13 =  #709#12; |FINSI;
       #915#14 = #2#22;  ||Referencia
       #915#15 = #2#27;
       #915#16 = #2#28;
       |SI #0#18 = "S";
            aAlfa1 = #915#16; |QBF aAlfa1;
            aAlfa1 = aAlfa1 + " " + #2#22;
            #915#16 = aAlfa1;
       |FINSI;

       |SI #2#41 > "01.01.1900";
           #915#17 = #2#41;  ||Fecha Operacion
           aAlfa1  = #2#41;
           #915#57 = aAlfa1 % 105;
       |FINSI;
       #915#20 = #2#2;
       #915#21 = nFactura;
       aAlfa1  = fLaFecha
       #915#22 = aAlfa1;

       |SI #915#1 = 0; |Y #915#3 = 0;
           #915#4 = "";
           #915#5 = 0;
           #915#6 = "01.01.0000";
       |FINSI;
       |GRABA 020#915b;
   |FINSI;

   #915#15 = #1#3;  || Concepto
   #915#16 = #1#4;  || Descripcion
   #915#18 = #1#5;  || Deducible
   #915#19 = #1#17; || Inversion

 |SI #1#7 = 0;               ||  0%
     #915#27 = #915#27 + nBase;
     #915#28 = #915#28 + nBase;
 |SINO;
     #915#28 = #915#28 + nBase;
     #915#39 = #915#39 + nCuota;
     |SI #1#7 = 4;         ||  4%
         #915#23 = #915#23 + nBase;
         #915#29 = #1#7;
         #915#34 = #915#34 + nCuota;
     |SINO;
         |SI #1#7 = 7; |O #1#7 = 8; |O #1#7 = 10;  ||  7% 8% 10%
             #915#24 = #915#24 + nBase;
             #915#30 = #1#7;
             #915#35 = #915#35 + nCuota;
         |SINO;
             |SI #1#7 = 16;  |O #1#7 = 18; |O #1#7 = 21;   || 16% 18%
                 #915#25 = #915#25 + nBase;
                 #915#31 = #1#7;
                 #915#36 = #915#36 + nCuota;
             |SINO;
                 #915#26 = #915#26 + nBase;   || ??%
                 #915#32 = #1#7;
                 #915#37 = #915#37 + nCuota;
             |FINSI;
         |FINSI;
     |FINSI;
  |FINSI;

 |SI #1#9 ! 0;
     #915#50 = #915#50 + nRecargo;
     |SI #1#9 = #915#40;        || 0.5%
         #915#45 = #915#45 + nRecargo;
     |SINO;
         |SI #1#9 = #915#41; |O #1#9 = 1.40;   || 1.5%
             #915#46 = #915#46 + nRecargo;
         |SINO;
             |SI #1#9 = #915#42; |O #1#9 = 5.20; || 4%
                 #915#47 = #915#47 + nRecargo;
             |SINO;
                 #915#43 = #1#9;
                 #915#48 = #915#48 + nRecargo; || %
             |FINSI;
         |FINSI;
     |FINSI;
 |FINSI;

 |SI #0#14 = "S";
     #915#51 = #915#51 + nBase + nCuota + nRecargo - nCuotaI;
 |SINO;
     #915#51 = #915#51 + nBase + nCuota + nRecargo;
 |FINSI;

   #915#52 = #1#18;  || Concepto Irpf
   #915#53 = #1#19;  || Descripcion
   #915#54 = #915#54 + nBaseI;   || Base Irpf
   #915#55 = #1#21;              || %Irpf
   #915#56 = #915#56 + nCuotaI;  || Base Irpf

   #915#58 = aCtaAna;

   |SI nCuotaI  ! 0; |Y #0#14 = "S"; nSwReten   = 1; |FINSI;
   |SI nRecargo ! 0;                 nSwRecargo = 1; |FINSI;

   |GRABA 020#915a;
   |LIBERA #915;

   #911#0 = aCta915;
   |LEE 041#911=;
   |SI FSalida ! 0;
       #911#0 = aCta915;
       |HAZ LeeCtaAna;
       |GRABA 020#911n;
   |FINSI;

||   |SI nLinea915 = 0; |ACABA; |FINSI;  || no graba resumen de conceptos

   SwHayDatos = 1;

|| ... Graba Resumen 1 por Conceptos
    |SI #0#3 = 1; |O #0#3 = 3;
       #901#0 = #1#3;
       #901#5 = #2#36;
       #901#9 = aCta915;
       |LEE 101#901=;
       |SI FSalida ! 0;
           |DDEFECTO #901;
           #901#0 = #1#3;
           #901#5 = #2#36;
           #901#9 = aCta915;
           |HAZ ElCon;
           |GRABA 020#901b;
       |FINSI;
       #901#1 = #901#1 + nBase;
       #901#2 = #901#2 + nCuota;
       #901#3 = #901#3 + nRecargo;
       #901#4 = #901#4 + (nBase + nCuota + nRecargo - nCuotaI);
       #901#8 = #901#8 + nCuotaI;
       |GRABA 020#901a;
       |LIBERA #901;

       |SI #0#26 = "S";
           #903#0 = #1#3;
           #903#5 = #2#36;
           #903#6 = #1#7;
           #903#7 = #1#9;
           #903#9 = aCta915;
           |LEE 101#903=;
           |SI FSalida ! 0;
               |DDEFECTO #903;
               #903#0 = #1#3;
               #903#5 = #2#36;
               #903#6 = #1#7;
               #903#7 = #1#9;
               #903#9 = aCta915;
               |GRABA 020#903b;
           |FINSI;
           #903#1 = #903#1 + nBase;
           #903#2 = #903#2 + nCuota;
           #903#3 = #903#3 + nRecargo;
           #903#4 = #903#4 + (nBase + nCuota + nRecargo - nCuotaI);
           #903#8 = #903#8 + nCuotaI;
           #903#5 = #2#36;
           |GRABA 020#903a;
           |LIBERA #903;
       |FINSI;
    |FINSI;
|FPRC;

|PROCESO ElCon;
 |ABRE #300;
 #300#0 = #901#0;
 |LEE 001#300=;
 |SI FSalida ! 0;
     |DDEFECTO #300;
 |FINSI;
 #901#6 = #300#1;
 #901#7 = #300#2;
 |CIERRA #300;
|FPRC;

||///////////////////////////////////////////////////////////////////
|| ... Segundo nivel de grupos
|CALCULO ver_grupo2;
 |SI #107#2 ] #0#21; |Y #107#2 [ #0#22;
     edn_error2 = 0;
     |SI nOpera = 0;
         |ERROR10;
     |SINO;
         aCtaAna = #107#2;
         nTanto1 = #107#4;
         |HAZ Grabaw915;
     |FINSI;
 |FINSI;
|FCAL;

|DEFBUCLE selegrupo2;
  #107#1002;
  ;
  aCta2Gru, 000;
  aCta2Gru, 999;
  e;
  NULL, ver_grupo2;
|FIN;

|PROCESO ver_grupo;
 nTanto1 = -1;
 |SI #7#2 ] #0#21; |Y #7#2 [ #0#22;
     swsel = 1;
     |SI nOpera = 0;
         |ERROR10;
     |SINO;
         nTanto  = #7#4;
         aCtaAna = #7#2;
         |HAZ Grabaw915;
     |FINSI;
 |SINO;
     aAlfa1 = #7#2 % 103;
     |SI aAlfa1 = "GRU";
         edn_error2 = 1;
         aCta2Gru   = #7#2;
         nTanto     = #7#4;
         |BUCLE selegrupo2;
         |SI edn_error2 = 0;
             swsel = 1;
             |SI nOpera = 0;
                 |ERROR10;
             |FINSI;
         |FINSI;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE selegrupo;
   #7#1;
   ;
   #1#28, 000;
   #1#28, 999;
   e;
   NULL, ver_grupo;
|FIN;

|PROCESO ver_grupo_apte;
 nSwGApte = 1;
 |SI #13#6 ] #0#21; |Y #13#6 [ #0#22;
     swsel = 1;
     |SI nOpera = 0;
         |ERROR10;
     |SINO;
         nTanto  = -100;
         nBase   = #13#9;
         aCtaAna = #13#6;
         |HAZ Grabaw915;
     |FINSI;
 |SINO;
     aAlfa1 = #13#6 % 103;
     |SI aAlfa1 = "GRU";
         edn_error2 = 1;
         nTanto     = - 100;
         nBase      = #13#9;
         aCta2Gru   = #13#6;
         |BUCLE selegrupo2;
         |SI edn_error2 = 0;
             swsel = 1;
             |SI nOpera = 0;
                 |ERROR10;
             |FINSI;
         |FINSI;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE selgruApte;
 #13#1;
 ;
 #4#0, #4#1, #4#2, 0001, #4#29, 000;
 #4#0, #4#1, #4#2, 9999, #4#29, 999;
 e;
 NULL, ver_grupo_apte;
|FIN;

|PROCESO BuscaApte;  ||esta es la linea del asiento
   nTanto1 = -1;
   |SI #4#19 = "B"; |O #4#19 = "b";
       |SI #4#20 = #1#2; |O #4#20 = 0;
           |BUCLE selgruApte;
       |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE BuscaApte;
   #4#1;
   13,15,14,29;
   #2#7, #2#8, #2#9, 0001, #2#0, #2#2, #2#3, #1#28;
   #2#7, #2#8, #2#9, 9999, #2#0, #2#2, #2#3, #1#28;
   e;
   NULL, BuscaApte;
|FIN;

||///////////////////////////////////////////////////////////////////

|PROCESO LinLibro0;

  aAlfa1 = #1#30; |QBF aAlfa1; |SI aAlfa1 = ""; #1#30 = #2#10; |FINSI;
  |SI aMulDep = "S";
     |SI Haybasica = 1;
         nDepar  = #1#30; |SI nDepar = 0;  nDepar = 1;   |FINSI;
         nDdepar = #0#16;
         nHdepar = #0#17;
         |SI nDepar < nDdepar; |O nDepar > nHdepar; |ACABA; |FINSI;
     |SINO;
         |SI #1#30 < #0#16;  |O #1#30 > #0#17;  |ACABA;  |FINSI;
     |FINSI;
  |FINSI;
  nAlSi = 1;
  |SI nOpMdep = 0;  |ACABA;  |FINSI;  || Solo comprueba que exista
|| ---------------------------------------------------------------------

  enConcepto = #1#3;
  |SI enSwSelCp = 1;
      |HAZ CtrlConcepto;
      |SI nSwConcep = 1;
          |ACABA;
      |FINSI;
  |FINSI;
  nAlConcepto = 1;
  |SI nOpMdep = 2;  |ACABA;  |FINSI;  || Solo comprueba que exista
||----------------------------------------------------------------------

  aAlfa3 = #1#28 % 103;

  aCtaAna = #1#28;
  nTanto  = 100;
  nTanto1 = -1;

  swsel  = 0;         || Solo seleccion Desde/Hasta
  |SI aCtaAna ] #0#21; |Y aCtaAna [ #0#22;
      swsel = 1;
  |SINO;
      |SI aAlfa3 = "GRU";
         nOpera   = 0;
         nSwGApte = 0;
         |BUCLE BuscaApte;
         |SI nSwGApte = 0;
             |BUCLE selegrupo;
         |FINSI;
      |FINSI;
  |FINSI;
  |SI swsel = 0; |ACABA; |FINSI;

  aCtaAna    = #1#28;
  nTanto     = 100;
  nTanto1    = -1;
  nLineaBase = #1#2;

  |SI aAlfa3 ! "GRU";
      |HAZ Grabaw915;
  |SINO;
      |SI #0#25 = "N";  ||Grabamos el grupo o cta analitica entera
          |HAZ Grabaw915;
      |SINO;
          nOpera = 1;        || Para grabar
          nSwGApte = 0;
          |BUCLE BuscaApte;
          |SI nSwGApte = 0;
              |BUCLE selegrupo;
          |FINSI;
      |FINSI;
  |FINSI;

|FPRC;

|DEFBUCLE LinLibro;
   #1#1;
   ;
   #camaniva#0,#camaniva#1,01;
   #camaniva#0,#camaniva#1,99;
   ;
   NULL, LinLibro0;
|FIN;

|PROCESO Facturas;
   |SI aMulDep ! "S";
       |SI Haybasica = 1;
           nDepar  = #2#10; |SI nDepar = 0;  nDepar = 1;   |FINSI;
           nDdepar = #0#16;
           nHdepar = #0#17;
           |SI nDepar < nDdepar; |O nDepar > nHdepar; |ACABA; |FINSI;
       |SINO;
           |SI #2#10 < #0#16;  |O #2#10 > #0#17;  |ACABA;  |FINSI;
       |FINSI;
   |SINO;
        nOpMdep = 0;
        nAlSi   = 0;
        nAlNo   = 0;
        |BUCLE LinLibro;
        |SI nAlSi = 0; |ACABA; |FINSI;
   |FINSI;

|| ................ Comprueba que los conceptos de facturas son buenos
   |SI enSwSelCp = 1;
       nOpMdep     = 2;
       nAlConcepto = 0;
       |BUCLE LinLibro;
       |SI nAlConcepto = 0; |ACABA; |FINSI;
   |FINSI;

   nOpMdep = 1;

   fLaFecha = #2#4; |SI #0#9 = 2; fLaFecha = #2#8; |FINSI;

   nGraba  = 0;
   |SI #0#19 = 99;     || Seleccion por Fechas
       |SI fLaFecha ] #0#6;
           nGraba = 1;
       |FINSI;
   |SINO;
       |SI #2#40 ] #0#19;   || Seleccion por Periodo
           nGraba = 1;
       |FINSI;
   |FINSI;

   |BUCLE LinLibro;
   nFactura = nFactura + 1;
|FPRC;

|DEFBUCLE Facturas1;
   #camaniva#5;
   36,40;
   fDFecha, #0#4, 0000000001, aDTipo, nDLiqui;
   fHFecha, #0#5, 9999999999, aHTipo, nHLiqui;
   ;
   NULL, Facturas;
|FIN;

|DEFBUCLE Facturas2;
   #camaniva#1;
   8,36,40;
   #0#4,0000000001, fDFecha, aDTipo, nDLiqui;
   #0#5,9999999999, fHFecha, aHTipo, nHLiqui;
   ;
   NULL,NULL,NULL,NULL,NULL,Facturas;
   #camaniva#8, #camaniva#0, #camaniva#1;
|FIN;

|DEFBUCLE Facturas3;
   #camaniva#3;
   4,36,40;
   #0#4, "     ", 000001, fDFecha, aDTipo, nDLiqui;
   #0#5, "zzzzz", 999999, fHFecha, aHTipo, nHLiqui;
   ;
   NULL, Facturas;
|FIN;
|| //////////////////////////////////////////////////////////

|PROCESO ImprimeAux;
||   |SI #915#3 ! 0;
       SwLinea = 0;
       nLBasP = #915#54;
       nLTipP = #915#55;
       nLCuoP = #915#56;
       |PARA nPara1 = 23; |SI nPara1 [ 27; |HACIENDO nPara1 = nPara1 + 1;
             nLBase = #915nPara1;
             nNume1 = nPara1 +  6; nLTipoI = #915nNume1;
             nNume2 = nPara1 + 11; nLCuoI  = #915nNume2;
             nNume3 = nPara1 + 17; nLTipoR = #915nNume3;
             nNume4 = nPara1 + 22; nLCuoR  = #915nNume4;
             |SI nLCuoI = 0; nLTipoI = 0; |FINSI;
             |SI nLCuoR = 0; nLTipoR = 0; |FINSI;

             |SI nLBase ! 0; |O nLCuoI ! 0; |O nLCuoR ! 0;

                 |SI  #915#3 ! 0;  |PRINT;  |FINSI;

                 SwLinea = 1;
                 #1#6 =  nLBase;
                 #1#7 =  nLTipoI;
                 #1#8 =  nLCuoI;
                 #1#9 =  nLTipoR;
                 #1#10 = nLCuoR;
                 |HAZ sumaLiqIVA;
                 TotalBases  = (TotalBases + #1#6) ! EURODB;
                 TotalIvas   = (TotalIvas  + #1#8) ! EURODB;
                 TotalRecs   = (TotalRecs  + #1#10) ! EURODB;
                 TotalCuotas = (TotalIvas + TotalRecs) ! EURODB;

            |FINSI;
      |SIGUE;
      |SI SwLinea = 0;
          |SI #915#3 ! 0; |PRINT; |FINSI;
      |FINSI;
      SwPrim     = 1;
      SwHayDatos = 1;
||   |FINSI;

   #0#27 = #0#27 + #915#28;
   #0#28 = #0#28 + #915#39;
   #0#29 = #0#29 + #915#50;
   #0#30 = #0#30 + #915#51;
   #0#31 = #0#31 + #915#56;

|FPRC;

|DEFBUCLE ImprimeAux1;
   #915#2;
   ;
   #911#0, "01.01.0000", 00, 0000000000, 00;
   #911#0, "31.12.2999", 99, 9999999999, 99;
   ;
   NULL, ImprimeAux;
|FIN;

|DEFBUCLE ImprimeAux2;
   #915#3;
   ;
   #911#0, "     ", 000000;
   #911#0, "zzzzz", 999999;
   ;
   NULL, ImprimeAux;
|FIN;

|PROCESO LoBorro;
      |HAZ BorraAcum;
      nPagina   = 1;
      nPagDup   = nPagina - 1;
      nSwPagDup = 0;
      SwHayDatos = 0;
      SwPrim     = 0;
      |PDEFECTO #0, 27, 32;
      TotalBases  = 0;
      TotalIvas   = 0;
      TotalRecs   = 0;
      TotalCuotas = 0;
|FPRC;

|PROCESO Auxiliar911;
      informe = "any00010";
      |SI nSwReten   = 1; informe = "any20010"; |FINSI;
      |SI nSwRecargo = 1; informe = "any10010"; |FINSI;
      |SI #8#4 = "N";
          aAlfa1  = informe % 306;
          informe = "ar" + aAlfa1;
      |FINSI;

      |INFOR informe;
      |SI FSalida ! 0;
          |MENAV "    No existe informe.";
          |ACABA;
      |FINSI;

      enSwPAna   = 0;
      |HAZ LoBorro;
      aCtaAna = #911#0;
      aTitAna = #911#1;
      |SI aCtaAna = "       "; aTitAna = "Fras sin Cta Analitica"; |FINSI;
      |SI #0#23 = "S";
          enSwPAna   = 1;
      |FINSI;
      |SI #0#9 [ 2; |BUCLE ImprimeAux1; |FINSI;
      |SI #0#9 = 3; |BUCLE ImprimeAux2; |FINSI;
      |SI SwHayDatos = 1;
          |PIEINF;
          |FININF;
          |SI #0#3 = 3; |O #0#3  = 1; |HAZ Resumen1; |FINSI;
          |SI #0#3 = 3; |O #0#3  = 2; |HAZ imprimeLiqIVA; |FINSI;
      |SINO;
          |FININF;
      |FINSI;
|FPRC;

|DEFBUCLE Auxiliar911;
     #911#1001;
     ;
     "       ";
     "zzzzzzz";
     ;
     NULL, Auxiliar911;
|FIN;

|| //////////////////////////////////////////
|PROCESO dsmow031;
   |PRINT;
|FPRC;

|DEFBUCLE dsmow031;
   #901#1;
   ;
   " ", 001, #911#0;
   "z", 999, #911#0;
   ;
   NULL, dsmow031;
|FIN;

|PROCESO Resumen1;
   informe = inform1;
   |SI #0#26 = "S"; informe = inform3; |FINSI;
   |SI nSwReten = 1;
       informe = inform2;
       |SI #0#26 = "S"; informe = inform4; |FINSI;
   |FINSI;

   |INFOR informe;
   |SI FSalida ! 0;  |ACABA;  |FINSI;

   |BUCLE dsmow031;

   |PIEINF; |FININF;
|FPRC;

|| //////////////////////////////////////////
|| //////////////////////////////////////////

|PROCESO HazTodo;

   |ABRE #200;
   |DDEFECTO #200;
   |LEE 000#200p;
   |CIERRA #200;
   aMulDep = #200#132;

   |SI Haybasica = 0;  || si selecciona todos los departamentos no es necesario ver lineas
       |SI #0#16 = "  "; |Y #0#17 = "zz"; aMulDep = "N"; |FINSI;
   |SINO;
       |SI #0#16 = "  "; #0#16 = "00"; |FINSI;
       |SI #0#17 = "zz"; #0#17 = "99"; |FINSI;
       |SI #0#16 [ 1;    |Y #0#17 = 99;   aMulDep = "N"; |FINSI;
   |FINSI;

   |SI #0#0 = "R"; aDesTip = "REPERCUTIDO -"; eaClPro = "Cliente";  |FINSI;
   |SI #0#0 = "S"; aDesTip = "SOPORTADO -  "; eaClPro = "Proveedor"; |FINSI;

   |ABRE #8;
   |DDEFECTO #8;  ||Configuracion de Informes
   #8#0 = 1;
   |LEE 000#8=;
   |CIERRA #8;

   eaIa = #30#21;
   enM1 = 1 + (#0#19 -1) * 3;
   enM2 = 3 + (#0#20 -1) * 3;

   eaPortada = "N";
   eaTituloL = #0#13; ||Titulo del Libro

   |SI #0#2 = "S";
       eaImpresora = Impresora;
       |SUB_EJECUTA_NP ":basica/agiy0016";
   |FINSI;

   |HAZ LeeDatosEmpresa;
   #900#12 = #0#1;
   #900#13 = #0#6;
   #900#14 = #0#7;
   #900#27 = eaTituloL;

   SwHayDatos = 0;
   nSwReten   = 0;
   nSwRecargo = 0;

   fDFecha = #0#6;
   fHFecha = #0#7;
   nDLiqui = #0#19;
   nHLiqui = #0#20;
   |SI #0#19 = 99;
       nDLiqui = 0;
       nHLiqui = 99;
   |FINSI;

   |DELINDEX #911;
   |DELINDEX #901;
   |DELINDEX #915;
   |DELINDEX #903;

   nFactura  = 1;
   |ABRE #911;
   |ABRE #901;
   |ABRE #903;
   |ABRE #915;

   |SI #0#9 = 1;  |BUCLE Facturas1;  |FINSI;
   |SI #0#9 = 2;  |BUCLE Facturas2;  |FINSI;
   |SI #0#9 = 3;  |BUCLE Facturas3;  |FINSI;

  |CIERRA #915;
  |CIERRA #903;
  |CIERRA #901;
  |CIERRA #911;

  |SI SwHayDatos = 1;
      |BUCLE Auxiliar911;
  |FINSI;
|| ------------------------------------------------------
|FPRC;

|CALCULO Tipo3;  |TIPO 3;

|| .........  LeeCtrlLibro;
     aDTipo = "R"; aHTipo = "S";
     |SI enSwSelCp = 0;
         aDTipo = #0#0; aDTipo = aDTipo > " ";
         aHTipo = #0#0; aHTipo = aHTipo > " ";
     |FINSI;

     efFechaEmi = #0#10;

     aFichero = ("1w" + Usuario) % 108;
     |NOME_DAT #901 aFichero; |DELINDEX #901;

     aFichero = ("3w" + Usuario) % 108;
     |NOME_DAT #915 aFichero; |DELINDEX #915;

     aFichero = ("4c" + Usuario) % 108;
     |NOME_DAT #903 aFichero; |DELINDEX #903;

     aFichero = ("5c" + Usuario) % 108;
     |NOME_DAT #911 aFichero; |DELINDEX #911;

     |IMPRE 0;
     |SI FSalida < 0; |ACABA; |FINSI;

     |SI #48#27 = "S";
         |HAZ MultiEmpresa;
     |SINO;
          |DFICE dirfich0; |QBF dirfich0;
          |HAZ HazTodo;
     |FINSI;

     |FINIMP;
     |DELINDEX #901;
     |DELINDEX #915;
     |DELINDEX #903;
     |DELINDEX #911;
|FCAL;

|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;
   dirfich0 = #caselem1#3;
   |QBF dirfich0;
   |SI #0#9 > #caselem1#5; |O #0#10 < #caselem1#4;
       |ACABA;  || Fuera de la seleccion
   |FINSI;

   |HAZ Analitica; || empresa no analitca
   |SI enSwAna = 0;
        |ACABA;
   |FINSI;

   |PATH_DAT #1   dirfich0;
   |PATH_DAT #2   dirfich0;
   |PATH_DAT #3   dirfich0;
   |PATH_DAT #4   dirfich0;
   |PATH_DAT #7   dirfich0;
   |PATH_DAT #8   dirfich0;
   |PATH_DAT #11  dirfich0;
   |PATH_DAT #13  dirfich0;
   |PATH_DAT #107 dirfich0;
   |PATH_DAT #200 dirfich0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
   #caselem1#1;
   ;
   000001;
   999999;
   ;
   NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;

|PROGRAMA;

  |HAZ inicio;
  |HAZ DirAplicSt;

  aParam = PARAMETRO; |QBF aParam;
  aClave = aParam;
  |SI aClave ! "R"; |Y aClave ! "S"; aClave = "R"; |FINSI;

  |DBASE aMas;
  aMas  = aMas + "def/anm00003";
  |CARGA_DEF aMas, anm0003@;
  |SI FSalida ! 0;
      |MENAV "    Error al cargar el def anm00003";
      |ACABA;
  |FINSI;
  |DBASE aMas;
  aMas  = aMas + "def/anm00001";
  |CARGA_DEF aMas, anm0001@;
  |SI FSalida ! 0;
      |MENAV "    Error al cargar el def anm00001";
      |DESCARGA_DEF #107;
      |ACABA;
  |FINSI;

  |ABRE #0;
  |DDEFECTO #0;
  #0#0 = aClave;
  |LEE 101#0=;
  |SI FSalida ! 0;
      |DDEFECTO #0;
      #0#0  = aClave;
      #0#6  = fec1;
      #0#7  = fec2;
      |GRABA 020#0b;
      nEstado = 1;
  |FINSI;
  #0#10 = ~;
  #0#24 = 901; |SI #0#0 = "S"; #0#24 = 902; |FINSI;

  |HAZ eLeeParametro;

  |CLS;
  |CABEZA "Emision de Facturas";
  |PINPA #0#0;
  |PINDA #0#0;

  |ENDATOS #1#0;
  |SI FSalida = 0;
      #0#0 = aClave;
      |GRABA 020#0a;
  |FINSI;
  |LIBERA #0;
  |CIERRA #0;

  |DESCARGA_DEF #911;
  |DESCARGA_DEF #107;
|FPRO;
