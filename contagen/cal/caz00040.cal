|FICHEROS;
   caz00040 #0;
   caw00043 #900,MANTE;
   imagen@  #901;
   caextrp4 #6;

   cacuenta #1;
   camaasic #10;
   camaasil #11;
   caclipro #18;
   calicont #31;
   caivaasi #200;
   camanefe #347;

  :/modelos/def/dsmom030 #330;

   canempre #30;

   cawdatos #910;
   caselem0 #997;
   caselem1 #998;
|FIN;

|VARIABLES;
    &entrada = 1;

    aFichero   = "";
    nCampo     = 0;
    aAlfa1     = "";
    nSitua     = 0;
    &espe15    = "";
    &eSwMul    = 0;
    nEmpMulti  = 0;
    &espe16    = 0;
    informe    = "";

    aDCuenta   = "";
    aHCuenta   = "";
    nExis      = 0;
    nInkey     = 0;
    aNombre    = "";
    aNif       = "";
    nGrabo     = 0;
    nAny       = 0;
    nPer       = 0;
    nConta     = 0;
    nSwAlgun   = 0;
    nRegistro  = 0;
    nSwImprime = 0;
    nPrint     = 0;
    nCerrar    = 0;
    nSwPide    = 0;
    aContrapartida = "";
    aNomContra     = "";

    CodigoEmpresa  = 0;
    PeriodoEmpresa = 0;
    dirfichOri     = "";

    seleccio   = 0;
    lalinea    = 0;
    totallin   = 0;
    aParam     = "";
    aCampo25   = "";
    aCampo26   = "";
    aCampo63   = "";
    aMas       = "";
    aDef       = "";

    &enImporte = 0;
    &enCobro   = 0;
    &enPago    = 0;
    nPara1     = 0;
    nNume1     = 0;
    nNume2     = 0;
    &nAny1     = 0;
    &nAny2     = 0;
    &nAny3     = 0;
    &nAny4     = 0;
    &nAny5     = 0;
    &eSwLinea  = 0;
    nHayNif    = 0;
    nHayAny    = 0;
    nSw        = 0;
    aDTipo     = "";
    aHTipo     = "";
    aTipo      = "";
    &eaCoPa    = "";
    quince     = "";
    &enCalcCif = 0;
    &eaVarDni  = "";
    aSeg       = "";
    aRend      = "";
|FIN;

|INCLUYE dscont_i;

|| ***************** Calculos de Pantalla

|CALCULO defectos; |TIPO 0;
    #0#4 = fec1;
    |SI #0#22 = "S"; #0#4 = "01.01.2000"; |FINSI;
    |PINTA #0#4;
    #0#5 = fec2; |PINTA #0#5;
|FCAL;

|CALCULO fecha; |TIPO 0;
    |SI #0#22 = "N";
        |SI #0Campo < fec1; |O #0Campo > fec2;
            |MENAV "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
            |ERROR;
            |ACABA;
        |FINSI;
    |FINSI;
    |SI Campo = 5; |Y #0#5 < #0#4;
        |MENAV "    FECHA ERRONEA ";
        |ERROR;
    |FINSI;
|FCAL;

|CALCULO seleccion; |TIPO 0;
    |SI #0#6 = "S";
        |C_M #0#7,1,"S";
        |C_M #0#8,1,"S";
        |PARA nCampo = 9; |SI nCampo [ 20; |HACIENDO nCampo = nCampo + 1;
             |C_M #0nCampo,1,"N";
        |SIGUE;
    |SINO;
        |C_M #0#7,1,"N";
        |C_M #0#8,1,"N";
        |PARA nCampo = 9; |SI nCampo [ 20; |HACIENDO nCampo = nCampo + 1;
              |C_M #0nCampo,1,"S";
        |SIGUE;
    |FINSI;
|FCAL;

|CALCULO NoMasCuentas; |TIPO 0;
    aAlfa1 = #0Campo; |QBF aAlfa1;
    |SI aAlfa1 = "";
        |PARA nCampo = Campo + 1;  |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
              #0nCampo = "";  |C_M #0nCampo, 1, "N";
        |SIGUE;
    |SINO;
        |PARA nCampo = Campo + 1;  |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
              |C_M #0nCampo, 1, "S";
        |SIGUE;
    |FINSI;
|FCAL;

|CALCULO cuenta; |TIPO 6;
    |C_M #0Campo,0,aAlfa1;
    |SI aAlfa1 = "N"; |ACABA; |FINSI;

    eaCuenta = #0Campo;
    salidas  = FSalida;
    |HAZ FiltraPuntos;
    #0Campo = eaCuenta;
    |PINTA #0Campo;
|FCAL;

|PROCESO Todos;
    aAlfa1 = "S";
    |SI #0#21 ! "E"; aAlfa1 = "N"; |FINSI;

    |SI #0#21 = "N";
        #0#23 = 0; |PINTA #0#23;
        #0#24 = 0; |PINTA #0#24;
    |FINSI;

    |C_M #0#23,1,aAlfa1;
    |C_M #0#24,1,aAlfa1;
|FPRC;

|PROCESO SelNif; |TIPO 0;
   aAlfa1 = #0#65;
   |C_M #0#66, 1, aAlfa1;
   |C_M #0#67, 1, aAlfa1;
   |C_M #0#68, 1, aAlfa1;
   |C_M #0#69, 1, aAlfa1;
   |SI aAlfa1 = "N";
       #0#66 = ""; |PINTA #0#66;
       #0#67 = ""; |PINTA #0#67;
       #0#68 = ""; |PINTA #0#68;
       #0#69 = ""; |PINTA #0#69;
   |FINSI;
|FPRC;

|PROCESO elDNI; |TIPO 6;
  |C_M #0Campo, 0, aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

  |SI FSalida = 10;
      enCalcCif = 1;
      eaVarDni  = #0Campo;
      |HAZ CalculoDNI;
      #0Campo = eaVarDni;
      |PINTA #0Campo;
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO Resumen; |TIPO 0;
    |C_V #0#26,0, aAlfa1;
    |SI aAlfa1 = "N"; |ACABA; |FINSI;

    aAlfa1 = #0#26;
    |C_M #0#63,1,aAlfa1;
|FPRC;


|| *************************************************************************
|PROCESO ElTotal;
  |SI nHayAny = 1;
     ||  eSwLinea = 2; |PRINT;
  |FINSI;

  #0#56 = #0#39 - #0#45;
  #0#57 = #0#40 - #0#46;
  #0#58 = #0#41 - #0#47;
  #0#59 = #0#42 - #0#48;
  #0#60 = #0#43 - #0#49;
  #0#61 = #0#44 - #0#50;
  #0#62 = "COBROS";
  |SI #0#55 = "P";
      #0#56 = - #0#56;
      #0#57 = - #0#57;
      #0#58 = - #0#58;
      #0#59 = - #0#59;
      #0#60 = - #0#60;
      #0#61 = - #0#61;
      #0#62 = "PAGOS";
  |FINSI;
  eSwLinea = 3; |PRINT;
|FPRC;

|PROCESO elTotalE;
  #0#56 = #0#27 - #0#33;
  #0#57 = #0#28 - #0#34;
  #0#58 = #0#29 - #0#35;
  #0#59 = #0#30 - #0#36;
  #0#60 = #0#31 - #0#37;
  #0#61 = #0#32 - #0#38;
  |SI aTipo = "P";
      #0#56 = - #0#56;
      #0#57 = - #0#57;
      #0#58 = - #0#58;
      #0#59 = - #0#59;
      #0#60 = - #0#60;
      #0#61 = - #0#61;
  |FINSI;
|FPRC;

|PROCESO ParaImprimir;

  |SI aCampo26 = "S";
      |SI #0#51 ! #901#12; |O nSw = 0;
          |SI nHayNif = 1; |HAZ ElTotal; |FINSI;
          nHayNif = 0;
          nHayAny = 0;
          |PDEFECTO #0, 39, 63;
          #0#51 = #901#12;
          #0#52 = #901#5;
          #0#53 = #901#4;
          #0#54 = #901#10;
          #0#55 = #901#24;
          eSwLinea = 0;
          nSw = 1;
      |FINSI;
  |FINSI;

  |SI aCampo25 = "E";
      |SI #0#54 ! #901#10;
          |SI nHayAny = 1;
              |SI aCampo63 ! "S"; eSwLinea = 2; |PRINT; |FINSI;
          |FINSI;
          nHayAny = 0;
          #0#54 = #901#10;
          eSwLinea = 1;
      |FINSI;
  |FINSI;

  enCobro   = 0;
  enPago    = 0;
  enImporte = 0;
  |SI #901#24 = "C";
      enCobro = #901#9 - #901#8;
      enImporte = enCobro;
  |SINO;
      enPago  = #901#8 - #901#9;
      enImporte = enPago;
  |FINSI;

  |PARA nPara1 = 0; |SI nPara1 [ 26; |HACIENDO nPara1 = nPara1 + 1;
        #900nPara1 = #901nPara1;
  |SIGUE;

  |SI aCampo63 ! "S"; |PRINT; |FINSI;

  eSwLinea = 1;
  nPrint   = 1;
  nHayNif  = 1;
  nHayAny  = 1;

  nAny = #901#10;
  |SI aCampo63 = "S"; |Y nAny = 0;
      aAlfa1 = #901#1;
      aAlfa1 = aAlfa1 % -104;
      nAny   = aAlfa1;
  |FINSI;

  |SI nAny = nAny1; nNume1 = 28; nNume2 = 57; |FINSI;
  |SI nAny = nAny2; nNume1 = 29; nNume2 = 58; |FINSI;
  |SI nAny = nAny3; nNume1 = 30; nNume2 = 59; |FINSI;
  |SI nAny = nAny4; nNume1 = 31; nNume2 = 60; |FINSI;
  |SI nAny [ nAny5; nNume1 = 32; nNume2 = 61; |FINSI;
  |SI nAny = 0;     nNume1 = 27; nNume2 = 56; |FINSI;

                       #0nNume1 = #0nNume1 + enCobro;
  nNume1 = nNume1 + 6; #0nNume1 = #0nNume1 + enPago;
  nNume1 = nNume1 + 6; #0nNume1 = #0nNume1 + enCobro;
  nNume1 = nNume1 + 6; #0nNume1 = #0nNume1 + enPago;

  |SI aCampo26 = "N";
      #0nNume2 = #0nNume2 + enImporte;
  |FINSI;
|FPRC;

|DEFBUCLE Leesegunda;
  #901#2004;
  ;
  #0#4, #0#0, #0#2, 0001;
  #0#5, #0#1, #0#3, 9999;
  ;
  NULL, ParaImprimir;
|FIN;

|DEFBUCLE Leetercera;
  #901#3005;
  ;
  0000, #0#4, #0#0, #0#2, 0001;
  9999, #0#5, #0#1, #0#3, 9999;
  ;
  NULL, ParaImprimir;
|FIN;

|DEFBUCLE Leesegunda0;
  #901#4005;
  24;
  "               ", #0#4, #0#0, #0#2, 0001, aDTipo;
  "zzzzzzzzzzzzzzz", #0#5, #0#1, #0#3, 9999, aHTipo;
  ;
  NULL, ParaImprimir;
|FIN;

|DEFBUCLE Leetercera0;
  #901#5006;
  24;
  "               ", 0000, #0#4, #0#0, #0#2, 0001, aDTipo;
  "zzzzzzzzzzzzzzz", 9999, #0#5, #0#1, #0#3, 9999, aHTipo;
  ;
  NULL, ParaImprimir;
|FIN;

|| *************************************************************************
|PROCESO Impresion;
    |PDEFECTO #0, 27, 63;
    nHayNif = 0;
    nHayAny = 0;
    nSw     = 0;
    nPrint  = 0;
    |SI aCampo26 = "N";
        |SI aCampo25 = "F"; |BUCLE Leesegunda; |FINSI;
        |SI aCampo25 = "E"; |BUCLE Leetercera; |FINSI;
    |SINO;
        aNif = "";
        |SI aCampo25 = "F"; |BUCLE Leesegunda0; |FINSI;
        |SI aCampo25 = "E"; |BUCLE Leetercera0; |FINSI;
    |FINSI;

    |SI nPrint = 1;
        |SI aCampo26 = "S"; |Y nHayNif = 1;
            |HAZ ElTotal;
        |FINSI;
        |SI aCampo26 = "S";
            |HAZ elTotalE;
        |FINSI;
        |PIEINF;
    |FINSI;
|FPRC;

|PROCESO ElInforme;
    |SI aParam ! "I";
        |SI aCampo25 = ""; aCampo25 = "E"; |FINSI;
        |SI aCampo26 = ""; aCampo26 = "S"; |FINSI;
        |SI aCampo63 = ""; aCampo63 = "N"; |FINSI;
        |PUSHV 1908 2270;
        |BLANCO 1908 2268;
        |CUADRO 1908 2268;
        |ATRI I;
        |PINTA 2009, " Ordenar por Ejercicio Operacion/Fecha contable (E/F):   ";
        |PINTA 2109, " Agrupar por Nif (S/N):        Informe resumido (S/N):   ";
        |ATRI 0;
        |PINTA 2064, aCampo25;
        |PINTA 2133, aCampo26;
        |PINTA 2164, aCampo63;
        E_sup   = 1;
        E_inf   = "EFef";
        aCampo25 =  2064 ? 0;  aCampo25 = aCampo25 > " "; |PINTA 2064, aCampo25;
        E_sup   = 1;
        E_inf   = "SNsn";
        aCampo26 =  2133 ? 0;  aCampo26 = aCampo26 > " "; |PINTA 2133, aCampo26;
        |SI aCampo26 = "S";
            E_sup   = 1;
            E_inf   = "SNsn";
            aCampo63 =  2164 ? 0;  aCampo63 = aCampo63 > " "; |PINTA 2164, aCampo63;
        |FINSI;

        |POPV;
        nSwPide = 0;
        nCerrar = 1;
    |SINO;
        aCampo25 = #0#25;
        aCampo26 = #0#26;
        aCampo63 = #0#63;
    |FINSI;

    |SI aCampo26 = "N"; aCampo63 = "N"; |FINSI;

    |SI nSwPide = 0;
        |IMPRE 0;
        |SI FSalida ! 0;
            |MENAV "    Proceso interrumpido";
            |ACABA;
        |FINSI;

        informe = "caz00040";
        |SI aCampo26 = "S";
            informe = "caz10040";
            |SI aCampo63 = "S"; informe = "caz20040"; |FINSI;
        |FINSI;
        |SI #0#64 ! "A";
            |SI informe = "caz00040"; informe = "caz01040"; |FINSI;
            |SI informe = "caz10040"; informe = "caz11040"; |FINSI;
        |FINSI;
        |SI informe = "caz00040"; |O informe = "caz01040";
            |MENAV "0000 Recuerde seleccionar el papel de la impresora en Horizontal";
        |FINSI;

        |INFOR informe;
        |SI FSalida ! 0;
            |MENAV "    Error en el Informe";
            |FINIMP;
            |ACABA;
        |FINSI;

        nSwPide = 1;

        |HAZ LeeDatosEmpresa;
    |FINSI;

    aTipo = #0#64;
    aDTipo = " "; aHTipo = "z";
    |SI aCampo63 = "N";
        |HAZ Impresion;
    |SINO;
        |SI #0#64 = "A";
            #910#27 = "RELACION DE APUNTES DE COBROS EN EFECTIVO";
            aTipo = "C"; aDTipo = "C"; aHTipo = "C"; |HAZ Impresion;
            #910#27 = "RELACION DE APUNTES DE PAGOS EN EFECTIVO";
            aTipo = "P"; aDTipo = "P"; aHTipo = "P"; |HAZ Impresion;
        |SINO;
            aTipo = #0#64;
            |SI #0#64 = "C"; #910#27 = "RELACION DE APUNTES DE COBROS EN EFECTIVO"; |FINSI;
            |SI #0#64 = "P"; #910#27 = "RELACION DE APUNTES DE PAGOS EN EFECTIVO"; |FINSI;
            |HAZ Impresion;
        |FINSI;
    |FINSI;

    |SI nCerrar = 1;
        |FININF;
        |FINIMP;
        nSwPide = 0;
    |FINSI;
|FPRC;

|| *************************************************************************
|PROCESO Situafich0;
   enEmpresa = #900#20;
   enPeriodo = #900#21;
   dirfich0  = #900#22; |QBF dirfich0;
   |PATH_DAT #1 dirfich0;
   |PATH_DAT #10 dirfich0;
   |PATH_DAT #11 dirfich0;
|FPRC;

|PROCESO ModiPan_2;
   aAlfa1 = "N";
   |SI #10#7 ! 0;
       aAlfa1 = "S";
       |ATRI R; |PINTA 0861, "Asto Predef.: "; |ATRI 0;
   |FINSI;
   |C_V #6#17, 1, aAlfa1;

   |SI eaSubde = "N";
        |C_A #6#6, 1, 29;
        |C_P #6#7, 1, 1266;
        |C_V #6#16,1, "N";
        |PINTA 0961, "ÄÄÄÄÂÄÄ";
        |PINTA 1061, "    ³  ";
        |PINTA 1161, "ÄÄÄÄÅÄÄ";
        |PINTA 1261, "    ³  ";
        |PINTA 1361, "    ³  ";
        |PINTA 1461, "    ³  ";
        |PINTA 1561, "    ³  ";
        |PINTA 1661, "    ³  ";
        |PINTA 1761, "    ³  ";
        |PINTA 1861, "    ³  ";
        |PINTA 1961, "    ³  ";
        |ATRI R; |PINTA 1061, "    "; |PINTA 1066, "Dp"; |ATRI 0;
   |SINO;
        |SI enSubde = 1; |Y eaDepar = "N";
            |C_A #6#6, 1, 28;
            |C_V #6#7, 1, "N";
            |PINTA 0961, "Ä";
            |ATRI R; |PINTA 1061, "   "; |ATRI 0;
            |PINTA 1161, " ";
            |PINTA 1261, " ";
            |PINTA 1361, " ";
            |PINTA 1461, " ";
            |PINTA 1561, " ";
            |PINTA 1661, " ";
            |PINTA 1761, " ";
            |PINTA 1861, " ";
            |PINTA 1961, " ";
            |PINTA 2061, " ";
        |FINSI;
   |FINSI;
|FPRC;

|PROCESO lee_asiento1;             || CARGA LA PANTALLA DEL ASIENTO COMPLETO
   seleccio = 1;

   |ABRE #1;
   |DDEFECTO #1;
   #1#0 = #11#4;
   |LEE 001#1=;
   |CIERRA #1;

   |DDEFECTO #6;
   #caextrp4#00 = #11#3;        || apunte
   #caextrp4#01 = #11#16;       || cuenta DEBE
   #caextrp4#02 = #11#17;       || digito DEBE
   #caextrp4#03 = #11#10;       || cuenta HABER
   #caextrp4#04 = #11#11;       || digito HABER
   #caextrp4#05 = #11#6;        || concepto
   #caextrp4#06 = #11#7;        || descripcion
   #caextrp4#07 = #11#21;       || departamento
   #caextrp4#08 = #11#9;        || importe
   #caextrp4#09 = #cacuenta#2;        || descripcion
   #caextrp4#10 = #cacuenta#51;       || debe
   #caextrp4#11 = #cacuenta#52;       || haber
   #caextrp4#12 = #cacuenta#53;       || saldo
   #caextrp4#13 = #11#0;        || diario
   #caextrp4#14 = #11#1;        || fecha
   #caextrp4#15 = #11#2;        || documento
   #caextrp4#16 = #11#28;       || Subdepartamento
   #caextrp4#17 = #10#7;
   |SI #caextrp4#0 = #900#3;
       lalinea = #caextrp4#0;
   |FINSI;
   |GRABA 020#6n;
   totallin = totallin + 1;
|FPRC;

|DEFBUCLE lee_asiento;
   #11#1;
   ;
   #900#0, #900#1, #900#2,    1;
   #900#0, #900#1, #900#2, 9999;
   e;
   NULL, lee_asiento1;
|FIN;

|PROCESO mini;
   |SI FSalida = "POSICION";  |Y totallin > 8;
      #caextrp4#0 = lalinea;
   |SINO;
      #caextrp4#0 = 1;
   |FINSI;
|FPRC;

|PROCESO maxi;
   #caextrp4#0 = 9999;
|FPRC;

|PROCESO asiento;
   |SI #0#22 = "S";
       |HAZ Situafich0;
   |FINSI;

   aFichero = ("q" + Usuario);
   aFichero = (aFichero + "zzzzzzzz") % 108;
   |NOME_DAT #6 aFichero;
   |DELINDEX #6;

   seleccio = 0;
   |ABRE #10;
   |DDEFECTO #10;
   #10#0 = #900#0;
   #10#1 = #900#1;
   #10#2 = #900#2;
   |LEE 001#10=;
   |CIERRA #10;

   |ABRE #6;
   lalinea = 1;
   totallin = 0;
   |BUCLE lee_asiento;
   |CIERRA #6;

   |SI seleccio ! 0;
      |PUSHV 07012380;
      |PINPA #0#6; |HAZ ModiPan_2;
      |ENTLINEAL #1#6, -1, 4, 20, 0, mini, maxi;
      |POPV;
   |SINO;
      |MENAV "    Seleccion vacia ...";
   |FINSI;
   |DELINDEX #6;
|FPRC;

|PROCESO Ejercicio; |TIPO 7;
   |SI #900#10 = 0;
       aAlfa1 = #900#1;
       aAlfa1 = aAlfa1 % -104;
       #900#10 = aAlfa1;
       |PINTA #900#10;
   |FINSI;
|FPRC;

|PROCESO UnoSoloW; |TIPO 0;
    |SI Campo = 25;
        |SI #900#25 = "S"; #900#26 = "N"; |PINTA #900#26; |FINSI;
    |FINSI;
    |SI Campo = 26;
        |SI #900#26 = "S"; #900#25= "N";  |PINTA #900#25; |FINSI;

        |SI #900#26 = "S"; |Y #900#15 = "   ";
          |CONFI "    NNo informado el Subdepartamento en esta Op. de Arrendamiento. Continuar";
          |SI FSalida ! 0;
               #900#26 = "N"; |PINTA #900#26;
               |ERROR;
          |FINSI;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO Modifico; |TIPO 11;

 nInkey = FSalida;

 |SI nInkey = 12;
     |LEE 141#900=;

     nAny = #900#10;
     |ENCAMPO #10#900;

     |ENCAMPO #25#900;

     |ENCAMPO #26#900;
     |SI FSalida = 0;
         |GRABA 000#900a;
     |SINO;
         #900#10 = nAny;
         |PINTA  #900#10;
     |FINSI;
     |LIBERA #900;
     |ERROR;
 |FINSI;

 |SI nInkey = 11;
     |SALVA_DATOS #900;
     |HAZ ElInforme;
     |REPON_DATOS #900;
     |ERROR;
 |FINSI;

 |SI nInkey = 10;
     |HAZ asiento;
     |ERROR;
 |FINSI;
|FPRC;

|| *************************************************************************
|| **************************   MEOLLO   ***********************************
|| *************************************************************************
|PROCESO BuscaContra;
    aContrapartida = "";
    aNomContra     = "";

    |DDEFECTO #10;
    #10#0 = #11#0;
    #10#1 = #11#1;
    #10#2 = #11#2;
    |LEE 001#10=;
    nRegistro = #10#12;

    #31#0 = #11#0;
    #31#1 = #11#1;
    #31#2 = #11#2;
    #31#3 = #11#3;
    |LEE 001#31=;
    |SI FSalida = 0;
        aContrapartida = #31#4;
    |SINO;
        |SI #11#8 = "D"; aContrapartida = #10#10; |FINSI;
        |SI #11#8 = "H"; aContrapartida = #10#8;  |FINSI;
   |FINSI;

   #1#0 = aContrapartida;
   |LEE 001#1=;
   |SI FSalida = 0;
       aNomContra = #1#2;
   |FINSI;
|FPRC;

|PROCESO GrabaAuxiliar;
  nExis = 0;
  #330#0 = #11#6;
  |LEE 001#330=;
  |SI FSalida = 0; |Y #330#7 = "S";
       nExis = 1;
  |FINSI;

  |SI nExis = 0; |ACABA; |FINSI;

  nExis    = 0;
  aNif     = "";
  aNombre  = "";
  eaCuenta = #11#4;

  #18#23 = "C";
  #18#10 = eaCuenta;
  |LEE 001#18=;
  |SI FSalida = 0;
      nExis   = 1;
      aNombre = #18#1;
      aNif    = #18#9;
  |SINO;
      #18#23 = "P";
      #18#10 = eaCuenta;
      |LEE 001#18=;
      |SI FSalida = 0;
          nExis   = 1;
          aNombre = #18#1;
          aNif    = #18#9;
      |FINSI;
  |FINSI;

  |SI nExis = 0; |ACABA; |FINSI;

  quince = "               ";
  |SI #0#65 = "S";
      nExis = 0;
      |SI aNif = #0#66; |Y #0#66 ! quince; nExis = 1; |FINSI;
      |SI aNif = #0#67; |Y #0#67 ! quince; nExis = 1; |FINSI;
      |SI aNif = #0#68; |Y #0#68 ! quince; nExis = 1; |FINSI;
      |SI aNif = #0#69; |Y #0#69 ! quince; nExis = 1; |FINSI;

      |SI aNif = quince; |Y #0#66 = quince; |Y #0#67 = quince;
             |Y #0#68 = quince; |Y #0#69 = quince;
             nExis = 1;
      |FINSI;
      |SI nExis = 0; |ACABA; |FINSI;
  |FINSI;

  |SI #0#64 ! "A";
      |SI #18#23 ! #0#64; |ACABA; |FINSI;
  |FINSI;

  |HAZ BuscaContra;

  eOpDep = 0;
  enActividad = #11#21;
  eaCodDep    = #11#21;
  |HAZ Actividad;

  enConcepto  = #11#6;
  eaCodSubdep = #11#28;
  |HAZ SubActividad;

  nAny  = 0;
  nPer  = 0;
  aSeg  = "N";
  aRend = "N";

  #347#0 = #11#0;
  #347#1 = #11#1;
  #347#2 = #11#2;
  #347#3 = #11#3;
  |LEE 041#347=;
  |SI FSalida = 0;
      nAny  = #347#4;
      nPer  = #347#5;
      aSeg  = #347#7;
      aRend = #347#8;
  |SINO;
      |SI #11#28 ! "   ";
          |SI enTipoTribut ! 6; |Y enTipoTribut ! 7; |Y enTipoTribut ! 11;
              |Y enTipoTribut ! 12; |Y enTipoTribut ! 16; |Y enTipoTribut ! 17;
             aRend = "S";
          |FINSI;
      |FINSI;
  |FINSI;

  nGrabo = 0;
  |SI #0#21 = "S";              nGrabo = 1;  |FINSI;
  |SI #0#21 = "N"; |Y nAny = 0; nGrabo = 1;  |FINSI;
  |SI #0#21 = "E";
      |SI nAny ] #0#23; |Y nAny [ #0#24; nGrabo = 1;  |FINSI;
  |FINSI;

|| ... Grabo el Auxiliar
  |SI nGrabo = 1;
      nConta = nConta + 1;

      |DDEFECTO #900;
      #900#23 = nConta;

      #900#0  = #11#0;
      #900#1  = #11#1;
      #900#2  = #11#2;
      #900#3  = #11#3;
      #900#4  = #11#4;
      #900#5  = aNombre;
      #900#6  = #11#6;
      #900#7  = #11#7;
      |SI #11#8 = "D";
          #900#8 = #11#9;
          #900#9 = 0;
      |SINO;
          #900#8 = 0;
          #900#9 = #11#9;
      |FINSI;
      #900#10  = nAny;
      #900#11  = nPer;
      #900#12  = aNif;
      #900#13  = #11#21;
      #900#14  = eaNombreAct;
      #900#15  = #11#28;
      #900#16  = eaNomSubdep;
      #900#17  = aContrapartida;
      #900#18  = aNomContra;
      #900#19  = nRegistro;
      #900#20  = enEmpresa;
      #900#21  = enPeriodo;
      #900#22  = dirfich0;
      #900#24  = #18#23;
      #900#25  = aSeg;
      #900#26  = aRend;
      |GRABA 020#900n;
      nSwAlgun = 1;
  |FINSI;
|FPRC;

|DEFBUCLE CreaAuxiliar;
   #11#1;
   4;
   #0#0, #0#4, #0#2, 0001, aDCuenta;
   #0#1, #0#5, #0#3, 9999, aHCuenta;
   e;
   NULL,GrabaAuxiliar;
|FIN;

|PROCESO HazTodo;
   |ABRE #1;
   |ABRE #10;
   |ABRE #18;
   |ABRE #31;
   |ABRE #347;
   |ABRE #900;
   |ABRE #330;
   |SI #0#6 = "S";
       aDCuenta = #0#7;
       aHCuenta = #0#8;
       |BUCLE CreaAuxiliar;
   |SINO;
       |PARA nCampo = 9; |SI nCampo [ 20; |HACIENDO nCampo = nCampo + 1;
             |SI #0nCampo ! doce;
                 aDCuenta = #0nCampo;
                 aHCuenta = #0nCampo;
                 |BUCLE CreaAuxiliar;
             |FINSI;
       |SIGUE;
   |FINSI;
   |CIERRA #330;
   |CIERRA #900;
   |CIERRA #347;
   |CIERRA #31;
   |CIERRA #18;
   |CIERRA #10;
   |CIERRA #1;

   |SI nSwImprime = 1; |Y aParam = "I";
       |SI nConta ! 0; |HAZ ElInforme; |FINSI;
   |FINSI;
|FPRC;

|| ********************************************************************
|PROCESO Actual;
 |SI #0#22 = "S";
     |CIERRA #347;
     dirfich0 = #900#22; |QBF dirfich0;
     |PATH_DAT #347 dirfich0;
     |ABRE #347;
 |FINSI;

 #347#0 = #900#0;
 #347#1 = #900#1;
 #347#2 = #900#2;
 #347#3 = #900#3;
 |LEE 101#347=;
 |SI FSalida ! 0;
     |DDEFECTO #347;
     #347#0 = #900#0;
     #347#1 = #900#1;
     #347#2 = #900#2;
     #347#3 = #900#3;
     |GRABA 020#347b;
 |FINSI;
 #347#4 = #900#10;
 #347#5 = #900#11;
 #347#6 = #900#19;
 #347#7 = #900#25;
 #347#8 = #900#26;
 |GRABA 020#347a;
 |LIBERA #347;
 |SI #0#22 = "S";
     |CIERRA #347;
 |FINSI;
|FPRC;

|DEFBUCLE Actual;
   #900#1;
   ;
   0000000001;
   9999999999;
   ;
   NULL, Actual;
|FIN;

|PROCESO Actualiza;
   |SI #0#22 = "N"; |ABRE #347; |FINSI;
   |BUCLE Actual;
   |SI #0#22 = "N"; |CIERRA #347; |FINSI;
|FPRC;

|PROCESO AuxMin;
   #900#0 = 00;
   #900#1 = #0#4;
   #900#2 = 000001;
   #900#3 = 0001;
|FPRC;

|PROCESO AuxMax;
   #900#0 = 99;
   #900#1 = #0#5;
   #900#2 = 999999;
   #900#3 = 9999;
|FPRC;

|CALCULO impre; |TIPO 3;

   aFichero = ("4z" + Usuario) % 108;
   |NOME_DAT #900 aFichero;
   |DELINDEX #900;

   |DBASE aMas;
   aDef = aMas + "def/caw00043.mas";
   |CARGA_DEF aDef, imagen@;
   |SI FSalida ! 0;
       |MENAV "    Error cargando def caw00043";
       |ACABA;
   |FINSI;
   |NOME_DAT #901 aFichero;

   aDTipo = " "; aHTipo = "z";
   |SI #0#64 ! "A";
       aDTipo = #0#64;
       aHTipo = #0#64;
       eaCoPa = "Cobros";
       |SI #0#64 = "P"; eaCoPa = "Pagos"; |FINSI;
   |FINSI;

   aAlfa1     = #0#5;
   aAlfa1     = aAlfa1 % -104;
   |SI #0#21 = "E";  aAlfa1 = #0#24; |FINSI;
   nAny1      = aAlfa1;
   nAny2      = nAny1 - 1;
   nAny3      = nAny1 - 2;
   nAny4      = nAny1 - 3;
   nAny5      = nAny1 - 4;

   nSwImprime = 0;
   |SI aParam = "I";  nCerrar = 0; |FINSI;

   nSwAlgun   = 0;
   nConta     = 0;
   |SI #0#22 = "N";

       |SI #48#27 = "S"; |Y aParam = "I";
           nSwImprime = 1;
           |HAZ MultiEmpresa;
       |SINO;
           |DFICE dirfich0; |QBF dirfich0;
           |SI aParam = "I"; nSwImprime = 1; |FINSI;
           |HAZ HazTodo;
       |FINSI;

   |SINO;

       eSwMul = 1;
       |SI #48#27 = "S";  |Y aParam = "I"; eSwMul = 2; |FINSI;
       |HAZ MultiEjercicio;

   |FINSI;

   |SI nSwAlgun ! 0;
       |SI aParam ! "I";
           |PINPA #0#900;
           |ENTLINEAL #2#900, -1, 4, 22, 0, AuxMin, AuxMax;
           |CONFI "    SLos Datos introducidos son Correctos ";
           |SI FSalida = 0;
               |HAZ Actualiza;
           |FINSI;
       |FINSI;
   |SINO;
       |MENAV "    No existe Apuntes dentro de la seleccion informada";
   |FINSI;

   |SI nSwPide = 1;
       |FININF;
       |FINIMP;
   |FINSI;

   |DESCARGA_DEF #imagen@;
   |DELINDEX #900;
|FCAL;

|| ********************************************************************
|PROCESO MultiEmp1;
   nSitua = 1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;
   |SI #0#4 > #caselem1#5; |O #0#5 < #caselem1#4;
       |ACABA;  || Fuera de la seleccion
   |FINSI;

   |PATH_DAT #1   dirfich0;
   |PATH_DAT #10  dirfich0;
   |PATH_DAT #11  dirfich0;
   |PATH_DAT #18  dirfich0;
   |PATH_DAT #31  dirfich0;
   |PATH_DAT #200 dirfich0;
   |PATH_DAT #347 dirfich0;

   |ABRE #200;
   |LEE 101#200p;
   |SI FSalida = 0; |Y #200#164 = "N";
       #200#164 = "S";
       |GRABA 020#200a;
   |FINSI;
   |LIBERA #200;
   |CIERRA #200;

   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
   #caselem1#1;
   ;
   000001;
   999999;
   ;
   NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
   |SUB_EJECUTA "caselemp";
   |NOME_DAT #998 eaFichsel;
   |BUCLE MultiEmp0;
   |DELINDEX #998;
|FPRC;

|| .....

|DEFBUCLE MultiEmpEjer;
  #caselem1#2;
  ;
  #997#0, "01.01.0000";
  #997#0, "31.12.2999";
  ;
  NULL, MultiEmp1;
|FIN;

|PROCESO MultiEjer1;

   nConta = 0;
   |DELINDEX #900;
   |BUCLE MultiEmpEjer;

   |SI nConta ! 0; |Y aParam = "I";
       |HAZ ElInforme;
   |FINSI;
|FPRC;

|DEFBUCLE MultiEjer0;
   #caselem0#1;
   ;
   00001;
   99999;
   ;
   NULL,MultiEjer1;
|FIN;

|PROCESO MultiEjercicio;
   espe15 = "S";
   |SUB_EJECUTA "caselemp";
   |NOME_DAT #998 eaFichsel;
   |NOME_DAT #997 eaFichres;

   nEmpMulti = -1;
   |BUCLE MultiEjer0;
   |DELINDEX #998;
   |DELINDEX #997;
|FPRC;

|| ********************************************************************

|PROGRAMA;
  |HAZ inicio;
  CodigoEmpresa    = enEmpresa;
  PeriodoEmpresa   = enPeriodo;
  |DFICE dirfichOri;

  |HAZ DirAplicSt;

  |HAZ eLeeParametro;
  |SI eaCP347 = "N";
      |CONFI "    SNo tiene activado el Control de Cobros y Pagos para el Modelo 347. Continuar ";
      |SI FSalida = 0;
          |ABRE #200;
          |LEE 101#200p;
          |SI FSalida ! 0;
              |DDEFECTO #200;
              |GRABA 020#200b;
          |FINSI;
          #200#164 = "S";
          |GRABA 020#200a;
          |LIBERA #200;
          |CIERRA #200;
          eaCP347 = "S";
      |FINSI;
  |FINSI;
  |SI eaCP347 = "N";  |ACABA; |FINSI;

  aParam = PARAMETRO; |QBF aParam;

  |CLS;
  |PINPA #0#0;
  |SI aParam ! "I";
      |C_V #0#25,1,"N";
      |C_V #0#26,1,"N";
      |C_V #0#63,1,"N";
      |ATRI R;
      |PINTA 0507, "Relacion y Modificacion de Apuntes de Cobros/Pagos en Efectivo";
      |ATRI 0;
      |PINTA 2007, "À"; |PINTA 2073, "Ù";
      |PINTA 2107, "                                                                   ";
      |PINTA 2207, "                                                                   ";
      |PINTA 2307, "                                                                   ";
  |FINSI;
  |PINDA #0#0;

  |ENDATOS #1#0;

|FPRO;
