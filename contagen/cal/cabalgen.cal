|FICHEROS;
   cabalgen #0;
   cacuenta #1;         || cuentas
   caexistc #6;         || descarga cabs.
   caexistl #7;         || descarga lins.
   caconimp #8;

   canempre #30;
   caselem1 #998;

   cawdatos #900;
|FIN;

|VARIABLES;
   &pathbal  = "";
   &balance  = 1;
   &aparam   = "";
   &entrada  = 1;
   informe  = "cabalgen";
   alfa1     = "";
   nNume1   = 0;
   nNume2   = 0;
   nivel     = 0;
   traraf    = 0;

   &r_emp = 0;
   &r_per = 0;
   &d_mes = 0;
   &h_mes = 0;
   &estru = 0;
   &r_eje = 0;
   &r_nom = "";

   &eldesde  = 0;
   &elhasta  = 0;
   des_exi   = 0;
   has_exi   = 0;
   linea     = 0;
   descar    = 0;
   d_cuen    = "";
   h_cuen    = "";
   act_cuent = "";
   act_nivel = 0;

   &sw_inf = 0;
   &debe     = 0;
   &haber    = 0;
   &activo   = 0;
   &pasivo   = 0;
   &gastos   = 0;
   &ingresos = 0;
   &tdebe     = 0;
   &thaber    = 0;
   &tactivo   = 0;
   &tpasivo   = 0;
   &tgastos   = 0;
   &tingresos = 0;
   &swnocal   = 0;

   impuesto = 0;
   &vari1 = "";
   &vari2 = "";
   &ac1 = 0;
   &pa1 = 0;
   &ga1 = 0;
   &in1 = 0;
   &ac2 = 0;
   &pa2 = 0;
   &ga2 = 0;
   &in2 = 0;
   &ac3 = 0;
   &pa3 = 0;
   &ga3 = 0;
   &in3 = 0;

   x = 0;
   y = 0;
   sw_exp = 0;
   sw = 0;
   aNomCta = "";
|FIN;

|INCLUYE dscont_i;
|INCLUYE digito_i;
|INCLUYE masivo_i;

|PROCESO antesque;
   aNomCta = "cacuenta";
   aparam = PARAMETRO;
   |QBF aparam; |SI aparam = " N"; aparam = ""; |FINSI;
   |SI aparam = "fech";
      aNomCta = "cabalano";
   |FINSI;
   |NOME_DAT #1 aNomCta;
|FPRC;

|| ========================================================================
||                        CALCULOS PANTALLA
|| ========================================================================
|CALCULO inicio_b; |TIPO 8;
  |HAZ inicial;
  |SI swerror = 2; |PTEC 807; |ACABA; |FINSI;
  |SI swerror = 3; |PTEC 831; |PTEC 807; |ACABA; |FINSI;

  |HAZ DirAplicSt;
|FCAL;

|CALCULO estruc7; |TIPO 7;
 #0#6 = 1;
 |SI Haybasica = 1; |Y eaDepar = "S";
     #0#6 = 0;
 |FINSI;
 |PINTA #0#6;
|FCAL

|CALCULO elIS;
  alfa1 = #0#14;
  |C_M #0#16,1, alfa1;
|FCAL;

|PROCESO pintames; |TIPO 0;
   nNume1 = Campo + 2;
   |SI #0Campo ! 0; |Y #0Campo ! 13;
      NumMes = dig1 + (#0Campo - 1);
      |SI NumMes > 12; NumMes = NumMes - 12; |FINSI;
   |SINO;
      NumMes = #0Campo;
   |FINSI;
   |HAZ NombreMes;
   #0nNume1 = NombreMes;
   |PINTA #0nNume1;                       || Pinta el nombre del mes

   |SI Campo = 1; |Y  #0#0 > #0#1;
      |MENAV "    Error de Entrada ...";
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO pintames2; |TIPO 0;
   |SI #0Campo ! 0; |Y #0Campo ! 13;
      NumMes = #canempre#11 + (#0Campo - 1);   || Operacion para saber que mes es
      |SI NumMes > 12; NumMes = NumMes - 12; |FINSI;
   |SINO;
      NumMes = #0Campo;
   |FINSI;
   |HAZ NombreMes;
   |ATRI I;
   |SI Campo = 7;  |PINTA 1424, NombreMes;  |FINSI;
   |SI Campo = 8;  |PINTA 1460, NombreMes;  |FINSI;
   |ATRI 0;
   |SI Campo = 8;  |Y #0#7 > #0#8;
      |MENAV "    Error de Entrada ...";
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO miraniv; |TIPO 0;
   nivel = #0#5;
   |SI nivel = 1;|Y dig4 = 0;  |AVISO;  |ERROR;  |FINSI;
   |SI nivel = 2;|Y dig5 = 0;  |AVISO;  |ERROR;  |FINSI;
   |SI nivel = 3;|Y dig6 = 0;  |AVISO;  |ERROR;  |FINSI;
   |SI nivel = 4;|Y dig7 = 0;  |AVISO;  |ERROR;  |FINSI;
   |SI nivel = 5;|Y dig8 = 0;  |AVISO;  |ERROR;  |FINSI;
   |SI nivel = 6;|Y dig9 = 0;  |AVISO;  |ERROR;  |FINSI;
|FPRC;

|PROCESO estruct; |TIPO 0;
   |SI #0#6 = 0;  |ACABA;  |FINSI;
   |ABRE #6;
   #caexistc#0 = #0#6;
   |LEE 040#6=;
   |SI FSalida ! 0;
      |MENAV "    No existe la estructura de descarga.";
      |CIERRA #6;
      |ERROR;
      |ACABA;
   |FINSI;
   |CIERRA #6;
|FPRC;

|PROCESO conexi;
   enTExi = 1;
   d_mes = #0#7;
   h_mes = #0#8;
   estru = #0#6;
   r_emp = empre;
   r_per = ejerc;
   r_eje = #canempre#26;
   r_nom = #canempre#1;
   |SUB_EJECUTA "caexicon";
|FPRC;

|| ***************************************************************************
|| *********************  CALCULO DE IMPRESION *******************************
|| ***************************************************************************
|PROCESO acortacuen;
   |SI #cacuenta#1 < act_nivel;
      |SI #cacuenta#1 = 1;  traraf = 100 + dig4;  |FINSI;
      |SI #cacuenta#1 = 2;  traraf = 100 + dig5;  |FINSI;
      |SI #cacuenta#1 = 3;  traraf = 100 + dig6;  |FINSI;
      |SI #cacuenta#1 = 4;  traraf = 100 + dig7;  |FINSI;
      |SI #cacuenta#1 = 5;  traraf = 100 + dig8;  |FINSI;
      act_cuent = act_cuent % traraf;
      act_nivel = #cacuenta#1;
      act_cuent = act_cuent + "              ";
      act_cuent = act_cuent % 112;
   |FINSI;
|FPRC;

|PROCESO exis_expl;

   descar = #caexistl#des_exi# - #caexistl#has_exi#; || iniciales - finales
   |SI descar = 0; |ACABA; |FINSI;
   |SI descar ] 0;
      act_cuent = #caexistl#5;     || cuenta descarga D
      act_nivel = #caexistl#6;     || digito descarga D
   |SINO;
      act_cuent = #caexistl#8;     || cuenta descarga H
      act_nivel = #caexistl#9;     || digito descarga H
   |FINSI;

   eaCuenta = act_cuent;
   |HAZ SacaNivel;
   act_nivel = enNivel;

   |HAZ acortacuen;
   |SI act_cuent = #cacuenta#0;
      sw_exp = 1;
      |SI descar ] 0;
         debe  = debe  + descar;
      |SINO;
         haber = haber - descar;
      |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE exis2;
   #7#1;
   ;
   #0#6, 01;
   #0#6, 99;
   e;
   NULL, exis_expl;
|FIN;

|PROCESO exis_3000;
   sw_exp  = 1;
   debe    = debe  + #caexistl#des_exi# + #caexistl#has_exi#;
   haber   = haber + #caexistl#des_exi#;
|FPRC;

|DEFBUCLE exis1;
   #7#2;
   ;
   #0#6, d_cuen;
   #0#6, h_cuen;
   e;
   NULL,exis_3000;
|FIN;

|PROCESO c_exis1;
   d_cuen = #cacuenta#0;
   h_cuen = #cacuenta#0;
   |SI #cacuenta#1 = 1;  traraf = 100 + dig4;  |FINSI;
   |SI #cacuenta#1 = 2;  traraf = 100 + dig5;  |FINSI;
   |SI #cacuenta#1 = 3;  traraf = 100 + dig6;  |FINSI;
   |SI #cacuenta#1 = 4;  traraf = 100 + dig7;  |FINSI;
   |SI #cacuenta#1 = 5;  traraf = 100 + dig8;  |FINSI;
   d_cuen = d_cuen % traraf;
   d_cuen = d_cuen + "              ";
   d_cuen = d_cuen % 112;
   h_cuen = h_cuen % traraf;
   h_cuen = h_cuen + "99999999999999";
   h_cuen = h_cuen % 112;
   |BUCLE exis1;
|FPRC;

|PROCESO impresio;
   |HAZ digitos2;  || ... Comprueba nivel de Cuenta para imprimir
   |SI d_error ! 0;                         |ACABA; |FINSI;
   |SI #cacuenta#1 > nivel;                 |ACABA; |FINSI;
   |SI #cacuenta#3 = "N";  |Y #cacuenta#1 ! nivel; |ACABA; |FINSI;

   debe     = 0; || ... Calcular cantidades
   haber    = 0;
   activo   = 0;
   pasivo   = 0;
   gastos   = 0;
   ingresos = 0;
   sw_exp  = 0;
   alfa1 = #cacuenta#0 % 101;
   |SI alfa1 = "3";                 |HAZ c_exis1; |FINSI;
   |SI alfa1 = "6"; |O alfa1 = "7"; |BUCLE exis2; |FINSI;
   |SI sw_exp = 0;
      |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
         y = x + 1;
         debe  = debe  + #cacuenta#x#;
         haber = haber + #cacuenta#y#;
      |SIGUE;
   |FINSI;

   |SI debe = 0; |Y haber = 0; |ACABA; |FINSI;

   |SI #cacuenta#5 = "A"; activo   = debe - haber; |FINSI;
   |SI #cacuenta#5 = "P"; pasivo   = haber - debe; |FINSI;
   |SI #cacuenta#5 = "D"; gastos   = debe - haber; |FINSI;
   |SI #cacuenta#5 = "H"; ingresos = haber - debe; |FINSI;
   sw_inf = 0;
   |PRINT;
   sw = 1;
   tdebe     = tdebe     + debe;
   thaber    = thaber    + haber;
   tactivo   = tactivo   + activo;
   tpasivo   = tpasivo   + pasivo;
   tgastos   = tgastos   + gastos;
   tingresos = tingresos + ingresos;
|FPRC;

|DEFBUCLE cabalgen1;
   #1#1;
   ;
   "            ";
   "zzzzzzzzzzzz";
   e;
   NULL,impresio;
|FIN;

|PROCESO borrar;
   activo   = 0;
   pasivo   = 0;
   gastos   = 0;
   ingresos = 0;

   debe     = 0;
   haber    = 0;

   tdebe     = 0;
   thaber    = 0;
   tactivo   = 0;
   tpasivo   = 0;
   tgastos   = 0;
   tingresos = 0;
   sw = 0;
|FPRC;


|| ******************************** CALCULO TIPO 3
|PROCESO HazTodo;

  |HAZ antesque;
  |SI #0#15 = "S"; |HAZ conexi; |FINSI;

  eaIa = #canempre#21;
  enM1 = #0#0;
  enM2 = #0#1;
  |HAZ LeeDatosEmpresa;
  |SI enMAper ! dig1;
      |SI #0#1 < enMAper; |Y #0#1 ! 0;
          |ACABA;
      |FINSI;
  |FINSI;


  ||... Modificar Cuentas a balance segun saldo
  r_emp = empre;
  r_per = ejerc;
  eldesde = (#0#0 * 3) + 11;
  elhasta = (#0#1 * 3) + 11;
  enSwOper = 0;
  |SUB_EJECUTA "carecsig";

  |ABRE #8;
  #8#0 = 1;
  |LEE 001#8=;
  |CIERRA #8;
  |SI #caconimp#2 = "S";  |Y #caconimp#3 = "N";  |Y aparam = "";
      pathbal = dirfich0;
      balance = 1;
      |SI #caconimp#10 = "S";
           enSwOper = 2;
           |SUB_EJECUTA "carecsig";
           enSwOper = 0;
           |SI enECuadre = 1; |ACABA; |FINSI;
           |NOME_DAT #1 eaNomFCta;
      |SINO;
           |SUB_EJECUTA "careccue";
      |FINSI;
  |FINSI;

  eldesde = (#0#0 * 3) + 9;
  elhasta = (#0#1 * 3) + 9;
  des_exi = #0#7 + 11
  has_exi = #0#8 + 11
  || ---------------------------------------------------------------------

  |ABRET #1;
  |ABRET #7;
  |BUCLE cabalgen1;
  |HAZ final;
  |CIERRAT #1;
  |CIERRAT #7;
  |SI sw = 1; |PIEINF; |FINSI;
  |HAZ borrar;
|FPRC;

|| ******************************** CALCULO TIPO 3
|PROCESO impre; |TIPO 3;

   |SI enSwMvo = 0; |Y aparam ! "Impuesto";
       |IMPRE 0;
       |SI FSalida ! 0;  |ACABA;  |FINSI;
   |FINSI;

   |INFOR informe;
   |SI FSalida ! 0;
       |MENAV "     Error en informe.";
       |SI enSwMvo = 0; |Y aparam ! "Impuesto"; |FINIMP; |FINSI;
       |ACABA;
   |FINSI;

   |SI #48#27 = "S"; |Y enSwMvo = 0; |Y aparam ! "Impuesto";
      |HAZ MultiEmpresa;
   |SINO;
      |SI aparam ! "Impuesto"; |DFICE dirfich0; |FINSI;
      |HAZ HazTodo;
   |FINSI;
   |FININF;
   |SI enSwMvo = 0; |Y aparam ! "Impuesto"; |FINIMP; |FINSI;
|FPRC;

|| ******************************* C lculo Final
|PROCESO final;
   ac1 = 0; pa1 = 0; ga1 = 0; in1 = 0;
   ac2 = 0; pa2 = 0; ga2 = 0; in2 = 0;
   ac3 = 0; pa3 = 0; ga3 = 0; in3 = 0;

   pa1 = tactivo - tpasivo;
   |SI pa1 < 0; ac1 = - pa1; pa1 = 0; |FINSI;

   vari1 = "BENEFICIOS ANTES DE IMPUESTOS";
   vari2 = "BENEFICIOS DESPUES DE IMPUESTOS";
   ga1 = tingresos - tgastos;
   impuesto = (ga1 % #0#16) ! EURODB;
   |SI ga1 < 0;
      in1 = - ga1;
      ga1 = 0;
      vari1 = "PERDIDAS ANTES DE IMPUESTOS  ";
      vari2 = "PERDIDAS DESPUES DE IMPUESTOS";
   |FINSI;

   |SI impuesto ] 0;
      ac2 = 0;        ga2 = impuesto;
      pa2 = impuesto; in2 = 0;
   |SINO;
      ac2 = -impuesto; ga2 = 0;
      pa2 = 0;         in2 = -impuesto;
   |FINSI;

   |SI #0#14 = "N";  ac2 = 0; pa2= 0; ga2 = 0; in2 = 0; swnocal = 1; |FINSI;
   ac3 = ac1 - ac2;
   pa3 = pa1 - pa2;
   ga3 = ga1 - ga2;
   in3 = in1 - in2;

|FPRC;

|| ***********************************************************************
|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   nYaLoHeDicho = 0;
   |PATH_DAT #1 dirfich0;
   |PATH_DAT #6 dirfich0;
   |PATH_DAT #7 dirfich0;
   |PATH_DAT #8 dirfich0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 000001;
 999999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
   |SUB_EJECUTA "caselemp";
   |NOME_DAT #998 eaFichsel;
   |BUCLE MultiEmp0;
   |DELINDEX #998;
|FPRC;

|PROCESO MesNom;
     nNume1 = nNume2 + 2;
     |SI #0nNume2 ! 0; |Y #0nNume2 ! 13;
         NumMes = dig1 + (#0nNume2 - 1);
         |SI NumMes > 12; NumMes = NumMes - 12; |FINSI;
     |SINO;
          NumMes = #0nNume2;
     |FINSI;
     |HAZ NombreMes;
     #0nNume1 = NombreMes;
|FPRC;

|PROGRAMA;
 aparam = PARAMETRO; |QBF aparam;
 |SI enSwMvo = 1;
     |HAZ inicio_b;
     |DDEFECTO #0;
     alfa1 = efDFecM; alfa1 = alfa1 % 402; #0#0 = alfa1;
     alfa1 = efDFecM; alfa1 = alfa1 % 105;
     |SI alfa1 = "01.01"; #0#0 = 0; |FINSI;
     alfa1 = efHFecM; alfa1 = alfa1 % 402; #0#1 = alfa1;
     nNume2 = 0; |HAZ MesNom;
     nNume2 = 1; |HAZ MesNom;
     #0#5   = dig3; nivel = #0#5;
     #0#6   = 1; |SI Haybasica = 1; |Y eaDepar = "S"; #0#6 = 0; |FINSI;
     |HAZ impre;
 |SINO;
      |SI aparam = "Impuesto";
          |DDEFECTO #0;
          nNume2 = 0; |HAZ MesNom;
          nNume2 = 1; |HAZ MesNom;
          #0#5   = dig3 - 1; nivel = #0#5;
          #0#6   = 1; |SI Haybasica = 1; |Y eaDepar = "S"; #0#6 = 0; |FINSI;
          |PATH_DAT #1 dirfich0;
          |PATH_DAT #6 dirfich0;
          |PATH_DAT #7 dirfich0;
          |PATH_DAT #8 dirfich0;
          |HAZ impre;
      |SINO;
          |MANTE #0;
      |FINSI;
 |FINSI;
|FPRO;
