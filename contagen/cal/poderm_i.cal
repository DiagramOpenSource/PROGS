|FICHEROS;
 caivam01 #123;
 caivam02 #122;
 camaasic #1;

 dscam000@ #100;
 dscam067@ #101;
|FIN;

|VARIABLES;
     aAlfa      = "";
     aAlfa1     = "";
     aAlfa2     = "";
     aDescri    = "";
     aPassword  = "";
     aClave     = "";
     aHora      = "";
     &ant_dia   = 0;
     &ant_fec   = @;
     &ant_doc   = 0;
     &vie_dia   = 0;
     &vie_fec   = @;
     &vie_doc   = 0;
     nEstaEnCartera = 0;
     &enSwBaja  = 0;
|FIN;

|INCLUYE dscont_i;

|PROCESO eDejarModificar;
     eaTexError = "";
     enSwDejar = 0;

     |SI enTipoAsto = 150; |HAZ  eMiroCarMof; |FINSI;
     |SI enTipoAsto = 103; |O enTipoAsto = 104;
        |O enTipoAsto = 106; |O enTipoAsto = 190; |ACABA; |FINSI;

     |ABRE #123;
     #123#0 = enEmpresa;
     |LEE 001#123=;
     |SI FSalida ! 0;
         |CIERRA #123;
         |ACABA;
     |FINSI;
     |CIERRA #123;

     |SI enTipoAsto ! 110; |Y enTipoAsto ! 150; || no gestion sin RegIVA
                                                || ni Cartera
         |SI #123#1 = "N"; |ACABA; |FINSI; ||no puedo podifica
     |SINO;
         |SI enTipoAsto = 110; |Y #123#3 = "N"; |ACABA; |FINSI; ||no puedo podifica
         |SI enTipoAsto = 150; |Y #123#4 = "N"; |ACABA; |FINSI; ||no puedo podifica
     |FINSI;

     aClave = #123#2; |QBF aClave;

     aPassword  = "";

     |PUSHV 0501 2380;
     |BLANCO 0918 1569;
     |CUADRO 0918 1569;
     |SI enTipoAsto ! 110; |Y enTipoAsto ! 150;
             aDescri = " MODIFICAR: CONTRAPARTIDAS                   ";
         |SI enSwAna ! 0;
             aDescri  = " MODIFICAR: CONTRAPARTIDAS Y CTAS ANALITICAS     ";
         |FINSI;

         |ATRI P; |PINTA 1020, " ATENCION. FRA DESDE PASE DE GESTION. SOLO PODRA ";
                  |PINTA 1120, aDescri;
     |SINO;
         |SI enTipoAsto = 110;
             aDescri = " DE GESTION. ";
         |SINO;
             aDescri = " DE CARTERA. ";
         |FINSI;
         |SI enModoEnt = 2;
             |ATRI P; |PINTA 1020, " ATENCION. VA A MODIFICAR UN ASIENTO AUTOMATICO ";
                      |PINTA 1120, aDescri;
         |SINO;
             |ATRI P; |PINTA 1020, " ATENCION. VA A DAR DE BAJA UN ASIENTO AUTOMATICO ";
                      |PINTA 1120, aDescri;
         |FINSI;
     |FINSI;
     |ATRI 0;
     |PINTA 1320, "Para continuar introduzca Password : ";
     E_inf = "";
     E_sup = "10";
     aPassword = 1357 ? -1; |QBF aPassword;

     |SI aPassword ! aClave;
         |MENAV "     Permiso denegado.";
     |SINO;
         enSwDejar = 1;
     |FINSI;
     |POPV;
|FPRC;

|PROCESO eGrabaModificar;
     |ABRE #122;
     #122#0 = ant_doc;
     #122#1 = ant_dia;
     #122#2 = ant_fec;
     |LEE 101#122=;
     |SI FSalida ! 0;
         |DDEFECTO #122;
         #122#0  = ant_doc;
         #122#1  = ant_dia;
         #122#2  = ant_fec;
         |SI vie_doc [ 0;
             #122#9  = ant_dia;
             #122#10 = ant_fec;
             #122#11 = ant_doc;
         |SINO;
             #122#9  = vie_dia;
             #122#10 = vie_fec;
             #122#11 = vie_doc;
         |FINSI;
         #122#12 = enTipoAsto;
         |GRABA 020#122b;
     |FINSI;
     aAlfa = Usuario % 0810;
     #122#3 = aAlfa;
     #122#4 = ~;
     |HORA aHora; aAlfa = aHora % 105;
     #122#5 = aAlfa;
     |SI enSwBaja = 1;
        #122#13 = "B";
     |SINO;
        #122#13 = "M";
     |FINSI;
     |GRABA 020#122a;
     |LIBERA #122;
     |CIERRA #122;
|FPRC;


|PROCESO eMiroCarMof;
      nEstaEnCartera = 0;
      |DBASS aAlfa; |QBF aAlfa;
      aAlfa  = aAlfa + "dscarter/";
      aAlfa1 = aAlfa % 0201; |SI aAlfa1 = ":"; aAlfa = aAlfa % 0399; |FINSI;
      aAlfa1 = aAlfa + "def/dscam000.mas";
      |CARGA_DEF aAlfa1, dscam000@;
      |SI FSalida = 0;
         aAlfa1 = aAlfa + "fich/";
         |PATH_DAT #100 aAlfa1;
         aAlfa1 = aAlfa + "def/dscam067.mas";
         |CARGA_DEF aAlfa1, dscam067@;
         |DBASE aAlfa; |QBF aAlfa;
         |ABRE #dscam000@;
         #dscam000@#0 = 1;
         |LEE 000#dscam000@.];
         |PARA; |SI FSalida = 0; |HACIENDO;
            aAlfa1 = #dscam000@#16; |QBF aAlfa1;
            aAlfa2 = aAlfa1 % 0201; |SI aAlfa2 = ":"; aAlfa1 = aAlfa1 % 0399; |FINSI;
            |SI #dscam000@#14 = enEmpresa; |Y #dscam000@#19 = enPeriodo; |Y aAlfa = aAlfa1;
               |SAL;
            |FINSI;
            |LEE 000#dscam000@.s;
         |SIGUE;
         |SI #dscam000@#14 = enEmpresa; |Y #dscam000@#19 = enPeriodo; |Y aAlfa = aAlfa1;
            |DBASS aAlfa; |QBF aAlfa; aAlfa = aAlfa + "dscarter/";
            aAlfa1 = aAlfa + "fich/" + (("00000" + #dscam000@#0) % -105) + "/";
            |PATH_DAT #101 aAlfa1;
            |ABRE #dscam067@; |SELECT #2#dscam067@;
            #dscam067@#4 = #camaasic#0;
            #dscam067@#5 = #camaasic#1;
            #dscam067@#6 = #camaasic#2;
            #dscam067@#7 = 1;
            #dscam067@#8 = enEmpresa;
            #dscam067@#9 = enPeriodo;
            |LEE 000#dscam067@.];
            |SI FSalida = 0; |Y #dscam067@#4 = #camaasic#0; |Y #dscam067@#5 = #camaasic#1;
             |Y #dscam067@#6 = #camaasic#2; |Y #dscam067@#8 = enEmpresa; |Y #dscam067@#9 = enPeriodo;
                aAlfa = "    Este Asto es la contabilizacion de";
                |SI #dscam067@#0 = "CB"; aAlfa = aAlfa + " un cobro."; |FINSI;
                |SI #dscam067@#0 = "PG"; aAlfa = aAlfa + " un pago.";  |FINSI;
                |SI #dscam067@#0 = "RM";
                   aAlfa1 = #dscam067@#3; |QBF aAlfa1;
                   |SI aAlfa1 = "E";
                      aAlfa = aAlfa + " la emision de una remesa.";
                   |FINSI;
                   |SI aAlfa1 = "V";
                      aAlfa = aAlfa + "l vencimiento de una remesa.";
                   |FINSI;
                |FINSI;
                |SI #dscam067@#0 = "PA";
                   aAlfa1 = #dscam067@#3; |QBF aAlfa1;
                   |SI aAlfa1 = "E";
                      aAlfa = aAlfa + " la emision de un pagare.";
                   |FINSI;
                   |SI aAlfa1 = "V";
                      aAlfa = aAlfa + "l vencimiento de un pagare.";
                   |FINSI;
                |FINSI;
                |SI #dscam067@#0 = "TA";
                   aAlfa1 = #dscam067@#3; |QBF aAlfa1;
                   |SI aAlfa1 = "E";
                      aAlfa = aAlfa + " la emision de un talon.";
                   |FINSI;
                |FINSI;
                |SI #dscam067@#0 = "CF";
                   aAlfa1 = #dscam067@#3; |QBF aAlfa1;
                   |SI aAlfa1 = "E";
                      aAlfa = aAlfa + " la emision de un confirme.";
                   |FINSI;
                   |SI aAlfa1 = "V";
                      aAlfa = aAlfa + "l vencimiento de un confirme.";
                   |FINSI;
                |FINSI;
                eaTexError = aAlfa + "Entre a cartera y modifique desde la opcion correspondiente";
                nEstaEnCartera = 1;
            |FINSI;
            |SELECT #1#dscam067@;
            |CIERRA #dscam067@;
         |FINSI;
         |DESCARGA_DEF #dscam067@;
         |CIERRA #dscam000@;
         |DESCARGA_DEF #dscam000@;
      |FINSI;
|FPRC;
