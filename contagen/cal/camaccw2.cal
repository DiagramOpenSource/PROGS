|FICHEROS;
  camaniva #0;
  caivalin #1;

  cafantas #2;

  camaccw2 #598,MANTE;
  camaccw3 #599,MANTE;

  camacc01 #501;
  camacc02 #502;
  camacc03 #503;

  camacw3@ #999;
|FIN;

|VARIABLES;
  nOpera  = 0;
  nTipo   = 0;
  nLinea  = 0;
  nDLinea = 0;
  nHLinea = 0;
  nNumero = 0;
  aAlfa   = "";
  aAlfa1  = "";
  aAlfa2  = "";
  aAlfa3  = "";
  aAlfa4  = "";
  fFecha1 = @;
  nNume1  = 0;
  nNume2  = 0;
  nNume3  = 0;
  nTp     = 0;
  nModo   = 0;
  aFichero = "";

  nSuma1    = 0;
  nSuma2    = 0;
  nSuma3    = 0;
  nNumBase  = 0;
  nLineaUlt = 0;
  nSwOp     = 0;
  &eSwCambio = 0;
  nExiste    = 0;

  SwPasada = 0;
  nHay502  = 0;
  nHay501  = 0;
  nHay501S = 0;
  nEstado  = 0;
  nSalgo   = 0;

  nTecla = 0;
  nBoton2 = 0;
  nBoton3 = 0;
  nBoton4 = 0;
  nBoton5 = 0;
  nBoton6 = 0;
  nBoton9 = 0;
  sw      = 0;
  aDef    = "";
  aMas    = "";
  nDesde  = 0;
  nHasta  = 0;
  nTope   = 0;
  nConNum   = 0;
  nDImporte = 0;
  nHImporte = 0;
  nSw = 0;
  nLibro = 0;
  nRegis = 0;
  nCobro = 0;
  nDeven = 0;
  aCampo12 = "";
  aCampo13 = "";
  &ePrograma = 0;
  eModo = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoiv_i;

|| /////////////////////////////////////////////////////////////////////
|| /// campos pantalla
|PROCESO LaSuma;
      #501#6  = #501#6  + #502#11;
      #501#7  = #501#7  + #502#12;
      #501#12 = #501#12 + #502#10;
      #501#16 = #501#16 + #502#13;
|FPRC;

|DEFBUCLE LaSuma;
  #502#1;
  ;
  #501#17, #501#0, #501#1, 01;
  #501#17, #501#0, #501#1, 99;
  e;
  NULL, LaSuma;
|FIN;

|PROCESO LeoMacc03;
    |SI nOpera = 0;
        nNumBase = #503#3;
        #caivalin#0 = #503#0;
        #caivalin#1 = #503#1;
        #caivalin#2 = nNumBase;
        |LEE 141#caivalin.=;
        |SI FSalida ! 0;
            |BORRA 020#503a;
            |LIBERA #503;
            |ACABA;
        |SINO;
            |SI #503#3 = nLineaUlt;
                #503#4 = #503#4 + (#502#10 - nSuma1);
                #503#5 = #503#5 + (#502#11 - nSuma2);
                #503#6 = #503#6 + (#502#12 - nSuma3);
                |GRABA 020#503a;
                || hacer cuadre
            |FINSI;
        |FINSI;
    |SINO;
        |BORRA 020#503a;
    |FINSI;
    |LIBERA #503;
|FPRC;

|DEFBUCLE LeoMacc03;
    #503#1;
    ;
    #502#17, #502#0, #502#1, #502#2, 01;
    #502#17, #502#0, #502#1, #502#2, 99;
    be;
    NULL, LeoMacc03;
|FIN;

|PROCESO LeoLineaIVA;
     nNumBase = #caivalin#2;
     |DDEFECTO #503;
     #503#0 = #502#0;
     #503#1 = #502#1;
     #503#2 = #502#2;
     #503#3 = nNumBase;
     #503#7 = #502#17;
     |LEE 141#503=;
     |SI FSalida ! 0;
         |DDEFECTO #503;
         #503#0 = #502#0;
         #503#1 = #502#1;
         #503#2 = #502#2;
         #503#3 = nNumBase;
         #503#7 = #502#17;
         |GRABA 020#503b;
     |FINSI;

|| .. asi el calcul
     #503#4 = ((#caivalin#6 + #caivalin#8 - #caivalin#22) % #502#13) ! 2;
     #503#5 = (#caivalin#6 % #502#13) ! 2;
     #503#6 = (#caivalin#8 % #502#13) ! 2;

     nSuma1 = (nSuma1 + #503#4) ! 2;
     nSuma2 = (nSuma2 + #503#5) ! 2;
     nSuma3 = (nSuma3 + #503#6) ! 2;
     nLineaUlt = #caivalin#2;

     |GRABA 020#503a;
     |LIBERA #503;
|FPRC;

|DEFBUCLE LeoLineaIVA;
  #caivalin#1;
  ;
  #502#0, #502#1, 01;
  #502#0, #502#1, 99;
  e;
  NULL, LeoLineaIVA;
|FIN;

|PROCESO ActuaCw2;
  #502#17= #599#28;
  #502#0 = #599#0;
  #502#1 = #599#1;
  #502#2 = nLinea;
  |LEE 141#502=;
  |SI FSalida ! 0;
      |DDEFECTO #502;
      #502#0 = #599#0;
      #502#1 = #599#1;
      #502#2 = nLinea;
      #502#17 = #599#28;
      #502#16 = #599#29;
      |GRABA 020#502b;
  |FINSI;
  #502#3  = #598#0;
  #502#4  = #598#1;
  #502#5  = #598#2;
  #502#6  = #598#3;
  aAlfa1  = #598#1; aAlfa1 = aAlfa1 % 402;
  #502#7  = aAlfa1;
  #502#10 = #599#18;
  #502#11 = #599#19;
  #502#12 = #599#20;
  #502#13 = #599#23;
  #502#14 = #598#12;
  #502#15 = #598#13;
  |GRABA 020#502a;

  |SI eSwCambio = 1;
     nOpera = 0;
     nSuma1 = 0;
     nSuma2 = 0;
     nSuma3 = 0;
     nLineaUlt = 0;
     ePerioCaja = #599#29; |HAZ ePathCaja;
     |CIERRA #0; |PATH_DAT #0 ePathCaja;
     |CIERRA #1; |PATH_DAT #1 ePathCaja;
     |ABRE #503;      |BUCLE LeoLineaIVA; |CIERRA #503;
     |ABRE #caivalin; |BUCLE LeoMacc03;   |CIERRA #caivalin;
  |FINSI;

  |LIBERA #502;
|FPRC;

|PROCESO AltaCw2;
  nLinea = 1;
  #502#17 = #599#28;
  #502#0 = #599#0;
  #502#1 = #599#1;
  #502#2 = 99;
  |LEE 041#502];
  |SI FSalida ! 0;
      |LEE 041#502u;
  |SINO;
      |LEE 041#502a;
  |FINSI;
  |SI FSalida = 0; |Y #502#0 = #599#0; |Y #502#1 = #599#1; |Y #502#17 = #599#28;
      nLinea = #502#2 + 1;
  |FINSI;
  |HAZ ActuaCw2;
|FPRC;

|PROCESO BajaCw2;
  #502#17= #599#28;
  #502#0 = #599#0;
  #502#1 = #599#1;
  #502#2 = nLinea;
  |LEE 141#502=;
  |SI FSalida = 0;
      nOpera = 1;
      |BUCLE LeoMacc03;
      |BORRA 020#502a;
  |FINSI;
  |LIBERA #502;
|FPRC;

|PROCESO ActuaCw3;
  sw     = 0;
  nLinea = #599#2;
  |SI #599#21 = 0;
      |SI #599#2 ! 0;
          |HAZ ActuaCw2;
      |SINO;
          |HAZ AltaCw2;
      |FINSI;
      sw = 1;
  |FINSI;
  |SI #599#21 ! 0;
      |SI #599#2 ! 0;
          |HAZ BajaCw2;
          sw = 1;
      |FINSI;
  |FINSI;

  |SI sw = 1;
      #501#17 = #599#28;
      #501#0 = #599#0;
      #501#1 = #599#1;
      |LEE 141#501=;
      |SI FSalida = 0;
          #501#12 = 0;
          #501#16 = 0;
          #501#6 = 0;
          #501#7 = 0;
          |CIERRAT #502;
          |BUCLE LaSuma;
          |ABRET #502;
          |GRABA 020#501a;
     |FINSI;
     |LIBERA #501;
  |FINSI;
|FPRC;

|DEFBUCLE ActuaCw3;
  #599#1;
  ;
  #598#0, #598#1, #598#2, #598#3,     0, 0000, 01,0000000001, 00;
  #598#0, #598#1, #598#2, #598#3, nTope, 9999, 99,9999999999, 99;
  ;
  NULL, ActuaCw3;
|FIN;

|PROCESO Actualizacion;
   |SI eSwCambio = 0;
       |SI nHay502 = 1;
           |SI aCampo12 ! #598#12; |O aCampo13 ! #598#13;
               nTope = 0;
               eSwCambio = 2;
           |FINSI;
       |FINSI;
   |FINSI;

   |SI eSwCambio ! 0;
       |ABRE #501;
       |ABRE #502;
       |BUCLE ActuaCw3;
       |CIERRA #502;
       |CIERRA #501;
   |FINSI;
|FPRC;

|| ////////////////////
|PROCESO ClaveAlta02;
  #502#0 = #599#0;
  #502#1 = #599#1;
  #502#2 = 01;
  #502#17 = #599#28;
|FPRC;

|PROCESO ClaveBaja02;
  #502#0 = #599#0;
  #502#1 = #599#1;
  #502#2 = 99;
  #502#17 = #599#28;
|FPRC;

|CALCULO Tipo7Cw318; |TIPO 7;
   aAlfa = "    Pulse <F2> para cuadrar Importe Total ";
   |SI aTipoIVA = "S";
       aAlfa = aAlfa + "pagado";
   |SINO;
       aAlfa = aAlfa + "cobrado";
   |FINSI;
   |MENSA aAlfa;
|FCAL;

|PROCESO CalImporte; |TIPO 0;
   nNume1  = #599#11 - #599#15;
   nNume2  = #598#8  - #598#14;

   |SI FSalida = 10;
       #599#18 = nNume1;
   |FINSI;

   |SI #599#18 > nNume1; #599#18 = nNume1; |FINSI;
   |SI #599#18 > nNume2; #599#18 = nNume2; |FINSI;
   |PINTA #599#18;
|FPRC;

|PROCESO LeoEspejo;
   |SI #999#21 = 0;
       #598#14 = #598#14 - #999#18; |PINTA #598#14;
       #999#15 = #999#15 - #999#18;
       #999#16 = #999#16 - #999#19;
       #999#17 = #999#17 - #999#20;
       #999#22 = #999#22 - #999#23;
       #999#18 = 0;
       #999#19 = 0;
       #999#20 = 0;
       #999#23 = 0;
       #999#21 = 1;
       |SI #999#13 ! #598#9;  #999#21 = 2; |FINSI;
       |GRABA 020#999a;
  |SINO;
       nNume1  = #598#8  - #598#14;
       nNume2  = #999#11 - #999#15;
       |SI nNume1 [ nNume2;
           #999#18 = nNume1;
       |SINO;
           #999#18 = nNume2;
       |FINSI;
       #999#15 = #999#15 + #999#18;
       #598#14 = #598#14 + #999#18; |PINTA #598#14;
       #999#21 = 0;
       |HAZ ElTotalCuota;
       |GRABA 020#999a;
       |SI #598#14 = #598#8;
           |ERROR10;
       |FINSI;
   |FINSI;
   |LIBERA #999;
|FPRC;

|DEFBUCLE LeoEspejo;
   #999#1009;
   ;
   #598#0, #598#1, #598#2, #598#3, nDesde, 0000, 01, 0000000001, 00;
   #598#0, #598#1, #598#2, #598#3, nHasta, 9999, 99, 9999999999, 99;
   e;
   NULL, LeoEspejo;
|FIN;

|DEFBUCLE LeoEspejo_0;
   #999#2004;
   21;
   0000, 01, 0000000001, 00, nDesde;
   9999, 99, 9999999999, 99, nHasta;
   e;
   NULL, LeoEspejo;
|FIN;

|| ////////////////////////////////////////////////////////////////////
|PROCESO Tipo5Cw3;  |TIPO 5;
       |LEE 040#599p;
       #599#21 = 0;
       |LINEAS_BI_ATRI #599#21, D, I, -1;
|FPRC;


|PROCESO Tipo11Cw3; |TIPO 11;

  |SI nSw = 1; |ACABA; |FINSI;
  |SI eModoCaja ! 4;
      |HAZ QuitaBoton;
  |FINSI;

  |SI sw = 0;
      |SI FSalida = 1; |O FSalida = 7;
          nSalgo = 1;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO Tipo7Cw3; |TIPO 7;
  |SI nSw = 1; |ACABA; |FINSI;
  |SI eModoCaja = 4; |ACABA; |FINSI;

  aAlfa1 = "LBOTON,F2 Desasignar ";
  aAlfa2 = "LBOTON,F3 Modif.Importe";
  aAlfa3 = "LBOTON,F4 Desasignar Todo";
  aAlfa4 = "LBOTON,F9 VER TODOS";
  |SI nEstado = 1;
      |SI #599#21 ! 0; aAlfa1 = "LBOTON,F2 Asignar "; |FINSI;
      |SI #599#21 ! 0; aAlfa2 = "LBOTON,F3 Asig. y Mod.Imp"; |FINSI;
      |SI #599#21 ! 0; |Y #598#14 ! #598#8; aAlfa3 = "LBOTON,F4 Asignar todo"; |FINSI;
      aAlfa4 = "LBOTON,F9 VER ASIG.";
  |FINSI;

  |CONTROL_SIMPLE 0, aAlfa1, 1115, 1127, Boton2;
  nBoton2 = FSalida;
  |CONTROL_SIMPLE 0, aAlfa2, 1128, 1140, Boton3;
  nBoton3 = FSalida;
  |CONTROL_SIMPLE 0, aAlfa3, 1141, 1153, Boton4;
  nBoton4 = FSalida;
  |CONTROL_SIMPLE 0, "LBOTON,F5 Consulta Factura", 1154, 1166, Boton5;
  nBoton5 = FSalida;
  |CONTROL_SIMPLE 0, "LBOTON,F6 Busca Importe", 1167, 1179, Boton6;
  nBoton6 = FSalida;

  |CONTROL_SIMPLE 0, aAlfa4, 1101, 1113, Boton9;
  nBoton9 = FSalida;

|FPRC;

|PROCESO QuitaBoton;
 |FIN_CONTROL_WINDOWS nBoton2;
 |FIN_CONTROL_WINDOWS nBoton3;
 |FIN_CONTROL_WINDOWS nBoton4;
 |FIN_CONTROL_WINDOWS nBoton5;
 |FIN_CONTROL_WINDOWS nBoton6;
 |FIN_CONTROL_WINDOWS nBoton9
|FPRC;

|PROCESO Boton2;
  |PTEC 824;
|FPRC;

|PROCESO Boton3;
  |PTEC 825;
|FPRC;

|PROCESO Boton4;
  |PTEC 826;
|FPRC;

|PROCESO Boton5;
  |PTEC 827;
|FPRC;

|PROCESO Boton6;
  |PTEC 828;
|FPRC;

|PROCESO Boton9;
  |PTEC 831;
|FPRC;

|PROCESO ElTotal;
     |PINTA #599#18;
     #598#14 = #598#14 + #599#18; |PINTA #598#14;
     #599#15 = #599#15 + #599#18; |PINTA #599#15;
     |HAZ ElTotalCuota;
|FPRC;

|PROCESO ElTotalCuota;
     nNume3 = (#599#9 + #599#10) ! 2;

     eSwCambio = 1;
     |SI #599#11 = #599#18;
         nTp = 100;
         #599#19 = #599#9;
         #599#20 = #599#10;
     |SINO;
          nTp   = ((#599#18 * 100) / #599#11) ! 4;
          #599#19 = (#599#9  % nTp) ! 2;
          #599#20 = (#599#10 % nTp) ! 2;
          |SI #599#11 = #599#15;
              nTp = (100 - #599#22) ! 4;
              #599#19 = #599#9  - #599#16;
              #599#20 = #599#10 - #599#17;
          |FINSI;
          |SI nNume3 = #599#11;   ||no hay IRPF
              nNume2 = #599#19 + #599#20;
              |SI nNume2 ! #599#18;     ||para cuadrar lo mas posible
                  #599#20 = (#599#20 + (#599#18 - nNume2)) ! 2;
              |FINSI;
          |FINSI;
     |FINSI;
     #599#16 = #599#16 + #599#19;
     #599#17 = #599#17 + #599#20;
     #599#23 = nTp;
     #599#22 = #599#22 + #599#23;
|FPRC;

|PROCESO Tipo0Cw3; |TIPO 0;
  nTecla = FSalida;

  |SI nSw = 1;
      nLibro = #999#0;
      nRegis = #999#1;
      nCobro = #999#2;
      nTipo  = #999#21;
      nDeven = #999#28;
      |ACABA;
  |FINSI;

  |SI nTecla = 0; |ERROR; |ACABA; |FINSI;

  |SI eModoCaja ! 4;
      |HAZ QuitaBoton;
  |FINSI;
  |PULSATECLA;

  |SI #598#16 ! -1;
      eModo = eModoCaja;
      eModoCaja = 4;
  |FINSI;

  |SI eModoCaja = 4; |Y nTecla ] 10; |Y nTecla [ 17;
      |ERROR;
      eModoCaja = eModo;
      |ACABA;
  |FINSI;

  |SI nTecla = 10;

      |LEE 041#599=;

      |SI #599#21 = 0;
          #598#14 = #598#14 - #599#18; |PINTA #598#14;

          #599#15 = #599#15 - #599#18; |PINTA #599#15;
          #599#16 = #599#16 - #599#19;
          #599#17 = #599#17 - #599#20;
          #599#22 = #599#22 - #599#23;
          #599#18 = 0;                 |PINTA #599#18;
          #599#19 = 0;
          #599#20 = 0;
          #599#23 = 0;
          #599#21 = 1;
          |SI #599#13 ! #598#9;  #599#21 = 2; |FINSI;
          |GRABA 020#599a;
          |LIBERA #599;
          |SI #598#14 = 0; nHay502 = 0; |FINSI;
          eSwCambio = 1;
      |SINO;
          nNume1 = #598#14 - #599#18;
          |SI nNume1 = #598#8;
              |MENAV "    Importe totalmente Asignado";
              |LIBERA #599;
              |ACABA;
          |FINSI;

          |SI #599#21 = 2; |Y #599#18 = 0;
              |CONFI "    NEsta factura no tiene el mismo Titular. Continuar";
              |SI FSalida ! 0;
                  |LIBERA #599;
                  |ACABA;
              |FINSI;
          |FINSI;

          #599#15 = #599#15 - #599#18;
          nNume1  = #598#8  - #598#14;
          nNume2  = #599#11 - #599#15;

          |SI nNume1 [ nNume2;
              #599#18 = nNume1;
          |SINO;
              #599#18 = nNume2;
          |FINSI;
          |HAZ ElTotal;
          #599#21 = 0;
          nHay502 = 1;
          |GRABA 020#599a;
          |LIBERA #599;
          eSwCambio = 1;
      |FINSI;
      |PONCONTROL 23, "SI";
  |FINSI;

  |SI nTecla = 11;

      |LEE 041#599=;

      |SI #599#21 = 0;
          #598#14 = #598#14 - #599#18; |PINTA #598#14;
          #599#15 = #599#15 - #599#18; |PINTA #599#15;
          #599#16 = #599#16 - #599#19;
          #599#17 = #599#17 - #599#20;
          #599#22 = #599#22 - #599#23;
      |FINSI;

      |ENCAMPO #18#599;
      |HAZ ElTotal;

      |SI #599#18 = 0;
          #599#21 = 1;
          |SI #599#13 ! #598#9;  #599#21 = 2; |FINSI;
          |SI #598#14 = 0; nHay502 = 0; |FINSI;
      |SINO;
          #599#21 = 0;
          nHay502 = 1;
      |FINSI;

      |GRABA 020#599a;
      |LIBERA #599;
      eSwCambio = 1;
      |ERROR;
      |PONCONTROL 23, "SI";
  |FINSI;

  |SI nTecla = 12;

      |LEE 041#599=;

      nDesde = 0; nHasta = 0;
      nOpera = 0;
      |SI #599#21 ! 0;
          nOpera = 1;
          |SI #598#14 = #598#8; nOpera = 0; |FINSI;

              ||  |MENAV "    Operacion ya Asignada";
              ||  |LIBERA #599;
              ||  |ACABA;
      |FINSI;
      |SI nOpera = 1;
          nDesde = 1;
          nHasta = nTope;
      |FINSI;
      |SI nOpera = 0;
          |BUCLE LeoEspejo_0;
          nEstado = 1;
      |SINO;
          |BUCLE LeoEspejo;
          nEstado = 0;
      |FINSI;
      |PTEC 802;
      |PTEC 802;
      |PTEC 802;
      |PTEC 806;

      nHay502 = 1;
      |SI #598#14 = 0; nHay502 = 0; |FINSI;
      eSwCambio = 1;

      |PONCONTROL 23, "SI";
  |FINSI;

  |SI nTecla = 13;
      eLibroCaja = #599#0;
      eRegisCaja = #599#1;
      eDevenCaja = #599#28;
      |SUB_EJECUTA_NP "camacc01;Consulta";
  |FINSI;

  |SI nTecla = 14;

      |HAZ LaBusquedaI;
      |SI nConNum ! 0;

          #599#0  = nLibro;
          #599#1  = nRegis;
          #599#2  = nCobro;
          #599#21 = nTipo;
          #599#28 = nDeven;
          |LEE 041#599=;

          |PTEC 809;
          |PONCONTROL 23, "SI";
      |FINSI;
  |FINSI;

  |SI nTecla = 17;
      |SI nEstado = 0;
          nEstado = 1;
      |SINO;
          |SI nHay502 = 0;
              |MENAV "    No hay registros asignados";
              |ACABA;
          |FINSI;
          nEstado = 0;
      |FINSI;
      |PTEC 802;
      |PTEC 802;
      |PTEC 802;
      |PTEC 806;
  |FINSI;
  eModoCaja = eModo;
|FPRC;

|| ////////////////////////////////////////////////////////////////////

|PROCESO Comprobar;
  nConNum = nConNum + 1;
  nLibro = #999#0;
  nRegis = #999#1;
  nCobro = #999#2;
  nTipo  = #999#21;

  |SI nConNum > 1;
      |ERROR10;
  |FINSI;
|FPRC;

|DEFBUCLE Comprobar;
  #999#3006;
  ;
  nDImporte, nDesde, 0000, 01, 0000000001, 00;
  nHImporte, nHasta, 9999, 99, 9999999999, 99;
  e;
  NULL, Comprobar;
|FIN;

||*******************************************************
|PROCESO Alta998;
  #999#24 = #598#0;
  #999#25 = #598#1;
  #999#26 = #598#2;
  #999#27 = #598#3;
  #999#28 = 0;

  #999#11 = nDImporte;
  #999#0  = 01;
  #999#1  = 0000000001;
  #999#2  = 00;
  #999#21 = nDesde;
|FPRC;

|PROCESO Baja998;
  #999#24 = #598#0;
  #999#25 = #598#1;
  #999#26 = #598#2;
  #999#27 = #598#3;
  #999#28 = 9999;

  #999#11 = nHImporte;
  #999#0  = 99;
  #999#1  = 9999999999;
  #999#2  = 99;
  #999#21 = nHasta;
|FPRC;

|PROCESO LaBusquedaI;

       |PUSHV 1022 2353;
       |PINTA 1023, "                              ";
       |PINTA 1123, " Del importe:                 ";
       |PINTA 1223, " Al importe :                 ";
       |PINTA 1323, "                              ";
       |CUADRO 1022 1452;
       |C_P #2#12, 1, 1137; |PINTA 1137, #2#12;
       |C_P #2#13, 1, 1237; |PINTA 1237, #2#13;

    |ET Camp10;
       |ENCAMPO #12#2;
       |SI FSalida = 1; |VETE  Campo0; |FINSI;
       #2#13 = #2#12; |PINTA #2#13;

    |ET Camp11;
       |ENCAMPO #13#2;
       |SI FSalida = 2; |VETE Camp10; |FINSI;
       |SI #2#13 < #2#12;
           |MENAV "    Error. Importe inferior al desde";
           |VETE Camp11;
       |FINSI;

    |ET Campo0;
       |POPV;

       nDImporte = #2#12;
       nHImporte = #2#13;
       |SI nEstado = 0;
           nDesde = 0; nHasta = 0;
       |SINO;
           nDesde = 0; nHasta = nTope;
       |FINSI;

       nConNum  = 0;
       |PUSHV 0801 2080;
       |BUCLE Comprobar;
       |SI nConNum = 0;
           |MENAV "    No Existe Apuntes dentro de la Seleccion";
       |SINO;
           |SI nConNum ! 1;
               |PINPA #0#999;
               |ATRI R; |PINTA 1131, #2#12;
                        |PINTA 1150, #2#13; |ATRI 0;
               nSw = 1;
               |ENTLINEAL #3#999, -6, 5, 18, 0, Alta998, Baja998;
               nSw = 0;
           |FINSI;
       |FINSI;
       |POPV;
|FPRC;

|| ////////////////////////////////////////////////////////////////////
|PROCESO ClaveAlta99_0;
  #599#24 = #598#0;
  #599#25 = #598#1;
  #599#26 = #598#2;
  #599#27 = #598#3;
  #599#28 = 0;

  #599#0  = 01;
  #599#1  = 0000000001;
  #599#2  = 00;
  #599#21 = 0;
|FPRC;

|PROCESO ClaveBaja99_0;
  #599#24 = #598#0;
  #599#25 = #598#1;
  #599#26 = #598#2;
  #599#27 = #598#3;
  #599#28 = 9999;

  #599#0  = 99;
  #599#1  = 9999999999;
  #599#2  = 99;
  #599#21 = 0;
|FPRC;

|PROCESO ClaveAlta99_02;
  #599#24 = #598#0;
  #599#25 = #598#1;
  #599#26 = #598#2;
  #599#27 = #598#3;
  #599#28 = 0;

  #599#0  = 01;
  #599#1  = 0000000001;
  #599#2  = 00;
  #599#21 = 0;
|FPRC;

|PROCESO ClaveBaja99_02;
  #599#24 = #598#0;
  #599#25 = #598#1;
  #599#26 = #598#2;
  #599#27 = #598#3;
  #599#28 = 9999;

  #599#0  = 99;
  #599#1  = 9999999999;
  #599#2  = 99;
  #599#21 = nTope;
|FPRC;

|PROCESO Tipo7Cw2; |TIPO 7;
  |SI nEstado = 0;
      |ATRI R;
      |SI aTipoIVA = "R";|PINTA 1202, "FACTURAS ASIGNADAS AL COBRO"; |FINSI;
      |SI aTipoIVA = "S";|PINTA 1202, "FACTURAS ASIGNADAS AL PAGO";  |FINSI;
      |ATRI 0;
      |ENTLINEAL #1#599, -8, 7, 18, 0, ClaveAlta99_0,ClaveBaja99_0;
  |SINO;
      |ENTLINEAL #1#599, -8, 7, 18, 0, ClaveAlta99_02,ClaveBaja99_02;
  |FINSI;
|FPRC;

|PROCESO Tipo3Cw2; |TIPO 12;
  |SI FSalida = 1; |O FSalida = 7; nSalgo = 2; |ACABA; |FINSI;

  nSalgo = 0;
  |PARA; |SI nSalgo = 0; |HACIENDO;
         sw = 0;
         aAlfa1 = "FACTURAS ASIGNADAS AL COBRO";
         |SI aTipoIVA = "S"; aAlfa1 = "FACTURAS ASIGNADAS AL PAGO"; |FINSI;
         |SI nEstado = 1;
             aAlfa1 = "ASIGNAR FACTURAS PENDIENTES DE COBRAR";
             |SI aTipoIVA = "S"; aAlfa1 = "ASIGNAR FACTURAS PENDIENTES DE PAGAR"; |FINSI;
         |FINSI;
         |PINTA 1202, "컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴";
         |ATRI R; |PINTA 1202, aAlfa1; |ATRI 0;

         |SI nEstado = 0;
             |ENTLINEAL #1#599, -8, 4, 18, 0, ClaveAlta99_0,ClaveBaja99_0;
         |SINO;
             |ENTLINEAL #1#599, -8, 4, 18, 0, ClaveAlta99_02,ClaveBaja99_02;
         |FINSI;

         |PULSATECLA;

         |SI nSalgo = 1; |SAL; |FINSI;
   |SIGUE;
|FPRC;

|PROCESO HazTodo;

  |SI eModoCaja ! 4;
      |SI nHay501S = 0; |Y nHay501 = 0; |Y nHay502 = 0;
         |MENAV "2400Operacion en Criterio de Caja. No hay Fra. pendientes para poder asignarla";
         |SI SwPasada = 1;
             |MENAV "2400Facturas exportadas al siguiente ejercicio";
         |FINSI;
         |ACABA;
     |FINSI;
     |SI nForzCaja ! 1;
         aAlfa1 =  "2400NOperacion de Critero de Caja. Desea introducir los datos ";
         |SI eModoCaja = 2; |O amCCaja = "A";
             aAlfa1 =  "2400SOperacion de Critero de Caja. Desea modificar los datos ";
         |FINSI;
         |AVISO;
         |CONFI aAlfa1;
         |SI FSalida ! 0;
             |ACABA;
         |FINSI;
     |FINSI;
  |FINSI;

  aAlfa1 = "";
  |SI eModoCaja ! 4;
      |SI nHay502 = 0;
          |SI nHay501 = 0;
              aAlfa1 = "2400SNo existen Operaciones de este Titular. Buscar otras Titulares.";
              |SI nHay501S = 0;
                  |ACABA;
              |FINSI;
          |SINO;
              |SI nHay501S ! 0;
                  aAlfa1 = "2400NBuscar tambien fras. de otros Titulares.";
              |FINSI;
          |FINSI;
      |SINO;
          |SI nHay501S = 1;
              aAlfa1 = "2400NBuscar tambien fras. de otros Titulares.";
          |FINSI;
      |FINSI;
  |FINSI;

  nTope = 1;
  |SI aAlfa1 ! "";
      |CONFI aAlfa1;
      |SI FSalida = 0; nTope = 2; |FINSI;
  |FINSI;
  |SI nHay501S = 2; nTope = 2; |FINSI;

  |PINPA #0#598;
  |SI #598#10 = "S";
      |ATRI I; |PINTA 0502, "Pago "; |ATRI 0;
               |PINTA 0911, "Pago ";
      |ATRI R; |PINTA 1359, "Pagado "; |ATRI 0;
  |FINSI;
  |PINDA #0#598;

  |SI eModoCaja = 4;
      |ENTLINEAL #1#599, -8, 5, 18, 0, ClaveAlta99_0,ClaveBaja99_0;
      |ACABA;
  |FINSI;

  eSwCambio = 0;
  |SI #598#14 ! #598#8; |O eModoCaja = 1;
      eSwCambio = 1;
  |FINSI;

  |SI nHay502 ! 0; |Y #598#8 = #598#14;
      nEstado = 0;
  |SINO;
      nEstado = 1;
  |FINSI;

  |DBASE aMas;
 aDef = aMas + "def/camaccw3";
  |CARGA_DEF aDef, camacw3@;
  |SI FSalida ! 0;
      |ACABA;
  |FINSI;
  |NOME_DAT #999 aFichero;

   FSalida = 0;
   |PARA;|SI FSalida = 0; |HACIENDO;
         |PINDA #0#598;
         |ENDATOS #2#598;
         |SI nSalgo = 2; |O FSalida ! 0;
             nNume1 = FSalida;
             |SI #598#8 ! #598#14;
                 nSalgo = 0;
                 |CONFI "1234NNo asignado el Importe total del Asiento. Es Correcto";
                 |SI FSalida = 0;
                     nSalgo = 2;
                 |FINSI;
             |FINSI;
             |SI nSalgo = 2; |SAL; |FINSI;
             FSalida = nNume1;
         |FINSI;
   |SIGUE;
   |DESCARGA_DEF #camacw3@;

   |CONFI "    SSon correctos los datos Introducidos";
   |SI FSalida = 0;
       |HAZ Actualizacion;
   |SINO;
       |MENAV "    Calculos no actualizados";
   |FINSI;
|FPRC;

|| //////////////////////////////////////////////// Grabacion
|PROCESO GraboAuxw3;
  ePerioCaja = #501#18; |HAZ ePathCaja;
  |CIERRA #0; |PATH_DAT #0 ePathCaja;

  |ABRE #0;
  |DDEFECTO #0;
  #0#0 = #501#0;
  #0#1 = #501#1;
  |LEE 041#0=;
  |SI FSalida ! 0;
      |CIERRA #0;
      |ACABA;
  |FINSI;
  |CIERRA #0;

  |SI #599#21 = -1;
      |SI #501#12 ! #0#21;
          #599#21 = 1;
          |SI #0#14 ! #598#9;  #599#21 = 2;  |FINSI;
      |FINSI;
  |FINSI;
  |SI #599#21 = -1; |ACABA; |FINSI;

  #599#0  = #0#0;
  #599#1  = #0#1;
  #599#2  = #502#2;
  #599#3  = #0#3;
  #599#4  = #0#2;
  #599#5  = #0#4;
  #599#6  = #0#12;
  #599#7  = #0#27;
  #599#8  = #0#28;

  #599#9  = #0#19; || Base  Fra.
  #599#10 = #0#23; || Cuota Fra.
  #599#11 = #0#21; || Total Fra.

  #599#12 = #0#13;
  #599#13 = #0#14;
  #599#14 = #0#36;

  #599#15 = #501#12; || Total Cobrado
  #599#16 = #501#6;  || Base Cobrada
  #599#17 = #501#7;  || Cuota Cobrada
  #599#22 = #501#16; || % cobrado

  #599#18 = #502#10;
  #599#19 = #502#11;
  #599#20 = #502#12;
  #599#23 = #502#13;
  #599#24 = #598#0;
  #599#25 = #598#1;
  #599#26 = #598#2;
  #599#27 = #598#3;
  #599#28 = #501#17;
  #599#29 = #501#18;
  |GRABA 020#599n;

  |SI #599#13 = #598#9;
      nHay501 = 1;
  |SINO;
      |SI nHay501S = 0;
          nHay501S = 1;
      |FINSI;
  |FINSI;
  |SI #599#2 ! 0;
      nHay502 = 1;
      |SI #599#13 ! #598#9; nHay501S = 2; |FINSI;
  |FINSI;
|FPRC;

|PROCESO BuscoLineas;
  #598#14 = #598#14 + #502#10;
  #598#12 = #502#14;
  #598#13 = #502#15;
  #598#15 = #502#17;
  #598#16 = #502#18;

  aCampo12 = #502#14;
  aCampo13 = #502#15;

  |DDEFECTO #501;
  #501#0  = #502#0;
  #501#1  = #502#1;
  #501#17 = #502#17;
  |LEE 041#501=;

  #599#21 = 0;    || elegido
  |HAZ GraboAuxw3;
|FPRC;

|DEFBUCLE BuscoLineas;
  #502#2;
  ;
  #598#0, #598#1, #598#2, #598#3, 0000, 00, 0000000001, 00;
  #598#0, #598#1, #598#2, #598#3, 9999, 99, 9999999999, 99;
  e;
  NULL, BuscoLineas;
|FIN;

|PROCESO BuscoFras;

  |SI #501#17 = 0; |Y #501#18 ! -1;
      || ... Pasada al a쨚 siguiente
      SwPasada = 1;
      |ACABA;
  |FINSI;

  #599#21 = 0;
  #599#28 = #501#17;
  #599#0  = #501#0;
  #599#1  = #501#1;
  #599#2  = 1;
  |LEE 041#599];
  |SI FSalida = 0; |Y #599#0 = #501#0; |Y #599#1 = #501#1; |Y #599#28 = #501#17;
      |ACABA;
  |FINSI;

  |DDEFECTO #502;
  #502#2 = 0;
  #599#21 = -1;
  #502#17 = #501#17;
  #502#18 = -1;

  |HAZ GraboAuxw3;
|FPRC;

|DEFBUCLE BuscoFras;
  #501#1;
  5,13;
  0000, 00, 0000000001, "N", aTipoIVA;
  9999, 99, 9999999999, "N", aTipoIVA;
  e;
  NULL, BuscoFras;
|FIN;

|| ///////////////////////////////////////////////////////////////////////

|PROGRAMA;
  |NOME_DAT #598 eaFicCaja;

  |ABRE #598;
  |LEE 020#598p;
  |SI FSalida ! 0;
      |CIERRA #598;
      |ACABA;
  |FINSI;

  aFichero = "zz" + Usuario;
  |NOME_DAT #599 aFichero;
  |DELINDEX #599;

  aCampo12 = "";
  aCampo13 = "";
  nHay502  = 0;
  nHay501  = 0;
  nHay501S = 0;
  #598#14  = 0;

  |ABRE #599;
  |ABRE #501;
  |BUCLE BuscoLineas;
  |CIERRA #501;

  |BUCLE BuscoFras;
  |CIERRA #599;

  |SI eModoCaja = 4; |Y nHay502 = 0;
      aAlfa1 = "    Cobro"; |SI aTipoIVA = "S"; aAlfa1 = "    Pago"; |FINSI;
      aAlfa1 = aAlfa1 + " no asignado a factura de Criterio de Caja";
      |MENAV aAlfa1;
  |SINO;
      |HAZ HazTodo;
  |FINSI;

  |BORRA 020#598a;
  |CIERRA #598;
  |DELINDEX #598;
  |DELINDEX #599;
|FPRO;
