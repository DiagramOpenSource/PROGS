|FICHEROS;
  cacalend #0;
  calendar #1;
|FIN;

|VARIABLES;
    &enCalen = 0;

    Calc  = 0;
    nume1 = 0;
    trab1 = "";
    varalfa = "";
    varalfa1 = "";
    varalfa2  = "";
    vardia = 0;
    varmes = 0;
    fechadesde = "";
    fechadesde1 = "";
    fechadesde2 = "";
    fechahasta = "";
    fechahasta1 = "";
    fechahasta2 = "";
    camp = 0;
    procesa = "     Estoy procesando espere un poco Por Favor";
    modifi = "2330S  Modificar el Calendario S/N : ";
    alta = "2330S     Dar el alta al Calendario S/N : ";
    errorfec1 = "     Fecha erronea DD/MM";
    errorfec2 = "     Fecha Hasta menor que la Fecha Desde";
    errorfec3 = "     No existe ese dia el a¤o no es bisiesto";
    bisiesto = "";
    swbis = 0;
    anyo = "";  year = 0;

    DiasEnero      = 31; DiasFebrero = 28; DiasMarzo     = 31; DiasAbril     = 30;
    DiasMayo       = 31; DiasJunio   = 30; DiasJulio     = 31; DiasAgosto    = 31;
    DiasSeptiembre = 30; DiasOctubre = 31; DiasNoviembre = 30; DiasDiciembre = 31;

    sdia = 0; alfa1 = ""; fech1 = @; fech2 = @;

    Cont = 0; ContMes = 0; ContDias = 0;
    ModoEntrada = 0; NumDias = 0; Comodin = ""; Comodin1 = "";

    Desde = ""; Hasta = ""; Peso = 0; PesoAnt = 0;

    DDia = 0; DMes = 0; HDia = 0; HMes = 0;
|FIN;

|INCLUYE dscont_i;

|PROCESO calanyo;  |TIPO 7;
  trab1 = fec1 % 704;
  nume1 = trab1;
  #0#1 = nume1;
  |PINTA #0#1;
  year = #0#1;
  |HAZ Bisiesto;
|FPRC;

|PROCESO  fechbien;  |TIPO 0;        ||Calcula si la fecha es correcta
  varalfa = #0Campo;
  |SI varalfa = "    ";  |ACABA;  |FINSI;
  |SI varalfa = "2902";|Y swbis = 0;
      |MENAV errorfec3;
      |ERROR;
      |ACABA;
  |FINSI;
  varalfa1 = varalfa / 2;
  varalfa2 = varalfa % A302;
  vardia = varalfa1;
  varmes = varalfa2;
  |SI varmes [ 0;|O varmes > 12;
       |MENAV errorfec1;
       |ERROR;
       |ACABA;
  |FINSI;
  |SI varmes = 1;  |O varmes = 3;  |O varmes = 5;  |O varmes = 7;
                   |O varmes = 8;  |O varmes = 10; |O varmes = 12;
      |SI vardia > 31; |O vardia [ 0;
          |MENAV errorfec1;
          |ERROR;
      |FINSI;
  |SINO;
      |SI varmes = 2;
          |SI vardia > 29;  |O vardia [ 0;
              |MENAV errorfec1;
              |ERROR;
          |FINSI;
      |SINO;
          |SI vardia > 30; |O vardia [ 0;
              |MENAV errorfec1;
              |ERROR;
          |FINSI;
      |FINSI;
  |FINSI;
|FPRC;

                    ||Calcula si la fecha desde es mayor a la fecha desde
|PROCESO desdefec;|TIPO 0;
  camp = Campo - 1;
  fechadesde = #0camp;
  fechadesde1 = fechadesde % A302;
  fechadesde2 = fechadesde % A102;
  fechadesde = fechadesde1 + fechadesde2;
  fechahasta = #0Campo;
  fechahasta1 = fechahasta % A302;
  fechahasta2 = fechahasta % A102;
  fechahasta = fechahasta1 + fechahasta2
  |SI fechadesde > fechahasta;
      |MENAV errorfec2;
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO anyo;|TIPO 0;
  |SI nume1 ! #0#1;
      |MENAV "      LA FECHA NO SE CORRESPONDE CON EL EJERCICIO";
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO peso;|TIPO 3;

  ModoEntrada = (FEntrada / 100) ! 0;
  |SI ModoEntrada ! 1;  |Y ModoEntrada ! 2;
      |ACABA;
  |FINSI;
  enCalen  = #0#0;

  |HAZ CreaCalend;
|FPRC;

|PROCESO Bisiesto;
   Calc = year $ 4;

   |SI Calc = 0;
      Calc = year $ 100;
      |SI Calc = 0;
         swbis = 0;
         Calc = year $ 400;
         |SI Calc = 0;
            swbis = 1;
         |FINSI;
      |SINO;
         swbis = 1;
      |FINSI;
   |SINO;
      swbis = 0
   |FINSI;
|FPRC;
