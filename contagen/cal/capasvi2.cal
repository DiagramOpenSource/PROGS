|FICHEROS;
   capasvi2 #0;

   caenlace #10;

   cacuenta #100;
   cacuebal #101;
   caclipro #102;
   camaniva #103;
   caivalin #104;
   caivaasi #105;

   cacuent@ #1000;
   caconcp@ #1001;
|FIN;

|VARIABLES;
   aCuenta = "";
   aNombre = "";
   aRef    = "";
   nNivel  = 0;
   nPepe   = 0;

   aAlfa     = "";
   aAlfa1    = "";
   aAlfa2    = "";
   aMes      = "";
   aFecha    = "";
   aLong     = "";

   nContFila = 0;
   nBlancos  = 0;
   nAsiento  = 0;
   nContador = 0;
   nEmpresa  = 0;
   nPeriodo  = 0;
   nCalc     = 0;
   nCalc1    = 0;
   nHandle   = 0;
   nLong     = 0;
   nPrimero  = 0;
   nEjer     = 0;
   nLinea    = 0;
   nTipo     = 0;
   nEjercicio = 0;
   nNume1     = 0;
|FIN;

|INCLUYE dscont_i;

|PROCESO MiraNivel;
   aLong = aCuenta;
   |QBF aLong; aLong = aLong % 0; nLong = aLong;
   nNivel = 0;

   |SI nLong = #48#14; |Y #48#14 ! 0; nNivel = 1; |FINSI;
   |SI nLong = #48#15; |Y #48#15 ! 0; nNivel = 2; |FINSI;
   |SI nLong = #48#16; |Y #48#16 ! 0; nNivel = 3; |FINSI;
   |SI nLong = #48#17; |Y #48#17 ! 0; nNivel = 4; |FINSI;
   |SI nLong = #48#18; |Y #48#18 ! 0; nNivel = 5; |FINSI;
   |SI nLong = #48#19; |Y #48#19 ! 0; nNivel = 6; |FINSI;
|FPRC;

|PROCESO LeeDiario;
   aAlfa = "    Traspasando diario contable. Leyendo mes de " + aMes; |MENSA aAlfa;

   |ALFACALLDLL "agixl97.dll", "selecciona_hoja", aMes;

   nContFila = 1; nBlancos = 0; nContador = 1;
   aFecha = ""; nAsiento = 0;

   |PARA; |SI; |HACIENDO;
      aAlfa = "A" + nContFila;
      |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa;
      aAlfa1 = aAlfa % 0106;
      |SI aAlfa1 = "Fecha:";
         aAlfa1 = aAlfa - "Fecha: "; aFecha = aAlfa1;
         aAlfa = "G" + nContFila;
         |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa;
         aAlfa1 = aAlfa - "NO.ASIENTO: ";
         nAsiento = aAlfa1;
         nContador = 1;
      |SINO;
         |SI aAlfa = "";
            nBlancos = nBlancos + 1;
            |SI nBlancos > 10; |SAL; |FINSI;
         |SINO;
            nBlancos = 0;
            |SI aFecha ! "";
               |DDEFECTO #caenlace;
               #caenlace#0 = 1;         || Diario
               #caenlace#1 = aFecha;
               #caenlace#2 = nAsiento;
               #caenlace#3 = nContador; nContador = nContador + 1;
               aAlfa = "A" + nContFila; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa;
               #caenlace#6 = aAlfa;
               aAlfa = "E" + nContFila; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa;
               |QBF aAlfa;
               |SI aAlfa ! "";
                  #caenlace#4 = aAlfa; #caenlace#8 = "D";
               |FINSI;
               aAlfa = "F" + nContFila; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa;
               |QBF aAlfa;
               |SI aAlfa ! "";
                  #caenlace#4 = aAlfa; #caenlace#8 = "H";
               |FINSI;
               aCuenta = #caenlace#4; |HAZ MiraNivel;
               #caenlace#5 = nNivel;
               aAlfa = "G" + nContFila; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa;
               #caenlace#7 = aAlfa;
               |SI #caenlace#8 = "D";
                  aAlfa = "H";
               |SINO;
                  aAlfa = "I";
               |FINSI;
               aAlfa = aAlfa + nContFila; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa;
               #caenlace#9 = aAlfa;
               #caenlace#37 = nEmpresa;
               #caenlace#38 = nPeriodo;

               aAlfa = "C" + nContFila; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa;
               #caenlace#45 = aAlfa;

               |GRABA 020#caenlace.n;
            |FINSI;
         |FINSI;
      |FINSI;
      nContFila = nContFila + 1;
   |SIGUE;
|FPRC;

|PROCESO Diario;
   aAlfa = "pu" + Usuario; |NOME_DAT #caenlace#aAlfa#;
   |DELINDEX #caenlace; |ABRE #caenlace;

   |DFICE aAlfa; |QBF aAlfa;
   aAlfa = aAlfa % -201;
   nPeriodo = aAlfa;
   |DFICE aAlfa; |QBF aAlfa;
   aAlfa = aAlfa % -206; aAlfa = aAlfa % 0105;
   nEmpresa = aAlfa;

   aAlfa = #capasvi2#0; |QBF aAlfa;
   |ALFACALLDLL "agixl97.dll", "carga_book", aAlfa;
   aMes = "Enero";      |HAZ LeeDiario;
   aMes = "Febrero";    |HAZ LeeDiario;
   aMes = "Marzo";      |HAZ LeeDiario;
   aMes = "Abril";      |HAZ LeeDiario;
   aMes = "Mayo";       |HAZ LeeDiario;
   aMes = "Junio";      |HAZ LeeDiario;
   aMes = "Julio";      |HAZ LeeDiario;
   aMes = "Agosto";     |HAZ LeeDiario;
   aMes = "Septiembre"; |HAZ LeeDiario;
   aMes = "Octubre";    |HAZ LeeDiario;
   aMes = "Noviembre";  |HAZ LeeDiario;
   aMes = "Diciembre";  |HAZ LeeDiario;
   |ALFACALLDLL "agixl97.dll", "fin_book", "salva";

   |CIERRA #caenlace;

   |DFICE aAlfa;
   aAlfa = "dsapunte.tab;" + aAlfa + ",pu" + Usuario;
   |SUB_EJECUTA aAlfa;

|FPRC;

|PROCESO PlanContable;
   |ABRE #cacuenta; |ABRE #cacuebal;

   aAlfa = #capasvi2#1; |QBF aAlfa;
   |ALFACALLDLL "agixl97.dll", "carga_book", aAlfa;
   aAlfa = "LISTADO PLAN CONTABLE";
   |ALFACALLDLL "agixl97.dll", "selecciona_hoja", aAlfa;

   nContFila = 1; nContador = 1;
   |PARA; |SI; |HACIENDO;
      aAlfa = "A" + nContFila;
      |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
      |SI aAlfa = ""; |SAL; |FINSI;
      aCuenta = aAlfa; |HAZ MiraNivel;

      #cacuenta#0 = aCuenta;
      #cacuenta#1 = nNivel;
      |LEE 000#cacuenta.=;
      |SI FSalida ! 0;
         |DDEFECTO #cacuenta;
         #cacuenta#0 = aCuenta;
         #cacuenta#1 = nNivel;
         aAlfa = "B" + nContFila;
         |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
         #cacuenta#2 = aAlfa;
         #cacuenta#3 = "S";

         aAlfa = "    Pasando la cuenta " + aCuenta + " " + aAlfa;
         |MENSA aAlfa;

         |PARA nContador = 6; |SI nContador ] 1; |HACIENDO nContador = nContador - 1;
            nCalc = 13 + nContador;
            |SI #48#nCalc# ! 0;
               nCalc = 100 + #48#nCalc#;
               aAlfa = aCuenta % nCalc;

               #cacuebal#0 = aAlfa;
               |LEE 000#cacuebal.=;
               |SI FSalida = 0;
                  #cacuenta#4 = #cacuebal#1;
                  #cacuenta#5 = #cacuebal#2;
                  #cacuenta#6 = #cacuebal#3;
                  #cacuenta#7 = #cacuebal#4;
                  #cacuenta#8 = #cacuebal#5;
                  |SAL;
               |FINSI;
            |FINSI;
         |SIGUE;
         |GRABA 020#cacuenta.n;
      |FINSI;
      nContFila = nContFila + 1;
   |SIGUE;

   |ALFACALLDLL "agixl97.dll", "fin_book", "salva";
   |CIERRA #cacuenta; |CIERRA #cacuebal;
|FPRC;

|PROCESO CliPro;
   |DELINDEX #caclipro; |ABRE #caclipro;

   aAlfa = #capasvi2#2; |QBF aAlfa;
   |ALFACALLDLL "agixl97.dll", "carga_book", aAlfa;
   aAlfa = "Hoja1";
   |ALFACALLDLL "agixl97.dll", "selecciona_hoja", aAlfa;

   nContFila = 1; nContador = 1; nPrimero = 1;
   |PARA; |SI; |HACIENDO;
      aAlfa = "A" + nContFila;
      |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
      |SI aAlfa = ""; |SAL; |FINSI;

      aAlfa = aAlfa - "Codigo ";
      |SI FSalida ! 0;
         |SI nPrimero ! 1;
            |GRABA 020#caclipro.a; |LIBERA #caclipro;
         |SINO;
            nPrimero = 0;
         |FINSI;
         |DDEFECTO #caclipro;
         aAlfa1 = aAlfa % 0102;
         |SI aAlfa1 = "43";
            #caclipro#23 = "C";
         |SINO;
            #caclipro#23 = "P";
         |FINSI;
         aCuenta = aAlfa;
         aAlfa1 = "";
         aAlfa2 = aCuenta % 0101;
         |PARA; |SI aAlfa2 ! " "; |HACIENDO;
            aAlfa1 = aAlfa1 + aAlfa2; aCuenta = aCuenta % 0299;
            aAlfa2 = aCuenta % 0101;
         |SIGUE;
         aNombre = aCuenta % 0299; aCuenta = aAlfa1;
         |HAZ MiraNivel;
         #caclipro#10 = aCuenta;
         #caclipro#11 = nNivel;
         #caclipro#1  = aNombre;
         |GRABA 020#caclipro.b;

         aAlfa = "    Grabando datos " + aNombre; |MENSA aAlfa;
         aAlfa = "";
      |FINSI;

      aAlfa = aAlfa - "N.I.F. ";
      |SI FSalida ! 0;
         #caclipro#9 = aAlfa;
         aAlfa = "";
      |FINSI;

      aAlfa = aAlfa - "Direccion ";
      |SI FSalida ! 0;
         #caclipro#2 = aAlfa;
         aAlfa = "";
      |FINSI;

      aAlfa = aAlfa - "Poblacion ";
      |SI FSalida ! 0;
         aAlfa1 = "";
         aAlfa2 = aAlfa % 0101;
         |PARA; |SI aAlfa2 ! " "; |HACIENDO;
            aAlfa1 = aAlfa1 + aAlfa2; aAlfa = aAlfa % 0299;
            aAlfa2 = aAlfa % 0101;
         |SIGUE;
         aAlfa = aAlfa  % 0299; #caclipro#6 = aAlfa;
         aAlfa = aAlfa1 % 0102; #caclipro#3 = aAlfa;
         aAlfa = aAlfa1 % 0303; #caclipro#4 = aAlfa;
         enCodProv = #caclipro#3;   |HAZ LeeProvincia;
         #caclipro#5 = eaProvincia; aAlfa = "";
      |FINSI;

      aAlfa = aAlfa - "Telefono ";
      |SI FSalida ! 0;
         #caclipro#7 = aAlfa;
         aAlfa = "";
      |FINSI;

      aAlfa = aAlfa - "Fax ";
      |SI FSalida ! 0;
         #caclipro#8 = aAlfa;
         aAlfa = "";
      |FINSI;

      aAlfa = aAlfa - "E-Mail ";
      |SI FSalida ! 0;
         #caclipro#8 = aAlfa;
         aAlfa = "";
      |FINSI;

      nContFila = nContFila + 1;
   |SIGUE;

   |SI nPrimero ! 1;
      |GRABA 020#caclipro.a; |LIBERA #caclipro;
   |FINSI;
   |CIERRA #caclipro;
|FPRC;

|PROCESO Soportado;
   aAlfa = #capasvi2#3; |QBF aAlfa;
   |ALFACALLDLL "agixl97.dll", "carga_book", aAlfa;
   aAlfa = "Libro Registro";
   |ALFACALLDLL "agixl97.dll", "selecciona_hoja", aAlfa;

   nContFila = 1; nContador = 1; nPrimero = 1;

   |ABRE #caenlace; |SELECT #7#caenlace;
   |ABRE #camaniva; |ABRE #caivalin;

   |PARA; |SI; |HACIENDO;
      aAlfa = "J" + nContFila;
      |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;

      aAlfa = aAlfa - ",";
      |PARA; |SI FSalida ! 0; |HACIENDO;
         nCalc  = ((FSalida / 100) ! 0) + 99;
         nCalc1 = FSalida + 98;
         aAlfa = (aAlfa % nCalc) + "." + (aAlfa % nCalc1);
         aAlfa = aAlfa - ",";
      |SIGUE;

      nCalc = aAlfa; aAlfa1 = nCalc;
      |SI aAlfa = aAlfa1;
         aAlfa = "C" + nContFila;
         |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
         aAlfa1 = "    Grabando datos soportado " + aAlfa; |MENSA aAlfa1;

         aAlfa = "A" + nContFila;
         |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
         |SI aAlfa ! "";  || Nueva Factura;

            |SI nPrimero ! 1;
               |GRABA 020#camaniva.a; |LIBERA #camaniva;
            |SINO;
               nPrimero = 0;
            |FINSI;

            |DDEFECTO #camaniva;
            #camaniva#0 = 2;
            #camaniva#1 = aAlfa;
            #camaniva#9 = 0;
            #camaniva#2 = "";
            #camaniva#3 = aAlfa;

            aCuenta = "";
            aAlfa = "C" + nContFila; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
            #caenlace#45 = aAlfa; aRef = #caenlace#45;
            aAlfa = "B" + nContFila; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
            #caenlace#1  = aAlfa;
            |LEE 000#caenlace.];
            |PARA; |SI FSalida = 0; |Y #caenlace#45 = aRef; |HACIENDO;
               |SI #caenlace#8 = "H"; |Y aCuenta = ""; aCuenta = #caenlace#4; |FINSI;
               |SI #camaniva#9 = 0;
                  |SI #caenlace#1 = aAlfa;
                     #camaniva#7 = #caenlace#0;
                     #camaniva#8 = #caenlace#1;
                     #camaniva#9 = #caenlace#2;
                  |FINSI;
               |SINO;
                  |SI #caenlace#1 ! aAlfa;
                     #camaniva#7 = 0;
                     #camaniva#8 = "01.01.0000";
                     #camaniva#9 = 0;
                     aCuenta     = "";
                     |SAL;
                  |FINSI;
               |FINSI;
               |LEE 000#caenlace.s;
            |SIGUE;

            aAlfa = "B" + nContFila; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
            #camaniva#4 = aAlfa;
            aAlfa = #camaniva#4; aAlfa = aAlfa % 0402;
            |SI nEjercicio [ 2009;
                |SI #caivaasi#46 = "T";
                    |SI aAlfa ] "01"; |Y aAlfa [ "03"; aAlfa = "01"; |FINSI;
                    |SI aAlfa ] "04"; |Y aAlfa [ "06"; aAlfa = "02"; |FINSI;
                    |SI aAlfa ] "07"; |Y aAlfa [ "09"; aAlfa = "03"; |FINSI;
                    |SI aAlfa ] "10"; |Y aAlfa [ "12"; aAlfa = "04"; |FINSI;
                |FINSI;
            |FINSI;
            #camaniva#5 = aAlfa;

            |QBF aCuenta;
            |SI aCuenta ! "";
               #camaniva#12 = aCuenta;
            |SINO;
               #camaniva#12 = "410000000";
            |FINSI;
            ||aAlfa = "D" + nContFila; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
            ||#camaniva#12 = aAlfa;
            aAlfa = "E" + nContFila; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
            #camaniva#13 = aAlfa;
            aAlfa = "F" + nContFila; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
            #camaniva#14 = aAlfa;
            #camaniva#36 = "S";
            #camaniva#27 = 600;
            #camaniva#10 = 1;

            #caconcp@#0 = 600;
            |LEE 000#caconcp@.=;
            |SI FSalida ! 0; |DDEFECTO #caconcp@; |FINSI;

            aAlfa = "C" + nContFila; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
            aAlfa1 = #caconcp@#1; |QBF aAlfa1;
            aAlfa = aAlfa1 + " " + aAlfa;
            #camaniva#28 = aAlfa;
            #camaniva#29 = #camaniva#27;
            #camaniva#30 = #camaniva#28;

            aAlfa = "B" + nContFila; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
            aAlfa = aAlfa % 0704; nEjer = aAlfa;
            enEjercicio = nEjer; aTipoIVA = "S";
            efFechaIVA = #camaniva#4; enLineaIVA = 1; |HAZ SacaIVA;

            #camaniva#31 = eaDesgIVA;
            #camaniva#32 = eaDesgCTP;
            #camaniva#33 = "N";
            #camaniva#34 = eaDesgDTO;

            |GRABA 020#camaniva.b;
            nLinea = 1;
         |FINSI;

         aAlfa = "G" + nContFila; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
         nCalc = aAlfa;
         #camaniva#19 = #camaniva#19 + nCalc;
         aAlfa = "I" + nContFila; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
         nCalc = aAlfa;
         #camaniva#23 = #camaniva#23 + nCalc;
         #camaniva#21 = #camaniva#23 + #camaniva#19;
         |GRABA 020#camaniva.a;

         |DDEFECTO #caivalin;
         #caivalin#0 = #camaniva#0;
         #caivalin#1 = #camaniva#1;
         #caivalin#2 = nLinea; nLinea = nLinea + 1;
         aAlfa = "B" + nContFila; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
         nTipo = aAlfa;

         enEjercicio = nEjer; aTipoIVA = "S";
         efFechaIVA = #camaniva#4; enLineaIVA = nTipo; |HAZ SacaIVA;
         #caivalin#13 = eaCtaIVA;
         #caivalin#14 = eaCtaREC;
         #caivalin#15 = eaCtaDTO;

         #caivalin#7 = enPorcIVA;
         #caivalin#9 = enPorcREC;
         #caivalin#3 = #camaniva#29;
         #caivalin#4 = #camaniva#30;
         aAlfa = "G" + nContFila; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
         #caivalin#6 = aAlfa;
         aAlfa = "H" + nContFila; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
         #caivalin#7 = aAlfa;
         aAlfa = "I" + nContFila; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
         #caivalin#8 = aAlfa;
         |GRABA 020#caivalin.n;
         nContador = 1;
      |SINO;
         aAlfa = "E" + nContFila;
         |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
         |SI aAlfa = "SUMA TOTAL"; |SAL; |FINSI;

         aAlfa = "A" + nContFila; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
         |SI aAlfa = ""; nContador = nContador + 1; |FINSI;
         |SI nContador > 10; |SAL; |FINSI;
      |FINSI;

      nContFila = nContFila + 1;
   |SIGUE;

   |CIERRA #camaniva; |CIERRA #caivalin;
   |SELECT #1#caenlace; |CIERRA #caenlace;
|FPRC;

|PROCESO Repercutido;
   aAlfa = #capasvi2#4; |QBF aAlfa;
   |ALFACALLDLL "agixl97.dll", "carga_book", aAlfa;
   aAlfa = "Libro Registro";
   |ALFACALLDLL "agixl97.dll", "selecciona_hoja", aAlfa;

   nContFila = 1; nContador = 1; nPrimero = 1;

   |ABRE #caenlace; |SELECT #7#caenlace;
   |ABRE #camaniva; |ABRE #caivalin;

   |PARA; |SI; |HACIENDO;
      aAlfa = "J" + nContFila;
      |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;

      aAlfa = aAlfa - ",";
      |PARA; |SI FSalida ! 0; |HACIENDO;
         nCalc  = ((FSalida / 100) ! 0) + 99;
         nCalc1 = FSalida + 98;
         aAlfa = (aAlfa % nCalc) + "." + (aAlfa % nCalc1);
         aAlfa = aAlfa - ",";
      |SIGUE;

      nCalc = aAlfa; aAlfa1 = nCalc;
      |SI aAlfa = aAlfa1;
         aAlfa = "C" + nContFila;
         |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
         aAlfa1 = "    Grabando datos repercutido " + aAlfa; |MENSA aAlfa1;

         aAlfa = "A" + nContFila;
         |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
         |SI aAlfa ! "";  || Nueva Factura;
            |SI nPrimero ! 1;
               |GRABA 020#camaniva.a; |LIBERA #camaniva;
            |SINO;
               nPrimero = 0;
            |FINSI;

            |DDEFECTO #camaniva;
            #camaniva#0 = 1;
            #camaniva#1 = aAlfa;
            #camaniva#2 = "";
            #camaniva#3 = aAlfa;
            #camaniva#9 = 0;

            aAlfa = "C" + nContFila; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
            #caenlace#45 = aAlfa; aRef = #caenlace#45;
            aAlfa = "B" + nContFila; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
            #caenlace#1  = aAlfa;
            |LEE 000#caenlace.];
            |PARA; |SI FSalida = 0; |Y #caenlace#45 = aRef; |HACIENDO;
               |SI #camaniva#9 = 0;
                  |SI #caenlace#1 = aAlfa;
                     #camaniva#7 = #caenlace#0;
                     #camaniva#8 = #caenlace#1;
                     #camaniva#9 = #caenlace#2;
                  |FINSI;
               |SINO;
                  |SI #caenlace#1 ! aAlfa;
                     #camaniva#7 = 0;
                     #camaniva#8 = "01.01.0000";
                     #camaniva#9 = 0;
                     |SAL;
                  |FINSI;
               |FINSI;
               |LEE 000#caenlace.s;
            |SIGUE;

            aAlfa = "B" + nContFila; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
            #camaniva#4 = aAlfa;
            aAlfa = #camaniva#4; aAlfa = aAlfa % 0402;
            |SI nEjercicio [ 2009;
                |SI #caivaasi#46 = "T";
                    |SI aAlfa ] "01"; |Y aAlfa [ "03"; aAlfa = "01"; |FINSI;
                    |SI aAlfa ] "04"; |Y aAlfa [ "06"; aAlfa = "02"; |FINSI;
                    |SI aAlfa ] "07"; |Y aAlfa [ "09"; aAlfa = "03"; |FINSI;
                    |SI aAlfa ] "10"; |Y aAlfa [ "12"; aAlfa = "04"; |FINSI;
                |FINSI;
            |FINSI;
            #camaniva#5 = aAlfa;

            aAlfa = "D" + nContFila; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
            #camaniva#12 = aAlfa;
            aAlfa = "E" + nContFila; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
            #camaniva#13 = aAlfa;
            aAlfa = "F" + nContFila; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
            #camaniva#14 = aAlfa;
            #camaniva#36 = "R";
            #camaniva#27 = 700;
            #camaniva#10 = 1;

            #caconcp@#0 = 700;
            |LEE 000#caconcp@.=;
            |SI FSalida ! 0; |DDEFECTO #caconcp@; |FINSI;

            aAlfa = "C" + nContFila; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
            aAlfa1 = #caconcp@#1; |QBF aAlfa1;
            aAlfa = aAlfa1 + " " + aAlfa;
            #camaniva#28 = aAlfa;
            #camaniva#29 = #camaniva#27;
            #camaniva#30 = #camaniva#28;

            aAlfa = "B" + nContFila; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
            aAlfa = aAlfa % 0704; nEjer = aAlfa;
            enEjercicio = nEjer; aTipoIVA = "R";
            efFechaIVA = #camaniva#4; enLineaIVA = 1; |HAZ SacaIVA;

            #camaniva#31 = eaDesgIVA;
            #camaniva#32 = eaDesgCTP;
            #camaniva#33 = "N";
            #camaniva#34 = eaDesgDTO;

            |GRABA 020#camaniva.b;
            nLinea = 1;
         |FINSI;

         aAlfa = "G" + nContFila; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
         nCalc = aAlfa;
         #camaniva#19 = #camaniva#19 + nCalc;
         aAlfa = "I" + nContFila; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
         nCalc = aAlfa;
         #camaniva#23 = #camaniva#23 + nCalc;
         #camaniva#21 = #camaniva#23 + #camaniva#19;
         |GRABA 020#camaniva.a;

         |DDEFECTO #caivalin;
         #caivalin#0 = #camaniva#0;
         #caivalin#1 = #camaniva#1;
         #caivalin#2 = nLinea; nLinea = nLinea + 1;
         aAlfa = "B" + nContFila; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
         nTipo = aAlfa;
         enEjercicio = nEjer; aTipoIVA = "R";
         efFechaIVA = #camaniva#4; enLineaIVA = nTipo; |HAZ SacaIVA;
         #caivalin#13 = eaCtaIVA;
         #caivalin#14 = eaCtaREC;
         #caivalin#15 = eaCtaDTO;
         #caivalin#7 = enPorcIVA;
         #caivalin#9 = enPorcREC;

         #caivalin#3 = #camaniva#29;
         #caivalin#4 = #camaniva#30;
         aAlfa = "G" + nContFila; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
         #caivalin#6 = aAlfa;
         aAlfa = "H" + nContFila; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
         #caivalin#7 = aAlfa;
         aAlfa = "I" + nContFila; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
         #caivalin#8 = aAlfa;
         |GRABA 020#caivalin.n;
         nContador = 1;
      |SINO;
         aAlfa = "E" + nContFila;
         |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
         |SI aAlfa = "SUMA TOTAL"; |SAL; |FINSI;

         aAlfa = "A" + nContFila; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; |QBF aAlfa;
         |SI aAlfa = ""; nContador = nContador + 1; |FINSI;
         |SI nContador > 10; |SAL; |FINSI;
      |FINSI;

      nContFila = nContFila + 1;
   |SIGUE;

   |CIERRA #camaniva; |CIERRA #caivalin;
   |SELECT #1#caenlace; |CIERRA #caenlace;
|FPRC;

|PROCESO MiraSiExiste;
   |SI FSalida = 10;
      aAlfa = "F*.xls"; |BROWSE aAlfa;
      |SI aAlfa = ""; |ERROR; |ACABA; |FINSI;
      aAlfa = aAlfa % 0299;
      #capasvi2#Campo# = aAlfa; |PINTA #capasvi2#Campo#;
   |FINSI;

   aAlfa = #capasvi2#Campo#; |QBF aAlfa;
   |SI aAlfa = ""; |ACABA; |FINSI;

   |XABRE "CE", aAlfa, nHandle;
   |SI FSalida < 0;
      |MENAV "    Fichero inexistente";
      |ERROR;
   |FINSI;
|FPRC;

|PROGRAMA;
   nNume1 = #48#26;
   |SI nNume1 [ 80;
       nEjercicio = nNume1 + 2000;
   |SINO;
       nEjercicio = nNume1 + 1900;
   |FINSI;

   |ABRE #capasvi2;
   |LEE 000#capasvi2.p;
   |SI FSalida ! 0;
      |DDEFECTO #capasvi2;
      |GRABA 020#capasvi2.c;
   |FINSI;

   |CLS;
   |CABEZA "Importacion contabilidad";
   |PINPA #0#0; |PINDA #0#0;
   |ENDATOS #1#0;
   |SI FSalida = 0;
      |GRABA 020#capasvi2.a;
      |CARGADLL "agixl97.dll";
      |SI FSalida < 0;
         |MENAV "0000No se puede usar agixl97.dll";
         |ACABA;
      |FINSI;

      |DBASS aAlfa; |QBF aAlfa;
      aAlfa = aAlfa + "modelos/def/dsmom030.mas";
      |CARGA_DEF aAlfa, caconcp@;
      |SI FSalida = 0;
         |DBASS aAlfa; |QBF aAlfa;
         aAlfa = aAlfa + "modelos/fich/"; |PATH_DAT #caconcp@#aAlfa#;
         |ABRE #caconcp@;

         |HAZ PlanContable;
         |HAZ CliPro;
         |HAZ Diario;
         |HAZ Soportado;
         |HAZ Repercutido;

         |CIERRA #caconcp@; |DESCARGA_DEF #caconcp@;
      |SINO;
         |MENAV "    No se encuentra el fichero de conceptos. Proceso cancelado.";
      |FINSI;

      |DESCARGADLL "agixl97.dll";
   |FINSI;
   |CIERRA #capasvi2; |DELINDEX #caenlace;
|FPRO;
