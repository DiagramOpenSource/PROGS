|FICHEROS;
   cabatodo #0;
   cacuenta #1;
   caparexp #2;
   caparbal #9;
   caagrupa #11;
   caepigra #12;
   cabaexp0 #3; || fichero trabajo totales
   cabaexp2 #5; || fichero trabajo cuentas
   caexistc #6;
   caexistl #7;
   canempre #30;
   caconimp #8;
   catrabap #10; || Pantalla del Balance

   caselem1 #998;

   cawdatos #900;
|FIN;

|VARIABLES;

   nRedon = 0;
   nRedo1 = 0;
   nCam1  = 0;
   nCam2  = 0;
   nCam3  = 0;
   &pathbal = "";
   &balance = 1;
   &entrada = 0;
   &aparam = "";
   swNoExiste = 0;
   informe = "";
   alfa1 = "";
   alfa2 = "";
   alfa3 = "";
   nume1  = 0;
   nNume1 = 0;
   x = 0;
   y = 0;
   &r_emp = 0;
   &r_per = 0;
   &d_mes = 0;
   &h_mes = 0;
   &estru = 0;
   &r_eje = 0;
   &r_nom = "";
   &anu = 0;
   &anu1 = 0;
   varcamp = 0;
   nivel = 0;
   sw_error = 0;
   cue_perdi = "";
   dig_perdi = 0;
   traraf = 0;
   acor_c = "";
   alar_c = "";
   acor_d = 0;
   t_agru = "";
   t_epig = "";
   t_part = "";

   Empresa = 0;
   Periodo = 0;

   &empcon = "";
   &ejecon = 0;

   &eldesde  = 0;
   &elhasta  = 0;
   des_exi   = 0;
   has_exi   = 0;
   d_cuen    = "";
   h_cuen    = "";
   d_digi    = 0;
   h_digi    = 9;

   dos       = 0;
   acumula   = 0;
   acumul2   = 0;
   act_cuent = "";
   act_nivel = 0;
   saldoIni  = 0;
   saldo     = 0;
   debe      = 0;
   haber     = 0;
   sw_exp    = 0;
   descar    = 0;
   bene      = 0;
   activo    = 0;
   pasivo    = 0;
   explo     = 0;
   debe1     = 0;
   debe2     = 0;
   debe3     = 0;
   debe4     = 0;
   haber1    = 0;
   haber2    = 0;
   haber3    = 0;
   haber4    = 0;
   tipo      = "";
   bloque    = 0;
   agrupaci  = 0;
   epigrafe  = 0;
   partida   = 0;

   canti     = 0;
   punto_h1  = 0;
   punto_h2  = 0;
   punto_h3  = 0;
   punto_h4  = 0;
   punto_h5  = 0;
   punto_h6  = 0;
   punto_d1  = 0;
   punto_d2  = 0;
   punto_d3  = 0;
   punto_d4  = 0;
   punto_d5  = 0;
   punto_d6  = 0;

   sw        = 0;
   sw_agr    = 0;
   sw_epi    = 0;
   sw_alguno = 0;
   swimp = 0;

   &titulo   = "";
   &sw_inf   = 0;
   &cantidad = 0;
   &cantidad2 = 0;
   &variable = "";
   &doh      = "";
   &cta      = "";
   &nom      = "";
   &mesi     = "";
   &mesf     = "";
   &fec      = @;
   d_tipo    = "";
   h_tipo    = "";
   d_bloque  = 0;
   h_bloque  = 0;
   {-1}menu  = "";           || Variables menu impresora o pantalla

   menu1 = "2400";
   menu2 = "1";
   menu3 = "Elija opcion: ";
   menu4 = "PI";
   menu5 = " Pantalla ";
   menu6 = " Impresora ";

   lin     = 0;
   swfinal = 0;

   Comodin = "";
   Comodin1 = "";

   Handle  = 0;
   aAlfa    = "";
   Calc    = 0;
   Compara = 0;
   Per1    = 0;
   Per2    = 0;

   CodigoEmpresa  = 0;
   EjercicioEmpresa = 0;
   NombreEmpresa  = "";

   nSwMen = 0;
   &enNumPag = 0;
   nSwLInv = 0;
   &efFecLeg = @;
   &eaDatLeg = "";
   aAlfa1 = "";
   aAlfa2 = "";

   nResultado = 0;
   nSwGrabo   = 0;

   aFichero = "";

   aNomCta  = "";
   aNomCta1 = "";

   &nPagDup = 0;
   &nSwPagDup = 0;
   &pagina = 0;
   sw_pagina = 0;
   &eaUnidadBasico = "";
   &eaUnidadBTmp   = "";
|FIN;

|INCLUYE dscont_i;
|INCLUYE digito_i;

|| ========================================================================
||                        CALCULOS PANTALLA
|| ========================================================================
|PROCESO &c_pagina;
   |SI sw_pagina = 0;
      pagina = #0#20;
      sw_pagina = 1;

      nPagDup = #0#20;
      nSwPagDup = 1;
   |SINO;
      pagina = pagina + 1;

      |HAZ PDuplex;
   |FINSI;
|FPRC;

|CALCULO AnaT13; |TIPO 13;
 aparam = PARAMETRO; |SI aparam = " N"; aparam = ""; |FINSI;
 |SI aparam = "Analitica";
     |ATRI P;
     |PINTA 0572, "ANALITICO";
     |ATRI 0;
     #30#1 = eaNomAna;
 |FINSI;
|FCAL;

|PROCESO mayor1;
   |SI #0Campo = "N";
       #0#14 = empre;
       #0#15 = ejerc;
       |C_M #0#14,1,"N";
       |C_M #0#15,1,"N";
       |PINTA #0#14;
       |PINTA #0#15;
   |SINO;
      |C_M #0#14,1,"S";
      |C_M #0#15,1,"S";
   |FINSI;
|FPRC;

|PROCESO deentrada;  |TIPO 7;
   |SI #0#14 = 0;  |Y #0#15 = 0;
       #0#14 = empre;
       #0#15 = ejerc - 1;
       |SI #0#15 < 0; #0#15 = 9; |FINSI;
       |PINTA #0#14;
       |PINTA #0#15;
   |FINSI;

   #0#6 = 1;
   |SI Haybasica = 1; |Y eaDepar = "S";
       #0#6 = 0;
   |FINSI;
   |PINTA #0#6;
|FPRC;

|PROCESO esbuena;
   |SI FSalida = 9;  |ACABA;  |FINSI;
   |ABRE #30;
   #30#2 = #0#14;
   #30#3 = 0;
   |LEE 011#30];
   |SI FSalida ! 0;  |O #30#2 ! #0#14;
      |SI nSwMen = 0; |MENAV "     NO Existe la empresa Contable"; |FINSI;
      |ERROR;
   |FINSI;
   |CIERRA #30;
|FPRC;

|PROCESO consulta;
   |SI FSalida ! 9;  |ACABA;  |FINSI;

   r_emp = 0;
   |SUB_EJECUTA "caconemp";

   |ERROR;
   |SI Campo = 14;
      #0#14 = r_emp;
      #0#15 = r_per;
      |PTEC 802;
      |PTEC 802;
   |FINSI;
|FPRC;

|PROCESO esbuena1;
   |SI FSalida = 2; |ACABA; |FINSI;
   swNoExiste = 0;
   |ABRE #30;
   #30#2 = #0#14;
   #30#3 = #0#15;
   |LEE 011#30=;
   |SI FSalida ! 0;
      |SI nSwMen = 0;
          |MENAV "     NO Existe Periodo en la empresa Contable";
          |ERROR;
      |FINSI;
      |CIERRA #30;
      swNoExiste = 1;
      |ACABA;
   |FINSI;
   |SI #30#13 ! dig3;  |O #30#14 ! dig4;  |O #30#15 ! dig5;
      |O #30#16 ! dig6;  |O #30#17 ! dig7;
      |O #30#18 ! dig8;  |O #30#19 ! dig9;
         |SI nSwMen = 0; |MENAV "     NO CORRESPONDEN LOS NIVELES"; |FINSI;
      |CIERRA #30;
      |ACABA;
   |FINSI;
   #0#16 = #30#1;
   anu1 = 1900 + #30#26;
   |SI anu1 [ 1970;
       anu1 = anu1 + 100;
   |FINSI;
   |PINTA #0#16;
   #30#2 = empre;
   #30#3 = ejerc;
   |LEE 011#30=;
   anu = 1900 + #30#26;
   |SI anu [ 1970;
      anu = anu + 100;
   |FINSI;
   |CIERRA #30;
|FPRC;

|PROCESO pintames; |TIPO 0;
   varcamp = Campo + 2;                || Coge el campo del nombre del mes
   |SI #0Campo ! 0;|Y #0Campo ! 13;
       NumMes = #30#11 + (#0Campo - 1);   || Operacion para saber que mes es
       |SI NumMes > 12; NumMes = NumMes - 12; |FINSI;
   |SINO;
       NumMes = #0Campo;
   |FINSI;
   |HAZ NombreMes;
   #0varcamp = NombreMes;
   |PINTA #0varcamp;                   || Pinta el nombre del mes
   |SI Campo = 1;  |Y #0#0 > #0#1;
      |MENAV "    Error de Entrada ...";
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO pintames2; |TIPO 0;
   |SI #0Campo ! 0;|Y #0Campo ! 13;
       NumMes = #30#11 + (#0Campo - 1);   || Operacion para saber que mes es
       |SI NumMes > 12; NumMes = NumMes - 12; |FINSI;
   |SINO;
       NumMes = #0Campo;
   |FINSI;
   |HAZ NombreMes;
   |ATRI I;
   |SI Campo = 7;  |PINTA 1525, NombreMes;  |FINSI;
   |SI Campo = 8;  |PINTA 1561, NombreMes;  |FINSI;
   |ATRI 0;
   |SI Campo = 8;  |Y #0#7 > #0#8;
      |MENAV "    Error de Entrada ...";
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO nivel; |TIPO 7;
   |SI #0#4 = "N";
       |C_M #0#5,1,"N";
   |SINO;
       |C_M #0#5,1,"S";
   |FINSI;
|FPRC;

|PROCESO miraniv; |TIPO 0;
   nivel = #0#5;
   |SI nivel = 1;|Y dig4 = 0;  |AVISO;  |ERROR;  |FINSI;
   |SI nivel = 2;|Y dig5 = 0;  |AVISO;  |ERROR;  |FINSI;
   |SI nivel = 3;|Y dig6 = 0;  |AVISO;  |ERROR;  |FINSI;
   |SI nivel = 4;|Y dig7 = 0;  |AVISO;  |ERROR;  |FINSI;
   |SI nivel = 5;|Y dig8 = 0;  |AVISO;  |ERROR;  |FINSI;
   |SI nivel = 6;|Y dig9 = 0;  |AVISO;  |ERROR;  |FINSI;
|FPRC;

|PROCESO estruct; |TIPO 0;
   |SI #0#6 = 0;  |ACABA;  |FINSI;
   |HAZ cue_per;
   |SI sw_error ! 0;
      |SI nSwMen = 0; |MENAV "    No existe la estructura de descarga."; |FINSI;
      |ERROR;
   |FINSI;
|FPRC;


|PROCESO cue_per; || calcula la cuenta de perdidas y ganancias
   sw_error = 0;
   |ABRE #6;
   |DDEFECTO #6;
   #6#0 = #0#6;
   |LEE 040#6=;
   |SI FSalida ! 0;
      sw_error = 1;
      |CIERRA #6;
      |ACABA;
   |FINSI;
   cue_perdi = #6#2;
   dig_perdi = #6#3;
   eaCuenta = #6#2;
   |HAZ SacaNivel;
   dig_perdi = enNivel;

   |SI #0#4 = "S";
      acor_c = cue_perdi;
      acor_d = dig_perdi;
      |HAZ acortacuen;
      cue_perdi = acor_c;
      dig_perdi = acor_d;
   |FINSI;
   |CIERRA #6;
|FPRC;

|PROCESO DatosEmp; |TIPO 7;
 |ABRE #8;
 |DDEFECTO #8;
 #8#0 = 1;
 |LEE 011#8=;
 |CIERRA #8;
 #0#18 = #8#7; |PINTA #0#18;
 alfa1 = #8#8; |C_M #0#18,1,alfa1;
|FPRC;

|| ... procesos iniciales
|PROCESO antesque;
   aparam = PARAMETRO;
   |QBF aparam; |SI aparam = " N"; aparam = ""; |FINSI;
   aNomCta = "cacuenta";
   |SI aparam = "fech";
       aNomCta = "cabalano";
   |FINSI;
   |SI aparam = "Analitica";
       aNomCta = eaUsuAna;
   |FINSI;

   |NOME_DAT #1 aNomCta;
   aNomCta1 = aNomCta;
|FPRC;
|| -------------------------------------------------

||.... .... Pide Existencias
|PROCESO conexi;
   enTExi = 1;
   nCam1 = empre;
   nCam2 = ejerc;
   nCam3 = enEjercicio;

   d_mes = #0#7;
   h_mes = #0#8;
   estru = #0#6;
   r_emp = empre;
   r_per = ejerc;
   r_eje = #30#26;
   r_nom = #30#1;
   |SUB_EJECUTA "caexicon.run";

   |SI #0#13 = "N"; |ACABA; |FINSI;
   r_emp = #0#14;
   r_per = #0#15;
   r_eje = anu1 - 2000; |SI r_eje < 0; r_eje = anu1 - 1900; |FINSI;
   r_nom = #0#16;
   |SUB_EJECUTA "caexicon.run";
   empre = nCam1;
   ejerc = nCam2;
   enEjercicio = nCam3;
|FPRC;

|| ========================================================================
||                     CALCULOS de FICHEROS AUXILIARES
|| ========================================================================
|PROCESO GraboTotal;
       #3#0 = #1#5;
       #3#1 = #1#4;
       #3#2 = #1#6;
       #3#3 = epigrafe;
       #3#4 = 0;
       |LEE 101#3=;
       |SI FSalida ! 0;
           |DDEFECTO #3;
           #3#0 = #1#5;
           #3#1 = #1#4;
           #3#2 = #1#6;
           #3#3 = epigrafe;
           #3#4 = 0;
           |HAZ leexplotA;
           #3#5 = t_agru;
           #3#6 = t_epig;
           #3#7 = t_part;
           |GRABA 020#3c;
       |FINSI;
       #3acumula = #3acumula + saldo;
       |SI saldo ! 0;
           #3#15 = 1;
           #3#16 = 1;
       |FINSI;
       |GRABA 020#3a;
       |LIBERA #3;
|FPRC;

|PROCESO grabatra;  || ... Graba temporal de Agrupaciones Exp0;

   saldo = saldo ! EURODB;

   |SI saldo = 0; |ACABA; |FINSI;

   |SI aMonedaC ! aMonedaA; |Y dos = 2;
       nRedo1 = EURODB;
       EURODB = nRedon;
   |FINSI;

   #3#0 = #1#5;
   #3#1 = #1#4;
   #3#2 = #1#6;
   #3#3 = #1#7;
   #3#4 = #1#8;
   |LEE 101#3=;
   |SI FSalida ! 0;
      |DDEFECTO #3;
      #3#0 = #1#5;
      #3#1 = #1#4;
      #3#2 = #1#6;
      #3#3 = #1#7;
      #3#4 = #1#8;
      |HAZ leexplot;
      #3#5 = t_agru;
      #3#6 = t_epig;
      #3#7 = t_part;
      |GRABA 020#3c;
   |FINSI;
   #3acumula = #3acumula + saldo;
   |SI saldo ! 0;
       #3#15 = 1;
       #3#16 = 1;
   |FINSI;
   |GRABA 020#3a;
   |LIBERA #3;


   || ... el total por grupos
|SI #0#19 = 2;
    epigrafe = 0;
    |SI #1#7 ! 0; |O #1#8 ! 0;
        |HAZ GraboTotal;
        |SI #1#4 ! 0;
            |SI #1#7 ! 0; |Y #1#8 ! 0;
                epigrafe = #1#7;
                |HAZ GraboTotal;
            |FINSI;
        |FINSI;
   |FINSI;
|FINSI;

   || ... Graba Auxiliar de Cuentas.

   |SI #0#4 = "N"; |ACABA; |FINSI;

   #5#0 = #1#5;
   #5#1 = #1#4;
   #5#2 = #1#6;
   #5#3 = #1#7;
   #5#4 = #1#8;
   #5#5 = #1#0;
   #5#6 = #1#1;
   #5#7 = #1#2;
   |LEE 000#5=;
   |SI FSalida ! 0;
      |DDEFECTO #5;
      #5#0 = #1#5;
      #5#1 = #1#4;
      #5#2 = #1#6;
      #5#3 = #1#7;
      #5#4 = #1#8;
      #5#5 = #1#0;
      #5#6 = #1#1;
      #5#7 = #1#2;
      |HAZ leexplot;
      #5#8  = t_agru;
      #5#9  = t_epig;
      #5#10 = t_part;
      #5#11 = 0;
      #5#12 = 0;
      |GRABA 020#5c;
   |FINSI;

   #5acumul2 = #5acumul2 + saldo;
   |GRABA 020#5a;
   |LIBERA #5;

   |SI aMonedaC ! aMonedaA; |Y dos = 2;
       EURODB = nRedo1;
   |FINSI;
|FPRC;

|PROCESO exis_expl;

   descar = #caexistl#des_exi# - #caexistl#has_exi#; || iniciales - finales
   |SI descar = 0; |ACABA; |FINSI;

   |SI aMonedaC ! aMonedaA; |Y dos = 2;
       impMoneda = descar;
       |HAZ ElCMoneda;
       descar = impMoneda;
   |FINSI;

  |SI descar ] 0;
      act_cuent = #caexistl#5;     || cuenta descarga D
      act_nivel = #caexistl#6;     || digito descarga D
   |SINO;
      act_cuent = #caexistl#8;     || cuenta descarga H
      act_nivel = #caexistl#9;     || digito descarga H
   |FINSI;

   eaCuenta = act_cuent;
   |HAZ SacaNivel;
   act_nivel = enNivel;

   acor_c = act_cuent; acor_d = act_nivel;
   |HAZ acortacuen;
   act_cuent = acor_c; act_nivel = acor_d;

   |SI act_cuent = #1#0;
      sw_exp = 1;
      |SI descar ] 0;
         debe = debe + descar;
      |SINO;
         haber = haber - descar;
      |FINSI;
      saldo = debe - haber;
   |FINSI;
|FPRC;

|DEFBUCLE exis2;
   #7#1;
   ;
   #0#6, 01;
   #0#6, 99;
   e;
   NULL, exis_expl;
|FIN;

|PROCESO exis_3000;
   sw_exp  = 1;
   impMoneda = #7has_exi;
   |SI aMonedaC ! aMonedaA; |Y dos = 2;  |HAZ ElCMoneda; |FINSI;

   saldo   = saldo + impMoneda;
|FPRC;

|DEFBUCLE exis1;
   #7#2;
   ;
   #0#6, #1#0;
   #0#6, alar_c;
   e;
   NULL,exis_3000;
|FIN;

|PROCESO calcular;
   saldo   = 0;
   debe    = 0;
   haber   = 0;
   sw_exp  = 0;
   alfa1   = #1#0 % 101;
   |SI alfa1 = "3";                 |BUCLE exis1; |FINSI;
   |SI alfa1 = "6"; |O alfa1 = "7"; |BUCLE exis2; |FINSI;

   |SI sw_exp = 1; |ACABA; |FINSI;

   |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
      y = x + 1;

      impMoneda = #1x;
      |SI aMonedaC ! aMonedaA; |Y dos = 2;  |HAZ ElCMoneda; |FINSI;
      debe  = debe  + impMoneda;

      impMoneda = #1y;
      |SI aMonedaC ! aMonedaA; |Y dos = 2; |HAZ ElCMoneda; |FINSI;
      haber = haber + impMoneda;

      saldo = debe - haber;
   |SIGUE;
|FPRC;

|PROCESO grabar;
   |HAZ digitos2;
   |SI d_error ! 0;  |ACABA;  |FINSI;
   |SI #0#4 = "N";  |Y #1#3 = "N";                     |ACABA;  |FINSI;
   |SI #0#4 = "S";  |Y #1#55 > nivel;                  |ACABA;  |FINSI;
   |SI #0#4 = "S";  |Y #1#3 = "N";  |Y #1#55 ! nivel;  |ACABA;  |FINSI;

   alar_c = #1#0;
   |HAZ alargacuen;

   |SI #1#0 ! cue_perdi; || no es perdidas y ganancias
       |HAZ calcular;
       |SI saldo ! 0;
           |SI #1#5 = "H"; |O #1#5 = "P";
               saldo = - saldo;
           |FINSI;
           |HAZ grabatra;
           |HAZ sumasaldo;
       |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE cabatodo0;
   #1#5;
   ;
   d_cuen, d_digi;
   h_cuen, h_digi;
   e;
   NULL, grabar;
|FIN;
|| ************************************************************************

|PROCESO sumasaldo;    || Calculamos cada cuenta
   |SI #1#5 = "A"; activo = activo + saldo; |FINSI;
   |SI #1#5 = "P"; pasivo = pasivo + saldo; |FINSI;
   |SI #1#5 = "D";
      |SI #1#4 = 1;
         debe1 = debe1 + saldo;
      |FINSI;
      |SI #1#4 = 2;
         debe2 = debe2 + saldo;
      |FINSI;
      |SI #1#4 = 3;
         debe3 = debe3 + saldo;
      |FINSI;
      |SI #1#4 = 4;
         debe4 = debe4 + saldo;
      |FINSI;
   |FINSI;
   |SI #1#5 = "H";
      |SI #1#4 = 1;
         haber1 = haber1 + saldo;
      |FINSI;
      |SI #1#4 = 2;
         haber2 = haber2 + saldo;
      |FINSI;
      |SI #1#4 = 3;
         haber3 = haber3 + saldo;
      |FINSI;
      |SI #1#4 = 4;
         haber4 = haber4 + saldo;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO calcula_tot;    || Calculamos totales

   punto_d1 = 0;   punto_h1 = 0;
   punto_d2 = 0;   punto_h2 = 0;
   punto_d3 = 0;   punto_h3 = 0;
   punto_d4 = 0;   punto_h4 = 0;
   punto_d5 = 0;   punto_h5 = 0;
   punto_d6 = 0;   punto_h6 = 0;

   canti = haber1 - debe1;
   |SI canti < 0;
      punto_h1 = - canti;
   |SINO;
      punto_d1 = canti;
   |FINSI;

   canti = haber2 - debe2;
   |SI canti < 0;
      punto_h2 = - canti;
   |SINO;
      punto_d2 = canti;
   |FINSI;

   canti = punto_d1 + punto_d2 - punto_h1 - punto_h2;
   |SI canti < 0;
      punto_h3 = - canti;
   |SINO;
      punto_d3 = canti;
   |FINSI;

   canti = haber3 - debe3;
   |SI canti < 0;
      punto_h4 = - canti;
   |SINO;
      punto_d4 = canti;
   |FINSI;

   canti = punto_d3 + punto_d4 - punto_h3 - punto_h4;
   |SI canti < 0;
      punto_h5 = - canti;
   |SINO;
      punto_d5 = canti;
   |FINSI;

  punto_h6 = punto_h5;
  |SI punto_h5 > 0;
      punto_h6 = punto_h6 + debe4;
  |FINSI;
  |SI punto_h6 < 0; punto_h6 = 0; |FINSI;

  punto_d6 = punto_d5;
  |SI punto_d6 > 0;
      punto_d6 = punto_d6 - debe4;
  |SINO;
      |SI debe4 > 0;                       || R.PRIEGO (24.03.99) impuesto neg.
          punto_d6 = punto_d6 - debe4;
      |FINSI;
  |FINSI;
  |SI punto_d6 < 0;
      |SI punto_h6 = 0;  punto_h6 = - punto_d6; |FINSI;
      punto_d6 = 0;
  |FINSI;
|FPRC;
|| ***********************************************************************
|PROCESO gr_total2;

   saldoIni = saldo;
   |SI #1#5 = "P"; saldo = - saldo; |FINSI;
   |HAZ grabatra; || ....................... Graba la cuenta 129. como otra mas.
   |SI #1#5 = "A"; activo = activo + saldo; |FINSI;
   |SI #1#5 = "P"; pasivo = pasivo + saldo; |FINSI;

   debe  = debe1  + debe2  + debe3  + debe4;
   haber = haber1 + haber2 + haber3 + haber4;
   saldo = debe - haber;
   nResultado = saldo;
   |SI #1#5 = "P";
       saldo = - saldo;
   |FINSI;
   |SI #1#5 = "A"; activo = activo + saldo; |FINSI;
   |SI #1#5 = "P"; pasivo = pasivo + saldo; |FINSI;
|FPRC;

|PROCESO gr_final; || Grabar totales
   debe   = 0;
   haber  = 0;
   #1#0   = cue_perdi;
   #1#1   = dig_perdi;
   |LEE 001#1=;
   |SI FSalida ! 0;
      sw_error = 1;
      |SI nSwMen = 0; |MENAV "    ATENCION !!!. NO existe la cuenta de Perdidas y Ganancias "; |FINSI;
      |ACABA;
   |FINSI;
   |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
      y = x + 1;

      impMoneda = #1x;
      |SI aMonedaC ! aMonedaA; |Y dos = 2;  |HAZ ElCMoneda; |FINSI;
      debe  = debe  + impMoneda;

      impMoneda = #1y;
      |SI aMonedaC ! aMonedaA; |Y dos = 2; |HAZ ElCMoneda; |FINSI;
      haber = haber + impMoneda;
      saldo = debe - haber;

   |SIGUE;

   |SI #0#19 = 2;
       |HAZ gr_total2;
   |SINO;

       debe  = debe1  + debe2  + debe3  + debe4;
       haber = haber1 + haber2 + haber3 + haber4;
       saldo = saldo + ( debe - haber);
       |SI #1#5 = "P";
           saldo = - saldo;
       |FINSI;
       |HAZ grabatra; || ....................... Graba la cuenta 129. como otra mas.

       |SI #1#5 = "A"; activo = activo + saldo; |FINSI;
       |SI #1#5 = "P"; pasivo = pasivo + saldo; |FINSI;

   |FINSI;
   || Grabar totales
   |HAZ calcula_tot;

   |PARA nume1 = 1; |SI nume1 [ 15; |HACIENDO nume1 = nume1 + 1;
      saldo = 0;
      agrupaci = 99;
      epigrafe = 00;
      partida  = 00;
      |SI nume1 = 1;                bloque = 0;         tipo = "A"; |FINSI;
      |SI nume1 = 2;                bloque = 0;         tipo = "P"; |SI #0#19 = 2; agrupaci = 100; |FINSI; |FINSI;
      |SI nume1 ] 3; |Y nume1 [ 8;  bloque = nume1 - 2; tipo = "D"; |FINSI;
      |SI nume1 ] 9; |Y nume1 [ 14; bloque = nume1 - 8; tipo = "H"; |FINSI;
      |SI nume1 = 15;
          |SI #0#19 = 1;
              |VETE Salir;
          |SINO;
              bloque   = 0;
              tipo     = "P";
          |FINSI;
      |FINSI;
      |SI bloque = 3; bloque = 2; agrupaci = 100; |FINSI;
      |SI bloque = 4; bloque = 3; |FINSI;
      |SI bloque = 5; bloque = 3; agrupaci = 100; |FINSI;
      #3#0 = tipo;
      #3#1 = bloque;
      #3#2 = agrupaci;
      #3#3 = epigrafe;
      #3#4 = partida;
      |LEE 101#3=;
      |SI FSalida ! 0;
         |DDEFECTO #3;
         #3#0 = tipo;
         #3#1 = bloque;
         #3#2 = agrupaci;
         #3#3 = epigrafe;
         #3#4 = partida;
         |HAZ titulos;
         t_agru = variable %  140;
         t_epig = variable % 4120;
         #3#5 = t_agru;
         #3#6 = t_epig;
         #3#7 = "";
         |GRABA 020#3c;
      |FINSI;
      |SI tipo = "A"; saldo = activo; |FINSI;
      |SI tipo = "P";
          saldo = pasivo;
          |SI #0#19 = 2; |Y agrupaci = 99; saldo = nResultado; |FINSI;
      |FINSI;
      |SI tipo = "D";
         |SI nume1 = 3; saldo = punto_d1; |FINSI;
         |SI nume1 = 4; saldo = punto_d2; |FINSI;
         |SI nume1 = 5; saldo = punto_d3; |FINSI;
         |SI nume1 = 6; saldo = punto_d4; |FINSI;
         |SI nume1 = 7; saldo = punto_d5; |FINSI;
         |SI nume1 = 8; saldo = punto_d6; |FINSI;
      |FINSI;
      |SI tipo = "H";
         |SI nume1 =  9; saldo = punto_h1; |FINSI;
         |SI nume1 = 10; saldo = punto_h2; |FINSI;
         |SI nume1 = 11; saldo = punto_h3; |FINSI;
         |SI nume1 = 12; saldo = punto_h4; |FINSI;
         |SI nume1 = 13; saldo = punto_h5; |FINSI;
         |SI nume1 = 14; saldo = punto_h6; |FINSI;
      |FINSI;
      #3acumula = #3acumula + saldo;
       |SI saldo ! 0;
           #3#15 = 1;
           #3#16 = 1;
       |FINSI;

      |GRABA 020#3a;
      |LIBERA #3;
      |ET Salir;
   |SIGUE;
|FPRC;

|| ***********************************************************************
|PROCESO cantidad;
   |SI sw_inf = 0;
      |SI menu2 = 2;
         |PRINT; sw = 2;
      |SINO;
         |HAZ operapanta;
      |FINSI;
   |SINO;
      |SI cantidad ! 0; |O cantidad2 ! 0; |O swimp = 0;
         |SI menu2 = 2;
            |PRINT; sw = 2;
         |SINO;
            |HAZ operapanta;
         |FINSI;
      |SINO;
         |SI eaValeCero = "S"; |Y #3#2 = 99;
             |SI menu2 = 2;
                 |SI #3#0 = "A"; |O #3#0 = "P";
                     |PRINT; sw = 2;
                 |FINSI;
             |SINO;
                 |HAZ operapanta;
             |FINSI;
         |FINSI;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO cuenta1;
   sw_alguno = 1;
   cta = #cabaexp2#5;
   nom = #cabaexp2#7;
   cantidad = #cabaexp2#11;
   cantidad2 = #cabaexp2#12;
   sw_inf = 2;
   |HAZ cantidad;
   |SI menu2 = 1;
      |SI sw_error = 1; sw_alguno = 0; sw = 0; |ERROR10; |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE cabatodo5;
   #5#1;
   ;
   #3#0, #3#1, #3#2, #3#3, #3#4, "            ", 0;
   #3#0, #3#1, #3#2, #3#3, #3#4, "zzzzzzzzzzzz", 9;
   e;
   NULL, NULL, NULL, NULL, NULL, cuenta1;
   #5#5, #5#6;
|FIN;

|PROCESO impresio;
   |SI d_bloque = 0;
      |SI #3#0 = "D"; |O #3#0 = "H"; |ACABA; |FINSI; || hay algun error el la estructura
   |FINSI;
   |SI sw = 0;
      |HAZ canv_tipo;
      |SI menu2 = 1; |HAZ panta; |FINSI;
      sw = 1;
   |FINSI;

   |SI tipo ! #3#0;
      |SI menu2 = 2;
          |SI sw = 2; |PIEINF; |FINSI;
          sw = 0;
      |FINSI;
      |HAZ canv_tipo;
      |SI menu2 = 1;
         |HAZ cambiapag;
         |SI sw_error = 1; sw = 0; |ERROR10; |ACABA; |FINSI;
      |FINSI;
   |FINSI;

   |SI bloque ! #3#1; |HAZ canv_bloq; |FINSI;

   cantidad  = #3#8;
   cantidad2 = #3#10;

   |SI agrupaci ! #3#2; |HAZ canv_agru; |FINSI;

   |SI sw_agr = 0;
      |HAZ imp_agr;
      |SI menu2 = 1;
         |SI sw_error = 1; sw = 0; |ERROR10; |ACABA; |FINSI;
      |FINSI;
   |FINSI;

   |SI epigrafe ! #3#3; |HAZ canv_epi; |FINSI;

   |SI sw_epi = 0;
      |HAZ imp_epi;
      |SI menu2 = 1;
         |SI sw_error = 1; sw = 0; |ERROR10; |ACABA; |FINSI;
      |FINSI;
   |FINSI;

   |SI #3#4 ! 0; |Y #3#4 < 99;
      sw_inf = 1;
      variable = "          " + #3#7;
      |HAZ cantidad;
      |SI menu2 = 1;
         |SI sw_error = 1; sw = 0; |ERROR10; |ACABA; |FINSI;
      |FINSI;
   |FINSI;

   partida = #3#4;
   |SI #0#4 = "S";
      sw_alguno = 0;
      |BUCLE cabatodo5;
      |SI menu2 = 1;
         |SI sw_error = 1; sw = 0; |ERROR10; |ACABA; |FINSI;
      |FINSI;
      |SI sw_alguno = 1;
         sw_inf = 4;
         |SI menu2 = 2;
            |PRINT; sw = 2;
         |SINO;
            |HAZ operapanta;
         |FINSI;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO imp_agr;
   sw_inf = 0;
   variable = #3#5;
   |SI #3#2 ] 99;
      sw_epi = 1;
      sw_inf = 3;
      variable = #3#5 + #3#6;
   |SINO;
      |SI #3#3 = 0; |Y #3#4 = 0; || ... Agrupacion con importe.
         sw_epi = 1;
         sw_inf = 3; || ... |SI #0#4 = "N"; sw_inf = 1; |FINSI; ||aqui
      |FINSI;
   |FINSI;
   sw_agr = 1;
   |HAZ cantidad;
|FPRC;

|PROCESO imp_epi;
   sw_inf = 0;
   variable = "     " + #3#6;
   |SI #3#4 = 0;
      sw_inf = 1; |SI #0#4 = "S"; sw_inf = 3; |FINSI;  ||aqui
   |FINSI;
   sw_epi = 1;
   |SI #3#3 = 0; |ACABA; |FINSI;
   swimp = 0; |SI #3#16 = 0; swimp = 1; |FINSI;
   |HAZ cantidad;
   swimp = 0;
|FPRC;

|PROCESO canv_tipo;
   sw_agr = 0;
   sw_epi = 0;
   tipo     = #3#0;
   bloque   = #3#1;
   agrupaci = #3#2;
   epigrafe = #3#3;
   partida  = 0;
   |SI #3#0 = "A"; doh = "ACTIVO"; |FINSI;
   |SI #3#0 = "P"; doh = "PASIVO"; |FINSI;
   |SI #3#0 = "D"; doh = "DEBE";   |FINSI;
   |SI #3#0 = "H"; doh = "HABER";  |FINSI;
|FPRC;

|PROCESO canv_bloq;
   |SI #3#8 ! 0; |O #3#10 ! 0;
      sw_inf = 4;
      |SI menu2 = 2;
         |PRINT; sw = 2;
      |SINO;
         |HAZ operapanta;
      |FINSI;
   |FINSI;
   sw_epi = 0;
   bloque   = #3#1;
   agrupaci = #3#2; |SI agrupaci ] 99; sw_agr = 0; |FINSI;
   epigrafe = #3#3;
|FPRC;

|PROCESO canv_agru;
   |SI #3#8 ! 0; |O #3#10 ! 0; |O #3#15 ! 0;
      sw_inf = 4;
      |SI menu2 = 2;
         |PRINT; sw = 2;
      |SINO;
         |HAZ operapanta;
      |FINSI;
   |FINSI;
   sw_agr = 0;
   sw_epi = 0;
   agrupaci = #3#2;
   epigrafe = #3#3;
|FPRC;

|PROCESO canv_epi;
   sw_epi = 0;
   epigrafe = #3#3;
|FPRC;

|DEFBUCLE cabatodo2;
   #3#1;
   ;
   d_tipo, d_bloque,  00, 00, 00;
   h_tipo, h_bloque, 100, 99, 99;
   e;
   NULL, impresio;
|FIN;

|PROCESO cabatodo1;

   |SI nSwLInv = 0; enNumPag = #0#20; |FINSI;

   titulo   = "     < BALANCE DE SITUACION >     ";
   d_tipo   = "A";
   h_tipo   = "P";
   d_bloque = 0;
   h_bloque = 0;
   sw       = 0;
   |BUCLE cabatodo2;
   |SI sw ] 1;
      |SI menu2 = 2;
         |SI sw = 2; |PIEINF; |FINSI;
      |SINO;
         swfinal = 1;
         |HAZ cambiapag;
         |SI sw_error = 1; |ACABA; |FINSI;
         swfinal = 0;
      |FINSI;
   |FINSI;
   titulo = "< CUENTA DE PERDIDAS Y GANANCIAS >";
   d_tipo = "D";
   h_tipo = "H";
   d_bloque = 1;
   h_bloque = 9;
   sw = 0;

   |BUCLE cabatodo2;
   |SI sw ] 1;
      |SI menu2 = 2;
         |SI sw = 2; |PIEINF; |FINSI;
      |SINO;
         swfinal = 1;
         |HAZ cambiapag;
         |SI sw_error = 1; |ACABA; |FINSI;
         swfinal = 0;
      |FINSI;
   |FINSI;
|FPRC;

|| ******************** Programa por TIPO 3 **************************
|PROCESO impre;

   #0#20 = #0#20 - 1;
   aMonedaA = eaMoneda;
   |SI #0#13 = "N"; aMonedaC = aMonedaA; |FINSI;
   |HAZ antesque;

   |SI menu2 = 2;  || Ha elegido Impresora
       |SI aparam ! ""; |Y aparam ! "fech"; |Y aparam ! "Analitica";
           |SI nSwLInv = 0;
               Impresora = eaNomImp + "?"+ eaUnidadBTmp + "tmp1,,,?";
               |SI Compara = 1;
                   Impresora = Impresora + "Bal.Sit.Comp";
               |SINO;
                   Impresora = Impresora + "Bal.Sit.";
               |FINSI;
               |SI enBasImp ! 0;
                   |IMPRE -1;
               |SINO;
                   |IMPRE 0;
               |FINSI;
               |SI FSalida ! 0;  |ACABA;  |FINSI;
          |FINSI;
      |SINO;
          |IMPRE 0;
          |SI FSalida ! 0;  |ACABA;  |FINSI;
      |FINSI;

      |HAZ los_informes;
      |SI #0#13 = "N";
          informe = que_i1;
      |SINO;
          informe = que_i2;
      |FINSI;
      enDatos = 1;
      |SI #0#18 = "N";
          enDatos = 0;
          aAlfa1 = informe % 102;
          |SI aAlfa1 ! "cl";
              aAlfa2 = informe % -105;
              informe = aAlfa1 + "n" + aAlfa2;
          |FINSI;
      |FINSI;
      |INFOR informe;
      |SI FSalida ! 0;
          |SI nSwMen = 0; |MENAV "     Error en informe."; |FINSI;
          |SI nSwLInv = 0; |FINIMP; |FINSI;
          |ACABA;
      |FINSI;
   |FINSI;

   CodigoEmpresa    = enEmpresa;
   EjercicioEmpresa = enEjercicio;
   NombreEmpresa    = eaNombreEmp;

   |SI aparam = ""; |O aparam = "fech"; |O aparam = "Analitica";
       |SI #48#27= "S"; |Y aparam ! "Analitica";
           |HAZ MultiEmpresa;
       |SINO;
           aMonedaA = eaMoneda;
           |DFICE dirfich0;
           |HAZ impre1;
       |FINSI;
   |SINO;
       dirfich0 = fich_alta;
       aMonedaA = eaMoneda;
       |HAZ impre1;
   |FINSI;
   |SI menu2 = 2;  || Ha elegido Impresora
      |FININF;
      |SI nSwLInv = 0; |FINIMP; |FINSI;
   |FINSI;
|FPRC;


|PROCESO impre1;

   eaIa = #30#21;
   enM1 = #0#0;
   enM2 = #0#1;
   |HAZ LeeDatosEmpresa;
   |SI aparam = "fech";
       |SI enM1 ! enM1f;
           NumMes = enM1f; |HAZ NombreMes; #0#2 = NombreMes;
       |FINSI;
       |SI enM2 ! enM2f;
           NumMes = enM2f; |HAZ NombreMes; #0#3 = NombreMes;
       |FINSI;
   |FINSI;
   |SI enMAper ! dig1;
       |SI #0#1 < enMAper; |Y #0#1 ! 0;
           |ACABA;
       |FINSI;
   |FINSI;

   enNumPag = #0#20;
   nPagDup = enNumPag;

   |SI #0#17 = "S";
       |HAZ conexi;
       eaMoneda = aMonedaA; |HAZ Monedas;
   |FINSI;

   |ABRE #3;
   |CIERRA #3;
   |DELINDEX #3;
   |ABRE #5;
   |CIERRA #5;
   |DELINDEX #5;

   mesi = #0#2;
   mesf = #0#3;
   fec  = #0#12;

   eldesde = (#0#0 * 3) + 9;
   elhasta = (#0#1 * 3) + 9;
   des_exi =  #0#7 + 11;
   has_exi =  #0#8 + 11;
   d_cuen = "            "; d_digi = 0;
   h_cuen = "999999999999"; h_digi = 9;
   nivel = #0#5;

  |PARA dos = 1; |SI dos [ 2; |HACIENDO dos = dos + 1;
        |NOME_DAT #1 aNomCta;
        acumula = 8;
        acumul2 = 11;
        activo  = 0;
        pasivo  = 0;
        explo   = 0;
        debe1   = 0;
        debe2   = 0;
        debe3   = 0;
        debe4   = 0;
        haber1  = 0;
        haber2  = 0;
        haber3  = 0;
        haber4  = 0;
        |SI dos = 2; |Y #0#13 = "N";
            |SAL;
        |FINSI;
        |SI dos = 2;
            acumula = 10;
            acumul2 = 12;

            |DBASE dir_fich;
            dir_fich = dir_fich + "fich/";
            alfa1 = #0#14;   alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
            alfa2 = #0#15;   alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
            alfa3 = dir_fich + alfa1 + alfa2 + "/";

            |PATH_DAT #1  alfa3;
            |PATH_DAT #2  alfa3;
            |PATH_DAT #9  alfa3;
            |PATH_DAT #11 alfa3;
            |PATH_DAT #12 alfa3;
            |PATH_DAT #6  alfa3;
            |PATH_DAT #7  alfa3;
            |PATH_DAT #8  alfa3;

            |ABRE #30;
            #30#2 = #0#14;
            #30#3 = #0#15;
            |LEE 011#30=;
            |CIERRA #30;
            aMonedaC = #30#28;
            eaMoneda = aMonedaC;
            |HAZ Monedas;
            |SI aMonedaA = "E"; EURODB = 2; |FINSI;

            pathbal = dirfich0;
            dirfich0 = alfa3;
            |HAZ FecApeCie;
            dirfich0 = pathbal;
            |SI enMAper ! #30#11;
                |SI #0#1 < enMAper; |Y #0#1 ! 0;
                    |SAL;
                |FINSI;
            |FINSI;

        |FINSI;

       ||... Modificar Cuentas a balance segun saldo
       |SI dos = 1;
           r_emp = empre;
           r_per = ejerc;
        |SINO;
           r_emp = #0#14;
           r_per = #0#15;
        |FINSI;
        eldesde = (#0#0 * 3) + 11;
        elhasta = (#0#1 * 3) + 11;
        enSwOper = 0;
        |SI aparam = ""; |O aparam = "fech";
            |SUB_EJECUTA "carecsig";
        |FINSI;

        |ABRE #8;
        |DDEFECTO #8;
        #8#0 = 1;
        |LEE 000#8=;
        |CIERRA #8;

        |SI #8#2 = "S";  |Y #8#3 = "N";  |Y aparam =  "";
            |SI dos = 2;
                pathbal = alfa3;
            |SINO;
                pathbal = dirfich0;
            |FINSI;
            balance = 1;

            |SI #caconimp#10 = "S";
                enSwOper = 2;
                |SI dos = 1;
                    r_emp = empre;
                    r_per = ejerc;
                |SINO;
                    r_emp = #0#14;
                    r_per = #0#15;
                |FINSI;

                |SUB_EJECUTA "carecsig";

                enSwOper = 0;
                |SI enECuadre = 1; |ACABA; |FINSI;

                |NOME_DAT #1 eaNomFCta;
                |SI dos = 1; aNomCta1 = eaNomFCta; |FINSI;
            |SINO;
                |SUB_EJECUTA "careccue";
            |FINSI;
       |FINSI;

       eldesde = (#0#0 * 3) + 9;
       elhasta = (#0#1 * 3) + 9;
      || ---------------------------------------------------------------------
      |SI aMonedaA = "E"; EURODB = 2; |FINSI;

      |HAZ cue_per;  || calculamos la cuenta de perdidas y ganancias.

      |ABRET #1;
      |ABRE #2;
      |ABRE #9;
      |ABRE #3;
      |ABRE #5;
      |BUCLE cabatodo0;      || Bucle de Calculos cuentas
      |HAZ gr_final;         || Graba Totales

      |CIERRAT #1;
      |CIERRA #2;
      |CIERRA #9;
      |CIERRA #3;
      |CIERRA #5;
      |SI sw_error = 1; |ACABA; |FINSI;
 |SIGUE; || ==================================================================

   |ABRE #30;
   #30#2 = empre;
   #30#3 = ejerc;
   |LEE 011#30=;
   |CIERRA #30;

   |SI #0#13 = "S"; eaMoneda = aMonedaA; |HAZ Monedas; |FINSI;

   empcon = #30#1;
   ejecon = #30#26;
   |ABRE #3;
   |ABRE #5;

   |HAZ cabatodo1;      || Proceso de Impresion

   |CIERRA #3;
   |CIERRA #5;
   |DELINDEX #3;
   |DELINDEX #5;
|FPRC;

|PROCESO leexplot;  || Lee el fichero de explotacion
   t_agru = "";
   t_epig = "";
   t_part = "";
   |SI #1#4 = 0;
      #caparbal#0 = #1#5;
      #caparbal#1 = #1#6;
      #caparbal#3 = #1#7;
      #caparbal#5 = #1#8;
      |LEE 040#9=; |SI FSalida ! 0; |DDEFECTO #9; |FINSI;
      t_agru = #caparbal#2;
      t_epig = #caparbal#4;
      t_part = #caparbal#6;
   |SINO;
      #caparexp#0 = #1#4;
      #caparexp#1 = #1#5;
      #caparexp#2 = #1#6;
      #caparexp#4 = #1#7;
      #caparexp#6 = #1#8;
      |LEE 040#2=; |SI FSalida ! 0; |DDEFECTO #2; |FINSI;
      t_agru = #caparexp#3;
      t_epig = #caparexp#5;
      t_part = #caparexp#7;
   |FINSI;
|FPRC;

|PROCESO leexplotA;  || Lee el fichero de explotacion
   t_agru = "";
   t_epig = "";
   t_part = "";
   |SI epigrafe = 0;
       |ABRE #11;
       #11#0 = #1#5;
       #11#1 = #1#6;
       |LEE 040#11=; |SI FSalida ! 0; |DDEFECTO #11; |FINSI;
       t_agru = #11#2;
       |CIERRA #11;
   |SINO;
       |ABRE #12;
       #12#0 = #1#5;
       #12#1 = #1#6;
       #12#3 = epigrafe;
       |LEE 040#12=; |SI FSalida ! 0; |DDEFECTO #12; |FINSI;
       t_agru = #12#2;
       t_epig = #12#4;
       |CIERRA #12;
   |FINSI;
|FPRC;

|PROCESO acortacuen;
   |SI #0#4 = "N"; |ACABA; |FINSI;
   |SI nivel < acor_d;
      |SI nivel = 1;  traraf = 100 + dig4;  |FINSI;
      |SI nivel = 2;  traraf = 100 + dig5;  |FINSI;
      |SI nivel = 3;  traraf = 100 + dig6;  |FINSI;
      |SI nivel = 4;  traraf = 100 + dig7;  |FINSI;
      |SI nivel = 5;  traraf = 100 + dig8;  |FINSI;
      acor_c = acor_c % traraf;
      acor_d = nivel;
      acor_c = acor_c + "              ";
      acor_c = acor_c % 112;
   |FINSI;
|FPRC;

|PROCESO alargacuen;
   |QBF alar_c;
   alar_c = alar_c + "99999999999999";
   alar_c = alar_c % 112;
|FPRC;

|PROCESO titulos;

   |SI nume1 =  1; variable = "123456789*123456789*123456789*123456789*123456789*123456789*"; |FINSI;
   |SI nume1 =  1; variable = "                         T O T A L   A C T I V O :          "; |FINSI;
   |SI nume1 =  2; variable = "                         T O T A L   P A S I V O :          "; |FINSI;
   |SI nume1 =  3; variable = "  I. BENEFICIOS DE EXPLOTACION ............................."; |FINSI;
   |SI nume1 =  4; variable = " II. RESULTADOS FINANCIEROS POSITIVOS ......................"; |FINSI;
   |SI nume1 =  5; variable = "III. BENEFICIOS DE LAS ACTIVIDADES ORDINARIAS .............."; |FINSI;
   |SI nume1 =  6; variable = " IV. RESULTADOS EXTRAORDINARIOS POSITIVOS .................."; |FINSI;
   |SI nume1 =  7; variable = "  V. BENEFICIO ANTES DE IMPUESTOS .........................."; |FINSI;
   |SI nume1 =  8; variable = " VI. RESULTADO DEL EJERCICIO (BENEFICIOS) .................."; |FINSI;
   |SI nume1 =  9; variable = "  I. PERDIDAS DE EXPLOTACION ..............................."; |FINSI;
   |SI nume1 = 10; variable = " II. RESULTADOS FINANCIEROS NEGATIVOS ......................"; |FINSI;
   |SI nume1 = 11; variable = "III. PERDIDAS DE LAS ACTIVIDADES ORDINARIAS ................"; |FINSI;
   |SI nume1 = 12; variable = " IV. RESULTADOS EXTRAORDINARIOS NEGATIVOS .................."; |FINSI;
   |SI nume1 = 13; variable = "  V. PERDIDAS ANTES DE IMPUESTOS ..........................."; |FINSI;
   |SI nume1 = 14; variable = " VI. RESULTADO DEL EJERCICIO (PERDIDAS) ...................."; |FINSI;
   |SI nume1 = 15; variable = "                         RESULTADO DEL EJERCICIO ..........."; |FINSI;
|FPRC;

|| ***********************************************************************
|PROCESO operapanta;   || Operaciones por pantalla;

   |SI lin > 1300;
      |SI sw_inf = 4; |ACABA; |FINSI;
      swfinal = 0;
      |HAZ cambiapag;  || Cambia de pagina
      |SI sw_error = 1; |ACABA; |FINSI;
   |FINSI;

   |C_V #catrabap#9, 1,"N";
   |C_V #catrabap#10,1,"N";
   |C_V #catrabap#11,1,"N";
   |C_V #catrabap#12,1,"N";
   |C_V #catrabap#13,1,"N";
   |C_V #catrabap#14,1,"N";

   |PDEFECTO #10, 9,15;

   |SI sw_inf = 0;
      #catrabap#12  = variable; |C_V #catrabap#12,1,"S";
   |FINSI;

   |SI sw_inf = 1; |O sw_inf = 3;
      #catrabap#12  = variable; |C_V #catrabap#12,1,"S";
      |SI #0#13 = "S";
         #catrabap#13 = cantidad;  |C_V #catrabap#13,1,"S";
         #catrabap#14 = cantidad2; |C_V #catrabap#14,1,"S";
      |SINO;
         #catrabap#14 = cantidad;  |C_V #catrabap#14,1,"S";
      |FINSI;
   |FINSI;
   |SI sw_inf = 2;
      #catrabap#9 = cta + " " + nom; |C_V #catrabap#9,1,"S";
      #catrabap#10 = cantidad;       |C_V #catrabap#10,1,"S";
      |SI #0#13 = "S";
         #catrabap#11 = cantidad2; |C_V #catrabap#11,1,"S";
      |FINSI;
   |FINSI;

   Pos = lin;
   |PINDA #0#10;         || Pinta las lineas
   Pos = -1;
   lin = lin + 100;     || Posicionar una linea mas abajo

|FPRC;

|PROCESO cambiapag;
   sw_error = 0;
   lin = 100;
   |PAUSA;
   |SI 1 = FSalida;|O 7 = FSalida;
      sw_error = 1;
   |SINO;
      |SI swfinal = 0; |HAZ panta; |FINSI;
   |FINSI;
|FPRC;

|PROCESO panta;

   lin = 100;
   |DDEFECTO #10;
   |PINPA #0#10;       || Pinta la pantalla del informe
   #catrabap#0 = fec;        || Fecha
   #catrabap#1 = CodigoEmpresa;      || Empresa
   #catrabap#2 = EjercicioEmpresa;     || Ejercicio
   #catrabap#3 = NombreEmpresa;      || Nombre Empresa
   #catrabap#4 = mesi;       || mes desde
   #catrabap#5 = mesf;       || mes hasta
   #catrabap#6 = doh;        || titulo (Activo/Pasivo/Debe/Haber)
   #catrabap#7 = anu;
   #catrabap#8 = anu1;

   |SI #0#13 = "S";
      |ATRI R;
      |PINTA 0856, "A¤o: ";
      |PINTA 0870, "A¤o: ";
      |ATRI 0;
      |C_V #catrabap#7,1,"S";
      |C_V #catrabap#8,1,"S";
   |SINO;
      |C_V #catrabap#7,1,"N";
      |C_V #catrabap#8,1,"N";
   |FINSI;
   |C_V #catrabap#9, 1,"N";
   |C_V #catrabap#10,1,"N";
   |C_V #catrabap#11,1,"N";
   |C_V #catrabap#12,1,"N";
   |C_V #catrabap#13,1,"N";
   |C_V #catrabap#14,1,"N";
   |PINDA #0#10;
|FPRC;

|| ---------------------------------------------------------------------
|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |SI #0#13 = "S"; #0#14 = #caselem1#1; |FINSI;

   CodigoEmpresa    = enEmpresa;
   EjercicioEmpresa = enEjercicio;
   NombreEmpresa    = eaNombreEmp;


   nYaLoHeDicho = 0;
   |PATH_DAT #1  dirfich0;
   |PATH_DAT #2  dirfich0;
   |PATH_DAT #9  dirfich0;
   |PATH_DAT #11 dirfich0;
   |PATH_DAT #12 dirfich0;
   |PATH_DAT #3  dirfich0;
   |PATH_DAT #5  dirfich0;
   |PATH_DAT #6  dirfich0;
   |PATH_DAT #7  dirfich0;
   |PATH_DAT #8  dirfich0;

   aMonedaA = eaMoneda;
   |SI #0#13 = "N"; aMonedaC = aMonedaA; |FINSI;
   |HAZ impre1;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 000001;
 999999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;

|PROCESO Legalia;

    cod1 = eaLegEmp;
    cod2 = eaLegPer;
    emEmp = 1;  || Sin mensaje
    |HAZ leelo;
    |SI swerror ! 0;  |ACABA;   |FINSI;

    |DBASE fich_alta; |QBF fich_alta;
    fich_alta = fich_alta + "fich/" + eaLegEmp + eaLegPer + "/";

    |PATH_DAT #1  fich_alta;
    |PATH_DAT #2  fich_alta;
    |PATH_DAT #3  fich_alta;
    |PATH_DAT #5  fich_alta;
    |PATH_DAT #6  fich_alta;
    |PATH_DAT #7  fich_alta;
    |PATH_DAT #8  fich_alta;
    |PATH_DAT #9  fich_alta;
    |PATH_DAT #11 fich_alta;
    |PATH_DAT #12 fich_alta;
    |ABRE #8;
    |DDEFECTO #8;
    #8#0 = 1;
    |LEE 000#8=;
    |CIERRA #8;

    |DDEFECTO #0;
    |SI Compara = 1;
         #0#13 = "S";
         #0#14 = eaLegEmp;
         |SI eaLegPer > 0;
             #0#15 = eaLegPer - 1;
         |SINO;
             #0#15 = 9;
         |FINSI;
         |HAZ esbuena;
         |HAZ esbuena1;
         |SI swNoExiste = 1; |ACABA; |FINSI;
     |FINSI;
     #0#0  = Per1; #0#7 = Per1;
     #0#1  = Per2; #0#8 = Per2;
     Campo = 0; |HAZ pintames;
     Campo = 1; |HAZ pintames;
     Campo = 7; |HAZ pintames;
     Campo = 8; |HAZ pintames;
     #0#4   = "S";
     |SI dig4 [ 4; |Y dig4 ! 0; #0#5   = 1; |FINSI;
     |SI dig5 [ 4; |Y dig5 ! 0; #0#5   = 2; |FINSI;
     |SI dig6 [ 4; |Y dig6 ! 0; #0#5   = 3; |FINSI;
     |SI dig7 [ 4; |Y dig7 ! 0; #0#5   = 4; |FINSI;
     |SI dig8 [ 4; |Y dig8 ! 0; #0#5   = 5; |FINSI;
     |SI dig9 [ 4; |Y dig9 ! 0; #0#5   = 6; |FINSI;

     #0#6   = 00;
     #0#17  = "N";
     #0#12  = efFecLeg;
     #0#18  = eaDatLeg;
     #0#19  = 1;
     aMonedaA = eaMoneda;
     |SI #0#13 = "N"; aMonedaC = aMonedaA; |FINSI;
     |HAZ impre;
|FPRC;

|PROGRAMA;
   |HAZ DirAplicSt;

   aparam = PARAMETRO; |QBF aparam;
   |SI aparam = ""; |O aparam = "fech"; |O aparam = "Analitica"; |O aparam = " N";

       aFichero = ("bt" + Usuario) % 108;
       |NOME_DAT #3 aFichero;
       aFichero = ("bc" + Usuario) % 108;
       |NOME_DAT #5 aFichero;


       |HAZ inicial;
       |SI swerror = 2; |ACABA; |FINSI;
       |SI swerror = 3; |PTEC 831; |ACABA; |FINSI;
       |SI aparam =  " N"; aparam = ""; |FINSI;

       |CLS;
       |CABEZA "Balance Situacion y Cta.resultados";
       |PINPA #0#0;
       |PINDA #0#0;

       |MENU menu;
       menu2 = FSalida;
       |SI 0 ] menu2; |ACABA; |FINSI;
       |SI menu2 = 1;
           #0#18 = "N"; |PINTA #0#18;
           |C_M #0#18,1,"N";
       |FINSI;

       |ENDATOS #1#0;
       |SI FSalida = 0;
           |HAZ impre;
       |FINSI;

   |SINO;

      nSwMen = 1;
      Comodin = PARAMETRO;
      Comodin = Comodin - ",C";
      |SI FSalida ! 0;
          Compara = 1;
      |SINO;
          Compara = 0;
      |FINSI;
      Comodin = Comodin - ",L";
      |SI FSalida ! 0;
          nSwLInv = 1;
      |SINO;
          nSwLInv = 0;
      |FINSI;
      Comodin = Comodin - ",";
      |SI FSalida ! 0;
         Calc = ((FSalida / 100) ! 0) + 99;
         Comodin1 = Comodin % 0101;
         Per1 = Comodin1; Comodin = Comodin - Comodin1;
         Per2 = Comodin;
      |FINSI;
      menu2 = 2;
      |HAZ Legalia;
   |FINSI;

|FPRO;


|PROCESO ElCMoneda;
     |SI aMonedaC = "P";                  || pesetas
         mulMoneda = 1 / 166.386;
         nRedon = 2;
     |FINSI;
     |SI aMonedaC = "E";                  || euros
         mulMoneda = 166.386;
         nRedon = 0;
     |FINSI;
     impMoneda = (impMoneda * mulMoneda) ! nRedon;
|FPRC;
