|FICHEROS;
   cadescua #0;
   camaasic #1;
   camaasil #2;
   cadescu1 #3;

   canempre #30;
   caselem1 #998;
|FIN;

|VARIABLES;
   &entrada = 1;
   aAlfa1 = "";
   alfa1 = "";
   alfa2 = "";
   alfa3 = "";
   alfa4 = "";
   nume1 = 0;
   nume2 = 0;
   nume3 = 0;
   nume4 = 0;
   fech1 = @;
   fech2 = @;
   fech3 = @;
   fech4 = @;
   seleccio = 0;
   seleccio2 = 0;
   {-1}menu = "";
   menu1 = "2400";
   menu2 = "1";
   menu3 = " Elija opcion: ";
   menu4 = "IP";
   menu5 = " Impresora ";
   menu6 = " Pantalla ";
   debe_p = 0;
   haber_p = 0;
   saldo_p = 0;
   saldo_a = 0;
   opcion = 0;
   &error = "";
   &numero = 0;
   mas = 0;
   posic = 0;
   linea = 0;
   prime = 0;
   &antdia = 0;
   &antfec = @;
   &antdoc = 0;
|FIN;

|INCLUYE dscont_i;

|PROCESO defectos; |TIPO 7;        || desde campo 'desde diario'
   #cadescua#2 = fec1;
   #cadescua#3 = fec2;
   |PINTA #cadescua#2;
   |PINTA #cadescua#3;
|FPRC;

|PROCESO fecha; |TIPO 0;           || desde campos 'desde/hasta fecha'
   |SI #cadescua#Campo# < fec1; |Y #cadescua#Campo# > fec2;
      |MENAV "    ATENCION!!! Fecha fuera de periodo ...";
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO correte;
   |CAMPO_POSICION #cadescu1#0,0,posic; posic=posic+mas; |CAMPO_POSICION #cadescu1#0,1,posic;
   |CAMPO_POSICION #cadescu1#1,0,posic; posic=posic+mas; |CAMPO_POSICION #cadescu1#1,1,posic;
   |CAMPO_POSICION #cadescu1#2,0,posic; posic=posic+mas; |CAMPO_POSICION #cadescu1#2,1,posic;
   |CAMPO_POSICION #cadescu1#3,0,posic; posic=posic+mas; |CAMPO_POSICION #cadescu1#3,1,posic;
|FPRC;

|PROCESO pantalla2;
   linea = linea + 1;
   seleccio = 1;
   |DDEFECTO #3;
   #cadescu1#0 = antdia;    || diario
   #cadescu1#1 = antfec;    || fecha
   #cadescu1#2 = antdoc;    || documento
   #cadescu1#3 = error;   || incidencia
   |PINDA #0#3;
   |SI linea < 12;
       mas = 100;
   |SINO;
       mas = -1100;
       linea = 0;
       |PAUSA;
       |HAZ laPantalla;
   |FINSI;
   |HAZ correte;
|FPRC;

|PROCESO impresora2;
   seleccio = 1;
   |PRINT;
|FPRC;

|PROCESO haycambio;
   #camaasic#0 = antdia;
   #camaasic#1 = antfec;
   #camaasic#2 = antdoc;
   |LEE 001#1=;
   saldo_a = #camaasic#4 - #camaasic#5;
   saldo_p = debe_p - haber_p;

   nume1 = #camaasic#4 - debe_p;
   aAlfa1 = nume1; |QBF aAlfa1;
   |SI aAlfa1 = "0"; |O aAlfa1 = "-0";
       nume1 = 0;
   |FINSI;
   nume2 = #camaasic#5 - haber_p;
   aAlfa1 =nume2; |QBF aAlfa1;
   |SI aAlfa1 = "0"; |O aAlfa1 = "-0";
       nume2 = 0;
   |FINSI;

   error = "";
   |SI nume1 ! 0; |O nume2 ! 0;
         error = "Saldos Asiento y Apuntes distintos. ";
   |FINSI;

   aAlfa1 = saldo_a; |QBF aAlfa1;
   |SI aAlfa1 = "0"; |O aAlfa1 = "-0";
       saldo_a = 0;
   |FINSI;
   aAlfa1 = saldo_p; |QBF aAlfa1;
   |SI aAlfa1 = "0"; |O aAlfa1 = "-0";
       saldo_p = 0;
   |FINSI;

   |SI saldo_a ! 0;|O saldo_p ! 0;
      |SI saldo_a ! saldo_p;
         error = "Saldos Asiento y Apuntes distintos. Descuadre.";
      |SINO;
         error = "Descuadre.";
      |FINSI;
   |FINSI;

   |SI error ! "";
      numero = numero + 1;
      |SI opcion = 1;  |HAZ impresora2;  |FINSI;
      |SI opcion = 2;  |HAZ pantalla2;   |FINSI;
   |FINSI;
   antdia = #camaasil#0;
   antfec = #camaasil#1;
   antdoc = #camaasil#2;
   debe_p = 0;
   haber_p = 0;
|FPRC;

|PROCESO informa;
   seleccio2 = 1;
   |SI prime = 0;
      prime = 1;
      antdia = #camaasil#0;
      antfec = #camaasil#1;
      antdoc = #camaasil#2;
      debe_p = 0;
      haber_p = 0;
   |FINSI;
   |SI #camaasil#0 ! antdia;  |O #camaasil#1 ! antfec;  |O #camaasil#2 ! antdoc;
      |HAZ haycambio;
   |FINSI;
   |SI #camaasil#8 = "D";
      debe_p = debe_p + #camaasil#9;
   |SINO;
      haber_p = haber_p + #camaasil#9;
   |FINSI;
|FPRC;

|DEFBUCLE lee_asientos;
   #2#1;
   ;
   #0#0, #0#2, #0#4, 1;
   #0#1, #0#3, #0#5, 9999;
   e;
   NULL, informa;
|FIN;

|PROCESO laPantalla;
    |PINPA #0#3;
    |ATRI I;
    alfa1 = " "; alfa1 = alfa1 * 80;
    |PINTA 0401, alfa1;
    |ATRI S;
    |PINTA 0401, #canempre#2;
    |PINTA 0407, #canempre#1;
    |PINTA 0472, "A¤o: ";
    |PINTA 0477, #canempre#26;
    |ATRI 0;

|SI prime = 0;
    posic=0903; |CAMPO_POSICION #cadescu1#0,1,posic;
    posic=0906; |CAMPO_POSICION #cadescu1#1,1,posic;
    posic=0917; |CAMPO_POSICION #cadescu1#2,1,posic;
    posic=0924; |CAMPO_POSICION #cadescu1#3,1,posic;
|FINSI;
|FPRC;

|PROCESO pantalla;
   |HAZ laPantalla;

   |ABRE #1;
   |BUCLE lee_asientos;
   |SI seleccio2 ! 0;
       |HAZ haycambio;
   |FINSI;
   |CIERRA #1;

   |SI seleccio2 ! 0;
      |SI seleccio ! 0;
         |PAUSA;
      |SINO;
         |MENAV "    No hay descuadres ...";
      |FINSI;
   |SINO;
      |MENAV "    No hay apuntes ...";
   |FINSI;
|FPRC;

|PROCESO impresora;
   |ABRE #1;
   |BUCLE lee_asientos;
   |SI seleccio2 ! 0;
       |HAZ haycambio;
   |FINSI;
   |CIERRA #1;

   |SI seleccio2 ! 0; |Y seleccio ! 0;
       |PIEINF;
   |FINSI;
|FPRC;

|PROCESO descuadra; |TIPO 3;       || desde campo 'diario'
   |MENU menu;
   opcion = FSalida;
   |SI opcion [ 0; |ACABA; |FINSI;

   |SI opcion = 1;
       |IMPRE 0;
       |SI FSalida ! 0;  |ACABA;  |FINSI;
       |INFOR "cadescua";
       |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;
   |FINSI;

   |SI #48#27 = "S";
       |HAZ MultiEmpresa;
   |SINO;
       |DFICE dirfich0; |QBF dirfich0;
       |HAZ HazTodo;
   |FINSI;
   |SI opcion = 1;
       |FININF;
       |FINIMP;
   |FINSI;
|FPRC;

|PROCESO HazTodo;
   linea = 0;
   prime = 0;
   seleccio2 = 0;
   seleccio  = 0;
  |SI opcion = 1;  |HAZ impresora;  |FINSI;
  |SI opcion = 2;  |HAZ pantalla;   |FINSI;
|FPRC;

|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;

   |PATH_DAT #1 dirfich0;
   |PATH_DAT #2 dirfich0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
   #caselem1#1;
   ;
   00001;
   99999;
   ;
   NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;
