|FICHEROS;
   otp00005 #0;
   otp00006 #1;
   otp00007 #2,MANTE;
   otp00008 #3,MANTE;
   otp00009 #4,MANTE;
   otp00010 #5,MANTE;
   otp00011 #6,MANTE;
   otp00012 #6;

   canempre #30;

   canempr@ #100;
   ctrlspa@ #101;
|FIN;

|VARIABLES;
 &enPor = 0;
 &enSPA = 0;
 nTecla = 0;
 nModo = 0;
 nModoEntrada = 0;
 nInkey = 0;

 &nMaxDes  = 0;
 &nMaxOri  = 0;
 {-1}punt1 = 0;
 ddig3 = 0;
 ddig4 = 0;
 ddig5 = 0;
 ddig6 = 0;
 ddig7 = 0;
 ddig8 = 0;
 ddig9 = 0;

 &eaDirDestino = "";
 &eaDirOrigen  = "";
 aAlfa  = "";
 aAlfa1 = "";
 aAlfa2 = "";
 aBaseAse = "";
 aNumAse  = "";
 nNumPer  = 0;
 nNumSPA  = 0;

 nCalc    = 0;
 nPrimero = 0;
 nNivelOr1  = 0;
 nNivelOr2  = 0;
 nNivelOr3  = 0;
 nNivelOr4  = 0;
 nNivelOr5  = 0;
 nNivelOr6  = 0;

 nNivelDt1  = 0;
 nNivelDt2  = 0;
 nNivelDt3  = 0;
 nNivelDt4  = 0;
 nNivelDt5  = 0;
 nNivelDt6  = 0;
|FIN;


|INCLUYE dscont_i;

|| **********************************************************
|CALCULO LeeEmp; |TIPO 0;
  nModoEntrada = (FEntrada / 100) ! 0;

  |DBASS aAlfa; |QBF aAlfa;
  aAlfa2 = ""; aAlfa1 = ""; aAlfa = aAlfa % -299;
  |PARA; |SI aAlfa1 ! "/"; |Y aAlfa1 ! "\"; |HACIENDO;
     aAlfa1 = aAlfa % -101; aAlfa = aAlfa % -299;
     aAlfa2 = aAlfa1 + aAlfa2;
  |SIGUE;
  aBaseAse = aAlfa + "/";

  |SI #otp00005#11 = #otp00007#20; |Y #otp00005#0 = #otp00007#1;
     |MENAV "    ERROR !! Introducida empresa destino";
     |ERROR; |ACABA;
  |FINSI;

  aAlfa = aBaseAse + "ase" + (("0000" + #otp00007#20) % -104) + "/contagen/fich/";
  |PATH_DAT #30aAlfa;
  |ABRE #30;
  |SELECT #4#30;
  #30#2  = #2#1;
  #30#26 = #0#10;
  |LEE 010#30=;
  |SI FSalida ! 0;
      |MENAV "    No existe la empresa ";
      |CIERRA #30;
      |SI nModoEntrada = 1;
          |ERROR;
          |ACABA;
      |FINSI;
  |FINSI;
  |CIERRA #30;
  |SELECT #1#30;

  ddig3 = #30#13;
  ddig4 = #30#14;       || 1 nivel
  ddig5 = #30#15;       || 2 nivel
  ddig6 = #30#16;       || 3 nivel
  ddig7 = #30#17;       || 4 nivel
  ddig8 = #30#18;       || 5 nivel
  ddig9 = #30#19;       || 6 nivel

  || Recoger informacion sobre los niveles ...
  Ipunt1 = ddig3<-;
  Ipunt1 = Ipunt1 + ddig3;
  nMaxOri = punt1;

  |SI MaximoDigitos ! nMaxOri;
      |MENAV "    No corresponde Nivel Cuentas ";
      |ERROR;
      |ACABA;
  |FINSI;

  #2#0 = #0#0;
  #2#2 = #30#1; |PINTA #2#2;

  aAlfa1 = aBaseAse + "ase" + (("0000" + #otp00007#20) % -104) + "/contagen/fich/";
  aAlfa2 = #30#0; |QBF aAlfa2;
  aAlfa2 = ("000000" + #30#0) % -106;
  eaDirOrigen = aAlfa1 + aAlfa2 + "/";
|FCAL;

|CALCULO AntesDepar; |TIPO 7;
  |SI #30#24 = "N";
      |C_M #2#12, 1, "N"; #2#12 = "00";
  |SINO;
      |C_M #2#12, 1, "S";
  |FINSI;
|FCAL;

|CALCULO AntesDig; |TIPO 7;
  |SI #0#6 = 0; |Y #0#7 = 0;
      |C_M #2#3, 1, "N"; #2#3 = "000";
      |C_M #2#22, 1, "N";
      |C_M #2#24, 1, "N";
  |SINO;
      |C_M #2#3, 1, "S";
      |C_M #2#22, 1, "S";
      |C_M #2#24, 1, "S";
  |FINSI;
|FCAL;

|CALCULO AbansDig; |TIPO 7;
  |SI #2#22 = 1;
      aAlfa1 = "    Introduzca el Digito diferenciador 0-9";
  |SINO;
      |SI #2#22 = 2;
          aAlfa1 = "    Introduzca el Digito diferenciador 00-99";
      |SINO;
          aAlfa1 = "    Introduzca el Digito diferenciador 000-999";
      |FINSI;
  |FINSI;
  |MENSA aAlfa1;
|FCAL;

|CALCULO DespuDig; |TIPO 0;

  |BLIN 4;
  |SI FSalida = 2; |ACABA; |FINSI;

  |C_M #2#3, 0, aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

  |SI #2#22 = 1; |Y #2#3 > 9;
      |ERROR;
  |SINO;
      |SI #2#22 = 2; |Y #2#3 > 99;
          |ERROR;
      |FINSI;
  |FINSI;
|FCAL;

|CALCULO ConcepH; |TIPO 0;
  |SI #2#14 < #2#13;
      |ERROR;
  |FINSI;
|FCAL;

|CALCULO AntesDiario; |TIPO 7;
  |SI #0#5 ! 3;
      |C_M #2#4, 1, "S";
      |C_M #2#7, 1, "S";
  |SINO;
      |C_M #2#4, 1, "N"; #2#4 = "S"; |PINTA #2#4;
      |C_M #2#5, 1, "N"; #2#5 = 00; |PINTA #2#5;
      |C_M #2#6, 1, "N"; #2#6 = 99; |PINTA #2#6;
      |C_M #2#7, 1, "N"; #2#7 = 0;  |PINTA #2#7;
  |FINSI;
|FCAL;

|CALCULO losDiarios; |TIPO 0;
  |C_M #2#4, 0, aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

  |SI #2#4 = "S";
      |C_M #2#5, 1, "S";
      |C_M #2#6, 1, "S";
  |SINO;
      |C_M #2#5, 1, "N"; #2#5 = 00; |PINTA #2#5;
      |C_M #2#6, 1, "N"; #2#6 = 99; |PINTA #2#6;
  |FINSI;
|FCAL;

|CALCULO AntesLibro; |TIPO 7;
  |SI enPor ! 0; |ACABA; |FINSI;
  |SI #0#5 ] 3;
      |C_M #2#8,  1, "S";
      |C_M #2#11, 1, "S";
      |C_M #2#23, 1, "S";
  |SINO;
      |C_M #2#8,  1, "N"; #2#8  = "S"; |PINTA #2#8;
      |C_M #2#9,  1, "N"; #2#9  = 01;  |PINTA #2#9;
      |C_M #2#10, 1, "N"; #2#10 = 99;  |PINTA #2#10;
      |C_M #2#11, 1, "N"; #2#11 = 0;   |PINTA #2#11;
      |C_M #2#23, 1, "N"; #2#23 = "N"; |PINTA #2#23;
  |FINSI;
|FCAL;

|CALCULO losLibros; |TIPO 0;
  |SI enPor ! 0; |ACABA; |FINSI;

  |C_M #2#8, 0, aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

  |SI #2#8 = "S";
      |C_M #2#9,  1, "S";
      |C_M #2#10, 1, "S";
  |SINO;
      |C_M #2#9,  1, "N"; #2#9  = 01; |PINTA #2#9;
      |C_M #2#10, 1, "N"; #2#10 = 99; |PINTA #2#10;
  |FINSI;
|FCAL;


|| ------------------------------------
|PROCESO BorraLin30;
 |BORRA 020#3a;
 |LIBERA #3;
|FPRC;

|DEFBUCLE borrarLin3;
 #3#1;
 ;
 #2#19, #2#0, #2#20, #2#1, 00;
 #2#19, #2#0, #2#20, #2#1, 99;
 be;
 NULL, BorraLin30;
|FIN;

|PROCESO BorraLin40;
 |BORRA 020#4a;
 |LIBERA #4;
|FPRC;

|DEFBUCLE borrarLin4;
 #4#1;
 ;
 #2#19, #2#0, #2#20, #2#1, 00;
 #2#19, #2#0, #2#20, #2#1, 99;
 be;
 NULL, BorraLin40;
|FIN;

|PROCESO BorraLin50;
 |BORRA 020#5a;
 |LIBERA #5;
|FPRC;

|DEFBUCLE borrarLin5;
 #5#1;
 ;
 #2#19, #2#0, #2#20, #2#1, 000;
 #2#19, #2#0, #2#20, #2#1, 999;
 be;
 NULL, BorraLin50;
|FIN;

|CALCULO Tipo1; |TIPO 1;
 nModoEntrada = (FEntrada / 100) ! 0;

 |SI nModoEntrada = 3;
     |BUCLE borrarLin3;
     |BUCLE borrarLin4;
     |BUCLE borrarLin5;
 |FINSI;

|FCAL;

|| **********************************************************************
|RUTINA ClaveBaja3;
     #3#0 = #2#0;
     #3#1 = #2#1;
     #3#2 = 01;
     #3#8 = #2#19;
     #3#9 = #2#20;
|FRUT;

|RUTINA ClaveAlta3;
     #3#0 = #2#0;
     #3#1 = #2#1;
     #3#2 = 99;
     #3#8 = #2#19;
     #3#9 = #2#20;
|FRUT;

|RUTINA ClaveBaja4;
     #4#0  = #2#0;
     #4#1  = #2#1;
     #4#2  = 01;
     #4#9  = #2#19;
     #4#10 = #2#20;
|FRUT;

|RUTINA ClaveAlta4;
     #4#0 = #2#0;
     #4#1 = #2#1;
     #4#2 = 99;
     #4#9  = #2#19;
     #4#10 = #2#20;
|FRUT;

|RUTINA ClaveBaja5;
     #5#5 = #2#19;
     #5#0 = #2#0;
     #5#6 = #2#20;
     #5#1 = #2#1;
     #5#2 = 001;
|FRUT;

|RUTINA ClaveAlta5;
     #5#5 = #2#19;
     #5#0 = #2#0;
     #5#6 = #2#20;
     #5#1 = #2#1;
     #5#2 = 999;
|FRUT;


|PROCESO Tipo13;  |TIPO 13;
     nModo = 7;
     |ENTLINEAL #1#3, -5, 7, 22, 0, ClaveBaja3, ClaveAlta3;
     |SI enPor = 0;
         |ENTLINEAL #1#4, -5, 7, 22, 0, ClaveBaja4, ClaveAlta4;
     |FINSI;
|FPRC;

|PROCESO Tipo11;  |TIPO 11;
  nInkey = FSalida;
  |SI nInkey = 10; |O nInkey = 11;
      |HAZ ConsultaCli;
      |ERROR;
      |PTEC 802;
  |FINSI;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
 |SI #2#4 = "N";
     |ENTLINEAL #1#3, -5, 2, 22, 0, ClaveBaja3, ClaveAlta3;
 |SINO;
     |BUCLE borrarLin3;
     |ENTLINEAL #1#3, -5, 7, 22, 0, ClaveBaja3, ClaveAlta3;
 |FINSI;
 |SI enPor = 0;
     |SI #2#8 = "N";
         |ENTLINEAL #1#4, -5, 2, 22, 0, ClaveBaja4, ClaveAlta4;
     |SINO;
         |BUCLE borrarLin4;
         |ENTLINEAL #1#4, -5, 7, 22, 0, ClaveBaja4, ClaveAlta4;
     |FINSI;
 |FINSI;
 |SI #2#15 = "N";
     |BUCLE borrarLin5;
 |FINSI;
|FPRC;

|CALCULO Excluir; |TIPO 0;
 |PUSHV 1301 2380;
 |SI #2#15 = "S";
      |ENTLINEAL #1#5, -5, 2, 22, 1, ClaveBaja5, ClaveAlta5;
 |SINO;
     |BUCLE borrarLin5;
     |ENTLINEAL #1#5, -5, 7, 22, 1, ClaveBaja5, ClaveAlta5;
 |FINSI;
 |POPV;
|FCAL;

|CALCULO Antes17; |TIPO 7;
 aAlfa1 = #2#15;
 |C_M #2#17,1,aAlfa1;
 |SI #0#5 ! 1;
     #2#17 = "S";
     |PINTA #2#17;
     |C_M #2#17,1,"N";
 |FINSI;
|FCAL;

|RUTINA inf1;
   #otp00011#0 = 1;
|FRUT;

|RUTINA sup1;
   #otp00011#0 = 99999;
|FRUT;

|RUTINA inf2;
   #otp00011#2 = " " * 40;
|FRUT;

|RUTINA sup2;
   #otp00011#2 = "z" * 40;
|FRUT;

|PROCESO RecogeDatos;
   nCalc = #canempr@#26;
   |SI nCalc ] 80; nCalc = nCalc + 1900; |SINO; nCalc = nCalc + 2000; |FINSI;
   |SI nCalc ! #otp00005#3; |ACABA; |FINSI;

   |DDEFECTO #otp00011;
   #otp00011#0 = #canempr@#2;
   #otp00011#1 = #canempr@#8;
   #otp00011#2 = #canempr@#4;
   |GRABA 020#otp00011.n;
|FPRC;

|DEFBUCLE RecogeDatos;
   #canempr@#1002;
   ;
   00000, 0;
   99999, 9;
   ;
   NULL, RecogeDatos;
|FIN;

|PROCESO ConsultaCli;
   nTecla = FSalida;

   |DBASS aAlfa; |QBF aAlfa;
   aAlfa2 = ""; aAlfa1 = ""; aAlfa = aAlfa % -299;
   |PARA; |SI aAlfa1 ! "/"; |Y aAlfa1 ! "\"; |HACIENDO;
      aAlfa1 = aAlfa % -101; aAlfa = aAlfa % -299;
      aAlfa2 = aAlfa1 + aAlfa2;
   |SIGUE;
   aBaseAse = aAlfa + "/";

   |SI nTecla = 10; |O nTecla = 11;
      |PUSHV 0501 2380;
      aAlfa = "tm" + Usuario; |NOME_DAT #otp00011#aAlfa#;
      |DELINDEX #otp00011; |ABRE #otp00011;

      |DDEF aAlfa; |QBF aAlfa; aAlfa = aAlfa + "canempre.mas";
      |CARGA_DEF aAlfa, canempr@;
      |SI FSalida = 0;
         aAlfa = aBaseAse + "ase" + (("0000" + #otp00007#20) % -104) + "/contagen/fich/";
         |PATH_DAT #canempr@#aAlfa#;
         |ABRE #canempr@; |BUCLE RecogeDatos; |CIERRA #canempr@;

         nPrimero = 1;
         |SI nTecla = 10; |ENTLINEAL #1#otp00011, -1, 4, 21, 1, inf1, sup1; |FINSI;
         |SI nTecla = 11; |ENTLINEAL #2#otp00011, -1, 4, 21, 1, inf2, sup2; |FINSI;
         |CIERRA #otp00011;   |DELINDEX #otp00011;
         |CIERRA #canempr@; |DESCARGA_DEF #canempr@;
         |POPV;
         |PINTA #otp00007#1; |PINTA #otp00007#2;
      |SINO;
         |MENAV "    Problemas para cargar el def de empresas contables";
         |POPV;
         |ERROR; |ACABA;
      |FINSI;
   |FINSI;

   |SI #otp00005#11 = #otp00007#20; |Y #otp00005#0 = #otp00007#1;
      |MENAV "    ERROR !! Introducida empresa destino";
      |ERROR; |ACABA;
   |FINSI;

   |DDEF aAlfa; |QBF aAlfa; aAlfa = aAlfa + "canempre.mas";
   |CARGA_DEF aAlfa, canempr@;
   |SI FSalida = 0;
      aAlfa = aBaseAse + "ase" + (("0000" + #otp00005#11) % -104) + "/contagen/fich/";
      |PATH_DAT #canempr@#aAlfa#;
      |ABRE #canempr@;
      #canempr@#2 = #otp00005#0;
      #canempr@#3 = #otp00005#1;
      |LEE 000#canempr@.=;
      |SI FSalida = 0;
         nNivelDt1 = #canempr@#14; nNivelDt2 = #canempr@#15; nNivelDt3 = #canempr@#16;
         nNivelDt4 = #canempr@#17; nNivelDt5 = #canempr@#18; nNivelDt6 = #canempr@#19;
      |SINO;
         nNivelDt1 = 0; nNivelDt2 = 0; nNivelDt3 = 0;
         nNivelDt4 = 0; nNivelDt5 = 0; nNivelDt6 = 0;
      |FINSI;
      |CIERRA #canempr@;

      aAlfa = aBaseAse + "ase" + (("0000" + #otp00007#20) % -104) + "/contagen/fich/";
      |PATH_DAT #canempr@#aAlfa#;
      |ABRE #canempr@; nNumPer = -1;
      #canempr@#2 = #otp00007#1;
      #canempr@#3 = 0;
      |LEE 000#canempr@.];
      |PARA; |SI FSalida = 0; |Y #canempr@#2 = #otp00007#1; |HACIENDO;
         nCalc = #canempr@#26;
         |SI nCalc ] 80; nCalc = nCalc + 1900; |SINO; nCalc = nCalc + 2000; |FINSI;
         |SI nCalc = #otp00005#3;
            nNumPer = #canempr@#3;
            nNivelOr1 = #canempr@#14; nNivelOr2 = #canempr@#15;
            nNivelOr3 = #canempr@#16; nNivelOr4 = #canempr@#17;
            nNivelOr5 = #canempr@#18; nNivelOr6 = #canempr@#19;

            |SI nNivelOr1 = nNivelDt1; |Y nNivelOr2 = nNivelDt2;
             |Y nNivelOr3 = nNivelDt3; |Y nNivelOr4 = nNivelDt4;
             |Y nNivelOr5 = nNivelDt5; |Y nNivelOr6 = nNivelDt6;
               |SAL;
            |FINSI;
         |FINSI;
         |LEE 000#canempr@.s;
      |SIGUE;
      |CIERRA #canempr@; |DESCARGA_DEF #canempr@;

      |SI nNumPer = -1;
         |MENAV "    No existe la empresa";
         |ERROR; |ACABA;
      |SINO;
         |SI nNivelOr1 ! nNivelDt1; |O nNivelOr2 ! nNivelDt2;
          |O nNivelOr3 ! nNivelDt3;  |O nNivelOr4 ! nNivelDt4;
          |O nNivelOr5 ! nNivelDt5;  |O nNivelOr6 ! nNivelDt6;
            |MENAV "    No corresponde nivel cuentas";
            |ERROR; |ACABA;
         |FINSI;
      |FINSI;
   |SINO;
      |MENAV "    Problemas para cargar el def de empresas contables";
      |ERROR; |ACABA;
   |FINSI;

|FPRC;

|PROCESO ConsultaSPAS;
   |DBASS aAlfa; |QBF aAlfa;
   aAlfa2 = ""; aAlfa1 = ""; aAlfa = aAlfa % -299;
   |PARA; |SI aAlfa1 ! "/"; |Y aAlfa1 ! "\"; |HACIENDO;
      aAlfa1 = aAlfa % -101; aAlfa = aAlfa % -299;
      aAlfa2 = aAlfa1 + aAlfa2;
   |SIGUE;
   aBaseAse = aAlfa + "/asespas/";

   |SI nTecla = 10;
      |PUSHV 0501 2380;
      aAlfa = "otp00012.tab;" + aBaseAse; |QBF aAlfa;
      |SUB_EJECUTA aAlfa;
      |POPV;
      #otp00007#20 = enSPA; |PINTA #otp00007#20;
   |FINSI;

   |PATH_DAT #otp00012#aBaseAse#;
   aAlfa = "ctrlspas"; |NOME_DAT #otp00012#aAlfa#;
   |ABRE #otp00012;
   #otp00012#0 = #otp00007#20;
   |LEE 000#otp00012.=;
   |SI FSalida ! 0;
      |MENAV "    ERROR !! No existe el SPA indicado";
      |ERROR;
   |SINO;
      #otp00007#21 = #otp00012#1; |PINTA #otp00007#21;
   |FINSI;
   |CIERRA #otp00012;
|FPRC;

|PROCESO MensaSPAS; |TIPO 7;
   |BLIN 4;
   |MENSA "    <F2> Consulta SPAS";
|FPRC;

|PROCESO MensaClientes; |TIPO 7;
   |SI FSalida ! 0; nTecla = FSalida; |HAZ ConsultaSPAS; |FINSI;

   |BLIN 4;
   |MENSA "    <F2> Consulta clientes por codigo, <F3> Consulta clientes por nombre";
|FPRC;

|PROCESO Tipo13C0011; |TIPO 13;
   |SI FSalida = 0;
      |SI nPrimero = 1;
         nPrimero = 0;
      |SINO;
         |LEE 000#otp00011.a;
         #otp00007#1 = #otp00011#0; |PINTA #otp00007#1;
         #otp00007#2 = #otp00011#2; |PINTA #otp00007#2;
         |PTEC 807;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO Tipo13C0311; |TIPO 13;
   |SI FSalida = 0;
      |SI nPrimero = 1;
         nPrimero = 0;
      |SINO;
         |LEE 000#otp00011.a;
         #otp00007#1 = #otp00011#0; |PINTA #otp00007#1;
         #otp00007#2 = #otp00011#2; |PINTA #otp00007#2;
         |PTEC 807;
      |FINSI;
   |FINSI;
|FPRC;
