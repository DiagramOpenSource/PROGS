|FICHEROS;
   ca8eqmay #0,MANTE;    || cuentas

   ca8comay #1;
   ca8cumay #2;
   cacuenta #3;
   ca8z0002 #10;
|FIN;

|VARIABLES;
   &entrada = 1;
   nModo    = 0;
   cinco    = "     ";
   cta      = "     ";
   ctanum   = 0;
   ctaalf   = "";
   longalf  = "";
   longnum  = "";
   uno      = "";
   dos      = "";
   nada     = "";
   mascara  = "";
   aAlfa1   = "";
   nNume1   = 0;
   &aPathPlan = "";
   nInkey   = 0;
|FIN;

|INCLUYE dscont_i;

||  ***************************************

|PROCESO inf;
 #3#0 = "    ";
 #3#1 = 1;
|FPRC;

|PROCESO sup;
 #3#0 = "zzzzz";
 #3#1 = 6;
|FPRC;

|PROCESO inf0;
 #2#12 = 2008;
 #2#10 = #0#0;
 #2#0  = "    ";
|FPRC;

|PROCESO sup0;
 #2#12 = 2008;
 #2#10 = #0#0;
 #2#0 = "zzzzz";
|FPRC;


|PROCESO nivel_11; |TIPO 11;

 nInkey = FSalida;
 |SI nInkey = 10;
     |ABRE #3;
     #3#0 = #0#1;
     |CONSULTA_F_CLAVE #5#3, inf, sup;
     #0#1 = #3#0; |PINTA #0#1;
     |CIERRA #3;
 |FINSI;
|FPRC;

|PROCESO nivel_a; |TIPO 7;
  aAlfa1 = #0#3; |QBF aAlfa1;
  |SI aAlfa1 = "";
      #0#3 = #0#1; |PINTA #0#3;
  |FINSI;
|FPRC;


|PROCESO nivel_m; |TIPO 0;

   nInkey = FSalida;
   |SI nInkey = 10; |Y Campo = 3;
       |ABRE #2;
       #2#12 = 2008;
       #2#10 = #0#0;
       #2#0  = #0#3;
       |CONSULTA_F_CLAVE #1#2, inf0, sup0;
       #0#3 = #2#0; |PINTA #0#3;
       |CIERRA #2;
   |FINSI;

   |SI nInkey = 11; |Y Campo = 3;
       #0#3 = #0#1; |PINTA #0#3;
   |FINSI;
   |SI nInkey = 12; |Y Campo = 3;
       #0#3 = "PDTE "; |PINTA #0#3;
   |FINSI;

   nModo = (FEntrada / 100) ! 0;
   |SI #0Campo = cinco;
       |SI nModo = 4; |ACABA; |FINSI;
   |FINSI;

   |SI Campo = 3;
       #0#3 = #0#3 > "";
       |SI #0#3 = "PDTE ";
           #0#4 = "DESAPARECE, PENDIENTE DE AJUSTE A...";
           |PINTA #0#3;
           |PINTA #0#4;
           |ACABA;
       |FINSI;
   |FINSI;

   cta = #0Campo; |QBF cta;
   nada = cta % A101;
   |SI nada ! "0";
       ctanum = cta;
       ctaalf = ctanum;
       longnum = ctaalf % A0;
   |SINO;
       ctanum = 1;
       ctaalf = cta;
       longnum = ctaalf % A0;
   |FINSI;
   longalf = cta    % A0;
   |SI longnum ! longalf;|O ctanum = 0;
       |MENAV "    CUENTA ERRONEA!!!";
       |PINTA #0Campo;
       |ERROR;
       |ACABA;
   |FINSI;

   |SI Campo = 1;
       |ABRE #3;
       #3#0 = #0#1;
       |LEE 001#3=;
       |SI FSalida ! 0;
           |MENAV "    No existe Cuenta en el Plan Contable de 1990";
           |ERROR;
       |SINO;
           #0#2 = #3#2; |PINTA #0#2;
       |FINSI;
       |CIERRA #3;
   |FINSI;

   |SI Campo = 3;
       |ABRE #2;
       #2#12 = 2008;
       #2#10 = #0#0;
       #2#0  = #0#3;
       |LEE 001#2=;
       |SI FSalida ! 0;
           |MENAV "    No existe Cuenta en el Plan Contable de 2008";
           |ERROR;
       |SINO;
           #0#4 = #2#1; |PINTA #0#4;
       |FINSI;
       |CIERRA #2;
   |FINSI;
|FPRC;

|PROCESO ElTipo5;  |TIPO 5;
      |LEE 040#0p;
      #0#3 = "PDTE ";
      |LINEAS_BI_ATRI #3#0, D, R, -1;

      |PATH_DAT #3 aPathPlan;
|FPRC;
