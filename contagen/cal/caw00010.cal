|FICHEROS;
     cacuiv2c #0;
     cacuiv2l #1;
     caw00010 #10;
|FIN;

|VARIABLES;
    &nEsteAny = 0;
    aT        = "";
    nNum      = 0;
    aParam    = "";
    fFecha    = @;
    fFecha1   = @;
    &nCaw010  = 0;
|FIN;

|INCLUYE dscont_i;

|| ******************************************** Calculos Pantalla
|CALCULO wTipo12; |TIPO 12;
|FCAL;

|CALCULO descri;  |TIPO 0;
  |SI #10#0 = "S"; #10#1 = "Soportado";   |FINSI;
  |SI #10#0 = "R"; #10#1 = "Repercutido"; |FINSI;
  |SI #10#0 = "N";
      #10#1 = "Reg.sin IVA";
  |FINSI;
  |PINTA #10#1;
  aTipoIVA = #10#0;
|FCAL;

|| /////////////////////// PROCESOS DE TRABAJO \\\\\\\\\\\\\\\\\\\\
|RUTINA min;
 |SI FSalida = "POSICION";
     #0#20 = enEjercicio;
     #0#21 = "01.01.2000";
     #0#0  = #10#0;
 |SINO;
     #0#20 = 1990;
     #0#21 = "01.01.2000";
     #0#0  = #10#0;
 |FINSI;
|FRUT;

|RUTINA max;
  #0#0  = #10#0;
  #0#20 = 2999;
  #0#21 = "31.12.2999";
|FRUT;

|PROCESO Tipo3;  |TIPO 3;
  |ENTLINEAL #1#0, -2, 2, 22, 0, min, max;
  #0#1 = #10#1;
|FPRC;

|PROGRAMA;
   nCaw010 = 1;
   aParam = PARAMETRO; |QBF aParam;
   |SI aParam = "";
       |CLS;
       |HAZ inicio;
       nEsteAny = enEjercicio;
       |CABEZA "Control Tipos IVA e IRPF";
       |DDEFECTO #10;
       |PINPA #0#10;
       |PINDA #0#10;
       |ENDATOS #1#10;
   |SINO;
        aT = "R"; |HAZ AltaIVA;
        aT = "S"; |HAZ AltaIVA;
   |FINSI;
   nCaw010 = 0;
|FPRO;

|PROCESO Alta;
   #cacuiv2l#12 = enEjercicio;
   #cacuiv2l#13 = fFecha;
   |GRABA 000#cacuiv2l.n;
|FPRC;

|DEFBUCLE Alta;
   #cacuiv2l#1;
   ;
   aT, nNum, fFecha1, 01;
   aT, nNum, fFecha1, 99;
   ;
   NULL, Alta;
|FIN;

|PROCESO BuscaFecha;
  fFecha1 = #cacuiv2c#21;
|FPRC;

|DEFBUCLE BuscaFecha;
   #cacuiv2c#1;
   ;
   aT, nNum, "01.01.2000";
   aT, nNum, "31.12.2999";
   ;
   NULL, BuscaFecha;
|FIN;

|PROCESO AltaIVA;
   fFecha  = "01.01.2000";
   fFecha1 = "01.01.2000";

   |ABRE #cacuiv2c;

   #cacuiv2c#0  = aT;
   #cacuiv2c#20 = enEjercicio;
   #cacuiv2c#21 = fFecha;
   |LEE 001#cacuiv2c.];
   |SI FSalida ! 0; |O aT ! #cacuiv2c#0; |O enEjercicio ! #cacuiv2c#20;

       nNum  = enEjercicio - 1;
       |BUCLE BuscaFecha;

       #cacuiv2c#0  = aT;
       #cacuiv2c#20 = nNum;
       #cacuiv2c#21 = fFecha1;
       |LEE 001#cacuiv2c.=;
       |SI FSalida = 0;
           #cacuiv2c#0  = aT;
           #cacuiv2c#20 = enEjercicio;
           #cacuiv2c#21 = fFecha;
           |GRABA 000#cacuiv2c.n;
           |BUCLE Alta;
       |FINSI;
       |CIERRA #cacuiv2c;
   |SINO;
        |CIERRA #cacuiv2c;
    |FINSI;
|FPRC;
