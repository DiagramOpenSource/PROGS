|FICHEROS;
  anz00004 #0;

  camaasil #1;
  caivalin #2;
  camaniva #3;

  anm00001 #6;
  anw00002 #7,MANTE;

  anm00002 #102;
  anm00003 #103;

  anm00012 #112;
  anm00013 #113;

  nm00003@ #203;
  nm00013@ #213;
|FIN;

|VARIABLES;
  &entrada = 1;

  aFichero = "";
  alfa1 = "";
  alfa2 = "";
  alfa3 = "";
  alfa4 = "";
  x     = 0;
  nume1 = 0;
  nume2 = 0;
  nume3 = 0;
  para1 = 0;

  ctaana    = "";
  ctaana1   = "";
  n_alfa1   = "";
  n_alfa2   = "";
  n_alfa3   = "";
  masca     = "";
  guia      = 0;

  swSiCambio = 0;
  nSwQueHago = 0;
  aAlfa1   = "";
  aAlfa2   = "";
  aAlfa3   = "";
  aAlfa4   = "";

  nSwGApte = 0;
  nImporte = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoan_i;

|| ******************************************************************
|| ************ Proceso Campos en Pantalla
|CALCULO anacta; |TIPO 6;
  salidas = FSalida;
  |HAZ FPuntosAna;
|FCAL;

|CALCULO anactah; |TIPO 0;
  |SI #0#2 > #0#3;
      |MENAV "   ERROR EN CUENTA ANALITICA ";
      |ERROR;
  |FINSI;
|FCAL;

|CALCULO Antes21; |TIPO 7;
 |C_M #0#21,1,"N";
 aAlfa1 = #0#2; |QBF aAlfa1;
 aAlfa2 = #0#3; |QBF aAlfa2;
 |SI aAlfa1 = ""; |O aAlfa2 = "";
     |C_M #0#21,1,"S";
 |FINSI;
|FCAL;

|CALCULO cuenta; |TIPO 6;
     eaCuenta = #anz00004#Campo#;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #anz00004#Campo# = eaCuenta;
     |PINTA #anz00004#Campo#;
|FCAL;

|CALCULO fechah; |TIPO 0;
  |SI #0#0 > #0#1;
      |MENAV "    ERROR EN FECHA CONTABLE";
      |ERROR;
  |FINSI;
|FCAL;

|CALCULO mayor1; |TIPO 0;
 |SI #0Campo = "S";
     |C_M #0#9, 1,"S";
     |C_M #0#10,1,"S";
     |C_M #0#11,1,"S";
     |C_M #0#13,1,"N"; #0#13 = 0; |PINTA #0#13;
     |C_M #0#14,1,"N"; #0#14 = 0; |PINTA #0#14;
     |C_M #0#15,1,"N"; #0#15 = 0; |PINTA #0#15;
 |SINO;
     |C_M #0#9, 1,"N"; #0#9  = 0; |PINTA #0#9;
     |C_M #0#10,1,"N"; #0#10 = 0; |PINTA #0#10;
     |C_M #0#11,1,"N"; #0#11 = ""; |PINTA #0#11;
     |C_M #0#13,1,"S";
     |C_M #0#14,1,"S";
     |C_M #0#15,1,"S";
 |FINSI;
|FCAL;

|CALCULO lalongi; |TIPO 0;
 |SI FSalida = 2; |ACABA; |FINSI;
 |C_M #0Campo,0,alfa1;
 |SI alfa1 = "N"; |ACABA; |FINSI;
 x     = Campo - 1;
 nume2 = #0x + #0Campo - 1;
 |SI nume2 > 7;
     |MENAV "    Longitud Erronea, sobrepasa la Cuenta Analitica";
     |ERROR;
 |FINSI;
 |SI #0x ! 0; |Y #0x = 0; |ERROR; |FINSI;
|FCAL;

|CALCULO elvalor; |TIPO 0;
 |C_M #0Campo,0,alfa1;
 |SI alfa1 = "N"; |ACABA; |FINSI;
 nume2 = #0#9 + #0#10 - 1;
 nume2 = 100 + nume2;
 alfa1 = nume2;
 alfa2 = #0#11 % alfa1;
 alfa2 = (alfa2 + "       ") % 107;
 #0#11 = alfa2; |PINTA #0#11;
|FCAL;

|CALCULO destino; |TIPO 7;
 |C_M #0Campo,0,alfa1;
 |SI alfa1 = "N"; |ACABA; |FINSI;
 #0#15 = 7 - #0#14 + 1;
 |PINTA #0#15;
|FCAL;

|CALCULO defectos; |TIPO 7;
 #0#0 = fec1; |PINTA #0#0;
 #0#1 = fec2; |PINTA #0#1;
|FCAL;

|| ************************************************************************
|| ************************************************************************
|PROCESO canviana;
 swSiCambio = 0;
 #7#1 = ctaana;
 |LEE 001#7=;
 |SI FSalida = 0;
     swSiCambio = 1;
     ctaana = #7#3;
     ctaana1 = #7#2;
     #6#0 = ctaana;
     |LEE 001#6=;
     |SI FSalida = 0;
         ctaana1 = #6#1;
     |FINSI;
 |FINSI;
|FPRC;

|| ************************************************************************
|PROCESO ElGrupo0;
     #203#0 = #7#3;
     #203#1 = #103#1;
     #203#2 = #103#2;
     #203#3 = #103#3;
     #203#4 = #103#4;
     |GRABA 020#203n;
|FPRC;

|DEFBUCLE ElGrupo;
 #103#1;
 ;
 #102#0, 001;
 #102#0, 999;
 ;
 NULL, ElGrupo0;
|FIN;

|PROCESO CreaCuentas1;

 #6#0 = #7#3;
 |LEE 001#6=;
 |SI FSalida ! 0;
     |DDEFECTO #6;
     #6#0 = #7#3;
     #6#1 = #7#2;
     |GRABA 020#6n;
 |FINSI;
 |LIBERA #6;

 aAlfa1 = #7#1; |QBF aAlfa1;
 aAlfa1 = aAlfa1 % 103;

 |SI aAlfa1 = "GRU";
     nSwQueHago = 0;
     aAlfa2 = #7#3; |QBF aAlfa2;
     aAlfa2 = aAlfa2 % 103;
     |SI aAlfa2 = "GRU";    nSwQueHago = 1;  |FINSI;

     |SI nSwQueHago = 1;           || El nuevo tambien es un grupo.
         #102#0 = #7#3;            || pero si existe no hace nada
         |LEE 101#102=;
         |SI FSalida ! 0;
             #102#0 = #7#1;
             |LEE 101#102=;
             |SI FSalida = 0;
                 |ABRE #203;
                 |BUCLE ElGrupo;
                 |CIERRA #203;
                 #102#0 = #7#3;
                 #102#1 = #7#2;
                 |GRABA 040#102n;
             |FINSI;
             |LIBERA #102;
        |FINSI;
     |FINSI;
     |LIBERA #102;
     |CIERRA #102;
 |FINSI;

|FPRC;

|DEFBUCLE LeeTemporal;
 #7#1;
 ;
 00001;
 99999;
 ;
 NULL, CreaCuentas1;
|FIN;

|PROCESO CreaCuentas;
 |ABRE #6;
 |ABRE #102;
 |BUCLE LeeTemporal;
 |CIERRA #102;
 |CIERRA #6;
|FPRC;

|| ---------------------------- /// Operaciones
|PROCESO sustituir;
    alfa1 = ""; alfa2 = "";
    |SI n_alfa1 ! "";
        alfa1 = ctaana % n_alfa1;
    |FINSI;
    |SI n_alfa2 ! "";
        alfa2 = ctaana % n_alfa2;
    |FINSI;
    ctaana = alfa1 + masca + alfa2;
|FPRC;

|PROCESO mover;
    alfa1 = ""; alfa2 = ""; alfa3 = "";
    |SI n_alfa1 ! "";
        alfa1 = ctaana % n_alfa1;
    |FINSI;
    |SI n_alfa2 ! "";
        alfa2 = ctaana % n_alfa2;
    |FINSI;
    |SI n_alfa3 ! "";
        alfa3 = ctaana % n_alfa3;
    |FINSI;
    |SI #0#15 > #0#13;
        ctaana = alfa1 + masca + alfa3 + alfa2;
    |FINSI;
    |SI #0#15 < #0#13;
        ctaana = alfa1 + alfa3 + masca + alfa2;
    |FINSI;
|FPRC;

|| ******************************************************************
|| ................................... IVA/IRPF
|PROCESO sus_ivaLinea;
 ctaana = #2#28;
 |HAZ canviana;
 |SI swSiCambio = 1;
     #2#28 = ctaana;
     |GRABA 020#2a;
 |FINSI;
 |LIBERA #2;
 |PINTA 2250, #2#0;
 |PINTA 2253, #2#1;
 |PINTA 2265, #2#2;
|FPRC;

|DEFBUCLE ivaLinea;
 #2#1;
 28,12;
 #3#0, #3#1, 00, #0#2, #0#16;
 #3#0, #3#1, 99, #0#3, #0#17;
 be;
 NULL, sus_ivaLinea;
|FIN;

|PROCESO sus_iva;
 |BUCLE ivaLinea;
|FPRC;

|DEFBUCLE ivaCabecera;
 #3#5;
 9;
 #0#0, 00, 0000000000, #0#19;
 #0#1, 99, 9999999999, #0#20;
 e;
 NULL, sus_iva;
|FIN;


|| ................................... Apuntes
|PROCESO ElGrApte1;
 nSwGApte = 1;
 #213#0 = #113#0;
 #213#1 = #113#1;
 #213#2 = #113#2;
 #213#3 = #113#3;
 #213#4 = ctaana;
 #213#5 = #113#5;
 #213#6 = #113#6;
 #213#7 = #113#7;
 #213#8 = #113#8;
 #213#9 = #113#9;
 |GRABA 020#213n;
 nImporte = nImporte + #113#9;

|FPRC;

|PROCESO ElGrApte2;
 |SI nSwGApte = 0; |ACABA; |FINSI;

 |ABRE #112;
 #112#0 = #1#0;
 #112#1 = #1#1;
 #112#2 = #1#2;
 #112#3 = #1#3;
 #112#4 = ctaana;
 #112#5 = ctaana1;
 #112#6 = 0;
 #112#7 = #1#9;
 #112#8 = nImporte;
 |GRABA 040#112n;
 |CIERRA #112;
|FPRC;

|DEFBUCLE ElGrApte;
 #113#1;
 ;
 #1#0, #1#1, #1#2, #1#3, #1#29, 000;
 #1#0, #1#1, #1#2, #1#3, #1#29, 999;
 e;
 NULL, ElGrApte1, NULL, NULL, ElGrApte2;
|FIN;

|PROCESO ElBorrado;
 |SI #112#4 ! ctaana;
     |BUCLELIN 1NULL#112;
     |BORRA 020#112a;
 |FINSI;
 |LIBERA #112;
|FPRC;

|DEFBUCLE ElBorrado;
 #112#1;
 ;
 #1#0, #1#1, #1#2, #1#3, "       ";
 #1#0, #1#1, #1#2, #1#3, "zzzzzzz";
 e;
 NULL, ElBorrado;
|FIN;

|PROCESO sus_apuntes;
 ctaana = #1#29;
 |HAZ canviana;
 |SI swSiCambio = 1;

     |ABRE #213;
     nSwGApte = 0;
     nImporte = 0;
     |BUCLE ElGrApte;
     |CIERRA #213;
     |SI nSwGApte = 1;
         |BUCLE ElBorrado;
     |FINSI;

     #1#29 = ctaana;
     |GRABA 020#1a;
 |FINSI;
 |LIBERA #1;
 |PINTA 2240, #1#1;
 |PINTA 2251, #1#2;
|FPRC;

|DEFBUCLE LeeApuntes;
 #1#1;
 4,29;
 00, #0#0, #0#19, 0001, #0#16, #0#2;
 99, #0#1, #0#20, 9999, #0#17, #0#3;
 be;
 NULL, sus_apuntes;
|FIN;

|| ................................... Grupos
|PROCESO sus_grupos;
 ctaana = #103#2;
 |HAZ canviana;
 |SI swSiCambio = 1;
     #103#2 = ctaana;
     #103#3 = ctaana1;
     |GRABA 020#103a;
 |FINSI;
 |LIBERA #103;
 |PINTA 2240, #103#0;
 |PINTA 2248, #103#1;
 |PINTA 2252, #103#2;
|FPRC;

|DEFBUCLE LeeGrupos;
 #103#1;
 ;
 "       ", 000;
 "zzzzzzz", 999;
 be;
 NULL, sus_grupos;
|FIN;


|PROCESO sus_grupos_apte2;
 ctaana = #113#6;
 |HAZ canviana;
 |SI swSiCambio = 1;
     #113#6 = ctaana;
     #113#7 = ctaana1;
     |GRABA 020#113a;
 |FINSI;
 |LIBERA #113;
 |PINTA 2247, #113#1;
 |PINTA 2258, #113#2;
 |PINTA 2265, #113#4;
|FPRC;

|DEFBUCLE LeeGrupos_apte1;
 #113#1;
 ;
 #1#0, #1#1, #1#2, #1#3, #1#29, 000;
 #1#0, #1#1, #1#2, #1#3, #1#29, 999;
 be;
 NULL, sus_grupos_apte2;
|FIN;

|PROCESO sus_grupos_apte;
 |BUCLE LeeGrupos_apte1;
|FPRC;

|DEFBUCLE LeeGrupos_apte;
 #1#1;
 4, 29;
 00, #0#0, #0#19, 0001, #0#16, "GRU    ";
 99, #0#1, #0#20, 9999, #0#17, "GRUzzzz";
 be;
 NULL, sus_grupos_apte;
|FIN;

|| **********************************************************************
|| **********************************************************************
|PROCESO graba_t;
 alfa1 = #6#0; |QBF alfa1;
 |SI alfa1 = ""; |Y #0#21 = "N";
     |LIBERA #6;
     |ACABA;
 |FINSI;
 ctaana = #6#0;
 |SI #0#12 = "S";
     |HAZ sustituir;
 |SINO;
     |HAZ mover;
 |FINSI;

 |SI #6#0 ! ctaana;
     guia = guia + 1;
     #7#0 = guia;
     #7#1 = #6#0;
     #7#2 = #6#1;
     #7#3 = ctaana;
     |GRABA 020#7n;
 |FINSI;
 |LIBERA #6;
 |PINTA 2229, #6#0;
|FPRC;

|DEFBUCLE crear_temporal;
 #6#1;
 ;
 #0#2;
 #0#3;
 be;
 NULL, graba_t;
|FIN;
|| ***********************************************************************

|RUTINA min;
 #7#0 = 1;
|FRUT;

|RUTINA max;
 #7#0 = guia;
|FRUT;


|PROCESO modificar; |TIPO 3;
  aFichero = ("zk" + Usuario) % 108;
  |NOME_DAT #7 aFichero;

  |ABRE #7;  |CIERRA #7;  |DELINDEX #7;

|| ------------------------------------------------------------------------\
  |SI #0#12 = "S";
       n_alfa1 = "";
       nume1 = #0#9 - 1; || Antes
       |SI nume1 > 0;
           nume1 = 100 + nume1;
           n_alfa1 = nume1;
      |FINSI;

      n_alfa2 = "";
      nume2 = #0#9 + #0#10; || Despues
      |SI nume2 [ 7;
          nume3 = 8 - nume2;
          nume3 = nume2 * 100 + nume3;
          n_alfa2 = nume3;
      |FINSI;
      masca = #0#11;  |QBF masca;
 |SINO;
      n_alfa1 = "";
      n_alfa2 = "";
      n_alfa3 = "";
      nume3 = #0#15 - #0#13;  || Posicion Destino - Posicion Inicial
      nume1 = nume3; |SI nume3 < 0; nume1 = - nume3; |FINSI;
      masca = " " * nume1;
      |SI nume3 > 0;
          nume1 = #0#13 - 1; || Antes
          |SI nume1 > 0;
              nume1 = 100 + nume1;
              n_alfa1 = nume1;
          |FINSI;
          nume2 = #0#15 + #0#14; || Despues
          |SI nume2 [ 7;
              nume3 = 8 - nume2;
              nume3 = nume2 * 100 + nume3;
              n_alfa2 = nume3;
          |FINSI;
      |FINSI;
      |SI nume3 < 0;
          nume1 = #0#15 - 1; || Antes
          |SI nume1 > 0;
              nume1 = 100 + nume1;
              n_alfa1 = nume1;
          |FINSI;
          nume2 = #0#13 + #0#14; || Despues
          |SI nume2 [ 7;
              nume3 = 8 - nume2;
              nume3 = nume2 * 100 + nume3;
              n_alfa2 = nume3;
          |FINSI;
      |FINSI;
      nume2 = (#0#13 * 100) + #0#14; || Parte a Desplazar
      n_alfa3 = nume2;
  |FINSI;
|| ------------------------------------------------------------------------/

 |D_CUADRO 2115, 2372;
 |PINTA 2216," En Proceso:                                            ";

 guia = 0;
 |ABRE #7;
 |BUCLE crear_temporal;
 |CIERRA #7;

 |SI guia = 0;
     |MENAV "    No existen Cuentas de Analitica para Cambiar ";
     |DELINDEX #7;
     |ACABA;
 |FINSI;

 |PUSHV 0501 2380;
 |PINPA #0#7;
 |ENTLINEAL #1#7, -1, 2, 22, 0, min, max;

 |CONFI "2400S Desea realizar el cambio S/N: ";
 |SI FSalida =  0;
      |POPV;

      |DBASE aAlfa3; |QBF aAlfa3;
      aAlfa4 = aAlfa3 + "def/anm00003.mas";
      |CARGA_DEF aAlfa4, nm00003@;
      |SI FSalida = 0;
          aAlfa4 = aAlfa3 + "def/anm00013.mas";
          |CARGA_DEF aAlfa4, nm00013@;
          |SI FSalida = 0;
              |HAZ ElProceso;
              |DESCARGA_DEF #nm00013@;
          |FINSI;
          |DESCARGA_DEF #nm00003@;
      |FINSI;
 |SINO;
      |POPV;
 |FINSI;
 |DELINDEX #7;
|FPRC;


|PROCESO ElProceso;
      |HAZ CreaCuentas;

      |ABRE #6;
      |ABRE #7;
      |SELECT #2#7;

 |SI #0#18 = "S";
      |ATRI I; |PINTA 2229,"Grupos  ->                                 "; |ATRI 0;
      |BUCLE LeeGrupos;
 |FINSI;

      |ATRI I; |PINTA 2229,"Apuntes ->                                 "; |ATRI 0;
      |BUCLE LeeApuntes;

      |ATRI I; |PINTA 2229,"Grupos Apuntes ->                          "; |ATRI 0;
      |BUCLE LeeGrupos_apte;

      |ATRI I; |PINTA 2229,"Registro IVA/IRPF ->                       "; |ATRI 0;
      |BUCLE ivaCabecera
      |CIERRA #7;
      |CIERRA #6;
|FPRC;
