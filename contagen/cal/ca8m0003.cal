|FICHEROS;
   ca8m0003 #0,MANTE;    || cuentas
   cacuenta #1;
   ca8cumay #2;
   &cacuent@ #3;

   cacuebal #5;
   ca8m0002 #422;

   ca8z0003 #10;
   ca8ctrle #400;  ||Control Empresa
   ca8comay #401;  ||Control Cuentas de mayor

   ca8m003@ #900;  ||reflejo ar buscar
|FIN;

|VARIABLES;
   &entrada = 1;
   &nSwQueO = 0;
   &enConsulta = 0;
   nModo    = 0;
   cinco    = "     ";
   aCta1    = "";
   aCta2    = "";
   cta      = "     ";
   ctanum   = 0;
   ctaalf   = "";
   longalf  = "";
   longnum  = "";
   nada     = "";
   aAlfa1   = "";
   nNume1   = 0;
   &aPathPlan = "";
   nInkey   = 0;

   mesdes = "";
   meshas = "";

   nYaExiste = 0;
   aMas      = "";
   aDef      = "";
   nSw       = 0;
   xx        = 0;
   aCta2008  = "";
   aCta2007  = "";
|FIN;

|INCLUYE dscont_i;
|INCLUYE defec1_i;

||  ***************************************

|PROCESO inf;
 #3#0 = "           ";
 #3#3 = "S";
|FPRC;

|PROCESO sup;
 #3#0 = "zzzzzzzzzzzz";
 #3#3 = "S";
|FPRC;

|PROCESO inf0;
 #1#0 = "           ";
 #1#3 = "S";
|FPRC;

|PROCESO sup0;
 #1#0 = "zzzzzzzzzzzz";
 #1#3 = "S";
|FPRC;

|CALCULO tipo7; |TIPO 7;
 |SI nSwQueO = 2;
     |SI enConsulta = 0;
         |MENSA "    <F2> Consulta , <F3> Modificar Equivalencia, <F4> Ver solo las cambiadas";
     |FINSI;

     |SI enConsulta = 1;
         |MENSA "    <F2> Consulta , <F3> Modificar Equivalencia, <Esc> Ver todas";
     |FINSI;
 |FINSI;

 |SI nSwQueO = 1;
     |MENSA "    Entre Cta Plan 1990, <F2> Consulta , <F3> Modificar Equivalencia";
 |FINSI;

 |SI nSwQueO = 0;
     |MENSA "    Entre Cuenta del Plan Contable de 1990, <F2> Consulta Plan 1990 ";
 |FINSI;
|FCAL;

|CALCULO Tipo11; |TIPO 11;
 aCta1 = #0#2;
 aCta2 = #0#3;

 nInkey = FSalida;
 |SI nInkey = 10;
     |ABRE #3;
     #3#0 = #0#0;
     |CONSULTA_F_CLAVE #6#3, inf, sup;
     #0#0 = #3#0; |PINTA #0#0;
     #0#1 = #3#2; |PINTA #0#1;
     |CIERRA #3;
 |FINSI;

 |SI nInkey = 11; |Y nSwQueO ! 0;
     |LEE 141#0=;

     |SI nSwQueO = 1;
         |ENCAMPO #4#0;
         |ENCAMPO #5#0;
     |SINO;
         |C_M #0#2, 1, "S"; |C_M #0#3, 1, "S";
         |ENCAMPO #2#0;
         |ENCAMPO #3#0;
         |C_M #0#2, 1, "N"; |C_M #0#3, 1, "N";
         |SI aCta1 ! #0#2;
             #0#6 = "*"; |PINTA #0#6;
             #0#4 = aCta1;
             #0#5 = aCta2;
         |FINSI;
     |FINSI;

     |GRABA 000#0a;
     |LIBERA #0;

     |ERROR;
 |FINSI;

 |SI nInkey = 12; |Y nSwQueO = 2;  |Y enConsulta = 0;
     |PTEC 807;
     enConsulta = 1;
     |ACABA;
 |FINSI;

 |SI FSalida ! 20;  |ACABA;  |FINSI;
  salidas  = FSalida;
 |HAZ FiltraPuntos;
|FCAL;

|CALCULO cuenta; |TIPO 6;
   |C_M #0Campo, 0, aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   eaCuenta = #0Campo
   salidas  = FSalida;
   |HAZ FiltraPuntos;
   #0Campo = eaCuenta;
   |PINTA #0Campo;
   FSalida = salidas;
|FCAL;

|CALCULO cuefin; |TIPO 0;
   nInkey = FSalida;

   |C_M #0Campo, 0, aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   |SI nInkey = 10; |Y Campo = 0;
      |ABRE #3;
      #3#0 = #0#0;
      |CONSULTA_F_CLAVE #6#3, inf, sup;
      #0#0 = #3#0; |PINTA #0#0;
      #0#1 = #3#2; |PINTA #0#1;
      |CIERRA #3;
   |FINSI;

   |SI nInkey = 10;
       |SI Campo = 2; |O Campo = 4;
           |ABRE #1;
           #1#0  = #0Campo;
           |CONSULTA_F_CLAVE #6#1, inf0, sup0;
           nNume1 = Campo + 1;
           #0Campo  = #1#0; |PINTA #0Campo;
           #0nNume1 = #1#2; |PINTA #0nNume1;
           |CIERRA #1;
       |FINSI;
   |FINSI;

   |SI Campo = 2; |O Campo = 4;
      |SI nInkey = 11;
          |SI Campo = 2; #0#2 = #0#0; |PINTA #0#2; |FINSI;
          |SI Campo = 4; #0#4 = #0#0; |PINTA #0#4; |FINSI;
      |FINSI;

      |SI nInkey = 12;
           |SI Campo = 2; #0#2 = "PDTE "; |PINTA #0#2; |FINSI;
           |SI Campo = 4; #0#4 = "PDTE "; |PINTA #0#4; |FINSI;
      |FINSI;

      |SI nInkey = 13;
           |SI Campo = 2; #0#2 = ""; |PINTA #0#2; |FINSI;
           |SI Campo = 4; #0#4 = ""; |PINTA #0#4; |FINSI;
      |FINSI;
  |FINSI;

  |SI Campo = 2; |O Campo = 4;
      #0Campo = #0Campo > "";
      |SI #0Campo = "PDTE        ";
          nNume1 = Campo + 1;
          #0nNume1 = "DESAPARECE, PENDIENTE DE AJUSTE A...";
          |PINTA #0Campo;
          |PINTA #0nNume1;
          |ACABA;
      |FINSI;
  |FINSI;

   emCta    = 1;
   eaCuenta = #0Campo;
   |HAZ SacaNivel;
   |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea

   |SI Campo = 2;

       |HAZ MiroSiYaEsta;
       |SI nYaExiste = 1;
           #0#2 = aCta1; |PINTA #0#2;
           #0#3 = aCta2; |PINTA #0#3;
           |ERROR;
           |ACABA;
       |FINSI;

       emCta = 0;
       |HAZ LeeCuenta;
       |SI eswCta ! 0;
           |CONFI "    SNo Existe Cuenta. Dar de Alta";
           |SI FSalida ! 0;
               #0#2 = aCta1; |PINTA #0#2;
               #0#3 = aCta2; |PINTA #0#3;
               |ERROR;
               |ACABA;
           |SINO;
               |HAZ AltaCta;
           |FINSI;
        |FINSI;  ||Cuenta erronea
        #0#3 = #1#2; |PINTA #0#3;
   |FINSI;

   |SI Campo = 0;
       |ABRE #3;
       #3#0 = #0Campo;
       |LEE 001#3=;
       |SI FSalida ! 0;
           |MENAV "    Error, NO existe Cuenta en Empresa Anterior";
           |CIERRA #3;
           |ERROR;
           |ACABA;
       |FINSI;
       |CIERRA #3;
       |SI #3#3 = "N";
           |MENAV "    Cuenta sin pase Directo";
       |FINSI;
       #0#1 = #3#2; |PINTA #0#1;

       nModo = (FEntrada / 100) ! 0;
       |SI nModo = 1; |Y nSwQueO > 1;
           aAlfa1 = #0#0; |QBF aAlfa1;
           eaCta2008 = eaCuenta;
           eSwTabla  = 0;
           |SI aAlfa1 ! ""; |HAZ  ProvCta08; |FINSI;
           #0#2 = eaCta2008; |PINTA #0#2;
           #0#3 = #0#1;      |PINTA #0#3;
       |FINSI;
   |FINSI;
|FCAL;

|CALCULO HeCambiado; |TIPO 0;
  |SI aCta1 ! #0#2;
      |ABRE #1;
      #1#0 = aCta1;
      |LEE 101#1=;
      |SI FSalida = 0;
          nSw = 0;
          |PARA xx = 9; |SI xx [ 53; |HACIENDO xx = xx + 1;
                |SI #1xx ! 0; nSw = 2; |SAL; |FINSI;
          |SIGUE;
          |SI nSw = 0;
              aCta2008 = #1#0;
              aCta2007 = #0#0;
              |HAZ MiroSiYaEsta0;
              nSw = nYaExiste;
          |FINSI;
          |SI nSw = 0;
              |CONFI "    SDar de Baja la Cuenta Asignada Antes";
              |SI FSalida = 0;
                  |BORRA 020#1a;
              |FINSI;
          |FINSI;
          |SI nSw = 2;
              |MENAV "    La Cuenta Asignada Anteriormente tiene Movimientos. Revisar su contabilidad";
          |FINSI;
      |FINSI;
      |LIBERA #1;
      |CIERRA #1;
  |FINSI;
|FCAL;

|PROCESO ElTipo5;  |TIPO 5;
      |PATH_DAT #3 aPathPlan;
|FPRC;

|PROCESO AltaCta;
   |ABRE #1;
   #1#0 = #0#2;
   #1#1 = enNivel;
   |LEE 001#1=;
   |SI FSalida ! 0;
       NumMes = dig1; |HAZ NombreMes; mesdes = NombreMes;
       NumMes = dig1 + 11; |SI NumMes > 12; NumMes = NumMes - 12; |FINSI;
       |HAZ NombreMes; meshas = NombreMes;
       |DDEFECTO #1;
       #1#0 = #0#2;
       #1#1 = enNivel;
       #1#2 = #0#1;
       #1#3 = "N"; |SI enNivel = dig3; #1#3 = "S"; |FINSI;
       nMensa   = 1;
       nMensa9  = 1;
       |HAZ defect1;
       #1#56   = mesdes;
       #1#57   = meshas;
       |GRABA 020#1n;
  |FINSI;
  |LIBERA #1;
  |CIERRA #1;
|FPRC;

|PROCESO MiroSiYaEsta;
 aCta2008 = #0#2;
 aCta2007 = #0#0;
 |HAZ MiroSiYaEsta0;
 |SI nYaExiste = 1;
     |CONFI "    NEsta Cta YA esta Asignada a otra Cta del 2007, ¨ Es correcto ?";
     |SI FSalida = 0;
         nYaExiste = 0;
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO LeoEspejo;
 |SI #900#0 ! aCta2007;
     nYaExiste = 1;
 |FINSI;
|FPRC;

|DEFBUCLE LeoEspejo;
 #900#2002;
 ;
 aCta2008, "            ";
 aCta2008, "zzzzzzzzzzzz";
 ;
 NULL, LeoEspejo;
|FIN;

|PROCESO MiroSiYaEsta0;
 nYaExiste = 0;
 |DBASE aMas;
 aDef = aMas + "def/ca8m0003";
 |CARGA_DEF aDef, ca8m003@;
 |SI FSalida ! 0;
     |MENAV "    Error al Carga fichero de la Tabla Equivalencia";
     |ACABA;
 |FINSI;

 |BUCLE LeoEspejo;

 |DESCARGA_DEF #900;
|FPRC;
