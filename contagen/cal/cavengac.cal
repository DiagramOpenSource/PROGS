|FICHEROS;
    cavengac #0;
    camaasic #1;
    camaasil #2;

    camaniva #4;
    caivalin #14;
    cagruiva #11;

    cavengca #6;
    cavengli #7;
    caivaasi #200;
    calibros #201;
    caivases #202;

    catracfe #3; || def para los tipos de IVA;
    catracf2 #8; || trabajo lineas Factura
    catracf1 #9; || trabajo para apuntes
|FIN;

|VARIABLES;
   mensa1   = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
   mensa2   = "    ERROR FECHA INFERIOR O IGUAL AL BLOQUEO DE DATOS";
    &entrada = 1;
    &enQSerie = 0;

    LineaIVA = 0;
    aFichero = "";
    alfa1 = "";
    alfa2 = "";
    alfa4 = "";
    alfa5 = "";
    aCampo42 = "";
    aCampo43 = "";
    aCampo44 = "";
    avisos = "";

    libro = 0;
    serie = "";
    sw = 0;
    NumDoc = 0;

    sw_ERROR = 0;
    sw_che = 0;
    antnum = 0;

    diario = 0;
    fecha  = @;
    docume = 0;
    asient = 0;

    numfac1 = 0;
    NumFactura = 0;
    desant = "";
    desact = "";
    primera = "";
    ultima = "";

    iv1 = 0;
    iv2 = 0;
    iv3 = 0;
    iv4 = 0;
    iv5 = 0;
    re1 = 0;
    re2 = 0;
    re3 = 0;
    re4 = 0;
    re5 = 0;

    nNume1 = 0;
    nNume2 = 0;

    num = 0;
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    nume5 = 0;
    nume6 = 0;
    nume7 = 0;
    nume8 = 0;
    num21 = 0;
    num22 = 0;
    num23 = 0;
    num24 = 0;
    num25 = 0;

    tope_cant = 0;
    totanter = 0;

    tipo   = "";
    cue    = "";
    niv    = 0;
    tpiva  = 0;
    tprec  = 0;
    concep = 0;
    imp    = 0;
    descr  = "";

    cam = 0;
    error = 0;

    linea = 0;
    sw_fin = 0;
    aun_hay = 0;
    ac = 0;
    sw_ac = 0;
    sihay = 0;
    y  = 0;
    x  = 0;
    x1 = 0;
    x2 = 0;
    x3 = 0;
    x4 = 0;
    x5 = 0;
    ceros = "000000";
    estado = 0;
    swPaso = 0;

   fecalf = "";
   ini = 0;
   mes = "";
   mesfec = 0;
   mesac = 0;
   a = 0;
   b = 0;
   c = 0;


|FIN;

|INCLUYE dscont_i;

|| _________________________________ Calculos de pantalla

|PROCESO control; |TIPO 8;
 |HAZ inicial; |SI swerror = 2; |ACABA; |FINSI;

 |ABRE #200;
 |DDEFECTO #200;
 |LEE 001#200p;
 tope_cant = #200#38;     || cantidad tope de facturas englobadas
 enQSerie = 0; |SI #caivaasi#44 = "S"; enQSerie = 1; |FINSI;
 eaPaCTA1 = #caivaasi#135;  ||permitir alta no estructuras 2008
 eaPaCTA2 = #caivaasi#136;  ||Aviso 1990
 eaPaNif1 = #caivaasi#152;
 eaPaNif2 = #caivaasi#153;
 eaABanco = #caivaasi#161;
 eaObClPr = #caivaasi#186;

 |CIERRA #200;
 alfa5 = #caivaasi#97; |QBF alfa5;
|FPRC;

|PROCESO def_fechas; |TIPO 7;
 #0#0 = fec1; |PINTA #0#0;
 #0#1 = fec2; |PINTA #0#1;
|FPRC;

|PROCESO fecha; |TIPO 0;
   |SI #0Campo < fec1; |O #0Campo > fec2;
      |MENAV mensa1;
      |ERROR;
      |ACABA;
   |SINO;
      |SI #0Campo [ fec3;
          |MENAV mensa2;
          |ERROR;
          |ACABA;
      |FINSI;
   |FINSI;
  |SI #0Campo = 1;
      |SI #0#1 > #0#0;
          |MENAV "     Fecha ERRONEA ";
          |ERROR;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO entrada; |TIPO 0;
 |SI swPaso = 0;
     |HAZ contador;
     #0#3 = nDOCUMENTO; |PINTA #0#3;
     #0#4 = #0#3; |PINTA #0#4;
     swPaso = 1;
     |ABRE #6;
     |LEE 001#6p;
     #0#7 = #6#13; |PINTA #0#7;
     #0#5 = #6#1;  |PINTA #0#5;
     #0#6 = #6#3;  |PINTA #0#6;
     |CIERRA #6;
 |FINSI;
|FPRC;

|| **************************************************************************
||**************************************************************************
|PROCESO si_quedan;
    aun_hay = 1;
    #6#15 = #6#15 + #7#4  + #7#5  +  #7#6 +  #7#7 +  #7#8;
    #6#16 = #6#16 + #7#9  + #7#10 + #7#11 + #7#12 + #7#13;
    #6#17 = #6#17 + #7#14 + #7#15 + #7#16 + #7#17 + #7#18;
    #6#19 = #6#15 + #6#16 + #6#17;
|FPRC;

|PROCESO borra_cab1;
    aun_hay = 0;
    #6#15 = 0;
    #6#16 = 0;
    #6#17 = 0;
    #6#19 = 0;
    |BUCLELIN 2si_quedan#6;
    |SI aun_hay = 0;
       |BORRA 020#6a;
    |SINO;
       |GRABA 020#6a;
    |FINSI;
    |LIBERA #6;
|FPRC;

|DEFBUCLE borra_cab;
 #6#1;
 ;
 #0#0;
 #0#1;
 e;
 NULL, borra_cab1;
|FIN;

____________________________________/ ACTUALIZACION

|PROCESO lineas;
 |SI cam = 0;
     |HAZ nueva;
     |SI sw_ERROR ! 0; |ERROR10; |ACABA; |FINSI;
     cam = 1;
 |FINSI;

 totanter = #3#6 + #7#3;
 |SI totanter > tope_cant;
     |HAZ factura;
     |HAZ nueva;
     |SI sw_ERROR ! 0; |ERROR10; |ACABA; |FINSI;
 |FINSI;
 |HAZ traba_lineas;
 |SI sw_ERROR ! 0; |ERROR10; |ACABA; |FINSI;

 |SI error = 0;
     |BORRA 020#7a;
 |FINSI;
|FPRC;

|PROCESO hacer;
     cam = 0;
  fecha  = #6#0; |SI fecha [ fec3; |ACABA; |FINSI;
  libro  = #6#1;
  serie  = #6#13; |QBF serie;

  |ABRE #4;
  |ABRE #14;
  |HAZ BuscaDoc;
  |HAZ ult_fra;
  |SI NumFactura ] 999999; |HAZ errorgordo; |ERROR10; |ACABA; |FINSI;

  |BUCLELIN 0lineas#6;
  |HAZ factura;
  |HAZ ActNumero;
  |SI sw_ERROR ! 0; |ERROR10; |FINSI;
  |CIERRA #4;
  |CIERRA #14;
|FPRC;

|DEFBUCLE cavengac0;
 #6#1;
 ;
 #0#0;
 #0#1;
 be;
 NULL, hacer;
|FIN;

||**************************************************************************
|| ******************* PROCESO POR TIPO 3 **********************************
||**************************************************************************

|PROCESO actual; |TIPO 3;

  aFichero = ("t8" + Usuario) % 108;
  |NOME_DAT #8 aFichero;
  aFichero = ("t9" + Usuario) % 108;
  |NOME_DAT #9 aFichero;

  diario = #0#2;
  docume = #0#3;
  asient = #0#4;
  aTipoIVA = "R";   ||Ventas

  efFechaIVA = #0#0; |HAZ SacaIVA;

  sw = 0;
  |ABRE #1;
  |ABRE #2;
  sw_fin = 0;
  |BUCLE cavengac0;
  |CIERRA #camaasil;
  |CIERRA #camaasic;

  |BUCLE borra_cab;
  |DELINDEX #8;
  |DELINDEX #9;
|FPRC;

|| **********************************************************************
|PROCESO nueva; || para crear la factura ya
  iv1 = 0; iv2 = 0; iv3 = 0; iv4 = 0; iv5 = 0;
  re1 = 0; re2 = 0; re3 = 0; re4 = 0; re5 = 0;
  |DDEFECTO #3;
  #3#0    = libro;
  numfac1 = NumFactura + 1;
  #3#1    = numfac1;     || Primera  la que es
  #3#2    = NumFactura;  || Ultima
  #3#7    = #6#13;       || Serie

  |ABRE #8;  |CIERRA #8;  |DELINDEX #8;
  |ABRE #9;  |CIERRA #9;  |DELINDEX #9;

  |SI sw_fin = 1;
      |HAZ contador;
      docume = nDOCUMENTO;
      asient = asient + 1;
  |FINSI;
  sw_fin = 1;
|FPRC;

|PROCESO traba_lineas;
 error = 0;
 |ABRE #8;
 #8#0 = #7#2;
 |LEE 101#8=;
 |SI FSalida ! 0;
     |DDEFECTO #8;
     #8#0 = #7#2;
     |GRABA 020#8b;
 |FINSI;
 |PARA num = 4; |SI num [ 18; |HACIENDO num = num + 1;
       #8num =  #8num + #7num;  || sumo bases y cuotas
 |SIGUE;
 |GRABA 020#8a;
 |LIBERA #8;
 |CIERRA #8;

 #3#3 = #3#3  + #7#4  + #7#5  + #7#6  + #7#7  + #7#8;
 #3#4 = #3#4  + #7#9  + #7#10 + #7#11 + #7#12 + #7#13;
 #3#5 = #3#5  + #7#14 + #7#15 + #7#16 + #7#17 + #7#18;
 #3#6 = #3#3 + #3#4 + #3#5;
 NumFactura = NumFactura + 1;
 #3#2 = NumFactura;
 #3#8 = #3#8 + 1;
|FPRC;

||------------------------------------------------------------------------

|| --- introduccion lineas
|PROCESO lasLineas0;

 #11#0 = #8#0;
 |LEE 001#11=;

 |PARA num = 1; |SI num [ 5; |HACIENDO num = num + 1 ;

       |DDEFECTO #14;
       |HAZ sumanume;
       |SI #8num21 ! 0;   || Existe Importe de Base.

           LineaIVA = LineaIVA + 1;
           tpiva = #11nume3;
           tprec = #11nume6;
|| .............................................../ Base
           tipo   = "B";         ||Base
           cue    = #11nume1;    || cuenta
           concep = #11nume2;    || concepto
           x      = nume2 + 15;
           descr  = #11x;        || descripcion
           imp    = #8num21;
           |HAZ gratra1;
|| .............................................../ IVA
           |SI #8num23 ! 0;
               tipo = "I";         || IVA
               cue  =  #11nume4;   || cuenta
               concep = #11nume5;  || concepto
               x = nume5 + 15;
               descr  = #11x;      || descripcion
               imp = #8num23;
               |HAZ gratra1;
           |FINSI;
|| ............................................../ Recargo
           |SI #8num25 ! 0;
               tipo = "R";        || Recargo
               cue  =  #11nume7;  || cuenta
               concep = #11nume8; || concepto
               x = nume8 + 15;
               descr  = #11x;     || descripcion
               imp = #8num25;
               |HAZ gratra1;
           |FINSI;
|| ........................................................................
           #14#0  = #4#0;
           #14#1  = #4#1;
           #14#2  = LineaIVA;
           #14#3  = #11nume2;    || concepto
           x = nume2 + 15;
           #14#4  = #11x;
           #14#6  = #8num21;     || Base Imponible
           #14#7  = #11nume3;    || IVA
           #14#8  = #8num23;     || Cuota IVA
           #14#9  = #11nume6;    || Recargo
           #14#10 = #8num25;     || Cuota Recargo
           #14#12  =  #11nume1;   || cuenta
           #14#13  =  #11nume4;   ||IVA
           #14#14  =  #11nume7;   || Recargo
           |SI #14#8  = 0; #14#7 = 0; #14#13 = ""; |FINSI;
           |SI #14#10 = 0; #14#9 = 0; #14#14 = ""; |FINSI;

           efFechaIVA = #4#4;
           enPorcIVA = #14#7;  enPorcREC = #14#9;
           eaCtaIVA  = #14#13; eaCtaREC  = #14#14;
           |HAZ BuscaTipoIVA;
           #14#16 = enLineaIVA;

           #14#30 = #4#10;    || Departamento
           #14#31 = #4#11;    || subdepartamento
           |GRABA 020#14n;
           |LIBERA #14;
     |FINSI;
 |SIGUE;
|FPRC;

|DEFBUCLE lasLineas;  ||crear lineas de bases y facturas
 #8#1;
 ;
 01;
 99;
 be;
 NULL, lasLineas0;
|FIN;

|PROCESO factura; || Crear la factura;
 |SI numfac1 > NumFactura; |ACABA; |FINSI; || no hay ninguna factura;

     primera = numfac1;    primera = (ceros + primera) % -106;
     ultima  = NumFactura; ultima  = (ceros + ultima ) % -106;
     desant =  " " + primera + " a " + ultima;
     |SI numfac1 = NumFactura; desant = " "+ primera; |FINSI;

     alfa4 = serie + alfa5; |QBF alfa4;
     aCampo42 = #6#23;
     aCampo43 = "";
     aCampo44 = "";
     |SI aCampo42 = "A"; |O aCampo42 = "B";
         |SI numfac1 ! NumFactura;
             aCampo43 = alfa4 + primera;
             aCampo44 = alfa4 + ultima;
          |SINO;
              |SI aCampo42 = "A"; aCampo42 = " "; |FINSI;
              |SI aCampo42 = "B"; aCampo42 = "J"; |FINSI;
          |FINSI;
    |FINSI;
|| ................... .Graba Cabecera de IVA (Camaniva)
 |ABRE #4;
 |DDEFECTO #4;
 #4#0 = #3#0;     ||Libro
 #4#1 = NumDoc;   ||Documento
 |LEE 001#4=;
 estado = FSalida;
 #4#0 = #3#0;     ||Libro
 #4#1 = NumDoc;   ||Documento
 #4#2 = #3#7;     ||Serie
 #4#3 = #3#2;     ||Numero de Factura

 #0#5 = #4#0; |PINTA #0#5;
 #0#7 = #4#2; |PINTA #0#7;
 #0#6 = #4#3; |PINTA #0#6;

 #4#4  = fecha;   || Fecha Factura
 #4#5  = #6#12;   || Periodo
 #4#6  = "N";      || Liquidada "N"
 #4#7  = diario;
 #4#8  = fecha;
 #4#9  = docume;

 #4#10 = #6#10;    || Departamento
 #4#11 = #6#20;    || subdepartamento
 #4#12 = #6#4;     || Cuenta Cliente
 #4#13 = #6#8;     || Nombre Cliente
 #4#14 = #6#9;     || Nif

 #4#15 = #3#3;          ||total bases
 #4#19 = #3#3;          ||total bases
 #4#20 = 0;             || Irpf
 #4#23 = #3#4 + #3#5;   ||Cuotas IVA Y Recargo
 #4#21 = #4#19 + #4#23 - #4#26 - #4#20;
 #4#17 = #4#15 %  #4#16;

 #4#22 = desant;    || Referencia
 #4#24 = #6#21;     || Actividad
 #4#25 = #6#22;     || subactividad
 #4#26 = 0;

 #4#27  = #6#6;  #4#29 = #6#6;
 #4#28  = #6#7;  #4#30 = #6#7;

 #4#31 = eaDesgIVA;
 #4#32 = eaDesgCTP;
 #4#33 = "N";
 #4#34 = eaDesgDTO;

 #4#35 = "N";
 #4#36 = "R";
 #4#37 = "N";
 #4#38 = #3#8;  ||numero de facturas
 #4#39 = #caivaasi#48;
 #4#40 = 0;
 #4#42 = aCampo42;
 #4#43 = aCampo43;
 #4#44 = aCampo44;
 |SI estado = 0;
     |GRABA 020#4a;
 |SINO;
     |GRABA 020#4n;
 |FINSI;
 |LIBERA #4;
 |CIERRA #4;

 |HAZ asiento1;   || cabecera y primera

 LineaIVA = 0;
 |ABRE #11;
 |ABRE #9;
 |BUCLE lasLineas;
 |CIERRA #9;
 |HAZ losApuntes;
 |CIERRA #11;
 NumDoc = NumDoc + 1;
|FPRC;

|PROCESO asiento1; || Graba el Asiento del Cliente
   |HAZ cabecera;
   |DDEFECTO #2;
   #2#0 = diario;
   #2#1 = fecha;
   #2#2 = docume;
   linea = linea + 10;
   #2#3 = linea;
   #2#4 = #6#4;
   eaCuenta = #2#4; |HAZ SacaNivel;
   #2#5  = enNivel;
   #2#6  = #6#6; desact = #6#7; |QBF desact;
   #2#7  = desact + desant;
   #2#8  = "D";
   #2#9  = #3#6;
   #2#19 = "F";
   #2#20 = 0;
   |HAZ adapta;
   |GRABA 020#2n;
   |LIBERA #2;
   |HAZ act_cabecera;
   |HAZ Actualiza;
|FPRC;

|PROCESO LeeAux9;
   #2#0 = diario;
   #2#1 = fecha;
   #2#2 = docume;
   linea = linea + 10;
   #2#3 = linea;
   #2#4 = #9#1; eaCuenta = #2#4; |HAZ SacaNivel;
   #2#5 = enNivel;
   #2#6 = #9#6; desact = #9#7; |QBF desact; || concepto para este apunte
   #2#7 = desact + desant;
   #2#8 = "H";
   #2#9 = #9#3;
   #2#19 = #9#0;          || tipo acumulador
   #2#20 = #9#8;          || numero acumulador

   |HAZ adapta;
   |GRABA 020#2n;
   |HAZ act_cabecera;
   |HAZ Actualiza;
|FPRC;

|DEFBUCLE LeeAux9;
 #9#1;
 ;
 tipo, "            ", 00, 001;
 tipo, "zzzzzzzzzzzz", 99, 999;
 ;
 NULL, LeeAux9;
|FIN;

|PROCESO losApuntes;
 tipo = "I"; |BUCLE LeeAux9;
 tipo = "R"; |BUCLE LeeAux9;
 tipo = "B"; |BUCLE LeeAux9;
|FPRC;

|PROCESO cabecera; || lee la cabecera.
  |DDEFECTO #1;
  #1#0 = diario;
  #1#1 = fecha;
  #1#2 = docume;
  #1#3 = asient;
  linea = -9;
  |GRABA 020#1n;
  |SI FSalida ! 0;
      #2#0 = #1#0;
      #2#1 = #1#1;
      #2#2 = #1#2;
      #2#3 = 9999;
      |LEE 040#2];
      |SI FSalida = 0;
          |LEE 040#2a;
          |SI #2#0 = #1#0; |Y #2#1 = #1#1; |Y #2#2 = #1#2;
              linea = #2#3;
          |FINSI;
      |SINO;
          |LEE 040#2u;
          |SI FSalida = 0; |Y  #2#0 = #1#0; |Y #2#1 = #1#1; |Y #2#2 = #1#2;
              linea = #2#3;
              nREGISTRO = #1#12;
          |FINSI;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO act_cabecera;
  #1#0 = diario;
  #1#1 = fecha;
  #1#2 = docume;
  |LEE 101#1=;
  |SI #2#8 = "D";
       #1#4 = #1#4 + #2#9;
       |SI #1#09 = 0; #1#08 = #2#4; #1#09 = #2#5; |FINSI;  || contrp.HABER
  |SINO;
       #1#5 = #1#5 + #2#9;
       |SI #1#11 = 0; #1#10 = #2#4; #1#11 = #2#5; |FINSI;  || contrp.DEBE
  |FINSI;
  #1#6 = #1#4 - #1#5;        || saldo
  #1#12 = nREGISTRO;
  #1#13 = 1;
  |GRABA 020#1a;
  |LIBERA #1;
|FPRC;

|PROCESO adapta;
 |SI #2#8 = "D";
        #2#16 = #2#4;
        #2#17 = #2#5;
        #2#10 = "";
        #2#11 = 0;
 |SINO;
        #2#16 = "";
        #2#17 = 0;
        #2#10 = #2#4;
        #2#11 = #2#5;
 |FINSI;
 #2#21 = #6#10;
 #2#28 = #6#20;
 #2#24 = nREGISTRO;
 #2#12 = "R";           || repercutido
 #2#13 = #4#0;          || libro
 #2#14 = #4#3;          || factura
 #2#15 = #4#2;          || serie (inhibida)
|FPRC;

|PROCESO Actualiza;
   MasMenos     = #camaasil#9;
   DebeHaber    = #camaasil#8;
   aCUENTA      = #camaasil#4;
   nREGISTRO    = #camaasil#24;
   nDIARIO      = #camaasil#0;
   aFECHA       = #camaasil#1;
   nDOCUMENTO   = #camaasil#2;
   nNIVELCUENTA = #camaasil#5;

   |HAZ ActDiario;          || diario
   |HAZ ActCuenta;          || cuenta
   |HAZ ActTesteo;
|FPRC;


|PROCESO sumanume;
  nume1 = num + 1;
  nume2 = nume1 + 30; || concepto Base
  nume3 = nume1 + 60; || % IVA
  nume4 = nume1 +  5; || Cta IVA
  nume5 = nume1 + 35; || Concepto IVA
  nume6 = nume1 + 65; || % Recargo
  nume7 = nume1 + 10; || Cta Recargo
  nume8 = nume1 + 40; || Concepto Recargo

  num21 = num + 3;
  num23 = num21 +  5; || Cuota IVA
  num25 = num21 + 10; || Cuota Recargo
|FPRC;

||*********************** MENSAJES
|PROCESO color_c;        || activa el color caracter en avisos
|COLOR 14, 0;
|ATRI 0;
|FPRC;

|PROCESO color_f;        || activa el color graficos en avisos
|COLOR 4, 0;
|ATRI 0;
|FPRC;

|PROCESO errorgordo;
   |AVISO;
   |CUADRO 1007 1373;
   |ATRI R;
   avisos = "00" + libro;
   avisos = avisos % -102;
   avisos = " ­­­ ERROR !!!. SE HA LLEGADO AL FINAL DEL LIBRO " + avisos + " DE FACTURAS. ";
   |PINTA 1108, avisos;
   |PINTA 1208, " ! EL PROCESO NO PUEDE CONTINUAR.!  VERIFIQUE SUS DATOS ........ ";
   |ATRI 0;
   |PAUSA;
   sw_ERROR = 1;
|FPRC;

|| **************************************************************
|| PROCESOS CON LOS NUMEROS DE FACTURAS
|| ___________________/ Calcular el ultimo numero de factura del libro
|PROCESO ult_fra;
 NumFactura = 1;
 |SI enQSerie = 0;
     |ABRE #calibros;
     #calibros#0 = libro;
     |LEE 101#calibros.=;
     |SI FSalida = 0;
         NumFactura = #calibros#4;
     |FINSI;
     |LIBERA #calibros;
     |CIERRA #calibros;
 |SINO;
     |ABRE #caivases;
     #caivases#0 = serie;
     #caivases#1 = aTipoIVA;
     |LEE 101#caivases.=;
     |SI FSalida = 0;
         NumFactura = #caivases#3;
     |FINSI;
     |LIBERA #caivases;
     |CIERRA #caivases;
  |FINSI;
  NumFactura = NumFactura - 1; ||numero anterior
|FPRC;

|PROCESO ActNumero;
 |SI enQSerie = 0;
     |ABRE #calibros;
     #calibros#0 = libro;
     |LEE 101#calibros.=;
     |SI FSalida ! 0;
         |DDEFECTO #calibros;
         #calibros#0 = libro;
         #calibros#1 = "LIBRO DE VENTAS ENGLOBADAS";
         #calibros#2 = aTipoIVA;
         #calibros#3 = 1;
         |GRABA 020#calibros.b;
     |FINSI;
     #calibros#4 = NumFactura + #calibros#3;
     |GRABA 020#calibros.a;
     |LIBERA #calibros;
     |CIERRA #calibros;
 |SINO;
     |ABRE #caivases;
      #caivases#0 = serie;
      #caivases#1 = aTipoIVA;
      |LEE 101#caivases.=;
      |SI FSalida ! 0;
          |DDEFECTO #caivases;
          #caivases#0 = serie;
          #caivases#1 = aTipoIVA;
          #caivases#2 = "SERIE DE VENTAS ENGLOBADAS";
          #caivases#4 = 1;
          |GRABA 020#caivases.b;
      |FINSI;
      #caivases#3 = NumFactura  + #caivases#4;
      |GRABA 020#caivases.a;
      |LIBERA #caivases;
      |CIERRA #caivases;
  |FINSI;
|FPRC;

|PROCESO BuscaDoc;
   #camaniva#0 = libro;
   #camaniva#1 = 9999999999;
   |LEE 010#camaniva.];
   |SI FSalida = 0;
       |LEE 010#camaniva.a;
   |SINO;
       |LEE 010#camaniva.u;
   |FINSI;
    NumDoc = 1;
    |SI FSalida = 0; |Y #camaniva#0 = libro;
        NumDoc = #camaniva#1 + 1;
   |FINSI;
|FPRC;

|PROCESO gratra1; || trabajo para crear apuntes y factura.
 nNume1 = 31; |SI tipo = "B"; nNume1 = 32; |FINSI;
 nNume2 = LineaIVA;
 |SI #4nNume1 = "N"; nNume2 = 0; |FINSI;
 #9#0 = tipo;    || B/I/R
 #9#1 = cue;     || cuenta
 #9#2 = niv;     || digito
 #9#6 = concep;  || concepto.
 #9#8 = nNume2;
 |LEE 101#9=;
 |SI FSalida ! 0;
     #9#0 = tipo;    || B/I/R
     #9#1 = cue;     || cuenta
     #9#2 = niv;     || digito
     #9#3 = 0;       || Importe;
     #9#4 = tpiva;   || % iva
     #9#5 = tprec;   || % rec.
     #9#6 = concep;  || concepto.
     #9#7 = "";      || descripcion;
     #9#8 = nNume2;
     |GRABA 020#9b;
 |FINSI;
 #9#3 = #9#3 + imp;
 #9#7 = descr;
 |SI tpiva ! #9#4; |O tprec ! #9#5; || quitando esto dejamos que grabe la
     #9#4 = -1; #9#5 = -1;          || misma cuenta segun el %IVA.
 |FINSI;                            || As¡, agrupamos las cuentas en apunte.
 |GRABA 020#9a;
 |LIBERA #9;
|FPRC;
