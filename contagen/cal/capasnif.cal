|FICHEROS;
   capasnif #0;
   caclipro #1;

   canempre #30;
   caselem1 #998;

   &regisnif@ #709;
   :/basica/def/agim0003  #710;
   :/integral/def/dsam0103 #711;

   &poblaci@  #703;
   &sgpaises@ #718;
|FIN;

|VARIABLES;
 &entrada  = 1;
 aDirFich  = "";
 aAlfa1    = "";
 aAlfa2    = "";
 aZDClPr   = "";
 aZHClPr   = "";
 aDClPr    = "";
 aHClPr    = "";
 aParam    = "";
 aDirec    = "";
 aDirec0   = "";
 aDirec1   = "";
 aDirec2   = "";
 nSw       = 0;
 nEstado   = 0;
|FIN;

|INCLUYE dscont_i;

|CALCULO cuenta; |TIPO 0;
     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;
|FCAL;
|| --------------------------------------------------
|PROCESO LaDivision;
     aDirec0 = "";
     aDirec1 = aDirec;
     aDirec2 = "";

     nSw    = 0;
     aAlfa1 = aDirec % 103;
     |SI aAlfa1 = "CL ";  nSw = 1; |FINSI;
     |SI aAlfa1 = "AV ";  nSw = 1; |FINSI;
     |SI aAlfa1 = "PA ";  nSw = 1; |FINSI;
     |SI aAlfa1 = "PZ ";  nSw = 1; |FINSI;
     |SI aAlfa1 = "PL ";  nSw = 1; |FINSI;
     |SI aAlfa1 = "CR ";  nSw = 1; |FINSI;
     |SI nSw = 1;
         aDirec0 = aAlfa1;
         aDirec1 = aDirec % 440;
     |FINSI;
|FPRC;

|PROCESO CreaFicNif;
    eaCodNif = #1#9; |QBF eaCodNif;
    |SI eaCodNif = ""; |ACABA; |FINSI;

    #709#0 = eaCodNif;
    |LEE 141#709=;
    nEstado = FSalida;
    |SI nEstado ! 0; |O #0#5 = "S";

        |SI nEstado ! 0;
            |DDEFECTO #709;
        |FINSI;


        #709#0  = eaCodNif;   || Nif
        #709#1  = #1#1;       || Nombre y Apellidos

        aDirec  = #1#2;
        |HAZ LaDivision;
        #709#2  = aDirec1;    || Direccion
        #709#3  = aDirec2;    || No tengo el numero

        #709#4  = #1#3;       || Codigo Provincia
        #709#5  = #1#4;       || Codigo Poblacion
        #709#6  = #1#6;       || Poblacion
        #709#7  = #1#5;       || Provincia
        #709#8  = #1#7;       || Telefono
        #709#17 = #1#8;       || Fax
        #709#9  = aDirec0;    || SG
                              || #709#10 =  Piso
                              || #709#11 =  Puerta
                              || #709#16 =  Escalera
        |SI #1#24 = "   "; #1#24 = "ES "; |FINSI;
        #709#12 = #1#24;      || Codigo Pais
        eaCodPais = #1#24;
        |HAZ LeePais;
        #709#13 = #718#1;     || Nombre Pais
                              || #709#14 = Codigo Comunidad
                              || #709#15 = Nombre Comunidad
        |SI enSwDeDonde = 1;
            enCodProv = #1#3;
            enCodPobl = #1#4;
            enSwMensa = 1;
            |HAZ LeeLocalidad;
            enSwMensa = 0;
            #709#14 = #703#4;
            #709#15 = #703#5;
        |FINSI;


        |SI nEstado ! 0; |GRABA 020#709n; |FINSI;
        |SI nEstado = 0; |GRABA 020#709a; |FINSI;
    |FINSI;

    |LIBERA #709;
|FPRC;

|DEFBUCLE LeeClieProv;
 #1#1;
 ;
 aZDClPr, aDClPr;
 aZHClPr, aHClPr;
 e;
 NULL, CreaFicNif;
|FIN;


|PROCESO HazTodo;

   aZDClPr = #0#0;
   aZHClPr = #0#0;
   |SI #0#0 = "T"; aZDClPr = "C"; aZHClPr = "P"; |FINSI;

   aDClPr = #0#3;
   aHClPr = #0#4;


   |ABRE #709;
   |BUCLE LeeClieProv;
   |CIERRA #709;

|FPRC;

|CALCULO Tipo3; |TIPO 3;

       |SI #48#27 = "S";
           |HAZ MultiEmpresa;
       |SINO;
           |DFICE dirfich0; |QBF dirfich0;
           |HAZ HazTodo;
       |FINSI;
|FCAL;

|| **************************************************************************
|PROGRAMA;

   aParam = PARAMETRO; |QBF aParam;
   |SI aParam ! "";

       #0#0 = "T";
       #0#1 = enEmpresa;
       #0#2 = enPeriodo;
       #0#3 = "            ";
       #0#4 = "zzzzzzzzzzzz";
       #0#5 = "N";

       |DBASE aDirFich; |QBF aDirFich;
       aAlfa1 = ("00000" + enEmpresa) % -105;
       aAlfa2 = ("0" + enPeriodo)     % -101;

       aDirFich = aDirFich + "fich/"+ aAlfa1 + aAlfa2 + "/";

       |ABRE #30;
       #30#2 = enEmpresa;
       #30#3 = enPeriodo;
       |LEE 101#30=;
       |SI FSalida ! 0;
           |CIERRA #30;
           |ACABA;                  || Si no existe no puedo seguir
       |FINSI;
       |CIERRA #30;
       |PATH_DAT #1  aDirFich;
   |SINO;

       |HAZ inicio;
       #0#1 = enEmpresa;
       #0#2 = enPeriodo;

   |FINSI;

   ewNif = 0;
   enSwDeDonde = -1;
   |HAZ DirAplicSt;
   |SI HayAcceso = 1;
       enSwDeDonde = 1;
       |REFERENCIA_DEF regisnif@, #711;
   |SINO;
       |SI Haybasica = 1;
           enSwDeDonde = 0;
           |REFERENCIA_DEF regisnif@, #710;
       |FINSI;
   |FINSI;

   |SI enSwDeDonde = -1; ewNif = 1; |ACABA; |FINSI; || no existe Basica ni Integrada

   |SI aParam = "";
       |CLS;
       |PINPA #0#0;
       |PINDA #0#0;
       |ENDATOS #1#0;
   |SINO;
       dirfich0 = aDirFich;
       |HAZ HazTodo;
   |FINSI;
|FPRO;

|| *************************************
|PROCESO MultiEmp1;
   cod1 = #caselem1#1;
   cod2 = #caselem1#2;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   dirfich0 = #caselem1#3;
   |QBF dirfich0;
   |PATH_DAT #1  dirfich0;

   |ATRI N; |CUADRO 1508 1850;
   |ATRI R; |PINTA 1609, "Empresa: ";      |ATRI 0;
   |PINTA 1618, #30#2; |PINTA 1709, #30#1; |ATRI 0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #caselem1#1;
 ;
 001;
 999;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "caselemp";
  |NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;
