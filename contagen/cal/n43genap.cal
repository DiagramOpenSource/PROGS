|FICHEROS;
    n43genap #0;       || Este Def.

    camaasil #1;       || Apuntes.

    cacuenta #2;       || Cuentas Contables.
    n43datos #3;       || Datos Importados.

    n43tempw #4,MANTE; || Temporal Datos Importados.
    n43tempa #5,MANTE; || Temporal Asiento.
    n43tempv #6,MANTE; || contrapartidas varias
    n43tempc #7,MANTE; || Pide Cuenta..

    caextrp4 #999;

    cientasc #50;
    cisalant #60;

    :/modelos/def/dsmom030 #333;     || Conceptos Contab.

    espejo@ #900;
    espej2@ #902;

    n43temp3 #993;
    n43temp4 #994,MANTE;
|FIN;

|VARIABLES;
    &entrada  = 1;
    men2      = "     Longitud de Cuenta Fuera de Nivel !";
    vacio     = "            ";
    aGuarda   = "";
    aFichero  = "";
    nInkey    = 0;
    nInkey7   = 0;
    &nSaldod  = 0;
    &nSaldoh  = 0;
    &nSaldot  = 0;
    nConta    = 0;
    aTitulo   = "";
    &dpant    = "";
    &sdant    = "";
    &fecapu   = @;
    aAlfa1    = "";
    nNume1    = 0;
    nNume2    = 0;
    avisos    = ""
    aDeha     = "";
    nImpo     = 0;
    aDocu     = "";
{-1}menu = "";
    menu1 = "2400";
    menu2 = "1";
    menu3 = "";
    menu4 = "RCS";
    menu5 = " Revisar ";
    menu6 = " Contabilizar ";
    menu7 = " Salir ";
    opcion   = 0;

   &eaCta43  = "";
   &enTip43  = 0;
   &enDia43  = 0;
   nUltimo   = 0;
   nOpe994   = 0;
   nOpera    = 0;
   nRegis    = 0;
   &eaMarca  = "";
   &enModo   = 0;
   &eaAlfa1  = "";
   aAlfa2    = "";
   nSwTodos  = 0;
   aMas      = "";
   nLinea    = 0;
   nContador = 0;
   nSw       = 0;

   nMFecha   = 0;
   nCFecha   = 0;
   &efDFecha = @;
   &efHFecha = @;
   aPunteado = "";
   nSw994    = 0;


   nDiario  = 0;
   fFecha   = @;
   nDocum   = 0;
   nApunte  = 0;
   aDCuenta = "";
   aHCuenta = "";
   nDConcep = 0;
   nHConcep = 999;
   nSwTempc7 = 0;
   nEstado   = 0;
   aSigno    = "";

   aCampo0   = "";
   nCampo1   = 0;
   nCampo4   = 0;
   aCampo5   = "";
   aCampo6   = "";
   nCampo7   = 0;
   nCampo8   = 0;
   aCampo9   = "";

|FIN;

|INCLUYE dscont_i;

|| ................Proceso n43genap
|PROCESO cuenta;|TIPO 6;
    |SI #0Campo ! vacio;
        eaCuenta = #0Campo;
        salidas  = FSalida;
        |HAZ FiltraPuntos;
        #0Campo = eaCuenta; |PINTA #0Campo;
    |SINO;

        |ABRE #3;
        |LEE 040#3u;
        |SI FSalida = 0;
            #0Campo = #3#1; |PINTA #0Campo;
        |SINO;
            |CIERRA #3;
            |MENAV "    No existen Datos";
            |ERROR;
            |ACABA;
        |FINSI;
        |CIERRA #3;
   |FINSI;

   eaCuenta = #0#0;
   |HAZ SacaNivel; #0#1 = enNivel;
   |SI eswCta = 1;
       |MENAV men2;
       |ERROR;
   |SINO;
       |ABRE #60;
       |DDEFECTO #60;
       #60#0 = #0#0;
       |LEE 040#60=;
       |SI FSalida ! 0; |DDEFECTO #60; |FINSI;
       |CIERRA #60;

       |HAZ LeeCuenta;
       aTitulo = eaNomCta;
  |FINSI;

|FPRC;

|| ......................................................

|PROCESO LeoAuxTempC;
   #7#15 = #4#1;
   #7#16 = aSigno;
   |LEE 041#7=;
   |SI FSalida ! 0;
       |DDEFECTO #7;
       #7#15 = #4#1;
       #7#16 = aSigno;

       #7#0  = #60#5;
       #7#1  = #60#6;
       #7#4  = #60#8;
       #7#5  = #60#10;
       eaAlfa1 = #60#10; |QBF eaAlfa1;
       |GRABA 020#7c;
  |FINSI;
|FPRC;

|PROCESO LeoAuxTempCX;
   #7#15 = #4#1;
   #7#16 = "X";
   |LEE 041#7=;
   |SI FSalida ! 0;

       aSigno = "D"; |HAZ LeoAuxTempC;
       aCampo0 = #7#0;
       nCampo1 = #7#1;
       nCampo4 = #7#4;
       aCampo5 = #7#5;
       aSigno = "H"; |HAZ LeoAuxTempC;
       aCampo6 = #7#0;
       nCampo7 = #7#1;
       nCampo8 = #7#4;
       aCampo9 = #7#5;

       |DDEFECTO #7;
       #7#15 = #4#1;
       #7#16 = "X";
       #7#0  = aCampo0;
       #7#1  = nCampo1;
       #7#4  = nCampo4;
       #7#5  = aCampo5;
       #7#6  = aCampo6;
       #7#7  = nCampo7;
       #7#8  = nCampo8;
       #7#9  = aCampo9;

       |GRABA 020#7c;
  |FINSI;
|FPRC;

|PROCESO GrabaAuxTempC;
  |GRABA 020#7a;
  |LIBERA #7;
|FPRC;

|PROCESO ElMarcado;
  |SI nOpe994 = 1;
      #902#11 = " ";
      |GRABA 020#902a;
  |FINSI;

  |SI nOpe994 = 0;

      eaMarca   = "*";
      aPunteado = "û";
      |SI nRegis = 0;
          nConta = nConta + 1;
          nRegis = nConta;
      |FINSI;

      #5#0 = #0#0;
      #5#1 = #0#1;
      #5#2 = nRegis;
      |LEE 041#5=;
      nEstado = FSalida;

      #5#0 = #0#0;
      #5#1 = #0#1;
      #5#2 = nRegis;
      #5#3 = #4#2;
      #5#4 = #7#4;
      #5#5 = #7#5;
      #5#6 = #4#6;
      #5#7 = #4#7;
      #5#8 = #994#4;
      #5#9 = #994#5;
      #5#10 = #7#4;
      #5#11 = #7#5;
      #5#12 = dpant;
      #5#13 = sdant;
      #5#16 = aPunteado;

      nSaldod = #5#7;
      nSaldoh = #5#7;
      nSaldot = 0;
      fecapu = #4#2;
      |SI nEstado = 0; |GRABA 020#5a; |FINSI;
      |SI nEstado ! 0; |GRABA 020#5n; |FINSI;

      |HAZ Alta993;
  |FINSI;
  |LIBERA #902;
|FPRC;

|DEFBUCLE ElMarcado;
  #902#1004;
  11;
  01, fec1, 000001, 0001, "*";
  98, fec2, 999999, 9999, "*";
  ;
  NULL, ElMarcado;
|FIN;

|PROCESO Baja993;
   |SELECT #1#993;
   #993#0 = #4#0;
   |LEE 041#993=;
   |SI FSalida = 0;
       |BORRA 020#993a;
   |FINSI;
|FPRC;

|PROCESO Alta993;
   |SELECT #1#993;
   #993#0 = #4#0;
   |LEE 041#993=;
   |SI FSalida ! 0;
       |DDEFECTO #993;
       #993#0 = #4#0;
       |GRABA 020#993c;
   |FINSI;
   #993#1 = nRegis;
   #993#2 = #994#0;
   #993#3 = #994#1;
   #993#4 = #994#2;
   #993#5 = #994#3;
   |GRABA 020#993a;
   |LIBERA #993;
|FPRC;

|PROCESO Puntear;
  #1#0 = #993#2;
  #1#1 = #993#3;
  #1#2 = #993#4;
  #1#3 = #993#5;
  |LEE 141#1=;
  |SI FSalida = 0;
      #1#18 = "û";
      |GRABA 020#1a;
  |FINSI;
  |LIBERA #1;
|FPRC;

|DEFBUCLE puntear;
  #993#1;
  ;
  000001;
  999999;
  ;
  NULL, Puntear;
|FIN;

|PROCESO LoMarco; |TIPO 0;
   |SI FSalida = 0;

       |LEE 041#994=;

       |SI #994#11 = "*";
           #994#11 = " ";
       |SINO;
           nOpe994 = 1; |BUCLE ElMarcado;
           #994#11 = "*";
       |FINSI;
       |PINTA #994#11;
       |GRABA 020#994a;

       |PONCONTROL 23, "SI";
  |FINSI;
|FPRC;

|PROCESO BuscaAstos;

   |DDEFECTO #993;
   #993#2 = #1#0;
   #993#3 = #1#1;
   #993#4 = #1#2;
   #993#5 = #1#3;
   |LEE 040#993=;
   |SI FSalida = 0; |Y #993#0 ! #4#0;
       |ACABA;
   |FINSI;

   |SI #1#4 = #4#1; |ACABA; |FINSI;

   #994#0 = #1#0;
   #994#1 = #1#1;
   #994#2 = #1#2;
   #994#3 = #1#3;
   #994#4 = #1#4;
   #994#5 = #1#5;
   #994#6 = #1#6;
   #994#7 = #1#7;
   #994#8 = #1#8;
   #994#9 = #1#9;
   eaCuenta = #1#4; |HAZ cuentaN;
   #994#10 = #cacuenta#2;

   #994#11 = " ";
   |SI #993#0 = #4#0;
       nDiario = #1#0;
       fFecha  = #1#1;
       nDocum  = #1#2;
       nApunte = #1#3;
       #994#11 = "*";
   |FINSI;

   #994#12 = #1#12;
   #994#13 = #1#13;
   #994#14 = #1#14;
   #994#15 = #1#15;
   #994#16 = #1#21;
   #994#17 = #1#28;

   |GRABA 040#994n;
   nSw994 = 1;
|FPRC;

|DEFBUCLE BuscaAstos;
    #1#3;
    0, 6, 8, 9, 18;
    aDCuenta, fec1, 000001, 0001, 01, nDConcep, #4#6, #4#7, " ";
    aHCuenta, fec2, 999999, 9999, 98, nHConcep, #4#6, #4#7, " ";
    e;
    NULL, BuscaAstos;
|FIN;

|PROCESO inf994;
   |SI FSalida = "POSICION";
       #994#0 = nDiario;
       #994#1 = fFecha;
       #994#2 = nDocum;
       #994#3 = nApunte;
   |SINO;
       #994#0 = 01;
       #994#1 = fec1;
       #994#2 = 000001;
       #994#3 = 001;
   |FINSI;
|FPRC;

|PROCESO sup994;
   #994#0 = 98;
   #994#1 = fec2;
   #994#2 = 999999;
   #994#3 = 999;
|FPRC;

|PROCESO BuscaDiario;
    |DBASE aMas;
    aMas = aMas + "def/n43temp4";
    |CARGA_DEF aMas, espej2@;
    |SI FSalida ! 0;
       |MENAV "    Error Cargado " + aMas;
       |ACABA;
    |FINSI;

    aFichero = ("4x" + Usuario) % 108;
    |NOME_DAT #994 aFichero; |DELINDEX #994;
    |NOME_DAT #902 aFichero;

    nDiario = 1;
    fFecha  = fec1;
    nDocum  = 1;
    nApunte = 1;

    nSw994 = 0;
    |ABRE #993;
    |ABRE #994;
    |SELECT #3#993;
    |BUCLE BuscaAstos;
    |CIERRA #994;

    |SI nSw994 = 0;
        |MENAV "0000No existen movimientos con ese Importe";
    |SINO;
        |HAZ Baja993;
        |PUSHV 13012380;
        |PINPA #0#994;
        |ENTLINEAL #1#994, -4 , 4 , 22 , 0 , inf994 , sup994;
        |POPV;
    |FINSI;

    aPunteado = "";
    eaMarca   = "";
    nOpe994   = 0; |BUCLE ElMarcado;

    |SI eaMarca = ""; |Y nRegis ! 0;
        #5#0 = #0#0;
        #5#2 = nRegis;
        |LEE 041#5=;
        |SI FSalida = 0;
            |BORRA 020#5a;
        |FINSI;
        nRegis = 0;
    |FINSI;
    |CIERRA #993;

    |DESCARGA_DEF #espej2@;
    |DELINDEX #994;
|FPRC;

|PROCESO cuentac;|TIPO 6;

    |C_M #7Campo, 0, aAlfa1;
    |SI aAlfa1 = "N"; |ACABA; |FINSI;

    |SI FSalida = 18;  #7Campo = #0#0;  |FINSI;

    |SI #7Campo = vacio;
        |SI nSwTempc7 = 0;
            |MENAV "    Introduzca una Cuenta";
            |ERROR;
        |FINSI;
        |ACABA;
    |FINSI;

    aGuarda = #0#0;
    eaCuenta = #7Campo;
    salidas  = FSalida;
    |HAZ FiltraPuntos;
    #7Campo = eaCuenta; |PINTA #7Campo;

    |SI nSwTempc7 ! 0; |ACABA; |FINSI;

    #0#0 = aGuarda;
    eaCuenta = #7Campo;
    |HAZ SacaNivel;
    nNume1   = Campo + 1;
    #7nNume1 = enNivel;
    |SI eswCta = 1;
        |MENAV men2;
        |ERROR;
    |SINO;
        emCta = ;
        |HAZ LeeCuenta;
        |SI eswCta = 2;
            |MENAV "0000Esta Cuenta NO tiene Pase Directo."
            |ERROR;
        |FINSI;
        emCta = 0;
    |FINSI;
|FPRC;

|CALCULO TrasConcepto7;  |TIPO 0;
   |C_M #7Campo, 0, aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   |SI FSalida = 10;
       enConcepto = #7Campo;
       |HAZ VerConcepto;
       #7Campo = enConcepto; |PINTA #7Campo;
   |FINSI;

   |SI nSwTempc7 = 0; |O Campo = 4;
       enFilConc = 0;
       eaCuenta   = #7#0;
       enConcepto = #7Campo;
       |HAZ FiltroConcepto;  |SI eswLect ! 0; |ACABA; |FINSI;

       eaAlfa1  = #dsmom030#1; |QBF eaAlfa1;
       |SI eaAlfa1 ! "";  eaAlfa1 = eaAlfa1 + " ";  |FINSI;
       nNume1   = Campo + 1;
       |SI nSwTodos = 0;
           #7nNume1 = eaAlfa1 + #4#5;
       |SINO;
           #7nNume1 = #dsmom030#1;
       |FINSI;
       |PINTA #7nNume1;
   |FINSI;
|FCAL;

|CALCULO PonAlFinal; |TIPO 7;
    |C_M #7Campo, 0, aAlfa1;
    |SI aAlfa1 = "N"; |ACABA; |FINSI;

   |PTEC 816;
|FCAL;

|CALCULO Tipo12tempc; |TIPO 12;
|FCAL;

|| /////////////////////////////////////////////////////
|PROCESO ModiPan_2;
   |SI eaSubde = "N";
        |C_A #999#6, 1, 29;
        |C_P #999#7, 1, 1266;
        |C_V #999#16,1, "N";
        |PINTA 0961, "ÄÄÄÄÂÄÄ";
        |PINTA 1061, "    ³  ";
        |PINTA 1161, "ÄÄÄÄÅÄÄ";
        |PINTA 1261, "    ³  ";
        |PINTA 1361, "    ³  ";
        |PINTA 1461, "    ³  ";
        |PINTA 1561, "    ³  ";
        |PINTA 1661, "    ³  ";
        |PINTA 1761, "    ³  ";
        |PINTA 1861, "    ³  ";
        |PINTA 1961, "    ³  ";
        |ATRI R; |PINTA 1061, "    "; |PINTA 1066, "Dp"; |ATRI 0;
   |SINO;
        |SI enSubde = 1; |Y eaDepar = "N";
            |C_A #999#6, 1, 28;
            |C_V #999#7, 1, "N";
            |PINTA 0961, "Ä";
            |ATRI R; |PINTA 1061, "   "; |ATRI 0;
            |PINTA 1161, " ";
            |PINTA 1261, " ";
            |PINTA 1361, " ";
            |PINTA 1461, " ";
            |PINTA 1561, " ";
            |PINTA 1661, " ";
            |PINTA 1761, " ";
            |PINTA 1861, " ";
            |PINTA 1961, " ";
            |PINTA 2061, " ";
        |FINSI;
   |FINSI;
|FPRC;

|PROCESO mini;
   #999#0 = 1;
|FPRC;

|PROCESO maxi;
   #999#0 = 9999;
|FPRC;

|PROCESO cuentaN;
  |ABRE #2;
  |DDEFECTO #2;
  #2#0 = eaCuenta;
  |LEE 041#2=;
  |CIERRA #2;
|FPRC;

|PROCESO LasContra;
       nLinea = nLinea + 1;
       eaCuenta = #6#4; |HAZ cuentaN;
       #999#00 = nLinea;      || apunte
       |SI #900#6 = "D";
           #999#03 = #6#4;    || cuenta DEBE
           #999#04 = #6#5;     || digito DEBE
           #999#01 = "";
           #999#02 = "";
       |SINO;
           #999#01 = #6#4;       || cuenta HABER
           #999#02 = #6#4;       || digito HABER
           #999#03 = "";
           #999#04 = "";
       |FINSI;
       #999#05 = #6#6;        || concepto
       #999#06 = #6#7;        || descripcion
       #999#08 = #6#8;        || importe
       #999#09 = #cacuenta#2;        || descripcion
       #999#10 = #cacuenta#51;       || debe
       #999#11 = #cacuenta#52;       || haber
       #999#12 = #cacuenta#53;       || saldo
       |GRABA 020#999n;
|FPRC;

|DEFBUCLE LasContra;
   #6#1;
   ;
   #5#0, #5#2, 1;
   #5#0, #5#2, 99999;
   ;
   NULL,LasContra;
|FIN;

|PROCESO lee_asiento1;             || CARGA LA PANTALLA DEL ASIENTO COMPLETO

   #5#0 = #0#0;
   #5#1 = #0#1;
   #5#2 = #900#15;
   |LEE 041#5=;
   |SI FSalida ! 0;
       |ACABA;
   |FINSI;

   eaCuenta = #0#0; |HAZ cuentaN;

   |DDEFECTO #999;
   nLinea = nLinea + 10;
   #999#00 = nLinea;      || apunte
   |SI #900#6 = "D";
       #999#01 = #0#0;    || cuenta DEBE
       #999#02 = #0#1;     || digito DEBE
   |SINO;
       #999#03 = #0#0;       || cuenta HABER
       #999#04 = #0#1;       || digito HABER
   |FINSI;

   #999#05 = #5#4;        || concepto
   #999#06 = #5#5;        || descripcion
   #999#07 = #5#12;       || departamento
   #999#08 = #900#7;        || importe
   #999#09 = #cacuenta#2;        || descripcion
   #999#10 = #cacuenta#51;       || debe
   #999#11 = #cacuenta#52;       || haber
   #999#12 = #cacuenta#53;       || saldo
   #999#13 = #7#3;        || diario
   #999#14 = #5#3;        || fecha
   #999#15 = nContador;   || documento
   #999#16 = #5#13;       || Subdepartamento
   |GRABA 020#999n;

   aAlfa1 = #5#8; |QBF aAlfa1;
   |SI aAlfa1 ! "";
       nLinea = nLinea + 1;
       eaCuenta = #5#8; |HAZ cuentaN;
       #999#00 = nLinea;      || apunte
       |SI #900#6 = "D";
           #999#03 = #5#8;    || cuenta DEBE
           #999#04 = #5#9;     || digito DEBE
           #999#01 = "";
           #999#02 = "";
       |SINO;
           #999#01 = #5#8;       || cuenta HABER
           #999#02 = #5#9;       || digito HABER
           #999#03 = "";
           #999#04 = "";
       |FINSI;
       #999#09 = #cacuenta#2;        || descripcion
       #999#10 = #cacuenta#51;       || debe
       #999#11 = #cacuenta#52;       || haber
       #999#12 = #cacuenta#53;       || saldo
       |GRABA 020#999n;
   |SINO;
       |BUCLE LasContra;
   |FINSI;
|FPRC;

|DEFBUCLE lee_asiento;
   #900#2001;
   14;
   000001, "*";
   999999, "*";
   e;
   NULL, lee_asiento1;
|FIN;

|PROCESO VerApuntes;

   aFichero = ("q" + Usuario);
   aFichero = (aFichero + "zzzzzzzz") % 108;
   |NOME_DAT #999 aFichero; |DELINDEX #999;

   |HAZ contador;
   nContador = nDOCUMENTO;

   nLinea = -9;
   |ABRE #5;
   |ABRE #999;
   |BUCLE lee_asiento;
   |CIERRA #999;
   |CIERRA #5;

   |SI nLinea > 0;
      |PUSHV 07012380;
      |PINPA #0#999; |HAZ ModiPan_2;
      |ENTLINEAL #1#999, -1, 4, 20, 0, mini, maxi;
      |POPV;
   |SINO;
      |MENAV "    Seleccion vacia ...";
   |FINSI;
   |DELINDEX #999;
|FPRC;

|| /////////////////////////////////////////////////////
|PROCESO leeselec_no
         |SI nOpera = 1;
             nSw = 1;
             |ERROR10;
         |SINO;
             nConta = nConta + 1;
             #5#0 = #0#0;
             #5#1 = #0#1;
             #5#2 = nConta;
             #5#3 = #4#2;
             |SI #4#6 = "D";
                 eaAlfa1 = #7#5; |QBF eaAlfa1;
                 #5#4  = #7#4;
                 #5#8  = #7#0;
                 #5#9  = #7#1;
                 #5#10 = #7#4;
             |SINO;
                 eaAlfa1 = #7#9; |QBF eaAlfa1;
                 #5#4  = #7#8;
                 #5#8  = #7#6;
                 #5#9  = #7#7;
                 #5#10 = #7#8;
             |FINSI;
             |SI eaAlfa1 ! "";  eaAlfa1 = eaAlfa1 + " ";  |FINSI;
             |SI #7#10 = "S";
                 #5#5  = eaAlfa1 + #4#5;
                 #5#11 = eaAlfa1 + #4#5;
             |SINO;
                 #5#5  = eaAlfa1;
                 #5#11 = eaAlfa1;
             |FINSI;

             #5#6 = #4#6;
             #5#7 = #4#7;
             #5#12 = dpant;
             #5#13 = sdant;
             #5#16 = "";
             |GRABA 020#5n;
             nSaldod = #5#7;
             nSaldoh = #5#7;
             nSaldot = 0;
             fecapu = #4#2;

             #4#15 = nConta;
             #4#14 = "*";
             |GRABA 000#4a;
         |FINSI;
|FPRC;

|DEFBUCLE leeselec_no;
        #4#2;
        14;
        000000," ";
        999999," ";
        ;
        NULL,leeselec_no;
|FIN;

|PROCESO leeselec;
         |SI nOpera = 1;
             |SI nConta < #4#15; nConta = #4#15; |FINSI;
             |ACABA;
         |FINSI;

         nConta = nConta + 1;
         #5#0 = #0#0;
         #5#1 = #0#1;
         #5#2 = nConta;
         #5#3 = #4#2;
         #5#4 = #7#4;
         #5#5 = #7#5;
         #5#6 = #4#6;
         #5#7 = #4#7;
         #5#8 = #7#0;
         #5#9 = #7#1;
         #5#10 = #7#4;
         #5#11 = #7#5;
         #5#12 = dpant;
         #5#13 = sdant;
         #5#16 = aPunteado;
         |GRABA 020#5n;
         nSaldod = #5#7;
         nSaldoh = #5#7;
         nSaldot = 0;
         fecapu = #4#2;
|FPRC;

|DEFBUCLE leeselec;
        #4#2;
        14;
        000000,"*";
        999999,"*";
        ;
        NULL,leeselec;
|FIN;


|PROCESO desmarca;
   |ABRE #3;
   #3#0 = #4#0;
   |LEE 101#3=;
   #3#9 = "S";
   #3#11 = "S";
   |GRABA 020#3a;
   |CIERRA #3;

   |BORRA 041#4a;
|FPRC;

|DEFBUCLE desmarca;
   #4#2;
   14;
   000001,"*";
   999999,"*";
   ;
   NULL,desmarca;
|FIN;


|PROCESO contabiliza;
     |HAZ LeoAuxTempCX;

     nSwTempc7 = 0;
     |C_V #7#0, 1, "N"; |C_M #7#0, 1, "N";
     |C_V #7#4, 1, "N"; |C_M #7#4, 1, "N";
     |C_V #7#5, 1, "N"; |C_M #7#5, 1, "N";

     |C_V #7#2, 1, "S"; |C_M #7#2, 1, "S";
     |C_V #7#3, 1, "S"; |C_M #7#3, 1, "S";

     |PUSHV 10051960;
     |BLANCO 10051457;
     |PINTA 1106, " Diario:                                             ";
     |PINTA 1206, " Tipo Actualizacion:    1- Cada linea un Documento   ";
     |PINTA 1306, "                        2- Agrupando por fechas      ";
     |CUADRO 10051457;

     |PINDA #0#7;
     |ENDATOS #2#7;
     |C_V #7#0, 1, "S"; |C_M #7#0, 1, "S";
     |C_V #7#4, 1, "S"; |C_M #7#4, 1, "S";
     |C_V #7#5, 1, "S"; |C_M #7#5, 1, "S";

     |C_V #7#2, 1, "N"; |C_M #7#2, 1, "N";
     |C_V #7#3, 1, "N"; |C_M #7#3, 1, "N";

     |CONFI "    SConforme S/N";
     |SI FSalida = 0;
         eaCta43 = #0#0;
         enTip43 = #7#2;
         enDia43 = #7#3;

         aSigno = "X";
         |HAZ  GrabaAuxTempC;

         aFichero = ("T3" + Usuario) % 108;
         |NOME_DAT #50 aFichero; |DELINDEX #50;

         |ABRE #50;
         |DDEFECTO #50;
         #50#0 = #0#0;
         #50#1 = #0#1;
         #50#2 = aTitulo;
         |GRABA 020#50n;
         |CIERRA #50;

         |SUB_EJECUTA "ciactasc;n43";

         |BUCLE desmarca;
         |ABRE #1;
         |BUCLE puntear;
         |CIERRA #1;
     |SINO;
         |MENAV "    Registros no Contabilizados";
     |FINSI;
     |POPV;
|FPRC;

|PROCESO asiento_Todos;
        nSw    = 0;
        nOpera = 1;
        |BUCLE leeselec_no;
        nOpera = 0;
        |SI nSw = 0;
            |MENAV "    Marcados Todos los Movimiento.";
            |ACABA;
        |FINSI;

        |HAZ LeoAuxTempCX;
        nSwTempc7 = 0;
        |C_V #7#6,  1, "S"; |C_M #7#6,  1, "S";
        |C_V #7#8,  1, "S"; |C_M #7#8,  1, "S";
        |C_V #7#9,  1, "S"; |C_M #7#9,  1, "S";
        |C_V #7#10, 1, "S"; |C_M #7#10, 1, "S";

        |PUSHV 14052361;
        |BLANCO 14052361;
        |PINPA #0#7;

        |ATRI R; |PINTA 1506, " Datos de las Contrapartidas para los Asientos        "; |ATRI 0;
        |ATRI P; |PINTA 1606, " Movimientos al DEBE                                  "; |ATRI 0;
                 |PINTA 1706, " Cuenta ...:                                          ";
                 |PINTA 1806, " Conceptos.:                                          ";
        |ATRI P; |PINTA 1906, " Movimientos al HABER                                 "; |ATRI 0;
                 |PINTA 2006, " Cuenta ...:                                          ";
                 |PINTA 2106, " Conceptos.:                                          ";
        |ATRI S; |PINTA 2206, " Incorporar el Concepto del Banco:                    "; |ATRI 0;
        |CUADRO 14052360;

/*
        #7#0 = #60#5;
        #7#1 = #60#6;
        #7#4 = #60#8;
        #7#5 = #60#10;
*/
        eaAlfa1 = #7#5; |QBF eaAlfa1;
        |SI #7#8 = 0;
            #7#6 = #60#5;
            #7#7 = #60#6;
            #7#8 = #60#8;
            #7#9 = #60#10;
        |FINSI;

        |PINDA #0#7;

        |ENDATOS #2#7;
        |C_V #7#6,  1, "N";  |C_M #7#6, 1, "N";
        |C_V #7#8,  1, "N";  |C_M #7#8, 1, "N";
        |C_V #7#9,  1, "N";  |C_M #7#9,  1, "N";
        |C_V #7#10, 1, "N";  |C_M #7#10, 1, "N";
        |SI FSalida ! 0;
            |POPV;
            |ACABA;
        |FINSI;

        aAlfa2 = #7#5;
        #60#5  = #7#0;
        #60#6  = #7#1;
        #60#8  = #7#4;
        #60#10 = eaAlfa1;
        #7#5   = eaAlfa1;
        |HAZ GrabaAuxTempC;
        #7#5 = aAlfa2;

        |POPV;
        |ABRE #5;
        |BUCLE leeselec_no;
        |CIERRA #5;
        nRegis = nConta;
        eaMarca = "*";
|FPRC;

|RUTINA infa5;
     #5#0 = #0#0;
     #5#1 = #0#1;
     #5#2 = nRegis;
|FRUT;

|RUTINA supa5;
     #5#0 = #0#0;
     #5#1 = #0#1;
     #5#2 = nRegis;
|FRUT;

|PROCESO asiento;
    nSwTempc7 = 0;
    |SI nRegis = 0;
        aSigno = #4#6;
        |HAZ LeoAuxTempC;

        |PUSHV 15051961;
        |BLANCO 15051961;

        eaAlfa1 = #7#5; |QBF eaAlfa1;
        |SI eaAlfa1 ! "";  eaAlfa1 = eaAlfa1 + " ";  |FINSI;
        #7#5 = eaAlfa1 + #4#5;

        |PINPA #0#7;
        |PINDA #0#7;

        |ENDATOS #2#7;
        |SI FSalida ! 0;
            |POPV;
            |ACABA;
        |FINSI;

        aAlfa2 = #7#5;
        #7#5 = eaAlfa1;
        |HAZ GrabaAuxTempC;
        #7#5 = aAlfa2;

        aPunteado = "";
        |POPV;
        |ABRE #5;
        |HAZ leeselec;
        |CIERRA #5;
        nRegis  = nConta;
        eaMarca = "*";
  |FINSI;
  enModo = 2;
  |ENTLINEAL #1#5, -1 , 2 , 19 , 0 , infa5 , supa5;
|FPRC;

|PROCESO asiento_Concilia;

        aSigno = #4#6;
        |HAZ LeoAuxTempC;

        nSwTempc7 = 1;
        |C_V #7#0,  1, "N"; |C_M #7#0,  1, "N";
        |C_V #7#11, 1, "S"; |C_M #7#11, 1, "S";
        |C_V #7#12, 1, "S"; |C_M #7#12, 1, "S";
        |C_V #7#13, 1, "S"; |C_M #7#13, 1, "S";
        |C_V #7#14, 1, "S"; |C_M #7#14, 1, "S";

        |C_P #7#4,1,1719;
        |C_P #7#5,1,1725;
        |PUSHV 14052361;
        |BLANCO 15052161;

        |PINPA #0#7;
        |ATRI R; |PINTA 1606, " Buscar Asiento No Conciliados en el Diario           "; |ATRI 0;
                 |PINTA 1706, " Concepto .:                                          ";
        |ATRI I; |PINTA 1806, " Seleccion   DESDE                HASTA               "; |ATRI 0;
                 |PINTA 1906, " Cuentas...:                                          ";
                 |PINTA 2006, " Conceptos :                                          ";

        |CUADRO 15052160;

        eaAlfa1 = #60#10; |QBF eaAlfa1;
        |SI nRegis = 0;
            #7#4 = #60#8;
            eaAlfa1 = #60#10; |QBF eaAlfa1;
            |SI eaAlfa1 ! "";  eaAlfa1 = eaAlfa1 + " ";  |FINSI;
            #7#5 = eaAlfa1 + #4#5;
        |SINO;
            #5#0 = #0#0;
            #5#1 = #0#1;
            #5#2 = nRegis;
            |LEE 041#5=;
            #7#4 = #5#4;
            #7#5 = #5#5;
        |FINSI;

        |PINDA #0#7;

        |ENDATOS #2#7;
        |C_V #7#0,  1, "S"; |C_M #7#0,  1, "S";
        |C_V #7#11, 1, "N"; |C_M #7#11, 1, "N";
        |C_V #7#12, 1, "N"; |C_M #7#12, 1, "N";
        |C_V #7#13, 1, "N"; |C_M #7#13, 1, "N";
        |C_V #7#14, 1, "N"; |C_M #7#14, 1, "N";
        |C_P #7#4,1,1819;
        |C_P #7#5,1,1825;
        |SI FSalida ! 0;
            |POPV;
            |ACABA;
        |FINSI;

        aDCuenta = #7#11;
        aHCuenta = #7#12;
        nDConcep = #7#13;
        nHConcep = #7#14;

        aAlfa2 = #7#5;
        #7#5   = eaAlfa1;
        |HAZ GrabaAuxTempC;
        #7#5   = aAlfa2;
        |POPV;

        |ABRE #5;
        |HAZ BuscaDiario;
        |CIERRA #5;

  |SI nRegis ! 0;
      enModo = 2;
      |ENTLINEAL #1#5, -1 , 2 , 19 , 0 , infa5 , supa5;
  |FINSI;
|FPRC;

|| ........................... n43tempw
|PROCESO Tipo7tempw; |TIPO 7;
  |SI #4#14 = "*";
      nRegis = #4#15;
      enModo = 7;
      |ENTLINEAL #1#5, -1 , 7, 19 , 0 , infa5 , supa5;
      |HAZ pinta_cta;
  |SINO;
      nRegis = 0;
      |PINPA #0#5;
  |FINSI;
|FPRC;

|PROCESO tecla; |TIPO 11;
||     |SI FSalida = 1; |Y nInkey ! 10;
||          nInkey = 9;
||     |FINSI;

     |SI FSalida = 9;
         |PTEC 806;
         nInkey = 9;
     |FINSI;

     |SI FSalida = 10;

         |LEE 041#4=;

         |SI #4#14 = "*";
             nRegis = #4#15;
         |SINO;
             nRegis = 0;
             #4#14 = "*";
         |FINSI;
         eaMarca = "*";

         |HAZ asiento;
         #4#15 = nRegis;
         #4#14 = eaMarca;
         |GRABA 000#4a;

          nUltimo = #4#0;
          nInkey = 10;
    |FINSI;

    |SI FSalida = 13;
        |HAZ VerApuntes;
        nInkey = 10;
    |FINSI;

    |SI FSalida = 12;
        nSwTodos = 1;
        |HAZ asiento_Todos;
        nSwTodos = 0;
        nInkey = 10;
    |FINSI;

    |SI FSalida = 11;

         |LEE 041#4=;

         |SI #4#14 = "*";
             nRegis = #4#15;
             eaMarca = "*";
         |SINO;
             nRegis  = 0;
             eaMarca = "";
         |FINSI;

         |HAZ asiento_Concilia;
         #4#15 = nRegis;
         #4#14 = eaMarca;
         |GRABA 000#4a;

         nUltimo = #4#0;
         nInkey = 10;
    |FINSI;
|FPRC;

|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO n43datos;

     |SI #3#2 < fec1; |O #3#2 > fec2;
         |SI nMFecha = 0;
             nMFecha = 1;
             nCFecha = 0;
             |CONFI "1234NHay Movimientos fuera del Ejercicio. Desea Moverlos ahora al Ejercicio correcto ";
             |SI FSalida = 0;
                 nCFecha = 1;
             |FINSI;
             efDFecha = #3#2;
             efHFecha = #3#2;
         |FINSI;
         |SI efDFecha > #3#2; efDFecha = #3#2; |FINSI;
         |SI efHFecha < #3#2; efHFecha = #3#2; |FINSI;
         |ACABA;
     |FINSI;

     #4#0   = #3#0;
     #4#1   = #3#1;
     #4#2   = #3#2;
     #4#3   = #3#3;
     #4#4   = #3#4;
     #4#5   = #3#5;
     #4#6   = #3#6;
     #4#7   = #3#7;
     #4#8   = #3#8;
     #4#9   = #3#9;
     #4#10  = #3#10;
     #4#11  = #3#11;
     #4#12  = #3#12;
     #4#13  = 0;
     #4#14  = "";
     #4#15  = 0;
     |GRABA 000#4n;
     nSw    = 1;
|FPRC;

|DEFBUCLE n43datos;
    #3#2;
    9,11;
    #0#0, 000000, "N", "N";
    #0#0, 999999, "N", "N";
    ;
    NULL, n43datos;
|FIN;

|| //////////////////////
|RUTINA inff;
     #4#1 = #0#0;
     #4#0 = 000001;
     |SI FSalida = "POSICION";
         #4#0 = nUltimo;
     |FINSI;
|FRUT;

|RUTINA supf;
     #4#1 = #0#0;
     #4#0 = 999999;
|FRUT;

|PROCESO cargad;

     nUltimo  = 0;
     aFichero = ("T0" + Usuario) % 108;
     |NOME_DAT #4 aFichero;

     |DBASE aMas;
     aMas = aMas + "def/n43tempw";
     |CARGA_DEF aMas, espejo@;
     |SI FSalida ! 0;
        |MENAV "    Error Cargado " + aMas;
        |ACABA;
     |FINSI;
     |NOME_DAT #900 aFichero;

     aFichero = ("T2" + Usuario) % 108;
     |NOME_DAT #5 aFichero; |DELINDEX #5;

     aFichero = ("T1" + Usuario) % 108;
     |NOME_DAT #6 aFichero; |DELINDEX #6;

     aFichero = ("3x" + Usuario) % 108;
     |NOME_DAT #993 aFichero; |DELINDEX #993;

   |ET enlisi;
      efDFecha = "01.01.0000";
      efHFecha = "01.01.0000";
      nMFecha  = 0;
      nCFecha  = 0;
      nConta   = 0;
      nSw      = 0;
      |DELINDEX #4;

      |ABRE #4;
      |BUCLE n43datos;
      |CIERRA #4;
      |SI nCFecha = 1;
          |SUB_EJECUTA_NP "n43ztr01;Asto";
          |PULSATECLA;
      |FINSI;

      |SI nSw = 0;
          |MENAV "    No existe Datos Importados de esta Cuenta";
          |VETE ASalir;
      |FINSI;

   |ET enli;
      |PINPA #0#4;
      nInkey = 0;
      |ENTLINEAL #1#4, -2 , 4 , 11 , 0 , inff , supf;
      |SI nInkey ! 9; |Y nInkey ! 11; |VETE enli; |FINSI;
      |SI nInkey = 11;
          |VETE enlisi;
      |FINSI;

      |SI nInkey = 9;
          nConta = 0;
          nOpera = 1;
          |BUCLE leeselec;
          nOpera = 0;
          |SI nConta ! 0;
              |CONFI "    SContabilizar (S/N): ";
              |SI FSalida ! 0;
                  |CONFI "1234NSi no contabiliza se perderan los datos introducidos. Es Correcto";
                  |SI FSalida ! 0;
                      |VETE enli;
                  |FINSI;
              |SINO;
                  |HAZ contabiliza;
              |FINSI;
          |FINSI;
      |FINSI;

|ET ASalir;
   |DESCARGA_DEF #espejo@;

   |DELINDEX #4;
   |DELINDEX #5;
   |DELINDEX #6;
   |DELINDEX #993;
|FPRC;

|PROGRAMA;

     |HAZ inicio;
     enFilConc = 1;          || Conceptos sin IVA
     |HAZ CreaConcepto;
     dpant = eaDfDepC;
     sdant = eaDfSubC;
     |HAZ eLeeParametro;

     |CLS;
     |CABEZA "Consolidacion Manual";
     |ABRE #7;
 |ET Vuelve;
     |PINPA #0#0;
     nInkey = 0;
     |ENCAMPO #0#0;
     |SI FSalida = 0;
         |HAZ cargad;
         |VETE Vuelve;
     |FINSI;
     |CIERRA #7;
|FPRO;
