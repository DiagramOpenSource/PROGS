|FICHEROS;
    any00009 #0;       || Informe suma y saldos Analitica
    camaasil #1;

    calicont #31;  ||nuevo
    camaasic #32;  ||nuevos 32 31

    cacuenta #10;

    anm00001 #2;
    anm00004 #3;
    anm00005 #4;       || Selector
    anm00006 #5;

    anw00005 #6;         || trabajo Ctas Analitica
    anw00007 #906;       || trabajo Ctas Analitica

    anm00003 #7;         || Grupos Analiticas
    anm00013 #13;        || Grupos Analiticas  por apuntes
    anm0003@ #107;       || Espejo grupos analitica
|FIN;

|VARIABLES;
  &pagina = 0;
  sw_pagina = 0;

  mensa1 = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
  mensa2 = "    FECHA ERRONEA !!!";
  mensa3 = "    CUENTA ANALITICA ERRONEA !!!";

  error1 = "0000Nivel repetido";
  error2 = "0000No existe informe";
  error3 = "0000El n§ niveles no coincide con el n§ niveles Analitica de la Empresa";
  error4 = "0000Nivel de Rotura erroneo !";

  siete = "0000000";
  cta   = "       ";
  ctab  = "       ";
  nada     = "";
  ctanum   = 0;
  ctaalf   = " ";
  longalf  = " ";
  longnum  = " ";
  nivel    = 0;
  antnivel = 0;
  dos_ant = "xx";

  informe = "";

  alfa1 = "";
  alfa2 = "";
  alfa3 = "";
  nume1 = 0;
  nume2 = 0;

  desfec = @;
  hasfec = @;

  incta = "";
  ficta = "";
  nrot = 0;
  nr1 = 0;
  nr2 = 0;
  nr3 = 0;
  nr4 = 0;
  nr5 = 0;
  selec = 0;
  swalgun = 0;
  swsel = 0;
  debe = 0;
  haber = 0;
  ctaana = "";
  digana = 0;
  anacta = "";
  sw = 0;

  ctanueva = "";
  ctaultima = "";
  grupoc = "";
  grupod = 0;
  grupoh = 0;
  acdebe = 0;
  achaber = 0;
  swgrupo = 0;
  idig = 0;
  descri = "";
  sw_con = 0;

  &icta = "";
  &idesc = "";
  &idebe = 0;
  &ihaber = 0;
  &isaldo = 0;
  &ideudor = 0;
  &iacreedor = 0;
  &xdeudor = 0;
  &xacreedor = 0;
  &tdebe = 0;
  &thaber = 0;
  &tsaldo = 0;
  &tdeudor = 0;
  &tacreedor = 0;
  &swlinea = 0;
  &swcomen = 0;
  &swtipo = 0;
  &swesplin = 0;

  {-1}form  = "";           || Variables menu impresora o pantalla
  form1 = "2400";
  form2 = "1";
  form3 = "Elija formato: ";
  form4 = "12";
  form5 = " < 1 > ";
  form6 = " < 2 > ";
  op_form = 0;
  opanterior = 0;

  aMas     = "";
  aFichero = "";
  nNivCta  = 0;
  nLNvCta  = 0;
  nSw      = 0;

  nSwGApte   = 0;
  aCta2Gru   = "";
  edn_error2 = 0;
  grupod2    = 0;
  acdebe2    = 0;
  grupoh2    = 0;
  achaber2   = 0;
  swgrupo2   = 0;
  ctaultima2 = "";
  nSwOp      = 0;
  aNuevoGrupo = "";

  aContra    = "";
  nSwContra  = 0;
  aDCuenta   = "";
  aHCuenta   = "";
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoan_i;
|INCLUYE digana_i;

|| ***********************************************************************
||                       CALCULOS DE PANTALLA
|| ***********************************************************************

|CALCULO comienzo; |TIPO 8;
 |HAZ InicioAna;
 |SI enSwAna = 0; |ACABA; |FINSI;

 |C_M #0#6,1,"N";
 |C_M #0#7,1,"N";
 |C_M #0#8,1,"N";
 |C_M #0#9,1,"N";
 |C_M #0#10,1,"N";

 |SI nAdig0 > 1;
     |C_M #0#6,1,"S";
 |FINSI;
|FCAL;

|CALCULO LeeSelec; |TIPO 6;
 |SI #0Campo = 0;
     #0#1 = "SIN SELECTOR DE CUENTAS";
 |SINO;
     |ABRE #4;
     #4#0 = #0Campo;
     |LEE 001#4=;
     |SI FSalida ! 0;
         #4#1 = "ERROR SELECTOR NO EXISTE";
         |ERROR;
     |FINSI;
     #0#1 = #4#1;
     |CIERRA #4;
 |FINSI;
 |PINTA #0#1;
|FCAL;

|CALCULO defecto; |TIPO 7;
 #0#2 = fec1;
 #0#3 = fec2;
 |PINTA #0#2;
 |PINTA #0#3;
 |SI dig4 [ 4; |Y dig4 ! 0; #0#41 = 1; |FINSI;
 |SI dig5 [ 4; |Y dig5 ! 0; #0#41 = 2; |FINSI;
 |SI dig6 [ 4; |Y dig6 ! 0; #0#41 = 3; |FINSI;
 |SI dig7 [ 4; |Y dig7 ! 0; #0#41 = 4; |FINSI;
 |SI dig8 [ 4; |Y dig8 ! 0; #0#41 = 5; |FINSI;
 |SI dig9 [ 4; |Y dig9 ! 0; #0#41 = 6; |FINSI;
|FCAL;

|CALCULO fecha; |TIPO 0;
  |SI #0Campo < fec1; |O #0Campo > fec2;
      |MENAV mensa1;
      |ERROR;
      |ACABA;
  |FINSI;
  |SI Campo = 3;
      |SI #0#2 > #0#3;
          |MENAV mensa2;
          |ERROR;
      |FINSI;
  |FINSI;
|FCAL;


|CALCULO anacta; |TIPO 6;
  salidas = FSalida;
  |HAZ FPuntosAna;

  |SI Campo = 5;
      |SI #0#4 > #0#5;
          |MENAV mensa3;
          |ERROR;
      |FINSI;
  |FINSI;
|FCAL;

|CALCULO rotura;|TIPO 0;
  |SI #0Campo ] nAdig0;
      |MENAV error3;
      |ERROR;
  |FINSI;
|FCAL;

|CALCULO nivel; |TIPO 7;
 |C_M #0Campo, 1,"S";
 |SI Campo = 7; |Y #0#6 < 1; || Si solo hemos seleccionado
     #0Campo = 0; |PINTA #0Campo;
     |C_M #0Campo, 1,"N";
 |FINSI;
 |SI Campo = 8; |Y #0#6 < 2; || Si solo hemos seleccionado
     #0Campo = 0; |PINTA #0Campo;
     |C_M #0Campo, 1,"N";
 |FINSI;
 |SI Campo = 9; |Y #0#6 < 3; || Si solo hemos seleccionado
     #0Campo = 0; |PINTA #0Campo;
     |C_M #0Campo, 1,"N";
 |FINSI;
 |SI Campo = 10; |Y #0#6 < 4; || Si solo hemos seleccionado
    #0Campo = 0; |PINTA #0Campo;
    |C_M #0Campo, 1,"N";
 |FINSI;
|FCAL;

|CALCULO nivelrep;|TIPO 0;  || Para comprobar que no repetimos los niveles
 |SI #0Campo ! 0; |Y #0Campo ] nAdig0;
      |MENAV error4;
      |ERROR;
      |ACABA;
 |FINSI;

 |SI nivel = 0;        || Buscamo el nivel mas bajo;
     nivel = #0#7;
 |FINSI;

 |SI Campo = 8;
    |SI #0#8 = #0#7; |Y #0#8 ! 0;    || Si hay dos niveles iguales error
      |MENAV error1;
      |ERROR;
      |ACABA;
    |SINO;
       |SI #0#8 < #0#7;  || Busca el nivel mas alto
           nivel = #0#8;
       |SINO;
           nivel = #0#7;
       |FINSI;
    |FINSI;
 |FINSI;

 |SI Campo = 9; |Y #0#9 ! 0;
    |SI #0#9 = #0#7; |O #0#9 = #0#8; || Si hay dos niveles iguales error
       |MENAV error1;
       |ERROR;
       |ACABA;
    |FINSI;

    |SI #0#9 < nivel;  || Busca el nivel mas alto
        nivel = #0#9;
    |FINSI;
 |FINSI;

 |SI Campo = 10; |Y #0#10 ! 0;      || Si hay dos niveles iguales error
    |SI #0#10 = #0#7; |O #0#10 = #0#8; |O #0#10 = #0#9;
       |MENAV error1;
       |ERROR;
       |ACABA;
    |FINSI;

    |SI #0#10 < nivel;  || Busca el nivel mas alto
        nivel = #0#10;
    |FINSI;
 |FINSI;
|FCAL;

|CALCULO h_concepto;
 |SI #0#17 > #0#18;
     |MENAV "    ERROR EN SELECCION DE CONCEPTOS";
     |ERROR;
 |FINSI;
|FCAL;

|CALCULO nomodi; |TIPO 0;
 |C_M #0Campo,0,alfa1;
 |SI alfa1 = "N"; |ACABA; |FINSI;
 |SI #0Campo = 0; alfa2 = "N"; |FINSI;
 |SI #0Campo ! 0; alfa2 = "S"; |FINSI;

     |PARA nume1 = Campo + 1;  |SI nume1 [ 28;  |HACIENDO nume1 = nume1 + 1;
           #0nume1 = 0;
           |C_M #0nume1, 1, alfa2;
     |SIGUE;
|FCAL;

|CALCULO seleccio_c; |TIPO 0;
 |SI #0#16 = "N";
     |PARA nume1 = 19; |SI nume1 [ 28; |HACIENDO nume1 = nume1 + 1;
           |C_M #0nume1,1,"S";
     |SIGUE;
     |C_M #0#17, 1, "N";
     |C_M #0#18, 1, "N";
     #0#17 = 1;       |PINTA #0#17;
     #0#18 = 999;     |PINTA #0#18;
 |SINO;
     |PARA nume1 = 19; |SI nume1 [ 28; |HACIENDO nume1 = nume1 + 1;
           |C_M #0nume1,1,"N";
           #0nume1 = 0; |PINTA #0nume1;
     |SIGUE;
     |C_M #0#17, 1, "S";
     |C_M #0#18, 1, "S";
 |FINSI;
|FCAL;

|CALCULO AntesDesg; |TIPO 7;
 alfa1 = #0#40; |C_M #0#41,1,alfa1;
|FCAL;

|PROCESO DespDesg; |TIPO 0;
/*
  nNivCta = #0#41; nSw = 0;
  |SI nNivCta = 1; |Y dig4 = 0; nSw = 1; |FINSI;
  |SI nNivCta = 2; |Y dig5 = 0; nSw = 1; |FINSI;
  |SI nNivCta = 3; |Y dig6 = 0; nSw = 1; |FINSI;
  |SI nNivCta = 4; |Y dig7 = 0; nSw = 1; |FINSI;
  |SI nNivCta = 5; |Y dig8 = 0; nSw = 1; |FINSI;
  |SI nNivCta = 6; |Y dig9 = 0; nSw = 1; |FINSI;
  |SI nSw = 1;
      |MENAV "    Nivel Incorrecto";
      |ERROR;
  |FINSI;
*/
|FPRC;

|PROCESO seleccio2; |TIPO 0;
   alfa1 = "N"; alfa2 = "S";
   |SI #0#46 = "S";
       alfa1 = "S";
       alfa2 = "N";
   |FINSI;
   |C_M #0#47,1, alfa1; |C_M #0#48,1, alfa1;
   |C_M #0#49,1, alfa2; |C_M #0#50,1, alfa2;
   |C_M #0#51,1, alfa2; |C_M #0#52,1, alfa2;
   |C_M #0#53,1, alfa2; |C_M #0#54,1, alfa2;
   |C_M #0#55,1, alfa2; |C_M #0#56,1, alfa2;
   |C_M #0#57,1, alfa2; |C_M #0#58,1, alfa2;

   |SI #0#46 = "N";
       #0#47 = "            "; |PINTA #0#47;
       #0#48 = "            "; |PINTA #0#48;
   |SINO;
       #0#49 = ""; |PINTA #0#49;
       #0#50 = ""; |PINTA #0#50;
       #0#51 = ""; |PINTA #0#51;
       #0#52 = ""; |PINTA #0#52;
       #0#53 = ""; |PINTA #0#53;
       #0#54 = ""; |PINTA #0#54;
       #0#55 = ""; |PINTA #0#55;
       #0#56 = ""; |PINTA #0#56;
       #0#57 = ""; |PINTA #0#57;
       #0#58 = ""; |PINTA #0#58;
   |FINSI;
|FPRC;

|PROCESO cuenta; |TIPO 6;
   |C_M #0Campo, 0, alfa1;
   |SI alfa1 = "N"; |ACABA; |FINSI;

   eaCuenta = #0Campo;
   salidas  = FSalida;
   |HAZ FiltraPuntos;
   #0Campo = eaCuenta; |PINTA #0Campo;
   |SI #0Campo = "            "; |ACABA; |FINSI;
   |HAZ SacaNivel;
|FPRC;

|| **********************************************************************
||                       CALCULOS DE IMPRESION
|| **********************************************************************
|PROCESO &c_pagina;
  |SI sw_pagina = 0;
      pagina = #0#12;
      sw_pagina = 1;
  |SINO;
      pagina = pagina + 1;
  |FINSI;
|FPRC;

|| ------------------------------------------------------------------------
|PROCESO ggrupos2;
 ctanueva = #107#2;
 debe  = 0;
 haber = 0;
 |SI grupod2 ! 0; debe  = (grupod2 * #107#4 / 100) ! EURODB; |FINSI;
 |SI grupoh2 ! 0; haber = (grupoh2 * #107#4 / 100) ! EURODB; |FINSI;

 |SI debe ! 0; |O haber ! 0;
     |SI ctanueva ] incta; |Y ctanueva [ ficta;
         |HAZ gragrup;
     |FINSI;
 |FINSI;

 swgrupo2   = 1;
 ctaultima2 = #107#2;
 acdebe2    = (acdebe2 + debe)   ! EURODB;
 achaber2   = (achaber2 + haber) ! EURODB;
|FPRC;

|PROCESO fingrupo2;
 |SI swgrupo2 = 0; |ACABA; |FINSI;
 |SI acdebe2 = grupod2; |Y achaber2 = grupoh2; |ACABA; |FINSI;

 ctanueva = ctaultima2;
 debe  = grupod2 - acdebe2;
 haber = grupoh2 - achaber2;
 |SI debe ! 0; |O haber ! 0;
     |SI ctanueva ] incta; |Y ctanueva [ ficta;
         |HAZ gragrup;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE leegrupo2;
 #107#1002;
 ;
 aCta2Gru, 000;
 aCta2Gru, 999;
 e;
 NULL, ggrupos2, NULL, NULL, fingrupo2;
|FIN;

|PROCESO Subgrupo;
 aCta2Gru = ctanueva;
 grupod2  = debe;  acdebe2  = 0;
 grupoh2  = haber; achaber2 = 0;
 swgrupo2  = 0;
 ctaultima2 = "";
 |BUCLE leegrupo2;
|FPRC;

|PROCESO TrataCtaGrupo;
 alfa3 = ctanueva % 103;
 |SI alfa3 ! "GRU";   ||Tratamiento normal de Cuenta Analitica
     |SI ctanueva ] incta; |Y ctanueva [ ficta;
         |HAZ gragrup;
     |FINSI;
 |SINO;
     |SI #0#42 = "N";   || Si quiere grabar grupos
         |SI ctanueva ] incta; |Y ctanueva [ ficta;
             |HAZ gragrup;
         |SINO;
             |HAZ Subgrupo;
         |FINSI;
     |SINO;
         |HAZ Subgrupo;
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO ggruposA;
 nSwGApte = 1;
 ctanueva = #13#6;
 |SI #1#8 = "H";
     debe = #13#9;
     haber = 0;
 |SINO;
     debe = 0;
     haber = #13#9;
 |FINSI;

 |HAZ TrataCtaGrupo;
|FPRC;

|DEFBUCLE leegrApte;
 #13#1;
 ;
 #1#0, #1#1, #1#2, #1#3, grupoc, 000;
 #1#0, #1#1, #1#2, #1#3, grupoc, 999;
 e;
 NULL, ggruposA;
|FIN;

|| -----------------
|PROCESO ggrupos;
 ctanueva = #7#2;
 debe  = 0;
 haber = 0;
 |SI grupod ! 0; debe  = (grupod * #7#4 / 100) ! EURODB; |FINSI;
 |SI grupoh ! 0; haber = (grupoh * #7#4 / 100) ! EURODB; |FINSI;

 swgrupo   = 1;
 ctaultima = #7#2;
 acdebe    = (acdebe + debe)   ! EURODB;
 achaber   = (achaber + haber) ! EURODB;

 |SI debe ! 0; |O haber ! 0;
     |HAZ TrataCtaGrupo;
 |FINSI;

|FPRC;

|PROCESO fingrupo;

 |SI swgrupo = 0; |ACABA; |FINSI;
 |SI acdebe = grupod; |Y achaber = grupoh; |ACABA; |FINSI;

 ctanueva = ctaultima;
 debe  = grupod - acdebe;
 haber = grupoh - achaber;
 |SI debe ! 0; |O haber ! 0;
     |HAZ TrataCtaGrupo;
 |FINSI;
|FPRC;

|DEFBUCLE leegrupo;
 #7#1;
 ;
 grupoc, 000;
 grupoc, 999;
 e;
 NULL, ggrupos, NULL, NULL, fingrupo;
|FIN;


|PROCESO BuscoOtros;
     nSwGApte = 0;
     grupoc  = ctanueva;
     |BUCLE leegrApte;
     |SI nSwGApte = 0;
         ctanueva = #1#29;
         swgrupo  = 0;
         grupod   = debe;  acdebe  = 0;
         grupoh   = haber; achaber = 0;
         ctaultima = "";
         |BUCLE leegrupo;
     |FINSI;
|FPRC;

|PROCESO grabatra;  || ... Graba temporales
 |SI #1#9 = 0; |ACABA; |FINSI;
 |SI #1#8 = "H";
     debe = #1#9;
     haber = 0;
 |SINO;
     debe = 0;
     haber = #1#9;
 |FINSI;
 ctanueva = #1#29;

 alfa3 = ctanueva % 103;
 |SI alfa3 ! "GRU";   ||Tratamiento normal de Cuenta Analitica
     |HAZ gragrup;
 |SINO;
     |SI #0#42 = "N";   || Si quiere grabar grupos
         |SI ctanueva ] incta; |Y ctanueva [ ficta;
             |HAZ gragrup;
         |SINO;
             |HAZ BuscoOtros;
         |FINSI;
     |SINO;
         |HAZ BuscoOtros;
     |FINSI;
 |FINSI;
|FPRC;

|| ------------------------------------------------
|PROCESO LeeCta;
 |ABRE #10;
 |DDEFECTO #10;
 #10#0 = aContra;
 |LEE 000#10=;
 |CIERRA #10;
 |ABRE #2;
 |DDEFECTO #2;
 #2#0 = aNuevoGrupo;
 |LEE 000#2=;
 |CIERRA #2;
|FPRC;

|PROCESO GraboNuevo;
     #906#0  = ctanueva;
     #906#8  = aContra;
     #906#11 = aNuevoGrupo;
     |LEE 101#906=;
     |SI FSalida ! 0;
         |DDEFECTO #906;
         #906#0 = ctanueva;
         #906#1 = nAdig0;
         #906#8 = aContra;
         #906#11 = aNuevoGrupo;
         |HAZ LeeCta;
         #906#10 = #10#2;
         #906#12 = #2#1;
        |GRABA 020#906c;
     |FINSI;

     #906#2 = #906#2 + debe;
     #906#3 = #906#3 + haber;
     #906#4 = #906#2 - #906#3;
     |GRABA 020#906a;
     |LIBERA #906;
|FPRC;

|PROCESO GrCtaCont;

 aNuevoGrupo = #1#29;
 |HAZ GraboNuevo;

 |SI #0#44 = "S";
     aNuevoGrupo = #1#29;
     |SI aContra ! "";
         aContra = "";
         |HAZ GraboNuevo;
    |FINSI;
 |FINSI;
|FPRC;

|PROCESO gragrup;

 || ... Graba temporal de Cuentas de Analitica
 |SI ctanueva [ "       "; |ACABA; |FINSI;

 dn_cuent = ctanueva;
 |HAZ dig_ana2;
 |SI dn_error !  0; |ACABA; |FINSI;

 #6#0  = ctanueva;
 #6#1  = nAdig0;
 #6#24 = aContra;
 |LEE 101#6=;
 |SI FSalida ! 0;
     |DDEFECTO #6;
     #6#0 = ctanueva;
     #6#1 = nAdig0;
     #6#8 = #6#0;
     #6#9 = #6#1;
     #6#24 = aContra;
     |GRABA 020#6c;
 |FINSI;

 #6#2 = #6#2 + debe;
 #6#3 = #6#3 + haber;
 #6#4 = #6#2 - #6#3;
 #6#10 = "C";
 |GRABA 020#6a;
 |LIBERA #6;

 |SI #0#44 = "S";
     |HAZ GrCtaCont;
 |FINSI;

 swalgun = 1;
 || ... Graba Auxiliar de Cuentas.

 |SI nrot = 0; |ACABA; |FINSI;

 |SI nr1 ! 0;
     digana = nr1;
     ctaana = ctanueva;
     |HAZ creamayor;
     |HAZ graba2;
 |FINSI;

 |SI nr2 ! 0;
     digana = nr2;
     ctaana = ctanueva;
     |HAZ creamayor;
     |HAZ graba2;
 |FINSI;

 |SI nr3 ! 0;
     digana = nr3;
     ctaana = ctanueva;
     |HAZ creamayor;
     |HAZ graba2;
 |FINSI;

 |SI nr4 ! 0;
     digana = nr4;
     ctaana = ctanueva;
     |HAZ creamayor;
     |HAZ graba2;
 |FINSI;
|FPRC;

|PROCESO graba2;

 |SI ctaana = ctanueva;
     |ACABA;
 |FINSI;
 #6#0  = ctaana;
 #6#1  = digana;
 #6#24 = aContra;
 |LEE 101#6=;
 |SI FSalida ! 0;
     |DDEFECTO #6;
     #6#0 = ctaana;
     #6#1 = digana;
     #6#8 = anacta;
     #6#9 = digana;
     #6#24 = aContra;
     |GRABA 020#6c;
 |FINSI;
 #6#2 = #6#2 + debe;
 #6#3 = #6#3 + haber;
 #6#4 = #6#2 - #6#3;
 #6#10 = "M";
 |GRABA 020#6a;
 |LIBERA #6;
|FPRC;

|PROCESO creamayor;
    |SI digana = 1;  nume1 = nAdig1;  |FINSI;
    |SI digana = 2;  nume1 = nAdig2;  |FINSI;
    |SI digana = 3;  nume1 = nAdig3;  |FINSI;
    |SI digana = 4;  nume1 = nAdig4;  |FINSI;
    nume1 = 100 + nume1;
    alfa1 = ctaana % nume1;
    alfa2 = ctaana % nume1;
    alfa1 = alfa1 + "       ";
    alfa2 = alfa2 + "zzzzzzz";
    ctaana = alfa1 % 107;
    anacta = alfa2 % 107;
|FPRC;

|| -----------------------------------------------------------------
|| ---- Seleccion de apuntes.

|PROCESO ver_selec;
 |SI #1#4 ] #5#2; |Y #1#4 [ #5#3;
     swsel = 1;
     |ERROR10;
 |FINSI;
|FPRC;

|DEFBUCLE seleccion;
 #5#1;
 ;
 selec, 000;
 selec, 999;
 e;
 NULL, ver_selec;
|FIN;

|| ... Segundo nivel de grupos
|CALCULO ver_grupo2;
 |SI #107#2 ] incta; |Y #107#2 [ ficta;
     dn_cuent = #107#2;
     |HAZ dig_ana2;
     |SI dn_error = 0;
         edn_error2 = 0;
         |ERROR10;
     |FINSI;
 |FINSI;
|FCAL;

|DEFBUCLE selegrupo2;
 #107#1002;
 ;
 aCta2Gru, 000;
 aCta2Gru, 999;
 e;
 NULL, ver_grupo2;
|FIN;

|PROCESO ver_grupo_apte;
 nSwGApte = 1;
 |SI #13#6 ] incta; |Y #13#6 [ ficta;
     dn_cuent = #13#6;
     |HAZ dig_ana2;
     |SI dn_error = 0;
         swsel = 1;
        |ERROR10;
     |FINSI;
 |SINO;
     alfa1 = #13#6 % 103;
     |SI alfa1 = "GRU";
         edn_error2 = 1;
         aCta2Gru   = #13#6;
         |BUCLE selegrupo2;
         |SI edn_error2 = 0;
             swsel = 1;
             |ERROR10;
         |FINSI;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE selgruApte;
 #13#1;
 ;
 #1#0, #1#1, #1#2, #1#3, #1#29, 000;
 #1#0, #1#1, #1#2, #1#3, #1#29, 999;
 e;
 NULL, ver_grupo_apte;
|FIN;

|PROCESO ver_grupo;
 |SI #7#2 ] incta; |Y #7#2 [ ficta;
     dn_cuent = #7#2;
     |HAZ dig_ana2;
     |SI dn_error = 0;
         swsel = 1;
        |ERROR10;
     |FINSI;
 |SINO;
     alfa1 = #7#2 % 103;
     |SI alfa1 = "GRU";
         edn_error2 = 1;
         aCta2Gru   = #7#2;
         |BUCLE selegrupo2;
         |SI edn_error2 = 0;
             swsel = 1;
             |ERROR10;
         |FINSI;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE selegrupo;
 #7#1;
 ;
 #1#29, 000;
 #1#29, 999;
 e;
 NULL, ver_grupo;
|FIN;

|CALCULO concepto; || Para seleccionar el concepto de apuntes
  sw_con = 0;
  |PARA nume2 = 19; |SI nume2 [ 28; |HACIENDO nume2 = nume2 + 1;
        |SI #0nume2  = #1#6;
            sw_con = 1;
            |SAL;
        |FINSI;
  |SIGUE;
|FCAL;

|PROCESO leoContra;
   aContra   = doce;
   nSwContra = 0;
   |DDEFECTO #31;
   #31#0 = #1#0;
   #31#1 = #1#1;
   #31#2 = #1#2;
   #31#3 = #1#3;
   |LEE 001#31=;
   |SI FSalida = 0;
        aContra = #31#4;
   |FINSI;

   |SI aContra = doce;
       |DDEFECTO #32;
       #32#0 = #1#0;
       #32#1 = #1#1;
       #32#2 = #1#2;
       |LEE 040#32=;
       |SI #1#8 = "D";
           aContra = #32#10;
       |SINO;
           aContra = #32#8;
       |FINSI;
   |FINSI;

   nSwContra = 1;
   |SI aContra = doce; |ACABA; |FINSI;

   |SI #0#46 = "S";
       |SI aContra ] #0#47; |Y aContra [ #0#48;
           nSwContra = 0;
       |FINSI;
   |SINO;
       |PARA nume1 = 49; |SI nume1 [ 58; |HACIENDO nume1 = nume1 + 1;
             |SI #0nume1 = aContra;
                 nSwContra = 0;
                 |SAL;
             |FINSI;
       |SIGUE;
   |FINSI;
|FPRC;

|PROCESO ver_apunte;
 sw_con = 1;
 |SI #0#16 = "N"; |HAZ concepto; |FINSI;
 |SI sw_con = 0; |ACABA; |FINSI;

 eaCuenta = #1#4;
 |SI selec ! 0;
     swsel = 0;
     |BUCLE seleccion;
     |SI swsel = 0; |ACABA; |FINSI;
 |FINSI;

 swsel = 0;           || Solo comprueba si esta dentro de la seleccion
 alfa3 = #1#29 % 103;
 |SI #1#29 ] incta; |Y #1#29 [ ficta;
     dn_cuent = #1#29;
     |HAZ dig_ana2;
     |SI dn_error = 0; swsel = 1; |FINSI;
 |SINO;
     |SI alfa3 = "GRU";  || ...  |Y #0#42 = "S";
         nSwGApte = 0;
         |BUCLE selgruApte;
         |SI nSwGApte = 0;
             |BUCLE selegrupo;
         |FINSI;
     |FINSI;
 |FINSI;
 |SI swsel = 0; |ACABA; |FINSI;

 |HAZ leoContra;
 |SI nSwContra = 1; |ACABA; |FINSI;  ||no esta seleccionado

 |ATRI R;
 |PINTA 2340, #1#1;
 |ATRI 0;

 |HAZ grabatra;
|FPRC;

|DEFBUCLE cargatra;
 #1#1;
 6;
 00, desfec, 000001, 0001, #0#17;
 99, hasfec, 999999, 9999, #0#18;
 e;
 NULL, ver_apunte;
|FIN;

|| *************************************
|PROCESO leetitulo;
 |SI #6#10 = "C";
     #2#0 = #6#0;
     |LEE 001#2=;
     |SI FSalida = 0;
         idesc = #2#1;
     |SINO;
         idesc = "Cuenta Analitica sin definir ";
     |FINSI;
 |SINO;
     #3#0 = #6#0;
     #3#1 = #6#1;
     |LEE 001#3=;
     |SI FSalida = 0;
         idesc = #3#2;
     |SINO;
         idesc = "Cuenta Analitica Mayor sin definir ";
     |FINSI;
 |FINSI;
|FPRC;

|CALCULO artenvas;
||    alfa1 = #6#0;
||    alfa1 = alfa1 % 102;

|| |SI dos_ant = "xx"; dos_ant = alfa1;    |FINSI;

|| |SI alfa1 ! dos_ant;
||        dos_ant = alfa1;
        swlinea = 1;          || cuando solo se ha seleccionado un nivel y
|| |SINO;                        ||/cambia en sus dos primeros digitos se
||         swlinea = 0;          ||/imprime una linea en blanco!!!
|| |FINSI;
|FCAL;

|PROCESO ImpCtaC;
 swesplin  = 0;
 xdeudor   = 0;
 xacreedor = 0;
 |SI #906#4 ] 0;
     xdeudor = #906#4;
 |SINO;
     xacreedor = - #906#4;
 |FINSI;

 |SI #0#44 = "N";
     swtipo = 2;
 |SINO;
     |SI #0#40 = "N";
         swtipo = 3;
     |SINO;
         alfa1 = #906#8; |QBF alfa1;
         |SI alfa1 = "";
             swtipo = 3;
         |SINO;
             swtipo = 2;
         |FINSI;
     |FINSI;
 |FINSI;
 |PRINT;
|FPRC;

|DEFBUCLE ImpCtaC;
 #906#1;
 ;
 #6#0, "       ", aContra;
 #6#0, "zzzzzzz", aContra;
 ;
 NULL, ImpCtaC;
|FIN;


|PROCESO elprintcta;
 |SI nSwOp  = 0;
     sw = 1;
     |ERROR10;
     |ACABA;
 |FINSI;

 swesplin = 0;
 |SI sw = 0;
     sw = 1;
     antnivel = #6#1;
 |SINO;
     |SI #6#10 = "C";
         |SI #0#40 = "S"; |O #0#44 = "S";
             swesplin = 1;
         |FINSI;
     |FINSI;
 |FINSI;

 icta = #6#0;
 idig = #6#1;
 |HAZ leetitulo;
 idebe  = #6#2;
 ihaber = #6#3;
 isaldo = #6#4;
 ideudor   = 0;
 iacreedor = 0;
 |SI isaldo ] 0;
     ideudor = isaldo;
 |SINO;
     iacreedor = - isaldo;
 |FINSI;
 swlinea = 0;
 |SI nrot ! 0;
     |SI #6#1 ! antnivel;
         |SI #6#1 < antnivel;
             swlinea = 1;
         |FINSI;
         antnivel = #6#1;
     |FINSI;
 |FINSI;
||  |SI nrot = 0; |HAZ artenvas; |FINSI;

 swtipo = 0;
 |SI #6#10 = "C";  swtipo = 1; |FINSI;
 |SI #6#10 ! "C"; |Y #6#1 = nr5;  swtipo = 2; |FINSI;
 |PRINT;
 swcomen = 1;
 |SI #6#10 = "C";
      |SI #0#40 = "S"; |O #0#44 = "S";
          swlinea = 0;
          nSw         = 0;
          aNuevoGrupo = "";
          |BUCLE ImpCtaC;
      |FINSI;
      tdebe = tdebe + idebe;
      thaber = thaber + ihaber;
      tsaldo = tsaldo + isaldo;
      tdeudor = tdeudor + ideudor;
      tacreedor = tacreedor + iacreedor;
 |FINSI;
|FPRC;


|PROCESO finimpr;
 |SI sw = 1;
     |PIEINF;
 |FINSI;
 sw_pagina = 0;
 tdebe     = 0;
 thaber    = 0;
 tsaldo    = 0;
 tacreedor = 0;
 tdeudor   = 0;
 sw        = 0;
 dos_ant   = "xx";
 swcomen   = 0;
|FPRC;

|DEFBUCLE imprctamy;
 #6#3;
 ;
 aContra, "       ";
 aContra, "zzzzzzz";
 ;
 NULL, elprintcta;
|FIN;

|PROCESO Paraimprimir;
 aContra = #10#0;
 nSwOp = 0;
 sw    = 0;
 |BUCLE imprctamy;
 |SI sw = 1;
     sw    = 0;
     nSwOp = 1;
     |ABRE #2;
     |ABRE #3;
     |BUCLE imprctamy;
     |HAZ finimpr;
     |CIERRA #2;
     |CIERRA #3;
 |FINSI;
|FPRC;

|DEFBUCLE PorContra;
 #10#1;
 3;
 aDCuenta, "S";
 aHCuenta, "S";
 e;
 NULL, Paraimprimir;
|FIN;

|| *************************************************************************
|| ****************** COMIENZO CON TIPO 3 **********************************
|| *************************************************************************

|CALCULO tipo3; |TIPO 3;
|| op_form = opanterior;
|| |MENU form;
|| op_form = FSalida;
|| |SI op_form [ 0; |ACABA; |FINSI;
|| opanterior = op_form
|| |SI #0#44 = "N"; informe = "anyn0009"; |FINSI;
|| |SI informe = "any00009";
||     |CONFI "    SUtilizar impresora Crystal Reports";
||     |SI FSalida ! 0; informe = "any20009"; |FINSI;
|| |FINSI;

 informe = "any00009";

 |IMPRE 0;
 |SI FSalida ! 0; |ACABA; |FINSI;

  selec = #0#0;
 desfec = #0#2;
 hasfec = #0#3;
  incta = #0#4;
  ficta = #0#5;
   nrot = #0#6;
    nr1 = #0#7;
    nr2 = #0#8;
    nr3 = #0#9;
    nr4 = #0#10;
  |SI nrot ! 0;
      nr5 = 7;
      |SI nr1 ! 0; nr5 = nr1; |FINSI;
      |SI nr5 > nr2; |Y nr2 ! 0; nr5 = nr2; |FINSI;
      |SI nr5 > nr3; |Y nr3 ! 0; nr5 = nr3; |FINSI;
      |SI nr5 > nr4; |Y nr4 ! 0; nr5 = nr4; |FINSI;
  |FINSI;
  nNivCta  = 0;
  |SI #0#40 = "S";
      nNivCta  = #0#41;
      |SI #0#41 = 1;  nLNvCta  = dig4; |FINSI;
      |SI #0#41 = 2;  nLNvCta  = dig5; |FINSI;
      |SI #0#41 = 3;  nLNvCta  = dig6; |FINSI;
      |SI #0#41 = 4;  nLNvCta  = dig7; |FINSI;
      |SI #0#41 = 5;  nLNvCta  = dig8; |FINSI;
      |SI #0#41 = 6;  nLNvCta  = dig9; |FINSI;
 |FINSI;
 || .... Crear los ficheros auxiliares

 swalgun = 0;
 |ABRE #6;
 |ABRE #906;

 |ATRI R;
 |PINTA 2321, " Grabando temporal            ";
 |ATRI 0;

 |ABRE #31;
 |ABRE #32;
 |BUCLE cargatra;
 |CIERRA #32;
 |CIERRA #31;

 |CIERRA #906;
 |CIERRA #6;

 |SI swalgun = 0;
     |MENAV "    Seleccion Vacia ...";
 |SINO;
      |PINTA 2321, "                              ";

      |INFOR informe;
      |SI FSalida ! 0; |FINIMP; |VETE ParaSalir; |FINSI;

      |PINTA 2321, " Imprimiendo ...              ";

      |SI #0#46 = "S";
          aDCuenta = #0#47;
          aHCuenta = #0#48;
          |BUCLE PorContra;
      |SINO;
          |PARA nume1 = 49; |SI nume1 [ 58; |HACIENDO nume1 = nume1 + 1;
                |SI #0nume1 ! doce;
                    aDCuenta = #0nume1;
                    aHCuenta = #0nume1;
                    |BUCLE PorContra;
                |FINSI;
          |SIGUE;
      |FINSI;
 |FINSI;

|ET ParaSalir;
  |DELINDEX #6;
  |DELINDEX #906;
  |FININF;
  |FINIMP;
|FCAL;


|PROGRAMA;

  |CLS;
  |CABEZA "Balance Sumas y Saldos Ctas Analiticas";

  |HAZ inicio;
  |HAZ Analitica;
  |SI  enSwAna = 0;
       alfa1 = "    CONTABILIDAD NO ANALITICA ";
       |SI eaAnNiv = "E";
           alfa1 = "    EMPRESA NO ANALITICA ";
       |FINSI;
       |MENAV alfa1;
       |ACABA;
  |FINSI;

  |C_M #0#6,1,"N";
  |C_M #0#7,1,"N";
  |C_M #0#8,1,"N";
  |C_M #0#9,1,"N";
  |C_M #0#10,1,"N";
  |SI nAdig0 > 1; |C_M #0#6,1,"S"; |FINSI;

  |DBASE aMas;
  aMas  = aMas + "def/anm00003";
  |CARGA_DEF aMas, anm0003@;
  |SI FSalida ! 0;
      |MENAV "    Error Cargado Fichero de Grupos Analiticos. Proceso Interrumpido";
      |ACABA;
  |FINSI;

  aFichero = ("y9" + Usuario) % 108;
  |NOME_DAT #6 aFichero;
  |DELINDEX #6;

  aFichero = ("9y" + Usuario) % 108;
  |NOME_DAT #906 aFichero;
  |DELINDEX #906;

  |ABRE #0;
  |LEE 001#0p;
  |SI FSalida ! 0;
      |DDEFECTO #0;

      #0#2 = fec1;
      #0#3 = fec2;
      |SI dig4 [ 4; |Y dig4 ! 0; #0#41 = 1; |FINSI;
      |SI dig5 [ 4; |Y dig5 ! 0; #0#41 = 2; |FINSI;
      |SI dig6 [ 4; |Y dig6 ! 0; #0#41 = 3; |FINSI;
      |SI dig7 [ 4; |Y dig7 ! 0; #0#41 = 4; |FINSI;
      |SI dig8 [ 4; |Y dig8 ! 0; #0#41 = 5; |FINSI;
      |SI dig9 [ 4; |Y dig9 ! 0; #0#41 = 6; |FINSI;
      |GRABA 020#0c;
  |FINSI;

  FSalida = 0;
  |PARA;|SI FSalida = 0; |HACIENDO;
         |PINPA #0#0;
         |PINDA #0#0;                    || Pinta los datos por defecto
         |ENDATOS #1#0;                  || Seleccion informe
  |SIGUE;

  |GRABA 020#0a;
  |CIERRA #0;

  |DESCARGA_DEF #107;
|FPRO;
