|| Ahora se utilizara tb. para importar otras contabilidades
|| depende del parametro que se le pasa al ejecutarse

|FICHEROS;
     dscsvz10;

     canempre;
     cacuenta;
     caclipro;
     camaasic;
     camaasil;
     camaniva;
     caivalin;

     :modelos/dsmom030;

     dscsvw06;
     dscsvw07;
     dscsvw08;
     dscsvw09;
     dscsvw10;

     &regisnif@;
|FIN;

|VARIABLES;
     &eaModoImpo = "";
     &eaRepeSopo = "";

     &eaFicheroCSV = "";
     &eaTipoCSV = "";

     aNombre1 = "";
     aNombre2 = "";
     aNombre3 = "";
     aNombre4 = "";
     aModificaAsiIva = "";
     nDigiOrigen = 0;
     nDigiDestino = 0;
     nOrden1 = 0;
     nOrden2 = 0;

     aFicheroLCK = "";
     nPausa = 0;
     aPathContagen = "";
     aBorra = "";
     aEjecuta = "";
     aBaseConta = "";

     &eaFicheroIVA = "";

     aAnyo = "";
     &eaFicheroDatos = "";
     &eaNombreDatos = "";

     &eaBinarioConve = "";

     aMensaje = "";
     aFicheroMDB = "";
     aFicheroAux = "";
     nBtnInfo = -1;
     &eaNombrePDF = "";
     aPathDavid = "";

     aTextoVentana = "";
     aParametro = "";

     n0Vent          = -1;
     n1Vent          = -1;
     n2Vent          = -1;
     nBtnUni         = -1;
     nBtnCan         = -1;
     nBtnCar1        = -1;
     nBtnCar2        = -1;
     nBtnCar3        = -1;
     nBtnCar4        = -1;
     nBtnCar5        = -1;
     nLblReg1        = -1;
     nLblReg2        = -1;
     nLblReg3        = -1;
     nLblReg4        = -1;
     nLblReg5        = -1;

     nRango          = 0;
     nHandle         = 0;
     nIde            = 0;
     nDCampo         = 0;
     nCampo          = 0;
     nPanta          = 0;
     nLong           = 0;
     nCarac          = 0;
     nPos            = 0;
     nFSalida        = 0;
     nColum          = 0;
     nNume1          = 0;
     nNume2          = 0;
     nRam1           = 0;
     nRam2           = 0;
     nPide           = 0;
     nFila           = 0;
     nCargar         = 0;
     nPosi           = 0;
     nTotCmp         = 0;
     nTCampos        = 0;
     nTValor         = 0;
     nReg            = 0;
     nPorce          = 0;
     nError          = 0;
     nCmp            = 0;
     nLongVar        = 0;
     nCarVar         = 0;
     nPosVar         = 0;
     nDife           = 0;
     nDesdeCar       = 0;
     nPos1           = 0;
     nPos2           = 0;
     nChequeo        = 0;
     nDocu           = 0;
     nApun           = 0;
     nBarra          = 0;
     nDia            = 0;
     nMes            = 0;
     nAny            = 0;
     nLibro          = 0;
     nIBas           = 0;
     nIIva           = 0;
     nITot           = 0;
     nIRet           = 0;
     nConcep         = 0;
     nBuscoCli       = 0;
     nPrint          = 0;

     aCtaBas         = "";
     aCtaIva         = "";
     aCtaTot         = "";
     aCtaRet         = "";
     aAlfa           = "";
     aAlfa1          = "";
     aAlfa2          = "";
     aAlfa3          = "";
     aFecha          = "";
     aEsc1           = "";
     aEsc2           = "";
     aRetu           = "";
     aTecla          = "";
     aPathLocal      = "";
     aPathTmp        = "";
     aFicTree        = "";
     aAbre           = "";
     aTabla          = "";
     aTablaTree      = "";
     aFilaTree       = "";
     aDescri         = "";
     aRuta           = "";
     aLong           = "";
     aCadena         = "";
     aOrigen         = "";
     aDestino        = "";
     aDTabla         = "";
     aHTabla         = "";
     aBuscar         = "";
     aRefresca       = "";
     aCarac          = "";
     aContenido      = "";
     a100            = "";
     aVar            = "";
     aLee            = "";
     aC10            = "";
     aC13            = "";
     aFicTmp         = "";
     aLongVar        = "";
     aComilla        = "";
     aCarVar         = "";
     aTipo           = "";
     aAsiento        = "";
     aLibro          = "";
     aParam1         = "";
     aParam2         = "";
     aParam3         = "";
     aProg           = "";
     aFicBrowse      = "";
     aVer1           = "";
     aVer2           = "";
     aModAsiento     = "";

     {1}aConte       = "";

     {-1}sTree       = 0;
         sT_nID      = 0;
         sT_nOper    = 0;
         sT_aData    = "";

     {-1}aVent       = "";
         aVent1      = 0;
         aVent2      = 0;
         aVent3      = 0;
         aVent4      = 0;
         aVent5      = "";

     &eaUnidadBasico = "";
     &eaIP           = "";
|FIN;

|INCLUYE dscont_i;

|| ------------------------------------------------------------------------

|PROCESO Conversion;
     nError  = 0;

     aAlfa = aPathLocal + "*.csv";
     |R_PDIR aAlfa;
     |PARA;  |SI FSalida = 0;  |HACIENDO;
          |RFBORRA aAlfa;

          |R_SDIR aAlfa;
     |SIGUE;

     aParam2 = aPathLocal + "ficcsv.txt";  |RFBORRA aParam2;

     aVer1   = "";
     aVer2   = "";
     aConte  = aBaseConta + "plugin/dsofficetxt.exe";
     |ESPECIFICA "MD5", aConte;
     aVer1 = FSalida;

     aConte  = aPathLocal + "dsofficetxt.exe";
     |ESPECIFICA "REMOTOMD5", aConte;
     aVer2 = FSalida;

     |SI aVer1 ! aVer2;
         aOrigen  = aBaseConta + "plugin/dsofficetxt.exe";
         aDestino = aPathLocal + "dsofficetxt.exe";

         |SI eaIP = "";
             |COPIA_FICHERO aOrigen, aDestino;
         |SINO;
             |ENVIA_FICHERO aOrigen, aDestino;
         |FINSI;

         aAlfa = aPathLocal + "dsofficetxt.exe";
         |XABRE "EC", aAlfa, 0;
         |SI FSalida < 0;
             |MENAV "0000No encuentro el conversor a CSV, por favor consulte con su administrador informatico.";
             aFicBrowse = "";
             |ACABA;
         |FINSI;
     |FINSI;

     aProg   = aPathLocal + "dsofficetxt.exe";
     aParam1 = aFicBrowse;
     aParam2 = aPathLocal + "ficcsv.csv";
     aParam3 = "";

     aAlfa  = "cmd " + &34 + "/c START /WAIT " + aProg + " " + aParam1 + " " + aParam2 + aParam3 + &34;
     |RSYSTEM aAlfa;

     |XABRE "EC", aParam2, 0;
     |SI FSalida < 0;
         aFicBrowse = "";
         |MENAV "0000No se ha podido convertir a CSV el fichero seleccionado.";
         |ACABA;
     |FINSI;

     aFicBrowse = aParam2;
|FPRC;

|PROCESO MiniBrowse;
     aFicBrowse = "F*.*";
     |BROWSE aFicBrowse;

     aAlfa      = aFicBrowse;   |QBF aAlfa;
     aFicBrowse = "";
     aLong      = aAlfa % 0;
     nLong      = aLong;
     |PARA nCarac = 2;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos  = (nCarac * 100) + 1;
           aFicBrowse = aFicBrowse + (aAlfa % nPos);
     |SIGUE;
|FPRC;

|PROCESO Browse;
     |HAZ MiniBrowse;

     |SI aFicBrowse = "";
         |ACABA;
     |FINSI;

     |HAZ Conversion;
|FPRC;

|| ------------------------------------------------------------------------

|PROCESO CalculaFecha;
     |QBT aFecha;
     aLongVar = aFecha % 0;
     nLongVar = aLongVar;
     nBarra   = 0;
     aAlfa1   = "";
     aAlfa2   = "";
     aAlfa3   = "";
     |PARA nCarVar = 1;  |SI nCarVar [ nLongVar;  |HACIENDO nCarVar = nCarVar + 1;
           nPosVar = (nCarVar * 100) + 1;
           aCarVar = aFecha % nPosVar;

           |SI  aCarVar = "0"; |O aCarVar = "1"; |O aCarVar = "2";
             |O aCarVar = "3"; |O aCarVar = "4"; |O aCarVar = "5";
             |O aCarVar = "6"; |O aCarVar = "7"; |O aCarVar = "8";
             |O aCarVar = "9";
                |SI nBarra = 0;  aAlfa1 = aAlfa1 + aCarVar;  |FINSI;
                |SI nBarra = 1;  aAlfa2 = aAlfa2 + aCarVar;  |FINSI;
                |SI nBarra = 2;  aAlfa3 = aAlfa3 + aCarVar;  |FINSI;
           |SINO;
                |SI aCarVar = ".";  |O aCarVar = "/";  |O aCarVar = "\";
                    nBarra = nBarra + 1;
               |FINSI;
           |FINSI;
      |SIGUE;

      |QBF aAlfa1;
      |QBF aAlfa2;
      |QBF aAlfa3;

      nDia = aAlfa1;
      nMes = aAlfa2;
      nAny = aAlfa3;

      |SI nAny < 1000;
          nAny = 2000 + nAny;
      |FINSI;

      |SI nDia = 0;  nDia = 1;                    |FINSI;
      |SI nMes = 0;  nMes = 1;                    |FINSI;
      |SI nAny = 0;  nAny = 2000 + #canempre#26;  |FINSI;

      aFecha = (("00" + nDia) % -102);
      aFecha = aFecha + "." + (("00" + nMes) % -102);
      aFecha = aFecha + "." + (("0000" + nAny) % -104);
|FPRC;

|PROCESO QuitaPuntos;
     |QBT aAlfa;
     aLongVar = aAlfa % 0;
     nLongVar = aLongVar;
     aAlfa1   = "";
     |PARA nCarVar = 1;  |SI nCarVar [ nLongVar;  |HACIENDO nCarVar = nCarVar + 1;
           nPosVar = (nCarVar * 100) + 1;
           aCarVar = aAlfa % nPosVar;

           |SI  aCarVar ! ".";
                aAlfa1 = aAlfa1 + aCarVar;
           |FINSI;
      |SIGUE;

      aAlfa = aAlfa1;
|FPRC;

|PROCESO QuitaComillas;
     aLongVar = aAlfa % 0;
     nLongVar = aLongVar;

     aAlfa1   = "";
     aComilla = &34;
     |PARA nCarVar = 1;  |SI nCarVar [ nLongVar;  |HACIENDO nCarVar = nCarVar + 1;
           nPosVar = (nCarVar * 100) + 1;
           aCarVar = aAlfa % nPosVar;

           |SI aCarVar ! aComilla;
                aAlfa1 = aAlfa1 + aCarVar;
           |FINSI;
     |SIGUE;

     aAlfa = aAlfa1;
|FPRC;

|PROCESO CalculaCuenta;
     |QBF eaCuenta;
     |SI eaCuenta = "";  |ACABA;  |FINSI;

     aLongVar = eaCuenta % 0;
     nLongVar = aLongVar;

     |SI nLongVar ! MaximoDigitos;
         nDife = MaximoDigitos - nLongVar;
         |SI nDife < 0;  |ACABA;  |FINSI;

         nDesdeCar = 5;

         nPos1    = 100 + nDesdeCar;
         nPos2    = (100 * (nDesdeCar + 1)) + 99;
         aAlfa    = (eaCuenta % nPos1) + ("0" * nDife) + (eaCuenta % nPos2);
         eaCuenta = aAlfa;
     |FINSI;

     |HAZ SacaNivel;
|FPRC;

|PROCESO dscsvw10Campos;
     nTCampos  = nTCampos + 1;
|FPRC;

|DEFBUCLE dscsvw10Campos;
     #dscsvw10#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dscsvw10Campos;
|FIN;

|PROCESO GrabaCliente;
     #dscsvw08#0  = nReg;
     #dscsvw08#24 = "C";

     |SI nCampo = 0;  #dscsvw08#11 = #dscsvw10#1;  |FINSI;
     |SI nCampo = 2;  #dscsvw08#2  = #dscsvw10#1;  |FINSI;
     |SI nCampo = 3;  #dscsvw08#3  = #dscsvw10#1;  |FINSI;
     |SI nCampo = 4;  #dscsvw08#26 = #dscsvw10#1;  |FINSI;
     |SI nCampo = 5;  #dscsvw08#7  = #dscsvw10#1;  |FINSI;
     |SI nCampo = 7;  #dscsvw08#9  = #dscsvw10#1;  |FINSI;
     |SI nCampo = 8;  #dscsvw08#52 = #dscsvw10#1;  |FINSI;
     |SI nCampo = 11; #dscsvw08#34 = #dscsvw10#1;  |FINSI;
     |SI nCampo = 13; #dscsvw08#10 = #dscsvw10#1;  |FINSI;
|FPRC;

|PROCESO GrabaProveedor;
     #dscsvw08#0  = nReg;
     #dscsvw08#24 = "P";

     |SI nCampo = 0;  #dscsvw08#11 = #dscsvw10#1;  |FINSI;
     |SI nCampo = 2;  #dscsvw08#2  = #dscsvw10#1;  |FINSI;
     |SI nCampo = 3;  #dscsvw08#3  = #dscsvw10#1;  |FINSI;
     |SI nCampo = 4;  #dscsvw08#26 = #dscsvw10#1;  |FINSI;
     |SI nCampo = 5;  #dscsvw08#7  = #dscsvw10#1;  |FINSI;
     |SI nCampo = 6;  #dscsvw08#6  = #dscsvw10#1;  |FINSI;
     |SI nCampo = 8;  #dscsvw08#34 = #dscsvw10#1;  |FINSI;
     |SI nCampo = 13; #dscsvw08#10 = #dscsvw10#1;  |FINSI;
     |SI nCampo = 16; #dscsvw08#52 = #dscsvw10#1;  |FINSI;
|FPRC;

|PROCESO GrabaAsiento;
     #dscsvw06#0  = nReg;

     |SI nCampo = 2;
         aFecha      = #dscsvw10#1;
         |HAZ CalculaFecha;
         #dscsvw06#2 = aFecha;
     |FINSI;

     |SI nCampo = 3;  #dscsvw06#5  = #dscsvw10#1;  |FINSI;
     |SI nCampo = 5;  #dscsvw06#8  = #dscsvw10#1;  |FINSI;

     |SI nCampo = 6;
         aAlfa = #dscsvw10#1;
         |HAZ QuitaPuntos;
         #dscsvw06#10  = aAlfa;
         #dscsvw06#9   = "D";

         |SI #dscsvw06#10 < 0;
             #dscsvw06#10  = -#dscsvw06#10;
             #dscsvw06#9   = "H";
         |FINSI;
     |FINSI;

     |SI nCampo = 11;
         aAlfa = #dscsvw10#1;  |QBF aAlfa;
         |SI aAlfa ! "";
             aAlfa1 = aAlfa % 105;
             |SI aAlfa1 ! "43000";
                 #dscsvw10#1 = "43000" + aAlfa;
             |FINSI;

             #dscsvw06#5  = #dscsvw10#1;
             #dscsvw06#35 = aAlfa;
         |FINSI;
     |FINSI;

     |SI nCampo = 12;
         aAlfa = #dscsvw10#1;  |QBF aAlfa;
         |SI aAlfa ! "";
             aAlfa1 = aAlfa % 105;
             |SI aAlfa1 ! "40000";
              |Y aAlfa1 ! "41000";
              |Y aAlfa1 ! "44000";
                 #dscsvw10#1 = "40000" + aAlfa;
             |FINSI;

             #dscsvw06#5  = #dscsvw10#1;
             #dscsvw06#35 = aAlfa;
         |FINSI;
     |FINSI;

     |SI nCampo = 1;   #dscsvw06#33  = #dscsvw10#1;  |FINSI;  ||Tipo de asiento
     |SI nCampo = 4;   #dscsvw06#34  = #dscsvw10#1;  |FINSI;  ||Referencia
|FPRC;

|PROCESO GrabaIva;
     #dscsvw09#0  = nReg;

     |SI nCampo = 0;
         aFecha      = #dscsvw10#1;  |QBF aFecha;
         |HAZ CalculaFecha;
         #dscsvw09#1 = aFecha;
     |FINSI;

     |SI nCampo = 1;  #dscsvw09#2  = #dscsvw10#1;  |FINSI;
     |SI nCampo = 2;  #dscsvw09#3  = #dscsvw10#1;  |FINSI;
     |SI nCampo = 3;  #dscsvw09#4  = #dscsvw10#1;  |FINSI;
     |SI nCampo = 4;  #dscsvw09#5  = #dscsvw10#1;  |FINSI;

     |SI nCampo = 5;
         aAlfa = #dscsvw10#1;
         |HAZ QuitaPuntos;
         #dscsvw09#6  = aAlfa;
     |FINSI;

     |SI nCampo = 6;
         aAlfa = #dscsvw10#1;
         |HAZ QuitaPuntos;
         #dscsvw09#7  = aAlfa;
     |FINSI;

     |SI nCampo = 7;
         aAlfa = #dscsvw10#1;
         |HAZ QuitaPuntos;
         #dscsvw09#8  = aAlfa;
     |FINSI;

     |SI nCampo = 8;  #dscsvw09#9  = #dscsvw10#1;  |FINSI;
     |SI nCampo = 9;  #dscsvw09#10 = #dscsvw10#1;  |FINSI;
     |SI nCampo = 10; #dscsvw09#11 = #dscsvw10#1;  |FINSI;
|FPRC;

|PROCESO dscsvw10Carga;
     nCampo = #dscsvw10#0 - 1;

     |SI aTabla = "cacuenta";
                          #dscsvw07#0 = nReg;
         |SI nCampo = 0;  #dscsvw07#1 = #dscsvw10#1;  |FINSI;
         |SI nCampo = 1;  #dscsvw07#3 = #dscsvw10#1;  |FINSI;
     |FINSI;

     |SI aTabla = "clientes";
         |HAZ GrabaCliente;
     |FINSI;

     |SI aTabla = "proveedores";
         |HAZ GrabaProveedor;
     |FINSI;

     |SI aTabla = "asientos";
         |HAZ GrabaAsiento;
     |FINSI;

     |SI aTabla = "iva";
         |HAZ GrabaIva;
     |FINSI;
|FPRC;

|DEFBUCLE dscsvw10Carga;
     #dscsvw10#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dscsvw10Carga;
|FIN;

|PROCESO Print;
     |SI aTabla = "cacuenta";
         |SI nLblReg1 ! -1;
             |FIN_CONTROL_WINDOWS nLblReg1;
         |FINSI;

         aAlfa =  "LABEL,{{15}}" + (("00000" + nReg) % -105);
         |CONTROL_SIMPLE nPanta, aAlfa, 0790, 0798, NULL;
         nLblReg1 = FSalida;
     |FINSI;

     |SI aTabla = "clientes";
         |SI nLblReg2 ! -1;
             |FIN_CONTROL_WINDOWS nLblReg2;
         |FINSI;

         aAlfa =  "LABEL,{{15}}" + (("00000" + nReg) % -105);
         |CONTROL_SIMPLE nPanta, aAlfa, 1090, 1098, NULL;
         nLblReg2 = FSalida;
     |FINSI;

     |SI aTabla = "proveedores";
         |SI nLblReg3 ! -1;
             |FIN_CONTROL_WINDOWS nLblReg3;
         |FINSI;

         aAlfa =  "LABEL,{{15}}" + (("00000" + nReg) % -105);
         |CONTROL_SIMPLE nPanta, aAlfa, 1390, 1398, NULL;
         nLblReg3 = FSalida;
     |FINSI;

     |SI aTabla = "asientos";
         |SI nLblReg4 ! -1;
             |FIN_CONTROL_WINDOWS nLblReg4;
         |FINSI;

         aAlfa =  "LABEL,{{15}}" + (("00000" + nReg) % -105);
         |CONTROL_SIMPLE nPanta, aAlfa, 1690, 1698, NULL;
         nLblReg3 = FSalida;
     |FINSI;

     |SI aTabla = "iva";
         |SI nLblReg5 ! -1;
             |FIN_CONTROL_WINDOWS nLblReg5;
         |FINSI;

         aAlfa =  "LABEL,{{15}}" + (("00000" + nReg) % -105);
         |CONTROL_SIMPLE nPanta, aAlfa, 1990, 1998, NULL;
         nLblReg5 = FSalida;
     |FINSI;
|FPRC;

|PROCESO GrabaDatos;
     |CIERRA #dscsvw10;
     nTCampos = 0;
     |BUCLE dscsvw10Campos;

     nError   = 0;
     nTValor  = nTotCmp;
     |SI nTCampos < nTValor;
         |DELINDEX #dscsvw10;
         |ACABA;
     |FINSI;

     nPorce = ((nTValor * 100) / nTCampos) ! 0;
     |SI nPorce < 70;
         |DELINDEX #dscsvw10;
         |ACABA;
     |FINSI;

     nReg = nReg + 1;
     |SI nReg = 1;
         |DELINDEX #dscsvw10;
         |ACABA;
     |FINSI;

||     nPrint = nReg $ 100;
||     |SI nPrint = 0;  |HAZ Print;  |FINSI;

     |SI aTabla = "cacuenta";
         |DDEFECTO #dscsvw07;

         |BUCLE dscsvw10Carga;

         |GRABA 020#dscsvw07.n;
         |LIBERA #dscsvw07;
     |FINSI;

     |SI aTabla = "clientes";
         |DDEFECTO #dscsvw08;

         |BUCLE dscsvw10Carga;

         |GRABA 020#dscsvw08.n;
         |LIBERA #dscsvw08;
     |FINSI;

     |SI aTabla = "proveedores";
         |DDEFECTO #dscsvw08;

         |BUCLE dscsvw10Carga;

         |GRABA 020#dscsvw08.n;
         |LIBERA #dscsvw08;
     |FINSI;

     |SI aTabla = "asientos";
         |DDEFECTO #dscsvw06;

         |BUCLE dscsvw10Carga;

         |GRABA 020#dscsvw06.n;
         |LIBERA #dscsvw06;
     |FINSI;

     |SI aTabla = "iva";
         |DDEFECTO #dscsvw09;

         |BUCLE dscsvw10Carga;

         |GRABA 000#dscsvw09.n;
         |LIBERA #dscsvw09;
     |FINSI;

     |DELINDEX #dscsvw10;
     |PULSATECLA;
|FPRC;

|PROCESO Grabaw10;
/*
     |OEM_ANSI aVar, aVar;
     aAlfa = aVar;
     |HAZ QuitaComillas;
     aVar = aAlfa;
*/

     #dscsvw10#0 = nCmp;
     #dscsvw10#1 = aVar;
     |GRABA 020#dscsvw10.n;
     |LIBERA #dscsvw10;
|FPRC;

|PROCESO SacaDatos;
     aLong  = aLee % 0;
     nLong  = aLong;
     aC10   = &10;
     aC13   = &13;
     |PARA nCarac = 1;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos   = (nCarac * 100) + 1;
           aCarac = aLee % nPos;

           |SI aCarac = aC13;
               aCarac = ";";
           |FINSI;

           |SI aCarac = ";";
               nCmp   = nCmp + 1;
               |HAZ Grabaw10;

               aVar   = "";
               aCarac = "";
           |FINSI;

           |SI aCarac = aC10;
               aCarac = "";
           |FINSI;

           |SI nCmp = nTotCmp;  |Y nCmp ! 0;
               |HAZ GrabaDatos;

               |ABRE #dscsvw10;

               nCmp = 0;
           |FINSI;

           aVar = aVar + aCarac;
      |SIGUE;
|FPRC;

|PROCESO LeeCSV;
     aAlfa = "Procesando registros del fichero " + aTabla;
     |VENTANA 0501, 0540, -1, 16, aAlfa;
     n2Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n2Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     |ABRE #dscsvw10;

     nCmp  = 0;
     nReg  = 0;
     |XABRE "B", aAbre, nHandle;
     |PARA;  |SI;  |HACIENDO;
          |XLEE nHandle, 255, aLee;
          |SI FSalida [ 0;  |SAL;  |FINSI;

          |HAZ SacaDatos;
     |SIGUE;
     |XCIERRA nHandle;

     |LEE 000#dscsvw10.p;
     |SI FSalida = 0;
         nCmp   = nCmp + 1;
         |HAZ Grabaw10;
         |HAZ GrabaDatos;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n2Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n2Vent;

     |HAZ Print;

     |ABRE #dscsvw10; |CIERRA #dscsvw10; |DELINDEX #dscsvw10;
|FPRC;

|PROCESO dscsvw07Cheq;
     aAlfa = #dscsvw07#0;  |QBF aAlfa;
     |SI aAlfa = "";
         |MENAV "0000Registros con cuentas sin contenido. Cancelamos la importaci¢n";

         nError = 1;
         |ERROR10;
         |ACABA;
     |FINSI;

     eaCuenta = #dscsvw07#1;
     |HAZ CalculaCuenta;

     #dscsvw07#1 = eaCuenta;
     #dscsvw07#2 = dig3;

     |GRABA 020#dscsvw07.a;
     |LIBERA #dscsvw07;
|FPRC;

|DEFBUCLE dscsvw07Cheq;
     #dscsvw07#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL, dscsvw07Cheq;
|FIN;

|PROCESO dscsvw07Gra;
     #cacuenta#0 = #dscsvw07#1;
     |LEE 000#cacuenta.=;
     |SI FSalida = 0;  |ACABA;  |FINSI;

     #cacuenta#0 = #dscsvw07#1;
     #cacuenta#1 = #dscsvw07#2;
     #cacuenta#2 = #dscsvw07#3;
     #cacuenta#3 = "S";

     |GRABA 020#cacuenta.n;
     |LIBERA #cacuenta;
|FPRC;

|DEFBUCLE dscsvw07Gra;
     #dscsvw07#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dscsvw07Gra;
|FIN;

|PROCESO CreaCuentas;
     aAbre = #dscsvz10#0;  |QBF aAbre;
     |SI aAbre = "";  |ACABA;  |FINSI;

     aFicTmp = "dscsvw07" + Usuario;  |NOME_DAT #dscsvw07#aFicTmp#;
     aFicTmp = "dscsvw10" + Usuario;  |NOME_DAT #dscsvw10#aFicTmp#;

     |ABRE #dscsvw07;  |CIERRA #dscsvw07;  |DELINDEX #dscsvw07;

     nTotCmp  = 14;

     |ABRE #dscsvw07;
     aTabla = "cacuenta";
     |HAZ LeeCSV;
     |CIERRA #dscsvw07;

     nError = 0;
     |BUCLE dscsvw07Cheq;
     |SI nChequeo = 1;
         |CONFI "0000SRegistros de cuentas preparados para importar. Quiere consultarlos antes?";
         |SI FSalida = 0;
             |ABRE #dscsvw07;
             |CONSULTA_CLAVE #1#dscsvw07;
             |CIERRA #dscsvw07;
         |FINSI;
     |FINSI;

     |BUCLE dscsvw07Gra;
|FPRC;

|PROCESO dscsvw08Cheq;
     aAlfa  = #dscsvw08#11;  |QBT aAlfa;
     aAlfa1 = aAlfa % 105;

     |SI #dscsvw08#24 = "C";
         |SI aAlfa1 ! "43000";
             #dscsvw08#11 = "43000" + aAlfa;
         |FINSI;
     |FINSI;

     |SI #dscsvw08#24 = "P";
         |SI aAlfa1 ! "40000";
          |Y aAlfa1 ! "41000";
          |Y aAlfa1 ! "44000";
             #dscsvw08#11 = "40000" + aAlfa;
         |FINSI;
     |FINSI;

     eaCuenta = #dscsvw08#11;
     |HAZ CalculaCuenta;

     #dscsvw08#1  = eaCuenta;
     #dscsvw08#11 = eaCuenta;
     #dscsvw08#12 = dig3;

     aAlfa = #dscsvw08#26 % 102;  #dscsvw08#4 = aAlfa;
     aAlfa = #dscsvw08#26 % 303;  #dscsvw08#5 = aAlfa;

     |GRABA 020#dscsvw08.a;
     |LIBERA #dscsvw08;
|FPRC;

|DEFBUCLE dscsvw08Cheq;
     #dscsvw08#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL, dscsvw08Cheq;
|FIN;

|PROCESO dscsvw08Gra;
     #caclipro#23 = #dscsvw08#24;
     #caclipro#10 = #dscsvw08#11;
     |LEE 000#caclipro.=;
     |SI FSalida = 0;  |ACABA;  |FINSI;

     |PARA nCampo = 1;  |SI nCampo [ 54;  |HACIENDO nCampo = nCampo + 1;
           nDCampo = nCampo - 1;
           #caclipro#nDCampo# = #dscsvw08#nCampo#
     |SIGUE;

     |GRABA 020#caclipro.n;
     |LIBERA #caclipro;

     #cacuenta#0 = #caclipro#10;
     |LEE 000#cacuenta.=;
     |SI FSalida ! 0;
         |DDEFECTO #cacuenta;
         #cacuenta#0 = #caclipro#10;
         #cacuenta#1 = #caclipro#11;
         #cacuenta#2 = #caclipro#1;
         #cacuenta#3 = "S";

         |GRABA 020#cacuenta.n;
         |LIBERA #cacuenta;
     |FINSI;

     eaCodNif = #caclipro#9;
     |HAZ LeeNifs;
     |SI ewNif = 0; |ACABA;  |FINSI;

     |ABRE #regisnif@;
     |DDEFECTO #regisnif@;

     #regisnif@#0  = #caclipro#9;
     #regisnif@#1  = #caclipro#1;
     #regisnif@#2  = #caclipro#2;
     #regisnif@#4  = #caclipro#3;
     #regisnif@#5  = #caclipro#4;
     #regisnif@#7  = #caclipro#5;
     #regisnif@#6  = #caclipro#6;
     #regisnif@#8  = #caclipro#7;
     #regisnif@#17 = #caclipro#8;
     #regisnif@#12 = #caclipro#24;

     |GRABA 020#regisnif@.n;
     |LIBERA #regisnif@;
     |CIERRA #regisnif@;
|FPRC;

|DEFBUCLE dscsvw08Gra;
     #dscsvw08#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dscsvw08Gra;
|FIN;

|PROCESO CreaClientes;
     aAbre = #dscsvz10#1;  |QBF aAbre;
     |SI aAbre = "";  |ACABA;  |FINSI;

     aFicTmp = "dscsvw08" + Usuario;  |NOME_DAT #dscsvw08#aFicTmp#;
     aFicTmp = "dscsvw10" + Usuario;  |NOME_DAT #dscsvw10#aFicTmp#;

     |ABRE #dscsvw08;  |CIERRA #dscsvw08;  |DELINDEX #dscsvw08;

     nTotCmp  = 22;

     |ABRE #dscsvw08;
     aTabla = "clientes";
     |HAZ LeeCSV;
     |CIERRA #dscsvw08;

     nError = 0;
     |BUCLE dscsvw08Cheq;
     |SI nChequeo = 1;
         |CONFI "0000SRegistros de clientes preparados para importar. Quiere consultarlos antes?";

         |SI FSalida = 0;
             |ABRE #dscsvw08;
             |CONSULTA_CLAVE #1#dscsvw08;
             |CIERRA #dscsvw08;
         |FINSI;
     |FINSI;

     |BUCLE dscsvw08Gra;
|FPRC;

|PROCESO CreaProveedores;
     aAbre = #dscsvz10#2;  |QBF aAbre;
     |SI aAbre = "";  |ACABA;  |FINSI;

     aFicTmp = "dscsvw08" + Usuario;  |NOME_DAT #dscsvw08#aFicTmp#;
     aFicTmp = "dscsvw10" + Usuario;  |NOME_DAT #dscsvw10#aFicTmp#;

     |ABRE #dscsvw08;  |CIERRA #dscsvw08;  |DELINDEX #dscsvw08;

     nTotCmp  = 23;

     |ABRE #dscsvw08;
     aTabla = "proveedores";
     |HAZ LeeCSV;
     |CIERRA #dscsvw08;

     nError = 0;
     |BUCLE dscsvw08Cheq;
     |SI nChequeo = 1;
         |CONFI "0000SRegistros de proveedores preparados para importar. Quiere consultarlos antes?";
         |SI FSalida = 0;
             |ABRE #dscsvw08;
             |CONSULTA_CLAVE #1#dscsvw08;
             |CIERRA #dscsvw08;
         |FINSI;
     |FINSI;

     |BUCLE dscsvw08Gra;
|FPRC;

|PROCESO dscsvw06Cheq;
     |SI #dscsvw06#34 ! aAsiento;
         nApun = 0;
         nDocu = nDocu + 1;
     |FINSI;

     #dscsvw06#3  = nDocu;
     #dscsvw06#1  = 1;
     #dscsvw06#7  = 1;

     aAlfa = #dscsvw06#33 > "";

     aAlfa1 = aAlfa - "APERTURA";
     |SI FSalida ! 0;
         #dscsvw06#1 = 0;
         #dscsvw06#7 = 96;
     |FINSI;

     aAlfa1 = aAlfa - "VENTA";
     |SI FSalida ! 0;
         #dscsvw06#7 = 700;
     |FINSI;

     aAlfa1 = aAlfa - "COMPRA";
     |SI FSalida ! 0;
         #dscsvw06#7 = 600;
     |FINSI;

     aAlfa1 = aAlfa - "CIERRE";
     |SI FSalida ! 0;
         #dscsvw06#1 = 99;
         #dscsvw06#7 = 95;
     |FINSI;

     eaCuenta = #dscsvw06#5;
     |HAZ CalculaCuenta;
     #dscsvw06#5  = eaCuenta;
     #dscsvw06#6  = dig3;

     eaCuenta = #dscsvw06#35;  |QBF eaCuenta;
     |SI eaCuenta ! "";
         |HAZ CalculaCuenta;
         #dscsvw06#35  = eaCuenta;
     |FINSI;

     aAlfa = #dscsvw06#2;  |QBF aAlfa;
     |SI aAlfa = "";
         |MENAV "0000Registros con fecha de apunte sin contenido. Cancelamos la importaci¢n";

         nError = 1;
         |ERROR10;
         |ACABA;
     |FINSI;

     aAlfa = #dscsvw06#5;  |QBF aAlfa;
     |SI aAlfa = "";
         |MENAV "0000Registros con cuentas sin contenido. Cancelamos la importaci¢n";

         nError = 1;
         |ERROR10;
         |ACABA;
     |FINSI;

     |SI #dscsvw06#9 = " ";
         |MENAV "0000Registros sin signo en la cuenta. Cancelamos la importaci¢n";

         nError = 1;
         |ERROR10;
         |ACABA;
     |FINSI;

     |SI #dscsvw06#9 = "D";
         #dscsvw06#17 = #dscsvw06#5;
         #dscsvw06#18 = #dscsvw06#6;
     |FINSI;

     |SI #dscsvw06#9 = "H";
         #dscsvw06#11 = #dscsvw06#5;
         #dscsvw06#12 = #dscsvw06#6;
     |FINSI;

     |SI #dscsvw06#22 = "  ";
         #dscsvw06#22 = #canempre#30;
     |FINSI;

     nApun = nApun + 1;
     #dscsvw06#4 = nApun;
     |GRABA 020#dscsvw06.a;
     |LIBERA #dscsvw06;

     aAsiento = #dscsvw06#34;
|FPRC;

|DEFBUCLE dscsvw06Cheq;
     #dscsvw06#3;
     ;
     INICIO;
     FINAL;
     be;
     NULL, dscsvw06Cheq;
|FIN;

|PROCESO camaniva;
     |SI #dscsvw06#10 ! #camaniva#19;
         |ACABA;
     |FINSI;

     #camaniva#7 = #camaasic#0;
     #camaniva#8 = #camaasic#1;
     #camaniva#9 = #camaasic#2;

     aAlfa = ("00" + #camaasic#0) % -102;
     aAlfa = aAlfa + (#camaasic#1 % 102);
     aAlfa = aAlfa + (#camaasic#1 % 402);
     aAlfa = aAlfa + (#camaasic#1 % 704);
     aAlfa = aAlfa + ("000000" + #camaasic#2) % -106;

     #camaniva#22 = aAlfa;
     #camaasil#26 = aAlfa;
     #camaasic#17 = aAlfa;

     |GRABA 020#camaniva.a;
     |LIBERA #camaniva;
|FPRC;

|DEFBUCLE camaniva;
     #camaniva#1;
     22;
     00, 0000000000, #camaasic#17;
     99, 9999999999, #camaasic#17;
     be;
     NULL, camaniva;
|FIN;

|PROCESO dscsvw06Gra;
     |PARA nCampo = 1;  |SI nCampo [ 31;  |HACIENDO nCampo = nCampo + 1;
           nDCampo = nCampo - 1;
           #camaasil#nDCampo# = #dscsvw06#nCampo#
     |SIGUE;

     #camaasil#26 = #dscsvw06#34;

     |GRABA 020#camaasil.n;
     |LIBERA #camaasil;

     #camaasic#0 = #camaasil#0;
     #camaasic#1 = #camaasil#1;
     #camaasic#2 = #camaasil#2;
     |LEE 000#camaasic.=;
     |SI FSalida = 0;  |ACABA;  |FINSI;

     |DDEFECTO #camaasic;
     #camaasic#0  = #camaasil#0;
     #camaasic#1  = #camaasil#1;
     #camaasic#2  = #camaasil#2;
     #camaasic#17 = #dscsvw06#34;

     |GRABA 020#camaasic.n;
     |LIBERA #camaasic;
|FPRC;

|DEFBUCLE dscsvw06Gra;
     #dscsvw06#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dscsvw06Gra;
|FIN;

|PROCESO camaasil_Cheq;
     #camaasil#12 = #camaniva#36;
     #camaasil#13 = #camaniva#0;
     #camaasil#14 = #camaniva#1;
     #camaasil#15 = #camaniva#2;
     #camaasil#26 = #camaasic#17;

     |GRABA 020#camaasil.a;
     |LIBERA #camaasil;
|FPRC;

|DEFBUCLE camaasil_Cheq;
     #camaasil#1;
     ;
     #camaasic#0, #camaasic#1, #camaasic#2, 0001;
     #camaasic#0, #camaasic#1, #camaasic#2, 9999;
     be;
     NULL, camaasil_Cheq;
|FIN;

|PROCESO camaniva_Cheq;
     |BUCLE camaasil_Cheq;
|FPRC;

|DEFBUCLE camaniva_Cheq;
     #camaniva#1;
     22;
     00, 0000000000, #camaasic#17;
     99, 9999999999, #camaasic#17;
     be;
     NULL, camaniva_Cheq;
|FIN;

|PROCESO camaasic;
     aAlfa1 = ("00" + #camaasic#0) % -102;
     aAlfa1 = aAlfa1 + (#camaasic#1 % 102);
     aAlfa1 = aAlfa1 + (#camaasic#1 % 402);
     aAlfa1 = aAlfa1 + (#camaasic#1 % 704);
     aAlfa1 = aAlfa1 + ("000000" + #camaasic#2) % -106;

     aAlfa2 = #camaasic#17;  |QBF aAlfa2;
     |SI aAlfa1 ! aAlfa2;
         |ACABA;
     |FINSI;

     |BUCLE camaniva_Cheq;
|FPRC;

|DEFBUCLE camaasic;
     #camaasic#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, camaasic;
|FIN;

|PROCESO CreaAsientos;
     aAbre = #dscsvz10#3;  |QBF aAbre;
     |SI aAbre = "";  |ACABA;  |FINSI;

     aFicTmp = "dscsvw06" + Usuario;  |NOME_DAT #dscsvw06#aFicTmp#;
     aFicTmp = "dscsvw10" + Usuario;  |NOME_DAT #dscsvw10#aFicTmp#;

     |ABRE #dscsvw06;  |CIERRA #dscsvw06;  |DELINDEX #dscsvw06;

     nTotCmp  = 22;

     |ABRE #dscsvw06;
     aTabla = "asientos";
     |HAZ LeeCSV;
     |CIERRA #dscsvw06;

     nDocu = 0;
     |SELECT #3#camaasic;
     |LEE 000#camaasic.u;
     |SI FSalida = 0;
         nDocu = #camaasic#2;
     |FINSI;

     nError = 0;
     |BUCLE dscsvw06Cheq;
     |SI nChequeo = 1;
         |CONFI "0000SRegistros de asientos preparados para importar. Quiere consultarlos antes?";

         |SI FSalida = 0;
             |ABRE #dscsvw06;
             |CONSULTA_CLAVE #1#dscsvw06;
             |CIERRA #dscsvw06;
         |FINSI;
     |FINSI;

     |BUCLE dscsvw06Gra;
|FPRC;

|PROCESO dscsvw09Cheq;
     aAlfa =  #dscsvw09#3;  |QBF aAlfa;
     |SI aAlfa = "REPERCUTIDO:";  aLibro = "R";  |FINSI;
     |SI aAlfa = "SOPORTADO:";    aLibro = "S";  |FINSI;

     aAlfa =  #dscsvw09#2;  |QBF aAlfa;
     |SI aAlfa = "";
          |BORRA 020#dscsvw09.a;
          |LIBERA #dscsvw09;
          |ACABA;
     |FINSI;

     aAlfa =  #dscsvw09#3;  |QBF aAlfa;
     |SI aAlfa = "Tipo de asiento";
          |BORRA 020#dscsvw09.a;
          |LIBERA #dscsvw09;
          |ACABA;
     |FINSI;

     #dscsvw09#12 = aLibro;
     |GRABA 020#dscsvw09.a;
     |LIBERA #dscsvw09;
|FPRC;

|DEFBUCLE dscsvw09Cheq;
     #dscsvw09#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL, dscsvw09Cheq;
|FIN;

|PROCESO camaasil;
     |SI nBuscoCli = 0;
         aAlfa = #camaasil#4 % 103;
         |SI aAlfa = "400";  |O aAlfa = "410";
          |O aAlfa = "430";  |O aAlfa = "440";
             aCtaTot = #camaasil#4;

             |ERROR10;
         |FINSI;

         |ACABA;
     |FINSI;

     |SI #camaasil#9 = nIBas;  aCtaBas = #camaasil#4;  |ACABA;  |FINSI;
     |SI #camaasil#9 = nIIva;  aCtaIva = #camaasil#4;  |ACABA;  |FINSI;

     |SI aCtaRet = "";
         aAlfa = aCtaTot % 103;
         |SI aAlfa = "400";  |O aAlfa = "410";
          |O aAlfa = "430";  |O aAlfa = "440";
             aAlfa = #camaasil#4 % 102;
             |SI aAlfa = "47";  |O aAlfa = "47";
                 aCtaRet = #camaasil#4;
                 nIRet   = #camaasil#9;
                 |SI #dscsvw09#8 < 0;
                     nIRet = -#camaasil#9;
                 |FINSI;
             |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE camaasil;
     #camaasil#6;
     ;
     #camaasic#17, "01.01.0000";
     #camaasic#17, "31.12.9999";
     ;
     NULL, camaasil;
|FIN;

|PROCESO camaasilGra;
     #camaasil#12 = #camaniva#36;
     #camaasil#13 = #camaniva#0;
     #camaasil#14 = #camaniva#3;
     #camaasil#15 = #camaniva#2;
     #camaasil#20 = 1;

     |SI aCtaBas = #camaasil#4;  #camaasil#19 = "B";  |FINSI;
     |SI aCtaIva = #camaasil#4;  #camaasil#19 = "I";  |FINSI;
     |SI aCtaTot = #camaasil#4;  #camaasil#19 = "F";  #camaasil#20 = 0;  |FINSI;
     |SI aCtaRet = #camaasil#4;
         |SI #camaniva#0 = 1;  #camaasil#6 = 500;  |FINSI;
         |SI #camaniva#0 = 2;  #camaasil#6 = 410;  |FINSI;

         nConcep = #camaasil#6;  |HAZ LeeConcep;
         #camaasil#7  = aDescri;
         #camaasil#19 = "P";
     |FINSI;

     |GRABA 020#camaasil.a;
     |LIBERA #camaasil;
|FPRC;

|DEFBUCLE camaasilGra;
     #camaasil#6;
     ;
     #camaasic#17, "01.01.0000";
     #camaasic#17, "31.12.9999";
     be;
     NULL, camaasilGra;
|FIN;

|PROCESO dscsvw06Bus;
     |SI aCtaTot = "";
         aAlfa = #dscsvw06#35 % 103;
         |SI aAlfa = "400";  |O aAlfa = "410";
          |O aAlfa = "430";  |O aAlfa = "440";
             aCtaTot = #camaasil#4;
         |FINSI;
     |FINSI;

     |SI aCtaBas = "";
         aAlfa = #dscsvw06#5 % 101;
         |SI aAlfa ! "4";  |Y aAlfa ! " ";
             aCtaBas = #dscsvw06#5;  |QBF aCtaBas;
         |FINSI;
     |FINSI;

     |SI aCtaIva = "";
         aAlfa = #dscsvw06#5 % 103;
         |SI aAlfa = "472";  |O aAlfa = "477";
             aCtaIva = #dscsvw06#5;  |QBF aCtaIva;
         |FINSI;
     |FINSI;

     |SI aCtaTot ! "";  |Y aCtaBas ! "";  |Y aCtaIva ! "";
         |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE dscsvw06Bus;
     #dscsvw06#3;
     ;
     #camaasic#17, 00000;
     #camaasic#17, 99999;
     ;
     NULL, dscsvw06Bus;
|FIN;

|PROCESO LeeConcep;
     #dsmom030#0 = nConcep;
     |LEE 000#dsmom030.=;
     |SI FSalida ! 0;  |DDEFECTO #dsmom030;  |FINSI;

     aDescri = #dsmom030#1;
|FPRC;

|PROCESO dscsvw09Gra;
     |SELECT #5#camaasic;
     #camaasic#17 = #dscsvw09#2;
     |LEE 000#camaasic.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI #dscsvw09#2 ! aAsiento;
         aCtaRet = "";
         nIRet   = 0;
     |FINSI;

     nIBas = #dscsvw09#8;  aCtaBas = "";
     nIIva = #dscsvw09#7;  aCtaIva = "";
     nITot = #dscsvw09#6;  aCtaTot = "";

     |SI nIBas < 0; nIBas = -nIBas;  |FINSI;
     |SI nIIva < 0; nIIva = -nIIva;  |FINSI;
     |SI nITot < 0; nITot = -nITot;  |FINSI;

                              nLibro = 1;
     |SI #dscsvw09#12 = "S";  nLibro = 2;  |FINSI;

     nBuscoCli = 0;  |BUCLE camaasil;
     nBuscoCli = 1;  |BUCLE camaasil;

     |SI aCtaTot = "";  |O aCtaBas = "";  |O aCtaIva = "";
         |BUCLE dscsvw06Bus;
     |FINSI;

     nIBas = #dscsvw09#8;
     nIIva = #dscsvw09#7;
     nITot = #dscsvw09#6;

                              nLibro = 1;
     |SI #dscsvw09#12 = "S";  nLibro = 2;  |FINSI;

     |SI #dscsvw09#2 ! aAsiento;
         #camaniva#0 = nLibro;
         #camaniva#1 = 9999999999;
         |LEE 000#camaniva.];
         |SI FSalida = 0;
             |LEE 000#camaniva.a;
         |SINO;
             |LEE 000#camaniva.u;
         |FINSI;

         nDocu = 1;
         |SI FSalida = 0;  |Y #camaniva#0 = nLibro;
             nDocu = #camaniva#1 + 1;
         |FINSI;

         |DDEFECTO #camaniva;
         #camaniva#0  = nLibro;
         #camaniva#1  = nDocu;
         #camaniva#2  = "";
         #camaniva#3  = nDocu;
         #camaniva#4  = #dscsvw09#1;
         aAlfa        = #dscsvw09#1 % 402;  #camaniva#5 = aAlfa;
         #camaniva#6  = "N";

         #camaniva#7  = #camaasic#0;
         #camaniva#8  = #camaasic#1;
         #camaniva#9  = #camaasic#2;
         #camaniva#10 = #canempre#30;
         #camaniva#24 = #canempre#32;
         #camaniva#12 = aCtaTot;
         #camaniva#13 = #dscsvw09#11;
         #camaniva#14 = #dscsvw09#10;
         #camaniva#27 = 700;
         #camaniva#36 = "R";
         |SI #camaniva#0 = 2;
             #camaniva#27 = 600;
             #camaniva#36 = "S";
         |FINSI;
         #camaniva#22 = #camaasic#17;
         #camaniva#29 = #camaniva#27;

         nConcep = #camaniva#27;  |HAZ LeeConcep;  #camaniva#28 = aDescri;
         nConcep = #camaniva#29;  |HAZ LeeConcep;  #camaniva#30 = aDescri;

         |GRABA 020#camaniva.n;
         |LIBERA #camaniva;

         nApun = 0;
     |FINSI;

     nApun = nApun + 1;

     |DDEFECTO #caivalin;
     #caivalin#0  = #camaniva#0;
     #caivalin#1  = #camaniva#1;
     #caivalin#2  = nApun;
     #caivalin#3  = #camaniva#27;
     #caivalin#6  = nIBas;
     #caivalin#7  = #dscsvw09#5;
     #caivalin#8  = nIIva;
     #caivalin#12 = aCtaBas;
     #caivalin#13 = aCtaIva;
     #caivalin#30 = #camaniva#10;

     |SI nIRet ! 0;
         #caivalin#20 = nIBas;
         #caivalin#22 = nIRet;
         #caivalin#21 = ((nIRet * 100) / nIBas) ! 2;
         #caivalin#23 = aCtaRet;

         |SI #camaniva#0 = 1;  #caivalin#18 = 500;  |FINSI;
         |SI #camaniva#0 = 2;  #caivalin#18 = 410;  |FINSI;
     |FINSI;

     nConcep = #caivalin#3;  |HAZ LeeConcep;  #caivalin#4  = aDescri;
     nConcep = #caivalin#18; |HAZ LeeConcep;  #caivalin#19 = aDescri;

     efFechaIVA = #camaniva#4;
     enLineaIVA = 0;
     enPorcIVA  = #caivalin#7;
     enPorcREC  = #caivalin#9;
     aTipoIVA   = "S";
     |HAZ BuscaTipoIVA;
     #caivalin#16 = enLineaIVA;

     |GRABA 020#caivalin.n;
     |LIBERA #caivalin;

     |BUCLE camaasilGra;

     aAsiento = #dscsvw09#2;
     nIRet    = 0;
|FPRC;

|DEFBUCLE dscsvw09Gra;
     #dscsvw09#2;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dscsvw09Gra;
|FIN;

|PROCESO CreaIVA;
     aAbre = #dscsvz10#4;  |QBF aAbre;
     |SI aAbre = "";  |ACABA;  |FINSI;

     aFicTmp = "dscsvw09" + Usuario;  |NOME_DAT #dscsvw09#aFicTmp#;
     aFicTmp = "dscsvw10" + Usuario;  |NOME_DAT #dscsvw10#aFicTmp#;

     |ABRE #dscsvw09;  |CIERRA #dscsvw09;  |DELINDEX #dscsvw09;

     nTotCmp  = 13;

     |ABRE #dscsvw09;
     aTabla = "iva";
     |HAZ LeeCSV;
     |CIERRA #dscsvw09;

     |BUCLE dscsvw09Cheq;

     |SI nChequeo = 1;
         |CONFI "0000SRegistros de IVA preparados para importar. Quiere consultarlos antes?";

         |SI FSalida = 0;
             |ABRE #dscsvw09;
             |CONSULTA_CLAVE #1#dscsvw09;
             |CIERRA #dscsvw09;
         |FINSI;
     |FINSI;

     |ABRET #dscsvw06;
     |BUCLE dscsvw09Gra;
     |CIERRAT #dscsvw06;
|FPRC;

|PROCESO Importacion;
     |HAZ CreaCuentas;
     |HAZ CreaClientes;
     |HAZ CreaProveedores;
     |HAZ CreaAsientos;

     |SI aParametro = "";
          |HAZ CreaIVA;
     |FINSI;

     |HAZ RecalculosFinales;
|FPRC;

|PROCESO RecalculosFinales;
     |ABRE #dscsvw06;  |CIERRA #dscsvw06;  |DELINDEX #dscsvw06;
     |ABRE #dscsvw07;  |CIERRA #dscsvw07;  |DELINDEX #dscsvw07;
     |ABRE #dscsvw08;  |CIERRA #dscsvw08;  |DELINDEX #dscsvw08;
     |ABRE #dscsvw09;  |CIERRA #dscsvw09;  |DELINDEX #dscsvw09;

     |SI aModAsiento = "NO";
         aAlfa = "*:contagen/careccab canempr1 " + (("00000" + #48#2) % -105) + #48#3 + ";" + "100";
     |SINO;
         aAlfa = "*:contagen/careccab canempr1 " + (("00000" + #48#2) % -105) + #48#3 + ";" + "000";
     |FINSI;
     |SUB_EJECUTA aAlfa;

     aAlfa = "*:contagen/caz00023 canempr1 " + (("00000" + #48#2) % -105) + #48#3 + ";" + "CREA";
     |SUB_EJECUTA aAlfa;

     |MENAV "0000Importaci¢n realizada. A continuaci¢n se van a realizar una serie de chequeos";

     |MENAV "0000Chequeo general de asientos";
     aAlfa = "*:contagen/cacheapu canempr1 " + (("00000" + #48#2) % -105) + #48#3 + ";" + "CHEQUEO";
     |SUB_EJECUTA aAlfa;

     |MENAV "0000Chequeo general de facturas";
     aAlfa = "*:contagen/caconiva canempr1 " + (("00000" + #48#2) % -105) + #48#3 + ";" + "CHEQUEO";
     |SUB_EJECUTA aAlfa;

     |MENAV "0000Chequeo cuentas de IVA";
     aAlfa = "*:contagen/cacheiva canempr1 " + (("00000" + #48#2) % -105) + #48#3 + ";" + "CHEQUEO";
     |SUB_EJECUTA aAlfa;

     |MENAV "0000Asistente de balances";
     aAlfa = "*:contagen/caut0009 canempr1 " + (("00000" + #48#2) % -105) + #48#3 + ";" + "CHEQUEO";
     |SUB_EJECUTA aAlfa;

     |MENAV "0000Recuperaci¢n de saldos";
     aAlfa = "*:contagen/carecupe canempr1 " + (("00000" + #48#2) % -105) + #48#3 + ";" + "CHEQUEO";
     |SUB_EJECUTA aAlfa;

     |MENAV "0000Proceso terminado.";
|FPRC;

|PROCESO Ubica;
     nFSalida = FSalida;

     |ESTADO_CONTROL nBtnCar1, "DISABLE";
     |ESTADO_CONTROL nBtnCar2, "DISABLE";
     |ESTADO_CONTROL nBtnCar3, "DISABLE";
     |ESTADO_CONTROL nBtnCar4, "DISABLE";
     |ESTADO_CONTROL nBtnCar5, "DISABLE";

     |HAZ Browse;

     |ESTADO_CONTROL nBtnCar1, "ENABLE";
     |ESTADO_CONTROL nBtnCar2, "ENABLE";
     |ESTADO_CONTROL nBtnCar3, "ENABLE";
     |ESTADO_CONTROL nBtnCar4, "ENABLE";
     |ESTADO_CONTROL nBtnCar5, "ENABLE";

     |SI aFicBrowse = "";  |ACABA;  |FINSI;

     |SI nFSalida = nBtnCar1;
         aOrigen  = aFicBrowse;
         aDestino = aPathTmp + "cuentas.csv";

         |SI eaIP = "";
             |COPIA_FICHERO aOrigen, aDestino;
         |SINO;
             |RECIBE_FICHERO aOrigen, aDestino;
         |FINSI;

         #dscsvz10#0 = aDestino;  |PINTA #dscsvz10#0;
     |FINSI;

     |SI nFSalida = nBtnCar2;
         aOrigen  = aFicBrowse;
         aDestino = aPathTmp + "clientes.csv";

         |SI eaIP = "";
             |COPIA_FICHERO aOrigen, aDestino;
         |SINO;
             |RECIBE_FICHERO aOrigen, aDestino;
         |FINSI;

         #dscsvz10#1 = aDestino;  |PINTA #dscsvz10#1;
     |FINSI;

     |SI nFSalida = nBtnCar3;
         aOrigen  = aFicBrowse;
         aDestino = aPathTmp + "proveedores.csv";

         |SI eaIP = "";
             |COPIA_FICHERO aOrigen, aDestino;
         |SINO;
             |RECIBE_FICHERO aOrigen, aDestino;
         |FINSI;

         #dscsvz10#2 = aDestino;  |PINTA #dscsvz10#2;
     |FINSI;

     |SI nFSalida = nBtnCar4;
         aOrigen  = aFicBrowse;
         aDestino = aPathTmp + "asientos.csv";

         |SI eaIP = "";
             |COPIA_FICHERO aOrigen, aDestino;
         |SINO;
             |RECIBE_FICHERO aOrigen, aDestino;
         |FINSI;

         #dscsvz10#3 = aDestino;  |PINTA #dscsvz10#3;
     |FINSI;

     |SI nFSalida = nBtnCar5;
         aOrigen  = aFicBrowse;
         aDestino = aPathTmp + "iva.csv";

         |SI eaIP = "";
             |COPIA_FICHERO aOrigen, aDestino;
         |SINO;
             |RECIBE_FICHERO aOrigen, aDestino;
         |FINSI;

         #dscsvz10#4 = aDestino;  |PINTA #dscsvz10#4;
     |FINSI;
|FPRC;

|PROCESO Cancelar;
     |PTEC 807;
|FPRC;

|PROCESO ModAsiento;
     |FIN_CONTROL_WINDOWS nBtnUni;

     |SI aModAsiento = "SI";
         aModAsiento = "NO";
     |SINO;
         aModAsiento = "SI";
     |FINSI;

     aAlfa = "BOTON," + aModAsiento;
     |CONTROL_SIMPLE nPanta, aAlfa, 2116, 2319, ModAsiento; nBtnUni = FSalida;
|FPRC;

|PROCESO Instrucciones;
     |HAZ LeeUnidadBasico;

     eaNombrePDF = "dscsvz10";

     |HAZ MuestraPDF;
|FPRC;

|PROGRAMA;
     |LEE 000#camaasil.p;
     |SI FSalida = 0;
         |CONFI "0000NYa existe movimientos en esta empresa/periodo. Continuamos con la importaci¢n?";
         |SI FSalida ! 0;  |ACABA;  |FINSI;
     |SINO;
         |LEE 000#camaniva.p;
         |SI FSalida = 0;
             |MENAV "0000Ya existe movimientos de IVA en esta empresa/periodo. Cancelamos la importaci¢n";
             |ACABA;
         |FINSI;
     |FINSI;

     aTextoVentana = "Importaci¢n CSV Economics";
     |VENTANA 0501, 3599, -1, 16, aTextoVentana;
     n0Vent = FSalida;

     |PINPA #0#dscsvz10;  nPanta = FSalida;
     |PINDA #0#dscsvz10;

     aModAsiento = "NO";

     |CONTROL_SIMPLE nPanta, "LABEL,{{15}}Asientos e IVA", 2202, 2215, NULL;
     |CONTROL_SIMPLE nPanta, "LABEL,{{15}}se dejan modificables", 2221, 2240, NULL;

     aAlfa = "BOTON," + aModAsiento;
     |CONTROL_SIMPLE nPanta, aAlfa, 2116, 2319, ModAsiento; nBtnUni = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{137}}Instrucciones", 0585, 0599, Instrucciones;
     nBtnInfo = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}", 0785, 0787, Ubica; nBtnCar1 = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}", 1085, 1087, Ubica; nBtnCar2 = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}", 1385, 1387, Ubica; nBtnCar3 = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}", 1685, 1687, Ubica; nBtnCar4 = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}", 1985, 1987, Ubica; nBtnCar5 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{207}}Cancelar", 3570, 3582, Cancelar;
     nBtnCan = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::{{230}}Continuar";
          |LEETECLA aTecla;
          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
          |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |SI aTecla = aEsc2;
         |MENAV "0000Proceso cancelado.";
         |FINVENTANA n0Vent;
         |ACABA;
     |FINSI;

     |FIN_CONTROL_WINDOWS nBtnCar1;
     |FIN_CONTROL_WINDOWS nBtnCar2;
     |FIN_CONTROL_WINDOWS nBtnCar3;
     |FIN_CONTROL_WINDOWS nBtnCar4;
     |FIN_CONTROL_WINDOWS nBtnCar5;
     |FIN_CONTROL_WINDOWS nBtnCan;

     |HAZ Importacion;

     |FINVENTANA n0Vent;

     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
|FPRO;

|PROCESO Tipo12w18; |TIPO 12;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     nChequeo = 0;
     eaIP     = "";
     |IP_REMOTO eaIP;

     |HAZ LeeUnidadBasico;

     aPathLocal = eaUnidadBasico + "\documentos";   |RMKDIR aPathLocal;
     aPathLocal = aPathLocal + "\tmp";              |RMKDIR aPathLocal;
     aPathLocal = aPathLocal + "\";


     |DBASE aBaseConta;
     aPathTmp   = aBaseConta     + "tmp";           |MKDIR aPathTmp;
     aPathTmp   = aPathTmp  + "/" + Usuario;   |MKDIR aPathTmp;
     aPathTmp   = aPathTmp  + "/";

     aPathContagen = eaUnidadBasico + "\documentos";   |RMKDIR aPathContagen;
     aPathContagen = aPathContagen + "\contagen\";     |RMKDIR aPathContagen;

     aAlfa = aPathTmp + "cuentas.csv";       |FBORRA aAlfa;
     aAlfa = aPathTmp + "clientes.csv";      |FBORRA aAlfa;
     aAlfa = aPathTmp + "proveedores.csv";   |FBORRA aAlfa;
     aAlfa = aPathTmp + "asientos.csv";      |FBORRA aAlfa;
     aAlfa = aPathTmp + "codiva.csv";        |FBORRA aAlfa;
     aAlfa = aPathTmp + "iva.csv";           |FBORRA aAlfa;

     |ABRET #cacuenta;

     |HAZ inicio;

     |ABRET #caclipro;
     |ABRET #camaasic;
     |ABRET #camaasil;
     |ABRET #camaniva;
     |ABRET #caivalin;
     |ABRET #dsmom030;

     |PULSATECLA;
     |PULSATECLA;
|FPRC;

|PROCESO Tipo90;  |TIPO 90;
     aAlfa = aPathTmp + "cuentas.csv";       |FBORRA aAlfa;
     aAlfa = aPathTmp + "clientes.csv";      |FBORRA aAlfa;
     aAlfa = aPathTmp + "proveedores.csv";   |FBORRA aAlfa;
     aAlfa = aPathTmp + "asientos.csv";      |FBORRA aAlfa;
     aAlfa = aPathTmp + "codiva.csv";        |FBORRA aAlfa;
     aAlfa = aPathTmp + "iva.csv";           |FBORRA aAlfa;

     |CIERRAT #cacuenta;
     |CIERRAT #caclipro;
     |CIERRAT #camaasic;
     |CIERRAT #camaasil;
     |CIERRAT #camaniva;
     |CIERRAT #caivalin;
     |CIERRAT #dsmom030;

     |PULSATECLA;
     |PULSATECLA;
|FPRC;
