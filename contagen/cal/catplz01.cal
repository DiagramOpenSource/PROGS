|FICHEROS;
   catplz01 #0;

   :/modelos/def/dsmom030 #330;

   caivaasi #200;
   camaasic #1;
   camaasil #2;
   caclipro #4;
   cacuenta #5;

   caivases #13;
   calibros #10;
   caivalin #11;
   camaniva #12;
   cacuebal #15;

   catplw01 #101;
   catplw02 #102;
   catplw03 #103;
   catplw04 #104;
   catplw05 #105;
   catplw06;
   catplw2@ #1102;
   caenlace #92;

   catplm01 #111;

   &regisnif@ #709;
   :/basica/def/agim0003  #710;
   :/integral/def/dsam0103 #711;

   &poblaci@  #703;
   &sgpaises@ #718;

   ca8m0002 #422;
   ca8cumay #410;
|FIN;

|VARIABLES;
   &entrada = 1;

   nInkey    = 0;
   aAlfa     = "";
   aAlfa1    = "";
   aAlfa2    = "";
   aAlfa3    = "";
   aAlfa4    = "";
   nNume1    = 0;
   nNume2    = 0;
   nNume3    = 0;
   nNume4    = 0;
   nPara1    = 0;
   nModo     = 0;
   aParam    = "";
   aLong     = "";
   nLong     = 0;
   nHandle   = 0;

   Comodin   = "";
   aExcel    = "";
   nCopio    = 0;
   nSwGraba  = 0;
   aDirec    = "";
   aDirec0   = "";
   aDirec1   = "";
   aDirec2   = "";
   nEstado   = 0;
   nSw       = 0;
   nSwD      = 0;
   nSal      = 0;
   nHay      = 0;
   nSwHay    = 0;
   aFichero  = "";
   fFecUlt   = @;

   aQueQuiero = "";
   aNumCampos = "";
   nNumCampos = 0;
   aIDCampo   = "";
   nSwAlgun   = 0;
   nSwAlgCP   = 0;

   nIvas      = 0;
   aFecha     = "";
   nDiario    = 0;
   nLibro     = 0;
   nConcepto  = 0;
   nConceRet  = 0;
   aSerie     = "";
   nFactura   = 0;
   aFactura   = "";
   aTApunte   = "";
   nNumApte20 = 0;
   nNumApte   = 0;
   nNumIva    = 0;
   nNumRec    = 0;
   nNumRet    = 0;
   aDesReten  = "";
   nApunte    = 0;
   nDocum     = 0;
   nDocumSec  = 0;
   aDef       = "";
   aMas       = "";
   nUltDoc    = 0;

   nNumTApte  = 0;
   nNumTIva   = 0;
   nNumTRec   = 0;
   nNumTRet   = 0;
   nNumCIva   = 0;
   nSwIntra   = 0;
   nSwDeFora  = 0;
   aCtaIVA    = "";
   nYaPaso    = 0;
   nApunteF   = 0;
   nLaFactura = 0;
   aParam1    = "";
   aParam2    = "";
   aParam3    = "";
   aIVA1      = "";
   aIVA2      = "";
   aIVA3      = "";
   aIVA4      = "";
   aREC1      = "";
   aREC2      = "";
   aREC3      = "";
   aREC4      = "";
   aRET1      = "";
   aRET2      = "";
   aRET3      = "";
   aRET4      = "";
   aCdTr      = "";
   aCdT1      = "";
   aCdT2      = "";
   aCdT3      = "";
   aCdT4      = "";
   aDeduc     = "";
   aCtaNoDed  = "";
   aCtaNoDe1  = "";
   aCtaNoDe2  = "";
   aCtaNoDe3  = "";
   aCtaNoDe4  = "";

   nSwError   = 0;
   nSwSerie   = 0;
   nLetras    = 0;
   aCuenta    = "";
   nLineaMa l = 0;
   nLinFic    = 0;
   aDepar     = "";
   aHora      = "";
   aUsuario   = "";
   aUsuari1   = "";

   aPrograma  = "";
   aDirLocal  = "";
   aBorra     = "";
   aAbre      = "";
   aLinea     = "";
   aRuta      = "";
   nCampo     = 0;
   aNombreRea = "";
   nContiene  = 0;
   aContiene1 = "";
   aFicDestino = "";
   aFicOrigen  = "";
   aDocRemoto  = "";
   nCarac     = 0;
   nPos       = 0;
   aCaracter  = "";
   aDocServidor = "";
   {1}aContiene  = "";
   aIPRemoto  = "";

   nImpresora = 0;
   nError     = 0;
   informe    = "catplw05";
   nBase      = 0;
   aNuevaFra  = "";

   nLetraSerie = 0;
   nOpera      = 0;
   nSepara     = 0;

   nSumaAsto   = 0;
   nSumaFras   = 0;
   nSumaClPr   = 0;
   nSumaCtas   = 0;
   nTotalBase  = 0;
   nTotalCuota = 0;
   nTotalFras  = 0;
   nTotalReten = 0;

   &aHoja          = "";
   &efFecha        = @;
   &enNoModif      = 0;
   &eSwErrorExcel  = 0;
   &eSwPlusGes     = 0;
   &eaUnidadBasico = "";
   &eaDirServidor  = "";
   &eaOrigen       = "";
   &eaDestino      = "";
   &Local          = 0;
   &enTFich        = 0;
   &eaPath         = "";
   &eaPathExc      = "";
   &eaFichero      = "";
   &eaNomOri       = "";
   &PRMNT_enError  = 0;
|FIN;

|INCLUYE dscont_i;
|INCLUYE defec1_i;
|INCLUYE dscoiv_i;
|INCLUYE cla340_i;

|| ///////////////////////////////////  Fichero Nuevo de Historico
|PROCESO VerEste;
     #111#0 = eaNomOri;
     aAlfa  = #111#0;

     nLinFic = 0;
     #111#0 = aAlfa;
     #111#1 = 99;
     |LEE 041#111];
     |SI FSalida ! 0;
         |LEE 041#111u;
     |SINO;
         |LEE 041#111a;
     |FINSI;
     |SI FSalida = 0; |Y #111#0 = aAlfa;
         nLinFic = #111#1;
     |FINSI;
|FPRC;

|PROCESO CrearHist111;
      aUsuario = Usuario % 108;
      aUsuari1 = Usuario % 840;
      |ABRE #111;
      |HAZ VerEste;

      |DDEFECTO #111;
      #111#0 = eaNomOri;
      #111#1 = nLinFic + 1;
      #111#2 = #0#3;
      |SI nSwAlgCP ! 0; #111#3 = "SI"; |FINSI;
      |SI nSwAlgun ! 0; #111#4 = "SI"; #111#6 = "SI"; |FINSI;
      #111#7  = ~;
      |HORA aHora;
      #111#8  = aHora;
      #111#9  = aUsuario;
      #111#10 = aUsuari1;
      #111#11 = enEjercicio;
      #111#12 = enEmpresa;
      #111#13 = enPeriodo;

      #111#14 = nSumaAsto;
      #111#15 = nSumaFras;
      #111#16 = nTotalBase;
      #111#17 = nTotalCuota;
      #111#18 = nTotalReten;
      #111#19 = nTotalFras;
      #111#20 = nSumaClPr;
      #111#21 = nSumaCtas;

      |GRABA 020#111n;
      |LIBERA #111;
      |CIERRA #111;

      |PARA nPara1 = 0; |SI nPara1 [ nNumCampos; |HACIENDO nPara1 = nPara1 + 1;
            #catplw06#nPara1# = #catplm01#nPara1#;
      |SIGUE;
      |PINPA #0#catplw06;
      |PINDA #0#catplw06;
      |PAUSA;
|FPRC;

|PROCESO ClaveBaja111;
     #111#0 = "                                        ";
     #111#1 = 01;
     #111#7 = "01.01.1900";
     #111#8 = "     ";
|FPRC;

|PROCESO ClaveAlta111;
     #111#0 = "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz";
     #111#1 = 99;
     #111#7 = "31.12.2099";
     #111#8 = "zzzzz";
|FPRC;

|| ////////////////////////////////////////////////////////////////////////

|CALCULO PonFichero; |TIPO 0;
     nInkey = FSalida;

     |SI nInkey = 11;
         |ABRE #111;
         #111#0 = #0#3;
         |CONSULTA_F_CLAVE #2#111, ClaveBaja111, ClaveAlta111;
         |CIERRA #111;
         |ERROR;
         |ACABA;
     |FINSI;

     Comodin = "";
     |IP_REMOTO Comodin; |QBF Comodin;
     |SI Comodin = "";
         Local = 1;
      |SINO;
         Local = 0;
     |FINSI;

     |SI nInkey = 10;
         |HAZ Browse;
         #0Campo = aRuta; || eaOrigen;
     |FINSI;
     |PINTA #0Campo;

     aAlfa3 = #0Campo; |QBF aAlfa3;
     |SI aAlfa3 = "";
         |MENAV "0000Error. Introduzca Fichero a Importar";
         |ERROR;
         |ACABA;
     |FINSI;

     |ABRE #111;
     |HAZ VerEste;
     |CIERRA #111;
     |SI nLinFic ! 0;
         aAlfa = "0000NFichero Ya importado con Fecha " + #111#7;
         aAlfa = aAlfa + " .Desea volverlo a Importar";
         |CONFI aAlfa;
         |SI FSalida ! 0;
             #0Campo = ""; |PINTA #0Campo;
             |ERROR;
         |FINSI;
     |FINSI;
|FCAL;

|CALCULO TrasLibro; |TIPO 6;
   |C_M #0Campo, 0, aAlfa;
   |SI aAlfa = "N"; |ACABA; |FINSI;

   |ABRE #10;
   #10#0 = #0Campo;
   |LEE 010#10=;
   |SI FSalida ! 0;
      |MENAV "     No existe el libro";
      |CIERRA #10;
      |ERROR;
      |ACABA;
   |FINSI;
   |CIERRA #10;
   |SI Campo = 16; |Y #10#2 ! "R";
       |MENAV "     Libro de IVA No Correcto. No es de Repercutido";
       |ERROR;
       |ACABA;
   |FINSI;
   |SI Campo = 17; |Y #10#2 ! "S";
       |MENAV "     Libro de IVA No Correcto. No es de Soportado";
       |ERROR;
       |ACABA;
   |FINSI;
|FCAL;

|CALCULO TrasConcepto;  |TIPO 0;
   |C_M #0Campo, 0, aAlfa;
   |SI aAlfa = "N"; |ACABA; |FINSI;

   |SI Campo = 18;
       enLibIVA = #0#16;
       aTipoIVA = "R";
   |FINSI;
   |SI Campo = 19;
       enLibIVA = #0#17;
       aTipoIVA = "S";
   |FINSI;

   enFilConc = 2;          || Concepto de IVA
   eaCuenta = " ";
   eaReperc = " ";
   enConcepto = #0Campo;
   |HAZ FiltroConcepto;  |SI eswLect ! 0; |ACABA; |FINSI;
   nNume1 = Campo + 2;
   #0nNume1 = #dsmom030#1; |PINTA #0nNume1;
|FCAL;

|PROCESO QueHago;

   aAlfa1 = #0Campo;
   |SI Campo = 5;
       |C_M #0#14, 1, aAlfa1;
       |C_M #0#15, 1, aAlfa1;
       |C_M #0#16, 1, aAlfa1;
       |C_M #0#17, 1, aAlfa1;
       |C_M #0#18, 1, aAlfa1;
       |C_M #0#19, 1, aAlfa1;
   |FINSI;
   |SI Campo = 4;
       |C_M #0#12, 1, aAlfa1;
   |FINSI;
|FPRC;

|PROCESO Tipo12_z; |TIPO 12;
   || .... Para que no pregunte conforme S/N
|FPRC;

|| //////////////////////////////////////////////////////

|PROCESO excelHoja;
     |SUB_EJECUTA_NP "catplz03";
     |SI eSwErrorExcel = 0;
         nSwHay = 1;
     |FINSI;
|FPRC;

|PROCESO excel;
     |SI #0#4 = "S";
         aHoja = #0#8; |QBF aHoja;
         |SI aHoja ! "";
             enTFich = 1; || Clientes/Proveedores
             |HAZ excelHoja;
         |FINSI;
     |FINSI;
     |SI #0#5 = "S";
         aHoja = #0#9; |QBF aHoja;
         |SI aHoja ! "";
             enTFich = 2; || Apuntes
             |HAZ excelHoja;
         |FINSI;
     |FINSI;
     |SI #0#6 = "S";
         aHoja = #0#10; |QBF aHoja;
         |SI aHoja ! "";
             enTFich = 3; || Efectos
             |HAZ excelHoja;
         |FINSI;
     |FINSI;
     |SI #0#5 = "S";
         aHoja = #0#11; |QBF aHoja;
         |SI aHoja ! "";
             enTFich = 4; || Facturas
             |HAZ excelHoja;
         |FINSI;
     |FINSI;
|FPRC;

// *********************************************************************
|| //////////////////////// Pase Clientes y Proveedores
|PROCESO LaDivision;
     aDirec0 = "";
     aDirec1 = aDirec;
     aDirec2 = "";

     nSwD   = 0;
     aAlfa1 = aDirec % 103;
     |SI aAlfa1 = "CL ";  nSwD = 1; |FINSI;
     |SI aAlfa1 = "AV ";  nSwD = 1; |FINSI;
     |SI aAlfa1 = "PA ";  nSwD = 1; |FINSI;
     |SI aAlfa1 = "PZ ";  nSwD = 1; |FINSI;
     |SI aAlfa1 = "PL ";  nSwD = 1; |FINSI;
     |SI aAlfa1 = "CR ";  nSwD = 1; |FINSI;
     |SI nSwD = 1;
         aDirec0 = aAlfa1;
         aDirec1 = aDirec % 440;
     |FINSI;
|FPRC;

|PROCESO CreaFicNif;

    #709#0 = eaCodNif;
    |LEE 141#709=;
    nEstado = FSalida;
    |SI nEstado ! 0; |O #0#12 = "S";

        |SI nEstado ! 0;
            |DDEFECTO #709;
        |FINSI;

        #709#0  = eaCodNif;   || Nif
        #709#1  = #4#1;       || Nombre y Apellidos

        aDirec  = #4#2;
        |HAZ LaDivision;
        #709#2  = aDirec1;    || Direccion
        #709#3  = aDirec2;    || No tengo el numero

        #709#4  = #4#3;       || Codigo Provincia
        #709#5  = #4#4;       || Codigo Poblacion
        #709#6  = #4#6;       || Poblacion
        #709#7  = #4#5;       || Provincia
        #709#8  = #4#7;       || Telefono
        #709#17 = #4#8;       || Fax
        #709#9  = aDirec0;    || SG
                              || #709#10 =  Piso
                              || #709#11 =  Puerta
                              || #709#16 =  Escalera
        #709#12 = #4#24;      || Codigo Pais
        #709#13 = #101#12;    || Nombre Pais
                              || #709#14 = Codigo Comunidad
                              || #709#15 = Nombre Comunidad
        |SI enSwDeDonde = 1;
            enCodProv = #1#3;
            enCodPobl = #1#4;
            enSwMensa = 1;
            |HAZ LeeLocalidad;
            enSwMensa = 0;
            #709#14 = #703#4;
            #709#15 = #703#5;
        |FINSI;

        |SI nEstado ! 0; |GRABA 020#709n; |FINSI;
        |SI nEstado = 0; |GRABA 020#709a; |FINSI;
    |FINSI;

    |LIBERA #709;
|FPRC;

|PROCESO LeeAux101;
   aAlfa  = #101#5;
   aAlfa1 = #101#2 % 102;
   |SI aAlfa ! "C"; |Y aAlfa ! "P";
       |SI aAlfa1 = "40"; |O aAlfa1 = "41";
           aAlfa = "P";
       |FINSI;
       |SI aAlfa1 = "43"; |O aAlfa1 = "44";
            aAlfa = "C";
       |FINSI;
   |FINSI;

   nSwGraba = 1;
   |SI aAlfa = "C"; |O aAlfa = "P";
       nSwGraba = 0;
       #4#23 = aAlfa;
       #4#10 = #101#2;
       |LEE 141#4=;
       |SI FSalida ! 0;
           |DDEFECTO #4;
           #4#23 = aAlfa;
           #4#10 = #101#2;
           |GRABA 020#4b;
       |SINO;
           |SI #0#12 = "N"; nSwGraba = 1; |FINSI;
       |FINSI;
   |FINSI;

   |SI nSwGraba = 0;
       nSumaClPr = nSumaClPr + 1;
       eaCuenta = #4#10;
       |HAZ SacaNivel;
       #4#11 = enNivel;
       #4#0  = #4#10;
       #4#1  = #101#3;
       #4#2  = #101#8;
       aAlfa1 = #101#9;
       nNume1 = aAlfa1;
       |SI nNume1 ! 0;
           aAlfa1 = ("00000" + nNume1) % -105;
           aAlfa2 = aAlfa1 % 0102;  #4#3  = aAlfa2;
           aAlfa2 = aAlfa1 % -0103; #4#4  = aAlfa2;
       |FINSI;
       #4#5  = #101#11;
       #4#6  = #101#10;
       #4#7  = #101#13;
       #4#9  = #101#4;
       #4#12 = #101#17;
       #4#20 = #101#19;
       #4#21 = #101#15;
       eaCuenta = #4#21; |HAZ SacaNivel;
       #4#22 = enNivel;
       #4#24 = #101#6;
       #4#25 = (("00" + #4#3) % -102) + (("000" + #4#4) % -103)
       #4#42 = #101#17;
       #4#51 = #101#14;
       #4#52 = #101#18;
       |GRABA 020#4a;
       nSwAlgCP = 1;

       eaCodNif = #4#9; |QBF eaCodNif;
       |SI eaCodNif ! "";
           |HAZ CreaFicNif;
       |FINSI;
   |FINSI;
   |LIBERA #4;

   nSwGraba = 0;
   #5#0 = #101#2;
   |LEE 141#5=;
   |SI FSalida ! 0;
       |DDEFECTO #5;
       #5#0 = #101#2;
       |GRABA 020#5b;
   |SINO;
       |SI #0#12 = "N"; nSwGraba = 1; |FINSI;
   |FINSI;
   |SI nSwGraba = 0;
       nSumaCtas = nSumaCtas + 1;
       eaCuenta = #5#0;
       |HAZ SacaNivel;
       #5#1 = enNivel;
       #5#2 = #101#3;
       #5#3 = "S";

       mascara = #5#0; |QBF mascara;
       mascara = mascara + "zzzzzzzzzzzz";
       mascara = mascara % 112;

       #5#54 = mascara;
       #5#55 = #5#1;
       |HAZ defect1;

       |GRABA 020#5a;
       nSwAlgCP = 1;

   |FINSI;
   |LIBERA #5;

   nSw = 1;
|FPRC;

|DEFBUCLE LeeAux101;
   #101#1;
   ;
   0000001;
   9999999;
   ;
   NULL, LeeAux101;
|FIN;

|PROCESO PasoClieProv;
    |ABRE #5;
    |ABRE #4;
    |SI ewNif = 0; |ABRE #709; |FINSI;

    |BUCLE LeeAux101;

    |SI ewNif = 0; |CIERRA #709; |FINSI;
    |CIERRA #4;
    |CIERRA #5;
|FPRC;

|| //////////////////////// Pase Apuntes
|PROCESO CalSerieNum;
     nOpera   = 1;
     |SI aSerie ! "";
         |HAZ MiraLetras;
         |SI nLetras ! 0;
             aFactura = aFactura - aSerie;
         |FINSI;
         |SI nSepara = 1;
             aFactura = aFactura - aSepSer;
         |FINSI;
     |FINSI;

     nFactura = aFactura;
     |SI nFactura > 999999;
         aAlfa    = nFactura;
         aAlfa1   = aAlfa % 0; nNume1 = aAlfa1;
         aAlfa1   = aAlfa % -106;  ||  % 1110
         nFactura = aAlfa1;
         |SI #200#44 = "S"; |Y aSerie = "";
             nNume1 = nNume1 - 7;
             nNume1 = 100 + nNume1;
             aSerie = aAlfa % nNume1;
         |FINSI;
     |FINSI;
     |SI nSwSerie = 0; aSerie = ""; |FINSI;
|FPRC;

|PROCESO MiraLetras;
     aSepSer = #caivaasi#97; |QBF aSepSer;
     aAlfa1  = aSerie - aSepSer;
     |SI FSalida ! 0;
         aSepSer = "";
     |FINSI;
|| ... Miro si la serie tambien son solo letras
    |SI nOpera = 1;
        aNuevaFra   = "";
        nLetraSerie = 0;

        |SI aSerie ! "";
            aAlfa1  = aSerie % 0;
            nNume1  = aAlfa1;
            |PARA nPara1 = 1; |SI nPara1 [ nNume1; |HACIENDO nPara1 = nPara1 + 1;
                  nNume2 = (100 * nPara1) + 01;
                  aAlfa3 = aSerie % nNume2;
                  |SI aAlfa3 ! "0"; |Y aAlfa3 ! "1"; |Y aAlfa3 ! "2";
                    |Y aAlfa3 ! "3"; |Y aAlfa3 ! "4"; |Y aAlfa3 ! "5";
                     |Y aAlfa3 ! "6"; |Y aAlfa3 ! "7"; |Y aAlfa3 ! "8";
                       |Y aAlfa3 ! "9";
                          nLetraSerie = 1;
                          |SAL;
                  |FINSI;
            |SIGUE;
            |SI nLetraSerie = 0;  || la serie son solo numeros
                nNume1 = 100 + nNume1;
                aNuevaFra = aFactura % nNume1; |QBF aNuevaFra;
            |FINSI;
        |FINSI;
    |FINSI;

    nSepara = 0;
    nLetras = 0;
    aAlfa1  = aFactura % 0;
    nNume1  = aAlfa1;
    aAlfa2 = "";
    |PARA nPara1 = 1; |SI nPara1 [ nNume1; |HACIENDO nPara1 = nPara1 + 1;
          nNume2 = (100 * nPara1) + 01;
          aAlfa3 = aFactura % nNume2;
          |SI aAlfa3 ! "0"; |Y aAlfa3 ! "1"; |Y aAlfa3 ! "2";
            |Y aAlfa3 ! "3"; |Y aAlfa3 ! "4"; |Y aAlfa3 ! "5";
             |Y aAlfa3 ! "6"; |Y aAlfa3 ! "7"; |Y aAlfa3 ! "8";
               |Y aAlfa3 ! "9"; |Y aAlfa3 ! aSepSer;
                  nLetras = 1;
                  |SAL;
          |SINO;
               |SI aSepSer = aAlfa3;
                   |SI nSepara = 1;
                       nLetras = 1;
                       |SAL;
                   |SINO;
                       nSepara = 1;
                   |FINSI;
               |FINSI;
               aAlfa2 = aAlfa2 + aAlfa3;
          |FINSI;
    |SIGUE;


    |SI nLetras = 0; |Y nLetraSerie = 0; |Y nSepara = 1; |Y aSerie ! ""; |Y nOpera = 1;
        nLetras = 1;
    |FINSI;
    nOpera = 0;
|FPRC;

|PROCESO PasoPrimeroF;
 |SALVA_DATOS #102;
 |PARA nPara1 = 0; |SI nPara1 [ 64; |HACIENDO nPara1 = nPara1 + 1;
       #102nPara1 = #1102nPara1;
 |SIGUE;
 aFecha    = #102#3;
 nDocumSec = #102#4;
 |HAZ ElBuenApunte;
 nLaFactura = #1102#0;
 |REPON_DATOS #102;
|FPRC;

|PROCESO  BuscaNumero;
   |SI aTipoIVA = "R";
       aSerie   = #104#7; |QBF aSerie;
       aFactura = #104#6; |QBF aFactura;
       |HAZ CalSerieNum;
   |SINO;
       aSerie = #104#7;
       |SI #caivaasi#44 = "N";
           |ABRE #calibros;
           #calibros#0 = nLibro;
           |LEE 101#calibros.=;
           |SI FSalida = 0;
               nFactura = #calibros#4;
               #calibros#4 = #calibros#4 + #calibros#3;
               |GRABA 020#calibros.a;
               |LIBERA #calibros;
           |FINSI;
           |CIERRA #calibros;
       |SINO;
           |ABRE #caivases;
           #caivases#0 = #104#7;
           #caivases#1 = aTipoIVA;
           |LEE 101#caivases.=;
           |SI FSalida = 0;
               nFactura = #caivases#3;
               #caivases#3 = #caivases#3 + #caivases#4;
               |GRABA 020#caivases.a;
               |LIBERA #caivases;
           |FINSI;
           |CIERRA #caivases;
       |FINSI;
   |FINSI;
|FPRC;

|PROCESO LeoFicha;
   |DDEFECTO #4;
   #4#23 = "P";
   #4#10 = eaCuenta;
   |LEE 041#4=;
   |SI FSalida ! 0;
       #4#23 = "C";
       #4#10 = eaCuenta;
       |LEE 041#4=;
   |FINSI;
   |SI #4#24 = "   "; |O #4#24 = "ES "; |O #4#24 = "ESP";
       nSwDeFora = 1;
   |FINSI;
|FPRC;

|PROCESO LeeOtraVez102;

   aAlfa = #1102#13; |QBF aAlfa;
   |SI aAlfa ! "";
       eaCuenta = #1102#6;
       eaCodNif = #1102#40;
       |HAZ LeoFicha;
       aAlfa = #1102#39; |QBF aAlfa;
       aTipoIVA = "R";
       nLibro    = #0#16;
       nConcepto = #0#18;
       nConceRet = 0;
       aDesReten = "";
       |SI nNumTRet ! 0;
           nConcepto = #0#22;
           nConceRet = #0#23;
           aDesReten = #0#26;
       |FINSI;
       |SI aAlfa = "P";
           aTipoIVA = "S";
           nLibro    = #0#17;
           nConcepto = #0#19;
           |SI nNumTRet ! 0;
               nConcepto = #0#24;
               nConceRet = #0#25;
               aDesReten = #0#26;
           |FINSI;
       |FINSI;
       aAlfa = #1102#50; |QBF aAlfa;
       |SI aAlfa ! "";
           nNume1 = #1102#50;
           |SI nNume1 > 0; nConcepto = #1102#50; |FINSI;
       |FINSI;

       aAlfa = #1102#49; |QBF aAlfa;
       |SI aAlfa ! "";  aDepar = aAlfa; |FINSI;

       |SI nSwIntra = 1;
           |SI aTipoIVA = "R"; nConcepto = #0#27; |FINSI;
           |SI aTipoIVA = "S";
               nConcepto = #0#28;
               |SI nSwDeFora = 1;  nConcepto = #0#29; |FINSI;
           |FINSI;
           nYaPaso = 1;
       |FINSI;
       |HAZ BuscaNumero;
       aCdT1 = #1102#50;
       aCdT2 = #1102#51;
       aCdT3 = #1102#52;
       aCdT4 = #1102#53;

       |SI #102#0 ! #1102#0;
           |HAZ PasoPrimeroF;
       |FINSI;

   |SINO;

       aAlfa3 = #1102#6; |QBF aAlfa3;
       aAlfa3 = aAlfa3 % 103;
       |SI aAlfa3 = "472"; |O aAlfa3 = "477";
           |SI aCtaIVA = ""; aCtaIVA = aAlfa3; |FINSI;
           |SI aCtaIVA ! aAlfa3;
               nSwIntra = 1;
           |FINSI;
       |FINSI;
       aAlfa1 = #1102#6; |QBF aAlfa1;
       aAlfa2 = aAlfa1 % 102;
       aAlfa1 = aAlfa1 % 101;
       |SI aAlfa1 = "6"; |O aAlfa1 = "7"; |O aAlfa1 = "2"; |O aAlfa2 = "52";
           nBase = nBase + 1;
           |SI nBase = 1; aCtaNoDe1 = #1102#6; |FINSI;
           |SI nBase = 2; aCtaNoDe2 = #1102#6; |FINSI;
           |SI nBase = 3; aCtaNoDe3 = #1102#6; |FINSI;
           |SI nBase = 4; aCtaNoDe4 = #1102#6; |FINSI;
       |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE LeeOtraVez102;
   #1102#2002;
   ;
   #102#4, #102#3;
   #102#4, #102#3;
   ;
   NULL, LeeOtraVez102;
|FIN;

|PROCESO LeeAux104;
    |SELECT #2#104;
    #104#3 = #102#64;
    |LEE 041#104=;
    |SI FSalida = 0;

        nIvas = 1;
        |PARA nPara1 = 17; |SI nPara1 [ 38; |HACIENDO nPara1 = nPara1 + 7;
                                   |SI #104nPara1 ! 0;
                                       nNumTIva = nNumTIva + 1;
                                   |FINSI;
              nNume1 = nPara1 + 1; |SI #104nNume1 ! 0;
                                       nNumCIva = nNumCIva + 1;
                                       |SI nPara1 = 17; aIVA1 = "x"; |FINSI;
                                       |SI nPara1 = 24; aIVA2 = "x"; |FINSI;
                                       |SI nPara1 = 31; aIVA3 = "x"; |FINSI;
                                       |SI nPara1 = 38; aIVA4 = "x"; |FINSI;
                                   |FINSI;
              nNume1 = nPara1 + 2; |SI #104nNume1 ! 0;
                                       nNumTRec = nNumTRec + 1;
                                       |SI nPara1 = 17; aREC1 = "x"; |FINSI;
                                       |SI nPara1 = 24; aREC2 = "x"; |FINSI;
                                       |SI nPara1 = 31; aREC3 = "x"; |FINSI;
                                       |SI nPara1 = 38; aREC4 = "x"; |FINSI;
                                   |FINSI;
              nNume1 = nPara1 + 4; |SI #104nNume1 ! 0;
                                       nNumTRet = nNumTRet + 1;
                                       |SI nPara1 = 17; aRET1 = "x"; |FINSI;
                                       |SI nPara1 = 24; aRET2 = "x"; |FINSI;
                                       |SI nPara1 = 31; aRET3 = "x"; |FINSI;
                                       |SI nPara1 = 38; aRET4 = "x"; |FINSI;
                                   |FINSI;
        |SIGUE;

        nBase = 0;
        |BUCLE LeeOtraVez102;
        |SI nSwIntra = 1;
           |SI aTipoIVA = "R"; nConcepto = #0#27; |FINSI;
           |SI aTipoIVA = "S";
               nConcepto = #0#28;
               |SI nSwDeFora = 1;  nConcepto = #0#29; |FINSI;
           |FINSI;
        |FINSI;
        |HAZ LaClau340;
        nSumaFras = nSumaFras + 1;
    |FINSI;
    |SELECT #1#104;
|FPRC;

|PROCESO LaClau340;
   |SI nConcepto ! 600; |Y nConcepto ! 700;
       |ABRE #330;
       |DDEFECTO #330;
       #330#0 = nConcepto;
       |LEE 041#330=;
       aClau340 = "";
       nNumFras = 1;
       |HAZ ClauDefecto;
   |FINSI;
|FPRC;

|PROCESO PasaEnlace;
 |SI aFecha ! #102#3; |O nDocumSec ! #102#4;
     aFecha     = #102#3;
     nDocumSec  = #102#4;
     nApunte    = -9;
     nDocum     = nDocum + 1;
     nLaFactura = 0;
     nApunteF   = 0;

     aDepar     = eaDfDepC;
     aClau340   = "";
     nYaPaso    = 0;
     nSwIntra   = 0;
     nSwDeFora  = 0;
     aTipoIVA   = "";
     nIvas      = 0;
     nNumApte   = 0;  nNumTApte  = 0;
     nNumIva    = 0;  nNumTIva   = 0;
     nNumCIva   = 0;  aCtaIVA    = "";
     nNumRec    = 0;  nNumTRec   = 0;
     nNumRet    = 0;  nNumTRet   = 0;
     aIVA1      = ""; aIVA2      = "";
     aIVA3      = ""; aIVA4      = "";
     aREC1      = ""; aREC2      = "";
     aREC3      = ""; aREC4      = "";
     aRET1      = ""; aRET2      = "";
     aRET3      = ""; aRET4      = "";
     aCdTr      = "";
     aCdT1      = ""; aCdT2      = "";
     aCdT3      = ""; aCdT4      = "";
     aCtaNoDed  = "";
     aCtaNoDe1  = ""; aCtaNoDe2 = "";
     aCtaNoDe3  = ""; aCtaNoDe4 = "";
     nSumaAsto  = nSumaAsto + 1;

     aAlfa1 = #102#64; |QBF aAlfa1;
     |SI aAlfa1 ! "";
         |HAZ LeeAux104;
     |FINSI;
 |FINSI;

 aFecha    = #102#3;
 nDocumSec = #102#4;
 |SI nLaFactura = #102#0;
     |SI nSwIntra = 1; |Y nYaPaso = 0;
         |HAZ ActualApteF;
     |FINSI;
     |ACABA;
 |FINSI;

 |SI nIvas = 0;
     |HAZ GraboLinea;
     |ACABA;
 |SINO;
     |HAZ ElBuenApunte;
 |FINSI;
|FPRC;

|PROCESO ActualApteF;
    |SI nApunteF ! 0;
        #92#0  = nDiario;   || Diaro
        #92#1  = aFecha;    || Fecha
        #92#2  = nDocum;    || Documento
        #92#3  = nApunteF;
        #92#37 = enEmpresa;
        #92#38 = enPeriodo;
        |LEE 141#92=;
        |SI FSalida = 0;
            #92#6 = nConcepto;
            |SI nSwIntra = 1;
                #92#30 = "S";
                |SI #92#6 = #0#29; #92#30 = "N"; #92#48 = "I"; |FINSI;
            |FINSI;
            |GRABA 020#92a;
        |FINSI;
        |LIBERA #92;
    |FINSI;
|FPRC;

|PROCESO ElBuenApunte;

 |SI #102#10 = 0; |ACABA; |FINSI;

 aTApunte   = "F";
 nNumApte20 = 0;

 aAlfa    = #102#6;
 aAlfa2   = aAlfa % 102;
 aAlfa1   = aAlfa % 101;
 |SI aAlfa1 = "6"; |O aAlfa1 = "7"; |O aAlfa1 = "2"; |O aAlfa2 = "52";
     aTApunte = "B";
     |SI aAlfa1 ! "7"; |Y #102#9 = "H"; aTApunte = "b"; |FINSI;
     |SI aAlfa1 = "7"; |Y #102#9 = "D"; aTApunte = "b"; |FINSI;
     nNumApte   = nNumApte + 1;
     nNumApte20 = nNumApte;
 |FINSI;

 aAlfa3 = aAlfa % 103;
 |SI aAlfa3 = "472"; |O aAlfa3 = "477";

     aTApunte = "I";
     |SI aAlfa3 = "472"; |Y #102#9 = "H"; aTApunte = "i"; |FINSI;
     |SI aAlfa3 = "477"; |Y #102#9 = "D"; aTApunte = "i"; |FINSI;

     |SI nSwIntra = 0;
         nNumIva    = nNumIva + 1;
         nNumApte20 = nNumIva;
         |SI nNumIva > nNumTIva;
             nNumIva = nNumTIva;
             |SI nNumTRec ! 0;
                 |SI aTApunte = "I";
                     aTApunte = "R";
                 |SINO;
                     aTApunte = "r";
                 |FINSI;
                 nNumRec = nNumRec + 1;
                 |SI nNumRec > nNumTRec; nNumRec = nNumTRec; |FINSI;
                 nNumApte20 = nNumRec;
             |SINO;
                 nNumApte20 = nNumIva;
             |FINSI;
         |FINSI;
     |FINSI;

     |SI nSwIntra = 1;
         |SI aAlfa3 = aCtaIVA;
             nNumIva = nNumIva + 1;
         |FINSI;
         nNumApte20 = nNumIva;
     |FINSI;
 |FINSI;

 aAlfa2 = aAlfa % 102;
 |SI aTApunte = "F";
     |SI #102#36 ! 0;
         |SI aAlfa2 ! "43"; |Y aAlfa2 ! "44"; |Y aAlfa2 ! "40"; |Y aAlfa2 ! "41";
             aTApunte = "P";
             nNumRet = nNumRet + 1;
             |SI nNumRet > nNumTRet; nNumRet = nNumTRet; |FINSI;
             nNumApte20 = nNumRet;
         |FINSI;
     |FINSI;
 |FINSI;

 |HAZ GraboLinea;
|FPRC;

|PROCESO VeoNumeroBueno;

     |SI nSwIntra = 1; |ACABA; |FINSI;

     |SI aAlfa1 = "I";
         |SI #92#27 [ 1; |Y aIVA1 = "";
             #92#27 = 2; nNumIva = nNumIva + 1; nNumTIva = nNumTIva + 1;
         |FINSI;
         |SI #92#27 = 2; |Y aIVA2 = "";
             #92#27 = 3; nNumIva = nNumIva + 1; nNumTIva = nNumTIva + 1;
         |FINSI;
         |SI #92#27 = 3; |Y aIVA3 = "";
             #92#27 = 4; nNumIva = nNumIva + 1; nNumTIva = nNumTIva + 1;
         |FINSI;
         |SI nNumTIva > 4; nNumTIva = 4; |FINSI;
     |FINSI;
     |SI aAlfa1 = "R";
         |SI #92#27 [ 1; |Y aREC1 = "";
             #92#27 = 2; nNumRec = nNumRec + 1; nNumTRec = nNumTRec + 1;
         |FINSI;
         |SI #92#27 = 2; |Y aREC2 = "";
             #92#27 = 3; nNumRec = nNumRec + 1; nNumTRec = nNumTRec + 1;
         |FINSI;
         |SI #92#27 = 3; |Y aREC3 = "";
             #92#27 = 4; nNumRec = nNumRec + 1; nNumTRec = nNumTRec + 1;
         |FINSI;
         |SI nNumTRec > 4; nNumTRec = 4; |FINSI;
     |FINSI;
     |SI aAlfa1 = "P";
         |SI #92#27 [ 1; |Y aRET1 = "";
             #92#27 = 2; nNumRet = nNumRet + 1; nNumTRet = nNumTRet + 1;
         |FINSI;
         |SI #92#27 = 2; |Y aRET2 = "";
             #92#27 = 3; nNumRet = nNumRet + 1; nNumTRet = nNumTRet + 1;
         |FINSI;
         |SI #92#27 = 3; |Y aRET3 = "";
             #92#27 = 4; nNumRet = nNumRet + 1; nNumTRet = nNumTRet + 1;
         |FINSI;
         |SI nNumTRet > 4; nNumTRet = 4; |FINSI;
     |FINSI;
|FPRC;

|PROCESO GraboLinea;

 nApunte = nApunte + 10;
 |HAZ Graba92;

 #92#4 = #102#6;
 eaCuenta = #92#4;
 |HAZ SacaNivel;
 #92#5 = enNivel;
 #92#6 = nConcepto;  || Concepto
 #92#7 = #102#5;     || Descripcion
 #92#8 = #102#9;     || Signo
 #92#9 = #102#10;    || Importe

 |SI nIvas = 1;
     #92#10 = aTipoIVA;
     #92#11 = nLibro;
     #92#12 = aSerie;
     #92#13 = nFactura;
     #92#26 = aTApunte;
     #92#14 = 0;
     #92#27 = nNumApte20;
     |SI nSwIntra = 1;
         #92#30 = "S";
         |SI #92#6 = #0#29;
             #92#30   = "N";
             aClau340 = "I";
         |FINSI;
     |FINSI;
     #102#48 = aClau340;
 |FINSI;

 #92#15 = aDepar;
 #92#19 = Usuario;  || Usuario
 #92#48 = #102#48;
 #92#49 = aDepar;
 #92#50 = eaDfSubC;
 #92#51 = #102#59;

 aCdTr  = "";
 aDeduc = #102#61;
 aAlfa1 = #92#26; aAlfa1 = aAlfa1 > " ";
 |SI aAlfa1 = "B"; |O aAlfa1 = "I"; |O aAlfa1 = "R";
     |SI aAlfa1 ! "B";
         |HAZ VeoNumeroBueno;
     |FINSI;

     |SI #92#27 [ 1; aCdTr = aCdT1; aCtaNoDed = aCtaNoDe1; nNume1 = 17; |FINSI;
     |SI #92#27 = 2; aCdTr = aCdT2; aCtaNoDed = aCtaNoDe2; nNume1 = 24; |FINSI;
     |SI #92#27 = 3; aCdTr = aCdT3; aCtaNoDed = aCtaNoDe3; nNume1 = 31; |FINSI;
     |SI #92#27 = 4; aCdTr = aCdT4; aCtaNoDed = aCtaNoDe4; nNume1 = 38; |FINSI;
     #92#28 = #104nNume1;

     |SI #92#28 = 0; |Y nNumCIva ! 0; |Y aAlfa1 = "I";
         nNume2 = nNume1 - 1;
         nNume3 = nNume1 + 1;
         nNume1 = ((#104nNume3 * 100) / #104nNume2) ! 0;
         |SI nNume1 ! 21; |Y nNume1 ! 10; |Y nNume1 ! 4;
             |SI nNume1 < 7;
                 nNume1 = 4;
             |SINO;
                 |SI nNume1 < 13;
                     nNume1 = 10;
                 |SINO;
                     nNume1 = 21;
                 |FINSI;
            |FINSI;
         |FINSI;
         #92#28 = nNume1;
     |FINSI;

     |SI #92#27 [ 1; nNume1 = 19; |FINSI;
     |SI #92#27 = 2; nNume1 = 26; |FINSI;
     |SI #92#27 = 3; nNume1 = 33; |FINSI;
     |SI #92#27 = 4; nNume1 = 40; |FINSI;
     #92#33 = #104nNume1;
 |FINSI;
 |SI aAlfa1 = "P";
     |HAZ VeoNumeroBueno;
     |SI #92#27 [ 1; nNume1 = 21; |FINSI;
     |SI #92#27 = 2; nNume1 = 28; |FINSI;
     |SI #92#27 = 3; nNume1 = 35; |FINSI;
     |SI #92#27 = 4; nNume1 = 42; |FINSI;
     #92#41 = #104nNume1;
     #92#42 = #102#34;
     #92#6  = nConceRet;
     #92#7  = aDesReten;
 |FINSI;
 |SI aAlfa1 = "F"; |O aAlfa1 = " ";
     |SI nIvas = 1;
         #92#43 = #102#41;
         #92#44 = #102#40;
         nApunteF = #92#3;
    |FINSI;
    #92#45 = #102#13;
 |FINSI;

 |QBF aCdTr;
 #92#29 = #104#2;  || Fecha Factura

 |SI #92#10 = "R"; |O #92#10 = "S";

     |SI #92#29 [ "01.01.1000";
         #92#29 = #102#59;
         |SI #92#29 [ "01.01.1000";
             #92#29 = #92#1;  || Fecha Asiento
         |FINSI;
     |FINSI;
     |SI #92#29 = #92#51;
         #92#51 = "01.01.0000";
     |FINSI;

     |SI #92#10 = "S"; |Y aCdTr = "625"; aDeduc = "N"; |FINSI;
     |SI #92#10 = "R"; |Y aCdTr = "725"; aDeduc = "N"; |FINSI;
     #92#31 = aDeduc;  || Deducible
     |SI aDeduc = "N";
        aAlfa = #92#26; aAlfa = aAlfa > " ";
        |SI aAlfa = "I"; |O aAlfa = "R";
            nNume1 = 82;
            |SI #92#10 = "R"; nNume1 = 81; |FINSI;
            #92#4 = #200nNume1;
            |SI #92#4 = doce;
                #92#4 = aCtaNoDed;
            |FINSI;
        |FINSI;
     |FINSI;

 |FINSI;

 |GRABA 020#92a;
 nSwAlgun = 1;
 nSw      = 1;
 |SI nUltDoc < nDocum; nUltDoc = nDocum; |FINSI;

 |SI nIvas = 1;
     |SI aTApunte = "F"; |O aTApunte = "f"; |O aTApunte = " ";
         nTotalFras = nTotalFras + #92#9;
     |FINSI;
     |SI aTApunte = "B"; |O aTApunte = "b";
         nTotalBase = nTotalBase + #92#9;
     |FINSI;
     |SI aTApunte = "I"; |O aTApunte = "b";
       |O aTApunte = "R"; |O aTApunte = "r";
         nTotalCuota = nTotalCuota + #92#9;
     |FINSI;
     |SI aTApunte = "P"; |O aTApunte = "p";
         nTotalReten= nTotalReten + #92#9;
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO Graba92;
 |DDEFECTO #92;
 #92#0 = nDiario;   || Diaro
 #92#1 = aFecha;    || Fecha
 #92#2 = nDocum;    || Documento
 #92#3 = nApunte;
 #92#37 = enEmpresa;
 #92#38 = enPeriodo;
 |LEE 001#92=;
 |SI FSalida ! 0;
     |DDEFECTO #92;
     #92#0 = nDiario;   || Diaro
     #92#1 = aFecha;    || Fecha
     #92#2 = nDocum;    || Documento
     #92#3 = nApunte;
     #92#37 = enEmpresa;
     #92#38 = enPeriodo;
     |GRABA 020#92c;
 |FINSI;
|FPRC;

|DEFBUCLE LeeAux102;
    #102#3;
    ;
    "01.01.0000", "                         ", 000001, 0000001;
    "31.12.2999", "zzzzzzzzzzzzzzzzzzzzzzzzz", 999999, 9999999;
    ;
    NULL, PasaEnlace;
|FIN;

|PROCESO GrabaRepe;
    aAlfa  = "";
    nError = 0;
    |SELECT #2#105;
    #105#1 = #104#6;
    #105#2 = #104#7;
    |LEE 041#105=;
    |SI FSalida = 0;

        nError = 1;
        nNume1 = #104#0 + 1;
        aAlfa  = " Factura Repetida en la LINEA: " + nNume1;
        #105#6 = 1;
        #105#7 = aAlfa;
        |GRABA 020#105a;

        nNume1 = #105#0 + 1;
        aAlfa  = " Factura Repetida en la LINEA: " + nNume1;
    |FINSI;
    |SELECT #1#105;

    |DDEFECTO #105;
    #105#0 = #104#0;
    #105#1 = #104#6;
    #105#2 = #104#7;
    #105#3 = #104#10;
    #105#6 = nError;
    #105#7 = aAlfa;
    #105#12 = #105#0 + 1;
    |GRABA 040#105n;
|FPRC;

|PROCESO Comprobar102;

   aAlfa = #102#13; |QBF aAlfa;
   |SI aAlfa ! "";
       aAlfa = #102#39; |QBF aAlfa;
       aAlfa = aAlfa % 101;

       aTipoIVA = "R";
       |SI aAlfa = "P"; aTipoIVA = "S"; |FINSI;

       aCuenta = #102#6;
       aAlfa   = #102#6; aAlfa = aAlfa % 102;
       |SI aTipoIVA = "R";
           |SI aAlfa ! "43"; |Y aAlfa ! "44";
               nSwError = 6;
               nLineaMal = #102#0;
           |FINSI;
       |SINO;
           |SI aAlfa = "43"; |O aAlfa = "44";
               nSwError = 5;
               nLineaMal = #102#0;
           |FINSI;
       |FINSI;
   |FINSI;

   aAlfa   = #104#7; |QBF aAlfa;
   aAlfa   = (aAlfa + "     ") % 105;
   aAlfa1  = #104#6; |QBF aAlfa1;
   aAlfa1  = ("                    " + aAlfa1) % -0120;
   aAlfa1  = aAlfa + aAlfa1;
   #102#65 = aAlfa1;   ||  #104#6;
   |GRABA 040#102a;
   |LIBERA #102;
|FPRC;

|DEFBUCLE Comprobar102;
    #102#4;
    ;
    #104#3, 0000001;
    #104#3, 9999999;
    e;
    NULL, Comprobar102;
|FIN;

|PROCESO Comprobar104;

    aTipoIVA = "";
    |BUCLE Comprobar102;
    |SI nSwError ] 5;  |ERROR10; |ACABA; |FINSI;

    |SI aTipoIVA = "R";
        nOpera   = 1;
        aFactura = #104#6; |QBF aFactura;
        aSerie   = #104#7; |QBF aSerie;
        |SI aSerie = "";
            |HAZ MiraLetras;
            |SI nLetras ! 0;
                nSwError = 4;  || no deja seguir
            |FINSI;
        |SINO;
            |HAZ MiraLetras;
            |SI nLetras = 0;
                nSwError = 1;
            |SINO;
                aFactura = aFactura - aSerie;
                |SI FSalida = 0;
                    |HAZ MiraLetras;
                    |SI nLetras ! 0;
                        nSwError = 3; || no deja seguir
                    |SINO;
                         nSwError = 2;
                    |FINSI;
                |SINO;
                     |HAZ MiraLetras;
                     |SI nLetras ! 0;
                         nSwError = 3; || no deja seguir
                     |SINO;
                         nSwError = 1;
                     |FINSI;
                |FINSI;
            |FINSI;
        |FINSI;

        |HAZ GrabaRepe;

        |SI nSwError ] 3;  |ERROR10;  |FINSI;

    |FINSI;
|FPRC;

|DEFBUCLE Comprobar104;
    #104#1;
    ;
    0000001;
    9999999;
    e;
    NULL, Comprobar104;
|FIN;

|PROCESO PrimComprobacion;
     aSepSer = #caivaasi#97; |QBF aSepSer;
     aCuenta   = "";
     nLineaMal = 0;
     nSwError  = 0;
     |ABRE #105;
     |BUCLE Comprobar104;
     |CIERRA #105;
     |SI nSwError = 1; |O nSwError = 2;
         aAlfa = "0000" + #200#44 + "Recoger Campo Serie en la Factura";
         |CONFI aAlfa;
         |SI FSalida = 0;  nSwSerie = 1; |FINSI;
     |FINSI;
     |SI nSwError = 3; |O nSwError = 4; |O nSwError = 5; |O nSwError = 6;
         aAlfa = "0000Existen Numeros de Facturas con Letras y NO esta Informada la Serie. ";
         |SI nSwError = 3;
             aAlfa = "0000Existen Numeros de Facturas con Letras y la Serie esta mal informada. ";
         |FINSI;
         |SI nSwError = 5;
             aAlfa = "0000Factura con la Cuenta " + aCuenta + " informada como Proveedor";
         |FINSI;
         |SI nSwError = 6;
             aAlfa = "0000Factura con la Cuenta " + aCuenta + " informada como Cliente";
         |FINSI;
         |MENAV aAlfa;
         |SI nSwError = 3; |O nSwError = 4;
             aSerie   = #104#7; |QBF aSerie;
             aFactura = #104#6; |QBF aFactura;
             aAlfa = "0000Factura: " + aFactura;
             |SI aSerie ! "";
                 aAlfa = aAlfa + " Serie: " + aSerie;
             |FINSI;
             nNume1 = #104#0 + 1;
             aAlfa = aAlfa + " HOJA: Facturas LINEA: " + nNume1;
         |SINO;
             aSerie   = #104#7; |QBF aSerie;
             aFactura = #104#6; |QBF aFactura;
             aAlfa = "0000Factura: " + aFactura;
             |SI aSerie ! "";
                 aAlfa = aAlfa + " Serie: " + aSerie;
             |FINSI;
             nNume1 = nLineaMal + 1;
             aAlfa = aAlfa + " HOJA: Movimientos LINEA: " + nNume1;
         |FINSI;
         |MENAV aAlfa;
         |MENAV "0000Proceso interrumpido. Revise los datos a Importar ";
     |FINSI;
|FPRC;

|PROCESO ParaImprimir;
   |SI nImpresora = 0;
       |MENAV "0000Hay Errores en las Facturas a Importar";
       |MENAV "0000El programa imprime un Informe de los mismos";

       |IMPRE 0;
       |SI FSalida !  0;
           |MENAV "0000Error en Impresora. Proceso abortado";
           |ACABA;
       |FINSI;
       |INFOR informe;
       |SI FSalida ! 0;
           |MENAV "0000Error en Informe. Proceso abortado";
           |ACABA;
       |FINSI;
       nImpresora = 1;
   |FINSI;
   |PRINT;
|FPRC;

|PROCESO Comprobar105;
     |SI #105#6 ! 0;
         nSwError = 7;
         |HAZ ParaImprimir;
         |SI nImpresora = 0; |ERROR10; |ACABA; |FINSI;
     |FINSI;

     aSerie   = #105#2; |QBF aSerie;
     aFactura = #105#1; |QBF aFactura;
     |HAZ CalSerieNum;

     #105#8 = #0#16;
     #105#4 = aSerie;
     #105#5 = nFactura;

     #12#0  = #105#8;
     #12#2  = #105#4;
     #12#3  = #105#5;
     |LEE 041#12=;
     |SI FSalida = 0;
         nSwError = 7;
         #105#6 = 2;
         nNume1 = #105#0 + 1;
         aAlfa  = "Ya existe Factura en Contabilidad";
         #105#7 = aAlfa;
         #105#9 = #12#1;
         #105#10 = #12#4;
         #105#11 = #12#28;
         |GRABA 020#105a;
         |HAZ ParaImprimir;
         |SI nImpresora = 0; |ERROR10; |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE Comprobar105;
     #105#2;
     ;
     "                    ", "     ", 000001;
     "zzzzzzzzzzzzzzzzzzzz", "zzzzz", 999999;
     ;
     NULL, Comprobar105;
|FIN;

|PROCESO SegunComprobacion;
     nImpresora = 0;
     |ABRE #12;
     |SELECT #3#12;
     |BUCLE Comprobar105;
     |SELECT #1#12;
     |CIERRA #12;
     |SI nImpresora = 1;
         |PIEINF;
     |FINSI;
|FPRC;

|PROCESO PasoApuntes;
     aFichero = "pu" + Usuario;
     |NOME_DAT #92 aFichero;
     |ABRE #92; |CIERRA #92; |DELINDEX #92;

     |HAZ PrimComprobacion;
     |SI nSwError > 2; |ACABA; |FINSI;

     |HAZ SegunComprobacion;
     |SI nSwError = 7;
         aAlfa ="0000Proceso interrumpido. Revise los datos a Importar ";
         |MENAV aAlfa;
         |ACABA;
     |FINSI;

     |HAZ contador;
     nDocum = nDOCUMENTO - 1;

     nDiario = #0#15;
     nSwAlgun = 0;
     |ABRE #1;
     |ABRE #4;
     |ABRE #12;
     |ABRE #92;
     |ABRE #104;
     |BUCLE LeeAux102;
     |CIERRA #104;
     |CIERRA #92;
     |CIERRA #12;
     |CIERRA #4;
     |CIERRA #1;
|| ... |ABRE #92; |CONSULTA #1#92; |CIERRA #92;
  |SI nSwAlgun = 1;
      nDOCUMENTO = nUltDoc;
      |HAZ ActContador;
      enNoModif = 0;
      |SI #0#14 = "N";
          enNoModif = 100;  || para que no se modifiquen los apuntes
      |FINSI;
      eSwPlusGes = 1;
      |DFICE aAlfa1; |QBF aAlfa1;
      aAlfa1 = "dsapunte.tab;" + aAlfa1 + "," + aFichero;
      |SUB_EJECUTA_NP aAlfa1;
      eSwPlusGes = 0;
      |SI PRMNT_enError ! 0;
          nSumaAsto   = 0;
          nSumaFras   = 0;
          nTotalBase  = 0;
          nTotalCuota = 0;
          nTotalFras  = 0;
          nTotalReten = 0;
          nSwAlgun    = 0;
          |SI nSwAlgCP = 0; nSw = 0; |FINSI;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO HazOperacion;
   |DBASE aMas; |QBF aMas;
   aDef = aMas + "def/catplw02.mas";
   |CARGA_DEF aDef, catplw2@;
   |SI FSalida ! 0;
       |MENAV "0000Error al cargar fichero Auxiliar";
       |ACABA;
   |FINSI;

   aFichero = ("1h" + Usuario) % 108;
   |NOME_DAT #101 aFichero; |DELINDEX #101;

   aFichero = ("2h" + Usuario) % 108;
   |NOME_DAT #102 aFichero; |DELINDEX #102;
   |NOME_DAT #1102 aFichero;

   aFichero = ("3h" + Usuario) % 108;
   |NOME_DAT #103 aFichero; |DELINDEX #103;

   aFichero = ("4h" + Usuario) % 108;
   |NOME_DAT #104 aFichero; |DELINDEX #104;

   aFichero = ("5h" + Usuario) % 108;
   |NOME_DAT #105 aFichero; |DELINDEX #105;

   nSumaClPr   = 0;
   nSumaCtas   = 0;
   nSumaAsto   = 0;
   nSumaFras   = 0;
   nTotalBase  = 0;
   nTotalCuota = 0;
   nTotalFras  = 0;
   nTotalReten = 0;

   nSwHay = 0;
   eaFichero = #0#3; |QBF eaFichero;
   |HAZ excel;

   |SI nSwHay = 1;
       nSw    = 0;
       |SI #0#4 = "S";
           |HAZ PasoClieProv;
       |FINSI;

       |SI #0#5 = "S";
           |HAZ PasoApuntes;
       |FINSI;

       |SI nSw = 1;
           |MENAV "0000Datos Pasados a la Contabilidad";
           |HAZ CrearHist111;
       |FINSI;
   |FINSI;
/*
   |DELINDEX #105;
   |DELINDEX #104;
   |DELINDEX #103;
   |DELINDEX #102;
   |DELINDEX #101;
*/
|FPRC;

|PROGRAMA;

   |CLS;
   |CABEZA "Importacion PlusGestiona";

   |HAZ inicio;

   ewNif = 0;
   enSwDeDonde = -1;
   |HAZ DirAplicSt;
   |SI HayAcceso = 1;
       enSwDeDonde = 1;
       |REFERENCIA_DEF regisnif@, #711;
   |SINO;
       |SI Haybasica = 1;
           enSwDeDonde = 0;
           |REFERENCIA_DEF regisnif@, #710;
       |FINSI;
   |FINSI;
   |SI enSwDeDonde = -1; ewNif = 1; |FINSI; || no existe Basica ni Integrada

   |HAZ LeeUnidadBasico;

   |ABRE #200;
   |DDEFECTO #200;
   |LEE 041#200p;
   |CIERRA #200;

   aQueQuiero = "|NC";
   aNumCampos = #catplm01#aQueQuiero#;
   nNumCampos = aNumCampos;
   nNumCampos = nNumCampos - 1;

   |ABRE #0;
   #0#0 = "A";
   |LEE 141#0=;
   |SI FSalida ! 0;
       |DDEFECTO #0;
       #0#16 = #200#3;
       #0#18 = #200#5;
       #0#17 = #200#29;
       #0#19 = #200#31;
       |GRABA 020#0b;
   |FINSI;

   |PINPA #0#0;
   |PINDA #0#0;
   |ENDATOS #1#0;
   |SI FSalida = 0;
       |SI #0#1 = "SI";
           |HAZ HazOperacion;
       |FINSI;
       #0#1 = "NO";
       #0#3 = "";
       |GRABA 020#0a;
   |FINSI;
   |LIBERA #0;
   |CIERRA #0;
|FPRO;

|PROCESO Pondsselect;
     |HAZ LeeUnidadBasico;
     |DBASS aBase;  |QBF aBase;

     aFicOrigen = aBase + "basica/plantillas/dsselect.exe";
     |XABRE "E", aFicOrigen, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     aDirLocal    = eaUnidadBasico + "\MODELOS";       |RMKDIR aDirLocal;
     aDirLocal    = eaUnidadBasico + "\MODELOS\CONTA"; |RMKDIR aDirLocal;
     aDirLocal    = eaUnidadBasico + "\MODELOS\CONTA\";
     aFicDestino  = aDirLocal + "dsselect.exe";

     aContiene1 = aFicDestino;
     |ESPECIFICA "REMOTOMD5", aContiene;
     aDocRemoto = FSalida;  |QBF aDocRemoto;

     aContiene1 = aFicOrigen;
     |ESPECIFICA "MD5", aContiene;
     aDocServidor = FSalida;  |QBF aDocServidor;

     |SI aDocRemoto ! aDocServidor;  |O aDocRemoto = "";
         aIPRemoto = "";
         |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aFicOrigen, aFicDestino;
         |SINO;
             |COPIA_FICHERO aFicOrigen, aFicDestino;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Browse;
     |HAZ Pondsselect;
     aPrograma = aDirLocal + "dsselect.exe " + aDirLocal + "dsselect.ini";

     aBorra = aDirLocal + "dsselect.txt";  |RFBORRA aBorra;
     aBorra = aDirLocal + "dsselect.ini";  |RFBORRA aBorra;

     aAbre = aDirLocal + "dsselect.ini";
     |XABRE "CW", aAbre, nHandle;
     |SI FSalida ] 0;
          aLinea = &34 + eaUnidadBasico + "\CONTA\" + &34 + " " + &34 + aDirLocal + "dsselect.txt" + &34;
          |XGRABA nHandle, aLinea;
          |XCIERRA nHandle;
     |FINSI;

     |RSYSTEM aPrograma;
     |PULSATECLA;

     aRuta = "";
     aAbre = aDirLocal + "dsselect.txt";
     |XABRE "CU", aAbre, nHandle;
     |SI FSalida ] 0;
         |XLEE nHandle, 250, aRuta;
     |FINSI;
     |XCIERRA nHandle;

     aBorra = aDirLocal + "dsselect.txt";
     |RFBORRA aBorra;

     |PULSATECLA;

     aAlfa = aRuta;
     aRuta = "";
     aLong = aAlfa % 0;
     nLong = aLong;
     |PARA nCarac = 1;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos       = (nCarac * 100) + 1;
           aCaracter  = aAlfa % nPos;
           aRuta      = aRuta      + aCaracter;
           eaNomOri   = eaNomOri + aCaracter;

           |SI aCaracter = "/";  |O aCaracter = "\";
               eaNomOri = "";
           |FINSI;
     |SIGUE;
|FPRC;
