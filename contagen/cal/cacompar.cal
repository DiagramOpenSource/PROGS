|FICHEROS;
   cacompar #0;

   cacuenta #5;         || plan contable
   caconimp #8;

   cacompt1 #11;        || tmp. comparacion
   canempre #30;
|FIN;

|VARIABLES;
   nRedon = 0;

   &pathbal = "";
   &balance = 1;
   &aparam  = "";
   alfa1 = "";
   alfa2 = "";
   alfa3 = "";
   nume1 = 0;
   para1 = 0;
   para2 = 0;
   para3 = 0;
   para4 = 0;
   fech1 = @;
   fech2 = @;
   fech3 = @;
   fech4 = @;
   elsw1 = 0;
   elsw2 = 0;
   elsw3 = 0;
   elsw4 = 0;

   seleccio = 0;
   sw_rup   = 0;
   saldo    = 0;

   p_mes = "";
   d_mes = 0;
   h_mes = 0;
   mas = 0;
   sw_chequeo = 0;
   sw_movimin = 0;
   acum = 0;

   &nom1 = "";
   &nom2 = "";
   &ej1  = 0;
   &ej2  = 0;
   &colum = "";
   &des = "";
   &has = "";
   &r_emp = 0;
   &r_per = 0;
   xx = 0;
   yy = 0;

   swniv = 0;
   niv1  = 0;
   niv2  = 0;
   niv3  = 0;
   niv4  = 0;
   niv5  = 0;
   niv6  = 0;

   aFichero = "";
|FIN;

|INCLUYE dscont_i;

____________________________________/ CALCULOS PANTALLA

|PROCESO inicio_y; |TIPO 8;
  |HAZ inicial;
  |SI swerror = 2; |PTEC 807; |ACABA; |FINSI;
  |SI swerror = 3; |PTEC 831; |PTEC 807; |ACABA; |FINSI;
|FPRC;

|CALCULO LosDefectos; |TIPO 7;
 #0#2 = enEmpresa;
 #0#3 = enPeriodo;
 #0#0 = #0#2;
 #0#1 = #0#3 - 1; |SI #0#1 < 0; #0#1 = 9; |FINSI;

 |PINTA #0#0;
 |PINTA #0#1;
 |PINTA #0#2;
 |PINTA #0#3;
|FCAL;


|CALCULO lee_empre; |TIPO 0;
   |SI FSalida = 2;  |ACABA;  |FINSI;
   nume1 = Campo - 1;
   |ABRE #30;
   #canempre#2 = #0nume1;
   #canempre#3 = #0Campo;
   |LEE 000#30=;
   |SI FSalida ! 0;
      #canempre#1 = "";
      |MENAV "    Empresa INEXISTENTE ...";
      |ERROR;
   |SINO;
      |SI Campo = 1;
          niv1 = #canempre#14;
          niv2 = #canempre#15;
          niv3 = #canempre#16;
          niv4 = #canempre#17;
          niv5 = #canempre#18;
          niv6 = #canempre#19;
      |SINO;
          swniv = 0;
          |SI niv1 ! #canempre#14; swniv= 1; |FINSI;
          |SI niv2 ! #canempre#15; swniv= 1; |FINSI;
          |SI niv3 ! #canempre#16; swniv= 1; |FINSI;
          |SI niv4 ! #canempre#17; swniv= 1; |FINSI;
          |SI niv5 ! #canempre#18; swniv= 1; |FINSI;
          |SI niv6 ! #canempre#19; swniv= 1; |FINSI;
          |SI swniv ! 0;
              |MENAV "    NO CORRESPONDEN LOS NIVELES";
              |ERROR;
          |FINSI;
      |FINSI;
   |FINSI;
   |ATRI I;

   |SI Campo = 1;
       |PINTA 0934, #canempre#1;
       nom1 = #canempre#1;
       ej1  = #canempre#26;
       |SI ej1 > 80;
           ej1 = 1900 + ej1;
       |SINO;
           ej1 = 2000 + ej1;
       |FINSI;
       aMonedaC = #30#28;
   |SINO;
       |PINTA 1034, #canempre#1;
       nom2 = #canempre#1;
       ej2 = #canempre#26;
       |SI ej2 > 80;
           ej2 = 1900 + ej2;
       |SINO;
           ej2 = 2000 + ej2;
       |FINSI;
       aMonedaA = #30#28;
   |FINSI;

   |CIERRA #30;
   |ATRI 0;
|FCAL;

|CALCULO consulta; |TIPO 0;

   |SI FSalida ! 9;  |ACABA;  |FINSI;
   |SUB_EJECUTA "caconemp";

   |ERROR;
   |SI Campo = 0;
       #0#0 = r_emp;
       #0#1 = r_per;
       |PTEC 802;
       |PTEC 802;
   |SINO;
       #0#2 = r_emp;
       #0#3 = r_per;
       |PTEC 802;
       |PTEC 802;
   |FINSI;
|FCAL;

|CALCULO misma; |TIPO 0;
   |SI #0#0 = #0#2;|Y #0#1 = #0#3;|Y FSalida ! 2;
       |MENAV "    ATENCION!!! No se puede comparar la misma EMPRESA ...";
       |ERROR;
   |FINSI;
|FCAL;

|CALCULO pintames; |TIPO 0;

  |SI #0Campo ! 0; |Y #0Campo ! 13;
      NumMes = #30#11 + (#0Campo - 1);   || Operacion para saber que mes es
     |SI NumMes > 12; NumMes = NumMes - 12; |FINSI;
  |SINO;
      NumMes = #0Campo;
  |FINSI;
  |HAZ NombreMes;

  |ATRI I;
  |SI Campo = 11;     |PINTA 1427, NombreMes; des = p_mes;   |FINSI;
  |SI Campo = 12;     |PINTA 1442, NombreMes; has = p_mes;   |FINSI;
|FCAL;

|CALCULO nosaldo; |TIPO 7;
   alfa1 = "S";
   |SI #0#18 = "S";  alfa1 = "N";  #0#19 = "S"; |FINSI;
   |C_M #0#19, 1, alfa1;
   |PINTA #0#19;
|FCAL;

|CALCULO cuenta; |TIPO 6;
  eaCuenta = #0Campo;
  salidas  = FSalida;
  |HAZ FiltraPuntos;

  nume1 = Campo + 2;
  #0Campo = eaCuenta; |PINTA #0Campo;
  |HAZ SacaNivel;
  #0nume1 = enNivel;
|FCAL;

|| *********************************************************************
____________________________________/ LECTURA DEL TEMPORAL
|PROCESO lee_tempo1;
   |PRINT;
   seleccio = 1;
|FPRC;

|DEFBUCLE lee_tempo;
   #11#1;
   ;
   "            ", 0;
   "zzzzzzzzzzzz", 9;
   e;
   NULL, lee_tempo1;
|FIN;
____________________________________/ GRABACION DEL TEMPORAL

|PROCESO chequeo;
   sw_chequeo = 0;
   |SI #0#19 = "N"; |Y saldo = 0; |Y sw_movimin = 1;
       sw_chequeo = 1;
   |FINSI;
   |SI sw_movimin = 0;|Y #0#18 = "N";
       sw_chequeo = 1;
   |FINSI;
|FPRC;

|PROCESO acumula;

   sw_movimin = 0;
   saldo = 0;
   |PARA para3 = d_mes; |SI para3 [ h_mes; |HACIENDO para3 = para3 + 3;

         impMoneda = #5para3;
         |SI aMonedaA ! aMonedaC; |Y acum = 2;
             |HAZ ElCMoneda;
         |FINSI;

         |SI impMoneda ! 0;  sw_movimin = 1;  |FINSI;

         |SI #0#17 = "S"; |Y impMoneda = 0;
             xx = para3 - 2; yy = para3 - 1;
             |SI #5xx ! 0; |O #5yy ! 0;
                 sw_movimin = 1;
             |FINSI;
         |FINSI;

         saldo = saldo + impMoneda;
   |SIGUE;
|FPRC;

|PROCESO columna1_1;

   |HAZ acumula;
   |HAZ chequeo;
   |SI sw_chequeo ! 0;  |ACABA;  |FINSI;

   #11#0 = #5#0;    || cuenta
   #11#1 = #5#1;    || digito
   |LEE 001#11=;
   |SI FSalida ! 0;
       |DDEFECTO #11;
       #11#0 = #5#0;     || Cuenta
       #11#1 = #5#1;     || digito
       #11#4 = #5#2;     || Nombre
       |GRABA 020#11c;
   |FINSI;

   #11acum = saldo;      || acumulado
   |GRABA 020#11a;
   |LIBERA #11;
|FPRC;

|DEFBUCLE columna1;
   #5#5;
   ;
   #0#13, #0#4;
   #0#14, #0#4;
   e;
   NULL, columna1_1;
|FIN;

|PROCESO antesque;
   aparam = PARAMETRO;
   |QBF aparam; |SI aparam = " N"; aparam = ""; |FINSI;
   |SI aparam = "fech";
       |NOME_DAT #5 "cabalano";
   |FINSI;
|FPRC;

|| ************************************************************************

|CALCULO tipo3; |TIPO 3;

   |CIERRA #5;
   |HAZ antesque;

   |IMPRE 0;
   |SI FSalida ! 0;  |ACABA;  |FINSI;

   |INFOR "cacompar";
   |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

   || *****************************************************************
   |SI #0#17 = "D";    mas = 0;  colum = "DEBE";     |FINSI;
   |SI #0#17 = "H";    mas = 1;  colum = "HABER";    |FINSI;
   |SI #0#17 = "S";    mas = 2;  colum = "SALDO";    |FINSI;
   d_mes = (9 + mas) + (#0#11 * 3);
   h_mes = (9 + mas) + (#0#12 * 3);

   aFichero = ("11y" + Usuario) % 108;
   |NOME_DAT #11 aFichero;
   |DELINDEX #11;

   acum = 2;
   |PARA para1 = 0; |SI para1 [ 2; |HACIENDO para1 = para1 + 2;
         para2 = para1 + 1;
         |DFICB dir_fich; |QBF dir_fich;
         alfa1 = #0para1;  alfa1 = "00000" + alfa1; alfa1 = alfa1 % -105;
         alfa2 = #0para2;  alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
         alfa3 = dir_fich + alfa1 + alfa2 + "/";

         |PATH_DAT #5 alfa3;
         |PATH_DAT #8 alfa3;

         |ABRE #30;
         #canempre#2 = #0para1;
         #canempre#3 = #0para2;
         |LEE 011#30=;
         |CIERRA #30;
         eaMoneda = #30#28; |HAZ Monedas; EURODB = 2;

         dirfich0 = alfa3;
         pathbal  = dirfich0;
         |HAZ FecApeCie;
         dirfich0 = pathbal;
         |SI enMAper ! #30#11;
             |SI #0#12 < enMAper; |Y #0#12 ! 0;
                 |VETE EstaNo;
             |FINSI;
         |FINSI;

         |ABRE #8;
         |DDEFECTO #8;
         #8#0 = 1;
         |LEE 000#8=;
         |SI #caconimp#2 = "S";  |Y #caconimp#3 = "N";  |Y aparam = "";
             balance = 1;
             |CIERRA #8;
             |SUB_EJECUTA "careccue";
         |SINO;
             |CIERRA #8;
         |FINSI;

         |SI para1 = 2;  acum = 3; |FINSI;

         |ABRE #11;
         |BUCLE columna1;
         |CIERRA #11;
         |ET EstaNo;
   |SIGUE;

   eaMoneda = aMonedaA; |HAZ Monedas;

   seleccio = 0;
   |BUCLE lee_tempo;
   |SI seleccio ! 0;
      |PIEINF;
   |FINSI;

   |FININF;
   |FINIMP;
   |DELINDEX #11;
|FCAL;

|PROCESO ElCMoneda;
     |SI aMonedaC = "P";                  || pesetas
         mulMoneda = 1 / 166.386;
         nRedon = 2;
     |FINSI;
     |SI aMonedaC = "E";                  || euros
         mulMoneda = 166.386;
         nRedon = 0;
     |FINSI;
     impMoneda = (impMoneda * mulMoneda) ! nRedon;
|FPRC;
