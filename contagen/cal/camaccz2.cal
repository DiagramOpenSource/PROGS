|FICHEROS;
   camaccz2 #0;

   camaasil #1;
   caconimp #8;
   caclipro #18;

   camaccw3 #599,MANTE;
   camaccw5 #999,MANTE;

   camaniva #11;
   catmpasi #14;

   camacc01 #501;
   camacc02 #502;
   camacc03 #503;

   canempre #30;
   cawdatos #900;
   imagen@  #990;
|FIN;

|VARIABLES;
    nInkey   = 0;
    aAlfa1   = "";
    aAlfa2   = "";
    nNume1   = 0;
    nNume2   = 0;
    aFichero = "";
    informe  = "";
    inform1  = "camaccz2";
    inform2  = "camaccy2";
    nModo    = 0;
    nAlta    = 0;
    &i_nom   = "";
    &i_cif   = "";
    nEste    = 0;

    nCampo   = 0;
    aMas     = "";
    aDef     = "";
    nSwAlgun = 0;
    aMulDep  = "";
    nDdepar  = 0;
    nHdepar  = 0;
    nSwSel   = 0;
    nSw      = 0;
    &enSwLinea = 0;
    aDH      = "";
    &eaTCta    = "";
    &eaFor1    = "";
    &eaFor2    = "";
    &enTotal   = 0;
    &eaTexto   = "";
    aParam     = "";
|FIN;

|INCLUYE dscont_i;
|INCLUYE dscoiv_i;

|| ***************** Calculos de Pantalla
|CALCULO QueIva; |TIPO 0;
   |SI #0#16 = "C";
       |PINTA 1709, "Cobro en Efectivo";
       |PINTA 1825, "Cobro";
       eaTexto = "Cobro";
       aTipoIVA = "R";
   |SINO;
       |PINTA 1709, "Pago en Efectivo .";
       |PINTA 1825, "Pago ";
       eaTexto = "Pago";
       aTipoIVA = "S";
   |FINSI;

   |HAZ LeeDefIVA;
   #0#11 = nCCCon1; |PINTA #0#11;
   #0#12 = nCCCon2; |PINTA #0#12;
|FCAL;

|CALCULO defectos; |TIPO 7;
    #0#4 = fec1; |PINTA #0#4;
    #0#5 = fec2; |PINTA #0#5;
|FCAL;

|CALCULO fecha; |TIPO 0;
   |SI #0Campo < fec1; |O #0Campo > fec2;
       |MENAV "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
       |ERROR;
       |ACABA;
   |FINSI;
   |SI Campo = 5; |Y #0#5 < #0#4;
       |MENAV "    FECHA ERRONEA ";
       |ERROR;
   |FINSI;
|FCAL;

|CALCULO cuenta;|TIPO 6;
     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta; |PINTA #0Campo;
|FCAL;

|CALCULO concepto; |TIPO 0;
|FCAL;

|CALCULO nodepar; |TIPO 7;
   |C_M #0#9, 1, eaDepar;
   |C_M #0#10, 1, eaDepar;
|FCAL;

|CALCULO Tipo7C15; |TIPO 7;
    |SI #0#16 = "C";
        aAlfa1 = "    (S) Cobros Asignados, (N) Cobros Pendientes, (T) Todos";
    |SINO;
        aAlfa1 = "    (S) Pagos Asignados, (N) Pagos Pendientes, (T) Todos";
    |FINSI;
    |MENSA aAlfa1;
|FCAL;

|| *************************************************************************
|CALCULO Tipo5Cw3;
|FCAL;

|CALCULO Tipo7Cw3; |TIPO 7;
  |MENSA "0000Pulse <F5> para consultar Factura en Reg. de  Criterio de Caja";
|FCAL;

|CALCULO Tipo0Cw3; |TIPO 0;
  |BLIN 4;
  |SI FSalida = 13;
      eLibroCaja = #599#0;
      eRegisCaja = #599#1;
      eDevenCaja = #599#28;
      |SUB_EJECUTA_NP "camacc01;Consulta";
  |FINSI;
|FCAL;

|CALCULO Tipo7Cw318; |TIPO 7;
|FCAL;

|CALCULO CalImporte; |TIPO 0;
|FCAL;

|PROCESO Tipo11Cw3; |TIPO 11;
|FPRC;
|| *************************************************************************
|PROCESO ModiPan_4;
   |SI eaSubde = "N";
        |C_A #14#7, 1, 80;
        |C_P #14#21,1, 1467;
        |C_V #14#28,1, "N";
        |PINTA 1162, "ÄÄÄÄÂÄÄ";
        |PINTA 1262, "    ³  ";
        |PINTA 1362, "ÄÄÄÄÅÄÄ";
        |PINTA 1462, "    ³  ";
        |PINTA 1562, "    ³  ";
        |PINTA 1662, "    ³  ";
        |PINTA 1762, "    ³  ";
        |PINTA 1862, "    ³  ";
        |PINTA 1962, "    ³  ";
        |PINTA 2062, "    ³  ";
        |PINTA 2162, "    ³  ";
        |PINTA 2262, "    ³  ";
        |ATRI R; |PINTA 1262, "    "; |PINTA 1267, "Dp"; |ATRI 0;
   |FINSI;
|FPRC;

|RUTINA Astoinf;
   #14#0 = #999#0;
   #14#1 = #999#1;
   #14#2 = #999#2;
   #14#3 = 0001;
   |SI FSalida = "POSICION";
       #14#3 = #999#3;
   |FINSI;
|FRUT;

|RUTINA Astosup;
   #14#0 = #999#0;
   #14#1 = #999#1;
   #14#2 = #999#2;
   #14#3 = 9999;
|FRUT;

|PROCESO VerAsiento;
        |BLIN 4;
        |PUSHV 05012380;
        |BLANCO 0601 2380;
        |SI enSwAna = 1;
            |C_V #14#26,1,"S";
            |C_V #14#29,1,"S";
        |FINSI;
        |PINPA #0#14; |HAZ ModiPan_4;
        |ENTLINEAL #1#14,-4,4,22,0,Astoinf,Astosup;
        |POPV;
|FPRC;

|PROCESO BorraPrimero;
   |BORRA 020#599a;
   |LIBERA #599;
|FPRC;

|DEFBUCLE BorraPrimero;
  #599#1;
  ;
  #999#0, #999#1, #999#2, #999#3, 0, 0000, 00, 0000000001, 00;
  #999#0, #999#1, #999#2, #999#3, 9, 9999, 99, 9999999999, 99;
  e;
  NULL, BorraPrimero;
|FIN;

|PROCESO ModCobros;
   enSwCaja = 1;
   |SI nInkey = 12;  eModoCaja = 4; |FINSI;
   |SI nInkey = 13;  eModoCaja = 2; |FINSI;
   |SI #999#15 ! -1; eModoCaja = 4; |FINSI;

   |ABRE #1;
   |DDEFECTO #1;
   #1#0 = #999#0;
   #1#1 = #999#1;
   #1#2 = #999#2;
   #1#3 = #999#3;
   |LEE 041#1=;
   |SI FSalida ! 0;
       |MENAV "    Hay un error en los ficheros, no existe apunte";
       |CIERRA #1;
       |ACABA;
   |FINSI;
   |CIERRA #1;

   nModo = eModoCaja;
   nEste = 1; |HAZ CambiaCcw3;
   |HAZ eTrataCobroZ;
   nEste = 0; |HAZ CambiaCcw3;
   eModoCaja = nModo;

   |SI eModoCaja = 2;
       #999#14 = 0;
       |BUCLE BorraPrimero;

       |HAZ ElAuxiliar2;

       |GRABA 020#999a;
   |FINSI;

   |PONCONTROL 23, "SI";
|FPRC;

|PROCESO ActCC02;
  #502#14 = #999#12;
  #502#15 = #999#13;
  |GRABA 020#502a;
  |LIBERA #502;
|FPRC;

|DEFBUCLE ActCC02;
   #502#2;
   ;
   #999#0, #999#1, #999#2, #999#3, 0000, 01, 0000000001, 01;
   #999#0, #999#1, #999#2, #999#3, 9999, 99, 9999999999, 99;
   be;
   NULL, ActCC02;
|FIN;

|| **********************************************

|PROCESO LeeCcw3;
  |PRINT;
  enSwLinea = 2;
|FPRC;

|DEFBUCLE LeeCcw3;
   #599#1;
   ;
   #1#0, #1#1, #1#2, #1#3, 0, 0000, 01, 0000000001, 01;
   #1#0, #1#1, #1#2, #1#3, 0, 9999, 99, 9999999999, 99;
   ;
   NULL, LeeCcw3;
|FIN;

|PROCESO ImprimeAux;
  |SI nSw = 0;
      eaCuenta = #990#4;
  |FINSI;

  |SI eaCuenta ! #990#4;
     |SI #0#17 = "C";
         enSwLinea = 3; |PRINT;
     |FINSI;
     eaCuenta = #990#4;
     enSwLinea = 0;
  |FINSI;

  #1#0 = #990#0;
  #1#1 = #990#1;
  #1#2 = #990#2;
  #1#3 = #990#3;
  |LEE 041#1=;

  eaTCta  = #990#5;
  enTotal = #990#14;
  eaFor1  = #990#12;
  eaFor2  = #990#13;
  |SI #0#17 ! "C"; enSwLinea = 0; |FINSI;

  |PRINT;
  |SI #0#17 = "D";
      enSwLinea = 1;
      |BUCLE LeeCcw3;
      |SI enSwLinea = 2; enSwLinea = 3; |PRINT; |FINSI;
  |FINSI;
  nSw       = 1;
  enSwLinea = 2;
|FPRC;

|DEFBUCLE ImprimeAux;
   #990#1004
   ;
   00, "01.01.0000", 000001, 0001;
   99, "31.12.2999", 999999, 9999;
   ;
   NULL, ImprimeAux;
|FIN;

|DEFBUCLE ImprimeAux_Cta;
   #990#2005;
   ;
   "            ", "01.01.0000", 00, 000001, 0001;
   "zzzzzzzzzzzz", "31.12.2999", 99, 999999, 9999;
   ;
   NULL, ImprimeAux;
|FIN;

|PROCESO Imprimir;
  eaIa = #canempre#21;
  |HAZ LeeDatosEmpresa;

  informe = inform2;
  |SI #0#17 ! "C";
      informe = inform1;
  |FINSI;
  |MENAV "0000 Recuerde seleccionar el papel de la impresora en Horizontal";

  |IMPRE 0;
  |SI FSalida ! 0;
      |MENAV "    Proceso interrumpido";
      |ACABA;
  |FINSI;
  |INFOR informe;
  |SI FSalida ! 0;
      |MENAV "    Error. No existe informe";
      |FINIMP;
      |ACABA;
  |FINSI;

  nSw = 0;
  enSwLinea = 0;
  |ABRE #1;
  |SI #0#17 ! "C";
      |BUCLE ImprimeAux;
  |SINO;
      |BUCLE ImprimeAux_Cta;
  |FINSI;
  |CIERRA #1;

  |SI nSw = 1;
      |SI #0#17 = "C";
          enSwLinea = 3; |PRINT;
      |FINSI;
      |PIEINF;
  |FINSI;
  |FININF;
  |FINIMP;
|FPRC;

|PROCESO AntesImprimir;
  |C_M #0#17,1,"S"; |C_V #0#17,1,"S"; |C_P #0#17, 1, 1134;
  |C_M #0#18,1,"S"; |C_V #0#18,1,"S"; |C_P #0#18, 1, 1222;
  |C_M #0#19,1,"S"; |C_V #0#19,1,"S"; |C_P #0#19, 1, 1322; |C_A #0#19,1,50;

  |PUSHV  1003 1474;
  |BLANCO 1003 1474;
  |CUADRO 1003 1474;
  |CUADRO 1004 1473;
  |PINTA 1105, " Inf. Det/Res/Res.por Cta. : "; |PINTA #0#17;
  |PINTA 1205, " Fecha Informe :  "; |PINTA #0#18
  |PINTA 1305, " Titulo Informe:  "; |PINTA #0#19

|ET Campo17;
  |ENCAMPO #17#0;
  |SI FSalida = 7; |POPV; |ACABA; |FINSI;
  |SI FSalida = 1; |VETE ElInforme; |FINSI;

|ET Campo18;
  |ENCAMPO #18#0;
  |SI FSalida = 1; |VETE ElInforme; |FINSI;
  |SI FSalida = 7; |POPV; |ACABA; |FINSI;
  |SI FSalida = 2; |VETE Campo17; |FINSI;

  |ENCAMPO #19#0;
  |SI FSalida = 1; |VETE ElInforme; |FINSI;
  |SI FSalida = 7; |POPV; |ACABA; |FINSI;
  |SI FSalida = 2; |VETE Campo18; |FINSI;

|ET ElInforme;
  |CONFI "0000SImprimir informe ";
  |SI FSalida = 0;
      |HAZ Imprimir;
  |FINSI;
  |POPV;
|FPRC;


|| ******************************************************
|PROCESO ClaveAlta599;
  #599#21 = 0;
  #599#28 = 0;
  #599#0  = 00;
  #599#1  = 000001;
  #599#2  = 00;
  #599#28 = 0000;
  #599#24 = #999#0;
  #599#25 = #999#1;
  #599#26 = #999#2;
  #599#27 = #999#3;
|FPRC;

|PROCESO ClaveBaja599;
  #599#28 = 9999;
  #599#0  = 99;
  #599#1  = 9999999999;
  #599#2  = 99;
  #599#21 = 0;
  #599#28 = 9999;

  #599#24 = #999#0;
  #599#25 = #999#1;
  #599#26 = #999#2;
  #599#27 = #999#3;
|FPRC;

|CALCULO Tipo7Ccw5; |TIPO 7;

   |SI aTipoIVA = "R";
       aAlfa1 = "0000F2-Cons.Asto, F3-Imprime, F4-Cons.Cob,F5-Mod.Cob,F6-Forma Cobro,F7-Ver Fras.";
       |SI #999#15 ! -1;
           aAlfa1 = "0000F2-Cons.Asto, F3-Imprime, F4-Cons.Cob,F6-Forma Cobro,F7-Ver Fras.";
       |FINSI;
   |SINO;
       aAlfa1 = "0000F2-Cons.Asto, F3-Imprime, F4-Cons.Pag,F5-Mod.Pag,F6-Forma Pago,F7-Ver Fras.";
       |SI #999#15 ! -1;
           aAlfa1 = "0000F2-Cons.Asto, F3-Imprime, F4-Cons.Pag,F6-Forma Pago,F7-Ver Fras.";
       |FINSI;
   |FINSI;
   |MENSA aAlfa1;

   |ATRI R;
   |SI aTipoIVA = "R";|PINTA 1702, "FACTURAS ASIGNADAS AL COBRO"; |FINSI;
   |SI aTipoIVA = "S";|PINTA 1702, "FACTURAS ASIGNADAS AL PAGO";  |FINSI;
   |ATRI 0;
   |ENTLINEAL #1#599, -7, 7, 22, 0, ClaveAlta599,ClaveBaja599;
|FCAL;

|CALCULO Tipo11Ccw5; |TIPO 11;
 nInkey = FSalida;

 |SI nInkey = 10;
     |HAZ VerAsiento;
     |ERROR;
 |FINSI;

 |SI nInkey = 11;
     |BLIN 4;
     |HAZ AntesImprimir;
 |FINSI;

 |SI nInkey = 13; |Y #999#15 ! -1;
     |ERROR;
     |ACABA;
 |FINSI;

 |SI nInkey = 12; |O nInkey = 13;
     |BLIN 4;
     |LEE 141#999=;
     |HAZ ModCobros;
     |LIBERA #999;
     |PONCONTROL 23, "SI";
 |FINSI;

 |SI nInkey = 14;
     |LEE 141#999=;

     |C_M #999#12,1,"S";
     |C_M #999#13,1,"S";

   |ET AlCampo12;
     |ENCAMPO #12#999;
     |ENCAMPO #13#999;
     |SI FSalida = 2;
         |VETE AlCampo12;
     |FINSI;
     |C_M #999#12,1,"N";
     |C_M #999#13,1,"N";

     |GRABA 020#999a;
     |LIBERA #999;
     |PONCONTROL 23, "SI"

     |BUCLE ActCC02;
 |FINSI;

 |SI nInkey = 15;
     |BLIN 4;
     |ENTLINEAL #1#599, -8, 4, 22, 0, ClaveAlta599,ClaveBaja599;
 |FINSI;
|FCAL;

|| *************************************************************************
|PROCESO LeeCliente;
   nAlta = 0;
   |ABRE #18;
   |DDEFECTO #18;
   #caclipro#23 = "C";
   #caclipro#10 = eaCuenta;
   |LEE 001#18=;
   |SI FSalida ! 0;
      |DDEFECTO #18;
      #caclipro#23 = "P";
      #caclipro#10 = eaCuenta;
      |LEE 001#18=;
      |SI FSalida = 0; nAlta = 1; |FINSI;
  |SINO;
      nAlta = 1;
  |FINSI;
  |SI nAlta = 1; i_cif = #caclipro#9; i_nom = #caclipro#1; |FINSI;
  |CIERRA #18;
|FPRC;

|PROCESO IgualoCampos;
       |DDEFECTO #999;
       #999#0  = #1#0;
       #999#1  = #1#1;
       #999#2  = #1#2;
       #999#3  = #1#3;
       #999#4  = #1#04;
       #999#5  = i_nom;
       #999#6  = #1#06;
       #999#7  = #1#07;
       #999#8  = #1#09;
       #999#9  = i_cif;
       #999#10 = aTipoIVA;
|FPRC;

|PROCESO BuscoLineas;
  |DDEFECTO #501;
  #501#0  = #502#0;
  #501#1  = #502#1;
  #501#17 = #502#17;
  |LEE 041#501=;
  |SI FSalida = 0;
      |SI #501#13 ! aTipoIVA; |ACABA; |FINSI;
  |FINSI;

  ePerioCaja = #501#18; |HAZ ePathCaja;
  |CIERRA #11; |PATH_DAT #11 ePathCaja;

  |ABRE #11;
  |DDEFECTO #11;
  #11#0 = #501#0;
  #11#1 = #501#1;
  |LEE 041#11=;
  |SI FSalida ! 0;
      |CIERRA #11;
      |ACABA;
  |FINSI;
  |CIERRA #11;

  #999#14 = #999#14 + #502#10;
  #999#12 = #502#14;
  #999#13 = #502#15;
  #999#15 = #502#18;

  #599#0  = #11#0;
  #599#1  = #11#1;
  #599#2  = #502#2;
  #599#3  = #11#3;
  #599#4  = #11#2;
  #599#5  = #11#4;
  #599#6  = #11#12;
  #599#7  = #11#27;
  #599#8  = #11#28;

  #599#9  = #11#19; || Base  Fra.
  #599#10 = #11#23; || Cuota Fra.
  #599#11 = #11#21; || Total Fra.

  #599#12 = #11#13;
  #599#13 = #11#14;
  #599#14 = #11#36;

  #599#15 = #501#12; || Total Cobrado
  #599#16 = #501#6;  || Base Cobrada
  #599#17 = #501#7;  || Cuota Cobrada
  #599#22 = #501#16; || % cobrado

  #599#18 = #502#10;
  #599#19 = #502#11;
  #599#20 = #502#12;
  #599#23 = #502#13;

  #599#24 = #999#0;
  #599#25 = #999#1;
  #599#26 = #999#2;
  #599#27 = #999#3;

  #599#28 = #501#17;
  #599#29 = #501#18;
  |GRABA 020#599n;
|FPRC;

|DEFBUCLE BuscoLineas;
  #502#2;
  ;
  #999#0, #999#1, #999#2, #999#3, 0000, 00, 0000000001, 01;
  #999#0, #999#1, #999#2, #999#3, 9999, 99, 9999999999, 99;
  e;
  NULL, BuscoLineas;
|FIN;

|PROCESO ElAuxiliar2;
  |ABRE #501;
  |ABRE #599;
  |BUCLE BuscoLineas;
  |CIERRA #599;
  |CIERRA #501;
|FPRC;

|PROCESO Asientos;
  enSwCaja = 0;
  |SI enEjercicio ] 2014; |Y #1#12 ! "R";
     |Y #1#12 ! "S"; |Y #1#12 ! "N";

      |SI #1#6 = #0#11; |O #1#6 = #0#12;
          eaCuenta = #1#4;
          |HAZ LeeCliente;
          |SI nAlta = 1; enSwCaja = 1; |FINSI;
      |FINSI;
  |FINSI;
  |SI enSwCaja = 0; |ACABA; |FINSI;

  |HAZ IgualoCampos;

  |HAZ ElAuxiliar2;

|| ... Graba Auxiliar
  |SI #0#16 ! "T";
      |SI #999#8 ! #999#14; |Y #0#16 = "S"; |ACABA; |FINSI;
      |SI #999#8 = #999#14; |Y #0#16 = "N"; |ACABA; |FINSI;
  |FINSI;

  |GRABA 020#999n;
  nSwAlgun = 1;
|FPRC;

|DEFBUCLE Asientos;
   #1#1;
   4, 21, 8;
   #0#0, #0#4, #0#2, 0001, #0#7, #0#9,  aDH;
   #0#1, #0#5, #0#3, 9999, #0#8, #0#10, aDH;
   e;
   NULL, Asientos;
|FIN;

|| ******************************************************
|PROCESO CambiaCcw3;

   |SI nEste = 0;
       aAlfa1 = "N";
       |C_P #599#0, 1,1903;
       |C_P #599#1, 1,1906;
       |C_P #599#5, 1,1917;
       |C_P #599#13,1,1928;
       |C_P #599#11,1,1943;
       |C_P #599#15,1,1955;
       |C_P #599#18,1,1968;
   |SINO;
       aAlfa1 = "S";
       |C_P #599#0, 1,1403;
       |C_P #599#1, 1,1406;
       |C_P #599#5, 1,1417;
       |C_P #599#13,1,1428;
       |C_P #599#11,1,1443;
       |C_P #599#15,1,1455;
       |C_P #599#18,1,1468;
   |FINSI;

   |C_V #599#3,1,aAlfa1;
   |C_V #599#4,1,aAlfa1;
   |C_V #599#6,1,aAlfa1;
   |C_V #599#7,1,aAlfa1;
   |C_V #599#8,1,aAlfa1;
   |C_V #599#9,1,aAlfa1;
   |C_V #599#10,1,aAlfa1;
   |C_V #599#12,1,aAlfa1;
   |C_V #599#16,1,aAlfa1;
   |C_V #599#17,1,aAlfa1;
   |C_V #599#19,1,aAlfa1;
   |C_V #599#20,1,aAlfa1;
   |C_V #599#30,1,aAlfa1;
|FPRC;

|PROCESO AuxMin;
 #999#0 = 01;
 #999#1 = #0#4;
 #999#2 = 000001;
 #999#3 = 0001;
|FPRC;

|PROCESO AuxMax;
 #999#0 = 99;
 #999#1 = #0#5;
 #999#2 = 999999;
 #999#3 = 9999;
|FPRC;

|CALCULO impre; |TIPO 3;
    |ABRE #8;
    |DDEFECTO #8;
    #8#0 = 1;
    |LEE 011#8=;
    |CIERRA #8;
    |SI #8#7 = "S"; enDatos = 1; |FINSI;

    |DBASE aMas;
    aDef = aMas + "def/camaccw5";
    |CARGA_DEF aDef, imagen@;
    |SI FSalida ! 0;
        |MENAV "    Error cargando def camaccw5";
        |ACABA;
    |FINSI;

   |HAZ DirAplicSt;
   |SI Haybasica = 1; |Y #0#10 = "zz"; #0#10 = "99"; |FINSI;

   aFichero = "x" + Usuario;
   |NOME_DAT #999 aFichero;
   |NOME_DAT #990 aFichero;
   |DELINDEX #999;

   aFichero = "xz" + Usuario;
   |NOME_DAT #599 aFichero;
   |DELINDEX #599;

   |HAZ LeeDefIVA;
   |SI Haybasica = 0;  || si selecciona todos los departamentos no es necesario ver lineas
       |SI #0#9 = "  "; |Y #0#10 = "zz"; aMulDep = "N"; |FINSI;
   |SINO;
       |SI #0#9 [ 1;    |Y #0#10 = 99;   aMulDep = "N"; |FINSI;
   |FINSI;

   nSwAlgun = 0;
   aDH = "D"; |SI #0#16 = "C"; aDH = "H"; |FINSI;

   |ABRE #999;
   |BUCLE Asientos;
   |CIERRA #999;

   |SI aParam ! "Informe";
       nEste = 0; |HAZ CambiaCcw3;
   |FINSI;

   nForzCaja = 1;
   |SI nSwAlgun = 1;
       |SI aParam = "Informe";
           |HAZ Imprimir;
       |SINO;
           |PINPA #0#999;
           |SI aTipoIVA = "S";
               |ATRI I; |PINTA 0513, "Pago"; |ATRI 0;
                        |PINTA 0517, "ÄÄ";
                        |PINTA 1611, "Pago ";
               |ATRI R; |PINTA 1859, "Pagado "; |ATRI 0;

           |FINSI;
           |ENTLINEAL #1#999, -4, 4, 13, 0, AuxMin, AuxMax;
       |FINSI;
   |SINO;
       |MENAV "    No existe Asientos dentro de la seleccion informada";
   |FINSI;
   nForzCaja = 0;

   |DESCARGA_DEF #imagen@;

   |DELINDEX #999;
|FCAL;

|PROGRAMA;
   aParam = PARAMETRO; |QBF aParam;
   |HAZ inicio;
   |SI enEjercicio < 2014;
       |MENAV "    Fras en Regimen de Cr. de Caja a partir del Ejercicio 2014";
       |ACABA;
   |FINSI;

   |CLS;
   |PINPA #0#0;

   |SI aParam = "Informe";
       |ATRI R;
       |PINTA 0607, "Informe Cobro/Pago de Fras en Regimen de Criterio de Caja";
       |ATRI 0;
       |PINTA 1907, "Ã";
       |PINTA 1972, "´";
       |PINTA 2007, "³ Fecha :                   Inf.Det/Res/Res.por Cta:             ³";
       |PINTA 2107, "³ Titulo:                                                        ³";
       |PINTA 2207, "ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ";
       |ATRI S; |PINTA 2039, "D";
                |PINTA 2043, "R";
                |PINTA 2055, "C"; |ATRI 0;
       |C_M #0#17, 1, "S"; |C_V #0#17, 1, "S";
       |C_M #0#18, 1, "S"; |C_V #0#18, 1, "S";
       |C_M #0#19, 1, "S"; |C_V #0#19, 1, "S";
   |FINSI;
   |PINDA #0#0;

   |ENDATOS #1#0;
|FPRO;
