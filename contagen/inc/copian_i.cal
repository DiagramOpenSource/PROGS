|FICHEROS;
  cacuenta #6; || plan contable

  camandia #18;   || diarios
  caexistl #19;   || descarga lins.
  caportad #21;   || portadas   MODELOS OFICIALES

  calibros #27;   || Libros IVA
  caivases #28;   || Series IVA

  :/modelos/def/dsmom040 #40;     || grupos amortiz.
  :/modelos/def/dsmom041 #41;     || Amortizaciones Cabs
  :/modelos/def/dsmom042 #42;     || Amortizaciones Lins
  :/modelos/def/dsmom044 #44;     || Historico Movimiento en Curso
  :/modelos/def/dsmom045 #45;     || Historico Movimiento Pago Leasing

  cafinanc #360;  || Financiacion
  cafinanl #361;  || Lineas Financiacion

  camoauxc #106;  || Desglose Cabecera
  camoauxl #107;  || Desglose Lineas
  camautol #108;  || asientos Automaticas Lineas
  camauto1 #109;  || asientos automaticas especiales lineas
  caratios #110;  || Ratios

  cadeflab #210;
  caivaasi #200;
  camandep;
  camansde;
|FIN;

|VARIABLES;
    &fich_copi = "";
    traba      = "";
    traba1     = "";
    temporal1  = "";
    temporal2  = "";
    &num_por    = 0;
    &num_por1   = 0;
    &any_por    = 0;
    &any_por1   = 0;
    swprogra   = 0;

    &enEmpreOri  = 0;
    &enEmpreDes  = 0;
    &eaCopiar    = "";

    swPonaCero  = 0;
    eTipOpera   = 0;

    nPara1    = 0;
    aT = "";
    aAlf1 = "";
    nNum = 0;
|FIN;

*****************************************************************************

|PROCESO copia_maes; || .......................  .... Solo copia Maestros
 || Ficheros Maestros

    || Estructura Balance

|SI eSwC2008 = 0;
    traba1 = "caagrupa";  |HAZ copiando;    ||  1 agrupaciones
    traba1 = "caepigra";  |HAZ copiando;    ||  2 epigrafes
    traba1 = "caparbal";  |HAZ copiando;    ||  3 partidas de balance
    traba1 = "caparexp";  |HAZ copiando;    ||  4 partidas de explotacion
    traba1 = "cacuebal";  |HAZ copiando;    ||  5 cuentas balance
    traba1 = "cacuesig";  |HAZ copiando;    ||  6 cuentas balance segun Signo

    traba1 = "cacuenta";  |HAZ copiando;    ||  7 plan contable
    |SI nEjerActual = 2008; traba1 = "ca8m0003";  |HAZ copiando;  |FINSI; ||solo a los ejercicio 2008
|FINSI;

    traba1 = "casieaca";  |HAZ copiando;    ||  8 predef. cabs.
    traba1 = "casieali";  |HAZ copiando;    ||  9 predef. lins.
    traba1 = "camautoc";  |HAZ copiando;    || 10 Astos Automaticos C
    traba1 = "camautol";  |HAZ copiando;    || 11 Astos Automaticos L
    traba1 = "camauto0";  |HAZ copiando;    || 10a Astos Automaticos C Esp.
    traba1 = "camauto1";  |HAZ copiando;    || 11a Astos Automaticos L Esp.

    traba1 = "camandia";  |HAZ copiando;    || 12 diarios

    || Datos Generales
    traba1 = "caclipro";  |HAZ copiando;    || 13 clientes/proveedores
    traba1 = "caforpag";  |HAZ copiando;    || 14 formas de pago
                                            || ---- Provincias          00000
                                            || ---- Poblaciones         00000
                                            || ---- Entidades Bancarias 00000
                                            || ---- Portadas            00000
    || Ficheros Asociados
                                            || ---- en Modelos --- Grupos de conceptos Cab
                                            || ----                Grupos de conceptos Lin
    traba1 = "caplctac";  |HAZ copiando;    || 15 Cuentas a Mod. 130 y libros (cab)
    traba1 = "caplctal";  |HAZ copiando;    || 16 Cuentas a Mod. 130 y libros (lin)
    traba1 = "caconlib";  |HAZ copiando;    || 17 Columnas Libros Igresos/Gastos
    traba1 = "casiegru";  |HAZ copiando;    || 18 grupos predef.
    traba1 = "casiesub";  |HAZ copiando;    || 19 subgrupos predef.
    traba1 = "camautti";  |HAZ copiando;    || 20 Tipos Astos Automaticos
    traba1 = "caexttip";  |HAZ copiando;    || 21 Tipo anotaciones
                                            || ---- Control Altas ctas 00000


 || Procesos Contables

    traba1 = "caconimp";  |HAZ copiando;    || 22 Tipo de impresos

 || Procesos de IVA e IRPF

    traba1 = "calibros";  |HAZ copiando;    || 23 libros
    traba1 = "caivases";  |HAZ copiando;    || 24 Series Facturas
                                            || ---> cuentas iva cabs.  000000
                                            || ---> cuentas iva lins.
    traba1 = "caivaasi";  |HAZ copiando;    || 25 control entrada iva
    traba1 = "caivaaxx";  |HAZ copiando;    ||    control entrada iva por Act.
    traba1 = "cadeflab";  |HAZ copiando;    || 26 Defectos Laboral
    traba1 = "cadefxab";  |HAZ copiando;    || 26 Defectos Laboral
    traba1 = "cadeflal";  |HAZ copiando;    || 27 Defectos Laboral Lineas
    traba1 = "cadefxal";  |HAZ copiando;    ||
    traba1 = "cadeflac";  |HAZ copiando;    || 28 Defectos Laboral Centros
    traba1 = "cadeflax";  |HAZ copiando;    || --
    traba1 = "cadefxax";  |HAZ copiando;    || --
    traba1 = "cadeflay";  |HAZ copiando;    || --
    traba1 = "cadeflaz";  |HAZ copiando;    || --
    traba1 = "cadefxaz";  |HAZ copiando;    || --

    traba1 = "cagruiva";  |HAZ copiando;    || 29 Grupos de Facturas Englobadas
    traba1 = "calendar";  |HAZ copiando;    || 30 Calendario
    traba1 = "cacalend";  |HAZ copiando;    || 31
                                            || ---> Tipo de Operaciones financieras

    traba1 = "caconcim";  |HAZ copiando;    || 33 Tipos de Conciliacion Cab
    traba1 = "caconci2";  |HAZ copiando;    || 34 Tipos de Conciliacion Lin


    traba1 = "caexistc";  |HAZ copiando;    || 35 descarga cabs.
    traba1 = "caexistl";  |HAZ copiando;    || 36 descarga lins.

                                            || ---> Impuesto Sociedades

    traba1 = "caxcontr";  |HAZ copiando;    || 37 Control traspasos externos
    traba1 = "camanads";  |HAZ copiando;    ||    Conf.Dep y Subdepartamento


    traba1 = "caagrupr";  |HAZ copiando;    || 38 Agrupaciones Presupuestarias

    traba1 = "camoauxc";  |HAZ copiando;    || 39 Desglose cuentas
    traba1 = "camoauxl";  |HAZ copiando;    || 40 Desglose cuentas
    traba1 = "cacamctm";  |HAZ copiando;    || 41 Tabla copia de Cuentas
    traba1 = "cacamctl";  |HAZ copiando;    ||    Tabla copia Ctas por conceptos

    traba1 = "caformul";  |HAZ copiando;    || 42 formulas de ratios
    traba1 = "caratios";  |HAZ copiando;    || 43 Ratios

    traba1 = "catesfin";  |HAZ copiando;    || 44 Tesoreria
    traba1 = "n43saldo";  |HAZ copiando;    || 45 Auxiliar Ctas banco norma 43
    traba1 = "n43tempc";  |HAZ copiando;    || 46 Defectos astos. norma 43

    traba1 = "casiim03";  |HAZ copiando;    || Defectos sii.

|| ... dsarchi

    traba1 = "gadm0000";  |HAZ copiando;    || Libro / Series GAD
    traba1 = "gadm0001";  |HAZ copiando;    || Datos predeterminados
    traba1 = "gadm0004";  |HAZ copiando;    || Parametros
    traba1 = "gadm0005";  |HAZ copiando;    || Series por departamentos

    swPonaCero = 0;

    |SI enSwAna = 1; |HAZ copia_analitica; |FINSI;
|FPRC;
*****************************************************************************

|PROCESO copia_analitica;
    traba1 = "anm00001";  |HAZ copiando;    ||  1 Cuentas Analiticas
    traba1 = "anm00002";  |HAZ copiando;    ||  2 Grupos Analiticas Cabeceras
    traba1 = "anm00003";  |HAZ copiando;    ||  3    "       "      Lineas
    traba1 = "anm00004";  |HAZ copiando;    ||  4 Cuentas de Mayor
    traba1 = "anm00005";  |HAZ copiando;    ||  5 Selector Cuentas Cabeceras
    traba1 = "anm00006";  |HAZ copiando;    ||  6    "       "      Lineas
    traba1 = "anm00007";  |HAZ copiando;    ||  7 Relacion Ctas Conta/Analitica
    traba1 = "anm00010";  |HAZ copiando;    ||  8 Relacion Ctas Conta/Analitica Lineas
    traba1 = "anm00011";  |HAZ copiando;    ||  9 Seleccion de empresas Balances
    traba1 = "anm00017";  |HAZ copiando;    ||  10 Agrupaciones Presupuestarias
|FPRC;

|| *************************************************************************
|PROCESO acumulados8;  ||Series
  #28#3 = 1;
  |GRABA 020#28a;
  |LIBERA #28;
|FPRC;

|DEFBUCLE acerar8;
 #28#1;
 ;
 INICIO;
 FINAL;
 be;
 NULL, acumulados8;
|FIN;

|PROCESO acumulados7;   || Libros
  #27#4 = 1;
  |GRABA 020#27a;
  |LIBERA #27;
|FPRC;

|DEFBUCLE acerar7;
 #27#1;
 ;
 00;
 99;
 be;
 NULL, acumulados7;
|FIN;


|PROCESO Duplico_sde;
  #camansde#0 = num_por;
  |GRABA 000#camansde.n;
|FPRC;

|DEFBUCLE Duplico_sde;
  #camansde#1;
  ;
  num_por1, "  ", "   ";
  num_por1, "zz", "zzz";
  e;
  NULL, Duplico_sde;
|FIN;

|PROCESO Duplico_dep;
  #camandep#0 = num_por;
  |GRABA 000#camandep.n;
|FPRC;

|DEFBUCLE Duplico_dep;
  #camandep#1;
  ;
  num_por1, "  ";
  num_por1, "zz";
  e;
  NULL, Duplico_dep;
|FIN;

|PROCESO acerar4;
  |SI num_por1 ! num_por;
      |BUCLE Duplico_dep;
      |BUCLE Duplico_sde;
  |FINSI;

  |SI num_por1 ! num_por; |O any_por1 ! any_por;
     |ABRE #21;
     #21#0 = num_por1;
     #21#1 = any_por1;
     |LEE 000#21=;
     |SI FSalida = 0;
         #21#0 = num_por;
         #21#1 = any_por;
         |GRABA 000#21n;
     |FINSI;
     |CIERRA #21;
  |FINSI;
|FPRC;

|PROCESO acumulados5;   || Ratios
  |SI eTipOpera = 0;
      |PDEFECTO #110,3,17;
  |SINO;
      |PARA nPara1 = 3; |SI nPara1 [ 14; |HACIENDO nPara1 = nPara1 + 1;
            impMoneda = #110nPara1; |HAZ ElCambioMoneda; #110nPara1 = impMoneda;
      |SIGUE;
  |FINSI;

  |GRABA 020#110a;
  |LIBERA #110;
|FPRC;

|DEFBUCLE acerar5;
 #110#1;
 ;
 0001;
 9999;
 be;
 NULL, acumulados5;
|FIN;

|PROCESO acumulados3;  ||Existencias
  |SI eTipOpera = 0;
     |PDEFECTO #19,11,25;
  |SINO;
     |PARA nPara1 = 11; |SI nPara1 [ 24; |HACIENDO nPara1 = nPara1 + 1;
            impMoneda = #19nPara1; |HAZ ElCambioMoneda; #19nPara1 = impMoneda;
     |SIGUE;
  |FINSI;
  |GRABA 020#19a;
  |LIBERA #19;
|FPRC;

|DEFBUCLE acerar3;
 #19#1;
 ;
  0,  0;
 99, 99;
 be;
 NULL, acumulados3;
|FIN;

|PROCESO acumulados2;  ||Diarios
  |SI eTipOpera = 0;
      #18#2 = 0;
      #18#3 = 0;
  |SINO;
      impMoneda = #18#2; |HAZ ElCambioMoneda; #18#2 = impMoneda;
      impMoneda = #18#3; |HAZ ElCambioMoneda; #18#3 = impMoneda;
  |FINSI;
  |GRABA 020#18a;
  |LIBERA #18;
|FPRC;

|DEFBUCLE acerar2;
 #18#1;
 ;
  0;
 99;
 be;
 NULL, acumulados2;
|FIN;

|PROCESO acumulados1;   || Cuentas
  |SI eTipOpera = 0;
      |PDEFECTO #6,9,54;
      #6#58 ="01.01.1900";
  |SINO;
      |PARA nPara1 = 9; |SI nPara1 [ 53; |HACIENDO nPara1 = nPara1 + 1;
            impMoneda = #6nPara1; |HAZ ElCambioMoneda; #6nPara1 = impMoneda;
      |SIGUE;
  |FINSI;

  |GRABA 020#6a;
  |LIBERA #6;
|FPRC;

|DEFBUCLE acerar1;
 #6#1;
 ;
 "            ";
 "zzzzzzzzzzzz";
 be;
 NULL, acumulados1;
|FIN;

|PROCESO pone0_maes;
     traba = " PONIENDO A CERO PLAN CONTABLE Y DIARIOS ...    ";
     |SI swprogra = 0; |PINTA 2301, traba; |FINSI;
     |SI swprogra = 1; |PINTA 2211, traba; |FINSI;

     eTipOpera = 0;
     |PATH_DAT #06 fich_alta;
     |PATH_DAT #18 fich_alta;
     |PATH_DAT #19 fich_alta;
     |PATH_DAT #27 fich_alta;
     |PATH_DAT #28 fich_alta;
     |PATH_DAT #110 fich_alta;

     |BUCLE acerar1;          || cuentas
     |BUCLE acerar2;          || diarios
     |BUCLE acerar3;          || existencias
     |BUCLE acerar5;          || Ratios
     |BUCLE acerar7;          || borra Contador Libros
     |BUCLE acerar8;          || borra Contador Series

     |DBASS aBase;
     aBase = aBase + "dscomer9/def/agifa024.mas";
     |XABRE "E", aBase, 0;
     |SI FSalida ] 0;
         aAlf1 = fich_alta % -305;
         enEmpreDes = aAlf1;
         |SUB_EJECUTA "gadm0005;APERTURA";
     |FINSI;

     swPonaCero = 1;
|FPRC;

|| ************************************************************
|CALCULO CambioFijo00;
 |SI #109#8 ! 0;
     impMoneda = #109#8; |HAZ ElCambioMoneda; #109#8 = impMoneda;
     |GRABA 020#109a;
 |FINSI;
 |LIBERA #109;
|FCAL;

|DEFBUCLE CambioFijo0;
 #109#1;
 ;
 00001,0001;
 99999,9999;
 be;
 NULL, CambioFijo00;
|FIN;

|CALCULO CambioFijos0;
 |SI #108#8 ! 0;
     impMoneda = #108#8; |HAZ ElCambioMoneda; #108#8 = impMoneda;
     |GRABA 020#108a;
 |FINSI;
 |LIBERA #108;
|FCAL;

|DEFBUCLE CambioFijos;
 #108#1;
 ;
 00001,0001;
 99999,9999;
 be;
 NULL, CambioFijos0;
|FIN;

|PROCESO poneEP_maes;
  || ---------------------- Cuentas, Existencias, Diarios,
  || ---------------------- Bancos, Amortizaciones, Desglose Ctas

  |SI aMonedaC = aMonedaA; |ACABA; |FINSI;   || no hay cambio de moneda

  eaMoneda = aMonedaA; |HAZ Monedas;

  |SI swPonaCero = 0;
      eTipOpera = 1;
      |PATH_DAT #06 fich_alta;
      |PATH_DAT #18 fich_alta;
      |PATH_DAT #19 fich_alta;
      |PATH_DAT #110 fich_alta;

      |BUCLE acerar1;          || cuentas
      |BUCLE acerar2;          || diarios
      |BUCLE acerar3;          || existencias
      |BUCLE acerar5;          || existencias
  |FINSI;

  |PATH_DAT #106 fich_alta;
  |PATH_DAT #107 fich_alta;
  |PATH_DAT #108 fich_alta;
  |PATH_DAT #109 fich_alta;

  |HAZ CambioDesg;
  |BUCLE CambioFijos;
  |BUCLE CambioFijo0;
|FPRC;

|PROCESO CambiarDesgC0;
 impMoneda = #106#3;  |HAZ ElCambioMoneda; #106#3  = impMoneda;
 impMoneda = #106#4;  |HAZ ElCambioMoneda; #106#4  = impMoneda;
 #106#5 = #106#3 - #106#4;

 |GRABA 020#106a;
 |LIBERA #106;
|FPRC;

|DEFBUCLE CambiarDesgC;
  #106#1;
  ;
  INICIO;
  FINAL;
  be;
  NULL, CambiarDesgC0;
|FIN;

|PROCESO CambiarDesgL0;
 impMoneda = #107#5; |HAZ ElCambioMoneda; #107#5 = impMoneda;
 #107#6 = #107#4 * #107#5;
 |GRABA 020#107a;
 |LIBERA #107;
|FPRC;

|DEFBUCLE CambiarDesgL;
  #107#1;
  ;
  INICIO;
  FINAL;
  be;
  NULL, CambiarDesgL0;
|FIN;

|PROCESO CambioDesg;
 |BUCLE CambiarDesgC;
 |BUCLE CambiarDesgL;
|FPRC;



|| ... Nuevo
|PROCESO OperFinanL0;
   #361#0 = enEmpreDes;
   |GRABA 040#361n;
   |LIBERA #361;
|FPRC;

|DEFBUCLE OperFinanL;
 #361#1;
 ;
 enEmpreOri, 00001, 001;
 enEmpreOri, 99999, 999;
 e;
 NULL, OperFinanL0;
|FIN;

|PROCESO OperFinan0;
   #360#0 = enEmpreDes;
   #360#32= eaNombreEmp;
   |GRABA 040#360n;
   |LIBERA #360;
|FPRC;

|DEFBUCLE OperFinan;
 #360#1;
 ;
 enEmpreOri, 00001;
 enEmpreOri, 99999;
 e;
 NULL, OperFinan0;
|FIN;

|PROCESO ElemAmorL0;
   #42#0 = enEmpreDes;
   |GRABA 040#42n;
   |LIBERA #42;
|FPRC;

|DEFBUCLE ElemAmorL;
 #42#1;
 ;
 enEmpreOri, "        ", 00, 000;
 enEmpreOri, "zzzzzzzz", 99, 999;
 e;
 NULL, ElemAmorL0;
|FIN;

|PROCESO ElemAmor0;
   #41#0 = enEmpreDes;
   #41#32= eaNombreEmp;
   |GRABA 040#41n;
   |LIBERA #41;
|FPRC;

|DEFBUCLE ElemAmor;
 #41#1;
 ;
 enEmpreOri, "        ", 00;
 enEmpreOri, "zzzzzzzz", 99;
 e;
 NULL, ElemAmor0;
|FIN;

|PROCESO GrupsAmor0;
   #40#0 = enEmpreDes;
   #40#8 = eaNombreEmp;
   |GRABA 040#40n;
   |LIBERA #40;
|FPRC;

|DEFBUCLE GrupsAmor;
 #40#1;
 ;
 enEmpreOri, 0001;
 enEmpreOri, 9999;
 e;
 NULL, GrupsAmor0;
|FIN;

|PROCESO copia_otros;
 |SI enEmpreOri ! enEmpreDes;
     |BUCLE GrupsAmor;  ||grupos amortizacion
     |SI eaCopiar = "T";
         |BUCLE ElemAmor;
         |BUCLE ElemAmorL;
         |BUCLE OperFinan;
         |BUCLE OperFinanL;
     |FINSI;
 |FINSI;
|FPRC;

|| ===============================================

|PROCESO copiando;
  traba = " COPIANDO FICHERO: " + traba1 + "        ";

  |SI swprogra = 0; |PINTA 2301, traba; |FINSI;
  |SI swprogra = 1; |PINTA 2211, traba; |FINSI;

  temporal1 = fich_copi + traba1 + ".ixx";
  temporal2 = fich_alta + traba1 + ".ixx";
  |COPIA_FICHERO temporal1,temporal2;
  |SI FSalida ! 0;  |ACABA;  |FINSI;
  temporal1 = fich_copi + traba1 + ".dat";
  temporal2 = fich_alta + traba1 + ".dat";
  |COPIA_FICHERO temporal1,temporal2;
  |SI FSalida ! 0;
      temporal2 = fich_alta + traba1 + ".ixx";
      |FBORRA temporal2;
      |ACABA;
  |FINSI;
  temporal1 = fich_copi + traba1 + ".ddx";
  temporal2 = fich_alta + traba1 + ".ddx";
  |COPIA_FICHERO temporal1,temporal2;
|FPRC;
