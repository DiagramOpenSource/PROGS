|FICHEROS;
     siim0001;
     siim0002;
     siim0004;
     siim0008;

     siiw0003;
     siiw0006;
     siiw0007;
     siiw0019;
     siiw0100;
|FIN;

|VARIABLES;
     n0Vent              = 0;
     n1Vent              = 0;
     nGrid4              = -1;
     nGrid8              = -1;
     nRango              = 0;
     nPanta              = 0;
     nPantaw6            = 0;
     nCampo              = 0;
     nIdSalir            = 0;
     nReg                = 0;
     nDReg               = 0;
     nHReg               = 0;
     nCmp1               = 0;
     nCmp2               = 0;
     nOk                 = 0;
     nConsul             = 0;

     aEsc1               = "";
     aEsc2               = "";
     aRetu               = "";
     aTecla              = "";
     aAlfa               = "";
     aQQ                 = "|NC";

     fFecha              = @;

     {2}nNivel           = 0;

     {-1}aVent           = "";
         aVent1          = 0;
         aVent2          = 0;
         aVent3          = 0;
         aVent4          = 0;
         aVent5          = "";

     &enEmp              = 0;
     &enEje              = 0;
     &enPer              = 0;
     &enLot              = 0;
     &enReg              = 0;
     &eaTip              = "";
     &eaPaq              = "";
     &enTCampo           = 0;
     &enBtnAlt           = -1;
     &enBtnMod           = -1;
     &enBtnBaj           = -1;
     &enBtnEnv           = -1;
     &enBtnCtb           = -1;
     &enBtnIde           = -1;
     &enBtnRec           = -1;
     &enBtnEnt           = -1;
     &enBtnBus           = -1;
     &enBtnMas           = -1;
     &enBtnMen           = -1;
     &eaBtnLim           = "";
     &eaH60              = "";

     &eaAzul             = "";
     &eaVerde            = "";
     &eaRojo             = "";
     &eaRosa             = "";
     &eaMarron           = "";
|FIN;

|PROCESO PideFactura;
     nOk = 0;

     |VENTANA 0501, 1265, -1, 16, "Identificar factura";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |C_M #siiw0006#1, 1, "S";

     |DDEFECTO #siiw0006;
     |PINPA #0#siiw0006; nPantaw6 = FSalida;
     |PINDA #0#siiw0006;

     |CONTROL_SIMPLE nPantaw6, "BOTON,{{155}}", 0560, 0765, Busqueda;

     |ENDATOS #1#siiw0006;
     |SI FSalida = 0;
         nOk = 1;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;
|FPRC;

|PROCESO Baja8;
     #siim0008#0 = #siiw0003#0;
     #siim0008#1 = #siiw0003#1;
     #siim0008#2 = #siiw0003#2;
     #siim0008#3 = "";
|FPRC;

|PROCESO Alta8;
     #siim0008#0 = #siiw0003#0;
     #siim0008#1 = #siiw0003#1;
     #siim0008#2 = #siiw0003#2;
     #siim0008#3 = "z" * 60;
|FPRC;

|PROCESO Evnt8;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#siim0008.=;
     |FINSI;
|FPRC;

|PROCESO Baja4;
     #siim0004#0 = #siim0002#0;
     #siim0004#1 = #siim0002#1;
     #siim0004#2 = nDReg;
|FPRC;

|PROCESO Alta4;
     #siim0004#0 = #siim0002#0;
     #siim0004#1 = #siim0002#1;
     #siim0004#2 = nHReg;
|FPRC;

|PROCESO PintaCampos;
     |PARA nCampo = 0;  |SI nCampo [ enTCampo;  |HACIENDO nCampo = nCampo + 1;
           |C_V #siiw0003#nCampo#, 0, aAlfa;
           |SI aAlfa = "S";
               |PINTA #siiw0003#nCampo#;
           |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Evnt4;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#siim0004.=;

         |SI eaBtnLim = "S";
             |ESTADO_CONTROL enBtnAlt, "DISABLE";
             |ESTADO_CONTROL enBtnCtb, "DISABLE";
         |FINSI;

         |ESTADO_CONTROL enBtnEnv, "DISABLE";

         |SI enBtnMod ! -1;  |ESTADO_CONTROL enBtnMod, "ENABLE";  |FINSI;
         |SI enBtnBaj ! -1;  |ESTADO_CONTROL enBtnBaj, "ENABLE";  |FINSI;

         |SI #siim0004#78 = "C";
             |SI enBtnMod ! -1;  |ESTADO_CONTROL enBtnMod, "DISABLE";  |FINSI;
             |SI enBtnBaj ! -1;  |ESTADO_CONTROL enBtnBaj, "DISABLE";  |FINSI;
         |FINSI;

         |SI #siim0004#78 ! "P";
             |ESTADO_CONTROL enBtnEnv, "ENABLE";
         |FINSI;

         |PARA nCampo = 0;  |SI nCampo [ enTCampo;  |HACIENDO nCampo = nCampo + 1;
               #siiw0003#nCampo# = #siim0004#nCampo#;
         |SIGUE;

         nConsul = 1;
         |HAZ ControlaBtnw3;

         |HAZ PintaCampos;
     |FINSI;
|FPRC;

|PROCESO QuitaBotonSalir;
     |ESTADO_CONTROL enBtnAlt, "DISABLE";
     |ESTADO_CONTROL enBtnEnv, "DISABLE";

     |SI enBtnMod ! -1;  |ESTADO_CONTROL enBtnMod, "DISABLE";  |FINSI;
     |SI enBtnBaj ! -1;  |ESTADO_CONTROL enBtnBaj, "DISABLE";  |FINSI;
     |SI enBtnCtb ! -1;  |ESTADO_CONTROL enBtnCtb, "DISABLE";  |FINSI;

     |ESTADO_CONTROL nGrid4,   "DISABLE";

     nIdSalir = 0;
     InNivel   = nNivel1<-;
     nNivel1   = 0;
     |ESPECIFICA "IDBOTONTECLA", nNivel;

     nIdSalir = nNivel2;
     |ESTADO_CONTROL nIdSalir, "HIDE";
|FPRC;

|PROCESO PonBotonSalir;
     |ESTADO_CONTROL nGrid4,   "ENABLE";
     |ESTADO_CONTROL nIdSalir, "SHOW";
     |ESTADO_CONTROL enBtnAlt, "ENABLE";
     |ESTADO_CONTROL enBtnEnv, "DISABLE";

     |SI enBtnMod ! -1;  |ESTADO_CONTROL enBtnMod, "DISABLE";  |FINSI;
     |SI enBtnBaj ! -1;  |ESTADO_CONTROL enBtnBaj, "DISABLE";  |FINSI;
     |SI enBtnCtb ! -1;  |ESTADO_CONTROL enBtnCtb, "ENABLE";   |FINSI;
|FPRC;

|PROCESO EntraDatos;
     |DDEFECTO #siiw0003;

     |PARA nCampo = 0;  |SI nCampo [ enTCampo;  |HACIENDO nCampo = nCampo + 1;
           #siiw0003#nCampo# = #siim0004#nCampo#;
     |SIGUE;

     |C_M #siiw0003#10, 1, "S";
     |C_M #siiw0003#14, 1, "S";

     |SI #siim0002#2 ! "21";
         |C_M #siiw0003#10, 1, "N";
         |C_M #siiw0003#14, 1, "N";
      |FINSI;

     |HAZ PintaCampos;

     nConsul = 0;
     |HAZ ControlaBtnw3;

     |ESTADO_CONTROL enBtnBus, "ENABLE";
     |ESTADO_CONTROL enBtnMas, "ENABLE";
     |ESTADO_CONTROL enBtnMen, "ENABLE";

     |PULSATECLA;
     |ENDATOS #1#siiw0003;

     |ESTADO_CONTROL enBtnBus, "DISABLE";
     |ESTADO_CONTROL enBtnMas, "DISABLE";
     |ESTADO_CONTROL enBtnMen, "DISABLE";
|FPRC;

|PROCESO BtnAlt;
     |HAZ QuitaBotonSalir;

     #siim0004#0  = #siim0002#0;
     #siim0004#1  = #siim0002#1;
     #siim0004#2  = 999999;
     |LEE 000#siim0004.];
     |SI FSalida = 0;
         |LEE 000#siim0004.a;
     |SINO;
         |LEE 000#siim0004.u;
     |FINSI;

     nReg = 1;
     |SI FSalida = 0;  |Y #siim0004#0  = #siim0002#0;
                       |Y #siim0004#1  = #siim0002#1;
         nReg = #siim0004#2 + 1;
     |FINSI;

     |DDEFECTO #siim0004;
     #siim0004#0  = #siim0002#0;
     #siim0004#1  = #siim0002#1;
     #siim0004#2  = nReg;
     #siim0004#3  = #siim0002#4;
     #siim0004#4  = #siim0002#5;
     |GRABA 020#siim0004.n;
     |LIBERA #siim0004;

     |SI #siim0002#2 ! "21";
         |HAZ PideFactura;
         |SI nOk = 0;
             #siim0004#0  = #siim0002#0;
             #siim0004#1  = #siim0002#1;
             #siim0004#2  = nReg;
             |LEE 101#siim0004.=;

             |BORRA 020#siim0004.a;

             |HAZ PonBotonSalir;
             |FOCOTECLADO nGrid4;
             |ACABA;
         |FINSI;
     |FINSI;

     FSalida = 0;
     |SI #siim0002#2 ! "25";
         |HAZ EntraDatos;
     |FINSI;

     |SI FSalida = 0;
         #siim0004#0  = #siim0002#0;
         #siim0004#1  = #siim0002#1;
         #siim0004#2  = nReg;
         |LEE 101#siim0004.=;

         |PARA nCampo = 3;  |SI nCampo [ enTCampo;  |HACIENDO nCampo = nCampo + 1;
               #siim0004#nCampo# = #siiw0003#nCampo#;
         |SIGUE;

         #siim0004#86 = "N";
         |SI #siim0002#2 = "25";
             #siim0004#86 = "S";
         |FINSI;

         |GRABA 020#siim0004.a;
     |SINO;
         #siim0004#0  = #siim0002#0;
         #siim0004#1  = #siim0002#1;
         #siim0004#2  = nReg;
         |LEE 101#siim0004.=;

         |BORRA 020#siim0004.a;
     |FINSI;

     |LIBERA #siim0004;

     |HAZ PonBotonSalir;

     |REFRESCACONTROL nGrid4;
     |FOCOTECLADO nGrid4;
|FPRC;

|PROCESO BtnMod;
     |LEE 101#siim0004.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |HAZ QuitaBotonSalir;

     |HAZ EntraDatos;
     |SI FSalida = 0;
         |PARA nCampo = 3;  |SI nCampo [ enTCampo;  |HACIENDO nCampo = nCampo + 1;
               #siim0004#nCampo# = #siiw0003#nCampo#;
         |SIGUE;

         #siim0004#86 = "N";
         |SI #siim0002#2 = "25";
             #siim0004#86 = "S";
         |FINSI;

         |GRABA 020#siim0004.a;
     |FINSI;

     |LIBERA #siim0004;

     |REFRESCACONTROL nGrid4;
     |FOCOTECLADO nGrid4;

     |HAZ PonBotonSalir;
|FPRC;

|PROCESO siim0008B;
     |BORRA 020#siim0008.a;
     |LIBERA #siim0008;
|FPRC;

|DEFBUCLE siim0008B;
     #siim0008#1;
     ;
     #siim0004#0, #siim0004#1, #siim0004#2, "";
     #siim0004#0, #siim0004#1, #siim0004#2, eaH60;
     be;
     NULL, siim0008B;
|FIN;

|PROCESO BtnBaj;
     |LEE 101#siim0004.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |CONFI "0000 Confirma la baja del registro seleccionado";
     |SI FSalida ! 0;
         |FOCOTECLADO nGrid4;
         |ACABA;
     |FINSI;

     |BORRA 020#siim0004.a;
     |LIBERA #siim0004;

     |BUCLE siim0008B;

     |ESTADO_CONTROL enBtnAlt, "ENABLE";
     |ESTADO_CONTROL enBtnEnv, "DISABLE";

     |SI enBtnMod ! -1;  |ESTADO_CONTROL enBtnMod, "DISABLE";  |FINSI;
     |SI enBtnBaj ! -1;  |ESTADO_CONTROL enBtnBaj, "DISABLE";  |FINSI;
     |SI enBtnCtb ! -1;  |ESTADO_CONTROL enBtnCtb, "ENABLE";   |FINSI;

     |REFRESCACONTROL nGrid4;
     |FOCOTECLADO nGrid4;
|FPRC;

|PROCESO BtnIde;
     |VENTANA 0501, 2080, -1, 16, "Informe de las facturas";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |SI nConsul = 0;
         nRango   = 1 + 2 + 4 + 8 + 16 + 32 + 128;
     |SINO;
         nRango   = 2 + 4 + 8 + 16 + 32 + 128;
     |FINSI;

     |LINEAL_SIMPLE #1#siim0008, nRango, 0502, 1879, Baja8, Alta8, Evnt8;
     nGrid8 = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::{{001}}Salir," + nGrid8;
          |LEETECLA aTecla;
          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
          |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;
|FPRC;

|PROCESO BtnRec;
     |VENTANA 0501, 1765, -1, 16, "Datos factura rectificativa";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     #siiw0100#0 = #siiw0003#70;
     #siiw0100#1 = #siiw0003#71;
     #siiw0100#2 = #siiw0003#72;
     #siiw0100#3 = #siiw0003#73;

     |SI #siiw0100#0 = " ";
         #siiw0100#0 = "I";
     |FINSI;

     |PINPA #0#siiw0100;
     |PINDA #0#siiw0100;
     |SI nConsul = 0;
         |ENDATOS #1#siiw0100;
         |SI FSalida = 0;
             #siiw0003#70 = #siiw0100#0;
             #siiw0003#71 = #siiw0100#1;
             #siiw0003#72 = #siiw0100#2;
             #siiw0003#73 = #siiw0100#3;
         |FINSI;
     |SINO;
         |PAUSA;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;
|FPRC;

|PROCESO BtnEnt;
     |VENTANA 0501, 1170, -1, 16, "Entidad Sucedida";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     #siiw0019#0 = #siiw0003#90;
     #siiw0019#1 = #siiw0003#91;

     |PINPA #0#siiw0019;
     |PINDA #0#siiw0019;
     |SI nConsul = 0;
         |ENDATOS #1#siiw0019;
         |SI FSalida = 0;
             #siiw0003#90 = #siiw0019#0;
             #siiw0003#91 = #siiw0019#1;
         |FINSI;
     |SINO;
         |PAUSA;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;
|FPRC;

|PROCESO BtnEnv;
     |VENTANA 0501, 1599, -1, 16, "Datos del env¡o";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     #siiw0007#0 = #siiw0003#75;
     #siiw0007#1 = #siiw0003#76;
     #siiw0007#2 = #siiw0003#77;

     |SI #siiw0003#78 = "C";  #siiw0007#3 = "Correcto";            |FINSI;
     |SI #siiw0003#78 = "A";  #siiw0007#3 = "AceptadaConErrores";  |FINSI;
     |SI #siiw0003#78 = "I";  #siiw0007#3 = "Incorrecto";          |FINSI;

     #siiw0007#4 = #siiw0003#81;
     #siiw0007#5 = #siiw0003#79;
     #siiw0007#6 = #siiw0003#80 % 180;
     #siiw0007#7 = #siiw0003#80 % 8180;
     #siiw0007#8 = #siiw0003#80 % 16180;

     |PINPA #0#siiw0007;
     |PINDA #0#siiw0007;
     |LEETECLA aAlfa;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;
|FPRC;

|PROCESO BtnCtb;
     |VENTANA 0501, 0501, -1, 16, "";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     enEmp = #siim0002#0;
     enLot = #siim0002#1;
     eaTip = #siim0002#2;
     enEje = #siim0002#4;
     enPer = #siim0002#5;

     |SI eaPaq = "BASICA";
         |SUB_EJECUTA "siiz0030;UNAUNA";
     |FINSI;

     |SI eaPaq = "AGICLI";
         |SUB_EJECUTA "siiz0031;UNAUNA";
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;

     |REFRESCACONTROL nGrid4;
     |FOCOTECLADO nGrid4;
|FPRC;

|PROCESO siim0004B;
     |BUCLE siim0008B;

     |BORRA 020#siim0004.a;
     |LIBERA #siim0004;
|FPRC;

|DEFBUCLE siim0004B;
     #siim0004#1;
     ;
     #siim0002#0, #siim0002#1, 000001;
     #siim0002#0, #siim0002#1, 999999;
     be;
     NULL, siim0004B;
|FIN;

|PROGRAMA;
     |SI PARAMETRO = "REFRESCA";
         |ACABA;
     |FINSI;

     |SI PARAMETRO = "BORRAR";
         |BUCLE siim0004B;

         |ACABA;
     |FINSI;

     aAlfa = "";

     |SI #siim0002#2 = "21";
         aAlfa = "Bandeja ALTAS facturas recibidas ";
     |FINSI;

     |SI #siim0002#2 = "22";
         aAlfa = "Bandeja MODIFICACIONES recibidas ";
     |FINSI;

     |SI #siim0002#2 = "25";
         aAlfa = "Bandeja BAJAS recibidas ";
     |FINSI;

     aAlfa = aAlfa  + "(Empresa " +(("00000" + #siim0001#0) % -105);
     aAlfa = aAlfa  + " " + #siim0001#1;  |QBF aAlfa;
     aAlfa = aAlfa  + " Periodo " + #siim0002#4 + "/" + (("00" + #siim0002#5) % -102);
     aAlfa = aAlfa  + " Lote " + (("0000000000" + #siim0002#1) % -110) + ")";
     |VENTANA 0501, 3799, -1, 16, aAlfa;
     n0Vent = FSalida;

     |PINPA #0#siiw0003;  nPanta = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}Alta ", 1264, 1274, BtnAlt;
     enBtnAlt = FSalida;

     |SI #siim0002#2 ! "25";
         |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Modificar ", 1276, 1286, BtnMod;
         enBtnMod = FSalida;
     |FINSI;

     |CONTROL_SIMPLE nPanta, "BOTON,{{199}}Borrar", 1288, 1298, BtnBaj;
     enBtnBaj = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Estado del env¡o", 1202, 1220, BtnEnv;
     enBtnEnv = FSalida;

     |SI eaPaq ! "";  |Y #siim0002#2 = "21";
         |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Recogida selectiva", 1222, 1240, BtnCtb;
         enBtnCtb = FSalida;
     |FINSI;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}", 3272, 3273, BtnIde;
     enBtnIde = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}", 3372, 3373, BtnRec;
     enBtnRec = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}", 3472, 3473, BtnEnt;
     enBtnEnt = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{155}}", 3643, 3646, BtnBus;
     enBtnBus = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON, + ", 3648, 3651, BtnMas;
     enBtnMas = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON, - ", 3653, 3656, BtnMen;
     enBtnMen = FSalida;

     |SI enBtnMod ! -1;  |ESTADO_CONTROL enBtnMod, "DISABLE";  |FINSI;
     |SI enBtnBaj ! -1;  |ESTADO_CONTROL enBtnBaj, "DISABLE";  |FINSI;

     |ESTADO_CONTROL enBtnEnv, "DISABLE";
     |ESTADO_CONTROL enBtnIde, "DISABLE";
     |ESTADO_CONTROL enBtnRec, "DISABLE";
     |ESTADO_CONTROL enBtnEnt, "DISABLE";
     |ESTADO_CONTROL enBtnBus, "DISABLE";
     |ESTADO_CONTROL enBtnMas, "DISABLE";
     |ESTADO_CONTROL enBtnMen, "DISABLE";

     |DDEFECTO #siiw0003;
     |HAZ PintaCampos;

     nDReg = 1;
     nHReg = 999999;
     |SI enReg ! 0;
         nDReg = enReg;
         nHReg = enReg;
     |FINSI;

     nRango   = 2 + 4 + 8 + 16 + 32 + 128;
     |LINEAL_SIMPLE #1#siim0004, nRango, 0502, 1198, Baja4, Alta4, Evnt4;
     nGrid4 = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::{{001}}Salir," + nGrid4;
          |LEETECLA aTecla;
          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
          |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FINVENTANA n0Vent;

     |PULSATECLA;
|FPRO;

|PROCESO T80;  |TIPO 80;
     |HAZ CargaColores;

     |ABRET #siim0004;
     |ABRET #siim0008;

     |ABRE #siim0001;
     |SELECT #1#siim0001;
     #siim0001#0 = enEmp;
     |LEE 000#siim0001.=;
     |CIERRA #siim0001;

     |ABRE #siim0002;
     #siim0002#0 = enEmp;
     #siim0002#1 = enLot;
     |LEE 000#siim0002.=;
     |CIERRA #siim0002;

     eaH60    = "z" * 60;
     aQQ      = "|NC";
     aAlfa    = #siiw0003#aQQ#;
     enTCampo = aAlfa;
     enTCampo = enTCampo - 1;
|FPRC;

|PROCESO siim0004C;
                              #siim0002#6  = #siim0002#6  + 1;
     |SI #siim0004#78 = "P";  #siim0002#7  = #siim0002#7  + 1;  |FINSI;
     |SI #siim0004#78 = "I";  #siim0002#7  = #siim0002#7  + 1;  |FINSI;
     |SI #siim0004#78 = "I";  #siim0002#10 = #siim0002#10 + 1;  |FINSI;
     |SI #siim0004#78 = "C";  #siim0002#8  = #siim0002#8  + 1;  |FINSI;
     |SI #siim0004#78 = "A";  #siim0002#9  = #siim0002#9  + 1;  |FINSI;

     |SI #siim0002#15 < #siim0004#76;
         #siim0002#15 = #siim0004#76;
     |FINSI;

     |SI #siim0002#15 = #siim0004#76;
         |SI #siim0002#16 < #siim0004#77;
             #siim0002#16 = #siim0004#77;
         |FINSI;
     |FINSI;

     #siim0002#17 = #siim0002#17 + #siim0004#20 + #siim0004#27 + #siim0004#34;
     #siim0002#17 = #siim0002#17 + #siim0004#41 + #siim0004#48 + #siim0004#55;
     #siim0002#17 = #siim0002#17 + #siim0004#61;

     #siim0002#18 = #siim0002#18 + #siim0004#22 + #siim0004#29 + #siim0004#36;
     #siim0002#18 = #siim0002#18 + #siim0004#43 + #siim0004#50 + #siim0004#57;
     #siim0002#18 = #siim0002#18 + #siim0004#63;

     #siim0002#19 = #siim0002#19 + #siim0004#24 + #siim0004#31 + #siim0004#38;
     #siim0002#19 = #siim0002#19 + #siim0004#45 + #siim0004#52 + #siim0004#59;

     #siim0002#20 = #siim0002#20 + #siim0004#64;

     #siim0004#82 = "";
     |SI #siim0004#78 = "C";
         #siim0004#82 = eaVerde;
     |FINSI;

     |SI #siim0004#78 = "A";
         #siim0004#82 = eaAzul;
     |FINSI;

     |SI #siim0004#78 = "I";
         #siim0004#82 = eaRojo;
     |FINSI;

     #siim0004#86 = "N";
     |SI #siim0002#2 = "25";
         #siim0004#86 = "S";
     |FINSI;

     |GRABA 020#siim0004.a;
     |LIBERA #siim0004;
|FPRC;

|DEFBUCLE siim0004C;
     #siim0004#1;
     ;
     #siim0002#0, #siim0002#1, 000001;
     #siim0002#0, #siim0002#1, 999999;
     be;
     NULL, siim0004C;
|FIN;

|PROCESO T90;  |TIPO 90;
     |ABRE #siim0002;
     #siim0002#0 = enEmp;
     #siim0002#1 = enLot;
     |LEE 101#siim0002.=;
     |SI FSalida = 0;
         #siim0002#6  = 0;
         #siim0002#7  = 0;
         #siim0002#8  = 0;
         #siim0002#9  = 0;
         #siim0002#10 = 0;
         #siim0002#15 = "01.01.0000";
         #siim0002#16 = "";
         #siim0002#17 = 0;
         #siim0002#18 = 0;
         #siim0002#19 = 0;
         #siim0002#20 = 0;

         |BUCLE siim0004C;

         #siim0002#11 = "";

         nReg = #siim0002#8 + #siim0002#9;
         |SI nReg = #siim0002#6;  |Y nReg ! 0;
             #siim0002#11 = eaVerde;
         |FINSI;

         |SI #siim0002#10 ! 0;
             #siim0002#11 = eaRojo;
         |FINSI;

         #siim0002#13 = #siim0001#1;
         #siim0002#14 = #siim0001#2;

         |GRABA 020#siim0002.a;
         |LIBERA #siim0002;
     |FINSI;
     |CIERRA #siim0002;

     |CIERRAT #siim0004;
     |CIERRAT #siim0008;
|FPRC;
