|FICHEROS;
     siim0500;
     siim0501;

     siiw0103;
|FIN;

|VARIABLES;
     n0Vent              = 0;
     n1Vent              = 0;
     nGrid500            = -1;
     nRango              = 0;
     nTree               = 0;
     nHandle             = 0;
     nPnt1               = 0;
     nPnt2               = 0;
     nCmp                = 0;
     nIdSalir            = 0;

     aEsc1               = "";
     aEsc2               = "";
     aRetu               = "";
     aTecla              = "";
     aAlfa               = "";
     aBase               = "";
     aPath               = "";
     aFic                = "";
     aAbre               = "";
     aContenido          = "";
     aVer                = "";

     {2}nNivel           = 0;

     {-1}sTree           = 0;
     sT_nID              = 0;
     sT_nOper            = 0;
     sT_aData            = "";

     {-1}aVent           = "";
         aVent1          = 0;
         aVent2          = 0;
         aVent3          = 0;
         aVent4          = 0;
         aVent5          = "";

     &enBtnAlt           = 0;
     &enBtnMod           = 0;
     &eaVer              = "";
     &eaEsq              = "";
|FIN;

|PROCESO EntraDatos;
     |DDEFECTO #siiw0103;

     |PARA nCmp = 0;  |SI nCmp [ 23;  |HACIENDO nCmp = nCmp + 1;
           #siiw0103#nCmp# = #siim0500#nCmp#;
     |SIGUE;

     |PINTA #siiw0103#0;
     |PINTA #siiw0103#1;
     |PINTA #siiw0103#2;
     |PINTA #siiw0103#3;
     |PINTA #siiw0103#4;

     |PULSATECLA;
     |ENDATOS #1#siiw0103;
|FPRC;

|PROCESO siim0501;
     #siim0501#0 = #siim0500#2;
     |GRABA 000#siim0501.n;
|FPRC;

|DEFBUCLE siim0501;
     #siim0501#1;
     ;
     aVer, "  ", 00, 00, 00, 00, 00, 00, 00, 00, 00, 00;
     aVer, "zz", 99, 99, 99, 99, 99, 99, 99, 99, 99, 99;
     ;
     NULL, siim0501;
|FIN;

|PROCESO BtnAlt;
     |VENTANA 0501, 3085, -1, 16, "Nueva versi¢n";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |DDEFECTO #siim0500;

     |C_M #siiw0103#0, 1, "S";
     |C_M #siiw0103#1, 1, "S";

     |HAZ EntraDatos;
     |SI FSalida = 0;
         |LEE 000#siim0500.p;
         aVer = #siim0500#2;

         |DDEFECTO #siim0500;
         |PARA nCmp = 0;  |SI nCmp [ 23;  |HACIENDO nCmp = nCmp + 1;
               #siim0500#nCmp# = #siiw0103#nCmp#;
         |SIGUE;

         |GRABA 020#siim0500.n;
         |LIBERA #siim0500;

         |BUCLE siim0501;

         |ESTADO_CONTROL nTree, "ENABLE";
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;

     |REFRESCACONTROL nGrid500;
     |FOCOTECLADO nGrid500;
|FPRC;

|PROCESO BtnMod;
     |VENTANA 0501, 3085, -1, 16, "Modificaci¢n";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |LEE 101#siim0500.=;

     |C_M #siiw0103#0, 1, "N";
     |C_M #siiw0103#1, 1, "N";

     |HAZ EntraDatos;
     |SI FSalida = 0;
         |PARA nCmp = 0;  |SI nCmp [ 23;  |HACIENDO nCmp = nCmp + 1;
               #siim0500#nCmp# = #siiw0103#nCmp#;
         |SIGUE;

         |GRABA 020#siim0500.a;
         |LIBERA #siim0500;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;

     |REFRESCACONTROL nGrid500;
     |FOCOTECLADO nGrid500;
|FPRC;

|PROCESO EvntTree;
     aContenido = Contenido % -102;  |QBF aContenido;
     |SI aContenido = "";  |ACABA;  |FINSI;

     |SI FEntrada = 0;
         |ACABA;
     |FINSI;

     |LEE 101#siim0500.=;

     eaVer = #siim0500#2;
     eaEsq = aContenido;

     |SUB_EJECUTA "siiz0501";

     |LIBERA #siim0500;

     |FOCOTECLADO nTree;
|FPRC;

|PROCESO Evnt500;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#siim0500.=;

         |ESTADO_CONTROL enBtnMod, "ENABLE";
         |ESTADO_CONTROL nTree, "ENABLE";
     |FINSI;
|FPRC;

|PROCESO GrabaTree;
     aAlfa = aAlfa + &13 + &10;
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO CreaTree;
     |DBASE aBase;  |QBF aBase;
     aPath = "C:\documentos";     |RMKDIR aPath;
     aPath = "C:\documentos\tmp"; |RMKDIR aPath;
     aPath = "C:\documentos\tmp\";
     aFic  = "siiz0500" + Usuario + ".txt";

     aAbre = aPath + aFic;

     |XABRE "WBC", aAbre, nHandle;

     nPnt1 = 0;
     nPnt2 = 0;
     nPnt1 = nPnt1 + 1;
     aAlfa = nPnt1 + ":Facturas expedidas";  |HAZ GrabaTree;

     nPnt2 = nPnt2 + 1;
     aAlfa = nPnt1 + "," + nPnt2 + ":Alta y Modificaci¢n {{11}}";  |HAZ GrabaTree;
     nPnt2 = nPnt2 + 1;
     aAlfa = nPnt1 + "," + nPnt2 + ":Suministro de Cobros {{12}}";  |HAZ GrabaTree;
     nPnt2 = nPnt2 + 1;
     aAlfa = nPnt1 + "," + nPnt2 + ":Baja de facturas {{13}}";  |HAZ GrabaTree;

     nPnt1 = nPnt1 + 1;
     nPnt2 = 0;
     aAlfa = nPnt1 + ":Facturas recibidas";  |HAZ GrabaTree;

     nPnt2 = nPnt2 + 1;
     aAlfa = nPnt1 + "," + nPnt2 + ":Alta y Modificaci¢n {{21}}";  |HAZ GrabaTree;
     nPnt2 = nPnt2 + 1;
     aAlfa = nPnt1 + "," + nPnt2 + ":Suministro de Pagos {{22}}";  |HAZ GrabaTree;
     nPnt2 = nPnt2 + 1;
     aAlfa = nPnt1 + "," + nPnt2 + ":Baja de facturas {{23}}";  |HAZ GrabaTree;

     nPnt1 = nPnt1 + 1;
     nPnt2 = 0;
     aAlfa = nPnt1 + ":Facturas bienes de inversi¢n";  |HAZ GrabaTree;

     nPnt2 = nPnt2 + 1;
     aAlfa = nPnt1 + "," + nPnt2 + ":Alta y Modificaci¢n {{31}}";  |HAZ GrabaTree;
     nPnt2 = nPnt2 + 1;
     aAlfa = nPnt1 + "," + nPnt2 + ":Baja de facturas {{33}}";  |HAZ GrabaTree;

     nPnt1 = nPnt1 + 1;
     nPnt2 = 0;
     aAlfa = nPnt1 + ":Operaciones en met lico";  |HAZ GrabaTree;

     nPnt2 = nPnt2 + 1;
     aAlfa = nPnt1 + "," + nPnt2 + ":Alta y Modificaci¢n {{41}}";  |HAZ GrabaTree;
     nPnt2 = nPnt2 + 1;
     aAlfa = nPnt1 + "," + nPnt2 + ":Baja de registros {{43}}";  |HAZ GrabaTree;

     |XCIERRA nHandle;

     aAlfa = "TREECTRL, Esquemas XML {{" + aAbre + "}}";
     |CONTROL_SIMPLE 0, aAlfa, 0670, 2598, EvntTree;
     nTree = FSalida;

     sT_nID   = nTree;
     sT_nOper = 2;
     sT_aData = ":1";
     |ESPECIFICA "COMMANDCONTROL", sTree;
|FPRC;

|PROGRAMA;
     |VENTANA 0501, 2899, -1, 16, "Bandeja de versiones";
     n0Vent = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{193}}Alta ", 2655, 2665, BtnAlt;
     enBtnAlt = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Modificar ", 2643, 2653, BtnMod;
     enBtnMod = FSalida;

     |HAZ CreaTree;

     |ESTADO_CONTROL nTree, "DISABLE";
     |ESTADO_CONTROL enBtnMod, "DISABLE";

     nRango    = 4 + 8 + 16 + 32 + 128;

     |LINEAL_SIMPLE #2#siim0500, nRango, 0602, 2565, NULL, NULL, Evnt500;
     nGrid500 = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}Salir," + nGrid500;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nTree;

     |FINVENTANA n0Vent;

     |PULSATECLA;
|FPRO;

|PROCESO T80;  |TIPO 80;
     |ABRET #siim0500;
|FPRC;

|PROCESO T90;  |TIPO 90;
     |CIERRAT #siim0500;

     |SI aAbre ! "";
         |RFBORRA aAbre;
     |FINSI;
|FPRC;
