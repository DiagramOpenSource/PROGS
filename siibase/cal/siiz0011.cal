|FICHEROS;
     siiz0011;
     siiw0108;

     siim1001;
|FIN;

|VARIABLES;
     n1Vent              = 0;
     nOk                 = 0;
     nCampo              = 0;
     nCar                = 0;
     nPos                = 0;
     nHandle             = 0;
     nIde                = 0;
     nCont               = 0;
     nPan                = 0;
     nBtnPres             = 0;
     nBtnPre             = 0;
     nCon                = 0;
     nPausa              = 0;

     aAlfa               = "";
     aFic                = "";
     aDocRemoto          = "";
     aDocServidor        = "";
     aOrigen             = "";
     aDestino            = "";
     aLon                = "";
     aDato               = "";
     aReg                = "";
     aSep                = "";
     aLetra              = "";
     aFicCert            = "";
     aPathLan            = "";
     aAbre               = "";
     aEjec               = "";

 {99}aValor              = "";
 {10}aPopup              = "";

 {-1}aVent               = "";
     aVent1              = 0;
     aVent2              = 0;
     aVent3              = 0;
     aVent4              = 0;
     aVent5              = "";

     &eaUnidadBasico     = "";
     &eaDSMAC            = "";
     &eaCert             = "";
|FIN;

|PROCESO T0C2;  |TIPO 0;
|FPRC;

|PROCESO BtnPres;
     nCont = 1;

     |PTEC 806;
|FPRC;

|PROCESO Cancelar;
     |PTEC 807;
|FPRC;

|PROCESO Certificados;
     |CONSULTA_CLAVE #2#siiw0108;
     |SI FSalida = 0;
         #siiz0011#0 = #siiw0108#1;  |PINTA #siiz0011#0;
         #siiz0011#1 = #siiw0108#2;  |PINTA #siiz0011#1;
         #siiz0011#2 = #siiw0108#3;
     |FINSI;

     |SI #siiz0011#0 ! "         ";
         |ESTADO_CONTROL nBtnPre, "ENABLE";
     |FINSI;
|FPRC;

|PROCESO Predeterminar;
     aPopup1  = nBtnPre;  || Si fuera -1 en la posicion
     aPopup2  = 2;
     aPopup3  = "Grabar predeterminado";
     aPopup4  = "Quitar predeterminado";

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida = 1;  |O FSalida = 2;
         |SI FSalida = 1;  aAlfa = #siiz0011#2;  |FINSI;
         |SI FSalida = 2;  aAlfa = "";  |FINSI;

         |ABRE #siim1001;
         #siim1001#0 = eaDSMAC;
         |LEE 101#siim1001.=;
         |SI FSalida ! 0;
             |DDEFECTO #siim1001;
             #siim1001#0 = eaDSMAC;
             |GRABA 020#siim1001.b;
         |FINSI;

         #siim1001#1 = aAlfa;
         |GRABA 020#siim1001.a;
         |LIBERA #siim1001;
         |CIERRA #siim1001;
     |FINSI;
|FPRC;

|PROGRAMA;
     |VENTANA 0501, 3599, -1, 16, "Certificados";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PINPA #0#siiz0011;  nPan = FSalida;
     |PINDA #0#siiz0011;

     |CONTROL_SIMPLE nPan, "LABEL,{{15}}Conforme ", 1202, 1213, NULL;

     aAlfa = "LABEL,{{7}}(Para finalizar el proceso marque la casilla ";
     aAlfa = aAlfa + &34 + "Conforme"  + &34 + "y pulse en " + &34 + "Presentar" + &34 + ")";
     |CONTROL_SIMPLE nPan, aAlfa, 1220, 1298, NULL;

     |CONTROL_SIMPLE nPan, "BOTON,{{197}}", 0921, 0922, Certificados;
     |CONTROL_SIMPLE 0, "BOTON,{{207}}Cancelar", 3469, 3582, Cancelar;
     |CONTROL_SIMPLE 0, "BOTON,{{197}}Presentar", 3485, 3598, BtnPres;
     nBtnPres = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Predeterminar presentador", 3502, 3525, Predeterminar;
     nBtnPre = FSalida;

     |SI #siiz0011#0 = "         ";
         |ESTADO_CONTROL nBtnPre, "DISABLE";
     |FINSI;

     nCont = 0;
     nOk   = 0;
     |PARA; |SI;  |HACIENDO;
         |SI #siiz0011#3 = "S";
             |ESTADO_CONTROL nBtnPres, "ENABLE";
         |SINO;
             |ESTADO_CONTROL nBtnPres, "DISABLE";
         |FINSI;

         |PINTA #siiz0011#3;

         |ENCAMPO #3#siiz0011;

         nOk = 0;
         |SI nCont   = 1;  nOk = 1;  |SAL;  |FINSI;
         |SI FSalida = 7;            |SAL;  |FINSI;
     |SIGUE;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;

     eaCert = "";
     |SI nOk = 1;
         eaCert = #siiz0011#2;
     |FINSI;
     |QBF eaCert;
|FPRO;

|PROCESO Datos;
     aValor1 = "";
     aValor2 = "";

     nCampo  = 1;
     IaValor = aValor1<-;
     aLon    = aDato % 0;
     aReg    = "";
     aSep    = ";";
     |PARA nCar = 1; |SI nCar [ aLon; |HACIENDO nCar = nCar + 1;
          nPos   = (nCar * 100) + 1;
          aLetra = aDato % nPos;
          |SI aLetra = aSep;
              aValor  = aReg;
              aReg    = "";
              nCampo  = nCampo + 1;
              IaValor = IaValor + 1;
          |SINO;
               aReg = aReg + aLetra;
          |FINSI;

          |SI nCampo > 90; |SAL; |FINSI;     || Por si.... :)
     |SIGUE;

     aValor = aReg;
|FPRC;

|PROCESO ListaCertificados;
     |HAZ TraeDslistacerti;

     aPathLan = eaUnidadBasico + "\documentos\siibase\dsmac\";
     aFicCert = aPathLan + "certifi.csv";

     aAbre = aPathLan + "ejecuta.bat";
     |XABRE "WBC", aAbre, nHandle;

     aAlfa = aPathLan + "ds_listacerti ds123456 " + aFicCert;
     |XGRABA nHandle, aAlfa;

     |XCIERRA nHandle;

     aEjec = eaUnidadBasico + "\documentos\siibase\dsmini " + aAbre;
     |RSYSTEM aEjec;

     nCon  = 0;
     |PARA;  |SI;  |HACIENDO;
             |PARA nPausa = 1; |SI nPausa [ 10000; |HACIENDO nPausa = nPausa + 1;
             |SIGUE;

             |XABRE "EC", aFicCert, 0;
             |SI FSalida ] 0;
                 |SAL;
             |FINSI;

             |SLEEP 1;
             |PULSATECLA;

             nCon = nCon + 1;
             |SI nCon > 60;  |SAL;  |FINSI;
     |SIGUE;

     aAbre = aFicCert;
     |XABRE "UC", aAbre, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     nIde = 0;
     |PARA; |SI; |HACIENDO;
          |XLEE nHandle, 250, aDato;
          |SI FSalida < 0;  |SAL;  |FINSI;

          |HAZ Datos;

          nIde = nIde + 1;

          #siiw0108#0 = nIde;
          #siiw0108#1 = aValor1;
          #siiw0108#2 = aValor2;
          #siiw0108#3 = aValor3;
          |GRABA 020#siiw0108.n;
          |LIBERA #siiw0108;
     |SIGUE;
     |XCIERRA nHandle;

     aAbre = aPathLan + "ejecuta.bat";  |RFBORRA aAbre;
     aAbre = aFicCert;                  |RFBORRA aAbre;
|FPRC;

|PROCESO T80;  |TIPO 80;
     eaDSMAC = "";
     |HAZ LeeUnidadBasico;

     aFic = "siiw0108" + Usuario;
     |NOME_DAT #siiw0108#aFic#;
     |ABRE #siiw0108;  |CIERRA #siiw0108;  |DELINDEX #siiw0108;
     |ABRET #siiw0108;

     |HAZ CogeMac;
     |HAZ ListaCertificados;

     |ABRE #siim1001;
     #siim1001#0 = eaDSMAC;
     |LEE 000#siim1001.=;
     |SI FSalida ! 0;  |DDEFECTO #siim1001;  |FINSI;
     |CIERRA #siim1001;

     |SELECT #2#siiw0108;
     #siiw0108#3 = #siim1001#1;
     |LEE 000#siiw0108.=;
     |SI FSalida ! 0;
         |DDEFECTO #siiw0108;
     |FINSI;
     |SELECT #1#siiw0108;

     #siiz0011#0 = #siiw0108#1;
     #siiz0011#1 = #siiw0108#2;
     #siiz0011#2 = #siiw0108#3;
|FPRC;
