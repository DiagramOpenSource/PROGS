|FICHEROS;
     siim0002;
     siim0003;
     siim0005;

     siiw0004,MANTE;
     siiw0012;
|FIN;

|VARIABLES;
     nOk                 = 0;
     nCobro              = 0;
     nLotA               = 0;
     nPas                = 0;
     nID                 = 0;

     aAlfa               = "";
     aFicTmp             = "";
     aID                 = "";
     aContenido          = "";

 {40}aPopup              = "";

     &eaVarDni           = "";
     &enCalcCif          = 0;
     &eaCodNif           = "";
     &eaNomNif           = "";
     &eaPaiNif           = "";
|FIN;

|PROCESO T12w4;   |TIPO 12;
     |SI FSalida = 7;  |ACABA;  |FINSI;

     |SI FSalida ! 1;
         |POSICIONATE #siiw0004#9;
         |ERROR;
         |ACABA;
     |FINSI;
|FPRC;

|PROCESO T0C16w4;  |TIPO 0;
     |C_M #siiw0004#16, 0, aAlfa;
     |SI aAlfa = "N"; |ACABA;  |FINSI;

     |SI #siiw0004#16 = "   ";
         #siiw0004#16 = "ES";
     |FINSI;

     |SI #siiw0004#16 = "ESP";
         #siiw0004#16 = "ES";
     |FINSI;

     |SI #siiw0004#14 ! "01";  |Y #siiw0004#14 ! "07";
         |SI #siiw0004#16 = "ES ";
             |MENAV "0000Pais incorrecto";
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;

     |SI #siiw0004#14 = "01";  |O #siiw0004#14 = "07";
         |SI #siiw0004#16 ! "ES ";
             |MENAV "0000Pais incorrecto";
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;

     |C_M #siiw0004#17, 1, "S";
     |SI #siiw0004#16 = "ES ";
         |C_M #siiw0004#17, 1, "N"; #siiw0004#17 = "";
     |FINSI;

     aAlfa = #siiw0004#17;  |QBF aAlfa;
     |SI #siiw0004#14 = "02";  |Y aAlfa = "";
         aAlfa = #siiw0004#16;  |QBF aAlfa;
         #siiw0004#17 = aAlfa + #siiw0004#7;
     |FINSI;

     |PINTA #siiw0004#17;
|FPRC;

|PROCESO T0C15w4;  |TIPO 0;
     |C_M #siiw0004#15, 0, aAlfa;
     |SI aAlfa = "N";  |ACABA;  |FINSI;

     nOk = 0;

     |SI #siiw0004#15 = "F1";  nOk = 1;  |FINSI;
     |SI #siiw0004#15 = "F2";  nOk = 1;  |FINSI;
     |SI #siiw0004#15 = "F3";  nOk = 1;  |FINSI;
     |SI #siiw0004#15 = "F4";  nOk = 1;  |FINSI;
     |SI #siiw0004#15 = "R1";  nOk = 1;  |FINSI;
     |SI #siiw0004#15 = "R2";  nOk = 1;  |FINSI;
     |SI #siiw0004#15 = "R3";  nOk = 1;  |FINSI;
     |SI #siiw0004#15 = "R4";  nOk = 1;  |FINSI;
     |SI #siiw0004#15 = "R5";  nOk = 1;  |FINSI;

     |SI nOk = 0;
         |MENAV "0000Contenido incorrecto. Pulse F11 para ver tabla de contenido";
         |ERROR;
         |ACABA;
     |FINSI;

     |C_M #siiw0004#18, 1, "S";
     |SI #siiw0004#15 ! "F4";
         |C_M #siiw0004#18, 1, "N";
     |FINSI;

     |PINTA #siiw0004#18;
|FPRC;

|PROCESO T66C15w4;  |TIPO 66;
     aAlfa = "|C015WID";
     aID   = #siiw0004#aAlfa#;
     nID   = aID;

     aPopup1  = nID;  || Si fuera -1 en la posicion
     aPopup2  = 9;
     aPopup3  = "F1. Factura completa o simplificada cualificada del art.7.2 y 7.3";
     aPopup4  = "F2. Factura simplificada o sin identificacion del destinatario art.6.1.d";
     aPopup5  = "F3. Factura emitida en sustitucion de facturas simplificadas";
     aPopup6  = "F4. Asiento resumen de facturas.";
     aPopup7  = "R1. Factura rectificativa. Error fundado y Articulo 80 uno y dos LIVA.";
     aPopup8  = "R2. Factura rectificativa. Articulo 80 tres LIVA - concurso acreedores";
     aPopup9  = "R3. Articulo 80 cuatro LIVA - deuda incobrable.";
     aPopup10 = "R4. Resto";
     aPopup11 = "R5. Rectificativa en facturas simplificadas.";

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
         IaPopup  = aPopup3  <-;
         IaPopup  = IaPopup + (FSalida - 1);

         aAlfa = aPopup % 102;  #siiw0004#15 = aAlfa;
     |FINSI;

     |PINTA #siiw0004#15;
|FPRC;

|PROCESO T0C14w4;   |TIPO 0;
     |C_M #siiw0004#14, 0, aAlfa;
     |SI aAlfa = "N";  |ACABA;  |FINSI;

     nOk = 0;

     |SI #siiw0004#14 = "01";  nOk = 1;  |FINSI;
     |SI #siiw0004#14 = "02";  nOk = 1;  |FINSI;
     |SI #siiw0004#14 = "03";  nOk = 1;  |FINSI;
     |SI #siiw0004#14 = "04";  nOk = 1;  |FINSI;
     |SI #siiw0004#14 = "05";  nOk = 1;  |FINSI;
     |SI #siiw0004#14 = "06";  nOk = 1;  |FINSI;
     |SI #siiw0004#14 = "07";  nOk = 1;  |FINSI;

     |SI nOk = 0;
         |MENAV "0000 Clave identificaci¢n pais de residencia incorrecto";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO T66C14w4;  |TIPO 66;
     aAlfa = "|C014WID";
     aID   = #siiw0004#aAlfa#;
     nID   = aID;

     aPopup1  = nID;
     aPopup2  = 7;
     aPopup3  = "01 NIF";
     aPopup4  = "02 NIF/IVA (NIF operador intracomunitario)";
     aPopup5  = "03 Pasaporte";
     aPopup6  = "04 Documento Oficial de identificacion expedido por el pais o territorio de residencia";
     aPopup7  = "05 Certificado de residencia fiscal";
     aPopup8  = "06 Otro documento probatorio";
     aPopup9  = "07 No censado";

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
         IaPopup  = aPopup3  <-;
         IaPopup  = IaPopup + (FSalida - 1);

         aAlfa = aPopup % 102;  #siiw0004#14 = aAlfa;
     |FINSI;

     |PINTA #siiw0004#14;
|FPRC;

|PROCESO T0C11w4;  |TIPO 0;
     |C_M #siiw0004#12, 1, "S";

     |SI #siiw0004#11 ! "1";
         |C_M #siiw0004#12, 1, "N";  #siiw0004#12 = "";
     |FINSI;

     |PINTA #siiw0004#12;
|FPRC;

|PROCESO T0C10w4;  |TIPO 0;
     nCobro = #siiw0004#10 + #siiw0004#13;
     |SI nCobro > #siiw0004#6;
         |MENAV "0000El importe cobrado supera al de la factura";
     |FINSI;
|FPRC;

|PROCESO T0C7w4;  |TIPO 0;
     eaVarDni  = #siiw0004#Campo#;
     enCalcCif = 0;
     |HAZ LetraNif;
     eaVarDni = (eaVarDni + "             ") % 112;
     |SI #siiw0004#Campo# ! eaVarDni;
         |CONFI "0000SEl NIF puede ser Erroneo. Desea Calcularlo.";
         |SI FSalida = 0;
             #siiw0004#Campo# = eaVarDni;  |PINTA #siiw0004#Campo#;
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;

     aContenido = Contenido;
     |SI #siiw0004#7 ! aContenido;
         eaCodNif = #siiw0004#7;
         |HAZ LeeNif;

         #siiw0004#8  = eaNomNif;
         #siiw0004#16 = eaPaiNif;
     |FINSI;

     aAlfa = #siiw0004#7 % 101;
     |SI aAlfa = "X";  |O aAlfa = "Y";
         #siiw0004#14 = "01";
     |FINSI;

     |PINTA #siiw0004#14;
     |PINTA #siiw0004#8;
     |PINTA #siiw0004#16;
|FPRC;

|PROCESO siim0003;
     |SI #siim0003#1 > #siim0002#1;
         nOk = 0;
         |ACABA;
     |FINSI;

     |SI #siim0003#103 = "P";  nOk = 0;  |ACABA;  |FINSI;
     |SI #siim0003#103 = "I";  nOk = 0;  |ACABA;  |FINSI;
     |SI #siim0003#111 = "S";  nOk = 0;  |ACABA;  |FINSI;

     |SI #siim0003#7 ! "07";  |Y #siim0003#8 ! "07"; |Y #siim0003#9 ! "07";
         nOk = 0;  |ACABA;
     |FINSI;

     nOk = 1;

     #siiw0004#6  = #siim0003#59;
     #siiw0004#7  = #siim0003#15;
     #siiw0004#8  = #siim0003#16;
     #siiw0004#14 = #siim0003#5;
     #siiw0004#15 = #siim0003#6;
     #siiw0004#16 = #siim0003#17;
     #siiw0004#17 = #siim0003#18;
     #siiw0004#18 = #siim0003#20;
     #siiw0004#19 = #siim0003#21;
|FPRC;

|DEFBUCLE siim0003;
     #siim0003#2;
     ;
     #siiw0004#0, #siiw0004#5,          0,      0;
     #siiw0004#0, #siiw0004#5, 9999999999, 999999;
     ;
     NULL, siim0003;
|FIN;

|PROCESO siim0005;
     |SI #siim0005#1 = #siiw0004#1;  |Y #siim0005#2 = #siiw0004#2;
         |ACABA;
     |FINSI;

     |SI nPas = 0;
         #siiw0004#6  = #siim0005#6;
         #siiw0004#7  = #siim0005#7;
         #siiw0004#8  = #siim0005#8;
         #siiw0004#14 = #siim0005#14;
         #siiw0004#15 = #siim0005#15;
         #siiw0004#16 = #siim0005#16;
         #siiw0004#17 = #siim0005#17;
         #siiw0004#18 = #siim0005#18;
         #siiw0004#19 = #siim0005#19;

         nOk = 1;
         |ACABA;
     |FINSI;

     #siiw0004#11 = #siim0005#11;
     #siiw0004#12 = #siim0005#12;
     #siiw0004#13 = #siiw0004#13 + #siim0005#10;
|FPRC;

|DEFBUCLE siim0005;
     #siim0005#2;
     ;
     #siiw0004#0, #siiw0004#5,           0,      0;
     #siiw0004#0, #siiw0004#5, #siiw0004#1, 999999;
     ;
     NULL, siim0005;
|FIN;

|PROCESO T0C5w4;  |TIPO 0;
     |C_M #siiw0004#5, 0, aAlfa;
     |SI aAlfa = "N"; |ACABA;  |FINSI;

     aAlfa = #siiw0004#5;  |QBF aAlfa;
     |SI aAlfa = "";
         |MENAV "0000Introduzca el identificador de la factura";
         |ERROR;
         |ACABA;
     |FINSI;

     |C_M #siiw0004#19, 1, "N";
     |C_M #siiw0004#14, 1, "N";
     |C_M #siiw0004#15, 1, "N";
     |C_M #siiw0004#16, 1, "N";
     |C_M #siiw0004#7,  1, "N";
     |C_M #siiw0004#8,  1, "N";
     |C_M #siiw0004#6,  1, "N";

     nOk = -1;
     |BUCLE siim0003;

     |SI nOk = 0;
         aAlfa = "0000Esta factura est  de baja, su tipo de operaci¢n no es 07 o no ha sido enviada.";
         |MENAV aAlfa;
         |ERROR;
         |ACABA;
     |FINSI;

     |SI nOk = -1;
         |SALVA_DATOS #siim0005;
         nPas = 0;  |BUCLE siim0005;
         |REPON_DATOS #siim0005;

         |SELECT #1#siim0005;
         |LEE 000#siim0005.=;

         |SI nOk = -1;
             aAlfa = "0000NEsta factura no ha sido introducida en facturas expedidas, ";
             aAlfa = aAlfa + "quiere enviar un cobro de la factura indicada.";
             |CONFI aAlfa;
             |SI FSalida ! 0;
                 |ERROR;
                 |ACABA;
             |FINSI;

             #siiw0004#14 = "";
             #siiw0004#15 = "";
             #siiw0004#16 = "";
             #siiw0004#17 = "";
             #siiw0004#18 = "";
             #siiw0004#6  = 0;
             #siiw0004#13 = 0;
             #siiw0004#10 = 0;
             #siiw0004#12 = "";

             |C_M #siiw0004#19, 1, "S";
             |C_M #siiw0004#14, 1, "S";
             |C_M #siiw0004#15, 1, "S";
             |C_M #siiw0004#16, 1, "S";
             |C_M #siiw0004#7,  1, "S";
             |C_M #siiw0004#8,  1, "S";
             |C_M #siiw0004#6,  1, "S";
         |FINSI;
     |FINSI;

     #siiw0004#13 = 0;
     |SALVA_DATOS #siim0005;
     nPas = 1;  |BUCLE siim0005;
     |REPON_DATOS #siim0005;

     |SELECT #1#siim0005;
     |LEE 000#siim0005.=;

     |PINTA #siiw0004#6;
     |PINTA #siiw0004#7;
     |PINTA #siiw0004#8;
     |PINTA #siiw0004#11;
     |PINTA #siiw0004#12;
     |PINTA #siiw0004#13;
     |PINTA #siiw0004#14;
     |PINTA #siiw0004#15;
     |PINTA #siiw0004#16;
     |PINTA #siiw0004#17;
     |PINTA #siiw0004#18;
     |PINTA #siiw0004#19;
|FPRC;

|PROCESO siim0003Busca;
     |SI #siim0003#1 > #siim0002#1;
         |ACABA;
     |FINSI;

     |SI #siim0003#103 = "P";  |ACABA;  |FINSI;
     |SI #siim0003#103 = "I";  |ACABA;  |FINSI;

     |SI #siim0003#7 ! "07";  |Y #siim0003#8 ! "07"; |Y #siim0003#9 ! "07";
         |ACABA;
     |FINSI;

     |SI #siim0003#111 = "S";  |O #siim0003#1 = #siim0002#1;
         #siiw0012#0 = #siim0003#19;
         |LEE 000#siiw0012.=;
         |SI FSalida = 0;
             |BORRA 020#siiw0012.a;
             |LIBERA #siiw0012;
         |FINSI;

         |ACABA;
     |FINSI;

     #siiw0012#0 = #siim0003#19;
     |LEE 000#siiw0012.=;
     |SI FSalida ! 0;
         |DDEFECTO #siiw0012;
         #siiw0012#0 = #siim0003#19;
         |GRABA 020#siiw0012.b;
     |FINSI;

     #siiw0012#1  = #siim0003#1;
     #siiw0012#2  = #siim0003#2;
     #siiw0012#3  = #siim0003#3;
     #siiw0012#4  = #siim0003#4;
     #siiw0012#5  = #siim0003#5;
     #siiw0012#6  = #siim0003#6;
     #siiw0012#7  = #siim0003#7;
     #siiw0012#8  = #siim0003#8;
     #siiw0012#9  = #siim0003#9;
     #siiw0012#10 = #siim0003#13;
     #siiw0012#11 = #siim0003#14;
     #siiw0012#12 = #siim0003#15;
     #siiw0012#13 = #siim0003#16;
     #siiw0012#14 = #siim0003#17;
     #siiw0012#15 = #siim0003#18;
     #siiw0012#16 = #siim0003#21;
     #siiw0012#17 = #siim0003#22;
     #siiw0012#18 = #siim0003#59;

     |GRABA 020#siiw0012.a;
     |LIBERA #siiw0012;
|FPRC;

|DEFBUCLE siim0003Busca;
     #siim0003#1;
     ;
     #siim0002#0,     0,      0;
     #siim0002#0, nLotA, 999999;
     ;
     NULL, siim0003Busca;
|FIN;

|PROCESO T66C5w4;  |TIPO 66;
     nLotA = #siim0002#1 - 1;

     aFicTmp = "siiw0012" + Usuario;
     |NOME_DAT #siiw0012#aFicTmp#;
     |ABRE #siiw0012;  |CIERRA #siiw0012;  |DELINDEX #siiw0012;

     |ABRE #siiw0012;
     |BUCLE siim0003Busca;

     |LEE 000#siiw0012.p;
     |CONSULTA_CLAVE #1#siiw0012;
     |SI FSalida = 0;
         #siiw0004#5 = #siiw0012#0;

         |PINTA #siiw0004#5;
     |FINSI;

     |CIERRA #siiw0012;
     |DELINDEX #siiw0012;
|FPRC;
