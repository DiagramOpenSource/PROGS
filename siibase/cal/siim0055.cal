|FICHEROS;
     siim0001;
     siim0055;
|FIN;

|VARIABLES;
     n0Vent              = 0;
     n1Vent              = 0;
     nGrid               = 0;
     nBtnBor             = 0;
     nRango              = 0;
     nEjer               = 0;
     nAnyo               = 0;
     nNumEje             = 0;
     nID                 = 0;
     nPor                = 0;

     aAlfa               = "";
     aEsc1               = "";
     aEsc2               = "";
     aRetu               = "";
     aTecla              = "";

     fFecha              = @;

 {50}aPopup              = "";

 {-1}aVent               = "";
     aVent1              = 0;
     aVent2              = 0;
     aVent3              = 0;
     aVent4              = 0;
     aVent5              = "";
|FIN;

|PROCESO Baja55;
     #siim0055#0 = nEjer;
     #siim0055#1 = "";
|FPRC;

|PROCESO Alta55;
     #siim0055#0 = nEjer;
     #siim0055#1 = "z" * 12;
|FPRC;

|PROCESO PidePorce;
     |LEE 101#siim0055.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |VENTANA 0501, 0723, -1, 16, "Porcentaje prorrata a aplicar";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     nPor  = #siim0055#3;
     E_inf = "0";
     E_sup = "100.00";
     nPor  = 0610 ? 1;

     #siim0055#3 = nPor;
     |GRABA 020#siim0055.a;
     |LIBERA #siim0055;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;

     |REFRESCACONTROL nGrid;
     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO Evnt;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#siim0055.=;

         |ESTADO_CONTROL nBtnBor, "ENABLE";
     |FINSI;

     |SI FSalida = 4;
         |HAZ PidePorce;
     |FINSI;
|FPRC;

|PROCESO Baja1;
     #siim0001#4 = "S";
     #siim0001#0 = 1;
|FPRC;

|PROCESO Alta1;
     #siim0001#4 = "S";
     #siim0001#0 = 99999;
|FPRC;

|PROCESO Alta;
     |CONSULTA_F_CLAVE #3#siim0001, Baja1, Alta1;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     #siim0055#0 = nEjer;
     #siim0055#1 = #siim0001#2;
     |LEE 000#siim0055.=;
     |SI FSalida = 0;
         |MENAV "0000La empresa ya est  introducida.";
         |ACABA;
     |FINSI;

     |DDEFECTO #siim0055;
     #siim0055#0 = nEjer;
     #siim0055#1 = #siim0001#2;
     #siim0055#2 = #siim0001#1;
     #siim0055#3 = 100;
     |GRABA 020#siim0055.n;
     |LIBERA #siim0055;

     |REFRESCACONTROL nGrid;
     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO Baja;
     |CONFI "0000NSeguro que quiere dar de baja el registro";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |LEE 101#siim0055.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |BORRA 020#siim0055.a;
     |LIBERA #siim0055;

     |ESTADO_CONTROL nBtnBor, "DISABLE";

     |REFRESCACONTROL nGrid;
     |FOCOTECLADO nGrid;
|FPRC;

|PROGRAMA;
     |CLS;
     |CABEZA "Prorrata provisional";

     |VENTANA 0501, 3580, -1, 16, "Prorrata provisional";
     n0Vent = FSalida;

     |CONTROL_SIMPLE 0, "LABEL,{{16}}Pulse doble click para cambiar el porcentaje de prorrata.", 0502, 0590, NULL;
     |CONTROL_SIMPLE 0, "BOTON,{{193}}Alta", 3268, 3278, Alta;

     |CONTROL_SIMPLE 0, "BOTON,{{199}}Baja", 3256, 3266, Baja;
     nBtnBor = FSalida;

     |ESTADO_CONTROL nBtnBor, "DISABLE";

     nRango   = 2 + 4 + 8 + 16 + 32 + 128;
     |LINEAL_SIMPLE #1#siim0055, nRango, 0602, 3180, Baja55, Alta55, Evnt;
     nGrid = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::{{001}}Salir," + nGrid;
          |LEETECLA aTecla;
          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
          |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FINVENTANA n0Vent;
|FPRO;

|PROCESO T80;  |TIPO 80;
     fFecha = ~;
     aAlfa  = fFecha % -104;  nAnyo = aAlfa;

     nID   = 0;

     aPopup1  = nID;  || Si fuera -1 en la posicion

     nAnyo   = nAnyo + 1;
     nNumEje = 0;

     IaPopup = aPopup2 <-;
     |PARA nEjer = 2017;  |SI nEjer [ nAnyo;  |HACIENDO nEjer = nEjer + 1;
           nNumEje = nNumEje + 1;
           IaPopup = IaPopup + 1;
           aPopup  = nEjer;
     |SIGUE;

     aPopup2  = nNumEje;

     nEjer    = nAnyo - 1;
     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
         IaPopup  = aPopup3  <-;
         IaPopup  = IaPopup + (FSalida - 1);

         nEjer = aPopup;
     |FINSI;

     |ABRET #siim0001;
     |ABRET #siim0055;
|FPRC;

|PROCESO T90;  |TIPO 90;
     |CIERRAT #siim0001;
     |CIERRAT #siim0055;
|FPRC;
