|FICHEROS;
     siim0001;
     siim0002;
     siim0005;

     siiw0004;
     siiw0007;
|FIN;

|VARIABLES;
     n0Vent              = 0;
     n1Vent              = 0;
     nGrid5              = -1;
     nRango              = 0;
     nPanta              = 0;
     nCampo              = 0;
     nTCampo             = 0;
     nIdSalir            = 0;
     nReg                = 0;
     nCmp1               = 0;
     nCmp2               = 0;

     aEsc1               = "";
     aEsc2               = "";
     aRetu               = "";
     aTecla              = "";
     aAlfa               = "";
     aQQ                 = "|NC";

     fFecha              = @;

     {2}nNivel           = 0;

     {-1}aVent           = "";
         aVent1          = 0;
         aVent2          = 0;
         aVent3          = 0;
         aVent4          = 0;
         aVent5          = "";

     &enEmp              = 0;
     &enEje              = 0;
     &enPer              = 0;
     &enLot              = 0;

     &enBtnAlt           = -1;
     &enBtnMod           = -1;
     &enBtnBaj           = -1;
     &enBtnEnv           = -1;

     &eaAzul             = "";
     &eaVerde            = "";
     &eaRojo             = "";
     &eaRosa             = "";
     &eaMarron           = "";
|FIN;

|PROCESO Baja5;
     #siim0005#0 = #siim0002#0;
     #siim0005#1 = #siim0002#1;
     #siim0005#2 = 1;
|FPRC;

|PROCESO Alta5;
     #siim0005#0 = #siim0002#0;
     #siim0005#1 = #siim0002#1;
     #siim0005#2 = 9999;
|FPRC;

|PROCESO PintaCampos;
     |PARA nCampo = 0;  |SI nCampo [ nTCampo;  |HACIENDO nCampo = nCampo + 1;
           |C_V #siiw0004#nCampo#, 0, aAlfa;
           |SI aAlfa = "S";
               |PINTA #siiw0004#nCampo#;
           |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Evnt5;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#siim0005.=;

         |ESTADO_CONTROL enBtnMod, "ENABLE";
         |ESTADO_CONTROL enBtnBaj, "ENABLE";
         |ESTADO_CONTROL enBtnEnv, "DISABLE";

         |SI #siim0005#23 = "C"; |O #siim0005#23 = "A";
             |SI enBtnMod ! -1;  |ESTADO_CONTROL enBtnMod, "DISABLE";  |FINSI;
             |SI enBtnBaj ! -1;  |ESTADO_CONTROL enBtnBaj, "DISABLE";  |FINSI;
         |FINSI;

         |SI #siim0005#23 ! "P";
             |ESTADO_CONTROL enBtnEnv, "ENABLE";
         |FINSI;

         |PARA nCampo = 0;  |SI nCampo [ nTCampo;  |HACIENDO nCampo = nCampo + 1;
               #siiw0004#nCampo# = #siim0005#nCampo#;
         |SIGUE;

         |HAZ PintaCampos;
     |FINSI;
|FPRC;

|PROCESO QuitaBotonSalir;
     |ESTADO_CONTROL enBtnAlt, "DISABLE";
     |ESTADO_CONTROL enBtnEnv, "DISABLE";

     |SI enBtnMod ! -1;  |ESTADO_CONTROL enBtnMod, "DISABLE";  |FINSI;
     |SI enBtnBaj ! -1;  |ESTADO_CONTROL enBtnBaj, "DISABLE";  |FINSI;

     |ESTADO_CONTROL nGrid5,   "DISABLE";

     nIdSalir = 0;
     InNivel   = nNivel1<-;
     nNivel1   = 0;
     |ESPECIFICA "IDBOTONTECLA", nNivel;

     nIdSalir = nNivel2;
     |ESTADO_CONTROL nIdSalir, "HIDE";
|FPRC;

|PROCESO PonBotonSalir;
     |ESTADO_CONTROL nGrid5,   "ENABLE";
     |ESTADO_CONTROL nIdSalir, "SHOW";
     |ESTADO_CONTROL enBtnAlt, "ENABLE";
     |ESTADO_CONTROL enBtnEnv, "DISABLE";

     |SI enBtnMod ! -1;  |ESTADO_CONTROL enBtnMod, "DISABLE";  |FINSI;
     |SI enBtnBaj ! -1;  |ESTADO_CONTROL enBtnBaj, "DISABLE";  |FINSI;
|FPRC;

|PROCESO EntraDatos;
     |DDEFECTO #siiw0004;

     |PARA nCampo = 0;  |SI nCampo [ nTCampo;  |HACIENDO nCampo = nCampo + 1;
           #siiw0004#nCampo# = #siim0005#nCampo#;
     |SIGUE;

     |HAZ PintaCampos;

     |PULSATECLA;
     |ENDATOS #1#siiw0004;
|FPRC;

|PROCESO BtnAlt;
     |HAZ QuitaBotonSalir;

     #siim0005#0  = #siim0002#0;
     #siim0005#1  = #siim0002#1;
     #siim0005#2  = 999999;
     |LEE 000#siim0005.];
     |SI FSalida = 0;
         |LEE 000#siim0005.a;
     |SINO;
         |LEE 000#siim0005.u;
     |FINSI;

     nReg = 1;
     |SI FSalida = 0;  |Y #siim0005#0  = #siim0002#0;
                       |Y #siim0005#1  = #siim0002#1;
         nReg = #siim0005#2 + 1;
     |FINSI;

     |DDEFECTO #siim0005;
     #siim0005#0  = #siim0002#0;
     #siim0005#1  = #siim0002#1;
     #siim0005#2  = nReg;
     |GRABA 020#siim0005.n;
     |LIBERA #siim0005;

     |C_M #siiw0004#5, 1, "S";

     |HAZ EntraDatos;
     |SI FSalida = 0;
         #siim0005#0  = #siim0002#0;
         #siim0005#1  = #siim0002#1;
         #siim0005#2  = nReg;
         |LEE 101#siim0005.=;

         |PARA nCampo = 3;  |SI nCampo [ nTCampo;  |HACIENDO nCampo = nCampo + 1;
               #siim0005#nCampo# = #siiw0004#nCampo#;
         |SIGUE;

         |GRABA 020#siim0005.a;
     |SINO;
         #siim0005#0  = #siim0002#0;
         #siim0005#1  = #siim0002#1;
         #siim0005#2  = nReg;
         |LEE 101#siim0005.=;

         |BORRA 020#siim0005.a;
     |FINSI;

     |LIBERA #siim0005;

     |HAZ PonBotonSalir;

     |REFRESCACONTROL nGrid5;
     |FOCOTECLADO nGrid5;
|FPRC;

|PROCESO BtnMod;
     |LEE 101#siim0005.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |HAZ QuitaBotonSalir;

     |C_M #siiw0004#5, 1, "N";

     |HAZ EntraDatos;
     |SI FSalida = 0;
         |PARA nCampo = 3;  |SI nCampo [ nTCampo;  |HACIENDO nCampo = nCampo + 1;
               #siim0005#nCampo# = #siiw0004#nCampo#;
         |SIGUE;

         |GRABA 020#siim0005.a;
     |FINSI;

     |LIBERA #siim0005;

     |HAZ PonBotonSalir;

     |REFRESCACONTROL nGrid5;
     |FOCOTECLADO nGrid5;
|FPRC;

|PROCESO BtnBaj;
     |LEE 101#siim0005.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |CONFI "0000 Confirma la baja del registro seleccionado";
     |SI FSalida ! 0;
         |FOCOTECLADO nGrid5;
         |ACABA;
     |FINSI;

     |BORRA 020#siim0005.a;
     |LIBERA #siim0005;

     |ESTADO_CONTROL enBtnAlt, "ENABLE";

     |SI enBtnMod ! -1;  |ESTADO_CONTROL enBtnMod, "DISABLE";  |FINSI;
     |SI enBtnBaj ! -1;  |ESTADO_CONTROL enBtnBaj, "DISABLE";  |FINSI;

     |REFRESCACONTROL nGrid5;
     |FOCOTECLADO nGrid5;
|FPRC;

|PROCESO BtnEnv;
     |VENTANA 0501, 1599, -1, 16, "Datos del env¡o";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     #siiw0007#0 = #siiw0004#20;
     #siiw0007#1 = #siiw0004#21;
     #siiw0007#2 = #siiw0004#22;

     |SI #siiw0004#23 = "C";  #siiw0007#3 = "Correcto";            |FINSI;
     |SI #siiw0004#23 = "A";  #siiw0007#3 = "AceptadaConErrores";  |FINSI;
     |SI #siiw0004#23 = "I";  #siiw0007#3 = "Incorrecto";          |FINSI;

     #siiw0007#4 = #siiw0004#26;
     #siiw0007#5 = #siiw0004#24;
     #siiw0007#6 = #siiw0004#25 % 180;
     #siiw0007#7 = #siiw0004#25 % 8180;
     #siiw0007#8 = #siiw0004#25 % 16180;

     |PINPA #0#siiw0007;
     |PINDA #0#siiw0007;
     |LEETECLA aAlfa;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;

     |FOCOTECLADO nGrid5;
|FPRC;

|PROCESO siim0005B;
     |BORRA 020#siim0005.a;
     |LIBERA #siim0005;
|FPRC;

|DEFBUCLE siim0005B;
     #siim0005#1;
     ;
     #siim0002#0, #siim0002#1, 000001;
     #siim0002#0, #siim0002#1, 999999;
     be;
     NULL, siim0005B;
|FIN;

|PROGRAMA;
     |SI PARAMETRO = "REFRESCA";
         |ACABA;
     |FINSI;

     |SI PARAMETRO = "BORRAR";
         |BUCLE siim0005B;

         |ACABA;
     |FINSI;

     aAlfa = "Bandeja cobros facturas expedidas ";
     aAlfa = aAlfa  + "(Empresa " +(("00000" + #siim0001#0) % -105);
     aAlfa = aAlfa  + " " + #siim0001#1;  |QBF aAlfa;
     aAlfa = aAlfa  + " Periodo " + #siim0002#4 + "/" + (("00" + #siim0002#5) % -102);
     aAlfa = aAlfa  + " Lote " + (("0000000000" + #siim0002#1) % -110) + ")";
     |VENTANA 0501, 3799, -1, 16, aAlfa;
     n0Vent = FSalida;

     |PINPA #0#siiw0004;  nPanta = FSalida;
     |DDEFECTO #siiw0004;
     |HAZ PintaCampos;

     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}Alta ", 1864, 1874, BtnAlt;
     enBtnAlt = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Modificar ", 1876, 1886, BtnMod;
     enBtnMod = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{199}}Borrar", 1888, 1898, BtnBaj;
     enBtnBaj = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Estado del env¡o", 1802, 1820, BtnEnv;
     enBtnEnv = FSalida;

     |ESTADO_CONTROL enBtnMod, "DISABLE";
     |ESTADO_CONTROL enBtnBaj, "DISABLE";

     nRango   = 2 + 4 + 8 + 16 + 32 + 128;
     |LINEAL_SIMPLE #1#siim0005, nRango, 0502, 1798, Baja5, Alta5, Evnt5;
     nGrid5 = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::{{001}}Salir," + nGrid5;
          |LEETECLA aTecla;
          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
          |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FINVENTANA n0Vent;

     |PULSATECLA;
|FPRO;

|PROCESO T80;  |TIPO 80;
     |HAZ CargaColores;

     |ABRET #siim0005;

     |ABRE #siim0001;
     |SELECT #1#siim0001;
     #siim0001#0 = enEmp;
     |LEE 000#siim0001.=;
     |CIERRA #siim0001;

     |ABRE #siim0002;
     #siim0002#0 = enEmp;
     #siim0002#1 = enLot;
     |LEE 000#siim0002.=;
     |CIERRA #siim0002;

     aQQ     = "|NC";
     aAlfa   = #siiw0004#aQQ#;
     nTCampo = aAlfa;
     nTCampo = nTCampo - 1;
|FPRC;

|PROCESO siim0005C;
                              #siim0002#6  = #siim0002#6  + 1;
     |SI #siim0005#23 = "P";  #siim0002#7  = #siim0002#7  + 1;  |FINSI;
     |SI #siim0005#23 = "I";  #siim0002#7  = #siim0002#7  + 1;  |FINSI;
     |SI #siim0005#23 = "I";  #siim0002#10 = #siim0002#10 + 1;  |FINSI;
     |SI #siim0005#23 = "C";  #siim0002#8  = #siim0002#8  + 1;  |FINSI;
     |SI #siim0005#23 = "A";  #siim0002#9  = #siim0002#9  + 1;  |FINSI;

     |SI #siim0002#15 < #siim0005#21;
         #siim0002#15 = #siim0005#21;
     |FINSI;

     |SI #siim0002#15 = #siim0005#21;
         |SI #siim0002#16 < #siim0005#22;
             #siim0002#16 = #siim0005#22;
         |FINSI;
     |FINSI;

     #siim0005#27 = "";
     |SI #siim0005#23 = "C";
         #siim0005#27 = eaVerde;
     |FINSI;

     |SI #siim0005#23 = "A";
         #siim0005#27 = eaAzul;
     |FINSI;

     |SI #siim0005#23 = "I";
         #siim0005#27 = eaRojo;
     |FINSI;

     |GRABA 020#siim0005.a;
     |LIBERA #siim0005;
|FPRC;

|DEFBUCLE siim0005C;
     #siim0005#1;
     ;
     #siim0002#0, #siim0002#1, 000001;
     #siim0002#0, #siim0002#1, 999999;
     ;
     NULL, siim0005C;
|FIN;

|PROCESO T90;  |TIPO 90;
     |ABRE #siim0002;
     #siim0002#0 = enEmp;
     #siim0002#1 = enLot;
     |LEE 101#siim0002.=;
     |SI FSalida = 0;
         #siim0002#6  = 0;
         #siim0002#7  = 0;
         #siim0002#8  = 0;
         #siim0002#9  = 0;
         #siim0002#10 = 0;
         #siim0002#15 = "01.01.0000";
         #siim0002#16 = "";
         #siim0002#17 = 0;
         #siim0002#18 = 0;
         #siim0002#19 = 0;
         #siim0002#20 = 0;

         |BUCLE siim0005C;

         #siim0002#11 = "";

         nReg = #siim0002#8 + #siim0002#9;
         |SI nReg = #siim0002#6;  |Y nReg ! 0;
             #siim0002#11 = eaVerde;
         |FINSI;

         |SI #siim0002#10 ! 0;
             #siim0002#11 = eaRojo;
         |FINSI;

         #siim0002#13 = #siim0001#1;
         #siim0002#14 = #siim0001#2;

         |GRABA 020#siim0002.a;
         |LIBERA #siim0002;
     |FINSI;
     |CIERRA #siim0002;

     |CIERRAT #siim0005;
|FPRC;
