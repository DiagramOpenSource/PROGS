|FICHEROS;
     siim0000;
     siim0001;
     siim0002;
     siim0005;
     siim0054;
     siim0500;
     siim0501;

     siiw0203;
     siiw0207;
|FIN;

|VARIABLES;
     n1Vent              = 0;
     n2Vent              = 0;
     nPausa              = 0;
     nCon                = 0;
     nHandle             = 0;
     nLin                = 0;
     nLin1               = 0;
     nLin2               = 0;
     nLin3               = 0;
     nLin4               = 0;
     nLin5               = 0;
     nLin6               = 0;
     nCmp                = 0;
     nCmp1               = 0;
     nCmp2               = 0;
     nCmp3               = 0;
     nCmp4               = 0;
     nCmp5               = 0;
     nOk                 = 0;
     nBases              = 0;
     nLong               = 0;
     nCarac              = 0;
     nPos                = 0;
     nActiv              = 0;
     nReg                = 0;
     nRegs               = 0;
     nTDesFac            = 0;
     nTDesFacSuj         = 0;
     nTDesFacSujEx       = 0;
     nTDesFacSujNEx      = 0;
     nTDesFacNSuj        = 0;
     nTDesTOpPrs         = 0;
     nTDesTOpPrsSuj      = 0;
     nTDesTOpPrsSujEx    = 0;
     nTDesTOpPrsSujNEx   = 0;
     nTDesTOpPrsNSuj     = 0;
     nTDesTOpEnt         = 0;
     nTDesTOpEntSuj      = 0;
     nTDesTOpEntSujEx    = 0;
     nTDesTOpEntSujNEx   = 0;
     nTDesTOpEntNSuj     = 0;
     nEsOtro             = 0;
     nUno                = 0;
     nPasada             = 0;

     aNIF                = "";
     aIdRes              = "";
     aIdent              = "";
     aFecEm              = "";
     aEstad              = "";
     aCodEr              = "";
     aDesEr              = "";
     aIdCSV              = "";
     aFEnvi              = "";
     aHEnvi              = "";
     aExt                = "";
     aLinea              = "";
     aModoImp            = "";
     aTDes               = "";
     aNDes               = "";
     aTDes1              = "";
     aTDes2              = "";
     aTDes3              = "";
     aTDes4              = "";
     aTDes5              = "";
     aTDes6              = "";
     aNDes1              = "";
     aNDes2              = "";
     aNDes3              = "";
     aNDes4              = "";
     aNDes5              = "";
     aNDes6              = "";
     aTip1               = "";
     aTip2               = "";
     aTipNiv             = "";
     aDesgl              = "";
     aCar                = "";

     aAlfa               = "";
     aAlfa1              = "";
     aAlfa2              = "";
     aLee                = "";
     aBaseSII            = "";
     aFicXml             = "";
     aFicRes             = "";
     aPathTmp            = "";
     aPathSII            = "";
     aPathPlu            = "";
     aAbreXml            = "";
     aHora               = "";
     aContenido          = "";
     aApli               = "";
     aH60                = "";
     aFec                = "";
     aClv                = "";
     aClvPpl             = "";
     aClvN1              = "";
     aClvN2              = "";
     aClvN3              = "";
     aClvN4              = "";
     aClvN5              = "";
     aTipoCmp            = "";
     aQQ                 = "";
     aDEtiq              = "";
     aHEtiq              = "";
     aDEti               = "";
     aHEti               = "";
     aAgru               = "";
     aAgruClv            = "";
     aIP                 = "";
     aOri                = "";
     aDes                = "";
     aVer1               = "";
     aVer2               = "";
     aEnlace             = "";
     aAbre               = "";
     aCaracter           = "";
     aError              = "";
     aLong               = "";
     aCadena             = "";
     aFicTmp             = "";
     aEsq                = "";
     aEjec               = "";

     fFecha              = @;
     fFechaSys           = @;

     {1}aConte           = "";

     {-1}aVent           = "";
         aVent1          = 0;
         aVent2          = 0;
         aVent3          = 0;
         aVent4          = 0;
         aVent5          = "";

     &enEmp              = 0;
     &enLot              = 0;
     &eaAlfa             = "";
     &eaCodNif           = "";
     &eaNomNif           = "";
     &eaUnidadBasico     = "";
     &eaVerde            = "";
     &eaRojo             = "";
     &eaAzul             = "";
     &eaPython           = "";
     &enOk               = 0;
     &eaCert             = "";
|FIN;

|PROCESO QuitaCeros;
     |PARA;  |SI;  |HACIENDO;
          aAlfa = aClvPpl % -102;
          |SI aAlfa = "00";
              aClvPpl = aClvPpl % -320;
          |SINO;
              |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO QuitaPrimero;
     aCadena = "";
     aLong   = aAlfa % 0;
     nLong   = aLong;
     |PARA nCarac = 1;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos       = (nCarac * 100) + 1;
           aCaracter  = aAlfa % nPos;
           aCar       = &194;
           |SI aCaracter = aCar;
               aCaracter = "";
           |FINSI;

           aCar       = &186;
           |SI aCaracter = aCar;
               aCaracter = "§";
           |FINSI;

           aCadena = aCadena + aCaracter;
     |SIGUE;

     aAlfa = aCadena;
|FPRC;

|PROCESO QuitaEne;
     aCadena = "";
     aLong   = aAlfa % 0;
     nLong   = aLong;
     |PARA nCarac = 1;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos       = (nCarac * 100) + 1;
           aCaracter  = aAlfa % nPos;
           aCar       = &195;
           |SI aCaracter = aCar;
               aCaracter = "";
           |FINSI;

           aCar       = &145;
           |SI aCaracter = aCar;
               aCaracter = "¥";
           |FINSI;

           aCadena = aCadena + aCaracter;
     |SIGUE;

     aAlfa = aCadena;
|FPRC;

|PROCESO SacaCadena;
     aAlfa   = aLee;  |QBF aAlfa;
     aCadena = "";
     aLong   = aAlfa % 0;
     nLong   = aLong;
     |PARA nCarac = 1;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos       = (nCarac * 100) + 1;
           aCaracter  = aAlfa % nPos;
           |SI aCaracter = "<";  |O aCaracter = ">";
               aCadena = "";
           |SINO;
               aCadena = aCadena + aCaracter;
           |FINSI;
     |SIGUE;
|FPRC;

|PROCESO GrabaEstado;
     nReg = nReg + 1;

     eaAlfa = aDesEr;  |HAZ QuitaRarosXml; aDesEr = eaAlfa;

     #siiw0203#0 = nReg;
     #siiw0203#1 = aIdent;
     #siiw0203#2 = aFecEm;
     #siiw0203#3 = aNIF;
     #siiw0203#4 = aIdRes;
     #siiw0203#5 = aEstad;
     #siiw0203#6 = aCodEr;
     #siiw0203#7 = aDesEr;
     #siiw0203#8 = eaVerde;

     |SI #siiw0203#5 = "Incorrecto          ";
         #siiw0203#8 = eaRojo;
     |FINSI;

     |SI #siiw0203#5 = "AceptadoConErrores  ";
         #siiw0203#8 = eaAzul;
     |FINSI;

     |GRABA 020#siiw0203.n;
     |LIBERA #siiw0203;
|FPRC;

|PROCESO LeeRespuesta;
     aOri  = aPathSII + aFicRes;
     aDes  = aPathTmp + aFicRes;
     |SI aIP = "";
         |COPIA_FICHERO aOri, aDes;
     |SINO;
         |RECIBE_FICHERO aOri, aDes;
     |FINSI;

     nOk     = 1;
     nActiv  = 0;
     aIdCSV  = "";
     aNIF    = "";
     aIdRes  = "";
     aIdent  = "";
     aFecEm  = "";
     aEstad  = "";
     aCodEr  = "";
     aDesEr  = "";
     aFEnvi  = "";
     aHEnvi  = "";

     aAbre  = aPathTmp + aFicRes;
     |XABRE "", aAbre, nHandle;

     |PARA; |SI;  |HACIENDO;
          |XLEE nHandle, 250, aLee;
          |SI FSalida < 0; |SAL;  |FINSI;

          aAlfa = aLee - "<siiR:CSV>";
          |SI FSalida ! 0;
              |SI aIdCSV = "";
                  |HAZ SacaCadena;
                  aIdCSV = aCadena;
              |FINSI;
          |FINSI;

          aAlfa = aLee - "<sii:TimestampPresentacion>";
          |SI FSalida ! 0;
              |HAZ SacaCadena;
              aFEnvi  = (aCadena % 102) + "." + (aCadena % 402) + "." + (aCadena % 704);
              aHEnvi  = aCadena % 1208;
          |FINSI;

          aAlfa = aLee - "<faultstring>";
          |SI FSalida ! 0;
              |HAZ SacaCadena;
              aError = aCadena;
              nOk    = 0;
              |SAL;
          |FINSI;

          aAlfa = aLee - "</siiR:RespuestaLinea>";
          |SI FSalida ! 0;
              nActiv = 0;
              |HAZ GrabaEstado;
          |FINSI;

          aAlfa = aLee - "<siiR:RespuestaLinea";
          |SI FSalida ! 0;
              aNIF   = "";
              aIdRes = "";
              aIdent = "";
              aFecEm = "";
              aEstad = "";
              aCodEr = "";
              aDesEr = "";

              nActiv = 1;
          |FINSI;

          |SI nActiv = 1;
              aAlfa = aLee - "<sii:NIF>";
              |SI FSalida ! 0;
                  |HAZ SacaCadena;
                  aNIF = aCadena;
              |FINSI;

              aAlfa = aLee - "<sii:IDOtro>";
              |SI FSalida ! 0;
                  nEsOtro = 1;
              |FINSI;

              aAlfa = aLee - "<sii:ID>";
              |SI FSalida ! 0;
                  |SI nEsOtro = 1;
                      |HAZ SacaCadena;
                      aIdRes = aCadena;
                  |FINSI;
              |FINSI;

              aAlfa = aLee - "<sii:NumSerieFacturaEmisor>";
              |SI FSalida ! 0;
                  |HAZ SacaCadena;
                  aIdent = aCadena;

                  aCar  = &194 + &186;    || §
                  aAlfa = aAlfa - aCar;
                  |SI FSalida ! 0;
                      aAlfa = aIdent;
                      |HAZ QuitaPrimero;
                      aIdent = aAlfa;
                  |FINSI;

                  aCar  = &195 + &145;    || ¥
                  aAlfa = aAlfa - aCar;
                  |SI FSalida ! 0;
                      aAlfa = aIdent;
                      |HAZ QuitaEne;
                      aIdent = aAlfa;
                  |FINSI;
              |FINSI;

              aAlfa = aLee - "<sii:FechaExpedicionFacturaEmisor>";
              |SI FSalida ! 0;
                  |HAZ SacaCadena;
                  aFecEm = aCadena;
              |FINSI;

              aAlfa = aLee - "<siiR:EstadoRegistro>";
              |SI FSalida ! 0;
                  |HAZ SacaCadena;
                  aEstad = aCadena;
              |FINSI;

              aAlfa = aLee - "<siiR:EstadoRegistro>";
              |SI FSalida ! 0;
                  |HAZ SacaCadena;
                  aEstad = aCadena;
              |FINSI;

              aAlfa = aLee - "<siiR:CodigoErrorRegistro>";
              |SI FSalida ! 0;
                  |HAZ SacaCadena;
                  aCodEr = aCadena;
              |FINSI;

              aAlfa = aLee - "<siiR:DescripcionErrorRegistro>";
              |SI FSalida ! 0;
                  |HAZ SacaCadena;
                  aDesEr = aCadena;
              |FINSI;
          |FINSI;
     |SIGUE;
     |XCIERRA nHandle;

     |SI nOk = 0;
         |SI aModoImp = "SIIBASE";
             aAlfa = "0000" + aError;
             |MENAV aAlfa;

             |MENAV "0000Proceso cancelado";
         |FINSI;

         |ACABA;
     |FINSI;

     |FBORRA aAbre;
|FPRC;

|PROCESO GrabaEnvio;
     #siiw0207#0  = #siim0005#0;
     #siiw0207#1  = #siim0005#1;
     #siiw0207#2  = #siim0005#2;
     #siiw0207#3  = #siiw0203#1;
     #siiw0207#4  = #siiw0203#2;
     #siiw0207#5  = #siiw0203#3;
     #siiw0207#6  = #siiw0203#4;
     #siiw0207#7  = #siiw0203#5;
     #siiw0207#8  = #siiw0203#6;
     #siiw0207#9  = #siiw0203#7;
     #siiw0207#10 = #siiw0203#8;

     |GRABA 000#siiw0207.n;
     |LIBERA #siiw0207;
|FPRC;

|PROCESO GrabaNoti;
     |LEE 000#siim0054.u;
     |SI FSalida ! 0;  |DDEFECTO #siim0054;  |FINSI;

     |HORA aHora;

     nReg = #siim0054#0 + 1;

     |DDEFECTO #siim0054;
     #siim0054#0 = nReg;
     #siim0054#1 = fFechaSys;
     #siim0054#2 = aHora;
     #siim0054#4 = #siim0005#24 + " / " + #siim0005#25;

     |SI #siim0005#23 = "C";
         #siim0054#3 = "Correcto";
         #siim0054#4 = "Registro aceptado";
     |FINSI;

     |SI #siim0005#23 = "A";  #siim0054#3 = "AceptadaConErrores";  |FINSI;
     |SI #siim0005#23 = "I";  #siim0054#3 = "Incorrecto";          |FINSI;

     #siim0054#5  = "PRESENTACION";
     #siim0054#7  = #siim0005#0;
     #siim0054#8  = #siim0005#1;
     #siim0054#9  = #siim0005#2;
     #siim0054#11 = #siim0005#5;

     |GRABA 020#siim0054.n;
     |LIBERA #siim0054;
|FPRC;

|PROCESO GrabaResp;
     |SI aFEnvi = "";
         |HORA aHora;

         aFEnvi = fFechaSys;
         aHEnvi = aHora;
     |FINSI;

     #siim0005#20 = Usuario % 810;
     #siim0005#21 = aFEnvi;
     #siim0005#22 = aHEnvi;
     #siim0005#24 = #siiw0203#6;
     #siim0005#25 = #siiw0203#7;
     #siim0005#26 = aIdCSV;
     #siim0005#23 = "C";
     #siim0005#27 = eaVerde;

     |SI #siiw0203#5 = "Incorrecto          ";
         #siim0005#21 = "01.01.0000";
         #siim0005#22 = "";
         #siim0005#23 = "I";
         #siim0005#26 = "";
         #siim0005#27 = eaRojo;
     |FINSI;

     |SI #siiw0203#5 = "AceptadoConErrores  ";
         #siim0005#23 = "A";
         #siim0005#27 = eaAzul;
     |FINSI;

     |GRABA 020#siim0005.a;
     |LIBERA #siim0005;

     |SI aModoImp = "SIIBOX";
         |HAZ GrabaNoti;
     |FINSI;

     |SI aModoImp = "SIIGEN";
         |HAZ GrabaEnvio;
     |FINSI;
|FPRC;

|PROCESO siiw0203;
     #siim0005#0  = #siim0002#0;
     #siim0005#5  = #siiw0203#1;
     #siim0005#1  = #siim0002#1;
     #siim0005#2  = 1;
     |LEE 101#siim0005.];
     |PARA;  |SI FSalida = 0;  |HACIENDO;
          |SI #siim0005#0 ! #siim0002#0;
              |SAL;
          |FINSI;

          |SI #siim0005#5 ! #siiw0203#1;
              |SAL;
          |FINSI;

          |SI #siim0005#1 ! #siim0002#1;
              |SAL;
          |FINSI;

          |SI #siim0005#23 ! "C";  |Y #siim0005#23 ! "A";
              |HAZ GrabaResp;
              |SAL;
          |FINSI;

          |LEE 000#siim0005.s;
     |SIGUE;
|FPRC;

|DEFBUCLE siiw0203;
     #siiw0203#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, siiw0203;
|FIN;

|PROCESO EnviaLote;
     |HAZ Ds_SII;
     |SI nOk = 0;  |ACABA;  |FINSI;

     aOri = aPathTmp + aFicXml;
     aDes = aPathSII + aFicXml;
     |SI aIP = "";
         |COPIA_FICHERO aOri, aDes;
     |SINO;
         |ENVIA_FICHERO aOri, aDes;
     |FINSI;

     aLinea  = "3"; || Linea 4 del fichero sii_urls.txt

     aEnlace = aPathSII + "ejecuta.bat";
     |XABRE "WBC", aEnlace, nHandle;

     aAlfa = aPathSII + "ds_sii.exe ";       |XGRABA nHandle, aAlfa;
     aAlfa = "ds123456 " + aLinea + " ";     |XGRABA nHandle, aAlfa;
     aAlfa = aPathSII + aFicXml + " ";       |XGRABA nHandle, aAlfa;
     aAlfa = aPathSII + aFicRes + " ";       |XGRABA nHandle, aAlfa;

     |SI aModoImp = "SIIBOX";
         aAlfa = &34 + #siim0000#1;   |QBF aAlfa;
         aAlfa = aAlfa + &34;   |XGRABA nHandle, aAlfa;
     |SINO;
         aAlfa = "NO ";                      |XGRABA nHandle, aAlfa;
         aAlfa =  eaCert;                    |XGRABA nHandle, aAlfa;
     |FINSI;

     |XCIERRA nHandle;

     aEjec   = aPathSII + "dsmini ";
     aEjec   = aEjec + aPathSII + "ejecuta.bat";
     |RSYSTEM aEjec;

     nCon  = 0;
     aAbre = aPathSII + aFicRes;
     |PARA;  |SI;  |HACIENDO;
             |PARA nPausa = 1; |SI nPausa [ 10000; |HACIENDO nPausa = nPausa + 1;
             |SIGUE;

             |XABRE "EC", aAbre, 0;
             |SI FSalida ] 0;
                 |SAL;
             |FINSI;

             |SLEEP 1;
             |PULSATECLA;
     |SIGUE;

     aAbre = aPathSII + aFicRes;
     |XABRE "EC", aAbre, 0;
     |SI FSalida ] 0;
         |HAZ LeeRespuesta;
     |SINO;
         |SI aModoImp = "SIIBASE";
             |MENAV "0000Sin respuesta. Revise la instalaci¢n y validez del certificado digital";
         |FINSI;

         nOk = 0;
     |FINSI;

     |SI nOk  = 0;
         |ACABA;
     |FINSI;

     |SELECT #2#siim0005;
     |BUCLE siiw0203;

     |SI aModoImp = "SIIBASE";
         |CONSULTA_CLAVE #1#siiw0203;
     |FINSI;
|FPRC;

|PROCESO ConverFec;
     aFec = (aFec % 102) + "-" + (aFec % 402) + "-" + (aFec % 704);
|FPRC;

|PROCESO CargaContenido;
     aAlfa = #siim0501#15;  |QBF aAlfa;
     |SI aAlfa = "";
         eaAlfa = aContenido;
         |HAZ QuitaRarosXml;
         aContenido = eaAlfa;
         |QBF aContenido;
         |ACABA;
     |FINSI;

     aApli    = #siim0501#15 % 208;
     aAlfa    = #siim0501#15 % 1103;
     nCmp     = aAlfa;
     aTipoCmp = "";
     aQQ      = "|C" + (("000" + nCmp) % -103) + "TIPO";

     aContenido = "";

     |SI aApli = "siim0001";
         aContenido = #siim0001#nCmp#;
         aTipoCmp   = #siim0001#aQQ#;
     |FINSI;

     |SI aApli = "siim0002";
         aContenido = #siim0002#nCmp#;
         aTipoCmp   = #siim0002#aQQ#;
     |FINSI;

     |SI aApli = "siim0005";
         aContenido = #siim0005#nCmp#;
         aTipoCmp   = #siim0005#aQQ#;
     |FINSI;

     |SI aApli = "siim0500";
         aContenido = #siim0500#nCmp#;
         aTipoCmp   = #siim0500#aQQ#;
     |FINSI;

     |SI #siim0501#15 = "#siim0002#005# ";
         aContenido = ("00" + #siim0002#5) % -102;
     |FINSI;

     |SI #siim0501#15 = "#siim0002#002# ";
         |SI #siim0002#2 = "11";  aContenido = "A0";  |FINSI;
         |SI #siim0002#2 = "12";  aContenido = "A1";  |FINSI;
     |FINSI;

     |SI #siim0501#15 = "#siim0005#011# ";
         aContenido = "0" + #siim0005#11;
     |FINSI;

     |QBF aContenido;

     |SI aTipoCmp = "F";
         fFecha = aContenido;
         |SI fFecha < "01.01.2000";
             aContenido = "";
             |ACABA;
         |FINSI;

         aFec = aContenido;
         |HAZ ConverFec;
         aContenido = aFec;
     |FINSI;

     |SI aTipoCmp = "A";
         eaAlfa = aContenido;
         |HAZ QuitaRarosXml;
         aContenido = eaAlfa;
     |FINSI;
|FPRC;

|PROCESO GrabaFicEspa;
     |QBF aAlfa;
                            aAlfa1 = "      ";
     |SI #siim0501#2  ! 0;  aAlfa1 = aAlfa1 + "   ";  |FINSI;
     |SI #siim0501#3  ! 0;  aAlfa1 = aAlfa1 + "   ";  |FINSI;
     |SI #siim0501#4  ! 0;  aAlfa1 = aAlfa1 + "   ";  |FINSI;
     |SI #siim0501#5  ! 0;  aAlfa1 = aAlfa1 + "   ";  |FINSI;
     |SI #siim0501#6  ! 0;  aAlfa1 = aAlfa1 + "   ";  |FINSI;
     |SI #siim0501#7  ! 0;  aAlfa1 = aAlfa1 + "   ";  |FINSI;
     |SI #siim0501#8  ! 0;  aAlfa1 = aAlfa1 + "   ";  |FINSI;
     |SI #siim0501#9  ! 0;  aAlfa1 = aAlfa1 + "   ";  |FINSI;
     |SI #siim0501#10 ! 0;  aAlfa1 = aAlfa1 + "   ";  |FINSI;
     |SI #siim0501#11 ! 0;  aAlfa1 = aAlfa1 + "   ";  |FINSI;

     aAlfa2 = #siim0501#13 % 201;
     |SI aAlfa2 = "/";
         aAlfa1 = aAlfa1 - "   ";
     |FINSI;

     aAlfa = aAlfa1 + aAlfa + &13 + &10;
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO GrabaFic;
     aAlfa = aAlfa + &13 + &10;
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO siim0501;
     |SI #siim0501#12 = "1";
         aAlfa = #siim0501#13;  |HAZ GrabaFicEspa;
     |FINSI;

     |SI #siim0501#12 = "2";
         |HAZ CargaContenido;

         |SI #siim0501#17 = "2";
             |SI aTipoCmp = "N";
                 |SI aContenido = "0";
                     aContenido = "";
                 |FINSI;
             |FINSI;
         |FINSI;

         |SI aContenido = "";  |Y #siim0501#17 ! "1";
             |ACABA;
         |FINSI;

         aAlfa = #siim0501#13;          |QBF aAlfa;
         aAlfa = aAlfa + aContenido;    |QBF aAlfa;
         aAlfa = aAlfa + #siim0501#14;  |QBF aAlfa;

         |HAZ GrabaFicEspa;
     |FINSI;

     aAlfa = #siim0501#18;
|FPRC;

|DEFBUCLE siim0501;
     #siim0501#3;
     ;
     #siim0500#2, aEsq, aDEti;
     #siim0500#2, aEsq, aHEti;
     ;
     NULL, siim0501;
|FIN;

|PROCESO PorClave3;
     |HAZ QuitaCeros;

     eaAlfa = aContenido;
     |HAZ QuitaRarosXml;
     aContenido = eaAlfa;

     |SELECT #3#siim0501;

     aClv         = (aClvPpl + aAgruClv + ("00" * 20)) % 120;
     #siim0501#0  = #siim0500#2;
     #siim0501#1  = aEsq;
     #siim0501#18 = aClv;
     |LEE 000#siim0501.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aAlfa = #siim0501#13;          |QBF aAlfa;
     aAlfa = aAlfa + aContenido;    |QBF aAlfa;
     aAlfa = aAlfa + #siim0501#14;  |QBF aAlfa;
     |HAZ GrabaFicEspa;
|FPRC;

|PROCESO LeeEsquema;
     |SELECT #2#siim0501;

     #siim0501#0  = #siim0500#2;
     #siim0501#1  = aEsq;
     #siim0501#13 = aDEtiq;
     |LEE 000#siim0501.=;

     aDEti   = #siim0501#18;
     aClvPpl = #siim0501#18;  |QBF aClvPpl;

     #siim0501#0  = #siim0500#2;
     #siim0501#1  = aEsq;
     #siim0501#13 = aHEtiq;
     |LEE 000#siim0501.=;

     aHEti  = #siim0501#18;

     |BUCLE siim0501;
|FPRC;

|PROCESO InicioFactura;
     aDEtiq = "<siiLR:RegistroLRCobros>";
     aHEtiq = "<siiLR:RegistroLRCobros>";
     |HAZ LeeEsquema;
|FPRC;

|PROCESO FinalFactura;
     aDEtiq = "</siiLR:RegistroLRCobros>";
     aHEtiq = "</siiLR:RegistroLRCobros>";
     |HAZ LeeEsquema;
|FPRC;

|PROCESO IDFactura;
     aDEtiq = "<siiLR:IDFactura>";
     aHEtiq = "</siiLR:IDFactura>";
     |HAZ LeeEsquema;
|FPRC;

|PROCESO Cobros;
     aDEtiq = "<siiLR:Cobros>";
     aHEtiq = "</siiLR:Cobros>";
     |HAZ LeeEsquema;
|FPRC;

|PROCESO siim0005;
     |SI nPasada = 0;
         |SI #siim0005#23 = "C";  |O #siim0005#23 = "A";
             |ACABA;
         |FINSI;

         |SI #siim0005#9 > fFechaSys;
             |ACABA;
         |FINSI;

         nOk = 1;
         |ERROR10;
         |ACABA;
     |FINSI;

     |SI #siim0005#23 = "C";  |O #siim0005#23 = "A";
         |ACABA;
     |FINSI;

     |SI #siim0005#9 > fFechaSys;
         |ACABA;
     |FINSI;

     |HAZ InicioFactura;
     |HAZ IDFactura;
     |HAZ Cobros;
     |HAZ FinalFactura;
|FPRC;

|DEFBUCLE siim0005;
     #siim0005#1;
     ;
     #siim0002#0, #siim0002#1, 000001;
     #siim0002#0, #siim0002#1, 999999;
     ;
     NULL, siim0005;
|FIN;

|PROCESO CabXml;
     aAlfa = "<?xml version=" + &34 + "1.0" + &34 + " encoding=" + &34 + "UTF-8" + &34 + "?>";
     |HAZ GrabaFic;

     aAlfa = "<soapenv:Envelope xmlns:soapenv=" + &34;
     aAlfa = aAlfa + "http://schemas.xmlsoap.org/soap/envelope/" + &34;
     |HAZ GrabaFic;

     aAlfa = "xmlns:siiLR=" + &34 + "https://www2.agenciatributaria.gob.es/";
     aAlfa = aAlfa + "static_files/common/internet/dep/aplicaciones/es/aeat/ssii/fact/ws/SuministroLR.xsd" + &34;
     |HAZ GrabaFic;

     aAlfa = "xmlns:sii=" + &34 + "https://www2.agenciatributaria.gob.es/static_files/";
     aAlfa = aAlfa + "common/internet/dep/aplicaciones/es/aeat/ssii/fact/ws/SuministroInformacion.xsd" + &34 + ">";
     |HAZ GrabaFic;

     aAlfa = "<soapenv:Header/>";
     |HAZ GrabaFic;

     aAlfa = "   <soapenv:Body>";
     |HAZ GrabaFic;

     aAlfa = "      <siiLR:SuministroLRCobrosEmitidas>";
     |HAZ GrabaFic;

     aDEtiq = "<sii:Cabecera>";
     aHEtiq = "</sii:Cabecera>";
     |HAZ LeeEsquema;
|FPRC;

|PROCESO PieXml;
     aAlfa = "      </siiLR:SuministroLRCobrosEmitidas>";
     |HAZ GrabaFic;

     aAlfa = "   </soapenv:Body>";
     |HAZ GrabaFic;

     aAlfa = "</soapenv:Envelope>";
     |HAZ GrabaFic;
|FPRC;

|PROCESO AbreVtn;
     |VENTANA 0501, 0540, -1, 16, "Realizando el env¡o";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;
|FPRC;

|PROCESO CierraVtn;
     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;
|FPRC;

|PROCESO SIIBASE;
     |SI #siim0002#6 = 0;
         |MENAV "0000Lote sin registros";
         |ACABA;
     |FINSI;

     |SI #siim0002#6 = #siim0002#8;
         |MENAV "0000El lote no tiene registros para enviar";
         |ACABA;
     |FINSI;

     |VENTANA 0501, 3599, -1, 0, "";
     n2Vent = FSalida;
     |PTEC 802;  |PAUSA;

     |SUB_EJECUTA "siiz0011";

     |FINVENTANA n2Vent;

     |SI eaCert = "";
         |MENAV "0000Proceso cancelado";
         |ACABA;
     |FINSI;

     |HAZ AbreVtn;

     |HORA aHora;

     aFicXml = ("00000" + #siim0002#0) % -105;
     aFicXml = aFicXml + (("0000000000" + #siim0002#1) % -110);
     aFicXml = aFicXml + fFechaSys % -104;
     aFicXml = aFicXml + fFechaSys % 402;
     aFicXml = aFicXml + fFechaSys % 102;
     aFicXml = aFicXml + aHora % 102;
     aFicXml = aFicXml + aHora % 402;
     aFicXml = aFicXml + aHora % 702;

     aFicRes = aFicXml + ".res";
     aFicXml = aFicXml + ".xml";

     aAbreXml = aPathTmp + aFicXml;

     |XABRE "WB", aAbreXml, nHandle;
     |HAZ CabXml;

     nPasada = 1;  |BUCLE siim0005;

     |HAZ PieXml;
     |XCIERRA nHandle;

     |HAZ EnviaLote;

     |HAZ CierraVtn;
|FPRC;

|PROCESO SIIBOX;
     |HORA aHora;

     aFicXml = ("00000" + #siim0002#0) % -105;
     aFicXml = aFicXml + (("0000000000" + #siim0002#1) % -110);
     aFicXml = aFicXml + fFechaSys % -104;
     aFicXml = aFicXml + fFechaSys % 402;
     aFicXml = aFicXml + fFechaSys % 102;
     aFicXml = aFicXml + aHora % 102;
     aFicXml = aFicXml + aHora % 402;
     aFicXml = aFicXml + aHora % 702;

     aFicRes = aFicXml + ".res";
     aFicXml = aFicXml + ".xml";

     aAbreXml = aPathTmp + aFicXml;

     |XABRE "WB", aAbreXml, nHandle;
     |HAZ CabXml;
     nPasada = 1;  |BUCLE siim0005;
     |HAZ PieXml;
     |XCIERRA nHandle;

     |HAZ EnviaLote;
|FPRC;

|PROGRAMA;
     #siim0002#0 = enEmp;
     #siim0002#1 = enLot;
     |LEE 000#siim0002.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     nOk     = 0;
     nPasada = 0;  |BUCLE siim0005;
     |SI nOk = 0;
         |SI aModoImp = "SIIBASE";
             |MENAV "0000El lote no tiene registros para enviar";
         |FINSI;

         |ACABA;
     |FINSI;

     |SI aModoImp = "SIIBASE";
         |HAZ SIIBASE;
     |FINSI;

     |SI aModoImp = "SIIBOX";  |O aModoImp = "SIIGEN";
         |HAZ SIIBOX;
     |FINSI;
|FPRO;

|PROCESO T80;  |TIPO 80;
     aModoImp  = PARAMETRO;  |QBF aModoImp;
     fFechaSys = ~;

     |HAZ LeeUnidadBasico;
     |HAZ CargaColores;

     |ABRE #siim0000;
     |LEE 000#siim0000.p;
     |CIERRA #siim0000;

     |ABRE #siim0500;
     |LEE 000#siim0500.u;
     |CIERRA #siim0500;

     |ABRE #siim0001;
     |SELECT #1#siim0001;
     #siim0001#0 = enEmp;
     |LEE 000#siim0001.=;
     |SI FSalida ! 0;  |DDEFECTO #siim0001;  |FINSI;
     |CIERRA #siim0001;

     eaCodNif = #siim0001#2;
     |HAZ LeeNif;  |QBF eaNomNif;
     |SI eaNomNif ! "";
         #siim0001#1 = eaNomNif;
     |FINSI;

     aH60 = "z" * 60;
     aEsq = "12";

     aIP  = "";
     |IP_REMOTO aIP;

     |DBASE aBaseSII;  |QBF aBaseSII;
     aPathTmp = aBaseSII + "tmp";                    |MKDIR aPathTmp;
     aPathTmp = aBaseSII + "tmp/" + Usuario;         |MKDIR aPathTmp;
     aPathTmp = aBaseSII + "tmp/" + Usuario + "/";

     aPathPlu = aBaseSII + "plugins/";

     aPathSII = eaUnidadBasico + "\documentos";   |RMKDIR aPathSII;
     aPathSII = aPathSII + "\siibase";            |RMKDIR aPathSII;
     aPathSII = aPathSII + "\";

     aAlfa = aPathTmp + "*.xml";
     |_PDIR aAlfa;
     |PARA;  |SI FSalida = 0;  |HACIENDO;
          |FBORRA aAlfa;

          |_SDIR aAlfa;
     |SIGUE;

     aAlfa = aPathSII + "*.*";
     |R_PDIR aAlfa;
     |PARA;  |SI FSalida = 0;  |HACIENDO;
          aExt = aAlfa % -103;
          |SI aExt = "res";  |O aExt = "xml";  |O aExt = "bat";
              |RFBORRA aAlfa;
          |FINSI;

          |R_SDIR aAlfa;
     |SIGUE;

     aFicTmp = "siiw0203" + Usuario;  |NOME_DAT #siiw0203#aFicTmp#;
     |ABRE #siiw0203;  |CIERRA #siiw0203;  |DELINDEX #siiw0203;

     |SI aModoImp = "SIIGEN";
         aFicTmp = "siiw0207" + Usuario;  |NOME_DAT #siiw0207#aFicTmp#;
         |ABRE #siiw0207;
     |FINSI;

     |HAZ CargaUrl;

     |ABRET #siim0002;
     |ABRET #siim0005;
     |ABRET #siim0054;
     |ABRET #siim0501;
     |ABRET #siiw0203;
|FPRC;

|PROCESO T90;  |TIPO 90;
     |CIERRAT #siim0002;
     |CIERRAT #siim0005;
     |CIERRAT #siim0054;
     |CIERRAT #siim0501;
     |CIERRAT #siiw0203;

     |SI aModoImp = "SIIGEN";
         |CIERRA #siiw0207;
     |FINSI;

     |DELINDEX #siiw0203;

     |SUB_EJECUTA "siiz0004;REFRESCA";
|FPRC;
