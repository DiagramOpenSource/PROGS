|FICHEROS;
     siim0001;
     siim0008;
     siim1000;

     siiw0003,MANTE;
|FIN;

|VARIABLES;
     n0Vent              = 0;
     nOk                 = 0;
     nID                 = 0;
     nCmp1               = 0;
     nCmp2               = 0;
     nCmp3               = 0;
     nContenido          = 0;
     nCuot               = 0;
     nCuot1              = 0;
     nCuot2              = 0;

     aAlfa               = "";
     aID                 = "";
     aContenido          = "";

     {-1}aVent           = "";
         aVent1          = 0;
         aVent2          = 0;
         aVent3          = 0;
         aVent4          = 0;
         aVent5          = "";

 {40}aPopup              = "";

     &eaVarDni           = "";
     &enCalcCif          = 0;
     &enBtnIde           = 0;
     &enBtnRec           = 0;
     &enBtnEnt           = 0;
     &eaH60              = "";
     &eaCodNif           = "";
     &eaNomNif           = "";
     &eaPaiNif           = "";
     &enID               = 0;
|FIN;

|PROCESO Chequeo;
     nOk = 0;

     aAlfa = #siiw0003#14;  |QBF aAlfa;
     |SI aAlfa = "";
         |MENAV "0000Contenido incorrecto en n£mero de factura";
         nOk = 0;  |ACABA;
     |FINSI;

     |SI #siiw0003#16 < "01.01.2000";
         |MENAV "0000Contenido incorrecto en fecha de expedici¢n";
         nOk = 0;  |ACABA;
     |FINSI;

     |SI #siiw0003#17 < #siiw0003#16;
         |MENAV "0000La fecha contable no puede ser inferior a la fecha expedici¢n";
         nOk = 0;  |ACABA;
     |FINSI;

     |SI #siiw0003#5 = "01";
         |SI aAlfa = "";
             |MENAV "0000Contenido incorrecto NIF, debe tener contenido";
             nOk = 0;  |ACABA;
         |FINSI;
     |FINSI;

     |SI #siiw0003#5 = "02";  |O #siiw0003#5 = "03"; |O #siiw0003#5 = "04";
      |O #siiw0003#5 = "05";  |O #siiw0003#5 = "06";
         aAlfa = #siiw0003#13;  |QBF aAlfa;
         |SI aAlfa = "";
             |MENAV "0000Contenido incorrecto NIF pais residencia, debe tener contenido";
             nOk = 0;  |ACABA;
         |FINSI;
     |FINSI;

     aAlfa = #siiw0003#12;  |QBF aAlfa;
     |SI aAlfa = "";
         #siiw0003#12 = "ES";
     |FINSI;

     |SI aAlfa = "ESP";
         #siiw0003#12 = "ES";
     |FINSI;

     |SI #siiw0003#6 = "F5";
         |SI #siiw0003#10 ! #siim0001#2;
             |MENAV "0000Tipo factura F5. Los datos del proveedor deben ser los del titular del libro";
             nOk = 0;  |ACABA;
         |FINSI;
     |FINSI;

     |SI #siiw0003#5 = "02";
         |SI #siiw0003#7 ! "09";
             |MENAV "0000Contenido incorrecto en Clave ident.oper.trans.tributaria 1 debe ser 09";
             nOk = 0;  |ACABA;
         |FINSI;
     |FINSI;

     |SI #siiw0003#7 = "13";
         |SI #siiw0003#6 ! "F6";
             |MENAV "0000Clave operaci¢n 13 el tipo de factura deber¡a ser F6";
             nOk = 0;  |ACABA;
         |FINSI;

         nOk = 0;
         |PARA nCmp1 = 22;  |SI nCmp1 [ 57;  |HACIENDO nCmp1 = nCmp1 + 7;
               |SI #siiw0003#nCmp1# ! 0;
                   nOk = 1;  |SAL;
               |FINSI;
         |SIGUE;

         |SI nOk = 1;
             |MENAV "0000Clave operaci¢n 13 y tipo de factura F6, no puede tener cuotas de IVA";
             nOk = 0;  |ACABA;
         |FINSI;
     |FINSI;

     |SI #siiw0003#7 = "02";
         |SI #siiw0003#6 ! "F6";
             |MENAV "Clave operaci¢n 02 el tipo de factura deber¡a ser F6";
             nOk = 0;  |ACABA;
         |FINSI;

         |SI #siiw0003#61 = 0;  |Y #siiw0003#63 = 0;
             |MENAV "0000Clave operaci¢n 02 y tipo de factura F6, no hay contenido en compensaci¢n REAGYP";
             nOk = 0;  |ACABA;
         |FINSI;
     |FINSI;

     aAlfa = #siiw0003#15;  |QBF aAlfa;
     |SI aAlfa ! "";
         |SI #siiw0003#15 = #siiw0003#14;
             |MENAV "0000Contenido incorrecto en £ltimo n£mero factura, debe ser distnto a n£mero factura";
             nOk = 0;  |ACABA;
         |FINSI;

         |SI #siiw0003#6 ! "F4";
             |MENAV "0000Contenido incorrecto en £ltimo n£mero factura, tiene contenido y el tipo de factura no es F4";
             nOk = 0;  |ACABA;
         |FINSI;
     |FINSI;

     nOk = 0;
     |SI #siiw0003#6 = "R1";  nOk = 1;  |FINSI;
     |SI #siiw0003#6 = "R2";  nOk = 1;  |FINSI;
     |SI #siiw0003#6 = "R3";  nOk = 1;  |FINSI;
     |SI #siiw0003#6 = "R4";  nOk = 1;  |FINSI;
     |SI #siiw0003#6 = "R5";  nOk = 1;  |FINSI;

     |SI nOk = 1;
         |SI #siiw0003#70 = " ";
             |MENAV "0000Sin contenido en tipo rectificativa";
             nOk = 0;  |ACABA;
         |FINSI;

         |SI #siiw0003#70 = "S";
             |SI #siiw0003#71 = 0; |O #siiw0003#72 = 0;
/*
                 |MENAV "0000Contenido incorrecto no viene informada la base y cuota de rectificaci¢n";
                 nOk = 0;  |ACABA;
*/
             |FINSI;
         |FINSI;
     |SINO;
         |SI #siiw0003#70 ! " ";
             |MENAV "0000No procede contenido en tipo rectificativa";
             nOk = 0;  |ACABA;
         |FINSI;
     |FINSI;

     |SI #siiw0003#65 ! 0;
         nCuot = #siiw0003#63;
         |PARA nCmp1 = 22;  |SI nCmp1 [ 57;  |HACIENDO nCmp1 = nCmp1 + 7;
               nCuot = nCuot + #siiw0003#nCmp1#;
         |SIGUE;

         nCuot1 = nCuot ! 2;
         nCuot2 = #siiw0003#65 ! 2;

         |SI nCuot1 < 0; nCuot1 = -nCuot1;  |FINSI;
         |SI nCuot2 < 0; nCuot2 = -nCuot2;  |FINSI;

         |SI nCuot2 > nCuot1;
             |MENAV "Contenido incorrecto cuota deducible superior a las sumas de cuotas";
             nOk = 0;  |ACABA;
         |FINSI;
     |FINSI;

     aAlfa = #siiw0003#67 + #siiw0003#68 + #siiw0003#69;  |QBF aAlfa;
     |SI aAlfa = "";
         |MENAV "0000Sin contenido en descripci¢n de la operaci¢n";
         nOk = 0;  |ACABA;
     |FINSI;

     nOk = 1;
|FPRC;

|PROCESO ControlaBtnw3;
     |ESTADO_CONTROL enBtnIde, "DISABLE";
     |ESTADO_CONTROL enBtnRec, "DISABLE";
     |ESTADO_CONTROL enBtnEnt, "ENABLE";

     |SI #siiw0003#6 = "F3";
         |ESTADO_CONTROL enBtnIde, "ENABLE";
     |FINSI;

     |SI #siiw0003#6 = "R1";  |O #siiw0003#6 = "R2";
      |O #siiw0003#6 = "R3";  |O #siiw0003#6 = "R4";
      |O #siiw0003#6 = "R5";
         |ESTADO_CONTROL enBtnIde, "ENABLE";
         |ESTADO_CONTROL enBtnRec, "ENABLE";
     |SINO;
         #siiw0003#70 = " ";
         #siiw0003#71 = 0;
         #siiw0003#72 = 0;
         #siiw0003#73 = 0;
     |FINSI;

     |C_M #siiw0003#7, 1, "S";
     |SI #siiw0003#5 = "02";
         |C_M #siiw0003#7, 1, "N";
         #siiw0003#7 = "09";
     |FINSI;

     |C_M #siiw0003#15, 1, "S";
     |SI #siiw0003#6 ! "F4";
         |C_M #siiw0003#15, 1, "N";
     |FINSI;

     |C_M #siiw0003#66, 1, "S";
     |SI #siiw0003#7 ! "06";  |Y #siiw0003#8 ! "06";  |Y #siiw0003#9 ! "06";
         |C_M #siiw0003#66, 1, "N";  #siiw0003#66 = 0;
     |FINSI;

     |C_M #siiw0003#87, 1, "S";
     |SI #siiw0003#6 = "F2";
         |C_M #siiw0003#87, 1, "N";  #siiw0003#87 = "S";
     |FINSI;

     |SI #siiw0003#6 = "LC";
         |C_M #siiw0003#87, 1, "N";  #siiw0003#87 = "N";
     |FINSI;

     |C_M #siiw0003#19, 1, "S";
     |C_M #siiw0003#20, 1, "S";
     |C_M #siiw0003#21, 1, "S";
     |C_M #siiw0003#22, 1, "S";
     |C_M #siiw0003#23, 1, "S";
     |C_M #siiw0003#24, 1, "S";

     |C_M #siiw0003#26, 1, "S";
     |C_M #siiw0003#27, 1, "S";
     |C_M #siiw0003#28, 1, "S";
     |C_M #siiw0003#29, 1, "S";
     |C_M #siiw0003#30, 1, "S";
     |C_M #siiw0003#31, 1, "S";

     |C_M #siiw0003#33, 1, "S";
     |C_M #siiw0003#34, 1, "S";
     |C_M #siiw0003#35, 1, "S";
     |C_M #siiw0003#36, 1, "S";
     |C_M #siiw0003#37, 1, "S";
     |C_M #siiw0003#38, 1, "S";

     |C_M #siiw0003#40, 1, "S";
     |C_M #siiw0003#41, 1, "S";
     |C_M #siiw0003#42, 1, "S";
     |C_M #siiw0003#43, 1, "S";
     |C_M #siiw0003#44, 1, "S";
     |C_M #siiw0003#45, 1, "S";

     |C_M #siiw0003#47, 1, "S";
     |C_M #siiw0003#48, 1, "S";
     |C_M #siiw0003#49, 1, "S";
     |C_M #siiw0003#50, 1, "S";
     |C_M #siiw0003#51, 1, "S";
     |C_M #siiw0003#52, 1, "S";

     |C_M #siiw0003#54, 1, "S";
     |C_M #siiw0003#55, 1, "S";
     |C_M #siiw0003#56, 1, "S";
     |C_M #siiw0003#57, 1, "S";
     |C_M #siiw0003#58, 1, "S";
     |C_M #siiw0003#59, 1, "S";

     |C_M #siiw0003#61, 1, "N";
     |C_M #siiw0003#62, 1, "N";
     |C_M #siiw0003#63, 1, "N";

     |SI #siiw0003#7 = "02";
         |C_M #siiw0003#19, 1, "N";  #siiw0003#19 = "N";
         |C_M #siiw0003#20, 1, "N";  #siiw0003#20 = 0;
         |C_M #siiw0003#21, 1, "N";  #siiw0003#21 = 0;
         |C_M #siiw0003#22, 1, "N";  #siiw0003#22 = 0;
         |C_M #siiw0003#23, 1, "N";  #siiw0003#23 = 0;
         |C_M #siiw0003#24, 1, "N";  #siiw0003#24 = 0;

         |C_M #siiw0003#26, 1, "N";  #siiw0003#26 = "N";
         |C_M #siiw0003#27, 1, "N";  #siiw0003#27 = 0;
         |C_M #siiw0003#28, 1, "N";  #siiw0003#28 = 0;
         |C_M #siiw0003#29, 1, "N";  #siiw0003#29 = 0;
         |C_M #siiw0003#30, 1, "N";  #siiw0003#30 = 0;
         |C_M #siiw0003#31, 1, "N";  #siiw0003#31 = 0;

         |C_M #siiw0003#33, 1, "N";  #siiw0003#33 = "N";
         |C_M #siiw0003#34, 1, "N";  #siiw0003#34 = 0;
         |C_M #siiw0003#35, 1, "N";  #siiw0003#35 = 0;
         |C_M #siiw0003#36, 1, "N";  #siiw0003#36 = 0;
         |C_M #siiw0003#37, 1, "N";  #siiw0003#37 = 0;
         |C_M #siiw0003#38, 1, "N";  #siiw0003#38 = 0;

         |C_M #siiw0003#40, 1, "N";  #siiw0003#40 = "N";
         |C_M #siiw0003#41, 1, "N";  #siiw0003#41 = 0;
         |C_M #siiw0003#42, 1, "N";  #siiw0003#42 = 0;
         |C_M #siiw0003#43, 1, "N";  #siiw0003#43 = 0;
         |C_M #siiw0003#44, 1, "N";  #siiw0003#44 = 0;
         |C_M #siiw0003#45, 1, "N";  #siiw0003#45 = 0;

         |C_M #siiw0003#47, 1, "N";  #siiw0003#47 = "N";
         |C_M #siiw0003#48, 1, "N";  #siiw0003#48 = 0;
         |C_M #siiw0003#49, 1, "N";  #siiw0003#49 = 0;
         |C_M #siiw0003#50, 1, "N";  #siiw0003#50 = 0;
         |C_M #siiw0003#51, 1, "N";  #siiw0003#51 = 0;
         |C_M #siiw0003#52, 1, "N";  #siiw0003#52 = 0;

         |C_M #siiw0003#54, 1, "N";  #siiw0003#54 = "N";
         |C_M #siiw0003#55, 1, "N";  #siiw0003#55 = 0;
         |C_M #siiw0003#56, 1, "N";  #siiw0003#56 = 0;
         |C_M #siiw0003#57, 1, "N";  #siiw0003#57 = 0;
         |C_M #siiw0003#58, 1, "N";  #siiw0003#58 = 0;
         |C_M #siiw0003#59, 1, "N";  #siiw0003#59 = 0;

         |C_M #siiw0003#61, 1, "S";
         |C_M #siiw0003#62, 1, "S";
         |C_M #siiw0003#63, 1, "S";
     |SINO;
         #siiw0003#61 = 0;
         #siiw0003#62 = 0;
         #siiw0003#63 = 0;
     |FINSI;

     |HAZ T0Totalw3;

     |PARA nCmp1 = 19;  |SI nCmp1 [ 63;  |HACIENDO nCmp1 = nCmp1 + 1;
           |PINTA #siiw0003#nCmp1#;
     |SIGUE;

     |PINTA #siiw0003#7;
     |PINTA #siiw0003#15;
     |PINTA #siiw0003#66;
     |PINTA #siiw0003#87;
|FPRC;

|PROCESO siim0008Bor;
     |BORRA 020#siim0008.a;
     |LIBERA #siim0008;
|FPRC;

|DEFBUCLE siim0008Bor;
     #siim0008#1;
     ;
     #siiw0003#0, #siiw0003#1, #siiw0003#2, "";
     #siiw0003#0, #siiw0003#1, #siiw0003#2, eaH60;
     be;
     NULL, siim0008Bor;
|FIN;

|PROCESO T12w3;   |TIPO 12;
     |SI FSalida = 7;
         |ESTADO_CONTROL enBtnIde, "DISABLE";
         |ESTADO_CONTROL enBtnRec, "DISABLE";
         |ESTADO_CONTROL enBtnEnt, "DISABLE";

         |ACABA;
     |FINSI;

     |SI FSalida ! 1;
         |POSICIONATE #siiw0003#5;
         |ERROR;
         |ACABA;
     |FINSI;

     |HAZ Chequeo;
     |SI nOk = 0;
         |ERROR;
         |POSICIONATE #siiw0003#5;
         |ACABA;
     |FINSI;

     |SI #siiw0003#6 ! "R1";  |Y #siiw0003#6 ! "R2";
      |Y #siiw0003#6 ! "R3";  |Y #siiw0003#6 ! "R4";
      |Y #siiw0003#6 ! "R5";  |Y #siiw0003#6 ! "F3";
         |BUCLE siim0008Bor;
     |FINSI;

     |ESTADO_CONTROL enBtnIde, "DISABLE";
     |ESTADO_CONTROL enBtnRec, "DISABLE";
     |ESTADO_CONTROL enBtnEnt, "DISABLE";
|FPRC;

|PROCESO T0Totalw3;  |TIPO 0;
     #siiw0003#25 = #siiw0003#20 + #siiw0003#22 + #siiw0003#24;
     #siiw0003#32 = #siiw0003#27 + #siiw0003#29 + #siiw0003#31;
     #siiw0003#39 = #siiw0003#34 + #siiw0003#36 + #siiw0003#38;
     #siiw0003#46 = #siiw0003#41 + #siiw0003#43 + #siiw0003#45;
     #siiw0003#53 = #siiw0003#48 + #siiw0003#50 + #siiw0003#52;
     #siiw0003#60 = #siiw0003#55 + #siiw0003#57 + #siiw0003#59;

     #siiw0003#64 = #siiw0003#25 + #siiw0003#32 + #siiw0003#39;
     #siiw0003#64 = #siiw0003#64 + #siiw0003#46 + #siiw0003#53;
     #siiw0003#64 = #siiw0003#64 + #siiw0003#60;
     #siiw0003#64 = #siiw0003#64 + #siiw0003#61 + #siiw0003#63;

     |PINTA #siiw0003#25;
     |PINTA #siiw0003#32;
     |PINTA #siiw0003#39;
     |PINTA #siiw0003#46;
     |PINTA #siiw0003#53;
     |PINTA #siiw0003#60;
     |PINTA #siiw0003#64;
|FPRC;

|PROCESO T7C65w3;  |TIPO 7;
     |SI #siiw0003#65 ! 0;  |ACABA;  |FINSI;

     #siiw0003#65 = #siiw0003#22 + #siiw0003#29 + #siiw0003#36;
     #siiw0003#65 = #siiw0003#65 + #siiw0003#43 + #siiw0003#50;
     #siiw0003#65 = #siiw0003#65 + #siiw0003#57 + #siiw0003#63;

     |PINTA #siiw0003#65;
|FPRC;

|PROCESO T0Porcw3;  |TIPO 0;
     |SI #siiw0003#Campo# = nContenido;
         |ACABA;
     |FINSI;

     nCmp2 = Campo;
     |SI nCmp2 = 21; nCmp1 = 20;  |FINSI;
     |SI nCmp2 = 23; nCmp1 = 20;  |FINSI;

     |SI nCmp2 = 28; nCmp1 = 27;  |FINSI;
     |SI nCmp2 = 30; nCmp1 = 27;  |FINSI;

     |SI nCmp2 = 35; nCmp1 = 34;  |FINSI;
     |SI nCmp2 = 37; nCmp1 = 34;  |FINSI;

     |SI nCmp2 = 42; nCmp1 = 41;  |FINSI;
     |SI nCmp2 = 44; nCmp1 = 41;  |FINSI;

     |SI nCmp2 = 49; nCmp1 = 48;  |FINSI;
     |SI nCmp2 = 51; nCmp1 = 48;  |FINSI;

     |SI nCmp2 = 56; nCmp1 = 55;  |FINSI;
     |SI nCmp2 = 58; nCmp1 = 55;  |FINSI;

     |SI nCmp2 = 62; nCmp1 = 61;  |FINSI;

     nCmp3 = nCmp2 + 1;

     #siiw0003#nCmp3# = #siiw0003#nCmp1# % #siiw0003#nCmp2#;

     |PINTA #siiw0003#nCmp3#;
|FPRC;

|PROCESO T7Porcw3;  |TIPO 7;
     nContenido = #siiw0003#Campo#;
|FPRC;

|PROCESO T0C13w3;  |TIPO 0;
     |C_M #siiw0003#13, 0, aAlfa;
     |SI aAlfa = "N";  |ACABA;  |FINSI;

     aAlfa = #siiw0003#13;  |QBF aAlfa;
     |SI aAlfa = "";
         |MENAV "0000Debe tener contenido.";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO T0C12w3;  |TIPO 0;
     |SI #siiw0003#12 = "   ";
         #siiw0003#12 = "ES";
     |FINSI;

     |SI #siiw0003#12 = "ESP";
         #siiw0003#12 = "ES";
     |FINSI;

     |SI #siiw0003#5 ! "01";  |Y #siiw0003#12 = "ES ";
         |MENAV "0000Pais incorrecto";
         |ERROR;
         |ACABA;
     |FINSI;

     |SI #siiw0003#5 = "01";  |Y #siiw0003#12 ! "ES ";
         |MENAV "0000Pais incorrecto";
         |ERROR;
         |ACABA;
     |FINSI;

     |C_M #siiw0003#13, 1, "S";
     |SI #siiw0003#12 = "ES ";
         |C_M #siiw0003#13, 1, "N"; #siiw0003#13 = "";
     |FINSI;

     aAlfa = #siiw0003#13;  |QBF aAlfa;
     |SI #siiw0003#5 = "02";  |Y aAlfa = "";
         aAlfa = #siiw0003#12;  |QBF aAlfa;

         |SI aAlfa = "GR";  aAlfa = "EL";  |FINSI;

         #siiw0003#13 = aAlfa + #siiw0003#10;
     |FINSI;

     |PINTA #siiw0003#13;
|FPRC;

|PROCESO T0C10w3;  |TIPO 0;
     eaVarDni  = #siiw0003#Campo#;
     enCalcCif = 0;
     |HAZ LetraNif;
     eaVarDni = (eaVarDni + "             ") % 112;
     |SI #siiw0003#Campo# ! eaVarDni;
         |CONFI "0000SEl NIF puede ser Erroneo. Desea Calcularlo.";
         |SI FSalida = 0;
             #siiw0003#Campo# = eaVarDni;  |PINTA #siiw0003#Campo#;
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;

     aContenido = Contenido;
     |SI #siiw0003#10 ! aContenido;
         eaCodNif = #siiw0003#10;
         |HAZ LeeNif;

         #siiw0003#11 = eaNomNif;
         #siiw0003#12 = eaPaiNif;
     |FINSI;

     aAlfa = #siiw0003#12 % 101;
     |SI aAlfa = "X";  |O aAlfa = "Y";
         #siiw0003#5 = "01";
     |FINSI;

     |PINTA #siiw0003#5;
     |PINTA #siiw0003#11;
     |PINTA #siiw0003#12;
     |PINTA #siiw0003#13;
|FPRC;

|PROCESO T0C7w3;   |TIPO 0;
     nOk = 0;

     |SI Campo ! 7;
         |SI #siiw0003#Campo# = "  ";  nOk = 1;  |FINSI;
     |FINSI;

     |SI #siiw0003#Campo# = "01";  nOk = 1;  |FINSI;
     |SI #siiw0003#Campo# = "02";  nOk = 1;  |FINSI;
     |SI #siiw0003#Campo# = "03";  nOk = 1;  |FINSI;
     |SI #siiw0003#Campo# = "04";  nOk = 1;  |FINSI;
     |SI #siiw0003#Campo# = "05";  nOk = 1;  |FINSI;
     |SI #siiw0003#Campo# = "06";  nOk = 1;  |FINSI;
     |SI #siiw0003#Campo# = "07";  nOk = 1;  |FINSI;
     |SI #siiw0003#Campo# = "08";  nOk = 1;  |FINSI;
     |SI #siiw0003#Campo# = "09";  nOk = 1;  |FINSI;
     |SI #siiw0003#Campo# = "12";  nOk = 1;  |FINSI;
     |SI #siiw0003#Campo# = "13";  nOk = 1;  |FINSI;
     |SI #siiw0003#Campo# = "14";  nOk = 1;  |FINSI;

     |SI nOk = 0;
         |MENAV "0000Contenido incorrecto. Pulse F11 para ver tabla de contenido";
         |ERROR;
         |ACABA;
     |FINSI;

     |SI Campo = 7;
         |C_M #siiw0003#8, 1, "S";
         |C_M #siiw0003#9, 1, "S";

         |SI #siiw0003#7 ! "05";  |Y #siiw0003#7 ! "07";  |Y #siiw0003#7 ! "12";
             |C_M #siiw0003#8, 1, "N";  #siiw0003#8 = "";
             |C_M #siiw0003#9, 1, "N";  #siiw0003#9 = "";
         |FINSI;

         |PINTA #siiw0003#8;
         |PINTA #siiw0003#9;

         |HAZ ControlaBtnw3;

         |ACABA;
     |FINSI;

     |SI #siiw0003#Campo# = "  ";
         |ACABA;
     |FINSI;

     nOk = 0;
     |SI #siiw0003#7 = "05";
         |SI #siiw0003#Campo# = "01";  nOk = 1;  |FINSI;
         |SI #siiw0003#Campo# = "07";  nOk = 1;  |FINSI;
         |SI #siiw0003#Campo# = "12";  nOk = 1;  |FINSI;
     |FINSI;

     |SI #siiw0003#7 = "07";
         |SI #siiw0003#Campo# = "01";  nOk = 1;  |FINSI;
         |SI #siiw0003#Campo# = "03";  nOk = 1;  |FINSI;
         |SI #siiw0003#Campo# = "05";  nOk = 1;  |FINSI;
         |SI #siiw0003#Campo# = "12";  nOk = 1;  |FINSI;
     |FINSI;

     |SI #siiw0003#7 = "12";
         |SI #siiw0003#Campo# = "05";  nOk = 1;  |FINSI;
         |SI #siiw0003#Campo# = "06";  nOk = 1;  |FINSI;
         |SI #siiw0003#Campo# = "07";  nOk = 1;  |FINSI;
         |SI #siiw0003#Campo# = "08";  nOk = 1;  |FINSI;
     |FINSI;

     |SI nOk = 0;
         |MENAV "0000Contenido incorrecto. Pulse F11 para ver tabla de contenido";
         |ERROR;
         |ACABA;
     |FINSI;

     |HAZ ControlaBtnw3;
|FPRC;

|PROCESO T66C7w3;  |TIPO 66;
     aAlfa = "|C" + (("000" + Campo) % -103) + "WID";
     aID   = #siiw0003#aAlfa#;
     nID   = aID;

     aPopup1  = nID;  || Si fuera -1 en la posicion

     |SI Campo = 7;
         aPopup2  = 12;
         aPopup3  = "01. Operaciones de régimen general";
         aPopup4  = "02. Operaciones por las que los Empresarios satisfacen compensaciones REAGYP";
         aPopup5  = "03. Operaciones de R.E.B.U.";
         aPopup6  = "04. Operaciones régimen especial oro de inversión";
         aPopup7  = "05. Operaciones régimen especial agencias de viajes.";
         aPopup8  = "06. Operaciones régimen especial grupo de entidades en iva (nivel avanzado)";
         aPopup9  = "07. Operaciones de R.E.C.C.";
         aPopup10 = "08. Operaciones sujetas al IPSI/IGIC";
         aPopup11 = "09. Adquisiciones intracomunitarias de bienes y prestaciones de servicios";
         aPopup12 = "12. Operaciones de arrendamiento de local de negocio";
         aPopup13 = "13. Factura correspondiente a una importación (informada sin asociar a un DUA)";
         aPopup14 = "14. Primer semestre 2017 y otras facturas anteriores a la inclusión del SII";
     |FINSI;

     |SI Campo ! 7;
         aPopup2  = "";
         |SI #siiw0003#7 = "05";
             aPopup2  = "4";
             aPopup3  = "    No procede clave de operación";
             aPopup4  = "01. Operaciones de régimen general";
             aPopup5  = "07. Operaciones de R.E.C.C.";
             aPopup6  = "12. Operaciones de arrendamiento de local de negocio";
         |FINSI;

         |SI #siiw0003#7 = "07";
             aPopup2  = "5";
             aPopup3  = "    No procede clave de operación";
             aPopup4  = "01. Operaciones de régimen general";
             aPopup5  = "03. Operaciones de R.E.B.U.";
             aPopup6  = "05. Operaciones régimen especial agencias de viajes.";
             aPopup7  = "12. Operaciones de arrendamiento de local de negocio";
         |FINSI;

         |SI #siiw0003#7 = "12";
             aPopup2  = "5";
             aPopup3  = "    No procede clave de operación";
             aPopup4  = "05. Operaciones régimen especial agencias de viajes.";
             aPopup5  = "06. Operaciones régimen especial grupo de entidades en iva (nivel avanzado)";
             aPopup6  = "07. Operaciones de R.E.C.C.";
             aPopup7  = "08. Operaciones sujetas al IPSI/IGIC";
         |FINSI;
     |FINSI;

     |SI aPopup2 = "";  |ACABA;  |FINSI;

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
         IaPopup  = aPopup3  <-;
         IaPopup  = IaPopup + (FSalida - 1);

         aAlfa = aPopup % 102;  #siiw0003#Campo# = aAlfa;
     |FINSI;

     |PINTA #siiw0003#Campo#;

     |HAZ ControlaBtnw3;
|FPRC;

|PROCESO T0C6w3;  |TIPO 0;
     nOk = 0;

     |SI #siiw0003#6 = "F1";  nOk = 1;  |FINSI;
     |SI #siiw0003#6 = "F2";  nOk = 1;  |FINSI;
     |SI #siiw0003#6 = "F3";  nOk = 1;  |FINSI;
     |SI #siiw0003#6 = "F4";  nOk = 1;  |FINSI;
     |SI #siiw0003#6 = "F5";  nOk = 1;  |FINSI;
     |SI #siiw0003#6 = "F6";  nOk = 1;  |FINSI;
     |SI #siiw0003#6 = "R1";  nOk = 1;  |FINSI;
     |SI #siiw0003#6 = "R2";  nOk = 1;  |FINSI;
     |SI #siiw0003#6 = "R3";  nOk = 1;  |FINSI;
     |SI #siiw0003#6 = "R4";  nOk = 1;  |FINSI;
     |SI #siiw0003#6 = "R5";  nOk = 1;  |FINSI;
     |SI #siiw0003#6 = "LC";  nOk = 1;  |FINSI;

     |SI nOk = 0;
         |MENAV "0000Contenido incorrecto. Pulse F11 para ver tabla de contenido";
         |ERROR;
         |ACABA;
     |FINSI;

     |HAZ ControlaBtnw3;
|FPRC;

|PROCESO T66C6w3;  |TIPO 66;
     aAlfa = "|C006WID";
     aID   = #siiw0003#aAlfa#;
     nID   = aID;

     aPopup1  = nID;  || Si fuera -1 en la posicion
     aPopup2  = 12;
     aPopup3  = "F1. Factura completa o simplificada cualificada del art.7.2 y 7.3";
     aPopup4  = "F2. Factura simplificada o sin identificacion del destinatario art.6.1.d";
     aPopup5  = "F3. Factura emitida en sustitucion de facturas simplificadas";
     aPopup6  = "F4. Asiento resumen de facturas.";
     aPopup7  = "F5. Importaciones (DUA)";
     aPopup8  = "F6. Otros justificantes contables y documentos justificativos";
     aPopup9  = "R1. Factura rectificativa. Error fundado y Articulo 80 uno y dos LIVA.";
     aPopup10 = "R2. Factura rectificativa. Articulo 80 tres LIVA - concurso acreedores";
     aPopup11 = "R3. Articulo 80 cuatro LIVA - deuda incobrable.";
     aPopup12 = "R4. Resto";
     aPopup13 = "R5. Rectificativa en facturas simplificadas.";
     aPopup14 = "LC. Aduanas - Liquidacion complementaria.";

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
         IaPopup  = aPopup3  <-;
         IaPopup  = IaPopup + (FSalida - 1);

         aAlfa = aPopup % 102;  #siiw0003#6 = aAlfa;
     |FINSI;

     |PINTA #siiw0003#6;
|FPRC;

|PROCESO T0C5w3;   |TIPO 0;
     nOk = 0;

     |SI #siiw0003#5 = "01";  nOk = 1;  |FINSI;
     |SI #siiw0003#5 = "02";  nOk = 1;  |FINSI;
     |SI #siiw0003#5 = "03";  nOk = 1;  |FINSI;
     |SI #siiw0003#5 = "04";  nOk = 1;  |FINSI;
     |SI #siiw0003#5 = "05";  nOk = 1;  |FINSI;
     |SI #siiw0003#5 = "06";  nOk = 1;  |FINSI;

     |SI nOk = 0;
         |MENAV "0000 Clave identificaci¢n pais de residencia incorrecto";
         |ERROR;
         |ACABA;
     |FINSI;

     |HAZ ControlaBtnw3;
|FPRC;

|PROCESO T66C5w3;  |TIPO 66;
     aAlfa = "|C005WID";
     aID   = #siiw0003#aAlfa#;
     nID   = aID;

     aPopup1  = nID;
     aPopup2  = 6;
     aPopup3  = "01 NIF";
     aPopup4  = "02 NIF/IVA (NIF operador intracomunitario)";
     aPopup5  = "03 Pasaporte";
     aPopup6  = "04 Documento Oficial de identificacion expedido por el pais o territorio de residencia";
     aPopup7  = "05 Certificado de residencia fiscal";
     aPopup8  = "06 Otro documento probatorio";

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
         IaPopup  = aPopup3  <-;
         IaPopup  = IaPopup + (FSalida - 1);

         aAlfa = aPopup % 102;  #siiw0003#5 = aAlfa;
     |FINSI;

     |PINTA #siiw0003#5;
|FPRC;

|PROCESO BtnBus;
     |ABRE #siim1000;
     |CONSULTA_CLAVE #2#siim1000;
     |SI FSalida = 0;
         #siiw0003#67 = #siim1000#1 % 180;
         #siiw0003#68 = #siim1000#1 % 8180;
         #siiw0003#69 = #siim1000#1 % 16180;

         |PINTA #siiw0003#67;
     |FINSI;
     |CIERRA #siim1000;

     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;

     |PTEC 808;
     |POSICIONATE #siiw0003#67;
|FPRC;

|PROCESO BtnMas;
     |VENTANA 0501, 0501, -1, 16, "";
     n0Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n0Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     |SUB_EJECUTA "siim1000;MAS";

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n0Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n0Vent;

     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;

     |SI enID ! 0;
         |ABRE #siim1000;
         #siim1000#0 = enID;
         |LEE 000#siim1000.=;
         |SI FSalida = 0;
             #siiw0003#67 = #siim1000#1 % 180;
             #siiw0003#68 = #siim1000#1 % 8180;
             #siiw0003#69 = #siim1000#1 % 16180;

             |PINTA #siiw0003#67;
         |FINSI;
         |CIERRA #siim1000;
     |FINSI;

     |PTEC 808;
     |POSICIONATE #siiw0003#62;
|FPRC;

|PROCESO BtnMen;
     |VENTANA 0501, 0501, -1, 16, "";
     n0Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n0Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     |SUB_EJECUTA "siim1000;MENOS";

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n0Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n0Vent;

     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;

     |PTEC 808;
|FPRC;
