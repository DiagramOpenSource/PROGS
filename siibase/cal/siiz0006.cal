|FICHEROS;
     siim0001;
     siim0002;
     siim0009;

     siiw0006;
     siiw0015;
     siiw0007;
|FIN;

|VARIABLES;
     n0Vent              = 0;
     n1Vent              = 0;
     nGrid9              = -1;
     nRango              = 0;
     nPanta              = 0;
     nPantaw6            = 0;
     nCampo              = 0;
     nIdSalir            = 0;
     nReg                = 0;
     nDReg               = 0;
     nHReg               = 0;

     nCmp1               = 0;
     nCmp2               = 0;
     nOk                 = 0;

     aEsc1               = "";
     aEsc2               = "";
     aRetu               = "";
     aTecla              = "";
     aAlfa               = "";
     aQQ                 = "|NC";

     fFecha              = @;

     {2}nNivel           = 0;

     {-1}aVent           = "";
         aVent1          = 0;
         aVent2          = 0;
         aVent3          = 0;
         aVent4          = 0;
         aVent5          = "";

     &enEmp              = 0;
     &enEje              = 0;
     &enPer              = 0;
     &enLot              = 0;
     &enReg              = 0;
     &enTCampo           = 0;

     &enBtnAlt           = -1;
     &enBtnMod           = -1;
     &enBtnBaj           = -1;
     &enBtnEnv           = -1;
     &eaBtnCon           = "";
     &eaPaq              = "";

     &eaAzul             = "";
     &eaVerde            = "";
     &eaRojo             = "";
     &eaRosa             = "";
     &eaMarron           = "";
|FIN;

|PROCESO PideFactura;
     nOk = 0;

     |VENTANA 0501, 1265, -1, 16, "Identificar factura";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |C_M #siiw0006#1, 1, "N";
     |C_V #siiw0006#1, 1, "N";

     |DDEFECTO #siiw0006;
     |PINPA #0#siiw0006; nPantaw6 = FSalida;
     |PINDA #0#siiw0006;

     |CONTROL_SIMPLE nPantaw6, "LABEL,{{7}}       ", 0501, 0520, NULL;
     |CONTROL_SIMPLE nPantaw6, "BOTON,{{155}}", 0560, 0765, Busqueda;

     |ENDATOS #1#siiw0006;
     |SI FSalida = 0;
         nOk = 1;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;
|FPRC;

|PROCESO Baja9;
     #siim0009#0 = #siim0002#0;
     #siim0009#1 = #siim0002#1;
     #siim0009#2 = nDReg;
|FPRC;

|PROCESO Alta9;
     #siim0009#0 = #siim0002#0;
     #siim0009#1 = #siim0002#1;
     #siim0009#2 = nHReg;
|FPRC;

|PROCESO PintaCampos;
     |PARA nCampo = 0;  |SI nCampo [ enTCampo;  |HACIENDO nCampo = nCampo + 1;
           |C_V #siiw0015#nCampo#, 0, aAlfa;
           |SI aAlfa = "S";
               |PINTA #siiw0015#nCampo#;
           |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Evnt9;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#siim0009.=;

         |ESTADO_CONTROL enBtnMod, "ENABLE";
         |ESTADO_CONTROL enBtnBaj, "ENABLE";
         |ESTADO_CONTROL enBtnEnv, "DISABLE";

         |SI #siim0009#21 = "C"; |O #siim0009#21 = "A";
             |SI enBtnMod ! -1;  |ESTADO_CONTROL enBtnMod, "DISABLE";  |FINSI;
             |SI enBtnBaj ! -1;  |ESTADO_CONTROL enBtnBaj, "DISABLE";  |FINSI;
         |FINSI;

         |SI #siim0009#21 ! "P";
             |ESTADO_CONTROL enBtnEnv, "ENABLE";
         |FINSI;

         |PARA nCampo = 0;  |SI nCampo [ enTCampo;  |HACIENDO nCampo = nCampo + 1;
               #siiw0015#nCampo# = #siim0009#nCampo#;
         |SIGUE;

         |HAZ PintaCampos;
     |FINSI;
|FPRC;

|PROCESO QuitaBotonSalir;
     |ESTADO_CONTROL enBtnAlt, "DISABLE";
     |ESTADO_CONTROL enBtnEnv, "DISABLE";

     |SI enBtnMod ! -1;  |ESTADO_CONTROL enBtnMod, "DISABLE";  |FINSI;
     |SI enBtnBaj ! -1;  |ESTADO_CONTROL enBtnBaj, "DISABLE";  |FINSI;

     |ESTADO_CONTROL nGrid9,   "DISABLE";

     nIdSalir = 0;
     InNivel   = nNivel1<-;
     nNivel1   = 0;
     |ESPECIFICA "IDBOTONTECLA", nNivel;

     nIdSalir = nNivel2;
     |ESTADO_CONTROL nIdSalir, "HIDE";
|FPRC;

|PROCESO PonBotonSalir;
     |ESTADO_CONTROL nGrid9,   "ENABLE";
     |ESTADO_CONTROL nIdSalir, "SHOW";
     |ESTADO_CONTROL enBtnAlt, "ENABLE";
     |ESTADO_CONTROL enBtnEnv, "DISABLE";

     |SI enBtnMod ! -1;  |ESTADO_CONTROL enBtnMod, "DISABLE";  |FINSI;
     |SI enBtnBaj ! -1;  |ESTADO_CONTROL enBtnBaj, "DISABLE";  |FINSI;
|FPRC;

|PROCESO EntraDatos;
     |DDEFECTO #siiw0015;

     |PARA nCampo = 0;  |SI nCampo [ enTCampo;  |HACIENDO nCampo = nCampo + 1;
           #siiw0015#nCampo# = #siim0009#nCampo#;
     |SIGUE;

     |HAZ PintaCampos;

     |PULSATECLA;
     |ENDATOS #1#siiw0015;
|FPRC;

|PROCESO BtnAlt;
     |HAZ QuitaBotonSalir;

     #siim0009#0  = #siim0002#0;
     #siim0009#1  = #siim0002#1;
     #siim0009#2  = 999999;
     |LEE 000#siim0009.];
     |SI FSalida = 0;
         |LEE 000#siim0009.a;
     |SINO;
         |LEE 000#siim0009.u;
     |FINSI;

     nReg = 1;
     |SI FSalida = 0;  |Y #siim0009#0  = #siim0002#0;
                       |Y #siim0009#1  = #siim0002#1;
         nReg = #siim0009#2 + 1;
     |FINSI;

     |DDEFECTO #siim0009;
     #siim0009#0  = #siim0002#0;
     #siim0009#1  = #siim0002#1;
     #siim0009#2  = nReg;
     #siim0009#3  = #siim0002#4;
     #siim0009#4  = #siim0002#5;
     |GRABA 020#siim0009.n;
     |LIBERA #siim0009;

     |SI #siim0002#2 ! "31";
         |HAZ PideFactura;
         |SI nOk = 0;
             #siim0009#0  = #siim0002#0;
             #siim0009#1  = #siim0002#1;
             #siim0009#2  = nReg;
             |LEE 101#siim0009.=;

             |BORRA 020#siim0009.a;

             |HAZ PonBotonSalir;
             |FOCOTECLADO nGrid9;
             |ACABA;
         |FINSI;

         |C_M #siiw0015#8, 1, "N";
         |C_M #siiw0015#5, 1, "N";
     |FINSI;

     FSalida = 0;
     |SI #siim0002#2 ! "35";
         |HAZ EntraDatos;
     |FINSI;

     |SI FSalida = 0;
         #siim0009#0  = #siim0002#0;
         #siim0009#1  = #siim0002#1;
         #siim0009#2  = nReg;
         |LEE 101#siim0009.=;

         |PARA nCampo = 3;  |SI nCampo [ enTCampo;  |HACIENDO nCampo = nCampo + 1;
               #siim0009#nCampo# = #siiw0015#nCampo#;
         |SIGUE;

         #siim0009#26 = "N";
         |SI #siim0002#2 = "35";
             #siim0009#26 = "S";
         |FINSI;

         |GRABA 020#siim0009.a;
     |SINO;
         #siim0009#0  = #siim0002#0;
         #siim0009#1  = #siim0002#1;
         #siim0009#2  = nReg;
         |LEE 101#siim0009.=;

         |BORRA 020#siim0009.a;
     |FINSI;

     |LIBERA #siim0009;

     |HAZ PonBotonSalir;

     |REFRESCACONTROL nGrid9;
     |FOCOTECLADO nGrid9;
|FPRC;

|PROCESO BtnMod;
     |LEE 101#siim0009.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |HAZ QuitaBotonSalir;

     |HAZ EntraDatos;
     |SI FSalida = 0;
         |PARA nCampo = 3;  |SI nCampo [ enTCampo;  |HACIENDO nCampo = nCampo + 1;
               #siim0009#nCampo# = #siiw0015#nCampo#;
         |SIGUE;

         #siim0009#26 = "N";
         |SI #siim0002#2 = "35";
             #siim0009#26 = "S";
         |FINSI;

         |GRABA 020#siim0009.a;
     |FINSI;

     |LIBERA #siim0009;

     |HAZ PonBotonSalir;

     |REFRESCACONTROL nGrid9;
     |FOCOTECLADO nGrid9;
|FPRC;

|PROCESO BtnBaj;
     |LEE 101#siim0009.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |CONFI "0000 Confirma la baja del registro seleccionado";
     |SI FSalida ! 0;
         |FOCOTECLADO nGrid9;
         |ACABA;
     |FINSI;

     |BORRA 020#siim0009.a;
     |LIBERA #siim0009;

     |ESTADO_CONTROL enBtnAlt, "ENABLE";

     |SI enBtnMod ! -1;  |ESTADO_CONTROL enBtnMod, "DISABLE";  |FINSI;
     |SI enBtnBaj ! -1;  |ESTADO_CONTROL enBtnBaj, "DISABLE";  |FINSI;

     |REFRESCACONTROL nGrid9;
     |FOCOTECLADO nGrid9;
|FPRC;

|PROCESO BtnEnv;
     |VENTANA 0501, 1599, -1, 16, "Datos del env¡o";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     #siiw0007#0 = #siiw0015#18;
     #siiw0007#1 = #siiw0015#19;
     #siiw0007#2 = #siiw0015#20;

     |SI #siiw0015#21 = "C";  #siiw0007#3 = "Correcto";            |FINSI;
     |SI #siiw0015#21 = "A";  #siiw0007#3 = "AceptadaConErrores";  |FINSI;
     |SI #siiw0015#21 = "I";  #siiw0007#3 = "Incorrecto";          |FINSI;

     #siiw0007#4 = #siiw0015#24;
     #siiw0007#5 = #siiw0015#22;
     #siiw0007#6 = #siiw0015#23 % 180;
     #siiw0007#7 = #siiw0015#23 % 8180;
     #siiw0007#8 = #siiw0015#23 % 16180;

     |PINPA #0#siiw0007;
     |PINDA #0#siiw0007;
     |LEETECLA aAlfa;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;

     |FOCOTECLADO nGrid9;
|FPRC;

|PROCESO siim0009B;
     |BORRA 020#siim0009.a;
     |LIBERA #siim0009;
|FPRC;

|DEFBUCLE siim0009B;
     #siim0009#1;
     ;
     #siim0002#0, #siim0002#1, 000001;
     #siim0002#0, #siim0002#1, 999999;
     be;
     NULL, siim0009B;
|FIN;

|PROGRAMA;
     |SI PARAMETRO = "REFRESCA";
         |ACABA;
     |FINSI;

     |SI PARAMETRO = "BORRAR";
         |BUCLE siim0009B;

         |ACABA;
     |FINSI;

     aAlfa = "Bandeja bienes de inversi¢n ";
     aAlfa = aAlfa  + "(Empresa " +(("00000" + #siim0001#0) % -105);
     aAlfa = aAlfa  + " " + #siim0001#1;  |QBF aAlfa;
     aAlfa = aAlfa  + " Periodo " + #siim0002#4 + "/" + (("00" + #siim0002#5) % -102);
     aAlfa = aAlfa  + " Lote " + (("0000000000" + #siim0002#1) % -110) + ")";
     |VENTANA 0501, 3799, -1, 16, aAlfa;
     n0Vent = FSalida;

     |PINPA #0#siiw0015;  nPanta = FSalida;
     |DDEFECTO #siiw0015;
     |HAZ PintaCampos;

     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}Alta ", 1664, 1674, BtnAlt;
     enBtnAlt = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Modificar ", 1676, 1686, BtnMod;
     enBtnMod = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{199}}Borrar", 1688, 1698, BtnBaj;
     enBtnBaj = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Estado del env¡o", 1602, 1620, BtnEnv;
     enBtnEnv = FSalida;

     |ESTADO_CONTROL enBtnMod, "DISABLE";
     |ESTADO_CONTROL enBtnBaj, "DISABLE";

     nDReg = 1;
     nHReg = 999999;
     |SI enReg ! 0;
         nDReg = enReg;
         nHReg = enReg;
     |FINSI;

     nRango   = 2 + 4 + 8 + 16 + 32 + 128;
     |LINEAL_SIMPLE #1#siim0009, nRango, 0502, 1598, Baja9, Alta9, Evnt9;
     nGrid9 = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::{{001}}Salir," + nGrid9;
          |LEETECLA aTecla;
          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
          |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FINVENTANA n0Vent;

     |PULSATECLA;
|FPRO;

|PROCESO PaseBienes;
     |SI PARAMETRO = "REFRESCA";
         |ACABA;
     |FINSI;

     |SI PARAMETRO = "BORRAR";
         |ACABA;
     |FINSI;

     |SI eaBtnCon ! "S";
         |ACABA;
     |FINSI;

     |SI eaPaq ! "BASICA";
         |ACABA;
     |FINSI;

     |SI #siim0002#2 ! "31";
         |ACABA;
     |FINSI;

     |SUB_EJECUTA "siiz0032";

     |SELECT #1#siim0009;

     #siim0009#0 = enEmp;
     #siim0009#1 = enLot;
     #siim0009#2 = 1;
     |LEE 000#siim0009.];
     |SI FSalida ! 0;  |O #siim0009#0 ! enEmp;
                       |O #siim0009#1 ! enLot;
         aAlfa = "0000No hay bienes de inversi¢n a regularizar ";
         aAlfa = aAlfa + "o no est  calculada la prorrata. Lote manual";
         |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO T80;  |TIPO 80;
     |HAZ CargaColores;

     |ABRET #siim0009;

     |ABRE #siim0001;
     |SELECT #1#siim0001;
     #siim0001#0 = enEmp;
     |LEE 000#siim0001.=;
     |CIERRA #siim0001;

     |ABRE #siim0002;
     #siim0002#0 = enEmp;
     #siim0002#1 = enLot;
     |LEE 000#siim0002.=;
     |CIERRA #siim0002;

     |HAZ PaseBienes;

     aQQ     = "|NC";
     aAlfa   = #siiw0015#aQQ#;
     enTCampo = aAlfa;
     enTCampo = enTCampo - 1;
|FPRC;

|PROCESO siim0009C;
                              #siim0002#6  = #siim0002#6  + 1;
     |SI #siim0009#21 = "P";  #siim0002#7  = #siim0002#7  + 1;  |FINSI;
     |SI #siim0009#21 = "I";  #siim0002#7  = #siim0002#7  + 1;  |FINSI;
     |SI #siim0009#21 = "I";  #siim0002#10 = #siim0002#10 + 1;  |FINSI;
     |SI #siim0009#21 = "C";  #siim0002#8  = #siim0002#8  + 1;  |FINSI;
     |SI #siim0009#21 = "A";  #siim0002#9  = #siim0002#9  + 1;  |FINSI;

     |SI #siim0002#15 < #siim0009#19;
         #siim0002#15 = #siim0009#19;
     |FINSI;

     |SI #siim0002#15 = #siim0009#19;
         |SI #siim0002#16 < #siim0009#20;
             #siim0002#16 = #siim0009#20;
         |FINSI;
     |FINSI;

     #siim0009#25 = "";
     |SI #siim0009#21 = "C";
         #siim0009#25 = eaVerde;
     |FINSI;

     |SI #siim0009#21 = "A";
         #siim0009#25 = eaAzul;
     |FINSI;

     |SI #siim0009#21 = "I";
         #siim0009#25 = eaRojo;
     |FINSI;

     |GRABA 020#siim0009.a;
     |LIBERA #siim0009;
|FPRC;

|DEFBUCLE siim0009C;
     #siim0009#1;
     ;
     #siim0002#0, #siim0002#1, 000001;
     #siim0002#0, #siim0002#1, 999999;
     ;
     NULL, siim0009C;
|FIN;

|PROCESO T90;  |TIPO 90;
     |ABRE #siim0002;
     #siim0002#0 = enEmp;
     #siim0002#1 = enLot;
     |LEE 101#siim0002.=;
     |SI FSalida = 0;
         #siim0002#6  = 0;
         #siim0002#7  = 0;
         #siim0002#8  = 0;
         #siim0002#9  = 0;
         #siim0002#10 = 0;
         #siim0002#15 = "01.01.0000";
         #siim0002#16 = "";
         #siim0002#17 = 0;
         #siim0002#18 = 0;
         #siim0002#19 = 0;
         #siim0002#20 = 0;

         |BUCLE siim0009C;

         #siim0002#11 = "";

         nReg = #siim0002#8 + #siim0002#9;
         |SI nReg = #siim0002#6;  |Y nReg ! 0;
             #siim0002#11 = eaVerde;
         |FINSI;

         |SI #siim0002#10 ! 0;
             #siim0002#11 = eaRojo;
         |FINSI;

         #siim0002#13 = #siim0001#1;
         #siim0002#14 = #siim0001#2;

         |GRABA 020#siim0002.a;
         |LIBERA #siim0002;
     |FINSI;
     |CIERRA #siim0002;

     |CIERRAT #siim0009;
|FPRC;
