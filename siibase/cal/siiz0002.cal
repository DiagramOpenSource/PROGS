|FICHEROS;
     siim0001;
     siim0002;
     siim0003;
     siim0007;

     siiw0002;
     siiw0006;
     siiw0007;
     siiw0019;
     siiw0100;
     siiw0101;
|FIN;

|VARIABLES;
     n0Vent              = 0;
     n1Vent              = 0;
     nGrid3              = -1;
     nGrid7              = -1;
     nRango              = 0;
     nPanta              = 0;
     nPantaw6            = 0;
     nCampo              = 0;
     nIdSalir            = 0;
     nReg                = 0;
     nDReg               = 0;
     nHReg               = 0;
     nCmp1               = 0;
     nCmp2               = 0;
     nOk                 = 0;
     nConsul             = 0;

     aEsc1               = "";
     aEsc2               = "";
     aRetu               = "";
     aTecla              = "";
     aAlfa               = "";
     aQQ                 = "";

     fFecha              = @;

     {2}nNivel           = 0;

     {-1}aVent           = "";
         aVent1          = 0;
         aVent2          = 0;
         aVent3          = 0;
         aVent4          = 0;
         aVent5          = "";

     &enEmp              = 0;
     &enEje              = 0;
     &enPer              = 0;
     &enLot              = 0;
     &enReg              = 0;
     &eaTip              = "";
     &eaPaq              = "";
     &enTCampo           = 0;
     &enTCampoP          = 0;
     &enBtnAlt           = -1;
     &enBtnMod           = -1;
     &enBtnBaj           = -1;
     &enBtnEnv           = -1;
     &enBtnCtb           = -1;
     &enBtnIde           = -1;
     &enBtnRec           = -1;
     &enBtnInm           = -1;
     &enBtnEnt           = -1;
     &enBtnBus           = -1;
     &enBtnMas           = -1;
     &enBtnMen           = -1;
     &eaBtnLim           = "";
     &eaH60              = "";

     &eaAzul             = "";
     &eaVerde            = "";
     &eaRojo             = "";
     &eaRosa             = "";
     &eaMarron           = "";
|FIN;

|PROCESO PideFactura;
     nOk = 0;

     |VENTANA 0501, 1265, -1, 16, "Identificar factura";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |C_M #siiw0006#1, 1, "N";
     |C_V #siiw0006#1, 1, "N";

     |DDEFECTO #siiw0006;
     |PINPA #0#siiw0006; nPantaw6 = FSalida;
     |PINDA #0#siiw0006;

     |CONTROL_SIMPLE nPantaw6, "LABEL,{{7}}       ", 0501, 0520, NULL;
     |CONTROL_SIMPLE nPantaw6, "BOTON,{{155}}", 0560, 0765, Busqueda;

     |ENDATOS #1#siiw0006;
     |SI FSalida = 0;
         nOk = 1;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;
|FPRC;

|PROCESO Baja7;
     #siim0007#0 = #siiw0002#0;
     #siim0007#1 = #siiw0002#1;
     #siim0007#2 = #siiw0002#2;
     #siim0007#3 = "";
|FPRC;

|PROCESO Alta7;
     #siim0007#0 = #siiw0002#0;
     #siim0007#1 = #siiw0002#1;
     #siim0007#2 = #siiw0002#2;
     #siim0007#3 = "z" * 60;
|FPRC;

|PROCESO Evnt7;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#siim0007.=;
     |FINSI;
|FPRC;

|PROCESO Baja3;
     #siim0003#0 = #siim0002#0;
     #siim0003#1 = #siim0002#1;
     #siim0003#2 = nDReg;
|FPRC;

|PROCESO Alta3;
     #siim0003#0 = #siim0002#0;
     #siim0003#1 = #siim0002#1;
     #siim0003#2 = nHReg;
|FPRC;

|PROCESO PintaCampos;
     |PARA nCampo = 0;  |SI nCampo [ enTCampoP;  |HACIENDO nCampo = nCampo + 1;
           |C_V #siiw0002#nCampo#, 0, aAlfa;
           |SI aAlfa = "S";
               |PINTA #siiw0002#nCampo#;
           |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Desglose2a3;
     #siiw0002#212 = #siim0003#13 % 101;
     #siiw0002#213 = #siim0003#14 % 102;
     #siiw0002#214 = #siim0003#13 % 201;
     #siiw0002#215 = #siim0003#14 % 302;
     #siiw0002#216 = #siim0003#13 % 301;
     #siiw0002#217 = #siim0003#14 % 502;
     #siiw0002#218 = #siim0003#13 % 401;
     #siiw0002#219 = #siim0003#14 % 702;
     #siiw0002#220 = #siim0003#13 % 501;
     #siiw0002#221 = #siim0003#14 % 902;
     #siiw0002#222 = #siim0003#13 % 601;
     #siiw0002#223 = #siim0003#14 % 1102;
|FPRC;

|PROCESO Desglose3a2;
     aAlfa = "";
     |PARA nCmp1 = 212;  |SI nCmp1 [ 222;  |HACIENDO nCmp1 = nCmp1 + 2;
           aAlfa = aAlfa + #siiw0002#nCmp1#;
     |SIGUE;

     #siim0003#13 = aAlfa;

     aAlfa = "";
     |PARA nCmp1 = 213;  |SI nCmp1 [ 223;  |HACIENDO nCmp1 = nCmp1 + 2;
           aAlfa = aAlfa + #siiw0002#nCmp1#;
     |SIGUE;

     #siim0003#14 = aAlfa;
|FPRC;

|PROCESO Evnt3;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#siim0003.=;

         |SI eaBtnLim = "S";
             |ESTADO_CONTROL enBtnCtb, "DISABLE";
             |ESTADO_CONTROL enBtnAlt, "DISABLE";
         |FINSI;

         |ESTADO_CONTROL enBtnEnv, "DISABLE";

         |SI enBtnMod ! -1;  |ESTADO_CONTROL enBtnMod, "ENABLE";  |FINSI;
         |SI enBtnBaj ! -1;  |ESTADO_CONTROL enBtnBaj, "ENABLE";  |FINSI;

         |SI #siim0003#103 = "C"; |O #siim0003#103 = "A";
             |SI enBtnMod ! -1;  |ESTADO_CONTROL enBtnMod, "DISABLE";  |FINSI;
             |SI enBtnBaj ! -1;  |ESTADO_CONTROL enBtnBaj, "DISABLE";  |FINSI;
         |FINSI;

         |SI #siim0003#103 ! "P";
             |ESTADO_CONTROL enBtnEnv, "ENABLE";
         |FINSI;

         |PARA nCampo = 0;  |SI nCampo [ enTCampo;  |HACIENDO nCampo = nCampo + 1;
               #siiw0002#nCampo# = #siim0003#nCampo#;
         |SIGUE;

         |HAZ Desglose2a3;

         nConsul = 1;
         |HAZ ControlaBtnw2;

         |HAZ PintaCampos;
     |FINSI;
|FPRC;

|PROCESO QuitaBotonSalir;
     |ESTADO_CONTROL enBtnAlt, "DISABLE";
     |ESTADO_CONTROL enBtnEnv, "DISABLE";

     |SI enBtnMod ! -1;  |ESTADO_CONTROL enBtnMod, "DISABLE";  |FINSI;
     |SI enBtnBaj ! -1;  |ESTADO_CONTROL enBtnBaj, "DISABLE";  |FINSI;
     |SI enBtnCtb ! -1;  |ESTADO_CONTROL enBtnCtb, "DISABLE";  |FINSI;

     |ESTADO_CONTROL nGrid3,   "DISABLE";

     nIdSalir = 0;
     InNivel   = nNivel1<-;
     nNivel1   = 0;
     |ESPECIFICA "IDBOTONTECLA", nNivel;

     nIdSalir = nNivel2;
     |ESTADO_CONTROL nIdSalir, "HIDE";
|FPRC;

|PROCESO PonBotonSalir;
     |ESTADO_CONTROL nGrid3,   "ENABLE";
     |ESTADO_CONTROL nIdSalir, "SHOW";
     |ESTADO_CONTROL enBtnAlt, "ENABLE";
     |ESTADO_CONTROL enBtnEnv, "DISABLE";

     |SI enBtnMod ! -1;  |ESTADO_CONTROL enBtnMod, "DISABLE";  |FINSI;
     |SI enBtnBaj ! -1;  |ESTADO_CONTROL enBtnBaj, "DISABLE";  |FINSI;
     |SI enBtnCtb ! -1;  |ESTADO_CONTROL enBtnCtb, "ENABLE";   |FINSI;
|FPRC;

|PROCESO EntraDatos;
     |DDEFECTO #siiw0002;

     |PARA nCampo = 0;  |SI nCampo [ enTCampo;  |HACIENDO nCampo = nCampo + 1;
           #siiw0002#nCampo# = #siim0003#nCampo#;
     |SIGUE;

     |C_M #siiw0002#15, 1, "S";
     |C_M #siiw0002#19, 1, "S";

     |SI #siim0002#2 ! "11";
         |C_M #siiw0002#15, 1, "N";
         |C_M #siiw0002#19, 1, "N";
     |FINSI;

     |HAZ Desglose2a3;

     |HAZ PintaCampos;

     nConsul = 0;
     |HAZ ControlaBtnw2;

     |ESTADO_CONTROL enBtnBus, "ENABLE";
     |ESTADO_CONTROL enBtnMas, "ENABLE";
     |ESTADO_CONTROL enBtnMen, "ENABLE";

     |PULSATECLA;
     |ENDATOS #1#siiw0002;

     |ESTADO_CONTROL enBtnBus, "DISABLE";
     |ESTADO_CONTROL enBtnMas, "DISABLE";
     |ESTADO_CONTROL enBtnMen, "DISABLE";
|FPRC;

|PROCESO BtnAlt;
     |HAZ QuitaBotonSalir;

     #siim0003#0  = #siim0002#0;
     #siim0003#1  = #siim0002#1;
     #siim0003#2  = 999999;
     |LEE 000#siim0003.];
     |SI FSalida = 0;
         |LEE 000#siim0003.a;
     |SINO;
         |LEE 000#siim0003.u;
     |FINSI;

     nReg = 1;
     |SI FSalida = 0;  |Y #siim0003#0  = #siim0002#0;
                       |Y #siim0003#1  = #siim0002#1;
         nReg = #siim0003#2 + 1;
     |FINSI;

     |DDEFECTO #siim0003;
     #siim0003#0  = #siim0002#0;
     #siim0003#1  = #siim0002#1;
     #siim0003#2  = nReg;
     #siim0003#3  = #siim0002#4;
     #siim0003#4  = #siim0002#5;
     |GRABA 020#siim0003.n;
     |LIBERA #siim0003;

     |SI #siim0002#2 ! "11";
         |HAZ PideFactura;
         |SI nOk = 0;
             #siim0003#0  = #siim0002#0;
             #siim0003#1  = #siim0002#1;
             #siim0003#2  = nReg;
             |LEE 101#siim0003.=;

             |BORRA 020#siim0003.a;

             |HAZ PonBotonSalir;
             |FOCOTECLADO nGrid3;
             |ACABA;
         |FINSI;

         |C_M #siiw0002#15, 1, "N";
         |C_M #siiw0002#19, 1, "N";
     |FINSI;

     FSalida = 0;
     |SI #siim0002#2 ! "15";
         |HAZ EntraDatos;
     |FINSI;

     |SI FSalida = 0;
         #siim0003#0  = #siim0002#0;
         #siim0003#1  = #siim0002#1;
         #siim0003#2  = nReg;
         |LEE 101#siim0003.=;

         |PARA nCampo = 3;  |SI nCampo [ enTCampo;  |HACIENDO nCampo = nCampo + 1;
               #siim0003#nCampo# = #siiw0002#nCampo#;
         |SIGUE;

         |HAZ Desglose3a2;

         #siim0003#111 = "N";
         |SI #siim0002#2 = "15";
             #siim0003#111 = "S";
         |FINSI;

         |GRABA 020#siim0003.a;
     |SINO;
         #siim0003#0  = #siim0002#0;
         #siim0003#1  = #siim0002#1;
         #siim0003#2  = nReg;
         |LEE 101#siim0003.=;

         |BORRA 020#siim0003.a;
     |FINSI;

     |LIBERA #siim0003;

     |HAZ PonBotonSalir;

     |REFRESCACONTROL nGrid3;
     |FOCOTECLADO nGrid3;
|FPRC;

|PROCESO BtnMod;
     |LEE 101#siim0003.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |HAZ QuitaBotonSalir;

     |HAZ EntraDatos;
     |SI FSalida = 0;
         |PARA nCampo = 3;  |SI nCampo [ enTCampo;  |HACIENDO nCampo = nCampo + 1;
               #siim0003#nCampo# = #siiw0002#nCampo#;
         |SIGUE;

         |HAZ Desglose3a2;

         #siim0003#111 = "N";
         |SI #siim0002#2 = "15";
             #siim0003#111 = "S";
         |FINSI;

         |GRABA 020#siim0003.a;
     |FINSI;

     |LIBERA #siim0003;

     |HAZ PonBotonSalir;

     |REFRESCACONTROL nGrid3;
     |FOCOTECLADO nGrid3;
|FPRC;

|PROCESO siim0007B;
     |BORRA 020#siim0007.a;
     |LIBERA #siim0007;
|FPRC;

|DEFBUCLE siim0007B;
     #siim0007#1;
     ;
     #siim0003#0, #siim0003#1, #siim0003#2, "";
     #siim0003#0, #siim0003#1, #siim0003#2, eaH60;
     be;
     NULL, siim0007B;
|FIN;

|PROCESO BtnBaj;
     |LEE 101#siim0003.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |CONFI "0000 Confirma la baja del registro seleccionado";
     |SI FSalida ! 0;
         |FOCOTECLADO nGrid3;
         |ACABA;
     |FINSI;

     |BORRA 020#siim0003.a;
     |LIBERA #siim0003;

     |BUCLE siim0007B;

     |ESTADO_CONTROL enBtnAlt, "ENABLE";

     |SI enBtnMod ! -1;  |ESTADO_CONTROL enBtnMod, "DISABLE";  |FINSI;
     |SI enBtnBaj ! -1;  |ESTADO_CONTROL enBtnBaj, "DISABLE";  |FINSI;
     |SI enBtnCtb ! -1;  |ESTADO_CONTROL enBtnCtb, "ENABLE";   |FINSI;

     |REFRESCACONTROL nGrid3;
     |FOCOTECLADO nGrid3;
|FPRC;

|PROCESO BtnIde;
     |VENTANA 0501, 2080, -1, 16, "Informe de las facturas";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |SI nConsul = 0;
         nRango   = 1 + 2 + 4 + 8 + 16 + 32 + 128;
     |SINO;
         nRango   = 2 + 4 + 8 + 16 + 32 + 128;
     |FINSI;

     |LINEAL_SIMPLE #1#siim0007, nRango, 0502, 1879, Baja7, Alta7, Evnt7;
     nGrid7 = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::{{001}}Salir," + nGrid7;
          |LEETECLA aTecla;
          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
          |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;
|FPRC;

|PROCESO BtnRec;
     |VENTANA 0501, 1765, -1, 16, "Datos factura rectificativa";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     #siiw0100#0 = #siiw0002#65;
     #siiw0100#1 = #siiw0002#66;
     #siiw0100#2 = #siiw0002#67;
     #siiw0100#3 = #siiw0002#68;

     |SI #siiw0100#0 = " ";
         #siiw0100#0 = "I";
     |FINSI;

     |PINPA #0#siiw0100;
     |PINDA #0#siiw0100;
     |SI nConsul = 0;
         |ENDATOS #1#siiw0100;
         |SI FSalida = 0;
             #siiw0002#65 = #siiw0100#0;
             #siiw0002#66 = #siiw0100#1;
             #siiw0002#67 = #siiw0100#2;
             #siiw0002#68 = #siiw0100#3;
         |FINSI;
     |SINO;
         |PAUSA;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;
|FPRC;

|PROCESO BtnInm;
     |VENTANA 0501, 2260, -1, 16, "Datos inmuebles";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PARA nCmp1 = 0;  |SI nCmp1 [ 29;  |HACIENDO nCmp1 = nCmp1 + 1;
           nCmp2 = nCmp1 + 69;
           #siiw0101#nCmp1# = #siiw0002#nCmp2#;
     |SIGUE;

     |PINPA #0#siiw0101;
     |PINDA #0#siiw0101;
     |SI nConsul = 0;
         |ENDATOS #1#siiw0101;
         |SI FSalida = 0;
            |PARA nCmp1 = 0;  |SI nCmp1 [ 29;  |HACIENDO nCmp1 = nCmp1 + 1;
                  nCmp2 = nCmp1 + 69;
                  #siiw0002#nCmp2# = #siiw0101#nCmp1#;
            |SIGUE;
         |FINSI;
     |SINO;
         |PAUSA;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;
|FPRC;

|PROCESO BtnEnt;
     |VENTANA 0501, 1170, -1, 16, "Entidad Sucedida";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     #siiw0019#0 = #siiw0002#116;
     #siiw0019#1 = #siiw0002#117;

     |PINPA #0#siiw0019;
     |PINDA #0#siiw0019;
     |SI nConsul = 0;
         |ENDATOS #1#siiw0019;
         |SI FSalida = 0;
             #siiw0002#116 = #siiw0019#0;
             #siiw0002#117 = #siiw0019#1;
         |FINSI;
     |SINO;
         |PAUSA;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;
|FPRC;

|PROCESO BtnEnv;
     |VENTANA 0501, 1599, -1, 16, "Datos del env¡o";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     #siiw0007#0 = #siiw0002#100;
     #siiw0007#1 = #siiw0002#101;
     #siiw0007#2 = #siiw0002#102;

     |SI #siiw0002#103 = "C";  #siiw0007#3 = "Correcto";            |FINSI;
     |SI #siiw0002#103 = "A";  #siiw0007#3 = "AceptadaConErrores";  |FINSI;
     |SI #siiw0002#103 = "I";  #siiw0007#3 = "Incorrecto";          |FINSI;

     #siiw0007#4 = #siiw0002#106;
     #siiw0007#5 = #siiw0002#104;
     #siiw0007#6 = #siiw0002#105 % 180;
     #siiw0007#7 = #siiw0002#105 % 8180;
     #siiw0007#8 = #siiw0002#105 % 16180;

     |PINPA #0#siiw0007;
     |PINDA #0#siiw0007;
     |LEETECLA aAlfa;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;
|FPRC;

|PROCESO BtnCtb;
     |VENTANA 0501, 0501, -1, 16, "";
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     enEmp = #siim0002#0;
     enLot = #siim0002#1;
     eaTip = #siim0002#2;
     enEje = #siim0002#4;
     enPer = #siim0002#5;

     |SI eaPaq = "BASICA";
         |SUB_EJECUTA "siiz0030;UNAUNA";
     |FINSI;

     |SI eaPaq = "AGICLI";
         |SUB_EJECUTA "siiz0031;UNAUNA";
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;

     |REFRESCACONTROL nGrid3;
     |FOCOTECLADO nGrid3;
|FPRC;

|PROCESO siim0003B;
     |BUCLE siim0007B;

     |BORRA 020#siim0003.a;
     |LIBERA #siim0003;
|FPRC;

|DEFBUCLE siim0003B;
     #siim0003#1;
     ;
     #siim0002#0, #siim0002#1, 000001;
     #siim0002#0, #siim0002#1, 999999;
     be;
     NULL, siim0003B;
|FIN;

|PROGRAMA;
     |SI PARAMETRO = "REFRESCA";
         |ACABA;
     |FINSI;

     |SI PARAMETRO = "BORRAR";
         |BUCLE siim0003B;

         |ACABA;
     |FINSI;

     aAlfa = "";

     |SI #siim0002#2 = "11";
         aAlfa = "Bandeja ALTAS facturas expedidas ";
     |FINSI;

     |SI #siim0002#2 = "12";
         aAlfa = "Bandeja MODIFICACIONES facturas expedidas ";
     |FINSI;

     |SI #siim0002#2 = "15";
         aAlfa = "Bandeja BAJAS facturas expedidas ";
     |FINSI;

     aAlfa = aAlfa  + "(Empresa " + (("00000" + #siim0001#0) % -105);
     aAlfa = aAlfa  + " " + #siim0001#1;  |QBF aAlfa;
     aAlfa = aAlfa  + " Periodo " + #siim0002#4 + "/" + (("00" + #siim0002#5) % -102);
     aAlfa = aAlfa  + " Lote " + (("0000000000" + #siim0002#1) % -110) + ")";
     |VENTANA 0501, 3799, -1, 16, aAlfa;
     n0Vent = FSalida;

     |PINPA #0#siiw0002;  nPanta = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}Alta ", 1264, 1274, BtnAlt;
     enBtnAlt = FSalida;

     |SI #siim0002#2 ! "15";
         |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Modificar ", 1276, 1286, BtnMod;
         enBtnMod = FSalida;
     |FINSI;

     |CONTROL_SIMPLE nPanta, "BOTON,{{199}}Borrar", 1288, 1298, BtnBaj;
     enBtnBaj = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Estado del env¡o", 1202, 1220, BtnEnv;
     enBtnEnv = FSalida;

     |SI eaPaq ! ""; |Y #siim0002#2 = "11";
         |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Recogida selectiva", 1222, 1240, BtnCtb;
         enBtnCtb = FSalida;
     |FINSI;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}", 3272, 3273, BtnIde;
     enBtnIde = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}", 3372, 3373, BtnRec;
     enBtnRec = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}", 3472, 3473, BtnInm;
     enBtnInm = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}", 3572, 3573, BtnEnt;
     enBtnEnt = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{155}}", 3643, 3646, BtnBus;
     enBtnBus = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON, + ", 3648, 3651, BtnMas;
     enBtnMas = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON, - ", 3653, 3656, BtnMen;
     enBtnMen = FSalida;

     |SI enBtnMod ! -1;  |ESTADO_CONTROL enBtnMod, "DISABLE";  |FINSI;
     |SI enBtnBaj ! -1;  |ESTADO_CONTROL enBtnBaj, "DISABLE";  |FINSI;

     |ESTADO_CONTROL enBtnEnv, "DISABLE";
     |ESTADO_CONTROL enBtnIde, "DISABLE";
     |ESTADO_CONTROL enBtnRec, "DISABLE";
     |ESTADO_CONTROL enBtnInm, "DISABLE";
     |ESTADO_CONTROL enBtnEnt, "DISABLE";
     |ESTADO_CONTROL enBtnBus, "DISABLE";
     |ESTADO_CONTROL enBtnMas, "DISABLE";
     |ESTADO_CONTROL enBtnMen, "DISABLE";

     |DDEFECTO #siiw0002;
     |HAZ PintaCampos;

     nDReg = 1;
     nHReg = 999999;
     |SI enReg ! 0;
         nDReg = enReg;
         nHReg = enReg;
     |FINSI;

     nRango   = 2 + 4 + 8 + 16 + 32 + 128;
     |LINEAL_SIMPLE #1#siim0003, nRango, 0502, 1198, Baja3, Alta3, Evnt3;
     nGrid3 = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::{{001}}Salir," + nGrid3;
          |LEETECLA aTecla;
          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
          |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FINVENTANA n0Vent;

     |PULSATECLA;
|FPRO;

|PROCESO T80;  |TIPO 80;
     |HAZ CargaColores;

     |ABRET #siim0003;
     |ABRET #siim0007;

     |ABRE #siim0001;
     |SELECT #1#siim0001;
     #siim0001#0 = enEmp;
     |LEE 000#siim0001.=;
     |CIERRA #siim0001;

     |ABRE #siim0002;
     #siim0002#0 = enEmp;
     #siim0002#1 = enLot;
     |LEE 000#siim0002.=;
     |CIERRA #siim0002;

     eaH60    = "z" * 60;

     aQQ      = "|NC";
     aAlfa    = #siim0003#aQQ#;
     enTCampo = aAlfa;
     enTCampo = enTCampo - 1;

     aQQ       = "|NC";
     aAlfa     = #siiw0002#aQQ#;
     enTCampoP = aAlfa;
     enTCampoP = enTCampoP - 1;
|FPRC;

|PROCESO siim0003C;
                               #siim0002#6  = #siim0002#6  + 1;
     |SI #siim0003#103 = "P";  #siim0002#7  = #siim0002#7  + 1;  |FINSI;
     |SI #siim0003#103 = "I";  #siim0002#7  = #siim0002#7  + 1;  |FINSI;
     |SI #siim0003#103 = "I";  #siim0002#10 = #siim0002#10 + 1;  |FINSI;
     |SI #siim0003#103 = "C";  #siim0002#8  = #siim0002#8  + 1;  |FINSI;
     |SI #siim0003#103 = "A";  #siim0002#9  = #siim0002#9  + 1;  |FINSI;

     |SI #siim0002#15 < #siim0003#101;
         #siim0002#15 = #siim0003#101;
     |FINSI;

     |SI #siim0002#15 = #siim0003#101;
         |SI #siim0002#16 < #siim0003#102;
             #siim0002#16 = #siim0003#102;
         |FINSI;
     |FINSI;

     #siim0002#17 = #siim0002#17 + #siim0003#23 + #siim0003#29 + #siim0003#35;
     #siim0002#17 = #siim0002#17 + #siim0003#41 + #siim0003#47 + #siim0003#53;

     #siim0002#18 = #siim0002#18 + #siim0003#25 + #siim0003#31 + #siim0003#37;
     #siim0002#18 = #siim0002#18 + #siim0003#43 + #siim0003#49 + #siim0003#55;

     #siim0002#19 = #siim0002#19 + #siim0003#27 + #siim0003#33 + #siim0003#39;
     #siim0002#19 = #siim0002#19 + #siim0003#45 + #siim0003#51 + #siim0003#57;

     #siim0002#20 = #siim0002#20 + #siim0003#59;

     #siim0003#107 = "";
     |SI #siim0003#103 = "C";
         #siim0003#107 = eaVerde;
     |FINSI;

     |SI #siim0003#103 = "A";
         #siim0003#107 = eaAzul;
     |FINSI;

     |SI #siim0003#103 = "I";
         #siim0003#107 = eaRojo;
     |FINSI;

     #siim0003#111 = "N";
     |SI #siim0002#2 = "15";
         #siim0003#111 = "S";
     |FINSI;

     |GRABA 020#siim0003.a;
     |LIBERA #siim0003;
|FPRC;

|DEFBUCLE siim0003C;
     #siim0003#1;
     ;
     #siim0002#0, #siim0002#1, 000001;
     #siim0002#0, #siim0002#1, 999999;
     be;
     NULL, siim0003C;
|FIN;

|PROCESO T90;  |TIPO 90;
     |ABRE #siim0002;
     #siim0002#0 = enEmp;
     #siim0002#1 = enLot;
     |LEE 101#siim0002.=;
     |SI FSalida = 0;
         #siim0002#6  = 0;
         #siim0002#7  = 0;
         #siim0002#8  = 0;
         #siim0002#9  = 0;
         #siim0002#10 = 0;
         #siim0002#15 = "01.01.0000";
         #siim0002#16 = "";
         #siim0002#17 = 0;
         #siim0002#18 = 0;
         #siim0002#19 = 0;
         #siim0002#20 = 0;

         |BUCLE siim0003C;

         #siim0002#11 = "";

         nReg = #siim0002#8 + #siim0002#9;
         |SI nReg = #siim0002#6;  |Y nReg ! 0;
             #siim0002#11 = eaVerde;
         |FINSI;

         |SI #siim0002#10 ! 0;
             #siim0002#11 = eaRojo;
         |FINSI;

         #siim0002#13 = #siim0001#1;
         #siim0002#14 = #siim0001#2;

         |GRABA 020#siim0002.a;
         |LIBERA #siim0002;
     |FINSI;
     |CIERRA #siim0002;

     |CIERRAT #siim0003;
     |CIERRAT #siim0007;
|FPRC;
