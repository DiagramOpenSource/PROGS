|FICHEROS;
     preinf02  #0;

     premvtol  #11;   || movtos. lins.
     pretopes  #12;   || ajustes de marcado

     prequiva  #20;   || tarjetas
     pretarea  #21;   || incidencias
     labempre  #23;   || empresas
     labtraba  #24;   || trabajadores
     nomtrcal  #25;   || calendario laboral

     pretmp01  #50;   || def de impresion del detalle
     pretopel;
     precontr;       || control de la aplicacion
     pretmp05  #51;   || def para pase a nominas

     mod00242@ #242;      || tipo de horas
     mod00243@ #243;      || horas trabajadas cabs.
     mod00244@ #244;      || horas trabajadas lins.
     mod00249@ #249;      || tipo de horas por trabajador
|FIN;

|VARIABLES;
     alfa1 = "";
     alfa2 = "";
     alfa3 = "";
     alfa4 = "";
     nume1 = 0;
     nume2 = 0;
     nume3 = 0;
     nume4 = 0;
     para1 = 0;
     para2 = 0;
     para3 = 0;
     para4 = 0;
     fech1 = @;
     fech2 = @;
     fech3 = @;
     fech4 = @;
     elsw1 = 0;
     elsw2 = 0;
     elsw3 = 0;
     elsw4 = 0;
     FEstado = 0;
     seleccio = 0;
     modo = 0;
     sw_rup = 0;

     p_mes = "";
     dia_ant = 0;
     bar_ant = 0;
     hor_ant = 0;
     min_ant = 0;
     lin_ant = 0;
     nhor = 0;
     nmin = 0;
     &shor = 0;
     &sext = 0;
     &dhor = 0;
     &dext = 0;
     &ghor = 0;
     &gext = 0;
     &error = "";
     &swlin = 0;
     swentra = 0;
     dhora = 0;
     hhora = 0;
     chora = 0;
     dminu = 0;
     hminu = 0;
     cminu = 0;
     desde = 0;
     hasta = 0;
     rajus = 0;
     hajus = 0;
     majus = 0;
     lectu = 0;
     rhora = 0;
     dlect = 0;
     hlect = 0;
     aju_cod = 0;
     aju_dia = @;
     elmes = 0;
     eldia = 0;
     fehas = @;
     emp_ant = 0;
     tra_ant = 0;

     nCodAjus = 0;
     swAjuste = 0;
     aParam = "";
     nPaso = 0;
     aAlfa = "";
     nError = 0;
     aError = "";
     aAnyo = "";
     {-1}menu = "";
     menu1 = "2400";
     menu2 = "1";
     menu3 = "Elegir Opcion:";
     menu4 = "HCE";
     menu5 = "Informe Horas";
     menu6 = "Informe Comprobacion";
     menu7 = "Exportar Horas";
     nOpcion = 0;
     aBase = "";

     aPathFich = "";
     aPathDef = "";
     aFabre = "";
     swentra2 = 0;

     &swParcial = 0;
     nEmp = 0;
     aEmp = "";
     nTra = 0;
     aTra = "";
     aTrab = "";
     &aTrabAnt = "";
     aDia = "";
     aMes = "";
     aAny = "";
     aFecha = "";
     &aFechaAnt = "";
     &nHorasFec = 0;
     &nHorasTrab = 0;
     &nHorasTot = 0;
     nTipo = 0;
     aTipo = "";
     &aTipoAnt = "";
     &swCabecera = 0;
     nContador = 0;

     &nAcumDia = 0;
|FIN;
____________________________________/
|PROCESO defecto; |TIPO 7; ||calcula el a¤o por defecto segun fecha sistema
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % -104;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO mirames; |TIPO 7; ||CALCULA EL MES POR DEFECTO SEGUN FECHA SISTEMA
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % 402;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO pintames; |TIPO 0;      || PINTA EL MES LITERAL
|SI #0Campo =  1;  p_mes = "ENERO     ";  |FINSI;
|SI #0Campo =  2;  p_mes = "FEBRERO   ";  |FINSI;
|SI #0Campo =  3;  p_mes = "MARZO     ";  |FINSI;
|SI #0Campo =  4;  p_mes = "ABRIL     ";  |FINSI;
|SI #0Campo =  5;  p_mes = "MAYO      ";  |FINSI;
|SI #0Campo =  6;  p_mes = "JUNIO     ";  |FINSI;
|SI #0Campo =  7;  p_mes = "JULIO     ";  |FINSI;
|SI #0Campo =  8;  p_mes = "AGOSTO    ";  |FINSI;
|SI #0Campo =  9;  p_mes = "SEPTIEMBRE";  |FINSI;
|SI #0Campo = 10;  p_mes = "OCTUBRE   ";  |FINSI;
|SI #0Campo = 11;  p_mes = "NOVIEMBRE ";  |FINSI;
|SI #0Campo = 12;  p_mes = "DICIEMBRE ";  |FINSI;
|PINTA 1136, p_mes;
|FPRC;

|PROCESO mayus; |TIPO 0;
    #0Campo = #0Campo > #0Campo;
|PINTA #0Campo;
|FPRC;

|PROCESO dias; |TIPO 7;
|SI #0#2 = #0#3;
        #0#12 = "N";
    |PINTA #0#12;
    |C_M #0#12, 1, "N";
|SINO;
    |C_M #0#12, 1, "S";
|FINSI;
|FPRC;
____________________________________/
|PROCESO h_extras;
    #24#0 = emp_ant;
    #24#1 = tra_ant;
|LEE 000#24=;
|SI FSalida = 0;
    |DDEFECTO #25;
        #25#0 = #24#156;
        #25#13 = #0#0;
    |LEE 000#25=;
        elmes = #0#1;
        eldia = (#50#0 * 100) + 1;
        alfa1 = #25elmes % eldia;
    |SI alfa1 = "1";
            dext = dhor;
    |SINO;
        |SI dhor > 8;
                dext = dhor - 8;
        |SINO;
                dext = 0;
        |FINSI;
    |FINSI;
|FINSI;

    sext = sext + dext;       || total tarjeta extras
    gext = gext + dext;       || total general extras
|FPRC;

|PROCESO AjusteLineal;
     desde = (#pretopel#2 * 100) + #pretopel#3;
     hasta = (#pretopel#4 * 100) + #pretopel#5;
     |SI lectu ] desde;|Y lectu [ hasta;
          nhor = #pretopel#6 - hajus;
          nmin = #pretopel#7 - majus;
          |SI nmin < 0;
               nhor = nhor - 1;
               nmin = 60 + nmin;
          |FINSI;
          rajus = (nhor * 60) + nmin;

          hajus = #pretopel#6;
          majus = #pretopel#7;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE AjusteLineal;
     #pretopel#1;
     ;
     #pretopes#0, #pretopes#142,  0,  0;
     #pretopes#0, #pretopes#142, 23, 59;
     ;
     NULL, AjusteLineal;
|FIN;

|PROCESO ajustes1;
    aju_cod = #12#0;
    aju_dia = #12#142;
|FPRC;

|DEFBUCLE ajustes;
 #12#1;
 ;
 nCodAjus, "01.01.1900";
 nCodAjus, fehas;
 ;
 NULL, ajustes1;
|FIN;

|PROCESO lee_ajuste;
     alfa1 = #11#3;  alfa1 = ("00" + alfa1) % -102;
     alfa2 = #0#1;   alfa2 = ("00" + alfa2) % -102;
     alfa3 = alfa1 + "." + alfa2 + "." + #0#0;
     fehas = alfa3;

     aju_cod = -1;

     |SI swAjuste = 1;        || tengo en cuenta ajuste de Salida y de Entrada
          |SI swentra2 = 0;              || significa que es ENTRADA
               nCodAjus = #20#3;
          |SINO;
               nCodAjus = #20#6;
          |FINSI;
     |SINO;
          nCodAjus = #20#3;   || para todos el mismo
     |FINSI;

     |BUCLE ajustes;

     |DDEFECTO #12;
     |SI aju_cod ! -1;
          #12#0 = aju_cod;
          #12#142 = aju_dia;
          |LEE 020#12=;
     |FINSI;
|FPRC;

|PROCESO descuentos;
|HAZ lee_ajuste;

    rajus = 0;
    dlect = (#50#5 * 100) + #50#6;
    hlect = (#50#7 * 100) + #50#8;
|PARA para1 = 122; |SI para1 [ 125; |HACIENDO para1 = para1 + 1;
        dhora = para1;         dminu = para1 + 4;
        hhora = para1 + 8;     hminu = para1 + 12;
        rhora = para1 + 16;

        desde = (#12dhora * 100) + #12dminu;
        hasta = (#12hhora * 100) + #12hminu;
    |SI desde ! 0;|Y hasta ! 0;
        |SI hlect ] desde;|Y dlect [ hasta;
                rajus = #12rhora;
        |FINSI;
    |FINSI;
|SIGUE;
|FPRC;

|PROCESO ajustes;
|HAZ lee_ajuste;

    rajus = 0;
    lectu = (hajus * 100) + majus;
|PARA para1 = 2; |SI para1 [ 21; |HACIENDO para1 = para1 + 1;
        dhora = para1;         dminu = para1 + 20;
        hhora = para1 + 40;    hminu = para1 + 60;
        chora = para1 + 80;    cminu = para1 + 100;

        desde = (#12dhora * 100) + #12dminu;
        hasta = (#12hhora * 100) + #12hminu;
    |SI lectu ] desde;|Y lectu [ hasta;
            nhor = #12chora - hajus;
            nmin = #12cminu - majus;
        |SI nmin < 0;     nhor = nhor - 1;    nmin = 60 + nmin;   |FINSI;
            rajus = (nhor * 60) + nmin;

            hajus = #12chora;
            majus = #12cminu;
        |SAL;
    |FINSI;
|SIGUE;

|BUCLE AjusteLineal;
|FPRC;

|PROCESO detalle;
    #50#0 = #11#3;       || dia
    #50#1 = hor_ant;     || desde hora (M)
    #50#2 = min_ant;     || hasta minutos (M)
    #50#3 = #11#4;       || hasta hora (M)
    #50#4 = #11#5;       || hasta minutos (M)
    #50#5 = hor_ant;     || desde hora (C)
    #50#6 = min_ant;     || hasta minutos (C)
    #50#7 = #11#4;       || hasta hora (C)
    #50#8 = #11#5;       || hasta minutos (C)
    #50#9 = 0;           || minutos ajustados
    #50#10 = 0;          || fraccion horas
    #50#11 = 0;          || fraccion minutos
    #50#12 = 0;          || tiempo en minutos
    #50#13 = 0;          || tiempo en horas

    swentra2 = 0;
    hajus = #50#5;  majus = #50#6;
|HAZ ajustes;                      || ajusta el desde
    #50#9 = #50#9 - rajus;
    #50#5 = hajus;  #50#6 = majus;

    swentra2 = 1;
    hajus = #50#7;  majus = #50#8;
|HAZ ajustes;                      || ajusta el hasta
    #50#9 = #50#9 + rajus;
    #50#7 = hajus;  #50#8 = majus;

    nhor = #50#7 - #50#5;
    nmin = #50#8 - #50#6
|SI nmin < 0;    nhor = nhor - 1;    nmin = 60 + nmin;   |FINSI;

|HAZ descuentos;
    #50#15 = rajus;
    nmin = nmin + #50#15;
|SI nmin < 0;    nhor = nhor - 1;    nmin = 60 + nmin;   |FINSI;

|SI nmin < 0;  nmin = 0; |FINSI;
|SI nhor < 0;  nhor = 0; |FINSI;

    #50#10 = nhor;
    #50#11 = nmin;
    #50#12 = (nhor * 60) + nmin;
    #50#13 = nhor + (nmin / 60);
    #50#14 = #11#2;      || linea
    #50#16 = #11#8;      || tipo incidencia

    shor = shor + #50#13;     || total por tarjeta
    dhor = dhor + #50#13;     || total por dia
    ghor = ghor + #50#13;     || total general
|FPRC;

|PROCESO rup_bar;
     |DDEFECTO #20;   #20#0 = bar_ant;              |LEE 000#20=;     || tarjeta
     |DDEFECTO #23;   #23#0 = #20#1;                |LEE 000#23=;     || empresa
     |DDEFECTO #24;   #24#0 = #20#1; #24#1 = #20#2; |LEE 000#24=;     || trabajador

     swlin = 1;
     |HAZ Enlace;
     swCabecera = 1;

     shor = 0;
     sext = 0;
     bar_ant = #11#7;
     swentra = 0;
|FPRC;

|PROCESO rup_dia;
|SI swentra = 1;
        error = "Tarjeta [" + bar_ant + "." + lin_ant + "] sin registro ";
        error= error + "de salida. [" + "Dia " + dia_ant + ". Hora:" + hor_ant + ":" + min_ant + "]";
        swlin = 3;
    |HAZ Enlace;
     swCabecera = 0;
|FINSI;

|HAZ h_extras;

|SI #0#12 = "S";
     |SI #50#13 ! dhor;       || solo si hay mas de una sola linea(lo comprue-
          swlin = 2;          || bo viendo si el numero de lineas del dia es
          |HAZ Enlace;        || distinto al de la ultima linea)
     swCabecera = 0;
     |FINSI;
|FINSI;

    dhor = 0;
    dia_ant = #11#3;
    swentra = 0;
|FPRC;

|PROCESO movimientos1;
     |DDEFECTO #20; #20#0 = #11#7; |LEE 000#20=;
     |SI #20#1 < #0#8; |O #20#1 > #0#9;      |ACABA;   |FINSI;   || empresas
     |SI #20#2 < #0#10;|O #20#2 > #0#11;     |ACABA;   |FINSI;   || trabajadores

     |SI swCabecera = 1;
     |DDEFECTO #24;   #24#0 = #20#1; #24#1 = #20#2; |LEE 000#24=;     || trabajador
     |FINSI;

     |SI sw_rup = 0;
          sw_rup = 1;
          bar_ant = #11#7;
          dia_ant = #11#3;
     |FINSI;

     |SI bar_ant ! #11#7;
          |HAZ rup_dia;
          |HAZ rup_bar;
     |SINO;
          |SI dia_ant ! #11#3;
               |HAZ rup_dia;
          |FINSI;
     |FINSI;

     |SI swentra = 1;
          swentra = 0;
          |HAZ detalle;
          swlin = 0;
          |HAZ Enlace;
     swCabecera = 0;
     |SINO;
          swentra = 1;

          hor_ant = #11#4;
          min_ant = #11#5;
          lin_ant = #11#2;
     |FINSI;

     emp_ant = #20#1;
     tra_ant = #20#2;
|FPRC;

|DEFBUCLE movimientos;
 #11#2;
 9;
 #0#0, #0#1, #0#4, #0#2,  0,  0,  0, #0#13;
 #0#0, #0#1, #0#5, #0#3, 23, 59, 59, #0#14;
 ;
 NULL, movimientos1;
|FIN;

|PROCESO Control;
     |LEE 000#precontr.p;
     |SI FSalida ! 0;    || No existe control de la aplicacion, entonces
          swAjuste = 0;  || el Codigo de Ajuste de Salida es el de Entrada
     |SINO;              || esto para los que usaran el programa antes de
          |SI #precontr#1 = "S";   || meter el codigo de salida, cuando
               swAjuste = 0;       || solo habia de entrada 26.05.2004
          |SINO;
               swAjuste = 1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO LeeTrab;
     |ABRE #labtraba;
     aAlfa = aTrabAnt % 105;
     #labtraba#0 = aAlfa;
     aAlfa = aTrabAnt % -105;
     #labtraba#1 = aAlfa;
     |LEE 000#labtraba.=;
     |CIERRA #labtraba;

     nContador = nContador + 1;
     |SI nContador = 1;           || la primera vez siempre
          swCabecera = 1;
     |FINSI;
|FPRC;

|PROCESO Imprime;
     |HAZ LeeTrab;
     nEmp = #51#0;
     aEmp = nEmp;  aEmp = "00000" + aEmp; aEmp = aEmp % -105;
     nTra = #51#1;
     aTra = nTra;  aTra = "00000" + aTra; aTra = aTra % -105;
     aTrab = aEmp + "." + aTra;
     aDia = #51#3; aDia = "00" + aDia; aDia = aDia % -102;
     aMes = #51#2; aMes = "00" + aMes; aMes = aMes % -102;
     aAny = #51#4;
     aFecha = aDia + "/" + aMes + "/" + aAny;
     aTipo = #51#6; aTipo = "0000" + aTipo; aTipo = aTipo % -104;

     |SI aTrab ! aTrabAnt; |Y aTrabAnt ! "";
          swParcial = 1;      || switch para pie de subtotales por trabajador
          |PRINT;
          nHorasTrab = 0;
          nHorasFec = 0;
          swCabecera = 1;
     |SINO;
          swParcial = 0;
          |SI aFecha ! aFechaAnt; |Y aFechaAnt ! "";
               |PRINT;
               nHorasFec = 0;
               swCabecera = 0;
               |SI swParcial = 1;
                    nHorasTrab = 0;
               |FINSI;
          |SINO;
               |SI aTipo ! aTipoAnt; |Y aTipoAnt ! "";
                    |PRINT;
                    swCabecera = 0;
                    nHorasFec = 0;
                    |SI swParcial = 1;
                         nHorasTrab = 0;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
     nHorasFec = nHorasFec + #51#5;
     nHorasTrab = nHorasTrab + #51#5;
     nHorasTot = nHorasTot + #51#5;
     aTrabAnt = aTrab;
     aFechaAnt = aFecha;
     aTipoAnt = aTipo;
|FPRC;

|DEFBUCLE ElInforme;
     #51#2;
     ;
     #0#8, #0#10,  1, 0000;
     #0#9, #0#11, 31, 9999;
     ;
     NULL, Imprime;
|FIN;

|PROCESO InformeComp;
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR "precom02";
          |BUCLE ElInforme;
          swParcial = 1;
          |PRINT;
          |PIEINF;
          |FININF;
          |FINIMP;
     |SINO;
          |MENAV "    Proceso abortado .... !!!";
     |FINSI;
|FPRC;

|PROCESO RellenaHoras;

     || horas definidas por trabajador

     aAlfa = #51#6;     || tipo de hora (incidencia)
     aAlfa = aAlfa % -101;

     |SI aAlfa = "0";
          aAlfa = " ";
     |FINSI;

     #242#0 = aAlfa;     || tipo de hora
     |LEE 000#242=;
     |SI FSalida ! 0;
          nError = 1;
          aError = "Tipo de Hora no existe";
          #51#8 = nError;
          #51#9 = aError;
          |GRABA 020#51.a;
          |ACABA;
     |FINSI;

     #249#0 = #51#0;     || empresa
     #249#1 = #51#1;     || trabajador
     #249#2 = aAlfa;     || tipo de hora
     |LEE 000#249=;
     |SI FSalida ! 0;
          nError = 2;
          aError = "Tipo de Hora no definida en trabajador";
          #51#8 = nError;
          #51#9 = aError;
          |GRABA 020#51.a;
          |ACABA;
     |FINSI;

     || horas trabajadas Cabs.

     #243#0 = #51#0;     || empresa
     #243#1 = #51#1;     || trabajador
     |LEE 000#243=;
     |SI FSalida ! 0;
          |GRABA 020#243n;
          |SI FSalida ! 0;
               nError = 3;
               aError = "Error en Grabacion de Cabecera";
               #51#8 = nError;
               #51#9 = aError;
               |GRABA 020#51.a;
               |ACABA;
          |FINSI;
     |FINSI;

     || horas trabajadas Lins.

     #244#0 = #51#0;     || empresa
     #244#1 = #51#1;     || trabajador
     aDia = #51#3; aDia = "00" + aDia; aDia = aDia % -102;
     aMes = #51#2; aMes = "00" + aMes; aMes = aMes % -102;
     aAnyo = #51#4;
     aAlfa = aDia + "." + aMes + "." + aAnyo;
     #244#2 = aAlfa;     || Fecha
     aAlfa = #51#6;
     aAlfa = aAlfa % -101;
     |SI aAlfa = "0";
          aAlfa = " ";
     |FINSI;
     #244#3 = aAlfa;     || tipo de hora (incidencia)
     |LEE 000#244=;
     |SI FSalida ! 0;
          #244#4 = #242#1;    || descripcion tipo de hora
          #244#5 = #51#5;          || numero de horas
          #244#6 = #249#3;    || precio
          |GRABA 020#244n;
     |SINO;
          #244#5 = #244#5 + #51#5;          || numero de horas
          |GRABA 020#244a;
     |FINSI;

     |SI FSalida ! 0;
          nError = 4;
          aError = "Error en Grabacion de Linea";
          #51#8 = nError;
          #51#9 = aError;
          |GRABA 020#51.a;
          |ACABA;
     |SINO;
          nError = 0;
          aError = "Registro importado correctamente";
          #51#8 = nError;
          #51#9 = aError;
          |GRABA 020#51.a;
          |ACABA;
     |FINSI;
     |LIBERA #51;
     |LIBERA #243;
     |LIBERA #244;
     |LIBERA #249;
|FPRC;

|DEFBUCLE RellenaHoras;
     #51#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL, RellenaHoras;
|FIN;

|PROCESO EnlacePrieto;
     |ABRE #242;
     |ABRE #243;
     |ABRE #244;
     |ABRE #249;
     |BUCLE RellenaHoras;
     |CIERRA #242;
     |CIERRA #243;
     |CIERRA #244;
     |CIERRA #249;
|FPRC;

|PROCESO Enlace;
     ||SI aParam = "ENLACE";
     |SI nOpcion = 2; |O nOpcion = 3;
          |SI nPaso = 1;
               aAlfa = Usuario; |QBT aAlfa; aAlfa = ("t" + aAlfa) % 108;
               |NOME_DAT #51 aAlfa;
               |DELINDEX #51;
               |ABRE #51;
          |FINSI;
          |SI nPaso = 2; |Y swlin = 0;
               #51#0 = #20#1;     || empresa
               #51#1 = #20#2;     || trabajador
               #51#2 = #0#1;            || mes
               #51#3 = #50#0;           || dia
               #51#4 = #0#0;            || a¤o
               #51#5 = #50#13;          || numero de horas
               #51#6 = #50#16;          || incidencia
               #51#7 = #50#14;          || numero de linea

               |GRABA 020#51.n;
          |FINSI;
          |SI nPaso = 3;
               |CIERRA #51;

               |SI nOpcion = 3;
                    |HAZ EnlacePrieto;
                    |HAZ InformeComp;
               |FINSI;
               |SI nOpcion = 2;
                    |HAZ InformeComp;
               |FINSI;
          |FINSI;
     |SINO;
          |SI nPaso = 2;
               |PRINT;
          |FINSI;
          |SI nPaso = 3;
               |PIEINF;
               |FININF;
               |FINIMP;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO tipo3;
     |ABRET #12;
     |ABRE #20;
     |ABRE #21;
     |ABRE #23;
     |ABRE #24;
     |ABRE #25;
     |ABRE #precontr;
     nPaso = 1;
     |HAZ Enlace;

     nPaso = 2;

     |HAZ Control;
     swCabecera = 1;
     |BUCLE movimientos;
     |SI sw_rup ! 0;
          |HAZ rup_dia;
          |HAZ rup_bar;
          |PIEINF;
     |SINO;
          |MENAV "    Seleccion vacia ...";
     |FINSI;
     |CIERRAT #12;
     |CIERRA #20;
     |CIERRA #21;
     |CIERRA #23;
     |CIERRA #24;
     |CIERRA #25;
     |CIERRA #precontr;
     nPaso = 3;
     |HAZ Enlace;
|FPRC;

|PROCESO Carga;
     |DBASS aBase;
     |QBF aBase;

     aPathFich = aBase + "aginom/fich/";
     aPathDef = aBase + "aginom/def/";

     aAlfa = aPathDef + "mod00242.mas";
     |CARGA_DEF aAlfa, mod00242@;
     |SI FSalida ! 0;
          aAlfa = "    ATENCION: Referencia erronea en fichero " + aAlfa;
          |MENAV aAlfa;
          |ACABA;
     |FINSI;

     aAlfa = aPathDef + "mod00243.mas";
     |CARGA_DEF aAlfa, mod00243@;
     |SI FSalida ! 0;
          aAlfa = "    ATENCION: Referencia erronea en fichero " + aAlfa;
          |MENAV aAlfa;
          |ACABA;
     |FINSI;

     aAlfa = aPathDef + "mod00244.mas";
     |CARGA_DEF aAlfa, mod00244@;
     |SI FSalida ! 0;
          aAlfa = "    ATENCION: Referencia erronea en fichero " + aAlfa;
          |MENAV aAlfa;
          |ACABA;
     |FINSI;

     aAlfa = aPathDef + "mod00249.mas";
     |CARGA_DEF aAlfa, mod00249@;
     |SI FSalida ! 0;
          aAlfa = "    ATENCION: Referencia erronea en fichero " + aAlfa;
          |MENAV aAlfa;
          |ACABA;
     |FINSI;

     |PATH_DAT #242 aPathFich;
     |PATH_DAT #243 aPathFich;
     |PATH_DAT #244 aPathFich;
     |PATH_DAT #249 aPathFich;
|FPRC;

|PROCESO Descarga;
     |DESCARGA_DEF #mod00249@;
     |DESCARGA_DEF #mod00244@;
     |DESCARGA_DEF #mod00243@;
     |DESCARGA_DEF #mod00242@;
|FPRC;

|PROCESO BuscaPrieto;
     |DBASE aBase;
     aBase = aBase + "bin/mod00243.tab";
     aFabre = "rb  " + aBase;
     |FABRE aFabre;
|FPRC;

|PROGRAMA;
     |HAZ BuscaPrieto;
     |SI FSalida < 0;
          |MENAV "    No dispone de modulo especial para esta opcion";
          |ACABA;
     |FINSI;
     nPaso = 0;
     aParam = PARAMETRO;
     |QBF aParam;
     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |MENU menu;
          nOpcion = FSalida;
          |SI nOpcion ! 1; |Y nOpcion ! 2; |Y nOpcion ! 3;
               |ACABA;
          |FINSI;
          ||SI aParam = "";
          |SI nOpcion = 1;
               |IMPRE 0;
               |SI FSalida = 0;
                    ||INFOR "preinf02";
                    || el informe preinf02.inf lo dejo ahi guardadito por
                    || si acaso pero uso el 03, por los cambios pedidoo por
                    || Prieto 13/07/04
                    |INFOR "preinf03";
                    |HAZ tipo3;
               |SINO;
                    |MENAV "    Proceso abortado ...!";
               |FINSI;
          |SINO;
               |HAZ Carga;
               |HAZ tipo3;
               |HAZ Descarga;
          |FINSI;
     |FINSI;
|FPRO;
