|FICHEROS;
     precompr #0;
     prequiva #20;       || tarjetas
     labtraba #24;       || trabadores
     premvtol #11;       || movimientos lins.
     pretmp04 #50;       || temporal
|FIN;

|VARIABLES;
     fech1 = @;
     alfa1 = "";
     p_mes = "";
     &aAnyo = "";
     &aMes = "";
|FIN;

|PROCESO defecto; |TIPO 7; ||calcula el a¤o por defecto segun fecha sistema
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % -104;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO mirames; |TIPO 7; ||CALCULA EL MES POR DEFECTO SEGUN FECHA SISTEMA
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % 402;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO pintames; |TIPO 0;      || PINTA EL MES LITERAL
|SI #0Campo =  1;  p_mes = "ENERO     ";  |FINSI;
|SI #0Campo =  2;  p_mes = "FEBRERO   ";  |FINSI;
|SI #0Campo =  3;  p_mes = "MARZO     ";  |FINSI;
|SI #0Campo =  4;  p_mes = "ABRIL     ";  |FINSI;
|SI #0Campo =  5;  p_mes = "MAYO      ";  |FINSI;
|SI #0Campo =  6;  p_mes = "JUNIO     ";  |FINSI;
|SI #0Campo =  7;  p_mes = "JULIO     ";  |FINSI;
|SI #0Campo =  8;  p_mes = "AGOSTO    ";  |FINSI;
|SI #0Campo =  9;  p_mes = "SEPTIEMBRE";  |FINSI;
|SI #0Campo = 10;  p_mes = "OCTUBRE   ";  |FINSI;
|SI #0Campo = 11;  p_mes = "NOVIEMBRE ";  |FINSI;
|SI #0Campo = 12;  p_mes = "DICIEMBRE ";  |FINSI;
|PINTA 1144, p_mes;
|FPRC;

|PROCESO imprime;
     aAnyo = #0#0;
     aMes = #0#1;
     |PRINT;
|FPRC;

|DEFBUCLE imprime;
     #50#1;
     ;
     0000, 01, 01, 000001, 00, 00;
     9999, 12, 31, 999999, 23, 59;
     ;
     NULL, imprime;
|FIN;

|PROCESO graba_temp;          || graba temporal con datos necesarios
     |DDEFECTO #20; #20#0 = #11#7; |LEE 000#20=;
     ||SI FSalida = 0;
          |SI #20#1 < #0#6; |O #20#1 > #0#7;      |ACABA;   |FINSI;   || empresas
          |SI #20#2 < #0#8; |O #20#2 > #0#9;      |ACABA;   |FINSI;   || trabajadores
          #24#0 = #20#1;      || Empresa
          #24#1 = #20#2;      || Trabajador
          |LEE 000#24=;       || Busca el calendario en labtraba para el temp.

          #50#0 = #11#7;      || Tarjeta
          #50#1 = #11#1;      || Mes
          #50#2 = #11#3;      || Dia
          #50#3 = #11#4;      || Hora
          #50#4 = #11#5;      || Minuto
          #50#5 = #11#6;      || Segundo
          #50#6 = #11#9;      || Terminal
          #50#7 = #24#156;    || Calendario
          #50#8 = #11#2;      || Registro
          #50#9 = #24#2;      || Nombre Trabajador
          #50#10 = #11#10;    || Flag E/S
          |GRABA 000#50n;

     ||FINSI;
|FPRC;

|DEFBUCLE movimientos;
     #11#2;
     9;
     #0#0, #0#1, #0#4,  1,  0,  0,  0, #0#2;  || a¤o,mes,tarjeta,dia,hora,min,seg
     #0#0, #0#1, #0#5, 31, 23, 59, 59, #0#3;
     ;
     NULL, graba_temp;
|FIN;


|PROCESO tipo3; |TIPO 3;
     |IMPRE 0;
     |SI FSalida = 0;
          |ABRE #20;
          |ABRE #24;
          |DELINDEX #50;
          |ABRE #50;
          |INFOR "precompr";
          |BUCLE movimientos;
          |BUCLE imprime;
          |PIEINF;
          |FININF;
          |CIERRA #20;
          |CIERRA #24;
          |CIERRA #50;
     |SINO;
          |MENAV "    Proceso abortado ....";
     |FINSI;
     |FINIMP;
|FPRC;
