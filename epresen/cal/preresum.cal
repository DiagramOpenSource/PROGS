|FICHEROS;
    preresum #0;

    prerectm #13;   || recibos (temporal, refunde almacen y nominas)
    prequiva #20;   || tarjetas
    precotiz #22;   || configuracion cotizaciones

    nomcalca #25;   || calculos cabs.
    labtraba #27;   || trabajadores
    nomauxi1 #28;   || areas
    nomauxi2 #29;   || secciones
    labcentr #30;   || centros
    labempre #31;   || empresas
    nomhicca #32;   || historico cabs.

    prerecib #99;   || recibos almacen
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    p_mes = "";
    emp_ant = 0;
    cen_ant = 0;
    are_ant = "";
    sec_ant = "";
    &horas = 0;
    &bruto = 0;
    birpf = 0;
    &rirpf = 0;
    &aposs = 0;
    &antic = 0;
    &espec = 0;
    &liqui = 0;
    &ssemp = 0;
    &prest = 0;
    &coste = 0;
    &antes = 0;
    &swlin = 0;
    &sbruto = 0;
    &srirpf = 0;
    &saposs = 0;
    &santic = 0;
    &sespec = 0;
    &sliqui = 0;
    &sssemp = 0;
    &sprest = 0;
    &scoste = 0;
    &santes = 0;
    &abruto = 0;
    &arirpf = 0;
    &aaposs = 0;
    &aantic = 0;
    &aespec = 0;
    &aliqui = 0;
    &assemp = 0;
    &aprest = 0;
    &acoste = 0;
    &aantes = 0;
    &cbruto = 0;
    &crirpf = 0;
    &caposs = 0;
    &cantic = 0;
    &cespec = 0;
    &cliqui = 0;
    &cssemp = 0;
    &cprest = 0;
    &ccoste = 0;
    &cantes = 0;
    &ebruto = 0;
    &erirpf = 0;
    &eaposs = 0;
    &eantic = 0;
    &eespec = 0;
    &eliqui = 0;
    &essemp = 0;
    &eprest = 0;
    &ecoste = 0;
    &eantes = 0;
    &fbruto = 0;
    &frirpf = 0;
    &faposs = 0;
    &fantic = 0;
    &fespec = 0;
    &fliqui = 0;
    &fssemp = 0;
    &fprest = 0;
    &fcoste = 0;
    &fantes = 0;
    hany = 0;
|FIN;
____________________________________/
|PROCESO mayus; |TIPO 0;
    #0Campo = #0Campo > #0Campo;
|PINTA #0Campo;
|FPRC;

|PROCESO defecto; |TIPO 7; ||calcula el a¤o por defecto segun fecha sistema
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % -104;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO mirames; |TIPO 7; ||CALCULA EL MES POR DEFECTO SEGUN FECHA SISTEMA
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % 402;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO pintames; |TIPO 0;      || PINTA EL MES LITERAL
|SI #0Campo =  1;  p_mes = "ENERO     ";  |FINSI;
|SI #0Campo =  2;  p_mes = "FEBRERO   ";  |FINSI;
|SI #0Campo =  3;  p_mes = "MARZO     ";  |FINSI;
|SI #0Campo =  4;  p_mes = "ABRIL     ";  |FINSI;
|SI #0Campo =  5;  p_mes = "MAYO      ";  |FINSI;
|SI #0Campo =  6;  p_mes = "JUNIO     ";  |FINSI;
|SI #0Campo =  7;  p_mes = "JULIO     ";  |FINSI;
|SI #0Campo =  8;  p_mes = "AGOSTO    ";  |FINSI;
|SI #0Campo =  9;  p_mes = "SEPTIEMBRE";  |FINSI;
|SI #0Campo = 10;  p_mes = "OCTUBRE   ";  |FINSI;
|SI #0Campo = 11;  p_mes = "NOVIEMBRE ";  |FINSI;
|SI #0Campo = 12;  p_mes = "DICIEMBRE ";  |FINSI;
|PINTA 0936, p_mes;
|FPRC;

|PROCESO ordenacion; |TIPO 0;
|C_M #0#6, 1, "S";
|C_M #0#7, 1, "S";
|C_M #0#8, 1, "S";
|C_M #0#9, 1, "S";
|SI #0#10 = 1;           || empresa+centro
                         #0#9 = "S";
                         #0#6 = "S";
    |C_M #0#7, 1, "N";   #0#7 = "N";
    |C_M #0#8, 1, "N";   #0#8 = "N";
|FINSI;
|SI #0#10 = 2;           || area+seccion
    |C_M #0#9, 1, "N";   #0#9 = "N";
    |C_M #0#6, 1, "N";   #0#6 = "N";
                         #0#7 = "S";
                         #0#8 = "S";
|FINSI;
|SI #0#10 = 3;
    |C_M #0#9, 1, "N";   #0#9 = "N";
    |C_M #0#6, 1, "N";   #0#6 = "N";
    |C_M #0#7, 1, "N";   #0#7 = "N";
    |C_M #0#8, 1, "N";   #0#8 = "N";
|FINSI;
|PINTA #0#9;
|PINTA #0#6;
|PINTA #0#7;
|PINTA #0#8;
|FPRC;
____________________________________/
|PROCESO rup_sec;
|SI #0#8 = "N";     sec_ant = #27#63;   |ACABA;   |FINSI;

|DDEFECTO #29; #29#0 = are_ant; #29#1 = sec_ant; |LEE 000#29=;
    swlin = 1;  |PRINT;
    sbruto = 0; srirpf = 0; saposs = 0; santic = 0; sespec = 0; sliqui = 0;
    sssemp = 0; sprest = 0; scoste = 0; santes = 0;
    sec_ant = #27#63;
|FPRC;

|PROCESO rup_are;
|SI #0#7 = "N";     are_ant = #27#62;   |ACABA;   |FINSI;

|DDEFECTO #28; #28#0 = are_ant; |LEE 000#28=;
    swlin = 2;  |PRINT;
    abruto = 0; arirpf = 0; aaposs = 0; aantic = 0; aespec = 0; aliqui = 0;
    assemp = 0; aprest = 0; acoste = 0; aantes = 0;
    are_ant = #27#62;
|FPRC;

|PROCESO rup_cen;
|SI #0#6 = "N";     cen_ant = #27#77;   |ACABA;   |FINSI;

|DDEFECTO #30; #30#0 = emp_ant; #30#1 = cen_ant; |LEE 000#30=;
    swlin = 3;  |PRINT;
    cbruto = 0; crirpf = 0; caposs = 0; cantic = 0; cespec = 0; cliqui = 0;
    cssemp = 0; cprest = 0; ccoste = 0; cantes = 0;
    cen_ant = #27#77;
|FPRC;

|PROCESO rup_emp;
|SI #0#9 = "N";     emp_ant = #27#0;    |ACABA;   |FINSI;

|DDEFECTO #31; #31#0 = emp_ant; |LEE 000#31=;
    swlin = 4;  |PRINT;
    ebruto = 0; erirpf = 0; eaposs = 0; eantic = 0; eespec = 0; eliqui = 0;
    essemp = 0; eprest = 0; ecoste = 0; eantes = 0;
    emp_ant = #27#0;
|FPRC;

|PROCESO resumen2;
|SI sw_rup = 0;
        sw_rup = 1;
        seleccio = 1;
        emp_ant = #27#0;
        cen_ant = #27#77;
        are_ant = #27#62;
        sec_ant = #27#63;
|FINSI;

|SI #0#10 = 1;
    |SI emp_ant ! #13#0;
        |HAZ rup_cen;
        |HAZ rup_emp;
    |SINO;
        |SI cen_ant ! #27#77;
            |HAZ rup_cen;
        |FINSI;
    |FINSI;
|FINSI;
|SI #0#10 = 2;
    |SI are_ant ! #27#62;
        |HAZ rup_sec;
        |HAZ rup_are;
    |SINO;
        |SI sec_ant ! #27#63;
            |HAZ rup_sec;
        |FINSI;
    |FINSI;
|FINSI;

    bruto = #13#6 + #13#7;
    birpf = #13#13;
    rirpf = #13#9;
    aposs = #13#17;
    antic = #13#10;
    espec = #13#11;
    liqui = #13#12;
    horas = #13#4;
    ssemp = #13#21;
    prest = #13#22;
    coste = #13#23;
    antes = antic + espec;

    swlin = 0;
|PRINT;

   fbruto = fbruto + bruto;  frirpf = frirpf + rirpf; faposs = faposs + aposs;
   fantic = fantic + antic;  fespec = fespec + espec; fliqui = fliqui + liqui;
   fssemp = fssemp + ssemp;  fprest = fprest + prest; fcoste = fcoste + coste;
   fantes = fantes + antes;

   ebruto = ebruto + bruto;  erirpf = erirpf + rirpf; eaposs = eaposs + aposs;
   eantic = eantic + antic;  eespec = eespec + espec; eliqui = eliqui + liqui;
   essemp = essemp + ssemp;  eprest = eprest + prest; ecoste = ecoste + coste;
   eantes = eantes + antes;

   cbruto = cbruto + bruto;  crirpf = crirpf + rirpf; caposs = caposs + aposs;
   cantic = cantic + antic;  cespec = cespec + espec; cliqui = cliqui + liqui;
   cssemp = cssemp + ssemp;  cprest = cprest + prest; ccoste = ccoste + coste;
   cantes = cantes + antes;

   abruto = abruto + bruto;  arirpf = arirpf + rirpf; aaposs = aaposs + aposs;
   aantic = aantic + antic;  aespec = aespec + espec; aliqui = aliqui + liqui;
   assemp = assemp + ssemp;  aprest = aprest + prest; acoste = acoste + coste;
   aantes = aantes + antes;

   sbruto = sbruto + bruto;  srirpf = srirpf + rirpf; saposs = saposs + aposs;
   santic = santic + antic;  sespec = sespec + espec; sliqui = sliqui + liqui;
   sssemp = sssemp + ssemp;  sprest = sprest + prest; scoste = scoste + coste;
   santes = santes + antes;
|FPRC;

|PROCESO resumen1;
    #27#0 = #13#0;
    #27#1 = #13#1;
|LEE 000#27=;             || lee trabajador
|SI FSalida = 0;
        #22#0 = #27#42;
    |LEE 000#22=;         || lee config.cotizacion
    |SI FSalida = 0;
        |HAZ resumen2;
    |SINO;
            alfa1 = "    Clave dias salario [" + #22#0 + "] no configurada ...";
        |MENAV alfa1;
    |FINSI;
|SINO;
        alfa1 = "    Trabajador [" + #27#0 + "-" + #27#1 + "] inexistente ...";
    |MENAV alfa1;
|FINSI;
|FPRC;

|PROCESO ordena;
|DDEFECTO #27;
    #27#0 = #13#0;
    #27#1 = #13#1;
|LEE 000#27=;            || lee trabajador
|FPRC;

|DEFBUCLE resumen3;      || codigo trabajador
 #13#1;
 ;
 #0#2, #0#4, #0#0, #0#1;
 #0#3, #0#5, #0#0, #0#1;
 ;
 NULL, resumen1;
|FIN;

|DEFBUCLE resumen2;      || area+seccion
 #13#1;
 ;
 #0#2, #0#4, #0#0, #0#1;
 #0#3, #0#5, #0#0, #0#1;
 ;
 NULL, ordena, NULL, NULL, NULL, resumen1;
 #27#62, #27#63, #27#0, #27#77, #27#1;
|FIN;

|DEFBUCLE resumen1;      || empresa+centro
 #13#1;
 ;
 #0#2, #0#4, #0#0, #0#1;
 #0#3, #0#5, #0#0, #0#1;
 ;
 NULL, ordena, NULL, NULL, NULL, resumen1;
 #27#0, #27#77, #27#62, #27#63, #27#1;
|FIN;

|PROCESO tipo3; |TIPO 3;
|IMPRE 0;
|SI FSalida = 0;
    |HAZ refunde;
    |ABRE #20;      || cod. barras
    |SELECT #2#20;
    |ABRE #22;      || config.cotizacion
    |ABRE #27;      || trabajadores
    |ABRE #28;      || areas
    |ABRE #29;      || secciones
    |ABRE #30;      || centros
    |ABRE #31;      || empresas
    |INFOR "preresum";
    |SI #0#10 = 1;  |BUCLE resumen1;    |FINSI;   || empresa+centro
    |SI #0#10 = 2;  |BUCLE resumen2;    |FINSI;   || area+seccion
    |SI #0#10 = 3;  |BUCLE resumen3;    |FINSI;   || trabajador
    |SI seleccio ! 0;
        |HAZ rup_sec;
        |HAZ rup_are;
        |HAZ rup_cen;
        |HAZ rup_emp;
        |PIEINF;
    |SINO;
        |MENAV "    Seleccion vacia ...";
    |FINSI;
    |FININF;
    |CIERRA #20;
    |CIERRA #22;
    |CIERRA #27;
    |CIERRA #28;
    |CIERRA #29;
    |CIERRA #30;
    |CIERRA #31;
    |DELINDEX #13;
|SINO;
    |MENAV "    Proceso abortado ...";
|FINSI;
|FINIMP;
|FPRC;
____________________________________/
|PROCESO historico2;
     || graba en los campos del recibo de almacen los importes del recibo
     ||/oficial

    #13#4  = 0;                              || horas recibo (cero)
    #13#6  = #32#46;                         || salario base recibo (bruto)
    #13#13 = #32#15;                         || base IRPF recibo (nomina)
    #13#9  = #32#15;                         || retencion recibo (nomina)
    #13#10 = #32#52 + #32#53;                || anticipos
    #13#11 = #32#54;                         || en especie
    #13#17 = #32#48+#32#49+#32#50+#32#51;    || aportacion S.S.
    #13#12 = #32#46 - (#13#17+#13#10+#13#11+#13#9);    || liquido recibo (nomina)
    #13#21 = #32#45;                              || s.s. empresa
    #13#22 = #32#57 + #32#58 + #32#55 + #32#56;   || prestaciones ILT
    #13#23 = #32#46 - #13#22 + #32#45;            || coste p/trabajador
|GRABA 020#13n;
|FPRC;

|PROCESO historico1;
|DDEFECTO #13;
    #13#0 = #32#0;       || empresa
    #13#1 = #32#1;       || trabajador
    #13#2 = #0#0;        || a¤o
    #13#3 = #32#2;       || mes
|LEE 000#13=;
|SI FSalida ! 0;
    |HAZ historico2;       || si existe es que es de almacen
|FINSI;
|FPRC;

|DEFBUCLE historico;
 #32#1;
 ;
 #0#2, #0#4, hany, #0#1, 0;
 #0#3, #0#5, hany, #0#1, 0;
 ;
 NULL, historico1;
|FIN;

|PROCESO nominas2;
     || graba en los campos del recibo de almacen los importes del recibo
     ||/oficial

    #13#4  = 0;                              || horas recibo (cero)
    #13#6  = #25#46;                         || salario base recibo (bruto)
    #13#13 = #25#15;                         || base IRPF recibo (nomina)
    #13#9  = #25#15;                         || retencion recibo (nomina)
    #13#10 = #25#52 + #25#53;                || anticipos
    #13#11 = #25#54;                         || en especie
    #13#17 = #25#48+#25#49+#25#50+#25#51;    || aportacion S.S.
    #13#12 = #25#46 - (#13#17+#13#10+#13#11+#13#9);    || liquido recibo (nomina)
    #13#21 = #25#45;                              || s.s. empresa
    #13#22 = #25#57 + #25#58 + #25#55 + #25#56;   || prestaciones ILT
    #13#23 = #25#46 - #13#22 + #25#45;            || coste p/trabajador
|GRABA 020#13n;
|FPRC;

|PROCESO nominas1;
|DDEFECTO #13;
    #13#0 = #25#0;       || empresa
    #13#1 = #25#1;       || trabajador
    #13#2 = #0#0;        || a¤o
    #13#3 = #25#2;       || mes
|LEE 000#13=;
|SI FSalida ! 0;
    |HAZ nominas2;       || si existe es que es de almacen
|FINSI;
|FPRC;

|DEFBUCLE nominas;
 #25#1;
 ;
 #0#2, #0#4, #0#1, 0;
 #0#3, #0#5, #0#1, 0;
 ;
 NULL, nominas1;
|FIN;

|PROCESO historico;
|DDEFECTO #32;
    #32#0 = #99#0;  || empresa
    #32#1 = #99#1;  || trabajador
    #32#66= hany;   || a¤o
    #32#2 = #99#3;  || mes
    #32#3 = 0;      || tipo
|LEE 000#32=;

|DDEFECTO #13;
|PARA para1 = 0; |SI para1 [ 20; |HACIENDO para1 = para1 + 1;
        #13para1 = #99para1;
|SIGUE;
    #13#21 = #32#45;                              || s.s. empresa
    #13#22 = #32#57 + #32#58 + #32#55 + #32#56;   || prestaciones ILT
    #13#23 = (#13#6 + #13#7) - #13#22 + #32#45;   || coste p/trabajador
|GRABA 020#13n;
|FPRC;

|PROCESO mensual;
|DDEFECTO #25;
    #25#0 = #99#0;  || empresa
    #25#1 = #99#1;  || trabajador
    #25#2 = #99#3;  || mes
    #25#3 = 0;      || tipo
|LEE 000#25=;

|DDEFECTO #13;
|PARA para1 = 0; |SI para1 [ 20; |HACIENDO para1 = para1 + 1;
        #13para1 = #99para1;
|SIGUE;
    #13#21 = #25#45;                              || s.s. empresa
    #13#22 = #25#57 + #25#58 + #25#55 + #25#56;   || prestaciones ILT
    #13#23 = (#13#6 + #13#7) - #13#22 + #25#45;   || coste p/trabajador
|GRABA 020#13n;
|FPRC;

|PROCESO almacen1;
|SI #0#11 = "M";
    |HAZ mensual;
|SINO;
    |HAZ historico;
|FINSI;
|FPRC;

|DEFBUCLE almacen;
 #99#1;
 ;
 #0#2, #0#4, #0#0, #0#1;
 #0#3, #0#5, #0#0, #0#1;
 ;
 NULL, almacen1;
|FIN;

|PROCESO refunde;
|MENSA "    Preparando Informacion ...";
    alfa1 = Usuario;  |QBT alfa1;
|NOME_DAT #13 alfa1;
|DELINDEX #13;
|ABRE #13;

    hany = #0#0 $ 100;

|ABRE #25;
|ABRE #32;
|BUCLE almacen;
|CIERRA #25;
|CIERRA #32;

|SI #0#11 = "M";
    |BUCLE nominas;
|SINO;
    |BUCLE historico;
|FINSI;

|CIERRA #13;
|BLIN 4;
|FPRC;
