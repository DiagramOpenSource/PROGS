|FICHEROS;
    prehoras #0;

    premvtol #11;   || movtos. lins.
    prehisvc #12;   || historico cabs.
    prehisvl #13;   || historico lins.
    pretopes #14;   || ajustes de marcado

    prequiva #20;   || codigos de barras
    pretarea #21;   || tareas
    precotiz #22;   || cotizacion claves

    nomtrcal #25;   || calendario laboral
    labtraba #26;   || trabajadores

    pretmp01 #50;   || campos del detalle
    pretopel;       || lineal Ampliacion Topes
    precontr;
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    p_mes = "";
    bar_ant = 0;
    hor_ant = 0;
    min_ant = 0;
    lin_ant = 0;
    dia_ant = 0;
    nhor = 0;
    nmin = 0;
    shor = 0;
    error = "";
    cuantos = 0;
    topes = 0;
    talta = 0;
    calen = "";
    elmes = 0;
    ndias = 0;
    hpend = 0;
    any_ant = 0;
    mes_ant = 0;
    swentra = 0;
    dhora = 0;
    hhora = 0;
    chora = 0;
    dminu = 0;
    hminu = 0;
    cminu = 0;
    desde = 0;
    hasta = 0;
    rajus = 0;
    hajus = 0;
    majus = 0;
    lectu = 0;
    rhora = 0;
    dlect = 0;
    hlect = 0;
    aju_cod = 0;
    aju_dia = @;
    fehas = @;
    SwBaja = 0;
    dalta = 0;

     swAjuste = 0;
     nCodAjus = 0;
     swentra2 = 0;
|FIN;
____________________________________/
|PROCESO mayus; |TIPO 0;
    #0Campo = #0Campo > #0Campo;
|PINTA #0Campo;
|FPRC;

|PROCESO defecto; |TIPO 7; ||calcula el a¤o por defecto segun fecha sistema
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % -104;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO mirames; |TIPO 7; ||CALCULA EL MES POR DEFECTO SEGUN FECHA SISTEMA
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % 402;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO pintames; |TIPO 0;      || PINTA EL MES LITERAL
|SI #0Campo =  1;  p_mes = "ENERO     ";  |FINSI;
|SI #0Campo =  2;  p_mes = "FEBRERO   ";  |FINSI;
|SI #0Campo =  3;  p_mes = "MARZO     ";  |FINSI;
|SI #0Campo =  4;  p_mes = "ABRIL     ";  |FINSI;
|SI #0Campo =  5;  p_mes = "MAYO      ";  |FINSI;
|SI #0Campo =  6;  p_mes = "JUNIO     ";  |FINSI;
|SI #0Campo =  7;  p_mes = "JULIO     ";  |FINSI;
|SI #0Campo =  8;  p_mes = "AGOSTO    ";  |FINSI;
|SI #0Campo =  9;  p_mes = "SEPTIEMBRE";  |FINSI;
|SI #0Campo = 10;  p_mes = "OCTUBRE   ";  |FINSI;
|SI #0Campo = 11;  p_mes = "NOVIEMBRE ";  |FINSI;
|SI #0Campo = 12;  p_mes = "DICIEMBRE ";  |FINSI;
|PINTA 1136, p_mes;
|FPRC;
____________________________________/ LECTURA Y CREACION SEGUN MES ANTERIOR
|PROCESO calcu_dias;
    elmes = #0#1;
    calen = #25elmes;
|QBF calen;
    alfa1 = calen % 0;   topes = alfa1;

|HAZ SiEsAlta;
|HAZ SiEsBaja;

    ndias = 0;
|PARA para1 = dalta; |SI para1 [ topes; |HACIENDO para1 = para1 + 1;
        nume1 = (para1 * 100) + 1;
        alfa1 = calen % nume1;
    |SI alfa1 = "1";
            ndias = ndias + 1;
    |FINSI;
|SIGUE;

    topes = #22#1;
|HAZ SiEsBaja;
|SI topes > #22#1;  topes = #22#1; |FINSI;

                    || dias cotizables - dias festivos - dias no alta
    ndias = topes - ndias - talta;
|FPRC;

|PROCESO lee_anterior;
|DDEFECTO #13;
    #13#0 = #12#0;
    #13#1 = #12#1;
    #13#2 = #12#2;
    #13#3 = mes_ant;
|LEE 000#13=;
    hpend = #13#13;
|FPRC;

|PROCESO mes_anterior2;
|HAZ lee_anterior;
|HAZ calcu_dias;
    #13#0 = #12#0;
    #13#1 = #12#1;
    #13#2 = #0#0;
    #13#3 = #0#1;
|LEE 101#13=;
    FEstado = FSalida;

|PDEFECTO #13, 4, 14;
    #13#4 = p_mes;
    #13#5 = hpend;            || horas pendientes
    #13#6 = 0;                || horas registradas
    #13#7 = #13#5 + #13#6;    || horas computo total
    #13#8 = #22#2;            || horas diarias a cotizar
    #13#9 = ndias;            || tope dias a cotizar
    #13#10= #13#8 * #13#9;    || tope horas a cotizar
|SI #26#44 = "A";                   || SI ESTA EN ALTA
    |SI #13#7 > #13#10;
            #13#11 = #13#9;            || tope de dias (nomina) EL TOPE
            #13#12 = #13#10;           || horas a cotizar (nomina) EL TOPE
            #13#13 = #13#7 - #13#12;   || horas reservadas (DIFERENCIA)
    |SINO;
            nume1 = ((#13#7 / #13#8) - .5) ! 0;
            #13#11 = nume1;             || dias a cotizar (nomina) AJUSTANDO
            #13#12 = #13#11 * #13#8;    || horas a cotizar (nomina)
            #13#13 = #13#7 - #13#12;    || horas reservadas (RESTO)
/*
        |SI #13#7 ! 0;|Y #13#11 = 0;
            |AVISO;
                alfa1 = "    STrabajador [" + #12#0 + "." + #12#1 + "] con menos de [" + #13#8 + "] horas. Asignar 1 dia S/N: ";
            |CONFI alfa1;
            |SI FSalida = 0;
                    #13#12 = #13#7;
                    #13#7 = 0;
                    #13#13 = 0;
                    #13#11 = 1;
            |FINSI;
        |FINSI;
*/
    |FINSI;
|SINO;                              || SI ESTA EN BAJA
        #13#4 = "*" + #13#4;           || marca
        #13#11 = 0;                    || dias a cotizar (nomina) NINGUNO
        #13#12 = 0;                    || horas a cotizar (nomina) NINGUNA
        #13#13 = #13#7;                || horas reservadas (TODAS)
|FINSI;

|SI FEstado = 0;    |GRABA 020#13a;     |FINSI;
|SI FEstado ! 0;    |GRABA 020#13n;     |FINSI;
|LIBERA #13;
|FPRC;

|PROCESO mes_anterior1;
    #26#0 = #12#0;       || empresa
    #26#1 = #12#1;       || trabajador
|LEE 000#26=;
|SI FSalida = 0;
        #25#0 = #26#156; || cod.calendario
        #25#13= #0#0;    || a¤o
    |LEE 000#25=;
    |SI FSalida = 0;
            #22#0 = #26#42;
        |LEE 000#22=;
        |SI FSalida = 0;
            |HAZ mes_anterior2;
        |SINO;
                alfa1 = "    Clave cotizacion [" + #22#0 + "] inexistente ...";
            |MENAV alfa1;
        |FINSI;
    |SINO;
            alfa1 = "    Calendario [" + #25#0 + "-" + #25#13 + "] inexistente ...";
        |MENAV alfa1;
    |FINSI;
|SINO;
        alfa1 = "    Trabajador [" + #26#0 + "-" + #26#1 + "] inexistente ...";
    |MENAV alfa1;
|FINSI;
|FPRC;

|DEFBUCLE mes_anterior;
 #12#1;
 ;
 #0#8, #0#10, any_ant;
 #0#9, #0#11, any_ant;
 ;
 NULL, mes_anterior1;
|FIN;
____________________________________/ LECTURA Y CREACION SEGUN MES ACTUAL
|PROCESO Ajustando;
|SI #26#44 = "A";                   || SI ESTA EN ALTA
    |SI #13#7 > #13#10;
            #13#11 = #13#9;             || tope de dias (nomina) EL TOPE
            #13#12 = #13#10;            || horas a cotizar (nomina) EL TOPE
            #13#13 = #13#7 - #13#12;    || horas reservadas (DIFERENCIA)
    |SINO;
        |SI SwBaja = 0;                 || si no es baja ajusta a 8 horas
                nume1 = ((#13#7 / #13#8) - .5) ! 0;
                #13#11 = nume1;             || dias a cotizar (nomina) AJUSTANDO
                #13#12 = #13#11 * #13#8;    || horas a cotizar (nomina) AJUSTANDO
                #13#13 = #13#7 - #13#12;    || horas reservadas (RESTO)
            |SI #13#7 ! 0;|Y #13#11 = 0;
                |AVISO;
                    alfa1 = "    STrabajador [" + #12#0 + "." + #12#1 + "] con menos de [" + #13#8 + "] horas. Asignar 1 dia S/N: ";
                |CONFI alfa1;
                |SI FSalida = 0;
                        #13#12 = #13#7;
                        #13#7 = 0;
                        #13#13 = 0;
                        #13#11 = 1;
                |FINSI;
            |FINSI;
        |SINO;
                nume1 = ((#13#7 / #13#8) - .5) ! 0;
                #13#11 = nume1;             || dias a cotizar (nomina) AJUSTANDO
                #13#12 = #13#7;             || horas a cotizar (nomina) TODAS
                #13#13 = 0;                 || horas reservadas (RESTO)
        |FINSI;
    |FINSI;
|SINO;                              || SI ESTA EN BAJA
        #13#4 = "*" + #13#4;           || marca
        #13#11 = 0;                    || dias a cotizar (nomina) NINGUNO
        #13#12 = 0;                    || horas a cotizar (nomina) NINGUNA
        #13#13 = #13#7;                || horas reservadas (TODAS)
|FINSI;
|FPRC;

|PROCESO gra_hist_lin;
    cuantos = cuantos + 1;
|DDEFECTO #13;
    #13#0 = #12#0;  || empresa
    #13#1 = #12#1;  || trabajador
    #13#2 = #12#2;  || a¤o
    #13#3 = #0#1;   || mes
|LEE 101#13=;
    FEstado = FSalida;

    #13#4 = p_mes;
    #13#6 = shor;             || horas registradas
    #13#7 = #13#5 + #13#6;    || horas computo total
|SI #0#13 = "S";
    |HAZ Ajustando;
|SINO;
||        #13#11 = #13#9;       || tope de dias (nomina) EL TOPE
        nume1 = ((#13#7 / #13#8) - .5) ! 0;
        #13#11 = nume1;       || dias a cotizar (nomina) AJUSTANDO
    |SI #13#11 > #13#9;  #13#11 = #13#9;     |FINSI;
        #13#12 = #13#7;       || horas a cotizar (nomina)
        #13#13 = 0;           || horas reservadas (DIFERENCIA)
|FINSI;

|SI FEstado = 0;    |GRABA 020#13a;     |FINSI;
|SI FEstado ! 0;    |GRABA 020#13n;     |FINSI;
|LIBERA #13;
|FPRC;

|PROCESO gra_hist_cab;
    #26#0 = #20#1;       || empresa
    #26#1 = #20#2;       || trabajador
|LEE 000#26=;
|SI FSalida = 0;
        #25#0 = #26#156; || cod.calendario
        #25#13= #0#0;    || a¤o
    |LEE 000#25=;
    |SI FSalida = 0;
            #22#0 = #26#42;
        |LEE 000#22=;
        |SI FSalida = 0;
            |DDEFECTO #12;
                #12#0 = #26#0;
                #12#1 = #26#1;
                #12#2 = #0#0;
            |LEE 000#12=;
            |SI FSalida ! 0;
                |GRABA 020#12n;
                |SI FSalida = 0;
                    |HAZ gra_hist_lin;
                |FINSI;
            |SINO;
                |HAZ gra_hist_lin;
            |FINSI;
        |SINO;
                alfa1 = "    Clave cotizacion [" + #22#0 + "] inexistente ...";
            |MENAV alfa1;
        |FINSI;
    |SINO;
            alfa1 = "    Calendario [" + #25#0 + "-" + #25#13 + "] inexistente ...";
        |MENAV alfa1;
    |FINSI;
|SINO;
        alfa1 = "    Trabajador [" + #26#0 + "-" + #26#1 + "] inexistente ...";
    |MENAV alfa1;
|FINSI;
|FPRC;

|PROCESO rup_bar;
|DDEFECTO #20;   #20#0 = bar_ant;              |LEE 000#20=;

|SI swentra = 1;
        error = "    Tarjeta [" + bar_ant + "." + dia_ant + "." + lin_ant + "] sin registro de salida.";
    |MENAV error;
|SINO;
    |HAZ gra_hist_cab;
|FINSI;

    shor = 0;
    bar_ant = #11#7;
    swentra = 0;
|FPRC;

|PROCESO AjusteLineal;
     desde = (#pretopel#2 * 100) + #pretopel#3;
     hasta = (#pretopel#4 * 100) + #pretopel#5;
     |SI lectu ] desde;|Y lectu [ hasta;
          nhor = #pretopel#6 - hajus;
          nmin = #pretopel#7 - majus;
          |SI nmin < 0;
               nhor = nhor - 1;
               nmin = 60 + nmin;
          |FINSI;
          rajus = (nhor * 60) + nmin;

          hajus = #pretopel#6;
          majus = #pretopel#7;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE AjusteLineal;
     #pretopel#1;
     ;
     #pretopes#0, #pretopes#142,  0,  0;
     #pretopes#0, #pretopes#142, 23, 59;
     ;
     NULL, AjusteLineal;
|FIN;

|PROCESO ajustes1;
    aju_cod = #14#0;
    aju_dia = #14#142;
|ERROR10;
|FPRC;

|DEFBUCLE ajustes;
 #14#4;
 ;
 nCodAjus, fehas;
 nCodAjus, "01.01.1900";
 ;
 NULL, ajustes1;
|FIN;

|PROCESO lee_ajuste;
    alfa1 = #11#3;  alfa1 = ("00" + alfa1) % -102;
    alfa2 = #0#1;   alfa2 = ("00" + alfa2) % -102;
    alfa3 = alfa1 + "." + alfa2 + "." + #0#0;
    fehas = alfa3;

    aju_cod = -1;

     |SI swAjuste = 1;        || tengo en cuenta ajuste de Salida y de Entrada
          |SI swentra2 = 0;              || significa que es ENTRADA
               nCodAjus = #20#3;
          |SINO;
               nCodAjus = #20#6;
          |FINSI;
     |SINO;
          nCodAjus = #20#3;   || para todos el mismo
     |FINSI;

|BUCLE ajustes;

|DDEFECTO #14;
|SI aju_cod ! -1;
        #14#0 = aju_cod;
        #14#142 = aju_dia;
    |LEE 020#14=;
|FINSI;
|FPRC;

|PROCESO descuentos;
|HAZ lee_ajuste;

    rajus = 0;
    dlect = (#50#5 * 100) + #50#6;
    hlect = (#50#7 * 100) + #50#8;
|PARA para1 = 122; |SI para1 [ 125; |HACIENDO para1 = para1 + 1;
        dhora = para1;         dminu = para1 + 4;
        hhora = para1 + 8;     hminu = para1 + 12;
        rhora = para1 + 16;

        desde = (#14dhora * 100) + #14dminu;
        hasta = (#14hhora * 100) + #14hminu;
    |SI desde ! 0;|Y hasta ! 0;
        |SI hlect ] desde;|Y dlect [ hasta;
                rajus = #14rhora;
        |FINSI;
    |FINSI;
|SIGUE;
|FPRC;

|PROCESO ajustes;
     |HAZ lee_ajuste;

     rajus = 0;
     lectu = (hajus * 100) + majus;
     |PARA para1 = 2; |SI para1 [ 21; |HACIENDO para1 = para1 + 1;
          dhora = para1;         dminu = para1 + 20;
          hhora = para1 + 40;    hminu = para1 + 60;
          chora = para1 + 80;    cminu = para1 + 100;

          desde = (#14dhora * 100) + #14dminu;
          hasta = (#14hhora * 100) + #14hminu;
          |SI lectu ] desde;|Y lectu [ hasta;
               nhor = #14chora - hajus;
               nmin = #14cminu - majus;
               |SI nmin < 0;
                    nhor = nhor - 1;
                    nmin = 60 + nmin;
               |FINSI;
               rajus = (nhor * 60) + nmin;

               hajus = #14chora;
               majus = #14cminu;
               |SAL;
          |FINSI;
     |SIGUE;

     |BUCLE AjusteLineal;
|FPRC;

|PROCESO detalle;
    #50#0 = #11#3;       || dia
    #50#1 = hor_ant;     || desde hora (M)
    #50#2 = min_ant;     || hasta minutos (M)
    #50#3 = #11#4;       || hasta hora (M)
    #50#4 = #11#5;       || hasta minutos (M)
    #50#5 = hor_ant;     || desde hora (C)
    #50#6 = min_ant;     || hasta minutos (C)
    #50#7 = #11#4;       || hasta hora (C)
    #50#8 = #11#5;       || hasta minutos (C)
    #50#9 = 0;           || minutos ajustados
    #50#10 = 0;          || fraccion horas
    #50#11 = 0;          || fraccion minutos
    #50#12 = 0;          || tiempo en minutos
    #50#13 = 0;          || tiempo en horas


    swentra2 = 0;
    hajus = #50#5;  majus = #50#6;
|HAZ ajustes;                      || ajusta el desde
    #50#9 = #50#9 - rajus;
    #50#5 = hajus;  #50#6 = majus;

    swentra2 = 1;
    hajus = #50#7;  majus = #50#8;
|HAZ ajustes;                      || ajusta el hasta
    #50#9 = #50#9 + rajus;
    #50#7 = hajus;  #50#8 = majus;

    nhor = #50#7 - #50#5;
    nmin = #50#8 - #50#6;
|SI nmin < 0;    nhor = nhor - 1;    nmin = 60 + nmin;   |FINSI;

|HAZ descuentos;
    #50#15 = rajus;
    nmin = nmin + #50#15;
|SI nmin < 0;    nhor = nhor - 1;    nmin = 60 + nmin;   |FINSI;

|SI nmin < 0;  nmin = 0; |FINSI;
|SI nhor < 0;  nhor = 0; |FINSI;

    #50#10 = nhor;
    #50#11 = nmin;
    #50#12 = (nhor * 60) + nmin;
    #50#13 = nhor + (nmin / 60);

    shor = shor + #50#13;     || total por tarjeta
|FPRC;

|PROCESO SiEsAlta;
    dalta = 1;
    talta = 0;
    alfa1 = #26#36 % -104;    nume1 = alfa1;
    alfa1 = #26#36 % 402;     nume2 = alfa1;
|SI nume1 = #0#0;|Y nume2 = #0#1;            || si es alfa este mes
        alfa1 = #26#36 % 102;
        talta = alfa1;
        dalta = talta;
        talta = talta - 1;
|FINSI;
|FPRC;

|PROCESO SiEsBaja;
    SwBaja = 0;
    alfa1 = #26#37 % -104;    nume1 = alfa1;
    alfa1 = #26#37 % 402;     nume2 = alfa1;
|SI nume1 = #0#0;|Y nume2 = #0#1;            || si es baja este mes
        alfa1 = #26#37 % 102;
        topes = alfa1;
        SwBaja = 1;
|FINSI;
|FPRC;

|PROCESO movimientos1;
|DDEFECTO #20; #20#0 = #11#7;                |LEE 000#20=;  || lee tarjetas
|SI #20#1 < #0#8; |O #20#1 > #0#9;      |ACABA;   |FINSI;   || sel.empresas
|SI #20#2 < #0#10;|O #20#2 > #0#11;     |ACABA;   |FINSI;   || sel.trabajad.

|DDEFECTO #26; #26#0 = #20#1; #26#1 = #20#2; |LEE 000#26=;  || lee trabajad.

    topes = 99;
|HAZ SiEsBaja;
|SI topes < #11#3;       || cuando excede de la fecha de baja
    ||ACABA;
|FINSI;

|SI sw_rup = 0;
        sw_rup = 1;
        bar_ant = #11#7;
|FINSI;

|SI bar_ant ! #11#7;
    |HAZ rup_bar;
|FINSI;

|SI swentra = 1;
        swentra = 0;
    |HAZ detalle;
|SINO;
        swentra = 1;

        hor_ant = #11#4;
        min_ant = #11#5;
        lin_ant = #11#2;
        dia_ant = #11#3;
|FINSI;
|FPRC;

|DEFBUCLE movimientos;
 #11#2;
 ;
 #0#0, #0#1, #0#4,  1,  0,  0,  0;
 #0#0, #0#1, #0#5, 31, 23, 59, 59;
 ;
 NULL, movimientos1;
|FIN;

|PROCESO Control;
     |LEE 000#precontr.p;
     |SI FSalida ! 0;    || No existe control de la aplicacion, entonces
          swAjuste = 0;  || el Codigo de Ajuste de Salida es el de Entrada
     |SINO;              || esto para los que usaran el programa antes de
          |SI #precontr#1 = "S";   || meter el codigo de salida, cuando
               swAjuste = 0;       || solo habia de entrada 26.05.2004
          |SINO;
               swAjuste = 1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO tipo3; |TIPO 3;
|SI #0#12 = "NO";   |ACABA;   |FINSI;

    any_ant = #0#0;
    mes_ant = #0#1 - 1;
|SI mes_ant = 0;
        mes_ant = 12;
        any_ant = any_ant - 1;
|FINSI;

|ABRET #12;    || computo cabs.
|ABRET #13;    || computo lins.
|ABRET #14;    || ajustes
|ABRE #20;     || cod.barras
|ABRE #21;     || tareas
|ABRE #22;     || cotizacion claves
|ABRE #25;     || calendario laboral
|ABRE #26;     || trabajadores
|ABRE #precontr;
|HAZ Control;
|BUCLE mes_anterior;
|BUCLE movimientos;
|SI sw_rup ! 0;
    |HAZ rup_bar;
        alfa1 = "    Procesados [" + cuantos + "] trabajadores ...";
    |MENAV alfa1;
|FINSI;
|CIERRAT #12;
|CIERRAT #13;
|CIERRAT #14;
|CIERRA #20;
|CIERRA #21;
|CIERRA #22;
|CIERRA #25;
|CIERRA #26;
|CIERRA #precontr;
|FPRC;
