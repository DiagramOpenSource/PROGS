|FICHEROS;
     preinfse #0;
     premvtoc #1;
     premvtol #2;
     presecci #3;
     pretmp07 #4;
     prequiva #5;
|FIN;

|VARIABLES;
     &enCoste = 0;
     &enHora = 0;
     nHoras = 0;
     nHoraD = 0;
     nHoraH = 0;
     nDAno = 0;
     nDMes = 0;
     nDDia = 0;
     nHAno = 0;
     nHMes = 0;
     nHDia = 0;
     aAlfa = "";
     aInf = "";
     nHora = 0;
     nConta = 0;
     nSeccion = -1;
     nHay = 0;
|FIN;

|PROCESO Movimi;
     |SI #2#10 = "E";
          |SELECT #1#4;
          nConta = nConta + 1;
          #4#0 = nConta;
          #4#1 = #2#11;
          #4#2 = #2#7;
          #4#3 = "E";
          aAlfa = (("00"+#2#3)%-102)+(("00"+#2#1)%-102)+(("0000"+#2#0)%-104);
          #4#4 = aAlfa;
          #4#5 = #2#4;
          #4#6 = #2#5;
          #4#7 = #2#6;
          |GRABA 020#4n;
     |SINO;
          |SI #2#10 = "S";
               |SELECT #2#4;
               #4#2 = #2#7;
               #4#3 = "E";
               #4#0 = 9999999999999;
               |LEE 000#4];
               |SI FSalida = 0;
                    |LEE 000#4a;
               |SINO;
                    |LEE 000#4u;
               |FINSI;
               |SI FSalida = 0; |Y #4#2 = #2#7; |Y #4#3 = "E";
                    aAlfa = (("00"+#2#3)%-102)+(("00"+#2#1)%-102)+(("0000"+#2#0)%-104);
                    #4#3 = "S";
                    #4#8 = aAlfa;
                    #4#9 = #2#4;
                    #4#10 = #2#5;
                    #4#11 = #2#6;

                    |SI #4#4 = #4#8;
                         nHoraD = #4#5 + (#4#6 / 60) + (#4#7 / 3600);
                         nHoraH = #4#9 + (#4#10 / 60) + (#4#11 / 3600);
                         #4#12 = nHoraH - nHoraD;
                    |SINO;
                         nHoras = #4#8 - #4#4; nHoras = nHoras - 1;
                         nHoraD = #4#5 + (#4#6 / 60) + (#4#7 / 3600);
                         nHoraH = #4#9 + (#4#10 / 60) + (#4#11 / 3600);
                         #4#12 = 24 - nHoraD + nHoraH + (nHoras * 24);
                    |FINSI;

                    #3#0 = #4#1;
                    |LEE 000#3=;
                    |SI FSalida ! 0; |DDEFECTO #3; |FINSI;

                    #4#13 = #4#12 * #3#2;
                    |GRABA 020#4a;
               |FINSI;
          |SINO;              || Anulacion
               |SELECT #3#4;
               #4#2 = #2#7;
               #4#0 = 9999999999999;
               |LEE 000#4];
               |SI FSalida = 0;
                    |LEE 000#4a;
               |SINO;
                    |LEE 000#4u;
               |FINSI;
               |SI FSalida = 0; |Y #4#2 = #2#7;
                    |SI #4#3 = "E";
                         |BORRA 020#4a;
                    |SINO;
                         #4#3 = "E";
                         #4#8 = "01.01.0000";
                         #4#9 = 0;
                         #4#10 = 0;
                         #4#11 = 0;
                         #4#12 = 0;
                         #4#13 = 0;
                         |GRABA 020#4a;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE Movimi;
     #2#1;
     11;
     nDAno, nDMes, nDDia, 000001, #0#2;
     nHAno, nHMes, nHDia, 999999, #0#3;
     ;
     NULL, Movimi;
|FIN;

|PROCESO Tmp;
     |SI #4#3 ! "S";
          |ACABA;        || No ha realizado la entrada
     |FINSI;

     |SI #0#4 = "D";
          |DDEFECTO #3;
          #3#0 = #4#1;
          |LEE 000#3=;

          |DDEFECTO #5;
          #5#0 = #4#2;
          |LEE 000#5=;

          |PRINT;
     |SINO;
          |SI nSeccion ! -1; |Y nSeccion ! #4#1;
               |PRINT;
               enHora = 0;
               enCoste = 0;
          |FINSI;
          enHora = enHora + #4#12;
          enCoste = enCoste + #4#13;
          nSeccion = #4#1;
          |DDEFECTO #3;
          #3#0 = #4#1;
          |LEE 000#3=;
     |FINSI;
     nHay = 1;
|FPRC;

|DEFBUCLE Tmp;
     #4#4;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tmp;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |IMPRE 0;
     |SI FSalida = 0;
          |SI #0#4 = "D";
               aInf = "preinfsd";
          |SINO;
               aInf = "preinfst";
          |FINSI;
          |INFOR aInf;
          |SI FSalida = 0;
               aAlfa = Usuario;
               |NOME_DAT #4 aAlfa;
               aAlfa = #0#0 % 102; nDDia = aAlfa;
               aAlfa = #0#0 % 402; nDMes = aAlfa;
               aAlfa = #0#0 % 704; nDAno = aAlfa;
               aAlfa = #0#1 % 102; nHDia = aAlfa;
               aAlfa = #0#1 % 402; nHMes = aAlfa;
               aAlfa = #0#1 % 704; nHAno = aAlfa;
               |ABRE #3;
               |ABRE #4;
               |BUCLE Movimi;
               |CIERRA #4;

               |ABRE #5;
               |BUCLE Tmp;
               |CIERRA #5;
               |CIERRA #3;

               |SI #0#4 ! "D"; |Y nSeccion ! -1;
                    |PRINT;
               |FINSI;
               |SI nHay ! 0; |PIEINF; |FINSI;
               |FININF;

               |DELINDEX #4;
          |SINO;
               aAlfa = "0000Error en informe '" + aInf + "'";
               |MENAV aAlfa;
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;
