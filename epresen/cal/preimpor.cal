|FICHEROS;
    preimpor #0;

    premvtoc #10;   || movimientos cabs.
    premvtol #11;   || movimientos lins.

    prequiva #20;   || codigos de tarjetas
    pretarea #21;   || codigos de incidencias
    pretermi #22;   || codigos de terminal
    labempre #23;   || empresas
    labtraba #24;   || trabajadores

    pretmp02 #51;   || tmp. errores repetidos
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    fichero = "";
    fabre = "";
    field = "";
    fcanl = "";
    bar = "";
    tar = "";
    any = "";
    mes = "";
    dia = "";
    hor = "";
    min = "";
    seg = "";
    ter = "";
    nregi = 0;
    topemes = 0;
    bisiesto = 0;
    nerro = 0;
    linea = 0;
    flg = "";
    sw_graba = 1;
    aAviso = "";
    nError = 0;
     aCampo = "";
     aTab = "";
     nTab = 0;
     swSigue = 0;
     aLinea = "";
     aLon = "";
     aLonCampo = "";
     nLon = 0;
     nLonCampo = 0;
     nPos = 0;
     nCaptura = 0;
     aCaracter = "";
     nHandle = 0;
|FIN;
____________________________________/
|PROCESO mayus; |TIPO 0;
    #0Campo = #0Campo > #0Campo;
|PINTA #0Campo;
|FPRC;

|PROCESO ponbarra; |TIPO 0;
    alfa1 = #0Campo;
|QBF alfa1;
    alfa2 = alfa1 % -101;
|SI alfa2 ! "/";
        alfa1 = alfa1 + "/";
        #0Campo = alfa1;
    |PINTA #0Campo;
|FINSI;
|FPRC;

____________________________________/
|PROCESO graba_lins;
    #11#0 = #10#0;
    #11#1 = #10#1;
    #11#3 = #10#2;
    #11#2 = 999999;
|LEE 000#11];
|SI FSalida = 0;
    |LEE 000#11a;
|SINO;
    |LEE 000#11u;
|FINSI;
|SI FSalida = 0;|Y #11#0 = #10#0;|Y #11#1 = #10#1;|Y #11#3 = #10#2;
        linea = #11#2 + 1;
|SINO;
        linea = 1;
|FINSI;

|DDEFECTO #11;
    #11#0 = #10#0;       || a¤o
    #11#1 = #10#1;       || mes
    #11#3 = #10#2;       || dia
    #11#4 = hor;         || hora
    #11#5 = min;         || minutos
    #11#6 = seg;         || segundos
    #11#7 = bar;         || codigo tarjeta
    #11#8 = tar;         || codigo incidencia
    #11#9 = ter;         || codigo terminal
    #11#10 = flg;        || flag incidencia
|SELECT #5#11;
|LEE 000#11=;
|SI FSalida ! 0;
     #11#2 = linea;       || linea
     |GRABA 020#11n;
     |SI FSalida = 0;
          nregi = nregi + 1;
     |FINSI;
|SINO;
     nError = nError + 1;
|FINSI;
|SELECT #1#11;
|FPRC;

|PROCESO graba_cabs;
    #10#0 = any;
    #10#1 = mes;
    #10#2 = dia;
|LEE 000#10=;
|SI FSalida ! 0;
    |GRABA 020#10n;
|FINSI;
|FPRC;

|PROCESO imp_error;
    #51#0 = alfa1;
|LEE 000#51=;
|SI FSalida ! 0;
    |GRABA 020#51n;
    |IMPRIME alfa1;
|FINSI;
    nerro = nerro + 1;
|FPRC;

|PROCESO diasmes;
    nume1 = any;
    bisiesto = nume1 $ 4;
|SI bisiesto ! 0;   bisiesto = 0;  |SINO;    bisiesto = 1;  |FINSI;
    nume1 = mes;
|SI nume1 = 1; topemes = 31;  |FINSI;
|SI nume1 = 2; topemes = 28 + bisiesto; |FINSI;
|SI nume1 = 3; topemes = 31;  |FINSI;
|SI nume1 = 4; topemes = 30;  |FINSI;
|SI nume1 = 5; topemes = 31;  |FINSI;
|SI nume1 = 6; topemes = 30;  |FINSI;
|SI nume1 = 7; topemes = 31;  |FINSI;
|SI nume1 = 8; topemes = 31;  |FINSI;
|SI nume1 = 9; topemes = 30;  |FINSI;
|SI nume1 = 10; topemes = 31;  |FINSI;
|SI nume1 = 11; topemes = 30;  |FINSI;
|SI nume1 = 12; topemes = 31;  |FINSI;
|FPRC;

|PROCESO compru_hor;
    nume1 = any;
|SI nume1 < 0;|O nume1 > 9999;
        alfa1 = "    A¤o [" + any + "] err¢neo ...";  |HAZ imp_error;
|FINSI;
    nume1 = mes;
|SI nume1 < 1;|O nume1 > 12;
        alfa1 = "    Mes [" + mes + "] err¢neo ...";  |HAZ imp_error;
|FINSI;
|HAZ diasmes;
    nume1 = dia;
|SI nume1 < 1;|O nume1 > topemes;
        alfa1 = "    Dia [" + dia + "] err¢neo ...";  |HAZ imp_error;
|FINSI;
    nume1 = hor;
|SI nume1 < 0;|O nume1 > 23;
        alfa1 = "    Hora [" + hor + "] err¢nea ...";  |HAZ imp_error;
|FINSI;
    nume1 = min;
|SI nume1 < 0;|O nume1 > 59;
        alfa1 = "    Minutos [" + min + "] err¢neos ...";  |HAZ imp_error;
|FINSI;
    nume1 = seg;
|SI nume1 < 0;|O nume1 > 59;
        alfa1 = "    Segundos [" + seg + "] err¢neos ...";  |HAZ imp_error;
|FINSI;
|FPRC;

|PROCESO compru_ter;
    #22#0 = ter;
|LEE 000#22=;
|SI FSalida ! 0;
        alfa1 = "Terminal [" + #22#0 + "] inexistente.";
    |HAZ imp_error;
|FINSI;
|FPRC;

|PROCESO compru_tar;
    #21#0 = tar;
|LEE 000#21=;
|SI FSalida ! 0;
        alfa1 = "Incidencia [" + #21#0 + "] inexistente.";
    |HAZ imp_error;
|FINSI;
|FPRC;

|PROCESO compru_bar;
    #20#0 = bar;
|LEE 000#20=;
|SI FSalida = 0;
        #23#0 = #20#1;
    |LEE 000#23=;
    |SI FSalida ! 0;
            alfa1 = "Empresa [" + #23#0 + "] inexistente.";
        |HAZ imp_error;
    |SINO;
            #24#0 = #20#1;
            #24#1 = #20#2;
        |LEE 000#24=;
        |SI FSalida ! 0;
                alfa1 = "Trabajador [" + #23#0 + "] inexistente.";
            |HAZ imp_error;
        |FINSI;
    |FINSI;
|SINO;
        alfa1 = "C¢digo de barras [" + #20#0 + "] inexistente.";
    |HAZ imp_error;
|FINSI;
|FPRC;

/*
|| de antes de que David metiera mano, cuando me llegaba el ficherito que
|| salia del programa

|PROCESO Campos;
     aTab = aCampo % -101;
     aCampo = aCampo - aTab;
     |SI nTab = 1;            || codigo terminal
          |SI aCampo = "Count";   || ultima linea
               swSigue = 0;
          |FINSI;
          ter = aCampo;
     |FINSI;

     |SI nTab = 3;            || fecha/hora
          aLonCampo = aCampo % 0;
          nLonCampo = aLonCampo;
          dia = aCampo % 102;
          mes = aCampo % 402;
          any = aCampo % 704;
          |SI nLonCampo = 18;             || hora H:MM:SS
               hor = "0" + (aCampo % 1201);
               min = aCampo % 1402;
               seg = aCampo % 1702;
          |SINO;                          || hora HH:MM:SS
               hor = aCampo % 1202;
               min = aCampo % 1502;
               seg = aCampo % 1802;
          |FINSI;
     |FINSI;

     |SI nTab = 4;            || incidencia
          |SI aCampo ! "Access(Fingerprint)";    || acceso, en principio no controlare
               swSigue = 0;             || nada mas
          |FINSI;
          tar = "0001";
     |FINSI;

     |SI nTab = 5;            || usuario: es el codgio de tarjeta;
          bar = aCampo;
     |FINSI;
|FPRC;
*/

|PROCESO Campos;
     aTab = aCampo % -101;
     aCampo = aCampo - aTab;
     |SI nTab = 1;            || codigo terminal
          |SI aCampo = "Count";   || ultima linea
               swSigue = 0;
          |FINSI;
          ter = aCampo;
     |FINSI;

     |SI nTab = 2;            || fecha/hora
          dia = aCampo % 102;
          mes = aCampo % 402;
          any = aCampo % 704;
          hor = aCampo % 1202;
          min = aCampo % 1502;
          seg = aCampo % 1802;
     |FINSI;

     |SI nTab = 3;            || incidencia
          tar = "0001";
     |FINSI;

     |SI nTab = 4;            || usuario: es el codgio de tarjeta;
          bar = aCampo;
     |FINSI;
|FPRC;

|PROCESO LimpiaVar;
     bar = "";
     dia = "";
     mes = "";
     any = "";
     hor = "";
     min = "";
     seg = "";
     tar = "";
     ter = "";
     flg = "";
|FPRC;

|PROCESO separa_03;
     |HAZ LimpiaVar;
     aLinea = field;
     |QBF aLinea;
     aLinea = aLinea + &9;
     aLon = aLinea % 0;
     nLon = aLon;
     nLon = nLon + 1;
     nTab = 0;
     aTab = &9;
     swSigue = 1;   || por defecto linea correcta
     |PARA nPos = 1; |SI nPos [ nLon; |HACIENDO nPos = nPos + 1;
          nCaptura = (nPos * 100) + 1;
          aCaracter = aLinea % nCaptura;
          aCampo = aCampo + aCaracter;
          |SI aCaracter = aTab;
               nTab = nTab + 1;
               |HAZ Campos;
               aCampo = "";
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO separa_02;

    bar = field % 1204;       || codigo tarjeta
    dia = field % 1602;       || dia
    mes = field % 1802;       || mes
    any = field % 2002;       || a¤o
    hor = field % 2202;       || hora
    min = field % 2402;       || minutos
    seg = "00";               || segundos
    tar = field % 3104;       || incidencia
    ter = field % 103;        || terminal
    flg = field % 3001;       || flag incidencia

    nume1 = any;
|SI nume1 < 80; nume1 = 2000 + nume1; |SINO; nume1 = 1900 + nume1; |FINSI;
    any = nume1;
|FPRC;

|PROCESO separa_01;
    bar = field % 304;        || codigo tarjeta
    dia = field % 702;        || dia
    mes = field % 902;        || mes
    any = field % 1102;       || a¤o
    hor = field % 1302;       || hora
    min = field % 1502;       || minutos
    seg = "00";               || segundos
    tar = field % 1702;       || incidencia
    ter = "001";              || terminal

    nume1 = any;
|SI nume1 < 80; nume1 = 2000 + nume1; |SINO; nume1 = 1900 + nume1; |FINSI;
    any = nume1;
|FPRC;

|PROCESO registro;
     |SI #0#5 = 1;  |HAZ separa_01;     |FINSI;
     |SI #0#5 = 2;  |HAZ separa_02;     |FINSI;
     |SI #0#5 = 3;
          |HAZ separa_03;
          |SI swSigue = 0;
               |ACABA;
          |FINSI;
     |FINSI;
     |HAZ compru_bar;
     |HAZ compru_tar;
     |HAZ compru_ter;
     |HAZ compru_hor;

     |HAZ graba_cabs;
     |HAZ graba_lins;
|FPRC;
/*
|PROCESO tipo3;
     fichero = #0#2;                     |QBF fichero;
     fichero = fichero + #0#3;           |QBF fichero;
     fabre = "rt  " + fichero;
     |FABRE fabre;
     |SI FSalida ] 0;
          fcanl = FSalida;
          field = fcanl + " 250";
          |FLEE field;
          |PARA; |SI FSalida ] 0; |HACIENDO;
               |HAZ registro;
               field = fcanl + " 250";
               |FLEE field;
          |SIGUE;
          |FCIERRA fcanl;
          |SI #0#4 = "S";
               |FBORRA fichero;
          |FINSI;
          alfa1 = "    Importados [" + nregi + "] registros ...";
          |MENAV alfa1;
     |SINO;
          alfa1 = "    [" + (fabre % 5) + "] inaccesible ...";
          |MENAV alfa1;
     |FINSI;
     Pos = -1;
     |SI nError > 0;
          aAviso = nError;
          aAviso = "    " + nError + " registros no importados por coincidencia con el Historico.";
          |MENAV aAviso;
     |FINSI;
|FPRC;
*/

|PROCESO tipo3;
     fichero = #0#2;                     |QBF fichero;
     fichero = fichero + #0#3;           |QBF fichero;
     |XABRE "", fichero, nHandle;
     |SI FSalida ] 0;
          fcanl = FSalida;
          field = fcanl + " 250";
          |XLEE nHandle, 250, field;
          |PARA; |SI FSalida ] 0; |HACIENDO;
               |HAZ registro;
               |XLEE nHandle, 250, field;
          |SIGUE;
          |XCIERRA nHandle;
          |SI #0#4 = "S";
               |RFBORRA fichero;
          |FINSI;
          alfa1 = "    Importados [" + nregi + "] registros ...";
          |MENAV alfa1;
     |SINO;
          alfa1 = "    [" + fichero + "] inaccesible ...";
          |MENAV alfa1;
     |FINSI;
     Pos = -1;
     |SI nError > 0;
          aAviso = nError;
          aAviso = "    " + nError + " registros no importados por coincidencia con el Historico.";
          |MENAV aAviso;
     |FINSI;
|FPRC;

|PROGRAMA;
     |CLS;
     |CABEZA "Importar Descarga";
     |PINPA #0#0;
     |DDEFECTO #0;
     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0;    |GRABA 020#0b; |FINSI;
     #0#1 = "SI";
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |SI #0#1 = "SI";
               |IMPRE 0;
               |SI FSalida = 0;
                    |ABRE #10;       || movtos. cabs.
                    |ABRE #11;       || movtos. lins.
                    |ABRE #20;       || cod.tarjetas
                    |ABRE #21;       || cod.incidencias
                    |ABRE #22;       || cod.terminales
                    |ABRE #23;       || empresas
                    |ABRE #24;       || trabajadores
                    |DELINDEX #51;
                    alfa1 = Usuario;     |QBF alfa1;
                    alfa1 = "v" + alfa1;
                    || |PATH_DAT #51 alfa1;  || Esto no me lo creo
                    |NOME_DAT #51 alfa1;     || Esto si me lo creo
                    |ABRE #51;

                    |HAZ tipo3;

                    |CIERRA #51;
                    |DELINDEX #51;
                    |CIERRA #10;
                    |CIERRA #11;
                    |CIERRA #20;
                    |CIERRA #21;
                    |CIERRA #22;
                    |CIERRA #23;
                    |CIERRA #24;
                    alfa1 = "----------------------------"; |IMPRIME alfa1;
                    alfa1 = "N£mero de errores : " + nerro; |IMPRIME alfa1;
                    alfa1 = &12;                            |IMPRIME alfa1;
               |SINO;
                    |MENAV "    Proceso abortado ...";
               |FINSI;
               |FINIMP;
          |FINSI;
          |GRABA 020#0a;
     |FINSI;
     |LIBERA #0;
     |CIERRA #0;
|FPRO;
