|FICHEROS;
    preinmes #0;

    premvtol #11;   || movtos. lins.
    pretopes #12;   || ajustes de marcado

    prequiva #20;   || tarjetas
    pretarea #21;   || incidencias
    labempre #23;   || empresas
    labtraba #24;   || trabajadores
    nomtrcal #25;   || calendario laboral

    pretmp04 #50;   || def de impresion del detalle
    pretopel;
    precontr;
|FIN;

|VARIABLES;
    aAlfa = "";

    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    nume1 = 0;
    nume2 = 0;
    fech1 = @;

    &p_mes = "";
    nhor = 0;
    nmin = 0;
    &error = "";
    &swlin = 0;
    swentra = 0;
    elmes = 0;
    eldia = 0;
    fehas = @;
    emp_ant = 0;
    tra_ant = 0;

     swEntrada = 0;
     swSalida = 0;
     nHoras = 0;
     nMinutos = 0;
     nHoraEnt = 0;
     nHoraSal = 0;
     nMinEnt = 0;
     nMinSal = 0;
     &nMinutosTot = 0;
     &nTarjetaAnt = 0;
     nDiaAnt = 0;
     aDiaAnt = "";
     &aError = "";
     swError = 0;
     swErrorNoc = 0;
     swControl = 0;
     swControl1 = 0;
     swNoct = 0;
     swPrimero = 0;
     aTipodia = "";
     &nMinutosLab = 0;
     &nMinutosFes = 0;
     &nMinutosFesNoc = 0;
     &nNoches = 0;
     &nDias = 0;
     &nFalta = 0;
     nDiasComputo = 0;
     &aNombre = "";
     i = 0;
     aFecha = "";
     fFecha = @;
     nFecha = 0;
     aDiaa = "";
     aMesa = "";
     aTipodia2 = "";
     &aAnyo = "";
     aTurnoAnt = "";
     nCalAnt = 0;
     swUltimo = 0;
     aESAnt = "";
     nDiaCont = 0;

     aSalida = "";       || a partir de aqui variables para ajustes
     nSalida = 0;
     aEntrada = "";
     nEntrada = 0;
     para1 = 0;
     nHorDes = 0;
     nMinDes = 0;
     nHorHas = 0;
     nMinHas = 0;
     nHorCon = 0;
     nMinCon = 0;
     aDesdeAju = "";
     nDesdeAju = 0;
     aHastaAju = "";
     nHastaAju = 0;
     nAjuste = 0;
     nMinAju = 0;
     swLab = 0;
     &nDiasLab = 0;
     swEntAju = 0;
     swSalAju = 0;

     swAjuste = 0;
     nCodAjus = 0;
|FIN;
____________________________________/
|PROCESO defecto; |TIPO 7; ||calcula el a¤o por defecto segun fecha sistema
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % -104;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO mirames; |TIPO 7; ||CALCULA EL MES POR DEFECTO SEGUN FECHA SISTEMA
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % 402;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO pintames; |TIPO 0;      || PINTA EL MES LITERAL
|SI #0Campo =  1;  p_mes = "ENERO     ";  |FINSI;
|SI #0Campo =  2;  p_mes = "FEBRERO   ";  |FINSI;
|SI #0Campo =  3;  p_mes = "MARZO     ";  |FINSI;
|SI #0Campo =  4;  p_mes = "ABRIL     ";  |FINSI;
|SI #0Campo =  5;  p_mes = "MAYO      ";  |FINSI;
|SI #0Campo =  6;  p_mes = "JUNIO     ";  |FINSI;
|SI #0Campo =  7;  p_mes = "JULIO     ";  |FINSI;
|SI #0Campo =  8;  p_mes = "AGOSTO    ";  |FINSI;
|SI #0Campo =  9;  p_mes = "SEPTIEMBRE";  |FINSI;
|SI #0Campo = 10;  p_mes = "OCTUBRE   ";  |FINSI;
|SI #0Campo = 11;  p_mes = "NOVIEMBRE ";  |FINSI;
|SI #0Campo = 12;  p_mes = "DICIEMBRE ";  |FINSI;
|PINTA 1147, p_mes;
aAnyo = #0#0;
|FPRC;

|PROCESO mayus; |TIPO 0;
    #0Campo = #0Campo > #0Campo;
|PINTA #0Campo;
|FPRC;
____________________________________/
|PROCESO calcula_lab;
     |SI swLab = 0;
          |DDEFECTO #25;
          #25#0 = #50#7;
          #25#13 = #0#0;
          |LEE 000#25=;
          elmes = #0#1;
          |PARA i = 1; |SI i [ 31; |HACIENDO i = i + 1;
               eldia = (i * 100) + 1;
               aTipodia = #25elmes % eldia;
               |SI aTipodia = "0"; nDiasLab = nDiasLab + 1; |FINSI;
          |SIGUE;
          nDiasComputo = nDiasLab;
          swLab = 1;          || para que solo lo haga una vez
     |FINSI;
|FPRC;

|PROCESO busca_cal;
     |DDEFECTO #25;
     #25#0 = #50#7;
     #25#13 = #0#0;
     |LEE 000#25=;
     |HAZ calcula_lab;
     elmes = #0#1;
     eldia = (#50#2 * 100) + 1;
     aTipodia = #25elmes % eldia;
|FPRC;

|PROCESO busca_cal2;
     |DDEFECTO #25;
     alfa1 = #50#2; alfa1 = "00" + alfa1; alfa1 = alfa1 % -102;
     alfa2 = #0#1;  alfa2 = "00" + alfa2; alfa2 = alfa2 % -102;
     alfa3 = #0#0;
     aFecha = alfa1 + "." + alfa2 + "." + alfa3;
     fFecha = aFecha;
     nFecha = fFecha;
     nFecha = nFecha - 1;
     fFecha = nFecha;
     aFecha = fFecha;
     aDiaa = aFecha % 102;         || todo esto para meterme en dos variables
     aMesa = aFecha % 402;          || dia y mes anterior a la que se calcula
     nume1 = aDiaa;
     nume2 = aMesa;

     #25#0 = #50#7;
     #25#13 = #0#0;
     |LEE 000#25=;
     elmes = nume2;
     eldia = (nume1 * 100) + 1;
     aTipodia = #25elmes % eldia;
|FPRC;

|PROCESO busca_cal3;
     |DDEFECTO #25;
     #25#0 = nCalAnt;
     #25#13 = #0#0;
     |LEE 000#25=;
     elmes = #0#1;
     eldia = (nDiaAnt * 100) + 1;
     aTipodia = #25elmes % eldia;
|FPRC;

|PROCESO inicializa;          || inicializa variables para ajustes
     i = 0;
     para1 = 0;
     alfa1 = "";
     alfa2 = "";
     alfa3 = "";
     nHorDes = 0;
     nMinDes = 0;
     nHorHas = 0;
     nMinHas = 0;
     nHorCon = 0;
     nMinCon = 0;
     aDesdeAju = "";
     nDesdeAju = 0;
     aHastaAju = "";
     nHastaAju = 0;
     nMinAju = 0;
     aEntrada = "";
     nEntrada = 0;
     nSalida = 0;
     nAjuste = 0;
     swEntAju = 0;
     swSalAju = 0;
|FPRC;

|PROCESO AjusteLineal;
     nHorDes = #pretopel#2;
     nMinDes = #pretopel#3;
     nHorHas = #pretopel#4;
     nMinHas = #pretopel#5;
     nHorCon = #pretopel#6;
     nMinCon = #pretopel#7;
     nume1 = nHorDes + nMinDes + nHorHas + nMinHas + nHorCon + nMinCon;
     |SI nume1 = 0;
          |ERROR;
     |FINSI;

     alfa1 = nHorDes;    alfa1 = "00" + alfa1;    alfa1 = alfa1 % -102;
     alfa2 = nMinDes;    alfa2 = "00" + alfa2;    alfa2 = alfa2 % -102;
     aDesdeAju = alfa1 + alfa2;
     nDesdeAju = aDesdeAju;      || paso horas y min a numeros(ajustes)

     alfa1 = nHorHas;    alfa1 = "00" + alfa1;    alfa1 = alfa1 % -102;
     alfa2 = nMinHas;    alfa2 = "00" + alfa2;    alfa2 = alfa2 % -102;
     aHastaAju = alfa1 + alfa2;
     nHastaAju = aHastaAju;      || paso horas y min a numeros(ajustes)

     alfa1 = nHoraEnt;    alfa1 = "00" + alfa1;    alfa1 = alfa1 % -102;
     alfa2 = nMinEnt;    alfa2 = "00" + alfa2;    alfa2 = alfa2 % -102;
     aEntrada = alfa1 + alfa2;
     nEntrada = aEntrada;      || paso horas y min a numeros(entrada)

     alfa1 = nHoraSal;    alfa1 = "00" + alfa1;    alfa1 = alfa1 % -102;
     alfa2 = nMinSal;    alfa2 = "00" + alfa2;    alfa2 = alfa2 % -102;
     aSalida = alfa1 + alfa2;
     nSalida = aSalida;        || paso horas y minu a numeros(salida)

     |SI swEntAju = 0;
          |SI nEntrada ] nDesdeAju; |Y nEntrada [ nHastaAju;
               nHoraEnt = nHorCon;
               nMinEnt = nMinCon;
               swEntAju = 1;
          |FINSI;
     |FINSI;

     |SI swSalAju = 0;
          |SI nSalida ] nDesdeAju; |Y nSalida [ nHastaAju;
               nHoraSal = nHorCon;
               nMinSal = nMinCon;
               swSalAju = 1;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE AjusteLineal;
     #pretopel#1;
     ;
     #pretopes#0, #pretopes#142,  0,  0;
     #pretopes#0, #pretopes#142, 23, 59;
     ;
     NULL, AjusteLineal;
|FIN;

|PROCESO AjusteClasico;
     |PARA i = 2; |SI i [ 21; |HACIENDO i = i + 1;
          para1 = i;
          nHorDes = #12para1; para1 = para1 + 20; || recorro el fichero de
          nMinDes = #12para1; para1 = para1 + 20; || ajustes.
          nHorHas = #12para1; para1 = para1 + 20; || si todos son cero
          nMinHas = #12para1; para1 = para1 + 20; || me salgo
          nHorCon = #12para1; para1 = para1 + 20;
          nMinCon = #12para1; para1 = para1 + 20;
          nume1 = nHorDes + nMinDes + nHorHas + nMinHas + nHorCon + nMinCon;
          |SI nume1 = 0;
               |SAL;
          |FINSI;

          alfa1 = nHorDes;    alfa1 = "00" + alfa1;    alfa1 = alfa1 % -102;
          alfa2 = nMinDes;    alfa2 = "00" + alfa2;    alfa2 = alfa2 % -102;
          aDesdeAju = alfa1 + alfa2;
          nDesdeAju = aDesdeAju;      || paso horas y min a numeros(ajustes)

          alfa1 = nHorHas;    alfa1 = "00" + alfa1;    alfa1 = alfa1 % -102;
          alfa2 = nMinHas;    alfa2 = "00" + alfa2;    alfa2 = alfa2 % -102;
          aHastaAju = alfa1 + alfa2;
          nHastaAju = aHastaAju;      || paso horas y min a numeros(ajustes)

          alfa1 = nHoraEnt;    alfa1 = "00" + alfa1;    alfa1 = alfa1 % -102;
          alfa2 = nMinEnt;    alfa2 = "00" + alfa2;    alfa2 = alfa2 % -102;
          aEntrada = alfa1 + alfa2;
          nEntrada = aEntrada;      || paso horas y min a numeros(entrada)

          alfa1 = nHoraSal;    alfa1 = "00" + alfa1;    alfa1 = alfa1 % -102;
          alfa2 = nMinSal;    alfa2 = "00" + alfa2;    alfa2 = alfa2 % -102;
          aSalida = alfa1 + alfa2;
          nSalida = aSalida;        || paso horas y minu a numeros(salida)

          |SI swEntAju = 0;
               |SI nEntrada ] nDesdeAju; |Y nEntrada [ nHastaAju;
                    nHoraEnt = nHorCon;
                    nMinEnt = nMinCon;
                    swEntAju = 1;
               |FINSI;
          |FINSI;

          |SI swSalAju = 0;
               |SI nSalida ] nDesdeAju; |Y nSalida [ nHastaAju;
                    nHoraSal = nHorCon;
                    nMinSal = nMinCon;
                    swSalAju = 1;
               |FINSI;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO ajusta;
     |HAZ inicializa;
     |HAZ AjusteClasico;
     |BUCLE AjusteLineal;


     |PARA i = 122; |SI i [ 126; |HACIENDO i = i + 1;
          para1 = i;
          nHorDes = #12para1; para1 = para1 + 4; || recorro el fichero de
          nMinDes = #12para1; para1 = para1 + 4; || ajustes.
          nHorHas = #12para1; para1 = para1 + 4; || si todos son cero
          nMinHas = #12para1; para1 = para1 + 4; || me salgo
          nMinAju = #12para1; para1 = para1 + 16;
          nume1 = nHorDes + nMinDes + nHorHas + nMinHas + nMinAju;
          |SI nume1 = 0;
               |ACABA;
          |FINSI;

          alfa1 = nHorDes;    alfa1 = "00" + alfa1;    alfa1 = alfa1 % -102;
          alfa2 = nMinDes;    alfa2 = "00" + alfa2;    alfa2 = alfa2 % -102;
          aDesdeAju = alfa1 + alfa2;
          nDesdeAju = aDesdeAju;      || paso horas y min a numeros(ajustes)

          alfa1 = nHorHas;    alfa1 = "00" + alfa1;    alfa1 = alfa1 % -102;
          alfa2 = nMinHas;    alfa2 = "00" + alfa2;    alfa2 = alfa2 % -102;
          aHastaAju = alfa1 + alfa2;
          nHastaAju = aHastaAju;      || paso horas y min a numeros(ajustes)

          |SI nDesdeAju < nHastaAju;  || intervalo entre horas del ajuste
               |SI nEntrada < nSalida;  || entrada menor que salida: diurno
                    nAjuste = nMinAju;
               |SAL;
               |FINSI;
          |SINO;
               |SI nEntrada > nSalida;  || entrada mayor que salida: nocturno
                    nAjuste = nMinAju;
               |SAL;
               |FINSI;
          |FINSI;
     |SIGUE;
|FPRC;

|DEFBUCLE ajustes;
     #12#1;
     ;
     nCodAjus, "01.01.1900";
     nCodAjus, fFecha;
     ;
     NULL, ajusta;
|FIN;

|PROCESO ajustes;
     alfa1 = #50#2; alfa1 = "00" + alfa1; alfa1 = alfa1 % -102;
     alfa2 = #0#1;  alfa2 = "00" + alfa2; alfa2 = alfa2 % -102;
     alfa3 = #0#0;
     aFecha = alfa1 + "." + alfa2 + "." + alfa3;
     fFecha = aFecha;

     |SI swAjuste = 1;        || tengo en cuenta ajuste de Salida y de Entrada
          |SI swentra = 0;              || significa que es ENTRADA
               nCodAjus = #20#3;
          |SINO;
               nCodAjus = #20#6;
          |FINSI;
     |SINO;
          nCodAjus = #20#3;   || para todos el mismo
     |FINSI;

     |BUCLE ajustes;
|FPRC;

|PROCESO ruptura_dia;
     swError = 0;
     swPrimero = 0;
     swUltimo = 0;
|FPRC;

|PROCESO ruptura_trab;
     |SI nTarjetaAnt ! 0;
          nMinutosTot = (nMinutosTot / 60) ! 2;
          nMinutosLab = (nMinutosLab / 60) ! 2;
          nMinutosFes = (nMinutosFes / 60) ! 2;
          nMinutosFesNoc = (nMinutosFesNoc / 60) ! 2;
          nFalta = nDias - nDiasComputo;
          alfa1 = aError % 0;
          nume1 = alfa1; nume1 = nume1 - 1; nume1 = 100 + nume1;
          aError = aError % nume1;
          |PRINT;
          nMinutosTot = 0;
          nMinutosLab = 0;
          nMinutosFes = 0;
          nMinutosFesNoc = 0;
          swControl = 0;
          swControl1 = 0;
          nDias = 0;
          nNoches = 0;
          aError = "";
          nHoraEnt = 0;
          nMinEnt = 0;
          nHoraSal = 0;
          nMinSal = 0;
          nDiaCont = 0;
     |FINSI;
|FPRC;

|PROCESO Cuenta_Dias;
     |SI nDiaCont = #50#2; |ACABA; |FINSI;    || si ya he contado eeste dia
     |SI nHoraEnt = 0; |Y nHoraSal = 0; |Y swControl = 0;
          nDias = nDias + 1;  || si hay q contarlo lo cuento
          nDiaCont = #50#2;   || y me lo guardo para no contarlo mas
     |FINSI;
|FPRC;

|PROCESO acumula;
     swError = 0;

     |SI swControl = 1;
          aError = aError + #50#2 + ",";
          swControl = 0;
          |ACABA;
     |FINSI;
     |SI swControl = 0; |Y nHoraEnt = 0;
          aError = aError + #50#2 + ",";
          swControl = 0;
          |ACABA;
     |FINSI;

     |HAZ ajustes;

     |SI nHoraEnt > nHoraSal;      || es nocturno, busco calendario del
          swNoct = 1;
          |HAZ busca_cal2;         || dia anterior
     |SINO;
          swNoct = 0;
          |HAZ busca_cal;          || diurno: calendario del dia
     |FINSI;

     |SI nHoraEnt < nHoraSal;
          nHoras = nHoraSal - nHoraEnt;
     |FINSI;
     |SI nHoraEnt > nHoraSal;
          nHoras = (24 - nHoraEnt) + nHoraSal;
     |FINSI;

     nMinutos = 0;
     |SI nMinSal > nMinEnt;
          nMinutos = nMinSal - nMinEnt;
     |FINSI;

     |SI nMinSal < nMinEnt;
          nHoras = nHoras - 1;
          nMinutos = (60 - nMinEnt) + nMinSal;
          |SI nHoras < 0; nHoras = 0; |FINSI;
     |FINSI;

     nMinutos = nMinutos + (nHoras * 60);    || suma los minutos de las horas
     nMinutos = nMinutos + nAjuste;          || suma los minutos del ajuste

     |SI aTipodia = "1";           || dia Festivo
          |SI swNoct = 0;          || de dia
               nMinutosFes = nMinutosFes + nMinutos;
          |SINO;                   || de noche
               nMinutosFesNoc = nMinutosFesNoc + nMinutos;
          |FINSI;
     |SINO;
          nMinutosLab = nMinutosLab + nMinutos;
     |FINSI;

     nNoches = nNoches + swNoct;
     nMinutosTot = nMinutosTot + nMinutos;
     swControl = 0;
     nHoraEnt = 0;
     nHoraSal = 0;
     nMinEnt = 0;
     nMinSal = 0;
|FPRC;

|PROCESO lee_temp;
     |SI #50#2 ! nDiaAnt; |Y nDiaAnt ! 0;
          |HAZ ruptura_dia;
     |FINSI;
     |SI #50#0 ! nTarjetaAnt;
          |HAZ ruptura_trab;       || ruptura por trabajador e impresion
     |FINSI;
     |SI swEntrada = 1;                 || el anterior era una entrada:
     ||SI #50#10 = "S";                   || el anterior era una entrada:
          swEntrada = 0;
          swSalida = 1;
          nHoraSal = #50#3;
          nMinSal = #50#4;
          swControl = swControl + 1;
          |HAZ acumula;            || clasifica y acumula segun tipo hora
          |HAZ Cuenta_Dias;
     |SINO;
          |ET entrada;
          swEntrada = 1;                || el anterior era una salida:
          swSalida = 0;                 || este es una entrada
          nHoraEnt = #50#3;
          nMinEnt = #50#4;
          swControl = swControl + 1;
          aNombre = #50#9;
     |FINSI;

     nTarjetaAnt = #50#0;
     nDiaAnt = #50#2;
     aESAnt = #50#10;
     nCalAnt = #50#7;
|FPRC;

|DEFBUCLE lee_temp;
  #50#1;
  ;
  0000, 01, 01, 000001, 00, 00;
  9999, 12, 31, 999999, 23, 59;
  ;
  NULL, lee_temp;
|FIN;

|PROCESO graba_temp;          || graba temporal con datos necesarios

     |DDEFECTO #20; #20#0 = #11#7; |LEE 000#20=;

     |SI #20#1 < #0#8; |O #20#1 > #0#9;      |ACABA;   |FINSI;   || empresas
     |SI #20#2 < #0#10;|O #20#2 > #0#11;     |ACABA;   |FINSI;   || trabajadores

     #24#0 = #20#1;      || Empresa
     #24#1 = #20#2;      || Trabajador
     |LEE 000#24=;       || Busca el calendario en labtraba para el temp.
     |SI FSalida ! 0; |DDEFECTO #24; |FINSI;

     #50#0 = #11#7;      || Tarjeta
     #50#1 = #11#1;      || Mes
     #50#2 = #11#3;      || Dia
     #50#3 = #11#4;      || Hora
     #50#4 = #11#5;      || Minuto
     #50#5 = #11#6;      || Segundo
     #50#6 = #11#9;      || Terminal
     #50#7 = #24#156;    || Calendario
     #50#8 = #11#2;      || Registro
     #50#9 = #24#2;      || Nombre Trabajador
     #50#10 = #11#10;    || flag incidencia
     |SI #50#10 = "_"; #50#10 = "S"; |FINSI;
     #50#11 = #20#3;     || Codigo ajustes de Marcado
     |GRABA 000#50n;
|FPRC;


|DEFBUCLE movimientos;
 #11#2;
 9;
 #0#0, #0#1, #0#4,  1,  0,  0,  0, #0#13;  || a¤o,mes,tarjeta,dia,hora,min,seg
 #0#0, #0#1, #0#5, 31, 23, 59, 59, #0#14;
 ;
 NULL, graba_temp;
|FIN;

|PROCESO Control;
     |LEE 000#precontr.p;
     |SI FSalida ! 0;    || No existe control de la aplicacion, entonces
          swAjuste = 0;  || el Codigo de Ajuste de Salida es el de Entrada
     |SINO;              || esto para los que usaran el programa antes de
          |SI #precontr#1 = "S";   || meter el codigo de salida, cuando
               swAjuste = 0;       || solo habia de entrada 26.05.2004
          |SINO;
               swAjuste = 1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO tipo3; |TIPO 3;
|IMPRE 0;
|SI FSalida = 0;
     |ABRET #12;
     |ABRE #20;
     |ABRE #21;
     |ABRE #23;
     |ABRE #24;
     |ABRE #25;
     |ABRE #precontr;
     |DELINDEX #50;
     |ABRE #50;
     |INFOR "preinmes";
     |HAZ Control;
     |BUCLE movimientos;
     |BUCLE lee_temp;
     |HAZ ruptura_dia;
     |HAZ ruptura_trab;
     |PIEINF;
     |FININF;
     |CIERRAT #12;
     |CIERRA #20;
     |CIERRA #21;
     |CIERRA #23;
     |CIERRA #24;
     |CIERRA #25;
     |CIERRA #precontr;
|SINO;
     |MENAV "    Proceso abortado ...";
|FINSI;
|FINIMP;
|FPRC;
