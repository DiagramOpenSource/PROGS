|FICHEROS;
     preimpte #0;
     premvtoc #1;
     premvtol #2;
|FIN;

|VARIABLES;
     {1}aContiene = "";
     aMd5Org = "";
     aMd5Des = "";
     aDes = "";
     aOrg = "";
     {1}aLocal = "";
     aBase = "";
     aIP = "";
     aTxt = "";
     aAlfa = "";
     aExe = "";
     nHandle = 0;
     nReg = 0;
     nCampo = 0;
     aLon = "";
     i = 0;
     nPos = 0;
     aLetra = "";
     aTab = "";
 {99}Valor = "";
     aDato = "";
     aReg = "";
     f = @;
     aFecha = "";
     aHora = "";
     aMinu = "";
     aSegu = "";
     aAno = "";
     aMes = "";
     aDia = "";
     nTar = 0;
     nSec = 0;
     nTer = 0;
     aFlag = "";
     aPath = "";
     aPathDes = "";
     aNom = "";
     aCar = "";
     aFic = "";
|FIN;

|PROCESO SacaNombre;
     aLon = aFic % A0;
     aNom = "";
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = -(i * 100 + 1)
          aCar = aFic % nPos;
          |SI aCar = "\"; |O aCar = "/"; |SAL; |FINSI;
          aNom = aCar + aNom;
     |SIGUE;
|FPRC;

|PROCESO Graba;
     aAlfa = Valor1 % 202; nTer = aAlfa;
     aAlfa = Valor1 % 701;
     |SI aAlfa = 1;
          aFlag = "E";
     |SINO;
          |SI aAlfa = 2;
               aFlag = "S";
          |SINO;
               aFlag = "_";   || Anulacion
          |FINSI;
     |FINSI;
     f = ~; aAlfa = f % 702;
     aAno = aAlfa + (Valor2 % 102);
     aMes = Valor2 % 302;
     aDia = Valor2 % 502;
     aHora = Valor3 % 102;
     aMinu = Valor3 % 302;
     aSegu = Valor3 % 502;
     nTar = Valor4;
     nSec = Valor5;

     #1#0 = aAno;
     #1#1 = aMes;
     #1#2 = aDia;
     |GRABA 000#1n;

     #2#0 = #1#0;
     #2#1 = #1#1;
     #2#3 = #1#2;
     #2#2 = 999999;
     |LEE 000#2];
     |SI FSalida = 0;
          |LEE 000#2a;
     |SINO;
          |LEE 000#2u;
     |FINSI;
     |SI #2#0 = #1#0; |Y #2#1 = #1#1; |Y #2#3 = #1#2;
          nReg = #2#2 + 1;
     |SINO;
          nReg = 1;
     |FINSI;
     |DDEFECTO #2;
     #2#0 = #1#0;
     #2#1 = #1#1;
     #2#3 = #1#2;
     #2#2 = nReg;
     #2#4 = aHora;
     #2#5 = aMinu;
     #2#6 = aSegu;
     #2#7 = nTar;
     #2#9 = nTer;
     #2#10 = aFlag;
     #2#11 = nSec;
     |GRABA 020#2n;
|FPRC;

|PROCESO Dato;
     aTab = " "; ||&9;
     nCampo = 1;
     IValor = Valor1<-;
     aLon = aDato % A0;
     aReg = "";
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aLetra = aDato % nPos;
          |SI aLetra = aTab;
               Valor = aReg;
               aReg = "";
               nCampo = nCampo + 1;
               IValor = IValor + 1;
          |SINO;
               aReg = aReg + aLetra;
          |FINSI;
          |SI nCampo > 90; |SAL; |FINSI;     || Por si.... :)
     |SIGUE;
     Valor = aReg;
|FPRC;

|PROCESO LeeTxt;
     |XABRE "", aTxt, nHandle;
     |SI FSalida = 0;
          |XLEE nHandle, 255, aDato;
          |PARA; |SI FSalida ] 0; |HACIENDO;
               |HAZ Dato;
               |HAZ Graba;
               |XLEE nHandle, 255, aDato;
          |SIGUE;
          |XCIERRA nHandle;
     |FINSI;
|FPRC;

|PROCESO CopiaExe;
     |DBASE aBase;
     aOrg = aBase + "plugins/DsHuella.exe";
     |SI aIP ! "";
          |ESPECIFICA "RBASS", aLocal;
          aBase = aLocal;
          aDes = aBase + "bin/DsHuella.exe";

          aContiene = aOrg;
          |ESPECIFICA "MD5", aContiene;
          aMd5Org = FSalida;
          aContiene = aDes;
          |ESPECIFICA "REMOTOMD5", aContiene;
          aMd5Des = FSalida;
          |SI aMd5Org ! aMd5Des; |ENVIA_FICHERO aOrg, aDes; |FINSI;
          aExe = aDes;
     |SINO;
          aExe = aOrg;
     |FINSI;
|FPRC;

|PROCESO Txt;
     |SI aIP ! "";
          |ESPECIFICA "RBASS", aLocal;
          aBase = aLocal;
          aTxt = aBase + Usuario + ".txt";

          aAlfa = aExe + " " + "0 " + #0#1 + " " + aTxt;
          |RSYSTEM aAlfa;

          |DFICE aAlfa;
          aAlfa = aAlfa + Usuario + ".txt";
          |RECIBE_FICHERO aTxt, aAlfa;
          |RFBORRA aTxt;
          aTxt = aAlfa;
     |SINO;
          |DBASS aBase;
          aTxt = aBase + Usuario + ".txt";

          aAlfa = aExe + " " + "0 " + #0#1 + " " + aTxt;
          |SYSTEM aAlfa;
     |FINSI;
|FPRC;

|PROCESO ImportacionX;   || Esta anulada
     aIP = "";
     |IP_REMOTO aIP;

     |HAZ CopiaExe;
     |HAZ Txt;
     |CIERRA #2;
     |CIERRA #1;
|FPRC;

|PROCESO Importacion;
     |ABRE #1;
     |ABRE #2;
     aPath = #0#3; |QBF aPath;
     aAlfa = aPath % -101;
     |SI aAlfa ! "/"; |Y aAlfa ! "\"; aPath = aPath + "/"; |FINSI;
     aPathDes = aPath + "copia/";

     |SI #0#2 = "P";
          |RMKDIR aPathDes;
          |DFICE aAlfa;
          aTxt = aAlfa + Usuario + ".txt";
          aPath = aPath + "*.*";
          |R_PDIR aPath;
          |PARA; |SI FSalida = 0; |HACIENDO;
               |RECIBE_FICHERO aPath, aTxt;
               |HAZ LeeTxt;
               |FBORRA aTxt;
               aFic = aPath;
               |HAZ SacaNombre
               aDes = aPathDes + aNom;
               |RRENOMBRA_FICHERO aPath, aDes;
               |R_SDIR aPath;
          |SIGUE;
     |SINO;
          |MKDIR aPathDes;
          aPath = aPath + "*.*";
          |_PDIR aPath;
          |PARA; |SI FSalida = 0; |HACIENDO;
               aTxt = aPath;
               |HAZ LeeTxt;
               aFic = aPath;
               |HAZ SacaNombre
               aDes = aPathDes +  aNom;
               |RENOMBRA_FICHERO aTxt, aDes;
               |_SDIR aPath;
          |SIGUE;
     |FINSI;
|FPRC;

|PROGRAMA;
     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     |SI PARAMETRO = "menu";
          |CLS;
          |CABEZA "Importacion";
          |PINPA #0#0;
          |PINDA #0#0;
          |ENDATOS #1#0;
     |SINO;
          FSalida = 0;
     |FINSI;
     |SI FSalida = 0;
          |GRABA 020#0a;
          |HAZ Importacion;
     |FINSI;
     |CIERRA #0;
|FPRO;
