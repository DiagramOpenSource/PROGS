|FICHEROS;
    precreav #0;

    prequiva #11;   || tarjetas
    prehisvl #12;   || historico lins.
    precotiz #13;   || cotizaciones

    nomvarca #25;   || variaciones cabs.
    nomvarli #26;   || variaciones lins.
    labtraba #27;   || trabajadores
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    p_mes = "";
    cuantos = 0;
    nHoras = 0;
    nActiv = 0;
    nProdu = 0;
    nNume1 = 0;
    nExces = 0;
|FIN;
____________________________________/
|PROCESO mayus; |TIPO 0;
    #0Campo = #0Campo > #0Campo;
|PINTA #0Campo;
|FPRC;

|PROCESO defecto; |TIPO 7; ||calcula el a¤o por defecto segun fecha sistema
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % -104;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO mirames; |TIPO 7; ||CALCULA EL MES POR DEFECTO SEGUN FECHA SISTEMA
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % 402;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO pintames; |TIPO 0;      || PINTA EL MES LITERAL
|SI #0Campo =  1;  p_mes = "ENERO     ";  |FINSI;
|SI #0Campo =  2;  p_mes = "FEBRERO   ";  |FINSI;
|SI #0Campo =  3;  p_mes = "MARZO     ";  |FINSI;
|SI #0Campo =  4;  p_mes = "ABRIL     ";  |FINSI;
|SI #0Campo =  5;  p_mes = "MAYO      ";  |FINSI;
|SI #0Campo =  6;  p_mes = "JUNIO     ";  |FINSI;
|SI #0Campo =  7;  p_mes = "JULIO     ";  |FINSI;
|SI #0Campo =  8;  p_mes = "AGOSTO    ";  |FINSI;
|SI #0Campo =  9;  p_mes = "SEPTIEMBRE";  |FINSI;
|SI #0Campo = 10;  p_mes = "OCTUBRE   ";  |FINSI;
|SI #0Campo = 11;  p_mes = "NOVIEMBRE ";  |FINSI;
|SI #0Campo = 12;  p_mes = "DICIEMBRE ";  |FINSI;
|PINTA 1136, p_mes;
|FPRC;

|PROCESO Ajuste; |TIPO 0;
     |SI #0#12 = "S";
          alfa1 = "N";
     |SINO;
          alfa1 = "S";
     |FINSI;
     |C_M #0#8, 1, alfa1;
     |C_M #0#9, 1, alfa1;
     |C_M #0#10, 1, alfa1;
     |C_M #0#11, 1, alfa1;
|FPRC;
____________________________________/
|PROCESO graba_lin_e;
     nExces = #12#11 * #12#8; || antes era igual al #12#10
     nHoras = #12#12;
     nActiv = 0;
     nProdu = 0;
     |SI nHoras > nExces;
          nNume1 = nHoras - nExces;
          nHoras = nExces;
          nActiv = (nNume1 % #0#8) ! 2;
          nProdu = nNume1 - nActiv;
     |FINSI;

|PARA para1 = 1; |SI para1 [ 5; |HACIENDO para1 = para1 + 1;
    |DDEFECTO #26;
        #26#0 = #25#0;       || empresa
        #26#1 = #25#1;       || trabajador
        #26#2 = #25#2;       || mes
        #26#3 = #25#3;       || tipo
    |SI para1 = 1;  #26#4 = #13#3; |FINSI;        || cto.nro.horas
    |SI para1 = 2;  #26#4 = 903;   |FINSI;        || cto.nro.jornadas
    |SI para1 = 3;  #26#4 = 914;   |FINSI;        || cto.nro.dias alta
    |SI para1 = 4;  #26#4 = #0#10; |FINSI;        || cto.plus actividad
    |SI para1 = 5;  #26#4 = #0#11; |FINSI;        || cto.plus produccion
    |LEE 101#26=;
        FEstado = FSalida;

    |SI para1 = 1;  alfa1 = nHoras;                          |FINSI; ||n.horas
    |SI para1 = 2;  alfa1 = #12#11;  #26#6 = "Jornadas";     |FINSI; ||d.alta
    |SI para1 = 3;  alfa1 = #12#11;  #26#6 = "Dias Alta TP"; |FINSI; ||jornad.
    |SI para1 = 4;  alfa1 = nActiv;                          |FINSI; ||activ.
    |SI para1 = 5;  alfa1 = nProdu;                          |FINSI; ||producc.
        alfa1 = ("          " + alfa1) % -110;
        #26#5 = alfa1;

    |SI FEstado ! 0;    |GRABA 020#26n;     |FINSI;
    |SI FEstado = 0;    |GRABA 020#26a;     |FINSI;
    |LIBERA #26;
|SIGUE;
|FPRC;

|PROCESO graba_lin;
|PARA para1 = 1; |SI para1 [ 3; |HACIENDO para1 = para1 + 1;
    |DDEFECTO #26;
        #26#0 = #25#0;       || empresa
        #26#1 = #25#1;       || trabajador
        #26#2 = #25#2;       || mes
        #26#3 = #25#3;       || tipo
    |SI para1 = 1;  #26#4 = #13#3; |FINSI;        || cto.nro.horas
    |SI para1 = 2;  #26#4 = 903;   |FINSI;        || cto.nro.jornadas
    |SI para1 = 3;  #26#4 = 914;   |FINSI;        || cto.nro.dias alta
    |LEE 101#26=;
        FEstado = FSalida;

    |SI para1 = 1;  alfa1 = #12#12;                          |FINSI; ||n.horas
    |SI para1 = 2;  alfa1 = #12#11;  #26#6 = "Jornadas";     |FINSI; ||d.alta
    |SI para1 = 3;  alfa1 = #12#11;  #26#6 = "Dias Alta TP"; |FINSI; ||jornad.
        alfa1 = ("          " + alfa1) % -110;
        #26#5 = alfa1;

    |SI FEstado ! 0;    |GRABA 020#26n;     |FINSI;
    |SI FEstado = 0;    |GRABA 020#26a;     |FINSI;
    |LIBERA #26;
|SIGUE;
|FPRC;

|PROCESO computo1;
|SI #12#11 = 0;|O #12#12 = 0; |ACABA;   |FINSI;   || sin dias ni horas

    #11#1 = #12#0;
    #11#2 = #12#1;
|LEE 000#11=;
|SI FSalida ! 0;
        alfa1 = "    Trabajador [" + #12#0 + "-" + #12#1 + "] sin tarjeta ...";
    |MENAV alfa1;
    |ACABA;
|FINSI;
|SI #11#4 = "N";
    |ACABA;
|FINSI;

|DDEFECTO #27; #27#0 = #12#0; #27#1 = #12#1; |LEE 000#27=;  || trabajadores
|DDEFECTO #13; #13#0 = #27#42;               |LEE 000#13=;  || cotizaciones

    cuantos = cuantos + 1;

|DDEFECTO #25;
    #25#0 = #12#0;       || empresa
    #25#1 = #12#1;       || trabajador
    #25#2 = #12#3;       || mes
    #25#3 = 0;           || tipo
|LEE 101#25=;
    FEstado = FSalida;

|SI FEstado = 0;    |GRABA 020#25a;     |FINSI;
|SI FEstado ! 0;    |GRABA 020#25n;     |FINSI;
|SI FSalida = 0;
    |SI #0#12 = "S";
        |HAZ graba_lin;
    |SINO;
        |HAZ graba_lin_e;
    |FINSI;
|FINSI;
|LIBERA #25;
|FPRC;

|DEFBUCLE computo;
 #12#1;
 ;
 #0#2, #0#4, #0#0, #0#1;
 #0#3, #0#5, #0#0, #0#1;
 ;
 NULL, computo1;
|FIN;

|PROCESO tipo3;
|SI #0#6 = "NO";    |ACABA;   |FINSI;

|ABRE #11;          || tarjetas
|SELECT #2#11;
|ABRE #13;          || cotizaciones
|ABRE #25;          || variaciones cabs.
|ABRE #26;          || variaciones lins.
|ABRE #27;          || trabajadores
|BUCLE computo;
|CIERRA #11;
|CIERRA #13;
|CIERRA #25;
|CIERRA #26;
|CIERRA #27;

    alfa1 = "    Creadas [" + cuantos + "] variaciones ...";
|MENAV alfa1;
|FPRC;

|PROGRAMA;
     |CLS;
     |CABEZA "Crear Variaciones";
     |PINPA #0#0;
     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0;    |GRABA 020#0b; |FINSI;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |HAZ tipo3;
          #0#6 = "NO";
          |GRABA 020#0a;
     |FINSI;
     |LIBERA #0;
     |CIERRA #0;
|FPRO;
