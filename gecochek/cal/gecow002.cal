|FICHEROS;
     gecom011 #11;
     gecow002 #902;
|FIN;

|VARIABLES;
     nCampoD      = 0;
     nCampoH      = 0;
     nPanta1      = 0;
     nBoton1_1    = 0;
     nRango       = 0;
     nPos1        = 0;
     nPos2        = 0;
     nEmpresa     = 0;

     &enEstado    = 0;
     &enCliente   = 0;
     &enModo      = 0;
     &eaVarDni    = "";
     &enCalcCif   = 0;
|FIN;

|| ------------------------------------------------------
|| Procesos del Incio del Programa
|| ------------------------------------------------------

|PROCESO Tipo7;  |TIPO 7;
     |SI enModo ! 1;  |ACABA;  |FINSI;

     |SELECT #1#11;
     |LEE 000#11u;
     |SI #11#0 = 99999;
         |PARA nEmpresa = 99999;  |SI nEmpresa ] 1;  |HACIENDO nEmpresa = nEmpresa - 1;
               #11#0 = nEmpresa;
               |LEE 000#11=;
               |SI FSalida ! 0;  |SAL;  |FINSI;
         |SIGUE;
     |SINO;
         nEmpresa = nEmpresa + 1;
     |FINSI;

     #902#0 = nEmpresa;
     |PINTA #902#0;
|FPRC,

|PROCESO CodCliente;
     |SI enModo ! 1;  |ACABA;  |FINSI;

     #11#0 = #902#0;
     |LEE 000#11=;
     |SI FSalida = 0;
         |MENAV "0000 El Codigo ya Existe.";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO LetraDni;  |TIPO 6;
     |SI FSalida = 10;
         eaVarDni   = #902Campo;
         enCalcCif  = 1;
         |HAZ CalculoDNI;
         #902Campo = eaVarDni;
         |PINTA #902Campo;
         |ERROR;
         |ACABA;
     |FINSI;

     eaVarDni  = #902Campo;
     enCalcCif = 0;
     |HAZ CalculoDNI;
     eaVarDni = (eaVarDni + "            ") % 112;
     |SI #902Campo ! eaVarDni;
         |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
         |SI FSalida = 0;
             #902Campo = eaVarDni;  |PINTA #902Campo;
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo12;  |TIPO 12;
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     FSalida = 2799;
|FPRC;

|PROGRAMA;
     |ABRE #11;
     |SI enModo ! 1;
         #11#0 = enCliente;
         |LEE 101#11=;
         |SI FSalida ! 0;  |DDEFECTO #11; |FINSI;

         #11#0 = enCliente;
         |PARA nCampoD = 2;  |SI nCampoD [ 17;  |HACIENDO nCampoD = nCampoD + 1;
               nCampoH = nCampoD - 1;
               #902nCampoH = #11nCampoD;
         |SIGUE;

         #902#0  = #11#0;
         #902#17 = #11#19;
     |FINSI;

     |CABEZA "Clientes";

     |PINPA #0#902;  nPanta1 = FSalida;
     |PINDA #0#902;

     |C_M #902#0, 1, "S";
     |SI enModo ! 1;
         |C_M #902#0, 1, "N";
     |FINSI;

     |SI enModo = 1;  |ENDATOS #1#902;  |FINSI;
     |SI enModo = 2;  |ENDATOS #2#902;  |FINSI;

     |SI FSalida = 0;
         #11#0 = #902#0;
         |LEE 101#11=;
         |SI FSalida ! 0;
             |DDEFECTO #11;
             #11#0 = #902#0;
             #11#1 = 0;
             |GRABA 020#11b;
         |FINSI;

         |PARA nCampoD = 2;  |SI nCampoD [ 17;  |HACIENDO nCampoD = nCampoD + 1;
               nCampoH = nCampoD - 1;
               #11nCampoD = #902nCampoH;
         |SIGUE;

         #11#19 = #902#17;

         |GRABA 020#11a;
         |LIBERA #11;

         enCliente = #11#0;
     |FINSI;
     |CIERRA #11;

     |PULSATECLA;
|FPRO;
