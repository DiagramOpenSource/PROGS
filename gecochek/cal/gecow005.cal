|FICHEROS;
     gecom011 #11;
     gecom014 #14;
     gecom018 #18;
     gecow005 #905;

     :/dsoporte/def/dsprem03 #103;
     :/dsoporte/def/dsprem04 #104;
     :/dsoporte/def/dsprem05 #105;
     :/dsoporte/def/dsprem06 #106;
     :/dsoporte/def/dsprem99 #99;
|FIN;

|VARIABLES;
     nRango     = 0;
     nPos1      = 0;
     nPos2      = 0;
     nGrid1     = 0;
     nGrid2     = 0;
     nPanta0    = 0;
     nPanta1    = 0;
     nCampo     = 0;
     nVentana   = 0;
     nGrupo     = 0;
     nReg       = 0;
     nBoton1_1  = 0;
     nBoton1_2  = 0;
     nBoton1_3  = 0;

     aEsc1      = "";
     aEsc2      = "";
     aRetu      = "";
     aTecla     = "";
     aBase      = "";
     aMas       = "";
     aFichero   = "";
     aAlfa      = "";
     aGrupo     = "";
     aPathDSoporte = "";
     aHora         = "";
     aOperacion    = "";
     aDOperacion   = "";

     &enCliente = 0;
     &enDelega  = 0;
     &eaUsuario = "";
     &enModo    = 0;
|FIN;

|PROCESO BajaPanta1_1;
     #14#0  = #18#0;
     #14#14 = #18#1;
     #14#1  = "";
|FPRC;

|PROCESO AltaPanta1_1;
     #14#0  = #18#0;
     #14#14 = #18#1;
     #14#1  = "zzzzzzzzzz";
|FPRC;

|PROCESO Tipo80;  |TIPO 80;
     |ABRET #14;

     FSalida = 2799;
|FPRC;

|PROCESO Tipo90;  |TIPO 90;
     |CIERRAT #14;
|FPRC;

|PROCESO EventoPanta1_1;
     |SI FSalida = 1;
         |ESTADO_CONTROL nBoton1_1, "ENABLE";

         |LEE 000#14=;
         |SI FSalida = 0;  |Y  #14#0 = #905#0;  |Y #14#14 = #905#2;
             |ESTADO_CONTROL nBoton1_2, "ENABLE";
             |ESTADO_CONTROL nBoton1_3, "ENABLE";
         |SINO;
             |ESTADO_CONTROL nBoton1_2, "DISABLE";
             |ESTADO_CONTROL nBoton1_3, "DISABLE";
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO GrabaLog;
     |ABRE #103;
     #103#0 = nGrupo;
     #103#1 = #11#1;
     #103#2 = #18#1;
     #103#3 = #14#10;
     #103#4 = #14#11;
     |LEE 000#103=;
     |SI FSalida ! 0;  |DDEFECTO #103;  |FINSI;
     |CIERRA #103;

     |ABRE #104;
     #104#0 = nGrupo;
     #104#1 = #11#1;
     #104#2 = #18#1;
     #104#3 = #14#10;
     #104#4 = #14#11;
     #104#5 = #14#1;
     |LEE 000#104=;
     |SI FSalida ! 0;  |DDEFECTO #104;  |FINSI;
     |CIERRA #104;

     |ABRE #105;
     #105#0 = 1;
     |LEE 000#105=;
     |SI FSalida ! 0;  |DDEFECTO #105;  |FINSI;
     |CIERRA #105;

     |ABRE #106;
     #106#0 = 1;
     |LEE 000#106=;
     |SI FSalida ! 0;  |DDEFECTO #106;  |FINSI;
     |CIERRA #106;

     |HORA aHora;

     |ABRE #99;
     nReg = 1;
     |LEE 000#99u;
     |SI FSalida = 0;
         nReg = #99#0 + 1;
     |FINSI;

     #99#0  = nReg;
     |GRABA 020#99b;

     #99#1  = #14#1;
     #99#2  = ~;
     #99#3  = aHora;
     #99#4  = aOperacion;
     #99#5  = aDOperacion;
     #99#6  = nGrupo + (("00" + #11#1) % -102) + (("00" + #18#1) % -102);
     #99#7  = 1;
     #99#8  = 1;
     #99#9  = aGrupo;
     #99#10 = #11#2;
     #99#11 = #18#2;
     #99#12 = #105#1;
     #99#13 = #106#1;
     aAlfa  = #105#1; |QBF aAlfa;
     aAlfa  = aAlfa + "-" + #106#1;
     #99#14 = nGrupo + (("00" + #11#1) % -102) + (("00" + #18#1) % -102) + " " + aAlfa;
     #99#15 = "N";
     #99#16 = "N";
     #99#17 = #14#4;
     #99#18 = #103#11;
     #99#19 = #104#8;
     #99#20 = Usuario % 810;
     |GRABA 020#99a;
     |LIBERA #99;
     |CIERRA #99;
|FPRC;

|PROCESO Boton1_1;
     |VENTANA 0501, 2799, -1, -1, "";
     nVentana = FSalida;

     enCliente   = #905#0;
     enDelega    = #905#2;
     eaUsuario   = "";
     enModo      = 1;
     |SUB_EJECUTA_NP "gecow006";

     |FINVENTANA nVentana;

     |SI eaUsuario ! "";
         aOperacion  = "A";
         aDOperacion = "ALTA DEL USUARIO";
         |HAZ GrabaLog;
     |FINSI;

     |REFRESCACONTROL nGrid1;
|FPRC;

|PROCESO Boton1_2;
     |LEE 000#14=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |VENTANA 0501, 2799, -1, -1, "";
     nVentana = FSalida;

     aOperacion  = "M";
     aDOperacion = "ENTRADA MODIFICACION DEL USUARIO";
     |HAZ GrabaLog;

     enCliente = #905#0;
     enDelega  = #905#2;
     eaUsuario = #14#1;
     enModo    = 2;
     |SUB_EJECUTA_NP "gecow006";

     |FINVENTANA nVentana;

     aOperacion  = "M";
     aDOperacion = "SALIDA MODIFICACION DEL USUARIO";
     |HAZ GrabaLog;

     |REFRESCACONTROL nGrid1;
|FPRC;

|PROCESO Boton1_3;
     aAlfa = "0000NSeguro de dar de baja a " + #14#2;
     |CONFI aAlfa;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |LEE 000#14=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |BORRA 020#14a;
     |LIBERA #14;

     aOperacion  = "B";
     aDOperacion = "BAJA DEL USUARIO";
     |HAZ GrabaLog;

     |REFRESCACONTROL nGrid1;
|FPRC;

|PROCESO BotonesPanta1;
     |CONTROL_SIMPLE nPanta1, "BOTON,{{193}} Nuevo", 0777, 0898, Boton1_1;
     nBoton1_1 = FSalida;

     |CONTROL_SIMPLE nPanta1, "BOTON,{{197}} Modificar", 1077, 1198, Boton1_2;
     nBoton1_2 = FSalida;

     |CONTROL_SIMPLE nPanta1, "BOTON,{{199}} Baja ", 1377, 1498, Boton1_3;
     nBoton1_3 = FSalida;

     |ESTADO_CONTROL nBoton1_1, "ENABLE";
     |ESTADO_CONTROL nBoton1_2, "DISABLE";
     |ESTADO_CONTROL nBoton1_3, "DISABLE";
|FPRC;

|PROGRAMA;
     |DBASS aBase;  |QBF aBase;
     aGrupo  = aBase % -201;
     nGrupo  = aGrupo;
     |SI nGrupo = 0;
         nGrupo = 1;
     |FINSI;

     aGrupo  = aBase % -205;

     aPathDSoporte = aBase + "dsoporte/fich/";

     |PATH_DAT #99  aPathDSoporte;
     |PATH_DAT #103 aPathDSoporte;
     |PATH_DAT #104 aPathDSoporte;
     |PATH_DAT #105 aPathDSoporte;
     |PATH_DAT #106 aPathDSoporte;

     |ABRE #11;
     #11#0 = enCliente;
     |LEE 000#11=;
     |SI FSalida ! 0;  |CIERRA #11;  |ACABA;  |FINSI;
     |CIERRA #11;

     |ABRE #18;
     #18#0 = enCliente;
     #18#1 = enDelega;
     |LEE 000#18=;
     |SI FSalida ! 0;  |CIERRA #18;  |ACABA;  |FINSI;
     |CIERRA #18;

     #905#0 = #11#0;
     #905#1 = #11#2;
     #905#2 = #18#1;
     #905#3 = #18#2;

     |CABEZA "Usuarios Cliente";

     |PINPA #0#905;  nPanta0 = FSalida;
     |PINDA #0#905;

     |PINPA #1#905;  nPanta1 = FSalida;
     |PINDA #1#905;

     nRango = 4 + 8 + 16 + 32;
     nPos1  = 0501;
     nPos2  = 2057;

     |LINEAL_SIMPLE #1#14, nRango, nPos1, nPos2, BajaPanta1_1, AltaPanta1_1, EventoPanta1_1;
     nGrid1 = FSalida;

     |HAZ BotonesPanta1;

     |FOCOTECLADO nGrid1;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::Salir," + nGrid1;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nBoton1_1;
     |FIN_CONTROL_WINDOWS nBoton1_2;
     |FIN_CONTROL_WINDOWS nBoton1_3;

     |DESTRUYECONTROL nGrid1;

     |PULSATECLA;
|FPRO;
