|FICHEROS;
     gecom001 #0;
     gecom014 #14;

     :/dsoporte/def/dsprem04 #104;
|FIN;

|VARIABLES;
     aBase         = "";
     aLong         = "";
     aAlfa         = "";
     aAlfa1        = "";
     aAlfa2        = "";
     aAbre         = "";
     aUsuario      = "";
     aPassword     = "";
     aMenav        = "";
     aPath         = "";
     aPathDSoporte = "";

     nLong       = 0;
     nPos        = 0;
     nCamuf      = 0;
     nCamuf1     = 0;
     nCamuf2     = 0;
     nNum        = 0;
     nCaracter   = 0;
     nHandle     = 0;

     &eaPass        = "";
     &eaPassEnc     = "";
     &eaPassDesEnc  = "";
     &eaIPDSCorreo  = "";
|FIN;

|PROCESO Desencripta;
     eaPassDesEnc = "";
     |PARA nCaracter = 1; |SI nCaracter [ 20; |HACIENDO nCaracter = nCaracter + 2;
          nPos   = (nCaracter * 100) + 1;       aAlfa1 = eaPass % nPos;
          nPos   = ((nCaracter + 1) * 100) + 1; aAlfa2 = eaPass % nPos;

          nCamuf2 = &aAlfa2;
          nCamuf2 = nCamuf2 - 65;

          nCamuf1 = &aAlfa1;
          nCamuf1 = nCamuf1 - 65;
          nCamuf1 = ((nCamuf1 * 26) + nCamuf2) / 2;

          eaPassDesEnc = eaPassDesEnc + &nCamuf1;
     |SIGUE;
|FPRC;

|PROCESO EncriptaUsuario;
     eaPassEnc = "";
     |PARA nCaracter = 1; |SI nCaracter [ 10; |HACIENDO nCaracter = nCaracter + 1;
           nPos   = (nCaracter * 100) + 1;
           aAlfa1 = eaPass % nPos;

           nCamuf = &aAlfa1;
           nCamuf = nCamuf * 2;

           nNum      = ((nCamuf - (nCamuf $ 26)) / 26 ) + 65;
           eaPassEnc = eaPassEnc + &nNum;

           nNum      = (nCamuf $ 26) + 65;
           eaPassEnc = eaPassEnc + &nNum;
     |SIGUE;
|FPRC;

|PROCESO EncriptaMAC;
     |QBF eaPass;
     aLong   = eaPass % 0;
     nLong   = aLong;
     eaPassEnc = "";
     |PARA nCaracter = 1; |SI nCaracter [ nLong; |HACIENDO nCaracter = nCaracter + 1;
           nPos   = (nCaracter * 100) + 1;
           aAlfa1 = eaPass % nPos;

           nCamuf = &aAlfa1;
           nCamuf = nCamuf * 2;

           nNum      = ((nCamuf - (nCamuf $ 26)) / 26 ) + 65;
           eaPassEnc = eaPassEnc + &nNum;

           nNum      = (nCamuf $ 26) + 65;
           eaPassEnc = eaPassEnc + &nNum;
     |SIGUE;
|FPRC;

|PROCESO MiraPassword;
     |SELECT #3#0;
     #0#4 = aUsuario;
     |LEE 000#0=;
     |SI FSalida = 0;  |Y #0#17 = "S";
         eaPass = #0#5;  |HAZ EncriptaUsuario;

         |SI eaPassEnc ! aPassword;
             |SELECT #1#0;
             |LEE 101#0=;
             |SI FSalida = 0;
                 eaPass = aPassword;  |HAZ Desencripta;
                 #0#5   = eaPassDesEnc;
                 |GRABA 020#0a;
                 |LIBERA #0;

                 aMenav = "0000El password del usuario " + #0#4;  |QBF aMenav;
                 aMenav = aMenav + " ha sido cambiado por ‚l mismo. ";
                 aMenav = aMenav + " Se ha actualizado en el fichero de Administradores";
                 |MENAV aMenav;
             |FINSI;
         |FINSI;
     |FINSI;

     |SELECT #2#14;
     #14#1 = aUsuario;
     |LEE 000#14=;
     |SI FSalida = 0;  |Y #14#15 = "S";
         eaPass = #14#3;  |HAZ EncriptaUsuario;

         |SI eaPassEnc ! aPassword;
             |SELECT #1#14;
             |LEE 101#14=;
             |SI FSalida = 0;
                 eaPass = aPassword;  |HAZ Desencripta;
                 #14#3  = eaPassDesEnc;
                 |GRABA 020#14a;
                 |LIBERA #14;

                 aMenav = "0000El password del usuario " + #14#1;  |QBF aMenav;
                 aMenav = aMenav + " ha sido cambiado por ‚l mismo. ";
                 aMenav = aMenav + " Se ha actualizado en el fichero gecom014.";
                 |MENAV aMenav;
             |FINSI;
         |FINSI;
     |FINSI;

     |SELECT #2#104;
     #104#5 = aUsuario;
     |LEE 000#104=;
     |SI FSalida = 0;  |Y #104#18 = "S";
         eaPass = #104#7;  |HAZ EncriptaUsuario;

         |SI eaPassEnc ! aPassword;
             |SELECT #1#104;
             |LEE 101#104=;
             |SI FSalida = 0;
                 eaPass = aPassword;  |HAZ Desencripta;
                 #104#7  = eaPassDesEnc;
                 |GRABA 020#104a;
                 |LIBERA #104;

                 aMenav = "0000El password del usuario " + #104#5;  |QBF aMenav;
                 aMenav = aMenav + " ha sido cambiado por ‚l mismo. ";
                 aMenav = aMenav + " Se ha actualizado en el fichero dsprem04.";
                 |MENAV aMenav;
             |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO RevisaUsuarios;
     |DBASS aBase;  |QBF aBase;
     aPathDSoporte = aBase + "dsoporte/fich/";

     |PATH_DAT #104 aPathDSoporte;

     |ABRE #0;
     |ABRE #14;
     |ABRE #104;

     |DBASS aBase; |QBF aBase;
     aAbre = aBase + "dev/usr/ds_sys.psw"
     |XABRE "U", aAbre, nHandle;
     |SI FSalida ] 0;
         |PARA; |SI; |HACIENDO;
              |XLEE nHandle, 37, aAlfa;
              |SI FSalida < 0;  |SAL;  |FINSI;

              aUsuario  = aAlfa % 410;
              aPassword = aAlfa % 1420;
              |HAZ MiraPassword;
         |SIGUE;
     |FINSI;
     |XCIERRA nHandle;

     |CIERRA #0;
     |CIERRA #14;
     |CIERRA #104;
|FPRC;

|PROCESO IPDSCorreo;
     eaIPDSCorreo = "";
     aPath        = "";

     aAbre = "/u/basico/bin/ipdscorreo.txt";
     |XABRE "E", aAbre, nHandle;
     |SI FSalida ] 0;
         aPath = "/u/basico/bin/";
     |FINSI;

     |SI aPath = "";
         |DBASS aPath;  |QBF aPath;
         aAbre = aPath + "bin/ipdscorreo.txt";
         |XABRE "E", aAbre, nHandle;
         |SI FSalida ] 0;
             aPath = aPath + "bin/";
         |SINO;
             aPath = "";
         |FINSI;
     |FINSI;

     |SI aPath ! "";
         aAbre = aPath + "ipdscorreo.txt";
         |XABRE "AB", aAbre, nHandle;
         |SI FSalida ] 0;
             |XLEE nHandle, 250, aAlfa;
         |FINSI;
         |XCIERRA nHandle;

         |QBF aAlfa;
         eaIPDSCorreo = aAlfa;
     |FINSI;

     |SI eaIPDSCorreo = "";
         eaIPDSCorreo = "172.16.100.101";
     |FINSI;
|FPRC;
