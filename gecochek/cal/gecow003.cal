|FICHEROS;
     gecom001 #1;
     gecom002 #2;
     gecom011 #11;
     gecom012 #12;
     gecom013 #13;
     gecow003 #903;

     gecom001@ #901;
|FIN;

|VARIABLES;
     nRango     = 0;
     nPos1      = 0;
     nPos2      = 0;
     nGrid1     = 0;
     nGrid2     = 0;
     nPanta0    = 0;
     nPanta1    = 0;
     nCampo     = 0;
     nVentana   = 0;
     nBoton1_1  = 0;
     nBoton1_2  = 0;
     nBoton1_3  = 0;

     aEsc1      = "";
     aEsc2      = "";
     aRetu      = "";
     aTecla     = "";
     aBase      = "";
     aMas       = "";
     aFichero   = "";

     &enCliente = 0;
     &enAdminis = 0;
|FIN;

|PROCESO BajaPanta1_1;
     #12#0 = #11#0;
     #12#1 = 00001;
|FPRC;

|PROCESO AltaPanta1_1;
     #12#0 = #11#0;
     #12#1 = 99999;
|FPRC;

|PROCESO BajaPanta1_2;
     #901#0 = 1;
|FPRC;

|PROCESO AltaPanta1_2;
     #901#0 = 99999;
|FPRC;

|PROCESO gecom001;
     #12#0 = enCliente;
     #12#1 = #1#0;
     |LEE 000#12=;
     |SI FSalida = 0;  |ACABA;  |FINSI;

     |PARA nCampo = 0;  |SI nCampo [ 16;  |HACIENDO nCampo = nCampo + 1;
          #901nCampo= #1nCampo;
     |SIGUE;

     |GRABA 020#901n;
     |LIBERA #901;
|FPRC;

|DEFBUCLE gecom001;
     #1#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, gecom001;
|FIN;

|PROCESO Tipo80;  |TIPO 80;
     |DBASE aBase;  |QBF aBase;

     aMas = aBase + "def/gecom001.mas";
     |CARGA_DEF aMas, gecom001@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aFichero = ("I1" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #901 aFichero;
     |ABRE #901;  |CIERRA #901;  |DELINDEX #901;

     |ABRET #901;
     |ABRET #12;

     |BUCLE gecom001;

     FSalida = 2799;
|FPRC;

|PROCESO Tipo90;  |TIPO 90;
     |CIERRAT #901;
     |CIERRAT #12;

     |DELINDEX #901;

     |DESCARGA_DEF #gecom001@;
|FPRC;

|PROCESO EventoPanta1_1;
     |SI FSalida = 1;  |O FSalida = 2;
         |ESTADO_CONTROL nBoton1_1, "DISABLE";

         |LEE 000#12=;
         |SI FSalida = 0;
             |ESTADO_CONTROL nBoton1_2, "ENABLE";
             |ESTADO_CONTROL nBoton1_3, "ENABLE";
         |SINO;
             |ESTADO_CONTROL nBoton1_2, "DISABLE";
             |ESTADO_CONTROL nBoton1_3, "DISABLE";
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO EventoPanta1_2;
     |SI FSalida = 1;  |O FSalida = 2;
         |ESTADO_CONTROL nBoton1_2, "DISABLE";
         |ESTADO_CONTROL nBoton1_3, "DISABLE";

         |LEE 000#901=;
         |SI FSalida = 0;
             |ESTADO_CONTROL nBoton1_1, "ENABLE";
         |SINO;
             |ESTADO_CONTROL nBoton1_1, "DISABLE";
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO gecom002;
     #13#0 = #12#0;
     #13#1 = #12#1;
     #13#2 = #2#0;
     |LEE 101#13=;
     |SI FSalida ! 0;
         |DDEFECTO #13;
         #13#0 = #12#0;
         #13#1 = #12#1;
         #13#2 = #2#0;
         #13#3 = #2#1;
         #13#4 = #2#3;
         #13#5 = #2#4;
         |GRABA 020#13n;
     |FINSI;
     |LIBERA #13;
|FPRC;

|DEFBUCLE gecom002;
     #2#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, gecom002;
|FIN;

|PROCESO Boton1_1;
     |LEE 101#901=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     #12#0 = #11#0;
     #12#1 = #901#0;
     #12#2 = #901#1;
     |GRABA 020#12n;
     |LIBERA #12;

     |BORRA 020#901a;
     |LIBERA #901;

     |ABRE #13;
     |BUCLE gecom002;
     |CIERRA #13;

     |REFRESCACONTROL nGrid1;
     |REFRESCACONTROL nGrid2;
|FPRC;

|PROCESO gecom013B;
     |BORRA 020#13a;
     |LIBERA #13;
|FPRC;

|DEFBUCLE gecom013B;
     #13#1;
     ;
     #12#0, #12#1, 000;
     #12#0, #12#1, 999;
     be;
     NULL, gecom013B;
|FIN;

|PROCESO Boton1_2;
     |LEE 101#12=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |ABRE #1;
     #1#0 = #12#1;
     |LEE 000#1=;
     |SI FSalida ! 0;  |CIERRA #1;  |LIBERA #12;  |ACABA;  |FINSI;
     |CIERRA #1;

     |PARA nCampo = 0;  |SI nCampo [ 16;  |HACIENDO nCampo = nCampo + 1;
          #901nCampo= #1nCampo;
     |SIGUE;

     |GRABA 020#901n;
     |LIBERA #901;

     |BUCLE gecom013B;

     |BORRA 020#12a;
     |LIBERA #12;

     |REFRESCACONTROL nGrid1;
     |REFRESCACONTROL nGrid2;
|FPRC;

|PROCESO Boton1_3;
     |VENTANA 0501, 2799, -1, -1, "";
     nVentana = FSalida;

     enCliente = #12#0;
     enAdminis = #12#1;
     |SUB_EJECUTA "gecow004";

     |FINVENTANA nVentana;
|FPRC;

|PROCESO BotonesPanta1;
     |CONTROL_SIMPLE nPanta1, "LBOTON,{{229}}", 1044, 1057, Boton1_1;
     nBoton1_1 = FSalida;

     |CONTROL_SIMPLE nPanta1, "LBOTON,{{230}}", 1344, 1357, Boton1_2;
     nBoton1_2 = FSalida;

     |CONTROL_SIMPLE nPanta1, "BOTON,{{137}}", 1749, 1751, Boton1_3;
     nBoton1_3 = FSalida;

     |ESTADO_CONTROL nBoton1_1, "DISABLE";
     |ESTADO_CONTROL nBoton1_2, "DISABLE";
     |ESTADO_CONTROL nBoton1_3, "DISABLE";
|FPRC;

|PROGRAMA;
     |ABRE #11;
     #11#0 = enCliente;
     |LEE 000#11=;
     |SI FSalida ! 0;  |CIERRA #11;  |ACABA;  |FINSI;
     |CIERRA #11;

     #903#0 = #11#0;
     #903#1 = #11#2;

     |CABEZA "Administradores Cliente";

     |PINPA #0#903;  nPanta0 = FSalida;
     |PINDA #0#903;

     |PINPA #1#903;  nPanta1 = FSalida;
     |PINDA #1#903;

     nRango = 4 + 8 + 16 + 32;
     nPos1  = 0660;
     nPos2  = 2199;

     |LINEAL_SIMPLE #1#901, nRango, nPos1, nPos2, BajaPanta1_2, AltaPanta1_2, EventoPanta1_2;
     nGrid2 = FSalida;

     nPos1  = 0601;
     nPos2  = 2140;

     |LINEAL_SIMPLE #1#12, nRango, nPos1, nPos2, BajaPanta1_1, AltaPanta1_1, EventoPanta1_1;
     nGrid1 = FSalida;

     |HAZ BotonesPanta1;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::Salir";
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nBoton1_1;
     |FIN_CONTROL_WINDOWS nBoton1_2;
     |FIN_CONTROL_WINDOWS nBoton1_3;

     |DESTRUYECONTROL nGrid1;
     |DESTRUYECONTROL nGrid2;

     |PULSATECLA;
|FPRO;
