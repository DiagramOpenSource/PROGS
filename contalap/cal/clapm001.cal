|FICHEROS;
   clapm001 #0;

   &canempr@ #330;
   &dsmo100@ #391;
|FIN;

|VARIABLES;
 nInkey  = 0;
 aBase   = "";
 aAlfa1  = "";
 aDef330 = "";
 aDef    = "";
 nDEmpresa = 0;
 nHEmpresa = 0;
 nDPeriodo = 0;
 nHPeriodo = 0;
|FIN;

|INCLUYE dsclap_i;

|CALCULO PorDefecto; |TIPO 0;
 nInkey = FSalida;
 |SI nInkey = 10;
    |SI Campo = 3;
        aAlfa1 = "C:\Archivos de programa\LAPIZ\";
    |FINSI;
    |SI Campo = 1;
        aAlfa1 = #0#3; |QBF aAlfa1;
        aAlfa1 = aAlfa1 + "Datos\";
    |FINSI;
    |SI Campo = 2;
        |HAZ eBuscaAplic;
        |SI eaPathContagen ! "";
            aAlfa1 = eaPathContagen;
        |SINO;
            aAlfa1 = eaPathAgicont;
        |FINSI;
        aAlfa1 = aAlfa1 + "fich/";
    |FINSI;
    #0Campo = aAlfa1; |PINTA #0Campo;
 |FINSI;

 aAlfa1 = #0Campo; |QBF aAlfa1;
 |SI aAlfa1 = "";
     |MENAV "    Atencion. Introduzca el Dato Correctamente";
     |ERROR;
 |FINSI;
|FCAL;

|CALCULO ElDefecto; |TIPO 0;
   eaPathM = "";
   nConta  = 0;
   eaPathDatos = #0#2; |QBF eaPathDatos;
   aAlfa1  = eaPathDatos - "contagen";
   |SI FSalida = 0; nConta = 1; |FINSI;
   |SI nConta = 0;
       aAlfa1  = eaPathDatos - "contagen/fich/";
       eaPathM = aAlfa1 + "modelos/fich/";
       aDef    = aAlfa1 + "modelos/def/";
       aDef    = aDef + "dsmom100";
       |CARGA_DEF aDef, dsmo100@;
       |SI FSalida = 0;
           |PATH_DAT #391 eaPathM;
           |ABRE #391;
           |DDEFECTO #391;
           |LEE 041#391p;
           |CIERRA #391;
           #0#9  = #391#1;  |PINTA #0#9;
           #0#10 = #391#2;  |PINTA #0#10;
           #0#11 = #391#3;  |PINTA #0#11;
           |DESCARGA_DEF #dsmo100@;
       |FINSI;
   |FINSI;
|FCAL;

|| *******************************************************************
|RUTINA infA2;
   #330#2 = nDEmpresa;
   #330#3 = 0;
|FRUT;

|RUTINA supA2;
   #330#2 = nHEmpresa;
   #330#3 = 9;
|FRUT;

|PROCESO DescargaEmp;
     |SI aDef330 ! "";
         |DESCARGA_DEF #canempr@;
         aDef330 = "";
     |FINSI;
|FPRC;

|PROCESO CargaEmpresa;
     swerror = 0;
     eaPathDatos = #0#2; |QBF eaPathDatos;
     aAlfa1    = eaPathDatos - "fich/";
     eaPathPan = aAlfa1 + "pan/";
     eaPathDef = aAlfa1 + "def/";
     aDef330   = eaPathDef + "canempre";
     |CARGA_DEF aDef330, canempr@;
     |SI FSalida ! 0;
         |MENAV "    Error Cargado Fichero de Empresas. Proceso Interrumpido";
         swerror = 1;
         aDef330 = "";
         |ACABA;
     |FINSI;
     |PATH_DAT #330 eaPathDatos;
|FPRC;

|CALCULO LeeEstaEmpresa; |TIPO 0;
  nInkey = FSalida; |SI nInkey = 2; |ACABA; |FINSI;

  |HAZ DescargaEmp;
  |HAZ CargaEmpresa;
  |SI swerror = 1; |ACABA; |FINSI;

  |SI nInkey = 10;
     nDEmpresa = 0; nHEmpresa = 99999;
     |ABRE #330;
     |CONSULTA_F_CLAVE #1#330,infA2,supA2;
     #0#9  = #330#2; |PINTA #0#9;
     #0#10 = #330#3; |PINTA #0#10;
     |CIERRA #330;
     |HAZ DescargaEmp;
     |PTEC 802;
     |ACABA;
  |FINSI;

  |ABRE #330;
  #330#2 = #0#9;
  #330#3 = 0;
  |LEE 011#330];
  |SI FSalida ! 0;  |O #330#2 ! #0#9;
      |MENAV "     NO Existe la empresa Contable";
      |ERROR;
  |SINO;
      #0#11 = #330#1; |PINTA #0#11;
  |FINSI;
  |CIERRA #330;
  |HAZ DescargaEmp;
|FCAL;

|CALCULO LeeEstePeriodo; |TIPO 0;
  nInkey = FSalida; |SI nInkey = 2; |ACABA; |FINSI;

  |HAZ DescargaEmp;
  |HAZ CargaEmpresa;

  |SI nInkey = 10;
     nDEmpresa = #0#9; nHEmpresa = #0#9;
     |ABRE #330;
     |CONSULTA_F_CLAVE #1#330,infA2,supA2;
     #0#10 = #330#3; |PINTA #0#10;
     |CIERRA #330;
  |FINSI;

  |ABRE #330;
  #330#2 = #0#9;
  #330#3 = #0#10;
  |LEE 011#330=;
  |SI FSalida ! 0;
      |MENAV "     NO Existe Periodo en la Empresa Contable";
      |ERROR;
  |SINO;
      #0#11 = #330#1;  |PINTA #0#11;
  |FINSI;
  |CIERRA #330;
  |HAZ DescargaEmp;
|FCAL;

|CALCULO ParaConfi; |TIPO 3;
   eaPathLapiz = #0#3; |QBF eaPathLapiz;
   eaPathExImp = #0#1; |QBF eaPathExImp;
   eaPathDatos = #0#2; |QBF eaPathDatos;
   eaFichPara  = #0#4; |QBF eaFichPara;
   eaPathEjec  = #0#8; |QBF eaPathEjec;
   eaFichEmps  = #0#5; |QBF eaFichEmps;
   eaFichCtas  = #0#6; |QBF eaFichCtas;
   eaFichAsto  = #0#7; |QBF eaFichAsto;
   |HAZ eCrearCfg;
|FCAL;

|PROGRAMA;

   |CLS;
   |CABEZA "Control de Aplicacion";
   |PINPA #0#0;

   |DDEFECTO #0;
   |ABRE #0;
   |LEE 101#0p;
   |SI FSalida ! 0;
      |GRABA 020#0b;
   |FINSI;
   |PINDA #0#0;

   |ENDATOS #2#0;
   |SI FSalida = 0;
      |GRABA 020#0a;
   |FINSI;
   |LIBERA #0;
   |CIERRA #0;
|FPRO;
