|| ................. Buscar Cuentas por ".", ".a" y ".u"
|FICHEROS;
     &canempr@ #330;
     &cacuent@ #310;
     &cacueba@ #313;
     &cacuetl@ #369,MANTE;
     &cacuntp@ #370,MANTE;
     &cacuen2@ #371;
     &caclipr@ #311;
     &cacuetp@ #400,MANTE;
|FIN;

|VARIABLES;
    p_lugar = 0;
    p_cuan3 = 0;
    p_cuan4 = 0;
    p_cuan5 = 0;
    p_alfa1 = "";
    p_alfa2 = "";
    p_alfa3 = "";
    p_alfa4 = "";
    p_alfa5 = "";
    p_alfa6 = "";
    p_guarda = "";
    p_nume1 = 0;
    p_nume2 = 0;
    p_ceros = "";
    p_error = 0;
    p_mensa = "0000LONGITUD DE CUENTA FUERA DE NIVEL!";
    desdec1 = "";
    hastac1 = "";
    varia_cod = "";
    sw_nom = 0;
    cuen11 = "";
    cuen21 = "";
    cuen31 = "";
    num_campo = 0;
    p_nifs = "";

    aLong       = "";
    aTecla      = "";
    aDesde      = "";
    aHasta      = "";
    aAlfa       = "";
    aAlfa1      = "";
    aAlfa2      = "";
    aAlfa3      = "";
    aAlfa4      = "";
    aAlfa5      = "";
    aAste       = "";
    aAsteN      = "";
    aCadena     = "";
    aDNombre    = "";
    aHNombre    = "";
    aFichero    = "";
    aNif        = "";
    aBuscar1    = "";
    aBuscar2    = "";
    aCuenta     = "";
    aLetra      = "";
    aCeros      = "";
    aPunto      = "";
    aGuarda     = "";

    nCamp1 = 0;
    nCampo = 0;
    nLong  = 0;
    nPos   = 0;
    nNume  = 0;
    nNume1 = 0;

    nSwFicha = 0;
    nSwSi    = 0;

    nSw400     = 0;
    nSw410     = 0;
    nSw523     = 0;
    nSw430     = 0;
    nSw440     = 0;
    nNumCtas   = 0;
    aDef       = "";
    aPath      = "";
    aEmpresa   = "";
    aPan       = "";
    nDevol = 0;

    mascara   = "";
    uno       = "";
    dos       = "";
|FIN;

|INCLUYE dsclap_i;

|PROCESO NombreMes;
  |SI NumMes = 0;  NombreMes = "Apertura  "; |FINSI;
  |SI NumMes = 1;  NombreMes = "Enero     "; |FINSI;
  |SI NumMes = 2;  NombreMes = "Febrero   "; |FINSI;
  |SI NumMes = 3;  NombreMes = "Marzo     "; |FINSI;
  |SI NumMes = 4;  NombreMes = "Abril     "; |FINSI;
  |SI NumMes = 5;  NombreMes = "Mayo      "; |FINSI;
  |SI NumMes = 6;  NombreMes = "Junio     "; |FINSI;
  |SI NumMes = 7;  NombreMes = "Julio     "; |FINSI;
  |SI NumMes = 8;  NombreMes = "Agosto    "; |FINSI;
  |SI NumMes = 9;  NombreMes = "Septiembre"; |FINSI;
  |SI NumMes = 10; NombreMes = "Octubre   "; |FINSI;
  |SI NumMes = 11; NombreMes = "Noviembre "; |FINSI;
  |SI NumMes = 12; NombreMes = "Diciembre "; |FINSI;
  |SI NumMes = 13; NombreMes = "Cierre    "; |FINSI;
|FPRC;

|PROCESO SacaNivel;
   eswCta = 1;     ||Errores en Cuenta
   aLong = eaCuenta;
   |QBF aLong;
   aLong = aLong % 0;
   nLong = aLong;
   enNivel = 0;
   |SI nLong = dig4; |Y dig4 ! 0; eswCta = 0; enNivel = 1; |FINSI;
   |SI nLong = dig5; |Y dig5 ! 0; eswCta = 0; enNivel = 2; |FINSI;
   |SI nLong = dig6; |Y dig6 ! 0; eswCta = 0; enNivel = 3; |FINSI;
   |SI nLong = dig7; |Y dig7 ! 0; eswCta = 0; enNivel = 4; |FINSI;
   |SI nLong = dig8; |Y dig8 ! 0; eswCta = 0; enNivel = 5; |FINSI;
   |SI nLong = dig9; |Y dig9 ! 0; eswCta = 0; enNivel = 6; |FINSI;

   |SI eswCta = 1; |Y  nLong ! 0;
       |SI emCta = 1;
           |MENAV p_mensa;
           |ERROR;
       |FINSI;
   |FINSI;
|FPRC;

|PROCESO NoBlanca;|TIPO 6;
   |SI #Fichero@#Campo# = doce;
       |MENAV "0000Introduzca una cuenta";
       |ERROR;
       |ACABA;
   |FINSI;
|FPRC;

|PROCESO defectos;
|| la 3a clave esta formada por la cta + zzzz y el campo fantasma del nivel;
   mascara = #310#0;
   |QBF mascara;
   mascara = mascara +"zzzzzzzzzzzz";
   mascara = mascara % 112;
   #310#54 = mascara;
   #310#1  = enNivel;
   #310#55 = #310#1;
   #310#3  = "S";

   |ABRE #313;
   uno = #310#0; |QBF uno;
   dos = uno + "000000000000";
   uno = dos % A101;
   #313#0 = uno;
   |LEE 040#313=;
   |SI FSalida = 0;  |HAZ sacaM;  |ACABA;  |FINSI;
   uno = dos % A102;
   #313#0 = uno;
   |LEE 040#313=;
   |SI FSalida = 0;  |HAZ sacaM;  |ACABA;  |FINSI;
   uno = dos % A103;
   #313#0 = uno;
   |LEE 040#313=;
   |SI FSalida = 0;  |HAZ sacaM;  |ACABA;  |FINSI;
   uno = dos % A104;
   #313#0 = uno;
   |LEE 040#313=;
   |SI FSalida = 0;
      |HAZ sacaM;
   |SINO;
      |CIERRA #313;
   |FINSI;
|FPRC;

|PROCESO sacaM;
   #310#4 = #313#1;
   #310#5 = #313#2;
   #310#6 = #313#3;
   #310#7 = #313#4;
   #310#8 = #313#5;
   |CIERRA #313;
|FPRC;

|| อออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออ
|| RUTINAS DE LAS UTILIDADES DE LAS CUENTAS
|| อออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออ

|PROCESO PintaUtilidades;
         |PUSHV 0501 2380;

         |BLANCO 0830 2380;
         |CUADRO 0830 2380;
         |ATRI R;
         |PINTA 0931, "           UTILIDADES EN LAS CUENTAS            ";
         |ATRI r;

         |ATRI I;
         |PINTA 1031, "XXX.a --> Alta Cuenta del grupo introducido + 1 ";
         |PINTA 1131, "XXX.u --> Presenta la ultima cuenta del grupo.  ";
         |PINTA 1231, "XXX.b --> Consulta de las cuentas del grupo     ";
         |PINTA 1331, "N*    --> Busqueda por Nombre como empieza.     ";
         |PINTA 1431, "T*    --> Busqueda por Nombre en cualquier sitio";
         |PINTA 1531, "          de la cadena.                         ";
         |PINTA 1631, "C*    --> Busqueda de Clientes por Nombre       ";
         |PINTA 1731, "P*    --> Busqueda de Proveedores por Nombre    ";
         |PINTA 1831, "F*    --> Busqueda de Clientes o Proveedores por";
         |PINTA 1931, "          NIF o CIF.                            ";
         |PINTA 2031, "F11   --> Consulta Directa en las Cuentas.      ";
         |ATRI i;

         |ATRI S;
         |PINTA 2231, "Nota XXX seria la cuenta del grupo ejemplo 430.b";
         |ATRI i;

         |LEETECLA aTecla;
         |POPV;
|FPRC;

|RUTINA ClaveBaja369C;
    #369#3 = "S";
    #369#0 = aDesde;
|FRUT;

|RUTINA ClaveAlta369C;
    #369#3 = "S";
    #369#0 = aHasta;
|FRUT;

|RUTINA ClaveBaja369N;
    #369#3 = "S";
    #369#2 = aDNombre;
    #369#0 = aDCuenta;
|FRUT;

|RUTINA ClaveAlta369N;
    #369#3 = "S";
    #369#2 = aHNombre;
    #369#0 = aHCuenta;
|FRUT;

|RUTINA ClaveBaja400N;
    #400#0 = " " * 12;
    #400#1 = " " * 36;
|FRUT;

|RUTINA ClaveAlta400N;
    #400#0 = "z" * 12;
    #400#1 = "z" * 36;
|FRUT;

|RUTINA ClaveBaja400F;
    #400#0 = " " * 12;
    #400#2 = " " * 12;
|FRUT;

|RUTINA ClaveAlta400F;
    #400#0 = "z" * 12;
    #400#2 = "z" * 12;
|FRUT;

|PROCESO PulsaIntro;
     nSwSi = 1;
     |ERROR;
     |PTEC 807;
|FPRC;

|PROCESO z200Tipo5;  |TIPO 5;
     |LEE 040#400p;
     #400#4 = " ";
     |LINEAS_BI_ATRI #400#4, D, S, -1;
     |SI aAsteN = "+";
         |ATRI R;
         |PINTA 666, "E";
         |ATRI 0;
     |FINSI;
|FPRC;

|PROCESO ConsultaPorCodigo;
     aDesde = aAlfa2;  |QBF aDesde;
     aDesde = (aDesde + "            ") % 112;

     aHasta = aAlfa2;  |QBF aHasta;
     aHasta = (aHasta + "zzzzzzzzzzzz") % 112;

     |PUSHV 0501 2380;
     |ENTLINEAL #6#369, -2, 4, 22, 1, ClaveBaja369C, ClaveAlta369C;
     |POPV;
|FPRC;

|PROCESO PidePatron;
     |PUSHV 0501 2380;
     |BLANCO 1530 1980;
     |CUADRO 1530 1980;
     |ATRI R;
     |PINTA 1631, "  INTRODUZCA LA CADENA A BUSCAR (EN BLANCO TODO) ";
     |ATRI r;

     E_inf    = "";
     E_sup    = "40";
     aBuscar1 = "      <Patron con el que buscar>        ";
     aBuscar1 = 1838 ? 1;

     |SI aBuscar1 = "      <Patron con el que buscar>        ";
         aBuscar1 = "";
     |FINSI;

     |POPV;

     |QBF aBuscar1;
|FPRC;

|PROCESO PidePatronF;
     |SI aAsteN = "+";
         aBuscar2 = eaCodNif;
         |ACABA;
     |FINSI;

     |PUSHV 0501 2380;
     |BLANCO 1530 1980;
     |CUADRO 1530 1980;
     |ATRI R;
     |MENSA "0000 Pulse <F2> Calculo la Letra del NIF o Comprobacion del CIF";
     |PINTA 1631, "  INTRODUZCA EL NIF/CIF A BUSCAR (EN BLANCO TODO)";
     |ATRI r;

     E_inf    = "";
     E_sup    = "12";
     aBuscar2 = "   <Patron> ";
     |PARA; |SI; |HACIENDO;
            aBuscar2 = 1848 ? 1;
            |SI FSalida = 10;
                eaVarDni  = aBuscar2;
                enCalcCif = 1;
                |HAZ CalculoDNI;
                aBuscar2  = eaVarDni;
            |SINO;
                |SAL;
            |FINSI;
     |SIGUE;

     |SI aBuscar2 = "   <Patron> ";
         aBuscar2 = "";
     |FINSI;

     |MENSA "0000                                                           ";
     |POPV;

     |QBF aBuscar2;
     aBuscar2 = aBuscar2 > " ";
|FPRC;

|PROCESO ConsultaPorNombre;
     |HAZ PidePatron;

     aDNombre = aBuscar1;
     aHNombre = aBuscar1;  |QBF aHasta;
     aHNombre = (aHNombre + ("z" * 36)) % 136;
     aDCuenta = "            ";
     aHCuenta = "zzzzzzzzzzzz";

     |PUSHV 0501 2380;
     |ENTLINEAL #7#369, -2, 4, 22, 1, ClaveBaja369N, ClaveAlta369N;
     |POPV;
|FPRC;

|PROCESO LeeNif;
     eaElCam23 = "";
     aNif      = "";
     nSwFicha  = 0;

     aAlfa1 = #369#0 % 101;

     |ABRE #311;
     #311#23 = "C";
     #311#10 = #369#0;
     #311#11 = #369#1;
     |LEE 040#311=;
     |SI FSalida = 0;
         aNif =  #311#9;
         nSwFicha  = 1;
         eaElCam23 = #311#23;
     |SINO;
         #311#23 = "P";
         #311#10 = #369#0;
         #311#11 = #369#1;
         |LEE 040#311=;
         |SI FSalida = 0;
             aNif =  #311#9;
             nSwFicha  = 1;
             eaElCam23 = #311#23;
         |FINSI;
     |FINSI;
     |CIERRA #311;
|FPRC;

|PROCESO GrabaTrabajo;
     |SI #369#3 ! "S";                              |ACABA;  |FINSI;
     |SI #369#0 < aDCuenta;  |O #369#0 > aHCuenta;  |ACABA;  |FINSI;

     |SI aAste = "P*";
          aAlfa1 = #369#0 % 103;
          |SI aAlfa1 ! "400"; |Y aAlfa1 ! "410"; |Y aAlfa1 ! "523";
              |ACABA;
          |FINSI;
     |FINSI;

     |HAZ LeeNif;

     |SI aAste = "F*";  |Y aBuscar2 ! "";
         aLong  = aBuscar2 % 0;
         nLong  = aLong;
         nPos   = 100 + nLong;
         aAlfa1 = aNif % nPos;
         |SI aAlfa1 ! aBuscar2;  |ACABA;  |FINSI;
     |FINSI;

     |SI aAste ="F*"; |Y nSwFicha = 0;
         |ACABA;                          || No tengo ficha Cliente/Proveedor
     |FINSI;

     |DDEFECTO #400;
     #400#0 = #369#0;
     #400#1 = #369#2;
     #400#2 = aNif;
     #400#3 = eaElCam23;
     #400#4 = "";
     |GRABA 020#400n;
     |LIBERA #400;
     nNumCtas = nNumCtas + 1;

     aAlfa1 = #400#0 % 103;
     |SI aAlfa1 = "400"; nSw400 = 1; |FINSI;
     |SI aAlfa1 = "410"; nSw410 = 1; |FINSI;
     |SI aAlfa1 = "430"; nSw430 = 1; |FINSI;
     |SI aAlfa1 = "440"; nSw440 = 1; |FINSI;
     |SI aAlfa1 = "523"; nSw523 = 1; |FINSI;
|FPRC;

|DEFBUCLE cacuentlT;
     #369#2001;
     ;
     "                                    ";
     "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz";
     f;
     NULL, GrabaTrabajo;
|FIN;

|DEFBUCLE cacuentl;
     #369#7003;
     ;
     "S", aDNombre, aDCuenta;
     "S", aHNombre, aHCuenta;
     ;
     NULL, GrabaTrabajo;
|FIN;

|PROCESO PorPuntos1;
     aPunto = eaCuenta - ".";
     |HAZ PorPuntos;
     |DDEFECTO #400;
     #400#0 = eaCuenta;
     #400#1 = eaNomCta;
     #400#2 = aBuscar2;
     #400#3 = eaElCam23;
     #400#4 = "N";
     |GRABA 020#400n;
     |LIBERA #400;
     nNumCtas = nNumCtas + 1;
|FPRC;

|PROCESO Alta200Varios;
   |SI nSw400 = 1; |O nSw410 = 1; |O nSw523 = 1; |O nSw430 = 1; |O nSw440 = 1;
       |ACABA;
   |FINSI;
         eaElCam23 = "P";
         |SI nSw400 = 0; eaCuenta = "4000.a      "; |HAZ PorPuntos1; |FINSI;
         |SI nSw410 = 0; eaCuenta = "4100.a      "; |HAZ PorPuntos1; |FINSI;
         |SI nSw523 = 0; eaCuenta = "5230.a      "; |HAZ PorPuntos1; |FINSI;
         eaElCam23 = "C";
         |SI nSw430 = 0; eaCuenta = "4300.a      "; |HAZ PorPuntos1; |FINSI;
         |SI nSw440 = 0; eaCuenta = "4400.a      "; |HAZ PorPuntos1; |FINSI;
|FPRC;

|PROCESO HazTrabajo;
     aFichero = ("400" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #400 aFichero;
     |ABRE #400;  |CIERRA #400;  |DELINDEX #400;

     |SI aAste = "T*";
         aAsteN = "";
         |HAZ PidePatron;
         aDCuenta = "            ";
         aHCuenta = "zzzzzzzzzzzz";
         aDNombre = " " * 36;
         aHNombre = "z" * 36;
     |FINSI;

     |SI aAste = "C*";
         aAsteN = "";
         |HAZ PidePatron;
         aDCuenta = "4300        ";
         aHCuenta = "449999999999";
         aDNombre = " " * 36;
         aHNombre = "z" * 36;
     |FINSI;

     |SI aAste = "P*";
         aAsteN = "";
         |HAZ PidePatron;
         aDCuenta = "4000        ";
         aHCuenta = "523999999999";
         aDNombre = " " * 36;
         aHNombre = "z" * 36;
     |FINSI;

     |SI aAste = "F*";
         |HAZ PidePatronF;
         aDCuenta = "            ";
         aHCuenta = "zzzzzzzzzzzz";
         aDNombre = " " * 36;
         aHNombre = "z" * 36;
     |FINSI;

     nSw400 = 0;
     nSw410 = 0;
     nSw430 = 0;
     nSw440 = 0;
     nSw523 = 0;

     nNumCtas = 0;
     |ABRE #400;
     |SI aBuscar1 ! "";
         FSalida = aBuscar1;
         |BUCLE cacuentlT;
     |SINO;
         |BUCLE cacuentl;
     |FINSI;
     |SI aAsteN = "+";
         |HAZ Alta200Varios;
     |FINSI;
     |CIERRA #400;

     |SI nNumCtas = 1; |Y aAsteN = "+";
         nSwSi = 1;
         |DELINDEX #400;
         |ACABA;
     |FINSI;

     |PUSHV 0501 2380;
     nSwSi = 0;
     |SI aAste ! "F*"; |O aAsteN = "+";
         |SI aAsteN = "+";
             |C_V #400#4, 1, "S";
             |C_A #400#1, 1, 34;
         |SINO;
             |C_V #400#4, 1, "N";
             |C_A #400#1, 1, 80;
         |FINSI;
         |ENTLINEAL #2#400, -1, 4, 22, 1, ClaveBaja400N, ClaveAlta400N;
     |SINO;
         |ENTLINEAL #3#400, -1, 4, 22, 1, ClaveBaja400F, ClaveAlta400F;
     |FINSI;
     |POPV;

     |DELINDEX #400;
|FPRC;

|PROCESO AltaUltima;
     |ABRE #369;
     nNume   = aAlfa2;
     nNume   = nNume + 1;
     aCuenta = nNume;
     #369#0 = aCuenta;
     #369#1 = "0";
     |LEE 021#369];
     |SI FSalida ! 0;
         |LEE 021#369u;
     |SINO;
         |LEE 021#369a;
         |SI FSalida ! 0;
             |LEE 021#369p;
         |SINO;
             |PARA; |SI #369#3 = "N"; |HACIENDO;
                    |LEE 021#369a;
                    |SI FSalida ! 0; |LEE 021#369p; |SAL; |FINSI;
             |SIGUE;
         |FINSI;
     |FINSI;

     aLong  = aAlfa2 % 0;
     nLong  = aLong;
     nPos   = nLong + 100;
     aAlfa1 = #369#0 % nPos;
     |SI aAlfa1 = aAlfa2;
         nNume = #369#0;
         |SI aLetra = "A";
              nNume = nNume + 1;
         |FINSI;
         aAlfa5 = nNume;
     |SINO;
         aCeros = "0" * nLong;
         aAlfa5 = aAlfa2 + aAlfa4 + "1";
     |FINSI;

     |DDEFECTO #369;
|FPRC;

|PROCESO PorPuntos;
     nNume1 = FSalida;
     nPos   = 100 + (((nNume1 / 100) ! 0) -1);
     aAlfa2 = eaCuenta % nPos;   |QBF aAlfa2;
     nPos   = (((nNume1 / 100) ! 0) * 100) + 199;
     aAlfa3 = eaCuenta % nPos;   |QBF aAlfa3;

     FSalida = nNume1;
     |PARA; |SI FSalida ! 0; |HACIENDO;
           aAlfa3 = aAlfa3 - "."; || limpiar mas puntos (los ignoramos)
     |SIGUE;

     nPos   = MaximoDigitos - ( (A \ aAlfa2 % 0) + (A \ aAlfa3 % 0));
     aAlfa4 = (A\ "0" * nPos);

     aLetra = (aAlfa3 % 101) > " ";
     |SI aLetra = "U";  |O aLetra = "A";
         |HAZ AltaUltima;
         eaCuenta = aAlfa5;
     |SINO;
         |SI aLetra = "B";
             nCampo   = Campo;
             |HAZ ConsultaPorCodigo;
             Campo    = nCampo;
             eaCuenta = #369#0;
             |ERROR;
         |SINO;
             eaCuenta  = (A\ aAlfa2 + aAlfa4 + aAlfa3);
         |FINSI;
    |FINSI;
|FPRC;

|PROCESO FiltraPuntos; |TIPO 6;
     aGuarda = FSalida;
     |SI eaCuenta = "            "; |ACABA; |FINSI;

     |SI nConta = 1;
         p_alfa1 = eaCuenta;
         |HAZ Puntos;  || subrutina de star3
         |ACABA;
     |FINSI;

     |SI salidas = 20;
         |HAZ PintaUtilidades;

         |ERROR;
         |ACABA;
     |FINSI;

     aDef     = eaPathDef + "cacuentl";
     |CARGA_DEF aDef, cacuetl@;
     |SI FSalida ! 0;
         |MENAV "    Error Cargado Fichero de Cuentas. Proceso Interrumpido";
         |ACABA;
     |FINSI;

     aDef     = eaPathDef + "cacuetmp";
     |CARGA_DEF aDef, cacuetp@;
     |SI FSalida ! 0;
         |MENAV "    Error Cargado Fichero de Cuentas. Proceso Interrumpido";
         |DESCARGA_DEF #cacuetl@;
         |ACABA;
     |FINSI;

     aAlfa1 = enEmpresa; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
     aAlfa2 = enPeriodo; |QBF aAlfa2; aAlfa2 = ("0"     + aAlfa2) % -101;
     aEmpresa = aAlfa1 + aAlfa2;
     aPath = eaPathDatos + aEmpresa + "/";

     |PATH_DAT #369 aPath;
     |PATH_DAT #400 aPath;


     aBuscar1 = "";
     aBuscar2 = "";
     aAste    = (eaCuenta % 102) > " ";
     aAsteN   = eaCuenta % 301;               ||permite Alta
     |SI eaCodNif = ""; aAsteN   = ""; |FINSI;
     aAsteN   = ""; || en modelos por ahora no puedo dar alta
     |SI aAste = "N*";
         nCampo   = Campo;
         |HAZ ConsultaPorNombre;
         Campo    = nCampo;
         eaCuenta = #369#0;
         FSalida  = aGuarda;

         |DESCARGA_DEF #cacuetp@;
         |DESCARGA_DEF #cacuetl@;

         |ERROR;
         |ACABA;
     |FINSI;

     |SI aAste = "T*";  |O aAste = "C*";  |O aAste = "P*";  |O aAste = "F*";
         nCampo   = Campo;
         |HAZ HazTrabajo;
         Campo    = nCampo;
         eaCuenta = #400#0;
         eaNomCta  = #400#1;
         eaElCam23 = #400#3;
         FSalida  = aGuarda;
         |SI nSwSi = 0;
             eaCuenta = "";
         |FINSI;
         |DESCARGA_DEF #cacuetp@;
         |DESCARGA_DEF #cacuetl@;
         |ERROR;
         |ACABA;
     |FINSI;

     aPunto = eaCuenta - ".";
     |SI FSalida ! 0;
         |HAZ PorPuntos;
     |FINSI;

     |DESCARGA_DEF #cacuetp@;
     |DESCARGA_DEF #cacuetl@;
     FSalida = aGuarda;
|FPRC;

|PROCESO LeeCtaSup;
   eaNomCtaSup = "";
   aAlfa1 = eaCuenta % 102;
   |SI aAlfa1 = "40"; |O aAlfa1 = "41"; |O aAlfa1 = "43"; |O aAlfa1 = "44";
       |ACABA;
   |FINSI;

   nNume1 = enNivel - 1;
   |SI nNume1 = 1; nNume = dig4; |FINSI;
   |SI nNume1 = 2; nNume = dig5; |FINSI;
   |SI nNume1 = 3; nNume = dig6; |FINSI;
   |SI nNume1 = 4; nNume = dig7; |FINSI;
   |SI nNume1 = 5; nNume = dig8; |FINSI;
   aAlfa1 = nNume;
   aAlfa1 = ("00" + aAlfa1) % -102;
   aAlfa1 = "1" + aAlfa1;
   aCuenta = eaCuenta % aAlfa1;

   |ABRE #369;
   #369#0 = aCuenta;
   |LEE 001#369=;
   |SI FSalida = 0;
       eaNomCtaSup = #369#2;
   |FINSI;
   |CIERRA #369;
|FPRC;

|| ... Subrutina de Agicont

    p_ceros = "";
    p_error = 0;
    p_mensa = "    LONGITUD DE CUENTA FUERA DE NIVEL!";
    desdec1 = "";
    hastac1 = "";
    varia_cod = "";
    sw_nom = 0;
    cuen11 = "";
    cuen21 = "";
    cuen31 = "";
    num_campo = 0;
    p_nifs = "";

|PROCESO Puntos;               || desde calculo 'cuenta'

   aDef     = eaPathDef + "cacuenta";
   |CARGA_DEF aDef, cacuen2@;
   |SI FSalida ! 0;
       |MENAV "    Error Cargado Fichero de Cuentas. Proceso Interrumpido";
       |ACABA;
   |FINSI;

   aDef     = eaPathDef + "cacuentp";
   |CARGA_DEF aDef, cacuntp@;
   |SI FSalida ! 0;
       |MENAV "    Error Cargado Fichero de Cuentas. Proceso Interrumpido";
       |DESCARGA_DEF #cacuen2@;
       |ACABA;
   |FINSI;

   aPan = eaPathDef - "def/";
   aPan = aPan + "pan/";
   |PATH_PAN #370 aPan;

   |SI salidas = 18;
       num_campo = Campo;
       |HAZ cuent1;
       |DESCARGA_DEF #cacuntp@;
       |DESCARGA_DEF #cacuen2@;
       |ACABA;
   |FINSI;

   p_error = 0;
   p_lugar = 0;
   p_cuan3 = 0;
   p_cuan4 = 0;
   p_alfa3 = "";
   p_alfa4 = "";
   p_alfa4 = p_alfa1 > " ";
   p_alfa1 = p_alfa4;
   p_alfa4 = "";
   |PARA p_nume1 = 1; |SI p_nume1 [ 12; |HACIENDO p_nume1 = p_nume1 + 1;
         p_nume2 = (p_nume1 * 100) + 1;
         p_alfa2 = p_alfa1 % p_nume2;
         |SI p_alfa2 ! " ";
             |SI p_alfa2 = ".";
                 p_lugar = p_nume1;
             |SINO;
                 |SI p_lugar=0; p_alfa3=p_alfa3+p_alfa2; p_cuan3=p_cuan3+1; |FINSI;
                 |SI p_lugar!0; p_alfa4=p_alfa4+p_alfa2; p_cuan4=p_cuan4+1; |FINSI;
             |FINSI;
         |FINSI;
   |SIGUE;
   |SI p_lugar ! 0;
       |SI dig3 = 1;  p_cuan5 = dig4;  |FINSI;
       |SI dig3 = 2;  p_cuan5 = dig5;  |FINSI;
       |SI dig3 = 3;  p_cuan5 = dig6;  |FINSI;
       |SI dig3 = 4;  p_cuan5 = dig7;  |FINSI;
       |SI dig3 = 5;  p_cuan5 = dig8;  |FINSI;
       |SI dig3 = 6;  p_cuan5 = dig9;  |FINSI;
       p_nume1 = p_cuan5 - (p_cuan3 + p_cuan4);
       |SI p_nume1 > 0;
           cuen11 = p_alfa4 % 101;
           p_ceros = "0" * p_nume1;
           p_alfa5 = p_alfa3 + p_ceros + p_alfa4;
           cuen11 = p_alfa4 % 101;
           |SI cuen11 = "U";  |O cuen11 = "A";
               cuen31 = cuen11;
               |HAZ ultialta;
           |FINSI;
           |SI nDevol = 0;
               eaCuenta = p_alfa5;
           |FINSI;
      |SINO;
          |MENAV p_mensa;
          p_error = 1;
      |FINSI;
  |SINO;
      p_alfa5 = p_alfa1;
  |FINSI;
  |DESCARGA_DEF #cacuntp@;
  |DESCARGA_DEF #cacuen2@;
|FPRC;

|PROCESO busca_cuen1;
    |SI #370#0 = "P";
        p_alfa6 = #371#0;
        p_alfa6 = p_alfa6 % 103;
        |SI p_alfa6 ! "400"; |Y p_alfa6 ! "410"; |Y p_alfa6 ! "523";
            |ACABA;
        |FINSI;
    |FINSI;
     cuen31 = #371#2;
     cuen31 = cuen31 > cuen31;
     cuen21 = cuen31 - cuen11;
     |SI cuen21 ! cuen31; |Y #371#3 = "S";
          |HAZ leepornifs;
          #370#1 = #371#2;
          #370#2 = #371#0;
          #370#4 = p_nifs;
          |PINTA #370#1;
          |PINTA #370#2;
          |PINTA #370#4;
          |PAUSA;
          |SI FSalida = 1;  sw_nom = 1;  |ERROR10;  |FINSI; ||Esc
          |SI FSalida = 7;  |ERROR10;  |FINSI;    ||Ctrl+C
     |FINSI;
|FPRC;

|DEFBUCLE consulta11;
  #371#1002;
  3;
  desdec1 , 0, "S";
  hastac1 , 9, "S";
  e;
  NULL , busca_cuen1;
|FIN;

|PROCESO cuent1; |TIPO 0;
          sw_nom = 0;
          |PUSHV 0501 2380;
          |DDEFECTO #370;
          |PINPA #0#370;
          |ENDATOS #1#370;
          |SI FSalida = 0;
               |SI #370#0 ] 1; |Y #370#0 [ 7;
                    desdec1 = #370#0 + "           ";
                    hastac1 = #370#0 + "99999999999";
               |SINO;
                    |SI #370#0 = "C";
                         desdec1 = "4300        ";
                         hastac1 = "449999999999";
                    |FINSI;
                    |SI #370#0 = "P";
                         desdec1 = "4000        ";
                         hastac1 = "523999999999";
                    |FINSI;
                    |SI #370#0 = "T";
                         desdec1 = "            ";
                         hastac1 = "zzzzzzzzzzzz";
                    |FINSI;
               |FINSI;
               cuen11 = #370#1; |QBF cuen11;
               cuen11 = cuen11 > cuen11;
               |MENSA "0000Pulse <Return> Para Seguir, <Esc> Para Aceptar, <Ctrl+C> Para Abandonar";
               |BUCLE consulta11;
          |FINSI;
          |POPV;
          |BLIN 4;
          |SI sw_nom = 1;
               varia_cod = #371#0;
               |QBF varia_cod;
               eaCuenta = varia_cod;
          |SINO;
               eaCuenta = p_alfa1;
          |FINSI;
          Campo = num_campo;
          |ERROR;
|FPRC;

|PROCESO ultialta;
  |ABRE #371;
  p_nume2 = p_alfa3;
  p_nume2 = p_nume2 + 1;
  cuen11 = p_nume2;
  #371#0 = cuen11;
  #371#1 = "0";
  |LEE 021#371];
  |SI FSalida ! 0;
      |LEE 021#371u;
  |SINO;
      |LEE 021#371a;
      |SI FSalida ! 0;
          |LEE 021#371p;
      |FINSI;
  |FINSI;
  cuen21 = p_alfa3 % 0;
  p_nume2 = cuen21;
  p_nume2 = p_nume2 + 100;
  cuen11 = p_nume2;
  cuen21 = #371#0 % cuen11;
  |SI cuen21 = p_alfa3;
      cuen21 = #371#0;
      p_nume2 = cuen21;
      |SI cuen31 = "A";
          p_nume2 = p_nume2 + 1;
      |FINSI;
      p_alfa5 = p_nume2;
  |SINO;
      p_ceros = "0" * p_nume1;
      p_alfa5 = p_alfa3 + p_ceros + "1";
  |FINSI;
  |DDEFECTO #371;
  |CIERRA #371;
|FPRC;

|PROCESO leepornifs;
 p_nifs = "               ";
 p_alfa1 = #371#0; p_alfa1 = p_alfa1 % 101;
 |SI p_alfa1 ! "4"; |Y p_alfa1 ! "5";
     |ACABA;
 |FINSI;

 |ABRE #311;
 #311#23 = "C";
 #311#10 = #371#0;
 #311#11 = #371#1;
 |LEE 001#311=;
 |SI FSalida = 0;
     p_nifs = #311#9;
 |SINO;
     #311#23 = "P";
     #311#10 = #371#0;
     #311#11 = #371#1;
     |LEE 001#311=;
     |SI FSalida = 0;
         p_nifs = #311#9;
     |FINSI;
 |FINSI;
 |CIERRA #311;
|FPRC;
