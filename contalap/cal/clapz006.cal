|FICHEROS;
  clapz006 #0;

  clapm002 #2;

  &canempr@ #330;
  &cacuent@ #310;
  &caconce@ #306;
|FIN;

|VARIABLES;
 nEstado    = 0;
 aCuenta    = "";
 aNomCta    = "";

 aAlfa1     = "";
 aAlfa2     = "";
 nNume1     = 0;
 nNume2     = 0;
 nNume3     = 0;
 nPara1     = 0;

 aDef       = "";
 aDef330    = "";
 aDef310    = "";
 aDef306    = "";

 nInkey     = 0;
 nDEmpresa  = 0;
 nHEmpresa  = 0;
 nDPeriodo  = 0;
 nHPeriodo  = 0;
 nDConcep   = 0;
 nHConcep   = 0;

 aDCta      = "";
 aHCta      = "";

 aPath      = "";
 aBase      = "";
 aEmpresa   = "";

 aFichero   = "";
 aPathM     = "";
 nTip       = 0;
 nLinea     = 0;
 nAlgun     = "";
|FIN;

|INCLUYE dsclap_i;

|| ************************************************************************
|CALCULO Tp8z006; |TIPO 8;
 |HAZ eLeePathFich;
 |SI eswError = 1;
     |ACABA;
 |FINSI;

 |SI nConta = 1;
     |MENAV "    ESTE PROCESO ES SOLO PARA CONTAGEN";
     |PTEC 807;
     |ACABA;
 |FINSI;

 aAlfa1 = eaPathDatos - "fich/";
 eaPathDef = aAlfa1 + "def/";
 aDef      = eaPathDef + "canempre";
 |CARGA_DEF aDef, canempr@;
 |SI FSalida ! 0;
     |MENAV "    Error Cargado Fichero de Empresas. Proceso Interrumpido";
     |PTEC 807;
     |ACABA;
 |FINSI;
 aDef330 = aDef;

 |PATH_DAT #330 eaPathDatos;

 aAlfa1    = eaPathDef - "contagen/def/";
 aAlfa1    = aAlfa1 + "modelos/def/";
 aDef      = aAlfa1 + "dsmom030";
 |CARGA_DEF aDef, caconce@;
 |SI FSalida ! 0;
     |MENAV "    Error Cargado Fichero de Conceptos. Proceso Interrumpido";
     |PTEC 807;
     |ACABA;
 |FINSI;
 aDef306 = aDef;

 aDef     = eaPathDef + "cacuenta";
 |CARGA_DEF aDef, cacuent@;
 |SI FSalida ! 0;
     |MENAV "    Error Cargado Fichero de Cuentas. Proceso Interrumpido";
     aDef310 = "";
     |ACABA;
 |FINSI;
 aDef310 = aDef;

 |PATH_DAT #306 eaPathM;
|FCAL;

|CALCULO Tp9z006; |TIPO 9;
  |SI aDef310 ! ""; |DESCARGA_DEF #cacuent@; aDef310 = ""; |FINSI;
  |SI aDef306 ! ""; |DESCARGA_DEF #caconce@; aDef306 = ""; |FINSI;
  |SI aDef330 ! ""; |DESCARGA_DEF #canempr@; aDef330 = ""; |FINSI;
|FCAL;

|| ********************************************************************
|RUTINA inf2;
   #310#0 = "            ";
   #310#1 = 0;
   #310#3 = "S";
|FRUT;

|RUTINA sup2;
   #310#0 = "zzzzzzzzzzzz";
   #310#1 = 9;
   #310#3 = "S";
|FRUT;

|CALCULO PrLeeCuenta; |TIPO 6;
     nInkey = FSalida;
     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;
|FCAL;

|CALCULO LeeCuenta; |TIPO 0;
   |SI nInkey = 10;
      |ABRE #310;
      #310#0 = #0Campo;
      |CONSULTA_F_CLAVE #1#310,inf2,sup2;
      #0Campo = #310#0; |PINTA #0Campo;
      |CIERRA #310;
      |ERROR;
      |ACABA;
   |FINSI;
   |SI Campo = 3; |Y #0#2 > #0#3;
      |MENAV "    Error en Cuentas Desde/Hasta";
      |ERROR;
  |FINSI;
|FCAL;

|RUTINA inf6;
   #306#0 = 001;
|FRUT;

|RUTINA sup6;
   #306#0 = 999;
|FRUT;

|CALCULO LeeConcepto; |TIPO 0;
   nInkey = FSalida;
   |SI nInkey = 10;
      |ABRE #306;
      |CONSULTA_F_CLAVE #1#306,inf6,sup6;
      #0Campo = #306#0; |PINTA #0Campo;
      |CIERRA #306;
      |ERROR;
   |FINSI;
   |SI Campo = 5; |Y #0#4 > #0#5;
       |MENAV "    Error en Concepto Desde/Hasta";
   |FINSI;
|FCAL;

|RUTINA infA2;
   #330#2 = nDEmpresa;
   #330#3 = 0;
|FRUT;

|RUTINA supA2;
   #330#2 = nHEmpresa;
   #330#3 = 9;
|FRUT;

|CALCULO LeeEstaEmpresa; |TIPO 0;
  nInkey = FSalida;

  |SI nInkey = 10;
     nDEmpresa = 0;
     nHEmpresa = 99999;
     |ABRE #330;
     |CONSULTA_F_CLAVE #1#330,infA2,supA2;
     #0#0 = #330#2; |PINTA #0#0;
     #0#1 = #330#3; |PINTA #0#1;
     |CIERRA #330;
  |FINSI;

  |SI #0#0 ! 0;
      |ABRE #330;
      #330#2 = #0#0;
      #330#3 = 0;
      |LEE 011#330];
      |SI FSalida ! 0;  |O #330#2 ! #0#0;
          |MENAV "     NO Existe la empresa Contable";
          |ERROR;
      |SINO;
          #0#1 = #330#1; |PINTA #0#1;
          #0#2 = #330#8; |PINTA #0#2;
      |FINSI;
      |CIERRA #330;
  |SINO;
      #0#1 = "EMPRESA GENERAL"; |PINTA #0#1;
      #0#2 = ""; |PINTA #0#2;
  |FINSI;

  enEmpresa = #0#0;
  enPeriodo = -1;
  |HAZ eLeeEmpresa;

  aAlfa1 = #330#2; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
  aAlfa2 = #330#3; |QBF aAlfa2; aAlfa2 = ("0"     + aAlfa2) % -101;
  aEmpresa = aAlfa1 + aAlfa2;
  aPath    = eaPathDatos + aAlfa1 + aAlfa2 + "/";
  |PATH_DAT #310 aPath;
|FCAL;

|| **************************************************************
|PROCESO Grabam002;

 |ABRE #2;
 nLinea = nLinea + 1;
 #2#0 = enEmpresa;
 #2#1 = aCuenta;
 #2#2 = enConcepto;
 #2#3 = nLinea;
 #2#4 = aDCta;
 #2#5 = aHCta;
 #2#6 = #0#1;
 #2#7 = #310#2;
 #2#8 = #306#1;
 |GRABA 000#2n;
 |CIERRA #2;
 nAlgun = 1;
|FPRC;

|PROCESO BuscaLinea;
 nLinea = #2#3;
|FPRC;

|DEFBUCLE BuscaLinea;
 #2#1;
 ;
 enEmpresa,aCuenta,enConcepto,001;
 enEmpresa,aCuenta,enConcepto,999;
 ;
 NULL, BuscaLinea;
|FIN;

|PROCESO LaContra;
 |SI aCuenta < #0#2; |O aCuenta > #0#3;
     |ACABA;
 |FINSI;

 |ABRE #310;
 #310#0 = aCuenta;
 |LEE 001#310=;
 |SI FSalida ! 0; #310#2 = aNomCta; |FINSI;
 |CIERRA #310;

 emCta = 0; eaCuenta = aCuenta;
 |HAZ SacaNivel;
 |SI enNivel = 0; |ACABA; |FINSI;

 nLinea = 0;
 |BUCLE BuscaLinea;

 nAlgun = 0;
 nNume1 = 19; nNume2 = 67; nNume3 = 71;
 |SI nTip = 2; nNume1 = 18; nNume2 = 62; nNume3 = 66; |FINSI;
 |SI #306nNume1 = "S";
     |PARA nPara1 = nNume2; |SI nPara1 [ nNume3; |HACIENDO nPara1 = nPara1 + 1;
           aAlfa1 = #306nPara1; |QBF aAlfa1;
           |SI aAlfa1 ! "";
               aDCta = (aAlfa1 + "0000000000000") % 112;
               aHCta = (aAlfa1 + "9999999999999") % 112;
               |HAZ Grabam002;
           |FINSI;
     |SIGUE;
 |FINSI;
 |SI nAlgun = 0;
     aDCta = "             ";
     aHCta = "zzzzzzzzzzzzz";
     |HAZ Grabam002;
 |FINSI;
|FPRC;

|PROCESO Leedsmom030;
 enConcepto = #306#0;

 aCuenta = #306#20;
 |SI aCuenta ! "            ";
     aNomCta    = #306#21;
     nTip       = 1;
     |HAZ LaContra;
 |FINSI;
 aCuenta = #306#22;
 |SI aCuenta ! "            ";
     aNomCta    = #306#23;
     nTip       = 2;
     |HAZ LaContra;
 |FINSI;
|FPRC;

|DEFBUCLE Leedsmom030;
 #306#1001;
 4;
 nDConcep, "S";
 nHConcep, "S";
 e;
 NULL, Leedsmom030;
|FIN;

|| **************************************************************
|PROCESO PrimeroBorra;
     |BORRA 020#2a;
     |LIBERA #2;
|FPRC;

|DEFBUCLE PrimeroBorra;
     #2#1;
     ;
     nDEmpresa, aDCuenta, nDConcep, 001;
     nHEmpresa, aHCuenta, nHConcep, 999;
     be;
     NULL, PrimeroBorra;
|FIN;
|| **************************************************************

|CALCULO Tp3z006; |TIPO 3;
 |SI #0#6 = "NO";
     |ACABA;
 |FINSI;

 nDEmpresa = #0#0;
 nHEmpresa = #0#0;
 aDCuenta  = #0#2;
 aHCuenta  = #0#3;
 nDConcep  = #0#4;
 nHConcep  = #0#5;
 |BUCLE PrimeroBorra;

 |BUCLE Leedsmom030;
|FCAL;
