|FICHEROS;
  clapz007 #0;
  clapm003 #3;

  &canempr@ #330;
  &cacuent@ #310;
  &caclipr@ #311;
|FIN;

|VARIABLES;
 aBlanc     = "";
 aAlfa1     = "";
 aAlfa2     = "";
 nNume1     = 0;
 nNume2     = 0;

 aDef       = "";
 aDef330    = "";
 aDef310    = "";
 aDef311    = "";

 nInkey     = 0;
 nDEmpresa  = 0;
 nHEmpresa  = 0;
 nDPeriodo  = 0;
 nHPeriodo  = 0;

 nCampo     = 0;
 nCamp1     = 0;
 nCamp2     = 0;
 nCamp3     = 0;
 nCamp4     = 0;

 nAlgun     = 0;
 aPath      = "";
 aBase      = "";
 aFiche     = "";
 nHadle     = 0;

 aEmpresa   = "";
 aContenido = "";
 aVarAlfa1  = "";
 aCif       = "";
 aCaja      = "";
|FIN;

|INCLUYE dsclap_i;


|CALCULO Tipo8z007; |TIPO 8;
 |HAZ eLeePathFich;
 |SI eswError = 1;
     |PTEC 807;
     |ACABA;
 |FINSI;

 aAlfa1   = eaPathDatos - "fich/";
 eaPathDef = aAlfa1 + "def/";
 aDef     = eaPathDef + "canempre";
 |CARGA_DEF aDef, canempr@;
 |SI FSalida ! 0;
     |MENAV "    Error Cargado Fichero de Empresas. Proceso Interrumpido";
     |PTEC 807;
     |ACABA;
 |FINSI;
 aDef330 = aDef;

 |PATH_DAT #330 eaPathDatos;

|FCAL;

|CALCULO Tipo9z007; |TIPO 9;
  |SI aDef311 ! ""; |DESCARGA_DEF #caclipr@; aDef311 = ""; |FINSI;
  |SI aDef310 ! ""; |DESCARGA_DEF #cacuent@; aDef310 = ""; |FINSI;
  |SI aDef330 ! ""; |DESCARGA_DEF #canempr@; aDef330 = ""; |FINSI;
|FCAL;

|| ////////////////////////////////////////////////////////////////
|RUTINA infA2;
   #330#2 = nDEmpresa;
   #330#3 = 0;
|FRUT;

|RUTINA supA2;
   #330#2 = nHEmpresa;
   #330#3 = 9;
|FRUT;

|CALCULO LeeEmpresa; |TIPO 0;
  nInkey = FSalida;

  |C_M #0Campo, 0, aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

  nCamp1 = Campo;
  nCamp2 = nCamp1 + 10;
  nCamp3 = nCamp1 + 20;
  nCamp4 = nCamp1 + 30;
  |SI nInkey = 10;
     nDEmpresa = 0; nHEmpresa = 99999;
     |ABRE #330;
     |CONSULTA_F_CLAVE #1#330,infA2,supA2;
     #0nCamp1 = #330#2; |PINTA #0nCamp1;
     #0nCamp2 = #330#3; |PINTA #0nCamp2;
     |CIERRA #330;
     |PTEC 802;
     |ACABA;
  |FINSI;

  |SI #0Campo = 0;
      |ACABA;
  |FINSI;

  |ABRE #330;
  #330#2 = #0nCamp1;
  #330#3 = 0;
  |LEE 011#330];
  |SI FSalida ! 0;  |O #330#2 ! #0nCamp1;
      |MENAV "     NO Existe la empresa Contable";
      |ERROR;
  |SINO;
      #0nCamp4 = #330#1; |PINTA #0nCamp4;
  |FINSI;
  |CIERRA #330;
|FCAL;

|CALCULO Select; |TIPO 0;
     |SI #0#6 = "N";
         #0#7  = 1;     |PINTA #0#7;  |C_M #0#7, 1, "N";
         #0#8  = 99999; |PINTA #0#8;  |C_M #0#8, 1, "N";
         #0#9  = 0;     |PINTA #0#9;  |C_M #0#9, 1, "N";
         #0#10 = 9;     |PINTA #0#10; |C_M #0#10,1, "N";
         |PARA nCampo = 11;  |SI nCampo [ 30;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |C_M #0#7, 1, "S";
         |C_M #0#8, 1, "S";
         |C_M #0#9, 1, "S";
         |C_M #0#10,1, "S";
         |PARA nCampo = 11; |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0; |PINTA #0nCampo; |C_M #0nCampo, 1, "N";
               nCamp2 = nCampo + 10;
               nCamp3 = nCampo + 20;
               nCamp4 = nCampo + 30;
               #0nCamp2 = 0; |PINTA #0nCamp2; |C_M #0nCamp2, 1, "N";
               #0nCamp3 = ""; |PINTA #0nCamp3;
               #0nCamp4 = ""; |PINTA #0nCamp4;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas; |TIPO 0;

     |PARA nCampo = Campo;  |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
           nCamp2 = nCampo + 10;
           nCamp3 = nCampo + 20;
           nCamp4 = nCampo + 30;
           |SI #0Campo = 0;
               |SI Campo ! nCampo;
                   #0nCampo = 0;  |C_M #0nCampo, 1, "N"; |PINTA #0nCampo;
               |FINSI;
               #0nCamp2 = 0;  |PINTA #0nCamp2; |C_M #0nCamp2, 1, "N";
               #0nCamp3 = ""; |PINTA #0nCamp3;
               #0nCamp4 = ""; |PINTA #0nCamp4;
           |SINO;
               |C_M #0nCampo, 1, "S";
               |C_M #0nCamp2, 1, "S";
          |FINSI;
    |SIGUE;
|FCAL;

|CALCULO LeeEmpresaP; |TIPO 0;
  nInkey = FSalida;

  |C_M #0Campo, 0, aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

  |SI nInkey = 2; |ACABA; |FINSI;

  nCamp2 = Campo;
  nCamp1 = nCamp2 - 10;
  nCamp3 = nCamp1 + 20;
  nCamp4 = nCamp1 + 30;
  |SI nInkey = 10;
     nDEmpresa = #0nCamp1; nHEmpresa = #0nCamp1;
     |ABRE #330;
     |CONSULTA_F_CLAVE #1#330,infA2,supA2;
     #0nCamp2 = #330#3; |PINTA #0nCamp2;
     |CIERRA #330;
  |FINSI;

  |ABRE #330;
  #330#2 = #0nCamp1;
  #330#3 = #0nCamp2;
  |LEE 011#330=;
  |SI FSalida ! 0;
      |MENAV "     NO Existe Periodo en la Empresa Contable";
      |ERROR;
  |SINO;
      |SI #330#26 > 80;
          nNume1 = #330#26 + 1900;
      |SINO;
          nNume1 = #330#26 + 2000;
      |FINSI;
      #0nCamp3 = nNume1; |PINTA #0nCamp3;
      #0nCamp4 = #330#1; |PINTA #0nCamp4;
  |FINSI;
  |CIERRA #330;
|FCAL;

|| /////////////////////////////////////////////////////////////////////////
|| /////////////////////////////////////////////////////////////////////////
|PROCESO HazTodo;

 enEmpresa = #330#2;
 enPeriodo = #330#3;

 |HAZ eVarEmpresa;
 aAlfa1 = #330#2; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
 aAlfa2 = #330#3; |QBF aAlfa2; aAlfa2 = ("0"     + aAlfa2) % -101;
 aEmpresa = aAlfa1 + aAlfa2;

 aDef     = eaPathDef + "cacuenta";
 |CARGA_DEF aDef, cacuent@;
 |SI FSalida ! 0;
     |MENAV "    Error Cargado Fichero de Cuentas. Proceso Interrumpido";
     aDef310 = "";
     |ACABA;
 |FINSI;
 aDef310 = aDef;
 aDef     = eaPathDef + "caclipro";
 |CARGA_DEF aDef, caclipr@;
 |SI FSalida ! 0;
     |MENAV "    Error Cargado Fichero de Clientes. Proceso Interrumpido";
     |DESCARGA_DEF #cacuent@; aDef310 = "";
     aDef311 = "";
     |ACABA;
 |FINSI;
 aDef311 = aDef;

 |PATH_DAT #310 eaPathConta;
 |PATH_DAT #311 eaPathConta;

 |HAZ eCargaRelacion;

 |SI aDef311 ! ""; |DESCARGA_DEF #caclipr@; aDef311 = ""; |FINSI;
 |SI aDef310 ! ""; |DESCARGA_DEF #cacuent@; aDef310 = ""; |FINSI;
|FPRC;


|DEFBUCLE LeeCanempre;
 #canempr@#1002;
 ;
 nDEmpresa, nDPeriodo;
 nHEmpresa, nHPeriodo;
 ;
 NULL, HazTodo;
|FIN;

|| /////////////////////////////////////////////////////////////////////////
|CALCULO Tipo3z007; |TIPO 3;
  |SI #0#0 = "NO"; |ACABA; |FINSI;

  aDCuenta = #0#1;
  aHCuenta = #0#2;

  |SI #0#6 = "S";
      nDEmpresa = #0#7; nDPeriodo = #0#9;
      nHEmpresa = #0#8; nHPeriodo = #0#10;
      |BUCLE LeeCanempre;
  |SINO;
      |PARA nCampo = 11; |SI nCampo [ 20; |HACIENDO nCampo = nCampo + 1;
            |SI #0nCampo ! 0;
                nCamp2 = nCampo + 10;
                nDEmpresa = #0nCampo; nHEmpresa = nDEmpresa;
                nDPeriodo = #0nCamp2; nHPeriodo = nDPeriodo;
                |BUCLE LeeCanempre;
            |FINSI;
      |SIGUE;
  |FINSI;
|FCAL;
