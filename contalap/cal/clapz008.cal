|FICHEROS;
  clapz008 #0;
  clapm003 #3;

  &canempr@ #330;
  &cacuent@ #310;
  &caclipr@ #311;
  &cacueba@ #313;
  &anm0001@ #321;
  clapw001 #901;
|FIN;

|VARIABLES;
 uno        = "";
 dos        = "";
 mascara    = "";
 aBlanc     = "";
 aAlfa1     = "";
 aAlfa2     = "";
 nNume1     = 0;
 nNume2     = 0;

 aDef       = "";
 aDef330    = "";
 aDef310    = "";
 aDef311    = "";
 aDef313    = "";
 aDef321    = "";

 nInkey     = 0;
 nDEmpresa  = 0;
 nHEmpresa  = 0;
 nDPeriodo  = 0;
 nHPeriodo  = 0;

 nCampo     = 0;
 nCamp1     = 0;
 nCamp2     = 0;
 nCamp3     = 0;
 nCamp4     = 0;

 nAlgun     = 0;
 aPath      = "";
 aBase      = "";
 aFiche     = "";

 aEmpresa   = "";
 aContenido = "";
 aVarAlfa1  = "";
 aCif       = "";
 aCaja      = "";

 nSwExi     = 0;

 aPathA     = "";
 aPathH     = "";
 aNombA     = "";
 nHadle     = 0;
 aField     = "";

 nEstado    = 0;
 nError     = 0;
 aNomCta    = "";
 aNombre    = "";
 aContra    = "";
 nContarAp  = 0;
 aTipo      = "";
 aFichero   = "";

 nPrimero   = 0;
 nDiario    = 0;
 fFecha     = @;
 nDocumento = 0;
 nDocumAct  = 0;
 nApunte    = 0;

 nMulti     = 0;
 aTMov      = "";
 aTMo1      = "";
 aTMo2      = "";
 nSerie     = 0;
 nLibro     = 0;
 aDesLib    = "";
 aSerie     = "";
 aTipoIVA   = "";
 nConcepto  = 0;
 nNumFra    = 0;
 aDfDepC    = "";
 nSExiste   = 0;
 nEmp       = 0;

 nRecibo    = 0;
 aParIva    = "";
 nParIva    = 0;
 nParIvaI   = 0;
 nTpIva     = 0;
 nTpRec     = 0;
 aNomClPr   = "";
 aCifClPr   = "";
 aCtaClPr   = "";
 nImporte   = 0;
 aSigno     = "";
 nSwGraba   = 0;
 nLineaIVA  = 0;
 &eaCtaCon  = "";
 &PRMNT_enError = 0;
 nPeriodo   = 0;
 aQueQuiero = "";
 aNumCampos = "";
 nNumCampos = 0;
 aCamp44    = "";
 aCamp46    = "";
 nSwB       = 0;
 nSwMas     = 0;
 nPrimer    = 0;
 aTipoalta  = "";
 aDigito2   = "";

 nTp100    = 0;
 sw_eof    = 0;
 aEl10     = "";
 aEl13     = "";
|FIN;


|INCLUYE dsclap_i;


|CALCULO Tipo8z008; |TIPO 8;
 |HAZ eLeePathFich;
 |SI eswError = 1;
     |PTEC 807;
     |ACABA;
 |FINSI;

 aAlfa1   = eaPathDatos - "fich/";
 eaPathDef = aAlfa1 + "def/";
 aDef     = eaPathDef + "canempre";
 |CARGA_DEF aDef, canempr@;
 |SI FSalida ! 0;
     |MENAV "    Error Cargado Fichero de Empresas. Proceso Interrumpido";
     |PTEC 807;
     |ACABA;
 |FINSI;
 aDef330 = aDef;

 |PATH_DAT #330 eaPathDatos;

|FCAL;

|CALCULO Tipo9z008; |TIPO 9;
  |SI aDef321 ! ""; |DESCARGA_DEF #anm0001@; aDef321 = ""; |FINSI;
  |SI aDef313 ! ""; |DESCARGA_DEF #caclipr@; aDef313 = ""; |FINSI;
  |SI aDef311 ! ""; |DESCARGA_DEF #caclipr@; aDef311 = ""; |FINSI;
  |SI aDef310 ! ""; |DESCARGA_DEF #cacuent@; aDef310 = ""; |FINSI;
  |SI aDef330 ! ""; |DESCARGA_DEF #canempr@; aDef330 = ""; |FINSI;
|FCAL;

|| ////////////////////////////////////////////////////////////////
|RUTINA infA2;
   #330#2 = nDEmpresa;
   #330#3 = 0;
|FRUT;

|RUTINA supA2;
   #330#2 = nHEmpresa;
   #330#3 = 9;
|FRUT;

|CALCULO LeeEmpresa; |TIPO 0;
  nInkey = FSalida;

  |C_M #0Campo, 0, aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

  nCamp1 = Campo;
  nCamp2 = nCamp1 + 10;
  nCamp3 = nCamp1 + 20;
  nCamp4 = nCamp1 + 30;
  |SI nInkey = 10;
     nDEmpresa = 0; nHEmpresa = 99999;
     |ABRE #330;
     |CONSULTA_F_CLAVE #1#330,infA2,supA2;
     #0nCamp1 = #330#2; |PINTA #0nCamp1;
     #0nCamp2 = #330#3; |PINTA #0nCamp2;
     |CIERRA #330;
     |PTEC 802;
     |ACABA;
  |FINSI;

  |SI #0Campo = 0;
      |ACABA;
  |FINSI;

  |ABRE #330;
  #330#2 = #0nCamp1;
  #330#3 = 0;
  |LEE 011#330];
  |SI FSalida ! 0;  |O #330#2 ! #0nCamp1;
      |MENAV "     NO Existe la empresa Contable";
      |ERROR;
  |SINO;
      #0nCamp4 = #330#1; |PINTA #0nCamp4;
  |FINSI;
  |CIERRA #330;
|FCAL;

|CALCULO Select; |TIPO 0;
     |SI #0#6 = "N";
         #0#7  = 1;     |PINTA #0#7;  |C_M #0#7, 1, "N";
         #0#8  = 99999; |PINTA #0#8;  |C_M #0#8, 1, "N";
         #0#9  = 0;     |PINTA #0#9;  |C_M #0#9, 1, "N";
         #0#10 = 9;     |PINTA #0#10; |C_M #0#10,1, "N";
         |PARA nCampo = 11;  |SI nCampo [ 30;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |C_M #0#7, 1, "S";
         |C_M #0#8, 1, "S";
         |C_M #0#9, 1, "S";
         |C_M #0#10,1, "S";
         |PARA nCampo = 11; |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0; |PINTA #0nCampo; |C_M #0nCampo, 1, "N";
               nCamp2 = nCampo + 10;
               nCamp3 = nCampo + 20;
               nCamp4 = nCampo + 30;
               #0nCamp2 = 0; |PINTA #0nCamp2; |C_M #0nCamp2, 1, "N";
               #0nCamp3 = ""; |PINTA #0nCamp3;
               #0nCamp4 = ""; |PINTA #0nCamp4;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas; |TIPO 0;

     |PARA nCampo = Campo;  |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
           nCamp2 = nCampo + 10;
           nCamp3 = nCampo + 20;
           nCamp4 = nCampo + 30;
           |SI #0Campo = 0;
               |SI Campo ! nCampo;
                   #0nCampo = 0;  |C_M #0nCampo, 1, "N"; |PINTA #0nCampo;
               |FINSI;
               #0nCamp2 = 0;  |PINTA #0nCamp2; |C_M #0nCamp2, 1, "N";
               #0nCamp3 = ""; |PINTA #0nCamp3;
               #0nCamp4 = ""; |PINTA #0nCamp4;
           |SINO;
               |C_M #0nCampo, 1, "S";
               |C_M #0nCamp2, 1, "S";
          |FINSI;
    |SIGUE;
|FCAL;

|CALCULO LeeEmpresaP; |TIPO 0;
  nInkey = FSalida;

  |C_M #0Campo, 0, aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

  |SI nInkey = 2; |ACABA; |FINSI;

  nCamp2 = Campo;
  nCamp1 = nCamp2 - 10;
  nCamp3 = nCamp1 + 20;
  nCamp4 = nCamp1 + 30;
  |SI nInkey = 10;
     nDEmpresa = #0nCamp1; nHEmpresa = #0nCamp1;
     |ABRE #330;
     |CONSULTA_F_CLAVE #1#330,infA2,supA2;
     #0nCamp2 = #330#3; |PINTA #0nCamp2;
     |CIERRA #330;
  |FINSI;

  |ABRE #330;
  #330#2 = #0nCamp1;
  #330#3 = #0nCamp2;
  |LEE 011#330=;
  |SI FSalida ! 0;
      |MENAV "     NO Existe Periodo en la Empresa Contable";
      |ERROR;
  |SINO;
      |SI #330#26 > 80;
          nNume1 = #330#26 + 1900;
      |SINO;
          nNume1 = #330#26 + 2000;
      |FINSI;
      #0nCamp3 = nNume1; |PINTA #0nCamp3;
      #0nCamp4 = #330#1; |PINTA #0nCamp4;
  |FINSI;
  |CIERRA #330;
|FCAL;

|| /////////////////////////////////////////////////////////////////////////
|| /////////////////////////////////////////////////////////////////////////
|| /////////////////////////////////////////////////////////////////////////

|PROCESO CargaAuxiliares;   || de cada empresa

 aFichero = "1w" + Usuario;
 |NOME_DAT #901 aFichero;     || Auxiliar relaciones

 |DELINDEX #901;
|FPRC;

|PROCESO LeeCuenta;
    nSwExi  = 0;
    aNomCta = "";
    |ABRE #310;
    |DDEFECTO #310;
    #310#0 = eaCuenta;
    #310#1 = enNivel;
    |LEE 040#310=;
    |SI FSalida ! 0;
        nSwExi = 1;
    |FINSI;
    |CIERRA #310;
    aNomCta = #310#2;
|FPRC;

|PROCESO AltaCuenta;
    |ABRE #310;
    |DDEFECTO #310;
    #310#0 = eaCuenta;
    #310#1 = enNivel;
    |LEE 101#310=;
    |SI FSalida ! 0;
        |DDEFECTO #310;
        #310#0 = eaCuenta;
        #310#1 = enNivel;
        #310#2 = #901#1;
        |HAZ defectos;
        |GRABA 020#310b;
    |FINSI;
    |LIBERA #310;
    |CIERRA #310;

    aTipoalta = "";
    aDigito2  = eaCuenta % 102;
    |SI aDigito2 = "43";  |O aDigito2 = "44";
        aTipoalta = "C";
    |SINO;
       |SI aDigito2 = "40";  |O aDigito2 = "41";  |O aDigito2 = "52"; |O aDigito2 = "17";
           aTipoalta = "P";
       |FINSI;
   |FINSI;

   |SI aTipoalta = ""; |ACABA; |FINSI;

   |ABRE #311;
   #311#23 = aTipoalta;
   #311#10 = eaCuenta;
   #311#11 = enNivel;
   |LEE 101#311=;
   |SI FSalida ! 0;
       #311#23 = aTipoalta;
       #311#10 = eaCuenta;
       #311#11 = enNivel;
       #311#1  = #901#1;
       #311#9  = #901#2;
       |GRABA 020#311b;
   |FINSI;
   |LIBERA #311;
   |CIERRA #311;
|FPRC;


|| ///////////////////////////////////////////////////////////////////////
|PROCESO LeeAuxw001;

 |PINTA 2324, #901#0;
 eaCuenta = #901#0;|HAZ SacaNivel;
 |HAZ AltaCuenta;

 #3#0 = enEmpresa;
 #3#1 = enPeriodo;
 #3#2 = #901#0;   ||Cuenta
 |LEE 101#3=;
 nEstado = FSalida;
 #3#0 = enEmpresa;
 #3#1 = enPeriodo;
 #3#2 = #901#0;   ||Cuenta
 |SI nEstado ! 0;
     #3#3  = #901#1;
     #3#4  = #901#2;
     #3#10 = eaNombreEmp;
     #3#11 = enEjer;
     eaCuenta = #3#0; |HAZ SacaNivel; #3#12 = enNivel;
 |FINSI;

 aAlfa1 = #901#4; |QBF aAlfa1;
 |SI aAlfa1 ! "";
     |SI #901#4 ! #3#5;
         #3#5 = #901#4;
         eaCuenta = #3#5; |HAZ SacaNivel; #3#13 = enNivel;
         |HAZ LeeCuenta;
         #3#6 = aNomCta;
     |FINSI;
 |FINSI;

 aAlfa1 = #901#3; |QBF aAlfa1;
 |SI aAlfa1 ! "";
     |SI #901#3 ! #3#7;
         #3#7 = #901#3;
         eaCuenta = #3#7; |HAZ SacaNivel; #3#14 = enNivel;
         |HAZ LeeCuenta;
         #3#8 = aNomCta;
     |FINSI;
 |FINSI;

 |SI nEstado = 0; |GRABA 020#3a; |FINSI;
 |SI nEstado ! 0; |GRABA 020#3n; |FINSI;
 |LIBERA #3;
|FPRC;

|DEFBUCLE LeeAuxw001;
 #901#1;
 ;
 "            ";
 "zzzzzzzzzzzz";
 ;
 NULL, LeeAuxw001;
|FIN;


|PROCESO ElPase;
 |BLIN 23;
 |PINTA 2306, "Grabando Cuentas: ";

 |ABRE #3;
 |BUCLE LeeAuxw001;
 |CIERRA #3;
|FPRC;

|| /////////////////////////////////////////////////////////////////////////
|PROCESO GrabaRel;
 |ABRE #901;
 #901#0 = eaCuenta;
 |LEE 001#901=;
 |SI FSalida ! 0;
     |DDEFECTO #901;
     #901#0 = eaCuenta;
     |GRABA 020#901c;
 |FINSI;
 |SI aTipo = "C";
     #901#1 = aNombre;
     #901#2 = aCif;
     #901#3 = aCaja;
 |FINSI;
 |SI aTipo = "R";
     #901#4 = aContra;
 |FINSI;
 |GRABA 020#901a;
 |CIERRA #901;
|FPRC;

|PROCESO LeoUnoaUno;
       sw_eof = 0;
       aField = "";
       nNume1 = 0;
       |PARA; |SI sw_eof = 0; |HACIENDO;
              |XLEE nHadle, 1, aAlfa1;
              |SI FSalida > 0;
                  |SI aAlfa1 = aEl10;
                      sw_eof = 1;
                  |SINO;
                      |SI aAlfa1 ! aEl13;
                          aField = aField + aAlfa1;
                          nNume1 = nNume1 + 1;
                      |FINSI;
                  |FINSI;
              |SINO;
                   sw_eof = 1;
              |FINSI;
        |SIGUE;
        FSalida = nNume1;
|FPRC;

|PROCESO LeeTipoC;
   aTipo = "C";
   ||... No es fijo a 91
   |HAZ LeoUnoaUno;
   |SI FSalida > 0;
       eaCuenta = aField %  112;
       aNombre  = aField % 1350;
       aCif     = aField % 6315;
       aCaja    = aField % 7812;
       |SI FSalida ] 92;
           aAlfa1   = aField % 9102; nTp100   = aAlfa1;
       |FINSI;
       |HAZ GrabaRel;
       |PINTA 2345, "Cuenta:                 "; |PINTA 2353, #901#0;
   |FINSI;
|FPRC;

|PROCESO LeeTipoM;
  aTipo = "M";
               || ...  No fijo a 190. |XLEE nHadle, 190, aField;
  |HAZ LeoUnoaUno;
|FPRC;

|PROCESO LeeTipoR;
   aTipo = "R";
               || ... No fijo a 26,  |XLEE nHadle, 26, aField;
   |HAZ LeoUnoaUno;
   |SI FSalida > 0;
       eaCuenta = aField %  112;
       aContra  = aField % 1312;
       |HAZ GrabaRel;
       |PINTA 2345, "Relacion:               "; |PINTA 2355, #901#0;
   |FINSI;
|FPRC;

|PROCESO HazTodo;

   enEmpresa = #330#2;
   enPeriodo = #330#3;
   |HAZ eVarEmpresa;
   aAlfa1 = #330#2; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
   aAlfa2 = #330#3; |QBF aAlfa2; aAlfa2 = ("0"     + aAlfa2) % -101;
   aEmpresa = aAlfa1 + aAlfa2;

   |PATH_DAT #310 eaPathConta;
   |PATH_DAT #311 eaPathConta;
   |PATH_DAT #313 eaPathConta;

   |HAZ eAnalitica;

   eaDestino = eaPathExImp + aEmpresa + "/" + eaFichAsto;
   aFiche = eaDestino; |QBF aFiche;
   eaFichLocal = aFiche;
   eaFicnombre = eaFichAsto;
   |HAZ eNombre;
   aFiche      = eaFichLocal;

   |XABRE "B", aFiche,nHadle;
   |SI FSalida < 0;
       |XABRE "CB", aFiche,nHadle;
   |FINSI;
   |SI FSalida < 0;
       Pos = -1;
       |MENAV "    No existe Datos para Actualizar ";
       |ACABA;
   |FINSI;

   |HAZ CargaAuxiliares;

   |BLIN 23;
   |PINTA 2302, "Empresa: ";
   |PINTA 2311, #330#2;
   |PINTA 2317, "Leyendo Fichero a importar. ";
   nDocumento = 0;
   nPrimero   = 0;
   |PARA; |SI; |HACIENDO;
          |XLEE nHadle, 3, aField;
          |SI FSalida > 0;
             aAlfa1 = aField;
             |ET Ditribuye;
                 |SI aAlfa1 = "[C]"; |HAZ LeeTipoC; |FINSI;
                 |SI aAlfa1 = "[M]"; |HAZ LeeTipoM; |FINSI;
                 |SI aAlfa1 = "[R]"; |HAZ LeeTipoR; |FINSI;
          |SINO;
              |SAL;
          |FINSI;
   |SIGUE;
   |XCIERRA nHadle;
   Pos = -1;

   |HAZ ElPase;

   |DELINDEX #901;
|FPRC;

|DEFBUCLE LeeCanempre;
 #canempr@#1002;
 ;
 nDEmpresa, nDPeriodo;
 nHEmpresa, nHPeriodo;
 ;
 NULL, HazTodo;
|FIN;

|| /////////////////////////////////////////////////////////////////////////
|CALCULO Tipo3z008; |TIPO 3;
  |SI #0#0 = "NO"; |ACABA; |FINSI;

  aEl10 = &10;
  aEl13 = &13;

  aDCuenta = #0#1;
  aHCuenta = #0#2;

  |DFICB aBase; |QBF aBase;
  |DBASE aAlfa1; |QBF aAlfa1;
  aPathH = aAlfa1 + "historic/";

  aDef     = eaPathDef + "cacuenta";
  |CARGA_DEF aDef, cacuent@;
  |SI FSalida ! 0;
      |MENAV "    Error Cargado Fichero de Cuentas. Proceso Interrumpido";
      |ACABA;
  |FINSI;
  aDef310 = aDef;

  aDef     = eaPathDef + "caclipro";
  |CARGA_DEF aDef, caclipr@;
  |SI FSalida ! 0;
      |MENAV "    Error Cargado Fichero de Clientes y Proveedores. Proceso Interrumpido";
      |ACABA;
  |FINSI;
  aDef311 = aDef;

  aDef     = eaPathDef + "cacuebal";
  |CARGA_DEF aDef, cacueba@;
  |SI FSalida ! 0;
      |MENAV "    Error Cargado Fichero de Cuentas. Proceso Interrumpido";
      |ACABA;
  |FINSI;
  aDef313 = aDef;

  |SI #0#6 = "S";
      nDEmpresa = #0#7; nDPeriodo = #0#9;
      nHEmpresa = #0#8; nHPeriodo = #0#10;
      |BUCLE LeeCanempre;
  |SINO;
      |PARA nCampo = 11; |SI nCampo [ 20; |HACIENDO nCampo = nCampo + 1;
            |SI #0nCampo ! 0;
                nCamp2 = nCampo + 10;
                nDEmpresa = #0nCampo; nHEmpresa = nDEmpresa;
                nDPeriodo = #0nCamp2; nHPeriodo = nDPeriodo;
                |BUCLE LeeCanempre;
            |FINSI;
      |SIGUE;
  |FINSI;
|FCAL;
