|FICHEROS;
  clapz004 #0;

  clapm003 #3,MANTE;

  clapw006 #56,MANTE;

  &canempr@ #330;
  &cacuent@ #310;
  &caclipr@ #311;
  &camanba@ #312;
|FIN;

|VARIABLES;
 aAlfa1     = "";
 aAlfa2     = "";
 nNume1     = 0;
 nNume2     = 0;
 nNume3     = 0;
 nEstado    = 0;
 nAny       = 0;

 aDef       = "";
 aDef330    = "";
 aDef310    = "";
 aDef311    = "";
 aDef312    = "";

 nInkey     = 0;
 nDEmpresa  = 0;
 nHEmpresa  = 0;
 nDPeriodo  = 0;
 nHPeriodo  = 0;

 nAlgun     = 0;
 aPath      = "";
 aBase      = "";

 aEmpresa   = "";
 aContenido = "";
 aVarAlfa1  = "";
 aCif       = "";
 aCaja      = "";

 nSalir = 0;
 nDigO  = 0;
 nDigit = 0;
|FIN;

|INCLUYE dsclap_i;


|| ////////////////////////////////////////////////////////////////
|RUTINA infA2;
   #330#2 = nDEmpresa;
   #330#3 = 0;
|FRUT;

|RUTINA supA2;
   #330#2 = nHEmpresa;
   #330#3 = 9;
|FRUT;


|CALCULO LeeEmpresa; |TIPO 0;
  nInkey = FSalida;

  |SI nInkey = 10;
     nDEmpresa = 0;
     nHEmpresa = 99999;
     |ABRE #330;
     |CONSULTA_F_CLAVE #1#330,infA2,supA2;
     #0#0 = #330#2; |PINTA #0#0;
     #0#1 = #330#3; |PINTA #0#1;
     |CIERRA #330;
     |PTEC 802;
     |ACABA;
  |FINSI;

  |SI nInkey = 11;
      |HAZ Copiar;
      |ERROR;
      |ACABA;
  |FINSI;

  |ABRE #330;
  #330#2 = #0#0;
  #330#3 = 0;
  |LEE 011#330];
  |SI FSalida ! 0;  |O #330#2 ! #0#0;
      |MENAV "     NO Existe la empresa Contable";
      |ERROR;
  |SINO;
      #0#2 = #330#1; |PINTA #0#2;
      #0#4 = #330#8; |PINTA #0#4;
  |FINSI;
  |CIERRA #330;
|FCAL;

|CALCULO LeeEmpresaP; |TIPO 0;
  nInkey = FSalida;
  |SI nInkey = 2; |ACABA; |FINSI;

  |SI nInkey = 10;
     nDEmpresa = #0#0;
     nHEmpresa = #0#0;
     |ABRE #330;
     |CONSULTA_F_CLAVE #1#330,infA2,supA2;
     #0#0 = #330#2; |PINTA #0#0;
     #0#1 = #330#3; |PINTA #0#1;
     |CIERRA #330;
  |FINSI;

  |ABRE #330;
  #330#2 = #0#0;
  #330#3 = #0#1;
  |LEE 011#330=;
  |SI FSalida ! 0;
      |MENAV "     NO Existe Periodo en la Empresa Contable";
      |ERROR;
  |SINO;
      |SI #330#26 > 80;
          nNume1 = #330#26 + 1900;
      |SINO;
          nNume1 = #330#26 + 2000;
      |FINSI;
      #0#2 = #330#1; |PINTA #0#2;
      #0#3 = nNume1; |PINTA #0#3;
      #0#4 = #330#8; |PINTA #0#4;
  |FINSI;
  |CIERRA #330;
|FCAL;

|| /////////////////////////////////////////////////////////////////////////
|| /////////////////////////////////////////////////////////////////////////
|| /////////////////////////////////////////////////////////////////////////

|PROCESO ClaveBaja3;
 #3#0 = enEmpresa;
 #3#1 = enPeriodo;
 #3#2 = "            ";
|FPRC;

|PROCESO ClaveAlta3;
 #3#0 = enEmpresa;
 #3#1 = enPeriodo;
 #3#2 = "zzzzzzzzzzzz";
|FPRC;


|| /////////////////////////////////////////////////////////////////////////
|CALCULO Tipo3z004; |TIPO 12;
  nInkey = FSalida;

  |SI nInkey = 1; |O nInkey = 7; nSalir = 1; |ACABA; |FINSI;
  enEmpresa = #0#0;
  enPeriodo = #0#1;
  |HAZ eLeeEmpresa;

  aAlfa1 = #330#2; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
  aAlfa2 = #330#3; |QBF aAlfa2; aAlfa2 = ("0"     + aAlfa2) % -101;
  aEmpresa = aAlfa1 + aAlfa2;

  aPath = eaPathDatos + aAlfa1 + aAlfa2 + "/";

  aDef     = eaPathDef + "cacuenta";
  |CARGA_DEF aDef, cacuent@;
  |SI FSalida ! 0;
      |MENAV "    Error Cargado Fichero de Cuentas. Proceso Interrumpido";
      aDef310 = "";
      |ACABA;
  |FINSI;
  aDef310 = aDef;
  aDef     = eaPathDef + "caclipro";
  |CARGA_DEF aDef, caclipr@;
  |SI FSalida ! 0;
      |MENAV "    Error Cargado Fichero de Clientes. Proceso Interrumpido";
      |DESCARGA_DEF #cacuent@; aDef310 = "";
      aDef311 = "";
      |ACABA;
  |FINSI;
  aDef311 = aDef;

 |PATH_DAT #310 aPath;
 |PATH_DAT #311 aPath;


 |ENTLINEAL #1#3, -3, 2, 22, 0, ClaveBaja3, ClaveAlta3;

 |SI aDef311 ! ""; |DESCARGA_DEF #caclipr@; aDef311 = ""; |FINSI;
 |SI aDef310 ! ""; |DESCARGA_DEF #cacuent@; aDef310 = ""; |FINSI;

|FCAL;


|PROGRAMA;

 |HAZ eLeePathFich;
 |SI eswError = 1;
     |ACABA;
 |FINSI;

 aAlfa1    = eaPathDatos - "fich/";
 eaPathDef = aAlfa1 + "def/";
 aDef      = eaPathDef + "canempre";
 |CARGA_DEF aDef, canempr@;
 |SI FSalida ! 0;
     |MENAV "    Error Cargado Fichero de Empresas. Proceso Interrumpido";
     |PTEC 807;
     |ACABA;
 |FINSI;
 aDef330 = aDef;

 |PATH_DAT #330 eaPathDatos;

 |CLS;
 |CABEZA "Relaccion Cuentas-Contrapartidas y Bancos";

 |ABRE #0;
 |DDEFECTO #0;
 |LEE 001#0p;
 |SI FSalida ! 0;
     |DDEFECTO #0;
     |GRABA 020#0c;
 |FINSI;

 |PINPA #0#0;
 |PINDA #0#0;
 |ENTLINEAL #1#3, -3, 7, 22, 0, ClaveBaja3, ClaveAlta3;

 nSalir = 0;
 |PARA; |SI nSalir = 0; |HACIENDO;
          |ENDATOS #2#0;
          |SI FSalida ! 0;
              nSalir = 1;
          |FINSI;
 |SIGUE;
 |GRABA 020#0a;
 |CIERRA #0;

  |SI aDef312 ! ""; |DESCARGA_DEF #camanba@; aDef312 = ""; |FINSI;
  |SI aDef311 ! ""; |DESCARGA_DEF #caclipr@; aDef311 = ""; |FINSI;
  |SI aDef310 ! ""; |DESCARGA_DEF #cacuent@; aDef310 = ""; |FINSI;
  |SI aDef330 ! ""; |DESCARGA_DEF #canempr@; aDef330 = ""; |FINSI;
|FPRO;

|| *****************************************************************
|RUTINA inf24;
   #310#0 = "            ";
   #310#1 = 0;
   #310#3 = "S";
|FRUT;

|RUTINA sup24;
   #310#0 = "zzzzzzzzzzzz";
   #310#1 = 9;
   #310#3 = "S";
|FRUT;

|CALCULO LeeContra;
   eaCuenta = #56Campo;
   salidas  = FSalida;
   |HAZ FiltraPuntos;
   #56Campo = eaCuenta;
   |PINTA #56Campo;
   |SI nInkey = 10;
      |ABRE #310;
      #310#0 = #56Campo;
      |CONSULTA_F_CLAVE #1#310,inf24,sup24;
      #56Campo = #310#0; |PINTA #56Campo;
      |CIERRA #310;
   |FINSI;
|FCAL;

|CALCULO LeeContraO;
 nInkey = FSalida;
 |HAZ LeeContra;
|FCAL;

|CALCULO LeeContraD;
 nInkey = FSalida;
 |HAZ LeeContra;
|FCAL;


|CALCULO LeeEmp;

                 nNume1 = 6; nNume2 = 10; nNume3 = 12;
  |SI Campo = 3; nNume1 = 7; nNume2 = 11; nNume3 = 13; |FINSI;
  |SI nInkey = 10;
     nDEmpresa = 0;
     nHEmpresa = 99999;
     |ABRE #330;
     |CONSULTA_F_CLAVE #1#330,infA2,supA2;
     #56Campo  = #330#2; |PINTA #56Campo;
     #56nNume2 = #330#3; |PINTA #56nNume2;
     |CIERRA #330;
  |FINSI;

  nEstado = 0;
  |ABRE #330;
  #330#2 = #56Campo;
  #330#3 = 0;
  |LEE 011#330];
  |SI FSalida ! 0;  |O #330#2 ! #56Campo;
      nEstado = 1;
      |MENAV "     NO Existe la empresa Contable";
  |FINSI;
  |CIERRA #330;
|FCAL;

|CALCULO LeeEmpPer;

                  nNume1 = 6; nNume2 = 0; nNume3 = 12;
  |SI Campo = 11; nNume1 = 7; nNume2 = 3; nNume3 = 13; |FINSI;
  |SI nInkey = 10;
     nDEmpresa = #56nNume2;
     nHEmpresa = #56nNume2;
     |ABRE #330;
     |CONSULTA_F_CLAVE #1#330,infA2,supA2;
     #56nNume2 = #330#2; |PINTA #56nNume2;
     #56Campo  = #330#3; |PINTA #56Campo;
     |CIERRA #330;
  |FINSI;

  nEstado = 0;
  |ABRE #330;
  #330#2 = #56nNume2;
  #330#3 = #56Campo;
  |LEE 011#330=;
  |SI FSalida ! 0;
      nEstado = 1;
      |MENAV "     NO Existe Periodo en la Empresa Contable";
  |SINO;
      |SI #330#26 > 80;
          nAny = #330#26 + 1900;
      |SINO;
          nAny = #330#26 + 2000;
      |FINSI;
      #56nNume1 = #330#1; |PINTA #56nNume1;
      #56nNume3 = nAny;   |PINTA #56nNume3;
      |SI #330#13 = 1; nDigit = #330#14; |FINSI;
      |SI #330#13 = 2; nDigit = #330#15; |FINSI;
      |SI #330#13 = 3; nDigit = #330#16; |FINSI;
      |SI #330#13 = 4; nDigit = #330#17; |FINSI;
      |SI #330#13 = 5; nDigit = #330#18; |FINSI;
      |SI #330#13 = 6; nDigit = #330#19; |FINSI;
  |FINSI;
  |CIERRA #330;
|FCAL;

|PROCESO LeeEmpO;
  nInkey = FSalida;
  |C_M #56Campo,0,aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

  |HAZ LeeEmp;
  |SI nEstado = 1;
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO LeePerO;
  nInkey = FSalida;
  |SI nInkey = 2; |ACABA; |FINSI;

  |C_M #56Campo,0,aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

  |HAZ LeeEmpPer;
  |SI nEstado = 1;
      |ERROR;
      |ACABA;
  |FINSI;
  nDigO = nDigit;

  |ABRE #3;
  #3#0 = #56#0;
  #3#1 = #56#10;
  #3#2 = "           ";
  |LEE 001#3];
  |SI FSalida ! 0; |O #3#0 ! #56#0; |O #3#1 ! #56#10;
      |MENAV "    ERROR. NO EXISTE DEFINICION PARA ESTA EMPRESA ";
      |ERROR;
  |FINSI;
  |CIERRA #3;
|FPRC;

|PROCESO LeeEmpD;
  nInkey = FSalida;
  |C_M #56Campo,0,aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;


  |HAZ LeeEmp;
  |SI nEstado = 1;
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO LeePerD;
  nInkey = FSalida;
  |SI nInkey = 2; |ACABA; |FINSI;

  |C_M #56Campo,0,aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

  |HAZ LeeEmpPer;
  |SI nEstado = 1;
      |ERROR;
      |ACABA;
  |FINSI;

  |SI #56#0 = #56#3; |Y #56#10 = #56#11;
      |MENAV "    ERROR. La Empresa/Periodo Destino es la misma que la Empresa/Periodo Origen";
      |ERROR;
      |ACABA;
  |FINSI;

  |SI nDigit ! nDigO;
      |MENAV "    No corresponde longitud de Cuentas";
      |ERROR;
      |ACABA;
  |FINSI;

  |ABRE #3;
  #3#0 = #56#3;
  #3#1 = #56#11;
  #3#2 = "           ";
  |LEE 001#3];
  |SI FSalida = 0; |Y #3#0 = #56#3; |Y #3#1 = #56#11;
      |CONFI "    NYA EXISTE DEFINICION PARA ESTA EMPRESA. CONTINUAR  ";
      |SI FSalida ! 0;
          |ERROR;
      |FINSI;;
  |FINSI;
  |CIERRA #3;
|FPRC;
|| *******************************************************************

|PROCESO LuegoCopia;
     |SALVA_FICHA #3;
     #3#0  = #56#3;
     #3#1  = #56#11;
     #3#10 = #56#7;
     #3#11 = #56#13;
     |GRABA 020#3n;
     |REPON_FICHA #3;
     |LIBERA #3;
|FPRC;

|DEFBUCLE LuegoCopia;
     #3#1;
     ;
     #56#0, #56#10, aDCuenta;
     #56#0, #56#10, aHCuenta;
     be;
     NULL, LuegoCopia;
|FIN;


|PROCESO PrimeroBorra;
     |BORRA 020#3a;
     |LIBERA #3;
|FPRC;

|DEFBUCLE PrimeroBorra;
     #3#1;
     ;
     #56#3, #56#11, aDCuenta;
     #56#3, #56#11, aHCuenta;
     be;
     NULL, PrimeroBorra;
|FIN;

|PROCESO Copiar;

 |PUSHV 0706 1858;
 |PINPA #0#56;
 |ATRI R;
 |PINTA 0807, " Copiar Relacion Cuenta- Contrap. y Caja o Banco";
 |ATRI S;
 |PINTA 1407, " Cuentas Contables";
 |ATRI 0;

 |DDEFECTO #56;
 #56#3  = #0#0;
 #56#10 = #0#1;
 #56#7  = #0#2;
 #56#0  = enEmpGral;
 #56#6  = enPerGral;
 |PINDA #0#56;
 |ENDATOS #1#56;
 |SI FSalida ! 0;
     |POPV;
     |ACABA;
 |FINSI;

 aDCuenta  = #56#1;
 aHCuenta  = #56#4;
 |BUCLE PrimeroBorra;

 |BUCLE LuegoCopia;
 |POPV;
|FPRC;
