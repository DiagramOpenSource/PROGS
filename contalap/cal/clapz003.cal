|FICHEROS;
  clapz003 #0;
  clapm002 #2;
  clapm003 #3;
  clapm004 #4;

  &canempr@ #330;
  &cacuent@ #310;
  &caclipr@ #311;
  &cacueba@ #313;

  &caconce@ #306;  || Conceptos Contables

  &caivaas@ #331;
  &caconas@ #332;  || contador documentos
  &calibro@ #333;  || libros
  &caivase@ #334;  || series
  &camandi@ #335;  || Diarios

  &caenlac@ #390;

  clapw001 #901;
  clapw002 #902;
  clapw003 #903;
|FIN;

|VARIABLES;
 aAlfa1     = "";
 aAlfa2     = "";
 aAlfa3     = "";
 aAlfa4     = "";
 aAlfa6     = "";
 nNume1     = 0;
 nNume2     = 0;

 aDef       = "";
 aDef330    = "";
 aDef310    = "";
 aDef311    = "";
 aDef313    = "";
 aDef390    = "";

 aDef331    = "";
 aDef332    = "";
 aDef333    = "";
 aDef334    = "";
 aDef335    = "";

 aDef306    = "";

 nCampo     = 0;
 nInkey     = 0;
 nDEmpresa  = 0;
 nHEmpresa  = 0;
 nDPeriodo  = 0;
 nHPeriodo  = 0;

 aPath      = "";
 aPathA     = "";
 aPathH     = "";
 aNombA     = "";
 aBase      = "";
 aFiche     = "";
 nHadle     = 0;
 aField     = "";
 aEmpresa   = "";

 nEstado    = 0;
 nError     = 0;
 aNomCta    = "";
 aNombre    = "";
 aCif       = "";
 aCaja      = "";
 aContra    = "";
 nContarAp  = 0;
 aTipo      = "";
 aFichero   = "";

 nPrimero   = 0;
 nDiario    = 0;
 fFecha     = @;
 nDocumento = 0;
 nDocumAct  = 0;
 nApunte    = 0;

 nMulti     = 0;
 aTMov      = "";
 aTMo1      = "";
 aTMo2      = "";
 nSerie     = 0;
 nLibro     = 0;
 aDesLib    = "";
 aSerie     = "";
 aTipoIVA   = "";
 nConcepto  = 0;
 nNumFra    = 0;
 aDfDepC    = "";
 nSExiste   = 0;
 nEmp       = 0;
 aProyec    = "";
 aDepar     = "";
 nRecibo    = 0;
 aParIva    = "";
 nParIva    = 0;
 nParIvaI   = 0;
 nParIvaR   = 0;
 nTpIva     = 0;
 nTpRec     = 0;
 aNomClPr   = "";
 aCifClPr   = "";
 aCtaClPr   = "";
 nImporte   = 0;
 aSigno     = "";
 nSwGraba   = 0;
 nLineaIVA  = 0;
 &eaCtaCon  = "";
 &PRMNT_enError = 0;
 nPeriodo   = 0;
 aQueQuiero = "";
 aNumCampos = "";
 nNumCampos = 0;
 aCamp44 = "";
 aCamp46 = "";
 nSwB    = 0;
 nSwMas  = 0;
 nPrimer = 0;
 aTipoalta = "";
 aDigito2  = "";
 nTp100    = 0;
 sw_eof    = 0;
 aEl10     = "";
 aEl13     = "";
 &eaFitxer = "";

 nSwContra = 0;
 nEsContra = 0;
 nPara1    = 0;
 nPara2    = 0;
 aCContra  = "";
 nTipoIrpf = 0;
 nBaseIrpf = 0;
 nConcIrpf = 0;

 snAboV    = "";
 snAboC    = "";
 snAbono   = "";
 nMinus    = 0;
 nGraba    = 0;
|FIN;

|INCLUYE dsclap_i;

|CALCULO Tipo8z003; |TIPO 8;
 |HAZ eLeePathFich;
 |SI eswError = 1;
     |PTEC 807;
     |ACABA;
 |FINSI;

 aAlfa1    = eaPathDatos - "fich/";
 eaPathDef = aAlfa1 + "def/";
 aDef      = eaPathDef + "canempre";
 |CARGA_DEF aDef, canempr@;
 |SI FSalida ! 0;
     |MENAV "    Error Cargado Fichero de Empresas. Proceso Interrumpido";
     |PTEC 807;
     |ACABA;
 |FINSI;
 aDef330 = aDef;

 aDef      = eaPathDef + "caivaasi";
 |CARGA_DEF aDef, caivaas@;
 |SI FSalida ! 0;
     |MENAV "    Error Cargado Fichero de Control de IVA. Proceso Interrumpido";
     |PTEC 807;
     |ACABA;
 |FINSI;
 aDef331 = aDef;

 aDef      = eaPathDef + "caconasi";
 |CARGA_DEF aDef, caconas@;
 |SI FSalida ! 0;
     |MENAV "    Error Cargado Fichero Contador de Asientos. Proceso Interrumpido";
     |PTEC 807;
     |ACABA;
 |FINSI;
 aDef332 = aDef;

 aDef      = eaPathDef + "calibros";
 |CARGA_DEF aDef, calibro@;
 |SI FSalida ! 0;
     |MENAV "    Error Cargado Fichero de Libros IVA. Proceso Interrumpido";
     |PTEC 807;
     |ACABA;
 |FINSI;
 aDef333 = aDef;

 |SI nConta = 0;
     aDef      = eaPathDef + "caivases";
     |CARGA_DEF aDef, caivase@;
     |SI FSalida ! 0;
         |MENAV "    Error Cargado Fichero Series. Proceso Interrumpido";
         |PTEC 807;
         |ACABA;
     |FINSI;
     aDef334 = aDef;
 |FINSI;

 aDef      = eaPathDef + "camandia";
 |CARGA_DEF aDef, camandi@;
 |SI FSalida ! 0;
     |MENAV "    Error Cargado Fichero de Diarios. Proceso Interrumpido";
     |PTEC 807;
     |ACABA;
 |FINSI;
 aDef335 = aDef;

 |SI nConta = 0;
     aAlfa1    = eaPathDef - "contagen/def/";
     aAlfa1    = aAlfa1 + "modelos/def/";
     aDef      = aAlfa1 + "dsmom030";
 |SINO;
     aDef      = eaPathDef + "caconcep";
 |FINSI;
 |CARGA_DEF aDef, caconce@;
 |SI FSalida ! 0;
     |MENAV "    Error Cargado Fichero de Cocneptos. Proceso Interrumpido";
     |PTEC 807;
     |ACABA;
 |FINSI;
 aDef306 = aDef;

 |SI nConta = 0;
     |PATH_DAT #306 eaPathM;
 |SINO;
     |C_A #0#17,1,2;
     |C_A #0#18,1,2;
 |FINSI;

 |PATH_DAT #330 eaPathDatos;
|FCAL;

|CALCULO Tipo9z003; |TIPO 9;

  |SI aDef390 ! ""; |DESCARGA_DEF #caenlac@; aDef390 = ""; |FINSI;
  |SI aDef313 ! ""; |DESCARGA_DEF #cacueba@; aDef313 = ""; |FINSI;
  |SI aDef311 ! ""; |DESCARGA_DEF #caclipr@; aDef311 = ""; |FINSI;
  |SI aDef310 ! ""; |DESCARGA_DEF #cacuent@; aDef310 = ""; |FINSI;

  |SI aDef306 ! ""; |DESCARGA_DEF #caconce@; aDef306 = ""; |FINSI;
  |SI aDef335 ! ""; |DESCARGA_DEF #camandi@; aDef335 = ""; |FINSI;
  |SI aDef334 ! ""; |DESCARGA_DEF #caivase@; aDef334 = ""; |FINSI;
  |SI aDef333 ! ""; |DESCARGA_DEF #calibro@; aDef333 = ""; |FINSI;
  |SI aDef332 ! ""; |DESCARGA_DEF #caconas@; aDef332 = ""; |FINSI;
  |SI aDef331 ! ""; |DESCARGA_DEF #caivaas@; aDef331 = ""; |FINSI;
  |SI aDef330 ! ""; |DESCARGA_DEF #canempr@; aDef330 = ""; |FINSI;
|FCAL;

|| ////////////////////////////////////////////////////////////////
|CALCULO selec; |TIPO 0;
 aAlfa1 = #0#29;
 |C_M #0#30,1,aAlfa1;
 |C_M #0#31,1,aAlfa1;
 |SI #0#29 = "N";
     #0#30 = "          "; |PINTA #0#30;
     #0#31 = "zzzzzzzzzz"; |PINTA #0#31;
 |FINSI;
|FCAL;

|RUTINA infA2;
   #330#2 = nDEmpresa;
   #330#3 = 0;
|FRUT;

|RUTINA supA2;
   #330#2 = nHEmpresa;
   #330#3 = 9;
|FRUT;

|CALCULO LeeEmpresa; |TIPO 0;
  nInkey = FSalida;

  |SI nInkey = 10;
     nDEmpresa = 0;
     nHEmpresa = 99999;
     |ABRE #330;
     |CONSULTA_F_CLAVE #1#330,infA2,supA2;
     #0#1 = #330#2; |PINTA #0#1;
     #0#2 = #330#3; |PINTA #0#2;
     |CIERRA #330;
     |PTEC 802;
     |ACABA;
  |FINSI;

  |ABRE #330;
  #330#2 = #0#1;
  #330#3 = 0;
  |LEE 011#330];
  |SI FSalida ! 0;  |O #330#2 ! #0#1;
      |MENAV "     NO Existe la empresa Contable";
      |ERROR;
  |SINO;
      #0#4 = #330#1; |PINTA #0#4;
  |FINSI;
  |CIERRA #330;
|FCAL;

|CALCULO LeeEmpresaP; |TIPO 0;
  nInkey = FSalida;
  |SI nInkey = 2; |ACABA; |FINSI;

  |SI nInkey = 10;
     nDEmpresa = #0#1;
     nHEmpresa = #0#1;
     |ABRE #330;
     |CONSULTA_F_CLAVE #1#330,infA2,supA2;
     #0#2 = #330#3; |PINTA #0#2;
     |CIERRA #330;
  |FINSI;

  |ABRE #330;
  #330#2 = #0#1;
  #330#3 = #0#2;
  |LEE 011#330=;
  |SI FSalida ! 0;
      |MENAV "     NO Existe Periodo en la Empresa Contable";
      |ERROR;
  |SINO;
      |SI #330#26 > 80;
          nNume1 = #330#26 + 1900;
      |SINO;
          nNume1 = #330#26 + 2000;
      |FINSI;
      #0#3 = nNume1; |PINTA #0#3;
      #0#4 = #330#1; |PINTA #0#4;
  |FINSI;
  |CIERRA #330;

  enEmpresa = #0#1;
  enPeriodo = #0#2;
  |HAZ CreaPath;

  |ABRE #331;
  |DDEFECTO #331;
  |LEE 001#331p;
  |CIERRA #331;
  aQueQuiero = "|NC";
  aNumCampos = #caivaas@#aQueQuiero#;
  nNumCampos = aNumCampos;
  nNumCampos = nNumCampos - 1;
  aCamp44 = "N"; |SI nNumCampos ] 44; aCamp44 = #331#44; |FINSI;
  aCamp46 = "T"; |SI nNumCampos ] 46; aCamp46 = #331#46; |FINSI;
  snAboV  = "N"; snAboC  = "N";
  |SI nNumCampos ] 93; snAboV = #331#93; |FINSI;
  |SI nNumCampos ] 94; snAboC = #331#94; |FINSI;

  #0#5 = #331#3; || Libro Repercutido
  #0#6 = #331#5; || Concepto Repercutido
  #0#7 = #331#29; || Libro Soportado
  #0#8 = #331#31; || Concepto Soportado
  |PINTA #0#5; |PINTA #0#6;
  |PINTA #0#7; |PINTA #0#8;
  |SI nConta = 0;
      #0#19 = #331#50;  || Concepto Cobros
      #0#21 = #331#51;
      #0#20 = #331#54;  || Concepto Pagos;
      #0#22 = #331#55;
      |PINTA #0#19; |PINTA #0#21;
      |PINTA #0#20; |PINTA #0#22;
  |FINSI;

  |SI aCamp44 = "S";
      |C_M #0#17,1,"S";
      |C_M #0#18,1,"S";
      |SI nConta = 0;
          #0#17 = #331#63; |PINTA #0#17;
          #0#18 = #331#65; |PINTA #0#18;
      |FINSI;
  |SINO;
      |C_M #0#17,1,"N";
      |C_M #0#18,1,"N";
      #0#17 = ""; |PINTA #0#17;
      #0#18 = ""; |PINTA #0#18;
  |FINSI;
  #0#27 = snAboV; |PINTA #0#27;
  #0#28 = snAboC; |PINTA #0#28;
|FCAL;

|CALCULO ElLibro;
  nError = 0;
  |ABRE #333;
  #333#0 = #0Campo;
  |LEE 001#333=;
  |SI FSalida ! 0;
      aAlfa1 = "    Error. No existe Libro";
      nError = 1;
  |SINO;
      |SI #333#2 ! "R"; |Y Campo = 5;
          aAlfa1 = "    Error. El Libro no es de IVA Repercutido";
          nError = 1;
      |SINO;
          |SI #333#2 ! "S"; |Y Campo = 7;
              aAlfa1 = "    Error. El Libro no es de IVA Soportado";
              nError = 1;
          |FINSI;
      |FINSI;
  |FINSI;
  |CIERRA #333;

  nCampo = 12; |SI Campo = 7; nCampo = 13; |FINSI;
  |SI nError = 1;
      #333#1 = "";
      |MENAV aAlfa1;
      |ERROR;
  |FINSI;
  #0nCampo = #333#1; |PINTA #0nCampo;
|FCAL;

|CALCULO ElConcepto;
  nError = 0;
  |ABRE #306;
  #306#0 = #0Campo;
  |LEE 001#306=;
  |SI FSalida ! 0;
      aAlfa1 = "    Error. No existe Concepto Contable";
      nError = 1;
  |FINSI;
  nCampo = 14; |SI Campo = 8;  nCampo = 15; |FINSI;
               |SI Campo = 19; nCampo = 21; |FINSI;
               |SI Campo = 20; nCampo = 22; |FINSI;
               |SI Campo = 23; nCampo = 25; |FINSI;
               |SI Campo = 24; nCampo = 26; |FINSI;
  |SI nError = 1;
      |MENAV aAlfa1;
      #306#1 = "";
      |ERROR;
  |FINSI;
  #0nCampo = #306#1; |PINTA #0nCampo;
|FCAL;

|CALCULO ElDiario;
  |ABRE #335;
  |DDEFECTO #335;
  #335#0 = #0#9;
  |LEE 001#335=;
  |SI FSalida ! 0;
      |MENAV "    Error. No existe Diario";
      |ERROR;
  |FINSI;
  |CIERRA #335;
  #0#16 = #335#1; |PINTA #0#16;

  |ABRE #332;
  |DDEFECTO #332;
  |LEE 101#332=;
||  #0#10 = #332#1; |PINTA #0#10;
||  #0#11 = #0#10;  |PINTA #0#11;
  |LIBERA #332;
  |CIERRA #332;
|FCAL;

|| /////////////////////////////////////////////////////////////////////////
|| /////////////////////////////////////////////////////////////////////////
|PROCESO EsContra;

 nEsContra = 0;
 aAlfa6 = aCContra % 101;
 |SI aAlfa6 = "6"; nEsContra = 1; |FINSI;
 |SI aAlfa6 = "7"; nEsContra = 2; |FINSI;

 |SI aAlfa6 ! "6"; |Y aAlfa6 ! "7"; |Y nSwContra = 1;
     |PARA nPara1 = 1; |SI nPara1 [ 19; |HACIENDO nPara1 = nPara1 + 2;
           nPara2 = nPara1 + 1;
           |SI #4nPara1 > "            ";
               |SI aCContra ] #4nPara1; |Y aCContra [ #4nPara2;
                   nEsContra = 1;
                   |SI nPara1 ] 11; nEsContra = 2; |FINSI;
                   |SAL;
               |FINSI;
           |FINSI;
     |SIGUE;
 |FINSI;

|FPRC;

|PROCESO LeeCtrlContra;
 nSwContra = 0;
 |ABRE #4;
 |DDEFECTO #4;
 #4#0 = enEmpresa;
 |LEE 001#4=;
 |SI FSalida = 0;
     nSwContra = 1;
 |SINO;
     #4#0 = 0; || Busco General
     |LEE 001#4=;
     |SI FSalida = 0;
         nSwContra = 1;
     |FINSI;
 |FINSI;
 |CIERRA #4;
|FPRC;

|PROCESO CreaPath;
  aAlfa1 = enEmpresa; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
  aAlfa2 = enPeriodo; |QBF aAlfa2; aAlfa2 = ("0"     + aAlfa2) % -101;
  aEmpresa = aAlfa1 + aAlfa2;
  aPath = eaPathDatos + aEmpresa + "/";

  |PATH_DAT #331 aPath;
  |PATH_DAT #332 aPath;
  |PATH_DAT #333 aPath;
  |PATH_DAT #335 aPath;

  |SI nConta = 0;
      |PATH_DAT #334 aPath;
  |SINO;
      |PATH_DAT #306 aPath;
  |FINSI;
|FPRC;

|PROCESO CargaAuxiliares;   || de cada empresa

 aFichero = "1w" + Usuario;
 |NOME_DAT #901 aFichero;     || Auxiliar relaciones

 aFichero = "2w" + Usuario;
 |NOME_DAT #902 aFichero;     || Auxiliar movimientos

 aFichero = "3w" + Usuario;
 |NOME_DAT #903 aFichero;     || Auxiliar movimientos

 |DELINDEX #901;
 |DELINDEX #902;
 |DELINDEX #903;

 |SI nConta = 0;
     aNombA   = ("dl" + Usuario) % 108;
     aPathA   = aPath;
 |SINO;
     aAlfa1  = enEmpresa; |QBF aAlfa1;
     aAlfa1  = ("0000" + aAlfa1) % -104;
     aNombA  = "enco" + aAlfa1; eaFitxer = aAlfa1;
     aPathA   = eaPathDatos;
 |FINSI;

 |PATH_DAT #390 aPathA;
 |NOME_DAT #390 aNombA;
 |DELINDEX #390;

 |PATH_DAT #310 aPath;  || Cuentas
 |PATH_DAT #311 aPath;  || Clientes/Proveedores
 |PATH_DAT #313 aPath;  || Balances

 |ABRE #331;
 |DDEFECTO #331;
 |LEE 001#331p;
 |CIERRA #331;
 aDfDepC = #331#19;

|FPRC;

|PROCESO LeeCuenta;
    aNomCta = "";
    |ABRE #310;
    |DDEFECTO #310;
    #310#0 = eaCuenta;
    #310#1 = enNivel;
    |LEE 040#310=;
    |CIERRA #310;
    aNomCta = #310#2;
|FPRC;

|PROCESO BuscaClPr;
    aNomClPr = "";
    aCifClPr = "";
    eaCuenta = aCtaClPr;
    |HAZ SacaNivel;
    |ABRE #311;
    #311#23 = "C";
    #311#10 = aCtaClPr;
    #311#11 = enNivel;
    |LEE 001#311=;
    |SI FSalida = 0;
        aNomClPr = #311#1;
        aCifClPr = #311#9;
    |SINO;
        #311#23 = "P";
        #311#10 = aCtaClPr;
        #311#11 = enNivel;
        |LEE 001#311=;
        |SI FSalida = 0;
            aNomClPr = #311#1;
            aCifClPr = #311#9;
        |SINO;
            |HAZ LeeCuenta;
            aNomClPr = aNomCta;
        |FINSI;
    |FINSI;
    |CIERRA #311;
|FPRC;
|| /////////////////////////////////////////////////////////////////////////
|| /////////////////////////////////////////////////////////////////////////
|PROCESO LeeConCop;
 |SI #2#4 [ aCtaClPr; |Y #2#5 ] aCtaClPr;
     nConcepto = #2#2;
     |ERROR10;
 |FINSI;
|FPRC;

|DEFBUCLE LeeConCop;
 #2#1;
 ;
 nEmp,eaCtaCon,001,001;
 nEmp,eaCtaCon,999,999;
 e;
 NULL, LeeConCop;
|FIN;

|PROCESO BuscaConcepto;
 nEmp     = 0;
 nSExiste = 0;
 |ABRE #2;
 #2#0 = enEmpresa;
 #2#1 = eaCtaCon;
 #2#2 = 001;
 #2#3 = 001;
 |LEE 001#2];
 |SI FSalida = 0; |Y #2#0 = enEmpresa; |Y #2#1 = eaCtaCon;
     nSExiste = 1;
 |FINSI;
 |CIERRA #2;
 |SI nSExiste = 1; nEmp = enEmpresa; |FINSI;
 |BUCLE LeeConCop;
|FPRC;

|| /////////////////////////////////////////////////////////////////////////
|PROCESO BuscaNumFra;
   |SI aCamp44 = "N"; |O nConta = 1;
       |ABRE #333;
       #333#0 = nLibro;
       |LEE 110#333=;
       |SI FSalida = 0;
           nNumFra = #333#4;
           #333#4  = #333#4 + #333#3;
           |GRABA 020#333a;
           |LIBERA #333;
       |FINSI;
       |CIERRA #333;
   |SINO;
       |ABRE #334;
       #334#0 = aSerie;
       #334#1 = aTipoIVA;
       |LEE 110#334=;
       |SI FSalida = 0;
           nNumFra = #334#3;
           #334#3  = #334#3 + #334#4;
           |GRABA 020#334a;
           |LIBERA #334;
      |FINSI;
      |CIERRA #334;
   |FINSI;
|FPRC;

|PROCESO EmpezarAsto;
 nSwB    = 0;
 nApunte = 0;
 nDocumento = #902#1;
 |ABRE #332;
 |DDEFECTO #332;
 |LEE 101#332=;
 |SI FSalida ! 0;
     |DDEFECTO #332;
     |GRABA 020#332b;
 |FINSI;
 nDocumAct = #332#1;
 #332#1    = #332#1 + 1;
 |GRABA 020#332a;
 |LIBERA #332;
 |CIERRA #332;

 nDiario = #0#9;
 fFecha  = #902#3;

 |DDEFECTO #903;
 #903#0 = #902#1;
 |LEE 001#903=;
 nRecibo  = 0;
 nParIvaI = 0;
 nTpIva   = 0;
 nTpRec   = 0;
 nMulti    = #903#5;
 nConcepto = #903#3;
 aTipoIVA  = #903#6;
 nLibro    = #903#7;
 aDesLib   = #903#16;
 aSerie    = #903#8;
 aCtaClPr  = #903#13;
 aNomClPr  = "";
 aCifClPr  = "";
 nConcIrpf = #903#42;
 nBaseIrpf = #903#43;
 nTipoIrpf = #903#44;
 |SI aTipoIVA ! "C"; |Y aTipoIVA ! "P";
     |HAZ BuscaNumFra;
     |HAZ BuscaClPr;
 |FINSI;
|FPRC;

|PROCESO VarContagen;

 |DDEFECTO #390;
 #390#0  = nDiario;
 #390#1  = fFecha;
 #390#2  = nDocumAct;
 #390#3  = nApunte;
 #390#4  = eaCuenta;   || Cuenta Contable
 |HAZ SacaNivel;
 #390#5  = enNivel;
 #390#6  = nConcepto;
 #390#7  = #902#4;
 #390#8  = aSigno;
 #390#9  = nImporte;
 #390#10 = aTipoIVA;
 #390#11 = nLibro;
 #390#12 = aSerie;
 #390#13 = nNumFra;
 #390#14 = nRecibo;
 #390#15 = aDepar;
 #390#26 = aParIva;
 #390#27 = nParIva;
 #390#28 = #902#13;
 #390#29 = #902#3;
 |SI #390#26 = "B"; |O #390#26 = "b";
     |SI #390#10 = "R"; #390#31 = #331#71; |FINSI;
     |SI #390#10 = "S"; #390#31 = #331#21; |FINSI;
 |FINSI;
 #390#33 = #902#14;
 #390#37 = enEmpresa;
 #390#38 = enPeriodo;
 #390#39 = aProyec;
 #390#40 = #902#8;
 #390#43 = aNomClPr;
 #390#44 = aCifClPr;
 |SI #390#10 = "C"; |O #390#10 = "P";
     #390#10 = "";
     #390#11 = 0;
     #390#12 = "";
     #390#13 = 0;
     #390#14 = nRecibo;
     #390#26 = "";
     #390#27 = 0;
 |FINSI;
 |SI aParIva = "P";
     #390#6  = nConcIrpf;
     #390#41 = nTipoIrpf;
     #390#42 = nBaseIrpf;
 |FINSI;
|FPRC;

|PROCESO VarAgicont;
|| aqui
 aQueQuiero = "|NC";
 aNumCampos = #390#aQueQuiero#;
 nNumCampos = aNumCampos;
 nNumCampos = nNumCampos - 1;
 |DDEFECTO #390;
 #390#0  = nDiario;
 #390#1  = fFecha;
 #390#2  = nDocumAct;
 #390#3  = nDocumAct;
 #390#4  = nApunte;

 |HAZ SacaNivel;

 |SI aSigno = "D";
     #390#5  = eaCuenta;   || Cuenta Contable
     #390#7  = enNivel;
 |SINO;
     #390#6  = eaCuenta;   || Cuenta Contable
     #390#8  = enNivel;
 |FINSI;

 #390#9  = nConcepto;
 #390#10 = #902#4;
 #390#11 = aSigno;
 #390#12 = nImporte;
 #390#13 = aTipoIVA;
 #390#14 = nLibro;
 #390#15 = aDesLib;
 #390#16 = nNumFra;
 #390#17 = aSerie;

 |SI aParIva = "F";
     #390#18 = #390#9;
     #390#19 = #390#10;
     #390#37 = #390#12;
 |FINSI;

 |SI nSwB = 0;
     #390#18 = #390#9;
     #390#19 = #390#10;
     #390#20 = eaCuenta;
     #390#21 = enNivel;

     #390#22 = #903#5 * #903#17;  ||bases
     #390#23 = #903#5 * #903#18;
     #390#24 = #903#5 * #903#19;
     #390#48 = #903#5 * #903#20;
     #390#49 = #903#5 * #903#21;

     #390#28 = #903#5 * #903#22;  || cuota iva
     #390#29 = #903#5 * #903#23;
     #390#30 = #903#5 * #903#24;
     #390#52 = #903#5 * #903#25;
     #390#53 = #903#5 * #903#26;

     #390#34 = #903#5 * #903#27;
     #390#35 = #903#5 * #903#28;
     #390#36 = #903#5 * #903#29;
     #390#56 = #903#5 * #903#30;
     #390#57 = #903#5 * #903#31;

     #390#25 = #903#32;
     #390#26 = #903#33;
     #390#27 = #903#34;
     #390#50 = #903#35;
     #390#51 = #903#36;
     #390#31 = #903#37;
     #390#32 = #903#38;
     #390#33 = #903#39;
     #390#54 = #903#40;
     #390#55 = #903#41;
     nSwB = 1;
 |FINSI;

 |SI aParIva = "I"; |O aParIva = "R";
     #390#20 = eaCuenta;
     #390#21 = enNivel;
     |SI aParIva = "I";
         #390#18 = #390#9;
         #390#19 = #390#10;
        || nNume1 = nParIva + 27; nNume2 = nParIva + 24;
        || |SI nParIva = 4; nNume1 = 52; nNume2 = 50; |FINSI;
        || |SI nParIva ] 5; nNume1 = 53; nNume2 = 51; |FINSI;
        || #390nNume1 = #390#12;
        || #390nNume2 = #902#13;
     |SINO;
        || nNume1 = nParIva + 33; nNume2 = nParIva + 30;
        || |SI nParIva = 4; nNume1 = 56; nNume2 = 54; |FINSI;
        || |SI nParIva ] 5; nNume1 = 57; nNume2 = 55; |FINSI;
        || #390nNume1 = #390#12;
        || #390nNume2 = #902#14;
     |FINSI;
 |FINSI;

 |SI aParIva = "B";
||     nNume1 = nParIva + 21;
||     |SI nParIva = 4; nNume1 = 48; |FINSI;
||     |SI nParIva ] 5; nNume1 = 49; |FINSI;
||     #390nNume1 = #390#12;
 |FINSI;

 |SI aParIva = "P";
     #390#9  = nConcIrpf;
     aParIva = " ";
     nParIva = 0;
 |FINSI;
 #390#40 = aDepar;
 #390#41 = #390#1;
 #390#42 = aParIva;
 #390#43 = nParIva;
 aAlfa1 = #390#1; aAlfa1 = aAlfa1 % 402;
 nNume1 = aAlfa1; nPeriodo = nNume1;
 |SI aCamp46 = "T";
     |SI nNume1 [ 3; nPeriodo = 1; |FINSI;
     |SI nNume1 > 3; |Y nPeriodo [ 6; nPeriodo = 2; |FINSI;
     |SI nNume1 > 6; |Y nPeriodo [ 9; nPeriodo = 3; |FINSI;
     |SI nNume1 > 9;                  nPeriodo = 4; |FINSI;
 |FINSI;
 #390#44 = nPeriodo;
 #390#45 = 0;  || ... #390#45 = enEmpresa;
 #390#46 = 0;  || .... #390#46 = enPeriodo;
 #390#47 = 0;
 #390#58 = aCifClPr;
 |SI nNumCampos ] 60;
     #390#60 = aProyec;
 |FINSI;
|FPRC;

|PROCESO LeeAuxw002;

 |PINTA 2324, #902#1;
 |PINTA 2336, #902#3;

 |SI nPrimero = 0;
     nPrimero   = 1;
     |HAZ EmpezarAsto;
 |FINSI;
 |SI nDocumento ! #902#1;
     |HAZ EmpezarAsto;
 |FINSI;

 aProyec  = "";
 aDepar   = aDfDepC;
 |SI enSwAna = 1;
     aProyec = #902#11;   || Cuenta de Analitica
     aDepar  = aDfDepC;
 |SINO;
     |SI enSwAna = 2;
         aProyec = "";
         aDepar  = #902#11;    || Actividad
     |FINSI;
 |FINSI;

 |SI aTipoIVA = "R"; |O aTipoIVA = "C"; snAbono = snAboV; |FINSI;
 |SI aTipoIVA = "S"; |O aTipoIVA = "P"; snAbono = snAboC; |FINSI;
 nMinus = 0;

 nSwGraba = 0;
 nApunte  = nApunte + 1;
 eaCuenta = #902#2;
 aAlfa1   = eaCuenta % 101;
 aAlfa3   = eaCuenta % 103;

 aSigno = #902#7;
 |SI nMulti = -1;    ||es un abono
     |SI snAbono = "N";
         |SI aSigno = "D";
             aSigno = "H";
         |SINO;
             |SI aSigno = "H";
                 aSigno = "D";
             |FINSI;
          |FINSI;
          |SI aSigno = "X"; aSigno = "x"; |FINSI;
          nImporte = #902#6 * nMulti;
          nMinus = 0;
      |SINO;
          nMinus = 1;
          nImporte = #902#6;
      |FINSI;
 |SINO;
      nImporte = #902#6;
 |FINSI;

 |SI aTipoIVA = "C"; |O aTipoIVA = "P";
     |SI aAlfa1 = "4"; nRecibo = nRecibo + 1; |FINSI;
 |FINSI;

 |SI aTipoIVA = "R"; |O aTipoIVA = "S";

     |SI #903#13 = #902#2;
         aParIva  = "F";
         nParIva  = 0;
     |SINO;
         aCContra = eaCuenta;
         |HAZ EsContra;
         |SI nEsContra ! 0; || ... aAlfa1 = "6"; |O aAlfa1 = "7"; || contrapartida
             aParIva = "B";
             nParIva = 1;
             |SI #903#10 ! 1;
                 nSwGraba = 1;
                 || ... no hace este apunte ya lo tiene hecho
             |FINSI;

         |SINO;

              nLineaIVA = 0;
              |SI #902#13 ! 0; |O #902#14 ! 0;  |O #902#15 ! 0; || Estoy en la linea de IVA

                  || ..modificacion recargo
                  || nLineaIVA = 1;
                  || nTpIva = #902#13;
                  || nTpRec = #902#14;
                  || nParIvaI = nParIvaI + 1;
                  || aParIva  = "I";

                  nLineaIVA = 1;
                  |SI #902#13 = 0; |Y #902#15 ! 0; |Y nParIvaR = 1;
                      |SI nParIvaI = 0; nParIvaI = 1; |FINSI;
                      aParIva  = "R";
                      nParIvaR = 0;
                  |SINO;
                      nTpIva = #902#13;
                      nTpRec = #902#14;
                      nParIvaI = nParIvaI + 1;
                      aParIva  = "I";
                      |SI nTpRec ! 0; nParIvaR = 1; |FINSI;
                  |FINSI;
              |SINO;
                  |SI #902#16 ! 0; || Existe Retencion
                      aParIva  = "P";
                  |SINO;
                      aParIva = "D";
                  |FINSI;
              |FINSI;

              |SI #903#10 = 1;
                  nParIva  = 1;
              |SINO;
                  nParIva  = nParIvaI;
                  |SI nLineaIVA = 0; |O nTpRec = 0;
                      |SI aSigno = "X"; |O aSigno = "x";
                          aSigno = "D"; |SI #903#14 = "D"; aSigno = "H"; |FINSI;
                      |FINSI;
                      |HAZ ParaGrabar;
                      aParIva  = "B";
                      eaCuenta = #903#1;
                      nImporte = #902#15 * nMulti;
                      nApunte  = nApunte + 1;
                  |FINSI;
              |FINSI;
         |FINSI;

     |FINSI;
   |FINSI;

 |SI nSwGraba = 0;
     |HAZ ParaGrabar;
 |SINO;
     nApunte = nApunte - 1;
 |FINSI;
|FPRC;

|PROCESO ParaGrabar;
     |SI nMinus = 1;
         |||SI  aParIva ! "F"; |Y aParIva ! "";  || Tique 309_373
         |SI  aParIva ! "";
              aParIva = aParIva < "";
         |FINSI;
     |FINSI;
     |SI nConta = 0;
         |HAZ VarContagen;
     |SINO;
          |SI nSwB = 0;
              |SI aTipoIVA ! "C"; |Y aTipoIVA ! "P";
                  |HAZ VarAgicont;
                  |GRABA 020#390n;
                  nApunte  = nApunte + 1;
              |FINSI;
              nSwB = 1;
          |FINSI;
          |HAZ VarAgicont;
     |FINSI;
     |GRABA 020#390n;
|FPRC;

|DEFBUCLE LeeAuxw002;
 #902#2;
 ;
 0000000001,0000000001;
 9999999999,9999999999;
 ;
 NULL, LeeAuxw002;
|FIN;

|| ///////////////////////////////////////////////////////////////////////
|PROCESO AltaCuenta;
    |ABRE #310;
    |DDEFECTO #310;
    #310#0 = eaCuenta;
    #310#1 = enNivel;
    |LEE 101#310=;
    |SI FSalida ! 0;
        |DDEFECTO #310;
        #310#0 = eaCuenta;
        #310#1 = enNivel;
        #310#2 = #901#1;
        |HAZ defectos;
        |GRABA 020#310b;
    |FINSI;
    |LIBERA #310;
    |CIERRA #310;

    aTipoalta = "";
    aDigito2  = eaCuenta % 102;
    |SI aDigito2 = "43";  |O aDigito2 = "44";
        aTipoalta = "C";
    |SINO;
       |SI aDigito2 = "40";  |O aDigito2 = "41";  |O aDigito2 = "52"; |O aDigito2 = "17";
           aTipoalta = "P";
       |FINSI;
   |FINSI;

   |SI aTipoalta = ""; |ACABA; |FINSI;

   |ABRE #311;
   #311#23 = aTipoalta;
   #311#10 = eaCuenta;
   #311#11 = enNivel;
   |LEE 101#311=;
   |SI FSalida ! 0;
       #311#23 = aTipoalta;
       #311#10 = eaCuenta;
       #311#11 = enNivel;
       #311#1  = #901#1;
       #311#9  = #901#2;
       |GRABA 020#311b;
   |FINSI;
   |LIBERA #311;
   |CIERRA #311;
|FPRC;

|PROCESO LeeAuxw001;

 |PINTA 2324, #901#0;

 eaCuenta = #901#0;|HAZ SacaNivel;
 |HAZ AltaCuenta;

 #3#0 = enEmpresa;
 #3#1 = enPeriodo;
 #3#2 = #901#0;   ||Cuenta
 |LEE 101#3=;
 nEstado = FSalida;
 #3#0 = enEmpresa;
 #3#1 = enPeriodo;
 #3#2 = #901#0;   ||Cuenta
 |SI nEstado ! 0;
     #3#3  = #901#1;
     #3#4  = #901#2;
     #3#10 = eaNombreEmp;
     #3#11 = enEjer;
     eaCuenta = #3#0; |HAZ SacaNivel; #3#12 = enNivel;
 |FINSI;

 aAlfa1 = #901#4; |QBF aAlfa1;
 |SI aAlfa1 ! "";
     |SI #901#4 ! #3#5;
         #3#5 = #901#4;
         eaCuenta = #3#5; |HAZ SacaNivel; #3#13 = enNivel;
         |HAZ LeeCuenta;
         #3#6 = aNomCta;
     |FINSI;
 |FINSI;

 aAlfa1 = #901#3; |QBF aAlfa1;
 |SI aAlfa1 ! "";
     |SI #901#3 ! #3#7;
         #3#7 = #901#3;
         eaCuenta = #3#7; |HAZ SacaNivel; #3#14 = enNivel;
         |HAZ LeeCuenta;
         #3#8 = aNomCta;
     |FINSI;
 |FINSI;

 |SI nEstado = 0; |GRABA 020#3a; |FINSI;
 |SI nEstado ! 0; |GRABA 020#3n; |FINSI;
 |LIBERA #3;

|FPRC;

|DEFBUCLE LeeAuxw001;
 #901#1;
 ;
 "            ";
 "zzzzzzzzzzzz";
 ;
 NULL, LeeAuxw001;
|FIN;


|PROCESO ElPase;
 |BLIN 23;
 |PINTA 2306, "Grabando Cuentas: ";

 |ABRE #3;
 |BUCLE LeeAuxw001;
 |CIERRA #3;

 |BLIN 23;
 |PINTA 2306, "Grabando Apuntes: ";
 nLibro    = 0;
 aDesLib   = "";
 aSerie    = "";
 nNumFra   = 0;
 nPrimero  = 0;
 |ABRE #903;
 |ABRE #390;
 |BUCLE LeeAuxw002;
 |CIERRA #390;
 |CIERRA #903;

 |BLIN 23;
 |SI nPrimero ! 0;
     |PINTA 2308, "Actualizando asientos en el Diario. ";
     |SI nConta = 0;
         aAlfa1 = ":contagen/dsapunte.tab;" + aPathA + "," + aNombA;
     |SINO;
         aAlfa1 = ":agicont/impgest5.tab canempr1 " + aEmpresa + ";LAPIZ";
     |FINSI;
     |SUB_EJECUTA_NP aAlfa1;
     |SI PRMNT_enError = 0;
         |CONFI "    SPase Realizado. Borrar Fichero Asto a Traspasar ?";
         |SI FSalida = 0;
             |RFBORRA eaDestino;
         |FINSI;
     |SINO;
         |FBORRA eaOrigen;
     |FINSI;
 |SINO;
     |MENAV "    No hay registro para actualizar";
 |FINSI;
|FPRC;

|| /////////////////////////////////////////////////////////////////////////
|PROCESO Repasow3;

 #903#0 = nDocumento;
 |LEE 001#903=;
 |SI FSalida ! 0;
     |ACABA;
 |FINSI;

 aTMov  = "";
 nMulti = 1;  || Multiplicador para los abonos
 aCtaClPr = #903#13; aAlfa1 = aCtaClPr % 103;
 eaCtaCon = #903#1;  aAlfa2 = eaCtaCon % 101;

 aCContra = eaCtaCon;
 |HAZ EsContra;

 |SI nEsContra = 1; |Y #903#9 ] 1;  || aAlfa2 = "6";
     aTMov  = "C";
     aSigno = "H";
 |FINSI;
 |SI nEsContra = 2; |Y #903#9 ] 1;  || aAlfa2 = "7";
     aTMov  = "V";
     aSigno = "D";
 |FINSI;

 aAlfa2 = #903#15; aAlfa2 = aAlfa2 > " ";
 aAlfa2 = aAlfa2 - "PAGO";
 |SI FSalida ! 0;
     aTMov  = "a";
     aSigno = "D";
 |SINO;
     aAlfa2 = aAlfa2 - "COBRO";
     |SI FSalida ! 0;
         aTMov = "o";
         aSigno = "H";
     |FINSI;
 |FINSI;

 nConcIrpf = 0;
 nBaseIrpf = 0;
 aTipoIVA  = "";
 nConcepto = 1;
 |SI aTMov = "C"; |O aTMov = "a";
     nLibro    = #0#7;
     aDesLib   = #0#13;
     aSerie    = #0#18;
     aTipoIVA  =  "S";
     nConcepto = #0#8;
     |SI aTMov = "a";
         aTipoIVA =  "P";
         nConcepto = #0#20;
     |FINSI;
     |SI aTMov = "C"; |Y #903#44 ! 0; ||existe irpf
         nConcIrpf = #0#24;
         nBaseIrpf = #903#17 + #903#18 + #903#19 + #903#20 + #903#21;
     |FINSI;
 |FINSI;
 |SI aTMov = "V"; |O aTMov = "o";
     nLibro   = #0#5;
     aDesLib  = #0#12;
     aSerie   = #0#17;
     aTipoIVA =  "R";
     nConcepto = #0#6;
     |SI aTMov = "o";
         aTipoIVA =  "C";
         nConcepto = #0#19;
         nBaseIrpf = #903#17 + #903#18 + #903#19 + #903#20 + #903#21;
     |FINSI;
     |SI aTMov = "V"; |Y #903#44 ! 0; ||existe irpf
         nConcIrpf = #0#23;
     |FINSI;
  |FINSI;

  |SI aSigno ! #903#14;
      nMulti = -1;
  |FINSI;

  |HAZ BuscaConcepto;
  #903#3  = nConcepto;
  #903#5  = nMulti;
  #903#6  = aTipoIVA;
  #903#7  = nLibro;
  #903#8  = aSerie;
  #903#16 = aDesLib;
  #903#42 = nConcIrpf;
  #903#43 = nBaseIrpf;
  |GRABA 020#903a;
  nParIvaR = 0;
|FPRC;

|PROCESO GrabaAux3;

 |ABRE #903;
 |SI nPrimero = 0;
     nDocumento = #902#1;
     nPrimero   = 1;
 |FINSI;
 |SI nDocumento ! #902#1;
     |HAZ Repasow3;
     nDocumento = #902#1;
 |FINSI;

 #903#0 = nDocumento;
 |LEE 101#903=;
 |SI FSalida ! 0;
     |DDEFECTO #903;
     #903#0 = nDocumento;
     |GRABA 020#903b;
     nApunte = 0;
 |FINSI;

 nApunte = nApunte + 1;
 eaCuenta = #902#2; |QBF eaCuenta;
 aAlfa1   = eaCuenta % 101;
 aAlfa3   = eaCuenta % 103;

 |SI nApunte = 1;
     #903#13 = eaCuenta;
     #903#14 = #902#7;    || Signo
     #903#15 = #902#4;    || Descripcion
 |FINSI;

 aCContra = eaCuenta;
 |HAZ EsContra;
 |SI nEsContra ! 0;   || aAlfa1 = 6 o aAlfa1 = 7 o otras contrapartidas
      #903#9 = #903#9 + 1;
      #903#1 = #902#2;
 |SINO;
      |SI #902#2 ! #903#13;  || este calculo afecta mas a agicont.

      |SI #902#13 ! 0; |O #902#14 ! 0; |O #902#15 ! 0;

          |SI #902#13 ! 0;
              #903#10 = #903#10 + 1;

              nNume1 = #903#10 + 16; |SI nNume1 > 21; nNume1 = 21; |FINSI;
              #903nNume1 = #902#15;
              nNume1 = nNume1 + 5;  #903nNume1 = #902#6;
              nNume1 = nNume1 + 10; #903nNume1 = #902#13;
              |SI #902#14 ! 0;
                  #903#11 = #903#11 + 1;
                  nNume1 = nNume1 + 10;
                  #903nNume1 = #902#14;
                  nParIvaR = 1;
              |FINSI;
          |FINSI;
          |SI #902#14 ! 0; |Y #902#13 = 0;
              #903#11 = #903#11 + 1;
              nNume1  = #903#11 + 26; |SI nNume1 > 31; nNume1 = 31; |FINSI;
              #903nNume1 = #902#6;
              nNume1 = nNume1 + 10;
              #903nNume1 = #902#14;
          |FINSI;
          |SI #902#15 ! 0; |Y #902#13 = 0; |Y #902#14 = 0;
              |SI nParIvaR = 0;
                  #903#10 = #903#10 + 1;

                  nNume1 = #903#10 + 16; |SI nNume1 > 21; nNume1 = 21; |FINSI;
                  #903nNume1 = #902#15;
                  nNume1 = nNume1 + 5;  #903nNume1 = #902#6;
                  nNume1 = nNume1 + 10; #903nNume1 = #902#13;
              |SINO;
                  nNume1  = #903#11 + 26; |SI nNume1 > 31; nNume1 = 31; |FINSI;
                  #903nNume1 = #902#6;
                  nParIvaR = 0;
              |FINSI;
          |FINSI;
      |FINSI;

      |FINSI;
 |FINSI;
 |SI #902#16 ! 0; #903#44 = #902#16; |FINSI;

 |GRABA 020#903a;
 |LIBERA #903;
 |CIERRA #903;
|FPRC;

|| /////////////////////////////////////////////////////////////////////////
|PROCESO GrabaRel;
 |ABRE #901;
 #901#0 = eaCuenta;
 |LEE 001#901=;
 |SI FSalida ! 0;
     |DDEFECTO #901;
     #901#0 = eaCuenta;
     |GRABA 020#901c;
 |FINSI;
 |SI aTipo = "C";
     #901#1 = aNombre;
     #901#2 = aCif;
     #901#3 = aCaja;
 |FINSI;
 |SI aTipo = "R";
     #901#4 = aContra;
 |FINSI;
 |GRABA 020#901a;
 |CIERRA #901;
|FPRC;

|PROCESO LeoUnoaUno;
       sw_eof = 0;
       aField = "";
       nNume1 = 0;
       |PARA; |SI sw_eof = 0; |HACIENDO;
              |XLEE nHadle, 1, aAlfa1;
              |SI FSalida > 0;
                  |SI aAlfa1 = aEl10;
                      sw_eof = 1;
                  |SINO;
                      |SI aAlfa1 ! aEl13;
                          aField = aField + aAlfa1;
                          nNume1 = nNume1 + 1;
                      |FINSI;
                  |FINSI;
              |SINO;
                   sw_eof = 1;
              |FINSI;
        |SIGUE;
        FSalida = nNume1;
|FPRC;

|PROCESO LeeTipoC;
   aTipo = "C";
                    || ... No es fijo 91
   |HAZ LeoUnoaUno;
   |SI FSalida > 0;
       eaCuenta = aField %  112;
       aNombre  = aField % 1350;
       aCif     = aField % 6315;
       aCaja    = aField % 7812;
       |SI FSalida ] 92;
           aAlfa1   = aField % 9102; nTp100   = aAlfa1;
       |FINSI;
       |HAZ GrabaRel;
       |PINTA 2334, "Cuenta:                 "; |PINTA 2342, #901#0;
   |FINSI;
|FPRC;

|PROCESO LeeTipoM;

  aTipo = "M";
               || ...  No fijo a 190. |XLEE nHadle, 190, aField;
  |HAZ LeoUnoaUno;
  |SI FSalida > 0;
      |DDEFECTO #902;
      nContarAp = nContarAp + 1;
      #902#0 = nContarAp;
      aAlfa1 = aField %  0110;  #902#1  = aAlfa1;
      aAlfa1 = aField %  1112;  #902#2  = aAlfa1;
      aAlfa1 = aField %  2308;
      aAlfa1 = (aAlfa1 % 0702) + "." + (aAlfa1 % 0502) + "." + (aAlfa1 % 0104);
      #902#3 = aAlfa1;
      aAlfa1 = aField %  3140;  #902#4  = aAlfa1;
      aAlfa1 = aField %  7112;  #902#5  = aAlfa1;
      aAlfa1 = aField %  8310;  #902#6  = aAlfa1;
      aAlfa1 = aField %  9301;  #902#7  = aAlfa1;
      aAlfa1 = aField %  9425;  #902#8  = aAlfa1;
      aAlfa1 = aField % 11910;  #902#9  = aAlfa1;
      aAlfa1 = aField % 12925;  #902#10 = aAlfa1;
      aAlfa1 = aField % 15410;  #902#11 = aAlfa1;
      aAlfa1 = aField % 16410;  #902#12 = aAlfa1;
      aAlfa1 = aField % 17402;  #902#13 = aAlfa1;
      aAlfa1 = aField % 17603;  #902#14 = aAlfa1;
      aAlfa1 = aField % 17910;  #902#15 = aAlfa1;
      |SI enVer = 1; |Y nNume1 ] 190;
          aAlfa1 = aField % 18902; #902#16 = aAlfa1;
      |FINSI;
      |SI #902#6 = 0; #902#7 = "X"; |FINSI;
      nGraba = 0;
      |SI #0#29 = "S";
          |SI #902#11 < #0#30; |O #902#11 > #0#31;  nGraba = 1;  |FINSI;
      |FINSI;
      |SI nGraba = 0;
          |GRABA 020#902n;
          |HAZ GrabaAux3;
          |PINTA 2334, "Asto fecha: "; |PINTA 2346, #902#3;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO LeeTipoR;
   aTipo = "R";
               || ...  No fijo a 26 |XLEE nHadle, 26, aField;
   |HAZ LeoUnoaUno;
   |SI FSalida > 0;
       eaCuenta = aField %  112;
       aContra  = aField % 1312;
       |HAZ GrabaRel;
       |PINTA 2334, "Relacion:               "; |PINTA 2344, #901#0;
   |FINSI;
|FPRC;

|PROCESO HazTodo;

   nContarAp   = 0;
   enEmpresa = #330#2;
   enPeriodo = #330#3;
   |HAZ eVarEmpresa;
   |HAZ CreaPath;
   |HAZ eAnalitica;
   |HAZ LeeCtrlContra;

   |HORA aAlfa1; aAlfa1 = (aAlfa1 % 102) + (aAlfa1 % 402);
   fFecha = ~;
   aAlfa2 = fFecha;
   aAlfa2 = (aAlfa2 % -104) + (aAlfa2 % 402) + (aAlfa2 % 102);
   aFichero  = aAlfa1 + aAlfa2 + ".txt";

   eaOrigen  = aPathH + aEmpresa + "/" + aFichero;
   eaDestino = eaPathExImp + aEmpresa + "/" + eaFichAsto;
   |HAZ eReciboFich;

   aFiche      = eaDestino; |QBF aFiche;
   eaFichLocal = aFiche;
   eaFicnombre = eaFichAsto;
   |HAZ eNombre;
   aFiche      = eaFichLocal;

   |XABRE "B", aFiche,nHadle;
   |SI FSalida < 0;
       |XABRE "CB", aFiche,nHadle;
   |FINSI;
   |SI FSalida < 0;
       Pos = -1;
       |MENAV "    No existe Facturas para Actualizar ";
       |ACABA;
   |FINSI;

   |HAZ CargaAuxiliares;

   |PINTA 2306, "Leyendo Fichero a importar. ";
   |ABRE #902;
   nDocumento = 0;
   nPrimero   = 0;
   |PARA; |SI; |HACIENDO;
          |XLEE nHadle, 3, aField;
          |SI FSalida > 0;
             aAlfa4 = aField;
             |ET Ditribuye;
                 |SI aAlfa4 = "[C]"; |HAZ LeeTipoC; |FINSI;
                 |SI aAlfa4 = "[M]"; |HAZ LeeTipoM; |FINSI;
                 |SI aAlfa4 = "[R]"; |HAZ LeeTipoR; |FINSI;
          |SINO;
              |SAL;
          |FINSI;
   |SIGUE;
   |XCIERRA nHadle;
   |CIERRA #902;
   Pos = -1;

   |SI nContarAp ! 0;
       || ... realizar el enco???? o dsapunte
       |SI nPrimero = 1;
            |ABRE #903;
            |HAZ Repasow3;
            |CIERRA #903;
       |FINSI;
       |HAZ ElPase;
   |FINSI;
   |DELINDEX #901;
   |DELINDEX #902;
   |DELINDEX #903;
|FPRC;

|DEFBUCLE LeeCanempre;
 #canempr@#1002;
 ;
 nDEmpresa, nDPeriodo;
 nHEmpresa, nHPeriodo;
 ;
 NULL, HazTodo;
|FIN;

|| /////////////////////////////////////////////////////////////////////////
|CALCULO Tipo3z003; |TIPO 3;
  |SI #0#0 = "NO"; |ACABA; |FINSI;

  snAboV = #0#27;
  snAboC = #0#28;

  aEl13 = &13;
  aEl10 = &10;

  |DFICB aBase; |QBF aBase;
  |DBASE aAlfa1; |QBF aAlfa1;
  aPathH = aAlfa1 + "historic/";

  aDef     = eaPathDef + "cacuenta";
  |CARGA_DEF aDef, cacuent@;
  |SI FSalida ! 0;
      |MENAV "    Error Cargado Fichero de Cuentas. Proceso Interrumpido";
      |ACABA;
  |FINSI;
  aDef310 = aDef;

  aDef     = eaPathDef + "caclipro";
  |CARGA_DEF aDef, caclipr@;
  |SI FSalida ! 0;
      |MENAV "    Error Cargado Fichero de Clientes y Proveedores. Proceso Interrumpido";
      |ACABA;
  |FINSI;
  aDef311 = aDef;

  aDef     = eaPathDef + "cacuebal";
  |CARGA_DEF aDef, cacueba@;
  |SI FSalida ! 0;
      |MENAV "    Error Cargado Fichero de Cuentas a Balance. Proceso Interrumpido";
      |ACABA;
  |FINSI;
  aDef313 =  aDef;

  |SI nConta = 0;
      aDef     = eaPathDef + "caenlace";
      |CARGA_DEF aDef, caenlac@;
      |SI FSalida ! 0;
          |MENAV "    Error Cargado Fichero puente para el Pase. Proceso Interrumpido";
          |ACABA;
      |FINSI;
      aDef390 = aDef;
  |SINO;
      aDef    = eaPathDef + "encoemp5";
      |CARGA_DEF aDef, caenlac@;
      |SI FSalida ! 0;
          aAlfa1 = eaPathDef - "/def/";
          aAlfa1 = aAlfa1 + "/vent/";
          aDef   = aAlfa1 + "encoemp5";
          |CARGA_DEF aDef, caenlac@;
          |SI FSalida ! 0;
              |MENAV "    Error Cargado Fichero puente para el Pase. Proceso Interrumpido";
              |ACABA;
          |FINSI;
      |FINSI;
      aDef390 = aDef;
  |FINSI;

  nDEmpresa = #0#1; nDPeriodo = #0#2;  || Por si se hace masivo
  nHEmpresa = #0#1; nHPeriodo = #0#2;
  |BUCLE LeeCanempre;
|FCAL;
