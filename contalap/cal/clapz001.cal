|FICHEROS;
  clapz001 #0;
  clapm002 #2;

  &canempr@ #330;
   camaasc@ #301;
  &caconce@ #306;
  &cacuent@ #310;
|FIN;

|VARIABLES;
 aBlanc     = "";
 aAlfa1     = "";
 aAlfa2     = "";
 nNume1     = 0;
 nNume2     = 0;
 nCampo     = 0;
 nCamp1     = 0;
 nCamp2     = 0;
 nCamp3     = 0;
 nCamp4     = 0;

 aDef       = "";
 aQueQuiero = "";
 aNumCampos = "";
 nNumCampos = 0;
 nCamposEmp = 0;
 nAlgun     = 0;

 aPath      = "";
 aBase      = "";
 aFiche     = "";
 nHadle     = 0;

 aDef330    = "";
 aDef301    = "";
 aDef310    = "";
 aDef306    = "";

 nUltAsto   = 0;
 nUltDocu   = 0;
 aEmpresa   = "";
 aFec1      = "";
 aFec2      = "";
 aContenido = "";
 aVarAlfa1  = "";
 aAna       = "";
 nEjercicio = 0;
 nAny       = 0;
 aDia       = "";
 aMes       = "";

 nInkey = 0;
 nDEmpresa = 0;
 nHEmpresa = 0;
 nDPeriodo = 0;
 nHPeriodo = 0;
|FIN;

|INCLUYE dsclap_i;

|CALCULO Tipo8z001; |TIPO 8;
 |HAZ eLeePathFich;
 |SI eswError = 1;
     |PTEC 807;
     |ACABA;
 |FINSI;

 aAlfa1   = eaPathDatos - "fich/";
 eaPathDef = aAlfa1 + "def/";
 aDef     = eaPathDef + "canempre";
 |CARGA_DEF aDef, canempr@;
 |SI FSalida ! 0;
     |MENAV "    Error Cargado Fichero de Empresas. Proceso Interrumpido";
     |PTEC 807;
     |ACABA;
 |FINSI;
 aDef330 = aDef;

 |PATH_DAT #330 eaPathDatos;

 |SI nConta = 0;
     aAlfa1    = eaPathDef - "contagen/def/";
     aAlfa1    = aAlfa1 + "modelos/def/";
     aDef      = aAlfa1 + "dsmom030";
 |SINO;
     aDef      = eaPathDef + "caconcep";
 |FINSI;
 |CARGA_DEF aDef, caconce@;
 |SI FSalida ! 0;
     |MENAV "    Error Cargado Fichero de Conceptos. Proceso Interrumpido";
     |PTEC 807;
     |ACABA;
 |FINSI;
 aDef306 = aDef;
|FCAL;

|RUTINA infA2;
   #330#2 = nDEmpresa;
   #330#3 = 0;
|FRUT;

|RUTINA supA2;
   #330#2 = nHEmpresa;
   #330#3 = 9;
|FRUT;

|CALCULO LeeEmpresa; |TIPO 0;
  nInkey = FSalida;

  |C_M #0Campo, 0, aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

  nCamp1 = Campo;
  nCamp2 = nCamp1 + 10;
  nCamp3 = nCamp1 + 20;
  nCamp4 = nCamp1 + 30;
  |SI nInkey = 10;
     nDEmpresa = 0; nHEmpresa = 99999;
     |ABRE #330;
     |CONSULTA_F_CLAVE #1#330,infA2,supA2;
     #0nCamp1 = #330#2; |PINTA #0nCamp1;
     #0nCamp2 = #330#3; |PINTA #0nCamp2;
     |CIERRA #330;
     |PTEC 802;
     |ACABA;
  |FINSI;

  |SI #0Campo = 0;
      |ACABA;
  |FINSI;

  |ABRE #330;
  #330#2 = #0nCamp1;
  #330#3 = 0;
  |LEE 011#330];
  |SI FSalida ! 0;  |O #330#2 ! #0nCamp1;
      |MENAV "     NO Existe la empresa Contable";
      |ERROR;
  |SINO;
      #0nCamp4 = #330#1; |PINTA #0nCamp4;
  |FINSI;
  |CIERRA #330;
|FCAL;

|CALCULO Select; |TIPO 0;
     |SI #0#6 = "N";
         #0#7  = 1;     |PINTA #0#7;  |C_M #0#7, 1, "N";
         #0#8  = 99999; |PINTA #0#8;  |C_M #0#8, 1, "N";
         #0#9  = 0;     |PINTA #0#9;  |C_M #0#9, 1, "N";
         #0#10 = 9;     |PINTA #0#10; |C_M #0#10,1, "N";
         |PARA nCampo = 11;  |SI nCampo [ 30;  |HACIENDO nCampo = nCampo + 1;
               |C_M #0nCampo, 1, "S";
         |SIGUE;
     |SINO;
         |C_M #0#7, 1, "S";
         |C_M #0#8, 1, "S";
         |C_M #0#9, 1, "S";
         |C_M #0#10,1, "S";
         |PARA nCampo = 11; |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
               #0nCampo = 0; |PINTA #0nCampo; |C_M #0nCampo, 1, "N";
               nCamp2 = nCampo + 10;
               nCamp3 = nCampo + 20;
               nCamp4 = nCampo + 30;
               #0nCamp2 = 0; |PINTA #0nCamp2; |C_M #0nCamp2, 1, "N";
               #0nCamp3 = ""; |PINTA #0nCamp3;
               #0nCamp4 = ""; |PINTA #0nCamp4;
         |SIGUE;
     |FINSI;
|FCAL;

|CALCULO NoMasEmpresas; |TIPO 0;

     |PARA nCampo = Campo;  |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
           nCamp2 = nCampo + 10;
           nCamp3 = nCampo + 20;
           nCamp4 = nCampo + 30;
           |SI #0Campo = 0;
               |SI Campo ! nCampo;
                   #0nCampo = 0;  |C_M #0nCampo, 1, "N"; |PINTA #0nCampo;
               |FINSI;
               #0nCamp2 = 0;  |PINTA #0nCamp2; |C_M #0nCamp2, 1, "N";
               #0nCamp3 = ""; |PINTA #0nCamp3;
               #0nCamp4 = ""; |PINTA #0nCamp4;
           |SINO;
               |C_M #0nCampo, 1, "S";
               |C_M #0nCamp2, 1, "S";
          |FINSI;
    |SIGUE;
|FCAL;

|CALCULO LeeEmpresaP; |TIPO 0;
  nInkey = FSalida;

  |C_M #0Campo, 0, aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

  |SI nInkey = 2; |ACABA; |FINSI;

  nCamp2 = Campo;
  nCamp1 = nCamp2 - 10;
  nCamp3 = nCamp1 + 20;
  nCamp4 = nCamp1 + 30;
  |SI nInkey = 10;
     nDEmpresa = #0nCamp1; nHEmpresa = #0nCamp1;
     |ABRE #330;
     |CONSULTA_F_CLAVE #1#330,infA2,supA2;
     #0nCamp2 = #330#3; |PINTA #0nCamp2;
     |CIERRA #330;
  |FINSI;

  |ABRE #330;
  #330#2 = #0nCamp1;
  #330#3 = #0nCamp2;
  |LEE 011#330=;
  |SI FSalida ! 0;
      |MENAV "     NO Existe Periodo en la Empresa Contable";
      |ERROR;
  |SINO;
      |SI #330#26 > 80;
          nNume1 = #330#26 + 1900;
      |SINO;
          nNume1 = #330#26 + 2000;
      |FINSI;
      #0nCamp3 = nNume1; |PINTA #0nCamp3;
      #0nCamp4 = #330#1; |PINTA #0nCamp4;
  |FINSI;
  |CIERRA #330;
|FCAL;


|| ////////////////////////////////////////////////////////////
|PROCESO CalFechas;

   aAlfa2 = nEjercicio;
   aAlfa1 = #330#11;
   aMes   = ("00" + aAlfa1) % -102;
   aFec1 = "01/" + aMes + "/" + aAlfa2;

   aAlfa1 = #330#12;
   aMes   = ("00" + aAlfa1) % -102;
   nAny   = nEjercicio;
   |SI #330#11 > #330#12; nAny = nAny + 1; |FINSI;
   aAlfa2 = nAny;

   aDia = "31";
   |SI #330#12 = 4; |O #330#12 = 6; |O #330#12 = 9; |O #330#12 = 11;
       aDia = "30";
   |FINSI;
   |SI #330#12 = 2;
      aDia = "28"; nNume1 = nAny / 4; nNume2 = (nAny / 4) ! 0;
      |SI nNume1 = nNume2;
          aDia = "29";
      |FINSI;
   |FINSI;
   aFec2 = aDia + "/" + aMes + "/" + aAlfa2;

   |SI nConta = 0;
       |SI nCamposEmp ] 35;
           |SI #330#35 > "01.01.1900";
               aAlfa1 = #330#35;
               aFec2  = (aAlfa1 % 102) + "/" + (aAlfa1 % 402) + "/" + (aAlfa1 % -104);
           |FINSI;
       |FINSI;
   |FINSI;
|FPRC;


|PROCESO LeeEmpresas;

 nUltAsto = 1;
 nUltDocu = 1;
 enEmpresa = #330#2;

 aAlfa1 = #330#2; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
 aAlfa2 = #330#3; |QBF aAlfa2; aAlfa2 = ("0"     + aAlfa2) % -101;
 aEmpresa = aAlfa1 + aAlfa2;

 aPath = eaPathDatos + aAlfa1 + aAlfa2 + "/";

 aDef     = eaPathDef + "camaasic";
 |CARGA_DEF aDef, camaasc@;
 |SI FSalida ! 0;

     |MENAV "    Error Cargado Fichero de Asientos. Proceso Interrumpido";
     aDef301 = "";

 |SINO;

     aDef301 = aDef;
     |PATH_DAT #301 aPath;

     |ABRE #301;
     |SELECT #2#301;
     |DDEFECTO #301;
     |LEE 041#301u;
     nUltAsto = #301#3;

     |SELECT #3#301;
     |DDEFECTO #301;
     |LEE 041#301u;
     nUltDocu = #301#2;

     |SELECT #1#301;
     |CIERRA #301;

 |FINSI;

 aBlanc = " " * 40;
 aAlfa1 = (aEmpresa + aBlanc) % 120;
 aVarAlfa1 = "[E]" + aAlfa1;     ||codigo de la empresa

 aAlfa1 = #330#1;
 eaAlfa = aAlfa1; |HAZ eQuitaRaros; aAlfa1 = eaAlfa;
 aAlfa1 = (aAlfa1 + aBlanc) % 140;
 aVarAlfa1 = aVarAlfa1 + aAlfa1;   || nombre

 |SI #330#13 = 1; nNume1 = #330#14; |FINSI;
 |SI #330#13 = 2; nNume1 = #330#15; |FINSI;
 |SI #330#13 = 3; nNume1 = #330#16; |FINSI;
 |SI #330#13 = 4; nNume1 = #330#17; |FINSI;
 |SI #330#13 = 5; nNume1 = #330#18; |FINSI;
 |SI #330#13 = 6; nNume1 = #330#19; |FINSI;
 aAlfa1 = nNume1; |QBF aAlfa1;
 aAlfa1 = ("00" + aAlfa1) % -102;
 aVarAlfa1 = aVarAlfa1 + aAlfa1;   || digitos del plan contable
 |SI #330#26 > 80;
     nEjercicio = 1900 + #330#26;
 |SINO;
     nEjercicio = 2000 + #330#26;
 |FINSI;
 aAlfa1 = nEjercicio;
 aVarAlfa1 = aVarAlfa1 + aAlfa1;   || Ejercicio

 |SI nConta = 0;  eaDepar  = #330#24; |FINSI;

 |HAZ CalFechas;
 aAlfa1 = aFec1;
 aVarAlfa1 = aVarAlfa1 + aAlfa1;   || Fecha Inicio
 aAlfa1 = aFec2;
 aVarAlfa1 = aVarAlfa1 + aAlfa1;   || Fecha Final

 aAna = "N";
 |HAZ eAnalitica;
 |SI enSwAna ! 0; aAna = "S"; |FINSI;

 aVarAlfa1 = aVarAlfa1 + aAna;   ||  Analitica S/N

 aAlfa1 = nUltAsto; |QBF aAlfa1;
 aAlfa1 = ("00000" + aAlfa1) % -105;
 aVarAlfa1 = aVarAlfa1 + aAlfa1;   ||  Ultimo Asiento

 aAlfa1 = nUltDocu; |QBF aAlfa1;
 aAlfa1 = ("0000000000" + aAlfa1) % -110;
 aVarAlfa1 = aVarAlfa1 + aAlfa1;   ||  Ultimo Documento

 aContenido = aVarAlfa1 + &013 + &010;
 |XGRABA nHadle, aContenido;

 nAlgun = nAlgun + 1;

 |SI aDef301 ! ""; |DESCARGA_DEF #camaasc@; aDef301 = ""; |FINSI;


 #0#1 = #330#2;       |PINTA #0#1;
 #0#2 = #330#3;       |PINTA #0#2;
 #0#3 = nEjercicio;   |PINTA #0#3;
 #0#4 = #330#1;       |PINTA #0#4;
|FPRC;

|DEFBUCLE LeeEmpresas;
 #canempr@#1002;
 ;
 nDEmpresa,nDPeriodo;
 nHEmpresa,nHPeriodo;
 ;
 NULL, LeeEmpresas;
|FIN;


|CALCULO Tipo3z001;  |TIPO 3;

 |SI #0#0 = "NO"; |ACABA; |FINSI;
 nAlgun = 0;

 |DFICB aBase;
 aFiche = ("em" + Usuario) % 108 + ".dat";
 aFiche = aBase + aFiche;
 eaOrigen = aFiche;

 |SI #0#51 = "A";
     eaDestino = eaPathExImp + eaFichEmps;
     |HAZ eReciboFich;
 |SINO;
     |FBORRA eaOrigen;
 |FINSI;

 |XABRE "BA", aFiche,nHadle;
 |SI FSalida ] 0;
     |SI #0#6 = "S";
         nDEmpresa = #0#7; nDPeriodo = #0#9;
         nHEmpresa = #0#8; nHPeriodo = #0#10;
         |BUCLE LeeEmpresas;
     |SINO;
         |PARA nCamp1 = 11; |SI nCamp1 [ 20; |HACIENDO nCamp1 = nCamp1 + 1;
               |SI #0nCamp1 ! 0;
                   nCamp2 = nCamp1 + 10;
                   nDEmpresa = #0nCamp1; nDPeriodo = #0nCamp2;
                   nHEmpresa = #0nCamp1; nHPeriodo = #0nCamp2;
                   |BUCLE LeeEmpresas;
               |FINSI;
         |SIGUE;
     |FINSI;
 |FINSI;

 |XCIERRA nHadle;

 |HAZ Descargar;
 |SI nAlgun ! 0;
     eaDestino = eaPathExImp + eaFichEmps;
     |HAZ eEnvioFich;
 |FINSI;
|FCAL;

|PROCESO Descargar; |TIPO 9;
 |SI aDef301 ! ""; |DESCARGA_DEF #camaasc@; aDef301 = ""; |FINSI;
 |SI aDef310 ! ""; |DESCARGA_DEF #cacuent@; aDef310 = ""; |FINSI;
 |SI aDef306 ! ""; |DESCARGA_DEF #caconce@; aDef306 = ""; |FINSI;
 |SI aDef330 ! ""; |DESCARGA_DEF #canempr@; aDef330 = ""; |FINSI;
|FPRC;
