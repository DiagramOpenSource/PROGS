|FICHEROS;
  clapz005 #0;

  clapm002 #2,MANTE;
  clapw004 #904,MANTE;
  clapw005 #905,MANTE;

  clapw006 #56,MANTE;

  &canempr@ #330;
  &cacuent@ #310;
  &caconce@ #306;
|FIN;

|VARIABLES;
 nCopi      = 0;
 nEstado    = 0;
 nModoEntrada = 0;
 aCuenta    = "";
 aNomCta    = "";

 aAlfa1     = "";
 aAlfa2     = "";
 nNume1     = 0;
 nNume2     = 0;

 aDef       = "";
 aDef330    = "";
 aDef310    = "";
 aDef306    = "";

 nInkey     = 0;
 nDEmpresa  = 0;
 nHEmpresa  = 0;
 nDPeriodo  = 0;
 nHPeriodo  = 0;

 nAlgun     = 0;
 aPath      = "";
 aBase      = "";

 aEmpresa   = "";

 nSalir = 0;
 aFichero = "";
 aPathM   = "";
|FIN;

|INCLUYE dsclap_i;

|| ************************************************************************
|RUTINA inf2;
   #310#0 = "            ";
   #310#1 = 0;
   #310#3 = "S";
|FRUT;

|RUTINA sup2;
   #310#0 = "zzzzzzzzzzzz";
   #310#1 = 9;
   #310#3 = "S";
|FRUT;

|| ************************************************************************
|PROCESO Tp3m002; |TIPO 3;
  #2#6 = #0#1;
  #2#7 = #904#3;
  #2#8 = #905#3;
  |SI #2#4 > #2#5;
      |MENAV "    Error en Cuentas Desde/Hasta";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO LaCuenta6; |TIPO 6;
     nInkey = FSalida;
     eaCuenta = #2Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #2Campo = eaCuenta;
     |PINTA #2Campo;
|FPRC;


|PROCESO LaCuenta0; |TIPO 0;
   |SI nInkey = 10;
      |ABRE #310;
      #310#0 = #2Campo;
      |CONSULTA_F_CLAVE #1#310,inf2,sup2;
      #2Campo = #310#0; |PINTA #2Campo;
      |CIERRA #310;
      |ERROR;
      |ACABA;
   |FINSI;
   |SI Campo = 5; |Y #2#4 > #2#5;
      |MENAV "    Error en Cuentas Desde/Hasta";
      |ERROR;
  |FINSI;
|FPRC;

|| ************************************************************************
|PROCESO Borram002;
 |BORRA 020#2a;
 |LIBERA #2;
|FPRC;

|DEFBUCLE Borram002;
 #2#1;
 ;
 #905#0, #905#1, #905#2, 001;
 #905#0, #905#1, #905#2, 999;
 be;
 NULL, Borram002;
|FIN;

|PROCESO Borraw005;
 |BUCLE Borram002;
 |BORRA 020#905a;
 |LIBERA #905;
|FPRC;

|DEFBUCLE Borraw005;
 #905#1;
 ;
 #904#0, #904#1, 001;
 #904#0, #904#1, 999;
 be;
 NULL, Borraw005;
|FIN;
|| +++++++++++++++++++++++++++++++++++++++++++++++++++++

|CALCULO PrLeeConcepto;
     nInkey = FSalida;

     |ABRE #306;
     #306#0 = #905#2;
     |LEE 001#306=;
     |SI FSalida ! 0;
         |MENAV "    Error. No existe Concepto";
         |ERROR;
         #905#3 = "";
     |SINO;
         #905#3 = #306#1;
     |FINSI;
     |PINTA #905#1;
     |CIERRA #306;
|FCAL;

|RUTINA inf6;
   #306#0 = 001;
|FRUT;

|RUTINA sup6;
   #306#0 = 999;
|FRUT;

|CALCULO LeeConcepto; |TIPO 0;

   |SI nInkey = 10;
      |ABRE #306;
      |CONSULTA_F_CLAVE #1#306,inf6,sup6;
      #905#2 = #306#0; |PINTA #905#2;
      #905#3 = #306#1; |PINTA #905#3;
      |CIERRA #306;
      |ERROR;
   |FINSI;
   |HAZ PrLeeConcepto;
|FCAL;

|CALCULO Tp11w005; |TIPO 11;
   nInkey = FSalida;

   |SI nInkey = 10;
       |HAZ LeeConcepto;
   |FINSI;
|FCAL;

|RUTINA ClauBaja2;
 #2#0 = #905#0;
 #2#1 = #905#1;
 #2#2 = #905#2;
 #2#3 = 001;
|FRUT;

|RUTINA ClauAlta2;
 #2#0 = #905#0;
 #2#1 = #905#1;
 #2#2 = #905#2;
 #2#3 = 999;
|FRUT;

|CALCULO Tp7w005; |TIPO 7;
 |PINDA #0#2;
 |ENTLINEAL #1#2, -4, 7, 22, 0, ClauBaja2, ClauAlta2;
|FCAL;

|CALCULO Tp3w005; |TIPO 3;
 |ENTLINEAL #1#2, -4, 2, 22, 0, ClauBaja2, ClauAlta2;
|FCAL;

|CALCULO Tp2w005; |TIPO 2;
 nModoEntrada = (FEntrada / 100) ! 0;
 nNume1 = 0 +. 1;
 |SI nModoEntrada = 3;
     |CONFI "    NBorrar los datos del Concepto ";
     |SI FSalida ! 0;
         |ERROR;
         |ACABA;
     |FINSI;
     |BUCLE Borram002;
 |FINSI;
|FCAL;

|PROCESO ClauBaja905;
  #905#0 = #904#0;
  #905#1 = #904#1;
  #905#2 = 001;
|FPRC;

|PROCESO ClauAlta905;
  #905#0 = #904#0;
  #905#1 = #904#1;
  #905#2 = 999;
|FPRC;
|| *************************************************************************

|CALCULO PrLeeCuenta; |TIPO 6;
     nInkey = FSalida;

     eaCuenta = #904Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #904Campo = eaCuenta;
     |PINTA #904Campo;
|FCAL;

|CALCULO LeeCuenta; |TIPO 0;
   nModoEntrada = (FEntrada / 100) ! 0;
   |SI nInkey = 10;
      |ABRE #310;
      #310#0 = #904#1;
      |CONSULTA_F_CLAVE #1#310,inf2,sup2;
      #904#1 = #310#0; |PINTA #904#0;
      #904#3 = #310#2; |PINTA #904#3;
      |CIERRA #310;
      |ERROR;
   |FINSI;

   emCta = 1;
   |SI nModoEntrada ! 1; emCta = 0; |FINSI;
   eaCuenta = #904#1;
   |HAZ SacaNivel;
   |SI enNivel = 0;
       |SI nModoEntrada ! 1;
           |CONFI "0000NLONGITUD DE CUENTA FUERA DE NIVEL!. CONTINUAR ";
           |SI FSalida ! 0;
               |ERROR;
           |FINSI;
       |FINSI;
       |ACABA;
   |SINO;
       #904#2 = enNivel;
   |FINSI;
   emCta = 0;

   |ABRE #310;
   #310#0 = eaCuenta;
   #310#1 = enNivel;
   |LEE 040#310=;
   |SI FSalida ! 0;
       |MENAV "    Error. No existe Cuenta Contable";
       |ERROR;
   |SINO;
       |SI #310#3 = "N";
           |MENAV "    Error. Cuenta sin Pase Directo";
           |ERROR;
       |SINO;
           #904#3 = #310#2; |PINTA #904#3;
       |FINSI;
   |FINSI;
   |CIERRA #310;
|FCAL;

|CALCULO Tp11w004; |TIPO 11;
   nInkey = FSalida;

   |SI nInkey = 20;
      eaCuenta = #904Campo;
      salidas  = FSalida;
      |HAZ FiltraPuntos;
      #904Campo =eaCuenta;
   |FINSI;

   |SI nInkey = 10;
       |HAZ LeeCuenta;
   |FINSI;

   |SI nInkey = 11;
       nCopi = 2;
       |HAZ Copiar;
       |ERROR;
   |FINSI;
|FCAL;

|CALCULO Tp7w004; |TIPO 7;
 |PINDA #0#905;
 |ENTLINEAL #1#905, -3, 7, 22, 0, ClauBaja905, ClauAlta905;
 #905#0 = #904#0;
 #905#1 = #904#1;
 |ENTLINEAL #1#2, -4, 7, 22, 0, ClauBaja2, ClauAlta2;
|FCAL;

|CALCULO Tp2w004; |TIPO 2;
 nModoEntrada = (FEntrada / 100) ! 0;
 nNume1 = 0 +. 1;
 |SI nModoEntrada = 3;
     |CONFI "    NBorrar Todos los datos de la Contrapartida";
     |SI FSalida ! 0;
         |ERROR;
         |ACABA;
     |FINSI;
     |BUCLE Borraw005;
 |FINSI;
|FCAL;

|CALCULO Tp3w004; |TIPO 3;
 |ENTLINEAL #1#905, -3, 2, 22, 0, ClauBaja905, ClauAlta905;
|FCAL;


|PROCESO ClaveBaja904;
  #904#0 = #0#0;
  #904#1 = "            ";
|FPRC;

|PROCESO ClaveAlta904;
  #904#0 = #0#0;
  #904#1 = "zzzzzzzzzzzz";
|FPRC;

|| ////////////////////////////////////////////////////////////////
|| //// Procesos z005
|| ////////////////////////////////////////////////////////////////

|CALCULO Tp12z005; |TIPO 12;
  nInkey = FSalida;
  |SI nInkey = 1; |O nInkey = 7; nSalir = 1; |ACABA; |FINSI;

  enEmpresa = #0#0;
  enPeriodo = -1;
  |HAZ eLeeEmpresa;

  aAlfa1 = #330#2; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
  aAlfa2 = #330#3; |QBF aAlfa2; aAlfa2 = ("0"     + aAlfa2) % -101;
  aEmpresa = aAlfa1 + aAlfa2;
  aPath    = eaPathDatos + aAlfa1 + aAlfa2 + "/";

  |PATH_DAT #310 aPath;
  |SI nConta ! 0;
      |PATH_DAT #306 aPath;
  |FINSI;

  |ENTLINEAL #1#904, -2, 2, 22, 0, ClaveBaja904, ClaveAlta904;
|FCAL;

|RUTINA infA2;
   #330#2 = nDEmpresa;
   #330#3 = 0;
|FRUT;

|RUTINA supA2;
   #330#2 = nHEmpresa;
   #330#3 = 9;
|FRUT;

|CALCULO LeeEmpresa; |TIPO 0;
  nInkey = FSalida;

  |SI nInkey = 10;
     nDEmpresa = 0;
     nHEmpresa = 99999;
     |ABRE #330;
     |CONSULTA_F_CLAVE #1#330,infA2,supA2;
     #0#0 = #330#2; |PINTA #0#0;
     #0#1 = #330#3; |PINTA #0#1;
     |CIERRA #330;
  |FINSI;

  |SI nInkey = 11;
      nCopi = 1;
      |HAZ Copiar;
      |ERROR;
      |ACABA;
  |FINSI;

  |SI #0#0 ! 0;
      |ABRE #330;
      #330#2 = #0#0;
      #330#3 = 0;
      |LEE 011#330];
      |SI FSalida ! 0;  |O #330#2 ! #0#0;
          |MENAV "     NO Existe la empresa Contable";
          |ERROR;
      |SINO;
          #0#1 = #330#1; |PINTA #0#1;
          #0#2 = #330#8; |PINTA #0#2;
      |FINSI;
      |CIERRA #330;
  |SINO;
      #0#1 = "EMPRESA GENERAL"; |PINTA #0#1;
      #0#2 = ""; |PINTA #0#2;
  |FINSI;
|FCAL;

|| *********************************************************************
|PROCESO LeeM002;
 #904#0 = #2#0; || Empresa;
 #904#1 = #2#1; || Contrapartida
 |LEE 040#904=;
 |SI FSalida ! 0;
     #904#0 = #2#0;
     #904#1 = #2#1;
     eaCuenta = #2#1; |HAZ SacaNivel; #904#2 = enNivel;
     #904#3 = #2#7;
     |GRABA 020#904n;
 |FINSI;

 #905#0 = #2#0;
 #905#1 = #2#1;
 #905#2 = #2#2;
 |LEE 040#905=;
 |SI FSalida ! 0;
     #905#0 = #2#0;
     #905#1 = #2#1;
     #905#2 = #2#2;
     #905#3 = #2#8;
     |GRABA 020#905n;
 |FINSI;
|FPRC;

|DEFBUCLE LeeM002;
 #2#1;
 ;
 nDEmpresa, aDCuenta, 001, 001;
 nHEmpresa, aHCuenta, 999, 999;
 ;
 NULL, LeeM002;
|FIN;

|PROCESO CargaAux904;
 aFichero = "4w" + Usuario;
 |NOME_DAT #904 aFichero;
 |DELINDEX #904;

 aFichero = "5w" + Usuario;
 |NOME_DAT #905 aFichero;
 |DELINDEX #905;


 nDEmpresa = 00000;
 nHEmpresa = 99999;
 aDCuenta  = "            ";
 aHCuenta = "zzzzzzzzzzzzz";
 |ABRE #904;
 |ABRE #905;
 |BUCLE LeeM002;
 |CIERRA #905;

 #904#0 = 00000;
 #904#1 = "            ";
 |LEE 001#904];
 |SI FSalida ! 0; |O #904#0 ! 00000;
     #904#0 = 00000;
     #904#1 = "            ";
     #904#2 = 0;
     |GRABA 020#904n;
 |FINSI;
 |CIERRA #904;

 |DELINDEX #905;
 |DELINDEX #904;
|FPRC;
|| *********************************************************************
|| *********************************************************************

|PROGRAMA;

 |HAZ eLeePathFich;
 |SI eswError = 1;
     |ACABA;
 |FINSI;

 aAlfa1    = eaPathDatos - "fich/";
 eaPathDef = aAlfa1 + "def/";
 aDef      = eaPathDef + "canempre";
 |CARGA_DEF aDef, canempr@;
 |SI FSalida ! 0;
     |MENAV "    Error Cargado Fichero de Empresas. Proceso Interrumpido";
     |PTEC 807;
     |ACABA;
 |FINSI;
 aDef330 = aDef;

 |PATH_DAT #330 eaPathDatos;
 |HAZ CargaAux904;

 |SI nConta = 0;
     aAlfa1    = eaPathDef - "contagen/def/";
     aAlfa1    = aAlfa1 + "modelos/def/";
     aDef      = aAlfa1 + "dsmom030";
 |SINO;
     aDef      = eaPathDef + "caconcep";
 |FINSI;
 |CARGA_DEF aDef, caconce@;
 |SI FSalida ! 0;
     |MENAV "    Error Cargado Fichero de Conceptos. Proceso Interrumpido";
     |PTEC 807;
     |VETE ElFinal;
 |FINSI;
 aDef306 = aDef;

 aDef     = eaPathDef + "cacuenta";
 |CARGA_DEF aDef, cacuent@;
 |SI FSalida ! 0;
     |MENAV "    Error Cargado Fichero de Cuentas. Proceso Interrumpido";
     aDef310 = "";
     |VETE ElFinal;
 |FINSI;
 aDef310 = aDef;

 |SI nConta = 0;
     |PATH_DAT #306 eaPathM;
 |FINSI;

 |CLS;
 |CABEZA "Relaccion Contrapartidas y Conceptos";

 |ABRE #0;
 |DDEFECTO #0;
 |LEE 001#0p;
 |SI FSalida ! 0;
     |DDEFECTO #0;
     |GRABA 020#0c;
 |FINSI;

 |PINPA #0#0;
 |PINDA #0#0;

 |ENTLINEAL #1#904, -2, 7, 22, 0, ClaveBaja904, ClaveAlta904;

 nSalir = 0;
 |PARA; |SI nSalir = 0; |HACIENDO;
          |ENDATOS #2#0;
          |SI FSalida ! 0;
              nSalir = 1;
          |FINSI;
 |SIGUE;
 |GRABA 020#0a;
 |CIERRA #0;

|ET ElFinal;
  |SI aDef310 ! ""; |DESCARGA_DEF #cacuent@; aDef310 = ""; |FINSI;
  |SI aDef306 ! ""; |DESCARGA_DEF #caconce@; aDef306 = ""; |FINSI;
  |SI aDef330 ! ""; |DESCARGA_DEF #canempr@; aDef330 = ""; |FINSI;
|FPRO;

|| *****************************************************************

|CALCULO LeePerO;
|FCAL;
|CALCULO LeePerD;
|FCAL;

|CALCULO LeeContra;

   nNume1 = 8; |SI Campo = 4; nNume1 = 9; |FINSI;
   eaCuenta = #56Campo;
   salidas  = FSalida;
   |HAZ FiltraPuntos;
   #56Campo = eaCuenta;
   |PINTA #56Campo;

   |SI nInkey = 10;
      |ABRE #310;
      #310#0 = #56Campo;
      |CONSULTA_F_CLAVE #1#310,inf2,sup2;
      #56Campo = #310#0; |PINTA #56Campo;
      |CIERRA #310;
   |FINSI;

   eaCuenta = #56Campo;
   |HAZ SacaNivel;

   nEstado = 0;
   |ABRE #310;
   #310#0 = eaCuenta;
   #310#1 = enNivel;
   |LEE 040#310=;
   |SI FSalida ! 0;
       nEstado = 1;
       |MENAV "    Error. No existe Cuenta Contable";
   |SINO;
       |SI #310#3 = "N";
           nEstado = 1;
           |MENAV "    Error. Cuenta sin Pase Directo";
       |FINSI;
   |FINSI;
   #56nNume1 = #310#2;
   |CIERRA #310;
|FCAL;

|PROCESO LeeContraO;
  nInkey = FSalida;
  |SI nCopi = 1; |ACABA; |FINSI;

  |HAZ LeeContra;
  |SI nEstado = 1;
      |ERROR;
      |ACABA;
  |FINSI;

  |ABRE #2;
  #2#0 = #56#0;
  #2#1 = #56#1;
  #2#2 = 001;
  |LEE 001#2];
  |SI FSalida ! 0; |O #2#0 ! #56#0; |O #2#1 ! #56#1;
      |MENAV "    ERROR. NO EXISTE DEFINICION PARA ESTA CUENTA ";
      |ERROR;
  |SINO;
      |PINTA 1530, #56#8;
  |FINSI;
  |CIERRA #2;
|FPRC;

|PROCESO LeeContraD;
  |SI nCopi = 1; |ACABA; |FINSI;
  nInkey = FSalida;

  |SI #56#1 = #56#4;
      |MENAV "    ERROR. La Cuenta Destino es la misma que la Cuenta Origen";
      |ERROR;
      |ACABA;
  |FINSI;

  |HAZ LeeContra;
  |SI nEstado = 1;
      |ERROR;
      |ACABA;
  |FINSI;

  |ABRE #2;
  #2#0 = #56#3;
  #2#1 = #56#4;
  #2#2 = 001;
  |LEE 001#2];
  |SI FSalida = 0; |Y #2#0 = #56#3; |Y #2#1 = #56#4;
      |CONFI "    NYA EXISTE DEFINICION PARA ESTA CUENTA. CONTINUAR  ";
      |SI FSalida ! 0;
          |ERROR;
      |FINSI;
  |FINSI;
  |PINTA 1630, #56#8;
  |CIERRA #2;
|FPRC;

|CALCULO LeeEmp;

  nNume1 = 6; |SI Campo = 3; nNume1 = 7; |FINSI;
  |SI nInkey = 10;
     nDEmpresa = 0;
     nHEmpresa = 99999;
     |ABRE #330;
     |CONSULTA_F_CLAVE #1#330,infA2,supA2;
     #56Campo  = #330#2; |PINTA #56Campo;
     #56nNume1 = #330#3; |PINTA #56nNume1;
     |CIERRA #330;
  |FINSI;

  nEstado = 0;
  |SI #56Campo ! 0;
      |ABRE #330;
      #330#2 = #56Campo;
      #330#3 = 0;
      |LEE 011#330];
      |SI FSalida ! 0;  |O #330#2 ! #56Campo;
          nEstado = 1;
          |MENAV "     NO Existe la empresa Contable";
      |FINSI;
      |CIERRA #330;
  |SINO;
      #330#1 = "EMPRESA GENERAL";
  |FINSI;
|FCAL;

|PROCESO LeeEmpO;
  nInkey = FSalida;
  |C_M #56Campo,0,aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

  |HAZ LeeEmp;
  |SI nEstado = 1;
      |ERROR;
      |ACABA;
  |FINSI;

  #56nNume1 = #330#1; |PINTA #56nNume1;
  |ABRE #2;
  #2#0 = #56#0;
  #2#1 = "           ";
  #2#2 = 001;
  |LEE 001#2];
  |SI FSalida ! 0; |O #2#0 ! #56#0;
      |MENAV "    ERROR. NO EXISTE DEFINICION PARA ESTA EMPRESA ";
      |ERROR;
  |FINSI;
  |CIERRA #2;
|FPRC;

|PROCESO LeeEmpD;
  nInkey = FSalida;
  |C_M #56Campo,0,aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

  |SI #56#0 = #56#3;
      |MENAV "    ERROR. La Empresa Destino es la misma que la Empresa Origen";
      |ERROR;
      |ACABA;
  |FINSI;

  |HAZ LeeEmp;
  |SI nEstado = 1;
      |ERROR;
      |ACABA;
  |FINSI;
  #56nNume1 = #330#1; |PINTA #56nNume1;

  |ABRE #2;
  #2#0 = #56#3;
  #2#1 = "           ";
  #2#2 = 001;
  |LEE 001#2];
  |SI FSalida = 0; |Y #2#0 = #56#3;
      |CONFI "    NYA EXISTE DEFINICION PARA ESTA EMPRESA. CONTINUAR  ";
      |SI FSalida ! 0;
          |ERROR;
      |FINSI;;
  |FINSI;
  |CIERRA #2;
|FPRC;

|PROCESO LuegoCopia;

     |SALVA_FICHA #2;
     aCuenta = #2#1;
     aNomCta = #2#7;
     |SI nCopi = 2;
         aCuenta = #56#4;
         aNomCta = #56#9;
     |FINSI;

     #2#0 = #56#3;
     #2#1 = aCuenta;
     #2#6 = #56#7;
     #2#7 = aNomCta;
     |GRABA 020#2n;
     |REPON_FICHA #2;
     |LIBERA #2;
|FPRC;

|DEFBUCLE LuegoCopia;
     #2#1;
     ;
     #56#0, aDCuenta, 001, 001;
     #56#0, aHCuenta, 999, 999;
     be;
     NULL, LuegoCopia;
|FIN;


|PROCESO PrimeroBorra;
     |BORRA 020#2a;
     |LIBERA #2;

     |SI nCopi = 1;
         |ABRE #904;
         #904#0 = #2#0;
         #904#1 = #2#1;
         |LEE 000#904=;
         |SI FSalida = 0;
             |BORRA 020#904a;
         |FINSI;
         |CIERRA #904;
     |FINSI;
     |ABRE #905;
     #905#0 = #2#0;
     #905#1 = #2#1;
     #905#2 = #2#2;
     |LEE 001#905=;
     |SI FSalida = 0;
         |BORRA 020#905a;
     |FINSI;
     |CIERRA #905;
|FPRC;

|DEFBUCLE PrimeroBorra;
     #2#1;
     ;
     #56#3, aDCuenta, 001, 001;
     #56#3, aHCuenta, 999, 999;
     be;
     NULL, PrimeroBorra;
|FIN;

|PROCESO Copiar;

 |PUSHV 0706 1858;
 |PINPA #0#56;

 |DDEFECTO #56;
 |C_M #56#0,1,"S";
 |C_M #56#1,1,"S";
 |C_M #56#3,1,"S";
 |C_M #56#4,1,"S";
 |C_M #56#10,1,"N"; |C_V #56#10,1,"N";
 |C_M #56#11,1,"N"; |C_V #56#11,1,"N";

 #56#3 = #0#0;
 #56#7 = #0#1;
 |SI nCopi = 2;
     #56#0 = #0#0; |C_M #56#0,1,"N";
     #56#6 = #0#1; |C_M #56#3,1,"N";
     |PINTA 1508, "Origen ";
     |PINTA 1608, "Destino";
 |FINSI;

 |PINDA #0#56;
 |ENDATOS #1#56;
 |SI FSalida ! 0;
     |POPV;
     |ACABA;
 |FINSI;

 aDCuenta  = #56#1;
 aHCuenta  = #56#4;
 |SI nCopi = 2;
     aDCuenta  = #56#4;
     aHCuenta  = #56#4;
 |FINSI;
 |BUCLE PrimeroBorra;

 aDCuenta  = #56#1;
 aHCuenta  = #56#4;
 |SI nCopi = 2;
     aDCuenta  = #56#1;
     aHCuenta  = #56#1;
 |FINSI;
 |BUCLE LuegoCopia;

 |SI nCopi = 1; |ABRE #904; |FINSI;
 nDEmpresa = #56#3;
 nHEmpresa = #56#3;
 aDCuenta  = "";
 aHCuenta  = "zzzzzzzzzzzz";
 |ABRE #905;
 |BUCLE LeeM002;
 |CIERRA #905;
 |SI nCopi = 1; |CIERRA #904; |FINSI;

 |SI nCopi = 2;
     |PTEC 802;
     |PTEC 807;
 |FINSI;
 |SI nCopi = 1;
     #0#0 = #56#3;
     |PINTA #0#0;
     |PTEC 802;
 |FINSI;
 |POPV;
|FPRC;
