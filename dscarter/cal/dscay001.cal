|FICHEROS;
   dscay001;
   dscam067;
   dscam030;

   camasic@ #100;
   camasil@ #101;

   camasi1@ #200;
   canemp1@ #201;
|FIN;

|VARIABLES;

   &enSw         = 0;
   &enConcepto   = 0;
   &eaConcepto   = "";
   &eaIncidencia = "";

   &eaTip1     = "";
   &eaTipo     = "";
   &eaEmpresa  = "";
   &eaPeriodo  = "";
   &enEjer     = 0;
   &enTError   = 0;
   &eaNomEmp   = "";
   &eaNomEnl   = "";
   &eaEnlace   = "";

   nError      = 0;
   aPath       = "";
   aPathFich   = "";
   aDef1       = "";
   aDef2       = "";
   aDef3       = "";
   aDef4       = "";
   nErrorEmpresa = 0;

   nOk         = 0;
   nCampo      = 0;
   aAlfa       = "";
   aClave      = "";
   nSw         = 0;
   nPrint      = 0;

   informe     = "";
  {20}aPopup   = "";
     nID       = 0;
     aID       = "";
|FIN;

|PROCESO Tipo0C2;
   aAlfa = #dscay001#2;
   |C_M #dscay001#3, 1, aAlfa;
   |C_M #dscay001#4, 1, aAlfa;
   |C_M #dscay001#5, 1, aAlfa;
   |C_M #dscay001#6, 1, aAlfa;
   |SI aAlfa = "N";
      #dscay001#3 = "";  |PINTA #dscay001#3;
      #dscay001#7 = "";  |PINTA #dscay001#7;
      #dscay001#4 = "";  |PINTA #dscay001#4;
      #dscay001#8 = "";  |PINTA #dscay001#8;
      #dscay001#5 = "";  |PINTA #dscay001#5;
      #dscay001#9 = "";  |PINTA #dscay001#9;
      #dscay001#6 = "";  |PINTA #dscay001#6;
      #dscay001#10 = ""; |PINTA #dscay001#10;
   |FINSI;
|FPRC;

|PROCESO Tipo66CTipo; |TIPO 66;
    |C_M #dscay001#Campo#,0,aAlfa;
    |SI aAlfa = "N"; |ACABA; |FINSI;

     aAlfa = "|C007WID";
     aID   = #dscay001#aAlfa#;
     nID   = aID;

     aPopup1  = nID;  || Si fuera -1 en la posicion
     aPopup2  = 8;
     aPopup3  = "     No seleccionar";
     aPopup4  = "CB- COBROS    ";
     aPopup5  = "PG- PAGOS     ";
     aPopup6  = "RM- REMESAS   ";
     aPopup7  = "TA- TALONES   ";
     aPopup8  = "PA- PAGARES   ";
     aPopup9  = "CF- CONFIRMES ";
     aPopup10 = "PD- PAGOS DOM.";

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
         IaPopup  = aPopup3  <-;
         IaPopup  = IaPopup + (FSalida - 1);

         aAlfa = aPopup % 102;
     |FINSI;
     #dscay001#Campo# = aAlfa;
     |PINTA #dscay001#Campo#;
|FPRC;

|PROCESO Tipo0CTipo; |TIPO 0;
    |C_M #dscay001#Campo#,0,aAlfa;
    |SI aAlfa = "N"; |ACABA; |FINSI;

    |POPV;
    nCampo = Campo + 4;
    aAlfa  = "";
    |SI #dscay001#Campo# = "CB"; aAlfa = "COBROS    "; |FINSI;
    |SI #dscay001#Campo# = "PG"; aAlfa = "PAGOS     "; |FINSI;
    |SI #dscay001#Campo# = "RM"; aAlfa = "REMESAS   "; |FINSI;
    |SI #dscay001#Campo# = "TA"; aAlfa = "TALONES   "; |FINSI;
    |SI #dscay001#Campo# = "PA"; aAlfa = "PAGARES   "; |FINSI;
    |SI #dscay001#Campo# = "CF"; aAlfa = "CONFIRMES "; |FINSI;
    |SI #dscay001#Campo# = "PD"; aAlfa = "PAGOS DOM."; |FINSI;

    |SI aAlfa = ""; |Y #dscay001#Campo# ! "  ";
        |MENAV "0000Tipo Enlace incorrecto";
        |ERROR;
    |FINSI;
    #dscay001#nCampo# = aAlfa;
    |PINTA #dscay001#nCampo#;
|FPRC;

|| ////////////////////////////////////////////////////////////////////////
|PROCESO Primero;
   aClave = aAlfa;
   enSw   = 0;
|FPRC;

|PROCESO FinEste;
   |SI enSw = 1;
       enSw = 2;
       |PRINT;
   |FINSI;
|FPRC;

|PROCESO LaEmpresa;

   nErrorEmpresa = 0;
   aPathFich = aPath + "fich/";
   |PATH_DAT #canemp1@#aPathFich#;
   |ABRE #canemp1@;
   #canemp1@#2 = #dscam067#8;
   #canemp1@#3 = #dscam067#9;
   |LEE 041#canemp1@.=;
   |SI FSalida ! 0;
       nErrorEmpresa = 1;
   |FINSI;
   |CIERRA #canemp1@;

   eaEmpresa = (("00000" + #dscam067#8) % -105);
   eaPeriodo = #dscam067#9;

   |SI nErrorEmpresa = 1;
       eaNomEmp  = "NO EXISTE EMPRESA CONTABLE";
       enEjer    = 0;
       |ACABA;
   |FINSI;

   eaNomEmp  = #canemp1@#1;
   enEjer    = 2000 + #canemp1@#26;
   aPathFich = aPath + "fich/" + eaEmpresa + eaPeriodo + "/";

   |PATH_DAT #camasic@#aPathFich#;
   |PATH_DAT #camasil@#aPathFich#;

|FPRC;

|PROCESO Lineas;
   eaIncidencia = #camasil@#7;
   |SI #camasil@#3 = #dscam067#7;
       |ERROR10;
   |FINSI;
|FPRC;

|DEFBUCLE Lineas;
   #camasil@#1004;
   ;
   #camasic@#0,#camasic@#1,#camasic@#2, 0001;
   #camasic@#0,#camasic@#1,#camasic@#2, 9999;
   ;
   NULL,Lineas;
|FIN;

|PROCESO HistEnl;

   enTError     = 0;
   eaIncidencia = "";

   |SI #dscay001#2 = "S";
       nOk = 0;
       |SI #dscay001#3 ! "  "; |Y #dscay001#3 = #dscam067#0; nOk = 1; |FINSI;
       |SI #dscay001#4 ! "  "; |Y #dscay001#4 = #dscam067#0; nOk = 1; |FINSI;
       |SI #dscay001#5 ! "  "; |Y #dscay001#5 = #dscam067#0; nOk = 1; |FINSI;
       |SI #dscay001#6 ! "  "; |Y #dscay001#6 = #dscam067#0; nOk = 1; |FINSI;
       |SI nOk = 0;
           |ACABA;
       |FINSI;
   |FINSI;

   #dscam030#0 = #dscam067#10;
   |LEE 000#dscam030.=;
   |SI FSalida ! 0;
       |ACABA;
   |FINSI;
   aPath  = #dscam030#6; |QBF aPath;

   |SI #dscay001#13 = 1;
       aAlfa = ("00000" + #dscam067#8) % -105;
       aAlfa = aAlfa + #dscam067#9;
   |FINSI;
   |SI #dscay001#13 = 2; aAlfa = #dscam067#10; |FINSI;

   |SI aAlfa ! aClave; |O nSw = 0;
       |SI nSw = 1;
           |HAZ FinEste;
       |FINSI;
       |HAZ Primero;
       nSw = 1;
   |FINSI;

   |CIERRA #camasil@;
   |CIERRA #camasic@;

   eaEnlace = #dscam067#10;
   eaNomEnl = #dscam030#1;
   eaTipo   = #dscam030#3;
   eaTip1   = #dscam067#0;
   |HAZ LaEmpresa;
   |SI nErrorEmpresa = 1;
       enTError     = 1;
       eaIncidencia = "NO EXISTE EMPRESA CONTABLE";
       |PRINT; nPrint = 1;
       enSw = 1;
       |ACABA;
   |FINSI;

   |ABRE #camasic@;
   #camasic@#0 = #dscam067#4;
   #camasic@#1 = #dscam067#5;
   #camasic@#2 = #dscam067#6;
   |LEE 000#camasic@.=;
   |SI FSalida = 0;
       |SI #dscay001#16 = "T";
           |BUCLE Lineas;
           |PRINT; nPrint = 1;
           enSw = 1;
       |FINSI;
   |SINO;
       enTError = 1;
       eaIncidencia = "NO EXISTE ASIENTO CONTABLE";
       |PRINT; nPrint = 1;
       enSw = 1;
   |FINSI;
   |CIERRA #camasic@;
|FPRC;

|DEFBUCLE HistEnl_2;
   #dscam067#3;
   5;
   #dscay001#0, #dscay001#11;
   #dscay001#1, #dscay001#12;
   e;
   NULL, HistEnl;
|FIN;

|DEFBUCLE HistEnl_1;
   #dscam067#2;
   10;
   00001, 0, 00, #dscay001#11, 000001, 0001, #dscay001#0;
   99999, 9, 99, #dscay001#12, 999999, 9999, #dscay001#1;
   e;
   NULL, HistEnl;
|FIN;
|| ////////////////////////////////////////////////////////////

|PROCESO CargaDefs;
      nError = 0;
      aPath = #48#16;  |QBF aPath;

      aDef1 = aPath + "def/canempre.mas";
      |CARGA_DEF aDef1, canemp1@;
      |SI FSalida ! 0;
          aDef1  = "";
          nError = 1;
          |ACABA;
      |FINSI;

      aDef2 = aPath + "def/camaasic.mas";
      |CARGA_DEF aDef2, camasic@;
      |SI FSalida ! 0;
          aDef2  = "";
          nError = 1;
          |ACABA;
      |FINSI;

      aDef3 = aPath + "def/camaasil.mas";
      |CARGA_DEF aDef3, camasil@;
      |SI FSalida ! 0;
          aDef3  = "";
          nError = 1;
          |ACABA;
      |FINSI;

      aDef4 = aPath + "def/camaasic.mas";
      |CARGA_DEF aDef4, camasi1@;
      |SI FSalida ! 0;
          aDef4  = "";
          nError = 1;
      |FINSI;
|FPRC;

|PROCESO DesCargaDefs;
     |SI aDef4 ! ""; |DESCARGA_DEF #camasi1@; aDef4 = ""; |FINSI;
     |SI aDef3 ! ""; |DESCARGA_DEF #camasil@; aDef3 = ""; |FINSI;
     |SI aDef2 ! ""; |DESCARGA_DEF #camasic@; aDef2 = ""; |FINSI;
     |SI aDef1 ! ""; |DESCARGA_DEF #canemp1@; aDef1 = ""; |FINSI;
|FPRC;

|PROCESO ElProceso;
      |HAZ CargaDefs;
      |SI nError = 1;
          |MENAV "0000Error al Cargar fichero de contabilidad.";
          |HAZ DesCargaDefs;
          |ACABA;
      |FINSI;

      |IMPRE 0;
      |SI FSalida ! 0;
          |MENAV "0000No existe Impresora. Proceso interrumpido";
          |ACABA;
      |FINSI;

      informe = "dscay001";
      |SI #dscay001#13 = 2; informe = "dscaya01"; |FINSI;
      |INFOR informe;
      |SI FSalida ! 0;
          |MENAV "0000No existe Informe";
          |FINIMP;
          |ACABA;
      |FINSI;

      aClave = "";
      nSw    = 0;
      |ABRE #dscam030;
      |SI #dscay001#13 = 1; |BUCLE HistEnl_1; |FINSI;
      |SI #dscay001#13 = 2; |BUCLE HistEnl_2; |FINSI;

      |SI nPrint = 1;
          |SI enSw = 1;
              |HAZ FinEste;
          |FINSI;
          |PIEINF;
      |FINSI;
      |FININF;
      |FINIMP;
      |CIERRA #dscam030;
|FPRC;

|PROGRAMA;
   |CLS;
   |CABEZA " Informe Chequeo Historico enlaces Astos Contables";

   |PINPA #0#dscay001;
   |PINDA #0#dscay001;
   |ENDATOS #1#dscay001;
   |SI FSalida = 0;
       |HAZ ElProceso;
   |FINSI;
|FPRO;
