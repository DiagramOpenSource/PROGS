|FICHEROS;
   cadie001 #0;
   cadie500 #1;

   dscam003 #10;
   dscam004 #11;
   dscam044 #12;
   dscam045 #13;

   agif025@ #100;
   agif134@ #101;
|FIN;

|VARIABLES;

   &Divisa = "";

   aAlfa    = "";
   aAlfa1   = "";
   DesdeRep = "";
   HastaRep = "";
   aAntRep  = "ZZZZ";

   &nDeci_Div = 0; &nDeci_Base = 0;
   &HayConta  = 0; &HayGestion = 0;
   SwError    = 0;
   nTotalPend = 0;
   nCalc      = 0;
   nCont      = 0;
   EmpCartera = 0;

   Primero    = 0;
   Calc       = 0;
   SwNeg      = 0;
   nDecimales = 0;
   Long       = 0;

   Comodin    = "";
   Comodin1   = "";
   aDecimales = "";
   LongAlfa   = "";
   Cadena     = "";
|FIN;

|PROCESO RecibosLin;
   nTotalPend = nTotalPend - (#dscam004#44 + #dscam004#46);
|FPRC;

|DEFBUCLE RecibosLin;
#dscam004#4;
;
"RPT",#dscam003#40;
"RPT",#dscam003#40;
;
NULL,RecibosLin;
|FIN;

|PROCESO Recibos;
   |SI #dscam003#14 ! "P"; |Y #dscam003#14 ! "C"; |Y #dscam003#14 ! "T";
      |ACABA;
   |FINSI;

   |SI #dscam003#3 > #cadie001#4; |ACABA; |FINSI;

   Divisa = #dscam003#57; |HAZ TomaDecim;

   nTotalPend = #dscam003#31;
   |SI #dscam003#14 = "T";
      |SI nTotalPend = 0; |ACABA; |FINSI;
      |BUCLE RecibosLin;
   |FINSI;
   |SI nTotalPend = 0; |ACABA; |FINSI;

   |SI #cadie001#5 = "S";
      SwError = 1; |HAZ CargaNDeci;
      #agif134@#49 = #dscam003#0;
      #agif134@#0  = #dscam003#1;
      |LEE 000#agif134@.=;
      |PARA; |SI FSalida = 0; |Y #agif134@#49 = #dscam003#0; |Y #agif134@#0 = #dscam003#1; |HACIENDO;
         aAlfa  = #agif134@#1; |QBF aAlfa;  aAlfa  = aAlfa  % -104;
         aAlfa1 = #dscam003#4; |QBF aAlfa1; aAlfa1 = aAlfa1 % -104;
         |SI aAlfa = aAlfa1; SwError = 0; |SAL; |FINSI;
         |LEE 000#agif134@.s;
      |SIGUE;
      |SI SwError = 1; |DDEFECTO #agif134@; |FINSI;
      #agif025@#0 = #dscam003#33;
      |LEE 000#agif025@.=;
      |SI FSalida ! 0; |DDEFECTO #agif025@; |FINSI;
/*
      #cadie500#0  = #dscam003#40;
      |LEE 000#cadie500.=;
      |SI FSalida = 0; |BORRA 020#cadie500.a; |FINSI;
*/
      |DDEFECTO #cadie500;
      #cadie500#0  = #dscam003#40;
      |LEE 000#cadie500.=;
      |SI FSalida ! 0;
         |DDEFECTO #cadie500;
         #cadie500#0  = #dscam003#40;
         #cadie500#1  = #dscam003#0;
         #cadie500#2  = #dscam003#1;
         #cadie500#3  = #dscam003#2;
         #cadie500#4  = #dscam003#32;
         #cadie500#5  = #dscam003#6;
         #cadie500#6  = #dscam003#4;
         #cadie500#7  = #dscam003#3;
         #cadie500#8  = #cadie001#7;
         #cadie500#9  = #dscam003#30;
         #cadie500#10 = nTotalPend;
         nCalc = (#agif134@#111 * #dscam003#30) / #agif134@#101;
         #cadie500#11 = nCalc;
         |SI #agif025@#39 = 0;
            nCalc = (#cadie500#11 * #cadie500#10) / #cadie500#9;
         |SINO;
            nCalc = #cadie500#10 % #agif025@#39;
         |FINSI;
         #cadie500#12 = nCalc;
         #cadie500#13 = 0;
         #cadie500#14 = #dscam003#33;
         #cadie500#15 = #dscam003#34;
         |GRABA 020#cadie500.n;
      |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE Recibos;
#dscam003#1;
4,33,46;
000000001,#cadie001#2,DesdeRep,#dscam045#0;
999999999,#cadie001#3,HastaRep,#dscam045#0;
;
NULL,Recibos;
|FIN;

|PROCESO Listado;
   |SI aAntRep ! #cadie500#14;
      |SI Primero = 0;
         Primero = 1;
      |SINO;
         |PIEINF;
      |FINSI;
      aAntRep = #cadie500#14;
   |FINSI;
   |PRINT;
|FPRC;

|DEFBUCLE Listado;
#cadie500#2;
;
#cadie001#7,DesdeRep,"00.00.0000";
#cadie001#7,HastaRep,"31.12.9999";
;
NULL,Listado;
|FIN;

|PROCESO Selec;
   aAlfa = #cadie001#8;
   |PARA nCont = 9; |SI nCont [ 16; |HACIENDO nCont = nCont + 1;
      |C_M #cadie001#nCont#,1,aAlfa;
   |SIGUE;
   |SI aAlfa = "S"; aAlfa = "N"; |SINO; aAlfa = "S"; |FINSI;
   |C_M #cadie001#0,1,aAlfa; |C_M #cadie001#1,1,aAlfa;
|FPRC;

|PROCESO PorEmpresa;
   |SI #cadie001#8 = "N";
      DesdeRep = #cadie001#0; HastaRep = #cadie001#1;
      |BUCLE Recibos;
   |SINO;
      |PARA nCont = 9; |SI nCont [ 16; |HACIENDO nCont = nCont + 1;
         DesdeRep = #cadie001#nCont#; HastaRep = #cadie001#nCont#;
         |BUCLE Recibos;
      |SIGUE;
   |FINSI;
|FPRC;

|PROCESO BucleEmp;

   #dscam044#0 = #dscam045#0;
   |LEE 000#dscam044.=;
   |SI FSalida = 0;
      aAlfa = #dscam044#1; |QBF aAlfa;
      aAlfa1 = aAlfa + "def/agifa134.mas";
      |CARGA_DEF aAlfa1, agif134@;
      |SI FSalida = 0;
         aAlfa1 = aAlfa + "def/agifa025.mas";
         |CARGA_DEF aAlfa1, agif025@;
         aAlfa1 = aAlfa + "fich/" + (("00000" + #dscam044#0) % -105) + "/";
         |PATH_DAT #100 aAlfa1; |PATH_DAT #101 aAlfa1;
         |ABRE #agif134@; |ABRE #agif025@;
         |HAZ PorEmpresa;
         |CIERRA #agif025@; |DESCARGA_DEF #agif025@;
         |CIERRA #agif134@; |DESCARGA_DEF #agif134@;
      |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE BucleEmp;
#dscam045#2;
2;
EmpCartera,"V";
EmpCartera,"V";
;
NULL,BucleEmp;
|FIN;

|PROCESO PonComas;

   Calc = Comodin;
   |SI Calc < 0;
      Calc = 0 - Calc;
      Comodin = Calc;
      SwNeg = 1;
   |SINO;
      SwNeg = 0;
   |FINSI;

   Comodin1 = Comodin;
   Comodin1 = Comodin1 - ".";
   |SI FSalida ! 0;
      Calc = FSalida + 98;
      Comodin1 = Comodin1 % Calc;
      nDecimales = Comodin1;
      aDecimales = Comodin1; |QBF aDecimales;

      LongAlfa = aDecimales % 0; Long = LongAlfa;
      |SI Long < 2;
         Long = 2 - Long;
         aDecimales = aDecimales + ("0" * Long);
      |FINSI;
||      Comodin = Comodin - Comodin1; Comodin = Comodin - ".";
      Calc = Calc - 199; Calc = 100 + ((Calc / 100) ! 0);
      Comodin = Comodin % Calc;
   |SINO;
      nDecimales = 0;
      aDecimales = "00";
   |FINSI;

   Comodin1 = Comodin;
   Comodin = "";
   |PARA; |SI Comodin1 ! ""; |HACIENDO;
      LongAlfa = Comodin1 % 0;
      Long = LongAlfa;
      |SI Long [ 3;
         Cadena = Comodin1;
         Comodin1 = "";
         |QBF Cadena;
         Comodin = Cadena + Comodin;
      |SINO;
         Cadena = Comodin1 % -103;
         Comodin1 = Comodin1 - Cadena;
         Comodin  = "." + Cadena + Comodin;
      |FINSI;
   |SIGUE;
   |SI SwNeg = 1;
      Comodin = "-" + Comodin;
   |FINSI;
   |SI nDecimales ] 0;
      Comodin = Comodin + ",";
      ||SI nDecimales < 10; Comodin = Comodin + "0"; |FINSI;
      Comodin = Comodin + aDecimales;
      ||Comodin = Comodin + nDecimales;
   |FINSI;
|FPRC;

|PROGRAMA;
   |HAZ Tipo8;

   |ABRE #cadie001;
   |LEE 000#cadie001.p;
   |SI FSalida ! 0;
      |DDEFECTO #cadie001;
      |GRABA 020#cadie001.c;
   |FINSI;

   |CLS;
   |CABEZA "Listado retrocesion comisiones recibos vencidos";
   |PINPA #0#0; |PINDA #0#0;
   |ENDATOS #1#0;
   |SI FSalida = 0;
      |GRABA 020#cadie001.a;
      |IMPRE 0;
      |SI FSalida ! 0; |ACABA; |FINSI;
      |INFOR "cadie001";
      |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

      |DFICB aAlfa; |PATH_DAT #12 aAlfa; |PATH_DAT #13 aAlfa;
      |ABRE #cadie500; |ABRE #dscam044;
      |SI #cadie001#6 = "N";
         EmpCartera = #48#0; |BUCLE BucleEmp;
      |FINSI;
      |SI #cadie001#8 = "N";
         DesdeRep = #cadie001#0; HastaRep = #cadie001#1;
         |BUCLE Listado;
      |SINO;
         |PARA nCont = 9; |SI nCont [ 16; |HACIENDO nCont = nCont + 1;
            DesdeRep = #cadie001#nCont#; HastaRep = #cadie001#nCont#;
            |BUCLE Listado;
         |SIGUE;
      |FINSI;

      |PIEINF; |FININF;
      |CIERRA #dscam044; |CIERRA #cadie500;
      |FINIMP;
   |FINSI;

   |CIERRA #cadie001;
   |HAZ Tipo9;
|FPRO;
