|FICHEROS;
     dscaz221 #0;
     dscam073 #73;  || Datos Ordenante
     dscam049 #49;  || Confirming Cabeceras
     dscam050 #50;  || Confirming Lineas
     dscam008 #8;   || Mantenimiento pagos
     dscam030 #30;  || Mantenimiento enlaces
     dscaw013 #13;  || Temporal enlaces
     dscam051 #51;  || Parametros generales
     dscam014 #14;  || Bancos..............
     caclipro@ #100;
|FIN;

|VARIABLES;
     &aInforme = "";
     &eaNombre1 = "";
     &eaOrigen = "";
     &HayConta = 0;

     &Divisa = "";
     &Niv1 = 0;
     &Niv2 = 0;
     &Niv3 = 0;
     &Niv4 = 0;
     &Niv5 = 0;
     &Niv6 = 0;

     &eaCodigo  = "";
     &eaTipoCod = "";
     &aCuenta   = "";

     &eaNif       = "";
     &eaNombre    = "";
     &eaApCorreos = "";
     &eaDireccion = "";
     &eaPoblacion = "";
     &eaProvincia = "";
     &eaCP        = "";
     &eaTelefono  = "";
     &eaFax       = "";

     &eaNifProv = "";
     &eaDirProv = "";

     nError = 0;
     menR1 = "0000No Existe Confirme !!!";
     menR2 = "0000Confirme YA contabilizado ";
     menR3 = "0000SConfirme YA Emitido en Papel. Aceptar S/N ";
     menR4 = "0000NConfirme YA lanzado en Diskette. Aceptar S/N ";
     mens = "";
     Comodin = "";
     nCalc = 0;
     NifProv = "";
     NombreProv = "";
     DirecProv  = "";
     PoblaProv  = "";
     ProviProv  = "";
     Cp1Prov    = "";
     Cp2Prov    = "";
     TelefProv  = "";
     FaxProv    = "";
     PaisProv   = "";
     Entidad    = "";
     Sucursal   = "";
     DC         = "";
     Cuenta     = "";
     aRef       = "";
     fFechaEmis = @;
     aCuentaCt = "";
     nNivel = 0;

     aAlfa = "";
     aPath = "";
     nHandle = 0;
     nSwImpre = 0;
     nRecibos = 0;
     nReg = 0;
     nImporte = 0;
     nPos = 0;
     nLon = 0;
     aFinReg = "";
     i = 0;
     aValor = "";
     aLon = "";
     aCar = "";
     aDato = "";
     aDatoCab = "";
     aDatoDeta = "";
     nImpo = 0;
|FIN;

|PROCESO Cabecera;
     aDato = "13";                 ||registro

     |SI #49#23 = "T";
          aDato = aDato + "60";    ||operacion
     |SINO;
          aDato = aDato + "70";    ||operacion
     |FINSI;

     aAlfa = #73#2; |HAZ QBI; |QBF aAlfa;
     aAlfa = (aAlfa + "          ") % 110;
     aDato = aDato + aAlfa;        ||cod. ordenante

     aAlfa = #73#26; |QBT aAlfa;
     aAlfa = (("000") + aAlfa) % -103;
     aDato = aDato + aAlfa;        ||sufijo cliente

     aAlfa = "         ";
     aDato = aDato + aAlfa;        ||libre

     aDatoCab = aDato;

     ||------------
     aDato = aDatoCab + "001";     ||n§ dato

     aAlfa = (#0#3 % 102) + (#0#3 % 402) + (#0#3 % 704);
     aDato = aDato + aAlfa;        ||fecha envio soporte

     aAlfa = (#0#15 % 102) + (#0#15 % 402) + (#0#15 % 704);
     aDato = aDato + aAlfa;        ||fecha emision ordenes

     aAlfa = #73#4; |QBT aAlfa;
     aAlfa = (("0000") + aAlfa) % -104;
     aDato = aDato + aAlfa;        ||Entidad

     aAlfa = #73#5; |QBT aAlfa;
     aAlfa = (("0000") + aAlfa) % -104;
     aDato = aDato + aAlfa;        ||Sucursal

     aAlfa = #73#6; |QBT aAlfa;
     aAlfa = (("0000000000") + aAlfa) % -110;
     aDato = aDato + aAlfa;        ||Cuenta

     |SI #49#23 = "T";
          aDato = aDato + " ";     ||Detalle
     |SINO;
          aDato = aDato + "0";     ||Detalle
     |FINSI;

     |SI #49#23 = "T";
          aDato = aDato + "  ";    ||Clave
     |SINO;
          aAlfa = #0#5; |QBT aAlfa;
          aAlfa = (("00") + aAlfa) % -102;
          aDato = aDato + aAlfa;   ||Clave?
     |FINSI;

     aDato = aDato + " ";          ||Libre

     aAlfa = #73#22; |QBT aAlfa;
     aAlfa = (("00") + aAlfa) % -102;
     aDato = aDato + aAlfa;        ||DC

     aDato = aDato + "   ";        ||Libre
     |HAZ Graba;

     ||------------
     aDato = aDatoCab + "002";     ||n§ dato

     aAlfa = #73#3; |HAZ QBI;
     aAlfa = (aAlfa + (" " * 36)) % 136;
     aDato = aDato + aAlfa;        ||nombre

     aDato = aDato + "       ";    ||libre
     |HAZ Graba;

     ||------------
     aDato = aDatoCab + "003";     ||n§ dato

     aAlfa = #73#7; |HAZ QBI;
     aAlfa = (aAlfa + (" " * 36)) % 136;
     aDato = aDato + aAlfa;        ||domicilio

     aDato = aDato + "       ";    ||libre
     |HAZ Graba;

     ||------------
     aDato = aDatoCab + "004";     ||n§ dato

     aAlfa = #73#10; |HAZ QBI;
     aAlfa = (aAlfa + (" " * 36)) % 136;
     aDato = aDato + aAlfa;        ||plaza

     aDato = aDato + "       ";    ||libre
     |HAZ Graba;
|FPRC;

|PROCESO Detalle;
     |HAZ LeeProveedor;

     aDato = "16";                 ||registro

     |SI #49#23 = "T";
          aDato = aDato + "60";    ||operacion
     |SINO;
          aDato = aDato + "70";    ||operacion
     |FINSI;

     aAlfa = #73#2; |HAZ QBI; |QBF aAlfa;
     aAlfa = (aAlfa + "          ") % 110;
     aDato = aDato + aAlfa;        ||cod. ordenante

     aAlfa = NifProv; |HAZ QBI; |QBF aAlfa;
     aAlfa = (aAlfa + "            ") % 112;
     aDato = aDato + aAlfa;        ||cif beneficiario

     aDatoDeta = aDato;

     ||------------
     aDato = aDatoDeta + "010";     ||n§ dato

     nImpo = (#50#20 * 100) ! 0;
     |SI nImpo < 0; nImpo = -nImpo; |FINSI;
     aAlfa = (("0" * 12) + nImpo) % -112;
     aDato = aDato + aAlfa;         ||importe

     aAlfa = Entidad; |QBT aAlfa;
     aAlfa = (("0000") + aAlfa) % -104;
     aDato = aDato + aAlfa;        ||Entidad

     aAlfa = Sucursal; |QBT aAlfa;
     aAlfa = (("0000") + aAlfa) % -104;
     aDato = aDato + aAlfa;        ||Sucursal

     aAlfa = Cuenta; |QBT aAlfa;
     aAlfa = (("0000000000") + aAlfa) % -110;
     aDato = aDato + aAlfa;        ||Cuenta

     |SI #49#23 = "T";
          ||aDato = aDato + "1";     ||gastos por cuenta ordenante ?
          aDato = aDato + "2";     ||gastos por cuenta beneficiario
     |SINO;
          aDato = aDato + " ";     ||gastos
     |FINSI;

     |SI #49#23 = "T";
          aDato = aDato + " ";     ||f.pago
     |SINO;
          ||aDato = aDato + "T";     ||f.pago transferencia
          aDato = aDato + "C";     ||f.pago cheque ?
     |FINSI;

     aDato = aDato + " ";         || libre

     aAlfa = DC; |QBT aAlfa;
     aAlfa = (("00") + aAlfa) % -102;
     aDato = aDato + aAlfa;        ||DC

     aDato = aDato + "        ";  || libre
     |HAZ Graba;

     ||------------
     aDato = aDatoDeta + "011";     ||n§ dato

     aAlfa = NombreProv; |HAZ QBI;
     aAlfa = (aAlfa + (" " * 36)) % 136;
     aDato = aDato + aAlfa;        ||nombre

     aDato = aDato + "       ";    ||libre
     |HAZ Graba;

     ||------------
     aDato = aDatoDeta + "012";     ||n§ dato

     aAlfa = DirecProv; |HAZ QBI; |QBF aAlfa;
     aLon = aAlfa % A0;
     aAlfa = (aAlfa + (" " * 36)) % 136;
     aDato = aDato + aAlfa;        ||direccion

     aDato = aDato + "       ";    ||libre
     |HAZ Graba;

     |SI aLon > 36;
          aDato = aDatoDeta + "013";     ||n§ dato

          aAlfa = DirecProv % 37;
          aAlfa = (aAlfa + (" " * 36)) % 136;
          aDato = aDato + aAlfa;        ||direccion2

          aDato = aDato + "       ";    ||libre
          |HAZ Graba;
     |FINSI;

     ||------------
     aDato = aDatoDeta + "014";     ||n§ dato

     aAlfa = (("00" + Cp1Prov) % -102) + (("000" + Cp2Prov) % -103) + PoblaProv
     aAlfa = (aAlfa + (" " * 36)) % 136;
     aDato = aDato + aAlfa;        ||cp+plaza

     aDato = aDato + "       ";    ||libre
     |HAZ Graba;

     ||------------
     aDato = "17";                ||n§ dato

     |SI #49#23 = "T";
          aDato = aDato + "60";   ||operacion
     |SINO;
          aDato = aDato + "70";   ||operacion
     |FINSI;

     aAlfa = #73#2; |HAZ QBI; |QBF aAlfa;
     aAlfa = (aAlfa + "          ") % 110;
     aDato = aDato + aAlfa;        ||cod. ordenante

     aAlfa = NifProv; |HAZ QBI; |QBF aAlfa;
     aAlfa = (aAlfa + "            ") % 112;
     aDato = aDato + aAlfa;        ||cif beneficiario

     aDato = aDato + "100";        ||n§ dato

     aAlfa = (#8#4 % 102) + (#8#4 % 402) + (#8#4 % 704);
     aDato = aDato + aAlfa;        ||fecha emision

     |SI #49#23 = "T";
          aAlfa = ("        " + #8#40) % -108; ||referencia factura
     |SINO;
          aAlfa = (#8#3 % 102) + (#8#3 % 402) + (#8#3 % 704);
          aDato = aDato + aAlfa;        ||fecha vto
     |FINSI;

     aAlfa = ((" " * 14) + #8#40) % -114;
     aDato = aDato + aAlfa;        ||numero factura

     nImpo = (#50#20 * 100) ! 0;
     |SI nImpo < 0; nImpo = -nImpo; |FINSI;
     aAlfa = (("0" * 12) + nImpo) % -112;
     aDato = aDato + aAlfa;        ||importe

     |SI #50#20 < 0;
          aDato = aDato + "-";     ||signo
     |SINO;
          aDato = aDato + " ";     ||signo
     |FINSI;

     |HAZ Graba;

     ||------------
     nRecibos = nRecibos + 1;
     nImporte = nImporte + #dscam050#20;

     eaNifProv = NifProv;
     eaDirProv = DirecProv;
     |SI nSwImpre = 1; |PRINT; |FINSI;
|FPRC;

|PROCESO Final;
     nReg = nReg + 1;

     aDato = "18";                ||n§ dato

     |SI #49#23 = "T";
          aDato = aDato + "60";   ||operacion
     |SINO;
          aDato = aDato + "70";   ||operacion
     |FINSI;

     aAlfa = #73#2; |HAZ QBI; |QBF aAlfa;
     aAlfa = (aAlfa + "          ") % 110;
     aDato = aDato + aAlfa;        ||cod. ordenante

     aDato = aDato + "            "; ||libre

     aDato = aDato + "   ";        ||libre

     nImpo = (nImporte * 100) ! 0;
     aAlfa = (("0" * 12) + nImpo) % -112;
     aDato = aDato + aAlfa;        ||importe total

     aAlfa = (("0" * 8) + nRecibos) % -108;
     aDato = aDato + aAlfa;        ||registros individuales

     aAlfa = (("0" * 10) + nReg) % -110;
     aDato = aDato + aAlfa;        ||registros totales

     aDato = aDato + "      "; ||libre

     aDato = aDato + "       "; ||libre
     |HAZ Graba;
|FPRC;

|PROCESO Graba;
     aDato = aDato + aFinReg;
     |XGRABA nHandle, aDato;
     nReg = nReg + 1;
|FPRC;

|PROCESO QBI; || quita blancos a izquierda
     aValor = "";
     aLon = aAlfa % A0;
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aCar = aAlfa % nPos;
          |SI aCar ! " "; |O aValor ! "";
               aValor = aValor + aCar;
          |FINSI;
     |SIGUE;
     aAlfa = aValor;
|FPRC;

|PROCESO SacaNivel1;
     |QBF aCuentaCt;
     aAlfa = aCuentaCt % 0; nNivel = aAlfa;
     |SI nNivel = Niv1; nNivel = 1; |FINSI;
     |SI nNivel = Niv2; nNivel = 2; |FINSI;
     |SI nNivel = Niv3; nNivel = 3; |FINSI;
     |SI nNivel = Niv4; nNivel = 4; |FINSI;
     |SI nNivel = Niv5; nNivel = 5; |FINSI;
     |SI nNivel = Niv6; nNivel = 6; |FINSI;
|FPRC;

|PROCESO LeeProveedor;
     NombreProv = "";
     DirecProv  = "";
     PoblaProv  = "";
     ProviProv  = "";
     Cp1Prov    = "";
     Cp2Prov    = "";
     TelefProv  = "";
     FaxProv    = "";
     PaisProv   = "";
     Entidad    = "";
     Sucursal   = "";
     DC         = "";
     Cuenta     = "";
     aRef = " " * 12;

     |DDEFECTO #dscam008;
     #dscam008#40 = #dscam050#13;
     |LEE 001#dscam008.=;

     fFechaEmis = #dscam008#4;

     eaCodigo = #dscam008#32; eaTipoCod = "P"; aCuenta = #dscam008#5;
     |HAZ LeeDatos;

     #caclipro@#23 = "P";
     #caclipro@#10 = #dscam008#5;
     aCuentaCt = #dscam008#5; |HAZ SacaNivel1;
     #caclipro@#11 = nNivel;
     |LEE 000#caclipro@.=;
     |SI FSalida = 0;
          NifProv    = eaNif;
          NombreProv = eaNombre;
          DirecProv  = eaDireccion;
          PoblaProv  = eaPoblacion;
          ProviProv  = eaProvincia;
          Cp1Prov    = eaCP % 0102;
          Cp2Prov    = eaCP % 0303;
          TelefProv  = eaTelefono;
          FaxProv    = eaFax;
          PaisProv   = #caclipro@#24;
          |SI #dscaz221#4 = "1"; aRef = #caclipro@#10; |FINSI;
          |SI #dscaz221#4 = "2"; aRef = #caclipro@#0;  |FINSI;
          |QBF aRef; aRef = ((" " * 12) + aRef) % -112;

          aAlfa = "|NC"; nCalc = #caclipro@#aAlfa#;
          |SI nCalc > 29;
               Entidad  = #caclipro@#29;
               Sucursal = #caclipro@#30;
               DC       = #caclipro@#31;
               Cuenta   = #caclipro@#32;
          |SINO;
               aAlfa    = #caclipro@#18; |QBF aAlfa;
               Entidad  = aAlfa % 0104;
               Sucursal = aAlfa % 0504;
               DC       = aAlfa % 0902;
               Cuenta = #caclipro@#17; |QBF Cuenta;
               Cuenta = ("0000000000" + Cuenta) % -110;
          |FINSI;
     |SINO;
          aAlfa = "    No se encuentran proveedor '" + #dscam008#5 + "'";
          aAlfa = aAlfa + " en contabilidad";
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO Secuencial; ||TIPO 3;
     aFinReg = &13 + &10;

     |DDEFECTO #dscam073;
     |ABRE #dscam073;
     #dscam073#0 = #dscaz221#2;
     |LEE 000#dscam073.=;
     |CIERRA #dscam073;

     |ABRE #dscam049;
     |HAZ leerem;
     |SI nError = 1; |O nError = 2;
          |MENAV "    ERROR !!! CONFIRME NO CORRECTO ";
          |ACABA;
     |FINSI;

     aPath = #48#16; |QBF aPath;
     aAlfa = aPath + "def/caclipro.mas";
     |CARGA_DEF aAlfa, caclipro@;
     |SI FSalida ! 0;
          |MENAV "    Problemas para abrir el fichero de clientes/proveedores de contabilidad";
          |ERROR; |ACABA;
     |FINSI;
     aAlfa = aPath + "fich/" + (("00000" + #48#14) % -105) + #48#19 + "/";
     |PATH_DAT #caclipro@#aAlfa#;
     |ABRE #caclipro@;

     |DFICE aAlfa;
     aAlfa = aAlfa + "casecmin.seq";
     |XABRE "WB", aAlfa, nHandle;
     |SI FSalida ] 0;
          |HAZ Cabecera;
          |ABRE #dscam008;
          |BUCLELIN 0Detalle#dscam049;
          |CIERRA #dscam008;
          |HAZ Final;
          |XCIERRA nHandle;

          #dscam049#7 = "D";
          |GRABA 020#dscam049.a;
     |FINSI;
     |CIERRA #dscam049;

     |CIERRA #caclipro@;
     |DESCARGA_DEF #caclipro@;


     |SI HayConta = 1; |Y #dscam049#7 = "N"; |Y #dscaz221#14 ! "    ";
          |ABRE #dscam030;
          #dscam030#0 = #dscaz221#14;
          |LEE 000#dscam030.=;
          |SI FSalida = 0;
               Comodin = Usuario; |QBF Comodin; Comodin = "ac" + Comodin;
               |NOME_DAT #dscaw013#Comodin#;
               |DELINDEX #dscaw013; |ABRE #dscaw013;
               |DDEFECTO #dscaw013;
               #dscaw013#0  = 1;            #dscaw013#15 = #dscaz221#14;
               #dscaw013#5  = #dscam049#0;  #dscaw013#6  = #dscam049#0;
               #dscaw013#11 = "N";          #dscaw013#12 = 5;
               #dscaw013#13 = "dscam050";   #dscaw013#14 = 0;
               |GRABA 020#dscaw013.n;
               |CIERRA #dscaw013;

               Comodin = #dscaz221#14;
               Comodin = "dscam033.tab;" + Comodin + ",E";
               |SUB_EJECUTA_NP Comodin;
          |FINSI;
     |FINSI;

     |CAMPO_VISUAL #dscaz221#10,1,"S";
     |CAMPO_VISUAL #dscaz221#11,1,"S";
     |CAMPO_VISUAL #dscaz221#12,1,"S";

     |D_CUADRO 1261, 2280;
     |ATRI R;
     |PINTA 1362, " Numero Recibos   ";
     |PINTA 1562, " Numero Registros ";
     |PINTA 1762, " Importe Total    ";
     |ATRI 0;
     |PINTA 1462, "                  ";
     |PINTA 1662, "                  ";
     |PINTA 1862, "                  ";
     |ATRI 0;

     #dscaz221#10 = nRecibos;
     #dscaz221#11 = nReg - 1;
     #dscaz221#12 = nImporte;

     |PINTA #dscaz221#10;
     |PINTA #dscaz221#11;
     |PINTA #dscaz221#12;

     |DFICE eaOrigen; |QBF eaOrigen;
     eaNombre1 = "casecmin.seq";
     |SUB_EJECUTA "dscaz032.tab";
|FPRC;

|PROCESO LeeOrdenante;
     |DDEFECTO #dscam073;
     |ABRE #dscam073;
     #dscam073#0 = #dscaz221#2;
     |LEE 000#dscam073.=;
     |CIERRA #dscam073;

     #dscaz221#5 = #dscam073#23; |PINTA #dscaz221#5;
     #dscaz221#13 = #dscam073#24; |PINTA #dscaz221#13;
|FPRC;

|PROCESO leerem;
     nError = 0;
     |DDEFECTO #dscam049;
     #dscam049#0 = #dscaz221#1;
     |LEE 001#dscam049.=;
     |SI FSalida ! 0;
         nError = 1; mens = menR1;
     |SINO;
         Divisa = #dscam049#16; |HAZ TomaDecim;
         |SI #dscam049#8 = "S";
             nError = 2; mens = menR2;
         |SINO;
             |SI #dscam049#7 = "S"; nError = 3; mens = menR3; |FINSI;
             |SI #dscam049#7 = "D"; nError = 4; mens = menR4; |FINSI;
         |FINSI;
         |ABRE #dscam014;
         #dscam014#0 = #dscam049#3;
         |LEE 000#dscam014.=;
         |CIERRA #dscam014;
     |FINSI;
|FPRC;

|PROCESO ver_confirming; |TIPO 6;
     |SI FSalida = 2; |ACABA; |FINSI;

     |ABRE #dscam049;
     |HAZ leerem;
     |CIERRA #dscam049;
     |SI nError ! 0;
         |SI nError < 3;
             |MENAV mens;
             |ERROR;
             |ACABA;
         |SINO;
             |CONFI mens;
             |SI FSalida ! 0;
                 |ERROR;
                 |ACABA;
             |FINSI;
         |FINSI;
     |FINSI;

     #dscaz221#2 = #dscam049#3; || Banco
     |HAZ LeeOrdenante;

     |PINTA 0935, #dscam049#2;   || Vto.
     |PINTA 0958, #dscam049#5;   || N§ Efectos
     |PINTA 1035, #dscam049#3;   || Banco
     |PINTA 1049, #dscam049#19;  || Importe Total
|FPRC;

|PROCESO MiraParam; |TIPO 7;
     |SI #dscam051#1 = "S";
          Comodin = #dscam051#61;
          |C_M #dscaz221#14,1,Comodin;
     |SINO;
          |C_M #dscaz221#14,1,"N";
     |FINSI;
|FPRC;

|PROGRAMA;
     |HAZ Tipo8;

     |ABRE #dscam051; |LEE 000#dscam051.p; |CIERRA #dscam051;

     |SI #dscam051#1 = "S";
          #dscaz221#14 = #dscam051#29;
     |SINO;
          #dscaz221#14 = "";
     |FINSI;

     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |CONFI "    NImprimir carta confirme ?";
          |SI FSalida = 0; nSwImpre = 1; |SINO; nSwImpre = 0; |FINSI;
          |SI nSwImpre = 0;
               |HAZ Secuencial;
          |SINO;
               |IMPRE 0;
               |SI FSalida = 0;
                    aInforme = "Carta emision de confirmes";
                    |HAZ ObtenInforme;
                    |INFOR aInforme;
                    |SI FSalida = 0;
                         |HAZ Secuencial;
                         |PIEINF;
                         |FININF;
                    |SINO;
                         aInforme = "0000Error en informe '" + aInforme + "'";
                         |MENAV aInforme;
                    |FINSI;
                    |FINIMP;
               |FINSI;
          |FINSI;
     |FINSI;
     |HAZ Tipo9;
|FPRO;
