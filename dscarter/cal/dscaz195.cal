|FICHEROS;
   dscaz195 #0;

   dscaw182 #10;
   dscam008 #11;
   dscam003 #12;

   catalm01@ #100;
   canempre@ #101;
   cacuenta@ #102;
   dscam000@ #103;
|FIN;

|VARIABLES;
   aAlfa   = "";
   aCuenta = "";
   aParte1 = "";
   aParte2 = "";
   aLong   = "";
   &aInforme = "";

   fFecha  = @;

   nEmpCont    = 0;
   nEmpCart    = 0;
   nCont       = 0;

   nAnyo       = 0;
   nLong       = 0;
   nCalc       = 0;
   nParte1     = 0;
   nParte2     = 0;
   nTecla      = 0;
   nSwCont     = 0;
   nSwHayDatos = 0;
   nMaxDigit   = 0;
   nMaxNivel   = 0;
|FIN;

|PROCESO RecogeDatosCont;
   |SI #catalm01@#8 = "S"; |ACABA; |FINSI;

   |DDEFECTO #dscaw182;
   #dscaw182#0  = #catalm01@#18;
   #dscaw182#1  = #catalm01@#0;
   #dscaw182#7  = #catalm01@#1;
   #dscaw182#8  = #catalm01@#14;
   #dscaw182#9  = #catalm01@#5;
   #dscaw182#10 = #catalm01@#6;
   #dscaw182#15 = #catalm01@#16;
   #dscaw182#16 = #catalm01@#17;
   #dscaw182#17 = 0-#catalm01@#9;
   #dscaw182#18 = 0-#catalm01@#9;
   #dscaw182#22 = "A";
   |GRABA 020#dscaw182.n;
   nSwHayDatos = 1;
|FPRC;

|DEFBUCLE RecogeDatosCont;
#catalm01@#1002;
1, 16, 5, 6;
nEmpCart, 000000, #0#3, #0#5, #0#7, #0#9;
nEmpCart, 999999, #0#4, #0#6, #0#8, #0#10;
;
NULL, RecogeDatosCont;
|FIN;

|PROCESO RecogeCarteraCmp;
   |SI #dscam008#73 ! "01.01.0000";
      |SI #dscam008#73 < #dscaz195#9; |O #dscam008#73 > #dscaz195#10; |ACABA; |FINSI;
   |SINO;
      |SI #dscam008#3  < #dscaz195#9; |O #dscam008#3  > #dscaz195#10; |ACABA; |FINSI;
   |FINSI;;
   |SI #dscam008#14 = "A"; |O #dscam008#14 = "D"; |ACABA; |FINSI;
   |SI #dscam008#4  < #dscaz195#7; |O #dscam008#4  > #dscaz195#8; |ACABA; |FINSI;
   |SI #dscam008#12 < #dscaz195#5; |O #dscam008#12 > #dscaz195#6; |ACABA; |FINSI;

   |DDEFECTO #dscaw182;
   #dscaw182#0  = nEmpCart;
   #dscaw182#2  = #dscam008#40;
   #dscaw182#3  = #dscam008#27;
   #dscaw182#4  = #dscam008#0;
   #dscaw182#5  = #dscam008#1;
   #dscaw182#6  = #dscam008#2;
   #dscaw182#7  = #dscam008#5;
   #dscaw182#8  = #dscam008#6;
   #dscaw182#9  = #dscam008#4;
   #dscaw182#10 = #dscam008#3;
   #dscaw182#11 = #dscam008#26;
   #dscaw182#12 = #dscam008#14;
   #dscaw182#13 = #dscam008#15;
   #dscaw182#14 = #dscam008#17;
   #dscaw182#15 = #dscam008#12;
   #dscaw182#16 = #dscam008#63;
   #dscaw182#17 = 0-#dscam008#25;
   |SI #dscam008#74 = "S"; |Y #dscam008#14 = "E";
      #dscaw182#18 = 0-#dscam008#25;
   |SINO;
      #dscaw182#18 = 0-#dscam008#31;
   |FINSI;
   #dscaw182#19 = #dscam008#39;
   #dscaw182#20 = #dscam008#47;
   #dscaw182#21 = #dscam008#73;
   #dscaw182#22 = "P";
   |GRABA 020#dscaw182.n;

   nSwHayDatos = 1;
|FPRC;

|DEFBUCLE RecogeCarteraCmp;
#dscam008#6;
;
#0#3, "      ", "01.01.0000";
#0#4, "zzzzzz", "31.12.9999";
;
NULL, RecogeCarteraCmp;
|FIN;

|PROCESO RecogeCarteraVnt;
   |SI #dscam003#14 = "T"; |O #dscam003#14 = "F";
      |SI #dscam003#71 < #dscaz195#9; |O #dscam003#71 > #dscaz195#10; |ACABA; |FINSI;
   |SINO;
      |SI #dscam003#3  < #dscaz195#9; |O #dscam003#3  > #dscaz195#10; |ACABA; |FINSI;
   |FINSI;
/* (1)
   |SI #dscam003#71 ! "01.01.0000";
      |SI #dscam003#71 < #dscaz195#9; |O #dscam003#71 > #dscaz195#10; |ACABA; |FINSI;
   |SINO;
      |SI #dscam003#3  < #dscaz195#9; |O #dscam003#3  > #dscaz195#10; |ACABA; |FINSI;
   |FINSI;
*/
   |SI #dscam003#14 = "A"; |O #dscam003#14 = "D"; |ACABA; |FINSI;
   |SI #dscam003#4  < #dscaz195#7; |O #dscam003#4  > #dscaz195#8; |ACABA; |FINSI;
   |SI #dscam003#12 < #dscaz195#5; |O #dscam003#12 > #dscaz195#6; |ACABA; |FINSI;
   |SI #dscam003#14 ! "P"; |Y #dscam003#14 ! "C"; |Y #dscam003#14 ! "I";
    |Y #dscam003#14 ! "T"; |Y #dscam003#14 ! "F";
      |ACABA;
   |FINSI;

   |DDEFECTO #dscaw182;
   #dscaw182#0  = nEmpCart;
   #dscaw182#2  = #dscam003#40;
   #dscaw182#3  = #dscam003#27;
   #dscaw182#4  = #dscam003#0;
   #dscaw182#5  = #dscam003#1;
   #dscaw182#6  = #dscam003#2;
   #dscaw182#7  = #dscam003#5;
   #dscaw182#8  = #dscam003#6;
   #dscaw182#9  = #dscam003#4;
/* (1)
   |SI #dscam003#71 ! "01.01.0000";
      #dscaw182#10 = #dscam003#71;
   |SINO;
      #dscaw182#10 = #dscam003#3;
   |FINSI;
*/
   |SI #dscam003#14 = "T"; |O #dscam003#14 = "F";
      #dscaw182#10 = #dscam003#71;
   |SINO;
      #dscaw182#10 = #dscam003#3;
   |FINSI;


   #dscaw182#11 = #dscam003#26;
   #dscaw182#12 = #dscam003#14;
   #dscaw182#13 = #dscam003#15;
   #dscaw182#14 = #dscam003#17;
   #dscaw182#15 = #dscam003#12;
   #dscaw182#16 = #dscam003#85;
   #dscaw182#17 = #dscam003#30;

   |SI #dscam003#47 = "S"; |Y #dscam003#14 = "E";
      #dscaw182#18 = #dscam003#30;
   |SINO;
      #dscaw182#18 = #dscam003#31;
   |FINSI;
   aAlfa = #dscam003#72; |QBF aAlfa;
   aAlfa = aAlfa + #dscam003#70; |QBF aAlfa;
   |SI aAlfa ! "";
      #dscaw182#19 = "T";
      #dscaw182#20 = "Talon/Pagare";
   |SINO;
      |SI #dscam003#17 ! 0;
         #dscaw182#19 = "R";
         #dscaw182#20 = "Remesado";
      |FINSI;
   |FINSI;
   #dscaw182#21 = #dscam003#71;
   #dscaw182#22 = "C";
   aAlfa = #dscam003#72; |QBF aAlfa;
   aAlfa = aAlfa + #dscam003#70; |QBF aAlfa;
   |SI aAlfa ! "";
      aAlfa = #dscam003#72 + "/" + #dscam003#70;
      #dscaw182#23 = aAlfa;
   |FINSI;
   |GRABA 020#dscaw182.n;

   nSwHayDatos = 1;
|FPRC;

|DEFBUCLE RecogeCarteraVnt;
#dscam003#4;
;
#0#1, 0000000001;
#0#2, 9999999999;
;
NULL, RecogeCarteraVnt;
|FIN;

|PROCESO Imprime;
   |SI #dscaw182#0 ! 0;
      |DFICB aAlfa; |QBF aAlfa;
      aAlfa = aAlfa + (("00000" + #dscaw182#0) % -105) + "/";
      |SI #dscaw182#22 = "C";
         |PATH_DAT #dscam003#aAlfa#;
         |ABRE #dscam003;
         #dscam003#40 = #dscaw182#2;
         |LEE 000#dscam003.=;
         |SI FSalida ! 0; |DDEFECTO #dscam003; |FINSI;
         |CIERRA #dscam003;
      |FINSI;
      |SI #dscaw182#22 = "P";
         |PATH_DAT #dscam008#aAlfa#;
         |ABRE #dscam008;
         #dscam008#40 = #dscaw182#2;
         |LEE 000#dscam008.=;
         |SI FSalida ! 0; |DDEFECTO #dscam008; |FINSI;
         |CIERRA #dscam008;
      |FINSI;
   |FINSI;

   |PRINT;
|FPRC;

|DEFBUCLE Imprime;
#dscaw182#1;
;
INICIO;
FINAL;
;
NULL, Imprime;
|FIN;

|PROCESO MiraEmpCont;
   nTecla = FSalida;

   aAlfa = #48#16; |QBF aAlfa;
   aAlfa = aAlfa + "def/canempre.mas";
   |CARGA_DEF aAlfa, canempre@;
   aAlfa = #48#16; |QBF aAlfa;
   aAlfa = aAlfa + "fich/"; |PATH_DAT #canempre@#aAlfa#;
   |ABRE #canempre@;
   |SI nTecla = 10;
      |CONSULTA_CLAVE #1#canempre@;
      |SI FSalida = 0;
         #0#11 = #canempre@#2; |PINTA #0#11;
         |ERROR;
      |FINSI;
   |FINSI;
   #canempre@#2 = #0#11;
   #canempre@#3 = 0;
   |LEE 000#canempre@.];
   |SI FSalida ! 0; |O #canempre@#2 ! #0#11;
      |MENAV "    Empresa inexistente";
      |ERROR;
   |SINO;
      fFecha = ~; aAlfa = fFecha; |QBF aAlfa;
      aAlfa = aAlfa % 0704; nAnyo = aAlfa;
      |SI nAnyo ] 2000;
         nAnyo = nAnyo - 2000;
      |SINO;
         nAnyo = nAnyo - 1900;
      |FINSI;
      |PARA; |SI FSalida = 0; |Y #canempre@#2 = #0#11; |HACIENDO;
         |SI nAnyo = #canempre@#26;
            nAnyo = #canempre@#3;
            #dscaz195#12 = #canempre@#1; |PINTA #dscaz195#12;

            nMaxDigit = 0; nMaxNivel = 0;
            |SI #canempre@#14 ] nMaxDigit; nMaxNivel = 1; nMaxDigit = #canempre@#14; |FINSI;
            |SI #canempre@#15 ] nMaxDigit; nMaxNivel = 2; nMaxDigit = #canempre@#15; |FINSI;
            |SI #canempre@#16 ] nMaxDigit; nMaxNivel = 3; nMaxDigit = #canempre@#16; |FINSI;
            |SI #canempre@#17 ] nMaxDigit; nMaxNivel = 4; nMaxDigit = #canempre@#17; |FINSI;
            |SI #canempre@#18 ] nMaxDigit; nMaxNivel = 5; nMaxDigit = #canempre@#18; |FINSI;
            |SI #canempre@#19 ] nMaxDigit; nMaxNivel = 6; nMaxDigit = #canempre@#19; |FINSI;
            |SAL;
         |FINSI;

         |LEE 000#canempre@.s;
      |SIGUE;
   |FINSI;
   |CIERRA #canempre@; |DESCARGA_DEF #canempre@;
|FPRC;

|PROCESO MiraCuenta;
   nTecla = FSalida;

   aCuenta = #dscaz195#Campo#; |QBF aCuenta;

   || Primero filtro los posibles puntos de la cuenta contable
   aCuenta = aCuenta - ".";
   |PARA; |SI FSalida ! 0; |HACIENDO;
      nParte1 = ((FSalida / 100) ! 0) + 99; aParte1 = aCuenta % nParte1;
      nParte2 = FSalida + 98;               aParte2 = aCuenta % nParte2;
      aLong = aParte1 % 0; nLong = aLong; nCalc = nLong;
      aLong = aParte2 % 0; nLong = aLong; nCalc = nCalc + nLong;
      nCalc = nMaxDigit - nCalc;
      aCuenta = aParte1 + ("0" * nCalc) + aParte2;
      aCuenta = aCuenta - ".";
   |SIGUE;

   || Y ahora presento el lineal de seleccion (en caso de que se haya invocado)
   |SI nTecla = 10;
      aAlfa = #48#16; |QBF aAlfa;
      aAlfa = aAlfa + "def/cacuenta.mas";
      |CARGA_DEF aAlfa, cacuenta@;
      aAlfa = #48#16; |QBF aAlfa;
      aAlfa = aAlfa + "fich/" + (("00000" + #48#0) % -105) + nAnyo + "/"; |PATH_DAT #cacuenta@#aAlfa#;
      |ABRE #cacuenta@;
      #cacuenta@#0 = aCuenta;
      #cacuenta@#1 = nMaxNivel;
      |LEE 000#cacuenta@.];
      |CONSULTA_CLAVE #1#cacuenta@;
      |SI FSalida = 0; aCuenta = #cacuenta@#0; |FINSI;
      |CIERRA #cacuenta@; |DESCARGA_DEF #cacuenta@;
   |FINSI;
   #dscaz195#Campo# = aCuenta; |PINTA #dscaz195#Campo#;
|FPRC;

|PROCESO MiraSiMulti;
   |SI #dscaz195#11 = "S";
      |C_M #dscaz195#12, 1, "S";
      |C_M #dscaz195#13, 1, "S"; |C_M #dscaz195#14, 1, "S";
   |SINO;
      |C_M #dscaz195#12, 1, "N";
      |C_M #dscaz195#13, 1, "N"; |C_M #dscaz195#14, 1, "N";
   |FINSI;
   |C_M #dscaz195#15, 1, "N"; |C_M #dscaz195#16, 1, "N";
   |C_M #dscaz195#17, 1, "N"; |C_M #dscaz195#18, 1, "N";
   |C_M #dscaz195#19, 1, "N"; |C_M #dscaz195#20, 1, "N";
   |C_M #dscaz195#21, 1, "N"; |C_M #dscaz195#22, 1, "N";
|FPRC;

|PROCESO MiraSiSelec;
   |SI #dscaz195#11 = "N";
      #dscaz195#13 = #48#0; |PINTA #dscaz195#13;
      #dscaz195#14 = #48#0; |PINTA #dscaz195#14;
      |ACABA;
   |FINSI;

   |SI #dscaz195#12 = "N";
      |C_M #dscaz195#13, 1, "S"; |C_M #dscaz195#14, 1, "S";
      |C_M #dscaz195#15, 1, "N"; |C_M #dscaz195#16, 1, "N";
      |C_M #dscaz195#17, 1, "N"; |C_M #dscaz195#18, 1, "N";
      |C_M #dscaz195#19, 1, "N"; |C_M #dscaz195#20, 1, "N";
      |C_M #dscaz195#21, 1, "N"; |C_M #dscaz195#22, 1, "N";
   |SINO;
      |C_M #dscaz195#13, 1, "N"; |C_M #dscaz195#14, 1, "N";
      |C_M #dscaz195#15, 1, "S"; |C_M #dscaz195#16, 1, "S";
      |C_M #dscaz195#17, 1, "S"; |C_M #dscaz195#18, 1, "S";
      |C_M #dscaz195#19, 1, "S"; |C_M #dscaz195#20, 1, "S";
      |C_M #dscaz195#21, 1, "S"; |C_M #dscaz195#22, 1, "S";
   |FINSI;
|FPRC;

|PROCESO Empresas;
   |DFICB aAlfa; |QBF aAlfa;
   aAlfa = aAlfa + (("00000" + #dscam000@#0) % -105) + "/";
   |PATH_DAT #dscam003#aAlfa#; |PATH_DAT #dscam008#aAlfa#;

   nEmpCart = #dscam000@#0;
   |SI nSwCont = 1; |BUCLE RecogeDatosCont; |FINSI;
   |BUCLE RecogeCarteraCmp; |BUCLE RecogeCarteraVnt;
|FPRC;

|DEFBUCLE Empresas;
#dscam000@#1001;
;
#dscaz195#13;
#dscaz195#14;
;
NULL, Empresas;
|FIN;

|PROGRAMA;
   |HAZ Tipo8;

   |CLS;
   |CABEZA "Informe vencimientos";

   |PINPA #0#0; |PINDA #0#0;
   |ENDATOS #1#0;
   |SI FSalida = 0;
      aAlfa = "tg" + Usuario; |NOME_DAT #dscaw182#aAlfa#;
      |DELINDEX #dscaw182; |ABRE #dscaw182;

      |DDEF aAlfa; |QBF aAlfa;
      aAlfa = aAlfa + "dscam000.mas";
      |CARGA_DEF aAlfa, dscam000@;
      |SI FSalida ! 0; |CIERRA #dscaw182; |ACABA; |FINSI;
      |DFICB aAlfa; |QBF aAlfa; |PATH_DAT #dscam000@#aAlfa#;

      aAlfa = #48#16; |QBF aAlfa;
      aAlfa = aAlfa + "def/catalm01.mas";
      |CARGA_DEF aAlfa, catalm01@;
      |SI FSalida ! 0; nSwCont = 0; |SINO; nSwCont = 1; |FINSI;

      |SI nSwCont = 1;
         aAlfa = #48#16; |QBF aAlfa; aAlfa = aAlfa + "fich/000000/";
         |PATH_DAT #catalm01@#aAlfa#; |ABRE #catalm01@;
      |FINSI;

      |SI #dscaz195#11 = "S";
         |SI #dscaz195#12 = "S";
            |PARA nCont = 15; |SI nCont [ 22; |HACIENDO nCont = nCont + 1;
               |DFICB aAlfa; |QBF aAlfa;
               aAlfa = aAlfa + (("00000" + #dscaz195#nCont#) % -105) + "/";
               |PATH_DAT #dscam003#aAlfa#; |PATH_DAT #dscam008#aAlfa#;
               nEmpCart = #dscaz195#nCont#;
               |SI nSwCont = 1; |BUCLE RecogeDatosCont; |FINSI;
               |BUCLE RecogeCarteraCmp; |BUCLE RecogeCarteraVnt;
            |SIGUE;
         |SINO;
            |BUCLE Empresas;
         |FINSI;
      |SINO;
         nEmpCart = #48#0;
         |SI nSwCont = 1; |BUCLE RecogeDatosCont; |FINSI;
         |BUCLE RecogeCarteraCmp; |BUCLE RecogeCarteraVnt;
      |FINSI;

      |SI nSwCont = 1;
         |CIERRA #catalm01@; |DESCARGA_DEF #catalm01@;
      |FINSI;
      |CIERRA #dscam000@; |DESCARGA_DEF #dscam000@;

      |SI nSwHayDatos = 1;
         Impresora = "Crystal Reports";
         |IMPRE 0;
         |SI FSalida = 0;
            aInforme = "Informe de vencimientos C/P"; |HAZ ObtenInforme;
            |SI aInforme = "Informe de vencimientos C/P";
               aInforme = "dscaz195";
            |FINSI;
            |INFOR aInforme;
            |SI FSalida = 0;
               |BUCLE Imprime;

               |PIEINF; |FININF;
            |FINSI;
            |FINIMP;
         |FINSI;
      |SINO;
         |MENAV "    Seleccion vacia";
      |FINSI;

      |CIERRA #dscaw182; |DELINDEX #dscaw182;
   |FINSI;

   |HAZ Tipo9;
|FPRO;
