|FICHEROS;
     dscaz226 #0;
     dscam073 #1;
     dscam049 #2;  || Confirming Cabeceras
     dscam050 #3;  || Confirming Lineas
     dscam008 #4;  || Mantenimiento pagos
     dscam030 #5;  || Mantenimiento enlaces
     dscaw013 #6;  || Temporal enlaces
     dscam051 #7;  || Parametros generales
     dscam014 #8;  || Bancos..............
     dscaw142 #10; || Tmp
     caclipro@ #100;
|FIN;

|VARIABLES;
     &fecha = @;
     &fp = "";
     &eaDirProv = "";
     &eaNifProv = "";
     &HayConta = 0;
     &eaCadena = "";

     &eaCodigo  = "";
     &eaTipoCod = "";
     &aCuenta   = "";

     &eaNif       = "";
     &eaNombre    = "";
     &eaApCorreos = "";
     &eaDireccion = "";
     &eaPoblacion = "";
     &eaProvincia = "";
     &eaCP        = "";
     &eaTelefono  = "";
     &eaFax       = "";
     &eaCodBIC    = "";
     &eaCodIBAN   = "";

     &eaVarDni  = "";
     &enCalcCif = 0;
     &aInforme = "";
     &eaNombre1 = "";
     &eaOrigen  = "";

     &Niv1 = 0;
     &Niv2 = 0;
     &Niv3 = 0;
     &Niv4 = 0;
     &Niv5 = 0;
     &Niv6 = 0;
     &Divisa = "";

     nError = 0;
     menR1    = "    No Existe Confirme !!!";
     menR2    = "    Confirme YA contabilizado ";
     menR3    = "2400SConfirme YA Emitido en Papel. Aceptar S/N ";
     menR4    = "2400NConfirme YA lanzado en Diskette. Aceptar S/N ";

     NifProv    = "";
     NombreProv = "";
     DirecProv  = "";
     PoblaProv  = "";
     ProviProv  = "";
     Cp1Prov    = "";
     Cp2Prov    = "";
     TelefProv  = "";
     FaxProv    = "";
     PaisProv   = "";
     Entidad    = "";
     Sucursal   = "";
     DC         = "";
     Cuenta     = "";
     aRef       = " ";

     aCuentaCt = "";
     nNivel = 0;
     aAlfa = "";
     aAlf1 = "";
     nCalc = 0;
     aPago = "";
     nSwImpre = 0;
     nRecibos = 0;
     nTotal = 0;
     nHandle = 0;
     Comodin = "";
     aMensa = "";
     aCtaAnt = "";
     aLF = "";
     aTipo = "";
     nPos = 0;
     nLon = 0;
     nNume = 0;
     nAbo = 0;
     nNu1 = 0;
     nNu2 = 0;
     nDec = 0;
     nTotaReg = 0;
     nImpo = 0;
|FIN;
*********************************************************************
|PROCESO Cabecera;
     nTotaReg = 1;

     aAlfa = "10";
     aTipo = "A"; nLon = 2; |HAZ Relleno;

     aAlfa = (("0000" + #dscam073#4)%-104)+(("0000" + #dscam073#5)%-104);
     aTipo = "N"; nLon = 9; |HAZ Relleno;    || Entidad

     aAlfa = #dscam073#24;
     aTipo = "A"; nLon = 11; |HAZ Relleno;   || Cliente

     aAlfa = #dscam073#27 + #dscam073#2 + #dscam073#26;
     aTipo = "A"; nLon = 14; |HAZ Relleno;   || Nif (2Pais+ 9Nif+ 3Sufijo)

     aAlfa = #dscaz226#13;
     aTipo = "A"; nLon = 10; |HAZ Relleno;   || Referencia

     aAlfa = #dscaz226#3;
     aTipo = "F"; nLon = 8; |HAZ Relleno;    || Fecha Emision

     aAlfa = "";
     aTipo = "A"; nLon = 34; |HAZ Relleno;   || Iban cuenta cliente

     aAlfa =  #dscam049#16;
     aTipo = "A"; nLon = 3; |HAZ Relleno;    || Divisa

     aAlfa = "";
     aTipo = "A"; nLon = 99; |HAZ Relleno;   || Filler
     aTipo = "A"; nLon = 99; |HAZ Relleno;   || Filler
     aTipo = "A"; nLon = 61; |HAZ Relleno;   || Filler

     |XGRABA nHandle, aLF;
|FPRC;

|PROCESO Proveedor;
     nTotaReg = nTotaReg + 1;

     aAlfa = "11";
     aTipo = "A"; nLon = 2; |HAZ Relleno;

     aAlfa = (("0000" + #dscam073#4)%-104)+(("0000" + #dscam073#5)%-104);
     aTipo = "N"; nLon = 9; |HAZ Relleno;    || Entidad

     aAlfa = #dscam073#24;
     aTipo = "A"; nLon = 11; |HAZ Relleno;   || Cliente

     aAlfa = #dscam073#27 + #dscam073#2 + #dscam073#26;
     aTipo = "A"; nLon = 14; |HAZ Relleno;   || Nif (2Pais+ 9Nif+ 3Sufijo)

     aAlfa = #dscaz226#13;
     aTipo = "A"; nLon = 10; |HAZ Relleno;   || Referencia

     |SI PaisProv = ""; PaisProv = "ES"; |FINSI; PaisProv = PaisProv % 102;
     NifProv = NifProv + "         "; NifProv = NifProv % 109;

     aAlfa = PaisProv + NifProv + #dscam073#26;
     aTipo = "A"; nLon = 14; |HAZ Relleno;   || Nif (2Pais+ 9Nif+ 3Sufijo)

     aAlfa = NombreProv;
     aTipo = "A"; nLon = 50; |HAZ Relleno;   || Nombre Prov

     aAlfa = DirecProv;
     aTipo = "A"; nLon = 50; |HAZ Relleno;   || Via Prov

     aAlfa = PoblaProv;
     aTipo = "A"; nLon = 50; |HAZ Relleno;   || Municipio Prov

     aAlfa = Cp1Prov + Cp2Prov;
     aTipo = "A"; nLon = 10; |HAZ Relleno;   || CP Prov

     aAlfa = TelefProv;
     aTipo = "A"; nLon = 15; |HAZ Relleno;   || Tlf

     aAlfa = "";
     aTipo = "A"; nLon = 10; |HAZ Relleno;   || Apartado correos

     aAlfa = "";
     aTipo = "A"; nLon = 15; |HAZ Relleno;   || Fax

     aAlfa = "";
     aTipo = "A"; nLon = 12; |HAZ Relleno;   || Referencia Beneficiario

     aAlfa = eaCodIBAN;
     aTipo = "A"; nLon = 34; |HAZ Relleno;   || IBAN Prv

     aAlfa = eaCodBIC;
     aTipo = "A"; nLon = 11; |HAZ Relleno;   || BIC Prv

     aAlfa = "";
     aTipo = "A"; nLon = 33; |HAZ Relleno;   || Filler

     |XGRABA nHandle, aLF;
|FPRC;

|PROCESO Factura;
     nTotaReg = nTotaReg + 1;

     aAlfa = "12";
     aTipo = "A"; nLon = 2; |HAZ Relleno;

     aAlfa = (("0000" + #dscam073#4)%-104)+(("0000" + #dscam073#5)%-104);
     aTipo = "N"; nLon = 9; |HAZ Relleno;    || Entidad

     aAlfa = #dscam073#24;
     aTipo = "A"; nLon = 11; |HAZ Relleno;   || Cliente

     aAlfa = #dscam073#27 + #dscam073#2 + #dscam073#26;
     aTipo = "A"; nLon = 14; |HAZ Relleno;   || Nif (2Pais+ 9Nif+ 3Sufijo)

     aAlfa = #dscaz226#13;
     aTipo = "A"; nLon = 10; |HAZ Relleno;   || Referencia

     aAlfa = #dscam008#63;
     aTipo = "A"; nLon = 15; |HAZ Relleno;   || Factura

     aAlfa =  #dscam049#16;
     aTipo = "A"; nLon = 3; |HAZ Relleno;    || Divisa

     aAlfa = #dscam050#20;
     aTipo = "N"; nLon = 13; nDec = 2; |HAZ Relleno;   || Importe

     aAlfa = #dscam008#4;
     aTipo = "F"; nLon = 8; |HAZ Relleno;    || F.Emision

     aAlfa = #dscam008#3;
     aTipo = "F"; nLon = 8; |HAZ Relleno;    || F.Vto

     aAlfa = "0";
     aTipo = "N"; nLon = 1; |HAZ Relleno;    || Factura=0, Abono=1

     ||HAZ Concepto;
     aAlfa = "";
     aTipo = "A"; nLon = 32; |HAZ Relleno;   || Informacion adicional

     aAlfa = "";
     aTipo = "A"; nLon = 6; |HAZ Relleno;    || concepto estadistico de pago

     aAlfa = "";
     aTipo = "A"; nLon = 99; |HAZ Relleno;  || Filler
     aTipo = "A"; nLon = 99; |HAZ Relleno;  || Filler
     aTipo = "A"; nLon = 20; |HAZ Relleno;  || Filler

     |XGRABA nHandle, aLF;
|FPRC;

|PROCESO Totales;
     nTotaReg = nTotaReg + 1;

     aAlfa = "13";
     aTipo = "A"; nLon = 2; |HAZ Relleno;

     aAlfa = (("0000" + #dscam073#4)%-104)+(("0000" + #dscam073#5)%-104);
     aTipo = "N"; nLon = 9; |HAZ Relleno;    || Entidad

     aAlfa = #dscam073#24;
     aTipo = "A"; nLon = 11; |HAZ Relleno;   || Cliente

     aAlfa = #dscam073#27 + #dscam073#2 + #dscam073#26;
     aTipo = "A"; nLon = 14; |HAZ Relleno;   || Nif (2Pais+ 9Nif+ 3Sufijo)

     aAlfa = #dscaz226#13;
     aTipo = "A"; nLon = 10; |HAZ Relleno;   || Referencia

     aAlfa = nTotaReg;
     aTipo = "N"; nLon = 6; |HAZ Relleno;    || Total Registros

     nImpo = (nTotal * 100) ! 0;
     aAlfa = nImpo;
     aTipo = "N"; nLon = 13; |HAZ Relleno;   || Total Importe

     aAlfa = "";
     aTipo = "A"; nLon = 99; |HAZ Relleno;   || Filler
     aTipo = "A"; nLon = 99; |HAZ Relleno;   || Filler
     aTipo = "A"; nLon = 87; |HAZ Relleno;   || Filler

     |XGRABA nHandle, aLF;
|FPRC;
*********************************************************************
|PROCESO Concepto;
     aAlfa = "";
     |SI #dscam073#21 = "S";
          aAlfa = #dscam073#11; |QBF aAlfa;
          |SI #dscam073#12 = "S";
               aAlfa = aAlfa + " " + #dscam008#0 + "/" + (("000000" + #dscam008#1) % -106);
          |FINSI;
          |SI #dscam073#13 = "S";
               aAlfa = aAlfa + " " + (("000" + #dscam008#2) % -103);
          |FINSI;
          |SI #dscam073#14 = "S";
               aAlfa = aAlfa + " " + #dscam008#3;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Relleno;
     |SI aTipo = "F";
          nPos = nLon;
          |SI aAlfa ! "";
               aAlfa = (aAlfa % -104) + (aAlfa % 402) + (aAlfa % 102);
          |FINSI;
          aAlf1 = " " * nPos;
          aAlfa = aAlfa + aAlf1;
          nPos = nPos + 100;
          aAlfa = aAlfa % nPos;
     |FINSI;

     |SI aTipo = "A";
          aAlf1 = " " * nLon;
          aAlfa = aAlfa + aAlf1;
          nPos = nLon + 100;
          aAlfa = aAlfa % nPos;
     |FINSI;

     |SI aTipo = "N";
          nNume = aAlfa;
          |SI nNume < 0; nAbo = -1; |SINO; nAbo = 1; |FINSI;
          |SI nNume < 0; nNume = nNume * - 1; |FINSI;
          nNu1 = nNume ! 0;
          |SI nNu1 > nNume; nNu1 = nNu1 - 1; |FINSI;
          nNu2 = nNume - nNu1;
          |SI nDec < 0;
               aAlfa = nNume;
          |SINO;
               |SI nDec = 0;
                    aAlfa = nNu1;
               |SINO;
                    aAlfa = nNu2;
                    aAlfa = aAlfa + "00000000000000000000000000";
                    nPos = nDec + 300;
                    aAlfa = aAlfa % nPos;

                    || CON PUNTO DECIMAL
                    ||aAlfa = nNu1 + "." + aAlfa;

                    || SIN PUNTO DECIMAL
                    aAlfa = nNu1 + aAlfa;
               |FINSI;
          |FINSI;
          aAlfa = "000000000000000000000000000000000000000000000" + aAlfa;
          |SI nAbo = 1;
               nPos = -1 * (nLon + 100);
               aAlfa = aAlfa % nPos;
          |SINO;
               nPos = -1 * (nLon + 99);
               aAlfa = "-" + aAlfa % nPos;
          |FINSI;
          nDec = 0;
     |FINSI;

     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO Tmp;
     #dscam050#0 = #dscaz226#1;
     #dscam050#1 = #10#1;
     |LEE 020#dscam050.=;

     |HAZ leecl_y_cobro;

     |SI #dscam050#16 = "T";
          aPago = "TRANS.";
     |SINO;
          aPago = "CHEQUE";
     |FINSI;

     eaNifProv = NifProv;
     eaDirProv = DirecProv;

     |ABRE #dscam008;
     #dscam008#40 = #dscam050#13;
     |LEE 000#dscam008.=;
     |SI FSalida ! 0; |DDEFECTO #dscam008; |FINSI;
     |CIERRA #dscam008;

     |SI nSwImpre = 0; |PRINT; |FINSI;

     nRecibos = nRecibos + 1;
     nTotal = nTotal + #dscam050#20;
     ||------------------------------------
     |SI aCtaAnt ! #10#0;
          aCtaAnt = #10#0;
          |HAZ Proveedor;
     |FINSI;
     |HAZ Factura;
|FPRC;

|PROCESO SacaNivel1;
   |QBF aCuentaCt;
   aAlfa = aCuentaCt % 0; nNivel = aAlfa;
   |SI nNivel = Niv1; nNivel = 1; |FINSI;
   |SI nNivel = Niv2; nNivel = 2; |FINSI;
   |SI nNivel = Niv3; nNivel = 3; |FINSI;
   |SI nNivel = Niv4; nNivel = 4; |FINSI;
   |SI nNivel = Niv5; nNivel = 5; |FINSI;
   |SI nNivel = Niv6; nNivel = 6; |FINSI;
|FPRC;

|PROCESO leecl_y_cobro;
     NombreProv = "";
     DirecProv  = "";
     PoblaProv  = "";
     ProviProv  = "";
     Cp1Prov    = "";
     Cp2Prov    = "";
     TelefProv  = "";
     FaxProv    = "";
     PaisProv   = "";
     Entidad    = "";
     Sucursal   = "";
     DC         = "";
     Cuenta     = "";

     aRef = " " * 12;

     |DDEFECTO #dscam008;
     #dscam008#40 = #dscam050#13;
     |LEE 001#dscam008.=;

     eaCodigo = #dscam008#32; eaTipoCod = "P"; aCuenta = #dscam008#5; |HAZ LeeDatos;

     #caclipro@#23 = "P";
     #caclipro@#10 = #dscam008#5;
     aCuentaCt = #dscam008#5; |HAZ SacaNivel1; #caclipro@#11 = nNivel;
     |LEE 000#caclipro@.=;
     |SI FSalida = 0;
        NifProv    = eaNif;       |QBF NifProv;
        NombreProv = eaNombre;    |QBF NombreProv;
        DirecProv  = eaDireccion; |QBF DirecProv;
        PoblaProv  = eaPoblacion; |QBF PoblaProv;
        ProviProv  = eaProvincia; |QBF ProviProv;
        Cp1Prov    = eaCP % 0102;
        Cp2Prov    = eaCP % 0303;
        TelefProv  = eaTelefono;  |QBF TelefProv;
        FaxProv    = eaFax;       |QBF FaxProv;
        PaisProv   = #caclipro@#24; |QBF PaisProv;
        |SI #dscaz226#4 = "1"; aRef = #caclipro@#10; |FINSI;
        |SI #dscaz226#4 = "2"; aRef = #caclipro@#0;  |FINSI;
        |QBF aRef; aRef = ((" " * 12) + aRef) % -112;

        aAlfa = "|NC"; nCalc = #caclipro@#aAlfa#;
        |SI nCalc > 29;
           Entidad  = #caclipro@#29;
           Sucursal = #caclipro@#30;
           DC       = #caclipro@#31;
           Cuenta   = #caclipro@#32;
        |SINO;
           aAlfa    = #caclipro@#18; |QBF aAlfa;
           Entidad  = aAlfa % 0104;
           Sucursal = aAlfa % 0504;
           DC       = aAlfa % 0902;
           Cuenta = #caclipro@#17; |QBF Cuenta;
           Cuenta = ("0000000000" + Cuenta) % -110;
        |FINSI;
     |SINO;
        aAlfa = "    No se encuentra el proveedor " + #dscam008#5; |QBF aAlfa;
        aAlfa = aAlfa + " en contabilidad";
        |MENAV aAlfa;
     |FINSI;
|FPRC;

|DEFBUCLE Tmp;
     #10#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tmp;
|FIN;

|PROCESO LineasRem;
     |DDEFECTO #dscam008;
     #dscam008#40 = #dscam050#13;
     |LEE 001#dscam008.=;

     #10#0 = #dscam008#5;
     #10#1 = #dscam050#1;
     |GRABA 020#10n;
|FPRC;

|PROCESO pasecuencial;
     |ABRE #dscam073;
     #dscam073#0 = #dscam049#3;
     |LEE 000#dscam073.=;
     |CIERRA #dscam073;

     |ABRE #dscam049;
     |HAZ leerem;
     |SI nError = 1; |O nError = 2;
          |MENAV "    ERROR !!! CONFIRME NO CORRECTO ";
          |ACABA;
     |FINSI;

     aAlfa = #48#16; |QBF aAlfa;
     aAlf1 = aAlfa + "def/caclipro.mas";
     |CARGA_DEF aAlf1, caclipro@;
     |SI FSalida ! 0;
          |MENAV "    Problemas para abrir el fichero de clientes/proveedores de contabilidad";
          |ERROR; |ACABA;
     |FINSI;
     aAlf1 = aAlfa + "fich/" + (("00000" + #48#14) % -105) + #48#19 + "/";
     |PATH_DAT #100 aAlf1; |ABRE #caclipro@;
     |ABRE #dscam008;

     |DFICE aAlfa;
     aAlfa = aAlfa + "casecmin.seq";
     |XABRE "WB", aAlfa, nHandle;
     |SI FSalida ] 0;
          |HAZ Cabecera;

          aAlfa = Usuario; |NOME_DAT #10 aAlfa; |DELINDEX #10;
          |ABRE #10;
          |BUCLELIN 0LineasRem#dscam049;
          |CIERRA #10;

          |ABRE #dscam050;
          |BUCLE Tmp;
          |CIERRA #dscam050;

          |DELINDEX #10;

          |HAZ Totales;
          |XCIERRA nHandle;
     |FINSI;

     |CIERRA #dscam008;
     |CIERRA #caclipro@; |DESCARGA_DEF #caclipro@;

     |SI HayConta = 1; |Y #dscam049#7 = "N"; |Y #dscaz226#14 ! "    ";
          |ABRE #dscam030;
          #dscam030#0 = #dscaz226#14;
          |LEE 010#dscam030.=;
          |SI FSalida = 0;
               Comodin = Usuario; |QBF Comodin; Comodin = "ac" + Comodin;
               |NOME_DAT #6 Comodin;
               |DELINDEX #dscaw013; |ABRE #dscaw013;
               |DDEFECTO #dscaw013;
               #dscaw013#0  = 1;            #dscaw013#15 = #dscaz226#14;
               #dscaw013#5  = #dscam049#0;  #dscaw013#6  = #dscam049#0;
               #dscaw013#11 = "N";          #dscaw013#12 = 5;
               #dscaw013#13 = "dscam050";   #dscaw013#14 = 0;
               |GRABA 020#dscaw013.n;
               |CIERRA #dscaw013;
               Comodin = #dscaz226#14;
               Comodin = "dscam033.tab;" + Comodin + ",E";
               |LIBERA #dscam049; |CIERRAT #dscam049;
               |LIBERA #dscam050; |CIERRAT #dscam050;
               |SUB_EJECUTA_NP Comodin;
               |ABRET #dscam049; |LEE 101#dscam049.=;
               |ABRET #dscam050; |LEE 101#dscam050.=;
          |FINSI;
          |CIERRA #dscam030;
     |FINSI;

     |SI nRecibos ! 0;
          #dscam049#7 = "D";
          |GRABA 020#dscam049.a; |LIBERA #dscam049;
     |FINSI;
     |CIERRA #dscam049;

     |D_CUADRO 1261, 2280;
     |ATRI R;
     |PINTA 1462, " Numero Recibos   ";
     |PINTA 1662, " Numero Registros ";
     |PINTA 1862, " Importe Total    ";
     |ATRI 0;
     |PINTA 1562, "                  ";
     |PINTA 1762, "                  ";
     |PINTA 1962, "                  ";
     |ATRI 0;

     #dscaz226#10 = nRecibos;  |PINTA #dscaz226#10;
     #dscaz226#11 = nTotaReg;  |PINTA #dscaz226#11;
     #dscaz226#12 = nTotal;    |PINTA #dscaz226#12;

     |DFICE eaOrigen; |QBF eaOrigen;
     eaNombre1 = "casecmin.seq";
     |SUB_EJECUTA "dscaz032.tab";
|FPRC;

|PROCESO leerem;
     nError = 0;
     |DDEFECTO #dscam049;
     #dscam049#0 = #dscaz226#1;
     |LEE 001#dscam049.=;
     |SI FSalida ! 0;
          nError = 1; aMensa = menR1;
     |SINO;
          Divisa = #dscam049#16; |HAZ TomaDecim;
          |SI #dscam049#8 = "S";
               nError = 2; aMensa = menR2;
          |SINO;
               |SI #dscam049#7 = "S";
                    nError = 3; aMensa = menR3;
               |FINSI;
               |SI #dscam049#7 = "D"; nError = 4; aMensa = menR4; |FINSI;
          |FINSI;
          |ABRE #dscam014;
          #dscam014#0 = #dscam049#3;
          |LEE 000#dscam014.=;
          |CIERRA #dscam014;
     |FINSI;
|FPRC;

|PROCESO ver_confirming; |TIPO 6;
     |SI FSalida = 2; |ACABA; |FINSI;

     |ABRE #dscam049;
     |HAZ leerem;
     |CIERRA #dscam049;

     |SI nError ! 0;
          |SI nError < 3;
               |MENAV aMensa;
               |ERROR;
               |ACABA;
          |SINO;
               |CONFI aMensa;
               |SI FSalida ! 0;
                   |ERROR;
                   |ACABA;
               |FINSI;
          |FINSI;
     |FINSI;

     |HAZ PinDatos;
|FPRC;

|PROCESO PinDatos;
    |PINTA 0935, #dscam049#2;   || Vto.
    |PINTA 0958, #dscam049#5;   || N§ Efectos
    |PINTA 1035, #dscam049#3;   || Banco
    |PINTA 1049, #dscam049#19;  || Importe Total
|FPRC;

|PROGRAMA;
     |HAZ Tipo8;

     aLF = &13; aLF = aLF + &10;
     |ABRE #dscam051; |LEE 000#dscam051.p;|CIERRA #dscam051;

     |SI #dscam051#1 = "S";
          #dscaz226#14 = #dscam051#29;
          |C_M #dscaz226#14, 1, "S";
     |SINO;
          #dscaz226#14 = "";
          |C_M #dscaz226#14, 1, "N";
     |FINSI;

     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     #dscam049#2 = "01.01.0000";
     #dscam049#3 = "";
     |HAZ PinDatos;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |ABRE #dscam073;
          #dscam073#0 = #dscam049#3;
          |LEE 000#dscam073.=;
          |CIERRA #dscam073;

          |CONFI "0000N¿Imprimir carta confirme?";
          nSwImpre = FSalida;
          |SI nSwImpre = 0;
               |IMPRE 0;
               |SI FSalida = 0;
                     aInforme = "Carta emision de confirmes"; |HAZ ObtenInforme;
                     |INFOR aInforme;
                     |SI FSalida = 0;
                         fecha = #dscaz226#3;
                         |HAZ pasecuencial;
                         |PIEINF;
                         |FININF;
                     |FINSI;
                     |FINIMP;
               |FINSI;
          |SINO;
               |HAZ pasecuencial;
          |FINSI;
     |FINSI;

     |HAZ Tipo9;
|FPRO;
