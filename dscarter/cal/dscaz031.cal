|FICHEROS;
   dscaz031 #0;

   FMaster@ #1;
   FEsclav@ #2;
   FMastCb@ #3;

   dscaw095  #50;

   dscam030@ #100;
|FIN;

|VARIABLES;

   &eaUsuario = ""; &enImpAnt = 0;  &enImpAct = 0;  &eaDescr = "";
   &eaTipoReg = ""; &eaNumero = ""; &eaTipoOp = "";

   nCont = 0;
   Calc  = 0;
   Calc1 = 0;

   FEstado = 0;
   aCodigo = "";
   nApunte = 0;

   Comodin  = "";
   Comodin1 = "";
   Path     = "";
   Fichero  = "";
   FichCab  = "";
   aAlfa    = "";
   aAlfa1   = "";
   aAlfa2   = "";
   aAlfa3   = "";
|FIN;

|PROCESO Actualiza2;
   |DBASE Comodin; |QBF Comodin;
   Comodin = Comodin + "def/" + Fichero + ".mas";
   |CARGA_DEF Comodin, FMaster@;
   |SI FSalida ! 0;
      Comodin = "    Problemas al cargar el def maestro " + Fichero + ".mas";
      |MENAV Comodin;
      |ACABA;
   |FINSI;

   |SI FichCab ! "";
      |DBASE Comodin; |QBF Comodin;
      Comodin = Comodin + "def/" + FichCab + ".mas";
      |CARGA_DEF Comodin, FMastCb@;
      |SI FSalida ! 0;
         Comodin = "    Problemas al cargar el def maestro de cabecera " + FichCab + ".mas";
         |MENAV Comodin;
         |DESCARGA_DEF #FMaster@;
         |ACABA;
      |FINSI;
   |FINSI;

   |DBASE Comodin; |QBF Comodin;
   Comodin = Comodin + "def/" + Fichero + ".mas";
   |CARGA_DEF Comodin, FEsclav@;
   |SI FSalida ! 0;
      Comodin = "    Problemas al cargar el def esclavo " + Fichero + ".mas";
      |MENAV Comodin;
      |DESCARGA_DEF #FMaster@;
      |SI FichCab ! ""; |DESCARGA_DEF #FMastCb@; |FINSI;
      |ACABA;
   |FINSI;

   |DBASE Comodin; |QBF Comodin; Comodin = Comodin + "fich/patrones/";
   |PATH_DAT #1 Comodin;
   |SI FichCab ! ""; |PATH_DAT #3 Comodin; |FINSI;
   |PATH_DAT #2 Path;

   |ABRE #FMaster@; |ABRE #FEsclav@;
   |SI FichCab ! ""; |ABRE #FMastCb@; |FINSI;

   |LEE 101#FMaster@.p;
   |SI FSalida = 0;
      |PARA; |SI FSalida = 0; |HACIENDO;
         |SI FichCab ! "";
            |DDEFECTO #FMastCb@;
            Comodin = "|K000"; Comodin = #FMastCb@#Comodin#;
            Comodin1 = Comodin % 0103; Calc = Comodin1; Comodin = Comodin % 0499;
            |PARA nCont = 1; |SI nCont [ Calc; |HACIENDO nCont = nCont + 1;
               Comodin1 = Comodin % 0103; Calc1 = Comodin1; Comodin = Comodin % 0499;
               |SI Fichero = "dscam036"; |Y FichCab = "dscam030";
                  #FMastCb@#0 = #FMaster@#15;
               |SINO;
                  |SI FichCab = "dscam031";
                     |SI Calc1 = 2;
                        #FMastCb@#2 = #FMaster@#1;
                     |SINO;
                        #FMastCb@#Calc1# = #FMaster@#Calc1#;
                     |FINSI;
                  |SINO;
                     #FMastCb@#Calc1# = #FMaster@#Calc1#;
                  |FINSI;
               |FINSI;
            |SIGUE;
            |LEE 000#FMastCb@.=;
            |SI FSalida ! 0; |VETE Sigue1; |FINSI;
         |FINSI;

         |DDEFECTO #FEsclav@;
         Comodin = "|K000"; Comodin = #FMaster@#Comodin#;
         Comodin1 = Comodin % 0103; Calc = Comodin1; Comodin = Comodin % 0499;
         |PARA nCont = 1; |SI nCont [ Calc; |HACIENDO nCont = nCont + 1;
            Comodin1 = Comodin % 0103; Calc1 = Comodin1; Comodin = Comodin % 0499;
            #FEsclav@#Calc1# = #FMaster@#Calc1#;
         |SIGUE;
         |LEE 101#FEsclav@.=;
         FEstado = FSalida;
         |SI Fichero = "dscam065";
            |SI FEstado = 0;
               aAlfa = #FEsclav@#0; |QBF aAlfa;
               |SI #FEsclav@#1 = "dscaz080"; |Y aAlfa = "Informe prevision tesoreria";
                  #FEsclav@#1 = "dscaz117"; |GRABA 020#FEsclav@.a;
               |FINSI;
               |SI #FEsclav@#1 = "recldeud"; |Y aAlfa = "Carta reclamacion deuda";
                  #FEsclav@#1 = "dscaz064"; |GRABA 020#FEsclav@.a;
               |FINSI;
            |FINSI;
         |FINSI;
         |SI Fichero = "dscam030";
            |SI FEstado ! 0;
               |DDEFECTO #dscaw095;
               #dscaw095#0 = #FEsclav@#0;
               |GRABA 020#dscaw095.n;
            |FINSI;
         |FINSI;
         |SI Fichero = "dscam032";
            #dscaw095#0 = #FEsclav@#0;
            |LEE 000#dscaw095.=;
            |SI FSalida = 0;
               FEstado = 111;
            |SINO;
               FEstado = 0;
               |SALVA_FICHA #FEsclav@;
               |DDEFECTO #FEsclav@;
               #FEsclav@#0 = #FMaster@#0;
               |LEE 000#FEsclav@.];
               |SI FSalida ! 0; |O #FEsclav@#0 ! #FMaster@#0;
                  FEstado = 111;
               |FINSI;
               |REPON_FICHA #FEsclav@;
            |FINSI;
         |FINSI;
         FSalida = FEstado;
         |SI FSalida ! 0;
            |DDEFECTO #FEsclav@;
            Comodin1 = "|NC"; Comodin1 = #FMaster@#Comodin1#; Calc1 = Comodin1;
            |PARA nCont = 0; |SI nCont < Calc1; |HACIENDO nCont = nCont + 1;
               #FEsclav@#nCont# = #FMaster@#nCont#;
            |SIGUE;
            |SI Fichero = "dscam030";
               |SI #FEsclav@#0 ! "    ";
                  #FEsclav@#6 = #48#16; #FEsclav@#4 = #48#14; #FEsclav@#5 = #48#19;
               |FINSI;
            |FINSI;
            |GRABA 020#FEsclav@.n;
         |SINO;
            |LIBERA #FEsclav@;
         |FINSI;
|ET Sigue1;
         |LIBERA #FMaster@;
         |LEE 101#FMaster@.s;
      |SIGUE;
   |FINSI;

   |SI FichCab ! ""; |CIERRA #FMastCb@; |DESCARGA_DEF #FMastCb@; |FINSI;
   |CIERRA #FMaster@; |CIERRA #FEsclav@;
   |DESCARGA_DEF #FEsclav@; |DESCARGA_DEF #FMaster@;
|FPRC;

|PROCESO PonPath;
   #dscam030@#6 = #48#16;
   #dscam030@#4 = #48#14;
   #dscam030@#5 = #48#19;
   |GRABA 020#dscam030@.a;
|FPRC;

|DEFBUCLE PonPath;
#dscam030@#1001;
;
"   !";                                       || El blanco no se toca ...
"zzzz";
;
NULL,PonPath;
|FIN;

|PROCESO Actualiza1;
   |DBASE Comodin; |QBF Comodin; Comodin = Comodin + "fich/patrones/" + Fichero;
   aAlfa1 = Comodin + ".dat";
   aAlfa2 = Path; |QBF aAlfa2; aAlfa2 = aAlfa2 + Fichero + ".dat";
   |COPIA_FICHERO aAlfa1, aAlfa2;
   aAlfa1 = Comodin + ".ddx";
   aAlfa2 = Path; |QBF aAlfa2; aAlfa2 = aAlfa2 + Fichero + ".ddx";
   |COPIA_FICHERO aAlfa1, aAlfa2;
   aAlfa1 = Comodin + ".ixx";
   aAlfa2 = Path; |QBF aAlfa2; aAlfa2 = aAlfa2 + Fichero + ".ixx";
   |COPIA_FICHERO aAlfa1, aAlfa2;
   |SI Fichero = "dscam030";
      |DBASE aAlfa; |QBF aAlfa; aAlfa = aAlfa + "/def/dscam030.mas";
      |CARGA_DEF aAlfa, dscam030@;
      |SI FSalida = 0;
         |PATH_DAT #100 Path; |ABRE #dscam030@;
         |BUCLE PonPath;
         |CIERRA #dscam030@; |DESCARGA_DEF #dscam030@;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO Actualiza;
   |SI #dscaz031#1 = "S";
      |HAZ Actualiza1;
   |SINO;
      |HAZ Actualiza2;
   |FINSI;
|FPRC;

|PROCESO ActPatrones;
   aAlfa = "ta" + Usuario; |NOME_DAT #dscaw095#aAlfa#;
   |DELINDEX #dscaw095; |ABRE #dscaw095;

   |DBASE Path; |QBF Path; Path = Path + "fich/";
   Fichero = "dscam064"; |HAZ Actualiza; FichCab = "dscam063"; || Apoyo aplicacion Lineas   (Campos)
   Fichero = "dscam063"; |HAZ Actualiza; || Apoyo aplicacion Cabecera (Ficheros)

   FichCab = ""; |DFICE Path; |QBF Path;
   |SI PARAMETRO ! "";
      Path = Path + (("00000" + #48#0) % -105) + "/";
   |FINSI;
   Fichero = "dscam065"; |HAZ Actualiza; || Apoyo informes
   Fichero = "dscam029"; |HAZ Actualiza; || Campos utilizables en enlaces
   Fichero = "dscam036"; FichCab = "dscam030"; |HAZ Actualiza; || Acotaciones enlaces
   Fichero = "dscam030"; FichCab = "";         |HAZ Actualiza; || Cabecera enlaces
   Fichero = "dscam031"; FichCab = "dscam030"; |HAZ Actualiza; || Asientos enlaces
   Fichero = "dscam032"; FichCab = "dscam031"; |HAZ Actualiza; || Apuntes enlaces
   Fichero = "dscam088"; FichCab = "";         |HAZ Actualiza; || Apuntes enlaces

   |CIERRA #dscaw095;   |DELINDEX #dscaw095;
|FPRC;

|PROGRAMA;
   |SI PARAMETRO = "";
      |CLS;
      |CABEZA "Actualizacion ficheros patrones";

      |PINPA #0#0; |PINDA #0#0;
      |ENDATOS #1#0;
      |SI FSalida = 0; |Y #0#0 = "S";
         eaUsuario = Usuario % 0810;
         eaDescr   = "ACTUALIZACION DE FICHEROS PATRONES";
         eaTipoReg = "AP";  eaNumero = "";
         eaTipoOp  = "A";   enImpAnt = 0;   enImpAct = 0;
         |HAZ GrabaAudit;

         |HAZ ActPatrones;
      |FINSI;
   |SINO;
      |BLOQUEA_MASTER;
      |SI FSalida < 1; #dscaz031#1 = "A"; |HAZ ActPatrones; |FINSI;
      |DESBLOQUEA_MASTER;
   |FINSI;
|FPRO;
