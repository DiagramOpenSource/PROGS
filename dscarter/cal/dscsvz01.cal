|FICHEROS;
     dscam018;
     dscam019;
     dscam023;
     dscam119;
     dscam020;
     dscam120;

     dscsvw01;
     dscsvw02;
     dscsvw03;
|FIN;

|VARIABLES;
     n1Vent              = 0;
     nGrid18             = 0;
     nGrid2              = 0;
     nGrid3              = 0;
     nRango              = 0;
     nBtnImp             = 0;
     nBtnAct             = 0;
     nHandle             = 0;
     nSwMD5              = 0;
     nError              = 0;
     nReg                = 0;
     nCampo              = 0;
     nTCampos            = 0;
     nTValor             = 0;
     nTotCmp             = 0;
     nPorce              = 0;
     nCmp                = 0;
     nLong               = 0;
     nCarac              = 0;
     nPos                = 0;
     nCar13              = 0;

     aFic                = "";
     aEsc1               = "";
     aEsc2               = "";
     aRetu               = "";
     aTecla              = "";
     aAlfa               = "";
     aAlfa1              = "";
     aAlfa2              = "";
     aUnidad             = "";
     aPathLocal          = "";
     aAbre               = "";
     aIPRemoto           = "";
     aOrigen             = "";
     aDestino            = "";
     aVar                = "";
     aVal1               = "";
     aVal2               = "";
     aVal3               = "";
     aLong               = "";
     aLee                = "";
     aC10                = "";
     aC13                = "";
     aCarac              = "";
     aAzul               = "18,28,245;255,255,255";
     aVerde              = "0,185,0;255,255,255";
     aRojo               = "0,0,0;255,80,80";

     {-1}aVent           = "";
         aVent1          = 0;
         aVent2          = 0;
         aVent3          = 0;
         aVent4          = 0;
         aVent5          = "";

  {1}aContiene           = "";
|FIN;

|PROCESO dscsvw03A;
     aVal1 = #dscsvw03#2;  |QBF aVal1;

     |SI aVal1 = "Riesgo solicitado";
         #dscam020#14 = ~;
         #dscam020#15 = #dscsvw03#4;
     |FINSI;

     |SI aVal1 = "Riesgo concedido";
         #dscam020#16 = ~;
         #dscam020#17 = #dscsvw03#4;
     |FINSI;

     |SI aVal1 = "Estado";
         #dscam020#18 = #dscsvw03#4 % 103;
         #dscam020#19 = #dscsvw03#4 % 520;
     |FINSI;

     |SI aVal1 = "Riesgo a cero";
         #dscam020#16 = ~;
         #dscam020#17 = 0;
     |FINSI;

     |BORRA 020#dscsvw03.a;
     |LIBERA #dscsvw03;

     #dscam120#0 = #dscam020#0;
     #dscam120#1 = #dscam020#2;
     #dscam120#2 = 99999;
     |LEE 000#dscam120.];
     |SI FSalida = 0;
         |LEE 000#dscam120.a;
     |SINO;
         |LEE 000#dscam120.u;
     |FINSI;

     nReg = 1;
     |SI FSalida = 0;  |Y #dscam120#0 = #dscam020#0;
                       |Y #dscam120#1 = #dscam020#2;
         nReg = #dscam120#2 + 1;
     |FINSI;

     |DDEFECTO #dscam120;
     #dscam120#0 = #dscam020#0;
     #dscam120#1 = #dscam020#2;
     #dscam120#2 = nReg;
     #dscam120#3 = #dscsvw03#2;
     #dscam120#4 = ~;
     #dscam120#5 = #dscsvw03#3;
     #dscam120#6 = #dscsvw03#4;

     |GRABA 020#dscam120.n;
     |LIBERA #dscam120;
|FPRC;

|DEFBUCLE dscsvw03A;
     #dscsvw03#1;
     ;
     #dscsvw02#0, #dscsvw02#1, "";
     #dscsvw02#0, #dscsvw02#1, "zzzzzzzzzzzzzzzzzzzz";
     be;
     NULL, dscsvw03A;
|FIN;

|PROCESO dscsvw02A;
     |SELECT #4#dscam020;
     #dscam020#0 = #dscam018#0;
     #dscam020#3 = #dscsvw02#5;
     |LEE 101#dscam020.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |BUCLE dscsvw03A;

     |GRABA 020#dscam020.a;
     |LIBERA #dscam020;

     #dscam023#0 = #dscam020#4;
     |LEE 101#dscam023.=;
     |SI FSalida = 0;
         #dscam023#11 = #dscam020#15;
         #dscam023#12 = #dscam020#14;
         #dscam023#13 = #dscam020#17;
         #dscam023#14 = #dscam020#16;

         #dscam023#19 = #dscam023#13 + #dscam023#15 + #dscam023#17;

         |GRABA 020#dscam023.a;
         |LIBERA #dscam023;
     |FINSI;

     #dscsvw02#14 = "V";
     #dscsvw02#15 = aVerde;

     |GRABA 020#dscsvw02.a;
     |LIBERA #dscsvw02;
|FPRC;

|DEFBUCLE dscsvw02A;
     #dscsvw02#1;
     14;
     #dscam018#0, 0000, "A";
     #dscam018#0, 9999, "A";
     be;
     NULL, dscsvw02A;
|FIN;

|PROCESO ActualizaTodo;
     |CONFI "0000SEst  seguro de actualizar los registros";
     |SI FSalida ! 0;
         |FOCOTECLADO nGrid2;
         |ACABA;
     |FINSI;

     |BUCLE dscsvw02A;

     |REFRESCACONTROL nGrid2;
     |FOCOTECLADO nGrid2;
|FPRC;

|PROCESO dscsvw01Campos;
     nTCampos  = nTCampos + 1;
|FPRC;

|DEFBUCLE dscsvw01Campos;
     #dscsvw01#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dscsvw01Campos;
|FIN;

|PROCESO dscsvw01Carga;
     nCampo = #dscsvw01#0;

     #dscsvw02#0 = #dscam018#0;
     #dscsvw02#1 = nReg - 1;

     |SI nCampo = 1;   #dscsvw02#2  = #dscsvw01#1;  |FINSI;
     |SI nCampo = 2;   #dscsvw02#3  = #dscsvw01#1;  |FINSI;
     |SI nCampo = 3;   #dscsvw02#4  = #dscsvw01#1;  |FINSI;
     |SI nCampo = 4;   #dscsvw02#5  = #dscsvw01#1;  |FINSI;
     |SI nCampo = 5;   #dscsvw02#6  = #dscsvw01#1;  |FINSI;
     |SI nCampo = 6;   #dscsvw02#7  = #dscsvw01#1;  |FINSI;
     |SI nCampo = 7;   #dscsvw02#8  = #dscsvw01#1;  |FINSI;
     |SI nCampo = 8;   #dscsvw02#9  = #dscsvw01#1;  |FINSI;
     |SI nCampo = 9;   #dscsvw02#10 = #dscsvw01#1;  |FINSI;
     |SI nCampo = 10;  #dscsvw02#11 = #dscsvw01#1;  |FINSI;
     |SI nCampo = 11;  #dscsvw02#12 = #dscsvw01#1;  |FINSI;
     |SI nCampo = 12;  #dscsvw02#13 = #dscsvw01#1;  |FINSI;
|FPRC;

|DEFBUCLE dscsvw01Carga;
     #dscsvw01#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dscsvw01Carga;
|FIN;

|PROCESO Grabaw03;
     |SI aVal1 = "Estado";
         #dscam019#0 = aVal2;
         |LEE 000#dscam019.=;
         |SI FSalida = 0;
             aVal2 = aVal2 + "-" + #dscam019#1;
         |FINSI;

         #dscam019#0 = aVal3;
         |LEE 000#dscam019.=;
         |SI FSalida = 0;
             aVal3 = aVal3 + "-" + #dscam019#1;
         |FINSI;
     |FINSI;

     #dscsvw02#14 = "A";
     #dscsvw02#15 = aAzul;

     #dscsvw03#0  = #dscsvw02#0;
     #dscsvw03#1  = #dscsvw02#1;
     #dscsvw03#2  = aVal1;
     #dscsvw03#3  = aVal2;
     #dscsvw03#4  = aVal3;
     |GRABA 020#dscsvw03.n;
     |LIBERA #dscsvw03;
|FPRC;

|PROCESO GrabaDatos;
     |CIERRA #dscsvw01;
     nTCampos = 0;
     |BUCLE dscsvw01Campos;

     nError   = 0;
     nTValor  = nTotCmp;
     |SI nTCampos < nTValor;
         |DELINDEX #dscsvw01;
         |ACABA;
     |FINSI;

     nPorce = ((nTValor * 100) / nTCampos) ! 0;
     |SI nPorce < 70;
         |DELINDEX #dscsvw01;
         |ACABA;
     |FINSI;

     nReg = nReg + 1;
     |SI nReg = 1;
         |DELINDEX #dscsvw01;
         |ACABA;
     |FINSI;

     |DDEFECTO #dscsvw02;
     |BUCLE dscsvw01Carga;

     #dscsvw02#14 = "R";
     #dscsvw02#15 = aRojo;

     |SELECT #4#dscam020;
     #dscam020#0 = #dscam018#0;
     #dscam020#3 = #dscsvw02#5;
     |LEE 101#dscam020.=;
     |SI FSalida = 0;
         #dscsvw02#14 = "V";
         #dscsvw02#15 = aVerde;

         |SELECT #2#dscam119;
         #dscam119#1 = #dscsvw02#8;
         |LEE 000#dscam119.=;
         |SI FSalida ! 0;  |DDEFECTO #dscam119;  |FINSI;

         #dscam019#0 = #dscam119#0;
         |LEE 000#dscam019.=;
         |SI FSalida = 0;  |Y #dscam019#2 = "S";
             |SI #dscam020#17 ! 0;
                 aVal1 = "Riesgo a cero";
                 aVal2 = "Riesgo concedido: " + #dscam020#17;
                 aVal3 = "Riesgo concedido: " + 0;
                 |HAZ Grabaw03;
             |SINO;
                 #dscsvw02#11 = 0;
             |FINSI;
         |FINSI;

         |SI #dscam020#15 ! #dscsvw02#10;
             aVal1 = "Riesgo solicitado";
             aVal2 = #dscam020#15;
             aVal3 = #dscsvw02#10;
             |HAZ Grabaw03;
         |FINSI;

         |SI #dscam020#17 ! #dscsvw02#11;
             aVal1 = "Riesgo concedido";
             aVal2 = #dscam020#17;
             aVal3 = #dscsvw02#11;
             |HAZ Grabaw03;
         |FINSI;

         |SI #dscam119#0 ! #dscam020#18;
             aVal1 = "Estado";
             aVal2 = #dscam020#18;
             aVal3 = #dscam119#0;
             |HAZ Grabaw03;
         |FINSI;

         |LIBERA #dscam020;
     |FINSI;

     |GRABA 000#dscsvw02.n;
     |LIBERA #dscsvw02;

     |DELINDEX #dscsvw01;
     |PULSATECLA;
|FPRC;

|PROCESO Grabaw01;
     #dscsvw01#0 = nCmp;
     #dscsvw01#1 = aVar;

     |GRABA 020#dscsvw01.n;
     |LIBERA #dscsvw01;
|FPRC;

|PROCESO SacaDatos;
     aLong  = aLee % 0;
     nLong  = aLong;
     aC10   = &10;
     aC13   = &13;
     nCar13 = 0;
     |PARA nCarac = 1;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
           nPos   = (nCarac * 100) + 1;
           aCarac = aLee % nPos;

           |SI aCarac = aC13;
               aCarac = ";";
               nCar13 = 1;
           |FINSI;

           |SI aCarac = aC10;
               |SI nCar13 = 0;   aCarac = ";";  |FINSI;
               |SI nCar13 = 1;   aCarac = "";   |FINSI;

               nCar13 = 0;
           |FINSI;

           |SI aCarac = ";";
               nCmp   = nCmp + 1;
               |HAZ Grabaw01;

               aVar   = "";
               aCarac = "";
           |FINSI;

           |SI nCmp = nTotCmp;  |Y nCmp ! 0;
               |HAZ GrabaDatos;

               |ABRE #dscsvw01;

               nCmp   = 0;
               nCar13 = 0;
           |FINSI;

           aVar = aVar + aCarac;
      |SIGUE;
|FPRC;

|PROCESO dscsvw03B;
     |BORRA 020#dscsvw03.a;
     |LIBERA #dscsvw03;
|FPRC;

|DEFBUCLE dscsvw03B;
     #dscsvw03#1;
     ;
     #dscsvw02#0, #dscsvw02#1, "";
     #dscsvw02#0, #dscsvw02#1, "zzzzzzzzzzzzzzzzzzzz";
     be;
     NULL, dscsvw03B;
|FIN;

|PROCESO dscsvw02B;
     |BUCLE dscsvw03B;

     |BORRA 020#dscsvw02.a;
     |LIBERA #dscsvw02;
|FPRC;

|DEFBUCLE dscsvw02B;
     #dscsvw02#1;
     ;
     #dscam018#0, 0000;
     #dscam018#0, 9999;
     be;
     NULL, dscsvw02B;
|FIN;

|PROCESO LeeCSV;
     aAlfa = "Procesando registros del fichero " + aAbre;
     |VENTANA 0501, 0540, -1, 16, aAlfa;
     n1Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     |ABRE #dscsvw01;  |CIERRA #dscsvw01;  |DELINDEX #dscsvw01;

     |ABRE #dscsvw01;

     |BUCLE dscsvw02B;

     nTotCmp = 12;
     nCmp    = 0;
     nReg    = 0;
     |XABRE "BC", aAbre, nHandle;
     |PARA;  |SI;  |HACIENDO;
          |XLEE nHandle, 255, aLee;
          |SI FSalida [ 0;  |SAL;  |FINSI;

          |HAZ SacaDatos;
     |SIGUE;
     |XCIERRA nHandle;

     |LEE 000#dscsvw01.p;
     |SI FSalida = 0;
         nCmp   = nCmp + 1;

         |HAZ Grabaw01;
         |HAZ GrabaDatos;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n1Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n1Vent;
|FPRC;

|PROCESO LeeLOG;
     aAbre = aPathLocal + "dsftp.log";   |RFBORRA aAlfa;
     |XABRE "C", aAbre, nHandle;
     |SI FSalida ] 0;
         |XLEE nHandle, 250, aAlfa;  |QBF aAlfa;
         aAlfa = "0000" + aAlfa;
         |MENAV aAlfa;
     |FINSI;
     |XCIERRA nHandle;
|FPRC;

|PROCESO Baja3;
     #dscsvw03#0 = #dscam018#0;
     #dscsvw03#1 = #dscsvw02#1;
     #dscsvw03#2 = "";
|FPRC;

|PROCESO Alta3;
     #dscsvw03#0 = #dscam018#0;
     #dscsvw03#1 = #dscsvw02#1;
     #dscsvw03#2 = "zzzzzzzzzzzzzzzzzzzz";
|FPRC;

|PROCESO Baja2;
     #dscsvw02#0 = #dscam018#0;
     #dscsvw02#1 = 1;
|FPRC;

|PROCESO Alta2;
     #dscsvw02#0 = #dscam018#0;
     #dscsvw02#1 = 9999;
|FPRC;

|PROCESO Evnt2;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#dscsvw02.=;

         |ESTADO_CONTROL nBtnAct, "ENABLE";

         |REFRESCACONTROL nGrid3;
     |FINSI;

     |SI FSalida = 4;
         |LEE 101#dscsvw02.=;
         |SI FSalida ! 0;  |ACABA;  |FINSI;

         |SI #dscsvw02#14 = "V";
             |MENAV "0000El registro est  actualizado";
             |ACABA;
         |FINSI;

         |SI #dscsvw02#14 = "R";
             |MENAV "0000No existe un expediente en clasificaciones con este registro";
             |ACABA;
         |FINSI;

         |CONFI "0000SEst  seguro de actualizar el registro seleccionado";
         |SI FSalida ! 0;  |ACABA;  |FINSI;

         |HAZ dscsvw02A;
         |REFRESCACONTROL nGrid2;
     |FINSI;
|FPRC;

|PROCESO Evnt18;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#dscam018.=;

         |ESTADO_CONTROL nBtnImp, "ENABLE";
         |ESTADO_CONTROL nBtnAct, "DISABLE";

         aAlfa = #dscam018#17 + #dscam018#18 + #dscam018#19;
         aAlfa = aAlfa        + #dscam018#20 + #dscam018#21;
         |QBF aAlfa;

         |SI aAlfa = "";
             |ESTADO_CONTROL nBtnImp, "DISABLE";
         |FINSI;

         |REFRESCACONTROL nGrid3;
         |REFRESCACONTROL nGrid2;
     |FINSI;
|FPRC;


|PROCESO Grabascript;
     |QBF aAlfa;
     aAlfa = aAlfa + &13 + &10;
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO MD5OrigDest;
     nSwMD5 = 1;
     aContiene1 = aDestino;
     |ESPECIFICA "REMOTOMD5", aContiene;
     aAlfa = FSalida;  |QBF aAlfa;

     aContiene1 = aOrigen;
     |ESPECIFICA "MD5", aContiene;
     aAlfa1 = FSalida;  |QBF aAlfa1;

     |SI aAlfa ! aAlfa1; nSwMD5 = 0; |FINSI;
|FPRC;

|PROCESO CopiaMD5;
     aIPRemoto = ""; |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;
     |HAZ MD5OrigDest;

     |SI nSwMD5 = 0;
         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aOrigen, aDestino;
         |SINO;
             |COPIA_FICHERO aOrigen, aDestino;
         |FINSI;
     |FINSI;

     |XABRE "EC", aDestino, nHandle;
     |SI FSalida < 0;
         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aOrigen, aDestino;
         |SINO;
             |COPIA_FICHERO aOrigen, aDestino;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO RecogeDSFTP;
     aContiene = "";
     |ESPECIFICA "RBASS", aContiene;
     aAlfa  = aContiene;
     aAlfa1 = "";
     aAlfa2 = aAlfa % 0101;
     aAlfa = aAlfa % 0299;
     |PARA; |SI aAlfa2 ! "/"; |Y aAlfa2 ! "\"; |Y aAlfa ! ""; |HACIENDO;
            aAlfa1 = aAlfa1 + aAlfa2;
            aAlfa2 = aAlfa % 0101;
            aAlfa  = aAlfa % 0299;
     |SIGUE;

     aUnidad = aAlfa1 + "\";

     aAlfa = aUnidad + "\DOCUMENTOS";  |RMKDIR aAlfa;
     aAlfa = aAlfa + "\CSV";           |RMKDIR aAlfa;

     |DBASE aAlfa;   |QBF aAlfa;
     aOrigen  = aAlfa  + "plugins/dsftp.exe";
     aDestino = aAlfa1 + "\DOCUMENTOS\CSV\dsftp.exe";
     |HAZ CopiaMD5;
|FPRC;


|PROCESO TraeteCSV;
     |HAZ RecogeDSFTP;

     aPathLocal = aUnidad + "DOCUMENTOS\CSV\";

     aAlfa = aPathLocal + "dsftp.bat";   |RFBORRA aAlfa;
     aAlfa = aPathLocal + #dscam018#21;  |QBF aAlfa;
                                         |RFBORRA aAlfa;
     aAlfa = aPathLocal + "dsftp.log";   |RFBORRA aAlfa;
     aAlfa = aPathLocal + "script.ftp";  |RFBORRA aAlfa;

     aAbre = aPathLocal + "dsftp.bat";
     |XABRE "WBC", aAbre, nHandle;

     aAlfa = "@ECHO OFF";
     |HAZ Grabascript;

     aAlfa = ":: Creamos el script";
     |HAZ Grabascript;

     aAlfa = ">" + aPathLocal + "script.ftp ECHO " + #dscam018#18;
     |HAZ Grabascript;

     aAlfa = ">>" + aPathLocal + "script.ftp ECHO " + #dscam018#19;
     |HAZ Grabascript;

     aAlfa = ">>" + aPathLocal + "script.ftp ECHO lcd " + aPathLocal;
     |HAZ Grabascript;

     aAlfa = ">>" + aPathLocal + "script.ftp ECHO bin";
     |HAZ Grabascript;

     aAlfa = ">>" + aPathLocal + "script.ftp ECHO cd " + #dscam018#20;
     |HAZ Grabascript;

     aAlfa = ">>" + aPathLocal + "script.ftp ECHO get " + #dscam018#21;
     |HAZ Grabascript;

     aAlfa = ">>" + aPathLocal + "script.ftp ECHO bye";
     |HAZ Grabascript;

     aAlfa = ":: Usamos el archivo recien creado";
     |HAZ Grabascript;

     aAlfa = aPathLocal + "dsftp.exe " + aPathLocal + "script.ftp " + #dscam018#17;
     |QBF aAlfa;
     aAlfa = aAlfa + " " + aPathLocal + "dsftp.log ";
     |HAZ Grabascript;

     aAlfa = "TYPE NUL > " + aPathLocal + "script.ftp";
     |HAZ Grabascript;

     aAlfa = "DEL " + aPathLocal + "script.ftp";
     |HAZ Grabascript;

     aAlfa = ":End";
     |HAZ Grabascript;

     |XCIERRA nHandle;

     aAlfa = "cmd " + &34 + "/c " + aPathLocal + "dsftp.bat " + &34;
     |RSYSTEM aAlfa;

     aAbre = aPathLocal + #dscam018#21;  |QBF aAbre;
     |XABRE "EC", aAbre, 0;
     |SI FSalida ] 0;
         |HAZ LeeCSV;
     |SINO;
         |HAZ LeeLOG;
     |FINSI;

     aAlfa = aPathLocal + "dsftp.bat";   |RFBORRA aAlfa;
     aAlfa = aPathLocal + #dscam018#21;  |QBF aAlfa;
                                         |RFBORRA aAlfa;
     aAlfa = aPathLocal + "dsftp.log";   |RFBORRA aAlfa;
|FPRC;

|PROCESO Importa;
     aAlfa = "0000SQuiere importar los datos de la compa¤¡a " + #dscam018#1;
     |QBF aAlfa;

     |CONFI aAlfa;
     |SI FSalida ! 0;
         |FOCOTECLADO nGrid18;
         |ACABA;
     |FINSI;

     |HAZ TraeteCSV;

     |REFRESCACONTROL nGrid2;
     |FOCOTECLADO nGrid2;
|FPRC;


|PROGRAMA;
     |CONTROL_SIMPLE 0, "LABEL,{{15}}Importaci¢n CSV CyC", 0502, 0530, NULL;

     aAlfa = "LABEL,{{7}}Colores: Rojo-Expediente no encontrado en clasificaciones. ";
     aAlfa = aAlfa + " Azul-Expediente con cambios ";
     aAlfa = aAlfa + " Verde-Expediente sin cambios ";
     |CONTROL_SIMPLE 0, aAlfa, 1202, 1290, NULL;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Importar CSV", 1185, 1198, Importa;
     nBtnImp = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Actualizar todo", 2485, 2498, ActualizaTodo;
     nBtnAct = FSalida;

     |CONTROL_SIMPLE 0, "LABEL,{{11}}Doble click en la l¡nea para su actualizaci¢n", 2402, 2480, NULL;

     |ESTADO_CONTROL nBtnImp, "DISABLE";
     |ESTADO_CONTROL nBtnAct, "DISABLE";

     nRango = 4 + 8 + 16 + 32 + 128;
     |LINEAL_SIMPLE #1#dscam018, nRango, 0602, 1098, NULL, NULL, Evnt18;
     nGrid18 = FSalida;

     |LINEAL_SIMPLE #1#dscsvw02, nRango, 1302, 2398, Baja2, Alta2, Evnt2;
     nGrid2 = FSalida;

     |LINEAL_SIMPLE #1#dscsvw03, nRango, 2602, 3098, Baja3, Alta3, NULL;
     nGrid3 = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}Salir," + nGrid18;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;
|FPRO;

|PROCESO T80;  |TIPO 80;
     aFic = "dscsvw01" + Usuario;  |NOME_DAT #dscsvw01#aFic#;
     aFic = "dscsvw02" + Usuario;  |NOME_DAT #dscsvw02#aFic#;
     aFic = "dscsvw03" + Usuario;  |NOME_DAT #dscsvw03#aFic#;

     |ABRE #dscsvw01;  |CIERRA #dscsvw01;  |DELINDEX #dscsvw01;
     |ABRE #dscsvw02;  |CIERRA #dscsvw02;  |DELINDEX #dscsvw02;
     |ABRE #dscsvw03;  |CIERRA #dscsvw03;  |DELINDEX #dscsvw03;

     |ABRET #dscam018;
     |ABRET #dscam019;
     |ABRET #dscam023;
     |ABRET #dscam119;
     |ABRET #dscam020;
     |ABRET #dscam120;
     |ABRET #dscsvw02;
     |ABRET #dscsvw03;

     FSalida = 3299;
|FPRC;

|PROCESO T90;  |TIPO 90;
     |CIERRAT #dscam018;
     |CIERRAT #dscam019;
     |CIERRAT #dscam023;
     |CIERRAT #dscam119;
     |CIERRAT #dscam020;
     |CIERRAT #dscam120;
     |CIERRAT #dscsvw02;  |DELINDEX #dscsvw02;
     |CIERRAT #dscsvw03;  |DELINDEX #dscsvw03;

     |ABRE #dscsvw01;  |CIERRA #dscsvw01;  |DELINDEX #dscsvw01;
|FPRC;
