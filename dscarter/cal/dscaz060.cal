|FICHEROS;
 dscaz060 #0;
 dscam073 #1;
 dscam049 #2;  || Confirming Cabeceras
 dscam050 #3;  || Confirming Lineas
 dscam008 #4;  || Mantenimiento pagos
 dscam030 #5;  || Mantenimiento enlaces
 dscaw013 #6;  || Temporal enlaces
 dscam051 #7;  || Parametros generales

/*
 caclipro #4;  || Clientes/Proveedores
 camanpag #5;  || Pagos
 caivasop #6;  || Iva
 caconcep #7;  || Concepto
*/
 dscam014 #8;  || Bancos..............
|FIN;

|VARIABLES;
 &fecha = @;
 &fp = "";
 &eaDirProv = "";
 &eaNifProv = "";
 &HayConta = 0;
 &aInforme = "";

 &eaCodigo    = "";
 &eaTipoCod   = "";
 &aCuenta     = "";
 &eaNif       = "";
 &eaNombre    = "";
 &eaDireccion = "";
 &eaPoblacion = "";
 &eaProvincia = "";
 &eaCP        = "";
 &eaCP1       = "";
 &eaCP2       = "";
 &eaTelefono  = "";
 &eaFax       = "";
 &eaEmail     = "";
 &eaCadena    = "";
 &eaPais      = "";

 &TipoDato = "";
 &Long     = 0;
 &LongDec  = 0;

 nSwError = 0;
 nImprime = 0;

 nCalc = 0;
 nCalc1 = 0;
 nCalc2 = 0;
 Regs  = 0;
 &eaNombre1 = "";
 &eaOrigen  = "";
 aDestino = "";
 Comodin  = "";
 nSwImpre = 0;

 &Niv1 = 0;
 &Niv2 = 0;
 &Niv3 = 0;
 &Niv4 = 0;
 &Niv5 = 0;
 &Niv6 = 0;
 &Divisa = "";

 aCuenta1 = ""; nNivel = 0;

 alfa1 = "";
 alfa2 = "";
 alfa3 = "";
 alfa4 = "";
 nume1 = 0;
 nume2 = 0;
 nume3 = 0;
 nume4 = 0;

 mens     = "";
 swErrorR = 0;
 swErrorP = 0;
 menR1    = "    No Existe Confirme !!!";
 menR2    = "    Confirme YA contabilizado ";
 menR3    = "2400SConfirme YA Emitido en Papel. Aceptar S/N ";
 menR4    = "2400NConfirme YA lanzado en Diskette. Aceptar S/N ";

 Nombre  = "";
 CodCli  = "";
 sino     = "";
 ceros    = "000000000000000";
 fechapre = "      ";
 dd       = "";
 mm       = "";
 aa       = "";

 l          = 0;
 trecibos   = 0;
 timporte   = 0;

 NifProv    = "";
 NombreProv = "";
 DirecProv  = "";
 PoblaProv  = "";
 ProviProv  = "";
 Cp1Prov    = "";
 Cp2Prov    = "";
 TelefProv  = "";
 FaxProv    = "";
 PaisProv   = "";
 Entidad    = "";
 Sucursal   = "";
 DC         = "";
 Cuenta     = "";
 varios     = 0;

 handle     = "";
 fiche      = "";
 dirempr    = "";
 varalfa1   = "";
 varalfa2   = "";
 contenido  = "";
 swERROR    = 0;
 CampoDni   = "";
 Letra1     = "";
 Letra2     = "";
 Letra3     = "";
 Letra4     = "";
 Letra5     = "";
 Letra6     = "";
 Letra7     = "";
 Letra8     = "";
 Caracter   = "";
 Carac1     = 0;
 Carac2     = 0;
 Carac3     = 0;
 Carac4     = 0;
 Carac5     = 0;
 XPara      = 0;

 ccc        = "";
 c_alfa1    = "";
 c_alfa2    = "";
 c_nume1    = 0;
 c_nume2    = 0;
 c_nume3    = 0;

 direcc = "";
 numero = 0;
 longi  = 0;
 concepto = "";
 TotConf = 0; TotFact = 0; TotRegs = 0;

 &programa = "";
 &ficcopia = "";
 aAlfa  = "";
 aAlfa1 = "";

 aProvAnt = "";
 aCtaPAnt = "";

     &eaVarDni  = "";
     &enCalcCif = 0;

{20}VAlfa = "";
|FIN;

|| *************************************************************************

|PROCESO ver_confirming; |TIPO 6;
 |SI FSalida = 2; |ACABA; |FINSI;
 |ABRE #dscam049;
 |HAZ leerem;
 |CIERRA #dscam049;
 |SI swErrorR ! 0;
     |SI swErrorR < 3;
         |MENAV mens;
         |ERROR;
         |ACABA;
     |SINO;
         |CONFI mens;
         |SI FSalida ! 0;
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;
 |FINSI;
 #dscaz060#2 = #dscam049#3; || Banco
 |HAZ TomaDatos;
 ||#dscaz060#5 = #dscam049#6; |PINTA #0#5; || Codigo cliente entidad
 #dscaz060#3 = #dscam049#1; |PINTA #0#3; || Fecha presentacion

 |PINTA 0935, #dscam049#2;   || Vto.
 |PINTA 0958, #dscam049#5;   || N§ Efectos
 |PINTA 1035, #dscam049#3;   || Banco
 |PINTA 1049, #dscam049#19;  || Importe Total
|FPRC;

|PROCESO leerem;
 swErrorR = 0;
 |DDEFECTO #dscam049;
 #dscam049#0 = #dscaz060#1;
 |LEE 001#dscam049.=;
 |SI FSalida ! 0;
     swErrorR = 1; mens = menR1;
 |SINO;
     Divisa = #dscam049#16; |HAZ TomaDecim;
     |SI #dscam049#8 = "S";
         swErrorR = 2; mens = menR2;
     |SINO;
         |SI #dscam049#7 = "S"; swErrorR = 3; mens = menR3; |FINSI;
         |SI #dscam049#7 = "D"; swErrorR = 4; mens = menR4; |FINSI;
     |FINSI;
     |ABRE #dscam014;
     #dscam014#0 = #dscam049#3;
     |LEE 000#dscam014.=;
     |CIERRA #dscam014;
 |FINSI;
|FPRC;

||...................................................................................
||***********************************************************
||************ REGISTRO DE CABECERA *************************
||***********************************************************
|PROCESO Cabecera;
  VAlfa1 = #dscaz060#15;
  VAlfa2 = " " * 12;
  VAlfa3 = " " * 10;
  VAlfa4 = ("0000" + #dscam073#4) % -104;
  VAlfa5 = "EUR00";
  VAlfa6 = " " * 26;

  aAlfa = "001" + VAlfa1 + VAlfa2 + Nombre + VAlfa3 + VAlfa4 + VAlfa5 + VAlfa6;
  eaCadena = aAlfa; |HAZ QuitaRaros; aAlfa = eaCadena;
  aAlfa = handle + " " + aAlfa + &#0#7 + &#0#8; |FGRABA aAlfa;
  TotConf = 0; TotFact = 0; TotRegs = 1;
|FPRC;

||***********************************************************
||******************* REGISTRO PROVEEDOR ********************
||***********************************************************
|PROCESO Facturas;
   #dscam008#40 = #dscam050#13;
   |LEE 000#dscam008.=;
   eaCodigo = #dscam008#32; eaTipoCod = "P"; aCuenta = #dscam008#5; |HAZ LeeDatos;

   eaNifProv = eaNif; eaDirProv = eaDireccion;
   |SI nSwImpre = 1; |PRINT; |FINSI;

   |SI aProvAnt ! #dscam008#32; |O aCtaPAnt ! #dscam008#5;
      aProvAnt = #dscam008#32; aCtaPAnt = #dscam008#5;

      VAlfa1 = "010";
      VAlfa2 = #dscaz060#15;
      |QBF eaNif;
      VAlfa3 = ("            " + eaNif) % -112;
      VAlfa4 = ("         "    + eaNif) % -109;
      |QBF eaNombre;
      VAlfa5 = (eaNombre + (" " * 72)) % 0172;
      VAlfa6 = " " * 4;
      aAlfa = VAlfa1 + VAlfa2 + VAlfa3 + VAlfa4 + VAlfa5 + VAlfa6;
      eaCadena = aAlfa; |HAZ QuitaRaros; aAlfa = eaCadena;
      aAlfa = handle + " " + aAlfa + &#0#7 + &#0#8; |FGRABA aAlfa;

      VAlfa1 = "011";
      VAlfa2 = #dscaz060#15;
      |QBF eaNif;
      VAlfa3 = ("            " + eaNif) % -112;
      |QBF eaDireccion;
      VAlfa4 = (eaDireccion + (" " * 72)) % 0172;
      VAlfa5 = " " * 13;
      aAlfa = VAlfa1 + VAlfa2 + VAlfa3 + VAlfa4 + VAlfa5;
      eaCadena = aAlfa; |HAZ QuitaRaros; aAlfa = eaCadena;
      aAlfa = handle + " " + aAlfa + &#0#7 + &#0#8; |FGRABA aAlfa;

      VAlfa1 = "012";
      VAlfa2 = #dscaz060#15;
      |QBF eaNif;
      VAlfa3 = ("            " + eaNif) % -112;
      |QBF eaPoblacion;
      VAlfa4 = (eaPoblacion + (" " * 36)) % 0136;
      VAlfa5 = ("00" + eaCP1) % -102 + ("000" + eaCP2) % -103;
      VAlfa6 = "   ";
      |QBF eaProvincia;
      VAlfa7 = (eaProvincia + (" " * 36)) % 0136;
      |QBF eaPais; |SI eaPais = ""; eaPais = "ES"; |FINSI;
      VAlfa8 = eaPais;
      VAlfa8 = VAlfa8 % 0102; VAlfa8 = VAlfa8 + VAlfa8;
      VAlfa8 = VAlfa8 + " ";
      aAlfa = VAlfa1 + VAlfa2 + VAlfa3 + VAlfa4 + VAlfa5 + VAlfa6 + VAlfa7 + VAlfa8;
      eaCadena = aAlfa; |HAZ QuitaRaros; aAlfa = eaCadena;
      aAlfa = handle + " " + aAlfa + &#0#7 + &#0#8; |FGRABA aAlfa;

      VAlfa1 = "013";
      VAlfa2 = #dscaz060#15;
      |QBF eaNif;
      VAlfa3 = ("            " + eaNif) % -112;
      |QBF eaTelefono;
      VAlfa4 = (eaTelefono + (" " * 15)) % 0115;
      |QBF eaFax;
      VAlfa5 = (eaFax + (" " * 15)) % 0115;
      VAlfa6 = (eaEmail + (" " * 55)) % 0155;
      aAlfa = VAlfa1 + VAlfa2 + VAlfa3 + VAlfa4 + VAlfa5 + VAlfa6;
      eaCadena = aAlfa; |HAZ QuitaRaros; aAlfa = eaCadena;
      aAlfa = handle + " " + aAlfa + &#0#7 + &#0#8; |FGRABA aAlfa;
      TotRegs = TotRegs + 4;
   |FINSI;

   VAlfa1 = "020";
   VAlfa2 = #dscaz060#15;
   |QBF eaNif;
   VAlfa3 = ("            " + eaNif) % -112;
   VAlfa4 = " " * 12;
/*
   VAlfa5 = ("00" + #dscam050#2) % -102; || Numero externo de la factura
   VAlfa5 = VAlfa5 + ("/" + #dscam050#4 + "/");
   VAlfa5 = VAlfa5 + (("000000" + #dscam050#3) % -106) + "/";
   VAlfa5 = VAlfa5 + (("000" + #dscam050#5) % -103);
*/
   VAlfa5 = #dscam050#35; |QBF VAlfa5; || Numero externo de la factura
   VAlfa5 = (VAlfa5 + "            ") % 0112;
   |SI #dscam050#7 < 0;
      VAlfa6 = "C";
   |SINO;
      VAlfa6 = "A";
   |FINSI;
   TipoDato = "N"; Long = 10; LongDec = 2; eaCadena = #dscam050#7; |HAZ Formateo;
   TotConf = TotConf + #dscam050#7;
   TotFact = TotFact + 1;
   VAlfa7 = eaCadena;
   VAlfa8 = " " * 48;
   aAlfa = VAlfa1 + VAlfa2 + VAlfa3 + VAlfa4 + VAlfa5 + VAlfa6 + VAlfa7 + VAlfa8;
   eaCadena = aAlfa; |HAZ QuitaRaros; aAlfa = eaCadena;
   aAlfa = handle + " " + aAlfa + &#0#7 + &#0#8; |FGRABA aAlfa;

   VAlfa1 = "021";
   VAlfa2 = #dscaz060#15;
   |QBF eaNif;
   VAlfa3 = ("            " + eaNif) % -112;
   VAlfa4 = #dscam008#4; |QBF VAlfa4; VAlfa4 = VAlfa4 - "."; VAlfa4 = VAlfa4 - ".";
   VAlfa5 = #dscam008#3; |QBF VAlfa5; VAlfa5 = VAlfa5 - "."; VAlfa5 = VAlfa5 - ".";
   |SI #dscam050#16 = "C"; VAlfa6 = "CH"; |FINSI;
   |SI #dscam050#16 = "T"; VAlfa6 = "TR"; |FINSI;
   |SI #dscam050#16 ! "C"; |Y #dscam050#16 ! "T";
      VAlfa6 = "    El recibo n§ " + #dscam050#13 + " contiene una forma de pago incorrecta [" + #dscam050#16 + "]";
      |MENAV VAlfa6;
      VAlfa6 = "  ";
   |FINSI;

   VAlfa7 = " " * 67;
   aAlfa = VAlfa1 + VAlfa2 + VAlfa3 + VAlfa4 + VAlfa5 + VAlfa6 + VAlfa7;
   eaCadena = aAlfa; |HAZ QuitaRaros; aAlfa = eaCadena;
   aAlfa = handle + " " + aAlfa + &#0#7 + &#0#8; |FGRABA aAlfa;

   VAlfa1 = "022";
   VAlfa2 = #dscaz060#15;
   |QBF eaNif;
   VAlfa3 = ("            " + eaNif) % -112;
   VAlfa4 = #dscam008#8;  |QBF VAlfa4; VAlfa4 = ("0000" + VAlfa4) % -104;
   VAlfa5 = #dscam008#9;  |QBF VAlfa5; VAlfa5 = ("0000" + VAlfa5) % -104;
   VAlfa6 = #dscam008#10; |QBF VAlfa6; VAlfa6 = ("00" + VAlfa6) % -102;
   VAlfa7 = #dscam008#11; |QBF VAlfa7; VAlfa7 = ("0000000000" + VAlfa7) % -110;
   VAlfa8 = " " * 65;
   aAlfa = VAlfa1 + VAlfa2 + VAlfa3 + VAlfa4 + VAlfa5 + VAlfa6 + VAlfa7 + VAlfa8;
   eaCadena = aAlfa; |HAZ QuitaRaros; aAlfa = eaCadena;
   aAlfa = handle + " " + aAlfa + &#0#7 + &#0#8; |FGRABA aAlfa;
   TotRegs = TotRegs + 3;
|FPRC;

|DEFBUCLE Facturas;
#dscam050#1;
;
#dscaz060#1,001;
#dscaz060#1,999;
;
NULL,Facturas;
|FIN;

||***********************************************************
||*******************  REGISTRO TOTALES  ********************
||***********************************************************

|PROCESO Totales;
   TotRegs = TotRegs + 1;
   VAlfa1 = "099";
   VAlfa2 = #dscaz060#15;
   VAlfa3 = " " * 12;
   TipoDato = "N"; Long = 10; LongDec = 2; eaCadena = TotConf; |HAZ Formateo;
   VAlfa4 = eaCadena;
   TipoDato = "N"; Long = 10; LongDec = 0; eaCadena = TotFact; |HAZ Formateo;
   VAlfa5 = eaCadena;
   TipoDato = "N"; Long = 10; LongDec = 0; eaCadena = TotRegs; |HAZ Formateo;
   VAlfa6 = eaCadena;
   VAlfa7 = " " * 53;

   aAlfa = VAlfa1 + VAlfa2 + VAlfa3 + VAlfa4 + VAlfa5 + VAlfa6 + VAlfa7;
   eaCadena = aAlfa; |HAZ QuitaRaros; aAlfa = eaCadena;
   aAlfa = handle + " " + aAlfa + &#0#7 + &#0#8; |FGRABA aAlfa;
|FPRC;

|| **************************************************************************

|PROCESO pasecuencial;

  |ABRE #dscam049;
  |HAZ leerem;
  |SI swErrorR = 1; |O swErrorR = 2;
      |MENAV "    ERROR !!! CONFIRME NO CORRECTO ";
      |ACABA;
  |FINSI;

  |HAZ varia_fich;

  |CAMPO_VISUAL #dscaz060#10,1,"S"; #dscaz060#10 = 0;
  |CAMPO_VISUAL #dscaz060#11,1,"S"; #dscaz060#11 = 0;
  |CAMPO_VISUAL #dscaz060#12,1,"S"; #dscaz060#12 = 0;

|| ...................................................
  |ABRE #dscam008;         || Abre Mantenimiento de efectos

|| Abrir Fichero Secuencial

  |DFICE dirempr;
  fiche = "wb  " + dirempr + "casecmin.seq";  || "nomfiche.ext";
  |QBF fiche;
  |FABRE fiche;
  handle = FSalida;
  |SI FSalida ] 0;
      |HAZ Cabecera;
      |BUCLE Facturas;
      |HAZ Totales;
  |FINSI;
  contenido = handle + " " + &#0#9;
  |FGRABA contenido;

  FSalida = handle;
  |FCIERRA FSalida;

  Pos = -1; |CIERRA #dscam008;

|| ...   |SI swERROR = 1; |ACABA; |FINSI;

  |SI HayConta = 1; |Y #dscam049#7 = "N"; |Y #dscaz060#14 ! "    ";
      |ABRE #dscam030;
      #dscam030#0 = #dscaz060#14;
      |LEE 010#dscam030.=;
      |SI FSalida = 0;
         Comodin = Usuario; |QBF Comodin; Comodin = "ac" + Comodin;
         |NOME_DAT #6 Comodin;
         |DELINDEX #dscaw013; |ABRE #dscaw013;
         |DDEFECTO #dscaw013;
         #dscaw013#0  = 1;            #dscaw013#15 = #dscaz060#14;
         #dscaw013#5  = #dscam049#0;  #dscaw013#6  = #dscam049#0;
         #dscaw013#11 = "N";          #dscaw013#12 = 5;
         #dscaw013#13 = "dscam050";   #dscaw013#14 = 0;
         |GRABA 020#dscaw013.n;
         |CIERRA #dscaw013;
         Comodin = #dscaz060#14;
         Comodin = "dscam033.tab;" + Comodin + ",E";
         |LIBERA #dscam049; |CIERRAT #dscam049;
         |LIBERA #dscam050; |CIERRAT #dscam050;
         |SUB_EJECUTA_NP Comodin;
         |ABRET #dscam049; |LEE 101#dscam049.=;
         |ABRET #dscam050; |LEE 101#dscam050.=;

         |DFICE aAlfa; |QBF aAlfa; aAlfa = "ac" + Usuario + ".dat"; |FBORRA aAlfa;
         |DFICE aAlfa; |QBF aAlfa; aAlfa = "ac" + Usuario + ".ddx"; |FBORRA aAlfa;
         |DFICE aAlfa; |QBF aAlfa; aAlfa = "ac" + Usuario + ".ixx"; |FBORRA aAlfa;
      |FINSI;
      |CIERRA #dscam030;
  |FINSI;

  |SI TotFact ! 0;
      #dscam049#7 = "D";
      |GRABA 020#dscam049.a; |LIBERA #dscam049;
  |FINSI;
  |CIERRA #dscam049;

  |D_CUADRO 1261, 2280;
  |ATRI R;
  |PINTA 1362, " Numero Recibos   ";
  |PINTA 1562, " Numero Registros ";
  |PINTA 1762, " Importe Total    ";
  |ATRI 0;
  |PINTA 1462, "                  ";
  |PINTA 1662, "                  ";
  |PINTA 1862, "                  ";
  |ATRI 0;

  #dscaz060#10 = TotFact; |PINTA #dscaz060#10;
  #dscaz060#11 = TotRegs; |PINTA #dscaz060#11;
  #dscaz060#12 = TotConf; |PINTA #dscaz060#12;

  |DFICE eaOrigen; |QBF eaOrigen;
  eaNombre1 = "casecmin.seq";
  |SUB_EJECUTA "dscaz032.tab";
|FPRC;


|| ********** Rutinas Varias

|PROCESO LetraDni;
     eaVarDni   = CampoDni;
     enCalcCif  = 3;
     |HAZ CalculoDNI;
     CampoDni = eaVarDni;
     CampoDni = ("000000000" + CampoDni) % -109;
|FPRC;

|PROCESO cccBanco;
 |QBT ccc;
 c_alfa1 = ccc % 0;
 c_nume1 = c_alfa1;
 c_alfa1 = "";
 c_alfa2 = "";
 |PARA c_nume2 = 1; |SI c_nume2 [ c_nume1; |HACIENDO c_nume2 = c_nume2 + 1;
       c_nume3 = c_nume2 * 100 + 1;
       c_alfa2 = ccc % c_nume3;
       |SI c_alfa2 ] "0"; |Y c_alfa2 [ "9";
           c_alfa1 = c_alfa1 + c_alfa2;
       |FINSI;
 |SIGUE;
 ccc = c_alfa1;
|FPRC;

|PROCESO varia_fich;
|| .......... Variables del Presentador, del Ordenante y comunes

  CodCli = #dscaz060#5; |QBF CodCli;
  CodCli = CodCli + "  "; CodCli = CodCli % 102;
  Nombre = #dscam073#3 + (" " * 40);     Nombre = Nombre % 140;

  alfa1 = #dscaz060#3;
  dd = alfa1 % A102;
  mm = alfa1 % A402;
  aa = alfa1 % A902;
  fechapre = aa + mm + dd;   || Fecha Formato a¤o + mes + dia
|FPRC;

|PROCESO separa;
  alfa3  = 0;
  numero = 0;
  |QBF direcc;
  alfa1 = direcc % 0;
  longi = alfa1;
  |PARA nume4 = longi; |SI nume4 > 1; |HACIENDO nume4 = nume4 - 1;
        nume2 = (nume4 * 100) + 1;
        alfa2 = direcc % nume2;
        |SI alfa2 = ",";
            nume3  = ((longi - nume4) + 100) * -1;
            alfa3  = direcc % nume3;
            nume2  = (nume4 - 1) + 100;
            direcc = direcc % nume2;
            |SAL;
        |FINSI;
  |SIGUE;
  |QBT alfa3;
  |SI alfa3 ! ""; numero = alfa3; |FINSI;
|FPRC;

|PROCESO SacaNivel1;
   |QBF aCuenta1;
   aAlfa = aCuenta1 % 0; nNivel = aAlfa;
   |SI nNivel = Niv1; nNivel = 1; |FINSI;
   |SI nNivel = Niv2; nNivel = 2; |FINSI;
   |SI nNivel = Niv3; nNivel = 3; |FINSI;
   |SI nNivel = Niv4; nNivel = 4; |FINSI;
   |SI nNivel = Niv5; nNivel = 5; |FINSI;
   |SI nNivel = Niv6; nNivel = 6; |FINSI;
|FPRC;

|PROCESO CodOrdenante;
   aAlfa = #dscaz060#15; |QBF aAlfa;
   |SI aAlfa = "";
      |MENAV "    Atencion , este campo no puede estar vacio ";
      |ERROR; |ACABA;
   |FINSI;
|FPRC;

|PROCESO TomaDatos;
   |ABRE #dscam073;
   #dscam073#0 = #dscaz060#2;
   |LEE 000#dscam073.=;
   |SI FSalida ! 0;
     |DDEFECTO #dscam073;
   |FINSI;
   |CIERRA #dscam073;

   #dscaz060#5 = #dscam073#23; |PINTA #dscaz060#5;
   #dscaz060#13 = #dscam073#24; |PINTA #dscaz060#13;
|FPRC;

|PROCESO MiraParam; |TIPO 7;
   |SI #dscam051#1 = "S";
      Comodin = #dscam051#61;
      |C_M #dscaz060#14,1,Comodin;
   |SINO;
      |C_M #dscaz060#14,1,"N";
   |FINSI;
|FPRC;

|PROCESO ImpreCab;
   |IMPRE -1;
   |SI FSalida = 0;
      nImprime = 1;
      aAlfa = "*" * 80; |IMPRIME aAlfa;
      aAlfa = "*****                 ESTADO RECIBOS A EMITIR EN CONFIRMING                *****"; |IMPRIME aAlfa;
      aAlfa = "*" * 80; |IMPRIME aAlfa;
      aAlfa = " ";      |IMPRIME aAlfa;
   |FINSI;
|FPRC;

|PROCESO Recibos;
   #dscam008#40 = #dscam050#13;
   |LEE 000#dscam008.=;
   |SI FSalida = 0;
      aAlfa = #dscam008#63; |QBF aAlfa;
      |SI aAlfa = "";
         |SI nImprime = 0; |HAZ ImpreCab; |FINSI;

         aAlfa = " El recibo n§ " + #dscam008#40 + " no tiene referencia indicada";
         |IMPRIME aAlfa;
         nSwError = 1;
      |FINSI;

      nCalc1 = #dscaz060#3; nCalc2 = #dscam008#3;
      nCalc1 = nCalc2 - nCalc1;
      |SI nCalc1 < 5; |Y nCalc1 ] 0;
         |SI nImprime = 0; |HAZ ImpreCab; |FINSI;

         aAlfa = " El recibo n§ " + #dscam008#40 + " vence en menos de 5 dias respecto de la fecha de emision del confirme";
         |IMPRIME aAlfa; nSwError = 1;
      |SINO;
         |SI nImprime = 0; |HAZ ImpreCab; |FINSI;

         |SI nCalc1 < 0;
            aAlfa = " El recibo n§ " + #dscam008#40 + " esta vencido";
            |IMPRIME aAlfa; nSwError = 1;
         |FINSI;
      |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE Recibos;
#dscam050#1;
;
#dscaz060#1, 000;
#dscaz060#1, 999;
;
NULL, Recibos;
|FIN;

|PROCESO RevisaRecs;
   |ABRE #dscam008;
   nImprime = 0;
   || Primero compruebo las referencias de cada recibo y el vencimiento para dar aviso
   |BUCLE Recibos;
   || Segundo presento el informe
   |SI nImprime ! 0; |FINIMP; |FINSI;
   |CIERRA #dscam008;
|FPRC;

|PROGRAMA;
   |HAZ Tipo8;

   |ABRE #dscam051;
   |LEE 000#dscam051.p;
   |SI FSalida ! 0;
      |DDEFECTO #dscam051;
      |GRABA 020#dscam051.c;
   |FINSI;
   |CIERRA #dscam051;
   |SI #dscam051#1 = "S";
      #dscaz060#14 = #dscam051#29;
   |SINO;
      #dscaz060#14 = "    ";
   |FINSI;

   |CLS;
   |CABEZA " Emision diskette confime Banesto";
   |PINPA #0#0; |PINDA #0#0;
   |ENDATOS #1#0;
   |SI FSalida = 0;
      |CONFI "    NImprimir carta confirme ?";
      |SI FSalida = 0; nSwImpre = 1; |SINO; nSwImpre = 0; |FINSI;
      |SI nSwImpre = 1;
         |IMPRE 0; |SI FSalida < 0; |ACABA; |FINSI;
         aInforme = "Carta emision de confirmes"; |HAZ ObtenInforme;
         |INFOR aInforme; fecha = #dscaz060#3;
         |SI FSalida < 0; |FINIMP; |ACABA; |FINSI;
      |FINSI;

      nSwError = 0; |HAZ RevisaRecs;
      |SI nSwError ! 0;
         aAlfa = "    NHa habido avisos en la comprobacion de recibos. " + &191 + " Continuar ? ";
         |CONFI aAlfa;
         |SI FSalida = 0; |HAZ pasecuencial; |FINSI;
      |SINO;
         |HAZ pasecuencial;
      |FINSI;
      |SI nSwImpre = 1; |PIEINF; |FININF; |FINIMP; |FINSI;
   |FINSI;

   |HAZ Tipo9;
|FPRO;
