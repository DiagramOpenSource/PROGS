|FICHEROS;
   cadie003 #0;
   cadie502 #1;

   dscam003 #10;
   dscam004 #11;
   dscam044 #12;
   dscam045 #13;

   agif025@ #100;
   agif134@ #101;
|FIN;

|VARIABLES;

   &Divisa = "";

   aAlfa    = "";
   aAlfa1   = "";
   DesdeRep = "";
   HastaRep = "";
   aAntRep  = "ZZZZ";

   fFechaImp = @;

   &nDeci_Div = 0; &nDeci_Base = 0;
   &HayConta  = 0; &HayGestion = 0;
   Primero    = 0;
   SwError    = 0;
   nTotalImp  = 0;
   nTotalPdt  = 0;
   nCalc      = 0;
   nCont      = 0;
   EmpCartera = 0;

   Calc       = 0;
   SwNeg      = 0;
   nDecimales = 0;
   Long       = 0;

   Comodin    = "";
   Comodin1   = "";
   aDecimales = "";
   LongAlfa   = "";
   Cadena     = "";
|FIN;

|PROCESO RecibosLin;
   nTotalImp = nTotalImp + #dscam004#27 + #dscam004#30;
   fFechaImp = #dscam004#6;
|FPRC;

|DEFBUCLE RecibosLin;
#dscam004#4;
6;
"IMP",#dscam003#40,#cadie003#2;
"IMP",#dscam003#40,#cadie003#3;
;
NULL,RecibosLin;
|FIN;

|PROCESO RecibosPdt;
   nTotalPdt = nTotalPdt - (#dscam004#44 - #dscam004#46);
|FPRC;

|DEFBUCLE RecibosPdt;
#dscam004#4;
;
"RPT",#dscam003#40;
"RPT",#dscam003#40;
;
NULL,RecibosPdt;
|FIN;

|PROCESO Recibos;
   |SI #dscam003#3 > #cadie003#4; |ACABA; |FINSI;

   Divisa = #dscam003#57; |HAZ TomaDecim;

   nTotalPdt = #dscam003#31;
   |SI #dscam003#14 = "T"; |BUCLE RecibosPdt; |FINSI;
   nTotalImp = 0; |BUCLE RecibosLin;
   |SI nTotalImp = 0; |O #dscam003#14 ! "I"; |ACABA; |FINSI;

   |SI #cadie003#5 = "S";
      SwError = 1; |HAZ CargaNDeci;
      #agif134@#49 = #dscam003#0;
      #agif134@#0  = #dscam003#1;
      |LEE 000#agif134@.=;
      |PARA; |SI FSalida = 0; |Y #agif134@#49 = #dscam003#0; |Y #agif134@#0 = #dscam003#1; |HACIENDO;
         aAlfa  = #agif134@#1; |QBF aAlfa;  aAlfa  = aAlfa  % -104;
         aAlfa1 = #dscam003#4; |QBF aAlfa1; aAlfa1 = aAlfa1 % -104;
         |SI aAlfa = aAlfa1; SwError = 0; |SAL; |FINSI;
         |LEE 000#agif134@.s;
      |SIGUE;
      |SI SwError = 1; |DDEFECTO #agif134@; |FINSI;
      #agif025@#0 = #dscam003#33;
      |LEE 000#agif025@.=;
      |SI FSalida ! 0; |DDEFECTO #agif025@; |FINSI;

      #cadie502#0  = #dscam003#40;
      |LEE 000#cadie502.=;
      |SI FSalida = 0; |BORRA 020#cadie502.a; |FINSI;

      |DDEFECTO #cadie502;
      #cadie502#0  = #dscam003#40;
      |LEE 000#cadie502.];
      |SI FSalida ! 0; |O #cadie502#0 ! #dscam003#40;
         |DDEFECTO #cadie502;
         #cadie502#0  = #dscam003#40;
         #cadie502#1  = #dscam003#0;
         #cadie502#2  = #dscam003#1;
         #cadie502#3  = #dscam003#2;
         #cadie502#4  = #dscam003#32;
         #cadie502#5  = #dscam003#6;
         #cadie502#6  = fFechaImp;
         #cadie502#7  = "01.01.0000";
         #cadie502#8  = #cadie003#7;
         #cadie502#9  = nTotalImp;
         |SI #agif025@#39 = 0;
            nCalc = (#agif134@#111 * nTotalImp) / #agif134@#101;
         |SINO;
            nCalc = nTotalImp % #agif025@#39;
         |FINSI;
         #cadie502#10 = nCalc;
         #cadie502#11 = #dscam003#33;
         #cadie502#12 = #dscam003#34;
         #cadie502#13 = nTotalPdt;
         || #cadie502#14 = PEPE;
         |GRABA 020#cadie502.n;
      |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE Recibos;
#dscam003#10;
;
"I",000000001,#dscam045#0,DesdeRep,"01.01.0000";
"I",999999999,#dscam045#0,HastaRep,#cadie003#4 ;
;
NULL,Recibos;
|FIN;

|PROCESO Listado;
   |SI aAntRep ! #cadie502#11;
      |SI Primero = 0;
         Primero = 1;
      |SINO;
         |PIEINF;
      |FINSI;
      aAntRep = #cadie502#11;
   |FINSI;
   |PRINT;
|FPRC;

|DEFBUCLE Listado;
#cadie502#2;
;
#cadie003#7,DesdeRep;
#cadie003#7,HastaRep;
;
NULL,Listado;
|FIN;

|PROCESO Selec;
   aAlfa = #cadie003#8;
   |PARA nCont = 9; |SI nCont [ 16; |HACIENDO nCont = nCont + 1;
      |C_M #cadie003#nCont#,1,aAlfa;
   |SIGUE;
   |SI aAlfa = "S"; aAlfa = "N"; |SINO; aAlfa = "S"; |FINSI;
   |C_M #cadie003#0,1,aAlfa; |C_M #cadie003#1,1,aAlfa;
|FPRC;

|PROCESO PorEmpresa;
   |SI #cadie003#8 = "N";
      DesdeRep = #cadie003#0; HastaRep = #cadie003#1;
      |BUCLE Recibos;
   |SINO;
      |PARA nCont = 9; |SI nCont [ 16; |HACIENDO nCont = nCont + 1;
         DesdeRep = #cadie003#nCont#; HastaRep = #cadie003#nCont#;
         |BUCLE Recibos;
      |SIGUE;
   |FINSI;
|FPRC;

|PROCESO BucleEmp;

   #dscam044#0 = #dscam045#0;
   |LEE 000#dscam044.=;
   |SI FSalida = 0;
      aAlfa = #dscam044#1; |QBF aAlfa;
      aAlfa1 = aAlfa + "def/agifa134.mas";
      |CARGA_DEF aAlfa1, agif134@;
      |SI FSalida = 0;
         aAlfa1 = aAlfa + "def/agifa025.mas";
         |CARGA_DEF aAlfa1, agif025@;
         aAlfa1 = aAlfa + "fich/" + (("00000" + #dscam044#0) % -105) + "/";
         |PATH_DAT #100 aAlfa1; |PATH_DAT #101 aAlfa1;
         |ABRE #agif134@; |ABRE #agif025@;
         |HAZ PorEmpresa;
         |CIERRA #agif025@; |DESCARGA_DEF #agif025@;
         |CIERRA #agif134@; |DESCARGA_DEF #agif134@;
      |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE BucleEmp;
#dscam045#2;
2;
EmpCartera,"V";
EmpCartera,"V";
;
NULL,BucleEmp;
|FIN;

|PROCESO PonComas;

   Calc = Comodin;
   |SI Calc < 0;
      Calc = 0 - Calc;
      Comodin = Calc;
      SwNeg = 1;
   |SINO;
      SwNeg = 0;
   |FINSI;

   Comodin1 = Comodin;
   Comodin1 = Comodin1 - ".";
   |SI FSalida ! 0;
      Calc = FSalida + 98;
      Comodin1 = Comodin1 % Calc;
      nDecimales = Comodin1;
      aDecimales = Comodin1; |QBF aDecimales;

      LongAlfa = aDecimales % 0; Long = LongAlfa;
      |SI Long < 2;
         Long = 2 - Long;
         aDecimales = aDecimales + ("0" * Long);
      |FINSI;
||      Comodin = Comodin - Comodin1; Comodin = Comodin - ".";
      Calc = Calc - 199; Calc = 100 + ((Calc / 100) ! 0);
      Comodin = Comodin % Calc;
   |SINO;
      nDecimales = 0;
      aDecimales = "00";
   |FINSI;

   Comodin1 = Comodin;
   Comodin = "";
   |PARA; |SI Comodin1 ! ""; |HACIENDO;
      LongAlfa = Comodin1 % 0;
      Long = LongAlfa;
      |SI Long [ 3;
         Cadena = Comodin1;
         Comodin1 = "";
         |QBF Cadena;
         Comodin = Cadena + Comodin;
      |SINO;
         Cadena = Comodin1 % -103;
         Comodin1 = Comodin1 - Cadena;
         Comodin  = "." + Cadena + Comodin;
      |FINSI;
   |SIGUE;
   |SI SwNeg = 1;
      Comodin = "-" + Comodin;
   |FINSI;
   |SI nDecimales ] 0;
      Comodin = Comodin + ",";
      ||SI nDecimales < 10; Comodin = Comodin + "0"; |FINSI;
      Comodin = Comodin + aDecimales;
      ||Comodin = Comodin + nDecimales;
   |FINSI;
|FPRC;

|PROGRAMA;
   |HAZ Tipo8;

   |ABRE #cadie003;
   |LEE 000#cadie003.p;
   |SI FSalida ! 0;
      |DDEFECTO #cadie003;
      |GRABA 020#cadie003.c;
   |FINSI;

   |CLS;
   |CABEZA "Listado retrocesion comisiones recibos vencidos";
   |PINPA #0#0; |PINDA #0#0;
   |ENDATOS #1#0;
   |SI FSalida = 0;
      |GRABA 020#cadie003.a;
      |IMPRE 0;
      |SI FSalida ! 0; |ACABA; |FINSI;
      |INFOR "cadie003";
      |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

      |DFICB aAlfa; |PATH_DAT #12 aAlfa; |PATH_DAT #13 aAlfa;
      |ABRE #cadie502; |ABRE #dscam044;
      |SI #cadie003#6 = "N";
         EmpCartera = #48#0; |BUCLE BucleEmp;
      |FINSI;
      |SI #cadie003#8 = "N";
         DesdeRep = #cadie003#0; HastaRep = #cadie003#1;
         |BUCLE Listado;
      |SINO;
         |PARA nCont = 9; |SI nCont [ 16; |HACIENDO nCont = nCont + 1;
            DesdeRep = #cadie003#nCont#; HastaRep = #cadie003#nCont#;
            |BUCLE Listado;
         |SIGUE;
      |FINSI;

      |PIEINF; |FININF;
      |CIERRA #dscam044; |CIERRA #cadie502;
      |FINIMP;
   |FINSI;

   |CIERRA #cadie003;

   |HAZ Tipo9;
|FPRO;
