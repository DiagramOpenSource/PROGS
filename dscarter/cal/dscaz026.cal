|FICHEROS;
   dscaz026 #0;

   agif114@ #1;
   agif116@ #2;
   agif174@ #3;
   agif178@ #4;
   agif179@ #5;
   agif182@ #6;
   agif184@ #7;
   agif041@ #8;
   agif024@ #9;
   agif025@ #10;
   agif134@ #11;
   &agif324@ #12;
   agif084@ #13;
   &agif321@ #14;
   agif079@ #15;
   agif148@ #16;
   agif112@ #17;
   agif010@ #18;
   agif104@ #19;

   dscam038 #34;

   dscam003 #20;
   dscam004 #21;
   dscam008 #22;
   dscam009 #23;
   dscam048 #24;
   dscam014 #25;

   dscam012 #26;
   dscam013 #27;

   dscam051 #35;

   agif091@ #40;
|FIN;

|VARIABLES;
   &enEmpCartera = 0; &enEmpGestion = 0; &enBanco = 0;
   &eaSerie = ""; &eaCliGest = ""; &nImporte = 0; &eaTipoOper = "";

   &eaEntidad = ""; &eaSucursal = ""; &eaCuenta = ""; &eaDC = "";
   &nDeci_Base = 0; &nDeci_Div = 0; &Divisa = "";
   Comodin = ""; Comodin1 = ""; Comodin2 = "";
   SwAbiertos = 0;

   CambioDiv  = 0;
   CambioBaseVn = 0;
   CambioBaseCm = 0;

   nSwEntr = 0;
   nNumEntrega = 0;
   nReciboACobrar = 0;

   UltRecibo   = 0;
   UltRemesa   = 0;
   UltPagare   = 0;
   UltTalon    = 0;
   UltConfirme = 0;

   NumRecs = 0; Calc = 0;
   LinCob = 0; UltOp = "";

   Empresa = 0; Mensaje = 0;
   aLong = "";  nLong = 0;
   LinPag = 0;
   nLinCob = 0;
   SwHayDatos = 0;
   Directa = 0;

   aEjer  = "";
   aAlfa  = "";
   aAlfa1 = "";
   aAlfa2 = "";
   FEstado = 0;

   nSw = 0;
|FIN;

|PROCESO AbreFichs;

   |SI FSalida = 10;
      |DBASS Comodin; |QBF Comodin;
      Comodin = Comodin + "dscomer9/";
      #dscaz026#0 = Comodin;
      |ERROR;
      |ACABA;
   |FINSI;

   Comodin = #dscaz026#0; |QBF Comodin;
   Comodin = Comodin % -101;
   |SI Comodin ! "/";
      Comodin = #dscaz026#0; |QBF Comodin;
      Comodin = Comodin + "/";
      #dscaz026#0 = Comodin;
      |PINTA #dscaz026#0;
   |FINSI;

   Comodin = #dscaz026#0; |QBF Comodin;
   Comodin1 = Comodin + "def/agifa041.mas";
   |CARGA_DEF Comodin1,agif041@;
   |SI FSalida = 0;
      Comodin = #dscaz026#0; |QBF Comodin;
      Comodin1 = Comodin + "fich/";
      |PATH_DAT #8 Comodin1;
      |ABRE #8;
      SwAbiertos = 1;
      Comodin1 = Comodin + "def/agifa114.mas"; |CARGA_DEF Comodin1,agif114@;
      Comodin1 = Comodin + "def/agifa116.mas"; |CARGA_DEF Comodin1,agif116@;
      Comodin1 = Comodin + "def/agifa174.mas"; |CARGA_DEF Comodin1,agif174@;
      Comodin1 = Comodin + "def/agifa178.mas"; |CARGA_DEF Comodin1,agif178@;
      Comodin1 = Comodin + "def/agifa179.mas"; |CARGA_DEF Comodin1,agif179@;
      Comodin1 = Comodin + "def/agifa182.mas"; |CARGA_DEF Comodin1,agif182@;
      Comodin1 = Comodin + "def/agifa184.mas"; |CARGA_DEF Comodin1,agif184@;
      Comodin1 = Comodin + "def/agifa024.mas"; |CARGA_DEF Comodin1,agif024@;
      Comodin1 = Comodin + "def/agifa025.mas"; |CARGA_DEF Comodin1,agif025@;
      Comodin1 = Comodin + "def/agifa134.mas"; |CARGA_DEF Comodin1,agif134@;
      Comodin1 = Comodin + "def/agifa324.mas"; |CARGA_DEF Comodin1,agif324@;
      Comodin1 = Comodin + "def/agifa084.mas"; |CARGA_DEF Comodin1,agif084@;
      Comodin1 = Comodin + "def/agifa321.mas"; |CARGA_DEF Comodin1,agif321@;
      Comodin1 = Comodin + "def/agifa079.mas"; |CARGA_DEF Comodin1,agif079@;
      Comodin1 = Comodin + "def/agifa148.mas"; |CARGA_DEF Comodin1,agif148@;
      Comodin1 = Comodin + "def/agifa112.mas"; |CARGA_DEF Comodin1,agif112@;
      Comodin1 = Comodin + "def/agifa104.mas"; |CARGA_DEF Comodin1,agif104@;
      Comodin1 = Comodin + "def/agifa010.mas"; |CARGA_DEF Comodin1,agif010@;
      Comodin1 = Comodin + "def/agifa091.mas"; |CARGA_DEF Comodin1,agif091@;
   |SINO;
      |MENAV "    No encuentro el fichero de empresas";
      |ERROR;
      |ACABA;
   |FINSI;
|FPRC;

|PROCESO ConsultaEmpresa;
   |SI FSalida = 2; |ACABA; |FINSI;
   |SI FSalida = 10;
      |CONSULTA_CLAVE #1#agif041@;
      #dscaz026#1 = #agif041@#0;
      #dscaz026#2 = #agif041@#1;
      |PINTA #dscaz026#1; |PINTA #dscaz026#2;
   |SINO;
      #agif041@#0 = #dscaz026#1;
      |LEE 101#agif041@.=;
      |SI FSalida = 0;
         #dscaz026#1 = #agif041@#0;
         #dscaz026#2 = #agif041@#1;
         |PINTA #dscaz026#1; |PINTA #dscaz026#2;
      |SINO;
         |MENAV "    No existe empresa";
         |ERROR;
         |ACABA;
      |FINSI;
      |LIBERA #agif041@;
   |FINSI;
|FPRC;

|PROCESO CobrosParciales;
   aAlfa  = #agif174@#16; |QBF aAlfa;  aAlfa  = aAlfa  % -104;
   |SI aAlfa ! aEjer; |ACABA; |FINSI;

   nSw = 1;
   #dscam004#17 = #dscam003#40;
   #dscam004#15 = #dscam003#27; || Libro
   #dscam004#0  = #agif174@#0;  || Serie
   #dscam004#1  = #agif174@#1;  || Factura
   #dscam004#2  = #agif174@#2;  || Recibo
   #dscam004#3  = LinCob + #agif174@#3;  || N§ cobro
   #dscam004#4  = #agif174@#5;  || Cuenta de cobro
   #dscam004#6  = #agif174@#6;  || Fecha de cobro
   #dscam004#7  = #agif174@#8 / #dscam003#59;  || Total Liquidado DIVISA
   #dscam004#25 = #agif174@#8;                 || Total Liquidado BASE
   |SI #agif174@#13 = "N";
      #dscam004#8  = #agif174@#9 / #dscam003#59;  || Total a cobrar DIVISA
      #dscam004#26 = #agif174@#9;                 || Total a cobrar BASE
      nImporte = #dscam004#8;
   |SINO;
      #dscam004#9  = #agif174@#9 / #dscam003#59;  || Total impagado DIVISA
      #dscam004#27 = #agif174@#9;                 || Total impagado BASE
      nImporte = 0 - #dscam004#9;
   |FINSI;
   #dscam004#12 = #agif174@#11 / #dscam003#59; || Gastos de negociacion DIVISA
   #dscam004#29 = #agif174@#11;                || Gastos de negociacion BASE

   #dscam004#13 = #agif174@#12 / #dscam003#59; || Gastos de devolucion DIVISA
   #dscam004#30 = #agif174@#12;                || Gastos de devolucion BASE

   #dscam004#18 = #agif174@#10; || Contabilizado

   #dscam003#17 = #agif174@#14; || Remesa
   #dscam003#37 = #agif174@#15; || Linea de Remesa
   #dscam003#24 = #dscam003#24 + (#agif174@#11 / #dscam003#59); || Gastos de negociacion DIVISA
   #dscam003#62 = #dscam003#62 + #agif174@#11;                  || Gastos de negociacion BASE
   #dscam003#25 = #dscam003#25 + (#agif174@#12 / #dscam003#59); || Gastos de devolucion DIVISA
   #dscam003#63 = #dscam003#63 + #agif174@#12;                  || Gastos de devolucion BASE
   #dscam003#30 = #dscam003#22 + #dscam003#24 + #dscam003#25; || Total recibo DIVISA
   #dscam003#30 = #dscam003#30 - (#dscam003#54 + #dscam003#55);
   #dscam003#64 = #dscam003#60 + #dscam003#62 + #dscam003#63 + #dscam003#56; || Total recibo BASE
   #dscam003#64 = #dscam003#64 - (#dscam003#67 + #dscam003#68);

   |SI #dscam003#17 ! 0;
      #agif178@#0 = #dscam003#17;
      |LEE 101#agif178@.=;
      |SI FSalida = 0; enBanco = #agif178@#2; |FINSI;
      |LIBERA #agif178@;

      #dscam004#5  = "REM";

      || Actualizo el riesgo de cliente
      || enEmpCartera = #48#0; enEmpGestion = #dscam003#46;
      enEmpCartera = -1;     enEmpGestion = #dscam003#46;
      eaSerie = #dscam003#0; eaCliGest = #dscam003#32;
      eaTipoOper = "+6";
      |SI nImporte < 0;
         eaTipoOper = eaTipoOper + "-7";
         nImporte = 0 - nImporte;
      |SINO;
         eaTipoOper = eaTipoOper + "-4";
      |FINSI;
      |HAZ BucleRiesgo;
   |SINO;
      |SI #agif174@#13 = "S";
         #dscam004#5  = "IMP";
         #dscam003#16 = #dscam003#16 + 1;
         #dscam003#14 = "I";
         #dscam003#15 = "Impagado";
      |SINO;
         |SI #agif174@#7 = #agif174@#9;
            #dscam004#5  = "COB";
         |SINO;
            #dscam004#5  = "COP";
         |FINSI;
         |SI #dscam003#23 = 0;
            #dscam003#14 = "P";
            #dscam003#15 = "Pendiente de cobro";
         |SINO;
            |SI #dscam003#31 = 0;
               #dscam003#14 = "L";
               #dscam003#15 = "Recibo liquidado";
            |SINO;
               #dscam003#14 = "C";
               #dscam003#15 = "Parcialmente cobrado";
            |FINSI;
         |FINSI;
      |FINSI;
   |FINSI;

   |GRABA 020#dscam003.a;

   |GRABA 020#dscam004.n;
|FPRC;

|DEFBUCLE CobrosParciales;
#agif174@#1004;
;
#agif114@#17,#agif114@#0,#agif114@#1,001;
#agif114@#17,#agif114@#0,#agif114@#1,999;
;
NULL,CobrosParciales;
|FIN;

|PROCESO RecibosVenta;
   #agif024@#0 = #agif114@#2; |LEE 000#agif024@.=;
   |SI FSalida ! 0; |O #agif024@#0 = "      "; |DDEFECTO #agif024@; |FINSI;

   nSwEntr = 0;
   |SELECT #3#agif148@;
   #agif148@#19 = #agif114@#17;
   #agif148@#0  = #agif114@#0;
   |LEE 000#agif148@.];
   |PARA; |SI FSalida = 0; |Y #agif148@#19 = #agif114@#17; |Y #agif148@#0  = #agif114@#0; |HACIENDO;
      |SI #agif148@#1 = #agif114@#1; |SAL; |FINSI;
      |LEE 000#agif148@.s;
   |SIGUE;
   |SI #agif148@#1 = #agif114@#1; |Y #agif148@#19 = #agif114@#17; |Y #agif148@#0  = #agif114@#0;
      nSwEntr = 1;
      |SELECT #6#dscam003;
      #dscam003#27 = 0;
      #dscam003#0  = #agif148@#19;
      #dscam003#1  = #agif148@#0;
      #dscam003#2  = #agif148@#1;
      #dscam003#69 = #agif148@#30;
      #dscam003#40 = 1;
      |LEE 000#dscam003.]; FEstado = FSalida;
      aAlfa1 = #dscam003#0;  |QBF aAlfa1;
      aAlfa2 = #agif148@#19; |QBF aAlfa2;
      |SI FEstado = 0; |Y aAlfa1 = aAlfa2; |Y #dscam003#1  = #agif148@#0;
       |Y #dscam003#2  = #agif148@#1; |Y #dscam003#69 = #agif148@#30;
         nNumEntrega = #dscam003#40;
      |FINSI;
      |SELECT #1#dscam003;
   |FINSI;
   |SELECT #1#agif148@;

   |SI nSwEntr = 1; |Y nNumEntrega = 0;
      aAlfa = "   PROBLEMAS !!! El recibo " + #agif114@#17 + "/" + #agif114@#0 + " est  compensado con una entrega que no existe.";
   |FINSI;

   Directa = 0;
   #agif134@#49 = #agif114@#17;
   #agif134@#0 = #agif114@#0;
   |LEE 000#agif134@.=;
   |SI FSalida ! 0;
      #agif084@#48 = #agif114@#17;
      #agif084@#0  = #agif114@#0;
      |LEE 101#agif084@.=;
      |SI FSalida = 0;
         Directa = 1;
         #agif324@#0 = #agif084@#65;
      |SINO;
         |DDEFECTO #agif134@;
         #agif134@#58 = "EUR";
         #agif134@#59 = 1;
         #agif324@#0 = #agif134@#58;
      |FINSI;
   |SINO;
      #agif324@#0 = #agif134@#58;
   |FINSI;

   |LEE 000#agif324@.=;

   nSw = 0;
   |SELECT #6#dscam003;
   #dscam003#27 = "00";         || Libro
   #dscam003#0  = #agif114@#17; || Serie
   #dscam003#1  = #agif114@#0;  || Factura
   #dscam003#2  = #agif114@#1;  || N§ recibo
   #dscam003#69 = " ";          || Documento a cuenta
   #dscam003#40 = 1;            || N§ documento
   |LEE 000#dscam003.];
   |PARA; |SI FSalida = 0; |Y #dscam003#27 = "00"; |Y #dscam003#0  = #agif114@#17;
    |Y #dscam003#1  = #agif114@#0; |Y #dscam003#2  = #agif114@#1; |HACIENDO;
       aAlfa  = #dscam003#4; |QBF aAlfa;  aAlfa  = aAlfa  % -104;
       aAlfa1 = #agif114@#7; |QBF aAlfa1; aAlfa1 = aAlfa1 % -104;
       |SI aAlfa = aAlfa1;
          nSw = 1; |SAL;
       |SINO;
          |LEE 000#dscam003.s;
       |FINSI;
   |SIGUE;
   |SI nSw = 1; |SELECT #1#dscam003; |ACABA; |FINSI;

   |SELECT #1#dscam003;
   |DDEFECTO #dscam003;
   #dscam003#27 = "00";         || Libro
   #dscam003#0  = #agif114@#17; || Serie
   #dscam003#1  = #agif114@#0;  || Factura
   #dscam003#2  = #agif114@#1;  || N§ recibo
   #dscam003#3  = #agif114@#8;  || Fecha de vencimiento
   #dscam003#4  = #agif114@#7;  || Fecha de emision
   #dscam003#5  = #agif024@#16; || Cuenta contable
   #dscam003#6  = #agif114@#3;  || Nombre del cliente

   #dscam003#12 = #agif114@#21; || Tipo de recibo

   |SI Directa = 0;
      #dscam003#57 = #agif134@#58; || DIVISA
      #dscam003#59 = #agif134@#59; || Cambio DIVISA en Euros
      Calc = CambioBaseVn / #agif134@#59;
   |SINO;
      #dscam003#57 = #agif084@#65; || DIVISA
      #dscam003#59 = #agif084@#66; || Cambio DIVISA
      Calc = CambioBaseVn / #agif084@#66;
   |FINSI;
   #dscam003#59 = Calc;         || Cambio DIVISA en moneda base
   |SI #dscam003#59 = 0; #dscam003#59 = 1; |FINSI;
   #dscam003#58 = #agif324@#1;  || Nombre DIVISA

   #dscam003#22 = #agif114@#9 / #dscam003#59;  || Importe recibo DIVISA
   #dscam003#60 = #agif114@#9;                 || Importe recibo BASE

   #dscam003#23 = #agif114@#19 / #dscam003#59; || Importe liquidado DIVISA
   #dscam003#61 = #agif114@#19;                || Importe liquidado BASE

   #dscam003#26 = #agif114@#16; || Fecha de cobro
   #dscam003#28 = #agif114@#15; || A aceptar
   #dscam003#29 = #agif114@#6;  || Aceptado

   #dscam003#30 = #dscam003#22 + #dscam003#24 + #dscam003#25; || Total recibo DIVISA
   #dscam003#30 = #dscam003#30 - (#dscam003#54 + #dscam003#55);
   #dscam003#64 = #dscam003#60 + #dscam003#62 + #dscam003#63 + #dscam003#56; || Total recibo BASE
   #dscam003#64 = #dscam003#64 - (#dscam003#67 + #dscam003#68);

   #dscam003#46 = #agif041@#0;  || Empresa Gestion
   #dscam003#40 = UltRecibo;

   |SI #agif114@#13 = "S"; |Y #dscam003#23 < #dscam003#30;
      #dscam003#23 = #dscam003#30; #dscam003#61 = #dscam003#64;
   |FINSI;
   #dscam003#31 = #dscam003#30 - #dscam003#23; || Total pendiente DIVISA
   #dscam003#65 = #dscam003#64 - #dscam003#61; || Total pendiente BASE

   |SI #dscam003#23 = 0;
      |SI #dscam003#16 = 0;
         #dscam003#14 = "P";
         #dscam003#15 = "Pendiente de cobro";
      |SINO;
         #dscam003#14 = "I";
         #dscam003#15 = "Impagado";
      |FINSI;
   |SINO;
      |SI #dscam003#31 = 0;
         #dscam003#14 = "L";
         #dscam003#15 = "Recibo liquidado";
      |SINO;
         #dscam003#14 = "C";
         #dscam003#15 = "Parcialmente cobrado";
      |FINSI;
   |FINSI;
   UltRecibo = UltRecibo + 1;

   #dscam003#32 = #agif114@#2;  || Codigo cliente
   #dscam003#48 = #agif114@#10; || Impreso S/N
   #dscam003#50 = #agif114@#18; || Cuenta de cobro
   #dscam003#51 = #agif024@#203;|| Banco de remesas

   #dscam014#0  = #dscam003#51; || Banco remesas
   |LEE 000#dscam014.=;
   |SI FSalida = 0;
      #dscam003#52 = #dscam014#1;
   |FINSI;

   #dscam003#7  = #agif114@#4;  || Banco domiciliacion
   #dscam003#8  = #agif024@#32; || Entidad Banco Cliente
   #dscam003#9  = #agif024@#33; || Sucursal Banco Cliente
   #dscam003#10 = #agif024@#31; || DC Banco Cliente
   #dscam003#11 = #agif024@#30; || Cuenta Banco Cliente
   #dscam003#53 = "          "; || Fecha de remesa
   #dscam003#39 = #agif024@#3;  || Zona

   |SI Directa = 0;
      #agif134@#49 = #agif114@#17;
      #agif134@#0  = #agif114@#0;
      |LEE 000#agif134@.=;
      |SI FSalida = 0;
         #dscam003#33 = #agif134@#7;
         #agif025@#0 = #agif134@#7;
         |LEE 000#agif025@.=;
         |SI FSalida = 0;
            #dscam003#34 = #agif025@#1;
         |FINSI;
      |FINSI;
   |SINO;
      #agif084@#49 = #agif114@#17;
      #agif084@#0  = #agif114@#0;
      |LEE 000#agif084@.=;
      |SI FSalida = 0;
         #dscam003#33 = #agif084@#6;
         #agif025@#0 = #agif084@#6;
         |LEE 000#agif025@.=;
         |SI FSalida = 0;
            #dscam003#34 = #agif025@#1;
         |FINSI;
      |FINSI;
   |FINSI;

   |GRABA 020#dscam003.c;

   aEjer = #agif114@#7; |QBF aEjer; aEjer = aEjer % -104;
   nSw = 0; |BUCLE CobrosParciales;
   |SI nSw = 0;
      |SI #dscam003#23 ! 0;
         |SI nSwEntr = 1;
            nReciboACobrar = #dscam003#40;
            |DDEFECTO #dscam004;
            #dscam004#17 = #dscam003#40;
            #dscam004#3  = 1;
            |GRABA 020#dscam004.b;
            #dscam004#0 = #dscam003#0;
            #dscam004#1 = #dscam003#1;
            #dscam004#2 = #dscam003#2;
            #dscam004#4  = #agif114@#12; || Cuenta de cobro
            #dscam004#5 = "CAC";
            #dscam004#6 = ~;
            #dscam004#7  = 0;            || Total Liquidado DIVISA
            #dscam004#25 = 0;            || Total Liquidado BASE
            #dscam004#8  = #dscam003#23; || Total a cobrar DIVISA
            #dscam004#26 = #dscam003#61; || Total a cobrar BASE
            #dscam004#15 = #dscam003#27;
            Comodin = ("000000000" + nNumEntrega) % -109;
            Comodin = "Cobro entr. " + Comodin;
            #dscam004#16 = Comodin;
            #dscam004#40 = nNumEntrega;
            #dscam004#41 = nLinCob;
            |GRABA 020#dscam004.a; |LIBERA #dscam004;

            |SALVA_FICHA #dscam003;
            #dscam003#40 = nNumEntrega;
            |LEE 000#dscam003.=;
            |SI FSalida = 0;
               nLinCob = 1;
               |DDEFECTO #dscam004;
               #dscam004#17 = nNumEntrega;
               #dscam004#3  = 99;
               |LEE 000#dscam004.];
               |SI FSalida = 0;
                  |SI #dscam004#17 = nNumEntrega;
                     nLinCob = #dscam004#3 + 1;
                  |SINO;
                     |LEE 000#dscam004.a;
                     |SI FSalida = 0;
                        |SI #dscam004#17 = nNumEntrega;
                           nLinCob = #dscam004#3 + 1;
                        |FINSI;
                     |FINSI;
                  |FINSI;
               |SINO;
                  |LEE 000#dscam004.u;
                  |SI FSalida = 0;
                     |SI #dscam004#17 = nNumEntrega;
                        nLinCob = #dscam004#3 + 1;
                     |FINSI;
                  |FINSI;
               |FINSI;

               |DDEFECTO #dscam004;
               #dscam004#17 = #dscam003#40;
               #dscam004#3  = nLinCob;
               |GRABA 020#dscam004.b;
               #dscam004#0 = #dscam003#0;
               #dscam004#1 = #dscam003#1;
               #dscam004#2 = #dscam003#2;
               #dscam004#5 = "REC";
               #dscam004#6 = ~;
               #dscam004#7 = #dscam003#23;
               #dscam004#8  = -#dscam003#31; #dscam004#26 = -#dscam003#65;
               #dscam004#23 = #dscam003#31;  #dscam004#32 = #dscam003#65;
               #dscam004#15 = #dscam003#27;
               Comodin = ("0000000000" + nReciboACobrar) % -110;
               Comodin = "Cobro Doc. " + Comodin;
               #dscam004#16 = Comodin;
               #dscam004#25 = #dscam003#61;
               |GRABA 020#dscam004.a; |LIBERA #dscam004;

               #dscam003#23 = -#dscam003#31; #dscam003#61 = -#dscam003#65;
               #dscam003#55 = #dscam003#55 + #dscam003#31;
               #dscam003#68 = #dscam003#68 + #dscam003#65;
               #dscam003#31 = 0; #dscam003#65 = 0;
               |SI #dscam003#23 = 0;
                  #dscam003#14 = "L"; #dscam003#15 = "Recibo Compensado";
               |SINO;
                  |SI #dscam003#23 = #dscam003#30;
                     #dscam003#14 = "e"; #dscam003#15 = "Pendiente de compensar";
                  |SINO;
                     #dscam003#14 = "E"; #dscam003#15 = "Compensado parcialmente";
                  |FINSI;
               |FINSI;
               #dscam003#30 = #dscam003#22 + #dscam003#24 + #dscam003#25 + #dscam003#38;
               #dscam003#30 = #dscam003#30 - (#dscam003#54 + #dscam003#55);
               #dscam003#64 = #dscam003#60 + #dscam003#62 + #dscam003#63 + #dscam003#66 + #dscam003#56;
               #dscam003#64 = #dscam003#64 - (#dscam003#67 + #dscam003#68);
               #dscam003#31 = #dscam003#30 - #dscam003#23; #dscam003#65 = #dscam003#64 - #dscam003#61;
               |GRABA 020#dscam003.a; |LIBERA #dscam003;
            |FINSI;

            |REPON_FICHA #dscam003;
         |SINO;
            |DDEFECTO #dscam004;
            #dscam004#17 = #dscam003#40;
            #dscam004#15 = #dscam003#27; || Libro
            #dscam004#0  = #dscam003#0;  || Serie
            #dscam004#1  = #dscam003#1;  || Factura
            #dscam004#2  = #dscam003#2;  || Recibo
            #dscam004#3  = 1;            || N§ cobro
            #dscam004#4  = #agif114@#12; || Cuenta de cobro
            |SI #dscam003#23 = #dscam003#30;
               #dscam004#5  = "COB";
            |SINO;
               #dscam004#5  = "COP";
            |FINSI;
            #dscam004#6  = #dscam003#26; || Fecha de cobro
            #dscam004#7  = 0;            || Total Liquidado DIVISA
            #dscam004#25 = 0;            || Total Liquidado BASE
            #dscam004#8  = #dscam003#23; || Total a cobrar DIVISA
            #dscam004#26 = #dscam003#61; || Total a cobrar BASE
            nImporte = #dscam004#8;
            #dscam004#12 = #dscam003#24; || Gastos de negociacion DIVISA
            #dscam004#29 = #dscam003#62; || Gastos de negociacion BASE

            #dscam004#13 = #dscam003#25; || Gastos de devolucion DIVISA
            #dscam004#30 = #dscam003#63; || Gastos de devolucion BASE

            #dscam004#18 = "N";          || Contabilizado

            |GRABA 020#dscam004.n;
         |FINSI;
      |FINSI;
   |FINSI;

   |LIBERA #dscam003;
|FPRC;

|DEFBUCLE RecibosVenta;
#agif114@#1003;
;
"  ",000001,001;
"zz",999999,999;
;
NULL,RecibosVenta;
|FIN;

|PROCESO PagosParciales;
   aAlfa  = #agif182@#16; |QBF aAlfa;  aAlfa  = aAlfa  % -104;
   |SI aAlfa ! aEjer; |ACABA; |FINSI;

   nSw = 1;
   |DDEFECTO #dscam009;
   #dscam009#14 = #dscam008#40;        || N§ documento
   #dscam009#13 = #dscam008#27;        || Libro
   #dscam009#0  = #agif182@#0;         || Serie
   #dscam009#1  = #agif182@#1;         || Factura
   #dscam009#2  = #agif182@#2;         || Recibo
   #dscam009#3  = LinPag + #agif182@#3;|| N§ de pago
   #dscam009#4  = #agif182@#5;         || Cuenta de pago
   |SI #agif182@#7 = #agif182@#9;
      #dscam009#5  = "PAG";
   |SINO;
      #dscam009#5  = "PAP";
   |FINSI;
   #dscam009#6  = #agif182@#6;         || Fecha de vencimiento
   #dscam009#7  = #agif182@#8 / #dscam008#61;  || Total liquidado DIVISA
   #dscam009#20 = #agif182@#8;                 || Total liquidado BASE
   #dscam009#8  = #agif182@#9 / #dscam008#61;  || Importe efecto DIVISA
   #dscam009#21 = #agif182@#9;                 || Importe efecto BASE
   #dscam009#11 = #agif182@#11 / #dscam008#61; || Gastos de negociacion DIVISA
   #dscam009#23 = #agif182@#11;                || Gastos de negociacion BASE
   #dscam009#12 = #agif182@#12 / #dscam008#61; || Otros Gastos DIVISA
   #dscam009#24 = #agif182@#12;                || Otros Gastos BASE
   #dscam009#13 = #dscam008#27;       || Libro
   #dscam009#14 = #dscam008#40;       || N§ documento
   #dscam009#16 = #agif182@#10;       ||
   |GRABA 020#dscam009.n;

|FPRC;

|DEFBUCLE PagosParciales;
#agif182@#1004;
;
#agif116@#17,#agif116@#0,#agif116@#1,001;
#agif116@#17,#agif116@#0,#agif116@#1,999;
;
NULL,PagosParciales;
|FIN;

|PROCESO RecibosCompra;
   #agif112@#0 = #agif116@#2; |LEE 000#agif112@.=;
   |SI FSalida ! 0; |DDEFECTO #agif112@; |FINSI;

   #agif079@#66 = #agif116@#17;
   #agif079@#0  = #agif116@#0;
   |LEE 101#agif079@.=;
   |SI FSalida = 0;
      #agif324@#0 = #agif079@#75;
      Divisa = #agif324@#0; |HAZ CargaNDeci;
   |FINSI;

   |LEE 000#agif324@.=;

   nSw = 0;
   |SELECT #2#dscam008;
   #dscam008#27 = "00";         || Libro
   #dscam008#0  = #agif116@#17; || Serie
   #dscam008#1  = #agif116@#0;  || Factura
   #dscam008#2  = #agif116@#1;  || N§ recibo
   |LEE 000#dscam008.];
   |PARA; |SI FSalida = 0; |Y #dscam008#27 = "00"; |Y #dscam008#0  = #agif116@#17;
    |Y #dscam008#1  = #agif116@#0; |Y #dscam008#2  = #agif116@#1; |HACIENDO;
       aAlfa  = #dscam008#4; |QBF aAlfa;  aAlfa  = aAlfa  % -104;
       aAlfa1 = #agif116@#7; |QBF aAlfa1; aAlfa1 = aAlfa1 % -104;
       |SI aAlfa = aAlfa1;
          nSw = 1; |SAL;
       |SINO;
          |LEE 000#dscam008.s;
       |FINSI;
   |SIGUE;
   |SI nSw = 1; |SELECT #1#dscam008; |ACABA; |FINSI;

   |SELECT #1#dscam008;
   |DDEFECTO #dscam008;
   #dscam008#61 = #agif079@#79 / #agif079@#76;  || Cambio Divisa
   |SI #dscam008#61 = 0; #dscam008#61 = 1; |FINSI;
   #dscam008#59 = #agif079@#75;  || Divisa
   #dscam008#60 = #agif324@#1;   || Nombre Divisa

   #dscam008#27 = "00";         || Libro
   #dscam008#0  = #agif116@#17; || Serie
   #dscam008#1  = #agif116@#0;  || Factura
   #dscam008#2  = #agif116@#1;  || N§ recibo
   #dscam008#3  = #agif116@#8;  || Fecha de vencimiento
   #dscam008#4  = #agif116@#7;  || Fecha de emision
   #dscam008#5  = #agif112@#10; || Cuenta de proveedor
   #dscam008#6  = #agif116@#3;  || Nombre de proveedor
   #dscam008#12 = #agif116@#18; || Tipo de recibo
   #dscam008#22 = #agif116@#9 / #dscam008#61;  || Importe recibo DIVISA
   #dscam008#32 = #agif116@#2;  || Codigo proveedor
   #dscam008#53 = #agif116@#9;                 || Importe recibo BASE
   #dscam008#23 = #agif116@#16 / #dscam008#61; || Importe liquidado DIVISA
   #dscam008#57 = #agif116@#16;                || Importe liquidado BASE
   #dscam008#25 = #dscam008#22 + #dscam008#24 + #dscam008#38; || Total Recibo DIVISA
   #dscam008#56 = #dscam008#53 + #dscam008#54 + #dscam008#55; || Total Recibo BASE
   #dscam008#26 = #agif116@#13; || Fecha de pago
   #dscam008#31 = #dscam008#25 - #dscam008#23; || Total pendiente DIVISA
   #dscam008#58 = #dscam008#56 - #dscam008#57; || Total pendiente BASE
   #dscam008#46 = #dscaz026#1;  || Empresa gestion
   #dscam008#40 = UltRecibo;
   |SI #dscam008#23 = 0;
      #dscam008#14 = "P";
      #dscam008#15 = "Pendiente de pago";
   |SINO;
      |SI #dscam008#31 = 0;
         #dscam008#14 = "L";
         #dscam008#15 = "Recibo liquidado";
      |SINO;
         #dscam008#14 = "C";
         #dscam008#15 = "Parcialmente pagado";
      |FINSI;
      #dscam008#26 = #dscam008#3;
   |FINSI;
   UltRecibo = UltRecibo + 1;

   #dscam008#29 = #agif116@#6;  || Aceptado
   #dscam008#48 = #agif116@#10; || Impreso
   #dscam008#50 = #agif116@#15; || Cuenta de pago
   #dscam008#51 = #agif116@#5;  || Domiciliado
   #dscam008#52 = #agif116@#4;  || Domiciliacion
   |GRABA 020#dscam008.c;

   aEjer = #agif116@#7; |QBF aEjer; aEjer = aEjer % -104;
   nSw = 0; |BUCLE PagosParciales;
   |SI nSw = 0;
      |SI #dscam008#23 ! 0;
         |DDEFECTO #dscam009;
         #dscam009#14 = #dscam008#40;        || N§ documento
         #dscam009#13 = #dscam008#27;        || Libro
         #dscam009#0  = #dscam008#0;         || Serie
         #dscam009#1  = #dscam008#1;         || Factura
         #dscam009#2  = #dscam008#2;         || Recibo
         #dscam009#3  = 1;                   || N§ de pago
         #dscam009#4  = #agif116@#12;        || Cuenta de pago
         |SI #dscam008#23 = #dscam008#25;
            #dscam009#5  = "PAG";
         |SINO;
            #dscam009#5  = "PAP";
         |FINSI;
         #dscam009#6  = #dscam008#3;         || Fecha de vencimiento
         #dscam009#7  = 0;                   || Total liquidado DIVISA
         #dscam009#20 = 0;                   || Total liquidado BASE
         #dscam009#8  = #dscam008#23;        || Importe efecto DIVISA
         #dscam009#21 = #dscam008#57;        || Importe efecto BASE
         #dscam009#11 = #dscam008#24;        || Gastos de negociacion DIVISA
         #dscam009#23 = #dscam008#54;        || Gastos de negociacion BASE
         #dscam009#12 = #dscam008#38;        || Otros Gastos DIVISA
         #dscam009#24 = #dscam008#55;        || Otros Gastos BASE
         #dscam009#13 = #dscam008#27;        || Libro
         #dscam009#14 = #dscam008#40;        || N§ documento
         #dscam009#16 = "N";                 || Contabilizado
         |GRABA 020#dscam009.n;
      |FINSI;
   |FINSI;
   |LIBERA #dscam008;
|FPRC;

|DEFBUCLE RecibosCompra;
#agif116@#1003;
;
"  ",000001,001;
"zz",999999,999;
;
NULL,RecibosCompra;
|FIN;

|PROCESO LinRemesas;
   |SELECT #6#dscam003;
   #dscam003#27 = "00";  || Libro
   #dscam003#0  = #agif179@#4;  || Serie
   #dscam003#1  = #agif179@#3;  || Factura
   #dscam003#2  = #agif179@#5;  || Recibo
   #dscam003#69 = " ";          || Documento a cuenta
   #dscam003#40 = 1;            || N§ documento
   |LEE 000#dscam003.];
   |SI FSalida = 0; |Y #dscam003#0  = #agif179@#4; |Y #dscam003#1  = #agif179@#3;
    |Y #dscam003#2  = #agif179@#5;
      |SELECT #1#dscam003;
      |LEE 101#dscam003.=;
      |SI FSalida = 0; |Y #dscam003#16 = 0;
         |DDEFECTO #dscam013;
         #dscam013#0  = #dscam012#0;     || Codigo de remesa
         #dscam013#1  = #agif179@#1;     || Linea de remesa
         #dscam013#2  = "00";            || Libro
         #dscam013#3  = #agif179@#3;     || N§ factura
         #dscam013#4  = #agif179@#4;     || Serie
         #dscam013#5  = #agif179@#5;     || N§ Cobro
         #dscam013#6  = #agif179@#6;     || Fecha Vto.
         #dscam013#7  = #agif179@#7;     || Importe
         #dscam013#8  = #agif179@#8;     || Dias
         #dscam013#9  = #agif179@#9;     || % Interes
         #dscam013#10 = #agif179@#10;    || Interes
         #dscam013#11 = #agif179@#11;    || Cuenta Cliente
         #dscam013#12 = #agif179@#16;    || Recibo vencido
         #dscam013#13 = #agif179@#13;    || Nombre Cliente
         #dscam013#14 = #agif179@#14;    || Poblacion
         #dscam013#15 = "N";             || Contabilizado Vto.
         #dscam013#16 = #dscam003#40;
         |GRABA 020#dscam013.n;
         |LIBERA #dscam013;
         |LEE 101#dscam013.=;

         #dscam003#26 = #dscam012#1;
         #dscam003#17 = #dscam013#0;
         #dscam003#37 = #dscam013#1;
         #dscam003#53 = #dscam012#1;
         #dscam003#14 = "R";
         #dscam003#15 = "Remesado";
         |GRABA 020#dscam003.a; |LIBERA #dscam003;
      |SINO;
         #dscam012#5  = #dscam012#5  - 1;            || Numero de efectos
         #dscam012#10 = #dscam012#10 - #agif179@#7;  || Total Comision
         #dscam012#11 = #dscam012#5  * #dscam012#6;  || Total Comision
         #dscam012#12 = #dscam012#12 - #agif179@#10; || Total interes
         |SI #agif179@#16 = "S";
            #dscam012#13 = #dscam012#13 - #agif179@#7; || Total vencidos
         |FINSI;
         |GRABA 020#dscam012.a; |LIBERA #dscam012;
         |LEE 101#dscam012.=;
      |FINSI;
      |SELECT #1#dscam003;
   |FINSI;

   NumRecs = NumRecs + 1;
|FPRC;

|DEFBUCLE LinRemesas;
#agif179@#1002;
;
#agif178@#0,001;
#agif178@#0,999;
;
NULL,LinRemesas;
|FIN;

|PROCESO Remesas;
   #dscam014#0 = #agif178@#2;
   |LEE 000#dscam014.=;
   |SI FSalida ! 0;
      |DDEFECTO #dscam014;
   |FINSI;

   |DDEFECTO #dscam012;
   #dscam012#0  = UltRemesa + #agif178@#0;  || Numero de remesa
   #dscam012#1  = #agif178@#1;  || Fecha de remesa
   #dscam012#2  = #agif178@#2;  || Codigo de banco
   #dscam012#3  = #agif178@#3;  || Nombre del banco
   #dscam012#4  = #dscam014#7;  || Cta.Cte.
   #dscam012#5  = #agif178@#5;  || Numero de efectos
   #dscam012#6  = #agif178@#6;  || Comision por efecto
   #dscam012#7  = #agif178@#7;  || Tipo de recibo
   #dscam012#8  = #agif178@#8;  || Emitida
   #dscam012#9  = #agif178@#9;  || Contabilizada
   #dscam012#10 = #agif178@#10; || Total remesa
   #dscam012#11 = #agif178@#11; || Total Comision
   #dscam012#12 = #agif178@#12; || Total Interes
   #dscam012#13 = #agif178@#13; || Total Vencidos
   #dscam012#14 = #agif178@#14; || Cuenta contable
   #dscam012#15 = #agif178@#15; || Digito contable
   #dscam012#16 = #dscam014#34; || Entidad
   #dscam012#17 = #dscam014#35; || Sucursal
   #dscam012#18 = #dscam014#37; || Cuenta
   #dscam012#19 = #dscam014#36; || DC
   |GRABA 020#dscam012.n; |LIBERA #dscam012;
   |LEE 101#dscam012.=;

   |BUCLE LinRemesas;

   |LIBERA #dscam012;
|FPRC;

|DEFBUCLE Remesas;
#agif178@#1001;
;
00001;
99999;
;
NULL,Remesas;
|FIN;

|PROCESO Bancos;
   |DDEFECTO #dscam014;
   #dscam014#0  = #agif184@#0;  || Codigo de banco
   #dscam014#1  = #agif184@#1;  || Nombre de banco
   #dscam014#2  = #agif184@#2;  || Direccion de banco
   #dscam014#3  = #agif184@#3;  || Codigo postal 1
   #dscam014#4  = #agif184@#4;  || Codigo postal 2
   #dscam014#5  = #agif184@#5;  || Provincia
   #dscam014#6  = #agif184@#6;  || Poblacion
   #dscam014#7  = #agif184@#7;  || Telefono
   #dscam014#8  = #agif184@#8;  || Cta.Cte.
   #dscam014#9  = #agif184@#9;  || C.C.C.
   #dscam014#10 = #agif184@#10; || Cta.Contable
   #dscam014#11 = #agif184@#11; || Digito Contable
   #dscam014#12 = #agif184@#12; || Riesgo concedido
   #dscam014#13 = #agif184@#13; || Riesgo Actual
   #dscam014#14 = #agif184@#14; || CIF
   #dscam014#15 = #agif184@#15; || Fax
   #dscam014#16 = #agif184@#16; || Comision por efecto
   #dscam014#17 = #agif184@#17; || Interes anual
   #dscam014#18 = #agif184@#18; || Persona de contacto
   #dscam014#19 = #agif184@#19; || Observaciones
   #dscam014#20 = #agif184@#20; || Interes desde 1
   #dscam014#21 = #agif184@#21; || Interes desde 2
   #dscam014#22 = #agif184@#22; || Interes desde 3
   #dscam014#23 = #agif184@#23; || Interes desde 4
   #dscam014#24 = #agif184@#24; || Interes hasta 1
   #dscam014#25 = #agif184@#25; || Interes hasta 2
   #dscam014#26 = #agif184@#26; || Interes hasta 3
   #dscam014#27 = #agif184@#27; || Interes hasta 4
   #dscam014#28 = #agif184@#28; || % Interes 1
   #dscam014#29 = #agif184@#29; || % Interes 2
   #dscam014#30 = #agif184@#30; || % Interes 3
   #dscam014#31 = #agif184@#31; || % Interes 4
   Comodin = #agif184@#9; |QBF Comodin;
   Comodin1 = Comodin % 0104;
   #dscam014#34 = Comodin1;     || Entidad
   Comodin1 = Comodin % 0504;
   #dscam014#35 = Comodin1;     || Sucursal
   #dscam014#37 = #agif184@#8;  || Cuenta

   eaEntidad = #dscam014#34; eaSucursal = #dscam014#35; eaCuenta = #dscam014#37;
   |HAZ CalculoDC;
   #dscam014#36 = eaDC;         || DC
   |GRABA 020#dscam014.n;
|FPRC;

|DEFBUCLE Bancos;
#agif184@#1001;
;
001;
999;
;
NULL,Bancos;
|FIN;

|PROCESO MiraRiesgo;
   SwHayDatos = 1; |ERROR10;
|FPRC;

|DEFBUCLE MiraRiesgo;
#dscam038#2;
;
Empresa,#dscaz026#1," ",001;
Empresa,#dscaz026#1,"z",999;
;
NULL,MiraRiesgo;
|FIN;

|PROCESO EntregasCta;
   #agif024@#0 = #agif148@#2; |LEE 000#agif024@.=;
   |SI FSalida ! 0; |O #agif024@#0 = "      "; |DDEFECTO #agif024@; |FINSI;

   #agif091@#0 = #agif148@#24; |LEE 000#agif091@.=;
   |SI FSalida ! 0; |DDEFECTO #agif091@; |FINSI;

   |SI #agif148@#30 = "A";
      #agif010@#52 = #agif148@#20;
      #agif010@#0  = #agif148@#17;
      |LEE 000#agif010@.=;
      |SI FSalida ! 0;
         |DDEFECTO #agif010@;
         #agif010@#68 = "EUR";
         #agif010@#69 = 1;
         #agif010@#70 = "B";
      |FINSI;
   |SINO;
      #agif104@#25 = #agif148@#20;
      #agif104@#0  = #agif148@#17;
      |LEE 000#agif104@.=;
      |SI FSalida ! 0;
         |DDEFECTO #agif104@;
         #agif104@#35 = "EUR";
         #agif104@#36 = 1;
         #agif104@#37 = "B";
      |FINSI;
   |FINSI;

   |DDEFECTO #dscam003;
   #dscam003#0  = #agif148@#20; || Serie
   #dscam003#1  = #agif148@#17; || Factura
   #dscam003#2  = #agif148@#18; || Recibo
   #dscam003#3  = #agif148@#8;  || Fecha Vencimiento
   #dscam003#4  = #agif148@#7;  || Fecha Emision
   #dscam003#5  = #agif148@#12; || Cuenta cliente
   #dscam003#6  = #agif148@#3;  || Nombre cliente
   #dscam003#7  = #agif148@#4;  || Nombre banco
   #dscam003#8  = #agif024@#32; || Entidad Banco Cliente
   #dscam003#9  = #agif024@#33; || Sucursal Banco Cliente
   #dscam003#10 = #agif024@#31; || DC Banco Cliente
   #dscam003#11 = #agif024@#30; || Cuenta Banco Cliente
   #dscam003#12 = #agif148@#22; || Tipo recibo
   #dscam003#14 = "e";          || Estado
   #dscam003#15 = "Pendiente de compensar"; || Descripcion estado
   #dscam003#22 = #agif148@#9;  || Importe recibo (DIV)
   #dscam003#26 = #agif148@#16; || Fecha de cobro
   #dscam003#60 = #agif148@#9;  || Importe recibo (BAS)
   #dscam003#30 = #agif148@#9;  || Importe Total Recibo (DIV)
   #dscam003#64 = #agif148@#9;  || Importe Total Recibo (BAS)
   #dscam003#23 = #agif148@#9;  || Importe Liquidado entrega (DIV)
   #dscam003#61 = #agif148@#9;  || Importe Liquidado entrega (BAS)
   #dscam003#32 = #agif148@#2;  || Codigo Cliente
   #dscam003#40 = UltRecibo;    || Numero documento
   UltRecibo = UltRecibo + 1;
   #dscam003#47 = "S";          || Recibo a cuenta
   |SI #agif148@#30 = "A";
      #dscam003#57 = #agif010@#68;  || Divisa
      |SI #dscam003#57 = "EUR"; #dscam003#58 = "EURO"; |FINSI;
      #dscam003#59 = #agif010@#69;  || Cambio Divisa
   |FINSI;
   |SI #agif148@#30 = "P";
      #dscam003#57 = #agif104@#35;  || Divisa
      |SI #dscam003#58 = "EUR"; #dscam003#58 = "EURO"; |FINSI;
      #dscam003#59 = #agif104@#36;  || Cambio Divisa
   |FINSI;
   #dscam003#69 = #agif148@#30; || Tipo de documento a cuenta
   #dscam003#80 = #agif148@#24; || Forma de pago
   #dscam003#81 = #agif091@#1;  || Descripcion forma de pago
   |GRABA 020#dscam003.n;

   |DDEFECTO #dscam004;
   #dscam004#17 = #dscam003#40;
   #dscam004#3  = 1;
   |GRABA 020#dscam004.b;
   #dscam004#0 = #dscam003#0;
   #dscam004#1 = #dscam003#1;
   #dscam004#2 = #dscam003#2;
   #dscam004#4 = #dscam003#74;
   #dscam004#5 = "RAC";
   #dscam004#6 = ~;
   #dscam004#7 = 0;
   #dscam004#8 = #dscam003#23; #dscam004#26 = #dscam003#61;
   #dscam004#15 = #dscam003#27;
   Comodin = "Entrega a cuenta";
   #dscam004#16 = Comodin;
   #dscam004#25 = #dscam003#61;
   #dscam004#42 = #dscam003#74;
   |GRABA 020#dscam004.a; |LIBERA #dscam004;
|FPRC;

|DEFBUCLE EntregasCta;
#agif148@#1004;
;
" ", "  ", 000000, 00;
"z", "zz", 999999, 99;
;
NULL,EntregasCta;
|FIN;

|PROGRAMA;
   |CLS;
   |CABEZA "Importacion desde contabilidad";
   SwAbiertos = 0;
   nDeci_Base = 6; nDeci_Div = 6;

   Empresa = #48#0;

   |ABRE #dscam051;
   |LEE 101#dscam051.p;
   |SI FSalida ! 0;
      |DDEFECTO #dscam051;
      |GRABA 020#dscam051.n;
   |FINSI;
   |LIBERA #dscam051;
   |CIERRA #dscam051;

   |ABRE #dscaz026;
   |LEE 000#dscaz026.p;
   |SI FSalida ! 0;
      |DDEFECTO #dscaz026;
      |GRABA 020#dscaz026.n;
      |LEE 101#dscaz026.p;
   |FINSI;

   |PINPA #0#0; |PINDA #0#0;

   |ENDATOS #1#0;
   |SI FSalida = 0;
      |GRABA 020#dscaz026.a;
      Comodin = #dscaz026#0; |QBF Comodin;
      Comodin2 = ("00" + #dscaz026#1) % -102;
      Comodin = Comodin + "fich/" + Comodin2 + "/";
      |PATH_DAT #1  Comodin; |ABRE #1;
      |PATH_DAT #2  Comodin; |ABRE #2;
      |PATH_DAT #3  Comodin; |ABRE #3;
      |PATH_DAT #4  Comodin; |ABRE #4;
      |PATH_DAT #5  Comodin; |ABRE #5;
      |PATH_DAT #6  Comodin; |ABRE #6;
      |PATH_DAT #7  Comodin; |ABRE #7;
      |PATH_DAT #9  Comodin; |ABRE #9;
      |PATH_DAT #10 Comodin; |ABRE #10;
      |PATH_DAT #11 Comodin; |ABRE #11;
      |PATH_DAT #12 Comodin; |ABRE #12;
      |PATH_DAT #13 Comodin; |ABRE #13;
      |PATH_DAT #14 Comodin; |ABRE #14;
      |PATH_DAT #15 Comodin; |ABRE #15;
      |PATH_DAT #16 Comodin; |ABRE #16;
      |PATH_DAT #17 Comodin; |ABRE #17;
      |PATH_DAT #18 Comodin; |ABRE #18;
      |PATH_DAT #19 Comodin; |ABRE #19;

      |DELINDEX #dscam014;

      |ABRE #dscam012;
      |ABRE #dscam013;
      |ABRE #dscam014;
      |ABRE #dscam003;
      |ABRE #dscam004;
      |ABRE #dscam008;
      |ABRE #dscam009;
      |ABRE #dscam014;
      |ABRE #dscam048;
      |DBASE Comodin; Comodin = Comodin + "fich/"; |PATH_DAT #34 Comodin;
      |ABRE #dscam038;

      |LEE 101#agif321@.p;
      |SI FSalida ! 0;
         |DDEFECTO #agif321@;
         |GRABA 020#agif321@.n;
         |LEE 101#agif321@.p;
      |FINSI;
      |LIBERA #agif321@;

      #agif324@#0 = #agif321@#9;
      |LEE 000#agif324@.=;
      |SI FSalida = 0;
         CambioBaseVn = #agif324@#2;
         CambioBaseCm = #agif324@#3;
      |FINSI;

      |BUCLE Bancos;

      |LEE 000#dscam003.u;
      |SI FSalida ! 0; |DDEFECTO #dscam003; #dscam003#40 = 0; |FINSI;

      UltRecibo = #dscam003#40 + 1;
      #dscam004#17 = #dscam003#40;
      #dscam004#3 = 1;
      |LEE 000#dscam004.];
      |SI FSalida = 0; |Y #dscam004#17 = #dscam003#40;
         LinCob = #dscam004#3;
         UltOp = #dscam004#5;
      |SINO;
         LinCob = 0;
      |FINSI;
      |SI #dscaz026#3 = "S";
         |BUCLE EntregasCta; |BUCLE RecibosVenta;
      |FINSI;

      |LEE 000#dscam008.u;
      |SI FSalida ! 0; |DDEFECTO #dscam008; #dscam008#40 = 0; |FINSI;
      UltRecibo = #dscam008#40 + 1;
      #dscam009#17 = #dscam008#40;
      #dscam009#3 = 1;
      |LEE 000#dscam009.];
      |SI FSalida = 0; |Y #dscam009#17 = #dscam008#40;
         LinCob = #dscam009#3;
         UltOp = #dscam009#5;
      |SINO;
         LinCob = 0;
      |FINSI;
      |SI #dscaz026#4 = "S"; |BUCLE RecibosCompra; |FINSI;

      |LEE 000#dscam012.u;
      |SI FSalida ! 0;
         |DDEFECTO #dscam012;
         UltRemesa = 0;
      |SINO;
         UltRemesa = #dscam012#0;
      |FINSI;
      |SI #dscaz026#5 = "S"; |BUCLE Remesas; |FINSI;

      |CIERRA #dscam038;
      |CIERRA #dscam048;
      |CIERRA #dscam014;
      |CIERRA #dscam009;
      |CIERRA #dscam008;
      |CIERRA #dscam004;
      |CIERRA #dscam003;
   |FINSI;

   |SI SwAbiertos = 1;
      |CIERRA #agif091@; |DESCARGA_DEF #agif091@;
      |CIERRA #agif010@; |DESCARGA_DEF #agif010@;
      |CIERRA #agif104@; |DESCARGA_DEF #agif104@;
      |CIERRA #agif112@; |DESCARGA_DEF #agif112@;
      |CIERRA #agif148@; |DESCARGA_DEF #agif148@;
      |CIERRA #agif079@; |DESCARGA_DEF #agif079@;
      |CIERRA #agif321@; |DESCARGA_DEF #agif321@;
      |CIERRA #agif084@; |DESCARGA_DEF #agif084@;
      |CIERRA #agif324@; |DESCARGA_DEF #agif324@;
      |CIERRA #agif134@; |DESCARGA_DEF #agif134@;
      |CIERRA #agif025@; |DESCARGA_DEF #agif025@;
      |CIERRA #agif024@; |DESCARGA_DEF #agif024@;
      |CIERRA #agif184@; |DESCARGA_DEF #agif184@;
      |CIERRA #agif182@; |DESCARGA_DEF #agif182@;
      |CIERRA #agif179@; |DESCARGA_DEF #agif179@;
      |CIERRA #agif178@; |DESCARGA_DEF #agif178@;
      |CIERRA #agif174@; |DESCARGA_DEF #agif174@;
      |CIERRA #agif116@; |DESCARGA_DEF #agif116@;
      |CIERRA #agif114@; |DESCARGA_DEF #agif114@;
      |CIERRA #agif041@; |DESCARGA_DEF #agif041@;
   |FINSI;

   |CIERRA #dscaz026;
|FPRO;
