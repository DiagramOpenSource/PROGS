     CALCULO ANTES DEL tiquet: 4197/151

/*
|FICHEROS;
   dscaz056 #0;

   dscaw041 #1;
   dscaw042 #2;

   dscam003 #3;
   dscam008 #8;

   agif134@ #100;
   agif133@ #101;
   agif084@ #103;

   agif079@ #102;
|FIN;

|VARIABLES;
   &HayGestion = 0; &Divisa = ""; &nDeci_Div = 0; &nDeci_Base = 0;
   &Switch = 0; &Diff = "";

   aAlfa  = "";
   aAlfa1  = "";
   Sw     = 0;
   nDec   = 0;
   nCalc  = 0;
   nCalc1 = 0;
     aParam = "";
     aFic = "";
     aMail = "";
     aEmp = "";
     fHoy = @;
     aIP = "";
     aServidor = "";
     aFrom = "";
     aTo = "";
     aMensaje = "";
     aSubject = "";
     aDjunto = "";
     aDImp = " "; aHImp = " ";
     nHay = 0;
     nHayCom = 0;
     nHayFac = 0;
|FIN;

|PROCESO dscaw041;
     nHayFac = 0;
     |DDEFECTO #2;
     #2#0 = #1#0;
     #2#1 = #1#1;
     |LEE 000#2];
     |PARA; |SI FSalida = 0; |Y #2#0 = #1#0; |Y #2#1 = #1#1; |HACIENDO;
          |SI #2#19 = #1#8;
               nHayFac = 1; |SAL;
          |FINSI;
          |LEE 000#2s;
     |SIGUE;
     |SI nHayFac = 0;
          |BORRA 020#1a;
     |FINSI;
|FPRC;

|DEFBUCLE dscaw041;
     #1#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dscaw041;
|FIN;

|PROCESO RecCompra;
     nHayCom = 1;
     |ERROR10;
|FPRC;

|DEFBUCLE RecCompra;
     #8#7;
     ;
     00, #2#0, #2#1, #2#2, 000000001;
     00, #2#0, #2#1, #2#2, 999999999;
     ;
     NULL, RecCompra;
|FIN;

|PROCESO dscaw042;
     |SI #2#19 = "V";
          #3#0 = #2#0;
          #3#1 = #2#1;
          #3#2 = #2#2;
          #3#4 = "01.01.0000";
          #3#12 = "";
          #3#32 = "";
          |LEE 000#3];
          |SI FSalida = 0; |Y #3#0 = #2#0; |Y #3#1 = #2#1; |Y #3#2 = #2#2;
               |BORRA 020#2a;
          |FINSI;
     |FINSI;
     |SI #2#19 = "C";
          nHayCom = 0;
          |BUCLE RecCompra;
          |SI nHayCom ! 0;
               |BORRA 020#2a;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE dscaw042;
     #2#1;
     ;
     "     ", 000000, 000, "     ", 000000, 000, 000000000,"C";
     "zzzzz", 999999, 999, "zzzzz", 999999, 999, 999999999,"V";
     ;
     NULL, dscaw042;
|FIN;

|PROCESO EnviaMail;
     fHoy = ~;
     |DFICE aEmp; aEmp = aEmp % -205;

     aIP = "xdscorreo.dv.diagram.net";
     aServidor = "asmtp.diagram.es";

     aFrom = aMail;
     aTo = aMail;
     aSubject = "Chequeo facturas/recibos empresa:" + aEmp + " (" + fHoy + ")";
     aMensaje = "$$$$" + aFic;
     aDjunto = aFic;
     |DSCORREO_ENVIA aIP, aServidor, aFrom, aTo, aSubject, aMensaje, aDjunto;
     |FBORRA aFic;
|FPRC;

|PROCESO VtosVenta;
   Divisa = #dscaw041#5; |HAZ TomaDecim;
   |DDEFECTO #dscaw042;
   #dscaw042#0  = #agif133@#4;
   #dscaw042#1  = #agif133@#0;
   #dscaw042#2  = #agif133@#1;
   #dscaw042#7  = #agif134@#1;
   #dscaw042#8  = #agif133@#3;
   #dscaw042#11 = #agif134@#58;
   #dscaw042#12 = #agif133@#2;
   aAlfa = #agif134@#31; |QBF aAlfa;
   aAlfa = aAlfa - ".";
   |SI FSalida ! 0;
      nCalc = FSalida + 98;
      aAlfa = aAlfa % nCalc; |QBF aAlfa;
      aAlfa = aAlfa % 0;     nDec = aAlfa;
   |SINO;
      nDec = 0;
   |FINSI;
   |SI nDeci_Base ! 0; nDec = nDeci_Base; |FINSI;
   nCalc = (#agif133@#2 * #agif134@#31) / #agif134@#78;
   nCalc = nCalc ! nDec;
   #dscaw042#13 = nCalc;
   #dscaw042#19 = "V";
   |GRABA 020#dscaw042.n;
|FPRC;

|DEFBUCLE VtosVenta;
#agif133@#1003;
;
#agif134@#49, #agif134@#0, 001;
#agif134@#49, #agif134@#0, 999;
;
NULL,VtosVenta;
|FIN;

|DEFBUCLE VtosVenta1;
#agif133@#1003;
;
#agif084@#48, #agif084@#0, 001;
#agif084@#48, #agif084@#0, 999;
;
NULL,VtosVenta;
|FIN;

|PROCESO FactVenta;
   Divisa = #agif134@#58; |HAZ CargaNDeci;

   |DDEFECTO #dscaw041;
   #dscaw041#0 = #agif134@#49;
   #dscaw041#1 = #agif134@#0;
   #dscaw041#2 = #agif134@#2;
   #dscaw041#3 = #agif134@#3;
   #dscaw041#4 = #agif134@#1;
   #dscaw041#5 = #agif134@#58;
   #dscaw041#6 = #agif134@#78;
   #dscaw041#7 = #agif134@#31;
   #dscaw041#8 = "V";
   |GRABA 020#dscaw041.n;

   |BUCLE VtosVenta;
|FPRC;

|DEFBUCLE FactVenta;
   #agif134@#1002;
   1,45;
   #dscaz056#2,#dscaz056#3,#dscaz056#1,aDImp;
   #dscaz056#5,#dscaz056#6,#dscaz056#4,aHImp;
   ;
   NULL,FactVenta;
|FIN;

|PROCESO FactDire;
   Divisa = #agif084@#65; |HAZ CargaNDeci;

   |DDEFECTO #dscaw041;
   #dscaw041#0 = #agif084@#48;
   #dscaw041#1 = #agif084@#0;
   #dscaw041#2 = #agif084@#3;
   #dscaw041#3 = #agif084@#4;
   #dscaw041#4 = #agif084@#1;
   #dscaw041#5 = #agif084@#65;
   #dscaw041#6 = #agif084@#71;
   #dscaw041#7 = #agif084@#41;
   #dscaw041#8 = "V";
   |GRABA 020#dscaw041.n;

   |BUCLE VtosVenta1;
|FPRC;

|DEFBUCLE FactDire;
   #agif084@#1002;
   1,10;
   #dscaz056#2,#dscaz056#3,#dscaz056#1,aDImp;
   #dscaz056#5,#dscaz056#6,#dscaz056#4,aHImp;
   ;
   NULL,FactDire;
|FIN;

|PROCESO FactCompra;
   Divisa = #agif079@#75; |HAZ CargaNDeci;

   |DDEFECTO #dscaw041;
   #dscaw041#0 = #agif079@#66;
   #dscaw041#1 = #agif079@#0;
   #dscaw041#2 = #agif079@#2;
   #dscaw041#3 = #agif079@#3;
   #dscaw041#4 = #agif079@#1;
   #dscaw041#5 = #agif079@#75;
   #dscaw041#6 = #agif079@#95;
   #dscaw041#7 = #agif079@#31;
   #dscaw041#8 = "C";
   |GRABA 020#dscaw041.n;

   |PARA nCalc = 33; |SI nCalc [ 44; |HACIENDO nCalc = nCalc + 1;
      |SI #agif079@#nCalc# ! 0;
         |DDEFECTO #dscaw042;
         #dscaw042#0  = #agif079@#66;
         #dscaw042#1  = #agif079@#0;
         #dscaw042#2  = nCalc - 32;
         #dscaw042#7  = #agif079@#1;
         |SI nCalc [ 38; nCalc1 = nCalc + 35; |SINO; nCalc1 = nCalc + 90; |FINSI;
         #dscaw042#8  = #agif079@#nCalc1#;
         #dscaw042#11 = #agif079@#75;
         #dscaw042#12 = #agif079@#nCalc#;
         aAlfa = #agif079@#31; |QBF aAlfa;
         aAlfa = aAlfa - ".";
         |SI FSalida ! 0;
            nCalc1 = FSalida + 98;
            aAlfa = aAlfa % nCalc1; |QBF aAlfa;
            aAlfa = aAlfa % 0; nDec = aAlfa;
         |SINO;
            nDec = 0;
         |FINSI;
         |SI nDeci_Base ! 0; nDec = nDeci_Base; |FINSI;
         nCalc1 = (#agif079@#nCalc# * #agif079@#31) / #agif079@#95;
         nCalc1 = nCalc1 ! nDec;
         #dscaw042#13 = nCalc1;
         #dscaw042#19 = "C";
         |GRABA 020#dscaw042.n;
      |FINSI;
   |SIGUE;
|FPRC;

|DEFBUCLE FactCompra;
   #agif079@#1002;
   1,45;
   #dscaz056#2,#dscaz056#3,#dscaz056#1,aDImp;
   #dscaz056#5,#dscaz056#6,#dscaz056#4,aHImp;
   ;
   NULL,FactCompra;
|FIN;

|PROCESO RecCarteraV;
   Divisa = #dscam003#57; |HAZ TomaDecim;
   Sw = 0;
   |DDEFECTO #dscaw042;
   #dscaw042#0 = #dscam003#0;
   #dscaw042#1 = #dscam003#1;
   #dscaw042#2 = #dscam003#2;
   |LEE 000#dscaw042.];
   |PARA; |SI FSalida = 0; |Y #dscaw042#0 = #dscam003#0; |Y #dscaw042#1 = #dscam003#1;
    |Y #dscaw042#2 = #dscam003#2; |HACIENDO;
       |SI #dscaw042#7 = #dscam003#4;
          Sw = 1;
          |SAL;
       |FINSI;
       |LEE 000#dscaw042.s;
   |SIGUE;
   |SI Sw = 0;
      |DDEFECTO #dscaw042;
      #dscaw042#0 = #dscam003#0;
      #dscaw042#1 = #dscam003#1;
      #dscaw042#2 = #dscam003#2;
   |FINSI;
   #dscaw042#3  = #dscam003#0;
   #dscaw042#4  = #dscam003#1;
   #dscaw042#5  = #dscam003#2;
   #dscaw042#6  = #dscam003#40;
   #dscaw042#9  = #dscam003#4;
   #dscaw042#10 = #dscam003#3;
   #dscaw042#14 = #dscam003#57;
   #dscaw042#15 = #dscam003#30;
   #dscaw042#16 = #dscam003#64;
   #dscaw042#17 = #dscam003#32;
   #dscaw042#18 = #dscam003#6;
   |SI Sw = 0;
      #dscaw042#19 = "V";
      |GRABA 020#dscaw042.n;
   |SINO;
      |GRABA 020#dscaw042.a;
   |FINSI;
|FPRC;

|DEFBUCLE RecCarteraV;
#dscam003#6;
4;
00,#dscaz056#2, #dscaz056#3, 001, " ", 000000001, #dscaz056#1;
99,#dscaz056#5, #dscaz056#6, 999, "z", 999999999, #dscaz056#4;
;
NULL,RecCarteraV;
|FIN;

|PROCESO RecCarteraC;
   Divisa = #dscam008#59; |HAZ TomaDecim;
   Sw = 0;
   |DDEFECTO #dscaw042;
   #dscaw042#0 = #dscam008#0;
   #dscaw042#1 = #dscam008#1;
   #dscaw042#2 = #dscam008#2;
   #dscaw042#19 = "C";
   |LEE 000#dscaw042.];
   |PARA; |SI FSalida = 0; |Y #dscaw042#0 = #dscam008#0; |Y #dscaw042#1 = #dscam008#1;
    |Y #dscaw042#2 = #dscam008#2; |HACIENDO;
       |SI #dscaw042#7 = #dscam008#4;
          Sw = 1;
          |SAL;
       |FINSI;
       |LEE 000#dscaw042.s;
   |SIGUE;
   |SI Sw = 0;
      |DDEFECTO #dscaw042;
      || #dscaw042#0 = #dscam008#0;
      || #dscaw042#1 = #dscam008#1;
      || #dscaw042#2 = #dscam008#2;
   |FINSI;
   #dscaw042#3  = #dscam008#0;
   #dscaw042#4  = #dscam008#1;
   #dscaw042#5  = #dscam008#2;
   #dscaw042#6  = #dscam008#40;
   #dscaw042#9  = #dscam008#4;
   #dscaw042#10 = #dscam008#3;
   #dscaw042#14 = #dscam008#59;
   #dscaw042#15 = #dscam008#25;
   #dscaw042#16 = #dscam008#56;
   #dscaw042#17 = #dscam008#32;
   #dscaw042#18 = #dscam008#6;
   |SI Sw = 0;
      #dscaw042#19 = "C";
      |GRABA 020#dscaw042.n;
   |SINO;
      |GRABA 020#dscaw042.a;
   |FINSI;
|FPRC;

|DEFBUCLE RecCarteraC;
#dscam008#2;
4;
00,#dscaz056#2, #dscaz056#3, 001, #dscaz056#1;
99,#dscaz056#5, #dscaz056#6, 999, #dscaz056#4;
;
NULL,RecCarteraC;
|FIN;

|PROCESO ListadoLin1;
   Diff = "";
   |SI #dscaw041#2  ! #dscaw042#17; Diff = "-->" ; |FINSI;
   |SI #dscaw042#7  ! #dscaw042#9;  Diff = "-->" ; |FINSI;
   |SI #dscaw042#8  ! #dscaw042#10; Diff = "-->" ; |FINSI;
   |SI #dscaw042#11 ! #dscaw042#14; Diff = "-->" ; |FINSI;
   |SI #dscaw042#12 ! #dscaw042#15; Diff = "-->" ; |FINSI;
   |SI #dscaw042#13 ! #dscaw042#16; Diff = "-->" ; |FINSI;
   |SI #dscaw042#1 = 0;             Diff = "-->" ; |FINSI;
   |SI #dscaw042#6 = 0;             Diff = "-->" ; |FINSI;
   |PRINT;
   #dscaw042#20 = "X"; |GRABA 020#dscaw042.a; |LIBERA #dscaw042;
|FPRC;

|DEFBUCLE ListadoLin1;
#dscaw042#1;
;
#dscaw041#0, #dscaw041#1, 000, "     ", 000000, 000, 000000000,#dscaw041#8;
#dscaw041#0, #dscaw041#1, 999, "zzzzz", 999999, 999, 999999999,#dscaw041#8;
be;
NULL,ListadoLin1;
|FIN;

|PROCESO ListadoLin2;
   |SI #dscaw042#1 = 0;             Diff = "-->" ; |FINSI;
   |SI #dscaw042#6 = 0;             Diff = "-->" ; |FINSI;
   |PRINT;
   #dscaw042#20 = "X"; |GRABA 020#dscaw042.a; |LIBERA #dscaw042;
|FPRC;

|DEFBUCLE ListadoLin2;
#dscaw042#1;
20;
"     ", 000000, 000, #dscaw041#0, #dscaw041#1, 000, 000000000,#dscaw041#8, " ";
"zzzzz", 999999, 999, #dscaw041#0, #dscaw041#1, 999, 999999999,#dscaw041#8, " ";
be;
NULL,ListadoLin2;
|FIN;

|PROCESO ListadoLin3;
   |SI #dscaw042#1 = 0;             Diff = "-->" ; |FINSI;
   |SI #dscaw042#6 = 0;             Diff = "-->" ; |FINSI;
   |PRINT;
   #dscaw042#20 = "X"; |GRABA 020#dscaw042.a; |LIBERA #dscaw042;
   nHay = 1;
|FPRC;

|DEFBUCLE ListadoLin3;
#dscaw042#1;
20;
"     ", 000000, 000, "     ", 000000, 000, 000000001,"C", " ";
"zzzzz", 999999, 999, "zzzzz", 999999, 999, 999999999,"V", " ";
be;
NULL,ListadoLin3;
|FIN;

|PROCESO Listado;
   nHay = 1;
   Switch = 4; |PRINT;
   Switch = 1; |PRINT; Switch = 2;
   |BUCLE ListadoLin1; |BUCLE ListadoLin2;
   Switch = 5; |PRINT;
   Switch = 0; |PRINT;
|FPRC;

|DEFBUCLE Listado;
   #dscaw041#1;
   ;
   INICIO;
   FINAL;
   be;
   NULL,Listado;
|FIN;

|PROCESO Inicio;
     aMail = #0#8; |QBF aMail;

     aDImp = " "; aHImp = "z";
     |SI #0#7 ! "A";
          aDImp = #0#7; aHImp = #0#7;
     |FINSI;

     aAlfa = #48#17; |QBF aAlfa;
     |SI #dscaz056#0 = "V"; |O #dscaz056#0 = "A";
        aAlfa1 = aAlfa + "def/agifa134.mas"; |CARGA_DEF aAlfa1, agif134@;
        |SI FSalida ! 0;
           |MENAV "0000Problemas al cargar la referencia de facturas de venta";
           |HAZ Tipo9; |ERROR; |ACABA;
        |FINSI;
        aAlfa1 = aAlfa + "def/agifa133.mas"; |CARGA_DEF aAlfa1, agif133@;
        |SI FSalida ! 0;
           |MENAV "0000Problemas al cargar la referencia de vtos. de facturas de venta";
           |DESCARGA_DEF #agif134@;
           |HAZ Tipo9; |ERROR; |ACABA;
        |FINSI;
        aAlfa1 = aAlfa + "def/agifa084.mas"; |CARGA_DEF aAlfa1, agif084@;
        |SI FSalida ! 0;
           |MENAV "0000Problemas al cargar la referencia de vtos. de facturas directas";
           |DESCARGA_DEF #agif134@; |DESCARGA_DEF #agif133@;
           |HAZ Tipo9; |ERROR; |ACABA;
        |FINSI;
     |FINSI;
     |SI #dscaz056#0 = "C"; |O #dscaz056#0 = "A";
        aAlfa1 = aAlfa + "def/agifa079.mas"; |CARGA_DEF aAlfa1, agif079@;
        |SI FSalida ! 0;
           |MENAV "0000Problemas al cargar la referencia de facturas de compra";
           |SI #dscaz056#0 = "A"; |DESCARGA_DEF #agif134@; |DESCARGA_DEF #agif133@; |FINSI;
           |HAZ Tipo9; |ERROR; |ACABA;
        |FINSI;
     |FINSI;
     aAlfa = #48#17; |QBF aAlfa;
     aAlfa = aAlfa + "fich/" + (("00000" + #48#15) % -105) + "/";
     |SI #dscaz056#0 = "V"; |O #dscaz056#0 = "A";
        |PATH_DAT #100 aAlfa; |PATH_DAT #101 aAlfa;
        |PATH_DAT #103 aAlfa;
     |FINSI;
     |SI #dscaz056#0 = "C"; |O #dscaz056#0 = "A";
        |PATH_DAT #102 aAlfa;
     |FINSI;

     aAlfa = "fc" + Usuario; |NOME_DAT #1 aAlfa;
     aAlfa = "vt" + Usuario; |NOME_DAT #2 aAlfa;
     |DELINDEX #dscaw041; |DELINDEX #dscaw042;
     |ABRE #dscaw041; |ABRE #dscaw042;

     |SI aMail = "";
          |IMPRE 0;
     |SINO;
          |DFICE aAlfa;
          aFic = aAlfa + Usuario + ".txt";
          Impresora = aFic;
          |IMPRE -77;
     |FINSI;
     |SI FSalida < 0; |ACABA; |FINSI;

     |INFOR "dscaz056";
     |SI FSalida < 0; |ACABA; |FINSI;

     |SI #dscaz056#0 = "V"; |O #dscaz056#0 = "A";
        |BUCLE FactVenta;
        |BUCLE FactDire;
        |DESCARGA_DEF #agif084@; |DESCARGA_DEF #agif133@; |DESCARGA_DEF #agif134@;
        |SI #0#10 = "T";
             |BUCLE RecCarteraV;
        |FINSI;
     |FINSI;
     |SI #dscaz056#0 = "C"; |O #dscaz056#0 = "A";
        |BUCLE FactCompra; |DESCARGA_DEF #agif079@;
        |SI #0#10 = "T";
             |BUCLE RecCarteraC;
        |FINSI;
     |FINSI;

     |SI #0#10 = "E";
          |ABRE #3; |SELECT #12#3;
          |BUCLE dscaw042;
          |CIERRA #3;
          |ABRE #2;
          |BUCLE dscaw041;
          |CIERRA #2;
     |FINSI;

     |BUCLE Listado;
     Switch = 0; |PRINT; |PRINT; |PRINT; |PRINT;
     |BUCLE ListadoLin3;

     |CIERRA #dscaw041; |CIERRA #dscaw042;
     |PIEINF; |FININF; |FINIMP;

     |SI aMail ! ""; |Y nHay ! 0;
          |HAZ EnviaMail;
     |FINSI;
|FPRC;

|PROCESO TodasErroneas; |TIPO 0;
     |SI #0#10 = "E";
          |C_M #0#7, 1, "N";
          #0#7 = "S";
     |SINO;
          |C_M #0#7, 1, "S";
     |FINSI;
     |PINTA #0#7;
|FPRC;

|PROGRAMA;
   aParam = PARAMETRO;
   |HAZ Tipo8;
   |SI HayGestion = 0;
      |SI aParam = "menu";
           |MENAV "0000Este listado solo se emite con una empresa de gestion asociada a cartera";
      |FINSI;
      |HAZ Tipo9;
      |ERROR; |ACABA;
   |FINSI;

   |ABRE #0;
   |LEE 101#0=;
   |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
   |SI aParam = "menu";
          |CLS;
          |CABEZA "Listado comprobacion facturas/vencimientos";
          |PINPA #0#0; |PINDA #0#0;
          |ENDATOS #1#0;
          |SI FSalida = 0;
              |GRABA 020#0a;
              |HAZ Inicio;
          |FINSI;
   |SINO;
          aMail = #0#8; |QBF aMail;
          |SI aMail ! "";
               |HAZ Inicio;
          |FINSI;
   |FINSI;
   |HAZ Tipo9;
|FPRO;
*/
