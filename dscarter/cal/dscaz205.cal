|FICHEROS;
   dscaz205 #0;

   dscam023 #10,MANTE;
   dscam044 #11;
   dscam045 #12;
   dscam013 #13;
   dscam003 #14;
   dscam052 #15;
   dscam215 #16,MANTE;
   dscam105 #17;
   dscam038 #18;
   dscam024 #19;
   dscam004 #20;
   dscam164 #164;

   agifa024@ #100;
   agifa010@ #101;
   agifa091@ #102;

   Referen@ #1000;
   artocoli@ #1001;
   artococa@ #1002;
   fab_hrca@ #1003;
|FIN;

|VARIABLES;
   &Divisa = "";
   &enSwLin     = 0;
   &eaSerieAlb  = "";
   &efFechaAlb  = @;
   &enNumeroAlb = 0;
   &enImportAlb = 0;
   &eaDescFP    = "";

   &enRDAlbaranes = 0;
   &enRDFacturas  = 0;
   &enRDEntregas  = 0;
   &enRDBancos    = 0;
   &enRDImpagados = 0;
   &enRiesgoAlmac = 0;
   &enRieImpTotal = 0;

   &eaSit1 = "";
   &eaSit2 = "";
   &eaSit3 = "";
   &eaSit4 = "";
   &eaSit5 = "";

   nCont  = 0;
   nCont1 = 0;
   nCalc  = 0;
   nCalc1 = 0;

   nSwPrint = 0;

   aAlfa  = "";
   aAlfa1 = "";
   aCliente = "";
   impNoFab = 0;
   impFab   = 0;
   impAlm   = 0;
   cajNoFab = 0;
   cajFab   = 0;
   cajAlm   = 0;

   nCliente = 0;

   _sw      = 0;
   naux     = 0;

   aEstado     = "";
   aDescEstado = "";
   nNumImp     = 0;
|FIN;

|PROCESO MiraSelec;
   |SI #dscaz205#0 = "N";
      |C_M #dscaz205#1, 1, "S"; |C_M #dscaz205#2, 1, "S";
      |PARA nCont = 3; |SI nCont [ 26; |HACIENDO nCont = nCont + 1;
         #dscaz205#nCont# = ""; |PINTA #dscaz205#nCont#; |C_M #dscaz205#nCont#, 1, "N";
      |SIGUE;
   |SINO;
      #dscaz205#1 = "";      |PINTA #dscaz205#1; |C_M #dscaz205#1, 1, "N";
      #dscaz205#2 = "zzzzz"; |PINTA #dscaz205#2; |C_M #dscaz205#2, 1, "N";
      |PARA nCont = 3; |SI nCont [ 26; |HACIENDO nCont = nCont + 1;
         |C_M #dscaz205#nCont#, 1, "S";
      |SIGUE;
   |FINSI;
|FPRC;

|PROCESO Tipo66Rel; |TIPO 66;
   #agifa024@#0 = #dscaz205#Campo#;
   |LEE 000#agifa024@.];

   |CONSULTA_CLAVE #1#agifa024@;
   |SI FSalida = 0;
      #dscaz205#Campo# = #agifa024@#0;
      |SI Campo ] 3; |PINTA 1521, #agifa024@#1; |FINSI;
   |FINSI;
   |ERROR;
|FPRC;

|PROCESO Tipo00Rel;
   |C_M #dscaz205#Campo#, 0, aAlfa; |SI aAlfa = "N"; |ACABA; |FINSI;
   |SI #dscaz205#0 = "N"; |ACABA; |FINSI;

   |SI #dscaz205#Campo# = "000000";
      |SI Campo ] 3;
         #dscaz205#Campo# = "      "; |PINTA #dscaz205#Campo#;
         nCalc1 = Campo + 1;
         |PARA nCont = nCalc1; |SI nCont [ 26; |HACIENDO nCont = nCont + 1;
            |C_M #dscaz205#nCont#, 1, "N";
         |SIGUE;
      |FINSI;
   |SINO;
      |SI #dscaz205#Campo# ! "      ";
         #agifa024@#0 = #dscaz205#Campo#;
         |LEE 000#agifa024@.=;
         |SI FSalida = 0;
            |SI Campo ] 3; |PINTA 1521, #agifa024@#1; |FINSI;
         |SINO;
            |MENAV "    Cliente inexistente";
            |ERROR;
         |FINSI;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO ImprimeAlb;
   |SI #agifa010@#38 = "S"; |ACABA; |FINSI;


   nSwPrint = 0;
   #dscam038#0 = #dscam044#0;
   #dscam038#1 = 1;
   |LEE 000#dscam038.];
   |PARA; |SI FSalida = 0; |Y #dscam038#0 = #dscam044#0; |HACIENDO;
      |SI #dscam038#2 = "A";
         |SI #agifa010@#52 ] #dscam038#3; |Y #agifa010@#52 [ #dscam038#4;
            nSwPrint = 1; |SAL;
         |FINSI;
      |FINSI;
      |LEE 000#dscam038.s;
   |SIGUE;

   |SI nSwPrint = 0; |ACABA; |FINSI;

   Divisa = #agifa010@#68; |HAZ CargaNDeci;

   eaSerieAlb  = #agifa010@#52;
   efFechaAlb  = #agifa010@#1;
   enNumeroAlb = #agifa010@#0;
   enImportAlb = #agifa010@#78;
   enSwLin = 1; |PRINT;
|FPRC;

|DEFBUCLE ImprimeAlb;
#agifa010@#3004;
53, 39;
#agifa024@#0, "01.01.0000", "     ", 000000, "     ", 000000;
#agifa024@#0, "31.12.9999", "zzzzz", 999999, "     ", 000000;
;
NULL, ImprimeAlb;
|FIN;

|PROCESO BucleAlbaranes;
   |SELECT #2#dscam045;
   #dscam045#6 = #48#0;
   |LEE 000#dscam045.];
   |PARA; |SI FSalida = 0; |Y #dscam045#6 = #48#0; |HACIENDO;
      |SI #dscam045#2 = "V";
         #dscam044#0 = #dscam045#0;
         |LEE 000#dscam044.=;
         |SI FSalida = 0;
            aAlfa = #dscam044#1; |QBF aAlfa;
            aAlfa1 = aAlfa + "def/agifa010.mas";
            |CARGA_DEF aAlfa1, agifa010@;
            |SI FSalida = 0;
               aAlfa1 = aAlfa + "fich/" + (("00000" + #dscam044#0) % -105) + "/";
               |PATH_DAT #agifa010@#aAlfa1#; |ABRE #agifa010@;
               |BUCLE ImprimeAlb;
               |CIERRA #agifa010@; |DESCARGA_DEF #agifa010@;
            |FINSI;
         |FINSI;
      |FINSI;
      |LEE 000#dscam045.s;
   |SIGUE;
|FPRC;

|PROCESO BucleRecibos;
   |SI #dscam003#14 ! "P"; |Y #dscam003#14 ! "I"; |Y #dscam003#14 ! "T";
    |Y #dscam003#14 ! "R"; |Y #dscam003#14 ! "C"; |Y #dscam003#14 ! "F";
    |Y #dscam003#14 ! "L"; |Y #dscam003#14 ! "p"; |Y #dscam003#14 ! "e";
    |Y #dscam003#14 ! "E"; |Y #dscam003#14 ! "M";
      |ACABA;
   |FINSI;

   |SI #dscam003#14 = "L";
      |SI #dscam003#17 = 0;
         |ACABA;
      |SINO;
         #dscam013#0 = #dscam003#17;
         #dscam013#1 = #dscam003#37;
         |LEE 000#dscam013.=;
         |SI FSalida = 0; |Y #dscam013#15 = "S"; |ACABA; |FINSI;
      |FINSI;
   |FINSI;

   eaSit1 = ""; eaSit2 = ""; eaSit3 = ""; eaSit4 = ""; eaSit5 = "";
   #dscam164#0 = #dscam003#40;
   #dscam164#1 = "01.01.0000";
   #dscam164#2 = "";
   |LEE 000#dscam164.];
   |PARA; |SI FSalida = 0; |Y #dscam164#0 = #dscam003#40; |HACIENDO;
      |SI eaSit1 = "";
         eaSit1 = #dscam164#5;
      |SINO;
         |SI eaSit2 = "";
            eaSit2 = #dscam164#5;
         |SINO;
            |SI eaSit3 = "";
               eaSit3 = #dscam164#5;
            |SINO;
               |SI eaSit4 = "";
                  eaSit4 = #dscam164#5;
               |SINO;
                  |SI eaSit5 = "";
                     eaSit5 = #dscam164#5;
                  |SINO;
                     |SAL;
                  |FINSI;
               |FINSI;
            |FINSI;
         |FINSI;
      |FINSI;
      |LEE 000#dscam164.s;
   |SIGUE;

   |HAZ ObtenEstado;

   enSwLin = 2; |PRINT;
|FPRC;

|DEFBUCLE BucleRecibos;
#dscam003#11;
;
aCliente, "01.01.0000", "     ";
aCliente, "31.12.9999", "zzzzz";
;
NULL, BucleRecibos;
|FIN;

|PROCESO RiesgoDesg;
   enRDAlbaranes = enRDAlbaranes + #dscam024#3;
   enRDFacturas  = enRDFacturas  + #dscam024#4;
   enRDEntregas  = enRDEntregas  + #dscam024#5;
   enRDBancos    = enRDBancos    + #dscam024#6;
   enRDImpagados = enRDImpagados + #dscam024#7;
|FPRC;

|DEFBUCLE RiesgoDesg;
#dscam024#1;
;
#dscam023#0, 01;
#dscam023#0, 99;
;
NULL, RiesgoDesg;
|FIN;

|PROCESO _VerEstadoHR;
     |SELECT #2#fab_hrca@;
     |DDEFECTO #fab_hrca@;
     #fab_hrca@#10 = #artocoli@#08;       || emp
     #fab_hrca@#11 = #artocoli@#00;       || ped
     #fab_hrca@#12 = #artocoli@#01;       || lin
     |LEE 040#fab_hrca@.=;

     |SI FSalida = 0; |Y #fab_hrca@#07 ! "N";
          _sw = 1;
     |FINSI;
     |SELECT #1#fab_hrca@;
|FPRC;

|PROCESO artocoli;
     |SI #artocoli@#26 = "S"; |ACABA; |FINSI;      || fact = SI.
     |SI #artocoli@#35 = "X"; |ACABA; |FINSI;      || cancelado = SI

     |SI #artocoli@#21 = "N";       || si aun no se ha metido todo en el alm.
          naux = #artocoli@#04 - #artocoli@#20;
          |SI naux < 0; naux = 0; |FINSI;

          _sw = 0;
          |HAZ _VerEstadoHR;

          |SI _sw = 0;
               cajNoFab = cajNoFab + naux;
               impNoFab = impNoFab + (naux * #artocoli@#05/1000);
          |SINO;
               cajFab = cajFab + naux;
               impFab = impFab + (naux * #artocoli@#05/1000);
          |FINSI;
     |FINSI;

     || entrada - servida
     naux = #artocoli@#20 - #artocoli@#07;
     |SI naux < 0; naux = 0; |FINSI;
     cajAlm = cajAlm + naux;
     impAlm = impAlm + (naux * #artocoli@#05/1000);

     enRieImpTotal = impNoFab + impFab + impAlm;
|FPRC;

|DEFBUCLE artocoli;
#artocoli@#1002;
;
#artococa@#0, 001;
#artococa@#0, 999;
;
NULL, artocoli;
|FIN;

|PROCESO artococa;
   |BUCLE artocoli;
|FPRC;

|DEFBUCLE artococa;
#artococa@#2002;
;
000, nCliente;
999, nCliente;
;
NULL, artococa, artocoli;
|FIN;

|PROCESO RiesgoCli;
   #agifa091@#0 = #agifa024@#20;
   |LEE 000#agifa091@.=;
   |SI FSalida ! 0; |DDEFECTO #agifa091@; |FINSI;
   eaDescFP = #agifa091@#1;

   #Referen@#0 = #agifa024@#0;
   |LEE 000#Referen@.=;
   |SI FSalida ! 0; |DDEFECTO #Referen@; |FINSI;

   enRiesgoAlmac = #Referen@#3;
   impNoFab = 0; impFab   = 0; impAlm   = 0;
   nCliente = #agifa024@#0;
   enRieImpTotal = 0; |BUCLE artococa;

   #dscam052#0 = #agifa024@#0;
   |LEE 000#dscam052.=;
   |SI FSalida ! 0;
      |DDEFECTO #dscam052;
      aAlfa = "|NC"; aAlfa = #dscam052#aAlfa#; nCalc = aAlfa;
      nCalc = nCalc - 1;
      |PARA nCont1 = 0; |SI nCont1 [ nCalc; |HACIENDO nCont1 = nCont1 + 1;
         #dscam052#nCont1# = #agifa024@#nCont1#;
      |SIGUE;
   |FINSI;

   #dscam023#0 = #agifa024@#0;
   |LEE 000#dscam023.=;
   |SI FSalida ! 0; |DDEFECTO #dscam023; |FINSI;

   enRDAlbaranes = 0; enRDFacturas  = 0; enRDEntregas  = 0;
   enRDBancos    = 0; enRDImpagados = 0;
   |BUCLE RiesgoDesg;

   #dscam215#4 = #agifa024@#0;
   |LEE 000#dscam215.=;
   |SI FSalida ! 0; |DDEFECTO #dscam215; |FINSI;

   nCalc = 0;
   #dscam105#0 = #dscam023#2;
   #dscam105#1 = #dscam023#5;
   #dscam105#2 = 0;
   |LEE 000#dscam105.];
   |PARA; |SI FSalida = 0; |Y #dscam105#0 = #dscam023#2; |Y #dscam105#1 = #dscam023#5; |HACIENDO;
      nCalc = #dscam105#2;

      |LEE 000#dscam105.s;
   |SIGUE;

   #dscam105#0 = #dscam023#2;
   #dscam105#1 = #dscam023#5;
   #dscam105#2 = nCalc;
   |LEE 000#dscam105.=;
   |SI FSalida ! 0; |DDEFECTO #dscam105; |FINSI;

   |SI #dscam023#20 ! 0;
      enSwLin = 0; |PRINT; aCliente = #agifa024@#0;

      |HAZ BucleAlbaranes;
      |BUCLE BucleRecibos;
   |FINSI;
|FPRC;

|DEFBUCLE RiesgoCli;
#agifa024@#1001;
;
#dscaz205#1;
#dscaz205#2;
;
NULL, RiesgoCli;
|FIN;

|PROCESO ObtenEstado;
   |HAZ EsArtenvas;
   |SI FSalida = 0;
      |SI #dscam003#83 ! 0; |Y #dscam003#14 = "P";
         aEstado     = "P";
         aDescEstado = "Pendiente de cobro";
         nNumImp = 0;

         |ABRE #dscam004;
         #dscam004#17 = #dscam003#83;
         #dscam004#3  = 1;
         |LEE 000#dscam004.];
         |PARA; |SI FSalida = 0; |Y #dscam004#17 = #dscam003#83; |HACIENDO;
            |SI #dscam004#5 = "IMP"; |O #dscam004#5 = "IMA";
               aEstado = "I";
               aDescEstado = "Impagado";
               nNumImp = nNumImp + 1;
            |SINO;
               aEstado = #dscam003#14;
               aDescEstado = #dscam003#15;
            |FINSI;
            |LEE 000#dscam004.s;
         |SIGUE;
         |SI #dscam003#14 ! aEstado;
            #dscam003#14 = aEstado;
            #dscam003#15 = aDescEstado;
         |FINSI;
         |SI #dscam003#16 ! nNumImp;
            #dscam003#16 = nNumImp;
         |FINSI;
         |CIERRA #dscam004;
      |FINSI;
   |FINSI;
|FPRC;

|PROGRAMA;
   |HAZ Tipo8;

   |CLS;
   |CABEZA "Informe financiero : riesgo de cliente";
   |PINPA #0#0; |PINDA #0#0;

   aAlfa = #48#17; |QBF aAlfa;
   aAlfa = aAlfa + "def/agifa024.mas";
   |CARGA_DEF aAlfa, agifa024@;
   |SI FSalida ! 0; |ACABA; |FINSI;

   aAlfa = #48#17; |QBF aAlfa;
   aAlfa = aAlfa + "def/agifa091.mas";
   |CARGA_DEF aAlfa, agifa091@;
   |SI FSalida ! 0; |DESCARGA_DEF #agifa024@; |ACABA; |FINSI;

   |DBASS aAlfa; |QBF aAlfa;
   aAlfa = aAlfa + "edi/def/artricaj.mas";
   |CARGA_DEF aAlfa, Referen@;
   |SI FSalida ! 0;
      |DESCARGA_DEF #agifa091@; |DESCARGA_DEF #agifa024@;
      |ACABA;
   |FINSI;

   |DBASS aAlfa; |QBF aAlfa;
   aAlfa = aAlfa + "edi/def/artocoli.mas";
   |CARGA_DEF aAlfa, artocoli@;
   |SI FSalida ! 0;
      |DESCARGA_DEF #Referen@; |DESCARGA_DEF #agifa091@; |DESCARGA_DEF #agifa024@;
      |ACABA;
   |FINSI;

   |DBASS aAlfa; |QBF aAlfa;
   aAlfa = aAlfa + "edi/def/artococa.mas";
   |CARGA_DEF aAlfa, artococa@;
   |SI FSalida ! 0;
      |DESCARGA_DEF #artocoli@; |DESCARGA_DEF #Referen@;
      |DESCARGA_DEF #agifa091@; |DESCARGA_DEF #agifa024@;
      |ACABA;
   |FINSI;

   |DBASS aAlfa; |QBF aAlfa;
   aAlfa = aAlfa + "fabrica/def/fab_hrca.mas";
   |CARGA_DEF aAlfa, fab_hrca@;
   |SI FSalida ! 0;
      |DESCARGA_DEF #artococa@;
      |DESCARGA_DEF #artocoli@; |DESCARGA_DEF #Referen@;
      |DESCARGA_DEF #agifa091@; |DESCARGA_DEF #agifa024@;
      |ACABA;
   |FINSI;

   aAlfa = #48#17; |QBF aAlfa;
   aAlfa = aAlfa + "fich/" + (("00000" + #48#15) % -105) + "/";
   |PATH_DAT #agifa024@#aAlfa#; |ABRE #agifa024@;
   |PATH_DAT #agifa091@#aAlfa#; |ABRE #agifa091@;

   aAlfa1 = "01";
   |SI #48#0 = 1; |O #48#0 = 3; aAlfa1 = "01"; |FINSI;
   |SI #48#0 = 7;               aAlfa1 = "07"; |FINSI;
   |DBASS aAlfa; |QBF aAlfa;
   aAlfa = aAlfa + "edi/fich/" + aAlfa1 + "/";
   |PATH_DAT #Referen@#aAlfa#;  |ABRE #Referen@;
   |PATH_DAT #artocoli@#aAlfa#; |ABRE #artocoli@;
   |PATH_DAT #artococa@#aAlfa#; |ABRE #artococa@;

   |DBASS aAlfa; |QBF aAlfa;
   aAlfa = aAlfa + "fabrica/fich/" + aAlfa1 + "/";
   |PATH_DAT #fab_hrca@#aAlfa#;  |ABRE #fab_hrca@;

   |ABRE #dscam023; |ABRE #dscam024;
   |ABRE #dscam013; |ABRE #dscam052; |ABRE #dscam105; |ABRE #dscam164;
   |ABRE #dscam215; |SELECT #3#dscam215;
   |DFICB aAlfa; |QBF aAlfa;
   |PATH_DAT #dscam044#aAlfa#; |ABRE #dscam044;
   |PATH_DAT #dscam045#aAlfa#; |ABRE #dscam045;
   |PATH_DAT #dscam038#aAlfa#; |ABRE #dscam038;

   |ENDATOS #1#0;
   |SI FSalida = 0; |Y #dscaz205#27 = "S";
      Impresora = "CrystalReport";
      |IMPRE -1;
      |SI FSalida = 0;
         |INFOR "dscaz205";
         |SI FSalida = 0;
            |SI #dscaz205#0 = "N";
               |BUCLE RiesgoCli;
            |SINO;
               |PARA nCont = 3; |SI nCont [ 26; |HACIENDO nCont = nCont + 1;
                  |SI #dscaz205#nCont# ! "      ";
                     #agifa024@#0 = #dscaz205#nCont#;
                     |LEE 000#agifa024@.=;
                     |SI FSalida = 0; |HAZ RiesgoCli; |FINSI;
                  |FINSI;
               |SIGUE;
            |FINSI;
            |PIEINF; |FININF;
         |FINSI;
         |FINIMP;
      |FINSI;
   |FINSI;

   |SELECT #1#dscam215; |CIERRA #dscam215;
   |CIERRA #fab_hrca@; |DESCARGA_DEF #fab_hrca@;
   |CIERRA #artococa@; |DESCARGA_DEF #artococa@;
   |CIERRA #artocoli@; |DESCARGA_DEF #artocoli@;
   |CIERRA #Referen@;  |DESCARGA_DEF #Referen@;
   |CIERRA #agifa091@; |DESCARGA_DEF #agifa091@;
   |CIERRA #agifa024@; |DESCARGA_DEF #agifa024@;
   |CIERRA #dscam038;
   |CIERRA #dscam023; |CIERRA #dscam024;
   |CIERRA #dscam013; |CIERRA #dscam052; |CIERRA #dscam164;
   |CIERRA #dscam044; |CIERRA #dscam045; |CIERRA #dscam105;

   |HAZ Tipo9;
|FPRO;
