|FICHEROS;
   dscaz170 #0;

   dscaw138 #10;
   dscam008 #11;

   catalm01@ #100;
   canempre@ #101;
   cacuenta@ #102;
   camaniva@ #103;
|FIN;

|VARIABLES;
   nError = 0;
   nNumDoc = 0;

   aAlfa   = "";
   aCuenta = "";
   aParte1 = "";
   aParte2 = "";
   aLong   = "";
   &aInforme = "";

   fFecha  = @;

   nAnyo       = 0;
   nLong       = 0;
   nCalc       = 0;
   nParte1     = 0;
   nParte2     = 0;
   nTecla      = 0;
   nSwCont     = 0;
   nSwHayDatos = 0;
   nMaxDigit   = 0;
   nMaxNivel   = 0;
|FIN;

|PROCESO RecogeDatosCont;
   |SI #catalm01@#8 = "S"; |ACABA; |FINSI;

   |DDEFECTO #dscaw138;
   #dscaw138#0  = #catalm01@#18;
   #dscaw138#1  = #catalm01@#0;
   #dscaw138#7  = #catalm01@#1;
   #dscaw138#8  = #catalm01@#14;
   #dscaw138#9  = #catalm01@#5;
   #dscaw138#10 = #catalm01@#6;
   #dscaw138#15 = #catalm01@#16;
   #dscaw138#16 = #catalm01@#17;
   #dscaw138#17 = #catalm01@#9;
   #dscaw138#18 = #catalm01@#9;
   |GRABA 020#dscaw138.n;
   nSwHayDatos = 1;
|FPRC;

|DEFBUCLE RecogeDatosCont;
#catalm01@#1002;
1, 16, 5, 6;
#0#11, 000000, #0#3, #0#5, #0#7, #0#9;
#0#11, 999999, #0#4, #0#6, #0#8, #0#10;
;
NULL, RecogeDatosCont;
|FIN;

|PROCESO RecogeDatosCart;
   |SI #dscam008#27 ! 0;
      aAlfa = #48#16; |QBF aAlfa; aAlfa = aAlfa + "fich/" + (("00000" + #dscam008#44) % -105) + #dscam008#45 + "/";
      |PATH_DAT #camaniva@#aAlfa#; |ABRE #camaniva@;
      |SELECT #3#camaniva@;
      #camaniva@#0 = #dscam008#27;
      #camaniva@#2 = #dscam008#0;
      #camaniva@#3 = #dscam008#1;
      |LEE 000#camaniva@.=;
      |SI FSalida = 0;
         nNumDoc = #camaniva@#9;
      |SINO;
         nNumDoc = -1;
      |FINSI;
      |SELECT #1#camaniva@; |CIERRA #camaniva@;
   |FINSI;

   aAlfa = #48#16; |QBF aAlfa; aAlfa = aAlfa + "def/camaniva.mas";
   |SI #dscam008#73 ! "01.01.0000";
      |SI #dscam008#73 < #dscaz170#9; |O #dscam008#73 > #dscaz170#10; |ACABA; |FINSI;
   |SINO;
      |SI #dscam008#3  < #dscaz170#9; |O #dscam008#3  > #dscaz170#10; |ACABA; |FINSI;
   |FINSI;;
   |SI #dscam008#14 = "A"; |O #dscam008#14 = "D"; |ACABA; |FINSI;
   |SI #dscam008#4  < #dscaz170#7; |O #dscam008#4  > #dscaz170#8; |ACABA; |FINSI;
   |SI #dscam008#12 < #dscaz170#5; |O #dscam008#12 > #dscaz170#6; |ACABA; |FINSI;
   |SI #dscaz170#2 = "N"; |Y #dscam008#39 ! " "; |ACABA; |FINSI;
   |SI #dscaz170#1 = "N"; |Y #dscam008#14 = "C"; |ACABA; |FINSI;
   |SI #dscaz170#0 = "N"; |Y #dscam008#14 = "P"; |ACABA; |FINSI;

   |DDEFECTO #dscaw138;
   #dscaw138#2  = #dscam008#40;
   #dscaw138#3  = #dscam008#27;
   #dscaw138#4  = #dscam008#0;
   #dscaw138#5  = #dscam008#1;
   #dscaw138#6  = #dscam008#2;
   #dscaw138#7  = #dscam008#5;
   #dscaw138#8  = #dscam008#6;
   #dscaw138#9  = #dscam008#4;
   #dscaw138#10 = #dscam008#3;
   #dscaw138#11 = #dscam008#26;
   #dscaw138#12 = #dscam008#14;
   #dscaw138#13 = #dscam008#15;
   #dscaw138#14 = #dscam008#17;
   #dscaw138#15 = #dscam008#12;
   #dscaw138#16 = #dscam008#63;
   #dscaw138#17 = #dscam008#25;
   |SI #dscam008#74 = "S"; |Y #dscam008#14 = "E";
      #dscaw138#18 = #dscam008#25;
   |SINO;
      #dscaw138#18 = #dscam008#31;
   |FINSI;
   #dscaw138#19 = #dscam008#39;
   #dscaw138#20 = #dscam008#47;
   #dscaw138#21 = #dscam008#73;
   #dscaw138#22 = nNumDoc;
   |GRABA 020#dscaw138.n;

   nSwHayDatos = 1;
|FPRC;

|DEFBUCLE RecogeDatosCart;
#dscam008#6;
;
#0#3, "      ", "01.01.0000";
#0#4, "zzzzzz", "31.12.9999";
;
NULL, RecogeDatosCart;
|FIN;

|PROCESO Imprime;
   |PRINT;
|FPRC;

|DEFBUCLE Imprime;
#dscaw138#1;
;
INICIO;
FINAL;
;
NULL, Imprime;
|FIN;

|PROCESO MiraEmpCont;
   nTecla = FSalida;

   nError = 1;
   aAlfa = #48#16; |QBF aAlfa;
   aAlfa = aAlfa + "def/canempre.mas";
   |CARGA_DEF aAlfa, canempre@;
   |SI FSalida ! 0; |ACABA; |FINSI;
   nError = 0;
   aAlfa = #48#16; |QBF aAlfa;
   aAlfa = aAlfa + "fich/"; |PATH_DAT #canempre@#aAlfa#;
   |ABRE #canempre@;
   |SI nTecla = 10;
      |CONSULTA_CLAVE #1#canempre@;
      |SI FSalida = 0;
         #0#11 = #canempre@#2; |PINTA #0#11;
         |ERROR;
      |FINSI;
   |FINSI;
   #canempre@#2 = #0#11;
   #canempre@#3 = 0;
   |LEE 000#canempre@.];
   |SI FSalida ! 0; |O #canempre@#2 ! #0#11;
      |MENAV "    Empresa inexistente";
      |ERROR;
   |SINO;
      fFecha = ~; aAlfa = fFecha; |QBF aAlfa;
      aAlfa = aAlfa % 0704; nAnyo = aAlfa;
      |SI nAnyo ] 2000;
         nAnyo = nAnyo - 2000;
      |SINO;
         nAnyo = nAnyo - 1900;
      |FINSI;
      |PARA; |SI FSalida = 0; |Y #canempre@#2 = #0#11; |HACIENDO;
         |SI nAnyo = #canempre@#26;
            nAnyo = #canempre@#3;
            #dscaz170#12 = #canempre@#1; |PINTA #dscaz170#12;

            nMaxDigit = 0; nMaxNivel = 0;
            |SI #canempre@#14 ] nMaxDigit; nMaxNivel = 1; nMaxDigit = #canempre@#14; |FINSI;
            |SI #canempre@#15 ] nMaxDigit; nMaxNivel = 2; nMaxDigit = #canempre@#15; |FINSI;
            |SI #canempre@#16 ] nMaxDigit; nMaxNivel = 3; nMaxDigit = #canempre@#16; |FINSI;
            |SI #canempre@#17 ] nMaxDigit; nMaxNivel = 4; nMaxDigit = #canempre@#17; |FINSI;
            |SI #canempre@#18 ] nMaxDigit; nMaxNivel = 5; nMaxDigit = #canempre@#18; |FINSI;
            |SI #canempre@#19 ] nMaxDigit; nMaxNivel = 6; nMaxDigit = #canempre@#19; |FINSI;
            |SAL;
         |FINSI;

         |LEE 000#canempre@.s;
      |SIGUE;
   |FINSI;
   |CIERRA #canempre@; |DESCARGA_DEF #canempre@;
|FPRC;

|PROCESO MiraCuenta;
   nTecla = FSalida;

   aCuenta = #dscaz170#Campo#; |QBF aCuenta;

   || Primero filtro los posibles puntos de la cuenta contable
   aCuenta = aCuenta - ".";
   |PARA; |SI FSalida ! 0; |HACIENDO;
      nParte1 = ((FSalida / 100) ! 0) + 99; aParte1 = aCuenta % nParte1;
      nParte2 = FSalida + 98;               aParte2 = aCuenta % nParte2;
      aLong = aParte1 % 0; nLong = aLong; nCalc = nLong;
      aLong = aParte2 % 0; nLong = aLong; nCalc = nCalc + nLong;
      nCalc = nMaxDigit - nCalc;
      aCuenta = aParte1 + ("0" * nCalc) + aParte2;
      aCuenta = aCuenta - ".";
   |SIGUE;

   || Y ahora presento el lineal de seleccion (en caso de que se haya invocado)
   |SI nTecla = 10;
      aAlfa = #48#16; |QBF aAlfa;
      aAlfa = aAlfa + "def/cacuenta.mas";
      |CARGA_DEF aAlfa, cacuenta@;
      aAlfa = #48#16; |QBF aAlfa;
      aAlfa = aAlfa + "fich/" + (("00000" + #dscaz170#11) % -105) + nAnyo + "/"; |PATH_DAT #cacuenta@#aAlfa#;
      |ABRE #cacuenta@;
      #cacuenta@#0 = aCuenta;
      #cacuenta@#1 = nMaxNivel;
      |LEE 000#cacuenta@.];
      |CONSULTA_CLAVE #1#cacuenta@;
      |SI FSalida = 0; aCuenta = #cacuenta@#0; |FINSI;
      |CIERRA #cacuenta@; |DESCARGA_DEF #cacuenta@;
   |FINSI;
   #dscaz170#Campo# = aCuenta; |PINTA #dscaz170#Campo#;
|FPRC;

|PROGRAMA;
   |HAZ Tipo8;

   |CLS;
   |CABEZA "Informe vencimientos";

   #0#11 = #48#14; |HAZ MiraEmpCont;
   |SI nError ! 0;
       |MENAV "0000ERROR al cargar empresa contable...";
       |ACABA;
   |FINSI;

   |PINPA #0#0; |PINDA #0#0;
   |ENDATOS #1#0;
   |SI FSalida = 0;
      aAlfa = "tg" + Usuario; |NOME_DAT #dscaw138#aAlfa#;
      |DELINDEX #dscaw138; |ABRE #dscaw138;

      aAlfa = #48#16; |QBF aAlfa;
      aAlfa = aAlfa + "def/catalm01.mas";
      |CARGA_DEF aAlfa, catalm01@;
      |SI FSalida ! 0; nSwCont = 0; |SINO; nSwCont = 1; |FINSI;

      |SI nSwCont = 1;
         aAlfa = #48#16; |QBF aAlfa; aAlfa = aAlfa + "fich/000000/";
         |PATH_DAT #catalm01@#aAlfa#; |ABRE #catalm01@;

         aAlfa = #48#16; |QBF aAlfa; aAlfa = aAlfa + "def/camaniva.mas";
         |CARGA_DEF aAlfa, camaniva@;
      |FINSI;

      |BUCLE RecogeDatosCart;

      |SI nSwCont = 1;
         |DESCARGA_DEF #camaniva@;

         |BUCLE RecogeDatosCont;
         |CIERRA #catalm01@; |DESCARGA_DEF #catalm01@;
      |FINSI;

      |SI nSwHayDatos = 1;
         Impresora = "Crystal Reports";
         |IMPRE 0;
         |SI FSalida = 0;
            |SI #0#13 = "S";
                 aInforme = "Informe de vencimientos desg."; |HAZ ObtenInforme;
                 |SI aInforme = "Informe de vencimientos desg.";
                    aInforme = "dscad170";
                 |FINSI;
            |SINO;
                 aInforme = "Informe de vencimientos"; |HAZ ObtenInforme;
                 |SI aInforme = "Informe de vencimientos";
                    aInforme = "dscaz170";
                 |FINSI;
            |FINSI;
            |INFOR aInforme;
            |SI FSalida = 0;
               |BUCLE Imprime;

               |PIEINF; |FININF;
            |FINSI;
            |FINIMP;
         |FINSI;
      |SINO;
         |MENAV "    Seleccion vacia";
      |FINSI;

      |CIERRA #dscaw138; |DELINDEX #dscaw138;
   |FINSI;

   |HAZ Tipo9;
|FPRO;
