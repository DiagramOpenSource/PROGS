|FICHEROS;
   dscaz138 #0;

   dscam008 #10;
   dscam009 #11;

   dscam022 #30;
   dscam047 #31;
   dscam050 #32;

   dscaw072 #100,MANTE;
|FIN;

|VARIABLES;
   &eaDocum  = "";
   &eaComent = "";
   &Divisa   = ""; &nDeci_Div = 0; &nDeci_Base = 0;
   &HayGestion = 0;
   &HayConta   = 0;

   nPendteD = 0; nPendteB = 0; nCalc = 0;
   aEstado = "";

   aAlfa = "";
|FIN;

|PROCESO Movimientos;
   |SI #dscam009#5 = "PAG"; |O #dscam009#5 = "PAP";
    |O #dscam009#5 = "PDM"; |O #dscam009#5 = "PCF";
    |O #dscam009#5 = "PTA"; |O #dscam009#5 = "PPA";
    |O #dscam009#5 = "CAC"; |O #dscam009#5 = "REC";
    |O #dscam009#5 = "RAC";
      |SI #dscam008#74 = "N";
         nPendteD = nPendteD - #dscam009#8;
         nPendteB = nPendteB - #dscam009#21;
         nCalc = nPendteD ! 2;
         |SI nCalc = 0; aEstado = "L"; |SINO; aEstado = "C"; |FINSI;
      |SINO;
         nPendteD = nPendteD + #dscam009#8;
         nPendteB = nPendteB + #dscam009#21;
         |SI #dscam009#5 = "RAC";
            aEstado = "e";
         |SINO;
            nCalc = nPendteD ! 2;
            |SI nCalc = 0;
               aEstado = "L";
            |SINO;
               aEstado = "H";
            |FINSI;
         |FINSI;
      |FINSI;
   |FINSI;
   |SI #dscam009#5 = "RCC";
      nPendteD = nPendteD + #dscam009#8;
      nPendteB = nPendteB + #dscam009#21;
      aEstado = "P";
   |FINSI;
|FPRC;

|DEFBUCLE Movimientos;
#dscam009#1;
;
#dscam008#40,01;
#dscam008#40,99;
be;
NULL,Movimientos;
|FIN;

|PROCESO Recibos;
   |SI #dscam008#14 = "A"; |O #dscam008#14 = "D";
      |ACABA;
   |FINSI;
   |SI #dscam008#74 = "S";
      aEstado = "p"; nPendteD = 0; nPendteB = 0;
   |SINO;
      aEstado = "P"; nPendteD = #dscam008#22; nPendteB = #dscam008#53;
   |FINSI;

   Divisa = #dscam008#59; |HAZ TomaDecim;

   |BUCLE Movimientos;
   nPendteD = nPendteD ! 2; nPendteB = nPendteB ! 2;
   |SI #dscaz138#2 ! "E";
      |SI #dscam008#74 = "N";
         |SI aEstado ! #dscam008#14; |O nPendteD ! #dscam008#31;
            eaDocum = ("0000000000" + #dscam008#40) % -110;
            |SI aEstado ! #dscam008#14;
               |SI aEstado ! "P"; |O #dscam008#14 ! "E";
                  eaComent = " El estado del recibo no era correcto. (";
                  eaComent = eaComent + #dscam008#14 + "/" + aEstado + ")";
                  |PRINT;
               |FINSI;
            |FINSI;
            |SI nPendteD ! #dscam008#31;
               eaComent = " Los totales no eran correctos. (Pdte.";
               eaComent = eaComent + #dscam008#31 + "/" + nPendteD + ")";
               |PRINT;
            |FINSI;
         |FINSI;
      |SINO;
         nCalc = #dscam008#23 + #dscam008#31;
         |SI nCalc ! #dscam008#25;
            eaComent = " Los totales parciales divisa son incorrectos. (";
            eaComent = eaComent + #dscam008#25 + "/" + nCalc + ")";
            |PRINT;
         |FINSI;

         nCalc = #dscam008#57 + #dscam008#58;
         |SI nCalc ! #dscam008#56;
            eaComent = " Los totales parciales base son incorrectos. (";
            eaComent = eaComent + #dscam008#56 + "/" + nCalc + ")";
            |PRINT;
         |FINSI;

         |SI aEstado ! #dscam008#14; |O nPendteD ! #dscam008#23;
            eaDocum = ("0000000000" + #dscam008#40) % -110;
            |SI aEstado ! #dscam008#14;
               |SI aEstado ! "P"; |O #dscam008#14 ! "E";
                  eaComent = " El estado del recibo no era correcto. (";
                  eaComent = eaComent + #dscam008#14 + "/" + aEstado + ")";
                  |PRINT;
               |FINSI;
            |FINSI;
            |SI nPendteD ! #dscam008#23;
               eaComent = " Los totales no eran correctos. (Pdte.";
               eaComent = eaComent + #dscam008#31 + "/" + nPendteD + ")";
               |PRINT;
            |FINSI;
         |FINSI;
      |FINSI;
   |SINO;
      |DDEFECTO #dscaw072;
      #dscaw072#0  = #dscam008#40;
      #dscaw072#1  = #dscam008#0;
      #dscaw072#2  = #dscam008#1;
      #dscaw072#3  = #dscam008#2;
      #dscaw072#4  = #dscam008#32;
      #dscaw072#5  = #dscam008#6;
      #dscaw072#6  = #dscam008#4;
      #dscaw072#7  = #dscam008#14;
      |SI #dscam008#14 = "L"; #dscaw072#8  = "Liquidado"; |FINSI;
      |SI #dscam008#14 = "P"; #dscaw072#8  = "Pendiente"; |FINSI;
      |SI #dscam008#14 = "I"; #dscaw072#8  = "Impagado "; |FINSI;
      |SI #dscam008#14 = "R"; #dscaw072#8  = "Remesado "; |FINSI;
      #dscaw072#10 = #dscam008#22;
      |GRABA 020#dscaw072.n;
   |FINSI;

   |SI #dscaz138#2 = "R";
      |SI nPendteD < 0; nPendteD = 0; |FINSI;
      |SI nPendteB < 0; nPendteB = 0; |FINSI;
      #dscam008#25 = #dscam008#22 + #dscam008#24 + #dscam008#38;
      #dscam008#56 = #dscam008#53 + #dscam008#54 + #dscam008#55 + #dscam008#62;

      |SI #dscam008#74 = "N";
         #dscam008#31 = nPendteD ! 2; #dscam008#23 = #dscam008#25 - #dscam008#31;
         #dscam008#58 = nPendteB ! 2; #dscam008#57 = #dscam008#56 - #dscam008#58;
      |SINO;
         #dscam008#31 = nPendteD ! 2; #dscam008#31 = #dscam008#25 - #dscam008#23;
         #dscam008#57 = nPendteB ! 2; #dscam008#58 = #dscam008#56 - #dscam008#57;
         |SI #dscam008#23 = 0;
            aEstado = "L";
         |FINSI;
      |FINSI;
      |SI aEstado = "P"; |Y #dscam008#39 ! " ";
         |SI #dscam008#39 = "P";
            |ABRE #dscam022;
            #dscam022#0 = #dscam008#17;
            #dscam022#1 = #dscam008#37;
            |LEE 000#dscam022.=;
            |SI FSalida = 0;
               |SI #dscam022#13 = #dscam008#40; aEstado = "E"; |FINSI;
            |FINSI;
            |CIERRA #dscam022;
         |FINSI;
         |SI #dscam008#39 = "T";
            |ABRE #dscam047;
            #dscam047#0 = #dscam008#17;
            #dscam047#1 = #dscam008#37;
            |LEE 000#dscam047.=;
            |SI FSalida = 0;
               |SI #dscam047#13 = #dscam008#40; aEstado = "E"; |FINSI;
            |FINSI;
            |CIERRA #dscam047;
         |FINSI;
         |SI #dscam008#39 = "C";
            |ABRE #dscam050;
            #dscam050#0 = #dscam008#17;
            #dscam050#1 = #dscam008#37;
            |LEE 000#dscam050.=;
            |SI FSalida = 0;
               |SI #dscam050#13 = #dscam008#40; aEstado = "E"; |FINSI;
            |FINSI;
            |CIERRA #dscam050;
         |FINSI;
      |FINSI;
      #dscam008#14 = aEstado;
      |SI aEstado = "L";
         |SI #dscam008#74 = "N";
            #dscam008#15 = "Recibo liquidado";
         |SINO;
            #dscam008#15 = "Recibo compensado";
         |FINSI;
      |FINSI;
      |SI aEstado = "P"; #dscam008#15 = "Pendiente de pago";       |FINSI;
      |SI aEstado = "C"; #dscam008#15 = "Parcialmente pagado";     |FINSI;
      |SI aEstado = "e"; #dscam008#15 = "Pendiente de compensar";  |FINSI;
      |SI aEstado = "H"; #dscam008#15 = "Compensado parcialmente"; |FINSI;
      |SI aEstado = "E";
         |SI #dscam008#39 = "P"; #dscam008#15 = "Englobado en pagare";   |FINSI;
         |SI #dscam008#39 = "C"; #dscam008#15 = "Englobado en confirme"; |FINSI;
         |SI #dscam008#39 = "T"; #dscam008#15 = "Englobado en talon";    |FINSI;
      |FINSI;
      |GRABA 020#dscam008.a;
   |FINSI;
|FPRC;

|DEFBUCLE Recibos;
#dscam008#3;
;
#dscaz138#3,#dscaz138#0;
#dscaz138#4,#dscaz138#1;
be;
NULL,Recibos;
|FIN;

|RUTINA infw072;
   #dscaw072#0 = 0;
|FRUT;

|RUTINA supw072;
   #dscaw072#0 = 9999999999;
|FRUT;

|RUTINA inf;
   #dscam009#17 = #dscam008#40;
   #dscam009#3 = 1;
|FRUT;

|RUTINA sup;
   #dscam009#17 = #dscam008#40;
   #dscam009#3 = 99;
|FRUT;

|PROCESO MarcaTodos;
   |SI #dscaw072#7 = "P"; |O #dscaw072#7 = "L";
      #dscaw072#9 = "*"; |GRABA 020#dscaw072.a;
   |FINSI;
|FPRC;

|DEFBUCLE MarcaTodos;
#dscaw072#1;
;
INICIO;
FINAL;
;
NULL,MarcaTodos;
|FIN;

|PROCESO DesmarcaTodos;
   #dscaw072#9 = " "; |GRABA 020#dscaw072.a;
|FPRC;

|DEFBUCLE DesmarcaTodos;
#dscaw072#1;
;
INICIO;
FINAL;
;
NULL,DesmarcaTodos;
|FIN;

|PROCESO Tipo11; |TIPO 11;
   |SI FSalida = 10;
      |SI #dscaw072#9 = " ";
         #dscaw072#9 = "*"; |GRABA 020#dscaw072.a;
      |SINO;
         #dscaw072#9 = " "; |GRABA 020#dscaw072.a;
      |FINSI;
   |FINSI;
   |SI FSalida = 11;
      |SALVA_FICHA #dscaw072;
      |BUCLE MarcaTodos;
      |REPON_FICHA #dscaw072;
      |PONCONTROL 23, "si";
   |FINSI;
   |SI FSalida = 12;
      |SALVA_FICHA #dscaw072;
      |BUCLE DesmarcaTodos;
      |REPON_FICHA #dscaw072;
      |PONCONTROL 23, "si";
   |FINSI;
   |SI FSalida = 13;
      |SI #dscaw072#7 = "L";
         #dscaw072#7 = "P"; #dscaw072#8 = "Pendiente ";
      |SINO;
         #dscaw072#7 = "L"; #dscaw072#8 = "Liquidado ";
      |FINSI;
      |SI #dscaw072#9 = " "; #dscaw072#9 = "*"; |FINSI;
      |GRABA 020#dscaw072.a;
      |PONCONTROL 23, "si";
   |FINSI;
   |SI FSalida = 9;
      |PUSHV 05012380;
      |ABRE #dscam008;
      #dscam008#40 = #dscaw072#0;
      |LEE 000#dscam008.=;
      |SI FSalida = 0;
         Divisa = #dscam008#59; |HAZ TomaDecim;
         |PINPA #0#dscam008; |PINDA #0#dscam008;
         |ENTLINEAL #1#dscam009,-2,1,20,1,inf,sup;
         |PINPA #0#dscam008; |PINDA #0#dscam008;
         |PAUSA;
      |FINSI;
      |CIERRA #dscam008;
      |POPV;
   |FINSI;
|FPRC;

|PROCESO BorraMovtos;
   |BORRA 020#dscam009.a;
|FPRC;

|DEFBUCLE BorraMovtos;
#dscam009#1;
;
#dscam008#40,001;
#dscam008#40,999;
;
NULL,BorraMovtos;
|FIN;

|PROCESO Regenera;
   |SI #dscaw072#7 = " "; |ACABA; |FINSI;

   #dscam008#40 = #dscaw072#0;
   |LEE 101#dscam008.=;
   |SI FSalida = 0;
      Divisa = #dscam008#59; |HAZ TomaDecim;
      |BUCLE BorraMovtos;
      #dscam008#24 = 0; #dscam008#38 = 0;
      #dscam008#54 = 0; #dscam008#55 = 0; #dscam008#62 = 0;
      #dscam008#25 = #dscam008#22; #dscam008#56 = #dscam008#53;
      #dscam008#23 = 0;            #dscam008#31 = #dscam008#25;
      #dscam008#57 = 0;            #dscam008#58 = #dscam008#64;
      #dscam008#26 = "01.01.0000";
      #dscam008#14 = "P";
      #dscam008#15 = "Pendiente de pago";
      |SI #dscaw072#7 = "L";
         |DDEFECTO #dscam009;
         #dscam009#0 = #dscam008#0;
         #dscam009#1 = #dscam008#1;
         #dscam009#2 = #dscam008#2;
         #dscam009#3 = 1;
         #dscam009#5 = "PAG";
         aAlfa = #dscam008#3; |QBF aAlfa;
         |SI aAlfa = "01.01.1900";
            aAlfa = #dscam008#4; |QBF aAlfa;
         |FINSI;
         #dscam009#6 = aAlfa;
         #dscam009#7 = 0;            #dscam009#20 = 0;
         #dscam009#8 = #dscam008#30; #dscam009#21 = #dscam008#64;
         #dscam009#13 = #dscam008#27;
         #dscam009#14 = #dscam008#40;
         #dscam009#25 = 1;
         |GRABA 020#dscam009.n;

         #dscam008#25 = #dscam008#22; #dscam008#56 = #dscam008#53;
         #dscam008#23 = #dscam008#25; #dscam008#31 = 0;
         #dscam008#57 = #dscam008#56; #dscam008#58 = 0;
         #dscam008#14 = "L";
         #dscam008#15 = "Recibo liquidado";
         #dscam008#26 = aAlfa;
      |FINSI;
      |GRABA 020#dscam008.a; |LIBERA #dscam008;
   |FINSI;
|FPRC;

|DEFBUCLE Regenera;
#dscaw072#1;
9,7;
0000000001,"*","!";
9999999999,"*","z";
;
NULL,Regenera;
|FIN;

|PROCESO PonDesc;
   |SI #dscaw072#7 = " "; #dscaw072#8 = "          "; |FINSI;
   |SI #dscaw072#7 = "L"; #dscaw072#8 = "Liquidado "; |FINSI;
   |SI #dscaw072#7 = "P"; #dscaw072#8 = "Pendiente "; |FINSI;
   |PINTA #dscaw072#8;
|FPRC;

|PROGRAMA;
   |HAZ Tipo8;

   |CLS;
   |CABEZA " Recalculo recibos ";
   |PINPA #0#0; |PINDA #0#0;
   |ENDATOS #1#0;
   |SI FSalida = 0;
      |DELINDEX #dscaw072; |ABRE #dscaw072;
      |SI #dscaz138#2 ! "E";
         |IMPRE 0;
         |SI FSalida ! 0; |ACABA; |FINSI;
         |INFOR "dscaz138";
         |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;
      |FINSI;
      |BUCLE Recibos;
      |SI #dscaz138#2 ! "E";
         |PIEINF; |FININF;
         |FINIMP;
      |FINSI;
      |SI #dscaz138#2 = "E";
         |ENTLINEAL #1#dscaw072,-1, 2, 20, 1, infw072, supw072;
         |CONFI "    NConforme con actualizar los datos editados ?";
         |SI FSalida = 0;
            |ABRE #dscam008; |ABRE #dscam009;
            |BUCLE Regenera;
            |CIERRA #dscam008; |CIERRA #dscam009;
         |FINSI;
      |FINSI;
      |CIERRA #dscaw072; |DELINDEX #dscaw072;
   |FINSI;

   |HAZ Tipo9;
|FPRO;
