|FICHEROS;
   dscam008 #0;
   dscam009 #1,MANTE;

   dscam052 #10;
   dscam025 #11;
   dscam067 #12;
   dscam022 #13;
   dscam047 #14;
   dscam050 #15;
   dscam096 #16;

   dscam106 #506;

   referen@ #1000;
   dscam009@ #1001;

   gestr149@ #2000;
   gestr168@ #2001;
   canempre@ #2002;
   gestr150@ #2003;
|FIN;

|VARIABLES;
   &enSwEsFinal = 0;
   &enSwLinea   = 0;
   &efFechaImp  = @;
   &enSwErr     = 0;

   nAnyo    = 0;
   nPeriodo = 0;
   nCalc    = 0;

   aAlfa    = "";
   aAlfa1   = "";
   aBase    = "";
   aMas     = "";
   aSwDire  = "";
   aInforme = "";
   aEstado  = "";
   aAux     = "";
   aMensaje = "";

   nUltimoSubir   = 0;
   nUltimoBajar   = 0;
   nUltimo        = 0;
   nSwCargaDef3   = 0;
   nSwModoDiv     = 0;
   nSwMiraSiFinal = 0;
   nSwMiraMas     = 0;
   nSwImpTraza    = 0;
   nSwImpCarta    = 0;
   nSwCompleto    = 0;
   nSwVertical    = 0;
   Linea          = 0;
   Documento      = 0;
   nDocOri        = 0;
   nDocOriDiv     = 0;
   nDocDestino    = 0;
   nDocAgrup      = 0;
   nImpoOri       = 0;
   nSwPie         = 0;
   nVoy           = 0;

   nBotonImp      = 0;
   nBotonCarta    = 0;

   nDesde   = 0;
   nDif     = 0;
   nSuma    = 0;
   nTopeDiv = 0;
   nEmpr    = 0;

    aClv1    = "";
    aClv2    = "";
    aClv3    = "";
    aClv4    = "";
    aMovOrig = "";
|FIN;

|PROCESO inf106;
     #dscam106#0 = 001;
     #dscam106#2 = 000000000;
|FPRC;

|PROCESO sup106;
     #dscam106#0 = 999;
     #dscam106#2 =9999999999;
|FPRC;

|PROCESO BotonCarta;
     nSwImpCarta = 1;
     |PTEC 806;
|FPRC;

|PROCESO BotonImp;
     nSwImpTraza = 1;
     |PTEC 806;
|FPRC;

|PROCESO MeteRestoCampos;
     |SI nSwCargaDef3 = 1;
          #referen@#40 = #dscam106#3;
          |LEE 000#referen@.=;
          |SI FSalida = 0;
               #dscam106#10 = #referen@#14;
               #dscam106#11 = #referen@#15;
               #dscam106#12 = #referen@#56;
               #dscam106#13 = 0;
               #dscam106#14 = #referen@#23;
               #dscam106#15 = #referen@#12;
               #dscam106#16 = #referen@#2;
               #dscam106#17 = #referen@#58;
               #dscam106#19 = #referen@#0;
               #dscam106#20 = #referen@#1;
               #dscam106#21 = #referen@#4;
               #dscam106#22 = #referen@#3;
          |FINSI;
          #referen@#40 = #dscam106#2;
          |LEE 000#referen@.=;
          |SI FSalida = 0;
               #dscam106#1 = #referen@#4;
               #dscam106#4 = #referen@#0;
               #dscam106#5 = #referen@#1;
               #dscam106#23 = #referen@#3;
               #dscam106#24 = #referen@#2;
               #dscam106#25 = #referen@#58;
               #dscam106#26 = #referen@#14;
               #dscam106#27 = #referen@#15;
               #dscam106#28 = #referen@#56;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO GrabaRecTraza;
     Linea = Linea + 1;

     |SI aSwDire = "SUBIR";
          nUltimoSubir = nUltimoSubir + 1;
          nUltimo = nUltimoSubir;
     |SINO;
          nUltimoBajar = nUltimoBajar - 1;
          nUltimo = nUltimoBajar;
     |FINSI;

     |DDEFECTO #dscam106;
     #dscam106#0 = nUltimo;
     |LEE 101#dscam106.=;
     |SI FSalida ! 0;
          |SI aSwDire = "SUBIR";
               #dscam106#2 = Documento;
               |SI nSwModoDiv = 1;
                    #dscam106#3 = #dscam008#40;
               |SINO;
                    |SI nSwModoDiv = 0;
                         #dscam106#3 = #dscam008#41;
                    |SINO;
                         #dscam106#3 = Documento;
                    |FINSI;
               |FINSI;
               |SI nSwModoDiv = 1;
                    #dscam106#6 = "Division";
               |SINO;
                    |SI nSwModoDiv = 0;
                         #dscam106#6 = "Agrupacion";
                    |SINO;
                         #dscam106#6 = "Cobro";
                    |FINSI;
               |FINSI;
               |SI nSwModoDiv = 1; |Y nTopeDiv ! 0;
                    nSuma = nSuma + #dscam008#64;
                    |SI nSuma ] nTopeDiv;
                          nDif = nSuma - nTopeDiv;
                          #dscam106#7 = #dscam008#64 - nDif;
                          nSwCompleto = 1;
                    |SINO;
                          #dscam106#7 = #dscam008#64;
                    |FINSI;
               |SINO;
                    |SI nSwModoDiv = 2; |Y nTopeDiv ! 0;
                         ||Total Liquidado
                         nSuma = nSuma + #dscam008#23;
                         |SI nSuma ] nTopeDiv;
                               nDif = nSuma - nTopeDiv;
                               #dscam106#7 = #dscam008#23 - nDif;
                               nSwCompleto = 1;
                         |SINO;
                               #dscam106#7 = #dscam008#23;
                         |FINSI;
                    |SINO;
                         #dscam106#7 = #dscam008#64;
                    |FINSI;
               |FINSI;
               |SI nSwModoDiv = 2;
                    #dscam106#8 = "NO";
               |SINO;
                    #dscam106#8 = "PE";
               |FINSI;
               #dscam106#9 = "NO";

               |HAZ MeteRestoCampos;
          |SINO;
               ||MODO BAJAR. TODO CCC. ARA

               #dscam106#2 = #dscam008#40;
               #dscam106#3 = nDocOri;
               |SI nSwModoDiv = 1;
                    #dscam106#6 = "Division";
               |SINO;
                    |SI nSwModoDiv = 0;
                         #dscam106#6 = "Agrupacion";
                    |SINO;
                         #dscam106#6 = "Cobro";
                    |FINSI;
               |FINSI;
               |SI nSwModoDiv = 0;
                    #dscam106#7 = #dscam008#64;
               |SINO;
                    #dscam106#7 = nImpoOri;
               |FINSI;
               #dscam106#8 = "NO";
               #dscam106#9 = "PE";
               |HAZ MeteRestoCampos;
          |FINSI;
          |GRABA 020#dscam106.n;

          |SI nSwMiraSiFinal = 1;
               enSwEsFinal = 0;
          |FINSI;
     |FINSI;
     |LIBERA #dscam106;

     |SI nSwCompleto = 1;
          nSwCompleto = 0;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE AgrupadosTraza;
#dscam008#1;
41;
000000001,nDocOri;
999999999,nDocOri;
;
NULL,GrabaRecTraza;
|FIN;

|DEFBUCLE DivididosTraza;
#dscam008#1;
70;
000000001,Documento;
999999999,Documento;
;
NULL,GrabaRecTraza;
|FIN;

|PROCESO MasTraza;
     nSwModoDiv = 1;
     |BUCLE DivididosTraza;
     nSwModoDiv = 0;
     #dscam008#40 = Documento;
     |LEE 000#dscam008.=;
     |SI FSalida = 0;
          nDocAgrup = #dscam008#41;
          |SI nDocAgrup ! 0;
               nSwModoDiv = 0;
               |HAZ GrabaRecTraza;
          |FINSI;
          aEstado = #dscam008#14;
          |SI aEstado = "L"; |O aEstado = "C";
               nSwModoDiv = 2;
               |HAZ GrabaRecTraza;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO MasTrazaBajar;
     nSwModoDiv = 1;
     |SI nDocOriDiv ! 0;
          |DDEFECTO #dscam008;
          #dscam008#40 = nDocOriDiv;
          |LEE 000#dscam008.=;
          |SI FSalida = 0;
               |HAZ GrabaRecTraza;
          |FINSI;
     |FINSI;

     nSwModoDiv = 0;
     |||BUCLE AgrupadosTraza;  ||Necesito recorrer este bucle al reves.
     |LEE 000#dscam008.u;      ||Lo hacemos con un |PARA.
     |SI FSalida = 0;
          |PARA; |SI; |HACIENDO;
               |SI #dscam008#41 = nDocOri;
                    |HAZ GrabaRecTraza;
               |FINSI;
               |LEE 000#dscam008.a;
               |SI FSalida ! 0; |SAL; |FINSI;
          |SIGUE;
     |FINSI;
|FPRC;

|PROCESO LineasRecibo;
     enSwLinea = 4;
     |PRINT;
|FPRC;

|DEFBUCLE LineasRecibo;
 #dscam009#1;
 ;
 #dscam008#40, 01;
 #dscam008#40, 99;
 ;
 NULL, LineasRecibo;
|FIN;

|PROCESO MiraMas;
     |SI #dscam106#2 = nDocDestino;
          nSwMiraMas = 1;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE MiraMas;
 #dscam106#1;
 ;
 nDesde;
 999;
 ;
 NULL, MiraMas;
|FIN;

|PROCESO ImpTraza;
     nSwPie = 0;
     Impresora = "CrystalReport"
     |IMPRE -1;
     ||IMPRE 0;
     |SI FSalida = 0;
          aInforme = "dsca106a";
          |INFOR aInforme;
          |SI FSalida = 0;
               nVoy = nUltimoBajar - 1;
               |PARA; |SI; |HACIENDO;
                    nVoy = nVoy + 1;
                    |DDEFECTO #506;
                    #506#0 = nVoy;
                    |LEE 000#506.=;
                    |SI FSalida ! 0;
                         |SI nVoy ! 500;
                              |SAL;
                         |FINSI;
                    |FINSI;
                    |SI nVoy ! 500;
                         enSwLinea = 1;
                         |PRINT;

                         aAux = #dscam106#6;
                         |QBF aAux;
                         |SI aAux = "Cobro";
                              enSwLinea = 3;
                              |PRINT;
                         |SINO;
                              nSwMiraMas = 0;
                              nDocDestino = #506#3;
                              nDesde = nVoy + 1;
                              |BUCLE MiraMas;
                              |SI nSwMiraMas = 0;
                                   #506#0 = nVoy;
                                   |LEE 000#506.=;
                                   enSwLinea = 2;
                                   |PRINT;
                              |FINSI;
                         |FINSI;
                         nSwPie = 1;
                    |FINSI;
               |SIGUE;
               |BUCLE LineasRecibo;
               |SI nSwPie = 1;
                    |PIEINF;
               |FINSI;
               |FININF;
          |SINO;
               aMensaje = "0000Informe " + aInforme + " inaccesible.";
               |MENAV aMensaje;
          |FINSI;
          |FINIMP;
     |SINO;
          |MENAV "0000Proceso cancelado.";
     |FINSI;
|FPRC;

|PROCESO ImpCarta;
     nSwPie = 0;

     aMensaje = "0000NUtilizar formato Vertical" + &13 + "Carta de Impagado?";
     |CONFI aMensaje;
     |SI FSalida = 0;
          nSwVertical = 1;
     |SINO;
          nSwVertical = 0;
     |FINSI;

     Impresora = "CrystalReport"
     |IMPRE -1;
     |SI FSalida = 0;
          |SI nSwVertical = 0;
               aInforme = "Carta impagado horizontal"; |HAZ ObtenInforme;
               |SI aInforme = "Carta impagado horizontal";
                  aInforme = "luxe0106";
               |FINSI;
          |SINO;
               aInforme = "Carta impagado vertical"; |HAZ ObtenInforme;
               |SI aInforme = "Carta impagado vertical";
                  aInforme = "luxe0107";
               |FINSI;
          |FINSI;
          |INFOR aInforme;
          |SI FSalida = 0;
            |DDEF aAlfa; |QBF aAlfa;
            aAlfa = aAlfa + "dscam009.mas";
            |CARGA_DEF aAlfa, dscam009@;
            |SI FSalida = 0;
               |DFICE aAlfa; |QBF aAlfa; |PATH_DAT #dscam009@#aAlfa#;
               |ABRE #dscam009@;
               #dscam009@#17 = #dscam008#40;
               #dscam009@#3  = 99;
               |LEE 000#dscam009@.];
               |SI FSalida = 0;
                  |SI #dscam009@#17 ! #dscam008#40;
                     |LEE 000#dscam009@.a;
                     |SI FSalida = 0;
                        |SI #dscam009@#17 ! #dscam008#40;
                           |DDEFECTO #dscam009@;
                        |FINSI;
                     |SINO;
                        |DDEFECTO #dscam009@;
                     |FINSI;
                  |FINSI;
               |SINO;
                  |LEE 000#dscam009@.u;
                  |SI FSalida = 0;
                     |SI #dscam009@#17 ! #dscam008#40;
                        |DDEFECTO #dscam009@;
                     |FINSI;
                  |SINO;
                     |DDEFECTO #dscam009@;
                  |FINSI;
               |FINSI;
               |PARA; |SI FSalida = 0; |Y #dscam009@#17 ! #dscam008#40; |HACIENDO;
                  |SI #dscam009@#5 = "IMP"; |SAL; |FINSI;
                  |LEE 000#dscam009@.s;
               |SIGUE;
               |SI #dscam009@#5 ! "IMP"; |DDEFECTO #dscam009@; |FINSI;
               efFechaImp = #dscam009@#6;
               |CIERRA #dscam009@; |DESCARGA_DEF #dscam009@;

               |ABRE #dscam052;
               |DDEFECTO #dscam052;
               #dscam052#0 = #dscam008#32;
               |LEE 000#dscam052.=;
               |CIERRA #dscam052;

               nVoy = nUltimoBajar - 1;
               |PARA; |SI; |HACIENDO;
                    nVoy = nVoy + 1;
                    |DDEFECTO #506;
                    #506#0 = nVoy;
                    |LEE 000#506.=;
                    |SI FSalida ! 0;
                         |SI nVoy ! 500;
                              |SAL;
                         |FINSI;
                    |FINSI;
                    |SI nVoy ! 500;
                         |PRINT;
                    |FINSI;
               |SIGUE;

               nVoy = nVoy + 1;
               #dscam106#0 = nVoy;
               #dscam106#1 = #dscam008#3;
               #dscam106#2 = #dscam008#40;
               #dscam106#3 = #dscam008#40;
               #dscam106#4 = #dscam008#0;
               #dscam106#5 = #dscam008#1;
               #dscam106#6 = "Impagado";
               #dscam106#7 = #dscam008#64;
               #dscam106#8 = "NO";
               #dscam106#9 = "NO";
               #dscam106#10 = #dscam008#14;
               #dscam106#11 = #dscam008#15;
               #dscam106#12 = #dscam008#60;
               #dscam106#13 = #dscam008#25;
               #dscam106#14 = #dscam008#23;
               #dscam106#15 = #dscam008#12;
               #dscam106#16 = #dscam008#2;
               #dscam106#17 = #dscam008#65;
               #dscam106#19 = #dscam008#0;
               #dscam106#20 = #dscam008#1;
               #dscam106#21 = #dscam008#4;
               #dscam106#22 = #dscam008#3;

               #dscam106#1 = #dscam008#4;
               #dscam106#4 = #dscam008#0;
               #dscam106#5 = #dscam008#1;
               #dscam106#23 = #dscam008#3;
               #dscam106#24 = #dscam008#2;
               #dscam106#25 = #dscam008#65;
               #dscam106#26 = #dscam008#14;
               #dscam106#27 = #dscam008#15;
               #dscam106#28 = #referen@#64;

               ||ARA55
               |PRINT;

               |PIEINF;
               |FININF;
            |FINSI;
          |SINO;
               aMensaje = "0000Informe " + aInforme + " inaccesible.";
               |MENAV aMensaje;
          |FINSI;
          |FINIMP;
     |SINO;
          |MENAV "0000Proceso cancelado.";
     |FINSI;
|FPRC;

|PROCESO Trazabilidad;
     aAlfa1 = Usuario;
     |QBF aAlfa1;

     aAlfa1 = "55" + aAlfa1;
     |NOME_DAT #506 aAlfa1;
     |ABRE #506;
     |CIERRA #506;

     |DELINDEX #506;

     |ABRET #506;

     nSwCargaDef3 = 0;
     |DBASE aBase; |QBF aBase;
     aMas = aBase + "def/dscam008.mas";
     |CARGA_DEF aMas, referen@;
     |SI FSalida = 0;
          nSwCargaDef3 = 1;
     |FINSI;
     |SI nSwCargaDef3 = 1;
          |ABRE #referen@;
     |FINSI;

     nSuma = 0;

     |LEE 101#dscam008.=;
     |PUSHV 0501 2380;
     Documento = #dscam008#40;
     nDocOri = #dscam008#40;
     nTopeDiv = #dscam008#56;
     nImpoOri = #dscam008#56;
     nDocOriDiv = #dscam008#70;
     Linea = 1;
     |SALVA_FICHA #dscam008;
     |DELINDEX #dscam025;

     aSwDire = "SUBIR";
     nUltimoSubir = 500;

     nSwMiraSiFinal = 1;
     enSwEsFinal = 1;

     nSwModoDiv = 1;
     |BUCLE DivididosTraza;
     nSwModoDiv = 0;
     #dscam008#40 = Documento;
     |LEE 000#dscam008.=;
     |SI FSalida = 0;
          nDocAgrup = #dscam008#41;
          |SI nDocAgrup ! 0;
               nSwModoDiv = 0;
               |HAZ GrabaRecTraza;
          |FINSI;
          aEstado = #dscam008#14;
          |SI aEstado = "L"; |O aEstado = "C";
               nSwMiraSiFinal = 0;
               nSwModoDiv = 2;
               |HAZ GrabaRecTraza;
          |FINSI;
     |FINSI;

     nSwMiraSiFinal = 0;

     ||Aqui pongo el bucle para ir rellenado con el resto de niveles.
     nVoy = 500;
     |PARA; |SI; |HACIENDO;
          |SI Linea > 499; |SAL; |FINSI;
          nVoy = nVoy + 1;
          |DDEFECTO #506;
          #506#0 = nVoy;
          |LEE 101#506.=;
          |SI FSalida ! 0;
               |SAL;
          |SINO;
               |SI #506#8 = "PE";
                    #506#8 = "NO";
                    |GRABA 020#506.a;
                    |LIBERA #506;
                    Documento = #506#3;
                    nTopeDiv = #506#7;
                    nSwCompleto = 0;
                    nSuma = 0;
                    |HAZ MasTraza;
               |SINO;
                    |LIBERA #506;
               |FINSI;
          |FINSI;
     |SIGUE;

     ||ARA HACEMOS EL PROCESO CONTRARIO.
     aSwDire = "BAJAR";
     nTopeDiv = 0;
     nUltimoBajar = 500;
     nSwModoDiv = 1;
     |SI nDocOriDiv ! 0;
          |DDEFECTO #dscam008;
          #dscam008#40 = nDocOriDiv;
          |LEE 000#dscam008.=;
          |SI FSalida = 0;
               |HAZ GrabaRecTraza;
          |FINSI;
     |FINSI;

     nSwModoDiv = 0;
     |||BUCLE AgrupadosTraza;  ||Necesito recorrer este bucle al reves.
     |LEE 000#dscam008.u;      ||Lo hacemos con un |PARA.
     |SI FSalida = 0;
          |PARA; |SI; |HACIENDO;
               |SI #dscam008#41 = nDocOri;
                    |HAZ GrabaRecTraza;
               |FINSI;
               |LEE 000#dscam008.a;
               |SI FSalida ! 0; |SAL; |FINSI;
          |SIGUE;
     |FINSI;

     ||Aqui pongo el bucle para ir rellenado con el resto de niveles. (BAJAR)
     nVoy = 500;
     |PARA; |SI; |HACIENDO;
          |SI Linea > 499; |SAL; |FINSI;
          nVoy = nVoy - 1;
          |DDEFECTO #506;
          #506#0 = nVoy;
          |LEE 101#506.=;
          |SI FSalida ! 0;
               |SAL;
          |SINO;
               |SI #506#9 = "PE";
                    #506#9 = "NO";
                    |GRABA 020#506.a;
                    |LIBERA #506;

                    |DDEFECTO #dscam008;
                    #dscam008#40 = #506#2;
                    |LEE 000#dscam008.=;
                    |SI FSalida = 0;
                         nDocOri = #dscam008#40;
                         nTopeDiv = #dscam008#56;
                         nImpoOri = #dscam008#56;
                         nDocOriDiv = #dscam008#70;
                         |HAZ MasTrazaBajar;
                    |FINSI;
               |SINO;
                    |LIBERA #506;
               |FINSI;
          |FINSI;
     |SIGUE;

     |REPON_FICHA #dscam008;

     |CONTROL_SIMPLE 0, "BOTON, {{205}} Inf. Trazabilidad", 2302, 2316, BotonImp;
     nBotonImp = FSalida; |PULSATECLA;

     ||SI es Impagado y luxe. Permite imprimir CARTA DE IMPAGADO.
     |SI #dscam008#14 = "I";
          |HAZ EsLuxe;
          |SI FSalida = 0;
               |CONTROL_SIMPLE 0, "BOTON, {{205}} Carta de Impagado", 2318, 2334, BotonCarta;
               nBotonCarta = FSalida; |PULSATECLA;
          |FINSI;
     |FINSI;

     |ENTLINEAL #2#dscam106,-2,4,21,1,inf106,sup106;

     |POPV;
     |LIBERA #dscam008;
     |FIN_CONTROL_WINDOWS nBotonImp;

     |SI nBotonCarta ! 0;
          |FIN_CONTROL_WINDOWS nBotonCarta;
          nBotonCarta = 0;
     |FINSI;

     |SI nSwImpTraza = 1;
          nSwImpTraza = 0;
          |HAZ ImpTraza;
     |FINSI;

     |SI nSwImpCarta = 1;
          nSwImpCarta = 0;
          |HAZ ImpCarta;
     |FINSI;

     |SI nSwCargaDef3 = 1;
          |CIERRA #referen@;
          |DESCARGA_DEF #referen@;
          nSwCargaDef3 = 0;
     |FINSI;

     |CIERRAT #506;

     |DELINDEX #506;
     nTopeDiv = 0;
|FPRC;

|PROCESO Liquidaciones;
   #gestr150@#0 = #gestr149@#0;
   #gestr150@#1 = #gestr149@#1;
   #gestr150@#2 = #gestr149@#2;
   #gestr150@#3 = 1;
   |LEE 000#gestr150@.];
   |PARA; |SI FSalida = 0; |Y #gestr150@#0 = #gestr149@#0; |Y #gestr150@#1 = #gestr149@#1;
     |Y #gestr150@#2 = #gestr149@#2; |HACIENDO;
      |SI #gestr150@#4 = "C"; |Y #gestr150@#6 = #dscam008#40; |SAL; |FINSI;
      |LEE 000#gestr150@.s;
   |SIGUE;
   |SI FSalida = 0; |Y #gestr150@#0 = #gestr149@#0; |Y #gestr150@#1 = #gestr149@#1;
     |Y #gestr150@#2 = #gestr149@#2; |Y #gestr150@#4 = "C"; |Y #gestr150@#6 = #dscam008#40;
        |ERROR10;
   |FINSI;
|FPRC;

|DEFBUCLE Liquidaciones;
#gestr149@#2003;
;
nEmpr, " ", 000000000;
nEmpr, "z", 999999999;
;
NULL, Liquidaciones;
|FIN;

|PROCESO ConsAstoViuda;
   enSwErr = 0;
   |DBASS aAlfa; |QBF aAlfa;
   aAlfa = aAlfa + "gesenvia/def/gestr168.mas";
   |CARGA_DEF aAlfa, gestr168@;
   |SI FSalida = 0;
      |DBASS aAlfa; |QBF aAlfa;
      aAlfa = aAlfa + "gesenvia/def/gestr149.mas";
      |CARGA_DEF aAlfa, gestr149@;

      |DBASS aAlfa; |QBF aAlfa;
      aAlfa = aAlfa + "gesenvia/def/gestr150.mas";
      |CARGA_DEF aAlfa, gestr150@;

      aAlfa = #48#16; |QBF aAlfa;
      aAlfa = aAlfa + "def/canempre.mas";
      |CARGA_DEF aAlfa, canempre@;

      aAlfa = #48#16; |QBF aAlfa;
      aAlfa = aAlfa + "fich/"; |PATH_DAT #canempre@#aAlfa#;

      |DBASS aAlfa; |QBF aAlfa;
      aAlfa = aAlfa + "gesenvia/fich/" + (("00000" + #48#15) % -105) + "/";
      |PATH_DAT #gestr149@#aAlfa#; |PATH_DAT #gestr150@#aAlfa#;
      |PATH_DAT #gestr168@#aAlfa#;

      nEmpr = #48#0; |ABRE #gestr150@;
      |BUCLE Liquidaciones;
      |CIERRA #gestr150@;

      |ABRE #canempre@; |ABRE #gestr168@;

      aAlfa = #gestr149@#6; |QBF aAlfa;
      aAlfa = aAlfa % -104; nAnyo = aAlfa;
      nPeriodo = -1;
      #canempre@#2 = #gestr149@#4;
      #canempre@#3 = 0;
      |LEE 000#canempre@.];
      |PARA; |SI FSalida = 0; |Y #canempre@#2 = #gestr149@#4; |HACIENDO;
         nCalc = #canempre@#26;
         |SI nCalc > 80;
            nCalc = nCalc + 1900;
         |SINO;
            nCalc = nCalc + 2000;
         |FINSI;
         |SI nCalc = nAnyo;
            nPeriodo = #canempre@#3; |SAL;
         |FINSI;
         |LEE 000#canempre@.s;
      |SIGUE;

      |SI nPeriodo ! -1;
         #gestr168@#0 = "L";
         #gestr168@#1 = #gestr149@#0;
         #gestr168@#2 = #gestr149@#1;
         #gestr168@#3 = #gestr149@#2;
         #gestr168@#7 = #gestr149@#4;
         #gestr168@#8 = nPeriodo;
         |LEE 000#gestr168@.=;
         |SI FSalida = 0;
            #dscam067#4 = #gestr168@#4;
            #dscam067#5 = #gestr168@#5;
            #dscam067#6 = #gestr168@#6;
            #dscam067#8 = #gestr168@#7;
            #dscam067#9 = #gestr168@#8;
         |SINO;
            enSwErr = -1;
         |FINSI;
      |SINO;
         enSwErr = -1;
      |FINSI;
      |CIERRA #canempre@; |DESCARGA_DEF #canempre@;
      |CIERRA #gestr150@; |DESCARGA_DEF #gestr150@;
      |CIERRA #gestr149@; |DESCARGA_DEF #gestr149@;
      |CIERRA #gestr168@; |DESCARGA_DEF #gestr168@;
   |SINO;
      enSwErr = -1;
   |FINSI;
|FPRC;

|PROCESO ActualizaLineas;
   |DDEFECTO #dscam067;
   aClv1 = "PG";
   aClv2 = #dscam009#14; aClv2 = (aClv2 + (" " * 50))% 0150;
   aClv3 = #dscam009#3;  aClv3 = (aClv3 + (" " * 50))% 0150;
   aClv4 = "";           aClv4 = (aClv4 + (" " * 50))% 0150;
   |SI #dscam009#5 = "PPA";
      |ABRE #dscam022; |SELECT #2#dscam022;
      #dscam022#13 = #dscam009#14;
      #dscam022#14 = #dscam009#3;
      |LEE 000#dscam022.=;
      |SI FSalida ! 0;
         |MENAV "    No puedo encontrar el vinculo a la linea del pagare";
         |CIERRA #dscam022; |CIERRA #dscam067; |ERROR; |ACABA;
      |FINSI;
      |CIERRA #dscam022;
      aClv1 = "PA";
      aClv2 = #dscam022#0; aClv2 = (aClv2 + (" " * 50))% 0150;
      aClv3 = #dscam022#1; aClv3 = (aClv3 + (" " * 50))% 0150;
      aClv4 = "I"; aClv4 = (aClv4 + (" " * 50))% 0150;
      #dscam067#0 = aClv1;
      #dscam067#1 = aClv2;
      #dscam067#2 = aClv3;
      #dscam067#3 = aClv4;
      |LEE 000#dscam067.];
      |SI FSalida ! 0; |O #dscam067#0 ! aClv1;  |O #dscam067#1 ! aClv2;
       |O #dscam067#2 ! aClv3;  |O #dscam067#3 ! aClv4;
         aClv4 = "V"; aClv4 = (aClv4 + (" " * 50))% 0150;
      |FINSI;
   |FINSI;
   |SI #dscam009#5 = "PTA";
      |ABRE #dscam047; |SELECT #2#dscam047;
      #dscam047#13 = #dscam009#14;
      #dscam047#14 = #dscam009#3;
      |LEE 000#dscam047.=;
      |SI FSalida ! 0;
         |MENAV "    No puedo encontrar el vinculo a la linea del talon";
         |CIERRA #dscam047; |CIERRA #dscam067; |ERROR; |ACABA;
      |FINSI;
      |CIERRA #dscam047;
      aClv1 = "TA";
      aClv2 = #dscam047#0; aClv2 = (aClv2 + (" " * 50))% 0150;
      aClv3 = #dscam047#1; aClv3 = (aClv3 + (" " * 50))% 0150;
      aClv4 = ""; aClv4 = (aClv4 + (" " * 50))% 0150;
   |FINSI;
   |SI #dscam009#5 = "PCF";
      |ABRE #dscam050; |SELECT #2#dscam050;
      #dscam050#13 = #dscam009#14;
      #dscam050#14 = #dscam009#3;
      |LEE 000#dscam050.=;
      |SI FSalida ! 0;
         |MENAV "    No puedo encontrar el vinculo a la linea del confirme";
         |CIERRA #dscam050; |CIERRA #dscam067; |ERROR; |ACABA;
      |FINSI;
      |CIERRA #dscam050;
      aClv1 = "CF";
      aClv2 = #dscam050#0; aClv2 = (aClv2 + (" " * 50))% 0150;
      aClv3 = #dscam050#1; aClv3 = (aClv3 + (" " * 50))% 0150;
      aClv4 = "V"; aClv4 = (aClv4 + (" " * 50))% 0150;
   |FINSI;
   |SI #dscam009#5 = "PDM";
      |ABRE #dscam096; |SELECT #2#dscam096;
      #dscam096#16 = #dscam009#14;
      #dscam096#17 = #dscam009#3;
      |LEE 000#dscam096.=;
      |SI FSalida ! 0;
         |MENAV "    No puedo encontrar el vinculo a la linea del pago domiciliado";
         |CIERRA #dscam096; |CIERRA #dscam067; |ERROR; |ACABA;
      |FINSI;
      |CIERRA #dscam096;
      aClv1 = "PD";
      aClv2 = #dscam096#0; aClv2 = (aClv2 + (" " * 50))% 0150;
      aClv3 = #dscam096#1; aClv3 = (aClv3 + (" " * 50))% 0150;
      aClv4 = "V"; aClv4 = (aClv4 + (" " * 50))% 0150;
   |FINSI;
   #dscam067#0 = aClv1;
   #dscam067#1 = aClv2;
   #dscam067#2 = aClv3;
   #dscam067#3 = aClv4;
   |LEE 000#dscam067.];
   |SI FSalida = 0; |Y #dscam067#0 = aClv1;  |Y #dscam067#1 = aClv2;
    |Y #dscam067#2 = aClv3;  |Y #dscam067#3 = aClv4;
      #dscam009#15 = "S"; |GRABA 020#dscam009.a;
   |FINSI;
|FPRC;

|DEFBUCLE ActualizaLineas;
#dscam009#1;
;
#dscam008#40, 01;
#dscam008#40, 99;
be;
NULL, ActualizaLineas;
|FIN;

|PROCESO Tipo5; |TIPO 5;
    ||Actualiza el campo 'Contabilizado' del lineal de movimientos
    |SALVA_FICHA #dscam009;
    |ABRE #dscam067;
    |BUCLE ActualizaLineas;
    |CIERRA #dscam067;
    |REPON_FICHA #dscam009;

    |LINEAS_BI_ATRI #dscam009#15,D,S,-1;
|FPRC;
