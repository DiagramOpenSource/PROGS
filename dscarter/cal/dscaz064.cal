|FICHEROS;
   dscaz064 #0;
   dscam051 #1;

   dscaw047 #10;
   dscam003 #11;
   &agif024@ #12;
|FIN;

|VARIABLES;
   &eaNomCliente = "";
   &eaDirCliente = "";
   &eaCPCliente = "";
   &eaPobCliente = "";
   &eaPrvCliente = "";
   &eaTlfCliente = "";
   &eaFaxCliente = "";

   &enTotalDeuda = 0;

   &aInforme    = "";

   &TipoCliente = "";
   &CodCliente  = "";
   &Nombre      = "";
   &DCampoG     = 0;
   &HCampoG     = 0;
   &DCampoC     = 0;
   &HCampoC     = 0;
   &Fichero     = "";
   &Tipo        = "";
   &Cmp         = "";
   &CmpNum      = 0;

   aCliente     = "";

   FEstado    = 0;

   nContador  = 0;
   nContRegs  = 0;
   nSw        = 0;
   nCont      = 0;
   nConta     = 0;
   nCalc      = 0;
   nWord      = 0;
   Primero    = 0;
   Calc       = 0;
   Long       = 0;
   SwNeg      = 0;
   nDecimales = 0;
   nHandle    = 0;

   aDecimales = "";
   LongAlfa   = "";
   Cadena     = "";
   Comodin    = "";
   Comodin1   = "";
   aIP        = "";
   aAlfa      = "";
   aAlfa1     = "";
   aAlfa2     = "";
   aAlfa3     = "";
   aCliAnt    = "XXXXX";

   aA01 = ""; aA02 = ""; aA03 = "";
   aA04 = ""; aA05 = "";
   aA11 = ""; aA12 = ""; aA13 = "";
   aA14 = ""; aA15 = "";

   aCadena = "";
   aLong   = "";

   nPag    = 0;
   nLong   = 0;
   nCont1  = 0;
   Calc1   = 0;
   nPasada = 0;

   nHandle1 = 0;
   nHandle2 = 0;

{10}Totales = 0;
{1000}Datos   = "";
|FIN;

|PROCESO PintaPan;
   DCampoG = 0; HCampoG = 9;
   |PARA nCont = DCampoG; |SI nCont [ HCampoG; |HACIENDO nCont = nCont + 1;
      TipoCliente = "G"; CodCliente = #0#nCont#;
      |HAZ ExisteCliente;
      |SI TipoCliente ! "ERROR";
         nCalc = ((nCont + 9) * 100) + 30;
         #dscaz064#10     = Nombre;     |PINTA nCalc, #dscaz064#10;
      |FINSI;
   |SIGUE;
|FPRC;

|PROCESO MiraCliente;
   |SI FSalida = 2; |ACABA; |FINSI;
   |SI FSalida ! 10; |Y #dscaz064#Campo# = "      "; |ACABA; |FINSI;

   aAlfa = #dscaz064#Campo#; |QBF aAlfa;
   aAlfa = ("000000" + aAlfa) % -106; #dscaz064#Campo# = aAlfa;

   DCampoG = 0; HCampoG = 9;
   TipoCliente = "G"; CodCliente = #0#Campo#;
   |HAZ ExisteCliente;
   |SI TipoCliente = "ERROR";
      |ERROR;
   |SINO;
      nCalc = ((Campo + 9) * 100) + 30;
      #dscaz064#Campo# = CodCliente; |PINTA #dscaz064#Campo#;
      #dscaz064#10     = Nombre;     |PINTA nCalc, #dscaz064#10;
   |FINSI;
|FPRC;

|PROCESO VaciaTodo;
   |PARA nCont = 0; |SI nCont [ 999; |HACIENDO nCont = nCont + 1;
      IDatos = Datos1<-; IDatos = IDatos + nCont; Datos = "";
   |SIGUE;
   aA11 = ""; aA12 = ""; aA13 = "";
   aA14 = ""; aA15 = ""; nContador = 0; IDatos = Datos1<-;
|FPRC;

|PROCESO Imprime;
   enTotalDeuda = Totales;
   |SI aCliAnt ! #dscaw047#5;
      Fichero = "Clientes"; Tipo = "G";
      Cmp = "Codigo Cliente"; |HAZ ObtenDato;
      #agif024@#CmpNum# = #dscaw047#5;
      |LEE 000#agif024@.=;
      |SI FSalida = 0; |Y #agif024@#CmpNum# ! "      ";
         Fichero = "Clientes"; Tipo = "G"; Cmp = "Nombre Cliente"; |HAZ ObtenDato;
         eaNomCliente = #agif024@#CmpNum#;
         Fichero = "Clientes"; Tipo = "G"; Cmp = "Direccion Cobro"; |HAZ ObtenDato;
         eaDirCliente = #agif024@#CmpNum#;
         Fichero = "Clientes"; Tipo = "G"; Cmp = "Codigo Postal Cobro"; |HAZ ObtenDato;
         eaCPCliente = #agif024@#CmpNum#;
         Fichero = "Clientes"; Tipo = "G"; Cmp = "Poblacion Cobro"; |HAZ ObtenDato;
         eaPobCliente  = #agif024@#CmpNum#;
         Fichero = "Clientes"; Tipo = "G"; Cmp = "Provincia Cobro"; |HAZ ObtenDato;
         eaPrvCliente = #agif024@#CmpNum#;
         Fichero = "Clientes"; Tipo = "G"; Cmp = "Telefono"; |HAZ ObtenDato;
         eaTlfCliente = #agif024@#CmpNum#;
         Fichero = "Clientes"; Tipo = "G"; Cmp = "Fax"; |HAZ ObtenDato;
         eaFaxCliente = #agif024@#CmpNum#;
      |SINO;
         eaNomCliente = ""; eaDirCliente = "";
         eaCPCliente = "";  eaPobCliente = "";
         eaPrvCliente = "";
         eaTlfCliente = ""; eaFaxCliente = "";
      |FINSI;

      aCliAnt = #dscaw047#5;
   |FINSI;

   |PRINT;
|FPRC;

|DEFBUCLE Imprime;
#dscaw047#1;
;
aCliente, 000000001;
aCliente, 999999999;
;
NULL,Imprime;
|FIN;

|PROCESO TomaDatos;
   nSw = 0;
   |PARA nCont = 0; |SI nCont [ 9; |HACIENDO nCont = nCont + 1;
      |SI #dscam003#32 = #dscaz064#nCont#; nSw = 1; |SAL; |FINSI;
   |SIGUE;
   |SI nSw = 0; |ACABA; |FINSI;

   |SI #dscam051#104 = "S";
      |SI #dscam003#70 ! "          "; |O #dscam003#72 ! "    ";
         |SI #dscam003#71 < #0#12; |O #dscam003#71 > #0#13; |ACABA; |FINSI;
      |SINO;
         |SI #dscam003#3 < #0#12; |O #dscam003#3 > #0#13; |ACABA; |FINSI;
      |FINSI;
   |FINSI;

   ITotales = Totales1<-;
   ITotales = ITotales + nCont;

   |SI #dscam003#14 ! "P"; |Y #dscam003#14 ! "I"; |Y #dscam003#14 ! "C"; |ACABA; |FINSI;

   |DDEFECTO #dscaw047;
   #dscaw047#0 = #dscam003#40;
   aAlfa = (("     " + #dscam003#0) % -105) + "/" + (("000000" + #dscam003#1) % -106);
   aAlfa = aAlfa + "/" + (("000" + #dscam003#2) % -103);
   #dscaw047#1 = aAlfa;
   #dscaw047#2 = #dscam003#4;
   #dscaw047#3 = #dscam003#3;
   #dscaw047#4 = #dscam003#31;
   #dscaw047#5 = #dscam003#32;
   |GRABA 020#dscaw047.n;

   Totales = Totales + #dscam003#31;
   nContRegs = nContRegs + 1;
|FPRC;

|DEFBUCLE TomaDatos;
   #dscam003#2;
   ;
   #0#12, 000000001;
   #0#13, 999999999;
   ;
   NULL,TomaDatos;
|FIN;

|DEFBUCLE TomaDatos1;
   #dscam003#1;
   ;
   000000001;
   999999999;
   ;
   NULL,TomaDatos;
|FIN;

|PROCESO PonComas;

   Calc = Comodin;
   |SI Calc < 0;
      Calc = 0 - Calc;
      Comodin = Calc;
      SwNeg = 1;
   |SINO;
      SwNeg = 0;
   |FINSI;

   Comodin1 = Comodin;
   Comodin1 = Comodin1 - ".";
   |SI FSalida ! 0;
      Calc = FSalida + 98;
      Comodin1 = Comodin1 % Calc;
      nDecimales = Comodin1;
      aDecimales = Comodin1; |QBF aDecimales;

      LongAlfa = aDecimales % 0; Long = LongAlfa;
      |SI Long < 2;
         Long = 2 - Long;
         aDecimales = aDecimales + ("0" * Long);
      |FINSI;
||       Comodin = Comodin - Comodin1; Comodin = Comodin - ".";
      Calc = Calc - 199; Calc = 100 + ((Calc / 100) ! 0);
      Comodin = Comodin % Calc;
   |SINO;
      nDecimales = 0;
      aDecimales = "00";
   |FINSI;

   Comodin1 = Comodin;
   Comodin = "";
   |PARA; |SI Comodin1 ! ""; |HACIENDO;
      LongAlfa = Comodin1 % 0;
      Long = LongAlfa;
      |SI Long [ 3;
         Cadena = Comodin1;
         Comodin1 = "";
         |QBF Cadena;
         Comodin = Cadena + Comodin;
      |SINO;
         Cadena = Comodin1 % -103;
         Comodin1 = Comodin1 - Cadena;
         Comodin  = "." + Cadena + Comodin;
      |FINSI;
   |SIGUE;
   |SI SwNeg = 1;
      Comodin = "-" + Comodin;
   |FINSI;
   |SI nDecimales ] 0;
      Comodin = Comodin + ",";
      ||SI nDecimales < 10; Comodin = Comodin + "0"; |FINSI;
      Comodin = Comodin + aDecimales;
      ||Comodin = Comodin + nDecimales;
   |FINSI;
|FPRC;

|PROCESO QuitaRaros1;
   aLong = aCadena % 0; nLong = aLong; aAlfa2 = "";
   |PARA nCont1 = 1; |SI nCont1 [ nLong; |HACIENDO nCont1 = nCont1 + 1;
      Calc1 = (nCont1 * 100) + 1; aAlfa1  = aCadena % Calc1;
      |SI aAlfa1 = " ";                  |VETE Sigue1; |FINSI;
      |SI aAlfa1 ] "A"; |Y aAlfa1 [ "Z"; |VETE Sigue1; |FINSI;
      |SI aAlfa1 ] "a"; |Y aAlfa1 [ "z"; |VETE Sigue1; |FINSI;
      |SI aAlfa1 ] "0"; |Y aAlfa1 [ "9"; |VETE Sigue1; |FINSI;
      |SI aAlfa1 = "."; |O aAlfa1 = ","; |O aAlfa1 = ";"; |O aAlfa1 = ":";
       |O aAlfa1 = "_"; |O aAlfa1 = "'"; |O aAlfa1 = "-";
          |VETE Sigue1;
      |FINSI;

      |SI aAlfa1 = "¥";  aAlfa1 = &"0209"; |VETE Sigue1;|FINSI;
      |SI aAlfa1 = "¤";  aAlfa1 = &0241;   |VETE Sigue1;|FINSI;
      |SI aAlfa1 = "á";  aAlfa1 = &0225;   |VETE Sigue1;|FINSI;
      |SI aAlfa1 = " ";  aAlfa1 = &0225;   |VETE Sigue1;|FINSI;
      |SI aAlfa1 = "‚";  aAlfa1 = &0233;   |VETE Sigue1;|FINSI;
      |SI aAlfa1 = "¡";  aAlfa1 = &0237;   |VETE Sigue1;|FINSI;
      |SI aAlfa1 = "¢";  aAlfa1 = &0243;   |VETE Sigue1;|FINSI;
      |SI aAlfa1 = "£";  aAlfa1 = &0250;   |VETE Sigue1;|FINSI;
      |SI aAlfa1 = "§";  aAlfa1 = &0176;   |VETE Sigue1;|FINSI;
      |SI aAlfa1 = "¦";  aAlfa1 = &0170;   |VETE Sigue1;|FINSI;
      |SI aAlfa1 = "¨";  aAlfa1 = &0191;   |VETE Sigue1;|FINSI;
      |SI aAlfa1 = "€";  aAlfa1 = &0199;   |VETE Sigue1;|FINSI;
      |SI aAlfa1 = "‡";  aAlfa1 = &0231;   |VETE Sigue1;|FINSI;

      aAlfa1 = " ";
      |ET Sigue1;
      aAlfa2 = aAlfa2 + aAlfa1;
   |SIGUE;
   aCadena = aAlfa2;
|FPRC;

|PROGRAMA;
   |HAZ Tipo8;

   |ABRE #dscam051;
   |LEE 000#dscam051.p;
   |SI FSalida ! 0; |DDEFECTO #dscam051; |FINSI;
   |CIERRA #dscam051;

   |CLS;
   |CABEZA " Emision carta de deuda";
   |PINPA #0#0; |PINDA #0#0; |HAZ PintaPan;
   |ENDATOS #1#0;
   |SI FSalida = 0;
      |PARA nCont = 0; |SI nCont [ 9; |HACIENDO nCont = nCont + 1;
         ITotales = Totales1 <-; ITotales = ITotales + nCont; Totales = 0
      |SIGUE;

      nContRegs = 0;
      |DELINDEX #dscaw047; |ABRE #dscaw047;
      |SI #dscam051#104 = "S";
         |BUCLE TomaDatos1;
      |SINO;
         |BUCLE TomaDatos;
      |FINSI;

      |SI #dscaz064#14 = "S";
         aInforme = "Carta reclamacion deuda"; |HAZ ObtenInforme;
         |SI aInforme = "Carta reclamacion deuda"; aInforme = "dscaz064"; |FINSI;
         aAlfa1 = aInforme + ".inf";
         aAlfa2 = "informe.inf";

         |DDEF aAlfa; |QBF aAlfa; aAlfa = aAlfa + aAlfa1;
         |XABRE "B", aAlfa, nHandle1;
         |SI FSalida ] 0;
            |DDEF aAlfa; |QBF aAlfa; aAlfa = aAlfa + aAlfa2;
            |XABRE "BW", aAlfa, nHandle2;
            |SI FSalida ] 0;
               FEstado = 1;
               |PARA; |SI FEstado > 0; |HACIENDO;
                  |XLEE nHandle1, 1, aAlfa; nCalc = &aAlfa; FEstado = FSalida;
                  |SI nCalc = 10;
                     |XGRABA nHandle2, aAlfa1; |SAL;
                  |SINO;
                     |XGRABA nHandle2, aAlfa;
                  |FINSI;
               |SIGUE;
               |XLEE nHandle1, 1, aAlfa; nCalc = &aAlfa;
               |SI nCalc = 13;
                  |XGRABA nHandle2, aAlfa;
                  |XLEE nHandle1, 1, aAlfa; nCalc = &aAlfa;
                  |SI nCalc = 10; |XGRABA nHandle2, aAlfa; |FINSI;
               |SINO;
                  |SI nCalc = 10; |XGRABA nHandle2, aAlfa; |FINSI;
               |FINSI;
               aAlfa = "$F$#dscaz064#15" + &13 + &10; |XGRABA nHandle2, aAlfa;
               FEstado = 1;
               |PARA; |SI FEstado > 0; |HACIENDO;
                  |XLEE nHandle1, 1, aAlfa; FEstado = FSalida;
                  |XGRABA nHandle2, aAlfa;
               |SIGUE;
               |XCIERRA nHandle2;
            |FINSI;
            |XCIERRA nHandle1;
         |FINSI;
         |DDEF aAlfa; |QBF aAlfa;
         aAlfa1 = aAlfa + aInforme + ".rpt";
         aAlfa2 = aAlfa + "informe.rpt";
         |COPIA_FICHERO aAlfa1, aAlfa2;
      |FINSI;

      |PARA nConta = 0; |SI nConta [ 9; |HACIENDO nConta = nConta + 1;
        aCliente = #dscaz064#nConta#;
        ITotales = Totales1<-; ITotales = ITotales + nConta;

        |SI aCliente ! "      "; |Y Totales ! 0;
            aCliAnt = "XXXXXXXX";

            |SI #dscaz064#14 = "N";
               Impresora = "CrystalReport";
               |IMPRE -1;
               |SI FSalida = 0;
                  aInforme = "Carta reclamacion deuda"; |HAZ ObtenInforme;
                  |SI aInforme = "Carta reclamacion deuda"; aInforme = "dscaz064"; |FINSI;

                  |INFOR aInforme;
                  |SI FSalida = 0;
                     |BUCLE Imprime;

                     |PIEINF; |FININF;
                  |FINSI;

                  |FINIMP;
               |FINSI;
            |SINO;
               Fichero = "Clientes"; Tipo = "G";
               Cmp = "Codigo Cliente"; |HAZ ObtenDato;
               #agif024@#CmpNum# = aCliente;
               |LEE 000#agif024@.=;
               |SI FSalida = 0;
                  Fichero = "Clientes"; Tipo = "G";
                  Cmp = "Fax"; |HAZ ObtenDato;
                  #dscaz064#15 = #agif024@#Cmp#;
                  aAlfa = #dscaz064#15; |QBF aAlfa;
                  |SI aAlfa = "";
                     Fichero = "Clientes"; Tipo = "G";
                     Cmp = "Nombre Cliente"; |HAZ ObtenDato;
                     aAlfa = "    El cliente [" + aCliente + "] " + #agif024@#CmpNum#; |QBF aAlfa;
                     aAlfa = aAlfa + " no tiene el n§ de fax indicado. No se enviara la carta.";
                     |MENAV aAlfa;
                  |SINO;
                     aAlfa = "Fax;-" + #dscaz064#15; |QBF aAlfa;
                     aAlfa = aAlfa + "*";
                     Impresora = aAlfa;
                     |IMPRE -1;
                     |SI FSalida = 0;
                        |INFOR "informe";
                        |SI FSalida = 0;
                           |BUCLE Imprime;
                           |PIEINF; |FININF;
                        |FINSI;
                        |FINIMP;
                     |SINO;
                        |MENAV "    Imposible seleccionar impresora FAX";
                     |FINSI;
                  |FINSI;
               |FINSI;
            |FINSI;
         |FINSI;
      |SIGUE;

      |SI #dscaz064#14 = "S";
         |DDEF aAlfa; |QBF aAlfa;
         aAlfa1 = aAlfa + "informe.inf"; |FBORRA aAlfa1;
         aAlfa1 = aAlfa + "informe.rpt"; |FBORRA aAlfa1;
      |FINSI;

      |CIERRA #dscaw047;
   |FINSI;

   |HAZ Tipo9;
|FPRO;
