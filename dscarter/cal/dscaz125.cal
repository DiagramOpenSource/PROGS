|FICHEROS;
     dscaz125 #0; ||Este Def.
     dscam021 #1;
     dscam022 #7;
     dscam008 #8;

     &agif024@ #100;
     &caclipr@ #101;
|FIN;

|VARIABLES;
   &eaCodProv = "";
   &eaCtaProv = "";
   &eaNomProv = "";
   &eaAviso   = "";

   MonBase  = 0;
   Moneda   = "";
   Cantidad = 0;
   aPrefijo = "";
   &HayGestion = 0; &HayConta = 0;
   &MaxDigit = 0;

   ffff  = @;
   aEmp  = "";
   empresa = ""
   aPresa = "";
   swVic = 0;
   cabe1 = "";
   cabe2 = "";
   cabe3 = "";
   cabe4 = "";
   cabe5 = "";
   cabe6 = "";
   cabe7 = "";
   libre4  = "    ";
   libre10 = "          ";
   libre40 = "                                        ";
   s10 = "          ";
   s15 = "               ";
   s50 = "                                                  ";
   s36 = "                                    ";
   handle = "";
   varalfa1 = "";
   varalfa2 = "";
   contenido = "";
   fiche = "";
   espacio = " ";
   dd = "";
   mm = "";
   aa = "";
   fechalf = "";
   impalf = "";
   trecibo = 0;
   timpsuc = 0;
   tnumsuc = 0;
   contreg = 0;
   nPro    = 0;
   elefe   = "";
   ceerre  = "";
   l       = 0;
   traba   = "";
   &programa = "";
   &ficcopia = "";
   aAlfa     = "";

   aOrigen = "";
   aNombre = "";
   aDestino = "";
   Comodin  = "";
   ip       = "";
   aLong    = "";
   nLong    = 0;
   aParte1  = "";
   aParte2  = "";
   nParte1  = 0;
   nParte2  = 0;
   MaxDig   = 0;
   Calc     = 0;

   nLinea   = 0;
   nSal     = 0;
   aAlfa1   = "";
   aAlfa2   = "";

   aNif       = "";
   aNombreNif = "";
   aDireccion = "";
   aPoblacion = "";
   aProvincia = "";
   aCodigoPos = "";
   aCCC       = "";
   aCta       = "";

   nCont      = 0;
   nCalc      = 0;
   nSw        = 0;

   aCodProv = "";
   aCtaProv = "";
   aNomProv = "";

{300}aAviso   = "";
{300}aCodPrv  = "";
{300}aCtaPrv  = "";
{300}aNomPrv  = "";
|FIN;

|PROCESO CargaPagaLin;
     ||------------------------------------------------------
     ||------ PRIMER REGISTRO OPCIONAL ----------------------
     ||------------------------------------------------------
     cabe1 = aPrefijo + "6" + (("00" + nLinea) % -102); nLinea = nLinea + 1;

     cabe3 = #dscam022#37; cabe3 = cabe3 % 0118;
     cabe4 = " " * 36;
     cabe5 = " " * 4;
     varalfa1 = cabe1 + cabe2 + cabe3 + cabe4 + cabe5;
     || varalfa   =  cabe1     Fijo 0180
     ||           +  cabe2     Cod.Beneficiario
     ||           +  cabe3     Concepto 3
     ||           +  cabe4     Concepto 4
     ||           +  cabe5     Libre
     contenido = handle + espacio + varalfa1 + elefe;
     |FGRABA contenido;
     l = l + 1;
|FPRC;

|DEFBUCLE CargaPagaLin;
#dscam022#1;
;
#dscam021#0,001;
#dscam021#0,999;
;
NULL,CargaPagaLin;
|FIN;

|PROCESO CargaPaga;
     #dscam022#0 = #dscam021#0;
     #dscam022#1 = 1;
     |LEE 000 #dscam022.];
     |SI FSalida ! 0; |O #dscam022#0 ! #dscam021#0;
        |ACABA;
     |SINO;
        |SI #dscaz125#2 > #dscam022#8; |O #dscaz125#3 < #dscam022#8;
           |ACABA;
        |FINSI;
     |FINSI;

     |ABRE #dscam008;
     #dscam008#40 = #dscam022#13;
     |LEE 000#dscam008.=;
     |SI FSalida ! 0; |DDEFECTO #dscam008; |FINSI;
     |CIERRA #dscam008;

     |SI HayGestion ! 0;
        #agif024@#0 = #dscam008#32;
        |LEE 000#agif024@.=;
        |SI FSalida ! 0; |ACABA; |FINSI;
        aNif       = #agif024@#15;
        aNombreNif = #agif024@#1;
        aDireccion = #agif024@#5;
        aPoblacion = #agif024@#6;
        aProvincia = #agif024@#7;
        aCodigoPos = #agif024@#180;
        aCCC       = #agif024@#32 + #agif024@#33 + #agif024@#31;
        aCta       = #agif024@#30;
     |SINO;
        |SI HayConta ! 0;
           #caclipr@#23 = "P";
           #caclipr@#10 = #dscam022#8;
           |LEE 000#caclipr@.=;
           |SI FSalida ! 0; |ACABA; |FINSI;
           aNif       = #caclipr@#9;
           aNombreNif = #caclipr@#1;
           aDireccion = #caclipr@#44;
           aPoblacion = #caclipr@#48;
           aProvincia = #caclipr@#47;
           aCodigoPos = (("00" + #caclipr@#45) % -102) + (("000" + #caclipr@#46) % -103);
           aCCC       = #caclipr@#29 + #caclipr@#30 + #caclipr@#31;
           aCta       = #caclipr@#32;
        |SINO;
           |SI FSalida ! 0; |ACABA; |FINSI;
        |FINSI;
     |FINSI;

     |SI swVic = 0;
          |HAZ AbrirSecuencial;
          swVic = 1;
     |FINSI;

     ||------------------------------------------------------
     ||------ PRIMER REGISTRO INDIVIDUAL --------------------
     ||------------------------------------------------------
     cabe1 = aPrefijo + "301";
     aAlfa = aNif; |QBF aAlfa;
     cabe2 = (s10 + aAlfa) % -110;

     cabe3 = (aNombreNif + (" " * 40)) % 0140;

     Cantidad = #dscam021#9; |HAZ Conversion; impalf = Cantidad;
     ||impalf  = #dscam021#9;       || Importe pagare rellenado de ceros
     impalf  = "0000000000" + impalf;
     cabe4   = impalf % -110;

     |SI #dscaz125#12 = "P";
        fechalf = #dscam021#1;       || Fecha de emision del Pagare
     |SINO;
        fechalf = #dscam008#4;       || Fecha de emision del recibo
     |FINSI;
     dd      = fechalf % A102;
     mm      = fechalf % A402;
     aa      = fechalf % A902;
     cabe5   = dd + mm + aa;
     fechalf = #dscam021#2;       || Fecha vencimiento del Pagare
     dd      = fechalf % A102;
     mm      = fechalf % A402;
     aa      = fechalf % A902;
     cabe6   = dd + mm + aa;

     varalfa1 = cabe1 + cabe2 + "00000" + "0000001" + cabe3 + cabe4 + cabe5 + cabe6 + "0" + " ";
     || varalfa   =  cabe1     Fijo 0301
     ||           +  cabe2     Cod.Beneficiario (NIF)
     ||           +  00000     Libre 00000
     ||           +  0000001   Referencia Fichero
     ||           +  cabe3     Nombre del Beneficiario
     ||           +  cabe4     Importe Pagare
     ||           +  cabe5     Fecha Emision
     ||           +  cabe6     Fecha Vencimiento
     ||           +  0         Forma de Pago
     ||           +  " "       LIBRE 1
     contenido = handle + espacio + varalfa1 + elefe;
     |FGRABA contenido;
     l = l + 1;

     ||------------------------------------------------------
     ||------ SEGUNDO REGISTRO INDIVIDUAL -------------------
     ||------------------------------------------------------
     cabe1 = aPrefijo + "302";

     cabe3 = aDireccion; |QBF cabe3; cabe3 = (cabe3 + s36) % 136;

     varalfa1 = cabe1 + cabe2 + cabe3 + s36 + libre4;
     || varalfa   =  cabe1     Fijo 0180
     ||           +  cabe2     Cod.Beneficiario
     ||           +  cabe3     Domicilio
     ||           +  s36       LIBRE 36
     ||           +  libre4    LIBRE 4
     contenido = handle + espacio + varalfa1 + elefe;
     |FGRABA contenido;
     l = l + 1;
     ||------------------------------------------------------
     ||------ TERCER REGISTRO INDIVIDUAL --------------------
     ||------------------------------------------------------
     cabe1 = aPrefijo + "303";

     cabe3 = aCodigoPos; |QBF cabe3; cabe3 = (cabe3 + "     ") % 105;
     cabe4 = aPoblacion;  |QBF cabe4; cabe4 = (cabe4 + s36) % 136;
     cabe5 = aProvincia;  |QBF cabe5; cabe5 = (cabe5 + s15) % 115;
     cabe6 = aCCC % 110;
     cabe7 = aCta % 110;
     cabe6 = cabe6 + cabe7;
     varalfa1 = cabe1 + cabe2 + cabe3 + cabe4 + cabe5 + cabe6;
     || varalfa   =  cabe1     Fijo 0180
     ||           +  cabe2     Cod.Beneficiario
     ||           +  cabe3     Codigo Postal
     ||           +  cabe4     Poblacion
     ||           +  cabe5     Provincia
     ||           +  cabe6     Cuenta Abono
     contenido = handle + espacio + varalfa1 + elefe;
     |FGRABA contenido;
     l = l + 1;

     nLinea = 1;
     |BUCLE CargaPagaLin;

     trecibo = trecibo + 1;
     timpsuc = timpsuc + Cantidad;

     #dscam021#7 = "A"; |GRABA 020#dscam021.a;
|FPRC;

|DEFBUCLE CargaPaga;
     #dscam021#5;
     1,7; || ................. ordenado por banco+vto, selec. prov y emision
     #0#8, #0#6, #0#0, #0#4, " ";
     #0#8, #0#7, #0#1, #0#5, "z";
     be;
     NULL, CargaPaga;
|FIN;

|PROCESO ChequeaError;
   |SI nSw > 5;
      IaAviso = aAviso1<-; IaAviso = IaAviso + nCont;
      |SI nSw = 6; aAviso = "Ficha del proveedor de gestion no encontrada"; |FINSI;
      |SI nSw = 7; aAviso = "Ficha del proveedor de contabilidad no encontrada"; |FINSI;
      |SI nSw = 8; aAviso = "Registro sin codigo ni cuenta de proveedor"; |FINSI;
      IaCodPrv = aCodPrv1<-; IaCodPrv = IaCodPrv + nCont;
      IaCtaPrv = aCtaPrv1<-; IaCtaPrv = IaCtaPrv + nCont;
      IaNomPrv = aNomPrv1<-; IaNomPrv = IaNomPrv + nCont;
      aCodPrv = aCodProv; aCtaPrv = aCtaProv; aNomPrv = aNomProv;
      nCont = nCont + 1;
      |ACABA;
   |FINSI;

   |QBF aAlfa;
   |SI aAlfa = "";
      IaAviso = aAviso1<-; IaAviso = IaAviso + nCont;
      |SI nSw = 1; aAviso = "NIF no informado en la ficha";       |FINSI;
      |SI nSw = 2; aAviso = "Direccion no informada en la ficha"; |FINSI;
      |SI nSw = 3; aAviso = "CP Envio no informado en la ficha";  |FINSI;
      |SI nSw = 4; aAviso = "Provincia no informada en la ficha"; |FINSI;
      |SI nSw = 5; aAviso = "Poblacion no informada en la ficha"; |FINSI;

      IaCodPrv = aCodPrv1<-; IaCodPrv = IaCodPrv + nCont;
      IaCtaPrv = aCtaPrv1<-; IaCtaPrv = IaCtaPrv + nCont;
      IaNomPrv = aNomPrv1<-; IaNomPrv = IaNomPrv + nCont;
      aCodPrv = aCodProv; aCtaPrv = aCtaProv; aNomPrv = aNomProv;

      nCont = nCont + 1;
   |FINSI;
|FPRC;

|PROCESO Comprueba;
     #dscam022#0 = #dscam021#0;
     #dscam022#1 = 1;
     |LEE 000 #dscam022.];
     |SI FSalida ! 0; |O #dscam022#0 ! #dscam021#0;
        |ACABA;
     |SINO;
        |SI #dscaz125#2 > #dscam022#8; |O #dscaz125#3 < #dscam022#8;
           |ACABA;
        |FINSI;
     |FINSI;

     |ABRE #dscam008;
     #dscam008#40 = #dscam022#13;
     |LEE 000#dscam008.=;
     |SI FSalida ! 0; |DDEFECTO #dscam008; |FINSI;
     |CIERRA #dscam008;

     |SI HayGestion ! 0; |Y #dscam008#32 ! "      ";
        #agif024@#0 = #dscam008#32;
        |LEE 000#agif024@.=;
        |SI FSalida ! 0;
           nSw = 6; |HAZ ChequeaError; |ACABA;
        |FINSI;
        aNif       = #agif024@#15;
        aNombreNif = #agif024@#1;
        aDireccion = #agif024@#5;
        aPoblacion = #agif024@#6;
        aProvincia = #agif024@#7;
        aCodigoPos = #agif024@#180;
        aCCC       = #agif024@#32 + #agif024@#33 + #agif024@#31;
        aCta       = #agif024@#30;
     |SINO;
        |SI HayConta ! 0; |Y #dscam022#8 ! "            ";
           #caclipr@#23 = "P";
           #caclipr@#10 = #dscam022#8;
           |LEE 000#caclipr@.=;
           |SI FSalida ! 0;
              nSw = 7; |HAZ ChequeaError; |ACABA;
              |ACABA;
           |FINSI;
           aNif       = #caclipr@#9;
           aNombreNif = #caclipr@#1;
           aDireccion = #caclipr@#44;
           aPoblacion = #caclipr@#48;
           aProvincia = #caclipr@#47;
           aCodigoPos = (("00" + #caclipr@#45) % -102) + (("000" + #caclipr@#46) % -103);
           aCCC       = #caclipr@#29 + #caclipr@#30 + #caclipr@#31;
           aCta       = #caclipr@#32;
        |SINO;
           |SI FSalida ! 0;
              nSw = 8; |HAZ ChequeaError; |ACABA;
              |ACABA;
           |FINSI;
        |FINSI;
     |FINSI;

     aCodProv = #dscam008#32; aCtaProv = #dscam008#5;
     aNomProv = aNombreNif;
     || Primera comprobacion .... NIF vacio
     nSw = 1; aAlfa = aNif;       |HAZ ChequeaError;
     || Segunda comprobacion .... Direccion vacia
     nSw = 2; aAlfa = aDireccion; |HAZ ChequeaError;
     || Tercera comprobacion .... CP Envio vacio
     nSw = 3; aAlfa = aCodigoPos; |HAZ ChequeaError;
     || Cuarta comprobacion ..... Provincia vacia
     nSw = 4; aAlfa = aProvincia; |HAZ ChequeaError;
     || Quinta comprobacion ..... Poblacion vacia
     nSw = 5; aAlfa = aPoblacion; |HAZ ChequeaError;
|FPRC;

|DEFBUCLE Comprueba;
     #dscam021#5;
     1,7; || ................. ordenado por banco+vto, selec. prov y emision
     #0#8, #0#6, #0#0, #0#4, " ";
     #0#8, #0#7, #0#1, #0#5, "z";
     be;
     NULL, Comprueba;
|FIN;

|PROCESO ejec; |TIPO 3;
     |ABRE #dscam021; |ABRE #dscam022;
     |SI #0#9 = "N"; |ACABA; |FINSI;

     nCont = 0; |BUCLE Comprueba;
     |SI nCont > 0;
        aAlfa = "    NSe han encontrado incidencias. " + &191 + " Emitir un listado con las mismas ?";
        |CONFI aAlfa;
        |SI FSalida = 0;
           Impresora = "Crystal Reports";
           |IMPRE 0;
           |SI FSalida = 0;
              |SI Impresora = "CrystalReport";
                 |INFOR "dsccz125";
              |SINO;
                 |INFOR "dscaz125";
              |FINSI;
              |SI FSalida = 0;
                 nCalc = nCont;
                 |PARA nCont = 0; |SI nCont < nCalc; |HACIENDO nCont = nCont + 1;
                    IaAviso = aAviso1<-; IaAviso = IaAviso + nCont;
                    IaCodPrv = aCodPrv1<-; IaCodPrv = IaCodPrv + nCont;
                    IaCtaPrv = aCtaPrv1<-; IaCtaPrv = IaCtaPrv + nCont;
                    IaNomPrv = aNomPrv1<-; IaNomPrv = IaNomPrv + nCont;

                    eaCodProv = aCodPrv; eaCtaProv = aCtaPrv;
                    eaNomProv = aNomPrv; eaAviso   = aAviso;

                    |PRINT;
                 |SIGUE;
                 |PIEINF; |FININF;
              |FINSI;
              |FINIMP;
           |FINSI;
        |FINSI;
        aAlfa = "    N" + &191 + " Continuar con la emision del soporte ?";
        |CONFI aAlfa;
        |SI FSalida ! 0; |ACABA; |FINSI;
     |FINSI;

     aPrefijo = "5";
     elefe = &010;                 || Cr
     ceerre = &013;                || Lf
     traba = ceerre + elefe;
     elefe = traba;
     |BUCLE CargaPaga;
     |SI swVic = 1;
          |HAZ campo_totales;
          FSalida = handle;
          |FCIERRA FSalida;
     |FINSI;
     |SI swVic = 1;
          |AVISO;
          |SI #dscaz125#12 = "P";
             aNombre = "casecpag.seq";
          |SINO;
             aNombre = "confirme.seq";
          |FINSI;
          |DFICE aOrigen;
          |CONFI "2400SDESEA GRABAR AHORA EL DISKETTE ";
          |SI FSalida = 0;
             |HAZ GrabaDiskette;
          |FINSI;
          |HAZ GuardaFichero;
     |FINSI;
     |CIERRA #dscam021; |CIERRA #dscam022;
|FPRC;

|PROCESO AbrirSecuencial;
  |DFICE empresa;
  aPresa = empresa % -107;
  aPresa = aPresa % 106;
  |SI #dscaz125#12 = "P";
     fiche = "wb  " + empresa + "casecpag.seq";
  |SINO;
     fiche = "wb  " + empresa + "confirme.seq";
  |FINSI;
  |QBF fiche;
  |FABRE fiche;
  handle = FSalida;
  |SI FSalida ] 0;
      |HAZ cabecera_p;
  |FINSI;
|FPRC;

||***********************************************************;
||************ REGISTRO DE CABECERA DE PRESENTADOR **********;
||***********************************************************;
|PROCESO cabecera_p;
  cabe1 = aPrefijo + "101";

  cabe2 = #48#2; |QBF cabe2; cabe2 = (s10 + cabe2) % -110;

  ffff    = ~;
  fechalf = ffff;    || Fecha Soporte
  dd = fechalf % A102;
  mm = fechalf % A402;
  aa = fechalf % A902;
  cabe3 = dd + mm + aa;

  cabe4 = "00000001";
  cabe5 =  #dscam021#12;  || Codigo Banco
  cabe6 =  #dscam021#13;  || Codigo Sucursal.
  cabe7 =  #dscam021#14;  || Cuenta bancaria.

  varalfa1 = cabe1 + cabe2 + cabe3 + cabe4 + #48#1 + cabe5 + cabe6 + cabe7 + libre4;
  || varalfa   =  cabe1     Fijo 0101
  ||           +  cabe2     nif
  ||           +  cabe3     Fecha ddmmaa
  ||           +  cabe4     Referencia Fichero
  ||           +  #6#1     Nombre del presentador
  ||           +  cabe5     Codigo Banco
  ||           +  cabe6     Codigo Sucursal
  ||           +  cabe7     Codigo Cuenta
  ||           +  libre4    LIBRE 4  espacios
  contenido = handle + espacio + varalfa1 + elefe;
  |FGRABA contenido;
  l = l + 1;

  cabe1 = aPrefijo + "102";

  cabe3 = #48#3; |QBF cabe3; cabe3 = (cabe3 + s36) % 136;

  varalfa1 = cabe1 + cabe2 + cabe3 + s36 + libre4;
  || varalfa   =  cabe1     Fijo 0180
  ||           +  cabe2     nif
  ||           +  cabe3     Domicilio
  ||           +  s36       LIBRE 36
  ||           +  libre4    LIBRE 4
  contenido = handle + espacio + varalfa1 + elefe;
  |FGRABA contenido;
  l = l + 1;

  cabe1 = aPrefijo + "103";

  cabe3 = (("00" + #48#8) % -102) + (("000" + #48#9) % -103); |QBF cabe3; cabe3 = (cabe3 + "     ") % 105;
  cabe4 = #48#10; |QBF cabe4; cabe4 = (cabe4 + s36) % 136;
  cabe5 = #48#11; |QBF cabe5; cabe5 = (cabe5 + s36) % 125;


  varalfa1 = cabe1 + cabe2 + cabe3 + cabe4 + cabe5 + libre10;
  || varalfa   =  cabe1     Fijo 0180
  ||           +  cabe2     nif
  ||           +  cabe3     Codigo Postal
  ||           +  cabe4     Poblacion
  ||           +  cabe5     Provincia
  ||           +  libre10   LIBRE 10
  contenido = handle + espacio + varalfa1 + elefe;
  |FGRABA contenido;
  l = l + 1;
|FPRC;

|PROCESO campo_totales;

|| **** REGISTRO TOTAL ORDENANTE

  cabe4 = trecibo;
  cabe4 = "000000000000" + cabe4;
  cabe4 = cabe4 % -112;
  impalf = timpsuc;
  impalf = "000000000000" + impalf;
  impalf = impalf % -112;
  cabe3  = impalf;
  l = l + 1;

  cabe5  = l;    ||Numero Registros
  cabe5  = "000000000000" + cabe5;
  cabe5  = cabe5 % -112;

  cabe1  = aPrefijo + "901";
  cabe2  = #48#2; |QBF cabe2; cabe2 = (s10 + cabe2) % -110;

  varalfa1 = cabe1 + cabe2 + cabe3 + cabe4 + cabe5 + libre40;
  || varalfa1  =     cabe1    Fijo 0901
  ||           +     cabe2    Nif Ordenante
  ||           +     cabe3    Importe Total
  ||           +     cabe4    N§ordenes
  ||           +     cabe5    N§Registros
  ||           +     libre40  LIBRE 40
  contenido = handle + espacio + varalfa1 + elefe;
  |FGRABA contenido;
|FPRC;

|PROCESO PuntoCuenta; |TIPO 6;
   MaxDig = MaxDigit;

   Comodin = #dscaz125#Campo#;
   Comodin = Comodin - ".";
   |SI FSalida ! 0;
      Calc = FSalida;
      nParte1 = 99 + ((Calc / 100) ! 0);
      aParte1 = Comodin % nParte1;
      nParte2 = 98 + Calc;
      aParte2 = Comodin % nParte2;
      |QBF Comodin;
      aLong = Comodin % 0; nLong = aLong;
      nLong = MaxDig - nLong;
      Comodin = aParte1 + ("0" * nLong) + aParte2;
      #dscaz125#Campo# = Comodin;
      |PINTA #dscaz125#Campo#;
   |FINSI;
|FPRC;

|PROCESO GrabaDiskette;
   |VARIABLES;
   |FIN;

   |PUSHV 0501 2380;
   |BLANCO 0814 1465;
   |CUADRO 0914 1365;
   |IP_REMOTO ip; |QBF Comodin;
   |PINTA 1015, "      Introduzca un disquete en su PC             ";
   |PINTA 1115, "               y pulse una tecla                  ";
   |PAUSA;

   aDestino = "/ds/tmp/"; |MKDIR aDestino;
   aDestino = aDestino + aNombre;
   Comodin = aOrigen; |QBF Comodin; Comodin = Comodin + aNombre;
   |SI ip ! "";
      |ENVIA_FICHERO Comodin, aDestino;
   |SINO;
      |COPIA_FICHERO Comodin, aDestino;
   |FINSI;
   Comodin = "COPY " + aDestino + " A:";
   |RSYSTEM Comodin;
   |POPV;
|FPRC;

|PROCESO GuardaFichero;
   |IP_REMOTO ip;

   aDestino = #0#10; |QBF aDestino;
   |SI aDestino ! "";
       |MKDIR aDestino;
       aLong = aDestino % 0;
       aLong = aLong + "01";
       aAlfa1 = aDestino % aLong;
       |SI aAlfa1 ! "/";
           aDestino = aDestino + "/";
       |FINSI;
       aDestino = aDestino + aNombre;
       Comodin = aOrigen; |QBF Comodin; Comodin = Comodin + aNombre;
       |SI ip ! "";
           |ENVIA_FICHERO Comodin, aDestino;
       |SINO;
           |COPIA_FICHERO Comodin, aDestino;
       |FINSI;
   |FINSI;
|FPRC;

|PROCESO PideMoneda;
   |PUSHV 2103 2337;
   |BLANCO 2103 2337;
   |CUADRO 2105 2335;
   |ATRI R; |PINTA 2106 , " Seleccione moneda de emision "; |ATRI N;
   |PINTA 2213, "(Pesetas/Euros)";
   |ATRI I; |PINTA 2214, "P"; |PINTA 2222, "E"; |ATRI N;
   E_sup  = 1;
   E_inf  = "EP";
   |SI MonBase = 1; Moneda = "P"; |SINO; Moneda = "E"; |FINSI;
   Moneda = 2229 ? 0;
   |POPV;
|FPRC;

|PROCESO Conversion;
   |SI MonBase = 1;
      |SI Moneda = "E";
         Cantidad = ((Cantidad / 166.386) ! 2) * 100;
      |FINSI;
   |SINO;
      |SI Moneda = "P";
         Cantidad = (Cantidad * 166.386) ! 0;
      |SINO;
         Cantidad = Cantidad * 100;
      |FINSI;
   |FINSI;
|FPRC;

|PROGRAMA;
     |HAZ Tipo8;

     |CLS;
     |CABEZA "Pase Pagares a Soporte Magnetico";
     |ABRE #0;
     |LEE 101#0=;
     nSal = FSalida;
     #0#9 = "N";
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI nSal = 0; |GRABA 020#0a; |FINSI;
     |SI nSal ! 0; |GRABA 020#0n; |FINSI;
     |LIBERA #0;
     |CIERRA #0;

     |HAZ Tipo9;
|FPRO;
