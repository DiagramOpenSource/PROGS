|FICHEROS;
   dscam046 #0;
   dscam047 #1;
   dscam014 #2;
   dscam008 #3;
   dscaw001 #4,MANTE;
   dscam009 #5;
   dscam051 #51;
   dscam067 #67;

   dscaw013 #13;

   dscaw102 #15,MANTE;

   dscam042 #16;
   camasic@ #100;
|FINSI;

|VARIABLES;
   &eaFichClas = "";  &eaTipoClas = ""; &enSwClas = 0;
   &enSwLineal = 0; &enClave = 0;
   &enNumDocumento = 0;
   &enEmpGestion = 0; &enSwVisado = 0;

   &eaCodigo = ""; &eaCuenta = ""; &enSwErr = 0;

   &Divisa = ""; &NombreDiv = ""; &eaMBase = ""; &eaNombreMBase = "";
   &CambioDivCm = 0; &nDeci_Base = 0; &nDeci_Div = 0;
   &PosLibro = 0; &PosSerie = 0; &PosFactu = 0; &PosRecib = 0; &enDocum = 0;
   &eaUsuario = ""; &enImpAnt = 0;  &enImpAct = 0;  &eaDescr = "";
   &eaTipoReg = ""; &eaNumero = ""; &eaTipoOp = "";
   &eaTipoRecibo = ""; &enNumDocum = 0; &eaMensRet = ""; &enSwError = 0; &eaTOperacion = "";
   &Pagos = 0; &eaLlamada = ""; &enTipoRec = 0;

   &efFecha      = @;
   &eaEmpContab  = "";
   &eaDirConta   = "";

   &efFechaVto   = @;
   &eaCliente    = "";
   &enDocumento  = 0;

   aDCodProv = "";
   aHCodProv = "";
   aDCtaProv = "";
   aHCtaProv = "";

   Clave1 = ""; Clave2 = ""; aResp = "";
   nAutom = 0; SwClave = 0;
   nSwErr = 0; nSwErr1 = 0;
   aAlfa  = "";
   aAlfa1 = "";
   aAlfa2 = "";

   aCuenta = "";

   Codigo = 0;
   Linea  = 0;
   FEstado = 0;

   ModoEntrada = 0;
   nFlag = 0;
   Comodin = "";
   SwError = 0;
   nSwError = 0;

   NumDias  = 0;
   nPorcInt = 0;
   nInteres = 0;
   Calc     = 0;
   Calc1    = 0;
   Calc2    = 0;

   LinCob   = 0;

   &CodProv = "";
   &CtaProv = "";
|FIN;

|PROCESO Contador; |TIPO 7;
   CtaProv = ""; CodProv = "";

   ModoEntrada = (FEntrada / 100) ! 0;
   |SI ModoEntrada ! 1; |ACABA; |FINSI;

   |ABRE #dscam046;
   |LEE 010#dscam046.u;
   |SI FSalida = 0;
      Codigo = #dscam046#0;
      |DDEFECTO #dscam046;
      #dscam046#0 = Codigo + 1;
      |PINTA #dscam046#0;
   |FINSI;
   |CIERRA #dscam046;
|FPRC;

|PROCESO EntregasPdtes;
   nSwErr1 = 1; |ERROR10;
|FPRC;

|DEFBUCLE EntregasPdtes;
#dscam008#5;
14;
aCuenta, 000000001, "e";
aCuenta, 999999999, "e";
;
NULL, EntregasPdtes;
|FIN;

|PROCESO RecogeRec;
   |SI nAutom = 0;
      |SI #dscam051#121 = "N";
         eaCodigo = #dscam008#32; eaCuenta = #dscam008#5; |HAZ MiraSiBloq;
         |SI enSwErr ! 0;
            |MENAV "    Proveedor bloqueado";
            |ERROR; |ACABA;
         |FINSI;
      |SINO;
         || Certificados de contratistas
         eaCliente = #dscam008#32; eaCuenta = #dscam008#5;
         efFechaVto = #dscam046#1;
         enDocumento = #dscam008#40; |HAZ MiraCertContr;
         |SI enSwErr ! 0; |ERROR; |ACABA; |FINSI;
      |FINSI;

      enNumDocumento = #dscam008#40;
      enEmpGestion = #dscam008#46; |HAZ MiraSiVisado;
      |SI enSwVisado < 0;
         |MENAV "    Factura proveedor no visada, no se permite el pago";
         |ERROR;  SwError = 1; |ACABA;
      |FINSI;

      enSwError = 0;
      eaTOperacion = "C"; eaTipoRecibo = "C"; enNumDocum = #dscam047#13; |HAZ ProcesaTempo;
      |SI enSwError = 1; |ERROR; |ACABA; |FINSI;
   |FINSI;

   |ABRE #dscam008;
   #dscam008#40 = #dscam047#13;
   |LEE 010#dscam008.=;
   |SI FSalida = 0;
      enSwClas   = 1; eaFichClas = "dscam046"; eaTipoClas = #dscam008#12;
      |HAZ CompruebaClas;
      |SI eaFichClas = "ERROR";
         |MENAV "    Documento inoperable en el perfil actual ";
         |ERROR; |ACABA;
      |FINSI;
      |SI eaFichClas = "INFOR";
         |CONFI "    Documento inoperable en el perfil actual. Cambiar Tipo recibo ?";
         |SI FSalida = 0;
            eaFichClas = "dscam046";
            enNumDocum = #dscam008#40; enTipoRec = #dscam008#12; |HAZ CambiaTR;
         |SINO;
            |CIERRA #dscam008; |ERROR; |ACABA;
         |FINSI;
      |FINSI;
      |SI #dscam008#14 = "A"; |O #dscam008#14 = "D";
         |SI #dscam008#14 = "A"; |MENAV "    El recibo está agrupado"; |FINSI;
         |SI #dscam008#14 = "D"; |MENAV "    El recibo está dividido"; |FINSI;
         |CIERRA #dscam008; |ERROR; |ACABA;
      |FINSI;

      aCuenta = #dscam008#5;
      nSwErr1 = 0;
      |SALVA_FICHA #dscam008;
      |BUCLE EntregasPdtes;
      |REPON_FICHA #dscam008;
      |SI nSwErr1 = 1;
         aAlfa = "    NEste proveedor tiene entregas a cuenta pendientes de saldar. " + &191 + " Continuar ?";
         |CONFI aAlfa;
         |SI FSalida ! 0;
             |CIERRA #dscam008; |ERROR; |ACABA;
         |FINSI;
      |FINSI;

      |SI CodProv ! ""; |O CtaProv ! "";
         |SI #dscam008#32 ! "      "; |Y CodProv ! "      "; |Y #dscam008#32 ! CodProv;
            |MENAV "    El proveedor del documento marcado no coincide con el de los anteriores";
            |ERROR; |ACABA;
         |FINSI;
         |SI #dscam008#5 ! "            "; |Y CtaProv ! "            "; |Y #dscam008#5 ! CtaProv;
            |MENAV "    El proveedor del documento marcado no coincide con el de los anteriores";
            |ERROR; |ACABA;
         |FINSI;
      |FINSI;
      |SI #dscam008#16 ! #dscam046#3; |Y #dscam046#24 = "S";
         |MENAV "    El banco asociado al pago no coincide con el seleccionado";
         |ERROR; |ACABA;
      |FINSI;
      Divisa = #dscam008#59; |HAZ TomaDecim;
      |SI #dscam008#31 = 0;
         |MENAV "    Recibo liquidado";
         |ERROR; |ACABA;
      |SINO;
         nSwError = 0;
         |SI #dscam008#14 = "E";
            |SI ModoEntrada = 1;
               nSwError = 1;
               |MENAV "    Recibo en cartera"; |ERROR; |ACABA;
            |SINO;
               |SI #dscam008#17 ! #dscam047#0; |O #dscam008#37 ! #dscam047#1;
                  nSwError = 1;
                  |MENAV "    Recibo en cartera"; |ERROR; |ACABA;
               |FINSI;
            |FINSI;
         |FINSI;
         |SI nSwError = 0;
            #dscam047#2 = #dscam008#27;
            #dscam047#3 = #dscam008#1;
            #dscam047#4 = #dscam008#0;
            #dscam047#5 = #dscam008#2;
            #dscam047#6 = #dscam008#3;
            #dscam047#7 = #dscam008#31;
            #dscam047#8 = #dscam008#5;
            #dscam047#10 = #dscam008#6;
            #dscam047#12 = "N";
            #dscam047#18 = #dscam008#58;
            #dscam047#25 = #dscam008#63;
         |FINSI;
      |FINSI;
   |FINSI;
   |CIERRA #dscam008;
|FPRC;

|PROCESO BorraLin;
   |SI #dscam046#7 = "S"; |O #dscam046#8 = "S";
      |ABRE #dscam009;
      #dscam009#14 = #dscam047#13;
      #dscam009#3  = #dscam047#14;
      |LEE 101#dscam009.=;
      |SI FSalida = 0;
         |ABRE #dscam051;
         |LEE 000#dscam051.p;
         |SI FSalida ! 0;
            |DDEFECTO #dscam051;
            |GRABA 000#dscam051.c;
         |FINSI;
         |CIERRA #dscam051;
         |SI #dscam051#1 = "S";
            |ABRE #dscam067;
            #dscam067#8 = 0;
            #dscam067#9 = 0;
            #dscam067#0 = "TA";
            #dscam067#1 = #dscam046#0;
            #dscam067#2 = 1;
            #dscam067#3 = "";
            #dscam067#11 = 1;
            |LEE 000#dscam067.];
            FEstado = FSalida;
            Clave1 = #dscam067#1; Clave2 = #dscam067#2;
            |PARA; |SI FEstado = 0; |Y #dscam067#0 = "TA"; |Y Clave1 = #dscam046#0; |HACIENDO;
               |SI FEstado = 0; |Y Clave1 = #dscam046#0; |Y #dscam067#0 = "TA";
                  |SAL;
               |FINSI;
               |LEE 000#dscam067.s;
               FEstado = FSalida;
               Clave1 = #dscam067#1; Clave2 = #dscam067#2;
            |SIGUE;

            |SI FEstado = 0; |Y Clave1 = #dscam046#0;
               Comodin = Usuario; |QBF Comodin; Comodin = "ac" + Comodin;
               |NOME_DAT #13 Comodin;
               |DELINDEX #dscaw013; |ABRE #dscaw013;
               |DDEFECTO #dscaw013;
               #dscaw013#15 = #dscam067#10;  #dscaw013#0  = 1;
               #dscaw013#13 = "dscam047";    #dscaw013#14 = 0;
               #dscaw013#11 = "N";           #dscaw013#12 = 5;
               #dscaw013#5 = #dscam046#0;    #dscaw013#6 = #dscam046#0;
               |GRABA 020#dscaw013.n;
               Comodin = #dscam067#10;
               Comodin = "dscam033.tab;" + Comodin + ",B";
               |LIBERA #dscam046; |CIERRAT #dscam046;
               |LIBERA #dscam047; |CIERRAT #dscam047;
               |SUB_EJECUTA_NP Comodin;
               |ABRET #dscam046; |LEE 101#dscam046.=;
               |ABRET #dscam047; |LEE 101#dscam047.=;

               |DFICE aAlfa; |QBF aAlfa; aAlfa = "ac" + Usuario + ".dat"; |FBORRA aAlfa;
               |DFICE aAlfa; |QBF aAlfa; aAlfa = "ac" + Usuario + ".ddx"; |FBORRA aAlfa;
               |DFICE aAlfa; |QBF aAlfa; aAlfa = "ac" + Usuario + ".ixx"; |FBORRA aAlfa;
            |FINSI;
            |CIERRA #dscam067;
         |SINO;
            |MENAV "    Ha modificado un efecto contabilizado. Debera volver a ejecutar el enlace ";
         |FINSI;
         |BORRA 020#dscam009.a;
      |FINSI;
      |LIBERA #dscam009; |CIERRA #dscam009;
   |FINSI;

   |ABRE #dscam008;
   #dscam008#40 = #dscam047#13;
   |LEE 101#dscam008.=;
   |SI FSalida = 0;
      |SI ModoEntrada = 3;
         #dscam008#17 = "00000";
         #dscam008#37 = 0;
         #dscam008#39 = "";
         #dscam008#47 = "";
         #dscam008#26 = "";
      |FINSI;
      |SI #dscam046#7 = "S"; |O #dscam046#8 = "S";
         #dscam008#23 = #dscam008#23 - #dscam047#7;  #dscam008#57 = #dscam008#57 - #dscam047#18;
         #dscam008#31 = #dscam008#22 - #dscam008#23; #dscam008#58 = #dscam008#53 - #dscam008#57;
      |FINSI;
      |SI #dscam008#23 = 0;
         |SI #dscam008#17 = "00000";
            #dscam008#14 = "P";
            #dscam008#15 = "Pendiente de pago";
         |SINO;
            #dscam008#14 = "E";
            #dscam008#15 = "Englobado en Talon";
            |SI #dscam051#110 = "S";
               #dscam008#16 = #dscam046#3; #dscam008#52 = #dscam046#4;
            |FINSI;
         |FINSI;
      |SINO;
         |SI #dscam008#31 = 0;
            #dscam008#14 = "L";
            #dscam008#15 = "Recibo liquidado";
         |SINO;
            #dscam008#14 = "C";
            #dscam008#15 = "Parcialmente pagado";
         |FINSI;
      |FINSI;
      |GRABA 020#dscam008.a;
   |FINSI;
   |LIBERA #dscam008;
   |CIERRA #dscam008;

   |SI ModoEntrada = 3;
      |BORRA 020#dscam047.a;
   |FINSI;

|FPRC;

|DEFBUCLE BorraLin;
#dscam047#1;
;
#dscam046#0,001;
#dscam046#0,999;
;
NULL,BorraLin;
|FIN;

|PROCESO BuscaLin;
   |SI #dscam047#12 = "S";
      SwError = 1; |ERROR10;
   |FINSI;
|FPRC;

|DEFBUCLE BuscaLin;
#dscam047#1;
;
#dscam046#0,001;
#dscam046#0,999;
;
NULL,BuscaLin;
|FIN;

|PROCESO Baja; |TIPO 1;
   ModoEntrada = (FEntrada / 100) ! 0;

   |SI ModoEntrada = 2;
      |SI #dscam046#7 = "S"; |O #dscam046#8 = "S";
         |ABRE #dscam051;
         |DDEFECTO #dscam051;
         |LEE 000#dscam051.p;
         |SI FSalida ! 0;
            |DDEFECTO #dscam051;
            |GRABA 020#dscam051.n;
         |FINSI;
         |CIERRA #dscam051;
         |SI #dscam051#1 = "N";
            |CONFI "    NVa a modificar un talon ya contabilizado. ¨ Continuar ?";
         |SINO;
            |PUSHV 0820 1460;
            |BLANCO 0820 1460;
            |CUADRO 0921 1359;
            |ATRI R; |PINTA 1022, "Talon ya contabilizado. Si continua  "; |ATRI N;
            |ATRI R; |PINTA 1122, "se modificaran los asientos afectados"; |ATRI N;
            |ATRI R; |PINTA 1222, "en contabilidad. ¨ Continuar ?       "; |ATRI N;
            E_sup = 1;
            E_inf = "";
            aResp = 1253 ? 1;
            |POPV;
            |SI aResp = "S";
               FSalida = 0;
            |SINO;
               FSalida = 1;
            |FINSI;
         |FINSI;
         |SI FSalida ! 0; |ERROR; |ACABA; |FINSI;
         |HAZ BuscaCierre;
         |SI nSwErr ! 0;
            |MENAV "    La empresa contable esta cerrada. No se puede modificar";
            |ERROR; |ACABA;
         |FINSI;
         eaLlamada = "A";
         |BUCLE BorraLin;
         #dscam046#7 = "N"; |PINTA #dscam046#7;
         #dscam046#8 = "N"; |PINTA #dscam046#8;
      |FINSI;
      eaUsuario = Usuario % 0810;
      eaTipoReg = "TA";  eaNumero = ("00000" + #dscam046#0) % -105;
      eaTipoOp  = "M";   enImpAnt = #dscam046#9;
      eaDescr   = "MODIFICACION TALON (" + (("00000" + #dscam046#0) % -105) + ")";
   |FINSI;

   |SI ModoEntrada = 3;
      eaUsuario = Usuario % 0810;
      eaDescr   = "BAJA TALON (" + (("00000" + #dscam046#0) % -105) + ")";
      eaTipoReg = "TA";  eaNumero = ("00000" + #dscam046#0) % -105;
      eaTipoOp  = "B";   enImpAnt = #dscam046#9; enImpAct = 0;
      |HAZ GrabaAudit;

      |ABRE #dscam008;
      |ABRE #dscam047;
      SwError = 0; |BUCLE BuscaLin;
      |SI SwError = 0;
         eaLlamada = "A";
        |BUCLE BorraLin;
      |FINSI;
      |CIERRA #dscam047;
      |CIERRA #dscam008;
      |SI SwError = 1;
         |MENAV "    Hay vencimientos ya contabilizados. No se dara de baja la ficha";
         |ERROR;
         |ACABA;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO Total; |TIPO 2;
   ModoEntrada = (FEntrada /100) ! 0;
   nFlag = 0 +. 1;

   |SI ModoEntrada = 1;
      eaUsuario = Usuario % 0810;
      eaTipoReg = "TA";  eaNumero = ("00000" + #dscam046#0) % -105;
      eaTipoOp  = "A"; enImpAnt = #dscam046#9; enImpAct = #dscam046#9 + #dscam047#7;
      eaDescr   = "ALTA LINEA TALON (" + (("00000" + #dscam046#0) % -105) + "/" + (("000" + #dscam047#1)% -103) + ")";
      |HAZ GrabaAudit;
   |FINSI;

   |ABRE #dscam008;
   #dscam008#40 = #dscam047#13;
   |LEE 101#dscam008.=;
   |SI FSalida = 0;
      |SI nFlag < 0;
         |SI #dscam008#23 = 0;
            #dscam008#14 = "P";
            #dscam008#15 = "Pendiente de pago";
         |SINO;
            |SI #dscam008#31 = 0;
               #dscam008#14 = "L";
               #dscam008#15 = "Recibo liquidado";
            |SINO;
               #dscam008#14 = "C";
               #dscam008#15 = "Parcialmente pagado";
            |FINSI;
         |FINSI;
         #dscam008#17 = "00000";
         #dscam008#37 = 0;
         #dscam008#39 = "";
         #dscam008#47 = "";

         eaTOperacion = "A"; eaTipoRecibo = "C"; enNumDocum = #dscam047#13; |HAZ ProcesaTempo;
      |SINO;
         #dscam008#17 = #dscam047#0;
         #dscam008#37 = #dscam047#1;
         #dscam008#39 = "T";
         #dscam008#47 = "Talon";
         #dscam008#14 = "E";
         #dscam008#15 = "Englobado en Talon";
         |SI #dscam051#110 = "S";
            #dscam008#16 = #dscam046#3; #dscam008#52 = #dscam046#4;
         |FINSI;
         eaTOperacion = "B"; eaTipoRecibo = "C"; enNumDocum = #dscam047#13; |HAZ ProcesaTempo;
      |FINSI;
      |GRABA 020#dscam008.a;
   |FINSI;
   |LIBERA #dscam008;
   |CIERRA #dscam008;

   #dscam046#5  = #dscam046#5  +. 1;
   #dscam046#9  = #dscam046#9  +. #dscam047#7;
   #dscam046#17 = #dscam046#17 +. #dscam047#18;
   |SI #dscam047#6 < #dscam046#1;
      #dscam046#13 = #dscam046#13 +. #dscam047#7;
   |FINSI;
   |PINTA #dscam046#5;
   |PINTA #dscam046#9;
   |PINTA #dscam046#13;

   |SI #dscam046#9 = 0;
      CodProv = ""; CtaProv = "";
   |SINO;
      |SI CodProv = ""; |Y CtaProv = "";
         CodProv = #dscam008#32; CtaProv = #dscam008#5;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO RellenaTempo;
   |SI #dscam008#14 ! "P"; |Y #dscam008#14 ! "C"; |ACABA; |FINSI;
   |SI #dscam008#16 ! #dscam046#3; |Y #dscam046#24 = "S"; |ACABA; |FINSI;

   enSwClas   = 1; eaFichClas = "dscam046"; eaTipoClas = #dscam008#12;
   |HAZ CompruebaClas;
   |SI eaFichClas = "ERROR"; |ACABA; |FINSI;

   enNumDocumento = #dscam008#40;
   enEmpGestion = #dscam008#46; |HAZ MiraSiVisado;
   |SI enSwVisado < 0; |ACABA; |FINSI;

   Divisa = #dscam008#59; |HAZ TomaDecim;

   |DDEFECTO #dscaw001;
   #dscaw001#0 = Linea;
   Linea = Linea + 1;
   #dscaw001#1 = #dscam008#27;
   #dscaw001#2 = #dscam008#0;
   #dscaw001#3 = #dscam008#1;
   #dscaw001#4 = #dscam008#2;
   #dscaw001#5 = #dscam008#3;
   #dscaw001#6 = #dscam008#31;
   #dscaw001#7 = " ";
   #dscaw001#8 = #dscam008#40;
   #dscaw001#9 = #dscam008#59;
   #dscaw001#10 = #dscam008#5;
   #dscaw001#11 = #dscam008#32;
   #dscaw001#12 = #dscam008#6;
   #dscaw001#14 = #dscam008#35;
   #dscaw001#15 = #dscam008#36;
   |GRABA 010#dscaw001.n;
|FPRC;

|DEFBUCLE RellenaTempo;
#dscam008#2;
59, 32, 5;
00,"     ",000000,000,#dscam046#18, aDCodProv, aDCtaProv;
99,"zzzzz",999999,999,#dscam046#18, aHCodProv, aHCtaProv;
;
NULL,RellenaTempo;
|FIN;

|RUTINA inf;
   #dscaw001#0 = 1;
   #dscaw001#10 = "";
   #dscaw001#11 = "";
|FRUT;

|RUTINA sup;
   #dscaw001#0 = 99999;
   #dscaw001#10 = "zzzzzzzzzzzz";
   #dscaw001#11 = "zzzzzz";
|FRUT;

|RUTINA inf01a;
   #dscaw001#0 = 1;
   #dscaw001#10 = "";
   #dscaw001#11 = "";
|FRUT;

|RUTINA sup01a;
   #dscaw001#0 = 99999;
   #dscaw001#10 = "zzzzzzzzzzzz";
   #dscaw001#11 = "zzzzzz";
|FRUT;

|RUTINA inf01b;
   #dscaw001#0 = 1;
   #dscaw001#5 = "01.01.0000";
|FRUT;

|RUTINA sup01b;
   #dscaw001#0 = 99999;
   #dscaw001#5 = "31.12.9999";
|FRUT;

|PROCESO VuelcaRecibos;
   |ABRE #dscam008;
   #dscam008#40 = #dscaw001#8;
   |LEE 010#dscam008.=;
   |CIERRA #dscam008;

   Divisa = #dscam008#59; |HAZ TomaDecim;

   |DDEFECTO #dscam047;
   #dscam047#0 = #dscam046#0;
   #dscam047#1 = Linea;         Linea = Linea + 1;
   #dscam047#2 = #dscaw001#1;
   #dscam047#4 = #dscaw001#2;
   #dscam047#3 = #dscaw001#3;
   #dscam047#5 = #dscaw001#4;
   #dscam047#6 = #dscaw001#5;
   #dscam047#13 = #dscaw001#8;

   |SI #dscam051#121 = "N";
      eaCodigo = #dscam008#32; eaCuenta = #dscam008#5; |HAZ MiraSiBloq;
      |SI enSwErr ! 0;
         |MENAV "    Proveedor bloqueado";
         |ERROR; |ACABA;
      |FINSI;
   |SINO;
      || Certificados de contratistas
      eaCliente = #dscam008#32; eaCuenta = #dscam008#5;
      efFechaVto = #dscam046#1;
      enDocumento = #dscam008#40; |HAZ MiraCertContr;
      |SI enSwErr ! 0; |ERROR; |ACABA; |FINSI;
   |FINSI;

   |HAZ RecogeRec;
   |GRABA 020#dscam047.n;
   |LEE 010#dscam047.=;
   |HAZ Total;

   eaTOperacion = "B"; eaTipoRecibo = "C"; enNumDocum = #dscam047#13; |HAZ ProcesaTempo;
|FPRC;

|DEFBUCLE VuelcaRecibos;
#dscaw001#1;
7;
00001,"*";
99999,"*";
;
NULL,VuelcaRecibos;
|FIN;

|PROCESO TalAut;
  |ABRE #dscam051;
  |LEE 000#dscam051.p;
  |SI FSalida ! 0; |DDEFECTO #dscam051; |FINSI;
  |CIERRA #dscam051;

   nAutom = 0; Pagos = 1;
   ModoEntrada = (FEntrada / 100) ! 0;
   |SI ModoEntrada = 1;
      Linea = 1;
      |CONFI "0400N Seleccion automatica ?";
      |SI FSalida = 0;
         #dscaw102#0 = #dscam051#100;
         |PUSHV 0501 2380;
         |PINPA #0#dscaw102; |PINDA #0#dscaw102;
         |ENDATOS #1#dscaw102;
         FEstado = FSalida;
         |POPV;
         |SI FEstado = 0;
            |SI #dscaw102#0 = "G";
               aDCodProv = #dscaw102#1;    aHCodProv = #dscaw102#1;
               aDCtaProv = "            "; aHCtaProv = "zzzzzzzzzzzz";
            |SINO;
               aDCodProv = "      ";       aHCodProv = "zzzzzz";
               aDCtaProv = #dscaw102#2;    aHCtaProv = #dscaw102#2;

               aCuenta = #dscaw102#2;
               nSwErr = 0;
               |SALVA_FICHA #dscam008;
               |BUCLE EntregasPdtes;
               |REPON_FICHA #dscam008;
               |SI nSwErr1 = 1;
                  aAlfa = "    NEste proveedor tiene entregas a cuenta pendientes de saldar. " + &191 + " Continuar ?";
                  |CONFI aAlfa;
                  |SI FSalida ! 0; |PTEC 807; |ACABA; |FINSI;
               |FINSI;
            |FINSI;
            nAutom = 1;
            Comodin = "ta" + Usuario; |NOME_DAT #4 Comodin;
            |DELINDEX #dscaw001;
            |ABRE #dscaw001;
            |ABRE #dscam047;
            Linea = 1;
            eaTipoRecibo = "C";
            |BUCLE RellenaTempo;
            |PUSHV 05012380;
            enSwLineal = 1; enClave = 1;
            |PARA; |SI enSwLineal ! 0; |HACIENDO;
               |SI enClave = 1; enSwLineal = 0; |ENTLINEAL #2#dscaw001,-3,4,17,1,inf01a,sup01a; |FINSI;
               |SI enClave = 2; enSwLineal = 0; |ENTLINEAL #3#dscaw001,-2,4,17,1,inf01b,sup01b; |FINSI;
            |SIGUE;
            FEstado = FSalida;
            |POPV;
            |SI FEstado = 0;
               Linea = 1;
               |BUCLE VuelcaRecibos;
            |FINSI;
            |CIERRA #dscam047;
            |CIERRA #dscaw001;
            nAutom = 0;
         |FINSI;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO Decim1; |TIPO 13;
   Divisa = #dscaw001#9; |HAZ TomaDecim;
|FPRC;

|PROCESO MiraDivisa;
   Divisa = #dscam046#18; |HAZ TomaDecim;
   |SI NombreDiv = "ERROR";
      |MENAV "    Divisa inexistente";
      |ERROR;
      |ACABA;
   |SINO;
      #dscam046#19 = NombreDiv; |PINTA #dscam046#19;
      #dscam046#20 = CambioDivCm;
   |FINSI;
|FPRC;

|PROCESO Decim2; |TIPO 13;
   Divisa = #dscam046#18; |HAZ TomaDecim;
|FPRC;

|PROCESO Busca;
   |SI FSalida = 10;
      PosLibro = 1418; PosSerie = 1421; PosFactu = 1427; PosRecib = 1434;
      |HAZ ConsultaCompras;
      |SI enDocum ! -1;
         #dscam047#13 = enDocum; |PINTA #dscam047#13;
      |SINO;
         |MENAV "    Ficha inexistente"; |ERROR;
      |FINSI;
      SwClave = 1;
   |FINSI;
|FPRC;

|PROCESO TomaMBase;
   |HAZ TomaDecim;
   #dscam046#18 = eaMBase;       |PINTA #dscam046#18;
   #dscam046#19 = eaNombreMBase; |PINTA #dscam046#19;
   |ABRE #dscam051;
   |LEE 000#dscam051.p;
   |SI FSalida ! 0;
      |DDEFECTO #dscam051;
      |GRABA 000#dscam051.c;
   |FINSI;
   |CIERRA #dscam051;
   #dscam046#24 = #dscam051#63;
|FPRC;

|PROCESO BuscaCierre;
   |DFICB aAlfa; |QBF aAlfa;
   |PATH_DAT #16 aAlfa;
   aAlfa = "";

   |ABRE #dscam067; |ABRE #dscam042;
   #dscam067#0  = "TA";
   #dscam067#1  = #dscam046#0;
   #dscam067#2  = 1;
   #dscam067#3  = "";
   #dscam067#11 = 1;
   |LEE 000#dscam067.];
   aAlfa2 = #dscam067#3; |QBF aAlfa2;
   |SI FSalida = 0; |Y #dscam067#0  = "TA"; |Y #dscam067#1  = #dscam046#0;
    |Y #dscam067#2  = 1; |Y aAlfa2 = ""; |Y #dscam067#11 = 1;
      #dscam042#0 = #dscam067#8;
      #dscam042#1 = #dscam067#9;
      |LEE 000#dscam042.=;
      |SI FSalida = 0;
         aAlfa = #dscam042#2; |QBF aAlfa;
      |FINSI;
   |FINSI;
   |CIERRA #dscam067; |CIERRA #dscam042;

   |SI aAlfa = "";
      |MENAV "    El talon debia tener movimientos contables, pero no se han encontrado";
      |ACABA;
   |FINSI;

   enSwError   = 0;
   eaDirConta  = aAlfa; |QBF eaDirConta;
   eaEmpContab = (("00000" + #dscam042#0) % -105) + #dscam042#1;
   |HAZ CompruebaEmpContable;
   |SI enSwError < 0; |Y enSwError > -3;
      nSwErr = -1;
   |SINO;
      nSwErr = 0;
   |FINSI;
|FPRC;

|PROCESO MiraProveedor;
   |ABRE #dscam047;
   #dscam047#0 = #dscam046#0;
   #dscam047#1 = 1;
   |LEE 000#dscam047.];
   |SI FSalida = 0; |Y #dscam047#0 = #dscam046#0;
      |ABRE #dscam008;
      #dscam008#40 = #dscam047#13;
      |LEE 000#dscam008.=;
      |SI FSalida = 0;
         CodProv = #dscam008#32; CtaProv = #dscam008#5;
      |FINSI;
      |CIERRA #dscam008;
   |FINSI;
   |CIERRA #dscam047;
|FPRC;
