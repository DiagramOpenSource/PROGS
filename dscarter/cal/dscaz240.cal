|FICHEROS;
     dscaz240 #0;
     dscam003 #3;
     dscam004 #4;
     dscaw027 #27; ||TMP

     referen@;
|FIN;

|VARIABLES;
     aAlfa = "";
     nCuenta = 0;
     nDCobro = 0;
     nHCobro = 0;
     &enRecibo = 0;
     &enCobro = 0;
     &enSw = 0;
     &eaFichero = "";
|FIN;

|PROCESO Corrige;
     nHCobro = nHCobro + 1;

     |SI #0#1 = "S";
          |ABRE #referen@;
          #referen@#17 = #3#40;
          #referen@#3 = nHCobro;
          |LEE 121#referen@.=;
          |SI FSalida = 0;
               #referen@#7 = 0;
               #referen@#25 = 0;
               |GRABA 020#referen@.a;
          |FINSI;
          |CIERRA #referen@;
     |FINSI;

     enSw = 2;
     enRecibo = #3#40;
     enCobro = nHCobro;
     |PRINT;

     #27#0 = enRecibo;
     |GRABA 000#27n;
|FPRC;

|| Se borran las lineas REM consecutivas dejando la £ltima
|PROCESO Borra;
     enSw = 1;
     enRecibo = #referen@#17;
     enCobro = #referen@#3;
     |PRINT;

     |SI #0#1 = "S";
          |BORRA 020#referen@.a;
     |FINSI;
|FPRC;

|DEFBUCLE Borra;
     #referen@#1002;
     ;
     #3#40, nDCobro;
     #3#40, nHCobro;
     be;
     NULL, Borra;
|FIN;

|PROCESO Cobros;
     |SI #4#5 = "REM";
          |SI nCuenta = 0;
               nDCobro = #4#3;
          |FINSI;
          nHCobro = #4#3 - 1;
          nCuenta = nCuenta + 1;
     |SINO;
          |SI nCuenta > 1;
               |BUCLE Borra;
               |HAZ Corrige;
          |FINSI;
          nCuenta = 0;
     |FINSI;
|FPRC;

|DEFBUCLE Cobros;
     #4#1;
     ;
     #3#40, 01;
     #3#40, 99;
     ;
     NULL, Cobros;
|FIN;

|PROCESO R_ecibos;
     nCuenta = 0;
     |BUCLE Cobros;
     |SI nCuenta > 1;
          |BUCLE Borra;
          |HAZ Corrige;
     |FINSI;
|FPRC;

|DEFBUCLE R_ecibos;
     #3#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, R_ecibos;
|FIN;

|PROCESO T3; |TIPO 3;
     |SI #0#0 ! "SI"; |ACABA; |FINSI;

     aAlfa = Usuario;
     |NOME_DAT #27 aAlfa; |DELINDEX #27;
     eaFichero = aAlfa;

     |DDEF aAlfa;
     aAlfa = aAlfa + "dscam004";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |IMPRE 0;
          |SI FSalida = 0;
               |INFOR "dscaz240";
               |SI FSalida = 0;
                    |ABRE #27;
                    |BUCLE R_ecibos;
                    |CIERRA #27;
                    |PIEINF;
                    |FININF;
               |SINO;
                    |MENAV "0000Falta informe...";
               |FINSI;
               |FINIMP;
          |FINSI;
          |DESCARGA_DEF #referen@;
     |FINSI;

     |SI #0#1 = "S";
          |MENAV "0000Recalcular recibos. Pulse tecla.";
          ||SUB_EJECUTA_NP "dscaz099";
          |HAZ dscaz099;
     |FINSI;

     |DELINDEX #27;
|FPRC;
