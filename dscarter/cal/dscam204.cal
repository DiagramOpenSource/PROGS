|FICHEROS;
   dscam204 #0;

   caconce@ #100;
   &cacuent@ #101;
|FIN;

|VARIABLES;
   &MaxDigit = 0;

   aAlfa  = "";
   aAlfa1 = "";
   aAlfa2 = "";

   nCalc  = 0;

   nTecla = 0;
|FIN;

|PROCESO MiraCuenta;
   nTecla = FSalida;

   || Convierto los puntos de la cuenta contable ...
   aAlfa = #dscam204#0; |QBF aAlfa;
   aAlfa = aAlfa - ".";
   |SI FSalida ! 0;
      |PARA; |SI FSalida ! 0; |HACIENDO;
         nCalc = ((FSalida / 100) ! 0) + 99; aAlfa1 = aAlfa % nCalc;
         nCalc = FSalida + 98;               aAlfa2 = aAlfa % nCalc;
         aAlfa = aAlfa1 + aAlfa2; aAlfa = aAlfa % 0; nCalc = aAlfa;
         nCalc = MaxDigit - nCalc;
         aAlfa = aAlfa1 + ("0" * nCalc) + aAlfa2;

         aAlfa = aAlfa - ".";
      |SIGUE;
      #dscam204#0 = aAlfa; |PINTA #dscam204#0;
   |FINSI;

   |SI nTecla = 10;
      #cacuent@#0 = #dscam204#0;
      |LEE 000#cacuent@.];
      |CONSULTA_CLAVE #1#cacuent@;
      |SI FSalida = 0;
         #dscam204#0 = #cacuent@#0; |PINTA #dscam204#0;
         #dscam204#1 = #cacuent@#2; |PINTA #dscam204#1;
      |FINSI;
      |ERROR; |ACABA;
   |FINSI;

   #cacuent@#0 = #dscam204#0;
   |LEE 000#cacuent@.=;
   |SI FSalida ! 0;
      |MENAV "    No existe la cuenta contable indicada";
      |ERROR; |ACABA;
   |SINO;
      #dscam204#1 = #cacuent@#2; |PINTA #dscam204#1;
   |FINSI;
|FPRC;

|PROCESO MiraConcepto;
   nTecla = FSalida;

   aAlfa = #48#16; |QBF aAlfa;
   aAlfa1 = aAlfa - "contagen\";
   |SI FSalida = 0;
      aAlfa1 = aAlfa - "contagen/";
      |SI FSalida = 0;
         aAlfa1 = aAlfa - "agicont\";
         |SI FSalida = 0;
            aAlfa1 = aAlfa - "agicont/";
            |SI FSalida = 0;
               |MENAV "    No tiene configurada en la ficha de la empresa el path de contabilidad";
               |ERROR; |ACABA;
            |FINSI;
         |FINSI;
      |FINSI;
   |FINSI;

   aAlfa = aAlfa1 + "modelos/";
   aAlfa1 = aAlfa + "def/dsmom030.mas";
   |CARGA_DEF aAlfa1, caconce@;
   |SI FSalida = 0;
      aAlfa1 = aAlfa + "fich/"; |PATH_DAT #caconce@#aAlfa1#;
      |ABRE #caconce@;
      |SI nTecla = 10;
         #caconce@#0 = #dscam204#2;
         |LEE 000#caconce@.];
         |CONSULTA_CLAVE #1#caconce@;
         |SI FSalida = 0;
            #dscam204#2 = #caconce@#0; |PINTA #dscam204#2;
            aAlfa = #caconce@#1; |QBF aAlfa; |PINTA 1129, aAlfa;
         |FINSI;
         |CIERRA #caconce@; |DESCARGA_DEF #caconce@;
         |ERROR; |ACABA;
      |FINSI;
      #caconce@#0 = #dscam204#2;
      |LEE 000#caconce@.=;
      |SI FSalida = 0;
         aAlfa = #caconce@#1; |QBF aAlfa; |PINTA 1129, aAlfa;
      |SINO;
         |MENAV "    Concepto contable inexistente";
         |CIERRA #caconce@; |DESCARGA_DEF #caconce@;
         |ERROR; |ACABA;
      |FINSI;
      |CIERRA #caconce@; |DESCARGA_DEF #caconce@;
   |FINSI;
|FPRC;

|PROCESO MiraTipoDias;
   |SI #dscam204#4 = "L";
      |C_M #dscam204#5, 1, "S";
   |SINO;
      #dscam204#5 = 0;  |PINTA #dscam204#5;
      #dscam204#6 = ""; |PINTA #dscam204#6;
      |C_M #dscam204#5, 1, "N";
   |FINSI;
|FPRC;
