|FICHEROS;
   dscam030 #10;
   dscam023 #11,MANTE;
   dscam003 #12;
   dscam051 #13;
   dscaw113 #14;
   dscam166 #15;
   dscaz001 #16;
   dscaw030 #17;

   dscam980 #20;
   dscam981 #21;
   dscam982 #22;
   dscam014 #23;
   dscam090 #24;

   taglm01@ #30;

   dscaw126 #50,MANTE;
   dscam170 #51;

   &cacuent@ #492;

   canemp1@ #1000;
   agif091@ #1001;
   dsm0130@ #1002;
   gifa024@ #1003;

   visados@ #1005;

   RecibCmp@ #1006;
   canempre@ #1007;
   camaasic@ #1008;
|FIN;

|VARIABLES;
   &eaCodEnlace  = "";
   &eaSerie      = "";
   &enFactura    = 0;
   &enNumDocumento = 0;
   &enEmpGestion = 0;
   &enSwVisado   = 0;
   &efFecha      = @;
   &enEmpContab  = 0;
   &enSwError    = 0;
   &eaDirConta   = "";
   &eaEmpContab  = "";
   &PRMNT_enVer   = 0;
   &efFechaVto   = @;
   &efFechaOri   = @;
   &efFechaApl   = @;
   &enDocumento  = 0;
   &enTimbre     = 0;
   &enImporte    = 0;
   &eaEntidad    = "";
   &eaNumero     = "";
   &eaSesion     = "";
   &efFVtoPag    = @;
   &enImpPag     = 0;
   &eaTipoRecibo = "";
   &enNumDocum   = 0;
   &enAviso      = 0;
   &eaMensRet    = "";
   &eaTOperacion = "";
   &eaVarLog     = "";

   &enChksm      = 0;
   &enSwErr      = 0;

   &eaCuenta     = "";
   &enNivel      = 0;
   &Fichero      = "";
   &Cmp          = "";
   &Tipo         = "";
   &CmpNum       = 0;
   &HayConta     = 0;

   &enExistOblig = 0;
   &TipoCliente  = "";
   &CodCliente   = "";
   &NumDigito    = 0;
   &eaNif        = "";

   &eaDSMAC      = "";
   &eaDirect     = "";
   aAbre         = "";
   aEjecuta      = "";
   nPausa        = 0;
   aOrigen       = "";
   aDestino      = "";
   aIPRemoto     = "";
   aUnidad       = "";
   nSwMD5        = 0;
   aResultado    = "";
   nHandle       = 0;

   aPais         = "";
   aFiltro       = "";
   aModulo       = "";
   aAlfa         = "";
   aAlfa1        = "";
   aAlfa2        = "";
   aAlfa1PLB     = "";
   aAlfaX        = "";
   aAlfa1X       = "";
   aAlfa2X       = "";
   nCalc         = 0;
   nCalc1        = 0;
   nCalc2        = 0;
   nCalcX        = 0;
   nCalc1X       = 0;
   nHandleX      = 0;
   nCont         = 0;
   nSwSal        = 0;
   Tecla         = 0;

   nDesde        = 0;
   nHasta        = 0;
   nTimbre       = 0;
   nEsperas      = 0;

   nBotonOk      = 0;
   nBotonCancel  = 0;

   fFechaCalc    = @;
   fFecha2       = @;
   fFechaX       = @;

   nSwTagl       = 0;
   nSwPrim       = 0;

   nSwCond       = 0;
   nSwBloq       = 0;
   nDocumento    = 0;

   nEmpresa      = 0;
   nPeriodo      = 0;

   nImporte      = 0;
   nCoefi        = 0;

   Comodin       = "";
   HazAviso      = 0;

   nCheck        = 0;

{1}aContiene     = "";
|FIN;

|PROCESO ComprEmpContable;
   |ABRE #dscam030;
   #dscam030#0 = eaCodEnlace;
   |LEE 000#dscam030.=;
   |SI FSalida = 0;
      aAlfa = #dscam030#6; |QBF aAlfa;
      aAlfa = aAlfa + "def/canempre.mas";
      |CARGA_DEF aAlfa, canemp1@;
      |SI FSalida = 0;
         aAlfa = #dscam030#6; |QBF aAlfa;
         aAlfa = aAlfa + "fich/"; |PATH_DAT #canemp1@#aAlfa#;

         |ABRE #canemp1@;
         #canemp1@#2 = #dscam030#4;
         #canemp1@#3 = #dscam030#5;
         |LEE 000#canemp1@.];
         |PARA; |SI FSalida = 0; |Y #canemp1@#2 = #dscam030#4; |HACIENDO;
            aAlfa = efFecha; |QBF aAlfa;
            aAlfa = aAlfa % -104;
            nCalc = aAlfa; nCalc1 = #canemp1@#26;
            |SI nCalc1 > 80;
               nCalc1 = nCalc1 + 1900;
            |SINO;
               nCalc1 = nCalc1 + 2000;
            |FINSI;
            |SI nCalc = nCalc1; nCalc = #canemp1@#3; |SAL; |FINSI;
            |LEE 000#canemp1@.s;
            nCalc = -1;
         |SIGUE;
         |SI nCalc = -1;
            || Compruebo los periodos anteriores al que marca el enlace por si
            ||    hubieramos dado 'la vuelta al marcador'
            |SI #dscam030#5 > 0;
               nCalc = 0;
               #canemp1@#2 = #dscam030#4;
               #canemp1@#3 = 0;
               |LEE 000#canemp1@.];
               |PARA; |SI FSalida = 0; |Y #canemp1@#2 = #dscam030#4; |Y #canemp1@#3 < #dscam030#5; |HACIENDO;
                  aAlfa = efFecha; |QBF aAlfa;
                  aAlfa = aAlfa % -104;
                  nCalc = aAlfa; nCalc1 = #canemp1@#26;
                  |SI nCalc1 > 80;
                     nCalc1 = nCalc1 + 1900;
                  |SINO;
                     nCalc1 = nCalc1 + 2000;
                  |FINSI;
                  |SI nCalc = nCalc1; nCalc = #canemp1@#3; |SAL; |FINSI;
                  |LEE 000#canemp1@.s;
                  nCalc = -1;
               |SIGUE;
            |FINSI;
            |SI nCalc = -1;
               |MENAV "    No existe un periodo contable donde ubicar el movimiento."; |ERROR;
            |FINSI;
         |FINSI;

         |CIERRA #canemp1@; |DESCARGA_DEF #canemp1@;
      |FINSI;
   |FINSI;
   |CIERRA #dscam030;
|FPRC;


|VARIABLES;
   &fFechaFac = @;
   &nCamFPago = 0;
   &enFPago   = 0;
   &fFecVto   = @;

   fFecha     = @;
   fFechaHoy  = @;
   fFecha1    = @;
   fDFecha    = @;
   fHFecha    = @;

   nDiaPago   = 0;
   nDia1      = 0;
   nDia2      = 0;
   nDia3      = 0;
   nDias      = 0;
   nMult      = 0;
   nMesNoPago = 0;

   aMes       = "";
   aDia       = "";
   &eaCliente = "";
   aAno       = "";
   aDFecha    = "";
   aHFecha    = "";
|FIN;

|PROCESO dsm00130;
     aAno = fFecVto; aAno = aAno % -104;
     aDFecha = (("00" + #dsm0130@#2) % -102) +"."+(("00" + #dsm0130@#3) % -102) +"."+aAno;
     aHFecha = (("00" + #dsm0130@#4) % -102) +"."+(("00" + #dsm0130@#5) % -102) +"."+aAno;
     fDFecha = aDFecha;
     fHFecha = aHFecha;
     |SI fFecVto ] fDFecha; |Y fFecVto [ fHFecha;
          fFecha = fHFecha + 1;
          |HAZ DiasFijos;
          fFecVto = fFecha;
     |FINSI;
|FPRC;

|DEFBUCLE dsm00130;
     #dsm0130@#1002;
     ;
     eaCliente, 01;
     eaCliente, 99;
     ;
     NULL, dsm00130;
|FIN;

|PROCESO Cambios;
     fFecha1 = fFecha;
     aDia = fFecha1 % 102;
     nDiaPago = aDia;
|FPRC;

|PROCESO DiasFijos;
     |SI nDia1 = 0;
          |VETE FinFijos;
     |FINSI;
     |HAZ Cambios;
     |SI nDiaPago > nDia1;
          |VETE  Fijos2;
     |FINSI;

  |ET Fijos1;
     |SI nDiaPago = nDia1;
          |VETE FinFijos;
     |FINSI;
     fFecha = fFecha + 1;
     |HAZ Cambios;
     |VETE Fijos1;

  |ET Fijos2;
     |SI nDia2 = 0; |O nDiaPago [ nDia1;
          |VETE Fijos1;
     |FINSI;
     |SI nDiaPago > nDia2;
          |VETE Fijos3;
     |FINSI;
     |SI nDiaPago = nDia2;
          |VETE FinFijos;
     |FINSI;
     fFecha = fFecha + 1;
     |HAZ Cambios;
     |VETE Fijos2;

  |ET Fijos3;
     |SI nDia3 = 0;|O nDiaPago > nDia3; |O nDiaPago [ nDia1; |O nDiaPago [ nDia2;
          |VETE Fijos1;
     |FINSI;
     |SI nDiaPago = nDia3;
         |VETE FinFijos;
     |FINSI;
     fFecha = fFecha + 1;
     |HAZ Cambios;
     |VETE Fijos3;

  |ET FinFijos;
     fFecha1 = fFecha;
|FPRC;

|PROCESO Vencimientos;
   aAlfa = #48#17; |QBF aAlfa;
   aAlfa = aAlfa + "def/agifa091.mas";
   |CARGA_DEF aAlfa, agif091@;
   |SI FSalida = 0;
      aAlfa = #48#17; |QBF aAlfa;
      aAlfa = aAlfa + "def/dsm00130.mas";
      |CARGA_DEF aAlfa, dsm0130@;

      aAlfa = #48#17; |QBF aAlfa;
      aAlfa = aAlfa + "def/agifa024.mas";
      |CARGA_DEF aAlfa, gifa024@;

      aAlfa = #48#17;         |QBF aAlfa;
      aAlfa = aAlfa + "fich/" + (("00000" + #48#15) % -105) + "/"; |QBF aAlfa;
      |PATH_DAT #agif091@#aAlfa#; |ABRE #agif091@;
      |PATH_DAT #dsm0130@#aAlfa#; |ABRE #dsm0130@;
      |PATH_DAT #gifa024@#aAlfa#; |ABRE #gifa024@;

      #gifa024@#0 = eaCliente;
      |LEE 000#gifa024@.=;
      |SI FSalida ! 0; |DDEFECTO #gifa024@; |FINSI;

      aAlfa = enFPago; aAlfa = ("00" + aAlfa) % -102;
      #agif091@#0 = aAlfa;
      |LEE 000#agif091@.=;
      |SI FSalida ! 0; |DDEFECTO #agif091@; |FINSI;

      nMesNoPago = #gifa024@#21;
      |SI #agif091@#5 = "N";
         nMult = 1;
      |SINO;
         nMult = 1000;
      |FINSI;

      nDias = #agif091@#nCamFPago# / nMult;
      |SI nDias ! 0; |O nDia1 ! 0; |O nDia2 ! 0; |O nDia3 ! 0;
          fFecha = fFechaFac + nDias;
          |HAZ DiasFijos;
          aMes = fFecha % 402;
          |SI aMes = nMesNoPago;          ||Si es mes de no pago...
               fFecha = fFecha + .001;
               fFechaFac = fFechaFac + .001;
               fFecha1 = fFecha;
          |FINSI;
          fFecVto = fFecha1;
      |SINO;
          fFecVto = fFechaFac;
      |FINSI;

      |BUCLE dsm00130;

      |CIERRA #gifa024@;  |DESCARGA_DEF #gifa024@;
      |CIERRA #dsm0130@;  |DESCARGA_DEF #dsm0130@;
      |CIERRA #agif091@;  |DESCARGA_DEF #agif091@;
   |FINSI;
|FPRC;


||*******************************************************************************
||*******************************************************************************
||*******************************************************************************
||*******************************************************************************
||*******************************************************************************

|PROCESO MiraSiBloqTagloma;
   aAlfa = #dscam003#80; |QBF aAlfa; aAlfa = ("00" + aAlfa) % -102;
   #taglm01@#0 = aAlfa;
   |LEE 000#taglm01@.=;
   |SI FSalida ! 0; |ACABA; |FINSI;

   enFPago = #dscam003#80; eaCliente = #dscam003#32;
   nCamFPago = #dscam003#2 + 5;
   fFechaFac = #dscam003#4;
   |HAZ Vencimientos;

   |SI #dscam003#14 = "P"; |O #dscam003#14 = "C";
      |SI fFecVto [ fFechaHoy; nSwBloq = 1; |ERROR10; |FINSI;
   |FINSI;
|FPRC;

|PROCESO MiraSiBloqPrimicias;
   |SI #dscam003#30 < 0; |ACABA; |FINSI;
   |SI #dscam003#31 = 0; |ACABA; |FINSI;
   |SI #dscam003#14 ! "P"; |Y #dscam003#14 ! "C"; |ACABA; |FINSI;

   aAlfa = #dscam003#80; |QBF aAlfa; aAlfa = ("00" + aAlfa) % -102;
   #dscam982#0 = aAlfa;
   |LEE 000#dscam982.=;
   |SI FSalida ! 0;
      #dscam980#0 = aAlfa;
      |LEE 000#dscam980.=;
      |SI FSalida ! 0;
         |ACABA;
      |SINO;
         nSwCond = 1;
      |FINSI;
   |SINO;
      nSwCond = 2;
      #dscam980#0 = aAlfa;
      |LEE 000#dscam980.=;
      |SI FSalida = 0; nSwCond = 3; |FINSI;
   |FINSI;

   enFPago = #dscam003#80;
   nCamFPago = #dscam003#2 + 5;
   fFechaFac = #dscam003#4;
   fFechaCalc = #dscam003#4; nCalc = fFechaCalc;
   nCalc = nCalc + #dscam982#2; fFechaCalc = nCalc;

   |SI nSwCond = 1;
      nSwCond = 0;
      |SI #dscam003#3 < fFechaHoy; nSwCond = 1; |FINSI;
   |FINSI;
   |SI nSwCond = 2;
      nSwCond = 0;
      |SI fFechaCalc < fFechaHoy; nSwCond = 1; |FINSI;
   |FINSI;
   |SI nSwCond = 3;
      nSwCond = 0;
      |SI #dscam003#3 < fFechaHoy; |O fFechaCalc < fFechaHoy; nSwCond = 1; |FINSI;
   |FINSI;

   nSwBloq = nSwCond;
|FPRC;

|PROCESO MiraSiBloqueo;
   |SI nSwTagl = 1;
      |DDEF aAlfa; |QBF aAlfa;
      aAlfa = aAlfa + "taglm001.mas";
      |CARGA_DEF aAlfa, taglm01@;
      |SI FSalida = 0;
         |ABRE #taglm01@;
         |HAZ MiraSiBloqTagloma;
         |CIERRA #taglm01@; |DESCARGA_DEF #taglm01@;
      |FINSI;
   |FINSI;
   |SI nSwPrim = 1;
      |ABRE #dscam980; |ABRE #dscam981; |ABRE #dscam982;
      |HAZ MiraSiBloqPrimicias;
      |CIERRA #dscam980; |CIERRA #dscam981; |CIERRA #dscam982;
   |FINSI;
|FPRC;

|DEFBUCLE MiraSiBloqueo;
#dscam003#15;
;
eaCliente, "            ", " ";
eaCliente, "zzzzzzzzzzzz", "z";
;
NULL,MiraSiBloqueo;
|FIN;

|PROCESO MiraBloqueoRiesgo;
   |SALVA_FICHA #dscam003;

   |HAZ EsTagloma;
   |SI FSalida ! 0;
      nSwTagl = 0;
      |HAZ EsPrimicias;
      |SI FSalida ! 0;
         nSwPrim = 0;
         |REPON_FICHA #dscam003;
         |ACABA;
      |SINO;
         nSwPrim = 1;
      |FINSI;
   |SINO;
      nSwTagl = 1;
   |FINSI;

   nSwBloq = 0; fFechaHoy = ~;
   |ABRE #dscam023;
   #dscam023#0 = eaCliente;
   |LEE 000#dscam023.=;
   |SI FSalida = 0; |Y #dscam023#9 = "S";
      |BUCLE MiraSiBloqueo;
      |SI nSwBloq = 0;
         #dscam023#0 = eaCliente;
         |LEE 101#dscam023.=;
         |SI FSalida = 0;
            |SI nSwTagl = 1;
               #dscam023#9 = "A";
               #dscam023#10 = "AUTOMATICO";
            |FINSI;
            |SI nSwPrim = 1;
               #dscam023#9 = "N";
               #dscam023#10 = "";
            |FINSI;
            |GRABA 020#dscam023.a; |LIBERA #dscam023;
         |FINSI;
      |FINSI;
   |FINSI;
   |CIERRA #dscam023;

   |REPON_FICHA #dscam003;
|FPRC;

|PROCESO MiraVisados;
   |LEE 000#dscaw113.p;
   |PARA; |SI FSalida = 0; |HACIENDO;
      #RecibCmp@#40 = #dscaw113#0;
      |LEE 000#RecibCmp@.=;
      |SI FSalida = 0;
         |SI #RecibCmp@#49 ! "S"; |Y #RecibCmp@#69 ! "S";
            #visados@#0 = #RecibCmp@#27;
            #visados@#2 = #RecibCmp@#0;
            #visados@#3 = #RecibCmp@#1;
            |LEE 000#visados@.=;
            |SI FSalida = 0; enSwVisado = 1; |SINO; enSwVisado = -1; |SAL; |FINSI;
            |BORRA 020#dscaw113.a;
         |SINO;
            |SI #RecibCmp@#49 = "S";
               nDocumento = #RecibCmp@#40;

               |SELECT #10#RecibCmp@;
               #RecibCmp@#41 = nDocumento;
               #RecibCmp@#40 = 0;
               |LEE 000#RecibCmp@.];
               |PARA; |SI FSalida = 0; |Y #RecibCmp@#41 = nDocumento; |HACIENDO;
                  #dscaw113#0 = #RecibCmp@#40;
                  |LEE 000#dscaw113.=;
                  |SI FSalida ! 0;
                     |DDEFECTO #dscaw113;
                     #dscaw113#0 = #RecibCmp@#40;
                     #dscaw113#1 = #RecibCmp@#0;
                     #dscaw113#2 = #RecibCmp@#1;
                     |GRABA 020#dscaw113.n;
                  |FINSI;
                  |LEE 000#RecibCmp@.s;
               |SIGUE;
               |SELECT #1#RecibCmp@;

               #RecibCmp@#40 = nDocumento;
               |LEE 000#RecibCmp@.=;

               #dscaw113#0 = nDocumento;
               |LEE 000#dscaw113.=;
               |SI FSalida = 0; |BORRA 020#dscaw113.a; |FINSI;
            |FINSI;

            |SI #RecibCmp@#69 = "S";
               nDocumento = #RecibCmp@#40;
               #RecibCmp@#40 = #RecibCmp@#70;
               |LEE 000#RecibCmp@.=;
               |SI FSalida = 0;
                  #dscaw113#0 = #RecibCmp@#40;
                  |LEE 000#dscaw113.=;
                  |SI FSalida ! 0;
                     |DDEFECTO #dscaw113;
                     #dscaw113#0 = #RecibCmp@#40;
                     #dscaw113#1 = #RecibCmp@#0;
                     #dscaw113#2 = #RecibCmp@#1;
                     |GRABA 020#dscaw113.n;
                  |FINSI;
               |FINSI;
               #RecibCmp@#40 = nDocumento;
               |LEE 000#RecibCmp@.=;

               #dscaw113#0 = #RecibCmp@#40;
               |LEE 000#dscaw113.=;
               |SI FSalida = 0; |BORRA 020#dscaw113.a; |FINSI;
            |FINSI;
         |FINSI;
      |FINSI;
      |LEE 000#dscaw113.p;
   |SIGUE;
|FPRC;

|PROCESO MiraSiVisado;
   |ABRE #dscam051;
   |LEE 000#dscam051.p;
   |SI FSalida ! 0; |DDEFECTO #dscam051; |FINSI;
   |CIERRA #dscam051;

   |SI #dscam051#109 = "S";
      aAlfa = "vi" + Usuario; |NOME_DAT #dscaw113#aAlfa#;
      |DELINDEX #dscaw113; |ABRE #dscaw113;

      |DDEF aAlfa; |QBF aAlfa;
      aAlfa = aAlfa + "dscam008.mas";
      |CARGA_DEF aAlfa, RecibCmp@;
      |SI FSalida = 0;
         |DFICE aAlfa; |QBF aAlfa; |PATH_DAT #RecibCmp@#aAlfa#;
         |ABRE #RecibCmp@;

         ||DBASS aAlfa; |QBF aAlfa;
         ||aAlfa = aAlfa + "dscomer9/def/vilam006.mas";
         aAlfa = #48#16; |QBF aAlfa;
         aAlfa = aAlfa + "def/cavilm01.mas";
         |CARGA_DEF aAlfa, visados@;
         |SI FSalida = 0;
            #RecibCmp@#40 = enNumDocumento;
            |LEE 000#RecibCmp@.=;
            |SI FSalida = 0;
               aAlfa = #48#16; |QBF aAlfa;
               aAlfa = aAlfa + "fich/" + (("00000" + #RecibCmp@#44) % -105) + #RecibCmp@#45 + "/";
               |PATH_DAT #visados@#aAlfa#;
               |ABRE #visados@; |SELECT #2#visados@;

               |DDEFECTO #dscaw113;
               #dscaw113#0 = enNumDocumento;
               #dscaw113#1 = #RecibCmp@#0;
               #dscaw113#2 = #RecibCmp@#1;
               |GRABA 020#dscaw113.n;

               |HAZ MiraVisados;
            |FINSI;
            |SELECT #1#visados@;
            |CIERRA #visados@; |DESCARGA_DEF #visados@;
         |FINSI;
         |CIERRA #RecibCmp@; |DESCARGA_DEF #RecibCmp@;
      |FINSI;
      |CIERRA #dscaw113; |DELINDEX #dscaw113;
   |SINO;
      enSwVisado = 1;
   |FINSI;
|FPRC;

|PROCESO CompruebaEmpContable;
   || Tipos de error devueltos en enSwError :
   ||     0 Empresa disponible
   ||    -1 Hay diario de cierre (empresa contable cerrada)
   ||    -2 No se encuentran los ficheros de la contabilidad
   ||    -3 No se encuentra la empresa de contabilidad indicada
   ||    -4 Empresa bloqueada para la fecha indicada. Se devuelve en efFecha
   ||       la fecha de bloqueo
   aAlfa = eaDirConta + "def/camaasic.mas";
   |CARGA_DEF aAlfa, camaasic@;
   |SI FSalida = 0;
      aAlfa = eaDirConta + "def/canempre.mas";
      |CARGA_DEF aAlfa, canempre@;

      aAlfa = eaDirConta + "fich/"; |PATH_DAT #canempre@#aAlfa#;
      aAlfa = eaEmpContab; |QBF aAlfa; aAlfa = aAlfa % -299; nEmpresa = aAlfa;
      aAlfa = eaEmpContab; |QBF aAlfa; aAlfa = aAlfa % -101; nPeriodo = aAlfa;
      aAlfa = eaDirConta + "fich/" + (("00000" + nEmpresa) % -105) + nPeriodo + "/";
      |PATH_DAT #camaasic@#aAlfa#;

      |ABRE #canempre@; |ABRE #camaasic@; |DDEFECTO #camaasic@;

      #camaasic@#0 = 99;
      #camaasic@#1 = "01.01.0000";
      |LEE 000#camaasic@.];
      |SI FSalida = 0; |Y #camaasic@#0 = 99;
         enSwError = -1;
      |SINO;
         aAlfa = eaDirConta; |QBF aAlfa;
         aAlfa = aAlfa - "contagen";
         |SI FSalida ! 0;
            #canempre@#2 = nEmpresa;
            #canempre@#3 = nPeriodo;
            |LEE 000#canempre@.=;
            |SI FSalida = 0;
               |SI #canempre@#34 ] efFecha; |Y #canempre@#34 ! "01.01.0000";
                  efFecha = #canempre@#34;
                  enSwError = -4;
               |SINO;
                  enSwError = 0;
               |FINSI;
            |SINO;
               enSwError = -3;
            |FINSI;
         |SINO;
            #canempre@#2 = nEmpresa;
            #canempre@#3 = nPeriodo;
            |LEE 000#canempre@.=;
            |SI FSalida = 0;
               enSwError = 0;
            |SINO;
               enSwError = -3;
            |FINSI;
         |FINSI;
      |FINSI;
      |CIERRA #canempre@; |DESCARGA_DEF #canempre@;
      |CIERRA #camaasic@; |DESCARGA_DEF #camaasic@;
   |SINO;
      enSwError = -2;
   |FINSI;
|FPRC;

|PROCESO CogeVersion;
   PRMNT_enVer   = 2;
|FPRC;

|PROCESO BotonOk;
   aAlfa = "    N" + &191 + " Conforme con aceptar el aplazamiento ?";
   |CONFI aAlfa;
   |SI FSalida = 0;
      #dscam170#0 = enDocumento;
      #dscam170#3 = efFechaOri;
      |LEE 101#dscam170.=;
      |SI FSalida ! 0;
         |DDEFECTO #dscam170;
         #dscam170#0 = enDocumento;
         #dscam170#3 = efFechaOri;
         |GRABA 020#dscam170.b;
      |FINSI;
      #dscam170#1 = #dscaw126#1;
      #dscam170#2 = #dscaw126#4;
      #dscam170#5 = #dscaw126#3;
      #dscam170#6 = #dscaw126#5;
      #dscam170#7 = eaEntidad;
      #dscam170#8 = eaNumero;
      #dscam170#9 = efFVtoPag;
      #dscam170#10 = enImpPag;
      |GRABA 020#dscam170.a; |LIBERA #dscam170;
   |FINSI;

   aAlfa = "    N" + &191 + " Emitir la carta de aplazamiento ?";
   |CONFI aAlfa;
   |SI FSalida = 0;
      aAlfa = enDocumento; |QBF aAlfa;
      aAlfa = "dscaz163.tab;" + aAlfa;
      |SUB_EJECUTA aAlfa;
   |FINSI;

   nSwSal = 1; |PTEC 807;
|FPRC;

|PROCESO BotonCancel;
   nSwSal = 1; |PTEC 807;
|FPRC;

|PROCESO Aplaza;
   |PUSHV 0913 2067;
   |BLANCO 0913 1967;

   |ABRE #dscam170;
   #dscam170#0 = enDocumento;
   #dscam170#3 = efFechaOri;
   |LEE 000#dscam170.=;
   |SI FSalida = 0;
      |SI #dscam170#4 = "N";
         #dscaw126#0 = #dscam170#3;
         #dscaw126#1 = #dscam170#1;
         fFecha1 = #dscaw126#0; fFecha2 = #dscaw126#1;
         nCalc1 = fFecha1; nCalc2 = fFecha2;
         nCalc1 = nCalc2 - nCalc1;
         #dscaw126#2 = nCalc1;
         #dscaw126#3 = #dscam170#5;
         #dscaw126#4 = #dscam170#2;
         #dscaw126#5 = #dscam170#6;
      |SINO;
         #dscaw126#0 = efFechaOri;
         #dscaw126#1 = efFechaApl;
         #dscaw126#2 = 0;
         #dscaw126#4 = 0;
         #dscaw126#5 = 0;

         nCalc = #dscaw126#0; nCalc1 = #dscaw126#1;
         nCalc = nCalc1 - nCalc;
         |SI nCalc [ 0; nCalc = 0; |FINSI;
         #dscaw126#2 = nCalc; |PINTA #dscaw126#2;

         |ABRE #dscam014;
         #dscam014#0 = #dscaw126#3;
         |LEE 000#dscam014.=;
         |SI FSalida = 0;
            nCalc = (#dscam014#17 / 100) * enImporte * (#dscaw126#2 / 365);
            #dscaw126#4 = nCalc; |PINTA #dscaw126#4;
         |FINSI;
         |CIERRA #dscam014;
      |FINSI;
   |SINO;
      #dscaw126#0 = efFechaOri;
      #dscaw126#1 = efFechaApl;
      #dscaw126#2 = 0;
      #dscaw126#4 = 0;
      #dscaw126#5 = 0;

      nCalc = #dscaw126#0; nCalc1 = #dscaw126#1;
      nCalc = nCalc1 - nCalc;
      |SI nCalc [ 0; nCalc = 0; |FINSI;
      #dscaw126#2 = nCalc; |PINTA #dscaw126#2;

      |ABRE #dscam014;
      #dscam014#0 = #dscaw126#3;
      |LEE 000#dscam014.=;
      |SI FSalida = 0;
         nCalc = (#dscam014#17 / 100) * enImporte * (#dscaw126#2 / 365);
         #dscaw126#4 = nCalc; |PINTA #dscaw126#4;
      |FINSI;
      |CIERRA #dscam014;
   |FINSI;

   |PINPA #0#dscaw126;
   |SI enTimbre ! 0;
      |PINTA 1615, " Importe timbre ............                        ";
      |CUADRO 1014 1766;
      |C_M #dscaw126#5, 1, "S"; |C_V #dscaw126#5, 1, "S";

      |ABRE #dscam090;
      |LEE 000#dscam090.p;
      |SI FSalida ! 0; |DDEFECTO #dscam090; |FINSI;
      |PARA nCont = 1; |SI nCont [ 60; |HACIENDO nCont = nCont + 3;
         nDesde = nCont; nHasta = nCont + 1; nTimbre = nCont + 2;
         |SI #dscam090#nDesde# [ enTimbre; |Y #dscam090#nHasta# ] enTimbre;
            #dscaw126#5 = #dscam090#nTimbre#;
         |FINSI;
      |SIGUE;
      |CIERRA #dscam090;
   |SINO;
      |C_M #dscaw126#5, 1, "N"; |C_V #dscaw126#5, 1, "N";
   |FINSI;
   |PINDA #0#dscaw126;

   |ABRE #dscam014;
   #dscam014#0 = #dscaw126#3;
   |LEE 000#dscam014.=;
   |SI FSalida ! 0; |DDEFECTO #dscam014; |FINSI;
   |CIERRA #dscam014;
   |PINTA 1426, #dscam014#1;

   |CONTROL_SIMPLE 0, "BOTON,{{206}}Confirma aplazamiento", 1940, 1955, BotonOk;
   nBotonOk     = FSalida;
   |CONTROL_SIMPLE 0, "BOTON,{{207}}Cancelar", 1957, 1965, BotonCancel;
   nBotonCancel = FSalida;

   nSwSal = 0;
   |PARA; |SI nSwSal = 0; |HACIENDO;
      |ENDATOS #1#dscaw126;
   |SIGUE;

   |CIERRA #dscam170;

   |FIN_CONTROL_WINDOWS nBotonOk;
   |FIN_CONTROL_WINDOWS nBotonCancel;
   |POPV;
|FPRC;

|PROCESO BorraRec;
   |ABRE #dscaw030;
   #dscaw030#0 = eaTipoRecibo;
   #dscaw030#1 = enNumDocum;
   |LEE 101#dscaw030.=;
   |SI FSalida = 0;
      Comodin = Usuario; |QBF Comodin; Comodin = ((" "*10) + Comodin) % -110;
      |SI Comodin ! #dscaw030#1;
         enSwError = 0; |BORRA 020#dscaw030.a;
      |SINO;
         eaMensRet = #dscaw030#2; |QBF eaMensRet; enSwError = 1;
      |FINSI;
   |FINSI;
   |LIBERA #dscaw030; |CIERRA #dscaw030;
|FPRC;

|PROCESO IncluyeRec;
   |ABRE #dscaw030;
   #dscaw030#0 = eaTipoRecibo;
   #dscaw030#1 = enNumDocum;
   |LEE 101#dscaw030.=;
   |SI FSalida = 0;
      eaMensRet = #dscaw030#2; |QBF eaMensRet; enSwError = 1;
   |SINO;
      eaMensRet = ""; enSwError = 0;
      |DDEFECTO #dscaw030;
      #dscaw030#0 = eaTipoRecibo;
      #dscaw030#1 = enNumDocum;
      #dscaw030#2 = Usuario;
      Comodin = Usuario % 0810; #dscaw030#3 = Comodin;
      #dscaw030#4 = aModulo;
      #dscaw030#5 = ~;
      |HORA aAlfa1PLB; aAlfa1PLB = aAlfa1PLB % 0105;
      #dscaw030#6 = aAlfa1PLB;
      |GRABA 020#dscaw030.n;
   |FINSI;
   |LIBERA #dscaw030; |CIERRA #dscaw030;
|FPRC;

|PROCESO ConsultaRec;
   |ABRE #dscaw030;
   #dscaw030#0 = eaTipoRecibo;
   #dscaw030#1 = enNumDocum;
   |LEE 000#dscaw030.=;
   |SI FSalida = 0;
      Comodin = Usuario; |QBF Comodin; Comodin = ((" "*10) + Comodin) % -110;
      |SI Comodin ! #dscaw030#1;
         eaMensRet = #dscaw030#2; |QBF eaMensRet; enSwError = 1;
      |SINO;
         eaMensRet = "";          enSwError = 0;
      |FINSI;
   |SINO;
      eaMensRet = "";          enSwError = 0;
   |FINSI;
   |CIERRA #dscaw030;
|FPRC;

|PROCESO ProcesaTempo;
   |MODULO aModulo;
   |SI enSwError < 0;
      HazAviso = 0;
   |SINO;
      HazAviso = 1;
   |FINSI;
   enSwError = 0;
   |SI eaTOperacion = "A"; |HAZ IncluyeRec;  |FINSI;
   |SI eaTOperacion = "B"; |HAZ BorraRec;    |FINSI;
   |SI eaTOperacion = "C"; |HAZ ConsultaRec; |FINSI;
   |SI enSwError = 1; |Y HazAviso = 1;
      aAlfa1 = Usuario;
      |SI aAlfa1 ! eaMensRet;
         |SI enAviso = 0;
            Comodin = "    El recibo esta siendo usado por el usuario " + #dscaw030#3;
            |MENAV Comodin;
         |FINSI;
         |ERROR;
      |SINO;
         enSwError = 0;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO LimpiaTodos;
   eaVarLog = "Eliminando del temporal el documento " + #dscaw030#1;
   |SI #dscaw030#0 = "C"; eaVarLog = eaVarLog + " de compras "; |FINSI;
   |SI #dscaw030#0 = "V"; eaVarLog = eaVarLog + " de ventas  "; |FINSI;
   eaVarLog = eaVarLog + "incluido por el proceso [" + #dscaw030#4 + "]";
   |HAZ AnyadeLog;

   |BORRA 020#dscaw030.a;
|FPRC;

|DEFBUCLE LimpiaTodos;
#dscaw030#1;
2;
" ", 000000001, eaSesion;
"z", 999999999, eaSesion;
be;
NULL, LimpiaTodos;
|FIN;

|PROCESO LimpiaTempo;
   eaSesion = (eaSesion + (" " * 10)) % 0110;

   eaVarLog = "Limpiando recibos marcados como usandose para la sesion [" + eaSesion + "]"; |HAZ AnyadeLog;
   |BUCLE LimpiaTodos;
|FPRC;

|PROCESO ExisteCuenta;
   Tecla = FSalida;

   |SI HayConta = 1;
      Fichero = "Plan Contable"; Cmp = "Cuenta"; Tipo = "C"; |HAZ ObtenDato;
      #cacuent@#CmpNum# = eaCuenta; |HAZ SacaNivel;
      Fichero = "Plan Contable"; Cmp = "Digito"; Tipo = "C"; |HAZ ObtenDato;
      #cacuent@#CmpNum# = enNivel;
      |SI Tecla = 10;
         |ET VuelveC;
         aFiltro = "C";
         |LEE 000#cacuent@.];
         |CONSULTA_CLAVE #1#492;
         |SI FSalida < 0;
            TipoCliente = "ERROR"; |ACABA;
         |SINO;
            Fichero  = "Plan Contable"; Cmp = "Cuenta"; Tipo = "C"; |HAZ ObtenDato;
            eaCuenta = #cacuent@#CmpNum#;
            Fichero  = "Plan Contable"; Cmp = "Digito"; Tipo = "C"; |HAZ ObtenDato;
            enNivel  = #cacuent@#CmpNum#;
         |FINSI;
      |FINSI;
      |LEE 000#cacuent@.=;
      |SI FSalida ! 0;
         |SI eaCuenta ! "            ";
            |SI enExistOblig = 0;
               |MENAV "    Cuenta contable inexistente";
               TipoCliente = "ERROR";
            |FINSI;
         |FINSI;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO AnyadeLog;
   aContiene = "";
   |ESPECIFICA "RBASS", aContiene;
   |DBASE aAlfaX; |QBF aAlfaX;
   aAlfaX = aAlfaX  + "DOCUMENTOS/"; |MKDIR aAlfaX;
   aAlfaX = aAlfaX + "LOGS/";         |MKDIR aAlfaX;

   fFechaX = ~; aAlfa1X = fFechaX;
   aAlfa1X = (aAlfa1X % 0704) + (aAlfa1X % 0402) + (aAlfa1X % 0102);
   |HORA aAlfa2X; aAlfa2X = aAlfa2X % 0102; aAlfa1X = aAlfa1X + aAlfa2X;
   aAlfaX = aAlfaX + "log" + aAlfa1X + ".txt";

   |XABRE "E", aAlfaX, nHandleX;
   |SI FSalida ] 0;
      |XABRE "A", aAlfaX, nHandleX;
   |SINO;
      |XABRE "W", aAlfaX, nHandleX;
   |FINSI;

   aAlfa2X = Usuario % 0810 + "[" + Usuario + "] ";
   |MODULO aAlfa1X; |QBF aAlfa1X;
   |HORA aAlfaX;    |QBF aAlfaX;
   aAlfaX = aAlfaX + "  " + aAlfa2X + " " + aAlfa1X + "  " + eaVarLog; |XGRABA nHandleX, aAlfaX;

   |XCIERRA nHandleX;
|FPRC;

|PROCESO SacaUnidadLocal;
     aContiene = "";
     |ESPECIFICA "RBASS", aContiene;
     aAlfa = aContiene; aAlfa1 = "";
     aAlfa2 = aAlfa % 0101; aAlfa = aAlfa % 0299;
     |PARA; |SI aAlfa2 ! "/"; |Y aAlfa2 ! "\"; |Y aAlfa ! ""; |HACIENDO;
        aAlfa1 = aAlfa1 + aAlfa2;
        aAlfa2 = aAlfa % 0101; aAlfa = aAlfa % 0299;
     |SIGUE;

     aUnidad = aAlfa1;
|FPRC;

|PROCESO MD5OrigDest;
     nSwMD5 = 1;
     aContiene1 = aDestino; |ESPECIFICA "REMOTOMD5", aContiene;
     aAlfa = FSalida;  |QBF aAlfa;

     aContiene1 = aOrigen;  |ESPECIFICA "MD5", aContiene;
     aAlfa1 = FSalida;  |QBF aAlfa1;

     |SI aAlfa ! aAlfa1; nSwMD5 = 0; |FINSI;
|FPRC;

|PROCESO Ejecucion;
     |RFBORRA aAbre;

     |RSYSTEM aEjecuta;

     |XABRE "EC", aAbre, nHandle;
     |SI FSalida < 0;
         aEjecuta = "START "+ aDestino + " ";
         aEjecuta = aEjecuta + aAbre + &34;
         |RSYSTEM aEjecuta;

         |PARA;  |SI;  |HACIENDO;
                 |PARA nPausa = 1; |SI nPausa [ 10000; |HACIENDO nPausa = nPausa + 1;
                 |SIGUE;

                 |XABRE "EC", aAbre, nHandle;
                 |SI FSalida ] 0; |SAL; |FINSI;

                 |PULSATECLA;
         |SIGUE;
     |FINSI;

     aResultado = "";
     |XABRE "CU", aAbre, nHandle;
     |SI FSalida < 0;   |ACABA;  |FINSI;

     |XLEE nHandle, 250, aResultado;
     |SI FSalida < 0; aResultado = ""; |FINSI;
     |XCIERRA nHandle;

     |QBF aResultado;
|FPRC;

|PROCESO CopiaMD5;
     aIPRemoto = ""; |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;
     |HAZ MD5OrigDest;

     |SI nSwMD5 = 0;
         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aOrigen, aDestino;
         |SINO;
             |COPIA_FICHERO aOrigen, aDestino;
         |FINSI;
     |FINSI;

     |XABRE "EC", aDestino, nHandle;
     |SI FSalida < 0;
         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aOrigen, aDestino;
         |SINO;
             |COPIA_FICHERO aOrigen, aDestino;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO RecogeMac;
     |SI eaDSMAC ! "";  |ACABA;  |FINSI;

     aUnidad = ""; |HAZ SacaUnidadLocal;
     aAlfa = aUnidad + "\DOCUMENTOS";  |RMKDIR aAlfa;
     aAlfa = aAlfa + "\DSCARTER";      |RMKDIR aAlfa;

     |DBASE aAlfa;   |QBF aAlfa;
     aOrigen  = aAlfa  + "plugins/dsmac.exe";
     aDestino = aAlfa1 + "\DOCUMENTOS\DSCARTER\DSMAC.EXE";
     aAbre    = aAlfa1 + "\DOCUMENTOS\DSCARTER\DSMAC.TXT";
     |HAZ CopiaMD5;

     aEjecuta = "cmd " + &34 + "/c START /MIN /WAIT " + aDestino + " ";
     aEjecuta = aEjecuta + aAbre + &34;
     |HAZ Ejecucion; eaDSMAC = aResultado;
|FPRC;

|PROCESO RecogeDirectorio;
     aUnidad = ""; |HAZ SacaUnidadLocal;
     aAlfa = aUnidad + "\DOCUMENTOS";  |RMKDIR aAlfa;
     aAlfa = aAlfa + "\DSCARTER";      |RMKDIR aAlfa;

     |SI eaDirect = ""; eaDirect = aUnidad; |FINSI;

     aAlfa = aAlfa + "\DSSELECT.INI";
     |XABRE "CW", aAlfa, nHandle;
     |SI FSalida ] 0;
        aAlfa = eaDirect; |QBF aAlfa;
        aAlfa1 = aAlfa % -101;
        |SI aAlfa1 = "/"; |O aAlfa1 = "\";
           aAlfa = aAlfa % -299;
        |FINSI;
        aAlfa = &34 + aAlfa + &34 + " " + aUnidad + "\DOCUMENTOS\DSCARTER\DSSELECT.TXT C";
        |XGRABA nHandle, aAlfa;
        |XCIERRA nHandle;
     |FINSI;

     |DBASE aAlfa;   |QBF aAlfa;
     aOrigen  = aAlfa  + "plugins/dsselect.exe";
     aDestino = aUnidad + "\DOCUMENTOS\DSCARTER\DSSELECT.EXE";
     aAbre    = aUnidad + "\DOCUMENTOS\DSCARTER\DSSELECT.TXT";
     |HAZ CopiaMD5;

     aEjecuta = "cmd " + &34 + "/c START /WAIT " + aDestino + " ";
     aEjecuta = aEjecuta + aUnidad + "\DOCUMENTOS\DSCARTER\DSSELECT.INI" + &34;
     |HAZ Ejecucion; eaDirect = aResultado;
|FPRC;
