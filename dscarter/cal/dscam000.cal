|FICHEROS;
   dscam000 #0;
   canempr@ #1;
   agif041@ #2;

   dscam051 #3;
|FIN;

|VARIABLES;
   &Fichero = "";
   &Tipo    = "";
   &Cmp     = "";
   &CmpNum  = 0;

   Comodin  = "";
   Comodin1 = "";
   aOrigen  = "";
   aDestino = "";
   aAlfa    = "";
   aAlfa1   = "";
   aAlfa2   = "";

   Ref1     = "";
   Ref2     = "";
   Sistema  = "";

   confi    = "";
   Cont     = 0;
   ModoEntrada = 0;

   Tecla    = 0;
   nHandle  = 0;
   nChequea = 0;
|FIN;

|PROCESO CargaContaExt;
   |SI FSalida = 2; |ACABA; |FINSI;

   Comodin = #dscam000#16; |QBF Comodin;
   |SI Comodin = ""; |SI Campo ! 16; |ACABA; |FINSI; |FINSI;
   Comodin = Comodin % -101;
   |SI Comodin ! "/"; |Y Comodin ! "\";
      Comodin = #dscam000#16;
      |QBF Comodin;
      Comodin = Comodin + "/";
      #dscam000#16 = Comodin;
      |PINTA #dscam000#16;
   |FINSI;

   Comodin = #dscam000#16; |QBF Comodin;
   aAlfa = Comodin; |QBF aAlfa;
   ||Comodin = Comodin + "def/canempre.mas";
   Fichero = "Empresas"; Tipo = "C"; Cmp = ""; |HAZ ObtenFich;
   Comodin = Comodin + "def/" + Fichero + ".mas";
   |CARGA_DEF Comodin , canempr@;
   |SI FSalida ! 0;
      |SI aAlfa = "/";
         #dscam000#14 = 0; |C_M #dscam000#14,1,"N"; |PINTA #dscam000#14;
         #dscam000#19 = 0; |C_M #dscam000#19,1,"N"; |PINTA #dscam000#19;
         #dscam000#16 = ""; |PINTA #dscam000#16;
      |SINO;
         |C_M #dscam000#14,1,"S"; |C_M #dscam000#19,1,"S";
         |MENAV "    Problemas con el fichero de empresas de contabilidad ";
         |ERROR;
         Ref1 = "N";
      |FINSI;
   |SINO;
      |C_M #dscam000#14,1,"S"; |C_M #dscam000#19,1,"S";
      Ref1 = "S";
   |FINSI;
|FPRC;

|PROCESO CargaGestExt;
   |SI FSalida = 2; |ACABA; |FINSI;

   Comodin = #dscam000#17; |QBF Comodin;
   |SI Comodin = "";
      #dscam000#15 = "  "; |PINTA #dscam000#15;
      |C_M #dscam000#15,1,"N";
      |ACABA;
   |SINO;
      |C_M #dscam000#15,1,"S";
   |FINSI;

   Comodin = Comodin % -101;
   |SI Comodin ! "/"; |Y Comodin ! "\";
      Comodin = #dscam000#17;
      |QBF Comodin;
      Comodin = Comodin + "/";
      #dscam000#17 = Comodin;
      |PINTA #dscam000#17;
   |FINSI;

   Comodin = #dscam000#17; |QBF Comodin;
   ||Comodin = Comodin + "def/agifa041.mas";
   Fichero = "Empresas"; Tipo = "G"; Cmp = ""; |HAZ ObtenFich;
   Comodin = Comodin + "def/" + Fichero + ".mas";
   |CARGA_DEF Comodin , agif041@;
   |SI FSalida ! 0;
      |MENAV "    Problemas con el fichero de empresas de gestion comercial ";
      |ERROR;
      Ref2 = "N";
   |SINO;
      Ref2 = "S";
   |FINSI;
|FPRC;

|PROCESO DescargaExternos;
   |SI Ref1 = "S";
      Comodin = #dscam000#16;
      |QBF Comodin;
      |SI Comodin ! ""; |DESCARGA_DEF #1; |FINSI;
   |FINSI;
   |SI Ref2 = "S";
      Comodin = #dscam000#17;
      |QBF Comodin;
      |SI Comodin ! ""; |DESCARGA_DEF #2; |FINSI;
   |FINSI;
|FPRC;

|PROCESO ListaConta;
   Tecla = FSalida;
   |SI Tecla = 2; |ACABA; |FINSI;
   Comodin = #dscam000#16; |QBF Comodin;
   |SI Comodin = ""; |ACABA; |FINSI;
   Comodin = Comodin + "fich/";
   |PATH_DAT #1 Comodin;
   |ABRE #canempr@;
   |SI Tecla = 9;
      |CONSULTA_CLAVE #1#canempr@;
      |SI FSalida ] 0;
         Fichero = "Empresas"; Tipo = "C"; Cmp = "Codigo Empresa"; |HAZ ObtenDato;
         #dscam000#14 = #canempr@#CmpNum#
         Fichero = "Empresas"; Tipo = "C"; Cmp = "Periodo"; |HAZ ObtenDato;
         #dscam000#19 = #canempr@#CmpNum#;
         |PINTA #dscam000#14;
         |PINTA #dscam000#19;
      |FINSI;
   |SINO;
      Fichero = "Empresas"; Tipo = "C"; Cmp = "Codigo Empresa"; |HAZ ObtenDato;
      #canempr@#CmpNum# = #dscam000#14;
      Fichero = "Empresas"; Tipo = "C"; Cmp = "Periodo"; |HAZ ObtenDato;
      #canempr@#CmpNum# = #dscam000#19;
      |LEE 000#canempr@.=;
   |FINSI;

   |SI FSalida = 0;
      |SI confi = "C"; |O confi = "c";
         Fichero = "Empresas"; Tipo = "C"; Cmp = "Nombre Comercial"; |HAZ ObtenDato;
         #dscam000#1  = #canempr@#CmpNum#;
         Fichero = "Empresas"; Tipo = "C"; Cmp = "Nif/Cif"; |HAZ ObtenDato;
         #dscam000#2  = #canempr@#CmpNum#;
         Fichero = "Empresas"; Tipo = "C"; Cmp = "Direccion"; |HAZ ObtenDato;
         #dscam000#3  = #canempr@#CmpNum#;
         Fichero = "Empresas"; Tipo = "C"; Cmp = "Codigo Postal"; |HAZ ObtenDato;
         Comodin = #canempr@#CmpNum# % 0102;
         #dscam000#8  = Comodin;
         Comodin = #canempr@#CmpNum# % 0303;
         #dscam000#9  = Comodin;
         Fichero = "Empresas"; Tipo = "C"; Cmp = "Poblacion"; |HAZ ObtenDato;
         #dscam000#10 = #canempr@#CmpNum#;
         Fichero = "Empresas"; Tipo = "C"; Cmp = "Telefono"; |HAZ ObtenDato;
         #dscam000#12 = #canempr@#CmpNum#;
         Fichero = "Empresas"; Tipo = "C"; Cmp = "Fax"; |HAZ ObtenDato;
         #dscam000#13 = #canempr@#CmpNum#;
         |PARA Cont = 1; |SI Cont [ 13; |HACIENDO Cont = Cont + 1;
            |PINTA #dscam000#Cont#;
         |SIGUE;
      |FINSI;
   |SINO;
      |ERROR;
   |FINSI;

   |CIERRA #canempr@;
|FPRC;

|PROCESO ListaGest;
   Tecla = FSalida;
   Comodin = #dscam000#17; |QBF Comodin;
   |SI Comodin = ""; |ACABA; |FINSI;
   Comodin = Comodin + "fich/";
   |PATH_DAT #2 Comodin;
   |ABRE #agif041@;
   |SI Tecla = 9;
      |CONSULTA_CLAVE #1#agif041@;
      |SI FSalida ] 0;
          Fichero = "Empresas"; Tipo = "G"; Cmp = "Codigo Empresa"; |HAZ ObtenDato;
          #dscam000#15 = #agif041@#CmpNum#;
          |PINTA #dscam000#15;
      |FINSI;
   |SINO;
      Fichero = "Empresas"; Tipo = "G"; Cmp = "Codigo Empresa"; |HAZ ObtenDato;
      #agif041@#CmpNum# = #dscam000#15;
      |LEE 000#agif041@.=;
   |FINSI;

   |SI FSalida = 0;
      |SI confi = "G"; |O confi = "g";
         Fichero = "Empresas"; Tipo = "G"; Cmp = "Razon Social"; |HAZ ObtenDato;
         #dscam000#1  = #agif041@#CmpNum#;
         Fichero = "Empresas"; Tipo = "G"; Cmp = "Nif/Cif"; |HAZ ObtenDato;
         #dscam000#2  = #agif041@#CmpNum#;
         Fichero = "Empresas"; Tipo = "G"; Cmp = "Direccion"; |HAZ ObtenDato;
         #dscam000#3  = #agif041@#CmpNum#;
         #dscam000#8  = "00";
         #dscam000#9  = "000";
         Fichero = "Empresas"; Tipo = "G"; Cmp = "Poblacion"; |HAZ ObtenDato;
         #dscam000#10 = #agif041@#CmpNum#;
         Fichero = "Empresas"; Tipo = "G"; Cmp = "Provincia"; |HAZ ObtenDato;
         #dscam000#11 = #agif041@#CmpNum#;
         |PARA Cont = 1; |SI Cont [ 13; |HACIENDO Cont = Cont + 1;
            |PINTA #dscam000#Cont#;
         |SIGUE;
      |FINSI;
   |SINO;
      |ERROR;
   |FINSI;
   |CIERRA #agif041@;
|FPRC;

|PROCESO Comienzo; |TIPO 8;
   |HAZ CargaContaExt;
   |HAZ CargaGestExt;
|FPRC;

|PROCESO Final; |TIPO 9;
   |HAZ DescargaExternos;
|FPRC;

|PROCESO CreaDir; |TIPO 3;
   ModoEntrada = (FEntrada / 100) ! 0;
   |SI ModoEntrada ! 1; |ACABA; |FINSI;

   |DBASE Comodin; |QBF Comodin;
   Comodin = Comodin + "fich/" + (("00000" + #dscam000#0) % -105) + "/";
   |MKDIR Comodin;
   |HAZ CopiaPatrones;
|FPRC;

|PROCESO BorraDir; |TIPO 2;
   ModoEntrada = (FEntrada / 100) ! 0;

   |SI ModoEntrada = 3;
      Comodin = ""; |DIRECTORIO Comodin;
      |DBASE Comodin1; |QBF Comodin1;
      Comodin1 = Comodin1 + "fich/" + (("00000" + #dscam000#0) % -105);
      Comodin = Comodin + "bin/agirm -r " + Comodin1;
      |SYSTEM Comodin;
   |FINSI;
|FPRC;

|PROCESO TraeDatos;
   ModoEntrada = (FEntrada / 100) ! 0;

   |SI ModoEntrada = 1;
      |MENSA "2402Importar datos desde <C>Contabilidad <G> Gestion <N>Ninguno: ";
      E_sup = 1;
      E_inf = "CGNcgn";
      |AVISO;
      confi = 2463 ? 0;
      |SI confi = "C"; |O confi = "c"; |O confi = "G"; |O confi = "g";
         |PARA Cont = 1; |SI Cont [ 13; |HACIENDO Cont = Cont + 1;
            |C_M #dscam000#Cont#,1,"N";
         |SIGUE;
      |SINO;
         |PARA Cont = 1; |SI Cont [ 13; |HACIENDO Cont = Cont + 1;
            |C_M #dscam000#Cont#,1,"S";
         |SIGUE;
      |FINSI;
   |SINO;
      confi = "N";
   |FINSI;
|FPRC;

|PROCESO CopiaPatrones;
     |DBASE aOrigen;  |QBF aOrigen;  aOrigen = aOrigen + "fich/patrones/*.*";
     |_PDIR aOrigen;
     |PARA; |SI FSalida = 0; |HACIENDO;
        Comodin1 = aOrigen % -112; Comodin1 = Comodin1 % 0108;
        Comodin = aOrigen % -112;
        Comodin = Comodin - "ddd"; |SI FSalida ! 0; Comodin = Comodin + "dat"; |FINSI;
        Comodin = Comodin - "iii"; |SI FSalida ! 0; Comodin = Comodin + "ixx"; |FINSI;
        Comodin = Comodin - "zzz"; |SI FSalida ! 0; Comodin = Comodin + "ddx"; |FINSI;
        |SI Comodin1 = "dscam063"; |O Comodin1 = "dscam064";
            |DBASE aDestino; |QBF aDestino; aDestino = aDestino + "fich/";
            aAlfa2 = aDestino + "/" + Comodin;
            nChequea = 1;
        |SINO;
            |DFICE aDestino;
            aAlfa2 = aDestino + (("00000" + #dscam000#0) % -105) + "/" + Comodin;
            nChequea = 1;
        |FINSI;

        |SI nChequea = 1;
           |XABRE "E", aAlfa2, nHandle;
           |SI FSalida < 0;
              |QBF aDestino; aDestino = aDestino + (("00000" + #dscam000#0) % -105) + "/" + Comodin;
              |COPIA_FICHERO aOrigen, aDestino;
           |FINSI;
        |SINO;
           |QBF aDestino; aDestino = aDestino + (("00000" + #dscam000#0) % -105) + "/" + Comodin;
           |COPIA_FICHERO aOrigen, aDestino;
        |FINSI;
        |_SDIR aOrigen;
     |SIGUE;
|FPRC;
