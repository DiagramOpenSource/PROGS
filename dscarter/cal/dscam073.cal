|FICHEROS;
   dscam073 #0;
   dscam014 #1;

   paises@ #100;
|FIN;

|VARIABLES;
 NombreP = "";
 NifP    = "";
 EntP    = "";
 SucP    = "";
 CtaP    = "";
 DirP    = "";
 Cp1P    = "";
 Cp2P    = "";
 PobP    = "";
 modo    = 0;
 vale    = 0;
 alfa1   = "";

 aAlfa  = "";
 aAlfa1 = "";
 nCalc  = 0;
 nHandle = 0;

 NifProv    = "";
 NombreProv = "";
 DirecProv  = "";
 PoblaProv  = "";
 ProviProv  = "";
 Cp1Prov    = "";
 Cp2Prov    = "";
 TelefProv  = "";
 FaxProv    = "";
 PaisProv   = "";
 varios     = 0;

 nSwCarga_Def = 0;
|FIN;

|| *************************************************************************

|PROCESO tipo8; |TIPO 8;
  NombreP = #48#1;
  NifP    = #48#2;
  EntP    = "0000";
  SucP    = "0000";
  CtaP    = "0000000000";
  DirP    = #48#3;
  Cp1P    = #48#8;
  Cp2P    = #48#9;
  PobP    = #48#10;
|FPRC;

|PROCESO RegDirec; |TIPO 0;
 alfa1 = #0Campo;
 |SI alfa1 = "N";
    #dscam073#11 = "";  #dscam073#16 = "";
    #dscam073#12 = "N"; #dscam073#13 = "N"; #dscam073#14 = "N";
    #dscam073#17 = "N"; #dscam073#18 = "N"; #dscam073#19 = "N";
    |PINTA #dscam073#11; |PINTA #dscam073#12; |PINTA #dscam073#13;
    |PINTA #dscam073#14; |PINTA #dscam073#16; |PINTA #dscam073#17;
    |PINTA #dscam073#18; |PINTA #dscam073#19;
 |FINSI;
 |C_M #dscam073#11,1,alfa1;
 |C_M #dscam073#12,1,alfa1;
 |C_M #dscam073#13,1,alfa1;
 |C_M #dscam073#14,1,alfa1;
 |C_M #dscam073#16,1,alfa1;
 |C_M #dscam073#17,1,alfa1;
 |C_M #dscam073#18,1,alfa1;
 |C_M #dscam073#19,1,alfa1;
|FPRC;

|PROCESO defban; |TIPO 7;
 modo = (FEntrada / 100) ! 0;
 |SI modo ! 1; |ACABA; |FINSI;
 |DDEFECTO #0;
 #dscam073#2 = NifP;      #dscam073#7  = DirP;
 #dscam073#3 = NombreP;   #dscam073#8  = Cp1P;
 #dscam073#4 = EntP;      #dscam073#9  = Cp2P;
 #dscam073#5 = SucP;      #dscam073#10 = PobP;
 #dscam073#6 = CtaP;
 |PINDA #0#dscam073;
|FPRC;

|PROCESO banco; |TIPO 0;
 modo = (FEntrada / 100) ! 0;

 |SI FSalida = 9;       || Para Consultar Ficha Banco
     |PUSHV 05012380;
     |ABRE #dscam014;
     |PINPA #0#dscam014;
     #dscam014#0 = #dscam073#0;
     |CONSULTA #0#dscam014;
     vale = FSalida;
     |CIERRA #dscam014;
     |POPV;
     |SI vale = 0;
         #dscam073#0 = #dscam014#0; |PINTA #dscam073#0;
     |SINO;
         |ERROR; |ACABA;
     |FINSI;
 |FINSI;

 |SI modo ! 1; |ACABA; |FINSI;
 |ABRE #dscam014;
 |DDEFECTO #dscam014;
 #dscam014#0 = #dscam073#0;
 |LEE 001#dscam014.=;
 |SI FSalida = 0;
     #dscam073#1  = #dscam014#1;       |PINTA #dscam073#1;
     #dscam073#4  = #dscam014#34;      |PINTA #dscam073#4;
     #dscam073#5  = #dscam014#35;      |PINTA #dscam073#5;
     #dscam073#6  = #dscam014#37;      |PINTA #dscam073#6;
     #dscam073#22 = #dscam014#36;      |PINTA #dscam073#22;
 |FINSI;
 |CIERRA #dscam014;
|FPRC;

|PROCESO CargaDef;
   nSwCarga_Def = 0;
   aAlfa = #48#16; |QBF aAlfa;
   aAlfa = aAlfa - "contagen";
   |SI FSalida ! 0;
      nCalc = ((FSalida / 100) ! 0) + 99;
      aAlfa = aAlfa % nCalc;
      aAlfa = aAlfa + "integral/";
      aAlfa1 = aAlfa + "def/dsam0104.mas";
      |XABRE "E", aAlfa1, nHandle;
      |SI FSalida ] 0;
         |CARGA_DEF aAlfa1, paises@;
         |SI FSalida = 0;
            nSwCarga_Def = 1;
            aAlfa1 = aAlfa + "fich/"; |PATH_DAT #paises@#aAlfa1#;
            |ABRE #paises@;
         |FINSI;
      |SINO;
         aAlfa = aAlfa - "integral/";
         aAlfa = aAlfa + "basica/";
         aAlfa1 = aAlfa + "def/agim0016.mas";
         |XABRE "E", aAlfa1, nHandle;
         |SI FSalida ] 0;
            |CARGA_DEF aAlfa1, paises@;
            |SI FSalida = 0;
               nSwCarga_Def = 1;
               aAlfa1 = aAlfa + "fich/"; |PATH_DAT #paises@#aAlfa1#;
               |ABRE #paises@;
            |FINSI;
         |SINO;
            aAlfa = #48#17; |QBF aAlfa;
            aAlfa1 = aAlfa + "def/dsm00225.mas";
            |XABRE "E", aAlfa1, nHandle;
            |SI FSalida ] 0;
               |CARGA_DEF aAlfa1, paises@;
               |SI FSalida = 0;
                  nSwCarga_Def = 1;
                  aAlfa1 = aAlfa + "fich/" + (("00000"+#48#15)%-105) + "/";
                  |PATH_DAT #paises@#aAlfa1#;
                  |ABRE #paises@;
               |FINSI;
            |FINSI;
         |FINSI;
      |FINSI;
   |SINO;
      aAlfa = #48#17; |QBF aAlfa;
      aAlfa1 = aAlfa + "def/dsm00225.mas";
      |XABRE "E", aAlfa1, nHandle;
      |SI FSalida ] 0;
         |CARGA_DEF aAlfa1, paises@;
         |SI FSalida = 0;
            nSwCarga_Def = 1;
            aAlfa1 = aAlfa + "fich/" + (("00000" + #48#15) % -105) + "/";
            |PATH_DAT #paises@#aAlfa1#;
            |ABRE #paises@;
         |FINSI;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO Descarga_Def;
   |SI nSwCarga_Def = 1; |DESCARGA_DEF #paises@; |FINSI;
|FPRC;

|PROCESO PaisesT66; |TIPO 66;
   |HAZ CargaDef;
   |SI nSwCarga_Def = 1;
      |CONSULTA_CLAVE #1#paises@;
      |SI FSalida = 0;
         #dscam073#27 = #paises@#0; |PINTA #dscam073#27;
         #dscam073#28 = #paises@#1; |PINTA #dscam073#28;
      |FINSI;
      |HAZ Descarga_Def;
   |FINSI;
|FPRC;

|PROCESO PaisesT00;
   |HAZ CargaDef;
   |SI nSwCarga_Def = 1;
      #paises@#0 = #dscam073#27;
      |LEE 000#paises@.=;
      |SI FSalida = 0;
         #dscam073#28 = #paises@#1; |PINTA #dscam073#28;
      |SINO;
         |MENAV "    Pais no encontrado"; |ERROR;
      |FINSI;
      |HAZ Descarga_Def;
   |FINSI;
|FPRC;
