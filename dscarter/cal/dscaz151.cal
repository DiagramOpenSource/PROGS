|FICHEROS;
   dscaz151 #0;

   dscam008 #1;
   dscam009 #2;
   dscaw059 #3,MANTE;
   dscam051 #10;
   dscam030 #11;

   dscaw013 #50;

   &agif024@ #100;
   &agif324@ #101;
|FIN;

|VARIABLES;
   &eaFichClas = "";  &eaTipoClas = ""; &enSwClas = 0;

   &Fichero   = "";  &Cmp = "";     &Tipo = ""; &CmpNum = 0;
   &aInforme  = "";
   &nDeci_Base = 0;  &nDeci_Div = 0; &Divisa = "";
   &CambioDivVn = 0; &CambioDivCm = 0; &NombreDiv = "";
   &MaxDigit = 0;    &MonedaConta = "";
   &HayGestion = 0;  &HayConta = 0;
   &DCampoG = 0;  &HCampoG = 0;  &DCampoC = 0;  &HCampoC = 0;

   &CodCliente = "";  &TipoCliente = ""; &CtaCliente = ""; &Nombre = "";

   &enEmpCartera = 0; &enEmpGestion = 0; SwError = 0;
   &eaSerie = ""; &eaCliGest = ""; &nImporte = 0; &eaTipoOper = "";

   Comodin = "";
   aParte1 = ""; nParte1 = 0;
   aParte2 = ""; nParte2 = 0;
   aLong   = "";
   aCodEnl = "";

   nLong       = 0;
   Calc        = 0;
   NumEntregas = 0;
   ModoEntrada = 0;
   NumDoc      = 0;
   Linea       = 0;
|FIN;

|PROCESO MiraCodigo;

   Comodin = #dscaz151#1; |QBF Comodin;
   Comodin = ("000000" + Comodin) % -106; #dscaz151#1 = Comodin; |PINTA #dscaz151#1;
   DCampoG = 0; HCampoG = 2;
   CodCliente = #dscaz151#1; TipoCliente = "G"; |HAZ ExisteCliente;

   |SI TipoCliente = "ERROR"; |ERROR; |ACABA; |FINSI;

   |SI Campo = 1;
      Fichero = "Clientes"; Cmp = "Codigo Cliente"; Tipo = "G";
      |HAZ ObtenDato; #dscaz151#1 = #agif024@#CmpNum#; |PINTA #dscaz151#1;
   |SINO;
      Fichero = "Clientes/Proveedores"; Cmp = "Cuenta Contable"; Tipo = "C";
      |HAZ ObtenDato; #dscaz151#2 = #agif024@#CmpNum#; |PINTA #dscaz151#2;
   |FINSI;
   |SI Campo = 1;
      Fichero = "Clientes"; Cmp = "Nombre Cliente";  Tipo = "G"; |HAZ ObtenDato;
   |SINO;
      Fichero = "Clientes/Proveedores"; Cmp = "Razon Social"; Tipo = "C";
   |FINSI;
   |HAZ ObtenDato; #dscaz151#3 = #agif024@#CmpNum#; |PINTA #dscaz151#3;

   |SI Campo = 1;
      Fichero = "Clientes"; Cmp = "Divisa Defecto"; Tipo = "G"; |HAZ ObtenDato;
      |HAZ ObtenDato; #dscaz151#6 = #agif024@#CmpNum#; |PINTA #dscaz151#6;
   |SINO;
      |SI MonedaConta = "P";
         #dscaz151#6 = "ESP";
      |SINO;
         #dscaz151#6 = "EUR";
      |FINSI;
   |FINSI;
   Divisa = #dscaz151#6; |HAZ TomaDecim;
   |SI NombreDiv = "ERROR";
      |MENAV "    Divisa inexistente"; |ERROR;
   |SINO;
      #dscaz151#17 = NombreDiv; |PINTA #dscaz151#17;
      #dscaz151#7 = CambioDivVn;
   |FINSI;
|FPRC;

|PROCESO TomaDivisa;
   |SI #dscaz151#6 ! Contenido;
      Divisa = #dscaz151#6; |HAZ TomaDecim;
      |SI NombreDiv = "ERROR";
         |MENAV "    Divisa inexistente"; |ERROR;
      |SINO;
         |ATRI I; |PINTA 2120, NombreDiv; |ATRI N;
         #dscaz151#7 = CambioDivVn;
      |FINSI;
   |FINSI;
|FPRC;

|RUTINA inf041;
   #dscaz151#0 = 001;
|FRUT;

|RUTINA sup041;
   #dscaz151#0 = 999;
|FRUT;

|PROCESO Volcado;

   |LEE 000#dscam008.u;
   NumDoc = #dscam008#40 + 1;
   |DDEFECTO #dscam008;
   #dscam008#40 = NumDoc;
   |GRABA 020#dscam008.b;

   Fichero = "Clientes"; Cmp = "Codigo Cliente"; Tipo = "G"; |HAZ ObtenDato;
   #agif024@#CmpNum# = #dscaz151#1;
   |LEE 000#agif024@.=;
   |SI FSalida = 0; |Y #agif024@#CmpNum# ! "      ";
      Fichero = "Clientes"; Cmp = "Cuenta Contable"; Tipo = "G"; |HAZ ObtenDato;
      #dscam008#5  = #agif024@#CmpNum#;
      Fichero = "Clientes"; Cmp = "Nombre Cliente";  Tipo = "G"; |HAZ ObtenDato;
      #dscam008#6  = #agif024@#CmpNum#;
      Fichero = "Clientes"; Cmp = "Banco Domiciliacion";  Tipo = "G"; |HAZ ObtenDato;
      #dscam008#7  = #agif024@#CmpNum#;
      Fichero = "Clientes"; Cmp = "Entidad Bancaria"; Tipo = "G"; |HAZ ObtenDato;
      #dscam008#8  = #agif024@#CmpNum#;
      Fichero = "Clientes"; Cmp = "Sucursal Bancaria"; Tipo = "G"; |HAZ ObtenDato;
      #dscam008#9  = #agif024@#CmpNum#;
      Fichero = "Clientes"; Cmp = "DC Cuenta Bancaria"; Tipo = "G"; |HAZ ObtenDato;
      #dscam008#10 = #agif024@#CmpNum#;
      Fichero = "Clientes"; Cmp = "Num.Cuenta Bancaria"; Tipo = "G"; |HAZ ObtenDato;
      #dscam008#11 = #agif024@#CmpNum#;
   |FINSI;
   #dscam008#12 = #dscaz151#16;    || Tipo recibo
   #dscam008#22 = #dscaz151#8;     || Importe Recibo (Divisa)
   #dscam008#60 = #dscaz151#8 * #dscaz151#7;   || Importe Recibo (Base)
   #dscam008#30 = #dscaz151#8;     || Total Recibo (Divisa)
   #dscam008#64 = #dscaz151#8 * #dscaz151#7;   || Total Recibo (Base)
   |SI #dscaz151#10 = "S";
      #dscam008#23 = #dscaz151#8;     || Total Liquidado (Divisa)
      #dscam008#61 = #dscaz151#8 * #dscaz151#7;   || Total Liquidado (Base)
   |SINO;
      #dscam008#23 = 0;               || Total Liquidado (Divisa)
      #dscam008#61 = 0;               || Total Liquidado (Base)
   |FINSI;
   #dscam008#0  = #dscaz151#19
   #dscam008#57 = #dscaz151#6;
   #dscam008#58 = NombreDiv;
   #dscam008#50 = #dscaz151#9;     || Cuenta Entrega
   #dscam008#44 = #48#14;          || Empresa Contable
   #dscam008#45 = #48#19;          || Periodo Contable
   #dscam008#46 = #48#15;          || Empresa Gestion
   #dscam008#59 = #dscaz151#7;     || Cambio Divisa
   #dscam008#32 = #dscaz151#1;     || Codigo Cliente
   #dscam008#4  = #dscaz151#4;     || Fecha de emision
   #dscam008#47 = "S";             || Entrega a cuenta S/N
   #dscam008#59 = #dscaz151#7;     || Cambio Actual Divisa
   |SI #dscaz151#10 = "N";
      #dscam008#70 = #dscaz151#13;    || Numero talon entrega
      #dscam008#72 = #dscaz151#11;    || Entidad talon entrega
      #dscam008#71 = #dscaz151#14;    || Numero talon entrega
      #dscam008#3  = #dscaz151#14;    || Fecha de vencimiento
      #dscam008#31 = #dscam008#30;    || Total pendiente Divisa
      #dscam008#65 = #dscam008#64;    || Total pendiente Base
      #dscam008#78 = #dscam008#30;    || Total entrega Divisa
      #dscam008#79 = #dscam008#64;    || Total entrega Base
      #dscam008#14 = "p";
      #dscam008#15 = "Entrega pdte.cobro";
      #dscam008#90 = "S";             || Preparado
   |SINO;
      #dscam008#14 = "e";
      #dscam008#15 = "Pendiente de compensar";
   |FINSI;
   |GRABA 020#dscam008.a; |LIBERA #dscam008;

   #dscam009#17 = #dscam008#40;
   #dscam009#3 = 1;
   |LEE 000#dscam009.];
   |SI FSalida = 0; |Y #dscam009#17 = #dscam008#40;
      |PARA; |SI FSalida = 0; |Y #dscam009#17 = #dscam008#40; |HACIENDO;
         Linea = #dscam009#3 + 1;
         |LEE 000#dscam009.s;
      |SIGUE;
   |SINO;
      Linea = 1;
   |FINSI;

   |DDEFECTO #dscam009;
   #dscam009#17 = #dscam008#40;
   #dscam009#3  = Linea;
   |GRABA 020#dscam009.b;
   #dscam009#0 = #dscam008#0;
   #dscam009#1 = #dscam008#1;
   #dscam009#2 = #dscam008#2;
   #dscam009#4 = #dscaz151#9;
   #dscam009#5 = "RAC";
   #dscam009#6 = #dscaz151#4;
   #dscam009#7 = 0; #dscam009#25 = 0;
   |SI #dscaz151#10 = "S";
      #dscam009#8 = #dscaz151#8; #dscam009#26 = #dscaz151#5;
   |FINSI;
   #dscam009#15 = #dscam008#27;
   Comodin = "Entrega a cuenta";
   #dscam009#16 = Comodin;
   #dscam009#42 = #dscaz151#9;
   |GRABA 020#dscam009.a; |LIBERA #dscam009;

   eaTipoOper = "+5";     nImporte = #dscam008#64;
   enEmpCartera = -1;     enEmpGestion = -1;
   eaSerie = #dscam008#0; eaCliGest = #dscam008#32;
   |HAZ BucleRiesgo;

   |SI #dscam051#1 = "S"; |Y HayConta = 1; |Y aCodEnl ! "";
      Comodin = Usuario; |QBF Comodin; Comodin = "ac" + Comodin;
      |NOME_DAT #50 Comodin;
      |DELINDEX #dscaw013; |ABRE #dscaw013;
      #dscaw013#15 = aCodEnl;      #dscaw013#0  = 1;
      #dscaw013#13 = "dscam009";   #dscaw013#14 = 17;
      #dscaw013#5 = #dscam009#17;  #dscaw013#6  = #dscam009#17;
      #dscaw013#11 = "N";          #dscaw013#12 = 10;
      |GRABA 020#dscaw013.n; |LIBERA #dscaw013;
      #dscaw013#15 = aCodEnl;      #dscaw013#0  = 2;
      #dscaw013#13 = "dscam009";   #dscaw013#14 = 3;
      #dscaw013#5  = #dscam009#3;  #dscaw013#6  = #dscam009#3;
      #dscaw013#11 = "N";          #dscaw013#12 = 3;
      |GRABA 020#dscaw013.n; |LIBERA #dscaw013;
      Comodin = aCodEnl; |QBF Comodin;
      Comodin = "dscam033.tab;" + Comodin;
      |LIBERA #dscam008; |CIERRAT #dscam008;
      |LIBERA #dscam009; |CIERRAT #dscam009;
      |SUB_EJECUTA_NP Comodin;
      |CIERRA #dscaw013; |DELINDEX #dscaw013;
      |ABRET #dscam008;  |LEE 101#dscam008.=;
      |ABRET #dscam009;  |LEE 101#dscam009.=;
   |FINSI;
|FPRC;

|DEFBUCLE Volcado;
#dscaz151#1;
;
INICIO;
FINAL;
;
NULL,Volcado;
|FIN;

|PROCESO Baja; |TIPO 1;
   ModoEntrada = (FEntrada / 100) ! 0;
   |SI ModoEntrada = 3; NumEntregas = NumEntregas - 1; |FINSI;
|FPRC;

|PROCESO Alta; |TIPO 3;
   #dscaz151#5 = #dscaz151#8 * #dscaz151#7;
   ModoEntrada = (FEntrada / 100) ! 0;
   |SI ModoEntrada = 1; NumEntregas = NumEntregas + 1; |FINSI;
|FPRC;

|PROCESO PuntoCuenta; |TIPO 6;
   Comodin = #dscaz151#9;
   Comodin = Comodin - ".";
   |PARA; |SI FSalida ! 0; |HACIENDO;
      nParte1 = 100 + (((FSalida / 100) ! 0) - 1);
      nParte2 = ((((FSalida / 100) ! 0) + 1) * 100) + 99;
      aParte1 = #dscaz151#9 % nParte1;
      aParte2 = #dscaz151#9 % nParte2;
      |QBF aParte1; |QBF aParte2;
      Comodin = aParte1 + aParte2;
      aLong = Comodin % 0; nLong = aLong;
      nLong = MaxDigit - nLong;
      aLong = "0" * nLong;
      Comodin = aParte1 + aLong + aParte2;
      #dscaz151#9 = Comodin;
      |PINTA #dscaz151#9;
      Comodin = Comodin - ".";
   |SIGUE;
   Comodin = #dscaz151#9; |QBF Comodin;
   aLong = Comodin % 0; nLong = aLong;
   |SI nLong ! MaxDigit;
      |MENAV "    Cuenta fuera de nivel";
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO MiraImporte;
   |SI #dscaz151#8 [ 0; |ERROR; |FINSI;
|FPRC;

|PROCESO MiraTR;
   enSwClas   = 1; eaFichClas = "dscaz151"; eaTipoClas = #dscaz151#16;
   |HAZ CompruebaClas;
   |SI eaFichClas = "ERROR";
      |MENAV "    Documento inoperable en el perfil actual "; |ERROR;
   |FINSI;
|FPRC;

|PROCESO TomaDatos;
   |SI #dscaz151#10 = "N";
      |PUSHV 0501 2380;
      |PINPA #0#dscaw059; |PINDA #0#dscaw059;
      |ENDATOS #1#dscaw059;
      |SI FSalida = 0;
         #dscaz151#11 = #dscaw059#0;
         #dscaz151#12 = #dscaw059#1;
         #dscaz151#13 = #dscaw059#2;
         #dscaz151#14 = #dscaw059#3;
         #dscaz151#15 = #dscaw059#4;
      |SINO;
         |ERROR;
      |FINSI;
      |POPV;
   |SINO;
      #dscaz151#11 = "";
      #dscaz151#12 = "";
      #dscaz151#13 = "";
      #dscaz151#14 = "01.01.0000";
      #dscaz151#15 = "";
   |FINSI;
|FPRC;

|PROCESO BuscaNum;
   SwError = 1;
|FPRC;

|DEFBUCLE BuscaNum;
#dscam008#7;
;
#dscaw059#0,#dscaw059#2,000000001;
#dscaw059#0,#dscaw059#2,999999999;
;
NULL,BuscaNum;
|FIN;

|PROCESO MiraTalon;
   |SI FSalida = 2; |ACABA; |FINSI;
   SwError = 0;
   |BUCLE BuscaNum;
   |SI SwError = 1;
      |MENAV "    El numero de talon/pagare ya existe";
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO PideEnlace;
   |SI #dscam051#61 = "N";
      aCodEnl = #dscam051#73; |ACABA;
   |FINSI;
   |PUSHV 2022 2357; |BLANCO 2022 2357;
   |CUADRO 2123 2356;
   |ATRI R; |PINTA 2124, " Introduzca un codigo de enlace "; |ATRI N;

   |MENSA "    <F2> Consulta enlaces ";
   |ABRE #dscam030; |DDEFECTO #dscam030;
   #dscam030#0 = #dscam051#73;
   |LEE 000#dscam030.=;
   |SI FSalida ! 0; |DDEFECTO #dscam030; |FINSI;
   aCodEnl = #dscam030#0;

|ET OtraVez;
   |PINTA 2225, #dscam030#0; |PINTA 2230, #dscam030#1;
   E_sup = 4; E_inf = "";
   aCodEnl = 2225 ? 1;
   |SI FSalida = 0;
      #dscam030#0 = aCodEnl;
      |LEE 000#dscam030.=;
      |SI FSalida = 0;
         |PINTA 2225, #dscam030#0; |PINTA 2230, #dscam030#1;
         |CONFI "    NEs correcto ? ";
         |SI FSalida ! 0; aCodEnl = ""; |FINSI;
      |SINO;
         |MENAV "    No se encuentra la ficha ";
         |DDEFECTO #dscam030; |VETE OtraVez;
      |FINSI;
   |SINO;
      |SI FSalida = 10;
         #dscam030#0 = aCodEnl;
         |LEE 000#dscam030.];
         |CONSULTA_CLAVE #1#dscam030;
         |SI FSalida = 0;
            aCodEnl = #dscam030#0;
         |FINSI;
         |VETE OtraVez;
      |SINO;
         aCodEnl = "";
      |FINSI;
   |FINSI;
   |CIERRA #dscam030;
   |MENSA "     ";
   |POPV;
|FPRC;

|PROGRAMA;
   |HAZ Tipo8;

   |ABRE #dscam051;
   |LEE 000#dscam051.p;
   |SI FSalida ! 0;
      |DDEFECTO #dscam051;
      |GRABA 020#dscam051.c;
   |FINSI;

   |CLS;
   |CABEZA "Introduccion de entregas a cuenta";
   |PINPA #0#0; |PINDA #0#0;
   Comodin = "ea" + Usuario; |NOME_DAT #0 Comodin;
   |DELINDEX #dscaz151; |ABRE #dscaz151;
   NumEntregas = 0;
   |ENTLINEAL #1#dscaz151,-1,1,17,1,inf041,sup041;
   |SI NumEntregas > 0;
      |SI HayConta = 1; |HAZ PideEnlace; |QBF aCodEnl; |FINSI;
      |ABRE #dscam008; |ABRE #dscam009;
      |CONFI "    N¨ Desea volcar los datos introducidos ?";
      |SI FSalida = 0; |BUCLE Volcado; |FINSI;
      |CIERRA #dscam008; |CIERRA #dscam009;
   |FINSI;
   |CIERRA #dscaz151;

   |CIERRA #dscam051;
   |HAZ Tipo9;
|FPRO;
