|FICHEROS;
   dscaz980 #0;
   dscam980 #1;
   dscam981 #2;
   dscam982 #3;

   dscam003 #10;
   dscam023 #11,MANTE;

   agif091@ #100;
   dsm0130@ #130;
|FIN;

|VARIABLES;
   &nCamFPago = 0;
   &enFPago   = 0;
   &fFechaFac = @;
   &fFecVto   = @;
   &eaCliente   = "";
   fFecha     = @;
   HFecha     = @;
   fFechaHoy  = @;
   fFechaCalc = @;

   nSwCond    = 0;

   nCalc      = 0;
   nCalc1     = 0;
   &nFPago    = 0;

   aAlfa      = "";
   aAntCli    = "zzzzz";
|FIN;

|PROCESO Comprueba;
   |SI aAntCli = #dscam003#32; |ACABA; |FINSI;
   |SI #dscam003#30 < 0; |ACABA; |FINSI;
   |SI #dscam003#31 = 0; |ACABA; |FINSI;
   |SI #dscam003#14 ! "P"; |Y #dscam003#14 ! "C"; |Y #dscam003#14 ! "I"; |ACABA; |FINSI;

   aAlfa = #dscam003#80; |QBF aAlfa; aAlfa = ("00" + aAlfa) % -102;
   #dscam982#0 = aAlfa;
   |LEE 000#dscam982.=;
   |SI FSalida ! 0;
      #dscam980#0 = aAlfa;
      |LEE 000#dscam980.=;
      |SI FSalida ! 0;
         |ACABA;
      |SINO;
         nSwCond = 1;
      |FINSI;
   |SINO;
      nSwCond = 2;
      #dscam980#0 = aAlfa;
      |LEE 000#dscam980.=;
      |SI FSalida = 0; nSwCond = 3; |FINSI;
   |FINSI;

   enFPago = #dscam003#80; eaCliente = #dscam003#32;
   nCamFPago = #dscam003#2 + 5;
   fFechaFac = #dscam003#4;
   fFechaCalc = #dscam003#4; nCalc = fFechaCalc;
   nCalc = nCalc + #dscam982#2; fFechaCalc = nCalc;

   |SI nSwCond = 1;
      nSwCond = 0;
      |SI #dscam003#3 < fFechaHoy; nSwCond = 1; |FINSI;
   |FINSI;
   |SI nSwCond = 2;
      nSwCond = 0;
      |SI fFechaCalc < fFechaHoy; nSwCond = 1; |FINSI;
   |FINSI;
   |SI nSwCond = 3;
      nSwCond = 0;
      |SI #dscam003#3 < fFechaHoy; |O fFechaCalc < fFechaHoy; nSwCond = 1; |FINSI;
   |FINSI;

   |SI nSwCond = 1;
      #dscam023#0 = #dscam003#32;
      |LEE 000#dscam023.=;
      |SI FSalida = 0;
         |SI #dscam023#9 = "N"; |O #dscam023#9 = "A";
            aAntCli    = #dscam003#32;
            #dscam023#9 = "S";
            aAlfa = #0#0; |QBF aAlfa;
            aAlfa = (aAlfa % 0102) + (aAlfa % 0402) + (aAlfa % 0902);
            aAlfa = "BLOQ.AUTO.(" + aAlfa + ")";
            #dscam023#10 = aAlfa;
            |GRABA 020#dscam023.a;

            #dscam981#0 = #0#0;
            #dscam981#1 = #dscam003#32;
            |LEE 000#dscam981.=;
            |SI FSalida ! 0;
               |DDEFECTO #dscam981;
               #dscam981#0 = #0#0;
               #dscam981#1 = #dscam003#32;
               #dscam981#2 = #dscam003#6;
               |GRABA 020#dscam981.n;
            |FINSI;
         |FINSI;
      |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE Comprueba;
#dscam003#3;
;
"01.01.0000", 000000001;
HFecha, 999999999;
;
NULL,Comprueba;
|FIN;

|PROGRAMA;
   #0#0 = ~;
   |SI PARAMETRO ! "";
      |CLS;
      |CABEZA " BLOQUEO AUTOMATICO DE RIESGOS DE CLIENTE ";
      |PINPA #0#0; |PINDA #0#0;
      |ENDATOS #1#0;
      |SI FSalida ! 0; |ACABA; |FINSI;
   |FINSI;

   |ABRE #dscam023; |ABRE #dscam980; |ABRE #dscam981; |ABRE #dscam982;
   HFecha = #dscaz980#0; nCalc = HFecha;
   nCalc = nCalc - 1; HFecha = nCalc; fFechaHoy = #dscaz980#0;
   |BUCLE Comprueba;
   |CIERRA #dscam982; |CIERRA #dscam981; |CIERRA #dscam980; |CIERRA #dscam023;
|FPRO;
