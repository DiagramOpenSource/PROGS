|FICHEROS;
   dscaz117 #0;

   dscaw090 #10;
   dscam003 #11;
   dscam008 #12;
   dscam021;
   dscam022;
   dscam100 #13;
   dscam102 #14;
   dscam058 #15;
   dscam051 #16;

   agifa024@ #100;
   agifa023@ #101;
   agifa019@ #102;
   dsm00013@ #103;
   agifa066@ #104;
   agifa091@ #105;
|FIN;

|VARIABLES;
   &aInforme = "";
   &nDeci_PreVis = 0;
   &Divisa = ""; &nDeci_Div = 0; &nDeci_Base = 0;
   &enContLin = 0; &enSw = 0;
   nSwAnt = 0;

   fFecha = @;

   aAlfa      = "";
   aAlfa1     = "";
   aCliente   = "";
   aFormaPago = "";

   nSwError   = 0;
   nNumLineas = 0;
   nLins      = 0;
   nNumLins   = 0;
   nCalc      = 0;
   nCont      = 0;
   nNume      = 0;
   nVenc      = 0;
   nPIva      = 0;
   nPRec      = 0;

   nDia       = 0;
   nDia1      = 0;
   nDia2      = 0;
   nDia3      = 0;

   nImpLin    = 0;
   nNeto      = 0;
   nTFactura  = 0;
   nLinNeto   = 0;
   nIva       = 0;
   nDocumento = 0;

   aTipoImp   = "";
|FIN;

|PROCESO MiraContaV;
     aAlfa = "1";
     |ABRE #dscam022;
     |SELECT #2#dscam022;
     #dscam022#13 = #dscam008#40;
     #dscam022#14 = 0;
     |LEE 000#dscam022.];
     |SI FSalida = 0; |Y #dscam022#13 = #dscam008#40;
          |ABRE #dscam021;
          #dscam021#0 = #dscam022#0;
          |LEE 000#dscam021.=;
          |SI FSalida = 0; |Y #dscam021#8 = "S";
               aAlfa = "0";
          |FINSI;
          |CIERRA #dscam021;
     |FINSI;
     |CIERRA #dscam022;
     FSalida = aAlfa;
|FPRC;

|PROCESO MiraFPago;
   nSwError = 1;
   |SELECT #2#dscam102;
   #dscam102#0 = "";
   #dscam102#1 = aFormaPago;
   |LEE 000#dscam102.];
   |PARA; |SI FSalida = 0; |Y #dscam102#1 = aFormaPago; |HACIENDO;
      |SI #dscam102#0 = #dscaz117#14; nSwError = 0; |SAL; |FINSI;
      |LEE 000#dscam102.s;
   |SIGUE;
   |SELECT #1#dscam102;
|FPRC;

|PROCESO MiraCliente;
   nSwError = 1;
   |SELECT #2#dscam100;
   #agifa024@#0 = aCliente;
   |LEE 000#agifa024@.=;
   |SI FSalida = 0;
      #dscam100#0 = "";
      #dscam100#1 = #agifa024@#158;
      |LEE 000#dscam100.];
      |PARA; |SI FSalida = 0; |Y #dscam100#1 = #agifa024@#158; |HACIENDO;
         |SI #dscam100#0 = #dscaz117#13; nSwError = 0; |SAL; |FINSI;
         |LEE 000#dscam100.s;
      |SIGUE;
   |FINSI;
   |SELECT #1#dscam100;
|FPRC;

|PROCESO TempoVentas;
    |SI #dscam051#104 = "S";
       |SI #dscam003#70 ! "          "; |O #dscam003#72 ! "    ";
          |SI #dscam003#71 < #dscaz117#9; |O #dscam003#71 > #dscaz117#10; |ACABA; |FINSI;
       |SINO;
          |SI #dscam003#3 < #dscaz117#9; |O #dscam003#3 > #dscaz117#10; |ACABA; |FINSI;
       |FINSI;
    |FINSI;

    aCliente = #dscam003#32; |HAZ MiraCliente;
    |SI nSwError = 1; |ACABA; |FINSI;

/*
    aFormaPago = #dscam003#80; |HAZ MiraFPago;
    |SI nSwError = 1; |ACABA; |FINSI;
*/

    |SI #dscam003#14 ! "C"; |Y #dscam003#14 ! "P"; |Y #dscam003#14 ! "I";
       |ACABA;
    |FINSI;

    Divisa = #dscam003#57; |HAZ TomaDecim;

    #dscam058#0 = #dscam003#12;
    |LEE 000#dscam058.=;
    |SI FSalida = 0; |DDEFECTO #dscam058; |FINSI;

    |DDEFECTO #dscaw090;
    #dscaw090#0 = #dscam003#40;
    #dscaw090#1 = "C";
    #dscaw090#2 = #dscam003#32;
    #dscaw090#3 = #dscam003#6;
    #dscaw090#4 = #dscam003#0;
    #dscaw090#5 = #dscam003#1;
    #dscaw090#6 = #dscam003#2;
    #dscaw090#7 = #dscam003#12;
    #dscaw090#8 = #dscam058#1;
    #dscaw090#9 = #dscam003#4;
    #dscaw090#10 = #dscam003#3;
    ||#dscaw090#11 = #dscam003#30;
    #dscaw090#11 = #dscam003#31;
    |GRABA 020#dscaw090.n;
|FPRC;

|DEFBUCLE TempoVentas;
#dscam003#11;
;
"      ",#dscaz117#9 ,#dscaz117#11;
"zzzzzz",#dscaz117#10,#dscaz117#12;
;
NULL,TempoVentas;
|FIN;

|DEFBUCLE TempoVentasb;
#dscam003#11;
;
"      ","01.01.0000" ,#dscaz117#11;
"zzzzzz","31.12.9999" ,#dscaz117#12;
;
NULL,TempoVentas;
|FIN;

|PROCESO TempoCompras;
    |SI #dscam051#108 = "S";
       |SI #dscam008#73 ! "01.01.0000";
          |SI #dscam008#73 < #dscaz117#9; |O #dscam008#73 > #dscaz117#10; |ACABA; |FINSI;
       |SINO;
          |SI #dscam008#3 < #dscaz117#9; |O #dscam008#3 > #dscaz117#10; |ACABA; |FINSI;
       |FINSI;
    |FINSI;

    |SI #dscam008#14 ! "C"; |Y #dscam008#14 ! "P"; |Y #dscam008#14 ! "I";
          |Y #dscam008#14 ! "E";
       |ACABA;
    |FINSI;

    |SI #dscam008#14 = "E";
        |HAZ MiraContaV;
        |SI FSalida = 0; |ACABA; |FINSI;
    |FINSI;


    Divisa = #dscam008#59; |HAZ TomaDecim;

    #dscam058#0 = #dscam008#12;
    |LEE 000#dscam058.=;
    |SI FSalida = 0; |DDEFECTO #dscam058; |FINSI;

    |DDEFECTO #dscaw090;
    #dscaw090#0 = #dscam008#40;
    #dscaw090#1 = "P";
    #dscaw090#2 = #dscam008#32;
    #dscaw090#3 = #dscam008#6;
    #dscaw090#4 = #dscam008#0;
    #dscaw090#5 = #dscam008#1;
    #dscaw090#6 = #dscam008#2;
    #dscaw090#7 = #dscam008#12;
    #dscaw090#8 = #dscam058#1;
    #dscaw090#9 = #dscam008#4;
    #dscaw090#10 = #dscam008#3;
    ||#dscaw090#11 = #dscam008#25;
    #dscaw090#11 = #dscam008#58;
    |GRABA 020#dscaw090.n;
|FPRC;

|DEFBUCLE TempoCompras;
#dscam008#3;
71;
#dscaz117#9,  000000001, #dscaz117#11;
#dscaz117#10, 999999999, #dscaz117#12;
;
NULL,TempoCompras;
|FIN;

|DEFBUCLE TempoComprasb;
#dscam008#3;
71;
"01.01.0000" , 000000001, #dscaz117#11;
"31.12.9999" , 999999999, #dscaz117#12;
;
NULL,TempoCompras;
|FIN;

|PROCESO ImprePagos;
   enSw = 5; nLins = 1; |HAZ ImprimeLin;
|FPRC;

|DEFBUCLE ImprePagos;
#dscaw090#1;
;
000000001,"P";
999999999,"P";
;
NULL,ImprePagos;
|FIN;

|PROCESO ImpreCobros;
   enSw = 5; nLins = 1; |HAZ ImprimeLin;
|FPRC;

|DEFBUCLE ImpreCobros;
#dscaw090#1;
;
000000001,"C";
999999999,"C";
;
NULL,ImpreCobros;
|FIN;

|PROCESO Pagos;
   aTipoImp   = "P";
   enSw = 1; nLins = 13; |HAZ ImprimeLin;
   |BUCLE ImprePagos;
   enSw = 3; nLins = 3;  |HAZ ImprimeLin;
|FPRC;

|PROCESO Cobros;
   aTipoImp   = "C";
   enSw = 11; nLins = 4; |HAZ ImprimeLin;
   |BUCLE ImpreCobros;
   enSw = 13; nLins = 3;  |HAZ ImprimeLin;
|FPRC;

|PROCESO ImprimeLin;
   nCalc = enContLin + nLins;
   |SI nCalc > nNumLins;
      nCalc = enSw;
      |PARA nCont = enContLin; |SI nCont < nNumLins; |HACIENDO nCont = nCont + 1;
         enSw = 20; |PRINT;
      |SIGUE;
      |PIEINF; enContLin = 7;
      ||SI nCalc < 10; enSw = 1; |SINO; enSw = 11; |FINSI;
      |SI aTipoImp = "C"; enSw = 11; |SINO; enSw = 1; |FINSI;
      enContLin = enContLin + 4; |PRINT;
   |SINO;
      enContLin = enContLin + nLins; |PRINT;
   |FINSI;
|FPRC;

|PROCESO CalculaFact;
   #dsm00013@#0 = #agifa024@#0;
   #dsm00013@#1 = #agifa019@#30;
   |LEE 000#dsm00013@.=;
   |SI FSalida ! 0; |O #agifa024@#0 = "      ";
      #agifa066@#0 = #agifa019@#30;
      |LEE 000#agifa066@.=;
      |SI FSalida ! 0; |DDEFECTO #agifa066@; |FINSI;
      nPIva = #agifa066@#2; nPRec = #agifa066@#3;
   |SINO;
      nPIva = #dsm00013@#3; nPRec = #dsm00013@#4;
   |FINSI;

   nImpLin = (#agifa023@#2 < #agifa023@#32) < #agifa023@#34;

   nLinNeto = ((nImpLin < #agifa024@#37) < #agifa024@#156) < #agifa024@#38;
   nIva   = nIva + (nLinNeto % nPIva);
   |SI #agifa024@#47 = "S";
      nIva   = nIva + (nLinNeto % nPRec);
   |FINSI;
   nNeto  = nNeto + nLinNeto;
   nTFactura = nNeto + nIva;
|FPRC;

|DEFBUCLE CalculaFact;
#agifa023@#1002;
;
#agifa024@#0,"                    ";
#agifa024@#0,"zzzzzzzzzzzzzzzzzzzz";
;
NULL,CalculaFact;
|FIN;

|PROCESO Clientes;
   Divisa = #agifa024@#192; |HAZ CargaNDeci;

   || Calculo el importe que tendria la factura, leyendo la ficha de
   ||   precios cliente/articulo

   nTFactura = 0; |BUCLE CalculaFact;

   || Calculo los vencimientos que dicha factura generaria

   fFecha = #dscaz117#15;
   nDia1 = #agifa024@#22;
   nDia2 = #agifa024@#23;
   nDia3 = #agifa024@#24;

   #agifa091@#0 = #agifa024@#20;
   |LEE 000#agifa091@.=;
   |SI FSalida ! 0; |DDEFECTO #agifa091@; |FINSI;

   |PARA nVenc = 1; |SI nVenc [ #agifa091@#3; |HACIENDO nVenc = nVenc + 1;
      nCalc = 5 + nVenc;

      nNume = fFecha; nNume = nNume + #agifa091@#nCalc#; fFecha = nNume;

      |SI #agifa024@#22 ! 0; |O #agifa024@#23 ! 0; |O #agifa024@#24 ! 0;
         aAlfa = fFecha; |QBF aAlfa;
         aAlfa1 = aAlfa % 0102; nDia = aAlfa1;
         aAlfa = aAlfa % 0308;  |QBF aAlfa;

         |SI nDia [ #agifa024@#22;
            aAlfa = (("00" + #agifa024@#22) % -102) + aAlfa;
         |FINSI;

         |SI nDia [ #agifa024@#23; |Y aAlfa = "";
            aAlfa = (("00" + #agifa024@#23) % -102) + aAlfa;
         |FINSI;

         |SI nDia [ #agifa024@#24; |Y aAlfa = "";
            aAlfa = (("00" + #agifa024@#24) % -102) + aAlfa;
         |FINSI;

         |SI aAlfa = "";
            aAlfa = fFecha; |QBF aAlfa;
            aAlfa = aAlfa % 0402; nCalc = aAlfa; nCalc = nCalc + 1;
            aAlfa = fFecha; |QBF aAlfa;
            aAlfa = aAlfa % 0605; |QBF aAlfa;
            aAlfa = "." + (("00" + nCalc) % -102) + aAlfa;
            |SI #agifa024@#22 ! 0;
               aAlfa = (("00" + #agifa024@#22) % -102) + aAlfa;
            |SINO;
               |SI #agifa024@#23 ! 0;
                  aAlfa = (("00" + #agifa024@#23) % -102) + aAlfa;
               |SINO;
                  |SI #agifa024@#24 ! 0;
                     aAlfa = (("00" + #agifa024@#24) % -102) + aAlfa;
                  |FINSI;
               |FINSI;
            |FINSI;
         |FINSI;
         fFecha = aAlfa;
      |FINSI;

      aAlfa = fFecha; |QBF aAlfa;
      aAlfa = aAlfa % 0402; nCalc = aAlfa;
      |SI #agifa024@#21 = nCalc;
         fFecha = fFecha + .001;
      |FINSI;

      |SI #dscaz117#9 [ fFecha; |Y fFecha [ #dscaz117#10;
         nCalc = (nTFactura / #agifa091@#3) ! nDeci_PreVis;

         |SI nVenc = #agifa091@#3;
            nCalc = nCalc + (nTFactura - (nCalc * 3));
         |FINSI;

         #dscam058#0 = #agifa091@#69;
         |LEE 000#dscam058.=;
         |SI FSalida ! 0; |DDEFECTO #dscam058; |FINSI;

         |DDEFECTO #dscaw090;
         #dscaw090#0 = nDocumento; nDocumento = nDocumento + 1;
         #dscaw090#1 = "C";
         #dscaw090#2 = #agifa024@#0;
         #dscaw090#3 = #agifa024@#2;
         #dscaw090#7 = #agifa091@#69;
         #dscaw090#8 = #dscam058#1;
         #dscaw090#9 = #dscaz117#15;
         #dscaw090#10 = fFecha;
         #dscaw090#11 = nCalc;
         |GRABA 020#dscaw090.n;
      |FINSI;
   |SIGUE;
|FPRC;

|DEFBUCLE Clientes;
#agifa024@#1001;
3,158,20;
"      ",#dscaz117#11,#dscaz117#13,#dscam100#1;
"zzzzzz",#dscaz117#12,#dscaz117#13,#dscam100#1;
;
NULL,Clientes;
|FIN;

|PROCESO CalculaRecs;
   |BUCLE Clientes;
|FPRC;

|DEFBUCLE CalculaRecs;
#dscam100#1;
;
#dscaz117#13,"! ";
#dscaz117#13,"zz";
;
NULL,CalculaRecs;
|FIN;

|PROGRAMA;
   |HAZ Tipo8;

   |ABRE #dscam051;
   |LEE 000#dscam051.p;
   |SI FSalida ! 0; |DDEFECTO #dscam051; |FINSI;
   |CIERRA #dscam051;

   |ABRE #dscaz117;
   |LEE 000#dscaz117.p;
   |SI FSalida ! 0;
      |DDEFECTO #dscaz117; |GRABA 020#dscaz117.c;
   |FINSI;

   |CLS;
   |CABEZA " Informe prevision tesoreria ";
   |PINPA #0#0; |PINDA #0#0;

   |ENDATOS #1#dscaz117;
   |SI FSalida = 0;
      |GRABA 020#dscaz117.a;

      aAlfa1 = #48#17; |QBF aAlfa1;
      aAlfa = aAlfa1 + "def/agifa024.mas";
      |CARGA_DEF aAlfa, agifa024@;
      |SI FSalida = 0;
         aAlfa = aAlfa1 + "def/agifa023.mas"; |CARGA_DEF aAlfa, agifa023@;
         aAlfa = aAlfa1 + "def/agifa019.mas"; |CARGA_DEF aAlfa, agifa019@;
         aAlfa = aAlfa1 + "def/dsm00013.mas"; |CARGA_DEF aAlfa, dsm00013@;
         aAlfa = aAlfa1 + "def/agifa066.mas"; |CARGA_DEF aAlfa, agifa066@;
         aAlfa = aAlfa1 + "def/agifa091.mas"; |CARGA_DEF aAlfa, agifa091@;
         aAlfa = aAlfa1 + "fich/" + (("00000" + #48#15) % -105) + "/";
         |QBF aAlfa;
         |PATH_DAT #100 aAlfa; |ABRE #agifa024@
         |PATH_DAT #101 aAlfa; |ABRE #agifa023@
         |PATH_DAT #102 aAlfa; |ABRE #agifa019@
         |PATH_DAT #103 aAlfa; |ABRE #dsm00013@
         |PATH_DAT #104 aAlfa; |ABRE #agifa066@
         |PATH_DAT #105 aAlfa; |ABRE #agifa091@

         |DELINDEX #dscaw090; |ABRE #dscaw090;

         |ABRE #dscam003; |ABRE #dscam008; |ABRE #dscam058;
         |ABRE #dscam100; |ABRE #dscam102;

         |LEE 000#dscam003.u;
         |SI FSalida ! 0; |DDEFECTO #dscam003; |FINSI;
         nDocumento = #dscam003#40 + 1;

         |SI #dscam051#104 = "S";
            |BUCLE TempoVentasb;
         |SINO;
            |BUCLE TempoVentas;
         |FINSI;

         |SI #dscam051#108 = "S";
            |BUCLE TempoComprasb;
         |SINO;
            |BUCLE TempoCompras;
         |FINSI;

         |CIERRA #dscam100; |CIERRA #dscam102;
         |CIERRA #dscam003; |CIERRA #dscam008; |CIERRA #dscam058;
         |BUCLE CalculaRecs;

         |IMPRE 0;
         |SI FSalida = 0;
            |CIMPR 11; nNumLins = FSalida;
            |SI nNumLins < 0;
               nNumLins = 66; || Si se imprime por pdf nNumLins = -1
            |FINSI;

            aInforme = "Informe prevision tesoreria"; |HAZ ObtenInforme;
            |SI aInforme = "Informe prevision tesoreria"; aInforme = "dscaz117"; |FINSI;
            |INFOR aInforme;
            |SI FSalida = 0;
               enContLin = 7;
               |HAZ Pagos;
               |HAZ Cobros;
               enSw = 10; nLins = 2;  |HAZ ImprimeLin;
               |PRINT;
               |PIEINF; |FININF;
            |FINSI;
            |FINIMP;
         |FINSI;
         |CIERRA #dscaw090; |DELINDEX #dscaw090;

         |CIERRA #agifa091@; |DESCARGA_DEF #agifa091@;
         |CIERRA #agifa066@; |DESCARGA_DEF #agifa066@;
         |CIERRA #dsm00013@; |DESCARGA_DEF #dsm00013@;
         |CIERRA #agifa019@; |DESCARGA_DEF #agifa019@;
         |CIERRA #agifa023@; |DESCARGA_DEF #agifa023@;
         |CIERRA #agifa024@; |DESCARGA_DEF #agifa024@;
      |SINO;
         |MENAV "    No encuentro el fichero de clientes de gestion";
         |ERROR;
      |FINSI;
   |FINSI;
   |CIERRA #dscaz117;

   |HAZ Tipo9;
|FPRO;
