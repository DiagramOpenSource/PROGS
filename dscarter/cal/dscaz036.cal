|FICHEROS;
   dscaz036 #0;

   &agif041@ #1;
   &canempr@ #2;
   &cacuent@ #11;

   TablaCab@ #3;
   TablaLin@ #4;

   dscaw022 #5;
   dscaw023 #6;
   dscam023 #7,MANTE;
   dscam024 #8;
   dscam020 #9,MANTE;

   dscam003 #10;
   dscam008 #12; ||*
|FIN;

|VARIABLES;
   &HayGestion = 0; &HayConta = 0;
   &MaxDigit   = 0;

   &eaUsuario = ""; &enImpAnt = 0;  &enImpAct = 0;  &eaDescr = "";
   &eaTipoReg = ""; &eaNumero = ""; &eaTipoOp = "";

   Tecla = 0;
   nLong = 0;
   Calc  = 0;
   Salir = 0;
   nCont = 0;

   CueAnt   = "";
   CueNew   = "";
   CodAnt   = "";
   CodNew   = "";
   aParte1  = "";
   aParte2  = "";
   aLong    = "";
   Comodin  = "";
   Comodin1 = "";
   Cadena   = "";
|FIN;

|PROCESO CliPro; |TIPO 0;
     |SI #0#0 = "C"; |Y #0#10 = "P"; |Y FSalida ! 2;
          |MENAV "0000El cambio proveedor solo es para 'Codigo cliente gestion'";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Selec;
   |SI #0#0 = "G";
      |C_V #0#2,1,"N"; |PINPA #0#0; |PINDA #0#0;
      |C_M #0#2,1,"N"; |C_M #0#4,1,"S"; |C_M #0#6,1,"N"; |C_M #0#7,1,"N";
   |SINO;
      |C_V #0#2,1,"S"; |PINPA #0#0; |PINDA #0#0;
      |C_M #0#2,1,"S"; |C_M #0#4,1,"N"; |C_M #0#6,1,"S"; |C_M #0#7,1,"S";
   |FINSI;
|FPRC;

|PROCESO MiraEmpresa;
   |SI FSalida = 10;
      |SI #0#0 = "G";
         |CONSULTA_CLAVE #1#agif041@;
         #dscaz036#1 = #agif041@#0; |PINTA #dscaz036#1;
         #dscaz036#3 = #agif041@#1; |PINTA #dscaz036#3;
         |ERROR;
      |SINO;
         |CONSULTA_CLAVE #1#canempr@;
         #dscaz036#1 = #canempr@#2; |PINTA #dscaz036#1;
         #dscaz036#2 = #canempr@#3; |PINTA #dscaz036#2;
         #dscaz036#3 = #canempr@#1; |PINTA #dscaz036#3;
         |ERROR;
      |FINSI;
   |SINO;
      |SI #0#0 = "G";
         #agif041@#0 = #dscaz036#1;
         |LEE 000#agif041@.=;
         |SI FSalida = 0;
            #dscaz036#3 = #agif041@#1; |PINTA #dscaz036#3;
         |SINO;
            |MENAV "    Empresa inexistente"; |ERROR;
         |FINSI;
      |SINO;
         #canempr@#2 = #dscaz036#1;
         #canempr@#3 = #dscaz036#2;
         |LEE 000#canempr@.=;
         |SI FSalida = 0;
            #dscaz036#3 = #canempr@#1; |PINTA #dscaz036#3;
            MaxDigit = 0;
            |SI #canempr@#14 > MaxDigit; MaxDigit = #canempr@#14; |FINSI;
            |SI #canempr@#15 > MaxDigit; MaxDigit = #canempr@#15; |FINSI;
            |SI #canempr@#16 > MaxDigit; MaxDigit = #canempr@#16; |FINSI;
            |SI #canempr@#17 > MaxDigit; MaxDigit = #canempr@#17; |FINSI;
            |SI #canempr@#18 > MaxDigit; MaxDigit = #canempr@#18; |FINSI;
            |SI #canempr@#19 > MaxDigit; MaxDigit = #canempr@#19; |FINSI;
         |SINO;
            |MENAV "    Empresa inexistente"; |ERROR;
         |FINSI;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO MiraTabla;
   Tecla = FSalida;
   |SI Tecla = 2; |ACABA; |FINSI;
   |SI #dscaz036#0 = "C"; |ACABA; |FINSI;

   Comodin = #48#17; |QBF Comodin;
   Comodin = Comodin + "fich/" + (("00000" + #dscaz036#1) % -105) + "/";
   |PATH_DAT #3 Comodin; |PATH_DAT #4 Comodin;
   |ABRE #TablaCab@; |ABRE #TablaLin@;

   |SI Tecla = 10;
      |CONSULTA_CLAVE #1#TablaCab@;
      #dscaz036#4 = #TablaCab@#0; |PINTA #dscaz036#4;
      #dscaz036#5 = #TablaCab@#1; |PINTA #dscaz036#5;
      |ERROR;
   |SINO;
      #TablaCab@#0 = #dscaz036#4;
      |LEE 000#TablaCab@.=;
      |SI FSalida = 0;
         #dscaz036#5 = #TablaCab@#1; |PINTA #dscaz036#5;
      |SINO;
         |MENAV "    Tabla inexistente"; |ERROR;
      |FINSI;
   |FINSI;
   |SI #TablaCab@#2 ! #0#10;
      |SI #0#10 = "C";
          |MENAV "    La tabla de conversion no es de codigos de clientes";
      |SINO;
          |MENAV "    La tabla de conversion no es de codigos de probveedores";
      |FINSI;
      |ERROR;
   |FINSI;

   |CIERRA #TablaLin@; |CIERRA #TablaCab@;
|FPRC;

|PROCESO Cuenta;
   Tecla = FSalida;
   |SI Tecla = 10;
      |CONSULTA_CLAVE #1#cacuent@;
      #dscaz036#Campo# = #cacuent@#0; |PINTA #dscaz036#Campo#;
      |ERROR;
   |SINO;
      Comodin = #dscaz036#Campo#; |QBF Comodin;
      Comodin = Comodin - ".";
      |SI FSalida ! 0;
         aLong = Comodin % 0; nLong = aLong;
         Calc = ((FSalida / 100) ! 0) + 99; aParte1 = Comodin % Calc;
         Calc = FSalida + 98;               aParte2 = Comodin % Calc;
         Calc = MaxDigit - nLong;
         Comodin1 = "0" * Calc;
         Comodin = aParte1 + Comodin1 + aParte2;
         #dscaz036#Campo# = Comodin; |PINTA #dscaz036#Campo#;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO Recibos;
   |SI #dscaz036#0 = "G";
      #dscam003#32 = CodNew;
      eaDescr = #dscam003#32;
   |SINO;
      #dscam003#5 = CueNew;
      eaDescr = #dscam003#5;
   |FINSI;
   |GRABA 020#dscam003.a;
   eaUsuario = Usuario % 0810;
   eaTipoReg = "CB";  eaNumero = ("000000000" + #dscam003#40) % -109;
   eaDescr   = "CAMBIO CODIGO CLIENTE RECIBO (" + (("000000000" + #dscam003#40) % -109) + ") (" + eaDescr;
   |SI #dscaz036#0 = "G";
      eaDescr   = eaDescr + "/" + CodNew + ")";
   |SINO;
      eaDescr   = eaDescr + "/" + CueNew + ")";
   |FINSI;
   eaTipoOp  = "E"; enImpAnt = #dscam003#30; enImpAct = #dscam003#30;
   |HAZ GrabaAudit;
|FPRC;

|DEFBUCLE Recibos1;
#dscam003#1;
32;
000000001,CodAnt;
999999999,CodAnt;
be;
NULL,Recibos;
|FIN;

|DEFBUCLE RecibosVenta;
#dscam003#1;
5;
000000001,CueAnt;
999999999,CueAnt;
be;
NULL,Recibos;
|FIN;

|PROCESO RecibosCompra;
   #dscam008#5 = CueNew; |GRABA 020#dscam008.a;
|FPRC;

|DEFBUCLE RecibosCompra;
#dscam008#1;
5;
000000001,CueAnt;
999999999,CueAnt;
be;
NULL,RecibosCompra;
|FIN;

|PROCESO CambiProv;
   #dscam008#32 = CodNew; |GRABA 020#dscam008.a;
|FPRC;

|DEFBUCLE CambiProv;
#dscam008#1;
32;
000000001,CodAnt;
999999999,CodAnt;
be;
NULL, CambiProv;
|FIN;


|PROCESO Cambio;
   |SI #dscaz036#0 = "G"; |Y #dscaz036#10 = "P";
      |BUCLE CambiProv;
   |FINSI;

   |SI #dscaz036#0 = "G"; |Y #dscaz036#10 = "C";
      |BUCLE Recibos1;

      #dscam020#4 = CodAnt;
      |LEE 101#dscam020.=;
      |SI FSalida = 0;
         #dscam020#4 = CodNew;
         |GRABA 020#dscam020.a; |LIBERA #dscam020;
      |FINSI;

      #dscam023#0 = CodAnt;
      |LEE 101#dscam023.=;
      |SI FSalida = 0;
         |SALVA_FICHA #dscam023;
         #dscam023#0 = CodNew;
         |GRABA 020#dscam023.n;
         |REPON_FICHA #dscam023;
         |LIBERA #dscam023;

         #dscam024#0 = CodAnt;
         #dscam024#1 = 1;
         |LEE 101#dscam024.];
         |SI FSalida = 0; |Y #dscam024#0 = CodAnt;
            |PARA; |SI FSalida = 0; |Y #dscam024#0 = CodAnt; |HACIENDO;
               |SALVA_FICHA #dscam024;
               #dscam024#0 = CodNew;
               |GRABA 020#dscam024.n;
               |REPON_FICHA #dscam024;
               |LIBERA #dscam024;
               |LEE 101#dscam024.s;
            |SIGUE;
            |LIBERA #dscam024;
         |FINSI;
      |FINSI;

      #dscam020#4 = CodAnt;
      |LEE 000#dscam020.=;
      |SI FSalida = 0;
         |BORRA 020#dscam020.a;
      |FINSI;

      #dscam023#0 = CodAnt;
      |LEE 000#dscam023.=;
      |SI FSalida = 0;
         |BORRA 020#dscam023.a;

         Salir = 0;
         |PARA; |SI Salir = 0; |HACIENDO;
            #dscam024#0 = CodAnt;
            #dscam024#1 = 1;
            |LEE 101#dscam024.];
            |SI FSalida = 0; |Y #dscam024#0 = CodAnt;
               |BORRA 020#dscam024.a;
            |SINO;
               Salir = 1;
            |FINSI;
         |SIGUE;
      |FINSI;
   |FINSI;
   |SI #dscaz036#0 = "C";
      CueAnt = #dscaz036#6; CueNew = #dscaz036#7;
      |BUCLE RecibosVenta;
      |BUCLE RecibosCompra;
   |FINSI;
|FPRC;

|PROCESO Cambio1;
   CodAnt = #dscaw022#0;
   CodNew = #dscaw022#2;
   |HAZ Cambio;
|FPRC;

|DEFBUCLE Cambio1;
#dscaw022#1;
;
INICIO;
FINAL;
;
NULL,Cambio1;
|FIN;

|PROCESO Cambio2;
   CodAnt = #dscaw022#2;
   CodNew = #dscaw022#1;
   |HAZ Cambio;
|FPRC;

|DEFBUCLE Cambio2;
#dscaw022#1;
;
INICIO;
FINAL;
;
NULL,Cambio2;
|FIN;

|PROCESO Temporal;
   |DDEFECTO #dscaw022;
   #dscaw022#0 = #TablaLin@#2;
   #dscaw022#1 = #TablaLin@#3;

   Comodin1 = "";
   Cadena = #TablaLin@#2; |QBF Cadena;
   aLong = Cadena % 0; nLong = aLong;
   |PARA nCont = 1; |SI nCont [ nLong; |HACIENDO nCont = nCont + 1;
      Calc = (nCont * 100) + 1;
      Comodin = Cadena % Calc;
      |SI Comodin = "1"; Comodin =  "A"; |FINSI;
      |SI Comodin = "2"; Comodin =  "B"; |FINSI;
      |SI Comodin = "3"; Comodin =  "C"; |FINSI;
      |SI Comodin = "4"; Comodin =  "D"; |FINSI;
      |SI Comodin = "5"; Comodin =  "E"; |FINSI;
      |SI Comodin = "6"; Comodin =  "F"; |FINSI;
      |SI Comodin = "7"; Comodin =  "G"; |FINSI;
      |SI Comodin = "8"; Comodin =  "H"; |FINSI;
      |SI Comodin = "9"; Comodin =  "I"; |FINSI;
      |SI Comodin = "0"; Comodin =  "J"; |FINSI;
      Comodin1 = Comodin1 + Comodin;
   |SIGUE;
   #dscaw022#2 = Comodin1; |GRABA 000#dscaw022.n;
|FPRC;

|DEFBUCLE Temporal;
#TablaLin@#1002;
;
#dscaz036#4,00001;
#dscaz036#4,99999;
;
NULL,Temporal;
|FIN;

|PROCESO Tablas;

   || Debo apoyarme sobre un temporal para no pisar los datos que se van
   ||    pasando

   || 1¦ PASADA ..............

   |DELINDEX #dscaw022;
   |ABRE #dscam020; |ABRE #dscam023; |ABRE #dscam024; |ABRE #dscaw022;
   |SELECT #3#dscam020;

   || Creo un temporal asignando el codigo antiguo de cada registro de la
   ||   tabla a un registro de los nuevos tomando nota del codigo nuevo

   |LEE 000#dscaw023.p;
   |BUCLE Temporal;

   || Hecho esto voy asignando los codigos antiguos por orden a los libres
   ||   para en la segunda pasada volver a colocarlos
   |BUCLE Cambio1;

   || 2¦ PASADA ..................

   || Directamente hago el cambio de los codigos libres a los codigos nuevos
   ||   y vuelvo a librerar los codigos libres
   |BUCLE Cambio2;

   |SELECT #1#dscam020;
   |CIERRA #dscam020; |CIERRA #dscam023; |CIERRA #dscam024; |CIERRA #dscaw022;

|FPRC;

|PROGRAMA;
   |HAZ Tipo8;

   |SI HayGestion = 1;
      |SI HayConta = 1;
         |C_M #0#0,1,"S";
      |SINO;
         |C_M #0#0,1,"N"; #0#0 = "G"; |PINTA #0#0;
      |FINSI;
   |SINO;
      |SI HayConta = 1;
         |C_M #0#0,1,"N"; #0#0 = "C"; |PINTA #0#0;
      |FINSI;
   |FINSI;

   Comodin = #48#17; |QBF Comodin;
   Comodin1 = Comodin + "def/agifa153.mas";
   |CARGA_DEF Comodin1, TablaCab@;
   |SI FSalida < 0; |HAZ Tipo9; |ACABA; |FINSI;

   Comodin1 = Comodin + "def/agifa154.mas";
   |CARGA_DEF Comodin1, TablaLin@;
   |SI FSalida < 0; |DESCARGA_DEF #TablaCab@; |HAZ Tipo9; |ACABA; |FINSI;

   |SI PARAMETRO = "";
      |CLS;
      |CABEZA "Cambio de cogigos";
      |PINPA #0#0; |PINDA #0#0;
      |ENDATOS #1#0;
      |SI FSalida = 0; |Y #0#9 = "SI";
         |SI #dscaz036#0 = "G";
            |HAZ Tablas;
         |SINO;
         |FINSI;
      |FINSI;
   |SINO;
   |FINSI;
   |DESCARGA_DEF #TablaLin@; |DESCARGA_DEF #TablaCab@;
   |HAZ Tipo9;
|FPRO;
