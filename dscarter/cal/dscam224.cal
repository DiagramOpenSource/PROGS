|FICHEROS;
     dscam224 #0;
     dscaz230 #1,MANTE; || Pan Aux
|FIN;

|VARIABLES;
    &eaCta     = "";
    &eaPais    = "";
    &eaIBAN    = "";
    &eaIBANant = "";
    aAlfa      = "";
    &eaCodPais = "";
    &eaCodIBAN = "";
    &eaCodBIC  = "";
    aParam     = "";
    nRecibo    = 0;
    nModIBAN   = 0;
    nModBIC    = 0;
    aIBANCli   = "";
    aBICCli    = "";
    &nSalida   = 0;
|FIN;

|PROCESO T12; |TIPO 12;
|FPRC;

|PROCESO Tipo7IBAN_m224;  |TIPO 7;

     aAlfa = #0#1;   |QBF aAlfa;

    |SI nSalida = 1;
        eaIBANant = "";
        eaPais    = eaCodPais;
        |HAZ CalcuIBAN;
        |SI eaIBAN = "";
            eaIBAN = eaCodIBAN;
        |FINSI;
        |SI eaIBANant ! eaIBAN;
            |CONFI "0000SHa cambio los datos de la Cuenta. Recalcular IBAN";
            |SI FSalida = 0;
                #0#1 = eaIBAN;
                |PINTA #0#1;
            |FINSI;
        |FINSI;
        nModIBAN = 1;
        |ACABA;
     |FINSI;

     |SI aAlfa ! ""; |O nModIBAN = 1;
         eaCta = "";
         |ACABA;
     |FINSI;

     eaIBANant = "";
     |SI eaCodIBAN ! "";
         eaIBANant = eaCodIBAN
     |FINSI;
     eaPais    = eaCodPais;
     |HAZ CalcuIBAN;
     |SI eaIBAN = "";
         eaIBAN = eaCodIBAN;
     |FINSI;

     #0#1 = eaIBAN;
     |PINTA #0#1;
     nModIBAN = 1;
|FPRC;

|PROCESO Tipo0IBAN_m224;  |TIPO 0;
     |SI FSalida = 11;
          #1#0 = #0#3;
          #1#1 = #0#4;
          |PUSHV 0501 2380;
          |BLANCO 1301 1658;
          |PINPA #0#1;
          |PINDA #0#1;
          |ENDATOS #1#1;
          |POPV;
          #0#3 = #1#0;
          #0#4 = #1#1;
          |ERROR; |ACABA;
     |FINSI;

     |SI FSalida = 10;
         #0#1 = eaCodIBAN;
         |PINTA #0#1;
         nSalida = 0;
         |ACABA;
     |FINSI;

     eaPais    = eaCodPais;
     eaIBANant = #0#1;
     |HAZ CheqIBAN;

     aAlfa = eaIBANant; |QBF aAlfa;
     |SI aAlfa ! eaIBAN;
         |SI aAlfa ! "";
             |CONFI "0000SEl codigo IBAN puede ser erroneo, quiere calcularlo.";
             |SI FSalida = 0;
                 #0#1 = eaIBAN;
                 |ERROR;
             |FINSI;
         |FINSI;
     |FINSI;
     eaCta = "";
|FPRC;

|PROCESO Tipo7BIC_m224;  |TIPO 7;
     aAlfa = #0#2;   |QBF aAlfa;
     |SI aAlfa = ""; |Y nModBIC = 0;
         #0#2 = eaCodBIC;
         |PINTA #0#2;
     |FINSI;
     nModBIC = 1;
|FPRC;

|PROCESO Tipo0BIC_m224;  |TIPO 0;
     |SI FSalida = 10;
         #0#2 = eaCodBIC;
         |PINTA #0#2;
         |ERROR;
     |FINSI;
|FPRC;

|PROGRAMA;
     aParam = PARAMETRO; |QBF aParam;
     nRecibo = aParam;

     aIBANCli = eaCodIBAN;
     aBICCli  = eaCodBIC;

     nModIBAN = 1;
     nModBIC  = 1;
     |ABRE #0;
     #0#0 = nRecibo;
     |LEE 101#0=;
     |SI FSalida ! 0;
         |DDEFECTO #0;
         #0#0 = nRecibo;
         #0#1 = eaCodIBAN;
         #0#2 = eaCodBIC;
         |GRABA 020#0b;
         nModIBAN = 0;
         nModBIC  = 0;
     |FINSI;

     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |GRABA 020#0a;
     |LIBERA #0;

     |CIERRA #0;
|FPRO;
