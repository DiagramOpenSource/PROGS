|FICHEROS;
   dscaw178 #6,MANTE;

   dscam001 #10;
|FIN;

|VARIABLES;
   &TipoCliente = ""; &CodCliente = "";
   &enExistOblig = 0;
   &DCampoG = 0;  &HCampoG = 0;  &DCampoC = 0;  &HCampoC = 0;
   &HayGestion = 0; &HayConta   = 0;
   &MaxDigit = 0;

   Cont    = 0;
   Calc    = 0;
   nTecla  = 0;
   nParte1 = 0;
   nParte2 = 0;

   aParte1 = "";
   aParte2 = "";

   Comodin = "";
|FIN;

|PROCESO MensaAntes1; |TIPO 7;
   |SI #dscaw178#31 = "G";
      |MENSA "    <F2> Consulta clientes";
   |SINO;
      |MENSA "    <F2> Consulta cuentas contables";
   |FINSI;
|FPRC;

|PROCESO MensaDespues1;
   |SI #dscaw178#31 = "G";
      |SI Campo = 1; |O Campo = 2;  |BLIN 4; |FINSI;
   |SINO;
      |SI Campo = 32; |O Campo = 33; |BLIN 4; |FINSI;
   |FINSI;
|FPRC;

|PROCESO MiraCliente1;
   TipoCliente = #dscaw178#31;
   enExistOblig = 1;
   CodCliente = #dscaw178#Campo#;

   DCampoG = Campo; HCampoG = Campo;
   DCampoC = Campo; HCampoC = Campo;
   |HAZ ExisteCliente;
   #dscaw178#Campo# = CodCliente; |PINTA #dscaw178#Campo#;
|FPRC;

|PROCESO Seleccion1;
   |SI HayConta = 0; |Y #dscaw178#31 = "C";
      |MENAV "    La contabilidad no esta instalada";
      |ERROR; |ACABA;
   |FINSI;

   |SI HayGestion = 0; |Y #dscaw178#31 = "G";
      |MENAV "    La gestion comercial no esta instalada";
      |ERROR; |ACABA;
   |FINSI;

   |SI #dscaw178#31 = "C";
      |C_M #dscaw178#1,1,"N"; |C_V #dscaw178#1,1,"N";
      |C_M #dscaw178#2,1,"N"; |C_V #dscaw178#2,1,"N";
      |PARA Cont = 7; |SI Cont [ 30; |HACIENDO Cont = Cont + 1;
         |C_M #dscaw178#Cont#,1,"N"; |C_V #dscaw178#Cont#,1,"N";
      |SIGUE;
      |SI #dscaw178#0 = "S";
         |C_M #dscaw178#32,1,"S"; |C_V #dscaw178#32,1,"S";
         |C_M #dscaw178#33,1,"S"; |C_V #dscaw178#33,1,"S";
      |SINO;
         |C_M #dscaw178#32,1,"N"; |C_V #dscaw178#32,1,"S";
         |C_M #dscaw178#33,1,"N"; |C_V #dscaw178#33,1,"S";
      |FINSI;
      |PINTA #dscaw178#32; |PINTA #dscaw178#33;
      |PARA Cont = 34; |SI Cont [ 45; |HACIENDO Cont = Cont + 1;
         |SI #dscaw178#0 = "S";
            |C_M #dscaw178#Cont#,1,"N"; |C_V #dscaw178#Cont#,1,"S";
         |SINO;
            |C_M #dscaw178#Cont#,1,"S"; |C_V #dscaw178#Cont#,1,"S";
         |FINSI;
         |PINTA #dscaw178#Cont#;
      |SIGUE;
   |SINO;
      |C_M #dscaw178#32,1,"N"; |C_V #dscaw178#32,1,"N";
      |C_M #dscaw178#33,1,"N"; |C_V #dscaw178#33,1,"N";
      |PARA Cont = 34; |SI Cont [ 45; |HACIENDO Cont = Cont + 1;
         |C_M #dscaw178#Cont#,1,"N"; |C_V #dscaw178#Cont#,1,"N";
      |SIGUE;
      |SI #dscaw178#0 = "S";
         |C_M #dscaw178#1,1,"S"; |C_V #dscaw178#1,1,"S";
         |C_M #dscaw178#2,1,"S"; |C_V #dscaw178#2,1,"S";
      |SINO;
         |C_M #dscaw178#1,1,"N"; |C_V #dscaw178#1,1,"S";
         |C_M #dscaw178#2,1,"N"; |C_V #dscaw178#2,1,"S";
      |FINSI;
      |PINTA #dscaw178#1; |PINTA #dscaw178#2;
      |PARA Cont = 7; |SI Cont [ 30; |HACIENDO Cont = Cont + 1;
         |SI #dscaw178#0 = "S";
            |C_M #dscaw178#Cont#,1,"N"; |C_V #dscaw178#Cont#,1,"S";
         |SINO;
            |C_M #dscaw178#Cont#,1,"S"; |C_V #dscaw178#Cont#,1,"S";
         |FINSI;
         |PINTA #dscaw178#Cont#;
      |SIGUE;
   |FINSI;
   |PINPA #0#6;
   |PINDA #0#6;
|FPRC;

|PROCESO Cta1; |TIPO 6;
   nTecla = FSalida;
   Comodin = #Fichero@#Campo#; |QBF Comodin;
   Comodin = Comodin - ".";

   |SI FSalida ! 0;
      nParte1 = (FSalida / 100) ! 0 + 99; aParte1 = Comodin % nParte1;
      nParte2 = FSalida + 98;            aParte2 = Comodin % nParte2;
      |PARA; |SI FSalida ! 0; |HACIENDO; aParte1 = aParte1 - "."; |SIGUE;
      FSalida = 1;
      |PARA; |SI FSalida ! 0; |HACIENDO; aParte2 = aParte2 - "."; |SIGUE;
      Comodin = aParte1 % 0; Calc = Comodin;
      Comodin = aParte2 % 0; Calc = Calc + Comodin;
      Calc = MaxDigit - Calc; Comodin = "0" * Calc;
      Comodin = aParte1 + Comodin + aParte2;
      #Fichero@#Campo# = Comodin; |PINTA #Fichero@#Campo#;
   |FINSI;
   FSalida = nTecla;
|FPRC;

|PROCESO ExistClient21;
   |SI FSalida = 2; |ACABA; |FINSI;
   |SI #dscaw178#0 = "S"; |ACABA; |FINSI;

   DCampoG = 7;  HCampoG = 30;
   DCampoC = 34; HCampoC = 45;

   TipoCliente = #dscaw178#31;
   CodCliente  = #dscaw178#Campo#;
   |HAZ ExisteCliente;
   |SI TipoCliente = "ERROR";
      |ERROR;
   |SINO;
      #dscaw178#Campo# = CodCliente;
      |PINTA #dscaw178#Campo#;
   |FINSI;
|FPRC;

|PROCESO RevisaCuentas1;
   |PARA Cont = 34; |SI Cont [ 45; |HACIENDO Cont = Cont + 1;
      |SI Cont ! Campo;
         |SI #dscaw178#Cont# = #dscaw178#Campo#; |Y #dscaw178#Cont# ! "            ";
            |MENAV "    Cuenta repetida";
            |ERROR;
            |ACABA;
         |FINSI;
      |FINSI;
   |SIGUE;
|FPRC;

|PROCESO MensaEntAnt1; |TIPO 7;
   |MENSA "    <F2> Consulta Entidades";
|FPRC;

|PROCESO MensaEntDesp1;
   |BLIN 4;
|FPRC;

|PROCESO MiraEntidad1;
   nTecla = FSalida;

   |ABRE #dscam001;
   |SI nTecla = 10;
      #dscam001#0 = #dscaw178#Campo#;
      |LEE 000#dscam001.];
      |CONSULTA_CLAVE #1#dscam001;
      |SI FSalida = 0;
         #dscaw178#Campo# = #dscam001#0; |PINTA #dscaw178#Campo#;
      |FINSI;
   |FINSI;
   |CIERRA #dscam001;
|FPRC;
