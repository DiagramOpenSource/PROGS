ordenante=acreedor

|FICHEROS;
   dscaz223 #0;
   dscam015;

   dscam012 #12;    ||Reme Cab
   dscam013 #13;    ||Reme Lin
   dscam014 #14;    ||Bancos
   dscam003 #3;     ||Recibos
   dscam051;
   dscam030;
   dscam004;
   dscaw013;
   dscaw038;
   dscam002;
   dscam044;
   dscam093,MANTE;
   dscam161,MANTE;
   dscam040;
   dscam001;
   dscam225 #225;

   &agif024@;
   &caclipr@;
   &agif321@;

   dscam000@;
   dscaw229 #229;
   dscaw230 #230;

   gestr037@ #37;  || Viuda
   gestr035@ #35;
   gestr985@ #985;
   referen@;
|FIN;

|VARIABLES;
     nSwOTP = 0;
     aPrefijo = "";
     aSerie = "";
     aBass = "";
     nSwViuda = 0;
     nSwChequea = 0;
     nError = 0;
     nHayError = 0;
     nSumDocumentos = 0;
     nConta = 0;
     fDFecVto = @;
     fHFecVto = @;
     aFinanciada = "";
     aHayFinanciada = "";
     aHayNormal = "";
     aLon = "";
     nLon = 0;
     aDeci = "";

     &enDRemesa = 0;
     &enHRemesa = 0;
     &efDFecha = @;
     &efHFecha = @;
     &enDBanco = 0;
     &enHBanco = 0;
     &efFecEmi = @;
     &efFecCar = @;
     &eaB2B = "";
     &eaRedu = "";
     &eaEnlace = "";
     ||
     &eaPain = ""; &eaFichero = ""; &eaTag = ""; &eaVal = ""; &eaGrupo = "";
     nSwCuenta = 0;
     nRegistros = 0;
     nTotal = 0;
     fHoy = @;
     nNume = 0;
     aPais = "";
     aNif = "";
     aCar = "";
     aCadena = "";
     aId = "";
     aSufijo = "";
     nPersona = 0;
     aHora = "";

     ||-----------------------
     aAlfa = "";
     aAlfa1 = "";
     aAlfa2 = "";
     SwHay = 0;
     SwHayNoEmit = 0;
     nPos1 = 0;
     nPos2 = 0;
     nModo = 0;
     aBase = "";
     LinCob = 0;
     SwEmit = "";
     nSwConta = 0;
     EmpreAnt = 0;
     PerioAnt = 0;
     Comodin = "";
     Comodin1 = "";
     &enError = 0;
     &eaCodPais = "";
     &eaCta = "";
     &eaIBAN = "";
     &eaUsuario = ""; &enImpAnt = 0;  &enImpAct = 0;  &eaDescr = "";
     &eaTipoReg = ""; &eaNumero = ""; &eaTipoOp = "";
     &HayGestion = 0; &HayConta = 0; &nDeci_Base = 0; &nDeci_Div = 0;
     &Fichero   = ""; &Cmp = "";      &Tipo = "";     &CmpNum = 0;
     &Niv1 = 0; &Niv2 = 0; &Niv3 = 0; &Niv4 = 0;  &Niv5 = 0; &Niv6 = 0;
     &eaCodigo    = "";
     &eaTipoCod   = "";
     &aCuenta     = "";

     &eaRefMand = "";
     &eaFechaMand = "";
     &eaNif       = "";
     &eaNombre    = "";
     &eaDireccion = "";
     &eaPoblacion = "";
     &eaProvincia = "";
     &eaCPExt     = "";
     &eaCP        = "";
     &eaCP1       = "";
     &eaCP2       = "";
     &eaTelefono  = "";
     &eaNombre1   = "";
     &eaPais      = "";
     &eaCodBIC    = "";
     &eaCodIBAN   = "";

     &eaOrigen = "";
     &Divisa   = "";
     &enEmpCartera = 0; &enEmpGestion = 0; &enBanco = 0;
     &eaSerie = ""; &eaCliGest = ""; &nImporte = 0; &eaTipoOper = "";
|FIN;

|PROCESO NumFactuAgrupa;
     |ABRE #225;
     #225#0 = #3#40;
     |LEE 000#225=;
     |SI FSalida = 0; |Y #225#4 = #3#4; |Y #225#5 = #3#3;
          aAlfa = "Factura:" + #225#1; |QBF aAlfa;
          aAlfa = aAlfa + "/" +  (("000000" + #225#2)%-106) + "-" + (("000" + #225#3) % -103);
     |FINSI;
     |CIERRA #225;
|FPRC;


|PROCESO LeeDatoOTP;
/*
     |DDEF aAlfa;
     aAlfa = aAlfa+ "dscam224";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
         |ABRE #referen@;
         #referen@#0 = #dscam003#40;
         |LEE 000#referen@.=;
         |SI FSalida = 0;

||             aAlfa = #referen@#1; |QBF aAlfa;
||             |SI aAlfa ! ""; eaCodIBAN = #referen@#1; |FINSI;

||             aAlfa = #referen@#2; |QBF aAlfa;
||             |SI aAlfa ! "";  eaCodBIC = #referen@#2; |FINSI;

               eaCodIBAN = #referen@#1;   || tique 700000_9839
               eaCodBIC  = #referen@#2;

               eaFechaMand = #referen@#3;
               eaRefMand = #referen@#4;

         |FINSI;
         |CIERRA #referen@;
         |DESCARGA_DEF #referen@;
     |FINSI;
*/
|FPRC;

|PROCESO LeeDatoViuda;
     aAlfa = #3#4 % 704;
     #gestr985@#0 = aAlfa;
     |LEE 000#gestr985@.=;

     #gestr035@#7 = #3#80;
     |LEE 000#gestr035@.=;
     |SI FSalida ! 0; |DDEFECTO #gestr035@; |FINSI;

     aSerie = #3#0; |QBF aSerie;
     aPrefijo = #gestr985@#1; |QBF aPrefijo;
     aSerie = aSerie - aPrefijo;
     #gestr037@#0 = #3#32;
     #gestr037@#1 = aSerie; aSerie = #gestr037@#1;
     #gestr037@#2 = #gestr035@#0;
     |LEE 000#gestr037@.];
     |SI FSalida ! 0;
          #gestr037@#0 = "";
          |LEE 000#gestr037@.];
          |SI FSalida ! 0; |O #gestr037@#0 ! #3#32; |O #gestr037@#1 ! aSerie;
               |DDEFECTO #gestr037@;
          |FINSI;
     |FINSI;
     eaCodIBAN   = #gestr037@#18;
     eaCodBIC    = #gestr037@#19;
     eaFechaMand = #gestr037@#20;
|FPRC;

|PROCESO Errores;
     |PRINT;
|FPRC;

|DEFBUCLE Errores;
     #230#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Errores;
|FIN;

|PROCESO ImpError;
     |CONFI "0000SExisten errores en los datos. ¿Imprimir errores?";
     |SI FSalida = 0;
          |IMPRE 0;
          |SI FSalida = 0;
               |INFOR "dscaz223";
               |SI FSalida = 0;
                    |BUCLE Errores;
                    |PIEINF;
                    |FININF;
               |SINO;
                    |MENAV "0000Error en informe 'dscaz223'...";
               |FINSI;
               |FINIMP;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Error;
     |LEE 000#230u;
     |SI FSalida = 0;
          nConta = #230#0 + 1;
     |SINO;
          nConta = 1;
     |FINSI;
     #230#0 = nConta;
     |SI nError ! 3;
          #230#1 = #12#0;
          #230#2 = #3#40;
     |FINSI;
     #230#3 = nError;
     |SI nError = 1; #230#4 = "Sin NIF"; |FINSI;
     |SI nError = 2; #230#4 = "Sin fecha firma SEPA"; |FINSI;
     |SI nError = 3; #230#4 = "Sin codigo BIC en banco propio:" + #14#0; |FINSI;
     |SI nError = 4; #230#4 = "Sin codigo pais"; |FINSI;
     |SI nError = 5; #230#4 = "Sin codigo IBAN"; |FINSI;
     |SI nError = 6; #230#4 = "Sin codigo BIC"; |FINSI;
     |GRABA 020#230n;
|FPRC;

|PROCESO NumDeci;
     nNume = (nNume * 100) ! 0;
     aAlfa = nNume;
     aLon = aAlfa % A0; nLon = aLon;
     aDeci = aAlfa % -102;
     nLon = nLon + 100 - 2;
     aAlfa = (aAlfa % nLon) + "." + aDeci;
|FPRC;

|PROCESO FormaFecha;
     eaVal = (eaVal % 704) + "-" + (eaVal % 402) + "-" + (eaVal % 102);
|FPRC;

|PROCESO HoraActual;
     fHoy = ~;
     |HORA aAlfa;
     aAlfa = (fHoy % 704) + "-" + (fHoy % 402) + "-" + (fHoy % 102) + "T" + aAlfa;
|FPRC;

|PROCESO Mod_9710;
     aAlfa2 = aAlfa % 0101;
     |PARA; |SI aAlfa2 = "0"; |HACIENDO;
          aAlfa = aAlfa % 0299; aAlfa2 = aAlfa % 0101;
     |SIGUE;

     aAlfa1 = "";
     |PARA; |SI aAlfa ! ""; |HACIENDO;
          aAlfa2 = aAlfa % 0; nNume = aAlfa2;
          |SI nNume > 5;
               aAlfa2 = aAlfa % 0105;
               aAlfa = aAlfa % 0699;
          |SINO;
               aAlfa2 = aAlfa;
               aAlfa = "";
          |FINSI;
          aAlfa1 = aAlfa1 + aAlfa2;
          nNume = aAlfa1;
          nNume = nNume / 97;
          aAlfa2 = nNume;
          aAlfa2 = aAlfa2 - ".";
          |SI FSalida ! 0;
               nNume = FSalida + 98;
               aAlfa2 = aAlfa2 % nNume;
               aAlfa2 = "0." + aAlfa2;
               nNume = aAlfa2;
          |SINO;
               nNume = 0;
          |FINSI;
          nNume = (nNume * 97) ! 0;
          aAlfa1 = nNume;
     |SIGUE;
     nNume  = aAlfa1;
     nNume = 98 - nNume;
     aAlfa = ("00" + nNume) % -102;
||nNume  = 98 - (nNume - (((nNume / 97) ! 0) * 97)); || MOD 97-10  ISO 7604
|FPRC;

|PROCESO ObtenId;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "/"; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "-"; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "?"; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - ":"; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "("; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - ")"; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "."; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - ","; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "'"; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "+"; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - &34; |SIGUE;

     aAlfa = aNif + aPais + "00";

     aAlfa = aAlfa > aAlfa;
     aCar = ""; aCadena = "";
     |PARA; |SI aAlfa ! ""; |HACIENDO;
          aCar = aAlfa % 0101; aAlfa = aAlfa % 0299;
          |SI aCar ] "0"; |Y aCar [ "9";
               aCadena = aCadena + aCar;
          |SINO;
               |SI aCar ] "A"; |Y aCar [ "Z"; |Y aCar ! "Ñ";
                    nNume = &aCar;
                    nNume = nNume - 55;
                    aCar = nNume;
                    aCadena = aCadena + aCar;
               |FINSI;
          |FINSI;
     |SIGUE;
     aAlfa = aCadena;
     |HAZ Mod_9710;
     aId = aPais + aAlfa + aSufijo + aNif;
|FPRC;

|PROCESO Cabecera;  || acreedor
     eaGrupo = "S";
     eaTag = "CstmrDrctDbtInitn";|HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "GrpHdr"; |HAZ XmlTag;

     fHoy = ~;
     |HORA aHora;
     aAlfa = #14#47; |QBF aAlfa;
     eaVal = aAlfa + (fHoy % 102) + (fHoy % 402) + (fHoy % 704);
     eaVal = eaVal + (aHora % 102) + (aHora % 402) + (aHora % 702);
     eaVal = eaVal + ("00000" + #12#0) % -105;
     |SI aFinanciada = "S";
          |SI #0#22 = "N";
               eaVal = "FSDD" + eaVal;
          |FINSI;
     |FINSI;
     eaTag = "MsgId"; |HAZ XmlTag;

     |HAZ HoraActual; eaVal = aAlfa;
     eaTag = "CreDtTm"; |HAZ XmlTag;

     eaVal = nRegistros;
     eaTag = "NbOfTxs"; |HAZ XmlTag;

     nNume = nTotal; |HAZ NumDeci; eaVal = aAlfa;
     eaTag = "CtrlSum"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "InitgPty"; |HAZ XmlTag;

     eaVal = #14#46; |QBF eaVal;
     |SI eaVal ! "";
          eaTag = "Nm"; |HAZ XmlTag;
     |FINSI;

     eaGrupo = "S";
     eaTag = "Id"; |HAZ XmlTag;

     aAlfa = #14#47; |QBF aAlfa; aAlfa = aAlfa % 0101;
     |SI aAlfa ] "A"; |Y aAlfa [ "W";
          nPersona = 1; || Persona juridica
     |SINO;
          nPersona = 2; || Persona fisica
     |FINSI;

     |SI nPersona = 1;
          eaGrupo = "S";
          eaTag = "OrgId"; |HAZ XmlTag;
     /*
          eaVal =
          eaTag = "BICOrBEI"; |HAZ XmlTag;
     */
          eaGrupo = "S";
          eaTag = "Othr"; |HAZ XmlTag;

          aPais = "ES";
          aNif = #14#47;
          |SI aFinanciada = "S";
               aSufijo = #14#79; |QBF aSufijo; aSufijo = ("000" + aSufijo) % -103;
          |SINO;
               aSufijo = #14#78; |QBF aSufijo; aSufijo = ("000" + aSufijo) % -103;
          |FINSI;
          |HAZ ObtenId;
          eaVal = aId;
          eaTag = "Id"; |HAZ XmlTag;

          eaGrupo = "N"; |HAZ XmlTag;
          eaGrupo = "N"; |HAZ XmlTag;
     |SINO;
          eaGrupo = "S";
          eaTag = "PrvtId"; |HAZ XmlTag;

          eaGrupo = "S";
          eaTag = "Othr"; |HAZ XmlTag;

          aPais = "ES";
          aNif = #14#47;
          |SI aFinanciada = "S";
               aSufijo = #14#79; |QBF aSufijo; aSufijo = ("000" + aSufijo) % -103;
          |SINO;
               aSufijo = #14#78; |QBF aSufijo; aSufijo = ("000" + aSufijo) % -103;
          |FINSI;
          |HAZ ObtenId;
          eaVal = aId;
          eaTag = "Id"; |HAZ XmlTag;

          eaGrupo = "N"; |HAZ XmlTag;
          eaGrupo = "N"; |HAZ XmlTag;
     |FINSI;

     eaGrupo = "N"; |HAZ XmlTag;
     eaGrupo = "N"; |HAZ XmlTag;
     eaGrupo = "N"; |HAZ XmlTag;
|FPRC;

|PROCESO InfoPago;
     nSumDocumentos = nSumDocumentos + 1;

     eaGrupo = "S";
     eaTag = "PmtInf"; |HAZ XmlTag;

     aAlfa = #14#47; |QBF aAlfa;
     eaVal = aAlfa + (fHoy % 102) + (fHoy % 402) + (fHoy % 704);
     eaVal = eaVal + (aHora % 102) + (aHora % 402) + (aHora % 702);
     eaVal = eaVal + (("00000" + #12#0) % -105) + (("000" + nSumDocumentos)%-103);
     eaTag = "PmtInfId"; |HAZ XmlTag;

     eaVal = "DD";
     eaTag = "PmtMtd"; |HAZ XmlTag;

     |SI #0#21 = "S";
          eaVal = "true";
     |SINO;
          eaVal = "false";
     |FINSI;
     eaTag = "BtchBookg"; |HAZ XmlTag;

     eaVal = nRegistros;
     eaTag = "NbOfTxs"; |HAZ XmlTag;

     nNume = nTotal; |HAZ NumDeci; eaVal = aAlfa;
     eaTag = "CtrlSum"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "PmtTpInf"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "SvcLvl"; |HAZ XmlTag;

     eaVal = "SEPA";
     eaTag = "Cd"; |HAZ XmlTag;

     eaGrupo= "N"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "LclInstrm"; |HAZ XmlTag;

     |SI #0#16 = "S";
          eaVal = "B2B";
     |SINO;
          |SI #0#19 = "S";
               eaVal = "COR1";
          |SINO;
               eaVal = "CORE";
          |FINSI;
     |FINSI;
     eaTag = "Cd"; |HAZ XmlTag;

     eaGrupo= "N"; |HAZ XmlTag;

     |SI #229#3 = "P"; eaVal = "FRST"; |FINSI;
     |SI #229#3 = "R"; eaVal = "RCUR"; |FINSI;
     |SI #229#3 = "U"; eaVal = "OOFF"; |FINSI;
     eaTag = "SeqTp"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "CtgyPurp"; |HAZ XmlTag;

     eaVal = "SUPP";
     eaTag = "Cd"; |HAZ XmlTag;

     eaGrupo= "N"; |HAZ XmlTag;
     eaGrupo= "N"; |HAZ XmlTag;

     eaVal = #229#0; |HAZ FormaFecha;
     eaTag = "ReqdColltnDt"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "Cdtr"; |HAZ XmlTag;

     eaVal = #14#52; |QBF eaVal;
     |SI eaVal ! "";
          eaTag = "Nm"; |HAZ XmlTag;
     |FINSI;

     eaGrupo = "N"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "CdtrAcct"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "Id"; |HAZ XmlTag;

     eaVal = #14#42;
     eaTag = "IBAN"; |HAZ XmlTag;

     eaGrupo = "N"; |HAZ XmlTag;
     eaGrupo = "N"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "CdtrAgt"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "FinInstnId"; |HAZ XmlTag;

     eaVal = #14#45; |QBF eaVal;
     |SI eaVal ! "";
          eaTag = "BIC"; |HAZ XmlTag;
     |SINO;
          eaGrupo = "S";
          eaTag = "Othr"; |HAZ XmlTag;

          eaVal = "NOTPROVIDED";        || ***
          eaTag = "Id"; |HAZ XmlTag;

          eaGrupo = "N"; |HAZ XmlTag;
     |FINSI;
     eaGrupo = "N"; |HAZ XmlTag;
     eaGrupo = "N"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "CdtrSchmeId"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "Id"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "PrvtId"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "Othr"; |HAZ XmlTag;

     aPais = "ES";
     aNif = #14#53;
     |SI aFinanciada = "S";
         aSufijo = #14#81; |QBF aSufijo; aSufijo = ("000" + aSufijo) % -103;
     |SINO;
         aSufijo = #14#80; |QBF aSufijo; aSufijo = ("000" + aSufijo) % -103;
     |FINSI;
     |HAZ ObtenId;
     eaVal = aId;
     eaTag = "Id"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "SchmeNm"; |HAZ XmlTag;

     eaVal = "SEPA";
     eaTag = "Prtry"; |HAZ XmlTag;

     eaGrupo = "N"; |HAZ XmlTag;
     eaGrupo = "N"; |HAZ XmlTag;
     eaGrupo = "N"; |HAZ XmlTag;
     eaGrupo = "N"; |HAZ XmlTag;
     eaGrupo = "N"; |HAZ XmlTag;
|FPRC;

|PROCESO InfoAdeudo;
     |SI nSwViuda = 0;
         |HAZ LeeDatoViuda;
     |SINO;
         |HAZ LeeDatoOTP;
     |FINSI;
     |QBF eaPoblacion;
     |QBF eaProvincia;
     |QBF eaDireccion;
     |QBF eaNif;
     |QBF eaCodBIC;
     |QBF eaCodPais;
     |QBF eaCodIBAN;

     aAlfa = eaNif; |QBF aAlfa; aAlfa = aAlfa % 0101;
     |SI aAlfa ] "A"; |Y aAlfa [ "W";
          nPersona = 1; || Persona juridica
     |SINO;
          nPersona = 2; || Persona fisica
     |FINSI;

     eaGrupo = "S";
     eaTag = "DrctDbtTxInf"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "PmtId"; |HAZ XmlTag;

     aAlfa = #14#53; |QBF aAlfa;
     aAlfa = aAlfa + (#12#1%102) + (#12#1%402) + (#12#1%704);
     eaVal = aAlfa + (("00000" + #13#0) % -105) + (("000" + #13#1) % -103);
     eaTag = "EndToEndId"; |HAZ XmlTag;

     eaGrupo = "N"; |HAZ XmlTag;

     nNume = #13#7; |HAZ NumDeci; eaVal = aAlfa;
     eaTag = "InstdAmt Ccy=" + &34 + "EUR" + &34; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "DrctDbtTx"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "MndtRltdInf"; |HAZ XmlTag;

||2.48
     aAlfa = eaRefMand;  || eaNif + "CLI" + eaCodigo;
     eaVal = aAlfa;
     eaTag = "MndtId"; |HAZ XmlTag;

     eaVal = eaFechaMand; |HAZ FormaFecha;
     eaTag = "DtOfSgntr"; |HAZ XmlTag;

     eaGrupo = "N"; |HAZ XmlTag;
     eaGrupo = "N"; |HAZ XmlTag;

||2.70
     eaGrupo = "S";
     eaTag = "DbtrAgt"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "FinInstnId"; |HAZ XmlTag;

     eaVal = eaCodBIC; |QBF eaVal;
     |SI eaVal ! "";
          eaTag = "BIC"; |HAZ XmlTag;
     |SINO;
          eaGrupo = "S";
          eaTag = "Othr"; |HAZ XmlTag;

          eaVal = "NOTPROVIDED";
          eaTag = "Id"; |HAZ XmlTag;

          eaGrupo = "N"; |HAZ XmlTag;
     |FINSI;

     eaGrupo = "N"; |HAZ XmlTag;
     eaGrupo = "N"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "Dbtr"; |HAZ XmlTag;

     eaVal = #3#6; |QBF eaVal;
     |SI eaVal ! "";
          eaTag = "Nm"; |HAZ XmlTag;
     |FINSI;

     eaGrupo = "S";
     eaTag = "PstlAdr"; |HAZ XmlTag;

     eaVal = eaCodPais;
     eaTag = "Ctry"; |HAZ XmlTag;

     |SI eaDireccion ! "";
          eaVal = eaDireccion;
          eaTag = "AdrLine"; |HAZ XmlTag;
     |FINSI;

     |SI eaPoblacion = "";
          |SI eaProvincia ! "";
               eaVal = eaProvincia;
               eaTag = "AdrLine"; |HAZ XmlTag;
          |FINSI;
     |SINO;
          |SI eaProvincia ! "";
               eaVal = eaPoblacion + ", " + eaProvincia;
               eaTag = "AdrLine"; |HAZ XmlTag;
          |SINO;
               eaVal = eaPoblacion;
               eaTag = "AdrLine"; |HAZ XmlTag;
          |FINSI;
     |FINSI;

     eaGrupo = "N"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "Id"; |HAZ XmlTag; || 2.72

     |SI nPersona = 1;   || Persona juridica (2.72)
          eaGrupo = "S";
          eaTag = "OrgId"; |HAZ XmlTag;
/*                                                ***
          |QBF eaCodBIC;
          |SI eaCodBIC ! "";
               eaVal = eaCodBIC;
               eaTag = "BICOrBEI"; |HAZ XmlTag;
          |SINO;
*/
               eaGrupo = "S";
               eaTag = "Othr"; |HAZ XmlTag;

               eaVal = eaNif;
               eaTag = "Id"; |HAZ XmlTag;

               eaGrupo = "N"; |HAZ XmlTag;
/*
          |FINSI;
*/
          eaGrupo = "N"; |HAZ XmlTag;
     |SINO;              || Persona fisica
          eaGrupo = "S";
          eaTag = "PrvtId"; |HAZ XmlTag;

          eaGrupo = "S";
          eaTag = "Othr"; |HAZ XmlTag;

          eaVal = eaNif;
          eaTag = "Id"; |HAZ XmlTag;

          eaGrupo = "N"; |HAZ XmlTag;
          eaGrupo = "N"; |HAZ XmlTag;
     |FINSI;
     eaGrupo = "N"; |HAZ XmlTag;
     eaGrupo = "N"; |HAZ XmlTag;
||2.73
     eaGrupo = "S";
     eaTag = "DbtrAcct"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "Id"; |HAZ XmlTag;

     eaVal = eaCodIBAN;
     eaTag = "IBAN"; |HAZ XmlTag;

     eaGrupo = "N"; |HAZ XmlTag;

     eaGrupo = "N"; |HAZ XmlTag;

||2.76
     eaGrupo = "S";
     eaTag = "Purp"; |HAZ XmlTag;

     eaVal = "SUPP";
     eaTag = "Cd"; |HAZ XmlTag;

     eaGrupo = "N"; |HAZ XmlTag;

||2.88
     eaGrupo = "S";
     eaTag = "RmtInf"; |HAZ XmlTag;

/*     aAlfa = #dscam014#59; |QBF aAlfa;
     |SI aAlfa ! ""; aAlfa = aAlfa + " "; |FINSI;
     |SI #dscam014#60 = "S"; aAlfa = aAlfa + #dscam003#0; |FINSI;
     |SI #dscam014#61 = "S"; aAlfa = aAlfa + "/" + (("000000" + #dscam003#1) % -106); |FINSI;
     |SI #dscam014#62 = "S"; aAlfa = aAlfa + "/" + (("000" + #dscam003#2) % -103); |FINSI;
*/
     aAlfa = "Factura:" + #3#0; |QBF aAlfa;
     aAlfa = aAlfa + "/" +  (("000000" + #3#1)%-106) + "-" + (("000" + #3#2) % -103);
     |HAZ NumFactuAgrupa;
     eaVal = aAlfa;
     eaTag = "Ustrd"; |HAZ XmlTag;

     eaGrupo = "N"; |HAZ XmlTag;
     eaGrupo = "N"; |HAZ XmlTag;
|FPRC;

|PROCESO ActRecibo;
   || Actualizo  el historico de cobros
   #dscam004#17 = #dscam013#16;
   #dscam004#3 = 99;
   |LEE 000#dscam004.];
   |SI FSalida ! 0;
      |LEE 000#dscam004.u;
      |SI FSalida = 0; |Y #dscam004#17 = #dscam013#16;
         LinCob = #dscam004#3 + 1;
      |SINO;
         LinCob = 1;
      |FINSI;
   |SINO;
      |SI #dscam004#17 = #dscam013#16;
         LinCob = #dscam004#3 + 1;
      |SINO;
         |LEE 000#dscam004.a;
         |SI FSalida = 0; |Y #dscam004#17 = #dscam013#16;
            LinCob = #dscam004#3 + 1;
         |SINO;
            LinCob = 1;
         |FINSI;
      |FINSI;
   |FINSI;

   |DDEFECTO #dscam004;
   #dscam004#17 = #dscam013#16;
   #dscam004#15 = #dscam013#2;
   #dscam004#0  = #dscam013#4;
   #dscam004#1  = #dscam013#3;
   #dscam004#2  = #dscam013#5;
   #dscam004#3  = LinCob;
   #dscam004#4  = #dscam012#14;
   #dscam004#5  = "EMR";
   #dscam004#6  = #dscaz223#6;
   #dscam003#40 = #dscam013#16;
   |LEE 000#dscam003.=;
   |SI FSalida = 0;
      #dscam004#7  = #dscam003#23; #dscam004#25  = #dscam003#61;
   |FINSI;
   #dscam004#8  = 0;
   #dscam004#9  = 0;
   #dscam004#10 = 0;
   #dscam004#26 = 0;
   |GRABA 020#dscam004.n;

   #dscam013#24 = LinCob; |GRABA 020#dscam013.a;
|FPRC;

|PROCESO RemesasLin;
     #12#0 = #13#0;
     |LEE 121#12=;

     |SI #0#22 = "N";
          |SI #12#22 = 2; |O #12#22 = 4;
               |SI aFinanciada = "N"; |ACABA; |FINSI;
          |SINO;
               |SI aFinanciada = "S"; |ACABA; |FINSI;
          |FINSI;
     |FINSI;

     |SI #12#1 ] #0#2; |Y #12#1 [ #0#3; |Y #12#2 = #14#0;
          #dscam015#0 = #dscam012#2;
          |LEE 000#dscam015.=;
          |SI FSalida = 0; |DDEFECTO #dscam015; |FINSI;

          #dscam003#40 = #dscam013#16;
          |LEE 121#dscam003.=;
          |SI FSalida ! 0; |ACABA; |FINSI;

          |HAZ CargaContaGes;
          |SI enError ! 0; |ACABA; |FINSI;

          #dscam001#0 = #dscam003#8;
          |LEE 000#dscam001.=;
          |SI FSalida ! 0; |DDEFECTO #dscam001; |FINSI;

          Divisa = #dscam003#57; |HAZ TomaDecim;
          eaCodigo = #dscam003#32; eaTipoCod = "C"; aCuenta = #dscam003#5;

          |HAZ LeeDatos;
          |HAZ RefMandato;

          |SI nSwCuenta = 1;
               nRegistros = nRegistros + 1;
               nTotal = nTotal + #13#7;
               |DDEFECTO #229;
               #229#3 = #3#106;
               |SI aFinanciada = "S";
                    #229#0 = #13#6;
               |SINO;
                    #229#0 = #0#7;
               |FINSI;
               |LEE 101#229=;
               |SI FSalida ! 0; |GRABA 020#229b; |FINSI;
               #229#1 = #229#1 + 1;
               #229#2 = #229#2 + #13#7;
               |GRABA 020#229a;
               |LIBERA #229;
          |SINO;
               |SI #229#3 = #3#106;
                    |HAZ InfoAdeudo;
                    |SI #dscam012#23 = "N"; |HAZ ActRecibo; |FINSI;
               |FINSI;
          |FINSI;
          |LIBERA #dscam003;

          |HAZ DescargaContaGes;
     |FINSI;
     |LIBERA #12;
|FPRC;

|DEFBUCLE RemesasLin;
     #13#1;
     6;
     #0#0, 001, fDFecVto;
     #0#1, 999, fHFecVto;
     ;
     NULL, RemesasLin;
|FIN;

|PROCESO TmpAgrupa;
     nRegistros = #229#1;
     nTotal = #229#2;
     |HAZ InfoPago;

     |SI aFinanciada = "S";
          fDFecVto = #229#0;
          fHFecVto = #229#0;
     |SINO;
          fDFecVto = "01.01.0000";
          fHFecVto = "31.12.9999";
     |FINSI;
     |BUCLE RemesasLin;
     eaGrupo = "N"; |HAZ XmlTag;
|FPRC;

|DEFBUCLE TmpAgrupa;
     #229#1;
     ;
     " ", "01.01.0000";
     "z", "31.12.9999";
     ;
     NULL, TmpAgrupa;
|FIN;

|PROCESO Tmp038;
     #dscam012#0 = #dscaw038#0;
     |LEE 121#dscam012.=;
     SwEmit = #dscam012#23;

     eaUsuario = Usuario % 0810;
     eaDescr   = "EMISION REMESA (" + (("00000" + #dscam012#0) % -105);
     eaDescr   = eaDescr + ") EN DISKETTE DESDE NORMA 19.44";
     eaTipoReg = "RE";  eaNumero = ("00000" + #dscam012#0) % -105;
     eaTipoOp  = "E";   enImpAnt = #dscam012#10; enImpAct = #dscam012#10;
     |HAZ GrabaAudit;

     |SI #dscam012#23 = "N";
          #dscam012#23 = "S"; |GRABA 020#dscam012.a;
     |FINSI;

     |SI nSwConta = 1; |Y SwEmit = "N"; |Y #dscam051#1 = "S";
          |ABRE #dscam030;
          #dscam030#0 = #dscaz223#14;
          |LEE 010#dscam030.=;
          |SI FSalida = 0;
               |SUB_EJECUTA_NP "dscaz169.tab;INICIO";

               |SI #dscam030#4 ! #dscaw038#3; |O #dscam030#5 ! #dscaw038#4;
                    EmpreAnt = #dscam030#4; PerioAnt = #dscam030#5;
                    #dscam030#4 = #dscaw038#3; #dscam030#5 = #dscaw038#4;
                    |GRABA 020#dscam030.a;
               |SINO;
                    EmpreAnt = -1;
               |FINSI;
               Comodin = Usuario; |QBF Comodin; Comodin = "ac" + Comodin;

               |NOME_DAT #dscaw013#Comodin#;
               |DELINDEX #dscaw013; |ABRE #dscaw013;
               Comodin = #dscaz223#14;
               |SI Comodin = "    ";
                    |ABRE #dscam093;
                    #dscam093#0 = "E";
                    |LEE 000#dscam093.=;
                    |SI FSalida = 0;
                         |SI #dscam012#22 = 1; Comodin = #dscam093#1; |FINSI;
                         |SI #dscam012#22 = 2; Comodin = #dscam093#3; |FINSI;
                         |SI #dscam012#22 = 3; Comodin = #dscam093#5; |FINSI;
                         |SI #dscam012#22 = 4; Comodin = #dscam093#7; |FINSI;
                         |SI #dscam012#22 = 5; Comodin = #dscam093#9; |FINSI;
                    |FINSI;
                    |CIERRA #dscam093;
               |FINSI;

               |DDEFECTO #dscaw013;
               #dscaw013#0  = 1;            #dscaw013#15 = Comodin;
               #dscaw013#5  = #dscam012#0;  #dscaw013#6  = #dscam012#0;
               #dscaw013#11 = "N";          #dscaw013#12 = 5;
               #dscaw013#13 = "dscam013";   #dscaw013#14 = 0;
               |GRABA 020#dscaw013.n;
               |CIERRA #dscaw013;
               |LIBERA #dscam012;

               Comodin = "dscam033.tab;" + Comodin + ",I";
               |LIBERA #dscam012; |CIERRAT #dscam012;
               |SUB_EJECUTA_NP Comodin;
               |ABRET #dscam012; |LEE 101#dscam012.=;

               |DFICE aAlfa; |QBF aAlfa; aAlfa = "ac" + Usuario + ".dat"; |FBORRA aAlfa;
               |DFICE aAlfa; |QBF aAlfa; aAlfa = "ac" + Usuario + ".ddx"; |FBORRA aAlfa;
               |DFICE aAlfa; |QBF aAlfa; aAlfa = "ac" + Usuario + ".ixx"; |FBORRA aAlfa;

               |SI EmpreAnt ! -1;
                    #dscam030#4 = EmpreAnt; #dscam030#5 = PerioAnt;
                    |GRABA 020#dscam030.a;
               |FINSI;

               |SUB_EJECUTA_NP "dscaz169.tab;FINAL";
          |FINSI;
          |CIERRA #dscam030;
     |FINSI;

     |LIBERA #dscam012;
|FPRC;

|DEFBUCLE Tmp038;
     #dscaw038#1;
     ;
     00001, " ";
     99999, "z";
     ;
     NULL, Tmp038;
|FIN;

|PROCESO MiraRemeLin;
     |SI nSwChequea = 0; |ACABA; |FINSI;

     #3#40 = #13#16;
     |LEE 000#3=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |HAZ CargaContaGes;
     |SI enError ! 0; |ACABA; |FINSI;

     Divisa = #dscam003#57; |HAZ TomaDecim;
     eaCodigo = #dscam003#32; eaTipoCod = "C"; aCuenta = #dscam003#5;

     |HAZ LeeDatos;
     |HAZ RefMandato;

     |HAZ DescargaContaGes;

     |SI nSwViuda = 0;
        |HAZ LeeDatoViuda;
     |SINO;
         |HAZ LeeDatoOTP;
     |FINSI;

     |QBF eaNif;
     |QBF eaFechaMand;
     |QBF eaCodBIC;
     |QBF eaCodPais;
     |QBF eaCodIBAN;

     |SI eaNif = "";
          nError = 1; |HAZ Error;
     |FINSI;
     |SI eaFechaMand = ""; |O eaFechaMand < "01.01.1900";
          nError = 2; |HAZ Error;
     |FINSI;
     |SI eaCodPais = "";
         nError = 4; |HAZ Error;
     |FINSI;
     |SI eaCodBIC = "";
         |SI eaCodPais ! ""; |Y eaCodPais ! "ES";
             nError = 6; |HAZ Error;
         |FINSI;
     |FINSI;
     |SI eaCodIBAN = "";
          nError = 5; |HAZ Error;
     |FINSI;
|FPRC;

|PROCESO MiraRemesas;
     SwHay = 1;
     |DDEFECTO #dscaw038;
     #dscaw038#0 = #dscam012#0;
     #dscaw038#1 = #dscam012#10;
     #dscaw038#2 = #dscaz223#14;
     #dscaw038#5 = #dscam012#23;
     |SI #dscaw038#5 = "N";
          SwHayNoEmit = 1;
     |FINSI;

     |ABRE #dscam030;
     #dscam030#0 = #dscaz223#14;
     |LEE 000#dscam030.=;
     |SI FSalida = 0;
          #dscaw038#3 = #dscam030#4;
          #dscaw038#4 = #dscam030#5;
     |FINSI;
     |CIERRA #dscam030;

     |SI #dscam012#22 = 2; |O #dscam012#22 = 4;
         #dscaw038#6 = "S";
         aHayFinanciada = "S";
     |SINO;
         #dscaw038#6 = "N";
         aHayNormal = "S";
     |FINSI;

     |GRABA 020#dscaw038.n;
|FPRC;

|DEFBUCLE MiraRemesas;
     #12#1;
     1,2;
     #0#0, #0#2, #14#0;
     #0#1, #0#3, #14#0;
     ;
     NULL, MiraRemesas, MiraRemeLin;
|FIN;

|RUTINA infw038;
   #dscaw038#0 = 00001;
   #dscaw038#5 = "N";
|FRUT;

|RUTINA supw038;
   #dscaw038#0 = 99999;
   #dscaw038#5 = "N";
|FRUT;

|PROCESO Secuencial;
   aHayFinanciada = "N";
   aHayNormal = "N";

   SwHayNoEmit = 0;
   SwHay = 0;
   |DELINDEX #dscaw038;
   |ABRE #dscaw038;
   |ABRE #3;
   |BUCLE MiraRemesas;
   |CIERRA #3;
   |CIERRA #dscaw038;

   |SI SwHay = 0; |ACABA; |FINSI;

   |SI nSwChequea = 1;

|| ... Preguntar a Carlos Frases
||      aAlfa = #14#45; |QBF aAlfa;
||      |SI aAlfa = ""; nError = 3; |HAZ Error; |FINSI;

      |ACABA;
   |FINSI;

   |SI #dscaz223#15 = "N";
      |SI SwHayNoEmit = 1;
         |PUSHV 0501 2380;
         |ENTLINEAL #1#dscaw038,1,2,19,1,infw038,supw038;
         |POPV;
      |SINO;
         |MENAV "    No hay remesas sin emitir para redireccionar";
      |FINSI;
   |FINSI;

   aAlfa = #dscam014#46; |QBF aAlfa;
   |SI aAlfa = "";
      aAlfa = #dscam014#47; |QBF aAlfa;
      |SI aAlfa = "";
         aAlfa = #dscam014#53; |QBF aAlfa;
         |SI aAlfa = "";
            aAlfa = #dscam014#54; |QBF aAlfa;
            |SI aAlfa = "";
               aAlfa = #dscam014#72; |QBF aAlfa;
               |SI aAlfa = "";
                  |PUSHV 0910 1970; |BLANCO 0910 1970;
                  |CUADRO 0910 1970;
                  |ATRI R;
                  |PINTA 1012, "Los datos para la emision de remesas en soporte magnetico";
                  |PINTA 1112, "estan vacios. Seleccione un registro del fichero anterior";
                  |PINTA 1212, "del cual importar los datos necesarios.                  ";
                  |ATRI N;
                  nPos1 = 1312; nPos2 = 1868; nModo = 2 + 4 + 8 + 16;
                  |ABRE #dscam015;
                  |ET OtraVez;
                  |LINEAL_SIMPLE #1#dscam015,nModo, nPos1, nPos2, NULL, NULL, NULL;
                  |SI FSalida < 0;
                     |CONFI "    SSin datos para emitir remesas en soporte magnetico no podra emitir dichas normas. Continuar ? ";
                     |SI FSalida ! 0;
                        |VETE OtraVez;
                     |SINO;
                        |ACABA;
                     |FINSI;
                  |SINO;
                     #dscam014#46 = #dscam015#2;
                     #dscam014#47 = #dscam015#3;
                     #dscam014#48 = #dscam015#12;
                     #dscam014#49 = #dscam015#13;
                     #dscam014#72 = #dscam015#19;
                     #dscam014#51 = #dscam015#20;
                     #dscam014#52 = #dscam015#6;
                     #dscam014#53 = #dscam015#7;
                     #dscam014#54 = #dscam015#21;
                     #dscam014#55 = #dscam015#8;
                     #dscam014#56 = #dscam015#9;
                     #dscam014#57 = #dscam015#11;
                     #dscam014#58 = #dscam015#10;
                     #dscam014#59 = #dscam015#14;
                     #dscam014#60 = #dscam015#15;
                     #dscam014#61 = #dscam015#16;
                     #dscam014#62 = #dscam015#17;
                     |GRABA 020#dscam014.a;
                  |FINSI;
                  |CIERRA #dscam015;
                  |POPV;
               |FINSI;
            |FINSI;
         |FINSI;
      |FINSI;
   |FINSI;

   #dscaz223#8  = #dscam014#78;
   #dscaz223#17 = #dscam014#80;
   |SI #dscaz223#8 ! "   "; |Y #dscaz223#17 = 0;
      #dscaz223#17 = #dscaz223#8;
   |FINSI;
   |SI #dscaz223#8 = "   "; |Y #dscaz223#17 ! 0;
      #dscaz223#8 = #dscaz223#17;
   |FINSI;

  |SI aHayNormal = "S"; |Y #0#22 = "N";
     aFinanciada = "N";
     |DFICB Comodin;
     Comodin1 = ("000" + #dscam014#0) % -103;
     Comodin1 = "n1900" + Comodin1 + ".xml";
     eaOrigen = Comodin;
     eaNombre1 = Comodin1;
     eaFichero = Comodin + Comodin1;
     eaPain = "008.001.02";
     |HAZ XmlIni;
     |SI FSalida ] 0;
        |ABRE #dscam004;
        |ABRE #dscam003;
        |ABRE #dscam012;
        |ABRE #dscam013;
        |ABRE #dscam015;

        nSumDocumentos = 0;
        fDFecVto = "01.01.0000";
        fHFecVto = "31.12.9999";
        |DELINDEX #229;
        |ABRE #229;
        nSwCuenta = 1; |BUCLE RemesasLin
        |CIERRA #229;

        |HAZ Cabecera;
        nSwCuenta = 2; |BUCLE TmpAgrupa;

        eaGrupo = "N"; |HAZ XmlTag;

        |CIERRA #dscam015;
        |CIERRA #dscam013;
        |CIERRA #dscam012;
        |CIERRA #dscam003;
        |CIERRA #dscam004;

        |HAZ XmlFin;
        |SUB_EJECUTA_NP "dscaz032.tab";
     |FINSI;
   |FINSI;

  |SI aHayFinanciada = "S"; |O #0#22 = "S";
     aFinanciada = "S";
     |DFICB Comodin;
     Comodin1 = ("000" + #dscam014#0) % -103;
     Comodin1 = "n19f0" + Comodin1 + ".xml";
     eaOrigen = Comodin;
     eaNombre1 = Comodin1;
     eaFichero = Comodin + Comodin1;
     eaPain = "008.001.02";
     |HAZ XmlIni;
     |SI FSalida ] 0;
        |ABRE #dscam004;
        |ABRE #dscam003;
        |ABRE #dscam012;
        |ABRE #dscam013;
        |ABRE #dscam015;

        nSumDocumentos = 0;
        fDFecVto = "01.01.0000";
        fHFecVto = "31.12.9999";
        |DELINDEX #229;
        |ABRE #229;
        nSwCuenta = 1; |BUCLE RemesasLin
        |CIERRA #229;

        |HAZ Cabecera;
        nSwCuenta = 2; |BUCLE TmpAgrupa;

        eaGrupo = "N"; |HAZ XmlTag;

        |CIERRA #dscam015;
        |CIERRA #dscam013;
        |CIERRA #dscam012;
        |CIERRA #dscam003;
        |CIERRA #dscam004;

        |HAZ XmlFin;
        |SUB_EJECUTA_NP "dscaz032.tab";
     |FINSI;
   |FINSI;

   |ABRE #dscam012;
   |BUCLE Tmp038;
   |CIERRA #dscam012;
|FPRC;

|DEFBUCLE Secuencial;
     #dscam014#1;
     ;
     #0#4;
     #0#5;
     be;
     NULL,Secuencial;
|FIN;

|RUTINA infm161;
   #dscam161#0 = 000001;
|FRUT;

|RUTINA supm161;
   #dscam161#0 = 999999;
|FRUT;

|PROCESO Boton;
   |PUSHV 1501 2380;
   |ENTLINEAL #1#dscam161, -1, 1, 22, 1, infm161, supm161;
   |POPV;
|FPRC;

|PROCESO Tipo; |TIPO 0;
     |SI #0#16 = "N";
          |C_M #0#19, 1, "S";
     |SINO;
          |C_M #0#19, 1, "N";
     |FINSI;
|FPRC;

|PROCESO XmlTxt; |TIPO 0;
     |SI #0#20 = "X";
          |C_M #0#21, 1, "S";
     |SINO;
          |C_M #0#21, 1, "N";
     |FINSI;
|FPRC;

|PROCESO AlVto; |TIPO 0;
     |SI #0#22 = "S";
          |C_M #0#7, 1, "N";
     |SINO;
          |C_M #0#7, 1, "S";
     |FINSI;
|FPRC;

|PROGRAMA;
     |ABRE #dscam051; |LEE 000#dscam051.p; |CIERRA #dscam051;

     |HAZ EsViuda; nSwViuda = FSalida;
     nSwOTP = 0;

     |DBASE aBase;
     aAlfa = aBase + "def/dscam000";
     |CARGA_DEF aAlfa, dscam000@;
     |SI FSalida = 0;
          aAlfa = aBase + "fich/";
          |PATH_DAT #dscam000@#aAlfa#;
          |DFICE aAlfa;
          aAlfa = aAlfa % -205;
          |ABRE #dscam000@;
          #dscam000@#0 = aAlfa;
          |LEE 020#dscam000@.=;
          |CIERRA #dscam000@;

          aAlfa = #dscam000@#16; |QBF aAlfa;
          |SI aAlfa ! ""; nSwConta = 1; |FINSI;

          |DESCARGA_DEF #dscam000@;
     |FINSI;

     |SI nSwConta = 1; |Y #dscam051#1 = "S";
          #dscaz223#14 = #dscam051#53; |PINTA #dscaz223#14;
          aAlfa = #dscam051#61; |QBF aAlfa;
          |C_M #dscaz223#14,1,aAlfa;
     |SINO;
          #dscaz223#14 = ""; |PINTA #dscaz223#14;
          |C_M #dscaz223#14,1,"N";
     |FINSI;

     |CLS;
     |CABEZA "Emision diskette norma 19";
     |PINPA #0#0; |PINDA #0#0;

     |ENDATOS #1#0;
     |SI FSalida = 0;
         |SI #0#20 = "X";
             |SI nSwViuda = 0;
                 |DBASS aBass;
                 aAlfa = aBass + "gesenvia/def/gestr035"; |CARGA_DEF aAlfa, gestr035@;
                 aAlfa = aBass + "gesenvia/def/gestr037"; |CARGA_DEF aAlfa, gestr037@;
                 aAlfa = aBass + "gesenvia/def/gestr985"; |CARGA_DEF aAlfa, gestr985@;
                 aAlfa = aBass + "gesenvia/fich/" + (("00000" + #48#15) % -105) + "/";
                 |PATH_DAT #gestr035@#aAlfa#;
                 |PATH_DAT #gestr037@#aAlfa#;
                 |PATH_DAT #gestr985@#aAlfa#;
                 |ABRE #gestr035@; |SELECT #2#gestr035@;
                 |ABRE #gestr037@;
                 |ABRE #gestr985@;
             |FINSI;

             aAlfa = Usuario; aAlfa = "a1"+ aAlfa; |NOME_DAT #229 aAlfa;
             aAlfa = Usuario; aAlfa = "a2"+ aAlfa; |NOME_DAT #230 aAlfa;
             |DELINDEX #229;
             |DELINDEX #230;

             |ABRE #230;
             |ABRE #dscam001;
             nSwChequea = 1; |BUCLE Secuencial;
             |LEE 000#230p;
             |SI FSalida = 0;
                 |HAZ ImpError;
                 |CONFI "0000NExisten errores. ¿Continuar con la creación del xml?";
             |SINO;
                  FSalida = 0;
             |FINSI;
             |SI FSalida = 0;
                 nSwChequea = 0; |BUCLE Secuencial;
             |FINSI;
             |CIERRA #dscam001;
             |CIERRA #230;

             |DELINDEX #229;
             |DELINDEX #230;

             |SI nSwViuda = 0;
                 |CIERRA #gestr985@;
                 |CIERRA #gestr037@;
                 |CIERRA #gestr035@;
                 |DESCARGA_DEF #gestr985@;
                 |DESCARGA_DEF #gestr037@;
                 |DESCARGA_DEF #gestr035@;
             |FINSI;
         |SINO;
             enDRemesa = #0#0;
             enHRemesa = #0#1;
             efDFecha = #0#2;
             efHFecha = #0#3;
             enDBanco = #0#4;
             enHBanco = #0#5;
             efFecEmi = #0#6;
             efFecCar = #0#7;
             eaB2B = #0#16;
             eaRedu = #0#19;
             eaEnlace = #0#14;
             |SUB_EJECUTA_NP "dscaz215;AUTO";
         |FINSI;
     |FINSI;
|FPRO;
