|FICHEROS;
   cadie005 #0;
   cadie504 #1;

   dscam003 #10;
   dscam004 #11;
   dscam044 #12;
   dscam045 #13;

   agif025@ #100;
   agif134@ #101;
|FIN;

|VARIABLES;

   &Divisa = "";

   aAlfa    = "";
   aAlfa1   = "";
   DesdeRep = "";
   HastaRep = "";
   aAntRep  = "ZZZZ";

   &nDeci_Div = 0; &nDeci_Base = 0;
   &HayConta  = 0; &HayGestion = 0;
   nTotFact   = 0; nTotCom = 0; nAgrup = 0;
   Primero    = 0;
   SwError    = 0;
   nTotalPag  = 0;
   nTotalPdt  = 0;
   nTotalDto  = 0;
   nCalc      = 0;
   nCont      = 0;
   EmpCartera = 0;

   Calc       = 0;
   SwNeg      = 0;
   nDecimales = 0;
   Long       = 0;

   Comodin    = "";
   Comodin1   = "";
   aDecimales = "";
   LongAlfa   = "";
   Cadena     = "";
|FIN;

|PROCESO RecibosLin;
||   nTotalPag = nTotalPag + #dscam004#27;
   nTotalPag = nTotalPag + #dscam004#44;
   nTotalPdt = nTotalPdt - (#dscam004#44 - #dscam004#46);
   nTotalDto = nTotalDto + #dscam004#46;
|FPRC;

|DEFBUCLE RecibosLin;
#dscam004#4;
;
"RPT",#dscam003#40;
"RPT",#dscam003#40;
;
NULL,RecibosLin;
|FIN;

|PROCESO Agrupados;
   #agif134@#49 = #dscam003#0;
   #agif134@#0  = #dscam003#1;
   |LEE 000#agif134@.=;
   |PARA; |SI FSalida = 0; |Y #agif134@#49 = #dscam003#0; |Y #agif134@#0 = #dscam003#1; |HACIENDO;
      aAlfa  = #agif134@#1; |QBF aAlfa;  aAlfa  = aAlfa  % -104;
      aAlfa1 = #dscam003#4; |QBF aAlfa1; aAlfa1 = aAlfa1 % -104;
      |SI aAlfa = aAlfa1; SwError = 0; |SAL; |FINSI;
      |LEE 000#agif134@.s;
   |SIGUE;
   |SI SwError = 1; |DDEFECTO #agif134@; |FINSI;

   nTotFact = nTotFact + #agif134@#101;
   nTotCom  = nTotCom  + #agif134@#111;
|FPRC;

|DEFBUCLE Agrupados;
#dscam003#8;
;
000000001,nAgrup;
999999999,nAgrup;
;
NULL,Agrupados;
|FIN;

|PROCESO Recibos;
   |SI #dscam003#3 > #cadie005#4; |ACABA; |FINSI;
   |SI #dscam003#71 < #cadie005#17; |ACABA; |FINSI;

   Divisa = #dscam003#57; |HAZ TomaDecim;

   nTotalPdt = #dscam003#31; nTotalDto = 0;
   nTotalPag = 0;
   |SI #dscam003#14 = "T"; |BUCLE RecibosLin; |FINSI;
   |SI nTotalPag = 0; |ACABA; |FINSI;

   |SI #cadie005#5 = "S";
      SwError = 1; |HAZ CargaNDeci;
      |SI #dscam003#49 = "N";
         |SI #dscam003#82 = "S";
            nAgrup = #dscam003#83;
            |SALVA_FICHA #dscam003;
            #dscam003#40 = nAgrup;
            |LEE 000#dscam003.=;
            |SI FSalida = 0;
               |SI #dscam003#49 = "S";
                  |SALVA_FICHA #dscam003;
                  nAgrup = #dscam003#40; nTotFact = 0; nTotCom = 0;
                  |BUCLE Agrupados;
                  |REPON_FICHA #dscam003;
                  |DDEFECTO #agif134@;
                  #agif134@#101 = nTotFact;
                  #agif134@#111 = nTotCom;
               |FINSI;
            |FINSI;
            |REPON_FICHA #dscam003;
         |SINO;
            #agif134@#49 = #dscam003#0;
            #agif134@#0  = #dscam003#1;
            |LEE 000#agif134@.=;
            |PARA; |SI FSalida = 0; |Y #agif134@#49 = #dscam003#0; |Y #agif134@#0 = #dscam003#1; |HACIENDO;
               aAlfa  = #agif134@#1; |QBF aAlfa;  aAlfa  = aAlfa  % -104;
               aAlfa1 = #dscam003#4; |QBF aAlfa1; aAlfa1 = aAlfa1 % -104;
               |SI aAlfa = aAlfa1; SwError = 0; |SAL; |FINSI;
               |LEE 000#agif134@.s;
            |SIGUE;
            |SI SwError = 1; |DDEFECTO #agif134@; |FINSI;
         |FINSI;
      |SINO;
         |SALVA_FICHA #dscam003;
         nAgrup = #dscam003#40; nTotFact = 0; nTotCom = 0;
         |BUCLE Agrupados;
         || Hacer bucle de recibos agrupados, leer su factura correspondiente,
         ||    acumular comision (Campo 111) y Total factura (Campo 101) y
         ||    sobre el defecto de la 1¦ factura escribir dichos acumulados
         |REPON_FICHA #dscam003;
         |DDEFECTO #agif134@;
         #agif134@#101 = nTotFact;
         #agif134@#111 = nTotCom;
      |FINSI;
      #agif025@#0 = #dscam003#33;
      |LEE 000#agif025@.=;
      |SI FSalida ! 0; |DDEFECTO #agif025@; |FINSI;

      #cadie504#11 = #cadie005#7;
      #cadie504#0  = #dscam003#40;
      |LEE 000#cadie504.=;
      |SI FSalida = 0; |BORRA 020#cadie504.a; |FINSI;

      |DDEFECTO #cadie504;
      #cadie504#0  = #dscam003#40;
      |LEE 000#cadie504.];
      |SI FSalida ! 0; |O #cadie504#0 ! #dscam003#40;
         |DDEFECTO #cadie504;
         #cadie504#0  = #dscam003#40;
         #cadie504#1  = #dscam003#0;
         #cadie504#2  = #dscam003#1;
         #cadie504#3  = #dscam003#2;
         #cadie504#4  = #dscam003#32;
         #cadie504#5  = #dscam003#6;
         #cadie504#6  = #dscam003#4;
         #cadie504#7  = #dscam003#3;
         #cadie504#8  = #dscam003#71;
         #cadie504#9  = #dscam003#72;
         #cadie504#10 = #dscam003#70;
         #cadie504#11 = #cadie005#7;
         #cadie504#12 = "01.01.0000";
         #cadie504#13 = #dscam003#78;
         #cadie504#14 = #dscam003#30;
         #cadie504#15 = nTotalPag;
         #cadie504#16 = nTotalPdt;
         #cadie504#17 = (#agif134@#111 * #cadie504#14) / #agif134@#101;
         |SI #agif025@#39 = 0;
            nCalc = (#cadie504#17 * nTotalPag) / #cadie504#14;
         |SINO;
            nCalc = #cadie504#15 % #agif025@#39;
         |FINSI;
         #cadie504#18 = nCalc;
         #cadie504#19 = 0;
         #cadie504#20 = #dscam003#33;
         #cadie504#21 = #dscam003#34;
         |GRABA 020#cadie504.n;
      |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE Recibos;
#dscam003#1;
4,33,46;
000000001,#cadie005#2,DesdeRep,#dscam045#0;
999999999,#cadie005#3,HastaRep,#dscam045#0;
;
NULL,Recibos;
|FIN;

|PROCESO Listado;
   |SI aAntRep ! #cadie504#20;
      |SI Primero = 0;
         Primero = 1;
      |SINO;
         |PIEINF;
      |FINSI;
      aAntRep = #cadie504#20;
   |FINSI;
   |PRINT;
|FPRC;

|DEFBUCLE Listado;
#cadie504#2;
;
DesdeRep, "01.01.0000", #cadie005#7,"    ","          ";
HastaRep, "31.12.9999", #cadie005#7,"zzzz","zzzzzzzzzz";
;
NULL,Listado;
|FIN;

|PROCESO Selec;
   aAlfa = #cadie005#8;
   |PARA nCont = 9; |SI nCont [ 16; |HACIENDO nCont = nCont + 1;
      |C_M #cadie005#nCont#,1,aAlfa;
   |SIGUE;
   |SI aAlfa = "S"; aAlfa = "N"; |SINO; aAlfa = "S"; |FINSI;
   |C_M #cadie005#0,1,aAlfa; |C_M #cadie005#1,1,aAlfa;
|FPRC;

|PROCESO PorEmpresa;
   |SI #cadie005#8 = "N";
      DesdeRep = #cadie005#0; HastaRep = #cadie005#1;
      |BUCLE Recibos;
   |SINO;
      |PARA nCont = 9; |SI nCont [ 16; |HACIENDO nCont = nCont + 1;
         DesdeRep = #cadie005#nCont#; HastaRep = #cadie005#nCont#;
         |BUCLE Recibos;
      |SIGUE;
   |FINSI;
|FPRC;

|PROCESO BucleEmp;

   #dscam044#0 = #dscam045#0;
   |LEE 000#dscam044.=;
   |SI FSalida = 0;
      aAlfa = #dscam044#1; |QBF aAlfa;
      aAlfa1 = aAlfa + "def/agifa134.mas";
      |CARGA_DEF aAlfa1, agif134@;
      |SI FSalida = 0;
         aAlfa1 = aAlfa + "def/agifa025.mas";
         |CARGA_DEF aAlfa1, agif025@;
         aAlfa1 = aAlfa + "fich/" + (("00000" + #dscam044#0) % -105) + "/";
         |PATH_DAT #100 aAlfa1; |PATH_DAT #101 aAlfa1;
         |ABRE #agif134@; |ABRE #agif025@;
         |HAZ PorEmpresa;
         |CIERRA #agif025@; |DESCARGA_DEF #agif025@;
         |CIERRA #agif134@; |DESCARGA_DEF #agif134@;
      |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE BucleEmp;
#dscam045#2;
2;
EmpCartera,"V";
EmpCartera,"V";
;
NULL,BucleEmp;
|FIN;

|PROCESO PonComas;

   Calc = Comodin;
   |SI Calc < 0;
      Calc = 0 - Calc;
      Comodin = Calc;
      SwNeg = 1;
   |SINO;
      SwNeg = 0;
   |FINSI;

   Comodin1 = Comodin;
   Comodin1 = Comodin1 - ".";
   |SI FSalida ! 0;
      Calc = FSalida + 98;
      Comodin1 = Comodin1 % Calc;
      nDecimales = Comodin1;
      aDecimales = Comodin1; |QBF aDecimales;

      LongAlfa = aDecimales % 0; Long = LongAlfa;
      |SI Long < 2;
         Long = 2 - Long;
         aDecimales = aDecimales + ("0" * Long);
      |FINSI;
||       Comodin = Comodin - Comodin1; Comodin = Comodin - ".";
      Calc = Calc - 199; Calc = 100 + ((Calc / 100) ! 0);
      Comodin = Comodin % Calc;
   |SINO;
      nDecimales = 0;
      aDecimales = "00";
   |FINSI;

   Comodin1 = Comodin;
   Comodin = "";
   |PARA; |SI Comodin1 ! ""; |HACIENDO;
      LongAlfa = Comodin1 % 0;
      Long = LongAlfa;
      |SI Long [ 3;
         Cadena = Comodin1;
         Comodin1 = "";
         |QBF Cadena;
         Comodin = Cadena + Comodin;
      |SINO;
         Cadena = Comodin1 % -103;
         Comodin1 = Comodin1 - Cadena;
         Comodin  = "." + Cadena + Comodin;
      |FINSI;
   |SIGUE;
   |SI SwNeg = 1;
      Comodin = "-" + Comodin;
   |FINSI;
   |SI nDecimales ] 0;
      Comodin = Comodin + ",";
      ||SI nDecimales < 10; Comodin = Comodin + "0"; |FINSI;
      Comodin = Comodin + aDecimales;
      ||Comodin = Comodin + nDecimales;
   |FINSI;
|FPRC;

|PROGRAMA;
   |HAZ Tipo8;

   |ABRE #cadie005;
   |LEE 000#cadie005.p;
   |SI FSalida ! 0;
      |DDEFECTO #cadie005;
      |GRABA 020#cadie005.c;
   |FINSI;

   |CLS;
   |CABEZA "Listado retrocesion comisiones recibos vencidos";
   |PINPA #0#0; |PINDA #0#0;
   |ENDATOS #1#0;
   |SI FSalida = 0;
      |GRABA 020#cadie005.a;
      |IMPRE 0;
      |SI FSalida ! 0; |ACABA; |FINSI;
      |INFOR "cadie005";
      |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

      |DFICB aAlfa; |PATH_DAT #12 aAlfa; |PATH_DAT #13 aAlfa;
      |ABRE #cadie504; |ABRE #dscam044;
      |SI #cadie005#6 = "N";
         EmpCartera = #48#0; |BUCLE BucleEmp;
      |FINSI;
      |SI #cadie005#8 = "N";
         DesdeRep = #cadie005#0; HastaRep = #cadie005#1;
         |BUCLE Listado;
      |SINO;
         |PARA nCont = 9; |SI nCont [ 16; |HACIENDO nCont = nCont + 1;
            DesdeRep = #cadie005#nCont#; HastaRep = #cadie005#nCont#;
            |BUCLE Listado;
         |SIGUE;
      |FINSI;

      |PIEINF; |FININF;
      |CIERRA #dscam044; |CIERRA #cadie504;
      |FINIMP;
   |FINSI;

   |CIERRA #cadie005;

   |HAZ Tipo9;
|FPRO;
