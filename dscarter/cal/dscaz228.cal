|FICHEROS;
     dscaz228 #0;
     dscam073 #1;
     dscam049 #2;  || Confirming Cabeceras
     dscam050 #3;  || Confirming Lineas
     dscam008 #4;  || Mantenimiento pagos
     dscam030 #5;  || Mantenimiento enlaces
     dscaw013 #6;  || Temporal enlaces
     dscam051 #7;  || Parametros generales
     dscam014 #8;  || Bancos..............
     dscaw142 #10; || Tmp
     caclipro@ #100;
|FIN;

|VARIABLES;
     &eaEmail = "";
     aMailProv  = "";

     &fecha = @;
     &fp = "";
     &eaPais = "";
     &eaCodPais = "";
     &eaDirProv = "";
     &eaNifProv = "";
     &HayConta = 0;
     &eaCadena = "";

     &eaCodigo  = "";
     &eaTipoCod = "";
     &aCuenta   = "";

     &eaNif       = "";
     &eaNombre    = "";
     &eaApCorreos = "";
     &eaDireccion = "";
     &eaPoblacion = "";
     &eaProvincia = "";
     &eaCP        = "";
     &eaTelefono  = "";
     &eaFax       = "";
     &eaCodBIC    = "";
     &eaCodIBAN   = "";

     &eaVarDni  = "";
     &enCalcCif = 0;
     &aInforme = "";
     &eaNombre1 = "";
     &eaOrigen  = "";

     &Niv1 = 0;
     &Niv2 = 0;
     &Niv3 = 0;
     &Niv4 = 0;
     &Niv5 = 0;
     &Niv6 = 0;
     &Divisa = "";

     nError = 0;
     menR1    = "    No Existe Confirme !!!";
     menR2    = "    Confirme YA contabilizado ";
     menR3    = "2400SConfirme YA Emitido en Papel. Aceptar S/N ";
     menR4    = "2400NConfirme YA lanzado en Diskette. Aceptar S/N ";

     NifProv    = "";
     NombreProv = "";
     DirecProv  = "";
     PoblaProv  = "";
     ProviProv  = "";
     Cp1Prov    = "";
     Cp2Prov    = "";
     TelefProv  = "";
     FaxProv    = "";
     PaisProv   = "";
     Entidad    = "";
     Sucursal   = "";
     DC         = "";
     Cuenta     = "";
     aRef       = " ";

     aCuentaCt = "";
     nNivel = 0;
     aAlfa = "";
     aAlf1 = "";
     nCalc = 0;
     aPago = "";
     nSwImpre = 0;
     nRecibos = 0;
     nTotal = 0;
     nHandle = 0;
     Comodin = "";
     aMensa = "";
     aCtaAnt = "";
     aLF = "";
     aTipo = "";
     nPos = 0;
     nLon = 0;
     nNume = 0;
     nAbo = 0;
     nNu1 = 0;
     nNu2 = 0;
     nDec = 0;
     nTotaReg = 0;
     nImpo = 0;
     aNumPago = "";
     aAlfa1 = "";
     nModulo = 0;
     nCalc1 = 0;
|FIN;

|PROCESO Modulo;
     aAlfa1 = aAlfa % 0101;
     |PARA; |SI aAlfa1 = "0"; |HACIENDO;
          aAlfa = aAlfa % 0299;
          aAlfa1 = aAlfa % 0101;
     |SIGUE;

     |PARA; |SI aAlfa ! ""; |HACIENDO;
          aAlfa1 = aAlfa % 0110; aAlfa = aAlfa % 1199;
          nCalc = aAlfa1;
          nCalc1 = nCalc / nModulo;
          nCalc1 = ((nCalc1 - 0.49) ! 0);
          nCalc1 = nCalc1 * nModulo;
          nCalc = nCalc - nCalc1;
          aAlfa1 = nCalc;
          |SI aAlfa ! ""; aAlfa = aAlfa1 + aAlfa; |FINSI;
     |SIGUE;
     aAlfa = nCalc;
|FPRC;

|PROCESO NumeroPago;
     aNumPago = ("0000000" + aAlfa) % -107;
     || aNumPago = "2434157"; el del ejemplo da cono DC=6

     aAlfa = "9000" + aNumPago;
     nModulo = 7; |HAZ Modulo;
     aAlfa = aNumPago + aAlfa;
|FPRC;

*********************************************************************
|PROCESO Cabecera;
     aAlfa = "03"
     aTipo = "A"; nLon = 2; |HAZ Relleno;    || Registro

     aAlfa = "59"
     aTipo = "A"; nLon = 2; |HAZ Relleno;    || Operacion

     aAlfa = #dscam073#2;
     aTipo = "A"; nLon = 9; |HAZ Relleno;    || Ordenante=Nif

     aAlfa = #dscam073#26;
     aTipo = "N"; nLon = 3; |HAZ Relleno;    || Sufijo

     aAlfa = "";
     aTipo = "A"; nLon = 12; |HAZ Relleno;   || Libre

     aAlfa = "001";
     aTipo = "N"; nLon = 3; |HAZ Relleno;   || N§ dato

     aAlfa = #dscaz228#3;
     aTipo = "F"; nLon = 6; |HAZ Relleno;   || F.Envio

     aAlfa = "";
     aTipo = "A"; nLon = 9; |HAZ Relleno;   || Libre

     aAlfa = #dscam014#42;
     aTipo = "A"; nLon = 24; |HAZ Relleno;   || IBAN

     aAlfa = "";
     aTipo = "A"; nLon = 30; |HAZ Relleno;   || Libre

     nTotaReg = 1;
     |XGRABA nHandle, aLF;
|FPRC;

|PROCESO Proveedor;
|FPRC;

|PROCESO Comun;
     aAlfa = "06"
     aTipo = "A"; nLon = 2; |HAZ Relleno;    || Registro

     aAlfa = "59"
     aTipo = "A"; nLon = 2; |HAZ Relleno;    || Operacion

     aAlfa = #dscam073#2;
     aTipo = "A"; nLon = 9; |HAZ Relleno;    || Ordenante=Nif

     aAlfa = #dscam073#26;
     aTipo = "N"; nLon = 3; |HAZ Relleno;    || Sufijo

     aAlfa = NifProv;
     aTipo = "A"; nLon = 12; |HAZ Relleno;    || Beneficiario=Nif
|FPRC;

|PROCESO Factura;
     |HAZ Comun;
     aAlfa = "010"
     aTipo = "A"; nLon = 3; |HAZ Relleno;    || N§ dato

     aAlfa = NombreProv;
     aTipo = "A"; nLon = 40; |HAZ Relleno;    || Nonbre

     aAlfa = "";
     aTipo = "A"; nLon = 29; |HAZ Relleno;    || Libre

     nTotaReg = nTotaReg + 1;
     |XGRABA nHandle, aLF;


     |HAZ Comun;
     aAlfa = "011"
     aTipo = "A"; nLon = 3; |HAZ Relleno;    || N§ dato

     aAlfa = DirecProv;
     aTipo = "A"; nLon = 45; |HAZ Relleno;    || Domicilio

     aAlfa = "";
     aTipo = "A"; nLon = 24; |HAZ Relleno;    || Libre

     nTotaReg = nTotaReg + 1;
     |XGRABA nHandle, aLF;

||Registro3
     |HAZ Comun;
     aAlfa = "012"
     aTipo = "A"; nLon = 3; |HAZ Relleno;    || N§ dato

     aAlfa = Cp1Prov + Cp2Prov;
     aTipo = "A"; nLon = 5; |HAZ Relleno;    || CP

     aAlfa = PoblaProv;
     aTipo = "A"; nLon = 40; |HAZ Relleno;    || Plaza

     aAlfa = "";
     aTipo = "A"; nLon = 24; |HAZ Relleno;    || Libre

     nTotaReg = nTotaReg + 1;
     |XGRABA nHandle, aLF;

||Registro4
     |HAZ Comun;
     aAlfa = "013"
     aTipo = "A"; nLon = 3; |HAZ Relleno;    || N§ dato

     |SI PaisProv = "ES";
          aAlfa = "         ";
     |SINO;
          aAlfa = Cp1Prov + Cp2Prov;
     |FINSI;
     aTipo = "A"; nLon = 9; |HAZ Relleno;    || CP

     aAlfa = ProviProv;
     aTipo = "A"; nLon = 30; |HAZ Relleno;    || Provincia

     aAlfa = eaPais;
     aTipo = "A"; nLon = 20; |HAZ Relleno;    || Pais

     aAlfa = "";
     aTipo = "A"; nLon = 10; |HAZ Relleno;    || Libre

     nTotaReg = nTotaReg + 1;
     |XGRABA nHandle, aLF;

||Registro5
     |HAZ Comun;
     aAlfa = "014"
     aTipo = "A"; nLon = 3; |HAZ Relleno;    || N§ dato

     aAlfa = #dscam008#40;
     |HAZ NumeroPago;
     aTipo = "N"; nLon = 8; |HAZ Relleno;    || N§ Pago

     aAlfa = #dscam008#3;
     aTipo = "F"; nLon = 8; |HAZ Relleno;    || F. Pago

     nImpo = (#dscam050#20 * 100) ! 0;
     aAlfa = nImpo;
     aTipo = "N"; nLon = 12; |HAZ Relleno;    || Importe

     aAlfa = "0";
     aTipo = "A"; nLon = 1; |HAZ Relleno;    || Indicativo presentacion

     aAlfa = ""; || PaisProv;
     aTipo = "A"; nLon = 2; |HAZ Relleno;    || Pais

     aAlfa = ""; || PaisProv;
     aTipo = "A"; nLon = 6; |HAZ Relleno;    || Código Estadístico del Concepto de pago

     aAlfa = "";
     aTipo = "A"; nLon = 32; |HAZ Relleno;    || Libre

     nTotaReg = nTotaReg + 1;
     |XGRABA nHandle, aLF;

||Tipo de registro Sexto: DETALLE DE PAGO
     |HAZ Comun;
     aAlfa = "015"
     aTipo = "A"; nLon = 3; |HAZ Relleno;    || N§ dato

     aAlfa = #dscam008#40;
     |HAZ NumeroPago;
     aTipo = "N"; nLon = 8; |HAZ Relleno;    || N§ Pago

     aAlfa = #dscam008#0+(("000000"+#dscam008#1)%-106)+(("000"+#dscam008#2)%-103);
     aTipo = "N"; nLon = 12; |HAZ Relleno;    || Referencia

     aAlfa = #dscam008#4;
     aTipo = "F"; nLon = 8; |HAZ Relleno;    || F.Emision

     nImpo = (#dscam050#20 * 100) ! 0;
     |SI nImpo < 0; nImpo = -nImpo; |FINSI;
     aAlfa = nImpo;
     aTipo = "N"; nLon = 12; |HAZ Relleno;    || Importe

     |SI #dscam050#20 > 0; aAlfa = "H"; |SINO; aAlfa = "D"; |FINSI;
     aTipo = "A"; nLon = 1; |HAZ Relleno;    || Signo

     |HAZ Concepto;
     aTipo = "A"; nLon = 28; |HAZ Relleno;   ||Concepto

     nTotaReg = nTotaReg + 1;
     |XGRABA nHandle, aLF;

||Tipo de registro Septimo
     |HAZ Comun;
     aAlfa = "044"
     aTipo = "A"; nLon = 3; |HAZ Relleno;    || N§ dato

     aAlfa = eaCodIBAN;
     aTipo = "A"; nLon = 34; |HAZ Relleno;    || IBAN

     aAlfa = eaCodBIC;
     aTipo = "A"; nLon = 35; |HAZ Relleno;    || BIC

     nTotaReg = nTotaReg + 1;
     |XGRABA nHandle, aLF;

||Tipo de registro Octavo: Mail Proveedor
     |SI aMailProv ! "";
          |HAZ Comun;
          aAlfa = "045"
          aTipo = "A"; nLon = 3; |HAZ Relleno;    || N§ dato

          aAlfa = aMailProv;
          aTipo = "A"; nLon = 69; |HAZ Relleno;    || Mail

          nTotaReg = nTotaReg + 1;
          |XGRABA nHandle, aLF;
     |FINSI;
|FPRC;

|PROCESO Totales;
     nTotaReg = nTotaReg + 1;

     aAlfa = "08"
     aTipo = "A"; nLon = 2; |HAZ Relleno;    || Registro

     aAlfa = "59"
     aTipo = "A"; nLon = 2; |HAZ Relleno;    || Operacion

     aAlfa = #dscam073#2;
     aTipo = "A"; nLon = 9; |HAZ Relleno;    || Ordenante=Nif

     aAlfa = #dscam073#26;
     aTipo = "A"; nLon = 3; |HAZ Relleno;    || Sufijo

     aAlfa = "";
     aTipo = "A"; nLon = 12; |HAZ Relleno;    || Libre

     aAlfa = "";
     aTipo = "A"; nLon = 3; |HAZ Relleno;    || Libre

     nImpo = (nTotal * 100) ! 0;
     aAlfa = nImpo;
     aTipo = "N"; nLon = 12; |HAZ Relleno;    || Importe

     aAlfa = nTotaReg;
     aTipo = "N"; nLon = 10; |HAZ Relleno;    || Registros

     aAlfa = "";
     aTipo = "A"; nLon = 42; |HAZ Relleno;    || Libre
     aTipo = "A"; nLon = 5; |HAZ Relleno;    || Libre

     |XGRABA nHandle, aLF;
|FPRC;
*********************************************************************
|PROCESO Concepto;
     aAlfa = "";
     |SI #dscam073#21 = "S";
          aAlfa = #dscam073#11; |QBF aAlfa;
          |SI #dscam073#12 = "S";
               aAlfa = aAlfa + " " + #dscam008#0 + "/" + (("000000" + #dscam008#1) % -106);
          |FINSI;
          |SI #dscam073#13 = "S";
               aAlfa = aAlfa + " " + (("000" + #dscam008#2) % -103);
          |FINSI;
          |SI #dscam073#14 = "S";
               aAlfa = aAlfa + " " + #dscam008#3;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Relleno;
     |SI aTipo = "F";
          nPos = nLon;
          |SI aAlfa ! "";
               ||aAlfa = (aAlfa % -104) + (aAlfa % 402) + (aAlfa % 102);
               |SI nLon = 6;
                    aAlfa = (aAlfa % 102) + (aAlfa % 402) + (aAlfa % -102);
               |SINO;
                    aAlfa = (aAlfa % 102) + (aAlfa % 402) + (aAlfa % -104);
               |FINSI;
          |FINSI;
          aAlf1 = " " * nPos;
          aAlfa = aAlfa + aAlf1;
          nPos = nPos + 100;
          aAlfa = aAlfa % nPos;
     |FINSI;

     |SI aTipo = "A";
          aAlf1 = " " * nLon;
          aAlfa = aAlfa + aAlf1;
          nPos = nLon + 100;
          aAlfa = aAlfa % nPos;
     |FINSI;

     |SI aTipo = "N";
          nNume = aAlfa;
          |SI nNume < 0; nAbo = -1; |SINO; nAbo = 1; |FINSI;
          |SI nNume < 0; nNume = nNume * - 1; |FINSI;
          nNu1 = nNume ! 0;
          |SI nNu1 > nNume; nNu1 = nNu1 - 1; |FINSI;
          nNu2 = nNume - nNu1;
          |SI nDec < 0;
               aAlfa = nNume;
          |SINO;
               |SI nDec = 0;
                    aAlfa = nNu1;
               |SINO;
                    aAlfa = nNu2;
                    aAlfa = aAlfa + "00000000000000000000000000";
                    nPos = nDec + 300;
                    aAlfa = aAlfa % nPos;

                    || CON PUNTO DECIMAL
                    ||aAlfa = nNu1 + "." + aAlfa;

                    || SIN PUNTO DECIMAL
                    aAlfa = nNu1 + aAlfa;
               |FINSI;
          |FINSI;
          aAlfa = "000000000000000000000000000000000000000000000" + aAlfa;
          |SI nAbo = 1;
               nPos = -1 * (nLon + 100);
               aAlfa = aAlfa % nPos;
          |SINO;
               nPos = -1 * (nLon + 99);
               aAlfa = "-" + aAlfa % nPos;
          |FINSI;
          nDec = 0;
     |FINSI;

     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO Tmp;
     #dscam050#0 = #dscaz228#1;
     #dscam050#1 = #10#1;
     |LEE 020#dscam050.=;

     |HAZ leecl_y_cobro;

     |SI #dscam050#16 = "T";
          aPago = "TRANS.";
     |SINO;
          aPago = "CHEQUE";
     |FINSI;

     eaNifProv = NifProv;
     eaDirProv = DirecProv;

     |ABRE #dscam008;
     #dscam008#40 = #dscam050#13;
     |LEE 000#dscam008.=;
     |SI FSalida ! 0; |DDEFECTO #dscam008; |FINSI;
     |CIERRA #dscam008;

     |SI nSwImpre = 0; |PRINT; |FINSI;

     nRecibos = nRecibos + 1;
     nTotal = nTotal + #dscam050#20;
     ||------------------------------------
     |SI aCtaAnt ! #10#0;
          aCtaAnt = #10#0;
          |HAZ Proveedor;
     |FINSI;
     |HAZ Factura;
|FPRC;

|PROCESO SacaNivel1;
   |QBF aCuentaCt;
   aAlfa = aCuentaCt % 0; nNivel = aAlfa;
   |SI nNivel = Niv1; nNivel = 1; |FINSI;
   |SI nNivel = Niv2; nNivel = 2; |FINSI;
   |SI nNivel = Niv3; nNivel = 3; |FINSI;
   |SI nNivel = Niv4; nNivel = 4; |FINSI;
   |SI nNivel = Niv5; nNivel = 5; |FINSI;
   |SI nNivel = Niv6; nNivel = 6; |FINSI;
|FPRC;

|PROCESO leecl_y_cobro;
     NombreProv = "";
     DirecProv  = "";
     PoblaProv  = "";
     ProviProv  = "";
     Cp1Prov    = "";
     Cp2Prov    = "";
     TelefProv  = "";
     FaxProv    = "";
     PaisProv   = "";
     Entidad    = "";
     Sucursal   = "";
     DC         = "";
     Cuenta     = "";
     aMailProv  = "";

     aRef = " " * 12;

     |DDEFECTO #dscam008;
     #dscam008#40 = #dscam050#13;
     |LEE 001#dscam008.=;

     eaCodigo = #dscam008#32; eaTipoCod = "P"; aCuenta = #dscam008#5;
     |HAZ LeeDatos;

     #caclipro@#23 = "P";
     #caclipro@#10 = #dscam008#5;
     aCuentaCt = #dscam008#5; |HAZ SacaNivel1; #caclipro@#11 = nNivel;
     |LEE 000#caclipro@.=;
     |SI FSalida = 0;
        |SI eaPais = "ESPA¥A"; eaPais = "ESPAÑA"; |FINSI;

        aMailProv  = eaEmail;     |QBF aMailProv;
        NifProv    = eaNif;       |QBF NifProv;
        NombreProv = eaNombre;    |QBF NombreProv;
        DirecProv  = eaDireccion; |QBF DirecProv;
        PoblaProv  = eaPoblacion; |QBF PoblaProv;
        ProviProv  = eaProvincia; |QBF ProviProv;
        Cp1Prov    = eaCP % 0102;
        Cp2Prov    = eaCP % 0303;
        TelefProv  = eaTelefono;  |QBF TelefProv;
        FaxProv    = eaFax;       |QBF FaxProv;
        PaisProv   = #caclipro@#24; |QBF PaisProv;
        |SI #dscaz228#4 = "1"; aRef = #caclipro@#10; |FINSI;
        |SI #dscaz228#4 = "2"; aRef = #caclipro@#0;  |FINSI;
        |QBF aRef; aRef = ((" " * 12) + aRef) % -112;

        aAlfa = "|NC"; nCalc = #caclipro@#aAlfa#;
        |SI nCalc > 29;
           Entidad  = #caclipro@#29;
           Sucursal = #caclipro@#30;
           DC       = #caclipro@#31;
           Cuenta   = #caclipro@#32;
        |SINO;
           aAlfa    = #caclipro@#18; |QBF aAlfa;
           Entidad  = aAlfa % 0104;
           Sucursal = aAlfa % 0504;
           DC       = aAlfa % 0902;
           Cuenta = #caclipro@#17; |QBF Cuenta;
           Cuenta = ("0000000000" + Cuenta) % -110;
        |FINSI;
     |SINO;
        aAlfa = "    No se encuentra el proveedor " + #dscam008#5; |QBF aAlfa;
        aAlfa = aAlfa + " en contabilidad";
        |MENAV aAlfa;
     |FINSI;
|FPRC;

|DEFBUCLE Tmp;
     #10#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tmp;
|FIN;

|PROCESO LineasRem;
     |DDEFECTO #dscam008;
     #dscam008#40 = #dscam050#13;
     |LEE 001#dscam008.=;

     #10#0 = #dscam008#5;
     #10#1 = #dscam050#1;
     |GRABA 020#10n;
|FPRC;

|PROCESO pasecuencial;
     |ABRE #dscam073;
     #dscam073#0 = #dscam049#3;
     |LEE 000#dscam073.=;
     |CIERRA #dscam073;

     |ABRE #dscam049;
     |HAZ leerem;
     |SI nError = 1; |O nError = 2;
          |MENAV "    ERROR !!! CONFIRME NO CORRECTO ";
          |ACABA;
     |FINSI;

     aAlfa = #48#16; |QBF aAlfa;
     aAlf1 = aAlfa + "def/caclipro.mas";
     |CARGA_DEF aAlf1, caclipro@;
     |SI FSalida ! 0;
          |MENAV "    Problemas para abrir el fichero de clientes/proveedores de contabilidad";
          |ERROR; |ACABA;
     |FINSI;
     aAlf1 = aAlfa + "fich/" + (("00000" + #48#14) % -105) + #48#19 + "/";
     |PATH_DAT #100 aAlf1; |ABRE #caclipro@;
     |ABRE #dscam008;

     |DFICE aAlfa;
     aAlfa = aAlfa + "casecmin.seq";
     |XABRE "WB", aAlfa, nHandle;
     |SI FSalida ] 0;
          |HAZ Cabecera;

          aAlfa = Usuario; |NOME_DAT #10 aAlfa; |DELINDEX #10;
          |ABRE #10;
          |BUCLELIN 0LineasRem#dscam049;
          |CIERRA #10;

          |ABRE #dscam050;
          |BUCLE Tmp;
          |CIERRA #dscam050;

          |DELINDEX #10;

          |HAZ Totales;
          |XCIERRA nHandle;
     |FINSI;

     |CIERRA #dscam008;
     |CIERRA #caclipro@; |DESCARGA_DEF #caclipro@;

     |SI HayConta = 1; |Y #dscam049#7 = "N"; |Y #dscaz228#14 ! "    ";
          |ABRE #dscam030;
          #dscam030#0 = #dscaz228#14;
          |LEE 010#dscam030.=;
          |SI FSalida = 0;
               Comodin = Usuario; |QBF Comodin; Comodin = "ac" + Comodin;
               |NOME_DAT #6 Comodin;
               |DELINDEX #dscaw013; |ABRE #dscaw013;
               |DDEFECTO #dscaw013;
               #dscaw013#0  = 1;            #dscaw013#15 = #dscaz228#14;
               #dscaw013#5  = #dscam049#0;  #dscaw013#6  = #dscam049#0;
               #dscaw013#11 = "N";          #dscaw013#12 = 5;
               #dscaw013#13 = "dscam050";   #dscaw013#14 = 0;
               |GRABA 020#dscaw013.n;
               |CIERRA #dscaw013;
               Comodin = #dscaz228#14;
               Comodin = "dscam033.tab;" + Comodin + ",E";
               |LIBERA #dscam049; |CIERRAT #dscam049;
               |LIBERA #dscam050; |CIERRAT #dscam050;
               |SUB_EJECUTA_NP Comodin;
               |ABRET #dscam049; |LEE 101#dscam049.=;
               |ABRET #dscam050; |LEE 101#dscam050.=;
          |FINSI;
          |CIERRA #dscam030;
     |FINSI;

     |SI nRecibos ! 0;
          #dscam049#7 = "D";
          |GRABA 020#dscam049.a; |LIBERA #dscam049;
     |FINSI;
     |CIERRA #dscam049;

     |D_CUADRO 1261, 2280;
     |ATRI R;
     |PINTA 1462, " Numero Recibos   ";
     |PINTA 1662, " Numero Registros ";
     |PINTA 1862, " Importe Total    ";
     |ATRI 0;
     |PINTA 1562, "                  ";
     |PINTA 1762, "                  ";
     |PINTA 1962, "                  ";
     |ATRI 0;

     #dscaz228#10 = nRecibos;  |PINTA #dscaz228#10;
     #dscaz228#11 = nTotaReg;  |PINTA #dscaz228#11;
     #dscaz228#12 = nTotal;    |PINTA #dscaz228#12;

     |DFICE eaOrigen; |QBF eaOrigen;
     eaNombre1 = "casecmin.seq";
     |SUB_EJECUTA "dscaz032.tab";
|FPRC;

|PROCESO leerem;
     nError = 0;
     |DDEFECTO #dscam049;
     #dscam049#0 = #dscaz228#1;
     |LEE 001#dscam049.=;
     |SI FSalida ! 0;
          nError = 1; aMensa = menR1;
     |SINO;
          Divisa = #dscam049#16; |HAZ TomaDecim;
          |SI #dscam049#8 = "S";
               nError = 2; aMensa = menR2;
          |SINO;
               |SI #dscam049#7 = "S";
                    nError = 3; aMensa = menR3;
               |FINSI;
               |SI #dscam049#7 = "D"; nError = 4; aMensa = menR4; |FINSI;
          |FINSI;
          |ABRE #dscam014;
          #dscam014#0 = #dscam049#3;
          |LEE 000#dscam014.=;
          |CIERRA #dscam014;
     |FINSI;
|FPRC;

|PROCESO ver_confirming; |TIPO 6;
     |SI FSalida = 2; |ACABA; |FINSI;

     |ABRE #dscam049;
     |HAZ leerem;
     |CIERRA #dscam049;

     |SI nError ! 0;
          |SI nError < 3;
               |MENAV aMensa;
               |ERROR;
               |ACABA;
          |SINO;
               |CONFI aMensa;
               |SI FSalida ! 0;
                   |ERROR;
                   |ACABA;
               |FINSI;
          |FINSI;
     |FINSI;

     |HAZ PinDatos;
|FPRC;

|PROCESO PinDatos;
    |PINTA 0935, #dscam049#2;   || Vto.
    |PINTA 0958, #dscam049#5;   || N§ Efectos
    |PINTA 1035, #dscam049#3;   || Banco
    |PINTA 1049, #dscam049#19;  || Importe Total
|FPRC;

|PROGRAMA;
     |HAZ Tipo8;

     aLF = &13; aLF = aLF + &10;
     |ABRE #dscam051; |LEE 000#dscam051.p;|CIERRA #dscam051;

     |SI #dscam051#1 = "S";
          #dscaz228#14 = #dscam051#29;
          |C_M #dscaz228#14, 1, "S";
     |SINO;
          #dscaz228#14 = "";
          |C_M #dscaz228#14, 1, "N";
     |FINSI;

     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     #dscam049#2 = "01.01.0000";
     #dscam049#3 = "";
     |HAZ PinDatos;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |ABRE #dscam073;
          #dscam073#0 = #dscam049#3;
          |LEE 000#dscam073.=;
          |CIERRA #dscam073;

          |CONFI "0000N¿Imprimir carta confirme?";
          nSwImpre = FSalida;
          |SI nSwImpre = 0;
               |IMPRE 0;
               |SI FSalida = 0;
                     aInforme = "Carta emision de confirmes"; |HAZ ObtenInforme;
                     |INFOR aInforme;
                     |SI FSalida = 0;
                         fecha = #dscaz228#3;
                         |HAZ pasecuencial;
                         |PIEINF;
                         |FININF;
                     |FINSI;
                     |FINIMP;
               |FINSI;
          |SINO;
               |HAZ pasecuencial;
          |FINSI;
     |FINSI;

     |HAZ Tipo9;
|FPRO;
