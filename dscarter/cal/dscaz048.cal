|FICHEROS;
   dscaz048 #0;

   dscam003 #1;
   dscaw035 #2;
   dscaw078 #3;
   dscam051 #10;
   dscam000 #100;
   &agif024@ #103;

   agifa024@ #24;
|FIN;

|VARIABLES;
   aAlfa = "";
   aPath = "";
   Primero = 1;
   nCont = 0;
   SwError = 0;
   RepAnt = "";

   &aCodigo = "";
   &MaxDigit = 0;
   &SwDetalle = 0;
   &SwPieCli  = 0;
   &Fichero    = ""; &Tipo = ""; &Cmp = ""; &CmpNum = 0;
   &DirCliente = ""; &Cp   = ""; &PobCliente = ""; &ProvCliente = "";
   &Telefono   = ""; &Fax  = ""; &Movil      = ""; &PersCont    = "";
   &aInforme = "";
   &eaCodigo    = "";
   &eaTipoCod   = "";
   &aCuenta     = "";
   &eaNif       = "";
   &eaNombre    = "";
   &eaDireccion = "";
   &eaPoblacion = "";
   &eaProvincia = "";
   &eaCP        = "";
   &eaCP1       = "";
   &eaCP2       = "";
   &eaTelefono  = "";
   &TipoCliente = ""; &CodCliente = "";
   &DCampoG = 0;      &HCampoG = 0;
   &DCampoC = 0;      &HCampoC = 0;

   CodCliAnt = ""; CtaCliAnt = "";
   nTecla = 0;

   Comodin = ""; Comodin1 = "";
   Cuenta = "";
   aLong = ""; nLong = 0;
   Parte1 = "";
   Parte2 = "";
   Posic1 = 0;
   Posic2 = 0;
   FEstado = 0;

   aAlfa1 = "";
   aAlfa2 = "";
|FIN;

|PROCESO MiraCliente;
   |SI FSalida = 10;
      TipoCliente = "G"; DCampoG = 28; HCampoG = 29;
      |HAZ ExisteCliente;
      #dscaz048#Campo# = CodCliente; |PINTA #dscaz048#Campo#;
   |FINSI;
|FPRC;

|PROCESO MiraCuenta;
   aAlfa1 = " " * 12; aAlfa2 = "z" * 12;
   |SI #dscaz048#30 ! aAlfa1; |O #dscaz048#31 ! aAlfa2;
      #dscaz048#28 = "      "; |PINTA #dscaz048#28;
   |FINSI;

   |SI FSalida = 10;
      TipoCliente = "C"; DCampoC = 30; HCampoC = 31;
      |HAZ ExisteCliente;
      #dscaz048#Campo# = CodCliente; |PINTA #dscaz048#Campo#;
   |FINSI;
|FPRC;

|PROCESO PuntoCuenta; |TIPO 6;
   nTecla = FSalida;
      Cuenta = #Fichero@#Campo#;
      Cuenta = Cuenta - ".";
      |PARA; |SI FSalida ! 0; |HACIENDO;
         FEstado = FSalida;
         Posic1 = ((FEstado / 100) ! 0) + 99;
         Posic2 = FEstado + 98;
         Parte1 = Cuenta % Posic1;
         Parte2 = Cuenta % Posic2;
         |QBF Cuenta;
         aLong = Cuenta % 0; nLong = aLong;
         nLong = MaxDigit - nLong;
         Cuenta = "0" * nLong;
         Cuenta = Parte1 + Cuenta + Parte2;
         Cuenta = Cuenta - ".";
      |SIGUE;
      #Fichero@#Campo# = Cuenta; |PINTA #Fichero@#Campo#;
   FSalida = nTecla;
|FPRC;

|PROCESO SelRep;
   |SI #dscaz048#0 = "S";
      |PARA nCont = 1; |SI nCont [ 10; |HACIENDO nCont = nCont + 1;
         |C_M #dscaz048#nCont#,1,"S";
      |SIGUE;
      #dscaz048#32 = "  "; |PINTA #dscaz048#32; |C_M #dscaz048#32,1,"N";
      #dscaz048#33 = "zz"; |PINTA #dscaz048#33; |C_M #dscaz048#33,1,"N";
   |SINO;
      |PARA nCont = 1; |SI nCont [ 10; |HACIENDO nCont = nCont + 1;
         #dscaz048#nCont# = "  "; |PINTA #dscaz048#nCont#; |C_M #dscaz048#nCont#,1,"N";
      |SIGUE;
      |C_M #dscaz048#32,1,"S"; |C_M #dscaz048#33,1,"S";
   |FINSI;
|FPRC;

|PROCESO SelTR;
   |SI #dscaz048#11 = "S";
      |PARA nCont = 12; |SI nCont [ 21; |HACIENDO nCont = nCont + 1;
         |C_M #dscaz048#nCont#,1,"S";
      |SIGUE;
      #dscaz048#26 = 01; |PINTA #dscaz048#26; |C_M #dscaz048#26,1,"N";
      #dscaz048#27 = 99; |PINTA #dscaz048#27; |C_M #dscaz048#27,1,"N";
   |SINO;
      |PARA nCont = 12; |SI nCont [ 21; |HACIENDO nCont = nCont + 1;
         #dscaz048#nCont# = 0; |PINTA #dscaz048#nCont#; |C_M #dscaz048#nCont#,1,"N";
      |SIGUE;
      |C_M #dscaz048#26,1,"S"; |C_M #dscaz048#27,1,"S";
   |FINSI;
|FPRC;

|PROCESO Imprime;

   |SI CodCliAnt ! #dscaw035#3; |O CtaCliAnt ! #dscaw035#4;
      |SI #dscaw035#3 = "      ";
         |SI CtaCliAnt ! #dscaw035#4;
            |SI Primero = 0;
               SwPieCli = 1; SwDetalle = 0; |PRINT; SwPieCli = 0;
            |FINSI;
         |FINSI;
      |SINO;
         |SI Primero = 0;
            SwPieCli = 1; SwDetalle = 0; |PRINT; SwPieCli = 0;
         |FINSI;
      |FINSI;
      CodCliAnt = #dscaw035#3;
      CtaCliAnt = #dscaw035#4;
      #dscaw078#0 = CodCliAnt;
      |LEE 000#dscaw078.=;
      |SI FSalida ! 0; |DDEFECTO #dscaw078; |FINSI;
   |FINSI;

   |SI RepAnt ! #dscaw035#0;
      |SI Primero = 1;
         Primero = 0;
      |SINO;
         |PIEINF;
      |FINSI;
      RepAnt = #dscaw035#0;
   |FINSI;

   SwDetalle = 1;

   #dscam003#40 = #dscaw035#9; |LEE 000#dscam003.=; |HAZ TomaDecim;
   eaCodigo = #dscam003#32; eaTipoCod = "C"; aCuenta = #dscam003#5; |HAZ LeeDatos;
   DirCliente = eaDireccion;
   Cp = eaCP;
   PobCliente = eaPoblacion;
   ProvCliente = eaProvincia;
   Telefono = eaTelefono;

      Fichero = "Clientes"; Tipo = "G";
      Cmp = "Codigo Cliente"; |HAZ ObtenDato;
      #agif024@#CmpNum# = #dscam003#32;
      |LEE 000#agif024@.=;
      |SI FSalida = 0; |Y #agif024@#CmpNum# ! "      ";
/*
         Fichero = "Clientes"; Tipo = "G"; Cmp = "Direccion Cobro"; |HAZ ObtenDato;
         DirCliente  = #agif024@#CmpNum#;
         Fichero = "Clientes"; Tipo = "G"; Cmp = "Codigo Postal Cobro"; |HAZ ObtenDato;
         Cp          = #agif024@#CmpNum#;
         Fichero = "Clientes"; Tipo = "G"; Cmp = "Poblacion Cobro"; |HAZ ObtenDato;
         PobCliente  = #agif024@#CmpNum#;
         Fichero = "Clientes"; Tipo = "G"; Cmp = "Provincia Cobro"; |HAZ ObtenDato;
         ProvCliente = #agif024@#CmpNum#;
         Fichero = "Clientes"; Tipo = "G"; Cmp = "Telefono"; |HAZ ObtenDato;
         Telefono = #agif024@#CmpNum#;
*/
         Fichero = "Clientes"; Tipo = "G"; Cmp = "Fax"; |HAZ ObtenDato;
         Fax = #agif024@#CmpNum#;
         Fichero = "Clientes"; Tipo = "G"; Cmp = "Movil"; |HAZ ObtenDato;
         Movil = #agif024@#CmpNum#;
         Fichero = "Clientes"; Tipo = "G"; Cmp = "Persona Contacto"; |HAZ ObtenDato;
         PersCont = #agif024@#CmpNum#;
      |FINSI;

   aCodigo = #dscam003#32; |QBF aCodigo;
   |SI aCodigo = "";
      aCodigo = #dscam003#5; |QBF aCodigo;
   |FINSI;
   |SI #dscam051#104 = "S";
      |SI #dscam003#70 ! "          "; |O #dscam003#72 ! "    ";
         #dscam003#3 = #dscam003#71;
      |FINSI;
   |FINSI;
   |SI #0#40 = "C"; |HAZ NonComerCli; |FINSI;
   |PRINT;
|FPRC;

|PROCESO NonComerCli;
/*
     aAlfa = aPath + "fich/" + (("00000" + #dscam003#44)%-105) + "/";
     |PATH_DAT #agifa024@#aAlfa#;
*/
     |ABRE #agifa024@;
     #agifa024@#0 = #dscam003#32;
     |LEE 000#agifa024@.=;
     |SI FSalida = 0;
          #dscam003#6 = #agifa024@#2;
     |FINSI;
     |CIERRA #agifa024@;
|FPRC;

|DEFBUCLE Imprime1;
#dscaw035#1;
;
INICIO;
FINAL;
;
NULL,Imprime;
|FIN;

|PROCESO Temporal;

   |SI #dscam003#14 = "A"; |O #dscam003#14 = "D"; |ACABA; |FINSI;
   |SI #dscam003#69 ! " "; |O #dscam003#47 = "S";
      #dscaw078#0 = #dscam003#32;
      |LEE 000#dscaw078.=;
      |SI FSalida ! 0;
         |DDEFECTO #dscaw078;
         #dscaw078#0 = #dscam003#32;
         |GRABA 020#dscaw078.b;
      |FINSI;
      #dscaw078#1 = #dscaw078#1 + #dscam003#30;
      |GRABA 020#dscaw078.a; |LIBERA #dscaw078;
      |ACABA;
   |FINSI;

   |SI #dscam051#104 = "S";
      |SI #dscam003#70 ! "          "; |O #dscam003#72 ! "    ";
         |SI #dscam003#71 < #dscaz048#34; |O #dscam003#71 > #dscaz048#35; |ACABA; |FINSI;
      |SINO;
         |SI #dscam003#3 < #dscaz048#34; |O #dscam003#3 > #dscaz048#35; |ACABA; |FINSI;
      |FINSI;
   |SINO;
      |SI #dscam003#3 < #dscaz048#34; |O #dscam003#3 > #dscaz048#35; |ACABA; |FINSI;
   |FINSI;

   |SI #dscam003#14 = "I";
      |SI #dscaz048#41 = "N"; |ACABA; |FINSI;
   |SINO;
      |SI #dscaz048#38 = "L";
         |SI #dscam003#14 ! "L"; |ACABA; |FINSI;
      |FINSI;
      |SI #dscaz048#38 = "P";
         |SI #dscam003#14 ! "P"; |Y #dscam003#14 ! "C"; |ACABA; |FINSI;
      |FINSI;
   |FINSI;

   SwError = 1;            || Comprueba el representante
   |SI #dscaz048#0 = "S";
      |PARA nCont = 1; |SI nCont [ 10; |HACIENDO nCont = nCont + 1;
         |SI #dscaz048#nCont# = #dscam003#33;
            SwError = 0; |SAL;
         |FINSI;
      |SIGUE;
   |SINO;
      |SI #dscaz048#32 [ #dscam003#33; |Y #dscam003#33 [ #dscaz048#33;
         SwError = 0;
      |FINSI;
   |FINSI;
   |SI SwError = 1; |ACABA; |FINSI;

   SwError = 1;            || Comprueba el tipo de recibo
   |SI #dscaz048#11 = "S";
      |PARA nCont = 12; |SI nCont [ 21; |HACIENDO nCont = nCont + 1;
         |SI #dscaz048#nCont# = #dscam003#12;
            SwError = 0; |SAL;
         |FINSI;
      |SIGUE;
   |SINO;
      |SI #dscaz048#26 [ #dscam003#12; |Y #dscam003#12 [ #dscaz048#27;
         SwError = 0;
      |FINSI;
   |FINSI;
   |SI SwError = 1; |ACABA; |FINSI;

   |DDEFECTO #dscaw035;
   #dscaw035#0 = #dscam003#33;
   #dscaw035#1 = #dscam003#34;
   #dscaw035#2 = #dscam003#12;
   #dscaw035#3 = #dscam003#32;
   #dscaw035#4 = #dscam003#5;
   #dscaw035#5 = #dscam003#3;
   #dscaw035#6 = #dscam003#0;
   #dscaw035#7 = #dscam003#1;
   #dscaw035#8 = #dscam003#2;
   #dscaw035#9 = #dscam003#40;
   |GRABA 020#dscaw035.n;
|FPRC;

|DEFBUCLE Temporal;
#dscam003#1;
0,1,32,5;
000000001,#0#22,#0#24,#0#28,#0#30;
999999999,#0#23,#0#25,#0#29,#0#31;
;
NULL,Temporal;
|FIN;

|PROCESO Tipo; |TIPO 0;
     |SI #dscaz048#38 = "P"; |O #dscaz048#38 = "A";
          #dscaz048#41 = "S";
          |C_M #dscaz048#41, 1, "S";
     |SINO;
          |C_M #dscaz048#41, 1, "N";
          #dscaz048#41 = "N";
     |FINSI;
     |PINTA #dscaz048#41;
|FPRC;

|PROGRAMA;
   |HAZ Tipo8;

   |CLS;
   |CABEZA "Informe de cobros por representante";

   |SI PARAMETRO ! ""; #dscaz048#39 = PARAMETRO; |FINSI;


   |PINPA #0#0; |PINDA #0#0;

   |ABRE #dscam051;
   |LEE 000#dscam051.p;
   |SI FSalida ! 0; |DDEFECTO #dscam051; |FINSI;
   |CIERRA #dscam051;

   |ENDATOS #1#0;
   |SI FSalida = 0;
      |SI #0#40 = "C";
        |DBASE aPath;
        aAlfa = aPath + "fich/";
        |PATH_DAT #100 aAlfa;
        |DFICE aPath;
        aAlfa = aPath % -205;
        |ABRE #100;
        #100#0 = aAlfa;
        |LEE 000#100=;
        |CIERRA #100;

        aPath = #100#17; |QBF aPath;
        aAlfa = aPath + "def/agifa024";
        |CARGA_DEF aAlfa, agifa024@;
        |SI FSalida ! 0;
             |MENAV "0000Error al cargar fichero clientes de gestion....";
             |ACABA;
        |FINSI;
        aAlfa = aPath + "fich/" + (("00000" + #100#15)%-105) + "/";
        |PATH_DAT #agifa024@#aAlfa#;
      |FINSI;

      |IMPRE 0;
      |SI FSalida ! 0; |ACABA; |FINSI;
      aInforme = "Informe cobros representante"; |HAZ ObtenInforme;
      |INFOR aInforme;
      |SI FSalida ! 0; |ACABA; |FINSI;
      Comodin = #48#17; |QBF Comodin;
      Comodin1 = ("00000" + #48#15) % -105;
      Comodin1 = Comodin + "fich/" + Comodin1 + "/";
      |PATH_DAT #3 Comodin1;
      |ABRE #agif024@;
      |DELINDEX #dscaw035; |ABRE #dscaw035; |ABRE #dscam003;
      |DELINDEX #dscaw078; |ABRE #dscaw078;

      |BUCLE Temporal;
      |SI #dscaz048#39 = "R"; |BUCLE Imprime1; |FINSI;
      |CIERRA #dscaw078;
      |CIERRA #dscam003; |CIERRA #dscaw035; |CIERRA #agif024@;
      |PIEINF;
      |FININF;
      |FINIMP;
      |SI #0#40 = "C"; |DESCARGA_DEF #agifa024@; |FINSI;
   |FINSI;
   |HAZ Tipo9;
|FPRO;
