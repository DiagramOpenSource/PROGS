|FICHEROS;
     taglz003 #0;
     referen@ #1;
     taglw002 #19;
     taglw003 #3;
     dscam013 #13;
     dscam067 #67;
|FIN;

|VARIABLES;
     aClave1 = "";
     aClave2 = "";
     conta = 0;
     nNume = 0;
     aCar = "";
     nOk = 0;
     i = 0;
     nLonCam = 0;
     nCamClave = 0;
     nPosCam = 0;
     nPos = 0;
     nCampo = 0;
     aValor = "";
     aCamClave = "";
     aValClave = "";
     aCampo = "";
     aBase = "";
     aPath = "";
     aDes = "";
     aFic = "";
     aAlfa = "";
     aOrg = "";
     aEmpAnt = "";
     aDefAnt = "";
     aEmp = "";
     aLon = "";
     aPathApli1 = "";
     aPathApli2 = "";
     aFicDef = "";
     nSwCarga = 0;
|FIN;

|PROCESO dscam067;
     |BORRA 020#67a;
|FPRC;

|DEFBUCLE dscam067;
     #67#1;
     ;
     "RM", aClave1, aClave2, "V", 000000, 00000, 0;
     "RM", aClave1, aClave2, "V", 999999, 99999, 9;
     be;
     NULL, dscam067;
|FIN;

|PROCESO Tmp;
     #13#0 = #3#0;
     #13#1 = #3#1;
     |LEE 020#13=;
     |PRINT;
|FPRC;

|DEFBUCLE Tmp;
     #3#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tmp;
|FIN;

|PROCESO LeeReg;
     aValClave = #19#7;
     aAlfa = "|K000";
     aCamClave = #1aAlfa;
     aAlfa = aCamClave % 103;
     nCamClave = aAlfa;
     nPos = 4;
     nPosCam = 1;
     |PARA i = 1; |SI i [ nCamClave; |HACIENDO i = i + 1;
          nNume = nPos * 100 + 3;
          aCampo = aCamClave % nNume;
          nCampo = aCampo;

          aAlfa = "|C" + aCampo + "LONGITUD";
          aAlfa = #1aAlfa;
          nLonCam = aAlfa;
          nNume = nPosCam * 100 + nLonCam;
          aValor = aValClave % nNume;

          #1nCampo = aValor;
          nPos = nPos + 3;
          nPosCam = nPosCam + nLonCam + 1;
     |SIGUE;
     |LEE 101#1=;
     |SI FSalida ! 0; |GRABA 020#1b; |FINSI;
|FPRC;

|PROCESO CargaDef;
     aAlfa = aPathApli1 + "def/" +  aFicDef;
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida ! 0; |Y aPathApli2 ! "";
          aAlfa = aPathApli2 + "def/" + aFicDef;
          |CARGA_DEF aAlfa, referen@;
          aPathApli1 = aPathApli2;
     |FINSI;
     nSwCarga = FSalida;

     |BLIN 21; |PINTA 2102, aAlfa;
|FPRC;

|PROCESO Log;
     nOk = 0;

     |SI #19#3 = "C"; nOk = 1; |FINSI;  || tipo
     |SI #19#3 = "R"; |Y #19#4 = "32"; nOk = 1; |FINSI; ||clave accion

     |SI nOk = 0; |ACABA; |FINSI;

     aFicDef = #19#5; |QBF aFicDef;
     aEmp = #19#9;    |QBF aEmp;

     |SI aFicDef ! "dscam013"; |ACABA; |FINSI;  || solo remesas

    ||.................. la ponemos fija
     |DFICE aAlfa;
     aEmp = aAlfa % -205;
     |DBASE aBase;
     aPathApli1 = aBase;

/************************************ || al ser fija esto no vale
     aLon = aEmp % A0;
     |SI aLon = 5;  || Cartera/Dscomer9
          aPathApli1 = aBase + "dscomer9";
          aPathApli2 = aBase + "dscarter";
     |FINSI;
     |SI aLon = 6;  || Agicont
          aPathApli1 = aBase + "agicont";
          aPathApli2 = "";
     |FINSI;
     |SI aLon = 2;  || Dscomer8
          aPathApli1 = aBase + "dscomer8";
          aPathApli2 = "";
     |FINSI;
*************************/
     |SI aEmp ! aEmpAnt; |O aFicDef ! aDefAnt;
          |SI nSwCarga = 0;
               |CIERRA #1;
               |DESCARGA_DEF #1;
          |FINSI;
          aEmpAnt = aEmp;
          aDefAnt = aFicDef;
          |HAZ CargaDef;
          |SI nSwCarga = 0;
               aAlfa = aPathApli1 + "fich/" + aEmp + "/";
               |PATH_DAT #1 aAlfa;
               |ABRE #1;
               |BLIN 22; |PINTA 2202, aAlfa;
          |FINSI;
     |FINSI;
     |SI nSwCarga ! 0; |ACABA; |FINSI;

     |DDEFECTO #1;
     |HAZ LeeReg;

     |SI #1#6 > "07.09.2006";
          |SI #0#2 = "I";
               #3#0 = #1#0;
               #3#1 = #1#1;
               |GRABA 020#3n;
          |SINO;
               #1#15 = "N";
               |GRABA 020#1a;
               aClave1 = #1#0;
               aClave2 = #1#1;
               |BUCLE dscam067;
          |FINSI;
     |FINSI;
     |LIBERA #1;

     conta = conta + 1;
     |SI conta < 0; |ERROR10; |FINSI; || para depurar
|FPRC;

|DEFBUCLE Log;
     #19#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, NULL, NULL, NULL, NULL, Log;
     #19#9, #19#5, #19#1, #19#2; ||Empresa, Def, Fecha, Hora
|FIN;

|PROCESO Tipo3; |TIPO 3;
     aAlfa = #0#1; |QBF aAlfa;
     aLon = aAlfa % A0;
     aPath = "";
     |PARA i = aLon; |SI i ] 1; |HACIENDO i = i - 1;
          nPos = i * 100 + 1;
          aCar = aAlfa % nPos;
          |SI aPath ! "";
               aPath = aCar + aPath;
          |SINO;
               |SI aCar = "/"; |O aCar = "\";
                    aPath = aCar;
               |SINO;
                    aFic = aCar + aFic;
               |FINSI;
          |FINSI;
     |SIGUE;

     aFic = Usuario;
     |DFICE aAlfa;
FSalida = 0;

     aDes = aAlfa + aFic + ".ixx";
     |FBORRA aDes;
     aDes = aAlfa + aFic + ".ddx";
     |FBORRA aDes;
     aDes = aAlfa + aFic + ".dat";
     aOrg = #0#1; |QBF aOrg;
     |COPIA_FICHERO aOrg, aDes;

     |SI FSalida ! 0;
          |MENAV "0000No puedo copiar el fichero, verifique los permisos";
     |SINO;
          |NOME_DAT #19 aFic;
          aEmpAnt = "";
          nSwCarga = -1;
||ABRE #19;
||CONSULTA_CLAVE #1#19;
||CIERRA #19;
          aAlfa = "1"+Usuario;
          |NOME_DAT #3 aAlfa;
          |DELINDEX #3;
          |ABRE #3;
          |BUCLE Log;
          |CIERRA #3;
          |DELINDEX #19;
          |SI nSwCarga = 0;
               |CIERRA #1;
               |DESCARGA_DEF #1;
          |FINSI;

          |SI #0#2 = "I";
               |IMPRE 0;
               |SI FSalida = 0;
                    |INFOR "taglz003";
                    |SI FSalida = 0;
                         |ABRE #13;
                         |BUCLE Tmp;
                         |CIERRA #13;
                         |PIEINF;
                         |FININF;
                    |SINO;
                         |MENAV "0000No existe informe taglz003";
                    |FINSI;
                    |FINIMP;
               |FINSI;
          |FINSI;
          |DELINDEX #3;
     |FINSI;
|FPRC;

|PROGRAMA;
     |CLS;
     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     #0#2 = "I";
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |GRABA 020#0a;
     |FINSI;
     |CIERRA #0;
|FPRO;
