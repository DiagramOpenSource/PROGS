|FICHEROS;
     dscaz218 #0;
     dscaw141 #1,MANTE;
|FIN;

|VARIABLES;
     &eaImpre = "";
     &eaCliente = "";
     &enCodigo = 0;
     &efFecha = @;

     aIP = "";
     aServidor = "";
     aFrom = "";
     aTo = "";
     aSubject = "";
     aMensaje = "";
     aDjunto = "";
     aAlfa = "";
     nBtnFic = 0;
     aDato = "";
     nHandle = 0;
     nCampo = 0;
     aLon = "";
     i = 0;
     nPos = 0;
     aLetra = "";
     aReg = "";
     aSepara = "";
     nMarca = 0;
     aMarca = "";
     aPdf = "";
     aOrg = "";
     aIpRemoto = "";
     eaAlfa = "";
     aCar = "";
 {1}aBaseLocal = "";
 {99}Valor = "";
|FIN;

|PROCESO QuitaRarosExp;
     |QBF eaAlfa;
     aAlfa = eaAlfa;
     aLon = aAlfa % A0;
     aDato = "";
     |PARA i = 0; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = (i + 1) * 100 + 1;
          aCar = aAlfa % nPos;
          |SI aCar = "¥"; aCar = &209; |FINSI;
          |SI aCar = "¤"; aCar = &241; |FINSI;
          |SI aCar = " "; aCar = &225; |FINSI;
          |SI aCar = "‚"; aCar = &233; |FINSI;
          |SI aCar = "¡"; aCar = &237; |FINSI;
          |SI aCar = "¢"; aCar = &243; |FINSI;
          |SI aCar = "£"; aCar = &250; |FINSI;
          |SI aCar = "§"; aCar = &176; |FINSI;
          |SI aCar = "¦"; aCar = &170; |FINSI;
          |SI aCar = "°"; aCar = &176; |FINSI;  ||Euro
          aDato = aDato + aCar;
     |SIGUE;
     eaAlfa = aDato;
|FPRC;

|PROCESO Cliente;
     aAlfa = #1#3; |QBF aAlfa;
     |SI aAlfa = "";
          aAlfa = "0000Cliente sin mail informado [" + #1#0 + "]";
          |MENAV aAlfa;
          |ACABA;
     |FINSI;

     |DFICE aAlfa;
     aPdf = aAlfa + #1#0 + ".pdf";
     |SI aIpRemoto = "";
          eaImpre = "CrystalReport;" + aPdf;
     |SINO;
          |ESPECIFICA "RBASS", aBaseLocal;
          aOrg = aBaseLocal1 + #1#0 + ".pdf";
          eaImpre = "CrystalReport;" + aOrg;
     |FINSI;
     eaCliente = #1#0;
     enCodigo = #0#3;
     efFecha = #0#2;
     |SUB_EJECUTA_NP "dscaz217";
     |SI aIpRemoto ! "";
          |RECIBE_FICHERO aOrg, aPdf;
          |RFBORRA aOrg;
     |FINSI;

     |XABRE "E", aPdf, 0;
     |SI FSalida ] 0;
          aIP = "xdscorreo.dv.diagram.net";
          aServidor = "asmtp.diagram.es";
          aFrom = #0#4; |QBF aFrom;
          aTo = #1#3; |QBF aTo;
          aSubject = #0#5; |QBF aSubject;
          aMensaje = #0#6; |QBF aMensaje;
          aMensaje = aMensaje + " " + #0#7; |QBF aMensaje;

          eaAlfa = aMensaje; |HAZ QuitaRarosExp; aMensaje = eaAlfa;
          eaAlfa = aSubject; |HAZ QuitaRarosExp; aSubject = eaAlfa;

          aDjunto = aPdf;
          |DSCORREO_ENVIA aIP, aServidor, aFrom, aTo, aSubject, aMensaje, aDjunto;
          |SI FSalida < 0;
               aAlfa = "0000Error al enviar PDF cliente [" + #1#0 + "]";
               |MENAV aAlfa;
          |FINSI;
          |FBORRA aPdf;
     |SINO;
          aAlfa = "0000Error al crear PDF cliente [" + #1#0 + "]";
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|DEFBUCLE Cliente;
     #1#1;
     4;
     "      ", "*";
     "zzzzzz", "*";
     ;
     NULL, Cliente;
|FIN;

|PROCESO Tmp;
     #1#4 = aMarca;
     |SI #1#4 = "*";
          nMarca = nMarca + 1;
     |FINSI;
     |GRABA 020#1a;
|FPRC;

|DEFBUCLE Tmp;
     #1#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tmp;
|FIN;

|PROCESO MarcaTodo; |TIPO 11;
     |SI FSalida = 13;
          |SI #1#4 = " ";
               aMarca = "*";
          |SINO;
               aMarca = " ";
          |FINSI;
          nMarca = 0;

          |PONCONTROL 23, "SI";
          |SALVA_DATOS #1;
          |BUCLE Tmp;
          |REPON_DATOS #1;
          |LEE 000#1=;
     |FINSI;
|FPRC;

|PROCESO Marca; |TIPO 0;
     |SI #1Campo = Contenido;
          |SI #1#4 = "*";
               #1#4 = "";
               nMarca = nMarca - 1;
          |SINO;
              #1#4 = "*";
               nMarca = nMarca + 1;
          |FINSI;
          |PINTA #1#4;
          |GRABA 020#1a;
     |FINSI;
|FPRC;

|PROCESO Graba;
     |QBF Valor1;
     #1#0 = ("000000" + Valor1) % -106;
     #1#1 = Valor2;
     #1#2 = Valor3;
     #1#3 = Valor4;
     |GRABA 000#1n;
|FPRC;

|PROCESO Dato;
     aSepara = ",";
     nCampo = 1;
     IValor = Valor1<-;
     aLon = aDato % A0;
     aReg = "";
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aLetra = aDato % nPos;
          |SI aLetra = aSepara;
               Valor = aReg;
               aReg = "";
               nCampo = nCampo + 1;
               IValor = IValor + 1;
          |SINO;
               aReg = aReg + aLetra;
          |FINSI;
          |SI nCampo > 90; |SAL; |FINSI;     || Por si.... :)
     |SIGUE;
     Valor = aReg;
|FPRC;

|PROCESO Min;
     #1#0 = "      ";
|FPRC;

|PROCESO Max;
     #1#0 = "zzzzzz";
|FPRC;

|PROCESO T3; |TIPO 3;
     aAlfa = #0#4; |QBF aAlfa;
     |SI aAlfa = "";
          |MENAV "0000Debe de indicar mail origen...";
          |ACABA;
     |FINSI;

     aIpRemoto = ""; |IP_REMOTO aIpRemoto;
     |ESTADO_CONTROL nBtnFic, "HIDE";

     aAlfa = Usuario;
     |NOME_DAT #1 aAlfa; |DELINDEX #1;
     |ABRE #1;

     aAlfa = #0#1; |QBF aAlfa;
     |XABRE "C", aAlfa, nHandle;
     |SI FSalida = 0;
          |XLEE nHandle, 250, aDato;
          |PARA; |SI FSalida > 0; |HACIENDO;
               |HAZ Dato;
               |HAZ Graba;
               |XLEE nHandle, 250, aDato;
          |SIGUE;
          |XCIERRA nHandle;

          |LEE 000#1p;
          |SI FSalida = 0;
               |PINPA #0#1;
               |ENTLINEAL #1#1, 1, 4, 20, 0, Min, Max;
               |SI nMarca > 0;
                    |CONFI "0000SEnviar carta a los clientes seleccionados S/N:";
                    |SI FSalida = 0;
                         |BUCLE Cliente;
                    |FINSI;
               |FINSI;
          |SINO;
               |MENAV "0000No hay registros...";
          |FINSI;
     |SINO;
          |MENAV "0000Error al abrir el fichero...";
     |FINSI;

     |CIERRA #1;
|FPRC;

|PROCESO T0; |TIPO 0;
     |SI FSalida  = 13;
          aAlfa = "F*.csv";
          |BROWSE aAlfa;

          aAlfa = aAlfa % 2; |QBF aAlfa;
          |SI aAlfa = "F"; aAlfa = ""; |FINSI;

          |QBF aAlfa;
          |SI aAlfa = "";
               |ERROR;
          |SINO;
               #0#1 = aAlfa; |PINTA #0#1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO BtnFic;
     |PTEC 824;
|FPRC;

|PROGRAMA;
     |CLS;
     |CONTROL_SIMPLE 0, "BOTON,F5.- Fichero ", 805, 813, BtnFic;
     nBtnFic = FSalida;

     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     #0#2 = ~;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0; |GRABA 020#0a; |FINSI;
     |CIERRA #0;

     |FIN_CONTROL_WINDOWS nBtnFic;
     |PULSATECLA;
|FPRO;
