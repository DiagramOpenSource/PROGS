     Org dscaz216

|FICHEROS;
     dscaz224 #0;
     dscam049 #49;  || Confirming Cabeceras
     dscam050 #50;  || Confirming Lineas
     dscam073 #73;
     dscam008 #8;  || Mantenimiento pagos
     dscam030;  || Mantenimiento enlaces
     dscaw013;  || Temporal enlaces
     dscam051;  || Parametros generales
     dscam014 #14;  || Bancos..............
     dscam040;  || Provincias

     caclipro@;
     dsam0104@;
     agim0016@;
     agifa112@;
     dsm00225@;

     dscaw225 #225; ||Tmp
|FIN;

|VARIABLES;
     &enConfirme = 0;
     &eaRefeCli  = "";
     &efFecSop   = @;
     &efFecPro   = @;
     &eaEnlace   = "";
     &eaCheq     = "";
     &eaQIban    = "";
     nSw = 0;
     aHora = "";
     nPersona = 0;
     aDeci = "";
     &enSwDebug = 0;
     &eaPain = "";
     &eaFichero = "";
     &eaTag = "";
     &eaVal = "";
     &eaGrupo = "";
     nSwCuenta = 0;
     nRegistros1 = 0;
     nRegistros2 = 0;
     nRegistros3 = 0;
     nTotal1 = 0;
     nTotal2 = 0;
     nTotal3 = 0;
     fHoy = @;
     nNume = 0;
     aPais = "";
     aNif = "";
     aSufijo = "";
     aId = "";

     aCar = "";
     aCadena = "";
     aLon = "";
     nLon = 0;
     aConce = "";
     aConce1 = "";
     aConce2 = "";
     &eaEntidad    = "";
     &eaSucursal   = "";
     &eaCuenta     = "";
     &eaDC         = "";

     aPath = "";
     nSwIntegral = 0;
     nSwBasica = 0;
     nSwGestion = 0;
     nAvisa = 0;
     &fecha = @;
     &eaDirProv = "";
     &eaNifProv = "";
     &HayConta = 0;
     &eaCadena = "";

     &eaCodigo  = "";
     &eaTipoCod = "";
     &aCuenta   = "";

     &eaNif       = "";
     &eaNombre    = "";
     &eaApCorreos = "";
     &eaDireccion = "";
     &eaPoblacion = "";
     &eaProvincia = "";
     &eaCP        = "";
     &eaTelefono  = "";
     &eaFax       = "";
     &eaCodPais   = "";
     &eaPais      = "";
     &eaCodIBAN   = "";
     &eaCodBIC    = "";
     &eaCta       = "";
     &eaIBANant   = "";
     &eaIBAN      = "";

     nCalc      = 0;
     aRef       = "";
     fFechaEmis = @;

     &eaNombre1 = "";
     &eaOrigen  = "";
     aDestino = "";
     Comodin  = "";
     Comodin1  = "";

     &Niv1 = 0;
     &Niv2 = 0;
     &Niv3 = 0;
     &Niv4 = 0;
     &Niv5 = 0;
     &Niv6 = 0;
     &Divisa = "";

     aCuentaCt = "";
     nNivel = 0;

     mens     = "";
     swErrorR = 0;
     swErrorP = 0;
     menR1    = "    No Existe Confirme !!!";
     menR2    = "    Confirme YA contabilizado ";
     menR3    = "2400SConfirme YA Emitido en Papel. Aceptar S/N ";
     menR4    = "2400NConfirme YA lanzado en Diskette. Aceptar S/N ";

     Nombre  = "";
     CodCli  = "";
     aPaisSEPA = "";

     trecibos   = 0;

     NifProv    = "";
     NombreProv = "";
     DirecProv  = "";
     PoblaProv  = "";
     ProviProv  = "";
     Cp1Prov    = "";
     Cp2Prov    = "";
     TelefProv  = "";
     FaxProv    = "";
     PaisProv   = "";
     Entidad    = "";
     Sucursal   = "";
     DC         = "";
     Cuenta     = "";

     swERROR    = 0;

     aAlfa  = "";
     aAlfa1 = "";
     aAlfa2 = "";

     &eaVarDni  = "";
     &enCalcCif = 0;

     nTF = 0;
     nSwError = 0;
     aErrTit = "";

     nSituacionSEPA = 0;
     nVuelta  = 0;
     nSwCab = 0;
|FIN;

|PROCESO Individual1;    ||Transferecnia Sepa
     trecibos = trecibos + 1;
     |QBF eaCodBIC;
     |QBF eaCodIBAN;
     |QBF eaDireccion;
     |QBF eaPoblacion;
     |QBF eaProvincia;

     eaGrupo = "S";
     eaTag = "CdtTrfTxInf";|HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "PmtId";|HAZ XmlTag;

||2.30
     |QBF eaNif;
     eaVal = eaNif + (fHoy % 102) + (fHoy % 402) + (fHoy % 704);
     eaVal = eaVal + (aHora % 102) + (aHora % 402) + (aHora % 702);
     eaVal = eaVal + (("00000" + #50#0) % -105) + (("000" + #50#1) % -103);
     eaTag = "EndToEndId"; |HAZ XmlTag;

     eaGrupo = "N"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "Amt";|HAZ XmlTag;

     nNume = #50#7; |HAZ NumDeci; eaVal = aAlfa;
     eaTag = "InstdAmt Ccy=" + &34 + "EUR" + &34; |HAZ XmlTag;

     eaGrupo = "N"; |HAZ XmlTag;
||2.77
     |SI eaCodBIC ! "";
          eaGrupo = "S";
          eaTag = "CdtrAgt";|HAZ XmlTag;

          eaGrupo = "S";
          eaTag = "FinInstnId";|HAZ XmlTag;

          eaVal = eaCodBIC;
          eaTag = "BIC"; |HAZ XmlTag;

          eaGrupo = "N"; |HAZ XmlTag;
          eaGrupo = "N"; |HAZ XmlTag;
     |FINSI;
||2.79
     eaGrupo = "S";
     eaTag = "Cdtr";|HAZ XmlTag;

     eaVal = #8#6; |QBF eaVal;
     |SI eaVal ! "";
          eaTag = "Nm"; |HAZ XmlTag;
     |FINSI;

     eaGrupo = "S";
     eaTag = "PstlAdr";|HAZ XmlTag;

     eaVal = eaCodPais; |QBF eaCodPais;
     |SI eaCodPais = ""; eaVal = "ES"; |FINSI;
     eaTag = "Ctry"; |HAZ XmlTag;

     eaVal = eaDireccion; |QBF eaVal;
     |SI eaVal ! "";
          eaTag = "AdrLine"; |HAZ XmlTag;
     |FINSI;

     |SI eaPoblacion = "";
          |SI eaProvincia ! "";
               eaVal = eaProvincia;
               eaTag = "AdrLine"; |HAZ XmlTag;
          |FINSI;
     |SINO;
          |SI eaProvincia ! "";
               eaVal = eaPoblacion + ", " + eaProvincia;
               eaTag = "AdrLine"; |HAZ XmlTag;
          |SINO;
               eaVal = eaPoblacion;
               eaTag = "AdrLine"; |HAZ XmlTag;
          |FINSI;
     |FINSI;

     eaGrupo = "N"; |HAZ XmlTag;
     eaGrupo = "N"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "CdtrAcct";|HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "Id";|HAZ XmlTag;

     eaVal = eaCodIBAN;
     eaTag = "IBAN"; |HAZ XmlTag;

     eaGrupo = "N"; |HAZ XmlTag;
     eaGrupo = "N"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "RmtInf";|HAZ XmlTag;
/*
     aAlfa = "FACTURA:" + #8#0; |QBF aAlfa;
     aAlfa = aAlfa + "/" + (("000000"+#8#1)%-106) + "-" + (("000"+#8#2)%-103);
     eaVal = #8#4; |HAZ FormaFecha;
     eaVal = aAlfa + " " + eaVal;
*/
     eaVal = aConce;
     eaTag = "Ustrd"; |HAZ XmlTag;

     eaGrupo = "N"; |HAZ XmlTag;
     eaGrupo = "N"; |HAZ XmlTag;
|FPRC;

|PROCESO Individual2;    ||Transferencias Otras
     |HAZ Individual1;
|FPRC;

|PROCESO Individual3;    ||Cheques
     |HAZ Individual1;
|FPRC;

|PROCESO InfoPago;
     nSwCab = 1;

     eaGrupo = "S";
     eaTag = "PmtInf";|HAZ XmlTag;

     fHoy = ~;
     |HORA aHora;
     aAlfa = #73#2; |QBF aAlfa;
     eaVal = (fHoy % 102) + (fHoy % 402) + (fHoy % 704);
     eaVal = eaVal + (aHora % 102) + (aHora % 402) + (aHora % 702);
     eaVal = eaVal + aAlfa + "001";
     eaTag = "PmtInfId"; |HAZ XmlTag;

     |SI nVuelta = 3; eaVal = "CHK"; |SINO; eaVal = "TRF"; |FINSI;
     eaTag = "PmtMtd"; |HAZ XmlTag;

     eaVal = #0#15; |HAZ FormaFecha;
     eaTag = "ReqdExctnDt"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "Dbtr"; |HAZ XmlTag;

     eaVal = #73#3; |QBF eaVal;
     |SI eaVal ! "";
          eaTag = "Nm"; |HAZ XmlTag;
     |FINSI;

     eaGrupo = "S";
     eaTag = "PstlAdr"; |HAZ XmlTag;

     eaVal = #73#27;
     eaTag = "Ctry"; |HAZ XmlTag;

     eaVal = #73#10; |QBF eaVal;
     |SI eaVal ! "";
          eaTag = "AdrLine"; |HAZ XmlTag;
     |FINSI;

     eaVal =  #73#7; |QBF eaVal;
     |SI eaVal ! "";
          eaTag = "AdrLine"; |HAZ XmlTag;
     |FINSI;

     eaGrupo = "N"; |HAZ XmlTag;
     eaGrupo = "N"; |HAZ XmlTag;
||2.20  ????
     eaGrupo = "S";
     eaTag = "DbtrAcct"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "Id"; |HAZ XmlTag;

     aAlfa = #14#42; |QBF aAlfa;
     |SI aAlfa ! "";
          eaVal = aAlfa;
          eaTag = "IBAN"; |HAZ XmlTag;
     |SINO;
          eaGrupo = "S";
          eaTag = "Othr"; |HAZ XmlTag;

          aPais = "ES";
          aNif = #73#2;
          aSufijo = #73#26; |QBF aSufijo; aSufijo = ("000" + aSufijo) % -103;
          |HAZ ObtenId;
          eaVal = aId;
          eaTag = "Id"; |HAZ XmlTag;

          eaGrupo = "N"; |HAZ XmlTag;
     |FINSI;
     eaGrupo = "N"; |HAZ XmlTag;
     eaGrupo = "N"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "DbtrAgt"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "FinInstnId"; |HAZ XmlTag;

/* ***
     aAlfa = #14#45; |QBF aAlfa;
     |SI aAlfa ! "";
          eaVal = aAlfa;
          eaTag = "BIC"; |HAZ XmlTag;
     |FINSI;

     eaGrupo = "S";
     eaTag = "Othr"; |HAZ XmlTag;

     eaVal = #14#14;
     eaTag = "Id"; |HAZ XmlTag;

     eaGrupo = "N"; |HAZ XmlTag;
*/

     aAlfa = #14#45; |QBF aAlfa;
     |SI aAlfa ! "";
          eaVal = aAlfa;
          eaTag = "BIC"; |HAZ XmlTag;
     |SINO;
          eaGrupo = "S";
          eaTag = "Othr"; |HAZ XmlTag;

          eaVal = "NOTPROVIDED";
          eaTag = "Id"; |HAZ XmlTag;

          eaGrupo = "N"; |HAZ XmlTag;
     |FINSI;

     eaGrupo = "N"; |HAZ XmlTag;
     eaGrupo = "N"; |HAZ XmlTag;
|FPRC;

|PROCESO Cabecera;
     eaGrupo = "S";
     eaTag = "CstmrCdtTrfInitn";|HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "GrpHdr"; |HAZ XmlTag;

     eaVal = ("00000" + #49#0) % -105;
     eaTag = "MsgId"; |HAZ XmlTag;

     |HAZ HoraActual; eaVal = aAlfa;
     eaTag = "CreDtTm"; |HAZ XmlTag;

     nNume = nRegistros1 + nRegistros2 + nRegistros3;
     eaVal = nNume;
     eaTag = "NbOfTxs"; |HAZ XmlTag;

     nNume = nTotal1 + nTotal2 + nTotal3; |HAZ NumDeci;
     eaVal = aAlfa;
     eaTag = "CtrlSum"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "InitgPty"; |HAZ XmlTag;

     eaVal = #73#3; |QBF eaVal;
     |SI eaVal ! "";
          eaTag = "Nm"; |HAZ XmlTag;
     |FINSI;

     aAlfa = #73#2; |QBF aAlfa; aAlfa = aAlfa % 0101;
     |SI aAlfa ] "A"; |Y aAlfa [ "W";
          nPersona = 1; || Persona juridica
     |SINO;
          nPersona = 2; || Persona fisica
     |FINSI;

     eaGrupo = "S";
     eaTag = "Id"; |HAZ XmlTag;

     |SI nPersona = 1;
          eaGrupo = "S";
          eaTag = "OrgId"; |HAZ XmlTag;

          eaGrupo = "S";
          eaTag = "Othr"; |HAZ XmlTag;
/* ***
          aPais = "ES";
          aNif = #73#2;
          aSufijo = #73#26; |QBF aSufijo; aSufijo = ("000" + aSufijo) % -103;
          |HAZ ObtenId;
          eaVal = aId;
*/
          aNif = #73#2; |QBF aNif;
          aSufijo = #73#26; |QBF aSufijo; aSufijo = ("000" + aSufijo) % -103;
          eaVal = aNif + aSufijo;
          eaTag = "Id"; |HAZ XmlTag;

          eaGrupo = "N"; |HAZ XmlTag;
          eaGrupo = "N"; |HAZ XmlTag;
     |SINO;
          eaGrupo = "S";
          eaTag = "PrvtId"; |HAZ XmlTag;

          eaGrupo = "S";
          eaTag = "Othr"; |HAZ XmlTag;

/* ***
          aPais = "ES";
          aNif = #73#2;
          aSufijo = #73#26; |QBF aSufijo; aSufijo = ("000" + aSufijo) % -103;
          |HAZ ObtenId;
          eaVal = aId;
*/
          aNif = #73#2; |QBF aNif;
          aSufijo = #73#26; |QBF aSufijo; aSufijo = ("000" + aSufijo) % -103;
          eaVal = aNif + aSufijo;
          eaTag = "Id"; |HAZ XmlTag;

          eaGrupo = "N"; |HAZ XmlTag;
          eaGrupo = "N"; |HAZ XmlTag;
     |FINSI;

     eaGrupo = "N"; |HAZ XmlTag;
     eaGrupo = "N"; |HAZ XmlTag;
     eaGrupo = "N"; |HAZ XmlTag;
|FPRC;

|PROCESO NumDeci;
     nNume = (nNume * 100) ! 0;
     aAlfa = nNume;
     aLon = aAlfa % A0; nLon = aLon;
     aDeci = aAlfa % -102;
     nLon = nLon + 100 - 2;
     aAlfa = (aAlfa % nLon) + "." + aDeci;
|FPRC;

|PROCESO FormaFecha;
     eaVal = (eaVal % 704) + "-" + (eaVal % 402) + "-" + (eaVal % 102);
|FPRC;

|PROCESO HoraActual;
     fHoy = ~;
     |HORA aAlfa;
     aAlfa = (fHoy % 704) + "-" + (fHoy % 402) + "-" + (fHoy % 102) + "T" + aAlfa;
|FPRC;

|PROCESO ObtenId;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "/"; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "-"; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "?"; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - ":"; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "("; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - ")"; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "."; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - ","; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "'"; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "+"; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - &34; |SIGUE;

     aAlfa = aNif + aPais + "00";

     aAlfa = aAlfa > aAlfa;
     aCar = ""; aCadena = "";
     |PARA; |SI aAlfa ! ""; |HACIENDO;
          aCar = aAlfa % 0101; aAlfa = aAlfa % 0299;
          |SI aCar ] "0"; |Y aCar [ "9";
               aCadena = aCadena + aCar;
          |SINO;
               |SI aCar ] "A"; |Y aCar [ "Z"; |Y aCar ! "Ñ";
                    nNume = &aCar;
                    nNume = nNume - 55;
                    aCar = nNume;
                    aCadena = aCadena + aCar;
               |FINSI;
          |FINSI;
     |SIGUE;
     aAlfa = aCadena;
     |HAZ Mod_9710;
     aId = aPais + aAlfa + aSufijo + aNif;
|FPRC;

|PROCESO Mod_9710;
     aAlfa2 = aAlfa % 0101;
     |PARA; |SI aAlfa2 = "0"; |HACIENDO;
          aAlfa = aAlfa % 0299; aAlfa2 = aAlfa % 0101;
     |SIGUE;

     aAlfa1 = "";
     |PARA; |SI aAlfa ! ""; |HACIENDO;
          aAlfa2 = aAlfa % 0; nNume = aAlfa2;
          |SI nNume > 5;
               aAlfa2 = aAlfa % 0105;
               aAlfa = aAlfa % 0699;
          |SINO;
               aAlfa2 = aAlfa;
               aAlfa = "";
          |FINSI;
          aAlfa1 = aAlfa1 + aAlfa2;
          nNume = aAlfa1;
          nNume = nNume / 97;
          aAlfa2 = nNume;
          aAlfa2 = aAlfa2 - ".";
          |SI FSalida ! 0;
               nNume = FSalida + 98;
               aAlfa2 = aAlfa2 % nNume;
               aAlfa2 = "0." + aAlfa2;
               nNume = aAlfa2;
          |SINO;
               nNume = 0;
          |FINSI;
          nNume = (nNume * 97) ! 0;
          aAlfa1 = nNume;
     |SIGUE;
     nNume  = aAlfa1;
     nNume = 98 - nNume;
     aAlfa = ("00" + nNume) % -102;
||nNume  = 98 - (nNume - (((nNume / 97) ! 0) * 97)); || MOD 97-10  ISO 7604
|FPRC;

|PROCESO Tiene;
     |SI nAvisa = 0;
          |QBF aAlfa;
          |SI aAlfa = "";
               nAvisa = 1;
               aAlfa = #caclipro@#10; |QBF aAlfa;
               |SI #caclipro@#23 = "C";
                    aAlfa = "0000Cliente: [" + aAlfa + "] con datos incompletos en contabilidad...";
               |SINO;
                    aAlfa = "0000Proveedor: [" + aAlfa + "] con datos incompletos en contabilidad...";
               |FINSI;
               |MENAV aAlfa;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CheqDatosPrv;
     #225#0 = #caclipro@#23;
     #225#1 = #caclipro@#10;
     |LEE 000#225=;
     |SI FSalida = 0;
          |ACABA;
     |SINO;
          |GRABA 020#225n;
     |FINSI;

     nAvisa = 0;
     aAlfa = #caclipro@#29; |HAZ Tiene;
     aAlfa = #caclipro@#30; |HAZ Tiene;
     aAlfa = #caclipro@#31; |HAZ Tiene;
     aAlfa = #caclipro@#32; |HAZ Tiene;
     aAlfa = #caclipro@#13; |HAZ Tiene;
     aAlfa = #caclipro@#14; |HAZ Tiene;
     aAlfa = #caclipro@#19; |HAZ Tiene;

     |SI #0#16 = "S";
         |SI nSwIntegral = 1;
            #dsam0104@#0 = #caclipro@#24;
            |LEE 000#dsam0104@.=;
            |SI FSalida = 0; |Y #dsam0104@#5 = "S";
               aAlfa = #caclipro@#38; |HAZ Tiene;
               aAlfa = #caclipro@#39; |HAZ Tiene;
               aAlfa = #caclipro@#40; |HAZ Tiene;
               aAlfa = #caclipro@#41; |HAZ Tiene;
            |FINSI;
         |FINSI;
         |SI nSwBasica = 1;
            #agim0016@#0 = #caclipro@#24;
            |LEE 000#agim0016@.=;
            |SI FSalida = 0; |Y #agim0016@#5 = "S";
               aAlfa = #caclipro@#38; |HAZ Tiene;
               aAlfa = #caclipro@#39; |HAZ Tiene;
               aAlfa = #caclipro@#40; |HAZ Tiene;
               aAlfa = #caclipro@#41; |HAZ Tiene;
            |FINSI;
         |FINSI;
     |FINSI;

     aAlfa = "";
     |SI #caclipro@#15 = 0; |HAZ Tiene; |FINSI;
     |SI #caclipro@#16 = 0; |HAZ Tiene; |FINSI;
|FPRC;

|PROCESO SacaNivel1;
   |QBF aCuentaCt;
   aAlfa = aCuentaCt % 0; nNivel = aAlfa;
   |SI nNivel = Niv1; nNivel = 1; |FINSI;
   |SI nNivel = Niv2; nNivel = 2; |FINSI;
   |SI nNivel = Niv3; nNivel = 3; |FINSI;
   |SI nNivel = Niv4; nNivel = 4; |FINSI;
   |SI nNivel = Niv5; nNivel = 5; |FINSI;
   |SI nNivel = Niv6; nNivel = 6; |FINSI;
|FPRC;

|PROCESO Concepto;
     aConce1 = "";
     aAlfa = #73#11; |QBF aAlfa;
     |SI aAlfa ! ""; |Y #73#21 = "S";
          aConce1 = aAlfa;
          |SI #73#12 = "S";
               aConce1 = aConce1 + #dscam008#63; |QBF aConce1;
          |FINSI;
          |SI #73#13 = "S";
               aConce1 = aConce1 + "-" + (("000"+#dscam050#5)%-103);
          |FINSI;
          |SI #73#14 = "S";
               aAlfa = (#dscam008#4%102)+"/"+(#dscam008#4%402)+"/"+(#dscam008#4%704);
               aConce1 = aConce1 + " " + aAlfa;
          |FINSI;
     |FINSI;

     aConce2 = "";
     aAlfa = #73#16; |QBF aAlfa;
     |SI aAlfa ! ""; |Y #73#21 = "S";
          aConce2 = aAlfa;
          |SI #73#17 = "S";
               aConce2 = aConce2 + #dscam008#63; |QBF aConce2;
          |FINSI;
          |SI #73#18 = "S";
               aConce2 = aConce2 + "-" + (("000"+#dscam050#5)%-103);
          |FINSI;
          |SI #73#19 = "S";
               aAlfa = (#dscam008#4%102)+"/"+(#dscam008#4%402)+"/"+(#dscam008#4%704);
               aConce2 = aConce2 + " " + aAlfa;
          |FINSI;
     |FINSI;

     aAlfa = aConce1 + " " + aConce2;
     aLon = aAlfa % A0; nLon = aLon; nLon = 140 - nLon;
     aConce = aAlfa + (" " * nLon);       || aqui tenemos 140 como maximo
/*
     |SI nVuelta = 1; || long=140
         ||los que trae
     |FINSI;
     |SI nVuelta = 2; || long=72
          aConce = aConce % 172;
     |FINSI;
*/
|FPRC;

|PROCESO leecl_y_cobro;
     NombreProv = "";
     DirecProv  = "";
     PoblaProv  = "";
     ProviProv  = "";
     Cp1Prov    = "";
     Cp2Prov    = "";
     TelefProv  = "";
     FaxProv    = "";
     PaisProv   = "";
     Entidad    = "";
     Sucursal   = "";
     DC         = "";
     Cuenta     = "";

     aRef = " " * 12;

     |DDEFECTO #dscam008;
     #dscam008#40 = #dscam050#13;
     |LEE 001#dscam008.=;

     fFechaEmis = #dscam008#4;

     eaCodigo = #dscam008#32; eaTipoCod = "P"; aCuenta = #dscam008#5;
     |HAZ LeeDatos;
     PaisProv = eaCodPais; |QBF PaisProv;

     aAlfa = #dscam008#75; |QBF aAlfa;
     |SI aAlfa = ""; #dscam008#75 = eaCodIBAN; |FINSI;
     eaCodIBAN = #dscam008#75;
     aAlfa = #dscam008#76; |QBF aAlfa;
     |SI aAlfa = ""; #dscam008#76 = eaCodBIC; |FINSI;
     eaCodBIC = #dscam008#76;

     #caclipro@#23 = "P";
     #caclipro@#10 = #dscam008#5;
     aCuentaCt = #dscam008#5; |HAZ SacaNivel1; #caclipro@#11 = nNivel;
     |LEE 000#caclipro@.=;
     |SI FSalida = 0;
          |SI #0#16 = "S";
               |HAZ CheqDatosPrv;
          |FINSI;
          NifProv    = eaNif;
          NombreProv = eaNombre;
          DirecProv  = eaDireccion;
          PoblaProv  = eaPoblacion;
          ProviProv  = eaProvincia;
          Cp1Prov    = eaCP % 0102;
          Cp2Prov    = eaCP % 0303;
          TelefProv  = eaTelefono;
          FaxProv    = eaFax;
          |SI PaisProv = "";
               PaisProv   = #caclipro@#24;
               |QBF PaisProv;
          |FINSI;
          |SI #dscaz224#4 = "1"; aRef = #caclipro@#10; |FINSI;
          |SI #dscaz224#4 = "2"; aRef = #caclipro@#0;  |FINSI;
          |QBF aRef; aRef = ((" " * 12) + aRef) % -112;

          aAlfa = "|NC"; nCalc = #caclipro@#aAlfa#;
          |SI nCalc > 29;
               Entidad  = #caclipro@#29;
               Sucursal = #caclipro@#30;
               DC       = #caclipro@#31;
               Cuenta   = #caclipro@#32;
          |SINO;
               aAlfa    = #caclipro@#18; |QBF aAlfa;
               Entidad  = aAlfa % 0104;
               Sucursal = aAlfa % 0504;
               DC       = aAlfa % 0902;
               Cuenta = #caclipro@#17; |QBF Cuenta;
               Cuenta = ("0000000000" + Cuenta) % -110;
          |FINSI;
     |SINO;
          aAlfa = "    No se encuentran los datos del proveedor " + #dscam008#6; |QBF aAlfa;
          aAlfa = aAlfa + " en la ficha de contabilidad";
          ||MENAV aAlfa;
     |FINSI;

     aAlfa = PaisProv; |QBF aAlfa;

     nSituacionSEPA = 0;
     |SI nSwIntegral = 1;
          #dsam0104@#0 = PaisProv;
          |LEE 000#dsam0104@.=;
          |SI FSalida = 0;
               |SI aAlfa = "ES";
                    nSituacionSEPA = 1;
               |SINO;
                    |SI #dsam0104@#7 = "S";
                         nSituacionSEPA = 2;
                    |SINO;
                         nSituacionSEPA = 3;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;

     |SI nSwBasica = 1;
          #agim0016@#0 = PaisProv;
          |LEE 000#agim0016@.=;
          |SI FSalida = 0;
               #agim0016@#0 = #caclipro@#24;
               |LEE 000#agim0016@.=;
               |SI FSalida = 0;
                    |SI aAlfa = "ES";
                         nSituacionSEPA = 1;
                    |SINO;
                         |SI #agim0016@#7 = "S";
                              nSituacionSEPA = 2;
                         |SINO;
                              nSituacionSEPA = 3;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;

     |SI nSituacionSEPA = 0; |Y nSwGestion = 1;
          #dsm00225@#0 = PaisProv;
          |LEE 000#dsm00225@.=;
          |SI FSalida = 0;
               |SI aAlfa = "ES";
                    nSituacionSEPA = 1;
               |SINO;
                    |SI #dsm00225@#7 = "S";
                         nSituacionSEPA = 2;
                    |SINO;
                         nSituacionSEPA = 3;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;

     |SI nSituacionSEPA = 0;
          |SI aAlfa = "ES";
               nSituacionSEPA = 1;
          |SINO;
               nSituacionSEPA = 3;
          |FINSI;
     |FINSI;

     |SI eaCodIBAN = "";
         |SI eaCodPais = "ES"; |O eaCodPais = "ES ";
             eaIBANant = "";
             eaCta     = #dscam008#8 + #dscam008#9 + #dscam008#10 + #dscam008#11;
             eaPais    = "ES";
             |HAZ CalcuIBAN;
             eaCodIBAN = eaIBAN;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO ProcesaRec;
     |SI nSwGestion = 1;
          aAlfa = #48#17; |QBF aAlfa;
          aAlfa1 = aAlfa + "fich/" + (("00000" + #dscam008#46) % -105) + "/";
          |PATH_DAT #dsm00225@#aAlfa1#; |ABRE #dsm00225@;
     |FINSI;

     swERROR = 0;
     |HAZ leecl_y_cobro;
     |SI swERROR = 0;
          eaNifProv = NifProv;
          eaDirProv = DirecProv;
          |HAZ Concepto;
          |SI nVuelta = 1; || Transferencias SEPA
               |SI nSituacionSEPA = 1; |O nSituacionSEPA = 2;
                    |SI #dscam050#16 = "T";
                         |SI nSwCuenta = 1;
                              nRegistros1 = nRegistros1 + 1;
                              nTotal1 = nTotal1 + #dscam050#20;
                         |SINO;
                              |SI nSwCab = 0; |HAZ InfoPago; |FINSI;
                              |HAZ Individual1;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
          |SI nVuelta = 2; || Transferencias No SEPA
               |SI #dscam050#16 = "T"; |Y nSituacionSEPA = 3;
                    |SI nSwCuenta = 1;
                         nRegistros2 = nRegistros2 + 1;
                         nTotal2 = nTotal2 + #dscam050#20;
                    |SINO;
                         |SI nSwCab = 0; |HAZ InfoPago; |FINSI;
                         |HAZ Individual2;
                    |FINSI;
               |FINSI;
          |FINSI;
          |SI nVuelta = 3; || Cheques
               |SI #dscam050#16 = "C";
                    |SI nSwCuenta = 1;
                         nRegistros3 = nRegistros3 + 1;
                         nTotal3 = nTotal3 + #dscam050#20;
                    |SINO;
                         |SI nSwCab = 0; |HAZ InfoPago; |FINSI;
                         |HAZ Individual3;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;

     |SI nSwGestion = 1; |CIERRA #dsm00225@; |FINSI;
|FPRC;

|PROCESO ProcesaRecibos;
     nSwCuenta = 1;
     |PARA nVuelta = 1; |SI nVuelta [ 3; |HACIENDO nVuelta = nVuelta + 1;
          |BUCLELIN 0ProcesaRec#49;
     |SIGUE;

     |HAZ Cabecera;

     nSwCuenta = 2;
     |PARA nVuelta = 1; |SI nVuelta [ 3; |HACIENDO nVuelta = nVuelta + 1;
          nSwCab = 0;
          |BUCLELIN 0ProcesaRec#49;
          |SI nSwCab ! 0;
               eaGrupo = "N"; |HAZ XmlTag;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO pasecuencial; ||TIPO 3;
     aAlfa = #48#17; |QBF aAlfa;
     aAlfa1 = aAlfa + "def/dsm00225.mas";
     |CARGA_DEF aAlfa1, dsm00225@;
     |SI FSalida = 0;
          nSwGestion = 1;
     |FINSI;

     aAlfa = #48#16; |QBF aAlfa;
     aAlfa1 = aAlfa + "def/caclipro.mas";
     |CARGA_DEF aAlfa1, caclipro@;
     |SI FSalida ! 0;
          |DESCARGA_DEF #dsm00225@;
          |MENAV "    Problemas para abrir el fichero de clientes/proveedores de contabilidad";
          |ERROR; |ACABA;
     |FINSI;

     aAlfa1 = aAlfa + "fich/" + (("00000" + #48#14) % -105) + #48#19 + "/";
     |PATH_DAT #caclipro@#aAlfa1#; |ABRE #caclipro@;

     |ABRE #73;
     #73#0 = #dscaz224#2;
     |LEE 000#73=;
     |SI FSalida ! 0; |DDEFECTO #73; |FINSI;
     |CIERRA #73;

     aAlfa = aAlfa - "contagen/";
     aAlfa = aAlfa - "contagen\";
     aPath = aAlfa;
     aAlfa = aPath + "integral/";
     aAlfa1 = aAlfa + "def/dsam0104.mas";
     |CARGA_DEF aAlfa1, dsam0104@;
     |SI FSalida ! 0;
          aAlfa = aPath + "basica/";
          aAlfa1 = aAlfa + "def/agim0016.mas";
          |CARGA_DEF aAlfa1, agim0016@;
          |SI FSalida = 0;
               nSwBasica = 1;
               aAlfa1 = aPath + "basica/fich/";
               |PATH_DAT #agim0016@#aAlfa1#; |ABRE #agim0016@;
          |FINSI;
     |SINO;
          nSwIntegral = 1;
          aAlfa1 = aPath + "integral/fich/";
          |PATH_DAT #dsam0104@#aAlfa1#; |ABRE #dsam0104@;
     |FINSI;

     |ABRE #dscam008;         || Abre Mantenimiento de efectos

     |DFICB Comodin;
     Comodin1 = ("000" + #dscaz224#1) % -103;
     Comodin1 = "n3400" + Comodin1 + ".xml";
     eaOrigen = Comodin;
     eaNombre1 = Comodin1;
     eaFichero = Comodin + Comodin1;
     eaPain = "001.001.03";
     |HAZ XmlIni;
     |SI FSalida ] 0;
          #dscam049#0 = #dscaz224#1;
          |LEE 141#dscam049.=;
          |SI FSalida = 0;
               |HAZ ProcesaRecibos;
          |FINSI;
          |HAZ XmlFin;
     |FINSI;

     |CIERRA #dscam008;

     |SI nSwIntegral = 1; |CIERRA #dsam0104@; |DESCARGA_DEF #dsam0104@; |FINSI;
     |SI nSwBasica = 1; |CIERRA #agim0016@; |DESCARGA_DEF #agim0016@; |FINSI;
     |DESCARGA_DEF #caclipro@;
     |SI nSwGestion = 1; |DESCARGA_DEF #dsm00225@; |FINSI;

     |SI HayConta = 1; |Y #dscam049#7 = "N"; |Y #dscaz224#14 ! "    ";
          |ABRE #dscam030;
          #dscam030#0 = #dscaz224#14;
          |LEE 010#dscam030.=;
          |SI FSalida = 0;
               Comodin = Usuario; |QBF Comodin; Comodin = "ac" + Comodin;
               |NOME_DAT #dscaw013#Comodin#;
               |DELINDEX #dscaw013; |ABRE #dscaw013;
               |DDEFECTO #dscaw013;
               #dscaw013#0  = 1;            #dscaw013#15 = #dscaz224#14;
               #dscaw013#5  = #dscam049#0;  #dscaw013#6  = #dscam049#0;
               #dscaw013#11 = "N";          #dscaw013#12 = 5;
               #dscaw013#13 = "dscam050";   #dscaw013#14 = 0;
               |GRABA 020#dscaw013.n;
               |CIERRA #dscaw013;
               Comodin = #dscaz224#14;
               Comodin = "dscam033.tab;" + Comodin + ",E";

               |SUB_EJECUTA_NP Comodin;
          |FINSI;
          |CIERRA #dscam030;
     |FINSI;
     |SI trecibos ! 0;
          #dscam049#0 = #dscaz224#1;
          |LEE 101#dscam049.=;
          #dscam049#7 = "D";
          |GRABA 020#dscam049.a;
          |LIBERA #dscam049;
     |FINSI;

     |SUB_EJECUTA "dscaz032.tab";
|FPRC;

|PROCESO MiraParam; |TIPO 7;
     |SI #dscam051#1 = "S";
          Comodin = #dscam051#61;
          |C_M #dscaz224#14,1,Comodin;
     |SINO;
          |C_M #dscaz224#14,1,"N";
     |FINSI;
|FPRC;

|PROCESO leerem;
     swErrorR = 0;
     |ABRE #dscam049;
     |DDEFECTO #dscam049;
     #dscam049#0 = #dscaz224#1;
     |LEE 041#dscam049.=;
     |SI FSalida ! 0;
         swErrorR = 1; mens = menR1;
     |SINO;
         Divisa = #dscam049#16; |HAZ TomaDecim;
         |SI #dscam049#8 = "S";
             swErrorR = 2; mens = menR2;
         |SINO;
             |SI #dscam049#7 = "S"; swErrorR = 3; mens = menR3; |FINSI;
             |SI #dscam049#7 = "D"; swErrorR = 4; mens = menR4; |FINSI;
         |FINSI;

         |ABRE #14;
         #14#0 = #dscam049#3;
         |LEE 000#14=;
         |CIERRA #14;
     |FINSI;
     |CIERRA #dscam049;
|FPRC;

|PROCESO TomaDatos;
     |ABRE #73;
     #73#0 = #dscaz224#2;
     |LEE 000#73=;
     |SI FSalida ! 0; |DDEFECTO #73; |FINSI;
     |CIERRA #73;

     #dscaz224#5 = #73#23; |PINTA #dscaz224#5;
     #dscaz224#13 = #73#24; |PINTA #dscaz224#13;
|FPRC;

|PROCESO ver_confirming; |TIPO 6;
     |SI FSalida = 2; |ACABA; |FINSI;
     |HAZ leerem;
     |SI swErrorR ! 0;
         |SI swErrorR < 3;
             |MENAV mens;
             |ERROR;
             |ACABA;
         |SINO;
             |CONFI mens;
             |SI FSalida ! 0;
                 |ERROR;
                 |ACABA;
             |FINSI;
         |FINSI;
     |FINSI;
     #dscaz224#2 = #dscam049#3; || Banco
     |HAZ TomaDatos;
     |PINTA 0935, #dscam049#2;   || Vto.
     |PINTA 0958, #dscam049#5;   || N§ Efectos
     |PINTA 1035, #dscam049#3;   || Banco
     |PINTA 1049, #dscam049#9;  || Importe Total
|FPRC;

|PROGRAMA;
     |HAZ Tipo8;

     |ABRE #dscam051; |LEE 000#dscam051.p; |CIERRA #dscam051;

     aAlfa = Usuario; |NOME_DAT #225 aAlfa; |DELINDEX #225;
     |ABRE #225;

     |SI #dscam051#1 = "S";
          #dscaz224#14 = #dscam051#29;
     |SINO;
          #dscaz224#14 = "";
     |FINSI;

     |CLS;
     |CABEZA " Emision normas SEPA: Norma 34.14 XML";
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
         |SI #0#17 = "X";
             |ABRE #dscam049;
             |HAZ pasecuencial;
             |CIERRA #dscam049;
         |SINO;
             |CIERRA #225; |DELINDEX #225;
             |HAZ Tipo9;
             nSw = 1;

             enConfirme = #0#1;
             eaRefeCli  = #0#13;
             efFecSop = #0#3;
             efFecPro = #0#15;
             eaEnlace = #0#14;
             eaCheq   = #0#16;
             eaQIban  = #0#18;
             |SUB_EJECUTA_NP "dscaz216";
             |ACABA;
          |FINSI;
     |FINSI;
     |CIERRA #225; |DELINDEX #225;
     |HAZ Tipo9;
|FPRO;
