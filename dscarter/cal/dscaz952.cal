|FICHEROS;
   dscaz952 #0;
   dscam044 #1;
   dscam045 #2;

   dscam003 #10;
   dscam051 #11;
   dscam998 #12,MANTE;

   agif024@ #100;
|FIN;

|VARIABLES;
   &eaEntrada = "";
   &eaSalida = "";

   aAlfa  = "";
   aAlfa1 = "";
   aAlfa2 = "";
   aAlfa3 = "";

   nContador = 0;
   nCalc     = 0;
   nHandle   = 0;
   nSwCom    = 0;
   nEmpres   = 0;

   aDsCorreoIP = "";
   aDirOrigen  = "";
   aDirDestino = "";
   aServidor   = "";
   aAsunto     = "";
   aTexto      = "";
   aFichero    = "";

{1000}aResultados = "";
|FIN;

|RUTINA inf;
  #dscam998#0 = 1;
|FRUT;

|RUTINA sup;
  #dscam998#0 = 999999999;
|FRUT;

|PROCESO MiraTecla;
   |SI FSalida = 10;
      |ABRE #dscam998;
      |PUSHV 08092272;
      |ENTLINEAL #1#dscam998, -1, 1, 19, 1, inf, sup;
      |POPV;
      |CIERRA #dscam998;
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO EnviaCorreo;
   |DFICB aAlfa; |QBF aAlfa; aAlfa = aAlfa + (("00000" + #48#0) % -105) + "/";
   |PATH_DAT #dscam051#aAlfa#;
   aFichero = aAlfa + "aj" + Usuario + ".txt";

   |ABRE #dscam051;
   |LEE 000#dscam051.p;
   |CIERRA #dscam051;

   aAlfa = #dscam051#81; |QBF aAlfa;
   aAlfa = aAlfa + "." + #dscam051#82; |QBF aAlfa;
   aAlfa = aAlfa + "." + #dscam051#83; |QBF aAlfa;
   aAlfa = aAlfa + "." + #dscam051#84; |QBF aAlfa;
   |SI aAlfa = "0.0.0.0"; |ACABA; |FINSI;

   aDsCorreoIP = aAlfa;
   aDirOrigen  = #dscam051#90;
   aServidor   = #dscam051#88; |QBF aServidor;
   aAsunto     = " Diferencias entre cuentas bancarias (gestion/cartera)";
   aTexto      = "$$$$" + aFichero;
   |SI #dscam051#89 = "S";
      eaEntrada = #dscam051#92; |HAZ Encriptado;
      aAlfa = eaSalida;             |QBF aAlfa; aAlfa = aAlfa + ":";
      aAlfa = aAlfa + #dscam051#86; |QBF aAlfa; aAlfa = aAlfa + ":";
      aDirOrigen = aAlfa + aDirOrigen;
   |FINSI;

   |DSCORREO_ENVIA aDsCorreoIP, aServidor, aDirOrigen, aDirDestino, aAsunto, aTexto, aFichero;
   |SI FSalida < 0;
      |MENAV "    Problemas al enviar correo";
   |FINSI;
|FPRC;

|PROCESO AnyadeComent;
   IaResultados = aResultados1<-; IaResultados = IaResultados + nContador;
   nContador = nContador + 1;
   |SI nSwCom = 0;
      aResultados = " ";
      nSwCom = 1;
      IaResultados = aResultados1<-; IaResultados = IaResultados + nContador;
      nContador = nContador + 1;
      aAlfa3 = "  CLIENTE " + #dscam003#32 + " " + #dscam003#6; |QBF aAlfa3;
      aAlfa3 = aAlfa3 + " Documento " + #dscam003#40 + "[";
      aAlfa3 = aAlfa3 + #dscam003#0 + "/" + (("000000" + #dscam003#1) % -106) + "/" + (("000" + #dscam003#2) % -103) + "]";
      aResultados = aAlfa3;
      nSwCom = 1;
      IaResultados = aResultados1<-; IaResultados = IaResultados + nContador;
      nContador = nContador + 1;
   |FINSI;
   aResultados = aAlfa;
|FPRC;

|PROCESO Recibos;
   #dscam998#0 = #dscam003#40;
   |LEE 000#dscam998.=;
   |SI FSalida = 0; |ACABA; |FINSI;

   nSwCom = 0;

   #agif024@#0 = #dscam003#32;
   |LEE 000#agif024@.=;
   |SI FSalida = 0;
      aAlfa1 = #dscam003#8; |QBF aAlfa1; aAlfa2 = #agif024@#32; |QBF aAlfa2;
      |SI aAlfa1 ! aAlfa2;
         aAlfa = "        La entidad de la cuenta bancaria no coincide [" + #dscam003#8 + "|" + #agif024@#32 + "]"; |HAZ AnyadeComent;
      |FINSI;
      aAlfa1 = #dscam003#9; |QBF aAlfa1; aAlfa2 = #agif024@#33; |QBF aAlfa2;
      |SI aAlfa1 ! aAlfa2;
         aAlfa = "        La sucursal de la cuenta bancaria no coincide [" + #dscam003#9 + "|" + #agif024@#33 + "]"; |HAZ AnyadeComent;
      |FINSI;
      aAlfa1 = #dscam003#10; |QBF aAlfa1; aAlfa2 = #agif024@#31; |QBF aAlfa2;
      |SI aAlfa1 ! aAlfa2;
         aAlfa = "        El digito de control de la cuenta bancaria no coincide [" + #dscam003#10 + "|" + #agif024@#31 + "]"; |HAZ AnyadeComent;
      |FINSI;
      aAlfa1 = #dscam003#11; |QBF aAlfa1; aAlfa2 = #agif024@#30; |QBF aAlfa2;
      |SI aAlfa1 ! aAlfa2;
         aAlfa = "        El numero de cuenta de la cuenta bancaria es distinta [" + #dscam003#11 + "|" + #agif024@#30 + "]"; |HAZ AnyadeComent;
      |FINSI;
   |SINO;
      aAlfa = "        El cliente no existe"; |HAZ AnyadeComent;
   |FINSI;
|FPRC;

|DEFBUCLE Recibos;
   #dscam003#9;
   46;
   00, #dscam045#4, 000000, 000, 000000001, #dscam045#0;
   99, #dscam045#5, 999999, 999, 999999999, #dscam045#0;
   ;
   NULL, Recibos;
|FIN;

|PROCESO Control;
   #dscam044#0 = #dscam045#0;
   |LEE 000#dscam044.=;
   |SI FSalida = 0;
      aAlfa = #dscam044#1; |QBF aAlfa;
      aAlfa1 = aAlfa + "def/agifa024.mas";
      |CARGA_DEF aAlfa1, agif024@;
      |SI FSalida = 0;
         aAlfa1 = aAlfa + "fich/" + (("00000" + #dscam044#0) % -105) + "/";
         |PATH_DAT #agif024@#aAlfa1#;
         |ABRE #agif024@;
         |BUCLE Recibos;
         |CIERRA #agif024@; |DESCARGA_DEF #agif024@;
      |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE Control;
#dscam045#2;
2;
nEmpres, "V";
nEmpres, "V";
;
NULL, Control;
|FIN;

|PROGRAMA;
   |SI PARAMETRO ! "";
      #dscaz952#0 = PARAMETRO;
      FSalida = 0;
   |SINO;
      |CLS;
      |CABEZA "";
      |PINPA #0#dscaz952; |PINDA #0#dscaz952;
      |ENDATOS #1#dscaz952;
   |FINSI;

   aAlfa = #dscaz952#0; |QBF aAlfa;
   |SI FSalida = 0;  |Y aAlfa ! "";
      nEmpres = #48#0;
      |DFICB aAlfa; |QBF aAlfa;
      |PATH_DAT #dscam044#aAlfa#; |PATH_DAT #dscam045#aAlfa#;
      |ABRE #dscam044; |ABRE #dscam998;
      |BUCLE Control;
      |SI nContador > 0;
         |DFICE aAlfa; |QBF aAlfa;
         aAlfa = aAlfa + "aj" + Usuario + ".txt";
         |XABRE "W", aAlfa, nHandle;
         |SI FSalida ] 0;
            |PARA nCalc = 0; |SI nCalc [ nContador; |HACIENDO nCalc = nCalc + 1;
               IaResultados = aResultados1<-; IaResultados = IaResultados + nCalc;
               aAlfa = aResultados;
               |XGRABA nHandle, aAlfa;
            |SIGUE;
            |XCIERRA nHandle;
         |FINSI;

         aAlfa3 = #dscaz952#0; |QBF aAlfa3;
         aAlfa1 = aAlfa3 - ":";
         |SI FSalida ! 0;
            nCalc = ((FSalida / 100) ! 0) + 99;
            aAlfa1 = aAlfa3 % nCalc;  aAlfa2 = aAlfa1 + ":"; aAlfa3 = aAlfa3 - aAlfa2;
            |PARA; |SI aAlfa3 ! ""; |HACIENDO;
               aDirDestino = aAlfa1; |HAZ EnviaCorreo;
               aAlfa1 = aAlfa3 - ":";
               |SI FSalida ! 0;
                  nCalc = ((FSalida / 100) ! 0) + 99;
                  aAlfa1 = aAlfa3 % nCalc;  aAlfa2 = aAlfa1 + ":"; aAlfa3 = aAlfa3 - aAlfa2;
               |SINO;
                  aDirDestino = aAlfa3; |HAZ EnviaCorreo; aAlfa3 = "";
               |FINSI;
            |SIGUE;
         |SINO;
            aDirDestino = aAlfa3; |HAZ EnviaCorreo;
         |FINSI;
      |FINSI;
      |CIERRA #dscam044; |CIERRA #dscam998;
   |FINSI;
|FPRO;
