|FICHEROS;
   dscaz215 #0;
   dscam015 #1;

   dscam012 #2;
   dscam013 #3;
   dscam003 #4;
   dscam014 #5;
   dscam051 #6;
   dscam030 #7;
   dscam004 #8;
   dscaw013 #9;
   dscaw038 #10;
   dscam002 #11;
   dscam044 #12;
   dscam093 #13,MANTE;
   dscam161 #14,MANTE;
   dscam040 #16;
   dscam001 #17;

   &agif024@ #50;
   &caclipr@ #51;
   &agif321@ #100;

   dscam000@;
|FIN;

|VARIABLES;
     &enDRemesa = 0;
     &enHRemesa = 0;
     &efDFecha = @;
     &efHFecha = @;
     &enDBanco = 0;
     &enHBanco = 0;
     &efFecEmi = @;
     &efFecCar = @;
     &eaB2B = "";
     &eaRedu = "";
     &eaEnlace = "";
     ||
   aBase       = "";
   nSwConta    = 0;
   &enError    = 0;
   &eaCodPais  = "";
   &eaCta      = "";
   &eaIBANant  = "";
   &eaIBAN     = "";

   &eaUsuario = ""; &enImpAnt = 0;  &enImpAct = 0;  &eaDescr = "";
   &eaTipoReg = ""; &eaNumero = ""; &eaTipoOp = "";
   &HayGestion = 0; &HayConta = 0; &nDeci_Base = 0; &nDeci_Div = 0;
   &Fichero   = ""; &Cmp = "";      &Tipo = "";     &CmpNum = 0;
   &Niv1 = 0; &Niv2 = 0; &Niv3 = 0; &Niv4 = 0;  &Niv5 = 0; &Niv6 = 0;
   &eaCodigo    = "";
   &eaTipoCod   = "";
   &aCuenta     = "";

   &eaFechaMand = "";
   &eaNif       = "";
   &eaNombre    = "";
   &eaDireccion = "";
   &eaPoblacion = "";
   &eaProvincia = "";
   &eaCPExt     = "";
   &eaCP        = "";
   &eaCP1       = "";
   &eaCP2       = "";
   &eaTelefono  = "";
   &eaNombre1   = "";
   &eaPais      = "";
   &eaCodBIC    = "";
   &eaCodIBAN   = "";

   nBase  = 0;
   nIVA   = 0;
   nTotal = 0;
   nPos1  = 0;
   nPos2  = 0;
   nModo  = 0;
   nBoton = 0;

   aConcepto = ""; nLinea = 0;
   fFecha = @;
   nCalc = 0;        nCalc1 = 0;
   nNumConcepto = 0; aAlfa = ""; aAlfa1 = ""; aAlfa2 = "";
   aParte1 = ""; aParte2 = ""; aParte3 = "";
   EmpreAnt = 0; PerioAnt = 0;
   aFinLin = ""; aFinFic = "";
   VAlfa   = ""; CIF = "";
   VAlfa01 = ""; VAlfa02 = ""; VAlfa03 = ""; VAlfa04 = ""; VAlfa05 = ""; VAlfa06 = "";
   VAlfa07 = ""; VAlfa08 = ""; VAlfa09 = ""; VAlfa10 = ""; VAlfa11 = ""; VAlfa12 = "";
   VAlfa13 = ""; VAlfa14 = ""; VAlfa15 = ""; VAlfa16 = ""; VAlfa17 = ""; VAlfa18 = "";
   VAlfa19 = ""; VAlfa20 = ""; VAlfa21 = ""; VAlfa22 = "";

   Field = ""; aTipo = ""; Fecha = ""; Comodin = ""; Comodin1 = "";
   NumDig = 0; Calc = 0;  Handle = 0; LinCob  = 0; Tipo1 = "";

   nSw = 0;

   aConcepto1 = ""; aConcepto2 = ""; aConcepto3 = "";
   aConcepto4 = ""; aConcepto5 = ""; aConcepto6 = "";

   NumOrd = 0; TotalOrd = 0;
   NumOrdDet = 0;
   NumInd = 0; NumIndDet = 0; TotalInd = 0;
   nTotal003Ord = 0;
   nTotal003    = 0;
   nTotalRegs   = 0;
   nNumero = 0;
   NumRegs = 0;
   NumDomics = 0;
   SwHayNoEmit = 0;
   aLong = ""; nLong = 0;
   SwEmit = "";

   handle = 0;
   SwHay  = 0;

   Cambio = 1;
   Redond = 0;
   Moneda = "P";
   Prefijo = "";

   &eaOrigen = "";
   &Divisa   = "";
   &enEmpCartera = 0; &enEmpGestion = 0; &enBanco = 0;
   &eaSerie = ""; &eaCliGest = ""; &nImporte = 0; &eaTipoOper = "";

   aPais   = "";
   aSufijo = "";
   aDC     = "";
   aNif    = "";
   aIdent  = "";
   aCadena = "";
   aChar1  = "";
   aChar2  = "";
|FIN;

|PROCESO Tipo; |TIPO 0;
     |SI #0#16 = "N";
          |C_M #0#19, 1, "S";
     |SINO;
          |C_M #0#19, 1, "N";
     |FINSI;
|FPRC;

|PROCESO IniVars;
   VAlfa01 = ""; VAlfa02 = ""; VAlfa03 = ""; VAlfa04 = ""; VAlfa05 = ""; VAlfa06 = "";
   VAlfa07 = ""; VAlfa08 = ""; VAlfa09 = ""; VAlfa10 = ""; VAlfa11 = ""; VAlfa12 = "";
   VAlfa13 = ""; VAlfa14 = ""; VAlfa15 = ""; VAlfa16 = ""; VAlfa17 = ""; VAlfa18 = "";
   VAlfa19 = ""; VAlfa20 = ""; VAlfa21 = ""; VAlfa22 = "";
   Field = ""; aTipo = ""; Fecha = ""; Comodin = "";
   NumDig = 0; Calc = 0;
   aLong = ""; nLong = 0;
|FPRC;


|PROCESO ObtenId;
   aPais = "ES";

   FSalida = 1; |PARA; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "/"; |SIGUE;
   FSalida = 1; |PARA; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "-"; |SIGUE;
   FSalida = 1; |PARA; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "?"; |SIGUE;
   FSalida = 1; |PARA; |SI FSalida ! 0; |HACIENDO; aNif = aNif - ":"; |SIGUE;
   FSalida = 1; |PARA; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "("; |SIGUE;
   FSalida = 1; |PARA; |SI FSalida ! 0; |HACIENDO; aNif = aNif - ")"; |SIGUE;
   FSalida = 1; |PARA; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "."; |SIGUE;
   FSalida = 1; |PARA; |SI FSalida ! 0; |HACIENDO; aNif = aNif - ","; |SIGUE;
   FSalida = 1; |PARA; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "'"; |SIGUE;
   FSalida = 1; |PARA; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "+"; |SIGUE;
   FSalida = 1; |PARA; |SI FSalida ! 0; |HACIENDO; aNif = aNif - &34; |SIGUE;

   ||aAlfa = aPais + "00" + aNif;
   aAlfa = aNif + aPais + "00";

   aAlfa = aAlfa > aAlfa;
   aChar1 = ""; aCadena = "";
   |PARA; |SI aAlfa ! ""; |HACIENDO;
      aChar1 = aAlfa % 0101; aAlfa = aAlfa % 0299;
      |SI aChar1 ] "0"; |Y aChar1 [ "9";
          aCadena = aCadena + aChar1;
      |SINO;
         |SI aChar1 ] "A"; |Y aChar1 [ "Z"; |Y aChar1 ! "Ñ";
            nCalc = &aChar1; nCalc = nCalc - 55; aChar1 = nCalc;
            aCadena = aCadena + aChar1;
         |FINSI;
      |FINSI;
   |SIGUE;
   aAlfa = aCadena;
   |HAZ Mod_9710;
   aIdent = aPais + aAlfa + aSufijo + aNif;
|FPRC;

|PROCESO Mod_9710;
   aAlfa2 = aAlfa % 0101;
   |PARA; |SI aAlfa2 = "0"; |HACIENDO;
      aAlfa = aAlfa % 0299; aAlfa2 = aAlfa % 0101;
   |SIGUE;

   aAlfa1 = "";
   |PARA; |SI aAlfa ! ""; |HACIENDO;
      aAlfa2 = aAlfa % 0; nCalc = aAlfa2;
      |SI nCalc > 5;
         aAlfa2 = aAlfa % 0105; aAlfa = aAlfa % 0699;
      |SINO;
         aAlfa2 = aAlfa; aAlfa = "";
      |FINSI;
      aAlfa1 = aAlfa1 + aAlfa2;
      nCalc = aAlfa1;
      nCalc = nCalc / 97;
      aAlfa2 = nCalc;
      aAlfa2 = aAlfa2 - ".";
      |SI FSalida ! 0;
         nCalc = FSalida + 98;
         aAlfa2 = aAlfa2 % nCalc;
         aAlfa2 = "0." + aAlfa2; nCalc = aAlfa2;
      |SINO;
         nCalc = 0;
      |FINSI;
      nCalc = (nCalc * 97) ! 0;
      aAlfa1 = nCalc;
   |SIGUE;
   nCalc  = aAlfa1;
   nCalc = 98 - nCalc;
   aAlfa = ("00" + nCalc) % -102;
|| nCalc  = 98 - (nCalc - (((nCalc / 97) ! 0) * 97)); || MOD 97-10  ISO 7604
|FPRC;

|PROCESO Presentador;
   |HAZ IniVars;

   |SI #dscaz215#16 = "N";
      |SI #dscaz215#19 = "N";
          VAlfa01 = "0119143001";
      |SINO;
          VAlfa01 = "0119154001";
      |FINSI;
   |SINO;
      VAlfa01 = "0119445001";
   |FINSI;
   aSufijo = #dscam014#78; aSufijo = ("000" + aSufijo) % -103; aNif = #dscam014#47; |HAZ ObtenId;
   Field = aIdent;       aTipo = "A"; NumDig = 35; |HAZ Formato ; VAlfa02 = Field;
   Field = #dscam014#46; aTipo = "A"; NumDig = 70; |HAZ Formato ; VAlfa03 = Field;
   Fecha = #dscaz215#6;  |HAZ RetocaFecha; VAlfa04 = Fecha;
   fFecha = ~; aAlfa = fFecha; |HORA aAlfa1;
   aAlfa  = (aAlfa % 0704)  + (aAlfa % 0402)  + (aAlfa % 0102);
   aAlfa1 = (aAlfa1 % 0102) + (aAlfa1 % 0402) + (aAlfa1 % 0702) + "00000";
   VAlfa05 = "PRE" + aAlfa + aAlfa1 + #dscam014#47; ||***
   Field = VAlfa05;      aTipo = "A"; NumDig = 35; |HAZ Formato ; VAlfa05 = Field;
   Field = #dscam014#34; aTipo = "N"; NumDig = 4;  |HAZ Formato ; VAlfa06 = Field;
   Field = #dscam014#35; aTipo = "N"; NumDig = 4;  |HAZ Formato ; VAlfa07 = Field;
   VAlfa = VAlfa01 + VAlfa02 + VAlfa03 + VAlfa04 + VAlfa05 + VAlfa06 + VAlfa07;
   VAlfa = Handle + " " + VAlfa; |FGRABA VAlfa;

   VAlfa08 = (" " * 200);
   VAlfa = Handle + " " + VAlfa08; |FGRABA VAlfa;
   VAlfa09 = (" " * 234);
   VAlfa = Handle + " " + VAlfa09 + aFinLin; |FGRABA VAlfa;

   nTotalRegs   = nTotalRegs   + 1;
   TotalOrd = 0;
   NumOrd = 0; NumOrdDet = 0;
   nNumero = 0;
|FPRC;

|PROCESO Ordenante;
   |HAZ IniVars;

   |SI #dscaz215#16 = "N";
      |SI #dscaz215#19 = "N";
          VAlfa01 = "0219143002";
      |SINO;
          VAlfa01 = "0219154002";
      |FINSI;
   |SINO;
      VAlfa01 = "0219445002";
   |FINSI;
   aSufijo = #dscam014#80; aSufijo = ("000" + aSufijo) % -103; aNif = #dscam014#53; |HAZ ObtenId;
   Field = aIdent;       aTipo = "A"; NumDig = 35; |HAZ Formato ; VAlfa02 = Field;
   Fecha = #dscaz215#7;  |HAZ RetocaFecha; VAlfa03 = Fecha;
   Field = #dscam014#52; aTipo = "A"; NumDig = 70; |HAZ Formato ; VAlfa04  = Field;

   VAlfa = VAlfa01 + VAlfa02 + VAlfa03 + VAlfa04;
   VAlfa = Handle + " " + VAlfa; |FGRABA VAlfa;

   Field = #dscam014#74;   aTipo = "A"; NumDig = 50; |HAZ Formato ; VAlfa05 = Field;
   Field = #dscam014#54;   aTipo = "A"; NumDig = 50; |HAZ Formato ; VAlfa06 = Field;
   Field = #dscam014#75;   aTipo = "A"; NumDig = 40; |HAZ Formato ; VAlfa07 = Field;
   VAlfa08 = #dscam014#76;
   aAlfa = (("0000" + #dscam014#55) % -104) + (("0000" + #dscam014#56) % -104) + (("00" + #dscam014#58) % -102);
   aAlfa = aAlfa + (("0000000000" + #dscam014#57) % -110) + #dscam014#76 + "00";

   aAlfa = aAlfa > aAlfa;
   aChar1 = ""; aCadena = "";
   |PARA; |SI aAlfa ! ""; |HACIENDO;
      aChar1 = aAlfa % 0101; aAlfa = aAlfa % 0299;
      |SI aChar1 ] "0"; |Y aChar1 [ "9";
          aCadena = aCadena + aChar1;
      |SINO;
         |SI aChar1 > "A"; |Y aChar1 < "Z"; |Y aChar1 ! "Ñ";
            nCalc = &aChar1; nCalc = nCalc - 55; aChar1 = nCalc;
            aCadena = aCadena + aChar1;
         |FINSI;
      |FINSI;
   |SIGUE;
   aAlfa = aCadena;
   |HAZ Mod_9710;

   VAlfa09 = #dscam014#76 + aAlfa + (("0000" + #dscam014#55) % -104) + (("0000" + #dscam014#56) % -104);
   VAlfa09 = VAlfa09 + (("00" + #dscam014#58) % -102) + (("0000000000" + #dscam014#57) % -110);
   VAlfa09 = VAlfa09 + (" " * 10);

   VAlfa = VAlfa05 + VAlfa06 + VAlfa07;
   VAlfa = VAlfa   + VAlfa08 + VAlfa09;
   VAlfa = Handle + " " + VAlfa; |FGRABA VAlfa;

   VAlfa10 = " " * 200;
   VAlfa = Handle + " " + VAlfa10; |FGRABA VAlfa;
   VAlfa11 = " " * 101;
   VAlfa = Handle + " " + VAlfa11 + aFinLin; |FGRABA VAlfa;

   TotalInd = 0;
   NumInd = 0; NumIndDet = 0;
   nTotal003Ord = 0;
   nNumero = nNumero + 1;
   nTotalRegs   = nTotalRegs   + 1;
|FPRC;

|PROCESO Individual;
   #dscam003#40 = #dscam013#16;
   |LEE 121#dscam003.=;
   |SI FSalida ! 0; |ACABA; |FINSI;

   |HAZ CargaContaGes;
   |SI enError ! 0; |ACABA; |FINSI;
   |HAZ MiraMoneda;

   #dscam001#0 = #dscam003#8;
   |LEE 000#dscam001.=;
   |SI FSalida ! 0; |DDEFECTO #dscam001; |FINSI;

   Divisa = #dscam003#57; |HAZ TomaDecim;

   |HAZ IniVars;
   eaCodigo = #dscam003#32; eaTipoCod = "C"; aCuenta = #dscam003#5;
   |HAZ LeeDatos;
   eaCodPais = (eaCodPais + "  ") % 102;
   |QBF eaCodPais;
   |QBF eaCodBIC;

   eaNif = eaNif > eaNif;
   Field = eaNif; aTipo = "A"; NumDig = 12; |HAZ Formato ; CIF = Field;

   |HAZ IniVars;

   |SI #dscaz215#16 = "N";
      |SI #dscaz215#19 = "N";
          VAlfa01 = "0319143003";
      |SINO;
          VAlfa01 = "0319154003";
      |FINSI;
   |SINO;
      VAlfa01 = "0319445003";
   |FINSI;

   VAlfa02 = eaNif; |QBF VAlfa02;
   VAlfa02 = VAlfa02 + "CLI" + #dscam003#32;

   VAlfa02 = VAlfa02 + #dscam003#0+(("000000"+#dscam003#1)%-106)+(("000"+#dscam003#2)%-103);

   VAlfa02 = (VAlfa02 + (" " * 35)) % 0135;
   Field = CIF;  aTipo = "A"; NumDig = 9; |HAZ Formato ; aAlfa = Field;
   aAlfa = aAlfa + "000";
   VAlfa03 = aAlfa;
   VAlfa03 = (VAlfa03 + (" " * 35)) % 0135;
   VAlfa04 = "RCUR";
   VAlfa05 = "SUPP";

   Field = #dscam013#7;  aTipo = "I"; NumDig = 11;
   |HAZ Formato; VAlfa06  = Field;

   aAlfa = eaFechaMand; |QBF aAlfa;
   aAlfa = ("          " + aAlfa) % -110;
   VAlfa07 = (aAlfa % 0704) + (aAlfa % 0402) + (aAlfa % 0102);

   |SI eaCodPais = "ES";
          Field = #dscam001#3;
   |SINO;
          Field = eaCodBIC;
   |FINSI;

   aTipo = "A"; NumDig = 11; |HAZ Formato; VAlfa08  = Field;

   Field = #dscam003#6; aTipo = "A"; NumDig = 70; |HAZ Formato; VAlfa09  = Field;

   VAlfa = VAlfa01 + VAlfa02 + VAlfa03 + VAlfa04 + VAlfa05 + VAlfa06 + VAlfa07;
   VAlfa = VAlfa   + VAlfa08 + VAlfa09;
   VAlfa = Handle + " " + VAlfa; |FGRABA VAlfa;

   Field = eaDireccion;  aTipo = "A"; NumDig = 50; |HAZ Formato; VAlfa10  = Field;
   |SI eaCodPais = "ES";
       aAlfa = eaCP;
   |SINO;
      aAlfa = eaCPExt;
   |FINSI;
   Field = aAlfa + " " + eaPoblacion; aTipo = "A"; NumDig = 50; |HAZ Formato; VAlfa11  = Field;
   Field = eaProvincia;  aTipo = "A"; NumDig = 40; |HAZ Formato; VAlfa12  = Field;

   VAlfa13 = eaCodPais;
   |SI eaCodPais = ""; VAlfa13 = "ES"; |FINSI;

   aAlfa = eaNif; |QBF aAlfa; aAlfa = aAlfa % 0101;
   |SI aAlfa ] "A"; |Y aAlfa [ "W";
      VAlfa14 = "1"; || Persona juridica
   |SINO;
      VAlfa14 = "2"; || Persona fisica
   |FINSI;

   |SI VAlfa14 = "1"; || Persona juridica
      aAlfa = eaCodBIC; |QBF aAlfa;
      |SI aAlfa = "";
         VAlfa15 = "I" + eaNif;
         Field = VAlfa15;  aTipo = "A"; NumDig = 36; |HAZ Formato; VAlfa15  = Field;
      |SINO;
         VAlfa15 = "A" + eaCodBIC;
         Field = VAlfa15;  aTipo = "A"; NumDig = 36; |HAZ Formato; VAlfa15  = Field;
      |FINSI;
   |FINSI;
   |SI VAlfa14 = "2"; || Persona juridica
      VAlfa15 = "J" + eaNif;
      Field = VAlfa15;  aTipo = "A"; NumDig = 36; |HAZ Formato; VAlfa15  = Field;
   |FINSI;

   VAlfa = VAlfa10 + VAlfa11 + VAlfa12 + VAlfa13 + VAlfa14 + VAlfa15;
   VAlfa = Handle + " " + VAlfa; |FGRABA VAlfa;

   VAlfa16 = " " * 35;
   VAlfa17 = "A";

   |SI eaCodPais = "ES";
          eaIBANant = "";
          eaCta     = #dscam003#8 + #dscam003#9 + #dscam003#10 + #dscam003#11;
          eaPais    = "ES";
          |HAZ CalcuIBAN;
          Field = eaIBAN;
   |SINO;
          Field = eaCodIBAN;
   |FINSI;

   aTipo = "A"; NumDig = 34; |HAZ Formato;
   VAlfa18  = Field;
   VAlfa19 = "SUPP";

   VAlfa20 = #dscam014#59; |QBF VAlfa20;
   |SI VAlfa20 ! ""; VAlfa20 = VAlfa20 + " "; |FINSI;
   |SI #dscam014#60 = "S"; VAlfa20 = VAlfa20 + #dscam003#0; |FINSI;
   |SI #dscam014#61 = "S"; VAlfa20 = VAlfa20 + "/" + (("000000" + #dscam003#1) % -106); |FINSI;
   |SI #dscam014#62 = "S"; VAlfa20 = VAlfa20 + "/" + (("000" + #dscam003#2) % -103); |FINSI;
   Field = VAlfa20;  aTipo = "A"; NumDig = 90; |HAZ Formato; VAlfa20 = Field;
   VAlfa21 = " " * 50;
   VAlfa22 = " " * 19;

   VAlfa = VAlfa16 + VAlfa17 + VAlfa18 + VAlfa19 + VAlfa20 + VAlfa21 + VAlfa22;
   VAlfa = Handle + " " + VAlfa + aFinLin; |FGRABA VAlfa;

   NumInd = NumInd + 1; NumIndDet = NumIndDet + 1;
   nTotal003Ord = nTotal003Ord + 1; nTotal003 = nTotal003 + 1;
   nTotalRegs   = nTotalRegs   + 1;
   TotalInd = TotalInd + #dscam013#7;
   TotalOrd = TotalOrd + #dscam013#7;
   NumDomics = NumDomics + 1;

   |SI #dscam012#23 = "N"; |HAZ ActRecibo; |FINSI;
   |LIBERA #dscam003;

   |HAZ DescargaContaGes;
|FPRC;

|PROCESO ActRecibo;
   || Actualizo  el historico de cobros
   #dscam004#17 = #dscam013#16;
   #dscam004#3 = 99;
   |LEE 000#dscam004.];
   |SI FSalida ! 0;
      |LEE 010#dscam004.u;
      |SI FSalida = 0; |Y #dscam004#17 = #dscam013#16;
         LinCob = #dscam004#3 + 1;
      |SINO;
         LinCob = 1;
      |FINSI;
   |SINO;
      |SI #dscam004#17 = #dscam013#16;
         LinCob = #dscam004#3 + 1;
      |SINO;
         |LEE 010#dscam004.a;
         |SI FSalida = 0; |Y #dscam004#17 = #dscam013#16;
            LinCob = #dscam004#3 + 1;
         |SINO;
            LinCob = 1;
         |FINSI;
      |FINSI;
   |FINSI;

   |DDEFECTO #dscam004;
   #dscam004#17 = #dscam013#16;
   #dscam004#15 = #dscam013#2;
   #dscam004#0  = #dscam013#4;
   #dscam004#1  = #dscam013#3;
   #dscam004#2  = #dscam013#5;
   #dscam004#3  = LinCob;
   #dscam004#4  = #dscam012#14;
   #dscam004#5  = "EMR";
   #dscam004#6  = #dscaz215#6;
   #dscam003#40 = #dscam013#16;
   |LEE 010#dscam003.=;
   |SI FSalida = 0;
      #dscam004#7  = #dscam003#23; #dscam004#25  = #dscam003#61;
   |FINSI;
   #dscam004#8  = 0;
   #dscam004#9  = 0;
   #dscam004#10 = 0;
   #dscam004#26 = 0;
   |GRABA 020#dscam004.n;

   #dscam013#24 = LinCob; |GRABA 020#dscam013.a;
|FPRC;

|PROCESO TotalOrden;
   VAlfa01 = "04";

   aSufijo = #dscam014#80; aSufijo = ("000" + aSufijo) % -103; aNif = #dscam014#53; |HAZ ObtenId;
   Field = aIdent;       aTipo = "A"; NumDig = 35; |HAZ Formato; VAlfa02 = Field;
   Fecha = #dscaz215#7;  |HAZ RetocaFecha;                       VAlfa03 = Fecha;
   Field = TotalInd;     aTipo = "I"; NumDig = 17; |HAZ Formato; VAlfa04  = Field;
   Field = NumInd;       aTipo = "N"; NumDig = 8;  |HAZ Formato; VAlfa05  = Field;
   Calc = NumIndDet + 2;  || Las lineas contadas mas la linea del ordenante
                          ||     y esta misma
   Field = Calc; aTipo = "N"; NumDig = 10;|HAZ Formato ; VAlfa06  = Field;
   VAlfa = VAlfa01 + VAlfa02 + VAlfa03 + VAlfa04 + VAlfa05 + VAlfa06;
   VAlfa = Handle + " " + VAlfa; |FGRABA VAlfa;

   VAlfa07 = " " * 120;
   VAlfa = Handle + " " + VAlfa07; |FGRABA VAlfa;

   VAlfa08 = " " * 200;
   VAlfa = Handle + " " + VAlfa08;           |FGRABA VAlfa;
   VAlfa = Handle + " " + VAlfa08 + aFinLin; |FGRABA VAlfa;

   nTotalRegs   = nTotalRegs   + 1;

   Calc = NumIndDet + 3;
   Field = Calc; aTipo = "N"; NumDig = 10;|HAZ Formato ; VAlfa06  = Field;
   VAlfa01 = "05";
   VAlfa = VAlfa01 + VAlfa02 + VAlfa04 + VAlfa05 + VAlfa06;
   VAlfa = Handle + " " + VAlfa;             |FGRABA VAlfa;

   VAlfa07 = " " * 128;
   VAlfa = Handle + " " + VAlfa07;           |FGRABA VAlfa;

   VAlfa08 = " " * 200;
   VAlfa = Handle + " " + VAlfa08;           |FGRABA VAlfa;
   VAlfa = Handle + " " + VAlfa08 + aFinLin; |FGRABA VAlfa;

   NumOrd = NumOrd + NumInd; NumOrdDet = NumOrdDet + NumIndDet + 2;
   nTotalRegs   = nTotalRegs   + 1;
|FPRC;

|PROCESO TotalGeneral;
   VAlfa01 = "99";
   Field = TotalOrd;     aTipo = "I"; NumDig = 17;|HAZ Formato ; VAlfa02 = Field;
   Field = nTotal003;    aTipo = "N"; NumDig = 8; |HAZ Formato ; VAlfa03  = Field;
   Calc = nTotalRegs   + 1;
   Field = Calc;         aTipo = "N"; NumDig = 10;|HAZ Formato ; VAlfa04  = Field;
   VAlfa = VAlfa01 + VAlfa02 + VAlfa03 + VAlfa04;
   VAlfa = Handle + " " + VAlfa; |FGRABA VAlfa;

   VAlfa05 = " " * 163;
   VAlfa = Handle + " " + VAlfa05; |FGRABA VAlfa;

   VAlfa06 = " " * 200;
   VAlfa = Handle + " " + VAlfa06; |FGRABA VAlfa;
   VAlfa = Handle + " " + VAlfa06 + aFinLin; |FGRABA VAlfa;
|FPRC;

|DEFBUCLE Recibos;
#dscam013#1;
;
#dscam012#0,001;
#dscam012#0,999;
be;
NULL,Individual;
|FIN;

|PROCESO Remesas;

   #dscam012#0 = #dscaw038#0;
   |LEE 101#dscam012.=;
   |SI FSalida = 0; SwEmit = #dscam012#23; |FINSI;

   eaUsuario = Usuario % 0810;
   eaDescr   = "EMISION REMESA (" + (("00000" + #dscam012#0) % -105);
   eaDescr   = eaDescr + ") EN DISKETTE DESDE NORMA 19.44";
   eaTipoReg = "RE";  eaNumero = ("00000" + #dscam012#0) % -105;
   eaTipoOp  = "E";   enImpAnt = #dscam012#10; enImpAct = #dscam012#10;
   |HAZ GrabaAudit;

   #dscam015#0 = #dscam012#2;
   |LEE 000#dscam015.=;
   |SI FSalida = 0; |DDEFECTO #dscam015; |FINSI;

   |BUCLE Recibos;
   |SI #dscam012#23 = "N";
      #dscam012#23 = "S"; |GRABA 020#dscam012.a;
   |FINSI;

   |SI nSwConta = 1; |Y SwEmit = "N"; |Y #dscam051#1 = "S";
      |ABRE #dscam030;
      #dscam030#0 = #dscaz215#14;
      |LEE 010#dscam030.=;
      |SI FSalida = 0;
         |SI #dscam030#4 ! #dscaw038#3; |O #dscam030#5 ! #dscaw038#4;
            EmpreAnt = #dscam030#4; PerioAnt = #dscam030#5;
            #dscam030#4 = #dscaw038#3; #dscam030#5 = #dscaw038#4;
            |GRABA 020#dscam030.a;
         |SINO;
            EmpreAnt = -1;
         |FINSI;
         Comodin = Usuario; |QBF Comodin; Comodin = "ac" + Comodin;
         |NOME_DAT #9 Comodin;
         |DELINDEX #dscaw013; |ABRE #dscaw013;
         Comodin = #dscaz215#14;
         |SI Comodin = "    ";
            |ABRE #dscam093;
            #dscam093#0 = "E";
            |LEE 000#dscam093.=;
            |SI FSalida = 0;
               |SI #dscam012#22 = 1; Comodin = #dscam093#1; |FINSI;
               |SI #dscam012#22 = 2; Comodin = #dscam093#3; |FINSI;
               |SI #dscam012#22 = 3; Comodin = #dscam093#5; |FINSI;
               |SI #dscam012#22 = 4; Comodin = #dscam093#7; |FINSI;
               |SI #dscam012#22 = 5; Comodin = #dscam093#9; |FINSI;
            |FINSI;
            |CIERRA #dscam093;
         |FINSI;

         |DDEFECTO #dscaw013;
         #dscaw013#0  = 1;            #dscaw013#15 = Comodin;
         #dscaw013#5  = #dscam012#0;  #dscaw013#6  = #dscam012#0;
         #dscaw013#11 = "N";          #dscaw013#12 = 5;
         #dscaw013#13 = "dscam013";   #dscaw013#14 = 0;
         |GRABA 020#dscaw013.n;
         |CIERRA #dscaw013;
         |LIBERA #dscam012;

         Comodin = "dscam033.tab;" + Comodin + ",I";
         |LIBERA #dscam012; |CIERRAT #dscam012;
         |SUB_EJECUTA_NP Comodin;
         |ABRET #dscam012; |LEE 101#dscam012.=;

         |DFICE aAlfa; |QBF aAlfa; aAlfa = "ac" + Usuario + ".dat"; |FBORRA aAlfa;
         |DFICE aAlfa; |QBF aAlfa; aAlfa = "ac" + Usuario + ".ddx"; |FBORRA aAlfa;
         |DFICE aAlfa; |QBF aAlfa; aAlfa = "ac" + Usuario + ".ixx"; |FBORRA aAlfa;

         |SI EmpreAnt ! -1;
            #dscam030#4 = EmpreAnt; #dscam030#5 = PerioAnt;
            |GRABA 020#dscam030.a;
         |FINSI;
      |FINSI;
      |CIERRA #dscam030;
   |FINSI;

   |LIBERA #dscam012;
|FPRC;

|DEFBUCLE Remesas;
#dscaw038#1;
;
INICIO;
FINAL;
;
NULL,Remesas;
|FIN;

|PROCESO MiraRemesas;
   SwHay = 1;
   |DDEFECTO #dscaw038;
   #dscaw038#0 = #dscam012#0;
   #dscaw038#1 = #dscam012#10;
   #dscaw038#2 = #dscaz215#14;
   #dscaw038#5 = #dscam012#23;
   |SI #dscaw038#5 = "N";
      SwHayNoEmit = 1;
   |FINSI;

   |ABRE #dscam030;
   #dscam030#0 = #dscaz215#14;
   |LEE 000#dscam030.=;
   |SI FSalida = 0;
      #dscaw038#3 = #dscam030#4;
      #dscaw038#4 = #dscam030#5;
   |FINSI;
   |CIERRA #dscam030;
   |GRABA 020#dscaw038.n;
|FPRC;

|DEFBUCLE MiraRemesas;
#dscam012#1;
1,2;
#0#0,#0#2,#dscam014#0;
#0#1,#0#3,#dscam014#0;
;
NULL,MiraRemesas;
|FIN;

|RUTINA infw038;
   #dscaw038#0 = 00001;
   #dscaw038#5 = "N";
|FRUT;

|RUTINA supw038;
   #dscaw038#0 = 99999;
   #dscaw038#5 = "N";
|FRUT;

|PROCESO Secuencial;

   |DELINDEX #dscaw038;
   SwHayNoEmit = 0;
   SwHay       = 0;
   |ABRE #dscaw038;
   |BUCLE MiraRemesas;
   |CIERRA #dscaw038;
   |SI SwHay = 0; |ACABA; |FINSI;

   |SI #dscaz215#15 = "N";
      |SI SwHayNoEmit = 1;
         |PUSHV 0501 2380;
         |ENTLINEAL #1#dscaw038,1,2,19,1,infw038,supw038;
         |POPV;
      |SINO;
         |MENAV "    No hay remesas sin emitir para redireccionar";
      |FINSI;
   |FINSI;

   aAlfa = #dscam014#46; |QBF aAlfa;
   |SI aAlfa = "";
       aAlfa = #dscam014#47; |QBF aAlfa;
       |SI aAlfa = "";
           aAlfa = #dscam014#53; |QBF aAlfa;
           |SI aAlfa = "";
               aAlfa = #dscam014#54; |QBF aAlfa;
               |SI aAlfa = "";
                   aAlfa = #dscam014#72; |QBF aAlfa;
                   |SI aAlfa = "";
                      |PUSHV 0910 1970; |BLANCO 0910 1970;
                      |CUADRO 0910 1970;
                      |ATRI R;
                      |PINTA 1012, "Los datos para la emision de remesas en soporte magnetico";
                      |PINTA 1112, "estan vacios. Seleccione un registro del fichero anterior";
                      |PINTA 1212, "del cual importar los datos necesarios.                  ";
                      |ATRI N;
                       nPos1 = 1312; nPos2 = 1868; nModo = 2 + 4 + 8 + 16;
                      |ABRE #dscam015;
                    |ET OtraVez;
                      |LINEAL_SIMPLE #1#dscam015,nModo, nPos1, nPos2, NULL, NULL, NULL;
                      |SI FSalida < 0;
                          |CONFI "    SSin datos para emitir remesas en soporte magnetico no podra emitir dichas normas. Continuar ? ";
                          |SI FSalida ! 0;
                              |VETE OtraVez;
                          |SINO;
                              |LIBERA #dscam014;
                              |ACABA;
                          |FINSI;
                      |SINO;
                          #dscam014#46 = #dscam015#2;
                          #dscam014#47 = #dscam015#3;
                          #dscam014#48 = #dscam015#12;
                          #dscam014#49 = #dscam015#13;
                          #dscam014#72 = #dscam015#19;
                          #dscam014#51 = #dscam015#20;
                          #dscam014#52 = #dscam015#6;
                          #dscam014#53 = #dscam015#7;
                          #dscam014#54 = #dscam015#21;
                          #dscam014#55 = #dscam015#8;
                          #dscam014#56 = #dscam015#9;
                          #dscam014#57 = #dscam015#11;
                          #dscam014#58 = #dscam015#10;
                          #dscam014#59 = #dscam015#14;
                          #dscam014#60 = #dscam015#15;
                          #dscam014#61 = #dscam015#16;
                          #dscam014#62 = #dscam015#17;
                          |GRABA 020#dscam014.a;
                      |FINSI;
                      |CIERRA #dscam015;
                      |POPV;
                 |FINSI;
              |FINSI;
           |FINSI;
        |FINSI;
   |FINSI;

   #dscaz215#8  = #dscam014#78;
   #dscaz215#17 = #dscam014#80;
   |SI #dscaz215#8 ! "   "; |Y #dscaz215#17 = 0;
       #dscaz215#17 = #dscaz215#8;
   |FINSI;
   |SI #dscaz215#8 = "   "; |Y #dscaz215#17 ! 0;
       #dscaz215#8 = #dscaz215#17;
   |FINSI;

   |DFICB Comodin;
   Comodin1 = ("000" + #dscam014#0) % -103;
   Comodin1 = "n1900" + Comodin1 + ".seq";
   eaOrigen = Comodin; eaNombre1 = Comodin1;
   Comodin = "wb  " + Comodin + Comodin1;
   |QBF Comodin;
   |FABRE Comodin;
   handle = FSalida;
   |SI FSalida ] 0;
      |ABRE #dscam004;
      |ABRE #dscam003;
      |ABRE #dscam012;
      |ABRE #dscam013;
      |ABRE #dscam015;

      nCalc = 13; aFinLin = &nCalc;
      nCalc = 10; aFinLin = aFinLin + &nCalc;
      aFinFic = "";

      |HAZ Presentador;  NumRegs = NumRegs + 1;
      |HAZ Ordenante;    NumRegs = NumRegs + 1;
      |BUCLE Remesas;    NumRegs = NumRegs + NumInd;
      |HAZ TotalOrden;   NumRegs = NumRegs + 1;
      |HAZ TotalGeneral; NumRegs = NumRegs + 1;
      #0#9 = NumDomics;
      #0#10 = NumRegs;
      #0#11 = TotalOrd;
      |PINTA #0#9; |PINTA #0#10; |PINTA #0#11;
      |PAUSA;
      |CIERRA #dscam015;
      |CIERRA #dscam013;
      |CIERRA #dscam012;
      |CIERRA #dscam003;
      |CIERRA #dscam004;
   |FINSI;
   VAlfa = Handle + " " + aFinFic; |FGRABA VAlfa;

   FSalida = handle; |FCIERRA FSalida;

   |LIBERA #dscam014;

   |SUB_EJECUTA_NP "dscaz032.tab";
|FPRC;

|DEFBUCLE Secuencial;
   #dscam014#1;
   ;
   #0#4;
   #0#5;
   be;
   NULL,Secuencial;
|FIN;

|RUTINA infm161;
   #dscam161#0 = 000001;
|FRUT;

|RUTINA supm161;
   #dscam161#0 = 999999;
|FRUT;

|PROCESO Boton;
   |PUSHV 1501 2380;
   |ENTLINEAL #1#dscam161, -1, 1, 22, 1, infm161, supm161;
   |POPV;
|FPRC;

|PROCESO RetocaFecha;
   |QBF Fecha;
   Comodin = Fecha % 0704;  || A¤o
   Comodin = Comodin + (Fecha % 0402);  || Mes
   Comodin = Comodin + (Fecha % 0102);  || Dia
   Fecha = Comodin;
|FPRC;

|PROCESO Formato;
   |SI aTipo = "I";
      Calc = Field;
      Calc = (Calc * Cambio) ! Redond;
      |SI Moneda = "E"; Calc = Calc * 100; |FINSI;
      Field = Calc;
   |FINSI;
   |SI aTipo = "A";
      Comodin = " " * NumDig;
      Comodin = Field + Comodin;
      Calc = 100 + NumDig;
   |SINO;
      Comodin = "0" * NumDig;
      Comodin = Comodin + Field;
      Calc = 0 - (100 + NumDig);
   |FINSI;

   |QBF Field;
   Field = Comodin % Calc;
|FPRC;

|PROCESO CampoEnlace; |TIPO 7;
   |ABRE #dscam051;
   |LEE 000#dscam051.p;
   |SI FSalida ! 0;
      |DDEFECTO #dscam051;
      |GRABA 020#dscam051.n;
      |LEE 000#dscam051.p;
   |FINSI;
   |CIERRA #dscam051;
   |SI nSwConta = 1; |Y #dscam051#1 = "S";
      |ABRE #dscam030;
      #dscam030#0 = #dscam051#53;
      |LEE 000#dscam030.=;
      |SI FSalida = 0;
         ||PINTA 2120, #dscam030#1;
      |FINSI;
      |CIERRA #dscam030;
      #dscaz215#14 = #dscam051#53; |PINTA #dscaz215#14;
      aAlfa = #dscam051#61; |QBF aAlfa;
      |C_M #dscaz215#14,1,aAlfa;
   |SINO;
      #dscaz215#14 = ""; |PINTA #dscaz215#14;
      |C_M #dscaz215#14,1,"N";
   |FINSI;
|FPRC;

|PROCESO MiraMoneda;
      Moneda = "E";
      Cambio = 1;
      Redond = 2;
      Prefijo = "5";
      |SI HayGestion = 1;
         |SI #agif321@#9 = "ESP";
            |SI Moneda = "P";
               Cambio = 1;
               Redond = 0;
               Prefijo = "0";
            |SINO;
               Cambio = 1/166.386;
               Redond = 2;
               Prefijo = "5";
            |FINSI;
         |FINSI;
         |SI #agif321@#9 = "EUR";
            |SI Moneda = "P";
               Cambio = 166.386;
               Redond = 0;
               Prefijo = "0";
            |SINO;
               Cambio = 1;
               Redond = 2;
               Prefijo = "5";
            |FINSI;
         |FINSI;
      |FINSI;
|FPRC;

|PROGRAMA;
     ||HAZ Tipo8;

     |DBASE aBase;
     aAlfa = aBase + "def/dscam000";
     |CARGA_DEF aAlfa, dscam000@;
     |SI FSalida = 0;
          aAlfa = aBase + "fich/";
          |PATH_DAT #dscam000@#aAlfa#;
          |DFICE aAlfa;
          aAlfa = aAlfa % -205;
          |ABRE #dscam000@;
          #dscam000@#0 = aAlfa;
          |LEE 020#dscam000@.=;
          |CIERRA #dscam000@;

          aAlfa = #dscam000@#16; |QBF aAlfa;
          |SI aAlfa ! ""; nSwConta = 1; |FINSI;

          |DESCARGA_DEF #dscam000@;
     |FINSI;


     |SI PARAMETRO = "AUTO";
          #0#0 = enDRemesa;
          #0#1 = enHRemesa;
          #0#2 = efDFecha;
          #0#3 = efHFecha;
          #0#4 = enDBanco;
          #0#5 = enHBanco;
          #0#6 = efFecEmi;
          #0#7 = efFecCar;
          #0#16 = eaB2B;
          #0#19 = eaRedu;
          #0#14 = eaEnlace;
          FSalida = 0;
     |SINO;
          |CLS;
          |CABEZA "Emision diskette norma 19";
          |PINPA #0#0;
          |PINDA #0#0;
          |ENDATOS #1#0;
     |FINSI;
     |SI FSalida = 0;
          |HAZ MiraMoneda;
          nTotal003 = 0; nTotalRegs   = 0;
          |ABRE #dscam001;
          |BUCLE Secuencial;
          |CIERRA #dscam001;
     |FINSI;

     ||HAZ Tipo9;
|FPRO;
