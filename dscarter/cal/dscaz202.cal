|FICHEROS;
     dscaz202 #0;
     dscam003 #3;
     dscam004 #4;
     dscam008 #8;
     dscam009 #9;
     dscam067 #67;
|FIN;

|VARIABLES;
     &aInforme = "";
     aAlfa = "";
     i = 0;
     nSwContable = 0;
     aClave1 = "";
     aClave2 = "";
     aClave3 = "";
|FIN;

|PROCESO Contable;
     |PRINT;
     nSwContable = 1;
     |ERROR10;
|FPRC;

|DEFBUCLE Contable;
     #67#1;
     ;
     "  ", aClave1, aClave2, aClave3, 000000, 00000, 0;
     "zz", aClave1, aClave2, aClave3, 999999, 99999, 9;
     ;
     NULL, Contable;
|FIN;

|PROCESO Pagos;
     |SI #0#4 = "S";
          |SI #9#5 < #0#5; |O #9#5 > #0#6;
               |ACABA;
          |FINSI;
     |SINO;
          |PARA i = 7; |SI i [ 26; |HACIENDO i = i + 1;
               |SI #9#5 = #0i; |SAL; |FINSI;
          |SIGUE;
          |SI i > 26; |ACABA; |FINSI;
     |FINSI;

     |DDEFECTO #8;
     #8#40 = #9#14;
     |LEE 000#8=;

     |SI #8#12 < #0#27; |O #8#12 > #0#28;
          |ACABA; || tipo recibo
     |FINSI;

     aClave1 = #9#14;
     aClave2 = #9#3;
     aClave3 = " " * 50;
     nSwContable = 0;
     |BUCLE Contable;
     |SI nSwContable = 0; |Y #0#1 = "N";
          |PRINT;
     |FINSI;
|FPRC;

|DEFBUCLE Pagos;
     #9#4;
     ;
     #0#2;
     #0#3;
     ;
     NULL, Pagos;
|FIN;

|PROCESO Cobros;
     |SI #0#4 = "S";
          |SI #4#5 < #0#5; |O #4#5 > #0#6;
               |ACABA;
          |FINSI;
     |SINO;
          |PARA i = 7; |SI i [ 26; |HACIENDO i = i + 1;
               |SI #4#5 = #0i; |SAL; |FINSI;
          |SIGUE;
          |SI i > 26; |ACABA; |FINSI;
     |FINSI;

     |DDEFECTO #3;
     #3#40 = #4#17;
     |LEE 000#3=;

     |SI #3#12 < #0#27; |O #3#12 > #0#28;
          |ACABA; || tipo recibo
     |FINSI;

     aClave1 = #4#17;
     aClave2 = #4#3;
     aClave3 = " " * 50;
     nSwContable = 0;
     |BUCLE Contable;
     |SI nSwContable = 0; |Y #0#1 = "N";
          |PRINT;
     |FINSI;
|FPRC;

|DEFBUCLE Cobros;
     #4#7;
     ;
     #0#2, 000000001;
     #0#3, 999999999;
     ;
     NULL, Cobros;
|FIN;

|PROCESO Opera; |TIPO 0;
     |SI #0Campo ! "   "; |Y #0Campo ! "COB"; |Y #0Campo ! "RCB";
               |Y #0Campo ! "REC"; |Y #0Campo ! "CAC"; |Y #0Campo ! "RCB";
               |Y #0Campo ! "COP"; |Y #0Campo ! "IMP"; |Y #0Campo ! "RCB";
               |Y #0Campo ! "RAC"; |Y #0Campo ! "RTA"; |Y #0Campo ! "RCB";
               |Y #0Campo ! "RPT"; |Y #0Campo ! "PRE"; |Y #0Campo ! "RCB";
               |Y #0Campo ! "ENV"; |Y #0Campo ! "ACE"; |Y #0Campo ! "RCB";
               |Y #0Campo ! "REM"; |Y #0Campo ! "EMR"; |Y #0Campo ! "RCB";
               |Y #0Campo ! "PRC"; |Y #0Campo ! "RCE"; |Y #0Campo ! "RCB";
               |Y #0Campo ! "RCF"; |Y #0Campo ! "FAP"; |Y #0Campo ! "FAR";
               |Y #0Campo ! "CFR";
          |MENAV "0000Operacion incorrecta...";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO DesdeHasta; |TIPO 0;
     |SI #0#4 = "S";
          |C_M #0#5, 1, "S";
          |C_M #0#6, 1, "S";
          |PARA i = 7; |SI i [ 26; |HACIENDO i = i + 1;
               |C_M #0i,1, "N";
          |SIGUE;
     |SINO;
          |C_M #0#5, 1, "N";
          |C_M #0#6, 1, "N";
          |PARA i = 7; |SI i [ 26; |HACIENDO i = i + 1;
               |C_M #0i,1, "S";
          |SIGUE;
     |FINSI;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |IMPRE 0;
     |SI FSalida = 0;
          |SI #0#0 = "C";
               aInforme = "Inf.estado contable mov. cob."; |HAZ ObtenInforme;
               |INFOR aInforme;
          |SINO;
               aInforme = "Inf.estado contable mov. pag."; |HAZ ObtenInforme;
               |INFOR aInforme;
          |FINSI;
          |SI FSalida = 0;
               |SI #0#0 = "C";
                    |ABRE #3;
                    |BUCLE Cobros;
                    |CIERRA #3;
               |SINO;
                    |ABRE #8;
                    |BUCLE Pagos;
                    |CIERRA #8;
               |FINSI;
               |PIEINF;
               |FININF;
          |SINO;
               aAlfa = "0000Error informe '" + aInforme + "'";
               |MENAV aAlfa;
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;
