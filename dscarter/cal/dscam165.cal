|FICHEROS;
   dscam165  #0;
   dscam043  #10;

   dsmom030@ #100;
   camandia@ #101;
|FIN;

|VARIABLES;
   aAlfa  = "";
   aAlfa1 = "";
   aEmpresa = "";

   nCampo = 0;
|FIN;

|PROCESO MiraDiario;
   |SI FSalida = 10;
      #camandia@#0 = #dscam165#0;
      |LEE 000#camandia@.];
      |CONSULTA_CLAVE #1#camandia@;
      #dscam165#1 = #camandia@#0; |PINTA #dscam165#1;
      |PINTA 1233, #camandia@#1;
      |ACABA;
   |FINSI;

   #camandia@#0 = #dscam165#1;
   |LEE 000#camandia@.=;
   |SI FSalida = 0;
      |PINTA 1233, #camandia@#1;
   |SINO;
      |MENAV "    No existe el diario indicado";
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO MiraConcepto;
   nCampo = Campo + 1;
   |SI FSalida = 10;
      #dsmom030@#0 = #dscam165#Campo#;
      |LEE 000#dsmom030@.];
      |CONSULTA_CLAVE #1#dsmom030@;
      #dscam165#Campo#  = #dsmom030@#0; |PINTA #dscam165#Campo#;
      #dscam165#nCampo# = #dsmom030@#1; |PINTA #dscam165#nCampo#;
      |ACABA;
   |FINSI;

   #dsmom030@#0 = #dscam165#Campo#;
   |LEE 000#dsmom030@.=;
   |SI FSalida = 0;
      #dscam165#nCampo# = #dsmom030@#1; |PINTA #dscam165#nCampo#;
   |SINO;
      |MENAV "    No existe el concepto indicado";
      |ERROR;
   |FINSI;
|FPRC;

|PROGRAMA;
   |ABRE #dscam165;
   |LEE 000#dscam165.p;
   |SI FSalida ! 0;
      |DDEFECTO #dscam165;
      |GRABA 020#dscam165.n;
   |FINSI;

   |DFICB aAlfa1; |QBF aAlfa1; |PATH_DAT #dscam043#aAlfa1#;
   |ABRE #dscam043;
   |SELECT #2#dscam043;
   #dscam043#9 = #48#0;
   |LEE 000#dscam043.=;
   |SI FSalida ! 0;
      |MENAV "    No encuentro la empresa de contabilidad";
      |CIERRA #dscam043; |CIERRA #dscam165;
      |ACABA;
   |SINO;
      aEmpresa = (("00000" + #dscam043#0) % -105) + #dscam043#1;
   |FINSI;

   |DBASS aAlfa; |QBF aAlfa;
   aAlfa = aAlfa + "contagen/def/camandia.mas";
   |CARGA_DEF aAlfa, camandia@;
   |SI FSalida = 0;
      |DBASS aAlfa; |QBF aAlfa;
      aAlfa = aAlfa + "modelos/def/dsmom030.mas";
      |CARGA_DEF aAlfa, dsmom030@;
      |SI FSalida = 0;
         |DBASS aAlfa; |QBF aAlfa;
         aAlfa = aAlfa + "contagen/fich/" + aEmpresa + "/"; |PATH_DAT #camandia@#aAlfa#;
         |DBASS aAlfa; |QBF aAlfa;
         aAlfa = aAlfa + "modelos/fich/"; |PATH_DAT #dsmom030@#aAlfa#;
         |ABRE #camandia@;   |ABRE #dsmom030@;

         |PINPA #0#0; |PINDA #0#0;

         #camandia@#0 = #dscam165#1;
         |LEE 000#camandia@.=;
         |SI FSalida ! 0;
            |DDEFECTO #camandia@;
         |FINSI;
         |PINTA 1233, #camandia@#1;

         |ENDATOS #1#0;
         |SI FSalida = 0;
            |GRABA 020#dscam165.a;
         |FINSI;

         |CIERRA #camandia@; |CIERRA #dsmom030@;
         |DESCARGA_DEF #dsmom030@;
      |FINSI;
      |DESCARGA_DEF #camandia@;
   |SINO;
      |MENAV "    No encuentro el fichero de diarios de contabilidad";
   |FINSI;

   |CIERRA #dscam043; |CIERRA #dscam165;
|FPRO;
