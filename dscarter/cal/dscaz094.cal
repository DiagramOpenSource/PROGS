|FICHEROS;
 dscaz094 #0;
 dscam073 #1;
 dscam049 #2;  || Confirming Cabeceras
 dscam050 #3;  || Confirming Lineas
 dscam008 #4;  || Mantenimiento pagos
 dscam030 #5;  || Mantenimiento enlaces
 dscaw013 #6;  || Temporal enlaces
 dscam051 #7;  || Parametros generales
 dscam014 #8;  || Bancos..............
 dscam008 #9;  || Recibos compra ......
 caclipro@ #100;
|FIN;

|VARIABLES;
 &fecha = @;
 &fp = "";
 &HayConta = 0;
 &eaCadena = "";

 &eaCodigo  = "";
 &eaTipoCod = "";
 &aCuenta   = "";

 &eaNif       = "";
 &eaNombre    = "";
 &eaApCorreos = "";
 &eaDireccion = "";
 &eaPoblacion = "";
 &eaProvincia = "";
 &eaCP        = "";
 &eaTelefono  = "";
 &eaFax       = "";

 nCalc      = 0;
 nImporte   = 0;
 Prefijo    = "";
 aRef       = "";
 aDirec1    = "";
 aDirec2    = "";
 fFechaEmis = @;

 &aInforme = "";
 &eaNombre1 = "";
 &eaOrigen  = "";
 aDestino = "";
 Comodin  = "";
 nSwImpre = 0;

 &Niv1 = 0;
 &Niv2 = 0;
 &Niv3 = 0;
 &Niv4 = 0;
 &Niv5 = 0;
 &Niv6 = 0;
 &Divisa = "";

 aCuentaCt = ""; nNivel = 0;

 alfa1 = "";
 alfa2 = "";
 alfa3 = "";
 alfa4 = "";
 nume1 = 0;
 nume2 = 0;
 nume3 = 0;
 nume4 = 0;

 mens     = "";
 swErrorR = 0;
 swErrorP = 0;
 menR1    = "    No Existe Confirme !!!";
 menR2    = "    Confirme YA contabilizado ";
 menR3    = "2400SConfirme YA Emitido en Papel. Aceptar S/N ";
 menR4    = "2400NConfirme YA lanzado en Diskette. Aceptar S/N ";

 Nombre  = "";
 CodCli  = "";
 sino     = "";
 ceros    = "000000000000000";
 fechapre = "      ";
 dd       = "";
 mm       = "";
 aa       = "";
 cabe1    = "061";  || Registro Cabecera
 cabe2    = "062";  || Registro Detalle Pagos
 cabe3    = "063";  || Registro Total

 libre    = " ";
 libre40  = "";
 ca1  = "";
 ca2  = "";
 ca3  = "";
 ca4  = "";
 ca5  = "";
 ca6  = "";
 ca7  = "";
 ca8  = "";
 ca9  = "";
 ca10 = "";
 ca11 = "";
 ca12 = "";
 ca13 = "";
 ca14 = "";
 ca15 = "";
 ca115 = "";
 ca16 = "";
 ca17 = "";
 ca18 = "";
 ca19 = "";
 ca20 = "";
 ca21 = "";
 ca22 = "";
 ca23 = "";
 l          = 0;
 trecibos   = 0;
 timporte   = 0;

 NifProv    = "";
 NombreProv = "";
 DirecProv  = "";
 PoblaProv  = "";
 ProviProv  = "";
 Cp1Prov    = "";
 Cp2Prov    = "";
 TelefProv  = "";
 FaxProv    = "";
 PaisProv   = "";
 Entidad    = "";
 Sucursal   = "";
 DC         = "";
 Cuenta     = "";
 varios     = 0;

 handle     = "";
 fiche      = "";
 dirempr    = "";
 varalfa1   = "";
 varalfa2   = "";
 contenido  = "";
 swERROR    = 0;
 CampoDni   = "";
 Letra1     = "";
 Letra2     = "";
 Letra3     = "";
 Letra4     = "";
 Letra5     = "";
 Letra6     = "";
 Letra7     = "";
 Letra8     = "";
 Caracter   = "";
 Carac1     = 0;
 Carac2     = 0;
 Carac3     = 0;
 Carac4     = 0;
 Carac5     = 0;
 XPara      = 0;

 ccc        = "";
 c_alfa1    = "";
 c_alfa2    = "";
 c_nume1    = 0;
 c_nume2    = 0;
 c_nume3    = 0;

 direcc = "";
 numero = 0;
 longi  = 0;
 concepto = "";

 &programa = "";
 &ficcopia = "";
 aAlfa  = "";
 aAlfa1 = "";

     &eaVarDni  = "";
     &enCalcCif = 0;
|FIN;

|| *************************************************************************

|PROCESO ver_confirming; |TIPO 6;
 |SI FSalida = 2; |ACABA; |FINSI;
 |ABRE #dscam049;
 |HAZ leerem;
 |CIERRA #dscam049;
 |SI swErrorR ! 0;
     |SI swErrorR < 3;
         |MENAV mens;
         |ERROR;
         |ACABA;
     |SINO;
         |CONFI mens;
         |SI FSalida ! 0;
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;
 |FINSI;
 #dscaz094#2 = #dscam049#3; || Banco
 |HAZ TomaDatos;
 ||#dscaz094#5 = #dscam049#6; |PINTA #0#5; || Codigo cliente entidad
 #dscaz094#3 = #dscam049#1; |PINTA #0#3; || Fecha presentacion

 |PINTA 0935, #dscam049#2;   || Vto.
 |PINTA 0958, #dscam049#5;   || N§ Efectos
 |PINTA 1035, #dscam049#3;   || Banco
 |PINTA 1049, #dscam049#19;  || Importe Total
|FPRC;

|PROCESO TomaDatos;
   |ABRE #dscam073;
   #dscam073#0 = #dscaz094#2;
   |LEE 000#dscam073.=;
   |SI FSalida ! 0;
     |DDEFECTO #dscam073;
   |FINSI;
   |CIERRA #dscam073;

   #dscaz094#5 = #dscam073#23; |PINTA #dscaz094#5;
   #dscaz094#13 = #dscam073#24; |PINTA #dscaz094#13;
|FPRC;

|PROCESO leerem;
 swErrorR = 0;
 |DDEFECTO #dscam049;
 #dscam049#0 = #dscaz094#1;
 |LEE 001#dscam049.=;
 |SI FSalida ! 0;
     swErrorR = 1; mens = menR1;
 |SINO;
     Divisa = #dscam049#16; |HAZ TomaDecim;
     |SI #dscam049#8 = "S";
         swErrorR = 2; mens = menR2;
     |SINO;
         |SI #dscam049#7 = "S"; swErrorR = 3; mens = menR3; |FINSI;
         |SI #dscam049#7 = "D"; swErrorR = 4; mens = menR4; |FINSI;
     |FINSI;
     |ABRE #dscam014;
     #dscam014#0 = #dscam049#3;
     |LEE 000#dscam014.=;
     |CIERRA #dscam014;
 |FINSI;
|FPRC;

||...................................................................................
||***********************************************************
||************ REGISTRO DE CABECERA *************************
||***********************************************************
|PROCESO cabecera;
  ca1 = #dscaz094#3;                 || Fecha presentacion
  ca1 = (ca1 % 0704) + (ca1 % 0402) + (ca1 % 0102);
  ca2 = #dscam073#2;  |QBF ca2; ca2 = (ca2 + (" " * 10)) % 0110;
  ca3 = #dscaz094#13; |QBF ca3; ca3 = (ca3 + (" " * 7))  % 0107;
  ca4 = #dscam073#3;  |QBF ca4; ca4 = (ca4 + (" " * 36)) % 0136;
  ca5 = #dscam049#2;  |QBF ca5;
  ca5 = (ca5 % 0704) + (ca5 % 0402) + (ca5 % 0102);
  ca6 = #dscam073#4;  |QBF ca6; ca6 = ("0000" + ca6) % -104;

  varalfa1 = cabe1 + ca1 + ca2 + ca3 + ca4 + ca5 + ca6;
  eaCadena = varalfa1; |HAZ QuitaRaros; varalfa1 = eaCadena;
  contenido = handle + " " + varalfa1;
  |FGRABA contenido;
  varalfa1 = " " * 224;
  contenido = handle + " " + varalfa1 + &#0#7 + &#0#8;
  |FGRABA contenido;
  l = l + 1;
|FPRC;

||***********************************************************
||******************* REGISTRO INDIVIDUAL *******************
||***********************************************************

|PROCESO leecl_y_cobro;
 || .............. Lee la ficha del Cliente para obtener los datos necesarios

 NombreProv = "";
 DirecProv  = "";
 PoblaProv  = "";
 ProviProv  = "";
 Cp1Prov    = "";
 Cp2Prov    = "";
 TelefProv  = "";
 FaxProv    = "";
 PaisProv   = "";
 Entidad    = "";
 Sucursal   = "";
 DC         = "";
 Cuenta     = "";

 aRef = " " * 12;

 |DDEFECTO #dscam008;
 #dscam008#40 = #dscam050#13;
 |LEE 001#dscam008.=;

 fFechaEmis = #dscam008#4;

 #caclipro@#23 = "P";
 #caclipro@#10 = #dscam008#5;
 aCuentaCt = #dscam008#5; |HAZ SacaNivel1; #caclipro@#11 = nNivel;
 |LEE 000#caclipro@.=;
 |SI FSalida = 0;
/*
    NifProv    = #caclipro@#9;
    NombreProv = #caclipro@#1;
    DirecProv  = #caclipro@#2;
    PoblaProv  = #caclipro@#6;
    ProviProv  = #caclipro@#5;
    Cp1Prov    = #caclipro@#3;
    Cp2Prov    = #caclipro@#4;
    TelefProv  = #caclipro@#7;
    FaxProv    = #caclipro@#8;
*/
    eaCodigo = #dscam008#32; eaTipoCod = "P"; aCuenta = #dscam008#5; |HAZ LeeDatos;
    NifProv    = eaNif;
    NombreProv = eaNombre;
    DirecProv  = eaDireccion;
    PoblaProv  = eaPoblacion;
    ProviProv  = eaProvincia;
    Cp1Prov    = eaCP % 0102;
    Cp2Prov    = eaCP % 0303;
    TelefProv  = eaTelefono;
    FaxProv    = eaFax;

    PaisProv   = #caclipro@#24;
    |SI #dscaz094#4 = "1"; aRef = #caclipro@#10; |FINSI;
    |SI #dscaz094#4 = "2"; aRef = #caclipro@#0;  |FINSI;
    |QBF aRef; aRef = ((" " * 12) + aRef) % -112;

    aAlfa = "|NC"; nCalc = #caclipro@#aAlfa#;
    |SI nCalc > 29;
       Entidad  = #caclipro@#29;
       Sucursal = #caclipro@#30;
       DC       = #caclipro@#31;
       Cuenta   = #caclipro@#32;
    |SINO;
       aAlfa    = #caclipro@#18; |QBF aAlfa;
       Entidad  = aAlfa % 0104;
       Sucursal = aAlfa % 0504;
       DC       = aAlfa % 0902;
       Cuenta = #caclipro@#17; |QBF Cuenta;
       Cuenta = ("0000000000" + Cuenta) % -110;
    |FINSI;
 |SINO;
    aAlfa = "    No se encuentran los datos del proveedor " + #dscam008#6; |QBF aAlfa;
    aAlfa = aAlfa + " en la ficha de contabilidad";
    |MENAV aAlfa;
 |FINSI;

|FPRC;

|PROCESO grabarecib; ||Calculo registros individuales;
  swERROR = 0;

  |HAZ leecl_y_cobro;
  |SI swERROR = 1; |ACABA; |FINSI;   || Salta este Cobro. NO el resto

  Prefijo = Prefijo + #dscam073#2 + #dscaz094#13 + "  ";

  ca1  = NifProv;     |QBF ca1; ca1 = (ca1 + (" " * 10)) % 0110;
  ca2  = " " * 10;
  ca3  = NombreProv;  |QBF ca3; ca3 = (ca3 + (" " * 50)) % 0150;
  ca4  = " " * 50;
  ca5  = DirecProv;   |QBF ca5; ca5 = (ca5 + (" " * 30)) % 0130;
  ca6  = (("00" + Cp1Prov) % -102) + (("000" + Cp2Prov) % -103);
  ca7  = PoblaProv;   |QBF ca7; ca7 = (ca7 + (" " * 25)) % 0125;
  ca8  = TelefProv;   |QBF ca8; ca8 = ("000" + ca8) % -110
  ca9  = FaxProv;     |QBF ca9; ca9 = ("000" + ca9) % -110
  ca10 = "011000"
  ca11 = Entidad;
  ca12 = Sucursal;
  ca13 = DC;
  ca14 = Cuenta;
  ca15 = #dscam008#63; |QBF ca15;
  |SI ca15 = "";
     aAlfa = #dscam050#4; |QBF aAlfa; aAlfa = (aAlfa + (" " * 5)) % 0105;
     ca16 = aAlfa + "/";
     aAlfa = #dscam050#3; |QBF aAlfa; aAlfa = (("0" * 6) + aAlfa) % -106;
     ca15 = ca15 + aAlfa; |QBF ca15;
  |FINSI;
  ca15 = (ca15 + (" " * 10)) % 0110;
  ca16 = #dscam008#4; |QBF ca16;
  ca16 = (ca16 % 0704) + (ca16 % 0402) + (ca16 % 0102);
  nImporte = #dscam050#20;
  |SI nImporte < 0;
     ca21 = "-"; nImporte = -nImporte;
  |SINO;
    ca21 = " ";
  |FINSI;
  nImporte = (nImporte * 100) ! 0;
  aAlfa = nImporte; |QBF aAlfa; aAlfa = (("0" * 15) + aAlfa) % -115;
  ca17 = aAlfa;
  ca18 = #dscam008#3; |QBF ca18;
  ca18 = (ca18 % 0704) + (ca18 % 0402) + (ca18 % 0102);

  |SI #dscam050#16 = "C";
     ca19 = "1";
     ca20 = #dscam049#1; |QBF ca20;
     ca20 = (ca20 % 0704) + (ca20 % 0402) + (ca20 % 0102);
  |SINO;
     ca19 = "2";
     ca20 = "0" * 8;
  |FINSI;
  ca22 = " " * 10;
  ca23 = "EUR       ";

  varalfa1 = cabe2 + ca1 + ca2 + ca3 + ca4 + ca5 + ca6 + ca7 + ca8;
  eaCadena = varalfa1; |HAZ QuitaRaros; varalfa1 = eaCadena;
  varalfa1 = handle + " " + varalfa1;                 |FGRABA varalfa1;
  varalfa1 = varalfa1 + ca9 + ca10 + ca11 + ca12 + ca13 + ca14 + ca15 + ca16;
  eaCadena = varalfa1; |HAZ QuitaRaros; varalfa1 = eaCadena;
  varalfa1 = handle + " " + varalfa1;                 |FGRABA varalfa1;
  varalfa1 = varalfa1 + ca17 + ca18 + ca19 + ca20 + ca21 + ca22 + ca23;
  eaCadena = varalfa1; |HAZ QuitaRaros; varalfa1 = eaCadena;
  varalfa1 = handle + " " + varalfa1 + &#0#7 + &#0#8; |FGRABA varalfa1;
  l = l + 1;

  trecibos = trecibos + 1;
  timporte = timporte + #dscam050#20;
|FPRC;

|PROCESO regindiv;
  trecibos = 0;
  timporte = 0;
  |BUCLELIN 0grabarecib#2;
  |SI swERROR = 1;
      |MENAV "    ATENCION !!!. DATOS DE CLIENTES INCOMPLETOS. ";
  |FINSI;
|FPRC;

||***********************************************************
||********** REGISTRO TOTAL ORDENANTE Y GENERAL *************
||***********************************************************
|PROCESO campo_totales;
  l = l + 1;
  nCalc = (trecibos * 100) ! 0; ca1 = nCalc; |QBF ca1;
  ca1 = (("0" * 13) + ca1) % -113;
  nCalc = timporte * 100; ca2 = nCalc; |QBF ca2;
  ca2 = (("0" * 15) + ca2) % -115;
  ca3 = 150;
  ca4 = 119;

  varalfa1 = cabe3 + ca1 + ca2;
  eaCadena = varalfa1; |HAZ QuitaRaros; varalfa1 = eaCadena;
  varalfa1 = handle + " " + varalfa1; |FGRABA varalfa1;
  varalfa1 = ca3;
  eaCadena = varalfa1; |HAZ QuitaRaros; varalfa1 = eaCadena;
  varalfa1 = handle + " " + varalfa1; |FGRABA varalfa1;
  varalfa1 = ca4 + &#0#7 + &#0#8;
  eaCadena = varalfa1; |HAZ QuitaRaros; varalfa1 = eaCadena;
  varalfa1 = handle + " " + varalfa1; |FGRABA varalfa1;
|FPRC;

|| **************************************************************************

|PROCESO pasecuencial; ||TIPO 3;
  |ABRE #dscam073;
  #dscam073#0 = #dscaz094#2;
  |LEE 000#dscam073.=;
  |SI FSalida ! 0;
     |DDEFECTO #dscam073;
  |FINSI;
  |CIERRA #dscam073;

  |ABRE #dscam049;
  |HAZ leerem;
  |SI swErrorR = 1; |O swErrorR = 2;
      |MENAV "    ERROR !!! CONFIRME NO CORRECTO ";
      |ACABA;
  |FINSI;

  |HAZ varia_fich;

  |CAMPO_VISUAL #dscaz094#10,1,"S"; #dscaz094#10 = 0;
  |CAMPO_VISUAL #dscaz094#11,1,"S"; #dscaz094#11 = 0;
  |CAMPO_VISUAL #dscaz094#12,1,"S"; #dscaz094#12 = 0;

|| ...................................................
  aAlfa = #48#16; |QBF aAlfa;
  aAlfa1 = aAlfa + "def/caclipro.mas";
  |CARGA_DEF aAlfa1, caclipro@;
  |SI FSalida ! 0;
     |MENAV "    Problemas para abrir el fichero de clientes/proveedores de contabilidad";
     |ERROR; |ACABA;
  |FINSI;
  aAlfa1 = aAlfa + "fich/" + (("00000" + #48#14) % -105) + #48#19 + "/";
  |PATH_DAT #100 aAlfa1; |ABRE #caclipro@;

  |ABRE #dscam008;         || Abre Mantenimiento de efectos

|| Abrir Fichero Secuencial

  |DFICE dirempr;
  fiche = "wb  " + dirempr + "casecmin.seq";  || "nomfiche.ext";
  |QBF fiche;
  |FABRE fiche;
  handle = FSalida;
  |SI FSalida ] 0;
      |HAZ cabecera;
      |HAZ regindiv;
      ||   |SI swERROR = 0;
               |HAZ campo_totales;
      ||   |FINSI;
  |FINSI;
  contenido = handle + " " + &#0#9;
  |FGRABA contenido;

  FSalida = handle;
  |FCIERRA FSalida;

  Pos = -1; |CIERRA #dscam008;
  |CIERRA #caclipro@; |DESCARGA_DEF #caclipro@;

|| ...   |SI swERROR = 1; |ACABA; |FINSI;

  |SI HayConta = 1; |Y #dscam049#7 = "N"; |Y #dscaz094#14 ! "    ";
      |ABRE #dscam030;
      #dscam030#0 = #dscaz094#14;
      |LEE 010#dscam030.=;
      |SI FSalida = 0;
         Comodin = Usuario; |QBF Comodin; Comodin = "ac" + Comodin;
         |NOME_DAT #6 Comodin;
         |DELINDEX #dscaw013; |ABRE #dscaw013;
         |DDEFECTO #dscaw013;
         #dscaw013#0  = 1;            #dscaw013#15 = #dscaz094#14;
         #dscaw013#5  = #dscam049#0;  #dscaw013#6  = #dscam049#0;
         #dscaw013#11 = "N";          #dscaw013#12 = 5;
         #dscaw013#13 = "dscam050";   #dscaw013#14 = 0;
         |GRABA 020#dscaw013.n;
         |CIERRA #dscaw013;
         Comodin = #dscaz094#14;
         Comodin = "dscam033.tab;" + Comodin + ",E";
         |LIBERA #dscam049; |CIERRAT #dscam049;
         |LIBERA #dscam050; |CIERRAT #dscam050;
         |SUB_EJECUTA_NP Comodin;
         |ABRET #dscam049; |LEE 101#dscam049.=;
         |ABRET #dscam050; |LEE 101#dscam050.=;
      |FINSI;
      |CIERRA #dscam030;
  |FINSI;

  |SI trecibos ! 0;
      #dscam049#7 = "D";
      |GRABA 020#dscam049.a; |LIBERA #dscam049;
  |FINSI;
  |CIERRA #dscam049;

  |D_CUADRO 1261, 2280;
  |ATRI R;
  |PINTA 1362, " Numero Recibos   ";
  |PINTA 1562, " Numero Registros ";
  |PINTA 1762, " Importe Total    ";
  |ATRI 0;
  |PINTA 1462, "                  ";
  |PINTA 1662, "                  ";
  |PINTA 1862, "                  ";
  |ATRI 0;

  #dscaz094#10 = trecibos;  |PINTA #dscaz094#10;
  #dscaz094#11 = l;         |PINTA #dscaz094#11;
  #dscaz094#12 = timporte;  |PINTA #dscaz094#12;

  |DFICE eaOrigen; |QBF eaOrigen;
  eaNombre1 = "casecmin.seq";
  |SUB_EJECUTA "dscaz032.tab";
|FPRC;


|| ********** Rutinas Varias

|PROCESO LetraDni;
     eaVarDni   = CampoDni;
     enCalcCif  = 3;
     |HAZ CalculoDNI;
     CampoDni = eaVarDni;
     CampoDni = ("000000000" + CampoDni) % -109;
|FPRC;

|PROCESO cccBanco;
 |QBT ccc;
 c_alfa1 = ccc % 0;
 c_nume1 = c_alfa1;
 c_alfa1 = "";
 c_alfa2 = "";
 |PARA c_nume2 = 1; |SI c_nume2 [ c_nume1; |HACIENDO c_nume2 = c_nume2 + 1;
       c_nume3 = c_nume2 * 100 + 1;
       c_alfa2 = ccc % c_nume3;
       |SI c_alfa2 ] "0"; |Y c_alfa2 [ "9";
           c_alfa1 = c_alfa1 + c_alfa2;
       |FINSI;
 |SIGUE;
 ccc = c_alfa1;
|FPRC;

|PROCESO varia_fich;
|| .......... Variables del Presentador, del Ordenante y comunes

  CodCli = #dscaz094#5; |QBF CodCli;
  CodCli = CodCli + "  "; CodCli = CodCli % 102;

  libre40 = libre * 40;
  || Nombre = #48#1 + libre40;     Nombre = Nombre % 140;
  Nombre = #dscam073#3 + libre40;     Nombre = Nombre % 140;

  alfa1 = #dscaz094#3;
  dd = alfa1 % A102;
  mm = alfa1 % A402;
  aa = alfa1 % A902;
  fechapre = aa + mm + dd;   || Fecha Formato a¤o + mes + dia
|FPRC;

|PROCESO separa;
  alfa3  = 0;
  numero = 0;
  |QBF direcc;
  alfa1 = direcc % 0;
  longi = alfa1;
  |PARA nume4 = longi; |SI nume4 > 1; |HACIENDO nume4 = nume4 - 1;
        nume2 = (nume4 * 100) + 1;
        alfa2 = direcc % nume2;
        |SI alfa2 = ",";
            nume3  = ((longi - nume4) + 100) * -1;
            alfa3  = direcc % nume3;
            nume2  = (nume4 - 1) + 100;
            direcc = direcc % nume2;
            |SAL;
        |FINSI;
  |SIGUE;
  |QBT alfa3;
  |SI alfa3 ! ""; numero = alfa3; |FINSI;
|FPRC;

|PROCESO SacaNivel1;
   |QBF aCuentaCt;
   aAlfa = aCuentaCt % 0; nNivel = aAlfa;
   |SI nNivel = Niv1; nNivel = 1; |FINSI;
   |SI nNivel = Niv2; nNivel = 2; |FINSI;
   |SI nNivel = Niv3; nNivel = 3; |FINSI;
   |SI nNivel = Niv4; nNivel = 4; |FINSI;
   |SI nNivel = Niv5; nNivel = 5; |FINSI;
   |SI nNivel = Niv6; nNivel = 6; |FINSI;
|FPRC;

|PROCESO MiraParam; |TIPO 7;
   |SI #dscam051#1 = "S";
      Comodin = #dscam051#61;
      |C_M #dscaz094#14,1,Comodin;
   |SINO;
      |C_M #dscaz094#14,1,"N";
   |FINSI;
|FPRC;

|PROGRAMA;
   |HAZ Tipo8;

   |ABRE #dscam051;
   |LEE 000#dscam051.p;
   |SI FSalida ! 0;
      |DDEFECTO #dscam051;
      |GRABA 020#dscam051.c;
   |FINSI;
   |CIERRA #dscam051;
   |SI #dscam051#1 = "S";
      #dscaz094#14 = #dscam051#29;
   |SINO;
      #dscaz094#14 = "    ";
   |FINSI;

   |CLS;
   |CABEZA " Emision diskette confime Bancaja ";
   |PINPA #0#0; |PINDA #0#0;
   |ENDATOS #1#0;
   |SI FSalida = 0;
      |CONFI "    NImprimir carta confirme ?";
      |SI FSalida = 0; nSwImpre = 1; |SINO; nSwImpre = 0; |FINSI;
      |SI nSwImpre = 1;
         |IMPRE 0; |SI FSalida < 0; |ACABA; |FINSI;
         aInforme = "Carta emision de confirmes"; |HAZ ObtenInforme;
         |INFOR aInforme; fecha = #dscaz094#3;
         |SI FSalida < 0; |FINIMP; |ACABA; |FINSI;
      |FINSI;
      |HAZ pasecuencial;
      |SI nSwImpre = 1; |PIEINF; |FININF; |FINIMP; |FINSI;
   |FINSI;

   |HAZ Tipo9;
|FPRO;
