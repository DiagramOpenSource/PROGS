|FICHEROS;
     rem00051 #20;
     rew99998 #900,MANTE;
     espejo@ #99;
|FIN;

|VARIABLES;
     aAlfa         = "";
     aAlfa1        = "";
     aLong         = "";
     aQueQuiero    = "";
     aNumCampos    = "";
     aNumPanta     = "";
     aPathRenta    = "";
     aDef          = "";
     aDescri       = "";
     aAlfa2        = "";
     aTecla        = "";

     nLong         = 0;
     nCaracter     = 0;
     nPosi         = 0;
     nPanta        = 0;
     nNumCampos    = 0;
     nNumPanta     = 0;
     nCampo        = 0;
     nCampo1       = 0;
     nCampo2       = 0;
     nCampo3       = 0;
     nCampo4       = 0;
     nCampo5       = 0;
     nModo         = 0;
     nEntrada      = 0;
     nPtas         = 0;
     nRetencion    = 0;
     nCampo15      = 0;
     nCampo16      = 0;
     nCampo17      = 0;
     nCampo20      = 0;
     nCampo27      = 0;
     nValor        = 0;
     nDias         = 0;
     nAnyoVenta    = 0;
     nAnyoCompra   = 0;
     nDiferencia   = 0;
     nCoefi        = 0;
     nLinea        = 0;
     nImputable    = 0;
     nContenido    = 0;

     fFechaAdq     = @;
     fFechaTra     = @;
     fFecha1       = @;
     fFecha2       = @;

     &enEmpresa    = 0;
     &enModo       = 0;
     &eaDeclarante = "";
     &enComunes    = 0;
     &enMinusvalia = 0;
     &eaMinuAyuda  = "";
     &eaFallece    = "";
     &eaSwVarios   = "";
     &enSwLiquida  = 0;
     &enSwRetorno  = 0;
     &eaPrograma   = "";
     &eaPunto      = "";

     &eaLetra      = "";
     &eaDeDonde    = "";
     &eaLinDonde   = "";
     &eaAnyoUsu    = "";
     &eaEdadDec    = "";
     &eaPatr06     = "";
     &eaPatr07     = "";
     &eaPatr08     = "";
     &eaPatr09     = "";
     &eaPatr10     = "";
     &eaPatr11     = "";
     &eaPatr12     = "";
     &eaPatr13     = "";
     &eaPatr14     = "";
     &eaPatr15     = "";
     &eaPatr16     = "";
     &eaPatr16_1   = "";
     &eaPatr17     = "";
     &eaPatr18     = "";
     &eaPatr19     = "";
     &eaPatr20     = "";
     &eaPatr21     = "";
|FIN;

|| ************************************************************************
|| PROCESOS DE CONSULTA DE PANTALLAS NORMALES
|| ************************************************************************

|PROCESO Calculadora;
     |SI FSalida ! 13;  |ACABA;  |FINSI;

     |PUSHV 0501 2380;
     |CUADRO 1030 1672;
     |BLANCO 1131 1571;

     |PINTA 1131, "Importe 1 .............:                ";
     |PINTA 1231, "Importe 2 .............:                ";
     |PINTA 1331, "Importe 3 .............:                ";
     |PINTA 1431, "Importe 4 .............:                ";
     |PINTA 1531, "Importe 5 .............:                ";

     E_inf = -999999999.99;
     E_sup = 9999999999.99;

     nCampo1 = #20Campo;
     nCampo2 = 0;
     nCampo3 = 0;
     nCampo4 = 0;
     nCampo5 = 0;

     |ET nCampo1;
         nCampo1 = 1156 ? 1;
         |SI FSalida = 1;  |VETE Abajo;    |FINSI;
         |SI FSalida = 7;  |VETE Abajo;    |FINSI;

     |ET nCampo2;
         nCampo2 = 1256 ? 1;
         |SI FSalida = 2;  |VETE nCampo1;  |FINSI;
         |SI FSalida = 1;  |VETE Abajo;    |FINSI;
         |SI FSalida = 7;  |VETE Abajo;    |FINSI;

     |ET nCampo3;
         nCampo3 = 1356 ? 1;
         |SI FSalida = 2;  |VETE nCampo2;  |FINSI;
         |SI FSalida = 1;  |VETE Abajo;    |FINSI;
         |SI FSalida = 7;  |VETE Abajo;    |FINSI;

     |ET nCampo4;
         nCampo4 = 1456 ? 1;
         |SI FSalida = 2;  |VETE nCampo3;  |FINSI;
         |SI FSalida = 1;  |VETE Abajo;    |FINSI;
         |SI FSalida = 7;  |VETE Abajo;    |FINSI;

     |ET nCampo5;
         nCampo5 = 1556 ? 1;
         |SI FSalida = 2;  |VETE nCampo4;  |FINSI;
         |SI FSalida = 1;  |VETE Abajo;    |FINSI;
         |SI FSalida = 7;  |VETE Abajo;    |FINSI;

     |ET Abajo;
         #20Campo = nCampo1 + nCampo2 + nCampo3 + nCampo4 + nCampo5;

     |POPV;

     |PINTA #20Campo;
|FPRC;

|PROCESO PrimeraVez1;  |TIPO 7;
     |SI #20Campo = 0;
         #20Campo = #20#15;
         |PINTA #20Campo;
     |FINSI;
|FPRC;

|PROCESO PrimeraVez2;  |TIPO 7;
     |SI #20Campo = 0;
         #20Campo = #20#21;
         |PINTA #20Campo;
     |FINSI;
|FPRC;

|PROCESO nContenido;
     nContenido = Contenido;
     |SI #20Campo ! nContenido;
         ||#20#29 = (#20#23 + #20#24 - #20#26) % #20#28;
         #20#29 = #20#26 % #20#28;
         |PINTA #20#29;
     |FINSI;
|FPRC;

|PROCESO Retencion;
     nContenido = Contenido;
     |SI #20Campo = nContenido;  |Y #20#29 ! 0;  |ACABA;  |FINSI;

     ||#20#29 = (#20#23 + #20#24 - #20#26) % #20#28;
     #20#29 = #20#26 % #20#28;
     |PINTA #20#29;
|FPRC;

|PROCESO Totalm020;
     #20#23 = #20#21 - #20#22;

     |SI #20#25 ! -1;
         |C_M #20#26, 1, "N";

         #20#25 = 0;
         |SI #20#19 = "V";
             |SI #20#20 [ 39;                   #20#25 = 40;  |FINSI;
             |SI #20#20 ] 40;  |Y #20#20 [ 49;  #20#25 = 35;  |FINSI;
             |SI #20#20 ] 50;  |Y #20#20 [ 59;  #20#25 = 28;  |FINSI;
             |SI #20#20 ] 60;  |Y #20#20 [ 65;  #20#25 = 24;  |FINSI;
             |SI #20#20 ] 66;  |Y #20#20 [ 69;  #20#25 = 20;  |FINSI;
             |SI #20#20 ] 70;                   #20#25 = 8;   |FINSI;
         |SINO;
             |SI #20#20 [ 5;                    #20#25 = 12;  |FINSI;
             |SI #20#20 ] 6;   |Y #20#20 [ 10;  #20#25 = 16;  |FINSI;
             |SI #20#20 ] 11;  |Y #20#20 [ 15;  #20#25 = 20;  |FINSI;
             |SI #20#20 ] 16;                   #20#25 = 25;  |FINSI;
         |FINSI;

         #20#26 = #20#24 % #20#25;
     |SINO;
         |C_M #20#26, 1, "S";
     |FINSI;

     |PINTA #20#23;
     |PINTA #20#25;
     |PINTA #20#26;
|FPRC;

|PROCESO Usufructuario;
     |C_M #20#14, 1, "S";
     |SI #20#13 ! "N";
         |C_M #20#14, 1, "N";  #20#14 = 0;  |PINTA #20#14;
     |FINSI;
|FPRC;

|PROCESO Varios;  |TIPO 0;
     |SI #20#2 ! "VARIOS    ";
         #20#5  = 0;
         #20#6  = 0;
         #20#7  = 0;
         #20#8  = 0;
         #20#9  = 0;
         #20#10 = 0;
         |ACABA;
     |FINSI;

     |C_M #900#0,  1, "N";
     |C_M #900#1,  1, "N";
     |C_M #900#2,  1, "N";
     |C_M #900#3,  1, "N";
     |C_M #900#4,  1, "N";
     |C_M #900#5,  1, "N";

     aAlfa = eaSwVarios;
     aLong = aAlfa % 0;
     nLong = aLong;
     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPosi = (100 * nCaracter) + 1;
           aAlfa1 = aAlfa % nPosi;
           |SI aAlfa1 = "T";  |C_M #900#0,  1, "S";  |FINSI;
           |SI aAlfa1 = "C";  |C_M #900#1,  1, "S";  |FINSI;
           |SI aAlfa1 = "1";  |C_M #900#2,  1, "S";  |FINSI;
           |SI aAlfa1 = "2";  |C_M #900#3,  1, "S";  |FINSI;
           |SI aAlfa1 = "3";  |C_M #900#4,  1, "S";  |FINSI;
           |SI aAlfa1 = "4";  |C_M #900#5,  1, "S";  |FINSI;
     |SIGUE;

     #900#0 = #20#5;
     #900#1 = #20#6;
     #900#2 = #20#7;
     #900#3 = #20#8;
     #900#4 = #20#9;
     #900#5 = #20#10;

     |PUSHV 0501 2380;

     |PINPA #0#900;
     |PINDA #0#900;
     |ENDATOS #1#900;
     |SI FSalida = 0;
         #20#5  = #900#0;
         #20#6  = #900#1;
         #20#7  = #900#2;
         #20#8  = #900#3;
         #20#9  = #900#4;
         #20#10 = #900#5;
     |FINSI;

     |POPV;
|FPRC;

|RUTINA ClaveBaja20;
     #20#0 = enEmpresa;
     #20#1 = 1;
|FRUT;

|RUTINA ClaveAlta20;
     #20#0 = enEmpresa;
     #20#1 = 99;
|FRUT;

|PROCESO PonDeclarante;  |TIPO 7;
     eaDeclarante = #20#2;
     enComunes    = 1;
     |HAZ SacaDeclarantes;
     #20#2 = eaDeclarante;
     |PINTA #20#2;
|FPRC;

|PROCESO Fallece;  |TIPO 7;
     |SI eaFallece = "S";
         |C_M #20#4, 1, "S";
     |SINO;
         |C_M #20#4, 1, "N";  #20#4 = 0;  |PINTA #20#4;
     |FINSI;
|FPRC;

|PROCESO TipoX;
     |SI #20Campo = "X";
         |PTEC 806;
     |FINSI;
|FPRC;

|PROCESO Patrimonio;
     |SI #20#3  = "R";           |ACABA;  |FINSI;
     |SI #20#3  = "X";           |ACABA;  |FINSI;
     |SI #20#2  = "1 FAMILIAR";  |ACABA;  |FINSI;
     |SI #20#2  = "2 FAMILIAR";  |ACABA;  |FINSI;
     |SI #20#2  = "3 FAMILIAR";  |ACABA;  |FINSI;
     |SI #20#2  = "4 FAMILIAR";  |ACABA;  |FINSI;
     |SI #20#15 = 0;             |ACABA;  |FINSI;

     |HAZ LimpiaPatrim;

     eaLetra    = #20#11;
     eaDeDonde  = "C3";
     eaLinDonde = #20#1;
     eaAnyoUsu  = #20#14;
     eaPatr06   = #20#17;
     eaPatr16   = #20#13;
     eaPatr16_1 = #20#19;
     eaPatr10   = #20#15;
     eaPatr14   = #20#24;
     eaPatr20   = #20#5;
     eaPatr21   = #20#6;

     |HAZ GrabaPatrimonio;
|FPRC;

|PROCESO Tipo11m20;  |TIPO 11;
     |SI FSalida = 10;
         |SUB_EJECUTA_NP "rew00551";
         |ERROR;
     |FINSI;

     |SI FSalida = 11;
         |SI #20#2 ! "VARIOS    ";  |ACABA;  |FINSI;

         #900#0 = #20#5;
         #900#1 = #20#6;
         #900#2 = #20#7;
         #900#3 = #20#8;
         #900#4 = #20#9;
         #900#5 = #20#10;

         |PUSHV 0501 2380;
         |PINPA #0#900;
         |PINDA #0#900;
         |LEETECLA aTecla;
         |POPV;
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo2m20;  |TIPO 2;
     nModo = (FEntrada / 100) ! 0;

     nEntrada = 0 +. 1;
     |SI nEntrada ! 1;
         eaDeDonde  = "C3";
         eaLinDonde = #20#1;
         eaLetra    = #20#11;
         |HAZ BorraPatrimonio;
     |FINSI;

     |SI nModo = 3;  enSwRetorno  = 1;  |ACABA;  |FINSI;
     |SI nModo = 1;  enSwRetorno  = 1;           |FINSI;

     |SI nEntrada ! 1;
         |PARA nCampo = 0;  |SI nCampo [ nNumCampos;  |HACIENDO nCampo = nCampo + 1;
               #99nCampo = #20nCampo;
         |SIGUE;
     |SINO;
         |PARA nCampo = 0;  |SI nCampo [ nNumCampos;  |HACIENDO nCampo = nCampo + 1;
               |SI #99nCampo ! #20nCampo;
                   enSwRetorno  = 1;
               |FINSI;
         |SIGUE;

         |HAZ Patrimonio;
     |FINSI;
|FPRC;

|PROGRAMA;
     aQueQuiero = "|NP";
     aNumPanta  = #20#aQueQuiero#;
     nNumPanta  = aNumPanta;
     nNumPanta  = nNumPanta - 1;

     aQueQuiero = "|NC";
     aNumCampos = #20#aQueQuiero#;
     nNumCampos = aNumCampos;
     nNumCampos = nNumCampos - 1;

     |DBASE aPathRenta;  |QBT aPathRenta;
     aDef = aPathRenta + "def/rem00051.mas";
     |CARGA_DEF aDef, espejo@;
     |SI FSalida ! 0;
         aAlfa = "     Error en cargar el Def rem00051.mas";
         |MENAV aAlfa;
         |ERROR;
     |FINSI;

     |ENTLINEAL #1#20, -2, enModo, 22, 1, ClaveBaja20, ClaveAlta20;

     |DESCARGA_DEF #espejo@;

     eaPrograma   = "rem00051";
     eaPunto      = "  ";
     |HAZ ProgramaActivo;
|FPRO;
