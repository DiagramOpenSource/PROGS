|FICHEROS;
     vipez008 #0;
     dsw00078 #1; ||Tmp DBF
     agifa019 #19;
     vipem023 #23;
     agifa024 #24;
     vipem024 #25;
     agifa142 #142;
     agifa501 #501;
     agifa502 #502;
     agifa515 #515;
     agifa516 #516;
     tmp2t600 #600; ||Tmp para recalcular cajas
|FIN;

|VARIABLES;
     &EURODB = 0;
     nNume = 0;
     nError = 0;
     aOrg = "";
     aDes = "";
     &enForma = 0;
     &Tempo = "";
     &eCaja    = 0;
     &eTienda  = 0;
     &eFecha   = "";
{100}Valor = "";
     aTmp1 = "";
     aTmp600 = "";
     nLin = 0;
     nIva = 0;
     nContador = 0;
     aFic = "";
     aLon = "";
     aPath = "";
     aAlfa = "";
     aDato = "";
     aHora = "";
     aUsu = "";
     aIP = "";
     aRuta = "";
     nHandle = 0;
     nPos = 0;
     i = 0;
     fFecha = @;
     aTipFic = "";
     nVersion = 0;
     aReg = "";
     nOK = 0;
     fUltimo = @;
     nNumCam = 0;
     nNumReg = 0;
     nLonCab = 0;
     nLonReg = 0;
     nCampo = 0;

     regis = 0;
     sw_seleccio = 0;
     campoalf = "";
     any = 0;
     mes = 0;
     dia = 0;
     campo = 0;
     alfa1 = "";
     alfa2 = "";
     alfa3 = "";
     alfa4 = "";
     alfa5 = "";
     nume1 = 0;
     field = "";
     bytes = 0;
     para1 = 0;
     nhexa = "";
     ndeci = 0;
     nalfa1 = "";
     nalfa2 = "";
     nnume1 = 0;
     nnume2 = 0;
     npara1 = 0;
     npara2 = 0;
     npara3 = 0;
     nlongi = 0;

     vuelt0 = "";
     vuelt1 = "";
     vuelt2 = "";
     longa = "";
     longn = 0;
     vuelta = 0;
     coger = 0;
     flg_bor = 0;

     beta = 0;
     nDto = 0;
     fmes = "";
     imneto = 0;
     nImpo = 0;
     &enImpCliCom = 0;
     &enImpCliDto = 0;
     &enImpCliMar = 0;
     &enImpCliFac = 0;
     &eaCliente = "";
     &efFecha = @;
|FIN;

|PROCESO actua_tarj;
     |SI #501#40 ! 0;
          |ABRE #515;
          |DDEFECTO #515;
          #515#0 = #501#46;
          |LEE 101#515=;
          |SI FSalida ! 0; |GRABA 021#515b; |FINSI;
          #515#2 = #515#2 + (#501#40);    ||Total Ptas cargadas
          #515#3 = #501#14;                  ||Fecha Ult. Oper.
          #515#4 = #501#40;                  ||Ptas Cargadas
          |GRABA 021#515a;
          |LIBERA #515;
          |CIERRA #515;
          |ABRE #516;
          |DDEFECTO #516;
          #516#0 = #501#46;
          #516#1 = #501#14;
          |LEE 101#516=;
          |SI FSalida ! 0;    |GRABA 021#516b; |FINSI;
          #516#2 = #516#2 + (#501#40);    ||Total Ptas cargadas
          |GRABA 021#516a;
          |LIBERA #516;
          |CIERRA #516;
     |FINSI;
|FPRC;

|PROCESO ActCab;
     |SI #501#9 > 0; |O #501#31 ! "V";
          |SI #501#3 > 0; beta = 1; |SINO; beta = -1; |FINSI;
          #501#3 = #501#3 * beta;
          nDto = (((#501#3 < #501#17) < #501#19) < #501#18) * beta;
          #501#3 = #501#3 * beta;
          nDto = #501#3 - nDto;   || Total Dto Cabecera
          ||-------------------- Cliente
          |SI #501#31 ! "V";
               |ABRE #24;
               |DDEFECTO #24;
               #24#0 = #501#1;
               |LEE 101#24=;
               |SI FSalida ! 0; |GRABA 020#24b; |FINSI;
               fmes = #501#14 % A402;
               mes = ((fmes - 1) * 4) + 54;
               imneto = #501#3 - nDto;
               |SI #501#35 = "A";
                    #24#50 = #24#50 +. imneto;
               |SINO;
                    |SI #142#115 = "N";
                         nImpo = (imneto * 100) / (100 - #501#18);
                         nDto = nDto + (imneto - nImpo);
                    |SINO;
                         nImpo = imneto;
                    |FINSI;
                    #24mes = #24mes +. nImpo;
                    mes = mes + 1;
                    #24mes = #24mes +. nDto;
                    mes = mes + 1;
                    #24mes = #24mes +. #501#8;
                    #24#102 = #24#102 +. nImpo;
                    #24#103 = #24#103 +. nDto;
                    #24#104 = #24#104 +. #501#8;
                    beta = (FEntrada / 100) ! 0;
                    |SI beta = 1;
                         #24#45 = #501#14;
                    |FINSI;
                    #24#46 = imneto;
                    #24#110 = #24#110 +. imneto;

                    enImpCliCom = nImpo;
                    enImpCliDto = nDto;
                    enImpCliMar = #501#8;
                    enImpCliFac = imneto;
                    eaCliente = #501#1;
                    efFecha = #501#14;
                    |HAZ AcumulaCli;

               |FINSI;
               |GRABA 020#24a;
               |CIERRA #24;
          |FINSI;
          ||-------------------
          |SI #501#31 ! "V";
               |HAZ actua_tarj;           ||Actualiza la tarjeta
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO GraPlatos;
     |DDEFECTO #19;
     #19#0 = Valor2;
     #19#1 = Valor3;
   ||  |QBT Valor1;
   ||  #19#2 = Valor1;
     #19#18 = Valor5;
     #19#19 = Valor6;
     #19#30 = 2;
     #19#20 = Valor7;
     |SI Valor4 = "T";   || Verdadero = T, Falso = F
          #19#125 = 1;
     |SINO;
          #19#125 = 2;
     |FINSI;
     #19#137 = "N";
     |LEE 000#19=;
     |SI FSalida ! 0; |GRABA 020#19n; |FINSI;
|FPRC;

|PROCESO GraHcmesa;
     |DDEFECTO #23;
     #23#0 = #0#2;
     #23#1 = Valor1;
     |LEE 000#23=;
     |SI FSalida = 0; |ACABA; |FINSI;
     |GRABA 020#23n;

     |DDEFECTO #501;
     #501#0 = Valor1;
     #501#1 = Valor12;
     #501#3 = Valor13;
     #501#4 = Valor14;
     #501#9 = Valor15;
     #501#10 = Valor15;
     #501#12 = 1;
     #501#14 = Valor3;
     #501#23 = Valor13;
     #501#24 = Valor14;
     #501#31 = "E";
     #501#32 = 1;
     #501#33 = Valor8;
     #501#34 = Valor4;
     #501#35 = "T";
     #501#36 = #0#2;
     #501#38 = 0;
     #501#55 = Valor2;
     #501#56 = Valor7;
     #501#58 = Valor9;
     |LEE 000#501=;
     |SI FSalida ! 0;
          |GRABA 040#501n;
     |FINSI;
|FPRC;

|PROCESO GraHpagos;
     #23#0 = #0#2;
     #23#1 = Valor2;
     |LEE 101#23=
     |SI FSalida ! 0; |ACABA; |FINSI;
     |SI #23#2 = "S"; |LIBERA #23; |ACABA; |FINSI;
||     #23#2 = "S";
||     |GRABA 020#23a;
||     |LIBERA #23;

     #501#36 = #0#2;
     #501#0 = Valor2;
     |LEE 101#501=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     #501#10 = Valor10;
||     #501#11 = #501#10 - #501#9;

     |SI Valor6 = 1;
          ||Paga Metalico
          nNume = #501#39 + #501#40;
          |SI nNume = 0;
               #501#31 = "E";
          |FINSI;
          #501#38 = #501#38 + Valor9;
     |FINSI;
||     nNume = #501#38 + #501#39 + #501#40;
||     |SI nNume = #501#9;
||          #23#2 = "S";
||     |FINSI;

     |SI Valor6 = 2; |O Valor6 = 3; |O Valor6 = 4; |O Valor6 = 6;
          ||Paga con tarjeta
          #501#31 = "T";
          #501#40 = #501#40  + Valor9;
          #501#46 = Valor6;
     |FINSI;
     |SI Valor6 = 7;
          ||Paga con Cheque
          #501#31 = "C";
          #501#39 = #501#39 + Valor9;
     |FINSI;
     |SI Valor6 = 5;
          || Invitacion.
          #501#28 = #501#9;
          #501#4 = 0;
          #501#9 = 0;
          #501#10 = 0;
          #501#17 = 100;
          #501#23 = 0;
          #501#24 = 0;
          #501#31 = "I";
          #501#38 = 0;
          #501#64 = #501#28;
     |FINSI;

     nNume = (#501#38 + #501#39 + #501#40) ! EURODB;
     |SI nNume = #501#9;
          #23#2 = "S";
     |FINSI;
     |SI nNume = #501#9;
          #23#2 = "S";
     |FINSI;

     |GRABA 020#23a;
     |LIBERA #23;

     |HAZ ActCab;
     |GRABA 020#501a;
     |LIBERA #501;

     aAlfa = #501#14;
     aAlfa = aAlfa + #501#12;
     #600#0 = aAlfa;
     |GRABA 040#600n;
|FPRC;

|PROCESO GraHlmesa;
     #25#0 = Valor1;     || Nume
     #25#1 = Valor2;     || Plato
     #25#2 = Valor3;     || Grupo
     #25#3 = Valor4;     || Unidades
     #25#4 = Valor5;     || Mesa
     #25#5 = Valor10;    || Cocina
     #25#6 = Valor16;    || Barra
     #25#7 = Valor14;    || Precio
     |LEE 000#25=;
     |SI FSalida = 0; |ACABA; |FINSI;   || Que no se repitan
     |GRABA 020#25n;

     #23#0 = #0#2;
     #23#1 = Valor1;
     |LEE 101#23=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     #23#3 = "S";
     |GRABA 020#23a;
     |LIBERA #23;

     #501#36 = #0#2;
     #501#0 = Valor1;
     |LEE 000#501=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     ||...Busca  ultima linea
     #502#20 = #501#36;
     #502#0 = #501#0;
     #502#1 = 999;
     |LEE 000#502];
     |SI FSalida = 0;
          |LEE 000#502a;
     |SINO;
          |LEE 000#502u;
     |FINSI;
     |SI FSalida = 0; |Y #502#20 = #501#36; |Y #502#0 = #501#0;
          nLin = #502#1 + 1;
     |SINO;
          nLin = 1;
     |FINSI;

     |DDEFECTO #502;
     #502#20 = #501#36;
     #502#0 = #501#0;
     #502#1 = nLin;
     #502#2 = Valor2;    || arti
     #502#3 = 1;
     #502#4 = #501#1;
     #502#5 = Valor4;    || uni
     nIva = Valor14;     ||
     nIva = (nIva * 7 / 100) ! 2; ||Importe IVa Línea
     #502#6 = Valor14 + nIva;
     #502#7 = Valor4 * #502#6;
     #502#8 = Valor6;
     #502#9 = #501#14;
     #502#10 = Valor14;
     |QBT Valor3;
     #502#11 = Valor3;
     #502#13 = 2;
     #502#15 = 1;
     #502#16 = #501#34;
     #502#17 = #501#33;
     #502#18 = "T";
     #502#19 = #502#7 - (Valor4 * Valor14);
     #502#21 = Valor15;
     |GRABA 020#502n;
     enForma = 1;
     |HAZ AcumulaTPV;
|FPRC;

|PROCESO lee_campos;
     |XLEE nHandle, 1, field;
     |SI FSalida < 0; nError = 1; |FINSI;
     alfa5 = field % 0;  nume1 = alfa5;
     |SI nume1 > 1;
          field = " ";     || caracter cero (lo transforma a 2 bytes)
     |FINSI;
     campoalf = campoalf + field;
|FPRC;

|PROCESO quita_cabecera;
     |PARA para1 = 1; |SI para1 [ nLonCab; |HACIENDO para1 = para1 + 1;
          |XLEE nHandle, 1, field;
     |SIGUE;
|FPRC;

|PROCESO Datos;
     |XABRE "B", aDes, nHandle;
     |SI FSalida < 0;
          |MENAV "0000ERROR al abrir el Fichero...";
     |SINO;
          |HAZ quita_cabecera;
          |PARA regis = 1;|SI regis [ nNumReg;|HACIENDO regis = regis + 1;
               aAlfa = regis + "                  ";
               |PINTA 2302, aAlfa;
               sw_seleccio = 0;
               campoalf = "";
               |HAZ lee_campos;
               flg_bor = &campoalf;

               IValor = Valor1<-;
               Valor = "";

               |PARA campo = 1;|SI campo [ nNumCam;|HACIENDO campo = campo + 1;
                    #1#0 = campo;
                    |LEE 020#1=;
                    |SI FSalida = 0;
                         campoalf = "";
                         |PARA longn = 1;|SI longn [ #1#2;|HACIENDO longn = longn + 1;
                              |HAZ lee_campos;
                         |SIGUE;
                         |SI #1#4 = "FECHA     ";
                              campoalf = (campoalf % 7) + "." + (campoalf % 502) + "." + (campoalf % 104);
                         |FINSI;
                         Valor = campoalf;
                         IValor = IValor + 1;
                    |FINSI;
                    |SI nError = 1; |SAL; |FINSI;
               |SIGUE;
               |SI flg_bor = 32; || Todo OK
                    |SI aFic = "platos.dbf"; |HAZ GraPlatos; |FINSI;
                    |SI aFic = "hcmesa.dbf"; |HAZ GraHcmesa; |FINSI;
                    |SI aFic = "hlmesa.dbf"; |HAZ GraHlmesa; |FINSI;
                    |SI aFic = "hpagos.dbf"; |HAZ GraHpagos; |FINSI;
               |FINSI;
               |SI nError = 1; |SAL; |FINSI;
          |SIGUE;
     |FINSI;
|FPRC;

|PROCESO Campos;
     |XLEE nHandle, 20, field; || espacio reservado DBase I

     alfa1 = Usuario;     |QBF alfa1;
     |PARA campo = 1; |SI campo [ nNumCam; |HACIENDO campo = campo + 1;
          |DDEFECTO #1;
          #1#0 = campo;                   || nro.campo
          bytes = 11; |HAZ LeeAsc;
          #1#1 = vuelt1;                  || descripcion
          bytes = 1;  |HAZ LeeAsc;
          |SI vuelt1 = "C";    #1#4 = "ALFANUMERI"; |FINSI;
          |SI vuelt1 = "N";    #1#4 = "NUMERICO  "; |FINSI;  || tipo de campo
          |SI vuelt1 = "L";    #1#4 = "LOGICO    "; |FINSI;
          |SI vuelt1 = "D";    #1#4 = "FECHA     "; |FINSI;
          |SI vuelt1 = "M";    #1#4 = "MEMORIA   "; |FINSI;
          bytes = 4;  |HAZ LeeAsc;   || (basura)
          bytes = 1;  |HAZ LeeHex;
          #1#2 = ndeci;                   || longitud campo
          bytes = 1;  |HAZ LeeHex;
          #1#3 = ndeci;                   || nro. decimales
          bytes = 14;  |HAZ LeeAsc;  || (basura)
          |GRABA 020#1n;
     |SIGUE;

  ||   aAlfa = "0000Fichero:" + aFic; |MENAV aAlfa; |CONSULTA_CLAVE #1#1;
|FPRC;

|PROCESO deci_hexa;
    nhexa = "";
    |PARA npara1 = 2; |SI npara1 ] 1; |HACIENDO npara1 = npara1 - 1;
        |SI npara1 = 1; nnume1 = ndeci $ 16;          |FINSI;
        |SI npara1 = 2; nnume1 = (ndeci / 16) $ 16;   |FINSI;

        |SI nnume1 < 10;     nhexa = nhexa + nnume1;  |FINSI;
        |SI nnume1 = 10;     nhexa = nhexa + "A";     |FINSI;
        |SI nnume1 = 11;     nhexa = nhexa + "B";     |FINSI;
        |SI nnume1 = 12;     nhexa = nhexa + "C";     |FINSI;
        |SI nnume1 = 13;     nhexa = nhexa + "D";     |FINSI;
        |SI nnume1 = 14;     nhexa = nhexa + "E";     |FINSI;
        |SI nnume1 = 15;     nhexa = nhexa + "F";     |FINSI;
    |SIGUE;
|FPRC;

|PROCESO hexa_deci;
    nalfa1 = nhexa % 0;
    nlongi = nalfa1;
    ndeci = 0;
    |PARA npara1 = nlongi; |SI npara1 ] 1; |HACIENDO npara1 = npara1 - 1;

        nnume1 = (((nlongi - npara1) + 1) * 100) + 1; || pos.en la cadena
        nalfa2 = nhexa % nnume1;    || contenido de esa posicion
        nnume2 = nalfa2;            || contenido numerico
        |SI nalfa2 = "A";     nnume2 = 10;   |FINSI;
        |SI nalfa2 = "B";     nnume2 = 11;   |FINSI;
        |SI nalfa2 = "C";     nnume2 = 12;   |FINSI;
        |SI nalfa2 = "D";     nnume2 = 13;   |FINSI;
        |SI nalfa2 = "E";     nnume2 = 14;   |FINSI;
        |SI nalfa2 = "F";     nnume2 = 15;   |FINSI;

        nnume1 = 1;
        npara3 = npara1 - 1;
        |PARA npara2 = 1; |SI npara2 [ npara3; |HACIENDO npara2 = npara2 + 1;
            nnume1 = nnume1 * 16;
        |SIGUE;
        ndeci = ndeci + (nnume1 * nnume2);

     |SIGUE;
|FPRC;

|PROCESO da_vuelta;
    longa = vuelt1 % 0;   longn = longa;
    longn = longn - 1;
    vuelt2 = "";
    |PARA vuelta = longn; |SI vuelta ] 1; |HACIENDO vuelta = vuelta - 2;
        coger = (vuelta * 100) + 2;
        vuelt0 = vuelt1 % coger;
        vuelt2 = vuelt2 + vuelt0;
    |SIGUE;
|FPRC;

|PROCESO LeeHex;
     vuelt1 = "";
     |PARA para1 = 1; |SI para1 [ bytes; |HACIENDO para1 = para1 + 1;
          |XLEE nHandle, 1, field;
          alfa5 = field % 0;
          nume1 = alfa5;
          |SI nume1 > 1;
               ndeci = 0;        || caracter cero (lo transforma a 2 bytes)
          |SINO;
               ndeci = &field;
          |FINSI;
          |HAZ deci_hexa;
           vuelt1 = vuelt1 + nhexa;
     |SIGUE;
     |HAZ da_vuelta;
     nhexa = vuelt2;
     |HAZ hexa_deci;
|FPRC;

|PROCESO LeeAsc;
     vuelt1 = "";
     |PARA para1 = 1; |SI para1 [ bytes; |HACIENDO para1 = para1 + 1;
          |XLEE nHandle, 1, field;
          alfa5 = field % 0;
          nume1 = alfa5;
          |SI nume1 = 1;                      || el caracter cero lo desprecia
               vuelt1 = vuelt1 + field;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO UltimoAcceso;
     |XLEE nHandle, 1, field; any = &field;  || lee a¤o
     |XLEE nHandle, 1, field; mes = &field;  || lee mes
     |XLEE nHandle, 1, field; dia = &field;  || lee dia

     alfa1 = dia;    alfa1 = ("00" + alfa1) % -102;
     alfa2 = mes;    alfa2 = ("00" + alfa2) % -102;
     |SI any < 80;  any = any + 2000;   |SINO;    any = any + 1900;   |FINSI;
     alfa3 = alfa1 + "." + alfa2 + "." + any;
     fUltimo = alfa3;
|FPRC;

|PROCESO LonRegistro;
      bytes = 2; |HAZ LeeHex;
      nLonReg = ndeci;
|FPRC;

|PROCESO LonCabeza;
     bytes = 2; |HAZ LeeHex;
     nLonCab = ndeci;
     nNumCam = (nLonCab - 33) /  32;
     nNumCam = nNumCam ! 0;
     || No es correcto el N§ campos, lo asigno a pelo
     |SI aFic = "platos.dbf"; nNumCam = 9; |FINSI;
     |SI aFic = "hcmesa.dbf"; nNumCam = 18; |FINSI;
     |SI aFic = "hpagos.dbf"; nNumCam = 13; |FINSI;
     |SI aFic = "hlmesa.dbf"; nNumCam = 16; |FINSI;
|FPRC;

|PROCESO NumRegistro;
     bytes = 4; |HAZ LeeHex;
     nNumReg = ndeci;
|FPRC;

|PROCESO Version;
     |XLEE nHandle, 1, aReg;
     nVersion = &aReg;
|FPRC;

|PROCESO Importa;
     |SI aIP = "";  ||Local
          aDes = aPath + aFic;
     |SINO;         ||Remoto
          |DFICE aAlfa;
          aDes = aAlfa + "dbf.dbf";
          aOrg = aPath + aFic;
          |RECIBE_FICHERO aOrg, aDes;
     |FINSI;

     |DELINDEX #1;
     |ABRE #1;
     |XABRE "B", aDes, nHandle;
     |SI FSalida ] 0;
          |HAZ Version;
          |SI nVersion = 3;
               |PINTA 2202, "Calculando estructura...";
               |HAZ UltimoAcceso;
               |HAZ NumRegistro;
               |HAZ LonCabeza;
               |HAZ LonRegistro;
               |HAZ Campos;
               |XCIERRA nHandle;
               |BLIN 22;
               aAlfa = "Procesando " + aFic + "               ";
               |PINTA 2202, aAlfa;
               |HAZ Datos;
          |SINO;
               |XCIERRA nHandle;
               aAlfa = "0000Version incorrecta, version DBF: " + nVersion;
               |MENAV aAlfa;
          |FINSI;
     |SINO;
          aAlfa = "0000ERROR al abrir '" + aFic + "'";
          |MENAV aAlfa;
     |FINSI;
     |CIERRA #1;
     |SI aIP ! ""; |FBORRA aDes; |FINSI;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     aIP = ""; |IP_REMOTO aIP; |QBF aIP;

     fFecha = ~;
     |HORA aHora;
     aUsu = Usuario % 810;

     aPath = #0#1; |QBF aPath;
     aAlfa = aPath % -101;
     |SI aAlfa ! "/"; |Y aAlfa ! "\";
          aPath = aPath + "/";
     |FINSI;

     |ABRE #25;
     |ABRE #23;
     |ABRET #19;
     |ABRE #501;
     |ABRE #502;
     |ABRE #600;
     aFic = "platos.dbf"; |HAZ Importa;
     aFic = "hcmesa.dbf"; |HAZ Importa;
     aFic = "hpagos.dbf"; |HAZ Importa;
     aFic = "hlmesa.dbf"; |HAZ Importa;
     |LEE 000#600p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          |DFICE aAlfa;
          aAlfa = aAlfa % -205;
          eTienda = aAlfa;
          aAlfa = #600#0 % 110;
          eFecha = aAlfa;
          aAlfa = #600#0;
          aAlfa = aAlfa % 11;
          eCaja = aAlfa;
          |SUB_EJECUTA_NP "agifa565;algo";
          |LEE 000#600s;
     |SIGUE;
     |CIERRA #600;
     |CIERRA #502;
     |CIERRA #501;
     |CIERRAT #19;
     |CIERRA #23;
     |CIERRA #25;
|FPRC;

|PROCESO Browse; |TIPO 0;
     |SI FSalida ! 13; |ACABA; |FINSI;
     aRuta = #0#1; |QBF aRuta;
     |SI aRuta = ""; aRuta = "*.*"; |FINSI;
     aRuta = "D" + aRuta;
     |BROWSE aRuta;
     aRuta = aRuta % 260; |QBF aRuta;
     |SI aRuta = "";
          |ERROR;
     |SINO;
          #0#1 = aRuta;
          |PINTA #0#1;
     |FINSI;
|FPRC;

|PROGRAMA;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     |HAZ CreaTempo; |NOME_DAT #1Tempo; aTmp1 = Tempo;
     |HAZ CreaTempo; |NOME_DAT #600Tempo; aTmp600 = Tempo;

     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |GRABA 020#0a;
     |FINSI;
     |CIERRA #0;

     |DELINDEX #1; Tempo = aTmp1; |HAZ BoraTempo;
     |DELINDEX #600; Tempo = aTmp600; |HAZ BoraTempo;
|FPRO;
