|FICHEROS;
     diefa313 #0; ||Este Def.
     diefa114 #1; ||Recibos.
     agifa134 #2; ||Facturas (C).
     diefz313 #5; ||Temporal.
     agifa025 #6; ||Representantes.
     agifa024 #7; ||Clientes.

     dscam045@ #100; ||Control recibos gestion
     dscam003@ #101; ||Recibos Venta Cartera
|FIN;

|VARIABLES;
     nImp  = 0;
     aFich = "";
     nSal  = 0;
     nDif  = 0;
     nFde  = 0;
     nFha  = 0;
     swImp = 0;

     aAlfa  = "";
     aAlfa1 = "";
|FIN;

|PROCESO diefz313;
     |SI swImp = 0;
          swImp = 1;
     |FINSI;
     |DDEFECTO #6;
     #6#0 = #5#0;
     |LEE 000#6=;
     |PRINT;
     #0#9  = #0#9  + #5#1;
     #0#10 = #0#10 + #5#2;
     #0#11 = #0#11 + #5#3;
     #0#12 = #0#12 + #5#4;
     #0#13 = #0#13 + #5#5;
|FPRC;

|DEFBUCLE diefz313;
     #5#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL,diefz313;
|FIN;

|PROCESO diefa114;
     aAlfa = #0#2;
     |QBF aAlfa;
     |SI aAlfa ! "";
          |SI #1#17 ! #0#2; |Y #1#17 ! #0#3; |Y #1#17 ! #0#4;
           |Y #1#17 ! #0#5; |Y #1#17 ! #0#6; |Y #1#17 ! #0#6;
               |ACABA;
          |FINSI;
     |FINSI;
     nImp = #1#9 - #1#19;
     |SI nImp = 0; |ACABA; |FINSI;
     |DDEFECTO #7;
     #7#0 = #1#2; ||Cod.Cliente.
     |LEE 000#7=;
     nFha = #1#8;
     nDif = nFde - nFha; ||  - nFde;
     |SI nDif > 0;
          |DDEFECTO #5;
          #5#0 = #7#25;  ||Cod.Representante Ficha Cliente.
          |LEE 101#5=;
          nSal = FSalida;
          |SI nDif [ 30;
               #5#1 = #5#1 + (#1#9 - #1#19);
          |FINSI;
          |SI nDif ] 31; |Y nDif [ 60;
               #5#2 = #5#2 + (#1#9 - #1#19);
          |FINSI;
          |SI nDif ] 61; |Y nDif [ 90;
               #5#3 = #5#3 + (#1#9 - #1#19);
          |FINSI;
          |SI nDif ] 91; |Y nDif [ 120;
               #5#4 = #5#4 + (#1#9 - #1#19);
          |FINSI;
          |SI nDif ] 121;
               #5#5 = #5#5 + (#1#9 - #1#19);
          |FINSI;
          |SI nSal ! 0; |GRABA 020#5n; |FINSI;
          |SI nSal = 0; |GRABA 020#5a; |FINSI;
          |LIBERA #5;
     |FINSI;
|FPRC;

|DEFBUCLE diefa114;
     #1#3;
     17;
     #0#0,"      "," ","N",00,#0#14;
     #0#1,"zzzzzz","z","N",99,#0#15;
     ;
     NULL,diefa114;
|FIN;

|PROCESO RellenaTempo;
   |SI #dscam003@#14 = "A"; |O #dscam003@#14 = "D"; |O #dscam003@#69 ! " "; |ACABA; |FINSI;

   aAlfa = #0#2; |QBF aAlfa;
   |SI aAlfa ! "";
      |SI #dscam003@#0 ! #0#2; |Y #dscam003@#0 ! #0#3; |Y #dscam003@#0 ! #0#4;
       |Y #dscam003@#0 ! #0#5; |Y #dscam003@#0 ! #0#6; |Y #dscam003@#0 ! #0#7;
          |ACABA;
      |FINSI;
   |FINSI;

   |DDEFECTO #diefa114;
   #diefa114#0  = #dscam003@#1;
   #diefa114#1  = #dscam003@#2;
   #diefa114#2  = #dscam003@#32;
   #diefa114#3  = #dscam003@#6;
   #diefa114#4  = #dscam003@#7;
   #diefa114#5  = "N";
   #diefa114#6  = #dscam003@#29;
   #diefa114#7  = #dscam003@#4;
   #diefa114#8  = #dscam003@#3;
   #diefa114#9  = #dscam003@#64;
   #diefa114#10 = #dscam003@#48;
   #diefa114#11 = "N";
   #diefa114#12 = #dscam003@#5;
   |SI #dscam003@#14 = "L"; |O #dscam003@#14 = "R"; |O #dscam003@#14 = "M";
    |O #dscam003@#14 = "T"; |O #dscam003@#14 = "E"; |O #dscam003@#14 = "e";
      #diefa114#13 = "S";
   |SINO;
      #diefa114#13 = "N";
   |FINSI;
   |SI #dscam003@#14 = "I"; #diefa114#14 = "S"; |FINSI;
   #diefa114#15 = #dscam003@#28;
   #diefa114#16 = #dscam003@#26;
   #diefa114#17 = #dscam003@#0;
   #diefa114#18 = #dscam003@#50;
   #diefa114#19 = #dscam003@#61;
   #diefa114#20 = #dscam003@#63;
   #diefa114#21 = #dscam003@#12;
   aAlfa1 = #dscam003@#15; |QBF aAlfa1; aAlfa1 = aAlfa1 - "Recibo ";
   #diefa114#22 = aAlfa1;
   #diefa114#23 = "N";
   #diefa114#24 = #dscam003@#40;
   |GRABA 020#diefa114.n;
|FPRC;

|DEFBUCLE RellenaTempo;
#dscam003@#6005;
3;
00,#0#14,000000,000," ",#0#0;
99,#0#15,999999,999,"z",#0#1;
;
NULL,RellenaTempo;
|FIN;

|PROCESO EmpCartera;
   aAlfa1 = ("00000" + #dscam045@#6) % -105;
   aAlfa1 = aAlfa + "dscarter/fich/" + aAlfa1 + "/";
   |PATH_DAT #101 aAlfa1;

   |BUCLE RellenaTempo;
|FPRC;

|DEFBUCLE EmpCartera;
#dscam045@#1002;
2;
#48#0,001,"V";
#48#0,999,"V";
;
NULL,EmpCartera;
|FIN;

|PROCESO CreaTempo;
     |DBASS aAlfa; |QBF aAlfa;
     aAlfa1 = aAlfa + "dscarter/def/dscam045.mas";
     |CARGA_DEF aAlfa1, dscam045@;
     |SI FSalida ! 0;
        |MENAV "    Problemas al cargar el def de control de recibos de cartera";
        |ERROR; |ACABA;
     |FINSI;

     aAlfa1 = aAlfa + "dscarter/def/dscam003.mas";
     |CARGA_DEF aAlfa1, dscam003@;
     |SI FSalida ! 0;
        |DESCARGA_DEF #dscam045@;
        |MENAV "    Problemas al cargar el def de recibos de cartera";
        |ERROR; |ACABA;
     |FINSI;

     aAlfa1 = aAlfa + "dscarter/fich/"; |PATH_DAT #100 aAlfa1;

     |BUCLE EmpCartera;

     |DESCARGA_DEF #dscam003@; |DESCARGA_DEF #dscam045@;
|FPRC;

|PROCESO ejec; |TIPO 3;

     |DELINDEX #1; |ABRE #1; |HAZ CreaTempo; |CIERRA #1;

     |IMPRE 0;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |INFOR "diefa313";
     |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;
     aFich = Usuario;
     |NOME_DAT #5 aFich; |ABRE #5;  |CIERRA #5;  |DELINDEX #5;
     |ABRE #5;
     |ABRE #7;
     nFde = #0#8;
     |BUCLE diefa114;
     |CIERRA #5;
     |CIERRA #7;
     |ABRE #6;
     |BUCLE diefz313;
     |CIERRA #6;
     |SI swImp = 1;
          |PIEINF;
     |FINSI;
     |FININF;
     |FINIMP;
     |DELINDEX #5;
|FPRC;
