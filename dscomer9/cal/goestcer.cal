|FICHEROS;
     goestcer #0;
     agifa024 #1;        ||Clientes
     goestpan #2,MANTE;  ||Lineal certificadas (LIN-CAB)
     goestpa2 #3;        ||Lineal certificadas (LIN-LIN)
     gocercab #4;        ||Certificaciones (CAB)
     gocerlin #5;        ||Certificaciones (LIN)
     gocerhis #6;        ||Historico de certificaciones
     gopre001 #7;        ||Presupuestos
     gopre003 #8;
     goestpen #9,MANTE;  ||Lineal pendientes (LIN-CAB)
     goestpe2 #10;       ||Lineal pendientes (LIN-LIN)
     goparame #100;
|FIN;
-----------------------------------------------------------------------------
|VARIABLES;
     &cCodMonBase   = "";
     &cDesMonBase   = "";
     &cCodMonTrab   = "";
     &cDesMonTrab   = "";

     &nDeci_PBase   = 0;
     &nDeci_TBase   = 0;
     &nDeci_PTrab   = 0;
     &nDeci_TTrab   = 0;
     &nDeci_TotVis  = 0;
     &nEurComTrab   = 0;
     &nEurComBase   = 0;
     &nEurVenTrab   = 0;
     &nEurVenBase   = 0;
     cFich = "";
     nHay = 0;
     nLinea = 0;
     nLineaL = 0;
     nTotalPen = 0;
|FIN;
-----------------------------------------------------------------------------
-----------------------------------------------------------------------------
|PROCESO Inicio; |TIPO 7;
     |ABRE #1;
     |LEE 000#1p;
     |SI FSalida = 0;
          #0#4 = #1#0;        ||Cliente
          #0#9 = #1#1;        ||Nombre cliente
     |FINSI;
     |LEE 000#1u;
     |SI FSalida = 0;
          #0#5  = #1#0;       ||Cliente
          #0#10 = #1#1;       ||Nombre cliente
     |FINSI;
     |CIERRA #1;
     |ABRE #7;
     |LEE 000#7p;
     |SI FSalida = 0;
          #0#0 = #7#0;        ||Serie presupuesto
          #0#2 = #7#1;        ||Numero presupuesto
     |FINSI;
     |LEE 000#7u;
     |SI FSalida = 0;
          #0#1 = #7#0;        ||Serie presupuesto
          #0#3 = #7#1;        ||Numero presupuesto
     |FINSI;
     |CIERRA #7;
     |PINDA #0#0;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO MiraFecha; TIPO 0;
     |SI #0#7 < #0#6;
          |MENAV "0000La fecha debe ser igual o mayor";
          |ERROR;
     |FINSI;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO MiraCliente; TIPO 0;
     |SI #0#5 < #0#4;
          |MENAV "0000El cliente debe ser igual o mayor";
          |ERROR;
     |FINSI;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO GrabaLinC;
     #3#8 = #2#1;                  ||Serie presupuesto
     #3#9 = #2#2;                  ||Numero presupuesto
     #3#10 = #2#8;                 ||Numero certificacion
     nLineaL = nLineaL + 1;
     #3#0 = nLineaL;               ||Linea
     #3#1 = #6#4;                  ||Partida
     #3#2 = #6#5;                  ||Descripcion partida
     #3#3 = #6#18;
     #3#4 = #6#7;                  ||Unidades
     #3#5 = #6#6;                  ||Unidad medida
     #3#6 = #6#9;                  ||Importe partida
     #3#7 = #6#8;                  ||Precio
     #3#11 = #6#12;                ||% certificado
     #3#12 = #6#10;                ||Importe certificado
     |GRABA 020#3n;
|FPRC;
-----------------------------------------------------------------------------
|DEFBUCLE GrabaLinC;
     #6#1;
     ;
     #2#1,#2#2,#2#8,1;
     #2#1,#2#2,#2#8,999;
     ;
     NULL, GrabaLinC;
|FIN
-----------------------------------------------------------------------------
|PROCESO Certifi;
     nLinea = nLinea + 1;
     #2#0 = nLinea;                ||Linea
     #2#1 = #4#0;                  ||Serie presupuesto
     #2#2 = #4#1;                  ||Numero presupuesto
     #2#3 = #4#2;                  ||Cliente
     #2#4 = #4#3;                  ||Nombre cliente
     #2#5 = #4#6;                  ||Moneda
     #2#6 = #5#4;                  ||Serie factura
     cCodMonTrab = #2#5;
     |HAZ MonBas;
     |SI #5#5 ! 0;
          #2#7 = #5#5;             ||Numero factura
     |FINSI;
     #2#8 = #5#2;                  ||Numero certificacion
     #2#9 = #5#6;                  ||Total certificacion
     |GRABA 020#2n;
     #2#7 = "      ";
     |BUCLE GrabaLinC;
     nHay = 1;
     |SI #0#8 = "I";
          |PRINT;
     |FINSI;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO GrabaCer;
     |BUCLELIN 2 Certifi #4;
|FPRC;
-----------------------------------------------------------------------------
|DEFBUCLE BuscaCer;
     #4#1;
     2,9;
     #0#0,#0#2,#0#4,#0#6;
     #0#1,#0#3,#0#5,#0#7;
     ;
     NULL,GrabaCer;
|FIN;
-----------------------------------------------------------------------------
|PROCESO MiraPen;
     |SI #8#19 ! 0;
          cCodMonTrab = #7#7;
          |HAZ MonBas;
          #10#7 = #8#0;                 ||Serie presupuesto
          #10#8 = #8#1;                 ||Numero presupuesto
          nLineaL = nLineaL + 1;
          #10#0 = nLineaL;              ||Linea
          #10#1 = #8#4;                 ||Partida
          #10#2 = #8#5;                 ||Descripcion
          #10#3 = #8#6;                 ||Unidad medida
          #10#4 = #8#19;                ||% Pendiente de certificar
          #10#5 = #8#13;                ||Importe Certificado
          #10#6 = #8#20;                ||Importe pendiente
          #10#9 = #8#14;                ||Cantidad Certificada.
          #10#10 = #8#15;               ||Cantidad Pdte. certificar.
          nTotalPen = nTotalPen + #10#6;
          |GRABA 020#10n;
          nHay = 1;
     |FINSI;
|FPRC;
-----------------------------------------------------------------------------
|DEFBUCLE MiraPen;
     #8#3;
     ;
     #7#0,#7#1,1;
     #7#0,#7#1,999;
     ;
     NULL,MiraPen;
|FIN;
-----------------------------------------------------------------------------
|PROCESO BuscaPen;
     nTotalPen = 0;
     |BUCLE MiraPen;
     |SI nHay = 1;
          nLinea = nLinea + 1;
          #9#0 = nLinea;                ||Linea
          #9#1 = #7#0;                  ||Serie presupuesto
          #9#2 = #7#1;                  ||Numero presupuesto
          #9#3 = #7#4;                  ||Fecha presupuesto
          #9#4 = #7#2;                  ||Cliente
          #9#5 = #7#3;                  ||Nombre cliente
          #9#6 = #7#7;                  ||Moneda
          #9#7 = #7#19;                 ||Tipo edificacion
          #9#8 = nTotalPen;             ||Total pendiente
          |GRABA 020#9n;
          |SI #0#8 = "I";
               |PRINT;
          |FINSI;
     |FINSI;
|FPRC;
-----------------------------------------------------------------------------
|DEFBUCLE BuscaPen;
     #7#1;
     2,4;
     #0#0,#0#2,#0#4,#0#6;
     #0#1,#0#3,#0#5,#0#7;
     ;
     NULL,BuscaPen;
|FIN;
-----------------------------------------------------------------------------
|PROCESO Dispos; |TIPO 3;

     |SI FSalida ! 2;
          |HAZ Temporales;              ||Borro fichero temporales

          |SI #0#11 = "C";              ||Busco los presupuestos CERTIFICADOS
               |ABRE #2;
               |ABRE #3;
               nHay = 0;
               nLinea = 0;
               nLineaL = 0;
               |BUCLE BuscaCer;

               |SI nHay = 1;

                    |SI #0#8 = "P";
                         |ENTLINEAL #1#2,-1,4,11,1,min,max;
                    |SINO;
                         |HAZ ImpreCer;      ||Proceso de impresion CERT.
                    |FINSI;

               |FINSI;

               |CIERRA #2;
               |CIERRA #3;

          |FINSI;

          |SI #0#11 = "P";              ||Busco los presupuestos PENDIENTES
               |ABRE #9;
               |ABRE #10;
               nHay = 0;
               nLinea = 0;
               nLineaL = 0;
               |BUCLE BuscaPen;
               |SI nHay = 1;
                    |SI #0#8 = "P";
                         |ENTLINEAL #1#9,-1,4,11,1,minp,maxp;
                    |SINO;
                         |HAZ ImprePen;      ||Proceso de impresion PEND.
                    |FINSI;
               |FINSI;
               |CIERRA #9;
               |CIERRA #10;
          |FINSI;

          |SI nHay = 0;
               |MENAV "0000No encuentro ningun dato";
               |ACABA;

          |FINSI;

     |FINSI;

|FPRC;
-----------------------------------------------------------------------------
|PROCESO min;
     #2#0 = 1;
     #2#1 = "     ";
     #2#2 = 1;
     #2#8 = 1;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO max;
     #2#0 = 999;
     #2#1 = "zzzzz";
     #2#2 = 999999;
     #2#8 = 999;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO minp;
     #9#0 = 1;
     #9#1 = "     ";
     #9#2 = 1;
     #9#3 = "01.01.0001";
|FPRC;
-----------------------------------------------------------------------------
|PROCESO maxp;
     #9#0 = 999;
     #9#1 = "zzzzz";
     #9#2 = 999999;
     #9#3 = "31.12.9999";
|FPRC;
-----------------------------------------------------------------------------
|PROCESO Temporales;
     cFich = "TM0" + Usuario;         ||Borrar temporal Certifacados LIN-CAB
     |QBF cFich;
     cFich = cFich + "zzzzzzzz";
     cFich = cFich % 108;
     |NOME_DAT #2 cFich;
     |ABRE #2;
     |CIERRA #2;
     |DELINDEX #2;
     |ABRE #2;
     |DDEFECTO #2;
     cFich = "TM1" + Usuario;         ||Borrar temporal Certificados LIN-LIN
     |QBF cFich;
     cFich = cFich + "zzzzzzzz";
     cFich = cFich % 108;
     |NOME_DAT #3 cFich;
     |ABRE #3;
     |CIERRA #3;
     |DELINDEX #3;
     |ABRE #3;
     |DDEFECTO #3;
     cFich = "TM2" + Usuario;         ||Borrar temporal Pendientes LIN-CAB
     |QBF cFich;
     cFich = cFich + "zzzzzzzz";
     cFich = cFich % 108;
     |NOME_DAT #9 cFich;
     |ABRE #9;
     |CIERRA #9;
     |DELINDEX #9;
     |ABRE #9;
     |DDEFECTO #9;
     cFich = "TM3" + Usuario;         ||Borrar temporal Pendientes LIN-LIN
     |QBF cFich;
     cFich = cFich + "zzzzzzzz";
     cFich = cFich % 108;
     |NOME_DAT #10 cFich;
     |ABRE #10;
     |CIERRA #10;
     |DELINDEX #10;
     |ABRE #10;
     |DDEFECTO #10;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO ImpCer;
     |PRINT;
|FPRC;
-----------------------------------------------------------------------------
|DEFBUCLE ImpCerCab;
     #2#1;
     ;
       1,"     ",1,1;
     999,"zzzzz",999999,999;
     ;
     NULL,ImpCer;
|FIN;
-----------------------------------------------------------------------------
|DEFBUCLE ImpCerLin;
     #3#1;
     ;
     "     ",1,1,1;
     "zzzzz",999999,999,999;
     ;
     NULL,ImpCer;
|FIN;
-----------------------------------------------------------------------------
|PROCESO ImpreCer;
     |SUB_EJECUTA_NP "goinform;IMPgoestcer";
     |INFOR "goestcer";
     |BUCLE ImpCerCab;
     |PIEINF;
     |FININF;
     |SUB_EJECUTA_NP "goinform;ENVgoestcer";
     |SUB_EJECUTA_NP "goinform;IMPgoestce2";
     |INFOR "goestce2";
     |BUCLE ImpCerLin;
     |PIEINF;
     |FININF;
     |SUB_EJECUTA_NP "goinform;ENVgoestce2";
     |SUB_EJECUTA_NP "goinform;RUNgoestcer";
|FPRC;
-----------------------------------------------------------------------------
|DEFBUCLE ImpPenCab;
     #9#1;
     ;
       1,"     ",1,"01.01.0001";
     999,"zzzzz",999999,"31.12.9999";
     ;
     NULL,ImpCer;
|FIN;
-----------------------------------------------------------------------------
|DEFBUCLE ImpPenLin;
     #10#1;
     ;
     "     ",1,1;
     "zzzzz",999999,999;
     ;
     NULL,ImpCer;
|FIN;
-----------------------------------------------------------------------------
|PROCESO ImprePen;
     |SUB_EJECUTA_NP "goinform;IMPgoestpen";
     |INFOR "goestpen";
     |BUCLE ImpPenCab;
     |PIEINF;
     |FININF;
     |SUB_EJECUTA_NP "goinform;ENVgoestpen";
     |SUB_EJECUTA_NP "goinform;IMPgoestpe2";
     |INFOR "goestpe2";
     |BUCLE ImpPenLin;
     |PIEINF;
     |FININF;
     |SUB_EJECUTA_NP "goinform;ENVgoestpe2";
     |SUB_EJECUTA_NP "goinform;RUNgoestpen";
|FPRC;
-----------------------------------------------------------------------------

|PROCESO Tipo13; |TIPO 13;
     |ABRE #100; |LEE 000#100p; |CIERRA #100;
     |SI #100#141 = "O";
          |ATRI R; |PINTA 1105, "  O.T.     "; |ATRI 0;
     |FINSI;
|FPRC;
