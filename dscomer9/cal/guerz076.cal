|FICHEROS;
     guerz076 #0;
     guerm220 #1;
     guerm205 #205;
     guerm201 #201;
     agifa112 #112;
     gotrabaj #100;
     guerm203 #203;
     guerm216 #216;
     guerm226 #226; || Parametros correo
|FIN;

|VARIABLES;
     aAlfa = "";
     aDirDestino = "";
     aIP = "";
     aServidor = "";
     aFrom = "";
     aTo = "";
     aSubject = "";
     aMensaje = "";
     aDjunto = "";
     aFicMail = "";
     i = 0;
|FIN;

|PROCESO Imprime;
     |PRINT;
|FPRC;

|PROCESO guerm220;
     |SI #1#20 = "S"; |Y #1#27 ! 0;
          |PARA i = 0; |SI i [ 28; |HACIENDO i = i + 1;
               #216i = #1i;
          |SIGUE;

          |DFICE aAlfa;
          aFicMail = aAlfa + "salida_" + #1#27 + ".txt";
          Impresora = aFicMail;
          |IMPRE -77;
          |SI FSalida ! 0; |ACABA; |FINSI;
          |INFOR "guerm215";
          |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

          #205#0 = #1#27;
          |LEE 000#205=;
          |BUCLELIN 2 Imprime #205;
          |PIEINF;
          |FININF;
          |FINIMP;

          |DDEFECTO #201;
          #201#0 = #1#17;
          |LEE 000#201=;

          |SI #1#24 = "S";  ||Subcontratado
               |DDEFECTO #112;
               #112#0 = #1#28;
               |LEE 000#112=;
               aDirDestino = #112#84;   || mail proveedor

               |DDEFECTO #203;
               #203#0 = #1#31;
               |LEE 000#203=;
               |SI #203#5 = "S";
                    aDirDestino = #203#6;
               |FINSI;
          |SINO;
               |DDEFECTO #100;
               #100#0 = #1#31;
               |LEE 000#100=;
               aDirDestino = #100#127;  || mail trabajador
          |FINSI;
          |QBF aDirDestino;
     ||     aIP = "xdscorreo.dv.diagram.net";
     ||     aServidor = "asmtp.diagram.es";
          |SI #226#1 = "F"; aIP = #226#2; |FINSI;
          |SI #226#1 = "L"; |IP_REMOTO aIP; |FINSI;
          |SI #226#1 = "S"; |IP_LOCAL aIP; |FINSI;
          |QBF aIP;
          aServidor = #226#3; |QBF aServidor;

          aFrom = aDirDestino;
          aTo = aDirDestino;
          aSubject = "Salida nº:" + #1#27;
          aMensaje = "$$$$" + aFicMail;
          aDjunto = aFicMail;
          |DSCORREO_ENVIA aIP, aServidor, aFrom, aTo, aSubject, aMensaje, aDjunto;
          |SI FSalida < 0;
               aAlfa = "Salida nº " + (("000000" + #1#27) % -106);
               aAlfa = "0000Error al enviar correo:" + &13 + aAlfa;
               |MENAV aAlfa;
          |FINSI;
          |FBORRA aFicMail;
     |FINSI;
|FPRC;

|DEFBUCLE guerm220;
     #1#1;
     37;
     #0#0, "     ", 000001, 001, 000, "S";
     #0#0, "zzzzz", 999999, 999, 999, "S";
     ;
     NULL, guerm220;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |ABRE #226; |LEE 000#226p; |CIERRA #226;
     |ABRE #205;
     |ABRE #201;
     |ABRE #203;
     |ABRE #112;
     |ABRE #100;
     |BUCLE guerm220;
     |CIERRA #100;
     |CIERRA #112;
     |CIERRA #203;
     |CIERRA #201;
     |CIERRA #205;
|FPRC;
