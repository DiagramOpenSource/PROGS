|FICHEROS;
     dsz00066 #0;
     agifa007 #7;
     agifa010 #10;
     agifa019 #19;
     agifa024 #24;
     agifa065 #65;
     agifa071 #71;
     dsw00072 #72;  || Tmp para crear albaranes
     dsw00079 #79;  || Tmp Presencia
     dsm00085 #85;  || Histo Importa
     dsm00086 #86;  || Para poder identificar el registro y no duplicarlo al reimportar
     tmp00000 #100; || Tmp Recalcular tiquet
     agifa142 #142;
     dsm00241 #241;
     dsm00255 #255;
     dsm00260 #260;
     dsm00274 #274;
     dsm00276 #276;
     agifa501 #501;
     agifa502 #502;
     agifa505 #505;
     agifa509 #509;
     agifa512 #512;
     agifa513 #513;
     agifa514 #514;
     agifa515 #515;
     agifa516 #516;
     agifa517 #517;
     tmp2t600 #600; || Tmp para Fechas
     agifa027 #27;
     agifa321 #321;
     agifa324 #324;

     agifa134 #134;
|FIN;

|VARIABLES;
     nSwVipei = 0;
     &enTiva = 0;
     &efFiva = @;
     &eaDiva = "";
     &enIva = 0;
     &enRec = 0;
     aNomFicImp = "";
     nHandlePalm = 0;
     nGrabaHotel = 0;
     aPathHotel = "";
     nSwDs_suite = 0;
     nSwPalmHotel = 0;
     nEmpre = 0;
     aSubProg = "";
     aDes = "";
     &enError = 0;
     &eaFichero = "";
     &enConCierre = 0;
     &enConTiqCab = 0;
     &enConTiqLin = 0;
     &enConPagos = 0;
     &enConPrese = 0;
     &enConCierre0 = 0;
     &enConTiqCab0 = 0;
     &enConTiqLin0 = 0;
     &enConPagos0 = 0;
     &enConPrese0 = 0;
     aRec = "";
     aDoc = "";
     &eaTag = "";
     aAlfa1 = "";
     aAlfa2 = "";
     &eaCli = "";
     &eaRga = "";
     &aMoneda = "";
     &aDivisa = "";
     aSer = "";
     claveconta = "";
     conta_reg = 0;
     &Tempo = "";
     aTmp72 = "";
     aTmp79 = "";
     aTmp600 = "";
     aTmp100 = "";
     aArtOrig = "";
     nMul = 0;
     aMul = "";
     nNume = 0;
     nConta = 0;
     &eCaja    = 0;
     &eTienda  = 0;
     &eFecha   = "";
     &EURODB = 0;
     nImpo = 0;
     nTotal = 0;
     nBase = 0;
     nIva = 0;
     fmes = "";
     mes = 0;
     imneto = 0;
     nerror = 0;
     beta = 0;
     nDto = 0;
     &enForma = 1;
     enImpCliCom = 0;
     enImpCliDto = 0;
     enImpCliMar = 0;
     enImpCliFac = 0;
     eaCliente = "";
     efFecha = @;
     aEmp = "";
     aFecha = "";
     nHandle = 0;
     aAlfa = "";
     aIP = "";
     aTipo = "";
     aRuta = "";
     aPath = "";
     aPathOrg = "";
     aFic = "";
     aUsu = "";
     fFecha = @;
     aHora = "";
     aTipoDato = "";

 {90}Valor = "";
     aReg = "";
     aLon = "";
     aDato = "";
     nPos = 0;
     aTab = "";
     i = 0;
     nCampo = 0;
     aLetra = "";
|FIN;

|PROCESO PalmHotelGra;
     nGrabaHotel = 1;
     aAlfa = #505#182; |QBF aAlfa;
     aPathHotel = aAlfa;
     aAlfa = aAlfa % 101;
     |SI aAlfa ! "/"; |Y aAlfa ! "/"; aPathHotel = aPathHotel + "/"; |FINSI;

     aAlfa = (aNomFicImp % 902) + (aNomFicImp % 602) + (aNomFicImp % 104);
     aAlfa = aPathHotel + aAlfa + ".txt";

     |XABRE "A", aAlfa, nHandlePalm;
     |SI FSalida < 0;
          |XABRE "W", aAlfa, nHandlePalm;
     |FINSI;
     |SI FSalida ] 0;
          || N§ habitaci¢n, Fecha, Hora, Importe
          aAlfa = #501#61 + " " + #501#14 + " " + #501#34 + " " + #501#59;
          |XGRABA nHandlePalm, aAlfa;
          |XCIERRA nHandlePalm;
     |FINSI;
|FPRC;

|PROCESO PalmHotelTxt;
     #501#61 = Valor27;
     |SI #501#31 = "D";
          #505#0 = #501#12;
          |LEE 000#505=;
          |SI FSalida = 0;
               aAlfa = #505#100;
               |QBT aAlfa;
               |SI aAlfa ! "";
                    #501#12 = #501#12 + 1000;     || caja B
                    #501#59 = #501#9;
                    |HAZ PalmHotelGra;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO SacaPath;
     aLon = aFic % A0;
     |PARA i = aLon; |SI i > 0; |HACIENDO i = i - 1;
          nPos = 100 + i;
          aFic = aFic % nPos;
          aAlfa = aFic % -101;
          |SI aAlfa = "."; |SAL; |FINSI;
     |SIGUE;
|FPRC;

|PROCESO CreaLinAlb;
     #19#0 = #502#2;
     |LEE 020#19=;

     |DDEFECTO #71;
     #71#29 = #10#52;            ||Serie
     #71#0 = #10#0;
     #71#1 = #502#1;
     #71#2 = #502#2;              ||Articulo
     #71#3 = #502#3;           ||almacen
     #71#4 = #502#8;              ||Descripcion
     #71#5 = #502#5;              ||Unidades
     #71#6 = #502#10;             ||Precio
     #71#38 = #71#6;
     #71#8 = #502#14;             ||Dto
     |SI #19#194 = 2;
          #71#7 = #502#29 * #71#6;     ||Importe
          #71#9 = (#502#29 * #71#6) < #71#8;||Imp.Tot
     |SINO;
          #71#7 = #502#5 * #71#6;     ||Importe
          #71#9 = (#502#5 * #71#6) < #71#8;||Imp.Tot
     |FINSI;
     #71#11 = #502#13;            ||Tipo de Iva
     #71#13 = #502#11;            ||Familia
     #71#14 = #502#23;            ||Coste
     #71#15 = #501#1;             ||Cliente
     #71#16 = #502#34;           ||Tipo Comision
     #71#17 = #10#35;           ||Tipo Cliente
     #71#48 = #502#29;            ||Uni2
     #71#49 = #502#30;            ||Partida
     #71#50 = #502#31;            ||Merma
     |SI #10#70 = "D";
          #71#38 = #71#6;
     |SINO;
          #71#38 = #71#36;
     |FINSI;
     |HAZ TotalLin71;
     |GRABA 021#71n;

     #65#0 = #502#2;
     #65#1 = #501#14;
     #65#2 = "TI";
     #65#3 = #501#0;
     #65#4 = #502#1;
     #65#11 = #501#57;
     |LEE 121#65=;
     |SI FSalida = 0;
          |BORRA 020#65a;
          |LIBERA #65;

          #65#2 = "AV";
          #65#3 = #10#0;
          |GRABA 020#65n;
     |FINSI;
|FPRC;

|PROCESO CreaCabAlb;
     aMoneda = "B";
     aDivisa = #321#9;
     |HAZ Divisas010;

     |DDEFECTO #7;
     #7#26 = #501#57;
     |LEE 000#7=;

     |DDEFECTO #24;
     #24#0 = #501#1;
     |LEE 000#24=;

     #10#58 = "N";
     #10#59 = "00.00.0000";
     |SI #501#3 > 0; beta = 1; |SINO; beta = -1; |FINSI;
     #501#3 = #501#3 * beta;
     nDto = (((#501#3 < #501#17) < #501#19) < #501#18) * beta;
     #501#3 = #501#3 * beta;
     nDto = #501#3 - nDto;   || Total Dto Cabecera

     #10#1 = #501#14;             ||Fecha
     #10#3 = #501#1;              ||Cliente
     #10#4 = #24#1;              ||Nombre
     #10#5 = #501#17;             ||Dto
     aAlfa1 = #505#111;    |QBF aAlfa1;
     |SI aAlfa1 ! "";
         #10#6 = ("00" + #505#111) % -102;   || representante fijo
     |SINO;
         #10#6 = ("00" + #501#33) % -102;    || cajero como representante
     |FINSI;
     #10#7 = #501#18;             ||Dto pp
     #10#8 = #24#20;             ||Forma de Pago
     #10#9 = #24#35;             ||Agencia Transportes
     #10#15 = #501#3;             ||Total bruto
     #10#16 = #501#20;            ||Base Iva 1
     #10#36 = #501#29;            || Recargo de Equivalencia || *(3)
     #10#40 = #24#41;
     #10#50 = #24#40;           ||Albaran valorado S/N;
     #10#51 = #7#28;
     #10#91 = #24#206;

     eaCli = #501#1; efFiva = #501#14;
     enTiva = 1; |HAZ  IVACli;
     #10#17 = #10#16 % enIva;   || Cuota I.Va. 1
     |SI #10#36 = "S";
          #10#18 = #10#16 % enRec;   || Cuota Rec I.Va. 1
     |FINSI;
     #10#19 = #501#23;            ||Base Iva 2

     enTiva = 2; |HAZ  IVACli;
     #10#20 = #10#19 % enIva;   || Cuota I.Va. 2
     |SI #10#36 = "S";
          #10#21 = #10#19 % enRec;   || Cuota Rec I.Va. 2
     |FINSI;
     #10#22 = #501#26;            ||Base Iva 3

     enTiva = 3; |HAZ  IVACli;
     #10#23 = #10#22 % enIva;   || Cuota I.Va. 3
     |SI #10#36 = "S";
         #10#54 = #10#22 % enRec;   ||Cuota Rec Iva 3
     |FINSI;
     #10#25 = nDto;            ||Total descuento
     #10#28 = #501#30;            ||Base Exenta
     #10#29 = #24#2;             ||Entregar a
     #10#30 = #24#5;             ||Dir Entrega
     #10#31 = #24#6;             ||Pob Entrega
     #10#32 = #501#19;            ||Dto Acumulado
     #10#33 = #24#7;             ||Prov Entrega
     #10#34 = #24#7;             ||Tipo Comision
     #10#35 = #24#4;             ||Tipo Cliente
     #10#63 = #24#15;             ||DNI;
     #10#44 = #501#48;            ||Base iva 4

     enTiva = 4; |HAZ  IVACli;
     #10#45 = #10#44 % enIva;   || Cuota I.Va. 4
     |SI #10#36 = "S";
         #10#46 = #10#44 % enRec;   || Cuota Rec I.Va. 4
     |FINSI;
     #10#47 = #501#51;            ||Base iva 5

     enTiva = 5; |HAZ  IVACli;
     #10#48 = #10#47 % enIva;   || Cuota I.Va. 5
     |SI #10#36 = "S";
         #10#49 = #10#47 % enRec;   || Cuota Rec I.Va. 5
     |FINSI;
     #10#24 = #10#17+#10#20+#10#23+#10#18+#10#21+#10#45+#10#46+#10#48+#10#49+#10#54;
     #10#61 = 0;     || A cuenta

     #10#68 = #321#9;      ||Divisa
     #10#69 = #324#2;      ||Cambio divisa a euros
     #10#70 = "B";         ||Moneda de trabajo
     #10#71 = "A";         ||Cambio en albaran
     #10#73 = #321#9;      ||Moneda Base
     #10#74 = #324#2;      ||Cambio moneda base a euros
     #10#75 = #501#3;        ||Bruto en Divisa
     #10#76 = #501#9;        ||Total Albaran en Divisa
     #10#77 = #501#3;        ||Bruto (vis)
     #10#78 = #501#9;        ||Total (vis)
     #10#79 = #501#3;        ||Bruto (no vis)
     #10#80 = #501#9;        ||Total (no vis)
     |GRABA 020#10a;
|FPRC;

|DEFBUCLE CreaLinAlb;
     #502#1;
     ;
     #501#36, #501#0, 001;
     #501#36, #501#0, 999;
     ;
     NULL, CreaLinAlb;
|FIN;

|PROCESO contique;
     |ABRE #27;
     #27#0 = claveconta;
     #27#2 = #501#57;
     |LEE 101#27=;
     |SI FSalida ! 0;
          #27#1 = 1;
          |GRABA 021#27b;
     |FINSI;
     conta_reg = #27#1;
     #27#1 = #27#1 + 1;
     |GRABA 021#27a;
     |CIERRA #27;
|FPRC;

|PROCESO SiguienteAlb;
     #10#52 = #501#57;
     #10#0 = 999999;
     |LEE 000#10];
     |SI FSalida = 0;
          |LEE 000#10a;
     |SINO;
          |LEE 000#10u;
     |FINSI;
     |SI FSalida = 0; |Y #10#52 = #501#57;
          conta_reg = #501#0 + 1;
     |SINO;
          conta_reg = 1;
     |FINSI;
|FPRC;

|PROCESO TmpAlbaran;
     #501#36 = #72#1;
     #501#0 = #72#2;
     |LEE 121#501=;
     |SI FSalida = 0;

          #501#57 = #501#36; ||Serie Albaran = Serie Tiquet

          |SI #142#137 = "F";
               |PARA FSalida = 0; |SI FSalida = 0; |HACIENDO;
                    |DDEFECTO #10;
                    claveconta = "valbaran";
                    |HAZ contique;
                    #10#52 = #501#57;
                    #10#0 = conta_reg;
                    |LEE 001#10=;
               |SIGUE;
          |SINO;
               |HAZ SiguienteAlb;
          |FINSI;
          |DDEFECTO #10;
          #10#52 = #501#57;
          #10#0 = conta_reg;
          |GRABA 020#10b
          |SI FSalida = 0;
               |HAZ CreaCabAlb;
               |BUCLE CreaLinAlb;
               |GRABA 020#10a;
               |LIBERA #10;
               eaTag = "AV";  |HAZ Riesgos;

               #501#58 =  #10#0;   || Actualiza Tiquet
               |GRABA 020#501a;
          |FINSI;
          |LIBERA #501;
     |FINSI;
|FPRC;

|DEFBUCLE TmpAlbaran;
     #72#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, TmpAlbaran;
|FIN;

|PROCESO CtrlPresencia;
     |DDEFECTO #514;
     #514#0 = #79#1;
     |LEE 000#514=;

     ||--Busca ultimo registro
     |SELECT #2#274;
     #274#1 = #79#1;
     #274#3 = "31.12.9999";
     #274#4 = "zzzzzzzz";
     #274#2 = "z";
     |LEE 000#274];
     |SI FSalida = 0;
          |LEE 000#274a;
     |SINO;
          |LEE 000#274u;
     |FINSI;
     |SI FSalida ! 0; |O #274#1 ! #79#1;
          FSalida = 1;   || No existe anterior
     |FINSI;
     |SI #274#2 = "S";
          FSalida = 1;   || El anterior es salida
     |FINSI;
     |SI #79#2 = "E";
          FSalida = 1;   || El actual es entrada
     |FINSI;

     |SI FSalida = 0;    || Lee lineal
          |SELECT #2#276;
          #276#1 = #274#1;
          #276#3 = #274#3;
          #276#4 = #274#4;
          |LEE 101#276=;
     |FINSI;
     |SI FSalida = 0;    || Todo OK actualiza lineal
          #276#5 = #79#3;
          #276#6 = #79#4;
          |GRABA 020#276a;
     |SINO;              ||Crea uno nuevo
          |SELECT #1#276;
          |LEE 000#276u;
          |SI FSalida = 0;
               nConta = #276#0 + 1;
          |SINO;
               nConta = 1;
          |FINSI;
          |DDEFECTO #276;
          #276#0 = nConta;
          #276#1 = #514#0;
          #276#2 = #514#1;
          |SI #79#2 = "E";
               #276#3 = #79#3;
               #276#4 = #79#4;
          |SINO;
               #276#5 = #79#3;
               #276#6 = #79#4;
          |FINSI;
          |GRABA 020#276n;
     |FINSI;
     |LIBERA #276;

     || Graba registro
     #274#0 = #79#0;
     #274#1 = #79#1;
     #274#2 = #79#2;
     #274#3 = #79#3;
     #274#4 = #79#4;
     #274#5 = #79#5;
     |GRABA 020#274n;
|FPRC;

|DEFBUCLE CtrlPresencia;
     #79#2;
     ;
     INICIO;
     FINAL;
     ;
     NULL, CtrlPresencia;
|FIN;

|PROCESO Presencia;
     enConPrese0 = enConPrese0 + 1;
     #86#0 = aDato
     |LEE 000#86=;
     |SI FSalida = 0; |ACABA; |FINSI;
     |GRABA 020#86n;

     |DDEFECTO #79;
     aFecha = (Valor1 % 902) + "." + (Valor1 % 602) + "." + (Valor1 % 104);
     #79#0 = aFecha;
     #79#1 = Valor2;
     aFecha = (Valor4 % 902) + "." + (Valor4 % 602) + "." + (Valor4 % 104);
     aHora = Valor4 % -108;
     #79#3 = aFecha;
     #79#4 = aHora;
     |LEE 101#79=;
     |SI FSalida ! 0; |GRABA 020#79b; |FINSI;
     #79#2 = Valor3;
     #79#5 = Valor5;
     |GRABA 020#79a;
     |LIBERA #79;
     enConPrese = enConPrese + 1;
     aAlfa = aTipoDato + " " + enConPrese + "               ";
     |PINTA 2302, aAlfa;
|FPRC;

|PROCESO Pagos;
     enConPagos0 = enConPagos0 + 1;
     #86#0 = aDato
     |LEE 000#86=;
     |SI FSalida = 0; |ACABA; |FINSI;
     |GRABA 020#86n;

     |DDEFECTO #512;
     #512#12 = Valor1;
     Valor1 = #512#12; || formateo
     #512#0 = 999999;
     |LEE 000#512];
     |SI FSalida = 0;
          |LEE 000#512a;
     |SINO;
          |LEE 000#512u;
     |FINSI;
     |SI FSalida = 0; |Y #512#12 = Valor1;
          nConta = #512#0 + 1;
     |SINO;
          nConta = 1;
     |FINSI;
     |DDEFECTO #512;
     #512#0 = nConta;
||     #512#1 = Valor8;

     #512#3 = Valor7;
     #512#4 = Valor9;
     #512#5 = Valor2;
     #512#6 = Valor5;
     aFecha = (Valor3 % 902) + "." + (Valor3 % 602) + "." + (Valor3 % 104);
     #512#11 = aFecha;
     #512#12 = Valor1;
     #512#14 = Valor4;
     |GRABA 020#512n;
     |LIBERA #512;
     enConPagos = enConPagos + 1;
     aAlfa = aTipoDato + " " + enConPagos + "               ";
     |PINTA 2302, aAlfa;

     |DFICE aEmp;
     aEmp = aEmp % -205;
     |DDEFECTO #509;
     #509#63 = aEmp;
     #509#0 = #512#5;
     #509#1 = #512#11
     |LEE 101#509=;
     |SI FSalida ! 0; |GRABA 020#509b; |FINSI;
     #509#5 = #509#5 + #512#4;
     |GRABA 020#509a;
     |LIBERA #509;
|FPRC;

|PROCESO Cierre;
     enConCierre0 = enConCierre0 + 1;
     |DFICE aEmp;
     aEmp = aEmp % -205;
     |DDEFECTO #509;
     #509#63 = aEmp;
     #509#0 = Valor3;
     aFecha = (Valor1 % 902) + "." + (Valor1 % 602) + "." + (Valor1 % 104);
     #509#1 = aFecha;
     |LEE 000#509=;
     |SI FSalida = 0; |ACABA; |FINSI;
     |GRABA 020#509b;
     #509#56 = Valor4;
     #509#24 = Valor9;
     #509#15 = Valor9;
     #509#53 = Valor10;
     #509#54 = Valor11;
     #509#55 = Valor12;
     #509#52 = Valor14;
     #509#105 = Valor17;
     #509#104 = Valor18;
     #509#103 = Valor19;
     #509#102 = Valor20;
     #509#101 = Valor21;
     #509#100 = Valor22;
     #509#99 = Valor23;
     #509#98 = Valor24;
     #509#97 = Valor25;
     #509#96 = Valor26;
     #509#95 = Valor27;
     #509#94 = Valor28;
     #509#92 = Valor29;
     |GRABA 020#509a;
     |LIBERA #509;

     enConCierre = enConCierre + 1;
     aAlfa = aTipoDato + " " + enConCierre + "               ";
     |PINTA 2302, aAlfa;
|FPRC;

|PROCESO actua_tarj;
     |SI #501#40 ! 0;
          |ABRE #515;
          |DDEFECTO #515;
          #515#0 = #501#46;
          |LEE 101#515=;
          |SI FSalida ! 0;    |GRABA 021#515b; |FINSI;
          #515#2 = #515#2 + (#501#40);    ||Total Ptas cargadas
          #515#3 = #501#14;                  ||Fecha Ult. Oper.
          #515#4 = #501#40;                  ||Ptas Cargadas
          |GRABA 021#515a;
          |LIBERA #515;
          |CIERRA #515;
          |ABRE #516;
          |DDEFECTO #516;
          #516#0 = #501#46;
          #516#1 = #501#14;
          |LEE 101#516=;
          |SI FSalida ! 0;    |GRABA 021#516b; |FINSI;
          #516#2 = #516#2 + (#501#40);    ||Total Ptas cargadas
          |GRABA 021#516a;
          |LIBERA #516;
          |CIERRA #516;
     |FINSI;
|FPRC;

|PROCESO ActCab;
     |SI #501#9 > 0; |O #501#31 ! "V";
          |SI #501#3 > 0; beta = 1; |SINO; beta = -1; |FINSI;
          #501#3 = #501#3 * beta;
          nDto = (((#501#3 < #501#17) < #501#19) < #501#18) * beta;
          #501#3 = #501#3 * beta;
          nDto = #501#3 - nDto;   || Total Dto Cabecera
          ||-------------------- Cliente
          |SI #501#31 ! "V";
               |ABRE #24;
               |DDEFECTO #24;
               #24#0 = #501#1;
               |LEE 101#24=;
               |SI FSalida ! 0; |GRABA 020#24b; |FINSI;
               fmes = #501#14 % A402;
               mes = ((fmes - 1) * 4) + 54;
               imneto = #501#3 - nDto;
               |SI #501#35 = "A";
                    #24#50 = #24#50 +. imneto;
               |SINO;
                    |SI #142#115 = "N";
                         nImpo = (imneto * 100) / (100 - #501#18);
                         nDto = nDto + (imneto - nImpo);
                    |SINO;
                         nImpo = imneto;
                    |FINSI;
                    #24mes = #24mes +. nImpo;
                    mes = mes + 1;
                    #24mes = #24mes +. nDto;
                    mes = mes + 1;
                    #24mes = #24mes +. #501#8;
                    #24#102 = #24#102 +. nImpo;
                    #24#103 = #24#103 +. nDto;
                    #24#104 = #24#104 +. #501#8;
                    beta = (FEntrada / 100) ! 0;
                    |SI beta = 1;
                         #24#45 = #501#14;
                    |FINSI;
                    #24#46 = imneto;
                    #24#110 = #24#110 +. imneto;

                    enImpCliCom = nImpo;
                    enImpCliDto = nDto;
                    enImpCliMar = #501#8;
                    enImpCliFac = imneto;
                    eaCliente = #501#1;
                    efFecha = #501#14;
                    |HAZ AcumulaCli;

               |FINSI;
               |GRABA 020#24a;
               |CIERRA #24;
          |FINSI;
          ||-------------------
          |SI #501#31 ! "V";
               |HAZ actua_tarj;           ||Actualiza la tarjeta
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO TiqCab;
     enConTiqCab0 = enConTiqCab0 + 1;
     |DDEFECTO #501;
     #501#36 = Valor1;
     #501#0 = Valor2;
     |LEE 000#501=;
     |SI FSalida = 0;         ||Grabamos el temporal de caja para que
          aAlfa = #501#14;    ||se recalculen estas (reimportacion)
          aAlfa = aAlfa + #501#12;
          #600#0 = aAlfa;
      ||    |GRABA 040#600n;

          |ACABA;
     |FINSI;
     |GRABA 020#501b;
     #501#12 = Valor3; || Valor22;  modificado segun tiquet 000778/00202
     |SI nSwVipei = 0;
          #501#12 = Valor22;
     |FINSI;

     aFecha = (Valor5 % 902) + "." + (Valor5 % 602) + "." + (Valor5 % 104);
     #501#14 = aFecha;
     #501#34 = Valor7 + ":00";
     #501#9 = Valor8;
     #501#76 = Valor9;
     #501#32 = Valor10;
     #501#10 = Valor12;
     #501#28 = Valor13;

     aAlfa = Valor28;
     |SI aAlfa = "";
          #501#1 = "000001";
     |SINO;
          #501#1 = Valor28;
     |FINSI;

     aAlfa = Valor16; |QBF aAlfa;
     |SI aAlfa ! "";
          |DDEFECTO #260;
          #260#0 = Valor16;
          |LEE 000#260=;
          |SI FSalida = 0;
               #501#1 = #260#1;
          |FINSI;
     |FINSI;
     #501#3 = Valor8;
     #501#9 = #501#3 - Valor13;
     #501#10 = Valor12 + Valor17;
     #501#11 = #501#10 - #501#9;
     #501#13 = "S";
     #501#17 = Valor14;
  ||   #501#23 = #501#9 / 1.16; || ???
  ||   #501#24 = #501#9 - #501#23;
  ||   #501#4 = #501#9 - #501#23;
     #501#31 = Valor15;
     #501#33 = Valor4;
     #501#35 = "T";
     |SI #501#31 = "D";
          #501#35 = "A";

          #72#1 = #501#36;  ||Graba Tmp Albaranes
          #72#2 = #501#0;
          |GRABA 040#72n;
     |SINO;
          #501#35 = "T";
     |FINSI;
     |SI #501#31 = "E"; #501#38 = #501#9 - Valor17; |FINSI;
     |SI #501#31 = "C"; #501#39 = #501#9 - Valor17; |FINSI;
     |SI #501#31 = "T"; #501#40 = #501#9 - Valor17; |FINSI;
     |SI #501#31 = "A"; #501#63 = #501#28; |FINSI;

     #501#55 = Valor19;
     #501#57 = Valor22;
     #501#58 = Valor23;
     #501#70 = Valor23;
     |SI #501#31 = "R"; #501#62 = #501#28; |FINSI;
     |SI #501#31 = "O"; #501#63 = #501#28; |FINSI;
     #501#46 = Valor21;
     #501#81 = Valor35;

     |SI nSwPalmHotel = 0; |O nSwDs_suite = 0;
          |HAZ PalmHotelTxt;
     |FINSI;

     |GRABA 020#501a;
     |LIBERA #501;

     |HAZ ActCab;

     aAlfa = #501#14;
     aAlfa = aAlfa + #501#12;
     #600#0 = aAlfa;
     |GRABA 040#600n;

     #100#0 = #501#36;
     #100#1 = #501#0;
     |GRABA 020#100n;

     enConTiqCab = enConTiqCab + 1;
     aAlfa = aTipoDato + " " + enConTiqCab + "               ";
     |PINTA 2302, aAlfa;
|FPRC;

|PROCESO TiqLin;
     enConTiqLin0 = enConTiqLin0 + 1;
     |DDEFECTO #502;
     #502#20 = Valor1;
     #502#0 = Valor2;
     #502#1 = Valor3 + 1;
     |LEE 000#502=;
     |SI FSalida = 0; |ACABA; |FINSI;
     |DDEFECTO #501;
     #501#36 = #502#20;
     #501#0 = #502#0;
     |LEE 101#501=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |GRABA 020#502b;

     aAlfa = Valor4 % 205;
     |SI aAlfa = "COBRO";
          #502#22 = "COBRO";
     |SINO;
          #502#22 = Valor4;
     |FINSI;
     #502#5 = Valor5;
     #502#6 = Valor6;
     #502#7 = Valor7;
     #502#8 = Valor8;
     #502#13 = Valor11;
     #502#14 = Valor13;

     |DDEFECTO #501;
     #501#36 = #502#20;
     #501#0 = #502#0;
     |LEE 000#501=;

     |ABRE #19;
     |DDEFECTO #19;
     |SELECT #4#19;
     #19#128 = #502#22;
     |LEE 000#19=;
     |SELECT #1#19;

     #502#2 = #19#0;

     aAlfa = #502#8 % 103;
     |SI aAlfa ! "(C)";
          aArtOrig = #502#2;
          #255#0 = #502#2;
          |LEE 000#255=;
     |SINO;
          |DDEFECTO #255;
          #255#0 = aArtOrig;
          |LEE 000#255=;
     |FINSI;
     |SI #255#5 = "N";
          |SI #19#204 = "S";
               #241#0 = #19#5;
               |QBF Valor14;
               #241#2 = #19#206;
               |SELECT #2#241;
               |LEE 000#241=;
               |SI Valor14 = "CHU"; #241#5 = #241#5 * 2; |FINSI;
               |SI #241#6 = "D";
                    #502#5 = Valor5 * #241#5;
               |SINO;
                    #502#5 = Valor5 / #241#5;
               |FINSI;
          |SINO;
               #502#5 = Valor5;
          |FINSI;
     |SINO;
          |SI #502#2 ! aArtOrig;
               |DDEFECTO #19;
               #19#0 = aArtOrig;
               |LEE 000#19=;

               |DDEFECTO #241;
               #241#0 = #19#5;
               #241#2 = #19#206;
               |SELECT #2#241;
               |LEE 000#241=;
               |QBF Valor14;
               aMul = Valor14 % 201;
               nMul = aMul;
               |SI nMul = 0; nMul = 1; |FINSI;
               nNume = Valor5;
               |SI #241#6 = "D";
                    #241#5 = #241#5 / nMul;
                    #502#5 = nNume * #241#5;
               |SINO;
                    #241#5 = #241#5 * nMul
                    #502#5 = nNume / #241#5;
               |FINSI;
               ||VOLVEMOS A LEER EL ARTICULO COMBINDADO.
               #19#0 = #502#2
               |LEE 000#19=;
          |SINO;
               #502#5 = Valor5;
          |FINSI;
     |FINSI;
     |CIERRA #19;

     #502#5 = #502#5 ! 3; || Redondeamos a 3 decimales.
     #502#4 = #501#1;
     #502#9 = #501#14;

     ||nNume = Valor11;
     #505#0 = #501#12;
     |LEE 000#505=;
     |SI FSalida = 0;
          #502#3 = #505#87;
          |SI #505#177 = "S";
               enTiva = #19#30; || TIPO IVA ARTICULO
          |SINO;
               enTiva = #505#178;   ||TIPO IVA CAJA
          |FINSI;
     |SINO;
          enTiva = 2;
     |FINSI;
     efFiva = #502#9;
     |HAZ SacaIVA;
     nNume = enIva;

     #502#10 = #502#6 / (1 + (nNume / 100));
     #502#11 = #19#2;
     #502#12 = #19#3;
||     #502#13 = 2;             || ??? pendiente
     #502#13 = enTiva;

     #502#15 = #501#12;
     #502#16 = Valor10;
     #502#17 = #501#32;
     #502#18 = "T";

     nTotal = ((#502#6 * Valor5) < #502#14) ! EURODB;
     nBase = (nTotal / (1 + (nNume / 100))) ! EURODB;
     nIva = nTotal - nBase;
     #502#21 = nBase;
     #502#19 = nIva;

     #502#23 = #502#5 * #19#9;
     #502#43 = #19#204;
     #502#44 = Valor5;
     aAlfa = Valor8 % 103;
     |SI aAlfa = "(C)";
          #502#45 = "S";
     |SINO;
          #502#45 = "N";
     |FINSI;
     #502#46 = Valor12;

     |SI #501#31 = "R";  || Si es rotura se rellena el campo merma
          #502#31 = #502#5;
     |FINSI;

     enForma = 1;
     |HAZ AcumulaTPV;
     ||#502#23 = enCoste;

     |SI #502#21 > 0;
         nerror = 1;
     |SINO;
         nerror = -1;
         #502#21 =  -#502#21;
     |FINSI;
     imneto = ((((#502#21 < #501#17) < #501#19) < #501#18)) * nerror;
     #502#21 = #502#21 * nerror;

     #501#8 = #501#8 + (imneto - #502#23);

     nDto = (#501#28 * 100) / #501#3;
     nTotal = (nTotal  < nDto) ! EURODB;
     nBase = (nTotal / (1 + (nNume / 100))) ! EURODB;
     nIva = nTotal - nBase;

     |SI #502#13 = 0;
          #501#30 = #501#30 + nBase + nIva;
     |FINSI;
     |SI #502#13 = 1;
          #501#20 = #501#20 + nBase
          #501#21 = #501#21 + nIva;
          #501#4 = #501#4 + nIva;
     |FINSI;
     |SI #502#13 = 2;
          #501#23 = #501#23 + nBase
          #501#24 = #501#24 + nIva;
          #501#4 = #501#4 + nIva;
     |FINSI;
     |SI #502#13 = 3;
          #501#26 = #501#26 + nBase
          #501#27 = #501#27 + nIva;
          #501#4 = #501#4 + nIva;
     |FINSI;
     |SI #502#13 = 4;
          #501#48 = #501#48 + nBase
          #501#49 = #501#49 + nIva;
          #501#4 = #501#4 + nIva;
     |FINSI;
     |SI #502#13 = 5;
          #501#51 = #501#51 + nBase
          #501#52 = #501#52 + nIva;
          #501#4 = #501#4 + nIva;
     |FINSI;
     |GRABA 020#501a;
     |LIBERA #501;

     |GRABA 020#502a;
     |LIBERA  #502;
     enConTiqLin = enConTiqLin + 1;
     aAlfa = aTipoDato + " " + enConTiqLin + "               ";
     |PINTA 2302, aAlfa;
|FPRC;

|PROCESO SacaDocSerie;
     aSer = "";
     aDoc = "";
     aRec = "";
     aLon = Valor8 % A0;
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aAlfa = Valor8 % nPos;
          |SI aAlfa = "-";
               |SI aSer ! "";
                    |SI aRec ! ""; |SAL; |FINSI;
                    |SI aDoc = "";
                         aDoc = " ";
                    |SINO;
                         aRec = " ";
                    |FINSI;
               |SINO;
                    aSer = " ";
               |FINSI;
          |SINO;
               |SI aDoc ! "";
                    |SI aRec ! "";
                         aRec = aRec + aAlfa;
                    |SINO;
                         aDoc = aDoc + aAlfa;
                    |FINSI;
               |SINO;
                    |SI aSer ! ""; aSer = aSer + aAlfa; |FINSI;
               |FINSI;
          |FINSI;
     |SIGUE;
     |QBT aSer; |QBT aDoc; |QBT aRec;
|FPRC;

|PROCESO Dato;
     IValor = Valor1<-;
     |PARA i = 1; |SI i [ 90; |HACIENDO i = i + 1;
          Valor = "";
          IValor = IValor + 1;
     |SIGUE;
     nCampo = 1;
     IValor = Valor1<-;
     aLon = aDato % A0;
     aReg = "";
     aTab = &9;
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aLetra = aDato % nPos;
          |SI aLetra = aTab;
               Valor = aReg;
               aReg = "";
               nCampo = nCampo + 1;
               IValor = IValor + 1;
          |SINO;
               aReg = aReg + aLetra;
          |FINSI;
          |SI nCampo > 90; |SAL; |FINSI;     || Por si.... :)
     |SIGUE;
     Valor = aReg;
|FPRC;

|PROCESO ProcesaFic;
     enConCierre = 0;
     enConTiqCab = 0;
     enConTiqLin = 0;
     enConPagos = 0;
     enConPrese = 0;
     enConCierre0 = 0;
     enConTiqCab0 = 0;
     enConTiqLin0 = 0;
     enConPagos0 = 0;
     enConPrese0 = 0;
     aFic = "";
     aLon = aPath % A0;
     |PARA i = aLon ; |SI i > 0; |HACIENDO i = i - 1;
          nPos = i * 100 + 1;
          aAlfa = aPath % nPos;
          |SI aAlfa = "/"; |O aAlfa = "\"; |SAL; |FINSI;
          aFic = aAlfa + aFic;
     |SIGUE;

     aNomFicImp = aFic;

     |SI eaFichero = "";
          |DDEFECTO #85;
          #85#0 = aFic;
          |LEE 000#85=;
          |SI FSalida = 0; |ACABA; |FINSI;
          #85#1 = aUsu;
          #85#2 = fFecha;
          #85#3 = aHora;
          #85#4 = "T";
          |||GRABA 020#85n;  se graba al finalizar
     |FINSI;

     |DFICE aDes;
     aDes = aDes + Usuario;
     |SI aTipo = "C";
          |RECIBE_FICHERO aPath, aDes;
     |SINO;
          |COPIA_FICHERO aPath, aDes;
     |FINSI;

     aDato = "";
     |XABRE "", aDes, nHandle;
     |SI FSalida = 0;
          |XLEE nHandle, 240, aDato;
          |XCIERRA nHandle;
     |FINSI;
     aAlfa1 = aDato % 108;
     aAlfa2 = aDato % 119;
     |SI aAlfa1 ! "[CIERRE]"; |Y aAlfa2 ! "[CIERRERESTAURANTE]";
          |MENAV "0000No esta utilizando la importación correcta, utilice la del táctil";
          |ACABA;
     |FINSI;

     |XABRE "", aDes, nHandle;
     |SI FSalida = 0;
          |PARA; |SI; |HACIENDO;
               |XLEE nHandle, 240, aDato;
               |SI FSalida [ 0; |SAL; |FINSI;
               |QBF aDato;
               |SI aDato = "[CIERRE]"; |O aDato = "[CIERRERESTAURANTE]";
                         |O aDato = "[TIQUETS_CAB]";
                         |O aDato = "[TIQUETS_LIN]"; |O aDato = "[PAGOS]";
                         |O aDato = "[CONTROL_PRESENCIA]";
                    aTipoDato = aDato;
               |SINO;
                    |HAZ Dato;
                 ||   |SI aTipoDato = "[CIERRE]"; |HAZ Cierre; |FINSI;
                    |SI aTipoDato = "[TIQUETS_CAB]"; |HAZ TiqCab; |FINSI;
                    |SI aTipoDato = "[TIQUETS_LIN]"; |HAZ TiqLin; |FINSI;
                    |SI aTipoDato = "[PAGOS]"; |HAZ Pagos; |FINSI;
                    |SI aTipoDato = "[CONTROL_PRESENCIA]"; |HAZ Presencia; |FINSI;
               |FINSI;
          |SIGUE;
          |XCIERRA nHandle;

          |SI eaFichero = "";
               |GRABA 020#85n;
               aAlfa = aPath + ".ok";
               |SI aIP = "";
                    |RENOMBRA_FICHERO aPath, aAlfa;
               |SINO;
                    |RRENOMBRA_FICHERO aPath, aAlfa;
               |FINSI;
          |SINO;
                aPath = aPath - ".ok";
          |FINSI;

          aFic = aPath; |HAZ SacaPath;
          aFic = aFic + "ok";
          aAlfa = "W" + aTipo;
          |XABRE aAlfa, aFic, nHandle;
          |SI FSalida = 0;
               aAlfa = "[CIERRE] " + enConCierre + " de " + enConCierre0;
               |XGRABA nHandle, aAlfa;
               aAlfa = "[TIQUETS_CAB] " + enConTiqCab + " de " + enConTiqCab0;
               |XGRABA nHandle, aAlfa;
               aAlfa = "[TIQUETS_LIN] " + enConTiqLin + " de " + enConTiqLin0;
               |XGRABA nHandle, aAlfa;
               aAlfa = "[PAGOS] " + enConPagos + " de " + enConPagos0;
               |XGRABA nHandle, aAlfa;
               aAlfa = "[CONTROL_PRESENCIA] " + enConPrese + " de " + enConPrese0;
               |XGRABA nHandle, aAlfa;
               |XCIERRA nHandle;
          |FINSI;

          enError = 0;
     |SINO;
          |SI eaFichero ! "";
               aAlfa = "0000No existe:" + &13 + aPath;
               |MENAV aAlfa;
          |FINSI;
     |FINSI;

     |FBORRA aDes;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     enError = 1;
     fFecha = ~;
     |HORA aHora;
     aUsu = Usuario % 810;

     |ABRE #600;
     |ABRET #260;
     |ABRET #241;
     |ABRE #85;
     |ABRE #86;
     |ABRE #501;
     |ABRE #502;
     |ABRE #509;
     |ABRE #512;
     |ABRE #513;
     |ABRE #505;
     |ABRE #255;
     |ABRE #79;
     |ABRE #72;
     aPath = #0#1; |QBF aPath;
     aAlfa = aPath % -101;
     |SI aAlfa ! "/"; |Y aAlfa ! "\";
          aPath = aPath + "/";
     |FINSI;
     aPathOrg = aPath;
     aIP = ""; |IP_REMOTO aIP; |QBF aIP;

     |SI eaFichero ! "";
          |SI aIP = ""; aTipo = ""; |SINO; aTipo = "C"; |FINSI;
          aPath = aPathOrg + eaFichero;
          |HAZ ProcesaFic;
     |SINO;
          |SI aIP = "";
               aPath = aPathOrg + "*.txt";
               aTipo = "";
               |_PDIR aPath;
               |PARA; |SI FSalida = 0; |HACIENDO;
                    |HAZ ProcesaFic;
                    |_SDIR aPath;
               |SIGUE;
               aPath = aPathOrg + "*.TXT";
               aTipo = "";
               |_PDIR aPath;
               |PARA; |SI FSalida = 0; |HACIENDO;
                    |HAZ ProcesaFic;
                    |_SDIR aPath;
               |SIGUE;
          |SINO;
               aPath = aPathOrg + "*.txt"
               aTipo = "C";
               |R_PDIR aPath;
               |PARA; |SI FSalida = 0; |HACIENDO;
                    |HAZ ProcesaFic;
                    |R_SDIR aPath;
               |SIGUE;
               aPath = aPathOrg + "*.TXT"
               aTipo = "C";
               |R_PDIR aPath;
               |PARA; |SI FSalida = 0; |HACIENDO;
                    |HAZ ProcesaFic;
                    |R_SDIR aPath;
               |SIGUE;
          |FINSI;
     |FINSI;
     |CIERRA #72;
     |CIERRA #79;
     |CIERRA #255;
     |CIERRA #505;
     |CIERRA #512;
     |CIERRA #513;
     |CIERRA #509;
     |CIERRA #502;
     |CIERRA #501;
     |CIERRA #86;
     |CIERRA #85;
     |CIERRAT #241;
     |CIERRAT #260;

     |ABRE #274;
     |ABRE #276;
     |ABRE #514;
     |BUCLE CtrlPresencia;
     |CIERRA #514;
     |CIERRA #276;
     |CIERRA #274;

     |LEE 000#600p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          |DFICE aAlfa;
          aAlfa = aAlfa % -205;
          eTienda = aAlfa;;
          aAlfa = #600#0 % 110;
          eFecha = aAlfa;
          aAlfa = #600#0;
          aAlfa = aAlfa % 11;
          eCaja = aAlfa;
          |SI eCaja ! 0;
               |SUB_EJECUTA_NP "agifa565;algo";
          |FINSI;
          |LEE 000#600s;
     |SIGUE;
     |CIERRA #600;

     |ABRE #501;
     |ABRE #10;
     |ABRE #71;
     |ABRE #7;
     |ABRE #24;
     |ABRE #19;
     |ABRE #65;
     |BUCLE TmpAlbaran;
     |CIERRA #65;
     |CIERRA #19;
     |CIERRA #24;
     |CIERRA #7;
     |CIERRA #71;
     |CIERRA #10;
     |CIERRA #501;
|FPRC;

|PROCESO Browse; |TIPO 0;
     |SI FSalida ! 13; |ACABA; |FINSI;
     aRuta = #0#1; |QBF aRuta;
     |SI aRuta = ""; aRuta = "*.*"; |FINSI;
     aRuta = "D" + aRuta;
     |BROWSE aRuta;
     aRuta = aRuta % 260; |QBF aRuta;
     |SI aRuta = "";
          |ERROR;
     |SINO;
          #0#1 = aRuta;
          |PINTA #0#1;
     |FINSI;
|FPRC;

|PROCESO RecorreLin;
     #501#3 = #501#3 + #502#21;
|FPRC;

|DEFBUCLE RecorreLin;
     #502#1;
     ;
     #501#36, #501#0, 001;
     #501#36, #501#0, 999;
     ;
     NULL, RecorreLin;
|FIN;

|PROCESO RecalBruto;
     #501#36 = #100#0;
     #501#0 = #100#1;
     |LEE 121#501=;
     |SI FSalida = 0;
          #501#3 = 0;
          |BUCLE RecorreLin;
          |GRABA 020#501a;
          |LIBERA #501;
     |FINSI;
|FPRC;

|DEFBUCLE RecalBruto;
     #100#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, RecalBruto;
|FIN;

|PROGRAMA;
     |HAZ EsPalmHotel; nSwPalmHotel = FSalida;
     |HAZ EsDs_suite; nSwDs_suite = FSalida;
     |HAZ EsVipei; nSwVipei = FSalida;

     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     |ABRE #321; |LEE 000#321p; |CIERRA #321;
     |ABRE #517; |LEE 000#517p; |CIERRA #517;

     |ABRE #324;
     #324#0 = #321#9;
     |LEE 000#324=;
     |CIERRA #324;

     |HAZ CreaTempo; |NOME_DAT #600 Tempo; |DELINDEX #600; aTmp600 = Tempo;
     |HAZ CreaTempo; |NOME_DAT #79 Tempo; |DELINDEX #79; aTmp79 = Tempo;
     |HAZ CreaTempo; |NOME_DAT #72 Tempo; |DELINDEX #72; aTmp72 = Tempo;
     |HAZ CreaTempo; |NOME_DAT #100 Tempo; |DELINDEX #100; aTmp100 = Tempo;

     |ABRE #100;
     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     |SI PARAMETRO = "menu";
          |CLS;
          |PINPA #0#0;
          |PINDA #0#0;
          |ENDATOS #1#0;
          |SI FSalida = 0;
               |GRABA 020#0a;
          |FINSI;
     |SINO;
          |SI PARAMETRO ! "";
               #0#1 = PARAMETRO;
          |FINSI;
          |HAZ Tipo3;
     |FINSI;
     |ABRE #501;
     |BUCLE RecalBruto;
     |CIERRA #501;
     |CIERRA #0;
     |CIERRA #100;

     aTmp600 = Tempo; |HAZ BoraTempo; |DELINDEX #600;
     aTmp79 = Tempo; |HAZ BoraTempo; |DELINDEX #79;
     aTmp72 = Tempo; |HAZ BoraTempo; |DELINDEX #72;
     aTmp100 = Tempo; |HAZ BoraTempo; |DELINDEX #100;

/*
     |SI nSwPalmHotel = 0; |O nSwDs_suite = 0;
          |SI nGrabaHotel = 1;
          |SI aPathHotel = "";
               |MENAV "0000Carperta envio hotel no indicada en 'Parametros Caja'";
               |ACABA;
          |FINSI;
          nEmpre = #517#26;
          |SI nEmpre = 0;
               |MENAV "0000Empresa hotel no indicada en 'Parametros Generales TVP";
          |SINO;
               aSubProg = ":ds-suite/tarectac taempre1 " + (("00000" + nEmpre) % -105);
               |SUB_EJECUTA_NP aSubProg;
          |FINSI;
          |FINSI;
     |FINSI;
*/
|FPRO;
