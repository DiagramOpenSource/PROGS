     ||Copia del agifa383

|FICHEROS;
  tecaz027 #0;
  agifa345 #1;
  agifa007 #5;   || Series
  agifa019 #7;   || Articulos

  agifa065 #8;   ||Historico

  agifa010 #15;  ||albaran
  agifa071 #16;  ||lineas albaran
  agifa084 #17;  ||Contado
  agifa069 #18;  ||lineas contado
  agifa036 #19;  ||Depositos
  agifa070 #20;  ||lineas depositos
  agifa077 #21;  ||Albaran Compras
  agifa080 #22;  ||lineas albaran compras
  agifa321 #321;
  agifa324 #324;
  agifa142 #142;
|FIN;

|VARIABLES;
     &PRMNT_aHisto = "";
     aAlfa = "";
     aEmp = "";
     aProg = "";
     nAlma = 0;
     &eaArticulo = "";
     &nDeci_PreBase = 0;
     &Tempo     = "";
     aTempo1    = "";
     nom_tmp    = "";
     &operz     = "";
     &factu     = 0;
     nomodi     = 0;

     stock6     = 0;
     c          = 0;
     m          = 0;
     mensaje    = "";
     pintar     = "";
     i          = 0;
     paso       = 0;
     codigo     = "";
     sw         = 0;
     swno       = 0;

     des_art    = "";
     has_art    = "";
     &c_prove   = "";
     &c_artic   = "";
     &c_serie   = "";
     &entrada   = 0;
     &ser_alb   = "";
     &num_alb   = 0;
|FIN;

|| **************************************************************************
|PROCESO DeciPrecio;
     |ABRE #agifa324;
     |ABRE #agifa321;
     |LEE 001#agifa321.p;
     #agifa324#0 = #agifa321#9; ||Igualo a moneda base
     |LEE 001#agifa324.=;
     |CIERRA #agifa321;
     |CIERRA #agifa324;
     nDeci_PreBase = #agifa324#9;  ||Decimales precio de moneda base
|FPRC;



|PROGRAMA;
  |ABRE #142; |LEE 000#142p; |CIERRA #142;
  |CLS;
  |CABEZA "Consulta Precios Proveedor";
  |HAZ DeciPrecio;
  |HAZ CreaTempo; aTempo1 = Tempo;
  |NOME_DAT #1 aTempo1; |DELINDEX #1;

  |DDEFECTO #0;
  || Bucle menu
  |SI entrada = 1;
      #0#0  = c_prove; |C_M #0#0, 1,"N";
      #0#13 = c_artic; |SI c_artic ! ""; |C_M #0#13,1,"N"; |FINSI;
      #0#15 = c_serie; #0#16 = c_serie;
  |FINSI;
  |PINPA #0#0;    || Pinta la pantalla del informe
  |ENTLINEAL #1#1 , 1 , 7 , 23 , 1 , pro1 , pro2;
  |PARA FSalida = 1; |SI 0 < FSalida; |HACIENDO;
         |PINDA #0#0;    || Pinta los datos por defecto del informe
         |ENDATOS #1#0;        || Seleccion informe
         |SI FSalida = 0;
             |HAZ impresion;
             FSalida = 1;
        |FINSI;
        |SI entrada = 1; |SAL; |FINSI;
  |SIGUE;

  |DELINDEX #1; Tempo = aTempo1; |HAZ BoraTempo;
|FPRO;
|| **************************************************************************

|PROCESO grabar;
 swno = 0;
 |HAZ calbles;
 |SI swno = 1; |ACABA; |FINSI;

 || Seleccion de las Operaciones
 |PINTA 1129 , #8#1;      || ...  Pinta Fecha

 |SI sw = 0;
     codigo = #8#0;
     nAlma = #8#5;
     |HAZ leearticul;
     stock6 = 0;
     sw = 1;
 |FINSI;

 |SI codigo ! #8#0; |O nAlma ! #8#5;
     codigo = #8#0;
     nAlma = #8#5;
     |HAZ leearticul;
     stock6 = 0;
 |FINSI;

 |DDEFECTO #1;
 c = c + 1;
 stock6 = stock6 + #8#6;

 #1#0  = c;
 #1#1  = #8#10;
 #1#2  = #8#1;
 #1#3  = #8#2;
 #1#4  = #8#3;
 #1#5  = #8#4;
 #1#6  = #8#5;
 #1#7  = #8#6;
 #1#8  = stock6;
 #1#9  = #8#8;
 #1#10 = #8#9;
 #1#11 = #8#0;
 #1#12 = #8#11;
 #1#13 = #7#1;
 #1#14 = #8#14;
 |GRABA 021#1n;
|FPRC;

|PROCESO leearticul;
 |DDEFECTO #7;
 #7#0 = #8#0;
 |LEE 001#7=;
 |SI FSalida ! 0;
     #7#1 = "NO EXISTE ARTICULO EN MAESTRO";
 |FINSI;
|FPRC;

|PROCESO calbles;
 operz = #8#2;
 #1#12 = #8#11; || Serie
 |HAZ leeserie;
 |SI operz = "AV"; |O operz = "BV"; |O operz = "FC"; |O operz = "DP";
                   |O operz = "DV"; |O operz = "SF";
     swno = 1;                                        || Es un Cliente
 |FINSI;
|FPRC;

|DEFBUCLE pasa;
 #8#1;
 10;
 des_art, #0#1        , "     " ,       0 ,     0 , #0#15 , #0#0;
 has_art, "31.12.2090", "zzzzz" ,  999999 , 99999 , #0#16 , #0#0;
 e;
 NULL, NULL, NULL, NULL, NULL, grabar;
 #8#0, #8#5, #8#1;
|FIN;

|| ***************************************************************************

|PROCESO impresion;
 stock6 = 0;
 c      = 0;
 sw     = 0;

 |ABRE #1;
 |CIERRA #1;
 |DELINDEX #1;

 |PINTA 1110 , "Procesando Fecha : ";

 des_art = #0#13;
 has_art = "zzzzzzzzzzzzzzzzzzzz"; |SI entrada = 1; has_art = des_art; |FINSI;

 |ABRE #1;
 |ABRE #7;
 |BUCLE pasa;
 |CIERRA #7;
 |CIERRA #1;
 |PINTA 1110 , "                   ";
 |ENTLINEAL #1#1 , 1 , 4 , 23 , 1 , pro1 , pro2;

 |DELINDEX #1;
|FPRC;

|RUTINA pro1;
     #1#0 = 1;
|FRUT;

|RUTINA pro2;
     #1#0 = 999999;
|FRUT;

|PROCESO sal; |TIPO 11;
 |SI FSalida = 9;
     |HAZ imprime;
 |FINSI;
|FPRC;

|| ::::::::::::::::: Ha pulsado F1 (Ver Documento)
|PROCESO imprime;

 operz = #1#3;
 |HAZ leeserie;
 nomodi = 0;

 |PUSHV 0501 2380;
 |SI operz = "AV";|O operz = "BV";    ||Albaran
     |HAZ albaval;
 |FINSI;
 |SI operz = "FC";    ||Factura Contado
     |HAZ faconta;
 |FINSI;
 |SI operz = "DP";    ||Deposito
     |HAZ depo;
 |FINSI;
 |SI operz = "AC"; |O operz =  "BC";    ||Albaran Compras
     |HAZ albacom;
 |FINSI;
 |POPV;
|FPRC;

|PROCESO albaval;
 |ABRE #16;
 |ABRE #15;
 #15#52 = #1#12;
 #15#0 = #1#4;
 |LEE 001#15=;
 |SI FSalida = 0;
     |PINPA #0#15;
     |PINDA #0#15;
     |ENDATOS #4#15;
 |SINO;
      mensaje = "0000No Encuentro el Albaran " + #1#12 + "\" + #1#4;
      |MENAV mensaje;
 |FINSI;
 |CIERRA #15;
 |CIERRA #16;
|FPRC;

|PROCESO faconta;
 |ABRE #17;
 |ABRE #18;
 #17#48 = #1#12;
 #17#0 = #1#4;
 |LEE 001#17=;
 |SI FSalida = 0;
      |PINPA #0#17;
      |PINDA #0#17;
      |ENDATOS #4#17;
 |SINO;
      mensaje = "0000No Encuentro la Factura " + #1#12 + "\" + #1#4;
      |MENAV mensaje;
 |FINSI;
 |CIERRA #17;
 |CIERRA #18;
|FPRC;

|PROCESO depo;
 |ABRE #20;
 |ABRE #19;
 #19#46 = #1#12;
 #19#0 = #1#4;
 |LEE 001#19=;
 |SI FSalida = 0;
      |PINPA #0#19;
      |PINDA #0#19;
      |ENDATOS #4#19;
 |SINO;
      mensaje = "0000No Encuentro el Deposito " + #1#12 + "\" + #1#4;
      |MENAV mensaje;
 |FINSI;
 |CIERRA #19;
 |CIERRA #20;
|FPRC;

|PROCESO albacom;
 |ABRE #21;
 |ABRE #22;
 #21#0 = #1#4;
 #21#50 = #1#12;
 |LEE 001#21=;
 |SI FSalida = 0;
     |PINPA #0#21;
     |PINDA #0#21;
     ser_alb = #21#50;
     num_alb = #21#0;
     |PTEC 802;
     PRMNT_aHisto = ser_alb + num_alb;
     |DFICE aAlfa; aEmp = aAlfa % -205;
     aProg = ":dscomer9/agifa077.tab agif0041 " + aEmp;
     |SUB_EJECUTA_NP aProg;
     PRMNT_aHisto = "";
 |SINO;
     mensaje = "0000No Encuentro el Albaran " + #1#4;
     |MENAV mensaje;
 |FINSI;
 |CIERRA #21;
 |CIERRA #22;
|FPRC;

|PROCESO leeserie; || ....... Lee la Serie por si es Interno
 |ABRE #5;
 |DDEFECTO #5;
 #5#26 = #1#12; ||Serie
 |LEE 001#5=;
 |SI #5#37 = "S";
     |SI operz = "AC"; operz = "AV"; |FINSI;
     |SI operz = "BC"; operz = "BV"; |FINSI;
 |FINSI;
 |CIERRA #5;
|FPRC;

|PROCESO Llena383; |TIPO 6;
     eaArticulo = #0Campo;
     |HAZ LlenaArticulo;
     |SI FSalida = 0;
          #0Campo = eaArticulo;
          |PINTA #0Campo;
     |SINO;
          |ERROR; |ACABA;
     |FINSI;
|FPRC;
