|FICHEROS;
     agifv001 #0; || Pantalla Seleccion.
     agifa010 #1; || Cabecera Albaranes Venta.
     agifa071 #2; || Lineas      ''       ''
     agifa077 #3; || Cabecera Albaranes Compra.
     agifa080 #4; || Lineas      ''       ''
     agifa027 #5;
     agifa007 #6; || Series.
     agifa112 #7; || Proveedores.
     agifa017 #8; || Almacenes.
     agifa019 #9; || Articulos.
     fanarti@ #99;|| Articulos. (FANTASMA) UUUUUUHHHHHH
     agifa142 #10;|| Parametros Generales.
     agifa065 #11;|| Historico.
     agifv017 #33;|| Articulo Familia Fantasma para cargar la familia si no existe.
     agifa060 #34;|| Familias.
     agifa321 #35;  || Parametros de Divisas
     agifa328 #36;  || Clientes/Divisas
     agifa329 #37;  || Lineas de Clientes/Divisas
     agifa324 #38;  || Divisas
     agif0041 #906; || datos empresa;
     dsm00005 #905;
|FIN;


|VARIABLES;
     aTmp33 = "";
     aTmp99 = "";
     &Tempo = "";
     nLinV           = 0;
     &enSwMenu       = 0;

     aDef            = "";
     aDatos          = "";
     aDirEmp         = "";
     nCodEmp         = 0;

     &nDeci_Base     = 0;   || Decimales en Totales de la moneda base
     &nDeci_PreBase  = 0;   || Decimales en el Precio en moneda base
     &nDeci_BaseMx   = 0;   || Decimales en Totales de la moneda base (Maxima precision)
     &nDeci_PreBaseMx= 0;   || Decimales en el Precio en moneda base (Maxima precision)
     &nDeci_Div      = 0;   || Decimales en Totales de la divisa
     &nDeci_PreDiv   = 0;   || Decimales en el Precio en divisa
     &nDeci_TotVis   = 0;   || Decimales en Totales visibles (el de la moneda de trabajo)
     &nDeci_TotNoVis = 0;   || Decimales en Totales no visibles (el de la otra moneda)
     &nDeci_PreVis   = 0;   || Decimales en el Precio visual. (lineas)
     &nDeci_ImpVis   = 0;   || Decimales en el Importe visual. (lineas)
     nfecha    = 0;
     &ac_divisa = "N";   || Vale "S" cuando las divisas en ventas estan
                         || activadas y la serie es de divisas
     &pintado = 0;       || Sirve para controlar los pintados de las lineas
     &decim_divisa = 0;  || Decimales de la divisa
     &precio_divisa = 0; || Decimales del precio en divisa
     &decim_base = 0;    || Decimales de la moneda base
     &precio_base = 0;   || Decimales del precio en moneda base
     div_act = 0;        || Divisa actualizada. Me sirve en un PARA para que
                         || no salga hasta que este actualizada (div_act=1)
     &cli_divisa = "";   || Codigo de Cliente. Se pasa a CLIENTES/DIVISAS
      fdes = "";
      forg = "";
      fich = "";
      org = "";
      def = "";
      alfa = "";
      pm=0;
      canti2=0;
      tipalb=1;
      de=0;
      pe=0;
      imneto= 0;
      impret=0;
      st=0;
      a=0;
      calpre=0;
      neto=0;
      codiBarra="";
      n_dec = 0;
      n_pre = 0;
      salida=0;
      d=0;
      j=0;
      contador=0;
      vic=0;
      swvic=0;
      swdir=0;
      desde="";
      emp=0;
      direc="";
      diremp="";
      albcomp="";
fmes = "  ";
mes = 0;
mes1 = 0;
operacion1 = "AC";
operacion2 = "BC";
operacion = "  ";
mascara = "0000000";
tempo = " ";
filtro = "-106";
|FIN;

|INCLUYE i_varart;

|PROCESO cafa;
     #33#0 = #9#0;
     #33#1 = #9#2;
     #33#5 = #9#3;
     #33#6 = #9#128;
     #33#7 = #9#125;

     #34#0 = #9#2;
     |LEE 000#34=;
     |SI FSalida ! 0;  |DDEFECTO #34;  |FINSI;

     #33#2 = #34#1;
     #33#3 = #34#2;
     #33#4 = #34#3;
     |GRABA 020#33n;
     |LIBERA #33;
|FPRC;

|DEFBUCLE agifa019;
     #9#1;
     ;
     "                    ";
     "zzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, cafa;
|FIN;

|PROCESO prolive1;
     |DFICB direc;
     diremp = #0#1;
     diremp = ("00000" + diremp) % -105;
     diremp = direc + diremp + "/";
     |DFICE org;    |QBF org;
     |DDEF def;     |QBF def;
     fich = "agifa019";
     forg = def + fich;
     fdes = diremp + fich;

     #9#0 = #2#2;
     |LEE 101#9=;
     |SI FSalida = 0;
         #99#0 = #2#2;
         |LEE 040#99=;
         |SI FSalida ! 0;
             |GRABAENDEF forg, fdes, 0, #9#0,  1, #9#1,   2, #9#2,  3, #9#3, 4, #9#4;
             |GRABAENDEF forg, fdes, 0, #9#0,  5, #9#5,   9, #9#9,  18, #9#18, 19, #9#19;
             |GRABAENDEF forg, fdes, 0, #9#0, 20, #9#20, 21, #9#21, 22, #9#22, 23, #9#23;
             |GRABAENDEF forg, fdes, 0, #9#0, 30, #9#30, 102, #9#102, 127, #9#127, 128, #9#128;
             |GRABAENDEF forg, fdes, 0, #9#0, 135, #9#135, 136, #9#136, 137, #9#137, 138, #9#138;
             |GRABAENDEF forg, fdes, 0, #9#0, 139, #9#139, 140, #9#140, 141, #9#141, 142, #9#142;
             |GRABAENDEF forg, fdes, 0, #9#0, 143, #9#143, 144, #9#144, 145, #9#145, 146, #9#146;
             |GRABAENDEF forg, fdes, 0, #9#0, 147, #9#147, 148, #9#148, 149, #9#149, 150, #9#150;
             |GRABAENDEF forg, fdes, 0, #9#0, 151, #9#151, 152, #9#152;
             |GRABAENDEF forg, fdes, 0, #9#0, 179, #9#179, 180, #9#180, 181, #9#181, 182, #9#182;
             |GRABAENDEF forg, fdes, 0, #9#0, 183, #9#183, 184, #9#184, 185, #9#185;
         |FINSI;
     |FINSI;
     |LIBERA #9;
|FPRC;

|DEFBUCLE agifa010;
     #1#1;
     65;
     desde,      0, "N";
     desde, 999999, "N";
     ;
     NULL, NULL, prolive1;
|FIN;

|PROCESO cargaart;
     |ABRE #0;
     |LEE 000#0p;
     |PARA; |SI  FSalida = 0; |HACIENDO;
        |SI #0#1 ! 0;
            |NOME_DAT #99 "agifa019";
            |DFICB direc;
            diremp = #0#1;
            diremp = ("00000"+diremp) % -105;
            diremp = direc+diremp+"/";
            |PATH_DAT #99 diremp;
            |ABRE #9;
            |ABRE #99;
            desde = #0#0;
            |BUCLE agifa010;
            |CIERRA #9;
            |CIERRA #99;
        |FINSI;
        |LEE 000#0s;
     |SIGUE;
     |CIERRA #0;
|FPRC;

|PROCESO cargafam;
     |DELINDEX #33;

     |ABRE #34;
     |ABRE #33;
     |BUCLE agifa019;
     |CIERRA #34;
     |CIERRA #33;
|FPRC;


|PROCESO leecont;
     |ABRE #6;
     |DDEFECTO #6;
     #6#26=#0#2;
     |LEE 111#6=;
     |SI FSalida ! 0;
         #6#27 = "Albaranes Traspaso.";
         |GRABA 020#6n;
     |FINSI;
     |LIBERA #6;
     |CIERRA #6;
|FPRC;

|PROCESO leepro;
     |ABRE #7;
     #7#0 = #0#27;
     |LEE 111#7=;
     |LIBERA #7;
     |CIERRA #7;
|FPRC;

|PROCESO cambidir;
     |SI #0#1 = 0; |ACABA; |FINSI;
     |DFICB direc;
     diremp = #0#1;
     diremp = ("00000"+diremp) % -105;
     diremp = direc+diremp+"/";

     |PATH_DAT #3 diremp;
     |PATH_DAT #4 diremp;
     |PATH_DAT #5 diremp;
     |PATH_DAT #6 diremp;
     |PATH_DAT #7 diremp;
     |PATH_DAT #8 diremp;
     |PATH_DAT #9 diremp;
     |PATH_DAT #10 diremp;
     |PATH_DAT #11 diremp;
     |PATH_DAT #34 diremp;
||     |PATH_DAT #24 diremp;

     |ABRE #3;
     |ABRE #4;
     swdir=1;

     |HAZ leecont;
     |HAZ leepro;
|FPRC;

|PROCESO buscafam;
      |ABRE #33;
      #33#0 = #9#0;
      |LEE 101#33=;
      |SI FSalida ! 0; |LIBERA #33; |CIERRA #33; |ACABA; |FINSI;
      #9#2   = #33#1;
      #9#3   = #33#5;
      #9#125 = #33#7;
      #9#128 = #33#6;
      |ABRE #34;
      #34#0 = #33#1;
      |LEE 101#34=;
      |SI FSalida = 0; |LIBERA #33; |CIERRA #33; |LIBERA #34; |CIERRA #34; |ACABA; |FINSI;
      #34#0 = #33#1;
      #34#1 = #33#2;
      #34#2 = #33#3;
      #34#3 = #33#4;
      |GRABA 020#34n;
      |LIBERA #34;
      |CIERRA #34;
      |LIBERA #33;
      |CIERRA #33;
|FPRC;

|PROCESO mira_deci;
     |ABRE #10;
     |DDEFECTO #10;
     #10#0 = "A";
     |LEE 001#10=;
     n_dec = #10#8;
     n_pre = #10#9;
     codiBarra = #10#58;
     calpre = #10#60;
     |CIERRA #10;
|FPRC;

|PROCESO miraques;
     |SI #3#42 = AN;
         a = 1;
         operacion = operacion1;
     |SINO;
         a = -1;
         operacion = operacion2;
     |FINSI;
|FPRC;

|PROCESO altahisC;
     |HAZ miraques;

     |ABRE #11;
     |DDEFECTO #11;
     #11#0 = #4#2;
     #11#1 = #3#1;
     #11#2 = operacion;
     #11#3 = #4#0;
     #11#4 = #4#1;
     #11#5 = #4#3;         || almacen de entrada
     #11#6 = #4#5 * a;
     #11#7 = #9#6 + #11#6;
     #11#8 = #9#9;
     #11#9 = #4#6;
     #11#10 = #3#3;
     #11#11 = #4#27;
     #11#12 = #4#8;
     #11#13 = #4#26;
     #11#21 = #4#45;
     #11#14 = (((((#11#9 < #11#12) < #11#13) < #11#21) < #1#5) < #1#32);
     |SI #10#115 = "S";
          #11#14 = #11#14 < #1#7;
     |FINSI;
     |GRABA 121#11n;
     |LIBERA #11;
     |CIERRA #11;
|FPRC;

|PROCESO actuar;
     |HAZ mira_deci;

     mes = 0 +. 1;
     pe  = 0;
     de  = 0;
     pm  = 0;

     imneto = #4#9 < #3#5;
     imneto = imneto < #3#32;
     imneto = imneto < #3#7;

     |ABRE #7;
     #7#0 = #4#15;
     |LEE 101#7=;
     |SI FSalida ! 0;
         |DDEFECTO #7;
         #7#0= #4#15;
         |GRABA 021#7b;
         FSalida = 0;
     |FINSI;
     |SI FSalida = 0;
         fmes = #3#1 % A402;
         mes1 = fmes - 1;
         mes1 = mes1 + 27;
         |SI #3#42 = "N";
             #7mes1 = #7mes1 +. imneto;
             #7#39 = #7#39 +. imneto;
         |SINO;
             #7mes1 = #7mes1 -. imneto;
             #7#39 = #7#39 -. imneto;
         |FINSI;
         |GRABA 021#7a;
         |LIBERA #7;
     |FINSI;
     |CIERRA #7;

     |SI #3#42 = "N";
          enImpPrvCom = imneto;
     |SINO;
          enImpPrvCom = -imneto;
     |FINSI;
     efFecha = #3#1;
     eaProve = #3#3;
     |HAZ AcumulaPrv;

     |SI mes = 1;
         #3#15 = #3#15 +. #4#9;
         impret = #4#9 % #3#52;
         #3#53 = #3#53 +. impret;
     |FINSI;

     enForma = 1;
     eaDocu = "agifa077";
     |HAZ StockEntra;
|FPRC;

|PROCESO PasaACompra;
     |SALVA_DATOS #905;

     #905#0 = "AC";
     #905#1 = #4#27;
     #905#2 = #4#0;
     #905#3 = #4#1;
     |GRABA 020#905n;
     |LIBERA #905;

     enUnidades   = #905#6;
     eaTipo       = "AC";
     eaSerie      = #4#27;
     enCodigo     = #4#0;
     enLinea      = #4#1;
     efFecha      = #3#1;
     eaArticulo   = #4#2;
     enAlmacen    = #4#3;
     eaComponente = #905#4;
     efFecha      = #3#1;
     enCamBas     = #3#57;
     eaMoneda     = #3#58;
     enCamDiv     = #3#62;
     |SUB_EJECUTA_NP "dsz00002;ACC";
     |REPON_DATOS #905;
     |LEE 000#905=;
|FPRC;

|DEFBUCLE dsm00005;
     #905#1;
     ;
     "AV", #2#29, #2#0, nLinV, "      ";
     "AV", #2#29, #2#0, nLinV, "zzzzzz";
     ;
     NULL, PasaACompra;
|FIN;

|PROCESO prolive;
     |DDEFECTO #4;
     #4#27 = #0#2;
     #4#0  = albcomp;
     #4#1  = #2#1;
     |LEE 111#4=;
     salida = FSalida;
     |PARA d=2; |SI d [ 13; |HACIENDO d=d+1;
           #4d = #2d;
     |SIGUE;

     #4#15  = #0#27;
     #4#3   = #6#31;

     #4#30  = #2#36; ||PRECIO EN DIVISA
     #4#31  = #2#37; ||IMPORTE VISUAL.
     #4#32  = #2#38; ||PRECIO VISUAL.
     #4#33  = #2#39; ||IMPORTE VISUAL.

     #4#36  = #2#48; ||Uni2
     #4#37  = #2#49; ||Partida
     #4#38  = #2#50; ||Merma

     |SI salida = 0; |GRABA 020#4a; |FINSI;
     |SI salida ! 0; |GRABA 020#4n; |FINSI;
     nLinV = #2#1;
     |BUCLE dsm00005;

     |HAZ actuar;
|FPRC;

|PROCESO LeeMonBase; ||Lee los parametros de la moneda base
     |ABRE #agifa321;
     |ABRE #agifa324;
     |LEE 010#agifa321.p;   ||Leo parametros de divisa
     #agifa324#0 = #agifa321#9;
     |LEE 010#agifa324.=;         ||Leo la ficha de la moneda base en Divisas
     decim_base  = #agifa324#7;    ||Asigno los decimales de la moneda base
     precio_base = #agifa324#9;   ||Asigno los decimales del precio en la mon. base
     |CIERRA #agifa324;
     |CIERRA #agifa321;
|FPRC;

|PROCESO Param_Divisas; |TIPO 0;
     pintado     = 0;  ||Controla pintados de las lineas

     |ABRE #agifa324;
     #agifa324#0 = #agifa010#68;
     |LEE 001#agifa324.=;   || Leo la divisa
     |CIERRA #agifa324;

     decim_divisa  = #agifa324#7;  || Decimales de la divisa
     precio_divisa = #agifa324#9;  || Decimales en el precio de la divisa
     nDeci_Div     = decim_divisa;   || Decimales de la divisa
     nDeci_PreDiv  = precio_divisa;  || Decimales en el precio de la divisa
     |SI #agifa010#70 = "B";   || Si trabajo con la moneda base ...
         nDeci_PreVis    = precio_divisa;
         nDeci_ImpVis    = decim_divisa;
         nDeci_Base      = decim_base;
         nDeci_PreBase   = precio_base;
         nDeci_BaseMx    = decim_base;
         nDeci_PreBaseMx = precio_base;
         nDeci_TotVis    = decim_base;
         nDeci_TotNoVis  = decim_divisa;
     |SINO;     || Si trabajo con la divisa ...
         nDeci_PreVis    = precio_base;
         nDeci_ImpVis    = decim_base;
         nDeci_Base      = decim_base; || Max. precision en los decimales de la moneda base
         nDeci_PreBase   = precio_base;  || Max. precision en los dec. del precio de la moneda base
         |SI #agifa010#69 = 1;
              nDeci_BaseMx    = decim_base;    || sin triangulacion
              nDeci_PreBaseMx = precio_base;
         |SINO;
              nDeci_BaseMx    = #agifa321#10;  || con triangulacion
              nDeci_PreBaseMx = #agifa321#11;
         |FINSI;
         nDeci_TotVis    = decim_divisa;
         nDeci_TotNoVis  = decim_base;
     |FINSI;
     |HAZ Deci_IVA;
|FPRC;

|PROCESO contalco;
     enSwMenu = 1;
     #3#50 = #0#2; ||serie
     |HAZ ContaAC7;
     albcomp = #3#0;
|FPRC;

|PROCESO DatosEmpresa;
     |DFICE aDirEmp;            || tomo el directorio de la empresa
     |DFICB aDatos;             || tomo el directorio de los datos
     aDirEmp = aDirEmp % -205;  || me quedo con los 2 digitos de la empresa
     nCodEmp = aDirEmp;         || y los paso a una variable numerica
     |PATH_DAT #906 aDatos;      || pongo el path al fichero agifa041
     |ABRE #906;                 || abre el fichero agifa041
     #906#0 = nCodEmp;
     |LEE 020#906=;              || y busco la empresa en la que estoy
     |SI FSalida = 0;
          #3#29 = #906#1;        || nombre de la empresa
          #3#30 = #906#2;        || direccion de la empresa
          #3#31 = #906#3;        || poblacion de la empresa
          #3#33 = #906#4;        || provincia de la empresa
     |SINO;
          #3#29 = "";
          #3#30 = "";
          #3#31 = "";
          #3#33 = "";
     |FINSI;
     |CIERRA #99;               || cierro el fichero agifa041
|FPRC;


|PROCESO procave;
     |HAZ LeeMonBase;
     |HAZ Param_Divisas;
     #1#65="S";
     |GRABA 020#1a;

     |HAZ contalco;

     |DDEFECTO #3;
     #3#50 = #0#2;
     #3#0  = albcomp;
     |LEE 111#3=;
     salida = FSalida;

     |PARA d = 1; |SI d < 42; |HACIENDO d = d + 1;
          #3d = #1d;
     |SIGUE;

     |PARA d=42; |SI d<49; |HACIENDO d=d+1;
           j = d+1;
           #3d = #1j;
     |SIGUE;

     |HAZ DatosEmpresa;

     #3#49 = #1#54;
     #3#3  = #0#27;
     #3#4  = #7#2;

     #3#10 = "N";
     #3#38 = "N";
     #3#77 = "00.00.0000";
     #3#51 = "";
     #3#39 = "";
     #3#55 = #1#67;
     #3#56 = #1#68; ||DIVISA
     #3#57 = #1#69; ||CAMBIO
     #3#58 = #1#70; ||MONEDA DE TRABAJO
     #3#59 = #1#71; ||CAMBIO PACTADO A,F,O
     #3#60 = #1#72; ||FECHA PACTADA DE CAMBIO
     #3#61 = #1#73; ||MONEDA BASE
     #3#62 = #1#74; ||CAMBIO MONEDA BASE

     #3#63 = #1#75; ||TOTAL BRUTO EN DIVISA
     #3#64 = #1#76; ||TOTAL ALB. EN DIVISA
     #3#65 = #1#77; ||TOTAL BRUTO VIS.
     #3#66 = #1#78; ||TOTAL ALB. VIS.
     #3#67 = #1#79; ||TOTAL BRUTO NO VIS.
     #3#68 = #1#80; ||TOTAL ALB. NO VIS.
     #3#69 = #1#81; ||PORTES VIS.
     #3#70 = #1#82; ||VARIOS VIS.

     alfa  = (("00000") + #48#0) % -105;
     alfa  = alfa + #1#52 + #1#0;
     #3#54 = alfa;

     |SI salida = 0; |GRABA 020#3a; |FINSI;
     |SI salida ! 0; |GRABA 020#3n; |FINSI;
|FPRC;

|PROCESO FinCab;
     enForma = 1;
     eaDocu = "agifa080";
     |HAZ StockEntra;
|FPRC;

|DEFBUCLE agifa010T;
     #1#1;
     65;
     desde,      0, "N";
     desde, 999999, "N";
     be;
     NULL, procave, prolive, FinCab;
|FIN;

|PROCESO traspaso;
     |ABRE #0;
     |LEE 000#0p;
     |PARA; |SI  FSalida = 0; |HACIENDO;
              desde = #0#0;
              swdir = 0;
              |HAZ cambidir;
              |SI swdir ! 0;
                  |BUCLE agifa010T;
                  |CIERRA #3;
                  |CIERRA #4;
              |FINSI;
          |LEE 000#0s;
     |SIGUE;
|FPRC;

|RUTINA infc;
    #0#28 = 1;
|FRUT;

|RUTINA supc;
    #0#28 = 99;
|FRUT;

|PROGRAMA;
     |CLS;
     |CABEZA "Traspaso Alb.Ventas Alb.Compra.";
     |PINPA #0#0;
     |ENTLINEAL #1#0, -1 , 1 , 19 , 0 , infc , supc;
     |BLIN 24;
     |CONFI "2400SConforme (S/N).: ";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |DDEF aDef;
     aDef = aDef + "agifa019";
     |CARGA_DEF aDef, fanarti@;
     |SI FSalida = 0;
          |HAZ CreaTempo; aTmp33 = Tempo;
          |NOME_DAT #33 aTmp33; |DELINDEX #33;

          |HAZ cargafam;
          |HAZ cargaart;
          |HAZ traspaso;
          |LIBERA #0;
          |CIERRA #0;

          Tempo = aTmp33; |HAZ BoraTempo; |DELINDEX #33;
          |DESCARGA_DEF #fanarti@;
     |SINO;
          aDef = "0000ERROR. No puedo cargar: " + aDef;
          |MENAV aDef;
     |FINSI;
|FPRO;
