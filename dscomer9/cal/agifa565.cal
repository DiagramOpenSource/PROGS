|FICHEROS;
     agifa565 #0;
     agifa501 #1;
     agifa509 #2;   ||Resumen Parcial
     agifa505 #3;   ||Parametros Caja
     agifa510 #4;   ||Horarios
     agifa513 #5;   ||Cobros
     agifa512 #6;   ||Pagos
     agifa516 #516; ||Tarjeta
     agifa502 #502;
|FIN;

|VARIABLES;
     &enCobroTar = 0;
     &enCobro = 0;
     &eCaja    = 0;
     &eTienda  = 0;
     &eFecha   = "";

     aAlfa     = "";
     aTienda   = "";
     aDef      = "";
     fFecha    = @;
     aParam    = "";
     i = 0;
     tot_cobrado = 0;
     horat = 0;
     tmphora = "";
     esta = "N";
     contador = 0;
     inicio = 0;
     final = 0;
     periodo = 0;
     periodo1 = 0;
     puente    = "";
|FIN;

|PROCESO horario;
   periodo=0;
   |PARA contador = 1;|SI contador [ 19;|HACIENDO contador = contador + 2;
       esta = "N";
       inicio = contador;
       final  = contador +1;
       periodo= periodo +1;    || Contador para saber que periodo es

       tmphora = #1#34 % 102 + #1#34 % 402;
       horat = tmphora;

       |SI horat ] #4inicio;                 || Si es mayor que el inicio
           |SI horat [ #4final;              || Compruebo que ademas
                esta="S";
                contador= 20;               || Fuerzo salida del bucle
           |FINSI;
       |FINSI;
   |SIGUE;

     |SI esta = "S";
          periodo1 = periodo + 29;
          #2periodo1 = #2periodo1 + 1;
          periodo1 =periodo + 39;
          #2periodo1 = #2periodo1 + #502#21;
     |SINO;
     |FINSI;
|FPRC;


|DEFBUCLE horario;
     #agifa502#1;
     ;
     #agifa501#36, #agifa501#0, 001;
     #agifa501#36, #agifa501#0, 999;
     ;
     NULL, horario;
|FIN;

|PROCESO tiquet;
     fFecha = #1#14;
     |HAZ AltaCaja;

     #0#8 = #1#35;  ||Tipo
     #0#7 = #1#36;  ||serie
     #0#6 = #1#0;   ||registro
     #0#5 = #1#37;  ||documento

     |SI #1#35 = "T";
          |SI #2#109 = 0; |O #1#58 < #2#109;
               #2#109 = #1#58
          |FINSI;
          |SI #2#110 = 0; |O #1#58> #2#110;
               #2#110 = #1#58;
          |FINSI;
     |FINSI;


     |SI aParam = ""; |PINDA #0#0; |FINSI;

     |SI #1#31 = "V"; ||Vales;
          #2#84 = #2#84 + #1#66;
          |GRABA 020#2a;
          |LIBERA #2;
          |ACABA;
     |FINSI;
     |SI #1#31 = "R"; ||Roturas
          #2#67 = #2#67 + #1#9;
          |GRABA 020#2a;
          |LIBERA #2;
          |ACABA;
     |FINSI;
     |SI #1#31 = "A"; ||Invita Casa
          #2#68 = #2#68 + #1#63;
          |GRABA 020#2a;
          |LIBERA #2;
          |ACABA;
     |FINSI;
     |SI #1#31 = "L"; ||Invita Cliente
          #2#69 = #2#69 + #1#64;
          |GRABA 020#2a;
          |LIBERA #2;
          |ACABA;
     |FINSI;

     |SI #1#35 = "F"; |O #1#35 = "T";
          #2#11 = #2#11 + 1;  ||ventas unidades total
          #2#24 = #2#24 + #1#9;     ||Venta total Impuestos
          #2#25 = #2#25 + (#1#21 + #1#24 + #1#27);     ||Total Cuota iva
          #2#26 = #2#26 + (#1#22 + #1#25);             ||total iva rec
          #2#27 = #2#27 + (#1#20 + #1#23 + #1#26);     ||Venta total S/impuestos
          #2#28 = #2#28 + #1#28;   ||total descuentos
          |SI #3#163 = "S"; |O #1#9 > 0;
              #2#55 = #2#55 + #1#38 + #1#40 + #1#39;  ||Total caja, no cuento
          |FINSI;
     |FINSI;                                      ||porque no es dinero en caja

     |SI #3#163 = "S"; |O #1#9 > 0;
          #2#52 = #2#52 + #1#38;   ||Total efectivo
     |FINSI;
     #2#53 = #2#53 + #1#40;   ||Total tarjeta
     #2#54 = #2#54 + #1#39;   ||total cheque

     #2#57 = #2#57 + #1#15;   ||Restos no cobrados
     #2#58 = #2#58 + #1#41;   ||Cobrado En Credito

||     tot_cobrado = #1#38 + #1#40 + #1#39;
||     |SI tot_cobrado ! #1#9;       ||Se ha entregado menos cantidad
||          #2#57 = #2#57 + (tot_cobrado - #1#9);   ||Restos no cobrados
||     |FINSI;

     |SI #1#35 = "T";         ||Segun el tipo tengo que actualizar diferente
          #2#6 = #2#6 + 1;    ||Unidades Tiquet
          #2#15 = #2#15 + #1#9;
     |FINSI;
     |SI #1#35 = "A";
          #2#24 = #2#24 + #1#9;
          #2#25 = #2#25 + #1#4;
          #2#26 = #2#26 + (#1#22 + #1#25);
          #2#27 = #2#27 + (#1#20 + #1#23 + #1#26);
          #2#28 = #2#28 + #1#28;
          #2#29 = #2#29 + #1#3;    ||bruto
          #2#8 = #2#8 + 1;
          #2#17 = #2#17 + (#1#20 + #1#23 + #1#26);
     |FINSI;
     |SI #1#35 = "F";
          #2#7 =#2#7 + 1;
          #2#16 = #2#16 + #1#9;
     |FINSI;
     |SI #1#35 = "F"; |O #1#35 = "A"; |O #1#35 = "T";
          |HAZ horario;
          |BUCLE horario;
     |FINSI;

     |SI #1#40 ! 0;
          |DDEFECTO #516;
          #516#0 = #1#46;
          #516#1 = #1#14;
          |LEE 101#516=;
          |SI FSalida ! 0; |GRABA 020#516b; |FINSI;
          #516#2 = #516#2 + #1#40;
          |GRABA 020#516a;
          |LIBERA #516;
     |FINSI;

     |GRABA 020#2a;
     |LIBERA #2;
|FPRC;


|DEFBUCLE tiquet;
     #1#3;
     12;
     #0#3, "      ", " ", "     ",       0, #3#0;
     #0#12, "zzzzzz", "z", "zzzzz",  999999, #3#0;
     ;
     NULL, tiquet;
|FIN;

|PROCESO cobro;
     fFecha = #5#11;
     |HAZ AltaCaja;

     #0#8 = "C";        ||Tipo
     #0#7 = #5#14;      ||serie
     #0#6 = #5#0;       ||registro
     #0#5 = "      ";   ||documento

     |SI aParam = ""; |PINDA #0#0; |FINSI;

     |SI #5#12 = "E";
          #2#52 = #2#52 + #5#4;
     |FINSI;
     |SI #5#12 = "T";
          #2#53 = #2#53 + #5#4;
     |FINSI;
     |SI #5#12 = "C";
          #2#54 = #2#54 + #5#4;
     |FINSI;
     #2#4 = #2#4 + #5#4;
     #2#55 = #2#55 + #5#4;

     |GRABA 020#2a;
     |LIBERA #2;
|FPRC;

|DEFBUCLE cobro;
     #5#1;
     5,11;
     "     ",      0, #3#0, #0#3;
     "zzzzz", 999999, #3#0, #0#12;
     ;
     NULL, cobro;
|FIN;

|PROCESO pago;
     fFecha = #6#11;
     |HAZ AltaCaja;

     #0#8 = "P";        ||Tipo
     #0#7 = #6#12;      ||serie
     #0#6 = #6#0;       ||registro
     #0#5 = "      ";   ||documento

     |SI aParam = ""; |PINDA #0#0; |FINSI;

     #2#52 = #2#52 - #6#4;
     #2#5 = #2#5 + #6#4;
     #2#55 = #2#55 - #6#4;

     |GRABA 020#2a;
     |LIBERA #2;
|FPRC;

|DEFBUCLE pago;
     #6#1;
     5,11;
     "     ",      0, #3#0, #0#3;
     "zzzzz", 999999, #3#0, #0#12;
     ;
     NULL, pago;
|FIN;

|PROCESO AltaCaja;
     |DDEFECTO #2;
     #2#63 = #0#9;
     #2#0 = #3#0;
     #2#1 = fFecha;
     |LEE 101#2=;
     |SI FSalida ! 0;
          |GRABA 021#2b;
     |FINSI;
|FPRC;

|PROCESO Caja;
     |ABRE #1;
     |ABRE #2;

     |DDEFECTO #4;
     |ABRE #4;
     #4#0 = #3#18; ||cod hora;
     |LEE 000#4=;
     |CIERRA #4;

     |ABRE #516;
     |BUCLE tiquet;
     |CIERRA #516;
     |ABRE #5;
     |BUCLE cobro;
     |CIERRA #5;
     |ABRE #6;
     |BUCLE pago;
     |CIERRA #6;

     |HAZ EsSematel;
     |SI FSalida = 0;
          |HAZ CobroSematel;
     |FINSI;

     |CIERRA #2;
     |CIERRA #1;
|FPRC;

|DEFBUCLE Caja;
     #3#1;
     ;
     #0#0;
     #0#11;
     ;
     NULL, Caja;
|FIN;

|PROCESO BorraTarje;
     #516#2 = 0;
     |GRABA 020#516a;
     |LIBERA #516;
|FPRC;

|DEFBUCLE BorraTarje;
     #516#1;
     ;
     00, #0#3;
     99, #0#12;
     be;
     NULL, BorraTarje;
|FIN;

|PROCESO BorraResu;
     |PARA i = 3; |SI i [ 49; |HACIENDO i = i + 1;
          #2i = 0;
     |SIGUE;
     #2#52 = 0;
     #2#53 = 0;
     #2#54 = 0;
     #2#55 = 0;
     #2#57 = 0;
     #2#58 = 0;
     #2#67 = 0;
     #2#68 = 0;
     #2#69 = 0;
     #2#84 = 0;
     #2#109 = 0;
     #2#110 = 0;
     |GRABA 020#2a;
     |LIBERA #2;
|FPRC;

|DEFBUCLE BorraResu;
     #2#1;
     ;
     #0#9, #0#0, #0#3;
     #0#9, #0#11, #0#12;
     be;
     NULL, BorraResu;
|FIN;

|PROCESO Resumen;
     |SI #2#60 = "N";
          #2#61 = "00.00.0000";
     |FINSI;
     #2#55 = #2#55 - #2#108 + #2#2;
     |GRABA 021#2a;
     |LIBERA #2;
|FPRC;

|DEFBUCLE Resumen;
     #2#1;
     ;
     #0#9, #0#0, #0#3;
     #0#9, #0#11, #0#12;
     be;
     NULL, Resumen;
|FIN;

|PROCESO inicio; |TIPO 3;
     |SI #0#4 = "SI";
          |BUCLE BorraTarje;
          |BUCLE BorraResu;
          |BUCLE Caja;
          |BUCLE Resumen;
     |FINSI;
|FPRC;

|PROGRAMA;
     aParam = PARAMETRO;
     |SI aParam = "";
          |DFICE puente; |QBF puente;
          puente = puente % -205;
          #0#9 = puente;
          |CLS;
          |PINPA #0#0;
          |PINDA #0#0;
          |ENDATOS #1#0;
     |SINO;
          #0#9 = eTienda;
          #0#0 = eCaja;
          #0#3 = eFecha;
          #0#11 = #0#0;
          #0#12 = #0#3;

          #0#4 = "SI";
          |HAZ inicio;
     |FINSI;
|FPRO;

|PROCESO CobroSematel;
     |PARA; fFecha = #0#3; |SI fFecha [ #0#12; |HACIENDO fFecha = fFecha + 1;
          eTienda = #0#9;
          eCaja = #3#0;
          eFecha = fFecha;
          enCobro = 0;
          enCobroTar = 0;
          |SUB_EJECUTA_NP "tlfz0001;RECALCOBRO";
          |SI enCobro ! 0; |O enCobroTar ! 0;
               |HAZ AltaCaja;
               #2#4 = #2#4 + enCobro + enCobroTar;
               #2#55 = #2#55 + enCobro;
               #2#52 = #2#52 + enCobro;
               #2#53 = #2#53 + enCobroTar;
               |GRABA 020#2a;
               |LIBERA #2;
          |FINSI;
     |SIGUE;
|FPRC;
