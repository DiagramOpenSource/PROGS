|FICHEROS;
     dsadapta #0;
     dsadapt1 #3;
     origen@  #1;
     destin@  #2;
|FIN;

|VARIABLES;
     &eaPinta = "";
     &eaNomDef = "";
     &enCamposDef = 0;
     informe   = "dsadapta";
     aDefEmpre = "agifa041";
     aDefSerie = "agifa007";
     aBase     = "";
     aPath     = "";
     aEmpresa  = "";
     aMensa    = "";
     aDefOrg   = "";
     aDefDes   = "";
     aDatOrg   = "";
     aDatDes   = "";
     aLetra    = "";
     aFichero  = "";
     aDirec    = "";
     aEmpAntes = "";
     aDdx      = "";
     aDef      = "";
     aDat      = "";
     aPathOrg  = "";
     aPathDes  = "";
     aLon      = "";
     i         = 0;
     nEmpresa  = 0;
     nPosi     = 0;
     nBufer    = 0;
     nOrigen   = 0;
     nDestin   = 0;
     nCampo    = 0;
     nCampDes  = 0;
     nCampOrg  = 0;

     nCampoParche = 0;
     nLong        = 0;
     nCaracter    = 0;

     aLong        = "";
     aCaracter    = "";
     aCadena      = "";
     aAlfa        = "";
     aDes         = "";
     aOrg         = "";
     aInfOrg      = "";
     aInfDes      = "";
|FIN;

|PROCESO CopiaInf;
     |SI #0#14 ! "S"; |ACABA; |FINSI;
     |SI aFichero ! aDefSerie; |ACABA; |FINSI;

     aDes = aBase + "/def/";
     aOrg = #0#1; |QBF aOrg;
     aOrg = aOrg + "/def/";

     |PARA i = 0; |SI i [ nCampo; |HACIENDO i = i + 1;
          aAlfa = #1i;
          aLong = aAlfa % A0;
          nLong = aLong;
          |SI nLong = 8;      ||Es un Informe;
               |QBF aAlfa;
               aInfOrg = aOrg + aAlfa + ".inf";
               aInfDes = aDes + aAlfa + ".inf";
               |COPIA_FICHERO aInfOrg, aInfDes;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO CopiaEmpresa;
     |SI nCampOrg > nCampDes;
          nCampo = nCampDes;
     |SINO;
          nCampo = nCampOrg;
     |FINSI;
     nOrigen = 0;
     nDestin = 0;
     |SI #0#17 = "N"; |PINTA 2102, " Registro:             "; |FINSI;

     ||DELINDEX #2;
     |ABRE #1;
     |ABRE #2;
     |LEE 040#1p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          ||*****SUPONEMOS QUE EL CAMPO CLAVE DE LA EMPRESA ES EL 0*******
          |SI #1#0 ] #0#2; |Y #1#0 [ #0#3;
               |PARA i = 0; |SI i [ nCampo; |HACIENDO i = i + 1;
                    #2i = #1i;
               |SIGUE;
               |GRABA 040#2n;
               |SI FSalida = 0;
                    nDestin = nDestin + 1;
               |FINSI;
               nOrigen = nOrigen + 1;
               |SI #0#17 = "N"; |PINTA 2113, nOrigen; |FINSI;
          |FINSI;
          |LEE 040#1s;
     |SIGUE;
     |CIERRA #1;
     |CIERRA #2;
|FPRC;

|PROCESO Parche;
     |SI #0#18 = "N"; |ACABA; |FINSI;

     aAlfa   = #1nCampoParche;
     aCadena = "";
     aLong   = aAlfa % 0;
     nLong   = aLong;
     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPosi = (nCaracter * 100) + 1;
           aCaracter = aAlfa % nPosi;
           |SI aCaracter < "0";  |O aCaracter > "9";
               #3#0 = aCaracter;
               |LEE 040#3=;
               |SI FSalida = 0;
                   aCadena = aCadena + #3#1;
               |FINSI;
           |SINO;
               aCadena = aCadena + aCaracter;
           |FINSI;
     |SIGUE;
     #2nCampoParche = aCadena;
|FPRC;

|PROCESO Copia;
     |SI nCampOrg > nCampDes;
          nCampo = nCampDes;
     |SINO;
          nCampo = nCampOrg;
     |FINSI;
     nOrigen = 0;
     nDestin = 0;
     |SI #0#17 = "N"; |PINTA 2102, " Registro:             "; |FINSI;

     |DELINDEX #2;
     |ABRE #1;
     |ABRE #2;
     |LEE 040#1p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          |PARA i = 0; |SI i [ nCampo; |HACIENDO i = i + 1;
               #2i = #1i;
          |SIGUE;

          |SI aFichero = "agifa010";  nCampoParche = 0;  |HAZ Parche;  |FINSI;
          |SI aFichero = "agifa071";  nCampoParche = 0;  |HAZ Parche;  |FINSI;
          |SI aFichero = "agifa065";  nCampoParche = 3;  |HAZ Parche;  |FINSI;
          |SI aFichero = "agifa059";  nCampoParche = 2;  |HAZ Parche;  |FINSI;
          |SI aFichero = "agifa134";  nCampoParche = 0;  |HAZ Parche;  |FINSI;
          |SI aFichero = "agifa059";  nCampoParche = 0;  |HAZ Parche;  |FINSI;

          |SI aFichero = "agifa049";
               #2#12 = #1#4;
               #2#15 = #1#5;
          |FINSI;

          |GRABA 040#2n;
          |SI FSalida = 0;
               nDestin = nDestin + 1;
          |FINSI;
          nOrigen = nOrigen + 1;
          |LEE 040#1s;
          |SI #0#17 = "N"; |PINTA 2113, nOrigen; |FINSI;
     |SIGUE;
     |CIERRA #1;
     |CIERRA #2;
|FPRC;

|PROCESO BuscaCampos;
/*
     eaNomDef = aDat;
     |HAZ CamposDef;
     nCampo = enCamposDef;
*/
     nCampo = 0;
     |LEEDEDEF aDef, aDat;
     |SI FSalida = 0;
          aDdx = aDat + ".ddx";
          |LEESECUENCIAL aDdx, nBufer, nCampo, 0, 0;
          |FINDIM nBufer;
     |FINSI;
     nCampo = nCampo - 2;
|FPRC;

|PROCESO Adapta;
     |SI aEmpresa ! aEmpAntes;
          |SI aEmpAntes ! "";
               |PIEINF;
          |FINSI;
          aDirec = aBase + "fich/000" + aEmpresa;
          |MKDIR aDirec;
          aEmpAntes = aEmpresa;
     |FINSI;

     aMensa = (aPath + "                                     ") % 178;
     |SI #0#17 = "N"; |PINTA 2202, aMensa; |FINSI;

     aFichero = "";
     |PARA i = 1; |SI i [ 200; |HACIENDO i = i + 1;
          nPosi = (i * -100) - 1;
          aLetra = aPath % nPosi;
          |SI aLetra = "\"; |O aLetra = "/";
               |SAL;
          |FINSI;
          aFichero = aLetra + aFichero;
     |SIGUE;
     aFichero = aFichero - ".dat";

     aLon = aFichero % A0;
     |SI aLon > 8; |ACABA; |FINSI;

     #0#6 = aEmpresa;
     #0#7 = aFichero;
     #0#8 = 0;
     #0#9 = 0;
     #0#10 = 0;
     #0#11 = 0;
     #0#12 = "ERROR AL LEER DEF ORIGEN";

     aDefOrg = #0#1; |QBF aDefOrg;
     aDefOrg = aDefOrg + "/def/" + aFichero;
     |CARGA_DEF aDefOrg, origen@;
     |SI FSalida = 0;
          #0#12 = "NO EXISTE DEF DESTINO";
          |SI aFichero = "agifa148";
               aDefDes = aBase + "def/" + "agis0002";
          |SINO;
               aDefDes = aBase + "def/" + aFichero;
          |FINSI;
          |CARGA_DEF aDefDes, destin@;
          |SI FSalida = 0;
               aDatOrg = #0#1; |QBF aDatOrg;
               aDatOrg = aDatOrg + "/fich/" + aEmpresa + "/" + aFichero;
               aDef = aDefOrg;
               aDat = aDatOrg;
               |HAZ BuscaCampos;
               nCampOrg = nCampo;

               aPathOrg = #0#1; |QBF aPathOrg;
               aPathOrg = aPathOrg + "/fich/" + aEmpresa + "/";
               |PATH_DAT #1 aPathOrg;

               aDatDes = aBase + "fich/000" + aEmpresa + "/" + aFichero;
               aDef = aDefDes;
               aDat = aDatDes;
               |HAZ BuscaCampos;
               nCampDes = nCampo;

               aPathDes = aBase + "fich/000" + aEmpresa + "/";
               |PATH_DAT #2 aPathDes;

               #0#8 = nCampOrg;
               #0#9 = nCampDes;
               |SI nCampDes ] 0; |Y nCampOrg ] 0;
                    |HAZ Copia;
                    |HAZ CopiaInf;
                    #0#10 = nOrigen;
                    #0#11 = nDestin;
                    |SI #0#10 ! #0#11;
                         #0#12 = "Fichero Actualizado*";
                    |SINO;
                         #0#12 = "Fichero Actualizado";
                    |FINSI;
               |SINO;
                    #0#12 = "ERROR EN CAMPOS";
               |FINSI;
               |DESCARGA_DEF #destin@;
          |FINSI;
          |DESCARGA_DEF #origen@;
     |FINSI;
     |PRINT;
|FPRC;

|PROCESO FinalEmpresa;
     aPathOrg = #0#1; |QBF aPathOrg;
     aPathOrg = aPathOrg + "/fich/" + aEmpresa + "/";
     aPathDes = aBase + "fich/000" + aEmpresa + "/";
     eaPinta = #0#17;
     |SI #0#15 = "S";
          aAlfa = "dsz00003" + &59 + aPathDes;
          |SUB_EJECUTA_NP aAlfa;
     |FINSI;
     |SI #0#16 = "S";
          aAlfa = "dsz00040" + &59 + aPathDes;
          |SUB_EJECUTA_NP aAlfa;
     |FINSI;
     |SI #0#19 = "S";
          aAlfa = "dsw00029" + &59 + aPathDes;
          |SUB_EJECUTA_NP aAlfa;
     |FINSI;
     || Pone en campo  "A CTA" de facturas el total entregas de albaranes
     aAlfa = "dsz00125" + &59 + aPathDes;
     |SUB_EJECUTA_NP aAlfa;
|FPRC;

|PROCESO Inicio;
     aDefOrg = #0#1; |QBF aDefOrg;
     aDefOrg = aDefOrg + "/def/" + aDefEmpre;
     |CARGA_DEF aDefOrg, origen@;
     |SI FSalida = 0;
          aDefDes = aBase + "def/" + aDefEmpre;
          |CARGA_DEF aDefDes, destin@;
          |SI FSalida = 0;
               aDatOrg = #0#1; |QBF aDatOrg;
               aDatOrg = aDatOrg + "/fich/" + aDefEmpre;
               aDef = aDefOrg;
               aDat = aDatOrg;
               |HAZ BuscaCampos;
               nCampOrg = nCampo;

               aPathOrg = #0#1; |QBF aPathOrg;
               aPathOrg = aPathOrg + "/fich/";
               |PATH_DAT #1 aPathOrg;

               aDatDes = aBase + "fich/" + aDefEmpre;
               aDef = aDefDes;
               aDat = aDatDes;
               |HAZ BuscaCampos;
               nCampDes = nCampo;

               aPathDes = aBase + "fich/";
               |PATH_DAT #2 aPathDes;

               |SI nCampDes ] 0; |Y nCampOrg ] 0;
                    |HAZ CopiaEmpresa;
                    |PARA nEmpresa = #0#2; |SI nEmpresa [ #0#3;
                              |HACIENDO nEmpresa = nEmpresa + 1;
                         aEmpresa = ("00" + nEmpresa) % -102;
                         aPath = #0#1; |QBF aPath;
                         aPath = aPath + "/fich/" + aEmpresa + "/" + "*.dat";
                         |_PDIR aPath;
                         |PARA; |SI FSalida = 0; |HACIENDO;
                              |HAZ Adapta;
                              |_SDIR aPath;
                         |SIGUE;
                         |HAZ FinalEmpresa;
                    |SIGUE;
               |SINO;
                    |MENAV "0000ERROR EN CAMPOS EN EL FICHERO DE EMPRESA...";
               |FINSI;
          |SINO;
               |MENAV "0000ERROR AL LEER DEF EMPRESA DESTINO...";
          |FINSI;
     |SINO;
          |MENAV "0000ERROR AL LEER DEF EMPRESA ORIGEN...";
     |FINSI;
|FPRC;
------------------------------------------------------------------------
|PROGRAMA;
     |CLS;
     |CABEZA "Adaptacion ficheros";
     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 021#0b; |FINSI;
     #0#4 = "NO";
     #0#14 = "N";
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #2#0;
     |SI FSalida = 0; |Y #0#4 = "SI";
          |GRABA 021#0a;
          |DBASE aBase; |QBF aBase;
          |IMPRE 0;
          |SI FSalida = 0;
               |INFOR informe;
               |SI FSalida = 0;
                    |ABRE #3;
                    |HAZ Inicio;
                    |CIERRA #3;
                    |PIEINF;
                    |FININF;
               |SINO;
                    |MENAV "0000Falta informe 'dsadapta'....";
               |FINSI;
               |FINIMP;
          |FINSI;
     |FINSI;
     |CIERRA #0;
|FPRO;

|RUTINA ClaveBaja;
     #3#0 = " ";
|FRUT;

|RUTINA ClaveAlta;
     #3#0 = "z";
|FRUT;

|PROCESO TeclaOculta;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     |PUSHV 0501 2380;
     |ENTLINEAL #1#3, 1, 1, 17, 1, ClaveBaja, ClaveAlta;
     |POPV;

     |ERROR;
|FPRC;
