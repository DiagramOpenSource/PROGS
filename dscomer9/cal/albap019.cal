|FICHEROS;
albap019 #0;  ||Parte Carga
agifa019 #1;  ||Articulos
agifa017 #2;  ||Datos Articulo/Almacen
agifa027 #4;  ||Contadores
albap004 #5;  ||Cabecera Documentos Psion
albap005 #6;  ||Lineas Documentos Psion
tmpsi019 #7;  ||Temporal Listado Psion019
tmpum019 #8;  ||Temporal Agrupacion Por Unidad De Medida
agifa065 #9;  ||Historico
|FIN;

|VARIABLES;
ncontador = 0;
operacion1= "MS";
operacion2= "ME";
clave = "movimiento";
mascara = "0000000";
tempo = " ";
filtro = "-106";
codigo = "";
fmes = "";
mes = 0;
sto = 0;
|FIN;

|CAMPOS;
#4#1 contador;
|FIN;

|CALCULO movim;
|CAMPO_MODIFICA #0#9 , 1 , "S";
|CAMPO_MODIFICA #0#10 , 1 , "S";
|CAMPO_MODIFICA #0#11 , 1 , "S";
|SI #0#8 = "NO";
  |CAMPO_MODIFICA #0#9 , 1 , "N";
  |CAMPO_MODIFICA #0#10 , 1 , "N";
  |CAMPO_MODIFICA #0#11 , 1 , "N";
|FINSI;
|FCAL;

|CALCULO alma;|TIPO 0;
|SI #0#9 = #0#10;|Y #0#8 = "SI";
  |MENAV "0400El Almacen De Salida NO Puede Ser Igual Al Almacen De Entrada";
  |ERROR;
|FINSI;
|FCAL;

|CALCULO listado;
|PRINT;
|SI #0#8 = "SI";         ||Genera Movimientos
  |HAZ cuenta;
  |HAZ artic;
|FINSI;
|ABRE #1;
#1#0 = #7#0;
|LEE 000#1=;
|LIBERA #1;
|CIERRA #1;
|ABRE #8;                ||Grabacion Totales Por Unidades De Carga
#8#0 = #1#5;
|LEE 000#8=;
|SI FSalida ! 0;
  |GRABA 000#8c;
  |LIBERA #8;
|FINSI;
#8#1 = #8#1 + #7#2;
|GRABA 000#8a;
|LIBERA #8;
|CIERRA #8;
#8#1 = 0;
|FCAL;

|DEFBUCLE lee_car;
#7#1;
;
"             ";
"zzzzzzzzzzzzz";
be;
NULL,listado
|FIN;

|CALCULO lee_lin;
#7#0 = #6#4;
|LEE 000#7=;
|SI FSalida ! 0;
  |GRABA 000#7c;
  |LIBERA #7;
|FINSI;
#7#1 = #6#5;
#7#2 = #7#2 + #6#6;
|GRABA 000#7a;
|LIBERA #7;

#7#0 = "";
#7#1 = "";
#7#2 = 0;
|FCAL;

|CALCULO pasa_cab;
|BUCLELIN 2lee_lin#5;
|FCAL;

|DEFBUCLE lee_cab;
#5#1;
3;
#0#4 , #0#0 , #0#2 , #0#6;
#0#5 , #0#1 , #0#3 , #0#7;
be;
NULL,pasa_cab;
|FIN;

|CALCULO inicio;|TIPO 3;
|DELINDEX #8;
|DELINDEX #7;
|ABRE #7;

|SI #0#12 = "SI";
  |BUCLE lee_cab;           ||Grabacion Temporal Y Movimientos

  |IMPRE 1;
  |INFOR "albap019";
  |BUCLE lee_car;           ||Listado Cargas.
  |PIEINF;
  |FININF;
  |FINIMP;
|FINSI;

|LIBERA #7;
|CIERRA #7;
|FCAL;

|CALCULO artic;
|ABRE #1;
#1#0 = #7#0;
|LEE 120#1=;
|SI 0 = FSalida;
  |ABRE #2;
  #2#0 = #7#0;
  #2#1 = #0#9;
  |LEE 120#2=;
  |SI 0 = FSalida;
     #2#39 = #2#39 - #7#2;
     #2#5 = #2#5 - #7#2;
     sto = #2#5 - #2#4;
     |SI sto > 0;
        #2#3 = sto * #1#9;
     |SINO;
        #2#3 = 0;
     |FINSI;
     #0#3 = #2#39;
     fmes = #0#11 % 402;
     mes = fmes - 1;
     mes = mes * 2;
     mes = mes + 11;

     #2mes = #2mes + #7#2;
     #2#35 = #2#35 + #7#2;

     |GRABA 020#2a;
     |LIBERA #2;
     #2#0 = #7#0;
     #2#1 = #0#10;
     |LEE 121#2=;
     |SI 0 = FSalida;
        #2#39 = #2#39 + #7#2;
        #2#5 = #2#5 + #7#2;
        sto = #2#5 - #2#4;
        |SI sto > 0;
           #2#3 = sto * #1#9;
        |SINO;
           #2#3 = 0;
        |FINSI;

        fmes = #0#11 % 402;
        mes  = fmes - 1;
        mes = mes * 2;
        mes = mes + 12;
        #2mes = #2mes + #7#2;
        #2#36 = #2#36 + #7#2;

        |GRABA 020#2a;

        |HAZ altahis;
     |FINSI;
|FINSI;
|LIBERA #2;
|CIERRA #2;
|FINSI;
|LIBERA #1;
|CIERRA #1;
|FCAL;

|CALCULO cuenta; |TIPO 0;
|ABRE #4;
|SI 0 ! FSalida;
   |INDEXA #4;
   |CIERRA #4;
   |ABRE #4;
|FINSI;
#4#0 = clave;
|LEE 140#4=;
|SI FSalida ! 0;
   #4#0 = clave;
   #4#1 = 1;
   |GRABA 020#4b;
|FINSI;
|SI contador ] 999999;
   contador = 1;
|FINSI;
ncontador = contador;
tempo = mascara + ncontador;
codigo = tempo % filtro;
contador = contador + 1;
|GRABA 020#4a;
|LIBERA #4;
|CIERRA #4;
|FCAL;

|CALCULO altahis;|TIPO 0;
|ABRE #9;
|DDEFECTO #9;
#9#0 = #7#0;
#9#1 = #0#11;
#9#2 = operacion1;
#9#3 = codigo;
#9#4 = 1;
#9#5 = #0#9;        || almacen salida
#9#6 = #7#2 * -1 ;
#9#7 = #1#6;
#9#8 = #1#9;
#9#9 = 0;
|GRABA 121#9n;

#9#0 = #7#0;
#9#1 = #0#11;
#9#2 = operacion2;
#9#3 = codigo;
#9#4 = 1;
#9#5 = #0#10;        || almacen entrada
#9#6 = #7#2;
#9#7 = #1#6;
#9#8 = #1#9;
#9#9 = 0;
|GRABA 121#9n;
|LIBERA #9;
|CIERRA #9;
|FCAL;
