|FICHEROS;
     albad039 #0; || agif9117
     agifa010 #1;
     albad037 #2; || agif9114
     albad040 #3,MANTE; || tem91171
     agifa024 #4;
     albad038 #5; || agif9174
     albad041 #6,MANTE; || tempfact
     agifa142 #142;
|FIN;

|VARIABLES;
     Comodin = "";
     aLong = "";
     nLong = 0;
     Calc = 0;
     Parte1 = "";
     Parte2 = "";
     pro = "";
     {-1}menu = "";
     menu1 = "2400";
     menu2 = "1";
     menu3 = "Entrada:  ";
     menu4 = "AM";
     menu5 = " Automatica ";
     menu6 = " Manual ";

     lin = 0;
     fmes = "";
     mes = 0;
     nombre = "";
     salmenu = 0;
|FIN;

|PROCESO Cuenta;
   |ABRE #142; |LEE 000#142p; |CIERRA #142;
   Comodin = #3Campo; |QBF Comodin;
   Comodin = Comodin - ".";
   |SI FSalida ! 0;
      aLong = Comodin % 0; nLong = aLong;
      Calc = ((FSalida / 100) ! 0) + 99;
      Parte1 = Comodin % Calc;
      Parte2 = Comodin - Parte1;
      Calc = #142#11 - nLong;
      Comodin = "0" * Calc;
      Comodin = Parte1 + Comodin + Parte2;
      #3Campo = Comodin;
      |PINTA #3Campo;
   |FINSI;
|FPRC;


|PROCESO entra;|TIPO 13;
     |SI FSalida = 9;
          |HAZ funcion11;
     |FINSI;
     |SI FSalida = 10;
          |HAZ funcion12;
     |FINSI;
|FPRC;

|PROCESO consulta;
|SI pro = "M"; |ACABA; |FINSI;
|SI FSalida = 0;    ||SE ha pulsado Return
     |LEE 101#0=;        ||Leo el registro actual corriente bloqueando
     |HAZ tmp117;
     |GRABA 021#0a;
     |LIBERA #0;
|FINSI;
|FPRC;

|PROCESO tmp117;
     |DDEFECTO #3;
     #3#0 = #0#4;        ||importe
     #3#1 = #0#14;       ||Importe Pendiente
     #3#2 = #0#14;       ||Importe A cobrar
     #3#5 = "N";
     #3#7 = #0#5;        ||Fecha de Cobro
     #3#6 = #0#7;
     #3#9 = #0#15;       ||Tipo de Recibo
     |PUSHV 0501 2380;
     |BLANCO 0742 1880;
     |ATRI R;
     |PINDA #0#0;
     |PINPA #0#3;
     |PINDA #0#3;
     |ENDATOS #1#3;
     |SI FSalida = 0;
          #0#5 = #3#7;
          #0#7 = #3#6;
          #0#9 = #3#8;
          #0#10 = #3#5;
          #0#11 = #3#2;
          #0#12 = #3#3;
          #0#15 = #3#9;       ||Tipo de recibo
     |FINSI;
     |ATRI 0;
     |POPV;
     |PINDA #0#0;
|FPRC;

|PROCESO funcion11;      ||Se consultan facturas
|PUSHV 0501 2380;
|LEE 001#0c;
|ABRE #1;
#1#52 = #0#8;  ||serie albaran
#1#0 = #0#0;
|LEE 001#1=;
|SI FSalida ! 0;
     |MENAV "0000No se ha encontrado la Factura";
|SINO;
     |PINPA #0#1;
     |PINDA #0#1;
     |PAUSA;
     ||ENDATOS #4#1;
|FINSI;
|CIERRA #1;
|POPV;
|FPRC;

|PROCESO funcion12;
|PUSHV 0501 2380;
|ABRE #2;
#2#17 = #0#8;
#2#0 = #0#0;
#2#1 = #0#1;
|LEE 001#2=;
|SI FSalida ! 0;
     |MENAV "0000No se ha encontrado el Recibo";
|SINO;
     |PINPA #0#2;
     |PINDA #0#2;
     |PAUSA;
     ||ENDATOS #4#2;
|FINSI;
|CIERRA #2;
|POPV;
|FPRC;

|PROCESO act;
     |SI #0#7 = "s";
          #0#7 = "S";
     |FINSI;
     |SI #0#7 = "n";
          #0#7 = "N";
     |FINSI;
     |PINTA #0#7;
|FPRC;

|PROCESO reci;
     |SI pro = "A"; |ACABA; |FINSI;
     |ABRE #2;
     #2#17 = #0#8;
     #2#0 = #0#0;
     #2#1 = #0#1;
     |LEE 001#2=;
     |SI FSalida = 0;
          |SI #2#13 = "S";
               |MENAV "0000Este Recibo ya esta Cobrado";
               |CIERRA #2;
               |ERROR;
               |ACABA;
          |FINSI;
     |SINO;
          |MENAV "0000Recibo Inexistente";
          |CIERRA #2;
          |ERROR;
          |ACABA;
     |FINSI;
     #0#2 = #2#2;
     #0#3 = #2#3;
     #0#4 = #2#9;
     #0#14 = #2#9 - #2#19;
     #0#13 = #2#7;
     |PINDA #0#0;
     |HAZ tmp117;
     |CIERRA #2;
|FPRC;

|PROCESO copia;
 #4#0 = #2#2;
 |LEE 000#4=;
 |LIBERA #4;
 |SI FSalida = 0;
   |SI #4#41 ] #6#0;|Y #4#41 [ #6#1;
    |SI #4#20 ] #6#2;|Y #4#20 [ #6#3;
     |DDEFECTO #0;
     lin = lin + 1;
     #0#6 = lin;    ||Clave linea
     #0#0 = #2#0;   ||factura
     #0#1 = #2#1;   ||recibo
     #0#2 = #2#2;   ||cliente
     #0#3 = #2#3;   ||proveedor
     #0#4 = #2#9;   ||Importe
     #0#5 = #2#8;   ||Fecha del vencimiento
     #0#7 = "N";
     #0#8 = #2#17;
     #0#9 = #2#18;
     #0#11 = #2#19;
     #0#13 = #2#7;       ||Fecha Emision
     #0#14 = #2#9 - #2#19;
     |GRABA 021#0n;
     |SI lin = 999; |ERROR10; |FINSI;
    |FINSI;
   |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE leer;
#2#2;
;
"     " , 000000,   0 , "N" , " " , "N";
"zzzzz" , 999999, 999 , "N" , "z" , "N";
e;
NULL , copia;
|FIN;

|PROCESO autom;
     |ABRE #0;
     |ABRE #4;
     |BUCLE leer;
     |CIERRA #0;
     |CIERRA #4;
     |BLIN 4;
     |SI lin = 0;
          |MENAV "0000***** NO hay Recibos Pendientes de COBRO *****";
          |ACABA;
     |FINSI;
     |PINPA #0#0;
     |ENTLINEAL #1#0 , 1 , 4 , 20 , 0 , pro1 , pro2;
|FPRC;

|PROCESO pro1;
     #0#6 = 1;
|FPRC;

|PROCESO pro2;
     #0#6 = 999;
|FPRC;

|PROCESO manual;
     |ENTLINEAL #1#0 , 1 , 1 , 20 , 1 , pro1 , pro2;
|FPRC;

|PROCESO actua_cli;
   |ABRE #2;
   #2#17 = #0#8;
   #2#0 = #0#0;
   #2#1 = #0#1;
   |LEE 121#2=;
   |SI FSalida = 0;
       |ABRE #4;
       #4#0 = #0#2;
       |LEE 141#4=;
       |SI FSalida = 0;
           |SI #2#13 = AN;         || ya esta cobrado
               |SI #2#14 = AS;     || si ya esta impagado se paga
                       #2#14 = AN;
                       fmes = #2#8 % A402;
                       mes = fmes - 1;
                       mes = mes * 4;
                       mes = mes + 54;
                       mes = mes + 3;
                       #4mes = #4mes - #0#11;
                       #4#49 = #4#49 - #0#11;
                       #4#105 = #4#105 - #0#11;
                       #4#51 = #4#51 - #0#11;
               |SINO;
                       #4#49 = #4#49 - #0#11;
               |FINSI;
               #2#19 = #2#19 + #0#11;
               |SI #2#9 ] 0;
                    |SI #2#9 [ #2#19;
                         #2#13 = AS; ||Cobrado
                    |FINSI;
               |SINO;         || Negativo
                    |SI #2#9 ] #2#19;
                         #2#13 = AS; ||Cobrado
                    |FINSI;
               |FINSI;
               #2#16 = #0#5;              || fecha cobro
               #2#18 = #0#9;   ||  Cuenta de ingreso.
               |HAZ cobro_par;
               |GRABA 121#4a;
               |GRABA 121#2a;
               |LIBERA #4;
               |LIBERA #2;
           |FINSI;
      |FINSI;
      |CIERRA #4;
   |FINSI;
   |CIERRA #2;
|FPRC;

|DEFBUCLE actual;
#0#2;
;
"            " , "S";
"zzzzzzzzzzzz" , "S";
e;
NULL , actua_cli;
|FIN;

|PROCESO actu;
     |BUCLE actual;
|FPRC;

|PROCESO cobro_par;
     |ABRE #5;
     #5#0 = #0#8;
     #5#1 = #0#0;
     #5#2 = #0#1;
     #5#3 = 999;
     |LEE 001#5];
     |SI FSalida = 0;
          |LEE 001#5a;
     |SINO;
          |LEE 001#5u;
     |FINSI;
     |SI FSalida = 0; |Y #5#0 = #0#8; |Y #5#1 = #0#0; |Y #5#2 = #0#1;
          lin = #5#3 + 1;
          |DDEFECTO #5;
          #5#0 = #0#8; #5#1 = #0#0; #5#2 = #0#1; #5#3 = lin; |GRABA 021#5b;
     |SINO;
          |DDEFECTO #5;
          #5#0 = #0#8; #5#1 = #0#0; #5#2 = #0#1; #5#3 = 1; |GRABA 021#5b;
     |FINSI;
          #5#16 = #0#13; ||Fecha Emision
          #5#4 = #0#2;   ||Cliente
          #5#5 = #0#9;   ||Cuenta Ingreso
          #5#6 = #0#5;   ||Fecha Cobro
          #5#7 = #0#4;   ||Importe del Recibo
          #5#8 = #2#19;  ||Importe cobrado
          #5#9 = #0#11;  ||Importe Cobrado
          #5#10 = "N";   ||Contabilizado
          #5#11 = #0#12; ||Gastos Negociacion
          #5#18 = #0#15; ||Tipo de Recibo
          |GRABA 021#5a;
          |LIBERA #5;
     |CIERRA #5;
|FPRC;

|PROCESO Tipo12; |TIPO 12;
|FPRC;

|PROGRAMA;
     |CLS;
     |CABEZA "Control Cobros";
     |PINPA #0#0;
     nombre = "cc" + Usuario;
     |QBT nombre;
     |NOME_DAT #0 nombre;
     |ABRE #0; |CIERRA #0; |DELINDEX #0;
     |MENU menu;
     salmenu = FSalida;
     |SI salmenu = 1;
          pro = "A";
          |PINPA #0#6;
          |PINDA #0#6;
          |ENDATOS #1#6;
          |MENSA "0000Pulse ESPACIO para detener el Proceso";
          |HAZ autom;
     |FINSI;
     |SI salmenu = 2;
          pro = "M";
          |HAZ manual;
     |FINSI;
     |SI salmenu = 1; |O salmenu = 2;
          |CONFI "2400SSon Correctos los valores Entrados : ";
          |SI FSalida = 0;
               |HAZ actu;
          |FINSI;
     |FINSI;
     |DELINDEX #0;
|FPRO;
