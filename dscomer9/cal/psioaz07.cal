|FICHEROS;
     psioaz07 #99;  || Este def.
     psioa001 #0;   || Fichero de control de parametros para el pase.
     psioa075 #75;
     psioa076 #76;
     agifa142 #142;
|FIN;

|VARIABLES;
     aAux = "";
     aCampos = "";
     aPath = "";
     aLon = "";
     nPos = 0;
     aEjecuta = "";
     aFinReg = "";
     aParam = "";
     campo = "";
     variable = "";
     aAlfa = "";
     aIP = "";
     aOrigen = "";
     aDestino = "";
     aFicDes = "";
     aFicOrg = "";
     aBase = "";
     {1}aLocal = "";
     {1}aConte = "";
     aVer1 = "";
     aVer2 = "";
     aExe = "";
     i = 0;
     aCar = "";
     empresa = "";
     tab = "";
     nHandle = 0;
     nPsion = 0;
|FIN;

|PROCESO Nombre1; |TIPO 0;
     |SI #99#2 = "N";
          |ABRE #0;
          #0#0 = #99Campo;
          |LEE 000#0=;
          |SI FSalida ! 0;
               |MENAV "0000No existe código...";
               |ERROR;
          |FINSI;
          |CIERRA #0;
     |FINSI;
|FPRC;

|PROCESO Nombre2; |TIPO 0;
     |SI #99#2 = "S";
          |ABRE #0;
          #0#0 = #99Campo;
          |LEE 000#0=;
          |SI FSalida ! 0; |Y #99Campo ! "          ";
               |MENAV "0000No existe código...";
               |ERROR;
          |FINSI;
          |CIERRA #0;
     |FINSI;
|FPRC;

|PROCESO Ansi2Utf8;
     |QUE_SISTEMA aAlfa;
     |SI #0#59 = 1; |O #0#74 = "S"; |O aAlfa = "ESDOS";
          |DFICE aOrigen; aOrigen = aOrigen + Usuario;
          |SI #0#74 = "C";
               |ENVIA_FICHERO aOrigen, aFicDes;
          |SINO;
               |COPIA_FICHERO aOrigen, aFicDes;
          |FINSI;
          |FBORRA aOrigen;
     |SINO;
          aIP = "";
          |IP_REMOTO aIP;
          |DBASE aBase;
          |ESPECIFICA "RBASS", aLocal;

          aOrigen = aBase + "00psion/ansitoutf8.exe"
          aDestino = aLocal + "/bin/ansitoutf8.exe";
          aConte = aOrigen;
          |ESPECIFICA "MD5", aConte;
          aVer1 = FSalida;
          aConte = aDestino;
          |ESPECIFICA "REMOTOMD5", aConte;
          aVer2 = FSalida;
          |SI aVer1 ! aVer2;
               |SI aIP ! "";
                    |ENVIA_FICHERO aOrigen, aDestino;
               |SINO;
                    |COPIA_FICHERO aOrigen, aDestino;
               |FINSI;
          |FINSI;

          |DFICE aOrigen; aOrigen = aOrigen + Usuario;
          |ENVIA_FICHERO aOrigen, aFicOrg;

          aAlfa = aDestino + " 4 " + aFicOrg + " " + aFicDes;
          aLon = aAlfa % A0;
          aExe = "";
          |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
               nPos = i * 100 + 1;
               aCar = aAlfa % nPos;
               |SI aCar = "/"; aCar = "\"; |FINSI;
               aExe = aExe + aCar;
          |SIGUE;

          aExe = "cmd " + &34 + "/c START /MIN /WAIT " + aExe + &34;
          |RSYSTEM aExe;
          |RFBORRA aFicOrg;
     |FINSI;
|FPRC;

|PROCESO psioa075;
     variable = #75#13 + " " + #75#14;
     |PINTA 1922,"                                                         ";
     |PINTA 1922, "SEGUIMIENTO:";
     |PINTA 1936,variable;

     aAlfa = "|NC";
     aCampos = #75aAlfa;

     variable = "";
     |PARA i = 0; |SI i < aCampos; |HACIENDO i = i + 1;
          aAux = #75i;
          |QBF aAux;
          |SI i = 0;
               variable = aAux;
          |SINO;
               variable = tab + aAux;
          |FINSI;
          |XGRABA nHandle, variable;
     |SIGUE;

     |XGRABA nHandle, aFinReg;
|FPRC;

|DEFBUCLE psioa075;
     #75#1;
     ;
     0000, #0#77;
     9999, #0#77;
     ;
     NULL, psioa075;
|FIN;

|PROCESO Seguimientos;
     empresa = #0#29;
     |QBF empresa;
     aFicOrg = empresa + "seguimiento.stxt";
     aFicDes = empresa + "seguimiento.asc";
     |DFICE empresa; empresa = empresa + Usuario;

     |XABRE "WB", empresa, nHandle;
     |SI FSalida ] 0;
          |BUCLE psioa075;
          |XCIERRA nHandle;
     |SINO;
          aAlfa = "0000Error al crear:" + empresa;
          |SI aParam = ""; |MENAV aAlfa; |FINSI;
     |FINSI;
|FPRC;

|PROCESO psioa076;
     variable = #76#0 + " " + #76#13;
     |PINTA 1922,"                                                         ";
     |PINTA 1922, "POTENCIALES:";
     |PINTA 1936,variable;

     aAlfa = "|NC";
     aCampos = #76aAlfa;

     variable = "";
     |PARA i = 0; |SI i < aCampos; |HACIENDO i = i + 1;
          aAux = #76i;
          |QBF aAux;
          |SI i = 0;
               variable = aAux;
          |SINO;
               variable = tab + aAux;
          |FINSI;
          |XGRABA nHandle, variable;
     |SIGUE;

     |XGRABA nHandle, aFinReg;
|FPRC;

|DEFBUCLE psioa076;
     #76#1;
     ;
     "      ", #0#77;
     "zzzzzz", #0#77;
     ;
     NULL, psioa076;
|FIN;

|PROCESO Potenciales;
     empresa = #0#29;
     |QBF empresa;
     aFicOrg = empresa + "potenciales.txt";
     aFicDes = empresa + "potenciales.asc";
     |DFICE empresa; empresa = empresa + Usuario;

     |XABRE "WB", empresa, nHandle;
     |SI FSalida ] 0;
          |BUCLE psioa076;
          |XCIERRA nHandle;
     |SINO;
          aAlfa = "0000Error al crear:" + empresa;
          |SI aParam = ""; |MENAV aAlfa; |FINSI;
     |FINSI;
|FPRC;

|CALCULO inicio;
     tab = &9;
     |HAZ Potenciales; |HAZ Ansi2Utf8;
     |HAZ Seguimientos; |HAZ Ansi2Utf8;

     aPath = #0#29; |QBF aPath;
     aLon = aPath % A0;
     nPos = 100 + (aLon - 1);
     aPath = aPath % nPos;
     |SI #99#2 = "S";
          aEjecuta = #0#78; |QBF aEjecuta;
     |SINO;
          aEjecuta = #99#13; |QBF aEjecuta;
     |FINSI;
     |SI aEjecuta ! "";
          aEjecuta = aEjecuta + " " + #0#77; |QBF aEjecuta;
          aEjecuta = aEjecuta + " " + aPath;
          |SI #0#79 = "S";
               |SYSTEM aEjecuta;
          |SINO;
               |RSYSTEM aEjecuta;
          |FINSI;
     |FINSI;
|FCAL;

||------------------------- RUTINA PRINCIPAL --------------------------------
|PROCESO pasa_psion; |TIPO 3;
     aFinReg = &13;
     aFinReg = aFinReg + &10;

     |SI #99#1 = "SI";|O #99#1 = "si";
         |HAZ DecimalesBase;
         |SI #99#2 = "S";|O #99#2 = "s";
             |PARA nPsion = 3;|SI nPsion [ 12;|HACIENDO nPsion = nPsion + 1;
                   |SI #99nPsion = "";|O #99nPsion = "          ";
                       |SAL;
                   |FINSI;
                   |ABRE #0;
                   #0#0 = #99nPsion;
                   |LEE 000#0=;
                   |SI FSalida = 0;
                       |HAZ inicio;
                   |FINSI;
                   |CIERRA #0;
             |SIGUE;
         |SINO;
             |ABRE #0;
             #0#0 = #99#0;
             |LEE 020#0=;
             |SI FSalida = 0;
                 |HAZ inicio;
             |FINSI;
             |CIERRA #0;
         |FINSI;
         |SI aParam = ""; |MENAV "0000FIN DE PROCESOS"; |FINSI;
     |FINSI;
|FPRC;

|CALCULO varios;
     |SI #99#2 = "N";
          |PARA i = 3;|SI i [ 12;|HACIENDO i = i + 1;
               |C_M #99i , 1 , "N";
          |SIGUE;
          |C_M #99#0 , 1 , "S";
          |C_M #99#13, 1, "S";
     |SINO;
          |PARA i = 3;|SI i [ 12;|HACIENDO i = i + 1;
               |C_M #99i , 1 , "S";
          |SIGUE;
          |C_M #99#0 , 1 , "N";
          |C_M #99#13, 1, "N";
          #99#13 = ""; |PINTA #99#13;
     |FINSI;
|FCAL;

|PROGRAMA;
     aParam = PARAMETRO;

     |ABRE #142; |LEE 000#142p; |CIERRA #142;

     |ABRE #99;
     |LEE 101#99p;
     |SI FSalida ! 0; |GRABA 020#99b; |FINSI;


     |SI aParam = "";
          #99#1 = "NO";
          |PINPA #0#99;
          |PINDA #0#99;
          |ENDATOS #1#99;
          |SI FSalida = 0; |GRABA 020#99a; |FINSI;
     |SINO;
          #99#0 = aParam;
          #99#1 = "SI";
          |HAZ pasa_psion;
     |FINSI;

     |CIERRA #99;
|FPRO;
