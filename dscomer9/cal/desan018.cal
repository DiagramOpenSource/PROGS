|FICHEROS;
     desan018 #0;
     desan019 #1,MANTE;
     agifa019 #19;
     agifa060 #60;
|FIN;

|VARIABLES;
     nHay = 0;
     aAlfa = "";
     fFecha = @;
     aIP = "";
     aServidor = "";
     aFrom = "";
     aTo = "";
     aSubject = "";
     aMensaje = "";
     aDjunto = "";
     aPath = "";
|FIN;

|PROCESO Envialos;
     aIP = "";
     |SI #1#2 = "L";
          |IP_REMOTO aIP;
     |SINO;
          |SI #1#2 = "S";
               |IP_LOCAL aIP;
          |SINO;
               aIP = #1#3; |QBF aIP;
          |FINSI;
     |FINSI;
     aServidor = #1#1; |QBF aServidor;
     aFrom = #1#4; |QBF aFrom;
     aTo = #0#2; |QBF aTo;
     fFecha = ~;
     aSubject = "Informe artículos bajo mínimo " + fFecha;
     aMensaje = "Adjunto informe artículos bajo mínimo " + fFecha;
     |DSCORREO_ENVIA aIP, aServidor, aFrom, aTo, aSubject, aMensaje, aDjunto;
|FPRC;

|DEFBUCLE Envialos;
     #0#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Envialos;
|FIN;

|PROCESO agifa019;
     |SI #19#104 < #19#7; |Y #19#7 > 0;
          |DDEFECTO #60;
          #60#0 = #19#2;
          |LEE 000#60=;

          |PRINT;
          nHay = 1;
     |FINSI;
|FPRC;

|DEFBUCLE agifa019;
     #19#7;
     ;
     INICIO;
     FINAL;
     ;
     NULL, agifa019;
|FIN;

|PROCESO Envia;
     nHay = 0;
     |DFICE aAlfa;
     aDjunto = aAlfa + "minimos.txt";
     Impresora = aDjunto;
     |IMPRE -77;
     |SI FSalida = 0;
          |INFOR "desan018";
          |SI FSalida = 0;
               |ABRE #60;
               |BUCLE agifa019;
               |CIERRA #60;
               |PIEINF;
               |FININF;
          |FINSI;
          |FINIMP;
     |FINSI;

     |SI nHay ! 0;
          |ABRE #1; |LEE 000#1p; |CIERRA #1;
          |BUCLE Envialos;
     |FINSI;
     |FBORRA aDjunto;
|FPRC;

|PROCESO DSCorreo; |TIPO 0;
     |QUE_SISTEMA aAlfa;
     |SI aAlfa = "ESBSD";|Y #1#2 = "S";
          |MENAV "    Configuracion no permitida, servidor UNIX. Recomendado: [F]";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO SoloFija; |TIPO 7;
     |SI #1#2 = "F";
          aAlfa = #1#3; |QBF aAlfa;
          |SI aAlfa = "Automatico";
               #1#3 = "";
               |PINTA #1#3;
          |FINSI;
          |C_M #1#3, 1, "S";
     |SINO;
          |C_M #1#3, 1, "N";
          #1#3 = "Automatico";
          |PINTA #1#3;
     |FINSI;
|FPRC;


|PROCESO T12; |TIPO 12;
|FPRC;

|PROCESO Min;
     #0#0 = 1;
|FPRC;

|PROCESO Max;
     #0#0 = 999;
|FPRC;

|PROGRAMA;
     |SI PARAMETRO = "menu";
          |CLS;
          |ABRE #1;
          |LEE 101#1p;
          |SI FSalida ! 0; |GRABA 020#1b; |FINSI;
          |PINPA #0#1;
          |PINDA #0#1;
          |ENTLINEAL #1#0, 1, 7, 22, 0, Min, Max;
          |ENDATOS #1#1;
          |SI FSalida = 0; |GRABA 020#1a; |FINSI;
          |CIERRA #1;
          |ENTLINEAL #1#0, 1, 1, 22, 0, Min, Max;
          |CONFI "0000NEmitir informe bajo minimos?";
          |SI FSalida = 0;
               |HAZ Envia;
          |FINSI;
     |SINO;
          |HAZ Envia;
     |FINSI;
|FPRO;
