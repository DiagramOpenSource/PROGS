|FICHEROS;
     mecaz008 #0;
     mecam009 #9;
     mecam011 #11;
     mecam012 #12;
     mecaw013 #13,MANTE; ||Tmp
     agifa017 #17;
     agifa019 #19;
     dsm00024 #24;
     agifa065 #65;
|FIN;

|VARIABLES;
     &Tempo = "";
     nHay = 0;
     nUni = 0;
     nForma = 0;
     aPartida = "";
     {-1}Menu = "";
     Menu1 = "2400";
     Menu2 = "1";
     Menu3 = "Opciones:";
     Menu4 = "RAC";
     Menu5 = " Repasar ";
     Menu6 = " Aceptar ";
     Menu7 = " Cancelar ";
     nReser = 0;
     nDispo = 0;
     nTotal = 0;
|FIN;

|PROCESO Historico;
     |DDEFECTO #65;
     #65#0 = #13#1;
     #65#1 = ~;
     #65#2 = "SF";
     #65#3 = #0#1;
     #65#4 = 1;
     #65#11 = #0#0;
     |GRABA 000#65b;
     |PARA; |SI FSalida ! 0; |HACIENDO;
          #65#4 = #65#4 + 1;
          |GRABA 000#65b;
     |SIGUE;

     #65#5 = #13#2;
     #65#6 = -#13#6;
     #65#7 = #17#5;
     #65#8 = #19#9;
     #65#9 = 0;
     #65#10 = "000000";
     #65#18 = #13#8;
     |GRABA 020#65a;
     |LIBERA #65;
|FPRC;

|PROCESO Stock;
     #19#0 = #13#1;
     |LEE 101#19=;
     |SI FSalida = 0;
          #19#12 = #19#12 + nReser;
          #19#6 = #19#6 + nTotal;
          #19#104 = #19#104 + nDispo;

          #17#0 = #13#1;
          #17#1 = #13#2;
          |LEE 101#17=;
          |SI FSalida = 0;
               #17#8 = #17#8 + nReser;
               #17#5 = #17#5 + nTotal;
               #17#39 = #17#39 + nDispo;
               |SI aPartida = "S";
                    #24#0 = #13#1;
                    #24#1 = #13#2;
                    #24#2 = #13#8;
                    |LEE 101#24=;
                    |SI FSalida = 0;
                         #24#11 = #24#11 - nTotal;
                         #24#22 = #24#22 - nTotal;
                         #24#24 = #24#24 + nReser;
                         |HAZ ValoraStock;
                         |GRABA 020#24a;
                    |FINSI;
               |FINSI;
               |HAZ ValoraStock;
               |GRABA 020#17a;
          |SINO;
               |HAZ ValoraStock;
          |FINSI;
          |GRABA 020#19a;
     |FINSI;
|FPRC;

|PROCESO Tmp;
     #11#0 = #0#0;
     #11#1 = #0#1;
     #11#2 = #13#0;
     #11#3 = #13#1;
     #11#5 = #13#3;
     |LEE 121#11=;
     |SI FSalida = 0;
          #11#8 = #13#6;
          #11#9 = "S";
          |GRABA 020#11a;
          |LIBERA #11;
     |FINSI;

     |SI #13#4 = "S";    || con partidas
          #12#0 = #0#0;
          #12#1 = #0#1;
          #12#2 = #13#0;
          #12#3 = #13#1;
          #12#6 = #13#8;
          #12#5 = #13#3;
          |LEE 121#12=;
          |SI FSalida = 0;
               #12#9 = #13#6;
               #12#10 = "S";
               |GRABA 020#12a;
               |LIBERA #12;
          |FINSI;
     |FINSI;

     aPartida = #13#4;
     nReser = -#13#5;
     nDispo = (#13#5 - #13#6);
     nTotal = -#13#6;
     |HAZ Stock;

     |HAZ Historico;
|FPRC;

|DEFBUCLE Tmp;
     #13#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tmp;
|FIN;

|PROCESO Actualiza;
     |ABRE #19;
     |ABRE #17;
     |ABRE #11;
     |ABRE #12;
     |ABRE #24;
     |ABRE #65;
     |BUCLE Tmp;
     |CIERRA #65;
     |CIERRA #24;
     |CIERRA #12;
     |CIERRA #11;
     |CIERRA #17;
     |CIERRA #19;
|FPRC;

|PROCESO Tecla; |TIPO 0;
     |SI #13Campo = Contenido; |Y FSalida = 0;
          |ENCAMPO #6#13;
          |SI FSalida = 0;
               |GRABA 020#13a;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO mecam012;
     |DDEFECTO #13;
     #13#0 = #12#2;
     #13#1 = #12#3;
     #13#2 = #12#4;
     #13#3 = #12#5;
     #13#4 = "S";
     #13#5 = #12#7;
     #13#6 = #12#7;
     |SI #12#10 = "S";
          #13#6 = #12#9;
     |FINSI;

     |DDEFECTO #19;
     #19#0 = #13#1;
     |LEE 000#19=;

     #13#7 = #19#1;
     #13#8 = #12#6;
     |GRABA 020#13n;
     nHay = 1;
|FPRC;

|DEFBUCLE mecam012;
     #12#1;
     10;
     #11#0, #11#1, #11#2, #11#3, "               ", #11#5, "N";
     #11#0, #11#1, #11#2, #11#3, "zzzzzzzzzzzzzzz", #11#5, "N";
     ;
     NULL, mecam012;
|FIN;

|PROCESO mecam011;
     |SI #11#6 = "S";
          |BUCLE mecam012;
     |SINO;
          |DDEFECTO #13;
          #13#0 = #11#2;
          #13#1 = #11#3;
          #13#2 = #11#4;
          #13#3 = #11#5;
          #13#4 = "N";
          #13#5 = #11#7;
          #13#6 = #11#7;
          |SI #11#9 = "S";
               #13#6 = #11#8;
          |FINSI;

          |DDEFECTO #19;
          #19#0 = #13#1;
          |LEE 000#19=;
          #13#7 = #19#1;
          #13#8 = "";
          |GRABA 020#13n;
          nHay = 1;
     |FINSI;
|FPRC;

|DEFBUCLE mecam011;
     #11#1;
     9;
     #0#0, #0#1, "                    ", "                    ", " ", "N";
     #0#0, #0#1, "zzzzzzzzzzzzzzzzzzzz", "zzzzzzzzzzzzzzzzzzzz", "z", "N";
     ;
     NULL, mecam011;
|FIN;

|PROCESO Min;
     #13#0 = " " * 20;
     #13#1 = " " * 20;
     #13#8 = " " * 15;
     #13#3 = " ";
|FPRC

|PROCESO Max;
     #13#0 = "z" * 20;
     #13#1 = "z" * 20;
     #13#8 = "z" * 15;
     #13#3 = "z";
|FPRC;

|PROCESO DefeConsu; |TIPO 7;
     |SI #13#6 = 0;
          #13#6 = #13#5;
     |FINSI;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |ABRE #9;
     #9#0 = #0#0;
     #9#1 = #0#1;
     |LEE 101#9=;
     |SI FSalida ! 0;
          |MENAV "0000No existe Orden";
          |ACABA;
     |FINSI;

     |HAZ CreaTempo; |NOME_DAT #13 Tempo; |DELINDEX #13;
     |ABRE #13;
     |ABRE #19;
     |BUCLE mecam011;
     |CIERRA #19;
     |CIERRA #13;
     |SI nHay = 0;
          |MENAV "0000No hay registros";
     |SINO;
          |PARA; |SI; |HACIENDO;
               |ENTLINEAL #1#13, 1, 4, 20, 0, Min, Max;
               |MENU Menu;
               |SI FSalida = 0; |O FSalida = 3; |SAL; |FINSI;
               |SI FSalida = 2;
                    |HAZ Actualiza;
                    |SAL;
               |FINSI;
          |SIGUE;
     |FINSI;

     |HAZ BoraTempo; |DELINDEX #13;
     |CIERRA #9;
|FPRC;

|PROGRAMA;
     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENTLINEAL #1#13, 1, 7, 20, 0, Min, Max;
     |ENDATOS #1#0;
|FPRO;
