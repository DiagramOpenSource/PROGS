|FICHEROS;
     malpe087 #0;
     agifa024 #1;
     malpe088 #3;
     agifa038 #13;
     agifa019 #19;
     agifa007 #40;
     agifa142 #41;  || Parametros Generales
     agifa027 #42;  || Contador.
     agis0002 #90;  || Recibos adelantados.
     agifa321 #35;  || Parametros de Divisas
     agifa328 #36;  || Clientes/Divisas
     agifa329 #37;  || Lineas de Clientes/Divisas
     agifa324 #38;  || Divisas
     agifa204 #204;
     dsm00113 #113;
     dsm00117 #117;
     dsm00246 #140; ||Control Riesgo Por Usuarios.
     dsm00247 #141; ||Pantalla Informe Riesgo.
     dsm00248 #143; ||Auditoria.

     malpe081 #81;
     malpe137 #137;
     malpe184 #184;
     malpe186 #186,MANTE;
     malpe185 #185;
     malpe001 #39;
     referen@;
|FIN;

|VARIABLES;
     &enObra = 0;
     aSinError = "";
     aDef = "";
     aSql = "";
     nSal = 0;
     nConta = 0;
     aCam68a = "";
     aCam69a = "";
     aCam70a = "";
     aCam68b = "";
     aCam69b = "";
     aCam70b = "";
     nErrClave = 0;
     i = 0;
     nPendiente = 0;
     nPendiente2= 0;
     nNume11    = 0;
     nModo      = 0;
     nNume7     = 0;
     nCalc      = 0;
     nConAu     = 0;
     aUs        = "";
     &nMasRies  = 0;
     nTries     = 0;
     nAudito    = 0;
     &nRieTotal = 0;
     &nRieUtili = 0;
     &aRieBloqu = "";
     &aBloquead = "";
     &eaTag     = "";
     nMult      = 0;
     aProg      = "";
     nForma     = 0;
     &nImpo     = 0;
     &cli       = "";
     nPosi      = 0;
     nfecha     = 0;
     nTecla     = 0;
     nSwVersion = 0;

     &enSw      = 0;

     &nDeci_Base     = 0;   || Decimales en Totales de la moneda base
     &nDeci_PreBase  = 0;   || Decimales en el Precio en moneda base
     &nDeci_BaseMxP  = 0;   || Decimales en Totales de la moneda base (Maxima precision)
     &nDeci_PreBasMxP= 0;   || Decimales en el Precio en moneda base (Maxima precision)
     &nDeci_DivP     = 0;   || Decimales en Totales de la divisa
     &nDeci_PreDivP  = 0;   || Decimales en el Precio en divisa
     &nDeci_TotVisP  = 0;   || Decimales en Totales visibles (el de la moneda de trabajo)
     &nDeci_TotNoVisP = 0;  || Decimales en Totales no visibles (el de la otra moneda)
     &nDeci_PreVisP  = 0;   || Decimales en el Precio visual. (lineas)
     &nDeci_ImpVisP  = 0;   || Decimales en el Importe visual. (lineas)
     &nDeci_PreVis  = 0;    || para agifa023
     &nDeci_ImpVis  = 0;    ||
     &ac_divisa = "N";   || Vale "S" cuando las divisas en ventas estan
                         || activadas y la serie es de divisas
     &pintado = 0;       || Sirve para controlar los pintados de las lineas
     &decim_divisa = 0;  || Decimales de la divisa
     &precio_divisa = 0; || Decimales del precio en divisa
     &decim_base = 0;    || Decimales de la moneda base
     &precio_base = 0;   || Decimales del precio en moneda base
     div_act = 0;        || Divisa actualizada. Me sirve en un PARA para que
                         || no salga hasta que este actualizada (div_act=1)
     &cli_divisa = "";   || Codigo de Cliente. Se pasa a CLIENTES/DIVISAS
     &divisa = "";       || Codigo de Divisa. Se pasa a ACTUAL. de DIVISAS
     nModo104 = 0;
     sw_reserva     = 0;
     alfa           = "";
     contador       = 0;
     cont           = "";
     &ext1          = "";
     &ser_ped       = "";
     &num_ped       = 0;
     &bl            = "                              ";
     dto_prom       = "N";
     def_exp        = "N";
     avisa_st       = "N";
     recoge         = "N";    || Lo utilizo en agifa073
     fmes           = "  ";
     mensaje10      = "0000El Pedido ya esta Servido en parte o totalmente";
     mensaje11      = "0423Actualizando linea:";
     mensaje1       = "2400NDesea imprimir el recibo? ";
     informe        = "";
     puente         = "";
     &cant          = 0;
     &tolin         = 0;
     n_dec          = 0;
     n_pre          = 0;
     def_alm        = 0;
     con            = 0;
     indtope        = 0;
     ind            = 0;
     mes            = 0;
     control        = 0;
     modo           = 0;
     nEntrada       = 0;
     aAlfa          = "";
     || Variables de comunicacion con agifa218
     &aSeriePedido  = "";
     &aNumeroPedido = 0;
     &SPedido       = "";
     &NPedido       = 0;
     &BAdela        = 0;
     &TipoCta       = "P";
     &ser           = "";
     &doc           = 0;
     &eaCodigo      = "";
     &eaTipoCodigo  = "CL";

     || MENU PARA INTRODUCIR EL CODIGO DEL CLIENTE DE FORMA AUTOMATICA.
 {-1}menu           = "";
     menu1          = "2400";
     menu2          = "1";
     menu3          = "Contador:  ";
     menu4          = "AM";
     menu5          = " Automatico ";
     menu6          = " Manual ";
     so             = "";
     no             = 0;
     ser_a          = "";
     num_a          = 0;
     lin_a          = 0;
     art_a          = "";
     uni_a          = 0;
     serie          = "";
     numero         = 0;
     caltot         = 0;
     recalpre       = 0;
     nDirEnt        = 0;
     nImpDiv        = 0;
     nPend          = 0;
 {-1}aMenu          = "";
     aMenu1         = "2400";
     aMenu2         = "1";
     aMenu3         = "Opciones: ";
     aMenu4         = "IOn";
     aMenu5         = " Imprimir ";
     aMenu6         = " Obser. Contrato ";
     aMenu7         = " Incidencias Obra ";
|FIN;

|INCLUYE i_varart;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO Entra104; |TIPO 7;
     |HAZ ControlUsuPV;
     |SI enErr ! 0; |PTEC 807; |ACABA; |FINSI;
|FPRC;

|PROCESO Promo104_7; |TIPO 7;
     |SI #41#129 = "S";
          |C_M #0#56, 1, "S";
     |FINSI;
|FPRC;

|PROCESO Promo104_0; |TIPO 0;
     |SI #0#56 ! 0;
          |ABRE #117;
          #117#0 = #0#56;
          |LEE 000#117=;
          |SI FSalida ! 0;
               |MENAV "0000Promocion Inexistente...";
               |ERROR;
          |FINSI;
          |CIERRA #117;
     |FINSI;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO pedcon; |TIPO 6;
     con = (FEntrada / 100) ! 0;
     |HAZ PinRemanente;
     |SI con ! 1; |Y #0Campo ! Contenido;
          |SI con ! 4;
               |SI #0#24 > 0;
                    ||#0Campo = Contenido;
                    ||PINTA #0Campo;
                    |MENAV mensaje10;
                    |ERROR; nError6 = 1;
               |SINO;
                    control = 1;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO ModiFec104;
     nPendiente = #3#5 - #3#43;
     nPendiente2 = #3#44 - #3#49;
     |SI nPendiente > 0; |O nPendiente2 > 0;
          #3#13 = #0#2;
          |GRABA 020#3a;
          |LIBERA #3;
     |FINSI;
|FPRC;

|PROCESO Fecha104; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 2; |Y #0Campo ! Contenido;
          |CONFI "0000NModificar fecha entrega de lineas ";
          |SI FSalida = 0;
               |BUCLELIN 0ModiFec104 #0;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO altac; |TIPO 6;
     alfa = #0#4; |QBF alfa;
     |SI alfa ! "";
          |ABRE #1;
          #1#0 = #0#4;
          |LEE 000#1=;
          |SI FSalida ! 0; |Y #41#1 = "S";
               |HAZ Menus;
          |FINSI;
          |CIERRA #1;
     |FINSI;
|FPRC;

|PROCESO mirar_d; |TIPO 6;
     control = 0;
     nModo = (FEntrada / 100) ! 0;

     |ABRE #41; |LEE 001#41p; |CIERRA #41;
     n_dec = #41#8;
     n_pre = #41#9;
     dto_prom = #41#18;
     avisa_st = #41#35;
     |ABRE #agifa007;
     #agifa007#26 = #0#25;
     |LEE 001#agifa007.=;
     |CIERRA #agifa007;
     def_alm = #agifa007#29;
     def_exp = #agifa007#30;
     recoge  = #agifa142#38;
     |HAZ LeeMonBase;
     |HAZ LeeParamDiv;
|FPRC;

|PROCESO DirEntrega;
     con = (FEntrada / 100) ! 0;
     |SI con = 1;
          |ABRE #1;
          #1#0 = #0#4;
          |LEE 000#1=;
          |CIERRA #1;
          #0#7 = #1#25;
     |FINSI;      || Cargo el Representante
     |SI nTecla = 11; |O #41#102 = "S"; |O #41#102 = "X";
          eaCodigo = #0#4;
          eaFuerza = "N";
          |SI nTecla = 11; eaFuerza = "S"; |FINSI;
          |SUB_EJECUTA_NP "dsz99994;alquelotoquelecortolosguevos";
          |SI #41#102 = "N"; |Y con ! 2; |ERROR;  |FINSI;
     |FINSI;
|FPRC;


|PROCESO Cliente6; |TIPO 6;
     nDirEnt = 0;
     nTecla = FSalida;
     nError6 = 0;

     |HAZ mirar_d;
     |HAZ altac;
     |HAZ DirEntrega;

     |HAZ Extracto;
     |HAZ pedcon;
     |HAZ alriesgo;

     |SI nError6 ! 0; |ERROR; |FINSI;
|FPRC;

|PROCESO ser55; |TIPO 0;
     cli = #0#4;
     ser = #0#25;
     |SUB_EJECUTA_NP "agifv011.tab;por_ejemplo"
|FPRC;

|PROCESO alriesgo; |TIPO 0;
     ||컴컴컴컴컴컴컴컴컴컴컴컴컴 Observaciones Clientes.
     |SI #agifa142#14 = "S";
          puente = #agifa024#53;     |QBF puente;
          |SI puente ! "";
               |PUSHV 1008 1370;
               |SI #agifa142#20 = "S"; |AVISO; |FINSI;
               |BLANCO 1008 1370;
               |CUADRO 1008 1370;
               |ATRI R;
               |PINTA 1110 , #agifa024#53;
               |ATRI 0;
               |PAUSA;
               |POPV;
          |FINSI;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴컴컴컴컴 Control Riesgo Cliente.
     nMasRies   = 0;
     eaSerie = #0#25;
     eaTag = "PE";  |HAZ SacaRiesgoMalpe;
     |SI #agifa142#166 = "S"; |Y eaExisteCliRie = "S";
          eaDocu = "C";
          eaTipo = "P";
          enImpoDoc = #0#11;
          ||eaMuestra =
          |HAZ ControlRiesgo;
          |SI enErrRie ! 0; |ERROR; nError6 = 1; |ACABA; |FINSI;
          |SI enAudito = 0;
               |ABRE #143;
               |LEE 000#143u;
               |SI FSalida = 0;
                    nConAu = #143#0 + 1;
               |SINO;
                    nConAu = 1;
               |FINSI;
               |DDEFECTO #143;
               #143#0 = nConAu;
               #143#12 = "PEDIDO";
               #143#1 = #0#25;
               #143#2 = #0#0;
               #143#3 = #0#1;
               #143#4 = #0#4;
               #143#5 = #0#8;
               #143#6 = Usuario % 810;
               #143#7 = #141#0;
               #143#8 = #141#1;
               #143#9 = #141#2;
               #143#10 = #141#3;
               #143#11 = #141#4;
               |GRABA 020#143n;
               |CIERRA #143;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Cliente0; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;

     |SI #0Campo ! Contenido; |O nModo = 1;
          |HAZ DefDirEnt;
     |FINSI;

     |HAZ CambioDivisa;
     |HAZ ser55;
     |HAZ caltotal;

     eaCliente = #0#4;
     eaSerie = #0#25;
     eaTipo = "P";
     |HAZ ExisteCliRie;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO comp_res;
     |SI #3#30 ! "      ";
          sw_reserva = 1; |ERROR10;
     |FINSI;
     |SI #3#12 ! 0;  |O #3#43 ! 0; |O #3#47 ! 0; |O #3#49 ! 0;
          sw_reserva = 2; |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE mira_res;
     #3#1;
     ;
     #0#25,#0#0,001;
     #0#25,#0#0,999;
     ;
     NULL,comp_res;
|FIN;

|PROCESO CorrigeVari;
     #3#12 = 0;
     #3#49 = 0;
     |GRABA 020#3a;
     |LIBERA #3;
|FPRC;

|DEFBUCLE CorrigeVari;
     #3#1;
     ;
     #0#25,#0#0,001;
     #0#25,#0#0,999;
     be;
     NULL, CorrigeVari;
|FIN;

|PROCESO CalcConIva;
     |ABRE #dsm00113;
     #dsm00113#0 = #0#4;
     #dsm00113#1 = #3#14;
     |LEE 000#dsm00113.=;
     |SI FSalida = 0;
        nCalc = #3#11 % #dsm00113#3;
        |SI #agifa024#47 = "S";
           nCalc = nCalc + (#3#11 % #dsm00113#4);
        |FINSI;
     |SINO;
        enTiva = #3#14;
        efFiva = #0#1;
        |HAZ SacaIVA;
        nCalc = #3#11 % enIva;
        |SI #agifa024#47 = "S";
              nCalc = nCalc + (#3#11 % enRec);
        |FINSI;
     |FINSI;
     |CIERRA #dsm00113;

     nImpo = nImpo + (#3#11 + nCalc);
|FPRC;

|DEFBUCLE CalcConIva;
#3#1;
;
#0#25, #0#0,001;
#0#25, #0#0,999;
;
NULL,CalcConIva;
|FIN;

|PROCESO ActCli;
     |DDEFECTO #19;
     #19#0 = #3#2;
     |LEE 000#19=;

     #1#0 = #0#4;
     |LEE 101#1=;
     |SI FSalida = 0;
          |SI #19#194 = 2;
               nImpo = ((((#3#44 * #3#6) < #3#8) < #3#29) < #0#5) < #0#17;
          |SINO;
               nImpo = ((((#3#5 * #3#6) < #3#8) < #3#29) < #0#5) < #0#17;
          |FINSI;
          |SI #41#115 = "S";
               nImpo = nImpo < #0#6;
          |FINSI;
          nImpo = nImpo * nForma;
          fmes = #0#1 % A402;
          mes = fmes - 1;
          mes = mes * 3;
          mes = mes + 114;
          #1mes = #1mes   + nImpo;     || VENTA.
          #1#150 = #1#150 + nImpo;
          |GRABA 020#1a;

          eaCliente = #malpe087#4;
          efFecha = #malpe087#1;
          enImpCliPed = nImpo;
          |HAZ AcumulaCli;
     |FINSI;
|FPRC;

|PROCESO BorraObsCto;
     |BORRA 020#186a;
|FPRC;

|DEFBUCLE BorraObsCto;
     #186#1;
     ;
     #0#25, #0#0, 001;
     #0#25, #0#0, 999;
     ;
     NULL, BorraObsCto;
|FIN;

|PROCESO pedaccli; |TIPO 2;
     |HAZ ControlUsuPV;
     |SI enErr ! 0; |ERROR; |ACABA; |FINSI;

     con = (FEntrada / 100) ! 0;
     nModo = con;
     nForma = 0 +. 1;
     |SI nModo ! 1; |HAZ Clave87; |FINSI;
     |SI nErrClave ! 0; |ERROR; |ACABA; |FINSI;

     |SI nForma = 1;
          aCam68a = #0#68;
          aCam69a = #0#69;
          aCam70a = #0#70;
     |SINO;
          aCam68b = #0#68;
          aCam69b = #0#69;
          aCam70b = #0#70;
     |FINSI;


     |SI con = 2;
          |SI nForma = 1; |O #0#24 = 0;
               |C_M #0#1, 1, "S";
               |C_M #0#7, 1, "S";
               |C_M #0#35, 1, "S";
               |C_M #0#36, 1, "S";
               |C_M #0#37, 1, "S";
               |C_M #0#5, 1, "S";
               |C_M #0#17, 1, "S";
          |SINO;
               |C_M #0#1, 1, "N";
               |C_M #0#7, 1, "N";
               |C_M #0#35, 1, "N";
               |C_M #0#36, 1, "N";
               |C_M #0#37, 1, "N";
               |C_M #0#5, 1, "N";
               |C_M #0#17, 1, "N";
          |FINSI;
     |FINSI;
     |SI con = 2;|O con = 3;
          |SI #0#27 = "S";
               |MENAV "0000Pedido no modificable. Esta exportado.";
               |ERROR; |ACABA;
          |FINSI;
     |FINSI;
     |SI con = 3;|Y #0#24 > 0;
          |MENAV mensaje10;
          |ERROR; |ACABA;
     |FINSI;
     |SI con = 3;                  || BAJA.
          sw_reserva = 0;
          |BUCLE mira_res;
          |SI sw_reserva = 1;
               |MENAV "0000EL PEDIDO TIENE LINEAS CON RESERVAS";
               |ERROR; |ACABA;
          |FINSI;
          |SI sw_reserva = 2;
               |ERROR; |ACABA;
          |FINSI;
          |SI sw_reserva = 3;
               |BUCLE CorrigeVari;
          |FINSI;

          |BUCLE BorraObsCto;

          BAdela = 1;
          TipoCta = "P";
          SPedido = #0#25;
          NPedido = #0#0;
          |SUB_EJECUTA_NP "agis0002"; || Borra Entragas a cuenta
     |FINSI;

     |SI control = 1;
          control = 0;
          #0#12 = 0;
          #0#13 = 0;
          #0#43 = 0;
          #0#44 = 0;
          |HAZ baallpp;
     |FINSI;
     |ABRE #1;
     |ABRE #19;
     |BUCLELIN 2 ActCli #0;
     |CIERRA #1;
     |CIERRA #19;

     ||SALVA_FICHA #3;
     nImpo = 0; |BUCLE CalcConIva;
     ||REPON_FICHA #3;

     eaTag = "PE";  |HAZ RiesgosMalpe;
|FPRC;

|PROCESO aallpp;|TIPO 0;
     |ABRE #19;
     #19#0 = #3#2;
     |LEE 020#19=;
     |CIERRA #19;

     |SI caltot = 0;
          |LEE 101#3=;
          |PINTA 445,#3#1;
     |FINSI;

     #3#19 = #1#4;
     #3#16 = #0#7;
     #3#17 = #0#16;
     #3#20 = #0#4;
     #3#23 = #0#5;
     #3#24 = #0#6;
     #3#25 = #0#17;
     #3#26 = #0#9;

     |HAZ MalpeLin73;
     |SI ac_divisa = "S"; |Y #agifa321#3= "S";
          |PINTA #3#41; || Si las divisas y la visualizacion estan activadas, pinto
     |FINSI;

     #0#11 = #0#11 + #3#9;
     #0#12 = #0#12 + #3#11;
     #0#13 = #0#11 - #0#12;

     |SI #19#194 = 2;
         nPend = (#3#44 - #3#43) * #3#38;
     |SINO;
         nPend = (#3#5 - #3#43)  * #3#38;
     |FINSI;
     nPend = ((((nPend < #3#8) < #3#29) < #0#5) < #0#17) < #0#6;

     #0#42 = #0#42 + #3#39;
     #0#43 = #0#43 + nPend;
     #0#44 = #0#42 - #0#43;

     |GRABA 020#3a;
     |LIBERA #3;
|FPRC;

|PROCESO baallpp;|TIPO 0;
     |MENSA mensaje11;
     #0#11 = 0;
     #0#12 = 0;
     #0#42 = 0;
     #0#43 = 0;
     |BUCLELIN 2aallpp#0;
     |BLIN 4;
|FPRC;

|PROCESO PantaDesglo;
     |C_P #0#52, 0, nPosi;
     |C_P #0#52, 1, 1251;
     |C_M #0#52, 1, "S";

     |PUSHV 0501 2380;

     |BLANCO 1127 1455;
     |CUADRO 1127 1455;
     |PINTA 1228, " PORCENTAJE DESGLOSE :     ";
     |ENCAMPO #52#0;

     |POPV;
     |C_P #0#52, 1, nPosi;
     |C_M #0#52, 1, "N";
|FPRC;

|PROCESO siimpre1;|TIPO 3;
     nEntrada = (FEntrada /100) ! 0;
     |HAZ CalCabMalpe087;
     |ABRE #1;
     #1#0 = #0#4;
     |LEE 040#1=;
     |SI FSalida ! 0;  |DDEFECTO #1;  |FINSI;
     |CIERRA #1;

     |SI #0#13 = 0;
         |SI #0#52 = 0;
             |SI #1#198 ! " ";
                 |CONFI "2400NDetectado Cliente con Desglose lo activamos en el pedido : ";
                 |SI FSalida = 0;
                     |HAZ PantaDesglo;
                 |FINSI;
             |FINSI;
         |SINO;
             |HAZ PantaDesglo;
         |FINSI;
     |FINSI;

     || A partir pedido crear el de compras

     |SI nEntrada [ 2; |Y #agifa142#37 = "S"; |Y #0#34 = "N";
          |CONFI "0000NDesea generar los Pedidos de Compra de forma automatica ? ";
          |SI FSalida = 0;
               |GRABA 010#0a; || Si no grababa, perdia los valores en el sub_ejecuta
               |LIBERA #0;
               aSeriePedido  = #0#25;
               aNumeroPedido = #0#0;
               |CIERRAT #0;
               |SUB_EJECUTA_NP "agifa218.tab";
               |ABRET #0;
               |LEE 101#0=;
          |FINSI;
     |FINSI;
     |CONFI "2400NDesea imprimir este pedido? ";
     |SI 0 = FSalida;
          |HAZ impreal1;
     |FINSI;

     |SI aCam68b ! ""; |Y aCam69b ! ""; |Y aCam70b ! "";
          |SI aCam68a ! aCam68b; |O aCam69a ! aCam69b; |O aCam70a ! aCam70b;
               |HAZ HistoModPed;
          |FINSI;
     |FINSI;
     aCam68a = ""; aCam69a = ""; aCam70a = ""; aCam68b = ""; aCam69b = ""; aCam70b = "";
|FPRC;

|PROCESO HistoModPed;
     |ABRE #184;
     |LEE 000#184u;
     |SI FSalida = 0;
          nConta = #184#0 + 1;
     |SINO;
          nConta = 1;
     |FINSI;
     |DDEFECTO #184;
     #184#0 = nConta;
     #184#1 = #0#25;
     #184#2 = #0#0;
     #184#3 = ~;
     |HORA aAlfa;
     #184#4 = aAlfa;
     #184#5 = Usuario % 810;
     #184#6 = aCam68b;
     #184#7 = aCam69b;
     #184#8 = aCam70b;
     #184#9 = aCam68a;
     #184#10 = aCam69a;
     #184#11 = aCam70a;
     |GRABA 020#184n;
     |CIERRA #184;
|FPRC;

|PROCESO impreal1;
     modo = (FEntrada / 100) ! 0;
     |SI modo = 1; |O modo = 2;
          |GRABA 021#0a;
          |LIBERA #0;
     |FINSI;
     ser_ped = #0#25;
     num_ped = #0#0;
     |CIERRAT #0;
     |SUB_EJECUTA_NP "malpe105.run";
     |ABRET #0;
     #0#25 = ser_ped;
     #0#0 = num_ped;
     |SI modo = 4;
          |LEE 001#0=;
     |SINO;
          |LEE 101#0=;
     |FINSI;
|FPRC;

|PROCESO mirainf;
     |ABRE #40;
     #40#26 = #0#25;
     |LEE 001#40=;
     |SI FSalida = 0;
          informe = #40#0;
     |SINO;
          informe = "malpe104";
     |FINSI;
     |CIERRA #40;
     |QBF informe;
|FPRC;

|PROCESO Extracto;
     |SI nTecla = 13; ||F5
          modo = (FEntrada / 100) ! 0;
          |SI modo = 4;
               |ABRE #1;
               #1#0 = #0#4;
               |LEE 001#1=;
               |SI FSalida ! 0; |DDEFECTO #1; |FINSI;
               |CIERRA #1;
          |FINSI;
          |PUSHV 0501 2380;
          ext1 = #1#16;
          |SUB_EJECUTA_NP "caextrap.run";
          |POPV;
          |ERROR; nError6 = 1;
     |FINSI;
|FPRC;

|PROCESO Menus;
     |MENU menu;
     |SI FSalida = 1;    ||Automatico
          |ABRE #42;
          #42#2 = "";
          #42#0 = "clientes";
          |LEE 101#42=;
          |SI FSalida = 0;
               #42#1 = #42#1 + 1;
               contador = #42#1;
               |GRABA 021#42a;
          |SINO;
               contador = 1;
               #42#1 = 1;
               |GRABA 021#42n;
          |FINSI;
          cont = contador;
          cont = ("000000" + cont) % -106;
          #0Campo = cont;
          |PINTA #0Campo;
          |LIBERA #42;
          |CIERRA #42;
     |FINSI;
|FPRC;

|PROCESO LeeMonBase; ||Lee los parametros de la moneda base
     |ABRE #agifa321;
     |ABRE #agifa324;
     |LEE 010#agifa321.p;   ||Leo parametros de divisa
     #0#40 = #agifa321#9;  ||Cojo la Moneda Base de parametros
     #agifa324#0 = #agifa321#9;
     |LEE 010#agifa324.=;         ||Leo la ficha de la moneda base en Divisas
     #0#41 = #agifa324#2;  ||Asigno el cambio de la moneda base con el euro
     decim_base = #agifa324#7;    ||Asigno los decimales de la moneda base
     precio_base = #agifa324#9;   ||Asigno los decimales del precio en la mon. base
     |CIERRA #agifa324;
     |CIERRA #agifa321;
|FPRC;

|PROCESO Param_Divisas; |TIPO 0;
     pintado = 0;  ||Controla pintados de las lineas

     |ABRE #agifa324;
     #agifa324#0 = #0#35;
     |LEE 001#agifa324.=;   || Leo la divisa
     |CIERRA #agifa324;

     decim_divisa = #agifa324#7;  || Decimales de la divisa
     precio_divisa = #agifa324#9;  || Decimales en el precio de la divisa
     nDeci_DivP = decim_divisa;   || Decimales de la divisa
     nDeci_PreDivP = precio_divisa;  || Decimales en el precio de la divisa
     |SI #0#37 = "B";   ||Si trabajo con la moneda base ...
          nDeci_PreVis    = precio_divisa;
          nDeci_ImpVis    = decim_divisa;
          nDeci_PreVisP   = precio_divisa;
          nDeci_ImpVisP   = decim_divisa;
          nDeci_Base     = decim_base;
          nDeci_PreBase  = precio_base;
          nDeci_BaseMxP  = decim_base;
          nDeci_PreBasMxP = precio_base;
          nDeci_TotVisP   = decim_base;
          nDeci_TotNoVisP = decim_divisa;
     |SINO;     ||Si trabajo con la divisa ...
          nDeci_PreVis    = precio_base;
          nDeci_ImpVis    = decim_base;
          nDeci_PreVisP   = precio_base;
          nDeci_ImpVisP   = decim_base;
          nDeci_Base      = decim_base; || Max. precision en los decimales de la moneda base
          nDeci_PreBase   = precio_base;  || Max. precision en los dec. del precio de la moneda base
         |SI #0#36 = 1;
              nDeci_BaseMxP   = decim_base;    || sin triangulacion
              nDeci_PreBasMxP = precio_base;
         |SINO;
              nDeci_BaseMxP   = #agifa321#10;  || con triangulacion
              nDeci_PreBasMxP = #agifa321#11;
         |FINSI;
          nDeci_TotVisP   = decim_divisa;
          nDeci_TotNoVisP = decim_base;
     |FINSI;
|FPRC;

|PROCESO LeeParamDiv; |TIPO 0;
|HAZ Param_Divisas;
|ACABA;
     |ABRE #agifa321;
     |LEE 001#agifa321.p;    || Leo parametros de divisas
     |ABRE #agifa007;
     #agifa007#26 = #0#25;
     |LEE 001#agifa007.=;        || Leo la serie

     |SI #agifa007#39 = "N"; |O #agifa321#1 = "N";
          #0#37 = "B";
     |FINSI;

     |SI #0#37 = "D";      || Si la mon. de trabajo es la divisa...
       || ...escondo los campos de la moneda base :
       |CAMPO_POSICION #3#6 , 1 , 1009;
       |CAMPO_POSICION #3#9 , 1 ,1026;
       |CAMPO_VISUAL #3#6 , 1 , "N";
       |CAMPO_VISUAL #3#9 , 1 , "N";
       |CAMPO_LINEAL #3#6 , 1 , "N";
       |CAMPO_LINEAL #3#9 , 1 , "N";
       |CAMPO_MODIFICA #3#6 , 1 , "N";
       || ...y pongo en pantalla los de divisa
       |CAMPO_POSICION #3#38 , 1 , 1544;
       |CAMPO_POSICION #3#39 , 1 ,1568;
       |CAMPO_VISUAL #3#38 , 1 , "S";
       |CAMPO_VISUAL #3#39 , 1 , "S";
       |CAMPO_MODIFICA #3#38 , 1 , "S";
       |CAMPO_LINEAL #3#38 , 1 , "S";
       |CAMPO_LINEAL #3#39 , 1 , "S";
       |SI #agifa007#39 = "S"; |Y #agifa321#1 = "S"; ||Si la serie es de ...
          ac_divisa = "S";  || ... divisas, y en parametros estan activadas ...
          || ... pongo como modificables los campos relacionados con divisas
          |CAMPO_MODIFICA #0#35 , 1 , "S";
          |CAMPO_MODIFICA #0#36 , 1 , "S";
          |CAMPO_MODIFICA #0#37 , 1 , "S";
          |CAMPO_MODIFICA #0#38 , 1 , "S";
          |CAMPO_MODIFICA #0#39 , 1 , "S";
          |SI #agifa321#3 = "S";  || Si la visualizacion de parametros esta activada
            || ... pongo los campos visual. de lineas como visualizables
            |CAMPO_VISUAL #3#40, 1 , "S";
            |CAMPO_VISUAL #3#41 , 1 , "S";
          |SINO;
            || ... los pongo como no visualizables
            |CAMPO_VISUAL #3#40 , 1 , "N";
            |CAMPO_VISUAL #3#41 , 1 , "N";
          |FINSI;
       |SINO; || Si la serie no es de divisas, o no estan activados parametros
          ac_divisa = "N";
          #0#37 = "B";  || La moneda de trabajo es por fuerza la moneda base
          |PINTA #0#37;
          || Pongo como no modificables los campos relacionados con divisas
          |CAMPO_MODIFICA #0#37 , 1 , "N";
          |CAMPO_MODIFICA #0#38 , 1 , "N";
          |CAMPO_MODIFICA #0#39 , 1 , "N";
          |CAMPO_MODIFICA #0#35 , 1 , "N";
          |CAMPO_MODIFICA #0#36 , 1 , "N";
          || Pongo como no visualizables los campos visual. de lineas
          |CAMPO_VISUAL #3#40, 1 , "N";
          |CAMPO_VISUAL #3#41, 1 , "N";
       |FINSI;
     |SINO;  || Si la moneda de trabajo es la moneda base
       || ...escondo los campos en divisa :
       |CAMPO_POSICION #3#38 , 1 , 1009;
       |CAMPO_POSICION #3#39 , 1 ,1026;
       |CAMPO_VISUAL #3#38 , 1 , "N";
       |CAMPO_VISUAL #3#39 , 1 , "N";
       |CAMPO_MODIFICA #3#38 , 1 , "N";
       |CAMPO_LINEAL #3#38 , 1 , "N";
       |CAMPO_LINEAL #3#39 , 1 , "N";
       || ...coloco los campos en moneda base:
       |CAMPO_POSICION #3#6 , 1 , 1544;
       |CAMPO_POSICION #3#9 , 1 ,1568;
       |CAMPO_VISUAL #3#6 , 1 , "S";
       |CAMPO_VISUAL #3#9 , 1 , "S";
       |CAMPO_MODIFICA #3#6 , 1 , "S";
       |CAMPO_LINEAL #3#6 , 1 , "S";
       |CAMPO_LINEAL #3#9 , 1 , "S";
       |SI #agifa007#39 = "S"; |Y #agifa321#1 = "S"; || Si la serie es de divisas...
          ac_divisa = "S"; || ... y divisas estan activadas en parametros
          || Pongo como modificables los campos relacionados con divisas
          |CAMPO_MODIFICA #0#35 , 1 , "S";
          |CAMPO_MODIFICA #0#36 , 1 , "S";
          |CAMPO_MODIFICA #0#37 , 1 , "S";
          |CAMPO_MODIFICA #0#38 , 1 , "S";
          |CAMPO_MODIFICA #0#39 , 1 , "S";
          |SI #agifa321#3 = "S";  || Si la visualizacion esta activada en parametros ...
            || Pongo como visualizables los campos visual. de las lineas
            |CAMPO_VISUAL #3#40, 1 , "S";
            |CAMPO_VISUAL #3#41 , 1 , "S";
          |SINO; || Los pongo como no visualizables
            |CAMPO_VISUAL #3#40 , 1 , "N";
            |CAMPO_VISUAL #3#41 , 1 , "N";
          |FINSI;
       |SINO;  || Si la serie no es de divisas o no estan activados los parametros
          ac_divisa = "N";
          #0#37 = "B";  || La moneda de trabajo es por fuerza la moneda base
          || Pongo como no modificables los campos relacionados con divisas
          |CAMPO_MODIFICA #0#37 , 1 , "N";
          |CAMPO_MODIFICA #0#38 , 1 , "N";
          |CAMPO_MODIFICA #0#39 , 1 , "N";
          |CAMPO_MODIFICA #0#35 , 1 , "N";
          |CAMPO_MODIFICA #0#36 , 1 , "N";
          || Pongo como no visualizables los campos visual. de lineas
          |CAMPO_VISUAL #3#40, 1 , "N";
          |CAMPO_VISUAL #3#41, 1 , "N";
       |FINSI;
     |FINSI;
     |CIERRA #agifa007;
     |CIERRA #agifa321;
     |HAZ Param_Divisas;
|FPRC;

|PROCESO CambioDivisa; |TIPO 0;
     nModo104 = (FEntrada / 100) ! 0;
     |SI #0Campo ! Contenido; |Y nModo104 = 1;   ||Si cambia el valor o es alta
       |SI Campo = 4; || Si llamo al proceso desde el cod. de cliente ...
         |LEE 001#agifa024.=;
         #0#35 = #agifa024#192;  || Asigno la divisa por defecto del cliente
         #0#37 = #agifa024#193; || Asigno la moneda de trabajo del cliente
         |PINTA #0#37;
       |FINSI;
       #agifa007#26 = #0#25;
       |LEE 001#agifa007.=;  || Leo la serie
        |SI #agifa007#39 = "S"; |Y #agifa321#1 = "S";
           |SI #agifa024#191 = "S"; || Si el cliente es de divisa pactada
              |ABRE #agifa328;
              |ABRE #agifa329;
              #agifa329#0 = #0#4;
              #agifa329#2 = #0#35;
              #agifa329#7 = "N";
              |SELECT #2#agifa329;  || Con esta clave ...
              |LEE 011#agifa329.=;  || ... busco periodos no finalizados de esa divisa
              |SI FSalida = 0;  || Si existe un periodo no finalizado
                |PARA div_act = 0; |SI div_act = 0; |HACIENDO;
                  #0#35 = #agifa329#2;        || Cargo la divisa ...
                  #0#36 = #agifa329#3;        || ...y el cambio
                  |LEE 001#agifa329.=;   || Leo las lineas de Clientes/Divisas
                  nfecha = #0#1 - #agifa329#6;  || Resto la fecha de fin de periodo a la actual
                  |SI nfecha > 0;  || Si la fecha actual supera a la del periodo asignado ...
                     |MENAV "0000ATENCION deberia Actualizar primero la divisa en Clientes/Divisas";
                     cli_divisa = #0#4;  || Le envio el cod. de cliente a "agifa328"
                     |SUB_EJECUTA_NP "agifa328.tab";  || Ejecuto Clientes/Divisas
                     |ERROR;
                  |SINO;
                     div_act = 1;  || Para salir del PARA
                  |FINSI;
                |SIGUE;
              |SINO;  || Si no existe un periodo no finalizado para esa divisa
                |MENAV "0000No existe un periodo activo para la divisa en Clientes / Divisas";
                cli_divisa = #0#4;  || Le paso a "agifa328" el cliente
                |SUB_EJECUTA_NP "agifa328.tab";  || Ejecuto Clientes / Divisas
                |ERROR;
                ||#0#35 = #0#40;
                ||#0#36 = #0#41;
              |FINSI;
              |SELECT #1#agifa329;
              |CIERRA #agifa329;
              |CIERRA #agifa328;
           |SINO; || Si el cliente no es de Divisa pactada ...
              |ABRE #agifa324; || ...miro directamente en divisas
              #agifa324#0 = #0#35;
              |LEE 001#agifa324.=;
              |SI FSalida = 0; ||Si existe la divisa...
                 #0#36 = #agifa324#2; || la recojo
                 |SI #agifa324#6 ! -1; || Si los dias de margen de actualizacion no
                                       || son = -1 (indefinidos)
                   |PARA div_act = 0; |SI div_act = 0; |HACIENDO;
                      #0#35 = #agifa324#0;        ||Cargo el valor de la divisa
                      #0#36 = #agifa324#2;        ||Cargo el valor del cambio
                      |LEE 001#agifa324.=;  || Leo la divisa
                      nfecha = #0#1 - #agifa324#4;
                      |SI nfecha > #agifa324#6; |Y #agifa324#6 ! -1;  || Si la fecha actual
                         |MENAV "0000ATENCION deberia Actualizar primero la divisa";
                         divisa = #0#35;
                         |SUB_EJECUTA_NP "agifa326.tab";
                         |ERROR;
                      |SINO;
                         div_act = 1;
                      |FINSI;
                   |SIGUE;
                 |FINSI;
              |FINSI;
              |CIERRA #agifa324;
           |FINSI;
        |SINO;
          #0#35 = #0#40;
          #0#36 = #0#41;
        |FINSI;
     |PINTA #0#35;
     |PINTA #0#36;
     |SI Campo = 4; || Si llamo al proceso desde el cod. de cliente ...
       |HAZ LeeParamDiv;
       |HAZ Param_Divisas;
     |SINO;
       |HAZ Param_Divisas;
     |FINSI;
     |FINSI;
|FPRC;

|PROCESO PinRemanente;
     |DDEFECTO #137;
     |ABRE #137;
     #137#0 = #0#7;
     |LEE 000#137=;
     |CIERRA #137;
     |PINTA 845, #137#7;
     |PINTA 865, #137#8;
|FPRC;


|PROCESO LeePed; |TIPO 13;  || Sirve para cargar los valores cada vez que
                            || cambio de ficha
     pintado = 0;   || controla pintado de las lineas
     |HAZ LeeMonBase;
     |HAZ LeeParamDiv;
     |HAZ Param_Divisas;

     |HAZ PinRemanente;
     |HAZ PinInciObra;
|FPRC;

|PROCESO cambfecha;
|SI #0#38 ! "O";
  #0#39 = "        ";
  |PINTA #0#39;
  |C_M #0#39,1,"N";
  |C_V #0#39,1,"N";
|SINO;
  |C_M #0#39,1,"S";
  |C_V #0#39,1,"S";
|FINSI;
|FPRC;

|PROCESO modomod; |TIPO 6;
   |SI nModo104 = 2; |Y #0Campo ! Contenido;
       |MENAV "0000No se puede modificar el campo.";
       #0Campo = Contenido;
       |PINTA #0Campo;
       |ERROR;
   |FINSI;
  |SI #0Campo ! Contenido; |Y Campo = 37;
    |HAZ LeeParamDiv;
    |HAZ Param_Divisas;
  |FINSI;
|FPRC;

|PROCESO caltotal; |TIPO 0; ||Proceso especial para casos determinados como
                            ||modificar los campos de cabecera en dtos, cambio divisas etc
                            ||Recalcula total pedido y pendiente...
                            ||En el caso de cambiar el cambio de divisas recalcula tambien precio... de lineas
     nModo104 = (FEntrada/100) ! 0;
     |SI nModo104 = 4; |ACABA; |FINSI;

   |SI #0Campo ! Contenido;||Solo si cambia
     caltot = 1;
     |SI Campo = 36; ||Si viene del cambio divisa
         recalpre = 1; ||Recalcular tambien precios linea
     |FINSI;
     #0#11 = 0;
     #0#12 = 0;
     #0#42 = 0;
     #0#43 = 0;
     |BUCLELIN 2aallpp#0;
     |SI #0#37 = "D"; || Si la moneda de trabajo es la divisa
       #0#45 = #0#42;
       #0#46 = #0#43;
       #0#47 = #0#44;
       #0#48 = #0#11;
       #0#49 = #0#12;
       #0#50 = #0#13;
     |SINO;
       #0#45 = #0#11;
       #0#46 = #0#12;
       #0#47 = #0#13;
       #0#48 = #0#42;
       #0#49 = #0#43;
       #0#50 = #0#44;
     |FINSI;
     |PINTA #0#45;
     |PINTA #0#46;
     |PINTA #0#47;
     |GRABA 021#0a;  ||Grabo actual porque he rectificado las lineas y si
                     ||sale con Ctrl-c no coincidira el Dto de cabecera con las lineas
     caltot = 0;
     recalpre = 0;
   |FINSI;
|FPRC;

|PROCESO C; |TIPO 0;
     nModo104 = (FEntrada / 100) ! 0;
     |MODULO aProg;                          || Cuando viene del Adj.Pedido
     |SI aProg = "agifa210"; |ACABA; |FINSI; || F10 en linea
     |SI aProg = "dsm01000"; |ACABA; |FINSI; || F10 en linea

     |SI nTecla = 11; |O #41#102 = "S"; |O #41#102 = "X";
          eaCodigo = #0#4;
          eaFuerza = "N";
          |SI nTecla = 11; eaFuerza = "S"; |FINSI;
          |SUB_EJECUTA_NP "dsz99994";
          |SI #41#102 = "N"; |Y nModo104 ! 2; |ERROR;  |FINSI;
     |FINSI;
|FPRC;

|PROCESO PintaObs; |TIPO 14;
     |ABRE #204;
     |LEE 040#204p;
     |SI FSalida ! 0;  |DDEFECTO #204;  |FINSI;
     |CIERRA #204;

     aAlfa = #agifa204#6; |QBF aAlfa;
     |SI aAlfa ! "";
         |ATRI I;  |PINTA 2105 , aAlfa;    |ATRI 0;
         |CAMPO_MODIFICA #0#53 , 1 , "S";
     |FINSI;

     aAlfa = #agifa204#7; |QBF aAlfa;
     |SI aAlfa ! "";
         |ATRI I;  |PINTA 2130 , aAlfa;    |ATRI 0;
         |CAMPO_MODIFICA #0#54 , 1 , "S";
     |FINSI;

     aAlfa = #agifa204#8; |QBF aAlfa;
     |SI aAlfa ! "";
         |ATRI I;  |PINTA 2155 , aAlfa;    |ATRI 0;
         |CAMPO_MODIFICA #0#55 , 1 , "S";
     |FINSI;

     |ATRI I;
|FPRC;

|PROCESO FEntra104_7; |TIPO 7;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO ContaMp0; |TIPO 0;
     nModo = (FEntrada/100) ! 0;
     |SI nModo ! 1; |ACABA; |FINSI;
     |SI nErrClave = 0;
          |HAZ MalpePV0;
     |SINO;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO ContaMp7; |TIPO 7;
     nModo = (FEntrada/100) ! 0;
     |SI nModo = 1;
          |HAZ Clave87;
          |SI nErrClave = 0;
               |HAZ MalpePV7;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO PideClave;
     |MODULO aAlfa;
     |SI aAlfa = "malpe151"; aAlfa = #81#1; |ACABA; |FINSI;

     |PUSHV 2401 2480;
     |ATRI S;
     |PINTA 2425, " Introduzca Clave:             ";
     |ATRI 0;
     E_sup = 10;
     E_inf = "";
     aAlfa = "";
     aAlfa = 2445 ? -1;
     |SI FSalida ! 0; aAlfa = ""; |FINSI;
     |POPV;
|FPRC;

|PROCESO Clave87;
     |ABRE #81; |LEE 000#81p; |CIERRA #81;
     |SI nModo = 2; |O nModo = 3;
          |SI nForma = -1;
               |HAZ PideClave;
               |SI aAlfa ! #81#1;
                    nErrClave = 1;
                    |MENAV "0000Clave incorrecta...";
               |SINO;
                    nErrClave = 0;
               |FINSI;
          |FINSI;
     |SINO;
          |HAZ PideClave;
          |SI aAlfa ! #81#1;
               nErrClave = 1;
               |MENAV "0000Clave incorrecta...";
          |SINO;
               nErrClave = 0;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO NeceContra; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     aAlfa = #0#68;
     |C_M #0#69, 1, aAlfa;
     |C_M #0#70, 1, aAlfa;
     |SI nModo = 1;
          |SI aAlfa = "S";
               #0#69 = "01";
               #0#70 = "01.01.1900";
          |SINO;
               #0#69 = "99";
               #0#70 = #0#1;
          |FINSI;
          |PINTA #0#69;
          |PINTA #0#70;
     |FINSI;
|FPRC;

|PROCESO FVerifi; |TIPO 7;
     |SI #0#75 = "01.01.0000";
          #0#75 = #0#1;
          |PINTA #0#75;
     |FINSI;
|FPRC;

|PROCESO Tipo87_20; |TIPO 20;
     |SI FSalida = "OPCION";
          FSalida = "Opciones";
     |SINO;

          |LEE 101#0=;
          |SI FSalida ! 0; |AVISO; |ACABA; |FINSI;
          |MENU aMenu;
          |SI FSalida = 1;
               |HAZ impreal1;
          |FINSI;
          |SI FSalida = 2;
               |HAZ ObsContrato;
          |FINSI;
          |SI FSalida = 3;
               |HAZ InciObra;
          |FINSI;
          |LIBERA #0;
     |FINSI;
|FPRC;

|PROCESO Min186;
     #186#0 = #0#25;
     #186#1 = #0#0;
     #186#2 = 1;
|FPRC;

|PROCESO Max186;
     #186#0 = #0#25;
     #186#1 = #0#0;
     #186#2 = 999;
|FPRC;

|PROCESO Tipo186_2; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          #0#70 = #186#4;
          #0#69 = #186#5;
     |FINSI;
|FPRC;

|PROCESO DefUsu; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          #186#3 = Usuario % 810;
          |PINTA #186#3;
     |FINSI;
|FPRC;

|PROCESO ObsContrato;
     |PUSHV 0501 2380;

     |BLANCO 807 2274;
     |ENTLINEAL #1#186, 3, 2, 20, 1, Min186, Max186;

     |POPV;
     |PINTA #0#70;
     |PINTA #0#69;
     |GRABA 020#0a;
|FPRC;

|PROCESO PinInciObra;
     |PINTA 516, "컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴";
     |ABRE #185;
     #185#0 = #0#4;
     #185#1 = #0#63;
     #185#2 = 1;
     |LEE 000#185];
     |SI FSalida = 0; |Y #185#0 = #0#4; |Y #185#1 = #0#63;
          |ATRI S; |PINTA 516, " Obra con incidencias "; |ATRI 0;
          |PARA; |SI FSalida = 0; |Y #185#0 = #0#4; |Y #185#1 = #0#63; |HACIENDO;
               |SI #185#7 = "A";
                    |ATRI S; |PINTA 516, " OBRA CON INCIDENCIAS  ABIERTAS"; |ATRI 0;
               |FINSI;
               |LEE 000#185s;
          |SIGUE;
     |FINSI;
     |CIERRA #185;
|FPRC;

|PROCESO InciObra;
     eaCliente = #0#4;
     enObra = #0#63;
     enModo = 4;
     |SUB_EJECUTA_NP "malpe185";
|FPRC;

|PROCESO DefDirEnt;
     |ABRE #39;
     #39#0 = #0#4;
     #39#1 = #0#57;
     |LEE 101#39=;
     |SI FSalida = 0;
          |HAZ AsignaDir104;
     |FINSI;
     |CIERRA #39;
|FPRC;

|PROCESO AsignaDir104;
     #0#63 = #39#1; |PINTA #0#63;
     #0#64 = #39#2; |PINTA #0#64;
     #0#65 = #39#3; |PINTA #0#65;
     #0#66 = #39#4; |PINTA #0#66;
     #0#14 = #39#5; |PINTA #0#14;
     #0#20 = #39#6; |PINTA #0#20;
     #0#59 = #39#25; |PINTA #0#59;
     #0#21 = #39#7; |PINTA #0#21;
     #0#15 = #39#16; |PINTA #0#15;
|FPRC;

|PROCESO MinDir104;
     #39#0 = #0#4;
     #39#1 = 1;
|FPRC;

|PROCESO MaxDir104;
     #39#0 = #0#4;
     #39#1 = 9999;
|FPRC;

|PROCESO Obr104_0; |TIPO 0;
     |SI FSalida = 13;
          |HAZ Obr104_66;
          |ACABA;
     |FINSI;
     |SI FSalida = 2; |ACABA; |FINSI;
     nTecla = FSalida;

     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 2; |Y #0Campo ! Contenido; |Y #0#24 > 0;
          |MENAV mensaje10;
          |ERROR; nError6 = 1; |ACABA;
     |FINSI;

     |ABRE #39;
     #39#0 = #0#4;
     #39#1 = #0#63;
     |LEE 000#39=;
     |SI FSalida = 0;
          |SI #39#38 ! "C";
               |AVISO;
               |SI #39#38 = "T"; |CONFI "0000NObra terminada. 풠ontinuar?"; |FINSI;
               |SI #39#38 = "P"; |CONFI "0000NObra pendiente. 풠ontinuar?"; |FINSI;
               |SI FSalida ! 0; |ERROR; |ACABA; |FINSI;
          |FINSI;
     |SINO;
          |MENAV "0000No existe direccion de entrega...";
          |ERROR; |ACABA;
     |FINSI;
     |CIERRA #39;
     |SI #0Campo ! Contenido;
          |HAZ AsignaDir104;
     |FINSI;
|FPRC;

|PROCESO Obr104_7; |TIPO 7;
     |SI #41#71 = "S";
          |ABRE #39;
          #39#0 = #0#4;
          #39#1 = 1;
          |LEE 000#39];
          |SI FSalida = 0; |Y #39#0 = #0#4;
               |LEE 000#39s;
               |SI #39#0 = #0#4; |Y FSalida = 0;
                    #39#0 = #0#4;
                    #39#1 = #0#63;
                    |LEE 000#39];
                    |CONSULTA_F_CLAVE #1#39, MinDir104, MaxDir104;
                    |SI FSalida = 0;
                         |HAZ AsignaDir104;
                    |SINO;
                         |ERROR; |ACABA;
                    |FINSI;
               |FINSI;
          |FINSI;
          |CIERRA #39;
     |FINSI;
|FPRC;

|PROCESO Obr104_66;
     |ABRE #39;
     #39#0 = #0#4;
     #39#1 = #0#63;
     |LEE 000#39];
     |CONSULTA_F_CLAVE #1#39, MinDir104, MaxDir104;
     |SI FSalida = 0;
          |HAZ AsignaDir104;
     |SINO;
          |ERROR; |ACABA;
     |FINSI;
     |CIERRA #39;
|FPRC;
