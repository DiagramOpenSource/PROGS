|FICHEROS;
     marhu351 #0;
     marhu354 #1;   ||Tmp
     marhu352 #352;
     marhu353 #353;
     agifa010 #10;
     agifa071 #71;
     agifa019 #19;
     marhu371 #371;      ||Grupos Series (CAB)
     marhu372 #372;      ||Grupos Series (LIN)
|FIN;

|VARIABLES;
     &Tempo    = "";
     aTempo1   = "";
     aInforme  = "";
     aInforme1 = "marhu351";
     aInforme2 = "marhr351";
     aAlfa     = "";
     i         = 0;
     nHay      = 0;
     nCampo    = 0;
     nTotal    = 0;
     nRenta    = 0;
     aDesdeSer = "";
     aHastaSer = "";
|FIN;

|PROCESO Desglosa; |TIPO 0;
     |SI #0#33 = "D"; aAlfa = "S"; |SINO; aAlfa = "N"; |FINSI;
     |C_M #0#2, 1, aAlfa;
     |C_M #0#3, 1, aAlfa;
     |C_M #0#4, 1, aAlfa;
     |C_M #0#5, 1, aAlfa;
     |C_M #0#6, 1, aAlfa;
|FPRC;

|PROCESO Tmp;
     nHay = 1;
     |PRINT;
|FPRC;

|DEFBUCLE Tmp;
     #1#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tmp;
|FIN;

|PROCESO Desglosado;
     |PARA i = 2; |SI i [ 6; |HACIENDO i = i + 1;
          #353#0 = #0i;
          #353#2 = #71#13;
          |LEE 000#353=;
          |SI FSalida = 0;
               |DDEFECTO #1;
               #1#0 = #10#6;
               |LEE 101#1=;
               |SI FSalida ! 0; |GRABA 020#1b; |FINSI;
               nCampo = i - 1;
               #1nCampo = #1nCampo + #71#9;
               nCampo = nCampo + 6;
               #1nCampo = #1nCampo + #71#14;
               #1#6 = #1#1 + #1#2 + #1#3 + #1#4 + #1#5;
               #1#12 = #1#7 + #1#8 + #1#9 + #1#10 + #1#11;
               |GRABA 020#1a;
               nTotal = nTotal + #71#9;
               nRenta = nRenta + #71#14;
               |LIBERA #1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Resumido;
     |DDEFECTO #1;
     #1#0 = #10#6;
     |LEE 101#1=;
     |SI FSalida ! 0; |GRABA 020#1b; |FINSI;
     nCampo = 1;
     #1nCampo = #1nCampo + #71#9;
     nCampo = nCampo + 6;
     #1nCampo = #1nCampo + #71#14;
     #1#6 = #1#1 + #1#2 + #1#3 + #1#4 + #1#5;
     #1#12 = #1#7 + #1#8 + #1#9 + #1#10 + #1#11;
     |GRABA 020#1a;
     nTotal = nTotal + #71#9;
     nRenta = nRenta + #71#14;
     |LIBERA #1;
|FPRC;

|PROCESO marhu372;
     |SI #marhu372#2 = #agifa071#29;
          |HAZ Principal;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE marhu372;
 #marhu372#1;
 ;
 #marhu351#47, 001;
 #marhu351#47, 999;
 ;
 NULL, marhu372;
|FIN;

|PROCESO agifa071;
     ||TODO CCC
     |SI #0#36 = "P";
          ||Comprobar uno a uno todas las series
          |PARA i = 37; |SI i [ 46; |HACIENDO i = i + 1;
               |SI #marhu351#i# = #agifa071#29; |Y #marhu351#i# ! ".    ";
                    |HAZ Principal;
                    |SAL;
               |FINSI;
          |SIGUE;
     |SINO;
          |SI #0#36 = "G";
               ||Ver si la serie pertenece al grupo
               |BUCLE marhu372;
          |SINO;         ||(L)imites
               |HAZ Principal;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Principal;
     |LIBERA #19;
     #19#0 = #71#2;
     |LEE 041#19=;
     |SI #19#125 = "Z ";  |ACABA;  |FINSI;  ||RB C.Banyo
     |SI #0#33 = "D";
          |HAZ Desglosado;
     |SINO;
          |HAZ Resumido;
     |FINSI;
|FPRC;

|DEFBUCLE agifa010;
     #10#4;
     ;
     aDesdeSer, #0#0, 000001;
     aHastaSer, #0#1, 999999;
     ;
     NULL, NULL, agifa071;
|FIN;

|PROCESO Recal;
     #1#13 = #1#1 * 100 / nTotal;
     #1#14 = #1#2 * 100 / nTotal;
     #1#15 = #1#3 * 100 / nTotal;
     #1#16 = #1#4 * 100 / nTotal;
     #1#17 = #1#5 * 100 / nTotal;

     #1#18 = (#1#1 - #1#7)  * 100 / #1#1;
     #1#19 = (#1#2 - #1#8)  * 100 / #1#2;
     #1#20 = (#1#3 - #1#9)  * 100 / #1#3;
     #1#21 = (#1#4 - #1#10) * 100 / #1#4;
     #1#22 = (#1#5 - #1#11) * 100 / #1#5;
     |GRABA 020#1a;

     #0#13 = #0#13 + #1#1;
     #0#14 = #0#14 + #1#13;
     #0#15 = #0#15 + #1#7;
     #0#16 = #0#15 * 100 / #0#13;

     #0#17 = #0#17 + #1#2;
     #0#18 = #0#18 + #1#14;
     #0#19 = #0#19 + #1#8;
     #0#20 = (#0#17 - #0#19) * 100 / #0#17;

     #0#21 = #0#21 + #1#3;
     #0#22 = #0#22 + #1#15;
     #0#23 = #0#23 + #1#9;
     #0#24 = (#0#21 - #0#23) * 100 / #0#21;

     #0#25 = #0#25 + #1#4;
     #0#26 = #0#26 + #1#16;
     #0#27 = #0#27 + #1#10;
     #0#28 = (#0#25 - #0#27) * 100 / #0#25;

     #0#29 = #0#29 + #1#5;
     #0#30 = #0#30 + #1#17;
     #0#31 = #0#31 + #1#11;
     #0#32 = (#0#29 - #0#31) * 100 / #0#29;
|FPRC;

|DEFBUCLE Recal;
     #1#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL, Recal;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |SI #0#33 = "D";
          aInforme = aInforme1;
     |SINO;
          aInforme = aInforme2;
     |FINSI;

     |HAZ CreaTempo; aTempo1 = Tempo;
     |NOME_DAT #1 aTempo1; |DELINDEX #1;

     |ABRE #352;
     |PARA i = 2; |SI i [ 6; |HACIENDO i = i + 1;
          |DDEFECTO #352;
          #352#0 = #0i;
          |LEE 000#352=;
          nCampo = i + 5;
          #0nCampo = #352#1;
     |SIGUE;
     |CIERRA #352;

     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR aInforme;
          |SI FSalida = 0;
               |ABRE #1;
               |ABRE #19;
               |ABRE #353;
               |SELECT #2#353;
               |SI #0#36 = "L";
                    aDesdeSer = #0#34;
                    aHastaSer = #0#35;
               |SINO;
                    aDesdeSer = "     ";
                    aHastaSer = "zzzzz";
               |FINSI;
               |BUCLE agifa010;
               |CIERRA #1;
               |CIERRA #353;

               |BUCLE Recal;

               |BUCLE Tmp;
               |SI nHay ! 0; |PIEINF; |FINSI;
               |FININF;
               |CIERRA #19;
          |SINO;
               aAlfa = "0000No existe informe: " + aInforme;
               |MENAV aAlfa;
          |FINSI;
          |FINIMP;
     |FINSI;

     |DELINDEX #1;  Tempo = aTempo1; |HAZ BoraTempo;
|FPRC;

|PROCESO Serie; |TIPO 0;
     |SI #0#36 = "L";
          |C_M #0#34, 1, "S";
          |C_M #0#35, 2, "S";
          |PARA i = 37; |SI i [ 46; |HACIENDO i = i + 1;
               |C_M #0i, 1, "N";
          |SIGUE;
          |C_M #0#47, 1, "N";
     |SINO;
          |SI #0#36 = "P";
               |C_M #0#34, 1, "N";
               |C_M #0#35, 2, "N";
               |PARA i = 37; |SI i [ 46; |HACIENDO i = i + 1;
                    |C_M #0i, 1, "S";
               |SIGUE;
               |C_M #0#47, 1, "N";
          |SINO;              ||(G)rupo
               |C_M #0#34, 1, "N";
               |C_M #0#35, 2, "N";
               |PARA i = 37; |SI i [ 46; |HACIENDO i = i + 1;
                    |C_M #0i, 1, "N";
               |SIGUE;
               |C_M #0#47, 1, "S";
          |FINSI;
     |FINSI;
|FPRC;
