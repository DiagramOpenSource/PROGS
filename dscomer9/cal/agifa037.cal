|FICHEROS;
     agifa037 #0;        ||devoluciones
     agifa019 #1;
     agifa036 #3;        ||Depositos
     agifa070 #4;        ||Lineas de depositos
     agifa142 #10;
     agifa048 #13;       || Escandallo
     agifa049 #14;       || Lineas Escandallo
     agifa017 #17;
     dsm00024 #24;
     agifa321 #35;  || Parametros de Divisas
     agifa328 #36;  || Clientes/Divisas
     agifa329 #37;  || Lineas de Clientes/Divisas
     agifa324 #38;  || Divisas
     dsm00005 #905;
|FIN;

|VARIABLES;
     &enDepo = 0;
     &eaArtiAnt = "";

     aTipo = "";
     nLinV = 0;
     nSwColor = 0;
     nForma = 0;
     nLin = 0;
     {-1}aMenu = "";
     aMenu1 = "2400";
     aMenu2 = "1";
     aMenu3 = "Opciones:  ";
     aMenu4 = "RAC";
     aMenu5 = " Repasar ";
     aMenu6 = " Aceptar ";
     aMenu7 = " Cancelar ";

     &Tempo           = "";
     aTempo0          = "";
     &nDeci_Base      = 0;   || Decimales en Totales de la moneda base
     &nDeci_PreBase   = 0;   || Decimales en el Precio en moneda base
     &nDeci_BaseMxD   = 0;   || Decimales en Totales de la moneda base (Maxima precision)
     &nDeci_PreBasMxD = 0;   || Decimales en el Precio en moneda base (Maxima precision)
     &nDeci_DivD      = 0;   || Decimales en Totales de la divisa
     &nDeci_PreDivD   = 0;   || Decimales en el Precio en divisa
     &nDeci_TotVisD   = 0;   || Decimales en Totales visibles (el de la moneda de trabajo)
     &nDeci_TotNoVisD = 0;   || Decimales en Totales no visibles (el de la otra moneda)
     &nDeci_PreVisD   = 0;   || Decimales en el Precio visual. (lineas)
     &nDeci_ImpVisD   = 0;   || Decimales en el Importe visual. (lineas)
     &decim_divisa = 0;  || Decimales de la divisa
     &precio_divisa = 0; || Decimales del precio en divisa
     &decim_base = 0;    || Decimales de la moneda base
     &precio_base = 0;   || Decimales del precio en moneda base

     nNume = 0;
     Unidades = 0;
     asto = 0;
     s = 0;
     qp = 0;
     qp1 = 0;
     operacion = "DV";
     orden = 1;
     forden = "";
     sto = 0;
     linea = 14;
     imneto = 0;
     margen = 0;
     mes = 0;
     canescan = 0;
     preescan = 0;
     cantidad = 0;
     nImpDiv  = 0;
|FIN ;

|INCLUYE i_varart;

|PROCESO Min70;
     #4#22 = #0#5;
     #4#0 = #0#1;
     #4#1 = 1;
|FPRC;

|PROCESO Max70;
     #4#22 = #0#5;
     #4#0 = #0#1;
     #4#1 = 999;
|FPRC;

|PROCESO Tipo66; |TIPO 66;
     |ABRE #4;
     #4#22 = #0#5;
     #4#0 = #0#1;
     #4#1 = 1;
     |LEE 000#4];
     |CONSULTA_F_CLAVE #1#4, Min70, Max70;
     |SI FSalida = 0;
          #0#0 = #4#2; |PINTA #0#2;
          #0#3 = #4#5; |PINTA #0#3;
          #0#4 = #4#5; |PINTA #0#4;
          #0#9 = #4#28;
          #0#10 = #4#28;
          #0#11 = #4#29;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #4;
|FPRC;

|PROCESO Llena037; |TIPO 6;
     eaArticulo = #0Campo;
     |HAZ LlenaArticulo;
     |SI FSalida = 0;
          #0Campo = eaArticulo;
          |PINTA #0Campo;
     |SINO;
          |ERROR; |ACABA;
     |FINSI;
|FPRC;

|PROCESO Tipo2; |TIPO 2;
     nForma = 0 +. 1;
     |SI nForma = -1; |Y nSwColor ! 0;
          |MENAV "0000No se puede modificar....";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO MaxUni; |TIPO 0;
     |SI #0#4 > #0#3;
          |AVISO;
          #0#4 = #0#3; |PINTA #0#4;
     |FINSI;
     |SI #0#10 > #0#9;
          #0#10 = #0#9; ||PINTA #0#9;
     |FINSI;
|FPRC;

|PROCESO Busca;
     |SI #0#0 = #4#2; |Y #0#11 = #4#29;
          nLin = #4#1;
          #0#2 = #4#4; |PINTA #0#2;
          #0#15 = #4#3;
          #0#3 = #4#5; |PINTA #0#3;
     |FINSI;
|FPRC;

|PROCESO CheqArt;
     |SI #0#0 = #4#2;
          nLin = #4#1; |ERROR;
     |FINSI;
|FPRC;

|PROCESO Art7; |TIPO 7;
     eaArtiAnt = #0#0;
|FPRC;

|PROCESO BusLin; |TIPO 0;
     |ABRE #3;
     #3#46 = #0#5;
     #3#0 = #0#1;
     |LEE 121#3=;
     |SI FSalida = 0;
          nLin = 0;
          |BUCLELIN 2CheqArt#3;
          |SI nLin = 0;
               |MENAV "0000El articulo no existe en este deposito...";
               |ERROR;
          |SINO;
               ||eaArtiAnt  (se carga antes)
               eaSerie = #0#5;
               enDepo = #0#1;
               enAlmacen  = #0#15;
               eaArticulo = #0#0;
               enUnidad1 = #0#4;
               enUnidad2 = #0#10;
               enMedida1 = #0#12;
               enMedida2 = #0#13;
               enMedida3 = #0#14;
               eaPartida = #0#11;
               enModoVta = (FEntrada / 100) ! 0;
               |SUB_EJECUTA_NP "dsz99907;DEVOLUCION";
               |SI eaSw2Stock = "S";
                    #0#4 = enUnidad1; |PINTA #3#8;
                    #0#10 = enUnidad2;
                    #0#12 = enMedida1;
                    #0#13 = enMedida2;
                    #0#14 = enMedida3;
                    #0#11 = eaPartida;
                    #0#15 = enAlmacen;
               |FINSI;

               nLin = 0;
               |BUCLELIN 2Busca#3;
               |SI nLin = 0;
                    |MENAV "0000El articulo/partida no existe en este deposito...";
                    |ERROR;
               |SINO;
                    #0#8 = nLin;
                    |SI eaSw2Stock = "N"; |HAZ entrare; |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #3;
|FPRC;

|PROCESO decim;
     #0#4 = #0#4 ! #10#8;
     |PINTA #0#4;
     |HAZ MaxUni;
|FPRC;

||***************************** CALCULO ESCANDALLOS *******************;


|| --------------------------------------------------------------------------

|PROCESO LeeMonBase; ||Lee los parametros de la moneda base
     |ABRE #agifa321;
     |ABRE #agifa324;
     |LEE 010#agifa321.p;   ||Leo parametros de divisa
     #agifa036#52 = #agifa321#9;  ||Cojo la Moneda Base de parametros
     #agifa324#0 = #agifa321#9;
     |LEE 010#agifa324.=;         ||Leo la ficha de la moneda base en Divisas
     #agifa036#53 = #agifa324#2;  ||Asigno el cambio de la moneda base con el euro
     decim_base = #agifa324#7;    ||Asigno los decimales de la moneda base
     precio_base = #agifa324#9;   ||Asigno los decimales del precio en la mon. base
     |CIERRA #agifa324;
     |CIERRA #agifa321;
|FPRC;

|PROCESO Param_Divisas; |TIPO 0;
     |ABRE #agifa324;
     #agifa324#0 = #agifa036#49;
     |LEE 001#agifa324.=;   || Leo la divisa
     |CIERRA #agifa324;

     decim_divisa = #agifa324#7;  || Decimales de la divisa
     precio_divisa = #agifa324#9;  || Decimales en el precio de la divisa
     nDeci_DivD = decim_divisa;   || Decimales de la divisa
     nDeci_PreDivD = precio_divisa;  || Decimales en el precio de la divisa
     |SI #3#51 = "B";   || Si trabajo con la moneda base ...
          nDeci_PreVisD   = precio_divisa;
          nDeci_ImpVisD   = decim_divisa;
          nDeci_Base      = decim_base;
          nDeci_PreBase   = precio_base;
          nDeci_BaseMxD   = decim_base;
          nDeci_PreBasMxD = precio_base;
          nDeci_TotVisD   = decim_base;
          nDeci_TotNoVisD = decim_divisa;
     |SINO;     || Si trabajo con la divisa ...
          nDeci_PreVisD   = precio_base;
          nDeci_ImpVisD   = decim_base;
          nDeci_Base      = decim_base;    || Max. precision en los decimales de la moneda base
          nDeci_PreBase   = precio_base;   || Max. precision en los dec. del precio de la moneda base
          nDeci_BaseMxD   = #agifa321#10;  || Max. precision en los decimales de la moneda base
          nDeci_PreBasMxD = #agifa321#11;  || Max. precision en los dec. del precio de la moneda base
          nDeci_TotVisD   = decim_divisa;
          nDeci_TotNoVisD = decim_base;
     |FINSI;
     |HAZ Deci_IVA;
|FPRC;

|PROCESO salida;
     |SI #0#4 = 0; |ACABA; |FINSI;

     enUnidades  = -#0#4;
     eaArticulo  = #4#2;
     enAlmacen   = #4#3;
     eaTipo      = "T1";
     ||SUB_EJECUTA_NP "dsz99991;DP";

     #4#19 = #4#19 + #0#4;
     #4#5  = #4#5  - #0#4;
     #4#20 = #0#6;
     #0#3  = #4#5;

     |SI #3#51 = "D";                   || Si mon. de trabajo es divisa ...
          #4#6  = #4#24 / #3#50 * #3#53; || Recalculo precio en mon. base
          #4#26 = #4#6;                 || Precio visual, en mon. base
     |SINO;                             || Si mon. de trabajo es mon. base
          #4#24 = #4#6 / #3#53 * #3#50; || Recalculo precio en divisa
          #4#26 = #4#24;                || Precio visual, en divisa
     |FINSI;

     |SI #1#194 = 2;                    || Unidades Facturacion
          #4#7    = #4#28 * #4#6;
          nImpDiv = #4#28 * #4#24;
     |SINO;
          #4#7    = #4#5 * #4#6;
          nImpDiv = #4#5 * #4#24;
     |FINSI;

     #4#9   = (#4#7 < #4#8) < #4#23;
     #4#25  = (nImpDiv < #4#8) < #4#23;

     |SI #3#51 = "D";     || Si la moneda de trabajo es la divisa ...
         #4#27 = #4#9;   || el campo visualiz. es el importe en moneda base
     |SINO;               || Si la moneda de trabajo es la moneda base ...
         #4#27 = #4#25;  || el campo visualiz. es el importe en divisa
     |FINSI;

     |GRABA 020#4a;
|FPRC;

|PROCESO GrabaT1;
     #905#0 = "T1";
     |GRABA 020#905n;
     |LIBERA #905;
|FPRC;

|DEFBUCLE dsm00005p1;
     #905#1;
     ;
     "DP", #4#22, #4#0, nLinV, "      ";
     "DP", #4#22, #4#0, nLinV, "zzzzzz";
     be;
     NULL, GrabaT1;
|FIN;

|PROCESO GrabaDP;
     nNume = #905#6;
     |SALVA_DATOS #905;

     #905#0 = "DP";
     |LEE 101#905=;
     |SI FSalida = 0;
         #905#6 = #905#6 - nNume;
         #905#8 = #905#6 * #905#7;
         |GRABA 020#905a;
         |LIBERA #905;
     |FINSI;

     |REPON_DATOS #905;
     |LEE 101#905=;
     |SI FSalida = 0;
         |BORRA 020#905a;
         |LIBERA #905;
     |FINSI;
|FPRC;

|DEFBUCLE dsm00005p2;
     #905#1;
     ;
     "T1", #4#22, #4#0, nLinV, "      ";
     "T1", #4#22, #4#0, nLinV, "zzzzzz";
     be;
     NULL, GrabaDP;
|FIN;

|PROCESO entrare;
     |ABRE #1;
     #1#0 = #4#2;
     |LEE 040#1=;
     |SI FSalida ! 0;  |DDEFECTO #1;  |FINSI;
     |CIERRA #1;

     |C_M #0#4, 1, "S";
     |SI #1#176 = "S";
         eaSerie     = #4#22;
         enCodigo    = #4#0;
         enLinea     = #4#1;
         efFecha     = #3#1;
         eaTipo      = "T1";
         enAlmacen   = #4#3;
         enUnidades  = #4#5;
         |SUB_EJECUTA_NP "dsz00002;BAJA";
         nLinV = #4#1;
         |BUCLE dsm00005p1;

         eaSerie     = #4#22;
         enCodigo    = #4#0;
         enLinea     = #4#1;
         efFecha     = #3#1;
         eaArticulo  = #4#2;
         enAlmacen   = #4#3;
         enUnidades  = #4#5;
         enTBruto    = #4#7;
         eaTipo      = "T1";
         |SUB_EJECUTA_NP "dsz00002";
         nLinV = #4#1;
         |BUCLE dsm00005p2;

         #0#4 = enUnidades;
         |C_M #0#4, 1, "N";
         nSwColor = 1;
     |SINO;
          #0#4 = #4#5;
     |FINSI;
|FPRC;

|PROCESO ActuaCab;
     |HAZ TotalLin70;
     |HAZ CalCabAgifa036;
|FPRC;

|PROCESO Stock;
     |ABRE #1;
     |ABRE #17;
     |ABRE #24;
     #1#0 = #0#0;
     |LEE 121#1=;
     #17#0 = #1#0;
     #17#1 = #4#3;
     |LEE 121#17=;

     #1#14 = #1#14 - #0#4;
     #1#184 = #1#184 - #0#10;
     #1#104 = #1#104 + #0#4;
     #1#193 = #1#193 + #0#10;

     #17#10 = #17#10 - #0#4;
     #17#46 = #17#46 - #0#10;
     #17#39 = #17#39 + #0#4;
     #17#48 = #17#48 + #0#10;

     #24#0 = #0#0;
     #24#1 = #4#3;
     #24#2 = #0#11;
     |LEE 101#24=;
     |SI FSalida = 0;
          #24#20 = #24#20 - #0#4;
          #24#21 = #24#21 - #0#10;
          #24#31 = #24#31 + #0#4;
          #24#32 = #24#31 + #0#10;
          |HAZ ValoraStock;
          |GRABA 020#24a;
     |SINO;
          |HAZ ValoraStock;
     |FINSI;
     |GRABA 020#17a;
     |GRABA 020#1a;
     |CIERRA #1;
     |CIERRA #17;
     |CIERRA #24;
|FPRC;

|PROCESO Procesa;
     |ABRE #3;
     |ABRE #4;
     #3#46 = #0#5;
     #3#0 = #0#1;
     |LEE 121#3=;
     |SI FSalida = 0;
          |HAZ LeeMonBase;  || Lee variables mon. base
          |HAZ Param_Divisas; || Asigna decimales divisa
          #4#22 = #3#46;
          #4#0 = #3#0;
          #4#1 = #0#8;
          |LEE 121#4=;
          |SI FSalida = 0;
               |ABRE #1;
               #1#0 = #4#2;
               |LEE 040#1=;
               |SI FSalida ! 0;  |DDEFECTO #1;  |FINSI;
               |CIERRA #1;

               |HAZ salida;
               |HAZ Stock;
          |FINSI;
          |HAZ LimCabAgifa036;
          |BUCLELIN 0ActuaCab#3;
          |GRABA 020#3a;
     |FINSI;
     |CIERRA #3;
     |CIERRA #4;
|FPRC;

|DEFBUCLE Procesa;
     #0#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Procesa;
|FIN;

|PROCESO Min;
     #0#7 = 1;
|FPRC;

|PROCESO Max;
     #0#7 = 99;
|FPRC;

|PROCESO DefSerie; |TIPO 7;
     |SI #agifa142#185 = "S"; #0#5 = #agifa142#187; |FINSI;
|FPRC;

|PROGRAMA;
     |CLS;
     |PINPA #0#0;

     |ABRE #10; |LEE 001#10p; |CIERRA #10;

     |HAZ CreaTempo; aTempo0 = Tempo;
     |NOME_DAT #0 aTempo0; |DELINDEX #0;

     |PARA; |SI aMenu2 = 1; |HACIENDO;
          |ENTLINEAL #1#0, -1, 1, 20, 0, Min, Max;
          |SI nSwColor = 1;
               aMenu2 = 2;
          |SINO;
               |MENU aMenu;
               aMenu2 = FSalida;
          |FINSI;
     |SIGUE;

     |SI aMenu2 = 2; |BUCLE Procesa; |FINSI;

     |DELINDEX #0; Tempo = aTempo0; |HAZ BoraTempo;
|FPRO;
