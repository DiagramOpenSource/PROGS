      Los procesos marcados con (*) son los necesarios para
      simular un cabeceras/lineas (el dsm00303 es mas simple)


|FICHEROS;
     dsm00300 #0;
     dsm00301 #1,MANTE;
     agifa024 #24;
     agifm001 #10;
     agifa142 #142;
     dsm00303 #303;
     dsm00304 #304;
     dsm00306 #306;
     referen@;

     dsmw0301 #301,MANTE;      ||Aux. Intro Lin.Visitas
|FIN;

|VARIABLES;
     fFecha = @;
     &eaMensaje = "";
     &Tempo = "";
     aTmp = "";
      {-1}get = "";
         get1 = 0;
         get2 = 0;
         get3 = 0;
         get4 = 0;
         get5 = "";

     aMensaje = "";
     nModoLin = 0;
     nPanta = 0;
     nBoton1 = 0;
     nBoton2 = 0;
     nBoton3 = 0;
     nBoton4 = 0;
     nVentana = 0;

     aAlfa = "";
     nGrid1 = -1;
     nRango = 0;
     nPos1 = 0;
     nPos2 = 0;
     nModo = 0;
     nHay = 0;
     nLinea = 0;
     nEstado = 0;
|FIN;

|PROCESO CP300; |TIPO 0;
     #0#6 = (("00" + #0#4) % -102) + (("000" + #0#5) % -103);
|FPRC;

|PROCESO Evento;
     |SI FSalida = 4; |Y nEstado = 1;
          |HAZ Boton2;
     |FINSI;
|FPRC;

|PROCESO Min;
     #1#0 = #0#0;
     #1#4 = #0#14;
     #1#1 = 1;
|FPRC;

|PROCESO Max;
     #1#0 = #0#0;
     #1#4 = #0#14;
     #1#1 = 999;
|FPRC;

|PROCESO Tipo14; |TIPO 14;    ||(*)
     |SI nGrid1 = -1;
          nRango = 4 + 8 + 16 + 32;
          nPos1  = 1802;
          nPos2  = 2498;
          |LINEAL_SIMPLE #1#1, nRango, nPos1, nPos2, Min, Max, Evento;
          nGrid1 = FSalida;

          |PINPA #0#dsm00300;
          nPanta = FSalida;

          |HAZ Botonera;
     |SINO;
          |REFRESCACONTROL nGrid1;
     |FINSI;
|FPRC;

|PROCESO Botonera;
     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}Alta ", 2553, 2563, Boton1;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{197}}Modificar ", 2565, 2575, Boton2;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{199}}Baja ", 2577, 2587, Boton3;
     nBoton3 = FSalida;

     |CONTROL_SIMPLE nPanta, "LABEL,{{001}}Lineas de visitas: ", 2538, 2552, NULL;

     nEstado = 0;
     |ESTADO_CONTROL nBoton1, "DISABLE";
     |ESTADO_CONTROL nBoton2, "DISABLE";
     |ESTADO_CONTROL nBoton3, "DISABLE";


     |CONTROL_SIMPLE nPanta, "BOTON,{{137}}Recargar Clientes", 0580, 0599, Boton4;
     nBoton4 = FSalida;
|FPRC;

|PROCESO AltaModi;
     |VENTANA 1601, 2699, -1, 1, "Lineas Visitas";
     nVentana = FSalida;

     |PINPA #0#301;
     |PINDA #0#301;

     get1 = -1;
     get2 = -1;
     get3 = 0;
     get4 = nVentana;
     get5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", get;

     |DDEFECTO #301;

     |SI nModoLin = 1;
          |C_M #301#0, 1, "S";
          |C_M #301#4, 1, "S";
          |C_M #301#1, 1, "N";

          nLinea = 1;
          |HAZ DameUltimaLinea;

          #301#0 = #dsm00300#0;
          #301#4 = #dsm00300#14;
          #301#1 = nLinea;
     |SINO;
          |C_M #301#0, 1, "N";
          |C_M #301#4, 1, "N";
          |C_M #301#1, 1, "N";

          #301#0 = #dsm00301#0;
          #301#1 = #dsm00301#1;
          #301#2 = #dsm00301#2;
          #301#4 = #dsm00301#4;
          #301#5 = #dsm00301#5;
          #301#6 = #dsm00301#3;
          #301#7 = #dsm00301#6;
          #301#8 = #dsm00301#7;
          #301#9 = #dsm00301#8;
     |FINSI;

     |PINDA #0#301;
     |ENDATOS #1#301;
     |SI FSalida = 0;
          |HAZ GrabaDatos301;
     |FINSI;

     get1 = -1;
     get2 = -1;
     get3 = 0;
     get4 = nVentana;
     get5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", get;

     |FINVENTANA nVentana;
|FPRC;

|PROCESO GrabaDatos301;
     |DDEFECTO #dsm00301;
     #dsm00301#0 = #301#0;
     #dsm00301#4 = #301#4;
     #dsm00301#1 = #301#1;
     |LEE 101#dsm00301.=;
     |SI FSalida ! 0; |GRABA 020#dsm00301.b; |FINSI;
     #dsm00301#2 = #301#2;
     #dsm00301#3 = #301#6;
     #dsm00301#5 = #301#5;
     #dsm00301#6 = #301#7;
     #dsm00301#7 = #301#8;
     #dsm00301#8 = #301#9;
     |GRABA 020#dsm00301.a;
     |LIBERA #dsm00301;
|FPRC;

|PROCESO DameUltimaLinea;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;

     #dsm00301#0 = #dsm00300#0;
     #dsm00301#4 = #dsm00300#14;
     #dsm00301#1 = 999;
     |LEE 000#dsm00301.];
     |SI FSalida = 0;
          |LEE 000#dsm00301.a;
     |SINO;
          |LEE 000#dsm00301.u;
     |FINSI;

     |SI FSalida = 0; |Y #dsm00301#0 = #dsm00300#0; |Y #dsm00301#4 = #dsm00300#14;
          nLinea = #dsm00301#1 + #142#236;
     |SINO;
          nLinea = #142#236;
     |FINSI;
|FPRC;

|PROCESO Boton1;
/*
     |LEE 000#0.=;
     |SI FSalida ! 0;
          aMensaje = "0000Primero seleccione Cliente + Fecha Visita en el mantenimiento";
          |MENAV aMensaje;
          |ACABA;
     |FINSI;
*/
     nModoLin = 1;
     |HAZ AltaModi;
     |REFRESCACONTROL nGrid1;
|FPRC;

|PROCESO Boton2;
/*
     |LEE 000#0.=;
     |SI FSalida ! 0;
          aMensaje = "0000Primero seleccione Cliente + Fecha Visita en el mantenimiento";
          |MENAV aMensaje;
          |ACABA;
     |FINSI;
*/

     nModoLin = 2;
     |HAZ AltaModi;
     |REFRESCACONTROL nGrid1;
|FPRC;

|PROCESO Boton3;
/*
     |LEE 000#0.=;
     |SI FSalida ! 0;
          aMensaje = "0000Primero seleccione Cliente + Fecha Visita en el mantenimiento";
          |MENAV aMensaje;
     |FINSI;
*/
     |LEE 000#dsm00301.=;
     |SI FSalida = 0; |Y #dsm00300#0 = #dsm00301#0; |Y #dsm00300#14 = #dsm00301#4;
          aMensaje = "0000NEsta seguro que quiere borra la linea " + #dsm00301#3;
          |QBF aMensaje;
          aMensaje = aMensaje + "?";
          |CONFI aMensaje;
          |SI FSalida = 0;
               |LEE 101#dsm00301.=;
               |SI FSalida = 0;
                    |BORRA 020#dsm00301.a;
                    |LIBERA #dsm00300;
                    |REFRESCACONTROL nGrid1;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Boton4;
     aAlfa = Usuario % 810;
     |ABRE #306;
     #306#0 = aAlfa;
     |LEE 121#306=;
     |SI FSalida = 0;
          #306#1 = "01.01.0000";
     |FINSI;
     |GRABA 000#306a;
     |CIERRA #306;
     |HAZ CargaTemporal;
|FPRC;

|PROCESO CliRepre;
     #304#0 = aAlfa;
     #304#1 = #24#25;
     |LEE 000#304=;
     |SI FSalida = 0;
          |SALVA_DATOS #24;
          |REPON_DATOS #referen@;
          |GRABA 020#referen@.n;
     |FINSI;
|FPRC;

|DEFBUCLE CliRepre;
     #24#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, CliRepre;
|FIN;

|PROCESO Tipo8; |TIPO 8; ||(*)
     |ABRET #1;
     |HAZ CargaTemporal;
|FPRC;

|PROCESO Tipo9; |TIPO 9; ||(*)
     |CIERRAT #1;

     ||DESTRUYE BOTONES
     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;
     |FIN_CONTROL_WINDOWS nBoton4;
|FPRC;

|PROCESO BorraLin;
     |BORRA 020#1a;
     |LIBERA #1;
|FPRC;

|DEFBUCLE BorraLin;
     #1#1;
     ;
     #0#0, #0#14, 001;
     #0#0, #0#14, 999;
     be;
     NULL, BorraLin;
|FIN;

|PROCESO Tipo2; |TIPO 2; ||(*)
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 3;
          |BUCLE BorraLin;
     |FINSI;
|FPRC;


|PROCESO PinDefecto;
     |DDEFECTO #24;
     |ABRE #24;
     #24#0 = #0#0;
     |LEE 000#24=;
     |CIERRA #24;

     |DDEFECTO #10;
     |ABRE #10;
     #10#0 = #24#3;
     |LEE 000#10=;
     |CIERRA #10;

     #0#2 = #24#1; |PINTA #0#2;
     #0#3 = #24#8; |PINTA #0#3;
     #0#4 = #24#181; |PINTA #0#4;
     #0#5 = #24#182; |PINTA #0#5;
     |HAZ CP300;
     #0#7 = #24#9; |PINTA #0#7;
     #0#8 = #24#10; |PINTA #0#8;
     #0#9 = #24#17; |PINTA #0#9;
     #0#10 = #24#18; |PINTA #0#10;
     #0#11 = #24#196; |PINTA #0#11;
     #0#12 = #24#3; |PINTA #0#12;
     #0#13 = #10#1; |PINTA #0#13;
     #0#16 = #24#25;
|FPRC;

|PROCESO Tipo0; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI FSalida = 13; ||F5;
/*
          |DDEF aAlfa;
          aAlfa = aAlfa + "agifa024";
          |CARGA_DEF aAlfa, referen@;
          |SI FSalida = 0;
               aAlfa = Usuario;
               |NOME_DAT #referen@#aAlfa#; |DELINDEX #referen@;

               aAlfa = Usuario % 810;
               |ABRE #referen@;
               |ABRE #304;
               |BUCLE CliRepre;
               |CIERRA #304;

               #referen@#0 = #0#0;
               |LEE 000#referen@.];
               |CONSULTA_CLAVE #1#referen@;
               |SI FSalida = 0;
                    #0#0 = #referen@#0; |PINTA #0#0;
                    |HAZ PinDefecto;
               |SINO;
                    |ERROR;
               |FINSI;
               |CIERRA #referen@; |DELINDEX #referen@;
               |DESCARGA_DEF #referen@;
          |FINSI;
*/
          aAlfa = Usuario % 810;
          |ABRE #303;
          #303#0 = aAlfa;
          |LEE 000#303=;
          |SI FSalida = 0;
               |DDEF aAlfa;
               aAlfa = aAlfa + "agifa024";
               |CARGA_DEF aAlfa, referen@;
               |SI FSalida = 0;
                    |NOME_DAT #referen@#aTmp#;
                    |ABRE #referen@;
                    #referen@#0 = #0#0;
                    |LEE 000#referen@.];
                    |CONSULTA_CLAVE #1#referen@;
                    |SI FSalida = 0;
                         #0#0 = #referen@#0; |PINTA #0#0;
                         |HAZ PinDefecto;
                    |SINO;
                         |ERROR;
                    |FINSI;
                    |CIERRA #referen@;
                    |DESCARGA_DEF #referen@;
               |FINSI;
          |SINO;
               |DDEF aAlfa;
               aAlfa = aAlfa + "agifa024";
               |CARGA_DEF aAlfa, referen@;
               |SI FSalida = 0;
                    |ABRE #referen@;
                    #referen@#0 = #0#0;
                    |LEE 000#referen@.];
                    |CONSULTA_CLAVE #1#referen@;
                    |SI FSalida = 0;
                         #0#0 = #referen@#0; |PINTA #0#0;
                         |HAZ PinDefecto;
                    |SINO;
                         |ERROR;
                    |FINSI;
                    |CIERRA #referen@;
                    |DESCARGA_DEF #referen@;
               |FINSI;
          |FINSI;
          |ACABA;
     |FINSI;

     |SI nModo ! 4;
          |HAZ PinDefecto;
     |FINSI;
|FPRC;

|PROCESO UltimaVisita;
     |SI #0#1 < #referen@#14;
          #0#1 = #referen@#14;
     |FINSI;
|FPRC;

|DEFBUCLE UltimaVisita;
     #referen@#1002;
     ;
     #0#0, "01.01.0000";
     #0#0, "31.12.9999";
     ;
     NULL, UltimaVisita;
|FIN;

|PROCESO FechaVisita; |TIPO 0;     ||(*)
     |REFRESCACONTROL nGrid1;

     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          |DDEF aAlfa;
          aAlfa = aAlfa + "dsm00300";
          |CARGA_DEF aAlfa, referen@;
          |SI FSalida = 0;
               |BUCLE UltimaVisita;
               |PINTA #0#1;
               |DESCARGA_DEF #referen@;
          |FINSI;
     |FINSI;
|FPRC

|PROCESO Tipo12; |TIPO 12;    ||(*)
     nEstado = 0;
     |ESTADO_CONTROL nBoton1, "DISABLE";
     |ESTADO_CONTROL nBoton2, "DISABLE";
     |ESTADO_CONTROL nBoton3, "DISABLE";
|FPRC;

|PROCESO Tipo30; |TIPO 30;    ||(*)
     nModo = (FEntrada / 100) ! 0;
     |ABRE #1;
     #1#0 = #0#0;
     #1#4 = #0#14;
     #1#2 = 1;
     |LEE 000#1];
     |SI FSalida ! 0; |O #1#0 ! #0#0; |O #1#4 ! #0#14;
          |SI nModo = 1;
               |MENAV "0000Se anulan los datos entrados.";
          |SINO;
               |MENAV "0000No existen lineas, se borra la cabecera.";
          |FINSI;
          |BORRA 020#0a;
          |ERROR;
     |FINSI;
     |CIERRA #1;
|FPRC;

|PROCESO Linea7; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo ! 1; |ACABA; |FINSI;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;

     #1#0 = #0#0;
     #1#4 = #0#14;
     #1#1 = 999;
     |LEE 000#1];
     |SI FSalida = 0;
          |LEE 000#1a;
     |SINO;
          |LEE 000#1u;
     |FINSI;

     |SI FSalida = 0; |Y #1#0 = #0#0; |Y #1#4 = #0#14;
          nLinea = #1#1 + #142#236;
     |SINO;
          nLinea = #142#236;
     |FINSI;
     |DDEFECTO #1;
     #1#0 = #0#0;
     #1#4 = #0#14;
     #1#1 = nLinea;
|FPRC;

|PROCESO Tipo12w0301; |TIPO 12;
|FPRC;

|PROCESO FechaProx; |TIPO 7;
     nEstado = 1;
     |ESTADO_CONTROL nBoton1, "ENABLE";
     |ESTADO_CONTROL nBoton2, "ENABLE";
     |ESTADO_CONTROL nBoton3, "ENABLE";
|FPRC;

|PROCESO CargaTemporal;
     aAlfa = Usuario % 810;

     |ABRE #303;
     #303#0 = aAlfa;
     |LEE 000#303=;
     |SI FSalida = 0;
          |ABRE #306;
          #306#0 = aAlfa;
          |LEE 101#306=;
          |SI FSalida ! 0; |GRABA 020#306b; |FINSI;
          aTmp = #306#2; |QBF aTmp;
          fFecha = ~;
          |SI fFecha ! #306#1;
               |DDEF aAlfa;
               aAlfa = aAlfa + "agifa024";
               |CARGA_DEF aAlfa, referen@;
               |SI FSalida = 0;
                    |SI aTmp ! "";
                         Tempo = aTmp; |HAZ BoraTempo;
                         |NOME_DAT #referen@#aTmp#;
                         |DELINDEX #referen@;
                    |FINSI;

                    eaMensaje = "Cargando clientes...";
                    |HAZ IniMensaje;

                    |HAZ CreaTempo; aTmp = Tempo;
                    |NOME_DAT #referen@#aTmp#; |DELINDEX #referen@;
                    aAlfa = Usuario % 810;
                    |ABRE #referen@;
                    |ABRE #304;
                    |BUCLE CliRepre;
                    |CIERRA #304;

                    #306#1 = fFecha;
                    #306#2 = aTmp;
                    |GRABA 020#306a;

                    |HAZ FinMensaje;
                    |DESCARGA_DEF #referen@;
               |FINSI;
          |FINSI;
          |CIERRA #306;
     |FINSI;
     |CIERRA #303;
|FPRC;
