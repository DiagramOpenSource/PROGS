|FICHEROS;
     agifa065 #0;
     dsm00010 #910;
     dsm00006 #906;
|FIN;

|VARIABLES;
     &eaArticulo = "";
     nSwModifica = 0;
     aDopera   = "  ";
     aHopera   = "zz";
     nSw       = 1;
     nNume     = 0;
     aAlfa     = "";
     nModo     = 0;
     nUni = 0;
|FIN;

|PROCESO Llena065; |TIPO 6;
     eaArticulo = #0Campo;
     |HAZ LlenaArticulo;
     |SI FSalida = 0;
          #0Campo = eaArticulo;
          |PINTA #0Campo;
     |SINO;
          |ERROR; |ACABA;
     |FINSI;
|FPRC;

|PROCESO BorraEleme;
     |BORRA 020#906a;
|FPRC;

|DEFBUCLE BorraEleme;
     #906#1;
     ;
     #0#0, #0#5, #0#1, #0#2, #0#11, #0#3, #0#4, "      ";
     #0#0, #0#5, #0#1, #0#2, #0#11, #0#3, #0#4, "zzzzzz";
     be;
     NULL, BorraEleme;
|FIN;

|PROCESO Tipo2; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 3;
          |BUCLE BorraEleme;
     |FINSI;
|FPRC;

|PROCESO Clave;
     |BLIN 24;
     |ATRI I;
     |PINTA 2420, "Entre la clave...:";
     |ATRI 0;
     E_sup = 10;
     E_inf = "";
     aAlfa = 2439 ? -1; |QBF aAlfa;
     |BLIN 24;
     |SI aAlfa = "<MYPASWD>";
          FSalida = 0;
     |SINO;
          FSalida = 1;
     |FINSI;
|FPRC;

|PROCESO Min;
     #0#0 = " " * 20;
     #0#1 = "00.00.0000";
     #0#2 = aDopera;
     #0#3 = 000000;
     #0#4 = 00000;
     #0#11 = "     ";
|FPRC;

|PROCESO Max;
     #0#0 = "z" * 20;
     #0#1 = "99.99.9999";
     #0#2 = aHopera;
     #0#3 = 999999;
     #0#4 = 99999;
     #0#11 = "zzzzz";
|FPRC;

|RUTINA ClaveBaja;
     #906#0 = #0#0;
     #906#1 = #0#5;
     #906#2 = #0#1;
     #906#3 = #0#2;
     #906#4 = #0#11;
     #906#5 = #0#3;
     #906#6 = #0#4;
     #906#7 = "      ";
|FRUT;

|RUTINA ClaveAlta;
     #906#0 = #0#0;  ||Art
     #906#1 = #0#5;  ||Alm
     #906#2 = #0#1;  ||Fe
     #906#3 = #0#2;
     #906#4 = #0#11; ||Ser
     #906#5 = #0#3;  ||Cod
     #906#6 = #0#4;  ||Lin
     #906#7 = "zzzzzz";
|FRUT;

|PROCESO Tecla; |TIPO 11;
     |SI FSalida = 10;
          |PUSHV 0501 2380;
          |PINTA 0517, "Operacion: ";
          E_sup = 2;
          E_inf = "";
          aDopera = 0528 ? 1;
          aDopera = aDopera > "";
          |SI aDopera = "  ";
               aHopera = "zz";
          |SINO;
               aHopera = aDopera;
          |FINSI;
          |POPV;
          nSw = 1;
          |PTEC 807;
     |FINSI;
     |SI FSalida = 11;
         |ABRE #910;
         |LEE 040#910p;
         |SI FSalida ! 0;  |DDEFECTO #910;  |FINSI;
         |CIERRA #910;

         nNume         = #910#21;
         aAlfa         = "\" + #910#23 + "+#";
         |CAMPO_ANCHO  #906#7, 1, nNume;
         |CAMPO_MAXIMO #906#7, 1, aAlfa;

         |PUSHV 0501 2380;
         |BLANCO 1801 2380;
         |SI nSwModifica = 1;
               |C_M #906#7, 1, "S";
               |C_M #906#8, 1, "S";
               |C_M #906#9, 1, "S";
               |C_M #906#10, 1, "S";
               |ENTCOLUMNA #1#906, 8, 2, 5, 1, ClaveBaja, ClaveAlta;
         |SINO;
               |ENTCOLUMNA #1#906, 8, 4, 5, 1, ClaveBaja, ClaveAlta;
         |FINSI;
         |POPV;
         |ERROR;
     |FINSI;
     |SI FSalida = 12;
          |SI nSwModifica = 1;
               |ERROR;
          |SINO;
               |HAZ Clave;
               |SI FSalida ! 0;
                    |MENAV "0000Clave incorrecta";
                    |ERROR;
               |SINO;
                    nSwModifica = 1;
                    nSw = 1;
                    |PTEC 807;
               |FINSI;
          |FINSI;
     |FINSI;
     |SI FSalida = 13;
          |CONSULTA_CLAVE #1#agifa065;
          |ERROR;
     |FINSI;
|FPRC;

|PROGRAMA;
     |CLS;
     |CABEZA "Historico";
     |PARA; |SI nSw = 1; |HACIENDO;
          nSw = 0;
          |SI nSwModifica = 1;
               |ENTLINEAL #1#0, -1, 2, 22, 1, Min, Max;
          |SINO;
               |ENTLINEAL #1#0, -1, 4, 22, 1, Min, Max;
          |FINSI;
     |SIGUE;
|FPRO;
