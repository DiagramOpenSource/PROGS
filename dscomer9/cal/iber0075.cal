|FICHEROS;
     iber0075 #0;
     dsm00200 #1;
     dsm00204 #3;
|FIN;

|VARIABLES;
     aAlfa = "";
     ip_serv = "";
     serv_correo = "";
     from = "";
     to = "";
     subject = "";
     mensaje = "";
     ficheros = "";
     prim = 0;
     param = "";
     aMenLog = "";
     nErrLog = 0;
     aMensaje = "";
     fFecha = @;
     aFecha = "";
     aHora = "";
     aVarHora = "";
     aFichero = "";
     aNomPaq = "";
     aLong = "";
     nLong = 0;
     aTipo = "";
     aNombre = "";
     nHandle = 0;
     nBytes = 0;
     nCampo = 0;
     aConta = "";
     aOrigen = "";
     aDVers = "";
     aDirOri = "";
     nConta = 0;
     aCampo = "";
     aLetra = "";
     aDBase = "";
     aDMenu = "";
     aDNMen = "002472.men";
     aDAlfa = "";
     nDHand = 0;
     nSwTar = 0;
     aDsk = "";
     aTxt = "";
     aPathPaq = "";
     aFicPaq = "";
     aFicTxt = "";
|FIN;

|PROCESO tipo11; |TIPO 11;
     |SI FSalida = 11;
          |SI #0#2 = "*";
               #0#2 = "";
          |SINO;
               #0#2 = "*";
          |FINSI;
          |PINTA #0#2;
          |GRABA 020#0a;
     |FINSI;
|FPRC;

|PROCESO para_lin;
     |SI #0#2 ! "*"; |ACABA; |FINSI;

     |HAZ CreaTxt;

     |SI nSwTar = 1;
          ip_serv = "";
          |SI #1#1 = 0;|Y #1#2 = 0;|Y #1#3 = 0;|Y #1#4 = 0;
               |IP_LOCAL ip_serv;
          |SINO;
               ip_serv = #1#1+"."+#1#2+"."+#1#3+"."+#1#4;
          |FINSI;
          serv_correo = #1#5;
          from = #1#6;
          subject = #1#8;
          mensaje = #1#9;
          to = #0#3;
          ||ficheros = #0#4;
          ficheros = aFicPaq;

          |QBT ip_serv ;
          |QBF serv_correo;
          |QBF from;
          |QBF to;
          |QBF subject;
          |QBF mensaje;
          |QBF ficheros;
          |MENSA "0000Enviando fichero ...";
          |DSCORREO_ENVIA ip_serv,serv_correo,from,to,subject,mensaje,ficheros;
          |SI FSalida ! 0;
               aAlfa = "0000Error al enviar a: " + to;
               |MENAV aAlfa;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE para;
     #0#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL,para_lin;
|FIN;

|RUTINA pro1;
     #0#0 = 1;
|FRUT;

|RUTINA pro2;
     #0#0 = 999;
|FRUT;

|PROGRAMA;
     aTipo = "50";
     fFecha = ~;
     aFecha = fFecha;
     |HORA aHora;
     aVarHora = aHora % 105;
     aAlfa = PARAMETRO; |QBF aAlfa;
     |NOME_DAT #0 aAlfa;

||     |PINPA #0#0;
||     |ENTLINEAL #1#0 , -1 , 4 , 22 , 0 , pro1 , pro2;
||     |CONFI "0000SEsta seguro de que desea enviar los datos ";
     FSalida = 0;
     |SI FSalida = 0;
          |ABRE #1;
          |LEE 010#1p;
          |SI FSalida ! 0;
               |MENAV "0000No se han establecido los parametros de envio";
          |SINO;
               |BUCLE para;
          |FINSI;
          |CIERRA #1;
     |FINSI;
|FPRO;

______________________________________________________________________

|PROCESO CreaTxt;
     aOrigen = #1#6;
     |QBF aOrigen;
     aDirOri = #0#3;
     |QBF aDirOri;
     aFichero = #iber0075#4;
     |QBF aFichero;

     |HAZ SacaNombre;

     aPathPaq = aFichero - aNomPaq;
     aNombre = aNomPaq;
     |XABRE "B", aFichero, nHandle;
     |SI FSalida < 0;
          aMenLog = "    Longitud de [" + aFichero + "] inaccesible.";
          nErrLog = 1;
          |HAZ Log;
          |ACABA;
     |SINO;
          nBytes = 0;
          |XPOSICION nHandle, nBytes, 2;
          |XCIERRA nHandle;
     |FINSI;

     |HAZ SacaVersion;

     aFicTxt = aFichero + ".txt";
     |XABRE "W", aFicTxt, nHandle;
     |SI FSalida < 0;
          aMenLog = "    No se pudo crear [" + aFicTxt + "].";
          nErrLog = 1;
          |HAZ Log;
     |SINO;
          aAlfa = aTipo;  |XGRABA nHandle, aAlfa;
          aAlfa = aFecha; |XGRABA nHandle, aAlfa;
          aAlfa = aVarHora;  |XGRABA nHandle, aAlfa;
          aAlfa = "PAQ." + aNombre;
          |XGRABA nHandle, aAlfa;
          |PARA nCampo = 1; |SI nCampo [ 12; |HACIENDO nCampo = nCampo + 1;
               aAlfa = "";         || Des + Obs
               |XGRABA nHandle, aAlfa;
          |SIGUE;
          aAlfa = aNombre; |XGRABA nHandle, aAlfa;
          aAlfa = nBytes;  |XGRABA nHandle, aAlfa;
          aConta = aNombre % -103;
          |QBF aConta;
          |XGRABA nHandle, aConta;
          |XGRABA nHandle, aOrigen;
          |XGRABA nHandle, aDVers;
          aAlfa = aDirOri; |XGRABA nHandle, aAlfa;
          |XCIERRA nHandle;
     |FINSI;
     ||Falta unir el paquete con el txt, creando el paquete bueno.(*.tar)
     |HAZ CreaPaqTgz;
|FPRC;

|PROCESO Log;
     aMensaje = aMenLog;
     |MENAV aMensaje;
|FPRC;

|PROCESO SacaNombre;
     aNomPaq = "";
     aLong = aFichero % A0;
     nLong = aLong;
     |PARA nConta = 1; |SI nConta [ nLong; |HACIENDO nConta = nConta + 1;
          |SI nConta [ 99;
               aCampo = ("00" + nConta) % -102;
               aCampo = "-" + aCampo + "01";
               nCampo = aCampo;
               aLetra = aFichero % nCampo;
               |SI aLetra ! "/"; |Y aLetra ! "\";
                    aNomPaq = aLetra + aNomPaq;
               |SINO;
                    |SAL;
               |FINSI;
          |SINO;
               aNomPaq = "";
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;


|PROCESO SacaVersion;
     aDVers = "00.00.000";
     |DBASE aDBase;
     aDMenu = aDBase + "/" + aDNMen;
     |XABRE "U", aDMenu, nDHand;
     |SI FSalida ] 0;
          |XLEE nDHand, 250, aDAlfa;
          aDVers = (aDAlfa % -105) + "." + (aDAlfa % 403);
          |XCIERRA nDHand;
     |FINSI;
|FPRC;


|PROCESO CreaPaqTgz;
     ||Une el txt y el paquete, con un ATAR, luego los comprime.
     aDsk = aNombre;
     aTxt = aNombre + ".txt";
     aFicPaq = aFichero + ".tar";
     aAlfa = aFicPaq + " " + aDsk + " " + aTxt;
     |ATAR aAlfa, aPathPaq;
     |SI FSalida = 0;
          nSwTar = 1;
     |SINO;
          nSwTar = 0;
          aMenLog = "    Problemas al comprimir [" + aFicPaq + "]";
          nErrLog = 1;
          |HAZ Log;
     |FINSI;
|FPRC;
