||Tema de Decimales Divisas.  Okie.
||Temporal. Okie
||Botones
||etc ...

|FICHEROS;
     aparm136 #0;   || Este def.
     agifa134 #1;   || Facturas(C).
     agifa059 #2;   || Facturas(L).

     agifa024 #3;   || Clientes.
     agifa010 #4;   || Albaranes(C).
     agifa071 #5;   || Albaranes(L).
     agifa091 #6;   || Formas de pago.
     dsz99984 #11;

     aparm135 #135;  ||Mant. Impresora p IP
     aparm137 #137,MANTE;  ||Aux. Asist.Imp.Fact.C
     aparm138 #138;  ||Aux. Asist.Imp.Fact.L

     agifa324 #12;   ||Divisas
     agifa321 #13;   ||Parametros divisas
     agifa142 #41;   ||Parametros

     aparm025 #25;   ||Parametros aparecida.

     otroref@ #999;
|FIN;

|VARIABLES;
     &eaSwFullDuplex = "";

     &eaImpresora = "";
     &enNumDocErp = 0;
     &eaFichero = "";

     aDestino = "";
     aImpresora = "";
     nError = 0;
     aOrigen = "";
     {1}aContiene     = "";
     aLinea = "";
     aDocDestino = "";
     aDocOrigen = "";
     nHandle = 0;


     aSerieAra = "";
     nNumAra = 0;

     aImpresoraWin = "";
     nSwSalir = 0;
     aIPRemoto = "";

     aFicERP = "";
     aDSERP = "";
     aPathDef = "";
     aPathFic = "";
     aClave = "";
     aBase = "";
     aParametro = "";

     aAplica = "";
     aNomDef = "";
     nEmpresa = 0;
     aEmpresa = "";

     &PRMNT_aHisto = "";
     &PRMNT_autoERP = "";
     aEjecuta = "";
     aEmp = "";
     aAlfa = "";
     &ser_fac  = "";
     &num_fac  = 0;
     &reimp    = "";
     &eaOrdenado = "";
     aPrograma = "";
     &enFormato = 0;

     nDocAlb = 0;
     aCien = " ";
     nEmpErp = 0;
     aAplicaErp = "";
     aDefErp = "";
     aClaveErp = "";

     nSwCargaDef = 0;
     aBass = "";
     aErp = "";
     aPathErp = "";

     nBoton1 = 0;
     nBoton2 = 0;
     nBoton3 = 0;
     nBoton4 = 0;
     nBoton5 = 0;
     nBoton6 = 0;

     nBotonImpresora = 0;
     nBotonImprimir = 0;
     nBotonSalir = 0;

     nPanta = 0;
     nDocAsociados = 0;
     aTempo137 = "";
     aTempo138 = "";
     &Tempo = "";

     nSwHay = 0;
     aMensaje = "";

     &EURO = 0;
     &EURODB = 0;
     &EURODV = 0;
     &decim_divisa = 0;

     &nDeci_PreBaseMx = 0;
     &nDeci_PreDiv  = 0;
     &nDeci_Base = 0;
     &nDeci_PreBase = 0;
     &precio_base = 0;
     cambio_div = 0;
     cambio_base = 0;
|FIN;

|PROCESO Tipo11; |TIPO 11;
     |SI FSalida = 10;
          |HAZ Boton1;
     |FINSI;
     |SI FSalida = 11;
          |HAZ Boton2;
     |FINSI;
     |SI FSalida = 12;
          |HAZ Boton3;
     |FINSI;
     |SI FSalida = 13;
          |HAZ Boton4;
     |FINSI;
     |SI FSalida = 14;
          |HAZ BotonImprimirUna;
     |FINSI;
     |SI FSalida = 15;
          |HAZ Boton5;
     |FINSI;
     |SI FSalida = 18;
          |HAZ BotonSalir;
     |FINSI;
|FPRC;


|PROCESO DocErp;
     nDocAsociados = nDocAsociados + 1;
     nDocAlb = nDocAlb + 1;
|FPRC;

|DEFBUCLE DocErp;
 #otroref@#1005;
 ;
 aAplicaErp, nEmpErp, aDefErp, aClaveErp, 01;
 aAplicaErp, nEmpErp, aDefErp, aClaveErp, 99;
 ;
 NULL, DocErp;
|FIN;

|PROCESO agifa059_nd;
     |DDEFECTO #agifa010;
     #agifa010#52 = #agifa059#15;
     #agifa010#0 = #agifa059#2;
     |LEE 000#agifa010.=;
     |SI FSalida = 0;
          nDocAlb = 0;
          |SI nSwCargaDef = 1;
               aCien = " " * 100;
               nEmpErp = #48#0;
               aAplicaErp = "dscomer9";
               aDefErp = "agifa010";
               aClaveErp = #agifa010#52 + ("000000" + #agifa010#0) % -106;
               |QBF aClaveErp;
               aClaveErp = (aClaveErp + aCien) % 199;
               aClaveErp = aClaveErp + " ";
               |BUCLE DocErp;
          |FINSI;
          |DDEFECTO #aparm138;
          #aparm138#0 = #aparm137#0;
          #aparm138#1 = #aparm137#1;
          #aparm138#2 = #agifa010#52;
          #aparm138#3 = #agifa010#0;
          |LEE 101#aparm138.=;
          |SI FSalida ! 0; |GRABA 020#aparm138.b; |FINSI;
          #aparm138#4 = #agifa010#1;
          #aparm138#5 = #agifa010#67;
          #aparm138#6 = nDocAlb;
          |GRABA 020#aparm138.a;
          |LIBERA #aparm138;
     |FINSI;
|FPRC;

|DEFBUCLE agifa059_nd;
 #agifa059#1;
 ;
 #agifa134#49, #agifa134#0,   1;
 #agifa134#49, #agifa134#0, 999;
 ;
 NULL, agifa059_nd;
|FIN;


|PROCESO DameNumAsociados;
     |BUCLE agifa059_nd;
|FPRC;

|CALCULO agifa134;
     ||Cartera. Para obtener Tipo Recibo
     |DDEFECTO #11;
     #6#0 = #1#11;
     |LEE 000#6=;
     |SI #6#69 < #0#44; |O #6#69 > #0#45; ||Tipo Recibo
          |ACABA;
     |FINSI;

     ||Cliente
     #3#0 = #1#2;
     |LEE 041#3=;
     |SI 0 ! FSalida;|DDEFECTO #3;|FINSI;

     |DDEFECTO #137;
     #137#0 = #1#49;
     #137#1 = #1#0;
     |LEE 101#137.=;
     |SI FSalida ! 0; |GRABA 020#137.b; |FINSI;
     #137#2 = #1#2;
     #137#3 = #1#3;
     #137#4 = #1#1;
     #137#5 = #1#31;
     #137#6 = #1#11;
     #137#7 = #6#69;
     nDocAsociados = 0;
     |HAZ DameNumAsociados;
     #137#8 = nDocAsociados;
     |GRABA 020#137.a;
     |LIBERA #137;

     nSwHay = 1;
|FCAL;


|DEFBUCLE agifa134;
     #1#1;
     2,1,45,31,11;
     #0#9, #0#2 , #0#0 , #0#4 , #0#6 ,  #0#7, #0#42;
     #0#10,#0#3 , #0#1 , #0#5 , #0#6 ,  #0#8, #0#43;
     be;
     NULL, agifa134;
|FIN;

|PROCESO min137;
     |SI FSalida = "POSICION";
          #137#0 = aSerieAra;
          #137#1 = nNumAra;
     |SINO;
          #137#0 = "     ";
          #137#1 = 000001;
     |FINSI;
|FPRC;

|PROCESO max137;
     #137#0 = "zzzzz";
     #137#1 = 999999;
|FPRC;

|PROCESO Decimales;
     |ABRE #agifa324;
     #agifa324#0 = "EUR";
     |LEE 001#agifa324.=;
     nDeci_PreDiv = #agifa324#9;
     cambio_div = #agifa324#3;
     |ABRE #agifa321;
     |LEE 001#agifa321.p;
     #agifa324#0 = #agifa321#9; ||Igualo a moneda base
     |LEE 001#agifa324.=;
     nDeci_Base = #agifa324#7;  ||Decimales moneda base
     nDeci_PreBase = #agifa324#9;  ||Decimales precio de moneda base
     cambio_base = #agifa324#3;
     |CIERRA #agifa321;
     |CIERRA #agifa324;
|FPRC;

|PROCESO Baja138;
     #aparm138#0 = #aparm137#0;
     #aparm138#1 = #aparm137#1;
     #aparm138#2 = "     ";
     #aparm138#3 = 000000;
|FPRC;

|PROCESO Alta138;
     #aparm138#0 = #aparm137#0;
     #aparm138#1 = #aparm137#1;
     #aparm138#2 = "zzzzz";
     #aparm138#3 = 999999;
|FPRC;

|PROCESO VerFactura;
     |PUSHV 0501 2380;
     |CLS;

     |DDEFECTO #agifa134;
     #agifa134#49 = #137#0;
     #agifa134#0 = #137#1;
     |LEE 000#agifa134.=;
     |SI FSalida ! 0;
          aMensaje = "0000Error: No se puede leer la factura";
          |MENAV aMensaje;
          |ACABA;
     |FINSI;

     |PINPA #0#1;
     |PINDA #0#1;
     ser_fac = #1#49;
     num_fac = #1#0;
     aEmp = #48#0;

     |PTEC 802;
     PRMNT_aHisto = ser_fac + num_fac;
     aEjecuta = ":dscomer9/agifa134.tab agif0041 " + aEmp;
     |SUB_EJECUTA_NP aEjecuta;
     PRMNT_aHisto = "";

     |POPV;
|FPRC;

|PROCESO VerCliente;
     |PUSHV 0501 2380;
     |CLS;

     |DDEFECTO #agifa024;
     #agifa024#0 = #137#2;
     |LEE 000#agifa024.=;
     |SI FSalida ! 0;
          aMensaje = "0000Error: No se puede leer la ficha del cliente";
          |MENAV aMensaje;
          |ACABA;
     |FINSI;

     |PINPA #0#agifa024;
     |PINDA #0#agifa024;
     aEmp = #48#0;
     |PTEC 802;
     PRMNT_aHisto = #137#2;
     aEjecuta = ":dscomer9/agifa024.tab agif0041 " + aEmp;
     |SUB_EJECUTA_NP aEjecuta;
     PRMNT_aHisto = "";

     |POPV;
|FPRC;

|PROCESO EnlaceERP;
     |DBASS aFicERP;
     |QBF aFicERP;
     aDSERP = aFicERP + "dserp/def/dserpm01.mas";
     |XABRE "E", aDSERP, 0;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |DBASE aBase;
     |QBF aBase;
     aPathDef = aBase + "def/";

     |DFICE aPathFic;
     |QBF aPathFic;


     aClave = #aparm138#2 + ("000000" + #aparm138#3) % -106;
     |QBF aClave;
     aAlfa = (aClave + "                                                                                                    ") % 199;
     aClave = aAlfa;
     aAlfa = (aClave + "                                                                                                    ") % 10001;
     aClave = aClave + aAlfa;

     aAplica = "dscomer9";
     aNomDef = "agifa010";
     nEmpresa = #48#0;
     aEmpresa = ("00000" + nEmpresa) % -105;

     aParametro = aAplica + aEmpresa + aNomDef + aClave + aPathDef + "@" + aPathFic;
     PRMNT_autoERP = "AUTODOCS";

     aAlfa      = ":dserp/dserpm08;" + aParametro;
     |SUB_EJECUTA aAlfa;

     PRMNT_autoERP = "";
|FPRC;

|PROCESO Boton1; ||Edita Documentos (DSERP)
     aSerieAra = #aparm137#0;
     nNumAra = #aparm137#1;

     |PARA; |SI; |HACIENDO;
          |CONSULTA_F_CLAVE #1#aparm138, Baja138, Alta138;
          |SI FSalida = 0;
               |HAZ EnlaceERP;
               #agifa134#49 = aSerieAra;
               #agifa134#0 = nNumAra;
               |LEE 000#agifa134.=;
               |SI FSalida = 0;
                    |HAZ agifa134;
               |FINSI;
          |SINO;
               |SAL;
          |FINSI;
     |SIGUE;

     |PTEC 807;
|FPRC;

|PROCESO Boton2; ||Consulta factura
     aSerieAra = #aparm137#0;
     nNumAra = #aparm137#1;

     |HAZ VerFactura;

     |PTEC 807;
|FPRC;

|PROCESO Boton3; ||Consulta Cliente
     aSerieAra = #aparm137#0;
     nNumAra = #aparm137#1;

     |HAZ VerCliente;

     |PTEC 807;
|FPRC;

|PROCESO Boton4; ||Vista Previa -- Imprimir
     aSerieAra = #aparm137#0;
     nNumAra = #aparm137#1;

     |DDEFECTO #agifa134;
     #agifa134#49 = #137#0;
     #agifa134#0 = #137#1;
     |LEE 000#agifa134.=;
     |SI FSalida ! 0;
          aMensaje = "0000Error: No se puede leer la factura";
          |MENAV aMensaje;
          |ACABA;
     |FINSI;

     ser_fac = #1#49;
     num_fac = #1#0;
     reimp = #1#45;
     eaOrdenado = #41#141;

     aPrograma = "agifa136";

     |SUB_EJECUTA_NP aPrograma;

     |PTEC 807;
|FPRC;

|PROCESO Boton5; ||Quitar de la seleccion
     aSerieAra = #aparm137#0;
     nNumAra = #aparm137#1;

     |BORRA 020#aparm137.a;

     #aparm137#0 = aSerieAra;
     #aparm137#1 = nNumAra;
     |LEE 000#aparm137.];
     |SI FSalida ! 0;
          |LEE 000#aparm137.u;
     |FINSI;
     aSerieAra = #aparm137#0;
     nNumAra = #aparm137#1;

     |PTEC 807;
|FPRC;

|PROCESO CargaDef;
     |DBASS aBass;
     |QBF aBass;
     aErp = aBass + "dserp/def/dserpm09.mas";
     |XABRE "E", aErp, 0;
     |SI FSalida ] 0;
          |CARGA_DEF aErp, otroref@;
          |SI FSalida = 0;
                 aPathErp = aBass + "dserp/fich/";
                 |PATH_DAT #999 aPathErp;
                 nSwCargaDef = 1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO TraeteDSBrowserPrint;
     |DBASE aBase;
     |QBF aBase;

     nError = 0;

     aOrigen = aBase + "plugins/dsbrowserprint.exe";
     |XABRE "E", aOrigen, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     aDestino = "C:\DOCUMENTOS\DSCOMER9\";
     |RMKDIR aDestino;

     aDestino = "C:\DOCUMENTOS\DSCOMER9\dsbrowserprint.exe";

     aContiene1 = aDestino;
     |ESPECIFICA "REMOTOMD5", aContiene;
     aDocDestino = FSalida;  |QBF aDocDestino;

     aContiene1 = aOrigen;
     |ESPECIFICA "MD5", aContiene;
     aDocOrigen = FSalida;  |QBF aDocOrigen;

     |SI aDocDestino ! aDocOrigen;  |O aDocDestino = "";
         aIPRemoto = "";
         |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aOrigen, aDestino;
         |SINO;
             |COPIA_FICHERO aOrigen, aDestino;
         |FINSI;
     |FINSI;

     aAlfa = "C:\DOCUMENTOS\DSCOMER9\dsbrowserprint.exe";
     |XABRE "EC", aAlfa, nHandle;
     |SI FSalida < 0;
         |MENAV "0000 No puedo instalar el Selector de Impresoras (dsbrowserprint.exe)";
         nError = 1;
     |FINSI;
|FPRC;

|PROCESO BotonImpresora;
     |HAZ TraeteDSBrowserPrint;

     aDestino = "C:\DOCUMENTOS\DSCOMER9\impresora.sel";

     aEjecuta = "C:\DOCUMENTOS\DSCOMER9\dsbrowserprint.exe";
     aEjecuta = aEjecuta + " " + "C:\DOCUMENTOS\DSCOMER9\impresora.sel";
     |RSYSTEM aEjecuta;

     aImpresora = "";
     aAlfa = "C:\DOCUMENTOS\DSCOMER9\impresora.sel";
     |XABRE "CU", aAlfa, nHandle;
     |SI FSalida < 0;
          aImpresora = "";
     |SINO;
          |XLEE nHandle, 250, aLinea;
          |SI FSalida ] 0;
               aImpresora = aLinea;
          |SINO;
               aImpresora = "";
          |FINSI;
     |FINSI;
     |XCIERRA nHandle;

     |QBF aImpresora;
     aMensaje = "0000Impresora [" + aImpresora + "] seleccionada";
     ||MENAV aMensaje;

     |SI aImpresora ! "";
          |DDEFECTO #aparm135;
          #aparm135#0 = aIPRemoto;
          |LEE 101#aparm135.=;
          |SI FSalida ! 0; |GRABA 020#aparm135.b; |FINSI;

          #aparm135#1 = aImpresora;
          |GRABA 020#aparm135.a;
          |LIBERA #aparm135;

          |HAZ PintaImpresora;
     |FINSI;
     ||ARA
|FPRC;

|PROCESO BotonImprimirUna;
     |QBF eaImpresora;
     |SI eaImpresora = "";
          aMensaje = "0000Primero seleccione la impresora destino";
          |MENAV aMensaje;
          |ACABA;
     |FINSI;

     aSerieAra = #aparm137#0;
     nNumAra = #aparm137#1;

     |DDEFECTO #agifa134;
     #agifa134#49 = #137#0;
     #agifa134#0 = #137#1;
     |LEE 000#agifa134.=;
     |SI FSalida ! 0;
          aMensaje = "0000Error: No se puede leer la factura";
          |MENAV aMensaje;
          |ACABA;
     |FINSI;

     ser_fac = #1#49;
     num_fac = #1#0;
     enNumDocErp = #137#8;

     aPrograma = "aparz136;NOMEQUITESQUETECORTOLOSHUEVOSSIOSIAVERSINOPETAYA";
     |SUB_EJECUTA_NP aPrograma;
     ||ARA

     ser_fac = "";
     num_fac = 0;
     |PTEC 807;

     |PTEC 806;
     |PAUSA;

     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;

     |PTEC 806;
     |PAUSA;
|FPRC;

|PROCESO BotonImprimirTodo;
     |QBF eaImpresora;
     |SI eaImpresora = "";
          aMensaje = "0000Primero seleccione la impresora destino";
          |MENAV aMensaje;
          |ACABA;
     |FINSI;

     ser_fac = "";
     num_fac = 0;

     ||TODO CCC
     ||Le paso el nombre del temporal de las facturas que tiene que imprimir
     ||ARA66
     eaFichero = aTempo137;

     aPrograma = "aparz136;NOMEQUITESQUETECORTOLOSHUEVOSSIOSIAVERSINOPETAYA";
     |SUB_EJECUTA_NP aPrograma;
     ||ARA
     ser_fac = "";
     num_fac = 0;

     |PTEC 806;
     |PAUSA;

     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;

     |PTEC 806;
     |PAUSA;
|FPRC;


|PROCESO BotonSalir;
     |PTEC 807;
     nSwSalir = 1;
|FPRC;

|PROCESO PintaImpresora;
     |DDEFECTO #aparm135;
     #aparm135#0 = aIPRemoto;
     |LEE 000#aparm135.=;
     |SI FSalida = 0;
          aImpresoraWin = #aparm135#1;
          aImpresoraWin = aImpresoraWin % 136;
          |PINTA 2402, aImpresoraWin;

          eaImpresora = aImpresoraWin;
     |SINO;
          |PINTA 2402, "* * SIN IMPRESORA * * ";
          eaImpresora = "";
     |FINSI;
|FPRC;

|PROGRAMA;
     |IP_REMOTO aIPRemoto;
     |QBT aIPRemoto;

     |ABRE #41;
     |LEE 000#41p;
     |SI FSalida ! 0;
          |DDEFECTO #41;
     |FINSI;
     |CIERRA #41;

     |CLS;
     |CABEZA "Imp.Facturas";
     |DDEFECTO #0;

     ||TODO CCC
     #0#6 = "S";

     |ABRE #aparm025;
     |DDEFECTO #aparm025;
     |LEE 000#aparm025.p;
     |CIERRA #aparm025;

     eaSwFullDuplex = #aparm025#6;

     #aparm136#49 = eaSwFullDuplex;

     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;

          eaSwFullDuplex = #aparm136#49;

          |ABRE #aparm025;
          |DDEFECTO #aparm025;
          |LEE 101#aparm025.p;
          |SI FSalida ! 0; |GRABA 020#aparm025.b; |FINSI;
          #aparm025#6 = eaSwFullDuplex;
          |GRABA 020#aparm025.a;
          |LIBERA #aparm025;
          |CIERRA #aparm025;


          nSwCargaDef = 0;
          |HAZ CargaDef;

          ||Tema de Decimales Divisas.
          |HAZ Decimales;

          |HAZ CreaTempo; aTempo137 = Tempo;
          |NOME_DAT #137 aTempo137; |DELINDEX #137;

          |HAZ CreaTempo; aTempo138 = Tempo;
          |NOME_DAT #138 aTempo138; |DELINDEX #138;

          |ABRET #137;
          |ABRET #138;
          |ABRET #agifa134;
          |ABRET #agifa024;
          |ABRET #aparm135;

          |ABRE #agifa010;
          nSwHay = 0;
          |BUCLE agifa134;
          |SI nSwHay = 0;
               aMensaje = "0000Seleccion Vacia";
               |MENAV aMensaje;
          |SINO;

               |PUSHV 0501 2380;
               |PINPA #0#137;
               nPanta = FSalida;

               |HAZ PintaImpresora;

               |CONTROL_SIMPLE nPanta, "BOTON, [F2] Documentos", 2201, 2213, Boton1;
               nBoton1 = FSalida;

               |CONTROL_SIMPLE nPanta, "BOTON, [F3] Ver Factura", 2214, 2226, Boton2;
               nBoton2 = FSalida;

               |CONTROL_SIMPLE nPanta, "BOTON, [F4] Ver Cliente", 2227, 2239, Boton3;
               nBoton3 = FSalida;

               |CONTROL_SIMPLE nPanta, "BOTON, [F5] Vista Previa", 2240, 2252, Boton4;
               nBoton4 = FSalida;

               |CONTROL_SIMPLE nPanta, "BOTON, [F6] Imprimir Fact.", 2253, 2265, BotonImprimirUna;
               nBoton6 = FSalida;

               |CONTROL_SIMPLE nPanta, "BOTON, [F7] Quitar Fact.", 2266, 2279, Boton5;
               nBoton5 = FSalida;

               |CONTROL_SIMPLE nPanta, "BOTON,Cambiar Impresora", 2440, 2452, BotonImpresora;
               nBotonImpresora = FSalida;

               |CONTROL_SIMPLE nPanta, "BOTON,Imprimir Todas", 2453, 2465, BotonImprimirTodo;
               nBotonImprimir = FSalida;

               |CONTROL_SIMPLE nPanta, "BOTON,{{001}} [F10] Salir", 2468, 2479, BotonSalir;
               nBotonSalir = FSalida;

               nSwSalir = 0;
               |PARA; |SI; |HACIENDO;
                    |HAZ PintaImpresora;
                    |ENTLINEAL #1#137, -1, 4, 21, 1, min137, max137;
                    |SI nSwSalir = 1;
                         |SAL;
                    |FINSI;
               |SIGUE;

               |FIN_CONTROL_WINDOWS nBoton1;
               |FIN_CONTROL_WINDOWS nBoton2;
               |FIN_CONTROL_WINDOWS nBoton3;
               |FIN_CONTROL_WINDOWS nBoton4;
               |FIN_CONTROL_WINDOWS nBoton5;
               |FIN_CONTROL_WINDOWS nBoton6;
               |FIN_CONTROL_WINDOWS nBotonSalir;
               |FIN_CONTROL_WINDOWS nBotonImpresora;
               |FIN_CONTROL_WINDOWS nBotonImprimir;

               |POPV;
               |SI nSwCargaDef = 1;
                    |DESCARGA_DEF #otroref@;
               |FINSI;
          |FINSI;
          |CIERRA #agifa010;

          |CIERRAT #aparm135;
          |CIERRAT #agifa024;
          |CIERRAT #agifa134;
          |CIERRAT #137;
          |CIERRAT #138;

          |DELINDEX #137; Tempo = aTempo137; |HAZ BoraTempo;
          |DELINDEX #138; Tempo = aTempo138; |HAZ BoraTempo;
     |FINSI;
|FPRO;
