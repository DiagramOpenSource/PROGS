|FICHEROS;
     vigz0035 #0;
     vigm0002 #2; || Formulas
     vigm0003 #3;
     vigm0004 #4; || Captura
     vigm0005 #5;
     agifa017 #17;
     vigm0019 #19; ||Orden
     vigm0020 #20;
     dsm00024 #24;
     vigm0028 #28; ||Entrada producto
     vigm0033 #33;
     vigm0034 #34;
     agifa065 #65;
     agifa019 #119;
     vigw0020 #200,MANTE;
     vigz0035@;
|FIN;

|VARIABLES;
     &Tempo = "";
     nUniMulti = 0;
     aTmp0 = "";
     aTmp200 = "";
     i = 0;
     nHay = 0;
     nError = 0;
     aAlfa = "";
     nLin = 0;
     nOtraUni = 0;
     nUni = 0;
     aPartida = "";
     nNume = 0;
     nPrimeNue = 0;
|FIN;

|PROCESO SacaParti;
     |SI #24#31 > 0; |Y nUni > 0;
          aPartida = #24#2;
          |SI #24#31 > nUni; || Dispo
               #24#31 = #24#31 - nUni;
               #24#22 = #24#22 - nUni;
               #24#11 = #24#11 - nUni;
               nUni = 0;
          |SINO;
               nOtraUni = #24#31;
               #24#31 = #24#31 - nNume;
               #24#22 = #24#22 - nNume;
               #24#11 = #24#11 - nNume;
               nUni = nUni - nOtraUni;
          |FINSI;
          |GRABA 020#24a;
     |FINSI;
     |LIBERA #24;
|FPRC;

|DEFBUCLE SacaParti;
     #24#1;
     4;
     #0#2, #0#17, "                              ", "01.01.0000";
     #0#2, #0#17, "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz", "31.12.9999";
     be;
     NULL, SacaParti;
     #24#4;
|FIN;

|PROCESO SacaMate;
     nUni = (#0#14 * #5#4 / #4#3) ! 4;

     |DDEFECTO #119;
     #119#0 = #5#2;
     |LEE 101#119=;
     |SI FSalida ! 0; |GRABA 020#119b; |FINSI;
     |DDEFECTO #17;
     #17#0 = #5#2;
     #17#1 = #5#6;
     |LEE 101#17=;
     |SI FSalida ! 0; |GRABA 020#17b; |FINSI;

     #119#6 = #119#6 - nUni;
     #119#104 = #119#104 - nUni;

     #17#5 = #17#5 - nUni;
     #17#39 = #17#39 - nUni;

     aPartida = "";
     |BUCLE SacaParti;
  ||   |SI nUni > 0;
          |DDEFECTO #24;
          #24#0 = #5#2;
          #24#1 = #5#6;
          #24#2 = aPartida;
          |LEE 101#24=;
          |SI FSalida ! 0; |GRABA 020#24b; |FINSI;
          #24#31 = #24#31 - nUni;
          #24#22 = #24#22 - nUni;
          #24#11 = #24#11 - nUni;
          |GRABA 020#24a;
          |LIBERA #24;
 ||    |FINSI;

     #24#9 = #24#9 + #0#14;
     #24#22 = #24#22 + #0#14;
     |HAZ ValoraStock;
     |GRABA 020#119a;
     |GRABA 020#17a;
     |GRABA 020#24a;
     |LIBERA #119;
     |LIBERA #17;
     |LIBERA #24;
|FPRC;

|DEFBUCLE SacaMate;
     #4#1;
     ;
     #0#12;
     #0#12;
     ;
     NULL, NULL, SacaMate;
|FIN;

|PROCESO EntraProdu;
     #28#0 = #0#7;
     #28#1 = #0#0;
     #28#2 = #0#1;
     #28#3 = 999;
     |LEE 000#28];
     |SI FSalida = 0;
          |LEE 000#28a;
     |SINO;
          |LEE 000#28u;
     |FINSI;
     |SI FSalida = 0; |Y #28#0 = #0#7; |Y #28#1 = #0#0; |Y #28#2 = #0#1;
          nLin = #28#3 + 1;
     |SINO;
          nLin = 1;
     |FINSI;
     |DDEFECTO #28;
     #28#0 = #0#7;
     #28#1 = #0#0;
     #28#2 = #0#1;
     #28#3 = nLin;
     #28#4 = #0#12;
     #28#5 = #0#13;
     #28#6 = #0#14;
     #28#7 = #0#15;
     #28#8 = #0#17;
     |GRABA 020#28n;

     |DDEFECTO #119;
     #119#0 = #0#2;
     |LEE 000#119=;

     |DDEFECTO #65;
     #65#0 = #0#2;
     #65#1 = #0#13;
     #65#2 = "FB";
     #65#3 = #0#0;
     #65#4 = #0#1;
     #65#11 = #0#7;
     |LEE 101#65=;
     |SI FSalida ! 0; |GRABA 020#65b; |FINSI;
     #65#5 = #0#17;
     #65#6 = #0#14;
     #65#7 = #119#6 + #0#14;
     #65#8 = #119#9;
     #65#9 = #119#9;
     #65#10 = "";
     #65#14 = #119#9;
     #65#18 = #0#15;
     |GRABA 020#65a;
     |LIBERA #65;

     |DDEFECTO #119;
     #119#0 = #0#2;
     |LEE 101#119=;
     |SI FSalida ! 0; |GRABA 020#119b; |FINSI;
     |DDEFECTO #17;
     #17#0 = #0#2;
     #17#1 = #0#14;
     |LEE 101#17=;
     |SI FSalida ! 0; |GRABA 020#17b; |FINSI;
     |DDEFECTO #24;
     #24#0 = #0#2;
     #24#1 = #0#14;
     #24#2 = #0#15;
     |LEE 101#24=;
     |SI FSalida ! 0;
          #24#4 = #0#13;
          |GRABA 020#24b;
     |FINSI;

     #119#13 = #119#13 - (#0#14 + #0#16);
     #119#6 = #119#6 + #0#14;
     #119#104 = #119#104 + #0#14;

     #17#9 = #17#9 - (#0#14 + #0#16);
     #17#5 = #17#5 + #0#14;
     #17#39 = #17#39 + #0#14;

     #24#9 = #24#9 + #0#14;
     #24#22 = #24#22 + #0#14;
     |HAZ ValoraStock;
     |GRABA 020#119a;
     |GRABA 020#17a;
     |GRABA 020#24a;
     |LIBERA #119;
     |LIBERA #17;
     |LIBERA #24;
|FPRC;

|PROCESO TmpAct;
     |SI #0#14 = 0; |Y #0#16 = 0;
          |ACABA;
     |FINSI;

     #20#7 = #0#7;
     #20#0 = #0#0;
     #20#1 = #0#1;
     |LEE 121#20=;
     |SI FSalida = 0;
          #20#5 = #20#5 + #0#14;
          |SI #0#16 ! 0; || Hay unidades despreciadas
               #20#11 = "S";
          |FINSI;
          |SI #20#5 ] #20#4; || Estan todas fabricadas
               #20#11 = "S";
          |FINSI;
          |HAZ EntraProdu;
          |BUCLE SacaMate;
          |GRABA 020#20a;
          |LIBERA #20;

          #4#0 = #0#12;
          |LEE 101#4=
          |SI FSalida = 0;
                #4#9 = #4#9 + #0#14;
                |SI #4#9 = #4#3;
                    #4#7 = "S";
                |FINSI;
                |GRABA 020#4a;
                |LIBERA #4;
          |FINSI;
     |FINSI;

     ||----Granuladora
     |DDEFECTO #33;
     #33#0 = #0#13;
     |LEE 000#33=;
     |SI FSalida ! 0; |GRABA 020#33n; |FINSI;
     #34#0 = #33#0;
     #34#1 = 999;
     |LEE 000#34];
     |SI FSalida = 0;
          |LEE 000#34a;
     |SINO;
          |LEE 000#34u;
     |FINSI;
     |SI FSalida = 0; |Y #33#0 = #0#13;
          nLin = #34#1 + 1;
     |SINO;
          nLin = 1;
     |FINSI;
     |DDEFECTO #34;
     #34#0 = #33#0;
     #34#1 = nLin;
     #34#2 = #0#8;
     #34#3 = #0#10;
     #34#4 = #0#3;
     #34#8 = #0#14;
     |GRABA 020#34n;
|FPRC;

|DEFBUCLE TmpAct;
     #0#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, TmpAct;
|FIN;

|PROCESO Actualiza;
     |ABRET #24;
     |ABRE #17;
     |ABRE #19;
     |ABRE #20;
     |ABRE #28;
     |ABRE #24;
     |ABRE #17;
     |ABRE #119;
     |ABRE #65;
     |ABRE #4;
     |ABRE #33;
     |ABRE #34;
     |BUCLE TmpAct;
     |CIERRA #34;
     |CIERRA #33;
     |CIERRA #4;
     |CIERRA #65;
     |CIERRA #119;
     |CIERRA #17;
     |CIERRA #24;
     |CIERRA #28;
     |CIERRA #20;
     |CIERRA #19;
     |CIERRA #17;
     |CIERRAT #24;
|FPRC;

|PROCESO Min4;
     #4#7 = "N";
     #4#0 = 1;
|FPRC;

|PROCESO Max4;
     #4#7 = "N";
     #4#0 = 999999;
|FPRC;

|PROCESO Captu66; |TIPO 66
     |ABRE #4;
     #4#0 = #0#12;
     |LEE 000#4];
     |CONSULTA_F_CLAVE #2#4, Min4, Max4;
     |SI FSalida = 0;
          #0#12 = #4#0; |PINTA #0#12;
     |SINO;
          |ERROR; |ACABA;
     |FINSI;
     |CIERRA #4;
|FPRC;

|PROCESO CapturasPen;
     #vigz0035@#12 = #4#0;
     |LEE 000#vigz0035@.=;
     |SI FSalida = 0; |ACABA; |FINSI;

     |PARA i = 0; |SI i [ 9; |HACIENDO i = i + 1;
          #200i = #4i;
     |SIGUE;
     #200#10 = #4#3 - #4#9;
     #200#11 = "";
     |GRABA 020#200n;
|FPRC;

|DEFBUCLE CapturasPen;
     #4#2;
     ;
     "N", 0000000;
     "N", 9999999;
     ;
     NULL, CapturasPen;
|FIN;

|PROCESO CreaNuevas;
     #vigz0035@#7 = #0#7;
     #vigz0035@#0 = #0#0;
     #vigz0035@#1 = #0#1;
     #vigz0035@#19 = 999;
     |LEE 000#vigz0035@.];
     |SI FSalida = 0;
          |LEE 000#vigz0035@.a;
     |SINO;
          |LEE 000#vigz0035@.u;
     |FINSI;
     |SI FSalida = 0; |Y #vigz0035@#7 = #0#7; |Y #vigz0035@#0 = #0#0;
               |Y #vigz0035@#1 = #0#1;
          nLin = #vigz0035@#19 + 1;
     |SINO;
          nLin = 1;
     |FINSI;
     |SI nPrimeNue = 0;
          nPrimeNue = 1;
          #0#12 = #200#0;
          #0#13 = #200#4;
          #0#14 = #200#10;
          #0#15 = #200#12;
          #0#16 = 0;
          #0#17 = #4#5;
          |GRABA 020#0a;
     |SINO;
          |SALVA_DATOS #0;
          |REPON_DATOS #vigz0035@;
          #0#12 = #200#0;
          #0#13 = #200#4;
          #0#14 = #200#10;
          #0#15 = #200#12;
          #0#16 = 0;
          #0#17 = #4#5;
          #0#19 = nLin;
          |GRABA 020#0n;
     |FINSI;
|FPRC;

|DEFBUCLE CreaNuevas;
     #200#1;
     11;
     0000000, "*";
     9999999, "*";
     ;
     NULL, CreaNuevas;
|FIN;

|PROCESO Min200;
     #200#0 = 0;
|FPRC;

|PROCESO Max200;
     #200#0 = 9999999;
|FPRC;

|PROCESO MultiCaptura;
     |DDEF aAlfa;
     aAlfa = aAlfa + "vigz0035";
     |CARGA_DEF aAlfa, vigz0035@;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |NOME_DAT #vigz0035@#aTmp0#;

     |HAZ CreaTempo; aTmp200 = Tempo;
     |NOME_DAT #200 Tempo; |DELINDEX #200;

     |ABRE #vigz0035@;
     |SELECT #3#vigz0035@;
     |ABRE #200;
     |BUCLE CapturasPen;
     |LEE 000#200p;
     |SI FSalida = 0;
          |ENTLINEAL #1#200, 1, 4, 20, 1, Min200, Max200;
          nPrimeNue = 0;
          |SELECT #1#vigz0035@;
          |BUCLE CreaNuevas;
     |SINO;
          |MENAV "0000No hay capturas pendientes...";
          |ERROR;
     |FINSI;
     |CIERRA #200;

     |DELINDEX #200;

     |CIERRA #vigz0035@;
     |DESCARGA_DEF #vigz0035@;
|FPRC

|PROCESO MarcaCaptu; |TIPO 0;
     |SI FSalida = 0; |Y #200#0 = Contenido;
          |SI #200#11 = "*";
               #200#11 = "";
               #200#12 = "";
               |GRABA 020#200a;
               nUniMulti = nUniMulti + #200#10;
          |SINO;
               |ENCAMPO #12#200;
               |SI FSalida ! 0;
                    |ERROR; |ACABA;
               |SINO;
                    nUniMulti = nUniMulti - #200#10;
                    |SI nUniMulti < 0;
                         |MENAV "0000Se superan las unidades a fabricar...";
                         |ERROR; |ACABA;
                    |SINO;
                         #200#11 = "*";
                         |GRABA 020#200a;
                    |FINSI;
               |FINSI;
          |FINSI;
          |PINTA #200#11;
          |PINTA #200#12;
     |FINSI;
|FPRC;

|PROCESO Captura; |TIPO 0;
     |SI FSalida = 13;   || F5;
          nUniMulti = #0#4 - #0#18;
          |PUSHV 0501 2380;
          |HAZ MultiCaptura;
          |POPV;
          |PONCONTROL 23, "SI";
          |PTEC 807;
          |ACABA;
     |FINSI;

     |ABRE #4;
     #4#0 = #0#12;
     |LEE 000#4=;
     |SI FSalida = 0; |Y #4#7 = "S";
          aAlfa = "0000Captura actualizada.";
          |MENAV aAlfa;
          |ERROR; |ACABA;
     |FINSI;
     |CIERRA #4;

     |ABRE #4;
     #4#0 = #0#12;
     |LEE 000#4=;
     |SI FSalida = 0;
          #0#13 = #4#4;
          #0#14 = #4#3 - #4#9;
          #0#17 = #4#5;
        ||  #0#16 = #0#4 - #0#18 - #0#14;
        ||  |SI #0#16 < 0; #0#16 = 0; |FINSI;
          |PINTA #0#13;
          |PINTA #0#14;
          |PINTA #0#16;
          |PINTA #0#17;
     |SINO;
          |SI #0#12 ! 0;
               |MENAV "0000No existe captura...";
               |ERROR;
          |SINO;
               #0#13 = ~; |PINTA #0#13;
          |FINSI;
     |FINSI;
     |CIERRA #4;
|FPRC;

|PROCESO Despreciar; |TIPO 0;
     nNume =  #0#18 + #0#14 + #0#16;
     |SI nNume > #0#4;
          |MENAV "0000Se superan las unidades a fabricar...";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Unidades; |TIPO 0;
     nNume = #0#18 + #0#14;
     |SI nNume > #0#4;
          |MENAV "0000Se superan las unidades a fabricar...";
          |ERROR;
     |SINO;
          |SI #0#12 = 0;
               #0#16 = #0#4 - #0#18 - #0#14;
               |SI #0#16 < 0; #0#16 = 0; |FINSI;
               |PINTA #0#16;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tecla; |TIPO 0;
     |SI #0Campo = Contenido; |Y FSalida = 0;
        |ET E0;
          |ENCAMPO #12#0;
          |SI FSalida = 7; |VETE Sal; |FINSI;
        |ET E1;
          |ENCAMPO #13#0;
          |SI FSalida = 7; |VETE Sal; |FINSI;
          |SI FSalida = 2; |VETE E0; |FINSI;
        |ET E2;
          |ENCAMPO #14#0;
          |SI FSalida = 7; |VETE Sal; |FINSI;
          |SI FSalida = 2; |VETE E1; |FINSI;
        |ET E3;
          |ENCAMPO #15#0;
          |SI FSalida = 2; |VETE E2; |FINSI;
        |ET E4;
          |ENCAMPO #16#0;
          |SI FSalida = 7; |VETE Sal; |FINSI;
          |SI FSalida = 2; |VETE E3; |FINSI;
        |ET E5;
          |ENCAMPO #17#0;
          |SI FSalida = 7; |VETE Sal; |FINSI;
          |SI FSalida = 2; |VETE E4; |FINSI;
        |ET Sal;
          |SI FSalida ! 7;
               |GRABA 020#0a;
          |SINO;
               |LEE 000#0a;
               |PINDA #0#0;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Ordenes;
     |SI #20#11 = "N";
          |DDEFECTO #0;
          |PARA i = 0; |SI i [ 10; |HACIENDO i = i + 1;
               #0i = #20i;
          |SIGUE;
          #0#11 = #19#2;
          #0#13 = "01.01.0000";
          #0#17 = #20#6;
          #0#18 = #20#5;
          |GRABA 020#0n;
          nHay = 1;
     |FINSI;
|FPRC;

|DEFBUCLE Ordenes;
     #19#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, NULL, Ordenes;
|FIN;

|PROCESO Min;
     #0#7 = "     ";
     #0#0 = 1;
     #0#2 = " " * 20;
|FPRC;

|PROCESO Max;
     #0#7 = "zzzzz";
     #0#0 = 999999;
     #0#2 = "z" * 20;
|FPRC;

|PROGRAMA;
     |CLS;
     |HAZ CreaTempo; aTmp0 = Tempo; |NOME_DAT #0#Tempo#; |DELINDEX #0;
     |ABRE #0;
     |BUCLE Ordenes;
     |CIERRA #0;
     |SI nHay = 0;
          |MENAV "0000No hay ordenes pendientes...";
     |SINO;
          |PINPA #0#0;
          |ENTLINEAL #2#0, 3, 4, 20, 0, Min, Max;
          |CONFI "2400NActualizar entrada";
          |SI FSalida = 0;
               |HAZ Actualiza;
          |FINSI;
     |FINSI;

     Tempo = aTmp0; |HAZ BoraTempo; |DELINDEX #0;
|FPRO;
