|FICHEROS;
     desan020 #0;
     agifa017 #17;
     agifa029 #29;
     agifa019 #19,MANTE;
     desan045 #45;
     agifa067 #67;
     referen@;
|FIN;

|VARIABLES;
     &Tempo = "";
     nPdte = 0;
     i = 0;
     nNum = 0;
     nSw = 0;
     nAlma = 0;
     aLoca = "";
     nPunto = 0;
     nMini = 0;
     nMaxi = 0;
     aAlfa = "";
|FIN;

|PROCESO desan020_N;
     #19#0 = #0#1;
     |LEE 020#19=;

     #17#0 = #0#0;
     #17#1 = 0;
     |LEE 000#17];
     |PARA; |SI FSalida = 0; |Y #17#0 = #0#0; |HACIENDO;
          #29#0 = #17#1;
          |LEE 000#29=;
          |SI FSalida ! 0; |DDEFECTO #29; |FINSI;

          |SALVA_FICHA #17;
          |DDEFECTO #referen@;
          #referen@#0 = #19#0;
          #referen@#1 = #17#1;
          |LEE 101#referen@.=;
          |SI FSalida = 0;
/* modificado tiquet 536 */
               #referen@#6 = #17#6;
               #referen@#7 = #17#7;
/**/
               #referen@#40 = #19#1;
               #referen@#41 = #29#1;
               |GRABA 020#referen@.a;
          |SINO;
               #referen@#2 = #17#2;
               #referen@#6 = #17#6;
               #referen@#7 = #17#7;
               #referen@#40 = #19#1;
               #referen@#41 = #29#1;
               #referen@#49 = #17#49;
               |GRABA 020#referen@.n;
          |FINSI;
          |LIBERA #referen@;

          |REPON_FICHA #17;
          |LEE 000#17s;
     |SIGUE;
|FPRC;

|DEFBUCLE desan020_N;
     #0#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, desan020_N;
|FIN;

|PROCESO agifa067;
     nPdte = #67#5 - #67#38;
     |SI nPdte [ 0; |ACABA; |FINSI;

     #19#0 = #0#0;
     |LEE 121#19=;
     |SI FSalida = 0;
          |DDEFECTO #17;
          #17#0 = #0#0;
          #17#1 = #67#3;
          |LEE 101#17=;
          |SI FSalida ! 0; |GRABA 020#17b; |FINSI;

          #19#16 = #19#16 - nPdte;
          #17#43 = #17#43 - nPdte;
          |HAZ ValoraStock;
          |GRABA 020#19a;
          |GRABA 020#17a;
          |LIBERA #19;
          |LIBERA #17;
     |FINSI;


     #19#0 = #0#1;
     |LEE 121#19=;
     |SI FSalida = 0;
          |DDEFECTO #17;
          #17#0 = #0#1;
          #17#1 = #67#3;
          |LEE 101#17=;
          |SI FSalida ! 0;
               #17#43 = 0;
               |GRABA 020#17b;
          |FINSI;

          #19#16 = #19#16 + nPdte;
          #17#43 = #17#43 + nPdte;
          |HAZ ValoraStock;

          |GRABA 020#17a;
          |GRABA 020#19a;
          |LIBERA #17;
          |LIBERA #19;

          #67#2 = #0#1;
          #67#4 = #19#1;
          |GRABA 020#67a;
     |FINSI;
     |LIBERA #67;

|FPRC;

|DEFBUCLE agifa067;
     #67#3;
     ;
     #0#0, "     ", 000001, "      ";
     #0#0, "zzzzz", 999999, "zzzzzz";
     be;
     NULL, agifa067;
|FIN;

|PROCESO desan020;
     |BUCLE agifa067;

     |LEE 000#45u;
     |SI FSalida = 0;
          nNum = #45#0 + 1;
     |SINO;
          nNum = 1;
     |FINSI;
     #45#0 = nNum;
     #45#1 = #0#0;
     #45#2 = #0#1
     #45#3 = Usuario % 810;
     #45#4 = ~;
     |GRABA 020#45n;
|FPRC;

|DEFBUCLE desan020;
     #0#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, desan020;
|FIN;

|PROCESO Sustituye;
     |ABRE #19;
     |ABRE #17;
     |ABRE #45;
     |BUCLE desan020;

     |ABRE #29;
     |DDEF aAlfa;
     aAlfa = aAlfa + "agifa017";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |ABRE #referen@;
          |BUCLE desan020_N;
          |CIERRA #referen@;
          |DESCARGA_DEF #referen@;
     |FINSI;
     |CIERRA #29;

     |CIERRA #45;
     |CIERRA #17;
     |CIERRA #19;
|FPRC;

|PROCESO Nuevo; |TIPO 6;
     |SI #0#0 = #0#1;
          |MENAV "0000Es el mismo articulo...";
          |ERROR; |ACABA;
     |FINSI;
     nSw = 0;
     |ABRE #19;
     #19#0 = #0#0;
     |LEE 000#19=;
     #19#0 = #0#1;
     |LEE 000#19=;
     |SI FSalida ! 0;
          nSw = 1;
          #19#15 = 0;
          #19#16 = 0;
          #19#182 = 0;
          #19#6 = 0;
          #19#185 = 0;
          #19#104 = 0;
          #19#193 = 0;
          #19#10 = 0;
          |PARA i = 32; |SI i [ 101; |HACIENDO i = i + 1;
               #19i = 0;
          |SIGUE;
          |HAZ ValoraStock;
          |GRABA 020#19b;
     |FINSI;
     |PUSHV 0501 2380;
     |PINPA #0#19;
     |PINDA #0#19;
     |ENDATOS #1#19;
     |SI FSalida = 0;
          |GRABA 020#19a;
     |SINO;
          |SI nSw = 1; |BORRA 020#19c; |FINSI;
          |ERROR;
     |FINSI;
     |POPV;
     |CIERRA #19;
|FPRC;

|PROCESO Antes; |TIPO 6;
     |ABRE #19;
     #19#0 = #0#0;
     |LEE 000#19=;
     |SI FSalida ! 0;
          |MENAV "0000No existe articulo...";
          |ERROR;
     |FINSI;
     |CIERRA #19;
|FPRC;

|PROCESO Min;
     #0#0 = "                    ";
|FPRC;

|PROCESO Max;
     #0#0 = "zzzzzzzzzzzzzzzzzzzz";
|FPRC;

|PROGRAMA;
     |HAZ CreaTempo; |NOME_DAT #0 Tempo; |DELINDEX #0;
     |PINPA #0#0;
     |ENTLINEAL #1#0, 1, 1, 22, 0, Min, Max;
     |ABRE #0;
     |LEE 000#0p;
     |SI FSalida = 0;
          |CONFI "0000NSustituir articulos:";
          |SI FSalida = 0;
               |HAZ Sustituye;
          |FINSI;
     |FINSI;
     |CIERRA #0;
     |HAZ BoraTempo; |DELINDEX #0;
|FPRO;
