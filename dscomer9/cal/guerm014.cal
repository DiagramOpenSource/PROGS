|FICHEROS;
  guerm014 #0;       || Cabeceras
  agifa027 #3;           ||Contadores
  agifa024 #4;       || Clientes
  guerm005 #5;       || Control
  guerm004 #8;       || Camiones
  guerm009 #9;
|FIN;

|VARIABLES;
  fecha     = @;
  hora      = "";
  opcion    = 0;
  opcio     = 0;
  &tipo     = "";
  &sumalin  = 0;
  &codpos   = "";
  &descrip  = "";
  traba = "";
  nume = 0;
  x = 0;
  alfa = "";
  alf1 = "";
  &EURODB = 0;
  &estoyen = "";
     || MENU PARA INTRODUCIR EL CODIGO DEL CLIENTE DE FORMA AUTOMATICA.
 {-1}menu           = "";
     menu1          = "2400";
     menu2          = "1";
     menu3          = "Contador:  ";
     menu4          = "AM";
     menu5          = " Automatico ";
     menu6          = " Manual ";
|FIN;
---------------------------------------------
|PROCESO Pintar;
  |PRINT;
|FPRC;

|PROCESO impre; |TIPO 4;
  |IMPRE 0;
  |SI FSalida ! 0;  |ACABA;  |FINSI;
  nume = (#0#6 * 1000) + #0#7;
  traba = nume;
  traba = "000000" + traba
  codpos = traba % -105;
  |ABRE #5;
  |LEE 101#5p;
  |CIERRA #5;
  |INFOR "cantealm";
  |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;
  alfa = #0#18;
  alfa = "000000" + alfa;
  alf1 = alfa % -106;
  ||tipo = "          " + alf1;
  tipo = #0#40 + " " + alf1;
  |PARA x = 1;  |SI x [ #0#25;  |HACIENDO;  x = x + 1;
        |BUCLELIN 0Pintar#0;
        |PIEINF;
  |SIGUE;
  |FININF;
  |FINIMP;
|FPRC;


|PROCESO Menus2;
  |SI opcion ! 1;  |ACABA;  |FINSI;
  |MENU menu;
  |SI FSalida = 1;    ||Automatico
      |ABRE #5;
      |LEE 101#5p;
      #5#36 = #5#36 + 1;
      |GRABA 101#5a;
      #0#0 = #5#36;
      |PINTA #0#0;
      |CIERRA #5;
  |FINSI;
|FPRC;


|PROCESO Contador;
  |SI FSalida ! 13;  |ACABA;  |FINSI;
  |ABRE #5;
  |LEE 101#5p;
  #5#38 = #5#38 + 1;
  |GRABA 101#5a;
  #0#18 = #5#38;
  |PINTA #0#18;
  |CIERRA #5;
|FPRC;

|PROCESO tipo8;  |TIPO 8;
  |ABRE #5;
  |LEE 101#5p;
  |SI FSalida ! 0;  |DDEFECTO #5;  |FINSI;
  |CIERRA #5;
  estoyen = #5#25;

  |HAZ DecimalesBase;
|FPRC;

--------------------------------------------
|PROCESO tipo1;  |TIPO 1;
  |SI #0#37 = "S";
      |CONFI "2400NAlbaran pasado a Facturacion CONTINUAR";
      |SI FSalida ! 0;
          |PTEC 807; || Ctrl-C
          |ACABA;
      |FINSI;
      |MENAV "    LAS MODIFICACIONES NO SE REFLEJARAN EN FACTURACION";
  |SINO;
      |SI #0#36 = "S";
          |MENAV "     ALBARAN VERIFICADO, ANULADA SU VERIFICACION";
          #0#36 = "N";
          |PINTA #0#36;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO movi; |TIPO 7;
  opcion = (FEntrada / 100) ! 0;
  |SI opcion = 1;  |Y #5#25 = "C";
      |MENAV "0000No puede dar de alta";
      |PTEC 807; || Ctrl-C
      |ACABA;
  |FINSI;
  |SI opcion = 2;  |O opcion = 1;
      |CAMPO_MODIFICA #0#1,  1, "S";
      |CAMPO_MODIFICA #0#15, 1, "S";
      |CAMPO_MODIFICA #0#18, 1, "S";
      |CAMPO_MODIFICA #0#40, 1, "S";
  |SINO;
      |CAMPO_MODIFICA #0#1,  1, "N";
      |CAMPO_MODIFICA #0#15, 1, "N";
      |CAMPO_MODIFICA #0#18, 1, "N";
      |CAMPO_MODIFICA #0#40, 1, "N";
  |FINSI;
  |HAZ Menus2;
|FPRC;

|PROCESO consultando; |TIPO 7;
  opcio = (FEntrada / 100) ! 0;
  |SI opcio = 1;  |O opcio = 2;
      |SI #0#37 = "S";
         |CONFI "    Atencion ALBARAN PASADO A FACTURACION DESEA CONTINUAR";
      |FINSI;
  |FINSI;

  |SI opcio ! 2;  |Y opcio ! 1;
      |CAMPO_MODIFICA #0#1,  1, "N";
      |CAMPO_MODIFICA #0#15, 1, "N";
      |CAMPO_MODIFICA #0#18, 1, "N";
      |CAMPO_MODIFICA #0#40, 1, "N";
  |SINO;
      |CAMPO_MODIFICA #0#1,  1, "S";
      |CAMPO_MODIFICA #0#15, 1, "S";
      |CAMPO_MODIFICA #0#18, 1, "S";
      |CAMPO_MODIFICA #0#40, 1, "S";
  |FINSI;
|FPRC;

---------------------------------------------
|PROCESO elcliente; |TIPO 0;
  sumalin = #0#20;
  |CAMPO_MODIFICA #0#4,1,"S";
  |CAMPO_MODIFICA #0#5,1,"S";
  |CAMPO_MODIFICA #0#6,1,"S";
  |CAMPO_MODIFICA #0#7,1,"S";
  |CAMPO_MODIFICA #0#9,1,"S";
  |CAMPO_MODIFICA #0#10,1,"S";
  |SI #0#3 = 999999;
      |SI Contenido ! 999999;
          #0#4= "";
          #0#5= "";
          #0#6= "";
          #0#7= "";
          #0#9= "";
          #0#10= "";
          |PINTA #0#4;
          |PINTA #0#5;
          |PINTA #0#6;
          |PINTA #0#7;
          |PINTA #0#8;
          |PINTA #0#9;
          |PINTA #0#10;
      |FINSI;
      |HAZ leecamion;
      |ACABA;
  |FINSI;
  |ABRE #4;
  |DDEFECTO #4;
  #4#0 = #0#3;
  |LEE 101#4=;
  |SI FSalida ! 0;
      |ERROR;
  |SINO;
      #0#4 = #4#1;   || Nombre
      #0#5 = #4#8;   || Direccion
      #0#6 = #4#181; || Cp1
      #0#7 = #4#182; || Cp2
      #0#8 = #4#10;  || Provincia
      #0#9 = #4#9;   || Poblacion
      #0#10= #4#15;   || CIF
      |PINTA #0#4;
      |PINTA #0#5;
      |PINTA #0#6;
      |PINTA #0#7;
      |PINTA #0#8;
      |PINTA #0#9;
      |PINTA #0#10;
      |CAMPO_MODIFICA #0#4,1,"N";
      |CAMPO_MODIFICA #0#5,1,"N";
      |CAMPO_MODIFICA #0#6,1,"N";
      |CAMPO_MODIFICA #0#7,1,"N";
      |CAMPO_MODIFICA #0#9,1,"N";
      |CAMPO_MODIFICA #0#10,1,"N";
  |FINSI;
  |LIBERA #4;
  |CIERRA #4;
  |HAZ leecamion;
|FPRC;

|PROCESO leecamion;
  |ABRE #8;
  #8#0 = #0#11;
  |LEE 101#8=;
  |SI FSalida = 0;
      #0#35 = #8#6;
      |SI #8#7 = #0#3;
          #0#35 = "N";
      |FINSI;
      |PINTA #0#35;
  |FINSI;
  |CIERRA #8;
|FPRC;

|PROCESO antesde;  |TIPO 7;
  |SI #0#15 = "A";
      |CAMPO_MODIFICA #0#21, 1, "N";
  |SINO;
      |CAMPO_MODIFICA #0#21, 1, "S";
  |FINSI;
|FPRC;

|PROCESO despuesde;
  #0#22 = #0#20 - #0#21;
  #0#23 = ((#0#22 * #0#14) / 100) ! EURODB;
  #0#24 = #0#22 + #0#23;
  |SI #0#15 = "A";
      #0#21 = 0;
      #0#22 = 0;
      #0#23 = 0;
      #0#24 = 0;
  |FINSI;
  |SI #0#15 = "T";
      #0#23 = 0;
      #0#24 = #0#22;
  |FINSI;
  |PINTA #0#22;
  |PINTA #0#23;
  |PINTA #0#24;
|FPRC;
