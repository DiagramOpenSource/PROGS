|FICHEROS;
     sctrz085 #0;
     sctrm003 #3;
     sctrw014 #14;
     sctrm015 #15;
     sctrw025 #25;  ||Tmp lineas
     sctrm026 #26;
     sctrm036 #36;
     sctrm032 #32;
     sctrm042 #42;
     sctrm052 #52;
     sctrm053 #53;
     sctrm072 #72;
     sctrm074 #74;
     sctrm075 #75;
     sctrm001 #100;
     sctrw026 #126; ||Errores
     referen@;
|FIN;

|VARIABLES;
     aHora = "";
     nFSal = 0;
     &eaLog = "";
     &eaFicErr = "";
     &eaXml = "";
     &eaReservas = "";
     &eaMensa = "";
     &enError = 0;
     &enSwVuelta = 0;
     &enHay = 0;
     &Tempo = "";
     &eaBloqueado = "";
     &enPax = 0;
     &enLinea = 0;
     &eaSentido = "";
     &efFechaViaje = @;
     &eaNomLogo = "";
     &enPantaLogo = 0;
     aFicAnsi = "";
     aNom = "";
     aTmp126 = "";
     aReservas = "";
     aPrimeGrupo = "";
     nImpTasa = 0;
     nImpBillete = 0;
     nImpSupleI = 0;
     nImpSupleV = 0;
     aDivisa = "";
     aTmp14 = "";
     nLin = 0;
     nRese = 0;
     fHoy = @;
     {1}aLocal = "";
     nPanta = 0;
     aAlfa = "";
     aAlfa1 = "";
     aAlfa2 = "";
     aOrg = "";
     aIP = "";
     aBase = "";
     aDes = "";
     aLon = "";
     i = 0;
     nPos = 0;
     aCar = "";
     aExe = "";
     aFic = "";
     nHandle = 0;
     nHandleErr = 0;
     nReg = 0;
     nRem = 0;
     nNivel = 0;
     aVal = "";
     aTmp = "";
     aTag = "";
     aIniTag = "N";
     aFinTag = "N";
     aIniPro = "N";
     aFinPro = "N";
     aFinTagVacio = "N";
     aIniAtri = "N";
     aTagAtri = "";
     aResul = "";
     aLetra = "";
     RETORNO = "";
     nError = 0;
     nAviso = 0;
     {100}Matriz = "";
     aDer = "";
     aIzq = "";

     nSwGeneral = 0;
     nSwSalida = 0;
     nSwLlegada = 0;
     nSwCliente = 0;
     nSwLinea = 0;
     aMensa = "";

     aCliente = "";
     aInforma = "";
     aFecRese = "";
     aHoraRese = "";

     aFecSalIda = "";
     aHoraSalIda = "";
     aPobSalIda = "";
     aLugarSalIda = "";
     aFecLlegaIda = "";
     aHoraLlegaIda = "";
     aPobLlegaIda = "";
     aLugarLlegaIda = "";

     aFecSalVuelta = "";
     aHoraSalVuelta = "";
     aPobSalVuelta = "";
     aLugarSalVuelta = "";
     aFecLlegaVuelta = "";
     aHoraLlegaVuelta = "";
     aPobLlegaVuelta = "";
     aLugarLlegaVuelta = "";

     aApellido1 = "";
     aNombre1 = "";
     aTlf = "";
     aMail = "";
     aApellido = "";
     aNombre = "";
     nPrecio = "";
     aType = "";
     aPobla = "";
     aLugar = "";
     nHay = 0;
     nEsta = 0;
     nLinea = 0;
     aTmp25 = "";
     nNume = 0;
     nTotalImporte = 0;
     nHanleErr = 0;
     nCorreos = 0;
     aDir = "";
     aDSCorreo = "";
     aUsu = "";
     aPwd = "";
     aPop3 = "";
     nSw = 0;
     aBaseLocal = "";
     aPath = "";
     aTicket = "";
     &eaEsXml = "S";

     {90}Valor = "";
     aSepara = "";
     aQuote = "";
     aLF = "";
     aCR = "";
     aConQuote = "";
     aCampo = "";
     aGrupoTari = "";
     nPrecioCsv = 0;
     nErroresCsv = 0;
     nHayAvisosCsv = 0;
     nCar1 = 0;
     nCar2 = 0;
     nCar3 = 0;
|FINSI;

|PROCESO QuitaMarcaUrf8;
     || Si trae la marca de UTF8 = EB BB BF
     || Utf82Ansi los convierte a 80

     aCar = aTmp % 101; nCar1 = &aCar;
     aCar = aTmp % 201; nCar2 = &aCar;
     aCar = aTmp % 301; nCar3 = &aCar;
     |SI nCar1 = 128; |Y nCar2 = 128; |Y nCar3 = 128;
          aTmp = aTmp % 4;
     |FINSI;
|FPRC;

|PROCESO GrabaMensaCsv;
     aMensa = "Linea csv: " + nReg + ". " + aMensa;
     |HAZ GrabaMensa;
|FPRC;

|PROCESO GrabaReseCsv;
     fHoy = ~;
     |HORA aHora;

     aCliente = "888888";
     aFecRese = Valor1 % 902 +"."+ Valor1 % 602 +"."+ Valor1 % 104;
     aHoraRese = Valor1 % 1205;
     aFecSalIda = Valor2 % 902 +"."+ Valor2 % 602 +"."+ Valor2 % 104;
     aHoraSalIda = Valor2 % 1205;

     aPobSalIda = Valor3;
     aPobLlegaIda = Valor5;
     aGrupoTari = "01";

     aInforma = Valor9 + "/" + Valor10;
     aDivisa = "EUR";
     aNombre = Valor7;

     aAlfa = Valor8 % 5;
     nPrecioCsv = aAlfa;

     |ABRE #52;
     |ABRE #53;
     |DDEFECTO #52;
     |DDEFECTO #53;

     |SELECT #9#52;
     #52#16 = aInforma;
     |LEE 000#52=;
     |SI FSalida ! 0;
          |SELECT #1#52;
          #52#43 = aCliente;
          #52#16 = aInforma;
          #52#1 = aFecRese;
          #52#2 = aHoraRese;
          #52#20 = aFecSalIda;
          #52#21 = aHoraSalIda;
          #52#8 = aPobSalIda;
          #52#12 = aPobLlegaIda;
          #52#17 = 1;
          #52#19 = 1;
          #52#3 = "GOEURO";
          #52#4 = 1;
          #52#5 = "Portal Internet";
          #52#6 = 2;
          ||#52#45 =
          #52#94 = aDivisa;

          #53#1 = 1;
          #53#3 = aNombre;
          #53#11 = nPrecioCsv;
          #53#7 = aGrupoTari;

          |ABRE #3;
          #3#0 = #52#8;
          |LEE 000#3=;
          |SI FSalida = 0;
               #52#9 = #3#1;
               #52#10 = #3#2;
               #52#11 = #3#4;

               #3#0 = #52#12;
               |LEE 000#3=;
               |SI FSalida = 0;
                    #52#13 = #3#1;
                    #52#14 = #3#2;
                    #52#15 = #3#4;
               |SINO;
                    nError = 1;
                    aMensa = "0000No existe c¢digo de poblaci¢n.";
               |FINSI;
          |SINO;
               nError = 1;
               aMensa = "No existe c¢digo de poblaci¢n.";
          |FINSI;
          |CIERRA #3;

          |SI nError = 0;
               enSwVuelta = 0;
               |HAZ DatosSalida;
               |SI enHay = 0;
                    aMensa = "No existe trayecto para la salida / llegada.";
                    nError = 1;
               |SINO;
                    fHoy = ~;
                    |SI #52#20 ] fHoy;
                         enLinea = #52#75;
                         eaSentido = #52#101;
                         nNume = #52#20 - #52#99 + 1;
                         efFechaViaje = nNume;
                         |HAZ CtrlViajerosCheq;
                         |SI eaBloqueado = "S";
                              nError = 1;
                              aMensa = "Servicio bloqueado para esta fecha.";
                         |FINSI;
                         |SI enPax < #52#19;
                              nAviso = 1;
                              aMensa = "Se supera el pax disponible.";
                              |HAZ GrabaMensaCsv;
                         |FINSI;
                    |FINSI;
                    |SI nError = 0;
                         |ABRE #26;
                         #26#0 = #52#96;
                         #26#2 = #52#75;
                         #26#4 = #52#101;
                         #26#5 = #52#22;
                         nNume = #52#20 - #52#99 + 1;
                         #26#7 = nNume;
                         |LEE 000#26=;
                         |SI FSalida ! 0;
                              nError = 1;
                              aMensa = "0000No existe viaje para esa fecha.";
                         |SINO;
                              |SI #26#24 = "S";
                                   nError = 1;
                                   aMensa = "El servicio para ese dia esta suspendido.";
                              |FINSI;
                              |SI #26#24 = "C";
                                   nError = 1;
                                   aMensa = "El servicio para ese dia esta cerrado.";
                              |FINSI;
                              nNume = #26#20 - #26#26;
                              |SI nNume < #52#19;
                                   nAviso = 1;
                                   aMensa = "No hay Pax libres para ese viaje, desea incluirla la reserva en la lista de espera?";
                                   |HAZ GrabaMensaCsv;
                              |SINO;
                                   #52#52 = "P";
                              |FINSI;
                         |FINSI;
                         |CIERRA #26;
                    |FINSI;
               |FINSI;
          |FINSI;
          |SI nError = 0;
               |HAZ CreaTempo; |NOME_DAT #14 Tempo; aTmp14 = Tempo;
               |DELINDEX #14;
               |ABRE #14;
               |HAZ CargaTari;
               aPrimeGrupo = "";
               nHay = 0;
               |LEE 000#14p;
               |PARA; |SI FSalida = 0; |HACIENDO;
                    aAlfa = #14#0 % 102;

                    |SI aPrimeGrupo = "";
                         aPrimeGrupo = aAlfa;
                    |FINSI;

                    |SI aGrupoTari = aAlfa;
                         nHay = 1; |SAL;
                    |FINSI;
                    nHay = 2;
                    |LEE 000#14s;
               |SIGUE;
               |SI nHay = 0;
                    nAviso = 1;
                    aMensa = "No existe tipo tarifa:" + aGrupoTari;
                    |HAZ GrabaMensaCsv;
               |SINO;
                    |SI nHay = 2
                         #53#7 = aPrimeGrupo;
                    |SINO;
                         #53#7 = aAlfa;
                    |FINSI;
               |FINSI;

               |SALVA_DATOS #52;
               |LEE 0000#52u;
               |SI FSalida = 0; nRese = #52#0 + 1; |SINO; nRese = 1; |FINSI;
               |REPON_DATOS #52;
               #52#0 = nRese;
               |GRABA 020#52b;
               |SI FSalida = 0;
                    |HAZ Linea;

                    #52#19 = 1;     || pax , ahora asigna 1

                    #52#36 = 0; #52#32 = 0;
                    |ABRE #15;
                    |ABRE #42;
                    |HAZ BTmp2Lin;
                    |CIERRA #42;
                    |CIERRA #15;
                    #52#108 = nImpTasa;
                    #52#109 = nImpBillete;
                    #52#110 = nImpSupleI;
                    #52#111 = nImpSupleV;

                    |HAZ Comision;
                    |HAZ Actualiza;
                    |HAZ NumViajeDia;
                    #52#7 = nLin;
                    aAlfa = (#52#1 % 705) + (#52#1 % 402) + (#52#1 % 102) + (("0000" + #52#7) % -104);
                    #52#42 = aAlfa;

                    |SI #52#17 = 1; #52#18 = "Ida Cerrada"; |FINSI;
                    |SI #52#17 = 2; #52#18 = "Ida Cerrada Vuelta Abierta"; |FINSI;
                    |SI #52#17 = 3; #52#18 = "Ida y Vuelta Cerrada"; |FINSI;

                    |ABRE #36;
                    #36#1 = #52#43;
                    |LEE 000#36=;
                    |SI FSalida ! 0; |DDEFECTO #36; |FINSI;
                    |CIERRA #36;
                    #52#44 = #36#2;
                    #52#56 = #100#30;
                    #52#57 = #100#31;
                    #52#58 = #52#1 + #100#15;
                    #52#61 = "01.01.0000";

                    |GRABA 020#52a;

                    nTotalImporte = nTotalImporte + #52#32;

                    enPax = 1;
                    enLinea = #52#75;
                    eaSentido = #52#101;
                    nNume = #52#20 - #52#99 + 1;
                    efFechaViaje = nNume;
                    |HAZ CtrlViajerosAct;
                    |SI #52#17 ] 2; |Y #52#26 ! "01.01.0000";
                         enPax = 1;
                         enLinea = #52#80;
                         eaSentido = #52#102;
                         nNume = #52#26 - #52#100 + 1;
                         efFechaViaje = nNume;
                         |HAZ CtrlViajerosAct;
                    |FINSI;

                    |SI #52#21 ! aHoraSalIda;
                         nAviso = 1;
                         aMensa = "Hora salida original: " + aHoraSalIda + ", en la reserva: " + #52#21;
                         |HAZ GrabaMensaCsv;
                    |FINSI;
                    |SI nPrecioCsv ! #52#32;
                         nAviso = 1;
                         aMensa = "Reserva: " + (("0000000"+#52#0)%-107) + " precio original: " + nPrecioCsv + ", en la reserva: " + #52#32;
                         |HAZ GrabaMensaCsv;
                    |FINSI;
               |SINO;
                    nError = 2;
                    aMensa = "Error al grabar la reserva.";
               |FINSI;

               |CIERRA #14;
               Tempo = aTmp14; |HAZ BoraTempo; |DELINDEX #14;
          |FINSI;
     |FINSI;
     |CIERRA #53;
     |CIERRA #52;
|FPRC;

|PROCESO ProcesaCsv;
     |SI nReg >  1;
          aAlfa = Valor1 + Valor2; |QBF aAlfa;
          |SI aAlfa = ""; |ACABA; |FINSI;

          nError = 0;
          nAviso = 0;
          |HAZ GrabaReseCsv;
          |SI nError ! 0; |O nAviso ! 0;
               eaXml = aFic;
               |SI nError = 2; nAviso = 2; |FINSI; ||Error graba reserva
               eaMensa = "Linea csv: " + nReg + ". " + aMensa;
               |SI nError = 1;
                    enError = nError;
                    |ABRE #36;
                    #36#1 = aCliente;
                    |LEE 000#36=;
                    |SI FSalida ! 0; |DDEFECTO #36; |FINSI;
                    |CIERRA #36;
                    |HAZ MailError;
               |SINO;
                    |SI nAviso = 2;
                         |HAZ CreaError;
                         enError = nAviso;
                         eaReservas = aReservas;
                         |HAZ MailSocitransa;
                    |SINO;
                         nHayAvisosCsv = 1;
                         |SI aReservas = "";
                              aReservas = (("0000000" + #52#0)%-107);
                         |SINO;
                              aReservas = aReservas + " / " + (("0000000" + #52#0)%-107);
                         |FINSI;
                    |FINSI;
               |FINSI;
               ||aAlfa = "0000" + aMensa; |MENAV aAlfa;
          |FINSI;
     |FINSI;
     |HAZ PinLinea;
|FPRC;

|PROCESO CaracterCsv;
     |SI aCar = aLF;
          |SI aConQuote = "N";
               Valor = aCampo;
          |FINSI;
          |HAZ ProcesaCsv;
          aCampo = "";
          aConQuote = "N";

          IValor = Valor1<-;
          |PARA i = 1; |SI i [ 90; |HACIENDO i = i + 1;
               Valor = "";
               IValor = IValor + 1;
          |SIGUE;
          IValor = Valor1<-;
     |SINO;
          |SI aCar ! aCR;
               |SI aCar = " "; |Y aCampo = ""; |Y aConQuote = "N";
                    || empieza por espacio
               |SINO;
                    |SI aConQuote = "S"; |O aConQuote = "F";
                         |SI aConQuote = "F";
                              |SI aCar = aSepara;
                                   aCampo = "";
                                   aConQuote = "N";
                              |FINSI;
                         |SINO;
                              |SI aCar = aQuote;
                                   Valor = aCampo;
                                   IValor = IValor + 1;
                                   aConQuote = "F";
                              |SINO;
                                   aCampo = aCampo + aCar;
                              |FINSI;
                         |FINSI;
                    |SINO;
                         |SI aCar = aSepara;
                              Valor = aCampo;
                              IValor = IValor + 1;
                              aCampo = "";
                         |SINO;
                              |SI aCampo = ""; |Y aCar = aQuote;
                                   aConQuote = "S";
                              |SINO;
                                   aCampo = aCampo + aCar;
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CSV;
     nReg = 0;
     aCampo = "";
     aConQuote = "N";
     IValor = Valor1<-;

     |XLEE nHandle, 250, aTmp;
     |HAZ QuitaMarcaUrf8;

     |HAZ PinLinea;
     |PARA; |SI FSalida > 0; |HACIENDO;
          |PARA; |SI aTmp ! ""; |HACIENDO;
               aCar = aTmp % 101;
               |SI aTmp = aCar;
                    aTmp = ""; ||con un caracter falla: "aTmp = aTmp % 2"
               |SINO;
                    aTmp = aTmp % 2;
               |FINSI;
               |HAZ CaracterCsv;
          |SIGUE;
          |XLEE nHandle, 250, aTmp;
     |SIGUE;
|FPRC;

|PROCESO EsXmlCsv;
     aAlfa = "";
     |XABRE "B", aFic, nHandle;
     |SI FSalida ] 0;
          |XLEE nHandle, 250, aTmp;
          |HAZ QuitaMarcaUrf8;
          aLon = aTmp % A0;
          |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
               nPos = i * 100 + 1;
               aCar = aTmp % nPos;
               |SI aCar = ",";
                    |SAL;
               |SINO;
                    |SI aCar = " "; |Y aAlfa = "";
                    |SINO;
                         |SI aCar ! aQuote;
                              aAlfa = aAlfa + aCar;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |SIGUE;
          |XCIERRA nHandle;
     |FINSI;
     |SI aAlfa = "Fecha de la transacción"; || el 1§ campo del csv
          eaEsXml = "N";
     |SINO;
          eaEsXml = "S";
     |FINSI;
|FPRC;

|PROCESO ExisteFic;
     aAlfa = aDir % -101;
     |SI aAlfa = "\"; |O aAlfa = "/";
          aPath = aDir;
     |SINO;
          aPath = aDir + "/";
     |FINSI;
     aFic = aPath + aNom;
     |XABRE "E", aFic, 0;
     |SI FSalida ] 0;
          nError = 0;
     |FINSI;
|FPRC;

|PROCESO Recoge;
     aDir = #100#35; |QBF aDir;
     |MKDIR aDir

     |SI #100#11 = "F";
          aDSCorreo = #100#10;  |QBF aDSCorreo;
     |SINO;
          |SI #100#11 = "L"; |IP_REMOTO aDSCorreo; |FINSI;
          |SI #100#11 = "S"; |IP_LOCAL aDSCorreo; |FINSI;
     |FINSI;
     aUsu = #100#34; |QBF aUsu;
     aPwd = #100#37; |QBF aPwd;
     aPop3 = #100#36; |QBF aPop3;
     aNombre = "";

     aAlfa = "0000Recibiendo [" + aDSCorreo + "]";
     |MENSA aAlfa;
     |DSCORREO_RECIBE aDSCorreo, aPop3, aUsu, aPwd, aDir, aNombre;
     nError = FSalida;

     eaLog = "DSCORREO FSalida:" + nError; |HAZ Log;

     |PARA aAlfa = aNombre; |SI; |HACIENDO;
          aNombre = aNombre - ";";
          |SI aNombre = aAlfa; |SAL; |FINSI;
          aAlfa = aNombre;
     |SIGUE;

     |SI nError ! 0;
          |SI aNombre = "";
               eaLog = "DSCORREO sin Fichero "; |HAZ Log;
          |SINO;
               aFic = aNombre;
               |HAZ SacaNom;
               |HAZ ExisteFic;
               |SI nError = 0;
                    eaLog = "SI Existe Fichero" + aNom; |HAZ Log;
               |SINO;
                    eaLog = "NO Existe Fichero" + aNom; |HAZ Log;
               |FINSI;
          |FINSI;
     |FINSI;

     |BLIN 4;
     |SI nError < 0;
          aAlfa = "    Problemas al recibir correo ..." + nError;
          |SI nSw = 0; |MENAV aAlfa; |FINSI;
          eaLog = "Problemas al recibir correo"; |HAZ Log;
     |FINSI;
     |SI nError = 0;
          aFic = aNombre;
          |HAZ SacaNom;
          aAlfa = "    Correo recibido con exito [" + nError + "][" + aNom + "]";
          |SI nSw = 0; |MENAV aAlfa; |FINSI;
          eaLog = "Correo recibido:" + aNom; |HAZ Log;
     |FINSI;
     |SI nError = 1;
          |SI nCorreos = 1;
               aAlfa = "    No hay correo ...";
               |SI nSw = 0; |MENAV aAlfa; |FINSI;
               eaLog = "No hay correo"; |HAZ Log;
          |SINO;
               nCorreos = nCorreos - 1;
               aAlfa = "0000" + nCorreos + " Correos recibidos...";
               |SI nSw = 0; |MENAV aAlfa; |FINSI;
               eaLog =  nCorreos + " Correos recibidos"; |HAZ Log;
          |FINSI;
     |FINSI;
|FPRC;

|| No lo grabo directamente en el fichero porque no se porque
|| este queda vacio !!!
|PROCESO CreaError;
     |DFICE aAlfa; aAlfa = aAlfa + Usuario + ".txt";
     eaFicErr = aAlfa;
     |XABRE "W", aAlfa, nHandleErr;
     |SI FSalida ] 0;
          |LEE 000#126p;
          |PARA; |SI FSalida = 0; |HACIENDO;
               aAlfa = #126#1; |QBF aAlfa;
               |XGRABA nHandleErr, aAlfa;
               |LEE 000#126s;
          |SIGUE;
          |LEE 000#126s;
          |XCIERRA nHandleErr;
     |FINSI;
|FPRC;

|PROCESO GrabaMensa;
     |LEE 000#126u;
     |SI FSalida = 0;
          #126#0 = #126#0 + 1;
          #126#1 = aMensa;
          |GRABA 020#126n;
     |SINO;
          #126#0 = 1;
          #126#1 = aMensa;
          |GRABA 020#126n;
     |FINSI;
|FPRC;

|PROCESO NumViajeDia;
     |DDEF aAlfa;
     aAlfa = aAlfa + "sctrm052";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |ABRE #referen@;
          |SELECT #5#referen@;
          #referen@#1 = #52#1;
          #referen@#7 = 9999;
          #referen@#0 = 0;
          |LEE 000#referen@.];
          |SI FSalida = 0;
               |LEE 000#referen@.a;
          |SINO;
               |LEE 000#referen@.u;
          |FINSI;
          |SI FSalida = 0; |Y #referen@#1 = #52#1
               nLin = #referen@#7 + 1;
          |SINO;
               nLin = 1;
          |FINSI;
          |CIERRA #referen@;
          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;

|PROCESO ActRese;
     |SI aReservas = "";
          aReservas = (("0000000" + #25#5)%-107);
     |SINO;
          aReservas = aReservas + " / " + (("0000000" + #25#5)%-107);
     |FINSI;

     #52#0 = #25#5;
     |LEE 121#52=;
     |SI FSalida = 0;
          #52#106 = nLinea;    ||Pax Original
          #52#107 = nTotalImporte;
          |GRABA 020#52a;
          |LIBERA #52;
     |FINSI;
|FPRC;

|DEFBUCLE ActRese;
     #25#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, ActRese;
|FIN;

|PROCESO Tmp2Lin;
     #52#36 = #52#36 + #53#13;

     #15#0 = #52#33;
     |LEE 000#15=;
     |SI FSalida ! 0; |DDEFECTO #15; |FINSI;

     |SI #15#5 = "S";
          #52#32 = #52#32 + (#53#11 - (#53#11 * #15#2 / 100));
     |SINO;
          #52#32 = #52#32 + #53#10 + (#53#9 + #53#13 - ((#53#9 + #53#13) * #15#2 / 100));
     |FINSI;

     nImpBillete = #53#9;
     nImpTasa = #53#10;
     nImpSupleI = #53#14;
     nImpSupleV = #53#15;

     aAlfa = #53#2; |QBF aAlfa;
     |SI aAlfa ! "";
          |DDEFECTO #42;
          #42#0 = #53#2;
          |LEE 101#42=;
          |SI FSalida ! 0; |GRABA 020#42b; |FINSI;
          #42#1 = #53#3;
          #42#2 = #53#4;
          #42#3 = #53#5;
          |GRABA 020#42a;
          |LIBERA #42;
     |FINSI;
|FPRC;

|DEFBUCLE Tmp2Lin;
     #53#1;
     ;
     #52#0, 00;
     #52#0, 99;
     ;
     NULL, Tmp2Lin;
|FIN;

|PROCESO BTmp2Lin;
     |HAZ Tmp2Lin;
|FPRC;

|PROCESO GrabaRese;
     |ABRE #52;
     |ABRE #53;
     |DDEFECTO #52;
     |DDEFECTO #53;

     #52#43 = aCliente;
     #52#16 = aInforma;
     #52#1 = aFecRese;
     #52#2 = aHoraRese;
     #52#20 = aFecSalIda;
     #52#21 = aHoraSalIda;
     #52#8 = aPobSalIda;
     #52#23 = aFecLlegaIda;
     #52#24 = aHoraLlegaIda;
     #52#12 = aPobLlegaIda;
     #52#17 = 1;
     |SI aFecSalVuelta ! "";
          #52#26 = aFecSalVuelta;
          #52#27 = aHoraSalVuelta;
          #52#17 = 2;
          |SI aFecLlegaVuelta ! "01.01.0000";
               #52#29 = aFecLlegaVuelta;
               #52#30 = aHoraLlegaVuelta;
               #52#17 = 3;
          |FINSI;
     |FINSI;
     #52#19 = nLinea; || pax, aqui asigna el total de lineas (tiquets)
                      || par poder chequear la ocupacion
                      || despues asignamos 1 antes de actualizar

     #52#3 = "Internet";
     #52#4 = 1;
     #52#5 = "Portal Internet";
     #52#6 = 2;
     #52#45 = #25#6;
     #52#94 = aDivisa;

     aAlfa = #25#1; |QBF aAlfa;
     aAlfa = aAlfa + " " + #25#2;
     #53#1 = #25#0;
     #53#3 = aAlfa;
     #53#11 = #25#3;
     #53#7 = #25#4;
     #53#4 = aTlf;
     #53#5 = aMail;

     |ABRE #3;
     #3#0 = #52#8;
     |LEE 000#3=;
     |SI FSalida = 0;
          #52#9 = #3#1;
          #52#10 = #3#2;
          #52#11 = #3#4;

          #3#0 = #52#12;
          |LEE 000#3=;
          |SI FSalida = 0;
               #52#13 = #3#1;
               #52#14 = #3#2;
               #52#15 = #3#4;
          |SINO;
               aMensa = "0000No existe c¢digo de poblaci¢n.";
               nError = 1;
          |FINSI;
     |SINO;
          aMensa = "No existe c¢digo de poblaci¢n.";
          nError = 1;
     |FINSI;
     |CIERRA #3;

     |SI nError = 0;
          enSwVuelta = 0;
          |HAZ DatosSalida;
          |SI enHay = 0;
               aMensa = "No existe trayecto para la salida / llegada.";
               nError = 1;
          |SINO;
               |SI #25#0 = 1; || Solo lo hacemos en el primer tiquet
                    fHoy = ~;
                    |SI #52#20 ] fHoy;
                         enLinea = #52#75;
                         eaSentido = #52#101;
                         nNume = #52#20 - #52#99 + 1;
                         efFechaViaje = nNume;
                         |HAZ CtrlViajerosCheq;
                         |SI eaBloqueado = "S";
                              aMensa = "Servicio bloqueado para esta fecha.";
                              nError = 1;
                         |FINSI;
                         |SI enPax < #52#19;
                              aMensa = "Se supera el pax disponible.";
                              nAviso = 1;
                              |HAZ GrabaMensa;
                         |FINSI;
                    |FINSI;
                    |SI nError = 0;
                         |ABRE #26;
                         #26#0 = #52#96;
                         #26#2 = #52#75;
                         #26#4 = #52#101;
                         #26#5 = #52#22;
                         nNume = #52#20 - #52#99 + 1;
                         #26#7 = nNume;
                         |LEE 000#26=;
                         |SI FSalida ! 0;
                              aMensa = "0000No existe viaje para esa fecha.";
                              nError = 1;
                         |SINO;
                              |SI #26#24 = "S";
                                   aMensa = "El servicio para ese dia esta suspendido.";
                                   nError = 1;
                              |FINSI;
                              |SI #26#24 = "C";
                                   aMensa = "El servicio para ese dia esta cerrado.";
                                   nError = 1;
                              |FINSI;
                              nNume = #26#20 - #26#26;
                              |SI nNume < #52#19;
                                   aMensa = "No hay Pax libres para ese viaje, desea incluirla la reserva en la lista de espera?";
                                   nAviso = 1;
                                   |HAZ GrabaMensa;
                              |SINO;
                                   #52#52 = "P";
                              |FINSI;
                         |FINSI;
                         |CIERRA #26;
                    |FINSI;
               |SINO;
                    #52#52 = "P";
               |FINSI;
          |FINSI;
     |FINSI;
     |SI #52#17 = 3; |Y nError = 0;
          enSwVuelta = 1;
          |HAZ DatosVuelta;
          |SI enHay = 0;
               aMensa = "No existe trayecto para la salida / llegada.";
               nError = 1;
          |SINO;
               fHoy = ~;
               |SI #25#0 = 1;  || Solo lo hacemos en el primer tiquet
                    |SI #52#26 ! "01.01.0000"; |Y #52#26 ] fHoy;
                         enLinea = #52#80;
                         eaSentido = #52#102;
                         nNume = #52#26 - #52#100 + 1;
                         efFechaViaje = nNume;
                         |HAZ CtrlViajerosCheq;
                         |SI eaBloqueado = "S";
                              aMensa = "0000Servicio bloqueado para esta fecha.";
                              nError = 1;
                         |FINSI;
                         |SI enPax < #52#19;
                              aMensa = "0000Se supera el pax disponible.";
                              nAviso = 1;
                              |HAZ GrabaMensa;
                         |FINSI;
                    |FINSI;
               |FINSI;
               |SI nError = 0;
                    |ABRE #26;
                    #26#0 = #52#105;
                    #26#2 = #52#80;
                    #26#4 = #52#102;
                    #26#5 = #52#28;
                    nNume = #52#26 - #52#100 + 1;
                    #26#7 = nNume;
                    |LEE 000#26=;
                    |SI FSalida ! 0;
                         aMensa = "No existe viaje vuelta para esa fecha.";
                         nError = 1;
                    |SINO;
                         |SI #26#24 = "S";
                              aMensa = "El servicio para ese dia esta suspendido.";
                              nError = 1;
                         |FINSI;
                         |SI #26#24 = "C";
                              aMensa = "El servicio para ese dia esta cerrado.";
                              nError = 1;
                         |FINSI;
                         nNume = #26#20 - #26#26;
                         |SI nNume < #52#19;
                              aMensa = "No hay Pax libres para ese viaje de vuelta";
                              nAviso = 1;
                              |HAZ GrabaMensa;
                         |SINO;
                              #52#53 = "P";
                         |FINSI;
                    |FINSI;
                    |CIERRA #26;
               |SINO;
                    #52#53 = "P";
               |FINSI;
          |FINSI;
     |FINSI;
     |SI nError = 0;
          |HAZ CreaTempo; |NOME_DAT #14 Tempo; aTmp14 = Tempo;
          |DELINDEX #14;
          |ABRE #14;
          |HAZ CargaTari;
          aPrimeGrupo = "";
          nHay = 0;
          |LEE 000#14p;
          |PARA; |SI FSalida = 0; |HACIENDO;
               aAlfa = #14#0 % 102;

               |SI aPrimeGrupo = "";
                    aPrimeGrupo = aAlfa;
               |FINSI;

               |SI #53#7 = aAlfa;
                    nHay = 1; |SAL;
               |FINSI;
               nHay = 2;
               |LEE 000#14s;
          |SIGUE;
          |SI nHay = 0;
               nAviso = 1;
               aMensa = "No existe tipo tarifa:" + #25#4;
               |HAZ GrabaMensa;
          |SINO;
               |SI nHay = 2
                    #53#7 = aPrimeGrupo;
               |SINO;
                    #53#7 = aAlfa;
               |FINSI;
          |FINSI;

          |SALVA_DATOS #52;
          |LEE 0000#52u;
          |SI FSalida = 0; nRese = #52#0 + 1; |SINO; nRese = 1; |FINSI;
          |REPON_DATOS #52;
          #52#0 = nRese;
          |GRABA 020#52b;
          |SI FSalida = 0;
               #25#5 = #52#0;
               |GRABA 020#25a;  || Graba Numero Reserva

               |HAZ Linea;

               #52#19 = 1;     || pax , ahora asigna 1

               #52#36 = 0; #52#32 = 0;
               |ABRE #15;
               |ABRE #42;
               |BUCLE Tmp2Lin;
               |CIERRA #42;
               |CIERRA #15;
               #52#108 = nImpTasa;
               #52#109 = nImpBillete;
               #52#110 = nImpSupleI;
               #52#111 = nImpSupleV;

               |HAZ Comision;
               |HAZ Actualiza;
               |HAZ NumViajeDia;
               #52#7 = nLin;
               aAlfa = (#52#1 % 705) + (#52#1 % 402) + (#52#1 % 102) + (("0000" + #52#7) % -104);
               #52#42 = aAlfa;

               |SI #52#17 = 1; #52#18 = "Ida Cerrada"; |FINSI;
               |SI #52#17 = 2; #52#18 = "Ida Cerrada Vuelta Abierta"; |FINSI;
               |SI #52#17 = 3; #52#18 = "Ida y Vuelta Cerrada"; |FINSI;

               |ABRE #36;
               #36#1 = #52#43;
               |LEE 000#36=;
               |SI FSalida ! 0; |DDEFECTO #36; |FINSI;
               |CIERRA #36;
               #52#44 = #36#2;
               #52#56 = #100#30;
               #52#57 = #100#31;
               #52#58 = #52#1 + #100#15;
               #52#61 = "01.01.0000";

               |GRABA 020#52a;

               nTotalImporte = nTotalImporte + #52#32;

               enPax = 1;
               enLinea = #52#75;
               eaSentido = #52#101;
               nNume = #52#20 - #52#99 + 1;
               efFechaViaje = nNume;
               |HAZ CtrlViajerosAct;
               |SI #52#17 ] 2; |Y #52#26 ! "01.01.0000";
                    enPax = 1;
                    enLinea = #52#80;
                    eaSentido = #52#102;
                    nNume = #52#26 - #52#100 + 1;
                    efFechaViaje = nNume;
                    |HAZ CtrlViajerosAct;
               |FINSI;

               |SI #25#0 = 1;
                    |SI #52#21 ! aHoraSalIda;
                         nAviso = 1;
                         aMensa = "Hora salida original: " + aHoraSalIda + ", en la reserva: " + #52#21;
                         |HAZ GrabaMensa;
                    |FINSI;
                    |SI #52#23 ! aFecLlegaIda;
                         nAviso = 1;
                         aMensa = "Fecha llegada original: " + aFecLlegaIda + ", en la reserva: " + #52#23;
                         |HAZ GrabaMensa;
                    |FINSI;
                    |SI #52#24 ! aHoraLlegaIda;
                         nAviso = 1;
                         aMensa = "Hora llegada original: " + aHoraLlegaIda + ", en la reserva: " + #52#24;
                         |HAZ GrabaMensa;
                    |FINSI;
                    |SI aFecSalVuelta ! "";
                         |SI #52#27 ! aHoraSalVuelta;
                              nAviso = 1;
                              aMensa = "Hora salida vuelta original: " + aHoraSalVuelta + ", en la reserva: " + #52#27;
                              |HAZ GrabaMensa;
                         |FINSI;
                         |SI #52#29 ! aFecLlegaVuelta;
                              nAviso = 1;
                              aMensa = "Hora fecha llegada vuelta original: " + aFecLlegaVuelta + ", en la reserva: " + #52#29;
                              |HAZ GrabaMensa;
                         |FINSI;
                         |SI #52#30 ! aHoraLlegaVuelta;
                              nAviso = 1;
                              aMensa = "Hora llegada vuelta original: " + aHoraLlegaVuelta + ", en la reserva: " + #52#30;
                              |HAZ GrabaMensa;
                         |FINSI;
                    |FINSI;
               |FINSI;
               |SI #25#3 ! #52#32;
                    nAviso = 1;
                    aMensa = "Reserva: " + (("0000000"+#52#0)%-107) + " precio original: " + #25#3 + ", en la reserva: " + #52#32;
                    |HAZ GrabaMensa;
               |FINSI;
          |SINO;
               nError = 2;
               aMensa = "Error al grabar la reserva.";
               |HAZ GrabaMensa;
          |FINSI;

          |CIERRA #14;
          Tempo = aTmp14; |HAZ BoraTempo; |DELINDEX #14;
     |FINSI;

     |CIERRA #52;
     |CIERRA #53;
|FPRC;

|DEFBUCLE GrabaRese;
     #25#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, GrabaRese;
|FIN;

|PROCESO CheqRese;
     aLon = aCliente % A0;
     #36#1 = aCliente; aCliente = #36#1;
     |LEE 000#36=;
     |SI FSalida ! 0; |O aLon > 6;
          nError = 1; aMensa = "Cliente/Agencia incorrecto"; |ACABA;
     |FINSI;

     aPobla = aPobSalIda;
     aLugar = aLugarSalIda;
     |HAZ CheqPobla;
     |SI nError ! 0; aMensa = "Poblacion salida ida incorrecta"; |ACABA; |FINSI;
     aPobSalIda = aPobla;
     aLugarSalIda = aLugar;

     aPobla = aPobLlegaIda;
     aLugar = aLugarLlegaIda;
     |HAZ CheqPobla;
     |SI nError ! 0; aMensa = "Poblacion llegada ida incorrecta"; |ACABA; |FINSI;
     aPobLlegaIda = aPobla;
     aLugarLlegaIda = aLugar;

     |SI aPobSalVuelta ! "";
          aPobla = aPobSalVuelta;
          aLugar = aLugarSalVuelta;
          |HAZ CheqPobla;
          |SI nError ! 0; aMensa = "Poblacion salida vuelta incorrecta"; |ACABA; |FINSI;
          aPobSalVuelta = aPobla;
          aLugarSalVuelta = aLugar;

          aPobla = aPobLlegaVuelta;
          aLugar = aLugarLlegaVuelta;
          |HAZ CheqPobla;
          |SI nError ! 0; aMensa = "Poblacion llegada vuelta incorrecta"; |ACABA; |FINSI;
          aPobLlegaVuelta = aPobla;
          aLugarLlegaVuelta = aLugar;

          |SI aPobSalIda ! aPobLlegaVuelta;
                    |O aPobLlegaIda ! aPobSalVuelta;
               nError = 1; aMensa = "Poblaciones salida/llegada incorrectas con la vuelta";
               |ACABA;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CheqType;
     |SELECT #2#74;
     #74#0 = aCliente;
     #74#3 = #25#4;
     |LEE 000#74=;
     |SI FSalida ! 0;
          nError = 1; aMensa = "No existe 'Type' tarifa agencia";
          |HAZ GrabaMensa;
     |FINSI;
     #25#4 = #74#1;
     |GRABA 020#25a;
|FPRC;

|DEFBUCLE CheqType;
     #25#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, CheqType;
|FIN;

|PROCESO CheqPobla;
     #3#0 = aPobla;
     |LEE 000#3=;
     |SI FSalida = 0;
          aPobla = #3#0;
          aLugar = #3#2;
     |SINO;
          nHay = 0;
          nEsta = 0;
          #72#5 = aLugar; aLugar = #72#5;
          |SELECT #2#72;
          #72#0 = aCliente;
          #72#4 = aPobla; aPobla = #72#4;
          #72#5 = "";
          |LEE 000#72];
          nHay = 0; nEsta = 0;
          |PARA; |SI FSalida = 0; |Y #72#0 = aCliente; |Y #72#4 = aPobla; |HACIENDO;
               nHay = nHay + 1;
               aAlfa1 = #72#1;
               aAlfa2 = #72#3;
               |SI aLugar = #72#5; nEsta = 1; |SAL; |FINSI;
               |LEE 000#72s;
          |SIGUE;
          |SI nHay = 0;
               nError = 1;
          |FINSI;
          |SI nEsta = 0;
               |SI nHay > 1;
                    nError = 1;
               |FINSI;
          |FINSI;
          aPobla = aAlfa1;
          aLugar = aAlfa2;
     |FINSI;
|FPRC;

|PROCESO FormaFecha;
     |QBF aVal;
     aAlfa = (aVal%902)+"."+(aVal%602)+"."+(aVal%104);
     |SI aAlfa = "00.00.0000"; |O aVal = "";
          aAlfa = "01.01.0000";
     |FINSI;
|FPRC;

|PROCESO ProcesaTagPop;
||     aAlfa = "0000Push: " + aTag + " => " + aVal; |MENAV aAlfa;

     |SI nSwGeneral = 1;
          |SI aTag = "currency";
               aDivisa = aVal;
          |FINSI;
          |SI aTag = "booking_number";
               aAlfa = aVal;
               aAlfa = aAlfa - "-";
               |SI FSalida > 0;
                    nPos = (FSalida - 1) / 100 + 1;
                    aDer = aVal % nPos;
                    nPos = nPos + 98;
                    aIzq = aVal % nPos;

                    aCliente = aIzq;
                    aInforma = aDer;
               |SINO;
                    aCliente = aVal;
                    aInforma = "";
               |FINSI;
          |FINSI;
          |SI aTag = "booking_date";
               |HAZ FormaFecha;
               aFecRese = aAlfa;
               aHoraRese = (aVal%1205);
          |FINSI;
     |FINSI;
     |SI nSwSalida = 1;
          |SI aTag = "departure_date";
               |HAZ FormaFecha;
               aFecSalIda = aAlfa;
               aHoraSalIda = (aVal%1205)
          |FINSI;
          |SI aTag = "departure_city";
               aPobSalIda = aVal;
          |FINSI;
          |SI aTag = "departure_station";
               aLugarSalIda = aVal;
          |FINSI;
          |SI aTag = "arrival_date";
               |HAZ FormaFecha;
               aFecLlegaIda = aAlfa;
               aHoraLlegaIda = (aVal%1205)
          |FINSI;
          |SI aTag = "arrival_city";
               aPobLlegaIda = aVal;
          |FINSI;
          |SI aTag = "arrival_station";
               aLugarLlegaIda = aVal;
          |FINSI;
     |FINSI;
     |SI nSwLlegada = 1;
          |SI aTag = "departure_date";
               |HAZ FormaFecha;
               aFecSalVuelta = aAlfa;
               aHoraSalVuelta = (aVal%1205)
          |FINSI;
          |SI aTag = "departure_city";
               aPobSalVuelta = aVal;
          |FINSI;
          |SI aTag = "departure_station";
               aLugarSalVuelta = aVal;
          |FINSI;
          |SI aTag = "arrival_date";
               |HAZ FormaFecha;
               aFecLlegaVuelta = aAlfa;
               aHoraLlegaVuelta = (aVal%1205)
          |FINSI;
          |SI aTag = "arrival_city";
               aPobLlegaVuelta = aVal;
          |FINSI;
          |SI aTag = "arrival_station";
               aLugarLlegaVuelta = aVal;
          |FINSI;
     |FINSI;
     |SI nSwCliente = 1;
          |SI aTag = "last_name";
               aApellido1 = aVal;
          |FINSI;
          |SI aTag = "first_name";
               aNombre1 = aVal;
          |FINSI;
          |SI aTag = "contact_phone";
               aTlf = aVal;
          |FINSI;
          |SI aTag = "contact_email";
               aMail = aVal;
          |FINSI;
     |FINSI;
     |SI nSwLinea = 1;
          |SI aTag = "first_name";
               aApellido = aVal;
          |FINSI;
          |SI aTag = "last_name";
               aNombre = aVal;
          |FINSI;
          |SI aTag = "price";
               nPrecio = aVal;
          |FINSI;
          |SI aTag = "type";
               aType = aVal;
          |FINSI;
          |SI aTag = "ticket_number";
               aTicket = aVal;
          |FINSI;
     |FINSI;

     |SI aTag = "general_data"; nSwGeneral = 0; |FINSI;
     |SI aTag = "outbound_information"; nSwSalida = 0; |FINSI;
     |SI aTag = "return_information"; nSwLlegada = 0; |FINSI;
     |SI aTag = "customer_information"; nSwCliente = 0; |FINSI;
     |SI aTag = "ticket";
          nLinea = nLinea + 1;
          #25#0 = nLinea;
          #25#1 = aNombre;
          #25#2 = aApellido;
          #25#3 = nPrecio;
          #25#4 = aType;
          #25#6 = aTicket;
          |GRABA 020#25n;
          nSwLinea = 0;
     |FINSI;
|FPRC;

|PROCESO ProcesaTagPush;
||     aAlfa = "0000Push: " + aTag + " => " + aTagAtri; |MENAV aAlfa;
     |SI aTag = "general_data"; nSwGeneral = 1; |FINSI;
     |SI aTag = "outbound_information"; nSwSalida = 1; |FINSI;
     |SI aTag = "return_information"; nSwLlegada = 1; |FINSI;
     |SI aTag = "customer_information"; nSwCliente = 1; |FINSI;
     |SI aTag = "ticket"; nSwLinea = 1; |FINSI;
|FPRC;

|PROCESO ProcesaTagVacio;
|FPRC;

|PROCESO ProcesaPrologo;
|FPRC;

|PROCESO ProcesaRem;
|FPRC;

|PROCESO QuitaBlancos;   || a izquierda y derecha
     aResul = "";
     aLon = aAlfa % A0;
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aLetra = aAlfa % nPos;
          |SI aLetra ! " "; |O aResul ! "";
               aResul = aResul + aLetra;
          |FINSI;
     |SIGUE;
     aAlfa = aResul;
     |QBF aAlfa;
|FPRC;

|PROCESO PopTag;
     nNivel = nNivel - 1;
     IMatriz = Matriz1<-;
     IMatriz = IMatriz + nNivel;
     FSalida = Matriz;
|FPRC;

|PROCESO PushTag;
     IMatriz = Matriz1<-;
     IMatriz = IMatriz + nNivel;
     Matriz = aTag;
     nNivel = nNivel + 1;
|FPRC;

|PROCESO PinLinea;
     nReg = nReg + 1;
     aAlfa = "Linea: " + nReg;
     |PINTA 1206, aAlfa;
|FPRC;

|PROCESO Caracter;
     |SI aCar = RETORNO; |HAZ PinLinea; |FINSI;

     |SI aCar < " "; |O nError ! 0; |ACABA; |FINSI;

     |SI nRem > 0;
          |SI nRem < 3;
               |SI aCar = "-";
                    nRem = nRem + 1;
               |SINO;
                    aAlfa = "0000ERROR en XML: " + aTag;
                    |SI nSw = 0; |MENAV aAlfa; |FINSI;
                    nError = 1;
               |FINSI;
          |SINO;
               |SI aCar = ">"; || Un comentario no puede tener el caracter '>'
                    aAlfa = aTag % -102;
                    |SI aAlfa = "--";
                         aLon = aTag % A0;
                         nPos = (aLon - 2) + 100;
                         aTag = aTag % nPos;
                         |HAZ ProcesaRem;
                    |SINO;
                         aAlfa = "0000ERROR en XML: " + aTag;
                         |SI nSw = 0; |MENAV aAlfa; |FINSI;
                         nError = 1;
                    |FINSI;
                    aTag = ""; nRem = 0;
               |SINO;
                    aLon = aTag % A0;
                    |SI aLon < 250; aTag = aTag + aCar; |FINSI;
               |FINSI;
          |FINSI;
          |ACABA;
     |FINSI;

     |SI aCar = "<";
          aIniTag = "S"; aTag = "";
     |SINO;
          |SI aIniTag = "S";
               |SI aCar = "/"; |Y aIniAtri = "N";
                    |SI aTag = "";
                         aFinTag = "S";
                    |SINO;
                         aFinTagVacio = "S";
                    |FINSI;
               |SINO;
                    |SI aCar = "!"; |Y aIniAtri = "N";
                         |SI aTag = "";
                              nRem = 1;
                         |SINO;
                              aAlfa = "0000ERROR en XML: " + aTag;
                              |SI nSw = 0; |MENAV aAlfa; |FINSI;
                              nError = 1;
                         |FINSI;
                         |ACABA;
                    |FINSI;
                    |SI aCar = "?"; |Y aIniAtri = "N";
                         |SI aIniPro = "S";
                              aFinPro = "S";
                              aIniPro = "N";
                         |SINO;
                              |SI aFinPro = "S";
                                   aAlfa = "0000ERROR en XML: " + aTag;
                                   |SI nSw = 0; |MENAV aAlfa; |FINSI;
                                   nError = 1;
                              |SINO;
                                   aIniPro = "S"; aTag = "";
                              |FINSI;
                         |FINSI;
                    |SINO;
                         |SI aCar = ">";
                              aAlfa = aTag; |HAZ QuitaBlancos; aTag = aAlfa;
                              aAlfa = aTagAtri; |HAZ QuitaBlancos; aTagAtri = aAlfa;
                              |SI aFinPro = "S";
                                   |HAZ ProcesaPrologo;
                                   aFinPro = "N";
                              |SINO;
                                   |SI aFinTag = "S";
                                        |HAZ PopTag;
                                        |SI aTag ! FSalida;
                                             aAlfa = "0000ERROR en XML: <" + FSalida + "> " + aVal;
                                             |SI nSw = 0; |MENAV aAlfa; |FINSI;
                                             nError = 1;
                                        |SINO;
                                             |HAZ ProcesaTagPop;
                                        |FINSI;
                                   |SINO;
                                        |SI aFinTagVacio = "S";
                                             |HAZ ProcesaTagVacio;
                                             aFinTagVacio = "N";
                                        |SINO;
                                             |HAZ PushTag;
                                             |HAZ ProcesaTagPush;
                                        |FINSI;
                                   |FINSI;
                              |FINSI;
                              aIniTag = "N"; aFinTag = "N"; aTag = ""; aVal = "";
                              aIniAtri = "N"; aTagAtri = "";
                         |SINO;
                              aLon = aTag % A0;
                              |SI aLon < 250;
                                   |SI aCar = " "; |Y aIniAtri = "N";
                                        aIniAtri = "S";
                                   |SINO;
                                        |SI aIniAtri = "S";
                                             aTagAtri = aTagAtri + aCar;
                                        |SINO;
                                             aTag = aTag + aCar;
                                        |FINSI;
                                   |FINSI;
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |SINO;
               aLon = aVal % A0;
               |SI aLon < 250; aVal = aVal + aCar; |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO XML;
     nReg = 0;
     nRem = 0;
     nNivel = 0;
     aVal = "";
     aTmp = "";
     aTag = "";
     aIniTag = "N";
     aFinTag = "N";
     aIniPro = "N";
     aFinPro = "N";
     aFinTagVacio = "N";
     aIniAtri = "N";
     aTagAtri = "";

     |XLEE nHandle, 250, aTmp;
     |HAZ PinLinea;
     |PARA; |SI FSalida > 0; |HACIENDO;
          |PARA; |SI aTmp ! ""; |HACIENDO;
               aCar = aTmp % 101;
               |SI aTmp = aCar;
                    aTmp = ""; ||con un caracter falla: "aTmp = aTmp % 2"
               |SINO;
                    aTmp = aTmp % 2;
               |FINSI;
               |HAZ Caracter;
               |SI nError ! 0; |SAL; |FINSI;
          |SIGUE;
          |XLEE nHandle, 250, aTmp;
          |SI nError ! 0; |SAL; |FINSI;
     |SIGUE;
|FPRC;

|PROCESO IniVar;
     aReservas = "";
     nError = 0;
     nAviso = 0;
     nTotalImporte = 0;
     nSwGeneral = 0;
     nSwSalida = 0;
     nSwLlegada = 0;
     nSwCliente = 0;
     nSwLinea = 0;

     aCliente = "";
     aFecRese = "";
     aHoraRese = "";
     aFecSalIda = "";
     aHoraSalIda = "";
     aPobSalIda = "";
     aLugarSalIda = "";
     aFecLlegaIda = "";
     aHoraLlegaIda = "";
     aPobLlegaIda = "";
     aLugarLlegaIda = "";
     aFecSalVuelta = "";
     aHoraSalVuelta = "";
     aPobSalVuelta = "";
     aLugarSalVuelta = "";
     aFecLlegaVuelta = "";
     aHoraLlegaVuelta = "";
     aPobLlegaVuelta = "";
     aLugarLlegaVuelta = "";
     aApellido1 = "";
     aNombre1 = "";
     aTlf = "";
     aMail = "";
     aApellido = "";
     aNombre = "";
     nPrecio = "";
     aType = "";
     nLinea = 0;
|FPRC;

|PROCESO SacaNom;
     aLon = aFic % A0;
     aNom = "";
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = -(i * 100 + 1)
          aCar = aFic % nPos;
          |SI aCar = "\"; |O aCar = "/"; |SAL; |FINSI;
          aNom = aCar + aNom;
     |SIGUE;
|FPRC;

|PROCESO T3; |TIPO 3;
     eaLog = "TIPO3:" + aFic; |HAZ Log;

     RETORNO = &10;
     aFic = #0#1; |QBF aFic;
     |HAZ SacaNom;
     |XABRE "EC", aFic, 0;
     |SI FSalida ] 0;
          |HAZ Utf82Ansi;
          aFicAnsi = aFic;

          eaLog = "ANSI:" + aFicAnsi; |HAZ Log;

          |HAZ IniVar;
          |ABRE #36;
          |ABRE #72;
          |ABRE #74;
          |ABRE #3;
          |HAZ CreaTempo; |NOME_DAT #25 Tempo; |DELINDEX #25; aTmp25 = Tempo;
          |HAZ CreaTempo; |NOME_DAT #126 Tempo; |DELINDEX #126; aTmp126 = Tempo;
          |ABRE #3;
          |ABRE #25;
          |ABRE #126;

          |DFICE aFic;
          aFic = aFic + aNom;
          |FBORRA aFic;
          |RECIBE_FICHERO aFicAnsi, aFic;
          nFSal = FSalida;

          eaLog = "Recibe del puesto:" + FSalida; |HAZ Log;

          |SI nFSal ! 0;
               |SI nSw = 0;
                   |MENAV "0000Error al copiar fichero al servidor.";
               |FINSI;
          |SINO;
               |HAZ EsXmlCsv;

               |XABRE "B", aFic, nHandle;
               |SI FSalida ] 0;
                    |SI eaEsXml = "S";
                         eaLog = "Abre XML:" + aFic; |HAZ Log;
                         |HAZ XML;
                         eaLog = "Cierra XML:" + aFic; |HAZ Log;
                    |SINO;
                         eaLog = "Abre CSV:" + aFic; |HAZ Log;
                         |HAZ CSV;
                         eaLog = "Cierra CSV:" + aFic; |HAZ Log;
                    |FINSI;
                    |XCIERRA nHandle;
               |FINSI;
          |FINSI;

          |SI eaEsXml = "S";
               |HAZ CheqRese;
               |SI nError = 0;
                    |BUCLE CheqType;
               |FINSI;
          |FINSI;

          |CIERRA #25;
          |CIERRA #74;
          |CIERRA #72;
          |CIERRA #36;

          |SI eaEsXml = "S";
               |SI nError = 0;
                    |BUCLE GrabaRese;
                    |SI nError = 0;
                         |ABRE #52;
                         |BUCLE ActRese;
                         |CIERRA #52;
                    |FINSI;
               |FINSI;

               eaLog = "Finaliza-> Errores:" + nError + " Avisos:" + nAviso; |HAZ Log;

               |SI nError ! 0; |O nAviso ! 0;
                    eaXml = aFic;
                    |SI nError = 2; nAviso = 2; |FINSI; ||Error graba reserva
                    eaMensa = aMensa;
                    |SI nError = 1;
                         enError = nError;
                         |ABRE #36;
                         #36#1 = aCliente;
                         |LEE 000#36=;
                         |SI FSalida ! 0; |DDEFECTO #36; |FINSI;
                         |CIERRA #36;
                         |HAZ MailError;
                    |SINO;
                         |HAZ CreaError;
                         enError = nAviso;
                         eaReservas = aReservas;
                         |HAZ MailSocitransa;
                    |FINSI;
                    ||aAlfa = "0000" + aMensa; |MENAV aAlfa;
               |FINSI;
          |SINO;
               |SI nHayAvisosCsv ! 0;
                    |HAZ CreaError;
                    enError = nAviso;
                    eaReservas = aReservas;
                    |HAZ MailSocitransa;
               |FINSI;
          |FINSI;
          |CIERRA #126;
          Tempo = aTmp25; |HAZ BoraTempo; |DELINDEX #25;
          Tempo = aTmp126; |HAZ BoraTempo; |DELINDEX #126;
          |RFBORRA aFicAnsi;
     |SINO;
          aAlfa = "0000Error al abrir:" + aFic;
          |SI nSw = 0; |MENAV aAlfa; |FINSI;

          eaLog = "Error al abrir:" + aFic; |HAZ Log;
     |FINSI;
|FPRC;

Entrada: aFic
Salida : aFic
|PROCESO Utf82Ansi;
     |DBASE aAlfa;
     aOrg = aAlfa + "plugins/ansitoutf8.exe";

     aIP = "";
     |IP_REMOTO aIP;
     |SI aIP = "";
          |DBASS aBase;
          aDes = aBase + "bin/ansitoutf8.exe";
          |COPIA_FICHERO aOrg, aDes;
     |SINO;
          |ESPECIFICA "RBASS", aLocal;
          aBase = aLocal;
          aDes = aBase + "bin/ansitoutf8.exe";
          |ENVIA_FICHERO aOrg, aDes;
     |FINSI;

     aAlfa = aDes + " 1 " + aFic + " " + aFic + "_";
     aLon = aAlfa % A0;
     aExe = "";
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aCar = aAlfa % nPos;
          |SI aCar = "/"; aCar = "\"; |FINSI;
          aExe = aExe + aCar;
     |SIGUE;

     aExe = "cmd " + &34 + "/c START /MIN /WAIT " + aExe + &34;
     |SI aIP = "";
          |SYSTEM aExe;
     |SINO;
          |RSYSTEM aExe;
     |FINSI;
     aFic = aFic + "_";
|FPRC;

|PROCESO T0; |TIPO 0;
     |SI FSalida = 13;
          aAlfa = "F*.*";

          |BROWSE aAlfa;

          aAlfa = aAlfa % 2; |QBF aAlfa;
          |SI aAlfa = "F"; aAlfa = ""; |FINSI;

          |QBF aAlfa;
          |SI aAlfa ! "";
               #0#1 = aAlfa;
               |PINTA #0#1;
          |FINSI;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO BtnFichero;
     |PTEC 827;
|FPRC;

|PROCESO BtnBorraLog;
     |CONFI "0000N¿Borrar todos los registros del log?";
     |SI FSalida = 0;
          |DELINDEX #75;
     |FINSI;
|FPRC;

|PROCESO BtnVerLog;
     |ABRE #75;
     |CONSULTA_CLAVE #1#75;
     |CIERRA #75;
|FPRC;

|PROGRAMA;
     aSepara = ",";
     aQuote = &34;
     aCR = &13;
     aLF = &10;
     |ABRE #100; |LEE 000#100p; |CIERRA #100;
     |SI PARAMETRO = "menu";
          |PINPA #0#0; nPanta = FSalida;
          eaNomLogo = #100#1;
          |HAZ TraeLogoPan;
          enPantaLogo = nPanta;
          |HAZ PintaLogoPan;

          |CONTROL_SIMPLE nPanta, "BOTON,{{136}}", 1681, 1684, BtnFichero;
          |CONTROL_SIMPLE nPanta, "BOTON,{{207}}Borra Log", 1081, 1089, BtnBorraLog;
          |CONTROL_SIMPLE nPanta, "BOTON,{{204}}Consulta Log", 1071, 1079, BtnVerLog;

          |ABRE #0;
          |LEE 000#0p;
          |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
          |PINPA #0#0;
          |PINDA #0#0;
          |ENDATOS #1#0;
          |SI FSalida = 0; |GRABA 020#0a; |FINSI;
          |CIERRA #0;
     |SINO;
          nSw = 1;       || 1 sin mensajes, 0 con mensajes
          nCorreos = 0;

          eaLog = "INICIO..."; |HAZ Log;

          |PARA; |SI; |HACIENDO;
               nError = 0;
               nCorreos = nCorreos + 1;
               |HAZ Recoge;

               eaLog = "Recoge Correo:" + nCorreos + ", Error":  + nError; |HAZ Log;
               |SI nError ! 0; |SAL; |FINSI;

               aAlfa = aDir % -101;
               |SI aAlfa = "\"; |O aAlfa = "/";
                    aPath = aDir;
               |SINO;
                    aPath = aDir + "/";
               |FINSI;

               aAlfa = aPath + "_cuerpo_.txt";
               |FBORRA aAlfa;

               eaLog = "Recibido:" + aNom; |HAZ Log;

               aFic = aPath + aNom;

               |ESPECIFICA "RBASS", aLocal;
               aBaseLocal = aLocal1;
               aAlfa = aBaseLocal + "tmp/";
               |MKDIR aAlfa;
               aAlfa = aAlfa + aNom;

               |ENVIA_FICHERO aFic, aAlfa;
               nFSal = FSalida;

               eaLog = "Envio al servidor:" + nFSal; |HAZ Log;

               |SI nFSal = 0;
                     #0#1 = aAlfa; |HAZ T3;
               |FINSI;

               aAlfa = #0#1; |QBF aAlfa;
               |RFBORRA aAlfa;

               eaLog = "Finaliza fichero:" + aAlfa; |HAZ Log;
          |SIGUE;

          eaLog = "...FINAL (correos:" + nCorreos + ")"; |HAZ Log;
     |FINSI;
|FPRO;
