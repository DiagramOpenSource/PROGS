|FICHEROS;
     gruaz001 #0;
     gruam001 #1;
     gruam002 #2;
     dsm00003 #3; || Dir Ent
     agifa019 #19;
     dsm00020 #20; || Agencia Trans
     agifa024 #24;
     agifa025 #25;
     agifa069 #69;
     agifa084 #84;
     tempo134 #134;      || Tmp impresion
     agifa142 #142;
     agifa321 #321;
     agifa324 #324;
|FIN;

|INCLUYE i_varart;

|VARIABLES;
     fIniFactu = @;
     fFinFactu = @;
     fConIni = @;
     fConFin = @;
     fFecha = @;
     fFactu = @;
     fDesde = @;
     fHasta = @;
     fDesde1 = @;
     fHasta1 = @;
     f = @;
     nMesIni = 0;
     nNume = 0;
     nNume1 = 0;
     nMes = 0;
     nAno = 0;
     nFAno = 0;
     nFMes = 0;
     nInc = 0;
     i = 0;
     nDias = 0;
     nDiasPeriodo = 0;
     nSwMontaje = 0;
     nSwDesmonta = 0;
     nPrecio = 0;
     nUni = 0;
     aPeri = "";
     aAlfa = "";
     aArti = "";
     aMes = "";
     aAno = "";
     retencion = 0;
     nMargen = 0;
     nForma = 0;
     imneto = 0;
     nDto = 0;
     nHay = 0;
     aTmp = "";
     nLinea = 0;
     fmes = "";
     mes = 0;
     nImpo = 0;
     &enSwMenu = 0;
     &aDivisa = "";
     &aMoneda = "";
     &eaTag = "";
|FIN;

|PROCESO Defecto; |TIPO 7;
     |SI #0#1 = 0;
          f = ~;
          aAlfa = f % -104;
          #0#0 = aAlfa; |PINTA #0#0;
          aAlfa = f % 402;
          #0#1 = aAlfa; |PINTA #0#1;
     |FINSI;
|FPRC;

|PROCESO ActArti;
     fmes = #84#1 % A402;
     mes = fmes - 1;
     mes = mes * 5;
     mes = mes + 35;
     #19#0 = #69#2;
     |LEE 101#19=;
     |SI FSalida = 0;
          |SI #19#194 = 2;
               nImpo = ((((#69#30 * #69#6) < #69#8) < #69#21) < #84#5) < #84#32;
          |SINO;
               nImpo = ((((#69#5 * #69#6) < #69#8) < #69#21) < #84#5) < #84#32;
          |FINSI;
          |SI #142#115 = "S";
               nImpo = nImpo < #84#7;
          |FINSI;
          nMargen = nImpo - #69#14;
          #19mes = #19mes + (nImpo * nForma);
          #19#95 = #19#95 + (nImpo * nForma);
          mes = mes + 1;
          #19mes = #19mes + (nMargen * nForma);
          #19#96 = #19#96 + (nMargen * nForma);
          |GRABA 020#19a;
     |FINSI;
     |LIBERA #19;

     efFecha = #84#1;
     eaArticulo = #69#2;
     enImpVta = (nImpo * nForma);
     enImpMar = (nMargen * nForma);
     |HAZ AcumulaArt;
|FPRC;

|PROCESO Actualiza;
     nForma = 0 +. 1;

     |ABRE #24;
     #24#0 = #84#3;
     |LEE 121#24=;
     |SI 0 = FSalida;
          fmes = #84#1 % A402;
          mes = fmes - 1;
          mes = mes * 4;
          mes = mes + 54;
          imneto = #84#15 - #84#25;
          |SI #142#115 = "N";
               nImpo = (imneto * 100) / (100 - #84#7);
               nDto = #84#25 + (imneto - nImpo);
          |SINO;
               nImpo = imneto;
               nDto = #84#25;
          |FINSI;
          #24mes = #24mes +. nImpo; || imneto;
          mes = mes + 1;
          #24mes = #24mes +. nDto;
          mes = mes + 1;
          #24mes = #24mes +. #84#37;
          #24#102 = #24#102 +. nImpo; || imneto;
          #24#103 = #24#103 +. nDto;
          #24#104 = #24#104 +. #84#37;
          mes = FEntrada / 100;
          mes = mes ! 0;
          |SI 1 = mes;
               #24#45 = #84#1;
               #24#46 = imneto;
          |FINSI;
          #24#110 = #24#110 +. imneto;
          imneto = imneto + #84#24;
          |GRABA 020#24a;
          |LIBERA #24;

          enImpCliCom = nImpo * nForma;
          enImpCliDto = nDto * nForma;
          enImpCliMar = #84#37 * nForma;
          enImpCliFac = imneto * nForma;
          eaCliente = #84#3;
          efFecha = #84#1;
          |HAZ AcumulaCli;
     |FINSI;
     |CIERRA #24;

     |ABRE #25;
     #25#0 = #84;
     |LEE 121#25=;
     |SI 0 = FSalida;
           imneto = #84#15 - #84#25;
           fmes = #84#1 % A402;
           mes = fmes - 1;
           mes = mes * 2;
           mes = mes + 11;
           #25mes = #25mes +. #84#26;
           retencion = #25mes % #25#39;
           mes = mes + 1;
           #25mes = #25mes +. imneto;
           #25#35 = #25#35 +. #84#26;
           #25#36 = #25#36 +. imneto;
           mes = fmes - 1;
           mes = mes + 40;
           #25mes = retencion;
           retencion = #25#35 % #25#39;
           #25#52 = retencion;
           |GRABA 020#25a;
           |LIBERA #25;

          enImpRepComi = #84#26 * nForma;
          enImpRepVta = imneto * nForma;
          enImpRepRete = retencion * nForma;
          eaRepre = #84#6;
          efFecha = #84#1;
          |HAZ AcumulaRep;
     |FINSI;
     |CIERRA #25;

     |ABRE #19;
     |BUCLELIN 2 ActArti #84;
     |CIERRA #2;

     |SI #142#47 = "S";  ||컴컴컴컴컴 Acumula en riesgos Si Crea Recibo
          eaTag = "FC";  |HAZ Riesgos;
     |FINSI;
|FPRC;

|PROCESO Linea;
     |DDEFECTO #19;
     |ABRE #19;
     #19#0 = aArti;
     |LEE 000#19=;
     |CIERRA #19;

     nLinea = nLinea + 1;
     |DDEFECTO #69;
     #69#20 = #84#48;
     #69#0 = #84#0;
     #69#1 = nLinea;
     #69#2 = #19#0;
     #69#3 = 1;
     #69#4 = #19#1;
     #69#5 = nUni;
     #69#6 = nPrecio;
     #69#11 = #19#30;
     #69#13 = #19#2;
     #69#15 = #84#3;
     #69#16 = #84#34;
     #69#17 = #84#35;

     enForma = 1;
     eaProgVta = "agifa084";
     |HAZ AcumulaFC;
     #69#14 = enCoste;
     |HAZ TotalLin69;
     |GRABA 020#69n;
|FPRC;

|PROCESO Cabecera;
     |DDEFECTO #24;
     #24#0 = #1#1;
     |LEE 000#24=;

     |DDEFECTO #25;
     #25#0 = #24#25;
     |LEE 000#25=;

     nLinea = 0;

     |DDEFECTO #84;
     #84#48 = #1#10;
     enSwMenu = 1; |HAZ ContaFD7;
     |GRABA 020#84b;
     #84#1 = #0#2;
     #84#3 = #24#0;
     #84#4 = #24#1;
     #84#5 = #24#37;
     #84#6 = #25#0;
     #84#7 = #24#38;
     #84#8 = #24#20;
     #84#29 = #24#1;
     #84#30 = #24#8;
     #84#31 = #24#9;
     #84#32 = #24#156;
     #84#33 = #24#10;
     #84#34 = #25#7;
     #84#35 = #24#4;
     #84#36 = #24#47;
     #84#40 = #24#16;
     #84#60 = #24#15;
     #84#64 = #24#183;
     #84#65 = #24#192;

     #324#0 = #84#65;
     |LEE 000#324=;
     #84#66 = #324#2;

     #84#67 = #24#193;
     #84#68 = #321#9;

     #324#0 = #84#68;
     |LEE 000#324=;
     #84#69 = #324#2;

     #84#78 = #24#202;
     #84#88 = #24#23;
     #84#89 = #24#24;
     #84#90 = #24#25
     #84#91 = 1;         || Dir. Entrega

     aAlfa = ("000000" + #1#0) % -106;
     #84#87 = aAlfa;     || Curso = N Contrato

     #20#0 = #84#78;
     |LEE 000#20=;
     |SI FSalida = 0;
          #84#9 = #20#1;
     |FINSI;

     #3#0 = #84#3;
     #3#1 = #84#91;
     |LEE 000#3=;
     |SI FSalida = 0;
          #84#2 = #3#10;
          #84#29 = #3#10;
          #84#30 = #3#2;
          #84#31 = #3#3;
          #84#64 = #3#6;
          #84#33 = #3#7;
     |FINSI;

     aDivisa = #84#65;
     aMoneda = #84#67;
     |HAZ Divisas084;

     #134#0 = #84#48;
     #134#1 = #84#0;
     |GRABA 020#134n;
     nHay = 1;
|FPRC;

|PROCESO CreaFactura;
     |ABRE #84;
     |ABRE #69;
     |ABRE #24;
     |ABRE #25;
     |ABRE #324;
     |ABRE #20;
     |ABRE #3;
     |ABRE #134;

     |HAZ Cabecera;

     |SI nSwMontaje ! 0;
          aArti = #2#2;
          nUni = 1;
          nPrecio = #1#8;
          |HAZ Linea;
     |FINSI;

     aArti = #2#4;
     nPrecio = #1#7;
     |SI aPeri = "D";
          nUni  = nDias;
     |SINO;
          nUni = 1;
          |SI nSwMontaje = 1; |O nSwDesmonta = 1;
               nPrecio = nPrecio / nDiasPeriodo * nDias;
          |FINSI;
     |FINSI;
     |HAZ Linea;

     |SI nSwDesmonta ! 0;
          aArti = #2#3;
          nUni = 1;
          nPrecio = #1#9;
          |HAZ Linea;
     |FINSI;

     |HAZ LimCabAgifa084;
     |BUCLELIN 0 CalCabAgifa084 #84;
     |GRABA 020#84a;
     |LIBERA #84;

     |CIERRA #134;
     |CIERRA #3;
     |CIERRA #20;
     |CIERRA #324;
     |CIERRA #25;
     |CIERRA #24;
     |CIERRA #69;
     |CIERRA #84;
|FPRC;

|PROCESO SacaDias;
     nDias = 0;
     |PARA f = fDesde; |SI f [ fHasta; |HACIENDO f = f + 1;
          nDias = nDias + 1;
     |SIGUE;
|FPRC;

|PROCESO UltDiasMes;
     aMes = fFecha % 402;
     |PARA f = fFecha; |SI; |HACIENDO f = f + 1;
          aAlfa = f % 402;
          |SI aAlfa ! aMes; |SAL; |FINSI;
          fFecha = f;
     |SIGUE;
|FPRC;

|PROCESO Calculo;
     aAlfa = "0000Periodo:" + fIniFactu + "-" + fFinFactu;
     ||MENAV aAlfa;

     fDesde = fIniFactu;
     fHasta = fFinFactu;
     |HAZ SacaDias;
     nDiasPeriodo = nDias;  || total dias del periodo a facturar

     fHasta1 = fHasta;
     fDesde1 = fDesde;
     |SI fConIni ] fDesde; |Y fConIni [ fHasta;
          fDesde1 = fConIni;
          nSwMontaje = 1;
     |FINSI;
     |SI fConFin ] fDesde; |Y fConFin [ fHasta;
          fHasta1 = fConFin;
          nSwDesmonta = 1;
     |FINSI;

     |SI nSwMontaje = 1; |O nSwDesmonta = 1;
          fDesde = fDesde1;
          fHasta = fHasta1;
          |HAZ SacaDias;  || dias a facturar (parcial)
     |SINO;
          |HAZ SacaDias;  || dias a facturar (completo)
     |FINSI;

     aAlfa = "0000Factura: " + fDesde + " - " + fHasta + " Periodo/Dias factura:" + nDiasPeriodo +"/"+nDias;
     ||MENAV aAlfa;

     |HAZ CreaFactura;
|FPRC;

|PROCESO Contrato;
     |DDEFECTO #2;
     #2#0 = #1#4;
     |LEE 000#2=;

     fConIni = #1#2;
     fConFin = #1#3;
     |SI fConFin = "01.01.0000"; fConFin = "31.12.9999"; |FINSI;
     nMesIni = #1#6;
     aPeri = #1#5;

     nFAno = #0#0;
     nFMes = #0#1;

     |SI aPeri = "D"; nInc = 1; nMesIni = nFMes; |FINSI;
     |SI aPeri = "M"; nInc = 1; nMesIni = nFMes; |FINSI;
     |SI aPeri = "B"; nInc = 2; |FINSI;
     |SI aPeri = "T"; nInc = 3; |FINSI;

     nNume = nFMes - nInc + 1;
     nNume1 = nFAno;
     |SI nNume < 1;
          nNume = nNume + 12;
          nNume1 = nFAno - 1;
     |FINSI;
     aAlfa = "01." + (("00" + nNume) % -102) + "." + (("0000" + nNume1) % -104);
     fIniFactu = aAlfa; || primer dia a facturar

     aAlfa = "01." + (("00" + nFMes) % -102) + "." + (("0000" + nFAno) % -104);
     fFecha = aAlfa;
     |HAZ UltDiasMes;
     fFinFactu = fFecha; || ultimo dia a facturar

     |SI fIniFactu [ fConFin; |Y fFinFactu ] fConIni; ||si entra en el periodo
          |PARA i = nMesIni; |SI; |HACIENDO i = i + nInc;
               nMes = ((i - 1) $ 12) + 1; || que no sea > 12
               |SI nMes = nMesIni; |Y i ! nMesIni; |SAL; |FINSI;
               |SI nMes = nFMes; ||si es el mes de factura
                    |HAZ Calculo;
               |FINSI;
          |SIGUE;
     |FINSI;
|FPRC;

|DEFBUCLE Contrato;
     #1#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Contrato;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |ABRE #321; |LEE 000#321p; |CIERRA #321;
     |ABRE #142; |LEE 000#142p; |CIERRA #142;

     aTmp = Usuario;
     |NOME_DAT #134 aTmp;
     |DELINDEX #134;

     |ABRE #2;
     |BUCLE Contrato;
     |CIERRA #2;

     |SI nHay ! 0;
          |CONFI "0000SImprimir Facturas";
          |SI FSalida = 0;
               eaFichero = aTmp;
               |SUB_EJECUTA_NP "agifa086";
          |FINSI;
     |FINSI;

     |DELINDEX #134;
|FPRC;
