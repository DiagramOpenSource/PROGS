|FICHEROS;
     primy003 #0;        || este def
     agifa104 #1;        || Pedido Venta (C)
     agifa073 #2;        || pedido Venta (L)
     primw002 #99;       || aux para el informe
|FIN;

|VARIABLES;
     aAlfa   = "";
     mes     = "";
     mesnu   = 0;
     &nTotal = 0;

     &enAlta = 0;
     &eaArticulo = "";
|FIN;

||INCLUYE llena_c1;
||Rellenar con 0 el cliente y el proveedor esta obsoleto

|PROCESO agifa073;
     |DDEFECTO #1;
     #1#0  = #2#0;       || nro pedido
     #1#25 = #2#28;      || serie pedido
     |LEE 020#1=;

     |SI #1#1 ] #0#4; |Y #1#1 [ #0#5;     || filtra la fecha
          mes = #1#1 % 402;   || coge el mes de la fecha
          |DDEFECTO #99;
          #99#0 = #2#2;
          |LEE 101#99=;         || busca en el tmp si ya esta
          |SI FSalida = 0;
               #99#1   = #2#4;  || descripcion del articulo
               mesnu   = mes;
               mesnu   = mesnu + 1;
               #99mesnu = #99mesnu + (#2#5 - #2#43);||Acumula unidades pdte.
               |GRABA 020#99a;
          |SINO;
               #99#1   = #2#4;
               mesnu   = mes;
               mesnu   = mesnu +1;
               #99mesnu = (#2#5 - #2#43);
               |GRABA 020#99n;
          |FINSI;
          |LIBERA #99;
     |FINSI;
|FPRC;

|DEFBUCLE agifa073;
     #2#1;
     2,20;
     #0#6, 000000, 000, #0#2, #0#0;
     #0#7, 999999, 999, #0#3, #0#1;
     be;
     NULL, agifa073;
|FIN;

|PROCESO dsw00022;
     nTotal = #99#2+#99#3+#99#4+#99#5+#99#6+#99#7+#99#8+#99#9+#99#10+#99#11+#99#12+#99#13;
     |PRINT;
|FPRC;

|DEFBUCLE dsw00022;
     #99#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, dsw00022;
|FIN;

|PROCESO Inicio;    |TIPO 3;
     aAlfa = Usuario % 108;
     |QBF aAlfa;
     |NOME_DAT #99 aAlfa;
     |ABRE #99;     |CIERRA #99;   |DELINDEX #99;
     |ABRE #99;
     |ABRE #1;
     |BUCLE agifa073;         || lee lineas de pedido de venta
     |IMPRE 1;
     |SI FSalida = 0;
          |INFOR "primy003";
          |SI FSalida = 0;
               |BUCLE dsw00022;         || imprime el temporal
               |PIEINF;
               |FININF;
          |FINSI;
          |FINIMP;
     |FINSI;
     |CIERRA #1;
     |CIERRA #99;
     |DELINDEX #99;
|FPRC;
