|FICHEROS;
     legim001 #0;
     legim002 #2;

     legim005 #5;        ||Formatos por series

     legim006 #6;        ||Parametros modulo
     legim008 #8;        ||Calendario Laboral

     legim003 #3;        ||Partes CAB
     legim004 #4;        ||Partes LIN
     legim017 #17;
|FIN;

|VARIABLES;
     i = 0;
     nConta = 0;
     aDia = "";
     aDiaIni = "";
     fFecha = @;
     nNume = 0;
     nLineas = 0;        ||Numero de lineas para cada periodo

     &enPartes = 0;
     &eaSerieContrato = "";
     &enNumeroContrato = 0;

     nUltimo = 0;
     aFechaVoy = "";
     aAnyo = "";
     nAnyo = 0;
     nSubLin = 0;

     fDiaSem = @;
     cDiaSem = "";

     nSwFestivo = 0;
     aPeriodo = "";
     fFechaVoy = @;
     f = @;

     aAlfa = "";
     aUsuario = "";
     aFicheroTxt = "";
     aTxt = "";
     aExe = "";
     nHandle = 0;
     aBase = "";
     aPathCli = "";
     aIPRemoto = "";
     fFechaFin = @;
     fFechaIni = @;
     aFechaFin = "";

     nModo = 0;
     contador = 0;
     cont = "";

     aMensaje = "";
     aAlfa1 = "";

     qMod = 0;
     aSerie = "";
     nNumero = 0;

     aInforme = "";
     nSwPie = 0;
|FIN;


|PROCESO EsFestivo;
     ||Miro los dias laborables y el calendario laboral
     ||devuelve nSwFestivo

     fDiaSem = fFechaVoy % 1;
     cDiaSem = fDiaSem % 11;

     |SI cDiaSem = "Lunes"; |Y #legim006#1 = "N"; nSwFestivo = 1; |FINSI;
     |SI nSwFestivo = 1; |ACABA; |FINSI;
     |SI cDiaSem = "Martes"; |Y #legim006#2 = "N"; nSwFestivo = 1; |FINSI;
     |SI nSwFestivo = 1; |ACABA; |FINSI;
     |SI cDiaSem = "Miercoles"; |Y #legim006#3 = "N"; nSwFestivo = 1; |FINSI;
     |SI nSwFestivo = 1; |ACABA; |FINSI;
     |SI cDiaSem = "Jueves"; |Y #legim006#4 = "N"; nSwFestivo = 1; |FINSI;
     |SI nSwFestivo = 1; |ACABA; |FINSI;
     |SI cDiaSem = "Viernes"; |Y #legim006#5 = "N"; nSwFestivo = 1; |FINSI;
     |SI nSwFestivo = 1; |ACABA; |FINSI;
     |SI cDiaSem = "Sabado"; |Y #legim006#6 = "N"; nSwFestivo = 1; |FINSI;
     |SI nSwFestivo = 1; |ACABA; |FINSI;
     |SI cDiaSem = "Domingo"; |Y #legim006#7 = "N"; nSwFestivo = 1; |FINSI;
     |SI nSwFestivo = 1; |ACABA; |FINSI;

     ||ARA legim006 y legim008

     aFechaVoy = fFechaVoy;
     aAnyo = aFechaVoy % -104;
     nAnyo = aAnyo;

     |DDEFECTO #legim008;
     |SELECT #2#legim008;
     #legim008#0 = nAnyo;
     #legim008#2 = fFechaVoy;
     |LEE 000#legim008.=;
     |SI FSalida = 0;
          nSwFestivo = 1;
     |FINSI;
|FPRC;

|PROCESO BorraDA;
     |BORRA 020#17a;
|FPRC;

|DEFBUCLE BorraDA;
     #17#1;
     ;
     "P", #4#0, #4#1, #4#2, 001;
     "P", #4#0, #4#1, #4#2, 999;
     be;
     NULL, BorraDA;
|FIN;

|PROCESO CopiaDA;
     #17#0 = "P";
     #17#1 = #4#0;
     #17#2 = #4#1;
     #17#3 = #4#2;
     |GRABA 020#17n;
|FPRC;

|DEFBUCLE CopiaDA;
     #17#1;
     ;
     "C", #2#0, #2#1, #2#2, 001;
     "C", #2#0, #2#1, #2#2, 999;
     ;
     NULL, CopiaDA;
|FIN;

|PROCESO legim002Parte;
     nSubLin = nSubLin + 1;
     |DDEFECTO #legim004;
     #legim004#0 = #legim003#0;
     #legim004#1 = #legim003#1;
     #legim004#2 = nSubLin;
     |LEE 101#legim004.=;
     |SI FSalida ! 0; |GRABA 020#legim004.b; |FINSI;

     #legim004#3 = #legim002#3;
     #legim004#4 = #legim002#4;
     #legim004#5 = #legim002#5;
     #legim004#6 = #legim002#6;
     #legim004#7 = #legim002#7;
     #legim004#8 = #legim002#8 / nConta;
     #legim004#10 = #legim002#10;
     #legim004#11 = #legim002#11;
     #legim004#9 = #legim004#7 * #legim004#8;
     #legim004#12 = #legim004#9 < #legim004#10;
     #legim004#12 = #legim004#12 < #legim004#11;
     |GRABA 020#legim004.a;
     |LIBERA #legim004;

     |BUCLE BorraDA;
     |BUCLE CopiaDA;
|FPRC;

|DEFBUCLE legim002Parte;
 #legim002#2;
 ;
 #legim001#0, #legim001#1, aPeriodo, 001;
 #legim001#0, #legim001#1, aPeriodo, 999;
 ;
 NULL, legim002Parte;
|FIN;

|PROCESO Parte;
     enPartes = enPartes + 1;

     |DDEFECTO #legim003;
     #legim003#0 = #legim001#0;
     #legim003#1 = 999999;
     |LEE 000#legim003.];
     |SI FSalida = 0;
          |LEE 000#legim003.a;
     |SINO;
          |LEE 000#legim003.u;
     |FINSI;
     |SI FSalida = 0; |Y #legim003#0 = #legim001#0;
          nUltimo = #legim003#1 + 1;
     |SINO;
          nUltimo = 1;
     |FINSI;

     |DDEFECTO #legim003;
     #legim003#0 = #legim001#0;
     #legim003#1 = nUltimo;
     |LEE 101#legim003.=;
     |SI FSalida ! 0; |GRABA 020#legim003.b; |FINSI;
     #legim003#2 = #legim001#2;
     #legim003#3 = #legim001#3;
     #legim003#4 = #legim001#0;
     #legim003#5 = #legim001#1;
     #legim003#6 = fFechaVoy;
     #legim003#13 = #legim001#9;
     #legim003#14 = #legim001#10;
     |GRABA 020#legim003.a;
     |LIBERA #legim003;

     nSubLin = 0;
     |BUCLE legim002Parte;
|FPRC;

|PROCESO legim002Cuenta;
     nLineas = nLineas + 1;
|FPRC;

|DEFBUCLE legim002Cuenta;
 #legim002#2;
 ;
 #legim001#0, #legim001#1, aPeriodo, 001;
 #legim001#0, #legim001#1, aPeriodo, 999;
 ;
 NULL, legim002Cuenta;
|FIN;

Asigna el dia de la fecha inicial para hacer el n§ partes correcto
si el contrato 31.07.2012/31.07.2013 y Periodo Trimestral debe de hacer
4 partes, sin esto hace 5:
31.07.2012
31.10.2012
31.01.2013
30.04.2013 ||porque no hay 31
30.07.2013 ||este seria incorrecto, deberia ser 31 y entonces no realizarlo

|PROCESO AjustaDia;
     aDia = f % 102;
     |SI aDia ! aDiaIni;
          aAlfa = aDiaIni + (f % 308);
          fFecha = aAlfa;
          nNume = fFecha;  || corrige la fecha 31.04.2013 no existe
          fFecha = nNume;  || al pasarla a numero quedaria 30.04.2013
          |SI aAlfa = fFecha;
               f = fFecha;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO PartesPeriodo;
     ||Bucle Para sumando dias .. y controlando que son laborables y NO festivos
     ||Y generando los partes

     fFechaIni = #legim001#4;
     fFechaFin = #legim001#5;
     aDiaIni = #legim001#4 % 102;

     nConta = 0;
     |PARA i = 1; |SI i [ 2; |HACIENDO i = i + 1;
          |PARA f = fFechaIni; |SI f < fFechaFin; |HACIENDO;
               fFechaVoy = f;
               |PARA; |SI; |HACIENDO;
                    nSwFestivo = 0;
                    |HAZ EsFestivo;
                    |SI nSwFestivo = 1;
                         fFechaVoy = fFechaVoy + 1;
                    |SINO;
                         |SAL;
                    |FINSI;
               |SIGUE;
               |SI i = 1;
                    nConta = nConta + 1;
               |SINO;
                    |HAZ Parte;
               |FINSI;
               |SI aPeriodo = "S"; nNume = 7; |FINSI;
               |SI aPeriodo = "Q"; nNume = 14; |FINSI;
               |SI aPeriodo = "M"; nNume = 1/1000; |FINSI;
               |SI aPeriodo = "3"; nNume = 3/1000; |FINSI;
               |SI aPeriodo = "4"; nNume = 4/1000; |FINSI;
               |SI aPeriodo = "E"; nNume = 6/1000; |FINSI;
               |SI aPeriodo = "A"; nNume = 1/1000000; |FINSI;
               f = f + nNume;
               |SI aPeriodo = "M"; |O aPeriodo = "3"; |O aPeriodo = "4";
                         |O aPeriodo = "E";
                    |HAZ AjustaDia;
               |FINSI;
          |SIGUE;
     |SIGUE;
|FPRC;

|PROGRAMA;
     enPartes = 0;

     |ABRE #legim006;
     |ABRE #legim008;
     |ABRE #legim003;
     |ABRE #legim004;
     |ABRE #legim001;

     |DDEFECTO #legim001;
     #legim001#0 = eaSerieContrato;
     #legim001#1 = enNumeroContrato;
     |LEE 000#legim001.=;
     |SI FSalida ! 0;
          |ACABA;
     |FINSI;

     |DDEFECTO #legim006;
     |LEE 000#legim006.p;

/*
     aPeriodo = #legim001#6;
     |QBF aPeriodo;
*/
     ||Ahora creo los partes segun la periodicidad de las lineas


     ||Semana
     nLineas = 0;
     aPeriodo = "S";
     |BUCLE legim002Cuenta;
     |SI nLineas ! 0;
          |HAZ PartesPeriodo;
     |FINSI;

     ||Quincenal
     nLineas = 0;
     aPeriodo = "Q";
     |BUCLE legim002Cuenta;
     |SI nLineas ! 0;
          |HAZ PartesPeriodo;
     |FINSI;

     ||Mensual
     nLineas = 0;
     aPeriodo = "M";
     |BUCLE legim002Cuenta;
     |SI nLineas ! 0;
          |HAZ PartesPeriodo;
     |FINSI;

     ||Trimestral
     nLineas = 0;
     aPeriodo = "3";
     |BUCLE legim002Cuenta;
     |SI nLineas ! 0;
          |HAZ PartesPeriodo;
     |FINSI;

     ||Quatrimestral
     nLineas = 0;
     aPeriodo = "4";
     |BUCLE legim002Cuenta;
     |SI nLineas ! 0;
          |HAZ PartesPeriodo;
     |FINSI;

     ||Semestral
     nLineas = 0;
     aPeriodo = "E";
     |BUCLE legim002Cuenta;
     |SI nLineas ! 0;
          |HAZ PartesPeriodo;
     |FINSI;

     ||Anual
     nLineas = 0;
     aPeriodo = "A";
     |BUCLE legim002Cuenta;
     |SI nLineas ! 0;
          |HAZ PartesPeriodo;
     |FINSI;

     |CIERRA #legim001;
     |CIERRA #legim003;
     |CIERRA #legim004;
     |CIERRA #legim006;
     |CIERRA #legim008;
|FPRO;
