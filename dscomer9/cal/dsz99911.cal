     Procesos: CopiaPy, VerPython

|VARIABLES;
     &eaFicPy = "";

     &VERSION = "20080625";    ||Version Python
     VERMENS2 = "0000Es necesario que cierre todas las ventanas DIAGRAM y vuelva a entrar a esta opción.";

     nVersion = 0;
     nHandle = 0;
     aSistema = "";
     aNueva = "";
     aCar = "";
     nPos = 0;
     aLon = "";
     i = 0;
     aBat = "";

     {1}aContiene = "";
     {1}aLocal = "";
     aOrg = "";
     aDes = "";
     aAlfa = "";
     aBase = "";
     aIP = "";
     aFicPy = "";
     aMd5Org = "";
     aMd5Des = "";
     aFicTar = "";
     aFnt = "";
     nSwCorta = 0;
     nCheq = 0;
|FIN;
------------------------------------------------------------
|PROCESO CopiaRMD5;
     aContiene = aOrg;
     |ESPECIFICA "MD5", aContiene;
     aMd5Org = FSalida;

     aContiene = aDes;
     |ESPECIFICA "REMOTOMD5", aContiene;
     aMd5Des = FSalida;

     |SI aMd5Org ! aMd5Des;
          |ENVIA_FICHERO aOrg, aDes;
     |FINSI;
|FPRC;

|PROCESO CopiaMD5;
     aContiene = aOrg;
     |ESPECIFICA "MD5", aContiene;
     aMd5Org = FSalida;

     aContiene = aDes;
     |ESPECIFICA "MD5", aContiene;
     aMd5Des = FSalida;

     |SI aMd5Org ! aMd5Des;
          |COPIA_FICHERO aOrg, aDes;
     |FINSI;
|FPRC;

|PROCESO CopiaPy;
     aFicPy = eaFicPy;

     |DBASE aBase;
     aOrg = aBase + "plugins/" + aFicPy;

     aIP = "";
     |IP_REMOTO aIP;
     |SI aIP = "";
          |DBASS aBase;
          aAlfa = aBase + "python/";
          aAlfa = aAlfa + "aplicaciones/";
          |MKDIR aAlfa;
          aDes = aAlfa + aFicPy;
          |HAZ CopiaMD5;
     |SINO;
          |ESPECIFICA "RBASS", aLocal;
          aBase = aLocal; aAlfa = aBase + "python/";
          aAlfa = aAlfa + "aplicaciones/";
          |RMKDIR aAlfa;
          aDes = aAlfa + aFicPy;
          |HAZ CopiaRMD5;
     |FINSI;
|FPRC;
------------------------------------------------------------
|PROCESO VerServidor;
     |DBASS aBase;
     aAlfa = aBase + "python/python.ver";
     |XABRE "", aAlfa, nHandle;
     |SI FSalida = 0;
          |XLEE nHandle, 250, aAlfa;
          |XCIERRA nHandle;
          nVersion = aAlfa;
     |FINSI;
|FPRC;

|PROCESO VerPuesto;
     |ESPECIFICA "RBASS", aLocal;
     aBase = aLocal; aAlfa = aBase + "python/python.ver";
     |XABRE "C", aAlfa, nHandle;
     |SI FSalida = 0;
          |XLEE nHandle, 250, aAlfa;
          |XCIERRA nHandle;
          nVersion = aAlfa;
     |FINSI;
|FPRC;

|PROCESO CheqDllServidor;
     nCheq = 0;
     aAlfa = aBase + "python/dspython.dll";
     |XABRE "CE", aAlfa, nHandle;
     |SI FSalida = -1;
          |MENAV "0000Puesto preparado para actualizar.";
     |SINO;
          aAlfa = aBase + "python/python.bor";
          |XABRE "CW", aAlfa, nHandle;
          aAlfa = "pendiente de borrar";
          |XGRABA nHandle, aAlfa;
          |XCIERRA nHandle;
          |MENAV VERMENS2;
          nCheq = 1;
     |FINSI;
|FPRC;

|PROCESO CheqDllLocal;
     nCheq = 0;
     aAlfa = aBase + "python/dspython.dll";
     |XABRE "E", aAlfa, nHandle;
     |SI FSalida = -1;
          |MENAV "0000Local preparado para actualizar.";
     |SINO;
          aAlfa = aBase + "python/python.bor";
          |XABRE "W", aAlfa, nHandle;
          aAlfa = "pendiente de borrar";
          |XGRABA nHandle, aAlfa;
          |XCIERRA nHandle;
          |MENAV VERMENS2;
          nCheq = 1;
     |FINSI;
|FPRC;

|PROCESO CambiaBarra;
     aNueva = "";
     aLon = aAlfa % A0;
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aCar = aAlfa % nPos;
          |SI aCar = "/"; aCar = "\"; |FINSI;
          aNueva = aNueva + aCar;
     |SIGUE;
     aAlfa = aNueva;
|FPRC;

|PROCESO RBorraDir;
     |HAZ CambiaBarra;
     aAlfa = "RD /S /Q " + aAlfa;

     aBat = "C:\" + Usuario + ".bat";

     |XABRE "CW", aBat, nHandle;
     |XGRABA nHandle, aAlfa;
     |XCIERRA nHandle;

     |RSYSTEM aBat;
     |RFBORRA aBat;
|FPRC;

|PROCESO BorraDir;
     |QUE_SISTEMA aSistema;
     |SI aSistema = "ESDOS";
          |HAZ CambiaBarra;
          aAlfa = "RD /S /Q " + aAlfa;

          aBat = "C:\" + Usuario + ".bat";

          |XABRE "CW", aBat, nHandle;
          |XGRABA nHandle, aAlfa;
          |XCIERRA nHandle;

          |RSYSTEM aBat;
          |RFBORRA aBat;
     |SINO;
          aAlfa = "rm -R " + aAlfa;
          |SYSTEM aAlfa;
     |FINSI;
|FPRC;

|PROCESO VerPython;
     nSwCorta = 0;
     aIP = "";
     |IP_REMOTO aIP;
     |SI aIP = "";
          |DBASE aBase;
          aAlfa = aBase + "python/python.bor";
          |XABRE "E", aAlfa, nHandle;
          |SI FSalida ! -1;
               aAlfa = aBase + "python";
               |HAZ BorraDir;
               |HAZ CheqDllLocal;
               |SI nCheq = 0;
                    |MENAV "0000Desinstalación anterior completada.";
               |SINO;
                    FSalida = 1;
                    |ACABA;
               |FINSI;
          |FINSI;

          |HAZ VerServidor;
          FSalida = 0;
          |SI nVersion ] VERSION;  |ACABA;   |FINSI;   || version LOCAL OK

          |SI nVersion ! 0;   || la version cero es que no esta instalado
               aAlfa = aBase + "python";
               |HAZ BorraDir;

               aAlfa = aBase + "agitool/python.tgz";
               |FBORRA aAlfa;

               |HAZ CheqDllLocal;
               |SI nCheq ! 0;
                    FSalida = 1;
                    |ACABA;
               |FINSI;
          |FINSI;
     |SINO;
          |ESPECIFICA "RBASS", aLocal;
          aBase = aLocal; aAlfa = aBase + "python/python.bor";
          |XABRE "CE", aAlfa, nHandle;
          |SI FSalida ! -1;
               aAlfa = aBase + "python";
               |HAZ RBorraDir;
               |HAZ CheqDllServidor;
               |SI nCheq = 0;
                    |MENAV "0000Desinstalación anterior completada.";
               |SINO;
                    FSalida = 1;
                    |ACABA;
               |FINSI;
          |FINSI;

          |HAZ VerPuesto;
          FSalida = 0;
          |SI nVersion ] VERSION;  |ACABA;   |FINSI;   || version PUESTO OK

          |SI nVersion ! 0;   || la version cero es que no esta instalado
               aAlfa = aBase + "python";
               |HAZ RBorraDir;

               aAlfa = aBase + "agitool/python.tgz";
               |RFBORRA aAlfa;

               |HAZ CheqDllServidor;
               |SI nCheq ! 0;
                    FSalida = 1;
                    |ACABA;
               |FINSI;
          |FINSI;

          |HAZ VerServidor;
          |SI nVersion ] VERSION;  |ACABA;   |FINSI;   || version SERVIDOR OK

          |DBASS aBase;
          aAlfa = aBase + "python";
          |HAZ BorraDir;

          aAlfa = aBase + "agitool/python.tgz";
          |FBORRA aAlfa;

          FSalida = 0;
          |MENAV "0000Servidor preparado para actualizar.";
     |FINSI;
|FPRC;
