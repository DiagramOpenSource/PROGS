|FICHEROS;
     edi00009 #0;
     dsm00003 #3;
     agifa019 #19;
     agifa024 #24;
     agifa025 #25;
     edi00003 #30;
     agifa073 #73;
     agifa104 #104;
     agifa142 #142;
     agifa321 #321;
     agifa324 #324;

     agifa073@;
     referen@;
|FIN;

|INCLUYE i_varart;

|VARIABLES;
     aTmp73 = "";
     nConta = 0;
     nSwCheq = 0;
     &Tempo = "";
     aPathGofre = "";
     aNom = "";
     aOrg = "";
     aDes = "";
     nErrorArti = 0;
     nSwEsGofre = 0;
     &enSwMenu = 0;
     nSwCli = 09;
     nSwLin = 0;
     nSwCab = 0;
     nLin = 0;
     &nImpo = 0;
     nImpoIva = 0;
     mes = 0;
     fmes = "";
     nCalc = 0;
     &eaTag = "";

     &fed_dt = @;
     &art_dt = "";
     &cli_dt = "";
     &fam_dt = "";
     &iva_dt = "";
     &dtos_2 = 0;
     &dto_pt = 0;
     &dtos_1 = 0;
     &pvp_ve = 0;
     &tarifa = 0;
     &gru_dt = "";
     &uni_dt = 0;
     &uni_dt2 = 0;
     &nPromo = 0;
     &enMensaje = "";
     &aParam = "";
     &enDescuento1 = 0;
     &enDescuento2 = 0;
     &eaTComision = "";
     &enComision = 0;
     ||...
     RETORNO = "";
     nReg = 0;
     aSoloUno = "";
     aFicheros = "";
     nError = 0;
     nPos = 0;
     nRem = 0;
     aAlfa = "";
     aPath = "";
     aCar = "";
     nHandle = 0;
     aFic = "";
     aVal = "";
     nNivel = 0;
     aTmp = "";
     {100}Matriz = "";
     aLon = "";
     aTag = "";
     aIniTag = "N";
     aFinTag = "N";
     aIniPro = "N";
     aFinPro = "N";
     aFinTagVacio = "N";
     aIniAtri = "N";
     aTagAtri = "";
     aResul = "";
     aLetra = "";
     i = 0;
     aDato = "";
     nPosD = 0;
     nPosI = 0;
     aIniComi = "";
     aComilla = "";
|FIN;

|PROCESO SacaNombre;
     aLon = aOrg % A0;
     aNom = "";
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = -(i * 100 + 1)
          aCar = aOrg % nPos;
          |SI aCar = "\"; |O aCar = "/"; |SAL; |FINSI;
          aNom = aCar + aNom;
     |SIGUE;
|FPRC;

|PROCESO MueveFichero;
     |HAZ SacaNombre;
     aDes = aPathGofre + aNom;
     |RCOPIA_FICHERO aOrg, aDes;
     |SI FSalida = 0;
          |RFBORRA aOrg;
     |FINSI;
|FPRC;

|PROCESO ActCli;
     #19#0 = #73#2;
     |LEE 020#19=;

     #24#0 = #104#4;
     |LEE 101#24=;
     |SI FSalida = 0;
          |SI #19#194 = 2;
               nImpo = ((((#73#44 * #73#6) < #73#8) < #73#29) < #104#5) < #104#17;
          |SINO;
               nImpo = ((((#73#5 * #73#6) < #73#8) < #73#29) < #104#5) < #104#17;
          |FINSI;
          |SI #142#115 = "S";
               nImpo = nImpo < #104#6;
          |FINSI;
          fmes = #104#1 % A402;
          mes = fmes - 1;
          mes = mes * 3;
          mes = mes + 114;
          #24mes = #24mes   + nImpo;     || VENTA.
          #24#150 = #24#150 + nImpo;
          |GRABA 020#24a;
          |LIBERA #24;

          eaCliente = #104#4;
          efFecha = #104#1;
          enImpCliPed = nImpo;
          |HAZ AcumulaCli;
     |FINSI;

     eaCli = #104#4;
     enTiva = #73#14;
     efFiva = #104#1;
     |HAZ IVACli;
     nCalc = #73#11 % enIva;
     |SI #24#47 = "S";
          nCalc = nCalc + (#73#11 % enRec);
     |FINSI;
     nImpoIva = nImpoIva + (#73#11 + nCalc);
|FPRC;

|PROCESO Dto;
     fed_dt = #104#1;   || Fecha....
     art_dt = #73#2;   || Articulo.
     cli_dt = #73#20;  || Cliente..
     fam_dt = #73#18;  || Familia.
     iva_dt = #19#30;  || Tipo Iva.
     dtos_2 = 0;      || Descuento 2.
     dto_pt = 0;      || Descuento Ptas.
     dtos_1 = 0;      || Descuento 1.
     pvp_ve = 0;      || Precio Venta.
     tarifa = #24#39;  || Tarifa Cliente....
     gru_dt = #24#158; || Grupo Cliente.
     |SI #73#36 ! 0;
           uni_dt = #73#36;
     |SINO;
           uni_dt = #73#5;
     |FINSI;
     uni_dt2 = #73#44;
     nPromo = #104#56;
     enMensaje = 0;
     aParam = "";
     eaSerie = #73#28;
     enDirEnt = #104#57;
     |SI #19#176 = "S";  enMensaje = 1;  |FINSI;
     enAlmacen = #73#3;
     enInterno = tarifa;
     |HAZ calcdtos;    || Calculo Dtos.....
     #73#6 = pvp_ve - dto_pt;  || Precio Venta. sin iva.
     #73#8 = dtos_1;  || 1¦ Dto.
     #73#29 = dtos_2;  || 2§ Dto.
|FPRC;

|PROCESO Comi;
     enDescuento1 = #73#8;
     enDescuento2 = #73#29;
     eaTComision  = #73#17;
     eaCliente    = #104#4;
     eaRepre      = #104#7;
     eaArticulo   = #73#2;
     efFecha      = #104#1;
     eaSerie      = #73#28;
     enDirEnt     = #104#57;
     enAlmacen    = #73#3;
     |SI #73#36 ! 0; uni_dt = #73#36; |SINO; uni_dt = #73#5; |FINSI;
     |HAZ Comisiones;
     #73#10 = enComision;
|FPRC;

|PROCESO DatosCab;
     |ABRE #24;
     #24#0 = #104#4;
     |LEE 000#24=;
     |SI FSalida ! 0; |DDEFECTO #24; |FINSI;
     |CIERRA #24;

     |DDEFECTO #3;
     |ABRE #3;
     #3#0 = #104#4;
     #3#1 = 1;
     |LEE 000#3=;
     |CIERRA #3;

     |DDEFECTO #25;
     |ABRE #25;
     |SI #3#14 = "  ";
         #25#0 = #24#25;
     |SINO;
         #25#0 = #3#14;
     |FINSI;
     #25#0 = #24#25;
     |LEE 000#25=;
     |CIERRA #25;

     #104#5 = #24#37;
     #104#6 = #24#39;
     #104#8 = #24#2;
     #104#9 = #24#20;

     #104#57 = #3#1;
     #104#19 = #3#10;
     #104#14 = #3#2;
     #104#20 = #3#3;
     #104#21 = #3#7;
     #104#59 = #3#6;
     #104#7 = #25#0;

     #104#16 = #25#7;
     #104#17 = #24#156;
     #104#18 = #24#4;
     #104#22 = #24#47;
     #104#23 = #24#41;
     #104#35 = #24#192;

     #324#0 = #104#35;
     |LEE 000#324=;
     #104#36 = #324#2;

     #104#37 = #24#193;
     #104#40 = #321#9;

     #324#0 = #104#40;
     |LEE 000#324=;
     #104#41 = #324#2;

     #104#60 = #24#202;
     #104#64 = #24#15;

     |HAZ Divisas104;
     |HAZ CalCabAgifa104;
     |GRABA 020#104a;

     nImpoIva = 0;
     |ABRE #24;
     |ABRE #19;
     |BUCLELIN 2 ActCli #104;
     |CIERRA #24;
     |CIERRA #19;
     nImpo = nImpoIva; eaTag = "PE";  |HAZ Riesgos;
|FPRC;

|PROCESO DatosLin;
     |ABRE #19;
     #19#0 = #73#2;
     |LEE 000#19=;
     |SI FSalida ! 0; |DDEFECTO #19; |FINSI;
     |CIERRA #19;

     #73#28 = #104#25;
     #73#0 = #104#0;
     #73#3 = 1;
     #73#13 = #104#2;
     #73#14 = #19#30;
     #73#16 = #104#7;
     #73#17 = #104#16;
     #73#18 = #19#2;
     #73#19 = #104#18;
     #73#20 = #104#4;
     #73#23 = #104#5;
     #73#24 = #104#6;
     #73#25 = #104#17;
     #73#26 = #104#9;

     |HAZ Dto;
     |HAZ Comi;
     |HAZ TotalLin73;
     enForma = 1;
     |HAZ AcumulaPV;
     |GRABA 020#73n;
|FPRC;

|PROCESO Tmp2Lineas;
     |ABRE #19;
     |SELECT #4#19;
     #19#128 = #agifa073@#2;
     |LEE 000#19=;
     |SI FSalida = 0;
          #agifa073@#2 = #19#0;
     |SINO;
          aAlfa = #agifa073@#2; |QBF aAlfa;
          aAlfa = "0000Revisar [" + aNom + "], codigo EAN: [" + aAlfa + "]" + " no existe.";
          |MENAV aAlfa;
          nErrorArti = 1;
     |FINSI;
     |CIERRA #19;
     #agifa073@#6 = #19#18;

     |SI nSwCheq = 0;
          |SALVA_DATOS #agifa073@;
          |REPON_DATOS #73;
          |HAZ DatosLin;
     |FINSI;
|FPRC;

|DEFBUCLE Tmp2Lineas;
     #agifa073@#1003;
     ;
     "     ", 000000, 001;
     "zzzzz", 999999, 999;
     ;
     NULL, Tmp2Lineas;
|FIN;

|PROCESO ProcesaTagPop;
     ||aAlfa = "0000Push: " + aTag + " => " + aVal; |MENAV aAlfa;

     |SI aTag = "CABECERA";
          |HAZ SacaNombre;
          |ABRE #73;
          nErrorArti = 0;
          nSwCheq = 1; |BUCLE Tmp2Lineas;
          |SI nErrorArti = 0;
               |SALVA_DATOS #104;
               enSwMenu = 1;
               |HAZ ContaPV7;
               nConta = #104#0;
               |REPON_DATOS #104;
               #104#0 = nConta;
               |GRABA 020#104b;
               nSwCheq = 0;|BUCLE Tmp2Lineas;
               |HAZ DatosCab;
          |FINSI;
          |CIERRA #73;

          |CIERRA #104;
          |CIERRA #agifa073@;
          |DELINDEX #agifa073@;
          nSwCab = 0;
          nLin = 0;
     |FINSI;
     |SI aTag = "LINEA";
          nLin = nLin + 1;
          #agifa073@#1 = nLin;
          |GRABA 020#agifa073@.n;
          nSwLin = 0;
     |FINSI;
     |SI nSwCab = 1;
          |SI aTag = "NUMPED";
               #104#3 = aVal;
          |FINSI;
          |SI aTag = "FECHA";
               aAlfa = (aVal % 702) +"."+ (aVal % 502) +"-"+ (aVal % 104);
               #104#1 = aAlfa;
          |FINSI;
          |SI aTag = "FECHAEPR";
               aAlfa = (aVal % 702) +"."+ (aVal % 502) +"-"+ (aVal % 104);
               #104#2 = aAlfa;
          |FINSI;
          |SI aTag = "OBSER";
               aAlfa = #104#15; |QBF aAlfa;
               |SI aAlfa ! ""; aAlfa = aAlfa + " "; |FINSI;
               aAlfa = aAlfa + aVal;
               #104#15 = aAlfa;
          |FINSI;
          |SI aTag = "RECEPTOR";
               nSwCli = 0;
          |FINSI;
          |SI aTag = "COD_INTERLOCUTOR"; |Y nSwCli = 1;
               |ABRE #30; |SELECT #3#30;
               #30#1 = aVal;
               |LEE 000#30=;
               |SI FSalida = 0;
                    #104#4 = #30#0;
               |FINSI;
               |CIERRA #30;
          |FINSI;
     |FINSI;
     |SI nSwLin = 1;
          |SI aTag = "REFEAN";
               #agifa073@#2 = aVal;
          |FINSI;
          |SI aTag = "DESCMER";
               #agifa073@#4 = aVal;
          |FINSI;
          |SI aTag = "CANTPED";
               #agifa073@#5 = aVal;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO ProcesaTagPush;
     ||aAlfa = "0000Push: " + aTag + " => " + aTagAtri; |MENAV aAlfa;

     |SI aTag = "CABECERA";
          nSwCab = 1;
          |ABRE #104;
          |ABRE #agifa073@;
          |DDEFECTO #104;
          |DDEFECTO #agifa073@;
          |SI #142#185 = "S";
               #104#25 = #142#186;  ||Serie defecto
          |FINSI;
     |FINSI;
     |SI aTag = "RECEPTOR"; |Y nSwCab = 1;
          nSwCli = 1;
     |FINSI;
     |SI aTag = "LINEA"; |Y nSwCab = 1;
          nSwLin = 1;
     |FINSI;
|FPRC;

|PROCESO ProcesaTagVacio;
|FPRC;

|PROCESO ProcesaPrologo;
|FPRC;

|PROCESO ProcesaRem;
|FPRC;

|PROCESO GetAtri;
     aResul = "";
     aAlfa = aTagAtri - aAlfa;
     |SI FSalida > 0;
          aResul = ("00000" + FSalida) % -105;
          nPosI = (A\(aResul % 103));
          nPosD = (A\(aResul % 402));
          nPos = nPosI + nPosD;
          aDato = aTagAtri;

          || si nPos > 100, FALLA: aTagAtri % nPos;
          |PARA; |SI nPos > 0; |HACIENDO;
               nPos = nPos - 90;
               |SI nPos < 0;
                    nPos = nPos + 90;
                    aDato = aDato % nPos;
                    |SAL;
               |SINO;
                    aDato = aDato % 91;
               |FINSI;
          |SIGUE;
          || busca la parte entre ""
          aComilla = &34;
          aIniComi = "N";
          aResul = "";
          aLon = aDato % A0;
          |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
               nPos = i * 100 + 1;
               aLetra = aDato % nPos;
               |SI aLetra = aComilla;
                    |SI aIniComi = "S";
                         |SAL;
                    |SINO;
                         aIniComi = "S";
                    |FINSI;
               |SINO;
                    |SI aIniComi = "S";
                         aResul = aResul + aLetra;
                    |FINSI;
               |FINSI;
          |SIGUE;
     |FINSI;
    aAlfa = aResul;
|FPRC;

|PROCESO QuitaBlancos;   || a izquierda y derecha
     aResul = "";
     aLon = aAlfa % A0;
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aLetra = aAlfa % nPos;
          |SI aLetra ! " "; |O aResul ! "";
               aResul = aResul + aLetra;
          |FINSI;
     |SIGUE;
     aAlfa = aResul;
     |QBF aAlfa;
|FPRC;

|PROCESO PopTag;
     nNivel = nNivel - 1;
     IMatriz = Matriz1<-;
     IMatriz = IMatriz + nNivel;
     FSalida = Matriz;
|FPRC;

|PROCESO PushTag;
     IMatriz = Matriz1<-;
     IMatriz = IMatriz + nNivel;
     Matriz = aTag;
     nNivel = nNivel + 1;
|FPRC;

|PROCESO PinLinea;
     nReg = nReg + 1;
     aAlfa = "Linea: " + nReg + "             ";
     |PINTA 1402, aAlfa;
|FPRC;

|PROCESO Caracter;
     |SI aCar = RETORNO; |HAZ PinLinea; |FINSI;

     |SI aCar < " "; |O nError ! 0; |ACABA; |FINSI;

     |SI nRem > 0;
          |SI nRem < 3;
               |SI aCar = "-";
                    nRem = nRem + 1;
               |SINO;
                    aAlfa = "0000ERROR en XML: " + aTag;
                    |MENAV aAlfa;
                    nError = 1;
               |FINSI;
          |SINO;
               |SI aCar = ">"; || Un comentario no puede tener el caracter '>'
                    aAlfa = aTag % -102;
                    |SI aAlfa = "--";
                         aLon = aTag % A0;
                         nPos = (aLon - 2) + 100;
                         aTag = aTag % nPos;
                         |HAZ ProcesaRem;
                    |SINO;
                         aAlfa = "0000ERROR en XML: " + aTag;
                         |MENAV aAlfa;
                         nError = 1;
                    |FINSI;
                    aTag = ""; nRem = 0;
               |SINO;
                    aLon = aTag % A0;
                    |SI aLon < 250; aTag = aTag + aCar; |FINSI;
               |FINSI;
          |FINSI;
          |ACABA;
     |FINSI;

     |SI aCar = "<";
          aIniTag = "S"; aTag = "";
     |SINO;
          |SI aIniTag = "S";
               |SI aCar = "/"; |Y aIniAtri = "N";
                    |SI aTag = "";
                         aFinTag = "S";
                    |SINO;
                         aFinTagVacio = "S";
                    |FINSI;
               |SINO;
                    |SI aCar = "!"; |Y aIniAtri = "N";
                         |SI aTag = "";
                              nRem = 1;
                         |SINO;
                              aAlfa = "0000ERROR en XML: " + aTag;
                              |MENAV aAlfa;
                              nError = 1;
                         |FINSI;
                         |ACABA;
                    |FINSI;
                    |SI aCar = "?"; |Y aIniAtri = "N";
                         |SI aIniPro = "S";
                              aFinPro = "S";
                              aIniPro = "N";
                         |SINO;
                              |SI aFinPro = "S";
                                   aAlfa = "0000ERROR en XML: " + aTag;
                                   |MENAV aAlfa;
                                   nError = 1;
                              |SINO;
                                   aIniPro = "S"; aTag = "";
                              |FINSI;
                         |FINSI;
                    |SINO;
                         |SI aCar = ">";
                              aAlfa = aTag; |HAZ QuitaBlancos; aTag = aAlfa;
                              aAlfa = aTagAtri; |HAZ QuitaBlancos; aTagAtri = aAlfa;
                              |SI aFinPro = "S";
                                   |HAZ ProcesaPrologo;
                                   aFinPro = "N";
                              |SINO;
                                   |SI aFinTag = "S";
                                        |HAZ PopTag;
                                        |SI aTag ! FSalida;
                                             aAlfa = "0000ERROR en XML: <" + FSalida + "> " + aVal;
                                             |MENAV aAlfa;
                                             nError = 1;
                                        |SINO;
                                             |HAZ ProcesaTagPop;
                                        |FINSI;
                                   |SINO;
                                        |SI aFinTagVacio = "S";
                                             |HAZ ProcesaTagVacio;
                                             aFinTagVacio = "N";
                                        |SINO;
                                             |HAZ PushTag;
                                             |HAZ ProcesaTagPush;
                                        |FINSI;
                                   |FINSI;
                              |FINSI;
                              aIniTag = "N"; aFinTag = "N"; aTag = ""; aVal = "";
                              aIniAtri = "N"; aTagAtri = "";
                         |SINO;
                              aLon = aTag % A0;
                              |SI aLon < 250;
                                   |SI aCar = " "; |Y aIniAtri = "N";
                                        aIniAtri = "S";
                                   |SINO;
                                        |SI aIniAtri = "S";
                                             aTagAtri = aTagAtri + aCar;
                                        |SINO;
                                             aTag = aTag + aCar;
                                        |FINSI;
                                   |FINSI;
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |SINO;
               aLon = aVal % A0;
               |SI aLon < 250; aVal = aVal + aCar; |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO XML;
     nReg = 0;
     nRem = 0;
     nNivel = 0;
     aVal = "";
     aTmp = "";
     aTag = "";
     aIniTag = "N";
     aFinTag = "N";
     aIniPro = "N";
     aFinPro = "N";
     aFinTagVacio = "N";
     aIniAtri = "N";
     aTagAtri = "";

     |XLEE nHandle, 250, aTmp;
     |HAZ PinLinea;
     |PARA; |SI FSalida > 0; |HACIENDO;
          |PARA; |SI aTmp ! ""; |HACIENDO;
               aCar = aTmp % 101;
               |SI aTmp = aCar;
                    aTmp = ""; ||con un caracter falla: "aTmp = aTmp % 2"
               |SINO;
                    aTmp = aTmp % 2;
               |FINSI;
               |HAZ Caracter;
               |SI nError ! 0; |SAL; |FINSI;
          |SIGUE;
          |XLEE nHandle, 250, aTmp;
          |SI nError ! 0; |SAL; |FINSI;
     |SIGUE;
|FPRC;

|PROCESO T3; |TIPO 3;
     aSoloUno = "N";
     aAlfa = #0#1; |QBF aAlfa;

     |SI aSoloUno = "S";           || #0#1 es fichero
          |XABRE "EC", aAlfa, 0;
          |SI FSalida ] 0;
               |DFICE aFic;
               aFic = aFic + Usuario;
               |RECIBE_FICHERO aAlfa, aFic;
               |SI FSalida ! 0;
                    |MENAV "0000Error al copiar fichero al servidor...";
               |SINO;
                    |XABRE "B", aFic, nHandle;
                    |SI FSalida ] 0;
                         |HAZ XML;
                         |XCIERRA nHandle;
                         |FBORRA aFic;
                    |FINSI;
               |FINSI;
          |SINO;
               |MENAV "0000No existe fichero...";
          |FINSI;
     |SINO;                   || #0#1 es directorio
          aCar = #0#1 % -101;
          |SI aCar ! "/"; |Y aCar ! "\";
               aAlfa = aAlfa + "/";
          |FINSI;
          aAlfa = aAlfa + "*.xml";
          aFicheros = aAlfa;
          |R_PDIR aFicheros;
          |PARA; |SI FSalida = 0; |HACIENDO;
               aAlfa  = aFicheros;
               aOrg = aAlfa;
               nErrorArti = 0;
               |PINTA 1302, aAlfa;

               |DFICE aFic;
               aFic = aFic + Usuario;
               |RECIBE_FICHERO aAlfa, aFic;
               |SI FSalida ! 0;
                    |MENAV "0000Error al copiar fichero al servidor...";
               |SINO;
                    |XABRE "B", aFic, nHandle;
                    |SI FSalida ] 0;
                         |HAZ XML;
                         |XCIERRA nHandle;
                         |FBORRA aFic;

                         |SI nSwEsGofre = 0; |Y nError = 0;
                              |SI nErrorArti = 0;
                                   |HAZ MueveFichero;
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |FINSI;

               |SI nError ! 0; |SAL; |FINSI;
               |R_SDIR aFicheros;
          |SIGUE;
     |FINSI;
|FPRC;

|PROCESO Fichero; |TIPO 0;
     |SI FSalida = 10;
          aAlfa = "D*.*";
          |BROWSE aAlfa;

          aAlfa = aAlfa % 2; |QBF aAlfa;
          |SI aAlfa = "D"; aAlfa = ""; |FINSI;

          |QBF aAlfa;
          |SI aAlfa = "";
               |ERROR;
          |SINO;
               #0#1 = aAlfa; |PINTA #0#1;
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROGRAMA;
     |HAZ EsGofre; nSwEsGofre = FSalida;

     |ABRE #142; |LEE 000#142p; |CIERRA #142;
     |ABRE #321; |LEE 000#321p; |CIERRA #321;
     |HAZ DecimalesBase;

     |SI nSwEsGofre = 0;
          |DDEF aAlfa;
          aAlfa = aAlfa + "gofrem01";
          |CARGA_DEF aAlfa, referen@;
          |SI FSalida = 0;
               |ABRE #referen@; |LEE 000#referen@.p; |CIERRA #referen@;
               aPathGofre = #referen@#3; |QBF aPathGofre;
               |DESCARGA_DEF #referen@;

               aAlfa = aPathGofre % -101;
               |SI aAlfa ! "/"; |Y aAlfa ! "\"; aPathGofre = aPathGofre + "/"; |FINSI;
               |RMKDIR aPathGofre;
          |FINSI;
     |FINSI;

     |DDEF aAlfa;
     aAlfa = aAlfa + "agifa073";
     |CARGA_DEF aAlfa, agifa073@;
     |HAZ CreaTempo; |NOME_DAT #agifa073@#Tempo#; aTmp73 = Tempo;

     RETORNO = &10;
     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0; |GRABA 020#0a; |FINSI;
     |CIERRA #0;

     Tempo = aTmp73; |HAZ BoraTempo; |DELINDEX #agifa073@;
     |DESCARGA_DEF #agifa073@;
|FPRO;
