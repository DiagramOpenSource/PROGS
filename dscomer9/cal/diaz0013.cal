|FICHEROS;
     diaz0013 #0;

     diam0023 #23;       ||Mant. Ofertas CAB
     diam0024 #24;       ||Mant. Ofertas LIN
     diam0025 #25;       ||Articulos Ofertas
     diam0027 #27;       ||Referencias

     :/dspartes/def/dstecm08 #508;      ||Grupos Ficha Cliente

     ma000001@ #200;
|FIN;

|VARIABLES;
     nExiste      = 0;
     nNumero      = 0;
     nTecGuardian = 0;

     aMensaje     = "";
     aAlfa        = "";
     aAlfa1       = "";

     fFechaD      = @;
     fFechaH      = @;
|FIN;

|PROCESO UltimoNumero;
     ||Obtiene el Ultimo"NUMERO" en Referencias. diam0027

     #27#0 = #0#7;
     #27#1 = #0#8;
     #27#2 = 99999;
     |LEE 000#27];
     |SI FSalida = 0;
          |LEE 000#27a;
     |SINO;
          |LEE 000#27u;
     |FINSI;

     nNumero = 1;
     |SI FSalida = 0; |Y #27#0 = #0#7; |Y #27#1 = #0#8;
          nNumero = #27#2 + 1;
     |FINSI;
|FPRC;

|PROCESO ma000001;
     nTecGuardian = #200#60;
|FPRC;

|DEFBUCLE ma000001;
     #200#2002;
     44;
     #27#4,      1,      1;
     #27#4, 999999, 999999;
     ;
     NULL, ma000001;
|FIN;

|PROCESO PonGuardian;
     nTecGuardian = 0;
     aAlfa1       = "";
     |DBASS aAlfa1;
     aAlfa1 = aAlfa1 + "dsman9/def/ma000001.mas";
     |CARGA_DEF aAlfa1, ma000001@;
     |SI FSalida ! 0;
         |MENAV "0000 Error en cargar el Def ma000001.mas";
         |ACABA;
     |FINSI;

     aAlfa1 = "";
     |DBASS aAlfa1;
     aAlfa1 = aAlfa1 + "dsman9/fich/84/";
     |PATH_DAT #200 aAlfa1;

     |BUCLE ma000001;

     |DESCARGA_DEF #ma000001@;
|FPRC;

|PROCESO dstecm08;
     |SELECT #4#27;
     #27#0 = #0#7;
     #27#1 = #0#8;
     #27#3 = #23#2;
     #27#4 = #508#0;
     |LEE 000#27=;
     |SI FSalida = 0;  |ACABA;  |FINSI;

     |SELECT #1#27;

     |DDEFECTO #27;
     #27#0 = #0#7;
     #27#1 = #0#8;
     #27#2 = nNumero;
     #27#3 = #23#2;
     #27#4 = #508#0;
     #27#6 = "S";

     |HAZ PonGuardian;
     #27#13 = nTecGuardian;

     |GRABA 020#27n;
     |LIBERA #27;

     nNumero = nNumero + 1;
|FPRC;

|DEFBUCLE dstecm08;
     #508#1;
     ;
     #0#1, #24#1;
     #0#2, #24#1;
     ;
     NULL, dstecm08;
|FIN;

|PROCESO diam0024;
     |SI #24#1 < #0#3;  |O #24#1 > #0#4;
         |ACABA;
     |FINSI;

     |BUCLE dstecm08;
|FPRC;

|PROCESO diam0023;
     #25#0 = #23#2;
     |LEE 000#25=;
     |SI FSalida ! 0;  |DDEFECTO #25;  |FINSI;

     |SI #25#2 < fFechaD;  |O #25#2 > fFechaH;
         |ERROR;
     |FINSI;
|FPRC;

|DEFBUCLE diam0023;
     #23#1;
     ;
     #0#5;
     #0#6;
     ;
     NULL, diam0023, diam0024;
|FIN;

|PROCESO Tipo3;  |TIPO 3;
     |SI #0#9 ! "SI";  |ACABA;  |FINSI;

     aAlfa   = "01." + (("00" + #0#8) % -102) + "." + #0#7;
     fFechaD = aAlfa;

     aAlfa   = "31." + (("00" + #0#8) % -102) + "." + #0#7;
     fFechaH = aAlfa;

     |ABRE #25;
     |ABRE #27;
     |HAZ UltimoNumero;
     |BUCLE diam0023;
     |CIERRA #25;
     |CIERRA #27;
|FPRC;

|PROGRAMA;
     |CLS;
     |ABRE #0;
     |DDEFECTO #0;
     |LEE 101#0p;
     |SI FSalida ! 0;
         |DDEFECTO #0;
         |GRABA 020#0b;
     |FINSI;

     #0#9 = "NO";
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
         |GRABA 020#0a;
     |FINSI;
     |LIBERA #0;
     |CIERRA #0;
|FPRO;
