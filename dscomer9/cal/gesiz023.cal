|FICHEROS;
     gesiz023 #0;
     gesim001 #1;
     gesim003 #3;
     gesiw004 #4;   ||Tmp marcas
     gesim006 #6;
     gesiw003 #30;  ||Tmp tecnicos
     gesiz024 #24;
     agifa024 #240;
|FIN;

|VARIABLES;
     &Tempo = "";
     &enTecnico = 0;
     {-1}aGet       = 0;
         aGet1      = 0;
         aGet2      = 0;
     aTmp30 = "";
     aTmp4 = "";
     aAlfa = "";
     nPanta = 0;
     nRango = 0;
     {14}Horas = 0;
     {7}Dias = 0;
     {7}CAviso = 0;
     {200}Marca = 0;
     {200}Parte = "";
     nPosPan = 0;
     nPos1 = 0;
     nPos2 = 0;
     nInc = 0;
     i = 0;
     f = @;
     f1 = @;
     fHoy = @;
     nHora = 0;
     aHora = "";
     nNume = 0;
     nDia = 0;
     fDia = @;
     aConte = "";
     nFila = 0;
     nCol = 0;
     nFinHora = 0;
     nIniHora = 0;
     aId = "";
     fHasta = @;
     aFecha = "";
     nCampo = 0;
     aNume = "";
     nGrid = 0;
     nAviso = 0;
     nCtrl = 0;
     nTotaAviso = 0;
     nBtnArriba = 0;
     nBtnAbajo = 0;
     nIniHoraParam = 0;
     nFinHoraParam = 0;
     aTipo = "";
|FIN;

|PROCESO BtnMarcaTodo;
     |SI #30#0 = "*";
          aAlfa = "";
     |SINO;
          aAlfa = "*";
     |FINSI;
     |LEE 000#30p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          #30#0 = aAlfa;
          |GRABA 020#30a;
          |LEE 000#30s;
     |SIGUE;
     |LEE 000#30p;
     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO EvtGrid;
     |SI FSalida = 1; |O FSalida = 2;
          |LEE 000#30=;
     |FINSI;
     |SI FSalida = 4;
          |SI #30#0 = "*";
               #30#0 = "";
          |SINO;
               #30#0 = "*";
          |FINSI;
          |GRABA 020#30a;
          |PINTA #30#0;
     |FINSI;
|FPRC;

|PROCESO Min;
     #30#1 = 0;
|FPRC;

|PROCESO Max;
     #30#1 = 99999;
|FPRC;

|PROCESO BtnTecnico;
     |VENTANA 0925, 3275, -1, 17, "Selecci¢n tecnicos";
     |HAZ PushVenModal;

     |CONTROL_SIMPLE 0, "BOTON,Marca/Desmarca todo", 1026, 1045, BtnMarcaTodo;
     |CONTROL_SIMPLE 0, "LABEL,{{011}}Doble click marca/desmarca tecnico", 1047, 1174, NULL;
     nRango = 4 + 8 + 16 + 32;
     |LINEAL_SIMPLE #1#30, nRango, 1126, 3074, Min, Max, EvtGrid;
     nGrid = FSalida;
     |PAUSA;

     |HAZ PopVenModal;

     |HAZ Refresca;
|FPRC;

|PROCESO SelePartes;
     #30#0 = "*";
     #30#1 = #6#10;
     |LEE 000#30=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     aAlfa = #6#62 % 102;
     |SI aAlfa ] nHora; |Y aAlfa [ nHora;
          |DDEFECTO #240;
          #240#0 = #6#39;
          |LEE 000#240=;

          |SI #6#63 = "N";
               aAlfa = "NORMAL";
               #24nCampo = aAlfa; nCampo = nCampo + 1;
          |FINSI;
          |SI #6#63 = "U";
               aAlfa = "URGENTE";
               #24nCampo = aAlfa; nCampo = nCampo + 1;
          |FINSI;
          |SI #6#63 = "E";
               aAlfa = "EXPRESS";
               #24nCampo = aAlfa; nCampo = nCampo + 1;
          |FINSI;

          aAlfa = "N§ Aviso:" + #6#0 + "/" + (("000000" + #6#1)%-106) + "/" + (("000" + #6#2)%-103);
          #24nCampo = aAlfa; nCampo = nCampo + 1;
          aAlfa = "Tecnico:" + (("00000" + #30#1)%-105) + #30#2;
          #24nCampo = aAlfa; nCampo = nCampo + 1;
          aAlfa = "Cliente:" + #6#39 + " " + #240#1;
          #24nCampo = aAlfa; nCampo = nCampo + 1;
          aAlfa= #6#12; |QBF aAlfa;
          |SI aAlfa ! ""; #24nCampo = #6#12; nCampo = nCampo + 1; |FINSI;
          aAlfa= #6#13; |QBF aAlfa;
          |SI aAlfa ! ""; #24nCampo = #6#13; nCampo = nCampo + 1; |FINSI;
          aAlfa= #6#14; |QBF aAlfa;
          |SI aAlfa ! ""; #24nCampo = #6#14; nCampo = nCampo + 1; |FINSI;
          aAlfa = " ---";
          #24nCampo = aAlfa; nCampo = nCampo + 1;
     |FINSI;
|FPRC;

|DEFBUCLE SelePartes;
     #6#9;
     63;
     "A", aFecha, "        ", aTipo;
     "A", aFecha, "zzzzzzzz", aTipo;
     ;
     NULL, SelePartes;
|FIN;

|PROCESO Muestra;
     aId = FSalida;
     IParte = Parte1<-;
     |PARA i = 0; |SI i < 100; |HACIENDO i = i + 1;
          aAlfa = Parte;
          aAlfa = aAlfa - aId;
          |SI FSalida > 0;
               aFecha = aAlfa % 110;
               aHora = aAlfa % 1102;
               nHora = aHora;
               aNume = aAlfa % 13;

               nCampo = 0;
               |DDEFECTO #24;
               |SALVA_DATOS #30;
               |SELECT #2#30;
               |ABRE #240;
               |BUCLE SelePartes;
               |CIERRA #240;
               |REPON_DATOS #30;
               |SELECT #1#30;
               |LEE 000#30=;

               aAlfa = "Avisos:" + aNume;
               |VENTANA 0912, 3188, -1, 17, aAlfa;
               |HAZ PushVenModal;
               |PINPA #0#24;
               |PINDA #0#24;
               |PAUSA;
               |HAZ PopVenModal;
               |SAL;
          |FINSI;
          IParte = IParte + 1;
     |SIGUE;
|FPRC;

|PROCESO BtnMarcaN;
     aTipo = "N";
     |HAZ Muestra;
|FPRC;

|PROCESO BtnMarcaU;
     aTipo = "U";
     |HAZ Muestra;
|FPRC;

|PROCESO BtnMarcaE;
     aTipo = "E";
     |HAZ Muestra;
|FPRC;

|PROCESO CuentaAviso;
     nAviso = nAviso + #4#2;
|FPRC;

|DEFBUCLE CuentaAviso;
     #4#1;
     ;
     f1, 0;
     f1, 99;
     ;
     NULL, CuentaAviso;
|FIN;

|PROCESO PinAviso;
     nPosPan = 3615;
     nTotaAviso = 0;
     f1 = #0#0;
     ICAviso = CAviso1<-;
     |PARA i = 0; |SI i < 7; |HACIENDO i = i + 1;
          nAviso = 0;
          |BUCLE CuentaAviso;
          nTotaAviso = nTotaAviso + nAviso;
          |SI nAviso = 0;
               aAlfa = "LABEL,{{007}}";
          |SINO;
               aAlfa = "LABEL,{{007}}" + nAviso;
          |FINSI;
          nPos1 = nPosPan + (i * 12);
          nPos2 = nPos1 + 5;
          |SI CAviso ! 0;
               nCtrl = CAviso;
               |FIN_CONTROL_WINDOWS nCtrl;
          |FINSI;
          |CONTROL_SIMPLE nPanta, aAlfa, nPos1, nPos2, NULL;
          CAviso = FSalida;
          ICAviso = ICAviso + 1;
          f1 = f1 + 1;
     |SIGUE;
     #0#1 = nTotaAviso; |PINTA #0#1;
|FPRC;

|PROCESO TmpPartes;
     nPosPan = 1014;
     nHora = #4#1;
     fDia = #4#0;
     nFinHora = nIniHora + 12;
     |SI nHora ] nIniHora; |Y nHora [ nFinHora;
          nCol = fDia - #0#0;
          nFila = nHora - nIniHora;
          nPos1 = nPosPan + (nFila * 100 * 2) + (nCol * 12);
          nPos2 = nPos1 + 3;
          |SI #4#3 ! 0;
               aAlfa = "BOTON,{{238}}" + #4#3;
               |CONTROL_SIMPLE nPanta, aAlfa, nPos1, nPos2, BtnMarcaN;
               Marca = FSalida;
               aAlfa = fDia; aAlfa = aAlfa + (("00" + nHora)%-102) + #4#3;
               Parte = FSalida + aAlfa;
               IMarca = IMarca + 1;
               IParte = IParte + 1;
               nPos1 = nPos2 + 1;
               nPos2 = nPos1 + 3;
          |FINSI;
          |SI #4#4 ! 0;
               aAlfa = "BOTON,{{237}}" + #4#4;
               |CONTROL_SIMPLE nPanta, aAlfa, nPos1, nPos2, BtnMarcaU;
               Marca = FSalida;
               aAlfa = fDia; aAlfa = aAlfa + (("00" + nHora)%-102) + #4#4;
               Parte = FSalida + aAlfa;
               IMarca = IMarca + 1;
               IParte = IParte + 1;
               nPos1 = nPos2 + 1;
               nPos2 = nPos1 + 3;
          |FINSI;
          |SI #4#5 ! 0;
               aAlfa = "BOTON,{{236}}" + #4#5;
               |CONTROL_SIMPLE nPanta, aAlfa, nPos1, nPos2, BtnMarcaE;
               Marca = FSalida;
               aAlfa = fDia; aAlfa = aAlfa + (("00" + nHora)%-102) + #4#5;
               Parte = FSalida + aAlfa;
               IMarca = IMarca + 1;
               IParte = IParte + 1;
               nPos1 = nPos2 + 1;
               nPos2 = nPos1 + 3;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE TmpPartes;
     #4#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, TmpPartes;
|FIN;

|PROCESO Partes;
     #30#0 = "*";
     #30#1 = #6#10;
     |LEE 000#30=;
     |SI FSalida = 0;
          aAlfa = #6#62 % 102;

          ||esto no deberia de ocurrir nunca
          |SI aAlfa < nIniHoraParam; |O aAlfa > nFinHoraParam;
               |LEE 121#6=;
               #6#62 = #1#36;
               |GRABA 020#6a;
               |LIBERA #6;
               aAlfa = #6#62 % 102;
          |FINSI;
          |DDEFECTO #4;
          #4#0 = #6#7;
          #4#1 = aAlfa;
          |LEE 101#4=;
          |SI FSalida ! 0; |GRABA 020#4b; |FINSI;
          #4#2 = #4#2 + 1;
          |SI #6#63 = "N"; #4#3 = #4#3 + 1; |FINSI;
          |SI #6#63 = "U"; #4#4 = #4#4 + 1; |FINSI;
          |SI #6#63 = "E"; #4#5 = #4#5 + 1; |FINSI;
          |GRABA 020#4a;
          |LIBERA #4;
     |FINSI;
|FPRC;

|DEFBUCLE Partes;
     #6#9;
     ;
     "A", #0#0, "        ";
     "A", fHasta, "zzzzzzzz";
     ;
     NULL, Partes;
|FIN;

|PROCESO PonMarca;
     IMarca = Marca1<-;
     IParte = Parte1<-;
     |PARA i = 0; |SI i < 100; |HACIENDO i = i + 1;
          nCtrl = Marca;
          |SI nCtrl > 0; |FIN_CONTROL_WINDOWS nCtrl; |FINSI;
          Marca = 0;

          Parte = "";
          IMarca = IMarca + 1;
          IParte = IParte + 1;
     |SIGUE;
     IMarca = Marca1<-;
     IParte = Parte1<-;

     |DELINDEX #4;
     fHasta = #0#0 + 7;
     |SALVA_DATOS #30;
     |SELECT #2#30;
     |ABRE #4;
     |BUCLE Partes;
     |CIERRA #4;
     |REPON_DATOS #30;
     |SELECT #1#30;
     |LEE 000#30=;
     |BUCLE TmpPartes;
|FPRC;

|PROCESO PinDias;
     |SI #0#0 = "01.01.0000";
          ||Busca el lunes
          #0#0 = ~;
          |PARA; |SI; |HACIENDO;
               f = #0#0 % 1;
               aAlfa = f % 1101;
               |SI aAlfa = "L"; |SAL; |FINSI;
               nNume = #0#0; nNume = nNume - 1; #0#0 = nNume;
          |SIGUE;
     |FINSI;

     nPosPan = 815;
     f1 = #0#0;
     fHoy = ~;
     IDias = Dias1<-;
     |PARA i = 0; |SI i < 7; |HACIENDO i = i + 1;
          f = f1 % 1;
          aAlfa = (f % 102); nDia = aAlfa;
          aAlfa = f % 11;
          aAlfa = aAlfa + " " + nDia;
          |SI f = fHoy;
               aAlfa = "LABEL,{{003}}" + aAlfa;
          |SINO;
               aAlfa = "LABEL,{{007}}" + aAlfa;
          |FINSI;
          nPos1 = nPosPan + (i * 12);
          nPos2 = nPos1 + 9;
          |SI Dias ! 0;
               nCtrl = Dias;
               |FIN_CONTROL_WINDOWS nCtrl;
          |FINSI;
          |CONTROL_SIMPLE nPanta, aAlfa, nPos1, nPos2, NULL;
          Dias = FSalida;
          IDias = IDias + 1;
          f1 = f1 + 1;
     |SIGUE;
|FPRC;

|PROCESO PinHoras;
     nPosPan = 1006;
     |HORA aHora; aAlfa = aHora % 102; nNume = aAlfa;
     IHoras = Horas1<-;
     |PARA i = 0; |SI i < 13; |HACIENDO i = i + 1;
          nHora = (nIniHora + i);
          |SI nHora > nFinHoraParam; |SAL; |FINSI;
          |SI nNume = nHora;
               |SI nHora ] 10;
                    aAlfa = "LABEL,{{003}}" + nHora + ":00";
               |SINO;
                    aAlfa = "LABEL,{{003}}0" + nHora + ":00";
               |FINSI;
          |SINO;
               |SI nHora ] 10;
                    aAlfa = "LABEL,{{007}}" + nHora + ":00";
               |SINO;
                    aAlfa = "LABEL,{{007}}0" + nHora + ":00";
               |FINSI;
          |FINSI;
          nPos1 = nPosPan + (i * 100 * 2);
          nPos2 = nPos1 + 4;
          |SI Horas ! 0;
               nCtrl = Horas;
               |FIN_CONTROL_WINDOWS nCtrl;
          |FINSI;
          |CONTROL_SIMPLE nPanta, aAlfa, nPos1, nPos2, NULL;
          Horas = FSalida;
          IHoras = IHoras + 1;
     |SIGUE;
|FPRC;

|PROCESO Conte; |TIPO 7;
     aConte = #0#0;
|FPRC;

|PROCESO Fecha; |TIPO 0;
     |SI #0Campo ! aConte;
          |HAZ Refresca;
     |FINSI;
|FPRC;

|PROCESO BtnArriba;
     |SI nIniHora = nIniHoraParam;
          |AVISO;
     |SINO;
          nIniHora = nIniHoraParam;
          |HAZ Refresca;
     |FINSI;
|FPRC;

|PROCESO BtnAbajo;
     nNume = nFinHoraParam - 12;
     |SI nNume = nIniHora;
          |AVISO;
     |SINO;
          nIniHora = nFinHoraParam - 12;
          |HAZ Refresca;
     |FINSI;
|FPRC;

|PROCESO BtnActua;
     |REFERENCIA_DEF aGet1, #0;
     aGet2  = 0;    || numero de campo
     IaGet = aGet1<-;
     |ESPECIFICA "GETDATACONTROL", aGet;
     #0#0 = FSalida; |PINTA #0#0;

     |HAZ Refresca;
|FPRC;

|PROCESO Refresca;
     |HAZ PinDias;
     |HAZ PinHoras;
     |HAZ PonMarca;
     |HAZ PinAviso;
|FPRC;

|PROCESO LlenaTmp;
     #30#0 = "";
     |SI enTecnico ! 0; |Y #3#0 = enTecnico;
          #30#0 = "*";
     |FINSI;
     |SI PARAMETRO = "menu";
          #30#0 = "*";
     |FINSI;
     #30#1 = #3#0;
     #30#2 = #3#1;
     |GRABA 020#30n;
|FPRC

|DEFBUCLE LlenaTmp;
     #3#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, LlenaTmp;
|FIN;

|PROCESO BtnSalir;
     |PTEC 807;
|FPRC;

|PROCESO T80; |TIPO 80;
     FSalida = 3899;
|FPRC;

|PROGRAMA;
     |ABRE #1; |LEE 000#1p; |CIERRA #1;
     aAlfa = #1#36 % 102; nIniHoraParam = aAlfa;
     aAlfa = #1#37 % 102; nFinHoraParam = aAlfa;
     nIniHora = nIniHoraParam;

     |HAZ CreaTempo; |NOME_DAT #30Tempo; aTmp30 = Tempo;
     |DELINDEX #30; |ABRE #30;
     |HAZ CreaTempo; |NOME_DAT #4Tempo; aTmp4 = Tempo;
     |DELINDEX #4;
     |BUCLE LlenaTmp;
     |LEE 000#30p;
     |PINPA #0#0; nPanta = FSalida;
     |PINDA #0#0;
     |CONTROL_SIMPLE nPanta, "BOTON,{{001}}Salir", 3887, 3897, BtnSalir;
     |CONTROL_SIMPLE nPanta, "BOTON,{{231}}", 806, 808, BtnArriba;
     nBtnArriba = FSalida;
     |CONTROL_SIMPLE nPanta, "BOTON,{{232}}", 809, 811, BtnAbajo;
     nBtnAbajo = FSalida;
     nNume = nFinHoraParam - nIniHoraParam;
     |SI nNume < 13;
          |ESTADO_CONTROL nBtnArriba, "DISABLE";
          |ESTADO_CONTROL nBtnAbajo, "DISABLE";
     |FINSI;
     |CONTROL_SIMPLE nPanta, "BOTON,Actualizar", 625, 635, BtnActua;
     |CONTROL_SIMPLE nPanta, "BOTON,Tecnicos", 650, 660, BtnTecnico;
     |HAZ Refresca;
     |PARA; |SI; |HACIENDO;
          FSalida = "::ENDATOS::SINBOTONES::";
          |ENDATOS #1#0;
          |SI FSalida ! 0; |SAL; |FINSI;
     |SIGUE;
     |CIERRA #30; |DELINDEX #30; Tempo = aTmp30; |HAZ BoraTempo;
     |DELINDEX #4; Tempo = aTmp4; |HAZ BoraTempo;
     |PULSATECLA;
|FPRO;
