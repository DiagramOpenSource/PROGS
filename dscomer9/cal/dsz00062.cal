|FICHEROS;
     dsz00062 #0;
     dsw00057 #1;
     dsw00058 #2;
     dsm00006 #6;
     agifa007 #7;
     agifa010 #10;
     agifa071 #71;
     agifa065 #65;
     agifa019 #19;
     agifa084 #84;
     agifa069 #69;
     agifa077 #77;
     agifa080 #80;
     agifa142 #142;
     dsm00056 #56; ||Valora
     dsm00057 #57;
     dsm00043 #43; ||Regula
     dsm00044 #44;
     dsm00054 #54; ||Movi
     dsm00055 #55;
     agifa134 #134;
     agifa059 #59;
     dsm00010 #100;
     agifa017 #17;
     dsm00024 #24;
     agifa065@ #99;

     agifa024;
|FIN;

|VARIABLES;
     &eaArticulo = "";
     &enPcm = 0;
     &efFecha = @;
     &eaDes = "";
     &enInci = 0;
     nPrimeEntra = 0;
     nPorceMax = 0;
     nPorceMin = 0;
     nSwVigor = 0;
     aDef = "";
     nEsUltimo = 0;
     fFecAnt = "";
     aArtAnt = "";

     aAlfa = "";
     fMas = @;
     nValor = 0;

     aUlt0 = "";
     fUlt1 = @;
     aUlt2 = "";
     nUlt3 = 0;
     nUlt4 = 0;
     aUlt11 = "";

     nPcm = 0;
     nXPcm = 0;
     nHayEntra = "";
     nNume = 0;
     nStock = 0;
     nStock2 = 0;
     nPrecio = 0;
     nMargen = 0;
     nError = 0;
     nCanti = 0;
     nNeto = 0;
     nPm = 0;
     nUni0 = 0;
     nSwPcm = 0;
     nNume1 = 0;
     nImpo = 0;
     nSto = 0;
     nPrime = 0;
     nImpre = -1;
     nInfor = -1;
|FIN;

|PROCESO ImpInciden;
     |SI #0#9 = "N"; |ACABA; |FINSI;

     |SI nPrime = 0;
          nPrime = 1;
          |IMPRE 0;
          nImpre = FSalida;
          |SI FSalida = 0;
               |INFOR "dsz00062";
               nInfor = FSalida;
               |SI FSalida ! 0;
                    |MENAV "0000Error en informe 'dsz00062'...";
               |FINSI;
          |FINSI;
     |FINSI;

     enPcm = nPcm;
     |SI enInci = 1; eaDes = "Stock negativo"; efFecha = #99#1; |FINSI;
     |SI enInci = 2; eaDes = "Desviación precio"; efFecha = #77#1; |FINSI;
     |SI enInci = 3; eaDes = "Desviación precio"; efFecha = #56#1; |FINSI;
     |SI enInci = 4; eaDes = "Desviación precio"; efFecha = #65#1; |FINSI;
     |SI nInfor = 0; |PRINT; |FINSI;
|FPRC;

|PROCESO ValoraParti;
     |HAZ ValoraStock;
     |GRABA 020#24a;
|FPRC;

|DEFBUCLE ValoraParti;
     #24#1;
     ;
     #19#0, 01, "                              ";
     #19#0, 99, "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, ValoraParti;
|FIN;

|PROCESO ValoraAlma;
     |HAZ ValoraStock;
     |GRABA 020#17a;
|FPRC;

|DEFBUCLE ValoraAlma;
     #17#1;
     ;
     #19#0, 01;
     #19#0, 99;
     ;
     NULL, ValoraAlma;
|FIN;
ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
|PROCESO AlbLin;
     |SI #71#9 ] 0;
          nError = 1;
     |SINO;
          nError = -1;
          #71#9 = -#71#9;
     |FINSI;

     nCanti = #71#9 < #10#5;
     nCanti = nCanti < #10#32;
     |SI #142#115 = "S";
          nCanti = nCanti < #10#7;
     |FINSI;
     nCanti = nCanti * nError;
     nMargen = nCanti - #71#14;
     #10#37 = #10#37 + nMargen;

     #71#9  = #71#9  * nError;
|FPRC;

|PROCESO AlbCab;
     #10#37 = 0;
|FPRC;

|PROCESO AlbFin;
     |GRABA 020#10a;
     |LIBERA #10;
|FPRC;

|PROCESO FacDirLin
     |SI #69#9 ] 0;
          nError = 1;
     |SINO;
          nError = -1;
          #69#9   = -#69#9;
     |FINSI;

     nCanti = #69#9 < #84#5;
     nCanti = nCanti < #84#32;
     |SI #142#115 = "S";
          nCanti = nCanti < #84#7;
     |FINSI;
     nCanti = nCanti * nError;
     nMargen = nCanti - #69#14;
     #84#37 = #84#37 + nMargen;
     #69#9 = #69#9  * nError;
|FPRC;

|PROCESO FacDirCab;
     #84#37 = 0;
|FPRC;

|PROCESO FacDirFin;
     |GRABA 020#84a;
     |LIBERA #84;;
|FPRC;

|PROCESO FacLin;
     #10#52 = #59#15;
     #10#0 = #59#2;
     |LEE 000#10=;
     |SI FSalida = 0;
          #134#46 = #134#46 + #10#37;
     |FINSI;
|FPRC;

|PROCESO FacCab;
     #134#46 = 0;
|FPRC;

|PROCESO FacFin;
     |GRABA 020#134a;
     |LIBERA #134;
|FPRC;

|DEFBUCLE MargenAlbaran;
     #10#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL, AlbCab, AlbLin, AlbFin;
|FIN;

|DEFBUCLE MargenFacDir;
     #84#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL, FacDirCab, FacDirLin, FacDirFin;
|FIN;

|DEFBUCLE MargenFactura;
     #134#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL, FacCab, FacLin, FacFin;
|FIN;
ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
|PROCESO CosteAlbVta;
     #71#29 = #65#11;
     #71#0 = #65#3;
     #71#1 = #65#4;
     |LEE 101#71=;
     |SI FSalida = 0;
          |SI #19#194 = 1;
               #71#14 = #71#5 * nPcm;
          |SINO;
               #71#14 = #71#48 * nPcm;
          |FINSI;
          |GRABA 020#71a;
          |LIBERA #71;
     |FINSI;
|FPRC;

|PROCESO CosteFacDir;
     #69#20 = #65#11;
     #69#0 = #65#3;
     #69#1 = #65#4;
     |LEE 101#69=;
     |SI FSalida = 0;
          |SI #19#194 = 1;
               #69#14 = #69#5 * nPcm;
          |SINO;
               #69#14 = #69#30 * nPcm;
          |FINSI;
          |GRABA 020#69a;
          |LIBERA #69;
     |FINSI;
|FPRC;

|PROCESO ActPCM;
     |SI #65#2 = "SA"; nPcm = #65#9; |FINSI;
     #65#7 = nStock;
     #65#8 = nPcm;
     ||GRABA 020#65a;    modificado para inforpor
     |BORRA 020#65a;
     |GRABA 020#65n;

     |SI #65#2 = "AV"; |O #65#2 = "BV"; |HAZ CosteAlbVta; |FINSI;
     |SI #65#2 = "FC"; |HAZ CosteFacDir; |FINSI;
|FPRC;

|PROCESO Salidas;
     #65#0 = aUlt0;
     #65#1 = fUlt1;
     #65#2 = aUlt2;
     #65#3 = nUlt3;
     #65#4 = nUlt4;
     #65#11 = aUlt11;
     |LEE 101#65>;
     |PARA; |SI FSalida = 0; |Y #65#0 = aUlt0; |Y #65#1 [ fMas; |HACIENDO;
          |SI #65#6 < 0;
               nNume = nStock + #65#6;
               |SI nNume ] 0; |O nEsUltimo = 1;
                    nStock = nStock + #65#6;
                    nStock2 = nStock2 + #65#6;
                    aUlt0 = #65#0;
                    fUlt1 = #65#1;
                    aUlt2 = #65#2;
                    nUlt3 = #65#3;
                    nUlt4 = #65#4;
                    aUlt11 = #65#11;
                    |SI nPcm = 0;
                         nPcm = #19#9;
                         |HAZ ActPCM;
                         nPcm = 0;
                    |SINO;
                         |HAZ ActPCM;
                    |FINSI;
               |SINO;
                    |SAL;
               |FINSI;
          |FINSI;
          |LIBERA #65;
          |LEE 101#65s;
     |SIGUE;
     |LIBERA #65;
|FPRC;

|PROCESO VigorPcm;
     |ABRE #7;
     #7#26 = #65#11;
     |LEE 000#7=;
     |CIERRA #7;
     |SI #7#37 = "S";  ||Serie interna
          FSalida = 1;
     |SINO;
          FSalida = 0;
     |FINSI;
|FPRC;

|PROCESO PCM_AC;
     |SI nSwVigor = 0;
          |HAZ VigorPcm;
          |SI FSalida ! 0; |ACABA; |FINSI;
     |FINSI;

     |DDEFECTO #77;
     #77#50 = #65#11;
     #77#0 = #65#3;
     |LEE 000#77=;
     |DDEFECTO #80;
     #80#27 = #65#11;
     #80#0 = #65#3;
     #80#1 = #65#4;
     |LEE 000#80=;

     |SI nPrimeEntra = 1;
          |SI #80#6 ] nPorceMin; |Y #80#6 [ nPorceMax; |Y #80#6 ! 0;
               enInci = 2; |HAZ ImpInciden;
          |FINSI;
     |FINSI;

     nNeto = (#80#9 < #77#5) < #77#32;
     |SI #142#114 = "S";
          nNeto = nNeto < #77#7;
     |FINSI;

     nPm = 0;
     |SI #142#104 = "S"; nPm = (#77#11 + #77#13) / #77#15; |FINSI;
     |SI nPm ! 0; nPm = (nNeto * nPm) + nNeto; |SINO; nPm = nNeto; |FINSI;

     || nPm = Coste de la linea
     nXPcm = nPcm;
     |SI #19#196 = 1;
          nUni0 = #80#5;
     |SINO;
          nUni0 = #80#36;
     |FINSI;

     nSwPcm = 0;
     |SI nUni0 > 0; |Y nPm > 0;
          nSwPcm = 1;
     |SINO;
          |SI #142#148 = "S"; nSwPcm = 1;|FINSI;
     |FINSI;

     |SI #77#42 = "S"; nPm = -nPm; nUni0 = -nUni0; |FINSI;

     |SI nSwPcm = 1;
          nPm = nPm / nUni0;
          |SI #19#196 = 1;
               nNume = nStock;
          |SINO;
               nNume = nStock2;
          |FINSI;
          |SI nNume > 0;
               nPm  = nPm * nUni0;
               nNume1  = nValor + nPm;
               nXPcm = nNume1 / (nNume + nUni0);
          |SINO;
               nXPcm = nPm;
          |FINSI;
     |FINSI;
     |SI #77#42 = "S"; nPm = -nPm; nUni0 = -nUni0; |FINSI;

     |SI nXPcm ! 0;  |Y #142#30 ! "X";
          nPcm = nXPcm;
     |FINSI;
|FPRC;

|PROCESO PCM_EV;
     |DDEFECTO #56;
     #56#2 = #65#11;
     #56#0 = #65#3;
     |LEE 000#56=;
     #57#28 = #65#11;
     #57#27 = #65#3;
     #57#14 = #65#4;
     |LEE 000#57=;

     |SI nPrimeEntra = 1;
          |SI #57#19 ] nPorceMin; |Y #57#19 [ nPorceMax; |Y #57#19 ! 0;
               enInci = 3; |HAZ ImpInciden;
          |FINSI;
     |FINSI;

     |SI #57#0 ! #65#0; |O #56#1 ! #65#1;
          #57#4 = #65#6;
          #57#11 = #65#14;
          #57#21 = #65#17;
     |FINSI;

     |SI #57#4 = 0;
          nImpo = #57#11;
     |SINO;
          |SI #19#196 = 1;
               nImpo = #57#4 * #57#11;
          |SINO;
               nImpo = #57#21 * #57#11;
          |FINSI;
     |FINSI;

     |SI #19#196 = 1;
          nUni0 = #57#4;
          nSto = nStock;
     |SINO;
           nSto = nStock2;
           nUni0 = #57#21;
     |FINSI;

     nSwPcm = 0;
     |SI nSto > 0; |Y nImpo > 0;
          nSwPcm = 1;
     |SINO;
          |SI #142#148 = "S"; nSwPcm = 1;|FINSI;
     |FINSI;

     |SI nSwPcm = 1;
          |SI nUni0 = 0;
               nXPcm = #57#11;
          |SINO;
               nXPcm = nUni0 * #57#11;
          |FINSI;
          nXPcm = nXPcm + nValor;
          nXPcm = nXPcm / (nSto + nUni0);
     |SINO;
         nXPcm = #57#11;
     |FINSI;

     |SI nXPcm ! 0;  |Y #142#30 ! "X";
          nPcm = nXPcm;
     |FINSI;
|FPRC;

|PROCESO PCM_FB;
     |SI nPrimeEntra = 1;
          |SI #65#9 ] nPorceMin; |Y #65#9 [ nPorceMax; |Y #65#9 ! 0;
               enInci = 4; |HAZ ImpInciden;
          |FINSI;
     |FINSI;

     nImpo = #65#6 * #65#9;
     nUni0 = #65#6;
     nSto = nStock;
     nSwPcm = 0;
     |SI nSto > 0; |Y nImpo > 0;
          nSwPcm = 1;
     |SINO;
          |SI #142#148 = "S"; nSwPcm = 1;|FINSI;
     |FINSI;

     |SI nSwPcm = 1;
          |SI nUni0 = 0;
               nXPcm = #65#9;
          |SINO;
               nXPcm = nUni0 * #65#9;
          |FINSI;
          nXPcm = nXPcm + nValor;
          nXPcm = nXPcm / (nSto + nUni0);
     |SINO;
         nXPcm = #65#9;
     |FINSI;

     |SI nXPcm ! 0;  |Y #142#30 ! "X";
          nPcm = nXPcm;
     |FINSI;
|FPRC;

|PROCESO Entradas;
     |DDEFECTO #65;
     #65#0 = #19#0;
     #65#1 = fMas;
     |LEE 101#65];
     |PARA; |SI FSalida = 0; |Y #65#0 = #19#0; |Y #65#1 = fMas; |HACIENDO;
          |SI #65#6 ] 0;
               |SI #19#196 = 1
                    nValor = nStock * nPcm;
               |SINO;
                    nValor = nStock2 * nPcm;
               |FINSI;
               |SI nValor < 0; nValor = 0; |FINSI;
               |SI #65#2 = "AC"; |O #65#2 = "BC";
                    nHayEntra = "S";
                    |HAZ PCM_AC;
                    nPrimeEntra = 1;
               |FINSI;
               |SI #65#2 = "EV";
                    nHayEntra = "S";
                    |HAZ PCM_EV;
                    nPrimeEntra = 1;
               |FINSI;
               |SI #65#2 = "FB";
                    nHayEntra = "S";
                    |HAZ PCM_FB;
                    nPrimeEntra = 1;
               |FINSI;
               nStock = nStock + #65#6;
               nStock2 = nStock2 + #65#17;
               |HAZ ActPCM;

               nPorceMin = (nPcm + (nPcm * #0#10 / 100)) ! 5;
               nPorceMax = (nPcm - (nPcm * #0#10 / 100)) ! 5;
          |FINSI;
          |LIBERA #65;
          |LEE 101#65s;
     |SIGUE;
     |LIBERA #65;
|FPRC;

|PROCESO IniArti;
     |DDEFECTO #19;
     #19#0 = #99#0;
     |LEE 101#19=
     |SI FSalida ! 0; |GRABA 020#19b; |FINSI;

     |ATRI I; |PINTA 2302, "Arti:" |ATRI 0;
     |PINTA 2306, #19#0;
     nEsUltimo = 0;

     aUlt0 = #19#0;
     fUlt1 = "01.01.0000";
     aUlt2 = "";
     nUlt3 = 0;
     nUlt4 = 0;
     aUlt11 = "";

     nHayEntra = "N";

     nStock = 0;
     nStock2 = 0;
     nValor = 0;
     nPcm = 0;
|FPRC;

|PROCESO CadaDia;
     fMas = #99#1;
     nPrimeEntra = 0;
     |HAZ Entradas;
     |SI nStock > 0;
          |HAZ Salidas;
     |SINO;
          eaArticulo = #99#0;
          enInci = 1; |HAZ ImpInciden;
     |FINSI;
|FPRC;

|PROCESO FinArti;
     |SI aArtAnt = ""; |ACABA; |FINSI;

     nEsUltimo = 1;
     fMas = "31.12.9999";
     |HAZ Salidas;

     |SI nHayEntra = "S";
          #19#9 = nPcm;
     |FINSI;
     |BUCLE ValoraAlma;
     |BUCLE ValoraParti;
     |GRABA 020#19a;
     |LIBERA #19;
|FPRC;

|PROCESO Historico;
     |SI fFecAnt ! #99#1; |O aArtAnt ! #99#0;
          |SI aArtAnt ! #99#0;
               |HAZ FinArti;
               |HAZ IniArti;
               aArtAnt = #99#0;
          |FINSI;
          |HAZ CadaDia;
          fFecAnt = #99#1;
     |FINSI;
|FPRC;

|DEFBUCLE Historico;
     #99#1006;
     ;
     #0#6, "01.01.0000", "  ", 000000, 00000, "     ";
     #0#7, "31.12.9999", "zz", 999999, 99999, "zzzzz";
     ;
     NULL, Historico;
|FIN;

|PROCESO inicio; |TIPO 3;
     |SI #0#5 = "SI";
          |DDEF aDef;
          aDef = aDef + "agifa065";
          |CARGA_DEF aDef, agifa065@;
          |SI FSalida ! 0;
               |MENAV "0000ERROR. Carga def";
               |ACABA;
          |FINSI;

          |ABRE #142; |LEE 000#142p; |CIERRA #142;

          |ABRE #71;
          |ABRE #69;
          |ABRE #65;
          |ABRE #56;
          |ABRE #57;
          |ABRE #77;
          |ABRE #80;
          |ABRE #19;
          |ATRI I; |PINTA 2302, "Calculando PCM"; |ATRI 0;
          |BUCLE Historico;
          |HAZ FinArti;
          |CIERRA #19;
          |CIERRA #80;
          |CIERRA #77;
          |CIERRA #57;
          |CIERRA #56;
          |CIERRA #65;
          |CIERRA #69;
          |CIERRA #71;

          |SI #0#8 = "S";
               |ATRI I; |PINTA 2302, "Calculando margen albaran"; |ATRI 0;
               |BUCLE MargenAlbaran;

               |ATRI I; |PINTA 2302, "Calculando margen factura directa"; |ATRI 0;
               |BUCLE MargenFacDir;

               |ATRI I; |PINTA 2302, "Calculando margen facturas       "; |ATRI 0;
               |ABRE #10;
               |BUCLE MargenFactura;
               |CIERRA #10;
          |FINSI;

          |DESCARGA_DEF #agifa065@;

          |SI nInfor ! -1;
               |PIEINF;
               |FININF;
          |FINSI;
          |SI nImpre ! -1; |FINIMP; |FINSI;
     |FINSI;
|FPRC;

|PROCESO Incidencias; |TIPO 0;
     aAlfa = #0#9;
     |C_M #0#10, 1, aAlfa;
|FPRC;

|PROGRAMA;

     |HAZ EsVigor;
     nSwVigor = FSalida;

     |HAZ DecimalesBase;

     |CLS;
     |ABRE #100; |LEE 000#100p; |CIERRA #100;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
|FPRO;
