          || Copia Ficheros a otra Empresa

|FICHEROS;
     conz0018 #0;
     agifa041 #41;
|FIN;

|VARIABLES;
     &eaErrorEmp = "";
     aFicOrg = "";
     aFicDes = "";
     aAlfa = "";
     aCar = "";
     nPos = 0;
     i = 0;
     aNomEmp = "";
     aEmpOrg = "";
     aEmpDes = "";
     aBase = "";
     aDirOrg = "";
     aDirDes = "";
|FIN;

|PROCESO CopiaSN; |TIPO 0;
     aAlfa = #0#22;
     |C_M #0#23, 1, aAlfa;
     |C_M #0#24, 1, aAlfa;
|FPRC;

|PROCESO NumeEmp; |TIPO 0;
     |SI #0#22 = "N"; |ACABA; |FINSI;
     |SI FSalida = 13;   ||F5
          |DBASE aBase;
          aAlfa = aBase + "fich/";
          |PATH_DAT #41 aAlfa;
          |ABRE #41;
          #41#0 = #0#23;
          |LEE 000#41];
          |CONSULTA_CLAVE #1#41;
          |SI FSalida = 0;
               #0#23 = #41#0; |PINTA #0#23;
          |SINO;
               |ERROR; |ACABA;
          |FINSI;
          |CIERRA #41;
     |FINSI;

     |HAZ CheqEmp;
     |SI eaErrorEmp = "S"; |ERROR; |ACABA; |FINSI;
     #0#24 = "Copia de datos " + aNomEmp;
     |PINTA #0#24;
|FPRC;

|PROCESO CheqEmp;
     eaErrorEmp = "N";
     |SI #0#22 = "S";
          |SI #0#23 = 0;
               |MENAV "0000La empresa de copia no puede ser '00000' ";
               eaErrorEmp = "S";
               |ACABA;
          |FINSI;
          |DBASE aBase;
          aAlfa = aBase + "fich/";
          |PATH_DAT #41 aAlfa;
          |ABRE #41;
          #41#0 = #0#23;
          |LEE 000#41=;
          |SI FSalida = 0;
               |MENAV "0000La empresa de copia EXISTE...";
               eaErrorEmp = "S";
          |SINO;
               |DFICE aAlfa;
               aEmpOrg = aAlfa % -205;
               #41#0 = aEmpOrg;
               |LEE 020#41=;
               aNomEmp = #41#1;
          |FINSI;
          |CIERRA #41;
     |FINSI;
|FPRC;

|PROCESO CopiaEmp;
     |SI #0#22 = "N"; |ACABA; |FINSI;

     |DFICE aAlfa;
     aEmpOrg = aAlfa % -205;
     aEmpDes = ("00000" + #0#23) % -105;

     |DBASE aBase;
     aAlfa = aBase + "fich/";
     |PATH_DAT #41 aAlfa;
     |ABRE #41;
     #41#0 = aEmpOrg;
     |LEE 000#41=;
     |SI FSalida = 0;
          #41#0 = aEmpDes;
          #41#1 = #0#24;
          |GRABA 020#41n;
          |SI FSalida = 0;
               |HAZ CopiaFicheros;
          |SINO;
               |MENAV "0000ERROR AL CREAR LA EMPRESA COPIA";
          |FINSI;
     |SINO;
          |MENAV "0000NO EXISTE LA EMPRESA ORIGEN!!!";
     |FINSI;
     |CIERRA #41;
|FPRC;

|PROCESO CopiaFicheros;
     aDirOrg = aBase + "fich/" + aEmpOrg + "/";
     aDirDes = aBase + "fich/" + aEmpDes + "/";
     |MKDIR aDirDes;

     aAlfa = aDirOrg + "*";
     |_PDIR aAlfa;
     |PARA; |SI FSalida = 0; |HACIENDO;
          aFicOrg = aAlfa;
          aFicDes = "";
          |PARA i = 1; |SI; |HACIENDO i = i + 1;
               nPos = -1 * (i * 100 + 1)
               aCar = aFicOrg % nPos;
               |SI aCar = "/"; |O aCar = "\"; |SAL; |FINSI;
               aFicDes = aCar + aFicDes;
          |SIGUE;
          |PINTA 2205, aFicDes;
          aFicDes = aDirDes + aFicDes;
          |COPIA_FICHERO aFicOrg, aFicDes;
          |_SDIR aAlfa;
     |SIGUE;
|FPRC;
