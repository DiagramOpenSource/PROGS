|FICHEROS;
     inmoz014 #0;
     inmom001 #1;
     inmom002 #2;
     inmow007 #7,MANTE;
     inmow008 #8,MANTE; || = inmom002  (son el mismo fichero)
|FIN;

|VARIABLES;
     &Tempo = "";
     aTmp7 = "";
     aTmp8 = "";
     aDSegui = "";
     aHSegui = "";
     nTecla = 0;
|FIN;

|PROCESO Desasigna;
     #1#10 = "";
     |GRABA 020#1a;
     |LIBERA #1;
|FPRC;

|DEFBUCLE Desasigna;
     #1#4;
     11, 10;
     #8#0, "                    ", #7#2, #7#1; || obra, serie , seguidor
     #8#0, "zzzzzzzzzzzzzzzzzzzz", #7#2, #7#1;
     be;
     NULL, Desasigna;
|FIN;

|PROCESO Asigna;
     #1#10 = #8#1;
     |GRABA 020#1a;
     |LIBERA #1;
|FPRC;

|DEFBUCLE Asigna;
     #1#4;
     11, 10;
     #8#0, "                    ", #7#2, "      "; || obra, serie , seguidor
     #8#0, "zzzzzzzzzzzzzzzzzzzz", #7#2, "      ";
     be;
     NULL, Asigna;
|FIN;

|PROCESO Tipo7_11; |TIPO 11;
     nTecla = FSalida;
     |LEE 121#8=;

     |SI nTecla = 11;   ||F3
          |SI #8#4 < #8#3;
               |SI #7#1 ! "      ";
                    |MENAV "0000Serie ya asignada...";
                    |ERROR;
               |SINO;
                    |BUCLE Asigna;

                    |BORRA 020#7c;
                    #7#1 = #8#1; ; |PINTA #7#1;
                    |GRABA 020#7n;

                    #8#4 = #8#4 + 1;
                    |GRABA 020#8a;

                    |PONCONTROL 23, "SI";
               |FINSI;
          |SINO;
               |MENAV "0000No quedan pendientes de asignar";
               |ERROR;
          |FINSI;
     |FINSI;
     |SI nTecla = 12;   ||F4
          |SI #8#4 > 0;
               |SI #7#1 = "      ";
                    |MENAV "0000Serie no asignada...";
                    |ERROR; |ACABA;
               |FINSI;
               |SI #7#1 ! #8#1;
                    |MENAV "0000Seguidores distintos...";
                    |ERROR;
               |SINO;
                    |BUCLE Desasigna;

                    |BORRA 020#7c;
                    #7#1 = ""; |PINTA #7#1;
                    |GRABA 020#7n;

                    #8#4 = #8#4 - 1;
                    |GRABA 020#8a;

                    |PONCONTROL 23, "SI";
               |FINSI;
          |SINO;
               |MENAV "0000No hay ninguna asignada";
               |ERROR;
          |FINSI;
     |FINSI;

     |LIBERA #8;
|FPRC;

|PROCESO Tipo8_11; |TIPO 11;
     |SI FSalida = 10; || F2;
          |ENTLINEAL #2#7, 4, 4, 22, 0, Min7, Max7; || es #2#7, 4
     |FINSI;
|FPRC;

|PROCESO Min7;
     #7#0 = #8#0;
     #7#3 = #8#2;
     #7#1 = "      ";
     #7#2 = "    ";
|FPRC;

|PROCESO Max7;
     #7#0 = #8#0;
     #7#3 = #8#2;
     #7#1 = "zzzzzz"
     #7#2 = "zzzz";
|FPRC;

|PROCESO Min8;
     #8#0 = #0#0;
     #8#1 = "      ";
     #8#2 = 0;
     #8#3 = 0;
|FPRC;

|PROCESO Max8;
     #8#0 = #0#0;
     #8#1 = "zzzzzz";
     #8#2 = 99;
     #8#3 = 99;
|FPRC;

|PROCESO Tipo8_7; |TIPO 7;
     |ENTLINEAL #2#7, 4, 7, 22, 0, Min7, Max7;
|FPRC;

|PROCESO inmom001;
     |DDEFECTO #7;
     #7#0 = #1#9;
     #7#1 = #1#10;
     #7#2 = #1#11;
     |LEE 101#7=
     |SI FSalida ! 0; |GRABA 020#7b; |FINSI;
     #7#3 = #7#3 + 1;
     |GRABA 020#7a;
     |LIBERA #7;
|FPRC;

|DEFBUCLE inmom001;
     #1#4;
     ;
     #0#0, "                    ";
     #0#0, "zzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, inmom001;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |DDEFECTO #2;
     |ABRE #2;
     #2#0 = #0#0;
     |LEE 000#2];
     |SI FSalida ! 0; |O #2#0 ! #0#0;
          |MENAV "0000La obra no tiene asignado seguidores...";
     |SINO;
          |HAZ CreaTempo; |NOME_DAT #7 Tempo; |DELINDEX #7; aTmp7 = Tempo;

          |ABRE #7;
          |BUCLE inmom001;
          |CIERRA #7;
          |ENTLINEAL #1#8, 1, 4, 22, 1, Min8, Max8;

          Tempo = aTmp7; |HAZ BoraTempo; |DELINDEX #7;
     |FINSI;
     |CIERRA #2;
|FPRC;
