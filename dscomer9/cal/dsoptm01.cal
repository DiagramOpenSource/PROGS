|FICHEROS;
     dsoptm01 #0;
|FIN;

|VARIABLES;
     aEsc1  = "";
     aEsc2  = "";
     aRetu  = "";
     aTecla = "";

     nExiste = 0;
     aMensaje = "";
     nPanta = 0;
     aNombreLogo = "";
     nBotonLogo = 0;
     aBase = "";
     aLogoOrigen = "";
     aLogoDestino = "";
     aExt = "";
     nHandle = 0;
     aDirLocal = "";
     {1}aContiene     = "";
     aDocRemoto = "";
     aDocServidor = "";
     aIPRemoto = "";
     aLogo = "";
     aAlfa = "";
     aDirec = "";
|FIN;

|PROCESO Tipo12; |TIPO 12;
|FPRC;

|PROCESO Tipo80; |TIPO 80;
     FSalida = 2999;
|FPRC;

|PROCESO PonImagen;
     aNombreLogo = "logooptica";
     |SI nBotonLogo ! -1;
          |FIN_CONTROL_WINDOWS nBotonLogo;
     |FINSI;

     |DBASE aBase;  |QBF aBase;

     aLogoOrigen = aBase + "logos/" + aNombreLogo + ".jpg";
     aExt        = ".jpg";
     |XABRE "E", aLogoOrigen, nHandle;
     |SI FSalida < 0;
         aLogoOrigen = aBase + "logos/" + aNombreLogo + ".bmp";
         aExt        = ".bmp";
         |XABRE "E", aLogoOrigen, nHandle;
         |SI FSalida < 0;
             aLogoOrigen = aBase + "logos/insertarlogo.jpg";
             aExt        = ".jpg";
             |XABRE "E", aLogoOrigen, nHandle;
             |SI FSalida < 0;
                 |MENAV "0000Hay un problema en su aplicaci¢n, no est n instalado los logos";
                 |ACABA;
             |FINSI;
         |FINSI;
     |FINSI;

     aDirLocal    = "C:\DOCUMENTOS";                     |RMKDIR aDirLocal;
     aDirLocal    = "C:\DOCUMENTOS\DSCOMER9";            |RMKDIR aDirLocal;
     aDirLocal    = "C:\DOCUMENTOS\DSCOMER9\LOGOS";      |RMKDIR aDirLocal;
     aLogoDestino = aDirLocal + "/" + aNombreLogo + aExt;

     aContiene1 = aLogoDestino;
     |ESPECIFICA "REMOTOMD5", aContiene;
     aDocRemoto = FSalida;  |QBF aDocRemoto;

     aContiene1 = aLogoOrigen;
     |ESPECIFICA "MD5", aContiene;
     aDocServidor = FSalida;  |QBF aDocServidor;

     |SI aDocRemoto ! aDocServidor;  |O aDocRemoto = "";
         aIPRemoto = "";
         |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aLogoOrigen, aLogoDestino;
         |SINO;
             |COPIA_FICHERO aLogoOrigen, aLogoDestino;
         |FINSI;
     |FINSI;

     aLogo = "IMAGEN,"  + aLogoDestino;
     |CONTROL_SIMPLE nPanta, aLogo, 0770, 1182, NULL;
     nBotonLogo = FSalida;
|FPRC;

|PROCESO CambiaLogo;
     aNombreLogo = "logooptica";

     aAlfa = aDirLocal + "/" + aNombreLogo + ".jpg";   |RFBORRA aAlfa;
     aAlfa = aDirLocal + "/" + aNombreLogo + ".bmp";   |RFBORRA aAlfa;

     aDirec = "F";
     |BROWSE aDirec;
     aDirec = aDirec % 290;  |QBF aDirec;
     |SI aDirec = "";  |ACABA;  |FINSI;

     aExt = (aDirec % -103) < "";
     |SI aExt ! "bmp";  |Y aExt ! "jpg";
         |MENAV "0000Por favor seleccione una imagen JPG o BMP";
         |ACABA;
     |FINSI;

     aAlfa = aBase + "logos/" + aNombreLogo + ".jpg";  |FBORRA aAlfa;
     aAlfa = aBase + "logos/" + aNombreLogo + ".bmp";  |FBORRA aAlfa;

     aLogoOrigen  = aDirec;
     aLogoDestino = aBase + "logos/" + aNombreLogo + "." + aExt;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |SI aIPRemoto ! "";
         |RECIBE_FICHERO aLogoOrigen, aLogoDestino;
     |SINO;
         |COPIA_FICHERO aLogoOrigen, aLogoDestino;
     |FINSI;

     |HAZ PonImagen;
|FPRC;

|PROGRAMA;
     |CLS;
     |ABRE #0;
     |DDEFECTO #0;
     |LEE 101#0p;
     nExiste = FSalida;
     |PINPA #0#0; nPanta = FSalida;
     |PINDA #0#0;

     |CONTROL_SIMPLE nPanta, "BOTON,Pulse aqu¡ para cambiar el logo", 1370, 1497, CambiaLogo;
     |CONTROL_SIMPLE nPanta, "LABEL,{{16}}Imagen JPG o BMP de 90 X 90", 1570, 1597, NULL;

     nBotonLogo = -1;
     |HAZ PonImagen;

     |PTEC 802;
     |PAUSA;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}Salir";
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |SI nExiste = 0;
          |GRABA 020#0a;
     |SINO;
          |GRABA 020#0n;
     |FINSI;
     |LIBERA #0;

     |SI nBotonLogo ! -1;
          |FIN_CONTROL_WINDOWS nBotonLogo;
     |FINSI;
     |CIERRA #0;
|FPRO;
