|FICHEROS;
     fergaz09 #0;

     agifa010 #10;
     agifa071 #71;

     fergam09 #9;        ||Mant. Impresora p IP

     fergam07 #7;        ||Orden Componentes
     fergam11 #11;       ||Orden Partidas

     fergaw01 #101;      ||Tmp Arti/Alma/Parti

     otroref@;
|FIN;

|VARIABLES;
     &eaNombreBinario = "";

     aAuxImp = "";
     aTempo101 = "";
     &Tempo = "";

     aNomFicheroDestino = "";
     aNomFicheroPDF = "";
     aDestinoPDF = "";
     aFicheroTxt = "";
     nHandleTxt = 0;
     aFicheroLCK = "";

     aMensaje = "";
     nAlmacen = 0;
     aVacio30 = "";
     aZeta30 = "";
     nOrden = 0;
     aArticulo = "";
     aArticuloPadre = "";
     nSwHayPartidas = 0;

     &eaUnidad = "";

     {1}aContiene     = "";
     aBase = "";
     nError = 0;
     aOrigen = "";
     nHandle = 0;
     aDestino = "";
     aDocDestino = "";
     aDocOrigen = "";
     aAlfa = "";
     aEjecuta = "";
     aLinea = "";

     nSwEstaDocErp = 0;
     aBass = "";
     aErp = "";
     aPathErp = "";
     aCien = " ";
     nEmpErp = 0;
     aAplicaErp = "";
     aDefErp = "";
     aClaveErp = "";
     aDef = "";
     nSwHayDoc = 0;
     aImpresora = "";
     aIPRemoto = "";
     nSwHayDsPrintIni = 0;
     aNomFichero = "";
     aExtension = "";
     aAlfa1 = "";
     aFicFin = "";
     aAux = "";
     aAbre = "";
|FIN;

|PROCESO fergam11;
     |DDEFECTO #fergam07;
     #fergam07#0 = #fergam11#0;
     #fergam07#1 = #fergam11#1;
     |LEE 000#fergam07.=;
     aArticulo = #fergam07#2;

     |DDEFECTO #fergaw01;
     #fergaw01#0 = aArticulo;
     #fergaw01#1 = nAlmacen;
     #fergaw01#2 = #fergam11#2;
     |LEE 101#fergaw01.=;
     |SI FSalida ! 0;
          |GRABA 020#fergaw01.n;
     |FINSI;
     |LIBERA #fergaw01;

     nSwHayPartidas = 1;
|FPRC;

|DEFBUCLE fergam11;
 #fergam11#1;
 ;
 nOrden, 000, aVacio30;
 nOrden, 999, aZeta30;
 ;
 NULL, fergam11;
|FIN;

|PROCESO agifa071;
     |SI #agifa071#12 ! 0;
          ||CCC. El numero de Pedido (#agifa071#2), correspondera SIEMPRE
          ||     con el numero de orden (no tiene serie)

          nOrden = #agifa071#2;
          aArticuloPadre = #agifa071#2; |QBF aArticuloPadre;
          nAlmacen = #agifa071#3;
          |BUCLE fergam11;
     |FINSI;
|FPRC;

|DEFBUCLE agifa071;
 #agifa071#1;
 ;
 #agifa010#52, #agifa010#0,   1;
 #agifa010#52, #agifa010#0, 999;
 ;
 NULL, agifa071;
|FIN;

|PROCESO agifa010;
     |SI #agifa010#1 < #fergaz09#0; |O #agifa010#1 > #fergaz09#1;
          |ACABA;
     |FINSI;

     |SI #agifa010#3 < #fergaz09#2; |O #agifa010#3 > #fergaz09#3;
          |ACABA;
     |FINSI;

     |SI #agifa010#84 < #fergaz09#4; |O #agifa010#84 > #fergaz09#5;
          |ACABA;
     |FINSI;

     |BUCLE agifa071;
|FPRC;

|DEFBUCLE agifa010;
 #agifa010#1;
 ;
 #fergaz09#6, #fergaz09#8;
 #fergaz09#7, #fergaz09#9;
 ;
 NULL, agifa010;
|FIN;

|PROCESO fergaw01;
     aMensaje = "0000Partida " + #fergaw01#2;
     |MENAV aMensaje;
|FPRC;

|DEFBUCLE fergaw01;
 #fergaw01#1;
 ;
 INICIO;
 FINAL;
 ;
 NULL, fergaw01;
|FIN;

|PROCESO TraeteBinario;
     |DBASE aBase;
     |QBF aBase;

     nError = 0;

     aOrigen = aBase + "plugins/" + eaNombreBinario;
     |XABRE "E", aOrigen, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     aDestino = eaUnidad + ":\DOCUMENTOS\";
     |RMKDIR aDestino;

     aDestino = eaUnidad + ":\DOCUMENTOS\DSCOMER9\";
     |RMKDIR aDestino;

     aDestino = eaUnidad + ":\DOCUMENTOS\DSCOMER9\" + eaNombreBinario;

     aContiene1 = aDestino;
     |ESPECIFICA "REMOTOMD5", aContiene;
     aDocDestino = FSalida;  |QBF aDocDestino;

     aContiene1 = aOrigen;
     |ESPECIFICA "MD5", aContiene;
     aDocOrigen = FSalida;  |QBF aDocOrigen;

     |SI aDocDestino ! aDocOrigen;  |O aDocDestino = "";
         aIPRemoto = "";
         |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aOrigen, aDestino;
         |SINO;
             |COPIA_FICHERO aOrigen, aDestino;
         |FINSI;
     |FINSI;

     aAlfa = eaUnidad + ":\DOCUMENTOS\DSCOMER9\" + eaNombreBinario;
     |XABRE "EC", aAlfa, nHandle;
     |SI FSalida < 0;
         aMensaje = "0000 No puedo instalar el binario: " + eaNombreBinario;
         |MENAV aMensaje;
         nError = 1;
     |FINSI;
|FPRC;


|PROCESO PideImpresora;
     eaNombreBinario = "dsbrowserprint.exe";
     |HAZ TraeteBinario;

     aDestino = eaUnidad + ":\DOCUMENTOS\DSCOMER9\impresora.sel";

     aEjecuta = eaUnidad + ":\DOCUMENTOS\DSCOMER9\dsbrowserprint.exe";
     aEjecuta = aEjecuta + " " + eaUnidad + ":\DOCUMENTOS\DSCOMER9\impresora.sel";
     |RSYSTEM aEjecuta;

     aImpresora = "";
     aAlfa = eaUnidad + ":\DOCUMENTOS\DSCOMER9\impresora.sel";
     |XABRE "CU", aAlfa, nHandle;
     |SI FSalida < 0;
          aImpresora = "";
     |SINO;
          |XLEE nHandle, 250, aLinea;
          |SI FSalida ] 0;
               aImpresora = aLinea;
          |SINO;
               aImpresora = "";
          |FINSI;
     |FINSI;
     |XCIERRA nHandle;
     |QBF aImpresora;
|FPRC;

|PROCESO LeeUnidadBasico;
     aContiene = "";
     |ESPECIFICA "RBASS", aContiene;
     eaUnidad = (aContiene1 % 102) > "";

     aAlfa = eaUnidad % -101;
     |SI aAlfa ! ":";
          eaUnidad = "";
     |SINO;
          eaUnidad = eaUnidad % 101;
     |FINSI;
|FPRC;

|PROCESO ImprimeFichero;
     ||Me traigo el documento
     aOrigen = aBass + "dserp/documentos/";
     |QBF aOrigen;
     aOrigen = aOrigen + aNomFichero;
     |QBF aOrigen;
     aDestino = eaUnidad + ":\DOCUMENTOS\DSCOMER9\" + aNomFicheroDestino;
     |QBF aDestino;

     aDestinoPDF = eaUnidad + ":\DOCUMENTOS\DSCOMER9\" + aNomFicheroPDF;
     |QBF aDestinoPDF;

     |SI aIPRemoto ! "";
         |ENVIA_FICHERO aOrigen, aDestino;
     |SINO;
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;

     ||DEBUG;
     aExtension = (aDestino % -104) > "";
     aAuxImp = aImpresora > "";
     |SI aExtension = ".PDF"; |Y aAuxImp = "DSPDF";
          aEjecuta = "cmd " + &34 + "/c START /WAIT " + eaUnidad + ":\DOCUMENTOS\DSCOMER9\dsabreextension.exe " + aDestino + &34;
          |RSYSTEM aEjecuta;

          |SLEEP 1;
     |SINO;

          |SI aExtension = ".DOC"; |Y aAuxImp = "DSPDF";

               ||Primero convierto el .doc en .pdf usando dsxml

               aFicheroTxt = eaUnidad + ":\DOCUMENTOS\DSCOMER9\" + Usuario + ".txt";
               |RFBORRA aFicheroTxt;

               aFicheroLCK = eaUnidad + ":\DOCUMENTOS\DSCOMER9\" + Usuario + ".LCK";
               |RFBORRA aFicheroLCK;


               |XABRE "WBC", aFicheroTxt, nHandleTxt;

               aAlfa = "[FICHERO]#" + aDestino + "#" + aDestinoPDF + &13 + &10;
               |XGRABA nHandleTxt, aAlfa;

               aAlfa = "[IMPRESORA]#I#2#dspdf#2" + &13 + &10;
               |XGRABA nHandleTxt, aAlfa;

               |XCIERRA nHandleTxt;

               aEjecuta = "cmd " + &34 + "/c START /WAIT " + eaUnidad + ":\DOCUMENTOS\DSCOMER9\dsxml.exe " + aFicheroTxt + &34;
               |RSYSTEM aEjecuta;

               |PARA;  |SI;  |HACIENDO;
                    aFicFin = aFicheroLCK;
                    |XABRE "EC", aFicFin, nHandle;
                    |SI FSalida ] 0;
                         |SAL;
                    |FINSI;
                    |SLEEP 1;
                    |PULSATECLA;
               |SIGUE;

               ||Y luego ya lo abro usando dsabreextension

               aEjecuta = "cmd " + &34 + "/c START /WAIT " + eaUnidad + ":\DOCUMENTOS\DSCOMER9\dsabreextension.exe " + aDestinoPDF + &34;
               |RSYSTEM aEjecuta;

               |SLEEP 1;
          |SINO;
               |SI nSwHayDsPrintIni = 1; |O aImpresora = "";
                    aEjecuta = eaUnidad + ":\DOCUMENTOS\DSCOMER9\dsprint.exe";
                    aEjecuta = aEjecuta + " " + aDestino;
               |SINO;
                    ||Ejecuto el dsprint.exe, pasandole la impresora
                    aEjecuta = eaUnidad + ":\DOCUMENTOS\DSCOMER9\dsprint.exe";
                    aEjecuta = aEjecuta + " " + aDestino + " ";
                    aEjecuta = aEjecuta + &34 + aImpresora + &34;
               |FINSI;
               |RSYSTEM aEjecuta;

               aExtension = (aDestino % -104) > "";
               aAlfa1 = aExtension % 101;
               |SI aAlfa1 ! ".";
                   aExtension = (aDestino % -105) > "";
               |FINSI;
               ||Control del "Fin.txt"
               |SI aExtension ! ".DOC";      ||Cuando dsprinto documentos Word, no me crea el "fin.txt"

                    |PARA;  |SI;  |HACIENDO;
                         aFicFin = eaUnidad + ":\ds\dspdf\documentos\fin.txt";
                         |XABRE "EC", aFicFin, nHandle;
                         |SI FSalida ] 0;
                              |SAL;
                         |FINSI;
                         aFicFin = "C:\ds\dspdf\documentos\fin.txt";
                         |XABRE "EC", aFicFin, nHandle;
                         |SI FSalida ] 0;
                              |SAL;
                         |FINSI;
                         |SLEEP 1;
                         |PULSATECLA;
                    |SIGUE;
               |SINO;
                    |SLEEP 1;
                    |SLEEP 1;
                    |SLEEP 1;
               |FINSI;
          |FINSI;
          |RFBORRA aFicFin;
     |FINSI;

     |SLEEP 1;
|FPRC;

|PROCESO documentosERP;
     aNomFichero = #otroref@#3;
     |QBF aNomFichero;

     aExtension = (aNomFichero % -104) > "";
     aAlfa1 = aExtension % 101;
     |SI aAlfa1 ! ".";
          aExtension = (aDestino % -105) > "";
     |FINSI;

     aNomFicheroDestino = #otroref@#8;
     |QBT aNomFicheroDestino;
     |SI aNomFicheroDestino ! "";
          aNomFicheroPDF = aNomFicheroDestino + ".pdf";
          aNomFicheroDestino = aNomFicheroDestino + aExtension;
     |SINO;
          aNomFicheroDestino = aNomFichero;
          aNomFicheroPDF = aNomFicheroDestino - aExtension;
          aNomFicheroPDF = aNomFicheroPDF + ".pdf";
     |FINSI;

     |HAZ ImprimeFichero;
|FPRC;

|DEFBUCLE documentosERP;
 #otroref@#1005;
 ;
 aAplicaErp, nEmpErp, aDefErp, aClaveErp, 01;
 aAplicaErp, nEmpErp, aDefErp, aClaveErp, 99;
 ;
 NULL, documentosERP;
|FIN;

|PROCESO fergaw01_imp;
     |SI #fergaw01#3 = "S";
          aCien = " " * 100;
          nEmpErp = #48#0;
          aAplicaErp = "dscomer9";
          aDefErp = "dsm00024";
          aClaveErp = #fergaw01#0 + (("00000" + #fergaw01#1) % -105) + #fergaw01#2;
          |QBF aClaveErp;
          aClaveErp = (aClaveErp + aCien) % 199;
          aClaveErp = aClaveErp + " ";
          |BUCLE documentosERP;
          ||Haz Impresion usando dsprint.
     |FINSI;
|FPRC;


|DEFBUCLE fergaw01_imp;
 #fergaw01#1;
 ;
 INICIO;
 FINAL;
 ;
 NULL, fergaw01_imp;
|FIN;


|PROCESO DocErp;
     nSwHayDoc = 1;
     nSwEstaDocErp = 1;
     |ERROR10;
|FPRC;

|DEFBUCLE DocErp;
 #otroref@#1005;
 ;
 aAplicaErp, nEmpErp, aDefErp, aClaveErp, 01;
 aAplicaErp, nEmpErp, aDefErp, aClaveErp, 99;
 ;
 NULL, DocErp;
|FIN;

|PROCESO fergaw01_mira;
     aCien = " " * 100;
     nEmpErp = #48#0;
     aAplicaErp = "dscomer9";
     aDefErp = "dsm00024";
     aClaveErp = #fergaw01#0 + (("00000" + #fergaw01#1) % -105) + #fergaw01#2;
     |QBF aClaveErp;
     aClaveErp = (aClaveErp + aCien) % 199;
     aClaveErp = aClaveErp + " ";

     nSwHayDoc = 0;
     |BUCLE DocErp;

     |SI nSwHayDoc = 1;
          #fergaw01#3 = "S";
          |GRABA 020#fergaw01.a;
     |FINSI;
|FPRC;

|DEFBUCLE fergaw01_mira;
 #fergaw01#1;
 ;
 INICIO;
 FINAL;
 be;
 NULL, fergaw01_mira;
|FIN;

|PROCESO MiraSiEstaDocErp;
     aErp = aBass + "dserp/def/dserpm09.mas";
     |XABRE "E", aErp, 0;
     |SI FSalida ] 0;
          |CARGA_DEF aErp, otroref@;
          |SI FSalida = 0;
               aPathErp = aBass + "dserp/fich/";
               |PATH_DAT #otroref@#aPathErp#;

               |CIERRA #fergaw01
               |BUCLE fergaw01_mira;

               |DESCARGA_DEF #otroref@;
          |FINSI;
     |FINSI;
|FPRC;


|PROCESO T3; |TIPO 3;
     |HAZ LeeUnidadBasico;

     |DBASS aBass;
     |QBF aBass;

     aVacio30 = (" " * 30);
     aZeta30 = ("z" * 30);

     |HAZ CreaTempo; aTempo101 = Tempo;
     |NOME_DAT #101 aTempo101; |DELINDEX #101;

     |ABRE #101;
     |ABRE #fergam07;

     |BUCLE agifa010;

     |SI nSwHayPartidas = 1;
          nSwEstaDocErp = 0;
          |HAZ MiraSiEstaDocErp;
          |SI nSwEstaDocErp = 1;

               eaNombreBinario = "dsprint.exe";
               |HAZ TraeteBinario;
               eaNombreBinario = "dsabreextension.exe";
               |HAZ TraeteBinario;
               eaNombreBinario = "dsxml.exe";
               |HAZ TraeteBinario;

               ||Traerme el dsprint.exe
               ||Creo por si no existe el directorio \ds\dspdf\documentos\

               aAux = eaUnidad + ":\ds\";
               |RMKDIR aAux;
               aAux = eaUnidad + ":\ds\dspdf\";
               |RMKDIR aAux;
               aAux = eaUnidad + ":\ds\dspdf\documentos\";
               |RMKDIR aAux;

               |ABRE #fergam09;

               aIPRemoto = "";
               |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

               aImpresora = "";
               #fergam09#0 = aIPRemoto;
               |LEE 000#fergam09.=;
               |SI FSalida = 0;
                    aImpresora = #fergam09#1;
                    |QBF aImpresora;
               |FINSI;

               |SI aImpresora = "";
                    |HAZ PideImpresora;
                    |SI aImpresora ! "";
                         |DDEFECTO #fergam09;
                         #fergam09#0 = aIPRemoto;
                         |LEE 101#fergam09.=;
                         |SI FSalida ! 0; |GRABA 020#fergam09.b; |FINSI;
                         #fergam09#1 = aImpresora;
                         |GRABA 020#fergam09.a;
                         |LIBERA #fergam09;
                    |FINSI;
               |FINSI;

               aAbre = eaUnidad + ":\DOCUMENTOS\DSCOMER9\dsprint.ini";
               |RFBORRA aAbre;
               nSwHayDsPrintIni = 0;
               aAux = aImpresora;
               |QBT aAux;
               |SI aAux = ""; |O aAux = aImpresora;
                    ||El nombre de la impresora NO tiene espacios en blanco.
                    nSwHayDsPrintIni = 0;
               |SINO;
                    ||SI la impresora tiene espacio en blanco creo el dsprint.ini
                    ||Creo el dsprint.ini
                    aAbre = eaUnidad + ":\DOCUMENTOS\DSCOMER9\dsprint.ini";
                    |XABRE "WC", aAbre, nHandle;
                    |SI FSalida < 0;
                         nSwHayDsPrintIni = 0;
                    |SINO;
                         nSwHayDsPrintIni = 1;
                         |XGRABA nHandle, aImpresora;
                         |XCIERRA nHandle;
                    |FINSI;
               |FINSI;

               aErp = aBass + "dserp/def/dserpm09.mas";
               |XABRE "E", aErp, 0;
               |SI FSalida ] 0;
                    |CARGA_DEF aErp, otroref@;
                    |SI FSalida = 0;
                         aPathErp = aBass + "dserp/fich/";
                         |PATH_DAT #otroref@#aPathErp#;

                         |BUCLE fergaw01_imp;

                         |DESCARGA_DEF #otroref@;
                    |FINSI;
               |FINSI;
               |CIERRA #fergam09;
          |SINO;
               aMensaje = "0000No hay documentaci¢n para esta selecci¢n.";
               |MENAV aMensaje;
          |FINSI;
     |SINO;
          aMensaje = "0000No hay partidas para esta selecci¢n.";
          |MENAV aMensaje;
     |FINSI;

     |CIERRA #fergam07;
     |CIERRA #101;
     |DELINDEX #101; Tempo = aTempo101; |HAZ BoraTempo;
|FPRC;
