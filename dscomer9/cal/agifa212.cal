|FICHEROS;
     agifa077 #0;
     agifa019 #2;
     agifa080 #3;
     agifa112 #4;
     agifa017 #8;
     agif0041 #11;  || datos empresa
     agifa007 #40;
     agifa142 #41;

     agifa321 #45;  || Parametros de Divisas
     agifa322 #46;  || Lineas de Proveedores/Divisas
     agifa323 #47;  || Proveedores/Divisas
     agifa324 #42;  || Divisas
     agifa067 #67;
     agifa072 #72;
     agifa079 #79;
     dsm00279 #279;
     dsm00115;
     agifa704;
     agifa716;
     agifa722;
     referen@;
|FIN;

|VARIABLES;
     nBtnItemDocAlc = -1;
     nSal = 0;
     &eaFecAlmaObra = "";
     &enContaFac = 0;
     &PRMNT_enError = 0;
     Comodin = "";
     Comodin1 = "";
     nSw = 0;
     nCalc = 0;
     FEstado = 0;
     aAlfa1 = "";
     aFichAnt = "";
     nCmpAnt = 0;
     nContador = 0;

     &qSerieF       = "";
     &qSerie        = "";
     &qAlbar        = 0;
     &qFecha        = @;
     &qAbono        = "";

     aSalida = "";
     nSwPCEGroup = 0;
     &enModoCab77 = 0;
     &enEsCto = 0;
     &enNumero = 0;
     &enSwAct = 0;
     aSwModiCon = "";

     &eaSerJefe = "";
     &enEvento  = 0;
     &enEnvio   = 0;
     &eaOrigen  = "";
     &eaDestino = "";
     &eaAsunto  = "";

     nModo      = 0;

     nSwFlag        = 0;
     mensa          = "";
     aSerie         = "";
     nNume          = 0;
     aProv          = "";

     &ser_ped       = "";
     &num_ped       = 0;
     nModo212       = 0;
     aDatos         = "";
     aDirEmp        = "";
     nCodEmp        = 0;

   &aSerAntes     = "";
   &nNumAntes     = 0;
   aAlfa          = "";
   aMes           = "";
   nForma         = 0;
   nImpo          = 0;
   nMes           = 0;
   nSwVersion     = 0;

   &enSw          = 0;

  &nDeci_Base     = 0;   || Decimales en Totales de la moneda base
  &nDeci_PreBase  = 0;   || Decimales en el Precio en moneda base
  &nDeci_BaseMx   = 0;   || Decimales en Totales de la moneda base (Maxima precision)
  &nDeci_PreBaseMx= 0;   || Decimales en el Precio en moneda base (Maxima precision)
  &nDeci_Div      = 0;   || Decimales en Totales de la divisa
  &nDeci_PreDiv   = 0;   || Decimales en el Precio en divisa
  &nDeci_TotVis   = 0;   || Decimales en Totales visibles (el de la moneda de trabajo)
  &nDeci_TotNoVis = 0;   || Decimales en Totales no visibles (el de la otra moneda)
  &nDeci_PreVis   = 0;   || Decimales en el Precio visual. (lineas)
  &nDeci_ImpVis   = 0;   || Decimales en el Importe visual. (lineas)
  &nDeci_BaseMxP  = 0;   || Decimales en Totales de la moneda base (Maxima precision)
  &nDeci_PreBasMxP= 0;   || Decimales en el Precio en moneda base (Maxima precision)
  &nDeci_DivP     = 0;   || Decimales en Totales de la divisa
  &nDeci_PreDivP  = 0;   || Decimales en el Precio en divisa
  &nDeci_TotVisP  = 0;   || Decimales en Totales visibles (el de la moneda de trabajo)
  &nDeci_TotNoVisP= 0;   || Decimales en Totales no visibles (el de la otra moneda)
  &nDeci_PreVisP  = 0;   || Decimales en el Precio visual. (lineas)
  &nDeci_ImpVisP  = 0;   || Decimales en el Importe visual. (lineas)
  nfecha    = 0;
  &ac_divisa = "N";   || Vale "S" cuando las divisas en compras estan
                      || activadas y la serie es de divisas
  &pintado = 0;       || Sirve para controlar los pintados de las lineas
  &decim_divisa = 0;  || Decimales de la divisa
  &precio_divisa = 0; || Decimales del precio en divisa
  &decim_base = 0;    || Decimales de la moneda base
  &precio_base = 0;   || Decimales del precio en moneda base
  div_act = 0;        || Divisa actualizada. Me sirve en un PARA para que
                      || no salga hasta que este actualizada (div_act=1)
  &prov_divisa = "";  || Codigo de Proveedor. Se pasa a PROVEED/DIVISAS
  &divisa = "";       || Codigo de Divisa. Se pasa a ACTUAL. de DIVISAS
  nModo077 = 0;
  param = "";
 asto = 0;
 imneto = 0;
 tf = 0;
 con = 0;
 margen = 0;
control = 0;
mensaje01 = "0423Actualizando linea:";
mensaje10 = "0000Este albaran contiene lineas provenientes de Pedidos";
mensaje12 = "0000Este albaran esta FACTURADO";
canti2 = 0;
pm = 0;
pcm = 0;
pe = 0;
de = 0;
m1 = 0;
m2 = 0;
m3 = 0;
     m4        = 0;
     m5        = 0;
     m6        = 0;
&n_dec = 0;
&n_pre = 0;
     &bl  = "                              ";
     &serv_art = "N";
     &act_ent = "N";
     &sw_modo       = 0;
     puente    = "";
     iva_ant = 0;
     contenido = 0;
     modo = 0;
     impret = 0;
     precio = 0;
     caltot = 0;
     recalpre = 0;
     pvp1   = 0;
     pvp2   = 0;
     pvp3   = 0;
     pvp4   = 0;
     pvp5   = 0;
     pvp6   = 0;
     factordec = 0;
     aConfir = "";
     i = 0;

     &eaCodigo     = "";
     &eaTipoCodigo = "PR";

     nImpDiv = 0;
     nPPort1 = 0;
     nPPort2 = 0;
     &eaSerAlb = "";
     &enNumAlb = 0;
     aArti = "";
 {-1}aMenuPCEG = "";
     aMenuPCEG1 = "2400";
     aMenuPCEG2 = "1";
     aMenuPCEG3 = "Imprimir:";
     aMenuPCEG4 = "AE";
     aMenuPCEG5 = " Albaran ";
     aMenuPCEG6 = " Etiqueta ";
|FIN;

|INCLUYE i_varart;

|PROCESO Tipo8m212;  |TIPO 8;
     |HAZ EsTagloma;
     |SI FSalida = 0;
          |CONTROL_SIMPLE 0, "BOTON, ItemDoc", 0570, 0579, BtnItemDocAlc;
          nBtnItemDocAlc = FSalida;
     |FINSI;

     |HAZ EsConstru;
     |SI FSalida = 0;
          |DDEF aAlfa; aAlfa = aAlfa + "goparame";
          |CARGA_DEF aAlfa, referen@;
          |SI FSalida = 0;
               |ABRE #referen@;
               |LEE 000#referen@.p;
               |CIERRA #referen@;
               aSwModiCon = #referen@#69;
               |DESCARGA_DEF #referen@;
          |FINSI;
     |FINSI;
     ||컴컴컴컴컴컴컴컴횺odulo 003026
     |HAZ EsAlbero;
     |SI FSalida = 0;
          |HAZ AlbeAlbCom8;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
     nSwVersion = 0;
     |ABRE #67;
     |LEE 040#67p;
     |SI FSalida = 0;
         |SI #67#37 = 0;  |Y #67#5 ! 0;
             nSwVersion = 1;
         |FINSI;
     |FINSI;
     |CIERRA #67;

     |SI nSwVersion = 1;  |SUB_EJECUTA_NP "dsz00005";  |FINSI;

     |SI enSw = 1;  |PTEC 807;  |FINSI;
|FPRC;

|PROCESO serie7; |TIPO 7;
     |HAZ ControlUsuAC;
     |SI enErr ! 0; |PTEC 807; |ACABA; |FINSI;
     |HAZ DefSerCom;

     param = PARAMETRO; |QBF param;     || LLamado por agivf015
     |SI param ! "";
          #0#50 = param % 102;
          aAlfa = param % 306;  #0#0 = aAlfa;
     |FINSI;
|FPRC;

|PROCESO serie30; |TIPO 30;
     ||컴컴컴컴컴컴Modulo 002743;
     |HAZ EsGuerola;
     |SI FSalida = 0;
          |HAZ GuerAdjPed;
     |FINSI;
     |HAZ EsConstru;
     |SI FSalida = 0;
          |GRABA 020#0a;
          |CIERRAT #0;
          eaSerAlb = #0#50;
          enNumAlb = #0#0;
          |HAZ AdjAutoExpe;
          |ABRET #0;
          |LEE 101#0=;
     |FINSI;

     |HAZ EsPalmHotel;
     |SI FSalida = 0;
          |HAZ CreaFactura;
     |FINSI;

     |SI param ! "";                    || LLamado por agivf015
          |PTEC 807;
          |PTEC 807;
     |FINSI;
|FPRC;

|CALCULO fecalbc;|TIPO 7;
     modo = (FEntrada / 100) ! 0;
     |SI modo = 2;
          |C_M #0#1 , 1 , "N";    || Fecha Albaran
          |C_M #0#3 , 1 , "N";    || Cod. Proveedor
          |C_M #0#58, 1 , "N";    || Moneda Trabajo
          |C_M #0#56, 1 , "N";    || Divisa
          |C_M #0#57, 1 , "N";    || Cambio
          |C_M #0#59, 1 , "N";    ||
          |C_M #0#60, 1 , "N";    ||
     |SINO;
          |C_M #0#1 , 1 , "S";
          |C_M #0#3 , 1 , "S";
          |C_M #0#58, 1 , "S";    || Moneda Trabajo
          |C_M #0#56, 1 , "S";    || Divisa
          |C_M #0#57, 1 , "S";    || Cambio
          |C_M #0#59, 1 , "S";    ||
          |C_M #0#60, 1 , "S";    ||
     |FINSI;
     |SI modo = 1; |Y eaFecAlmaObra ! "";
          #0#1 = eaFecAlmaObra; || lo llama goalbpa1 (modulo 00con)
     |FINSI;
|FCAL;

|CALCULO cogeconc;|TIPO 0;
control = 0;
|FCAL;

|CALCULO mirar_d;
     enModoCab77 = (FEntrada / 100) ! 0;
     |ABRE #41; |LEE 000#41p; |CIERRA #41;
     n_dec = #41#8; n_pre = #41#9; act_ent = #41#34;
     aConfir = #41#76;
     serv_art = #41#131;
     |ABRE #agifa007;
     #agifa007#26 = #agifa077#50;
     |LEE 001#agifa007.=;
     |SI FSalida ! 0; |DDEFECTO #agifa007; |FINSI;
     |CIERRA #agifa007;
     |SI #agifa007#30 = "S";
          #0#12 = 0;
          #0#14 = 0;
          |PINTA #0#12;
          |PINTA #0#14;
     |FINSI;
     |HAZ LeeMonBase2;
     |HAZ LeeParamDiv2;
|FCAL;

|CALCULO pedconc;|TIPO 6;
con = (FEntrada / 100) ! 0;
|SI con = 2;|Y #0Campo ! Contenido;
  |SI #0#41 > 0;|O #0#38 = AS;
    |SI #0#41 > 0;
      |MENAV mensaje10;
    |FINSI;
    |SI #0#38 = AS;
      |MENAV mensaje12;
    |FINSI;
  |SINO;
    control = 1;
  |FINSI;
|FINSI;
|FCAL;

|CALCULO dcon;|TIPO 6;
con = FEntrada / 100;
con = con ! 0;
|SI con = 2;|Y #0Campo ! Contenido;
    |SI #0#38 = AS;
       |MENAV mensaje12;
       |ERROR;
    |FINSI;
|FINSI;
|FCAL;

|PROCESO Tipo1_212; |TIPO 1;
     nSwFlag = 0;
|FPRC;

|PROCESO ConsBorAdjAuto;
     enModo = 3;
     eaSerie = #3#27;
     enCodigo = #3#0;
     enLinea = #3#1;
     |SUB_EJECUTA_NP "guerz061";
|FPRC;

|PROCESO PCEGCalibra;
     aArti = #3#2;
     aAlfa = aArti - "CAL";
     |SI FSalida > 0;
          eaSerie = #3#27;
          enCodigo = #3#0;
          enLinea = #3#1;
          enModo = con;
          enForma = nForma;
          |SUB_EJECUTA_NP "pcegz023;ALBARAN";
     |FINSI;
|FPRC;

|CALCULO acacpc;|TIPO 2;
     nForma = 0 +. 1;
     con = (FEntrada / 100) ! 0;
     enModoCab77 = con;
     |SI con = 1; nSwFlag = 0; |FINSI;
     |SI nSwFlag = 1; |ERROR; |ACABA; |FINSI;

     |HAZ ControlUsuAC;
     |SI enErr ! 0;
          nSwFlag = 1;
          |ERROR; |ACABA;
     |FINSI;

     |SI #0#57 = 0; || chequea el cambio
          |ABRE #agifa324;
          #agifa324#0 = #0#56;
          |LEE 020#agifa324.=;
          |SI FSalida = 0;
               #0#57 = #agifa324#3;
          |FINSI;
          |CIERRA #agifa324;
     |FINSI;

     ||컴컴컴컴컴컴컴횺odulo 00con
     |MODULO aAlfa;
     |HAZ EsConstru;
     |SI con ! 1; |Y FSalida = 0; |Y #0#34 ! "PE"; |Y aAlfa = "agifa212";
          |SI aSwModiCon = "S";
               |MENAV "0000Albaran Asignado a Expediente. No se perimite Modificacion...";
               nSwFlag = 1;
               |ERROR; |ACABA;
          |SINO;
               |MENAV "0000Albaran Asignado a Expediente.";
               |MENAV "0000Si modifica el albaran debera posteriormente modificar la asignacion al expediente";
          |FINSI;
     |FINSI;

     |SI nForma = -1;
          |HAZ EsPalmHotel;
          |SI FSalida = 0;
               |HAZ PalmHotelCtrl;
               |SI nSwFlag = 1; |ERROR; |ACABA; |FINSI;
          |FINSI;
     |FINSI;

     ||컴컴컴컴컴컴컴컴컴컴컴컴컴
     |SI con ! 1; |Y #0#41 = 0;
          |MENAV "0000El Albaran no proviene de pedido...";
          nSwFlag = 1;
          |ERROR; |ACABA;
     |FINSI;

     |SI con = 3;
          eaSer = #0#50;
          enDocu = #0#0;
          efFec = #0#1;
          |HAZ MiraHisC;
          |SI enErrHis ! 0;
               nSwFlag = 1;
               |ERROR; |ACABA;
          |FINSI;
     |FINSI;
     |SI con = 2;|Y #0#38 = AS;
          |MENAV mensaje12;
          nSwFlag = 1;
          |ERROR; |ACABA;
     |FINSI;
     |SI con = 3;|Y #0#38 = AS;
          |SI #0#38 = AS;
               |MENAV mensaje12;
          |FINSI;
          nSwFlag = 1;
          |ERROR; |ACABA;
     |SINO;
          |SI con = 1;
               |HAZ EsBerria;
               |SI FSalida = 0;
                    eaSer = #0#50;
                    enDocu = #0#0;
                    |SUB_EJECUTA_NP "berz0014;ADJ";
                    |HAZ actalc;
               |FINSI;
          |FINSI;
          |SI control = 1;
               |HAZ actalc;
          |FINSI;
     |FINSI;

     |SI #0#75 = "S"; |Y nForma = -1;
          |HAZ EsIber21;
          |SI FSalida = 0; |Y #41#164 = "D";
               |MENAV "0000Albaran Exportado...";
               |HAZ ClaveAlbIber;
               |SI FSalida ! 0;
                    nSwFlag = 1;
                    |ERROR; |ACABA;
               |FINSI;
          |SINO;
               |MENAV "0000Albaran Exportado, no se puede Modificar...";
               nSwFlag = 1;
               |ERROR; |ACABA;
          |FINSI;
     |FINSI;

     |HAZ EsPCEGroup;
     |SI FSalida = 0;
          |BUCLELIN 2 PCEGCalibra #0;
          |SI con = 3;
               |HAZ EsPCEGroup;
               |SI FSalida = 0;
                    eaSerie = #0#50;
                    enDocu = #0#0;
                    eaTipo = "AC"
                    eaPregunta = "S";
                    |SUB_EJECUTA_NP "pcegz019;BORRA";
               |FINSI;
          |FINSI;
     |FINSI;

     ||컴컴컴컴컴컴컴컴컴컴컴 Modulo 00con
     |HAZ EsConstru;
     |SI FSalida = 0;
          |SI con = 1;
               aAlfa = #0#34; |QBF aAlfa;
               |SI aAlfa = ""; #0#34 = "PE"; |FINSI;
          |FINSI;
          |SI con = 3;
               |BUCLELIN 0ConsBorAdjAuto#0;
          |FINSI;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴컴컴
     |SI con = 3;
          |HAZ EsAlbero;
          |SI FSalida = 0;
               |HAZ AlbeAlbBorra;
          |FINSI;
          |HAZ EsDesanch;
          |SI FSalida = 0;
               aAlfa = "desan016;AB" + #0#50 + (("000000" + #0#0) % -106);
               |SUB_EJECUTA aAlfa;
          |FINSI;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴컴컴

     #0#75 = "N"; || EXPORTADO

     pcm = 0;
     pm = 0;
     pe = 0;
     de = 0;
     |ABRE #2;
     |ABRE #8;
     |ABRE #4;
     enSwAct = -2;
     |BUCLELIN 0pcmlinea#0;
     enSwAct = 0;
     |CIERRA #4;
     |CIERRA #8;
     |CIERRA #2;
     ||컴컴컴컴컴컴Automatizacion Empresas
     |ABRE #dsm00115; |LEE 000#dsm00115.p; nSal = FSalida; |CIERRA #dsm00115;
     |SI nSal = 0;
          eaProve = #0#3;
          |SI nForma = 1;
               aAlfa = "dsz99982;ALBALTA" + #0#50 + (("000000" + #0#0) % -106);
               |SUB_EJECUTA aAlfa;
          |SINO;
               aAlfa = "dsz99982;ALBBAJA" + #0#50 + (("000000" + #0#0) % -106);
               |SUB_EJECUTA aAlfa;
          |FINSI;
     |FINSI;
     ||컴컴컴컴컴
|FCAL;

|CALCULO acttlc;|TIPO 0;
     |SI caltot = 0; ||Si no viene del proceso caltotal
         |PINTA 445,#3#1;
         #3#15 = #0#3;
         |GRABA 020#3a;
     |SINO;
         |SI recalpre = 1;
             |HAZ TotalLin80;
             |GRABA 001#3a; ||Grabo lineas
         |FINSI;
     |FINSI;

     |HAZ CalCabAgifa077;
|FCAL;

|DEFBUCLE agifa080;
     #3#1;
     ;
     #0#50, #0#0, 001;
     #0#50, #0#0, 999;
     ;
     NULL, acttlc;
|FIN;

|CALCULO actalc;|TIPO 0;
     |MENSA mensaje01;

     |HAZ LimCabAgifa077;
     |BUCLE agifa080;

     |SI caltot = 0;   |BLIN 4;  |FINSI;
|FCAL;

|CALCULO siimprec;|TIPO 3;
     ||컴컴컴컴컴컴컴컴컴컴Modulo 000323
     |HAZ EsTecatel;
     |SI FSalida = 0;
          eaSer = #0#50;
          enDocu = #0#0;
          eaCodigo = #0#3;
          eaTipo = "AI";
          |HAZ TecaTransAdjC;
     |FINSI;
     ||컴컴컴컴컴컴컴컴컴컴컴컴컴

     aSerAntes  = "";  || Inicializa variables anteriores
     nNumAntes  = 0;   || de Adj.


     |HAZ EsConstru;
     |SI FSalida = 0;
          |DDEF aAlfa;
          aAlfa = aAlfa + "goparame";
          |CARGA_DEF aAlfa, referen@;
          |SI FSalida = 0;
               |ABRE #referen@; |LEE 000#referen@.p; |CIERRA #referen@;
               aAlfa = #referen@#92;
               |DESCARGA_DEF #referen@;
          |FINSI;
          |SI aAlfa = "S";
               |CONFI "2400NDesea imprimir este albaran? ";
               |SI 0 = FSalida;
                    |HAZ imprealc;
               |FINSI;
          |FINSI;
     |SINO;
          |CONFI "2400NDesea imprimir este albaran? ";
          |SI 0 = FSalida;
               |HAZ imprealc;
          |FINSI;
     |FINSI;

     |HAZ EsPCEGroup;
     |SI FSalida = 0;
          eaSerie = #3#27;
          enCodigo = #3#0;
          enLinea = 0;
          |SUB_EJECUTA_NP "pcegz023;ESREPARA";
          |SI enLinea ! 0;         ||Si es 0 es de reparacion
               |HAZ EtiquePCEG;
          |FINSI;
     |FINSI;

     ||컴컴컴컴컴컴컴컴컴횺odulo Contruccion
     |HAZ EsConstru;
     |SI FSalida = 0;
          nModo = (FEntrada / 100) ! 0;
          |SI nModo = 1; |O nModo = 2;
               |GRABA 020#0a;
          |FINSI;
          |DBASS aAlfa;
          eaFicImpre= aAlfa + "spool/";
          aAlfa = Usuario;
          eaFicImpre = eaFicImpre + aAlfa;
          ser_ped = #0#50;
          num_ped = #0#0;
          |CIERRAT #0;
          aAlfa = "agifa016;" + eaFicImpre;
          |SUB_EJECUTA_NP aAlfa;
          |ABRET #0;
          #0#50 = ser_ped;
          #0#0  = num_ped;
          |LEE 101#0=;
          eaSerJefe = #0#50;
          eaPrograma = "agifa212"  + #0#50 + #0#0;
          |SI nModo = 1;
               enEvento = 3; eaTipo = "03";
               eaAsunto = "Alta albaran nro. '" + #agifa077#50 + "/" + (("000000" + #agifa077#0) % -106) + "'";
               eaOrigen = Usuario % 0810;
               |HAZ EnviaEvento;
          |FINSI;
          |SI nModo = 2;
               enEvento = 6; eaTipo = "06";
               eaAsunto = "Modificacion albaran nro. '" + #agifa077#50 + "/" + (("000000" + #agifa077#0) % -106) + "'";
               eaOrigen = Usuario % 0810;
               |HAZ EnviaEvento;
          |FINSI;
     |FINSI;
|FCAL;

|CALCULO imprealc;
     modo = (FEntrada / 100) ! 0;
     |SI modo = 1; |O modo = 2;
          |GRABA 021#0a;
          |LIBERA #0;
     |FINSI;
     ser_ped = #0#50;
     num_ped = #0#0;
     |CIERRAT #0;

     |SUB_EJECUTA_NP "agifa016";

     |ABRET #0;
     #0#50 = ser_ped;
     #0#0  = num_ped;
     |SI modo = 4;
          |LEE 001#0=;
     |SINO;
          |LEE 101#0=;
     |FINSI;
|FCAL;

|CALCULO totcomp;|TIPO 14;
     |SI #0#41 = 0; |Y #0#79 = 0;
          |PINTA 0577,"AC";
     |SINO;
          |SI #0#79 ! 0;
               |PINTA 0577,"DC";
          |SINO;
               |PINTA 0577,"PC";
          |FINSI;
     |FINSI;
|FCAL;

|PROCESO pcmlinea;
     ||컴컴컴컴컴컴 Doble/Multi
     enPrecioB  = precio_base;
     enDeciB    = nDeci_PreBase;
     enForma = nForma;
     eaDocu = "agifa212";
     |HAZ StockEntra;
     ||컴컴컴컴컴컴컴컴컴컴컴컴
     ||-------------------------------Prov
     nImpo = (#3#9 < #0#5) < #0#32;
     |SI #41#114 = "S";
          nImpo = nImpo < #0#7;
     |FINSI;
     #4#0 = #0#3;
     |LEE 101#4=;
     |SI FSalida = 0;
         aMes = #0#1 % A402;
         nMes = aMes + 26;
         |SI #0#42 = "N";
             #4nMes = #4nMes +. (nImpo * nForma);
             #4#39 = #4#39 +. (nImpo * nForma);
         |SINO;
             #4nMes = #4nMes -. (nImpo * nForma);
             #4#39 = #4#39 -. (nImpo * nForma);
         |FINSI;
         |GRABA 020#4a;
         |LIBERA #4;
     |FINSI;

     |SI #0#42 = "N";
          enImpPrvCom = nImpo * nForma;
     |SINO;
          enImpPrvCom = -nImpo * nForma;
     |FINSI;
     efFecha = #0#1;
     eaProve = #0#3;
     |HAZ AcumulaPrv;
|FPRC;

|PROCESO pr_obs; |TIPO 0;
     nModo212 = (FEntrada / 100) ! 0;
     |SI FSalida = 11; |O #41#102 = "S"; |O #41#102 = "X";
          eaCodigo = #0#3;
          eaFuerza = "N";
          |SI FSalida = 11; eaFuerza = "S"; |FINSI;
          |SUB_EJECUTA_NP "dsz99994";
          |SI #41#102 = "N"; |Y nModo212 ! 2; |ERROR;  |FINSI;
     |FINSI;

     |SI #agifa142#15 = "S";
          puente = #agifa112#26;     |QBF puente;
          |SI puente ! "";
               |PUSHV 1008 1370;
               |SI #agifa142#21 = "S"; |AVISO; |FINSI;
               |BLANCO 1009 1369;
               |CUADRO 1008 1370;
               |ATRI R;
               |PINTA 1110 , #agifa112#26;
               |ATRI 0;
               |PAUSA;
               |POPV;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO ant_fecha; |TIPO 7;
     sw_modo = (FEntrada / 100) ! 0;
|FPRC;

|PROCESO LeeMonBase2; ||Lee los parametros de la moneda base
     |ABRE #agifa321;
     |ABRE #agifa324;
     |LEE 010#agifa321.p;   ||Leo parametros de divisa
     #agifa077#61 = #agifa321#9;  ||Cojo la Moneda Base de parametros
     #agifa324#0 = #agifa321#9;
     |LEE 010#agifa324.=;         ||Leo la ficha de la moneda base en Divisas
     #agifa077#62 = #agifa324#3;  ||Asigno el cambio de la moneda base con el euro
     decim_base = #agifa324#7;    ||Asigno los decimales de la moneda base
     precio_base = #agifa324#9;   ||Asigno los decimales del precio en la mon. base
     |CIERRA #agifa324;
     |CIERRA #agifa321;
|FPRC;

|PROCESO Param_Divisas2; |TIPO 0;
     pintado = 0;  ||Controla pintados de las lineas

     |ABRE #agifa324;
     #agifa324#0 = #agifa077#56;
     |LEE 001#agifa324.=;   || Leo la divisa
     |CIERRA #agifa324;

     decim_divisa = #agifa324#7;  || Decimales de la divisa
     precio_divisa = #agifa324#9;  || Decimales en el precio de la divisa
     nDeci_Div = decim_divisa;   || Decimales de la divisa
     nDeci_PreDiv = precio_divisa;  || Decimales en el precio de la divisa
     nDeci_DivP = decim_divisa;   || Decimales de la divisa
     nDeci_PreDivP = precio_divisa;  || Decimales en el precio de la divisa
     |SI #0#58 = "B";   ||Si trabajo con la moneda base ...
          nDeci_Base      = decim_base;
          nDeci_PreBase   = precio_base;
          nDeci_PreVis    = precio_divisa;
          nDeci_ImpVis    = decim_divisa;
          nDeci_BaseMx    = decim_base;
          nDeci_PreBaseMx = precio_base;
          nDeci_TotVis    = decim_base;
          nDeci_TotNoVis  = decim_divisa;
          nDeci_PreVisP   = precio_divisa;
          nDeci_ImpVisP   = decim_divisa;
          nDeci_BaseMxP   = decim_base;
          nDeci_PreBasMxP = precio_base;
          nDeci_TotVisP   = decim_base;
          nDeci_TotNoVisP = decim_divisa;
     |SINO;     ||Si trabajo con la divisa ...
          nDeci_Base     = decim_base; || Max. precision en los decimales de la moneda base
          nDeci_PreBase  = precio_base;  || Max. precision en los dec. del precio de la moneda base
          nDeci_PreVis   = precio_base;
          nDeci_ImpVis   = decim_base;
         |SI #0#57 = 1;
              nDeci_BaseMx    = decim_base;    || sin triangulacion
              nDeci_PreBaseMx = precio_base;
         |SINO;
              nDeci_BaseMx    = #agifa321#10;  || con triangulacion
              nDeci_PreBaseMx = #agifa321#11;
         |FINSI;
          nDeci_TotVis   = decim_divisa;
          nDeci_TotNoVis = decim_base;
          nDeci_PreVisP  = precio_base;
          nDeci_ImpVisP  = decim_base;
         |SI #0#57 = 1;
              nDeci_BaseMxP   = decim_base;    || sin triangulacion
              nDeci_PreBasMxP = precio_base;
         |SINO;
              nDeci_BaseMxP   = #agifa321#10;  || con triangulacion
              nDeci_PreBasMxP = #agifa321#11;
         |FINSI;
          nDeci_TotVisP  = decim_divisa;
          nDeci_TotNoVisP= decim_base;
     |FINSI;
     |HAZ Deci_IVAC;
|FPRC;

|PROCESO LeeParamDiv2; |TIPO 0;
     |ABRE #agifa321;
     |LEE 001#agifa321.p;    || Leo parametros de divisas
     |ABRE #agifa007;
     #agifa007#26 = #agifa077#50;
     |LEE 001#agifa007.=;        || Leo la serie
     |SI #0#58 = "D";      || Si la mon. de trabajo es la divisa...
       || ...escondo los campos de la moneda base :
       |CAMPO_POSICION #3#6 , 1 , 1009;
       |CAMPO_POSICION #3#9 , 1 ,1026;
       |CAMPO_VISUAL #3#6 , 1 , "N";
       |CAMPO_VISUAL #3#9 , 1 , "N";
       |CAMPO_LINEAL #3#6 , 1 , "N";
       |CAMPO_LINEAL #3#9 , 1 , "N";
       |C_M #3#6 , 1 , "N";
       || ...y pongo en pantalla los de divisa
       |CAMPO_POSICION #3#30 , 1 , 1544;
       |CAMPO_POSICION #3#31 , 1 ,1562;
       |CAMPO_VISUAL #3#30 , 1 , "S";
       |CAMPO_VISUAL #3#31 , 1 , "S";
       |C_M #3#30 , 1 , "S";
       |CAMPO_LINEAL #3#30 , 1 , "S";
       |CAMPO_LINEAL #3#31 , 1 , "S";
       |SI #agifa007#39 = "S"; |Y #agifa321#2 = "S"; ||Si la serie es de ...
          ac_divisa = "S";  || ... divisas, y en parametros estan activadas ...
          || ... pongo como modificables los campos relacionados con divisas
          |C_M #0#56 , 1 , "S";
          |C_M #0#57 , 1 , "S";
          |C_M #0#58 , 1 , "S";
          |C_M #0#59 , 1 , "S";
          |C_M #0#60 , 1 , "S";
          |SI #agifa321#4 = "S";  || Si la visualizacion de parametros esta activada
            || ... pongo los campos visual. de lineas como visualizables
            |CAMPO_VISUAL #3#32, 1 , "S";
            |CAMPO_VISUAL #3#33 , 1 , "S";
          |SINO;
            || ... los pongo como no visualizables
            |CAMPO_VISUAL #3#32 , 1 , "N";
            |CAMPO_VISUAL #3#33 , 1 , "N";
          |FINSI;
       |SINO; || Si la serie no es de divisas, o no estan activados parametros
          ac_divisa = "N";
          #0#58 = "B";  || La moneda de trabajo es por fuerza la moneda base
          |PINTA #0#58;
          || Pongo como no modificables los campos relacionados con divisas
          |C_M #0#58 , 1 , "N";
          |C_M #0#59 , 1 , "N";
          |C_M #0#60 , 1 , "N";
          |C_M #0#56 , 1 , "N";
          |C_M #0#57 , 1 , "N";
          || Pongo como no visualizables los campos visual. de lineas
          |CAMPO_VISUAL #3#32, 1 , "N";
          |CAMPO_VISUAL #3#33, 1 , "N";
       |FINSI;
     |SINO;  || Si la moneda de trabajo es la moneda base
       || ...escondo los campos en divisa :
       |CAMPO_POSICION #3#30 , 1 , 1009;
       |CAMPO_POSICION #3#31 , 1 ,1026;
       |CAMPO_VISUAL #3#30 , 1 , "N";
       |CAMPO_VISUAL #3#31 , 1 , "N";
       |C_M #3#30 , 1 , "N";
       |CAMPO_LINEAL #3#30 , 1 , "N";
       |CAMPO_LINEAL #3#31 , 1 , "N";
       || ...coloco los campos en moneda base:
       |CAMPO_POSICION #3#6 , 1 , 1544;
       |CAMPO_POSICION #3#9 , 1 ,1562;
       |CAMPO_VISUAL #3#6 , 1 , "S";
       |CAMPO_VISUAL #3#9 , 1 , "S";
       |C_M #3#6 , 1 , "S";
       |CAMPO_LINEAL #3#6 , 1 , "S";
       |CAMPO_LINEAL #3#9 , 1 , "S";
       |SI #agifa007#39 = "S"; |Y #agifa321#2 = "S"; || Si la serie es de divisas...
          ac_divisa = "S"; || ... y divisas estan activadas en parametros
          || Pongo como modificables los campos relacionados con divisas
          |C_M #0#56 , 1 , "S";
          |C_M #0#57 , 1 , "S";
          |C_M #0#58 , 1 , "S";
          |C_M #0#59 , 1 , "S";
          |C_M #0#60 , 1 , "S";
          |SI #agifa321#4 = "S";  || Si la visualizacion esta activada en parametros ...
            || Pongo como visualizables los campos visual. de las lineas
            |CAMPO_VISUAL #3#32, 1 , "S";
            |CAMPO_VISUAL #3#33 , 1 , "S";
          |SINO; || Los pongo como no visualizables
            |CAMPO_VISUAL #3#32 , 1 , "N";
            |CAMPO_VISUAL #3#33 , 1 , "N";
          |FINSI;
       |SINO;  || Si la serie no es de divisas o no estan activados los parametros
          ac_divisa = "N";
          #0#58 = "B";  || La moneda de trabajo es por fuerza la moneda base
          || Pongo como no modificables los campos relacionados con divisas
          |C_M #0#58 , 1 , "N";
          |C_M #0#59 , 1 , "N";
          |C_M #0#60 , 1 , "N";
          |C_M #0#56 , 1 , "N";
          |C_M #0#57 , 1 , "N";
          || Pongo como no visualizables los campos visual. de lineas
          |CAMPO_VISUAL #3#32, 1 , "N";
          |CAMPO_VISUAL #3#33, 1 , "N";
       |FINSI;
     |FINSI;
     |CIERRA #agifa007;
|CIERRA #agifa321;
|HAZ Param_Divisas2;
|FPRC;

|PROCESO CambioDivisa2; |TIPO 0;
     nModo077 = (FEntrada / 100) ! 0;
     |SI #0Campo ! Contenido; |Y nModo077 = 1;   ||Si cambia el valor o es alta
          |SI Campo = 3; || Si llamo al proceso desde el cod. de proveedor ...
               |LEE 001#agifa112.=;
               #0#56 = #agifa112#81;  || Asigno la divisa por defecto del proveedor
               #0#58 = #agifa112#82;  || Asigno la moneda de trabajo del proveedor
               |PINTA #0#58;
          |FINSI;
          #agifa007#26 = #agifa077#50;
          |LEE 001#agifa007.=;        || Leo la serie
          |SI #agifa007#39 = "S"; |Y #agifa321#2 = "S";
               |SI #agifa112#80 = "S"; || Si el proveedor es de divisa pactada
                    |ABRE #agifa323;
                    |ABRE #agifa322;
                    #agifa322#0 = #agifa077#3;
                    #agifa322#2 = #agifa077#56;
                    #agifa322#7 = "N";
                    |SELECT #2#agifa322;  || Con esta clave ...
                    |LEE 011#agifa322.=;  || ... busco periodos no finalizados de esa divisa
                    |SI FSalida = 0;  || Si existe un periodo no finalizado
                         |PARA div_act = 0; |SI div_act = 0; |HACIENDO;
                              #agifa077#56 = #agifa322#2;        || Cargo la divisa ...
                              #agifa077#57 = #agifa322#3;        || ...y el cambio
                              |LEE 001#agifa322.=;   || Leo las lineas de Proveed/Divisas
                              nfecha = #agifa077#1 - #agifa322#6;  || Resto la fecha de fin de periodo a la actual
                              |SI nfecha > 0;  || Si la fecha actual supera a la del periodo asignado ...
                                   |MENAV "0000ATENCION deberia Actualizar primero la divisa en Proveedores/Divisas";
                                   prov_divisa = #agifa077#3;  || Le envio el cod. de proveedor a "agifa323"
                                   |SUB_EJECUTA_NP "agifa323.tab";  || Ejecuto Proveedores/Divisas
                                   |ERROR;
                              |SINO;
                                   div_act = 1;  || Para salir del PARA
                              |FINSI;
                         |SIGUE;
                    |SINO;  || Si no existe un periodo no finalizado para esa divisa
                         |MENAV "0000No existe un periodo activo para la divisa en Proveedores / Divisas";
                         prov_divisa = #agifa077#3;  || Le paso a "agifa323" el proveedor
                         |SUB_EJECUTA_NP "agifa323.tab";  || Ejecuto Proveedores / Divisas
                         |ERROR;
                    |FINSI;
                    |SELECT #1#agifa322;
                    |CIERRA #agifa322;
                    |CIERRA #agifa323;
               |SINO; || Si el Proveedor no es de Divisa pactada ...
                    |ABRE #agifa324; || ...miro directamente en divisas
                    #agifa324#0 = #agifa077#56;
                    |LEE 001#agifa324.=;
                    |SI FSalida = 0; ||Si existe la divisa...
                         #agifa077#57 = #agifa324#3; || la recojo
                         |SI #agifa324#6 ! -1; || Si los dias de margen de actualizacion no
                                               || son = -1 (indefinidos)
                              |PARA div_act = 0; |SI div_act = 0; |HACIENDO;
                                   #agifa077#56 = #agifa324#0;        ||Cargo el valor de la divisa
                                   #agifa077#57 = #agifa324#3;        ||Cargo el valor del cambio
                                   |LEE 001#agifa324.=;  || Leo la divisa
                                   nfecha = #agifa077#1 - #agifa324#4;
                                   |SI nfecha > #agifa324#6; |Y #agifa324#6 ! -1;  || Si la fecha actual
                                        |MENAV "0000ATENCION deberia Actualizar primero la divisa";
                                        divisa = #agifa077#56;
                                        |SUB_EJECUTA_NP "agifa326.tab";
                                        |ERROR;
                                   |SINO;
                                        div_act = 1;
                                   |FINSI;
                              |SIGUE;
                         |FINSI;
                    |FINSI;
                    |CIERRA #agifa324;
               |FINSI;
          |SINO;
               #agifa077#56 = #agifa077#61;
               #agifa077#57 = #agifa077#62;
          |FINSI;
          |PINTA #agifa077#56;
          |PINTA #agifa077#57;
          |SI Campo = 3; || Si llamo al proceso desde el cod. de proveedor ...
               |HAZ LeeParamDiv2;
          |SINO;
               |HAZ Param_Divisas2;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO convportes2; |TIPO 6;  || Convierte los portes a la mon. base
|SI #0#58 = "B";  || Si la moneda de trabajo es la moneda base ...
  #0#11 = #0#69;  || ... basta con asignarlo
|SINO;    || Si la moneda de trabajo es la divisa
  #0#11 = #0#69 / #0#57 * #0#62;   || ... lo convierto
|FINSI;
|HAZ dcon;
|FPRC;

|PROCESO convvarios2; |TIPO 6;  || Convierte los varios a la mon. base
|SI #0#58 = "B";  || Si la moneda de trabajo es la moneda base ...
  #0#13 = #0#70;  || ... basta con asignarlo
|SINO;    || Si la moneda de trabajo es la divisa ...
  #0#13 = #0#70 / #0#57 * #0#62;  || ... lo convierto
|FINSI;
|HAZ dcon;
|FPRC;


|PROCESO LeeAlb2; |TIPO 13;  || Sirve para cargar los valores cada vez que
                            || cambio de ficha
     pintado = 0;   || controla pintado de las lineas
     |HAZ LeeMonBase2;
     |HAZ LeeParamDiv2;
     ||TODO CCC
     |ABRE #41; |LEE 000#41p; |CIERRA #41;
     |ABRE #279; |LEE 000#279p; |CIERRA #279;
     |SI #41#148 = "N";
          |C_V #agifa077#73, 1, "N";
          |C_M #agifa077#73, 1, "N";
     |SINO;
          |C_V #agifa077#73, 1, "S";
          |C_M #agifa077#73, 1, "S";
          |PINTA 1503, "Portes Linea Facturables:";
     |FINSI;
     |HAZ EsConstru;
     |SI FSalida = 0;
          |ATRI I;
          |PINTA 1971, "Asig:";
          |ATRI 0;
          |C_V #0#34, 1, "S";
          |PINTA #0#34;
     |FINSI;
     |HAZ EsAlbero;
     |SI FSalida = 0;
          |HAZ AlbeAlbPinta;
     |FINSI;
     |HAZ EsInmMarro;
     |SI FSalida = 0;
          |PINTA 1061, "Matri:";
          |PINTA 1161, "컴컴컴";
     |FINSI;
     |HAZ EsDesanch;
     |SI FSalida = 0;
          |HAZ PinDesanch212;
     |FINSI;
|FPRC;

|CALCULO cambfecha2;
     |SI FSalida = 0;
          |HAZ EsDesanch;
          |SI FSalida = 0;
               aAlfa = "desan016;AA" + #0#50 + (("000000" + #0#0) % -106);
               |SUB_EJECUTA aAlfa;
               |HAZ PinDesanch212;
          |FINSI;
     |FINSI;
     |HAZ EsAlbero;
     |SI FSalida = 0;
          |HAZ AlbeAlbEntra;
     |FINSI;
     |SI #0#59 ! "O";
       #0#60 = "        ";
       |PINTA #0#60;
       |C_M #0#60,1,"N";
       |C_V #0#60,1,"N";
     |SINO;
       |C_M #0#60,1,"S";
       |C_V #0#60,1,"S";
     |FINSI;
|FCAL;

|CALCULO modomod2; |TIPO 6;
  |SI nModo077 = 2; |Y #0Campo ! Contenido;
    |MENAV "0000No se puede modificar el campo.";
    #0Campo = Contenido;
    |PINTA #0Campo;
    |ERROR;
  |FINSI;
  |SI #0Campo ! Contenido; |Y Campo = 58;
    |HAZ LeeParamDiv2;
  |FINSI;
|FCAL;

|PROCESO caltotala; |TIPO 0; ||Proceso especial para casos determinados como
                            ||modificar los campos de cabecera en dtos, cambio divisas etc
                            ||Recalcula total albaran, descuentos y ivas de lineas
                            ||En el caso de cambiar el cambio de divisas recalcula tambien precio... de lineas
     |SI #0Campo ! Contenido;||Solo si cambia
          caltot = 1;
          |SI Campo = 57; ||Si viene del cambio divisa
              recalpre = 1; ||Recalcular tambien precios linea
          |FINSI;
          |HAZ convportes2;
          |HAZ convvarios2;
          |HAZ actalc;

          |PINTA #0#66;
          caltot = 0;
          recalpre = 0;
     |FINSI;
|FPRC;

|PROCESO Fecha212; |TIPO 0;
|FPRC;

|PROCESO DatosEmpresa2;   |TIPO 7;
     nModo212 = (FEntrada / 100) ! 0;
     |SI nModo212 ! 1; |ACABA; |FINSI;
     |DFICE aDirEmp;            || tomo el directorio de la empresa
     |DFICB aDatos;             || tomo el directorio de los datos
     aDirEmp = aDirEmp % -205;  || me quedo con los 5 digitos de la empresa
     nCodEmp = aDirEmp;         || y los paso a una variable numerica
     |PATH_DAT #11 aDatos;      || pongo el path al fichero agifa041
     |ABRE #11;                 || abre el fichero agifa041
     #11#0 = nCodEmp;
     |LEE 020#11=;              || y busco la empresa en la que estoy
     |SI FSalida = 0;
          #0#29 = #11#1;        || nombre de la empresa
          #0#30 = #11#2;        || direccion de la empresa
          #0#31 = #11#3;        || poblacion de la empresa
          #0#33 = #11#4;        || provincia de la empresa
          |PINTA #0#29;
          |PINTA #0#30;
          |PINTA #0#31;
          |PINTA #0#33;
     |FINSI;
     |CIERRA #11;               || cierro el fichero agifa041
|FPRC;

|PROCESO SuAlbaran; |TIPO 0;
     aAlfa = #0#54; |QBF aAlfa;
     |SI aAlfa = ""; |ACABA; |FINSI;

     aAlfa = #0#54;
     aSerie = #0#50;
     aProv = #0#3;
     nNume = #0#0;
     mensa = "";
     |SALVA_FICHA #0;
     |SELECT #4#0;
     |LEE 000#0];
     |PARA; |SI FSalida = 0; |Y #0#3 = aProv; |Y #0#54 = aAlfa; |HACIENDO;
          |SI #0#50 ! aSerie; |O nNume ! #0#0;
               aAlfa = ("000000" + #0#0) % -106;
               mensa = "0000Codigo existente en Albaran:" + #0#50 + "/" + aAlfa;
               |SAL;
          |FINSI;
          |LEE 000#0s;
     |SIGUE;
     |SELECT #1#0;
     |REPON_FICHA #0;
     |SI mensa ! "";
          |MENAV mensa;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo212_9; |TIPO 9;
     |HAZ EsAlbero;
     |SI FSalida = 0;
          |HAZ AlbeAlbCom9;
     |FINSI;

     |SI nBtnItemDocAlc ! -1; |FIN_CONTROL_WINDOWS nBtnItemDocAlc; |FINSI;
|FPRC;

|PROCESO Pago212_0; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |HAZ EsAlbero;
     |SI FSalida = 0; |Y nModo = 1;
          |HAZ AlbeAlbFec;
          |HAZ AlbeAlbPinta;
     |FINSI;
     |HAZ EsConstru;
     |SI FSalida = 0;
          ||컴컴컴컴컴컴컴횮ontrol Cierre
          efFecha = #0#1;
          |HAZ EstaCerrado;
          |SI FSalida = 0;
               |ERROR; |ACABA;
          |FINSI;
          ||컴컴컴컴컴컴컴컴컴컴컴컴컴컴
     |FINSI;
|FPRC;

|PROCESO EtiquePCEG;
     modo = (FEntrada / 100) ! 0;
     |SI modo = 1; |O modo = 2;
          |GRABA 021#0a;
          |LIBERA #0;
     |FINSI;
     eaSerie = #0#50;
     enDocu = #0#0;
     eaTipo = "AC";
     eaPregunta = "S";
     |CIERRAT #0;

     |SUB_EJECUTA_NP "pcegz019";

     |ABRET #0;
     #0#50 = eaSerie;
     #0#0  = enDocu;
     |SI modo = 4;
          |LEE 001#0=;
     |SINO;
          |LEE 101#0=;
     |FINSI;
|FPRC;

|PROCESO Tipo212_20; |TIPO 20;
     |SI FSalida ! "OPCION";
          |SELECT #1#0;
          |LEE 000#0=;
          |SI FSalida ! 0;
               |MENAV "0000Por favor, consulte el registro correcto antes de imprimir.";
               |ACABA;
          |SINO;
               |PINDA #0#0;
          |FINSI;
     |FINSI;
     |HAZ T212_20;
|FPRC;

|PROCESO T212_20;
     aSalida = FSalida;
     |HAZ EsPCEGroup;
     nSwPCEGroup = FSalida;

     |SI aSalida = "OPCION";
          FSalida = "Imprimir";
          |SI nSwPCEGroup = 0; FSalida = "Opciones"; |FINSI;
          |ACABA;
     |FINSI;
     |SELECT #1#0;
     |LEE 000#0=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |SI nSwPCEGroup = 0;
          |HAZ OpcionePCEG;
          |ACABA;
     |FINSI;

     ||------------El proceso estandar

     |HAZ imprealc;
|FPRC;

|PROCESO OpcionePCEG;
     |MENU aMenuPCEG;
     |SI FSalida [ 0; |ACABA; |FINSI;
     |SI FSalida = 1;
          |HAZ imprealc;
     |FINSI;
     |SI FSalida = 2;
          eaSerie = #3#27;
          enCodigo = #3#0;
          enLinea = -1;
          |SUB_EJECUTA_NP "pcegz023;ESREPARA";
          |SI enLinea ! 0;         ||Si es 0 es de reparacion
               |HAZ EtiquePCEG;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CreaFactura;
     |GRABA 020#0a;
     |CIERRAT #0;
     aAlfa = #0#50;  |QBF aAlfa;
     qSerie = #0#50;
     qSerieF = "Z" + aAlfa;
     qAlbar = #0#0;
     qFecha = #0#1;
     qAbono = #0#42;
     enContaFac = #0#0;
     |PUSHV 0101 2480;
     |SUB_EJECUTA_NP "agifa229";
     |ABRET #0;
     |LEE 101#0=;
     #0#38 = "N";        || SE DEJA COMO NO FACTURADO !!!
     #0#51 = "";
     #0#39 = 0;
     |GRABA 020#0a;
|FPRC;

|PROCESO PalmHotelCtrl;
     |ABRE #79;
     #79#66 = "Z" + #0#50;
     #79#0 = #0#0;
     |LEE 101#79=;
     |SI FSalida = 0;
          |SI #79#48 = "S";
               |CONFI "0000N풞lbaran enlazado a contabilidad, desea continuar?";
               |SI FSalida ! 0; nSwFlag = 1; |FINSI;
          |FINSI;
          |SI nSwFlag = 0;
               |HAZ BorraAsiento;
               |SI nSw < 0;
                    nSwFlag = 1;
               |SINO;
                    |HAZ BorraFactura;
                    #0#38 = "N"; |PINTA #0#38;
               |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #79;
|FPRC;


|PROCESO BorraLinFac;
     |BORRA 020#72a;
|FPRC;

|PROCESO BorraFactura;
     |BUCLELIN 0BorraLinFac#79;
     |BORRA 020#79a;
|FPRC;

|PROCESO BorraAsiento;
   aAlfa1 = #agifa079#1; |QBF aAlfa1; aAlfa1 = aAlfa1 % -104; nSw = 0;
   |ABRE #agifa722;
   #agifa722#0  = "FC";
   #agifa722#1  = #agifa079#66;
   #agifa722#2  = #agifa079#0;
   #agifa722#3  = "";
   #agifa722#11 = 1;
   |LEE 000#agifa722.];

   FEstado = FSalida; aAlfa  = #agifa722#1; aAlfa = aAlfa % 0105;
   nCalc = #agifa722#2; FSalida = FEstado;

   |PARA; |SI FSalida = 0; |Y #agifa722#0 = "FC"; |Y aAlfa = #agifa079#66;
    |Y nCalc = #agifa079#0; |HACIENDO;
      aAlfa = #agifa722#3; |QBF aAlfa;
      |SI aAlfa = aAlfa1; nSw = 1; |SAL; |FINSI;
      |LEE 000#agifa722.s;
      FEstado = FSalida; aAlfa  = #agifa722#1; aAlfa = aAlfa % 0105; nCalc = #agifa722#2; FSalida = FEstado;
   |SIGUE;

   |SI nSw = 1;
      aAlfa = "ac" + Usuario; |NOME_DAT #agifa716#aAlfa#;
      |DELINDEX #agifa716; |ABRE #agifa716;

      |ABRE #agifa704;
      #agifa704#0 = #agifa722#10;
      |LEE 101#agifa704.=;
      |SI FSalida = 0;
         aFichAnt = #agifa704#16; nCmpAnt = #agifa704#17;
         |SI #agifa704#8 = "S";
            |DDEFECTO #agifa716;
            #agifa716#15 = #agifa722#10;  #agifa716#0  = 1;
            #agifa716#13 = "agifa080";    #agifa716#14 = 28;
            #agifa716#11 = "A";           #agifa716#12 = 5;
            #agifa716#3  = #agifa079#66;  #agifa716#4  = #agifa079#66;
            |GRABA 020#agifa716.n;

            |DDEFECTO #agifa716;
            #agifa716#15 = #agifa722#10;  #agifa716#0  = 2;
            #agifa716#13 = "agifa080";    #agifa716#14 = 25;
            #agifa716#11 = "N";           #agifa716#12 = 6;
            #agifa716#5  = #agifa079#0;   #agifa716#6  = #agifa079#0;
            |GRABA 020#agifa716.n;
            |LIBERA #agifa716; |CIERRA #agifa716;

            #agifa704#16 = "agifa080";    #agifa704#17 = 25;
            |GRABA 020#agifa704.a;
         |SINO;
            |DDEFECTO #agifa716;
            #agifa716#15 = #agifa722#10;  #agifa716#0  = 1;
            #agifa716#13 = "agifa079";    #agifa716#14 = 66;
            #agifa716#11 = "A";           #agifa716#12 = 5;
            #agifa716#3  = #agifa079#66;  #agifa716#4  = #agifa079#66;
            |GRABA 020#agifa716.n;

            |DDEFECTO #agifa716;
            #agifa716#15 = #agifa722#10;  #agifa716#0  = 2;
            #agifa716#13 = "agifa079";    #agifa716#14 = 0;
            #agifa716#11 = "N";           #agifa716#12 = 6;
            #agifa716#5  = #agifa079#0;   #agifa716#6  = #agifa079#0;
            |GRABA 020#agifa716.n;
            |LIBERA #agifa716; |CIERRA #agifa716;
            #agifa704#16 = "agifa079";    #agifa704#17 = 0;
            |GRABA 020#agifa704.a;
         |FINSI;
      |FINSI;
      |LIBERA #agifa704; |CIERRA #agifa704;

      aSerie = #agifa079#66; nContador = #agifa079#0;
      |LIBERA #agifa079; |CIERRAT #agifa079;

      Comodin1 = #agifa079#1; |QBF Comodin1; Comodin1 = Comodin1 % -104;
      Comodin = #agifa722#10; |QBF Comodin;
      Comodin = "agifa711.tab;" + Comodin + ",B*" + Comodin1;
      |SUB_EJECUTA_NP Comodin;
      |DELINDEX #agifa716;

      |ABRE #agifa704;
      #agifa704#0 = #agifa722#10;
      |LEE 101#agifa704.=;
      |SI FSalida = 0;
         #agifa704#16 = aFichAnt; #agifa704#17 = nCmpAnt;
         |GRABA 020#agifa704.a;
      |FINSI;
      |LIBERA #agifa704; |CIERRA #agifa704;

      |ABRET #agifa079;
      #agifa079#66 = aSerie; #agifa079#0 = nContador;
      |LEE 121#agifa079.=;

      |SI PRMNT_enError = 0;
          ||  #agifa079#48 = "N";   |PINTA #0#48;
          ||  |GRABA 020#agifa079.a;
          |BORRA 020#agifa722.a;
      |FINSI;
   |FINSI;

   nSw = PRMNT_enError;
   |CIERRA #agifa722;

|FPRC;

|PROCESO LeeSerie77; |TIPO 0;
     |ABRE #40;
     #40#26 = #0#50;
     |LEE 000#40=;
     |SI FSalida = 0; |Y #40#37 = "S";
          |MENAV "0000La serie es interna, no esta permitido...";
          |ERROR;
     |FINSI;
     |CIERRA #40;
|FPRC;

|PROCESO Curso77_7; |TIPO 7;
     aAlfa = "0000Campo informativo";

     |HAZ EsInmMarro;
     |SI FSalida = 0;
          aAlfa = "0000Matricula           <F11> Consulta";
     |FINSI;

     |MENSA aAlfa;
|FPRC;

|PROCESO Curso77_0; |TIPO 0;
     |HAZ EsInmMarro;
     |SI FSalida = 0;
          |DDEF aAlfa;
          aAlfa = aAlfa + "inmu0001";
          |CARGA_DEF aAlfa, referen@;
          |SI FSalida = 0;
               |ABRE #referen@;
               #referen@#0 = #0#71;
               |LEE 000#referen@.=;
               |SI FSalida ! 0;
                    eaAlfa = #0#71;
                    |SUB_EJECUTA_NP "inmu0001;alta";
                    |SI enError ! 0; |ERROR; |FINSI;
               |FINSI;
               |CIERRA #referen@;
               |DESCARGA_DEF #referen@;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Curso77_66; |TIPO 66;
     |HAZ EsInmMarro;
     |SI FSalida = 0;
          |DDEF aAlfa;
          aAlfa = aAlfa + "inmu0001";
          |CARGA_DEF aAlfa, referen@;
          |SI FSalida = 0;
               |ABRE #referen@;
               #referen@#0 = #0#71;
               |LEE 000#referen@.];
               |CONSULTA_CLAVE #1#referen@;
               |SI FSalida = 0;
                    #0#71 = #referen@#0; |PINTA #0#71;
               |SINO;
                    |ERROR;
               |FINSI;
               |CIERRA #referen@;
               |DESCARGA_DEF #referen@;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO IVA212_66; |TIPO 66;
     enTiva = #0Campo;
     efFiva = #0#1;
     |HAZ ConsuIVA;
     |SI eaDiva = "";
          |ERROR;
     |SINO;
          #0Campo = enTiva; |PINTA #0Campo;
     |FINSI;
|FPRC;

|PROCESO IVA212_0; |TIPO 0;
     enTiva = #0Campo;
     efFiva = #0#1;
     |HAZ SacaIVA;
     |SI eaDiva = "";
          |MENAV "0000No existe tipo iva para esta fecha...";
          |ERROR; |ACABA;
     |SINO;
          |HAZ caltotala;
     |FINSI;
|FPRC;

|PROCESO PinDesanch212;
     |DDEF aAlfa;
     aAlfa = aAlfa + "desan016";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida = 0;
          |DDEFECTO #referen@;
          |ABRE #referen@;
          #referen@#0 = "A";
          #referen@#1 = #0#50;
          #referen@#2 = #0#0;
          |LEE 000#referen@.=;
          |CIERRA #referen@;
          |PINTA 1403, "%Dto2:   %Dto3:  ";
          |ATRI I;
          |PINTA 1410, #referen@#3;
          |PINTA 1419, #referen@#4;
          |ATRI 0;
          |DESCARGA_DEF #referen@;
     |FINSI;
|FPRC;

|PROCESO BtnItemDocAlc;
     aAlfa = "tagz0025;AC" + #0#50 + #0#0;
     |SUB_EJECUTA_NP aAlfa;
|FPRC;
