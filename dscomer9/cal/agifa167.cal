|FICHEROS;
    agifa166 #0;  ||  Pantalla de pase.
    agifa143 #1;  ||  Temporal pase Facturas Proforma.
    agifa146 #2;  ||  Cabec. Facturas Proforma.
    agifa147 #3;  ||  Lineas Facturas Proforma.
    agifa084 #4;  ||  Cabec. Facturas Contado.
    agifa069 #5;  ||  Lineas Facturas Contado.
    agifa024 #7;  ||  Clientes.
    agifa019 #8;  ||  Articulos.
    agifa025 #9;  ||  Comisionistas.
    agifa017 #10; ||  Stock de almacenes.
    agifa142 #41; ||  Parametros Generales
    agifa321 #35;  || Parametros de Divisas
    agifa328 #36;  || Clientes/Divisas
    agifa329 #37;  || Lineas de Clientes/Divisas
    agifa324 #38;  || Divisas

    dsm00005 #905;
    dsm00048 #948;
    dsm00050 #950;
    dsm00053 #53;
    tagm0056@;

    dsm00247 #141;
    dsm00248 #143; ||Auditoria
|FIN;


|VARIABLES;
     nSwGestral = 0;
     nSwTagloma = 0;
     &eaTipAdj = "";
     &nDispoKit = 0;
     nHayStock = 0;
     nLinV           = 0;
     &eaTag          = "";
     &aRieBloqu      = "";
     &aBloquead      = "";
     &nMasRies       = 0;

     &enSwMenu       = 0;
     aFichero        = "";
     &nDeci_Base     = 0;   || Decimales en Totales de la moneda base
     &nDeci_PreBase  = 0;   || Decimales en el Precio en moneda base
     &nDeci_BaseMx   = 0;   || Decimales en Totales de la moneda base (Maxima precision)
     &nDeci_PreBaseMx= 0;   || Decimales en el Precio en moneda base (Maxima precision)
     &nDeci_Div      = 0;   || Decimales en Totales de la divisa
     &nDeci_PreDiv   = 0;   || Decimales en el Precio en divisa
     &nDeci_TotVis   = 0;   || Decimales en Totales visibles (el de la moneda de trabajo)
     &nDeci_TotNoVis = 0;  || Decimales en Totales no visibles (el de la otra moneda)
     &nDeci_PreVis  = 0;   || Decimales en el Precio visual. (lineas)
     &nDeci_ImpVis  = 0;   || Decimales en el Importe visual. (lineas)
     &nDeci_BaseMxF  = 0;   || Decimales en Totales de la moneda base (Maxima precision)
     &nDeci_PreBasMxF= 0;   || Decimales en el Precio en moneda base (Maxima precision)
     &nDeci_DivF     = 0;   || Decimales en Totales de la divisa
     &nDeci_PreDivF  = 0;   || Decimales en el Precio en divisa
     &nDeci_TotVisF  = 0;   || Decimales en Totales visibles (el de la moneda de trabajo)
     &nDeci_TotNoVisF= 0;  || Decimales en Totales no visibles (el de la otra moneda)
     &nDeci_PreVisF  = 0;   || Decimales en el Precio visual. (lineas)
     &nDeci_ImpVisF  = 0;   || Decimales en el Importe visual. (lineas)
     &decim_divisa = 0;  || Decimales de la divisa
     &precio_divisa = 0; || Decimales del precio en divisa
     &decim_base = 0;    || Decimales de la moneda base
     &precio_base = 0;   || Decimales del precio en moneda base
     nerror = 0;
     nMargen = 0;
     nImpo = 0;
     nDto = 0;
     asto = 0;
     contador = "";
     aAlfa = "";
     fmes = "";
     mes  = 0;
     campo = 0;
     imneto = 0;
     retencion = 0;
     margen = 0;
     nConAu = 0;
     &eaNomCliente = "";
     &eaCodCliente = "";
|FIN;

|INCLUYE i_varart;

|PROCESO Riesgo167;
     nMasRies = 0;
     eaCodCliente = #2#3;
     eaNomCliente = #2#4;
     eaSerie = #1#7; eaTag = "PROFO";  |HAZ SacaRiesgo;
     |SI #agifa142#6 = "S"; |Y eaExisteCliRie = "S";
          eaDocu = "C";
          eaTipo = "A";
          enImpoDoc = #2#41;
          eaMuestra = "S";
          |HAZ ControlRiesgo;
          |SI enErrRie ! 0;
               |ACABA;
          |FINSI;
          |SI enAudito = 0;
               |ABRE #143;
               |LEE 000#143u;
               |SI FSalida = 0;
                    nConAu = #143#0 + 1;
               |SINO;
                    nConAu = 1;
               |FINSI;
               |DDEFECTO #143;
               #143#0 = nConAu;
               #143#12 = "PROFORMA";
               #143#1 = #2#48;
               #143#2 = #2#0;
               #143#3 = #2#1;
               #143#4 = #2#3;
               #143#5 = #2#4;
               #143#6 = Usuario % 810;
               #143#7 = #141#0;
               #143#8 = #141#1;
               #143#9 = #141#2;
               #143#10 = #141#3;
               #143#11 = #141#4;
               |GRABA 020#143n;
               |CIERRA #143;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO TagPosFormados;
     |DDEF aAlfa;
     aAlfa = aAlfa + "tagm0056";
     |CARGA_DEF aAlfa, tagm0056@;
     |SI FSalida = 0;
          |ABRE #tagm0056@;

          #tagm0056@#0 = "PR";
          #tagm0056@#1 = #3#20;
          #tagm0056@#2 = #3#0;
          #tagm0056@#3 = #3#1;
          |LEE 000#tagm0056@.=;
          |SI FSalida = 0;
               #tagm0056@#0 = "FD";
               #tagm0056@#1 = #5#20;
               #tagm0056@#2 = #5#0;
               #tagm0056@#3 = #5#1;
               |GRABA 020#tagm0056@.n;
          |FINSI;

          |CIERRA #tagm0056@;
          |DESCARGA_DEF #tagm0056@;
     |FINSI;
|FPRC;

|PROCESO LineasKit;
     #948#0 = #5#20;          || serie factura
     #948#1 = #5#0;           || nro. factura
     #948#2 = #5#1;           || linea factura
     #948#6 = #950#6;         || linea kit
     #948#3 = #950#3;         || articulo
     #948#4 = #950#4;         || almacen
     #948#5 = #950#5;         || unidades
     #948#7 = #950#7;         || observaciones
     #948#8 = #950#8;         || precio coste medio
     #948#9 = #950#9;
     #948#10 = #950#10;
     #948#11 = #950#11;
     #948#12 = #950#12;
     |GRABA 020#948n;
|FPRC;

|DEFBUCLE LineasKit;
     #950#1;
     ;
     #3#20, #3#0, #3#1, 001;
     #3#20, #3#0, #3#1, 999;
     ;
     NULL, LineasKit;
|FIN;

|PROCESO graba_cabfac;
         |PARA campo = 0; |SI campo < 60; |HACIENDO campo = campo + 1;
               #4campo = #2campo;
         |SIGUE;

         #7#0 = #4#3;
         |LEE 040#7=;
         |SI FSalida ! 0;  |DDEFECTO #7;  |FINSI;

         #4#48 = #1#7;      ||  Serie;
         #4#0  = contador;  ||  Numero de pedido.
         #4#1 = #1#5;
         #4#10 = "N";
         #4#60 = #7#15;
         #4#64 = #2#81;
         #4#65 = #2#65;
         #4#66 = #2#66;
         #4#67 = #2#67;
         #4#68 = #2#68;
         #4#69 = #2#69;
         #4#102 = #2#85;
         |SI nSwTagloma = 0;
               eaAlfa = #4#48;
               |HAZ TagloDefFP;
               |SI eaAlfa ! ""; #4#8 = eaAlfa; |FINSI;
               |SI eaQuitaDto = "S";
                    #4#5 = 0;
                    #4#32 = 0;
                    #4#7 = 0;
               |FINSI;
         |FINSI;
         |SI nSwGestral = 0;
               eaAlfa = #4#48;
               |HAZ GestrDefFP;
               |SI eaAlfa ! ""; #4#8 = eaAlfa; |FINSI;
               |SI eaQuitaDto = "S";
                    #4#5 = 0;
                    #4#32 = 0;
                    #4#7 = 0;
               |FINSI;
         |FINSI;
         |GRABA 020#4b;
|FPRC;

|PROCESO GrabaCompoN;
     |SALVA_DATOS #905;

     #905#0 = "FC";
     #905#1 = #5#20;
     #905#2 = #5#0;
     #905#3 = #5#1;
     |GRABA 020#905n;
     |LIBERA #905;

     eaTipo        = "FC";
     eaSerie       = #5#20;
     enCodigo      = #5#0;
     enLinea       = #5#1;
     efFecha       = #4#1;
     eaArticulo    = #5#2;
     enAlmacen     = #5#3;
     eaComponente  = #905#4;
     enUnidades    = #5#5;
     enTBruto      = #5#7;
     eaAbono       = "N";
     enTarifa      = #7#39;
     enCamBas      = #4#66;
     eaMoneda      = #4#67;
     enCamDiv      = #4#69;

     |SUB_EJECUTA_NP "dsz00002;FCC";

     |REPON_DATOS #905;
     |LEE 040#905=;
|FPRC;

|DEFBUCLE dsm00005N;
     #905#1;
     ;
     "FP", #3#20, #3#0, nLinV, "      ";
     "FP", #3#20, #3#0, nLinV, "zzzzzz";
     ;
     NULL, GrabaCompoN;
|FIN;

|PROCESO Actualizacion;
     |SI #5#9 > 0;
         nerror = 1;
     |SINO;
         nerror = -1;
         #5#9 = -#5#9;
     |FINSI;

     imneto = ((((#5#9 < #4#5) < #4#32) < #4#7)) * nerror;
     #5#9   = #5#9 * nerror;

     eaArticulo = #3#2;
     |HAZ EsKit;
     |SI FSalida = 0;
           |ABRE #948;
           |BUCLE LineasKit;
           |CIERRA #948;
     |FINSI;
     ||-------------------Actualiza Historico, Articulo y Almacen
     enForma = 1;
     |HAZ AcumulaFC;

     #5#14      = enCoste;
     #4#37      = #4#37 + (imneto - enCoste);
     ||------------------------------Importe Articulo
     fmes = #4#1 % A402;
     mes = fmes - 1;
     mes = mes * 5;
     mes = mes + 35;
     #8#0 = #5#2;
     |LEE 101#8=;
     |SI FSalida = 0;
          |SI #8#194 = 2;
               nImpo = ((((#5#30 * #5#6) < #5#8) < #5#21) < #4#5) < #4#32;
          |SINO;
               nImpo = ((((#5#5 * #5#6) < #5#8) < #5#21) < #4#5) < #4#32;
          |FINSI;
          |SI #41#115 = "S";
               nImpo = nImpo < #4#7;
          |FINSI;
          nMargen = nImpo - #5#14;
          #8mes = #8mes + nImpo;
          #8#95 = #8#95 + nImpo;
          mes = mes + 1;
          #8mes = #8mes + nMargen;
          #8#96 = #8#96 + nMargen;
          |GRABA 020#8a;
          |LIBERA #8;

          efFecha = #4#1;
          eaArticulo = #5#2;
          enImpVta = nImpo;
          enImpMar = nMargen;
          |HAZ AcumulaArt;
     |FINSI;
|FPRC;

|PROCESO dsm00053;
     #53#0 = "F";
     #53#1 = #5#20;
     #53#2 = #5#0;
     #53#3 = #5#1;
     |GRABA 020#53n;
|FPRC;

|DEFBUCLE dsm0053;
     #53#1;
     ;
     "X", #3#20, #3#0, #3#1, 0001;
     "X", #3#20, #3#0, #3#1, 9999;
     ;
     NULL, dsm00053;
|FIN;

|PROCESO graba_linfac;
         #4#65 = #2#65;
         #4#66 = #2#66;
         #4#67 = #2#67;
         #4#68 = #2#68;
         #4#69 = #2#69;

         |HAZ LeeMonBase;
         |HAZ Param_Divisas;
         |DDEFECTO #5;
         |PARA campo = 0; |SI campo < 22; |HACIENDO campo = campo + 1;
               #5campo = #3campo;
         |SIGUE;
         #5#22 = #3#22;
         #5#23 = #3#23;

         #5#20 = #4#48;     ||  Serie.
         #5#0  = contador;

         #5#26 = #3#24;  || PRECIO EN DIVISA
         #5#27 = #3#25;  || IMPORTE EN DIVISA

         #5#30 = #3#28;
         #5#31 = #3#29;
         #5#32 = #3#30;
         #5#33 = #3#31;
         #5#34 = #3#32;
         #5#35 = #3#33;
         #5#43 = #3#34;
         #5#44 = #3#35;
         #5#45 = #3#36;
         |HAZ decim1;
         |HAZ visual_importe;

         |GRABA 020#5n;

         |HAZ Actualizacion;

         |BUCLE dsm0053;
         nLinV = #3#1;
         |BUCLE dsm00005N;

         |HAZ EsOrts;
         |SI FSalida = 0;
               eaSeriePedido = #agifa147#20;
               enCodigPedido = #agifa147#0;
               enLineaPedido = #agifa147#1;
               eaSerie = #agifa069#20;
               enCodigo = #agifa069#0;
               enLinea = #agifa069#1;
               |SUB_EJECUTA_NP "dsz99905;PROFAC";
         |FINSI;

         |HAZ EsAlbadis;||*
         |SI FSalida = 0;
               eaSerie = #3#20;
               enCodigo = #3#0;
               enLinea = #3#1;
               eaSeriePedido = #5#20;
               enCodigPedido = #5#0;
               enLineaPedido = #5#1;
               |HAZ BseProFac;
         |FINSI;

         |SI nSwTagloma = 0;
             eaTipAdj = "F";
             |HAZ TagAdjUniFami;
             |HAZ TagPosFormados;
         |FINSI;
|FPRC;

|PROCESO lee_contador;
     #4#48 = #1#7;       ||Serie
     enSwMenu = 1;
     |HAZ ContaFD7;
     enSwMenu = 0;
     contador = #4#0;
|FPRC;

|PROCESO LeeLineaFac;
     |HAZ CalCabAgifa084;
|FPRC;

|DEFBUCLE agifa069;
     #5#1;
     ;
     #4#48, #4#0, 001;
     #4#48, #4#0, 999;
     ;
     NULL, LeeLineaFac;
|FIN;

|| ------------------------ PROCESOS DE ACTUALIZACION -----------------------

|PROCESO actua_cliente;
         |DDEFECTO #7;
         #7#0 = #4#3;
         |LEE 121#7=;
         |SI FSalida = 0;
             fmes = #4#1 % A402;
             mes = fmes - 1;
             mes = mes * 4;
             mes = mes + 54;
             imneto = #4#15 - #4#25;
             |SI #41#115 = "N";
                  nImpo = (imneto * 100) / (100 - #4#7);
                  nDto = #4#25 + (imneto - nImpo);
             |SINO;
                  nImpo = imneto;
                  nDto = #4#25;
             |FINSI;
             #7mes = #7mes + nImpo;
             mes = mes + 1;
             #7mes = #7mes + nDto;
             mes = mes + 1;
             #7mes = #7mes + #4#37;
             #7#102 = #7#102 + nImpo;
             #7#103 = #7#103 + nDto;
             #7#104 = #7#104 + #4#37;
             #7#45 = #4#1;
             #7#46 = imneto;
             imneto = imneto + #4#24;
             #7#110 = #7#110 + imneto;
             |GRABA 020#7a;

             enImpCliCom = nImpo;
             enImpCliDto = nDto;
             enImpCliMar = #4#37;
             enImpCliFac = imneto;
             eaCliente = #4#3;
             efFecha = #4#1;
             |HAZ AcumulaCli;
         |FINSI;
         |LIBERA #7;
|FPRC;

|PROCESO actua_comi;
         |DDEFECTO #9;
         #9#0 = #4#6;
         |LEE 121#9=;
         |SI FSalida = 0;
             imneto = #4#15 - #4#25;
             fmes = #4#1 % A402;
             mes = fmes - 1;
             mes = mes * 2;
             mes = mes + 11;
             #9mes = #9mes + #4#26;
             retencion = #9mes % #9#39;
             mes = mes + 1;
             #9mes = #9mes + imneto;
             #9#35 = #9#35 + #4#26;
             #9#36 = #9#36 + imneto;
             mes = fmes - 1;
             mes = mes + 40;
             #9mes = retencion;
             retencion = #9#35 % #9#39;
             #9#52 = retencion;
             |GRABA 020#9a;


          enImpRepComi = #4#26;
          enImpRepVta = imneto;
          enImpRepRete = retencion;
          eaRepre = #4#6;
          efFecha = #4#1;
          |HAZ AcumulaRep;

         |FINSI;
         |LIBERA #9;
|FPRC;

|PROCESO LeeMonBase; ||Lee los parametros de la moneda base
|ABRE #agifa321;
|ABRE #agifa324;
|LEE 010#agifa321.p;   ||Leo parametros de divisa
#agifa324#0 = #agifa321#9;
|LEE 010#agifa324.=;         ||Leo la ficha de la moneda base en Divisas
#agifa084#69 = #agifa324#2;  ||Asigno el cambio de la moneda base con el euro
decim_base = #agifa324#7;    ||Asigno los decimales de la moneda base
precio_base = #agifa324#9;   ||Asigno los decimales del precio en la mon. base
|CIERRA #agifa324;
|CIERRA #agifa321;
|FPRC;

|PROCESO Param_Divisas; |TIPO 0;
     |ABRE #agifa324;
     #agifa324#0 = #agifa084#65;
     |LEE 001#agifa324.=;   || Leo la divisa
     |CIERRA #agifa324;

     decim_divisa = #agifa324#7;  || Decimales de la divisa
     precio_divisa = #agifa324#9;  || Decimales en el precio de la divisa
     nDeci_Div = decim_divisa;   || Decimales de la divisa
     nDeci_PreDiv = precio_divisa;  || Decimales en el precio de la divisa
     nDeci_DivF = decim_divisa;   || Decimales de la divisa
     nDeci_PreDivF = precio_divisa;  || Decimales en el precio de la divisa
     |SI #agifa084#67 = "B";   || Si trabajo con la moneda base ...
          nDeci_Base     = decim_base;
          nDeci_PreBase  = precio_base;
          nDeci_PreVis   = precio_divisa;
          nDeci_ImpVis   = decim_divisa;
          nDeci_BaseMx  = decim_base;
          nDeci_PreBaseMx = precio_base;
          nDeci_TotVis   = decim_base;
          nDeci_TotNoVis = decim_divisa;
          nDeci_PreVisF  = precio_divisa;
          nDeci_ImpVisF  = decim_divisa;
          nDeci_BaseMxF  = decim_base;
          nDeci_PreBasMxF= precio_base;
          nDeci_TotVisF  = decim_base;
          nDeci_TotNoVisF= decim_divisa;
     |SINO;     || Si trabajo con la divisa ...
          nDeci_Base     = decim_base; || Max. precision en los decimales de la moneda base
          nDeci_PreBase  = precio_base;  || Max. precision en los dec. del precio de la moneda base
          nDeci_PreVis   = precio_base;
          nDeci_ImpVis   = decim_base;
         |SI #agifa084#66 = 1;
              nDeci_BaseMx    = decim_base;    || sin triangulacion
              nDeci_PreBaseMx = precio_base;
         |SINO;
              nDeci_BaseMx    = #agifa321#10;  || con triangulacion
              nDeci_PreBaseMx = #agifa321#11;
         |FINSI;
          nDeci_TotVis   = decim_divisa;
          nDeci_TotNoVis = decim_base;
          nDeci_PreVisF   = precio_base;
          nDeci_ImpVisF   = decim_base;
         |SI #agifa084#66 = 1;
              nDeci_BaseMxF   = decim_base;    || sin triangulacion
              nDeci_PreBasMxF = precio_base;
         |SINO;
              nDeci_BaseMxF   = #agifa321#10;  || con triangulacion
              nDeci_PreBasMxF = #agifa321#11;
         |FINSI;
          nDeci_TotVisF   = decim_divisa;
          nDeci_TotNoVisF = decim_base;
     |FINSI;
     |HAZ Deci_IVA;
|FPRC;

|PROCESO decim1;
|SI #4#67 = "D";  || Si la moneda de trabajo es la divisa ...
  #5#6 = #5#26 / #4#66 * #4#69;  || ... recalculo en funcion de la divisa
|SINO;
  #5#26 = #5#6 / #4#69 * #4#66; || .. recalculo en funcion de la moneda base
|FINSI;
|HAZ visual_precio; || Visualiza el precio
|FPRC;

|PROCESO visual_precio; || Pinta el precio visual.
|SI #4#67 = "D";  || Si la moneda de trabajo es la divisa ...
  #5#28 = #5#6;   || ... el campo visualiz. es el precio en moneda base
|SINO;            || Si la moneda de trabajo es la moneda base ...
  #5#28 = #5#26;  || ... el campo visual. es el precio en divisa
|FINSI;
|FPRC;

|PROCESO visual_importe;  || Pinta el importe visual
|SI #4#67 = "D";  || Si la moneda de trabajo es la divisa ...
  #5#29 = #5#9;   || el campo visualiz. es el importe en moneda base
|SINO;            || Si la moneda de trabajo es la moneda base ...
  #5#29 = #5#27;  || el campo visualiz. es el importe en divisa
|FINSI;
|FPRC;

|PROCESO MiraStock;
     eaArticulo = #3#2;
     |HAZ EsKit;
     |SI FSalida = 0;
          |HAZ DispoKit;
          |SI nDispoKit ! 0;
               aAlfa = "0000Supera Stock para KIT: " + #3#2;
               |MENAV aAlfa;
               nHayStock = 1;
          |FINSI;
     |SINO;
          |ABRE #10;
          #10#0 = #3#2;
          #10#1 = #3#3;
          |LEE 000#10=;
          |CIERRA #10;
          |SI #10#39 < #3#5;
               aAlfa = "0000Supera el Stock disponible: " + #3#2;
               |MENAV aAlfa;
               nHayStock = 1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO factura;
     nMasRies = 0;
     #4#3 = #1#1;
     #4#4 = #1#4;
     eaSerie = #1#7; eaTag = "FD";  |HAZ SacaRiesgo;
     |SI aRieBloqu = "S";
         |HAZ BloqCart;
         |ERROR; |ACABA;
     |FINSI;

     #0#0 = #1#2;
     #0#1 = #1#3;
     |DDEFECTO #2;
     #2#48 = #1#2;  ||  Serie.
     #2#0  = #1#3;  ||  Factura.
     |LEE 101#2=;
     |SI FSalida = 0;
          |HAZ Riesgo167;
          |SI enErrRie = 0;
               nHayStock = 0;
               |SI #agifa142#235 = "S";
                    |BUCLELIN 0 MiraStock #2;
                    |SI nHayStock = 1;
                         aAlfa = "0000Factura: " + #1#2 +"/"+#1#3+ ", no pasada.";
                         |MENAV aAlfa;
                    |FINSI;
               |FINSI;
               |SI nHayStock = 0;
                    |PARA FSalida = 0; |SI FSalida = 0; |HACIENDO;
                         |HAZ lee_contador;
                         #4#48 = #1#7;
                         #4#0 = contador;
                         |LEE 001#4=;     ||Cuando FSalida ! 0 la factura no existe
                    |SIGUE;
                    #0#2 = #1#7;
                    #0#3 = contador;
                    |DDEFECTO #4;
                    |HAZ graba_cabfac;
                    |BUCLELIN 0graba_linfac#2;
                    |HAZ actua_cliente;
                    |HAZ actua_comi;
                    ||#2#60 = #4#48;  ||  Serie.
                    #4#60 = #2#86;
                    #2#61 = ("000000" + #4#0) % -106;   ||  Factura.
                    #2#62 = "F";    ||  Tipo de documento asignado (Pedido).

                    |HAZ LimCabAgifa084;
                    |BUCLE agifa069;

                    |GRABA 020#4a;
                    |LIBERA #4;

                    |GRABA 020#2a;
                    |SI #41#47 = "S";  ||컴컴컴컴컴 Acumula en riesgos Si Crea Recibo
                           eaTag = "FC";  |HAZ Riesgos;
                    |FINSI;
                    |PINDA #0#0;
               |FINSI;
          |FINSI;
     |SINO;
         #0#2 = "NO";
         #0#3 = "EXISTE";
         |PINDA #0#0;
         |PAUSA;
     |FINSI;
     |LIBERA #2;
|FPRC;

|DEFBUCLE lin_tmp;
          #1#1;
          8;
          001,"*";
          999,"*";
          e;
          NULL,factura;
|FIN;

|PROGRAMA;
     |HAZ EsTagloma; nSwTagloma = FSalida;
     |HAZ EsGestral; nSwGestral = FSalida;

     |ABRE #41; |LEE 000#41p; |CIERRA #41;

     aFichero = PARAMETRO; |QBF aFichero;
     |NOME_DAT #1 aFichero;

     |PINPA #0#0;
     |PINTA 1446,"Factura Contado";
     |ABRE #2;
     |ABRE #4;
     |ABRE #5;
     |ABRE #7;
     |ABRE #8;
     |ABRE #9;
     |ABRE #10;
     |BUCLE lin_tmp;
     |CIERRA #2;
     |CIERRA #4;
     |CIERRA #5;
     |CIERRA #7;
     |CIERRA #8;
     |CIERRA #9;
     |CIERRA #10;
     |DELINDEX #1;
|FPRO;
