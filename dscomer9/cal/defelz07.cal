/*
|FICHEROS;
     defelz07 #0;
     dsm00003 #3;
     defelw07 #7;   ||Tmp
     defelm04 #4;
     defelm06 #6;
     agifa010 #10;
     agifa024 #24;
     agifa059 #59;
     agifa071 #71;
     agifa134 #134;
     tmp00000 #300;
     dsm00005 #905;
     dsm00071 #971;
     dsm00072 #972;
|FIN;

|INCLUYE i_varart;

|VARIABLES;
     &Tempo = "";
     aAlfa = "";
     nPrime = 0;
     i = 0;
     aCampo = "";
     aTalla = "";
     nDesde = 0;
     nHasta = 0;
     nCamTalla = 0;
     nLin = 0;
     aSurtido = "";
     nCaja = 0;
     nLinV = 0;
|FIN;

|PROCESO Surtido;
     |SI #71#12 ! 0;
          #6#0 = "P";
          #6#1 = #71#31;
          #6#2 = #71#12;
          #6#3 = #71#21;
     |SINO;
          #6#0 = "A";
          #6#1 = #71#29;
          #6#2 = #71#0;
          #6#3 = #71#1;
     |FINSI;
     |LEE 000#6=;
     |SI FSalida ! 0; |DDEFECTO #6; |FINSI;

     #4#0 = #6#5;
     |LEE 000#4=;
     |SI FSalida ! 0; |DDEFECTO #4; |FINSI;

     aSurtido = #6#5; |QBF aSurtido;
|FPRC;

|PROCESO BusLin;
     nLin = 0;
     |PARA i = nDesde; |SI i [ nHasta; |HACIENDO i = i + 1;
          aCampo = #0i; |QBF aCampo;
          |SI aCampo = aTalla; |SAL; |FINSI;
     |SIGUE;
     |SI i > nHasta;
          |PARA i = nDesde; |SI i [ nHasta; |HACIENDO i = i + 1;
               aCampo = #0i; |QBF aCampo;
               |SI aCampo = ""; |SAL |FINSI;
          |SIGUE;
     |FINSI;
     |SI i [ nHasta; nLin = 1; |FINSI;
|FPRC;

|PROCESO Tallas;
     aTalla = #905#4; |QBF aTalla;
     nDesde = 17; nHasta = 24; |HAZ BusLin;
     |SI nLin ! 0;
          nLin = 1;
     |SINO;
          nDesde = 25; nHasta = 32; |HAZ BusLin;
          |SI nLin ! 0;
               nLin = 2;
          |SINO;
               nDesde = 33; nHasta = 40; |HAZ BusLin;
               |SI nLin ! 0;
                    nLin = 3;
               |SINO;
                    nDesde = 41; nHasta = 48; |HAZ BusLin;
                    |SI nLin ! 0;
                         nLin = 4;
                    |SINO;
                         |MENAV "0000Error, exiten mas de 32 tallas diferentes en el pedido...";
                         |ACABA;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;

     #0i = aTalla;
     nCamTalla = i - ((nLin - 1) * 8);

     |DDEFECTO #7;
     #7#0 = #134#49;
     #7#1 = #134#0;
     #7#2 = #71#31;
     #7#3 = #71#12;
     #7#4 = #971#0;
     #7#5 = #972#0;
     #7#6 = nLin;
     |LEE 101#7=;
     |SI FSalida ! 0; |GRABA 020#7b; |FINSI;
     #7#7 = #71#2;
     #7#8 = #971#1;
     #7#9 = #972#1;
     #7#10 = #71#4;
     #7#11 = #134#2;
     #7#12 = #134#115;
     #7#13 = 0;               ||Cajas
     #7#14 = 0;               ||N§Caja

     aAlfa = #7#15; |QBF aAlfa;
     |SI aAlfa = "";
          #7#15 = aSurtido;
     |SINO;
          aAlfa = aAlfa - aSurtido;
          |SI FSalida = 0;
               aAlfa = #7#15; |QBF aAlfa;
               aAlfa = aAlfa + "/" + aSurtido;
               #7#15 = aAlfa;
          |FINSI;
     |FINSI;
     #7nCamTalla = #7nCamTalla + #905#6;
     #7#16 = #7#17 + #7#18 + #7#19 + #7#20 + #7#21 + #7#22 + #7#23 + #7#24;
     |GRABA 020#7a;
     |LIBERA #7;
|FPRC;

|DEFBUCLE Tallas;
     #905#1;
     ;
     "AV", #71#29, #71#0, nLinV, "      ";
     "AV", #71#29, #71#0, nLinV, "zzzzzz";
     ;
     NULL, Tallas;
|FIN;

|PROCESO LinAlb;
     eAlfa = #71#2; |SUB_EJECUTA_NP "dsz99990;V";
     #971#0 = eAlfa1;
     |LEE 000#971=;
     |SI FSalida ! 0; |DDEFECTO #971; |FINSI;
     #972#0 = eAlfa2;
     |LEE 000#972=;
     |SI FSalida ! 0; |DDEFECTO #972; |FINSI;

     |HAZ Surtido;

     nLinV = #71#1;
     |BUCLE Tallas;
|FPRC;

|DEFBUCLE Albaran;
     #10#1;
     ;
     #59#15, #59#2;
     #59#15, #59#2;
     ;
     NULL, NULL, LinAlb;
|FIN;

|PROCESO LinFactu;
     |BUCLE Albaran;
|FPRC;

|PROCESO Tmp;
     |DDEFECTO #24;
     #24#0 = #7#11;
     |LEE 000#24=;

     |DDEFECTO #3;
     #3#0 = #7#11;
     #3#1 = #7#12;
     |LEE 000#3=;

     #134#49 = #7#0;
     #134#0 = #7#1;
     |LEE 000#134=;

     #7#13 = 1;
     nCaja = nCaja + #7#13;
     #7#14 = nCaja;

     |PRINT;
|FPRC;

|DEFBUCLE Tmp;
     #7#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tmp;
|FIN;

|PROCESO Imprime;
     |LEE 000#7p;
     |SI FSalida ! 0; |ACABA; |FINSI;

     Impresora = "CrystalReport";
     |IMPRE -1;
     |SI FSalida = 0;
          |INFOR "defelz07";
          |SI FSalida = 0;
               |BUCLE Tmp;
               |PIEINF;
               |FININF;
          |SINO;
               |MENAV "0000Error en informe 'defelz07'";
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;

|PROCESO Factura;
     |DDEFECTO #24;
     #24#0 = #134#2;
     |LEE 000#24=;
     |SI #24#112 ! "S"; |ACABA; |FINSI;

     |SI PARAMETRO ! "";
          |SI nPrime = 0;
               |CONFI "0000N¿Imprimir paking list?";
               |SI FSalida ! 0; |ERROR10; |ACABA; |FINSI;
               nPrime = 1;
          |FINSI;
     |FINSI;

     |BUCLELIN 2 LinFactu #134;
|FPRC;

|DEFBUCLE Factura;
     #134#1;
     ;
     #0#0, #0#2;
     #0#1, #0#3;
     ;
     NULL, Factura;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |BUCLE Factura;
     |HAZ Imprime;
|FPRC;

|PROCESO Tmp300;
     #134#49 = #300#0;
     #134#0 = #300#1;
     |LEE 020#134=;
     |SI FSalida = 0;
          |HAZ Factura;
     |FINSI;
|FPRC;

|DEFBUCLE Tmp300;
     #300#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tmp300;
|FIN;

|PROGRAMA;
     |HAZ CreaTempo; |NOME_DAT #7 Tempo; |DELINDEX #7; |ABRE #7;
     |ABRE #3;
     |ABRE #4;
     |ABRE #6;
     |ABRE #24;
     |ABRE #134;
     |ABRE #971;
     |ABRE #972;
     |SI PARAMETRO = "";
          |MANTE #0;
     |SINO;
          |NOME_DAT #300 PARAMETRO;
          |ABRE #134;
          |BUCLE Tmp300;
          |CIERRA #134;
          |HAZ Imprime;
     |FINSI;
     |CIERRA #972;
     |CIERRA #971;
     |CIERRA #134;
     |CIERRA #24;
     |CIERRA #6;
     |CIERRA #4;
     |CIERRA #3;
     |HAZ BoraTempo; |CIERRA #7; |DELINDEX #7;
|FPRO;
*/
