|FICHEROS;
     malpt001 #0; || Este
     malpt002 #1; || Tmp
     malpe104 #2; || Fecha de Servicios
     agifa019 #3; || Articulos
     malpe004 #4;
     malpe087 #87;
|FIN;

|VARIABLES;
     fichero = "";
     i       = 0;
     &acepta = 0;
     fFecha = @;
     nFecha = 0;
     aAlfa = "";
|FIN;

|PROCESO Carga;
     |DDEFECTO #1;
     |SI #3#0 ! #2#8;              || lee articulos
          |ABRE #3;
          #3#0 = #2#8;
          |LEE 021#3=;
          |SI FSalida ! 0; |DDEFECTO #3; |FINSI;
          |CIERRA #3;
     |FINSI;
     |PARA i = 0; |SI i [ 10; |HACIENDO i = i + 1;
          #1i = #2i;
     |SIGUE;
     #1#11 = #1#5 / #3#4; || Palets
     #1#13 = #3#4;
     #1#14 = #2#12;
     |GRABA 021#1n;
|FPRC;

|DEFBUCLE BCarga;
     #2#1;
     ;
     #0#0, #0#1, #0#2, 001;
     #0#0, #0#1, #0#2, 999;
     ;
     NULL, Carga;
|FIN;

|PROCESO infe;
     #1#0 = #0#0;
     #1#1 = #0#1;
     #1#2 = #0#2;
     #1#3 = 001;
|FPRC;

|PROCESO supe;
     #1#0 = #0#0;
     #1#1 = #0#1;
     #1#2 = #0#2;
     #1#3 = 999;
|FPRC;

|PROCESO Tmp;
     |SI #1#12 = "*";
          acepta = 1;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE BTmp;
     #1#1;
     ;
     #0#0, #0#1, #0#2, 001;
     #0#0, #0#1, #0#2, 999;
     ;
     NULL, Tmp;
|FIN;

|PROCESO tipo11; |TIPO 11;
     |SI FSalida ! 9; |ACABA; |FINSI;

     |ABRE #87;
     #87#25 = #0#0;
     #87#0 = #0#1;
     |LEE 020#87=;
     |CIERRA #87;
     nFecha = #87#75;
     nFecha = nFecha + 365;
     fFecha = nFecha;
     |SI fFecha < #4#2;
          |AVISO;
          |PUSHV 0501 2380;
          |COLOR 7,4; |ATRI 0
          |BLANCO 1119 1863;
          |CUADRO 1220 1862;
          |ATRI 0;
          |COLOR 4,7; |ATRI 0;
          |PINTA 1322, "La Fecha de verificacion de precios del";
          aAlfa = "Pedido:" + #0#0 + "/" + #0#1 + " no esta actualizada.";
          |PINTA 1422, aAlfa;
          aAlfa = "Fecha de ultima verificacion:" + #87#75;
          |PINTA 1522, aAlfa;
          |ATRI 0;
          |COLOR 15,4; |ATRI 0;
          |PINTA 1737, "Pulse F5 para continuar.";
          |ATRI 0;
          |PARA; |SI aAlfa ! "827"; |HACIENDO;
               |LEETECLA aAlfa;
               aAlfa = aAlfa % 2;
          |SIGUE;
          |POPV;
     |FINSI;

     fichero = Usuario;
     fichero = "1" + fichero;
     |NOME_DAT #1 fichero;
     |ABRE #1; |CIERRA #1; |DELINDEX #1;
     |ABRE #1;
     |BUCLE BCarga;
     |CIERRA #1;
     |PUSHV 0501 2380;
     |ENTLINEAL #1#1, 4, 4, 22, 1, infe, supe;
     |POPV;
     acepta = 0;
     |BUCLE BTmp;
     |SI acepta = 1;
          |PTEC 807;     || Ctrl-C
     |SINO;
          |DELINDEX #1;
          |ERROR;
     |FINSI;
|FPRC;
