|FICHEROS;
     dsw00054 #540;
     dsw00055 #550; ||Tmp
     dsm00053 #53; || Descripcion Ampliada
|FIN;

|VARIABLES;
     &aTipoDA = "";
     &eaSerie = "";
     &enCodigo = 0;
     &enLinea = 0;
     &eaArticulo = "";

     aSer = "";
     nNume = 0;
     nLin = 0;
     aArti = "";
     n2Lin = 0;
     aReg = "";
     aAlfa = "";
     aLon = "";
     nLon = 0;
     i = 0;
     nHay = 0;
     nPos = 0;
     aDato = "";
|FIN;

|PROCESO UltimoCB;
     #550#0 = #540#0;
     #550#1 = 9999;
     |LEE 000#550];
     |SI FSalida = 0;
          |LEE 000#550a;
     |SINO;
          |LEE 000#550u;
     |FINSI;
     |SI FSalida = 0; |Y #550#0 = #540#0;
          n2Lin = #550#1 + 1;
     |SINO;
          n2Lin = 1;
     |FINSI;
|FPRC;

|PROCESO UltimaLinDA;
     #53#0 = aTipoDA;
     #53#1 = aSer;
     aTipoDA = #53#0; aSer = #53#1; ||Formateo
     #53#2 = nNume;
     #53#3 = nLin;
     #53#4 = 999;
     |LEE 000#53];
     |SI FSalida = 0;
          |LEE 000#53a;
     |SINO;
          |LEE 000#53u;
     |FINSI;
     |SI FSalida = 0; |Y #53#0 = aTipoDA; |Y #53#1 = aSer;
               |Y #53#2 = nNume; |Y #53#3 = nLin;
          n2Lin = #53#4 + 1;
     |SINO;
          n2Lin = 1;
     |FINSI;
|FPRC;

|PROCESO CargaBar;
     aAlfa = #53#5; |QBF aAlfa;
     aLon = aAlfa % A0;
     aReg = "";
     nHay = 0;
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aDato = aAlfa % nPos;
          |SI aDato = "|";
               #540#0 = aReg;
               |HAZ UltimoCB;
               #550#0 = aReg;
               #550#1 = n2Lin;
               |GRABA 020#550n;
               aReg = "";
               nHay = 1;
          |SINO;
               aReg = aReg + aDato;
          |FINSI;
     |SIGUE;
     |SI nHay ! 0; |BORRA 020#53a; |FINSI;
|FPRC;

|DEFBUCLE CargaBar;
     #53#1;
     ;
     aTipoDA, aSer, nNume, nLin, 0001;
     aTipoDA, aSer, nNume, nLin, 9999;
     ;
     NULL, CargaBar;
|FIN;

|PROCESO GrabaCodBar;
     |HAZ UltimoCB;
     |SI n2Lin ! 1;
          |AVISO; |AVISO;
          aAlfa = "0000N" + #540#0 + &13 + "El codigo existe. Continuar?";
          |CONFI aAlfa;
          |SI FSalida ! 0; |ACABA; |FINSI;
     |FINSI;
     #550#0 = #540#0;
     #550#1 = n2Lin;
     |GRABA 020#550n;
|FPRC;

|PROCESO DesCargaBar;
     aAlfa = aReg + #550#0 + "|";
     aLon = aAlfa % A0;
     |SI aLon > nLon;
          |HAZ UltimaLinDA;
          #53#0 = aTipoDA;
          #53#1 = aSer;
          #53#2 = nNume;
          #53#3 = nLin;
          #53#4 = n2Lin;
          #53#5 = aReg;
          |GRABA 020#53n;
          aReg = #550#0 + "|";
     |SINO;
          aReg = aReg + #550#0 + "|";
     |FINSI;
|FPRC;

|DEFBUCLE DesCargaBar;
     #550#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, DesCargaBar;
|FIN;

|PROGRAMA;
     aSer = eaSerie;
     nNume = enCodigo;
     nLin = enLinea;
     aArti = eaArticulo;

     |ABRE #53;

     aAlfa = Usuario; |QBF aAlfa;
     |NOME_DAT #550 aAlfa;
     |DELINDEX #550;
     |ABRE #550;
     |BUCLE CargaBar;
     |DDEFECTO #540;
     |PINPA #0#540;
     |PINDA #0#540;
     |AVISO; |AVISO;
     |PARA; |SI; |HACIENDO;
          |ENCAMPO #0#540;
          aAlfa = #540#0; |QBF aAlfa;
          aAlfa = aAlfa > "";
          |SI FSalida ! 0; |O aAlfa = "FIN"; |SAL; |FINSI;
          |HAZ GrabaCodBar;
     |SIGUE;

     aLon = #53#5;
     aLon = aLon % A0;
     nLon = aLon;
     aReg = "";
     |BUCLE DesCargaBar;
     |SI aReg ! "";
          nLon = 0;
          |HAZ DesCargaBar;
     |FINSI;
     |CIERRA #550;
     |DELINDEX #550;

     |CIERRA #53;
|FPRO;
