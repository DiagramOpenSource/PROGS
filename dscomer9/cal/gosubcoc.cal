|FICHEROS;
     gosubcoc #0;
     gopre001 #1;        ||Presupuestos Obra (CAB)
     goaux002 #2;        ||Auxiliar Presupuesto Obra (LIN)
     goaux003 #3;        ||Auxiliar Pre.Descomp.Capitulo
     goaux004 #4;        ||Auxiliar Pre.Descomp.Partidas
     gosubcap #5,MANTE;  ||Subcontratas Capitulos
     gosubpar #6,MANTE;  ||Subcontratas Partidas
     gosubdes #7,MANTE;  ||Subcontratas Desconposicion.
     goparame #8;        ||Parametros Obra.
     goauxpar #9;        ||Auxiliar Por Tipos
     gosubpa1 #10;       ||Descripcion Ampliada.
     godeprel #11;       ||Descripcion Ampliada. Presu
     gotmpart #20;
     guerm030 #30;
     guerm039 #39;
     goartdes #50,MANTE;
     gosubcot #51,MANTE;
     agifa077 #77;
     agifa080 #80;
     goartive #100;
     gofamive #101;
     gosubive #102;
     goapaive #103;
     gomantl1 #104;
     referen@;
|FIN;

|VARIABLES;
     &enPos = 0;
     &eaDocu = "";
     &enForma = 0;
     &enSwMenu = 0;
     nTotalAnt = 0;
     &Tempo = "";
     &nPrecioComp   = 0;
     aProducto = "";
     nLinea = 0;
     &nErrorPar      = 0;
     &cParti        = "";
     &cPart         = "";
     &cPartAct      = "";
     cParam         = "";
     nImpo = 0;
     aCap = "";
     cSecMin = "";;
     cSecMax = "";;
     cFamilia = "";
     cSubFamilia = "";
     &cUsuario      = "";
     cCapitulo      = "";
     cFami          = "";
     nTecla         = 0;
     nModifi        = 0;
     &efFecha       = @;
     nForma         = 0;
     &nTip          = 0;
     &eaSerie       = "";
     &enNuPre       = 0;
     nPrecio        = 0;
     nNume          = 0;
     nVic           = 0;
     nNum           = 0;
     nSal           = 0;
     nCua           = 0;
     nMod           = 0;
     nModo          = 0;
     nModAct        = 0;
     nLin           = 0;
     nBotF02        = 0;
     nBotF04        = 0;
     nBotF05        = 0;
     nBotF06        = 0;
     nBotF2         = 0;
     nBotF4         = 0;
     nBotF5         = 0;
     nBotF9         = 0;
     nTipo          = 0;
     nContador      = 0;

     aTid           = "";
     aTih           = "";
     aAlfa          = "";

     {-1}aEnva      = "";
         aEnva1     = "2500";
         aEnva2     = "1";
         aEnva3     = "Forma Carga ";
         aEnva4     = "SPTD";
         aEnva5     = "  Selectivo   ";
         aEnva6     = "  Por Tipo    ";
         aEnva7     = "    Total     ";
         aEnva8     = " Desglose Par.";

     &cCodMonBase   = "";
     &cDesMonBase   = "";
     &cCodMonTrab   = "";
     &cDesMonTrab   = "";

     &cCodMonDe     = "";
     &cCodMonA      = "";

     &nDeci_STrab   = 0;
     &nDeci_PBase   = 0;
     &nDeci_TBase   = 0;
     &nDeci_PTrab   = 0;
     &nDeci_TTrab   = 0;
     &nDeci_TotVis  = 0;
     &nEurComTrab   = 0;
     &nEurComBase   = 0;
     &nEurVenTrab   = 0;
     &nEurVenBase   = 0;
     nHay           = 0;
     nError         = 0;
|FIN;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO DecimalSub; |TIPO 0;
     nDeci_STrab = #0#15;
|FPRC;

|PROCESO CtrlFec;
     efFecha = #0#1;
     |HAZ EstaCerrado;
     |SI FSalida = 0;
          |ERROR; |ACABA;
     |FINSI;
|FPRC;

|PROCESO SinEscape; ||Para el control fecha
     |SI FSalida = 1; |ERROR; |ACABA; |FINSI;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO PrecioDetalle;
     #6#13 = #6#13 + #7#13;
|FPRC;

|DEFBUCLE PrecioDetalle;
     #7#1;
     ;
     #0#0, #5#2, #6#3,   1;
     #0#0, #5#2, #6#3, 999;
     ;
     NULL, PrecioDetalle;
|FIN;

|PROCESO RepartePre;
     nImpo = #6#13 * #7#13 / nTotalAnt;
     #7#12 = nImpo / #7#14;

     #7#13 = #7#14 * #7#12;
     nPrecio = nPrecio + #7#13;
     |GRABA 020#7a;
     |LIBERA #7;
|FPRC;

|DEFBUCLE RepartePre;
     #7#1;
     ;
     #0#0, #5#2, #6#3,   1;
     #0#0, #5#2, #6#3, 999;
     be;
     NULL, RepartePre;
|FIN;

|PROCESO ModiPrecio; |TIPO 0;
     nTotalAnt = Contenido;
     |SI #6#13 ! nTotalAnt;
          nPrecio = 0;
          |BUCLE RepartePre;
          #6#13 = nPrecio;
     |FINSI;
     |HAZ TotaLinea;
     |PINTA #6#13;
|FPRC;

|PROCESO TotaLinea;
||     A = Cantidad x Precios
||     A = A - ((dto1 * A)/100)
 ||    Importes = A - ((dto2 * A)/100)

     nImpo = (#6#13 * #6#14);
     nImpo = nImpo - (( #6#17 * nImpo)/100);
     #6#12 = nImpo - (( #6#18 * nImpo)/100);
     |PINTA #6#12;
|FPRC;

|PROCESO TotaCabe;
     #0#12 = #0#13 - (#0#13 < #0#14);
     |PINTA #0#12;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO PinLin; |TIPO 20;
     |HAZ verpa;
     FSalida = "";
|FPRC;
-------------------------------------------------------------------------------
|PROCESO gosubdes;

     |SI nTipo = -1;
          #6#7  = #6#7  - #7#9;                   ||Precio Partida Coste.
          #6#10 = #6#10 - #7#11;                  ||Precio Partida Venta.
          #6#13 = #6#13 - #7#13;                  ||Precio Partida Contratado.
     |SINO;
          #6#7  = #6#7  + #7#9;                   ||Precio Partida Coste.
          #6#10 = #6#10 + #7#11;                  ||Precio Partida Venta.
          #6#13 = #6#13 + #7#13;                  ||Precio Contratado.
     |FINSI;

     |SI nMod = 3;
          #4#0 = #0#4;                            ||Serie.
          #4#1 = #0#5;                            ||N쬝resupuesto.
          #4#3 = #6#3;                            ||Partida
          #4#5 = #7#4;                            ||Descomposicion
          |LEE 101#4=;

          |SI FSalida = 0;
               #4#16 = 0;
               |GRABA 020#4a;
          |FINSI;

          |LIBERA #4;
          |BORRA 020#7a;
     |FINSI;

     #6#8  = #6#14*#6#7;                     ||Importe Coste Partida.                   ||
     #6#11 = #6#14*#6#10;                    ||Importe Venta Partida.
     |HAZ TotaLinea;
|FPRC;

|DEFBUCLE gosubdes;
     #7#1;
     ;
     #0#0, #5#2, #6#3,   1;
     #0#0, #5#2, #6#3, 999;
     ;
     NULL,gosubdes;
|FIN;
----------------------------------------------------------------------------
|PROCESO BuscaPre; |TIPO 0;
     |ABRE #1;
     #1#0 = #0#4;
     #1#1 = #0#5;
     |LEE 010#1=;
     |SI FSalida ! 0;
          |MENAV "0000Presupuesto no encontrado";
          |ERROR;
     |FINSI;
     |CIERRA #1;
|FPRC;
----------------------------------------------------------------------------
|PROCESO PulsaF2; |TIPO 0;
     nTecla = FSalida;
     nModo = (FEntrada/100)!0;
     |SI nTecla = 10; ||F2
          |ABRE #2;
          |LEE 000#2p;
          |CONSULTA_F_CLAVE #1#2, MinCap, MaxCap;
          |SI FSalida = 0;
               |SI Contenido ! #2#12; |Y nModo = 2;    ||Si selecciono un capitulo
                    |ERROR;                            ||diferente al que habia no
                    |ACABA;                            ||me deja.
               |SINO;
                    #5#2 = #2#12;            ||Capitulo
               |FINSI;
          |SINO;
               |ERROR;
               |ACABA;
          |FINSI;
          |CIERRA #2;
     |FINSI;
     |SI nTecla = 12;  ||F4
          |HAZ Indice;
     |FINSI;
     |SI nTecla = 13; ||F5
          |HAZ  Selectiva;
     |FINSI;
     |SI nTecla = 14; ||F6;
          |HAZ General;
     |FINSI;
     |SI #5Campo ! Contenido;

|| Wed May 18 09:13:44 CEST 2005
          |ABRE #102;
          #102#0 = #5#2;
          |LEE 000#102=;
          |SI FSalida = 0;
               #5#3 = #102#1;
          |FINSI;
          |CIERRA #102;

          |ABRE #2; |SELECT #3#2;
          #2#0 = #0#4;
          #2#1 = #0#5;
          #2#3 = #5#2;
          |LEE 000#2=;
          |SI FSalida = 0;
               #5#3 = #2#13;
               #5#7 = #2#10;     ||Coste
               #5#9 = #2#9;        ||Vta
          |FINSI;
          |CIERRA #2;
     |FINSI;
     |PINTA #5#2;
     |PINTA #5#3;
     |PINTA #5#7;
     |PINTA #5#9;
|FPRC;

|PROCESO Indice;
     cUsuario = Usuario % 103;
     |SUB_EJECUTA_NP "goindice;PRES";
     |ABRE #100;
     #100#0 = cPart;
     |LEE 000#100=;
     |SI FSalida = 0;
          cCapitulo = #100#13; |QBF cCapitulo;
          |SI cCapitulo = ""; |ERROR; |ACABA; |FINSI;
          |ABRE #103;
          #103#0 = cCapitulo;
          |LEE 000#103=;
          |SI FSalida ! 0;
               |MENAV "0000SELECION ERRONEA.DEBE SELECCIONAR CAPITULOS DE MAXIMO NIVEL";
               |ERROR; |ACABA;
          |SINO;
               cFami = cCapitulo % 103;
               |QBF cFami;
               |ABRE #102;
               #102#0 = cFami;
               |LEE 000#102=;
               |SI FSalida = 0;
                    #5#2 = cFami;
               |SINO;
                    |ERROR;
               |FINSI;
               |CIERRA #102;
          |FINSI;
          |CIERRA #103;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #100;
|FPRC;

|PROCESO MinConsul;
     #100#0  = cSecMin;
     #101#2 = cFamilia;
     #102#2 = cSubFamilia;
     #103#2 = cCapitulo;
|FPRC;

|PROCESO MaxConsul;
     #100#0  = cSecMax;
     #101#2 = cFamilia;
     #102#2 = cSubFamilia;
     #103#2 = cCapitulo;
|FPRC;

|PROCESO Selectiva;
     |HAZ BuscaPre;
     |ABRE #101;
     |ABRE #102;
     |ABRE #103;
     cFamilia = #1#19;
     |QBF cFamilia;
     |CONSULTA_F_CLAVE #4#101, MinConsul, MaxConsul;
     |SI FSalida = 0;
          cSubFamilia = #101#0;
          |QBF cSubFamilia;
          |CONSULTA_F_CLAVE #4#102, MinConsul, MaxConsul;
          |SI FSalida = 0;
               cCapitulo = #102#0;
               |QBF cCapitulo;
               |CONSULTA_F_CLAVE #4#103, MinConsul, MaxConsul;
               |SI FSalida = 0;
                    #5#2 = #102#0;
               |SINO;
                    |ERROR;
               |FINSI;
          |SINO;
               |ERROR;
          |FINSI;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #101;
     |CIERRA #102;
     |CIERRA #103;
|FPRC;

|PROCESO General;
     |ABRE #100
     |CONSULTA_CLAVE #2#100;
     |SI FSalida = 0;
          #5#2 = #100#0;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #100;
|FPRC;

----------------------------------------------------------------------------
|PROCESO MinCap;
     #2#0 = #0#4;
     #2#1 = #0#5;
     #2#2 = 1;
|FPRC;
----------------------------------------------------------------------------
|PROCESO MaxCap;
     #2#0 = #0#4;
     #2#1 = #0#5;
     #2#2 = 999;
|FPRC;
----------------------------------------------------------------------------
|PROCESO Contador; |TIPO 7;
     nModo = (FEntrada/100)!0;
     |SI nModo = 1;
          |LEE 101#0u;
          |SI FSalida ! 0;
               nContador = 1;
          |SINO;
               nContador = #0#0 + 1;
          |FINSI;
          |DDEFECTO #0;
          #0#0 = nContador;
          |PINTA #0#0;
     |FINSI;
|FPRC;
----------------------------------------------------------------------------
|PROCESO MiniPar;
     #6#0 = #0#0;
     #6#1 = #5#2;
     #6#2 = 1;
|FPRC;
----------------------------------------------------------------------------
|PROCESO MaxiPar;
     #6#0 = #0#0;
     #6#1 = #5#2;
     #6#2 = 999;
|FPRC;
----------------------------------------------------------------------------
|PROCESO gosubpar;
     |SI nMod = 3;
          |HAZ tipo22;
          |ABRE #4;
          |SELECT #3#4;
          |BUCLE gosubdes;
          |CIERRA #4;
          |BORRA 020#6a;
     |FINSI;
|FPRC;
-------------------------------------------------------------------------------
|DEFBUCLE gosubpar;
     #6#1;
     ;
     #0#0,#5#2,  1;
     #0#0,#5#2,999;
     ;
     NULL,gosubpar;
|FIN;
-------------------------------------------------------------------------------
|PROCESO MiraLinCerC;
     |SI #6#16 ] 1;
          nHay = 1;
     |FINSI;
|FPRC;
-------------------------------------------------------------------------------
|DEFBUCLE MiraLinCerC;
     #6#1;
     ;
     #0#0,#5#2,1;
     #0#0,#5#2,999;
     ;
     NULL, MiraLinCerC;
|FIN;
-------------------------------------------------------------------------------
|PROCESO tipo2; |TIPO 2;                               ||Capitulos.
     nMod = (FEntrada / 100) ! 0;

     |SI nMod = 3;
          nHay = 0;
          |BUCLE MiraLinCerC;
          |SI nHay = 1;
               |MENAV "0000BAJA NO PERMITIDA. HAY PARTIDAS CERTIFICADAS";
               |ERROR;
               |ACABA;
          |FINSI;
     |FINSI;

     nTipo = 0 +. 1;
     |SI nTipo = -1; #0#13 = #0#13 - #5#11; |FINSI;
     |BUCLE gosubpar;
     |SI nTipo = 1; #0#13 = #0#13 + #5#11; |FINSI;
     |PINTA #0#13;
     |HAZ TotaCabe;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO verpa; |TIPO 7;
     |ENTLINEAL #1#6 , -3 , 7 , 21 , 0 , MiniPar , MaxiPar;
     |PINTA #5#7;
     |PINTA #5#9;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO SacaDat; |TIPO 0;
     nModo = (FEntrada/100)!0;
     aAlfa = #5#2;
     |QBF aAlfa;

     |SI aAlfa = ""; |ERROR; |ACABA; |FINSI;

     |C_P #5#11, 0, enPos;

     nError = 0;
     |SALVA_FICHA #5;
     |SELECT #2#5;
     |LEE 000#5=;
     |SI FSalida = 0; |Y nModo = 1;
          |MENAV "0000EL CAPITULO YA ESTA INCLUIDO EN ESTA SUBCONTRATA";
          nError = 1;
     |FINSI;
     |SELECT #1#5;
     |REPON_FICHA #5;
     |SI nError = 1;
          nError = 0;
          #5#2 = "";
          |PINTA #5#2;
          |ERROR;
          |ACABA;
     |FINSI;

     |ABRE #2;
     |SELECT #6#2;
     #2#0 = #0#4;                                 ||Serie Presupuesto
     #2#1 = #0#5;                                 ||Numero Presupuesto.
     #2#12 = #5#2;                                ||Capitulo.
     |LEE 000#2=;

     |SI FSalida = 0;
          |SI nModo = 1; |Y #0#11 = "N";
               #5#3 = #2#4;                            ||Descripcion
               #5#4 = #2#6;                            ||Unidades
               |PINTA #5#2;
               |PINTA #5#3;
               |PINTA #5#4;
               |PINTA #5#7;
               |PINTA #5#9;
               |PINTA #5#11;
          |FINSI;
          |HAZ BHSub;
          |C_M #6#3, 1, "S";
          |ENTLINEAL #1#6 , -3 , 2 , 21 , 0 , MiniPar , MaxiPar;
     |SINO;
          |SI #0#11 = "S";
               |MENAV "0000EL CAPITULO NO EXISTE EN EL PRESUPUESTO";
               |SI nModo = 1; |ERROR; |ACABA; |FINSI;
          |FINSI;
          |C_M #6#3, 1, "S";
          |ENTLINEAL #1#6 , -3 , 2 , 21 , 0 , MiniPar , MaxiPar;
     |FINSI;

     |CIERRA #2;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO f3; |TIPO 0;

     nMod = (FEntrada / 100) ! 0;
     |SI nMod = 2;
          |C_M #5#2, 1, "N";
     |SINO;
          |C_M #5#2, 1, "S";
     |FINSI;
     |SI nMod ! 4; |ACABA; |FINSI;
     |ENTLINEAL #1#6 , -3 , nMod , 21 , 0 , MiniPar , MaxiPar;

|FPRC;
----------------------------------------------------------------------------
|PROCESO Descompo;
     |ABRE #3;
     |LEE 000#3p;
     |CONSULTA_F_CLAVE #5#3, MinPar, MaxPar;
     |SI FSalida = 0;
          #6#3 = #3#4;        ||Partida
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #3;
|FPRC;

|PROCESO ParIndice;
     cUsuario = Usuario % 103;
     |SUB_EJECUTA_NP "goindice;PRES";
     #6#3 = cPart;
|FPRC;

|PROCESO MinParCa;
     #20#11 = aCap;
     #20#0 = "                    ";
     #100#6 = #goparame#25;
|FPRC;

|PROCESO MaxParCa;
     #20#11 = aCap;
     #20#0 = "zzzzzzzzzzzzzzzzzzzz";
     #100#6 = #goparame#25;
|FPRC;

|PROCESO ParCapi;
     aCap = #5#2 % 103;
     |ABRE #20;
     |LEE 000#20p;
     |CONSULTA_F_CLAVE #8#20, MinParCa, MaxParCa;
     |SI FSalida = 0;
          #6#3  = #20#0;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #20;
|FPRC;

|PROCESO ParTodas;
     |ABRE #100;
     |LEE 000#100p;
     |CONSULTA_F_CLAVE #6#100, MinParCa, MaxParCa;
     aAlfa = #100#0; |QBF aAlfa;
     |SI aAlfa ! "";
          #6#3 = aAlfa;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #100;
|FPRC;

|PROCESO PulsaF22; |TIPO 0;
     nTecla = FSalida
     |SI nTecla = 10; ||F2
          |HAZ Descompo;
          |ERROR; |ACABA;
     |FINSI;
     |SI nTecla = 12; ||F4
          |HAZ ParIndice;
          |ERROR; |ACABA;
     |FINSI;
     |SI nTecla = 13; ||F5
          |HAZ ParCapi;
          |ERROR; |ACABA;
     |FINSI;
     |SI nTecla = 17;||F9
          |HAZ ParTodas;
          |ERROR; |ACABA;
     |FINSI;
     nErrorPar = 0;
     cParam = #6#3;
     |QBF cParam;
     cParam = cParam % -101;
     |SI cParam = "$";
          cPart = #6#3;
          |QBF cPart;
          |HAZ Parametrico;
          #6#3 = cPartAct;
     |FINSI;
     |SI nErrorPar = 1;
          |ERROR;
     |SINO;
          |ABRE #100;
          #100 = #6#3;
          |LEE 000#100=;
          |SI FSalida ! 0;
               |HAZ MiraPresu;
               |SI FSalida = 0; |ACABA; |FINSI;
               |HAZ CreaParti;
          |FINSI;
          |CIERRA #100;
          |SI nErrorPar = 1;
               |ERROR;
          |SINO;
               |SI #6#3 ! Contenido;
                    #6#4 = #100#1;         ||Des
                    #6#6 = #100#2;         ||UM
                    cCodMonDe = #100#5;    ||Moneda Gopre001
                    cCodMonA  = #0#8;      ||Moneda Goartive
                    nPrecioComp = #100#15; ||Ultimo Precio Compra
                    |HAZ DemonAmon;
                    #6#13 = nPrecioComp;  ||Precio

                    |PINTA #6#3;
                    |PINTA #6#4;
                    |PINTA #6#13;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO MiraPresu; || Si el Presupuesto es importado BC3, no graba el goartive
     |ABRE #3;
     |SELECT #2#3;
     #3#0 = #0#4;
     #3#1 = #0#5;
     #3#2 = #6#1;
     #3#4 = #6#3;
     |LEE 000#3=;
     nSal = FSalida;
     |SI FSalida = 0;
          #6#4 = #3#5; |PINTA #6#4;
          #6#6 = #3#6; |PINTA #6#6;
          #6#13 = #3#9; |PINTA #6#13;
          nSal = 0;
     |FINSI;
     |CIERRA #3;
     FSalida = nSal;
|FPRC;

|PROCESO CreaParti;
     nErrorPar = 0;
     |DDEFECTO #100;
     #100#0 = #6#3;
     |LEE 000#100=;

     /* Confirmacion para crear la nueva partida */

     |SI FSalida ! 0;                        ||La partida no existe en el banco de datos.
          |CONFI "0000SDESEA CREAR LA PARTIDA INTRODUCIDA";

          |SI FSalida = 0;
               |PUSHV 01012380;
               |PINPA #0#100;
               |PINDA #0#100;
               |ENDATOS #1#100;                ||Creamos la Partida en el banco de datos.
               |SI FSalida = 0;
                    |GRABA 020#100n;
               |SINO;
                    nErrorPar = 1;
               |FINSI;
               |POPV;
          |SINO;
               nErrorPar = 1;
          |FINSI;

     |FINSI;

|FPRC;
-----------------------------------------------------------------------------
|PROCESO MinPar;
     #3#0 = #0#4;
     #3#1 = #0#5;
     #3#2 = #5#2;
     #3#3 = 1;
     #3#21 = "N";
|FPRC;
-----------------------------------------------------------------------------
|PROCESO MaxPar;
     #3#0 = #0#4;
     #3#1 = #0#5;
     #3#2 = #5#2;
     #3#3 = 999;
     #3#21 = "N";
|FPRC;
-----------------------------------------------------------------------------
|PROCESO BajaAmpli;
     |ABRE #10;
     #10#0 = #6#0;
     #10#1 = #6#1;
     #10#2 = #6#2;
     #10#3 = 0;
     |LEE 000#10];
     |PARA; |SI FSalida = 0; |Y #10#0 = #6#0; |Y #10#1 = #6#1;
               |Y #10#2 = #6#2; |HACIENDO;
          |BORRA 020#10a;
          |LEE 000#10s;
     |SIGUE;
     |CIERRA #10;
|FPRC;

|PROCESO AltaAmpli;
     nLin = nLin  + 1;
     |DDEFECTO #10;
     #10#0 = #6#0;
     #10#1 = #6#1;
     #10#2 = #6#2;
     #10#3 = nLin;
     #10#4 = #11#4;
     |GRABA 020#10n;
|FPRC;

|DEFBUCLE AltaAmpli;
     #11#1;
     ;
     #6#3, #0#4, #0#5, #6#1, 001;
     #6#3, #0#4, #0#5, #6#1, 999;
     ;
     NULL, AltaAmpli;
|FIN;

|PROCESO tipo22; |TIPO 2;      ||Tipo 2 del fichero 6 Partidas
     nMod = (FEntrada / 100) ! 0;
     |HAZ TotaLinea;
     |SI nMod = 3; |O nMod = 2;
          |SI #6#16 ] 1;
               |MENAV "0000BAJA O MODIFICACION NO PERMITIDA. PARTIDA CERTIFICADA";
               |ERROR;
               |ACABA;
          |FINSI;
     |FINSI;
/*
     |ABRE #4;
     |SELECT #3#4;
     nTipo = 0 +. 1;
     |BUCLE gosubdes;
     |CIERRA #4;
*/
     |ABRE #3;
     |SELECT #2#3;
     #3#0 = #0#4;
     #3#1 = #0#5;
     #3#2 = #5#2;
     #3#4 = #6#3;
     |LEE 000#3=;

     |SI FSalida = 0;

          |SI nMod = 3;
               #3#22 = #3#22 - #6#14;
          |SINO;
               #3#22 = #3#22 +. #6#14;
          |FINSI;

          |SI nMod ! 3;
               |SI #6#15 = 0;
                    #3#21 = "S";
               |SINO;
                    #3#21 = "N";
               |FINSI;
          |SINO;
               #3#21 = "N";
          |FINSI;

          |GRABA 020#3a;
     |FINSI;

     |CIERRA #3;

     |SI nMod = 3;
          |HAZ BajaAmpli;
     |FINSI;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO MinDes;
     #7#0 = #0#0;
     #7#1 = #5#2;
     #7#2 = #6#3;
     #7#3 = 1;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO MaxDes;
     #7#0 = #0#0;
     #7#1 = #5#2;
     #7#2 = #6#3;
     #7#3 = 999;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO DesglosePar;
     |ABRE #50
     |SELECT #2#50
     #50#0 = #6#3;                            ||Cod. Partida.
     |LEE 000#50];
     |PARA; |SI FSalida = 0; |Y #50#0 = #6#3; |HACIENDO;
          nLinea = nLinea + 1;
          #7#0 = #0#0;
          #7#1 = #5#2;
          #7#2 = #6#3;
          #7#3 = nLinea;
          #7#4 = #50#2;
          #7#5 = #50#3;
          #7#7 = #50#4;
          #7#14 = #50#5;
          #7#12 = #50#6;
          #7#13 = #7#12 * #7#14;
          |GRABA 000#7n;
          |LEE 000#50s;
     |SIGUE;
     |CIERRA #50;
|FPRC;

|PROCESO BorraDesglose;
     |BORRA 020#7a;
|FPRC;

|DEFBUCLE BorraDesglose;
     #7#1;
     ;
     #6#0, #6#1, #6#3, 001;
     #6#0, #6#1, #6#3, 999;
     be;
     NULL, BorraDesglose;
|FIN;

|PROCESO SacaDa2; |TIPO 0;
     nMod = (FEntrada / 100) ! 0;

     |SI nMod = 1; |O #6Campo ! Contenido;
          |HAZ BajaAmpli;
          nLin = 0;
          |ABRE #10;
          |BUCLE AltaAmpli;
          |CIERRA #10;
     |FINSI;

     aAlfa = #6#3;
     |QBF aAlfa;

     |SI aAlfa = ""; |ERROR; |ACABA; |FINSI;

     |ABRE #3;
     |SELECT #2#3;
     #3#0 = #0#4;                                 ||Serie.
     #3#1 = #0#5;                                 ||N쬝resupuesto.
     #3#2 = #5#2;                                 ||Capitulo
     #3#4 = #6#3;                                 ||Partida
     |LEE 000#3=;
     |SI FSalida ! 0;
          |MENAV "0000No existe partida en el presupuesto...";
          |ERROR;
     |SINO;
          |HAZ BHSub;
          |SI nMod = 1;
               #6#5 = #3#7-#3#22;                     ||Unidades Presupuesto.
               #6#7 = #3#9;                           ||Precio de Coste Presupuesto.
               #6#8 = #3#11;                          ||Importe de Coste Presupuesto.
               #6#10 = #3#8;                           ||Precio de Venta Presupuesto.
               #6#11 = #3#10;                          ||Importe Venta Presupuesto.
               |SI #0#11 = "S";
                    #6#13 = #3#9;
               |FINSI;
               |HAZ TotaLinea;
               |PINTA #6#3;
               |PINTA #6#4;
               |PINTA #6#5;
               |PINTA #6#6;
               |PINTA #6#7;
               |PINTA #6#8;
               |PINTA #6#9;
               |PINTA #6#10;
               |PINTA #6#11;
               |PINTA #6#13;
               |BUCLE BorraDesglose;  ||Nos aseguramos de que no hay nada
               |HAZ menu;
               ||컴컴컴컴컴컴컴컴컴컴컴Crea 1 Linea si no hay ninguna
               |SI nLinea = 0;
                    |ABRE #7;
                    #7#0 = #0#0;
                    #7#1 = #5#2;
                    #7#2 = #6#3;
                    #7#3 = 1;
                    #7#4 = ".PA";
                    #7#5 = "PARTIDA ALZADA";
                    #7#14 = 1;
                    #7#12 = 1;
                    #7#13 = 1;
                    |GRABA 020#7n;
                    |CIERRA #7;
               |FINSI;
          |FINSI;
          #6#13 = 0;
          |BUCLE PrecioDetalle;
          |PINTA #6#13;

          |C_M #7#5, "1", "S";
          |PUSHV 0501 2380;
          nModifi = 0;
          |ENTLINEAL #1#7 , 4 , 2 , 20 , 1 , MinDes , MaxDes;
          |POPV;
          |SI nModifi ! 0;
               #6#13 = 0;
               |BUCLE PrecioDetalle;
               |PINTA #6#13;
          |FINSI;
     |FINSI;
     |CIERRA #3;
|FPRC;

-----------------------------------------------------------------------------
|PROCESO f33; |TIPO 0;
     nMod = (FEntrada / 100) ! 0;
     |SI nMod = 2;
          |C_M #6#3, 1, "N";
     |SINO;
          |C_M #6#3, 1, "S";
     |FINSI;
     |SI nMod ! 4; |ACABA; |FINSI;
     |PUSHV 0501 2380;
     |ENTLINEAL #1#7 , 4 , nMod , 20 , 1 , MinDes , MaxDes;
     |POPV;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO PulsaF222; |TIPO 0;

     |SI FSalida = 10;
          |ABRE #4;
          |LEE 000#4p;
          |CONSULTA_F_CLAVE #4#4,MiniDes,MaxiDes;

          |SI FSalida = 0;
               #7#4 = #4#5;        ||Descomposicion
          |SINO;
               |ERROR;
               |ACABA;
          |FINSI;

          |CIERRA #4;
     |FINSI;

     |HAZ BHSub;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO MiniDes;
     #4#0  = #0#4;
     #4#1  = #0#5;
     #4#3  = #6#3;
     #4#5  = "                    ";
     #4#16 = 0;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO MaxiDes;
     #4#0  = #0#4;
     #4#1  = #0#5;
     #4#3  = #6#3;
     #4#5  = "zzzzzzzzzzzzzzzzzzzz";
     #4#16 = 0;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO SacaDa22; |TIPO 0;
     nMod = (FEntrada/100)!0;
     aAlfa = #7#4;
     |QBF aAlfa;

     |SI aAlfa = ""; |ERROR; |ACABA; |FINSI;

     |ABRE #4;
     |SELECT #3#4;
     #4#0 = #0#4;                                 ||Serie.
     #4#1 = #0#5;                                 ||N쬝resupuesto.
     #4#3 = #6#3;                                 ||Partida
     #4#5 = #7#4;                                 ||Descomposicion
     |LEE 000#4=;
     |SI FSalida ! 0;
          |MENAV "0000No existe Descomposicion en el presupuesto...";
          |ERROR;
     |SINO;
          |SI #0#11 = "N";
               |SI #4#16 ! 0; |Y #0#0 ! #4#16;
                    aAlfa = "0000Descomposicion Asignado en Subcontrata " + #4#16;
                    |MENAV aAlfa;
                    |ERROR;
                    |ACABA;
               |FINSI;

               |SI nMod = 1;
                    #7#5  = #4#6;                      ||Concepto articulo.
                    #7#6  = #4#8;                      ||Cantidad Presupuestada.
                    #7#7  = #4#7;                      ||Unidad de Medida.
                    #7#8  = #4#10;                     ||Precio Coste Presupuesto.
                    #7#9  = #4#12;                     ||Importe Coste Presupuestado.
                    #7#10 = #4#11/#4#8;                ||Precio Venta Presupuestado.
                    #7#11 = #4#11;                     ||Importe Venta Presupuestado.
                    #7#12 = #4#10;                     ||Precio Defecto Contratista.
                    #7#14 = #4#8;                      ||Cantidad Defecto Contratada.
                    #7#13 = #7#12*#7#14;               ||Importe defecto Contratado.
                    #4#16 = #0#0;
                    |GRABA 000#4a;

                    |PINTA #7#4;
                    |PINTA #7#5;
                    |PINTA #7#6;
                    |PINTA #7#7;
                    |PINTA #7#8;
                    |PINTA #7#9;
                    |PINTA #7#10;
                    |PINTA #7#11;
                    |PINTA #7#12;
                    |PINTA #7#13;
                    |PINTA #7#14;
               |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #4;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO calcula; |TIPO 0;
     |SI Campo = 14; |O Campo = 12;
          |SI #7Campo ! Contenido;
               nModifi = 1;
          |FINSI;
     |FINSI;

     |SI #7#14 > #7#6;  |Y #0#11 = "S";
          |MENAV "0000LA CANTIDAD ES SUPERIOR A LA PRESUPUESTADA";
          |ACABA;
     |FINSI;

     #7#13 = #7#14 * #7#12;
     #7#15 = #7#6 - #7#14;
     |PINTA #7#15;
     |PINTA #7#13;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO Min51;
     #51#0 = 1;
|FPRC;

|PROCESO Max51;
     #51#0 = 999;
|FPRC;

|PROCESO EligeDes; |TIPO 0;
     |SI FSalida = 0; |Y #51Campo = Contenido;
          |SI #51#5 = "*";
               #51#5 = "";
          |SINO;
               #51#5 = "*";
          |FINSI;
          |GRABA 020#51a;
          |PINTA #51#5;
     |FINSI;
|FPRC;

|PROCESO DesgloseParPresu;
     #51#0 = #4#4;
     #51#1 = #4#5;
     #51#2 = #4#6;
     #51#3 = #4#7;
     #51#4 = #4#8;
     |GRABA 020#51n;
|FPRC;

|DEFBUCLE DesgloseParPresu;
     #4#2;
     ;
     #0#4, #0#5, #6#1, #6#3;
     #0#4, #0#5, #6#1, #6#3;
     ;
     NULL, DesgloseParPresu;
|FIN;

|PROCESO CargaSelectivo;
     #4#0 = #0#4;
     #4#1 = #0#5;
     #4#2 = #6#1;
     #4#3 = #6#3;
     #4#4 = #51#0;
     |LEE 020#4=;

     nLinea = nLinea + 1;
     #7#0 = #6#0;
     #7#1 = #6#1;
     #7#2 = #6#3;
     #7#3 = nLinea;
     #7#4 = #4#5;
     #7#5 = #4#6;
     #7#7 = #4#7;
     #7#14 = #4#8;
     #7#12 = #4#10;
     #7#13 = #7#12 * #7#14;
     |GRABA 000#7n;
|FPRC;

|DEFBUCLE CargaSelectivo;
     #51#1;
     5;
     001, "*";
     999, "*";
     ;
     NULL, CargaSelectivo;
|FIN;

|PROCESO Selectivo;
     |HAZ CreaTempo;
     |NOME_DAT #51 Tempo; |DELINDEX #51;
     |ABRE #51;
     |BUCLE DesgloseParPresu;
     |CIERRA #51;
     |PUSHV 0501 2380;
     |ENTLINEAL #1#51, 1, 4, 22, 1, Min51, Max51;
     |POPV;
     |ABRE #4;
     |BUCLE CargaSelectivo;
     |CIERRA #4;
     |HAZ BoraTempo; |DELINDEX #51;
|FPRC;

|PROCESO menu;
     nLinea = 0;
     |ABRE #7;
     |MENU aEnva;
     aEnva2 = FSalida;
     |SI aEnva2 = "1"; |HAZ Selectivo; |FINSI;
     |SI aEnva2 = "2";  |HAZ catasele; |FINSI;
     |SI aEnva2 = "3";  |HAZ cartotal; |FINSI;
     |SI aEnva2 = "4"; |HAZ DesglosePar; |FINSI;
     |CIERRA #7;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO gopre004;
     aProducto = #4#5;
     |ABRE #39;
     #39#0 = #4#5;
     |LEE 000#39=;
     |SI FSalida = 0;
          aProducto = #39#1;
     |FINSI;
     |CIERRA #39;

     nLinea = nLinea + 1;
     #7#0 = #0#0;                                 ||N Subcontrata.
     #7#1 = #4#2;                                 ||Capitulo.
     #7#2 = #4#3;                                 ||Partida.
     #7#3 = nLinea;                                 ||Linea.
     #7#4 = aProducto;                             ||Refe. Articulo. Descom.
     |LEE 101#7=;
     |SI FSalida ! 0
          #7#12 = #4#10;                          ||Precio Defecto Contratista.
          #7#14 = #4#8;                           ||Cantidad Defecto Contratada.
          #7#13 = #7#12*#7#14;                    ||Importe defecto Contratado.
          |GRABA 020#7c;
     |FINSI;
     #7#5  = #4#6;                                ||Concepto articulo.
     #7#6  = #4#8;                                ||Cantidad Presupuestada.
     #7#7  = #4#7;                                ||Unidad de Medida.
     #7#8  = #4#10;                               ||Precio Coste Presupuesto.
     #7#9  = #4#12;                               ||Importe Coste Presupuestado.
     #7#10 = #4#11/#4#8;                          ||Precio Venta Presupuestado.
     #7#11 = #4#11;                               ||Importe Venta Presupuestado.
     #4#16 = #0#0;
     |GRABA 020#4a;
     |GRABA 020#7a;
     |LIBERA #7;
|FPRC;
-----------------------------------------------------------------------------

|DEFBUCLE gopre004;
     #4#1;
     14;
     #0#4,#0#5,#5#2,#6#3,  1,aTid;
     #0#4,#0#5,#5#2,#6#3,999,aTih;
     ;
     NULL,gopre004;
|FIN;
-----------------------------------------------------------------------------

|PROCESO cartotal;
     |ABRE #7;
     aTid = " ";
     aTih = "z";
     |BUCLE gopre004;
     |CIERRA #7;
|FPRC;
-----------------------------------------------------------------------------

|PROCESO cartotal2;
     |ABRE #7;
     |PARA nVic = 0; |SI nVic [3; |HACIENDO nVic = nVic + 1;
          nNum = nVic + 8;
          |SI #9nNum = "S";
               aTid = #9nVic;
               aTih = #9nVic;
               |BUCLE gopre004;
          |FINSI;
     |SIGUE;
     |CIERRA #7;
|FPRC;
-----------------------------------------------------------------------------

|PROCESO catasele;
     |DDEFECTO #8;
     |DDEFECTO #9;
     |ABRE #8;
     #8#0 = "A";
     |LEE 000#8=;
     #9#0 = #8#5;
     #9#1 = #8#6;
     #9#2 = #8#7;
     #9#3 = #8#8;

     #9#4 = #8#9;
     #9#5 = #8#10;
     #9#6 = #8#11;
     #9#7 = #8#12;
     |PUSHV 0501 2380;
     |PINPA #0#9;
     |PINDA #0#9;
     |ENDATOS #1#9;
     |SI FSalida = 0;
          |HAZ cartotal2;
     |FINSI;
     |POPV;
     |CIERRA #8;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO marcades; |TIPO 2;
     nMod = (FEntrada / 100) ! 0;
     nCua = 0 +. 1;
     |ABRE #4;
     |SELECT #3#4;
     #4#0 = #0#4;    ||Serie.
     #4#1 = #0#5;    ||N쬝resupuesto.
     #4#3 = #6#3;    ||Partida
     #4#5 = #7#4;    ||Descomposicion
     |LEE 101#4=;

     |SI FSalida = 0;

          |SI nMod = 1;
               #4#16 = #0#0;
          |FINSI;

          |SI nMod = 2;
               #4#16 = 0;
          |FINSI;

          |SI nMod = 3;
               #4#16 = 0;
          |FINSI;

          |GRABA 020#4a;
     |FINSI;

     |LIBERA #4;
     |CIERRA #4;
|FPRC;
-----------------------------------------------------------------------------

|PROCESO tipo13; |TIPO 13;
     |HAZ TipoMone;
     |ENTLINEAL #1#6 , -3 , 7 , 21 , 0 , MiniPar , MaxiPar;

     |ABRE #8; |LEE 000#8p; |CIERRA #8;
     |SI #8#141 = "O";
          |PINTA 528, "        Recoger Datos de O.E.:";
          |ATRI I; |PINTA 702, "O.E...........:"; |ATRI 0;
     |FINSI;
|FPRC;
-----------------------------------------------------------------------------
|PROCESO BISub; |TIPO 80;
     |CLS;
     |CONTROL_SIMPLE 0, "BOTON,F2 - Pdt de Contratar", 2405, 2419, F2Sub;
     nBotF02 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,F4 - Busqueda Indice", 2424, 2438, F4Sub;
     nBotF04 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,F5 - Busqueda Selectiva", 2443, 2457, F5Sub;
     nBotF05 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,F6 - Busqueda General", 2461, 2476, F6Sub;
     nBotF06 = FSalida;
     ||컴컴컴컴컴
     |CONTROL_SIMPLE 0, "BOTON,F2 - Descomposicion", 2405, 2419, F2Par;
     nBotF2 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,F4 - Indice Partidas", 2424, 2438, F4Par;
     nBotF4 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,F5 - Partidas Capitulos", 2443, 2457, F5Par;
     nBotF5 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,F9 - Todas las Partidas", 2461, 2476, F9Par;
     nBotF9 = FSalida;

     |HAZ OcultaBoton;

     |MODULO aAlfa;
     |SI aAlfa = "gosubcoc";
          |HAZ CargaIve;
     |FINSI;
|FPRC;
------------------------------------------------------------------------------
|PROCESO BFSub; |TIPO 90;
     |FIN_CONTROL_WINDOWS nBotF02;
     |FIN_CONTROL_WINDOWS nBotF04;
     |FIN_CONTROL_WINDOWS nBotF05;
     |FIN_CONTROL_WINDOWS nBotF06;
     |FIN_CONTROL_WINDOWS nBotF2;
     |FIN_CONTROL_WINDOWS nBotF4;
     |FIN_CONTROL_WINDOWS nBotF5;
     |FIN_CONTROL_WINDOWS nBotF9;
     |PULSATECLA;
|FPRC;

|PROCESO BSSub; |TIPO 7;

     nModo = (FEntrada/100)!0;

     |SI Campo = 4; |O Campo = 2; |O Campo = 3;
          |SI nModo ! 2;
               |ESTADO_CONTROL nBotF02, "SHOW";        ||"SHOW""HIDE""ENABLE""DISABLE".
               |ESTADO_CONTROL nBotF04, "SHOW";        ||"SHOW""HIDE""ENABLE""DISABLE".
               |ESTADO_CONTROL nBotF05, "SHOW";        ||"SHOW""HIDE""ENABLE""DISABLE".
               |ESTADO_CONTROL nBotF06, "SHOW";        ||"SHOW""HIDE""ENABLE""DISABLE".
          |SINO;
               |HAZ OcultaBoton;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO BHSub; |TIPO 0;
     |HAZ OcultaBoton;
|FPRC;

|PROCESO BHSub7; |TIPO 7;
     |HAZ BHSub;
|FPRC;

|PROCESO BSPar; |TIPO 7;
     nModo = (FEntrada/100)!0;

     |SI Campo = 4; |O Campo = 2; |O Campo = 3;
          |SI nModo ! 2;
               |ESTADO_CONTROL nBotF2, "SHOW";        ||"SHOW""HIDE""ENABLE""DISABLE".
               |ESTADO_CONTROL nBotF4, "SHOW";        ||"SHOW""HIDE""ENABLE""DISABLE".
               |ESTADO_CONTROL nBotF5, "SHOW";        ||"SHOW""HIDE""ENABLE""DISABLE".
               |ESTADO_CONTROL nBotF9, "SHOW";        ||"SHOW""HIDE""ENABLE""DISABLE".
          |SINO;
               |HAZ OcultaBoton;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO BHPar; |TIPO 0;
     |HAZ OcultaBoton;
|FPRC;

|PROCESO BHPar7; |TIPO 7;
     |HAZ BHPar;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO F2Sub;
     nModAct = FEntrada;
     |PTEC 824;
     FEntrada = nModAct;
|FPRC;

|PROCESO F4Sub;
     |PTEC 826;
|FPRC;

|PROCESO F5Sub;
     |PTEC 827;
|FPRC;

|PROCESO F6Sub;
     |PTEC 828;
|FPRC;

|PROCESO F2Par;
     |PTEC 824;
|FPRC;

|PROCESO F4Par;
     |PTEC 826;
|FPRC;

|PROCESO F5Par;
     |PTEC 827;
|FPRC;

|PROCESO F9Par;
     |PTEC 831;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO Tipo11; |TIPO 11;
     |HAZ BHSub;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO MiraCant; |TIPO 0;
     |SI Campo = 14; |O Campo = 13;
          |SI #6Campo ! Contenido;
               nModifi = 1;
          |FINSI;
     |FINSI;

     |HAZ TotaLinea;

     #6#15 = #6#5 - #6#14;
     |SI #6#15 < 0; |Y #0#11 = "S";
          |MENAV "0000LA CANTIDAD ES SUPERIOR A LA PRESUPUESTADA";
     |FINSI;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO Escape; |TIPO 11;
     |HAZ BHSub;
     |SI Campo = 13; |O Campo = 14;
          |MENAV "0000DEBE COMPLETAR LA LINEA";
          |ERROR;
     |FINSI;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO TipoMone; |TIPO 0;
     cCodMonTrab = #0#8;                          ||Moneda de Trabajo.
     |HAZ MonBas;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO Imprime; |TIPO 3;
     |ABRE #8;
     #8#0 = "A";
     |LEE 000#8=;
     |CIERRA #8;

     |SI #8#19 = "S";
          |SI #8#20 = "S";
               |CONFI "0000NDESEA IMPRIMIR EL VISADO DEL DOCUMENTO";
          |SINO;
               FSalida = 0;
          |FINSI;
          |SI FSalida = 0;
               |MENAV "0000INTRODUZCA EL DOCUMENTO EN LA IMPRESORA";
               |IMPRE 0;
               |SI FSalida = 0;
                    |INFOR "gosubcoc";
                    |SI FSalida = 0;
                         |PRINT;
                         |PIEINF;
                         |FININF;
                    |FINSI;
                    |FINIMP;
               |FINSI;
          |FINSI;
     |FINSI;
     |CONFI "0000NDESEA IMPRIMIR EL PEDIDO";
     |SI FSalida = 0;
            |HAZ SubT4;
     |FINSI;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO MiraLinCerE;
     |SI #6#16 ] 1;
          nHay = 1;
     |FINSI;
|FPRC;
-------------------------------------------------------------------------------
|DEFBUCLE MiraLinCerE;
     #6#3;
     ;
     #0#0, "                    ", "                    ";
     #0#0, "zzzzzzzzzzzzzzzzzzzz", "zzzzzzzzzzzzzzzzzzzz";
     ;
     NULL, MiraLinCerE;
|FIN;
-------------------------------------------------------------------------------
|PROCESO CompBaja; |TIPO 2;   ||| De la cabecera
     nModo = (FEntrada/100)!0;
     nForma = 0 +. 1;
     |SI nModo ! 1;
          |ABRE #30; |SELECT #2#30;
          #30#2 = #0#0;
          |LEE 000#30=;
          |SI FSalida = 0;
               |SI nForma ! 1;
                    |MENAV "0000Subcontrata certificada...";
                    |SI #0#20 = "N"; |Y nModo ! 3;
                         |CONFI "0000NDesea Finalizar la subcontrata?";
                         |SI FSalida = 0;
                              #0#20 = "S"; |PINTA #0#20;
                              |GRABA 020#0a;
                              |ERROR; |ACABA;
                         |SINO;
                              |SI #0#21 = "S"; |PTEC 815; |FINSI; || lo meto en las lineas
                         |FINSI;
                    |FINSI;
                    |SI #0#20 = "S";
                         |MENAV "0000Subcontrata finalizada...";
                         |ERROR; |ACABA;
                    |FINSI;
               |FINSI;
               |SI #0#21 = "N"; |ERROR; |ACABA; |FINSI;
          |FINSI;
          |CIERRA #30;
     |FINSI;
     |SI nModo = 3;
          nHay = 0;
          |BUCLE MiraLinCerE;
          |SI nHay = 1;
               |MENAV "0000BAJA NO PERMITIDA. HAY PARTIDAS CERTIFICADAS";
               |ERROR;
               |ACABA;
          |FINSI;
     |FINSI;
     |SI #0#18 ! 0;
          |HAZ AlbaranCta;
     |FINSI;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO godeprel;
     aAlfa = #10#4; |QBF aAlfa;
     |SI aAlfa ! "";
          nTip = 3;
          |PRINT;
     |FINSI;
|FPRC;

|DEFBUCLE godeprel;
     #10#1;
     ;
     #6#0, #6#1, #6#2,  1;
     #6#0, #6#1, #6#2, 999;
     ;
     NULL, godeprel;
|FIN;

|PROCESO Detalle;
     nTip = 2;
     |PRINT;
     |BUCLE godeprel;
|FPRC;

|DEFBUCLE Detalle;
     #6#1;
     ;
     #5#0, #5#2, 001;
     #5#0, #5#2, 999;
     ;
     NULL, Detalle;
|FIN;

|PROCESO Lineas;
     nTip = 1;
     |PRINT;
     |BUCLE Detalle;
|FPRC;

|PROCESO SubT4; |TIPO 4;
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR "gosubcoc";
          |SI FSalida = 0;
               |BUCLELIN 2 Lineas #0;
               |PIEINF;
               |FININF;
          |SINO;
               |MENAV "0000No existe informe 'gosubcoc'";
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴

|PROCESO ConsuPres1; |TIPO 0;
     |HAZ ConsuPres;
     |SI enNuPre ! 0;
          #0#4 = eaSerie; |PINTA #0#4;
          #0#5 = enNuPre; |PINTA #0#5;
     |FINSI;
|FPRC;

|PROCESO OcultaBoton;
     |ESTADO_CONTROL nBotF02, "HIDE";        ||"SHOW""HIDE""ENABLE""DISABLE".
     |ESTADO_CONTROL nBotF04, "HIDE";        ||"SHOW""HIDE""ENABLE""DISABLE".
     |ESTADO_CONTROL nBotF05, "HIDE";        ||"SHOW""HIDE""ENABLE""DISABLE".
     |ESTADO_CONTROL nBotF06, "HIDE";        ||"SHOW""HIDE""ENABLE""DISABLE".
     |ESTADO_CONTROL nBotF2, "HIDE";        ||"SHOW""HIDE""ENABLE""DISABLE".
     |ESTADO_CONTROL nBotF4, "HIDE";        ||"SHOW""HIDE""ENABLE""DISABLE".
     |ESTADO_CONTROL nBotF5, "HIDE";        ||"SHOW""HIDE""ENABLE""DISABLE".
     |ESTADO_CONTROL nBotF9, "HIDE";        ||"SHOW""HIDE""ENABLE""DISABLE".
|FPRC;

|PROCESO Salta5; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
       ||   #5#1 = #5#1 + 9;
     |FINSI;
|FPRC;

|PROCESO Salta6; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
||          #6#2 = #6#2 + 9;
     |FINSI;
|FPRC;

|PROCESO NoSale; |TIPO 11;
     |SI FSalida = 7; |Y Campo ! 2;
          |MENAV "0000DEBE COMPLETAR LA LINEA.";
          |ERROR;
     |FINSI;
|FPRC;
-----------------------------------------------------------------------------------
|PROCESO PartiSubcon;
     #3#0 = #0#4;
     #3#1 = #0#5;
     #3#2 = #6#1;
     #3#4 = #6#3;
     |LEE 101#3=;
     |SI FSalida = 0;
          #3#22 = #3#22 + #6#14;
          |SI #3#22 ] #3#7;
               #3#21 = "S";
          |FINSI;
          |GRABA 020#3a;
          |LIBERA #3;
     |FINSI;
|FPRC;

|DEFBUCLE PartiSubcon;
     #6#1;
     ;
     nNume, "                    ", 001;
     nNume, "zzzzzzzzzzzzzzzzzzzz", 999;
     ;
     NULL, PartiSubcon;
|FIN;

|PROCESO Subcontrata;
     nNume = #referen@#0;
     |BUCLE PartiSubcon;
|FPRC;

|DEFBUCLE Subcontrata;
     #referen@#3002;
     ;
     #0#4, #0#5;
     #0#4, #0#5;
     ;
     NULL, Subcontrata;
|FIN;

|PROCESO PonCero;
     #3#22 = 0;
     #3#21 = "N";
     |GRABA 020#3a;
     |LIBERA #3;
|FPRC;

|DEFBUCLE PonCero;
     #3#1;
     ;
     #0#4, #0#5, "                    ", 001;
     #0#4, #0#5, "zzzzzzzzzzzzzzzzzzzz", 999;
     be;
     NULL, PonCero;
|FIN;

|PROCESO SubTipo5; |TIPO 5; || Corrige #gopre003#22 Uni.Contratadas
     |SI #0#8 = "   "; #0#8 = "EUR"; |FINSI;

     nModo = (FEntrada / 100) ! 0;
     |SI nModo ] 3; |ACABA; |FINSI;

     |DDEF aAlfa;
     aAlfa = aAlfa + "gosubcoc";
     |CARGA_DEF aAlfa, referen@;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |BUCLE PonCero;
     |ABRE #3; |SELECT #2#3;
     |BUCLE Subcontrata;
     |CIERRA #3;
     |DESCARGA_DEF #referen@;
|FPRC;

|PROCESO AlbaranCta;
     #0#19 = #0#18;

     |ABRE #77;
     |SI #0#17 = 0; |Y nModo ! 3;
          |DDEFECTO #104;
          |ABRE #104;
          |SELECT #3#104;
          #104#3 = #0#4;
          #104#4 = #0#5;
          |LEE 020#104=;
          |CIERRA #104;

          #0#16 = #104#0; ||Serie = Serie Expe

          #77#50 = #0#16;
          enSwMenu = 1;
          |HAZ ContaAC7;

          #77#74 = #0#18; || A cta
          #77#34 = "AS";
          |ABRE #1;
          #1#0 = #0#4;
          #1#1 = #0#5;
          |LEE 020#1=;
          |CIERRA #1;
          #77#56 = #1#7;
          #77#57 = "B";
          #77#58 = #1#7;
          |GRABA 020#77n;
          |SI FSalida = 0;
               |ABRE #80;
               #80#27 = #77#50;
               #80#0 = #77#0;
               #80#1 = 1;
               |GRABA 020#80n;
               |CIERRA #80;
               enForma = 1; eaDocu = "agifa080"; |HAZ StockEntra;
          |FINSI;
          #0#17 = #77#0;
     |FINSI;
     |SI nModo ! 1;
          #77#50 = #0#16;
          #77#0 = #0#17;
          |LEE 101#77=;
          |SI FSalida = 0;
               |SI nModo = 3;
                    |BUCLELIN 1 NULL #77;
                    |BORRA 020#77a;
                    #0#16 = "";
                    #0#17 = 0;

                    #80#27 = #77#50;
                    #80#0 = #77#0;
                    #80#1 = 1;
                    enForma = -1; eaDocu = "agifa080"; |HAZ StockEntra;
               |SINO;
                    #77#74 = #0#18; || A cta
                    |GRABA 020#77a;
               |FINSI;
          |FINSI;
     |FINSI;

     |CIERRA #77;
     |PINTA #0#16;
     |PINTA #0#17;
|FPRC;
