|FICHEROS;
     conz0017 #0;
     conw0004 #4;   ||Tmp
     agifa019 #19;
     agifa065 #65;
|FIN;

|VARIABLES;
     nBtn1 = 0;
     nBtn2 = 0;
     fDesde = @;
     aAlfa = "";
     nNume = 0;
     aFin = "";
     aDato = "";
     aIP = "";
     aFic = "";
     c = "";
     aDes = "";
     nHandle = 0;
     aShell = "";
     aCadena = "";
     aLen = "";
     nPosi = 0;
     aCaracter = "";
     nCampo = 0;
|FIN;

|| para poner acentos el xml debe de tener
|| la linea <?xml version="1.0" encoding="iso-8859-1"?>


|PROCESO QuitaRaros;
     |QBF aAlfa;
     aCadena = "";
     aLen = aAlfa % 0;
     |PARA nCampo = 1;  |SI nCampo [ aLen;  |HACIENDO nCampo = nCampo + 1;
           nPosi = (100 * nCampo) + 1;
           aCaracter = aAlfa % nPosi;
           |SI aCaracter = "¥";  aCaracter = "N"; |FINSI; ||&209; |FINSI;
           |SI aCaracter = "¤";  aCaracter = "n"; |FINSI; ||&241; |FINSI;
           |SI aCaracter = " ";  aCaracter = "a"; |FINSI; ||&225; |FINSI;
           |SI aCaracter = "‚";  aCaracter = "e"; |FINSI; ||&233; |FINSI;
           |SI aCaracter = "¡";  aCaracter = "i"; |FINSI; ||&237; |FINSI;
           |SI aCaracter = "¢";  aCaracter = "o"; |FINSI; ||&243; |FINSI;
           |SI aCaracter = "£";  aCaracter = "u"; |FINSI; ||&250; |FINSI;
           |SI aCaracter = "§";  aCaracter = " "; |FINSI; ||&176; |FINSI;
           |SI aCaracter = "¦";  aCaracter = " "; |FINSI; ||&170; |FINSI;
           aCadena = aCadena + aCaracter;
     |SIGUE;
     aAlfa = aCadena;
|FPRC;

|PROCESO XmlCab;
     aDato = "<DealerStock>" + aFin;
     aDato = aDato + "<Header RecordType=" + c + "10" + c + " xmlns=" + c + c + ">" + aFin;
     aAlfa = #0#2; |QBF aAlfa;
     aDato = aDato + "<AccountCode>" + aAlfa + "</AccountCode>" + aFin;
     aDato = aDato + "<FileType>" + #0#3 + "</FileType>" + aFin;
     aDato = aDato + "</Header>" + aFin;
     |XGRABA nHandle, aDato;
|FPRC;

|PROCESO XmlLin;
     #19#0 = #4#0;
     |LEE 020#19=;

     aDato = "<Line RecordType=" + c + "20" + c + " xmlns=" + c + c + ">" + aFin;
     aAlfa = #4#0; |QBF aAlfa;
     aDato = aDato + "<PartNumber>" + aAlfa + "</PartNumber>" + aFin;
     aDato = aDato + "<Prefix>AG</Prefix>" + aFin;
     aAlfa = #19#1 % 125;
     |HAZ QuitaRaros;
     aDato = aDato + "<Description>" + aAlfa + "</Description>" + aFin;
     |XGRABA nHandle, aDato;
     aDato = "<FreeStock>" + #19#104 + "</FreeStock>" + aFin;
     aAlfa = (#4#1 % 102) + (#4#1 % 402) + (#4#1 % 704);
     aDato = aDato + "<DateLastPurchase>" + aAlfa + "</DateLastPurchase>" + aFin;
     aAlfa = (#4#2 % 102) + (#4#2 % 402) + (#4#2 % 704);
     aDato = aDato + "<DateLastSale>" + aAlfa + "</DateLastSale>" + aFin;
     nNume = -#4#3;
     aDato = aDato + "<RollingYearSales>" + nNume + "</RollingYearSales>" + aFin;
     aDato = aDato + "</Line>" + aFin;
     |XGRABA nHandle, aDato;
|FPRC;

|PROCESO XmlFin;
     aDato = "</DealerStock>" + aFin;
     |XGRABA nHandle, aDato;
|FPRC;

|DEFBUCLE XmlLin;
     #4#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, XmlLin;
|FIN;

|PROCESO agifa065;
     |SI #65#2 = "AV"; |O #65#2 = "BV"; |O #65#2 = "FC"; |O #65#2 = "OR";
          #4#3 = #4#3 + #65#6;
     |FINSI;
|FPRC;

|DEFBUCLE agifa065;
     #65#1;
     ;
     #4#0, fDesde, "  ", 000000, 00000, "     ";
     #4#0, #0#0  , "zz", 999999, 99999, "zzzzz";
     ;
     NULL, agifa065;
|FIN;

|PROCESO TmpAcu;
     |BUCLE agifa065;
     |GRABA 020#4a;
|FPRC;

|DEFBUCLE TmpAcu;
     #4#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, TmpAcu;
|FIN;

|PROCESO agifa019;
     |SI #19#104 > 0;
          #4#0 = #19#0;
          #4#1 = #19#26;
          #4#2 = #19#24;
          |GRABA 020#4n;
     |FINSI;
|FPRC;

|DEFBUCLE agifa019;
     #19#1;
     213;
     "                    ", #0#1;
     "zzzzzzzzzzzzzzzzzzzz", #0#1;
     ;
     NULL, agifa019;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     aFin = &13; aFin = aFin + &10;
     c = &34;

     aDes = #0#4; |QBF aDes;
     |XABRE "W", aDes, nHandle;
     |SI FSalida = 0;
          |XCIERRA nHandle;
     |SINO;
          |MENAV "0000Error al crear fichero...";
          |ACABA;
     |FINSI;

     |DFICE aFic;
     aFic = aFic + Usuario;
     |XABRE "BW", aFic, nHandle;
     |SI FSalida ] 0;
          aAlfa = #0#0 % 704;
          nNume = aAlfa;
          nNume = aAlfa - 1;
          aAlfa = (#0#0 % 106) + nNume;
          fDesde = aAlfa;

          aAlfa = Usuario;
          |NOME_DAT #4 aAlfa; |DELINDEX #4;
          |ABRE #4;
          |BUCLE agifa019;
          |BUCLE TmpAcu;
          |CIERRA #4;

          |HAZ XmlCab;
          |ABRE #19;
          |BUCLE XmlLin;
          |CIERRA #19;
          |HAZ XmlFin;
          |XCIERRA nHandle;

          aShell = #0#5; |QBF aShell;
          |SI aShell ! "";
               aShell = aShell + " " + c + aDes + c;
          |FINSI;
          |QBF aIP;
          |IP_REMOTO aIP;
          |SI aIP = "";
               |COPIA_FICHERO aFic, aDes;
               |SI aShell ! ""; |SYSTEM aShell; |FINSI;
          |SINO;
               |ENVIA_FICHERO aFic, aDes;
               |SI aShell ! ""; |RSYSTEM aShell; |FINSI;
          |FINSI;
          |FBORRA aFic;
          |DELINDEX #4;
     |SINO;
          |MENAV "0000Error al crear fichero temporal...";
     |FINSI;
|FPRC;

|PROCESO Browse; |TIPO 0;
     |SI FSalida ! 10; |ACABA; |FINSI;

     aAlfa = #0Campo; |QBF aAlfa;
     |SI aAlfa = "";
          |SI Campo = 4;
               aAlfa = "F*.pdf";
          |SINO;
               aAlfa = "F*.bat";
          |FINSI;
     |SINO;
          aAlfa = "F" + aAlfa;
     |FINSI;

     |BROWSE aAlfa;

     aAlfa = aAlfa % 2; |QBF aAlfa;
     |SI aAlfa = "F"; aAlfa = ""; |FINSI;

     |QBF aAlfa;
     |SI aAlfa = "";
          |ERROR;
     |SINO;
          #0Campo = aAlfa;
          |PINTA #0Campo;
     |FINSI;
|FPRC;

|PROCESO Btn;
     |PTEC 824;
|FPRC;

|PROGRAMA;
     |CONTROL_SIMPLE 0, "BOTON,{{137}}Fichero", 1506, 1513, Btn;
     nBtn1 = FSalida;
     |CONTROL_SIMPLE 0, "BOTON,{{137}}Ejecuta", 1606, 1613, Btn;
     nBtn2 = FSalida;
     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0; |GRABA 020#0a; |FINSI;
     |CIERRA #0;
     |FIN_CONTROL_WINDOWS nBtn1;
     |FIN_CONTROL_WINDOWS nBtn2;
|FPRO;
