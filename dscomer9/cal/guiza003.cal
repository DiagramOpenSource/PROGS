     Usado por guiz0003 y guiz0005

|FICHEROS;
     guim0003 #3;
     guim0004 #4;
     guim0005 #5;
     guim0006 #6;
     dsm00020 #20; || Agencia
     dsm00003 #30; || Dir Ent
     agifa025 #25;
     agifa019 #19;
     agifa024 #24;
     dsm00053 #53;
     agifa073 #73;
     infom003 #103;
     agifa104 #104;
     agifa324 #324;

     infom000 #100;
     agifa142 #142;
     agifa321 #321;
|FIN;

|VARIABLES;
     &enSwRecupera = 0;
     &enPedido = 0;
     &eaParam = "";

     nLineasGes = 0;
     nLineasPed = 0;
     nLineasPre = 0;
     aCliDs = "";
     aMensa = "";
     &enForma = 0;
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa = "";
     aCod = "";
     nLin = 0;
     nUltLin = 0;
     nSwPrecio = 0;
     nSwArticu = 0;
     fmes = "";
     mes = 0;
     nImpo = 0;
     aArt = "";
     aDes = "";
     nUni = 0;
     nPre = 0;
     {99}Valor = "";
     nCampo = 0;
     aLon = "";
     aDato = "";
     aReg = "";
     aTab = "";
     i = 0;
     nPos = 0;
     aLetra = "";
     nLetra = 0;
     aCadena = "";
|FIN;

|PROCESO QuitaRaros;
     aCadena = "";
     |QBT aAlfa;
     aLon = aAlfa % A0;
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aLetra = aAlfa % nPos;
          nLetra = &aLetra;
          |SI nLetra < 48; |O nLetra > 122; aLetra = ""; |FINSI;
          |SI nLetra ] 58; |Y nLetra [ 64; aLetra = ""; |FINSI;
          |SI nLetra ] 91; |Y nLetra [ 96; aLetra = ""; |FINSI;
          aCadena = aCadena + aLetra;
     |SIGUE;
     aAlfa = aCadena;
|FPRC;

|PROCESO Dato;
     nCampo = 1;
     IValor = Valor1<-;
     aLon = aDato % A0;
     aReg = "";
     aTab = "#";
     |PARA i = 1; |SI i [ aLon; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aLetra = aDato % nPos;
          |SI aLetra = aTab;
               Valor = aReg;
               aReg = "";
               nCampo = nCampo + 1;
               IValor = IValor + 1;
          |SINO;
               aReg = aReg + aLetra;
          |FINSI;
          |SI nCampo > 90; |SAL; |FINSI;     || Por si.... :)
     |SIGUE;
     Valor = aReg;
|FPRC;

|PROCESO NueDirEntre;
     aAlfa = #3#7;
     aAlfa = aAlfa - "#";
     |SI FSalida ! 0;
          aDato = "#########"; |HAZ Dato;
          aDato = #3#7; |QBF aAlfa; |HAZ Dato;
          #104#19 = Valor1;
          #104#14 = Valor2;
          #104#59 = Valor3;
          #104#20 = Valor4;
          #104#21 = Valor5;

          #104#15 = Valor6;   || Per.Contacto
          #104#51 = Valor8;   || Movil
          aAlfa = Valor8; |QBF aAlfa;
          |SI aAlfa = "";
               #104#51 = Valor7; ||Tlf
          |FINSI;
     |SINO;
          |DDEFECTO #30;
          |ABRE #30;
          #30#0 = #104#4;
          #30#1 = #3#11;
          |LEE 000#30=;
          |CIERRA #30;
          #104#57 = #30#1;
          #104#19 = #30#10;
          #104#14 = #30#2;
          #104#20 = #30#3;
          #104#21 = #30#7;
          #104#59 = #30#6;
          |SI #30#14 = "  ";
              #104#7 = #24#25;
          |SINO;
              #104#7 = #30#14;
          |FINSI;

          aAlfa = #3#7; |QBT aAlfa; aAlfa = aAlfa > "";
          |SI aAlfa = "RECOGERENAGENCIA";
               #104#14 = #3#7;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO BuscaRepre;
     aAlfa = #3#6; |HAZ QuitaRaros; aAlfa1 = aAlfa;
     aCod = "";
     |ABRE #25;
     |LEE 000#25p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          aAlfa = #25#1; |HAZ QuitaRaros; aAlfa2 = aAlfa;;
          |SI aAlfa2 = aAlfa1;
               aCod = #25#0; |SAL;
          |FINSI;
          |LEE 000#25s;
     |SIGUE;
     |DDEFECTO #25;
     #25#0 = aCod;
     |LEE 000#25=;
     |CIERRA #25;
|FPRC;

|PROCESO BuscaAgencia;
     aAlfa = #3#8; |HAZ QuitaRaros; aAlfa1 = aAlfa;
     aCod = 0;
     |ABRE #20;
     |LEE 000#20p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          aAlfa = #20#1; |HAZ QuitaRaros; aAlfa2 = aAlfa;;
          |SI aAlfa2 = aAlfa1;
               aCod = #20#0; |SAL;
          |FINSI;
          |LEE 000#20s;
     |SIGUE;
     |DDEFECTO #20;
     #20#0 = aCod;
     |LEE 000#20=;
     |CIERRA #20;
|FPRC;

|PROCESO CreaLinPed0;
     |DDEFECTO #19;
     |ABRE #19;
     #19#0 = aArt;
     |LEE 000#19=;
     |CIERRA #19;
     |QBF aDes;
     |SI aDes = ""; aDes = #19#1; |FINSI;

     nLin = nLin + 1;
     |DDEFECTO #73;
     #73#28 = #104#25;
     #73#0 = #104#0;
     #73#1 = nLin;
     #73#2 = #19#0;
     #73#3 = 1;     ||Almacen
     #73#4 = aDes;
     #73#5 = nUni;
     #73#42 = nUni;
     #73#6 = nPre;
     #73#13 = #104#2;
     |SI #24#28 = "S";
          #73#14 = 0;
     |SINO;
          #73#14 = #19#30;
     |FINSI;
     #73#16 = #104#7;
     #73#18 = #19#2;
     #73#19 = #104#18;
     #73#20 = #104#4;
     #73#23 = #104#5;
     #73#24 = #104#6;
     #73#25 = #104#17;
     #73#26 = #104#9;
     #73#38 = nPre;
     #73#45 = #4#10;
     |GRABA 040#73n;
     |SI FSalida ! 0;
          |SI enSwRecupera = 0;
               aAlfa = "0000Pedido Existente: " + #73#28 + "/" + #73#0 + "/" + #73#1;;
               |SI eaParam = ""; |MENAV aAlfa; |SINO; |MENSA aAlfa; |FINSI;
          |FINSI;
     |SINO;
          enForma = 1;
          |HAZ AcumulaPV;

          #53#0 = "P";
          #53#1 = #73#28;
          #53#2 = #73#0;
          #53#3 = #73#1;
          #53#4 = 1;
          #53#5 = #4#7;
          |GRABA 040#53n;
     |FINSI;
|FPRC;

|PROCESO CreaLinPed;
     |SI #4#1 = nUltLin;
          aAlfa = #4#9; |QBF aAlfa;
          |SI aAlfa = "PAGADOS"; |ACABA; |FINSI;
          aArt = "9990";
          aDes = "";
          nUni = 1;
          nPre = #4#9;
          |HAZ CreaLinPed0;
          |ACABA;
     |FINSI;
     aArt = #4#2;
     aDes = #4#4;
     nUni = #4#5;
     nPre = #4#6;
     |HAZ CreaLinPed0;
     |SI #4#8 ! 0;
          |DDEFECTO #19;
          |ABRE #19;
          #19#0 = aArt;
          |LEE 000#19=;
          |CIERRA #19;

          aArt = #19#127;
          aDes = "";
          nUni = #4#5;
          nPre = #4#8;
          |HAZ CreaLinPed0;
     |FINSI;
|FPRC;

|PROCESO CreaCabPed;
     nLin = 0;
     ||HAZ BuscaRepre;
     |HAZ BuscaAgencia;
     |DDEFECTO #24;
     #24#0 = #3#1;
     |LEE 000#24=;
     |DDEFECTO #25;
     #25#0 = #24#25;
     |LEE 000#25=;

     |DDEFECTO #104;
     #104#25 = "";
     #104#0 = #3#0;
     #104#1 = #3#4;
     #104#2 = #3#4;
     #104#4 = #24#0;
     #104#5 = #24#37;
     #104#6 = #24#38;
     #104#7 = #25#0;
     #104#8 = #24#2;
     |SI #3#12 = 0;
          #104#9 = #24#20;
     |SINO;
          #104#9 = #100#20;
     |FINSI;
     #104#14 = #3#7;
     #104#16 = #25#7;
     #104#17 = #24#156;
     #104#18 = #24#4;
     #104#19 = #24#2;
     #104#20 = #24#6;
     #104#21 = #24#7;
     #104#22 = #24#47;
     #104#23 = #24#41;
     #104#35 = #24#192;     || Divisa

     #324#0 = #104#35;
     |LEE 000#324=;

     #104#36 = #324#2;     || Cambio
     #104#37 = #24#193;    || Moneda
     #104#40 = #321#9;     || Base

     #324#0 = #104#40;
     |LEE 000#324=;

     #104#41 = #324#2;     || Cambio

     #104#60 = #20#0;
     #104#61 = "P";   || #24#208;  cambiado segun 1980/1531
     #104#64 = #24#15;

     |HAZ NueDirEntre;

     |GRABA 040#104b;
     |SI FSalida ! 0;
          |SI enSwRecupera = 0;
               aAlfa = "0000Pedido Existente: " + #104#25 + "/" + #104#0;
               |SI eaParam = ""; |MENAV aAlfa; |SINO; |MENSA aAlfa; |FINSI;
          |SINO;
               |LEE 121#104=;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CreaLinPrepe0;
     |DDEFECTO #19;
     #19#0 = aArt;
     |LEE 000#19=;
     |QBF aDes;
     |SI aDes = ""; aDes = #19#1; |FINSI;

     |HAZ CheqPrecio;
     nLin = nLin + 1;
     |DDEFECTO #6;
     #6#0 = #5#0;
     #6#1 = nLin;
     #6#2 = aArt;
     #6#3 = aDes;
     #6#4 = nUni;
     #6#5 = nPre;
     #6#6 = #4#7;
     #6#7 = aMensa;
     #6#8 = #4#10;
     |GRABA 040#6n;
     |SI FSalida ! 0;
          |SI enSwRecupera = 0;
               aAlfa = "0000Linea Prepedido existente:" + #3#0 + "/" + #3#1;
               |SI eaParam = ""; |MENAV aAlfa; |SINO; |MENSA aAlfa; |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CreaLinPrepe;
     |SI #4#1 = nUltLin;
          aAlfa = #4#9; |QBF aAlfa;
          |SI aAlfa = "PAGADOS"; |ACABA; |FINSI;
          aArt = "9990";
          |DDEFECTO #19;
          #19#0 = aArt;
          |LEE 000#19=;
          aDes = #19#1;
          nUni = 1;
          nPre = #4#9;
          |HAZ CreaLinPrepe0;
          |ACABA;
     |FINSI;

     aArt = #4#2;
     |DDEFECTO #19;
     #19#0 = aArt;
     |LEE 000#19=;
     aDes = #4#4;
     nUni = #4#5;
     nPre = #4#6;
     |HAZ CreaLinPrepe0;
     |SI #4#8 ! 0;
          |DDEFECTO #19;
          #19#0 = aArt;
          |LEE 000#19=;
          aArt = #19#127;
          |DDEFECTO #19;
          #19#0 = aArt;
          |LEE 000#19=;

          aDes = #19#1;
          nUni = #4#5;
          nPre = #4#8;
          |HAZ CreaLinPrepe0;
     |FINSI;
|FPRC;

|PROCESO CreaCabPrepe;
     nLin = 0;
     ||HAZ BuscaRepre;
     |HAZ BuscaAgencia;
     |DDEFECTO #24;
     #24#0 = #3#1;
     |LEE 000#24=;
     |DDEFECTO #25;
     #25#0 = #24#25;
     |LEE 000#25=;
     |DDEFECTO #5;
     #5#0 = #3#0;
     #5#1 = #3#4;
     #5#2 = #3#1;
     #5#3 = #3#3;
     #5#4 = #25#0;
     #5#5 = #3#7;
     #5#6 = #20#0;
     #5#7 = #3#10;
     #5#8 = #3#11;
     #5#9 = #3#12;
     |GRABA 040#5n;
     |SI FSalida ! 0;
          |SI enSwRecupera = 0;
               aAlfa = "0000Prepedido existente:" + #3#0;
               |SI eaParam = ""; |MENAV aAlfa; |SINO; |MENSA aAlfa; |FINSI;
          |SINO;
               |LEE 121#5=;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CheqPrecio;
     aMensa = "";
     aAlfa = #4#9; |QBF aAlfa;
     |SI aAlfa ! ""; |ACABA; |FINSI;

  ||   |SI nUltLin = #4#1; |ACABA; |FINSI;

     #19#0 = #4#2;
     |LEE 000#19=;

     |SI #4#6 < #19#9; |O #4#6 < #19#28; |O FSalida ! 0;
          |SI FSalida ! 0;
               aAlfa = "0000No existe articulo:" + #19#0;
               |SI eaParam = ""; |MENAV aAlfa; |SINO; |MENSA aAlfa; |FINSI;
               aMensa = "No existe articulo";
               nSwArticu = 1;
          |SINO;
               aAlfa = "0000Precio inferior al de venta:" + #19#0;
               |SI eaParam = ""; |MENAV aAlfa; |SINO; |MENSA aAlfa; |FINSI;
               aMensa = "Precio inferior al de venta";
               nSwPrecio = 1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO UltimaLinea;
     |ABRE #4;
     #4#0 = #3#0;
     #4#1 = 999;
     |LEE 000#4];
     |SI FSalida = 0;
          |LEE 000#4a;
     |SINO;
          |LEE 000#4u;
     |FINSI;
     |SI FSalida = 0; |Y #4#0 = #3#0;
          nUltLin = #4#1;
     |SINO;
          nUltLin = 0;
     |FINSI;
     |CIERRA #4;
|FPRC;

|PROCESO ActCli;
     #19#0 = #73#2;
     |LEE 020#19=;

     #24#0 = #104#4;
     |LEE 101#24=;
     |SI FSalida = 0;
          |SI #19#194 = 2;
               nImpo = ((((#73#44 * #73#6) < #73#8) < #73#29) < #104#5) < #104#17;
          |SINO;
               nImpo = ((((#73#5 * #73#6) < #73#8) < #73#29) < #104#5) < #104#17;
          |FINSI;
          |SI #142#115 = "S";
               nImpo = nImpo < #104#6;
          |FINSI;
          fmes = #104#1 % A402;
          mes = fmes - 1;
          mes = mes * 3;
          mes = mes + 114;
          #24mes = #24mes   + nImpo;     || VENTA.
          #24#150 = #24#150 + nImpo;
          |GRABA 020#24a;
     |FINSI;
|FPRC;

|PROCESO TieneCli;
     aCliDs = "";
     |ABRE #103;
     #103#0 = #3#10;
     |LEE 000#103=;
     |SI FSalida = 0; |Y #103#2 ! "000000"; |Y #103#2 ! "      ";
          aCliDs = #103#2;
     |FINSI;
     |CIERRA #103;
|FPRC;

|PROCESO CuentaLinPre;
     nLineasPre = nLineasPre + 1;
|FPRC;

|PROCESO CuentaLinGes;
     nLineasGes = nLineasGes + 1;
|FPRC;

|PROCESO CuentaLinPed;
     |SI #4#1 = nUltLin;
          aAlfa = #4#9; |QBF aAlfa;
          |SI aAlfa = "PAGADOS"; |ACABA; |FINSI;

          nLineasPed = nLineasPed + 1;
          |ACABA;
     |FINSI;

     nLineasPed = nLineasPed + 1;

     |SI #4#8 ! 0;
          nLineasPed = nLineasPed + 1;
     |FINSI;
|FPRC;

|DEFBUCLE CuentaLinGes;
     #73#1;
     ;
     "     ", #3#0, 001;
     "     ", #3#0, 999;
     ;
     NULL, CuentaLinGes;
|FIN;

|DEFBUCLE CuentaLinPed;
     #4#1;
     ;
     #3#0, 001;
     #3#0, 999;
     ;
     NULL, CuentaLinPed;
|FIN;

|DEFBUCLE CuentaLinPre;
     #6#1;
     ;
     #3#0, 001;
     #3#0, 999;
     ;
     NULL, CuentaLinPre;
|FIN;

|PROCESO Pedido;
     nLineasPed = 0;
     nLineasGes = 0;

     |ABRE #5;
     |ABRE #6;
     |ABRE #104;
     |ABRE #73;
     |HAZ UltimaLinea;
     |ABRE #19;
     |BUCLELIN 2CheqPrecio#3;
     |CIERRA #19;
     |ABRE #25;
     |ABRE #24;

     |SI #3#1 = "000000"; |O #3#1 = "      ";
          |HAZ TieneCli;
     |SINO;
          aCliDs = #3#1;
     |FINSI;

     |SI nSwArticu = 0; |Y aCliDs ! "";
          #3#1 = aCliDs;
          |GRABA 020#3a;

          |ABRE #53;
          |HAZ CreaCabPed;
          |SI FSalida = 0;
               |BUCLELIN 2CreaLinPed#3;
               |HAZ CalCabAgifa104;
               |ABRE #19;
               |BUCLELIN 2ActCli#104;
               |CIERRA #19;

          |FINSI;
          |CIERRA #53;

          |BUCLE CuentaLinPed;
          |BUCLE CuentaLinGes;
          |SI nLineasGes = nLineasPed;
               #3#13 = "G";
               |GRABA 020#3a;
          |FINSI;
     |FINSI;
     |SI nSwArticu = 1; |O aCliDs = ""; |O nSwPrecio = 1;
          |HAZ CreaCabPrepe;
          |SI FSalida = 0;
               |ABRE #19;
               |BUCLELIN 2CreaLinPrepe#3;
               |CIERRA #19;
               aAlfa = "0000Creado Prepedido:" + #5#0;
               |SI eaParam = ""; |MENAV aAlfa; |SINO; |MENSA aAlfa; |FINSI;
          |FINSI;

          |BUCLE CuentaLinPed;
          |BUCLE CuentaLinPre;
          |SI nLineasPed = nLineasPre;
               #3#13 = "P";
               |GRABA 020#3a;
          |FINSI;
     |FINSI;
     |CIERRA #24;
     |CIERRA #25;
     |CIERRA #73;
     |CIERRA #104;
     |CIERRA #6;
     |CIERRA #5;
|FPRC;
