|FICHEROS;
  con00642 #0;
  con00637 #1;   || Cabecera
  con00625 #2;   || Ordenes de Reparacion
  con00626 #3;   || Lineas de OR
  agifa019 #5;  || Articulos
  agifa010 #10;
  con00683 #19;
  agifa007 #23;
  agifa024 #30;
  con00603 #63;  || Maestro M.Obra
  con00606 #66;  || Maestro S.Exteriores
  agifa071 #71;
  con00612 #612;
  con00696 #696; || Lines OR
  agifa142 #142;
|FIN;

|VARIABLES;
     aArti = "";
     fFecha = @;
     nMargen = 0;
     nCampo = 0;
     nImpo = 0;
     nUni = 0;
     &aDivisa = "";
     &aMoneda = "";
     nError = 0;
     &enTiva = 0;
     &efFiva = @;
     &eaDiva = "";
     &enIva = 0;
     &enRec = 0;
     &enErrCarte = 0;
     &eaSerie = "";
     &eaDocu = 0;
     expor = "N";
     nn = 0;
     tipo_iva = 0;
     nForma = 0;
     aAlfa = "";
     nOk = 0;
|FIN;

|PROCESO ActuaArti;
     |ABRE #5;
     #5#0 = aArti;
     |LEE 101#5=;
     |SI FSalida = 0;
          |SI nForma = 1;
               #5#24 = fFecha;
               #5#25 = nUni;
          |FINSI;
          #5#95 = #5#95 + (nImpo * nForma);
          #5#96 = #5#96 + (nMargen * nForma);
          aAlfa = fFecha % 402;
          nCampo = ((N\aAlfa) - 1) * 5 + 35;
          #5nCampo = #5nCampo + (nImpo * nForma);
          nCampo = nCampo + 1;
          #5nCampo = #5nCampo + (nMargen * nForma);
          |GRABA 020#5a;
     |FINSI;
     |CIERRA #5;
|FPRC;

|PROCESO Totales;
     nOk = 0;
     |HAZ EsTallerSC;
     |SI FSalida = 0;
          |SI #3#14 ! "S"; nOk = 1; |FINSI;
     |SINO;
          |SI #3#14 = "N"; |O #3#14 = "I"; nOk = 1; |FINSI;
     |FINSI;
     |SI nOk = 0; |ACABA; |FINSI;

||     |SI #3#14 = "N";
          |SI #3#11 ] 1; |Y #3#11 [ 999; ||\\ mano de obra;
               #1#19 = #1#19 + (#3#8 * nForma);
               #0#5 = #0#5 + (#3#8 * nForma);
               #63#0 = #3#2;
               |LEE 000#63=;
               |SI FSalida ! 0; |DDEFECTO #63; |FINSI;
               tipo_iva = #63#5;
          |FINSI;
          |SI #3#11 ] 1000; |Y #3#11 [ 1999; ||Piezas;
               #1#16 = #1#16 + (#3#8 * nForma);
               #0#5 = #0#5 + (#3#8 * nForma);
               #5#0 = #3#2;
               |LEE 000#5=;
               |SI FSalida ! 0 ; |DDEFECTO #5; |FINSI;
               tipo_iva = #5#30;
          |FINSI;
          |SI #3#11 ] 2000; |Y #3#11 [ 2999; || S.exte;
               #1#18 = #1#18 + (#3#8 * nForma);
               #0#5 = #0#5 + (#3#8 * nForma);
               #66#0 = #3#2;
               |LEE 000#66=;
               |SI FSalida ! 0 ; |DDEFECTO #66; |FINSI;
               tipo_iva = #66#4;
          |FINSI;
          |SI #3#11 ] 3000; |Y #3#11 [ 3999; ||lubricante;
                #1#17 = #1#17 + (#3#8 * nForma);
                #0#5 = #0#5 + (#3#8 * nForma);
                #5#0 = #3#2;
                |LEE 000#5=;
                |SI FSalida ! 0 ; |DDEFECTO #5; |FINSI;
                tipo_iva = #5#30;
          |FINSI;
          ||\\ EN FUNCION DEL TIPO DE IVA ACUMULAR EN BASE 1 O BASE 2
          |SI #0#0 = #19#11; |O #0#0 = #19#12; |O #0#0 = #19#13;
                    |O #0#0 = #19#17; |O #0#0 = #19#18; |O #0#0 = #19#19;
                    |O #0#0 = #19#21; |O #0#0 = #19#22; |O #0#0 = #19#23;
                    |O #0#0 = #19#24; |O #0#0 = #19#25; |O #0#0 = #19#26;
                    |O #0#0 = #19#27; |O #0#0 = #19#28; |O #0#0 = #19#29;
                    |O #0#0 = #19#30; |O #0#0 = #19#31; |O #0#0 = #19#32;
                    |O #0#0 = #19#33; |O #0#0 = #19#34; |O #0#0 = #19#35;
               tipo_iva = "0";
          |FINSI;

          |SI expor = "N";
               enTiva = tipo_iva;
               efFiva = #1#2;
               |HAZ SacaIVA;

               |SI tipo_iva = 1;
                    #1#7 = #1#7 + (#3#8 * nForma);
                    #1#8 = tipo_iva;
                    #1#9 = enIva;
                    #0#6 = #0#6 + ((#3#8 %  #1#9) * nForma);
                    #1#10 = #1#10 + ((#3#8 % #1#9) * nForma);
               |FINSI;
               |SI tipo_iva = 2;
                    #1#23 = #1#23 + (#3#8 * nForma);
                    #1#24 = tipo_iva;
                    #1#25 = enIva;
                    #0#6 = #0#6 + ((#3#8 %  #1#25) * nForma);
                    #1#26 = #1#26 + ((#3#8 %  #1#25) * nForma);
              |FINSI;
              |SI tipo_iva = 0;
                  #1#6 = #1#6 + (#3#8 * nForma);
              |FINSI;
          |SINO;
               #1#6 = #1#6 + (#3#8 * nForma);
          |FINSI;
          #0#7 = #0#5 + (#0#6 * nForma);
||     |FINSI;
/*
     |SI #3#14 = "I";    ||importe interno
          |SI #2#7 = "VN"; |O #2#7 = "VO";
               #1#11 = #1#11 + (#3#8 * nForma);
          |FINSI;
          #0#5 = #0#5 + (#3#8 * nForma);
          #0#7 = #0#6 + #0#5;
     |FINSI;
*/
     #1#11 = #1#6+#1#7+#1#10+#1#23+#1#26+#1#31+#1#34+#1#37+#1#32+#1#35+#1#38;

     |SI #3#28 = -1;          ||Linea Canon
          enTiva = tipo_iva;
          efFiva = #1#2;
          |HAZ SacaIVA;
          |SI expor = "N";
               #1#30 = #1#30 + #3#8;
          |SINO;
               #1#30 = #1#30 + (#3#8 + (#3#8 % enIva));
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO sumaf;
     |HAZ Totales;
     #696#0 = #0#0;
     #696#1 = #0#1;
     #696#2 = #0#2;
     #696#3 = #3#0;
     #696#4 = #3#1;
     #696#5 = #3#11;
     #696#6 = #1#2;
     #696#7 = #2#2;
     |GRABA 020#696n;

     #3#35 = #0#0;
     #3#36 = #0#1;
     #3#37 = #1#2;
     #3#38 = "F";

     fFecha = #1#2;
     aArti = #3#2;
     nUni = #3#4;
     |SI #3#9 = 2; |O #3#9 = 4;
          |SI #3#14 = "G"; |O #3#14 = "S";
               nMargen = -#3#17;
               nImpo = 0;
          |SINO;
               nMargen = #3#8 - #3#17;
               nImpo = #3#8;
          |FINSI;
          |HAZ ActuaArti;
     |FINSI;

     |GRABA 020#3a;
     |LIBERA #3;
|FPRC;

|DEFBUCLE suma_fac;
     #3#1;
     ;
     #0#3 , #0#4 ,    1;
     #0#3 , #0#4 , 9999;
     be;
     NULL , sumaf;
|FIN;

|PROCESO calcula_fac;
     |ABRE #63;
     |ABRE #66;
     |ABRE #5;
     |ABRE #696;
     |BUCLE suma_fac;   ||recorre las lineas de la orden de reparacion
     |CIERRA #696;
     #2#16 = 1;
     #2#17 = #1#2;
     |GRABA 010#2a;
     |CIERRA #63;
     |CIERRA #66;
     |CIERRA #5;
|FPRC;

|PROCESO FacParcial;
     |SI #3#36 ! 0;
          |ERROR10;
          nError = 1;
     |FINSI;
|FPRC;

|DEFBUCLE FacParcial;
     #3#1;
     ;
     #0#3, #0#4, 0001;
     #0#3, #0#4, 9999;
     ;
     NULL, FacParcial;
|FIN;

|PROCESO PonIVA;
     |PARA enTiva = 1; |SI enTiva [ 5; |HACIENDO enTiva = enTiva + 1;
          efFiva = #1#2;
          |HAZ SacaIVA;
          |SI enTiva = 1; |Y #1#10 ! 0;  #1#9 = enIva; |FINSI;
          |SI enTiva = 2; |Y #1#26 ! 0; #1#25 = enIva; |FINSI;
          |SI enTiva = 3; |Y #1#32 ! 0; #1#33 = enIva; |FINSI;
          |SI enTiva = 4; |Y #1#35 ! 0; #1#36 = enIva; |FINSI;
          |SI enTiva = 5; |Y #1#38 ! 0; #1#39 = enIva; |FINSI;
     |SIGUE;
|FPRC;

|PROCESO SumaAlb;
     #1#30 = #1#30 + (#10#96 * nForma);
     #1#6 = #1#6 + (#10#28 * nForma);
     #1#7 = #1#7 + (#10#16 * nForma);
     #1#10 = #1#10 + (#10#17 * nForma);
     #1#23 = #1#23 + (#10#19 * nForma);
     #1#26 = #1#26 + (#10#20 * nForma);
     #1#31 = #1#31 + (#10#22 * nForma);
     #1#32 = #1#32 + (#10#23 * nForma);
     #1#34 = #1#34 + (#10#44 * nForma);
     #1#35 = #1#35 + (#10#45 * nForma);
     #1#37 = #1#37 + (#10#47 * nForma);
     #1#38 = #1#38 + (#10#48 * nForma);
     #1#11 = #1#6+#1#7+#1#10+#1#23+#1#26+#1#31+#1#34+#1#37+#1#32+#1#35+#1#38;
|FPRC;

|PROCESO Recal10;
     fFecha = #1#2;
     aArti = #71#2;
     nUni = #71#5;
     nImpo = ((((#71#5 * #71#6) < #71#8) < #71#33) < #10#5) < #10#32;
     |SI #142#115 = "S";
          nImpo = nImpo < #10#7;
     |FINSI;
     |SI #10#57 = "S";
          nImpo = -nImpo;
          nMargen = nImpo + #71#14;
     |SINO;
          nMargen = nImpo - #71#14;
     |FINSI;
     |HAZ ActuaArti;

     |SI nForma = 1;
          |HAZ CalCabAgifa010;
     |FINSI;
|FPRC;

|PROCESO alta;
     |SI #0#10 = "A";
          #10#52 = #0#3;
          #10#0 = #0#4;
          |LEE 101#10=;
          |SI #10#38 = "S";
               |MENAV "0000Albaran facturado...";
               |ERROR;
          |SINO;
               #612#0 = #10#52;
               #612#1 = #10#0;
               |LEE 000#612=;
               |SI FSalida ! 0; |DDEFECTO #612; |FINSI;
               |SI #612#3 ! "      ";
                    |MENAV "0000Albaran asociado a OR...";
                    |ERROR;
               |SINO;
                    |SI #10#3 ! #1#3;
                         aAlfa = "0000Albaran del cliente: " + #10#3
                         |MENAV aAlfa;
                         |ERROR;
                    |SINO;
                         #10#38 = "S";
                         #10#53 = #0#0;
                         #10#39 = #0#1;
                         #10#60 = #1#2;
                         |GRABA 020#10a;

                         aDivisa = #10#68;
                         aMoneda = #10#70;
                         |HAZ Divisas010;

                         |HAZ LimCabAgifa010;
                         |BUCLELIN 0Recal10#10;

                         #0#5 = #10#15;
                         #0#6 = #10#24;
                         #0#7 = #10#67;
                         #0#8 = #1#2;
                         #0#9 = #10#1;
                         |HAZ SumaAlb;
                    |FINSI;
               |FINSI;
          |FINSI;
          |LIBERA #10;
     |SINO;
          ||comprobamos que es una or aparcada
          #2#0 = #0#3;
          #2#1 = #0#4;
          |LEE 010#2=;
          |SI FSalida = 0;
               |SI #2#16 ! 999999;
                    |ERROR;
                    |MENAV "0000LA ORDEN NO ESTA APARCADA";
               |SINO;
                    nError = 0;
                    |BUCLE FacParcial;
                    |SI nError = 0;
                         |HAZ calcula_fac;
                    |SINO;
                         |MENAV "0000LA ORDEN ESTA PARCIALMENTE FACTURADA";
                         |ERROR;
                    |FINSI;
               |FINSI;
          |SINO;
               |ERROR;
               |MENAV "0000LA ORDEN NO EXISTE";
          |FINSI;
          |LIBERA #2;
     |FINSI;
|FPRC;

|PROCESO RecorreLin2;
     #3#0 = #696#3;
     #3#1 = #696#4;
     #3#11 = #696#5;
     |LEE 121#3=;
     |SI FSalida = 0;
          |HAZ Totales;

          #2#0 = #3#0;
          #2#1 = #3#1;
          |LEE 020#2=;

          fFecha = #1#2;
          aArti = #3#2;
          nUni = #3#4;
          |SI #3#9 = 2; |O #3#09 = 4;
               |SI #3#14 = "G"; |O #3#14 = "S";
                    nMargen = -#3#17;
                    nImpo = 0;
               |SINO;
                    nMargen = #3#8 - #3#17;
                    nImpo = #3#8;
               |FINSI;
               |HAZ ActuaArti;
          |FINSI;
          #3#35 = "";
          #3#36 = 0;
          #3#37 = "01.01.0000";
          |SI #2#16 = 999999;
              #3#38 = "A";
          |SINO;
               #3#38 = " ";
          |FINSI;
          |GRABA 020#3a;
          |LIBERA #3;
     |FINSI;
     |BORRA 000#696c;
     |LIBERA #696;
|FPRC;

|DEFBUCLE RecorreLin2;
     #696#1;
     ;
     #0#0, #0#1, #0#2, 0001;
     #0#0, #0#1, #0#2, 9999;
     be;
     NULL, RecorreLin2;
|FIN;

|PROCESO bajal;
     |SI #0#10 = "A";
          #10#52 = #0#3;
          #10#0 = #0#4;
          |LEE 101#10=;
          |SI FSalida = 0;
               #10#38 = "N";
               #10#53 = "";
               #10#39 = 0;
               #10#60 = "01.01.1900";
               |GRABA 020#10a;
               |LIBERA #10;

               aDivisa = #10#68;
               aMoneda = #10#70;
               |HAZ Divisas010;

               |BUCLELIN 0Recal10#10;

               |HAZ SumaAlb;
          |FINSI;
     |SINO;
          #2#0 = #0#3;
          #2#1 = #0#4;
          |LEE 101#2=;
          |SI FSalida = 0;
               #2#15 = "";        || Aparcamos la orden
               #2#16 = 999999;
               #2#17 = "01.01.0000";
               |GRABA 010#2a;
               |LIBERA #2;
          |FINSI;
          |ABRE #63;
          |ABRE #66;
          |ABRE #5;
          |ABRE #3;
          |ABRE #2;
          |BUCLE RecorreLin2;
          |CIERRA #2;
          |CIERRA #3;
          |CIERRA #5;
          |CIERRA #66;
          |CIERRA #63;
     |FINSI;
|FPRC;

|PROCESO linea;|TIPO 2;
     |ABRE #19; |LEE 000#19p; |CIERRA #19;
     nn = (FEntrada / 100) ! 0;
     nForma = 0 +. 1;
     expor = "N";
     |ABRE #10;
     |ABRE #612;
     |ABRE #2;
     |ABRE #30;
     #30#0 = #1#3;
     |LEE 000#30=;
     |SI FSalida = 0; |Y #30#28 = "S";
          expor = "S";
     |FINSI;
     |CIERRA #30;

     |ABRE #23;
     #23#0 = #0#0;
     |LEE 000#23=;
     |SI FSalida = 0; |Y #23#30 = "S";
          expor = "S";
     |FINSI;
     |CIERRA #23;

     |SI nn = 3;
          |HAZ bajal;
||          |BUCLE BorraLin2;   ||Porque dicen que algunaa veces no se borra!!
     |FINSI;
     |SI nn = 2;
          |SI nForma > 0;
               |HAZ alta;
               |PINTA #0#5;
               |PINTA #0#6;
               |PINTA #0#7;
          |SINO;
               |HAZ bajal;
          |FINSI;
     |FINSI;

     |SI nn = 1;
          |HAZ alta;
          |PINTA #0#5;
          |PINTA #0#6;
          |PINTA #0#7;
     |FINSI;

     |SI nForma = 1;
          |SI #0#10 = "A";
               #10#52 = #0#3;
               #10#0 = #0#4;
               |LEE 020#10=;
               #612#0 = #10#52;
               #612#1 = #10#0;
               aAlfa = #1#5 + #1#28 + #1#29; |QBF aAlfa;
               |SI FSalida = 0; |Y aAlfa = "";
                    #1#5 = #612#8;  |PINTA 1416, #1#5;
               |FINSI;
               #0#8 = #1#2;
               #0#9 = #10#1;
          |SINO;
               #2#0 = #0#3;
               #2#1 = #0#4;
               |LEE 020#2=;
               aAlfa = #1#5 + #1#28 + #1#29; |QBF aAlfa;
               |SI FSalida = 0; |Y aAlfa = "";
                    #1#5 = #2#3;  |PINTA 1416, #1#5;
                    #1#28 = #2#30; |PINTA 1437, #1#28;
                    #1#29 = #2#20; |PINTA 1455, #1#29;
               |FINSI;
               #0#8 = #1#2;
               #0#9 = #2#2;
          |FINSI;
     |FINSI;

     |CIERRA #2;
     |CIERRA #10;
     |CIERRA #612;

     |HAZ PonIVA;
|FPRC;

|PROCESO ModiOrden; |TIPO 0;
     |SI enErrCarte ! 0;      || Modifica Orden cuando hay recibos
          eaSerie = #0#3;
          eaDocu = #0#4;
          |SUB_EJECUTA_NP "con00902"; || copia del con00625, solo modifica
     |FINSI;
|FPRC;

|PROCESO Min696;
     #696#0 = #0#0;
     #696#1 = #0#1;
     #696#2 = #0#2;
     #696#5 = 0;
|FPRC;

|PROCESO Max696;
     #696#0 = #0#0;
     #696#1 = #0#1;
     #696#2 = #0#2;
     #696#5 = 9999;
|FPRC;

|PROCESO LinOrden; |TIPO 11;
     |SI FSalida = 13;
          |PUSHV 0501 2380;
          |ENTLINEAL #1#696, 4, 4, 22, 1, Min696, Max696;
          |POPV;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Consu; |TIPO 66;
     |SI #0#10 = "A";
          |ABRE #10;
          #10#52 = #0#3;
          #10#0 = #0#4;
          |LEE 000#10];
          |CONSULTA_CLAVE #1#10;
          |SI FSalida = 0;
               #0#3 = #10#52; |PINTA #0#3;
               #0#4 = ("000000" + #10#0) % -106; |PINTA #0#4;
          |SINO;
               |ERROR;
          |FINSI;
          |CIERRA #10;
     |SINO;
          |ABRE #2;
          #2#0 = #0#3;
          #2#1 = #0#4;
          |LEE 000#2];
          |CONSULTA_CLAVE #1#2;
          |SI FSalida = 0;
               #0#3 = #2#0; |PINTA #0#3;
               #0#4 = #2#1; |PINTA #0#4;
          |SINO;
               |ERROR;
          |FINSI;
          |CIERRA #2;
     |FINSI;
|FPRC;
