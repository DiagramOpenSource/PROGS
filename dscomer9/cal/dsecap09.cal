|FICHEROS;
     dsecap09 #0;
     dsecap10 #1,MANTE;
     dsecap11 #2,MANTE;
     dsecap05 #5,MANTE;
     dsecap07 #7,MANTE;
     dsecap01 #11;
     dsecap12 #12;
     dsecap13 #13;
     dsecap32 #32,MANTE; || Tmp Insertar trabajador
     agifa084 #84;
     dsecaz03 #103,MANTE; ||Pan Dividir
|FIN;

|VARIABLES;
     &enTotaFac = 0;
     &Tempo = "";
     nPrime = 0;
     aConte = "";
     aAlfa = "";
     nGrid1 = -1;
     nGrid2 = -1;
     nGrid32 = 0;
     nRango = 0;
     nPos1 = 0;
     nPos2 = 0;
     nModoCab = 0;
     nModo = 0;
     nHay = 0;
     nLinea = 0;
     aSer = "";
     nConta = 0;
     f = @;
     fFecha = @;
     nLin = 0;
     nBtnFactu = 0;
     nBtnDivide = 0;
     nBtnCalen = 0;
     nBtnTraba = 0;
     nBtnNuevo = 0;
     nPanta = 0;
     nTra = 0;
     nNume = 0;
|FIN;

|PROCESO Evento2;
     |SI FSalida = 1; |O FSalida = 2;
          |LEE 000#2=;
     |FINSI;
|FPRC;

|PROCESO Min2;
     #2#0 = #0#0;
     #2#1 = #0#1;
     #2#2 = #1#2;;
     #2#3 = 000000;
|FPRC;

|PROCESO Max2;
     #2#0 = #0#0;
     #2#1 = #0#1;
     #2#2 = #1#2;
     #2#3 = 999999;
|FPRC;

|PROCESO Evento1;
     |SI FSalida = 1; |O FSalida = 2;
          |LEE 000#1=;
          |REFRESCACONTROL nGrid2;
          |SI #1#4 = "P";
               |ESTADO_CONTROL nBtnFactu, "ENABLE";
               |ESTADO_CONTROL nBtnDivide, "ENABLE";
          |SINO;
               |ESTADO_CONTROL nBtnFactu, "DISABLE";
               |ESTADO_CONTROL nBtnDivide, "DISABLE";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Min1;
     #1#0 = #0#0;
     #1#1 = #0#1;
     #1#2 = "000000";
|FPRC;

|PROCESO Max1;
     #1#0 = #0#0;
     #1#1 = #0#1;
     #1#2 = "999999";
|FPRC;

|PROCESO Tipo14; |TIPO 14;    ||(*)
     |SI nGrid1 = -1;
          |PINPA #0#0; nPanta = FSalida;
          nRango = 1 + 4 + 8 + 16 + 32;
          nPos1 = 2602;
          nPos2 = 3146;
          |LINEAL_SIMPLE #1#1, nRango, nPos1, nPos2, Min1, Max1, Evento1;
          nGrid1 = FSalida;
          nPos1 = 2648;
          nPos2 = 3198;
          |LINEAL_SIMPLE #1#2, nRango, nPos1, nPos2, Min2, Max2, Evento2;
          nGrid2 = FSalida;
          |CONTROL_SIMPLE nPanta, "BOTON,Facturar", 2512, 2522, BtnFactu;
          nBtnFactu = FSalida;
          |CONTROL_SIMPLE nPanta, "BOTON,Dividir factura", 2524, 2540, BtnDivide;
          nBtnDivide = FSalida;
          |CONTROL_SIMPLE nPanta, "BOTON,Calendario", 1888, 1898, BtnCalen;
          nBtnCalen = FSalida;
          |CONTROL_SIMPLE nPanta, "BOTON,Insertar Trabajador", 2561, 2577, BtnTraba;
          nBtnTraba = FSalida;
     |SINO;
          #1#1 = 0;
          |REFRESCACONTROL nGrid2;
          ||#1#0 = #0#0;
          ||#1#1 = #0#1;
          |REFRESCACONTROL nGrid1;
     |FINSI;
|FPRC;

|PROCESO Tipo8; |TIPO 8; ||(*)
     |ABRET #1;
     |ABRET #2;
|FPRC;

|PROCESO Tipo9; |TIPO 9; ||(*)
     |CIERRAT #2;
     |CIERRAT #1;
|FPRC;

|PROCESO BorraTra;
     |BORRA 020#2a;
     |LIBERA #2;
|FPRC;

|DEFBUCLE BorraTra;
     #2#1;
     ;
     #0#0, #0#1, #1#2, 000000;
     #0#0, #0#1, #1#2, 999999;
     be;
     NULL, BorraTra;
|FIN;

|PROCESO BorraEmp;
     |BUCLE BorraTra;
     |BORRA 020#1a;
     |LIBERA #1;
|FPRC;

|DEFBUCLE BorraEmp;
     #1#1;
     ;
     #0#0, #0#1, "      ";
     #0#0, #0#1, "zzzzzz";
     be;
     NULL, BorraEmp;
|FIN;

|PROCESO Tipo2; |TIPO 2; ||(*)
     |SI nModoCab = 3;
          |BUCLE BorraEmp;
     |FINSI;
     |HAZ Calendario;
|FPRC;


|PROCESO Tipo0; |TIPO 0;     ||(*)
     #2#1 = 0;
     |REFRESCACONTROL nGrid2;
     #1#0 = #0#0;
     #1#1 = #0#1;
     |REFRESCACONTROL nGrid1;
|FPRC

|PROCESO Tipo12; |TIPO 12;    ||(*)
     || No quitar
|FPRC;

|PROCESO Tipo80_9; |TIPO 80;
     FSalida = 3199;
|FPRC;

|PROCESO Tipo7; |TIPO 7;
     nModoCab = (FEntrada / 100) ! 0;
     |SI nModoCab = 1;
          aSer = #0#0;
          #0#1 = 999999;
          |LEE 000#0];
          |SI FSalida = 0;
               |LEE 000#0a;
          |SINO;
               |LEE 000#0u;
          |FINSI;
          |SI FSalida = 0; |Y #0#0 = aSer;
               nConta = #0#1 + 1;
          |SINO;
               nConta = 1;
          |FINSI;
          |DDEFECTO #0;
          #0#0 = aSer;
          #0#1 = nConta; |PINTA #0#1;
     |FINSI;
|FPRC;

|PROCESO Min7;
     #7#0 = #2#2;
     #7#1 = 0;
|FPRC;

|PROCESO Max7;
     #7#0 = #2#2;
     #7#1 = 999999;
|FPRC;

|PROCESO Tipo66_11; |TIPO 66;
     |ABRE #7;
     #7#0 = #2#2;
     #7#1 = #2#3;
     |LEE 000#7];
     |CONSULTA_F_CLAVE #1#7, Min7, Max7;
     |SI FSalida = 0;
          #2#3 = #7#1; |PINTA #2#3;
          #2#4 = #7#3; |PINTA #2#4;
          #2#5 = #7#4; |PINTA #2#5;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #7;
|FPRC;

|PROCESO Tipo0_11; |TIPO 0;
     |ABRE #7;
     #7#0 = #2#2;
     #7#1 = #2#3;
     |LEE 000#7=;
     |SI FSalida = 0;
          #2#4 = #7#3; |PINTA #2#4;
          #2#5 = #7#4; |PINTA #2#5;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #7;
|FPRC;

|PROCESO TipoC4007; |TIPO 7;
     nModoCab = (FEntrada / 100) ! 0;
|FPRC;

|PROCESO TipoC2007; |TIPO 7;
     nModoCab = (FEntrada / 100) ! 0;
     |HAZ Tipo0;
|FPRC;

|PROCESO ModCalen7; |TIPO 7;
     aConte = #0Campo;
|FPRC;

|PROCESO ModCalen0; |TIPO 0;
     |SI #0Campo ! aConte; |Y nModoCab = 2;
          |ABRE #12;
          #12#0 = #0#0;
          #12#1 = #0#1;
          |LEE 000#12=;
          |SI FSalida = 0;
               |CONFI "0000N¨Crear nuevo calendario?";
               |SI FSalida = 0;
                    |HAZ BorraCale;
                    |HAZ CreaCale;
               |FINSI;
          |FINSI;
          |CIERRA #12;
     |FINSI;
|FPRC;

|PROCESO Borra13;
     |BORRA 020#13a;
|FPRC;

|DEFBUCLE Borra13;
     #13#1;
     ;
     #0#0, #0#1, 000;
     #0#0, #0#1, 999;
     be;
     NULL, Borra13;
|FIN;

|PROCESO BorraCale;
     |ABRE #12;
     #12#0 = #0#0;
     #12#1 = #0#1;
     |BORRA 000#12c;
     |CIERRA #12;
     |BUCLE Borra13;
|FPRC;

|PROCESO EsFiesta;
     fFecha = f % A1;
     aAlfa = fFecha % 1102;
     aAlfa = aAlfa > "";
     FSalida = 1;
     |SI aAlfa = "LU"; |Y #0#28 = "S"; FSalida = 0; |FINSI;
     |SI aAlfa = "MA"; |Y #0#29 = "S"; FSalida = 0; |FINSI;
     |SI aAlfa = "MI"; |Y #0#30 = "S"; FSalida = 0; |FINSI;
     |SI aAlfa = "JU"; |Y #0#31 = "S"; FSalida = 0; |FINSI;
     |SI aAlfa = "VI"; |Y #0#32 = "S"; FSalida = 0; |FINSI;
     |SI aAlfa = "SA"; |Y #0#33 = "S"; FSalida = 0; |FINSI;
     |SI aAlfa = "DO"; |Y #0#34 = "S"; FSalida = 0; |FINSI;
|FPRC;

|PROCESO CreaCale;
     |ABRE #5;
     #5#0 = #0#2;
     |LEE 020#5=;
     |CIERRA #5;

     |ABRE #12;
     #12#0 = #0#0;
     #12#1 = #0#1;
     |LEE 101#12=;
     |SI FSalida ! 0;
          |GRABA 020#12n;
          |ABRE #13;
          |DDEFECTO #13;
          |SI #5#2 = #5#5;
               #13#6 = "S";
          |SINO;
               #13#6 = "N";
          |FINSI;
          nLin = 0;
          |PARA f = #0#10; |SI f [ #0#11; |HACIENDO f = f + 1;
               |HAZ EsFiesta;
               |SI FSalida = 0;
                    nLin = nLin + 1;
                    #13#0 = #0#0;
                    #13#1 = #0#1;
                    #13#2 = nLin;
                    #13#5 = f;
                    |SI #0#12 = "M";
                         #13#3 = 0700;
                         #13#4 = 1500;
                    |FINSI;
                    |SI #0#12 = "T";
                         #13#3 = 1501;
                         #13#4 = 2200;
                    |FINSI;
                    |SI #0#12 = "X";
                         #13#3 = 0700;
                         #13#4 = 1500;
                         |GRABA 020#13n;
                         nLin = nLin + 1;
                         #13#2 = nLin;
                         #13#3 = 1501;
                         #13#4 = 2200;
                    |FINSI;
                    |GRABA 020#13n;
               |FINSI;
          |SIGUE;
          |CIERRA #13;
     |FINSI;
     |CIERRA #12;
|FPRC;

|PROCESO Calendario;
     |SI nModoCab = 3;
          |CONFI "0000¨Borrar calendario?";
          |SI FSalida = 0;
               |HAZ BorraCale;
          |FINSI;
     |FINSI;
     |SI nModoCab = 1;
          |HAZ BorraCale;
          |HAZ CreaCale;
     |FINSI;
     |SI nModoCab = 2;
          |HAZ CreaCale;
     |FINSI;
|FPRC;

|PROCESO BtnFactu;
     |LEE 101#1=;
     |SI FSalida = 0;
          |SI #1#4 = "P";
               aAlfa = "0000N¨Crear factura al cliente:" + #1#2 + " ?";
               |CONFI aAlfa;
               |SI FSalida = 0;
                    |HAZ Factura;
                    #1#4 = "F";
                    #1#5 = #84#48;
                    #1#6 = #84#0;
                    |GRABA 020#1a;
                    |REFRESCACONTROL nGrid1;
               |FINSI;
          |FINSI;
          |LIBERA #1;
     |FINSI;
|FPRC;

|PROCESO Impo1_7; |TIPO 7;
|FPRC;

|PROCESO Impo2_7; |TIPO 7;
     ||#103#5 = enTotaFac - (#103#2 + #103#8 + #103#11);
|FPRC;

|PROCESO Impo3_7; |TIPO 7;
     ||#103#8 = enTotaFac - (#103#2 + #103#5 + #103#11);
|FPRC;

|PROCESO Impo4_7; |TIPO 7;
     ||#103#11 = enTotaFac - (#103#2 + #103#5 + #103#8);
|FPRC;

|PROCESO Impo2; |TIPO 0;
     |SI #103#5 = 0;
          |C_M #103#3, 1, "N"; |PINTA #103#3;
     |SINO;
          |C_M #103#3, 1, "S"; |PINTA #103#3;
     |FINSI;
|FPRC;

|PROCESO Impo3; |TIPO 0;
     |SI #103#8 = 0;
          |C_M #103#6, 1, "N"; |PINTA #103#6;
     |SINO;
          |C_M #103#6, 1, "S"; |PINTA #103#6;
     |FINSI;
|FPRC;

|PROCESO Impo4; |TIPO 0;
     |SI #103#11 = 0;
          |C_M #103#9, 1, "N"; |PINTA #103#9;
     |SINO;
          |C_M #103#9, 1, "S"; |PINTA #103#9;
     |FINSI;
|FPRC;

|PROCESO VenDivide;
     |HAZ TotalFac;
     aAlfa = "Dividir factura:" + enTotaFac + " euros";

     |VENTANA 1230, 2060, -1, 17, aAlfa;
     |HAZ PushVenModal;
     |DDEFECTO #103;
     #103#2 = enTotaFac;
     |PINPA #0#103;
     |PINDA #0#103;
     |ENDATOS #1#103;
     aAlfa = FSalida;
     |HAZ PopVenModal;
     FSalida = aAlfa;
     |SI #103#0 = "     "; |Y #103#2 = 0;
               |Y #103#3 = "     "; |Y #103#5 = 0;
               |Y #103#6 = "     "; |Y #103#8 = 0;
               |Y #103#9 = "     "; |Y #103#11 = 0;
          FSalida = 1;
     |FINSI;
|FPRC;

|PROCESO BtnDivide;
     |LEE 101#1=;
     |SI FSalida = 0;
          |SI #1#4 = "P";
               |HAZ VenDivide;
               |SI FSalida = 0;
                    |HAZ DivideFac;
                    #1#4 = "D";
                    |GRABA 020#1a;
                    |REFRESCACONTROL nGrid1;
               |FINSI;
          |FINSI;
          |LIBERA #1;
     |FINSI;
|FPRC;

|PROCESO FecFin; |TIPO 0;
     |SI #0Campo ! Contenido;
          #0#17 = #0#11; |PINTA #0#17;
     |FINSI;
|FPRC;

|PROCESO Tipo10_2; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 3;
          |BUCLE BorraTra;
          |REFRESCACONTROL nGrid2;
     |FINSI;

     |ABRE #dsecap01;
     #dsecap01#0 = #dsecap10#2;
     |LEE 101#dsecap01.=;
     |SI FSalida = 0;
        #dsecap01#32 = #dsecap01#32 +. #dsecap10#7;
        |GRABA 020#dsecap01.a; |LIBERA #dsecap01;
     |FINSI;
     |CIERRA #dsecap01;

|FPRC;

|PROCESO Min13;
     #13#0 = #0#0;
     #13#1 = #0#1;
     #13#2 = 0;
|FPRC;

|PROCESO Max13;
     #13#0 = #0#0;
     #13#1 = #0#1;
     #13#2 = 999;
|FPRC;

|PROCESO BtnCalen;
     |HAZ Calendario;

     |VENTANA 1108, 2589, -1, 17, "Calendario";
     |HAZ PushVenModal;

     |ABRE #13;
     #13#0 = #0#0;
     #13#1 = #0#1;
     #13#2 = 0;
     |LEE 000#13];
     nRango = 1 + 4 + 8 + 16 + 32;
     nPos1 = 1209; nPos2 = 2388;
     |LINEAL_SIMPLE #1#13, nRango, nPos1, nPos2, Min13, Max13, NULL;
     |PAUSA;
     |CIERRA #13;

     |HAZ PopVenModal;
|FPRC;

|PROCESO Accion6; |TIPO 6;
     |ABRE #5;
     #5#0 = #0#2;
     |LEE 000#5=;
     |SI FSalida ! 0;
          |HAZ AltaAccion;
     |FINSI;
     |CIERRA #5;
|FPRC;

|PROCESO AltaAccion;
     |VENTANA 0506, 2992, -1, 17, "Nueva Acci¢n";
     |HAZ PushVenModal;

     |PINPA #0#5;
     |PINDA #0#5;
     |ENDATOS #1#5;
     |SI FSalida = 0;
          #0#3 = #5#1; |PINTA #0#3;
          |GRABA 020#5n;
     |SINO;
          |ERROR;
     |FINSI;

     |HAZ PopVenModal;
|FPRC;

|PROCESO Tipo13; |TIPO 13;
     |SI nPrime [ 1;           ||Presenta el ultimo registro al entrar
          nPrime = nPrime + 1;
          |LEE 000#0u;
     |FINSI;

     |DDEFECTO #5;
     |ABRE #5;
     #5#0 = #0#2;
     |LEE 000#5=;
     |CIERRA #5;
     |PINTA 1142, #5#4;
     |SI #5#3 = "T";
          |PINTA 1169, #5#6;
     |SINO;
          |PINTA 1169, #5#5;
     |FINSI;
|FPRC;

|PROCESO Accion0; |TIPO 0;
     |HAZ Tipo13;
     |SI #0#2 = 0;
          |C_M #0#3, 1, "S";
     |SINO;
          |C_M #0#3, 1, "N";
     |FINSI;
|FPRC;

|PROCESO dsecap07;
     #2#0 = #0#0;
     #2#1 = #0#1;
     #2#2 = #7#0;
     #2#3 = #7#1;
     |LEE 000#2=;
     |SI FSalida = 0;
          #32#0 = "*";
     |SINO;
          #32#0 = "";
     |FINSI;

     #32#1 = #7#1;
     #32#2 = #7#3;
     #32#3 = #7#5;
     #32#4 = #7#4;
     |GRABA 020#32n;
|FPRC;

|DEFBUCLE dsecap07;
     #7#1;
     ;
     #1#2, 000000;
     #1#2, 999999;
     ;
     NULL, dsecap07;
|FIN;

|PROCESO Min32;
     #32#1 = 000000;
|FPRC;

|PROCESO Max32;
     #32#1 = 999999;
|FPRC;

|PROCESO Evt32;
     |SI FSalida = 4;
          |SI #32#0 = "*";
               #32#0 = "";
          |SINO;
               #32#0 = "*";
          |FINSI;
          |PINTA #32#0;
          |GRABA 020#32a;
     |FINSI;
|FPRC;

|PROCESO LlenaTra;
     |SI #32#0 = "*";
          #2#0 = #0#0;
          #2#1 = #0#1;
          #2#2 = #1#2;
          #2#3 = #32#1;
          #2#4 = #32#2;
          #2#5 = #32#4;
          #2#6 = #32#3;
          |GRABA 020#2n;
     |FINSI;
|FPRC;

|DEFBUCLE LlenaTra;
     #32#1;
     ;
     000000;
     999999;
     ;
     NULL, LlenaTra;
|FIN;

|PROCESO BtnTraba;
     |HAZ CreaTempo; |NOME_DAT #32 Tempo; |DELINDEX #32;
     |ABRE #32;
     |BUCLE dsecap07;

     |VENTANA 1108, 2589, -1, 17, "Insertar Trabajador";
     |HAZ PushVenModal;

     |CONTROL_SIMPLE 0, "BOTON,Nuevo trabajador", 1109, 1125, BtnNuevo;
     nBtnNuevo = FSalida;

     |LEE 000#32p;
     nRango = 4 + 8 + 16 + 32;
     nPos1 = 1209; nPos2 = 2388;
     |LINEAL_SIMPLE #1#32, nRango, nPos1, nPos2, Min32, Max32, Evt32;
     nGrid32 = FSalida;
     |PAUSA;

     |BUCLE BorraTra;
     |BUCLE LlenaTra;

     |CIERRA #32;
     |HAZ BoraTempo; |DELINDEX #32;

     |DESTRUYECONTROL nGrid32;
     |FIN_CONTROL_WINDOWS  nBtnNuevo;
     |HAZ PopVenModal;

     |REFRESCACONTROL nGrid2;
|FPRC;

|PROCESO BtnNuevo;
     |VENTANA 0504, 2597, -1, 17, "Nuevo Trabajador";
     |HAZ PushVenModal;

     |ABRE #7;
     #7#0 = #1#2;
     #7#1 = 999999;
     |LEE 000#7];
     |SI FSalida = 0;
          |LEE 000#7a;
     |SINO;
          |LEE 000#7u;
     |FINSI;
     |SI FSalida = 0;
          nTra = #7#1 + 1;
     |SINO;
          nTra = 1;
     |FINSI;

     |DDEFECTO #7;
     #7#0 = #1#2;
     #7#1 = nTra;
     #7#2 = #1#3;
     |PINPA #0#7;
     |PINDA #0#7;
     |ENDATOS #1#7;
     |SI FSalida = 0;
          |GRABA 020#7n;
          #32#0 = " ";
          #32#1 = #7#1;
          #32#2 = #7#3;
          #32#3 = #7#5;
          #32#4 = #7#4;
          |GRABA 020#32b;
          |REFRESCACONTROL nGrid32;
          |LIBERA #32;
     |FINSI;
     |CIERRA #7;

     |HAZ PopVenModal;
|FPRC;

|PROCESO Min0;
     #0#0 = aSer;
     #0#1 = 000000;
|FPRC;

|PROCESO Max0;
     #0#0 = aSer;
     #0#1 = 999999;
|FPRC;

|PROCESO Tipo66; |TIPO 66;
     aSer = #0#0;
     |CONSULTA_F_CLAVE #1#0, Min0, Max0;
|FPRC;
