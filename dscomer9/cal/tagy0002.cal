|FICHEROS;
     tagy0002 #0;
     agif0041 #1;
     tagw0004 #2;
     agifa024 #24;

     dscam003@ #3;
     agifa010@ #10;
     agifa084@ #84;
     dscam000@ #100;
     dscam045@ #45;
|FIN;

|VARIABLES;
     aAlfa = "";
     aAlf1= "";
     aPath = "";
     aDat = "";
     aDef = "";
     aCli = "";
     aSer = "";
     nImpo = 0;
     nHay = 0;
     nSw = 0;
     i = 0;
     nEmpresa = -1;
     nDocum = 0;
     nTotal = 0;
     nEmp = 0;
|FIN;

|PROCESO dscam045;
     |SI #3#0 ] #45#4; |Y #3#0 [ #45#5;
          nEmp = #45#0;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE dscam045;
     #45#2001;
     2;
     #0#22, "V";
     #0#22, "V";
     ;
     NULL, dscam045;
|FIN;

|PROCESO BuscaEmp;
     |BUCLE dscam045;
|FPRC;

|PROCESO Acumula;
     nEmp = #3#46;
     |SI #3#49 = "S"; |O #3#82 = "S";
          |HAZ BuscaEmp;
     |FINSI;

     |SI nEmpresa ! nEmp;
          |CIERRA #24;
          aAlfa = aPath + (("00000" + nEmp) % -105) + "/";
          |PATH_DAT #24 aAlfa;
          |ABRE #24;
          nEmpresa = nEmp;
     |FINSI;
     |DDEFECTO #24;
     #24#0 = #3#32;
     |LEE 000#24=;

     |DDEFECTO #2;
     #2#0 = #24#25;
     |LEE 101#2=;
     |SI FSalida ! 0; |GRABA 020#2b; |FINSI;

     |SI nSw = 1;
          #2#5 = #2#5 + nImpo;
     |SINO;
          #2#6 = #2#6 + nImpo;
     |FINSI;

     |GRABA 020#2a;
     |LIBERA #2;
|FPRC;

|PROCESO BuscaPrimero;
     nTotal = #3#22;
     nImpo = #3#65;
     |SI #dscam003@#49 = "S";
        |SALVA_FICHA #dscam003@;
        nDocum = #dscam003@#40;
        |SELECT #13#dscam003@;
        #dscam003@#40 = 1;
        #dscam003@#41 = nDocum;
        |LEE 000#dscam003@.];
        |PARA; |SI FSalida = 0; |Y #dscam003@#41 = nDocum; |HACIENDO;
           |SALVA_FICHA #dscam003@;
           |SELECT #1#dscam003@;
           nImpo = (#3#65 * nImpo) / nTotal;
           |HAZ BuscaPrimero;
           |HAZ Acumula;
           |SELECT #13#dscam003@;
           |REPON_FICHA #dscam003@;
           |LEE 000#dscam003@.s;
        |SIGUE;
        |SELECT #1#dscam003@; |REPON_FICHA #dscam003@;
     |FINSI;

     |SI #dscam003@#82 = "S";
        nDocum = #dscam003@#83;
        |SALVA_FICHA #dscam003@;
        #dscam003@#40 = nDocum;
        |LEE 000#dscam003@.=;
        |SI FSalida = 0;
             |HAZ BuscaPrimero;
        |FINSI;
        |REPON_FICHA #dscam003@;
     |FINSI;
|FPRC;

|PROCESO dscam003;
     || Ni agrupados , divididos, ni remesados
     |SI #3#14 = "A"; |O #3#14 = "D"; |O #3#14 = "R";
          |ACABA;
     |FINSI;

/* ACUMULA AL DEL RECIBO */
     nImpo = #3#65;
     |HAZ Acumula;

/*  REPARTE PROPORCIONALMENTE (los agrupados) QUITAR BuscaEmp en Acumula
     |SALVA_FICHA #3;
     |SI #3#49 = "S"; |O #3#82 = "S";
          |HAZ BuscaPrimero;
     |SINO;
          nImpo = #3#65;
          |HAZ Acumula;
     |FINSI;
     |REPON_FICHA #3;
*/
|FPRC;

|DEFBUCLE dscam003;
     #3#6006;
     ;
     00, aSer, 000000, 000, " ", 000000001;
     99, aSer, 999999, 999, "z", 999999999;
     ;
     NULL,  dscam003;
|FIN;

|PROCESO CargaPend;
     |DBASS aAlfa;
     aPath = aAlfa;
     aAlfa = aAlfa + "dscarter/def/dscam003";
     aPath = aPath + "dscarter/fich/";

     |CARGA_DEF aAlfa, dscam003@;
     |SI FSalida = 0;
          aAlfa = aPath + (("00000" + #0#22) % -105) + "/";
          |PATH_DAT #3 aAlfa;

          |DBASE aPath;
          aPath = aPath + "fich/";
          nSw = 1;
          |ABRE #24;
          |PARA i = 1; |SI i [ 10; |HACIENDO i = i + 1;
               aSer = #0i;
               |BUCLE dscam003;
          |SIGUE;
          |CIERRA #24;

          nSw = 2;
          |ABRE #24;
          |PARA i = 12; |SI i [ 21; |HACIENDO i = i + 1;
               aSer = #0i;
               |BUCLE dscam003;
          |SIGUE;
          |CIERRA #24;

          |DESCARGA_DEF #dscam003@;
     |FINSI;
|FPRC;

|PROCESO AcuVta;
     |DDEFECTO #24;
     #24#0 = aCli;
     |LEE 000#24=;

     |DDEFECTO #2;
     #2#0 = #24#25;
     |LEE 101#2=;
     |SI FSalida ! 0; |GRABA 020#2b; |FINSI;

     |SI nSw = 1;
          |SI i [ 5;
               #2#1 = #2#1 + nImpo;
          |SINO;
               #2#2 = #2#2 + nImpo;
          |FINSI;
     |SINO;
          |SI i [ 16;
               #2#3 = #2#3 + nImpo;
          |SINO;
               #2#4 = #2#4 + nImpo;
          |FINSI;
     |FINSI;

     |GRABA 020#2a;
     |LIBERA #2;
|FPRC;

|PROCESO agifa010;
     |SI #10#38 = "S";
          aCli = #10#3;
          |SI #10#43 = "S";
               nImpo = -#10#78;
          |SINO;
               nImpo = #10#78;
          |FINSI;
          |HAZ AcuVta;
     |FINSI;
|FPRC;

|PROCESO agifa084;
     aCli = #84#3;
     nImpo = #84#73;
     |HAZ AcuVta;
|FPRC;

|DEFBUCLE agifa010;
     #10#1002;
     ;
     aSer, 000001;
     aSer, 999999;
     ;
     NULL, agifa010;
|FIN;

|DEFBUCLE agifa084;
     #84#1002;
     ;
     aSer, 000001;
     aSer, 999999;
     ;
     NULL, agifa084;
|FIN;

|PROCESO CargaVta;
     |DDEF aDef;
     aAlfa = aDef + "agifa010";
     |CARGA_DEF aAlfa, agifa010@;
     |SI FSalida = 0;
          aAlfa = aDef + "agifa084";
          |CARGA_DEF aAlfa, agifa084@;
          |SI FSalida = 0;

               aAlfa = aPath + (("00000" + #0#0) % -105) + "/";
               |PATH_DAT #10 aAlfa;
               |PATH_DAT #84 aAlfa;
               |PATH_DAT #24 aAlfa;
               |ABRE #24;
               nSw = 1;
               |PARA i = 1; |SI i [ 10; |HACIENDO i = i + 1;
                    aSer = #0i;
                    |BUCLE agifa010;
                    |BUCLE agifa084;
               |SIGUE;
               |CIERRA #24;

               aAlfa = aPath + (("00000" + #0#11) % -105) + "/";
               |PATH_DAT #10 aAlfa;
               |PATH_DAT #84 aAlfa;
               |PATH_DAT #24 aAlfa;
               |ABRE #24;
               nSw = 2;
               |PARA i = 12; |SI i [ 21; |HACIENDO i = i + 1;
                    aSer = #0i;
                    |BUCLE agifa010;
                    |BUCLE agifa084;
               |SIGUE;
               |CIERRA #24;

               |DESCARGA_DEF #agifa084@;
          |FINSI;
          |DESCARGA_DEF #agifa010@;
     |FINSI;
|FPRC;

|PROCESO Imprime;
     nHay = 1;
     |PRINT;
|FPRC;

|DEFBUCLE Imprime;
     #2#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Imprime;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |DBASS aAlfa;
     aPath = aAlfa;
     aAlfa = aAlfa + "dscarter/def/dscam000";
     aAlf1 = aPath + "dscarter/def/dscam045";
     aPath = aPath + "dscarter/fich/";
     aDat = aPath;
     |CARGA_DEF aAlfa, dscam000@;
     aAlfa = "";
     |SI FSalida = 0;
          |PATH_DAT #100 aPath;
          |ABRE #100;
          #100#0 = #0#22;
          |LEE 000#100=;
          |SI FSalida ! 0;
               aAlfa = "0000La empresa cartera '"+ #0#22 + "' no existe...";
          |FINSI;
          |CIERRA #100;
          |DESCARGA_DEF #dscam000@;
     |SINO;
          aAlfa = "0000Error al cargar dscam000";
     |FINSI;
     |SI aAlfa ! "";
          |MENAV aAlfa;
          |ACABA;
     |FINSI;

     |DBASE aPath;
     aPath = aPath + "fich/";
     |PATH_DAT #1 aPath;
     |ABRE #1;
     #1#0 = #0#0;
     |LEE 000#1=;
     |SI FSalida ! 0;
          aAlfa = "0000La empresa '"+ #0#0 + "' no existe...";
          |MENAV aAlfa;
          |ACABA;
     |FINSI;
     #1#0 = #0#11;
     |LEE 000#1=;
     |SI FSalida ! 0;
          aAlfa = "0000La empresa '"+ #0#11 + "' no existe...";
          |MENAV aAlfa;
          |ACABA;
     |FINSI;
     |CIERRA #1;

     |CARGA_DEF aAlf1, dscam045@;
     |SI FSalida ! 0;
          |MENAV "0000Error al cargar dscam045...";
          |ACABA;
     |FINSI;
     |PATH_DAT #45 aDat;

     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR "tagy0002";
          |SI FSalida ! 0;
               |MENAV "0000No existe informe 'tagy0002' ...";
          |SINO;
               aAlfa = Usuario;
               |NOME_DAT #2 aAlfa;
               |DELINDEX #2;
               |ABRE #2;
               |HAZ CargaVta;
               |HAZ CargaPend;
               |CIERRA #2;
               |BUCLE Imprime;
               |DELINDEX #2;
               |SI nHay ! 0; |PIEINF; |FINSI;
               |FININF;
          |FINSI;
          |FINIMP;
     |FINSI;

     |DESCARGA_DEF #dscam045@;
|FPRC;

|PROGRAMA;
     |ABRE #0;
     |LEE 101#0=;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |GRABA 020#0a;
     |FINSI;
     |CIERRA #0;
|FPRO;
