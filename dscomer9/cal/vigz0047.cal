|FICHEROS;
     vigz0047 #0;
     tempo065 #1;   ||Temporal que contiene solo los articulos fabricados
     agifa007 #7;
     agifa017 #17;
     agifa019 #19;
     vigw0024 #24;  ||Temporal coste
     agifa065 #65;
|FIN;

|VARIABLES;
     &eaHarti = "";
     &eaDarti = "";
     &Tempo = "";
     aMil = "";
     aTmp1 = "";
     aTmp24 = "";
     aAlfa = "";
     fTope = @;
     nPrecio = 0;
     nUni = 0;
     nDias = 0;
     nTotal = 0;
     nCoste = 0;
     nPcm = 0;
     nConta = 0;
|FIN;

|PROCESO Tempo;
     |SI nTotal ] #24#3;
          nTotal = nTotal - #24#3;
          nUni = #24#3;
     |SINO;
          nUni = nTotal;
          nTotal = 0;
     |FINSI;
     nCoste = nCoste + (#24#2 * nUni);
     |SI nTotal = 0;
          nPcm = nCoste / #19#6;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE Tempo;
     #24#1;
     ;
     #19#0, 0;
     #19#0, 9999999999;
     ;
     NULL, Tempo;
|FIN;

|PROCESO Articulo;
     nConta = 0;
     nCoste = 0;
     nTotal = #19#6;
     |SI nTotal [ 0; |ACABA; |FINSI;
     |BUCLE Tempo;
     |SI nTotal ! 0;
          aAlfa = #19#0; |QBF aAlfa;
          aAlfa = "0000El Total Articulo '" + aAlfa + " ' supera las compras";
          |SI #0#0 = #0#1;
               |MENAV aAlfa;
          |SINO;
               |PINTA 1422, aAlfa;
          |FINSI;
     |SINO;
          #19#9 = nPcm;
          #19#10 = #19#9 * #19#6; ||Valor articulo;
          |GRABA 020#19a;

          |DDEFECTO #17;
          #17#0 = #19#0;
          #17#1 = 1;
          |LEE 101#17];
          |PARA; |SI FSalida = 0; |Y #17#0 = #19#0; |HACIENDO;
              #17#3 = #19#9 * #17#5; || Valor almacen;
              |GRABA 020#17a;
              |LIBERA #17;
              |LEE 101#17s;
          |SIGUE;
          |LIBERA #17;
     |FINSI;
     |LIBERA #19;
|FPRC;

|DEFBUCLE Articulo;
     #19#1;
     ;
     #0#0;
     #0#1;
     be;
     NULL, Articulo;
|FIN;

|PROCESO Historico;
     |SI #65#6 = 0;
          |ACABA;
     |FINSI;

     #7#26 = #65#11;
     |LEE 000#7=;
     |SI #7#37 = "S";  ||Serie interna
          |ACABA;
     |FINSI;

     #19#0 = #65#0;
     |LEE 000#19=;

     #1#0 = #65#0;
     |LEE 000#1=;
     |SI FSalida = 0;    ||Articulo Fabricado
          |SI #65#2 = "SA"; |O #65#2 = "FB";
               nDias = fTope - #65#1;
               nPrecio = #65#9;
               nUni = #65#6;

               #24#0 = #65#0;
               #24#1 = nDias;
               |LEE 000#24=;
               |SI FSalida = 0;
                    #24#2 = ((#24#2*#24#3) + (nPrecio * nUni) ) / (#24#3+nUni);
                    #24#3 = #24#3 + nUni;
                    |GRABA 020#24a;
              |SINO;
                    #24#2 = nPrecio;
                    #24#3 = nUni;
                    #24#4 = #65#1;
                    |GRABA 021#24n;
              |FINSI;
          |FINSI;
     |SINO;              ||Articulo Comprado
          |SI #65#2 = "SA"; |O #65#2 = "AC"; |O #65#2 = "EV";
               nDias = fTope - #65#1;
               nPrecio = #65#9;
               |SI #65#2 = "AC"; |O #65#2 = "EV";
                    nPrecio = #65#14;
                    aMil = #19#110; |QBF aMil; aMil = aMil > "";
                    |SI aMil = "S";
                         nPrecio = nPrecio / 1000;
                    |FINSI;
               |FINSI;
               nUni = #65#6;

               #24#0 = #65#0;
               #24#1 = nDias;
               |LEE 000#24=;
               |SI FSalida = 0;
                    #24#2 = ((#24#2*#24#3) + (nPrecio * nUni) ) / (#24#3+nUni);
                    #24#3 = #24#3 + nUni;
                    |GRABA 020#24a;
              |SINO;
                    #24#2 = nPrecio;
                    #24#3 = nUni;
                    #24#4 = #65#1;
                    |GRABA 021#24n;
              |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE Historico;
     #65#1;
     ;
     #0#0, "01.01.0000", "  ",      0, 00001, "     ";
     #0#1, "99.99.9999", "zz", 999999, 99999, "zzzzz";
     ;
     NULL, Historico;
|FIN;

|PROCESO Fabricado;
     #1#0 = #65#0;
     |LEE 000#1=;
     |SI FSalida ! 0; |GRABA 020#1n; |FINSI;
|FPRC;

|DEFBUCLE Fabricado;
     #65#1;
     ;
     #0#0, "01.01.0000", "FB",      0, 00001, "     ";
     #0#1, "99.99.9999", "FB", 999999, 99999, "zzzzz";
     ;
     NULL, Fabricado;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     fTope  = "31.12.2100";

     |HAZ CreaTempo; |NOME_DAT #1 Tempo; |DELINDEX #1; aTmp1 = Tempo;
     |HAZ CreaTempo; |NOME_DAT #24 Tempo; |DELINDEX #24; aTmp24 = Tempo;

     |ABRE #1;

     |BUCLE Fabricado;

     |ABRE #24;
     |ABRE #7;
     |ABRE #19;
     |ABRE #17;
     |BUCLE Historico;
||CONSULTA_CLAVE #1#24;
     |CIERRA #17;
     |CIERRA #19;
     |CIERRA #7;
     |CIERRA #24;

     |CIERRA #1;

     |BUCLE Articulo;

     Tempo = aTmp1; |HAZ BoraTempo;
     Tempo = aTmp24; |HAZ BoraTempo;
|FPRC;

|PROGRAMA;
     |SI PARAMETRO = "RECALCULA";
          #0#0 = eaDarti;
          #0#1 = eaHarti;
          |HAZ Tipo3;
     |SINO;
          |MANTE #0;
     |FINSI;
|FPRO;
