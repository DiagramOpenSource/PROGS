|FICHEROS;
     diam0025 #0;
|FIN;

|VARIABLES;
     nTecla       = 0;
     nHandle      = 0;

     aBase        = "";
     aDirPdf      = "";
     aFichero     = "";
     aAbre        = "";
     aRuta        = "";
     aExtension   = "";
     aPunto       = "";
     aOrigen      = "";
     aDestino     = "";
     aIPRemoto    = "";
     aAlfa        = "";
|FIN;

|PROCESO min;
     #0#0 = "                    ";
|FPRC;

|PROCESO max;
     #0#0 = "zzzzzzzzzzzzzzzzzzzz";
|FPRC;

|PROCESO Pdf;
     |SI #0#4 = "N";  |ACABA;  |FINSI;

     nTecla = FSalida;

     |DBASE aBase;  |QBF aBase;
     aDirPdf = aBase + "000577/pdfofertas";                |MKDIR aDirPdf;
     aDirPdf = aDirPdf + "/" + (("00000" + #48#0) % -105); |MKDIR aDirPdf;
     aDirPdf = aDirPdf + "/";

     aFichero = #0#0 + ".pdf";  |QBT aFichero;
     aAbre   = aDirPdf + aFichero;
     |XABRE "E", aAbre, nHandle;
     |SI FSalida < 0;  |O nTecla = 10;
         aRuta = "F*.pdf";
         |BROWSE aRuta;  |QBF aRuta;
         |SI aRuta = "F";  |ACABA;  |FINSI;

         aRuta      = aRuta % 0299;
         aExtension = (aRuta % -103) < "";
         aPunto     = (aRuta % -104) < "";
         |SI aPunto ! ".pdf";
             |MENAV "0000 Documento sin Extension PDF. Formato exigido PDF.";
             |ERROR;
             |ACABA;
         |FINSI;

         aOrigen  = aRuta;
         aDestino = aAbre;
         |RECIBE_FICHERO aOrigen, aDestino;
         nTecla = 0;
     |FINSI;
|FPRC;

|PROCESO Tipo11;  |TIPO 11;
     |SI #0#4    = "N";  |ACABA;  |FINSI;
     |SI FSalida ! 10;   |ACABA;  |FINSI;

     aOrigen  = "C:\DOCUMENTOS";           |RMKDIR aOrigen;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBF aIPRemoto;

     |DBASE aBase;  |QBT aBase;
     aFichero = #0#0 + ".pdf";  |QBT aFichero;
     aDirPdf  = aBase + "000577/pdfofertas/" + (("00000" + #48#0) % -105) + "/";
     aDestino = aOrigen + "/" + aFichero;
     aOrigen  = aDirPdf + aFichero;
     |SI aIPRemoto ! "";
         |ENVIA_FICHERO aOrigen, aDestino;
     |SINO;
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;

     |SI FSalida = 0;
         aAlfa = aDestino;
         |RSYSTEM aAlfa;
         |RFBORRA aDestino;
     |FINSI;
|FPRC;

|PROGRAMA;
     |CLS;
     |ABRE #0;
     |ENTLINEAL #1#0, 1, 2, 22, 1, min, max;
     |CIERRA #0;
|FPRO;
