|FICHEROS;
     dsm00056 #0;
     dsm00057 #1;
     agifa142 #42;
     agifa321;
     agifa324;
     refere1@;
     refere2@;
|FIN;

|VARIABLES;
     &eaMatri = "";
     aAlfa = "";
     aUsu = "";
     nContaLog = 0;
     aSer = "";
     aNum = "";
     aTipo = "";
     nTipo = 0;
     &enErrHis = 0;
     &efFec = @;
     &enAlma = 0;
     &nDeci_PreBaseMx = 0;
     nConta    = 0;
     nModo     = 0;
     aSerie    = "";
     &calpre   = 0;
     &aConfir  = "";

     aMensaje = "";
     nForma = 0;
     &enSwEstaDoc = 0;
     &eaTipoMov = "";
     &eaSerAlb = "";
     &enNumAlb = 0;
     &enLinAlb = 0;

     &eaDocAlbero = "";       ||Nos dice si viene de un proceso Externo.
     &eaSerAlbero = "";
     &enNumAlbero = 0;
|FIN;

|PROCESO Conta56; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo ! 1; |ACABA; |FINSI;
     aSerie = #0#2;
     #0#0 = 999999;
     |LEE 000#0];
     |SI FSalida = 0;
          |LEE 000#0a;
     |SINO;
          |LEE 000#0u;
     |FINSI;
     |SI FSalida = 0; |Y #0#2 = aSerie;
          nConta = #0#0 + 1;
     |SINO;
          nConta = 1;
     |FINSI;
     |DDEFECTO #0;
     #0#2 = aSerie;
     #0#0 = nConta;
|FPRC;

|PROCESO Tipo8; |TIPO 8;
     |ABRE #42; |LEE 000#42p; |CIERRA #42;
     calpre = #42#60;
     aConfir = #42#76;
     |HAZ LeeMonBasez;
|FPRC;

|PROCESO Fecha; |TIPO 7;
     |SI nModo = 1;
          |C_M #0#1, 1, "S";
     |SINO;
          |C_M #0#1, 1, "N";
     |FINSI;
|FPRC;

|PROCESO MiraCierre;
     efFec = #1#12;
     enAlma = #1#1;
     |HAZ MiraHis0;
     |SI enErrHis ! 0; |ERROR10; |FINSI;
|FPRC;

|PROCESO Tipo2_56; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     nForma = 0 +. 1;
     |SI #0#3 = "S";
          |MENAV "0000Registro exportado...";
          |ERROR;
     |FINSI;
     |SI nModo = 3;
          |BUCLELIN 2 MiraCierre #0;
          |SI enErrHis ! 0;
               |MENAV "0000Hay lineas con almacenes cerrados por inventario...";
               |ERROR;
          |FINSI;
     |FINSI;

     ||No puedo distinguir si viene o no de una mezclada .. AHORA YA SI.
     ||Tendre que meter en el albm0046 un campo de Generado Automaticamente S/N

     |HAZ EsAlbero;
     |SI FSalida = 0;
          |SI nModo = 2; |Y nForma = -1;
               enSwEstaDoc = 0;
               eaTipoMov = "EV";
               eaSerAlb = #dsm00056#2;
               enNumAlb = #dsm00056#0;
               |HAZ AlberoCtrlModBaj;
               |SI enSwEstaDoc = 1;
                    aMensaje = "0000Modificacion no permitida. Proceso Externo.";
                    |SI eaDocAlbero = "MZ";
                         aMensaje = aMensaje + " Mezclada: " + (("000000" + enNumAlbero) % -106);
                    |FINSI;
                    |SI eaDocAlbero = "TI";
                         aMensaje = aMensaje + " O.Tintado: " + (("000000" + enNumAlbero) % -106);
                    |FINSI;
                    |MENAV aMensaje;
                    |ERROR;
               |FINSI;
          |FINSI;
          |SI nModo = 3;
               enSwEstaDoc = 0;
               eaTipoMov = "EV";
               eaSerAlb = #dsm00056#2;
               enNumAlb = #dsm00056#0;
               |HAZ AlberoCtrlModBaj;
               |SI enSwEstaDoc = 1;
                    aMensaje = "0000Baja no permitida. Proceso Externo.";
                    |SI eaDocAlbero = "MZ";
                         aMensaje = aMensaje + " Mezclada: " + (("000000" + enNumAlbero) % -106);
                    |FINSI;
                    |MENAV aMensaje;
                    |ERROR;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO LeeMonBasez;
     |ABRE #agifa321;
     |ABRE #agifa324;
     |LEE 010#agifa321.p;   ||Leo parametros de divisa

     #agifa324#0 = #agifa321#9;
     |LEE 010#agifa324.=;         ||Leo la ficha de la moneda base en Divisas

     nDeci_PreBaseMx = #agifa324#9;   ||Asigno los decimales del precio en la mon. base
     |CIERRA #agifa324;
     |CIERRA #agifa321;
|FPRC;

|PROCESO T056_15; |TIPO 15;
     nTipo = 15; |HAZ Log056;
|FPRC;

|PROCESO T056_16; |TIPO 16;
     nTipo = 16; |HAZ Log056;
|FPRC;

|PROCESO T056_17; |TIPO 17;
     nTipo = 17; |HAZ Log056;
|FPRC;

|PROCESO Log056;
     |DDEF aAlfa;
     aAlfa = aAlfa + "dsm00087";
     |CARGA_DEF aAlfa, refere1@;
     |SI FSalida ! 0; |ACABA; |FINSI;

     aSer = #0#2;
     aNum = ("000000" + #0#0) % -106;
     aTipo = "EV";

     |ABRE #refere1@;
     |DDEFECTO #refere1@;
     |SELECT #2#refere1@;
     #refere1@#1 = aSer;
     #refere1@#2 = aNum;
     #refere1@#3 = aTipo;
     |LEE 101#refere1@.=;
     |SI FSalida ! 0;
          |SELECT #1#refere1@;
          |LEE 000#refere1@.u;
          |SI FSalida = 0;
               nContaLog = #refere1@#0 + 1;
          |SINO;
               nContaLog = 1;
          |FINSI;
          |DDEFECTO #refere1@;
          #refere1@#0 = nContaLog;
          #refere1@#1 = aSer;
          #refere1@#2 = aNum;
          #refere1@#3 = aTipo;
          |GRABA 020#refere1@.b;
     |FINSI;
     |SI FSalida = 0;
          |HORA aAlfa;
          aUsu = Usuario % 810;
          |SI nTipo = 15;
               #refere1@#4 = aUsu;
               #refere1@#5 = ~;
               #refere1@#6 = aAlfa;
               #refere1@#7 = "";
               #refere1@#8 = "01.01.0000";
               #refere1@#9 = ""
               #refere1@#10 = "";
               #refere1@#11 = "01.01.0000";
               #refere1@#12 = ""
          |FINSI;
          |SI nTipo = 16;
               #refere1@#7 = aUsu;
               #refere1@#8 = ~;
               #refere1@#9 = aAlfa;
          |FINSI;
          |SI nTipo = 17;
               #refere1@#10 = aUsu;
               #refere1@#11 = ~;
               #refere1@#12 = aAlfa;
          |FINSI;
          |GRABA 020#refere1@.a;
     |FINSI;
     |CIERRA #refere1@;
     |DESCARGA_DEF #refere1@;
|FPRC;

|PROCESO T056_13; |TIPO 13;
     |HAZ EsInforAlba;
     |SI FSalida = 0;
          |ATRI R; |PINTA 1952, "Matricula:"; |ATRI 0;
          |C_P #1#22, 1, 1962;
          |PINTA #1#22;
     |FINSI;
     |HAZ EsGesisat;
     |SI FSalida = 0;
          |HAZ GesiLeeMaqui;
          |ATRI R; |PINTA 1952, "  Maquina:"; |ATRI 0;
          aAlfa = #1#22; #1#22 = eaMatri;
          |C_P #1#22, 1, 1962;
          |PINTA #1#22;
          #1#22 = aAlfa;
     |FINSI;
|FPRC;

|PROCESO GesiLeeMaqui;
     |DDEF aAlfa;
     aAlfa = aAlfa + "gesim024";
     |CARGA_DEF aAlfa, refere2@;
     |SI FSalida = 0;
          |ABRE #refere2@;
          #refere2@#0 = "EV";
          #refere2@#1 = #1#28;
          #refere2@#2 = #1#27;
          #refere2@#3 = #1#14;
          |LEE 000#refere2@.=;
          |SI FSalida = 0;
               eaMatri = #refere2@#4;
          |SINO;
               eaMatri = "";
          |FINSI;
          |CIERRA #refere2@;
          |DESCARGA_DEF #refere2@;
     |SINO;
          |MENAV "0000Error al cargar 'gesim024'";
     |FINSI;
|FPRC;
