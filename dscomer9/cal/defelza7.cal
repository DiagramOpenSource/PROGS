|FICHEROS;
     defelz07 #0;
     dsm00003 #3;
     defelw07 #7;   ||Tmp
     defelw08 #8;   ||Tmp
     defelm04 #4;
     defelm05 #5;
     defelm06 #6;
     agifa010 #10;
     agifa024 #24;
     agifa059 #59;
     agifa071 #71;
     agifa134 #134;
     tmp00000 #300;
     dsm00005 #905;
     dsm00071 #971;
     dsm00072 #972;
|FIN;

|INCLUYE i_varart;

|VARIABLES;
     aTmp7 = "";
     aTmp8 = "";
     nCajas = 0;
     &Tempo = "";
     aAlfa = "";
     nPrime = 0;
     i = 0;
     aCampo = "";
     aTalla = "";
     nDesde = 0;
     nHasta = 0;
     nCamTalla = 0;
     nLin = 0;
     aSurtido = "";
     nCaja = 0;
     nLinV = 0;
|FIN;

|PROCESO Surtido;
     |SI #71#12 ! 0;
          #6#0 = "P";
          #6#1 = #71#31;
          #6#2 = #71#12;
          #6#3 = #71#21;
     |SINO;
          #6#0 = "A";
          #6#1 = #71#29;
          #6#2 = #71#0;
          #6#3 = #71#1;
     |FINSI;
     |LEE 000#6=;
     |SI FSalida ! 0; |DDEFECTO #6; |FINSI;

     #4#0 = #6#5;
     |LEE 000#4=;
     |SI FSalida ! 0; |DDEFECTO #4; |FINSI;

     aSurtido = #6#5; |QBF aSurtido;
     nCajas = #6#6;
|FPRC;

|PROCESO BusLin;
     nLin = 0;
     |PARA i = nDesde; |SI i [ nHasta; |HACIENDO i = i + 1;
          aCampo = #0i; |QBF aCampo;
          |SI aCampo = aTalla; |SAL; |FINSI;
     |SIGUE;
     |SI i > nHasta;
          |PARA i = nDesde; |SI i [ nHasta; |HACIENDO i = i + 1;
               aCampo = #0i; |QBF aCampo;
               |SI aCampo = ""; |SAL |FINSI;
          |SIGUE;
     |FINSI;
     |SI i [ nHasta; nLin = 1; |FINSI;
|FPRC;

|PROCESO SurtiLin;
     aTalla = #5#1; |QBF aTalla;
     nDesde = 17; nHasta = 24; |HAZ BusLin;   || 8 tallas max por linea
     |SI nLin ! 0;
          nLin = 1;
     |SINO;
          nDesde = 25; nHasta = 32; |HAZ BusLin;
          |SI nLin ! 0;
               nLin = 2;
          |SINO;
               nDesde = 33; nHasta = 40; |HAZ BusLin;
               |SI nLin ! 0;
                    nLin = 3;
               |SINO;
                    nDesde = 41; nHasta = 48; |HAZ BusLin;
                    |SI nLin ! 0;
                         nLin = 4;
                    |SINO;
                         |MENAV "0000Error, exiten mas de 32 tallas diferentes en el pedido...";
                         |ACABA;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;

     #0i = aTalla;
     nCamTalla = i - ((nLin - 1) * 8);

     |DDEFECTO #7;
     #7#0 = #8#0;
     #7#1 = #8#1;
     #7#2 = #8#2;
     #7#3 = #8#3;
     #7#4 = #8#4;
     #7#5 = #8#5;
     #7#6 = nLin;
     #7#14 = nCaja;               ||N§Caja
     |LEE 101#7=;
     |SI FSalida ! 0; |GRABA 020#7b; |FINSI;
     #7#7 = #8#7;
     #7#8 = #8#8;
     #7#9 = #8#9;
     #7#10 = #8#10;
     #7#11 = #8#11;
     #7#12 = #8#12;
     #7#13 = nCajas;               ||Cajas
     #7#15 = #8#6;

     #7nCamTalla = #7nCamTalla + #5#3;
     #7#16 = #7#17 + #7#18 + #7#19 + #7#20 + #7#21 + #7#22 + #7#23 + #7#24;
     |GRABA 020#7a;
     |LIBERA #7;
|FPRC;

|DEFBUCLE SurtiLin;
     #5#1;
     ;
     #4#0, "      ";
     #4#0, "zzzzzz";
     ;
     NULL, SurtiLin;
|FIN;

|PROCESO Tmp8;
     |PARA nCaja = 1; |SI nCaja [ #8#13; |HACIENDO nCaja = nCaja + 1;
          |BUCLE SurtiLin;
     |SIGUE;
|FPRC;

|DEFBUCLE Tmp8;
     #8#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tmp8;
|FIN;

|PROCESO LinAlb;
     eAlfa = #71#2; |SUB_EJECUTA_NP "dsz99990;V";
     #971#0 = eAlfa1;
     |LEE 000#971=;
     |SI FSalida ! 0; |DDEFECTO #971; |FINSI;
     #972#0 = eAlfa2;
     |LEE 000#972=;
     |SI FSalida ! 0; |DDEFECTO #972; |FINSI;

     |HAZ Surtido;

     |DDEFECTO #8;
     #8#0 = #134#49;
     #8#1 = #134#0;
     #8#2 = #71#31;
     #8#3 = #71#12;
     #8#4 = #971#0;
     #8#5 = #972#0;

     aAlfa = #7#15; |QBF aAlfa;
     |SI aAlfa = "";
          #8#6 = aSurtido;
     |SINO;
          aAlfa = aAlfa - aSurtido;
          |SI FSalida = 0;
               aAlfa = #8#6; |QBF aAlfa;
               aAlfa = aAlfa + "/" + aSurtido;
               #8#6 = aAlfa;
          |FINSI;
     |FINSI;

     |LEE 101#8=;
     |SI FSalida ! 0; |GRABA 020#8b; |FINSI;
     #8#7 = #71#2;
     #8#8 = #971#1;
     #8#9 = #972#1;
     #8#10 = #71#4;
     #8#11 = #134#2;
     #8#12 = #134#115;
     #8#13 = #8#13 + nCajas;
     |GRABA 020#8a;
     |LIBERA #8;
|FPRC;

|DEFBUCLE Albaran;
     #10#1;
     ;
     #59#15, #59#2;
     #59#15, #59#2;
     ;
     NULL, NULL, LinAlb;
|FIN;

|PROCESO LinFactu;
     |BUCLE Albaran;
|FPRC;

|PROCESO Tmp;
     |DDEFECTO #24;
     #24#0 = #7#11;
     |LEE 000#24=;

     |DDEFECTO #3;
     #3#0 = #7#11;
     #3#1 = #7#12;
     |LEE 000#3=;

     #134#49 = #7#0;
     #134#0 = #7#1;
     |LEE 000#134=;

     |PRINT;
|FPRC;

|DEFBUCLE Tmp;
     #7#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tmp;
|FIN;

|PROCESO Imprime;
     |LEE 000#7p;
     |SI FSalida ! 0; |ACABA; |FINSI;

     Impresora = "CrystalReport";
     |IMPRE -1;
     |SI FSalida = 0;
          |INFOR "defelz07";
          |SI FSalida = 0;
               |BUCLE Tmp;
               |PIEINF;
               |FININF;
          |SINO;
               |MENAV "0000Error en informe 'defelz07'";
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;

|PROCESO Factura;
     |DDEFECTO #24;
     #24#0 = #134#2;
     |LEE 000#24=;
     |SI #24#112 ! "S"; |ACABA; |FINSI;

     |SI PARAMETRO ! "";
          |SI nPrime = 0;
               |CONFI "0000N¿Imprimir paking list?";
               |SI FSalida ! 0; |ERROR10; |ACABA; |FINSI;
               nPrime = 1;
          |FINSI;
     |FINSI;

     |BUCLELIN 2 LinFactu #134;
|FPRC;

|DEFBUCLE Factura;
     #134#1;
     ;
     #0#0, #0#2;
     #0#1, #0#3;
     ;
     NULL, Factura;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |BUCLE Factura;
     |BUCLE Tmp8;
     |HAZ Imprime;
|FPRC;

|PROCESO Tmp300;
     #134#49 = #300#0;
     #134#0 = #300#1;
     |LEE 020#134=;
     |SI FSalida = 0;
          |HAZ Factura;
     |FINSI;
|FPRC;

|DEFBUCLE Tmp300;
     #300#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tmp300;
|FIN;

|PROGRAMA;
     |HAZ CreaTempo; |NOME_DAT #7 Tempo; |DELINDEX #7; |ABRE #7; aTmp7 = Tempo;
     |HAZ CreaTempo; |NOME_DAT #8 Tempo; |DELINDEX #8; |ABRE #8; aTmp8 = Tempo;
     |ABRE #3;
     |ABRE #4;
     |ABRE #6;
     |ABRE #24;
     |ABRE #134;
     |ABRE #971;
     |ABRE #972;
     |SI PARAMETRO = "";
          |MANTE #0;
     |SINO;
          |NOME_DAT #300 PARAMETRO;
          |ABRE #134;
          |BUCLE Tmp300;
          |CIERRA #134;
          |HAZ Imprime;
     |FINSI;
     |CIERRA #972;
     |CIERRA #971;
     |CIERRA #134;
     |CIERRA #24;
     |CIERRA #6;
     |CIERRA #4;
     |CIERRA #3;
     Tempo = aTmp7; |HAZ BoraTempo; |CIERRA #7; |DELINDEX #7;
     Tempo = aTmp8; |HAZ BoraTempo; |CIERRA #8; |DELINDEX #8;
|FPRO;
