|FICHEROS;
     diay0001 #0;
     diam0003 #3;
     dsparm05@ #100;
     dsparm01@ #101;
|FIN;

|VARIABLES;
     aIP       = "";
     aDjunto   = "";
     aMensaje  = "";
     aFrom     = "";
     aServidor = "";
     aSubject  = "";
     aTo       = "";
     aFic      = "";
     aAlfa     = "";
     aTecnico  = "";
     nHandle   = 0;
     nAbre     = 0;
     aDef1     = "";
     aDef2     = "";
     aDat      = "";
     aBase     = "";
|FIN;

|PROCESO Correo;
     |CARGA_DEF aDef2, dsparm01@;
     |SI FSalida = 0;
          |PATH_DAT #101 aDat;
          |ABRE #101; |LEE 000#101p; |CIERRA #101;

          #100#0 = aTecnico;
          |LEE 000#100=;
          |SI FSalida = 0;
               aTo = #100#3;       |QBF aTo;
               aSubject = "Recordatorio Visado";
               aFrom = "maria@alma.in";
               aServidor = #101#7; |QBF aServidor;
               aIP = #101#8;       |QBF aIP;
               aMensaje = "$$$$" + aFic;
               aDjunto = aFic;
               |DSCORREO_ENVIA aIP, aServidor, aFrom, aTo, aSubject, aMensaje, aDjunto;
               |SI FSalida ! 0;
                    |MENAV "0000Ha habido un error durante el envio.";
               |SINO;
       ||           |MENAV "0000Mensaje enviado."
               |FINSI;
          |FINSI;
          |DESCARGA_DEF #dsparm01@;
     |FINSI;
|FPRC;

|PROCESO diam0003;
     |SI aTecnico ! #3#2;
          |SI aTecnico ! ""; |Y nAbre ] 0;
               |XCIERRA nHandle;
               |HAZ Correo;
          |FINSI;
          |XABRE "W", aFic, nHandle;
          nAbre = FSalida;
     |FINSI;
     aAlfa = "Pedido: " + #3#0 + "/" + (("000000" + #3#1) % -106) + &13 + &10;
     |SI nAbre ] 0;
          |XGRABA nHandle, aAlfa;
     |FINSI;
     aTecnico = #3#2;
|FPRC;

|DEFBUCLE diam0003;
     #3#1;
     4;
     "     ", 000001, 0001, "N";
     "zzzzz", 999999, 9999, "N";
     ;
     NULL, NULL, NULL, NULL, NULL, diam0003;
     #3#2;
|FIN;

|PROCESO Inicio; |TIPO 3;
     |DBASS aBase; |QBF aBase;

     aDef1 = aBase + "dspartes/def/dsparm05";
     aDef2 = aBase + "dspartes/def/dsparm01";
     aDat  = aBase + "dspartes/fich/";

     |SI #0#0 = "SI";
          |CARGA_DEF aDef1, dsparm05@;
          |SI FSalida = 0;
               |PATH_DAT #100 aDat;
               |ABRE #100;

               |DFICE aFic;
               aFic = aFic + "visado.txt";
               |BUCLE diam0003;
               |SI aTecnico ! ""; |Y nAbre ] 0;
                    |XCIERRA nHandle;
                    |HAZ Correo;
               |FINSI;

               |CIERRA #100;
               |DESCARGA_DEF #dsparm05@;
          |SINO;
               aDef1 = "0000Error al cargar:" + aDef1;
               |MENAV aDef1;
          |FINSI;
     |FINSI;
|FPRC;
