     || Clientes1, CheqMovi, CheqRodacal

|FICHEROS;
     agifa024 #24,MANTE;
     psioa021 #1;
     psioa022 #2;
     psioa023 #3,MANTE;
     psioa024 #4,MANTE;
     dsm00003 #13,MANTE;
     agifa027 #27;
     psioa004 #40;
     psioa081 #81;
     agifa142 #142;
     refere24@ #240;

     agifa104 #104;
     agifa010 #10;
     agifa084 #84;
     dsw00072 #72; ||Tmp
|FIN;

|VARIABLES;
     nContaRod = 0;
     aCliRod = "";
     &nMasRie = 0;
     &eaTag = "";
     &eaCodCliente = "";
     &eaNomCliente = "";
     &aRieBloqu = "S";

     nHayDupli = 0;
     aAlfa = "";
     nSal = "";
     aCp1 = "";
     aCp2 = "";
     nConta = 0;
     nHayDir = 0;
     nHayCli = 0;
     i = 0;
 {-1}aMenuCli = "";
     aMenuCli1 = "2400";
     aMenuCli2 = "1";
     aMenuCli3 = "Contador:  ";
     aMenuCli4 = "AM";
     aMenuCli5 = " Automatico ";
     aMenuCli6 = " Manual ";
|FIN;

|PROCESO MoviCli;
     #1#18 = #40#0; ||Tipo
     #1#19 = #40#2; ||Ser
     #1#0 = #40#4;  ||Cli
     |LEE 101#1=;
     |SI FSalida = 0;
          #81#0 = #1#18;
          #81#1 = #1#19;
          #81#2 = #40#1;  ||Numero
          |LEE 101#81=;
          |SI FSalida ! 0; |GRABA 020#81b; |FINSI;
          #81#3 = #40#4;
          #81#4 = #1#20;
          |GRABA 020#81a;
          |LIBERA #81;

          #40#4 = #1#20;
          |GRABA 020#40a;
     |FINSI;
     |LIBERA #40;
|FPRC;

|DEFBUCLE MoviCli;
     #40#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL, MoviCli;
|FIN;

|PROCESO ModiDir;
     #1#18 = #2#9;
     #1#19 = #2#10;
     #1#0 = #2#0;
     |LEE 000#1=;
     |SI FSalida = 0;
          #2#11 = #1#20;
          |GRABA 020#2a;
     |FINSI;
     |SI #2#9 = "Z";
          #2#11 = #2#0;
          |GRABA 020#2a;
     |FINSI;
     |LIBERA #2;

     #4#9 = #2#9;
     #4#10 = #2#10;
     #4#0 = #2#0;
     #4#1 = #2#1;
     |LEE 101#4=;
     |SI FSalida = 0;
          #4#11 = #2#11;
          |GRABA 020#4a;
     |FINSI;
     |LIBERA #4;
|FPRC;

|DEFBUCLE ModiDir;
     #2#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, ModiDir;
|FIN;

|PROCESO ActDir;
     #4#9 = #2#9;
     #4#10 = #2#10;
     #4#0 = #2#0;
     #4#1 = #2#1;
     |LEE 020#4=;
     |SI FSalida = 0;
          #2#11 = #4#11;
          #2#12 = #4#12;
          |GRABA 020#2a;
     |FINSI;
     |LIBERA #4;
     |SI #2#12 = 0;
          #2#11 = "      ";
          nHayDir = 1;
     |FINSI;
|FPRC;

|DEFBUCLE ActDir;
     #2#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL, ActDir;
|FIN;

|PROCESO ActCli;
     #3#18 = #1#18;
     #3#19 = #1#19;
     #3#0 = #1#0;
     |LEE 020#3=;
     |SI FSalida = 0;
          #1#20 = #3#20;
          |GRABA 020#1a;
     |FINSI;
     |LIBERA #1;
     |SI #1#20 = "      "; |O #1#20 = "000000";
          nHayCli = 1;
     |FINSI;
|FPRC;

|DEFBUCLE ActCli;
     #1#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL, ActCli;
|FIN;

|PROCESO ModiCli;
     #24#0 = #3#20;
     |LEE 101#24=;
     |SI FSalida = 0;
          |PUSHV 0101 2380;
          |CLS;
          |PINPA #0#24;
          |PINDA #0#24;
          |ENDATOS #1#24;
          |SI FSalida = 0;
               |GRABA 020#24a;
          |FINSI;
          |POPV;
     |FINSI;
|FPRC;

|PROCESO AltaCli;
     |DDEFECTO #24;
     #24#0 = "000000";
     #24#1 = #3#1;
     #24#2 = #3#2;
     #24#5 = #3#3; #24#8 = #3#3; #24#11 = #3#3;
     #24#6 = #3#4; #24#9 = #3#4; #24#12 = #3#4;
     aCp1 = #3#5 % 102;
     aCp2 = #3#5 % -103;
     #24#178 = aCp1; #24#181 = aCp1; #24#184 = aCp1;
     #24#179 = aCp2; #24#182 = aCp2; #24#185 = aCp2;
     #24#180 = #3#5; #24#183 = #3#5; #24#186 = #3#5;
     #24#7 = #3#6; #24#10 = #3#6; #24#13 = #3#6;
     #24#15 = #3#7;
     #24#17 = #3#8;
     #24#47 = #3#9;
     #24#20 = #3#10;
     #24#39 = #3#11;
     #24#3 = #3#12;
     #24#158 = #3#13;
     #24#37 = #3#14;
     #24#38 = #3#15;
     #24#156 = #3#16;

     #24#18 = #3#21;
     #24#196 = #3#22;
     #24#197 = #3#23;
     #24#25 = #3#24;
     #24#52 = #3#25;
     #24#36 = #3#26;
     #24#53 = #3#27;
     |PUSHV 0101 2380;
     |CLS;
     |PINPA #0#24;
     |PINDA #0#24;
     |SI #142#1 = "S";
          |MENU aMenuCli;
          |SI FSalida = 1;
               |ABRE #27;
               #27#0 = "clientes";
               |LEE 101#27=;
               |SI FSalida = 0;
                    #27#1 = #27#1 + 1;
                    nConta = #27#1;
                    |GRABA 021#27a;
               |SINO;
                    nConta = 1;
                    #27#1 = 1;
                    |GRABA 021#27n;
               |FINSI;
               aAlfa = ("000000" + nConta) % -106;
               #24#0 = aAlfa;
               |PINTA #24#0;
               |CIERRA #27;
          |FINSI;
     |FINSI;
     |ENCAMPO #0#24;
     nSal = 1;
     |SI FSalida = 0;
          |GRABA 000#24b;
          |SI FSalida ! 0;
               |MENAV "0000El codigo existe...";
          |SINO;
               |ENDATOS #1#24;
               |SI FSalida = 0;
                    |GRABA 020#24a;
                    nSal = 0;
               |SINO;
                    |BORRA 020#24a;
               |FINSI;
          |FINSI;
     |FINSI;
     |POPV;
     FSalida = nSal;
|FPRC;

|PROCESO Min13;
     #13#0 = #4#11;
     #13#1 = 1;
|FPRC;

|PROCESO Max13;
     #13#0 = #4#11;
     #13#1 = 999;
|FPRC;

|PROCESO ConsulDir;
     |ABRE #13;
     #13#0 = #4#11;
     #13#1 = #4#12;
     |LEE 000#13];
     |CONSULTA_F_CLAVE #1#13, Min13, Max13;
     nSal = FSalida;
     |SI FSalida = 0;
          #4#12 = #13#1;
          |PINTA #4#12;
          |GRABA 020#4a;
     |FINSI;
     |CIERRA #13;
     FSalida = nSal;
|FPRC;

|PROCESO DirEntrega;
     |HAZ ConsulDir;
     |SI FSalida ! 0;
          |SI #4#12 = 0; #4#12 = #4#1; |FINSI;
     |FINSI;
     |ENCAMPO #12#4;
     |SI FSalida = 0;
          |GRABA 020#4a;
     |FINSI;
|FPRC;

|PROCESO Tecla24_11; |TIPO 11;
     |SI FSalida = 10; ||F2;
          |HAZ DirEntrega;
     |FINSI;
|FPRC;

|PROCESO Tecla24; |TIPO 0;
     |SI FSalida = 0; |Y #4Campo = Contenido;
          |HAZ DirEntrega;
     |FINSI;
|FPRC;


|PROCESO NifDupli;
     |SALVA_DATOS #24;
     |REPON_DATOS #240;
     |GRABA 020#240n;
     nHayDupli = 1;
|FPRC;

|DEFBUCLE NifDupli;
     #24#4;
     ;
     #3#7;
     #3#7;
     ;
     NULL, NifDupli;
|FIN;

|PROCESO Cliente;
     |ABRE #24;
     |SI #3#20 = "      "; |O #3#20 = "000000";
          nHayDupli = 0;
          |DELINDEX #240;
          |ABRE #240;
          aAlfa = #3#7; |QBF aAlfa;
          |SI aAlfa ! "";
               |BUCLE NifDupli;
          |FINSI;
          |SI nHayDupli = 0;
               |HAZ AltaCli;
          |SINO;
               |MENAV "0000Existen Clientes con el mismo NIF. Elija uno o pulse escape para crearlo nuevo.";
               |CONSULTA_CLAVE #1#240;
               |SI FSalida = 0;
                    #24#0 = #240#0;
               |SINO;
                    |HAZ AltaCli;
               |FINSI;
          |FINSI;
          |SI FSalida = 0;
               #3#20 = #24#0;
               |PINTA #3#20;
               |GRABA 020#3a;
          |FINSI;
          |CIERRA #240;
     |SINO;
          |HAZ ModiCli;
     |FINSI;
     |CIERRA #24;
|FPRC;

|PROCESO Tecla23; |TIPO 0;
     |SI FSalida = 0; |Y #3Campo = Contenido;
          |HAZ Cliente;
     |FINSI;
|FPRC;

|PROCESO Tecla23_11; |TIPO 11;
     |SI FSalida = 10; ||F2
          |HAZ Cliente;
     |FINSI;
     |SI FSalida = 11; ||F3
          |ENCAMPO #20#3;
          |ABRE #24;
          #24#0 = #3#20;
          |LEE 000#24=;
          |SI FSalida ! 0;
               |MENAV "0000No existe el cliente...";
               |ERROR;
          |SINO;
               |GRABA 020#3a;
          |FINSI;
          |CIERRA #24;
     |FINSI;
|FPRC;

|PROCESO CargaDir;
     nHayDir = 1;
     |PARA i = 0; |SI i [ 12; |HACIENDO i = i + 1;
          #4i = #2i;
     |SIGUE;
     |GRABA 020#4n;
|FPRC;

|DEFBUCLE CargaDir;
     #2#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, CargaDir;
|FIN;

|PROCESO CargaCli;
     nHayCli = 1;
     |PARA i = 0; |SI i [ 27; |HACIENDO i = i + 1;
          #3i = #1i;
     |SIGUE;
     |GRABA 020#3n;
|FPRC;

|DEFBUCLE CargaCli;
     #1#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, CargaCli;
|FIN;

|PROCESO Min23;
     #3#18 = " ";
     #3#19 = "  ";
     #3#0 = "      ";
|FPRC;

|PROCESO Max23;
     #3#18 = "z";
     #3#19 = "zz";
     #3#0 = "zzzzzz";
|FPRC;

|PROCESO Min24;
     #4#9 = " ";
     #4#10 = "  ";
     #4#0 = "      ";
     #4#1 = 1;
|FPRC;

|PROCESO Max24;
     #4#9 = "z";
     #4#10 = "zz";
     #4#0 = "zzzzzz";
     #4#1 = 999;
|FPRC;

|PROCESO Clientes1;
     |DDEF aAlfa;
     aAlfa = aAlfa + "agifa024";
     |CARGA_DEF aAlfa, refere24@;
     |SI FSalida ! 0;
          |MENAV "0000Error al cargar 'agifa024'";
          |MENAV aAlfa; |ACABA;
     |FINSI;
     aAlfa = Usuario;
     |NOME_DAT #240 aAlfa;
     |DELINDEX #240;

     |DELINDEX #3;
     |DELINDEX #4;
     |ABRE #3;
     |BUCLE CargaCli;
     |CIERRA #3;
     |ABRE #4;
     |BUCLE CargaDir;
     |CIERRA #4;

     |SI nHayDir ! 0; |O nHayCli ! 0;
          |PUSHV 0101 2380;
          |CLS;
          |SI nHayCli ! 0;
               |ENTLINEAL #1#3, 3, 4, 22, 1, Min23, Max23;
               nHayCli = 0;
               |ABRE #3;
               |BUCLE ActCli;
               |CIERRA #3;
          |FINSI;
          |ABRE #1;
          |ABRE #4;
          |BUCLE ModiDir;
          |CIERRA #4;
          |CIERRA #1;
          |SI nHayDir ! 0;
               |ENTLINEAL #1#4, 3, 4, 20, 1, Min24, Max24;
               nHayDir = 0;
               |ABRE #4;
               |BUCLE ActDir;
               |CIERRA #4;
          |FINSI;
          |POPV;
     |FINSI;

     nSal = nHayCli + nHayDir;
     |SI nSal ! 0;
          |MENAV "0000Importacion anulada...";
     |SINO;
          |ABRE #1;
          |ABRE #81;
          |BUCLE MoviCli;
          |CIERRA #81;
          |CIERRA #1;
     |FINSI;

     |DELINDEX #240;
     |DESCARGA_DEF #240;
     FSalida = nSal;
|FPRC;

|PROCESO Movimiento;
     |SI #40#0 = "A";
          #10#52 = #40#2;
          #10#0 = #40#1;
          |LEE 000#10=;
          |SI FSalida = 0;
               aAlfa = "0000Albaran existente: '" + #40#2 + "/" + (("000000" + #40#1) % -106) + "'";
               ||MENAV aAlfa;
               nSal = 1;
               #72#0 = #40#0;
               #72#1 = #40#2;
               #72#2 = #40#1;
               |GRABA 020#72n;
          |FINSI;
     |FINSI;
     |SI #40#0 = "P";
          #104#25 = #40#2;
          #104#0 = #40#1;
          |LEE 000#104=;
          |SI FSalida = 0;
               aAlfa = "0000Pedido existente: '" + #40#2 + "/" + (("000000" + #40#1) % -106) + "'";
               ||MENAV aAlfa;
               nSal = 1;
               #72#0 = #40#0;
               #72#1 = #40#2;
               #72#2 = #40#1;
               |GRABA 020#72n;
          |FINSI;
     |FINSI;
     |SI #40#0 = "F";
          #84#48 = #40#2;
          #84#0 = #40#1;
          |LEE 000#84=;
          |SI FSalida = 0;
               aAlfa = "0000Factura existente: '" + #40#2 + "/" + (("000000" + #40#1) % -106) + "'";
               ||MENAV aAlfa;
               nSal = 1;
               #72#0 = #40#0;
               #72#1 = #40#2;
               #72#2 = #40#1;
               |GRABA 020#72n;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE Movimiento;
     #40#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Movimiento;
|FIN;

|PROCESO Tmp;
     |PRINT;
|FPRC;

|DEFBUCLE Tmp;
     #72#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Tmp;
|FIN;

|PROCESO CheqMovi;
     aAlfa = Usuario;
     |NOME_DAT #72 aAlfa;
     |DELINDEX #72;

     nSal = 0;
     |ABRE #10;
     |ABRE #104;
     |ABRE #84;
     |ABRE #72;
     |BUCLE Movimiento;
     |CIERRA #72;
     |CIERRA #84;
     |CIERRA #104;
     |CIERRA #10;
     |SI nSal ! 0;
          |MENAV "0000No se puede importar, ya existen documentos.";
          |CONFI "0000SImprimir movimientos existentes:";
          |SI FSalida = 0;
               |IMPRE 0;
               |SI FSalida = 0;
                    |INFOR "psioa006";
                    |SI FSalida ! 0;
                         |MENAV "0000No existe informe: psioa006";
                    |SINO;
                         |BUCLE Tmp;
                         |PIEINF;
                         |FININF;
                    |FINSI;
                    |FINIMP;
               |FINSI;
          |FINSI;
     |FINSI;

     |DELINDEX #72;
     FSalida = nSal;
|FPRC;
-----------------------------------------


|PROCESO CheqRodacal0;
     |SI #40#0 = "A"; |O #40#0 = "P";
          nMasRie = 0;
          eaTag = "";
          eaCodCliente = #40#4;
          eaNomCliente = #40#5;
          |HAZ SacaRiesgo;
          |SI aRieBloqu = "S";
               nContaRod = nContaRod + 1;
               #72#2 = nContaRod;
               #72#3 = #40#4;
               |GRABA 020#72n;
               nSal = 1;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE CheqRodacal0;
     #40#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, CheqRodacal0;
|FIN;

|PROCESO TmpRodacal;
     |SI aCliRod ! #72#3;
          aCliRod = #72#3;
          |PRINT;
     |FINSI;
|FPRC;

|DEFBUCLE TmpRodacal;
     #72#2;
     ;
     INICIO;
     FINAL;
     ;
     NULL, TmpRodacal;
|FIN;

|PROCESO CheqRodacal;
     aAlfa = Usuario;
     |NOME_DAT #72 aAlfa;
     |DELINDEX #72;
     nSal = 0;
     |ABRE #72;
     |BUCLE CheqRodacal0;
     |CIERRA #72;
     |SI nSal ! 0;
          |MENAV "0000No se puede importar, existen clientes bloqueados en cartera";
          |CONFI "0000SImprimir clientes bloqueados en cartera";
          |SI FSalida = 0;
               |IMPRE 0;
               |SI FSalida = 0;
                    |INFOR "rodacal6";
                    |SI FSalida ! 0;
                         |MENAV "0000No existe informe: rodacal6";
                    |SINO;
                         |BUCLE TmpRodacal;
                         |PIEINF;
                         |FININF;
                    |FINSI;
                    |FINIMP;
               |FINSI;
          |FINSI;
     |FINSI;

     |DELINDEX #72;
     FSalida = nSal;
|FPRC;
