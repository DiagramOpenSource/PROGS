|FICHEROS;
     agifa134 #1;
     agifa024 #3;
     agifa091 #6;
     agifa133 #17;
     agis0002 #98;
     agifa177 #99;
     dsz99984 #11;
     agifa142 #41;
|FIN;

|VARIABLES;
     nVto = 0;
     nRecibo = 0;
     &eaProg = "agifa134";
|FIN;

|PROCESO hazreci;
     |ABRE #6;
     #6#0 = #1#11;
     |LEE 000#6=;
     |CIERRA #6;
     |ABRE #17;
     |ABRE #98;
     |ABRE #99;

     nVto = 0;
     #17#0 = #1#0;
     #17#4 = #1#49;
     #17#1 = 0;
     |LEE 000#17];
     |PARA ; |SI FSalida = 0; |Y #17#0 = #1#0;|Y #17#4 = #1#49; |HACIENDO;
          nVto = nVto + 1;
          |LEE 000#17s;
     |SIGUE;

     |HAZ AbreRecVenta;
     nRecibo = 0;
     #17#0 = #1#0;
     #17#4 = #1#49;
     #17#1 = 0;
     |LEE 000#17];
     |PARA ; |SI FSalida = 0; |Y #17#0 = #1#0;|Y #17#4 = #1#49; |HACIENDO;
          nRecibo = nRecibo + 1;
          |SI #17#5 = "S";
               |DDEFECTO #98;
               #98#30 = "A";       || busca como albaran
               #98#20 = #17#6;     || Serie pedido.
               #98#17 = #17#7;     || Numero pedido.
               #98#18 = #17#8;     || Numero recibo.
               |LEE 000#98=;
               |SI FSalida ! 0;
                    #98#30 = "P";       || busca como pedido
                    |LEE 000#98=;
               |FINSI;
          |FINSI;
          |DDEFECTO #11;
          #11#17 = #1#49;
          #11#0 = #1#0;
          #11#1 = #17#1;           || Linea;
          #11#2 = #1#2;            || cliente
          #11#3 = #1#3;            || nombre del cliente
          #11#4 = #1#9;            || Banco
          #11#5 = "S";             || domiciliado
          #11#6 = "N";             || aceptado
          #11#7 = #1#1;            || fecha de emision
          #11#8 = #17#3;           || Fecha de vencimiento
          #11#23 = nVto;
          #11#9 = #17#2;             || Importe
          |SI #17#5 = "S";
              #11#10 = #98#10;
              #11#11 = #98#11;
          |SINO;
              #11#10 = "N";            || Impreso
              #11#11 = "N";            || Contabilizado
          |FINSI;
          |SI #41#63 = "S";     ||  COGE LA CTA.CONTABLE DEL TIPO DE RECIBO.
              #99#0 = #6#69;
              |LEE 000#99=;
              |SI FSalida = 0;
                   #11#12 = #99#3;     || NUMERO CTA.CONTABLE.
              |SINO;
                   #11#12 = #1#12;     || NUMERO CTA.CONTABLE.
              |FINSI;
          |SINO;
              #11#12 = #1#12;     || NUMERO CTA.CONTABLE.
          |FINSI;
          #11#13 = #98#13;
          #11#16 = #98#16;
          #11#22 = "Cobrado";
          #11#24 = #1#11;          || Forma de Pago
          #11#25 = #6#1;           || Descripcion de la forma de Pago
          #11#14 = "N";            || Impagado
          #11#15 = #3#161;         || A aceptar
          |SI #17#5 = "S";
              #11#21 = #98#22;         || Tipo de Recibo (entr.a cta.)
          |SINO;
              #11#21 = #6#69;          || Tipo de Recibo (forma de pago)
          |FINSI;
          |SI nRecibo = nVto; |Y #6#70 ! 0;
              #11#21 = #6#73;          || Tipo de Recibo (Fianza Retenida)
          |FINSI;
          #11#24 = #6#0;
          #11#25 = #6#1;
          eaProg = "agifa134";
          ||HAZ CreaRecVenta;
          |HAZ RecVenta;
          |LEE 000#17s;
     |SIGUE;

     |HAZ CierraRecVenta;

     |CIERRA #11;
     |CIERRA #17;
     |CIERRA #98;
     |CIERRA #99;
|FPRC;
