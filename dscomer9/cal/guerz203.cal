
|FICHEROS;
     guerz203 #0;
     gopre001 #1;
     gopre002 #2;
     gopre003 #3;
     gopre004 #4;
     gomedici #5;
     godeprel #10;
     guerz204 #204; ||Tmp
     goparame #100;
|FIN;

|VARIABLES;
     aAlf1 = "";
     aAlf2 = "";
     aAlf3 = "";
     aCodigo = "";
     aTipo = "";
     aCod = "";
     aLog = "";
     aTab = "";
     aFinLin = "";
     nBoton1 = 0;
     i = 0;
     nDeci = 0;
     nHandle = 0;
     nNume = 0;
     aFic = "";
     aAlfa = "";
     aReg = "";
     aRuta = "";
     aCap = "";
     aPre = "";
     aDes = "";
     aFecha = "";
     nPrime = 0;
     aNumPre = "";
     aPar = "";
     aMedida = "";
     nCap = 0;
     nPar = 0;
     aMone = "";
     nError = 0;

     cCodMonTrab = "";
     &nDeci_Medi = 0;
     &nDeci_Meda = 0;
     &nDeci_Incr = 0;
     &nDeci_PreBase = 0;
     &nDeci_PTrab; = 0;
|FIN;

|PROCESO Mensaje;
     |PUSHV 0501 2480;
     |BLANCO 1405 2275;
     |CUADRO 1405 2275;
     |CUADRO 1506 2174;
     |PINTA 1607, aAlf1;
     |PINTA 1807, aAlf2;
     |PINTA 1907, aAlf3;
     |ATRI 0;
     |PAUSA;
     |POPV;
|FPRC;

|PROCESO Log;
     aLog = "*** " + aLog + " ***";
     aAlfa = "0000" + aLog;
     |MENSA aAlfa;
     |||||XGRABA nHandle, aLog;
|FPRC;

|PROCESO Error;
     |SI nError = 0;
          #204#0 = aCodigo;
          #204#1 = aTipo;
          |LEE 000#204=;
          |SI FSalida = 0;
               |QBF aCodigo;
               |QBF aTipo;
               |QBF aCap;
               |SI aAlfa = aTipo;
                    aAlf1 = "Duplicado Producto: " + aCodigo;
                    aAlf2 = "Capitulo: " + aCap;
                    aAlf3 = "Partida : " + aPar;
                    |HAZ Mensaje;
               |SINO;
                    aAlf1 = "ERROR, se ha encontrado un producto con codigo igual a la partida";
                    aAlf2 = "Capitulo: " + aCap;
                    aAlf3 = "Partida : " + aPar;
                    |HAZ Mensaje;
               |FINSI;
          |SINO;
               |GRABA 020#204n;
               |ACABA;
          |FINSI;
     |FINSI;
     |SI nError = 1;
          aAlf1 = "ERROR, se ha encontrado un capitulo sin codigo";
          aAlf2 = "Linea: " + #2#2;
          aAlf3 = "";
          |HAZ Mensaje;
     |FINSI;
     |SI nError = 2; |O nError = 4;
          aAlf1 = "ERROR, se ha encontrado una partida sin codigo";
          aAlf2 = "Capitulo:" + aCap;
          aAlf3 = "Linea " + #3#3;
          |HAZ Mensaje;
     |FINSI;
     |SI nError = 3;
          aAlf1 = "ERROR, se ha encontrado un producto sin codigo";
          aAlf2 = "Capitulo: " + aCap;
          aAlf3 = "Partida: " + aPar;
          |HAZ Mensaje;
     |FINSI;
     |CONFI "0000NContinuar?";
     nError = FSalida;
     |SI FSalida = 0; |ERROR; |FINSI;
|FPRC;

|PROCESO Moneda;
     cCodMonTrab = #1#7;
     |HAZ MonBas;
     nDeci_Medi = #1#24;
     nDeci_Incr = #1#25;
     nDeci_PreBase = nDeci_PTrab;
|FPRC;

|PROCESO gomedici;
     |SI nError ! 0;
          |ERROR10; |ACABA;
     |FINSI;

     |OEM_ANSI aAlfa, #5#10;
     aReg = "\" + aAlfa; |QBF aReg;
     |SI #5#5 ! 0; aReg = aReg + "\" + #5#5; |SINO; aReg = aReg + "\"; |FINSI;
     |SI #5#6 ! 0; aReg = aReg + "\" + #5#6; |SINO; aReg = aReg + "\"; |FINSI;
     |SI #5#7 ! 0; aReg = aReg + "\" + #5#7; |SINO; aReg = aReg + "\"; |FINSI;
     |SI #5#8 ! 0; aReg = aReg + "\" + #5#8; |SINO; aReg = aReg + "\";|FINSI;
     aReg = aReg + "\";

     |SI aReg ! "\\\\\\";
          aReg = aTab + aReg;
          |XGRABA nHandle, aReg;
     |FINSI;
|FPRC;

|DEFBUCLE gomedici;
     #5#1;
     ;
     #3#0, #3#1, #3#2, #3#4, 001;
     #3#0, #3#1, #3#2, #3#4, 999;
     ;
     NULL, gomedici;
|FIN;

|PROCESO gopre003Medidas;
     aCap = #3#2; |QBF aCap;
     aPar = #3#4; |QBF aPar;
     |SI aPar = ""; |O aCap = "";
          nError = 2;
          |HAZ Error;
     |FINSI;
     |SI nError ! 0;
          |ERROR10; |ACABA;
     |FINSI;

     aMedida = #3#7; |QBF aMedida;
     nPar = nPar + 1;
     aReg = "~M|" + aCap + "#\" + aPar + "|" + nCap +"\"+nPar+ "\|" + aMedida + "|";
     |XGRABA nHandle, aReg;
     |BUCLE gomedici;
     aReg = "|";
     |XGRABA nHandle, aReg;
|FPRC;

|DEFBUCLE gopre003Medidas;
     #3#1;
     ;
     #2#0, #2#1, #2#12, 001;
     #2#0, #2#1, #2#12, 999;
     ;
     NULL, gopre003Medidas;
|FIN;

|PROCESO gopre002Medidas;
     |SI nError ! 0;
          |ERROR10; |ACABA;
     |FINSI;

     nCap = nCap + 1;
     nPar = 0;
     |BUCLE gopre003Medidas;
|FPRC;

|DEFBUCLE gopre002Medidas;
     #2#1;
     ;
     #1#0, #1#1, 001;
     #1#0, #1#1, 999;
     ;
     NULL, gopre002Medidas;
|FIN;
컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
|PROCESO gopre004_2;
     aPar = #4#3; |QBF aPar;
     aCap = #4#2; |QBF aCap;
     aCod = #4#5; |QBF aCod;
     |SI aCod = "";
          nError = 3;
          |HAZ Error;
     |FINSI;
     |SI nError ! 0;
          |ERROR10; |ACABA;
     |FINSI;

     |OEM_ANSI aAlfa, #4#6;
     aDes = aAlfa; |QBF aDes;
     aMedida = #4#7; |QBF aMedida;
     |SI #0#4 = "V";
          aPre = #4#9;
     |SINO;
          aPre = #4#10;
     |FINSI;
     aReg = "~C|" + aCod + "|" + aMedida + "|"+ aDes + "|" + aPre + "|" + aFecha + "|0|";
     |XGRABA nHandle, aReg;
|FPRC;

|DEFBUCLE gopre004_2;
     #4#1;
     ;
     #3#0, #3#1, #3#2, #3#4, 001;
     #3#0, #3#1, #3#2, #3#4, 999;
     ;
     NULL, gopre004_2;
|FIN;

|PROCESO gopre004;
     aPar = #4#3; |QBF aPar;
     aCap = #4#2; |QBF aCap;
     aCod = #4#5; |QBF aCod;

     aCodigo = #4#5;
     aTipo = "Producto";
     |SI nError = 0; |HAZ Error; |FINSI;

     |SI aCod = "";
          nError = 3;
          |HAZ Error;
     |FINSI;
     |SI nError ! 0;
          |ERROR10; |ACABA;
     |FINSI;

     aMedida = #4#8; |QBF aMedida;
     |SI nPrime = 0;
          nPrime = 1;
          aReg = aTab + "|" + aCod + "\\" + aMedida;
     |SINO;
          aReg = aTab + "\" + aCod + "\\" + aMedida;
     |FINSI;
     |XGRABA nHandle, aReg;
|FPRC;

|DEFBUCLE gopre004;
     #4#1;
     ;
     #3#0, #3#1, #3#2, #3#4, 001;
     #3#0, #3#1, #3#2, #3#4, 999;
     ;
     NULL, gopre004;
|FIN;

|PROCESO Mira_gopre004;
     nPrime = nPrime + 1;
|FPRC;

|DEFBUCLE Mira_gopre004;
     #4#1;
     ;
     #3#0, #3#1, #3#2, #3#4, 001;
     #3#0, #3#1, #3#2, #3#4, 999;
     ;
     NULL, Mira_gopre004;
|FIN;

|PROCESO godeprel;
     |SI nPrime = 0;
          aReg = "~T|" + aPar + "|";
          |XGRABA nHandle, aReg;
          nPrime = 1;
     |FINSI;
     |OEM_ANSI aAlfa, #10#4;
     aReg = aAlfa; |QBF aReg;
     |XGRABA nHandle, aReg;
|FPRC;

|DEFBUCLE godeprel;
     #10#1;
     ;
     #3#4, #3#0, #3#1, #3#2, 001;
     #3#4, #3#0, #3#1, #3#2, 999;
     ;
     NULL, godeprel;
|FIN;

|PROCESO gopre003_22;
     aCap = #3#2; |QBF aCap;
     aPar = #3#4; |QBF aPar;
     |SI aPar = "";
          nError = 2;
          |HAZ Error;
     |FINSI;
     |SI nError ! 0;
          |ERROR10; |ACABA;
     |FINSI;

     |OEM_ANSI aAlfa, #3#5;
     aDes = aAlfa; |QBF aDes;
     aMedida = #3#6; |QBF aMedida;
     |SI #0#4 = "V";
          aPre = #3#8;
     |SINO;
          aPre = #3#9;
     |FINSI;
     aReg = "~C|" + aPar + "|"+ aMedida + "|"+ aDes + "|" + aPre + "|" + aFecha + "|0|";
     |XGRABA nHandle, aReg;

     aLog = "godeprel"; |HAZ Log;
     nPrime = 0;
     |BUCLE godeprel;
     |SI nPrime ! 0;
          aReg = "|";
          |XGRABA nHandle, aReg;
     |FINSI;

     nPrime = 0;
     |BUCLE Mira_gopre004;
     |SI nPrime ! 0;
          |DELINDEX #204;
          |ABRE #204;
          aCap = #3#2;
          aPar = #3#4;
          aCodigo = #3#4;
          aTipo = "Partida";
          |SI nError = 0; |HAZ Error; |FINSI;

          aLog = "gopre004"; |HAZ Log;
          aReg = "~D|" + aPar;
          |XGRABA nHandle, aReg;
          nPrime = 0;
          |BUCLE gopre004;
          aReg = aTab + "\|";
          |XGRABA nHandle, aReg;

          |CIERRA #204;
     |FINSI;

     aLog = "gopre004_2"; |HAZ Log;
     |BUCLE gopre004_2;
|FPRC;

|DEFBUCLE gopre003_22;
     #3#1;
     ;
     #1#0, #1#1, "                    ", 001;
     #1#0, #1#1, "zzzzzzzzzzzzzzzzzzzz", 999;
     ;
     NULL, gopre003_22;
|FIN;

|PROCESO gopre003_2;
     aCap = #3#2; |QBF aCap;
     aPar = #3#4; |QBF aPar;
     |SI aPar = "";
          nError = 4;
          |HAZ Error;
     |FINSI;
     |SI nError ! 0;
          |ERROR10; |ACABA;
     |FINSI;

     aMedida = #3#7; |QBF aMedida;
     |SI nPrime = 0;
          nPrime = 1;
          aReg = aTab + "|" + aPar + "\\" + aMedida;
     |SINO;
          aReg = aTab + "\" + aPar + "\\" + aMedida;
     |FINSI;
     |XGRABA nHandle, aReg;
|FPRC;

|DEFBUCLE gopre003_2;
     #3#1;
     ;
     #2#0, #2#1, #2#12, 001;
     #2#0, #2#1, #2#12, 999;
     ;
     NULL, gopre003_2;
|FIN;

|PROCESO gopre002_2;
     aCap = #2#12; |QBF aCap;
     |SI aCap = "";
          nError = 1;
          |HAZ Error;
     |FINSI;
     |SI nError ! 0;
          |ERROR10; |ACABA;
     |FINSI;

     |OEM_ANSI aAlfa, #2#13;
     aDes = aAlfa; |QBF aDes;
     |SI #0#4 = "V";
          aPre = #2#7;
     |SINO;
          aPre = #2#8;
     |FINSI;
     aReg = "~C|" + aCap + "#||" + aDes + "|" + aPre +"|" + aFecha + "|0|";
     |XGRABA nHandle, aReg;

     aLog = "gopre003_2"; |HAZ Log;
     aReg = "~D|" + aCap + "#";
     |XGRABA nHandle, aReg;

     nPrime = 0;
     |BUCLE gopre003_2;
     aReg = aTab + "\|";
     |XGRABA nHandle, aReg;
|FPRC;

|DEFBUCLE gopre002_2;
     #2#1;
     ;
     #1#0, #1#1, 001;
     #1#0, #1#1, 999;
     ;
     NULL, gopre002_2;
|FIN;

|PROCESO gopre002;
     aCap = #2#12; |QBF aCap;
     |SI aCap = "";
          nError = 1;
          |HAZ Error;
     |FINSI;
     |SI nError ! 0;
          |ERROR10; |ACABA;
     |FINSI;

     |SI nPrime = 0;
          nPrime = 1;
          aReg = aTab + "|" + aCap + "#\\1";
     |SINO;
          aReg = aTab + "\" + aCap + "#\\1";
     |FINSI;
     |XGRABA nHandle, aReg;
|FPRC;

|DEFBUCLE gopre002;
     #2#1;
     ;
     #1#0, #1#1, 001;
     #1#0, #1#1, 999;
     ;
     NULL, gopre002;
|FIN;

|PROCESO Fecha;
     aFecha = (aAlfa % 102) + (aAlfa % 402) + (aAlfa % 704);
|FPRC;

|PROCESO Cabecera;
     |OEM_ANSI aAlfa, #48#1;
     |QBF aAlfa;

     aReg = "~V|" + aAlfa + "|FIEBDC-3/98|dscomer9||ANSI|";
     |XGRABA nHandle, aReg;
     aReg = "~K|\2\2\4\2\2\2\2\EURO|3||"
     |XGRABA nHandle, aReg;

     aNumPre = #1#0 + (("000000" + #1#1) % -106);   ||Serie
     |SI #0#4 = "V";
          aPre = #1#5;                                ||Precio
     |SINO;
          aPre = #1#6;                                ||Precio
     |FINSI;
     aAlfa = #1#4; |HAZ Fecha;
     aReg = "~C|" + aNumPre + "##|||" + aPre + "|" + aFecha + "|0|";
     |XGRABA nHandle, aReg;
|FPRC;

|PROCESO Inicio;
     aAlfa = Usuario;
     |NOME_DAT #204 aAlfa;
     |DELINDEX #204;

     aFinLin = &13 + &10;
     aTab = &9;
     |ABRE #1;
     #1#0 = #0#2;
     #1#1 = #0#3;
     |LEE 000#1=;
     |SI FSalida = 0;
          |HAZ Moneda;
          aFic = #0#1; |QBF aFic;
          |XABRE "CE", aFic, 0;
          |SI FSalida ] 0;
               |CONFI "S0000Sobreescribir fichero...";
               |SI FSalida ! 0; |ACABA; |FINSI;
          |FINSI;

          |XABRE "CW", aFic, nHandle;
          |SI FSalida ] 0;
               |HAZ Cabecera;
               aLog = "gopre002"; |HAZ Log;
               aReg = "~D|" + aNumPre + "##";
               |XGRABA nHandle, aReg;

               nPrime = 0;
               |BUCLE gopre002;
               aReg = aTab + "\|";
               |XGRABA nHandle, aReg;

               aLog = "gopre002_2"; |HAZ Log;
               |BUCLE gopre002_2;

               aLog = "gopre003_22"; |HAZ Log;
               |BUCLE gopre003_22;
               nCap = 0;

               aLog =  "Medidas"; |HAZ Log;
               |BUCLE gopre002Medidas;
               |XCIERRA nHandle;

               |SI nError ! 0; |RFBORRA aFic; |FINSI;
          |SINO;
               aFic = "0000Error al abrir " + aFic;
               |MENAV aFic;
          |FINSI;
     |FINSI;
     |CIERRA #1;

     |DELINDEX #204;
|FPRC;

|PROCESO Boton1;
     |PTEC 824;
|FPRC;

|PROCESO Browser;
     |SI FSalida ! 10; |ACABA; |FINSI;

     aRuta = #0#1;  |QBF aRuta;
     |SI aRuta = "";
         aRuta = "\*.*";
     |FINSI;
     aRuta = "F" + aRuta;

     |BROWSE aRuta;

     aRuta = aRuta % 299;
     #0#1 = aRuta;
     |PINTA #0#1;
|FPRC;

|PROCESO Quita; |TIPO 0;
     |ESTADO_CONTROL nBoton1, "DISABLE";
|FPRC;

|PROCESO Pone; |TIPO 7;
     |ESTADO_CONTROL nBoton1, "ENABLE";
|FPRC;

|PROGRAMA;
     |ABRE #100; |LEE 000#100p; |CIERRA #100;
     nDeci_Meda = #100#115;

     |CONTROL_SIMPLE 0, "BOTON, Fichero", 1016,1024, Boton1;
     nBoton1 = FSalida;
     |HAZ Quita;

     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0; |GRABA 020#0b; |FINSI;

     |CLS;
     |PINPA #0#0;

     |SI #100#141 = "O";
          |PINTA 1217, "O.E........";
     |FINSI;

     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |HAZ Inicio;
          |GRABA 020#0a;
     |FINSI;

     |CIERRA #0;

     |FIN_CONTROL_WINDOWS nBoton1;
     |PULSATECLA;
|FPRO;
